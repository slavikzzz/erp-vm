#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// Возвращает объект ОписаниеТипов, соответствующий типу поля Регистратор набора записей регистра расчета.
// 
// Возвращаемое значение:
//  ОписаниеТипов - описание типов поля Регистратор
//
Функция ОписаниеТиповПоляРегистратор() Экспорт
	Возврат РегистрыРасчета.Начисления.СоздатьНаборЗаписей().Отбор.Регистратор.ТипЗначения;
КонецФункции

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ФизическоеЛицо)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ОбработчикиПравилРегистрации

// См. ЗарплатаКадрыРасширенныйСинхронизацияДанных.ШаблонОбработчика
Процедура ПриЗаполненииНастроекОбработчиковПравилРегистрации(Настройки) Экспорт
	ЗарплатаКадрыРасширенныйСинхронизацияДанных.ДляРегистраПодчиненногоРегистратору(Настройки);
КонецПроцедуры

#КонецОбласти

Процедура ЗапланироватьПреобразованиеСторноТекущегоПериода(НаборЗаписей, ИсправленныйДокумент) Экспорт
	
	// Преобразование строк выполняется перед записью набора.
	// См. РегистрыРасчета.Начисления.МодульНабораЗаписей Процедура ПреобразоватьСторноСтрокиТекущегоПериода.
	НаборЗаписей.ДополнительныеСвойства.Вставить("ЕстьПерерасчетыПриИсправленииВТекущемПериоде", Истина);
	НаборЗаписей.ДополнительныеСвойства.Вставить("ИсправленныйДокумент", ИсправленныйДокумент);
	
КонецПроцедуры

// Получает результирующий период действия по параметрам отбора
//
// Параметры:
//  Отборы       - Структура, см. ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор - параметры и их значения для
//                                                                                         получения ФПД.
//  ОтборыДляФПД - Структура, см. ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор - параметры, формирующие
//                                                                                         условия для ФПД.
// 
// Возвращаемое значение:
//   ТаблицаЗначений
//
Функция РезультирующийПериодДействия(Отборы, ОтборыДляФПД = Неопределено) Экспорт
	
	Реквизиты = "*";
	
	РПД = ЗарплатаКадрыРасширенный.РезультирующийПериодДействия(Метаданные.РегистрыРасчета.Начисления, Отборы, 
		Реквизиты, ОтборыДляФПД);
		
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Сторно", Ложь);
	ПараметрыОтбора.Вставить("ФиксСторно", Ложь);
	ПараметрыОтбора.Вставить("СторноТекущегоПериода", Неопределено);

	Результат = РПД.Скопировать(ПараметрыОтбора);
	
	Возврат Результат;
	
КонецФункции

// Формирует временную таблицу результирующих периодов действия (РПД).
//
// Параметры:
//  МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджер ВТ в который помещается результат.
//  Отборы         - Структура, см. ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор - параметры и их значения для
//                                                                                           получения ФПД.
//  Реквизиты               - Строка, Массив          - Имена получаемых полей. Поддерживается значение "*".
//  ИмяВТ                   - Строка                  - Имя временной таблицы, по умолчанию "ВТРезультирующийПериодДействия".
//
Процедура СоздатьВТРезультирующийПериодДействия(МенеджерВременныхТаблиц, Отборы, Реквизиты,
	ИмяВТ = "ВТРезультирующийПериодДействия") Экспорт
				
	ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(Отборы, "СторноТекущегоПериода", "=", Неопределено);

	РПД = ЗарплатаКадрыРасширенный.РезультирующийПериодДействия(Метаданные.РегистрыРасчета.Начисления, Отборы, Реквизиты);
		
	ЗарплатаКадры.СоздатьВТПоТаблицеЗначений(МенеджерВременныхТаблиц, РПД, ИмяВТ);
	
КонецПроцедуры

// Сторнирование движений документа исправленного в текущем периоде.
//
// Параметры:
//  НаборЗаписей         - РегистрРасчетаНаборЗаписей - Целевой набор записей в который будут добавлены сторнирующие строки.
//  ИсправленныйДокумент - ДокументСсылка             - Документ, записи которого необходимо сторнировать.
//  Записывать           - Булево                     - Если Истина, то набор будет записан сразу.
//                                                      Если Ложь, то набору будет установлен признак Записывать = Истина.
//
Процедура СторнироватьДвиженияВТекущемПериоде(НаборЗаписей, ИсправленныйДокумент, Записывать = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборИсправленный = СоздатьНаборЗаписей();
	НаборИсправленный.Отбор.Регистратор.Установить(ИсправленныйДокумент);
	НаборИсправленный.Прочитать();
	
	Если НаборИсправленный.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	МетаданныеРесурсыРегистра = Метаданные.РегистрыРасчета.Начисления.Ресурсы;
	
	Для Каждого Строка Из НаборИсправленный Цикл
		
		Если Строка.Сторно
			Или (Строка.СторноТекущегоПериода <> Неопределено И Строка.СторноТекущегоПериода <> ИсправленныйДокумент) Тогда
			Продолжить;
		КонецЕсли;
		
		// "Сторнируем" набор записей исправленного документа.
		МинусСтрока = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(МинусСтрока, Строка);
		Для Каждого Ресурс Из МетаданныеРесурсыРегистра Цикл
			МинусСтрока[Ресурс.Имя] = - МинусСтрока[Ресурс.Имя];
		КонецЦикла;
		МинусСтрока.СторноТекущегоПериода = ИсправленныйДокумент;
		МинусСтрока.СторнируемыйДокумент = ИсправленныйДокумент;
		МинусСтрока.ИдентификаторСтроки = - МинусСтрока.ИдентификаторСтроки;
		МинусСтрока.ФиксРасчет = Истина;
		
		// "Выключим" запись исправленного документа из расчета ФПД.
		Строка.СторноТекущегоПериода = ИсправленныйДокумент;
		НаборИсправленный.Записывать = Истина;
		
	КонецЦикла;
	
	Если НаборИсправленный.Записывать Тогда
		// Измененный набор записей исправленного документа будет записан перед записью целевого набора.
		// См. РегистрыРасчета.Начисления.МодульНабораЗаписей Процедура ЗаписатьНаборИсправляемыйВТекущемПериоде.
		НаборЗаписей.ДополнительныеСвойства.Вставить("НаборИсправляемыйВТекущемПериоде", НаборИсправленный);
	КонецЕсли;
	
	Если Записывать Тогда
		НаборЗаписей.Записать();
		НаборЗаписей.Записывать = Ложь;
	Иначе
		НаборЗаписей.Записывать = Истина;
	КонецЕсли;
	
КонецПроцедуры

// Используется при исправлении в текущем периоде в случае если выполнен перерасчет прошлого периода.
// В наборе записей уже могут быть сторно строки сформированные алгоритмом перерасчета, выполняется их
// преобразование в сторно строки текущего периода. Новые сторно записи не добавляются.
//
// Параметры:
//  НаборЗаписей         - РегистрРасчетаНаборЗаписей - Целевой набор записи которого будут преобразованы.
//  ИсправленныйДокумент - ДокументСсылка             - Документ, записи которого необходимо сторнировать.
//  Записывать           - Булево                     - Если Истина, то набор будет записан сразу.
//                                                      Если Ложь, то набору будет установлен признак Записывать = Истина.
//
Процедура ПреобразоватьСторноВСторноТекущегоПериода(НаборЗаписей, ИсправленныйДокумент, Записывать = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборИсправленный = СоздатьНаборЗаписей();
	НаборИсправленный.Отбор.Регистратор.Установить(ИсправленныйДокумент);
	НаборИсправленный.Прочитать();
	
	Если НаборИсправленный.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Строка Из НаборИсправленный Цикл
		
		Если Строка.Сторно Или Строка.СторноТекущегоПериода <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		// "Выключим" запись исправленного документа из расчета ФПД.
		Строка.СторноТекущегоПериода = ИсправленныйДокумент;
		НаборИсправленный.Записывать = Истина;
		
	КонецЦикла;
	
	Для Каждого СтрокаНабора Из НаборЗаписей Цикл
		Если (СтрокаНабора.Сторно Или СтрокаНабора.ФиксСторно)
			И СтрокаНабора.СторнируемыйДокумент = ИсправленныйДокумент Тогда
			
			СтрокаНабора.Сторно = Ложь;
			СтрокаНабора.СторноТекущегоПериода = ИсправленныйДокумент;
			НаборЗаписей.Записывать = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если НаборИсправленный.Записывать Тогда
		// Измененный набор записей исправленного документа будет записан перед записью целевого набора.
		// См. РегистрыРасчета.Начисления.МодульНабораЗаписей Процедура ЗаписатьНаборИсправляемыйВТекущемПериоде.
		НаборЗаписей.ДополнительныеСвойства.Вставить("НаборИсправляемыйВТекущемПериоде", НаборИсправленный);
	КонецЕсли;
	
	Если Записывать Тогда
		НаборЗаписей.Записать();
		НаборЗаписей.Записывать = Ложь;
	Иначе
		НаборЗаписей.Записывать = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОтменеИсправленияВТекущемПериоде(ИсправленныйДокумент) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборИсправленный = СоздатьНаборЗаписей();
	НаборИсправленный.Отбор.Регистратор.Установить(ИсправленныйДокумент);
	НаборИсправленный.Прочитать();
	
	Для Каждого Строка Из НаборИсправленный Цикл
		Если Строка.СторноТекущегоПериода = ИсправленныйДокумент Тогда
			Строка.СторноТекущегоПериода = Неопределено;
			НаборИсправленный.Записывать = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если НаборИсправленный.Записывать Тогда
		НаборИсправленный.ОбменДанными.Загрузка = Истина;
		НаборИсправленный.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
		НаборИсправленный.Записать();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СоздатьВТПодработкиИсправленногоДокумента(Регистратор, МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Сведения = ИсправлениеДокументовЗарплатаКадры.СведенияОбИсправленииДокумента(Регистратор);
	Если Сведения.ИсправленныйДокумент <> Неопределено Тогда
		
		// Все подработочные начисления исправляемого документа должны быть сторнированы
		// вне зависимости от того, какие подработки действуют на интервале регистрируемых начислений.
		// Для этого добавим сотрудников по подработочным движениям исправляемого документа.
		
		Запрос.УстановитьПараметр("ИсправленныйДокумент", Сведения.ИсправленныйДокумент);
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Начисления.Сотрудник.ГоловнойСотрудник КАК Сотрудник,
			|	Начисления.Сотрудник КАК Подработка
			|ПОМЕСТИТЬ ВТПодработкиИсправленногоДокумента
			|ИЗ
			|	РегистрРасчета.Начисления КАК Начисления
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТКонтролируемыеНачисления КАК КонтролируемыеНачисления
			|		ПО Начисления.ВидРасчета = КонтролируемыеНачисления.ВидРасчета
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачисленияНабора КАК СторнирующиеНачисленияРегистратора
			|		ПО (СторнирующиеНачисленияРегистратора.ВидРасчета = Начисления.ВидРасчета)
			|			И (СторнирующиеНачисленияРегистратора.ПериодДействияНачало = Начисления.ПериодДействияНачало)
			|			И (СторнирующиеНачисленияРегистратора.ПериодДействияКонец = Начисления.ПериодДействияКонец)
			|			И (СторнирующиеНачисленияРегистратора.Сотрудник = Начисления.Сотрудник)
			|ГДЕ
			|	Начисления.Регистратор = &ИсправленныйДокумент
			|	И НЕ Начисления.Сторно
			|	И Начисления.СторноТекущегоПериода = НЕОПРЕДЕЛЕНО
			|	И СторнирующиеНачисленияРегистратора.ВидРасчета ЕСТЬ NULL";
	Иначе
		Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 0
			|	НЕОПРЕДЕЛЕНО КАК Сотрудник,
			|	НЕОПРЕДЕЛЕНО КАК Подработка
			|ПОМЕСТИТЬ ВТПодработкиИсправленногоДокумента";
	КонецЕсли;
	
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ДополнитьНаборЗаписейНачислениямиСовместителейИПодработок(НаборЗаписей, ДобавленныеЗаписи = НеОпределено) Экспорт
	
	ЗаписиДобавлялись = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("НаборЗаписейНачисления", НаборЗаписей.Выгрузить());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Начисления.ПериодРегистрации КАК ПериодРегистрации,
	|	Начисления.Регистратор КАК Регистратор,
	|	Начисления.НомерСтроки КАК НомерСтроки,
	|	Начисления.ВидРасчета КАК ВидРасчета,
	|	Начисления.ПериодДействия КАК ПериодДействия,
	|	Начисления.ПериодДействияНачало КАК ПериодДействияНачало,
	|	Начисления.ПериодДействияКонец КАК ПериодДействияКонец,
	|	Начисления.БазовыйПериодНачало КАК БазовыйПериодНачало,
	|	Начисления.БазовыйПериодКонец КАК БазовыйПериодКонец,
	|	Начисления.Активность КАК Активность,
	|	Начисления.Сторно КАК Сторно,
	|	Начисления.СторноТекущегоПериода КАК СторноТекущегоПериода,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Начисления.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	Начисления.Результат КАК Результат,
	|	Начисления.ОтработаноДней КАК ОтработаноДней,
	|	Начисления.ОтработаноЧасов КАК ОтработаноЧасов,
	|	Начисления.РезультатВТомЧислеЗаСчетФБ КАК РезультатВТомЧислеЗаСчетФБ,
	|	Начисления.ГрафикРаботы КАК ГрафикРаботы,
	|	Начисления.ВидУчетаВремени КАК ВидУчетаВремени,
	|	Начисления.ВремяВЧасах КАК ВремяВЧасах,
	|	Начисления.ГрафикРаботыНорма КАК ГрафикРаботыНорма,
	|	Начисления.ВремяВЦеломЗаПериод КАК ВремяВЦеломЗаПериод,
	|	Начисления.ОбщийГрафик КАК ОбщийГрафик,
	|	Начисления.Организация КАК Организация,
	|	Начисления.ФиксСтрока КАК ФиксСтрока,
	|	Начисления.ФиксЗаполнение КАК ФиксЗаполнение,
	|	Начисления.ФиксРасчетВремени КАК ФиксРасчетВремени,
	|	Начисления.ФиксРасчет КАК ФиксРасчет,
	|	Начисления.РасчетнаяБазаЗаЕдиницуНормыВремени КАК РасчетнаяБазаЗаЕдиницуНормыВремени,
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Начисления.ПериодРегистрацииВремени КАК ПериодРегистрацииВремени,
	|	Начисления.ПериодРегистрацииНормыВремени КАК ПериодРегистрацииНормыВремени,
	|	Начисления.ДоляРезультата КАК ДоляРезультата,
	|	Начисления.ДокументОснование КАК ДокументОснование
	|ПОМЕСТИТЬ ВТВсеНачисленияНабора
	|ИЗ
	|	&НаборЗаписейНачисления КАК Начисления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Начисления.Ссылка КАК ВидРасчета
	|ПОМЕСТИТЬ ВТКонтролируемыеНачисления
	|ИЗ
	|	ПланВидовРасчета.Начисления КАК Начисления
	|ГДЕ
	|	(Начисления.ДублироватьДляВнутреннихСовместителейИПодработок
	|			ИЛИ Начисления.ДублироватьДляПодработок)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Начисления.ПериодРегистрации КАК ПериодРегистрации,
	|	Начисления.Регистратор КАК Регистратор,
	|	Начисления.НомерСтроки КАК НомерСтроки,
	|	Начисления.ВидРасчета КАК ВидРасчета,
	|	Начисления.ПериодДействия КАК ПериодДействия,
	|	Начисления.ПериодДействияНачало КАК ПериодДействияНачало,
	|	Начисления.ПериодДействияКонец КАК ПериодДействияКонец,
	|	Начисления.БазовыйПериодНачало КАК БазовыйПериодНачало,
	|	Начисления.БазовыйПериодКонец КАК БазовыйПериодКонец,
	|	Начисления.Активность КАК Активность,
	|	Начисления.Сторно КАК Сторно,
	|	Начисления.СторноТекущегоПериода КАК СторноТекущегоПериода,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Начисления.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	Начисления.Результат КАК Результат,
	|	Начисления.ОтработаноДней КАК ОтработаноДней,
	|	Начисления.ОтработаноЧасов КАК ОтработаноЧасов,
	|	Начисления.РезультатВТомЧислеЗаСчетФБ КАК РезультатВТомЧислеЗаСчетФБ,
	|	Начисления.ГрафикРаботы КАК ГрафикРаботы,
	|	Начисления.ВидУчетаВремени КАК ВидУчетаВремени,
	|	Начисления.ВремяВЧасах КАК ВремяВЧасах,
	|	Начисления.ГрафикРаботыНорма КАК ГрафикРаботыНорма,
	|	Начисления.ВремяВЦеломЗаПериод КАК ВремяВЦеломЗаПериод,
	|	Начисления.ОбщийГрафик КАК ОбщийГрафик,
	|	Начисления.Организация КАК Организация,
	|	Начисления.ФиксСтрока КАК ФиксСтрока,
	|	Начисления.ФиксЗаполнение КАК ФиксЗаполнение,
	|	Начисления.ФиксРасчетВремени КАК ФиксРасчетВремени,
	|	Начисления.ФиксРасчет КАК ФиксРасчет,
	|	Начисления.РасчетнаяБазаЗаЕдиницуНормыВремени КАК РасчетнаяБазаЗаЕдиницуНормыВремени,
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Начисления.ПериодРегистрацииВремени КАК ПериодРегистрацииВремени,
	|	Начисления.ПериодРегистрацииНормыВремени КАК ПериодРегистрацииНормыВремени,
	|	Начисления.ДоляРезультата КАК ДоляРезультата,
	|	Начисления.ДокументОснование КАК ДокументОснование
	|ПОМЕСТИТЬ ВТНачисленияНабора
	|ИЗ
	|	ВТВсеНачисленияНабора КАК Начисления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТКонтролируемыеНачисления КАК КонтролируемыеНачисления
	|		ПО Начисления.ВидРасчета = КонтролируемыеНачисления.ВидРасчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(НачисленияНабора.ПериодДействияНачало) КАК НачалоПериода,
	|	МАКСИМУМ(НачисленияНабора.ПериодДействияКонец) КАК ОкончаниеПериода,
	|	НачисленияНабора.Организация КАК Организация
	|ИЗ
	|	ВТНачисленияНабора КАК НачисленияНабора
	|ГДЕ
	|	НачисленияНабора.Сотрудник = ВЫРАЗИТЬ(НачисленияНабора.Сотрудник КАК Справочник.Сотрудники).ГоловнойСотрудник
	|
	|СГРУППИРОВАТЬ ПО
	|	НачисленияНабора.Организация";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		
		СоздатьВТПодработкиИсправленногоДокумента(НаборЗаписей.Отбор.Регистратор.Значение, Запрос.МенеджерВременныхТаблиц);
		
		УдалитьВременныеТаблицы = Ложь;
		ВыборкаПериодов = РезультатЗапроса.Выбрать();
		Пока ВыборкаПериодов.Следующий() Цикл
			
			ЧастиЗапроса = Новый Массив;
			
			Если УдалитьВременныеТаблицы Тогда
				ЧастиЗапроса.Добавить("УНИЧТОЖИТЬ ВТФизическиеЛица");
				ЧастиЗапроса.Добавить("УНИЧТОЖИТЬ ВТСотрудникиОрганизации");
				ЧастиЗапроса.Добавить("УНИЧТОЖИТЬ ВТСотрудникиССовместителями");
				ЧастиЗапроса.Добавить("УНИЧТОЖИТЬ ВТНачисленияНабораСНачислениямиСовместителей");
				ЧастиЗапроса.Добавить("УНИЧТОЖИТЬ ВТСотрудникиСПодработками");
			Иначе
				УдалитьВременныеТаблицы = Истина;
			КонецЕсли;
			
			ЧастиЗапроса.Добавить(
				"ВЫБРАТЬ РАЗЛИЧНЫЕ
				|	НачисленияНабора.ФизическоеЛицо
				|ПОМЕСТИТЬ ВТФизическиеЛица
				|ИЗ
				|	ВТНачисленияНабора КАК НачисленияНабора
				|ГДЕ
				|	НачисленияНабора.Сотрудник = НачисленияНабора.Сотрудник.ГоловнойСотрудник");
			
			Запрос.Текст = СтрСоединить(ЧастиЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
			Запрос.УстановитьПараметр("Организация", ВыборкаПериодов.Организация);
			Запрос.Выполнить();
			
			ПараметрыПолучения = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоВременнойТаблице();
			ЗаполнитьЗначенияСвойств(ПараметрыПолучения, ВыборкаПериодов);
			ПараметрыПолучения.КадровыеДанные = "ВидЗанятости,ГоловнойСотрудник";
			ПараметрыПолучения.ОтбиратьПоГоловнойОрганизации = Истина;
			ПараметрыПолучения.ПодработкиРаботниковПоТрудовымДоговорам = Истина;
			
			КадровыйУчет.СоздатьВТСотрудникиОрганизации(Запрос.МенеджерВременныхТаблиц, Ложь, ПараметрыПолучения);
			
			// Дублирование начислений внутренних совместителей.
			Запрос.Текст =
			"ВЫБРАТЬ
			|	СотрудникиОрганизации.Сотрудник КАК Сотрудник,
			|	СотрудникиОрганизацииСовместители.Сотрудник КАК Совместитель
			|ПОМЕСТИТЬ ВТСотрудникиССовместителями
			|ИЗ
			|	ВТСотрудникиОрганизации КАК СотрудникиОрганизации
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудникиОрганизации КАК СотрудникиОрганизацииСовместители
			|		ПО СотрудникиОрганизации.ФизическоеЛицо = СотрудникиОрганизацииСовместители.ФизическоеЛицо
			|			И (СотрудникиОрганизацииСовместители.ВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ВнутреннееСовместительство))
			|ГДЕ
			|	СотрудникиОрганизации.ВидЗанятости В (ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ОсновноеМестоРаботы), ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.Совместительство))
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	НачисленияНабора.ПериодРегистрации КАК ПериодРегистрации,
			|	НачисленияНабора.Регистратор КАК Регистратор,
			|	НачисленияНабора.НомерСтроки КАК НомерСтроки,
			|	НачисленияНабора.ВидРасчета КАК ВидРасчета,
			|	НачисленияНабора.ПериодДействия КАК ПериодДействия,
			|	НачисленияНабора.ПериодДействияНачало КАК ПериодДействияНачало,
			|	НачисленияНабора.ПериодДействияКонец КАК ПериодДействияКонец,
			|	НачисленияНабора.БазовыйПериодНачало КАК БазовыйПериодНачало,
			|	НачисленияНабора.БазовыйПериодКонец КАК БазовыйПериодКонец,
			|	НачисленияНабора.Активность КАК Активность,
			|	НачисленияНабора.Сторно КАК Сторно,
			|	НачисленияНабора.СторноТекущегоПериода КАК СторноТекущегоПериода,
			|	НачисленияНабора.Сотрудник КАК Сотрудник,
			|	НачисленияНабора.ФизическоеЛицо КАК ФизическоеЛицо,
			|	НачисленияНабора.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
			|	НачисленияНабора.Результат КАК Результат,
			|	НачисленияНабора.ОтработаноДней КАК ОтработаноДней,
			|	НачисленияНабора.ОтработаноЧасов КАК ОтработаноЧасов,
			|	НачисленияНабора.РезультатВТомЧислеЗаСчетФБ КАК РезультатВТомЧислеЗаСчетФБ,
			|	НачисленияНабора.ГрафикРаботы КАК ГрафикРаботы,
			|	НачисленияНабора.ВидУчетаВремени КАК ВидУчетаВремени,
			|	НачисленияНабора.ВремяВЧасах КАК ВремяВЧасах,
			|	НачисленияНабора.ГрафикРаботыНорма КАК ГрафикРаботыНорма,
			|	НачисленияНабора.ВремяВЦеломЗаПериод КАК ВремяВЦеломЗаПериод,
			|	НачисленияНабора.ОбщийГрафик КАК ОбщийГрафик,
			|	НачисленияНабора.Организация КАК Организация,
			|	НачисленияНабора.ФиксСтрока КАК ФиксСтрока,
			|	НачисленияНабора.ФиксЗаполнение КАК ФиксЗаполнение,
			|	НачисленияНабора.ФиксРасчетВремени КАК ФиксРасчетВремени,
			|	НачисленияНабора.ФиксРасчет КАК ФиксРасчет,
			|	НачисленияНабора.РасчетнаяБазаЗаЕдиницуНормыВремени КАК РасчетнаяБазаЗаЕдиницуНормыВремени,
			|	НачисленияНабора.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	НачисленияНабора.ПериодРегистрацииВремени КАК ПериодРегистрацииВремени,
			|	НачисленияНабора.ПериодРегистрацииНормыВремени КАК ПериодРегистрацииНормыВремени,
			|	НачисленияНабора.ДоляРезультата КАК ДоляРезультата,
			|	НачисленияНабора.ДокументОснование КАК ДокументОснование,
			|	ЛОЖЬ КАК ДобавленноеНачислениеНабора
			|ПОМЕСТИТЬ ВТНачисленияНабораСНачислениямиСовместителей
			|ИЗ
			|	ВТНачисленияНабора КАК НачисленияНабора
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	НачисленияНабора.ПериодРегистрации,
			|	НачисленияНабора.Регистратор,
			|	НачисленияНабора.НомерСтроки,
			|	НачисленияНабора.ВидРасчета,
			|	НачисленияНабора.ПериодДействия,
			|	НачисленияНабора.ПериодДействияНачало,
			|	НачисленияНабора.ПериодДействияКонец,
			|	НачисленияНабора.БазовыйПериодНачало,
			|	НачисленияНабора.БазовыйПериодКонец,
			|	НачисленияНабора.Активность,
			|	НачисленияНабора.Сторно,
			|	НачисленияНабора.СторноТекущегоПериода,
			|	СотрудникиССовместителями.Совместитель,
			|	НачисленияНабора.ФизическоеЛицо,
			|	НачисленияНабора.ГоловнаяОрганизация,
			|	НачисленияНабора.Результат,
			|	НачисленияНабора.ОтработаноДней,
			|	НачисленияНабора.ОтработаноЧасов,
			|	НачисленияНабора.РезультатВТомЧислеЗаСчетФБ,
			|	НачисленияНабора.ГрафикРаботы,
			|	НачисленияНабора.ВидУчетаВремени,
			|	НачисленияНабора.ВремяВЧасах,
			|	НачисленияНабора.ГрафикРаботыНорма,
			|	НачисленияНабора.ВремяВЦеломЗаПериод,
			|	НачисленияНабора.ОбщийГрафик,
			|	НачисленияНабора.Организация,
			|	НачисленияНабора.ФиксСтрока,
			|	НачисленияНабора.ФиксЗаполнение,
			|	НачисленияНабора.ФиксРасчетВремени,
			|	НачисленияНабора.ФиксРасчет,
			|	НачисленияНабора.РасчетнаяБазаЗаЕдиницуНормыВремени,
			|	НачисленияНабора.ИдентификаторСтроки,
			|	НачисленияНабора.ПериодРегистрацииВремени,
			|	НачисленияНабора.ПериодРегистрацииНормыВремени,
			|	НачисленияНабора.ДоляРезультата,
			|	НачисленияНабора.ДокументОснование,
			|	ИСТИНА
			|ИЗ
			|	ВТСотрудникиССовместителями КАК СотрудникиССовместителями
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНачисленияНабора КАК НачисленияНабора
			|		ПО СотрудникиССовместителями.Сотрудник = НачисленияНабора.Сотрудник
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачисленияНабора КАК НачисленияНабораСовместителей
			|		ПО СотрудникиССовместителями.Совместитель = НачисленияНабораСовместителей.Сотрудник
			|			И (НачисленияНабора.ВидРасчета = НачисленияНабораСовместителей.ВидРасчета)
			|			И (НачисленияНабора.ПериодДействияНачало = НачисленияНабораСовместителей.ПериодДействияНачало)
			|			И (НачисленияНабора.ПериодДействияКонец = НачисленияНабораСовместителей.ПериодДействияКонец)
			|ГДЕ
			|	НачисленияНабораСовместителей.Сотрудник ЕСТЬ NULL
			|	И ВЫРАЗИТЬ(НачисленияНабора.ВидРасчета КАК ПланВидовРасчета.Начисления).ДублироватьДляВнутреннихСовместителейИПодработок
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	НачисленияНабораСНачислениямиСовместителей.ПериодРегистрации КАК ПериодРегистрации,
			|	НачисленияНабораСНачислениямиСовместителей.Регистратор КАК Регистратор,
			|	НачисленияНабораСНачислениямиСовместителей.НомерСтроки КАК НомерСтроки,
			|	НачисленияНабораСНачислениямиСовместителей.ВидРасчета КАК ВидРасчета,
			|	НачисленияНабораСНачислениямиСовместителей.ПериодДействия КАК ПериодДействия,
			|	НачисленияНабораСНачислениямиСовместителей.ПериодДействияНачало КАК ПериодДействияНачало,
			|	НачисленияНабораСНачислениямиСовместителей.ПериодДействияКонец КАК ПериодДействияКонец,
			|	НачисленияНабораСНачислениямиСовместителей.БазовыйПериодНачало КАК БазовыйПериодНачало,
			|	НачисленияНабораСНачислениямиСовместителей.БазовыйПериодКонец КАК БазовыйПериодКонец,
			|	НачисленияНабораСНачислениямиСовместителей.Активность КАК Активность,
			|	НачисленияНабораСНачислениямиСовместителей.Сторно КАК Сторно,
			|	НачисленияНабораСНачислениямиСовместителей.Сотрудник КАК Сотрудник,
			|	НачисленияНабораСНачислениямиСовместителей.ФизическоеЛицо КАК ФизическоеЛицо,
			|	НачисленияНабораСНачислениямиСовместителей.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
			|	0 КАК Результат,
			|	0 КАК ОтработаноДней,
			|	0 КАК ОтработаноЧасов,
			|	0 КАК РезультатВТомЧислеЗаСчетФБ,
			|	НачисленияНабораСНачислениямиСовместителей.ГрафикРаботы КАК ГрафикРаботы,
			|	НачисленияНабораСНачислениямиСовместителей.ВидУчетаВремени КАК ВидУчетаВремени,
			|	НачисленияНабораСНачислениямиСовместителей.ВремяВЧасах КАК ВремяВЧасах,
			|	НачисленияНабораСНачислениямиСовместителей.ГрафикРаботыНорма КАК ГрафикРаботыНорма,
			|	НачисленияНабораСНачислениямиСовместителей.ВремяВЦеломЗаПериод КАК ВремяВЦеломЗаПериод,
			|	НачисленияНабораСНачислениямиСовместителей.Организация КАК Организация,
			|	НачисленияНабораСНачислениямиСовместителей.ФиксСтрока КАК ФиксСтрока,
			|	НачисленияНабораСНачислениямиСовместителей.ФиксЗаполнение КАК ФиксЗаполнение,
			|	НачисленияНабораСНачислениямиСовместителей.ФиксРасчетВремени КАК ФиксРасчетВремени,
			|	НачисленияНабораСНачислениямиСовместителей.ФиксРасчет КАК ФиксРасчет,
			|	НачисленияНабораСНачислениямиСовместителей.РасчетнаяБазаЗаЕдиницуНормыВремени КАК РасчетнаяБазаЗаЕдиницуНормыВремени,
			|	НачисленияНабораСНачислениямиСовместителей.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	НачисленияНабораСНачислениямиСовместителей.ПериодРегистрацииВремени КАК ПериодРегистрацииВремени,
			|	НачисленияНабораСНачислениямиСовместителей.ДоляРезультата КАК ДоляРезультата,
			|	НачисленияНабораСНачислениямиСовместителей.ДокументОснование КАК ДокументОснование
			|ИЗ
			|	ВТНачисленияНабораСНачислениямиСовместителей КАК НачисленияНабораСНачислениямиСовместителей
			|ГДЕ
			|	НачисленияНабораСНачислениямиСовместителей.ДобавленноеНачислениеНабора";
				
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				ЗаписиДобавлялись = Истина;
				НоваяЗапись = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
				// Записи с нулевыми идентификаторами - "технические" записи подработок и внутренних совместителей.
				НоваяЗапись.ИдентификаторСтроки = 0;
				Если ДобавленныеЗаписи <> НеОпределено Тогда
					ДобавленныеЗаписи.Добавить(НоваяЗапись);
				КонецЕсли;
			КонецЦикла;
			
			// Дублирование начислений внутренних подработок.
			Запрос.Текст =
			"ВЫБРАТЬ
			|	Сотрудники.Сотрудник КАК Сотрудник,
			|	Сотрудники.Подработка КАК Подработка,
			|	МИНИМУМ(Сотрудники.ТолькоДляСторно) КАК ТолькоДляСторно
			|ПОМЕСТИТЬ ВТСотрудникиСПодработками
			|ИЗ
			|	(ВЫБРАТЬ
			|		СотрудникиОрганизации.Сотрудник КАК Сотрудник,
			|		СотрудникиОрганизацииПодработки.Сотрудник КАК Подработка,
			|		ЛОЖЬ КАК ТолькоДляСторно
			|	ИЗ
			|		ВТСотрудникиОрганизации КАК СотрудникиОрганизации
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудникиОрганизации КАК СотрудникиОрганизацииПодработки
			|			ПО СотрудникиОрганизации.ФизическоеЛицо = СотрудникиОрганизацииПодработки.ФизическоеЛицо
			|				И СотрудникиОрганизации.Сотрудник = СотрудникиОрганизацииПодработки.ГоловнойСотрудник
			|				И (СотрудникиОрганизацииПодработки.ВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.Подработка))
			|	ГДЕ
			|		СотрудникиОрганизации.ВидЗанятости <> ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.Подработка)
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		ПодработкиИсправленногоДокумента.Сотрудник,
			|		ПодработкиИсправленногоДокумента.Подработка,
			|		ИСТИНА
			|	ИЗ
			|		ВТПодработкиИсправленногоДокумента КАК ПодработкиИсправленногоДокумента) КАК Сотрудники
			|
			|СГРУППИРОВАТЬ ПО
			|	Сотрудники.Сотрудник,
			|	Сотрудники.Подработка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	НачисленияНабора.ПериодРегистрации КАК ПериодРегистрации,
			|	НачисленияНабора.Регистратор КАК Регистратор,
			|	НачисленияНабора.НомерСтроки КАК НомерСтроки,
			|	НачисленияНабора.ВидРасчета КАК ВидРасчета,
			|	НачисленияНабора.ПериодДействия КАК ПериодДействия,
			|	НачисленияНабора.ПериодДействияНачало КАК ПериодДействияНачало,
			|	НачисленияНабора.ПериодДействияКонец КАК ПериодДействияКонец,
			|	НачисленияНабора.БазовыйПериодНачало КАК БазовыйПериодНачало,
			|	НачисленияНабора.БазовыйПериодКонец КАК БазовыйПериодКонец,
			|	НачисленияНабора.Активность КАК Активность,
			|	НачисленияНабора.Сторно КАК Сторно,
			|	НачисленияНабора.СторноТекущегоПериода КАК СторноТекущегоПериода,
			|	СотрудникиСПодработками.Подработка КАК Сотрудник,
			|	НачисленияНабора.ФизическоеЛицо КАК ФизическоеЛицо,
			|	НачисленияНабора.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
			|	0 КАК Результат,
			|	0 КАК ОтработаноДней,
			|	0 КАК ОтработаноЧасов,
			|	0 КАК РезультатВТомЧислеЗаСчетФБ,
			|	НачисленияНабора.ГрафикРаботы КАК ГрафикРаботы,
			|	НачисленияНабора.ВидУчетаВремени КАК ВидУчетаВремени,
			|	НачисленияНабора.ВремяВЧасах КАК ВремяВЧасах,
			|	НачисленияНабора.ГрафикРаботыНорма КАК ГрафикРаботыНорма,
			|	НачисленияНабора.ВремяВЦеломЗаПериод КАК ВремяВЦеломЗаПериод,
			|	НачисленияНабора.ОбщийГрафик КАК ОбщийГрафик,
			|	НачисленияНабора.Организация КАК Организация,
			|	НачисленияНабора.ФиксСтрока КАК ФиксСтрока,
			|	НачисленияНабора.ФиксЗаполнение КАК ФиксЗаполнение,
			|	НачисленияНабора.ФиксРасчетВремени КАК ФиксРасчетВремени,
			|	НачисленияНабора.ФиксРасчет КАК ФиксРасчет,
			|	НачисленияНабора.РасчетнаяБазаЗаЕдиницуНормыВремени КАК РасчетнаяБазаЗаЕдиницуНормыВремени,
			|	НачисленияНабора.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	НачисленияНабора.ПериодРегистрацииВремени КАК ПериодРегистрацииВремени,
			|	НачисленияНабора.ПериодРегистрацииНормыВремени КАК ПериодРегистрацииНормыВремени,
			|	НачисленияНабора.ДоляРезультата КАК ДоляРезультата,
			|	НачисленияНабора.ДокументОснование КАК ДокументОснование
			|ИЗ
			|	ВТСотрудникиСПодработками КАК СотрудникиСПодработками
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНачисленияНабораСНачислениямиСовместителей КАК НачисленияНабора
			|		ПО СотрудникиСПодработками.Сотрудник = НачисленияНабора.Сотрудник
			|			И (ВЫБОР
			|				КОГДА СотрудникиСПодработками.ТолькоДляСторно
			|					ТОГДА НачисленияНабора.Сторно
			|				ИНАЧЕ ИСТИНА
			|			КОНЕЦ)
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачисленияНабораСНачислениямиСовместителей КАК НачисленияНабораГоловныхСотрудников
			|		ПО СотрудникиСПодработками.Подработка = НачисленияНабораГоловныхСотрудников.Сотрудник
			|			И (НачисленияНабора.ВидРасчета = НачисленияНабораГоловныхСотрудников.ВидРасчета)
			|			И (НачисленияНабора.ПериодДействияНачало = НачисленияНабораГоловныхСотрудников.ПериодДействияНачало)
			|			И (НачисленияНабора.ПериодДействияКонец = НачисленияНабораГоловныхСотрудников.ПериодДействияКонец)
			|ГДЕ
			|	НачисленияНабораГоловныхСотрудников.Сотрудник ЕСТЬ NULL";
			
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				ЗаписиДобавлялись = Истина;
				НоваяЗапись = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
				// Записи с нулевыми идентификаторами - "технические" записи подработок и внутренних совместителей.
				НоваяЗапись.ИдентификаторСтроки = 0;
				Если ДобавленныеЗаписи <> НеОпределено Тогда
					ДобавленныеЗаписи.Добавить(НоваяЗапись);
				КонецЕсли;
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
		
	Возврат ЗаписиДобавлялись;
	
КонецФункции

Функция ЕстьДвиженияПоРегистратору(Регистратор) Экспорт 
	Если Не ЗначениеЗаполнено(Регистратор) Тогда
		Возврат Ложь;
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК Поле1
	|ИЗ
	|	РегистрРасчета.Начисления КАК Начисления
	|ГДЕ
	|	Начисления.Регистратор = &Регистратор";
	
	Возврат Не Запрос.Выполнить().Пустой();
КонецФункции	

#КонецОбласти

#КонецЕсли