#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства)
		Или ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Выпуск.Период,
	|	Выпуск.Регистратор,
	|	Выпуск.Организация,
	|	Выпуск.АналитикаУчетаНоменклатуры,
	|	Выпуск.Распоряжение,
	|	Выпуск.КодСтроки,
	|	Выпуск.Заказ,
	|	Выпуск.ВидЗапасов,
	|	Выпуск.Спецификация,
	|	Выпуск.Подразделение,
	|	Выпуск.СтатьяРасходов,
	|	Выпуск.АналитикаРасходов,
	|	Выпуск.АналитикаАктивовПассивов,
	|
	|	Выпуск.Количество,
	|
	|	Выпуск.АналитикаУчетаПродукции
	|ПОМЕСТИТЬ ВыпускПродукцииПередЗаписью
	|ИЗ
	|	РегистрНакопления.ВыпускПродукции КАК Выпуск
	|ГДЕ
	|	Выпуск.Регистратор = &Регистратор
	|");
	
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Или Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства)
		Или ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Таблица.Период                       КАК Период,
	|	Таблица.Организация                  КАК Организация,
	|	Таблица.Регистратор                  КАК Регистратор,
	|	Таблица.АналитикаУчетаНоменклатуры   КАК АналитикаУчетаНоменклатуры,
	|	Таблица.Распоряжение                 КАК Распоряжение,
	|	Таблица.КодСтроки                    КАК КодСтроки,
	|	Таблица.Заказ                        КАК Заказ,
	|	Таблица.ВидЗапасов                   КАК ВидЗапасов,
	|	Таблица.Спецификация                 КАК Спецификация,
	|	Таблица.Подразделение                КАК Подразделение,
	|	Таблица.СтатьяРасходов               КАК СтатьяРасходов,
	|	Таблица.АналитикаРасходов            КАК АналитикаРасходов,
	|	Таблица.АналитикаАктивовПассивов     КАК АналитикаАктивовПассивов,
	|	Таблица.АналитикаУчетаПродукции      КАК АналитикаУчетаПродукции,
	|	СУММА(Таблица.Количество)            КАК КоличествоИзменение
	|ПОМЕСТИТЬ ТаблицаИзмененийВыпускПродукции
	|ИЗ
	|	(ВЫБРАТЬ
	|		Выпуск.Период,
	|		Выпуск.Регистратор,
	|		Выпуск.Организация,
	|		Выпуск.АналитикаУчетаНоменклатуры,
	|		Выпуск.Распоряжение,
	|		Выпуск.КодСтроки,
	|		Выпуск.Заказ,
	|		Выпуск.ВидЗапасов,
	|		Выпуск.Спецификация,
	|		Выпуск.Подразделение,
	|		Выпуск.СтатьяРасходов,
	|		Выпуск.АналитикаРасходов,
	|		Выпуск.АналитикаАктивовПассивов,
	|
	|		Выпуск.Количество,
	|
	|		Выпуск.АналитикаУчетаПродукции
	|	ИЗ
	|		ВыпускПродукцииПередЗаписью КАК Выпуск
	|		
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Выпуск.Период,
	|		Выпуск.Регистратор,
	|		Выпуск.Организация,
	|		Выпуск.АналитикаУчетаНоменклатуры,
	|		Выпуск.Распоряжение,
	|		Выпуск.КодСтроки,
	|		Выпуск.Заказ,
	|		Выпуск.ВидЗапасов,
	|		Выпуск.Спецификация,
	|		Выпуск.Подразделение,
	|		Выпуск.СтатьяРасходов,
	|		Выпуск.АналитикаРасходов,
	|		Выпуск.АналитикаАктивовПассивов,
	|
	|		-Выпуск.Количество,
	|
	|		Выпуск.АналитикаУчетаПродукции
	|	ИЗ
	|		РегистрНакопления.ВыпускПродукции КАК Выпуск
	|	ГДЕ
	|		Выпуск.Регистратор = &Регистратор
	|	) КАК Таблица
	|СГРУППИРОВАТЬ ПО
	|	Таблица.Период,
	|	Таблица.Регистратор,
	|	Таблица.Организация,
	|	Таблица.АналитикаУчетаНоменклатуры,
	|	Таблица.Распоряжение,
	|	Таблица.КодСтроки,
	|	Таблица.Заказ,
	|	Таблица.ВидЗапасов,
	|	Таблица.Спецификация,
	|	Таблица.Подразделение,
	|	Таблица.СтатьяРасходов,
	|	Таблица.АналитикаРасходов,
	|	Таблица.АналитикаАктивовПассивов,
	|	Таблица.АналитикаУчетаПродукции
	|ИМЕЮЩИЕ
	|	СУММА(Таблица.Количество) <> 0
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВыпускПродукцииПередЗаписью
	|");
	
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
