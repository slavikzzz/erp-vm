#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПроведениеДокументов.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	БлокироватьДляИзменения = Истина;
	
	// Текущее состояние набора помещается во временную таблицу,
	// чтобы при записи получить изменение нового набора относительно текущего.
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЭтоНовый",    ДополнительныеСвойства.СвойстваДокумента.ЭтоНовый);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.Период              КАК Период,
	|	Таблица.КодСтроки           КАК КодСтроки,
	|	Таблица.Организация         КАК Организация,
	|	Таблица.Распоряжение        КАК Распоряжение,
	|	Таблица.Отправитель         КАК Отправитель,
	|	Таблица.Получатель          КАК Получатель,
	|	Таблица.Номенклатура        КАК Номенклатура,
	|	Таблица.Характеристика      КАК Характеристика,
	|	Таблица.Назначение          КАК Назначение,
	|	Таблица.Серия               КАК Серия,
	|	Таблица.Операция            КАК Операция,
	|	Таблица.ВидДвижения         КАК ВидДвижения,
	|	ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|			Таблица.Количество
	|		ИНАЧЕ
	|			-Таблица.Количество
	|	КОНЕЦ                       КАК КоличествоПередЗаписью
	|ПОМЕСТИТЬ ДвиженияРаспоряженияНаПередачуИзПроизводстваПередЗаписью
	|ИЗ
	|	РегистрНакопления.РаспоряженияНаПередачуИзПроизводства КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|	И НЕ &ЭтоНовый
	|";
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Или Не ПроведениеДокументов.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МАКСИМУМ(ТаблицаИзменений.ДатаКонтроляПриЗаписи) КАК ДатаКонтроля,
	|	ТаблицаИзменений.Организация                  КАК Организация,
	|	ТаблицаИзменений.Распоряжение                 КАК Распоряжение,
	|	ТаблицаИзменений.КодСтроки                    КАК КодСтроки,
	|	ТаблицаИзменений.Отправитель                  КАК Отправитель,
	|	ТаблицаИзменений.Получатель                   КАК Получатель,
	|	ТаблицаИзменений.Номенклатура                 КАК Номенклатура,
	|	ТаблицаИзменений.Характеристика               КАК Характеристика,
	|	ТаблицаИзменений.Назначение                   КАК Назначение,
	|	ТаблицаИзменений.Серия                        КАК Серия,
	|	ТаблицаИзменений.Операция                     КАК Операция,
	|	СУММА(ТаблицаИзменений.КоличествоИзменение)   КАК КоличествоИзменение
	|ПОМЕСТИТЬ ДвиженияРаспоряженияНаПередачуИзПроизводстваИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДАТАВРЕМЯ(1,1,1)                КАК ДатаКонтроляПриЗаписи,
	|		ВЫБОР
	|			КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА НАЧАЛОПЕРИОДА(Таблица.Период, МЕСЯЦ)
	|			ИНАЧЕ КОНЕЦПЕРИОДА(Таблица.Период, МЕСЯЦ)
	|		КОНЕЦ                           КАК ДатаКонтроляПередЗаписью,
	|		Таблица.Организация             КАК Организация,
	|		Таблица.Распоряжение            КАК Распоряжение,
	|		Таблица.КодСтроки               КАК КодСтроки,
	|		Таблица.Отправитель             КАК Отправитель,
	|		Таблица.Получатель              КАК Получатель,
	|		Таблица.Номенклатура            КАК Номенклатура,
	|		Таблица.Характеристика          КАК Характеристика,
	|		Таблица.Назначение              КАК Назначение,
	|		Таблица.Серия                   КАК Серия,
	|		Таблица.Операция                КАК Операция,
	|		Таблица.КоличествоПередЗаписью  КАК КоличествоИзменение
	|	ИЗ
	|		ДвиженияРаспоряженияНаПередачуИзПроизводстваПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА НАЧАЛОПЕРИОДА(Таблица.Период, МЕСЯЦ)
	|			ИНАЧЕ КОНЕЦПЕРИОДА(Таблица.Период, МЕСЯЦ)
	|		КОНЕЦ                           КАК ДатаКонтроляПриЗаписи,
	|		ДАТАВРЕМЯ(1,1,1)                КАК ДатаКонтроляПередЗаписью,
	|		Таблица.Организация             КАК Организация,
	|		Таблица.Распоряжение            КАК Распоряжение,
	|		Таблица.КодСтроки               КАК КодСтроки,
	|		Таблица.Отправитель             КАК Отправитель,
	|		Таблица.Получатель              КАК Получатель,
	|		Таблица.Номенклатура            КАК Номенклатура,
	|		Таблица.Характеристика          КАК Характеристика,
	|		Таблица.Назначение              КАК Назначение,
	|		Таблица.Серия                   КАК Серия,
	|		Таблица.Операция                КАК Операция,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|				-Таблица.Количество
	|			ИНАЧЕ
	|				Таблица.Количество
	|		КОНЕЦ                           КАК КоличествоИзменение
	|	ИЗ
	|		РегистрНакопления.РаспоряженияНаПередачуИзПроизводства КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор) КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.Организация,
	|	ТаблицаИзменений.Распоряжение,
	|	ТаблицаИзменений.КодСтроки,
	|	ТаблицаИзменений.Отправитель,
	|	ТаблицаИзменений.Получатель,
	|	ТаблицаИзменений.Номенклатура,
	|	ТаблицаИзменений.Характеристика,
	|	ТаблицаИзменений.Назначение,
	|	ТаблицаИзменений.Серия,
	|	ТаблицаИзменений.Операция
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаИзменений.КоличествоИзменение) > 0
	|		ИЛИ МАКСИМУМ(ТаблицаИзменений.ДатаКонтроляПриЗаписи) <> МАКСИМУМ(ТаблицаИзменений.ДатаКонтроляПередЗаписью)
	|;
	|УНИЧТОЖИТЬ ДвиженияРаспоряженияНаПередачуИзПроизводстваПередЗаписью
	|";
	
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	ЕстьИзменения = Выборка.Следующий() И Выборка.Количество > 0;
	
	ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(
		ДополнительныеСвойства,
		"ДвиженияРаспоряженияНаПередачуИзПроизводстваИзменение",
		ЕстьИзменения);
	
	Если ЕстьИзменения
		И ПолучитьФункциональнуюОпцию("ИспользоватьДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров") Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров",
			Константы.ДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров.Получить());
		Запрос.УстановитьПараметр("МерныеТипыЕдиницИзмерений",
			Справочники.УпаковкиЕдиницыИзмерения.МерныеТипыЕдиницИзмерений());
		Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Таблица.Отправитель,
		|	Таблица.Получатель,
		|	Таблица.Организация,
		|	Таблица.Распоряжение,
		|	Таблица.Номенклатура,
		|	Таблица.Характеристика,
		|	Таблица.Назначение,
		|	Таблица.Серия,
		|	Таблица.Операция,
		|	Таблица.КодСтроки
		|ПОМЕСТИТЬ ДвиженияРаспоряженияНаПередачуИзПроизводстваИзменениеМерныеТовары
		|ИЗ
		|	ДвиженияРаспоряженияНаПередачуИзПроизводстваИзменение КАК Таблица
		|
		|ГДЕ
		|	Таблица.Номенклатура.ЕдиницаИзмерения.ТипИзмеряемойВеличины В (&МерныеТипыЕдиницИзмерений)
		|
		|СГРУППИРОВАТЬ ПО
		|	Таблица.Отправитель,
		|	Таблица.Получатель,
		|	Таблица.Организация,
		|	Таблица.Распоряжение,
		|	Таблица.Номенклатура,
		|	Таблица.Характеристика,
		|	Таблица.Назначение,
		|	Таблица.Серия,
		|	Таблица.Операция,
		|	Таблица.КодСтроки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Таблица.Отправитель,
		|	Таблица.Получатель,
		|	Таблица.Организация,
		|	Таблица.Распоряжение,
		|	Таблица.Номенклатура,
		|	Таблица.Характеристика,
		|	Таблица.Назначение,
		|	Таблица.Серия,
		|	Таблица.Операция,
		|	Таблица.КодСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		// Основная таблица
		|ВЫБРАТЬ
		|	КПередаче.ВидДвижения,
		|	КПередаче.Отправитель,
		|	КПередаче.Получатель,
		|	КПередаче.Организация,
		|	КПередаче.Распоряжение,
		|	КПередаче.Номенклатура,
		|	КПередаче.Характеристика,
		|	КПередаче.Назначение,
		|	КПередаче.Серия,
		|	КПередаче.Операция,
		|	КПередаче.КодСтроки,
		|	СУММА(КПередаче.Количество) КАК Количество
		|ПОМЕСТИТЬ ВТРаспоряженияНаПередачуИзПроизводства
		|ИЗ
		|	РегистрНакопления.РаспоряженияНаПередачуИзПроизводства КАК КПередаче
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДвиженияРаспоряженияНаПередачуИзПроизводстваИзменение КАК Изменения
		|	ПО КПередаче.Отправитель			= Изменения.Отправитель
		|	И КПередаче.Получатель				= Изменения.Получатель
		|	И КПередаче.Организация				= Изменения.Организация
		|	И КПередаче.Распоряжение			= Изменения.Распоряжение
		|	И КПередаче.Номенклатура			= Изменения.Номенклатура
		|	И КПередаче.Характеристика			= Изменения.Характеристика
		|	И КПередаче.Назначение				= Изменения.Назначение
		|	И КПередаче.Серия					= Изменения.Серия
		|	И КПередаче.Операция				= Изменения.Операция
		|	И КПередаче.КодСтроки				= Изменения.КодСтроки
		|
		|СГРУППИРОВАТЬ ПО
		|	КПередаче.ВидДвижения,
		|	КПередаче.Отправитель,
		|	КПередаче.Получатель,
		|	КПередаче.Организация,
		|	КПередаче.Распоряжение,
		|	КПередаче.Номенклатура,
		|	КПередаче.Характеристика,
		|	КПередаче.Назначение,
		|	КПередаче.Серия,
		|	КПередаче.Операция,
		|	КПередаче.КодСтроки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	КПередаче.ВидДвижения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		// Допустимые отклонения
		|ВЫБРАТЬ
		|	КПередаче.Отправитель,
		|	КПередаче.Получатель,
		|	КПередаче.Организация,
		|	КПередаче.Распоряжение,
		|	КПередаче.Номенклатура,
		|	КПередаче.Характеристика,
		|	КПередаче.Назначение,
		|	КПередаче.Серия,
		|	КПередаче.Операция,
		|	КПередаче.КодСтроки,
		|	СУММА(КПередаче.Количество
		|		* (&ДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров / 100)) КАК ДопустимоеОтклонение
		|ПОМЕСТИТЬ ВТДопустимыеОтклоненияРаспоряженияНаПередачуИзПроизводства
		|ИЗ
		|	ВТРаспоряженияНаПередачуИзПроизводства КАК КПередаче
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДвиженияРаспоряженияНаПередачуИзПроизводстваИзменениеМерныеТовары КАК Изменения
		|	ПО КПередаче.Отправитель			= Изменения.Отправитель
		|	И КПередаче.Получатель				= Изменения.Получатель
		|	И КПередаче.Организация				= Изменения.Организация
		|	И КПередаче.Распоряжение			= Изменения.Распоряжение
		|	И КПередаче.Номенклатура			= Изменения.Номенклатура
		|	И КПередаче.Характеристика			= Изменения.Характеристика
		|	И КПередаче.Назначение				= Изменения.Назначение
		|	И КПередаче.Серия					= Изменения.Серия
		|	И КПередаче.Операция				= Изменения.Операция
		|	И КПередаче.КодСтроки				= Изменения.КодСтроки
		|
		|ГДЕ
		|	КПередаче.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|
		|СГРУППИРОВАТЬ ПО
		|	КПередаче.Отправитель,
		|	КПередаче.Получатель,
		|	КПередаче.Организация,
		|	КПередаче.Распоряжение,
		|	КПередаче.Номенклатура,
		|	КПередаче.Характеристика,
		|	КПередаче.Назначение,
		|	КПередаче.Серия,
		|	КПередаче.Операция,
		|	КПередаче.КодСтроки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	КПередаче.Отправитель,
		|	КПередаче.Получатель,
		|	КПередаче.Организация,
		|	КПередаче.Распоряжение,
		|	КПередаче.Номенклатура,
		|	КПередаче.Характеристика,
		|	КПередаче.Назначение,
		|	КПередаче.Серия,
		|	КПередаче.Операция,
		|	КПередаче.КодСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		// Удаление списка мерных товаров
		|УНИЧТОЖИТЬ ДвиженияРаспоряженияНаПередачуИзПроизводстваИзменениеМерныеТовары
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|
		//Остатки
		|ВЫБРАТЬ
		|	КПередаче.Отправитель,
		|	КПередаче.Получатель,
		|	КПередаче.Организация,
		|	КПередаче.Распоряжение,
		|	КПередаче.Номенклатура,
		|	КПередаче.Характеристика,
		|	КПередаче.Назначение,
		|	КПередаче.Серия,
		|	КПередаче.Операция,
		|	КПередаче.КодСтроки,
		|	СУММА(ВЫБОР
		|			КОГДА КПередаче.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				ТОГДА КПередаче.Количество
		|			ИНАЧЕ -КПередаче.Количество
		|	КОНЕЦ) КАК Количество
		|ПОМЕСТИТЬ ВТРаспоряженияНаПередачуИзПроизводстваОстатки
		|ИЗ
		|	ВТРаспоряженияНаПередачуИзПроизводства КАК КПередаче
		|
		|СГРУППИРОВАТЬ ПО
		|	КПередаче.Отправитель,
		|	КПередаче.Получатель,
		|	КПередаче.Организация,
		|	КПередаче.Распоряжение,
		|	КПередаче.Номенклатура,
		|	КПередаче.Характеристика,
		|	КПередаче.Назначение,
		|	КПередаче.Серия,
		|	КПередаче.Операция,
		|	КПередаче.КодСтроки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	КПередаче.Отправитель,
		|	КПередаче.Получатель,
		|	КПередаче.Организация,
		|	КПередаче.Распоряжение,
		|	КПередаче.Номенклатура,
		|	КПередаче.Характеристика,
		|	КПередаче.Назначение,
		|	КПередаче.Серия,
		|	КПередаче.Операция,
		|	КПередаче.КодСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		// Задания к корректировке строк мерных товаров
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Остатки.Распоряжение КАК Распоряжение
		|ИЗ
		|	ВТРаспоряженияНаПередачуИзПроизводстваОстатки КАК Остатки
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДопустимыеОтклоненияРаспоряженияНаПередачуИзПроизводства КАК ДопустимыеОтклонения
		|	ПО ДопустимыеОтклонения.Отправитель		= Остатки.Отправитель
		|	И ДопустимыеОтклонения.Получатель		= Остатки.Получатель
		|	И ДопустимыеОтклонения.Организация		= Остатки.Организация
		|	И ДопустимыеОтклонения.Распоряжение		= Остатки.Распоряжение
		|	И ДопустимыеОтклонения.Номенклатура		= Остатки.Номенклатура
		|	И ДопустимыеОтклонения.Характеристика	= Остатки.Характеристика
		|	И ДопустимыеОтклонения.Назначение		= Остатки.Назначение
		|	И ДопустимыеОтклонения.Серия			= Остатки.Серия
		|	И ДопустимыеОтклонения.Операция			= Остатки.Операция
		|	И ДопустимыеОтклонения.КодСтроки		= Остатки.КодСтроки
		|
		|ГДЕ
		|	Остатки.Количество <= ДопустимыеОтклонения.ДопустимоеОтклонение
		|	И Остатки.Количество >= -ДопустимыеОтклонения.ДопустимоеОтклонение
		|";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			РегистрыСведений.ОчередьЗаказовККорректировкеСтрокМерныхТоваров.ДобавитьЗаказВОчередь(Выборка.Распоряжение);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
