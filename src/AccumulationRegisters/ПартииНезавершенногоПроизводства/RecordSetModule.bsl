#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка
		Или РасчетСебестоимостиПрикладныеАлгоритмы.ДвиженияЗаписываютсяРасчетомПартийИСебестоимости(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ РасчетСебестоимостиПрикладныеАлгоритмы.ФормироватьДвиженияРегистровУчетаСебестоимости(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	РасчетСебестоимостиПрикладныеАлгоритмы.СохранитьДвиженияСформированныеРасчетомПартийИСебестоимости(ЭтотОбъект, Замещение);
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства)
		Или ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Партии.Период,
	|	Партии.Регистратор,
	|	Партии.Организация,
	|	Партии.АналитикаУчетаНоменклатуры,
	|	Партии.ВидЗапасов,
	|	Партии.ЗаказНаПроизводство,
	|	Партии.КодСтрокиПродукция,
	|	Партии.ДокументПоступления,
	|	Партии.Этап,
	|	Партии.СтатьяКалькуляции,
	|	Партии.АналитикаУчетаПартий,
	|
	|	Партии.Количество,
	|	Партии.Стоимость,
	|	Партии.СтоимостьБезНДС,
	|	Партии.СтоимостьРегл,
	|	Партии.НДСРегл,
	|	Партии.ПостояннаяРазница,
	|	Партии.ВременнаяРазница
	|ПОМЕСТИТЬ
	|	ПартииНезавершенногоПроизводстваПередЗаписью
	|ИЗ
	|	РегистрНакопления.ПартииНезавершенногоПроизводства КАК Партии
	|ГДЕ
	|	Партии.Регистратор = &Регистратор
	|");
	
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();

КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Или Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства)
		Или ПланыОбмена.ГлавныйУзел() <> Неопределено
		Или РасчетСебестоимостиПрикладныеАлгоритмы.ДвиженияЗаписываютсяРасчетомПартийИСебестоимости(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ РасчетСебестоимостиПрикладныеАлгоритмы.ФормироватьДвиженияРегистровУчетаСебестоимости(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Таблица.Период КАК Период,
	|	Таблица.Регистратор КАК Регистратор,
	|	Таблица.Организация КАК Организация,
	|	Таблица.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Таблица.ВидЗапасов КАК ВидЗапасов,
	|	Таблица.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|	Таблица.КодСтрокиПродукция КАК КодСтрокиПродукция,
	|	Таблица.ДокументПоступления КАК ДокументПоступления,
	|	Таблица.Этап КАК Этап,
	|	Таблица.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	Таблица.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	СУММА(Таблица.Количество) КАК КоличествоИзменение,
	|	СУММА(Таблица.Стоимость) КАК СтоимостьИзменение,
	|	СУММА(Таблица.СтоимостьБезНДС) КАК СтоимостьБезНДСИзменение,
	|	СУММА(Таблица.СтоимостьРегл) КАК СтоимостьРеглИзменение,
	|	СУММА(Таблица.НДСРегл) КАК НДСРеглИзменение,
	|	СУММА(Таблица.ПостояннаяРазница) КАК ПостояннаяРазницаИзменение,
	|	СУММА(Таблица.ВременнаяРазница) КАК ВременнаяРазницаИзменение
	|ПОМЕСТИТЬ ТаблицаИзмененийПартииНезавершенногоПроизводства
	|ИЗ
	|	(ВЫБРАТЬ
	|		Партии.Период,
	|		Партии.Регистратор,
	|		Партии.Организация,
	|		Партии.АналитикаУчетаНоменклатуры,
	|		Партии.ВидЗапасов,
	|		Партии.ЗаказНаПроизводство,
	|		Партии.КодСтрокиПродукция,
	|		Партии.ДокументПоступления,
	|		Партии.Этап,
	|		Партии.СтатьяКалькуляции,
	|		Партии.АналитикаУчетаПартий,
	|		Партии.Количество,
	|		Партии.Стоимость,
	|		Партии.СтоимостьБезНДС,
	|		Партии.СтоимостьРегл,
	|		Партии.НДСРегл,
	|		Партии.ПостояннаяРазница,
	|		Партии.ВременнаяРазница
	|	ИЗ
	|		ПартииНезавершенногоПроизводстваПередЗаписью КАК Партии
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		Партии.Период,
	|		Партии.Регистратор,
	|		Партии.Организация,
	|		Партии.АналитикаУчетаНоменклатуры,
	|		Партии.ВидЗапасов,
	|		Партии.ЗаказНаПроизводство,
	|		Партии.КодСтрокиПродукция,
	|		Партии.ДокументПоступления,
	|		Партии.Этап,
	|		Партии.СтатьяКалькуляции,
	|		Партии.АналитикаУчетаПартий,
	|		-Партии.Количество,
	|		-Партии.Стоимость,
	|		-Партии.СтоимостьБезНДС,
	|		-Партии.СтоимостьРегл,
	|		-Партии.НДСРегл,
	|		-Партии.ПостояннаяРазница,
	|		-Партии.ВременнаяРазница
	|	ИЗ
	|		РегистрНакопления.ПартииНезавершенногоПроизводства КАК Партии
	|	ГДЕ
	|		Партии.Регистратор = &Регистратор) КАК Таблица
	|СГРУППИРОВАТЬ ПО
	|	Таблица.Период,
	|	Таблица.Регистратор,
	|	Таблица.Организация,
	|	Таблица.АналитикаУчетаНоменклатуры,
	|	Таблица.ВидЗапасов,
	|	Таблица.ЗаказНаПроизводство,
	|	Таблица.КодСтрокиПродукция,
	|	Таблица.ДокументПоступления,
	|	Таблица.Этап,
	|	Таблица.СтатьяКалькуляции,
	|	Таблица.АналитикаУчетаПартий
	|ИМЕЮЩИЕ
	|	СУММА(Таблица.Количество) <> 0
	|	ИЛИ СУММА(Таблица.Стоимость) <> 0
	|	ИЛИ СУММА(Таблица.СтоимостьБезНДС) <> 0
	|	ИЛИ СУММА(Таблица.СтоимостьРегл) <> 0
	|	ИЛИ СУММА(Таблица.НДСРегл) <> 0
	|	ИЛИ СУММА(Таблица.ПостояннаяРазница) <> 0
	|	ИЛИ СУММА(Таблица.ВременнаяРазница) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПартииНезавершенногоПроизводстваПередЗаписью");
	
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
