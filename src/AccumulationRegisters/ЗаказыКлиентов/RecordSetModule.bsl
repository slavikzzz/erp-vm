#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если Не ПроведениеДокументов.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	БлокироватьДляИзменения = Истина;
	
	// Текущее состояние набора помещается во временную таблицу "ДвиженияТоварыКОтгрузкеПередЗаписью",
	// чтобы при записи получить изменение нового набора относительно текущего.
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЭтоНовый",    ДополнительныеСвойства.СвойстваДокумента.ЭтоНовый);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.ЗаказКлиента   КАК ЗаказКлиента,
	|	Таблица.Номенклатура   КАК Номенклатура,
	|	Таблица.Характеристика КАК Характеристика,
	|	Таблица.Серия          КАК Серия,
	|	Таблица.КодСтроки      КАК КодСтроки,
	|	Таблица.Склад          КАК Склад,
	|	ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|			Таблица.Заказано
	|		ИНАЧЕ
	|			-Таблица.Заказано
	|	КОНЕЦ                  КАК ЗаказаноПередЗаписью,
	|	ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|			Таблица.КОформлению
	|		ИНАЧЕ
	|			-Таблица.КОформлению
	|	КОНЕЦ                  КАК КОформлениюПередЗаписью,
	|	ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|			Таблица.КПередаче
	|		ИНАЧЕ
	|			-Таблица.КПередаче
	|	КОНЕЦ                  КАК КПередачеПередЗаписью,
	|	ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|			Таблица.Сумма
	|		ИНАЧЕ
	|			-Таблица.Сумма
	|	КОНЕЦ                  КАК СуммаПередЗаписью
	|ПОМЕСТИТЬ ДвиженияЗаказыКлиентовПередЗаписью
	|ИЗ
	|	РегистрНакопления.ЗаказыКлиентов КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|	И НЕ &ЭтоНовый
	|";
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)

	НаборЗаписейРаспоряженияНаОтгрузку = РегистрыНакопления.РаспоряженияНаОтгрузку.СоздатьНаборЗаписей();
	НаборЗаписейРаспоряженияНаОтгрузку.Отбор.Регистратор.Установить(Отбор.Регистратор.Значение);
	ЗаказыКлиента = Выгрузить();
	ЗаказыКлиента.Колонки.Добавить("ВидДвиженияРегистра", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыДвиженияНакопления"));
	ЗаказыКлиента.Колонки.ЗаказКлиента.Имя = "Распоряжение";
	Для Каждого Строка Из ЗаказыКлиента Цикл
		Если Строка.ВидДвижения = ВидДвиженияНакопления.Приход Тогда
			Строка.ВидДвиженияРегистра = Перечисления.ВидыДвиженияНакопления.Приход;
		Иначе
			Строка.ВидДвиженияРегистра = Перечисления.ВидыДвиженияНакопления.Расход;
			Строка.Заказано = -Строка.Заказано;
			Строка.КОформлению = -Строка.КОформлению;
			Строка.КПередаче = -Строка.КПередаче;
			Строка.Сумма = -Строка.Сумма;
		КонецЕсли;
	КонецЦикла;
	Если ЗаказыКлиента.Количество() > 0 Тогда
		НаборЗаписейРаспоряженияНаОтгрузку.Загрузить(ЗаказыКлиента);
	КонецЕсли;
	НаборЗаписейРаспоряженияНаОтгрузку.Записать();

	Если ОбменДанными.Загрузка Или Не ПроведениеДокументов.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаИзменений.ЗаказКлиента                КАК ЗаказКлиента,
	|	ТаблицаИзменений.Номенклатура                КАК Номенклатура,
	|	ТаблицаИзменений.Характеристика              КАК Характеристика,
	|	ТаблицаИзменений.Серия                       КАК Серия,
	|	ТаблицаИзменений.КодСтроки                   КАК КодСтроки,
	|	ТаблицаИзменений.Склад                       КАК Склад,
	|	СУММА(ТаблицаИзменений.ЗаказаноИзменение)    КАК ЗаказаноИзменение,
	|	СУММА(ТаблицаИзменений.КОформлениюИзменение) КАК КОформлениюИзменение,
	|	СУММА(ТаблицаИзменений.КПередачеИзменение)   КАК КПередачеИзменение,
	|	СУММА(ТаблицаИзменений.СуммаИзменение)       КАК СуммаИзменение
	|ПОМЕСТИТЬ ДвиженияЗаказыКлиентовИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.ЗаказКлиента            КАК ЗаказКлиента,
	|		Таблица.Номенклатура            КАК Номенклатура,
	|		Таблица.Характеристика          КАК Характеристика,
	|		Таблица.Серия                   КАК Серия,
	|		Таблица.КодСтроки               КАК КодСтроки,
	|		Таблица.Склад                   КАК Склад,
	|		Таблица.ЗаказаноПередЗаписью    КАК ЗаказаноИзменение,
	|		Таблица.КОформлениюПередЗаписью КАК КОформлениюИзменение,
	|		Таблица.КПередачеПередЗаписью   КАК КПередачеИзменение,
	|		Таблица.СуммаПередЗаписью       КАК СуммаИзменение
	|	ИЗ
	|		ДвиженияЗаказыКлиентовПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.ЗаказКлиента           КАК ЗаказКлиента,
	|		Таблица.Номенклатура           КАК Номенклатура,
	|		Таблица.Характеристика         КАК Характеристика,
	|		Таблица.Серия                  КАК Серия,
	|		Таблица.КодСтроки              КАК КодСтроки,
	|		Таблица.Склад                  КАК Склад,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|				-Таблица.Заказано
	|			ИНАЧЕ
	|				Таблица.Заказано
	|		КОНЕЦ                  КАК ЗаказаноПередЗаписью,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|				-Таблица.КОформлению
	|			ИНАЧЕ
	|				Таблица.КОформлению
	|		КОНЕЦ                  КАК КОформлениюИзменение,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|				-Таблица.КПередаче
	|			ИНАЧЕ
	|				Таблица.КПередаче
	|		КОНЕЦ                  КАК КПередачеИзменение,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|				-Таблица.Сумма
	|			ИНАЧЕ
	|				Таблица.Сумма
	|		КОНЕЦ                  КАК СуммаПередЗаписью
	|	ИЗ
	|		РегистрНакопления.ЗаказыКлиентов КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор) КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.ЗаказКлиента,
	|	ТаблицаИзменений.Номенклатура,
	|	ТаблицаИзменений.Характеристика,
	|	ТаблицаИзменений.Серия,
	|	ТаблицаИзменений.КодСтроки,
	|	ТаблицаИзменений.Склад
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаИзменений.КОформлениюИзменение) <> 0
	|	ИЛИ СУММА(ТаблицаИзменений.КПередачеИзменение) <> 0
	|	ИЛИ СУММА(ТаблицаИзменений.ЗаказаноИзменение) <> 0
	|	ИЛИ СУММА(ТаблицаИзменений.СуммаИзменение) <> 0
	|;
	|УНИЧТОЖИТЬ ДвиженияЗаказыКлиентовПередЗаписью
	|";
	
	МассивРезультатовЗапроса = Запрос.ВыполнитьПакет();
	РезультатЗапроса = МассивРезультатовЗапроса[0]; // РезультатЗапроса
	Выборка = РезультатЗапроса.Выбрать();
	ЕстьИзменения = Выборка.Следующий() И Выборка.Количество > 0;
	ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
		"ДвиженияЗаказыКлиентовИзменение", ЕстьИзменения);
	
	Если ЕстьИзменения
		И ПолучитьФункциональнуюОпцию("ИспользоватьДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров") Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров",
			Константы.ДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров.Получить());
		Запрос.УстановитьПараметр("МерныеТипыЕдиницИзмерений",
			Справочники.УпаковкиЕдиницыИзмерения.МерныеТипыЕдиницИзмерений());
		Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
		
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	Таблица.ЗаказКлиента,
		|	Таблица.Номенклатура,
		|	Таблица.Характеристика,
		|	Таблица.Серия,
		|	Таблица.Склад
		|ПОМЕСТИТЬ ДвиженияЗаказыКлиентовИзменениеМерныеТовары
		|ИЗ
		|	ДвиженияЗаказыКлиентовИзменение КАК Таблица
		|ГДЕ 
		|	Таблица.Номенклатура.ЕдиницаИзмерения.ТипИзмеряемойВеличины В (&МерныеТипыЕдиницИзмерений)
		|СГРУППИРОВАТЬ ПО
		|	Таблица.ЗаказКлиента,
		|	Таблица.Номенклатура,
		|	Таблица.Характеристика,
		|	Таблица.Серия,
		|	Таблица.Склад
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|";
		
		// Основная таблица
		ТекстЗапроса = ТекстЗапроса + "
		|ВЫБРАТЬ
		|	ЗаказыКлиентов.ВидДвижения    КАК ВидДвижения,
		|	ЗаказыКлиентов.ЗаказКлиента   КАК ЗаказКлиента,
		|	ЗаказыКлиентов.Номенклатура   КАК Номенклатура,
		|	ЗаказыКлиентов.Характеристика КАК Характеристика,
		|	ЗаказыКлиентов.Склад          КАК Склад,
		|	ЗаказыКлиентов.Серия          КАК Серия,
		|	ЗаказыКлиентов.КОформлению    КАК КОформлению,
		|	ЗаказыКлиентов.КПередаче      КАК КПередаче
		|ПОМЕСТИТЬ ВТЗаказыКлиентов
		|ИЗ
		|	РегистрНакопления.ЗаказыКлиентов КАК ЗаказыКлиентов
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		ДвиженияЗаказыКлиентовИзменение КАК Изменения
		|		ПО ЗаказыКлиентов.ЗаказКлиента      = Изменения.ЗаказКлиента
		|			И ЗаказыКлиентов.Номенклатура   = Изменения.Номенклатура
		|			И ЗаказыКлиентов.Характеристика = Изменения.Характеристика
		|			И ЗаказыКлиентов.Склад          = Изменения.Склад
		|			И ЗаказыКлиентов.Серия          = Изменения.Серия
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|";
		
		// Допустимые отклонения
		ТекстЗапроса = ТекстЗапроса + "
		|ВЫБРАТЬ
		|	ЗаказыКлиентов.ЗаказКлиента   КАК ЗаказКлиента,
		|	ЗаказыКлиентов.Номенклатура   КАК Номенклатура,
		|	ЗаказыКлиентов.Характеристика КАК Характеристика,
		|	ЗаказыКлиентов.Склад          КАК Склад,
		|	ЗаказыКлиентов.Серия          КАК Серия,
		|	СУММА(ЗаказыКлиентов.КОформлению
		|		* (&ДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров / 100)) КАК ДопустимоеОтклонение,
		|	СУММА(ЗаказыКлиентов.КПередаче
		|		* (&ДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров / 100)) КАК ДопустимоеОтклонениеКПередаче
		|ПОМЕСТИТЬ ВТДопустимыеОтклоненияЗаказыКлиентов
		|ИЗ
		|	ВТЗаказыКлиентов КАК ЗаказыКлиентов
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДвиженияЗаказыКлиентовИзменениеМерныеТовары КАК Изменения
		|		ПО ЗаказыКлиентов.ЗаказКлиента      = Изменения.ЗаказКлиента
		|			И ЗаказыКлиентов.Номенклатура   = Изменения.Номенклатура
		|			И ЗаказыКлиентов.Характеристика = Изменения.Характеристика
		|			И ЗаказыКлиентов.Склад          = Изменения.Склад
		|			И ЗаказыКлиентов.Серия          = Изменения.Серия
		|ГДЕ
		|	ЗаказыКлиентов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|СГРУППИРОВАТЬ ПО
		|	ЗаказыКлиентов.ЗаказКлиента,
		|	ЗаказыКлиентов.Номенклатура,
		|	ЗаказыКлиентов.Характеристика,
		|	ЗаказыКлиентов.Склад,
		|	ЗаказыКлиентов.Серия
		|ИНДЕКСИРОВАТЬ ПО
		|	ЗаказКлиента,
		|	Номенклатура,
		|	Характеристика,
		|	Склад,
		|	Серия
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|";
		
		//Остатки
		ТекстЗапроса = ТекстЗапроса + "
		|ВЫБРАТЬ
		|	ЗаказыКлиентов.ЗаказКлиента       КАК ЗаказКлиента,
		|	ЗаказыКлиентов.Номенклатура       КАК Номенклатура,
		|	ЗаказыКлиентов.Характеристика     КАК Характеристика,
		|	ЗаказыКлиентов.Склад              КАК Склад,
		|	ЗаказыКлиентов.Серия              КАК Серия,
		|	СУММА(ВЫБОР КОГДА ЗаказыКлиентов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
		|			ЗаказыКлиентов.КОформлению
		|		ИНАЧЕ
		|			-ЗаказыКлиентов.КОформлению
		|	КОНЕЦ)                            КАК КОформлениюОстаток,
		|	СУММА(ВЫБОР КОГДА ЗаказыКлиентов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
		|			ЗаказыКлиентов.КПередаче
		|		ИНАЧЕ
		|			-ЗаказыКлиентов.КПередаче
		|	КОНЕЦ)                            КАК КПередачеОстаток
		|ПОМЕСТИТЬ ВТЗаказыКлиентовОстатки
		|ИЗ
		|	ВТЗаказыКлиентов КАК ЗаказыКлиентов
		|СГРУППИРОВАТЬ ПО
		|	ЗаказыКлиентов.ЗаказКлиента,
		|	ЗаказыКлиентов.Номенклатура,
		|	ЗаказыКлиентов.Характеристика,
		|	ЗаказыКлиентов.Склад,
		|	ЗаказыКлиентов.Серия
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|";
		
		ТекстЗапроса = ТекстЗапроса + "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗаказыОстатки.ЗаказКлиента КАК ЗаказКлиента
		|ИЗ
		|	(ВЫБРАТЬ
		|		ЗаказыОстатки.ЗаказКлиента КАК ЗаказКлиента
		|	ИЗ
		|		ВТЗаказыКлиентовОстатки КАК ЗаказыОстатки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|			ВТДопустимыеОтклоненияЗаказыКлиентов КАК ДопустимыеОтклонения
		|			ПО
		|				ЗаказыОстатки.ЗаказКлиента     = ДопустимыеОтклонения.ЗаказКлиента
		|				И ЗаказыОстатки.Номенклатура   = ДопустимыеОтклонения.Номенклатура
		|				И ЗаказыОстатки.Характеристика = ДопустимыеОтклонения.Характеристика
		|				И ЗаказыОстатки.Склад          = ДопустимыеОтклонения.Склад
		|				И ЗаказыОстатки.Серия          = ДопустимыеОтклонения.Серия
		|	ГДЕ
		|		ЗаказыОстатки.КОформлениюОстаток <= ДопустимыеОтклонения.ДопустимоеОтклонение
		|		И ЗаказыОстатки.КОформлениюОстаток >= -ДопустимыеОтклонения.ДопустимоеОтклонение
		|		И ЗаказыОстатки.КОформлениюОстаток <> 0
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ЗаказыОстатки.ЗаказКлиента КАК ЗаказКлиента
		|	ИЗ
		|		ВТЗаказыКлиентовОстатки КАК ЗаказыОстатки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|			ВТДопустимыеОтклоненияЗаказыКлиентов КАК ДопустимыеОтклонения
		|			ПО
		|				ЗаказыОстатки.ЗаказКлиента     = ДопустимыеОтклонения.ЗаказКлиента
		|				И ЗаказыОстатки.Номенклатура   = ДопустимыеОтклонения.Номенклатура
		|				И ЗаказыОстатки.Характеристика = ДопустимыеОтклонения.Характеристика
		|				И ЗаказыОстатки.Склад          = ДопустимыеОтклонения.Склад
		|				И ЗаказыОстатки.Серия          = ДопустимыеОтклонения.Серия
		|	ГДЕ
		|		ЗаказыОстатки.КПередачеОстаток <= ДопустимыеОтклонения.ДопустимоеОтклонениеКПередаче
		|		И ЗаказыОстатки.КПередачеОстаток >= -ДопустимыеОтклонения.ДопустимоеОтклонениеКПередаче
		|		И ЗаказыОстатки.КПередачеОстаток <> 0
		|	) КАК ЗаказыОстатки
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|";
		
		Запрос.Текст = ТекстЗапроса;
		
		ВыборкаЗаказ = Запрос.Выполнить().Выбрать();
		
		Пока ВыборкаЗаказ.Следующий() Цикл
			
			РегистрыСведений.ОчередьЗаказовККорректировкеСтрокМерныхТоваров.ДобавитьЗаказВОчередь(ВыборкаЗаказ.ЗаказКлиента);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли