#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

#Область ОбработчикиПравилРегистрации

// См. ЗарплатаКадрыРасширенныйСинхронизацияДанных.ШаблонОбработчика
Процедура ПриЗаполненииНастроекОбработчиковПравилРегистрации(Настройки) Экспорт
	ЗарплатаКадрыРасширенныйСинхронизацияДанных.ДляРегистраПодчиненногоРегистратору(Настройки);
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОписаниеРегистра() Экспорт
	ОписаниеРегистра = УчетРабочегоВремениРасширенный.ОписаниеРегистраДанныхУчетаВремени();
	
	ОписаниеРегистра.МетаданныеРегистра = Метаданные.РегистрыНакопления.ВытесняемоеПлановоеВремяСотрудников;
	ОписаниеРегистра.ИмяПоляСотрудник = "Сотрудник";
	ОписаниеРегистра.ИмяПоляПериод = "Период";
	ОписаниеРегистра.ИмяПоляПериодРегистрации = "ПериодРегистрации";
	ОписаниеРегистра.ИмяПоляВидДанных = Неопределено;
	ОписаниеРегистра.ВидДанных = Перечисления.ВидыДанныхУчетаВремениСотрудников.ВытесняемоеПлановоеВремя;
	ОписаниеРегистра.ИмяПоляДни = Неопределено;
	
	Возврат ОписаниеРегистра;
КонецФункции

#Область ПервоначальноеЗаполнениеИОбновлениеИнформационнойБазы

Процедура ЗаполнитьОрганизацию(ПараметрыОбновления = Неопределено) Экспорт
	
	ПоследнийОбработанныйРегистратор = Неопределено;
	Пока Истина Цикл
		Запрос = ЗарплатаКадрыРасширенный.ЗапросПолученияРегистраторовДляОбработкиЗаполненияОрганизации("ВытесняемоеПлановоеВремяСотрудников", ПоследнийОбработанныйРегистратор);
		
		Результат = Запрос.Выполнить();

		Если Результат.Пустой() Тогда
			ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
			Возврат;
		Иначе
			ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
		КонецЕсли;
		
		Регистраторы = Результат.Выгрузить().ВыгрузитьКолонку("Регистратор");
		ОрганизацииДокументов = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(Регистраторы, "Организация");
		
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ВытесняемоеПлановоеВремяСотрудников.Период КАК Период,
			|	ВытесняемоеПлановоеВремяСотрудников.Регистратор КАК Регистратор,
			|	ВытесняемоеПлановоеВремяСотрудников.НомерСтроки КАК НомерСтроки,
			|	ВытесняемоеПлановоеВремяСотрудников.Активность КАК Активность,
			|	ВытесняемоеПлановоеВремяСотрудников.Сотрудник КАК Сотрудник,
			|	ВытесняемоеПлановоеВремяСотрудников.Сотрудник.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
			|	ВытесняемоеПлановоеВремяСотрудников.ПериодРегистрации КАК ПериодРегистрации,
			|	ВытесняемоеПлановоеВремяСотрудников.ВидУчетаВремени КАК ВидУчетаВремени,
			|	ВытесняемоеПлановоеВремяСотрудников.Часы КАК Часы,
			|	ВытесняемоеПлановоеВремяСотрудников.ПереходящаяЧастьПредыдущейСмены КАК ПереходящаяЧастьПредыдущейСмены,
			|	ВытесняемоеПлановоеВремяСотрудников.ПереходящаяЧастьТекущейСмены КАК ПереходящаяЧастьТекущейСмены,
			|	ВытесняемоеПлановоеВремяСотрудников.Смена КАК Смена,
			|	ВытесняемоеПлановоеВремяСотрудников.Организация КАК Организация
			|ИЗ
			|	РегистрНакопления.ВытесняемоеПлановоеВремяСотрудников КАК ВытесняемоеПлановоеВремяСотрудников
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРегистраторы КАК ВТРегистраторы
			|		ПО ВытесняемоеПлановоеВремяСотрудников.Регистратор = ВТРегистраторы.Регистратор
			|
			|УПОРЯДОЧИТЬ ПО
			|	ВытесняемоеПлановоеВремяСотрудников.Регистратор";
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.СледующийПоЗначениюПоля("Регистратор") Цикл
			Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрНакопления.ВытесняемоеПлановоеВремяСотрудников.НаборЗаписей", "Регистратор", Выборка.Регистратор) Тогда
				Продолжить;
			КонецЕсли;
			НаборЗаписей = РегистрыНакопления.ВытесняемоеПлановоеВремяСотрудников.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
			Пока Выборка.Следующий() Цикл
				НоваяСтрока = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
				НоваяСтрока.Организация = ОрганизацииДокументов[Выборка.Регистратор];
				Если Не ЗначениеЗаполнено(НоваяСтрока.Организация) Тогда
					НоваяСтрока.Организация = Выборка.ГоловнаяОрганизация;
				КонецЕсли;
			КонецЦикла;
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
			
			ПоследнийОбработанныйРегистратор = Выборка.Регистратор;
		КонецЦикла;
		Если ПараметрыОбновления <> Неопределено Тогда
			Возврат;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли


