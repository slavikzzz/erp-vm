#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Собирает структуру из текстов запросов для дальнейшей проверки даты запрета.
// 
// Параметры:
// 	Запрос - Запрос - Запрос по проверке даты запрета, можно установить параметры
// Возвращаемое значение:
// 	Структура - см. ЗакрытиеМесяцаСервер.ИнициализироватьСтруктуруТекстовЗапросов
Функция ТекстЗапросаКонтрольДатыЗапрета(Запрос) Экспорт
	ИмяРегистра = Метаданные.РегистрыНакопления.ДвиженияПоПрочимАктивамПассивам.Имя;
	ИмяТаблицыИзменений = "ТаблицаИзмененийДвиженияПоПрочимАктивамПассивам"; 
	СтруктураТекстовЗапросов = ПроведениеДокументов.ШаблонТекстЗапросаКонтрольДатыЗапрета(Запрос, 
		ИмяРегистра, 
		ИмяТаблицыИзменений, 
		"ФинансовыйКонтур");
	Возврат СтруктураТекстовЗапросов

КонецФункции

// Формирует текст запроса для формирования временной таблицы "ВтДвиженияПоПрочимАктивамПассивам".
//
// Параметры:
//  ДополнительныеПоля	 - Строка	 - Список дополнительный полей.
//  ВозможныРазныеПериодыВДвижениях - Булево - определяет где хранятся параметры партионного учета, в параметрах запроса или во временной таблице ВТПараметрыПартионногоУчетаДляПроведения
// 
// Возвращаемое значение:
//  Строка - Текст запроса формирования временной таблицы ВтДвиженияПоПрочимАктивамПассивам.
//
Функция ТекстЗапросаТаблицаВтДвиженияПоПрочимАктивамПассивам(ДополнительныеПоля = "", ВозможныРазныеПериодыВДвижениях = Ложь) Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Строки.Период                           КАК Период,
	|	Строки.Организация                      КАК Организация,
	|	Строки.Подразделение                    КАК Подразделение,
	|	Строки.Статья                           КАК Статья,
	|	Строки.Аналитика                        КАК Аналитика,
	|	(ВЫБОР
	|		КОГДА ЕСТЬNULL(НаправленияДеятельности.УчетЗатрат, ЛОЖЬ)
	|			ТОГДА Строки.НаправлениеДеятельности
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КОНЕЦ) КАК НаправлениеДеятельности,
	
	//++ НЕ УТ
	|	Строки.НастройкаСчетовУчета КАК НастройкаСчетовУчета,
	//-- НЕ УТ
	
	|	Строки.ДебетКредит КАК ДебетКредит,
	|
	|	Строки.СуммаСНДС                        КАК СуммаСНДС,
	|	Строки.СуммаБезНДС                      КАК СуммаБезНДС,
	|	(ВЫБОР
	|		КОГДА НЕ &УправленческийУчетОрганизаций
	|			ТОГДА 0
	|		КОГДА Строки.ВидДеятельностиНДС В (&ТипыНалогообложенияНДСУчитываетсяВСтоимости)
	|				И НЕ &ФормироватьНДСПредъявленныйПриВключенииВСтоимость
	|			ТОГДА Строки.СуммаУпр
	|		ИНАЧЕ Строки.СуммаБезНДСУпр КОНЕЦ)  КАК СуммаУпр,
	|	(ВЫБОР
	|		КОГДА Строки.ВидДеятельностиНДС В (&ТипыНалогообложенияНДСУчитываетсяВСтоимости)
	|				И НЕ &ФормироватьНДСПредъявленныйПриВключенииВСтоимость
	|			ТОГДА Строки.СуммаРегл
	|		ИНАЧЕ Строки.СуммаБезНДСРегл КОНЕЦ) КАК СуммаРегл,
	|
	|	Строки.ПостояннаяРазница                КАК ПостояннаяРазница,
	|	Строки.ВременнаяРазница                 КАК ВременнаяРазница,
	|
	|	Строки.ИдентификаторФинЗаписи           КАК ИдентификаторФинЗаписи,
	|	Строки.НастройкаХозяйственнойОперации   КАК НастройкаХозяйственнойОперации
	|
	|	,&ДополнительныеПоля
	|
	|ПОМЕСТИТЬ ВтДвиженияПоПрочимАктивамПассивам
	|ИЗ
	|	ВтИсходныеДвиженияПоПрочимАктивамПассивам КАК Строки
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НаправленияДеятельности КАК НаправленияДеятельности
	|		ПО НаправленияДеятельности.Ссылка = Строки.НаправлениеДеятельности
	|	ЛЕВОЕ СОЕДИНЕНИЕ Константы КАК ТаблицаКонстанты ПО ИСТИНА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Строки.Период                           КАК Период,
	|	Строки.Организация                      КАК Организация,
	|	Строки.Подразделение                    КАК Подразделение,
	|	Строки.Статья                           КАК Статья,
	|	Строки.Аналитика                        КАК Аналитика,
	|	(ВЫБОР
	|		КОГДА ЕСТЬNULL(НаправленияДеятельности.УчетЗатрат, ЛОЖЬ)
	|			ТОГДА Строки.НаправлениеДеятельности
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КОНЕЦ) КАК НаправлениеДеятельности,
	
	//++ НЕ УТ
	|	Строки.НастройкаСчетовУчета КАК НастройкаСчетовУчета,
	//-- НЕ УТ
	
	|	Строки.ДебетКредит КАК ДебетКредит,
	|	0                                       КАК СуммаСНДС,
	|	0                                       КАК СуммаБезНДС,
	|	(ВЫБОР
	|		КОГДА НЕ &УправленческийУчетОрганизаций
	|			ТОГДА 0
	|		ИНАЧЕ
	|			Строки.СуммаСНДС - Строки.СуммаБезНДС
	|		КОНЕЦ)                                КАК СуммаУпр,
	|	Строки.СуммаРегл - Строки.СуммаБезНДСРегл КАК СуммаРегл,
	|
	|	Строки.ПостояннаяРазница                КАК ПостояннаяРазница,
	|	Строки.ВременнаяРазница                 КАК ВременнаяРазница,
	|
	|	&Идентификатор                          КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ВключениеНДСВСтоимость) КАК НастройкаХозяйственнойОперации
	|
	|	,&ДополнительныеПоля
	|
	|ИЗ
	|	ВтИсходныеДвиженияПоПрочимАктивамПассивам КАК Строки
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НаправленияДеятельности КАК НаправленияДеятельности
	|		ПО НаправленияДеятельности.Ссылка = Строки.НаправлениеДеятельности
	|	ЛЕВОЕ СОЕДИНЕНИЕ Константы КАК ТаблицаКонстанты ПО ИСТИНА
	|ГДЕ
	|	&ФормироватьНДСПредъявленныйПриВключенииВСтоимость
	|	И Строки.ВидДеятельностиНДС В (&ТипыНалогообложенияНДСУчитываетсяВСтоимости)
	|";
	
	ТекстСоединения = "ЛЕВОЕ СОЕДИНЕНИЕ Константы КАК ТаблицаКонстанты ПО ИСТИНА";
	
	Если НЕ ВозможныРазныеПериодыВДвижениях Тогда
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ТекстСоединения, "");
		
	Иначе
		
		// Параметры партионного учета хранятся не в параметрах запроса, а в одноименных полях временной таблицы ВТПараметрыПартионногоУчетаДляПроведения.
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ТекстСоединения,
			"ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПараметрыПартионногоУчетаДляПроведения КАК ПараметрыПартионногоУчета
			|	ПО ПараметрыПартионногоУчета.Период = НАЧАЛОПЕРИОДА(Строки.Период, МЕСЯЦ)
			|	 И ПараметрыПартионногоУчета.Организация = Строки.Организация");
		
		МассивПараметров = Новый Массив;
		МассивПараметров.Добавить("УправленческийУчетОрганизаций");
		МассивПараметров.Добавить("ФормироватьНДСПредъявленныйПриВключенииВСтоимость");
		
		Для Каждого ТекущийПараметр Из МассивПараметров Цикл
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&" + ТекущийПараметр, "ПараметрыПартионногоУчета." + ТекущийПараметр);
		КонецЦикла;
		
	КонецЕсли;
	
	Если СтрНайти(ДополнительныеПоля, "ИдентификаторСтроки") <> 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Идентификатор", "Строки.ИдентификаторСтроки");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Идентификатор", "Строки.ИдентификаторФинЗаписи");
	КонецЕсли;
	
	ДобавитьДополнительныеПоляВТекстЗапроса(ДополнительныеПоля, ТекстЗапроса);
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Формирует текст запроса для формирования движений по регистру "Движения по прочим активам пассивам".
//
// Параметры:
//  ДополнительныеПоля	 - Строка	 - Список дополнительный полей.
// 
// Возвращаемое значение:
//  Строка - Текст запроса формирования движений в регистре ДвиженияПоПрочимАктивамПассивам.
//
Функция ТекстЗапросаТаблицаДвиженияПоПрочимАктивамПассивам(ДополнительныеПоля = "") Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	Строки.Подразделение КАК Подразделение,
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(НД.УчетЗатрат, ЛОЖЬ) ТОГДА
	|			Строки.НаправлениеДеятельности
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	Строки.Статья КАК Статья,
	|	Строки.Аналитика КАК Аналитика,
	
	//++ НЕ УТ
	|	Строки.НастройкаСчетовУчета КАК НастройкаСчетовУчета,
	//-- НЕ УТ
	
	|	Строки.ДебетКредит КАК ДебетКредит,
	|
	|	Строки.СуммаБезНДС КАК СуммаБезНДС,
	|	Строки.СуммаСНДС КАК СуммаСНДС,
	|	Строки.СуммаУпр КАК СуммаУпр,
	|
	|	Строки.СуммаРегл КАК СуммаРегл,
	|	Строки.ПостояннаяРазница КАК ПостояннаяРазница,
	|	Строки.ВременнаяРазница КАК ВременнаяРазница,
	|
	|	Строки.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|	Строки.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|
	|	,&ДополнительныеПоля
	|
	|ИЗ
	|	ВтДвиженияПоПрочимАктивамПассивам КАК Строки
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НаправленияДеятельности КАК НД
	|		ПО НД.Ссылка = Строки.НаправлениеДеятельности
	|ГДЕ
	|	(Строки.СуммаБезНДС <> 0 ИЛИ Строки.СуммаСНДС <> 0 ИЛИ Строки.СуммаУпр <> 0
	|	ИЛИ Строки.СуммаРегл <> 0 ИЛИ Строки.ПостояннаяРазница <> 0 ИЛИ Строки.ВременнаяРазница <> 0)
	|";
	
	ДобавитьДополнительныеПоляВТекстЗапроса(ДополнительныеПоля, ТекстЗапроса);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

//++ НЕ УТ

// Заполняет параметры отражения движений регистра в финансовом учете
//
// Параметры:
//  МетаданныеРегистра - ОбъектМетаданныхРегистрНакопления - Метаданные регистра накопления
//  РегистрацияКОтражению - Булево - Признак получения параметров для регистрации к отражению в учете
//
// Возвращаемое значение:
// 	см. ФинансовыйУчетПоДаннымБалансовыхРегистров.ПараметрыОтраженияДвиженийВФинансовомУчете
//
Функция ПараметрыОтраженияДвиженийВФинансовомУчете(МетаданныеРегистра = Неопределено, РегистрацияКОтражению = Ложь) Экспорт
	
	ПараметрыОтражения = ФинансовыйУчетПоДаннымБалансовыхРегистров.ПараметрыОтраженияДвиженийВФинансовомУчете(РегистрацияКОтражению);
	
	Если РегистрацияКОтражению Тогда
		Возврат ПараметрыОтражения;
	КонецЕсли;
	
	ПараметрыОтражения.ПутьКДаннымНаправлениеДеятельности = "НаправлениеДеятельности";
	ПараметрыОтражения.ПутьКДаннымПодразделение = "Подразделение";
	//++ НЕ УТКА
	ПараметрыОтражения.ПутьКДаннымМестоУчета = "Подразделение";
	//-- НЕ УТКА
	ПараметрыОтражения.РесурсыУпр.Добавить("СуммаУпр");
	ПараметрыОтражения.РесурсыРегл.Добавить("СуммаРегл");
	ПараметрыОтражения.ТипДанныхУчета = Перечисления.ТипыДанныхУчета.ПрочиеАктивыПассивы;
	ПараметрыОтражения.СтруктураАналитики = ОбщегоНазначения.СкопироватьРекурсивно(ИсточникиДанныхПовтИсп.СтруктураАналитикиПоТипуДанныхУчета(ПараметрыОтражения.ТипДанныхУчета));
	ПараметрыОтражения.СтруктураАналитики.СтатьяАктивовПассивов.ПутьКДанным = "Статья";
	ПараметрыОтражения.СтруктураАналитики.АналитикаАктивовПассивов.ПутьКДанным = "Аналитика";
	ПараметрыОтражения.СтруктураАналитики.ГруппаФинансовогоУчета.Вставить("Значение", Неопределено);
	ПараметрыОтражения.СтруктураАналитики.Контрагент.Вставить("Значение", Неопределено);
	ПараметрыОтражения.ПутьКДаннымДопНастройкаХозОперации = СтрЗаменить(
		"ВЫБОР
		|		КОГДА ПсевдонимИсточникаДанных.НастройкаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПрочиеРасходы)
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПрочиеРасходыАктивыПассивы)
		|		КОГДА ПсевдонимИсточникаДанных.НастройкаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПрочиеДоходы)
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПрочиеДоходыАктивыПассивы)
		|		КОГДА ПсевдонимИсточникаДанных.НастройкаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ЗакрытиеРасходовОтСписанияОС)
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СписаниеОС)
		|		КОГДА ПсевдонимИсточникаДанных.НастройкаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПеремещениеТоваровМеждуФилиалами)
		|			И ПсевдонимИсточникаДанных.ДебетКредит = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Кредит)
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ВнутреннееПоступлениеТоваров)
		|		КОГДА ПсевдонимИсточникаДанных.Статья В (ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РезервыПоВознаграждениям), ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РезервыПоСтраховымВзносам))
		|			И ПсевдонимИсточникаДанных.ДебетКредит = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Дебет)
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СписаниеРезервовПредстоящихРасходов)
		|		КОГДА ПсевдонимИсточникаДанных.Статья = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.НДСПоОтгрузкамБезПереходаПраваСобственности)
		|			И ПсевдонимИсточникаДанных.ДебетКредит = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Кредит)
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.НачислениеНДСпоОтгрузкеТоваровВПути)
		|		ИНАЧЕ ПсевдонимИсточникаДанных.НастройкаХозяйственнойОперации
		|	КОНЕЦ",
		"ПсевдонимИсточникаДанных",
		ПараметрыОтражения.ПсевдонимИсточникаДанных);
	ОпределитьПоказатели(ПараметрыОтражения);
	
	ПараметрыОтражения.УсловиеДебет = СтрЗаменить(
		"ПсевдонимИсточникаДанных.ДебетКредит = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Дебет)",
		"ПсевдонимИсточникаДанных",
		ПараметрыОтражения.ПсевдонимИсточникаДанных);
		
	ПараметрыОтражения.УсловиеКредит = СтрЗаменить(
		"ПсевдонимИсточникаДанных.ДебетКредит = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Кредит)",
		"ПсевдонимИсточникаДанных",
		ПараметрыОтражения.ПсевдонимИсточникаДанных);
	
	Если МетаданныеРегистра = Неопределено Тогда
		МетаданныеРегистра = СоздатьНаборЗаписей().Метаданные();
	КонецЕсли;
	
	ФинансовыйУчетПоДаннымБалансовыхРегистров.ЗаполнитьПараметрыОтраженияПоМетаданнымРегистра(ПараметрыОтражения, МетаданныеРегистра);
	
	Возврат ПараметрыОтражения;
	
КонецФункции

//-- НЕ УТ

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьДополнительныеПоляВТекстЗапроса(ДополнительныеПоля, ТекстЗапроса)

	Если НЕ ЗначениеЗаполнено(ДополнительныеПоля) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ",&ДополнительныеПоля", "");
		Возврат;
	КонецЕсли; 
	
	ТекстДополнительныеПоля = "";
	
	СписокПолей = СтрРазделить(ДополнительныеПоля, ",");
	Для каждого ИмяПоля Из СписокПолей Цикл
		ТекстДополнительныеПоля = ТекстДополнительныеПоля + "
		|	,Строки." + ИмяПоля;
	КонецЦикла; 
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ",&ДополнительныеПоля", ТекстДополнительныеПоля); 

КонецПроцедуры

//++ НЕ УТ

// Определяет показатели регистра
//
// Параметры:
//  ПараметрыОтражения - см. ПараметрыОтраженияДвиженийВФинансовомУчете
//
Процедура ОпределитьПоказатели(ПараметрыОтражения)
	
	// Сумма
	ПараметрыОтраженияПоказателя = Перечисления.ПоказателиАналитическихРегистров.ПараметрыОтраженияПоказателя();
	ПараметрыОтраженияПоказателя.РесурсыУпр.Добавить("СуммаУпр");
	ПараметрыОтраженияПоказателя.РесурсыРегл.Добавить("СуммаРегл");
	ПараметрыОтраженияПоказателя.ПутьКДаннымИдентификаторФинЗаписи = СтрЗаменить(
		"ВЫБОР
		|		КОГДА ПсевдонимИсточникаДанных.Статья = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.НДСПоОтгрузкамБезПереходаПраваСобственности)
		|			И ПсевдонимИсточникаДанных.ОбъектРасчетов <> ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)
		|			ТОГДА ПсевдонимИсточникаДанных.ОбъектРасчетов.УникальныйИдентификатор
		|		ИНАЧЕ ПсевдонимИсточникаДанных.ИдентификаторФинЗаписи
		|	КОНЕЦ",
		"ПсевдонимИсточникаДанных",
		ФинансовыйУчетПоДаннымБалансовыхРегистров.ПсевдонимИсточникаДанныхПоУмолчанию());
	ПараметрыОтражения.Показатели.Вставить(Перечисления.ПоказателиАналитическихРегистров.Сумма, ПараметрыОтраженияПоказателя);
	
КонецПроцедуры

//-- НЕ УТ

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыНакопления.ДвиженияПоПрочимАктивамПассивам.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "11.5.17.183";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("919746fa-cb93-4b40-96d3-cf3580744665");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыНакопления.ДвиженияПоПрочимАктивамПассивам.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Порядок = Перечисления.ПорядокОбработчиковОбновления.Обычный;
	Обработчик.Комментарий = ""
	//++ НЕ УТ
		+ НСтр("ru = 'Заполняет измерение ""Аналитика"" предопределенным значением ""Резерв ежегодных отпусков"" справочника ""Резервы""
		|в движениях со значением измерения ""Статья"" равным ""Резервы по вознаграждениям"" или ""Резервы по страховым взносам"".
		|';
		|en = 'Заполняет измерение ""Аналитика"" предопределенным значением ""Резерв ежегодных отпусков"" справочника ""Резервы""
		|в движениях со значением измерения ""Статья"" равным ""Резервы по вознаграждениям"" или ""Резервы по страховым взносам"".
		|'")
	//-- НЕ УТ
		+ НСтр("ru = 'Устанавливает время записей для документов ""Бронирование"".';
				|en = 'Устанавливает время записей для документов ""Бронирование"".'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.РегистрыНакопления.ДвиженияПоПрочимАктивамПассивам.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыНакопления.ДвиженияПоПрочимАктивамПассивам.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.РегистрыНакопления.ДвиженияПоПрочимАктивамПассивам.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");

КонецПроцедуры

// Описание
// 
// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры = Неопределено) Экспорт
	
	МетаданныеРегистра = СоздатьНаборЗаписей().Метаданные();
	ПолноеИмяРегистра = МетаданныеРегистра.ПолноеИмя();
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаРегистров = ПолноеИмяРегистра;
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Период УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Период УБЫВ");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиРегистраторыРегистра();
	
	ТекстЗапроса = "
	//++ НЕ УТ
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДвиженияПоПрочимАктивамПассивам.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.ДвиженияПоПрочимАктивамПассивам КАК ДвиженияПоПрочимАктивамПассивам
	|ГДЕ
	|	ЛОЖЬ
	//++ Локализация
	|	ИЛИ
	|	(ДвиженияПоПрочимАктивамПассивам.Статья В (
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РезервыПоВознаграждениям),
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РезервыПоСтраховымВзносам))
	|	И НЕ ДвиженияПоПрочимАктивамПассивам.Аналитика ССЫЛКА Справочник.Резервы)
	//-- Локализация
	|
	// Исправить время движений документов ""Бронирование""
	|ОБЪЕДИНИТЬ ВСЕ
	|
	//-- НЕ УТ
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДвиженияПоПрочимАктивамПассивам.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.ДвиженияПоПрочимАктивамПассивам КАК ДвиженияПоПрочимАктивамПассивам
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Бронирование КАК Бронирование
	|	ПО ДвиженияПоПрочимАктивамПассивам.Регистратор = Бронирование.Ссылка
	|ГДЕ
	|	&НоваяАрхитектураВзаиморасчетов
	|	И ГОД(ДвиженияПоПрочимАктивамПассивам.Период) = ГОД(Бронирование.Дата)
	|	И МЕСЯЦ(ДвиженияПоПрочимАктивамПассивам.Период) = МЕСЯЦ(Бронирование.Дата)
	|	И ДЕНЬ(ДвиженияПоПрочимАктивамПассивам.Период) = ДЕНЬ(Бронирование.Дата)
	|	И (ЧАС(ДвиженияПоПрочимАктивамПассивам.Период) <> ЧАС(Бронирование.Дата)
	|		ИЛИ МИНУТА(ДвиженияПоПрочимАктивамПассивам.Период) <> МИНУТА(Бронирование.Дата)
	|		ИЛИ СЕКУНДА(ДвиженияПоПрочимАктивамПассивам.Период) <> СЕКУНДА(Бронирование.Дата))
	|";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("НоваяАрхитектураВзаиморасчетов", ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов"));
	Результат = Запрос.Выполнить();
	
	Регистраторы = Результат.Выгрузить().ВыгрузитьКолонку("Регистратор");
	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(Параметры, Регистраторы, ПолноеИмяРегистра);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	МетаданныеРегистра = СоздатьНаборЗаписей().Метаданные();
	ПолноеИмяРегистра = МетаданныеРегистра.ПолноеИмя();
	
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Если ОбновляемыеДанные.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяРегистра);
		Возврат;
	КонецЕсли;
	
	Для Каждого Выборка Из ОбновляемыеДанные Цикл
		НачатьТранзакцию();
		
		Попытка
			
			Регистратор = Выборка.Регистратор;// ДокументСсылка - 
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяРегистра + ".НаборЗаписей");
			ЭлементБлокировки.УстановитьЗначение("Регистратор", Регистратор);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();
			
			НаборЗаписей = СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);
			НаборЗаписей.Прочитать();
			
			#Область ИсправлениеВремениОперацииБронирования
			ДатаДокумента = Дата(1,1,1);
			Если ТипЗнч(Выборка.Регистратор) = Тип("ДокументСсылка.Бронирование") Тогда
				ДатаДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Выборка.Регистратор, "Дата");
			КонецЕсли;
			#КонецОбласти
			
			Для Каждого ЗаписьРегистра Из НаборЗаписей Цикл
				//++ НЕ УТ
				
				//++ Локализация
				#Область ЗаполнениеАналитикиРезервов
				Если ЗаписьРегистра.Статья = ПланыВидовХарактеристик.СтатьиАктивовПассивов.РезервыПоВознаграждениям
				 ИЛИ ЗаписьРегистра.Статья = ПланыВидовХарактеристик.СтатьиАктивовПассивов.РезервыПоСтраховымВзносам Тогда
					Если ТипЗнч(ЗаписьРегистра.Аналитика) <> Тип("СправочникСсылка.Резервы") ИЛИ НЕ ЗначениеЗаполнено(ЗаписьРегистра.Аналитика) Тогда
						ЗаписьРегистра.Аналитика = Справочники.Резервы.РезервЕжегодныхОтпусков();
					КонецЕсли;
				КонецЕсли;
				#КонецОбласти
				//-- Локализация
				
				//-- НЕ УТ
				#Область ИсправлениеВремениОперацииБронирования
				Если ТипЗнч(ЗаписьРегистра.Регистратор) = Тип("ДокументСсылка.Бронирование")
					И ЗначениеЗаполнено(ДатаДокумента) Тогда
					РегистрыНакопления.РасчетыСПоставщиками.ПроверитьИУстановитьВремяПериодаЗаписи(ЗаписьРегистра, ДатаДокумента);
				КонецЕсли;
				#КонецОбласти
			КонецЦикла;
			
			Если НаборЗаписей.Модифицированность() Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(НаборЗаписей);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			Шаблон = НСтр("ru = 'Не удалось записать данные в регистр %1 по регистратору ""%2"", по причине: %3';
							|en = 'Cannot save the data to the %1 register by the ""%2"" recorder. Reason: %3'");
			
			ТекстСообщения = 	СтрШаблон(
				Шаблон,
				ПолноеИмяРегистра,
				Регистратор,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,
				МетаданныеРегистра,,
				ТекстСообщения);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяРегистра);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
