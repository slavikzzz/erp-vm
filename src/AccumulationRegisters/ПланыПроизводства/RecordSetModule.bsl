#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
//++ НЕ УТКА

	// В режиме "Новый расчет" ручные корректировки отменяются в пакетном режиме перед началом расчета
	Если НЕ ДополнительныеСвойства.Свойство("РежимРасчета")
		ИЛИ ДополнительныеСвойства.РежимРасчета = РасчетПланаПроизводства.РежимПересчет() Тогда
		
		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
		ДополнительныеСвойства.Вставить("МенеджерВременныхТаблиц", МенеджерВременныхТаблиц);
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Движения.Период КАК Период,
		|	Движения.Сценарий КАК Сценарий,
		|	Движения.Номенклатура КАК Номенклатура,
		|	Движения.Характеристика КАК Характеристика,
		|	Движения.Подразделение КАК Подразделение,
		|	Движения.ПланПроизводства КАК ПланПроизводства,
		|	Движения.Спецификация КАК Спецификация,
		|	Движения.Назначение КАК Назначение,
		|	Движения.ДатаВыпускаПолуфабриката КАК ДатаВыпускаПолуфабриката,
		|	Движения.ДатаЗапускаВыпуска КАК ДатаЗапускаВыпуска,
		|	Движения.ЭтоПолуфабрикат КАК ЭтоПолуфабрикат,
		|	Движения.ТипПроизводственногоПроцесса КАК ТипПроизводственногоПроцесса,
		|	Движения.Количество КАК Количество,
		|	Движения.КЗаказу КАК КЗаказу,
		|	Движения.РучнаяКорректировка КАК РучнаяКорректировка,
		|	Движения.ДатаКорректировки КАК ДатаКорректировки,
		|	Движения.АвторКорректировки КАК АвторКорректировки,
		|	Движения.Комментарий КАК Комментарий
		|ПОМЕСТИТЬ СписокКорректировокПередЗаписью
		|ИЗ
		|	РегистрНакопления.ПланыПроизводства КАК Движения
		|ГДЕ
		|	Движения.Регистратор = &Регистратор
		|	И Движения.РучнаяКорректировка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА
		|ИЗ
		|	СписокКорректировокПередЗаписью КАК СписокКорректировокПередЗаписью");
	
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		
		РезультатЗапроса = Запрос.ВыполнитьПакет();  
		Выборка = РезультатЗапроса[0].Выбрать();
		Выборка.Следующий();
		
		ДополнительныеСвойства.Вставить("ЕстьКорректировки", Выборка.Количество > 0);
		
	КонецЕсли;
//-- НЕ УТКА

КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
//++ НЕ УТКА

	// Информация об отмененных ручных корректировках сохраняется
	Если ДополнительныеСвойства.Свойство("ЕстьКорректировки") И ДополнительныеСвойства.ЕстьКорректировки Тогда
		
		РегистрыНакопления.ПланыПроизводства.СохранитьРучныеКорректировкиПриЗаписи(Отбор.Регистратор.Значение, ДополнительныеСвойства.МенеджерВременныхТаблиц);
		
	КонецЕсли;
//-- НЕ УТКА
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
