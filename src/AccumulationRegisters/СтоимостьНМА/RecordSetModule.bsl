#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ПодготовитьДанныеДляФормированияЗаданийПередЗаписью();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПодготовитьДанныеДляФормированияЗаданийПриЗаписи();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПодготовитьДанныеДляФормированияЗаданийПередЗаписью()
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаПередЗаписью.Активность КАК Активность,
	|	ТаблицаПередЗаписью.Период КАК Период,
	|	ТаблицаПередЗаписью.Организация КАК Организация,
	|	ТаблицаПередЗаписью.Подразделение КАК Подразделение,
	|	ТаблицаПередЗаписью.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	ТаблицаПередЗаписью.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаПередЗаписью.НематериальныйАктив КАК НематериальныйАктив,
	|	ТаблицаПередЗаписью.Стоимость КАК Стоимость,
	|	ТаблицаПередЗаписью.СтоимостьРегл КАК СтоимостьРегл,
	|	ТаблицаПередЗаписью.СтоимостьНУ КАК СтоимостьНУ,
	|	ТаблицаПередЗаписью.СтоимостьПР КАК СтоимостьПР,
	|	ТаблицаПередЗаписью.СтоимостьВР КАК СтоимостьВР,
	|	ТаблицаПередЗаписью.СтоимостьЦФ КАК СтоимостьЦФ,
	|	ТаблицаПередЗаписью.СтоимостьНУЦФ КАК СтоимостьНУЦФ,
	|	ТаблицаПередЗаписью.СтоимостьПРЦФ КАК СтоимостьПРЦФ,
	|	ТаблицаПередЗаписью.СтоимостьВРЦФ КАК СтоимостьВРЦФ,
	|	ТаблицаПередЗаписью.РезервПереоценкиСтоимости КАК РезервПереоценкиСтоимости,
	|	ТаблицаПередЗаписью.РезервПереоценкиСтоимостиРегл КАК РезервПереоценкиСтоимостиРегл,
	|	ТаблицаПередЗаписью.ПриростСтоимости КАК ПриростСтоимости,
	|	ТаблицаПередЗаписью.ПриростСтоимостиРегл КАК ПриростСтоимостиРегл,
	|	ТаблицаПередЗаписью.ПредварительнаяСтоимость КАК ПредварительнаяСтоимость,
	|	ТаблицаПередЗаписью.ПредварительнаяСтоимостьРегл КАК ПредварительнаяСтоимостьРегл,
	|	ТаблицаПередЗаписью.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ТаблицаПередЗаписью.ВидДвижения КАК ВидДвижения,
	|	ТаблицаПередЗаписью.ВариантПримененияЦелевогоФинансирования КАК ВариантПримененияЦелевогоФинансирования,
	|	ТаблицаПередЗаписью.ОтражатьВРеглУчете КАК ОтражатьВРеглУчете,
	|	ТаблицаПередЗаписью.ОтражатьВУпрУчете КАК ОтражатьВУпрУчете
	|
	|ПОМЕСТИТЬ СтоимостьНМАПередЗаписью
	|
	|ИЗ
	|	РегистрНакопления.СтоимостьНМА КАК ТаблицаПередЗаписью
	|
	|ГДЕ
	|	ТаблицаПередЗаписью.Регистратор = &Регистратор";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПодготовитьДанныеДляФормированияЗаданийПриЗаписи()
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаОтличий.Период КАК Период,
	|	ТаблицаОтличий.Организация КАК Организация,
	|	ТаблицаОтличий.НематериальныйАктив КАК НематериальныйАктив,
	|	МАКСИМУМ(ТаблицаОтличий.ОтражатьВРеглУчете) КАК ОтражатьВРеглУчете,
	|	МАКСИМУМ(ТаблицаОтличий.ОтражатьВУпрУчете) КАК ОтражатьВУпрУчете,
	|	&Регистратор КАК Документ
	|
	|ПОМЕСТИТЬ СтоимостьНМАИзменение
	|
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаПередЗаписью.Активность КАК Активность,
	|		ТаблицаПередЗаписью.Период КАК Период,
	|		ТаблицаПередЗаписью.Организация КАК Организация,
	|		ТаблицаПередЗаписью.НематериальныйАктив КАК НематериальныйАктив,
	|		ТаблицаПередЗаписью.Подразделение КАК Подразделение,
	|		ТаблицаПередЗаписью.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|		ТаблицаПередЗаписью.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПередЗаписью.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ТаблицаПередЗаписью.Стоимость
	|			ИНАЧЕ - ТаблицаПередЗаписью.Стоимость
	|		КОНЕЦ КАК Стоимость,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПередЗаписью.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ТаблицаПередЗаписью.СтоимостьРегл 
	|			ИНАЧЕ - ТаблицаПередЗаписью.СтоимостьРегл 
	|		КОНЕЦ КАК СтоимостьРегл,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПередЗаписью.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ТаблицаПередЗаписью.СтоимостьНУ
	|			ИНАЧЕ - ТаблицаПередЗаписью.СтоимостьНУ
	|		КОНЕЦ КАК СтоимостьНУ,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПередЗаписью.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ТаблицаПередЗаписью.СтоимостьПР
	|			ИНАЧЕ - ТаблицаПередЗаписью.СтоимостьПР
	|		КОНЕЦ КАК СтоимостьПР,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПередЗаписью.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ТаблицаПередЗаписью.СтоимостьВР
	|			ИНАЧЕ - ТаблицаПередЗаписью.СтоимостьВР
	|		КОНЕЦ КАК СтоимостьВР,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПередЗаписью.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ТаблицаПередЗаписью.СтоимостьЦФ
	|			ИНАЧЕ - ТаблицаПередЗаписью.СтоимостьЦФ
	|		КОНЕЦ КАК СтоимостьЦФ,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПередЗаписью.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ТаблицаПередЗаписью.СтоимостьНУЦФ
	|			ИНАЧЕ - ТаблицаПередЗаписью.СтоимостьНУЦФ
	|		КОНЕЦ КАК СтоимостьНУЦФ,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПередЗаписью.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ТаблицаПередЗаписью.СтоимостьПРЦФ
	|			ИНАЧЕ - ТаблицаПередЗаписью.СтоимостьПРЦФ
	|		КОНЕЦ КАК СтоимостьПРЦФ,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПередЗаписью.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ТаблицаПередЗаписью.СтоимостьВРЦФ
	|			ИНАЧЕ - ТаблицаПередЗаписью.СтоимостьВРЦФ
	|		КОНЕЦ КАК СтоимостьВРЦФ,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПередЗаписью.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ТаблицаПередЗаписью.РезервПереоценкиСтоимости
	|			ИНАЧЕ - ТаблицаПередЗаписью.РезервПереоценкиСтоимости
	|		КОНЕЦ КАК РезервПереоценкиСтоимости,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПередЗаписью.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ТаблицаПередЗаписью.РезервПереоценкиСтоимостиРегл
	|			ИНАЧЕ - ТаблицаПередЗаписью.РезервПереоценкиСтоимостиРегл
	|		КОНЕЦ КАК РезервПереоценкиСтоимостиРегл,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПередЗаписью.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ТаблицаПередЗаписью.ПриростСтоимости
	|			ИНАЧЕ - ТаблицаПередЗаписью.ПриростСтоимости
	|		КОНЕЦ КАК ПриростСтоимости,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПередЗаписью.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ТаблицаПередЗаписью.ПриростСтоимостиРегл
	|			ИНАЧЕ - ТаблицаПередЗаписью.ПриростСтоимостиРегл
	|		КОНЕЦ КАК ПриростСтоимостиРегл,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПередЗаписью.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ТаблицаПередЗаписью.ПредварительнаяСтоимость
	|			ИНАЧЕ - ТаблицаПередЗаписью.ПредварительнаяСтоимость
	|		КОНЕЦ КАК ПредварительнаяСтоимость,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПередЗаписью.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ТаблицаПередЗаписью.ПредварительнаяСтоимостьРегл
	|			ИНАЧЕ - ТаблицаПередЗаписью.ПредварительнаяСтоимостьРегл
	|		КОНЕЦ КАК ПредварительнаяСтоимостьРегл,
	|
	|		ТаблицаПередЗаписью.ОтражатьВРеглУчете  КАК ОтражатьВРеглУчете,
	|		ТаблицаПередЗаписью.ОтражатьВУпрУчете   КАК ОтражатьВУпрУчете
	|	ИЗ
	|		СтоимостьНМАПередЗаписью КАК ТаблицаПередЗаписью
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ТаблицаПриЗаписи.Активность КАК Активность,
	|		ТаблицаПриЗаписи.Период КАК Период,
	|		ТаблицаПриЗаписи.Организация КАК Организация,
	|		ТаблицаПриЗаписи.НематериальныйАктив КАК НематериальныйАктив,
	|		ТаблицаПриЗаписи.Подразделение КАК Подразделение,
	|		ТаблицаПриЗаписи.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|		ТаблицаПриЗаписи.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ТаблицаПриЗаписи.Стоимость
	|			ИНАЧЕ ТаблицаПриЗаписи.Стоимость
	|		КОНЕЦ КАК Стоимость,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ТаблицаПриЗаписи.СтоимостьРегл 
	|			ИНАЧЕ ТаблицаПриЗаписи.СтоимостьРегл 
	|		КОНЕЦ КАК СтоимостьРегл,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ТаблицаПриЗаписи.СтоимостьНУ
	|			ИНАЧЕ ТаблицаПриЗаписи.СтоимостьНУ
	|		КОНЕЦ КАК СтоимостьНУ,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ТаблицаПриЗаписи.СтоимостьПР
	|			ИНАЧЕ ТаблицаПриЗаписи.СтоимостьПР
	|		КОНЕЦ КАК СтоимостьПР,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ТаблицаПриЗаписи.СтоимостьВР
	|			ИНАЧЕ ТаблицаПриЗаписи.СтоимостьВР
	|		КОНЕЦ КАК СтоимостьВР,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ТаблицаПриЗаписи.СтоимостьЦФ
	|			ИНАЧЕ ТаблицаПриЗаписи.СтоимостьЦФ
	|		КОНЕЦ КАК СтоимостьЦФ,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ТаблицаПриЗаписи.СтоимостьНУЦФ
	|			ИНАЧЕ ТаблицаПриЗаписи.СтоимостьНУЦФ
	|		КОНЕЦ КАК СтоимостьНУЦФ,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ТаблицаПриЗаписи.СтоимостьПРЦФ
	|			ИНАЧЕ ТаблицаПриЗаписи.СтоимостьПРЦФ
	|		КОНЕЦ КАК СтоимостьПРЦФ,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ТаблицаПриЗаписи.СтоимостьВРЦФ
	|			ИНАЧЕ ТаблицаПриЗаписи.СтоимостьВРЦФ
	|		КОНЕЦ КАК СтоимостьВРЦФ,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ТаблицаПриЗаписи.РезервПереоценкиСтоимости
	|			ИНАЧЕ ТаблицаПриЗаписи.РезервПереоценкиСтоимости
	|		КОНЕЦ КАК РезервПереоценкиСтоимости,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ТаблицаПриЗаписи.РезервПереоценкиСтоимостиРегл
	|			ИНАЧЕ ТаблицаПриЗаписи.РезервПереоценкиСтоимостиРегл
	|		КОНЕЦ КАК РезервПереоценкиСтоимостиРегл,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ТаблицаПриЗаписи.ПриростСтоимости
	|			ИНАЧЕ ТаблицаПриЗаписи.ПриростСтоимости
	|		КОНЕЦ КАК ПриростСтоимости,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ТаблицаПриЗаписи.ПриростСтоимостиРегл
	|			ИНАЧЕ ТаблицаПриЗаписи.ПриростСтоимостиРегл
	|		КОНЕЦ КАК ПриростСтоимостиРегл,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ТаблицаПриЗаписи.ПредварительнаяСтоимость
	|			ИНАЧЕ ТаблицаПриЗаписи.ПредварительнаяСтоимость
	|		КОНЕЦ КАК ПредварительнаяСтоимость,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ТаблицаПриЗаписи.ПредварительнаяСтоимостьРегл
	|			ИНАЧЕ ТаблицаПриЗаписи.ПредварительнаяСтоимостьРегл
	|		КОНЕЦ КАК ПредварительнаяСтоимостьРегл,
	|
	|		ТаблицаПриЗаписи.ОтражатьВРеглУчете  КАК ОтражатьВРеглУчете,
	|		ТаблицаПриЗаписи.ОтражатьВУпрУчете   КАК ОтражатьВУпрУчете
	|	ИЗ
	|		РегистрНакопления.СтоимостьНМА КАК ТаблицаПриЗаписи
	|	ГДЕ
	|		ТаблицаПриЗаписи.Регистратор = &Регистратор
	|
	|	) КАК ТаблицаОтличий
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаОтличий.Активность,
	|	ТаблицаОтличий.Период,
	|	ТаблицаОтличий.Организация,
	|	ТаблицаОтличий.НематериальныйАктив,
	|	ТаблицаОтличий.Подразделение,
	|	ТаблицаОтличий.ГруппаФинансовогоУчета,
	|	ТаблицаОтличий.НаправлениеДеятельности
	|
	|ИМЕЮЩИЕ
	|	(СУММА(ТаблицаОтличий.Стоимость) <> 0
	|		ИЛИ СУММА(ТаблицаОтличий.СтоимостьРегл) <> 0
	|		ИЛИ СУММА(ТаблицаОтличий.СтоимостьНУ) <> 0
	|		ИЛИ СУММА(ТаблицаОтличий.СтоимостьПР) <> 0
	|		ИЛИ СУММА(ТаблицаОтличий.СтоимостьВР) <> 0
	|		ИЛИ СУММА(ТаблицаОтличий.СтоимостьЦФ) <> 0
	|		ИЛИ СУММА(ТаблицаОтличий.СтоимостьНУЦФ) <> 0
	|		ИЛИ СУММА(ТаблицаОтличий.СтоимостьПРЦФ) <> 0
	|		ИЛИ СУММА(ТаблицаОтличий.СтоимостьВРЦФ) <> 0
	|		ИЛИ СУММА(ТаблицаОтличий.ПредварительнаяСтоимость) <> 0
	|		ИЛИ СУММА(ТаблицаОтличий.ПредварительнаяСтоимостьРегл) <> 0
	|		ИЛИ СУММА(ТаблицаОтличий.РезервПереоценкиСтоимости) <> 0
	|		ИЛИ СУММА(ТаблицаОтличий.РезервПереоценкиСтоимостиРегл) <> 0
	|		ИЛИ СУММА(ТаблицаОтличий.ПриростСтоимости) <> 0
	|		ИЛИ СУММА(ТаблицаОтличий.ПриростСтоимостиРегл) <> 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Период              КАК Период,
	|	Таблица.Организация         КАК Организация,
	|	Таблица.НематериальныйАктив КАК ОбъектУчета,
	|	&Регистратор                КАК Документ
	|ПОМЕСТИТЬ СтоимостьНМА_ЗаданияКРасчетуСтоимостиВНАИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаПередЗаписью.Период               КАК Период,
	|		ТаблицаПередЗаписью.Организация          КАК Организация,
	|		ТаблицаПередЗаписью.НематериальныйАктив  КАК НематериальныйАктив
	|	ИЗ
	|		СтоимостьНМАПередЗаписью КАК ТаблицаПередЗаписью
	|	ГДЕ
	|		ТаблицаПередЗаписью.ХозяйственнаяОперация В (
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуНМА),
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.УлучшениеНМА),
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеНДС))
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ТаблицаПриЗаписи.Период,
	|		ТаблицаПриЗаписи.Организация,
	|		ТаблицаПриЗаписи.НематериальныйАктив
	|	ИЗ
	|		РегистрНакопления.СтоимостьНМА КАК ТаблицаПриЗаписи
	|	ГДЕ
	|		ТаблицаПриЗаписи.Регистратор = &Регистратор
	|		И ТаблицаПриЗаписи.ХозяйственнаяОперация В (
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуНМА),
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.УлучшениеНМА),
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеНДС))
	|	) КАК Таблица
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СтоимостьНМАПередЗаписью
	|";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Выборка = РезультатЗапроса[0].Выбрать();
	ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
		"СтоимостьНМАИзменение", Выборка.Следующий() И Выборка.Количество > 0);
	
	Выборка = РезультатЗапроса[1].Выбрать();
	ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
		"СтоимостьНМА_ЗаданияКРасчетуСтоимостиВНАИзменение", Выборка.Следующий() И Выборка.Количество > 0);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
