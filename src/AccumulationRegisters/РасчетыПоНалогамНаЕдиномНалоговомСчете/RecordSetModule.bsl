#Если Сервер ИЛИ ТолстыйКлиентОбычноеПриложение ИЛИ ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	// Проверка ОбменДанными.Загрузка не требуется, т.к. данный объект в РИБ при записи должен создавать задания.
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства)
		Или ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Таблица.Период            КАК Период,
		|	Таблица.ВидДвижения       КАК ВидДвижения,
		|	Таблица.Организация       КАК Организация,
		|	Таблица.Налог             КАК Налог,
		|	Таблица.КодБК             КАК КодБК,
		|	Таблица.СрокУплаты        КАК СрокУплаты,
		|	Таблица.ПлатежныйДокумент КАК ПлатежныйДокумент,
		|	Таблица.Сумма             КАК Сумма
		|ПОМЕСТИТЬ РасчетыПоНалогамНаЕдиномНалоговомСчетеПередЗаписью
		|ИЗ
		|	РегистрНакопления.РасчетыПоНалогамНаЕдиномНалоговомСчете КАК Таблица
		|ГДЕ
		|	Таблица.Регистратор = &Регистратор";
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	// Проверка ОбменДанными.Загрузка не требуется, т.к. данный объект в РИБ при записи должен создавать задания.
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства)
		Или ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ТипыНДФЛВзносы", ИнтеграцияБЗК.ТипыНалоговНДФЛИВзносов());
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаИзменений.Период КАК Период,
	|	ТаблицаИзменений.Организация КАК Организация,
	|	ТаблицаИзменений.Налог КАК Налог,
	|	ТаблицаИзменений.КодБК КАК КодБК,
	|	ТаблицаИзменений.СрокУплаты КАК СрокУплаты,
	|	ТаблицаИзменений.ПлатежныйДокумент КАК ПлатежныйДокумент,
	|	ТаблицаИзменений.ВидДвижения КАК ВидДвижения,
	|	СУММА(ТаблицаИзменений.СуммаИзменение) КАК СуммаИзменение
	|ПОМЕСТИТЬ РасчетыПоНалогамНаЕдиномНалоговомСчетеИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Период КАК Период,
	|		Таблица.Организация КАК Организация,
	|		Таблица.Налог КАК Налог,
	|		Таблица.КодБК КАК КодБК,
	|		Таблица.СрокУплаты КАК СрокУплаты,
	|		Таблица.ПлатежныйДокумент КАК ПлатежныйДокумент,
	|		Таблица.ВидДвижения КАК ВидДвижения,
	|		Таблица.Сумма КАК СуммаИзменение
	|	ИЗ
	|		РасчетыПоНалогамНаЕдиномНалоговомСчетеПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Период,
	|		Таблица.Организация,
	|		Таблица.Налог,
	|		Таблица.КодБК,
	|		Таблица.СрокУплаты,
	|		Таблица.ПлатежныйДокумент,
	|		Таблица.ВидДвижения,
	|		-Таблица.Сумма
	|	ИЗ
	|		РегистрНакопления.РасчетыПоНалогамНаЕдиномНалоговомСчете КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор) КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.Период,
	|	ТаблицаИзменений.Организация,
	|	ТаблицаИзменений.Налог,
	|	ТаблицаИзменений.КодБК,
	|	ТаблицаИзменений.СрокУплаты,
	|	ТаблицаИзменений.ВидДвижения,
	|	ТаблицаИзменений.ПлатежныйДокумент
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаИзменений.СуммаИзменение) <> 0
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОрганизацииНаУСН.Организация КАК Организация,
	|	ОрганизацииНаУСН.Период КАК НачалоПериода,
	|	ОрганизацииНаУСН.ПрименяетсяУСНДоходыМинусРасходы КАК ПрименяетсяУСНДоходыМинусРасходы,
	|	МИНИМУМ(ВЫБОР
	|		КОГДА ЕСТЬNULL(ПоздниеНастройкиНеУСН.Период, ДАТАВРЕМЯ(2120, 1, 1)) < ЕСТЬNULL(ПоздниеНастройкиУСН.Период, ДАТАВРЕМЯ(2120, 1, 1)) ТОГДА
	|			ЕСТЬNULL(ПоздниеНастройкиНеУСН.Период, ДАТАВРЕМЯ(2120, 1, 1))
	|		КОГДА ЕСТЬNULL(ПоздниеНастройкиНеУСН.Период, ДАТАВРЕМЯ(2120, 1, 1)) >= ЕСТЬNULL(ПоздниеНастройкиУСН.Период, ДАТАВРЕМЯ(2120, 1, 1)) ТОГДА
	|			ЕСТЬNULL(ПоздниеНастройкиУСН.Период, ДАТАВРЕМЯ(2120, 1, 1))
	|	КОНЕЦ) КАК КонецПериода
	|ПОМЕСТИТЬ ВТДанныеПоОрганизациямНаУСН
	|ИЗ
	|	РегистрСведений.НастройкиУчетаУСН КАК ОрганизацииНаУСН
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиСистемыНалогообложения КАК ПоздниеНастройкиНеУСН
	|		ПО ОрганизацииНаУСН.Организация = ПоздниеНастройкиНеУСН.Организация
	|			И ОрганизацииНаУСН.Период < ПоздниеНастройкиНеУСН.Период
	|			И (НЕ ПоздниеНастройкиНеУСН.ПрименяетсяУСН)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиУчетаУСН КАК ПоздниеНастройкиУСН
	|		ПО ОрганизацииНаУСН.Организация = ПоздниеНастройкиУСН.Организация
	|			И ОрганизацииНаУСН.Период < ПоздниеНастройкиУСН.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ОрганизацииНаУСН.Организация,
	|	ОрганизацииНаУСН.Период,
	|	ОрганизацииНаУСН.ПрименяетсяУСНДоходыМинусРасходы
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Период КАК Месяц,
	|	Таблица.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ОперацииЗакрытияМесяца.РасчетЕНС) КАК Операция,
	|	&Регистратор КАК Документ
	|ИЗ
	|	РасчетыПоНалогамНаЕдиномНалоговомСчетеПередЗаписью КАК Таблица
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Таблица.Период,
	|	Таблица.Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ОперацииЗакрытияМесяца.РасчетЕНС),
	|	&Регистратор
	|ИЗ
	|	РегистрНакопления.РасчетыПоНалогамНаЕдиномНалоговомСчете КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Период,
	|	Таблица.Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ОперацииЗакрытияМесяца.ПризнаниеРасходовПриУСН),
	|	&Регистратор
	|ИЗ
	|	РасчетыПоНалогамНаЕдиномНалоговомСчетеИзменение КАК Таблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДанныеПоОрганизациямНаУСН КАК ОрганизацииНаУСН
	|		ПО (ОрганизацииНаУСН.Организация = Таблица.Организация)
	|			И (Таблица.Период >= ОрганизацииНаУСН.НачалоПериода)
	|			И (Таблица.Период < ОрганизацииНаУСН.КонецПериода)
	|			И ОрганизацииНаУСН.ПрименяетсяУСНДоходыМинусРасходы
	|ГДЕ
	|	Таблица.Налог В (&ТипыНДФЛВзносы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ РасчетыПоНалогамНаЕдиномНалоговомСчетеПередЗаписью";
	
	Запрос.Текст = ТекстЗапроса;
	Результат = Запрос.ВыполнитьПакет();
	
	Если Не Результат[2].Пустой() И НЕ ТипЗнч(Отбор.Регистратор.Значение) = Тип("ДокументСсылка.РегламентнаяОперация") Тогда
		РегистрыСведений.ЗаданияКЗакрытиюМесяца.СоздатьЗаписиРегистраПоДаннымВыборки(Результат[2].Выбрать());
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли