
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	НастроитьОтборПоПолучателю();
	
	НастроитьДинамическийСписок();
	
	УстановитьЗаголовокФормы();
	
	МодификацияКонфигурацииПереопределяемый.ПриСозданииНаСервере(ЭтаФорма, СтандартнаяОбработка, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОтборПоПолучателюПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ОтборПоПолучателю", ОтборПоПолучателю);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовСписка

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	Если Элементы.Список.ТекущиеДанные <> Неопределено Тогда
		ЗначениеВыбора = Элементы.Список.ТекущиеДанные.ЭтапПотребитель;
	Иначе
		ЗначениеВыбора = Неопределено;
	КонецЕсли;
	
	ОповеститьОВыборе(ЗначениеВыбора);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	Возврат; // в данной конфигурации действия не предусмотрены
	
КонецПроцедуры

&НаСервере
Процедура НастроитьОтборПоПолучателю()
	
	ОтборПоПолучателю = Параметры.Свойство("Получатель") И ЗначениеЗаполнено(Параметры.Получатель);
	Если ОтборПоПолучателю Тогда
		Если ТипЗнч(Параметры.Получатель) = Тип("СправочникСсылка.Склады") Тогда
			Шаблон = НСтр("ru = 'Потребность на складе: ""%1""';
							|en = 'Demand in warehouse: ""%1""'");
		Иначе
			Шаблон = НСтр("ru = 'Потребность в подразделении: ""%1""';
							|en = 'Demand in business unit: ""%1""'");
		КонецЕсли;
		Элементы.ОтборПоПолучателю.Заголовок = СтрШаблон(Шаблон, Параметры.Получатель);
	КонецЕсли;
	Элементы.ОтборПоПолучателю.Видимость = ОтборПоПолучателю;

КонецПроцедуры

&НаСервере
Процедура НастроитьДинамическийСписок()
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Распоряжение", Параметры.Распоряжение);
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Номенклатура", Параметры.Номенклатура);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Характеристика", Параметры.Характеристика);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Назначение", Параметры.Назначение);
	
	Если ТипЗнч(Параметры.Получатель) = Тип("СправочникСсылка.Склады") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Склад", Параметры.Получатель);
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Склад", Справочники.Склады.ПустаяСсылка());
	КонецЕсли;
	Если ТипЗнч(Параметры.Получатель) = Тип("СправочникСсылка.СтруктураПредприятия") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Подразделение", Параметры.Получатель);
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Подразделение", Справочники.СтруктураПредприятия.ПустаяСсылка());
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ОтборПоПолучателю", ОтборПоПолучателю);
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Предшественники", Документы.ЭтапПроизводства2_2.ВсеПредшественники(Параметры.Источник));
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Регистратор", Параметры.Источник);
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ПустаяСсылкаЭтап", Документы.ЭтапПроизводства2_2.ПустаяСсылка());
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовокФормы()
	
	ТекстНоменклатура = СтрШаблон(НСтр("ru = 'Номенклатура: %1';
										|en = 'Items: %1'"), Параметры.Номенклатура);
	
	Если ЗначениеЗаполнено(Параметры.Характеристика) Тогда
		
		ТекстНоменклатура = ТекстНоменклатура + ", " + СтрШаблон(НСтр("ru = 'Характеристика: %1';
																		|en = 'Variant: %1'"), Параметры.Характеристика);
		
	КонецЕсли;
	
	Заголовок = СтрШаблон(НСтр("ru = 'Выбор этапа потребителя полуфабриката (%1)';
								|en = 'Select semi-finished product consumer stage (%1)'"), ТекстНоменклатура);
	
КонецПроцедуры

#КонецОбласти
