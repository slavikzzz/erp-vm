#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПроведениеДокументов.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	БлокироватьДляИзменения = Истина;
	
	// Текущее состояние набора помещается во временную таблицу,
	// чтобы при записи получить изменение нового набора относительно текущего.
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЭтоНовый",    ДополнительныеСвойства.СвойстваДокумента.ЭтоНовый);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.Распоряжение                 КАК Распоряжение,
	|	Таблица.Номенклатура                 КАК Номенклатура,
	|	Таблица.Характеристика               КАК Характеристика,
	|	Таблица.Серия                        КАК Серия,
	|	Таблица.Склад                        КАК Склад,
	|	Таблица.Подразделение                КАК Подразделение,
	|	Таблица.Назначение                   КАК Назначение,
	|	Таблица.НазначениеОтправителя        КАК НазначениеОтправителя,
	|	Таблица.КодСтроки                    КАК КодСтроки,
	|	ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|			Таблица.КОформлению
	|		ИНАЧЕ
	|			-Таблица.КОформлению
	|	КОНЕЦ                                КАК КОформлениюПередЗаписью
	|ПОМЕСТИТЬ ДвиженияЗаказыМатериаловВПроизводствоПередЗаписью
	|ИЗ
	|	РегистрНакопления.ЗаказыМатериаловВПроизводство КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|	И НЕ &ЭтоНовый
	|";
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Или Не ПроведениеДокументов.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаИзменений.Распоряжение                КАК Распоряжение,
	|	ТаблицаИзменений.Номенклатура                КАК Номенклатура,
	|	ТаблицаИзменений.Характеристика              КАК Характеристика,
	|	ТаблицаИзменений.Серия                       КАК Серия,
	|	ТаблицаИзменений.КодСтроки                   КАК КодСтроки,
	|	ТаблицаИзменений.Склад                       КАК Склад,
	|	ТаблицаИзменений.Подразделение               КАК Подразделение,
	|	ТаблицаИзменений.Назначение                  КАК Назначение,
	|	ТаблицаИзменений.НазначениеОтправителя       КАК НазначениеОтправителя,
	|	СУММА(ТаблицаИзменений.КОформлениюИзменение) КАК КОформлениюИзменение
	|ПОМЕСТИТЬ ДвиженияЗаказыМатериаловВПроизводствоИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Распоряжение                 КАК Распоряжение,
	|		Таблица.Номенклатура                 КАК Номенклатура,
	|		Таблица.Характеристика               КАК Характеристика,
	|		Таблица.Серия                        КАК Серия,
	|		Таблица.Склад                        КАК Склад,
	|		Таблица.Подразделение                КАК Подразделение,
	|		Таблица.Назначение                   КАК Назначение,
	|		Таблица.НазначениеОтправителя        КАК НазначениеОтправителя,
	|		Таблица.КодСтроки                    КАК КодСтроки,
	|		Таблица.КОформлениюПередЗаписью      КАК КОформлениюИзменение
	|	ИЗ
	|		ДвиженияЗаказыМатериаловВПроизводствоПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Распоряжение                 КАК Распоряжение,
	|		Таблица.Номенклатура                 КАК Номенклатура,
	|		Таблица.Характеристика               КАК Характеристика,
	|		Таблица.Серия                        КАК Серия,
	|		Таблица.Склад                        КАК Склад,
	|		Таблица.Подразделение                КАК Подразделение,
	|		Таблица.Назначение                   КАК Назначение,
	|		Таблица.НазначениеОтправителя        КАК НазначениеОтправителя,
	|		Таблица.КодСтроки                    КАК КодСтроки,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|				-Таблица.КОформлению
	|			ИНАЧЕ
	|				Таблица.КОформлению
	|		КОНЕЦ                                КАК КОформлениюИзменение
	|	ИЗ
	|		РегистрНакопления.ЗаказыМатериаловВПроизводство КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор) КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.Распоряжение,
	|	ТаблицаИзменений.Номенклатура,
	|	ТаблицаИзменений.Характеристика,
	|	ТаблицаИзменений.Серия,
	|	ТаблицаИзменений.Склад,
	|	ТаблицаИзменений.Подразделение,
	|	ТаблицаИзменений.Назначение,
	|	ТаблицаИзменений.НазначениеОтправителя,
	|	ТаблицаИзменений.КодСтроки
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаИзменений.КОформлениюИзменение) > 0
	|;
	|УНИЧТОЖИТЬ ДвиженияЗаказыМатериаловВПроизводствоПередЗаписью
	|";
	
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	ЕстьИзменения = Выборка.Следующий() И Выборка.Количество > 0;
	ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
		"ДвиженияЗаказыМатериаловВПроизводствоИзменение", ЕстьИзменения);
	
	Если ЕстьИзменения
		И ПолучитьФункциональнуюОпцию("ИспользоватьДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров") Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров",
			Константы.ДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров.Получить());
		Запрос.УстановитьПараметр("МерныеТипыЕдиницИзмерений",
			Справочники.УпаковкиЕдиницыИзмерения.МерныеТипыЕдиницИзмерений());
		Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
		
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	Таблица.Распоряжение,
		|	Таблица.Номенклатура,
		|	Таблица.Характеристика,
		|	Таблица.Серия,
		|	Таблица.Назначение,
		|	Таблица.НазначениеОтправителя,
		|	Таблица.Подразделение,
		|	Таблица.КодСтроки,
		|	Таблица.Склад
		|ПОМЕСТИТЬ ДвиженияЗаказыМатериаловВПроизводствоИзменениеМерныеТовары
		|ИЗ
		|	ДвиженияЗаказыМатериаловВПроизводствоИзменение КАК Таблица
		|ГДЕ 
		|	Таблица.Номенклатура.ЕдиницаИзмерения.ТипИзмеряемойВеличины В (&МерныеТипыЕдиницИзмерений)
		|СГРУППИРОВАТЬ ПО
		|	Таблица.Распоряжение,
		|	Таблица.Номенклатура,
		|	Таблица.Характеристика,
		|	Таблица.Серия,
		|	Таблица.Назначение,
		|	Таблица.НазначениеОтправителя,
		|	Таблица.Подразделение,
		|	Таблица.КодСтроки,
		|	Таблица.Склад
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|
		// Основная таблица
		|ВЫБРАТЬ
		|	Таблица.ВидДвижения    КАК ВидДвижения,
		|	Таблица.Распоряжение   КАК Распоряжение,
		|	Таблица.Номенклатура   КАК Номенклатура,
		|	Таблица.Характеристика КАК Характеристика,
		|	Таблица.Склад          КАК Склад,
		|	Таблица.Серия          КАК Серия,
		|	Таблица.Назначение     КАК Назначение,
		|	Таблица.НазначениеОтправителя КАК НазначениеОтправителя,
		|	Таблица.Подразделение  КАК Подразделение,
		|	Таблица.КодСтроки      КАК КодСтроки,
		|	Таблица.КОформлению    КАК КОформлению
		|ПОМЕСТИТЬ ВТЗаказыМатериаловВПроизводство
		|ИЗ
		|	ДвиженияЗаказыМатериаловВПроизводствоИзменениеМерныеТовары КАК Изменения
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		РегистрНакопления.ЗаказыМатериаловВПроизводство КАК Таблица
		|		ПО Таблица.Распоряжение      = Изменения.Распоряжение
		|			И Таблица.Номенклатура   = Изменения.Номенклатура
		|			И Таблица.Характеристика = Изменения.Характеристика
		|			И Таблица.Склад          = Изменения.Склад
		|			И Таблица.Серия          = Изменения.Серия
		|			И Таблица.Назначение     = Изменения.Назначение
		|			И Таблица.НазначениеОтправителя = Изменения.НазначениеОтправителя
		|			И Таблица.Подразделение  = Изменения.Подразделение
		|			И Таблица.КодСтроки      = Изменения.КодСтроки
		|ГДЕ
		|	НЕ Изменения.Распоряжение ЕСТЬ NULL
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|
		|УНИЧТОЖИТЬ ДвиженияЗаказыМатериаловВПроизводствоИзменениеМерныеТовары
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|
		// Допустимые отклонения
		|ВЫБРАТЬ
		|	Таблица.Распоряжение   КАК Распоряжение,
		|	Таблица.Номенклатура   КАК Номенклатура,
		|	Таблица.Характеристика КАК Характеристика,
		|	Таблица.Склад          КАК Склад,
		|	Таблица.Серия          КАК Серия,
		|	Таблица.Назначение     КАК Назначение,
		|	Таблица.НазначениеОтправителя КАК НазначениеОтправителя,
		|	Таблица.Подразделение  КАК Подразделение,
		|	Таблица.КодСтроки      КАК КодСтроки,
		|	СУММА(Таблица.КОформлению
		|		* (&ДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров / 100)) КАК ДопустимоеОтклонение
		|ПОМЕСТИТЬ ВТДопустимыеОтклоненияЗаказыМатериаловВПроизводство
		|ИЗ
		|	ВТЗаказыМатериаловВПроизводство КАК Таблица
		|ГДЕ
		|	Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|СГРУППИРОВАТЬ ПО
		|	Таблица.Распоряжение,
		|	Таблица.Номенклатура,
		|	Таблица.Характеристика,
		|	Таблица.Склад,
		|	Таблица.Серия,
		|	Таблица.Назначение,
		|	Таблица.НазначениеОтправителя,
		|	Таблица.Подразделение,
		|	Таблица.КодСтроки
		|ИНДЕКСИРОВАТЬ ПО
		|	Распоряжение,
		|	Номенклатура,
		|	Характеристика,
		|	Склад,
		|	Серия,
		|	Назначение,
		|	НазначениеОтправителя,
		|	Подразделение,
		|	КодСтроки
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|
		//Остатки
		|ВЫБРАТЬ
		|	Таблица.Распоряжение       КАК Распоряжение,
		|	Таблица.Номенклатура       КАК Номенклатура,
		|	Таблица.Характеристика     КАК Характеристика,
		|	Таблица.Склад              КАК Склад,
		|	Таблица.Серия              КАК Серия,
		|	Таблица.Назначение         КАК Назначение,
		|	Таблица.НазначениеОтправителя КАК НазначениеОтправителя,
		|	Таблица.Подразделение  КАК Подразделение,
		|	Таблица.КодСтроки          КАК КодСтроки,
		|	СУММА(ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
		|			Таблица.КОформлению
		|		ИНАЧЕ
		|			-Таблица.КОформлению
		|	КОНЕЦ)                     КАК КОформлениюОстаток
		|ПОМЕСТИТЬ ВТЗаказыМатериаловВПроизводствоОстатки
		|ИЗ
		|	ВТЗаказыМатериаловВПроизводство КАК Таблица
		|СГРУППИРОВАТЬ ПО
		|	Таблица.Распоряжение,
		|	Таблица.Номенклатура,
		|	Таблица.Характеристика,
		|	Таблица.Склад,
		|	Таблица.Серия,
		|	Таблица.Назначение,
		|	Таблица.НазначениеОтправителя,
		|	Таблица.Подразделение,
		|	Таблица.КодСтроки
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Таблица.Распоряжение КАК Распоряжение
		|ИЗ
		|	ВТЗаказыМатериаловВПроизводствоОстатки КАК Таблица
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		ВТДопустимыеОтклоненияЗаказыМатериаловВПроизводство КАК ДопустимыеОтклонения
		|		ПО
		|			Таблица.Распоряжение     = ДопустимыеОтклонения.Распоряжение
		|			И Таблица.КодСтроки      = ДопустимыеОтклонения.КодСтроки
		|			И Таблица.Номенклатура   = ДопустимыеОтклонения.Номенклатура
		|			И Таблица.Характеристика = ДопустимыеОтклонения.Характеристика
		|			И Таблица.Склад          = ДопустимыеОтклонения.Склад
		|			И Таблица.Серия          = ДопустимыеОтклонения.Серия
		|			И Таблица.Назначение     = ДопустимыеОтклонения.Назначение
		|			И Таблица.НазначениеОтправителя = ДопустимыеОтклонения.НазначениеОтправителя
		|			И Таблица.Подразделение  = ДопустимыеОтклонения.Подразделение
		|ГДЕ
		|	Таблица.КОформлениюОстаток <= ДопустимыеОтклонения.ДопустимоеОтклонение
		|	И Таблица.КОформлениюОстаток >= -ДопустимыеОтклонения.ДопустимоеОтклонение";
		
		Запрос.Текст = ТекстЗапроса;
		
		ВыборкаЗаказ = Запрос.Выполнить().Выбрать();
		
		Пока ВыборкаЗаказ.Следующий() Цикл
			
			РегистрыСведений.ОчередьЗаказовККорректировкеСтрокМерныхТоваров.ДобавитьЗаказВОчередь(ВыборкаЗаказ.Распоряжение);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
