
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ФизическоеЛицо)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ОбработчикиПравилРегистрации

// См. ЗарплатаКадрыРасширенныйСинхронизацияДанных.ШаблонОбработчика
Процедура ПриЗаполненииНастроекОбработчиковПравилРегистрации(Настройки) Экспорт
	ЗарплатаКадрыРасширенныйСинхронизацияДанных.ДляРегистраПодчиненногоРегистратору(Настройки);
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработчикиОбновления

Процедура ЗаполнитьДополнениеРасчетнойБазыУдержаний(ПараметрыОбновления = Неопределено) Экспорт 
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ВидыНачислений", Обработки.МенеджерРасчетаЗарплаты.ВидыНачисленийДополненияРасчетнойБазыУдержаний());
	
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ДополнениеРасчетнойБазыУдержаний.Регистратор КАК Регистратор
	               |ПОМЕСТИТЬ ВТРегистраторы
	               |ИЗ
	               |	РегистрНакопления.ДополнениеРасчетнойБазыУдержаний КАК ДополнениеРасчетнойБазыУдержаний
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	НачисленияУдержанияПоСотрудникам.Регистратор КАК Регистратор,
	               |	НачисленияУдержанияПоСотрудникам.Период КАК Период,
	               |	НачисленияУдержанияПоСотрудникам.Организация.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	               |	НачисленияУдержанияПоСотрудникам.ФизическоеЛицо КАК ФизическоеЛицо,
	               |	НачисленияУдержанияПоСотрудникам.Сотрудник КАК Сотрудник,
	               |	НачисленияУдержанияПоСотрудникам.НачислениеУдержание КАК Начисление,
	               |	НачисленияУдержанияПоСотрудникам.Организация КАК Организация,
	               |	СУММА(НачисленияУдержанияПоСотрудникам.Сумма) КАК Сумма
	               |ПОМЕСТИТЬ ВТДополнениеРасчетнойБазы
	               |ИЗ
	               |	РегистрНакопления.НачисленияУдержанияПоСотрудникам КАК НачисленияУдержанияПоСотрудникам
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТРегистраторы КАК Регистраторы
	               |		ПО НачисленияУдержанияПоСотрудникам.Регистратор = Регистраторы.Регистратор
	               |ГДЕ
	               |	НачисленияУдержанияПоСотрудникам.НачислениеУдержание В(&ВидыНачислений)
	               |	И Регистраторы.Регистратор ЕСТЬ NULL
	               |	И (ТИПЗНАЧЕНИЯ(НачисленияУдержанияПоСотрудникам.Регистратор) = ТИП(Документ.КомпенсацияЗаЗадержкуЗарплаты)
	               |			ИЛИ ТИПЗНАЧЕНИЯ(НачисленияУдержанияПоСотрудникам.Регистратор) = ТИП(Документ.НачислениеЗарплаты)
	               |			ИЛИ ТИПЗНАЧЕНИЯ(НачисленияУдержанияПоСотрудникам.Регистратор) = ТИП(Документ.НачислениеПоДоговорам)
	               |			ИЛИ ТИПЗНАЧЕНИЯ(НачисленияУдержанияПоСотрудникам.Регистратор) = ТИП(Документ.ПереносДанных))
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	НачисленияУдержанияПоСотрудникам.Регистратор,
	               |	НачисленияУдержанияПоСотрудникам.Период,
	               |	НачисленияУдержанияПоСотрудникам.Организация.ГоловнаяОрганизация,
	               |	НачисленияУдержанияПоСотрудникам.ФизическоеЛицо,
	               |	НачисленияУдержанияПоСотрудникам.Сотрудник,
	               |	НачисленияУдержанияПоСотрудникам.Организация,
	               |	НачисленияУдержанияПоСотрудникам.НачислениеУдержание
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	НачисленияУдержанияПоСотрудникам.Регистратор,
	               |	НачисленияУдержанияПоСотрудникам.Период,
	               |	НачисленияУдержанияПоСотрудникам.Организация.ГоловнаяОрганизация,
	               |	НачисленияУдержанияПоСотрудникам.ФизическоеЛицо,
	               |	НачисленияУдержанияПоСотрудникам.Сотрудник,
	               |	НачисленияУдержанияПоСотрудникам.НачислениеУдержание,
	               |	НачисленияУдержанияПоСотрудникам.Организация,
	               |	СУММА(НачисленияУдержанияПоСотрудникам.Сумма)
	               |ИЗ
	               |	РегистрНакопления.НачисленияУдержанияПоСотрудникам КАК НачисленияУдержанияПоСотрудникам
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПризПодарок КАК ПризПодарок
	               |		ПО НачисленияУдержанияПоСотрудникам.Регистратор = ПризПодарок.Ссылка
	               |			И (НачисленияУдержанияПоСотрудникам.НачислениеУдержание В (&ВидыНачислений))
	               |			И (НЕ ПризПодарок.ИсключаетсяИзРасчетнойБазыИсполнительныхЛистов)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТРегистраторы КАК Регистраторы
	               |		ПО НачисленияУдержанияПоСотрудникам.Регистратор = Регистраторы.Регистратор
	               |ГДЕ
	               |	Регистраторы.Регистратор ЕСТЬ NULL
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	НачисленияУдержанияПоСотрудникам.Регистратор,
	               |	НачисленияУдержанияПоСотрудникам.Период,
	               |	НачисленияУдержанияПоСотрудникам.Организация.ГоловнаяОрганизация,
	               |	НачисленияУдержанияПоСотрудникам.ФизическоеЛицо,
	               |	НачисленияУдержанияПоСотрудникам.Сотрудник,
	               |	НачисленияУдержанияПоСотрудникам.Организация,
	               |	НачисленияУдержанияПоСотрудникам.НачислениеУдержание
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	НачисленияУдержанияПоКонтрагентамАкционерам.Регистратор,
	               |	НачисленияУдержанияПоКонтрагентамАкционерам.Период,
	               |	НачисленияУдержанияПоКонтрагентамАкционерам.Организация.ГоловнаяОрганизация,
	               |	НачисленияУдержанияПоКонтрагентамАкционерам.ФизическоеЛицо,
	               |	НачисленияУдержанияПоКонтрагентамАкционерам.Сотрудник,
	               |	НачисленияУдержанияПоКонтрагентамАкционерам.НачислениеУдержание,
	               |	НачисленияУдержанияПоКонтрагентамАкционерам.Организация,
	               |	СУММА(НачисленияУдержанияПоКонтрагентамАкционерам.Сумма)
	               |ИЗ
	               |	РегистрНакопления.НачисленияУдержанияПоКонтрагентамАкционерам КАК НачисленияУдержанияПоКонтрагентамАкционерам
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТРегистраторы КАК Регистраторы
	               |		ПО НачисленияУдержанияПоКонтрагентамАкционерам.Регистратор = Регистраторы.Регистратор
	               |ГДЕ
	               |	НачисленияУдержанияПоКонтрагентамАкционерам.НачислениеУдержание В(&ВидыНачислений)
	               |	И Регистраторы.Регистратор ЕСТЬ NULL
	               |	И (ТИПЗНАЧЕНИЯ(НачисленияУдержанияПоКонтрагентамАкционерам.Регистратор) = ТИП(Документ.ВыплатаБывшимСотрудникам)
	               |			ИЛИ ТИПЗНАЧЕНИЯ(НачисленияУдержанияПоКонтрагентамАкционерам.Регистратор) = ТИП(Документ.ДивидендыФизическимЛицам)
	               |			ИЛИ ТИПЗНАЧЕНИЯ(НачисленияУдержанияПоКонтрагентамАкционерам.Регистратор) = ТИП(Документ.РегистрацияПрочихДоходов)
	               |			ИЛИ ТИПЗНАЧЕНИЯ(НачисленияУдержанияПоКонтрагентамАкционерам.Регистратор) = ТИП(Документ.ПереносДанных))
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	НачисленияУдержанияПоКонтрагентамАкционерам.Регистратор,
	               |	НачисленияУдержанияПоКонтрагентамАкционерам.Период,
	               |	НачисленияУдержанияПоКонтрагентамАкционерам.Организация.ГоловнаяОрганизация,
	               |	НачисленияУдержанияПоКонтрагентамАкционерам.ФизическоеЛицо,
	               |	НачисленияУдержанияПоКонтрагентамАкционерам.Сотрудник,
	               |	НачисленияУдержанияПоКонтрагентамАкционерам.Организация,
	               |	НачисленияУдержанияПоКонтрагентамАкционерам.НачислениеУдержание
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ ПЕРВЫЕ 1000
	               |	ДополнениеРасчетнойБазы.Регистратор КАК Регистратор,
	               |	ДополнениеРасчетнойБазы.Период КАК Период,
	               |	ДополнениеРасчетнойБазы.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	               |	ДополнениеРасчетнойБазы.ФизическоеЛицо КАК ФизическоеЛицо,
	               |	ДополнениеРасчетнойБазы.Сотрудник КАК Сотрудник,
	               |	ДополнениеРасчетнойБазы.Начисление КАК Начисление,
	               |	ДополнениеРасчетнойБазы.Организация КАК Организация,
	               |	ДополнениеРасчетнойБазы.Сумма КАК Сумма
	               |ИЗ
	               |	ВТДополнениеРасчетнойБазы КАК ДополнениеРасчетнойБазы
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Регистратор";
	
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1000", "");
	КонецЕсли;
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.Дивиденды") Тогда 
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ИЛИ ТИПЗНАЧЕНИЯ(НачисленияУдержанияПоКонтрагентамАкционерам.Регистратор) = ТИП(Документ.ДивидендыФизическимЛицам)", "");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Количество() = 0 Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	
	Пока Выборка.СледующийПоЗначениюПоля("Регистратор") Цикл
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрНакопления.ДополнениеРасчетнойБазыУдержаний.НаборЗаписей", "Регистратор", Выборка.Регистратор) Тогда
			Продолжить;
		КонецЕсли;
		
		НаборЗаписей = РегистрыНакопления.ДополнениеРасчетнойБазыУдержаний.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
		
		Пока Выборка.Следующий() Цикл 
			НоваяЗапись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
		КонецЦикла;
		
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
