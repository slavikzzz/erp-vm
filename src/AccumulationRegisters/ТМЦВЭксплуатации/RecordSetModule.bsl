#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	БлокироватьДляИзменения = Истина;
	
	// Текущее состояние набора помещается во временную таблицу,
	// чтобы при записи получить изменение нового набора относительно текущего.
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЭтоНовый",ДополнительныеСвойства.СвойстваДокумента.ЭтоНовый);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.Период КАК Период,
	|	Таблица.ВидДвижения КАК ВидДвижения,
	|	Таблица.Организация КАК Организация,
	|	Таблица.Подразделение КАК Подразделение,
	|	Таблица.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Таблица.Номенклатура КАК Номенклатура,
	|	Таблица.Характеристика КАК Характеристика,
	|	Таблица.Серия КАК Серия,
	|	Таблица.Партия КАК Партия,
	|	Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	Таблица.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	Таблица.НомерГТД КАК НомерГТД,
	|	Таблица.КоличествоПоРНПТ КАК КоличествоПоРНПТПередЗаписью,
	|	Таблица.Количество КАК КоличествоПередЗаписью
	|
	|ПОМЕСТИТЬ ДвиженияТМЦВЭксплуатацииПередЗаписью
	|
	|ИЗ
	|	РегистрНакопления.ТМЦВЭксплуатации КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|	И НЕ &ЭтоНовый";
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Или Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Таблица.Период КАК Период,
	|	Таблица.Организация КАК Организация,
	|	Таблица.Подразделение КАК Подразделение,
	|	Таблица.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Таблица.Номенклатура КАК Номенклатура,
	|	Таблица.Характеристика КАК Характеристика,
	|	Таблица.Серия КАК Серия,
	|	Таблица.Партия КАК Партия,
	|	Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	Таблица.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	Таблица.НомерГТД КАК НомерГТД,
	|	СУММА(Таблица.КоличествоПоРНПТИзменение) КАК КоличествоПоРНПТИзменение,
	|	СУММА(Таблица.КоличествоИзменение) КАК КоличествоИзменение
	|ПОМЕСТИТЬ ДвиженияТМЦВЭксплуатацииИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(Таблица.Период, ДЕНЬ) КАК Период,
	|		Таблица.Организация КАК Организация,
	|		Таблица.Подразделение КАК Подразделение,
	|		Таблица.ФизическоеЛицо КАК ФизическоеЛицо,
	|		Таблица.Номенклатура КАК Номенклатура,
	|		Таблица.Характеристика КАК Характеристика,
	|		Таблица.Серия КАК Серия,
	|		Таблица.Партия КАК Партия,
	|		Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		Таблица.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|		Таблица.НомерГТД КАК НомерГТД,
	|		ВЫБОР
	|			КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА Таблица.КоличествоПоРНПТПередЗаписью
	|			ИНАЧЕ -Таблица.КоличествоПоРНПТПередЗаписью
	|		КОНЕЦ КАК КоличествоПоРНПТИзменение,
	|		ВЫБОР
	|			КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА Таблица.КоличествоПередЗаписью
	|			ИНАЧЕ -Таблица.КоличествоПередЗаписью
	|		КОНЕЦ КАК КоличествоИзменение
	|	ИЗ
	|		ДвиженияТМЦВЭксплуатацииПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(Таблица.Период, ДЕНЬ),
	|		Таблица.Организация,
	|		Таблица.Подразделение,
	|		Таблица.ФизическоеЛицо,
	|		Таблица.Номенклатура КАК Номенклатура,
	|		Таблица.Характеристика КАК Характеристика,
	|		Таблица.Серия КАК Серия,
	|		Таблица.Партия КАК Партия,
	|		Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		Таблица.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|		Таблица.НомерГТД КАК НомерГТД,
	|		ВЫБОР
	|			КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -Таблица.КоличествоПоРНПТ
	|			ИНАЧЕ Таблица.КоличествоПоРНПТ
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -Таблица.Количество
	|			ИНАЧЕ Таблица.Количество
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.ТМЦВЭксплуатации КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор
	|
	|	) КАК Таблица
	|
	|СГРУППИРОВАТЬ ПО
	|	Таблица.Период,
	|	Таблица.Организация,
	|	Таблица.Подразделение,
	|	Таблица.ФизическоеЛицо,
	|	Таблица.Номенклатура,
	|	Таблица.Характеристика,
	|	Таблица.Серия,
	|	Таблица.Партия,
	|	Таблица.НаправлениеДеятельности,
	|	Таблица.ИнвентарныйНомер,
	|	Таблица.НомерГТД
	|
	|ИМЕЮЩИЕ
	|	(СУММА(Таблица.КоличествоИзменение) <> 0
	|		ИЛИ СУММА(Таблица.КоличествоПоРНПТИзменение) <> 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДвиженияТМЦВЭксплуатацииПередЗаписью";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ДатаНачалаДействияФСБУ5", РеглУчетКлиентСервер.НачалоПримененияФСБУ5_2019());

	Результат = Запрос.ВыполнитьПакет();
	
	Выборка = Результат[0].Выбрать();
	ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
		"ДвиженияТМЦВЭксплуатацииИзменение", Выборка.Следующий() И Выборка.Количество > 0);
		
	ТМЦВЭксплуатацииЛокализация.ПриЗаписиРегистраТМЦВЭксплуатации(
		Отбор.Регистратор.Значение, ДополнительныеСвойства.МенеджерВременныхТаблиц);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли