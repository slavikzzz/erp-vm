#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ПодготовитьДанныеДляФормированияЗаданийПередЗаписью();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПодготовитьДанныеДляФормированияЗаданийПриЗаписи();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПодготовитьДанныеДляФормированияЗаданийПередЗаписью()

	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаПередЗаписью.Активность КАК Активность,
	|	ТаблицаПередЗаписью.Период КАК Период,
	|	ТаблицаПередЗаписью.Организация КАК Организация,
	|	ТаблицаПередЗаписью.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаПередЗаписью.Подразделение КАК Подразделение,
	|	ТаблицаПередЗаписью.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	ТаблицаПередЗаписью.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаПередЗаписью.Арендатор КАК Арендатор,
	|	ТаблицаПередЗаписью.Стоимость КАК Стоимость,
	|	ТаблицаПередЗаписью.СтоимостьРегл КАК СтоимостьРегл,
	|	ТаблицаПередЗаписью.СтоимостьНУ КАК СтоимостьНУ,
	|	ТаблицаПередЗаписью.СтоимостьПР КАК СтоимостьПР,
	|	ТаблицаПередЗаписью.СтоимостьВР КАК СтоимостьВР,
	|	ТаблицаПередЗаписью.СтоимостьЦФ КАК СтоимостьЦФ,
	|	ТаблицаПередЗаписью.СтоимостьНУЦФ КАК СтоимостьНУЦФ,
	|	ТаблицаПередЗаписью.СтоимостьПРЦФ КАК СтоимостьПРЦФ,
	|	ТаблицаПередЗаписью.СтоимостьВРЦФ КАК СтоимостьВРЦФ,
	|	ТаблицаПередЗаписью.НеУчитываемаяСтоимостьНУ КАК НеУчитываемаяСтоимостьНУ,
	|	ТаблицаПередЗаписью.АмортизационнаяПремия КАК АмортизационнаяПремия,
	|	ТаблицаПередЗаписью.РезервПереоценкиСтоимости КАК РезервПереоценкиСтоимости,
	|	ТаблицаПередЗаписью.РезервПереоценкиСтоимостиРегл КАК РезервПереоценкиСтоимостиРегл,
	|	ТаблицаПередЗаписью.ПриростСтоимости КАК ПриростСтоимости,
	|	ТаблицаПередЗаписью.ПриростСтоимостиРегл КАК ПриростСтоимостиРегл,
	|	ТаблицаПередЗаписью.ЗалоговаяСтоимость КАК ЗалоговаяСтоимость,
	|	ТаблицаПередЗаписью.ПредварительнаяСтоимость КАК ПредварительнаяСтоимость,
	|	ТаблицаПередЗаписью.ПредварительнаяСтоимостьРегл КАК ПредварительнаяСтоимостьРегл,
	|	ТаблицаПередЗаписью.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ТаблицаПередЗаписью.ВидДвижения КАК ВидДвижения,
	|	ТаблицаПередЗаписью.АналитикаКапитализацииРасходов КАК АналитикаКапитализацииРасходов,
	|	ТаблицаПередЗаписью.ВидАналитикиКапитализацииРасходов КАК ВидАналитикиКапитализацииРасходов,
	|	ТаблицаПередЗаписью.ВариантПримененияЦелевогоФинансирования КАК ВариантПримененияЦелевогоФинансирования,
	|	ТаблицаПередЗаписью.ОтражатьВРеглУчете КАК ОтражатьВРеглУчете,
	|	ТаблицаПередЗаписью.ОтражатьВУпрУчете КАК ОтражатьВУпрУчете
	|
	|ПОМЕСТИТЬ СтоимостьОСПередЗаписью
	|
	|ИЗ
	|	РегистрНакопления.СтоимостьОС КАК ТаблицаПередЗаписью
	|
	|ГДЕ
	|	ТаблицаПередЗаписью.Регистратор = &Регистратор";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПодготовитьДанныеДляФормированияЗаданийПриЗаписи()
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	МассивТекстовЗапроса = Новый Массив;
	
	ТекстЗапросаИзменения = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаОтличий.Период КАК Период,
	|	ТаблицаОтличий.Организация КАК Организация,
	|	ТаблицаОтличий.ОсновноеСредство КАК ОсновноеСредство,
	|	МАКСИМУМ(ТаблицаОтличий.ОтражатьВРеглУчете) КАК ОтражатьВРеглУчете,
	|	МАКСИМУМ(ТаблицаОтличий.ОтражатьВУпрУчете) КАК ОтражатьВУпрУчете,
	|	&Регистратор КАК Документ
	|
	|ПОМЕСТИТЬ СтоимостьОСИзменение
	|
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаПередЗаписью.Активность КАК Активность,
	|		ТаблицаПередЗаписью.Период КАК Период,
	|
	|		ТаблицаПередЗаписью.Организация КАК Организация,
	|		ТаблицаПередЗаписью.ОсновноеСредство КАК ОсновноеСредство,
	|		ТаблицаПередЗаписью.Подразделение КАК Подразделение,
	|		ТаблицаПередЗаписью.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|		ТаблицаПередЗаписью.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ТаблицаПередЗаписью.Арендатор КАК Арендатор,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПередЗаписью.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ТаблицаПередЗаписью.Стоимость
	|			ИНАЧЕ -ТаблицаПередЗаписью.Стоимость
	|		КОНЕЦ КАК Стоимость,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПередЗаписью.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ТаблицаПередЗаписью.СтоимостьРегл
	|			ИНАЧЕ -ТаблицаПередЗаписью.СтоимостьРегл
	|		КОНЕЦ КАК СтоимостьРегл,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПередЗаписью.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ТаблицаПередЗаписью.СтоимостьНУ
	|			ИНАЧЕ -ТаблицаПередЗаписью.СтоимостьНУ
	|		КОНЕЦ КАК СтоимостьНУ,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПередЗаписью.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ТаблицаПередЗаписью.СтоимостьПР
	|			ИНАЧЕ -ТаблицаПередЗаписью.СтоимостьПР
	|		КОНЕЦ КАК СтоимостьПР,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПередЗаписью.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ТаблицаПередЗаписью.СтоимостьВР
	|			ИНАЧЕ -ТаблицаПередЗаписью.СтоимостьВР
	|		КОНЕЦ КАК СтоимостьВР,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПередЗаписью.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ТаблицаПередЗаписью.СтоимостьЦФ
	|			ИНАЧЕ -ТаблицаПередЗаписью.СтоимостьЦФ
	|		КОНЕЦ КАК СтоимостьЦФ,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПередЗаписью.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ТаблицаПередЗаписью.СтоимостьНУЦФ
	|			ИНАЧЕ -ТаблицаПередЗаписью.СтоимостьНУЦФ
	|		КОНЕЦ КАК СтоимостьНУЦФ,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПередЗаписью.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ТаблицаПередЗаписью.СтоимостьПРЦФ
	|			ИНАЧЕ -ТаблицаПередЗаписью.СтоимостьПРЦФ
	|		КОНЕЦ КАК СтоимостьПРЦФ,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПередЗаписью.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ТаблицаПередЗаписью.СтоимостьВРЦФ
	|			ИНАЧЕ -ТаблицаПередЗаписью.СтоимостьВРЦФ
	|		КОНЕЦ КАК СтоимостьВРЦФ,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПередЗаписью.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ТаблицаПередЗаписью.НеУчитываемаяСтоимостьНУ
	|			ИНАЧЕ -ТаблицаПередЗаписью.НеУчитываемаяСтоимостьНУ
	|		КОНЕЦ КАК НеУчитываемаяСтоимостьНУ,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПередЗаписью.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ТаблицаПередЗаписью.АмортизационнаяПремия
	|			ИНАЧЕ -ТаблицаПередЗаписью.АмортизационнаяПремия
	|		КОНЕЦ КАК АмортизационнаяПремия,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПередЗаписью.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ТаблицаПередЗаписью.ПредварительнаяСтоимость
	|			ИНАЧЕ -ТаблицаПередЗаписью.ПредварительнаяСтоимость
	|		КОНЕЦ КАК ПредварительнаяСтоимость,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПередЗаписью.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ТаблицаПередЗаписью.ПредварительнаяСтоимостьРегл
	|			ИНАЧЕ -ТаблицаПередЗаписью.ПредварительнаяСтоимостьРегл
	|		КОНЕЦ КАК ПредварительнаяСтоимостьРегл,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПередЗаписью.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ТаблицаПередЗаписью.РезервПереоценкиСтоимости
	|			ИНАЧЕ -ТаблицаПередЗаписью.РезервПереоценкиСтоимости
	|		КОНЕЦ КАК РезервПереоценкиСтоимости,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПередЗаписью.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ТаблицаПередЗаписью.РезервПереоценкиСтоимостиРегл
	|			ИНАЧЕ -ТаблицаПередЗаписью.РезервПереоценкиСтоимостиРегл
	|		КОНЕЦ КАК РезервПереоценкиСтоимостиРегл,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПередЗаписью.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ТаблицаПередЗаписью.ПриростСтоимости
	|			ИНАЧЕ -ТаблицаПередЗаписью.ПриростСтоимости
	|		КОНЕЦ КАК ПриростСтоимости,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПередЗаписью.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ТаблицаПередЗаписью.ПриростСтоимостиРегл
	|			ИНАЧЕ -ТаблицаПередЗаписью.ПриростСтоимостиРегл
	|		КОНЕЦ КАК ПриростСтоимостиРегл,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПередЗаписью.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ТаблицаПередЗаписью.ЗалоговаяСтоимость
	|			ИНАЧЕ -ТаблицаПередЗаписью.ЗалоговаяСтоимость
	|		КОНЕЦ КАК ЗалоговаяСтоимость,
	|
	|		ТаблицаПередЗаписью.ОтражатьВРеглУчете  КАК ОтражатьВРеглУчете,
	|		ТаблицаПередЗаписью.ОтражатьВУпрУчете   КАК ОтражатьВУпрУчете
	|	ИЗ
	|		СтоимостьОСПередЗаписью КАК ТаблицаПередЗаписью
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ТаблицаПриЗаписи.Активность КАК Активность,
	|		ТаблицаПриЗаписи.Период КАК Период,
	|
	|		ТаблицаПриЗаписи.Организация КАК Организация,
	|		ТаблицаПриЗаписи.ОсновноеСредство КАК ОсновноеСредство,
	|		ТаблицаПриЗаписи.Подразделение КАК Подразделение,
	|		ТаблицаПриЗаписи.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|		ТаблицаПриЗаписи.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ТаблицаПриЗаписи.Арендатор КАК Арендатор,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ТаблицаПриЗаписи.Стоимость
	|			ИНАЧЕ ТаблицаПриЗаписи.Стоимость
	|		КОНЕЦ КАК Стоимость,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ТаблицаПриЗаписи.СтоимостьРегл
	|			ИНАЧЕ ТаблицаПриЗаписи.СтоимостьРегл
	|		КОНЕЦ КАК СтоимостьРегл,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ТаблицаПриЗаписи.СтоимостьНУ
	|			ИНАЧЕ ТаблицаПриЗаписи.СтоимостьНУ
	|		КОНЕЦ КАК СтоимостьНУ,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ТаблицаПриЗаписи.СтоимостьПР
	|			ИНАЧЕ ТаблицаПриЗаписи.СтоимостьПР
	|		КОНЕЦ КАК СтоимостьПР,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ТаблицаПриЗаписи.СтоимостьВР
	|			ИНАЧЕ ТаблицаПриЗаписи.СтоимостьВР
	|		КОНЕЦ КАК СтоимостьВР,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ТаблицаПриЗаписи.СтоимостьЦФ
	|			ИНАЧЕ ТаблицаПриЗаписи.СтоимостьЦФ
	|		КОНЕЦ КАК СтоимостьЦФ,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ТаблицаПриЗаписи.СтоимостьНУЦФ
	|			ИНАЧЕ ТаблицаПриЗаписи.СтоимостьНУЦФ
	|		КОНЕЦ КАК СтоимостьНУЦФ,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ТаблицаПриЗаписи.СтоимостьПРЦФ
	|			ИНАЧЕ ТаблицаПриЗаписи.СтоимостьПРЦФ
	|		КОНЕЦ КАК СтоимостьПРЦФ,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ТаблицаПриЗаписи.СтоимостьВРЦФ
	|			ИНАЧЕ ТаблицаПриЗаписи.СтоимостьВРЦФ
	|		КОНЕЦ КАК СтоимостьВРЦФ,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ТаблицаПриЗаписи.НеУчитываемаяСтоимостьНУ
	|			ИНАЧЕ ТаблицаПриЗаписи.НеУчитываемаяСтоимостьНУ
	|		КОНЕЦ КАК НеУчитываемаяСтоимостьНУ,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ТаблицаПриЗаписи.АмортизационнаяПремия
	|			ИНАЧЕ ТаблицаПриЗаписи.АмортизационнаяПремия
	|		КОНЕЦ КАК АмортизационнаяПремия,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ТаблицаПриЗаписи.ПредварительнаяСтоимость
	|			ИНАЧЕ ТаблицаПриЗаписи.ПредварительнаяСтоимость
	|		КОНЕЦ КАК ПредварительнаяСтоимость,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ТаблицаПриЗаписи.ПредварительнаяСтоимостьРегл
	|			ИНАЧЕ ТаблицаПриЗаписи.ПредварительнаяСтоимостьРегл
	|		КОНЕЦ КАК ПредварительнаяСтоимостьРегл,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ТаблицаПриЗаписи.РезервПереоценкиСтоимости
	|			ИНАЧЕ ТаблицаПриЗаписи.РезервПереоценкиСтоимости
	|		КОНЕЦ КАК РезервПереоценкиСтоимости,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ТаблицаПриЗаписи.РезервПереоценкиСтоимостиРегл
	|			ИНАЧЕ ТаблицаПриЗаписи.РезервПереоценкиСтоимостиРегл
	|		КОНЕЦ КАК РезервПереоценкиСтоимостиРегл,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ТаблицаПриЗаписи.ПриростСтоимости
	|			ИНАЧЕ ТаблицаПриЗаписи.ПриростСтоимости
	|		КОНЕЦ КАК ПриростСтоимости,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ТаблицаПриЗаписи.ПриростСтоимостиРегл
	|			ИНАЧЕ ТаблицаПриЗаписи.ПриростСтоимостиРегл
	|		КОНЕЦ КАК ПриростСтоимостиРегл,
	|
	|		ВЫБОР
	|			КОГДА ТаблицаПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ТаблицаПриЗаписи.ЗалоговаяСтоимость
	|			ИНАЧЕ ТаблицаПриЗаписи.ЗалоговаяСтоимость
	|		КОНЕЦ КАК ЗалоговаяСтоимость,
	|
	|		ТаблицаПриЗаписи.ОтражатьВРеглУчете  КАК ОтражатьВРеглУчете,
	|		ТаблицаПриЗаписи.ОтражатьВУпрУчете   КАК ОтражатьВУпрУчете
	|	ИЗ
	|		РегистрНакопления.СтоимостьОС КАК ТаблицаПриЗаписи
	|	ГДЕ
	|		ТаблицаПриЗаписи.Регистратор = &Регистратор
	|
	|	) КАК ТаблицаОтличий
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаОтличий.Активность,
	|	ТаблицаОтличий.Период,
	|	ТаблицаОтличий.Организация,
	|	ТаблицаОтличий.ОсновноеСредство,
	|	ТаблицаОтличий.Подразделение,
	|	ТаблицаОтличий.ГруппаФинансовогоУчета,
	|	ТаблицаОтличий.НаправлениеДеятельности,
	|	ТаблицаОтличий.Арендатор
	|
	|ИМЕЮЩИЕ
	|	(СУММА(ТаблицаОтличий.Стоимость) <> 0
	|		ИЛИ СУММА(ТаблицаОтличий.СтоимостьРегл) <> 0
	|		ИЛИ СУММА(ТаблицаОтличий.СтоимостьНУ) <> 0
	|		ИЛИ СУММА(ТаблицаОтличий.СтоимостьПР) <> 0
	|		ИЛИ СУММА(ТаблицаОтличий.СтоимостьВР) <> 0
	|		ИЛИ СУММА(ТаблицаОтличий.СтоимостьЦФ) <> 0
	|		ИЛИ СУММА(ТаблицаОтличий.СтоимостьНУЦФ) <> 0
	|		ИЛИ СУММА(ТаблицаОтличий.СтоимостьПРЦФ) <> 0
	|		ИЛИ СУММА(ТаблицаОтличий.СтоимостьВРЦФ) <> 0
	|		ИЛИ СУММА(ТаблицаОтличий.НеУчитываемаяСтоимостьНУ) <> 0
	|		ИЛИ СУММА(ТаблицаОтличий.АмортизационнаяПремия) <> 0
	|		ИЛИ СУММА(ТаблицаОтличий.ПредварительнаяСтоимость) <> 0
	|		ИЛИ СУММА(ТаблицаОтличий.ПредварительнаяСтоимостьРегл) <> 0
	|		ИЛИ СУММА(ТаблицаОтличий.РезервПереоценкиСтоимости) <> 0
	|		ИЛИ СУММА(ТаблицаОтличий.РезервПереоценкиСтоимостиРегл) <> 0
	|		ИЛИ СУММА(ТаблицаОтличий.ПриростСтоимости) <> 0
	|		ИЛИ СУММА(ТаблицаОтличий.ПриростСтоимостиРегл) <> 0
	|		ИЛИ СУММА(ТаблицаОтличий.ЗалоговаяСтоимость) <> 0)";
	МассивТекстовЗапроса.Добавить(ТекстЗапросаИзменения);
	
	ТекстЗапросаЗаданияКРасчетуСтоимости =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Период           КАК Период,
	|	Таблица.Организация      КАК Организация,
	|	Таблица.ОбъектУчета      КАК ОбъектУчета,
	|	&Регистратор             КАК Документ
	|ПОМЕСТИТЬ СтоимостьОС_ЗаданияКРасчетуСтоимостиВНАИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаПередЗаписью.Период            КАК Период,
	|		ТаблицаПередЗаписью.Организация       КАК Организация,
	|		ТаблицаПередЗаписью.ОсновноеСредство  КАК ОбъектУчета
	|	ИЗ
	|		СтоимостьОСПередЗаписью КАК ТаблицаПередЗаписью
	|	ГДЕ
	|		ТаблицаПередЗаписью.ХозяйственнаяОперация В (
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОС), 
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуУзловКомпонентовАмортизации),
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуПредметовАренды),
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.МодернизацияОС),
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазукомплектацияОСЧастичная),
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеНДС))
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаПриЗаписи.Период,
	|		ТаблицаПриЗаписи.Организация,
	|		ТаблицаПриЗаписи.ОсновноеСредство
	|	ИЗ
	|		РегистрНакопления.СтоимостьОС КАК ТаблицаПриЗаписи
	|	ГДЕ
	|		ТаблицаПриЗаписи.Регистратор = &Регистратор
	|		И ТаблицаПриЗаписи.ХозяйственнаяОперация В (
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОС), 
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуУзловКомпонентовАмортизации),
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуПредметовАренды),
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.МодернизацияОС),
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазукомплектацияОСЧастичная),
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеНДС))
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаПередЗаписью.Период            КАК Период,
	|		ТаблицаПередЗаписью.Организация       КАК Организация,
	|		ТаблицаПередЗаписью.АналитикаКапитализацииРасходов КАК ОбъектУчета
	|	ИЗ
	|		СтоимостьОСПередЗаписью КАК ТаблицаПередЗаписью
	|	ГДЕ
	|		ТаблицаПередЗаписью.ХозяйственнаяОперация В (
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОС), 
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуУзловКомпонентовАмортизации),
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуПредметовАренды),
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.МодернизацияОС),
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазукомплектацияОСЧастичная),
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеНДС))
	|
	|		И ТаблицаПередЗаписью.АналитикаКапитализацииРасходов <> ТаблицаПередЗаписью.ОсновноеСредство
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаПриЗаписи.Период,
	|		ТаблицаПриЗаписи.Организация,
	|		ТаблицаПриЗаписи.АналитикаКапитализацииРасходов
	|	ИЗ
	|		РегистрНакопления.СтоимостьОС КАК ТаблицаПриЗаписи
	|	ГДЕ
	|		ТаблицаПриЗаписи.Регистратор = &Регистратор
	|		И ТаблицаПриЗаписи.ХозяйственнаяОперация В (
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОС), 
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуУзловКомпонентовАмортизации),
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуПредметовАренды),
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.МодернизацияОС),
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазукомплектацияОСЧастичная),
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеНДС))
	|
	|		И ТаблицаПриЗаписи.АналитикаКапитализацииРасходов <> ТаблицаПриЗаписи.ОсновноеСредство
	|
	|	) КАК Таблица
	|";
	МассивТекстовЗапроса.Добавить(ТекстЗапросаЗаданияКРасчетуСтоимости);
	МассивТекстовЗапроса.Добавить("УНИЧТОЖИТЬ СтоимостьОСПередЗаписью");
	
	ТекстЗапроса = СтрСоединить(МассивТекстовЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Результат = Запрос.ВыполнитьПакет();
	
	Выборка = Результат[0].Выбрать();
	ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
		"СтоимостьОСИзменение", Выборка.Следующий() И Выборка.Количество > 0);
	
	Выборка = Результат[1].Выбрать();
	ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
		"СтоимостьОС_ЗаданияКРасчетуСтоимостиВНАИзменение", Выборка.Следующий() И Выборка.Количество > 0);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
