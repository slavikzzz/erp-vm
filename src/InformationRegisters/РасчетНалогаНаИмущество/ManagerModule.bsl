#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	ДобавитьОбработчикОбновленияДляПереходаНаВерсиюАлгоритма25(Обработчики);
КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	ЗарегистрироватьДанныеДляПереходаНаВерсиюАлгоритма25(Параметры);
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	ПереходНаВерсиюАлгоритма25(Параметры);
КонецПроцедуры 

#Область ПереходНаВерсиюАлгоритма25
Процедура ДобавитьОбработчикОбновленияДляПереходаНаВерсиюАлгоритма25(Обработчики)
	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.РасчетНалогаНаИмущество.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.5.13.33";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("167d51bb-0af6-439a-a48d-d88bdbf9d4bd");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.РасчетНалогаНаИмущество.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";

	ТекстКомментария = НСтр("ru = 'Обновляет регистр ""Расчет налога на имущество"":
	|- с %1 данные по расчету налога на имущество переносятся в 2 новых регистра
	|  ""Расчет налога на имущество по кадастровой стоимости"" и
	|  ""Расчет налога на имущество по среднегодовой стоимости""';
	|en = 'Updates the ""Property tax calculation"" register:
	|- the property tax calculation data is transferred from %1 to the 2 new registers
	| - ""Property tax calculation by cadastral value"" and
	| ""Property tax calculation by average annual value""'");

	ТекстКомментария = СтрШаблон(ТекстКомментария,
						Формат(РасчетИмущественныхНалогов.НачалоПримененияНовогоРасчетаНалогаНаИмущество(), "ДЛФ=D;"));

	Обработчик.Комментарий = ТекстКомментария;

	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Документы.РегламентнаяОперация.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.КорректировкаРегистров.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");

	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыСведений.РасчетНалогаНаИмущество.ПолноеИмя());
	Изменяемые.Добавить(Метаданные.РегистрыСведений.РасчетНалогаНаИмуществоПоКадастровойСтоимости.ПолноеИмя());
	Изменяемые.Добавить(Метаданные.РегистрыСведений.РасчетНалогаНаИмуществоПоСреднегодовойСтоимости.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");

	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.РегистрыСведений.РасчетНалогаНаИмущество.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
КонецПроцедуры

Процедура ЗарегистрироватьДанныеДляПереходаНаВерсиюАлгоритма25(Параметры)
	ПолноеИмяОбъекта = "РегистрСведений.РасчетНалогаНаИмущество";

	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаРегистров = ПолноеИмяОбъекта;
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Период");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Период");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиРегистраторыРегистра();

	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоДвижения = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = ПолноеИмяОбъекта;
	
	// Требуется разделить старый регистр на 2 новых, начиная с даты, указанной в 
	// РасчетИмущественныхНалогов.НачалоПримененияНовогоРасчетаНалогаНаИмущество().
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор
	|ИЗ
	|	РегистрСведений.РасчетНалогаНаИмущество КАК ДанныеРегистра
	|ГДЕ
	|	ДанныеРегистра.ПериодРасчета >= &ДатаПереходаНаВерсиюАлгоритма25";

	Запрос = Новый Запрос(ТекстЗапроса);
	
	Запрос.УстановитьПараметр("ДатаПереходаНаВерсиюАлгоритма25",
								РасчетИмущественныхНалогов.НачалоПримененияНовогоРасчетаНалогаНаИмущество());

	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(
		Параметры, Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор"), ДополнительныеПараметры);
		
КонецПроцедуры

Процедура ПереходНаВерсиюАлгоритма25(Параметры)
	Параметры.ОбработкаЗавершена = Ложь; 
	
	ДанныеДляОбработки = Параметры.ОбновляемыеДанные.НаборДанных;
	Для Каждого НаборДанных Из ДанныеДляОбработки Цикл
		
		СписокРегистраторов = НаборДанных.Данные.ВыгрузитьКолонку("Регистратор");

		НачатьТранзакцию();
		
		Попытка
			
			// По каждому регистратору перенесем данные из регистра РасчетНалогаНаИмущество
			// в регистры РасчетНалогаНаИмуществоПоКадастровойСтоимости и
			// РасчетНалогаНаИмуществоПоСреднегодовойСтоимости.
			// Затем, по этим регистраторам, очистим движения в регистре РасчетНалогаНаИмущество.

			РегистраторыДляОчистки = Новый Массив;

			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("СписокРегистраторов", СписокРегистраторов);

#Область КадастроваяСтоимость

			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ПараметрыНачисленияНалогаНаИмущество.Организация КАК Организация,
			|	ПараметрыНачисленияНалогаНаИмущество.ОсновноеСредство КАК ОсновноеСредство,
			|	ПараметрыНачисленияНалогаНаИмущество.Период КАК ДатаСведений,
			|	ПараметрыНачисленияНалогаНаИмущество.НалоговаяСтавка КАК НалоговаяСтавка,
			|	ПараметрыНачисленияНалогаНаИмущество.КодВидаИмущества КАК КодВидаИмущества,
			|	ПараметрыНачисленияНалогаНаИмущество.ДатаПрекращенияПраваСобственности КАК ДатаПрекращенияПраваСобственности
			|ПОМЕСТИТЬ втПараметрыНачисленияННИ
			|ИЗ
			|	РегистрСведений.ПараметрыНачисленияНалогаНаИмущество КАК ПараметрыНачисленияНалогаНаИмущество
			|ГДЕ
			|	(ПараметрыНачисленияНалогаНаИмущество.Организация, ПараметрыНачисленияНалогаНаИмущество.ОсновноеСредство) В
			|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
			|				РасчетНалогаНаИмущество.Организация КАК Организация,
			|				РасчетНалогаНаИмущество.ОсновноеСредство КАК ОсновноеСредство
			|			ИЗ
			|				РегистрСведений.РасчетНалогаНаИмущество КАК РасчетНалогаНаИмущество
			|			ГДЕ
			|				РасчетНалогаНаИмущество.Регистратор В (&СписокРегистраторов)
			|				И РасчетНалогаНаИмущество.ВидНалоговойБазы = 
			|						ЗНАЧЕНИЕ(Перечисление.НалоговаяБазаПоНалогуНаИмущество.КадастроваяСтоимость))
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	втПараметрыНачисленияННИ.Организация КАК Организация,
			|	втПараметрыНачисленияННИ.ОсновноеСредство КАК ОсновноеСредство,
			|	МИНИМУМ(втПараметрыНачисленияННИ.ДатаСведений) КАК ДатаСведений,
			|	втПараметрыНачисленияННИ.НалоговаяСтавка КАК НалоговаяСтавка,
			|	втПараметрыНачисленияННИ.КодВидаИмущества КАК КодВидаИмущества
			|ПОМЕСТИТЬ втМинДатаСведений
			|ИЗ
			|	втПараметрыНачисленияННИ КАК втПараметрыНачисленияННИ
			|
			|СГРУППИРОВАТЬ ПО
			|	втПараметрыНачисленияННИ.Организация,
			|	втПараметрыНачисленияННИ.ОсновноеСредство,
			|	втПараметрыНачисленияННИ.НалоговаяСтавка,
			|	втПараметрыНачисленияННИ.КодВидаИмущества
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	втМинДатаСведений.Организация КАК Организация,
			|	втМинДатаСведений.ОсновноеСредство КАК ОсновноеСредство,
			|	втМинДатаСведений.ДатаСведений КАК ДатаСведений,
			|	втМинДатаСведений.НалоговаяСтавка КАК НалоговаяСтавка,
			|	втМинДатаСведений.КодВидаИмущества КАК КодВидаИмущества,
			|	МАКСИМУМ(втПараметрыНачисленияННИ.ДатаПрекращенияПраваСобственности) КАК ДатаПрекращенияПраваСобственности
			|ПОМЕСТИТЬ втДатыПрекращенияПраваСобственности
			|ИЗ
			|	втМинДатаСведений КАК втМинДатаСведений
			|		ЛЕВОЕ СОЕДИНЕНИЕ втПараметрыНачисленияННИ КАК втПараметрыНачисленияННИ
			|		ПО втМинДатаСведений.Организация = втПараметрыНачисленияННИ.Организация
			|			И втМинДатаСведений.ОсновноеСредство = втПараметрыНачисленияННИ.ОсновноеСредство
			|			И втМинДатаСведений.НалоговаяСтавка = втПараметрыНачисленияННИ.НалоговаяСтавка
			|			И втМинДатаСведений.КодВидаИмущества = втПараметрыНачисленияННИ.КодВидаИмущества
			|
			|СГРУППИРОВАТЬ ПО
			|	втМинДатаСведений.Организация,
			|	втМинДатаСведений.ОсновноеСредство,
			|	втМинДатаСведений.ДатаСведений,
			|	втМинДатаСведений.НалоговаяСтавка,
			|	втМинДатаСведений.КодВидаИмущества
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	РасчетНалогаНаИмущество.Регистратор                КАК Регистратор,
			|	РасчетНалогаНаИмущество.Активность                 КАК Активность,
			|	РасчетНалогаНаИмущество.ПериодРасчета              КАК ПериодРасчета,
			|	РасчетНалогаНаИмущество.Организация                КАК Организация,
			|	РасчетНалогаНаИмущество.ИФНС                       КАК ИФНС,
			|	РасчетНалогаНаИмущество.КодПоОКТМО                 КАК КодПоОКТМО,
			|	РасчетНалогаНаИмущество.КодВидаИмущества           КАК КодВидаИмущества,
			|	РасчетНалогаНаИмущество.ОсновноеСредство           КАК ОсновноеСредство,
			|	РасчетНалогаНаИмущество.НалоговаяСтавка            КАК НалоговаяСтавка,
			|	РасчетНалогаНаИмущество.ДатаСведений               КАК ДатаСведений,
			|	РасчетНалогаНаИмущество.СуммаНалога                КАК СуммаНалога,
			|	РасчетНалогаНаИмущество.СуммаНалогаКУплате         КАК СуммаНалогаКУплате,
			|	РасчетНалогаНаИмущество.СуммаАвансовыхПлатежей     КАК СуммаАвансовыхПлатежей,
			|	РасчетНалогаНаИмущество.СуммаУменьшенияСуммыНалога КАК СуммаУменьшенияСуммыНалога,
			|
			|	РасчетНалогаНаИмущество.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеОбщейСобственностиЗнаменатель,
			|	РасчетНалогаНаИмущество.ДоляВПравеОбщейСобственностиЧислитель   КАК ДоляВПравеОбщейСобственностиЧислитель,
			|
			|	РасчетНалогаНаИмущество.ДоляПлощадиЗнаменатель     КАК ДоляПлощадиЗнаменатель,
			|	РасчетНалогаНаИмущество.ДоляПлощадиЧислитель       КАК ДоляПлощадиЧислитель,
			|	РасчетНалогаНаИмущество.ДоляСтоимостиЗнаменатель   КАК ДоляСтоимостиЗнаменатель,
			|	РасчетНалогаНаИмущество.ДоляСтоимостиЧислитель     КАК ДоляСтоимостиЧислитель,
			|	РасчетНалогаНаИмущество.КадастроваяСтоимость       КАК КадастроваяСтоимость,
			|	РасчетНалогаНаИмущество.КадастровыйНомер           КАК КадастровыйНомер,
			|	РасчетНалогаНаИмущество.КадастровыйНомерПомещения  КАК КадастровыйНомерПомещения,
			|	РасчетНалогаНаИмущество.КоличествоМесяцевВладения  КАК КоличествоМесяцевВладения,
			|
			|	РасчетНалогаНаИмущество.КоличествоМесяцевИспользования   КАК КоличествоМесяцевИспользования,
			|	РасчетНалогаНаИмущество.НалоговаяБаза                    КАК НалоговаяБаза,
			|	РасчетНалогаНаИмущество.НеоблагаемаяКадастроваяСтоимость КАК НеоблагаемаяКадастроваяСтоимость,
			|	РасчетНалогаНаИмущество.ОснованиеЛьготы                  КАК ОснованиеЛьготы,
			|
			|	втДатыПрекращенияПраваСобственности.ДатаПрекращенияПраваСобственности КАК ДатаПрекращенияПраваСобственности,
			|
			|	РасчетНалогаНаИмущество.СуммаНалога - 
			|            РасчетНалогаНаИмущество.СуммаУменьшенияСуммыНалога КАК СуммаНалогаБезЛьгот,
			|
			|	РасчетНалогаНаИмущество.СуммаУменьшенияСуммыНалога КАК СуммаЛьгот,
			|	РасчетНалогаНаИмущество.СуммаНалога                КАК СуммаНалогаСУчетомНеоблагаемойБазы
			|
			|ИЗ
			|	РегистрСведений.РасчетНалогаНаИмущество КАК РасчетНалогаНаИмущество
			|
			|		ЛЕВОЕ СОЕДИНЕНИЕ втДатыПрекращенияПраваСобственности КАК втДатыПрекращенияПраваСобственности
			|		ПО РасчетНалогаНаИмущество.Организация = втДатыПрекращенияПраваСобственности.Организация
			|			И РасчетНалогаНаИмущество.ОсновноеСредство = втДатыПрекращенияПраваСобственности.ОсновноеСредство
			|			И РасчетНалогаНаИмущество.ДатаСведений = втДатыПрекращенияПраваСобственности.ДатаСведений
			|			И РасчетНалогаНаИмущество.НалоговаяСтавка = втДатыПрекращенияПраваСобственности.НалоговаяСтавка
			|			И РасчетНалогаНаИмущество.КодВидаИмущества = втДатыПрекращенияПраваСобственности.КодВидаИмущества
			|
			|ГДЕ
			|	РасчетНалогаНаИмущество.Регистратор В(&СписокРегистраторов)
			|	И РасчетНалогаНаИмущество.ВидНалоговойБазы = 
			|					ЗНАЧЕНИЕ(Перечисление.НалоговаяБазаПоНалогуНаИмущество.КадастроваяСтоимость)
			|
			|УПОРЯДОЧИТЬ ПО
			|	Организация,
			|	ПериодРасчета,
			|	Регистратор
			|
			|ИТОГИ ПО
			|	Регистратор";
			Результат = Запрос.Выполнить();

			Если Не Результат.Пустой() Тогда

				ВыборкаРегистратор = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

				Пока ВыборкаРегистратор.Следующий() Цикл

					НаборЗаписейКадастроваяСтоимость = 
								РегистрыСведений.РасчетНалогаНаИмуществоПоКадастровойСтоимости.СоздатьНаборЗаписей();
								
					НаборЗаписейКадастроваяСтоимость.Отбор.Регистратор.Значение = ВыборкаРегистратор.Регистратор;

					ВыборкаСтроки = ВыборкаРегистратор.Выбрать(ОбходРезультатаЗапроса.Прямой);
					Пока ВыборкаСтроки.Следующий() Цикл
						Стр = НаборЗаписейКадастроваяСтоимость.Добавить();
						ЗаполнитьЗначенияСвойств(Стр, ВыборкаСтроки);

					КонецЦикла;
					НаборЗаписейКадастроваяСтоимость.Записать();
					
					// Включим регистратор в список для очистки записей по старому регистру.
					Если РегистраторыДляОчистки.Найти(ВыборкаРегистратор.Регистратор) = Неопределено Тогда
						РегистраторыДляОчистки.Добавить(ВыборкаРегистратор.Регистратор);
					КонецЕсли;

				КонецЦикла;

			КонецЕсли;

#КонецОбласти

#Область СреднегодоваяСтоимость
			// Вероятные дубли строк разделим путем генерации даты сведений.
			Запрос.Текст =
			"ВЫБРАТЬ
			|	РасчетНалогаНаИмущество.Регистратор КАК Регистратор,
			|	РасчетНалогаНаИмущество.НомерСтроки КАК НомерСтроки,
			|	РасчетНалогаНаИмущество.Активность КАК Активность,
			|	РасчетНалогаНаИмущество.ПериодРасчета КАК ПериодРасчета,
			|	РасчетНалогаНаИмущество.Организация КАК Организация,
			|	РасчетНалогаНаИмущество.ИФНС КАК ИФНС,
			|	РасчетНалогаНаИмущество.КодПоОКТМО КАК КодПоОКТМО,
			|	РасчетНалогаНаИмущество.КодВидаИмущества КАК КодВидаИмущества,
			|	РасчетНалогаНаИмущество.ОсновноеСредство КАК ОсновноеСредство,
			|	РасчетНалогаНаИмущество.НалоговаяСтавка КАК НалоговаяСтавка,
			|	РасчетНалогаНаИмущество.КБК КАК КБК,
			|	РасчетНалогаНаИмущество.ДатаСведений КАК ДатаСведений,
			|	РасчетНалогаНаИмущество.СуммаНалога КАК СуммаНалога,
			|	РасчетНалогаНаИмущество.СуммаНалогаКУплате КАК СуммаНалогаКУплате,
			|	РасчетНалогаНаИмущество.СуммаАвансовыхПлатежей КАК СуммаАвансовыхПлатежей,
			|	РасчетНалогаНаИмущество.СуммаУменьшенияСуммыНалога КАК СуммаУменьшенияСуммыНалога,
			|	РасчетНалогаНаИмущество.ДоляСтоимостиЗнаменатель КАК ДоляСтоимостиЗнаменатель,
			|	РасчетНалогаНаИмущество.ДоляСтоимостиЧислитель КАК ДоляСтоимостиЧислитель,
			|	РасчетНалогаНаИмущество.КадастровыйНомер КАК КадастровыйНомер,
			|	РасчетНалогаНаИмущество.КадастровыйНомерПомещения КАК КадастровыйНомерПомещения,
			|	РасчетНалогаНаИмущество.НалоговаяБаза КАК НалоговаяБаза,
			|	РасчетНалогаНаИмущество.ОстаточнаяСтоимостьНа0101 КАК ОстаточнаяСтоимостьНа0101,
			|	РасчетНалогаНаИмущество.ОстаточнаяСтоимостьНа0102 КАК ОстаточнаяСтоимостьНа0102,
			|	РасчетНалогаНаИмущество.ОстаточнаяСтоимостьНа0103 КАК ОстаточнаяСтоимостьНа0103,
			|	РасчетНалогаНаИмущество.ОстаточнаяСтоимостьНа0104 КАК ОстаточнаяСтоимостьНа0104,
			|	РасчетНалогаНаИмущество.ОстаточнаяСтоимостьНа0105 КАК ОстаточнаяСтоимостьНа0105,
			|	РасчетНалогаНаИмущество.ОстаточнаяСтоимостьНа0106 КАК ОстаточнаяСтоимостьНа0106,
			|	РасчетНалогаНаИмущество.ОстаточнаяСтоимостьНа0107 КАК ОстаточнаяСтоимостьНа0107,
			|	РасчетНалогаНаИмущество.ОстаточнаяСтоимостьНа0108 КАК ОстаточнаяСтоимостьНа0108,
			|	РасчетНалогаНаИмущество.ОстаточнаяСтоимостьНа0109 КАК ОстаточнаяСтоимостьНа0109,
			|	РасчетНалогаНаИмущество.ОстаточнаяСтоимостьНа0110 КАК ОстаточнаяСтоимостьНа0110,
			|	РасчетНалогаНаИмущество.ОстаточнаяСтоимостьНа0111 КАК ОстаточнаяСтоимостьНа0111,
			|	РасчетНалогаНаИмущество.ОстаточнаяСтоимостьНа0112 КАК ОстаточнаяСтоимостьНа0112,
			|	РасчетНалогаНаИмущество.ОстаточнаяСтоимостьНа3112 КАК ОстаточнаяСтоимостьНа3112,
			|	РасчетНалогаНаИмущество.ОснованиеЛьготы КАК ОснованиеЛьготы,
			|	РасчетНалогаНаИмущество.УсловныйНомер КАК УсловныйНомер,
			|	РасчетНалогаНаИмущество.СредняяСтоимость КАК СреднегодоваяСтоимость,
			|	РасчетНалогаНаИмущество.НеоблагаемаяСредняяСтоимость КАК НеоблагаемаяСреднегодоваяСтоимость,
			|	РасчетНалогаНаИмущество.СуммаНалога КАК СуммаНалогаСУчетомНеоблагаемойБазы,
			|	НАЧАЛОПЕРИОДА(РасчетНалогаНаИмущество.ПериодРасчета, ГОД) КАК ГодРасчета
			|ПОМЕСТИТЬ втРасчетПоСреднегодовой
			|ИЗ
			|	РегистрСведений.РасчетНалогаНаИмущество КАК РасчетНалогаНаИмущество
			|ГДЕ
			|	РасчетНалогаНаИмущество.Регистратор В(&СписокРегистраторов)
			|	И РасчетНалогаНаИмущество.ВидНалоговойБазы = 
			|			ЗНАЧЕНИЕ(Перечисление.НалоговаяБазаПоНалогуНаИмущество.СреднегодоваяСтоимость)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	втРасчетПоСреднегодовой.Регистратор КАК Регистратор,
			|	втРасчетПоСреднегодовой.ПериодРасчета КАК ПериодРасчета,
			|	втРасчетПоСреднегодовой.Организация КАК Организация,
			|	втРасчетПоСреднегодовой.ИФНС КАК ИФНС,
			|	втРасчетПоСреднегодовой.КодПоОКТМО КАК КодПоОКТМО,
			|	втРасчетПоСреднегодовой.ОсновноеСредство КАК ОсновноеСредство,
			|	втРасчетПоСреднегодовой.ДатаСведений КАК ДатаСведений,
			|	СУММА(1) КАК КоличествоДублей
			|ПОМЕСТИТЬ втПроблемыКонвертации
			|ИЗ
			|	втРасчетПоСреднегодовой КАК втРасчетПоСреднегодовой
			|
			|СГРУППИРОВАТЬ ПО
			|	втРасчетПоСреднегодовой.ОсновноеСредство,
			|	втРасчетПоСреднегодовой.ИФНС,
			|	втРасчетПоСреднегодовой.ПериодРасчета,
			|	втРасчетПоСреднегодовой.ДатаСведений,
			|	втРасчетПоСреднегодовой.Организация,
			|	втРасчетПоСреднегодовой.КодПоОКТМО,
			|	втРасчетПоСреднегодовой.Регистратор
			|
			|ИМЕЮЩИЕ
			|	СУММА(1) > 1
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	втРасчетПоСреднегодовой.Регистратор      КАК Регистратор,
			|	втРасчетПоСреднегодовой.НомерСтроки      КАК НомерСтроки,
			|	втРасчетПоСреднегодовой.Активность       КАК Активность,
			|	втРасчетПоСреднегодовой.ПериодРасчета    КАК ПериодРасчета,
			|	втРасчетПоСреднегодовой.Организация      КАК Организация,
			|	втРасчетПоСреднегодовой.ИФНС             КАК ИФНС,
			|	втРасчетПоСреднегодовой.КодПоОКТМО       КАК КодПоОКТМО,
			|	втРасчетПоСреднегодовой.КодВидаИмущества КАК КодВидаИмущества,
			|	втРасчетПоСреднегодовой.ОсновноеСредство КАК ОсновноеСредство,
			|	втРасчетПоСреднегодовой.НалоговаяСтавка  КАК НалоговаяСтавка,
			|	втРасчетПоСреднегодовой.КБК              КАК КБК,
			|
			|// Дата сведений - первый ненулевой месяц.
			|	ВЫБОР
			|		КОГДА втРасчетПоСреднегодовой.ДатаСведений = ДАТАВРЕМЯ(1, 1, 1)
			|			ТОГДА ВЫБОР
			|				КОГДА втРасчетПоСреднегодовой.ОстаточнаяСтоимостьНа0101 <> 0
			|					ТОГДА втРасчетПоСреднегодовой.ГодРасчета
			|				КОГДА втРасчетПоСреднегодовой.ОстаточнаяСтоимостьНа0102 <> 0
			|					ТОГДА ДОБАВИТЬКДАТЕ(втРасчетПоСреднегодовой.ГодРасчета, МЕСЯЦ, 1)
			|				КОГДА втРасчетПоСреднегодовой.ОстаточнаяСтоимостьНа0103 <> 0
			|					ТОГДА ДОБАВИТЬКДАТЕ(втРасчетПоСреднегодовой.ГодРасчета, МЕСЯЦ, 2)
			|				КОГДА втРасчетПоСреднегодовой.ОстаточнаяСтоимостьНа0104 <> 0
			|					ТОГДА ДОБАВИТЬКДАТЕ(втРасчетПоСреднегодовой.ГодРасчета, МЕСЯЦ, 3)
			|				КОГДА втРасчетПоСреднегодовой.ОстаточнаяСтоимостьНа0105 <> 0
			|					ТОГДА ДОБАВИТЬКДАТЕ(втРасчетПоСреднегодовой.ГодРасчета, МЕСЯЦ, 4)
			|				КОГДА втРасчетПоСреднегодовой.ОстаточнаяСтоимостьНа0106 <> 0
			|					ТОГДА ДОБАВИТЬКДАТЕ(втРасчетПоСреднегодовой.ГодРасчета, МЕСЯЦ, 5)
			|				КОГДА втРасчетПоСреднегодовой.ОстаточнаяСтоимостьНа0107 <> 0
			|					ТОГДА ДОБАВИТЬКДАТЕ(втРасчетПоСреднегодовой.ГодРасчета, МЕСЯЦ, 6)
			|				КОГДА втРасчетПоСреднегодовой.ОстаточнаяСтоимостьНа0108 <> 0
			|					ТОГДА ДОБАВИТЬКДАТЕ(втРасчетПоСреднегодовой.ГодРасчета, МЕСЯЦ, 7)
			|				КОГДА втРасчетПоСреднегодовой.ОстаточнаяСтоимостьНа0109 <> 0
			|					ТОГДА ДОБАВИТЬКДАТЕ(втРасчетПоСреднегодовой.ГодРасчета, МЕСЯЦ, 8)
			|				КОГДА втРасчетПоСреднегодовой.ОстаточнаяСтоимостьНа0110 <> 0
			|					ТОГДА ДОБАВИТЬКДАТЕ(втРасчетПоСреднегодовой.ГодРасчета, МЕСЯЦ, 9)
			|				КОГДА втРасчетПоСреднегодовой.ОстаточнаяСтоимостьНа0111 <> 0
			|					ТОГДА ДОБАВИТЬКДАТЕ(втРасчетПоСреднегодовой.ГодРасчета, МЕСЯЦ, 10)
			|				КОГДА втРасчетПоСреднегодовой.ОстаточнаяСтоимостьНа0112 <> 0
			|					ТОГДА ДОБАВИТЬКДАТЕ(втРасчетПоСреднегодовой.ГодРасчета, МЕСЯЦ, 11)
			|				КОГДА втРасчетПоСреднегодовой.ОстаточнаяСтоимостьНа3112 <> 0
			|					ТОГДА КОНЕЦПЕРИОДА(втРасчетПоСреднегодовой.ГодРасчета, ГОД)
			|				КОНЕЦ
			|		ИНАЧЕ 
			|			втРасчетПоСреднегодовой.ДатаСведений
			|	КОНЕЦ КАК ДатаСведений,
			|
			|	втРасчетПоСреднегодовой.СуммаНалога                КАК СуммаНалога,
			|	втРасчетПоСреднегодовой.СуммаНалогаКУплате         КАК СуммаНалогаКУплате,
			|	втРасчетПоСреднегодовой.СуммаАвансовыхПлатежей     КАК СуммаАвансовыхПлатежей,
			|	втРасчетПоСреднегодовой.СуммаУменьшенияСуммыНалога КАК СуммаУменьшенияСуммыНалога,
			|	втРасчетПоСреднегодовой.ДоляСтоимостиЗнаменатель   КАК ДоляСтоимостиЗнаменатель,
			|	втРасчетПоСреднегодовой.ДоляСтоимостиЧислитель     КАК ДоляСтоимостиЧислитель,
			|	втРасчетПоСреднегодовой.КадастровыйНомер           КАК КадастровыйНомер,
			|	втРасчетПоСреднегодовой.КадастровыйНомерПомещения  КАК КадастровыйНомерПомещения,
			|	втРасчетПоСреднегодовой.НалоговаяБаза              КАК НалоговаяБаза,
			|
			|	втРасчетПоСреднегодовой.ОстаточнаяСтоимостьНа0101  КАК ОстаточнаяСтоимостьНа0101,
			|	втРасчетПоСреднегодовой.ОстаточнаяСтоимостьНа0102  КАК ОстаточнаяСтоимостьНа0102,
			|	втРасчетПоСреднегодовой.ОстаточнаяСтоимостьНа0103  КАК ОстаточнаяСтоимостьНа0103,
			|	втРасчетПоСреднегодовой.ОстаточнаяСтоимостьНа0104  КАК ОстаточнаяСтоимостьНа0104,
			|	втРасчетПоСреднегодовой.ОстаточнаяСтоимостьНа0105  КАК ОстаточнаяСтоимостьНа0105,
			|	втРасчетПоСреднегодовой.ОстаточнаяСтоимостьНа0106  КАК ОстаточнаяСтоимостьНа0106,
			|	втРасчетПоСреднегодовой.ОстаточнаяСтоимостьНа0107  КАК ОстаточнаяСтоимостьНа0107,
			|	втРасчетПоСреднегодовой.ОстаточнаяСтоимостьНа0108  КАК ОстаточнаяСтоимостьНа0108,
			|	втРасчетПоСреднегодовой.ОстаточнаяСтоимостьНа0109  КАК ОстаточнаяСтоимостьНа0109,
			|	втРасчетПоСреднегодовой.ОстаточнаяСтоимостьНа0110  КАК ОстаточнаяСтоимостьНа0110,
			|	втРасчетПоСреднегодовой.ОстаточнаяСтоимостьНа0111  КАК ОстаточнаяСтоимостьНа0111,
			|	втРасчетПоСреднегодовой.ОстаточнаяСтоимостьНа0112  КАК ОстаточнаяСтоимостьНа0112,
			|	втРасчетПоСреднегодовой.ОстаточнаяСтоимостьНа3112  КАК ОстаточнаяСтоимостьНа3112,
			|
			|	втРасчетПоСреднегодовой.ОснованиеЛьготы            КАК ОснованиеЛьготы,
			|	втРасчетПоСреднегодовой.УсловныйНомер              КАК УсловныйНомер,
			|	втРасчетПоСреднегодовой.СреднегодоваяСтоимость     КАК СреднегодоваяСтоимость,
			|
			|	втРасчетПоСреднегодовой.НеоблагаемаяСреднегодоваяСтоимость КАК НеоблагаемаяСреднегодоваяСтоимость,
			|	втРасчетПоСреднегодовой.СуммаНалогаСУчетомНеоблагаемойБазы КАК СуммаНалогаСУчетомНеоблагаемойБазы
			|
			|ПОМЕСТИТЬ втПроблемныеСДатамиСведений
			|
			|ИЗ
			|	втРасчетПоСреднегодовой КАК втРасчетПоСреднегодовой
			|
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПроблемыКонвертации КАК втПроблемыКонвертации
			|		ПО втРасчетПоСреднегодовой.Регистратор         = втПроблемыКонвертации.Регистратор
			|			И втРасчетПоСреднегодовой.ПериодРасчета    = втПроблемыКонвертации.ПериодРасчета
			|			И втРасчетПоСреднегодовой.Организация      = втПроблемыКонвертации.Организация
			|			И втРасчетПоСреднегодовой.ИФНС             = втПроблемыКонвертации.ИФНС
			|			И втРасчетПоСреднегодовой.ОсновноеСредство = втПроблемыКонвертации.ОсновноеСредство
			|			И втРасчетПоСреднегодовой.ДатаСведений     = втПроблемыКонвертации.ДатаСведений
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	втРасчетПоСреднегодовой.Регистратор                КАК Регистратор,
			|	втРасчетПоСреднегодовой.НомерСтроки                КАК НомерСтроки,
			|	втРасчетПоСреднегодовой.Активность                 КАК Активность,
			|	втРасчетПоСреднегодовой.ПериодРасчета              КАК ПериодРасчета,
			|	втРасчетПоСреднегодовой.Организация                КАК Организация,
			|	втРасчетПоСреднегодовой.ИФНС                       КАК ИФНС,
			|	втРасчетПоСреднегодовой.КодПоОКТМО                 КАК КодПоОКТМО,
			|	втРасчетПоСреднегодовой.КодВидаИмущества           КАК КодВидаИмущества,
			|	втРасчетПоСреднегодовой.ОсновноеСредство           КАК ОсновноеСредство,
			|	втРасчетПоСреднегодовой.НалоговаяСтавка            КАК НалоговаяСтавка,
			|	втРасчетПоСреднегодовой.КБК                        КАК КБК,
			|	втРасчетПоСреднегодовой.ДатаСведений               КАК ДатаСведений,
			|	втРасчетПоСреднегодовой.СуммаНалога                КАК СуммаНалога,
			|	втРасчетПоСреднегодовой.СуммаНалогаКУплате         КАК СуммаНалогаКУплате,
			|	втРасчетПоСреднегодовой.СуммаАвансовыхПлатежей     КАК СуммаАвансовыхПлатежей,
			|	втРасчетПоСреднегодовой.СуммаУменьшенияСуммыНалога КАК СуммаУменьшенияСуммыНалога,
			|	втРасчетПоСреднегодовой.ДоляСтоимостиЗнаменатель   КАК ДоляСтоимостиЗнаменатель,
			|	втРасчетПоСреднегодовой.ДоляСтоимостиЧислитель     КАК ДоляСтоимостиЧислитель,
			|	втРасчетПоСреднегодовой.КадастровыйНомер           КАК КадастровыйНомер,
			|	втРасчетПоСреднегодовой.КадастровыйНомерПомещения  КАК КадастровыйНомерПомещения,
			|	втРасчетПоСреднегодовой.НалоговаяБаза              КАК НалоговаяБаза,
			|
			|	втРасчетПоСреднегодовой.ОстаточнаяСтоимостьНа0101  КАК ОстаточнаяСтоимостьНа0101,
			|	втРасчетПоСреднегодовой.ОстаточнаяСтоимостьНа0102  КАК ОстаточнаяСтоимостьНа0102,
			|	втРасчетПоСреднегодовой.ОстаточнаяСтоимостьНа0103  КАК ОстаточнаяСтоимостьНа0103,
			|	втРасчетПоСреднегодовой.ОстаточнаяСтоимостьНа0104  КАК ОстаточнаяСтоимостьНа0104,
			|	втРасчетПоСреднегодовой.ОстаточнаяСтоимостьНа0105  КАК ОстаточнаяСтоимостьНа0105,
			|	втРасчетПоСреднегодовой.ОстаточнаяСтоимостьНа0106  КАК ОстаточнаяСтоимостьНа0106,
			|	втРасчетПоСреднегодовой.ОстаточнаяСтоимостьНа0107  КАК ОстаточнаяСтоимостьНа0107,
			|	втРасчетПоСреднегодовой.ОстаточнаяСтоимостьНа0108  КАК ОстаточнаяСтоимостьНа0108,
			|	втРасчетПоСреднегодовой.ОстаточнаяСтоимостьНа0109  КАК ОстаточнаяСтоимостьНа0109,
			|	втРасчетПоСреднегодовой.ОстаточнаяСтоимостьНа0110  КАК ОстаточнаяСтоимостьНа0110,
			|	втРасчетПоСреднегодовой.ОстаточнаяСтоимостьНа0111  КАК ОстаточнаяСтоимостьНа0111,
			|	втРасчетПоСреднегодовой.ОстаточнаяСтоимостьНа0112  КАК ОстаточнаяСтоимостьНа0112,
			|	втРасчетПоСреднегодовой.ОстаточнаяСтоимостьНа3112  КАК ОстаточнаяСтоимостьНа3112,
			|
			|	втРасчетПоСреднегодовой.ОснованиеЛьготы            КАК ОснованиеЛьготы,
			|	втРасчетПоСреднегодовой.УсловныйНомер              КАК УсловныйНомер,
			|	втРасчетПоСреднегодовой.СреднегодоваяСтоимость     КАК СреднегодоваяСтоимость,
			|
			|	втРасчетПоСреднегодовой.НеоблагаемаяСреднегодоваяСтоимость КАК НеоблагаемаяСреднегодоваяСтоимость,
			|	втРасчетПоСреднегодовой.СуммаНалогаСУчетомНеоблагаемойБазы КАК СуммаНалогаСУчетомНеоблагаемойБазы
			|ИЗ
			|	втРасчетПоСреднегодовой КАК втРасчетПоСреднегодовой
			|
			|		ЛЕВОЕ СОЕДИНЕНИЕ втПроблемыКонвертации КАК втПроблемыКонвертации
			|		ПО втРасчетПоСреднегодовой.Регистратор = втПроблемыКонвертации.Регистратор
			|			И втРасчетПоСреднегодовой.ПериодРасчета = втПроблемыКонвертации.ПериодРасчета
			|			И втРасчетПоСреднегодовой.Организация = втПроблемыКонвертации.Организация
			|			И втРасчетПоСреднегодовой.ИФНС = втПроблемыКонвертации.ИФНС
			|			И втРасчетПоСреднегодовой.КодПоОКТМО = втПроблемыКонвертации.КодПоОКТМО
			|			И втРасчетПоСреднегодовой.ОсновноеСредство = втПроблемыКонвертации.ОсновноеСредство
			|			И втРасчетПоСреднегодовой.ДатаСведений = втПроблемыКонвертации.ДатаСведений
			|
			|ГДЕ
			|	втПроблемыКонвертации.КоличествоДублей ЕСТЬ NULL
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	втПроблемныеСДатамиСведений.Регистратор,
			|	втПроблемныеСДатамиСведений.НомерСтроки,
			|	втПроблемныеСДатамиСведений.Активность,
			|	втПроблемныеСДатамиСведений.ПериодРасчета,
			|	втПроблемныеСДатамиСведений.Организация,
			|	втПроблемныеСДатамиСведений.ИФНС,
			|	втПроблемныеСДатамиСведений.КодПоОКТМО,
			|	втПроблемныеСДатамиСведений.КодВидаИмущества,
			|	втПроблемныеСДатамиСведений.ОсновноеСредство,
			|	втПроблемныеСДатамиСведений.НалоговаяСтавка,
			|	втПроблемныеСДатамиСведений.КБК,
			|	втПроблемныеСДатамиСведений.ДатаСведений,
			|	втПроблемныеСДатамиСведений.СуммаНалога,
			|	втПроблемныеСДатамиСведений.СуммаНалогаКУплате,
			|	втПроблемныеСДатамиСведений.СуммаАвансовыхПлатежей,
			|	втПроблемныеСДатамиСведений.СуммаУменьшенияСуммыНалога,
			|	втПроблемныеСДатамиСведений.ДоляСтоимостиЗнаменатель,
			|	втПроблемныеСДатамиСведений.ДоляСтоимостиЧислитель,
			|	втПроблемныеСДатамиСведений.КадастровыйНомер,
			|	втПроблемныеСДатамиСведений.КадастровыйНомерПомещения,
			|	втПроблемныеСДатамиСведений.НалоговаяБаза,
			|	втПроблемныеСДатамиСведений.ОстаточнаяСтоимостьНа0101,
			|	втПроблемныеСДатамиСведений.ОстаточнаяСтоимостьНа0102,
			|	втПроблемныеСДатамиСведений.ОстаточнаяСтоимостьНа0103,
			|	втПроблемныеСДатамиСведений.ОстаточнаяСтоимостьНа0104,
			|	втПроблемныеСДатамиСведений.ОстаточнаяСтоимостьНа0105,
			|	втПроблемныеСДатамиСведений.ОстаточнаяСтоимостьНа0106,
			|	втПроблемныеСДатамиСведений.ОстаточнаяСтоимостьНа0107,
			|	втПроблемныеСДатамиСведений.ОстаточнаяСтоимостьНа0108,
			|	втПроблемныеСДатамиСведений.ОстаточнаяСтоимостьНа0109,
			|	втПроблемныеСДатамиСведений.ОстаточнаяСтоимостьНа0110,
			|	втПроблемныеСДатамиСведений.ОстаточнаяСтоимостьНа0111,
			|	втПроблемныеСДатамиСведений.ОстаточнаяСтоимостьНа0112,
			|	втПроблемныеСДатамиСведений.ОстаточнаяСтоимостьНа3112,
			|	втПроблемныеСДатамиСведений.ОснованиеЛьготы,
			|	втПроблемныеСДатамиСведений.УсловныйНомер,
			|	втПроблемныеСДатамиСведений.СреднегодоваяСтоимость,
			|	втПроблемныеСДатамиСведений.НеоблагаемаяСреднегодоваяСтоимость,
			|	втПроблемныеСДатамиСведений.СуммаНалогаСУчетомНеоблагаемойБазы
			|ИЗ
			|	втПроблемныеСДатамиСведений КАК втПроблемныеСДатамиСведений
			|
			|ИТОГИ ПО
			|	Регистратор";

			Результат = Запрос.Выполнить();

			Если Не Результат.Пустой() Тогда

				ВыборкаРегистратор = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

				Пока ВыборкаРегистратор.Следующий() Цикл
					
					НаборЗаписейСреднегодоваяСтоимость = 
								РегистрыСведений.РасчетНалогаНаИмуществоПоСреднегодовойСтоимости.СоздатьНаборЗаписей();
								
					НаборЗаписейСреднегодоваяСтоимость.Отбор.Регистратор.Значение = ВыборкаРегистратор.Регистратор;

					ВыборкаСтроки = ВыборкаРегистратор.Выбрать(ОбходРезультатаЗапроса.Прямой);
					Пока ВыборкаСтроки.Следующий() Цикл
						Стр = НаборЗаписейСреднегодоваяСтоимость.Добавить();
						ЗаполнитьЗначенияСвойств(Стр, ВыборкаСтроки);

					КонецЦикла;
					НаборЗаписейСреднегодоваяСтоимость.Записать();
					
					// Включим регистратор в список для очистки записей по старому регистру.
					Если РегистраторыДляОчистки.Найти(ВыборкаРегистратор.Регистратор) = Неопределено Тогда
						РегистраторыДляОчистки.Добавить(ВыборкаРегистратор.Регистратор);
					КонецЕсли;

				КонецЦикла;

			КонецЕсли;

#КонецОбласти

#Область ОчисткаПереходногоПериода
			// Удаляем перенесенные записи из старого регистра сведений.
			Для Каждого Регистратор Из РегистраторыДляОчистки Цикл
				УдаляемыйНаборЗаписей = РегистрыСведений.РасчетНалогаНаИмущество.СоздатьНаборЗаписей();
				УдаляемыйНаборЗаписей.Отбор.Регистратор.Значение = Регистратор;

				УдаляемыйНаборЗаписей.Записать();
				
				// В корректировках регистров добавим новые регистры в список корректируемых регистров.
				Если ТипЗнч(Регистратор) = Тип("ДокументСсылка.КорректировкаРегистров") Тогда
					
					КорректировкаРегистров = Регистратор.ПолучитьОбъект();
					КорректировкаРегистров.ТаблицаРегистров.Добавить().Имя = "РасчетНалогаНаИмуществоПоКадастровойСтоимости";
					КорректировкаРегистров.ТаблицаРегистров.Добавить().Имя = "РасчетНалогаНаИмуществоПоСреднегодовойСтоимости";
					КорректировкаРегистров.Записать(РежимЗаписиДокумента.Запись);

				КонецЕсли;
				
			КонецЦикла;

#КонецОбласти
			
			// Отметим успешно обработанные объекты.
			Для Каждого Регистратор Из СписокРегистраторов Цикл
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Регистратор);
			КонецЦикла;

			ЗафиксироватьТранзакцию();

		Исключение
			ОтменитьТранзакцию();

			ПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());

			МетаданныеОбъекта = Метаданные.РегистрыСведений.РасчетНалогаНаИмущество;
			ПолноеИмяОбъекта  = МетаданныеОбъекта.ПолноеИмя();

			ТекстСообщения = НСтр("ru = 'Не удалось распределить данные регистра %ИмяРегистра%
								  |по двум новым регистрам сведений по причине: %Причина%';
								  |en = 'Cannot allocate the %ИмяРегистра% register data 
								  | by two new information registers due to: %Причина%'");

			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПредставлениеОшибки);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ИмяРегистра%", ПолноеИмяОбъекта);

			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
										УровеньЖурналаРегистрации.Предупреждение, 
										МетаданныеОбъекта, 
										Неопределено, 
										ТекстСообщения);

			ВызватьИсключение;

		КонецПопытки;

	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = Истина;
КонецПроцедуры

#КонецОбласти
#КонецОбласти
#КонецОбласти

#КонецЕсли
