
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Запись, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если Запись.Сумма + Запись.Проценты + Запись.Комиссия = 0 Тогда
		
		Текст  = НСтр("ru = 'В строке должна быть указана хотя бы одна из сумм графика!';
						|en = 'At least one of the schedule amounts should be specified in the line.'");		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст);
		Отказ = Истина;
		
	КонецЕсли;
	
	Если ДоговорЗакрыт(Запись.ВариантГрафика) Тогда
		Отказ = Истина;
		Текст = НСтр("ru = 'Изменения графика по закрытому договору запрещены!';
					|en = 'Changing schedule for a closed contract is not allowed.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДоговорЗакрыт(ВариантГрафика)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВариантГрафика, "Владелец")
		= Перечисления.СтатусыДоговоровКонтрагентов.Закрыт;
	
КонецФункции

#КонецОбласти
