
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Организация") И ЗначениеЗаполнено(Параметры.Организация) Тогда
		
		ОтборОрганизация = Параметры.Организация;
		УстановитьОтборСписка(ОтборОрганизация, "Организация");
		ВалютаРегл = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(ОтборОрганизация);
		Элементы.Сумма.Заголовок = СтрШаблон(НСтр("ru = 'Сумма (%1)';
													|en = 'Amount (%1)'"), Строка(ВалютаРегл));

	КонецЕсли;
	
	Если Параметры.Свойство("НаправлениеДеятельности") И ЗначениеЗаполнено(Параметры.НаправлениеДеятельности) Тогда
		
		ОтборНаправлениеДеятельности = Параметры.НаправлениеДеятельности;
		УстановитьОтборСписка(ОтборНаправлениеДеятельности, "НаправлениеДеятельности");
		
	КонецЕсли;
		
	Если Параметры.Свойство("НаправлениеРасходованияЦелевыхСредств") И ЗначениеЗаполнено(Параметры.НаправлениеРасходованияЦелевыхСредств) Тогда
		
		ОтборНаправлениеРасходованияЦелевыхСредств = Параметры.НаправлениеРасходованияЦелевыхСредств;
		УстановитьОтборСписка(ОтборНаправлениеРасходованияЦелевыхСредств, "НаправлениеРасходованияЦелевыхСредств");
		
	КонецЕсли;
	
	Если Параметры.Свойство("РежимРасшифровки") И Параметры.РежимРасшифровки Тогда
		
		Элементы.ОтборНаправлениеРасходованияЦелевыхСредств.Видимость = Ложь;
		Элементы.КоманднаяПанельФормы.Видимость = Ложь;
		Элементы.Список.КонтекстноеМеню.Доступность = Ложь;
		
		ШаблонЗаголовка = НСтр("ru = 'Показатель %1 ""%2"", графа ""%3"" - расшифровка значения';
								|en = 'Indicator %1 ""%2"", column ""%3"" - value details'");
		Показатель = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонЗаголовка, Параметры.КодПредставление, Параметры.ЗаголовокПоказателя, Параметры.Графа);
		Показатель = СтрЗаменить(Показатель, Символы.ПС, "");
		Элементы.Показатель.Заголовок = Показатель;
		Элементы.Показатель.Видимость = Истина;
	
	
		Если Параметры.Свойство("НачалоПериода") И ЗначениеЗаполнено(Параметры.НачалоПериода) Тогда
			
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				Список,
				"Период",
				Параметры.НачалоПериода,
				ВидСравненияКомпоновкиДанных.БольшеИлиРавно,
				,
				ЗначениеЗаполнено(Параметры.НачалоПериода));
				
		КонецЕсли;
	
		Если Параметры.Свойство("КонецПериода") И ЗначениеЗаполнено(Параметры.КонецПериода) Тогда
			
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				Список,
				"Период",
				Параметры.КонецПериода,
				ВидСравненияКомпоновкиДанных.МеньшеИлиРавно,
				,
				ЗначениеЗаполнено(Параметры.КонецПериода));
				
		КонецЕсли;
		
		Если Параметры.Свойство("УкрупненныеКоды") И ЗначениеЗаполнено(Параметры.УкрупненныеКоды) Тогда
			
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				Список,
				"НаправлениеРасходованияЦелевыхСредств.УкрупненныйКодРасходов",
				Параметры.УкрупненныеКоды,
				ВидСравненияКомпоновкиДанных.ВСписке,
				,
				ЗначениеЗаполнено(Параметры.УкрупненныеКоды));
				
		КонецЕсли;
		
		Если Параметры.Свойство("ДетализированныеКоды") И ЗначениеЗаполнено(Параметры.ДетализированныеКоды) Тогда
			
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				Список,
				"НаправлениеРасходованияЦелевыхСредств.Код",
				Параметры.ДетализированныеКоды,
				ВидСравненияКомпоновкиДанных.ВСписке,
				,
				ЗначениеЗаполнено(Параметры.ДетализированныеКоды));
				
		КонецЕсли;
	
	КонецЕсли;
		
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОтборОрганизацияПриИзменении(Элемент)
	
	УстановитьОтборСписка(ОтборОрганизация, "Организация");
	
	Если ЗначениеЗаполнено(ОтборОрганизация) Тогда
		ВалютаРегл = ЗначениеНастроекКлиентСерверПовтИсп.ВалютаРегламентированногоУчетаОрганизации(ОтборОрганизация);
		Элементы.Сумма.Заголовок = СтрШаблон(НСтр("ru = 'Сумма (%1)';
													|en = 'Amount (%1)'"), Строка(ВалютаРегл));
	Иначе
		Элементы.Сумма.Заголовок = СтрШаблон(НСтр("ru = 'Сумма';
													|en = 'Amount'"));
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ОтборНаправлениеДеятельностиПриИзменении(Элемент)
	УстановитьОтборСписка(ОтборНаправлениеДеятельности, "НаправлениеДеятельности");
КонецПроцедуры

&НаКлиенте
Процедура ОтборНаправлениеРасходованияЦелевыхСредствПриИзменении(Элемент)
	УстановитьОтборСписка(ОтборНаправлениеРасходованияЦелевыхСредств, "НаправлениеРасходованияЦелевыхСредств");
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьОтборСписка(Отбор, ИмяПоля)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список,
		ИмяПоля,
		Отбор,
		ВидСравненияКомпоновкиДанных.Равно,
		,
		ЗначениеЗаполнено(Отбор));
		
	Элементы[ИмяПоля].Видимость = Не ЗначениеЗаполнено(Отбор);
	
КонецПроцедуры

#КонецОбласти