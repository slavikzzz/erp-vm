#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОформлениеКД = ОбщегоНазначенияБЗК.ДобавитьУсловноеОформление(Список, "ВходящийДокумент");
	ОбщегоНазначенияБЗК.ДобавитьОтборУсловногоОформления(ОформлениеКД, "ТипСообщения", "=", 111);
	ОбщегоНазначенияБЗК.УстановитьПараметрУсловногоОформления(ОформлениеКД, "Текст", НСтр("ru = '<Сообщение об изменении ЭЛН>';
																							|en = '<ESLR change message>'"));
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(ТаблицаФормы, ИдентификаторСтроки, ПолеФормы, СтандартнаяОбработка)
	Если ИдентификаторСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ТекущаяСтрока = Элементы.Список.ДанныеСтроки(ИдентификаторСтроки);
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	СтандартнаяОбработка = Ложь;
	Если ПолеФормы = Элементы.СписокФизическоеЛицо Тогда
		ПоказатьЗначение(, ТекущаяСтрока.ФизическоеЛицо);
	ИначеЕсли ЗначениеЗаполнено(ТекущаяСтрока.ВходящийДокумент) Тогда
		ПоказатьЗначение(, ТекущаяСтрока.ВходящийДокумент);
	ИначеЕсли ТекущаяСтрока.ТипСообщения = 111 Тогда
		БазоваяОбработка = Истина;
		// АПК:278-выкл Базовые подсистемы могут условно вызывать расширенные.
		Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыРасширеннаяПодсистемы.ПособияСоциальногоСтрахования") Тогда
			Модуль = ОбщегоНазначенияКлиент.ОбщийМодуль("СЭДОФССРасширенныйКлиент");
			Модуль.ПриОткрытииФормыСпискаСообщений111(ТекущаяСтрока, БазоваяОбработка);
		КонецЕсли;
		// АПК:278-вкл
		Если БазоваяОбработка Тогда
			Отбор = Новый Структура;
			Отбор.Вставить("НомерЛисткаНетрудоспособности", ТекущаяСтрока.НомерЛН);
			Отбор.Вставить("ГоловнаяОрганизация",           ТекущаяСтрока.ГоловнаяОрганизация);
			ПараметрыФормы = Новый Структура("Отбор", Отбор);
			ОткрытьФорму("РегистрСведений.СведенияОбЭЛН.ФормаСписка", ПараметрыФормы, , Истина, , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
