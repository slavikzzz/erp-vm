#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ, Замещение)
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	// Изменение данных в этом регистре - это точка входа в обновление данных регистра ЗастрахованныеЛицаСЭДО - для того,
	// чтобы зарегистрировать необходимость отправки в Фонд сведений о том, что сотрудник не числился в организации.
	ЗначенияДоЗаписи = ОбщегоНазначенияБЗК.ЗначениеСвойства(ДополнительныеСвойства, "ЗначенияДоЗаписи");
	Если ТипЗнч(ЗначенияДоЗаписи) = Тип("ТаблицаЗначений") Тогда
		ТаблицаОбновления = РегистрыСведений.ЗастрахованныеЛицаСЭДО.ПустаяТаблицаОбновления();
		Фильтр = Новый Структура;
		Для Каждого Измерение Из Метаданные.РегистрыСведений.СНИЛСВходящихСообщенийСЭДО.Измерения Цикл
			Фильтр.Вставить(Измерение.Имя);
		КонецЦикла;
		ЗначенияДоЗаписи.Колонки.Добавить("Найдена", Новый ОписаниеТипов("Булево"));
		Для Каждого Запись Из ЭтотОбъект Цикл
			Если ТипЗнч(Запись.ВходящийДокумент) = Тип("ДокументСсылка.НесоответствиеСведенийОЗастрахованномЛицеСЭДО") Тогда
				Продолжить;
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(Фильтр, Запись);
			Найденные = ЗначенияДоЗаписи.НайтиСтроки(Фильтр);
			Если Найденные.Количество() > 0 Тогда
				Для Каждого СтрокаТаблицы Из Найденные Цикл
					СтрокаТаблицы.Найдена = Истина;
				КонецЦикла;
			Иначе // Регистрация к обновлению новых записей (с новыми значениями измерений, отсутствовавшими в старом наборе).
				ЗаполнитьЗначенияСвойств(ТаблицаОбновления.Добавить(), Запись);
			КонецЕсли;
		КонецЦикла;
		// Регистрация к обновлению старых записей (со старыми значениями измерений, отсутствующими в текущем наборе).
		НеНайденные = ЗначенияДоЗаписи.НайтиСтроки(Новый Структура("Найдена", Ложь));
		Для Каждого СтрокаТаблицы Из НеНайденные Цикл
			Если ТипЗнч(СтрокаТаблицы.ВходящийДокумент) = Тип("ДокументСсылка.НесоответствиеСведенийОЗастрахованномЛицеСЭДО") Тогда
				Продолжить;
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(ТаблицаОбновления.Добавить(), СтрокаТаблицы);
		КонецЦикла;
		Если ТаблицаОбновления.Количество() > 0 Тогда
			РегистрыСведений.ЗастрахованныеЛицаСЭДО.ОбновитьПоТаблице(ТаблицаОбновления, Истина);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли
