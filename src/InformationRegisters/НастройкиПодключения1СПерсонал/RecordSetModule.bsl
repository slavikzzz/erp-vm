#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;

	Настройки = РегистрыСведений.НастройкиПодключения1СПерсонал.Настройки();
	ЭтотОбъект.ДополнительныеСвойства.Вставить("ТекущийОтветственный", Настройки.Ответственный);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если Количество() = 0 Тогда
		Если Константы.ИспользуетсяПриложение1СПерсонал.Получить() Тогда
			Константы.ИспользуетсяПриложение1СПерсонал.Установить(Ложь);
		КонецЕсли;
	Иначе
		Приложение = Перечисления.ПриложенияДляИнтеграции.УправлениеПерсоналом;
		СостояниеПодключения = ИнтеграцияУправлениеПерсоналомМенеджер.СостояниеПодключенияПриложения(Приложение);
		Если Не ЗначениеЗаполнено(СостояниеПодключения.Состояние) Тогда
			// Значение константы ИспользуетсяПриложение1СПерсонал устанавливаем только если это не отложенное
			// подключение к приложению.
			Используется = ЗначениеЗаполнено(ЭтотОбъект[0].АдресПриложения) И ЗначениеЗаполнено(ЭтотОбъект[0].Идентификатор);
			Если Используется <> Константы.ИспользуетсяПриложение1СПерсонал.Получить() Тогда
				Константы.ИспользуетсяПриложение1СПерсонал.Установить(Используется);
			КонецЕсли;
		КонецЕсли;
		НовыйОтветственный = ЭтотОбъект[0].Ответственный;
		ТекущийОтветственный = Неопределено;
		ЭтотОбъект.ДополнительныеСвойства.Свойство("ТекущийОтветственный", ТекущийОтветственный);
		Если ТекущийОтветственный <> НовыйОтветственный Тогда
			ИнтеграцияУправлениеПерсоналом.ИсключениеИзПравилЗарегистрироватьОтветственного(Приложение, ТекущийОтветственный, НовыйОтветственный);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли