#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// В случае, если хозяйственная операция исключается из учетной политики,
	// то необходимо исключить из учетной политики все шаблоны проводок,
	// относящиеся к данной хозяйственной операции.
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ХозяйственныеОперации.НастройкаФормированияПроводок КАК НастройкаФормированияПроводок,
	|	ХозяйственныеОперации.Операция КАК Операция
	|ПОМЕСТИТЬ ХозяйственныеОперации
	|ИЗ 
	|	&ХозяйственныеОперации КАК ХозяйственныеОперации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ
	|	ПравилаОтраженияВУчете.НастройкаФормированияПроводок КАК НастройкаФормированияПроводок,
	|	ПравилаОтраженияВУчете.ШаблонПроводки КАК ШаблонПроводки
	|ИЗ
	|	ХозяйственныеОперации КАК ХозяйственныеОперации
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ПравилаОтраженияВМеждународномУчете КАК ПравилаОтраженияВУчете
	|	ПО
	|		ХозяйственныеОперации.НастройкаФормированияПроводок = ПравилаОтраженияВУчете.НастройкаФормированияПроводок
	|		И ХозяйственныеОперации.Операция = ПравилаОтраженияВУчете.ШаблонПроводки.Операция
	|";
	
	Запрос.УстановитьПараметр("ХозяйственныеОперации", ЭтотОбъект.Выгрузить());
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	НаборЗаписей = РегистрыСведений.ПравилаОтраженияВМеждународномУчете.СоздатьНаборЗаписей();
	
	Пока Выборка.Следующий() Цикл
		НаборЗаписей.Отбор.НастройкаФормированияПроводок.Установить(Выборка.НастройкаФормированияПроводок);
		НаборЗаписей.Отбор.ШаблонПроводки.Установить(Выборка.ШаблонПроводки);
		НаборЗаписей.Записать();
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли