#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьВариантыРаспределенияРасходовПоНастройкамСтатьиРасходов();
	НастройкиСистемыЛокализация.УстановитьВидимостьЭлементовЛокализации(ЭтотОбъект);
	УстановитьВидимостьЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ИзмененыНастройкиРаспределенияПостатейныхРасходов");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_НаборКонстант" И Источник = "ИспользоватьРеглУчет" Тогда
		УстановитьВидимостьЭлементовФормы();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СтатьяРасходовПриИзменении(Элемент)
	УстановитьВариантыРаспределенияРасходовПоНастройкамСтатьиРасходов();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВариантыРаспределенияРасходовПоНастройкамСтатьиРасходов()
	
	СтатьяРасходов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Запись.СтатьяРасходов, 
		"ВариантРаспределенияРасходовУпр,ВариантРаспределенияРасходовРегл,ВариантРаспределенияРасходовНУ") ;
	ВариантРаспределенияРасходовУпр =	СтатьяРасходов.ВариантРаспределенияРасходовУпр;
	ВариантРаспределенияРасходовРегл =	СтатьяРасходов.ВариантРаспределенияРасходовРегл;
	ВариантРаспределенияРасходовНУ =	СтатьяРасходов.ВариантРаспределенияРасходовНУ;

	МожноНастроитьУпр = 
		Перечисления.ВариантыРаспределенияРасходов.ВариантИспользуетПравилоРаспределения(ВариантРаспределенияРасходовУпр);
	МожноНастроитьРегл = 
		Перечисления.ВариантыРаспределенияРасходов.ВариантИспользуетПравилоРаспределения(ВариантРаспределенияРасходовРегл);
	МожноНастроитьНУ = НЕ ВариантРаспределенияРасходовНУ = ВариантРаспределенияРасходовРегл И 
		Перечисления.ВариантыРаспределенияРасходов.ВариантИспользуетПравилоРаспределенияНУ(ВариантРаспределенияРасходовНУ);

	Если НЕ МожноНастроитьУпр И ЗначениеЗаполнено(Запись.НастройкаУпр) Тогда
		Запись.НастройкаУпр = Неопределено;
		Модифицированность = Истина;
	КонецЕсли;
	Если НЕ МожноНастроитьРегл И ЗначениеЗаполнено(Запись.НастройкаРегл) Тогда
		Запись.НастройкаРегл = Неопределено;
		Модифицированность = Истина;
	КонецЕсли;
	Если НЕ МожноНастроитьНУ И ЗначениеЗаполнено(Запись.НастройкаНУ) Тогда
		Запись.НастройкаНУ = Неопределено;
		Модифицированность = Истина;
	КонецЕсли;
	
	Элементы.НастройкаУпр.Доступность =		МожноНастроитьУпр;
	Элементы.НастройкаРегл.Доступность =	МожноНастроитьРегл;
	Элементы.НастройкаНУ.Доступность =		МожноНастроитьНУ;
	
	СтрокаНеНастраивается = НСтр("ru = 'не настраивается в разрезе организаций и подразделний';
								|en = 'cannot be set by companies and business units'");
	Элементы.НастройкаУпр.ПодсказкаВвода =	?(МожноНастроитьУпр, "", СтрокаНеНастраивается);
	Элементы.НастройкаРегл.ПодсказкаВвода =	?(МожноНастроитьРегл, "", СтрокаНеНастраивается);
	Элементы.НастройкаНУ.ПодсказкаВвода =	?(МожноНастроитьНУ, "", СтрокаНеНастраивается);
	
	// Настройки отборов и параметров создания новых правил
	НазначениеПравилаУпр = Справочники.ПравилаРаспределенияРасходов.ПолучитьНазначениеПравилаПоВариантуРаспределения(ВариантРаспределенияРасходовУпр);
	НазначениеПравилаРегл = Справочники.ПравилаРаспределенияРасходов.ПолучитьНазначениеПравилаПоВариантуРаспределения(ВариантРаспределенияРасходовРегл);
	НазначениеПравилаНУ = Справочники.ПравилаРаспределенияРасходов.ПолучитьНазначениеПравилаПоВариантуРаспределения(ВариантРаспределенияРасходовНУ);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьЭлементовФормы()
	
	//++ Локализация
	Элементы.ЛокализацияГруппаНУ.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьРеглУчет");
	//-- Локализация
	
КонецПроцедуры 

#КонецОбласти
