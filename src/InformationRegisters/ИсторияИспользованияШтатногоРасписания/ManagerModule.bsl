#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"ПрисоединитьДополнительныеТаблицы
	|ЭтотСписок КАК Т
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПодчиненностьПодразделенийОрганизаций КАК Т2 
	|	ПО Т2.Подразделение = Т.ПозицияШтатногоРасписания.Подразделение
	|;
	|РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Т2.ВышестоящееПодразделение)
	|	И ЗначениеРазрешено(Т.ПозицияШтатногоРасписания.Владелец)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ОбработчикиПравилРегистрации

// См. ЗарплатаКадрыРасширенныйСинхронизацияДанных.ШаблонОбработчика
Процедура ПриЗаполненииНастроекОбработчиковПравилРегистрации(Настройки) Экспорт
	ЗарплатаКадрыРасширенныйСинхронизацияДанных.ДляРегистраПодчиненногоРегистратору(Настройки);
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбновитьТекущиеСведенияПозицииШтатногоРасписания(СписокПозицийШтатногоРасписания) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокПозицийШтатногоРасписания", СписокПозицийШтатногоРасписания);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ШтатноеРасписание.Ссылка КАК ПозицияШтатногоРасписания,
		|	ЕСТЬNULL(МИНИМУМ(ИсторияИспользованияШтатногоРасписания.Дата), ДАТАВРЕМЯ(1, 1, 1)) КАК Дата
		|ПОМЕСТИТЬ ВТДатыУтвержденияПозиций
		|ИЗ
		|	Справочник.ШтатноеРасписание КАК ШтатноеРасписание
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияИспользованияШтатногоРасписания КАК ИсторияИспользованияШтатногоРасписания
		|		ПО (ИсторияИспользованияШтатногоРасписания.ПозицияШтатногоРасписания = ШтатноеРасписание.Ссылка)
		|			И (ИсторияИспользованияШтатногоРасписания.Используется)
		|ГДЕ
		|	ШтатноеРасписание.Ссылка В(&СписокПозицийШтатногоРасписания)
		|
		|СГРУППИРОВАТЬ ПО
		|	ШтатноеРасписание.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИсторияИспользованияШтатногоРасписания.ПозицияШтатногоРасписания КАК ПозицияШтатногоРасписания,
		|	МАКСИМУМ(ИсторияИспользованияШтатногоРасписания.Дата) КАК Дата
		|ПОМЕСТИТЬ ВТПоследниеДатыИспользованияПозиций
		|ИЗ
		|	РегистрСведений.ИсторияИспользованияШтатногоРасписания КАК ИсторияИспользованияШтатногоРасписания
		|ГДЕ
		|	ИсторияИспользованияШтатногоРасписания.Используется
		|	И ИсторияИспользованияШтатногоРасписания.ПозицияШтатногоРасписания В(&СписокПозицийШтатногоРасписания)
		|
		|СГРУППИРОВАТЬ ПО
		|	ИсторияИспользованияШтатногоРасписания.ПозицияШтатногоРасписания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИсторияИспользованияШтатногоРасписания.ПозицияШтатногоРасписания КАК ПозицияШтатногоРасписания,
		|	МАКСИМУМ(ИсторияИспользованияШтатногоРасписания.Дата) КАК Дата
		|ПОМЕСТИТЬ ВТДатыЗакрытияПозиций
		|ИЗ
		|	РегистрСведений.ИсторияИспользованияШтатногоРасписания КАК ИсторияИспользованияШтатногоРасписания
		|ГДЕ
		|	НЕ ИсторияИспользованияШтатногоРасписания.Используется
		|	И ИсторияИспользованияШтатногоРасписания.ПозицияШтатногоРасписания В(&СписокПозицийШтатногоРасписания)
		|
		|СГРУППИРОВАТЬ ПО
		|	ИсторияИспользованияШтатногоРасписания.ПозицияШтатногоРасписания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДатыУтвержденияПозиций.ПозицияШтатногоРасписания КАК ПозицияШтатногоРасписания,
		|	ВЫБОР
		|		КОГДА ДатыУтвержденияПозиций.Дата = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК Утверждена,
		|	ДатыУтвержденияПозиций.Дата КАК ДатаУтверждения,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ДатыЗакрытияПозиций.Дата, ДАТАВРЕМЯ(1, 1, 1)) = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК Закрыта,
		|	ЕСТЬNULL(ДатыЗакрытияПозиций.Дата, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаЗакрытия,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ДатыЗакрытияПозиций.Дата, ДАТАВРЕМЯ(1, 1, 1)) <> ДАТАВРЕМЯ(1, 1, 1)
		|				И ДатыУтвержденияПозиций.Дата = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА 0
		|		ИНАЧЕ ИсторияИспользованияШтатногоРасписания.КоличествоСтавок
		|	КОНЕЦ КАК КоличествоСтавок,
		|	ИсторияИспользованияШтатногоРасписания.ГрафикРаботыСотрудников КАК ГрафикРаботыСотрудников,
		|	ИсторияИспользованияШтатногоРасписания.ТарифнаяСетка КАК ТарифнаяСетка,
		|	ИсторияИспользованияШтатногоРасписания.РазрядКатегория КАК РазрядКатегория,
		|	ИсторияИспользованияШтатногоРасписания.ТарифнаяСеткаНадбавки КАК ТарифнаяСеткаНадбавки,
		|	ИсторияИспользованияШтатногоРасписания.РазрядКатегорияНадбавки КАК РазрядКатегорияНадбавки,
		|	ИсторияИспользованияШтатногоРасписания.ФОТ КАК ФОТ,
		|	ИсторияИспользованияШтатногоРасписания.ФОТМин КАК ФОТМин,
		|	ИсторияИспользованияШтатногоРасписания.ФОТМакс КАК ФОТМакс,
		|	ИсторияИспользованияШтатногоРасписания.РайонныйКоэффициентРазмер КАК РайонныйКоэффициентРазмер,
		|	ИсторияИспользованияШтатногоРасписания.РайонныйКоэффициентРазмерМин КАК РайонныйКоэффициентРазмерМин,
		|	ИсторияИспользованияШтатногоРасписания.РайонныйКоэффициентРазмерМакс КАК РайонныйКоэффициентРазмерМакс,
		|	ИсторияИспользованияШтатногоРасписания.СевернаяНадбавкаРазмер КАК СевернаяНадбавкаРазмер,
		|	ИсторияИспользованияШтатногоРасписания.СевернаяНадбавкаРазмерМин КАК СевернаяНадбавкаРазмерМин,
		|	ИсторияИспользованияШтатногоРасписания.СевернаяНадбавкаРазмерМакс КАК СевернаяНадбавкаРазмерМакс,
		|	ИсторияИспользованияШтатногоРасписания.ОкладТариф КАК ОкладТариф,
		|	ИсторияИспользованияШтатногоРасписания.ОкладТарифМин КАК ОкладТарифМин,
		|	ИсторияИспользованияШтатногоРасписания.ОкладТарифМакс КАК ОкладТарифМакс,
		|	ИсторияИспользованияШтатногоРасписания.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	ИсторияИспользованияШтатногоРасписания.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
		|	ИсторияИспользованияШтатногоРасписания.СтатьяФинансирования КАК СтатьяФинансирования,
		|	ИсторияИспользованияШтатногоРасписания.НадбавкаЗаВредностьРазмер КАК НадбавкаЗаВредностьРазмер,
		|	ИсторияИспользованияШтатногоРасписания.НадбавкаЗаВредностьРазмерМин КАК НадбавкаЗаВредностьРазмерМин,
		|	ИсторияИспользованияШтатногоРасписания.НадбавкаЗаВредностьРазмерМакс КАК НадбавкаЗаВредностьРазмерМакс,
		|	ИсторияИспользованияШтатногоРасписания.ВзимаютсяВзносыЗаЗанятыхНаРаботахСДосрочнойПенсией КАК ВзимаютсяВзносыЗаЗанятыхНаРаботахСДосрочнойПенсией,
		|	ИсторияИспользованияШтатногоРасписания.ОснованиеДосрочногоНазначенияПенсии КАК ОснованиеДосрочногоНазначенияПенсии,
		|	ИсторияИспользованияШтатногоРасписания.ОсобыеУсловияТрудаПФР КАК ОсобыеУсловияТрудаПФР,
		|	ИсторияИспользованияШтатногоРасписания.КодПозицииСпискаПФР КАК КодПозицииСпискаПФР,
		|	ИсторияИспользованияШтатногоРасписания.ВыплачиваетсяНадбавкаЗаВредность КАК ВыплачиваетсяНадбавкаЗаВредность,
		|	ИсторияИспользованияШтатногоРасписания.ПроцентНадбавкиЗаВредность КАК ПроцентНадбавкиЗаВредность,
		|	ИсторияИспользованияШтатногоРасписания.УсловияПриема КАК УсловияПриема,
		|	ИсторияИспользованияШтатногоРасписания.ХарактерВыполняемыхРаботПФР КАК ХарактерВыполняемыхРаботПФР,
		|	ИсторияИспользованияШтатногоРасписания.ПервичныеДокументыПФР КАК ПервичныеДокументыПФР,
		|	ИсторияИспользованияШтатногоРасписания.ВидСтажаЛетныхЭкипажей КАК ВидСтажаЛетныхЭкипажей,
		|	ИсторияИспользованияШтатногоРасписания.ВидСтажаШахтеров КАК ВидСтажаШахтеров,
		|	ИсторияИспользованияШтатногоРасписания.ТрудоваяФункция КАК ТрудоваяФункция,
		|	ИсторияИспользованияШтатногоРасписания.ФОТУправленческий КАК ФОТУправленческий,
		|	ИсторияИспользованияШтатногоРасписания.ФОТУправленческийМин КАК ФОТУправленческийМин,
		|	ИсторияИспользованияШтатногоРасписания.ФОТУправленческийМакс КАК ФОТУправленческийМакс
		|ПОМЕСТИТЬ ВТДанныеПозиций
		|ИЗ
		|	ВТДатыУтвержденияПозиций КАК ДатыУтвержденияПозиций
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДатыЗакрытияПозиций КАК ДатыЗакрытияПозиций
		|		ПО ДатыУтвержденияПозиций.ПозицияШтатногоРасписания = ДатыЗакрытияПозиций.ПозицияШтатногоРасписания
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПоследниеДатыИспользованияПозиций КАК ПоследниеДатыИспользованияПозиций
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияИспользованияШтатногоРасписания КАК ИсторияИспользованияШтатногоРасписания
		|			ПО ПоследниеДатыИспользованияПозиций.ПозицияШтатногоРасписания = ИсторияИспользованияШтатногоРасписания.ПозицияШтатногоРасписания
		|				И ПоследниеДатыИспользованияПозиций.Дата = ИсторияИспользованияШтатногоРасписания.Дата
		|		ПО ДатыУтвержденияПозиций.ПозицияШтатногоРасписания = ПоследниеДатыИспользованияПозиций.ПозицияШтатногоРасписания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеПозиций.ПозицияШтатногоРасписания КАК ПозицияШтатногоРасписания,
		|	ДанныеПозиций.Утверждена КАК Утверждена,
		|	ДанныеПозиций.ДатаУтверждения КАК ДатаУтверждения,
		|	ДанныеПозиций.Закрыта КАК Закрыта,
		|	ДанныеПозиций.ДатаЗакрытия КАК ДатаЗакрытия,
		|	ДанныеПозиций.КоличествоСтавок КАК КоличествоСтавок,
		|	ДанныеПозиций.ГрафикРаботыСотрудников КАК ГрафикРаботыСотрудников,
		|	ДанныеПозиций.ТарифнаяСетка КАК ТарифнаяСетка,
		|	ДанныеПозиций.РазрядКатегория КАК РазрядКатегория,
		|	ДанныеПозиций.ТарифнаяСеткаНадбавки КАК ТарифнаяСеткаНадбавки,
		|	ДанныеПозиций.РазрядКатегорияНадбавки КАК РазрядКатегорияНадбавки,
		|	ДанныеПозиций.ФОТ КАК ФОТ,
		|	ДанныеПозиций.ФОТМин КАК ФОТМин,
		|	ДанныеПозиций.ФОТМакс КАК ФОТМакс,
		|	ДанныеПозиций.РайонныйКоэффициентРазмер КАК РайонныйКоэффициентРазмер,
		|	ДанныеПозиций.РайонныйКоэффициентРазмерМин КАК РайонныйКоэффициентРазмерМин,
		|	ДанныеПозиций.РайонныйКоэффициентРазмерМакс КАК РайонныйКоэффициентРазмерМакс,
		|	ДанныеПозиций.СевернаяНадбавкаРазмер КАК СевернаяНадбавкаРазмер,
		|	ДанныеПозиций.СевернаяНадбавкаРазмерМин КАК СевернаяНадбавкаРазмерМин,
		|	ДанныеПозиций.СевернаяНадбавкаРазмерМакс КАК СевернаяНадбавкаРазмерМакс,
		|	ДанныеПозиций.ОкладТариф КАК ОкладТариф,
		|	ДанныеПозиций.ОкладТарифМин КАК ОкладТарифМин,
		|	ДанныеПозиций.ОкладТарифМакс КАК ОкладТарифМакс,
		|	ДанныеПозиций.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	ДанныеПозиций.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
		|	ДанныеПозиций.СтатьяФинансирования КАК СтатьяФинансирования,
		|	ДанныеПозиций.НадбавкаЗаВредностьРазмер КАК НадбавкаЗаВредностьРазмер,
		|	ДанныеПозиций.НадбавкаЗаВредностьРазмерМин КАК НадбавкаЗаВредностьРазмерМин,
		|	ДанныеПозиций.НадбавкаЗаВредностьРазмерМакс КАК НадбавкаЗаВредностьРазмерМакс,
		|	ДанныеПозиций.ВзимаютсяВзносыЗаЗанятыхНаРаботахСДосрочнойПенсией КАК ВзимаютсяВзносыЗаЗанятыхНаРаботахСДосрочнойПенсией,
		|	ДанныеПозиций.ОснованиеДосрочногоНазначенияПенсии КАК ОснованиеДосрочногоНазначенияПенсии,
		|	ДанныеПозиций.ОсобыеУсловияТрудаПФР КАК ОсобыеУсловияТрудаПФР,
		|	ДанныеПозиций.КодПозицииСпискаПФР КАК КодПозицииСпискаПФР,
		|	ДанныеПозиций.ВыплачиваетсяНадбавкаЗаВредность КАК ВыплачиваетсяНадбавкаЗаВредность,
		|	ДанныеПозиций.ПроцентНадбавкиЗаВредность КАК ПроцентНадбавкиЗаВредность,
		|	ДанныеПозиций.УсловияПриема КАК УсловияПриема,
		|	ДанныеПозиций.ХарактерВыполняемыхРаботПФР КАК ХарактерВыполняемыхРаботПФР,
		|	ДанныеПозиций.ПервичныеДокументыПФР КАК ПервичныеДокументыПФР,
		|	ДанныеПозиций.ВидСтажаЛетныхЭкипажей КАК ВидСтажаЛетныхЭкипажей,
		|	ДанныеПозиций.ВидСтажаШахтеров КАК ВидСтажаШахтеров,
		|	ДанныеПозиций.ТрудоваяФункция КАК ТрудоваяФункция,
		|	ДанныеПозиций.ФОТУправленческий КАК ФОТУправленческий,
		|	ДанныеПозиций.ФОТУправленческийМин КАК ФОТУправленческийМин,
		|	ДанныеПозиций.ФОТУправленческийМакс КАК ФОТУправленческийМакс
		|ИЗ
		|	ВТДанныеПозиций КАК ДанныеПозиций
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ШтатноеРасписание КАК ШтатноеРасписание
		|		ПО ДанныеПозиций.ПозицияШтатногоРасписания = ШтатноеРасписание.Ссылка
		|ГДЕ
		|	ВЫБОР
		|			КОГДА ДанныеПозиций.Утверждена <> ШтатноеРасписание.Утверждена
		|				ТОГДА ИСТИНА
		|			КОГДА ДанныеПозиций.ДатаУтверждения <> ШтатноеРасписание.ДатаУтверждения
		|				ТОГДА ИСТИНА
		|			КОГДА ДанныеПозиций.Закрыта <> ШтатноеРасписание.Закрыта
		|				ТОГДА ИСТИНА
		|			КОГДА ДанныеПозиций.ДатаЗакрытия <> ШтатноеРасписание.ДатаЗакрытия
		|				ТОГДА ИСТИНА
		|			КОГДА ДанныеПозиций.КоличествоСтавок <> ШтатноеРасписание.КоличествоСтавок
		|				ТОГДА ИСТИНА
		|			КОГДА ДанныеПозиций.ГрафикРаботыСотрудников <> ШтатноеРасписание.ГрафикРаботыСотрудников
		|				ТОГДА ИСТИНА
		|			КОГДА ДанныеПозиций.ТарифнаяСетка <> ШтатноеРасписание.ТарифнаяСетка
		|				ТОГДА ИСТИНА
		|			КОГДА ДанныеПозиций.РазрядКатегория <> ШтатноеРасписание.РазрядКатегория
		|				ТОГДА ИСТИНА
		|			КОГДА ДанныеПозиций.ТарифнаяСеткаНадбавки <> ШтатноеРасписание.ТарифнаяСеткаНадбавки
		|				ТОГДА ИСТИНА
		|			КОГДА ДанныеПозиций.РазрядКатегорияНадбавки <> ШтатноеРасписание.РазрядКатегорияНадбавки
		|				ТОГДА ИСТИНА
		|			КОГДА ДанныеПозиций.ФОТ <> ШтатноеРасписание.ФОТ
		|				ТОГДА ИСТИНА
		|			КОГДА ДанныеПозиций.ФОТМин <> ШтатноеРасписание.ФОТМин
		|				ТОГДА ИСТИНА
		|			КОГДА ДанныеПозиций.ФОТМакс <> ШтатноеРасписание.ФОТМакс
		|				ТОГДА ИСТИНА
		|			КОГДА ДанныеПозиций.РайонныйКоэффициентРазмер <> ШтатноеРасписание.РайонныйКоэффициентРазмер
		|				ТОГДА ИСТИНА
		|			КОГДА ДанныеПозиций.РайонныйКоэффициентРазмерМин <> ШтатноеРасписание.РайонныйКоэффициентРазмерМин
		|				ТОГДА ИСТИНА
		|			КОГДА ДанныеПозиций.РайонныйКоэффициентРазмерМакс <> ШтатноеРасписание.РайонныйКоэффициентРазмерМакс
		|				ТОГДА ИСТИНА
		|			КОГДА ДанныеПозиций.СевернаяНадбавкаРазмер <> ШтатноеРасписание.СевернаяНадбавкаРазмер
		|				ТОГДА ИСТИНА
		|			КОГДА ДанныеПозиций.СевернаяНадбавкаРазмерМин <> ШтатноеРасписание.СевернаяНадбавкаРазмерМин
		|				ТОГДА ИСТИНА
		|			КОГДА ДанныеПозиций.СевернаяНадбавкаРазмерМакс <> ШтатноеРасписание.СевернаяНадбавкаРазмерМакс
		|				ТОГДА ИСТИНА
		|			КОГДА ДанныеПозиций.ОкладТариф <> ШтатноеРасписание.ОкладТариф
		|				ТОГДА ИСТИНА
		|			КОГДА ДанныеПозиций.ОкладТарифМин <> ШтатноеРасписание.ОкладТарифМин
		|				ТОГДА ИСТИНА
		|			КОГДА ДанныеПозиций.ОкладТарифМакс <> ШтатноеРасписание.ОкладТарифМакс
		|				ТОГДА ИСТИНА
		|			КОГДА ДанныеПозиций.СпособОтраженияЗарплатыВБухучете <> ШтатноеРасписание.СпособОтраженияЗарплатыВБухучете
		|				ТОГДА ИСТИНА
		|			КОГДА ДанныеПозиций.ОтношениеКЕНВД <> ШтатноеРасписание.ОтношениеКЕНВД
		|				ТОГДА ИСТИНА
		|			КОГДА ДанныеПозиций.СтатьяФинансирования <> ШтатноеРасписание.СтатьяФинансирования
		|				ТОГДА ИСТИНА
		|			КОГДА ДанныеПозиций.НадбавкаЗаВредностьРазмер <> ШтатноеРасписание.НадбавкаЗаВредностьРазмер
		|				ТОГДА ИСТИНА
		|			КОГДА ДанныеПозиций.НадбавкаЗаВредностьРазмерМин <> ШтатноеРасписание.НадбавкаЗаВредностьРазмерМин
		|				ТОГДА ИСТИНА
		|			КОГДА ДанныеПозиций.НадбавкаЗаВредностьРазмерМакс <> ШтатноеРасписание.НадбавкаЗаВредностьРазмерМакс
		|				ТОГДА ИСТИНА
		|			КОГДА ДанныеПозиций.ВзимаютсяВзносыЗаЗанятыхНаРаботахСДосрочнойПенсией <> ШтатноеРасписание.ВзимаютсяВзносыЗаЗанятыхНаРаботахСДосрочнойПенсией
		|				ТОГДА ИСТИНА
		|			КОГДА ДанныеПозиций.ОснованиеДосрочногоНазначенияПенсии <> ШтатноеРасписание.ОснованиеДосрочногоНазначенияПенсии
		|				ТОГДА ИСТИНА
		|			КОГДА ДанныеПозиций.ОсобыеУсловияТрудаПФР <> ШтатноеРасписание.ОсобыеУсловияТрудаПФР
		|				ТОГДА ИСТИНА
		|			КОГДА ДанныеПозиций.КодПозицииСпискаПФР <> ШтатноеРасписание.КодПозицииСпискаПФР
		|				ТОГДА ИСТИНА
		|			КОГДА ДанныеПозиций.ВыплачиваетсяНадбавкаЗаВредность <> ШтатноеРасписание.ВыплачиваетсяНадбавкаЗаВредность
		|				ТОГДА ИСТИНА
		|			КОГДА ДанныеПозиций.ПроцентНадбавкиЗаВредность <> ШтатноеРасписание.ПроцентНадбавкиЗаВредность
		|				ТОГДА ИСТИНА
		|			КОГДА (ВЫРАЗИТЬ(ДанныеПозиций.УсловияПриема КАК СТРОКА(1024))) <> (ВЫРАЗИТЬ(ШтатноеРасписание.УсловияПриема КАК СТРОКА(1024)))
		|				ТОГДА ИСТИНА
		|			КОГДА ДанныеПозиций.ХарактерВыполняемыхРаботПФР <> ШтатноеРасписание.ХарактерВыполняемыхРаботПФР
		|				ТОГДА ИСТИНА
		|			КОГДА ДанныеПозиций.ПервичныеДокументыПФР <> ШтатноеРасписание.ПервичныеДокументыПФР
		|				ТОГДА ИСТИНА
		|			КОГДА ДанныеПозиций.ВидСтажаЛетныхЭкипажей <> ШтатноеРасписание.ВидСтажаЛетныхЭкипажей
		|				ТОГДА ИСТИНА
		|			КОГДА ДанныеПозиций.ВидСтажаШахтеров <> ШтатноеРасписание.ВидСтажаШахтеров
		|				ТОГДА ИСТИНА
		|			КОГДА ДанныеПозиций.ТрудоваяФункция <> ШтатноеРасписание.ТрудоваяФункция
		|				ТОГДА ИСТИНА
		|			КОГДА ДанныеПозиций.ФОТУправленческий <> ШтатноеРасписание.ФОТУправленческий
		|				ТОГДА ИСТИНА
		|			КОГДА ДанныеПозиций.ФОТУправленческийМин <> ШтатноеРасписание.ФОТУправленческийМин
		|				ТОГДА ИСТИНА
		|			КОГДА ДанныеПозиций.ФОТУправленческийМакс <> ШтатноеРасписание.ФОТУправленческийМакс
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Попытка 
				ПозицияОбъект = Выборка.ПозицияШтатногоРасписания.ПолучитьОбъект();
				ПозицияОбъект.Заблокировать();
			Исключение
				ТекстИсключенияЗаписи = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось изменить позицию штатного расписания ""%1"".
				|Возможно, данные позиции редактируется другим пользователем';
				|en = 'Cannot change the ""%1"" headcount position.
				|Maybe, these positions are being edited by another user'"),
				ПозицияОбъект.Наименование);
				ВызватьИсключение ТекстИсключенияЗаписи;
			КонецПопытки;
			
			ЗаполнитьЗначенияСвойств(ПозицияОбъект, Выборка);
			Если ПозицияОбъект.ПометкаУдаления И Не ПозицияОбъект.Закрыта Тогда
				ПозицияОбъект.ПометкаУдаления = Ложь;
			КонецЕсли;
			
			УправлениеШтатнымРасписанием.ОтключитьОбновлениеСтруктурыШтатногоРасписания(ПозицияОбъект);
			УправлениеШтатнымРасписанием.ОтключитьОбновлениеНастройкиИспользованияСтраховыхВзносовПоКлассамУсловийТруда(ПозицияОбъект);
			УправлениеШтатнымРасписанием.ОтключитьОбновлениеНастройкиПубликации(ПозицияОбъект);
			ПозицияОбъект.Записать();
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПроверитьВозможностьВнесенияИзменений(ДокументСсылка, ИзменяемыеПозиции = Неопределено, ДатаИзменений = '00010101') Экспорт
	
	РезультатПроверки = Новый Структура("ИзмененияВозможны,РегистраторПредставление,Регистратор,Позиция,ДатаИзменений", Истина, "");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Регистратор", ДокументСсылка);
	
	Если ИзменяемыеПозиции <> Неопределено Тогда
		
		Запрос.УстановитьПараметр("ИзменяемыеПозиции", ИзменяемыеПозиции);
		Запрос.УстановитьПараметр("ДатаИзменений", ДатаИзменений);
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ШтатноеРасписание.Ссылка КАК ПозицияШтатногоРасписания,
			|	&ДатаИзменений КАК Дата
			|ПОМЕСТИТЬ ВТЗаписываемыеСведения
			|ИЗ
			|	Справочник.ШтатноеРасписание КАК ШтатноеРасписание
			|ГДЕ
			|	ШтатноеРасписание.Ссылка В(&ИзменяемыеПозиции)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ИсторияИспользованияШтатногоРасписания.ПозицияШтатногоРасписания КАК ПозицияШтатногоРасписания,
			|	ИсторияИспользованияШтатногоРасписания.Дата КАК Дата
			|ПОМЕСТИТЬ ВТПрежниеСведения
			|ИЗ
			|	РегистрСведений.ИсторияИспользованияШтатногоРасписания КАК ИсторияИспользованияШтатногоРасписания
			|ГДЕ
			|	ИсторияИспользованияШтатногоРасписания.Регистратор = &Регистратор
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ЕСТЬNULL(ПрежниеСведения.ПозицияШтатногоРасписания, ЗаписываемыеСведения.ПозицияШтатногоРасписания) КАК ПозицияШтатногоРасписания,
			|	ВЫБОР
			|		КОГДА ЕСТЬNULL(ПрежниеСведения.Дата, ЗаписываемыеСведения.Дата) < ЕСТЬNULL(ЗаписываемыеСведения.Дата, ПрежниеСведения.Дата)
			|			ТОГДА ЕСТЬNULL(ПрежниеСведения.Дата, ЗаписываемыеСведения.Дата)
			|		ИНАЧЕ ЕСТЬNULL(ЗаписываемыеСведения.Дата, ПрежниеСведения.Дата)
			|	КОНЕЦ КАК Дата
			|ПОМЕСТИТЬ ВТПозицииДокумента
			|ИЗ
			|	ВТПрежниеСведения КАК ПрежниеСведения
			|		ПОЛНОЕ СОЕДИНЕНИЕ ВТЗаписываемыеСведения КАК ЗаписываемыеСведения
			|		ПО ПрежниеСведения.ПозицияШтатногоРасписания = ЗаписываемыеСведения.ПозицияШтатногоРасписания";
			
	Иначе
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ИсторияИспользованияШтатногоРасписания.ПозицияШтатногоРасписания,
			|	ИсторияИспользованияШтатногоРасписания.Дата
			|ПОМЕСТИТЬ ВТПозицииДокумента
			|ИЗ
			|	РегистрСведений.ИсторияИспользованияШтатногоРасписания КАК ИсторияИспользованияШтатногоРасписания
			|ГДЕ
			|	ИсторияИспользованияШтатногоРасписания.Регистратор = &Регистратор";
		
	КонецЕсли;
		
	Запрос.Выполнить();	
		
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПозицииДокумента.ПозицияШтатногоРасписания,
		|	ПозицииДокумента.Дата,
		|	МИНИМУМ(ИсторияИспользованияШтатногоРасписания.Дата) КАК ДатаПоследующихИзменений
		|ПОМЕСТИТЬ ВТДатыПоследующихИзменений
		|ИЗ
		|	ВТПозицииДокумента КАК ПозицииДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияИспользованияШтатногоРасписания КАК ИсторияИспользованияШтатногоРасписания
		|		ПО ПозицииДокумента.ПозицияШтатногоРасписания = ИсторияИспользованияШтатногоРасписания.ПозицияШтатногоРасписания
		|			И ПозицииДокумента.Дата <= ИсторияИспользованияШтатногоРасписания.Дата
		|			И (ИсторияИспользованияШтатногоРасписания.Регистратор <> &Регистратор)
		|
		|СГРУППИРОВАТЬ ПО
		|	ПозицииДокумента.ПозицияШтатногоРасписания,
		|	ПозицииДокумента.Дата
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ДатыПоследующихИзменений.ПозицияШтатногоРасписания КАК Позиция,
		|	ДатыПоследующихИзменений.Дата,
		|	ДатыПоследующихИзменений.ДатаПоследующихИзменений КАК ДатаИзменений,
		|	ИсторияИспользованияШтатногоРасписания.Регистратор КАК Регистратор,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ИсторияИспользованияШтатногоРасписания.Регистратор) КАК РегистраторПредставление
		|ИЗ
		|	ВТДатыПоследующихИзменений КАК ДатыПоследующихИзменений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияИспользованияШтатногоРасписания КАК ИсторияИспользованияШтатногоРасписания
		|		ПО ДатыПоследующихИзменений.ПозицияШтатногоРасписания = ИсторияИспользованияШтатногоРасписания.ПозицияШтатногоРасписания
		|			И ДатыПоследующихИзменений.ДатаПоследующихИзменений = ИсторияИспользованияШтатногоРасписания.Дата
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаИзменений,
		|	Позиция";
		
	УстановитьПривилегированныйРежим(Истина);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(РезультатПроверки, Выборка);
		
		РезультатПроверки.ИзмененияВозможны = Ложь;
		
	КонецЕсли; 
	
	Возврат РезультатПроверки;
	
КонецФункции

Процедура ОбновитьСведенияПодразделенийПоСпискуПозиций(СписокПозицийШтатногоРасписания) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокПозицийШтатногоРасписания", СписокПозицийШтатногоРасписания);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ШтатноеРасписание.Подразделение КАК Подразделение
		|ИЗ
		|	Справочник.ШтатноеРасписание КАК ШтатноеРасписание
		|ГДЕ
		|	ШтатноеРасписание.Ссылка В(&СписокПозицийШтатногоРасписания)";
	
	ОбновляемыеПодразделения = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Подразделение");
	
	УправлениеШтатнымРасписанием.ОбновитьСведенияПодразделений(ОбновляемыеПодразделения);
	
КонецПроцедуры

Процедура ПодготовитьОбновлениеЗависимыхДанныхПриОбменеПередЗаписью(Объект) Экспорт
	ЗарплатаКадрыПериодическиеРегистры.КонтрольИзмененийПередЗаписью(Объект);
КонецПроцедуры

Процедура ПодготовитьОбновлениеЗависимыхДанныхПриОбменеПриЗаписи(Объект) Экспорт
	
	ЗарплатаКадрыПериодическиеРегистры.КонтрольИзмененийПриЗаписи(Объект);
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.КадровыйУчет.АналитикаДанных") Тогда
		МодульКадровыйУчетАналитикаДанных = ОбщегоНазначения.ОбщийМодуль("КадровыйУчетАналитикаДанных");
		МодульКадровыйУчетАналитикаДанных.ЗаполнитьТаблицуОбновленияИсторииИспользованияШтатногоРасписания(Объект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли