
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДанныеОбъекта = РеквизитФормыВЗначение("Запись");
	
	Элементы.РезультатПолученияСообщений.Видимость = Ложь;
	ДанныеПолученныхПакетноСообщений = ДанныеОбъекта.ДанныеПолученныхПакетноСообщений.Получить();
	Если НЕ ДанныеПолученныхПакетноСообщений = Неопределено Тогда
		Элементы.РезультатПолученияСообщений.Видимость = Истина;
		РезультатПолученияСообщений.Загрузить(ДанныеПолученныхПакетноСообщений);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаСервере
Процедура ГруппаСтраницыПриСменеСтраницыНаСервере(ТекущаяСтраница)
	
	ДанныеОбъекта = РеквизитФормыВЗначение("Запись");
	
	Если ТекущаяСтраница = "Запрос" Тогда
		Если НЕ ЗапросЗагружен Тогда
			Запрос			= ДанныеОбъекта.ДанныеЗапроса.Получить();
			ЗапросЗагружен	= Истина;
		КонецЕсли;
	ИначеЕсли ТекущаяСтраница = "Ответ" Тогда
		Если НЕ ОтветЗагружен Тогда
			ОтветСервера	= ДанныеОбъекта.ОтветСервера.Получить();
			ОтветЗагружен	= Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппаСтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	ГруппаСтраницыПриСменеСтраницыНаСервере(ТекущаяСтраница.Имя);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СохранитьЗапрос(Команда)
	
	Адрес = АдресЗапроса();
	ФайловаяСистемаКлиент.СохранитьФайл(, Адрес, "ЗапросСЭДО.xml");
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьОтвет(Команда)
	
	Адрес = АдресОтвета();
	ФайловаяСистемаКлиент.СохранитьФайл(, Адрес, "ОтветСЭДО.xml");

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция АдресЗапроса()
	
	ДанныеОбъекта = РеквизитФормыВЗначение("Запись");
	ДанныеЗапроса = ДанныеОбъекта.ДанныеЗапроса.Получить();
	Возврат ПоместитьВоВременноеХранилище(ДанныеЗапроса, Новый УникальныйИдентификатор);

КонецФункции

&НаСервере
Функция АдресОтвета()
	
	ДанныеОбъекта = РеквизитФормыВЗначение("Запись");
	ОтветСервера = ДанныеОбъекта.ОтветСервера.Получить();
	Возврат ПоместитьВоВременноеХранилище(ОтветСервера, Новый УникальныйИдентификатор);

КонецФункции

#КонецОбласти
