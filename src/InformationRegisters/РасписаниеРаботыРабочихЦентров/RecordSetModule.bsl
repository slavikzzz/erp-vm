//++ Устарело_Производство21
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Блокировки
	Блокировка = Новый БлокировкаДанных;
	
	// Блокировка маршрутного листа для чтения
	ЭлементБлокировки = Блокировка.Добавить("Документ.МаршрутныйЛистПроизводства");
	
	ЭлементБлокировки.УстановитьЗначение("Ссылка", Отбор.МаршрутныйЛист.Значение);
	
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
	
	// Блокировка доступности видов рабочих центров для чтения
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ВидыРабочихЦентров.ВидРабочегоЦентра КАК ВидРабочегоЦентра,
	|	ВидыРабочихЦентров.ДатаИнтервала     КАК ДатаИнтервала
	|ИЗ
	|	Документ.МаршрутныйЛистПроизводства.ВидыРабочихЦентров КАК ВидыРабочихЦентров
	|ГДЕ
	|	ВидыРабочихЦентров.Ссылка = &МаршрутныйЛист");
	
	Запрос.УстановитьПараметр("МаршрутныйЛист", Отбор.МаршрутныйЛист.Значение);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ДоступностьВидовРабочихЦентров");
	
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ВидРабочегоЦентра", "ВидРабочегоЦентра");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ДатаИнтервала",     "ДатаИнтервала");
	
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
	
	// Блокировка доступности видов рабочих центров для записи
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ДоступностьВидовРабочихЦентров.НаборЗаписей");
	
	ЭлементБлокировки.УстановитьЗначение("Регистратор", Отбор.МаршрутныйЛист.Значение);
	
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	Блокировка.Заблокировать();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		
		Возврат;
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	МАКСИМУМ(МаршрутныйЛист.Дата)                                 КАК Период,
	|	МаршрутныйЛист.Статус                                         КАК Статус,
	|	МаршрутныйЛист.Распоряжение                                   КАК Распоряжение,
	|	ДоступностьВидовРабочихЦентров.ВидРабочегоЦентра              КАК ВидРабочегоЦентра,
	|	ДоступностьВидовРабочихЦентров.ДатаИнтервала                  КАК ДатаИнтервала,
	|	СУММА(ДоступностьВидовРабочихЦентров.Занято)                  КАК Занято,
	|	МАКСИМУМ(ДоступностьВидовРабочихЦентров.КодСтрокиПродукция)   КАК КодСтрокиПродукция,
	|	МАКСИМУМ(ДоступностьВидовРабочихЦентров.КодСтрокиЭтапыГрафик) КАК КодСтрокиЭтапыГрафик,
	|	МАКСИМУМ(ДоступностьВидовРабочихЦентров.ВариантНаладки)       КАК ВариантНаладки
	|
	|ПОМЕСТИТЬ ВТЗанятоЗаказом
	|
	|ИЗ
	|	Документ.МаршрутныйЛистПроизводства КАК МаршрутныйЛист
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ДоступностьВидовРабочихЦентров КАК ДоступностьВидовРабочихЦентров
	|		ПО ДоступностьВидовРабочихЦентров.Активность
	|			И (НЕ ДоступностьВидовРабочихЦентров.ЭтоДвижениеВводаДоступности)
	|			И (ДоступностьВидовРабочихЦентров.Регистратор ССЫЛКА Документ.ЗаказНаПроизводство
	|					И ДоступностьВидовРабочихЦентров.Регистратор = МаршрутныйЛист.Распоряжение
	|					И ДоступностьВидовРабочихЦентров.Занято > 0
	|				ИЛИ ДоступностьВидовРабочихЦентров.Регистратор ССЫЛКА Документ.МаршрутныйЛистПроизводства
	|					И ДоступностьВидовРабочихЦентров.Регистратор <> &МаршрутныйЛист
	|					И ВЫРАЗИТЬ(ДоступностьВидовРабочихЦентров.Регистратор КАК Документ.МаршрутныйЛистПроизводства).Распоряжение = МаршрутныйЛист.Распоряжение
	|					И ДоступностьВидовРабочихЦентров.Занято < 0)
	|			И (ДоступностьВидовРабочихЦентров.КодСтрокиПродукция = МаршрутныйЛист.КодСтроки)
	|			И (ДоступностьВидовРабочихЦентров.КодСтрокиЭтапыГрафик = МаршрутныйЛист.КодСтрокиЭтапыГрафик)
	|			И ((ДоступностьВидовРабочихЦентров.ВидРабочегоЦентра, ДоступностьВидовРабочихЦентров.ДатаИнтервала) В
	|				(ВЫБРАТЬ
	|					ВидыРабочихЦентров.ВидРабочегоЦентра КАК ВидРабочегоЦентра,
	|					ВидыРабочихЦентров.ДатаИнтервала КАК ДатаИнтервала
	|				ИЗ
	|					Документ.МаршрутныйЛистПроизводства.ЗанятостьВидовРабочихЦентровПоГрафику КАК ВидыРабочихЦентров
	|				ГДЕ
	|					ВидыРабочихЦентров.Ссылка = &МаршрутныйЛист))
	|
	|ГДЕ
	|	МаршрутныйЛист.Ссылка = &МаршрутныйЛист
	|	И МаршрутныйЛист.Проведен
	|	И (МаршрутныйЛист.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыМаршрутныхЛистовПроизводства.Выполнен)
	|			ИЛИ МаршрутныйЛист.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыМаршрутныхЛистовПроизводства.Создан)
	|				И 1 В
	|					(ВЫБРАТЬ ПЕРВЫЕ 1
	|						1
	|					ИЗ
	|						РегистрСведений.РасписаниеРаботыРабочихЦентров КАК РасписаниеРаботыРабочихЦентров
	|					ГДЕ
	|						РасписаниеРаботыРабочихЦентров.МаршрутныйЛист = &МаршрутныйЛист))
	|
	|СГРУППИРОВАТЬ ПО
	|	МаршрутныйЛист.Распоряжение,
	|	МаршрутныйЛист.Статус,
	|	ДоступностьВидовРабочихЦентров.ВидРабочегоЦентра,
	|	ДоступностьВидовРабочихЦентров.ДатаИнтервала
	|
	|ИМЕЮЩИЕ
	|	СУММА(ДоступностьВидовРабочихЦентров.Занято) > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВидРабочегоЦентра,
	|	ДатаИнтервала
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ЗанятоЗаказом.Период)               КАК Период,
	|	ЗанятоЗаказом.ВидРабочегоЦентра              КАК ВидРабочегоЦентра,
	|	ЗанятоЗаказом.ДатаИнтервала                  КАК ДатаИнтервала,
	|	ВЫБОР 
	|		КОГДА МАКСИМУМ(ЕСТЬNULL(ЗанятоМаршрутнымЛистом.Занято, 0)) - СУММА(ЕСТЬNULL(ЗанятоЗаказомПредыдущиеИнтервалы.Занято, 0)) > ЗанятоЗаказом.Занято 
	|			ТОГДА -ЗанятоЗаказом.Занято
	|		ИНАЧЕ -(МАКСИМУМ(ЕСТЬNULL(ЗанятоМаршрутнымЛистом.Занято, 0)) - СУММА(ЕСТЬNULL(ЗанятоЗаказомПредыдущиеИнтервалы.Занято, 0)))
	|	КОНЕЦ                                        КАК Занято,
	|	ЗанятоЗаказом.Распоряжение                   КАК Распоряжение,
	|	МАКСИМУМ(ЗанятоЗаказом.КодСтрокиПродукция)   КАК КодСтрокиПродукция,
	|	МАКСИМУМ(ЗанятоЗаказом.КодСтрокиЭтапыГрафик) КАК КодСтрокиЭтапыГрафик,
	|	МАКСИМУМ(ЗанятоЗаказом.ВариантНаладки)       КАК ВариантНаладки
	|ИЗ
	|	ВТЗанятоЗаказом КАК ЗанятоЗаказом
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЗанятоЗаказом КАК ЗанятоЗаказомПредыдущиеИнтервалы
	|		ПО ЗанятоЗаказом.ВидРабочегоЦентра = ЗанятоЗаказомПредыдущиеИнтервалы.ВидРабочегоЦентра
	|			И ЗанятоЗаказом.ДатаИнтервала > ЗанятоЗаказомПредыдущиеИнтервалы.ДатаИнтервала
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ВидыРабочихЦентров.ВидРабочегоЦентра  КАК ВидРабочегоЦентра,
	|			ВидыРабочихЦентров.ВариантНаладки     КАК ВариантНаладки,
	|			СУММА(ВидыРабочихЦентров.ВремяРаботы) КАК Занято
	|		ИЗ
	|			Документ.МаршрутныйЛистПроизводства.ВидыРабочихЦентров КАК ВидыРабочихЦентров
	|
	|		ГДЕ
	|			ВидыРабочихЦентров.Ссылка = &МаршрутныйЛист
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ВидыРабочихЦентров.ВидРабочегоЦентра,
	|			ВидыРабочихЦентров.ВариантНаладки) КАК ЗанятоМаршрутнымЛистом
	|		ПО (ЗанятоМаршрутнымЛистом.ВидРабочегоЦентра = ЗанятоЗаказом.ВидРабочегоЦентра)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗанятоЗаказом.Распоряжение,
	|	ЗанятоЗаказом.Статус,
	|	ЗанятоЗаказом.ВидРабочегоЦентра,
	|	ЗанятоЗаказом.ДатаИнтервала,
	|	ЗанятоЗаказом.Занято
	|
	|ИМЕЮЩИЕ
	|	ВЫБОР КОГДА МАКСИМУМ(ЕСТЬNULL(ЗанятоМаршрутнымЛистом.Занято, 0)) - СУММА(ЕСТЬNULL(ЗанятоЗаказомПредыдущиеИнтервалы.Занято, 0))
	|		> ЗанятоЗаказом.Занято ТОГДА
	|		ЗанятоЗаказом.Занято
	|	ИНАЧЕ
	|		МАКСИМУМ(ЕСТЬNULL(ЗанятоМаршрутнымЛистом.Занято, 0)) - СУММА(ЕСТЬNULL(ЗанятоЗаказомПредыдущиеИнтервалы.Занято, 0))
	|	КОНЕЦ > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	МаршрутныйЛистПроизводства.Дата                               КАК Период,
	|	РасписаниеРаботыРабочихЦентров.РабочийЦентр.ВидРабочегоЦентра КАК ВидРабочегоЦентра,
	|	РасписаниеРаботыРабочихЦентров.ДатаИнтервала                  КАК ДатаИнтервала,
	|	РасписаниеРаботыРабочихЦентров.ВремяРаботыВРабочееВремя       КАК Занято,
	|	МаршрутныйЛистПроизводства.Распоряжение                       КАК Распоряжение,
	|	МаршрутныйЛистПроизводства.КодСтроки                          КАК КодСтрокиПродукция,
	|	МаршрутныйЛистПроизводства.КодСтрокиЭтапыГрафик               КАК КодСтрокиЭтапыГрафик,
	|	РасписаниеРаботыРабочихЦентров.ВариантНаладки                 КАК ВариантНаладки
	|ИЗ
	|	РегистрСведений.РасписаниеРаботыРабочихЦентров КАК РасписаниеРаботыРабочихЦентров
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛистПроизводства КАК МаршрутныйЛистПроизводства
	|	ПО МаршрутныйЛистПроизводства.Ссылка = РасписаниеРаботыРабочихЦентров.МаршрутныйЛист
	|		И МаршрутныйЛистПроизводства.Проведен
	|
	|ГДЕ
	|	РасписаниеРаботыРабочихЦентров.МаршрутныйЛист = &МаршрутныйЛист
	|	И РасписаниеРаботыРабочихЦентров.ВремяРаботыВРабочееВремя > 0
	|	И МаршрутныйЛистПроизводства.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыМаршрутныхЛистовПроизводства.КВыполнению),
	|											ЗНАЧЕНИЕ(Перечисление.СтатусыМаршрутныхЛистовПроизводства.Выполняется))");
	
	Запрос.УстановитьПараметр("МаршрутныйЛист", Отбор.МаршрутныйЛист.Значение);
	
	НаборЗаписей = РегистрыНакопления.ДоступностьВидовРабочихЦентров.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(Отбор.МаршрутныйЛист.Значение);
	
	НаборЗаписей.Загрузить(Запрос.Выполнить().Выгрузить());
	
	НаборЗаписей.Записать(Истина);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
//-- Устарело_Производство21