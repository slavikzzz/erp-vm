#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

Процедура ЗаполнитьОтчетыВБанкиОтложенно(Параметры) Экспорт

	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1000
	|	ДокРегламентированныйОтчет.Организация КАК Организация,
	|	ДокРегламентированныйОтчет.Ссылка КАК Предмет,
	|	ДокРегламентированныйОтчет.Банк КАК Банк,
	|	ДокРегламентированныйОтчет.Дата КАК ДатаЗаписиПредмета,
	|	ДокРегламентированныйОтчет.ДатаОтправки КАК ДатаОтправки,
	|	ДокРегламентированныйОтчет.ДатаНачала КАК ДатаНачала,
	|	ДокРегламентированныйОтчет.ДатаОкончания КАК ДатаОкончания,
	|	ДокРегламентированныйОтчет.Комментарий КАК Комментарий,
	|	ДокРегламентированныйОтчет.ПометкаУдаления КАК ПометкаУдаления,
	|	ДокРегламентированныйОтчет.СтатусОтчета КАК СтатусОтчета
	|ИЗ
	|	Документ.РегламентированныйОтчет КАК ДокРегламентированныйОтчет
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЖурналСтатусовФинОтчетностиВБанки КАК ЖурналСтатусовФинОтчетностиВБанки
	|		ПО ДокРегламентированныйОтчет.Организация = ЖурналСтатусовФинОтчетностиВБанки.Организация
	|			И ДокРегламентированныйОтчет.Ссылка = ЖурналСтатусовФинОтчетностиВБанки.Предмет
	|ГДЕ
	|	ДокРегламентированныйОтчет.ИсточникОтчета = ""БухгалтерскаяОтчетностьВБанк""
	|	И ЖурналСтатусовФинОтчетностиВБанки.Предмет ЕСТЬ NULL
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаОкончания,
	|	Предмет";
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	Если РезультатЗапроса.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = Ложь;
	
	ПроблемныхОбъектов = 0;
	ОбъектовОбработано = 0;
	
	Для каждого СтрокаРезультата Из РезультатЗапроса Цикл
		
		Попытка
			
			// Записываем через набор записей.
			НаборЗаписей = СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Организация.Установить(СтрокаРезультата.Организация);
			НаборЗаписей.Отбор.Предмет.Установить(СтрокаРезультата.Предмет);
			
			Запись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, СтрокаРезультата);		
			Запись.Наименование = ФинОтчетностьВБанки.НаименованиеОтчетностиВСбербанк();
			Запись.Статус       = ФинОтчетностьВБанки.СтатусОтчетностиВБанкиВСтатусФинОтчетности(СтрокаРезультата.СтатусОтчета);
			Запись.СостояниеСдачиОтчетности = ФинОтчетностьВБанки.ОпределитьСостояниеСдачиОтчетности(Запись.Статус);
			
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
			
			ОбъектовОбработано = ОбъектовОбработано + 1;
			
		Исключение
			// Если не удалось обработать какой-либо элемент справочника, повторяем попытку снова.
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось обработать отчет: %1 по причине:
					|%2';
					|en = 'Cannot process report %1. Reason:
					|%2'"),
					СтрокаРезультата.Предмет, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.РегистрыСведений.ЖурналСтатусовФинОтчетностиВБанки, СтрокаРезультата.Предмет, ТекстСообщения);
		КонецПопытки;
		
	КонецЦикла;
	
	Если ОбъектовОбработано = 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Процедуре ЗаполнитьОтчетыВБанкиОтложенно
				|не удалось обработать некоторые отчеты (пропущены): %1';
				|en = 'The ЗаполнитьОтчетыВБанкиОтложенно procedure
				|cannot process some reports (skipped): %1'"),
				ПроблемныхОбъектов);
		ВызватьИсключение ТекстСообщения;
	Иначе
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Информация,
			Метаданные.Справочники.НомераГТД,,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Процедура ЗаполнитьОтчетыВБанкиОтложенно
					|обработала очередную порцию отчетов в регистре ЖурналСтатусовФинОтчетностиВБанки: %1';
					|en = 'The ЗаполнитьОтчетыВБанкиОтложенно procedure
					|processed the following batch of reports in the ЖурналСтатусовФинОтчетностиВБанки register: %1'"), ОбъектовОбработано));
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьФинансовыйПериодОтложенно(Параметры) Экспорт

	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1000
	|	ЖурналСтатусовФинОтчетностиВБанки.Организация КАК Организация,
	|	ЖурналСтатусовФинОтчетностиВБанки.Предмет КАК Предмет,
	|	ЖурналСтатусовФинОтчетностиВБанки.ДатаЗаписиПредмета КАК ДатаЗаписиПредмета
	|ИЗ
	|	РегистрСведений.ЖурналСтатусовФинОтчетностиВБанки КАК ЖурналСтатусовФинОтчетностиВБанки
	|ГДЕ
	|	(ЖурналСтатусовФинОтчетностиВБанки.ФинансовыйПериод = """"
	|			ИЛИ ЖурналСтатусовФинОтчетностиВБанки.ДатаНачала = ДАТАВРЕМЯ(1, 1, 1)
	|				И ЖурналСтатусовФинОтчетностиВБанки.ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1))
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаЗаписиПредмета УБЫВ,
	|	Предмет";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = Ложь;
	
	ПроблемныхОбъектов = 0;
	ОбъектовОбработано = 0;
	
	СтрокаРезультата = РезультатЗапроса.Выбрать();
	
	Пока СтрокаРезультата.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			НаборЗаписей = СоздатьНаборЗаписей();
			
			НаборЗаписей.Отбор.Организация.Установить(СтрокаРезультата.Организация);
			НаборЗаписей.Отбор.Предмет.Установить(СтрокаРезультата.Предмет);
			
			НаборЗаписей.Прочитать();
			
			Для каждого Запись Из НаборЗаписей Цикл
				
				Если Не ЗначениеЗаполнено(Запись.ДатаНачала) 
					И ЗначениеЗаполнено(Запись.ДатаОкончания) Тогда
					
					Запись.ДатаНачала = НачалоМесяца(Запись.ДатаОкончания);
					
				КонецЕсли;
				
				Запись.ФинансовыйПериод = РегламентированнаяОтчетностьВызовСервера.ПредставлениеФинансовогоПериода(Запись.ДатаНачала, Запись.ДатаОкончания);
				
			КонецЦикла;
			
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
			
			ОбъектовОбработано = ОбъектовОбработано + 1;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось обработать отчет: %1 по причине:
					|%2';
					|en = 'Cannot process report %1. Reason:
					|%2'"),
					СтрокаРезультата.Предмет, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.РегистрыСведений.ЖурналСтатусовФинОтчетностиВБанки, СтрокаРезультата.Предмет, ТекстСообщения);
		КонецПопытки;
		
	КонецЦикла;
	
	Если ОбъектовОбработано = 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Процедуре ЗаполнитьФинансовыйПериодОтложенно
				|не удалось обработать некоторые отчеты (пропущены): %1';
				|en = 'The ЗаполнитьФинансовыйПериодОтложенно procedure
				|cannot process some reports (skipped): %1'"),
				ПроблемныхОбъектов);
		ВызватьИсключение ТекстСообщения;
	Иначе
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Информация,
			Метаданные.РегистрыСведений.ЖурналСтатусовФинОтчетностиВБанки,,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Процедура ЗаполнитьФинансовыйПериодОтложенно
					|обработала очередную порцию отчетов в регистре ЖурналСтатусовФинОтчетностиВБанки: %1';
					|en = 'The ЗаполнитьФинансовыйПериодОтложенно procedure
					|processed the following batch of reports in the ЖурналСтатусовФинОтчетностиВБанки register: %1'"), ОбъектовОбработано));
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли