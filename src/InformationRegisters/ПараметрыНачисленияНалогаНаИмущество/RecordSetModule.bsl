#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаПередЗаписью.Период,
	|	ТаблицаПередЗаписью.Организация,
	|	ТаблицаПередЗаписью.ОсновноеСредство,
	|	ТаблицаПередЗаписью.НалоговыйОрган,
	|	ТаблицаПередЗаписью.НалоговаяСтавка,
	|	ТаблицаПередЗаписью.СниженнаяНалоговаяСтавка,
	|	ТаблицаПередЗаписью.ДоляСтоимостиЧислитель,
	|	ТаблицаПередЗаписью.ДоляСтоимостиЗнаменатель,
	|	ТаблицаПередЗаписью.НалоговаяБаза,
	|	ТаблицаПередЗаписью.КадастроваяСтоимость,
	|	ТаблицаПередЗаписью.НеоблагаемаяКадастроваяСтоимость,
	|	ТаблицаПередЗаписью.ДатаРегистрацииПраваСобственности,
	|	ТаблицаПередЗаписью.ДатаПрекращенияПраваСобственности,
	|	ТаблицаПередЗаписью.ИспользуетсяТолькоВДеятельностиОблагаемойЕНВД,
	|	ТаблицаПередЗаписью.ДвижимоеИмуществоПодлежащееНалогообложению,
	|	ТаблицаПередЗаписью.КодВидаИмущества,
	|	ТаблицаПередЗаписью.ДоляВПравеОбщейСобственностиЧислитель,
	|	ТаблицаПередЗаписью.ДоляВПравеОбщейСобственностиЗнаменатель,
	|	ТаблицаПередЗаписью.УсловныйНомер,
	|	ТаблицаПередЗаписью.КБК,
	|	ТаблицаПередЗаписью.ДоляПлощадиЧислитель,
	|	ТаблицаПередЗаписью.ДоляПлощадиЗнаменатель,
	|	ТаблицаПередЗаписью.ОснованиеЛьготы,
	|	ТаблицаПередЗаписью.НачалоДействияЛьготы,
	|	ТаблицаПередЗаписью.ОкончаниеДействияЛьготы,
	|	ТаблицаПередЗаписью.НеПодлежитНалогообложению,
	|	ТаблицаПередЗаписью.СуммаУменьшения
	|
	|ПОМЕСТИТЬ ПараметрыНачисленияНалогаНаИмуществоПередЗаписью
	|
	|ИЗ
	|	РегистрСведений.ПараметрыНачисленияНалогаНаИмущество КАК ТаблицаПередЗаписью
	|
	|ГДЕ
	|	ТаблицаПередЗаписью.Регистратор = &Регистратор
	|	И ТаблицаПередЗаписью.ДатаИсправления = ДАТАВРЕМЯ(1,1,1)";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Или Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Период           КАК Период,
	|	Таблица.Организация      КАК Организация,
	|	Таблица.ОсновноеСредство КАК ОсновноеСредство,
	|	ИСТИНА                   КАК ОтражатьВРеглУчете,
	|	ЛОЖЬ                     КАК ОтражатьВУпрУчете,
	|	&Регистратор             КАК Документ
	|ПОМЕСТИТЬ ПараметрыНачисленияНалогаНаИмуществоИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаПередЗаписью.Период            КАК Период,
	|		ТаблицаПередЗаписью.Организация       КАК Организация,
	|		ТаблицаПередЗаписью.ОсновноеСредство  КАК ОсновноеСредство
	|	ИЗ
	|		ПараметрыНачисленияНалогаНаИмуществоПередЗаписью КАК ТаблицаПередЗаписью
	|			
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыНачисленияНалогаНаИмущество КАК ТаблицаПриЗаписи
	|			ПО ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|				И ТаблицаПриЗаписи.ОсновноеСредство = ТаблицаПередЗаписью.ОсновноеСредство
	|				И НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, ДЕНЬ) = НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, ДЕНЬ)
	|				И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|				И ТаблицаПриЗаписи.ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|		ГДЕ
	|			(ТаблицаПриЗаписи.НалоговыйОрган <> ТаблицаПередЗаписью.НалоговыйОрган
	|				ИЛИ ТаблицаПриЗаписи.НалоговаяСтавка <> ТаблицаПередЗаписью.НалоговаяСтавка
	|				ИЛИ ТаблицаПриЗаписи.СниженнаяНалоговаяСтавка <> ТаблицаПередЗаписью.СниженнаяНалоговаяСтавка
	|				ИЛИ ТаблицаПриЗаписи.ДоляСтоимостиЧислитель <> ТаблицаПередЗаписью.ДоляСтоимостиЧислитель
	|				ИЛИ ТаблицаПриЗаписи.ДоляСтоимостиЗнаменатель <> ТаблицаПередЗаписью.ДоляСтоимостиЗнаменатель
	|				ИЛИ ТаблицаПриЗаписи.НалоговаяБаза <> ТаблицаПередЗаписью.НалоговаяБаза
	|				ИЛИ ТаблицаПриЗаписи.КадастроваяСтоимость <> ТаблицаПередЗаписью.КадастроваяСтоимость
	|				ИЛИ ТаблицаПриЗаписи.НеоблагаемаяКадастроваяСтоимость <> ТаблицаПередЗаписью.НеоблагаемаяКадастроваяСтоимость
	|				ИЛИ ТаблицаПриЗаписи.ДатаРегистрацииПраваСобственности <> ТаблицаПередЗаписью.ДатаРегистрацииПраваСобственности
	|				ИЛИ ТаблицаПриЗаписи.ДатаРегистрацииПраваСобственности <> ТаблицаПередЗаписью.ДатаРегистрацииПраваСобственности
	|				ИЛИ ТаблицаПриЗаписи.ДатаПрекращенияПраваСобственности <> ТаблицаПередЗаписью.ДатаПрекращенияПраваСобственности
	|				ИЛИ ТаблицаПриЗаписи.ИспользуетсяТолькоВДеятельностиОблагаемойЕНВД <> ТаблицаПередЗаписью.ИспользуетсяТолькоВДеятельностиОблагаемойЕНВД
	|				ИЛИ ТаблицаПриЗаписи.ДвижимоеИмуществоПодлежащееНалогообложению <> ТаблицаПередЗаписью.ДвижимоеИмуществоПодлежащееНалогообложению
	|				ИЛИ ТаблицаПриЗаписи.КодВидаИмущества <> ТаблицаПередЗаписью.КодВидаИмущества
	|				ИЛИ ТаблицаПриЗаписи.ДоляВПравеОбщейСобственностиЧислитель <> ТаблицаПередЗаписью.ДоляВПравеОбщейСобственностиЧислитель
	|				ИЛИ ТаблицаПриЗаписи.ДоляВПравеОбщейСобственностиЗнаменатель <> ТаблицаПередЗаписью.ДоляВПравеОбщейСобственностиЗнаменатель
	|				ИЛИ ТаблицаПриЗаписи.УсловныйНомер <> ТаблицаПередЗаписью.УсловныйНомер
	|				ИЛИ ТаблицаПриЗаписи.ДоляПлощадиЧислитель <> ТаблицаПередЗаписью.ДоляПлощадиЧислитель
	|				ИЛИ ТаблицаПриЗаписи.ДоляПлощадиЗнаменатель <> ТаблицаПередЗаписью.ДоляПлощадиЗнаменатель
	|				ИЛИ ТаблицаПриЗаписи.ОснованиеЛьготы <> ТаблицаПередЗаписью.ОснованиеЛьготы
	|				ИЛИ ТаблицаПриЗаписи.НачалоДействияЛьготы <> ТаблицаПередЗаписью.НачалоДействияЛьготы
	|				ИЛИ ТаблицаПриЗаписи.ОкончаниеДействияЛьготы <> ТаблицаПередЗаписью.ОкончаниеДействияЛьготы
	|				ИЛИ ТаблицаПриЗаписи.НеПодлежитНалогообложению <> ТаблицаПередЗаписью.НеПодлежитНалогообложению
	|				ИЛИ ТаблицаПриЗаписи.СуммаУменьшения <> ТаблицаПередЗаписью.СуммаУменьшения
	|				ИЛИ ТаблицаПриЗаписи.Организация ЕСТЬ NULL)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаПриЗаписи.Период,
	|		ТаблицаПриЗаписи.Организация,
	|		ТаблицаПриЗаписи.ОсновноеСредство
	|	ИЗ
	|		РегистрСведений.ПараметрыНачисленияНалогаНаИмущество КАК ТаблицаПриЗаписи
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыНачисленияНалогаНаИмуществоПередЗаписью КАК ТаблицаПередЗаписью
	|			ПО ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|				И ТаблицаПриЗаписи.ОсновноеСредство = ТаблицаПередЗаписью.ОсновноеСредство
	|				И НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, ДЕНЬ) = НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, ДЕНЬ)
	|		ГДЕ
	|			(ТаблицаПриЗаписи.НалоговыйОрган <> ТаблицаПередЗаписью.НалоговыйОрган
	|				ИЛИ ТаблицаПриЗаписи.НалоговаяСтавка <> ТаблицаПередЗаписью.НалоговаяСтавка
	|				ИЛИ ТаблицаПриЗаписи.СниженнаяНалоговаяСтавка <> ТаблицаПередЗаписью.СниженнаяНалоговаяСтавка
	|				ИЛИ ТаблицаПриЗаписи.ДоляСтоимостиЧислитель <> ТаблицаПередЗаписью.ДоляСтоимостиЧислитель
	|				ИЛИ ТаблицаПриЗаписи.ДоляСтоимостиЗнаменатель <> ТаблицаПередЗаписью.ДоляСтоимостиЗнаменатель
	|				ИЛИ ТаблицаПриЗаписи.НалоговаяБаза <> ТаблицаПередЗаписью.НалоговаяБаза
	|				ИЛИ ТаблицаПриЗаписи.КадастроваяСтоимость <> ТаблицаПередЗаписью.КадастроваяСтоимость
	|				ИЛИ ТаблицаПриЗаписи.НеоблагаемаяКадастроваяСтоимость <> ТаблицаПередЗаписью.НеоблагаемаяКадастроваяСтоимость
	|				ИЛИ ТаблицаПриЗаписи.ДатаРегистрацииПраваСобственности <> ТаблицаПередЗаписью.ДатаРегистрацииПраваСобственности
	|				ИЛИ ТаблицаПриЗаписи.ДатаРегистрацииПраваСобственности <> ТаблицаПередЗаписью.ДатаРегистрацииПраваСобственности
	|				ИЛИ ТаблицаПриЗаписи.ДатаПрекращенияПраваСобственности <> ТаблицаПередЗаписью.ДатаПрекращенияПраваСобственности
	|				ИЛИ ТаблицаПриЗаписи.ИспользуетсяТолькоВДеятельностиОблагаемойЕНВД <> ТаблицаПередЗаписью.ИспользуетсяТолькоВДеятельностиОблагаемойЕНВД
	|				ИЛИ ТаблицаПриЗаписи.ДвижимоеИмуществоПодлежащееНалогообложению <> ТаблицаПередЗаписью.ДвижимоеИмуществоПодлежащееНалогообложению
	|				ИЛИ ТаблицаПриЗаписи.КодВидаИмущества <> ТаблицаПередЗаписью.КодВидаИмущества
	|				ИЛИ ТаблицаПриЗаписи.ДоляВПравеОбщейСобственностиЧислитель <> ТаблицаПередЗаписью.ДоляВПравеОбщейСобственностиЧислитель
	|				ИЛИ ТаблицаПриЗаписи.ДоляВПравеОбщейСобственностиЗнаменатель <> ТаблицаПередЗаписью.ДоляВПравеОбщейСобственностиЗнаменатель
	|				ИЛИ ТаблицаПриЗаписи.УсловныйНомер <> ТаблицаПередЗаписью.УсловныйНомер
	|				ИЛИ ТаблицаПриЗаписи.КБК <> ТаблицаПередЗаписью.КБК
	|				ИЛИ ТаблицаПриЗаписи.ДоляПлощадиЧислитель <> ТаблицаПередЗаписью.ДоляПлощадиЧислитель
	|				ИЛИ ТаблицаПриЗаписи.ДоляПлощадиЗнаменатель <> ТаблицаПередЗаписью.ДоляПлощадиЗнаменатель
	|				ИЛИ ТаблицаПриЗаписи.ОснованиеЛьготы <> ТаблицаПередЗаписью.ОснованиеЛьготы
	|				ИЛИ ТаблицаПриЗаписи.НачалоДействияЛьготы <> ТаблицаПередЗаписью.НачалоДействияЛьготы
	|				ИЛИ ТаблицаПриЗаписи.ОкончаниеДействияЛьготы <> ТаблицаПередЗаписью.ОкончаниеДействияЛьготы
	|				ИЛИ ТаблицаПриЗаписи.НеПодлежитНалогообложению <> ТаблицаПередЗаписью.НеПодлежитНалогообложению
	|				ИЛИ ТаблицаПриЗаписи.СуммаУменьшения <> ТаблицаПередЗаписью.СуммаУменьшения
	|				ИЛИ ТаблицаПередЗаписью.Организация ЕСТЬ NULL)
	|			И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|			И ТаблицаПриЗаписи.ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|
	|	) КАК Таблица
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПараметрыНачисленияНалогаНаИмуществоПередЗаписью";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
		"ПараметрыНачисленияНалогаНаИмуществоИзменение", Выборка.Следующий() И Выборка.Количество > 0);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
