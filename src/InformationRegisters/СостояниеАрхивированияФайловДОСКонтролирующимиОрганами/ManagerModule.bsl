#Область ПрограммныйИнтерфейс

Функция СостояниеАрхива() Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить("ВсегоВАрхиве", 0);
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЕСТЬNULL(КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СостояниеАрхивированияФайловДОСКонтролирующимиОрганами.Идентификатор), 0) КАК Всего
	|ИЗ
	|	РегистрСведений.СостояниеАрхивированияФайловДОСКонтролирующимиОрганами КАК СостояниеАрхивированияФайловДОСКонтролирующимиОрганами
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ТранспортноеСообщениеКонтейнерыПрисоединенныеФайлы КАК ТранспортноеСообщениеКонтейнерыПрисоединенныеФайлы
	|		ПО СостояниеАрхивированияФайловДОСКонтролирующимиОрганами.Файл = ТранспортноеСообщениеКонтейнерыПрисоединенныеФайлы.Ссылка
	|ГДЕ
	|	СостояниеАрхивированияФайловДОСКонтролирующимиОрганами.Архивный = ИСТИНА";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Результат.ВсегоВАрхиве = Выборка.Всего;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция НайтиЗапись(Идентификатор) Экспорт
	
	Результат = ТекущаяЗапись(Идентификатор);
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СостояниеАрхивированияФайловДОСКонтролирующимиОрганами.Идентификатор КАК Идентификатор,
	|	СостояниеАрхивированияФайловДОСКонтролирующимиОрганами.Архивный КАК Архивный,
	|	СостояниеАрхивированияФайловДОСКонтролирующимиОрганами.Восстановлен КАК Восстановлен,
	|	СостояниеАрхивированияФайловДОСКонтролирующимиОрганами.ДатаИзменения КАК ДатаИзменения,
	|	СостояниеАрхивированияФайловДОСКонтролирующимиОрганами.ОжидаетВосстановления КАК ОжидаетВосстановления,
	|	СостояниеАрхивированияФайловДОСКонтролирующимиОрганами.Объект КАК Объект,
	|	СостояниеАрхивированияФайловДОСКонтролирующимиОрганами.ИмяФайла КАК ИмяФайла,
	|	СостояниеАрхивированияФайловДОСКонтролирующимиОрганами.Владелец КАК Владелец,
	|	СостояниеАрхивированияФайловДОСКонтролирующимиОрганами.Файл КАК Файл,
	|	СостояниеАрхивированияФайловДОСКонтролирующимиОрганами.ИдентификаторВыгрузки КАК ИдентификаторВыгрузки,
	|	СостояниеАрхивированияФайловДОСКонтролирующимиОрганами.АбонентЭДО КАК АбонентЭДО
	|ИЗ
	|	РегистрСведений.СостояниеАрхивированияФайловДОСКонтролирующимиОрганами КАК СостояниеАрхивированияФайловДОСКонтролирующимиОрганами
	|ГДЕ
	|	СостояниеАрхивированияФайловДОСКонтролирующимиОрганами.Идентификатор = &Идентификатор";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Идентификатор", Идентификатор);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
		Результат.Существует = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция РегистрацияИзменений(ТекущаяЗапись, Замещать = Ложь) Экспорт
	
	Результат = Ложь;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если НЕ Замещать Тогда
		НашлиЗапись = НайтиЗапись(ТекущаяЗапись.Идентификатор);
		Для Каждого СтрокаКлюча Из ТекущаяЗапись Цикл
			Если СтрокаКлюча.Значение = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			НашлиЗапись[СтрокаКлюча.Ключ] = СтрокаКлюча.Значение;
		КонецЦикла;
	Иначе
		НашлиЗапись = ТекущаяЗапись
	КонецЕсли;
	
	ЗаписьРегистра = РегистрыСведений.СостояниеАрхивированияФайловДОСКонтролирующимиОрганами.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(ЗаписьРегистра, НашлиЗапись);
	ЗаписьРегистра.ДатаИзменения = ТекущаяДатаСеанса();
	
	Попытка
		ЗаписьРегистра.Записать(Истина);
		Результат = Истина;
	Исключение
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Электронный документооборот с контролирующими органами. Перенос в архив';
										|en = 'Электронный документооборот с контролирующими органами. Перенос в архив'"),
			УровеньЖурналаРегистрации.Ошибка, , ,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	УстановитьПривилегированныйРежим(Ложь);

	Возврат Результат;
	
КонецФункции

Функция ТекущаяЗапись(ТекущийИдентификатор = "") Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Идентификатор", ТекущийИдентификатор);
	Результат.Вставить("Архивный", Неопределено);
	Результат.Вставить("Восстановлен", Неопределено);
	Результат.Вставить("ДатаИзменения", Неопределено);
	Результат.Вставить("ОжидаетВосстановления", Неопределено);
	Результат.Вставить("Объект", Неопределено);
	Результат.Вставить("ИмяФайла", Неопределено);
	Результат.Вставить("Владелец", Неопределено);
	Результат.Вставить("Файл", Неопределено);
	Результат.Вставить("ИдентификаторВыгрузки", Неопределено);
	Результат.Вставить("АбонентЭДО", Неопределено);
	Результат.Вставить("Существует", Ложь);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти