#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

// Записывает дополнительные сведения о договоре контролируемой сделки.
//	Параметры:
//		СтруктураИзмерений - Структура - см. ИнициализироватьСтруктуруИзмерений();
//		ДопСведения - Структура - см. ИнициализироватьДопСведенияДоговоров().
//
Процедура СохранитьДопСведенияДоговоров(СтруктураИзмерений, ДопСведения) Экспорт
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ДоговорыУчастниковКонтролируемыхСделок");
		Для каждого ЭлементИзмерения Из СтруктураИзмерений Цикл
			ЭлементБлокировки.УстановитьЗначение(ЭлементИзмерения.Ключ, ЭлементИзмерения.Значение);
		КонецЦикла;
		Блокировка.Заблокировать();
		
		ЗаписьДопСведений = РегистрыСведений.ДоговорыУчастниковКонтролируемыхСделок.СоздатьМенеджерЗаписи();
		
		ЗаполнитьЗначенияСвойств(ЗаписьДопСведений, СтруктураИзмерений);
		ЗаполнитьЗначенияСвойств(ЗаписьДопСведений, ДопСведения);
		ЗаписьДопСведений.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ИмяСобытия = НСтр("ru = 'Запись дополнительных сведений договоров участников контролируемых сделок';
							|en = 'Recording additional information of the controlled transaction participant contracts'", ОбщегоНазначения.КодОсновногоЯзыка());
		ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка, ЗаписьДопСведений.Метаданные(), , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Возвращает структура измерений регистра.
//	Возвращаемое значение:
//		Структура:
//			* Организация - СправочникСсылка.Организации - организация указанная в договоре;
//			* Контрагент - СправочникСсылка.Контрагенты, СправочникСсылка.Организации - контрагент указаннай в договоре, в случае интеркомпани - организация;
//			* ДоговорКонтрагента - СправочникСсылка.ДоговорыКонтрагентов, СправочникСсылка.ДоговорыМеждуОрганизациями, СправочникСсылка.ДоговорыАренды, СправочникСсылка.ДоговорыКредитовИДепозитов
//				- договор для которого определяются дополнительные реквизиты.
//
Функция ИнициализироватьСтруктуруИзмерений() Экспорт
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	СтруктураВозврата.Вставить("Контрагент", Неопределено);
	СтруктураВозврата.Вставить("ДоговорКонтрагента", Неопределено);
	
	Возврат СтруктураВозврата;
	
КонецФункции

// Возвращает структура дополнительных сведений по договору, которые применяются для заполнения уведомления о контролируемых сделках.
//	Возвращаемое значение:
//		Структура:
//			* КодНаименованияСделки - Строка - см. макет Обработки.ПомощникПодготовкиУведомленияОКонтролируемыхСделках.КонтролируемыеСделкиСпискиКодов, область "ТипИСтороныДоговора";
//			* СпособОпределенияЦеныСделки - СправочникСсылка.СпособыОпределенияЦенКонтролируемыхСделок - способ определения цены сделки;
//			* КодУсловийПоставки - Строка - см. макет Обработки.ПомощникПодготовкиУведомленияОКонтролируемыхСделках.КонтролируемыеСделкиСпискиКодов, область "КодУсловийПоставки";
//			* СтороныПрименяютРазныеСтавкиПоНалогуНаПрибыль - Булево - признак отличия налогообложения между участниками контролируемой сделки;
//			* СделкаОтноситсяКДеятельностиНовогоМорскогоМесторождения - Булево - Сделка относится к деятельности нового морского месторождения;
//			* СделкаОтноситсяКДеятельностиОблагаемойНалогомНаДопДоход - Булево - Сделка относится к деятельности облагаемой налогом на дополнительный доход от добычи углеводородного сырья;
//
Функция ИнициализироватьДопСведенияДоговоров() Экспорт
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("КодНаименованияСделки");
	СтруктураВозврата.Вставить("СпособОпределенияЦеныСделки");
	СтруктураВозврата.Вставить("КодУсловийПоставки");
	СтруктураВозврата.Вставить("СтороныПрименяютРазныеСтавкиПоНалогуНаПрибыль");
	СтруктураВозврата.Вставить("СделкаОтноситсяКДеятельностиНовогоМорскогоМесторождения");
	СтруктураВозврата.Вставить("СделкаОтноситсяКДеятельностиОблагаемойНалогомНаДопДоход");
	СтруктураВозврата.Вставить("СделкаНеЯвляетсяКонтролируемойПоП4");
	
	Возврат СтруктураВозврата;
	
КонецФункции

#КонецОбласти

#КонецЕсли