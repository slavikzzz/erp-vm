#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	// АПК:75 инициалы и служебное ФИО перезаписываем всегда, даже если это загрузка из фреша в коробку или обратно
	Для каждого Запись Из ЭтотОбъект Цикл
		
		Если ПустаяСтрока(Запись.Инициалы) Тогда
			
			Если Не ПустаяСтрока(Запись.Имя) Тогда
				
				Запись.Инициалы = ФизическиеЛицаЗарплатаКадрыКлиентСервер.ИнициалыПоИмениОтчеству(
					Запись.Имя, Запись.Отчество);
				
			КонецЕсли;
			
		КонецЕсли;
		
		Запись.ФИОСлужебные = ФизическиеЛицаЗарплатаКадры.НаименованиеСлужебноеПоЧастямИмени(
			Запись.Фамилия, Запись.Имя, Запись.Отчество);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	ЗарплатаКадры.ОбработатьЗаписьНабораФИО(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли