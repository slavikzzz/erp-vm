
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПараметрыЗаписи = РегистрыСведений.ЖурналЗагрузкиЕНС.ПараметрыПолученияЗаписи();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СохранитьЗапись(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ПараметрыЗаписи, ТекущиеДанные);
	ПараметрыЗаписи.АдресХранилища = ПоместитьВоВременноеХранилище("", Новый УникальныйИдентификатор());
	ПолучитьЗаписьНаСервере();
	
	Оповещение = Новый ОписаниеОповещения("ПослеСохраненияЗаписиВФайл", ЭтотОбъект);
	ФайловаяСистемаКлиент.СохранитьФайл(Оповещение, ПараметрыЗаписи.АдресХранилища, ПараметрыЗаписи.ИмяФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьТекстЖурнала(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ПараметрыЗаписи, ТекущиеДанные);
	ПараметрыЗаписи.АдресХранилища = ПоместитьВоВременноеХранилище("", Новый УникальныйИдентификатор());
	ПолучитьЗаписьНаСервере(Ложь);
	
	Оповещение = Новый ОписаниеОповещения("ПослеСохраненияЗаписиВФайл", ЭтотОбъект);
	ФайловаяСистемаКлиент.СохранитьФайл(Оповещение, ПараметрыЗаписи.АдресХранилища, ПараметрыЗаписи.ИмяФайла);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПослеСохраненияЗаписиВФайл(ПолученныеФайлы, Ссылка) Экспорт
	
	Если ЗначениеЗаполнено(ПолученныеФайлы) Тогда
		ШаблонСообщения = НСтр("ru = 'Запись журнала выгружена: %1';
								|en = 'Запись журнала выгружена: %1'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, ПолученныеФайлы[0].ПолноеИмя);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьЗаписьНаСервере(ПолнаяЗапись = Истина)
	
	РегистрыСведений.ЖурналЗагрузкиЕНС.ПолучитьЗапись(ПараметрыЗаписи, ПолнаяЗапись);
	
КонецПроцедуры

#КонецОбласти
