#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

Процедура ПолучитьЗапись(ПараметрыЗаписи, ПолнаяЗапись = Истина) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДанныеИзХранилища = "";
	
	Если ЗначениеЗаполнено(ПараметрыЗаписи.ДатаВыполнения) Тогда
		ДатаВыполненияДляЗапроса = ПараметрыЗаписи.ДатаВыполнения;
	Иначе
		ДатаВыполненияДляЗапроса = НачалоДня(ТекущаяДатаСеанса());
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",    ПараметрыЗаписи.Организация);
	Запрос.УстановитьПараметр("ДатаВыполнения", ДатаВыполненияДляЗапроса);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЖурналЗагрузкиЕНС.ДатаВыполнения КАК ДатаВыполнения,
	|	ЖурналЗагрузкиЕНС.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.ЖурналЗагрузкиЕНС КАК ЖурналЗагрузкиЕНС
	|ГДЕ
	|	ЖурналЗагрузкиЕНС.Организация = &Организация
	|	И ЖурналЗагрузкиЕНС.ДатаВыполнения <= &ДатаВыполнения
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаВыполнения УБЫВ";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		МенеджерЗаписи = РегистрыСведений.ЖурналЗагрузкиЕНС.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Организация    = Выборка.Организация;
		МенеджерЗаписи.ДатаВыполнения = Выборка.ДатаВыполнения;
		МенеджерЗаписи.Прочитать();
		
		Если ПолнаяЗапись Тогда
			ДанныеИзХранилища = МенеджерЗаписи.Содержание.Получить();
		Иначе
			ДанныеИзХранилища = МенеджерЗаписи.ЖурналЗагрузки;
		КонецЕсли;
		ДатаВыполненияДляЗапроса = Выборка.ДатаВыполнения;
		
	КонецЕсли;
	
	Если ПолнаяЗапись Тогда
		Если ТипЗнч(ДанныеИзХранилища) <> Тип("ДвоичныеДанные") Тогда
			Попытка
				ДанныеИзХранилища = ПолучитьДвоичныеДанныеИзСтроки(Строка(ДанныеИзХранилища));
			Исключение
				ДанныеИзХранилища = ПолучитьДвоичныеДанныеИзСтроки(Строка(ТипЗнч(ДанныеИзХранилища)));
			КонецПопытки;
		КонецЕсли;
		ПоместитьВоВременноеХранилище(ПолучитьBase64СтрокуИзДвоичныхДанных(ДанныеИзХранилища), ПараметрыЗаписи.АдресХранилища);
	Иначе
		ПоместитьВоВременноеХранилище(ДанныеИзХранилища, ПараметрыЗаписи.АдресХранилища);
	КонецЕсли;
	
	ШаблонИмениФайлаЗаписи = ШаблонИмениФайлаЗаписи(ПолнаяЗапись);
	Если ПолнаяЗапись Тогда
		ИмяФайла = СтрШаблон(ШаблонИмениФайлаЗаписи,
			СтрЗаменить(Формат(ДатаВыполненияДляЗапроса, "ДЛФ=D"), ".", ""),
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрыЗаписи.Организация, "ИНН"));
	Иначе
		ИмяФайла = ШаблонИмениФайлаЗаписи;
	КонецЕсли;
	ПараметрыЗаписи.ИмяФайла = ИмяФайла;
	
КонецПроцедуры

Функция ПараметрыПолученияЗаписи() Экспорт
	
	ПараметрыЗаписи = Новый Структура();
	ПараметрыЗаписи.Вставить("Организация");
	ПараметрыЗаписи.Вставить("ДатаВыполнения");
	ПараметрыЗаписи.Вставить("АдресХранилища");
	ПараметрыЗаписи.Вставить("ИмяФайла");
	
	Возврат ПараметрыЗаписи;
	
КонецФункции

Функция ШаблонИмениФайлаЗаписи(ПолнаяЗапись) Экспорт
	
	Шаблон = "reglog_" + ?(ПолнаяЗапись, "d%1_i%2", "brief") + ?(ПолнаяЗапись, ".txt", ".json");
	
	Возврат Шаблон;
	
КонецФункции

#КонецОбласти

#КонецЕсли