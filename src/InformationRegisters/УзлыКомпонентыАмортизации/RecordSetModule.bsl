#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

	Для Каждого Запись Из ЭтотОбъект Цикл
		Если Не ЗначениеЗаполнено(Запись.ИдентификаторСтроки) Тогда
			Запись.ИдентификаторСтроки = Новый УникальныйИдентификатор;
		КонецЕсли;
	КонецЦикла;

	ПодготовитьДанныеДляФормированияЗаданийПередЗаписью();

КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПодготовитьДанныеДляФормированияЗаданийПриЗаписи();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПодготовитьДанныеДляФормированияЗаданийПередЗаписью()
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаПередЗаписью.Период              КАК Период,
	|	ТаблицаПередЗаписью.УзелКомпонент       КАК УзелКомпонент,
	|	ТаблицаПередЗаписью.ОсновноеСредство    КАК ОсновноеСредство
	|ПОМЕСТИТЬ УзлыКомпонентыАмортизацииПередЗаписью
	|ИЗ
	|	РегистрСведений.УзлыКомпонентыАмортизации КАК ТаблицаПередЗаписью
	|ГДЕ
	|	ТаблицаПередЗаписью.Регистратор = &Регистратор";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПодготовитьДанныеДляФормированияЗаданийПриЗаписи()
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Период              КАК Период,
	|	&Организация                КАК Организация, // для совместимости с другими таблицами
	|	Таблица.УзелКомпонент       КАК УзелКомпонент,
	|	Таблица.ОсновноеСредство    КАК ОсновноеСредство,
	|	&Регистратор                КАК Документ
	|ПОМЕСТИТЬ УзлыКомпонентыАмортизацииИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаПередЗаписью.Период              КАК Период,
	|		ТаблицаПередЗаписью.УзелКомпонент       КАК УзелКомпонент,
	|		ТаблицаПередЗаписью.ОсновноеСредство    КАК ОсновноеСредство
	|	ИЗ
	|		УзлыКомпонентыАмортизацииПередЗаписью КАК ТаблицаПередЗаписью
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УзлыКомпонентыАмортизации КАК ТаблицаПриЗаписи
	|			ПО ТаблицаПриЗаписи.УзелКомпонент = ТаблицаПередЗаписью.УзелКомпонент
	|				И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|	ГДЕ
	|		(ТаблицаПриЗаписи.Период ЕСТЬ NULL
	|				ИЛИ ТаблицаПриЗаписи.ОсновноеСредство <> ТаблицаПередЗаписью.ОсновноеСредство
	|				ИЛИ НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, ДЕНЬ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, ДЕНЬ))
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ТаблицаПриЗаписи.Период              КАК Период,
	|		ТаблицаПриЗаписи.УзелКомпонент       КАК УзелКомпонент,
	|		ТаблицаПриЗаписи.ОсновноеСредство    КАК ОсновноеСредство
	|	ИЗ
	|		РегистрСведений.УзлыКомпонентыАмортизации КАК ТаблицаПриЗаписи
	|			ЛЕВОЕ СОЕДИНЕНИЕ УзлыКомпонентыАмортизацииПередЗаписью КАК ТаблицаПередЗаписью
	|			ПО ТаблицаПриЗаписи.УзелКомпонент = ТаблицаПередЗаписью.УзелКомпонент
	|	ГДЕ
	|		(ТаблицаПередЗаписью.Период ЕСТЬ NULL
	|				ИЛИ ТаблицаПриЗаписи.ОсновноеСредство <> ТаблицаПередЗаписью.ОсновноеСредство
	|				ИЛИ НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, ДЕНЬ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, ДЕНЬ))
	|			И ТаблицаПриЗаписи.Регистратор = &Регистратор) КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ УзлыКомпонентыАмортизацииПередЗаписью";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("Организация", Отбор.Регистратор.Значение.Организация);
	
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
		"УзлыКомпонентыАмортизацииИзменение", Выборка.Следующий() И Выборка.Количество > 0);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли