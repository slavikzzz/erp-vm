#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ПодготовитьДанныеДляФормированияЗаданийПередЗаписью();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПодготовитьДанныеДляФормированияЗаданийПриЗаписи();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПодготовитьДанныеДляФормированияЗаданийПередЗаписью()
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаПередЗаписью.Период КАК Период,
	|	ТаблицаПередЗаписью.Организация КАК Организация,
	|	ТаблицаПередЗаписью.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаПередЗаписью.СрокИспользования КАК СрокИспользования,
	|	ТаблицаПередЗаписью.СрокИспользованияДляВычисленияАмортизации КАК СрокИспользованияДляВычисленияАмортизации,
	|	ТаблицаПередЗаписью.КоэффициентУскорения КАК КоэффициентУскорения,
	|	ТаблицаПередЗаписью.МетодНачисленияАмортизации КАК МетодНачисленияАмортизации,
	|	ТаблицаПередЗаписью.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость,
	|	ТаблицаПередЗаписью.ДатаПоследнегоИзменения КАК ДатаПоследнегоИзменения,
	|	ТаблицаПередЗаписью.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ОбъемПродукцииРаботДляВычисленияАмортизации
	|ПОМЕСТИТЬ ПараметрыАмортизацииОСУУПередЗаписью
	|ИЗ
	|	РегистрСведений.ПараметрыАмортизацииОСУУ КАК ТаблицаПередЗаписью
	|ГДЕ
	|	ТаблицаПередЗаписью.Регистратор = &Регистратор
	|	И ТаблицаПередЗаписью.ДатаИсправления = ДАТАВРЕМЯ(1,1,1)";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПодготовитьДанныеДляФормированияЗаданийПриЗаписи()
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Период           КАК Период,
	|	Таблица.Организация      КАК Организация,
	|	Таблица.ОсновноеСредство КАК ОсновноеСредство,
	|	ЛОЖЬ                     КАК ОтражатьВРеглУчете,
	|	ИСТИНА                   КАК ОтражатьВУпрУчете,
	|	&Регистратор             КАК Документ
	|ПОМЕСТИТЬ ПараметрыАмортизацииОСУУИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаПередЗаписью.Период            КАК Период,
	|		ТаблицаПередЗаписью.Организация       КАК Организация,
	|		ТаблицаПередЗаписью.ОсновноеСредство  КАК ОсновноеСредство
	|	ИЗ
	|		ПараметрыАмортизацииОСУУПередЗаписью КАК ТаблицаПередЗаписью
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСУУ КАК ТаблицаПриЗаписи
	|			ПО ТаблицаПриЗаписи.ОсновноеСредство = ТаблицаПередЗаписью.ОсновноеСредство
	|				И ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|				И НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, ДЕНЬ) = НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, ДЕНЬ)
	|				И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|				И ТаблицаПриЗаписи.ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|		ГДЕ
	|			(ТаблицаПриЗаписи.Период ЕСТЬ NULL
	|				ИЛИ ТаблицаПриЗаписи.СрокИспользованияДляВычисленияАмортизации <> ТаблицаПередЗаписью.СрокИспользованияДляВычисленияАмортизации
	|				ИЛИ ТаблицаПриЗаписи.КоэффициентУскорения <> ТаблицаПередЗаписью.КоэффициентУскорения
	|				ИЛИ ТаблицаПриЗаписи.МетодНачисленияАмортизации <> ТаблицаПередЗаписью.МетодНачисленияАмортизации
	|				ИЛИ ТаблицаПриЗаписи.ЛиквидационнаяСтоимость <> ТаблицаПередЗаписью.ЛиквидационнаяСтоимость
	|				ИЛИ ТаблицаПриЗаписи.ДатаПоследнегоИзменения <> ТаблицаПередЗаписью.ДатаПоследнегоИзменения
	|				ИЛИ ТаблицаПриЗаписи.ОбъемПродукцииРаботДляВычисленияАмортизации <> ТаблицаПередЗаписью.ОбъемПродукцииРаботДляВычисленияАмортизации)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаПриЗаписи.Период,
	|		ТаблицаПриЗаписи.Организация,
	|		ТаблицаПриЗаписи.ОсновноеСредство
	|	ИЗ
	|		РегистрСведений.ПараметрыАмортизацииОСУУ КАК ТаблицаПриЗаписи
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииОСУУПередЗаписью КАК ТаблицаПередЗаписью
	|			ПО ТаблицаПриЗаписи.ОсновноеСредство = ТаблицаПередЗаписью.ОсновноеСредство
	|				И ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|				И НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, ДЕНЬ) = НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, ДЕНЬ)
	|		ГДЕ
	|			(ТаблицаПередЗаписью.Период ЕСТЬ NULL
	|				ИЛИ ТаблицаПриЗаписи.СрокИспользованияДляВычисленияАмортизации <> ТаблицаПередЗаписью.СрокИспользованияДляВычисленияАмортизации
	|				ИЛИ ТаблицаПриЗаписи.КоэффициентУскорения <> ТаблицаПередЗаписью.КоэффициентУскорения
	|				ИЛИ ТаблицаПриЗаписи.МетодНачисленияАмортизации <> ТаблицаПередЗаписью.МетодНачисленияАмортизации
	|				ИЛИ ТаблицаПриЗаписи.ЛиквидационнаяСтоимость <> ТаблицаПередЗаписью.ЛиквидационнаяСтоимость
	|				ИЛИ ТаблицаПриЗаписи.ДатаПоследнегоИзменения <> ТаблицаПередЗаписью.ДатаПоследнегоИзменения
	|				ИЛИ ТаблицаПриЗаписи.ОбъемПродукцииРаботДляВычисленияАмортизации <> ТаблицаПередЗаписью.ОбъемПродукцииРаботДляВычисленияАмортизации)
	|			И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|			И ТаблицаПриЗаписи.ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|
	|	) КАК Таблица
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПараметрыАмортизацииОСУУПередЗаписью";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
		"ПараметрыАмортизацииОСУУИзменение", Выборка.Следующий() И Выборка.Количество > 0);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
