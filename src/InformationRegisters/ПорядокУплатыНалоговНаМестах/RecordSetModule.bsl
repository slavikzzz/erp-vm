#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

	СтруктураВременныеТаблицы = Новый Структура("МенеджерВременныхТаблиц", Новый МенеджерВременныхТаблиц);
	ДополнительныеСвойства.Вставить("СтруктураВременныеТаблицы", СтруктураВременныеТаблицы);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаПередЗаписью.Период,
	|	ТаблицаПередЗаписью.Организация,
	|	ТаблицаПередЗаписью.Налог,
	|	ТаблицаПередЗаписью.РегистрацияВНалоговомОргане,
	|	ТаблицаПередЗаписью.УплачиваютсяАвансы
	|ПОМЕСТИТЬ ПорядокУплатыНалоговНаМестахПередЗаписью
	|ИЗ
	|	РегистрСведений.ПорядокУплатыНалоговНаМестах КАК ТаблицаПередЗаписью
	|//ОтборОрганизация//";
	
	Если Отбор.Организация.Использование Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, 
									"//ОтборОрганизация//",
									"ГДЕ
									|	ТаблицаПередЗаписью.Организация = &Организация");
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Организация", Отбор.Организация.Значение);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.СтруктураВременныеТаблицы;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Таблица.Период           КАК Период,
	|	Таблица.Организация      КАК Организация,
	|	Таблица.Налог            КАК Налог,
	|	ИСТИНА                   КАК ОтражатьВРеглУчете,
	|	ЛОЖЬ                     КАК ОтражатьВУпрУчете,
	|	НЕОПРЕДЕЛЕНО             КАК Документ
	|ПОМЕСТИТЬ ПорядокУплатыНалоговНаМестахИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, МЕСЯЦ) КАК Период,
	|		ТаблицаПередЗаписью.Организация КАК Организация,
	|		ТаблицаПередЗаписью.Налог КАК Налог
	|	ИЗ
	|		ПорядокУплатыНалоговНаМестахПередЗаписью КАК ТаблицаПередЗаписью
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУплатыНалоговНаМестах КАК ТаблицаПриЗаписи
	|			ПО ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|				И ТаблицаПриЗаписи.Налог = ТаблицаПередЗаписью.Налог
	|				И ТаблицаПриЗаписи.РегистрацияВНалоговомОргане = ТаблицаПередЗаписью.РегистрацияВНалоговомОргане
	|		ГДЕ
	|			(ТаблицаПриЗаписи.УплачиваютсяАвансы <> ТаблицаПередЗаписью.УплачиваютсяАвансы
	|				ИЛИ ТаблицаПриЗаписи.Период <> ТаблицаПередЗаписью.Период
	|				ИЛИ ТаблицаПриЗаписи.Организация ЕСТЬ NULL)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, МЕСЯЦ),
	|		ТаблицаПриЗаписи.Организация,
	|		ТаблицаПриЗаписи.Налог
	|	ИЗ
	|		РегистрСведений.ПорядокУплатыНалоговНаМестах КАК ТаблицаПриЗаписи
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУплатыНалоговНаМестахПередЗаписью КАК ТаблицаПередЗаписью
	|			ПО ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|				И ТаблицаПриЗаписи.Налог = ТаблицаПередЗаписью.Налог
	|				И ТаблицаПриЗаписи.РегистрацияВНалоговомОргане = ТаблицаПередЗаписью.РегистрацияВНалоговомОргане
	|		ГДЕ
	|			(ТаблицаПриЗаписи.УплачиваютсяАвансы <> ТаблицаПередЗаписью.УплачиваютсяАвансы
	|				ИЛИ ТаблицаПриЗаписи.Период <> ТаблицаПередЗаписью.Период
	|				ИЛИ ТаблицаПередЗаписью.Организация ЕСТЬ NULL)
	|			//ОтборОрганизация//
	|
	|	) КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(МАКСИМУМ(ВЫБОР
	|			КОГДА ПорядокУплатыНалоговНаМестахИзменение.Налог = ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.НалогНаИмущество)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ), ЛОЖЬ) КАК СформироватьЗаданиеКРасчетуНалогаНаИмущество,
	|	ЕСТЬNULL(МАКСИМУМ(ВЫБОР
	|			КОГДА ПорядокУплатыНалоговНаМестахИзменение.Налог = ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.ТранспортныйНалог)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ), ЛОЖЬ) КАК СформироватьЗаданиеКРасчетуТранспортногоНалога,
	|	ЕСТЬNULL(МАКСИМУМ(ВЫБОР
	|			КОГДА ПорядокУплатыНалоговНаМестахИзменение.Налог = ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.ЗемельныйНалог)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ), ЛОЖЬ) КАК СформироватьЗаданиеКРасчетуЗемельногоНалога
	|ИЗ
	|	ПорядокУплатыНалоговНаМестахИзменение КАК ПорядокУплатыНалоговНаМестахИзменение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПорядокУплатыНалоговНаМестахПередЗаписью";
	
	Если Отбор.Организация.Использование Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, 
							"//ОтборОрганизация//", 
							"И ТаблицаПриЗаписи.Организация = &Организация");
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Организация", Отбор.Организация.Значение);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Выборка = РезультатЗапроса[0].Выбрать();
	Выборка.Следующий();
	
	СтруктураВременныеТаблицы.Вставить("ПорядокУплатыНалоговНаМестахИзменение", Выборка.Количество > 0);
	
	Выборка = РезультатЗапроса[1].Выбрать();
	Если Выборка.Следующий() Тогда
		Если Выборка.СформироватьЗаданиеКРасчетуНалогаНаИмущество Тогда
			РасчетИмущественныхНалоговУП.СформироватьЗаданиеКРасчетуНалогаНаИмущество(Неопределено, СтруктураВременныеТаблицы);
		КонецЕсли; 
		Если Выборка.СформироватьЗаданиеКРасчетуТранспортногоНалога Тогда
			РасчетИмущественныхНалоговУП.СформироватьЗаданиеКРасчетуТранспортногоНалога(Неопределено, СтруктураВременныеТаблицы);
		КонецЕсли; 
		Если Выборка.СформироватьЗаданиеКРасчетуЗемельногоНалога Тогда
			РасчетИмущественныхНалоговУП.СформироватьЗаданиеКРасчетуЗемельногоНалога(Неопределено, СтруктураВременныеТаблицы);
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
