#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает значения по умолчанию для ресурсов регистра.
// Имена ключей структуры должны строго соответствовать именам ресурсов регистра.
// 
// Параметры:
// Возвращаемое значение:
// 	Структура - структура значений ресурсов регистра.
Функция ЗначенияПоУмолчанию() Экспорт
	СтруктураЗначений = Новый Структура;
	СтруктураЗначений.Вставить("СистемаНалогообложения", Перечисления.СистемыНалогообложения.Общая);
	СтруктураЗначений.Вставить("ПрименяетсяЕНВД", Ложь);
	СтруктураЗначений.Вставить("ЯвляетсяПлательщикомНДПИ", Ложь);
	СтруктураЗначений.Вставить("ЯвляетсяПлательщикомНДД", Ложь);
	СтруктураЗначений.Вставить("ПрименяетсяУСН", Ложь);
	СтруктураЗначений.Вставить("ПлательщикНалогаНаПрибыль", Ложь);
	СтруктураЗначений.Вставить("ДатаПереходаНаУСН", Неопределено);
	СтруктураЗначений.Вставить("УведомлениеНомерУСН", Неопределено);
	СтруктураЗначений.Вставить("УведомлениеДатаУСН", Неопределено);
	СтруктураЗначений.Вставить("ПрименяетсяПСН", Ложь);
	СтруктураЗначений.Вставить("ПрименяетсяАУСН", Ложь);
	СтруктураЗначений.Вставить("ПлательщикЕНП", Ложь);
	СтруктураЗначений.Вставить("УчетнаяПолитикаСуществует", Ложь);
	
	Возврат СтруктураЗначений
КонецФункции

// Возращает текст запроса по данным регистра.
// 
// Возвращаемое значение:
// 	Строка - Текст запроса.
Функция ТекстЗапросаДействующиеПараметрыНалоговУчетныхПолитик() Экспорт
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВЫБОР КОГДА ТаблицаСрезПоследних.Период ЕСТЬ NULL Тогда
	|		ЛОЖЬ
	|	ИНАЧЕ
	|		ИСТИНА
	|	КОНЕЦ КАК УчетнаяПолитикаСуществует,
	|	ТаблицаСрезПоследних.Период КАК Период,
	|	ГоловныеОрганизации.ОбособленноеПодразделение КАК Организация,
	|	ТаблицаСрезПоследних.СистемаНалогообложения КАК СистемаНалогообложения,
	|	ТаблицаСрезПоследних.ПрименяетсяЕНВД КАК ПрименяетсяЕНВД,
	|	ТаблицаСрезПоследних.ПрименяетсяУСН КАК ПрименяетсяУСН,
	|	ТаблицаСрезПоследних.ПлательщикНалогаНаПрибыль КАК ПлательщикНалогаНаПрибыль,
	|	ТаблицаСрезПоследних.ЯвляетсяПлательщикомНДПИ КАК ЯвляетсяПлательщикомНДПИ,
	|	ТаблицаСрезПоследних.ДатаПереходаНаУСН КАК ДатаПереходаНаУСН,
	|	ТаблицаСрезПоследних.УведомлениеНомерУСН КАК УведомлениеНомерУСН,
	|	ТаблицаСрезПоследних.УведомлениеДатаУСН КАК УведомлениеДатаУСН,
	|	(ВЫБОР
	|		КОГДА ТаблицаСрезПоследних.СистемаНалогообложения = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Общая)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС) КОНЕЦ) КАК НалогообложениеНДСПоУмолчанию,
	|	ТаблицаСрезПоследних.ПрименяетсяПСН КАК ПрименяетсяПСН,
	|	ТаблицаСрезПоследних.ПрименяетсяАУСН КАК ПрименяетсяАУСН,
	|	ТаблицаСрезПоследних.ПлательщикНДД КАК ПлательщикНДД,
	|	ТаблицаСрезПоследних.ПлательщикЕНП КАК ПлательщикЕНП
	|ИЗ
	|	ВтГоловныеОрганизации КАК ГоловныеОрганизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиСистемыНалогообложения.СрезПоследних(&Период, Организация В
	|			(ВЫБРАТЬ
	|				ГоловныеОрганизации.Организация
	|			ИЗ
	|				ВтГоловныеОрганизации КАК ГоловныеОрганизации)) КАК ТаблицаСрезПоследних
	|		ПО ГоловныеОрганизации.Организация = ТаблицаСрезПоследних.Организация
	|";
	
	Возврат ТекстЗапроса
	
КонецФункции

// Формирует текстовое описание установленных параметров.
// 
// Параметры:
// 	Организация - СправочникСсылка.Организации - ссылка на организацию.
// 	ДатаДействия - Дата - период действия настроек.
// 	ДействующиеНастройки - Структура - действующие параметры учетной политики.
// Возвращаемое значение:
// 	Строка - Описание действующих параметров строкой.
Функция ОписаниеДействующихПараметров(Организация, ДатаДействия = Неопределено, ДействующиеНастройки = Неопределено) Экспорт
	
	Если ДействующиеНастройки = Неопределено Тогда
		ДействующиеНастройки = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитикНаДату(
			"НастройкиСистемыНалогообложения",
			Организация,
			ДатаДействия,
			Ложь);
	КонецЕсли;
	СтрокаШаблон = "%1: %2." + Символы.ПС;
	СтрокаШаблонБулево = "%1." + Символы.ПС;
	Если НЕ ЗначениеЗаполнено(ДействующиеНастройки) Тогда
		СтрокаОписанияНастроек = НСтр("ru = 'Не заданы параметры.';
										|en = 'Parameters are not specified.'");
		Возврат СтрокаОписанияНастроек;
	КонецЕсли;
	
	СтрокаОписанияНастроек = СтрШаблон(СтрокаШаблон, 
		НСтр("ru = 'Система налогообложения';
			|en = 'Taxation system'"),
		ДействующиеНастройки.СистемаНалогообложения);
	
	Если ДействующиеНастройки.ПрименяетсяЕНВД Тогда
		СтрокаОписанияНастроек = СтрокаОписанияНастроек
			+ СтрШаблон(СтрокаШаблонБулево,
				НСтр("ru = 'Организация является плательщиком ЕНВД';
					|en = 'Company is a UTII payer'"));
	КонецЕсли;
	
	Если ДействующиеНастройки.ЯвляетсяПлательщикомНДПИ Тогда
		СтрокаОписанияНастроек = СтрокаОписанияНастроек
			+ СтрШаблон(СтрокаШаблонБулево,
				НСтр("ru = 'Является плательщиком НДПИ по процентной ставке';
					|en = 'This is a MET payer at rate'"));
	КонецЕсли;
	
	Если ДействующиеНастройки.ПрименяетсяПСН Тогда
		СтрокаОписанияНастроек = СтрокаОписанияНастроек
			+ СтрШаблон(СтрокаШаблонБулево,
				НСтр("ru = 'Организация применяет патентную систему налогообложения (ПСН)';
					|en = 'Company applies the patent taxation system (PTS)'"));
	КонецЕсли;
	
	Если ДействующиеНастройки.ПлательщикНДД И РасчетСебестоимостиЛокализация.ПолучитьФункциональнуюОпциюУчетПоНДД() Тогда
		СтрокаОписанияНастроек = СтрокаОписанияНастроек
			+ СтрШаблон(СтрокаШаблонБулево,
				НСтр("ru = 'Организация является плательщиком НДД';
					|en = 'The company is an AIT payer'"));
	КонецЕсли;
	
	Если ДействующиеНастройки.ЯвляетсяПлательщикомНДПИ Тогда
		СтрокаОписанияНастроек = СтрокаОписанияНастроек
			+ СтрШаблон(СтрокаШаблонБулево,
				НСтр("ru = 'Является плательщиком НДПИ по процентной ставке';
					|en = 'This is a MET payer at rate'"));
	КонецЕсли;
	
	Если ДействующиеНастройки.СистемаНалогообложения = Перечисления.СистемыНалогообложения.Упрощенная Тогда
		Если ЗначениеЗаполнено(ДействующиеНастройки.ДатаПереходаНаУСН) Тогда
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблон,
					НСтр("ru = 'Дата перехода на УСН';
						|en = 'Date of transfer to STS'"),
					Формат(ДействующиеНастройки.ДатаПереходаНаУСН,"ДЛФ=DD;"));
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДействующиеНастройки.УведомлениеНомерУСН) Тогда
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблон,
					НСтр("ru = 'Уведомление о переходе на УСН №';
						|en = 'STS transition notification No.'"),
					ДействующиеНастройки.УведомлениеНомерУСН);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДействующиеНастройки.УведомлениеДатаУСН) Тогда
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблон,
					НСтр("ru = 'Дата уведомления о переходе на УСН';
						|en = 'STS transition notification date'"),
					Формат(ДействующиеНастройки.УведомлениеДатаУСН,"ДЛФ=DD;"));
		КонецЕсли;
		
	КонецЕсли;
	
	Если ДействующиеНастройки.ПлательщикЕНП Тогда
		СтрокаОписанияНастроек = СтрокаОписанияНастроек
			+ СтрШаблон(СтрокаШаблонБулево,
				НСтр("ru = 'Организация использует единый налоговый платеж при уплате налогов';
					|en = 'The company uses a unified tax payment when paying taxes'"));
	КонецЕсли;
	
	Возврат СтрокаОписанияНастроек
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#КонецЕсли
