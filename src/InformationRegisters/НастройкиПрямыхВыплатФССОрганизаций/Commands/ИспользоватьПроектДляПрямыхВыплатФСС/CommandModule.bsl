#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ЗарплатныйПроект, ПараметрыВыполненияКоманды)
	Результат = УстановитьПроектДляПрямыхВыплатФСС(ЗарплатныйПроект);
	Для Каждого ПараметрОповещения Из Результат.ПараметрыОповещений Цикл
		Оповестить("Запись_НастройкиПрямыхВыплатФССОрганизаций", ПараметрОповещения, ПараметрыВыполненияКоманды.Источник);
	КонецЦикла;
	Если Результат.Успех Тогда
		ПоказатьОповещениеПользователя(Результат.Кратко, , Результат.Подробно, БиблиотекаКартинок.Успешно32);
	Иначе
		ИнформированиеПользователяКлиент.Предупредить(Результат.Кратко, Результат.Подробно);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция УстановитьПроектДляПрямыхВыплатФСС(ЗарплатныйПроект)
	Результат = Новый Структура("Успех, Кратко, Подробно, ПараметрыОповещений");
	Результат.ПараметрыОповещений = Новый Массив;
	
	ИменаРеквизитов = "Представление, Организация, ПометкаУдаления, Организация.ГоловнаяОрганизация";
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЗарплатныйПроект, ИменаРеквизитов);
	Организация         = ЗначенияРеквизитов.Организация;
	ГоловнаяОрганизация = ЗначенияРеквизитов.ОрганизацияГоловнаяОрганизация;
	Если ЗначенияРеквизитов.ПометкаУдаления Тогда
		Результат.Успех = Ложь;
		Результат.Кратко = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Зарплатный проект %1 помечен на удаление.';
				|en = 'The payroll card program %1 is marked for deletion.'"),
			ЗарплатныйПроект);
		Возврат Результат;
	КонецЕсли;
	
	МенеджерРегистра = РегистрыСведений.НастройкиПрямыхВыплатФССОрганизаций;
	
	МенеджерЗаписи = МенеджерРегистра.Прочитать(Организация);
	МенеджерЗаписи.ИспользоватьЗарплатныйПроект = Истина;
	МенеджерЗаписи.ЗарплатныйПроект = ЗарплатныйПроект;
	
	Результат.Успех = МенеджерРегистра.ЗаписатьМенеджерЗаписи(МенеджерЗаписи);
	
	Результат.Кратко = Строка(Организация) + ", " + Строка(ЗначенияРеквизитов.Представление);
	Если Результат.Успех Тогда
		ПараметрОповещения = Новый Структура("Организация, ГоловнаяОрганизация", Организация, ГоловнаяОрганизация);
		Результат.ПараметрыОповещений.Добавить(ПараметрОповещения);
		Результат.Подробно = НСтр("ru = 'Установлен зарплатный проект для прямых выплат ФСС';
									|en = 'The payroll card program is set for SSF direct payments'");
	Иначе
		Результат.Кратко = НСтр("ru = 'Не удалось установить зарплатный проект для прямых выплат ФСС';
								|en = 'Cannot set payroll card program for SSF direct payments'");
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

#КонецОбласти
