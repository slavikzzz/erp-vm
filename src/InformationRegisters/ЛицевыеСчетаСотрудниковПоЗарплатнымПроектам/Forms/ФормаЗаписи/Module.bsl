#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустой() Тогда
		
		Если Не Параметры.ЗначениеКопирования.Пустой() Тогда
			Запись.ФизическоеЛицо = Справочники.ФизическиеЛица.ПустаяСсылка();
			Запись.НомерЛицевогоСчета = "";
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Запись.Организация)
			И ЗначениеЗаполнено(Запись.ЗарплатныйПроект) Тогда
			
			Запись.Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Запись.ЗарплатныйПроект, "Организация");
			
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Запись.ДатаОткрытияЛицевогоСчета) Тогда
			Запись.ДатаОткрытияЛицевогоСчета = ТекущаяДатаСеанса();
		КонецЕсли;
		
		УстановитьДоступностьЭлементовФормы(ЭтотОбъект, Истина);
		
	КонецЕсли;
	
	СменитьФорматФайла();
	УстановитьВидимостьЭлементовФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	УстановитьДоступностьЭлементовФормы(ЭтотОбъект, Истина);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	УстановитьДоступностьЭлементовФормы(ЭтотОбъект, Истина);
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ФизическоеЛицоПриИзменении(Элемент)
	
	ОчиститьДокументОснование();
	УстановитьДоступностьЭлементовФормы(ЭтотОбъект, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОчиститьДокументОснование();
	УстановитьДоступностьЭлементовФормы(ЭтотОбъект, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗарплатныйПроектПриИзменении(Элемент)
	
	ОчиститьДокументОснование();
	СменитьФорматФайла();
	УстановитьВидимостьЭлементовФормы(ЭтотОбъект);
	УстановитьДоступностьЭлементовФормы(ЭтотОбъект, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура НомерЛицевогоСчетаПриИзменении(Элемент)
	
	ОчиститьДокументОснование();
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьЭлементовФормы(Форма, ТолькоПросмотрЗаполненных)
	
	Элементы = Форма.Элементы;
	Запись = Форма.Запись;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ФизическоеЛицо",
		"ТолькоПросмотр",
		ТолькоПросмотрЗаполненных И ЗначениеЗаполнено(Запись.ФизическоеЛицо));
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"Организация",
		"ТолькоПросмотр",
		ТолькоПросмотрЗаполненных И ЗначениеЗаполнено(Запись.Организация));
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ЗарплатныйПроект",
		"ТолькоПросмотр",
		ТолькоПросмотрЗаполненных И ЗначениеЗаполнено(Запись.ЗарплатныйПроект)
			Или Не ЗначениеЗаполнено(Запись.Организация));
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"НомерЛицевогоСчета",
		"ТолькоПросмотр",
		Не ЗначениеЗаполнено(Запись.ЗарплатныйПроект)
			Или Не ЗначениеЗаполнено(Запись.ФизическоеЛицо));
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ДатаОткрытияЛицевогоСчета",
		"ТолькоПросмотр",
		Не ЗначениеЗаполнено(Запись.ЗарплатныйПроект)
			Или Не ЗначениеЗаполнено(Запись.ФизическоеЛицо));
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ДокументОснование",
		"Видимость",
		ЗначениеЗаполнено(Запись.ДокументОснование));
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ДокументОснование",
		"Видимость",
		ЗначениеЗаполнено(Запись.ДокументОснование));
		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьЭлементовФормы(Форма)
	
	Элементы = Форма.Элементы;
	Запись = Форма.Запись;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"СпособЗачисления",
		"Видимость",
		Форма.ЗарплатныйПроектПоддерживаетСпособыЗачисленияВыплат);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"БанковскийСчет",
		"Видимость",
		Форма.ПоддерживаетсяЗачислениеПоНомеруСчета);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"БанковскаяКарта",
		"Видимость",
		Форма.ПоддерживаетсяЗачислениеНаКарту);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"Телефон",
		"Видимость",
		Форма.ПоддерживаетсяЗачислениеПоНомеруТелефона);

КонецПроцедуры

&НаКлиенте
Процедура ОчиститьДокументОснование()
	
	Запись.ДокументОснование = Неопределено;
	
КонецПроцедуры

&НаСервере
Процедура СменитьФорматФайла()
	
	ЗарплатныйПроектПоддерживаетСпособыЗачисленияВыплат = 
		ОбменСБанкамиПоЗарплатнымПроектам.ЗарплатныйПроектПоддерживаетСпособыЗачисленияВыплат(Запись.ЗарплатныйПроект);
		
	ОбменСБанкамиПоЗарплатнымПроектам.ЗаполнитьСписокСпособовЗачисления(Элементы.СпособЗачисления, Запись.ЗарплатныйПроект);
	
	Для Каждого СпособЗачисления Из Элементы.СпособЗачисления.СписокВыбора Цикл
		
		Если СпособЗачисления.Значение = Перечисления.СпособыЗачисленияВыплат.ПоНомеруКарты Тогда
			ПоддерживаетсяЗачислениеНаКарту = Истина;
		ИначеЕсли СпособЗачисления.Значение = Перечисления.СпособыЗачисленияВыплат.ПоНомеруСчетаВБанк Тогда
			ПоддерживаетсяЗачислениеПоНомеруСчета = Истина;
		ИначеЕсли СпособЗачисления.Значение = Перечисления.СпособыЗачисленияВыплат.ПоНомеруТелефона Тогда
			ПоддерживаетсяЗачислениеПоНомеруТелефона	= Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Если Не ЗарплатныйПроектПоддерживаетСпособыЗачисленияВыплат Тогда
		Запись.СпособЗачисления = Перечисления.СпособыЗачисленияВыплат.ПоЛицевомуСчету;
		Запись.БанковскийСчет = Неопределено;
		Запись.БанковскаяКарта = Неопределено;
		Запись.Телефон = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

