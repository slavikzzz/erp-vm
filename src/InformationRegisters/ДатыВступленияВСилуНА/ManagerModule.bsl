#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// См. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления.
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	Если ЗарплатаКадры.АвтономнаяРаботаРазрешеноИзменениеДанных(Метаданные.РегистрыСведений.ДатыВступленияВСилуНА) Тогда
		// При добавлении нового ресурса:
		// 1. Повышается версия обработчика "НачальноеЗаполнение".
		// 2. Изменяется макет "ПредопределенныеЗначения".
		// 3. Изменяются формы.
		Обработчик = Обработчики.Добавить();
		Обработчик.Версия = "3.1.30.73";
		Обработчик.Процедура = "РегистрыСведений.ДатыВступленияВСилуНА.НачальноеЗаполнение";
		Обработчик.ОбщиеДанные = Истина;
		Обработчик.НачальноеЗаполнение = Истина;
	КонецЕсли;
КонецПроцедуры

// Начальное заполнение и обновление информационной базы.
Процедура НачальноеЗаполнение() Экспорт
	Обновить(ПолучитьМакет("ПредопределенныеЗначения").ПолучитьТекст(), Ложь);
КонецПроцедуры

// Обновляет данные регистра.
Процедура Обновить(ТекстXML, ПолучатьДанныеИзСервиса = Истина) Экспорт
	ЗарплатаКадры.ОбновитьКлассификатор(ТекстXML, ПолучатьДанныеИзСервиса);
КонецПроцедуры

Функция ДатаВступленияВСилуНА(ИмяНА) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	&Дата КАК Дата
	               |ИЗ
	               |	РегистрСведений.ДатыВступленияВСилуНА КАК ДатыВступленияВСилуНА";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Дата", "ДатыВступленияВСилуНА." + ИмяНА);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		Возврат Выборка.Дата;
	КонецЕсли;
	
	// В регистре нет ни одной записи => Начальное заполнение ИБ еще не сработало, а дата вступления в силу НА уже нужна.
	НачальноеЗаполнение();
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		Возврат Выборка.Дата;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#КонецЕсли