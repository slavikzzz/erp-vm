
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Заголовок = Метаданные.РегистрыСведений.ДатыВступленияВСилуНА.Представление();
	НавигационнаяСсылка = "e1cib/data/РегистрСведений.ДатыВступленияВСилуНА";
	
	ЭтоМодельСервиса = ОбщегоНазначения.РазделениеВключено() Или ОбщегоНазначения.ЭтоАвтономноеРабочееМесто();
	ЭтоАдминистратор = Пользователи.ЭтоПолноправныйПользователь(, Истина, Ложь);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ФормаВключитьВозможностьИзменения",
		"Доступность",
		Не ЭтоМодельСервиса И ЭтоАдминистратор);
	
	ПрочитатьНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаПеречитать(Команда)
	ПрочитатьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	ЗаполнитьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ВключитьВозможностьИзменения(Команда)
	ВключитьВозможностьИзмененияНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаписать(Команда)
	КомандаЗаписатьНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьНаСервере()
	РегистрыСведений.ДатыВступленияВСилуНА.НачальноеЗаполнение();
	Модифицированность = Ложь;
	ПрочитатьНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПрочитатьНаСервере()
	
	МенеджерЗаписи = РеквизитФормыВЗначение("Запись");
	МенеджерЗаписи.Прочитать();
	ЗначениеВРеквизитФормы(МенеджерЗаписи, "Запись");
	
	Модифицированность = Ложь;
	ТолькоПросмотр = Истина;
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ФормаКомандаЗаписать",
		"Видимость",
		Ложь);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ФормаВключитьВозможностьИзменения",
		"Видимость",
		Истина);
	
КонецПроцедуры

&НаСервере
Процедура ВключитьВозможностьИзмененияНаСервере()
	Модифицированность = Ложь;
	ТолькоПросмотр = Ложь;
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ФормаКомандаЗаписать",
		"Видимость",
		Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ФормаВключитьВозможностьИзменения",
		"Видимость",
		Ложь);
КонецПроцедуры

&НаСервере
Процедура КомандаЗаписатьНаСервере()
	НаборЗаписей = РегистрыСведений.ДатыВступленияВСилуНА.СоздатьНаборЗаписей();
	ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Запись);
	НаборЗаписей.ДополнительныеСвойства.Вставить("ЗаписьОбщихДанных");
	ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
	Модифицированность = Ложь;
	ОбновитьПовторноИспользуемыеЗначения();
КонецПроцедуры

#КонецОбласти
