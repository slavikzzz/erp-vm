#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Справочники.Организации.ИспользуетсяНесколькоОрганизаций() Тогда
		
		ОсновнаяОрганизация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
		
		Для каждого Запись Из ЭтотОбъект Цикл
			Если ЗначениеЗаполнено(Запись.ОсновноеСредство) И НЕ ЗначениеЗаполнено(Запись.Организация) Тогда
				Запись.Организация = ОсновнаяОрганизация;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаПередЗаписью.Период,
	|	ТаблицаПередЗаписью.Организация,
	|	ТаблицаПередЗаписью.ОсновноеСредство,
	|	ТаблицаПередЗаписью.ВидНалога,
	|	ТаблицаПередЗаписью.СтатьяРасходов,
	|	ТаблицаПередЗаписью.СпособОтраженияРасходовЗаданДокументом,
	|	ТаблицаПередЗаписью.СпособОтраженияРасходов
	|ПОМЕСТИТЬ СпособыОтраженияРасходовПоИмущественнымНалогамПередЗаписью
	|ИЗ
	|	РегистрСведений.СпособыОтраженияРасходовПоИмущественнымНалогам КАК ТаблицаПередЗаписью
	|ГДЕ
	|	ТаблицаПередЗаписью.Регистратор = &Регистратор";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Или Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Период           КАК Период,
	|	Таблица.Организация      КАК Организация,
	|	Таблица.ОсновноеСредство КАК ОсновноеСредство,
	|	Таблица.ВидНалога        КАК ВидНалога,
	|	ИСТИНА                   КАК ОтражатьВРеглУчете,
	|	ЛОЖЬ                     КАК ОтражатьВУпрУчете,
	|	&Регистратор             КАК Документ
	|ПОМЕСТИТЬ СпособыОтраженияРасходовПоИмущественнымНалогамИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДОБАВИТЬКДАТЕ(ТаблицаПередЗаписью.Период, МЕСЯЦ, 1) КАК Период,
	|		ТаблицаПередЗаписью.Организация       КАК Организация,
	|		ТаблицаПередЗаписью.ОсновноеСредство  КАК ОсновноеСредство,
	|		ТаблицаПередЗаписью.ВидНалога         КАК ВидНалога
	|	ИЗ
	|		СпособыОтраженияРасходовПоИмущественнымНалогамПередЗаписью КАК ТаблицаПередЗаписью
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СпособыОтраженияРасходовПоИмущественнымНалогам КАК ТаблицаПриЗаписи
	|			ПО ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|				И ТаблицаПриЗаписи.ОсновноеСредство = ТаблицаПередЗаписью.ОсновноеСредство
	|				И ТаблицаПриЗаписи.ВидНалога = ТаблицаПередЗаписью.ВидНалога
	|				И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|		ГДЕ
	|			(НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, МЕСЯЦ)
	|				ИЛИ ТаблицаПриЗаписи.СтатьяРасходов <> ТаблицаПередЗаписью.СтатьяРасходов
	|				ИЛИ ТаблицаПриЗаписи.СпособОтраженияРасходовЗаданДокументом <> ТаблицаПередЗаписью.СпособОтраженияРасходовЗаданДокументом
	|				ИЛИ ТаблицаПриЗаписи.СпособОтраженияРасходов <> ТаблицаПередЗаписью.СпособОтраженияРасходов
	|				ИЛИ ТаблицаПриЗаписи.Организация ЕСТЬ NULL)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДОБАВИТЬКДАТЕ(ТаблицаПриЗаписи.Период, МЕСЯЦ, 1) КАК Период,
	|		ТаблицаПриЗаписи.Организация,
	|		ТаблицаПриЗаписи.ОсновноеСредство,
	|		ТаблицаПриЗаписи.ВидНалога
	|	ИЗ
	|		РегистрСведений.СпособыОтраженияРасходовПоИмущественнымНалогам КАК ТаблицаПриЗаписи
	|			ЛЕВОЕ СОЕДИНЕНИЕ СпособыОтраженияРасходовПоИмущественнымНалогамПередЗаписью КАК ТаблицаПередЗаписью
	|			ПО ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|				И ТаблицаПриЗаписи.ОсновноеСредство = ТаблицаПередЗаписью.ОсновноеСредство
	|				И ТаблицаПриЗаписи.ВидНалога = ТаблицаПередЗаписью.ВидНалога
	|		ГДЕ
	|			(НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, МЕСЯЦ)
	|				ИЛИ ТаблицаПриЗаписи.СтатьяРасходов <> ТаблицаПередЗаписью.СтатьяРасходов
	|				ИЛИ ТаблицаПриЗаписи.СпособОтраженияРасходовЗаданДокументом <> ТаблицаПередЗаписью.СпособОтраженияРасходовЗаданДокументом
	|				ИЛИ ТаблицаПриЗаписи.СпособОтраженияРасходов <> ТаблицаПередЗаписью.СпособОтраженияРасходов
	|				ИЛИ ТаблицаПередЗаписью.Организация ЕСТЬ NULL)
	|			И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|
	|	) КАК Таблица
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СпособыОтраженияРасходовПоИмущественнымНалогамПередЗаписью";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
		"СпособыОтраженияРасходовПоИмущественнымНалогамИзменение", Выборка.Следующий() И Выборка.Количество > 0);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
