
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(Команда)
	Добавлено = ЗаполнитьНаСервере();
	Если Добавлено = 0 Тогда
		Текст = НСтр("ru = 'Новых организаций не найдено';
					|en = 'New companies are not found'");
		ПоказатьОповещениеПользователя(, , Текст, БиблиотекаКартинок.Информация);
	Иначе
		Текст = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
			НСтр("ru = ';Добавлена %1 организация;;Добавлено %1 организации;Добавлено %1 организаций;';
				|en = '; %1 company added;;%1 companies added;%1 companies added;'"), 
			Добавлено);
		ПоказатьОповещениеПользователя(, , Текст, БиблиотекаКартинок.Успешно);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ЗаполнитьНаСервере()
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Организации.Ссылка КАК Организация,
	|	ГоловныеОрганизации.Ссылка КАК ГоловнаяОрганизация,
	|	ВЫБОР
	|		КОГДА ГоловныеОрганизации.Ссылка ЕСТЬ NULL
	|				ИЛИ ГоловныеОрганизации.Ссылка = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|				ИЛИ Организации.ГоловнаяОрганизация = Организации.Ссылка
	|			ТОГДА Организации.Ссылка
	|		КОГДА Организации.ДополнительныйКодФСС = """"
	|				И Организации.РегистрационныйНомерФСС = """"
	|			ТОГДА ГоловныеОрганизации.Ссылка
	|		КОГДА ВЫБОР
	|				КОГДА Организации.ДополнительныйКодФСС = """"
	|					ТОГДА Организации.РегистрационныйНомерФСС
	|				ИНАЧЕ Организации.ДополнительныйКодФСС
	|			КОНЕЦ = ВЫБОР
	|				КОГДА ГоловныеОрганизации.ДополнительныйКодФСС = """"
	|					ТОГДА ГоловныеОрганизации.РегистрационныйНомерФСС
	|				ИНАЧЕ ГоловныеОрганизации.ДополнительныйКодФСС
	|			КОНЕЦ
	|			ТОГДА ГоловныеОрганизации.Ссылка
	|		ИНАЧЕ Организации.Ссылка
	|	КОНЕЦ КАК ОтчитывающаясяОрганизация
	|ПОМЕСТИТЬ ВТСтрахователиПоРеквизитам
	|ИЗ
	|	Справочник.Организации КАК Организации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ГоловныеОрганизации
	|		ПО Организации.ГоловнаяОрганизация = ГоловныеОрганизации.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТСтрахователиПоРеквизитам.Организация КАК Организация,
	|	ВТСтрахователиПоРеквизитам.ОтчитывающаясяОрганизация КАК ОтчитывающаясяОрганизация
	|ИЗ
	|	ВТСтрахователиПоРеквизитам КАК ВТСтрахователиПоРеквизитам
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтчитывающиесяПоВзносамОрганизации КАК ОтчитывающиесяПоВзносамОрганизации
	|		ПО ВТСтрахователиПоРеквизитам.Организация = ОтчитывающиесяПоВзносамОрганизации.Организация
	|ГДЕ
	|	ОтчитывающиесяПоВзносамОрганизации.ОтчитывающаясяОрганизация ЕСТЬ NULL
	|	И ВТСтрахователиПоРеквизитам.ОтчитывающаясяОрганизация <> ВТСтрахователиПоРеквизитам.Организация";
	
	Добавлено = 0;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НаборЗаписей = РегистрыСведений.ОтчитывающиесяПоВзносамОрганизации.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
		НаборЗаписей.Прочитать();
		Если НаборЗаписей.Количество() > 0 Тогда
			Продолжить;
		КонецЕсли;
		Запись = НаборЗаписей.Добавить();
		Запись.Организация               = Выборка.Организация;
		Запись.ОтчитывающаясяОрганизация = Выборка.ОтчитывающаясяОрганизация;
		НаборЗаписей.Записать(Истина);
		Добавлено = Добавлено + 1;
	КонецЦикла;
	
	Если Добавлено > 0 Тогда
		ОбновитьПовторноИспользуемыеЗначения();
	КонецЕсли;
	
	Элементы.Список.Обновить();
	
	Возврат Добавлено;
КонецФункции

#КонецОбласти
