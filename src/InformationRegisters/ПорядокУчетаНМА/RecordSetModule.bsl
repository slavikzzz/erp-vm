#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ПодготовитьДанныеДляФормированияЗаданийПередЗаписью();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПодготовитьДанныеДляФормированияЗаданийПриЗаписи();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПодготовитьДанныеДляФормированияЗаданийПередЗаписью()
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаПередЗаписью.Период КАК Период,
	|	ТаблицаПередЗаписью.Организация КАК Организация,
	|	ТаблицаПередЗаписью.НематериальныйАктив КАК НематериальныйАктив,
	|	ТаблицаПередЗаписью.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	ТаблицаПередЗаписью.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаПередЗаписью.ОбъемНаработки КАК ОбъемНаработки
	|ПОМЕСТИТЬ ПорядокУчетаНМАПередЗаписью
	|ИЗ
	|	РегистрСведений.ПорядокУчетаНМА КАК ТаблицаПередЗаписью
	|ГДЕ
	|	ТаблицаПередЗаписью.Регистратор = &Регистратор";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПодготовитьДанныеДляФормированияЗаданийПриЗаписи()
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Период              КАК Период,
	|	Таблица.Организация         КАК Организация,
	|	Таблица.НематериальныйАктив КАК НематериальныйАктив,
	|	ИСТИНА                      КАК ОтражатьВРеглУчете,
	|	ЛОЖЬ                        КАК ОтражатьВУпрУчете,
	|	&Регистратор                КАК Документ
	|ПОМЕСТИТЬ ПорядокУчетаНМАИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаПередЗаписью.Период               КАК Период,
	|		ТаблицаПередЗаписью.Организация          КАК Организация,
	|		ТаблицаПередЗаписью.НематериальныйАктив  КАК НематериальныйАктив
	|	ИЗ
	|		ПорядокУчетаНМАПередЗаписью КАК ТаблицаПередЗаписью
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаНМА КАК ТаблицаПриЗаписи
	|			ПО ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|				И ТаблицаПриЗаписи.НематериальныйАктив = ТаблицаПередЗаписью.НематериальныйАктив
	|				И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|		ГДЕ
	|			(ТаблицаПриЗаписи.Период ЕСТЬ NULL
	|				ИЛИ ТаблицаПриЗаписи.ГруппаФинансовогоУчета <> ТаблицаПередЗаписью.ГруппаФинансовогоУчета
	|				ИЛИ ТаблицаПриЗаписи.НаправлениеДеятельности <> ТаблицаПередЗаписью.НаправлениеДеятельности
	|				ИЛИ ТаблицаПриЗаписи.ОбъемНаработки <> ТаблицаПередЗаписью.ОбъемНаработки
	|				ИЛИ НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, МЕСЯЦ))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаПриЗаписи.Период,
	|		ТаблицаПриЗаписи.Организация,
	|		ТаблицаПриЗаписи.НематериальныйАктив
	|	ИЗ
	|		РегистрСведений.ПорядокУчетаНМА КАК ТаблицаПриЗаписи
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаНМАПередЗаписью КАК ТаблицаПередЗаписью
	|			ПО ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|				И ТаблицаПриЗаписи.НематериальныйАктив = ТаблицаПередЗаписью.НематериальныйАктив
	|		ГДЕ
	|			(ТаблицаПередЗаписью.Период ЕСТЬ NULL
	|				ИЛИ ТаблицаПриЗаписи.ГруппаФинансовогоУчета <> ТаблицаПередЗаписью.ГруппаФинансовогоУчета
	|				ИЛИ ТаблицаПриЗаписи.НаправлениеДеятельности <> ТаблицаПередЗаписью.НаправлениеДеятельности
	|				ИЛИ ТаблицаПриЗаписи.ОбъемНаработки <> ТаблицаПередЗаписью.ОбъемНаработки
	|				ИЛИ НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, МЕСЯЦ))
	|			И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|
	|	) КАК Таблица
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПорядокУчетаНМАПередЗаписью";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
		"ПорядокУчетаНМАИзменение", Выборка.Следующий() И Выборка.Количество > 0);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
