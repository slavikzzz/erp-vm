
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Добавляет запись в регистр соответствия объектов
// 
// Параметры:
//  ИсточникДанных - СправочникСсылка.ИсточникиЗагрузкиПроизводственнойНСИ
//  Идентификатор - Строка
//  СсылкаНаОбъект - ЛюбаяСсылка
// 
// Возвращаемое значение:
//  РегистрСведенийМенеджерЗаписи.СоответствиеОбъектовДляЗагрузкиНСИ
//
Функция ДобавитьЗапись(ИсточникДанных, Идентификатор, СсылкаНаОбъект) Экспорт
	
	Тип = ТипЗнч(СсылкаНаОбъект);
	
	Запись = СоздатьМенеджерЗаписи();
	Запись.ИсточникДанных    = ИсточникДанных;
	Запись.Идентификатор     = Идентификатор;
	Запись.ОбъектМетаданных  = Новый(Тип);
	Запись.СсылкаНаОбъект    = СсылкаНаОбъект;
	Запись.ДатаЗаписи        = ТекущаяДатаСеанса();
	
	Запись.Записать();
	
	Возврат Запись;
	
КонецФункции

// Добавляет запись в регистр соответствия объектов со значениями полей из источника свойств
// 
// Параметры:
//  ИсточникДанных - СправочникСсылка.ИсточникиЗагрузкиПроизводственнойНСИ
//  ИсточникСвойств - Структура
// 
// Возвращаемое значение:
//  РегистрСведенийМенеджерЗаписи.СоответствиеОбъектовДляЗагрузкиНСИ
//  
Функция ДобавитьЗаписьПоСвойствам(ИсточникДанных, ИсточникСвойств) Экспорт
	
	Запись = СоздатьМенеджерЗаписи();
	Запись.ИсточникДанных = ИсточникДанных;
	Запись.ДатаЗаписи     = ТекущаяДатаСеанса();
	ЗаполнитьЗначенияСвойств(Запись, ИсточникСвойств);
	
	Запись.Записать();
	
	Возврат Запись;
	
КонецФункции

#КонецОбласти

#КонецЕсли
