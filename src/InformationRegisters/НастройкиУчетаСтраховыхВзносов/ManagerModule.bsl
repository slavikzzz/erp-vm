
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Начальное заполнение и обновление информационной базы.
Процедура ЗаполнитьФОПоказаТарифаВПодразделении() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	НастройкиУчетаСтраховыхВзносов.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.НастройкиУчетаСтраховыхВзносов КАК НастройкиУчетаСтраховыхВзносов
	|ГДЕ
	|	НастройкиУчетаСтраховыхВзносов.ПрименяютсяЛьготныеТарифыВПодразделениях <> (НастройкиУчетаСтраховыхВзносов.ИспользуетсяТрудЧленовЭкипажейМорскихСудов
	|			И НастройкиУчетаСтраховыхВзносов.Организация.ЕстьОбособленныеПодразделения)";
	Если Запрос.Выполнить().Пустой() Тогда
		Возврат
	КонецЕсли; 

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиУчетаСтраховыхВзносов.Организация КАК Организация,
	|	НастройкиУчетаСтраховыхВзносов.ИспользуетсяТрудФармацевтов КАК ИспользуетсяТрудФармацевтов,
	|	НастройкиУчетаСтраховыхВзносов.ИспользуетсяТрудЧленовЛетныхЭкипажей КАК ИспользуетсяТрудЧленовЛетныхЭкипажей,
	|	НастройкиУчетаСтраховыхВзносов.ИспользуетсяТрудЧленовЭкипажейМорскихСудов КАК ИспользуетсяТрудЧленовЭкипажейМорскихСудов,
	|	НастройкиУчетаСтраховыхВзносов.ИспользуетсяТрудШахтеров КАК ИспользуетсяТрудШахтеров,
	|	НастройкиУчетаСтраховыхВзносов.ИспользуютсяРаботыСДосрочнойПенсией КАК ИспользуютсяРаботыСДосрочнойПенсией,
	|	НастройкиУчетаСтраховыхВзносов.ПрименяютсяРезультатыСпециальнойОценкиУсловийТруда КАК ПрименяютсяРезультатыСпециальнойОценкиУсловийТруда,
	|	НастройкиУчетаСтраховыхВзносов.ИспользуетсяТрудЧленовЭкипажейМорскихСудов
	|		И НастройкиУчетаСтраховыхВзносов.Организация.ЕстьОбособленныеПодразделения КАК ПрименяютсяЛьготныеТарифыВПодразделениях,
	|	*
	|ИЗ
	|	РегистрСведений.НастройкиУчетаСтраховыхВзносов КАК НастройкиУчетаСтраховыхВзносов";
	
	НаборЗаписей = РегистрыСведений.НастройкиУчетаСтраховыхВзносов.СоздатьНаборЗаписей();
	НаборЗаписей.Загрузить(Запрос.Выполнить().Выгрузить());
	ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
	
КонецПроцедуры

Процедура ЗаполнитьФОИспользованияИностранцевСОсобымиВзносами() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СтатусыЗастрахованныхФизическихЛиц.ФизическоеЛицо КАК ФизическоеЛицо
	|ПОМЕСТИТЬ ВТИностранцы
	|ИЗ
	|	РегистрСведений.СтатусыЗастрахованныхФизическихЛиц КАК СтатусыЗастрахованныхФизическихЛиц
	|ГДЕ
	|	СтатусыЗастрахованныхФизическихЛиц.ВидЗастрахованногоЛица = ЗНАЧЕНИЕ(Перечисление.ВидыЗастрахованныхЛицОбязательногоСтрахования.ВременноПребывающиеИностранцыСтрахуемыеФСС)";
	Если Запрос.Выполнить().Выгрузить()[0].Количество = 0 Тогда
		Возврат
	КонецЕсли; 
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Сотрудники.ГоловнаяОрганизация КАК Организация
	|ПОМЕСТИТЬ ВТОрганизации
	|ИЗ
	|	ВТИностранцы КАК ФизическиеЛица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
	|		ПО ФизическиеЛица.ФизическоеЛицо = Сотрудники.ФизическоеЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиУчетаСтраховыхВзносов КАК НастройкиУчетаСтраховыхВзносов
	|		ПО (НастройкиУчетаСтраховыхВзносов.Организация = Сотрудники.ГоловнаяОрганизация)
	|ГДЕ
	|	НЕ ЕСТЬNULL(НастройкиУчетаСтраховыхВзносов.ИспользуетсяТрудИностранцевСОсобымиВзносами, ЛОЖЬ)";
	Если Запрос.Выполнить().Выгрузить()[0].Количество = 0 Тогда
		Возврат
	КонецЕсли; 
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиУчетаСтраховыхВзносов.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА Организации.Организация ЕСТЬ NULL
	|			ТОГДА НастройкиУчетаСтраховыхВзносов.ИспользуетсяТрудИностранцевСОсобымиВзносами
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ИспользуетсяТрудИностранцевСОсобымиВзносами, 
	|	НастройкиУчетаСтраховыхВзносов.*
	|ИЗ
	|	РегистрСведений.НастройкиУчетаСтраховыхВзносов КАК НастройкиУчетаСтраховыхВзносов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОрганизации КАК Организации
	|		ПО НастройкиУчетаСтраховыхВзносов.Организация = Организации.Организация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Организации.Организация,
	|	ИСТИНА, 
	|	НастройкиУчетаСтраховыхВзносов.*
	|ИЗ
	|	ВТОрганизации КАК Организации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиУчетаСтраховыхВзносов КАК НастройкиУчетаСтраховыхВзносов
	|		ПО НастройкиУчетаСтраховыхВзносов.Организация = Организации.Организация
	|ГДЕ
	|	НастройкиУчетаСтраховыхВзносов.Организация ЕСТЬ NULL";
	
	НаборЗаписей = РегистрыСведений.НастройкиУчетаСтраховыхВзносов.СоздатьНаборЗаписей();
	НаборЗаписей.Загрузить(Запрос.Выполнить().Выгрузить());
	ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
