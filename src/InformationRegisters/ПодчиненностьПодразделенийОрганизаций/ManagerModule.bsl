#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(ВышестоящееПодразделение.Владелец)
	|	И ЗначениеРазрешено(ВышестоящееПодразделение)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ОбработчикиПравилРегистрации

// См. ЗарплатаКадрыРасширенныйСинхронизацияДанных.ШаблонОбработчика
Процедура ПриЗаполненииНастроекОбработчиковПравилРегистрации(Настройки) Экспорт
	ЗарплатаКадрыРасширенныйСинхронизацияДанных.ДляОтбораПоОрганизации(Настройки, , Неопределено);
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбновитьПодчиненностьПодразделений(СписокПодразделений) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПодразделенияОрганизаций.Ссылка КАК Ссылка,
		|	ПодразделенияОрганизаций.Родитель КАК Родитель,
		|	ПодразделенияОрганизаций.Владелец КАК Организация
		|ИЗ
		|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
		|ГДЕ
		|	ПодразделенияОрганизаций.Ссылка <> ПодразделенияОрганизаций.Родитель
		|	И ПодразделенияОрганизаций.Родитель <> ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)";
	
	РодителиПодразделений = Новый Соответствие;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		РодителиПодразделений.Вставить(Выборка.Ссылка, Выборка.Родитель);
	КонецЦикла;
	
	ОрганизацииПодразделений = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(СписокПодразделений, "Владелец");
	Для каждого Подразделение Из СписокПодразделений Цикл
		
		ОрганизацияПодразделения = ОрганизацииПодразделений.Получить(Подразделение);
		Если Не ЗначениеЗаполнено(ОрганизацияПодразделения) Тогда
			Продолжить;
		КонецЕсли;
		
		НаборЗаписей = РегистрыСведений.ПодчиненностьПодразделенийОрганизаций.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Подразделение.Установить(Подразделение);
		
		Запись = НаборЗаписей.Добавить();
		Запись.ВышестоящееПодразделение = Подразделение;
		Запись.Подразделение = Подразделение;
		Запись.Организация = ОрганизацияПодразделения;
		
		Родитель = РодителиПодразделений.Получить(Запись.ВышестоящееПодразделение);
		Пока Родитель <> Неопределено Цикл
			
			Запись = НаборЗаписей.Добавить();
			Запись.ВышестоящееПодразделение = Родитель;
			Запись.Подразделение = Подразделение;
			Запись.Организация = ОрганизацияПодразделения;
			
			Родитель = РодителиПодразделений.Получить(Запись.ВышестоящееПодразделение);
			
		КонецЦикла;
		
		НаборЗаписей.Записать();
		
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура ОбновитьПодчиненностьПодразделенийПриСменеРодителя(СписокПодразделений) Экспорт
	
	СписокРодителей = ОбщегоНазначения.СкопироватьРекурсивно(СписокПодразделений);
	Пока СписокРодителей.Количество() > 0 Цикл
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("СписокРодителей", СписокРодителей);
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ПодразделенияОрганизаций.Ссылка
			|ИЗ
			|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
			|ГДЕ
			|	ПодразделенияОрганизаций.Родитель В(&СписокРодителей)";
		
		УстановитьПривилегированныйРежим(Истина);
		СписокПодразделенийКОбновлению = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
		УстановитьПривилегированныйРежим(Ложь);
		
		Если СписокПодразделенийКОбновлению.Количество() = 0 Тогда
			Прервать;
		КонецЕсли;
		
		ОбновитьПодчиненностьПодразделений(СписокПодразделенийКОбновлению);
		СписокРодителей = ОбщегоНазначения.СкопироватьРекурсивно(СписокПодразделенийКОбновлению);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПодчиненностьПодразделений()
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПодчиненностьПодразделенийОрганизаций.ВышестоящееПодразделение
		|ИЗ
		|	РегистрСведений.ПодчиненностьПодразделенийОрганизаций КАК ПодчиненностьПодразделенийОрганизаций";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ПодразделенияОрганизаций.Ссылка
			|ИЗ
			|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций";
		
		ОбновитьПодчиненностьПодразделений(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПерезаполнитьПодчиненностьПодразделений(ПараметрыОбновления = Неопределено) Экспорт
	
	Блокировка = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОписаниеБлокируемыхДанных(Метаданные.РегистрыСведений.ПодчиненностьПодразделенийОрганизаций);
	Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.НачатьОбновлениеДанных(Блокировка, ПараметрыОбновления) Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПродолжитьОбработчик(ПараметрыОбновления);
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.ПодчиненностьПодразделенийОрганизаций.СоздатьНаборЗаписей();
	НаборЗаписей.Записать();
	
	ЗаполнитьПодчиненностьПодразделений();
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли