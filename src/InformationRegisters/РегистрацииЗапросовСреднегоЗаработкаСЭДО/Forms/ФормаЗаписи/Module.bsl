#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Запись.ИсходящийДокумент) Тогда
		ТолькоПросмотр = Истина;
	Иначе
		Элементы.ПоясняющаяНадписьВШапке.Видимость = Ложь;
		Элементы.ИсходныйДокументГруппа.Поведение = ПоведениеОбычнойГруппы.Обычное;
	КонецЕсли;
	
	Менеджер = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ИмяФормы);
	Менеджер.ЗаполнитьСписокВыбораСтатусов(Элементы.РегистрацияСтатус, Запись.РегистрацияСтатус);
	
	ОбновитьЭлементыФормы();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ЗапросСреднегоЗаработкаСЭДО"
		И (Источник = Запись.ИсходящийДокумент Или Не ЗначениеЗаполнено(Источник)) Тогда
		ПодключитьОбработчикОжиданияПрочитать();
		
	ИначеЕсли ИмяСобытия = СЭДОФССКлиент.ИмяСобытияПослеПолученияСообщенийОтФСС()
		Или ИмяСобытия = СЭДОФССКлиент.ИмяСобытияПослеОтправкиПодтвержденияПолучения() Тогда
		ПодключитьОбработчикОжиданияПрочитать();
		
	ИначеЕсли ИмяСобытия = "Запись_РегистрацииЗапросовСреднегоЗаработкаСЭДО"
		И (Источник = Запись.ИсходящийДокумент Или Не ЗначениеЗаполнено(Источник)) Тогда
		ПодключитьОбработчикОжиданияПрочитать();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_РегистрацииЗапросовСреднегоЗаработкаСЭДО", Запись.ИсходящийДокумент, Запись.ИсходящийДокумент);
	СЭДОФССКлиент.ОповеститьОНеобходимостиОбновитьТекущиеДела();
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПоясняющаяНадписьВШапкеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("Документ.ЗапросСреднегоЗаработкаСЭДО.ФормаОбъекта", Новый Структура("Ключ", Запись.ИсходящийДокумент));
КонецПроцедуры

&НаКлиенте
Процедура ВключитьВозможностьРедактирования(Команда)
	ТолькоПросмотр = Ложь;
	ОбновитьЭлементыФормы();
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДоставкаИдентификаторТекстXML(Команда)
	СЭДОФССКлиент.ПоказатьТекстXML(Запись.ДоставкаИдентификатор);
КонецПроцедуры

&НаКлиенте
Процедура РегистрацияИдентификаторТекстXML(Команда)
	СЭДОФССКлиент.ПоказатьТекстXML(Запись.РегистрацияИдентификатор);
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПодключитьОбработчикОжиданияПрочитать()
	ОтключитьОбработчикОжидания("ОбработчикОжиданияПрочитать");
	ПодключитьОбработчикОжидания("ОбработчикОжиданияПрочитать", 0.2, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОжиданияПрочитать()
	Если Не Модифицированность Тогда
		Прочитать();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыФормы()
	Элементы.ДоставкаИдентификаторТекстXML.Видимость    = ЗначениеЗаполнено(Запись.ДоставкаИдентификатор);
	Элементы.РегистрацияИдентификаторТекстXML.Видимость = ЗначениеЗаполнено(Запись.РегистрацияИдентификатор);
	
	ОтправленОператору = (Запись.ОтправленОператору Или ЗначениеЗаполнено(Запись.ДатаОтправкиОператору));
	Элементы.ОтправленОператору.Видимость          = Не ТолькоПросмотр Или ОтправленОператору;
	Элементы.ДатаОтправкиОператору.Видимость       = Не ТолькоПросмотр Или ОтправленОператору;
	Элементы.ДоставкаИдентификаторПакета.Видимость = Не ТолькоПросмотр Или ОтправленОператору
		Или ЗначениеЗаполнено(Запись.ДоставкаИдентификаторПакета);
	
	Элементы.ОшибкиЛогическогоКонтроляГруппа.Видимость = Не ТолькоПросмотр
		Или Запись.ЕстьОшибкиЛогическогоКонтроля;
	
	Если РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца Тогда
		Элементы.КоманднаяПанельПодвал.Видимость = Истина;
		КнопкаЗакрыть = Элементы.КнопкаЗакрытьВПодвале;
	Иначе
		Элементы.КоманднаяПанельПодвал.Видимость = Ложь;
		КнопкаЗакрыть = Элементы.КнопкаЗакрытьВШапке;
	КонецЕсли;
	
	Если Запись.Доставлен
		И Не ЗначениеЗаполнено(Запись.РегистрацияИдентификатор)
		И Не Запись.ЕстьОшибкиЛогическогоКонтроля Тогда
		Элементы.ПолучитьНовыеСообщенияСЭДОФСС.КнопкаПоУмолчанию = Истина;
	Иначе
		КнопкаЗакрыть.КнопкаПоУмолчанию = Истина;
	КонецЕсли;
	
	Элементы.РегистрацияДокумент.Видимость = Не ТолькоПросмотр Или ЗначениеЗаполнено(Запись.РегистрацияДокумент);
	Элементы.РегистрацияДокумент.Вид = ?(ТолькоПросмотр, ВидПоляФормы.ПолеНадписи, ВидПоляФормы.ПолеВвода);
	
КонецПроцедуры

#КонецОбласти
