#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	ВозможныеСостояния = Перечисления.СостоянияДокументаСЭДОФСС;
	Для Каждого Запись Из ЭтотОбъект Цикл
		Запись.КОтправке = (
			Запись.СостояниеСведений = ВозможныеСостояния.ПодготовленКОтправке
			Или Запись.СостояниеСведений = ВозможныеСостояния.ОшибкаПриОтправке);
		Запись.НаОформлении = (
			Запись.СостояниеСведений = ВозможныеСостояния.ВПроцессеПодготовки
			Или Запись.СостояниеСведений = ВозможныеСостояния.ПринятСЗамечаниями
			Или Запись.СостояниеСведений = ВозможныеСостояния.НеПринят
			Или Запись.СостояниеСведений = ВозможныеСостояния.ТребуетсяАктуализировать
			Или Запись.СостояниеСведений = ВозможныеСостояния.ТребуетсяСоздать
			Или Запись.СостояниеСведений = ВозможныеСостояния.ТребуетсяАннулировать
			Или Запись.СостояниеСведений = ВозможныеСостояния.Несоответствие);
		Запись.ВРаботе = (ЗначениеЗаполнено(Запись.СостояниеСведений)
			И Запись.СостояниеСведений <> ВозможныеСостояния.НеПланируетсяОтправлять
			И Запись.СостояниеСведений <> ВозможныеСостояния.Принят);
		Если Запись.ДатаОтправки >= Запись.МаксимальнаяДатаОтправки Тогда;
			Запись.МаксимальнаяДатаОтправки = '00010101';
		КонецЕсли;
		Если Запись.СостояниеСведений = ВозможныеСостояния.ТребуетсяСоздать
			И Запись.ПоляТребующиеАктуализации = ""
			И Не ЗначениеЗаполнено(Запись.ПоследниеСведения) Тогда
			Запись.ПоляТребующиеАктуализации = НСтр("ru = 'Требуется первичная отправка сведений';
													|en = 'Initial details sending is required'");
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли

