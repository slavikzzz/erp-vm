#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроверкаИзмененияПравилаСвязи();
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверкаИзмененияПравилаСвязи()
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТекущиеЗаписи", ЭтотОбъект.Выгрузить(, "СтатьяБюджетов, СвязанныйПоказательБюджетов"));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТекущиеЗаписи.СтатьяБюджетов КАК СтатьяБюджетов,
	|	ТекущиеЗаписи.СвязанныйПоказательБюджетов КАК СвязанныйПоказательБюджетов
	|ПОМЕСТИТЬ ТекущиеЗаписи
	|ИЗ
	|	&ТекущиеЗаписи КАК ТекущиеЗаписи
	|;
	|
	|ВЫБРАТЬ
	|	СвязиПоказателейБюджетов.СтатьяБюджетов КАК СтатьяБюджетов,
	|	СвязиПоказателейБюджетов.СвязанныйПоказательБюджетов КАК СвязанныйПоказательБюджетов
	|ПОМЕСТИТЬ ПредыдущиеЗаписиНабора
	|ИЗ
	|	РегистрСведений.СвязиПоказателейБюджетов КАК СвязиПоказателейБюджетов
	|ГДЕ
	|	&ОтборПоСтатье
	|	И &ОтборПоПоказателю
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПредыдущиеЗаписиНабора.СтатьяБюджетов КАК СтатьяПоказатель
	|ПОМЕСТИТЬ ОбъектыПроверки
	|ИЗ
	|	ПредыдущиеЗаписиНабора КАК ПредыдущиеЗаписиНабора
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаПолученияФактаПоСтатьямБюджетов КАК ПравилаПолученияФактаПоСтатьямБюджетов
	|		ПО ПредыдущиеЗаписиНабора.СтатьяБюджетов = ПравилаПолученияФактаПоСтатьямБюджетов.СтатьяБюджетов
	|			И (ПравилаПолученияФактаПоСтатьямБюджетов.ПромежуточноеКэшированиеРезультатовРаботыПравил)
	|			И (НЕ ПравилаПолученияФактаПоСтатьямБюджетов.ПометкаУдаления)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ТекущиеЗаписи.СтатьяБюджетов КАК СтатьяПоказатель
	|ИЗ
	|	ТекущиеЗаписи КАК ТекущиеЗаписи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаПолученияФактаПоСтатьямБюджетов КАК ПравилаПолученияФактаПоСтатьямБюджетов
	|		ПО ТекущиеЗаписи.СтатьяБюджетов = ПравилаПолученияФактаПоСтатьямБюджетов.СтатьяБюджетов
	|			И (ПравилаПолученияФактаПоСтатьямБюджетов.ПромежуточноеКэшированиеРезультатовРаботыПравил)
	|			И (НЕ ПравилаПолученияФактаПоСтатьямБюджетов.ПометкаУдаления)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ПредыдущиеЗаписиНабора.СвязанныйПоказательБюджетов
	|ИЗ
	|	ПредыдущиеЗаписиНабора КАК ПредыдущиеЗаписиНабора
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаПолученияФактаПоСтатьямБюджетов КАК ПравилаПолученияФактаПоСтатьямБюджетов
	|		ПО ПредыдущиеЗаписиНабора.СтатьяБюджетов = ПравилаПолученияФактаПоСтатьямБюджетов.СтатьяБюджетов
	|			И (ПравилаПолученияФактаПоСтатьямБюджетов.ПромежуточноеКэшированиеРезультатовРаботыПравил)
	|			И (НЕ ПравилаПолученияФактаПоСтатьямБюджетов.ПометкаУдаления)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ТекущиеЗаписи.СвязанныйПоказательБюджетов
	|ИЗ
	|	ТекущиеЗаписи КАК ТекущиеЗаписи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаПолученияФактаПоСтатьямБюджетов КАК ПравилаПолученияФактаПоСтатьямБюджетов
	|		ПО ТекущиеЗаписи.СтатьяБюджетов = ПравилаПолученияФактаПоСтатьямБюджетов.СтатьяБюджетов
	|			И (ПравилаПолученияФактаПоСтатьямБюджетов.ПромежуточноеКэшированиеРезультатовРаботыПравил)
	|			И (НЕ ПравилаПолученияФактаПоСтатьямБюджетов.ПометкаУдаления)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СтатьяПоказатель";
	
	Если ЭтотОбъект.Отбор.СтатьяБюджетов.Использование
		И ЗначениеЗаполнено(ЭтотОбъект.Отбор.СтатьяБюджетов.Значение)
		И ТипЗнч(ЭтотОбъект.Отбор.СтатьяБюджетов.Значение) = Тип("СправочникСсылка.СтатьиБюджетов") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоСтатье", "СвязиПоказателейБюджетов.СтатьяБюджетов = &СтатьяБюджетов");
		Запрос.УстановитьПараметр("СтатьяБюджетов", ЭтотОбъект.Отбор.СтатьяБюджетов.Значение);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоСтатье", "ИСТИНА");
	КонецЕсли;
	
	Если ЭтотОбъект.Отбор.СвязанныйПоказательБюджетов.Использование
		И ЗначениеЗаполнено(ЭтотОбъект.Отбор.СвязанныйПоказательБюджетов.Значение)
		И ТипЗнч(ЭтотОбъект.Отбор.СвязанныйПоказательБюджетов.Значение) = Тип("СправочникСсылка.ПоказателиБюджетов") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоПоказателю", "СвязиПоказателейБюджетов.СвязанныйПоказательБюджетов = &СвязанныйПоказательБюджетов");
		Запрос.УстановитьПараметр("СвязанныйПоказательБюджетов", ЭтотОбъект.Отбор.СвязанныйПоказательБюджетов.Значение);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоПоказателю", "ИСТИНА");
	КонецЕсли;
	
	ОчищатьКэшПоВидамБюджетов = Ложь;
	
	Результаты = Запрос.ВыполнитьПакет();
	
	ЕстьКэшируемыеПравила = Ложь;
	ТаблицаПравил = Результаты[2].Выгрузить();
	Если ТаблицаПравил.Количество() > 0
		И ТаблицаПравил[0].Количество <> 0 Тогда
		
		ЕстьКэшируемыеПравила = Истина;
	КонецЕсли;
	Если ЕстьКэшируемыеПравила Тогда
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Таблица.Ссылка КАК ВидБюджета
		|ИЗ
		|	Справочник.ВидыБюджетов.КэшСтатейИПоказателей КАК Таблица
		|ГДЕ
		|	Таблица.СтатьяПоказатель В
		|			(ВЫБРАТЬ
		|				ОбъектыПроверки.СтатьяПоказатель
		|			ИЗ
		|				ОбъектыПроверки)";
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			НаборПоВидуБюджета = РегистрыСведений.КэшВспомогательныхДанныхВидаБюджета.СоздатьНаборЗаписей();
			НаборПоВидуБюджета.Отбор.ВидБюджета.Установить(Выборка.ВидБюджета);
			НаборПоВидуБюджета.Записать(Истина);
		КонецЦикла;
		
	КонецЕсли;
	
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
