
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Определяет, есть ли расчет долей за месяц
//
// Параметры:
//  СписокОрганизаций	 - Массив, ФиксированныйМассив из СправочникСсылка.Организации - организации
//  Период				 - Дата - дата прошлого месяца
// 
// Возвращаемое значение:
//  Булево - Истина, если найден расчет долей за период
//
Функция ЕстьРасчетДолейЗаМесяц(СписокОрганизаций, Период) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("СписокОрганизаций", 		СписокОрганизаций);
	Запрос.УстановитьПараметр("НачалоПрошлогоМесяца", 	НачалоМесяца(Период));
	Запрос.УстановитьПараметр("КонецПрошлогоМесяца", 	КонецМесяца(Период));
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	РасчетДолейБазыНалогаНаПрибыль.ПериодРасчета
	|ИЗ
	|	РегистрСведений.РасчетДолейБазыНалогаНаПрибыль КАК РасчетДолейБазыНалогаНаПрибыль
	|ГДЕ
	|	РасчетДолейБазыНалогаНаПрибыль.Организация В(&СписокОрганизаций)
	|	И РасчетДолейБазыНалогаНаПрибыль.ПериодРасчета МЕЖДУ &НачалоПрошлогоМесяца И &КонецПрошлогоМесяца
	|	И РасчетДолейБазыНалогаНаПрибыль.Активность";
	
	Результат = Запрос.Выполнить(); 
	Возврат (НЕ Результат.Пустой());
	
КонецФункции

// Определяет, есть ли расчеты по закрытым подразделениям
//
// Параметры:
//  Организация		 - СправочникСсылка.Организации - организация
//  НачалоПериода	 - Дата - начало отчетного периода
//  КонецПериода	 - Дата - конец отчетного периода
// 
// Возвращаемое значение:
//  Булево - Истина, если есть расчеты по закрытым подразделениям
//
Функция ЕстьРасчетыПоЗакрытымПодразделениям(Организация, НачалоПериода, КонецПериода) Экспорт 
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация", 	Организация);
	Запрос.УстановитьПараметр("НачалоПериода", 	НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", 	КонецПериода);
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	РасчетДолейБазыНалогаНаПрибыль.Организация
	|ИЗ
	|	РегистрСведений.РасчетДолейБазыНалогаНаПрибыль КАК РасчетДолейБазыНалогаНаПрибыль
	|ГДЕ
	|	РасчетДолейБазыНалогаНаПрибыль.Организация = &Организация
	|	И РасчетДолейБазыНалогаНаПрибыль.ПериодРасчета МЕЖДУ &НачалоПериода И &КонецПериода
	|	И РасчетДолейБазыНалогаНаПрибыль.Закрыто
	|	И РасчетДолейБазыНалогаНаПрибыль.Активность";
	
	Результат = Запрос.Выполнить(); 
	Возврат (НЕ Результат.Пустой());
	
КонецФункции

// Для заполнения декларации по налогу на прибыль получает общую налоговую базу,
// рассчитанную для закрытых в течение текущего отчетного периода подразделений.
// 
// Параметры:
// 	КонтекстРасчета - см. НалогНаПрибыльДекларация.НовыйКонтекстРасчета
// Возвращаемое значение:
// 	Число, Неопределено - налоговая база закрытых подразделений (число),
// 	или в отчетном периоде не закрывались подразделения (неопределено).
Функция НалоговаяБазаЗакрытыхПодразделений(КонтекстРасчета) Экспорт
	
	Если Не БухгалтерскийУчетПереопределяемый.ВестиУчетПоПодразделениям() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(СУММА(Распределение.НалоговаяБаза), НЕОПРЕДЕЛЕНО) КАК НалоговаяБазаЗакрытыхПодразделений
	|ИЗ
	|	РегистрСведений.РасчетДолейБазыНалогаНаПрибыль КАК Распределение
	|ГДЕ
	|	Распределение.Организация = &Организация
	|	И Распределение.ПериодРасчета МЕЖДУ &НачалоМесяца И &КонецПериода
	|	И Распределение.Закрыто
	|	И Распределение.Активность";
		
	Запрос.УстановитьПараметр("Организация",   КонтекстРасчета.Организация);
	Запрос.УстановитьПараметр("НачалоМесяца",  НачалоМесяца(КонтекстРасчета.КонецПериода)); // В каждом месяце формируются записи, характеризующие весь налоговый период
	Запрос.УстановитьПараметр("КонецПериода",  КонтекстРасчета.КонецПериода);
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий(); // в выборке всегда 1 запись	
	
	Возврат Выборка.НалоговаяБазаЗакрытыхПодразделений; 
	
КонецФункции	

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#КонецЕсли
