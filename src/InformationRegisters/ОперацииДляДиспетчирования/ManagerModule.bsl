//++ Устарело_Производство21
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполняет регистр по данным пооперационного расписания производства.
//
// Параметры:
//  МаршрутныеЛисты	- Массив - маршрутные листы, расписание которых будет использовано для заполнения.
//
Процедура ЗаполнитьОперацииДляДиспетчирования(Знач МаршрутныеЛисты) Экспорт
	
	ДокументыОбработать = Новый Массив;
	Для каждого Элемент Из МаршрутныеЛисты Цикл
		ДокументыОбработать.Добавить(Элемент);
	КонецЦикла;
	
	ТекстЗапроса = ТекстЗапросаЗаполнитьОперации();
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("МаршрутныеЛисты", ДокументыОбработать);
	
	ВыборкаИтоги = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаИтоги.Следующий() Цикл
		
		НаборЗаписей = РегистрыСведений.ОперацииДляДиспетчирования.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.МаршрутныйЛист.Установить(ВыборкаИтоги.МаршрутныйЛист);
		
		Выборка = ВыборкаИтоги.Выбрать();
		Пока Выборка.Следующий() Цикл
			Запись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, Выборка);
		КонецЦикла;
		
		НаборЗаписей.Записать();
		
		ИндексДокумента = ДокументыОбработать.Найти(ВыборкаИтоги.МаршрутныйЛист);
		ДокументыОбработать.Удалить(ИндексДокумента);
		
	КонецЦикла;
	
	Для каждого МаршрутныйЛист Из ДокументыОбработать Цикл
		НаборЗаписей = РегистрыСведений.ОперацииДляДиспетчирования.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.МаршрутныйЛист.Установить(МаршрутныйЛист);
		НаборЗаписей.Записать();
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаЗаполнитьОперации()
	
	Результат = 
	"ВЫБРАТЬ
	|	ПооперационноеРасписание.МаршрутныйЛист КАК МаршрутныйЛист,
	|	ПооперационноеРасписание.ИдентификаторОперации КАК ИдентификаторОперации,
	|	ПооперационноеРасписание.ТактПараллельнойЗагрузки,
	|	ВЫРАЗИТЬ(ПооперационноеРасписание.РабочийЦентр КАК Справочник.РабочиеЦентры) КАК РабочийЦентр,
	|	ПооперационноеРасписание.Подразделение,
	|	ПооперационноеРасписание.СтатусВыполнения,
	|	ПооперационноеРасписание.Штрихкод,
	|	МИНИМУМ(ПооперационноеРасписание.Начало) КАК Начало,
	|	МАКСИМУМ(ПооперационноеРасписание.Окончание) КАК Окончание,
	|	МАКСИМУМ(ПооперационноеРасписание.Загрузка) КАК Загрузка
	|ПОМЕСТИТЬ ЗагрузкаПоТактам
	|ИЗ
	|	РегистрСведений.ПооперационноеРасписание КАК ПооперационноеРасписание
	|ГДЕ
	|	ПооперационноеРасписание.Моделирование = ЛОЖЬ
	|	И ПооперационноеРасписание.Вспомогательная = ЛОЖЬ
	|	И ПооперационноеРасписание.МаршрутныйЛист В(&МаршрутныеЛисты)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПооперационноеРасписание.ИдентификаторОперации,
	|	ВЫРАЗИТЬ(ПооперационноеРасписание.РабочийЦентр КАК Справочник.РабочиеЦентры),
	|	ПооперационноеРасписание.СтатусВыполнения,
	|	ПооперационноеРасписание.МаршрутныйЛист,
	|	ПооперационноеРасписание.Подразделение,
	|	ПооперационноеРасписание.Штрихкод,
	|	ПооперационноеРасписание.ТактПараллельнойЗагрузки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗагрузкаПоТактам.МаршрутныйЛист КАК МаршрутныйЛист,
	|	ЗагрузкаПоТактам.ИдентификаторОперации,
	|	ЗагрузкаПоТактам.РабочийЦентр,
	|	ЗагрузкаПоТактам.Подразделение,
	|	ЗагрузкаПоТактам.СтатусВыполнения,
	|	ЗагрузкаПоТактам.Штрихкод,
	|	МИНИМУМ(ЗагрузкаПоТактам.Начало) КАК Начало,
	|	МАКСИМУМ(ЗагрузкаПоТактам.Окончание) КАК Окончание,
	|	СУММА(ЗагрузкаПоТактам.Загрузка) КАК Загрузка
	|ИЗ
	|	ЗагрузкаПоТактам КАК ЗагрузкаПоТактам
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗагрузкаПоТактам.МаршрутныйЛист,
	|	ЗагрузкаПоТактам.ИдентификаторОперации,
	|	ЗагрузкаПоТактам.РабочийЦентр,
	|	ЗагрузкаПоТактам.Подразделение,
	|	ЗагрузкаПоТактам.СтатусВыполнения,
	|	ЗагрузкаПоТактам.Штрихкод
	|ИТОГИ ПО
	|	МаршрутныйЛист";
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли
//-- Устарело_Производство21