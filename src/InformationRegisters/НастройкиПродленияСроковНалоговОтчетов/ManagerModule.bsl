#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает настройки для переноса сроков уплаты налогов и представления отчетов для организации
//
// Параметры:
//  Организация - СправочникСсылка.Организации - организация, для которой требуется получить настройки
// 
// Возвращаемое значение:
//   Структура - см. НовыйНастройкиПродленияСроков, поля:
//     * ОснованияПродления                              - Массив
//     * ДеятельностьОтнесенаКПострадавшимОтКоронавируса - Булево
//
Функция ПараметрыПродленияСроков(Организация) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НастройкиПродленияСроковНалоговОтчетов.ДеятельностьОтнесенаКПострадавшимОтКоронавируса КАК ДеятельностьОтнесенаКПострадавшимОтКоронавируса
	|ИЗ
	|	РегистрСведений.НастройкиПродленияСроковНалоговОтчетов КАК НастройкиПродленияСроковНалоговОтчетов
	|ГДЕ
	|	НастройкиПродленияСроковНалоговОтчетов.Организация = &Организация";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Результат = НовыйНастройкиПродленияСроков();
	
	Если Выборка.Следующий() Тогда
		
		Результат.Вставить("ДеятельностьОтнесенаКПострадавшимОтКоронавируса", Выборка.ДеятельностьОтнесенаКПострадавшимОтКоронавируса);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает период, в рамках которого возможен перенос сроков уплаты налогов и авансовых платежей
// в соответствии с Федеральным законом от 01.04.2020 № 102-ФЗ.
// 
// Возвращаемое значение:
//   Структура
//	 	* Начало - Дата
//		* Окончание - Дата
//
Функция ПериодПереносаСроковНалоговОтчетов() Экспорт
	
	Период = Новый Структура;
	Период.Вставить("Начало",    '20200101');
	Период.Вставить("Окончание", '20201231');
	
	Возврат Период;
	
КонецФункции	

// Проверяет, продлены ли для организации сроки уплаты налогов в соответствии с Постановлением правительства РФ №409 от 02.04.2020
// Внимание! Функция не учитывает период действия освобождения - его нужно проверять отдельно.
//
// Параметры:
//  Организация	 - СправочникСсылка.Организации	 - организация, для которой проверяются настройки
// 
// Возвращаемое значение:
//   - Булево
//
Функция ПродленыСрокиУплатыНалоговПредставленияОтчетов(Организация) Экспорт 
	
	Возврат Ложь;
	
КонецФункции 

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"ПрисоединитьДополнительныеТаблицы
	|ЭтотСписок КАК ЭтотСписок
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОбособленныеПодразделения 
	|	ПО ОбособленныеПодразделения.ГоловнаяОрганизация = ЭтотСписок.Организация.ГоловнаяОрганизация
	|;
	|РазрешитьЧтение
	|ГДЕ
	| ЗначениеРазрешено(ОбособленныеПодразделения.Ссылка)
	|
	|;
	|РазрешитьИзменениеЕслиРазрешеноЧтение
	|ГДЕ
	| ЗначениеРазрешено(ЭтотСписок.Организация)";
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НовыйНастройкиПродленияСроков()
	
	Результат = Новый Структура;
	
	Результат.Вставить("ОснованияПродления",          Новый Массив);
	Результат.Вставить("ДеятельностьОтнесенаКПострадавшимОтКоронавируса", Ложь);
	
	Возврат Результат;
	
КонецФункции


#КонецОбласти

#КонецЕсли