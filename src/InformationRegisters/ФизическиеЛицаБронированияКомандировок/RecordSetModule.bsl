#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)

	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;

	ФизическоеЛицо = Отбор.ФизическоеЛицо.Значение;
	СистемаБронирования = Отбор.СистемаБронирования.Значение;
	ИспользованиеФизическоеЛицо = Отбор["ФизическоеЛицо"].Использование;
	ИспользованиеСистемаБронирования = Отбор["СистемаБронирования"].Использование;

	Если Не ИспользованиеФизическоеЛицо Или Не ИспользованиеСистемаБронирования 
		Или Не ЗначениеЗаполнено(ФизическоеЛицо) Или Не ЗначениеЗаполнено(СистемаБронирования) Тогда
		Возврат;
	КонецЕсли;

	Настройка = РегулярнаяОтправкаСотрудников.НастройкаОтправкиСотрудника(СистемаБронирования, ФизическоеЛицо);
	ДополнительныеСвойства.Вставить("ПредыдущаяНастройкаСотрудника", Настройка);

КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)

	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;

	Если Не ДополнительныеСвойства.Свойство("ПредыдущаяНастройкаСотрудника") Тогда
		Возврат;
	КонецЕсли;

	Настройка = ДополнительныеСвойства.ПредыдущаяНастройкаСотрудника;

	Если Количество() = 0 Тогда
		Если Настройка.Отправляется Тогда
			РегистрыСведений.ИзмененияФизическихЛицБронированияКомандировок.ЗарегистрироватьСотрудниковВСпискеОтправки(
				Настройка.СистемаБронирования, Настройка.ФизическоеЛицо);
		КонецЕсли;
	Иначе
		НовыйНаборЗаписей = ЭтотОбъект[0];
		Если НовыйНаборЗаписей.ПричинаОтправки <> Настройка.ПричинаОтправки 
			Или НовыйНаборЗаписей.Отправляется <> Настройка.Отправляется Тогда
			РегистрыСведений.ИзмененияФизическихЛицБронированияКомандировок.ЗарегистрироватьСотрудниковВСпискеОтправки(
				Настройка.СистемаБронирования, Настройка.ФизическоеЛицо);
		Иначе
			РегистрыСведений.ИзмененияФизическихЛицБронированияКомандировок.УдалитьСотрудниковИзСпискаОтправки(
				Настройка.СистемаБронирования, Настройка.ФизическоеЛицо);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли