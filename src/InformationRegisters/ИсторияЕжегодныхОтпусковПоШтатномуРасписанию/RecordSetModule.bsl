#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ТекущийНабор",	ЭтотОбъект);
	Запрос.УстановитьПараметр("Регистратор",	Отбор.Регистратор.Значение);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТекущийНабор.ПозицияШтатногоРасписания
	|ПОМЕСТИТЬ ВТТекущийНабор
	|ИЗ
	|	&ТекущийНабор КАК ТекущийНабор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсторияЕжегодныхОтпусковПоШтатномуРасписанию.ПозицияШтатногоРасписания КАК ПозицияШтатногоРасписания
	|ИЗ
	|	РегистрСведений.ИсторияЕжегодныхОтпусковПоШтатномуРасписанию КАК ИсторияЕжегодныхОтпусковПоШтатномуРасписанию
	|ГДЕ
	|	ИсторияЕжегодныхОтпусковПоШтатномуРасписанию.Регистратор = &Регистратор
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ТекущийНабор.ПозицияШтатногоРасписания
	|ИЗ
	|	ВТТекущийНабор КАК ТекущийНабор";
	
	МассивОбновляемыхПозиций = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ПозицияШтатногоРасписания");
	
	ДополнительныеСвойства.Вставить("МассивОбновляемыхПозиций", МассивОбновляемыхПозиций);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ДополнительныеСвойства.Свойство("МассивОбновляемыхПозиций")
		Или ДополнительныеСвойства.МассивОбновляемыхПозиций.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("СписокПозицийШтатногоРасписания", ДополнительныеСвойства.МассивОбновляемыхПозиций);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИсторияИспользованияШтатногоРасписания.ПозицияШтатногоРасписания,
	|	МАКСИМУМ(ИсторияИспользованияШтатногоРасписания.Дата) КАК Дата
	|ПОМЕСТИТЬ ВТПоследниеДатыИспользованияПозиций
	|ИЗ
	|	РегистрСведений.ИсторияИспользованияШтатногоРасписания КАК ИсторияИспользованияШтатногоРасписания
	|ГДЕ
	|	ИсторияИспользованияШтатногоРасписания.Используется
	|	И ИсторияИспользованияШтатногоРасписания.ПозицияШтатногоРасписания В(&СписокПозицийШтатногоРасписания)
	|
	|СГРУППИРОВАТЬ ПО
	|	ИсторияИспользованияШтатногоРасписания.ПозицияШтатногоРасписания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоследниеДатыИспользованияПозиций.ПозицияШтатногоРасписания КАК ПозицияШтатногоРасписания,
	|	ИсторияЕжегодныхОтпусковПоШтатномуРасписанию.ВидЕжегодногоОтпуска КАК ВидЕжегодногоОтпуска,
	|	ИсторияЕжегодныхОтпусковПоШтатномуРасписанию.КоличествоДнейВГод
	|ПОМЕСТИТЬ ВТОтпускаПозиций
	|ИЗ
	|	ВТПоследниеДатыИспользованияПозиций КАК ПоследниеДатыИспользованияПозиций
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияЕжегодныхОтпусковПоШтатномуРасписанию КАК ИсторияЕжегодныхОтпусковПоШтатномуРасписанию
	|		ПО ПоследниеДатыИспользованияПозиций.ПозицияШтатногоРасписания = ИсторияЕжегодныхОтпусковПоШтатномуРасписанию.ПозицияШтатногоРасписания
	|			И ПоследниеДатыИспользованияПозиций.Дата = ИсторияЕжегодныхОтпусковПоШтатномуРасписанию.Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЕСТЬNULL(ОтпускаПозиций.ПозицияШтатногоРасписания, ШтатноеРасписаниеЕжегодныеОтпуска.Ссылка) КАК Ссылка
	|ПОМЕСТИТЬ ВТИзмененныеПозиции
	|ИЗ
	|	ВТОтпускаПозиций КАК ОтпускаПозиций
	|		ПОЛНОЕ СОЕДИНЕНИЕ Справочник.ШтатноеРасписание.ЕжегодныеОтпуска КАК ШтатноеРасписаниеЕжегодныеОтпуска
	|		ПО ОтпускаПозиций.ПозицияШтатногоРасписания = ШтатноеРасписаниеЕжегодныеОтпуска.Ссылка
	|			И ОтпускаПозиций.ВидЕжегодногоОтпуска = ШтатноеРасписаниеЕжегодныеОтпуска.ВидЕжегодногоОтпуска
	|ГДЕ
	|	(ОтпускаПозиций.ВидЕжегодногоОтпуска ЕСТЬ NULL 
	|			ИЛИ ШтатноеРасписаниеЕжегодныеОтпуска.ВидЕжегодногоОтпуска ЕСТЬ NULL 
	|			ИЛИ ЕСТЬNULL(ОтпускаПозиций.КоличествоДнейВГод, 0) <> ЕСТЬNULL(ШтатноеРасписаниеЕжегодныеОтпуска.КоличествоДнейВГод, 0))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтпускаПозиций.ПозицияШтатногоРасписания КАК ПозицияШтатногоРасписания,
	|	ОтпускаПозиций.ВидЕжегодногоОтпуска,
	|	ОтпускаПозиций.КоличествоДнейВГод
	|ИЗ
	|	ВТОтпускаПозиций КАК ОтпускаПозиций
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТИзмененныеПозиции КАК ИзмененныеПозиции
	|		ПО ОтпускаПозиций.ПозицияШтатногоРасписания = ИзмененныеПозиции.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПозицияШтатногоРасписания,
	|	ОтпускаПозиций.ВидЕжегодногоОтпуска.Предопределенный УБЫВ,
	|	ОтпускаПозиций.ВидЕжегодногоОтпуска.ПредоставлятьОтпускВсемСотрудникам УБЫВ,
	|	ОтпускаПозиций.ВидЕжегодногоОтпуска";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.СледующийПоЗначениюПоля("ПозицияШтатногоРасписания") Цикл
		ПозицияОбъект = Выборка.ПозицияШтатногоРасписания.ПолучитьОбъект();
		ПозицияОбъект.ЕжегодныеОтпуска.Очистить();
		
		Пока Выборка.Следующий() И ЗначениеЗаполнено(Выборка.ВидЕжегодногоОтпуска) Цикл
			ЗаполнитьЗначенияСвойств(ПозицияОбъект.ЕжегодныеОтпуска.Добавить(), Выборка);
		КонецЦикла;
		
		УправлениеШтатнымРасписанием.ОтключитьОбновлениеСтруктурыШтатногоРасписания(ПозицияОбъект);
		УправлениеШтатнымРасписанием.ОтключитьОбновлениеНастройкиИспользованияСтраховыхВзносовПоКлассамУсловийТруда(ПозицияОбъект);
		УправлениеШтатнымРасписанием.ОтключитьОбновлениеНастройкиПубликации(ПозицияОбъект);
		
		ПозицияОбъект.Записать();
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли