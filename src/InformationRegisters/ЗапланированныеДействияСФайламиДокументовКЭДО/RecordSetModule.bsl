#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Отбор.ПрисоединенныйФайл.Использование Тогда
		Возврат
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПрисоединенныйФайл", Отбор.ПрисоединенныйФайл.Значение);
	Запрос.УстановитьПараметр("ИнтерактивныеДействия", Перечисления.ДействияСФайламиДокументовКЭДО.ИнтерактивныеДействия());
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗапланированныеДействияСФайламиДокументовКЭДО.ПрисоединенныйФайл КАК ПрисоединенныйФайл,
		|	ЗапланированныеДействияСФайламиДокументовКЭДО.Действие КАК Действие
		|ИЗ
		|	РегистрСведений.ЗапланированныеДействияСФайламиДокументовКЭДО КАК ЗапланированныеДействияСФайламиДокументовКЭДО
		|ГДЕ
		|	ЗапланированныеДействияСФайламиДокументовКЭДО.Действие В(&ИнтерактивныеДействия)
		|	И ЗапланированныеДействияСФайламиДокументовКЭДО.ПрисоединенныйФайл = &ПрисоединенныйФайл";
	
	ДанныеКОбновлению = Запрос.Выполнить().Выгрузить();
	
	ДанныеТекущегоНабора = Выгрузить();
	ДанныеТекущегоНабора.Свернуть("ПрисоединенныйФайл,Действие");
	ЗаписиКУдалению = Новый Массив();
	ЗаписиКДобавлению = Новый Массив();
	Для Каждого ДанныеЗаписи Из ДанныеТекущегоНабора Цикл
		СтруктураОтбора = Новый Структура("ПрисоединенныйФайл,Действие");
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, ДанныеЗаписи);
		ПрежниеЗаписи = ДанныеКОбновлению.НайтиСтроки(СтруктураОтбора);
		Если ПрежниеЗаписи.Количество() > 0 Тогда
			ЗаписиКУдалению.Добавить(ПрежниеЗаписи[0]);
		Иначе
			ЗаписиКДобавлению.Добавить(СтруктураОтбора);
		КонецЕсли;
	КонецЦикла;
	Для Каждого ЗаписьКУдалению Из ЗаписиКУдалению Цикл
		ДанныеКОбновлению.Удалить(ЗаписьКУдалению);
	КонецЦикла;
	Для Каждого ЗаписьКДобавлению Из ЗаписиКДобавлению Цикл
		ЗаполнитьЗначенияСвойств(ДанныеКОбновлению.Добавить(), ЗаписьКДобавлению);
	КонецЦикла;
	Если ДанныеКОбновлению.Количество() > 0 Тогда
		ФайлыДокументовКЭДО = ДанныеКОбновлению.ВыгрузитьКолонку("ПрисоединенныйФайл");
		КадровыйЭДО.ЗарегистрироватьФайлыДокументовКЭДОКПересчетуСостояний(ФайлыДокументовКЭДО);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли