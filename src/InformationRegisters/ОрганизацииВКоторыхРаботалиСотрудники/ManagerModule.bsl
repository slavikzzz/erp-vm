
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

#Область ОбменДанными

// Пересчитывает зависимые данные после загрузки сообщения обмена
//
// Параметры:
//		ЗависимыеДанные - ТаблицаЗначений:
//			* ВедущиеМетаданные - ОбъектМетаданных - Метаданные ведущих данных
//			* ЗависимыеМетаданные - ОбъектМетаданных - Метаданные текущего объекта
//			* ВедущиеДанные - Массив объектов, заполненный при загрузке сообщения обмена
//				по этим объектам требуется обновить зависимые данные
//
Процедура ОбновитьЗависимыеДанныеПослеЗагрузкиОбменаДанными(ЗависимыеДанные) Экспорт
	
	ТаблицаАнализаИзменений = КадровыйУчет.ТаблицаАнализаИзменений();
	
	Для Каждого СтрокаТаблицы Из ЗависимыеДанные Цикл
		
		Для Каждого ВедущиеДанные Из СтрокаТаблицы.ВедущиеДанные Цикл
			
			Если СтрокаТаблицы.ВедущиеМетаданные = Метаданные.Справочники.Сотрудники Тогда
				
				Если ВедущиеДанные.ДополнительныеСвойства.Свойство("ЭтоНовый")
					И ВедущиеДанные.ДополнительныеСвойства.ЭтоНовый = Истина Тогда
					
					СтрокаТаблицыАнализа = ТаблицаАнализаИзменений.Добавить();
					СтрокаТаблицыАнализа.Сотрудник = ВедущиеДанные.Ссылка;
					СтрокаТаблицыАнализа.Организация = ВедущиеДанные.ГоловнаяОрганизация;
					СтрокаТаблицыАнализа.ФлагИзменений = 1;
					
				КонецЕсли;
				
			ИначеЕсли СтрокаТаблицы.ВедущиеМетаданные = Метаданные.РегистрыСведений.ПериодыДействияДоговоровГражданскоПравовогоХарактера Тогда
				ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаАнализаИзменений, ВедущиеДанные.ДополнительныеСвойства.ТаблицаАнализаИзменений);
			Иначе
				
				Для каждого ЗаписьНабора Из ВедущиеДанные Цикл
					
					СтрокаТаблицыАнализа = ТаблицаАнализаИзменений.Добавить();
					СтрокаТаблицыАнализа.Сотрудник = ЗаписьНабора.Сотрудник;
					СтрокаТаблицыАнализа.Организация = ЗаписьНабора.Организация;
					СтрокаТаблицыАнализа.ФлагИзменений = 1;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	КадровыйУчет.ОбработатьИзменениеОрганизацийВНабореПоТаблицеИзменений(ТаблицаАнализаИзменений);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
