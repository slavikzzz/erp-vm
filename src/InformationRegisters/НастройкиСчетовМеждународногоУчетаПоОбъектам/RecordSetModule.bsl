#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ЗаписьНабора Из ЭтотОбъект Цикл
		Если ЗаписьНабора.ТипНастройки = Перечисления.ТипыНастроекОтраженияВМеждународномУчете.ОбщаяПоОбъектуУчета Тогда
			ЗаписьНабора.Организация = Неопределено;
			ЗаписьНабора.МестоУчета = Неопределено;
		ИначеЕсли НЕ ЗначениеЗаполнено(ЗаписьНабора.МестоУчета) Тогда
			ЗаписьНабора.МестоУчета = Неопределено;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(ЗаписьНабора.ОбъектНастройки) Тогда
			ЗаписьНабора.ОбъектНастройки = Неопределено;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	СчетаРасчетовПоНДС = ВыгрузитьКолонки();
	
	Для Каждого ЗаписьНабора Из ЭтотОбъект Цикл
		Если ЗначениеЗаполнено(ЗаписьНабора.СчетУчета) ИЛИ ЗначениеЗаполнено(ЗаписьНабора.СчетУчетаДолгосрочный) Тогда
			СчетаУчетаТребующиеНастройки = РегистрыСведений.СчетаМеждународногоУчетаТребующиеНастройки.СоздатьНаборЗаписей();
			СчетаУчетаТребующиеНастройки.Отбор.ПланСчетов.Установить(ЗаписьНабора.ПланСчетов);
			СчетаУчетаТребующиеНастройки.Отбор.НастройкаФормированияПроводок.Установить(ЗаписьНабора.НастройкаФормированияПроводок);
			СчетаУчетаТребующиеНастройки.Отбор.ОбъектНастройки.Установить(ЗаписьНабора.ОбъектНастройки);
			СчетаУчетаТребующиеНастройки.Отбор.ОбъектУчета.Установить(ЗаписьНабора.ОбъектУчета);
			
			Если ЗначениеЗаполнено(ЗаписьНабора.СчетУчета) И ЗначениеЗаполнено(ЗаписьНабора.СчетУчетаДолгосрочный) Тогда
			ИначеЕсли ЗначениеЗаполнено(ЗаписьНабора.СчетУчета) Тогда
				СчетаУчетаТребующиеНастройки.Отбор.Долгосрочный.Установить(Ложь);
			Иначе
				СчетаУчетаТребующиеНастройки.Отбор.Долгосрочный.Установить(Истина);
			КонецЕсли;
			
			СчетаУчетаТребующиеНастройки.Записать();
		КонецЕсли;
		
		Если ЗаписьНабора.ОбъектУчета = Перечисления.ОбъектыФинансовогоУчета.РасчетыПоНДС
		И НЕ ЗначениеЗаполнено(ЗаписьНабора.ОбъектНастройки) Тогда
			ЗаполнитьЗначенияСвойств(СчетаРасчетовПоНДС.Добавить(), ЗаписьНабора);
		КонецЕсли;
	КонецЦикла;
	
	Если СчетаРасчетовПоНДС.Количество() > 0 Тогда
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	СчетаРасчетовПоНДС.ПланСчетов КАК ПланСчетов,
		|	СчетаРасчетовПоНДС.НастройкаФормированияПроводок КАК НастройкаФормированияПроводок,
		|	СчетаРасчетовПоНДС.ТипНастройки КАК ТипНастройки,
		|	СчетаРасчетовПоНДС.Организация КАК Организация,
		|	СчетаРасчетовПоНДС.МестоУчета КАК МестоУчета,
		|	СчетаРасчетовПоНДС.СчетУчета КАК СчетУчета,
		|	СчетаРасчетовПоНДС.СчетУчетаДолгосрочный КАК СчетУчетаДолгосрочный
		|ПОМЕСТИТЬ втСчетаРасчетовПоНДС
		|ИЗ
		|	&СчетаРасчетовПоНДС КАК СчетаРасчетовПоНДС
		|ИНДЕКСИРОВАТЬ ПО
		|	ПланСчетов,
		|	НастройкаФормированияПроводок,
		|	ТипНастройки,
		|	Организация,
		|	МестоУчета
		|
		|;
		|
		|ВЫБРАТЬ
		|	СчетаРасчетовПоНДС.ПланСчетов КАК ПланСчетов,
		|	СчетаРасчетовПоНДС.НастройкаФормированияПроводок КАК НастройкаФормированияПроводок,
		|	СчетаРасчетовПоНДС.ТипНастройки КАК ТипНастройки,
		|	ЗНАЧЕНИЕ(Перечисление.ОбъектыФинансовогоУчета.РасчетыПоНалогамИВзносам) КАК ОбъектУчета,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.НДС) КАК ОбъектНастройки,
		|	СчетаРасчетовПоНДС.Организация КАК Организация,
		|	СчетаРасчетовПоНДС.МестоУчета КАК МестоУчета,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(НастройкиСчетовУчета.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Международный.ПустаяСсылка)) = ЗНАЧЕНИЕ(ПланСчетов.Международный.ПустаяСсылка)
		|			ТОГДА СчетаРасчетовПоНДС.СчетУчета
		|		ИНАЧЕ НастройкиСчетовУчета.СчетУчета
		|	КОНЕЦ КАК СчетУчета,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(НастройкиСчетовУчета.СчетУчетаДолгосрочный, ЗНАЧЕНИЕ(ПланСчетов.Международный.ПустаяСсылка)) = ЗНАЧЕНИЕ(ПланСчетов.Международный.ПустаяСсылка)
		|			ТОГДА СчетаРасчетовПоНДС.СчетУчетаДолгосрочный
		|		ИНАЧЕ НастройкиСчетовУчета.СчетУчетаДолгосрочный
		|	КОНЕЦ КАК СчетУчетаДолгосрочный
		|ИЗ
		|	втСчетаРасчетовПоНДС КАК СчетаРасчетовПоНДС
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиСчетовМеждународногоУчетаПоОбъектам КАК НастройкиСчетовУчета
		|	ПО СчетаРасчетовПоНДС.ПланСчетов = НастройкиСчетовУчета.ПланСчетов
		|	 И СчетаРасчетовПоНДС.НастройкаФормированияПроводок = НастройкиСчетовУчета.НастройкаФормированияПроводок
		|	 И СчетаРасчетовПоНДС.ТипНастройки = НастройкиСчетовУчета.ТипНастройки
		|	 И СчетаРасчетовПоНДС.Организация = НастройкиСчетовУчета.Организация
		|	 И СчетаРасчетовПоНДС.МестоУчета = НастройкиСчетовУчета.МестоУчета
		|	 И НастройкиСчетовУчета.ОбъектУчета = ЗНАЧЕНИЕ(Перечисление.ОбъектыФинансовогоУчета.РасчетыПоНалогамИВзносам)
		|	 И НастройкиСчетовУчета.ОбъектНастройки = ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.НДС)
		|ГДЕ
		|	(ЕСТЬNULL(НастройкиСчетовУчета.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Международный.ПустаяСсылка)) = ЗНАЧЕНИЕ(ПланСчетов.Международный.ПустаяСсылка)
		|	 И СчетаРасчетовПоНДС.СчетУчета <> ЗНАЧЕНИЕ(ПланСчетов.Международный.ПустаяСсылка))
		|	ИЛИ
		|	(ЕСТЬNULL(НастройкиСчетовУчета.СчетУчетаДолгосрочный, ЗНАЧЕНИЕ(ПланСчетов.Международный.ПустаяСсылка)) = ЗНАЧЕНИЕ(ПланСчетов.Международный.ПустаяСсылка)
		|	 И СчетаРасчетовПоНДС.СчетУчетаДолгосрочный <> ЗНАЧЕНИЕ(ПланСчетов.Международный.ПустаяСсылка))
		|";
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("СчетаРасчетовПоНДС", СчетаРасчетовПоНДС);
		Результат = Запрос.Выполнить();
		
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			МенеджерЗаписи = РегистрыСведений.НастройкиСчетовМеждународногоУчетаПоОбъектам.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка);
			МенеджерЗаписи.Записать(Истина);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли