#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	Перем Ошибки;
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Таблица = ЭтотОбъект.Выгрузить();
	Запрос = Новый Запрос();
	Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Период КАК Период,
	|	Таблица.НефинансовыйПоказатель КАК НефинансовыйПоказатель,
	|	Таблица.Сценарий КАК Сценарий,
	|	Таблица.Организация КАК Организация,
	|	Таблица.Подразделение КАК Подразделение,
	|	&ТекстТаблицаНабораАналитики
	|ПОМЕСТИТЬ ТаблицаНабора
	|ИЗ
	|	&Таблица КАК Таблица
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗначенияНефинансовыхПоказателей.Период КАК Период,
	|	ЗначенияНефинансовыхПоказателей.Регистратор КАК Регистратор,
	|	ЗначенияНефинансовыхПоказателей.НефинансовыйПоказатель КАК НефинансовыйПоказатель,
	|	ЗначенияНефинансовыхПоказателей.Сценарий КАК Сценарий,
	|	ЗначенияНефинансовыхПоказателей.Организация КАК Организация,
	|	ЗначенияНефинансовыхПоказателей.Подразделение КАК Подразделение,
	|	&ТекстТаблицаРегистраАналитики
	|ПОМЕСТИТЬ ТаблицаРегистра
	|ИЗ
	|	РегистрСведений.ЗначенияНефинансовыхПоказателей КАК ЗначенияНефинансовыхПоказателей
	|ГДЕ
	|	ЗначенияНефинансовыхПоказателей.Регистратор <> &Регистратор
	|	И (ЗначенияНефинансовыхПоказателей.Период,
	|		ЗначенияНефинансовыхПоказателей.НефинансовыйПоказатель,
	|		ЗначенияНефинансовыхПоказателей.Сценарий,
	|		ЗначенияНефинансовыхПоказателей.Организация,
	|		ЗначенияНефинансовыхПоказателей.Подразделение,
	|		&ТекстТаблицаРегистраУсловияАналитики) В
	|		(ВЫБРАТЬ
	|			ТаблицаНабора.Период,
	|			ТаблицаНабора.НефинансовыйПоказатель,
	|			ТаблицаНабора.Сценарий,
	|			ТаблицаНабора.Организация,
	|			ТаблицаНабора.Подразделение,
	|			&ТекстТаблицаРегистраПодзапросАналитики
	|		ИЗ
	|			ТаблицаНабора)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗначенияНефинансовыхПоказателей.Период КАК Период,
	|	ЗначенияНефинансовыхПоказателей.Регистратор КАК Регистратор,
	|	ЗначенияНефинансовыхПоказателей.НефинансовыйПоказатель КАК НефинансовыйПоказатель,
	|	ПРЕДСТАВЛЕНИЕ(ЗначенияНефинансовыхПоказателей.НефинансовыйПоказатель) КАК НефинансовыйПоказательПредставление,
	|	ПРЕДСТАВЛЕНИЕ(ЗначенияНефинансовыхПоказателей.Регистратор) КАК РегистраторПредставление
	|ИЗ
	|	ТаблицаНабора КАК ТаблицаНабора
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаРегистра КАК ЗначенияНефинансовыхПоказателей
	|		ПО ТаблицаНабора.Период = ЗначенияНефинансовыхПоказателей.Период
	|		И ТаблицаНабора.НефинансовыйПоказатель = ЗначенияНефинансовыхПоказателей.НефинансовыйПоказатель
	|		И ТаблицаНабора.Сценарий = ЗначенияНефинансовыхПоказателей.Сценарий
	|		И ТаблицаНабора.Организация = ЗначенияНефинансовыхПоказателей.Организация
	|		И ТаблицаНабора.Подразделение = ЗначенияНефинансовыхПоказателей.Подразделение
	|		И &ТекстСоединениеАналитики";
	
	МаксимальноеКоличествоАналитик = БюджетированиеКлиентСервер.МаксимальноеКоличествоАналитик();
	ТекстТаблицаНабораАналитики = "";
	ТекстТаблицаРегистраАналитики = "";
	ТекстТаблицаРегистраУсловияАналитики = "";
	ТекстТаблицаРегистраПодзапросАналитики = "";
	ТекстСоединениеАналитики = "";
	Для НомерАналитики = 1 По МаксимальноеКоличествоАналитик Цикл
		ТекстТаблицаНабораАналитики = ТекстТаблицаНабораАналитики
			+ ?(ТекстТаблицаНабораАналитики = "", "", "," + Символы.ПС + Символы.Таб)
			+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"Таблица.Аналитика%1 КАК Аналитика%1",
				НомерАналитики);
		
		ТекстТаблицаРегистраАналитики = ТекстТаблицаРегистраАналитики
			+ ?(ТекстТаблицаРегистраАналитики = "", "", "," + Символы.ПС + Символы.Таб)
			+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"ЗначенияНефинансовыхПоказателей.Аналитика%1 КАК Аналитика%1",
				НомерАналитики);
		
		ТекстТаблицаРегистраУсловияАналитики = ТекстТаблицаРегистраУсловияАналитики
			+ ?(ТекстТаблицаРегистраУсловияАналитики = "", "", "," + Символы.ПС + Символы.Таб)
			+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"ЗначенияНефинансовыхПоказателей.Аналитика%1",
				НомерАналитики);
		
		ТекстТаблицаРегистраПодзапросАналитики = ТекстТаблицаРегистраПодзапросАналитики
			+ ?(ТекстТаблицаРегистраПодзапросАналитики = "", "", "," + Символы.ПС + Символы.Таб)
			+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"ТаблицаНабора.Аналитика%1",
				НомерАналитики);
		
		ТекстСоединениеАналитики = ТекстСоединениеАналитики
			+ ?(ТекстСоединениеАналитики = "", "", Символы.ПС + Символы.Таб + Символы.Таб + "И ")
			+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"ТаблицаНабора.Аналитика%1 = ЗначенияНефинансовыхПоказателей.Аналитика%1",
				НомерАналитики);
	КонецЦикла;
	Текст = СтрЗаменить(Текст, "&ТекстТаблицаНабораАналитики", ТекстТаблицаНабораАналитики);
	Текст = СтрЗаменить(Текст, "&ТекстТаблицаРегистраАналитики", ТекстТаблицаРегистраАналитики);
	Текст = СтрЗаменить(Текст, "&ТекстТаблицаРегистраУсловияАналитики", ТекстТаблицаРегистраУсловияАналитики);
	Текст = СтрЗаменить(Текст, "&ТекстТаблицаРегистраПодзапросАналитики", ТекстТаблицаРегистраПодзапросАналитики);
	Текст = СтрЗаменить(Текст, "&ТекстСоединениеАналитики", ТекстСоединениеАналитики);
	
	Запрос.Текст = Текст;
	
	Запрос.УстановитьПараметр("Таблица", Таблица);
	Запрос.УстановитьПараметр("Регистратор", ЭтотОбъект.Отбор.Регистратор.Значение);
	ТаблицаДублей = Запрос.Выполнить().Выгрузить();
	
	ТаблицаДублей.Индексы.Добавить("НефинансовыйПоказатель");
	ТаблицаДублей.Индексы.Добавить("НефинансовыйПоказатель, Регистратор");
	
	МассивНФП = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивНФП, ТаблицаДублей.ВыгрузитьКолонку("НефинансовыйПоказатель"), Истина);
	Для Каждого НФП Из МассивНФП Цикл
		ПредставлениеНФП = ТаблицаДублей.Найти(НФП, "НефинансовыйПоказатель").НефинансовыйПоказательПредставление;
		ТекстСообщения = НСтр("ru = 'Для показателя ""%1"" на даты %2 значения уже установлены документом ""%3""';
								|en = 'Values are already set for the ""%1"" indicator on dates %2 by the ""%3"" document'");
		
		МассивРегистраторов = Новый Массив;
		РегистраторыПоПоказателю = ТаблицаДублей.Скопировать(Новый Структура("НефинансовыйПоказатель", НФП));
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивРегистраторов, РегистраторыПоПоказателю.ВыгрузитьКолонку("Регистратор"), Истина);
		
		Для Каждого Регистратор Из МассивРегистраторов Цикл
			
			МассивДат = Новый Массив;
			ДатыПоПоказателю = ТаблицаДублей.Скопировать(Новый Структура("НефинансовыйПоказатель, Регистратор", НФП, Регистратор));
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивДат, ДатыПоПоказателю.ВыгрузитьКолонку("Период"), Истина);
			МассивСвертки = Новый Массив;
			Для Каждого Значение Из МассивДат Цикл
				МассивСвертки.Добавить(Формат(Значение, "ДЛФ=D"));
			КонецЦикла;
			ТекстДат = СтрСоединить(МассивДат, ", ");
			ТекстРегистратора = ДатыПоПоказателю[0].РегистраторПредставление;
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ПредставлениеНФП, ТекстДат, ТекстРегистратора);
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки,,ТекстСообщения, "");
			
		КонецЦикла;
		
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
