&НаКлиенте
Перем КонтекстЭДО;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.ОрганизацияСсылка) Тогда
		
		ЗаписьПоОрганизации = РегистрыСведений.НастройкиОбменаФСРАР.СоздатьМенеджерЗаписи();
		ЗаписьПоОрганизации.Организация = Параметры.ОрганизацияСсылка;
		ЗаписьПоОрганизации.Пользователь = Справочники.Пользователи.ПустаяСсылка();
		ЗаписьПоОрганизации.Прочитать();
		
		Если ЗначениеЗаполнено(ЗаписьПоОрганизации.Организация) Тогда
			ЗначениеВДанныеФормы(ЗаписьПоОрганизации, Запись);
		Иначе
			Запись.Организация = Параметры.ОрганизацияСсылка;
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаполнитьСписокВыбораРегионовРФ();
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	УчетнаяЗапись = КонтекстЭДОСервер.УчетнаяЗаписьОрганизации(Запись.Организация);
	
	Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		МестоХраненияКлюча = КриптографияЭДКОКлиентСервер.КонтекстМоделиХраненияКлюча(Запись.Организация);
	Иначе
		МестоХраненияКлюча = КриптографияЭДКОКлиентСервер.КонтекстМоделиХраненияКлюча(Ложь);
	КонецЕсли;
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.СкрытьЭлементыФормыПриИспользованииОднойОрганизации(
		ЭтаФорма, "НадписьОрганизация");
	ЭтоПолноправныйПользователь =
		ОбщегоНазначения.РазделениеВключено() И КриптографияЭДКОКлиентСервер.ЭтоЛокальнаяПодпись(МестоХраненияКлюча)
		ИЛИ НЕ ОбщегоНазначения.РазделениеВключено() И Пользователи.ЭтоПолноправныйПользователь();
	ИспользоватьНесколькоСохраненное = (Запись.ИспользоватьНесколько = Истина);
	
	СвойстваОрганизацииИУчетнойЗаписи = Неопределено;
	Если КонтекстЭДОСервер <> Неопределено Тогда
		СвойстваОрганизацииИУчетнойЗаписи = КонтекстЭДОСервер.ЕстьВозможностьАвтонастройкиВУниверсальномФормате(
			Запись.Организация, Истина);
	КонецЕсли;
	
	ЗаполнениеПоддерживается = ДокументооборотСКОВызовСервера.НастройкиОбменаЕГАИС(
		Запись.Организация,
		НастройкиОбменаЕГАИС);
	Если НЕ ЗаполнениеПоддерживается Тогда
		Элементы.АдресУТМ.КнопкаВыбора = Неопределено;
		ИсходнаяШирина = Элементы.АдресУТМ.Ширина;
		Элементы.АдресУТМ.Ширина = ИсходнаяШирина + 2;
		Элементы.АдресУТМ.МаксимальнаяШирина = ИсходнаяШирина + 2;
	КонецЕсли;
	
	ОбменСУТМНаСервере = ?(Запись.ОбменСУТМНаСервере, 1, 0);
	
	СертификатыАбонентовПользователей = СписокПользователейИСертификатовАбонентов(Запись.Организация);
	Элементы.ИспользоватьНесколько.Видимость = СертификатыАбонентовПользователей.Количество() > 1
		ИЛИ (Запись.ИспользоватьНесколько = Истина);
	
	Элементы.ГруппаАвтонастройка.Видимость = СвойстваОрганизацииИУчетнойЗаписи <> Неопределено
		И СвойстваОрганизацииИУчетнойЗаписи.ЕстьВозможностьАвтонастройки;
	Элементы.ГруппаИнформация1СОтчетностьНеИспользуется.Видимость = (СвойстваОрганизацииИУчетнойЗаписи = Неопределено
		ИЛИ СвойстваОрганизацииИУчетнойЗаписи.ВидОбменаСКонтролирующимиОрганами <>
			ПредопределенноеЗначение("Перечисление.ВидыОбменаСКонтролирующимиОрганами.ОбменВУниверсальномФормате")
		ИЛИ НЕ ЗначениеЗаполнено(СвойстваОрганизацииИУчетнойЗаписи.УчетнаяЗаписьОбмена));
	
	КлючСохраненияПоложенияОкна = "НастройкиФСРАР" + ?(Элементы.ГруппаАвтонастройка.Видимость, "Автонастройка", "")
		+ ?(Элементы.ГруппаИнформация1СОтчетностьНеИспользуется.Видимость, "Подключение", "");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииЗавершение", ЭтотОбъект);
	ДокументооборотСКОКлиент.ПолучитьКонтекстЭДО(ОписаниеОповещения);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОрганизацияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатАбонентаПредставлениеОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Запись.ИспользоватьНесколько = Истина Тогда
		ТекущийПользователь = ПользователиКлиент.ТекущийПользователь();
		СертификатАбонентаПользователя = СертификатыАбонентовПользователей.НайтиПоЗначению(ТекущийПользователь);
		ОтпечатокСертификатаАбонента = ?(СертификатАбонентаПользователя = Неопределено,
			"", СертификатАбонентаПользователя.Представление);
	Иначе
		ОтпечатокСертификатаАбонента = Запись.СертификатАбонентаОтпечаток;
	КонецЕсли;
	
	Если Запись.ИспользоватьНесколько = Истина И ЗначениеЗаполнено(СертификатАбонентаПредставление)
		И НЕ ЗначениеЗаполнено(ОтпечатокСертификатаАбонента) Тогда
		
		ВыбратьНесколькоСертификатовАбонента();
		
	ИначеЕсли ЗначениеЗаполнено(ОтпечатокСертификатаАбонента) Тогда
		НовыйСертификат = Новый Структура("Отпечаток", ОтпечатокСертификатаАбонента);
		КриптографияЭДКОКлиентСервер.КонтекстМоделиХраненияКлюча(МестоХраненияКлюча, НовыйСертификат);
		КриптографияЭДКОКлиент.ПоказатьСертификат(НовыйСертификат);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатСубъектаРФПредставлениеОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НовыйСертификат = Новый Структура("Отпечаток", Запись.СертификатСубъектаРФОтпечаток);
	КриптографияЭДКОКлиентСервер.КонтекстМоделиХраненияКлюча(МестоХраненияКлюча, НовыйСертификат);
	КриптографияЭДКОКлиент.ПоказатьСертификат(НовыйСертификат);
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатФСРАРПредставлениеОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НовыйСертификат = Новый Структура("Отпечаток", Запись.СертификатФСРАРОтпечаток);
	КриптографияЭДКОКлиентСервер.КонтекстМоделиХраненияКлюча(МестоХраненияКлюча, НовыйСертификат);
	КриптографияЭДКОКлиент.ПоказатьСертификат(НовыйСертификат);
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатАбонентаПредставлениеОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Запись.ИспользоватьНесколько = Истина Тогда
		Запись.ИспользоватьНесколько = Ложь;
	КонецЕсли;
	
	Запись.СертификатАбонентаОтпечаток = "";
	СертификатАбонентаПредставление = "";
	
	КриптографияЭДКОКлиент.ОтобразитьПредставлениеСертификата(
		МестоХраненияКлюча,
		Элемент,
		Запись.СертификатАбонентаОтпечаток,
		ЭтотОбъект,
		"СертификатАбонентаПредставление");
	
	СкорректироватьПредставлениеСертификатаАбонента();
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатФСРАРПредставлениеОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Запись.СертификатФСРАРОтпечаток = "";
	СертификатФСРАРПредставление = "";
	
	КриптографияЭДКОКлиент.ОтобразитьПредставлениеСертификата(
		МестоХраненияКлюча,
		Элемент,
		Запись.СертификатФСРАРОтпечаток,
		ЭтотОбъект,
		"СертификатФСРАРПредставление");
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатСубъектаРФПредставлениеОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Запись.СертификатСубъектаРФОтпечаток = "";
	СертификатСубъектаРФПредставление = "";
	
	КриптографияЭДКОКлиент.ОтобразитьПредставлениеСертификата(
		МестоХраненияКлюча,
		Элемент,
		Запись.СертификатСубъектаРФОтпечаток,
		ЭтотОбъект,
		"СертификатСубъектаРФПредставление");
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьНесколькоПриИзменении(Элемент)
	
	ОтобразитьСертификатАбонента(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатАбонентаПредставлениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если Запись.ИспользоватьНесколько = Истина Тогда
		ВыбратьНесколькоСертификатовАбонента();
		
	Иначе
		Оповещение = Новый ОписаниеОповещения(
			"СертификатАбонентаПредставлениеНачалоВыбораЗавершение", ЭтотОбъект, Новый Структура("Элемент", Элемент));
		
		КриптографияЭДКОКлиент.ВыбратьСертификат(
			Оповещение, МестоХраненияКлюча, Запись.СертификатАбонентаОтпечаток, "My");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатСубъектаРФПредставлениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения(
		"СертификатСубъектаРФПредставлениеНачалоВыбораЗавершение", ЭтотОбъект, Новый Структура("Элемент", Элемент));
	
	КриптографияЭДКОКлиент.ВыбратьСертификат(
		Оповещение, МестоХраненияКлюча, Запись.СертификатСубъектаРФОтпечаток, "AddressBook");
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатФСРАРПредставлениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения(
		"СертификатФСРАРПредставлениеНачалоВыбораЗавершение", ЭтотОбъект, Новый Структура("Элемент", Элемент));
	
	КриптографияЭДКОКлиент.ВыбратьСертификат(
		Оповещение, МестоХраненияКлюча, Запись.СертификатФСРАРОтпечаток, "AddressBook");
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьОбменПриИзменении(Элемент)
	
	ОбновитьДоступностьЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияИнформацияУТМОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "#МашиночитаемыеДоверенности" Тогда
		СтандартнаяОбработка = Ложь;
		Если ДокументооборотСКОКлиентСервер.ПодсистемаЦПРРМЧДСуществует() Тогда
			ИмяСправочникаМашиночитаемыеДоверенности = "МашиночитаемыеДоверенности";
			ОткрытьФорму("Справочник." + ИмяСправочникаМашиночитаемыеДоверенности + ".ФормаСписка");
		Иначе
			ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("https://fsrar.gov.ru/egais/mchd");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АдресУТМНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("АдресУТМНачалоВыбораПослеВыбора", ЭтотОбъект);
	ДокументооборотСКОКлиент.ВыбратьНастройкиОбменаЕГАИС(
		ОписаниеОповещения,
		Запись.Организация,
		НастройкиОбменаЕГАИС);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбменСУТМНаСервереПриИзменении(Элемент)
	
	Запись.ОбменСУТМНаСервере = (ОбменСУТМНаСервере = 1);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьсяК1СОтчетности(Команда)
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьФормуМастераЗаявленияНаПодключение(Запись.Организация, ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПриОткрытииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	КонтекстЭДО = Результат.КонтекстЭДО;
	
	ОбновитьДоступностьЭлементов();
	
	КонтекстЭДО.УправлениеОтображениемОрганизации(ЭтаФорма, Запись.Организация);
	
	Если Запись.ИспользоватьНесколько = Истина Тогда
		ТекущийПользователь = ПользователиКлиент.ТекущийПользователь();
		СертификатАбонентаПользователя = СертификатыАбонентовПользователей.НайтиПоЗначению(ТекущийПользователь);
		ОтпечатокСертификатаАбонента = ?(СертификатАбонентаПользователя = Неопределено,
			"", СертификатАбонентаПользователя.Представление);
	Иначе
		ОтпечатокСертификатаАбонента = Запись.СертификатАбонентаОтпечаток;
	КонецЕсли;
	
	ПараметрыОтображенияСертификатов = Новый Массив;
	
	ПараметрыОтображенияСертификата = Новый Структура;
	ПараметрыОтображенияСертификата.Вставить("ПолеВвода", 								Элементы.СертификатАбонентаПредставление);
	ПараметрыОтображенияСертификата.Вставить("Сертификат", 								ОтпечатокСертификатаАбонента);
	ПараметрыОтображенияСертификата.Вставить("ИмяРеквизитаПредставлениеСертификата", 	"СертификатАбонентаПредставление");
	
	ПараметрыОтображенияСертификатов.Добавить(ПараметрыОтображенияСертификата);
	
	ПараметрыОтображенияСертификата = Новый Структура;
	ПараметрыОтображенияСертификата.Вставить("ПолеВвода", 								Элементы.СертификатСубъектаРФПредставление);
	ПараметрыОтображенияСертификата.Вставить("Сертификат", 								Запись.СертификатСубъектаРФОтпечаток);
	ПараметрыОтображенияСертификата.Вставить("ИмяРеквизитаПредставлениеСертификата", 	"СертификатСубъектаРФПредставление");
	
	ПараметрыОтображенияСертификатов.Добавить(ПараметрыОтображенияСертификата);
	
	ПараметрыОтображенияСертификата = Новый Структура;
	ПараметрыОтображенияСертификата.Вставить("ПолеВвода", 								Элементы.СертификатФСРАРПредставление);
	ПараметрыОтображенияСертификата.Вставить("Сертификат", 								Запись.СертификатФСРАРОтпечаток);
	ПараметрыОтображенияСертификата.Вставить("ИмяРеквизитаПредставлениеСертификата", 	"СертификатФСРАРПредставление");
	
	ПараметрыОтображенияСертификатов.Добавить(ПараметрыОтображенияСертификата);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииПослеОтображенияСертификатов", ЭтотОбъект);
	КриптографияЭДКОКлиент.ОтобразитьПредставленияСертификатов(
		ПараметрыОтображенияСертификатов,
		ЭтотОбъект,
		МестоХраненияКлюча,
		ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииПослеОтображенияСертификатов(Результат, ДополнительныеПараметры) Экспорт
	
	СкорректироватьПредставлениеСертификатаАбонента();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьНесколькоСертификатовАбонента(УстановленоИспользоватьНесколько = Ложь)
	
	Если Модифицированность И НЕ Записать() Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("УстановленоИспользоватьНесколько", УстановленоИспользоватьНесколько);
	Оповещение = Новый ОписаниеОповещения("ВыбратьНесколькоСертификатовАбонентаПослеВыбора",
		ЭтотОбъект, ДополнительныеПараметры);
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ОрганизацияСсылка", 					Запись.Организация);
	СтруктураПараметров.Вставить("УстановленоИспользоватьНесколько", 	УстановленоИспользоватьНесколько);
	
	ОткрытьФорму(
		"РегистрСведений.НастройкиОбменаФСРАР.Форма.ФормаСпискаПоПользователям",
		СтруктураПараметров,
		ЭтотОбъект,,,,
		Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьНесколькоСертификатовАбонентаПослеВыбора(Результат, ВходящийКонтекст) Экспорт
	
	УстановленоИспользоватьНесколько = ВходящийКонтекст.УстановленоИспользоватьНесколько;
	
	Если Результат <> КодВозвратаДиалога.ОК Тогда
		Если УстановленоИспользоватьНесколько Тогда
			Запись.ИспользоватьНесколько = Ложь;
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	СертификатыАбонентовПользователей = СписокПользователейИСертификатовАбонентов(Запись.Организация);
	Элементы.ИспользоватьНесколько.Видимость = СертификатыАбонентовПользователей.Количество() > 1
		ИЛИ Запись.ИспользоватьНесколько = Истина;
	
	ЗаписьПоОрганизации = НастройкиОбменаФСРАРОрганизации(Запись.Организация);
	
	Если (ЗаписьПоОрганизации.ИспользоватьНесколько = Истина) <> (Запись.ИспользоватьНесколько = Истина) Тогда
		Запись.ИспользоватьНесколько = (ЗаписьПоОрганизации.ИспользоватьНесколько = Истина);
	КонецЕсли;
	
	Если ЗаписьПоОрганизации.СертификатАбонентаОтпечаток <> Запись.СертификатАбонентаОтпечаток Тогда
		Запись.СертификатАбонентаОтпечаток = ЗаписьПоОрганизации.СертификатАбонентаОтпечаток;
	КонецЕсли;
	
	Если Запись.ИспользоватьНесколько = Истина Тогда
		ТекущийПользователь = ПользователиКлиент.ТекущийПользователь();
		СертификатАбонентаПользователя = СертификатыАбонентовПользователей.НайтиПоЗначению(ТекущийПользователь);
		ОтпечатокСертификатаАбонента = ?(СертификатАбонентаПользователя = Неопределено,
			"", СертификатАбонентаПользователя.Представление);
	Иначе
		ОтпечатокСертификатаАбонента = Запись.СертификатАбонентаОтпечаток;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьНесколькоСертификатовАбонентаПослеОтображения", ЭтотОбъект);
	
	КриптографияЭДКОКлиент.ОтобразитьПредставлениеСертификата(
		МестоХраненияКлюча,
		Элементы.СертификатАбонентаПредставление,
		ОтпечатокСертификатаАбонента,
		ЭтотОбъект,
		"СертификатАбонентаПредставление",
		ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьНесколькоСертификатовАбонентаПослеОтображения(Результат, ВходящийКонтекст) Экспорт
	
	СкорректироватьПредставлениеСертификатаАбонента(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатАбонентаПредставлениеНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Элемент = ДополнительныеПараметры.Элемент;
	
	Если Результат.Выполнено Тогда
		Запись.СертификатАбонентаОтпечаток = Результат.ВыбранноеЗначение.Отпечаток;
		
		КриптографияЭДКОКлиент.ОтобразитьПредставлениеСертификата(
			МестоХраненияКлюча,
			Элемент,
			Результат.ВыбранноеЗначение.Отпечаток,
			ЭтотОбъект,
			"СертификатАбонентаПредставление");
		
		СкорректироватьПредставлениеСертификатаАбонента();
		
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатСубъектаРФПредставлениеНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Элемент = ДополнительныеПараметры.Элемент;
	
	Если Результат.Выполнено Тогда
		Запись.СертификатСубъектаРФОтпечаток = Результат.ВыбранноеЗначение.Отпечаток;
		
		КриптографияЭДКОКлиент.ОтобразитьПредставлениеСертификата(
			МестоХраненияКлюча, 
			Элемент, 
			Результат.ВыбранноеЗначение.Отпечаток,
			ЭтотОбъект,
			"СертификатСубъектаРФПредставление");
		
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатФСРАРПредставлениеНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Элемент = ДополнительныеПараметры.Элемент;
	
	Если Результат.Выполнено Тогда
		Запись.СертификатФСРАРОтпечаток = Результат.ВыбранноеЗначение.Отпечаток;
		
		КриптографияЭДКОКлиент.ОтобразитьПредставлениеСертификата(
			МестоХраненияКлюча,
			Элемент, 
			Результат.ВыбранноеЗначение.Отпечаток,
			ЭтотОбъект, 
			"СертификатФСРАРПредставление");
		
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДоступностьЭлементов()
	
	Элементы.НадписьСертификатАбонента.Доступность = Запись.ИспользоватьОбмен;
	Элементы.СертификатАбонентаПредставление.Доступность = Запись.ИспользоватьОбмен;
	Элементы.НадписьСертификатСубъектаРФ.Доступность = Запись.ИспользоватьОбмен;
	Элементы.СертификатСубъектаРФПредставление.Доступность = Запись.ИспользоватьОбмен;
	Элементы.НадписьСертификатФСРАР.Доступность = Запись.ИспользоватьОбмен;
	Элементы.СертификатФСРАРПредставление.Доступность = Запись.ИспользоватьОбмен;
	Элементы.НадписьКодРегиона.Доступность = Запись.ИспользоватьОбмен;
	Элементы.КодРегиона.Доступность = Запись.ИспользоватьОбмен;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьСертификатАбонента(ВыборЕслиНеЗаданыПользовательские = Ложь)
	
	Если Запись.ИспользоватьНесколько = Истина Тогда
		ТекущийПользователь = ПользователиКлиент.ТекущийПользователь();
		ЕстьСертификатыАбонентовПользователей = Ложь;
		Для каждого СертификатАбонентаПользователя Из СертификатыАбонентовПользователей Цикл
			ОтпечатокСертификатаАбонента = СертификатАбонентаПользователя.Представление;
			Если ЗначениеЗаполнено(ОтпечатокСертификатаАбонента) Тогда
				ЕстьСертификатыАбонентовПользователей = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если ЕстьСертификатыАбонентовПользователей Тогда
			СертификатАбонентаПользователя = СертификатыАбонентовПользователей.НайтиПоЗначению(ТекущийПользователь);
			ОтпечатокСертификатаАбонента = ?(СертификатАбонентаПользователя = Неопределено,
				"", СертификатАбонентаПользователя.Представление);
		Иначе
			ОтпечатокСертификатаАбонента = "";
		КонецЕсли;
		
	Иначе
		ОтпечатокСертификатаАбонента = Запись.СертификатАбонентаОтпечаток;
	КонецЕсли;
	
	Если ВыборЕслиНеЗаданыПользовательские И Запись.ИспользоватьНесколько = Истина
		И НЕ ЕстьСертификатыАбонентовПользователей Тогда
		
		ВыбратьНесколькоСертификатовАбонента(Истина);
		
	Иначе
		ОписаниеОповещения = Новый ОписаниеОповещения("ОтобразитьСертификатАбонентаПослеОтображения", ЭтотОбъект);
		
		КриптографияЭДКОКлиент.ОтобразитьПредставлениеСертификата(
			МестоХраненияКлюча,
			Элементы.СертификатАбонентаПредставление,
			ОтпечатокСертификатаАбонента,
			ЭтотОбъект,
			"СертификатАбонентаПредставление",
			ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьСертификатАбонентаПослеОтображения(Результат, ВходящийКонтекст) Экспорт
	
	СкорректироватьПредставлениеСертификатаАбонента(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СкорректироватьПредставлениеСертификатаАбонента(ОбновлениеДоступностиЭлементов = Ложь)
	
	КоличествоСертификатовАбонентов = 0;
	ОтпечаткиСертификатовАбонентов = Новый Массив;
	
	Если Запись.ИспользоватьНесколько = Истина Тогда
		ТекущийПользователь = ПользователиКлиент.ТекущийПользователь();
		СертификатАбонентаПользователя = СертификатыАбонентовПользователей.НайтиПоЗначению(ТекущийПользователь);
		ОтпечатокСертификатаАбонента = ?(СертификатАбонентаПользователя = Неопределено,
			"", СертификатАбонентаПользователя.Представление);
		
		ПоказыватьОсновнойСертификат = Ложь;
		СертификатАбонентаТекущегоПользователяЗадан = Ложь;
		Для каждого СертификатАбонентаПользователя Из СертификатыАбонентовПользователей Цикл
			ОтпечатокСертификатаАбонента = СертификатАбонентаПользователя.Представление;
			Если ЗначениеЗаполнено(ОтпечатокСертификатаАбонента) Тогда
				КоличествоСертификатовАбонентов = КоличествоСертификатовАбонентов + 1;
				ОтпечаткиСертификатовАбонентов.Добавить(ОтпечатокСертификатаАбонента);
				
				Если СертификатАбонентаПользователя.Значение = ТекущийПользователь Тогда
					СертификатАбонентаТекущегоПользователяЗадан = Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		Если ПоказыватьОсновнойСертификат И СертификатАбонентаТекущегоПользователяЗадан Тогда
			Если КоличествоСертификатовАбонентов > 1 Тогда
				ТекстСообщения = НСтр("ru = 'и еще %1';
										|en = 'и еще %1'");
				СертификатАбонентаПредставление = СертификатАбонентаПредставление + " " + СтрШаблон(
					ТекстСообщения,
					Строка(КоличествоСертификатовАбонентов - 1));
			КонецЕсли;
		ИначеЕсли КоличествоСертификатовАбонентов > 0 Тогда
			ТекстСообщения = ОбщегоНазначенияЭДКОКлиентСервер.ПростоеСклонение(
				КоличествоСертификатовАбонентов,
				НСтр("ru = '%1 сертификат';
					|en = '%1 сертификат'"),
				НСтр("ru = '%1 сертификата';
					|en = '%1 сертификата'"),
				НСтр("ru = '%1 сертификатов';
					|en = '%1 сертификатов'"));
			СертификатАбонентаПредставление = СтрШаблон(
				ТекстСообщения,
				Строка(КоличествоСертификатовАбонентов));
		КонецЕсли;
		
	Иначе
		КоличествоСертификатовАбонентов = 1;
		ОтпечаткиСертификатовАбонентов.Добавить(Запись.СертификатАбонентаОтпечаток);
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОбновлениеДоступностиЭлементов", ОбновлениеДоступностиЭлементов);
	Оповещение = Новый ОписаниеОповещения(
		"СкорректироватьПредставлениеСертификатаАбонентаПослеПоискаСертификата", ЭтотОбъект, ДополнительныеПараметры);
	
	Если КоличествоСертификатовАбонентов = 1 Тогда
		СвойстваСертификата = Новый Структура("Отпечаток", ОтпечаткиСертификатовАбонентов[0]);
		КриптографияЭДКОКлиентСервер.КонтекстМоделиХраненияКлюча(МестоХраненияКлюча, СвойстваСертификата);
		
		КриптографияЭДКОКлиент.НайтиСертификат(Оповещение, СвойстваСертификата,, Ложь);
		
	Иначе
		ВыполнитьОбработкуОповещения(Оповещение, КоличествоСертификатовАбонентов >= 2);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СкорректироватьПредставлениеСертификатаАбонентаПослеПоискаСертификата(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Булево") Тогда
		Элементы.ГруппаПараметрыУТМ.Видимость = Результат;
		
	ИначеЕсли Результат.Выполнено И Результат.СертификатНайден Тогда
		СвойстваСертификата = Результат.СвойстваСертификата;
		// В том числе, портал ФСРАР принимает сертификаты физических лиц без ОГРНИП в качестве сертификатов ИП по ИНН
		ЭтоФизическоеЛицо = (КриптографияЭДКОКлиентСервер.ТипСубъектаСертификата(
			СвойстваСертификата.Владелец, Запись.Организация) = "ФЛ");
		Элементы.ГруппаПараметрыУТМ.Видимость = ЭтоФизическоеЛицо;
		
	Иначе
		Элементы.ГруппаПараметрыУТМ.Видимость = Истина;
	КонецЕсли;
	
	Если Элементы.ГруппаПараметрыУТМ.Видимость И НЕ ЗначениеЗаполнено(Запись.АдресУТМ)
		И НЕ ЗначениеЗаполнено(Запись.ПортУТМ) И НЕ ЗначениеЗаполнено(Запись.ТаймаутУТМ)
		И Запись.ОбменСУТМНаСервере <> Истина Тогда
		
		Если НастройкиОбменаЕГАИС.Количество() = 1 Тогда
			ЭлементНастроекОбмена = НастройкиОбменаЕГАИС[0].Значение;
			Запись.АдресУТМ = ЭлементНастроекОбмена.АдресУТМ;
			Запись.ПортУТМ = ЭлементНастроекОбмена.ПортУТМ;
			Запись.ТаймаутУТМ = ЭлементНастроекОбмена.Таймаут;
			Запись.ОбменСУТМНаСервере = ЭлементНастроекОбмена.ОбменНаСервере;
			
		Иначе
			Запись.АдресУТМ = "127.0.0.1";
			Запись.ПортУТМ = 8080;
			Запись.ТаймаутУТМ = 60;
			Запись.ОбменСУТМНаСервере = Истина;
		КонецЕсли;
		
		ОбменСУТМНаСервере = ?(Запись.ОбменСУТМНаСервере, 1, 0);
	КонецЕсли;
	
	Если ДополнительныеПараметры.ОбновлениеДоступностиЭлементов Тогда
		ОбновитьДоступностьЭлементов();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НастройкиОбменаФСРАРОрганизации(ОрганизацияСсылка)
	
	ЗаписьПоОрганизации = РегистрыСведений.НастройкиОбменаФСРАР.СоздатьМенеджерЗаписи();
	ЗаписьПоОрганизации.Организация = ОрганизацияСсылка;
	ЗаписьПоОрганизации.Пользователь = Справочники.Пользователи.ПустаяСсылка();
	ЗаписьПоОрганизации.Прочитать();
	
	Результат = Новый Структура;
	Результат.Вставить("ИспользоватьНесколько", 		ЗаписьПоОрганизации.ИспользоватьНесколько = Истина);
	Результат.Вставить("СертификатАбонентаОтпечаток", 	ЗаписьПоОрганизации.СертификатАбонентаОтпечаток);
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСписокВыбораРегионовРФ()
	
	КонтекстЭДО = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	МакетРегионов = КонтекстЭДО.ПолучитьМакет("ФСРАРПорталыРегионов");
	
	Для НомСтр = 1 По МакетРегионов.ВысотаТаблицы Цикл
		
		КодРегиона = СокрЛП(МакетРегионов.Область(НомСтр, 1, НомСтр, 1).Текст);
		НазваниеРегиона = СокрЛП(МакетРегионов.Область(НомСтр, 2, НомСтр, 2).Текст);
		
		Если ЗначениеЗаполнено(КодРегиона) Тогда
			Элементы.КодРегиона.СписокВыбора.Добавить(КодРегиона, КодРегиона + " - " + НазваниеРегиона);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ИзменениеНастроекЭДООрганизации", Запись.Организация);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СписокПользователейИСертификатовАбонентов(ОрганизацияСсылка)
	
	СписокПользователейНастроек = Новый СписокЗначений;
	
	Если НЕ ЗначениеЗаполнено(ОрганизацияСсылка) Тогда
		Возврат СписокПользователейНастроек;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Пользователи.Ссылка КАК Пользователь,
		|	Пользователи.Наименование КАК НаименованиеПользователя,
		|	НастройкиОбменаФСРАР.СертификатАбонентаОтпечаток КАК СертификатАбонентаОтпечаток
		|ИЗ
		|	Справочник.Пользователи КАК Пользователи
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиОбменаФСРАР КАК НастройкиОбменаФСРАР
		|		ПО НастройкиОбменаФСРАР.Организация = &Организация
		|		И Пользователи.Ссылка = НастройкиОбменаФСРАР.Пользователь
		|ГДЕ
		|	НЕ Пользователи.Недействителен
		|	И НЕ Пользователи.Служебный
		|	И Пользователи.ИдентификаторПользователяИБ <> &ПустойИдентификаторПользователяИБ
		|
		|УПОРЯДОЧИТЬ ПО
		|	НаименованиеПользователя";
	
	Запрос.УстановитьПараметр("Организация", ОрганизацияСсылка);
	ПустойИдентификаторПользователяИБ = Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000");
	Запрос.УстановитьПараметр("ПустойИдентификаторПользователяИБ", ПустойИдентификаторПользователяИБ);
	ТаблицаНастроек = Запрос.Выполнить().Выгрузить();
	
	Для каждого СтрокаТаблицы Из ТаблицаНастроек Цикл
		СписокПользователейНастроек.Добавить(СтрокаТаблицы.Пользователь, СтрокаТаблицы.СертификатАбонентаОтпечаток);
	КонецЦикла;
	
	Возврат СписокПользователейНастроек;
	
КонецФункции

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	МестоХраненияКлюча = КриптографияЭДКОКлиентСервер.КонтекстМоделиХраненияКлюча(Запись.Организация);
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	Элементы.ГруппаАвтонастройка.Видимость = КонтекстЭДОСервер <> Неопределено
		И КонтекстЭДОСервер.ЕстьВозможностьАвтонастройкиВУниверсальномФормате(Запись.Организация,
		Истина).ЕстьВозможностьАвтонастройки;
	
	Если НЕ ЭтоПолноправныйПользователь Тогда
		ЗаписьПоОрганизации = НастройкиОбменаФСРАРОрганизации(Запись.Организация);
		Если ЗаписьПоОрганизации.ИспользоватьНесколько = Истина И НЕ Запись.ИспользоватьНесколько = Истина Тогда
			Запись.ИспользоватьНесколько = Истина;
		КонецЕсли;
	КонецЕсли;
	ИспользоватьНесколькоСохраненное = (Запись.ИспользоватьНесколько = Истина);
	
	СертификатыАбонентовПользователей = СписокПользователейИСертификатовАбонентов(Запись.Организация);
	Элементы.ИспользоватьНесколько.Видимость = СертификатыАбонентовПользователей.Количество() > 1
		ИЛИ Запись.ИспользоватьНесколько = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура АдресУТМНачалоВыбораПослеВыбора(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЭлемент <> Неопределено Тогда
		Запись.АдресУТМ 			= ВыбранныйЭлемент.Значение.АдресУТМ;
		Запись.ПортУТМ 				= ВыбранныйЭлемент.Значение.ПортУТМ;
		Запись.ТаймаутУТМ 			= ВыбранныйЭлемент.Значение.Таймаут;
		Запись.ОбменСУТМНаСервере 	= ВыбранныйЭлемент.Значение.ОбменНаСервере;
		ОбменСУТМНаСервере = ?(Запись.ОбменСУТМНаСервере, 1, 0);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

