
#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НовыйПарольНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Элементы.НовыйПароль.РежимПароля Тогда
		
		Элементы.НовыйПароль.РежимПароля          = Ложь;
		Элементы.НовыйПароль.КартинкаКнопкиВыбора = БиблиотекаКартинок.ВводимыеСимволыСкрыты;
		
	Иначе
		
		Элементы.НовыйПароль.РежимПароля          = Истина;
		Элементы.НовыйПароль.КартинкаКнопкиВыбора = БиблиотекаКартинок.ВводимыеСимволыВидны;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ОчиститьСообщения();
	Отказ = ПроверитьЗаполнениеПароля();
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Закрыть(НовыйПароль);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ПроверитьЗаполнениеПароля()
	
	Если Не ЗначениеЗаполнено(НовыйПароль) Тогда
		
		ТекстСообщения = НСтр("ru = 'Проверьте правильность заполнения пароля.
							  |Пароль не может быть пустым.';
							  |en = 'Проверьте правильность заполнения пароля.
							  |Пароль не может быть пустым.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, НовыйПароль);
		Возврат Истина;
		
	КонецЕсли;
	
	Если Не СтрДлина(НовыйПароль) >= 8 Тогда
		
		ТекстСообщения = НСтр("ru = 'Проверьте правильность заполнения пароля.
							  |Пароль должен быть более 8 символов.';
							  |en = 'Проверьте правильность заполнения пароля.
							  |Пароль должен быть более 8 символов.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, НовыйПароль);
		Возврат Истина;
		
	КонецЕсли;
	
	Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(НовыйПароль)
		Или Не ЕстьЦифрыВСтроке(НовыйПароль) Тогда
		
		ТекстСообщения = НСтр("ru = 'Проверьте правильность заполнения пароля.
							  |Пароль должен содержать цифры и буквы.';
							  |en = 'Проверьте правильность заполнения пароля.
							  |Пароль должен содержать цифры и буквы.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, НовыйПароль);
		Возврат Истина;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаКлиенте
Функция ЕстьЦифрыВСтроке(Знач СтрокаПроверки)
	
	СтрокаЦифр = "0123456789";
	
	Для Индекс = 0 По СтрДлина(СтрокаПроверки) - 1 Цикл
		
		Если СтрНайти(СтрокаЦифр, Сред(СтрокаПроверки, Индекс, 1)) > 0 Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти