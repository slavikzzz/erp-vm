
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Параметры.Свойство("ШаблонПроводки") Тогда
	    ТекстСообщения = НСтр("ru = 'Непосредственное открытие этой формы не предусмотрено';
								|en = 'Application cannot open this form explicitly.'");
	    ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	ШаблонПроводки = Параметры.ШаблонПроводки;
	
	ЗаполнитьСписокНастроекФормированияПроводок();
	
	УстановитьДоступностьЭлементовНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	Оповестить("ИзмененоИспользованиеВПравилахОтраженияВМеждународномУчете", , ШаблонПроводки); 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ИзменитьПравилаОтраженияВМеждународномУчете(СписокНастроекФормированияПроводок, ШаблонПроводки);
	Закрыть(Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСписокНастроекФормированияПроводок()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПравилаОтражения.НастройкаФормированияПроводок КАК НастройкаФормированияПроводок,
	|	ПравилаОтражения.ШаблонПроводки КАК ШаблонПроводки
	|ПОМЕСТИТЬ ПравилаОтражения
	|ИЗ
	|	РегистрСведений.ПравилаОтраженияВМеждународномУчете КАК ПравилаОтражения
	|ГДЕ
	|	ПравилаОтражения.ШаблонПроводки = &ШаблонПроводки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Настройки.Ссылка КАК НастройкаФормированияПроводок,
	|	ВЫБОР
	|		КОГДА ПравилаОтражения.ШаблонПроводки ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Использование
	|ИЗ
	|	Справочник.НастройкиФормированияПроводокМеждународногоУчета КАК Настройки
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		ПравилаОтражения КАК ПравилаОтражения
	|	ПО 
	|		(ПравилаОтражения.НастройкаФормированияПроводок = Настройки.Ссылка)
	|ГДЕ
	|	Настройки.Владелец = &ПланСчетов
	|
	|УПОРЯДОЧИТЬ ПО
	|	Настройки.Наименование";
	
	Запрос.УстановитьПараметр("ШаблонПроводки", ШаблонПроводки);
	Запрос.УстановитьПараметр("ПланСчетов", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ШаблонПроводки,"ПланСчетов"));
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СписокНастроекФормированияПроводок.Добавить(Выборка.НастройкаФормированияПроводок,, Выборка.Использование);
	КонецЦикла;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ИзменитьПравилаОтраженияВМеждународномУчете(СписокНастроекФормированияПроводок, ШаблонПроводки)

	НаборЗаписей = РегистрыСведений.ПравилаОтраженияВМеждународномУчете.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ШаблонПроводки.Установить(ШаблонПроводки);
	Для Каждого ЭлементСписка Из СписокНастроекФормированияПроводок Цикл
		Если ЭлементСписка.Пометка Тогда
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.НастройкаФормированияПроводок = ЭлементСписка.Значение;
			НоваяЗапись.ШаблонПроводки = ШаблонПроводки;
		КонецЕсли;	
	КонецЦикла;	
	НаборЗаписей.Записать();

КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьЭлементовНаСервере()

	МассивЭлементов = Новый Массив;
	МассивЭлементов.Добавить("ФормаЗаписатьИЗакрыть");
	МассивЭлементов.Добавить("СписокНастроекФормированияПроводок");
	
	ДоступноИзменениеНастроекМФУ = МеждународныйУчетОбщегоНазначения.ДоступноИзменениеНастроекМеждународногоУчета();
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивЭлементов, "ТолькоПросмотр", НЕ ДоступноИзменениеНастроекМФУ);

КонецПроцедуры

#КонецОбласти
