#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		
		КалендарьБылНезаполнен = Ложь;
		
		ПодтверждениеОтправилОператор = ТребованияФНСВызовСервера.ПодтверждениеОтправилОператор(Запись.Ссылка);
		
		// Дата Ответа Рассчитанная
		Если (ЗначениеЗаполнено(Запись.ДатаПодтверждения) ИЛИ ПодтверждениеОтправилОператор)
			И ЗначениеЗаполнено(Запись.ДнейДоОтвета) Тогда
			
			Если ПодтверждениеОтправилОператор Тогда
				Результат = ТребованияФНС.ДатаПлюсДниПоКалендарю(Запись.ДатаОтправкиТребования, Запись.ДнейДоОтвета);
			Иначе
				Результат = ТребованияФНС.ДатаПлюсДниПоКалендарю(Запись.ДатаПодтверждения, Запись.ДнейДоОтвета);
			КонецЕсли;
			
			Запись.ОтветитьДо = Результат.Дата;
			
			Если НЕ Результат.КалендарьЗаполнен Тогда
				КалендарьБылНезаполнен = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Запись.ПодтвердитьДо)
			И ЗначениеЗаполнено(Запись.ДатаОтправкиТребования) Тогда
			
			Результат = ТребованияФНС.ДатаПлюсДниПоКалендарю(Запись.ДатаОтправкиТребования, 6);
			Запись.ПодтвердитьДо = Результат.Дата;
			
			Если НЕ Результат.КалендарьЗаполнен Тогда
				КалендарьБылНезаполнен = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
		Запись.КалендарьБылНезаполнен = КалендарьБылНезаполнен;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли


