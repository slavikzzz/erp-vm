#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

#Область ОбработчикиПравилРегистрации

// См. ЗарплатаКадрыРасширенныйСинхронизацияДанных.ШаблонОбработчика
Процедура ПриЗаполненииНастроекОбработчиковПравилРегистрации(Настройки) Экспорт
	ЗарплатаКадрыРасширенныйСинхронизацияДанных.ДляНезависимогоРегистра(Настройки, "Сотрудник",
		"РегламентированныеДанные");
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПодготовитьОбновлениеЗависимыхДанныхПриОбменеПередЗаписью(Объект) Экспорт
	СохранитьПрежнийНаборЗаписей(Объект);
КонецПроцедуры

Процедура ПодготовитьОбновлениеЗависимыхДанныхПриОбменеПриЗаписи(Объект) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ОстаткиОтпусков.АналитикаДанных") Тогда
		МодульОстаткиОтпусковАналитикаДанных = ОбщегоНазначения.ОбщийМодуль("ОстаткиОтпусковАналитикаДанных");
		МодульОстаткиОтпусковАналитикаДанных.ЗаполнитьТаблицуОбновленияЗаработанныхПравНаОтпуска(Объект);
	КонецЕсли;
	
КонецПроцедуры

Процедура СохранитьПрежнийНаборЗаписей(Объект) Экспорт
	
	Отбор = Объект.Отбор;
	Если Не Отбор.Сотрудник.Использование Или Не ЗначениеЗаполнено(Отбор.Сотрудник.Значение) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Сотрудник", Отбор.Сотрудник.Значение);
	Запрос.УстановитьПараметр("БезОтбораПоВидуОтпуска", Не Отбор.ВидЕжегодногоОтпуска.Использование);
	Запрос.УстановитьПараметр("ВидЕжегодногоОтпуска", Отбор.ВидЕжегодногоОтпуска.Значение);
	Запрос.УстановитьПараметр("БезОтбораПоПериоду", Не Отбор.Период.Использование);
	Запрос.УстановитьПараметр("Период", Отбор.Период.Значение);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДанныеНабора.Период КАК Период,
		|	ДанныеНабора.Сотрудник КАК Сотрудник,
		|	ДанныеНабора.ВидЕжегодногоОтпуска КАК ВидЕжегодногоОтпуска,
		|	ДанныеНабора.КоличествоДней КАК КоличествоДней
		|ИЗ
		|	РегистрСведений.ЗаработанныеПраваНаОтпуска КАК ДанныеНабора
		|ГДЕ
		|	ДанныеНабора.Сотрудник = &Сотрудник
		|	И (&БезОтбораПоВидуОтпуска
		|	ИЛИ ДанныеНабора.ВидЕжегодногоОтпуска = &ВидЕжегодногоОтпуска)
		|	И (&БезОтбораПоПериоду
		|	ИЛИ ДанныеНабора.Период = &Период)";
	
	Объект.ДополнительныеСвойства.Вставить("ПрежнийНабор", Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли