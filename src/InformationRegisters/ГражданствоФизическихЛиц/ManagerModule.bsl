
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(ФизическоеЛицо)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Создает новую структуру для сведений о гражданстве.
// 
// Возвращаемое значение:
//  Структура - Новое гражданство:
// * Гражданство - СправочникСсылка.СтраныМира -
// * Период - Дата -
Функция НовоеГражданство() Экспорт
	
	СтруктураЗаписи = ОбщегоНазначенияБЗК.СтруктураПоИмениРегистраСведений("ГражданствоФизическихЛиц");
	
	Возврат Новый Структура("Гражданство, Период", СтруктураЗаписи.Страна, СтруктураЗаписи.Период);
	
КонецФункции

// Добавляет запись о гражданстве физического лица.
// 
// Параметры:
//  ФизическоеЛицо - СправочникСсылка.ФизическиеЛица
//  Страна - СправочникСсылка.СтраныМира
//  Период - Дата
Процедура ДобавитьГражданствоФизическогоЛица(ФизическоеЛицо, Страна, Период = Неопределено) Экспорт

	НаборЗаписей = РегистрыСведений.ГражданствоФизическихЛиц.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ФизическоеЛицо.Установить(ФизическоеЛицо);
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НаборЗаписей.Заполнить(Неопределено);
	Если Период <> Неопределено Тогда
		НоваяЗапись.Период = Период;
	КонецЕсли;
	НоваяЗапись.ФизическоеЛицо = ФизическоеЛицо;
	НоваяЗапись.Страна = Страна;
	
	НаборЗаписей.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
	НаборЗаписей.Записать();

КонецПроцедуры

#КонецОбласти

#КонецЕсли
