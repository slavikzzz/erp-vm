#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ПодготовитьДанныеДляФормированияЗаданийПередЗаписью();

КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПодготовитьДанныеДляФормированияЗаданийПриЗаписи();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПодготовитьДанныеДляФормированияЗаданийПередЗаписью()
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьВнеоборотныеАктивы2_4")
		ИЛИ РасчетСтоимостиВНА.ДвиженияЗаписываютсяПриВыполненииОперации(ЭтотОбъект)
		ИЛИ РасчетСтоимостиВНА.ДвиженияЗаписываютсяПриАктуализацияДвиженийПоВНА(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ДополнительныеСвойства.Свойство("МенеджерВременныхТаблиц") Тогда
		ДополнительныеСвойства.Вставить("МенеджерВременныхТаблиц", Новый МенеджерВременныхТаблиц);
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаПередЗаписью.ПериодРасчета           КАК ПериодРасчета,
	|	ТаблицаПередЗаписью.Организация             КАК Организация,
	|	ТаблицаПередЗаписью.ОсновноеСредство        КАК ОсновноеСредство,
	|	ТаблицаПередЗаписью.СуммаНалога             КАК СуммаНалога,
	|	ТаблицаПередЗаписью.СуммаНалогаКУплате      КАК СуммаНалогаКУплате,
	|	ТаблицаПередЗаписью.СуммаАвансовыхПлатежей  КАК СуммаАвансовыхПлатежей
	|ПОМЕСТИТЬ РасчетНалогаНаИмуществоПередЗаписью
	|ИЗ
	|	РегистрСведений.РасчетНалогаНаИмуществоПоКадастровойСтоимости КАК ТаблицаПередЗаписью
	|ГДЕ
	|	ТаблицаПередЗаписью.Регистратор = &Регистратор";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПодготовитьДанныеДляФормированияЗаданийПриЗаписи()
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьВнеоборотныеАктивы2_4") 
		ИЛИ РасчетСтоимостиВНА.ДвиженияЗаписываютсяПриВыполненииОперации(ЭтотОбъект)
		ИЛИ РасчетСтоимостиВНА.ДвиженияЗаписываютсяПриАктуализацияДвиженийПоВНА(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Период                        КАК Период,
	|	НАЧАЛОПЕРИОДА(Таблица.Период, МЕСЯЦ)  КАК НачалоМесяца,
	|	КОНЕЦПЕРИОДА(Таблица.Период, МЕСЯЦ)   КАК КонецМесяца,
	|	Таблица.Организация                   КАК Организация,
	|	Таблица.ОсновноеСредство              КАК ОсновноеСредство
	|ПОМЕСТИТЬ ВТ_ИзмененныеДанные
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаПередЗаписью.ПериодРасчета     КАК Период,
	|		ТаблицаПередЗаписью.Организация       КАК Организация,
	|		ТаблицаПередЗаписью.ОсновноеСредство  КАК ОсновноеСредство
	|	ИЗ
	|		РасчетНалогаНаИмуществоПередЗаписью КАК ТаблицаПередЗаписью
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РасчетНалогаНаИмуществоПоКадастровойСтоимости КАК ТаблицаПриЗаписи
	|			ПО ТаблицаПриЗаписи.ОсновноеСредство = ТаблицаПередЗаписью.ОсновноеСредство
	|				И ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|				И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|		ГДЕ
	|			(ТаблицаПриЗаписи.ПериодРасчета ЕСТЬ NULL
	|				ИЛИ ТаблицаПриЗаписи.СуммаНалога <> ТаблицаПередЗаписью.СуммаНалога
	|				ИЛИ ТаблицаПриЗаписи.СуммаНалогаКУплате <> ТаблицаПередЗаписью.СуммаНалогаКУплате
	|				ИЛИ ТаблицаПриЗаписи.СуммаАвансовыхПлатежей <> ТаблицаПередЗаписью.СуммаАвансовыхПлатежей
	|				ИЛИ НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.ПериодРасчета, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.ПериодРасчета, МЕСЯЦ))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаПриЗаписи.ПериодРасчета,
	|		ТаблицаПриЗаписи.Организация,
	|		ТаблицаПриЗаписи.ОсновноеСредство
	|	ИЗ
	|		РегистрСведений.РасчетНалогаНаИмуществоПоКадастровойСтоимости КАК ТаблицаПриЗаписи
	|			ЛЕВОЕ СОЕДИНЕНИЕ РасчетНалогаНаИмуществоПередЗаписью КАК ТаблицаПередЗаписью
	|			ПО ТаблицаПриЗаписи.ОсновноеСредство = ТаблицаПередЗаписью.ОсновноеСредство
	|				И ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|		ГДЕ
	|			(ТаблицаПередЗаписью.ПериодРасчета ЕСТЬ NULL
	|				ИЛИ ТаблицаПриЗаписи.СуммаНалога <> ТаблицаПередЗаписью.СуммаНалога
	|				ИЛИ ТаблицаПриЗаписи.СуммаНалогаКУплате <> ТаблицаПередЗаписью.СуммаНалогаКУплате
	|				ИЛИ ТаблицаПриЗаписи.СуммаАвансовыхПлатежей <> ТаблицаПередЗаписью.СуммаАвансовыхПлатежей
	|				ИЛИ НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.ПериодРасчета, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.ПериодРасчета, МЕСЯЦ))
	|			И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|
	|	) КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ИзмененныеДанные.Период           КАК Месяц,
	|	ИзмененныеДанные.Организация      КАК Организация,
	|	ИзмененныеДанные.ОсновноеСредство КАК ОбъектУчета,
	|	&Регистратор                    КАК Документ
	|ИЗ
	|	ВТ_ИзмененныеДанные КАК ИзмененныеДанные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СтоимостьОС КАК ДанныеКРасчету
	|		ПО ДанныеКРасчету.Организация = ИзмененныеДанные.Организация
	|			И ДанныеКРасчету.ОсновноеСредство = ИзмененныеДанные.ОсновноеСредство
	|			И ДанныеКРасчету.Период >= ИзмененныеДанные.НачалоМесяца
	|			И ДанныеКРасчету.Период <= ИзмененныеДанные.КонецМесяца
	|			И ДанныеКРасчету.Активность
	|			И ДанныеКРасчету.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			И ДанныеКРасчету.ХозяйственнаяОперация В (
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОС), 
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуПредметовАренды),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.МодернизацияОС))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ИзмененныеДанные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ РасчетНалогаНаИмуществоПередЗаписью";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Выборка = Запрос.Выполнить().Выбрать();
	РегистрыСведений.ЗаданияКРасчетуСтоимостиВНА.СоздатьЗаписиРегистраПоДаннымВыборки(Выборка);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
