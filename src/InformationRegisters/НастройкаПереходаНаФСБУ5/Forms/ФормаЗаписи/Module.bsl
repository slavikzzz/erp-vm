
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ПриЧтенииСозданииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ПриЧтенииСозданииНаСервере();

КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)

	Если НЕ ЗначениеЗаполнено(Запись.Организация)
		И ДляВсехОрганизаций = 0 Тогда
		ТекстСообщения = НСтр("ru = 'Не заполнено поле ""Организация""';
								|en = 'The ""Company"" field is not filled in'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "Организация", "Запись", Отказ);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Запись.ГодСписанияСтоимости)
		И Запись.ГодСписанияСтоимости < Элементы.ГодСписанияСтоимости.МинимальноеЗначение Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Год списания не может быть раньше %1 года';
										|en = 'A write-off year cannot be earlier than %1'"), Формат(Элементы.ГодСписанияСтоимости.МинимальноеЗначение, "ЧГ=0;"));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "ГодСписанияСтоимости", "Запись", Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДляВсехОрганизацийПриИзменении(Элемент)
	Запись.Организация = ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка");
	НастроитьЗависимыеЭлементыФормыНаКлиентеНаСервере(ЭтотОбъект, Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ДляВыбраннойОрганизацииПриИзменении(Элемент)
	НастроитьЗависимыеЭлементыФормыНаКлиентеНаСервере(ЭтотОбъект, Элемент);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()

	НастроитьЗависимыеЭлементыФормыНаКлиентеНаСервере(ЭтотОбъект);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЗависимыеЭлементыФормыНаКлиентеНаСервере(Форма, Знач ИзмененныеРеквизитыИлиЭлемент = "")
	
	Если ТипЗнч(ИзмененныеРеквизитыИлиЭлемент) = Тип("Строка") Тогда
		ИзмененныеРеквизиты = ИзмененныеРеквизитыИлиЭлемент;
	Иначе
		ИзмененныеРеквизиты = ИзмененныеРеквизитыИлиЭлемент.Имя;
	КонецЕсли;
	
	СтруктураИзмененныхРеквизитов = Новый Структура(ИзмененныеРеквизиты);
	
	ОбновитьВсе = СтруктураИзмененныхРеквизитов.Количество() = 0;
	
	Запись = Форма.Запись;
	Элементы = Форма.Элементы;
	
	Если ОбновитьВсе Тогда
		Форма.ДляВсехОрганизаций = ?(ЗначениеЗаполнено(Запись.Организация), 0, 1);
		Элементы.ГодСписанияСтоимости.МинимальноеЗначение = Год(РеглУчетКлиентСервер.НачалоПримененияФСБУ5_2019()) - 1;
	КонецЕсли;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("ДляВсехОрганизаций")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ДляВыбраннойОрганизации")
		ИЛИ ОбновитьВсе Тогда
		
		Элементы.Организация.ТолькоПросмотр = Форма.ДляВсехОрганизаций;
		
		Элементы.Организация.ОтметкаНезаполненного = 
			Форма.ДляВсехОрганизаций = 0
			И НЕ ЗначениеЗаполнено(Запись.Организация);

	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
