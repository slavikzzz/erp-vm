#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Добавляет задания к расчету структуры заказа на производство
// 
// Параметры:
// 	Таблица - ТаблицаЗначений - задания к расчету
// 
Процедура ДобавитьЗадания(Таблица) Экспорт
	
	Колонки = ?(Таблица.Колонки.Найти("КлючПартия") = Неопределено,
		"ЗаказНаПроизводство",
		"ЗаказНаПроизводство, КлючПартия");
	Таблица.Свернуть(Колонки);
	
	УстановитьПривилегированныйРежим(Истина);
	
	СтруктураЗаказа.ЗаполнитьИЗаписатьНаборЗаписей(
		РегистрыСведений.ЗаданияКРасчетуСтруктурыЗаказаНормативныйГрафик.СоздатьНаборЗаписей(),
		Таблица,
		Ложь,
		Истина);
		
КонецПроцедуры

Процедура ДобавитьЗадание(ЗаказНаПроизводство, КлючПартия = Неопределено, ЗапуститьРасчет = Ложь) Экспорт
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("ЗаказНаПроизводство", Новый ОписаниеТипов("ДокументСсылка.ЗаказНаПроизводство2_2"));
	
	НоваяСтрока = Таблица.Добавить();
	НоваяСтрока.ЗаказНаПроизводство = ЗаказНаПроизводство;
	
	Если КлючПартия <> Неопределено Тогда
		Таблица.Колонки.Добавить("КлючПартия", Новый ОписаниеТипов("УникальныйИдентификатор"));
		НоваяСтрока.КлючПартия = КлючПартия;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	СтруктураЗаказа.ЗаполнитьИЗаписатьНаборЗаписей(
		РегистрыСведений.ЗаданияКРасчетуСтруктурыЗаказаНормативныйГрафик.СоздатьНаборЗаписей(),
		Таблица,
		Ложь,
		Истина);
	
	Если ЗапуститьРасчет Тогда
		РегистрыСведений.НормативныйГрафикСтруктурыЗаказа.ЗапуститьРасчет();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
