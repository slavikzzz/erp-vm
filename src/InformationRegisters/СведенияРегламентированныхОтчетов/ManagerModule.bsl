#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Начальное заполнение и обновление информационной базы.
Процедура НачальноеЗаполнение() Экспорт
	Обновить();
КонецПроцедуры

// Заполняет поставляемые сведения регистра.
Процедура Обновить() Экспорт
	
	НаборЗаписей = СоздатьНаборЗаписей();
	ТаблицаСоСсылками = НаборЗаписей.ВыгрузитьКолонки();
	
	ЗагружаемыеСведения = ОбщегоНазначения.ЗначениеИзСтрокиXML(ПолучитьМакет("ПредопределенныеЗначения").ПолучитьТекст());
	
	Для Каждого СтрокаТаблицы Из ЗагружаемыеСведения.Данные Цикл
		
		МетаданныеОтчета = Метаданные.Отчеты.Найти(СтрокаТаблицы.ИмяОтчета);
		РегламентированныйОтчет = Справочники.РегламентированныеОтчеты.НайтиПоРеквизиту("ИсточникОтчета", СтрокаТаблицы.ИмяОтчета);
		Если МетаданныеОтчета = Неопределено ИЛИ НЕ ЗначениеЗаполнено(РегламентированныйОтчет) Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаТаблицыССсылками = ТаблицаСоСсылками.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицыССсылками, СтрокаТаблицы);
		СтрокаТаблицыССсылками.РегламентированныйОтчет = РегламентированныйОтчет;
		
	КонецЦикла;
	
	НаборЗаписей.Загрузить(ТаблицаСоСсылками);
	НаборЗаписей.ДополнительныеСвойства.Вставить("ЗаписьОбщихДанных");
	ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли