#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

	ПодготовитьДанныеДляФормированияЗаданийПередЗаписью();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПодготовитьДанныеДляФормированияЗаданийПриЗаписи();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПодготовитьДанныеДляФормированияЗаданийПередЗаписью()
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаПередЗаписью.Период КАК Период,
	|	ТаблицаПередЗаписью.Договор КАК Договор,
	|	ТаблицаПередЗаписью.Состояние КАК Состояние
	|ПОМЕСТИТЬ ТаблицаПередЗаписью
	|ИЗ
	|	РегистрСведений.УсловияДоговоровАренды КАК ТаблицаПередЗаписью
	|ГДЕ
	|	ТаблицаПередЗаписью.Регистратор = &Регистратор";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПодготовитьДанныеДляФормированияЗаданийПриЗаписи()
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Регистратор КАК Документ,
	|	ТаблицаПередЗаписью.Договор.Организация КАК Организация,
	|	ТаблицаПередЗаписью.Период КАК Период
	|ПОМЕСТИТЬ УсловияДоговоровАрендыИзменение
	|ИЗ
	|	ТаблицаПередЗаписью КАК ТаблицаПередЗаписью
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УсловияДоговоровАренды КАК ТаблицаПриЗаписи
	|		ПО ТаблицаПриЗаписи.Договор = ТаблицаПередЗаписью.Договор
	|			И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|ГДЕ
	|	(ТаблицаПриЗаписи.Период ЕСТЬ NULL
	|		ИЛИ ТаблицаПриЗаписи.Состояние <> ТаблицаПередЗаписью.Состояние
	|		ИЛИ НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, ДЕНЬ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, ДЕНЬ))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Регистратор,
	|	ТаблицаПриЗаписи.Договор.Организация,
	|	ТаблицаПриЗаписи.Период
	|ИЗ
	|	РегистрСведений.УсловияДоговоровАренды КАК ТаблицаПриЗаписи
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаПередЗаписью КАК ТаблицаПередЗаписью
	|		ПО ТаблицаПриЗаписи.Договор = ТаблицаПередЗаписью.Договор
	|ГДЕ
	|	(ТаблицаПередЗаписью.Период ЕСТЬ NULL
	|		ИЛИ ТаблицаПриЗаписи.Состояние <> ТаблицаПередЗаписью.Состояние
	|		ИЛИ НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, ДЕНЬ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, ДЕНЬ))
	|	И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаПередЗаписью";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
		"УсловияДоговоровАрендыИзменение", Выборка.Следующий() И Выборка.Количество > 0);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли