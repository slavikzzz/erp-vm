#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

Функция ЕстьНастройкаПодключенияЕНС(Организация = Неопределено)Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СостоянияПодключенияЕНС.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.СостоянияПодключенияЕНС КАК СостоянияПодключенияЕНС
	|ГДЕ
	|	&УсловиеОрганизация";
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ГоловнаяОрганизация = РегламентированнаяОтчетность.ГоловнаяОрганизация(Организация);
		Запрос.УстановитьПараметр("Организация", ГоловнаяОрганизация);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеОрганизация", "СостоянияПодключенияЕНС.Организация = &Организация");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеОрганизация", "Истина");
		
	КонецЕсли;
	Запрос.Текст = ТекстЗапроса;
	
	Результат = Запрос.Выполнить();
	
	ЕстьПодключенияЕНС = Не Результат.Пустой();
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ЕстьПодключенияЕНС;
	
КонецФункции

Процедура СохранитьПервичнуюНастройкуПодключения(Организация) Экспорт
	
	Если ЕстьНастройкаПодключенияЕНС(Организация) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	МенеджерЗаписи = РегистрыСведений.СостоянияПодключенияЕНС.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Организация = Организация;
	МенеджерЗаписи.Записать();
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура УстановитьИспользоватьСервисДанныхЕНС(Организация, ИспользоватьСервисДанныхЕНС) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерЗаписи = РегистрыСведений.СостоянияПодключенияЕНС.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Организация = Организация;
	МенеджерЗаписи.Прочитать();
	Если Не МенеджерЗаписи.Выбран() Тогда
		УстановитьПривилегированныйРежим(Ложь);
		Возврат;
	КонецЕсли;
	МенеджерЗаписи.ИспользоватьСервисДанныхЕНС = ИспользоватьСервисДанныхЕНС;
	МенеджерЗаписи.Записать();
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Функция ПолучитьИспользоватьСервисДанныхЕНС(Организация) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	МенеджерЗаписи = РегистрыСведений.СостоянияПодключенияЕНС.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Организация = Организация;
	МенеджерЗаписи.Прочитать();
	Если Не МенеджерЗаписи.Выбран() Тогда
		УстановитьПривилегированныйРежим(Ложь);
		Возврат Ложь;
	КонецЕсли;
	ИспользоватьСервисДанныхЕНС = МенеджерЗаписи.ИспользоватьСервисДанныхЕНС;
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ИспользоватьСервисДанныхЕНС;
	
КонецФункции

Процедура СохранитьФактРегистрацииСервисом(Организация) Экспорт
	
	МенеджерЗаписи = РегистрыСведений.СостоянияПодключенияЕНС.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Организация = Организация;
	МенеджерЗаписи.Прочитать();
	Если Не МенеджерЗаписи.Выбран() Тогда
		МенеджерЗаписи.Организация = Организация;
	КонецЕсли;
	МенеджерЗаписи.ИнтеграцияПодключена = Истина;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

Процедура УстановитьСостояниеПодключения(Организация, ПараметрыСостояния) Экспорт
	
	МенеджерЗаписи = РегистрыСведений.СостоянияПодключенияЕНС.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Организация = Организация;
	МенеджерЗаписи.Прочитать();
	Если МенеджерЗаписи.Выбран() Тогда
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ПараметрыСостояния);
		МенеджерЗаписи.Записать();
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьСостяниеПодключения(Организация) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	СтруктураПараметров = СтруктураПараметровСостоянияПодключения();
	МенеджерЗаписи = РегистрыСведений.СостоянияПодключенияЕНС.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Организация = Организация;
	МенеджерЗаписи.Прочитать();
	ЗаполнитьЗначенияСвойств(СтруктураПараметров, МенеджерЗаписи);
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат СтруктураПараметров;
	
КонецФункции

Функция СтруктураПараметровСостоянияПодключения() Экспорт
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ИнтеграцияПодключена",         Ложь);
	СтруктураПараметров.Вставить("ИспользоватьЖурналирование",   Ложь);
	СтруктураПараметров.Вставить("ПредельнаяДатаЖурналирования", Дата(1,1,1));
	СтруктураПараметров.Вставить("ИспользоватьСервисДанныхЕНС",  Ложь);
	
	Возврат СтруктураПараметров;
	
КонецФункции

Процедура ОтключитьЖурналирование(Организация) Экспорт
	
	СостяниеПодключения = ПолучитьСостяниеПодключения(Организация);
	СостяниеПодключения.ИспользоватьЖурналирование = Ложь;
	СостяниеПодключения.ПредельнаяДатаЖурналирования = Дата(1,1,1);
	УстановитьСостояниеПодключения(Организация,СостяниеПодключения);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли