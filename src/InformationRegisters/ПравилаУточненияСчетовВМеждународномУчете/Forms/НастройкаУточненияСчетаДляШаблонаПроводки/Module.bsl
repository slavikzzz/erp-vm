
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не (Параметры.Свойство("ШаблонПроводки")
		И Параметры.Свойство("ВидДвижения")) Тогда
	    ТекстСообщения = НСтр("ru = 'Непосредственное открытие этой формы не предусмотрено';
								|en = 'Application cannot open this form explicitly.'");
	    ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	ШаблонПроводки = Параметры.ШаблонПроводки;
	РеквизитыШаблонПроводки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ШаблонПроводки,
		"ПланСчетов, ТипИсточникаУточненияСчетаДт, СчетДебетаПоУмолчанию, ТипИсточникаУточненияСчетаКт, СчетКредитаПоУмолчанию");
	ПланСчетов = РеквизитыШаблонПроводки.ПланСчетов;
	ВидДвижения = Параметры.ВидДвижения;

	Если ВидДвижения = Перечисления.ВидыДвиженийБухгалтерии.Дебет Тогда
		РеквизитыСчетаДт = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РеквизитыШаблонПроводки.СчетДебетаПоУмолчанию, "Код, Наименование");
		ТипИсточникаУточненияСчета = РеквизитыШаблонПроводки.ТипИсточникаУточненияСчетаДт;
		СчетПоУмолчаниюПредставление = РеквизитыСчетаДт.Код + ", " + РеквизитыСчетаДт.Наименование;
		Элементы.СчетПоУмолчаниюПредставление.Заголовок = НСтр("ru = 'Счет Дт (по умолчанию)';
																|en = 'Dr account (default)'");
	Иначе
		РеквизитыСчетаКт = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РеквизитыШаблонПроводки.СчетКредитаПоУмолчанию, "Код, Наименование");
		ТипИсточникаУточненияСчета = РеквизитыШаблонПроводки.ТипИсточникаУточненияСчетаКт;
		СчетПоУмолчаниюПредставление = РеквизитыСчетаКт.Код + ", " + РеквизитыСчетаКт.Наименование;
		Элементы.СчетПоУмолчаниюПредставление.Заголовок = НСтр("ru = 'Счет Кт (по умолчанию)';
																|en = 'Cr account (default)'");
	КонецЕсли;
	
	Элементы.ГруппыФинансовогоУчетаНаименование.Заголовок = ТипИсточникаУточненияСчета;
	
	ОтборГруппФинансовогоУчета = "Все";
	
	ПоказатьСчетаУчета();
		
	Если Параметры.Свойство("ГруппаФинансовогоУчета") Тогда
		УстановитьТекущуюСтрокуТаблицыСчетов(Параметры.ГруппаФинансовогоУчета);				
	КонецЕсли;
	
	УстановитьДоступностьВидимостьЭлементовНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	МассивВыделенныхСтрок = Элементы.ГруппыФинансовогоУчета.ВыделенныеСтроки;
	Если МассивВыделенныхСтрок.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		ЗаполнитьСчетаУчета(МассивВыделенныхСтрок, ВыбранноеЗначение);		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаЗаписиНового(НовыйОбъект, Источник, СтандартнаяОбработка)
	
	ПоказатьСчетаУчета();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборГруппФинансовогоУчетаПриИзменении(Элемент)
	
	ПоказатьСчетаУчета();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

&НаКлиенте
Процедура ГруппыФинансовогоУчетаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле = Элементы.ГруппыФинансовогоУчетаНаименование Тогда
		ТекущиеДанные = ГруппыФинансовогоУчета.НайтиПоИдентификатору(ВыбраннаяСтрока);
		
		ПараметрыФормы = Новый Структура("Ключ", ТекущиеДанные.Ссылка);
		ИмяСправочника = МеждународныйУчетКлиентСервер.ИмяСправочникаГруппФинансовогоУчета(ТипИсточникаУточненияСчета);
		ОткрытьФорму("Справочник." + ИмяСправочника + ".ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьГруппуФинансовогоУчета(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработчикПоказатьВопросПриСозданииГруппыФинансовогоУчета", ЭтотОбъект);
	ТекстВопроса = НСтр("ru = 'Текущие настройки уточнения счета будут записаны. Продолжить?';
						|en = 'The current settings of account specification will be saved. Continue?'");
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	МассивВыделенныхСтрок = Элементы.ГруппыФинансовогоУчета.ВыделенныеСтроки;
	Если МассивВыделенныхСтрок.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор", Новый Структура("ПланСчетов", ПланСчетов));
	ОткрытьФорму("ПланСчетов.Международный.ФормаВыбора", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ЗаписатьСчетаУчета();
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	
	ЗаписатьСчетаУчета();
	Модифицированность = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ГруппыФинансовогоУчетаСчетУчета.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ГруппыФинансовогоУчета.СчетУчета");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<Счет по умолчанию>';
																|en = '<Default account>'"));

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ГруппыФинансовогоУчетаНаименование.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ГруппыФинансовогоУчета.СчетУчета");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);

КонецПроцедуры

&НаСервере
Процедура ПоказатьСчетаУчета()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПравилаУточненияСчетов.ГруппаФинансовогоУчета,
	|	ПравилаУточненияСчетов.ШаблонПроводки,
	|	ПравилаУточненияСчетов.ВидДвижения,
	|	ПравилаУточненияСчетов.СчетУчета
	|ПОМЕСТИТЬ ПравилаУточненияСчетов
	|ИЗ
	|	РегистрСведений.ПравилаУточненияСчетовВМеждународномУчете КАК ПравилаУточненияСчетов
	|ГДЕ
	|	ПравилаУточненияСчетов.ШаблонПроводки = &ШаблонПроводки
	|	И ПравилаУточненияСчетов.ВидДвижения = &ВидДвижения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГруппыФинансовогоУчета.Ссылка КАК Ссылка,
	|	ГруппыФинансовогоУчета.Наименование КАК Наименование,
	|	ПравилаУточненияСчетов.СчетУчета КАК СчетУчета
	|ИЗ
	|	&СправочникГруппыФинансовогоУчета КАК ГруппыФинансовогоУчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПравилаУточненияСчетов КАК ПравилаУточненияСчетов
	|		ПО (ПравилаУточненияСчетов.ГруппаФинансовогоУчета = ГруппыФинансовогоУчета.Ссылка)
	|ГДЕ
	|	ГруппыФинансовогоУчета.ПометкаУдаления = ЛОЖЬ
	|	И ГруппыФинансовогоУчета.ЭтоГруппа = ЛОЖЬ
	|	И (ПравилаУточненияСчетов.СчетУчета ЕСТЬ НЕ NULL ИЛИ &ПоказыватьВсеГруппыФинансовогоУчета)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ГруппыФинансовогоУчета.Наименование
	|";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&СправочникГруппыФинансовогоУчета", 
	   "Справочник." + МеждународныйУчетКлиентСервер.ИмяСправочникаГруппФинансовогоУчета(ТипИсточникаУточненияСчета));
	
	Запрос.УстановитьПараметр("ШаблонПроводки", ШаблонПроводки);
	Запрос.УстановитьПараметр("ВидДвижения", ВидДвижения);
	Запрос.УстановитьПараметр("ПоказыватьВсеГруппыФинансовогоУчета", ОтборГруппФинансовогоУчета = "Все");
	
	ГруппыФинансовогоУчета.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТекущуюСтрокуТаблицыСчетов(ГруппаФинансовогоУчета)

	ПараметрыОтбора = Новый Структура("Ссылка", ГруппаФинансовогоУчета);
	МассивСтрок = ГруппыФинансовогоУчета.НайтиСтроки(ПараметрыОтбора);
	Если МассивСтрок.Количество() > 0 Тогда
		Элементы.ГруппыФинансовогоУчета.ТекущаяСтрока = МассивСтрок[0].ПолучитьИдентификатор();	
		Элементы.ГруппыФинансовогоУчета.ТекущийЭлемент = Элементы.ГруппыФинансовогоУчетаСчетУчета;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаписатьСчетаУчета()

	НаборЗаписей = РегистрыСведений.ПравилаУточненияСчетовВМеждународномУчете.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ШаблонПроводки.Установить(ШаблонПроводки);
	НаборЗаписей.Отбор.ВидДвижения.Установить(ВидДвижения);
	Для каждого  Строка Из ГруппыФинансовогоУчета Цикл
		Если ЗначениеЗаполнено(Строка.СчетУчета) Тогда
 			НоваяЗапись	= НаборЗаписей.Добавить();
			НоваяЗапись.ШаблонПроводки = ШаблонПроводки;
			НоваяЗапись.ГруппаФинансовогоУчета = Строка.Ссылка;
			НоваяЗапись.ВидДвижения = ВидДвижения;
			НоваяЗапись.СчетУчета = Строка.СчетУчета;
		КонецЕсли;
	КонецЦикла;
	НаборЗаписей.Записать();

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСчетаУчета(МассивВыделенныхСтрок, СчетУчета)

	Для каждого ИдентификаторСтроки Из МассивВыделенныхСтрок Цикл
		НайденнаяСтрока = ГруппыФинансовогоУчета.НайтиПоИдентификатору(ИдентификаторСтроки);
		НайденнаяСтрока.СчетУчета = СчетУчета;
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьВидимостьЭлементовНаСервере()

	ДоступноИзменениеНастроекМФУ = МеждународныйУчетОбщегоНазначения.ДоступноИзменениеНастроекМеждународногоУчета();
	
	МассивЭлементов = Новый Массив;
	МассивЭлементов.Добавить("ФормаЗаписатьИЗакрыть");
	МассивЭлементов.Добавить("ГруппыФинансовогоУчета");
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивЭлементов, "ТолькоПросмотр", НЕ ДоступноИзменениеНастроекМФУ);
	
	МассивЭлементов = Новый Массив;
	МассивЭлементов.Добавить("СоздатьГруппуФинансовогоУчета");
	МассивЭлементов.Добавить("ИзменитьВыделенные");
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивЭлементов, "Видимость", ДоступноИзменениеНастроекМФУ);

КонецПроцедуры

&НаКлиенте
Процедура ОбработчикПоказатьВопросПриСозданииГруппыФинансовогоУчета(РезультатВопроса, ДополнительныеПараметры) Экспорт

	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЗаписатьСчетаУчета();
		ОткрытьФорму("Справочник."
					+ МеждународныйУчетКлиентСервер.ИмяСправочникаГруппФинансовогоУчета(ТипИсточникаУточненияСчета)
					+ ".ФормаОбъекта",, ЭтаФорма);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
