
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Запись, Параметры);
	
	ПриЧтенииСозданииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриЧтенииСозданииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НоменклатураПриИзменении(Элемент)
	
	НоменклатураПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	
	УправлениеДоступностью();
	
КонецПроцедуры

&НаСервере
Процедура УправлениеДоступностью()
	
	ХарактеристикиИспользуются = Справочники.Номенклатура.ХарактеристикиИспользуются(Запись.Номенклатура);
	
	Элементы.Характеристика.Доступность = ХарактеристикиИспользуются
		ИЛИ ЗначениеЗаполнено(Запись.Характеристика);
	
КонецПроцедуры

&НаСервере
Процедура НоменклатураПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(Запись.Характеристика) Тогда
		
		РезультатПроверки = Справочники.Номенклатура.ХарактеристикаИУпаковкаПринадлежатВладельцу(
			Запись.Номенклатура, Запись.Характеристика, Неопределено);
		
		Запись.Характеристика = РезультатПроверки.Характеристика;
		
	КонецЕсли;
	
	УправлениеДоступностью();
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти