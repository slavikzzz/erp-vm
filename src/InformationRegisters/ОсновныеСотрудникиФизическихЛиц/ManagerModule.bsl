
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(ГоловнаяОрганизация)
	|	И ЗначениеРазрешено(ФизическоеЛицо)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ОбменДанными

// Пересчитывает зависимые данные после загрузки сообщения обмена
//
// Параметры:
//		ЗависимыеДанные - ТаблицаЗначений:
//			* ВедущиеМетаданные - ОбъектМетаданных - Метаданные ведущих данных
//			* ЗависимыеМетаданные - ОбъектМетаданных - Метаданные текущего объекта
//			* ВедущиеДанные - Массив - Массив объектов, заполненный при загрузке сообщения обмена.
//				По этим объектам требуется обновить зависимые данные.
//
Процедура ОбновитьЗависимыеДанныеПослеЗагрузкиОбменаДанными(ЗависимыеДанные) Экспорт
	
	МассивСотрудников = Новый Массив;
	МассивСотрудниковГПХ = Новый Массив;
	МассивСотрудниковТрудовыеДоговоры = Новый Массив;
	
	Для Каждого СтрокаТаблицы Из ЗависимыеДанные Цикл
		
		Для Каждого ВедущиеДанные Из СтрокаТаблицы.ВедущиеДанные Цикл
			
			Если СтрокаТаблицы.ВедущиеМетаданные = Метаданные.Справочники.Сотрудники Тогда
				
				Если ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ВедущиеДанные.ДополнительныеСвойства, "ЭтоНовый", Ложь) Тогда 					
					МассивСотрудников.Добавить(ВедущиеДанные.Ссылка);
				КонецЕсли;
				
			ИначеЕсли СтрокаТаблицы.ВедущиеМетаданные = Метаданные.РегистрыСведений.ПериодыДействияДоговоровГражданскоПравовогоХарактера Тогда
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивСотрудниковГПХ, ВедущиеДанные.ДополнительныеСвойства.ОбновляемыеСотрудники, Истина);
			ИначеЕсли СтрокаТаблицы.ВедущиеМетаданные = Метаданные.РегистрыСведений.ВидыЗанятостиСотрудников Тогда
				
				ТаблицаИзменившихсяДанныхНабора = ВедущиеДанные.ТаблицаИзменившихсяДанныхНабора();
				Сотрудники = ТаблицаИзменившихсяДанныхНабора.ВыгрузитьКолонку("Сотрудник");
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивСотрудниковТрудовыеДоговоры, Сотрудники, Истина);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Если МассивСотрудников.Количество() > 0 Тогда
		КадровыйУчет.ОбновитьОсновныхСотрудниковФизическихЛицПоСотрудникам(МассивСотрудников);
	КонецЕсли;
	Если МассивСотрудниковГПХ.Количество() > 0 Тогда
		КадровыйУчет.ОбновитьОсновныхСотрудниковФизическихЛицСотрудниковПоДоговорамГПХ(МассивСотрудниковГПХ);
	КонецЕсли;
	Если МассивСотрудниковТрудовыеДоговоры.Количество() > 0 Тогда
		КадровыйУчет.ОбновитьОсновныхСотрудниковФизическихЛицПоТрудовымДоговорам(МассивСотрудниковТрудовыеДоговоры);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПриДобавленииИсключенийПоискаСсылок

// См. ОбщегоНазначенияПереопределяемый.ПриДобавленииИсключенийПоискаСсылок.
Процедура ПриДобавленииИсключенийПоискаСсылок(ИсключенияПоискаСсылок) Экспорт
	ИсключенияПоискаСсылок.Добавить(Метаданные.РегистрыСведений.ОсновныеСотрудникиФизическихЛиц);
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
