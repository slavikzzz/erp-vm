#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтотОбъект.ТолькоПросмотр = Истина;
	
	ПоказыватьСсылкуНаЖурналРегистрации = (Запись.Состояние = "Ошибка")
	                                    И ПравоДоступа("ЖурналРегистрации", Метаданные);
	
	Элементы.ДекорацияИнформацияОбОшибках.Видимость = ПоказыватьСсылкуНаЖурналРегистрации;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДекорацияИнформацияОбОшибкахНажатие(Элемент)
	
	ОтборСобытий = Новый Структура;
	
	ОтборСобытий.Вставить("СобытиеЖурналаРегистрации", "Отчетность в банки");
	ОтборСобытий.Вставить("ИмяПриложения", ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве("BackgroundJob"));
	ОтборСобытий.Вставить("ДатаНачала", Запись.ДатаОтправки - 60);
	ОтборСобытий.Вставить("ДатаОкончания", Запись.ДатаОтправки + 600);
	
	ЖурналРегистрацииКлиент.ОткрытьЖурналРегистрации(ОтборСобытий, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти