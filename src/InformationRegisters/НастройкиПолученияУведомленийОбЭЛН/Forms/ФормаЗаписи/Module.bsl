#Область ОписаниеПеременных

&НаКлиенте
Перем ОбновитьИнтерфейс;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбновитьВидимостьДоступность(Истина);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_НастройкиПолученияУведомленийОбЭЛН", Новый Структура, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СтраховательПриИзменении(Элемент)
	ОбновитьВидимостьДоступность(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьСЭДОПриИзменении(Элемент)
	ПриИзмененииРеквизита(Элемент);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// Клиент

&НаКлиенте
Процедура ПриИзмененииРеквизита(Элемент)
	
	ОбновлятьИнтерфейс = Ложь;
	ПриИзмененииРеквизитаСервер(Элемент.Имя, ОбновлятьИнтерфейс);
	ОбновитьПовторноИспользуемыеЗначения();
	
	Если ОбновлятьИнтерфейс Тогда
		ОбновитьИнтерфейс = Истина;
		ПодключитьОбработчикОжидания("ОбновитьИнтерфейсПрограммы", 1, Истина);
	КонецЕсли;
	
	Оповестить("Запись_НастройкиПолученияУведомленийОбЭЛН", Новый Структура, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИнтерфейсПрограммы()
	
	Если ОбновитьИнтерфейс = Истина Тогда
		ОбновитьИнтерфейс = Ложь;
		ОбщегоНазначенияКлиент.ОбновитьИнтерфейсПрограммы();
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Вызов сервера

&НаСервере
Процедура ПриИзмененииРеквизитаСервер(ИмяЭлемента, ОбновлятьИнтерфейс)
	Если ИмяЭлемента = Элементы.ПолучатьУведомленияОбЭЛН.Имя Тогда
		ОбновлятьИнтерфейс = Истина;
	КонецЕсли;
	
	Записать();
	ОбновитьПовторноИспользуемыеЗначения();
	ОбновитьВидимостьДоступность(Истина);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Сервер

&НаСервере
Процедура ОбновитьВидимостьДоступность(ИзменятьВидимостьСтрахователя)
	
	Если ЗначениеЗаполнено(Запись.Страхователь) И ИзменятьВидимостьСтрахователя Тогда
		Элементы.Страхователь.Видимость = Ложь;
		АвтоЗаголовок = Ложь;
		Заголовок = СтрШаблон(НСтр("ru = '%1: Настройки получения состояний ЭЛН';
									|en = '%1: Settings for receiving ESLR statuses'"), Запись.Страхователь);
	КонецЕсли;
	
	Элементы.ПолучатьУведомленияОбЭЛН.ТолькоПросмотр = Не Запись.ПолучатьУведомленияОбЭЛН;
	
	КлючСохраненияПоложенияОкна = Формат(Элементы.Страхователь.Видимость, "БЛ=0; БИ=1");
	
КонецПроцедуры

#КонецОбласти
