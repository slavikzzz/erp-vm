#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	// Событие срабатывает, например, если указано стандартное имя формы, но не сработает если указана конкретная форма.
	// Например, сработает для "РегистрСведений.НастройкиПолученияУведомленийОбЭЛН.ФормаЗаписи",
	// но не сработает для "РегистрСведений.НастройкиПолученияУведомленийОбЭЛН.Форма.ФормаЗаписи".
	Если СтрСравнить(ВидФормы, "ФормаЗаписи") = 0 Тогда
		// К сожалению, если запись отсутствует в регистре сведений,
		// то при открытии формы записи возникнет ошибка "Объект не найден".
		// Вариант решения - создать запись перед открытием формы.
		Страхователь = ОбщегоНазначенияБЗК.ЗначениеСвойства(Параметры, "Ключ.Страхователь");
		Если ТипЗнч(Страхователь) = Тип("СправочникСсылка.Организации") И Не Страхователь.Пустая() Тогда
			УстановитьПривилегированныйРежим(Истина);
			Набор = НачатьЗаписьНабора(Страхователь);
			Если Набор.Количество() = 0 Тогда
				Набор.Добавить().Страхователь = Страхователь;
				ЗавершитьЗаписьНабора(Набор);
			Иначе
				ОтменитьЗаписьНабора(Набор);
			КонецЕсли;
			УстановитьПривилегированныйРежим(Ложь);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Страхователь)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ОбработчикиПравилРегистрации

// См. ЗарплатаКадрыРасширенныйСинхронизацияДанных.ШаблонОбработчика
Процедура ПриЗаполненииНастроекОбработчиковПравилРегистрации(Настройки) Экспорт
	ЗарплатаКадрыРасширенныйСинхронизацияДанных.ДляНезависимогоРегистра(Настройки, "Страхователь",
		"РегламентированныеДанные");
КонецПроцедуры

#КонецОбласти

#Область НаборЗаписей

// АПК:326-выкл. Методы поставляются комплектом и предназначены для совместного последовательного использования.
// АПК:325-выкл. Методы поставляются комплектом и предназначены для совместного последовательного использования.
// Транзакция открывается в методе НачатьЗаписьНабора, закрывается в ЗавершитьЗаписьНабора, отменяется в ОтменитьЗаписьНабора.

// Транзакционный вариант (с управляемой блокировкой) получения набора записей, соответствующего значениям измерений.
//
// Параметры:
//   Страхователь - ОпределяемыйТип.Страхователь - Значение отбора по соответствующему измерению.
//
// Возвращаемое значение:
//   РегистрСведенийНаборЗаписей.НастройкиПолученияУведомленийОбЭЛН - Если удалось установить блокировку
//       и прочитать набор записей. При необходимости, открывает свою локальную транзакцию. Для закрытия транзакции
//       следует использовать одну из терминирующих процедур: ЗавершитьЗаписьНабора, либо ОтменитьЗаписьНабора.
//   Неопределено - Если не удалось установить блокировку и прочитать набор записей.
//       Вызывать процедуры ЗавершитьЗаписьНабора, ОтменитьЗаписьНабора не требуется,
//       поскольку если перед блокировкой функции потребовалось открыть локальную транзакцию,
//       то после неудачной блокировки локальная транзакция была отменена.
//
Функция НачатьЗаписьНабора(Страхователь) Экспорт
	Если Не ЗначениеЗаполнено(Страхователь) Тогда
		Возврат Неопределено;
	КонецЕсли;
	ПолныеПраваИлиПривилегированныйРежим = Пользователи.ЭтоПолноправныйПользователь();
	Если Не ПолныеПраваИлиПривилегированныйРежим
		И Не ПравоДоступа("Изменение", Метаданные.РегистрыСведений.НастройкиПолученияУведомленийОбЭЛН) Тогда
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Недостаточно прав для изменения регистра ""%1"".';
				|en = 'Insufficient rights to change register ""%1"".'"),
			Метаданные.РегистрыСведений.НастройкиПолученияУведомленийОбЭЛН.Представление());
	КонецЕсли;
	ЛокальнаяТранзакция = Не ТранзакцияАктивна();
	Если ЛокальнаяТранзакция Тогда
		НачатьТранзакцию();
	КонецЕсли;
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.НастройкиПолученияУведомленийОбЭЛН");
		ЭлементБлокировки.УстановитьЗначение("Страхователь", Страхователь);
		Блокировка.Заблокировать();
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Страхователь.Установить(Страхователь);
		НаборЗаписей.Прочитать();
		НаборЗаписей.ДополнительныеСвойства.Вставить("ЛокальнаяТранзакция", ЛокальнаяТранзакция);
	Исключение
		Если ЛокальнаяТранзакция Тогда
			ОтменитьТранзакцию();
		КонецЕсли;
		ТекстСообщения = СтрШаблон(
			НСтр("ru = 'Не удалось изменить настройки СЭДО ФСС %1 по причине: %2';
				|en = 'Cannot change %1 SSF EDI settings. Reason: %2'"),
			Страхователь,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Предупреждение,
			Метаданные.РегистрыСведений.НастройкиПолученияУведомленийОбЭЛН,
			Страхователь,
			ТекстСообщения);
		НаборЗаписей = Неопределено;
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат НаборЗаписей;
КонецФункции

// Записывает набор и фиксирует локальную транзакцию, если она была открыта в функции НачатьЗаписьНабора.
//
// Параметры:
//   НаборЗаписей - РегистрСведенийНаборЗаписей.НастройкиПолученияУведомленийОбЭЛН
//
Процедура ЗавершитьЗаписьНабора(НаборЗаписей) Экспорт
	НаборЗаписей.Записать(Истина);
	ЛокальнаяТранзакция = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(НаборЗаписей.ДополнительныеСвойства, "ЛокальнаяТранзакция");
	Если ЛокальнаяТранзакция = Истина Тогда
		ЗафиксироватьТранзакцию();
	КонецЕсли;
КонецПроцедуры

// Отменяет запись набора и отменяет локальную транзакцию, если она была открыта в функции НачатьЗаписьНабора.
//
// Параметры:
//   НаборЗаписей - РегистрСведенийНаборЗаписей.НастройкиПолученияУведомленийОбЭЛН
//
Процедура ОтменитьЗаписьНабора(НаборЗаписей) Экспорт
	ЛокальнаяТранзакция = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(НаборЗаписей.ДополнительныеСвойства, "ЛокальнаяТранзакция");
	Если ЛокальнаяТранзакция = Истина Тогда
		ОтменитьТранзакцию();
	КонецЕсли;
КонецПроцедуры

// АПК:326-вкл.
// АПК:325-вкл.

#КонецОбласти

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Функция ВключенОбменЧерезСЭДО() Экспорт
	Возврат Истина;
КонецФункции

// Возвращает массив организаций, которые могут получать сообщения об изменении ЭЛН.
Функция ОрганизацииПолучающиеСостоянияЭЛН() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Организации.Ссылка КАК Организация
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.ПометкаУдаления = ЛОЖЬ
	|	И НЕ(Организации.РегистрационныйНомерФСС = """"
	|				И Организации.ДополнительныйКодФСС = """")";
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Организация");
КонецФункции

Процедура УстановитьФлажкиОрганизаций(СписокСтрахователей) Экспорт
	Для Каждого ЭлементСписка Из СписокСтрахователей Цикл
		Страхователь = ЭлементСписка.Значение;
		Набор = НачатьЗаписьНабора(Страхователь);
		Если Набор.Количество() = 0 Тогда
			Запись = Набор.Добавить();
			Запись.Страхователь = Страхователь;
		Иначе
			Запись = Набор[0];
		КонецЕсли;
		Если Запись.ПолучатьУведомленияОбЭЛН = ЭлементСписка.Пометка Тогда
			ОтменитьЗаписьНабора(Набор);
			Продолжить;
		КонецЕсли;
		Запись.ПолучатьУведомленияОбЭЛН = ЭлементСписка.Пометка;
		ЗавершитьЗаписьНабора(Набор);
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#КонецЕсли
