#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	ВозможныеСтатусы = РегистрыСведений.РегистрацииУведомленийОПрекращенииОтпускаПоУходуЗаРебенком.ВозможныеСтатусы();
	Для Каждого Запись Из ЭтотОбъект Цикл
		Запись.Состояние = СостояниеРегистрации(Запись, ВозможныеСтатусы);
	КонецЦикла;
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	УстановитьПривилегированныйРежим(Истина);
	Фильтр = Новый Структура("Состояние", Перечисления.СостоянияДокументаСЭДОФСС.Принят);
	ОтветыНаЗапросы = Выгрузить().Скопировать(Фильтр, "ИсходящийДокумент").ВыгрузитьКолонку("ИсходящийДокумент");
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВходящийЗапросФССДляРасчетаПособия.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ВходящийЗапросФССДляРасчетаПособия КАК ВходящийЗапросФССДляРасчетаПособия
	|ГДЕ
	|	ВходящийЗапросФССДляРасчетаПособия.ОтветНаЗапрос В(&ОтветыНаЗапросы)";
	Запрос.УстановитьПараметр("ОтветыНаЗапросы", ОтветыНаЗапросы);
	МассивСсылок = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	Для Каждого ВходящийЗапрос Из МассивСсылок Цикл
		ВходящийЗапросОбъект = ВходящийЗапрос.ПолучитьОбъект();
		ВходящийЗапросОбъект.Обработан = Истина;
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ВходящийЗапросОбъект);
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СостояниеРегистрации(Запись, ВозможныеСтатусы)
	
	Если Не ЗначениеЗаполнено(Запись.ДоставкаДата) И Не ЗначениеЗаполнено(Запись.ДатаОтправкиОператору) Тогда
		// Не отправлен.
		Возврат Перечисления.СостоянияДокументаСЭДОФСС.ПустаяСсылка();
		
	ИначеЕсли ЗначениеЗаполнено(Запись.ДоставкаТекстОшибки) Тогда
		// Ошибка при отправке.
		Возврат Перечисления.СостоянияДокументаСЭДОФСС.ОшибкаПриОтправке;
		
	ИначеЕсли Запись.ЕстьОшибкиФЛК Тогда
		// Отправлен, получены ошибки.
		Возврат Перечисления.СостоянияДокументаСЭДОФСС.ОшибкаЛогическогоКонтроля;
		
	КонецЕсли;
	
	// Заполнена дата отправка и есть информация о статусе.
	СведенияОСтатусе = ВозможныеСтатусы.Найти(Запись.РегистрацияСтатус, "Статус");
	Если СведенияОСтатусе <> Неопределено Тогда
		Возврат СведенияОСтатусе.Состояние;
	КонецЕсли;
	
	Если Запись.ОтправленОператору И Не Запись.ДоставкаУспех Тогда
		// Передан оператору для доставки.
		Возврат Перечисления.СостоянияДокументаСЭДОФСС.ОтправленОператору;
		
	ИначеЕсли Не ЗначениеЗаполнено(Запись.РегистрацияСтатус) Тогда
		// Отправлен - заполнена дата отправка, нет информации о статусе.
		Возврат Перечисления.СостоянияДокументаСЭДОФСС.Отправлен;
		
	Иначе
		// Успешная доставка, заполнен статус, статус неизвестен.
		Возврат Перечисления.СостоянияДокументаСЭДОФСС.НеПринят;
	КонецЕсли;
	
КонецФункции

#КонецОбласти



#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли