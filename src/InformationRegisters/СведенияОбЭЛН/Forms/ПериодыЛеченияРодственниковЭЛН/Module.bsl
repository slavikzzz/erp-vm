#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ТекстXML = РегистрыСведений.СведенияОбЭЛН.ИсходныйXML(Параметры.НомерЛисткаНетрудоспособности, Параметры.ГоловнаяОрганизация);
	Если Не ЗначениеЗаполнено(ТекстXML) Тогда
		Текст = НСтр("ru = 'Не удалось получить данные ЭЛН %1';
					|en = 'Cannot get ESLR data %1'");
		ВызватьИсключение Текст;
	КонецЕсли;
	ДанныеЭЛН = ЭЛНФСС.ДанныеЭЛНИзXML(ТекстXML);
	Если Не ДанныеЭЛН.Успех Тогда
		ВызватьИсключение ДанныеЭЛН.ТекстОшибки;
	КонецЕсли;
	ПериодыЛечения = ДанныеЭЛН.ПериодыЛеченияРодственников;
	ПериодыЛечения.Колонки.Вставить(ПериодыЛечения.Колонки.Индекс(ПериодыЛечения.Колонки.ВозрастЛет), "Возраст");
	Для Каждого СтрокаТаблицы Из ПериодыЛечения Цикл
		СтрокаТаблицы.КодСвязи = ПрямыеВыплатыПособийСоциальногоСтрахованияФормы.ПредставлениеРодственнойСвязи(СтрокаТаблицы.КодСвязи);
		СтрокаТаблицы.ТипЛечения = СтрокаТаблицы.ТипЛечения + " - " + СтрокаТаблицы.РежимЛечения;
		СтрокаТаблицы.Возраст = ЗарплатаКадрыКлиентСервер.ПредставлениеВозраста(СтрокаТаблицы.ВозрастЛет, СтрокаТаблицы.ВозрастМесяцев);
	КонецЦикла;
	ПериодыЛечения.Колонки.Удалить("Фамилия");
	ПериодыЛечения.Колонки.Удалить("Имя");
	ПериодыЛечения.Колонки.Удалить("Отчество");
	ПериодыЛечения.Колонки.Удалить("РежимЛечения");
	ПериодыЛечения.Колонки.Удалить("ВозрастЛет");
	ПериодыЛечения.Колонки.Удалить("ВозрастМесяцев");
	
	ТабличныйДокумент1 = Новый ТабличныйДокумент;
	
	ПостроительОтчета = Новый ПостроительОтчета;
	ПостроительОтчета.ИсточникДанных = Новый ОписаниеИсточникаДанных(ПериодыЛечения);
	ПостроительОтчета.ЗаполнитьНастройки();
	ПостроительОтчета.ВыводитьЗаголовокОтчета = Ложь;
	ПостроительОтчета.ВыводитьПодвалОтчета = Ложь;
	ПостроительОтчета.ВыводитьПодвалТаблицы = Ложь;
	ПостроительОтчета.ВыводитьОбщиеИтоги = Ложь;
	
	// Получение автоматически сгенерированного макета
	ПостроительОтчета.Макет = Неопределено;
	Макет = ПостроительОтчета.Макет;
	ИскатьПоСтрокам = Истина;
	ЯчейкаЦеликом   = Истина;
	
	ОбластьПараметров = Макет.Области.Детали;
	Область = Макет.НайтиТекст("ДатаРождения", , ОбластьПараметров, ИскатьПоСтрокам, ЯчейкаЦеликом);
	Область.Формат = "ДЛФ=D";
	Область = Макет.НайтиТекст("ДатаНачала", , ОбластьПараметров, ИскатьПоСтрокам, ЯчейкаЦеликом);
	Область.Формат = "ДЛФ=D";
	Область = Макет.НайтиТекст("ДатаОкончания", , ОбластьПараметров, ИскатьПоСтрокам, ЯчейкаЦеликом);
	Область.Формат = "ДЛФ=D";
	
	ОбластьЗаголовков = Макет.Области.ШапкаТаблицы;
	Область = Макет.НайтиТекст("ФИО", , ОбластьЗаголовков, ИскатьПоСтрокам, ЯчейкаЦеликом);
	Область.ШиринаКолонки = 30;
	Область = Макет.НайтиТекст("КодСвязи", , ОбластьЗаголовков, ИскатьПоСтрокам, ЯчейкаЦеликом);
	Область.Текст = НСтр("ru = 'Код связи';
						|en = 'Relationship code'");
	Область.ШиринаКолонки = 13;
	Область = Макет.НайтиТекст("ДатаРождения", , ОбластьЗаголовков, ИскатьПоСтрокам, ЯчейкаЦеликом);
	Область.Текст = НСтр("ru = 'Дата рождения';
						|en = 'Date of birth'");
	Область.ШиринаКолонки = 13;
	Область = Макет.НайтиТекст("Возраст", , ОбластьЗаголовков, ИскатьПоСтрокам, ЯчейкаЦеликом);
	Область.Текст = НСтр("ru = 'Возраст';
						|en = 'Age'");
	Область.ШиринаКолонки = 15;
	Область = Макет.НайтиТекст("СНИЛС", , ОбластьЗаголовков, ИскатьПоСтрокам, ЯчейкаЦеликом);
	Область.ШиринаКолонки = 12;
	Область = Макет.НайтиТекст("КодПричины", , ОбластьЗаголовков, ИскатьПоСтрокам, ЯчейкаЦеликом);
	Область.Текст = НСтр("ru = 'Код причины';
						|en = 'Reason code'");
	Область.ШиринаКолонки = 12;
	Область = Макет.НайтиТекст("ТипЛечения", , ОбластьЗаголовков, ИскатьПоСтрокам, ЯчейкаЦеликом);
	Область.Текст = НСтр("ru = 'Тип лечения';
						|en = 'Treatment type'");
	Область.ШиринаКолонки = 18;
	Область = Макет.НайтиТекст("ДатаНачала", , ОбластьЗаголовков, ИскатьПоСтрокам, ЯчейкаЦеликом);
	Область.Текст = НСтр("ru = 'С';
						|en = 'From'");
	Область.ШиринаКолонки = 9;
	Область = Макет.НайтиТекст("ДатаОкончания", , ОбластьЗаголовков, ИскатьПоСтрокам, ЯчейкаЦеликом);
	Область.Текст = НСтр("ru = 'По';
						|en = 'To'");
	Область.ШиринаКолонки = 9;
	
	ПостроительОтчета.Макет = Макет;
	ПостроительОтчета.Вывести(ТабличныйДокумент1);
	
	// Второй табличный документ нужен для того, чтобы:
	// - избавиться от пустой первой строки,
	// - восстановить форматирование ширины колонок.
	ТабличныйДокумент.Вывести(ТабличныйДокумент1.ПолучитьОбласть(1, 2, ТабличныйДокумент1.ВысотаТаблицы, ТабличныйДокумент1.ШиринаТаблицы));
	ТабличныйДокумент.Область().СоздатьФорматСтрок();
	
	ЗаголовокДокумента = НСтр("ru = 'Периоды лечения родственников в ЭЛН %1';
								|en = 'Relative care periods in ESLR %1'");
	ЗаголовокДокумента = СтрШаблон(ЗаголовокДокумента, Параметры.НомерЛисткаНетрудоспособности);
	Заголовок = ЗаголовокДокумента;
КонецПроцедуры

#КонецОбласти
