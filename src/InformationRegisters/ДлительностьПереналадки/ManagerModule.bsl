#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Устанавливает длительность переналадки при переходе с одного вида наладки на другой.
//
//	Параметры:
//		ВидРабочегоЦентра			- СправочникСсылка.ВидыРабочихЦентров - Вид рабочего центра, для которого устанавливается время переналадки
//		ТекущийВариантНаладки		- СправочникСсылка.ВариантыНаладки - Вариант наладки, с которого выполняется переход
//		СледующийВариантНаладки		- СправочникСсылка.ВариантыНаладки - Вариант наладки, на который выполняется переход
//		ВремяПереналадки			- Число - Время переналадки с одного варианта наладки на другой.
//
Процедура УстановитьДлительностьПереналадки(ВидРабочегоЦентра, Знач ТекущийВариантНаладки = Неопределено, Знач СледующийВариантНаладки = Неопределено, ВремяПереналадки) Экспорт
	
	Если ТекущийВариантНаладки = Неопределено Тогда
		ТекущийВариантНаладки = Справочники.ВариантыНаладки.ПустаяСсылка();
	КонецЕсли; 
	Если СледующийВариантНаладки = Неопределено Тогда
		СледующийВариантНаладки = Справочники.ВариантыНаладки.ПустаяСсылка();
	КонецЕсли; 
	
	МенеджерЗаписи = РегистрыСведений.ДлительностьПереналадки.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ВидРабочегоЦентра       = ВидРабочегоЦентра;
	МенеджерЗаписи.ТекущийВариантНаладки   = ТекущийВариантНаладки;
	МенеджерЗаписи.СледующийВариантНаладки = СледующийВариантНаладки;
	Если ВремяПереналадки <> 0 Тогда
		МенеджерЗаписи.ВремяПереналадки = ВремяПереналадки;
		
		МенеджерЗаписи.Записать();
	Иначе
		МенеджерЗаписи.Удалить();
	КонецЕсли; 
	
КонецПроцедуры

// Возвращает настройку переналадки всех вариантов наладки вида рабочего центра.
//
// Параметры:
//   ВидРабочегоЦентра - СправочникСсылка.ВидыРабочихЦентров - Вид рабочего центра, для которого определяется время переналадки.
//
// Возвращаемое значение:
//   ТаблицаЗначений - длительность переналадки:
//     * ТекущийВариантНаладки - СправочникСсылка.ВариантыНаладки - вариант с которого выполняется переход.
//     * СледующийВариантНаладки - СправочникСсылка.ВариантыНаладки - вариант на который выполняется переход.
//     * ВремяПереналадки - Число - длительность переналадки.
//     * ЕдиницаВремениПереналадки - ПеречислениеСсылка.ЕдиницыИзмеренияВремени - единица измерения времени.
//     * НастройкаУнаследована - Булево - Истина если настройка унаследована по неполному соответствию вариантов наладки.
//     * Приоритет - Число - приоритет использования настройки.
//
Функция НастройкаПереналадки(ВидРабочегоЦентра) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВариантыНаладки.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВариантыНаладки
	|ИЗ
	|	Справочник.ВариантыНаладки КАК ВариантыНаладки
	|ГДЕ
	|	ВариантыНаладки.Владелец = &ВидРабочегоЦентра
	|	И НЕ ВариантыНаладки.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Справочник.ВариантыНаладки.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СпрТекущийВариантНаладки.Ссылка КАК ТекущийВариантНаладки,
	|	СпрСледующийВариантНаладки.Ссылка КАК СледующийВариантНаладки,
	|	ВЫБОР
	|		КОГДА СпрТекущийВариантНаладки.Ссылка = ЗНАЧЕНИЕ(Справочник.ВариантыНаладки.ПустаяСсылка)
	|				И СпрСледующийВариантНаладки.Ссылка = ЗНАЧЕНИЕ(Справочник.ВариантыНаладки.ПустаяСсылка)
	|			ТОГДА 1
	|		КОГДА СпрТекущийВариантНаладки.Ссылка <> ЗНАЧЕНИЕ(Справочник.ВариантыНаладки.ПустаяСсылка)
	|				И СпрСледующийВариантНаладки.Ссылка = ЗНАЧЕНИЕ(Справочник.ВариантыНаладки.ПустаяСсылка)
	|				И ДлительностьПереналадкиРабочийЦентр1.ВремяПереналадки ЕСТЬ NULL
	|			ТОГДА 4
	|		КОГДА СпрТекущийВариантНаладки.Ссылка = ЗНАЧЕНИЕ(Справочник.ВариантыНаладки.ПустаяСсылка)
	|				И СпрСледующийВариантНаладки.Ссылка <> ЗНАЧЕНИЕ(Справочник.ВариантыНаладки.ПустаяСсылка)
	|				И ДлительностьПереналадкиРабочийЦентр1.ВремяПереналадки ЕСТЬ NULL
	|			ТОГДА 4
	|		КОГДА НЕ ДлительностьПереналадкиРабочийЦентр1.ВремяПереналадки ЕСТЬ NULL
	|			ТОГДА 1
	|		КОГДА НЕ ДлительностьПереналадкиРабочийЦентр2.ВремяПереналадки ЕСТЬ NULL
	|			ТОГДА 2
	|		КОГДА НЕ ДлительностьПереналадкиРабочийЦентр3.ВремяПереналадки ЕСТЬ NULL
	|			ТОГДА 3
	|		ИНАЧЕ 4
	|	КОНЕЦ КАК Приоритет,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ДлительностьПереналадкиРабочийЦентр1.ТекущийВариантНаладки,
	|				ЕСТЬNULL(ДлительностьПереналадкиРабочийЦентр2.ТекущийВариантНаладки,
	|				ЕСТЬNULL(ДлительностьПереналадкиРабочийЦентр3.ТекущийВариантНаладки,
	|				ЕСТЬNULL(ДлительностьПереналадкиРабочийЦентр4.ТекущийВариантНаладки, ЗНАЧЕНИЕ(Справочник.ВариантыНаладки.ПустаяСсылка))))) = СпрТекущийВариантНаладки.Ссылка 
	|			И ЕСТЬNULL(ДлительностьПереналадкиРабочийЦентр1.СледующийВариантНаладки,
	|				ЕСТЬNULL(ДлительностьПереналадкиРабочийЦентр2.СледующийВариантНаладки,
	|				ЕСТЬNULL(ДлительностьПереналадкиРабочийЦентр3.СледующийВариантНаладки,
	|				ЕСТЬNULL(ДлительностьПереналадкиРабочийЦентр4.СледующийВариантНаладки, ЗНАЧЕНИЕ(Справочник.ВариантыНаладки.ПустаяСсылка))))) = СпрСледующийВариантНаладки.Ссылка
	|			ТОГДА ЛОЖЬ 
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК НастройкаУнаследована,
	|	ЕСТЬNULL(ДлительностьПереналадкиРабочийЦентр1.ВремяПереналадки,
	|		ЕСТЬNULL(ДлительностьПереналадкиРабочийЦентр2.ВремяПереналадки,
	|		ЕСТЬNULL(ДлительностьПереналадкиРабочийЦентр3.ВремяПереналадки,
	|		ЕСТЬNULL(ДлительностьПереналадкиРабочийЦентр4.ВремяПереналадки, 0)))) КАК ВремяПереналадки,
	|
	|	СпрВидыРабочихЦентров.ЕдиницаВремениПереналадки КАК ЕдиницаВремениПереналадки
	|ИЗ
	|	ВариантыНаладки КАК СпрТекущийВариантНаладки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВариантыНаладки КАК СпрСледующийВариантНаладки
	|		ПО (СпрСледующийВариантНаладки.Ссылка = ЗНАЧЕНИЕ(Справочник.ВариантыНаладки.ПустаяСсылка)
	|				ИЛИ СпрСледующийВариантНаладки.Ссылка <> СпрТекущийВариантНаладки.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыРабочихЦентров КАК СпрВидыРабочихЦентров
	|		ПО СпрВидыРабочихЦентров.Ссылка = &ВидРабочегоЦентра
	
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДлительностьПереналадки КАК ДлительностьПереналадкиРабочийЦентр1
	|		ПО (ДлительностьПереналадкиРабочийЦентр1.ВидРабочегоЦентра = &ВидРабочегоЦентра)
	|			И (ДлительностьПереналадкиРабочийЦентр1.ТекущийВариантНаладки = СпрТекущийВариантНаладки.Ссылка)
	|			И (ДлительностьПереналадкиРабочийЦентр1.СледующийВариантНаладки = СпрСледующийВариантНаладки.Ссылка)
	
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДлительностьПереналадки КАК ДлительностьПереналадкиРабочийЦентр2
	|		ПО (ДлительностьПереналадкиРабочийЦентр2.ВидРабочегоЦентра = &ВидРабочегоЦентра)
	|			И (ДлительностьПереналадкиРабочийЦентр2.ТекущийВариантНаладки = ЗНАЧЕНИЕ(Справочник.ВариантыНаладки.ПустаяСсылка))
	|			И (ДлительностьПереналадкиРабочийЦентр2.СледующийВариантНаладки = СпрСледующийВариантНаладки.Ссылка)
	
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДлительностьПереналадки КАК ДлительностьПереналадкиРабочийЦентр3
	|		ПО (ДлительностьПереналадкиРабочийЦентр3.ВидРабочегоЦентра = &ВидРабочегоЦентра)
	|			И (ДлительностьПереналадкиРабочийЦентр3.ТекущийВариантНаладки = СпрТекущийВариантНаладки.Ссылка)
	|			И (ДлительностьПереналадкиРабочийЦентр3.СледующийВариантНаладки = ЗНАЧЕНИЕ(Справочник.ВариантыНаладки.ПустаяСсылка))
	
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДлительностьПереналадки КАК ДлительностьПереналадкиРабочийЦентр4
	|		ПО (ДлительностьПереналадкиРабочийЦентр4.ВидРабочегоЦентра = &ВидРабочегоЦентра)
	|			И (ДлительностьПереналадкиРабочийЦентр4.ТекущийВариантНаладки = ЗНАЧЕНИЕ(Справочник.ВариантыНаладки.ПустаяСсылка))
	|			И (ДлительностьПереналадкиРабочийЦентр4.СледующийВариантНаладки = ЗНАЧЕНИЕ(Справочник.ВариантыНаладки.ПустаяСсылка))";

	Запрос.УстановитьПараметр("ВидРабочегоЦентра", ВидРабочегоЦентра);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли