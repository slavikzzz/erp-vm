#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка 
		Или Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства)
		Или Не ВнеоборотныеАктивы.ИспользуетсяУправлениеВНА() Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаПередЗаписью.Период,
	|	ТаблицаПередЗаписью.ОбъектЭксплуатации,
	|	ТаблицаПередЗаписью.ПоказательНаработки,
	|	ТаблицаПередЗаписью.Значение,
	|	ТаблицаПередЗаписью.СреднесуточноеЗначение
	|ПОМЕСТИТЬ НаработкиОбъектовЭксплуатацииПередЗаписью
	|ИЗ
	|	РегистрСведений.НаработкиОбъектовЭксплуатации КАК ТаблицаПередЗаписью
	|ГДЕ
	|	ТаблицаПередЗаписью.Регистратор = &Регистратор
	|	И ТаблицаПередЗаписью.Активность
	|	И ТаблицаПередЗаписью.ОбъектЭксплуатации ССЫЛКА Справочник.ОбъектыЭксплуатации";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка 
		Или Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства)
		Или Не ВнеоборотныеАктивы.ИспользуетсяУправлениеВНА() Тогда
		Возврат;
	КонецЕсли;
	
	СписокУдаляемыхВТ = Новый Массив;
	СписокУдаляемыхВТ.Добавить("НаработкиОбъектовЭксплуатацииПередЗаписью");
	СписокУдаляемыхВТ.Добавить("НаработкиОбъектовЭксплуатацииИзменениеБезОрганизации");
	
	СписокЗапросов = Новый Массив;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	МИНИМУМ(Таблица.Период)  КАК Период,
	|	Таблица.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ НаработкиОбъектовЭксплуатацииИзменениеБезОрганизации
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаПередЗаписью.Период              КАК Период,
	|		ТаблицаПередЗаписью.ОбъектЭксплуатации  КАК ОсновноеСредство
	|	ИЗ
	|		НаработкиОбъектовЭксплуатацииПередЗаписью КАК ТаблицаПередЗаписью
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НаработкиОбъектовЭксплуатации КАК ТаблицаПриЗаписи
	|			ПО ТаблицаПриЗаписи.ОбъектЭксплуатации = ТаблицаПередЗаписью.ОбъектЭксплуатации
	|				И ТаблицаПриЗаписи.ПоказательНаработки = ТаблицаПередЗаписью.ПоказательНаработки
	|				И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|		ГДЕ
	|			(НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, ДЕНЬ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, ДЕНЬ)
	|				ИЛИ ТаблицаПриЗаписи.Значение <> ТаблицаПередЗаписью.Значение
	|				ИЛИ ТаблицаПриЗаписи.СреднесуточноеЗначение <> ТаблицаПередЗаписью.СреднесуточноеЗначение
	|				ИЛИ ТаблицаПриЗаписи.Период ЕСТЬ NULL)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаПриЗаписи.Период,
	|		ТаблицаПриЗаписи.ОбъектЭксплуатации
	|	ИЗ
	|		РегистрСведений.НаработкиОбъектовЭксплуатации КАК ТаблицаПриЗаписи
	|			ЛЕВОЕ СОЕДИНЕНИЕ НаработкиОбъектовЭксплуатацииПередЗаписью КАК ТаблицаПередЗаписью
	|			ПО ТаблицаПриЗаписи.ОбъектЭксплуатации = ТаблицаПередЗаписью.ОбъектЭксплуатации
	|				И ТаблицаПриЗаписи.ПоказательНаработки = ТаблицаПередЗаписью.ПоказательНаработки
	|		ГДЕ
	|			(НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, ДЕНЬ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, ДЕНЬ)
	|				ИЛИ ТаблицаПриЗаписи.Значение <> ТаблицаПередЗаписью.Значение
	|				ИЛИ ТаблицаПриЗаписи.СреднесуточноеЗначение <> ТаблицаПередЗаписью.СреднесуточноеЗначение
	|				ИЛИ ТаблицаПередЗаписью.Период ЕСТЬ NULL)
	|			И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|
	|	) КАК Таблица
	|
	|СГРУППИРОВАТЬ ПО
	|	Таблица.ОсновноеСредство
	|	
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = ВнеоборотныеАктивыЛокализация.ТекстЗапросаТаблицаНаработкиОбъектовЭксплуатацииИзменение(СписокУдаляемыхВТ);
	
	Если ТекстЗапроса = Неопределено Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	МАКСИМУМ(ПараметрыАмортизацииОСУУ.Период) КАК Период_Последнее,
		|	ТаблицаИзменение.Период КАК Период,
		|	ТаблицаИзменение.ОсновноеСредство КАК ОсновноеСредство
		|ПОМЕСТИТЬ ПоНаработкеУУ_Последнее
		|ИЗ
		|	НаработкиОбъектовЭксплуатацииИзменениеБезОрганизации КАК ТаблицаИзменение
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСУУ КАК ПараметрыАмортизацииОСУУ
		|		ПО (ПараметрыАмортизацииОСУУ.ОсновноеСредство = ТаблицаИзменение.ОсновноеСредство)
		|			И (ПараметрыАмортизацииОСУУ.Период <= ТаблицаИзменение.Период)
		|			И (ПараметрыАмортизацииОСУУ.ДатаИсправления = ДАТАВРЕМЯ(1,1,1))
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаИзменение.ОсновноеСредство,
		|	ТаблицаИзменение.Период
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Период,
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПоНаработкеУУ_Последнее.Период КАК Период,
		|	ПоНаработкеУУ_Последнее.ОсновноеСредство КАК ОсновноеСредство,
		|	ПараметрыАмортизацииОСУУ.Организация КАК Организация,
		|	ИСТИНА КАК ОтражатьВРеглУчете,
		|	ИСТИНА КАК ОтражатьВУпрУчете,
		|	&Регистратор КАК Документ
		|ПОМЕСТИТЬ НаработкиОбъектовЭксплуатацииИзменение
		|ИЗ
		|	ПоНаработкеУУ_Последнее КАК ПоНаработкеУУ_Последнее
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСУУ КАК ПараметрыАмортизацииОСУУ
		|		ПО (ПараметрыАмортизацииОСУУ.ОсновноеСредство = ПоНаработкеУУ_Последнее.ОсновноеСредство)
		|			И (ПараметрыАмортизацииОСУУ.Период = ПоНаработкеУУ_Последнее.Период_Последнее)
		|			И (ПараметрыАмортизацииОСУУ.ДатаИсправления = ДАТАВРЕМЯ(1,1,1))
		|ГДЕ
		|	ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации В (
		|				ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции), 
		|				ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПоЕНАОФНа1000кмПробега))";
		
		СписокУдаляемыхВТ.Добавить("ПоНаработкеУУ_Последнее");
		
	КонецЕсли; 
	СписокЗапросов.Добавить(ТекстЗапроса);

	ТекстЗапроса = СтрСоединить(СписокЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Выборка = Запрос.Выполнить().Выбрать();
	ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
		"НаработкиОбъектовЭксплуатацииИзменение", Выборка.Следующий() И Выборка.Количество > 0);
	
	ОбщегоНазначенияУТ.УничтожитьВременныеТаблицы(ДополнительныеСвойства.МенеджерВременныхТаблиц, СписокУдаляемыхВТ);
		
КонецПроцедуры

#КонецОбласти

#КонецЕсли
