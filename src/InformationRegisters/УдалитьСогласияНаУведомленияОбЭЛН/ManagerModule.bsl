#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

Процедура АдаптацияКУчетуВРазрезеСтрахователей() Экспорт
	
	// Чтение данных регистра (их количество примерно соответствует количеству сотрудников) и физических лиц.
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РегистрСогласий.Сотрудник КАК Сотрудник,
	|	РегистрСогласий.Организация КАК Организация,
	|	РегистрСогласий.Подписано КАК Подписано,
	|	РегистрСогласий.ДатаСогласия КАК ДатаСогласия,
	|	РегистрСогласий.ДатаОтзываСогласия КАК ДатаОтзываСогласия,
	|	РегистрСогласий.Согласие КАК Согласие,
	|	РегистрСогласий.Состояние КАК Состояние,
	|	РегистрСогласий.ОснованиеОтзываСогласия КАК ОснованиеОтзываСогласия,
	|	РегистрСогласий.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ЕСТЬNULL(Сотрудники.ФизическоеЛицо, ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)) КАК ФизическоеЛицоСотрудника
	|ИЗ
	|	РегистрСведений.УдалитьСогласияНаУведомленияОбЭЛН КАК РегистрСогласий
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
	|		ПО РегистрСогласий.Сотрудник = Сотрудники.Ссылка";
	
	// Определение страхователей.
	ТаблицаРегистра = Запрос.Выполнить().Выгрузить();
	СЭДОФСС.ЗаполнитьСтрахователяВТаблицеЗначений(ТаблицаРегистра);
	
	Для Каждого СтрокаТаблицы Из ТаблицаРегистра Цикл
		
		// Проверка наличия страхователя.
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.Страхователь) Тогда
			Продолжить;
		КонецЕсли;
		
		// Проверка наличия физического лица.
		Если ЗначениеЗаполнено(СтрокаТаблицы.ФизическоеЛицоСотрудника) Тогда
			СтрокаТаблицы.ФизическоеЛицо = СтрокаТаблицы.ФизическоеЛицоСотрудника;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.ФизическоеЛицо) Тогда
			Продолжить;
		КонецЕсли;
		
		// Добавление записи в регистр со страхователями и физическими лицами.
		Набор = РегистрыСведений.СогласияНаУведомленияОбЭЛН.НачатьЗаписьНабора(
			СтрокаТаблицы.Страхователь,
			СтрокаТаблицы.ФизическоеЛицо);
		Если Набор = Неопределено Тогда
			Продолжить; // Транзакция уже отменена.
		КонецЕсли;
		Если Набор.Количество() > 0 Тогда
			РегистрыСведений.СогласияНаУведомленияОбЭЛН.ОтменитьЗаписьНабора(Набор);
			Продолжить;
		КонецЕсли;
		Запись = Набор.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, СтрокаТаблицы);
		РегистрыСведений.СогласияНаУведомленияОбЭЛН.ЗавершитьЗаписьНабора(Набор);
		
		// Удаление записи текущего регистра.
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Организация.Установить(СтрокаТаблицы.Организация);
		НаборЗаписей.Отбор.Сотрудник.Установить(СтрокаТаблицы.Сотрудник);
		НаборЗаписей.Записать(Истина);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли