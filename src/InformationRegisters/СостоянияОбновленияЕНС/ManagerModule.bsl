#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

Функция ПолучитьАктуальноеСостояние(Организация) Экспорт
	
	МенеджерЗаписи = РегистрыСведений.СостоянияОбновленияЕНС.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Организация = Организация;
	МенеджерЗаписи.Прочитать();
	
	ОписаниеАктуальногоСостояния = ДанныеИзJSON(МенеджерЗаписи.Состояние);
	
	Возврат ОписаниеАктуальногоСостояния;
	
КонецФункции

Процедура УстановитьАктуальноеСостояние(Организация, Состояние) Экспорт
	
	МенеджерЗаписи = РегистрыСведений.СостоянияОбновленияЕНС.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Организация = Организация;
	МенеджерЗаписи.Состояние   = ЗначениеВJSON(Состояние);
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

Функция ОписаниеАктуальногоСостояния() Экспорт
	
	Описание = Новый Структура;
	Описание.Вставить("КодОшибки",            "");
	Описание.Вставить("РасшифровкаСостояния", "");
	Описание.Вставить("ОтветСервиса",         "");
	
	Состояние = Новый Структура;
	Состояние.Вставить("Дата",     Дата(1, 1, 1));
	Состояние.Вставить("Статус",   "");
	Состояние.Вставить("Описание", Описание);
	
	Возврат Состояние;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДанныеИзJSON(Строка)
	
	Если Не ЗначениеЗаполнено(Строка) Тогда
		Возврат ОписаниеАктуальногоСостояния();
	КонецЕсли;
	
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(Строка);
	Возврат ПрочитатьJSON(ЧтениеJSON);
	
КонецФункции

Функция ЗначениеВJSON(Данные)
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, Данные);
	Возврат ЗаписьJSON.Закрыть();
	
КонецФункции

#КонецОбласти

#КонецЕсли
