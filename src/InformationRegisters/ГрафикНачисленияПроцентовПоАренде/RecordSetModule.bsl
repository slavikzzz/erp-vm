#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ПодготовитьДанныеДляФормированияЗаданийПередЗаписью();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПодготовитьДанныеДляФормированияЗаданийПриЗаписи();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПодготовитьДанныеДляФормированияЗаданийПередЗаписью()
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаПередЗаписью.ДатаИзменения КАК ДатаИзменения,
	|	ТаблицаПередЗаписью.Договор КАК Договор,
	|	ТаблицаПередЗаписью.Дата КАК Дата,
	|	ТаблицаПередЗаписью.Проценты КАК Проценты
	|ПОМЕСТИТЬ ТаблицаПередЗаписью
	|ИЗ
	|	РегистрСведений.ГрафикНачисленияПроцентовПоАренде КАК ТаблицаПередЗаписью
	|ГДЕ
	|	ТаблицаПередЗаписью.Регистратор = &Регистратор";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПодготовитьДанныеДляФормированияЗаданийПриЗаписи()
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Регистратор КАК Документ,
	|	ТаблицаПередЗаписью.Договор.Организация КАК Организация,
	|	ТаблицаПередЗаписью.ДатаИзменения КАК Период
	|ПОМЕСТИТЬ ГрафикНачисленияПроцентовПоАрендеИзменение
	|ИЗ
	|	ТаблицаПередЗаписью КАК ТаблицаПередЗаписью
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикНачисленияПроцентовПоАренде КАК ТаблицаПриЗаписи
	|		ПО ТаблицаПриЗаписи.Договор = ТаблицаПередЗаписью.Договор
	|			И ТаблицаПриЗаписи.Дата = ТаблицаПередЗаписью.Дата
	|			И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|ГДЕ
	|	(ТаблицаПриЗаписи.Договор ЕСТЬ NULL
	|		ИЛИ ТаблицаПриЗаписи.Проценты <> ТаблицаПередЗаписью.Проценты
	|		ИЛИ НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.ДатаИзменения, ДЕНЬ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.ДатаИзменения, ДЕНЬ))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Регистратор,
	|	ТаблицаПриЗаписи.Договор.Организация,
	|	ТаблицаПриЗаписи.ДатаИзменения
	|ИЗ
	|	РегистрСведений.ГрафикНачисленияПроцентовПоАренде КАК ТаблицаПриЗаписи
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаПередЗаписью КАК ТаблицаПередЗаписью
	|		ПО ТаблицаПриЗаписи.Договор = ТаблицаПередЗаписью.Договор
	|			И ТаблицаПриЗаписи.Дата = ТаблицаПередЗаписью.Дата
	|ГДЕ
	|	(ТаблицаПередЗаписью.Договор ЕСТЬ NULL
	|		ИЛИ ТаблицаПриЗаписи.Проценты <> ТаблицаПередЗаписью.Проценты
	|		ИЛИ НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.ДатаИзменения, ДЕНЬ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.ДатаИзменения, ДЕНЬ))
	|	И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаПередЗаписью";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
		"ГрафикНачисленияПроцентовПоАрендеИзменение", Выборка.Следующий() И Выборка.Количество > 0);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли