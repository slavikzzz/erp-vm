#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Увеличивает номер задания в константе.
// 
// Возвращаемое значение:
//  Число - Предыдущий номер задания.
//
Функция УвеличитьНомерЗадания() Экспорт
	
	Возврат ЗакрытиеМесяцаСервер.УвеличитьНомерЗадания(Метаданные.Константы.НомерЗаданияКФормированиюДвиженийПоВНА.Имя);
	
КонецФункции

// Считывает записи регистра во временную таблицу за указанный период по отборам.
//
// Параметры:
//	НачалоПериода - Дата - Начало периода выборки данных.
//	ОкончаниеПериода - Дата - Конец периода выборки данных.
//	НомерЗаданияДоРасчета - Число -
//	Организации - Массив из СправочникСсылка.Организации - Фильтр по организации.
//
// Возвращаемое значение:
//	Структура:
//		* СписокОрганизаций - Массив из СправочникСсылка.Организации - Организации, для которых есть задания.
//		* НачалоПериода - Дата - Начало периода заданий.
//		* МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Содержит в себе временную таблицу ЗаданияКРасчету.
//
Функция ЗаданияКРасчету(НачалоПериода, ОкончаниеПериода, НомерЗаданияДоРасчета, Организации) Экспорт

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Задания.Месяц               КАК Месяц,
	|	Задания.ОбъектУчета         КАК ОбъектУчета,
	|	Задания.Организация         КАК Организация,
	|	Задания.НомерЗадания        КАК НомерЗадания,
	|	Задания.Документ            КАК Документ
	|
	|ПОМЕСТИТЬ ЗаданияКРасчету
	|
	|ИЗ
	|	РегистрСведений.ЗаданияКФормированиюДвиженийПоВНА КАК Задания
	|
	|ГДЕ
	|	(Задания.Месяц >= &НачалоПериода 
	|		ИЛИ &НачалоПериода = ДАТАВРЕМЯ(1,1,1))
	|	И (Задания.Месяц <= &ОкончаниеПериода
	|		ИЛИ &ОкончаниеПериода = ДАТАВРЕМЯ(1,1,1))
	|
	|	И Задания.НомерЗадания <= &НомерЗадания
	|	И (Задания.Организация В (&Организации)
	|			ИЛИ &ПоВсемОрганизациям)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(МИНИМУМ(ЗаданияКРасчету.Месяц), ДАТАВРЕМЯ(1,1,1)) КАК НачалоПериода
	|ИЗ
	|	ЗаданияКРасчету КАК ЗаданияКРасчету
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаданияКРасчету.Организация
	|ИЗ
	|	ЗаданияКРасчету КАК ЗаданияКРасчету";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("ОкончаниеПериода", ОкончаниеПериода);
	Запрос.УстановитьПараметр("НомерЗадания", НомерЗаданияДоРасчета);
	Запрос.УстановитьПараметр("Организации", Организации);
	Запрос.УстановитьПараметр("ПоВсемОрганизациям", НЕ ЗначениеЗаполнено(Организации));
	
	Результат = Запрос.ВыполнитьПакет();
	
	Выборка = Результат[Результат.ВГраница()-1].Выбрать();
	Если Выборка.Следующий() Тогда
		НачалоПериодаЗаданий = Выборка.НачалоПериода;
	Иначе
		НачалоПериодаЗаданий = '000101010000';
	КонецЕсли;
	
	ЗаданияКРасчету = Новый Структура;
	ЗаданияКРасчету.Вставить("СписокОрганизаций", Результат[Результат.ВГраница()].Выгрузить().ВыгрузитьКолонку("Организация"));
	ЗаданияКРасчету.Вставить("НачалоПериода", НачалоПериодаЗаданий);
	ЗаданияКРасчету.Вставить("МенеджерВременныхТаблиц", Запрос.МенеджерВременныхТаблиц);
	
	Возврат ЗаданияКРасчету;
	
КонецФункции

// Метод создает записи регистра с параметрами, полученными запросом.
//
// Параметры:
//  Выборка		 - ВыборкаИзРезультатаЗапроса - выборка, содержащая данные для формирования записей.
//  НомерЗадания - Число - номер задания; если не задано, то будет установлено значение из соответствующей константы.
//
Процедура СоздатьЗаписиРегистраПоДаннымВыборки(Выборка, НомерЗадания = Неопределено) Экспорт
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		// В РИБ данный регистр обрабатывается только в главном узле.
		Возврат;
	КонецЕсли;
	
	Если Выборка.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Попытка
		
		СтруктураПолей = Новый Структура("Месяц, Организация, ОбъектУчета, Документ");
		
		Если НомерЗадания = Неопределено Тогда
			НомерЗадания = ТекущийНомерЗадания();
		КонецЕсли;
		
		Пока Выборка.Следующий() Цикл
			
			Если НЕ ЗначениеЗаполнено(Выборка.ОбъектУчета) Тогда
				Продолжить;
			КонецЕсли;	
					
			ЗаполнитьЗначенияСвойств(СтруктураПолей, Выборка);
			
			СоздатьЗаписьРегистра(
				СтруктураПолей.Месяц, 
				СтруктураПолей.Документ, 
				СтруктураПолей.Организация, 
				СтруктураПолей.ОбъектУчета, 
				НомерЗадания);
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ТекстОшибки    = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ТекстСообщения = СтрШаблон(
			НСтр("ru = 'Не удалось сформировать задание к погашению стоимости ТМЦ за %1 в организации %2 по причине: %3';
				|en = 'Cannot generate a job to reimburse value of inventory for %1 in company %2. Reason: %3'"),
			Выборка.Месяц,
			Выборка.Организация,
			ТекстОшибки);
			
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Закрытие месяца';
				|en = 'Month-end closing'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()), 
			УровеньЖурналаРегистрации.Ошибка,,, 
			ТекстСообщения);
			
		ВызватьИсключение ТекстОшибки;
			
	КонецПопытки;
	
КонецПроцедуры

// Создает записи регистра на основании таблицы ВтЗаданияКФормированиюДвиженийПоВНА.
//
// Параметры:
//  МенеджерВременныхТаблиц	- МенеджерВременныхТаблиц - выборка, содержащая данные для формирования записей.
//  НомерЗадания - Число - номер задания; если не задано, то будет установлено значение из соответствующей константы.
//
Процедура СоздатьЗаписиРегистраПоВременнойТаблице(МенеджерВременныхТаблиц, НомерЗадания = Неопределено) Экспорт
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		// В РИБ данный регистр обрабатывается только в главном узле.
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Задания.Документ КАК Документ
	|ИЗ
	|	ВтЗаданияКФормированиюДвиженийПоВНА КАК Задания";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных();
		ПолеБлокировки = Блокировка.Добавить("РегистрСведений.ЗаданияКФормированиюДвиженийПоВНА");
		ПолеБлокировки.ИсточникДанных = РезультатЗапроса;
		ПолеБлокировки.ИспользоватьИзИсточникаДанных("Документ", "Документ");
		Блокировка.Заблокировать();
		
		Если НомерЗадания = Неопределено Тогда
			НомерЗадания = ТекущийНомерЗадания();
		КонецЕсли;
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Задания.Организация КАК Организация,
		|	Задания.Документ КАК Документ,
		|	Задания.ОбъектУчета КАК ОбъектУчета,
		|	НАЧАЛОПЕРИОДА(Задания.Месяц, МЕСЯЦ) КАК Месяц
		|
		|ИЗ
		|	ВтЗаданияКФормированиюДвиженийПоВНА КАК Задания
		|
		|ГДЕ
		|	НЕ ИСТИНА В
		|		(ВЫБРАТЬ ПЕРВЫЕ 1
		|			ИСТИНА
		|		ИЗ
		|			РегистрСведений.ЗаданияКФормированиюДвиженийПоВНА КАК ДанныеРегистра
		|		ГДЕ
		|			ДанныеРегистра.Организация = Задания.Организация
		|			И ДанныеРегистра.Документ = Задания.Документ
		|			И ДанныеРегистра.ОбъектУчета = Задания.ОбъектУчета
		|			И ДанныеРегистра.Месяц = Задания.Месяц
		|			И ДанныеРегистра.НомерЗадания = &НомерЗадания)
		|";
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("НомерЗадания", НомерЗадания);
		РезультатЗапросаЗаданий = Запрос.Выполнить();
		
		НаборЗаписей = СоздатьНаборЗаписей();
		
		Выборка = РезультатЗапросаЗаданий.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Если НЕ ЗначениеЗаполнено(Выборка.ОбъектУчета) Тогда
				Продолжить;
			КонецЕсли;	
			
			ЗаписьРегистра = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(ЗаписьРегистра, Выборка);
			ЗаписьРегистра.Месяц = НачалоМесяца(Выборка.Месяц);
			ЗаписьРегистра.НомерЗадания = НомерЗадания;
			
		КонецЦикла;
			
		Если НаборЗаписей.Модифицированность() Тогда
			НаборЗаписей.Записать(Ложь);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ТекстСообщения = ОбработкаОшибок.ПредставлениеОшибкиДляПользователя(ИнформацияОбОшибке());
			
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Закрытие месяца';
				|en = 'Month-end closing'", ОбщегоНазначения.КодОсновногоЯзыка()), 
			УровеньЖурналаРегистрации.Ошибка,,, 
			ТекстСообщения);
			
		ВызватьИсключение ТекстСообщения;
			
	КонецПопытки;
	
КонецПроцедуры

// Фиксирует удачное выполнение операции.
//
// Параметры:
//  Период - Дата -
//  СписокОрганизаций - Массив - 
//  НомерДоРасчета - Число - 
//  МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - 
//  ИмяТаблицыОбъектов - Строка -
//
Процедура ЗафиксироватьРасчет(Период, СписокОрганизаций, НомерДоРасчета, МенеджерВременныхТаблиц, ИмяТаблицыОбъектов) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);

	СформироватьЗаданияНаСледующийПериод(
		КонецМесяца(Период) + 1, СписокОрганизаций, МенеджерВременныхТаблиц, ИмяТаблицыОбъектов);

	УдалитьЗадания(Период, СписокОрганизаций, НомерДоРасчета, МенеджерВременныхТаблиц, ИмяТаблицыОбъектов);
	
КонецПроцедуры

// Возвращает период с которого для данной операции закрытия месяца данные становятся неактуальными.
//
// Параметры:
//  СписокОрганизаций	 - Массив	 - Массив организаций, для которых контролируется актуальность.
// 
// Возвращаемое значение:
//  Дата - Начало месяца на который неактуально закрытие месяца.
//
Функция НачалоРасчета(СписокОрганизаций) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(МИНИМУМ(Задания.Месяц), ДАТАВРЕМЯ(1, 1, 1)) КАК НачалоРасчета
	|ИЗ
	|	РегистрСведений.ЗаданияКФормированиюДвиженийПоВНА КАК Задания
	|ГДЕ
	|	Задания.Организация В (&СписокОрганизаций)";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("СписокОрганизаций", СписокОрганизаций);
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.НачалоРасчета;
	
КонецФункции

// См. ДатыЗапретаИзмененияПереопределяемый.ЗаполнитьИсточникиДанныхДляПроверкиЗапретаИзменения
Процедура ОписаниеРегистровДляКонтроляДатЗапретаИзменения(ИсточникиДанных) Экспорт
	
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, Метаданные.РегистрыНакопления.СтоимостьОС.ПолноеИмя(),
		"Период", "ФинансовыйКонтур", "Организация");
		
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, Метаданные.РегистрыНакопления.АмортизацияОС.ПолноеИмя(),
		"Период", "ФинансовыйКонтур", "Организация");
		
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, Метаданные.РегистрыНакопления.СтоимостьНМА.ПолноеИмя(),
		"Период", "ФинансовыйКонтур", "Организация");
		
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, Метаданные.РегистрыНакопления.АмортизацияНМА.ПолноеИмя(),
		"Период", "ФинансовыйКонтур", "Организация");
	
КонецПроцедуры

// Возвращает перечень объектов метаданных, на основании данных которых формируются записи в регистре.
//
//Возвращаемое значение:
// Массив из ОбъектМетаданных
Функция ВходящиеДанныеМеханизма() Экспорт
	
	ВходящиеДанные = Новый Массив;
	ВходящиеДанные.Добавить(Метаданные.РегистрыНакопления.АмортизацияНМА);
	ВходящиеДанные.Добавить(Метаданные.РегистрыНакопления.АмортизацияОС);
	ВходящиеДанные.Добавить(Метаданные.РегистрыНакопления.ИнвестицииВАренду);
	ВходящиеДанные.Добавить(Метаданные.РегистрыНакопления.СтоимостьНМА);
	ВходящиеДанные.Добавить(Метаданные.РегистрыНакопления.СтоимостьОС);
	
	ВходящиеДанные.Добавить(Метаданные.РегистрыСведений.АрендованныеОС);
	ВходящиеДанные.Добавить(Метаданные.РегистрыСведений.МестонахождениеОС);
	ВходящиеДанные.Добавить(Метаданные.РегистрыСведений.МестоУчетаНМА);
	ВходящиеДанные.Добавить(Метаданные.РегистрыСведений.ПараметрыАмортизацииНМАБУ);
	ВходящиеДанные.Добавить(Метаданные.РегистрыСведений.ПараметрыАмортизацииНМАУУ);
	ВходящиеДанные.Добавить(Метаданные.РегистрыСведений.ПараметрыАмортизацииОСБУ);
	ВходящиеДанные.Добавить(Метаданные.РегистрыСведений.ПараметрыАмортизацииОСУУ);
	ВходящиеДанные.Добавить(Метаданные.РегистрыСведений.ПервоначальныеСведенияНМА);
	ВходящиеДанные.Добавить(Метаданные.РегистрыСведений.ПереданныеВАрендуОС);
	ВходящиеДанные.Добавить(Метаданные.РегистрыСведений.ПорядокУчетаНМА);
	ВходящиеДанные.Добавить(Метаданные.РегистрыСведений.ПорядокУчетаНМАБУ);
	ВходящиеДанные.Добавить(Метаданные.РегистрыСведений.ПорядокУчетаНМАУУ);
	ВходящиеДанные.Добавить(Метаданные.РегистрыСведений.ПорядокУчетаОС);
	ВходящиеДанные.Добавить(Метаданные.РегистрыСведений.ПорядокУчетаОСБУ);
	ВходящиеДанные.Добавить(Метаданные.РегистрыСведений.ПорядокУчетаОСУУ);
	
	ВходящиеДанные.Добавить(Метаданные.Документы.РеализацияУслугПрочихАктивов);
	
	Возврат ВходящиеДанные;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекущийНомерЗадания()
	
	УстановитьПривилегированныйРежим(Истина);
	ТекущийНомерЗадания = Константы.НомерЗаданияКФормированиюДвиженийПоВНА.Получить();
	УстановитьПривилегированныйРежим(Ложь);
	Возврат ТекущийНомерЗадания;
	
КонецФункции

Процедура УдалитьЗадания(Период, СписокОрганизаций, НомерДоРасчета, МенеджерВременныхТаблиц, ИмяТаблицыОбъектов)
	
	СписокЗапросов = Новый Массив;
	
	Если МенеджерВременныхТаблиц.Таблицы.Найти("ОбработанныеДанные") = Неопределено Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	НЕОПРЕДЕЛЕНО КАК Ссылка,
		|	НЕОПРЕДЕЛЕНО КАК ОбъектУчета,
		|	ЛОЖЬ КАК ЕстьОшибки
		|ПОМЕСТИТЬ ОбработанныеДанные
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|";
		
		СписокЗапросов.Добавить(ТекстЗапроса);
		
	КонецЕсли;

	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Задания.Месяц КАК Месяц,
	|	Задания.Организация КАК Организация,
	|	Задания.ОбъектУчета КАК ОбъектУчета,
	|	Задания.НомерЗадания КАК НомерЗадания
	|
	|ИЗ
	|	РегистрСведений.ЗаданияКФормированиюДвиженийПоВНА КАК Задания
	|
	|ГДЕ
	|	Задания.Месяц <= &ОкончаниеПериода
	|	И Задания.НомерЗадания <= &НомерЗадания
	|	И Задания.Организация В (&СписокОрганизаций)
	|
	|	И Задания.ОбъектУчета В
	|		(ВЫБРАТЬ
	|			СписокОбъектов.ОбъектУчета
	|		ИЗ
	|			СписокОбъектов КАК СписокОбъектов)
	|
	|	И НЕ ИСТИНА В 
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			ОбработанныеДанные КАК ОбработанныеДанные
	|		ГДЕ
	|			ОбработанныеДанные.ОбъектУчета = Задания.ОбъектУчета
	|			И ОбработанныеДанные.ЕстьОшибки)
	|";
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = СтрСоединить(СписокЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СписокОбъектов", ИмяТаблицыОбъектов);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ОкончаниеПериода", КонецМесяца(Период));
	Запрос.УстановитьПараметр("СписокОрганизаций", СписокОрганизаций);
	Запрос.УстановитьПараметр("НомерЗадания", НомерДоРасчета);
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Набор = СоздатьНаборЗаписей();
		Набор.Отбор.Месяц.Установить(Выборка.Месяц);
		Набор.Отбор.ОбъектУчета.Установить(Выборка.ОбъектУчета);
		Набор.Отбор.Организация.Установить(Выборка.Организация);
		Набор.Отбор.НомерЗадания.Установить(Выборка.НомерЗадания);
		Набор.Записать(); 
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СформироватьЗаданияНаСледующийПериод(НачалоСледующегоМесяца, СписокОрганизаций, МенеджерВременныхТаблиц, ИмяТаблицыОбъектов)
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(МИНИМУМ(ДанныеДокументов.Дата), ДАТАВРЕМЯ(1, 1, 1)) КАК Месяц,
	|	ДанныеДокументов.Организация КАК Организация,
	|	ДанныеДокументов.ОбъектУчета КАК ОбъектУчета,
	|	&НомерЗадания КАК НомерЗадания
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДокументыПоОС.Дата КАК Дата,
	|		ДокументыПоОС.Организация КАК Организация,
	|		ДокументыПоОС.ОсновноеСредство КАК ОбъектУчета
	|	ИЗ
	|		РегистрСведений.ДокументыПоОС КАК ДокументыПоОС
	|	ГДЕ
	|		ДокументыПоОС.Дата >= &НачалоСледующегоМесяца
	|		И ДокументыПоОС.Проведен
	|		И ДокументыПоОС.ТипСсылки В(&ИдентификаторыДокументов)
	|		И ДокументыПоОС.Организация В (&СписокОрганизаций)
	|		И ДокументыПоОС.ОсновноеСредство В
	|				(ВЫБРАТЬ
	|					СписокОбъектов.ОбъектУчета
	|				ИЗ
	|					СписокОбъектов КАК СписокОбъектов)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ДокументыПоНМА.Дата КАК Дата,
	|		ДокументыПоНМА.Организация КАК Организация,
	|		ДокументыПоНМА.НематериальныйАктив КАК ОбъектУчета
	|	ИЗ
	|		РегистрСведений.ДокументыПоНМА КАК ДокументыПоНМА
	|	ГДЕ
	|		ДокументыПоНМА.Дата >= &НачалоСледующегоМесяца
	|		И ДокументыПоНМА.Проведен
	|		И ДокументыПоНМА.ТипСсылки В(&ИдентификаторыДокументов)
	|		И ДокументыПоНМА.Организация В (&СписокОрганизаций)
	|		И ДокументыПоНМА.НематериальныйАктив В
	|				(ВЫБРАТЬ
	|					СписокОбъектов.ОбъектУчета
	|				ИЗ
	|					СписокОбъектов КАК СписокОбъектов)
	|
	|	) КАК ДанныеДокументов
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокументов.Организация,
	|	ДанныеДокументов.ОбъектУчета
	|
	|ИМЕЮЩИЕ
	|	ЕСТЬNULL(МИНИМУМ(ДанныеДокументов.Дата), ДАТАВРЕМЯ(1, 1, 1)) <> ДАТАВРЕМЯ(1, 1, 1)
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СписокОбъектов", ИмяТаблицыОбъектов);

	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("НомерЗадания", ТекущийНомерЗадания());
	Запрос.УстановитьПараметр("НачалоСледующегоМесяца", НачалоСледующегоМесяца);
	Запрос.УстановитьПараметр("СписокОрганизаций", СписокОрганизаций);
	
	Запрос.УстановитьПараметр(
		"ИдентификаторыДокументов", 
		ВнеоборотныеАктивыПовтИсп.ИдентификаторыДокументовПоКоторымФормируютсяОтложенныеДвижения());
	
	Запрос.УстановитьПараметр(
		"ХозяйственныеОперацииПоКоторымРассчитываетсяСтоимость", 
		РасчетСтоимостиВНА.ХозяйственныеОперацииПоКоторымРассчитываетсяСтоимость());
	 
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяЗапись = РегистрыСведений.ЗаданияКФормированиюДвиженийПоВНА.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
		НоваяЗапись.Записать(Истина);
	КонецЦикла;
	
КонецПроцедуры

Процедура СоздатьЗаписьРегистра(ПериодЗадания, ДокументЗадания, Организация, ОбъектУчета, НомерЗадания)
				
	УстановитьПривилегированныйРежим(Истина);
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		// В РИБ данный регистр обрабатывается только в главном узле.
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	Попытка
	
		НаборЗаписей = РегистрыСведений.ЗаданияКФормированиюДвиженийПоВНА.СоздатьМенеджерЗаписи();
		НаборЗаписей.Месяц        = НачалоМесяца(ПериодЗадания);
		НаборЗаписей.ОбъектУчета  = ОбъектУчета;
		НаборЗаписей.Организация  = Организация;
		НаборЗаписей.Документ     = ДокументЗадания;
		НаборЗаписей.НомерЗадания = НомерЗадания;
		НаборЗаписей.Записать(Истина);
		
		ЗафиксироватьТранзакцию();
	
	Исключение
		
		ОтменитьТранзакцию();
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ВызватьИсключение ТекстОшибки;
	
	КонецПопытки; 
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
