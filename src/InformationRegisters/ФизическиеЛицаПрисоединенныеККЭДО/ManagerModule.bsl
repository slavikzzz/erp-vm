#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(ФизическоеЛицо)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

// Вычисляет и устанавливает факт подключения сотрудника к КЭДО. Все согласия должны быть подписаны со статусом согласие.
//	Параметры:
//		ФизическиеЛица - Массив Из СправочникСсылка.ФизическиеЛица - сотрудники для которого требуется установить факт подключения к КЭДО.
Процедура УстановитьПодключенККЭДОДляФизическогоЛица(ФизическиеЛица) Экспорт
	
	Запрос = Новый Запрос();
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	СУММА(ВЫБОР
	               |			КОГДА СостояниеСогласияНаПрисоединениеККЭДО.Состояние <> ЗНАЧЕНИЕ(Перечисление.СостоянияСогласийНаПрисоединениеККЭДО.Согласие)
	               |					ИЛИ НЕ СостояниеСогласияНаПрисоединениеККЭДО.Подтвержден
	               |				ТОГДА 1
	               |			ИНАЧЕ 0
	               |		КОНЕЦ) КАК КоличествоНеподтвержденныхСогласий,
	               |	СогласиеНаПрисоединениеККЭДО.ФизическоеЛицо КАК ФизическоеЛицо,
	               |	СУММА(ВЫБОР
	               |			КОГДА СостояниеСогласияНаПрисоединениеККЭДО.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияСогласийНаПрисоединениеККЭДО.Согласие)
	               |					И СостояниеСогласияНаПрисоединениеККЭДО.Подтвержден
	               |					И СостояниеСогласияНаПрисоединениеККЭДО.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	               |				ТОГДА 1
	               |			ИНАЧЕ 0
	               |		КОНЕЦ) КАК КоличествоПодтвержденныхСогласий
	               |ПОМЕСТИТЬ ВТСогласия
	               |ИЗ
	               |	Документ.СогласиеНаПрисоединениеККЭДО КАК СогласиеНаПрисоединениеККЭДО
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостояниеСогласияНаПрисоединениеККЭДО КАК СостояниеСогласияНаПрисоединениеККЭДО
	               |		ПО (СостояниеСогласияНаПрисоединениеККЭДО.Ссылка = СогласиеНаПрисоединениеККЭДО.Ссылка)
	               |ГДЕ
	               |	СогласиеНаПрисоединениеККЭДО.ПометкаУдаления = ЛОЖЬ
	               |	И СогласиеНаПрисоединениеККЭДО.ФизическоеЛицо В(&ФизическиеЛица)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СогласиеНаПрисоединениеККЭДО.ФизическоеЛицо
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ФизическиеЛица.Ссылка КАК ФизическоеЛицо,
	               |	ВТСогласия.КоличествоНеподтвержденныхСогласий КАК КоличествоНеподтвержденныхСогласий,
	               |	ВТСогласия.КоличествоПодтвержденныхСогласий КАК КоличествоПодтвержденныхСогласий
	               |ИЗ
	               |	Справочник.ФизическиеЛица КАК ФизическиеЛица
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТСогласия КАК ВТСогласия
	               |		ПО (ВТСогласия.ФизическоеЛицо = ФизическиеЛица.Ссылка)
	               |ГДЕ
	               |	ФизическиеЛица.Ссылка В(&ФизическиеЛица)";
	
	Запрос.УстановитьПараметр("ФизическиеЛица", ФизическиеЛица);
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	ФизическиеЛицаДляОбновления = Новый Массив;
	Пока Выборка.Следующий() Цикл
		
		МенеджерЗаписи = РегистрыСведений.ФизическиеЛицаПрисоединенныеККЭДО.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.ФизическоеЛицо = Выборка.ФизическоеЛицо;
		Если Выборка.КоличествоПодтвержденныхСогласий = 0 Тогда
			МенеджерЗаписи.Удалить();
			Продолжить;
		КонецЕсли;
		МенеджерЗаписи.Подключен = (Выборка.КоличествоНеподтвержденныхСогласий = 0);
		МенеджерЗаписи.Записать(Истина);
		
		ФизическиеЛицаДляОбновления.Добавить(Выборка.ФизическоеЛицо);
		
	КонецЦикла; 
	
	ИнтеграцияУправлениеПерсоналомСобытия.ЗарегистрироватьОбновлениеДоступныхФункцийФизическогоЛица(ФизическиеЛицаДляОбновления);
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#Область КадровыеДанныеФизическихЛиц

Функция ДобавитьПолеСведенийОПодключенииККЭДО(ИмяПоля, ТекстыОписанияПолей, ИсточникиДанных) Экспорт
	
	ДобавленоПолеСведений = Ложь;
	Если НеобходимыДанныеОПодключенииККЭДО(ИмяПоля) Тогда
		
		ДобавленоПолеСведений = Истина;
		ИсточникиДанных.Вставить("ФизическиеЛицаПрисоединенныеККЭДО", Истина);
		
		ПутьКДанным = ПутьКДаннымСведенийОПодключенииККЭДО(ИмяПоля);
		ТекстыОписанияПолей.Добавить(ПутьКДанным + " КАК " + ИмяПоля);
		
	КонецЕсли;
	
	Возврат ДобавленоПолеСведений;
	
КонецФункции

Функция НеобходимыДанныеОПодключенииККЭДО(ИмяПоля) Экспорт
	
	ИмяПоля = ВРег(ИмяПоля);
	Возврат ИмяПоля = ВРег("ПодключенККЭДО");
	
КонецФункции

Функция ПутьКДаннымСведенийОПодключенииККЭДО(ИмяПоля)
	
	ИмяПоляВВерхнемРегистре = ВРег(ИмяПоля);
	ПутьКДанным = "";
	
	Если ИмяПоляВВерхнемРегистре = ВРег("ПодключенККЭДО") Тогда
		ПутьКДанным = "	ФизическиеЛицаПрисоединенныеККЭДО.Подключен";
	КонецЕсли;
	
	Возврат ПутьКДанным;
	
КонецФункции

Процедура ДобавитьТекстЗапросаВТСведенияОПодключенииККЭДО(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ИсточникиДанных) Экспорт
	
	Если ИсточникиДанных.Получить("ФизическиеЛицаПрисоединенныеККЭДО") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЧастиЗапроса = Новый Массив;
	ЧастиЗапроса.Добавить(Запрос.Текст);
	ЧастиЗапроса.Добавить(
		"	{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФизическиеЛицаПрисоединенныеККЭДО КАК ФизическиеЛицаПрисоединенныеККЭДО
		|	ПО " + КадровыйУчет.ПутьКПолюФизическоеЛицо("ТаблицаОтборов", ОписательВременнойТаблицыОтборов.ИмяПоляФизическоеЛицо) + " = ФизическиеЛицаПрисоединенныеККЭДО.ФизическоеЛицо}");
	
	Запрос.Текст = СтрСоединить(ЧастиЗапроса, Символы.ПС);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
