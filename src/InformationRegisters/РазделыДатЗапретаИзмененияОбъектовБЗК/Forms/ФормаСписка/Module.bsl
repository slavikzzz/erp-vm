#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	ОбновитьВерсиюДатЗапретаИзменения();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбраннаяСтрока) = Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
		СтандартнаяОбработка = Ложь;
	ИначеЕсли Поле = Элементы.Применение Тогда
		
		СтандартнаяОбработка = Ложь;
		ТекущиеДанные = Элементы.Список.ТекущиеДанные;
		ИзменитьПрименениеЗаписи(ТекущиеДанные.РазделСсылка, ТекущиеДанные.ОбъектМетаданных,
			ТекущиеДанные.ПолеДаты, ТекущиеДанные.ПолеОбъекта);
		
		ОповеститьОбИзмененииНастроек();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередУдалением(Элемент, Отказ)
	
	Для Каждого ВыделеннаяСтрока Из Элементы.Список.ВыделенныеСтроки Цикл
		
		Если ТипЗнч(ВыделеннаяСтрока) = Тип("РегистрСведенийКлючЗаписи.РазделыДатЗапретаИзмененияОбъектовБЗК") Тогда
			
			ДанныеСтроки = Элементы.Список.ДанныеСтроки(ВыделеннаяСтрока);
			Если ДанныеСтроки.Предопределенный Тогда
				ПоказатьПредупреждение(, НСтр("ru = 'Нельзя удалять предопределенную настройку';
												|en = 'Cannot delete predefined settings'"));
				Отказ = Истина;
				Возврат;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИзменитьПрименение(Команда)
	
	Если ИзменитьПрименениеНаСервере() Тогда
		ОповеститьОбИзмененииНастроек();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВосстановитьНастройкуПоУмолчанию(Команда)
	
	ВосстановитьНастройкуНаСервере();
	ОповеститьОбИзмененииНастроек();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ИзменитьПрименениеЗаписи(Знач РазделСсылка, Знач ОбъектМетаданных, Знач ПолеДаты, Знач ПолеОбъекта)
	
	РегистрыСведений.РазделыДатЗапретаИзмененияОбъектовБЗК.ИзменитьПрименениеЗаписи(
		РазделСсылка, ОбъектМетаданных, ПолеДаты, ПолеОбъекта);
	
КонецПроцедуры

&НаСервере
Функция ИзменитьПрименениеНаСервере()
	
	ОбновитьСписок = Ложь;
	Для Каждого ВыделеннаяСтрока Из Элементы.Список.ВыделенныеСтроки Цикл
		Если ТипЗнч(ВыделеннаяСтрока) = Тип("РегистрСведенийКлючЗаписи.РазделыДатЗапретаИзмененияОбъектовБЗК") Тогда
			
			ОбновитьСписок = Истина;
			ИзменитьПрименениеЗаписи(ВыделеннаяСтрока.РазделСсылка, ВыделеннаяСтрока.ОбъектМетаданных,
				ВыделеннаяСтрока.ПолеДаты, ВыделеннаяСтрока.ПолеОбъекта);
			
		КонецЕсли;
	КонецЦикла;
	
	Возврат ОбновитьСписок;
	
КонецФункции

&НаСервере
Процедура ВосстановитьНастройкуНаСервере()
	
	РегистрыСведений.РазделыДатЗапретаИзмененияОбъектовБЗК.ВосстановитьНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура ОповеститьОбИзмененииНастроек()
	
	ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.РазделыДатЗапретаИзмененияОбъектовБЗК"));
	ОбновитьВерсиюДатЗапрета = Истина;
	ПодключитьОбработчикОжидания("ОбновитьВерсиюДатЗапретаИзменения", 1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВерсиюДатЗапретаИзменения()
	
	Если ОбновитьВерсиюДатЗапрета Тогда
		ОбновитьВерсиюДатЗапретаИзмененияНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьВерсиюДатЗапретаИзмененияНаСервере()
	
	ДатыЗапретаИзмененияСлужебный.ОбновитьВерсиюДатЗапретаИзменения();
	ОбновитьВерсиюДатЗапрета = Ложь;
	
КонецПроцедуры

#КонецОбласти