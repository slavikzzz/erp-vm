#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если Количество() > 0 Тогда
		
		ТаблицыОбъектов = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(
			ВыгрузитьКолонку("ОбъектМетаданных"), "ПолноеИмя");
		
		Для Каждого Запись Из ЭтотОбъект Цикл
			Если Запись.Применение Тогда
				
				Если Не ЗначениеЗаполнено(Запись.ОбъектМетаданных) Тогда
					ТекстСообщения = НСтр("ru = 'Поле ""Таблица"" не заполнено';
											|en = 'The ""Table"" field is not filled in'");
					ОбщегоНазначения.СообщитьПользователю(
						ТекстСообщения, КлючЗаписи(Запись), "Таблица", , Отказ);
					Продолжить;
				КонецЕсли;
				
				Таблица = ТаблицыОбъектов.Получить(Запись.ОбъектМетаданных);
				Если Метаданные.НайтиПоПолномуИмени(Таблица) = Неопределено Тогда
					ТекстСообщения = СтрШаблон(
						НСтр("ru = 'В составе метаданных отсутствует %1';
							|en = '%1 is missing in the metadata'"),
						Таблица);
					ОбщегоНазначения.СообщитьПользователю(
						ТекстСообщения, КлючЗаписи(Запись), "Таблица", , Отказ);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция КлючЗаписи(Запись)
	
	СтруктураЗаписи = Новый Структура(
		"РазделСсылка,ОбъектМетаданных,ПолеДаты,ПолеОбъекта");
	ЗаполнитьЗначенияСвойств(СтруктураЗаписи, Запись);
	
	Возврат РегистрыСведений.РазделыДатЗапретаИзмененияОбъектовБЗК.СоздатьКлючЗаписи(СтруктураЗаписи);
	
КонецФункции

#КонецОбласти


#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли