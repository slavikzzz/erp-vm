#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Параметры.Свойство("Заголовок", Заголовок);
	Параметры.Свойство("ОрганизацияСсылка", Организация);
	Параметры.Свойство("ФизическоеЛицо", ФизическоеЛицо);
	Параметры.Свойство("РежимВыбора", Элементы.Список.РежимВыбора);

	Если ЗначениеЗаполнено(Организация) И Не ЗначениеЗаполнено(ФизическоеЛицо) Тогда
		ПоказыватьСписокФизическихЛиц = Истина;
	КонецЕсли;
	
	Элементы.СписокФизическиеЛица.Видимость = ПоказыватьСписокФизическихЛиц;
	
	Если ЗначениеЗаполнено(Организация) Тогда

		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, "Организация", Организация, ВидСравненияКомпоновкиДанных.Равно, , Истина);
		Элементы.Организация.Видимость = Ложь;
		Элементы.ФизическоеЛицо.Видимость = Ложь;

		Если ЗначениеЗаполнено(ФизическоеЛицо) Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				Список, "ФизическоеЛицо", ФизическоеЛицо, ВидСравненияКомпоновкиДанных.Равно, , Истина);
		Иначе
			ЗаполнитьСписокФизическихЛиц();
		КонецЕсли;

	КонецЕсли;

	ДатаОтбора = ТекущаяДатаСеанса();
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Список, "ОтборПоДате", ДатаОтбора);
	УстановитьОтбор(ЭтотОбъект);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПоказыватьНеактуальныеОснованияПриИзменении(Элемент)
	
	УстановитьОтбор(ЭтотОбъект, НЕ ПоказыватьНеактуальныеОснования);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокФизическиеЛица

&НаКлиенте
Процедура ФизическиеЛицаПриАктивизацииСтроки(Элемент)
	
	Если Элементы.СписокФизическиеЛица.ТекущаяСтрока <> Неопределено Тогда
		ПодключитьОбработчикОжидания("ОбработкаОжидания", 0.2, Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПослеУдаления(Элемент)
	
	Если ПоказыватьСписокФизическихЛиц Тогда
		ЗаполнитьСписокФизическихЛиц();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокОбработкаЗаписиНового(НовыйОбъект, Источник, СтандартнаяОбработка)
	
	Если ПоказыватьСписокФизическихЛиц Тогда
		ЗаполнитьСписокФизическихЛиц();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтбор(Форма, Использование = Истина)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Форма.Список, "ОснованиеАктуальное", Истина, ВидСравненияКомпоновкиДанных.Равно,, Использование);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОжидания()
	
	Если Элементы.СписокФизическиеЛица.ТекущаяСтрока <> Неопределено Тогда
		
		ТекущееЗначение = Элементы.СписокФизическиеЛица.ТекущиеДанные.Значение;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, "ФизическоеЛицо", ТекущееЗначение, ВидСравненияКомпоновкиДанных.Равно, , Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокФизическихЛиц()
	СписокФизическиеЛица.ЗагрузитьЗначения(
		РегистрыСведений.ОснованияПолномочийОтветственныхЛиц.ФизическиеЛицаОрганизации(Организация));
КонецПроцедуры

#КонецОбласти