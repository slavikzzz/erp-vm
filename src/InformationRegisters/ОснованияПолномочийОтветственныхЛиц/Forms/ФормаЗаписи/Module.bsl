#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Элементы.Организация.Видимость = Не ЗначениеЗаполнено(Запись.Организация);

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	Если Не ТекущийОбъект.Выбран() Тогда

		ТекущийОбъект.НомерПоПорядку = РегистрыСведений.ОснованияПолномочийОтветственныхЛиц.НомерПоПорядкуВРегистре(
			ТекущийОбъект.Организация, ТекущийОбъект.ФизическоеЛицо);

	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаКлиенте
Процедура ФизическоеЛицоПриИзменении(Элемент)
	
	ФизическоеЛицоПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ФизическоеЛицоПриИзмененииНаСервере()

	Запись.Должность = Неопределено;
	
	Если Не ЗначениеЗаполнено(Запись.Организация) Или Не ЗначениеЗаполнено(Запись.ФизическоеЛицо) Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаСотрудников = КадровыйУчет.ОсновныеСотрудникиФизическихЛиц(Запись.ФизическоеЛицо, Истина, Запись.Организация, ТекущаяДатаСеанса());
	
	Если ТаблицаСотрудников.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаКадровыхДанных = КадровыйУчет.КадровыеДанныеСотрудников(Истина, ТаблицаСотрудников.ВыгрузитьКолонку("Сотрудник"), "Должность", ТекущаяДатаСеанса());
	
	Если ТаблицаКадровыхДанных.Количество() > 0 Тогда
		Запись.Должность = ТаблицаКадровыхДанных[0].Должность;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти