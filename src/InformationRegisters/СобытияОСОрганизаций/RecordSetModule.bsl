#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ПодготовитьДанныеДляФормированияЗаданийПередЗаписью();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПодготовитьДанныеДляФормированияЗаданийПриЗаписи();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПодготовитьДанныеДляФормированияЗаданийПередЗаписью()

	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаПередЗаписью.Период КАК Период,
	|	ТаблицаПередЗаписью.Организация КАК Организация,
	|	ТаблицаПередЗаписью.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаПередЗаписью.Событие КАК Событие
	|
	|ПОМЕСТИТЬ СобытияОСОрганизацийПередЗаписью
	|
	|ИЗ
	|	РегистрСведений.СобытияОСОрганизаций КАК ТаблицаПередЗаписью
	|
	|ГДЕ
	|	ТаблицаПередЗаписью.Регистратор = &Регистратор
	|	И ТаблицаПередЗаписью.Активность";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПодготовитьДанныеДляФормированияЗаданийПриЗаписи()
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	МассивТекстовЗапроса = Новый Массив;
	
	ТекстЗапросаИзменения = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаОтличий.Период КАК Период,
	|	ТаблицаОтличий.Организация КАК Организация,
	|	&Регистратор КАК Регистратор
	|
	|ПОМЕСТИТЬ СобытияОСОрганизацийИзменение
	|
	|ИЗ
	|	(ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, ДЕНЬ) КАК Период,
	|
	|		ТаблицаПередЗаписью.Организация КАК Организация,
	|		ТаблицаПередЗаписью.ОсновноеСредство КАК ОсновноеСредство,
	|		ТаблицаПередЗаписью.Событие КАК Событие,
	|
	|		1 КАК Сумма
	|
	|	ИЗ
	|		СобытияОСОрганизацийПередЗаписью КАК ТаблицаПередЗаписью
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, ДЕНЬ) КАК Период,
	|
	|		ТаблицаПриЗаписи.Организация КАК Организация,
	|		ТаблицаПриЗаписи.ОсновноеСредство КАК ОсновноеСредство,
	|		ТаблицаПриЗаписи.Событие КАК Событие,
	|
	|		-1 КАК Сумма
	|	ИЗ
	|		РегистрСведений.СобытияОСОрганизаций КАК ТаблицаПриЗаписи
	|	ГДЕ
	|		ТаблицаПриЗаписи.Регистратор = &Регистратор
	|		И ТаблицаПриЗаписи.Активность
	|
	|	) КАК ТаблицаОтличий
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаОтличий.Период,
	|	ТаблицаОтличий.Организация,
	|	ТаблицаОтличий.ОсновноеСредство,
	|	ТаблицаОтличий.Событие
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаОтличий.Сумма) <> 0
	|";
	МассивТекстовЗапроса.Добавить(ТекстЗапросаИзменения);
	
	МассивТекстовЗапроса.Добавить("УНИЧТОЖИТЬ СобытияОСОрганизацийПередЗаписью");
	
	ТекстЗапроса = СтрСоединить(МассивТекстовЗапроса, ОбщегоНазначенияУТ.РазделительЗапросовВПакете());
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Результат = Запрос.ВыполнитьПакет();
	
	Выборка = Результат[0].Выбрать();
	ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
		"СобытияОСОрганизацийИзменение", Выборка.Следующий() И Выборка.Количество > 0);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
