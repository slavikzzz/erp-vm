#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Отбор.Свойство("ФизическоеЛицо", ФизическоеЛицоСсылка);
	
	СотрудникиФормы.ПрочитатьНаборЗаписей(ЭтаФорма, "СпециальностиФизическихЛиц");
	СпециальностиФизическихЛиц.Сортировать("Период");
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыРасширеннаяПодсистемы.РасчетЗарплатыРасширенная.РазрядыКатегорииДолжностей") Тогда
		
		МодульРазрядыКатегорииДолжностей = ОбщегоНазначения.ОбщийМодуль("РазрядыКатегорииДолжностей");
		Элементы.РазрядКатегория.Заголовок =
			МодульРазрядыКатегорииДолжностей.ИнициализироватьЗаголовокФормыИРеквизитов("РеквизитРазрядКатегорияВКадровыхДокументах");
		
		ТолькоКвалификационныеКатегорииВСписке = Неопределено;
		Параметры.Свойство("ТолькоКвалификационныеКатегорииВСписке", ТолькоКвалификационныеКатегорииВСписке);
		Если ТолькоКвалификационныеКатегорииВСписке<>Неопределено И ТолькоКвалификационныеКатегорииВСписке Тогда 
			ИмяСправочника = "ТарифныеСетки";
			ПараметрВыбораКатегории = Новый ПараметрВыбора("Отбор.ТарифнаяСетка", Справочники[ИмяСправочника].ПустаяСсылка());
			ПараметрыВыбораКатегории = Новый Массив;
			ПараметрыВыбораКатегории.Добавить(ПараметрВыбораКатегории);
			Элементы.РазрядКатегория.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбораКатегории);
		КонецЕсли;
		
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"РазрядКатегория",
			"Видимость",
			Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ПредставлениеСпециальностей = 
		РегистрыСведений.СпециальностиФизическихЛиц.ПредставлениеСпециальностейПоКоллекцииЗаписей(СпециальностиФизическихЛиц);
		
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)

	Оповестить("ИзмененыСпециальностиФизическихЛиц", ПредставлениеСпециальностей, ВладелецФормы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНаборЗаписей

&НаКлиенте
Процедура НаборЗаписейПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.ФизическоеЛицо = ФизическоеЛицоСсылка;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
