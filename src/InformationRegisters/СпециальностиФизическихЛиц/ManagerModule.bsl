#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(ФизическоеЛицо)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ОбработчикиПравилРегистрации

// См. ЗарплатаКадрыРасширенныйСинхронизацияДанных.ШаблонОбработчика
Процедура ПриЗаполненииНастроекОбработчиковПравилРегистрации(Настройки) Экспорт
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыРасширеннаяПодсистемы") Тогда
		МодульЗарплатаКадрыРасширенныйСинхронизацияДанных = ОбщегоНазначения.ОбщийМодуль("ЗарплатаКадрыРасширенныйСинхронизацияДанных");
		МодульЗарплатаКадрыРасширенныйСинхронизацияДанных.ДляНезависимогоРегистра(Настройки, "ФизическоеЛицо", "ОбщиеДанные");
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПредставлениеСпециальностейФизическогоЛица(ФизическоеЛицоСсылка) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ФизическоеЛицоСсылка", ФизическоеЛицоСсылка);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СпециальностиФизическихЛицСрезПоследних.Специальность КАК Специальность,
		|	СпециальностиФизическихЛицСрезПоследних.РазрядКатегория КАК РазрядКатегория
		|ИЗ
		|	РегистрСведений.СпециальностиФизическихЛиц.СрезПоследних(, ФизическоеЛицо = &ФизическоеЛицоСсылка) КАК СпециальностиФизическихЛицСрезПоследних";
		
	Возврат ПредставлениеСпециальностейПоКоллекцииЗаписей(Запрос.Выполнить().Выгрузить());
	
КонецФункции

Функция ПредставлениеСпециальностейПоКоллекцииЗаписей(КоллекцияЗаписей) Экспорт
	
	ПредставлениеСпециальностей = "";
	
	Для Каждого СтрокаСпециальности Из КоллекцияЗаписей Цикл
		ПредставлениеСпециальностей 	= 
			ПредставлениеСпециальностей
			+ ?(ПустаяСтрока(ПредставлениеСпециальностей), "", ", ") 
			+ ?(ЗначениеЗаполнено(СтрокаСпециальности.Специальность), Строка(СтрокаСпециальности.Специальность), "")
			+ ?(ЗначениеЗаполнено(СтрокаСпециальности.РазрядКатегория), " - "+Строка(СтрокаСпециальности.РазрядКатегория), "");
	КонецЦикла;
	
	Если ПустаяСтрока(ПредставлениеСпециальностей) Тогда
		ПредставлениеСпециальностей = НСтр("ru = 'Не обладает специальностями';
											|en = 'Has no specialties'");
	КонецЕсли; 
	
	Возврат ПредставлениеСпециальностей;
	
КонецФункции

Процедура ЗарегистрироватьСпециальностиФизическихЛиц(КоллекцияЗаписей) Экспорт
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Загрузить(КоллекцияЗаписей);
	НаборЗаписей.Записать(Ложь);
	
КонецПроцедуры

Процедура ОчиститьСпециальностиФизическихЛиц(ДокументРегистратор) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СпециальностиФизическихЛиц.Период,
	|	СпециальностиФизическихЛиц.ФизическоеЛицо,
	|	СпециальностиФизическихЛиц.Специальность
	|ИЗ
	|	РегистрСведений.СпециальностиФизическихЛиц КАК СпециальностиФизическихЛиц
	|ГДЕ
	|	СпециальностиФизическихЛиц.ДокументРегистратор = &ДокументРегистратор";
	Запрос.УстановитьПараметр("ДокументРегистратор", ДокументРегистратор);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			НаборЗаписей = СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Период.Установить(Выборка.Период);
			НаборЗаписей.Отбор.ФизическоеЛицо.Установить(Выборка.ФизическоеЛицо);
			НаборЗаписей.Отбор.Специальность.Установить(Выборка.Специальность);
			НаборЗаписей.Записать();
		КонецЦикла;	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли