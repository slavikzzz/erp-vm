#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Запись.ИсходящийДокумент) Тогда
		ТолькоПросмотр = Истина;
	Иначе
		Элементы.ПоясняющаяНадписьВШапке.Видимость = Ложь;
		Элементы.ИсходныйДокументГруппа.Поведение = ПоведениеОбычнойГруппы.Обычное;
	КонецЕсли;
	
	Менеджер = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ИмяФормы);
	Менеджер.ЗаполнитьСписокВыбораСтатусов(Элементы.РегистрацияСтатус, Запись.РегистрацияСтатус);
	
	ОбновитьЭлементыФормы();
	
	// Первичная инициализация.
	Если Элементы.ВыплатаСтатус.СписокВыбора.Количество() = 0 Тогда
		СЭДОФСС.СписокВыбораТиповУведомлений(Элементы.ВыплатаСтатус.СписокВыбора, Запись.ВыплатаСтатус);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ОтветНаЗапросФССДляРасчетаПособия"
		И (Источник = Запись.ИсходящийДокумент Или Не ЗначениеЗаполнено(Источник)) Тогда
		ПодключитьОбработчикОжиданияПрочитать();
		
	ИначеЕсли ИмяСобытия = СЭДОФССКлиент.ИмяСобытияПослеПолученияСообщенийОтФСС()
		Или ИмяСобытия = СЭДОФССКлиент.ИмяСобытияПослеОтправкиПодтвержденияПолучения() Тогда
		ПодключитьОбработчикОжиданияПрочитать();
		
	ИначеЕсли ИмяСобытия = "Запись_РегистрацииОтветовНаЗапросыФССДляРасчетаПособий"
		И (Источник = Запись.ИсходящийДокумент Или Не ЗначениеЗаполнено(Источник)) Тогда
		ПодключитьОбработчикОжиданияПрочитать();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущаяЗапись, ПараметрыЗаписи)
	Менеджер = РегистрыСведений.РегистрацииЗаявленийОВозмещенииВыплатРодителямДетейИнвалидов;
	Менеджер.ЗаполнитьСостояниеРегистрацииИСтатусДокумента(ТекущаяЗапись);
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_РегистрацииОтветовНаЗапросыФССДляРасчетаПособий", Запись.ИсходящийДокумент, Запись.ИсходящийДокумент);
	СЭДОФССКлиент.ОповеститьОНеобходимостиОбновитьТекущиеДела();
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПоясняющаяНадписьВШапкеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура("Ключ", Запись.ИсходящийДокумент);
	ОткрытьФорму("Документ.ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.ФормаОбъекта", ПараметрыФормы);
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДоставкаИдентификаторТекстXML(Команда)
	СЭДОФССКлиент.ПоказатьТекстXML(Запись.ИдентификаторСообщения);
КонецПроцедуры

&НаКлиенте
Процедура РегистрацияИдентификаторТекстXML(Команда)
	СЭДОФССКлиент.ПоказатьТекстXML(Запись.РегистрацияИдентификатор);
КонецПроцедуры

&НаКлиенте
Процедура ВключитьВозможностьРедактирования(Команда)
	ТолькоПросмотр = Ложь;
	ОбновитьЭлементыФормы();
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПодключитьОбработчикОжиданияПрочитать()
	ОтключитьОбработчикОжидания("ОбработчикОжиданияПрочитать");
	ПодключитьОбработчикОжидания("ОбработчикОжиданияПрочитать", 0.2, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОжиданияПрочитать()
	Если Не Модифицированность Тогда
		Прочитать();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыФормы()
	Элементы.ДоставкаИдентификаторТекстXML.Видимость    = ЗначениеЗаполнено(Запись.ИдентификаторСообщения);
	Элементы.РегистрацияИдентификаторТекстXML.Видимость = ЗначениеЗаполнено(Запись.РегистрацияИдентификатор);
	
	ОтправленОператору = (Запись.ОтправленОператору Или ЗначениеЗаполнено(Запись.ДатаОтправкиОператору));
	Элементы.ОтправленОператору.Видимость          = Не ТолькоПросмотр Или ОтправленОператору;
	Элементы.ДатаОтправкиОператору.Видимость       = Не ТолькоПросмотр Или ОтправленОператору;
	Элементы.ДоставкаИдентификаторПакета.Видимость = Не ТолькоПросмотр Или ОтправленОператору
		Или ЗначениеЗаполнено(Запись.ДоставкаИдентификаторПакета);
	
	Элементы.ОшибкиЛогическогоКонтроляГруппа.Видимость = Не ТолькоПросмотр
		Или Запись.ЕстьОшибкиФЛК;
	
	ЕстьОтказВВозмещении = ЗначениеЗаполнено(Запись.ОтказВВозмещении);
	Элементы.ОтказВВозмещении.Видимость = Не ТолькоПросмотр Или ЕстьОтказВВозмещении;
	
	Если РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца Тогда
		Элементы.КоманднаяПанельПодвал.Видимость = Истина;
		КнопкаЗакрыть = Элементы.КнопкаЗакрытьВПодвале;
	Иначе
		Элементы.КоманднаяПанельПодвал.Видимость = Ложь;
		КнопкаЗакрыть = Элементы.КнопкаЗакрытьВШапке;
	КонецЕсли;
	
	Если Запись.ДоставкаУспех
		И Не ЗначениеЗаполнено(Запись.РегистрацияИдентификатор)
		И Не Запись.ЕстьОшибкиФЛК Тогда
		Элементы.ПолучитьНовыеСообщенияСЭДОФСС.КнопкаПоУмолчанию = Истина;
	Иначе
		КнопкаЗакрыть.КнопкаПоУмолчанию = Истина;
	КонецЕсли;
	
	КлючСохраненияПоложенияОкна = ?(ТолькоПросмотр,
		Формат(ОтправленОператору, "БЛ=0; БИ=1")
		+ Формат(Запись.ЕстьОшибкиФЛК, "БЛ=0; БИ=1")
		+ Формат(ЕстьОтказВВозмещении, "БЛ=0; БИ=1"),
		"");
	
КонецПроцедуры

#КонецОбласти
