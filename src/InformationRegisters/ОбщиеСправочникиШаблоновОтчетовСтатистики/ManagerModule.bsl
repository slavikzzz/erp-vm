
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗаполнитьРегистрСправочникамиШаблонов(Параметры = Неопределено) Экспорт
	
	//Итоговая ТЗ которую будет обрабатывать
	ТЗШаблоны = Новый ТаблицаЗначений;
	ТЗШаблоны.Колонки.Добавить("ИсточникОтчета",       Новый ОписаниеТипов("Строка"));
	ТЗШаблоны.Колонки.Добавить("ИмяФормыОтчетаПолное", Новый ОписаниеТипов("Строка"));
	ТЗШаблоны.Колонки.Добавить("ВРегистре",            Новый ОписаниеТипов("Число"));
	ТЗШаблоны.Колонки.Добавить("ВКонфигурации",        Новый ОписаниеТипов("Число"));
	ТЗШаблоны.Колонки.Добавить("Шаблон",               Новый ОписаниеТипов("Строка"));
	
	//Получим все загруженные в регистр шаблоны
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ОбщиеСправочникиШаблоновОтчетовСтатистики.ИсточникОтчета КАК ИсточникОтчета,
	               |	1 КАК ВРегистре
	               |ИЗ
	               |	РегистрСведений.ОбщиеСправочникиШаблоновОтчетовСтатистики КАК ОбщиеСправочникиШаблоновОтчетовСтатистики";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СтрокаТаблицыИсточники = ТЗШаблоны.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицыИсточники, Выборка);
	КонецЦикла;
	
	ДанныеАктуальныхШаблонов = ПолучитьДанныеАктуальныхШаблонов(ТЗШаблоны);
	
	Для Каждого Данные Из ДанныеАктуальныхШаблонов Цикл
		
		НайденнаяСтрока = ТЗШаблоны.Найти(Данные.ИсточникОтчета, "ИсточникОтчета");
		
		Если НайденнаяСтрока = Неопределено Тогда
			СтрокаТаблицыИсточники = ТЗШаблоны.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаблицыИсточники, Данные);
		Иначе
			ЗаполнитьЗначенияСвойств(НайденнаяСтрока, Данные);
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого Стр Из ТЗШаблоны Цикл
		
		НачатьТранзакцию();
		Попытка
			Если Стр.ВРегистре = 0 И Стр.ВКонфигурации = 1 Тогда
				РегламентированнаяОтчетностьФСГС.ЗагрузитьСправочникиШаблонаВРегистр(Стр.ИмяФормыОтчетаПолное, Стр.ИсточникОтчета, Стр.Шаблон, Истина);
			ИначеЕсли Стр.ВРегистре = 1 И Стр.ВКонфигурации = 0 Тогда
				НаборЗаписей = РегистрыСведений.ОбщиеСправочникиШаблоновОтчетовСтатистики.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.ИсточникОтчета.Установить(Стр.ИсточникОтчета);
				НаборЗаписей.Записать();
			КонецЕсли;
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьДанныеАктуальныхШаблонов(ТЗШаблоны)
	
	ТЗАктШаблоны = ТЗШаблоны.СкопироватьКолонки("ИсточникОтчета, ИмяФормыОтчетаПолное, ВКонфигурации, Шаблон");
	
	МассивОтчетов = РегламентированнаяОтчетностьПовтИсп.ПолучитьОтчетыСтатистикиРаботающиеНаСправочникахИзРегистра();
	
	Для Каждого ИмяОтчета Из МассивОтчетов Цикл
		
		Если Метаданные.Отчеты.Найти(ИмяОтчета) <> Неопределено Тогда
			
			ТаблицаФормОтчета = Отчеты[ИмяОтчета].ТаблицаФормОтчета();
			ТаблицаФормОтчета.Сортировать("ДатаНачалоДействия Убыв");
			
			ТекДата    = НачалоДня(ТекущаяДатаСеанса());
			ПустаяДата = РегламентированнаяОтчетностьКлиентСервер.ПустоеЗначениеТипа(Тип("Дата"));
			
			ИмяФормыОтчета = "";
			
			Для Каждого Редакция Из ТаблицаФормОтчета Цикл
				
				Если ТекДата >= Редакция.ДатаНачалоДействия
				   И (Редакция.ДатаКонецДействия = ПустаяДата Или ТекДата <= Редакция.ДатаКонецДействия) Тогда
					ИмяФормыОтчета = Редакция.ФормаОтчета;
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			
			Если ИмяФормыОтчета = "" Тогда
				Продолжить;
			КонецЕсли;
			
			Макеты = Метаданные.Отчеты[ИмяОтчета].Макеты;
			
			ИмяМакетаАтр = СтрЗаменить(ИмяФормыОтчета, "ФормаОтчета", "АтрибВыгрузкиXML");
			
			Для Каждого Макет Из Макеты Цикл
				Если СтрНачинаетсяС(Макет.Имя, ИмяМакетаАтр) Тогда
					
					ИмяФормыОтчетаПолное = "Отчет." + ИмяОтчета + ".Форма." + ИмяФормыОтчета;
					ДопАтрибуты = РегламентированнаяОтчетностьФСГС.СформироватьСтруктуруДопАтрибутов(ИмяФормыОтчетаПолное, Макет.Имя);
					
					Если Не РегламентированнаяОтчетность.УстановленЗапретВыгрузкиВМакетеАтрибутов(ДопАтрибуты) Тогда
						
						ИмяМакетаШаб = СтрЗаменить(Макет.Имя, "АтрибВыгрузкиXML", "ШаблонРосстата");
						Если Макеты.Найти(ИмяМакетаШаб) <> Неопределено Тогда
							
							НовСтр = ТЗАктШаблоны.Добавить();
							НовСтр.ИсточникОтчета = РегламентированнаяОтчетностьФСГС.СформироватьСтрокуИсточникОтчета(ДопАтрибуты);
							НовСтр.ИмяФормыОтчетаПолное = ИмяФормыОтчетаПолное;
							НовСтр.ВКонфигурации = 1;
							НовСтр.Шаблон = ИмяМакетаШаб;
							
						КонецЕсли;
						
					КонецЕсли;
					
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТЗАктШаблоны;
	
КонецФункции

#КонецОбласти

#КонецЕсли