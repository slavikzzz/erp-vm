#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ПодготовитьДанныеДляФормированияЗаданийПередЗаписью();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПодготовитьДанныеДляФормированияЗаданийПриЗаписи();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПодготовитьДанныеДляФормированияЗаданийПередЗаписью()
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаПередЗаписью.Период КАК Период,
	|	ТаблицаПередЗаписью.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаПередЗаписью.Организация КАК Организация,
	|	ТаблицаПередЗаписью.Партнер КАК Партнер,
	|	ТаблицаПередЗаписью.Контрагент КАК Контрагент,
	|	ТаблицаПередЗаписью.Договор КАК Договор,
	|	ТаблицаПередЗаписью.Стоимость КАК Стоимость,
	|	ТаблицаПередЗаписью.Состояние КАК Состояние
	|ПОМЕСТИТЬ АрендованныеОСПередЗаписью
	|ИЗ
	|	РегистрСведений.АрендованныеОС КАК ТаблицаПередЗаписью
	|ГДЕ
	|	ТаблицаПередЗаписью.Регистратор = &Регистратор";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПодготовитьДанныеДляФормированияЗаданийПриЗаписи()
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Регистратор               КАК Документ,
	|	Таблица.Период             КАК Период,
	|	Таблица.ОсновноеСредство   КАК ОсновноеСредство,
	|	Таблица.Организация        КАК Организация,
	|	Таблица.Договор            КАК Договор,
	|	ИСТИНА                     КАК ОтражатьВРеглУчете,
	|	ИСТИНА                     КАК ОтражатьВУпрУчете
	|
	|ПОМЕСТИТЬ АрендованныеОСИзменение
	|
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаПередЗаписью.Период            КАК Период,
	|		ТаблицаПередЗаписью.ОсновноеСредство  КАК ОсновноеСредство,
	|		ТаблицаПередЗаписью.Организация       КАК Организация,
	|		ТаблицаПередЗаписью.Договор           КАК Договор
	|	ИЗ
	|		АрендованныеОСПередЗаписью КАК ТаблицаПередЗаписью
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АрендованныеОС КАК ТаблицаПриЗаписи
	|			ПО ТаблицаПриЗаписи.ОсновноеСредство = ТаблицаПередЗаписью.ОсновноеСредство
	|				И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|
	|		ГДЕ
	|			(ТаблицаПриЗаписи.Период ЕСТЬ NULL
	|				ИЛИ ТаблицаПриЗаписи.Партнер <> ТаблицаПередЗаписью.Партнер
	|				ИЛИ ТаблицаПриЗаписи.Контрагент <> ТаблицаПередЗаписью.Контрагент
	|				ИЛИ ТаблицаПриЗаписи.Договор <> ТаблицаПередЗаписью.Договор
	|				ИЛИ ТаблицаПриЗаписи.Организация <> ТаблицаПередЗаписью.Организация
	|				ИЛИ ТаблицаПриЗаписи.Стоимость <> ТаблицаПередЗаписью.Стоимость
	|				ИЛИ ТаблицаПриЗаписи.Состояние <> ТаблицаПередЗаписью.Состояние)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаПриЗаписи.Период,
	|		ТаблицаПриЗаписи.ОсновноеСредство,
	|		ТаблицаПриЗаписи.Организация,
	|		ТаблицаПриЗаписи.Договор
	|	ИЗ
	|		РегистрСведений.АрендованныеОС КАК ТаблицаПриЗаписи
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ АрендованныеОСПередЗаписью КАК ТаблицаПередЗаписью
	|			ПО ТаблицаПриЗаписи.ОсновноеСредство = ТаблицаПередЗаписью.ОсновноеСредство
	|
	|		ГДЕ
	|			(ТаблицаПриЗаписи.Период ЕСТЬ NULL
	|				ИЛИ ТаблицаПриЗаписи.Партнер <> ТаблицаПередЗаписью.Партнер
	|				ИЛИ ТаблицаПриЗаписи.Контрагент <> ТаблицаПередЗаписью.Контрагент
	|				ИЛИ ТаблицаПриЗаписи.Договор <> ТаблицаПередЗаписью.Договор
	|				ИЛИ ТаблицаПриЗаписи.Организация <> ТаблицаПередЗаписью.Организация
	|				ИЛИ ТаблицаПриЗаписи.Стоимость <> ТаблицаПередЗаписью.Стоимость
	|				ИЛИ ТаблицаПриЗаписи.Состояние <> ТаблицаПередЗаписью.Состояние)
	|			И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|
	|	) КАК Таблица
	|
	|СГРУППИРОВАТЬ ПО
	|	Таблица.Период,
	|	Таблица.ОсновноеСредство,
	|	Таблица.Организация,
	|	Таблица.Договор";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	ИмяРегистра = "АрендованныеОС";
	
	Запрос.Текст = Запрос.Текст + ОбщегоНазначения.РазделительПакетаЗапросов() + "УНИЧТОЖИТЬ " + ИмяРегистра + "ПередЗаписью";
	
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
		"АрендованныеОСИзменение", Выборка.Следующий() И Выборка.Количество > 0);
	
КонецПроцедуры
 
#КонецОбласти

#КонецЕсли
