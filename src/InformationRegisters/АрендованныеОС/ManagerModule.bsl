#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.АрендованныеОС.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.5.17.12";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("367c71ef-594f-479e-9cea-783c2ba3c852");
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.АрендованныеОС.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Порядок = Перечисления.ПорядокОбработчиковОбновления.Обычный;
	Обработчик.Комментарий = НСтр("ru = 'Обновляет регистр ""Арендованные ОС"":
								  |- заполняет новый ресурс ""Номинальная стоимость"".
								  |- заполняет пустой реквизит ""Организация""';
								  |en = 'Updates the ""Leased fixed assets"" register:
								  |- Fills the new ""Par value"" resource.
								  |- Fills the empty ""Company"" attribute'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Документы.ЗаключениеДоговораАренды.ПолноеИмя());

	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыСведений.АрендованныеОС.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.РегистрыСведений.АрендованныеОС.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");

КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяОбъекта = "РегистрСведений.АрендованныеОС";
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЗаключениеДоговораАрендыОС.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ втЗаключениеДоговораАрендыОС
	|ИЗ
	|	Документ.ЗаключениеДоговораАренды.ОС КАК ЗаключениеДоговораАрендыОС
	|ГДЕ
	|	НЕ ЗаключениеДоговораАрендыОС.Ссылка ЕСТЬ NULL
	|	И ЗаключениеДоговораАрендыОС.СправедливаяСтоимость <> 0
	|	И ЗаключениеДоговораАрендыОС.Ссылка.Проведен = ИСТИНА
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаключениеДоговораАрендыОС.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИзменениеУсловийДоговораАрендыОС.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ втИзменениеУсловийДоговораАрендыОС
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды.ОС КАК ИзменениеУсловийДоговораАрендыОС
	|ГДЕ
	|	НЕ ИзменениеУсловийДоговораАрендыОС.Ссылка ЕСТЬ NULL
	|	И ИзменениеУсловийДоговораАрендыОС.СправедливаяСтоимость <> 0
	|	И ИзменениеУсловийДоговораАрендыОС.Ссылка.Проведен = ИСТИНА
	|
	|СГРУППИРОВАТЬ ПО
	|	ИзменениеУсловийДоговораАрендыОС.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПринятиеКУчетуОС2_4.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ втПринятиеКУчетуОС
	|ИЗ
	|	Документ.ПринятиеКУчетуОС2_4 КАК ПринятиеКУчетуОС2_4
	|ГДЕ
	|	НЕ ПринятиеКУчетуОС2_4.Ссылка ЕСТЬ NULL
	|	И ПринятиеКУчетуОС2_4.Проведен = ИСТИНА
	|	И ПринятиеКУчетуОС2_4.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуПредметовАренды)
	|	И ПринятиеКУчетуОС2_4.ДокументОснование В
	|			(ВЫБРАТЬ
	|				втЗаключениеДоговораАрендыОС.Ссылка КАК Ссылка
	|			ИЗ
	|				втЗаключениеДоговораАрендыОС КАК втЗаключениеДоговораАрендыОС)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПринятиеКУчетуОС2_4.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаписиРегистра.Регистратор КАК Регистратор
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВЫРАЗИТЬ(АрендованныеОС.Регистратор КАК Документ.ЗаключениеДоговораАренды) КАК Регистратор
	|	ИЗ
	|		РегистрСведений.АрендованныеОС КАК АрендованныеОС
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаключениеДоговораАрендыОС КАК втЗаключениеДоговораАрендыОС
	|			ПО АрендованныеОС.Регистратор = втЗаключениеДоговораАрендыОС.Ссылка
	|	ГДЕ
	|		АрендованныеОС.НоминальнаяСтоимость = 0
	|		И АрендованныеОС.Регистратор ССЫЛКА Документ.ЗаключениеДоговораАренды
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВЫРАЗИТЬ(АрендованныеОС.Регистратор КАК Документ.ПринятиеКУчетуОС2_4)
	|	ИЗ
	|		РегистрСведений.АрендованныеОС КАК АрендованныеОС
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПринятиеКУчетуОС КАК втПринятиеКУчетуОС
	|			ПО АрендованныеОС.Регистратор = втПринятиеКУчетуОС.Ссылка
	|	ГДЕ
	|		АрендованныеОС.НоминальнаяСтоимость = 0
	|		И АрендованныеОС.Регистратор ССЫЛКА Документ.ПринятиеКУчетуОС2_4
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВЫРАЗИТЬ(АрендованныеОС.Регистратор КАК Документ.ИзменениеУсловийДоговораАренды)
	|	ИЗ
	|		РегистрСведений.АрендованныеОС КАК АрендованныеОС
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ втИзменениеУсловийДоговораАрендыОС КАК втИзменениеУсловийДоговораАрендыОС
	|			ПО АрендованныеОС.Регистратор = втИзменениеУсловийДоговораАрендыОС.Ссылка
	|	ГДЕ
	|		АрендованныеОС.НоминальнаяСтоимость = 0
	|		И АрендованныеОС.Регистратор ССЫЛКА Документ.ИзменениеУсловийДоговораАренды) КАК ЗаписиРегистра
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДанныеРегистра.Регистратор КАК Регистратор
	|	ИЗ
	|		РегистрСведений.АрендованныеОС КАК ДанныеРегистра
	|	ГДЕ
	|		ДанныеРегистра.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|		И НЕ ДанныеРегистра.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|";
	
	Запрос = Новый Запрос(ТекстЗапроса);

	Регистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(Параметры, Регистраторы, ПолноеИмяОбъекта);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
		"РегистрСведений.АрендованныеОС.ОбработатьДанныеДляПереходаНаНовуюВерсию");
		
	ОбъектовОбработано = ВнеоборотныеАктивы.СформироватьДвиженияПриОбновленииИБ("АрендованныеОС", Параметры);
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, ОбъектовОбработано);	
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли