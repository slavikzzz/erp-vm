//++ Устарело_Производство21
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает настройку передачи материалов в производство для подразделения
//
//	Параметры:
//		Подразделение - СправочникСсылка.СтруктураПредприятия - подразделение, для которого необходимо получить настройку передачи материалов
//	Возвращаемое значение:
//		Структура - настройка передачи материалов в производство для подразделения
//			Склад - СправочникСсылка.Склады - склад, с которого передаются материалы в производство.
//
Функция ПолучитьНастройкуПередачиМатериалоВПроизводство(Подразделение) Экспорт
	
	НастройкаПередачиМатериалов = Новый Структура("Склад,ОснованиеДляПолучения");
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	НастройкаПередачиМатериаловВПроизводство.Склад КАК Склад,
	|	НастройкаПередачиМатериаловВПроизводство.ОснованиеДляПолучения КАК ОснованиеДляПолучения
	|ИЗ
	|	РегистрСведений.НастройкаПередачиМатериаловВПроизводство КАК НастройкаПередачиМатериаловВПроизводство
	|ГДЕ
	|	НастройкаПередачиМатериаловВПроизводство.Подразделение = &Подразделение
	|	И НастройкаПередачиМатериаловВПроизводство.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	И НастройкаПередачиМатериаловВПроизводство.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)");
	
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(НастройкаПередачиМатериалов, Выборка);
		
	КонецЕсли;
		
	Возврат НастройкаПередачиМатериалов;
	
КонецФункции

// Сохраняет настройку передачи материалов для подразделения
//
//	Параметры:
//		Подразделение - СправочникСсылка.СтруктураПредприятия - подразделение, для которого необходимо сохранить настройку передачи материалов
//		Склад - ПеречислениеСсылка.Склады - склад, с которого передаются материалы в подразделение.
//
Процедура УстановитьНастройкуПередачиМатериаловПодразделения(Подразделение, Склад) Экспорт
	
	НастройкаПередачиМатериалов = ПолучитьНастройкуПередачиМатериалоВПроизводство(Подразделение);
	
	Если НастройкаПередачиМатериалов.Склад <> Склад Тогда
		
		МенеджерЗаписи = РегистрыСведений.НастройкаПередачиМатериаловВПроизводство.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Подразделение = Подразделение;
		МенеджерЗаписи.Склад = Склад;
		МенеджерЗаписи.ОснованиеДляПолучения = Перечисления.ОснованияДляПолученияМатериаловВПроизводстве.ПоЗаказуНаПроизводство;
		
		МенеджерЗаписи.Записать();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
//-- Устарело_Производство21