//++ Устарело_Производство21
#Область ОписаниеПеременных

&НаКлиенте
Перем КэшированныеЗначения; //используется механизмом обработки изменения реквизитов ТЧ

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Запись, ЭтотОбъект);

	//++ НЕ УТКА
	ПолучениеПоЗаказуНаПроизводство = Перечисления.ОснованияДляПолученияМатериаловВПроизводстве.ПоЗаказуНаПроизводство;
	//-- НЕ УТКА
	
	Если Запись.ИсходныйКлючЗаписи.Пустой() Тогда
		Запись.Склад = ОбщегоНазначенияУТ.ПолучитьПроверитьСкладПоУмолчанию();
		ПриЧтенииСозданииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриЧтенииСозданииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_НастройкаПередачиМатериаловВПроизводство");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НоменклатураПриИзменении(Элемент)
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", Запись.Характеристика);

	СтруктураСтроки = Новый Структура;
	СтруктураСтроки.Вставить("Номенклатура", Запись.Номенклатура);
	СтруктураСтроки.Вставить("Характеристика", Запись.Характеристика);
	СтруктураСтроки.Вставить("ХарактеристикиИспользуются", ХарактеристикиИспользуются);
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(СтруктураСтроки, СтруктураДействий, КэшированныеЗначения);

	ЗаполнитьЗначенияСвойств(Запись, СтруктураСтроки);
	
	ХарактеристикиИспользуются = СтруктураСтроки.ХарактеристикиИспользуются;
	
	Если НЕ ЗначениеЗаполнено(Запись.Номенклатура) Тогда
		Запись.ОснованиеДляПолучения = ПолучениеПоЗаказуНаПроизводство;
	КонецЕсли; 
	
	УправлениеДоступностью(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()

	ХарактеристикиИспользуются = Справочники.Номенклатура.ХарактеристикиИспользуются(Запись.Номенклатура);
	УправлениеДоступностью(ЭтаФорма);
	
КонецПроцедуры
 
&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеДоступностью(Форма)

	Форма.Элементы.Характеристика.Доступность = Форма.ХарактеристикиИспользуются;
	
	Форма.Элементы.ОснованиеДляПолученияПоЗаказуНаПроизводство.ТолькоПросмотр = НЕ ЗначениеЗаполнено(Форма.Запись.Номенклатура);
	Форма.Элементы.ОснованиеДляПолученияПоЗаказуНаВнутреннееПотребление.ТолькоПросмотр = НЕ ЗначениеЗаполнено(Форма.Запись.Номенклатура);

КонецПроцедуры

#КонецОбласти

#КонецОбласти
//-- Устарело_Производство21