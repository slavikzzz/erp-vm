#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Код процедур и функций

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.Номенклатура        КАК Номенклатура,
	|	Таблица.Характеристика      КАК Характеристика,
	|	Таблица.Склад               КАК Склад,
	|	Таблица.Назначение          КАК Назначение,
	|	Таблица.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|	Таблица.КлючНоменклатура    КАК КлючНоменклатура,
	|	Таблица.Спецификация        КАК Спецификация,
	|	Таблица.Этап                КАК Этап,
	|	Таблица.Партия              КАК Партия,
	|	Таблица.Подразделение       КАК Подразделение,
	|	Таблица.ДатаПоступления     КАК ДатаПоступления,
	|	Таблица.ЗаказНаПоступление  КАК ЗаказНаПоступление,
	|	Таблица.Заказано            КАК Заказано,
	|	Таблица.КПроизводству       КАК КПроизводству,
	|	Таблица.Запущено            КАК Запущено,
	|	Таблица.Готово              КАК Готово
	|ПОМЕСТИТЬ ДвиженияЗаказыНаПроизводствоПередЗаписью
	|ИЗ
	|	РегистрСведений.ЗаказыНаПроизводство КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|;";
	Запрос.Выполнить();     

	УстановитьБлокировкуЗаказов(МенеджерВременныхТаблиц);
	
	ДополнительныеСвойства.Вставить("ВременныеТаблицыРасчетаСостоянииПродукцииЗаказа", МенеджерВременныхТаблиц);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.ВременныеТаблицыРасчетаСостоянииПродукцииЗаказа;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаИзменений.Номенклатура         КАК Номенклатура,
	|	ТаблицаИзменений.Характеристика       КАК Характеристика,
	|	ТаблицаИзменений.Склад                КАК Склад,
	|	ТаблицаИзменений.Назначение           КАК Назначение,
	|	ТаблицаИзменений.ЗаказНаПроизводство  КАК ЗаказНаПроизводство,
	|	СУММА(ТаблицаИзменений.Заказано)      КАК ЗаказаноИзменение,
	|	СУММА(ТаблицаИзменений.КПроизводству) КАК КПроизводствуИзменение,
	|	СУММА(ТаблицаИзменений.Запущено)      КАК ЗапущеноИзменение,
	|	СУММА(ТаблицаИзменений.Готово)        КАК ГотовоИзменение	
	|ПОМЕСТИТЬ ДвиженияЗаказыНаПроизводствоИзменения
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Номенклатура        КАК Номенклатура,
	|		Таблица.Характеристика      КАК Характеристика,
	|		Таблица.Склад               КАК Склад,
	|		Таблица.Назначение          КАК Назначение,
	|		Таблица.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|		Таблица.КлючНоменклатура    КАК КлючНоменклатура,
	|		Таблица.Спецификация        КАК Спецификация,
	|		Таблица.Этап                КАК Этап,
	|		Таблица.Партия              КАК Партия,
	|		Таблица.Подразделение       КАК Подразделение,
	|		Таблица.ДатаПоступления     КАК ДатаПоступления,
	|		Таблица.ЗаказНаПоступление  КАК ЗаказНаПоступление,
	|		-Таблица.Заказано           КАК Заказано,
	|		-Таблица.КПроизводству      КАК КПроизводству,
	|		-Таблица.Запущено           КАК Запущено,
	|		-Таблица.Готово             КАК Готово
	|	ИЗ
	|		ДвиженияЗаказыНаПроизводствоПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Номенклатура        КАК Номенклатура,
	|		Таблица.Характеристика      КАК Характеристика,
	|		Таблица.Склад               КАК Склад,
	|		Таблица.Назначение          КАК Назначение,
	|		Таблица.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|		Таблица.КлючНоменклатура    КАК КлючНоменклатура,
	|		Таблица.Спецификация        КАК Спецификация,
	|		Таблица.Этап                КАК Этап,
	|		Таблица.Партия              КАК Партия,
	|		Таблица.Подразделение       КАК Подразделение,
	|		Таблица.ДатаПоступления     КАК ДатаПоступления,
	|		Таблица.ЗаказНаПоступление  КАК ЗаказНаПоступление,
	|		Таблица.Заказано            КАК Заказано,
	|		Таблица.КПроизводству       КАК КПроизводству,
	|		Таблица.Запущено            КАК Запущено,
	|		Таблица.Готово              КАК Готово
	|	ИЗ
	|		РегистрСведений.ЗаказыНаПроизводство КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор) КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.Номенклатура,
	|	ТаблицаИзменений.Характеристика,
	|	ТаблицаИзменений.Склад,
	|	ТаблицаИзменений.Назначение,
	|	ТаблицаИзменений.ЗаказНаПроизводство,
	|	ТаблицаИзменений.КлючНоменклатура,
	|	ТаблицаИзменений.Спецификация,
	|	ТаблицаИзменений.Этап,
	|	ТаблицаИзменений.Партия,
	|	ТаблицаИзменений.Подразделение,
	|	ТаблицаИзменений.ДатаПоступления,
	|	ТаблицаИзменений.ЗаказНаПоступление
	|
	|ИМЕЮЩИЕ
	|	(СУММА(ТаблицаИзменений.Заказано) <> 0
	|		ИЛИ СУММА(ТаблицаИзменений.КПроизводству) <> 0
	|		ИЛИ СУММА(ТаблицаИзменений.Запущено) <> 0
	|		ИЛИ СУММА(ТаблицаИзменений.Готово) <> 0)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Склад,
	|	Назначение,
	|	ЗаказНаПроизводство
	|;
	|УНИЧТОЖИТЬ ДвиженияЗаказыНаПроизводствоПередЗаписью
	|";
	
	МассивРезультатовЗапроса = Запрос.ВыполнитьПакет();
	РезультатЗапроса = МассивРезультатовЗапроса[0]; // РезультатЗапроса
	Выборка = РезультатЗапроса.Выбрать();
	ЕстьИзменения = Выборка.Следующий() И Выборка.Количество > 0;
	
	Если ЕстьИзменения Тогда
		СтруктураЗаказаПроведениеДокументов.РассчитатьСостояниеПродукцииЗаказа(ДополнительныеСвойства.ВременныеТаблицыРасчетаСостоянииПродукцииЗаказа,
			"ДвиженияЗаказыНаПроизводствоИзменения");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Код процедур и функций

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьБлокировкуЗаказов(МенеджерВременныхТаблиц)
	
	Заказы = Выгрузить(, "ЗаказНаПроизводство");
	Заказы.Свернуть("ЗаказНаПроизводство");
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Заказы", Заказы);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Заказы.ЗаказНаПроизводство КАК ЗаказНаПроизводство
	|ПОМЕСТИТЬ Заказы
	|ИЗ
	|	&Заказы КАК Заказы
	|;
	|ВЫБРАТЬ
	|	Таблица.ЗаказНаПроизводство КАК ЗаказНаПроизводство
	|ИЗ
	|	Заказы КАК Таблица
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	Таблица.ЗаказНаПроизводство
	|ИЗ
	|	ДвиженияЗаказыНаПроизводствоПередЗаписью КАК Таблица
	|;
	|";
	РезультатЗапроса = Запрос.Выполнить();
	
	БлокировкаДанных = Новый БлокировкаДанных();
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.ЗаказыНаПроизводство");
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ЗаказНаПроизводство", "ЗаказНаПроизводство");
	БлокировкаДанных.Заблокировать();
	
КонецПроцедуры

#КонецОбласти

#Область Инициализация

#КонецОбласти

#КонецЕсли
