
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Таб.Организация,
	|	МАКСИМУМ(Таб.ДатаЗапрета) КАК ДатаЗапрета,
	|	Таб.Наименование КАК Наименование
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
	|		ДАТАВРЕМЯ(1, 1, 1) КАК ДатаЗапрета,
	|		"""" КАК Наименование
	|	
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ
	|		ДатыЗапретаФормированияПроводок.Организация,
	|		ДатыЗапретаФормированияПроводок.ДатаЗапрета,
	|		""""
	|	ИЗ
	|		РегистрСведений.ДатыЗапретаФормированияПроводокМеждународныйУчет КАК ДатыЗапретаФормированияПроводок
	|	ГДЕ
	|		ДатыЗапретаФормированияПроводок.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)) КАК Таб
	|
	|СГРУППИРОВАТЬ ПО
	|	Таб.Наименование,
	|	Таб.Организация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Организации.Ссылка,
	|	ДатыЗапретаФормированияПроводок.ДатаЗапрета,
	|	Организации.Наименование
	|ИЗ
	|	Справочник.Организации КАК Организации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДатыЗапретаФормированияПроводокМеждународныйУчет КАК ДатыЗапретаФормированияПроводок
	|		ПО Организации.Ссылка = ДатыЗапретаФормированияПроводок.Организация
	|ГДЕ
	|	Организации.Ссылка <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Наименование";
	
	ДатыЗапрета.Очистить();
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(Запрос.Выполнить().Выгрузить(),ДатыЗапрета);
	ТолькоПросмотр = НЕ ПравоДоступа("Изменение", Метаданные.РегистрыСведений.ДатыЗапретаФормированияПроводокМеждународныйУчет);

	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.Очистить();
	Для Каждого стр Из ДатыЗапрета Цикл
		Если ЗначениеЗаполнено(стр.ДатаЗапрета) Тогда
			НоваяСтрока = ТекущийОбъект.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, стр);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ИзмененаДатаЗапретаФормированияПроводок");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура УстановитьВсем(Команда)
	
	ОбработчикВводаДаты = Новый ОписаниеОповещения("УстановитьДатуЗапретаДляВсех",ЭтотОбъект);
	ПоказатьВводДаты(ОбработчикВводаДаты,ДатыЗапрета[0].ДатаЗапрета,НСтр("ru = 'Введите дату';
																		|en = 'Enter the date'"),ЧастиДаты.Дата);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьВсем(Команда)
	
	Чистильщик = Новый ОписаниеОповещения("ОчиститьДатуЗапретаДляВсех",ЭтотОбъект);
	ТекстВопроса = НСтр("ru = 'Убрать все даты?';
						|en = 'Remove all dates?'");
	Если Элементы.ДатыЗапрета.ВыделенныеСтроки.Количество() > 1 Тогда
		ТекстВопроса = НСтр("ru = 'Убрать выбранные даты?';
							|en = 'Remove the selected dates?'");
	КонецЕсли;
	ПоказатьВопрос(Чистильщик,ТекстВопроса,РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Да);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьДатуЗапретаДляВсех(ДатаЗапрета, Параметры) Экспорт
	
	Если ЗначениеЗаполнено(ДатаЗапрета) Тогда
		ЗаполнитьДату(ДатаЗапрета);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьДатуЗапретаДляВсех(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗаполнитьДату();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДату(НоваяДата = Неопределено)
	
	Если Элементы.ДатыЗапрета.ВыделенныеСтроки.Количество() > 1 Тогда
		Для Каждого ИдСтроки Из Элементы.ДатыЗапрета.ВыделенныеСтроки Цикл
			ДанныеСтроки = ДатыЗапрета.НайтиПоИдентификатору(ИдСтроки);
			ДанныеСтроки.ДатаЗапрета = НоваяДата;
		КонецЦикла;
	Иначе
		Для Каждого стр Из ДатыЗапрета Цикл
			стр.ДатаЗапрета = НоваяДата;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти