#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив();
	МассивНепроверяемыхРеквизитов.Добавить("ДатаКонтрольПроизводства");
	МассивНепроверяемыхРеквизитов.Добавить("ДатаРозницаРазрешительныйРежим");
	МассивНепроверяемыхРеквизитов.Добавить("ДатаРозницаЛокальныйРазрешительныйРежим");
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	МассивТГОбязательнойОнлайнПроверки = ОбщегоНазначенияИСМПКлиентСерверПовтИсп.ВидыПродукцийОбязательнойПроверкиПередРозничнойПродажей();
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		
		Если Не Запись.ВестиУчетПродукции Тогда
			Продолжить;
		КонецЕсли;
		
		Если Запись.ДатаКонтрольПроизводства = '00010101' Тогда
			ТекстОшибки = НСтр("ru = 'Для маркируемой продукции ""%1"" не указана дата контроля производства';
								|en = 'Для маркируемой продукции ""%1"" не указана дата контроля производства'");
			ТекстОшибки = СтрШаблон(ТекстОшибки, Запись.ВидПродукции);
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,, "ДатаКонтрольПроизводства", "Запись", Отказ);
		КонецЕсли;
		
		Если Запись.ДатаРозницаРазрешительныйРежим = '00010101'
			И МассивТГОбязательнойОнлайнПроверки.Найти(Запись.ВидПродукции) <> Неопределено Тогда
			ТекстОшибки = НСтр("ru = 'Для маркируемой продукции ""%1"" не указана дата включения обязательного разрешительного режима розничной проверки';
								|en = 'Для маркируемой продукции ""%1"" не указана дата включения обязательного разрешительного режима розничной проверки'");
			ТекстОшибки = СтрШаблон(ТекстОшибки, Запись.ВидПродукции);
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,, "ДатаРозницаРазрешительныйРежим", "Запись", Отказ);
		КонецЕсли;
		
		Если Запись.ДатаРозницаЛокальныйРазрешительныйРежим = '00010101'
			И МассивТГОбязательнойОнлайнПроверки.Найти(Запись.ВидПродукции) <> Неопределено Тогда
			ТекстОшибки = НСтр("ru = 'Для маркируемой продукции ""%1"" не указана дата включения обязательного локального разрешительного режима розничной проверки';
								|en = 'Для маркируемой продукции ""%1"" не указана дата включения обязательного локального разрешительного режима розничной проверки'");
			ТекстОшибки = СтрШаблон(ТекстОшибки, Запись.ВидПродукции);
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,, "ДатаРозницаЛокальныйРазрешительныйРежим", "Запись", Отказ);
		КонецЕсли;
		
		Если Запись.ДатаРозницаРазрешительныйРежим > Запись.ДатаРозницаЛокальныйРазрешительныйРежим
			И МассивТГОбязательнойОнлайнПроверки.Найти(Запись.ВидПродукции) <> Неопределено Тогда
			ТекстОшибки = НСтр("ru = 'Для маркируемой продукции ""%1"" дата включения локальной проверки разрешительного режима не должна быть ранее даты разрешительного режима';
								|en = 'Для маркируемой продукции ""%1"" дата включения локальной проверки разрешительного режима не должна быть ранее даты разрешительного режима'");
			ТекстОшибки = СтрШаблон(ТекстОшибки, Запись.ВидПродукции);
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,, "ДатаРозницаРазрешительныйРежим", "Запись", Отказ);
		КонецЕсли;
		
		Если Запись.ДатаРозницаУведомительныйРежим > Запись.ДатаРозницаРазрешительныйРежим
			И МассивТГОбязательнойОнлайнПроверки.Найти(Запись.ВидПродукции) <> Неопределено Тогда
			ТекстОшибки = НСтр("ru = 'Для маркируемой продукции ""%1"" дата включения разрешительного режима не должна быть ранее даты уведомительного режима';
								|en = 'Для маркируемой продукции ""%1"" дата включения разрешительного режима не должна быть ранее даты уведомительного режима'");
			ТекстОшибки = СтрШаблон(ТекстОшибки, Запись.ВидПродукции);
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,, "ДатаРозницаРазрешительныйРежим", "Запись", Отказ);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеУчитываемыеТГОбязательнойОнлайнПроверки = ОбщегоНазначенияИСМПВызовСервера.СписокУчитываемойПродукцииТребующейОбязательнойПроверкиПриПродажеВРозницу();
	
	Если ТекущиеУчитываемыеТГОбязательнойОнлайнПроверки.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	ЗапуститьПервоеВключениеТГОбязательнойПроверки = Ложь;
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		
		Если Запись.ВестиУчетПродукции И ЗначениеЗаполнено(Запись.ДатаРозницаРазрешительныйРежим) Тогда
			
			ЗапуститьПервоеВключениеТГОбязательнойПроверки = Истина;
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЗапуститьПервоеВключениеТГОбязательнойПроверки Тогда
		ДополнительныеСвойства.Вставить("ЗапуститьФоновоеОбновлениеНастроекРазрешительногоРежима", Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли