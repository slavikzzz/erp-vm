#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПодписиДокументовКЭДО.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ПодписиДокументовКЭДО.Отпечаток КАК Отпечаток
		|ИЗ
		|	РегистрСведений.ПодписиДокументовКЭДО КАК ПодписиДокументовКЭДО
		|ГДЕ
		|	ПодписиДокументовКЭДО.Объект = &Объект
		|	И ПодписиДокументовКЭДО.ФизическоеЛицо = &ФизическоеЛицо
		|	И ПодписиДокументовКЭДО.Отпечаток <> """"";
	Запрос.УстановитьПараметр("Объект", Отбор.Объект.Значение);
	Если Отбор.ФизическоеЛицо.Использование Тогда
		Запрос.УстановитьПараметр("ФизическоеЛицо", Отбор.ФизическоеЛицо.Значение);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПодписиДокументовКЭДО.ФизическоеЛицо = &ФизическоеЛицо", "(ИСТИНА)");
	КонецЕсли;
	ПодписиОбъекта = Запрос.Выполнить().Выгрузить();
	Если ЗначениеЗаполнено(ПодписиОбъекта) Тогда
		ДополнительныеСвойства.Вставить("ПрежниеПодписи", ПодписиОбъекта);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ПрежниеПодписи") Тогда
		ЭлектронныйДокумент = Неопределено;
		ПрежниеПодписи = ДополнительныеСвойства.ПрежниеПодписи;
		Для Каждого Запись Из ЭтотОбъект Цикл
			ДанныеПодписи = ПрежниеПодписи.Найти(Запись.ФизическоеЛицо, "ФизическоеЛицо");
			Если ДанныеПодписи <> Неопределено Тогда
				Если ДанныеПодписи.Отпечаток = Запись.Отпечаток Тогда
					Продолжить;
				КонецЕсли;
				Если ЭлектронныйДокумент = Неопределено Тогда
					ЭлектронныйДокумент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
						Отбор.Объект.Значение, "ЭлектронныйДокумент");
				КонецЕсли;
				Запрос = Новый Запрос;
				Запрос.УстановитьПараметр("ЭлектронныйДокумент", ЭлектронныйДокумент);
				Запрос.УстановитьПараметр("Отпечаток", ДанныеПодписи.Отпечаток);
				Запрос.Текст =
					"ВЫБРАТЬ
					|	ЭлектронныеПодписи.ПорядковыйНомер КАК ПорядковыйНомер
					|ИЗ
					|	РегистрСведений.ЭлектронныеПодписи КАК ЭлектронныеПодписи
					|ГДЕ
					|	ЭлектронныеПодписи.ПодписанныйОбъект = &ЭлектронныйДокумент
					|	И ЭлектронныеПодписи.Отпечаток = &Отпечаток";
				Выборка = Запрос.Выполнить().Выбрать();
				Если Выборка.Следующий() Тогда
					НаборЗаписей = РегистрыСведений.ЭлектронныеПодписи.СоздатьНаборЗаписей();
					НаборЗаписей.Отбор.ПодписанныйОбъект.Установить(ЭлектронныйДокумент);
					НаборЗаписей.Отбор.ПорядковыйНомер.Установить(Выборка.ПорядковыйНомер);
					НаборЗаписей.Записать();
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли
