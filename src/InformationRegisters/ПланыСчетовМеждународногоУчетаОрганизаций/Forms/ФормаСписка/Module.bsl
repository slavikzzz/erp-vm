
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("НастройкаФормированияПроводок") Тогда
		Элементы.НастройкаФормированияПроводок.Видимость = Ложь;
	КонецЕсли;
	Если Параметры.Отбор.Свойство("Организация") Тогда
		Элементы.Организация.Видимость = Ложь;
	КонецЕсли;
	
	ПланСчетов = Параметры.ПланСчетов;
	Если Параметры.Отбор.Свойство("ПланСчетов") И ЗначениеЗаполнено(Параметры.Отбор.ПланСчетов) Тогда
		ПланСчетов = Параметры.Отбор.ПланСчетов;
		Параметры.Отбор.Удалить("ПланСчетов");
		Элементы.ПланСчетовОтбор.Видимость = Ложь;
		Элементы.ПланСчетов.Видимость = Ложь;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ПланСчетов) Тогда
		ПланСчетов = Справочники.ПланыСчетовМеждународногоУчета.ПланСчетовПоУмолчанию();
	КонецЕсли;
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ПланСчетов", ПланСчетов, , , ЗначениеЗаполнено(ПланСчетов));
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Оповестить("ЗакрытиыеФормыСписка_ПланыСчетовМеждународногоУчетаОрганизаций", , ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПланСчетовОтборПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ПланСчетов", ПланСчетов, , , ЗначениеЗаполнено(ПланСчетов));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеФормы = Элементы.НастройкаФормированияПроводок; // ПолеФормы - 
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ПолеФормы.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.НастройкаФормированияПроводок");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Справочники.НастройкиФормированияПроводокМеждународногоУчета.ПустаяСсылка();
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Не используется';
																|en = 'Not used'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
КонецПроцедуры

#КонецОбласти
