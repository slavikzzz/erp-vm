#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Находит сообщение налогового органа об исчисленной сумме налога. При этом учитывается, что
// налог мог быть рассчитан "централизованно": какой-то один налоговый орган посчитал налог по
// всем объектам организации, в том числе находящимся на территории других налоговых органов. 
// В этом случае в составе результата функция вернет код налогового органа - отправителя сообщения. 
// Фактически это код НО, с которым нужно сверять расчет налога.
// Если не используется загрузка сообщений с расчетом имущественных налогов, то
// в результате будет возвращен переданный код НО.
//
// Параметры:
//  Организация - СправочникСсылка.Организации
//  Налог - ПеречислениеСсылка.ВидыИмущественныхНалогов - налог, по которому нужно сверить расчет 
//  Период - Дата - любая дата в налоговом периоде, за который нужно сверить расчет
//  КодНалоговогоОргана - Строка - код налогового органа, в котором зарегистрированы объекты налогообложения
//  КодОтправителя - Строка - код налогового органа, который прислал сообщение и с которым нужно сверить расчет 
// 
// Возвращаемое значение:
//   - Структура - содержит: 
//			* Сообщение - СправочникСсылка.ДокументыРеализацииПолномочийНалоговыхОрганов, Неопределено - сообщение с расчетом налога
//			* КодОтправителя - Строка - код налогового органа, который прислал сообщение и с которым нужно сверить расчет
//
Функция СведенияОСообщении(Организация, Налог, Период, КодНалоговогоОргана, КодОтправителя = Неопределено) Экспорт

	СведенияОСообщении = Новый Структура;
	СведенияОСообщении.Вставить("КодОтправителя", "");
	СведенияОСообщении.Вставить("Сообщение", Неопределено);
	
	// В некоторых случаях (например, для крупнейшего налогоплательщика) налоговый орган может посчитать 
	// налог по всем объектам организации, в том числе находящимся вне места ее нахождения (в обособленных подразделениях).
	// Если при этом из ФНС загружалось сообщение с расчетом налога, то в РС СообщенияСРасчетомИмущественныхНалогов хранится информация
	// о коде налогового органа - отправителя, а также о кодах налоговых органов, на территории которых находятся объекты. Таким образом,
	// при таком "централизованном" порядке сверка будет одна для объектов, зарегистрированных в нескольких налоговых органах.
	// При этом полагаем, что штатно для конкретного кода налоговой инспекции отправитель может быть только один (т.е. не можем получить
	// два и более расчетов из разных налоговых органов по одним и тем же объектам).
	// Если расчет налога из ФНС не загружался (то есть, применяется ручная сверка), то считаем, что сверка выполняется всегда в рамках
	// одного налогового органа (в терминах программы - по всем регистрациям с одинаковым кодом НО).
	
	// Запрос модифицируется ниже.
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	СообщенияСРасчетомИмущественныхНалогов.КодОтправителя КАК КодОтправителя,
	|	СообщенияСРасчетомИмущественныхНалогов.Сообщение КАК Сообщение
	|ИЗ
	|	РегистрСведений.СообщенияСРасчетомИмущественныхНалогов КАК СообщенияСРасчетомИмущественныхНалогов
	|ГДЕ
	|	СообщенияСРасчетомИмущественныхНалогов.Организация = &Организация
	|	И СообщенияСРасчетомИмущественныхНалогов.Налог = &Налог
	|	И СообщенияСРасчетомИмущественныхНалогов.НалоговыйПериод = &НалоговыйПериод";
	
	Запрос.УстановитьПараметр("Организация",         Организация);
	Запрос.УстановитьПараметр("Налог",               Налог);
	Запрос.УстановитьПараметр("НалоговыйПериод",     НачалоГода(Период));
	
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(Запрос.Текст);
	Если ЗначениеЗаполнено(КодОтправителя) Тогда
		СхемаЗапроса.ПакетЗапросов[0].Операторы[0].Отбор.Добавить(
			Новый ВыражениеСхемыЗапроса("СообщенияСРасчетомИмущественныхНалогов.КодОтправителя = &КодОтправителя"));
		Запрос.УстановитьПараметр("КодОтправителя", КодОтправителя);
	Иначе	
		СхемаЗапроса.ПакетЗапросов[0].Операторы[0].Отбор.Добавить(
			Новый ВыражениеСхемыЗапроса("СообщенияСРасчетомИмущественныхНалогов.КодНалоговогоОргана = &КодНалоговогоОргана"));
		Запрос.УстановитьПараметр("КодНалоговогоОргана", КодНалоговогоОргана);
	КонецЕсли;	
	
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		СведенияОСообщении.КодОтправителя = ?(ЗначениеЗаполнено(КодОтправителя), КодОтправителя, КодНалоговогоОргана);
		Возврат СведенияОСообщении;
	КонецЕсли;	
		
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();

	ЗаполнитьЗначенияСвойств(СведенияОСообщении, Выборка);
	
	Возврат СведенияОСообщении;
	
КонецФункции

// Определяет налоговые органы, в которых учтены объекты налогообложения, попавшие в расчет налога, присланный отправителем
//
// Параметры:
//  Организация - СправочникСсылка.Организации
//  Налог - ПеречислениеСсылка.ВидыИмущественныхНалогов - налог, по которому выполняется сверка 
//  Период - Дата - любая дата в налоговом периоде, за который нужно сверить расчет
//  КодОтправителя - Строка - код налогового органа, который прислал сообщение и с которым нужно сверить расчет
// 
// Возвращаемое значение:
//  Массив из Строка- массив кодов налоговых органов, в которых учтены объекты, попавшие в расчет налога,
//					  который прислал налоговый орган - отправитель.
//
Функция КодыНалоговыхОрганов(Организация, Налог, Период, КодОтправителя) Экспорт 

	КодыНалоговыхОрганов = Новый Массив;

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	СообщенияСРасчетомИмущественныхНалогов.КодНалоговогоОргана КАК КодНалоговогоОргана
	|ИЗ
	|	РегистрСведений.СообщенияСРасчетомИмущественныхНалогов КАК СообщенияСРасчетомИмущественныхНалогов
	|ГДЕ
	|	СообщенияСРасчетомИмущественныхНалогов.Организация = &Организация
	|	И СообщенияСРасчетомИмущественныхНалогов.Налог = &Налог
	|	И СообщенияСРасчетомИмущественныхНалогов.НалоговыйПериод = &НалоговыйПериод
	|	И СообщенияСРасчетомИмущественныхНалогов.КодОтправителя = &КодОтправителя";

	Запрос.УстановитьПараметр("Организация",     Организация);
	Запрос.УстановитьПараметр("Налог",           Налог);
	Запрос.УстановитьПараметр("НалоговыйПериод", НачалоГода(Период));
	Запрос.УстановитьПараметр("КодОтправителя",  КодОтправителя);

	РезультатЗапроса = Запрос.Выполнить();

	Если РезультатЗапроса.Пустой() Тогда
		КодыНалоговыхОрганов.Добавить(КодОтправителя);
		Возврат КодыНалоговыхОрганов;
	КонецЕсли;

	Выборка = РезультатЗапроса.Выбрать();

	Пока Выборка.Следующий() Цикл
		КодыНалоговыхОрганов.Добавить(Выборка.КодНалоговогоОргана);
	КонецЦикла;

	Возврат КодыНалоговыхОрганов;

КонецФункции

// Возвращает скорректированные сроки сверки по налогам, по которым получены сообщения с расчетом из ФНС.
// Корректировки связаны с тем, что после получения сообщения из ФНС срок передачи пояснений ограничен.
//
// Параметры:
//  Организация - СправочникСсылка.Организации
// 
// Возвращаемое значение:
//  ТаблицаЗначений - данные о переносе сроков сверки налогов, по которым из ФНС поступили сообщения с расчетом налога
//
Функция ОсобыеСрокиСверкиИмущественныхНалогов(Организация) Экспорт

	ОсобыеСрокиСверки = Новый ТаблицаЗначений;
	ОсобыеСрокиСверки.Колонки.Добавить("Налог", ОбщегоНазначения.ОписаниеТипаСтрока(50));
	ОсобыеСрокиСверки.Колонки.Добавить("ПериодСобытия", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ОсобыеСрокиСверки.Колонки.Добавить("КодНалоговогоОргана", ОбщегоНазначения.ОписаниеТипаСтрока(4));
	ОсобыеСрокиСверки.Колонки.Добавить("Срок", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СообщенияСРасчетомИмущественныхНалогов.Налог КАК Налог,
	|	СообщенияСРасчетомИмущественныхНалогов.НалоговыйПериод КАК НалоговыйПериод,
	|	СообщенияСРасчетомИмущественныхНалогов.КодНалоговогоОргана КАК КодНалоговогоОргана,
	|	МАКСИМУМ(СообщенияСРасчетомИмущественныхНалогов.Сообщение.ДатаДокумента) КАК Срок
	|ИЗ
	|	РегистрСведений.СообщенияСРасчетомИмущественныхНалогов КАК СообщенияСРасчетомИмущественныхНалогов
	|ГДЕ
	|	СообщенияСРасчетомИмущественныхНалогов.Организация = &Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	СообщенияСРасчетомИмущественныхНалогов.Налог,
	|	СообщенияСРасчетомИмущественныхНалогов.НалоговыйПериод,
	|	СообщенияСРасчетомИмущественныхНалогов.КодНалоговогоОргана";

	Запрос.УстановитьПараметр("Организация", Организация);

	Выборка = Запрос.Выполнить().Выбрать();
	День = 24 * 60 * 60;

	Пока Выборка.Следующий() Цикл
		ОсобыйСрок = ОсобыеСрокиСверки.Добавить();
		ОсобыйСрок.Налог = Выборка.Налог;
		ОсобыйСрок.ПериодСобытия = КонецГода(Выборка.НалоговыйПериод);
		ОсобыйСрок.КодНалоговогоОргана = Выборка.КодНалоговогоОргана;
		
		// Согласно п. 6 ст. 363 и с учетом п.6 ст. 6.1 НК организация вправе представить пояснения в течение 20 рабочих дней
		// после получения сообщения об исчисленной сумме имущественного налога.
		// Попытаемся рассчитать срок в рабочих днях, если заполнен производственный календарь.
		ОсобыйСрок.Срок = Выборка.Срок + 20 * День;
	КонецЦикла;

	ОсобыеСрокиСверки.Индексы.Добавить("Налог, ПериодСобытия, КодНалоговогоОргана");

	Возврат ОсобыеСрокиСверки;

КонецФункции

#КонецОбласти

#КонецЕсли