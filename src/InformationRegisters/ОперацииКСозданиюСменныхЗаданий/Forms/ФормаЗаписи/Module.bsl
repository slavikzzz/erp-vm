#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	&ТекстУчасток,
		|	
		|	ВЫБОР
		|		КОГДА Операция.ЕдиницаИзмерения = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
		|			ТОГДА &ЕдиницаИзмеренияОперацииПоУмолчанию
		|		ИНАЧЕ Операция.ЕдиницаИзмерения.Представление
		|	КОНЕЦ             КАК ЕдиницаИзмеренияПредставление,
		|
		|	Операция.Описание КАК Описание
		|ИЗ
		|	Справочник.ТехнологическиеОперации КАК Операция
		|ГДЕ
		|	Операция.Ссылка = &Ссылка");
	Запрос.УстановитьПараметр("Ссылка", Запись.Операция);
	Запрос.УстановитьПараметр("ЕдиницаИзмеренияОперацииПоУмолчанию", НСтр("ru = 'оп.';
																			|en = 'op.'"));
	
	ПараметрыЗаполнения = "Участок, ЕдиницаИзмеренияПредставление";
	
	Если ЗначениеЗаполнено(Запись.Участок) Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстУчасток,", "");
		ПараметрыЗаполнения = "ЕдиницаИзмеренияПредставление";
		Участок = Запись.Участок;
	
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстУчасток,",
		"ВЫБОР
		|		КОГДА Операция.РабочийЦентр ССЫЛКА Справочник.РабочиеЦентры
		|			ТОГДА Операция.РабочийЦентр.Участок
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПроизводственныеУчастки.ПустаяСсылка)
		|	КОНЕЦ             КАК Участок,");
	КонецЕсли;
		
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтаФорма, Выборка, ПараметрыЗаполнения);
		
		Элементы.ОперацияОписание.Видимость = ЗначениеЗаполнено(Выборка.Описание);
		
	КонецЕсли;
	
	Элементы.Участок.Видимость = ЗначениеЗаполнено(Участок);
	ВремяОбщее = Запись.Количество * Запись.ВремяШтучное + Запись.ВремяПЗ;
	
	Файлы = Новый Массив;
	РаботаСФайлами.ЗаполнитьПрисоединенныеФайлыКОбъекту(Запись.Операция, Файлы);
	Элементы.ПрисоединенныеФайлы.Видимость = Файлы.Количество()>0;
	Элементы.ПрисоединенныеФайлы.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Дополнительные материалы (%1)';
			|en = 'Additional materials (%1)'"),
		Файлы.Количество());
	
КонецПроцедуры

#КонецОбласти
