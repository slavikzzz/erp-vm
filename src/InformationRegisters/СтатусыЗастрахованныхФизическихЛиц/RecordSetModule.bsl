#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
		
	ФизическиеЛица = Новый Массив;
	Для Каждого СтрокаНабора Из ЭтотОбъект Цикл
		Если СтрокаНабора.ВидЗастрахованногоЛица = Перечисления.ВидыЗастрахованныхЛицОбязательногоСтрахования.ВременноПребывающиеИностранцыСтрахуемыеНаОПС
			Или СтрокаНабора.ВидЗастрахованногоЛица = Перечисления.ВидыЗастрахованныхЛицОбязательногоСтрахования.ВременноПребывающиеИностранцыСтрахуемыеФСС
			Тогда
			ФизическиеЛица.Добавить(СтрокаНабора.ФизическоеЛицо)
		КонецЕсли;
	КонецЦикла;
	
	// Если доходы относятся еще к 2020 году или ранее, проверять доходы не требуется.
	Если ФизическиеЛица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ФизическиеЛица", ФизическиеЛица);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Сотрудники.ГоловнаяОрганизация КАК ГоловнаяОрганизация
	|ИЗ
	|	Справочник.Сотрудники КАК Сотрудники
	|ГДЕ
	|	Сотрудники.ФизическоеЛицо В(&ФизическиеЛица)";
	Организации = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ГоловнаяОрганизация");
	
    Для каждого ГоловнаяОрганизация Из Организации Цикл
		ДанныеУчетнойПолитики = РегистрыСведений.НастройкиУчетаСтраховыхВзносов.СоздатьНаборЗаписей();
		ДанныеУчетнойПолитики.Отбор.Организация.Установить(ГоловнаяОрганизация);
		ДанныеУчетнойПолитики.Прочитать();
		Если ДанныеУчетнойПолитики.Количество() = 0 Тогда
			Запись = ДанныеУчетнойПолитики.Добавить();
			Запись.Организация = ГоловнаяОрганизация;
		Иначе
			Запись = ДанныеУчетнойПолитики[0] 
		КонецЕсли;
		Запись.ИспользуетсяТрудИностранцевСОсобымиВзносами = Истина;
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДанныеУчетнойПолитики);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли