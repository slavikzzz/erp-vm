#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ФормаУдалить",
		"Видимость",
		Не Запись.Предопределенный);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ИдентификаторПечатнойФормы",
		"ТолькоПросмотр",
		Запись.Предопределенный);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"Наименование",
		"КнопкаОчистки",
		Запись.Предопределенный);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ВариантПодписания",
		"Видимость",
		Не Запись.Предопределенный
			И ИнтеграцияКабинетСотрудника.ИспользуетсяВерсияDTO("2.1"));
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"КодДокументаКадровогоМероприятия",
		"ПодсказкаВвода",
		КадровыйЭДОПовтИсп.СвойстваКодаКадровогоДокумента().Код);
	
	УстановитьОтображениеПодсказкиКодаДокумента();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ТекущийОбъект.Предопределенный Тогда
		ТекущийОбъект.ПользовательскоеНаименование =
			ТекущийОбъект.Наименование <> НаименованиеПредопределенного(ТекущийОбъект.ИдентификаторПечатнойФормы);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НаименованиеОчистка(Элемент, СтандартнаяОбработка)
	
	Если Запись.Предопределенный Тогда
		СтандартнаяОбработка = Ложь;
		Запись.Наименование = НаименованиеПредопределенного(Запись.ИдентификаторПечатнойФормы);
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КодДокументаКадровогоМероприятияПриИзменении(Элемент)
	
	УстановитьОтображениеПодсказкиКодаДокумента();
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьОтображениеПодсказкиКодаДокумента()
	
	КодДокументаКадровогоМероприятия = Запись.КодДокументаКадровогоМероприятия;
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"КодДокументаКадровогоМероприятия",
		"Подсказка",
		КадровыйЭДОПовтИсп.СвойстваКодаКадровогоДокумента(
			КодДокументаКадровогоМероприятия).НаименованиеПолное);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НаименованиеПредопределенного(ИдентификаторПечатнойФормы)
	
	НастройкиПечатныхФормПоУмолчанию = КадровыйЭДО.ОписанияНастроекПечатныхФормПоУмолчанию();
	НастрокиПечатнойФормы = НастройкиПечатныхФормПоУмолчанию.Найти(ИдентификаторПечатнойФормы, "ИдентификаторПечатнойФормы");
	Если НастрокиПечатнойФормы <> Неопределено Тогда
		Возврат НастрокиПечатнойФормы.Наименование;
	КонецЕсли;
	
	Возврат ИдентификаторПечатнойФормы;
	
КонецФункции

#КонецОбласти
