#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Процедура ОбновитьЗаписиПоОбъектам(ПодписанныеОбъекты) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПодписанныеОбъекты", ПодписанныеОбъекты);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ОбъекдиненнаяТаблица.ПодписанныйОбъект КАК ПодписанныйОбъект,
		|	ОбъекдиненнаяТаблица.УстановившийПодпись КАК УстановившийПодпись,
		|	СУММА(ОбъекдиненнаяТаблица.ПризнакНаличияЗаписи) КАК ПризнакНаличияЗаписи
		|ИЗ
		|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ЭлектронныеПодписи.ПодписанныйОбъект КАК ПодписанныйОбъект,
		|		ЭлектронныеПодписи.УстановившийПодпись КАК УстановившийПодпись,
		|		1 КАК ПризнакНаличияЗаписи
		|	ИЗ
		|		РегистрСведений.ЭлектронныеПодписи КАК ЭлектронныеПодписи
		|	ГДЕ
		|		ЭлектронныеПодписи.ПодписанныйОбъект В(&ПодписанныеОбъекты)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ПодписанныеОбъектыПользователей.ПодписанныйОбъект,
		|		ПодписанныеОбъектыПользователей.УстановившийПодпись,
		|		-1
		|	ИЗ
		|		РегистрСведений.ПодписанныеОбъектыПользователей КАК ПодписанныеОбъектыПользователей
		|	ГДЕ
		|		ПодписанныеОбъектыПользователей.ПодписанныйОбъект В(&ПодписанныеОбъекты)) КАК ОбъекдиненнаяТаблица
		|
		|СГРУППИРОВАТЬ ПО
		|	ОбъекдиненнаяТаблица.ПодписанныйОбъект,
		|	ОбъекдиненнаяТаблица.УстановившийПодпись
		|
		|ИМЕЮЩИЕ
		|	СУММА(ОбъекдиненнаяТаблица.ПризнакНаличияЗаписи) <> 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПодписанныйОбъект";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("ПодписанныйОбъект") Цикл
		
		НачатьТранзакцию();
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПодписанныеОбъектыПользователей");
			ЭлементБлокировки.УстановитьЗначение("ПодписанныйОбъект", Выборка.ПодписанныйОбъект);
			Блокировка.Заблокировать();
			
			НаборЗаписей = РегистрыСведений.ПодписанныеОбъектыПользователей.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.ПодписанныйОбъект.Установить(Выборка.ПодписанныйОбъект);
			НаборЗаписей.Прочитать();
			
			ТаблицаНабора = НаборЗаписей.Выгрузить();
			Пока Выборка.Следующий() Цикл
				
				Если Выборка.ПризнакНаличияЗаписи = -1 Тогда
					
					ПараметрыОтбора = Новый Структура("ПодписанныйОбъект,УстановившийПодпись");
					ЗаполнитьЗначенияСвойств(ПараметрыОтбора, Выборка);
					
					НайденныеЗаписи = ТаблицаНабора.НайтиСтроки(ПараметрыОтбора);
					Для Каждого Запись Из НайденныеЗаписи Цикл
						ТаблицаНабора.Удалить(Запись);
					КонецЦикла;
					
				Иначе
					
					Запись = ТаблицаНабора.Добавить();
					Запись.ПодписанныйОбъект = Выборка.ПодписанныйОбъект;
					Запись.УстановившийПодпись = Выборка.УстановившийПодпись;
					
				КонецЕсли;
				
			КонецЦикла;
			
			НаборЗаписей.Загрузить(ТаблицаНабора);
			НаборЗаписей.Записать();
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			ОтменитьТранзакцию();
			ЗаписьЖурналаРегистрации(
				КадровыйЭДО.ИмяСобытияЖурналаРегистрации(
					НСтр("ru = 'Обновление информационной базы.Ошибка блокировки';
						|en = 'Updating the infobase.Lock error'", ОбщегоНазначения.КодОсновногоЯзыка())),
				УровеньЖурналаРегистрации.Предупреждение,
				,
				Выборка.ПодписанныйОбъект,
				"РегистрСведений.ПодписанныеОбъектыПользователей");
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПодписанныеОбъекты(ПараметрыОбновления) Экспорт
	
	Запрос = Новый Запрос;
	
	Если ПараметрыОбновления = Неопределено Тогда
		МассивОбработанных = Новый Массив;
	Иначе
		
		Если ПараметрыОбновления.Свойство("МассивОбработанных") Тогда
			МассивОбработанных = ПараметрыОбновления.МассивОбработанных;
		Иначе
			МассивОбработанных = Новый Массив;
			ПараметрыОбновления.Вставить("МассивОбработанных", МассивОбработанных);
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МассивОбработанных", МассивОбработанных);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
		|	ЭлектронныеПодписи.ПодписанныйОбъект КАК ПодписанныйОбъект
		|ИЗ
		|	РегистрСведений.ЭлектронныеПодписи КАК ЭлектронныеПодписи
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПодписанныеОбъектыПользователей КАК ПодписанныеОбъектыПользователей
		|		ПО ЭлектронныеПодписи.ПодписанныйОбъект = ПодписанныеОбъектыПользователей.ПодписанныйОбъект
		|ГДЕ
		|	НЕ ЭлектронныеПодписи.ПодписанныйОбъект В (&МассивОбработанных)
		|	И ПодписанныеОбъектыПользователей.ПодписанныйОбъект ЕСТЬ NULL
		|	И ЭлектронныеПодписи.УстановившийПодпись <> ЗНАЧЕНИЕ(справочник.Пользователи.ПустаяСсылка)";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	
	ОбъектыДляОбновления = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("ПодписанныйОбъект");
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивОбработанных, ОбъектыДляОбновления);
	ОбновитьЗаписиПоОбъектам(ОбъектыДляОбновления);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
