
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс
	
#Область ДляВызоваИзДругихПодсистем
	
	// СтандартныеПодсистемы.УправлениеДоступом
	
	// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
		Ограничение.Текст =
		"РазрешитьЧтениеИзменение
		|ГДЕ
		|	ЗначениеРазрешено(СтруктурнаяЕдиница.Владелец)";
КонецПроцедуры
	
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
// Обновляет историю СКЕ у подчиненных структурных единиц.
// Параметры:
//  СтруктурныеЕдиницы	 - СправочникСсылка.ПодразделенияОрганизаций - подразделения, у которых требуется обновить СКЕ.
//  РежимОбновления		 - Булево - признак выполнения в режиме обмена данными.
//
Процедура ОбновитьПодчиненныеСтруктурныеЕдиницы(СтруктурныеЕдиницы, РежимОбновления = Ложь) Экспорт
		
		УстановитьПривилегированныйРежим(Истина);
		
		Запрос = Новый Запрос;
		Запрос.Текст = Справочники.ПодразделенияОрганизаций.ТекстЗапросаСравненияНаборовСтруктурныхЕдиниц(
		"ИсторияСамостоятельныхКлассификационныхЕдиниц", "СКЕ");
		
		Для Каждого СтрокаСтруктурныеЕдиницы Из СтруктурныеЕдиницы Цикл
			
			Запрос.УстановитьПараметр("ГоловнаяСтруктурнаяЕдиница", СтрокаСтруктурныеЕдиницы.Ключ);
			Запрос.УстановитьПараметр("ПодчиненныеСтруктурныеЕдиницы", СтрокаСтруктурныеЕдиницы.Значение);
			РезультатЗапросов = Запрос.ВыполнитьПакет();
			
			ОсновнойНабор = РезультатЗапросов[РезультатЗапросов.Количество() - 1].Выгрузить();
			Выборка = РезультатЗапросов[РезультатЗапросов.Количество() - 2].Выбрать();
			Пока Выборка.Следующий() Цикл
				НовыйНаборЗаписей = РегистрыСведений.ИсторияСамостоятельныхКлассификационныхЕдиниц.СоздатьНаборЗаписей();
				НовыйНаборЗаписей.Отбор.СтруктурнаяЕдиница.Установить(Выборка.СтруктурнаяЕдиница);
				ОсновнойНабор.ЗаполнитьЗначения(Выборка.СтруктурнаяЕдиница, "СтруктурнаяЕдиница");
				НовыйНаборЗаписей.Загрузить(ОсновнойНабор);
				НовыйНаборЗаписей.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
				Если РежимОбновления Тогда
					НовыйНаборЗаписей.ОбменДанными.Загрузка = Истина;
				КонецЕсли;
				НовыйНаборЗаписей.Записать();
			КонецЦикла;
		КонецЦикла;
		
		УстановитьПривилегированныйРежим(Ложь);
		
КонецПроцедуры
		
#КонецОбласти
	
#КонецОбласти
	
#КонецЕсли
