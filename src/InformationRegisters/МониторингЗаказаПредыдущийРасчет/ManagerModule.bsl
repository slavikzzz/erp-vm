
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗаписатьСведения(Данные) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Набор = РегистрыСведений.МониторингЗаказаПредыдущийРасчет.СоздатьНаборЗаписей();
	Набор.Отбор.ЗаказНаПроизводство.Установить(Данные.ЗаказНаПроизводство);
	ЗаполнитьЗначенияСвойств(Набор.Добавить(), Данные);
	Набор.Записать(Истина);
	
КонецПроцедуры

Функция ПолучитьСведения(ЗаказНаПроизводство) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	СведенияОРасчете = РегистрыСведений.МониторингЗаказаПредыдущийРасчет.СоздатьМенеджерЗаписи();
	СведенияОРасчете.ЗаказНаПроизводство = ЗаказНаПроизводство;
	СведенияОРасчете.Прочитать();

	Если Не СведенияОРасчете.Выбран() Тогда
		// Значения по умолчанию
		СведенияОРасчете.ВремяВыполнения = 0;
		СведенияОРасчете.ПериодРасчета = '00010101';
		СведенияОРасчете.ПроцентОбеспеченности = 0;
		СведенияОРасчете.ПроцентГотовности = 0;
	КонецЕсли;
	
	Возврат ОбщегоНазначения.СтруктураПоМенеджеруЗаписи(
		СведенияОРасчете, Метаданные.РегистрыСведений.МониторингЗаказаПредыдущийРасчет);
	
КонецФункции

#КонецОбласти

#КонецЕсли
