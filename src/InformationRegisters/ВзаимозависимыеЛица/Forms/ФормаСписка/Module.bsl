#Область ОбработчикиСобытийФормы

// В обработчике события ПриСозданииНаСервере выполняется:
// - если задан параметр "Организация" - установка отбора динамического списка по переданному значению параметра
// - если задан параметр "Контрагент" - установка отбора динамического списка по переданному значению параметра.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
  	Если Параметры.Свойство("АвтоТест") Тогда
   		Возврат;
  	КонецЕсли;
	
	Если Параметры.Свойство("Организация") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Организация", 
			Параметры.Организация, ВидСравненияКомпоновкиДанных.Равно);
	КонецЕсли;
	Если Параметры.Свойство("Контрагент") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Контрагент",
			Параметры.Контрагент, ВидСравненияКомпоновкиДанных.Равно);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// При выполнении коменды ЗаполнитьПоОрганизации выполняется:
// - Подготовка параметров формы для открытия формы заполнения по организации
// - Открытие формы заполнения по организации.
//
&НаКлиенте
Процедура ЗаполнитьПоОрганизации(Команда)
	
	ПараметрыДляПередачи = Новый Структура;
	Для каждого Параметр Из Список.Отбор.Элементы Цикл
		Если Параметр.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Организация") И Параметр.Использование
			И Параметр.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно Тогда
			
			ПараметрыДляПередачи.Вставить("Организация", Параметр.ПравоеЗначение);
			
		КонецЕсли;
	КонецЦикла;
	
	ОткрытьФорму("РегистрСведений.ВзаимозависимыеЛица.Форма.ФормаЗаполнения", ПараметрыДляПередачи, ЭтаФорма);
КонецПроцедуры

#КонецОбласти