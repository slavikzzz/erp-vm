#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторСтатуса = Новый УникальныйИдентификатор;
	Для каждого Запись Из ЭтотОбъект Цикл
		Запись.ИдентификаторСтатуса = ИдентификаторСтатуса;
	КонецЦикла;
	
	Если ЭтотОбъект.Количество() = 0 Тогда
		// Ранее отраженный документ (или зарегистрированный к отражению) удаляется,
		//	обновим задания к закрытию месяца по фин. результату.
		Запрос = Новый Запрос();
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Статусы.Организация КАК Организация,
		|	Статусы.Регистратор КАК Документ,
		|	ЗНАЧЕНИЕ(Перечисление.ОперацииЗакрытияМесяца.ФормированиеФинансовогоРезультата) КАК Операция,
		|	МИНИМУМ(НАЧАЛОПЕРИОДА(Статусы.ДатаОтражения, Месяц)) КАК Месяц
		|ИЗ
		|	РегистрСведений.ОтражениеДокументовВРеглУчете КАК Статусы
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗаданияКЗакрытиюМесяца КАК Задания
		|		ПО (Статусы.Организация = Задания.Организация)
		|			И (Статусы.Регистратор = &Регистратор)
		|			И (Задания.Операция = ЗНАЧЕНИЕ(Перечисление.ОперацииЗакрытияМесяца.ФормированиеФинансовогоРезультата))
		|ГДЕ
		|	Статусы.Регистратор = &Регистратор
		|	И НЕ Статусы.Регистратор Ссылка Документ.ОперацияБух
		|	И НЕ (Статусы.Регистратор Ссылка Документ.КорректировкаРеализации И Статусы.ДатаОтражения < НАЧАЛОПЕРИОДА(&ТекущаяДата, ГОД))
		|	И НЕ (Статусы.Регистратор Ссылка Документ.РегламентнаяОперация И Статусы.Регистратор.ТипОперации В (&ТипыОперацийФинРеза))
		|
		|СГРУППИРОВАТЬ ПО
		|	Статусы.Организация,
		|	Статусы.Регистратор
		|
		|ИМЕЮЩИЕ
		|	МИНИМУМ(НАЧАЛОПЕРИОДА(Статусы.ДатаОтражения, Месяц)) < МИНИМУМ(ЕСТЬNULL(Задания.Месяц, КОНЕЦПЕРИОДА(&ТекущаяДата, ГОД)))";
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
		Запрос.УстановитьПараметр("ТипыОперацийФинРеза", Документы.РегламентнаяОперация.ТипыОперацийФинРеза());
		Выборка = Запрос.Выполнить().Выбрать(); 
		ДополнительныеСвойства.Вставить("ЗаписиЗаданийКЗакрытиюМесяца", Выборка);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = Неопределено;
	Если Не Отказ И ДополнительныеСвойства.Свойство("ЗаписиЗаданийКЗакрытиюМесяца", Выборка) Тогда
		РегистрыСведений.ЗаданияКЗакрытиюМесяца.СоздатьЗаписиРегистраПоДаннымВыборки(Выборка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
