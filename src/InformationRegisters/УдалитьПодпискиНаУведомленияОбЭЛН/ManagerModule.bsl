#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

Процедура АдаптацияКУчетуВРазрезеСтрахователей() Экспорт
	
	// Чтение данных регистра (их количество примерно соответствует количеству сотрудников) и физических лиц.
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РегистрПодписок.Сотрудник КАК Сотрудник,
	|	РегистрПодписок.Организация КАК Организация,
	|	РегистрПодписок.Действует КАК Действует,
	|	РегистрПодписок.ДатаОтправки КАК ДатаОтправки,
	|	РегистрПодписок.ТекстОшибки КАК ТекстОшибки,
	|	РегистрПодписок.БудетДействовать КАК БудетДействовать,
	|	РегистрПодписок.ИдентификаторОтвета КАК ИдентификаторОтвета,
	|	РегистрПодписок.ИдентификаторЗапроса КАК ИдентификаторЗапроса,
	|	РегистрПодписок.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ЕСТЬNULL(Сотрудники.ФизическоеЛицо, ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)) КАК ФизическоеЛицоСотрудника
	|ИЗ
	|	РегистрСведений.УдалитьПодпискиНаУведомленияОбЭЛН КАК РегистрПодписок
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
	|		ПО РегистрПодписок.Сотрудник = Сотрудники.Ссылка";
	
	// Определение страхователей.
	ТаблицаРегистра = Запрос.Выполнить().Выгрузить();
	СЭДОФСС.ЗаполнитьСтрахователяВТаблицеЗначений(ТаблицаРегистра);
	
	Для Каждого СтрокаТаблицы Из ТаблицаРегистра Цикл
		
		// Проверка наличия страхователя.
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.Страхователь) Тогда
			Продолжить;
		КонецЕсли;
		
		// Проверка наличия физического лица.
		Если ЗначениеЗаполнено(СтрокаТаблицы.ФизическоеЛицоСотрудника) Тогда
			СтрокаТаблицы.ФизическоеЛицо = СтрокаТаблицы.ФизическоеЛицоСотрудника;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.ФизическоеЛицо) Тогда
			Продолжить;
		КонецЕсли;
		
		// Добавление записи в регистр со страхователями и физическими лицами.
		Набор = РегистрыСведений.ПодпискиНаУведомленияОбЭЛН.НачатьЗаписьНабора(
			СтрокаТаблицы.Страхователь,
			СтрокаТаблицы.ФизическоеЛицо);
		Если Набор = Неопределено Тогда
			Продолжить; // Транзакция уже отменена.
		КонецЕсли;
		Если Набор.Количество() > 0 Тогда
			РегистрыСведений.ПодпискиНаУведомленияОбЭЛН.ОтменитьЗаписьНабора(Набор);
			Продолжить;
		КонецЕсли;
		Запись = Набор.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, СтрокаТаблицы);
		РегистрыСведений.ПодпискиНаУведомленияОбЭЛН.ЗавершитьЗаписьНабора(Набор);
		
		// Удаление записи текущего регистра.
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Организация.Установить(СтрокаТаблицы.Организация);
		НаборЗаписей.Отбор.Сотрудник.Установить(СтрокаТаблицы.Сотрудник);
		НаборЗаписей.Записать(Истина);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли