#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Функция НастройкиДляОбновления() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаНастройки();
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Условие", "Настройки.ТребуетсяОбновление");
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

Функция Настройки(ПричиныОтсутствий = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Если ЗначениеЗаполнено(ПричиныОтсутствий) Тогда
		Запрос.УстановитьПараметр("ПричиныОтсутствий", ПричиныОтсутствий);
	КонецЕсли;
	Запрос.Текст = ТекстЗапросаНастройки();
	Если ЗначениеЗаполнено(ПричиныОтсутствий) Тогда
		ТекстУсловия = "Настройки.ПричинаОтсутствия В(&ПричиныОтсутствий)";
	Иначе
		ТекстУсловия = "ИСТИНА";
	КонецЕсли;
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Условие", ТекстУсловия);
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

Процедура СохранитьНовыеНастройки(НовыеНастройки) Экспорт

	НаборЗаписей = РегистрыСведений.НастройкиОтсутствийКабинетСотрудника.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ПричинаОтсутствия.Установить(НовыеНастройки.ПричинаОтсутствия);
	ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), НовыеНастройки);
	НаборЗаписей.Записать();

КонецПроцедуры

Функция ТекстЗапросаНастройки()

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Настройки.ПричинаОтсутствия КАК ПричинаОтсутствия,
	|	Настройки.ОформлятьЗаднимЧислом КАК ОформлятьЗаднимЧислом,
	|	Настройки.ТребуетсяОбновление КАК ТребуетсяОбновление,
	|	Настройки.КоличествоДнейДоНачалаОтсутствияДляИзмененияЗаявки КАК КоличествоДнейДоНачалаОтсутствияДляИзмененияЗаявки
	|ИЗ
	|	РегистрСведений.НастройкиОтсутствийКабинетСотрудника КАК Настройки
	|ГДЕ
	|	&Условие";
	
	Возврат ТекстЗапроса;

КонецФункции

#КонецОбласти

#КонецЕсли