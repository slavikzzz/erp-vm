#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(СтруктурнаяЕдиница)
	|	ИЛИ ЗначениеРазрешено(СтруктурнаяЕдиница.Владелец)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбновитьПодчиненныеСтруктурныеЕдиницы(СтруктурныеЕдиницы, РежимОбновления = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаСравненияНаборовСтруктурныхЕдиниц("ТерриториальныеУсловияПФР", "ТерриториальныеУсловияПФР");
	
	Для Каждого СтрокаСтруктурныеЕдиницы Из СтруктурныеЕдиницы Цикл
		
		Запрос.УстановитьПараметр("ГоловнаяСтруктурнаяЕдиница", СтрокаСтруктурныеЕдиницы.Ключ);
		Запрос.УстановитьПараметр("ПодчиненныеСтруктурныеЕдиницы", СтрокаСтруктурныеЕдиницы.Значение);
		РезультатЗапросов = Запрос.ВыполнитьПакет();
		
		ОсновнойНабор = РезультатЗапросов[РезультатЗапросов.Количество() - 1].Выгрузить();
		Выборка = РезультатЗапросов[РезультатЗапросов.Количество() - 2].Выбрать();
		Пока Выборка.Следующий() Цикл
			НовыйНаборЗаписей = РегистрыСведений.ТерриториальныеУсловияПФР.СоздатьНаборЗаписей();
			НовыйНаборЗаписей.Отбор.СтруктурнаяЕдиница.Установить(Выборка.СтруктурнаяЕдиница);
			ОсновнойНабор.ЗаполнитьЗначения(Выборка.СтруктурнаяЕдиница, "СтруктурнаяЕдиница");
			НовыйНаборЗаписей.Прочитать();
			Если Не ОбщегоНазначения.КоллекцииИдентичны(НовыйНаборЗаписей.Выгрузить(), ОсновнойНабор) Тогда
				НовыйНаборЗаписей.Загрузить(ОсновнойНабор);
				
				НовыйНаборЗаписей.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
				Если РежимОбновления Тогда
					НовыйНаборЗаписей.ОбменДанными.Загрузка = Истина;
				КонецЕсли;
				НовыйНаборЗаписей.Записать();
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Функция ТекстЗапросаСравненияНаборовСтруктурныхЕдиниц(ИмяРегистра, ИмяРеквизита) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НаборРегистраСведений.Период КАК Период,
	|	НаборРегистраСведений.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	НаборРегистраСведений.ТерриториальныеУсловияПФР КАК ТерриториальныеУсловияПФР,
	|	НаборРегистраСведений.ПрименятьСевернуюНадбавку КАК ПрименятьСевернуюНадбавку
	|ПОМЕСТИТЬ ВТНаборПроверки
	|ИЗ
	|	РегистрСведений.ТерриториальныеУсловияПФР КАК НаборРегистраСведений
	|ГДЕ
	|	НаборРегистраСведений.СтруктурнаяЕдиница В(&ПодчиненныеСтруктурныеЕдиницы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НаборРегистраСведений.Период КАК Период,
	|	НаборРегистраСведений.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	НаборРегистраСведений.ТерриториальныеУсловияПФР КАК ТерриториальныеУсловияПФР,
	|	НаборРегистраСведений.ПрименятьСевернуюНадбавку КАК ПрименятьСевернуюНадбавку
	|ПОМЕСТИТЬ ВТОсновнойНабор
	|ИЗ
	|	РегистрСведений.ТерриториальныеУсловияПФР КАК НаборРегистраСведений
	|ГДЕ
	|	НаборРегистраСведений.СтруктурнаяЕдиница = &ГоловнаяСтруктурнаяЕдиница
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОсновнойНабор.Период КАК Период,
	|	Организации.Ссылка КАК СтруктурнаяЕдиница,
	|	ОсновнойНабор.ТерриториальныеУсловияПФР КАК ТерриториальныеУсловияПФР,
	|	ОсновнойНабор.ПрименятьСевернуюНадбавку КАК ПрименятьСевернуюНадбавку
	|ПОМЕСТИТЬ ВТВсеСтруктурныеЕдиницы
	|ИЗ
	|	Справочник.Организации КАК Организации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОсновнойНабор КАК ОсновнойНабор
	|		ПО (ИСТИНА)
	|ГДЕ
	|	Организации.Ссылка В(&ПодчиненныеСтруктурныеЕдиницы)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОсновнойНабор.Период,
	|	ПодразделенияОрганизаций.Ссылка,
	|	ОсновнойНабор.ТерриториальныеУсловияПФР,
	|	ОсновнойНабор.ПрименятьСевернуюНадбавку
	|ИЗ
	|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОсновнойНабор КАК ОсновнойНабор
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ПодразделенияОрганизаций.Ссылка В(&ПодчиненныеСтруктурныеЕдиницы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЕСТЬNULL(НаборПроверки.СтруктурнаяЕдиница, ВсеСтруктурныеЕдиницы.СтруктурнаяЕдиница) КАК СтруктурнаяЕдиница
	|ИЗ
	|	ВТНаборПроверки КАК НаборПроверки
	|		ПОЛНОЕ СОЕДИНЕНИЕ ВТВсеСтруктурныеЕдиницы КАК ВсеСтруктурныеЕдиницы
	|		ПО НаборПроверки.Период = ВсеСтруктурныеЕдиницы.Период
	|			И НаборПроверки.СтруктурнаяЕдиница = ВсеСтруктурныеЕдиницы.СтруктурнаяЕдиница
	|			И НаборПроверки.ТерриториальныеУсловияПФР = ВсеСтруктурныеЕдиницы.ТерриториальныеУсловияПФР
	|			И НаборПроверки.ПрименятьСевернуюНадбавку = ВсеСтруктурныеЕдиницы.ПрименятьСевернуюНадбавку
	|ГДЕ
	|	(НаборПроверки.ТерриториальныеУсловияПФР ЕСТЬ NULL
	|			ИЛИ ВсеСтруктурныеЕдиницы.ТерриториальныеУсловияПФР ЕСТЬ NULL)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОсновнойНабор.Период КАК Период,
	|	ОсновнойНабор.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ОсновнойНабор.ТерриториальныеУсловияПФР КАК ТерриториальныеУсловияПФР,
	|	ОсновнойНабор.ПрименятьСевернуюНадбавку КАК ПрименятьСевернуюНадбавку
	|ИЗ
	|	ВТОсновнойНабор КАК ОсновнойНабор";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ЗаполнитьПрименятьСевернуюНадбавку(ПараметрыОбновления = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Если ПараметрыОбновления = Неопределено Тогда
		МассивОбновленных = Новый Массив;
	Иначе
		
		Если ПараметрыОбновления.Свойство("МассивОбновленных") Тогда
			МассивОбновленных = ПараметрыОбновления.МассивОбновленных;
		Иначе
			МассивОбновленных = Новый Массив;
			ПараметрыОбновления.Вставить("МассивОбновленных", МассивОбновленных);
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МассивОбновленных", МассивОбновленных);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТерриториальныеУсловия.Период КАК Период,
		|	ТерриториальныеУсловия.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	ТерриториальныеУсловия.ТерриториальныеУсловияПФР КАК ТерриториальныеУсловияПФР
		|ИЗ
		|	(ВЫБРАТЬ
		|		ТерриториальныеУсловия.Период КАК Период,
		|		ТерриториальныеУсловия.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|		ТерриториальныеУсловия.ТерриториальныеУсловияПФР КАК ТерриториальныеУсловияПФР
		|	ИЗ
		|		РегистрСведений.ТерриториальныеУсловияПФР КАК ТерриториальныеУсловия
		|	ГДЕ
		|		НЕ ТерриториальныеУсловия.СтруктурнаяЕдиница В (&МассивОбновленных)
		|		И НЕ ТерриториальныеУсловия.ПрименятьСевернуюНадбавку
		|		И ТерриториальныеУсловия.ТерриториальныеУсловияПФР В (ЗНАЧЕНИЕ(Справочник.ТерриториальныеУсловияПФР.МКС), ЗНАЧЕНИЕ(Справочник.ТерриториальныеУсловияПФР.РКСР), ЗНАЧЕНИЕ(Справочник.ТерриториальныеУсловияПФР.МКСРКСР), ЗНАЧЕНИЕ(Справочник.ТерриториальныеУсловияПФР.МКСР), ЗНАЧЕНИЕ(Справочник.ТерриториальныеУсловияПФР.РКС), ЗНАЧЕНИЕ(Справочник.ТерриториальныеУсловияПФР.РКСМ))
		|		И ТИПЗНАЧЕНИЯ(ТерриториальныеУсловия.СтруктурнаяЕдиница) = ТИП(Справочник.Организации)
		|		И ВЫРАЗИТЬ(ТерриториальныеУсловия.СтруктурнаяЕдиница КАК Справочник.Организации).ПрименятьСевернуюНадбавку
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТерриториальныеУсловия.Период,
		|		ТерриториальныеУсловия.СтруктурнаяЕдиница,
		|		ТерриториальныеУсловия.ТерриториальныеУсловияПФР
		|	ИЗ
		|		РегистрСведений.ТерриториальныеУсловияПФР КАК ТерриториальныеУсловия
		|	ГДЕ
		|		НЕ ТерриториальныеУсловия.СтруктурнаяЕдиница В (&МассивОбновленных)
		|		И НЕ ТерриториальныеУсловия.ПрименятьСевернуюНадбавку
		|		И ТерриториальныеУсловия.ТерриториальныеУсловияПФР В (ЗНАЧЕНИЕ(Справочник.ТерриториальныеУсловияПФР.МКС), ЗНАЧЕНИЕ(Справочник.ТерриториальныеУсловияПФР.РКСР), ЗНАЧЕНИЕ(Справочник.ТерриториальныеУсловияПФР.МКСРКСР), ЗНАЧЕНИЕ(Справочник.ТерриториальныеУсловияПФР.МКСР), ЗНАЧЕНИЕ(Справочник.ТерриториальныеУсловияПФР.РКС), ЗНАЧЕНИЕ(Справочник.ТерриториальныеУсловияПФР.РКСМ))
		|		И ТИПЗНАЧЕНИЯ(ТерриториальныеУсловия.СтруктурнаяЕдиница) = ТИП(Справочник.ПодразделенияОрганизаций)
		|		И ВЫРАЗИТЬ(ТерриториальныеУсловия.СтруктурнаяЕдиница КАК Справочник.ПодразделенияОрганизаций).Владелец.ПрименятьСевернуюНадбавку
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТерриториальныеУсловия.Период,
		|		ТерриториальныеУсловия.СтруктурнаяЕдиница,
		|		ТерриториальныеУсловия.ТерриториальныеУсловияПФР
		|	ИЗ
		|		РегистрСведений.ТерриториальныеУсловияПФР КАК ТерриториальныеУсловия
		|	ГДЕ
		|		НЕ ТерриториальныеУсловия.СтруктурнаяЕдиница В (&МассивОбновленных)
		|		И НЕ ТерриториальныеУсловия.ПрименятьСевернуюНадбавку
		|		И ТерриториальныеУсловия.ТерриториальныеУсловияПФР В (ЗНАЧЕНИЕ(Справочник.ТерриториальныеУсловияПФР.ОКУ), ЗНАЧЕНИЕ(Справочник.ТерриториальныеУсловияПФР.ПРОЧ))
		|		И ТИПЗНАЧЕНИЯ(ТерриториальныеУсловия.СтруктурнаяЕдиница) = ТИП(Справочник.Организации)
		|		И ВЫРАЗИТЬ(ТерриториальныеУсловия.СтруктурнаяЕдиница КАК Справочник.Организации).ПрименятьСевернуюНадбавку
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТерриториальныеУсловия.Период,
		|		ТерриториальныеУсловия.СтруктурнаяЕдиница,
		|		ТерриториальныеУсловия.ТерриториальныеУсловияПФР
		|	ИЗ
		|		РегистрСведений.ТерриториальныеУсловияПФР КАК ТерриториальныеУсловия
		|	ГДЕ
		|		НЕ ТерриториальныеУсловия.СтруктурнаяЕдиница В (&МассивОбновленных)
		|		И НЕ ТерриториальныеУсловия.ПрименятьСевернуюНадбавку
		|		И ТерриториальныеУсловия.ТерриториальныеУсловияПФР В (ЗНАЧЕНИЕ(Справочник.ТерриториальныеУсловияПФР.ОКУ), ЗНАЧЕНИЕ(Справочник.ТерриториальныеУсловияПФР.ПРОЧ))
		|		И ТИПЗНАЧЕНИЯ(ТерриториальныеУсловия.СтруктурнаяЕдиница) = ТИП(Справочник.ПодразделенияОрганизаций)
		|		И ВЫРАЗИТЬ(ТерриториальныеУсловия.СтруктурнаяЕдиница КАК Справочник.ПодразделенияОрганизаций).Владелец.ПрименятьСевернуюНадбавку) КАК ТерриториальныеУсловия
		|
		|УПОРЯДОЧИТЬ ПО
		|	СтруктурнаяЕдиница";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 0 Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	
	Пока Выборка.СледующийПоЗначениюПоля("СтруктурнаяЕдиница") Цикл
		
		МассивОбновленных.Добавить(Выборка.СтруктурнаяЕдиница);
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(
			ПараметрыОбновления, "РегистрСведений.ТерриториальныеУсловияПФР", "СтруктурнаяЕдиница", Выборка.СтруктурнаяЕдиница) Тогда
			
			Продолжить;
		КонецЕсли;
		
		Пока Выборка.Следующий() Цикл
			НаборЗаписей = РегистрыСведений.ТерриториальныеУсловияПФР.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Период.Установить(Выборка.Период);
			НаборЗаписей.Отбор.СтруктурнаяЕдиница.Установить(Выборка.СтруктурнаяЕдиница);
			
			Запись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, Выборка);
			Запись.ПрименятьСевернуюНадбавку = Истина;
			
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
		КонецЦикла;
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли