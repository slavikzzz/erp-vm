
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустой() Тогда
		ЗаполнитьСтавкиПоУмолчанию();
	КонецЕсли;
	УправлениеФормой(ЭтаФорма);
	
	НастроитьЭлементыИнформацииОбИзмененииСтавкиНалога();
	
	ВыводитсяПредупреждение = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	ПериодПриИзмененииНаСервере();
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтавкиПоУмолчанию()
	
	Если ЗначениеЗаполнено(Запись.Период) Тогда
		Период = Запись.Период;
	Иначе
		Период = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Запись.СтавкаФБ = РегистрыСведений.СтавкиНалогаНаПрибыльДляВсехОрганизаций.ЗначениеПоУмолчаниюФедеральныйБюджет(Период);
	Запись.СтавкаСубъектРФ = РегистрыСведений.СтавкиНалогаНаПрибыльДляВсехОрганизаций.ЗначениеПоУмолчаниюРегиональныйБюджет(Период);
	
КонецПроцедуры

&НаКлиенте
Процедура СтавкаФБПриИзменении(Элемент)
	
	НастроитьЭлементыИнформацииОбИзмененииСтавкиНалога(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура СтавкаСубъектРФПриИзменении(Элемент)

	НастроитьЭлементыИнформацииОбИзмененииСтавкиНалога(Ложь);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастроитьЭлементыИнформацииОбИзмененииСтавкиНалога(ИзмененПериод = Истина)
		
	ИнформацияВыводится = ОбщегоНазначенияКлиентСервер.ЗначениеСвойстваЭлементаФормы(Элементы, "ГруппаИнформация", "Видимость");
	
	Если ИнформацияВыводится И НЕ ИзмененПериод Тогда
		Возврат;	
	КонецЕсли;

	ВыводитьИнформациюОбИзмененииСтавкиНалога = ВыводитьИнформациюОбИзмененииСтавкиНалога();
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, 
		"ГруппаИнформация", "Видимость", ВыводитьИнформациюОбИзмененииСтавкиНалога);
	
	Если ВыводитьИнформациюОбИзмененииСтавкиНалога Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Обратите внимание, есть документы расчета налога на прибыль после %1';
										|en = 'Обратите внимание, есть документы расчета налога на прибыль после %1'", 
			ОбщегоНазначения.КодОсновногоЯзыка()), Формат(Запись.Период, "ДФ=dd.MM.yyyy;")); 
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, 
			"ИнформацияОбИспользованииСтавкиНалога", "Заголовок", ТекстСообщения);
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Функция ВыводитьИнформациюОбИзмененииСтавкиНалога()

	ВыводитьИнформациюОбИзмененииСтавкиНалога = Ложь;
	
	Если Модифицированность ИЛИ Параметры.Ключ.Пустой() Тогда
		ВыводитьИнформациюОбИзмененииСтавкиНалога = НалоговыйУчет.ВыполненРасчетНалогаПослеИзмененияСтавки(Запись.Период);		
	КонецЕсли;		
	
	Возврат ВыводитьИнформациюОбИзмененииСтавкиНалога;
	
КонецФункции

&НаСервере
Процедура ПериодПриИзмененииНаСервере()

	ЗаполнитьСтавкиПоУмолчанию();
	НастроитьЭлементыИнформацииОбИзмененииСтавкиНалога();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УправлениеФормой(Форма)

	Форма.ПрименятьОднуСтавку = НЕ ПолучитьФункциональнуюОпцию("ПрименяютсяРазныеСтавкиНалогаНаПрибыль");
	Форма.Элементы.СтавкаСубъектРФ.Видимость = Форма.ПрименятьОднуСтавку;

КонецПроцедуры

#КонецОбласти