#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// Отражает подтверждение ставки 0% в регистрах учета НДС
//
// Параметры:
// 	ДокументРеализации - ДокументСсылка - Документ продажи на экспорт
// 	ДатаПодтверждения - Дата - Дата подтверждения экспорта
// 	Организация - СправочникСсылка.Организации - Организация, в которой подтверждается экспорт 
//
Процедура ОтразитьДокументПоПодтверждениюСтавки0(ДокументРеализации, ДатаПодтверждения, Организация, ФормироватьПроводки) Экспорт
	
	Движения = Новый Структура;
	
	НДСЗаписиКнигиПродаж = РегистрыНакопления.НДСЗаписиКнигиПродаж.СоздатьНаборЗаписей();
	НДСЗаписиКнигиПродаж.Отбор.Регистратор.Установить(ДокументРеализации);
	Движения.Вставить("НДСЗаписиКнигиПродаж", НДСЗаписиКнигиПродаж);
	
	//++ НЕ УТ
	Если ФормироватьПроводки Тогда
		ОтражениеДокументовВРеглУчете = РегистрыСведений.ОтражениеДокументовВРеглУчете.СоздатьНаборЗаписей();
		ОтражениеДокументовВРеглУчете.Отбор.Регистратор.Установить(ДокументРеализации);
		Движения.Вставить("ОтражениеДокументовВРеглУчете", ОтражениеДокументовВРеглУчете);
		
		Хозрасчетный = РегистрыБухгалтерии.Хозрасчетный.СоздатьНаборЗаписей();
		Хозрасчетный.Отбор.Регистратор.Установить(ДокументРеализации);
		Движения.Вставить("Хозрасчетный", Хозрасчетный);
	КонецЕсли;
	//-- НЕ УТ
	
	//++ НЕ УТКА
	Если ФормироватьПроводки Тогда
		ОтражениеДокументовВМеждународномУчете = РегистрыСведений.ОтражениеДокументовВМеждународномУчете.СоздатьНаборЗаписей();
		ОтражениеДокументовВМеждународномУчете.Отбор.Регистратор.Установить(ДокументРеализации);
		Движения.Вставить("ОтражениеДокументовВМеждународномУчете", ОтражениеДокументовВМеждународномУчете);
		
		Международный = РегистрыБухгалтерии.Международный.СоздатьНаборЗаписей();
		Международный.Отбор.Регистратор.Установить(ДокументРеализации);
		Движения.Вставить("Международный", Международный);
	КонецЕсли;
	//-- НЕ УТКА
	
	Регистры = Новый Структура;
	Регистры.Вставить("НДСЗаписиКнигиПродаж");
	//++ НЕ УТ
	Если ФормироватьПроводки Тогда
		Регистры.Вставить("ОтражениеДокументовВРеглУчете");
	КонецЕсли;
	//-- НЕ УТ

	//++ НЕ УТКА
	Если ФормироватьПроводки Тогда
		Регистры.Вставить("ОтражениеДокументовВМеждународномУчете");
	КонецЕсли;
	//-- НЕ УТКА
	
	ТаблицыДляДвижений = ПроведениеДокументов.ДанныеДокументаДляПроведения(ДокументРеализации, Регистры);
	
	УчетНДСУП.ОтразитьДвижения(ТаблицыДляДвижений, Движения, Ложь);
	//++ НЕ УТ
	Если ФормироватьПроводки Тогда
		ТаблицыДляДвижений.Вставить("ТаблицаВыборочнойРегистрацииКОтражению",
			РеглУчетПроведениеСервер.ТаблицаВыборочнойРегистрацииКОтражению());
		РеглУчетПроведениеСервер.ДобавитьПараметрыВыборочнойРегистрацииКОтражениюВРеглУчете(
			ТаблицыДляДвижений.ТаблицаВыборочнойРегистрацииКОтражению,
			Организация,
			ДатаПодтверждения);
			
		РеглУчетПроведениеСервер.ОтразитьДвижения(ТаблицыДляДвижений, Движения, Ложь);
	КонецЕсли;
	//-- НЕ УТ
	
	//++ НЕ УТКА
	Если ФормироватьПроводки Тогда
		МеждународныйУчетПроведениеСервер.ОтразитьДвижения(ТаблицыДляДвижений, Движения, Ложь);
	КонецЕсли;
	//-- НЕ УТКА
	
	Для Каждого Движение Из Движения Цикл
		Движение.Значение.Записать();
	КонецЦикла;
	
	//++ НЕ УТКА
	Если ФормироватьПроводки Тогда
		Документ = Новый Структура("Ссылка, Движения", ДокументРеализации, Движения);
		МеждународныйУчетПроведениеСервер.ЗарегистрироватьКОтражению(Документ, Новый Структура, Движения, Ложь);
	КонецЕсли;
	//-- НЕ УТКА
	
КонецПроцедуры


#КонецОбласти

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецЕсли
