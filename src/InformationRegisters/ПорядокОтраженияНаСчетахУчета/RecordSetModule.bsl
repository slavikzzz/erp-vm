#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого Стр Из ЭтотОбъект Цикл
		Если Стр.МестоУчета <> Неопределено И Стр.МестоУчета.Пустая() Тогда
			Стр.МестоУчета = Неопределено;
		КонецЕсли;
	КонецЦикла;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если Отказ ИЛИ ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПорядокОтражения.ВидСчета,
	|	ПорядокОтражения.Организация,
	|	ПорядокОтражения.АналитикаУчета,
	|	ПорядокОтражения.МестоУчета
	|ПОМЕСТИТЬ ДанныеЗаполненныхСчетовРегистра
	|ИЗ
	|	&ТаблицаНовыхДанных КАК ПорядокОтражения
	|ГДЕ
	|	ПорядокОтражения.СчетУчета <> ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)";
	Запрос.УстановитьПараметр("ТаблицаНовыхДанных", ЭтотОбъект.Выгрузить());
	Запрос.Выполнить();
	РегистрыСведений.СчетаРеглУчетаТребующиеНастройки.ОчиститьПриЗаписиРегистра(Запрос.МенеджерВременныхТаблиц);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ВидыСчетовТребующиеУказанияСтатейДоходов = Новый Массив;
	ВидыСчетовТребующиеУказанияСтатейДоходов.Добавить(Перечисления.ВидыСчетовРеглУчета.ВыручкаОтПродаж);
	ВидыСчетовТребующиеУказанияСтатейДоходов.Добавить(Перечисления.ВидыСчетовРеглУчета.СебестоимостьПродаж);
	ВидыСчетовТребующиеУказанияСтатейДоходов.Добавить(Перечисления.ВидыСчетовРеглУчета.НДСПриПродаже);
	ВидыСчетовТребующиеУказанияСтатейДоходов.Добавить(Перечисления.ВидыСчетовРеглУчета.НДСПриОтгрузкеБезПереходаПраваСобственности);
	ВидыСчетовТребующиеУказанияСтатейДоходов.Добавить(Перечисления.ВидыСчетовРеглУчета.ЗатратыНаПриобретениеТМЦ);
	ВидыСчетовТребующиеУказанияСтатейДоходов.Добавить(Перечисления.ВидыСчетовРеглУчета.ВыручкаОтПродажЕНВД);
	ВидыСчетовТребующиеУказанияСтатейДоходов.Добавить(Перечисления.ВидыСчетовРеглУчета.СебестоимостьПродажЕНВД);
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		Если ВидыСчетовТребующиеУказанияСтатейДоходов.Найти(Запись.ВидСчета) <> Неопределено Тогда
			ЭтоСчетПрочиеДоходы = БухгалтерскийУчетВызовСервераПовтИсп.ЭтоСчетПрочиеДоходы(Запись.СчетУчета);
			Если ЭтоСчетПрочиеДоходы И Не ЗначениеЗаполнено(Запись.Субконто) Тогда
				Отказ = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	Если ДанныеЗаполнения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Организация = Неопределено;
	ДанныеЗаполнения.Свойство("Организация", Организация);
	Если ТипЗнч(Организация) = Тип("СправочникСсылка.Организации") И Не ЗначениеЗаполнено(Организация) Тогда
		Организация = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#КонецЕсли