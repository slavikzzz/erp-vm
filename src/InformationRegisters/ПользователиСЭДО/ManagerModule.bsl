#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

#Область ОбновлениеИнформационнойБазы

// Индексация ответственных СЭДО СФР по данным документов.
//
// Параметры:
//   ПараметрыОбновления - Структура - Параметры отложенного обновления.
//
Процедура ЗаполнитьПоПрикладнымДокументам(ПараметрыОбновления = Неопределено) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Ответственный КАК Ответственный
	|ПОМЕСТИТЬ ВТОтветственные
	|ИЗ
	|	Документ.ИзвещениеФСС КАК Таблица
	|ГДЕ
	|	Таблица.ПометкаУдаления = ЛОЖЬ
	|	И Таблица.Ответственный <> ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Таблица.Ответственный
	|ИЗ
	|	Документ.ВходящийЗапросФССДляРасчетаПособия КАК Таблица
	|ГДЕ
	|	Таблица.ПометкаУдаления = ЛОЖЬ
	|	И Таблица.Ответственный <> ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Таблица.Ответственный
	|ИЗ
	|	Документ.ОтветНаЗапросФССДляРасчетаПособия КАК Таблица
	|ГДЕ
	|	Таблица.ПометкаУдаления = ЛОЖЬ
	|	И Таблица.Ответственный <> ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Таблица.Ответственный
	|ИЗ
	|	Документ.ЗапросСреднегоЗаработкаСЭДО КАК Таблица
	|ГДЕ
	|	Таблица.ПометкаУдаления = ЛОЖЬ
	|	И Таблица.Ответственный <> ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Таблица.Ответственный
	|ИЗ
	|	Документ.СведенияОЗастрахованномЛицеФСС КАК Таблица
	|ГДЕ
	|	Таблица.ПометкаУдаления = ЛОЖЬ
	|	И Таблица.Ответственный <> ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Таблица.Ответственный
	|ИЗ
	|	Документ.УведомлениеОПрекращенииОтпускаПоУходуЗаРебенком КАК Таблица
	|ГДЕ
	|	Таблица.ПометкаУдаления = ЛОЖЬ
	|	И Таблица.Ответственный <> ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТОтветственные.Ответственный КАК Ответственный
	|ИЗ
	|	ВТОтветственные КАК ВТОтветственные
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПользователиСЭДО КАК ПользователиСЭДО
	|		ПО ВТОтветственные.Ответственный = ПользователиСЭДО.Пользователь
	|ГДЕ
	|	ПользователиСЭДО.Пользователь ЕСТЬ NULL";
	МассивПользователей = КоллекцииБЗК.УникальныеЗначенияКолонки(Запрос.Выполнить().Выгрузить(), "Ответственный");
	Для Каждого Пользователь Из МассивПользователей Цикл
		Если ТипЗнч(Пользователь) <> Тип("СправочникСсылка.Пользователи")
			Или Пользователь.Пустая() Тогда
			Продолжить;
		КонецЕсли;
		МенеджерЗаписи = СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Пользователь = Пользователь;
		МенеджерЗаписи.Прочитать();
		Если Не МенеджерЗаписи.Выбран() Тогда
			МенеджерЗаписи.Пользователь = Пользователь;
			МенеджерЗаписи.Записать(Ложь);
		КонецЕсли;
	КонецЦикла;
	Если ПараметрыОбновления <> Неопределено Тогда
		ПараметрыОбновления.ОбработкаЗавершена = Истина;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьТекущегоПользователя() Экспорт
	УстановитьПривилегированныйРежим(Истина);
	Пользователь = Пользователи.ТекущийПользователь();
	Если Не ЗначениеЗаполнено(Пользователь) Тогда
		Возврат;
	КонецЕсли;
	НастройкиПользователя = НастройкиПользователя(Пользователь);
	Если Не НастройкиПользователя.ЕстьЗапись Тогда
		МенеджерЗаписи = СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Пользователь = Пользователь;
		МенеджерЗаписи.Записать(Ложь);
	КонецЕсли;
КонецПроцедуры

Функция НастройкиПользователя(Пользователь)
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПользователиСЭДО.ТекущиеДелаПоСЭДОДатаНачала КАК ТекущиеДелаПоСЭДОДатаНачала
	|ИЗ
	|	РегистрСведений.ПользователиСЭДО КАК ПользователиСЭДО
	|ГДЕ
	|	ПользователиСЭДО.Пользователь = &Пользователь";
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	Таблица = Запрос.Выполнить().Выгрузить();
	
	Результат = Новый Структура("ЕстьЗапись, ТекущиеДелаПоСЭДОДатаНачала");
	Если Таблица.Количество() = 1 Тогда
		Результат.ЕстьЗапись = Истина;
		Результат.ТекущиеДелаПоСЭДОДатаНачала = Таблица[0].ТекущиеДелаПоСЭДОДатаНачала;
	Иначе
		Результат.ЕстьЗапись = Ложь;
		Результат.ТекущиеДелаПоСЭДОДатаНачала = '39991231';
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

Процедура ОбновитьЗначенияРесурсов(ЗначенияРесурсов, Пользователь = Неопределено) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	Если Пользователь = Неопределено Тогда
		Пользователь = Пользователи.ТекущийПользователь();
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Пользователь) Тогда
		Возврат;
	КонецЕсли;
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Пользователь = Пользователь;
	МенеджерЗаписи.Прочитать();
	МенеджерЗаписи.Пользователь = Пользователь;
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ЗначенияРесурсов);
	МенеджерЗаписи.Записать(Истина);
КонецПроцедуры

Функция ПоказыватьТекущиеДелаПоСЭДО() Экспорт
	Пользователь = Пользователи.ТекущийПользователь();
	Если Не ЗначениеЗаполнено(Пользователь) Тогда
		Возврат Ложь;
	КонецЕсли;
	УстановитьПривилегированныйРежим(Истина);
	Возврат НастройкиПользователя(Пользователь).ТекущиеДелаПоСЭДОДатаНачала <= ТекущаяДатаСеанса();
КонецФункции

#КонецОбласти

#КонецЕсли