#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(ФизическоеЛицо)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ОбработчикиПравилРегистрации

// См. ЗарплатаКадрыРасширенныйСинхронизацияДанных.ШаблонОбработчика
Процедура ПриЗаполненииНастроекОбработчиковПравилРегистрации(Настройки) Экспорт
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыРасширеннаяПодсистемы") Тогда
		МодульЗарплатаКадрыРасширенныйСинхронизацияДанных = ОбщегоНазначения.ОбщийМодуль("ЗарплатаКадрыРасширенныйСинхронизацияДанных");
		МодульЗарплатаКадрыРасширенныйСинхронизацияДанных.ДляНезависимогоРегистра(Настройки, "ФизическоеЛицо", "ОбщиеДанные");
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПредставлениеПрофессийФизическогоЛица(ФизическоеЛицоСсылка) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПрофессииФизическихЛиц.Профессия
		|ИЗ
		|	РегистрСведений.ПрофессииФизическихЛиц КАК ПрофессииФизическихЛиц
		|ГДЕ
		|	ПрофессииФизическихЛиц.ФизическоеЛицо = &ФизическоеЛицоСсылка";
		
	Запрос.УстановитьПараметр("ФизическоеЛицоСсылка", ФизическоеЛицоСсылка);
	
	Возврат ПредставлениеПрофессийПоКоллекцииЗаписей(Запрос.Выполнить().Выгрузить());
	
КонецФункции

Функция ПредставлениеПрофессийПоКоллекцииЗаписей(НаборЗаписей) Экспорт
	
	ПредставлениеПрофессии = "";
	
	Для Каждого СтрокаПрофессии Из НаборЗаписей Цикл
		ПредставлениеПрофессии 	= 
			ПредставлениеПрофессии + ?(ПустаяСтрока(ПредставлениеПрофессии), "", ", ") 
			+ ?(ЗначениеЗаполнено(СтрокаПрофессии.Профессия), Строка(СтрокаПрофессии.Профессия), "");
	КонецЦикла;
	
	Если ПустаяСтрока(ПредставлениеПрофессии) Тогда
		ПредставлениеПрофессии = НСтр("ru = 'Не владеет профессиями';
										|en = 'Does not have any profession'");
	КонецЕсли; 
	
	Возврат ПредставлениеПрофессии;
	
КонецФункции

#КонецОбласти

#КонецЕсли