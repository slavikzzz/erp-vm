
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	Параметры.Свойство("Сценарий", Сценарий);
	Параметры.Свойство("Валюта",   Валюта);
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(ПрогнозныеКурсы, "Сценарий", Сценарий);
		
	Если ЗначениеЗаполнено(Валюта) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			ПрогнозныеКурсы.Отбор, "Валюта", Валюта, ВидСравненияКомпоновкиДанных.Равно, "Валюта", Истина);
	КонецЕсли;
	
	Элемент = Элементы.ПрогнозныеКурсыЗагрузить;
	Элемент.Видимость = (ПравоДоступа("Редактирование", Метаданные.РегистрыСведений.ПрогнозныеКурсыСценариев));
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗагруженыКурсыСценария" Тогда
		Элементы.ПрогнозныеКурсы.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВалютаПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Валюта) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ПрогнозныеКурсы.Отбор, "Валюта", Валюта);
	Иначе
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(ПрогнозныеКурсы.Отбор, "Валюта");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПрогнозныекурсы

&НаКлиенте
Процедура ПрогнозныеКурсыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
	Если Копирование Тогда
		
		ТекущиеДанные = Элемент.ТекущиеДанные;
		ЗначенияЗаполнения = Новый Структура;
		Если ТекущиеДанные <> Неопределено Тогда
			ЗначенияЗаполнения.Вставить("Сценарий", Сценарий);
			ЗначенияЗаполнения.Вставить("Валюта", ТекущиеДанные.Валюта);
			ЗначенияЗаполнения.Вставить("КурсЧислитель", ТекущиеДанные.КурсЧислитель);
			ЗначенияЗаполнения.Вставить("КурсЗнаменатель", ТекущиеДанные.КурсЗнаменатель);
		КонецЕсли;
		
	Иначе
		
		ЗначенияЗаполнения = Новый Структура;
		ЗначенияЗаполнения.Вставить("Сценарий", Сценарий);
		ЗначенияЗаполнения.Вставить("Валюта",   Валюта);
	
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму("РегистрСведений.ПрогнозныеКурсыСценариев.Форма.ФормаЗаписиДляЗаданногоСценария", 
		ПараметрыФормы,
		ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрогнозныеКурсыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФормуЗаписи();
	
КонецПроцедуры

&НаКлиенте
Процедура ПрогнозныеКурсыПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	ОткрытьФормуЗаписи();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьКурсы(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Сценарий", Сценарий);
	ПараметрыФормы.Вставить("Валюта",   Валюта);
	
	ОткрытьФорму("РегистрСведений.ПрогнозныеКурсыСценариев.Форма.ФормаЗагрузки", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

&НаКлиенте
Процедура ОткрытьФормуЗаписи()
	
	ПараметрыФормы = Новый Структура("Ключ", Элементы.ПрогнозныеКурсы.ТекущаяСтрока);
	
	ОткрытьФорму("РегистрСведений.ПрогнозныеКурсыСценариев.Форма.ФормаЗаписиДляЗаданногоСценария",
		ПараметрыФормы,
		ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
