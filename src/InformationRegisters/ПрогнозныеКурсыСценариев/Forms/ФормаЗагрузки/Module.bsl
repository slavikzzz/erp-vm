
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	Сценарий = Параметры.Сценарий;
	Валюта = Параметры.Валюта;
	
	ИнициализироватьПараметрыЗагрузки();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьКурсы(Команда)
	
	ОчиститьСообщения();
	
	Если Не ЗначениеЗаполнено(Валюта) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Укажите валюту для загрузки курсов';
				|en = 'Specify a currency for importing exchange rates'"), , "Валюта");
		Возврат;
	КонецЕсли;
	
	Если ЗагрузитьКурсыСервер() Тогда
		Закрыть();
	КонецЕсли;
	
	Оповестить("ЗагруженыКурсыСценария");
	
	Текст = НСтр("ru = 'Загрузка курсов выполнена';
				|en = 'Rates are imported'");
	ПоказатьОповещениеПользователя(Текст, , , БиблиотекаКартинок.Информация32);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

&НаСервере
Процедура СообщитьОбОшибкеПреобразования(НомерСтроки, ИмяКолонки, СтроковоеЗначение)	
	
	ШаблонТекста = НСтр("ru = 'Строка №%1 колонка ""%2"" - ошибка преобразования значения ""%3""
						|Строка не загружена!';
						|en = 'Conversion error for value ""%3""
						| in column ""%2"" of line %1. The line is not imported.'");
	Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста, НомерСтроки, ИмяКолонки, СтроковоеЗначение);
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст);
	
КонецПроцедуры

&НаСервере
Функция ТекстВДату(Знач СтроковоеЗначение, НомерСтроки, ИмяКолонки)
	
	Результат = Неопределено;
	
	ЛокальнаяДата = Формат(Дата('29991211'), "ДЛФ=D");
	ДлинаСтроки = СтрДлина(ЛокальнаяДата);
	
	Сч = 1;
	Пока Сч <= ДлинаСтроки Цикл
		ТекСимвол = Сред(ЛокальнаяДата, Сч, 1);
		Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ТекСимвол) Тогда
			Сч = Сч + 1;
		Иначе
			РазделительДробнойЧасти = ТекСимвол;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Попытка
		Цифры = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ЛокальнаяДата, РазделительДробнойЧасти);
		ИндексЧисло = Цифры.Найти("11");
		ИндексМесяц = Цифры.Найти("12");
		ИндексГод = Цифры.Найти("2999");
		
		Цифры = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтроковоеЗначение, РазделительДробнойЧасти);
		Результат = ?(СтрДлина(Цифры[ИндексГод]) = 2, "20" + Цифры[2], Цифры[2]) 
					+ Формат(Число(Цифры[ИндексМесяц]), "ЧЦ=2; ЧВН=;") 
					+ Формат(Число(Цифры[ИндексЧисло]), "ЧЦ=2; ЧВН=;");
		Результат = Дата(Результат);
	Исключение
		СообщитьОбОшибкеПреобразования(НомерСтроки, ИмяКолонки, СтроковоеЗначение);
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ТекстВЧисло(СтроковоеЗначение, НомерСтроки, ИмяКолонки)
	
	Если ПустаяСтрока(СтроковоеЗначение) ИЛИ СтроковоеЗначение = Неопределено Тогда
		Возврат 0;
	КонецЕсли;
	
	Результат = 0;
	
	Попытка
		Результат = Число(СтрЗаменить(СтроковоеЗначение," ",""));
	Исключение
		СообщитьОбОшибкеПреобразования(НомерСтроки, ИмяКолонки, СтроковоеЗначение);
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ЗагрузитьКурсыСервер()
	
	НомерСтроки = 2;
	СтроковыйНомер = Формат(НомерСтроки, "ЧН=0; ЧГ=0");
	ПоляЗагрузки = ПараметрыЗагрузки.Поля;
	ИменаПолейНаЯзыках = ПараметрыЗагрузки.ИменаПолейНаЯзыках;
	ЗаполненаДата = ЗначениеЗаполнено(ТабличныйДокумент.Область("R" + СтроковыйНомер + ПоляЗагрузки.Период).Текст);
	ЗагруженоБезОшибок = Истина;
	
	Пока ЗаполненаДата Цикл
		
		ДанныеСтроки = Неопределено;
		
		Попытка
			
			ДанныеСтроки = Новый Структура("Сценарий, Валюта", Сценарий, Валюта);
			
			Для Каждого Поле Из ПоляЗагрузки Цикл
				
				ТекстЗначения = ТабличныйДокумент.Область("R" + СтроковыйНомер + Поле.Значение).Текст;
				Если Поле.Ключ = "Период" Тогда
					ЗначениеПоля = ТекстВДату(ТекстЗначения, СтроковыйНомер, ИменаПолейНаЯзыках[Поле.Ключ]);
				Иначе
					ЗначениеПоля = ТекстВЧисло(ТекстЗначения, СтроковыйНомер, ИменаПолейНаЯзыках[Поле.Ключ]);
				КонецЕсли;
				ДанныеСтроки.Вставить(Поле.Ключ,ЗначениеПоля);
				
			КонецЦикла;
			
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗагруженоБезОшибок = Ложь;
		КонецПопытки;
		
		Если ЗначениеЗаполнено(ДанныеСтроки) Тогда
			
			// проверим все что прочитали
			ВсеПоляЗаполненыКорректно = Истина;
			
			Если Не ЗначениеЗаполнено(ДанныеСтроки.Период) Тогда
				ВсеПоляЗаполненыКорректно = Ложь;
				СообщениеОбОшибке = НСтр("ru = 'В строке %1 не заполнен период. Строка не загружена.';
										|en = 'Period in line %1 is not filled in. The line is not imported.'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(СообщениеОбОшибке, СтроковыйНомер));
			КонецЕсли;
			
			Для Каждого Поле Из ПараметрыЗагрузки.ПоляКонтроля Цикл
				
				СообщениеОбОшибке = "";
				
				Если ДанныеСтроки[Поле] < 0 Тогда
					СообщениеОбОшибке = НСтр("ru = 'В строке %1 в колонке %2 отрицательное значение. Строка не загружена.';
											|en = 'There is a negative value in line %1 of column %2. The line is not imported.'");
				ИначеЕсли ДанныеСтроки[Поле] = 0 Тогда
					СообщениеОбОшибке = НСтр("ru = 'В строке %1 в колонке %2 значение не заполнено. Строка не загружена.';
											|en = 'Column %2 in line %1 is not filled in. The line is not imported.'");
				КонецЕсли;
				
				Если ЗначениеЗаполнено(СообщениеОбОшибке) Тогда
					ВсеПоляЗаполненыКорректно = Ложь;
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
						СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(СообщениеОбОшибке, СтроковыйНомер, ИменаПолейНаЯзыках[Поле]));
					ЗагруженоБезОшибок = Ложь;
				КонецЕсли;
				
			КонецЦикла;
			
			Если ВсеПоляЗаполненыКорректно Тогда
				Запись = РегистрыСведений.ПрогнозныеКурсыСценариев.СоздатьМенеджерЗаписи();
				ЗаполнитьЗначенияСвойств(Запись, ДанныеСтроки);
				Запись.Записать(Истина);
			КонецЕсли;
			
		КонецЕсли;
		
		НомерСтроки = НомерСтроки + 1;
		СтроковыйНомер = Формат(НомерСтроки, "ЧН=0; ЧГ=0");
		
		Попытка
			ЗаполненаДата = ЗначениеЗаполнено(ТабличныйДокумент.Область("R" + СтроковыйНомер + ПоляЗагрузки.Период).Текст);
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗагруженоБезОшибок = Ложь;
		КонецПопытки;
		
	КонецЦикла;
	
	Возврат ЗагруженоБезОшибок;
	
КонецФункции

&НаСервере
Процедура ИнициализироватьПараметрыЗагрузки()
	
	ПараметрыЗагрузки = Новый Структура;
	
	ПоляЗагрузки = Новый Структура();
	ПоляЗагрузки.Вставить("Период", "C1");
	ПоляЗагрузки.Вставить("КурсЧислитель", "C2");
	ПоляЗагрузки.Вставить("КурсЗнаменатель","C3");
	
	ПараметрыЗагрузки.Вставить("Поля", ПоляЗагрузки);
	
	МассивПолей = Новый Массив;
	МассивПолей.Добавить("КурсЧислитель");
	МассивПолей.Добавить("КурсЗнаменатель");
	
	ПараметрыЗагрузки.Вставить("ПоляКонтроля", МассивПолей);
	
	ИменаПолейНаЯзыках = Новый Структура();
	ИменаПолейНаЯзыках.Вставить("Период", НСтр("ru = 'Период';
												|en = 'Period'"));
	ИменаПолейНаЯзыках.Вставить("КурсЧислитель", НСтр("ru = 'Курс числитель';
														|en = 'Rate numerator'"));
	ИменаПолейНаЯзыках.Вставить("КурсЗнаменатель", НСтр("ru = 'Курс знаменатель';
														|en = 'Rate denominator'"));
	
	ПараметрыЗагрузки.Вставить("ИменаПолейНаЯзыках", ИменаПолейНаЯзыках);
	
	МакетШаблона = РегистрыСведений.ПрогнозныеКурсыСценариев.ПолучитьМакет("МакетЗагрузкиКурсов");
	ТабличныйДокумент.Очистить();
	ОбластьКолонки = МакетШаблона.ПолучитьОбласть("КурсыВалюты");
	ТабличныйДокумент.Присоединить(ОбластьКолонки);
	ТабличныйДокумент.ФиксацияСверху = 1;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
