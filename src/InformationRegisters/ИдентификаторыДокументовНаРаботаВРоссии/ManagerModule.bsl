#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Функция ИдентификаторДокумента(ПрисоединенныйФайл) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПрисоединенныйФайл", ПрисоединенныйФайл);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаРегистра.Идентификатор КАК Идентификатор
		|ИЗ
		|	РегистрСведений.ИдентификаторыДокументовНаРаботаВРоссии КАК ТаблицаРегистра
		|ГДЕ
		|	ТаблицаРегистра.ПрисоединенныйФайл = &ПрисоединенныйФайл";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Идентификатор;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция ДокументПоИдентификатору(Идентификатор) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИдентификаторПРР", Идентификатор);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ИдентификаторыДокументовНаРаботаВРоссии.ПрисоединенныйФайл КАК ПрисоединенныйФайл
		|ИЗ
		|	РегистрСведений.ИдентификаторыДокументовНаРаботаВРоссии КАК ИдентификаторыДокументовНаРаботаВРоссии
		|ГДЕ
		|	ИдентификаторыДокументовНаРаботаВРоссии.Идентификатор = &ИдентификаторПРР";
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	Возврат Выборка.ПрисоединенныйФайл;
	
КонецФункции

Процедура ЗапомнитьИдентификатор(ПрисоединенныйФайл, Идентификатор) Экспорт
	
	Запись = РегистрыСведений.ИдентификаторыДокументовНаРаботаВРоссии.СоздатьМенеджерЗаписи();
	Запись.ПрисоединенныйФайл = ПрисоединенныйФайл;
	Запись.Идентификатор = Идентификатор;
	Запись.ДатаОтправки = ТекущаяУниверсальнаяДата();
	Запись.ТребуетсяПодпись = Истина;
	Запись.Записать();
	
КонецПроцедуры

Процедура ЗапомнитьПолучениеПодписиСотрудика(Идентификатор, ДатаПодписи = Неопределено, Документ = Неопределено) Экспорт
	
	Если ДатаПодписи = Неопределено Тогда
		ДатаПодписи = Дата(1, 1, 1);
	КонецЕсли;
	
	Если Документ = Неопределено Тогда
		Документ = ДокументПоИдентификатору(Идентификатор);
	КонецЕсли;
	
	Если Документ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Запись = РегистрыСведений.ИдентификаторыДокументовНаРаботаВРоссии.СоздатьМенеджерЗаписи();
	Запись.ПрисоединенныйФайл = Документ;
	Запись.Прочитать();
	
	Запись.ПодписьСотрудникаПолучена = ДатаПодписи;
	Запись.ТребуетсяПодпись = Ложь;
	Запись.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли