&НаКлиенте
Перем КонтекстЭДО;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОрганизацияСсылка = Параметры.ОрганизацияСсылка;
	
	ИспользоватьНесколько = 1;
	
	ИспользуетсяОднаОрганизация = РегламентированнаяОтчетностьВызовСервера.ИспользуетсяОднаОрганизация();
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	УчетнаяЗаписьСсылка = КонтекстЭДОСервер.УчетнаяЗаписьОрганизации(ОрганизацияСсылка);
	
	Если ЗначениеЗаполнено(УчетнаяЗаписьСсылка) Тогда
		МестоХраненияКлюча = КриптографияЭДКОКлиентСервер.КонтекстМоделиХраненияКлюча(ОрганизацияСсылка);
	Иначе
		МестоХраненияКлюча = КриптографияЭДКОКлиентСервер.КонтекстМоделиХраненияКлюча(Ложь);
	КонецЕсли;
	
	ЭтоПолноправныйПользователь =
		ОбщегоНазначения.РазделениеВключено() И КриптографияЭДКОКлиентСервер.ЭтоЛокальнаяПодпись(МестоХраненияКлюча)
		ИЛИ НЕ ОбщегоНазначения.РазделениеВключено() И Пользователи.ЭтоПолноправныйПользователь();
	
	Элементы.ИспользоватьНесколько.Доступность = ЭтоПолноправныйПользователь
		ИЛИ Параметры.УстановленоИспользоватьНесколько;
	
	ЕстьРеквизитНаименованиеСокращенное = Метаданные.Справочники.Организации.Реквизиты.Найти(
		"НаименованиеСокращенное") <> Неопределено;
	НаименованиеОрганизации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОрганизацияСсылка,
		?(ЕстьРеквизитНаименованиеСокращенное, "НаименованиеСокращенное", "Наименование"));
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.ДобавитьОрганизациюВЗаголовок(
		ЭтотОбъект.Заголовок,
		ИспользуетсяОднаОрганизация,
		НаименованиеОрганизации,
		НСтр("ru = 'Настройки отправки отчетности в Росприроднадзор по пользователям';
			|en = 'Настройки отправки отчетности в Росприроднадзор по пользователям'"));
	
	ЗаполнитьНастройкиОбменаПользователей();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииЗавершение", ЭтотОбъект);
	ДокументооборотСКОКлиент.ПолучитьКонтекстЭДО(ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СохранитьИЗакрыть(Команда)
	
	СохранитьНаСервере();
	Закрыть(КодВозвратаДиалога.ОК);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСертификат(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.Пользователь = ПользователиКлиент.ТекущийПользователь() Тогда
		ТекстСообщения =
			НСтр("ru = 'Для текущего пользователя следует выбрать сертификат из хранилища сертификатов, двойным нажатием мыши по записи';
				|en = 'Для текущего пользователя следует выбрать сертификат из хранилища сертификатов, двойным нажатием мыши по записи'");
		ПоказатьПредупреждение(, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	#Если ВебКлиент Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузитьСертификатПослеПомещенияФайла", ЭтотОбъект);
		НачатьПомещениеФайла(ОписаниеОповещения,,,, УникальныйИдентификатор);
		
	#Иначе
		// инициализируем свойства диалога
		Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
		Диалог.Заголовок = НСтр("ru = 'Выберите файл сертификата абонента';
								|en = 'Выберите файл сертификата абонента'");
		Диалог.МножественныйВыбор = Ложь;
		Диалог.ПроверятьСуществованиеФайла = Истина;
		Диалог.Фильтр = НСтр("ru = 'Сертификат (*.cer)|*.cer';
							|en = 'Сертификат (*.cer)|*.cer'");
		
		// показываем диалог
		Если НЕ Диалог.Выбрать() Тогда
			Возврат;
		КонецЕсли;
		
		ДанныеФайла = Новый ДвоичныеДанные(Диалог.ПолноеИмяФайла);
		АдресФайла = ПоместитьВоВременноеХранилище(ДанныеФайла, УникальныйИдентификатор);
		
		ЗагрузитьСертификатИмпортироватьСертификат(АдресФайла);
	#КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СертификатАбонентаПредставлениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("СертификатАбонентаПредставлениеНачалоВыбораЗавершение", ЭтотОбъект);
	
	КриптографияЭДКОКлиент.ВыбратьСертификат(
		Оповещение,
		МестоХраненияКлюча,
		СертификатАбонентаОтпечаток,
		"My");
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатАбонентаПредставлениеОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СертификатАбонентаОтпечаток = "";
	
	КриптографияЭДКОКлиент.ОтобразитьПредставлениеСертификата(
		МестоХраненияКлюча,
		Элементы.СертификатАбонентаПредставление,
		СертификатАбонентаОтпечаток,
		ЭтотОбъект,
		"СертификатАбонентаПредставление");
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатАбонентаПредставлениеОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СвойстваСертификата = Новый Структура;
	СвойстваСертификата.Вставить("Отпечаток", СертификатАбонентаОтпечаток);
	КриптографияЭДКОКлиентСервер.КонтекстМоделиХраненияКлюча(МестоХраненияКлюча, СвойстваСертификата);
	КриптографияЭДКОКлиент.ПоказатьСертификат(СвойстваСертификата);
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьНесколькоПриИзменении(Элемент)
	
	ОбновитьДоступностьЭлементов();
	КриптографияЭДКОКлиент.ОтобразитьПредставлениеСертификата(
		МестоХраненияКлюча,
		Элементы.СертификатАбонентаПредставление,
		СертификатАбонентаОтпечаток,
		ЭтотОбъект,
		"СертификатАбонентаПредставление");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицФормы

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле <> Неопределено И Поле.Имя = "СписокСертификатАбонентаПредставление" Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ВыбратьСертификатАбонентаСписка();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокСертификатАбонентаПредставлениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ВыбратьСертификатАбонентаСписка();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокСертификатАбонентаПредставлениеОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	ТекущиеДанные.СертификатАбонентаОтпечаток 		= "";
	ТекущиеДанные.СертификатАбонентаОшибка 			= "";
	ТекущиеДанные.СертификатАбонентаПредставление 	= "";
	
КонецПроцедуры

&НаКлиенте
Процедура СписокСертификатАбонентаПредставлениеОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	МестоХраненияКлючаПользователя = КриптографияЭДКОКлиентСервер.СвойстваМестаХраненияКлюча(
		Элементы.Список.ТекущиеДанные.МодельХраненияЗакрытогоКлюча, УчетнаяЗаписьСсылка);
	СвойстваСертификата = Новый Структура;
	СвойстваСертификата.Вставить("Отпечаток", Элементы.Список.ТекущиеДанные.СертификатАбонентаОтпечаток);
	КриптографияЭДКОКлиентСервер.КонтекстМоделиХраненияКлюча(МестоХраненияКлючаПользователя, СвойстваСертификата);
	КриптографияЭДКОКлиент.ПоказатьСертификат(СвойстваСертификата);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПриОткрытииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	КонтекстЭДО = Результат.КонтекстЭДО;
	
	ОбновитьНастройкиОбменаПользователей();
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатАбонентаПредставлениеНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Выполнено Тогда
		ВыбранныйСертификатАбонента = Результат.ВыбранноеЗначение;
		ВыбранныйСертификатАбонента = ОбщегоНазначенияКлиент.СкопироватьРекурсивно(ВыбранныйСертификатАбонента, Ложь);
		КриптографияЭДКОКлиентСервер.ЗаполнитьМестоХраненияКлюча(МестоХраненияКлюча, ВыбранныйСертификатАбонента);
		
		СертификатАбонентаОтпечаток = ВыбранныйСертификатАбонента.Отпечаток;
		
		КриптографияЭДКОКлиент.ОтобразитьПредставлениеСертификата(
			КриптографияЭДКОКлиентСервер.КонтекстМоделиХраненияКлюча(ДополнительныеПараметры),
			Элементы.СертификатАбонентаПредставление,
			ВыбранныйСертификатАбонента.Отпечаток,
			ЭтотОбъект,
			"СертификатАбонентаПредставление");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСертификатПослеПомещенияФайла(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	
	Если НЕ Результат Тогда
		Возврат;
	КонецЕсли;
	
	ЗагрузитьСертификатИмпортироватьСертификат(Адрес);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСертификатИмпортироватьСертификат(Адрес)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузитьСертификатПослеПомещенияФайлаПослеПолученияДанныхФайла",
		ЭтотОбъект);
	
	ОперацииСФайламиЭДКОКлиент.ДанныеССервераВФайл(ОписаниеОповещения, Адрес, ".cer");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСертификатПослеПомещенияФайлаПослеПолученияДанныхФайла(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ Результат.Выполнено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяФайлаСертификата = Результат.ИмяФайла;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузитьСертификатПослеПомещенияФайлаПослеИмпортаСертификата",
		ЭтотОбъект);
	
	КриптографияЭДКОКлиент.ИмпортироватьСертификат(ОписаниеОповещения, ИмяФайлаСертификата, "AddressBook");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСертификатПослеПомещенияФайлаПослеИмпортаСертификата(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ Результат.Выполнено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СвойстваСертификата = Результат.СвойстваСертификата;
	
	ЗадатьСертификатАбонента(СвойстваСертификата);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДоступностьЭлементов()
	
	Элементы.ЗагрузитьСертификат.Доступность 			= (ИспользоватьНесколько = 1);
	Элементы.ЗагрузитьСертификат.Видимость 				= ЭтоПолноправныйПользователь;
	Элементы.СертификатАбонентаПредставление.Видимость 	= (ИспользоватьНесколько <> 1);
	Элементы.Список.Видимость 							= (ИспользоватьНесколько = 1);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНастройкиОбменаПользователей()
	
	СертификатыАбонента = Новый Массив;
	Для каждого СвойстваОтпечаткаСертификата Из СвойстваУникальныхОтпечатковСертификатов Цикл
		СертификатАбонента = Новый Структура;
		СертификатАбонента.Вставить("Отпечаток", СвойстваОтпечаткаСертификата.СертификатАбонентаОтпечаток);
		МестоХраненияКлючаПользователя = КриптографияЭДКОКлиентСервер.СвойстваМестаХраненияКлюча(
			СвойстваОтпечаткаСертификата.МодельХраненияЗакрытогоКлюча, УчетнаяЗаписьСсылка);
		КриптографияЭДКОКлиентСервер.КонтекстМоделиХраненияКлюча(МестоХраненияКлючаПользователя, СертификатАбонента);
		
		СертификатыАбонента.Добавить(СертификатАбонента);
	КонецЦикла;
	
	Оповещение = Новый ОписаниеОповещения("ОбновитьНастройкиОбменаПользователейПослеПоискаСертификатов", ЭтотОбъект);
	КриптографияЭДКОКлиент.НайтиСертификаты(
		Оповещение,
		СертификатыАбонента,,
		Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНастройкиОбменаПользователейПослеПоискаСертификатов(Результат, ДополнительныеПараметры) Экспорт
	
	НайденныеСертификаты = Новый Соответствие;
	Если Результат.Выполнено Тогда
		Для каждого НайденныйСертификат Из Результат.Сертификаты Цикл
			Если ЗначениеЗаполнено(НайденныйСертификат.Отпечаток) Тогда
				НайденныеСертификаты.Вставить(НайденныйСертификат.Отпечаток, НайденныйСертификат);
			Конецесли;
		КонецЦикла;
	КонецЕсли;
	
	ТекущийПользователь = ПользователиКлиент.ТекущийПользователь();
	Список.Очистить();
	Для НомерОбхода = 1 По 2 Цикл
		ЭтоПоискТекущегоПользователя = (НомерОбхода = 1);
		
		Для каждого НастройкаОбменаПользователей Из НастройкиОбменаПользователей Цикл
			Если ЭтоПоискТекущегоПользователя И НастройкаОбменаПользователей.Пользователь <> ТекущийПользователь
				ИЛИ НЕ ЭтоПоискТекущегоПользователя И НастройкаОбменаПользователей.Пользователь = ТекущийПользователь Тогда
				
				Продолжить;
			КонецЕсли;
			
			ОтпечатокСертификата = НастройкаОбменаПользователей.СертификатАбонентаОтпечаток;
			ОшибкаСертификата = "";
			ПредставлениеСертификата = НастройкаОбменаПользователей.СертификатАбонентаОтпечаток;
			Если ЗначениеЗаполнено(НастройкаОбменаПользователей.СертификатАбонентаОтпечаток) Тогда
				СвойстваСертификата = НайденныеСертификаты[НастройкаОбменаПользователей.СертификатАбонентаОтпечаток];
				Если СвойстваСертификата <> Неопределено Тогда
					ОтпечатокСертификата = СвойстваСертификата.Отпечаток;
					ПредставлениеСертификата = КриптографияЭДКОКлиент.ПолноеПредставлениеСертификата(СвойстваСертификата);
				КонецЕсли;
				ОшибкаСертификата = ТекстОшибкиСертификата(СвойстваСертификата);
				ПредставлениеСертификата = ПредставлениеСертификата
					+ ?(ЗначениеЗаполнено(ОшибкаСертификата), " - " + ОшибкаСертификата, "");
			КонецЕсли;
			ЕстьПрава = ЭтоПолноправныйПользователь ИЛИ НастройкаОбменаПользователей.Пользователь = ТекущийПользователь;
			
			СтрокаСписка = Список.Добавить();
			СтрокаСписка.Пользователь 						= НастройкаОбменаПользователей.Пользователь;
			СтрокаСписка.НаименованиеПользователя 			= НастройкаОбменаПользователей.НаименованиеПользователя;
			СтрокаСписка.ПометкаУдаленияПользователя 		= НастройкаОбменаПользователей.ПометкаУдаленияПользователя;
			СтрокаСписка.СертификатАбонентаОтпечаток 		= ОтпечатокСертификата;
			СтрокаСписка.СертификатАбонентаПредставление 	= ПредставлениеСертификата;
			СтрокаСписка.СертификатАбонентаОшибка 			= ОшибкаСертификата;
			СтрокаСписка.ЕстьПрава 							= ЕстьПрава;
			СтрокаСписка.МодельХраненияЗакрытогоКлюча 		= НастройкаОбменаПользователей.МодельХраненияЗакрытогоКлюча;
		КонецЦикла;
	КонецЦикла;
	
	Оповещение = Новый ОписаниеОповещения("ОбновитьНастройкиОбменаПользователейПослеОтображения", ЭтотОбъект);
	КриптографияЭДКОКлиент.ОтобразитьПредставлениеСертификата(
		МестоХраненияКлюча,
		Элементы.СертификатАбонентаПредставление,
		СертификатАбонентаОтпечаток,
		ЭтотОбъект,
		"СертификатАбонентаПредставление",
		Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНастройкиОбменаПользователейПослеОтображения(Результат, ДополнительныеПараметры) Экспорт
	
	ОбновитьДоступностьЭлементов();
	Если НЕ Элементы.СертификатАбонентаПредставление.Доступность Тогда
		ЗаголовокБезФорматирования = Строка(Элементы.СертификатАбонентаПредставление.РасширеннаяПодсказка.Заголовок);
		Элементы.СертификатАбонентаПредставление.РасширеннаяПодсказка.Заголовок = ЗаголовокБезФорматирования;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьСертификатАбонентаСписка()
	
	Если НЕ Элементы.Список.ТекущиеДанные.ЕстьПрава Тогда
		ТекстСообения = НСтр("ru = 'Для изменения сертификатов абонентов других пользователей требуются полные права';
							|en = 'Для изменения сертификатов абонентов других пользователей требуются полные права'");
		ПоказатьПредупреждение(, ТекстСообения);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения(
		"ВыбратьСертификатАбонентаСпискаПослеВыбораСертификата", ЭтотОбъект);
	МестоХраненияКлючаПользователя = КриптографияЭДКОКлиентСервер.СвойстваМестаХраненияКлюча(
		Элементы.Список.ТекущиеДанные.МодельХраненияЗакрытогоКлюча, УчетнаяЗаписьСсылка);
	КриптографияЭДКОКлиент.ВыбратьСертификат(
		Оповещение,
		МестоХраненияКлючаПользователя,
		Элементы.Список.ТекущиеДанные.СертификатАбонентаОтпечаток,
		"My");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьСертификатАбонентаСпискаПослеВыбораСертификата(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ Результат.Выполнено Тогда
		Возврат;
	КонецЕсли;
	
	ЗадатьСертификатАбонента(Результат.ВыбранноеЗначение);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗадатьСертификатАбонента(СвойстваСертификата)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.СертификатАбонентаОтпечаток = СвойстваСертификата.Отпечаток Тогда
		Возврат;
	КонецЕсли;
	
	ЗаменяемыйОтпечаток = ТекущиеДанные.СертификатАбонентаОтпечаток;
	
	ТекущиеДанные.СертификатАбонентаОтпечаток = СвойстваСертификата.Отпечаток;
	ТекущиеДанные.СертификатАбонентаОшибка = ТекстОшибкиСертификата(СвойстваСертификата);
	ТекущиеДанные.СертификатАбонентаПредставление =
		КриптографияЭДКОКлиент.ПолноеПредставлениеСертификата(СвойстваСертификата)
		+ ?(ЗначениеЗаполнено(ТекущиеДанные.СертификатАбонентаОшибка),
		" - " + ТекущиеДанные.СертификатАбонентаОшибка, "");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНастройкиОбменаПользователей()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка) КАК Пользователь,
		|	"""" КАК НаименованиеПользователя,
		|	ЛОЖЬ КАК ПометкаУдаленияПользователя,
		|	НастройкиОбменаРПН.СертификатАбонентаОтпечаток КАК СертификатАбонентаОтпечаток
		|ИЗ
		|	РегистрСведений.НастройкиОбменаРПН КАК НастройкиОбменаРПН
		|ГДЕ
		|	НастройкиОбменаРПН.Организация = &Организация
		|	И НастройкиОбменаРПН.Пользователь = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Пользователи.Ссылка КАК Пользователь,
		|	Пользователи.Наименование КАК НаименованиеПользователя,
		|	Пользователи.ПометкаУдаления КАК ПометкаУдаленияПользователя,
		|	НастройкиОбменаРПН.СертификатАбонентаОтпечаток КАК СертификатАбонентаОтпечаток
		|ИЗ
		|	Справочник.Пользователи КАК Пользователи
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиОбменаРПН КАК НастройкиОбменаРПН
		|		ПО НастройкиОбменаРПН.Организация = &Организация
		|		И Пользователи.Ссылка = НастройкиОбменаРПН.Пользователь
		|ГДЕ
		|	НЕ Пользователи.Недействителен
		|	И НЕ Пользователи.Служебный
		|	И Пользователи.ИдентификаторПользователяИБ <> &ПустойИдентификаторПользователяИБ
		|
		|УПОРЯДОЧИТЬ ПО
		|	НаименованиеПользователя";
	
	Запрос.УстановитьПараметр("Организация", ОрганизацияСсылка);
	ПустойИдентификаторПользователяИБ = Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000");
	Запрос.УстановитьПараметр("ПустойИдентификаторПользователяИБ", ПустойИдентификаторПользователяИБ);
	ТаблицаНастроек = Запрос.Выполнить().Выгрузить();
	
	СертификатАбонентаОтпечаток = "";
	НастройкиОбменаПользователей.Очистить();
	СвойстваУникальныхОтпечатковСертификатов.Очистить();
	Для каждого СтрокаТаблицы Из ТаблицаНастроек Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.Пользователь) Тогда
			Если НЕ ЗначениеЗаполнено(СертификатАбонентаОтпечаток) Тогда
				СертификатАбонентаОтпечаток = СтрокаТаблицы.СертификатАбонентаОтпечаток;
			КонецЕсли;
			
		Иначе
			МодельХраненияЗакрытогоКлюча = ?(ЗначениеЗаполнено(УчетнаяЗаписьСсылка),
				Мультирежим.МодельХраненияСертификатаПользователя(УчетнаяЗаписьСсылка, СтрокаТаблицы.Пользователь), Неопределено);
			МодельХраненияЗакрытогоКлюча = ?(ЗначениеЗаполнено(МодельХраненияЗакрытогоКлюча), МодельХраненияЗакрытогоКлюча,
				Перечисления.МодельХраненияЗакрытогоКлюча.ЛокальныйКлюч);
			
			СтрокаНастроек = НастройкиОбменаПользователей.Добавить();
			СтрокаНастроек.Пользователь 				= СтрокаТаблицы.Пользователь;
			СтрокаНастроек.НаименованиеПользователя 	= СтрокаТаблицы.НаименованиеПользователя;
			СтрокаНастроек.ПометкаУдаленияПользователя 	= СтрокаТаблицы.ПометкаУдаленияПользователя;
			СтрокаНастроек.СертификатАбонентаОтпечаток 	= СтрокаТаблицы.СертификатАбонентаОтпечаток;
			СтрокаНастроек.МодельХраненияЗакрытогоКлюча = МодельХраненияЗакрытогоКлюча;
		Конецесли;
		
		Если ЗначениеЗаполнено(СтрокаТаблицы.СертификатАбонентаОтпечаток) Тогда
			ПараметрыОтбора = Новый Структура("СертификатАбонентаОтпечаток, МодельХраненияЗакрытогоКлюча",
				СтрокаТаблицы.СертификатАбонентаОтпечаток, МодельХраненияЗакрытогоКлюча);
			МассивСтрок = СвойстваУникальныхОтпечатковСертификатов.НайтиСтроки(ПараметрыОтбора);
			Если МассивСтрок.Количество() = 0 Тогда
				СтрокаСвойств = СвойстваУникальныхОтпечатковСертификатов.Добавить();
				СтрокаСвойств.СертификатАбонентаОтпечаток 	= СтрокаТаблицы.СертификатАбонентаОтпечаток;
				СтрокаСвойств.МодельХраненияЗакрытогоКлюча 	= МодельХраненияЗакрытогоКлюча;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНаСервере()
	
	РегистрыСведений.НастройкиОбменаРПН.СохранитьСписокПользователей(
		ОрганизацияСсылка, 
		Список, 
		ИспользоватьНесколько, 
		СертификатАбонентаОтпечаток);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ТекстОшибкиСертификата(СвойстваСертификата)
	
	Если СвойстваСертификата = Неопределено Тогда
		Возврат НСтр("ru = 'Сертификат не зарегистрирован в системном хранилище сертификатов';
					|en = 'Сертификат не зарегистрирован в системном хранилище сертификатов'");
	КонецЕсли;
	
	ДатаСеанса = ТекущаяДатаСеанса();
	
	Если ДатаСеанса > СвойстваСертификата.ДействителенПо Тогда
		Возврат НСтр("ru = 'Срок действия сертификата истек';
					|en = 'Срок действия сертификата истек'");
		
	ИначеЕсли ДатаСеанса < СвойстваСертификата.ДействителенС Тогда
		Возврат НСтр("ru = 'Срок действия сертификата еще не наступил';
					|en = 'Срок действия сертификата еще не наступил'");
	КонецЕсли;
	
КонецФункции

#КонецОбласти
