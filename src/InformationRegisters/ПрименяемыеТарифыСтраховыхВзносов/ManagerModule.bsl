#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Начальное заполнение и обновление информационной базы.

Процедура ВыделитьСведенияОбОрганизацияхИнвалидов() Экспорт 

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПрименяемыеТарифыСтраховыхВзносов.ГоловнаяОрганизация КАК ГоловнаяОрганизация
	|ИЗ
	|	РегистрСведений.ПрименяемыеТарифыСтраховыхВзносов КАК ПрименяемыеТарифыСтраховыхВзносов
	|ГДЕ
	|	ПрименяемыеТарифыСтраховыхВзносов.ВидТарифа = ЗНАЧЕНИЕ(Справочник.ВидыТарифовСтраховыхВзносов.ОрганизацияИнвалидов)
	|	И НЕ ПрименяемыеТарифыСтраховыхВзносов.ОрганизацияИнвалидов";
	Если Запрос.Выполнить().Пустой() Тогда
		Возврат
	КонецЕсли; 

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПрименяемыеТарифыСтраховыхВзносов.Период КАК Период,
	|	ПрименяемыеТарифыСтраховыхВзносов.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ПрименяемыеТарифыСтраховыхВзносов.ВидТарифа КАК ВидТарифа,
	|	ПрименяемыеТарифыСтраховыхВзносов.ДатаРегистрацииИзменений КАК ДатаРегистрацииИзменений,
	|	ПрименяемыеТарифыСтраховыхВзносов.ВидТарифа = ЗНАЧЕНИЕ(Справочник.ВидыТарифовСтраховыхВзносов.ОрганизацияИнвалидов) КАК ОрганизацияИнвалидов
	|ИЗ
	|	РегистрСведений.ПрименяемыеТарифыСтраховыхВзносов КАК ПрименяемыеТарифыСтраховыхВзносов";
	
	НаборЗаписей = РегистрыСведений.ПрименяемыеТарифыСтраховыхВзносов.СоздатьНаборЗаписей();
	НаборЗаписей.Загрузить(Запрос.Выполнить().Выгрузить());
	ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
