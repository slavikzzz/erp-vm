#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ДанныеЗаполнения = Неопределено Тогда
		ДанныеЗаполнения = Новый Структура;
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ИмяРеквизитаОрганизация = "Организация";
	
	Если Не ДанныеЗаполнения.Свойство(ИмяРеквизитаОрганизация) Тогда
		ДанныеЗаполнения.Вставить(ИмяРеквизитаОрганизация, Справочники.Организации.ОрганизацияПоУмолчанию());
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	ПризнакОбособленногоПодразделенияОрганизаций = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(
		ВыгрузитьКолонку("Организация"), "ОбособленноеПодразделение");
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		Если ПризнакОбособленногоПодразделенияОрганизаций[Запись.Организация] Тогда
			ОбщегоНазначения.СообщитьПользователю(
				НСтр("ru = 'В качестве организации нельзя указывать обособленное подразделение';
					|en = 'You cannot specify a business unit as a company'"),,,, Отказ);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#КонецЕсли
