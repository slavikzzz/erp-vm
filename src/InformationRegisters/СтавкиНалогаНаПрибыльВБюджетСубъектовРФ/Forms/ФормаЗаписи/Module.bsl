#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Ключ.Пустой() Тогда
		ЗаполнитьСтавкиПоУмолчанию();
	КонецЕсли;
	
	НастроитьЭлементыИнформацииОбИзмененииСтавкиНалога();

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	ПериодПриИзмененииНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура СтавкаСубъектРФПриИзменении(Элемент)
	
	НастроитьЭлементыИнформацииОбИзмененииСтавкиНалога(Ложь);

КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	НастроитьЭлементыИнформацииОбИзмененииСтавкиНалога();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастроитьЭлементыИнформацииОбИзмененииСтавкиНалога(ИзмененыКлюченыеРеквизиты = Истина)
		
	ИнформацияВыводится = ОбщегоНазначенияКлиентСервер.ЗначениеСвойстваЭлементаФормы(Элементы, "ГруппаИнформация", "Видимость");
	
	Если ИнформацияВыводится И НЕ ИзмененыКлюченыеРеквизиты Тогда
		Возврат;	
	КонецЕсли;

	ВыводитьИнформациюОбИзмененииСтавкиНалога = ВыводитьИнформациюОбИзмененииСтавкиНалога();
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, 
		"ГруппаИнформация", "Видимость", ВыводитьИнформациюОбИзмененииСтавкиНалога);
	
	Если ВыводитьИнформациюОбИзмененииСтавкиНалога Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Обратите внимание, есть документы расчета налога на прибыль после %1';
										|en = 'Обратите внимание, есть документы расчета налога на прибыль после %1'", 
			ОбщегоНазначения.КодОсновногоЯзыка()), Формат(Запись.Период, "ДФ=dd.MM.yyyy;")); 
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, 
			"ИнформацияОбИспользованииСтавкиНалога", "Заголовок", ТекстСообщения);
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Функция ВыводитьИнформациюОбИзмененииСтавкиНалога()

	ВыводитьИнформациюОбИзмененииСтавкиНалога = Ложь;
	
	Если Модифицированность ИЛИ Параметры.Ключ.Пустой() Тогда
		ВыводитьИнформациюОбИзмененииСтавкиНалога = НалоговыйУчет.ВыполненРасчетНалогаПослеИзмененияСтавки(Запись.Период, Запись.Организация);		
	КонецЕсли;		
	
	Возврат ВыводитьИнформациюОбИзмененииСтавкиНалога;
	
КонецФункции

&НаСервере
Процедура ПериодПриИзмененииНаСервере()

	ЗаполнитьСтавкиПоУмолчанию();
	НастроитьЭлементыИнформацииОбИзмененииСтавкиНалога();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтавкиПоУмолчанию()
	
	Если ЗначениеЗаполнено(Запись.Период) Тогда
		Период = Запись.Период;
	Иначе
		Период = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Запись.СтавкаСубъектРФ = РегистрыСведений.СтавкиНалогаНаПрибыльДляВсехОрганизаций.ЗначениеПоУмолчаниюРегиональныйБюджет(Период);
	
КонецПроцедуры

#КонецОбласти
