#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Добавляет в очередь задание к корректировке плана производства полуфабрикатов.
//
// Параметры:
//  ПланПроизводства - ДокументСсылка.ПланПроизводства	 - обрабатываемый документ
//  СписокКорректировок - ТаблицаЗначений				 - список корректировок
//
Процедура ДобавитьВОчередь(ПланПроизводства, СписокКорректировок) Экспорт
	
	Набор = РегистрыСведений.СодержаниеКорректировокПланаПроизводства.СоздатьНаборЗаписей();
	Набор.Отбор.ПланПроизводства.Установить(ПланПроизводства);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Для каждого Строка Из СписокКорректировок Цикл
		
		Набор.Отбор.Номенклатура.Установить(Строка.Номенклатура);
		Набор.Отбор.Характеристика.Установить(Строка.Характеристика);
		Набор.Отбор.Назначение.Установить(Строка.Назначение);
		Набор.Отбор.ДатаВыпускаПолуфабриката.Установить(Строка.ДатаВыпускаПолуфабриката);
		
		НоваяЗапись = Набор.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, Строка);
		
		Набор.Записать(Истина);
		
		Набор.Очистить();
		
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Очищает очередь корректировок по плану производства.
//
// Параметры:
//  ПланПроизводства - ДокументСсылка.ПланПроизводства	 - обрабатываемый документ
//
Процедура ОчиститьОчередь(ПланПроизводства) Экспорт
	
	Набор = РегистрыСведений.СодержаниеКорректировокПланаПроизводства.СоздатьНаборЗаписей();
	Набор.Отбор.ПланПроизводства.Установить(ПланПроизводства);
	
	УстановитьПривилегированныйРежим(Истина);
	Набор.Записать(Истина);
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Очищает очередь корректировок по плану производства.
//
// Параметры:
//  ПланПроизводства	 - ДокументСсылка.ПланПроизводства	 - обрабатываемый документ
//  СписокКорректировок	 - Массив, ТаблицаЗначений			 - список корректировок
//
Процедура ОтметитьВыполнение(ПланПроизводства, СписокКорректировок) Экспорт
	
	Если НЕ ЗначениеЗаполнено(СписокКорректировок) Тогда
		Возврат;
	КонецЕсли;
	
	Набор = РегистрыСведений.СодержаниеКорректировокПланаПроизводства.СоздатьНаборЗаписей();
	Набор.Отбор.ПланПроизводства.Установить(ПланПроизводства);
	
	УстановитьПривилегированныйРежим(Истина);
	Для каждого Строка Из СписокКорректировок Цикл
		
		Набор.Отбор.Номенклатура.Установить(Строка.Номенклатура);
		Набор.Отбор.Характеристика.Установить(Строка.Характеристика);
		Набор.Отбор.Назначение.Установить(Строка.Назначение);
		Набор.Отбор.ДатаВыпускаПолуфабриката.Установить(Строка.ДатаВыпускаПолуфабриката);
		
		Набор.Записать(Истина);
		
	КонецЦикла;
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли