#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьВидыМониторинга(
		Параметры.ВыбранныеФормыМониторинга,
		Параметры.ДоступныеФормыМониторинга);
		
	Элементы.ФормыМониторинга.Подсказка = Параметры.Подсказка;
	ТолькоПросмотр = Параметры.ТолькоПросмотр;
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ФормаОК",
		"Видимость",
		НЕ ТолькоПросмотр);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ФормаОтмена",
		"Видимость",
		НЕ ТолькоПросмотр);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы


#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	Закрыть(ПомеченныеВидыМониторинга());
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть(Неопределено);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьВидыМониторинга(ВыбранныеВидыМониторинга, ДоступныеФормыМониторинга = Неопределено)
	
	ФормыМониторинга.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВыбранныеВидыМониторинга", ВыбранныеВидыМониторинга);
	
	Текст = 
	"ВЫБРАТЬ
	|	ВидыФормМониторингаРаботниковСоциальнойСферы.Ссылка КАК Форма,
	|	ВЫБОР
	|		КОГДА ВидыФормМониторингаРаботниковСоциальнойСферы.Ссылка В (&ВыбранныеВидыМониторинга)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Пометка
	|ИЗ
	|	Перечисление.ВидыФормМониторингаРаботниковСоциальнойСферы КАК ВидыФормМониторингаРаботниковСоциальнойСферы";
	
	Если ДоступныеФормыМониторинга <> Неопределено Тогда
		Запрос.УстановитьПараметр("ДоступныеФормыМониторинга", ДоступныеФормыМониторинга);
		
		Текст = Текст + "
			|ГДЕ
			|	ВидыФормМониторингаРаботниковСоциальнойСферы.Ссылка В (&ДоступныеФормыМониторинга)";
	КонецЕсли;
	
	Запрос.Текст = Текст;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ФормыМониторинга.Добавить(), Выборка);
	КонецЦикла;
		
КонецПроцедуры

&НаСервере
Функция ПомеченныеВидыМониторинга()
	
	Результат = Новый Массив;
	Для Каждого Строка Из ФормыМониторинга Цикл
		Если Строка.Пометка Тогда
			Результат.Добавить(Строка.Форма);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
