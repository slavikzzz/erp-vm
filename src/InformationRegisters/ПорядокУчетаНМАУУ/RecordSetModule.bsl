#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ПодготовитьДанныеДляФормированияЗаданийПередЗаписью();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПодготовитьДанныеДляФормированияЗаданийПриЗаписи();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПодготовитьДанныеДляФормированияЗаданийПередЗаписью()
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаПередЗаписью.Период КАК Период,
	|	ТаблицаПередЗаписью.Организация КАК Организация,
	|	ТаблицаПередЗаписью.НематериальныйАктив КАК НематериальныйАктив,
	|	ТаблицаПередЗаписью.НачислятьАмортизациюУУ КАК НачислятьАмортизациюУУ,
	|	ТаблицаПередЗаписью.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|	ТаблицаПередЗаписью.СтатьяРасходов КАК СтатьяРасходов,
	|	ТаблицаПередЗаписью.АналитикаРасходов КАК АналитикаРасходов,
	|	ТаблицаПередЗаписью.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ТаблицаПередЗаписью.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов
	|ПОМЕСТИТЬ ПорядокУчетаНМАУУПередЗаписью
	|ИЗ
	|	РегистрСведений.ПорядокУчетаНМАУУ КАК ТаблицаПередЗаписью
	|ГДЕ
	|	ТаблицаПередЗаписью.Регистратор = &Регистратор";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
	
КонецПроцедуры

Процедура ПодготовитьДанныеДляФормированияЗаданийПриЗаписи()
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Период              КАК Период,
	|	Таблица.Организация         КАК Организация,
	|	Таблица.НематериальныйАктив КАК НематериальныйАктив,
	|	ЛОЖЬ                        КАК ОтражатьВРеглУчете,
	|	ИСТИНА                      КАК ОтражатьВУпрУчете,
	|	&Регистратор                КАК Документ
	|ПОМЕСТИТЬ ПорядокУчетаНМАУУИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаПередЗаписью.Период               КАК Период,
	|		ТаблицаПередЗаписью.Организация          КАК Организация,
	|		ТаблицаПередЗаписью.НематериальныйАктив  КАК НематериальныйАктив
	|	ИЗ
	|		ПорядокУчетаНМАУУПередЗаписью КАК ТаблицаПередЗаписью
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаНМАУУ КАК ТаблицаПриЗаписи
	|			ПО ТаблицаПриЗаписи.НематериальныйАктив = ТаблицаПередЗаписью.НематериальныйАктив
	|				И ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|				И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|		ГДЕ
	|			(ТаблицаПриЗаписи.Период ЕСТЬ NULL
	|				ИЛИ ТаблицаПриЗаписи.НачислятьАмортизациюУУ <> ТаблицаПередЗаписью.НачислятьАмортизациюУУ
	|				ИЛИ ТаблицаПриЗаписи.СпособОтраженияРасходов <> ТаблицаПередЗаписью.СпособОтраженияРасходов
	|				ИЛИ ТаблицаПриЗаписи.СтатьяРасходов <> ТаблицаПередЗаписью.СтатьяРасходов
	|				ИЛИ ТаблицаПриЗаписи.АналитикаРасходов <> ТаблицаПередЗаписью.АналитикаРасходов
	|				ИЛИ ТаблицаПриЗаписи.ПередаватьРасходыВДругуюОрганизацию <> ТаблицаПередЗаписью.ПередаватьРасходыВДругуюОрганизацию
	|				ИЛИ ТаблицаПриЗаписи.ОрганизацияПолучательРасходов <> ТаблицаПередЗаписью.ОрганизацияПолучательРасходов
	|				ИЛИ НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, ДЕНЬ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, ДЕНЬ))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаПриЗаписи.Период,
	|		ТаблицаПриЗаписи.Организация,
	|		ТаблицаПриЗаписи.НематериальныйАктив
	|	ИЗ
	|		РегистрСведений.ПорядокУчетаНМАУУ КАК ТаблицаПриЗаписи
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаНМАУУПередЗаписью КАК ТаблицаПередЗаписью
	|			ПО ТаблицаПриЗаписи.НематериальныйАктив = ТаблицаПередЗаписью.НематериальныйАктив
	|				И ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|		ГДЕ
	|			(ТаблицаПередЗаписью.Период ЕСТЬ NULL
	|				ИЛИ ТаблицаПриЗаписи.НачислятьАмортизациюУУ <> ТаблицаПередЗаписью.НачислятьАмортизациюУУ
	|				ИЛИ ТаблицаПриЗаписи.СпособОтраженияРасходов <> ТаблицаПередЗаписью.СпособОтраженияРасходов
	|				ИЛИ ТаблицаПриЗаписи.СтатьяРасходов <> ТаблицаПередЗаписью.СтатьяРасходов
	|				ИЛИ ТаблицаПриЗаписи.АналитикаРасходов <> ТаблицаПередЗаписью.АналитикаРасходов
	|				ИЛИ ТаблицаПриЗаписи.ПередаватьРасходыВДругуюОрганизацию <> ТаблицаПередЗаписью.ПередаватьРасходыВДругуюОрганизацию
	|				ИЛИ ТаблицаПриЗаписи.ОрганизацияПолучательРасходов <> ТаблицаПередЗаписью.ОрганизацияПолучательРасходов
	|				ИЛИ НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, ДЕНЬ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, ДЕНЬ))
	|			И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|
	|	) КАК Таблица
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаПередЗаписью.Организация       КАК Организация,
	|	ТаблицаПередЗаписью.НематериальныйАктив  КАК НематериальныйАктив
	|ПОМЕСТИТЬ ПорядокУчетаНМАУУ_НачислятьАмортизацию
	|ИЗ
	|	ПорядокУчетаНМАУУПередЗаписью КАК ТаблицаПередЗаписью
	|ГДЕ
	|	ТаблицаПередЗаписью.НачислятьАмортизациюУУ
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаПриЗаписи.Организация,
	|	ТаблицаПриЗаписи.НематериальныйАктив
	|ИЗ
	|	РегистрСведений.ПорядокУчетаНМАУУ КАК ТаблицаПриЗаписи
	|ГДЕ
	|	ТаблицаПриЗаписи.Регистратор = &Регистратор
	|	И ТаблицаПриЗаписи.НачислятьАмортизациюУУ
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПорядокУчетаНМАУУПередЗаписью";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
		"ПорядокУчетаНМАУУИзменение", Выборка.Следующий() И Выборка.Количество > 0);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

