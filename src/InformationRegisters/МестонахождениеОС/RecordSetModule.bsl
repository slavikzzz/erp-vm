#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ПодготовитьДанныеДляФормированияЗаданийПередЗаписью();

КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПодготовитьДанныеДляФормированияЗаданийПриЗаписи();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПодготовитьДанныеДляФормированияЗаданийПередЗаписью()
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаПередЗаписью.Период КАК Период,
	|	ТаблицаПередЗаписью.Организация КАК Организация,
	|	ТаблицаПередЗаписью.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаПередЗаписью.Местонахождение КАК Местонахождение,
	|	ТаблицаПередЗаписью.ОтражатьВРеглУчете КАК ОтражатьВРеглУчете,
	|	ТаблицаПередЗаписью.ОтражатьВУпрУчете КАК ОтражатьВУпрУчете
	|ПОМЕСТИТЬ МестонахождениеОСПередЗаписью
	|ИЗ
	|	РегистрСведений.МестонахождениеОС КАК ТаблицаПередЗаписью
	|ГДЕ
	|	ТаблицаПередЗаписью.Регистратор = &Регистратор
	|	И ТаблицаПередЗаписью.ДатаИсправления = ДАТАВРЕМЯ(1,1,1)";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПодготовитьДанныеДляФормированияЗаданийПриЗаписи()
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Период              КАК Период,
	|	Таблица.Организация         КАК Организация,
	|	Таблица.ОсновноеСредство    КАК ОсновноеСредство,
	|	Таблица.ОтражатьВРеглУчете  КАК ОтражатьВРеглУчете,
	|	Таблица.ОтражатьВУпрУчете   КАК ОтражатьВУпрУчете,
	|	&Регистратор                КАК Документ
	|ПОМЕСТИТЬ МестонахождениеОСИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаПередЗаписью.Период              КАК Период,
	|		ТаблицаПередЗаписью.Организация         КАК Организация,
	|		ТаблицаПередЗаписью.ОсновноеСредство    КАК ОсновноеСредство,
	|		ТаблицаПередЗаписью.ОтражатьВРеглУчете  КАК ОтражатьВРеглУчете,
	|		ТаблицаПередЗаписью.ОтражатьВУпрУчете   КАК ОтражатьВУпрУчете
	|	ИЗ
	|		МестонахождениеОСПередЗаписью КАК ТаблицаПередЗаписью
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОС КАК ТаблицаПриЗаписи
	|			ПО ТаблицаПриЗаписи.ОсновноеСредство = ТаблицаПередЗаписью.ОсновноеСредство
	|				И ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|				И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|				И НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, ДЕНЬ) = НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, ДЕНЬ)
	|				И ТаблицаПриЗаписи.ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|		ГДЕ
	|			(ТаблицаПриЗаписи.Период ЕСТЬ NULL
	|				ИЛИ ТаблицаПриЗаписи.Местонахождение <> ТаблицаПередЗаписью.Местонахождение)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаПриЗаписи.Период,
	|		ТаблицаПриЗаписи.Организация,
	|		ТаблицаПриЗаписи.ОсновноеСредство,
	|		ТаблицаПриЗаписи.ОтражатьВРеглУчете,
	|		ТаблицаПриЗаписи.ОтражатьВУпрУчете
	|	ИЗ
	|		РегистрСведений.МестонахождениеОС КАК ТаблицаПриЗаписи
	|			ЛЕВОЕ СОЕДИНЕНИЕ МестонахождениеОСПередЗаписью КАК ТаблицаПередЗаписью
	|			ПО ТаблицаПриЗаписи.ОсновноеСредство = ТаблицаПередЗаписью.ОсновноеСредство
	|				И ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|				И НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, ДЕНЬ) = НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, ДЕНЬ)
	|		ГДЕ
	|			(ТаблицаПередЗаписью.Период ЕСТЬ NULL
	|				ИЛИ ТаблицаПриЗаписи.Местонахождение <> ТаблицаПередЗаписью.Местонахождение)
	|			И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|			И ТаблицаПриЗаписи.ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|
	|	) КАК Таблица
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ МестонахождениеОСПередЗаписью";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
		"МестонахождениеОСИзменение", Выборка.Следующий() И Выборка.Количество > 0);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
