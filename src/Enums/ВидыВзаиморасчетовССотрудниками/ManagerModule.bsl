#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает виды взаиморасчетов, являющимися выплатами зарплаты
//
// Возвращаемое значение:
//  Массив из ПеречислениеСсылка.ВидыВзаиморасчетовССотрудниками
//
Функция Выплаты() Экспорт
	
	Выплаты = Новый Массив;
	
	Выплаты.Добавить(ВыплатаАванса);
	Выплаты.Добавить(ВыплатаВМежрасчетныйПериод);
	Выплаты.Добавить(ВыплатаЗарплаты);
	
	Возврат Выплаты;
	
КонецФункции	

// Возвращает виды взаиморасчетов, являющимися возвратами направленной на выплату зарплаты
//
// Возвращаемое значение:
//  Массив из ПеречислениеСсылка.ВидыВзаиморасчетовССотрудниками
//
Функция Возвраты() Экспорт
	
	Выплаты = Новый Массив;
	
	Выплаты.Добавить(ВозвратАванса);
	Выплаты.Добавить(ВозвратВМежрасчетныйПериод);
	Выплаты.Добавить(ВозвратЗарплаты);
	
	Возврат Выплаты;
	
КонецФункции	

// Проверяет, что указанный вид взаиморасчетов является выплатой зарплаты
//
// Параметры:
//  ВидВзаиморасчетов - ПеречислениеСсылка.ВидыВзаиморасчетовССотрудниками
//
// Возвращаемое значение:
//  Булево
//
Функция ЭтоВыплата(ВидВзаиморасчетов) Экспорт
	Возврат Выплаты().Найти(ВидВзаиморасчетов) <> Неопределено
КонецФункции	

// Проверяет, что указанный вид взаиморасчетов является возвратом направленной на выплату зарплаты
//
// Параметры:
//  ВидВзаиморасчетов - ПеречислениеСсылка.ВидыВзаиморасчетовССотрудниками
//
// Возвращаемое значение:
//  Булево
//
Функция ЭтоВозврат(ВидВзаиморасчетов) Экспорт
	Возврат Возвраты().Найти(ВидВзаиморасчетов) <> Неопределено
КонецФункции	

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает вид взаиморасчетов, являющимся невыплатой для указанной выплаты.
// Для видов взаиморасчетов, не являющихся выплатами, возвращается Неопределено.
//
// Параметры:
//  ВидВзаиморасчетов - ПеречислениеСсылка.ВидыВзаиморасчетовССотрудниками
//
// Возвращаемое значение:
//  ПеречислениеСсылка.ВидыВзаиморасчетовССотрудниками, Неопределено
//
Функция Невыплата(ВидВзаиморасчетов) Экспорт
	
	Если ВидВзаиморасчетов = ВыплатаАванса Тогда
		Возврат ВозвратАванса
	ИначеЕсли ВидВзаиморасчетов = ВыплатаВМежрасчетныйПериод Тогда
		Возврат ВозвратВМежрасчетныйПериод
	ИначеЕсли ВидВзаиморасчетов = ВыплатаЗарплаты Тогда
		Возврат ВозвратЗарплаты
	КонецЕсли;	
	
	Возврат Неопределено
	
КонецФункции	

#КонецОбласти

#КонецЕсли