#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = Новый СписокЗначений;
	
	ДоступныеГруппыВидовДоговоров = ДоступныеПоМетаданным();
	
	Для Каждого ГруппаВидовДоговоров Из ДоступныеГруппыВидовДоговоров Цикл
		ДанныеВыбора.Добавить(ГруппаВидовДоговоров);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Для методов служебного API использование не контролируем          
// АПК:581-выкл 
// АПК:299-выкл

Функция ДоступныеПоМетаданным() Экспорт
	
	ДоступныеЗначения = Новый Массив;
	ДоступныеЗначения.Добавить(Все);
	ДоступныеЗначения.Добавить(РаботникиСлужащие);
	ДоступныеЗначения.Добавить(ПоДоговорамГПХ);
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ГосударственнаяСлужба");	
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
			ДоступныеЗначения,
			Модуль.ГруппыВидовДоговоровССотрудникамДляВыплатыЗарплатыДоступныеПоМетаданным());
	КонецЕсли;	
	
	Возврат ДоступныеЗначения
	
КонецФункции

Функция ДоступныеПоФункциональнымОпциям() Экспорт
	
	ДоступныеПоФО = Новый Массив;
	ДоступныеПоФО.Добавить(Все);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьНачисленияПоДоговорам") Тогда
		ДоступныеПоФО.Добавить(РаботникиСлужащие);
		ДоступныеПоФО.Добавить(ПоДоговорамГПХ);
	КонецЕсли;	
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ГосударственнаяСлужба");	
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
			ДоступныеПоФО,
			Модуль.ГруппыВидовДоговоровССотрудникамДляВыплатыЗарплатыДоступныеПоФункциональнымОпциям());
	КонецЕсли;	
	
	ДоступныеЗначения = Новый Массив;
	Для Каждого Значение Из ДоступныеПоМетаданным() Цикл
		Если ДоступныеПоФО.Найти(Значение) <> Неопределено Тогда
			ДоступныеЗначения.Добавить(Значение);
		КонецЕсли;	
	КонецЦикла;	

	Возврат ДоступныеЗначения
	
КонецФункции

Функция ВидыДоговоровГруппы(ГруппаВидовДоговоров) Экспорт

	ВидыДоговоровГруппы = Новый Массив;

	Если ГруппаВидовДоговоров = Перечисления.ГруппыВидовДоговоровССотрудникамиДляВыплатыЗарплаты.Все Тогда
		ВидыДоговоровГруппы = ВсеВидыДоговоровССотрудникам();
	ИначеЕсли ГруппаВидовДоговоров = Перечисления.ГруппыВидовДоговоровССотрудникамиДляВыплатыЗарплаты.Гражданские Тогда
		ВидыДоговоровГруппы = Перечисления.ВидыДоговоровССотрудниками.ВидыДоговоровКромеВоеннойСлужбы();
	ИначеЕсли ГруппаВидовДоговоров = Перечисления.ГруппыВидовДоговоровССотрудникамиДляВыплатыЗарплаты.РаботникиСлужащие Тогда
		ВидыДоговоровГруппы = Перечисления.ВидыДоговоровССотрудниками.ВидыДоговоровКромеВоеннойСлужбы();
	ИначеЕсли ГруппаВидовДоговоров = Перечисления.ГруппыВидовДоговоровССотрудникамиДляВыплатыЗарплаты.ПоДоговорамГПХ Тогда
		// договорникам не соответствует ни один вид договора с сотрудниками
	ИначеЕсли ГруппаВидовДоговоров = Перечисления.ГруппыВидовДоговоровССотрудникамиДляВыплатыЗарплаты.ВоеннослужащиеПоКонтракту Тогда
		ВидыДоговоровГруппы.Добавить(Перечисления.ВидыДоговоровССотрудниками.КонтрактВоеннослужащего);
	ИначеЕсли ГруппаВидовДоговоров = Перечисления.ГруппыВидовДоговоровССотрудникамиДляВыплатыЗарплаты.ВоеннослужащиеПоПризыву Тогда
		ВидыДоговоровГруппы.Добавить(Перечисления.ВидыДоговоровССотрудниками.ВоеннослужащийПоПризыву);
	КонецЕсли;

	Возврат ВидыДоговоровГруппы

КонецФункции

// АПК:299-вкл
// АПК:581-вкл

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПараметрыПолученияСотрудниковОрганизаций(ПараметрыПолученияСотрудников, ГруппаВидовДоговоров) Экспорт
	
	РаботникиПоТрудовымДоговорам = Неопределено;
	РаботникиПоДоговорамГПХ      = Неопределено;
	Отбор                        = Неопределено;
	ПоляОтбора = "ВидСравнения, ПравоеЗначение";
	
	Если ГруппаВидовДоговоров = Перечисления.ГруппыВидовДоговоровССотрудникамиДляВыплатыЗарплаты.Все Тогда
		РаботникиПоТрудовымДоговорам = Истина;
		РаботникиПоДоговорамГПХ      = Истина;
	ИначеЕсли ГруппаВидовДоговоров = Перечисления.ГруппыВидовДоговоровССотрудникамиДляВыплатыЗарплаты.Гражданские Тогда
		РаботникиПоТрудовымДоговорам = Истина;
		РаботникиПоДоговорамГПХ      = Истина;
		Отбор = Новый Структура(ПоляОтбора, "Не В", Перечисления.ВидыДоговоровССотрудниками.ВидыДоговоровВоеннойСлужбы()); 
	ИначеЕсли ГруппаВидовДоговоров = Перечисления.ГруппыВидовДоговоровССотрудникамиДляВыплатыЗарплаты.РаботникиСлужащие Тогда
		РаботникиПоТрудовымДоговорам = Истина;
		РаботникиПоДоговорамГПХ      = Неопределено;
		Отбор = Новый Структура(ПоляОтбора, "Не В", Перечисления.ВидыДоговоровССотрудниками.ВидыДоговоровВоеннойСлужбы()); 
	ИначеЕсли ГруппаВидовДоговоров = Перечисления.ГруппыВидовДоговоровССотрудникамиДляВыплатыЗарплаты.ПоДоговорамГПХ Тогда
		РаботникиПоТрудовымДоговорам = Ложь;
		РаботникиПоДоговорамГПХ      = Истина;
	ИначеЕсли ГруппаВидовДоговоров = Перечисления.ГруппыВидовДоговоровССотрудникамиДляВыплатыЗарплаты.ВоеннослужащиеПоКонтракту Тогда
		РаботникиПоТрудовымДоговорам = Истина;
		РаботникиПоДоговорамГПХ      = Неопределено;
		Отбор = Новый Структура(ПоляОтбора, "=", Перечисления.ВидыДоговоровССотрудниками.КонтрактВоеннослужащего); 
	ИначеЕсли ГруппаВидовДоговоров = Перечисления.ГруппыВидовДоговоровССотрудникамиДляВыплатыЗарплаты.ВоеннослужащиеПоПризыву Тогда
		РаботникиПоТрудовымДоговорам = Истина;
		РаботникиПоДоговорамГПХ      = Неопределено;
		Отбор = Новый Структура(ПоляОтбора, "=", Перечисления.ВидыДоговоровССотрудниками.ВоеннослужащийПоПризыву); 
	КонецЕсли;
	
	ПараметрыПолученияСотрудников.РаботникиПоТрудовымДоговорам = РаботникиПоТрудовымДоговорам;
	ПараметрыПолученияСотрудников.РаботникиПоДоговорамГПХ      = РаботникиПоДоговорамГПХ;
	
	Если Отбор <> Неопределено Тогда		
		ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(
			ПараметрыПолученияСотрудников.Отборы, 
			"ВидДоговора", Отбор.ВидСравнения, Отбор.ПравоеЗначение); 
	КонецЕсли;		
	
КонецПроцедуры	
	
Функция ВсеВидыДоговоровССотрудникам()

	ВидыДоговоров = Новый Массив;

	Для Каждого ВидДоговора Из Перечисления.ВидыДоговоровССотрудниками Цикл
		ВидыДоговоров.Добавить(ВидДоговора);
	КонецЦикла;

	Возврат ВидыДоговоров

КонецФункции

#КонецОбласти

#КонецЕсли