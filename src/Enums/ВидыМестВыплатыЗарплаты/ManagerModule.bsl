#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Для методов служебного API использование не контролируем          
// АПК:581-выкл 
// АПК:299-выкл

// Возвращает все значения перечисления.
//
// Возвращаемое значение:
//		Массив из ПеречислениеСсылка.ВидыМестВыплатыЗарплаты
//
Функция ВсеЗначения() Экспорт
	
	ВсеЗначения = Новый Массив;
	
	Для Индекс = 0 По Количество()-1 Цикл
		ВсеЗначения.Добавить(Получить(Индекс));
	КонецЦикла;	
	
	Возврат ВсеЗначения
	
КонецФункции	

// Возвращает виды мест выплаты наличными.
//
// Возвращаемое значение:
//		Массив из ПеречислениеСсылка.ВидыМестВыплатыЗарплаты
//
Функция Наличные() Экспорт
	
	Значения = Новый Массив;
	Значения.Добавить(Касса);
	Значения.Добавить(Раздатчик);
	
	Возврат Значения
	
КонецФункции	

// Проверяет, является ли указанный вид места выплаты наличным
// 
// Параметры:
//  ВидМестаВыплаты - ПеречислениеСсылка.ВидыМестВыплатыЗарплаты 
//
// Возвращаемое значение:
//		Булево
//
Функция ЭтоНаличными(ВидМестаВыплаты) Экспорт
	Возврат Перечисления.ВидыМестВыплатыЗарплаты.Наличные().Найти(ВидМестаВыплаты) <> Неопределено
КонецФункции	
	
// Возвращает виды мест выплаты в безналичной форме
//
// Возвращаемое значение:
//		Массив из ПеречислениеСсылка.ВидыМестВыплатыЗарплаты
//
Функция Безналичные() Экспорт
	
	Значения = Новый Массив;
	Значения.Добавить(ЗарплатныйПроект);
	Значения.Добавить(БанковскийСчет);
	
	Возврат Значения
	
КонецФункции	

// Проверяет, является ли указанный вид места выплаты безналичным
// 
// Параметры:
//  ВидМестаВыплаты - ПеречислениеСсылка.ВидыМестВыплатыЗарплаты 
//
// Возвращаемое значение:
//		Булево
//
Функция ЭтоБезналично(ВидМестаВыплаты) Экспорт
	Возврат Перечисления.ВидыМестВыплатыЗарплаты.Безналичные().Найти(ВидМестаВыплаты) <> Неопределено
КонецФункции	

// Возвращает виды мест выплаты, соответствующие указанной форме оплаты
// 
// Параметры:
//  ФормаОплаты - ПеречислениеСсылка.ФормыОплаты
//
// Возвращаемое значение:
//		Массив из ПеречислениеСсылка.ВидыМестВыплатыЗарплаты
//
Функция ПоФормеОплаты(ФормаОплаты) Экспорт
	
	Если ФормаОплаты = Перечисления.ФормыОплаты.Безналичная Тогда
		Возврат Безналичные()
	ИначеЕсли ФормаОплаты = Перечисления.ФормыОплаты.Наличная Тогда	
		Возврат Наличные()
	КонецЕсли	

КонецФункции	

// АПК:299-вкл
// АПК:581-вкл

#КонецОбласти

#КонецЕсли
