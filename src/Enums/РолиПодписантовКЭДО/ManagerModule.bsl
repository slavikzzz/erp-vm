#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Функция ЭтоПодписьСервиса(РолиПодписи) Экспорт
	
	Возврат РолиПодписиСодержатРольПодписантов(
		КоллекцияПроверяемыхРолей(РолиПодписи), РолиПодписантовСервиса());
	
КонецФункции

Функция ЭтоПодписьРуководителя(РолиПодписи) Экспорт
	
	Возврат РолиПодписиСодержатРольПодписантов(
		КоллекцияПроверяемыхРолей(РолиПодписи), РолиРуководителей());
	
КонецФункции

Функция ЭтоПодписьСотрудника(РолиПодписи) Экспорт
	
	Возврат РолиПодписиСодержатРольПодписантов(
		КоллекцияПроверяемыхРолей(РолиПодписи), РолиСотрудников());
	
КонецФункции

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Функция РолиПодписантовСервиса()
	
	РолиПодписантов = РолиРуководителей();
	ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(РолиПодписантов, РолиСотрудников());
	
	Возврат РолиПодписантов
	
КонецФункции

Функция РолиРуководителей()
	
	РолиПодписантов = Новый Соответствие;
	РолиПодписантов.Вставить(НепосредственныйРуководитель, Истина);
	РолиПодписантов.Вставить(РуководителиПоВсемМестамРаботы, Истина);
	РолиПодписантов.Вставить(РуководительПоДополнительнымМестамРаботы, Истина);
	РолиПодписантов.Вставить(ВышестоящийРуководитель, Истина);
	РолиПодписантов.Вставить(НеформальныйРуководитель, Истина);
	
	Возврат РолиПодписантов;
	
КонецФункции

Функция РолиСотрудников()
	
	РолиПодписантов = Новый Соответствие;
	РолиПодписантов.Вставить(Сотрудник, Истина);
	
	Возврат РолиПодписантов;
	
КонецФункции

Функция РолиПодписиСодержатРольПодписантов(РолиПодписи, РолиПодписантов)
	
	Если ТипЗнч(РолиПодписи) = Тип("ПеречислениеСсылка.РолиПодписантовКЭДО") Тогда
		ПроверяемыеРолиРолиПодписи = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(РолиПодписи);
	Иначе
		ПроверяемыеРолиРолиПодписи = РолиПодписи;
	КонецЕсли;
	
	
	Для Каждого РольПодписанта Из ПроверяемыеРолиРолиПодписи Цикл
		Если РолиПодписантов.Получить(РольПодписанта) = Истина Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

Функция КоллекцияПроверяемыхРолей(РолиПодписи)
	
	Если ТипЗнч(РолиПодписи) = Тип("ПеречислениеСсылка.РолиПодписантовКЭДО") Тогда
		Возврат ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(РолиПодписи);
	КонецЕсли;
	
	Возврат РолиПодписи;
	
КонецФункции

#КонецОбласти


#КонецЕсли