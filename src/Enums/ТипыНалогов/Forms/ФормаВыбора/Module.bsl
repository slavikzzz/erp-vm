
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
//++ Локализация

//++ НЕ УТ

	ПараметрыОтбора = Параметры.Отбор;
	
	Период = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОтбора, "Период");
	Если Не ЗначениеЗаполнено(Период) Тогда
		Период = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	КонецЕсли;
	
	ВидОперации = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОтбора, "ВидОперации", "");
	
	Если ВидОперации = "Начисление" Тогда
		
		НедоступныеВидыНалогов = Новый Массив;
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(НедоступныеВидыНалогов,
			ЕдиныйНалоговыйСчет.ВидыНалоговУплачиваемыеОтдельно(Период));
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(НедоступныеВидыНалогов,
			ЕдиныйНалоговыйСчет.ВидыНалоговУстаревшие(Период), Истина);
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
			"Ссылка", НедоступныеВидыНалогов, ВидСравненияКомпоновкиДанных.НеВСписке, , Истина,
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
		
	ИначеЕсли ВидОперации = "Уведомление" Тогда
		
		НедоступныеВидыНалогов = Новый Массив;
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(НедоступныеВидыНалогов,
			ЕдиныйНалоговыйСчет.ВидыНалоговУплачиваемыеОтдельно(Период));
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(НедоступныеВидыНалогов,
			ЕдиныйНалоговыйСчет.ВидыНалоговУплачиваемыеБезУведомлений(), Истина);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(НедоступныеВидыНалогов,
			ЕдиныйНалоговыйСчет.ВидыНалоговУстаревшие(Период), Истина);
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
			"Ссылка", НедоступныеВидыНалогов, ВидСравненияКомпоновкиДанных.НеВСписке, , Истина,
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
		
	ИначеЕсли ВидОперации = "Оплата" Тогда
		
		ДоступныеВидыНалогов = Новый Массив;
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ДоступныеВидыНалогов,
			ЕдиныйНалоговыйСчет.ВидыНалоговУплачиваемыеОтдельно(Период));
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
			"Ссылка", ДоступныеВидыНалогов, ВидСравненияКомпоновкиДанных.ВСписке, , Истина,
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
		
	КонецЕсли;
	
//-- НЕ УТ

//-- Локализация

	Элементы.ФормаВыбратьИзПолногоСписка.Видимость = ЗначениеЗаполнено(Параметры.Отбор);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьИзПолногоСписка(Команда)
	ВыбратьИзПолногоСпискаНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ВыбратьИзПолногоСпискаНаСервере()
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ФормаВыбратьИзПолногоСписка",
		"Видимость",
		Ложь);
		
	ОтключитьЭлементыНастроек(Список.КомпоновщикНастроек.Настройки.Отбор.Элементы);
	ОтключитьЭлементыНастроек(Список.КомпоновщикНастроек.ФиксированныеНастройки.Отбор.Элементы);
	ОтключитьЭлементыНастроек(Список.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы);
	
КонецПроцедуры

&НаСервере
Процедура ОтключитьЭлементыНастроек(КоллекцияЭлементов)
	
	Для Каждого ЭлементНастроек Из КоллекцияЭлементов Цикл
		
		Если ТипЗнч(ЭлементНастроек) = Тип("ЭлементОтбораКомпоновкиДанных")
			Или ТипЗнч(ЭлементНастроек) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			
			ЭлементНастроек.Использование = Ложь;
		ИначеЕсли ТипЗнч(ЭлементНастроек) = Тип("КоллекцияЭлементовОтбораКомпоновкиДанных") Тогда
			ОтключитьЭлементыНастроек(ЭлементНастроек.Элементы)
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти