#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = Новый СписокЗначений;
	
	ДоступныеВидыДокументовОснований = ДоступныеПоМетаданным();
	
	Для Каждого ВидДокументаОснования Из ДоступныеВидыДокументовОснований Цикл
		ДанныеВыбора.Добавить(ВидДокументаОснования);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Для методов служебного API использование не контролируем          
// АПК:581-выкл 
// АПК:299-выкл

Функция ДоступныеПоМетаданным() Экспорт
	
	ДоступныеЗначения = Новый Массив;
	
	Для Каждого Значение Из Метаданные.Перечисления.ВидыДокументовОснованийВедомостейНаВыплатуЗарплаты.ЗначенияПеречисления Цикл
		Если Метаданные.Документы.Найти(Значение.Имя) <> Неопределено Тогда
			ДоступныеЗначения.Добавить(Перечисления.ВидыДокументовОснованийВедомостейНаВыплатуЗарплаты[Значение.Имя])
		КонецЕсли	
	КонецЦикла;
	
	Возврат ДоступныеЗначения
	
КонецФункции

Функция ДоступныеПоФункциональнымОпциям() Экспорт
	
	ДоступныеЗначения = Новый Массив;
	
	ДоступныеПоМетаданным = ДоступныеПоМетаданным();
	Для Каждого Значение Из Метаданные.Перечисления.ВидыДокументовОснованийВедомостейНаВыплатуЗарплаты.ЗначенияПеречисления Цикл
		Если Метаданные.Документы.Найти(Значение.Имя) <> Неопределено Тогда
			Если ОбщегоНазначения.ОбъектМетаданныхДоступенПоФункциональнымОпциям(Метаданные.Документы[Значение.Имя]) Тогда
				ДоступныеЗначения.Добавить(Перечисления.ВидыДокументовОснованийВедомостейНаВыплатуЗарплаты[Значение.Имя])
			КонецЕсли	
		КонецЕсли	
	КонецЦикла;
	
	Возврат ДоступныеЗначения
	
КонецФункции

// АПК:299-вкл
// АПК:581-вкл

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает значение перечисления, соответствующее переданному документу.
//
// Параметры:
//	Документ - ДокументОбъект, ДокументСсылка.
//
// Возвращаемое значение:
//	ПеречислениеСсылка.ВидыДокументовОснованийВедомостейНаВыплатуЗарплаты
//
Функция ПоДокументу(Документ) Экспорт
	
	ВидДокумента = Неопределено;
	
	ИмяДокумента = Документ.Метаданные().Имя;
	ИмяДокумента = СтрЗаменить(ИмяДокумента, "Списком", ""); // если "списком", то это тоже самое
		
	ЗначенияПеречисления = Метаданные.Перечисления.ВидыДокументовОснованийВедомостейНаВыплатуЗарплаты.ЗначенияПеречисления;
	ОбъектМетаданных = ЗначенияПеречисления.Найти(ИмяДокумента);
	Если ОбъектМетаданных <> Неопределено Тогда
		ИндексЗначения = ЗначенияПеречисления.Индекс(ОбъектМетаданных);
		Если ИндексЗначения <> -1 Тогда
			ВидДокумента = Перечисления.ВидыДокументовОснованийВедомостейНаВыплатуЗарплаты[ИндексЗначения];
		КонецЕсли	
	КонецЕсли;	
	
	Возврат ВидДокумента;
	
КонецФункции

// Возвращает представление списка документа, соответствующего переданному значению перечисления.
//
// Параметры:
//   ВидДокумента      - ПеречислениеСсылка.ВидыДокументовОснованийВедомостейНаВыплатуЗарплаты - вид документа.
//   РежимДоначисления - Булево - режим доначисления, имеет смысл только для НачислениеЗарплаты
//
// Возвращаемое значение: 
//  Строка - представление списка документов.
//
Функция ПредставлениеСпискаДокумента(ВидДокумента, РежимДоначисления = Ложь) Экспорт
	
	ОбъектМетаданных = Метаданные.Документы.Найти(ОбщегоНазначения.ИмяЗначенияПеречисления(ВидДокумента));
	
	Если ОбъектМетаданных = Метаданные.Документы.НачислениеЗарплаты И РежимДоначисления Тогда
		ПредставлениеСписка = НСтр("ru = 'Доначисления зарплаты';
									|en = 'Accrue salary additionally '")
	Иначе
		Если Не ПустаяСтрока(ОбъектМетаданных.РасширенноеПредставлениеСписка) Тогда
			ПредставлениеСписка = ОбъектМетаданных.РасширенноеПредставлениеСписка;
		ИначеЕсли Не ПустаяСтрока(ОбъектМетаданных.ПредставлениеСписка) Тогда
			ПредставлениеСписка = ОбъектМетаданных.ПредставлениеСписка;
		Иначе 
			ПредставлениеСписка = ОбъектМетаданных.Представление();
		КонецЕсли;		
	КонецЕсли;
	
	Возврат ПредставлениеСписка
	
КонецФункции	

// Возвращает метаданные документов, соответствующих переданному значению перечисления.
//
// Параметры:
//   ВидДокумента - ПеречислениеСсылка.ВидыДокументовОснованийВедомостейНаВыплатуЗарплаты - вид документа.
//
// Возвращаемое значение: 
//  Массив из ОбъектМетаданных - метаданные документов.
//
Функция МетаданныеДокументов(ВидДокумента) Экспорт
	
	ИмяВидаДокумента = ОбщегоНазначения.ИмяЗначенияПеречисления(ВидДокумента);
	ИменаВозможныхДокументов = Новый Массив;
	ИменаВозможныхДокументов.Добавить(ИмяВидаДокумента);
	ИменаВозможныхДокументов.Добавить(ИмяВидаДокумента + "Списком"); // АПК:1297 не локализуется, часть имени документа
	
	МетаданныеДокументов = Новый Массив;
	Для Каждого ИмяВозможногоДокумента Из ИменаВозможныхДокументов Цикл
		МетаданныеДокумента = Метаданные.Документы.Найти(ИмяВозможногоДокумента);
		Если МетаданныеДокумента <> Неопределено Тогда
			МетаданныеДокументов.Добавить(МетаданныеДокумента)
		КонецЕсли;
	КонецЦикла;	

    Возврат МетаданныеДокументов
	
КонецФункции

#КонецОбласти

#КонецЕсли