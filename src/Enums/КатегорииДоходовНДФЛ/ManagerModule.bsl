#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	ДоступныеЗначения = ВсеЗначения();
	Если Параметры.Отбор.Свойство("Ссылка") Тогда
		СписокОтбора = Новый Массив;
		Если ТипЗнч(Параметры.Отбор.Ссылка) = Тип("ПеречислениеСсылка.КатегорииДоходовНДФЛ") Тогда
			Если ДоступныеЗначения.Найти(Параметры.Отбор.Ссылка) <> Неопределено Тогда
				СписокОтбора.Добавить(Параметры.Отбор.Ссылка);
			КонецЕсли;
		Иначе
			Для Каждого ЗначениеОтбора Из Параметры.Отбор.Ссылка Цикл
				Если ДоступныеЗначения.Найти(ЗначениеОтбора) <> Неопределено Тогда
					СписокОтбора.Добавить(ЗначениеОтбора);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		Параметры.Отбор.Вставить("Ссылка", СписокОтбора);
	Иначе
		Параметры.Отбор.Вставить("Ссылка", ДоступныеЗначения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

// АПК:299-выкл 
// АПК:581-выкл 
#Область СлужебныйПрограммныйИнтерфейс

// Возвращает все доступные категории доходов НДФЛ.
//
// Возвращаемое значение:
//    Массив - Значения типа ПеречислениеСсылка.КатегорииДоходовНДФЛ
//
Функция ВсеЗначения() Экспорт
	
	Категории = ПоОсновнойСтавке();
	
	КатегорииДивидендов = Дивиденды();
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Категории, КатегорииДивидендов, Истина);
	
	КатегорииАвторскиеРоялти = АвторскиеРоялти();
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Категории, КатегорииАвторскиеРоялти);
	
	Категории.Добавить(ПрочиеДоходыПоСтавкеПункта11Статьи224НКРФ);

	Возврат Категории;
	
КонецФункции

// Возвращает категории доходов НДФЛ по дивидендам.
//
// Возвращаемое значение:
//    Массив - Значения типа ПеречислениеСсылка.КатегорииДоходовНДФЛ
//
Функция Дивиденды() Экспорт
	
	Категории = Новый Массив;
	Категории.Добавить(Дивиденды);
	
	Если ПолучитьФункциональнуюОпцию("ИспользуютсяСтавкиНДФЛМеждународныхДоговоров") Тогда
		Категории.Добавить(ДивидендыПоСтавке05);
		Категории.Добавить(ДивидендыПоСтавке10);
		Категории.Добавить(ДивидендыПоСтавке12);
	КонецЕсли;
	
	Возврат Категории;
	
КонецФункции

// Возвращает категории доходов НДФЛ по ценным бумагам и авторским вознаграждениям,
// исчисляемых по ставкам предусмотренными международными договорами РФ об избежании
// двойного налогообложения.
//
// Возвращаемое значение:
//    Массив - Значения типа ПеречислениеСсылка.КатегорииДоходовНДФЛ
//
Функция АвторскиеРоялти() Экспорт
	
	Категории = Новый Массив;
	
	Если ПолучитьФункциональнуюОпцию("ИспользуютсяСтавкиНДФЛМеждународныхДоговоров") Тогда
		Категории.Добавить(АвторскиеРоялтиПоСтавке03);
		Категории.Добавить(АвторскиеРоялтиПоСтавке06);
		Категории.Добавить(АвторскиеРоялтиПроцентыПоСтавке05);
		Категории.Добавить(АвторскиеРоялтиПроцентыПоСтавке07);
		Категории.Добавить(АвторскиеРоялтиПроцентыПоСтавке10);
		Категории.Добавить(АвторскиеРоялтиПроцентыПоСтавке15);
	КонецЕсли;
	
	Возврат Категории;
	
КонецФункции

// Возвращает категории доходов НДФЛ, для которых не переносятся даты получения дохода
//
// Возвращаемое значение:
//    Массив - Значения типа ПеречислениеСсылка.КатегорииДоходовНДФЛ
//
Функция СФиксированнойДатойПолученияДохода(ДатаПолученияДохода = '20000101') Экспорт
	
	Категории = Новый Массив;
	Категории.Добавить(ДоходВНатуральнойФормеОтТрудовойДеятельности);
	Категории.Добавить(ПрочиеНатуральныеДоходы);
	Категории.Добавить(ПрочиеДоходыПоСтавкеПункта11Статьи224НКРФ);
	Если ДатаПолученияДохода < УчетНДФЛ.ДатаЗакона263ФЗ() Тогда
		Категории.Добавить(ОплатаТруда);
		Категории.Добавить(ДоходыПредыдущихРедакций);
	КонецЕсли;
	Категории.Добавить(ДовольствиеСВОвНатуральнойФорме);
	
	Возврат Категории;
	
КонецФункции

// Возвращает категории доходов НДФЛ по оплате труда.
//
// Возвращаемое значение:
//    Массив - Значения типа ПеречислениеСсылка.КатегорииДоходовНДФЛ
//
Функция ОплатаТруда() Экспорт
	
	Категории = Новый Массив;
	Категории.Добавить(ОплатаТруда);
	Категории.Добавить(ОплатаТрудаАУСН);
	Категории.Добавить(ДовольствиеСВОвДенежнойФорме);
	Категории.Добавить(РайонныеСеверныеНадбавки);
	Категории.Добавить(РайонныеСеверныеНадбавкиАУСН);
	
	Возврат Категории;
	
КонецФункции

// Возвращает категории доходов, используемые при АУСН.
//
// Возвращаемое значение:
//    Массив - Значения типа ПеречислениеСсылка.КатегорииДоходовНДФЛ
//
Функция ДоходыАУСН() Экспорт
	
	Категории = Новый Массив;
	Категории.Добавить(ОплатаТрудаАУСН);
	Категории.Добавить(РайонныеСеверныеНадбавкиАУСН);
	Категории.Добавить(ПрочиеДоходыАУСН);
	
	Возврат Категории;
	
КонецФункции

// Возвращает категории доходов НДФЛ по натуральным доходам.
//
// Возвращаемое значение:
//    Массив - Значения типа ПеречислениеСсылка.КатегорииДоходовНДФЛ
//
Функция Натуральные() Экспорт
	
	Категории = Новый Массив;
	Категории.Добавить(ДоходВНатуральнойФормеОтТрудовойДеятельности);
	Категории.Добавить(ПрочиеНатуральныеДоходы);
	Категории.Добавить(ПрочиеДоходыПоСтавкеПункта11Статьи224НКРФ);
	Категории.Добавить(ДовольствиеСВОвНатуральнойФорме);
	
	Возврат Категории;
	
КонецФункции

// Возвращает категории все доходов, кроме оплаты труда и натуральных доходов.
//
// Возвращаемое значение:
//    Массив - Значения типа ПеречислениеСсылка.КатегорииДоходовНДФЛ
//
Функция Прочие() Экспорт
	
	НеПрочие = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(НеПрочие, ОплатаТруда());
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(НеПрочие, Натуральные());

	Категории = ОбщегоНазначенияКлиентСервер.РазностьМассивов(ВсеЗначения(), НеПрочие); 
	
	Возврат Категории;
	
КонецФункции

// Возвращает категории доходов НДФЛ от трудовой деятельности.
//
// Возвращаемое значение:
//    Массив - Значения типа ПеречислениеСсылка.КатегорииДоходовНДФЛ
//
Функция ДоходыОтТрудовойДеятельности() Экспорт
	
	Категории = Новый Массив;
	Категории.Добавить(ОплатаТруда);
	Категории.Добавить(ОплатаТрудаАУСН);
	Категории.Добавить(ДоходыПредыдущихРедакций);
	Категории.Добавить(ПрочиеДоходыВДенежнойФормеОтТрудовойДеятельности);
	Категории.Добавить(ДоходВНатуральнойФормеОтТрудовойДеятельности);
	Категории.Добавить(ДовольствиеСВОвДенежнойФорме);
	Категории.Добавить(ДовольствиеСВОвНатуральнойФорме);
	Категории.Добавить(РайонныеСеверныеНадбавки);
	Категории.Добавить(РайонныеСеверныеНадбавкиАУСН);
	
	Возврат Категории;
	
КонецФункции

// Налоговые базы до 2025 года

// Возвращает категории доходов НДФЛ, за исключением категорий доходов,
// исчисляемых по ставкам предусмотренными международными договорами РФ 
// об избежании двойного налогообложения.
//
// Возвращаемое значение:
//    Массив - Значения типа ПеречислениеСсылка.КатегорииДоходовНДФЛ
//
Функция ПоОсновнойСтавке() Экспорт
	
	Категории = Новый Массив;
	Категории.Добавить(ОплатаТруда);
	Категории.Добавить(ОплатаТрудаАУСН);
	Категории.Добавить(ДоходыПредыдущихРедакций);
	Категории.Добавить(ДоходВНатуральнойФормеОтТрудовойДеятельности);
	Категории.Добавить(ПрочиеНатуральныеДоходы);
	Категории.Добавить(ПрочиеДоходыВДенежнойФормеОтТрудовойДеятельности);
	Категории.Добавить(ПрочиеДоходы);
	Категории.Добавить(ПрочиеДоходыАУСН);
	Категории.Добавить(ДовольствиеСВОвДенежнойФорме);
	Категории.Добавить(ДовольствиеСВОвНатуральнойФорме);
	Категории.Добавить(РайонныеСеверныеНадбавки);
	Категории.Добавить(РайонныеСеверныеНадбавкиАУСН);
	// Категорию Дивиденды исключили из-за неясности в толковании п. 3 ст. 226 
	Категории.Добавить(ПрочиеДоходыОтДолевогоУчастия);
	Категории.Добавить(ПрочиеДоходыПоВыигрышам);
	Категории.Добавить(ПрочиеДоходыЦБ);
	Категории.Добавить(ПрочиеДоходыРЕПО);
	Категории.Добавить(ПрочиеДоходыЗаймЦБ);
	Категории.Добавить(ПрочиеДоходыПоСтрахованиюИПенсОбеспечению);
	Категории.Добавить(ПрочиеДоходыЦФА);
	Категории.Добавить(ПрочиеДоходыИИС);
	Категории.Добавить(ПрочиеДоходыПоОблигациям);
	// Категория ПрочиеДоходыПоСтавкеПункта11Статьи224НКРФ облагается по ставке 13%, но не по "основной", ее везде рассматриваем отдельно.
	
	Возврат Категории;
	
КонецФункции

// Возвращает категории доходов, составляющие основную налоговую базу.
//
// Возвращаемое значение:
//    Массив - Значения типа ПеречислениеСсылка.КатегорииДоходовНДФЛ
//
Функция ОсновнаяНалоговаяБаза() Экспорт
	
	Категории = Новый Массив;
	Категории.Добавить(ОплатаТруда);
	Категории.Добавить(ОплатаТрудаАУСН);
	Категории.Добавить(ДоходыПредыдущихРедакций);
	Категории.Добавить(ПрочиеДоходыВДенежнойФормеОтТрудовойДеятельности);
	Категории.Добавить(ДоходВНатуральнойФормеОтТрудовойДеятельности);
	Категории.Добавить(ПрочиеНатуральныеДоходы);
	Категории.Добавить(ПрочиеДоходы);
	Категории.Добавить(ПрочиеДоходыАУСН);
	Категории.Добавить(ДовольствиеСВОвДенежнойФорме);
	Категории.Добавить(ДовольствиеСВОвНатуральнойФорме);
	Категории.Добавить(РайонныеСеверныеНадбавки);
	Категории.Добавить(РайонныеСеверныеНадбавкиАУСН);
	
	Возврат Категории;
	
КонецФункции

// Налоговые базы с 2025 года

// Возвращает категории доходов, составляющие налоговую базу. Представитель базы - ОплатаТруда
//
// Возвращаемое значение:
//    Массив - Значения типа ПеречислениеСсылка.КатегорииДоходовНДФЛ
//
Функция БазаПоСтавкеПункта1Статьи224() Экспорт
	
	Категории = Новый Массив;
	Категории.Добавить(ОплатаТруда);
	Категории.Добавить(ОплатаТрудаАУСН);
	Категории.Добавить(ДоходыПредыдущихРедакций);
	Категории.Добавить(ПрочиеДоходыВДенежнойФормеОтТрудовойДеятельности);
	Категории.Добавить(ДоходВНатуральнойФормеОтТрудовойДеятельности);
	Категории.Добавить(ПрочиеНатуральныеДоходы);
	Категории.Добавить(ПрочиеДоходы);
	Категории.Добавить(ПрочиеДоходыАУСН);
	
	Возврат Категории;
	
КонецФункции

// Возвращает категории доходов, составляющие налоговую базу. Представитель базы - ПрочиеДоходыЦБ
//
// Возвращаемое значение:
//    Массив - Значения типа ПеречислениеСсылка.КатегорииДоходовНДФЛ
//
Функция БазаПоСтавкеПункта1_1Статьи224() Экспорт
	
	Категории = Новый Массив;
	Категории.Добавить(ПрочиеДоходыОтДолевогоУчастия);
	Категории.Добавить(ПрочиеДоходыПоВыигрышам);
	Категории.Добавить(ПрочиеДоходыЦБ);
	Категории.Добавить(ПрочиеДоходыРЕПО);
	Категории.Добавить(ПрочиеДоходыЗаймЦБ);
	Категории.Добавить(ПрочиеДоходыПоСтрахованиюИПенсОбеспечению);
	Категории.Добавить(ПрочиеДоходыЦФА);
	Категории.Добавить(ПрочиеДоходыИИС);
	Категории.Добавить(ПрочиеДоходыПоОблигациям);
	Категории.Добавить(ПрочиеДоходыПоСтавкеПункта11Статьи224НКРФ);
	Возврат Категории;
	
КонецФункции

// Возвращает категории доходов, составляющие налоговую базу. Представитель базы - РайонныеСеверныеНадбавки
//
// Возвращаемое значение:
//    Массив - Значения типа ПеречислениеСсылка.КатегорииДоходовНДФЛ
//
Функция БазаПоСтавкеПункта1_2Статьи224() Экспорт
	
	Категории = Новый Массив;
	Категории.Добавить(ДовольствиеСВОвДенежнойФорме);
	Категории.Добавить(ДовольствиеСВОвНатуральнойФорме);
	Категории.Добавить(РайонныеСеверныеНадбавки);
	Категории.Добавить(РайонныеСеверныеНадбавкиАУСН);
	Возврат Категории;
	
КонецФункции

// Возвращает категории доходов, составляющие налоговую базу в соответстии с пунктамм 1, 1.1, 1.2 статьи 224 НК РФ
// Возвращаемое значение:
//    Массив - Значения типа ПеречислениеСсылка.КатегорииДоходовНДФЛ
//
Функция БазаПоСтавкеПункта1иВложенныхПунктовСтатьи224() Экспорт
	
	Категории = БазаПоСтавкеПункта1Статьи224();
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Категории, БазаПоСтавкеПункта1_1Статьи224());
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Категории, БазаПоСтавкеПункта1_2Статьи224());
	
	Возврат Категории;
	
КонецФункции

// Возвращает категории доходов, к которым применимы социальные, имущественные, личные вычеты
//
// Возвращаемое значение:
//    Массив - Значения типа ПеречислениеСсылка.КатегорииДоходовНДФЛ
//
Функция КатегорииСПрименениемВычетов() Экспорт
	
	Категории = БазаПоСтавкеПункта1Статьи224();
	Категории.Добавить(ПрочиеДоходыПоСтавкеПункта11Статьи224НКРФ); // по ценным бумагам вычеты не предоставляются
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Категории, БазаПоСтавкеПункта1_2Статьи224());
	
	Возврат Категории;
	
КонецФункции

#КонецОбласти

#КонецЕсли