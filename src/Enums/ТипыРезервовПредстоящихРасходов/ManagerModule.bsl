#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает значения перечисления, соответствующие назначению использования.
//
// Параметры:
//   КлючНазначенияИспользования - Строка - назначение использования резервов
//
// Возвращаемое значение:
//   Массив - элементы массива - ПеречислениеСсылка.ТипыРезервовПредстоящихРасходов
//
Функция ТипыРезервовПоНазначению(КлючНазначенияИспользования) Экспорт
	
	Если КлючНазначенияИспользования = "РезервыПредстоящихРасходов" Тогда
		ТипыРезервов = РезервыПредстоящихРасходов();
	ИначеЕсли КлючНазначенияИспользования = "РезервыПоОплатеТруда" Тогда
		ТипыРезервов = РезервыПоОплатеТруда();
	Иначе
		ТипыРезервов = ВсеТипыРезервов();
	КонецЕсли;
	
	Возврат ТипыРезервов;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("КлючНазначенияИспользования") Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	Если Параметры.КлючНазначенияИспользования = "РезервыПоОплатеТруда" Тогда
		СписокВыбора = РезервыПоОплатеТруда();
	Иначе
		СписокВыбора = РезервыПредстоящихРасходов();
	КонецЕсли;
	
	ДанныеВыбора.ЗагрузитьЗначения(СписокВыбора);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ВсеТипыРезервов()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ Ссылка ИЗ Перечисление.ТипыРезервовПредстоящихРасходов";
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат Результат.ВыгрузитьКолонку("Ссылка");
	
КонецФункции

Функция РезервыПредстоящихРасходов()

	ТипыРезервов = Новый Массив;
	
	ТипыРезервов.Добавить(ЛиквидационныеОбязательства);
	
	Если НЕ ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения") Тогда
		ТипыРезервов.Добавить(ОплатаТруда);
	КонецЕсли;
	
	ТипыРезервов.Добавить(Прочие);
	
	Возврат ТипыРезервов;

КонецФункции

Функция РезервыПоОплатеТруда()

	ТипыРезервов = Новый Массив;
	
	ТипыРезервов.Добавить(ОплатаТруда);
	
	Возврат ТипыРезервов;

КонецФункции

#КонецОбласти

#КонецЕсли
