
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает текст подсказки по источнику состава наборов
//
// Параметры:
//  Источник - ПеречислениеСсылка.ИсточникиСоставаНаборовВПроизводстве
// 
// Возвращаемое значение:
//  Строка - подсказка по по источнику состава наборов.
//
Функция ПодсказкаПоИсточникуСоставаНаборов(Источник) Экспорт
	
	ТекстПодсказки =  "";
	
	//++ НЕ УТКА
	ТекстОграничения = "";
	
	Если Источник = Перечисления.ИсточникиСоставаНаборовВПроизводстве.Спецификации Тогда
		
		ТекстПодсказки   = НСтр("ru = 'Перечень комплектующих задается в спецификациях. Можно использовать возможности параметризации, создавать многоуровневые наборы комплектующих.';
								|en = 'Specify a component list in bills of materials. You can use parametrization and create multi-level components.'");
		ТекстОграничения = Нстр("ru = 'Не поддерживается для Маршрутных карт.';
								|en = 'Not supported for route sheets.'");
		
	ИначеЕсли Источник = Перечисления.ИсточникиСоставаНаборовВПроизводстве.СпецификацииИлиВариантыКомплектации Тогда
		
		ТекстПодсказки   = Нстр("ru = 'При наличии действующей спецификации - состав определяется спецификацией, иначе - вариантом комплектации номенклатуры.';
								|en = 'The content is determined by the bill of materials if there is a valid bill of materials, otherwise, by the item kit configuration.'");
		ТекстОграничения = Нстр("ru = 'Составы, заданные в спецификациях, не поддерживаются для Маршрутных карт.';
								|en = 'Contents specified in the bills of materials are not supported for route sheets.'");
		
	ИначеЕсли Источник = Перечисления.ИсточникиСоставаНаборовВПроизводстве.ВариантыКомплектации Тогда
		
		ТекстПодсказки = НСтр("ru = 'Перечень комплектующих задается в вариантах комплектации номенклатуры. Варианты комплектации можно использовать как в производственных, так и в торговых документах.';
								|en = 'Specify a component list in item kit configurations. You can use kit configurations both in production and trade documents.'");
		
	КонецЕсли;
	
	Если НЕ ПолучитьФункциональнуюОпцию("ХранитьОперацииВРесурсныхСпецификациях") Тогда
		ТекстПодсказки = ТекстПодсказки + " " + ТекстОграничения;
	КонецЕсли;
	//-- НЕ УТКА
	
	Возврат ТекстПодсказки;
	
КонецФункции

// Заполняет список выбора источника состава набора
// 
// Параметры:
//  Список - СписокЗначений из ПеречислениеСсылка.ИсточникиСоставаНаборовВПроизводстве
Процедура ЗаполнитьСписокВыбораИсточниковСоставаНаборов(Список) Экспорт
	
	Список.Очистить();
	
	//++ НЕ УТКА
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеПроизводством2_2")
			И НЕ ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеПроизводством") Тогда
		Список.Добавить(Перечисления.ИсточникиСоставаНаборовВПроизводстве.Спецификации, НСтр("ru = 'спецификациями';
																							|en = 'bills of materials'"));
		Список.Добавить(Перечисления.ИсточникиСоставаНаборовВПроизводстве.СпецификацииИлиВариантыКомплектации, НСтр("ru = 'спецификациями или вариантами комплектации';
																													|en = 'bills of materials or kit configurations'"));
	КонецЕсли;
	//-- НЕ УТКА
	
	Список.Добавить(Перечисления.ИсточникиСоставаНаборовВПроизводстве.ВариантыКомплектации, НСтр("ru = 'вариантами комплектации';
																								|en = 'kit configurations'"));
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
