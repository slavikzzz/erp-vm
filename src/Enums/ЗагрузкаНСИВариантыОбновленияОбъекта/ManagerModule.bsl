
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Формирует список значений из вариантов обновления объекта.
// 
// Параметры:
//  Включить - Строка - перечень включаемых значений
//  Исключить - Строка - перечень исключаемых значений
// 
// Возвращаемое значение:
//  СписокЗначений из ПеречислениеСсылка.ЗагрузкаНСИВариантыОбновленияОбъекта
//
Функция СписокЗначений(Включить = "", Исключить = "") Экспорт
	
	Результат = Новый СписокЗначений;
	
	СтруктураВключить  = Новый Структура(Включить);
	СтруктураИсключить = Новый Структура(Исключить);
	
	Для каждого Способ Из Перечисления.ЗагрузкаНСИВариантыОбновленияОбъекта Цикл
		
		Имя = XmlСтрока(Способ);
		
		Если СтруктураИсключить.Свойство(Имя) Тогда
			Продолжить;
		КонецЕсли;
		
		Если СтруктураВключить.Количество() > 0 И НЕ СтруктураВключить.Свойство(Имя) Тогда
			Продолжить;
		КонецЕсли;
		
		Результат.Добавить(Способ, Способ);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли