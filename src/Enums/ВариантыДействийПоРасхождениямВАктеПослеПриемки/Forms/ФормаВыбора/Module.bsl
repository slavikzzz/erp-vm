
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РасхожденияСервер.УправлениеСтраницамиВыборДействия(ЭтотОбъект, Параметры);
	
	ИспользоватьЗаказыПоставщикам = ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыПоставщикам");
	КоличествоУпаковокРасхождения = Параметры.КоличествоУпаковокРасхождения;
	ГрупповоеИзменение = Параметры.ГрупповоеИзменение;
	
	Если Параметры.ВыбранноеДействие = Перечисления.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОтнестиНедостачуНаПрочиеРасходы
		Или (Параметры.Свойство("ДействиеНедостачи")
			И Параметры.ДействиеНедостачи = Перечисления.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОтнестиНедостачуНаПрочиеРасходы) Тогда
		ЗаЧейСчет = ?(Параметры.ПоВинеСтороннейКомпании, "ПоВинеСтороннейКомпании", "ЗаНашСчет");
	КонецЕсли;
	
	ХозяйственныеОперацииРаздельнойЗакупки = ЗакупкиСервер.ХозяйственныеОперацииРаздельнойЗакупкиБезОтборов();
	Если Параметры.Свойство("ХозяйственнаяОперация") Тогда
		ЭтоРаздельнаяЗакупка = ХозяйственныеОперацииРаздельнойЗакупки.Найти(Параметры.ХозяйственнаяОперация) <> Неопределено;
	Иначе
		ЭтоРаздельнаяЗакупка = Ложь;
	КонецЕсли;
	
	Если Не ГрупповоеИзменение Тогда
		Если КоличествоУпаковокРасхождения >= 0 Тогда
			Элементы.Недостачи.Видимость = Ложь;
			Заголовок = НСтр("ru = 'Как отработать излишек';
							|en = 'Surplus treatment'");
		Иначе
			Элементы.Излишки.Видимость = Ложь;
			Заголовок = НСтр("ru = 'Как отработать недостачу';
							|en = 'Shortage treatment'");
		КонецЕсли;
	Иначе
		Заголовок = НСтр("ru = 'Как отработать расхождения';
						|en = 'Discrepancy treatment'");
	КонецЕсли;
	
	АктПриемкиНаХранение = Параметры.ТипАкта = Перечисления.ТипыОснованияАктаОРасхождении.ПриемкаТоваровНаХранение
							//++ НЕ УТ
							Или Параметры.ТипАкта = Перечисления.ТипыОснованияАктаОРасхождении.ПоступлениеТоваровОтПереработчика
							Или Параметры.ТипАкта = Перечисления.ТипыОснованияАктаОРасхождении.ВозвратТоваровОтПереработчика
							//-- НЕ УТ

							//++ НЕ УТКА
							Или Параметры.ТипАкта = Перечисления.ТипыОснованияАктаОРасхождении.ПоступлениеОтДавальца
							//-- НЕ УТКА
							Или Параметры.ТипАкта = Перечисления.ТипыОснованияАктаОРасхождении.ВозвратТоваровОтХранителя;
	
	Элементы.ПоВинеСтороннейКомпании.Видимость          = Не АктПриемкиНаХранение;
	Элементы.ПоВинеСтороннейКомпанииДекорация.Видимость = Не АктПриемкиНаХранение;
	
	Элементы.КартинкаПояснения.Видимость = Параметры.ПоказыватьПояснение;
	Элементы.Пояснение.Видимость         = Параметры.ПоказыватьПояснение;
	
	Элементы.ВернутьПерепоставленноеБезОформления.Видимость = Не ЭтоРаздельнаяЗакупка;
	Элементы.ВернутьПерепоставленноеБезОформленияДекорация.Видимость = Не ЭтоРаздельнаяЗакупка;
	
	СформироватьЗаголовки(Параметры.ТипАкта,
						Параметры.КоличествоУпаковокРасхождения,
						Параметры.СпособОтраженияРасхождений,
						Параметры.ГрупповоеИзменение,
						Параметры.СтрокаПоЗаказу);
	
	Если Не ГрупповоеИзменение Тогда
		Если КоличествоУпаковокРасхождения >= 0 Тогда
			
			Элементы.Страницы.ТекущаяСтраница = Элементы.Излишки;
			ДействиеИзлишки                   = Параметры.ВыбранноеДействие;
			
			Если Параметры.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Работа
				Или Параметры.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Услуга Тогда
				
				Если ДействиеИзлишки = Перечисления.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОформитьПерепоставленноеИВернуть Тогда
					ДействиеИзлишки = Перечисления.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ПустаяСсылка();
				КонецЕсли;
				
				Элементы.ОформитьПерепоставленноеИВернуть.Видимость                         = Ложь;
				Элементы.ОформитьПерепоставленноеИВернутьДекорация.Видимость                = ЛОжь;
				Элементы.ВернутьПерепоставленноеБезОформленияДекорация.Заголовок            = НСтр("ru = 'Операция по оформлению услуг или работы будет сторнирована';
																									|en = 'Registration of services or works will be reversed'");
				Элементы.ВернутьПерепоставленноеБезОформления.СписокВыбора[0].Представление = НСтр("ru = 'Сторнировать';
																									|en = 'Reverse'");
				
			КонецЕсли;
			
		Иначе
			Элементы.Страницы.ТекущаяСтраница = Элементы.Недостачи;
			ДействиеНедостачи = Параметры.ВыбранноеДействие;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Не ВыполняетсяЗакрытие И Модифицированность Тогда
		
		Отказ = Истина;
		ПоказатьВопрос(Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект),
		               НСтр("ru = 'Выполненные изменения будут утеряны. Все равно закрыть?';
							|en = 'Made changes will be lost. Close anyway?'"),
		               РежимДиалогаВопрос.ДаНет);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ЗаНашСчетПриИзменении(Элемент)
	
	ВариантОтраженияСписанияНаПрочиеРасходыПриИзменении();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоВинеСтороннейКомпанииПриИзменении(Элемент)
	
	ВариантОтраженияСписанияНаПрочиеРасходыПриИзменении();
	
КонецПроцедуры

&НаКлиенте
Процедура ОформитьНедостачуПриИзменении(Элемент)
	
	ДействиеНедостачиПриИзменении();
	
КонецПроцедуры

&НаКлиенте
Процедура ОформитьНедостачуИОжидатьДопоставкуПриИзменении(Элемент)
	
	ДействиеНедостачиПриИзменении();
	
КонецПроцедуры

&НаКлиенте
Процедура ОжидатьДопоставкуБезОформленияПриИзменении(Элемент)
	
	ДействиеНедостачиПриИзменении();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ВыполняетсяЗакрытие = Истина;
	
	ПараметрыЗакрытия = Новый Структура();
	ПараметрыЗакрытия.Вставить("ДействиеИзлишки", Неопределено);
	ПараметрыЗакрытия.Вставить("ДействиеНедостачи", Неопределено);
	ПараметрыЗакрытия.Вставить("ПоВинеСтороннейКомпании", Истина);
	
	Если ГрупповоеИзменение Тогда
		
		ПараметрыЗакрытия.ДействиеИзлишки   = ДействиеИзлишки;
		ПараметрыЗакрытия.ДействиеНедостачи = ДействиеНедостачи;
		ПараметрыЗакрытия.ПоВинеСтороннейКомпании = ПоВинеСтороннейКомпании();
		
	ИначеЕсли КоличествоУпаковокРасхождения > 0 Тогда
		
		ПараметрыЗакрытия.ДействиеИзлишки = ДействиеИзлишки;

	ИначеЕсли КоличествоУпаковокРасхождения < 0 Тогда
		
		ПараметрыЗакрытия.ДействиеНедостачи = ДействиеНедостачи;
		ПараметрыЗакрытия.ПоВинеСтороннейКомпании = ПоВинеСтороннейКомпании();
		
	КонецЕсли;
	
	Закрыть(ПараметрыЗакрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	ВыполняетсяЗакрытие = Истина;
	Закрыть(Неопределено);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СформироватьЗаголовки(ТипАкта, КоличествоУпаковокРасхождения, СпособОтраженияРасхождений, ГрупповоеИзменение, СтрокаПоЗаказу)
	
	Если ТипАкта = Перечисления.ТипыОснованияАктаОРасхождении.ПриобретениеТоваровУслуг
		Или ТипАкта = Перечисления.ТипыОснованияАктаОРасхождении.ПриемкаТоваровНаХранение Тогда
		
		// Недостачи
		Элементы.ГруппаНедостачиСогласованы.Заголовок   = НСтр("ru = 'Недостачи при поступлении согласованы с поставщиком';
																|en = 'Shortages on receipt are approved by the vendor'");
		Элементы.ГруппаНедостачиНеСогласованы.Заголовок = НСтр("ru = 'Недостачи при поступлении не согласованы с поставщиком';
																|en = 'Shortages on receipt are not approved by the vendor'");
		
		Если СпособОтраженияРасхождений = Перечисления.СпособыОтраженияАктовОРасхожденияПослеПоступления.ИсправлениеПервичныхДокументов Тогда
			Если СтрокаПоЗаказу Или (ГрупповоеИзменение И ИспользоватьЗаказыПоставщикам) Тогда
				Элементы.ОформитьНедостачуДекорация.Заголовок = НСтр("ru = 'Приобретение уменьшается на недопоставленный товар(работу, услугу) с последующей отменой недопоставленных строк заказа';
																	|en = 'Purchase is reduced by the amount of underdelivered goods (work or service), and underdelivered order lines are canceled'");
			Иначе
				Элементы.ОформитьНедостачуДекорация.Заголовок = НСтр("ru = 'Приобретение уменьшается на недопоставленный товар(работу, услугу)';
																	|en = 'Purchase is reduced by the amount of underdelivered goods (work or service)'");
			КонецЕсли;
			Элементы.ОформитьНедостачуИОжидатьДопоставкуДекорация.Заголовок = НСтр("ru = 'Приобретение уменьшается на недопоставленный товар(работу, услугу) с последующим оформлением нового приобретения недопоставленного товара';
																					|en = 'Purchase is reduced by the amount of underdelivered goods (work or service), and a new purchase of the underdelivered goods is registered'");
		ИначеЕсли СпособОтраженияРасхождений = Перечисления.СпособыОтраженияАктовОРасхожденияПослеПоступления.ОформлениеКорректировокПоступления Тогда
			Элементы.ОформитьНедостачуДекорация.Заголовок = НСтр("ru = 'На недопоставленный товар оформляется корректировка приобретения';
																|en = 'Purchase adjustment is registered for underdelivered goods'");
			Элементы.ОформитьНедостачуИОжидатьДопоставкуДекорация.Заголовок = НСтр("ru = 'На недопоставленный товар(работу, услугу) оформляется корректировка приобретения с последующим оформлением нового приобретения недопоставленного товара';
																					|en = 'Purchase adjustment is registered for underdelivered goods (work or service), and a new purchase of the underdelivered goods is registered'");
		КонецЕсли;
		
		Элементы.ОжидатьДопоставкуБезОформленияДекорация.Заголовок = НСтр("ru = 'Ожидается поступление недопоставленного товара(работы, услуги), документ приобретения не переоформляется';
																			|en = 'Pending receipt of underdelivered goods (work or service). The purchase document is not re-registered'");
		
		// Излишки
		Элементы.ГруппаИзлишкиСогласованы.Заголовок   = НСтр("ru = 'Излишки при поступлении согласованы с поставщиком';
															|en = 'Surpluses on receipt are approved by the vendor'");
		Элементы.ГруппаИзлишкиНеСогласованы.Заголовок = НСтр("ru = 'Излишки при поступлении не согласованы с поставщиком';
															|en = 'Surpluses on receipt are not approved by the vendor'");

		Если СпособОтраженияРасхождений = Перечисления.СпособыОтраженияАктовОРасхожденияПослеПоступления.ИсправлениеПервичныхДокументов Тогда
			Элементы.ОформитьПерепоставленноеДекорация.Заголовок = НСтр("ru = 'Приобретение увеличивается на перепоставленный товар';
																		|en = 'Purchase is increased by the amount of overdelivered goods'");
			Элементы.ОформитьПерепоставленноеИВернутьДекорация.Заголовок = НСтр("ru = 'Приобретение увеличивается на перепоставленный товар с последующим оформлением возврата перепоставленного товара';
																				|en = 'Purchase is increased by the amount of overdelivered goods, and a return of the overdelivered goods is registered'");
		ИначеЕсли СпособОтраженияРасхождений = Перечисления.СпособыОтраженияАктовОРасхожденияПослеПоступления.ОформлениеКорректировокПоступления Тогда
			Элементы.ОформитьПерепоставленноеДекорация.Заголовок = НСтр("ru = 'На перепоставленный товар оформляется корректировка поступления';
																		|en = 'Receipt adjustment is registered for overdelivered goods'");
			Элементы.ОформитьПерепоставленноеИВернутьДекорация.Заголовок = НСтр("ru = 'На перепоставленный товар оформляется корректировка поступления с последующим оформлением возврата перепоставленного товара';
																				|en = 'Receipt adjustment is registered for overdelivered goods. A return of overdelivered goods will be registered'");
		КонецЕсли;
		
		Элементы.ОтнестиПерепоставленноеНаПрочиеДоходыДекорация.Заголовок = НСтр("ru = 'Расхождения между поступлением и приобретением относятся на прочие доходы, документы не переоформляются';
																				|en = 'Discrepancies between receipt and purchase are allocated to other income. The documents are not reregistered'");
		Элементы.ВернутьПерепоставленноеБезОформленияДекорация.Заголовок  = НСтр("ru = 'Перепоставленный товар отгружается со склада, документы не переоформляются';
																				|en = 'Overdelivered goods are shipped from the warehouse. The documents are not registered'");
		
	ИначеЕсли ТипАкта = Перечисления.ТипыОснованияАктаОРасхождении.ВозвратТоваровОтКлиента Тогда
		
		// Недостачи
		Элементы.ГруппаНедостачиСогласованы.Заголовок   = НСтр("ru = 'Недостачи при возврате согласованы с клиентом';
																|en = 'Shortages on return are approved with the customer'");
		Элементы.ГруппаНедостачиНеСогласованы.Заголовок = НСтр("ru = 'Недостачи при возврате не согласованы с клиентом';
																|en = 'Shortages on return are not approved with the customer'");
		
		Элементы.ОформитьНедостачуДекорация.Заголовок                   = НСтр("ru = 'Возврат товаров от клиента уменьшается на недопоставленный товар';
																				|en = 'Return of goods from the customer is reduced by the amount of underdelivered goods'");
		Элементы.ОформитьНедостачуИОжидатьДопоставкуДекорация.Заголовок = НСтр("ru = 'Возврат товаров от клиента уменьшается на недопоставленный товар с последующим оформлением нового возврата недопоставленного товара';
																				|en = 'Return of goods from the customer is reduced by the amount of underdelivered goods. A new return of underdelivered goods will be registered'");
		Элементы.ОжидатьДопоставкуБезОформленияДекорация.Заголовок      = НСтр("ru = 'Ожидается поступление недопоставленного товара, документ приобретения не переоформляется';
																				|en = 'Waiting for the receipt of underdelivered goods. The purchase document is not reregistered'");
		
		// Излишки
		Элементы.ГруппаИзлишкиСогласованы.Заголовок = НСтр("ru = 'Излишки при возврате согласованы с поставщиком';
															|en = 'Surpluses on return are approved by the vendor'");
		Элементы.ГруппаИзлишкиНеСогласованы.Заголовок  = НСтр("ru = 'Излишки при возврате не согласованы с поставщиком';
																|en = 'Surpluses on return are not approved by the vendor'");
		
		Элементы.ОтнестиПерепоставленноеНаПрочиеДоходыДекорация.Заголовок  = НСтр("ru = 'Излишки возврата оприходуются, относятся на прочие доходы, документы не переоформляются';
																					|en = 'Return surpluses are received and allocated to income, documents are not reregistered'");
		Элементы.ОформитьПерепоставленноеИВернутьДекорация.Заголовок       = НСтр("ru = 'Возврат товаров от клиента увеличивается на перепоставленный товар с последующим оформлением поступления перепоставленного товара';
																					|en = 'Return of goods from the customer is increased by the amount of overdelivered goods. A receipt of overdelivered goods will be registered'");
		Элементы.ОформитьПерепоставленноеДекорация.Заголовок               = НСтр("ru = 'Возврат товаров от клиента увеличивается на перепоставленный товар';
																					|en = 'Return of goods from the customer is increased by the amount of overdelivered goods'");
		Элементы.ВернутьПерепоставленноеБезОформленияДекорация.Заголовок   = НСтр("ru = 'Перепоставленный товар отгружается со склада, документы не переоформляются';
																					|en = 'Overdelivered goods are shipped from the warehouse. The documents are not registered'");
		
	ИначеЕсли ТипАкта = Перечисления.ТипыОснованияАктаОРасхождении.ПриемкаТоваровНаХранение Тогда
		
		// Недостачи
		Элементы.ГруппаНедостачиСогласованы.Заголовок   = НСтр("ru = 'Недостачи при поступлении согласованы с поставщиком';
																|en = 'Shortages on receipt are approved by the vendor'");
		Элементы.ГруппаНедостачиНеСогласованы.Заголовок = НСтр("ru = 'Недостачи при поступлении не согласованы с поставщиком';
																|en = 'Shortages on receipt are not approved by the vendor'");
		
		Если СтрокаПоЗаказу Или (ГрупповоеИзменение И ИспользоватьЗаказыПоставщикам) Тогда
			Элементы.ОформитьНедостачуДекорация.Заголовок = НСтр("ru = 'Приобретение уменьшается на недопоставленный товар с последующей отменой недопоставленных строк заказа';
																|en = 'Purchase is reduced by the amount of underdelivered goods, and underdelivered order lines are canceled'");
		Иначе
			Элементы.ОформитьНедостачуДекорация.Заголовок = НСтр("ru = 'Приобретение уменьшается на недопоставленный товар';
																|en = 'Purchase is reduced by the amount of underdelivered goods'");
		КонецЕсли;
		
		Элементы.ОформитьНедостачуИОжидатьДопоставкуДекорация.Заголовок = НСтр("ru = 'Приобретение уменьшается на недопоставленный товар с последующим оформлением нового поступления недопоставленного товара';
																				|en = 'Purchase is reduced by the amount of underdelivered goods, and a new receipt of the underdelivered goods is registered'");
		
		Элементы.ОжидатьДопоставкуБезОформленияДекорация.Заголовок = НСтр("ru = 'Ожидается поступление недопоставленного товара, документ приобретения не переоформляется';
																			|en = 'Waiting for the receipt of underdelivered goods. The purchase document is not reregistered'");
		
		// Излишки
		Элементы.ГруппаИзлишкиСогласованы.Заголовок   = НСтр("ru = 'Излишки при поступлении согласованы с поставщиком';
															|en = 'Surpluses on receipt are approved by the vendor'");
		Элементы.ГруппаИзлишкиНеСогласованы.Заголовок = НСтр("ru = 'Излишки при поступлении не согласованы с поставщиком';
															|en = 'Surpluses on receipt are not approved by the vendor'");
		
		Элементы.ОформитьПерепоставленноеДекорация.Заголовок = НСтр("ru = 'Приобретение увеличивается на перепоставленный товар';
																	|en = 'Purchase is increased by the amount of overdelivered goods'");
		Элементы.ОформитьПерепоставленноеИВернутьДекорация.Заголовок = НСтр("ru = 'Приобретение увеличивается на перепоставленный товар с последующим оформлением отгрузки перепоставленного товара';
																			|en = 'Purchase is increased by the amount of overdelivered goods, and shipment of the overdelivered goods is registered'");
		
		Элементы.ОтнестиПерепоставленноеНаПрочиеДоходыДекорация.Заголовок = НСтр("ru = 'Перепоставленный товар оприходуется, документы не переоформляются';
																				|en = 'Overdelivered goods will be received, documents are not reissued'");
		Элементы.ВернутьПерепоставленноеБезОформленияДекорация.Заголовок  = НСтр("ru = 'Перепоставленный товар отгружается со склада, документы не переоформляются';
																				|en = 'Overdelivered goods are shipped from the warehouse. The documents are not registered'");
		
	ИначеЕсли ТипАкта = Перечисления.ТипыОснованияАктаОРасхождении.ВозвратТоваровОтХранителя Тогда
		
		// Недостачи
		Элементы.ГруппаНедостачиСогласованы.Заголовок   = НСтр("ru = 'Недостачи при поступлении согласованы с хранителем';
																|en = 'Shortages on receipt are agreed with the custodian'");
		Элементы.ГруппаНедостачиНеСогласованы.Заголовок = НСтр("ru = 'Недостачи при поступлении не согласованы с хранителем';
																|en = 'Shortages on receipt are not agreed with custodian'");
		
		Элементы.ОформитьНедостачуДекорация.Заголовок                   = НСтр("ru = 'Поступление товаров от хранителя уменьшается на недопоставленный товар';
																				|en = 'VMI return is reduced by short-delivered goods.'");
		Элементы.ОформитьНедостачуИОжидатьДопоставкуДекорация.Заголовок = НСтр("ru = 'Поступление товаров от хранителя уменьшается на недопоставленный товар с последующим оформлением нового поступления недопоставленного товара';
																				|en = 'VMI return is reduced by short-delivered goods, and a new receipt of short-delivered goods is issued'");
		Элементы.ОжидатьДопоставкуБезОформленияДекорация.Заголовок      = НСтр("ru = 'Ожидается поступление недопоставленного товара, документ приобретения не переоформляется';
																				|en = 'Waiting for the receipt of underdelivered goods. The purchase document is not reregistered'");
		
		// Излишки
		Элементы.ГруппаИзлишкиСогласованы.Заголовок = НСтр("ru = 'Излишки при поступлении согласованы с хранителем';
															|en = 'Surpluses on receipt are agreed with the custodian'");
		Элементы.ГруппаИзлишкиНеСогласованы.Заголовок  = НСтр("ru = 'Излишки при возврате не согласованы с хранителем';
																|en = 'Surpluses on return are not agreed with the custodian'");
		
		Элементы.ОтнестиПерепоставленноеНаПрочиеДоходыДекорация.Заголовок  = НСтр("ru = 'Излишки поступления товаров от хранителя оприходуются, относятся на прочие доходы, документы не переоформляются';
																					|en = 'Surplus goods are received from the custodian and allocated to income; the documents are not reissued'");
		Элементы.ОформитьПерепоставленноеИВернутьДекорация.Заголовок       = НСтр("ru = 'Поступление товаров от хранителя увеличивается на перепоставленный товар с последующим оформлением передачи перепоставленного товара';
																					|en = 'VMI return is increased by overdelivered goods, transfer of overdelivered goods will be registered'");
		Элементы.ОформитьПерепоставленноеДекорация.Заголовок               = НСтр("ru = 'Поступление товаров от хранителя увеличивается на перепоставленный товар';
																					|en = 'VMI return is increased by overdelivered goods'");
		Элементы.ВернутьПерепоставленноеБезОформленияДекорация.Заголовок   = НСтр("ru = 'Перепоставленный товар отгружается со склада, документы не переоформляются';
																					|en = 'Overdelivered goods are shipped from the warehouse. The documents are not registered'");
		
	ИначеЕсли ТипАкта = Перечисления.ТипыОснованияАктаОРасхождении.ВозвратОтКомиссионера Тогда
		
		// Недостачи
		Элементы.ГруппаНедостачиСогласованы.Заголовок   = НСтр("ru = 'Недостачи при поступлении согласованы с комиссионером';
																|en = 'Shortage upon receipt are approved by the consignee'");
		Элементы.ГруппаНедостачиНеСогласованы.Заголовок = НСтр("ru = 'Недостачи при поступлении не согласованы с комиссионером';
																|en = 'Shortage upon receipt are not approved by the consignee'");
		
		Элементы.ОформитьНедостачуДекорация.Заголовок                   = НСтр("ru = 'Поступление товаров от комиссионера уменьшается на недопоставленный товар';
																				|en = 'Receipt of goods from the consignee is reduced by underdelivered goods'");
		Элементы.ОформитьНедостачуИОжидатьДопоставкуДекорация.Заголовок = НСтр("ru = 'Поступление товаров от комиссионера уменьшается на недопоставленный товар с последующим оформлением нового поступления недопоставленного товара';
																				|en = 'Goods receipt from the consignee is reduced by underdelivered goods, and new receipt of underdelivered goods is registered'");
		Элементы.ОжидатьДопоставкуБезОформленияДекорация.Заголовок      = НСтр("ru = 'Ожидается поступление недопоставленного товара, документ приобретения не переоформляется';
																				|en = 'Waiting for the receipt of underdelivered goods. The purchase document is not reregistered'");
		
		// Излишки
		Элементы.ГруппаИзлишкиСогласованы.Заголовок = НСтр("ru = 'Излишки при поступлении согласованы с комиссионером';
															|en = 'Surplus upon receipt are approved by the consignee'");
		Элементы.ГруппаИзлишкиНеСогласованы.Заголовок  = НСтр("ru = 'Излишки при возврате не согласованы с комиссионером';
																|en = 'Surplus upon return is not approved by the consignee'");
		
		Элементы.ОтнестиПерепоставленноеНаПрочиеДоходыДекорация.Заголовок  = НСтр("ru = 'Излишки поступления товаров от комиссионера оприходуются, относятся на прочие доходы, документы не переоформляются';
																					|en = 'Surplus of the goods from the consignee are recognized and allocated to other income; the documents are not reregistered'");
		Элементы.ОформитьПерепоставленноеИВернутьДекорация.Заголовок       = НСтр("ru = 'Поступление товаров от комиссионера увеличивается на перепоставленный товар с последующим оформлением поступления перепоставленного товара';
																					|en = 'Receipt of goods from the consignee is increased by the overdelivered goods, receipt of overdelivered goods will be registered'");
		Элементы.ОформитьПерепоставленноеДекорация.Заголовок               = НСтр("ru = 'Поступление товаров от комиссионера увеличивается на перепоставленный товар';
																					|en = 'Receipt of goods from the consignee is increased by overdelivered goods'");
		Элементы.ВернутьПерепоставленноеБезОформленияДекорация.Заголовок   = НСтр("ru = 'Перепоставленный товар отгружается со склада, документы не переоформляются';
																					|en = 'Overdelivered goods are shipped from the warehouse. The documents are not registered'");
		
	//++ НЕ УТ
	ИначеЕсли ТипАкта = Перечисления.ТипыОснованияАктаОРасхождении.ПоступлениеТоваровОтПереработчика
		  Или ТипАкта = Перечисления.ТипыОснованияАктаОРасхождении.ВозвратТоваровОтПереработчика Тогда
	
		// Недостачи
		Элементы.ГруппаНедостачиСогласованы.Заголовок   = НСтр("ru = 'Недостачи при поступлении согласованы с переработчиком';
																|en = 'Shortage upon receipt is approved by the subcontractor'");
		Элементы.ГруппаНедостачиНеСогласованы.Заголовок = НСтр("ru = 'Недостачи при поступлении не согласованы с переработчиком';
																|en = 'Shortage upon receipt is not approved by the subcontractor'");
		
		Элементы.ОформитьНедостачуДекорация.Заголовок                   = НСтр("ru = 'Поступление товаров от переработчика уменьшается на недопоставленный товар';
																				|en = 'Receipt of goods from the subcontractor is reduced by underdelivered goods'");
		Элементы.ОформитьНедостачуИОжидатьДопоставкуДекорация.Заголовок = НСтр("ru = 'Поступление товаров от переработчика уменьшается на недопоставленный товар с последующим оформлением нового поступления недопоставленного товара';
																				|en = 'Goods receipt from the subcontractor is reduced by underdelivered goods, a new receipt of underdelivered goods is registered'");
		Элементы.ОжидатьДопоставкуБезОформленияДекорация.Заголовок      = НСтр("ru = 'Недопоставленный товар принимается на склад, документы не переоформляются';
																				|en = 'The underdelivered goods are received at the warehouse; the documents are not reregistered.'");
		
		// Излишки
		Элементы.ГруппаИзлишкиСогласованы.Заголовок = НСтр("ru = 'Излишки при поступлении согласованы с переработчиком';
															|en = 'Surplus upon receipt is approved by the subcontractor'");
		Элементы.ГруппаИзлишкиНеСогласованы.Заголовок  = НСтр("ru = 'Излишки при возврате не согласованы с переработчиком';
																|en = 'Surplus upon return is not approved by the subcontractor'");
		
		Элементы.ОтнестиПерепоставленноеНаПрочиеДоходыДекорация.Заголовок  = НСтр("ru = 'Излишки поступления товаров от переработчика оприходуются, относятся на прочие доходы, документы не переоформляются';
																					|en = 'Surplus of goods from the subcontractor are received and allocated to other income; the documents are not reissued'");
		Элементы.ОформитьПерепоставленноеИВернутьДекорация.Заголовок       = НСтр("ru = 'Поступление товаров от переработчика увеличивается на перепоставленный товар с последующим оформлением передачи перепоставленного товара';
																					|en = 'Receipt of goods from the subcontractor is increased by overdelivered goods, transfer of overdelivered goods will be registered'");
		Элементы.ОформитьПерепоставленноеДекорация.Заголовок               = НСтр("ru = 'Поступление товаров от переработчика увеличивается на перепоставленный товар';
																					|en = 'Receipt of goods from the subcontractor is increased by overdelivered goods'");
		Элементы.ВернутьПерепоставленноеБезОформленияДекорация.Заголовок   = НСтр("ru = 'Перепоставленный товар отгружается со склада, документы не переоформляются';
																					|en = 'Overdelivered goods are shipped from the warehouse. The documents are not registered'");
	//-- НЕ УТ
	
	//++ НЕ УТКА
	ИначеЕсли ТипАкта = Перечисления.ТипыОснованияАктаОРасхождении.ПоступлениеОтДавальца Тогда
		
		// Недостачи
		Элементы.ГруппаНедостачиСогласованы.Заголовок   = НСтр("ru = 'Недостачи при поступлении согласованы с давальцем';
																|en = 'Shortage at receipt is approved with the provider'");
		Элементы.ГруппаНедостачиНеСогласованы.Заголовок = НСтр("ru = 'Недостачи при поступлении не согласованы с давальцем';
																|en = 'Shortage at receipt is not approved with the provider'");
		
		Если СтрокаПоЗаказу
		 Или ГрупповоеИзменение И ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыДавальцев2_5") Тогда
			Элементы.ОформитьНедостачуДекорация.Заголовок = НСтр("ru = 'Поступление уменьшается на недопоставленный товар с последующей отменой недопоставленных строк заказа';
																|en = 'Receipt is reduced by underdelivered goods, underdelivered order lines will be canceled'");
		Иначе
			Элементы.ОформитьНедостачуДекорация.Заголовок = НСтр("ru = 'Поступление уменьшается на недопоставленный товар';
																|en = 'Receipt reduces for underdelivered goods'");
		КонецЕсли;
		
		Элементы.ОформитьНедостачуИОжидатьДопоставкуДекорация.Заголовок = НСтр("ru = 'Поступление уменьшается на недопоставленный товар с последующим оформлением нового поступления недопоставленного товара';
																				|en = 'Receipt is reduced by underdelivered goods, a new receipt of underdelivered goods is registered'");
		
		Элементы.ОжидатьДопоставкуБезОформленияДекорация.Заголовок = НСтр("ru = 'Недопоставленный товар принимается на склад, документы не переоформляются';
																			|en = 'The underdelivered goods are received at the warehouse; the documents are not reregistered.'");
		
		// Излишки
		Элементы.ГруппаИзлишкиСогласованы.Заголовок   = НСтр("ru = 'Излишки при поступлении согласованы с давальцем';
															|en = 'Surplus at receipt is approved with the provider'");
		Элементы.ГруппаИзлишкиНеСогласованы.Заголовок = НСтр("ru = 'Излишки при поступлении не согласованы с давальцем';
															|en = 'Surplus at receipt is not approved with the provider'");
		
		Элементы.ОформитьПерепоставленноеДекорация.Заголовок = НСтр("ru = 'Поступление увеличивается на перепоставленный товар';
																	|en = 'Receipt increases for overdelivered goods'");
		Элементы.ОформитьПерепоставленноеИВернутьДекорация.Заголовок = НСтр("ru = 'Поступление увеличивается на перепоставленный товар с последующим оформлением отгрузки перепоставленного товара';
																			|en = 'Receipt is increased by overdelivered goods, a shipment of overdelivered goods will be registered'");
		
		Элементы.ОтнестиПерепоставленноеНаПрочиеДоходыДекорация.Заголовок = НСтр("ru = 'Перепоставленный товар оприходуется, документы не переоформляются';
																				|en = 'Overdelivered goods will be received, documents are not reissued'");
		Элементы.ВернутьПерепоставленноеБезОформленияДекорация.Заголовок  = НСтр("ru = 'Перепоставленный товар отгружается со склада, документы не переоформляются';
																				|en = 'Overdelivered goods are shipped from the warehouse. The documents are not registered'");
	//-- НЕ УТКА
		
	КонецЕсли;
	
	АктПриемкиНаХранение = ТипАкта = Перечисления.ТипыОснованияАктаОРасхождении.ПриемкаТоваровНаХранение
							//++ НЕ УТ
							Или ТипАкта = Перечисления.ТипыОснованияАктаОРасхождении.ПоступлениеТоваровОтПереработчика
							Или ТипАкта = Перечисления.ТипыОснованияАктаОРасхождении.ВозвратТоваровОтПереработчика
							//-- НЕ УТ

							//++ НЕ УТКА
							Или ТипАкта = Перечисления.ТипыОснованияАктаОРасхождении.ПоступлениеОтДавальца
							//-- НЕ УТКА
							Или ТипАкта = Перечисления.ТипыОснованияАктаОРасхождении.ВозвратТоваровОтХранителя
							Или ТипАкта = Перечисления.ТипыОснованияАктаОРасхождении.ВозвратОтКомиссионера;
	
	Если АктПриемкиНаХранение Тогда
		ЗначениеСписания = Элементы.ЗаНашСчет.СписокВыбора.Получить(0);
		ЗначениеСписания.Представление = НСтр("ru = 'Списать недостачи';
												|en = 'Write off shortages'");
	КонецЕсли;
	
	ЗаголовокОформленияНедостачЗаНашСчет = ?(АктПриемкиНаХранение,
											НСтр("ru = 'Расхождения между поступлением и приобретением относятся на недостачи';
												|en = 'Discrepancies between receipt and purchase are allocated to shortages'"),
											НСтр("ru = 'Расхождения между поступлением и приобретением относятся на прочие расходы';
												|en = 'Discrepancies between receipt and purchase are allocated to other expenses'"));
	
	Элементы.ЗаНашСчетДекорация.Заголовок               = ЗаголовокОформленияНедостачЗаНашСчет;
	Элементы.ПоВинеСтороннейКомпанииДекорация.Заголовок = НСтр("ru = 'Расхождения между поступлением и приобретением относятся на прочие расходы. Ответственность за недостачи будет возложена на поставщика';
																|en = 'Discrepancies between receipt and purchase are allocated to other expenses. The vendor bears the responsibility for shortages'");
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Ответ = РезультатВопроса;
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ВыполняетсяЗакрытие = Истина;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПоВинеСтороннейКомпании()

	Возврат ?(ДействиеНедостачи = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОтнестиНедостачуНаПрочиеРасходы"),
	                                                      ?(ЗаЧейСчет = "ПоВинеСтороннейКомпании", Истина, Ложь),
	                                                      Ложь);

КонецФункции

&НаКлиенте
Процедура ВариантОтраженияСписанияНаПрочиеРасходыПриИзменении()
	
	ДействиеНедостачи = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОтнестиНедостачуНаПрочиеРасходы");
	
КонецПроцедуры

&НаКлиенте
Процедура ДействиеНедостачиПриИзменении()
	
	ЗаЧейСчет = "";
	
КонецПроцедуры

#КонецОбласти

