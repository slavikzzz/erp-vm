#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Функция ТаблицаВыбора(ДатаНачалаСобытия) Экспорт
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("КодПричины",    ОбщегоНазначения.ОписаниеТипаСтрока(2));
	Таблица.Колонки.Добавить("ПолныхЛетС",    ОбщегоНазначения.ОписаниеТипаЧисло(3));
	Таблица.Колонки.Добавить("ПолныхЛетПо",   ОбщегоНазначения.ОписаниеТипаЧисло(3));
	Таблица.Колонки.Добавить("ЭтоСтационар",  Новый ОписаниеТипов("Булево"));
	Таблица.Колонки.Добавить("СлучайУхода",   Новый ОписаниеТипов("ПеречислениеСсылка.СлучаиУходаЗаБольнымиДетьми"));
	Таблица.Колонки.Добавить("Представление", ОбщегоНазначения.ОписаниеТипаСтрока(100));
	
	Если УчетПособийСоциальногоСтрахования.ДействуетПолнаяОплатаПоУходуЗаРебенкомДо8Лет(ДатаНачалаСобытия)
		Или Не ЗначениеЗаполнено(ДатаНачалаСобытия) Тогда
		
		ДобавитьВТаблицу(Таблица, "09", 0,  6, ПоУходуДо7миЛетАмбулаторно);
		ДобавитьВТаблицу(Таблица, "09", 0,  6, ПоУходуДо7миЛетВСтационаре);
		ДобавитьВТаблицу(Таблица, "09", 7,  7, До7ПолныхЛетАмбулаторно);
		ДобавитьВТаблицу(Таблица, "09", 7,  7, До7ПолныхЛетВСтационаре);
		ДобавитьВТаблицу(Таблица, "09", 8, 14, ПоУходуДо15тиЛетАмбулаторно, НСтр("ru = 'От 8 до 15 лет, амбулаторно';
																				|en = '8 to 15 years old, outpatient'"));
		ДобавитьВТаблицу(Таблица, "09", 8, 14, ПоУходуДо15тиЛетВСтационаре, НСтр("ru = 'От 8 до 15 лет, в стационаре';
																				|en = '8 to 15 years old, hospital stay'"));
		
		ДобавитьВТаблицу(Таблица, "12", 0,  6, ПоУходуДо7миЛетПриОсобомЗаболеванииАмбулаторно);
		ДобавитьВТаблицу(Таблица, "12", 0,  6, ПоУходуДо7миЛетПриОсобомЗаболеванииВСтационаре);
		
		ДобавитьВТаблицу(Таблица, "13", 0,  7, ИнвалидомДо8ЛетАмбулаторно);
		ДобавитьВТаблицу(Таблица, "13", 0,  7, ИнвалидомДо8ЛетВСтационаре);
		ДобавитьВТаблицу(Таблица, "13", 8, 17, ПоУходуЗаРебенкомИнвалидомАмбулаторно, НСтр("ru = 'Инвалидом от 8 до 18 лет, амбулаторно';
																							|en = 'Disabled person 8 to 18 years old, outpatient'"));
		ДобавитьВТаблицу(Таблица, "13", 8, 17, ПоУходуЗаРебенкомИнвалидомВСтационаре, НСтр("ru = 'Инвалидом от 8 до 18 лет, в стационаре';
																							|en = 'Disabled person 8 to 18 years old, hospital stay'"));
		
		ДобавитьВТаблицу(Таблица, "14", 0,  7, До8ЛетПриПоствакцинальномОсложненииАмбулаторно);
		ДобавитьВТаблицу(Таблица, "14", 0,  7, До8ЛетПриПоствакцинальномОсложненииВСтационаре);
		ДобавитьВТаблицу(Таблица, "14", 8, 17, ПоУходуДо15тиЛетПриПоствакцинальномОсложненииАмбулаторно, НСтр("ru = 'От 8 до 18 лет при поствакцинальном осложнении, амбулаторно';
																												|en = '8 to 18 years old, postvaccinal complication, outpatient'"));
		ДобавитьВТаблицу(Таблица, "14", 8, 17, ПоУходуДо15тиЛетПриПоствакцинальномОсложненииВСтационаре, НСтр("ru = 'От 8 до 18 лет при поствакцинальном осложнении, в стационаре';
																												|en = '8 to 18 years old, postvaccinal complication, hospital stay'"));
		
		ДобавитьВТаблицу(Таблица, "15", 0,  7, До8ЛетПриВИЧ);
		ДобавитьВТаблицу(Таблица, "15", 8, 17, ПоУходуДо15тиЛетПриВИЧ, НСтр("ru = 'От 8 до 18 лет, ВИЧ-инфицированным, в стационаре';
																			|en = '8 to 18 years old, HIV positive, hospital stay'"));
		
	ИначеЕсли ДатаНачалаСобытия >= '20150101' Тогда
		
		ДобавитьВТаблицу(Таблица, "09", 0,  6, ПоУходуДо7миЛетАмбулаторно);
		ДобавитьВТаблицу(Таблица, "09", 0,  6, ПоУходуДо7миЛетВСтационаре);
		ДобавитьВТаблицу(Таблица, "09", 7, 14, ПоУходуДо15тиЛетАмбулаторно);
		ДобавитьВТаблицу(Таблица, "09", 7, 14, ПоУходуДо15тиЛетВСтационаре);
		
		ДобавитьВТаблицу(Таблица, "12", 0,  6, ПоУходуДо7миЛетПриОсобомЗаболеванииАмбулаторно);
		ДобавитьВТаблицу(Таблица, "12", 0,  6, ПоУходуДо7миЛетПриОсобомЗаболеванииВСтационаре);
		
		ДобавитьВТаблицу(Таблица, "13", 0, 17, ПоУходуЗаРебенкомИнвалидомАмбулаторно, НСтр("ru = 'Инвалидом до 18 лет, амбулаторно';
																							|en = 'Disabled person under 18 years old, outpatient'"));
		ДобавитьВТаблицу(Таблица, "13", 0, 17, ПоУходуЗаРебенкомИнвалидомВСтационаре, НСтр("ru = 'Инвалидом до 18 лет, в стационаре';
																							|en = 'Disabled person under 18 years old, hospital stay'"));
		
		ДобавитьВТаблицу(Таблица, "14", 0, 17, ПоУходуДо15тиЛетПриПоствакцинальномОсложненииАмбулаторно, НСтр("ru = 'До 18 лет при поствакцинальном осложнении, амбулаторно';
																												|en = 'Under 18 years old, postvaccinal complication, outpatient'"));
		ДобавитьВТаблицу(Таблица, "14", 0, 17, ПоУходуДо15тиЛетПриПоствакцинальномОсложненииВСтационаре, НСтр("ru = 'До 18 лет при поствакцинальном осложнении, в стационаре';
																												|en = 'Under 18 years old, postvaccinal complication, hospital stay'"));
		
		ДобавитьВТаблицу(Таблица, "15", 0, 17, ПоУходуДо15тиЛетПриВИЧ, НСтр("ru = 'До 18 лет, ВИЧ-инфицированным, в стационаре';
																			|en = 'Under 18 years old, HIV positive, hospital stay'"));
		
	ИначеЕсли ЗначениеЗаполнено(ДатаНачалаСобытия) Тогда
		
		ДобавитьВТаблицу(Таблица, "09", 0,  6, ПоУходуДо7миЛетАмбулаторно);
		ДобавитьВТаблицу(Таблица, "09", 0,  6, ПоУходуДо7миЛетВСтационаре);
		ДобавитьВТаблицу(Таблица, "09", 7, 14, ПоУходуДо15тиЛетАмбулаторно);
		ДобавитьВТаблицу(Таблица, "09", 7, 14, ПоУходуДо15тиЛетВСтационаре);
		
		ДобавитьВТаблицу(Таблица, "12", 0,  6, ПоУходуДо7миЛетПриОсобомЗаболеванииАмбулаторно);
		ДобавитьВТаблицу(Таблица, "12", 0,  6, ПоУходуДо7миЛетПриОсобомЗаболеванииВСтационаре);
		
		ДобавитьВТаблицу(Таблица, "13", 0, 14, ПоУходуЗаРебенкомИнвалидомАмбулаторно, НСтр("ru = 'Инвалидом до 15 лет, амбулаторно';
																							|en = 'Disabled person under 15 years old, outpatient'"));
		ДобавитьВТаблицу(Таблица, "13", 0, 14, ПоУходуЗаРебенкомИнвалидомВСтационаре, НСтр("ru = 'Инвалидом до 15 лет, в стационаре';
																							|en = 'Disabled person under 15 years old, hospital stay'"));
		
		ДобавитьВТаблицу(Таблица, "14", 0, 14, ПоУходуДо15тиЛетПриПоствакцинальномОсложненииАмбулаторно, НСтр("ru = 'До 15 лет при поствакцинальном осложнении, амбулаторно';
																												|en = 'Under 15 years old, postvaccinal complication, outpatient'"));
		ДобавитьВТаблицу(Таблица, "14", 0, 14, ПоУходуДо15тиЛетПриПоствакцинальномОсложненииВСтационаре, НСтр("ru = 'До 15 лет при поствакцинальном осложнении, в стационаре';
																												|en = 'Under 15 years old, postvaccinal complication, hospital stay'"));
		
		ДобавитьВТаблицу(Таблица, "15", 0, 14, ПоУходуДо15тиЛетПриВИЧ, НСтр("ru = 'До 15 лет, ВИЧ-инфицированным, в стационаре';
																			|en = 'Under 15 years old, HIV positive, hospital stay'"));
		
	КонецЕсли;
	
	Возврат Таблица;
КонецФункции

Процедура ДобавитьВТаблицу(Таблица, КодПричины, ПолныхЛетС, ПолныхЛетПо, СлучайУхода, ПредставлениеБезКода = Неопределено)
	СтрокаТаблицы = Таблица.Добавить();
	СтрокаТаблицы.КодПричины   = КодПричины;
	СтрокаТаблицы.ПолныхЛетС   = ПолныхЛетС;
	СтрокаТаблицы.ПолныхЛетПо  = ПолныхЛетПо;
	СтрокаТаблицы.ЭтоСтационар = ЭтоСтационар(СлучайУхода);
	СтрокаТаблицы.СлучайУхода  = СлучайУхода;
	Если ПредставлениеБезКода = Неопределено Тогда
		СтрокаТаблицы.Представление = Строка(КодПричины);
	Иначе
		СтрокаТаблицы.Представление = "(" + КодПричины + ") " + ПредставлениеБезКода;
	КонецЕсли;
КонецПроцедуры

Функция ЭтоСтационар(СлучайУхода) Экспорт
	Возврат СлучайУхода = ПоУходуДо7миЛетВСтационаре
		Или СлучайУхода = До7ПолныхЛетВСтационаре
		Или СлучайУхода = ПоУходуДо15тиЛетВСтационаре
		Или СлучайУхода = ПоУходуДо7миЛетПриОсобомЗаболеванииВСтационаре
		Или СлучайУхода = ИнвалидомДо8ЛетВСтационаре
		Или СлучайУхода = ПоУходуЗаРебенкомИнвалидомВСтационаре
		Или СлучайУхода = До8ЛетПриПоствакцинальномОсложненииВСтационаре
		Или СлучайУхода = ПоУходуДо15тиЛетПриПоствакцинальномОсложненииВСтационаре
		Или СлучайУхода = ПоУходуДо15тиЛетПриВИЧ;
КонецФункции

Функция КодПричины(СлучайУхода) Экспорт
	
	Если СлучайУхода = ПоУходуДо7миЛетАмбулаторно
		Или СлучайУхода = ПоУходуДо7миЛетВСтационаре
		Или СлучайУхода = До7ПолныхЛетАмбулаторно
		Или СлучайУхода = До7ПолныхЛетВСтационаре
		Или СлучайУхода = ПоУходуДо15тиЛетАмбулаторно
		Или СлучайУхода = ПоУходуДо15тиЛетВСтационаре Тогда
		
		Возврат "09";
	
	ИначеЕсли СлучайУхода = ПоУходуДо7миЛетПриОсобомЗаболеванииАмбулаторно
		Или СлучайУхода = ПоУходуДо7миЛетПриОсобомЗаболеванииВСтационаре Тогда
		
		Возврат "12";
	
	ИначеЕсли СлучайУхода = ИнвалидомДо8ЛетАмбулаторно
		Или СлучайУхода = ИнвалидомДо8ЛетВСтационаре
		Или СлучайУхода = ПоУходуЗаРебенкомИнвалидомАмбулаторно
		Или СлучайУхода = ПоУходуЗаРебенкомИнвалидомВСтационаре Тогда
		
		Возврат "13";
	
	ИначеЕсли СлучайУхода = До8ЛетПриПоствакцинальномОсложненииАмбулаторно
		Или СлучайУхода = До8ЛетПриПоствакцинальномОсложненииВСтационаре
		Или СлучайУхода = ПоУходуДо15тиЛетПриПоствакцинальномОсложненииАмбулаторно
		Или СлучайУхода = ПоУходуДо15тиЛетПриПоствакцинальномОсложненииВСтационаре Тогда
		
		Возврат "14";
	
	ИначеЕсли СлучайУхода = До8ЛетПриВИЧ
		Или СлучайУхода = ПоУходуДо15тиЛетПриВИЧ Тогда
		
		Возврат "15";
		
	Иначе
		
		Возврат Неопределено;
		
	КонецЕсли;
	
КонецФункции

Функция ПредставлениеСлучая(СлучайУхода, ДатаНачалаСобытия) Экспорт
	ТаблицаВыбора = ТаблицаВыбора(ДатаНачалаСобытия);
	СтрокаТаблицы = ТаблицаВыбора.Найти(СлучайУхода, "СлучайУхода");
	Если СтрокаТаблицы = Неопределено Тогда
		Возврат Строка(СлучайУхода);
	КонецЕсли;
	
	Если СтрокаТаблицы.КодПричины = "09" Тогда
		Шаблон = НСтр("ru = '(09) Уход за больным ребенком %1, %2';
						|en = '(09) Sick child care %1, %2'");
	ИначеЕсли СтрокаТаблицы.КодПричины = "12" Тогда
		Шаблон = НСтр("ru = '(12) Уход за больным ребенком %1, %2, заболевание включено в Перечень Минздравсоцразвития';
						|en = '(12) Sick child care %1, %2, disease is included in the list of diseases defined by the Ministry of Health of the Russian Federation'");
	ИначеЕсли СтрокаТаблицы.КодПричины = "13" Тогда
		Шаблон = НСтр("ru = '(13) Уход за больным ребенком-инвалидом %1, %2';
						|en = '(13) Disabled child care %1, %2'");
	ИначеЕсли СтрокаТаблицы.КодПричины = "14" Тогда
		Шаблон = НСтр("ru = '(14) Уход за больным ребенком %1, при поствакцинальном осложнении, %2';
						|en = '(14) Sick child care %1, postvaccinal complication, %2'");
	ИначеЕсли СтрокаТаблицы.КодПричины = "15" Тогда
		Шаблон = НСтр("ru = '(15) Уход за больным ребенком %1, ВИЧ-инфицированным, %2';
						|en = '(15) Sick child care %1, HIV positive, %2'");
	КонецЕсли;
	
	НеполныхЛетПо = СтрокаТаблицы.ПолныхЛетПо + 1;
	Если СтрокаТаблицы.ПолныхЛетС = 0 Тогда
		ПредставлениеВозраста = СтрШаблон(НСтр("ru = 'до %1';
												|en = 'until %1'"), ПредставлениеЛет(НеполныхЛетПо));
	ИначеЕсли СтрокаТаблицы.ПолныхЛетС = СтрокаТаблицы.ПолныхЛетПо Тогда
		ПредставлениеВозраста = ПредставлениеЛет(СтрокаТаблицы.ПолныхЛетС);
	Иначе
		ПредставлениеВозраста = СтрШаблон(НСтр("ru = 'от %1 до %2';
												|en = 'from %1 to %2'"), СтрокаТаблицы.ПолныхЛетС, ПредставлениеЛет(НеполныхЛетПо));
	КонецЕсли;
	
	Если СтрокаТаблицы.ЭтоСтационар Тогда
		ПредставлениеРежимаЛечения = НСтр("ru = 'в стационаре';
											|en = 'hospital stay'");
	Иначе
		ПредставлениеРежимаЛечения = НСтр("ru = 'амбулаторно';
											|en = 'outpatient'");
	КонецЕсли;
	
	Возврат СтрШаблон(Шаблон, ПредставлениеВозраста, ПредставлениеРежимаЛечения);
КонецФункции

// Отличие от функции ЗарплатаКадрыКлиентСервер.ПредставлениеЛет во втором склонении.
Функция ПредставлениеЛет(Лет)
	Возврат СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(НСтр("ru = ';%1 года;;%1 лет;%1 лет;';
																			|en = ';%1 years;;%1 years;%1 years;'"), Лет);
КонецФункции

#КонецОбласти

#КонецЕсли


