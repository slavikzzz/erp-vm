
#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если НЕ Параметры.Отбор.Свойство("Договор") Тогда
		Возврат
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ДанныеВыбора = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.АрендныеОбязательства) КАК Ссылка,
	|	1 КАК Порядок
	|ИЗ
	|	Справочник.ДоговорыАренды КАК Т
	|ГДЕ
	|	Т.Ссылка = &Договор
	|	И НЕ &ЭтоПлатеж
	|	И НЕ &ВозвратПлатежа
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.УслугаПоАренде) КАК Ссылка,
	|	2 КАК Порядок
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.УслугаПоАрендеАванс) КАК Ссылка,
	|	3 КАК Порядок
	|ИЗ
	|	Справочник.ДоговорыАренды КАК Т
	|ГДЕ
	|	Т.Ссылка = &Договор
	|	И Т.ВестиУчетАвансовПоТекущимАренднымПлатежамВРазрезеПлатежныхДокументов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.ОбеспечительныйПлатеж),
	|	4 КАК Порядок
	|ИЗ
	|	Справочник.ДоговорыАренды КАК Т
	|ГДЕ
	|	Т.Ссылка = &Договор
	|	И Т.ЕстьОбеспечительныйПлатеж
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.ВыкупнаяСтоимость) КАК Ссылка,
	|	5 КАК Порядок
	|ИЗ
	|	Справочник.ДоговорыАренды КАК Т
	|ГДЕ
	|	Т.Ссылка = &Договор
	|	И Т.ЕстьВыкупПредметовАренды
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.ВыкупнаяСтоимостьАванс) КАК Ссылка,
	|	6 КАК Порядок
	|ИЗ
	|	Справочник.ДоговорыАренды КАК Т
	|ГДЕ
	|	Т.Ссылка = &Договор
	|	И Т.ЕстьАвансированиеВыкупнойСтоимости
	|	И НЕ &КорректировкаЗадолженности
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.Проценты) КАК Ссылка,
	|	7 КАК Порядок
	|ИЗ
	|	Справочник.ДоговорыАренды КАК Т
	|ГДЕ
	|	Т.Ссылка = &Договор
	|	И НЕ &ЭтоПлатеж
	|	И НЕ &ВозвратПлатежа
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.НДС) КАК Ссылка,
	|	8 КАК Порядок
	|ИЗ
	|	Справочник.ДоговорыАренды КАК Т
	|ГДЕ
	|	Т.Ссылка = &Договор
	|	И НЕ &ЭтоПлатеж
	|	И НЕ &ВозвратПлатежа
	|УПОРЯДОЧИТЬ ПО
	|	Порядок";
	
	Запрос.УстановитьПараметр("Договор", Параметры.Отбор.Договор);
	Если Параметры.Отбор.Свойство("Платеж") Тогда
		Запрос.УстановитьПараметр("ЭтоПлатеж", Параметры.Отбор.Платеж);
	Иначе
		Запрос.УстановитьПараметр("ЭтоПлатеж", Ложь);
	КонецЕсли;
	Запрос.УстановитьПараметр("ВозвратПлатежа",
		?(Параметры.Отбор.Свойство("ВозвратПлатежа"), Параметры.Отбор.ВозвратПлатежа, Ложь));
	
	Если Параметры.Свойство("КорректировкаЗадолженности") И Параметры.КорректировкаЗадолженности Тогда
		Запрос.УстановитьПараметр("КорректировкаЗадолженности", Истина);
	Иначе
		Запрос.УстановитьПараметр("КорректировкаЗадолженности", Ложь);
	КонецЕсли;
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ДанныеВыбора.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти