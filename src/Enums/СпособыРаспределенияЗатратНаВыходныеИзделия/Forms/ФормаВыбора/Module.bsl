
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Параметры.Свойство("СпособРаспределенияЗатратНаВыходныеИзделия") Тогда
		ВызватьИсключение НСтр("ru = 'Для открытия формы необходимо передать параметры.';
								|en = 'To open the form, pass the parameters.'");
	КонецЕсли;
	
	СпособРаспределенияЗатратНаВыходныеИзделия = Параметры.СпособРаспределенияЗатратНаВыходныеИзделия;
	ПроверитьВывестиИнформационноеСообщение();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СпособРаспределенияЗатратНаВыходныеИзделияПриИзменении(Элемент)
	
	ПроверитьВывестиИнформационноеСообщение();
	
КонецПроцедуры

&НаКлиенте
Процедура ИнформационнаяСтрокаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ПерейтиРазделПроизводство" Тогда
		
		ОткрытьФорму("Обработка.ПанельАдминистрированияКА.Форма.Производство",, ЭтаФорма);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ЗначениеВыбора = Новый Структура;
	ЗначениеВыбора.Вставить("СпособРаспределенияЗатратНаВыходныеИзделия", СпособРаспределенияЗатратНаВыходныеИзделия);
	ОповеститьОВыборе(ЗначениеВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Модифицированность = Ложь;
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьВывестиИнформационноеСообщение()
	
	ИнформационнаяСтрока = Неопределено;
	
	Если СпособРаспределенияЗатратНаВыходныеИзделия = Перечисления.СпособыРаспределенияЗатратНаВыходныеИзделия.ПоПлановойСтоимости Тогда
		
		МассивСтрок = Новый Массив();
		ВидЦены = Справочники.ВидыЦен.ВидЦеныПлановойСтоимостиТМЦ();
		Если Не ЗначениеЗаполнено(ВидЦены) Тогда
			ТекстСтроки = НСтр("ru = 'В настройках раздела ""Производство"" не задан вид цены плановой стоимости выходных изделий';
								|en = 'Price type of the standard cost for finished products is not specified in the Production section settings'");
			МассивСтрок.Добавить(Новый ФорматированнаяСтрока(ТекстСтроки));
			МассивСтрок.Добавить(" ");
			МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = '(открыть)';
																	|en = '(open)'"),,,, "ПерейтиРазделПроизводство"));
		КонецЕсли;
		ИнформационнаяСтрока = Новый ФорматированнаяСтрока(МассивСтрок);
	
	КонецЕсли;
	
	Элементы.ИнформационнаяПанель.Видимость = ЗначениеЗаполнено(ИнформационнаяСтрока);
	
КонецПроцедуры

#КонецОбласти