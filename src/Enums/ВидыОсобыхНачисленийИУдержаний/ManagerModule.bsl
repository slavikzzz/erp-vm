#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает виды доходов исполнительного производства начислений
// Параметры:
//	Период - дата, на которую необходимо получить виды доходов исполнительного производства.
//
// Возвращаемое значение:
// 	Соответствие:
// 	 	* Ключ     - ПеречислениеСсылка.ВидыОсобыхНачисленийИУдержаний
// 	 	* Значение - ПеречислениеСсылка.ВидыДоходовИсполнительногоПроизводства
// 
Функция ВидыДоходовИсполнительногоПроизводства(Период) Экспорт

	ВидыДоходовИсполнительногоПроизводства = Новый Соответствие;
	
	ВидыДоходов = Перечисления.ВидыДоходовИсполнительногоПроизводства;
	ВидыДоходовИсполнительногоПроизводства.Вставить(Дивиденды,            ВидыДоходов.ДоходыБезОграниченияВзысканий);
	ВидыДоходовИсполнительногоПроизводства.Вставить(ДивидендыСотрудников, ВидыДоходов.ДоходыБезОграниченияВзысканий);
	
	ВидыДоходовИсполнительногоПроизводства.Вставить(ДоговорАвторскогоЗаказа, ВидыДоходов.ЗарплатаВознаграждения);
	ВидыДоходовИсполнительногоПроизводства.Вставить(ДоговорРаботыУслуги,     ВидыДоходов.ЗарплатаВознаграждения);
	
	ВидыДоходовИсполнительногоПроизводства.Вставить(КомпенсацияЗаЗадержкуЗарплаты, ВидыДоходов.ЗарплатаВознаграждения);
	
	// МатериальнаяВыгодаПоЗаймам не является доходом вообще, в том смысле, 
	// что ИЛ с ее сумм не должны начисляться, и ее НДФЛ можно регистрировать как угодно.
	// Призы проходят в "Перечне видов заработной платы и иного дохода, из которых производится удержание алиментов"
	// в пункте "м) с других видов выплат к заработной плате, установленных законодательством..."
	// Поэтому относим эти виды особых начислений и удержаний к "зарплатным" доходам.
	ВидыДоходовИсполнительногоПроизводства.Вставить(МатериальнаяВыгодаПоЗаймам, ВидыДоходов.ЗарплатаВознаграждения);
	ВидыДоходовИсполнительногоПроизводства.Вставить(СтоимостьПодарковПризов,    ВидыДоходов.ЗарплатаВознаграждения);
	Если ВидыДоходовИсполнительногоПроизводствавызовСервера.ВступилиВСилуИзменения353ФЗ(Период) Тогда
		ВидыДоходовИсполнительногоПроизводства.Вставить(ПособиеПриПостановкеНаУчетВРанниеСрокиБеременности, ВидыДоходов.КомпенсацииНеоблагаемые);
		ВидыДоходовИсполнительногоПроизводства.Вставить(ПособиеПриРожденииРебенка,     ВидыДоходов.КомпенсацииНеоблагаемыеЕдиновременные);
		ВидыДоходовИсполнительногоПроизводства.Вставить(ПособиеНаПогребение,           ВидыДоходов.КомпенсацииНеоблагаемыеЕдиновременные);
		ВидыДоходовИсполнительногоПроизводства.Вставить(ПособиеНаПогребениеСотруднику, ВидыДоходов.КомпенсацииНеоблагаемыеЕдиновременные);
	Иначе
		ВидыДоходовИсполнительногоПроизводства.Вставить(ПособиеПриПостановкеНаУчетВРанниеСрокиБеременности, ВидыДоходов.КомпенсацииНеоблагаемые);
		ВидыДоходовИсполнительногоПроизводства.Вставить(ПособиеПриРожденииРебенка,     ВидыДоходов.КомпенсацииНеоблагаемые);
		ВидыДоходовИсполнительногоПроизводства.Вставить(ПособиеНаПогребение,           ВидыДоходов.КомпенсацииНеоблагаемые);
		ВидыДоходовИсполнительногоПроизводства.Вставить(ПособиеНаПогребениеСотруднику, ВидыДоходов.КомпенсацииНеоблагаемые);
	КонецЕсли;
	
	Возврат ВидыДоходовИсполнительногоПроизводства
	
КонецФункции

Функция СтрокиИсчисленныхНалогов() Экспорт

	ПереченьСтрок = Новый Массив;
	ПереченьСтрок.Добавить(НДФЛ);
	ПереченьСтрок.Добавить(НДФЛСПревышения);
	ПереченьСтрок.Добавить(НДФЛСПревышенияПоСтавке18);
	ПереченьСтрок.Добавить(НДФЛСПревышенияПоСтавке20);
	ПереченьСтрок.Добавить(НДФЛСПревышенияПоСтавке22);
	
	Возврат ПереченьСтрок
	
КонецФункции

// Устанавливает соответствие имен колонок и значений перечисления
// Параметры:
//	ИмяПервойКолонки - строка.
//
// Возвращаемое значение:
// 	Соответствие:
// 	 	* Ключ     - строка
// 	 	* Значение - ПеречислениеСсылка.ВидыОсобыхНачисленийИУдержаний
// 
Функция СоответствиеКолонкамДанных(ИмяПервойКолонки = "Сумма") Экспорт 

	ОбратноеСоответствие = СоответствиеКолонокЗначениям(ИмяПервойКолонки);
	ПереченьСтрок = Новый Соответствие;
	Для каждого Элемент Из ОбратноеСоответствие Цикл
		ПереченьСтрок.Вставить(Элемент.Значение, Элемент.Ключ);
	КонецЦикла;
	
    Возврат ПереченьСтрок
	
КонецФункции 

// Устанавливает соответствие значений перечисления и имен колонок 
// Параметры:
//	ИмяПервойКолонки - строка.
//
// Возвращаемое значение:
// 	Соответствие:
// 	 	* Ключ     - ПеречислениеСсылка.ВидыОсобыхНачисленийИУдержаний
// 	 	* Значение - строка
// 
Функция СоответствиеКолонокЗначениям(ИмяПервойКолонки = "Сумма") Экспорт 

    Возврат УчетНДФЛКлиентСервер.СоответствиеКолонокНалогаЗначениямВидовОсобыхНачисленийИУдержаний(ИмяПервойКолонки)
	
КонецФункции 


#КонецОбласти

#КонецЕсли