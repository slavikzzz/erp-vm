#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает имя области макета списков, в котором содержится ограничение
// классификатора для переданного значения перечисления (ВидыСвободныхСтрокФормСтатистики).
//
// Параметры:
//	Значение - ПеречислениеСсылка.ВидыСвободныхСтрокФормСтатистики - Значение, для которого получается области.
//	ВерсияСписка - Строка - Имя списка с периодом применения.
//
// Возвращаемое значение:
//	Строка - Имя области.
//
Функция ИмяОбластиМакетаОграничения(Значение, ВерсияСписка = "") Экспорт
	
	ИмяОбластиМакетаОграничения = Неопределено;
	
	Если Значение = ВидыДеятельности Тогда
		
		ИмяОбластиМакетаОграничения = "pril_okved_51";	
		
	ИначеЕсли Значение = ВидыПродукцииПроизводство Тогда	
		
		Если ВерсияСписка = "Списки2010Кв1" Тогда
			ИмяОбластиМакетаОграничения = "pril_okp_50";	
		Иначе	
			ИмяОбластиМакетаОграничения = "pril_okp_55";	
		КонецЕсли;
		
	ИначеЕсли Значение = ВидыПродукцииРозница Тогда	
		
		ИмяОбластиМакетаОграничения = "pril_okp_6";	
		
	ИначеЕсли Значение = ВидыПродукцииОпт Тогда	
		
		ИмяОбластиМакетаОграничения = "pril_okp_7";	

	ИначеЕсли Значение = ВидыУслугРозница Тогда	
		
		ИмяОбластиМакетаОграничения = "pril_okp_U";	
		
	КонецЕсли;
	
	Возврат ИмяОбластиМакетаОграничения;	
	
КонецФункции

// Возвращает список значений перечисления которыми может ограничиваться отбор для переданного классификатора.
//
// Параметры:
// 	Классификатор - Строка - Имя справочника.
//
// Возвращаемое значение:
//	СписокЗначений - Список видов деятельности.
//
Функция СписокНазначенийДляКлассификатора(Классификатор) Экспорт
	
	СписокОграничений = Новый СписокЗначений();
	
	Если Классификатор = "КлассификаторВидовЭкономическойДеятельности" Тогда
		
		СписокОграничений.Добавить(ВидыДеятельности, НСтр("ru = 'Виды деятельности для форм статистики';
															|en = 'Activity categorys for statistics forms'"));
		 
	 ИначеЕсли Классификатор = "КлассификаторПродукцииПоВидамДеятельности" Тогда
	 
		СписокОграничений.Добавить(ВидыПродукцииПроизводство, 	НСтр("ru = 'Производственная продукция';
																		|en = 'Manufacturing products'"));
		СписокОграничений.Добавить(ВидыПродукцииОпт, 			НСтр("ru = 'Товары в оптовой торговле';
																		|en = 'Wholesale goods '"));
		СписокОграничений.Добавить(ВидыПродукцииРозница, 		НСтр("ru = 'Товары в рознице';
																		|en = 'Retail goods'"));
		СписокОграничений.Добавить(ВидыУслугРозница,			НСтр("ru = 'Услуги';
																	|en = 'Services'"));
		 
	КонецЕсли;	
	
	Возврат СписокОграничений;
	
КонецФункции

// Возвращает имя области макета списков, в котором содержатся данные
//	о единицах измерения для переданного значения перечисления.
//
// Параметры:
// 	Значение - Перечисления.ВидыСвободныхСтрокФормСтатистики - Значение, для которого требуется имя области.
//
// Возвращаемое значение:
//	Строка - Имя области.
//
Функция ИмяОбластиЕдиницаИзмерения(Значение) Экспорт
	
	ИмяОбластиЕдиницаИзмерения = Неопределено;
	
	Если Значение = ВидыПродукцииПроизводство Тогда	
		
		ИмяОбластиЕдиницаИзмерения = "s_okp_ei_50";	
		
	ИначеЕсли Значение = ВидыПродукцииРозница Тогда	
		
		ИмяОбластиЕдиницаИзмерения = "s_okp_ei_70";	
		
	ИначеЕсли Значение = ВидыПродукцииОпт Тогда	
		
		ИмяОбластиЕдиницаИзмерения = "s_okp_ei_80";	
		
	КонецЕсли;	
	
	Возврат ИмяОбластиЕдиницаИзмерения;	
	
КонецФункции

// Возвращает значение перечисления соответствующее переданному классификатору,
// для классификаторов которым соответствует несколько перечислений возвращается - неопределено.
//
// Параметры:
// 	Классификатор - Строка - Имя справочника.
//
// Возвращаемое значение:
//	ПеречислениеСсылка.ВидыСвободныхСтрокФормСтатистики, Неопределено - Значение перечисления.
//
Функция ЗначениеПоУмолчаниюДляКлассификатора(Классификатор) Экспорт
	
	Значение = Неопределено;
	
	Если Классификатор = "КлассификаторВидовЭкономическойДеятельности" Тогда
		
		Значение = ВидыДеятельности;
		 
	ИначеЕсли Классификатор = "КлассификаторУслугНаселению" Тогда
	 
		Значение = УслугиНаселению;
		 
	КонецЕсли;	
	
	Возврат Значение;
	
КонецФункции

// Возвращает краткое представление для классификатора.
//
// Параметры:
//	Значение - ПеречислениеСсылка.ВидыСвободныхСтрокФормСтатистики - Значение перечисления.
//
// Возвращаемое значение:
//	Строка - Представление классификатора.
//
Функция КраткоеПредставлениеКлассификатора(Значение) Экспорт
	
	Если Значение = ВидыДеятельности Тогда
		Возврат НСтр("ru = 'ОКВЭД';
					|en = 'RNCEA'");
	ИначеЕсли Значение = ВидыПродукцииОпт
		ИЛИ Значение = ВидыПродукцииПроизводство
		ИЛИ Значение = ВидыПродукцииРозница
		ИЛИ Значение = ВидыУслугРозница Тогда
		Возврат НСтр("ru = 'ОКПД';
					|en = 'RNCPA'");
	ИначеЕсли Значение = УслугиНаселению Тогда
		Возврат НСтр("ru = 'ОКУН';
					|en = 'RNCS'");
	Иначе
		Возврат НСтр("");
	КонецЕсли;
	
КонецФункции

// Возвращает тип справочника, содержащего классификатор.
//
// Параметры:
//	Значение - ПеречислениеСсылка.ВидыСвободныхСтрокФормСтатистики - Значение перечисления.
//
// Возвращаемое значение:
//	Тип - Тип справочника с классификатором.
//
Функция ТипКлассификатора(Значение) Экспорт
	
	Если Значение = ВидыДеятельности Тогда
		Возврат Тип("СправочникСсылка.КлассификаторВидовЭкономическойДеятельности");
	ИначеЕсли Значение = ВидыПродукцииОпт
		ИЛИ Значение = ВидыПродукцииПроизводство
		ИЛИ Значение = ВидыПродукцииРозница
		ИЛИ Значение = ВидыУслугРозница Тогда
		Возврат Тип("СправочникСсылка.КлассификаторПродукцииПоВидамДеятельности");
	ИначеЕсли Значение = УслугиНаселению Тогда
		Возврат Тип("СправочникСсылка.КлассификаторУслугНаселению");
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Возвращает имя справочника, содержащего классификатор.
//
// Параметры:
//	Значение - ПеречислениеСсылка.ВидыСвободныхСтрокФормСтатистики - Значение перечисления.
//
// Возвращаемое значение:
//	Строка - Имя справочника с классификатором.
//
Функция ИмяСправочника(Значение) Экспорт
		
	Если Значение = ВидыДеятельности Тогда
		Возврат "КлассификаторВидовЭкономическойДеятельности";
	ИначеЕсли Значение = ВидыПродукцииОпт
		ИЛИ Значение = ВидыПродукцииПроизводство
		ИЛИ Значение = ВидыПродукцииРозница
		ИЛИ Значение = ВидыУслугРозница Тогда
		Возврат "КлассификаторПродукцииПоВидамДеятельности";
	ИначеЕсли Значение = УслугиНаселению Тогда
		Возврат "КлассификаторУслугНаселению";
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецЕсли