#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Адрес = Параметры.Адрес;
	ЭтапПодготовкиБюджетов = Параметры.ЭтапПодготовкиБюджетов;
	МодельБюджетирования = Параметры.МодельБюджетирования;
	
	НастройкиДействия = ПолучитьИзВременногоХранилища(Адрес);
	Перечисления.ТипыДействийЭтаповПодготовкиБюджетов.ВосстановитьНастройкиДействия(НастройкиДействия, ЭтаФорма);
	
	Для каждого Элемент Из УтверждаемыеЭтапыПодготовкиБюджетов Цикл
		НоваяСтрока = ЭтапыПодготовкиБюджетов.Добавить();
		НоваяСтрока.ЭтапПодготовкиБюджетов = Элемент.Значение;
	КонецЦикла;
	
	ЗаполнитьПараметрыВыбораУтверждаемыхШагов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СохранитьНастройки(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Закрыть(СохранитьНастройкиНаСервере());
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьПараметрыВыбораУтверждаемыхШагов()
	
	// Определим шаги процесса, ввод документов на которых можно утверждать на данном шаге.
	ШагиДляУтверждения = Новый Массив;
	
	ТаблицаЗадач = Справочники.МоделиБюджетирования.ПолучитьБюджетныеЗадачи(МодельБюджетирования, , ТекущаяДатаСеанса());
	НачалоДействияМодели = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(МодельБюджетирования, "НачалоДействия");
	ПараметрыПоиска = Новый Структура("ЭтапПодготовкиБюджетов, Период", ЭтапПодготовкиБюджетов, НачалоДействияМодели);
	СтрокиЭтапов = ТаблицаЗадач.НайтиСтроки(ПараметрыПоиска);
	Если СтрокиЭтапов.Количество() Тогда
		СтрокаШагаПроцесса = СтрокиЭтапов[0];
		ЗаполнитьШагиДляУтвержденияРекурсивно(ТаблицаЗадач, СтрокаШагаПроцесса, ШагиДляУтверждения);
	КонецЕсли;
	
	ПараметрыВыбора = Новый Массив;
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", Новый ФиксированныйМассив(ШагиДляУтверждения)));
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.ПометкаУдаления", Ложь));
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.НеВыполняется", Ложь));
	Элементы.ЭтапПодготовкиБюджетов.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьШагиДляУтвержденияРекурсивно(ТаблицаЗадач, СтрокаШагаПроцесса, ШагиДляУтверждения)
	
	Для каждого ПредыдущаяЗадача Из СтрокаШагаПроцесса.ПредыдущиеЗадачи Цикл
		НомерЗадачи = ПредыдущаяЗадача.НомерЗадачи;
		СтрокаПредыдущейЗадачи = ТаблицаЗадач.Найти(НомерЗадачи, "НомерЗадачи");
		Если СтрокаПредыдущейЗадачи.Действие = Перечисления.ТипыДействийЭтаповПодготовкиБюджетов.ВводПлана
			Или СтрокаПредыдущейЗадачи.Действие = Перечисления.ТипыДействийЭтаповПодготовкиБюджетов.ВводБюджетов Тогда
			ШагиДляУтверждения.Добавить(СтрокаПредыдущейЗадачи.ЭтапПодготовкиБюджетов);
		КонецЕсли;
		ЗаполнитьШагиДляУтвержденияРекурсивно(ТаблицаЗадач, СтрокаПредыдущейЗадачи, ШагиДляУтверждения)
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция СохранитьНастройкиНаСервере()
	
	УтверждаемыеЭтапыПодготовкиБюджетов.Очистить();
	Для каждого Строка Из ЭтапыПодготовкиБюджетов Цикл
		УтверждаемыеЭтапыПодготовкиБюджетов.Добавить(Строка.ЭтапПодготовкиБюджетов);
	КонецЦикла;
	
	Настройки = Перечисления.ТипыДействийЭтаповПодготовкиБюджетов.НастройкиДействия(ЭтаФорма);
	Возврат ПоместитьВоВременноеХранилище(Настройки, Адрес);
	
КонецФункции

#КонецОбласти
