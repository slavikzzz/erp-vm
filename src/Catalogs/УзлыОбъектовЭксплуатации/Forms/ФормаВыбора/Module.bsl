
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Список.Параметры.УстановитьЗначениеПараметра("ИсточникНаработки", Ложь);
	Список.Параметры.УстановитьЗначениеПараметра("ОграничениеВыбора", Ложь);
	
	ОграничениеВыбора = Ложь;
	
	Если Параметры.Свойство("ИсточникНаработки") Тогда
		Список.Параметры.УстановитьЗначениеПараметра("ИсточникНаработки", Параметры.ИсточникНаработки);
		Список.Параметры.УстановитьЗначениеПараметра("ОграничениеВыбора", Истина);
		ОграничениеВыбора = Истина;
	КонецЕсли;
	
	Если Параметры.Свойство("Отбор") Тогда
		Если Параметры.Отбор.Свойство("Владелец") Тогда
			
			ОбъектЭксплуатации = Параметры.Отбор.Владелец;
			
		КонецЕсли;
	КонецЕсли;

	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено И Не ТекущиеДанные.ДоступностьВыбора Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ТекстПредупреждения = НСтр("ru = 'Узел ""%Узел%"" недоступен для выбора в документ.
		|Нет доступных показателей наработки, регистрируемых без источника';
		|en = 'Cannot select the ""%Узел%"" sub-asset for the document.
		|No available operating time units registered without a source were found'");
		ТекстПредупреждения = СтрЗаменить(ТекстПредупреждения, "%Узел%", ТекущиеДанные.Наименование);
		ПоказатьПредупреждение(Неопределено, ТекстПредупреждения);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Наименование.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.Наименование");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", ОбъектЭксплуатации);

КонецПроцедуры

#КонецОбласти
