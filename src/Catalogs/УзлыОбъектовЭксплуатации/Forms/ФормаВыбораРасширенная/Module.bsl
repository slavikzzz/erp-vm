
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("Владелец") Тогда
		Если ЗначениеЗаполнено(Параметры.Отбор.Владелец) Тогда
			ОбъектЭксплуатации = Параметры.Отбор.Владелец;
			Элементы.Владелец.Видимость = Ложь;
		Иначе
			Параметры.Отбор.Удалить("Владелец");
		КонецЕсли;
	КонецЕсли;
	
	Список.Параметры.УстановитьЗначениеПараметра("СписокДоступныхУзлов", УправлениеРемонтами.ДоступныеДляВыбораУзлы(Параметры));
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено И Не ТекущиеДанные.ДоступностьВыбора Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ТекстПредупреждения = НСтр(
			"ru = 'Узел ""%Узел%"" недоступен для выбора.
			|Нет доступных показателей наработки';
			|en = 'Cannot select the ""%Узел%"" sub-asset.
			|No available operating time units were found'");
		ТекстПредупреждения = СтрЗаменить(ТекстПредупреждения, "%Узел%", ТекущиеДанные.Наименование);
		ПоказатьПредупреждение(Неопределено, ТекстПредупреждения);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти
