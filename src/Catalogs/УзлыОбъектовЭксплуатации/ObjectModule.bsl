#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Процедура инициализации дополнительных свойств узла для последующей записи
//
// Параметры:
// 		ЗначенияДополнительныхСвойств - Структура - Структура доп. свойств для записи объекта.
//
Процедура ИнициализироватьДополнительныеСвойстваДляЗаписи(ЗначенияДополнительныхСвойств = Неопределено) Экспорт
	
	Если ДополнительныеСвойства.Свойство("ИнициализированыДляЗаписи") Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
	ДополнительныеСвойства.Вставить("ЭтоПодчиненный", ЗначениеЗаполнено(Родитель));
	ДополнительныеСвойства.Вставить("РаспространитьСтатус", Статус = Перечисления.СтатусыОбъектовЭксплуатации.Ликвидирован);
	ДополнительныеСвойства.Вставить("ОбновитьНаименованиеПолное", ПустаяСтрока(НаименованиеПолное));
	
	Если ЗначенияДополнительныхСвойств = Неопределено Тогда
		
		ДополнительныеСвойства.Вставить("ЗаписьИзВнешнегоОбъекта", Ложь);
		ДополнительныеСвойства.Вставить("ЗначенияЗаполнения", Неопределено);
		ДополнительныеСвойства.Вставить("ДействияЗаполнения", Неопределено);
		
		ДополнительныеСвойства.Вставить("ТребуетсяЗаполнениеПодчиненных", Ложь);
		ДополнительныеСвойства.Вставить("ЗначенияЗаполненияПодчиненных", Новый Структура);
		ДополнительныеСвойства.Вставить("ДействияЗаполненияПодчиненных", Новый Структура);
		
		Если ДополнительныеСвойства.ЭтоНовый Тогда
			
			ДополнительныеСвойства.Вставить(
				"РеквизитыДоЗаписи",
				Новый Структура("Наименование, НаименованиеПолное, Владелец, Родитель, Статус"));
			
			ЗаполнитьЗначенияСвойств(ДополнительныеСвойства.РеквизитыДоЗаписи, ЭтотОбъект);
			
		Иначе
			
			ДополнительныеСвойства.Вставить(
				"РеквизитыДоЗаписи",
				ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "Наименование, НаименованиеПолное, Владелец, Родитель, Статус"));
			
		КонецЕсли;
		
		РеквизитыДоЗаписи = ДополнительныеСвойства.РеквизитыДоЗаписи; // Структура - содержит свойства: Наименование
		
		ДополнительныеСвойства.ОбновитьНаименованиеПолное = (
			ПустаяСтрока(НаименованиеПолное)
			Или (НаименованиеПолное = РеквизитыДоЗаписи.НаименованиеПолное
				И РеквизитыДоЗаписи.НаименованиеПолное = РеквизитыДоЗаписи.Наименование));
		
		Если ДополнительныеСвойства.ЭтоПодчиненный Тогда
			
			ДополнительныеСвойства.Вставить(
				"РеквизитыРодителя",
				ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Родитель, "Статус"));
			
			ДополнительныеСвойства.Вставить(
				"БратьСтатусОтРодителя",
				ДополнительныеСвойства.РеквизитыРодителя.Статус = Перечисления.СтатусыОбъектовЭксплуатации.Ликвидирован);
			
			ДополнительныеСвойства.Вставить(
				"РаспространитьСтатус",
				ДополнительныеСвойства.БратьСтатусОтРодителя);
			
		КонецЕсли;
		
	Иначе
		
		ОбщегоНазначенияУТКлиентСервер.ДополнитьСтруктуру(ДополнительныеСвойства, ЗначенияДополнительныхСвойств, Истина);
		
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ИнициализированыДляЗаписи");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("Структура") Тогда
		
		ЗаполнитьНаОснованииСтруктуры(ДанныеЗаполнения);
		
	КонецЕсли;
	
	ИнициализироватьСправочник();
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ИнициализироватьСправочник();
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИнициализироватьДополнительныеСвойстваДляЗаписи();
	
	Если ДополнительныеСвойства.ЗаписьИзВнешнегоОбъекта Тогда
		ПередЗаписьюИзВнешнегоОбъекта(Отказ);
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.ОбновитьНаименованиеПолное Тогда
		
		НаименованиеПолное = Наименование;
		
	КонецЕсли;
	
	Если ДополнительныеСвойства.ЭтоПодчиненный И ДополнительныеСвойства.БратьСтатусОтРодителя Тогда
		Если ДополнительныеСвойства.РеквизитыРодителя.Статус <> Статус Тогда
			Статус = ДополнительныеСвойства.РеквизитыРодителя.Статус;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ДополнительныеСвойства.ЭтоНовый Тогда
		
		ЗаполнитьПринадлежность();
		
	Иначе
		
		Если ДополнительныеСвойства.РеквизитыДоЗаписи.Наименование <> Наименование Тогда
			
			ЗаполнитьПринадлежность();
			
			ДополнительныеСвойства.ТребуетсяЗаполнениеПодчиненных = Истина;
			ДополнительныеСвойства.ДействияЗаполненияПодчиненных.Вставить("ЗаполнитьПринадлежность");
			
		КонецЕсли;
		
		Если ДополнительныеСвойства.РеквизитыДоЗаписи.Статус <> Статус
			И ДополнительныеСвойства.РаспространитьСтатус Тогда
			
			ДополнительныеСвойства.ТребуетсяЗаполнениеПодчиненных = Истина;
			ДополнительныеСвойства.ЗначенияЗаполненияПодчиненных.Вставить("Статус", Статус);
			
		КонецЕсли;
		
		Если ДополнительныеСвойства.РеквизитыДоЗаписи.Владелец <> Владелец Тогда
			ДополнительныеСвойства.ТребуетсяЗаполнениеПодчиненных = Истина;
			ДополнительныеСвойства.ЗначенияЗаполненияПодчиненных.Вставить("Владелец", Владелец);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.ЗаписьИзВнешнегоОбъекта Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ДополнительныеСвойства.ТребуетсяЗаполнениеПодчиненных Тогда
		
		СтруктураДополнительныхСвойств = Новый Структура;
		СтруктураДополнительныхСвойств.Вставить("ЭтоНовый", Ложь);
		СтруктураДополнительныхСвойств.Вставить("ЗаписьИзВнешнегоОбъекта", Истина);
		СтруктураДополнительныхСвойств.Вставить("ЗначенияЗаполнения", ДополнительныеСвойства.ЗначенияЗаполненияПодчиненных);
		СтруктураДополнительныхСвойств.Вставить("ДействияЗаполнения", ДополнительныеСвойства.ДействияЗаполненияПодчиненных);
		СтруктураДополнительныхСвойств.Вставить("ТребуетсяЗаполнениеПодчиненных", Ложь);
		
		СтруктураОтбора = Новый Структура("Родитель", Ссылка);
		
		Справочники.УзлыОбъектовЭксплуатации.ЗаполнитьПоОтбору(СтруктураОтбора, СтруктураДополнительныхСвойств, Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	// Стандартный механизм проверки заполнения применяется только для проверки стандартных реквизитов: "Наименование",
	// "Код" и "Владелец" Проверка реквизитов, доп. реквизитов и табличных частей инициируется владельцем (объектом
	// эксплуатации) или формой элемента узла.
	
	ПроверяемыеРеквизиты.Очистить();
	
	ПроверяемыеРеквизиты.Добавить("Наименование");
	ПроверяемыеРеквизиты.Добавить("Владелец");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ИнициализироватьСправочник()
	
	Статус = Перечисления.СтатусыОбъектовЭксплуатации.ВЭксплуатации;
	
КонецПроцедуры

Процедура ПередЗаписьюИзВнешнегоОбъекта(Отказ)
	
	Если ДополнительныеСвойства.ЗначенияЗаполнения <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДополнительныеСвойства.ЗначенияЗаполнения);
	КонецЕсли;
	
	Если ДополнительныеСвойства.ДействияЗаполнения <> Неопределено Тогда
		
		Если ДополнительныеСвойства.ДействияЗаполнения.Свойство("ЗаполнитьПринадлежность") Тогда
			
			ЗаполнитьПринадлежность();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьНаОснованииСтруктуры(ДанныеЗаполнения)
	
	Если ДанныеЗаполнения.Свойство("Родитель") Тогда
		РеквизитыРодителя = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения.Родитель, "Статус");
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыРодителя);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПринадлежность()
	
	Результат = "";
	
	Разделитель = "\";
	
	ПолныйПуть = ЭтотОбъект.ПолноеНаименование();
	ПолныйПуть = СтрЗаменить(ПолныйПуть, "/", Символы.ПС);
	
	Для н=1 По СтрЧислоСтрок(ПолныйПуть) Цикл
		Результат = Разделитель + СтрПолучитьСтроку(ПолныйПуть, н) + Результат;
	КонецЦикла;
	
	Принадлежность = Сред(Результат, 2) + Разделитель + Строка(ЭтотОбъект.Владелец);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
