#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Наименование = НастройкаСчетовУчетаКлиентСервер.ПредставлениеНастройкиОтражения(ЭтотОбъект, Неопределено, Неопределено);
	
	Если НЕ ЗначениеЗаполнено(ВладелецНастройки) Тогда
		ВладелецНастройки = Неопределено;
	КонецЕсли;
	
	ХешСумма = ХешСуммаНастройкиСчетов();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ХешСуммаНастройкиСчетов()
	
	ДанныеХешСуммы = Новый Массив();
	
	//++ Локализация
	Если ЗначениеЗаполнено(СчетУчета) Тогда
		ДобавитьСчетИСубконтоВДанныеХешСуммы(ЭтотОбъект, ДанныеХешСуммы);
	КонецЕсли;
	//-- Локализация
	
	//++ НЕ УТКА
	НастройкиМУ = Новый ТаблицаЗначений();
	НастройкиМУ.Колонки.Добавить("ПланСчетов", ОбщегоНазначения.ОписаниеТипаСтрока(50));
	НастройкиМУ.Колонки.Добавить("СчетУчета", ОбщегоНазначения.ОписаниеТипаСтрока(50));
	НастройкиМУ.Колонки.Добавить("Субконто1");
	НастройкиМУ.Колонки.Добавить("Субконто2");
	НастройкиМУ.Колонки.Добавить("Субконто3");
	
	Для Каждого МеждународнаяНастройка Из НастройкиМеждународногоУчета Цикл
		Если ЗначениеЗаполнено(МеждународнаяНастройка.СчетУчета) Тогда
			НоваяСтрока = НастройкиМУ.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, МеждународнаяНастройка, "Субконто1,Субконто2,Субконто3");
			НоваяСтрока.ПланСчетов = XMLСтрока(МеждународнаяНастройка.ПланСчетов);
			НоваяСтрока.СчетУчета = XMLСтрока(МеждународнаяНастройка.СчетУчета);
		КонецЕсли;
	КонецЦикла;

	НастройкиМУ.Сортировать("ПланСчетов,СчетУчета");
	
	Для Каждого НастройкаМУ Из НастройкиМУ Цикл
		ДанныеХешСуммы.Добавить(НастройкаМУ.ПланСчетов);
		ДобавитьСчетИСубконтоВДанныеХешСуммы(НастройкаМУ, ДанныеХешСуммы);
	КонецЦикла;
	//-- НЕ УТКА
	
	ХешированиеДанных = Новый ХешированиеДанных(ХешФункция.SHA256);
	ХешированиеДанных.Добавить(СтрСоединить(ДанныеХешСуммы));
	
	Возврат Base64Строка(ХешированиеДанных.ХешСумма);
	
КонецФункции

Процедура ДобавитьСчетИСубконтоВДанныеХешСуммы(ДанныеНастройки, ДанныеХешСуммы)
	
	ДанныеХешСуммы.Добавить(XMLСтрока(ДанныеНастройки.СчетУчета));
	
	Если ЗначениеЗаполнено(ДанныеНастройки.Субконто1) Тогда
		ДанныеХешСуммы.Добавить(XMLСтрока(ДанныеНастройки.Субконто1));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеНастройки.Субконто2) Тогда
		ДанныеХешСуммы.Добавить(XMLСтрока(ДанныеНастройки.Субконто2));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеНастройки.Субконто3) Тогда
		ДанныеХешСуммы.Добавить(XMLСтрока(ДанныеНастройки.Субконто3));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли