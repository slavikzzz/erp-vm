#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	НастройкиПодсистемы = ПроизводствоСервер.НастройкиПодсистемыПроизводство();
	ПланируетсяГрафик =
		//++ Устарело_Производство21
		НастройкиПодсистемы.ИспользуетсяПроизводство21 ИЛИ
		//-- Устарело_Производство21
		НастройкиПодсистемы.ПланируетсяГрафикПроизводства;
	
	Если НЕ ПланируетсяГрафик Тогда
		МассивНепроверяемыхРеквизитов.Добавить("МаксимальнаяДоступностьРЦ");
		МассивНепроверяемыхРеквизитов.Добавить("ЕдиницаИзмеренияДоступностиРЦ");
	Иначе
		ПроверитьМаксимальнуюДоступностьРЦ(Отказ);
	КонецЕсли;
	
	Если НЕ ИспользуютсяВариантыНаладки Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ЕдиницаВремениПереналадки");
	КонецЕсли; 
	
	Если НЕ ПараллельнаяЗагрузка Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ЕдиницаИзмеренияЗагрузки");
		МассивНепроверяемыхРеквизитов.Добавить("ВариантЗагрузки");
	КонецЕсли; 
	
	Если НЕ ПараллельнаяЗагрузка 
		ИЛИ ВариантЗагрузки <> Перечисления.ВариантыЗагрузкиРабочихЦентров.Синхронный 
		ИЛИ ИспользуютсяВариантыНаладки Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ВремяРаботы");
		МассивНепроверяемыхРеквизитов.Добавить("ЕдиницаИзмерения");
	КонецЕсли; 
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	// Защита от неправильно установленных настроек
	Если ВводитьДоступностьДляВидаРЦ
		И НЕ УчитыватьДоступностьПоГрафикуРаботы Тогда
			
		ВводитьДоступностьДляВидаРЦ = Ложь;
		КоличествоРабочихЦентров = 0;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьМаксимальнуюДоступностьРЦ(Отказ)

	ПараметрыПодразделения = ПроизводствоСервер.ПараметрыПроизводственногоПодразделения(Подразделение);
	
	ИнтервалПланирования = ПараметрыПодразделения.ИнтервалПланирования;
	
	Если ЗначениеЗаполнено(ИнтервалПланирования) 
		И ЗначениеЗаполнено(МаксимальнаяДоступностьРЦ) 
		И ЗначениеЗаполнено(ЕдиницаИзмеренияДоступностиРЦ) Тогда
		
		МаксимальнаяДоступностьРЦВСекундах = ПланированиеПроизводстваКлиентСервер.ПолучитьВремяВСекундах(МаксимальнаяДоступностьРЦ, ЕдиницаИзмеренияДоступностиРЦ);
		
		Если ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.Час Тогда
			ИнтервалВСекундах = 3600;
		ИначеЕсли ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.День Тогда
			ИнтервалВСекундах = 86400;
		ИначеЕсли ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.Неделя Тогда
			ИнтервалВСекундах = 604800;
		ИначеЕсли ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.Месяц Тогда
			ИнтервалВСекундах = 2592000;
		КонецЕсли;
		
		Если МаксимальнаяДоступностьРЦВСекундах > ИнтервалВСекундах Тогда
			ТекстСообщения = НСтр("ru = 'Максимальная доступность не должна превышать интервал планирования.';
									|en = 'Maximum capacity should not exceed planning interval.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "МаксимальнаяДоступностьРЦ",, Отказ);
		КонецЕсли;
		
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
