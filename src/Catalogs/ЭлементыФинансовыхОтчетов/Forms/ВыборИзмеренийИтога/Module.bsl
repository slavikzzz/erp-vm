
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ДеревоЭлементов = ПолучитьИзВременногоХранилища(Параметры.АдресЭлементовОтчета); // См. БюджетнаяОтчетностьРасчетКэшаСервер.ДеревоЭлементовБюджета
	Если ЗначениеЗаполнено(Параметры.АдресТаблицыЭлементов) Тогда
		ТаблицаЭлементов = ПолучитьИзВременногоХранилища(Параметры.АдресТаблицыЭлементов); // См. БюджетнаяОтчетностьРасчетКэшаСервер.НовыйТаблицаЭлементов
	Иначе
		ТаблицаЭлементов = Неопределено;
	КонецЕсли;
	
	Если ТаблицаЭлементов = Неопределено Тогда
		
		СтрокаДерева = ДеревоЭлементов.Строки.Найти(Параметры.АдресРедактируемогоЭлемента,
			"АдресСтруктурыЭлемента", Истина);
		КорневойЭлемент = ФинансоваяОтчетностьСервер.КорневойЭлемент(СтрокаДерева,
			ВидЭлемента("ТаблицаПоказателиВСтроках"));
		ИсточникПодчиненных = Неопределено;
		Если КорневойЭлемент <> Неопределено Тогда
			ИсточникПодчиненных = ФинансоваяОтчетностьСервер.ПодчиненныйЭлемент(КорневойЭлемент,
				"ВидЭлемента", ВидЭлемента("Колонки"));
			Вышележащие = "Строки";
			Нижележащие = "Колонки";
		КонецЕсли;
		КорневойЭлемент = ФинансоваяОтчетностьСервер.КорневойЭлемент(СтрокаДерева,
			ВидЭлемента("ТаблицаПоказателиВКолонках"));
		Если КорневойЭлемент <> Неопределено Тогда
			ИсточникПодчиненных = ФинансоваяОтчетностьСервер.ПодчиненныйЭлемент(КорневойЭлемент,
				"ВидЭлемента", ВидЭлемента("Строки"));
			Вышележащие = "Колонки";
			Нижележащие = "Строки";
		КонецЕсли;
		Если ИсточникПодчиненных = Неопределено Тогда
			ИсточникПодчиненных = ФинансоваяОтчетностьСервер.КорневойЭлемент(СтрокаДерева);
			Вышележащие = "Строки";
			Нижележащие = Неопределено;
		КонецЕсли;
		
		ДополнитьВышестоящимиИзмерениями(СтрокаДерева, Вышележащие);
		ПеречислитьНижележащиеИзмерения(СтрокаДерева, Вышележащие);
		Если Нижележащие <> Неопределено Тогда
			ПеречислитьНижележащиеИзмерения(ИсточникПодчиненных, Нижележащие);
		Иначе
			ВидИтога = 1;
			Элементы.ВидИтога.Доступность = Ложь;
		КонецЕсли;
		
	Иначе
		
		ЯчейкаМатрицы = ТаблицаЭлементов.Найти(Параметры.АдресРедактируемогоЭлемента, "Элемент");
		
		СтрокаДерева = ДеревоЭлементов.Строки.Найти(ЯчейкаМатрицы.Строка, "АдресСтруктурыЭлемента", Истина);
		ДополнитьВышестоящимиИзмерениями(СтрокаДерева, "Строки");
		ПеречислитьНижележащиеИзмерения(СтрокаДерева, "Строки");
		
		КолонкаДерева = ДеревоЭлементов.Строки.Найти(ЯчейкаМатрицы.Колонка, "АдресСтруктурыЭлемента", Истина);
		ДополнитьВышестоящимиИзмерениями(КолонкаДерева, "Колонки");
		ПеречислитьНижележащиеИзмерения(КолонкаДерева, "Колонки");
		
	КонецЕсли;
	
	УстановитьВыбранныеИзмерения();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИспользоватьРасширенныеВозможности(Команда)
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидИтогаПриИзменении(Элемент)
	
	УстановитьТекстНадписи();
	УстановитьВыбранныеИзмерения();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	Закрыть(СписокИзмерений());
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СписокИзмерений()
	
	Массив = ВыборИзмерений.Выгрузить(Новый Структура("Флаг", Истина)).ВыгрузитьКолонку("ИдентификаторИзмерения");
	Возврат СтрСоединить(Массив, ", ");
	
КонецФункции

&НаСервере
Процедура ДополнитьВышестоящимиИзмерениями(Знач СтрокаДерева, Источник)
	
	Пока СтрокаДерева <> Неопределено
		И СтрокаДерева.ВидЭлемента <> ВидЭлемента("Строки")
		И СтрокаДерева.ВидЭлемента <> ВидЭлемента("Колонки") Цикл
		
		Если СтрокаДерева.ВидЭлемента = ВидЭлемента("Измерение") Тогда
			
			Представление = ПредставлениеИдентификаторИзмерения(СтрокаДерева, Ложь);
			Идентификатор = ПредставлениеИдентификаторИзмерения(СтрокаДерева, Истина);
			
			Если ВыборИзмерений.НайтиСтроки(Новый Структура("ИдентификаторИзмерения", Идентификатор)).Количество() Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = ВыборИзмерений.Добавить();
			НоваяСтрока.ПредставлениеИзмерения = Представление;
			НоваяСтрока.ИдентификаторИзмерения = Идентификатор;
			НоваяСтрока.ИсточникИзмерения = Источник;
			
		КонецЕсли;
		
		СтрокаДерева = СтрокаДерева.Родитель;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ЗначениеРеквизита(СтрокаДерева, ИмяРеквизита)
	
	Если ЗначениеЗаполнено(СтрокаДерева.АдресСтруктурыЭлемента) Тогда
		Значение = ФинансоваяОтчетностьВызовСервера.ЗначениеДополнительногоРеквизита(
					СтрокаДерева.АдресСтруктурыЭлемента, ИмяРеквизита);
	Иначе
		Значение = ФинансоваяОтчетностьВызовСервера.ЗначениеДополнительногоРеквизита(
					СтрокаДерева.ЭлементОтчета, ИмяРеквизита);
	КонецЕсли;
	
	Возврат Значение;
	
КонецФункции

&НаСервере
Функция ПредставлениеИдентификаторИзмерения(СтрокаДерева, ЭтоИдентификатор = Ложь)
	
	ТипИзмерения = ЗначениеРеквизита(СтрокаДерева, "ТипИзмерения");
	
	Если ТипИзмерения = Перечисления.ТипыИзмеренийФинансовогоОтчета.Период Тогда
		
		Периодичность = ЗначениеРеквизита(СтрокаДерева, "Периодичность");
		Если ЭтоИдентификатор Тогда
			Значение = ОбщегоНазначения.ИмяЗначенияПеречисления(Периодичность);
		Иначе
			Значение = Строка(Периодичность);
		КонецЕсли;
		
	ИначеЕсли ТипИзмерения = Перечисления.ТипыИзмеренийФинансовогоОтчета.Организация Тогда
		
		Если ЭтоИдентификатор Тогда
			Значение = "Организация";
		Иначе
			Значение = БюджетнаяОтчетностьКлиентСервер.ПредставлениеИзмеренияБюджетирования("Организация");
		КонецЕсли;
		
	ИначеЕсли ТипИзмерения = Перечисления.ТипыИзмеренийФинансовогоОтчета.Подразделение Тогда
		
		Если ЭтоИдентификатор Тогда
			Значение = "Подразделение";
		Иначе
			Значение = БюджетнаяОтчетностьКлиентСервер.ПредставлениеИзмеренияБюджетирования("Подразделение");
		КонецЕсли;
		
	ИначеЕсли ТипИзмерения = Перечисления.ТипыИзмеренийФинансовогоОтчета.Сценарий Тогда
		
		Если ЭтоИдентификатор Тогда
			Значение = "Сценарий";
		Иначе
			Значение = БюджетнаяОтчетностьКлиентСервер.ПредставлениеИзмеренияБюджетирования("Сценарий");
		КонецЕсли;
		
	ИначеЕсли ТипИзмерения = Перечисления.ТипыИзмеренийФинансовогоОтчета.Валюта Тогда
		
		Если ЭтоИдентификатор Тогда
			Значение = "Валюта";
		Иначе
			Значение = БюджетнаяОтчетностьКлиентСервер.ПредставлениеИзмеренияБюджетирования("Валюта");
		КонецЕсли;
		
	ИначеЕсли ТипИзмерения = Перечисления.ТипыИзмеренийФинансовогоОтчета.ИзмерениеРегистра Тогда
		
		ИмяИзмерения = ЗначениеРеквизита(СтрокаДерева, "ИмяИзмерения");
		Если ЭтоИдентификатор Тогда
			Значение = ИмяИзмерения;
		Иначе
			Значение = БюджетнаяОтчетностьКлиентСервер.ПредставлениеИзмеренияБюджетирования(ИмяИзмерения);
		КонецЕсли;
		
	ИначеЕсли ТипИзмерения = Перечисления.ТипыИзмеренийФинансовогоОтчета.Аналитика
		ИЛИ ТипИзмерения = Перечисления.ТипыИзмеренийФинансовогоОтчета.ФиксированнаяАналитика Тогда
		
		ВидАналитики = ЗначениеРеквизита(СтрокаДерева, "ВидАналитики");
		Если ЭтоИдентификатор Тогда
			Значение = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидАналитики, "Идентификатор");
		Иначе
			Значение = Строка(ВидАналитики);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Значение;
	
КонецФункции

&НаСервере
Процедура ПеречислитьНижележащиеИзмерения(Знач СтрокаДерева, Источник)
	
	Для Каждого ПодчиненнаяСтрока Из СтрокаДерева.Строки Цикл
		
		Если ПодчиненнаяСтрока.ВидЭлемента = ВидЭлемента("Измерение") Тогда
			
			Представление = ПредставлениеИдентификаторИзмерения(ПодчиненнаяСтрока, Ложь);
			Идентификатор = ПредставлениеИдентификаторИзмерения(ПодчиненнаяСтрока, Истина);
			
			Если ВыборИзмерений.НайтиСтроки(Новый Структура("ИдентификаторИзмерения", Идентификатор)).Количество() Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = ВыборИзмерений.Добавить();
			НоваяСтрока.ПредставлениеИзмерения = Представление;
			НоваяСтрока.ИдентификаторИзмерения = Идентификатор;
			НоваяСтрока.ИсточникИзмерения = Источник;
			
		КонецЕсли;
		
		ПеречислитьНижележащиеИзмерения(ПодчиненнаяСтрока, Источник);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ВидЭлемента(ИмяЭлемента)
	
	#Если Клиент Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ВидыЭлементовФинансовогоОтчета." + ИмяЭлемента);
	#Иначе
		Возврат Перечисления.ВидыЭлементовФинансовогоОтчета[ИмяЭлемента];
	#КонецЕсли
	
КонецФункции

&НаСервере
Процедура УправлениеФормой()
	
	Если Элементы.ИспользоватьРасширенныеВозможности.Заголовок = НСтр("ru = 'Использовать расширенные возможности';
																		|en = 'Use advanced features'") Тогда
		Элементы.ИспользоватьРасширенныеВозможности.Заголовок = НСтр("ru = 'Использовать упрощенные возможности';
																	|en = 'Use simplified features'");
		Элементы.Страницы.ТекущаяСтраница = Элементы.ДетальнаяНастройка;
	Иначе
		Элементы.ИспользоватьРасширенныеВозможности.Заголовок = НСтр("ru = 'Использовать расширенные возможности';
																	|en = 'Use advanced features'");
		Элементы.Страницы.ТекущаяСтраница = Элементы.ПростаяНастройка;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьТекстНадписи()
	
	Если ВидИтога = 0 Тогда
		Элементы.НадписьИтог.Заголовок = НСтр("ru = 'Функция вернет итог по всем значениям по строке отчета';
												|en = 'The function will return total by all values by the report line'");
	Иначе
		Элементы.НадписьИтог.Заголовок = НСтр("ru = 'Функция вернет итог по всем значениям по колонке отчета';
												|en = 'The function will return total by all values by the report column'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВыбранныеИзмерения()
	
	Если ВидИтога Тогда
		Источник = "Строки";
	Иначе
		Источник = "Колонки";
	КонецЕсли;
	
	Для Каждого СтрокаПоиска Из ВыборИзмерений Цикл
		
		СтрокаПоиска.Флаг = СтрокаПоиска.ИсточникИзмерения = Источник;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

