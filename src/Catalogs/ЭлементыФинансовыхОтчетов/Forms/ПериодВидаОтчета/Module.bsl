
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Справочники.ЭлементыФинансовыхОтчетов.ФормаПриСозданииНаСервере(ЭтаФорма);
	Заголовок = Параметры.ВидЭлемента;
	
	Если КоличествоПредыдущихЛет = 0 Тогда
		КоличествоПредыдущихЛет = 1;
	КонецЕсли;
	
	Если КоличествоПредыдущихПериодов = 0 Тогда
		КоличествоПредыдущихПериодов = 1;
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ДопРежим = Перечисления.ДополнительныеРежимыЭлементовОтчетов.ВидОтчета;
	Справочники.ЭлементыФинансовыхОтчетов.ФормаПередЗаписьюНаСервере(ЭтаФорма, ТекущийОбъект, Отказ, ДопРежим);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(АдресЭлементаВХранилище) Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	Если Не ВыводитьПоказателиАналогичныхПериодовПрошлыхЛет Тогда
		НепроверяемыеРеквизиты.Добавить("КоличествоПредыдущихЛет");
	КонецЕсли;
	Если Не ВыводитьПоказателиПредыдущихПериодов Тогда
		НепроверяемыеРеквизиты.Добавить("КоличествоПредыдущихПериодов");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодичностьПриИзменении(Элемент)
	
	Если Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Год") Тогда
		ВыводитьПоказателиАналогичныхПериодовПрошлыхЛет = Ложь;
	КонецЕсли;
	
	Объект.НаименованиеДляПечати = Строка(Периодичность);
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыводитьПоказателиАналогичныхПериодовПрошлыхЛетПриИзменении(Элемент)
	УправлениеФормой(ЭтотОбъект)
КонецПроцедуры

&НаКлиенте
Процедура ВыводитьПоказателиПредыдущихПериодовПриИзменении(Элемент)
	УправлениеФормой(ЭтотОбъект)
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ФинансоваяОтчетностьКлиент.ЗавершитьРедактированиеЭлементаОтчета(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	ПериодичностьГод = Ложь;
	Если Форма.Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Год") Тогда
		ПериодичностьГод = Истина;
	КонецЕсли;
	Форма.Элементы.ГруппаПоказателиАналогичныхПериодовПрошлыхЛет.Видимость = НЕ ПериодичностьГод;
	Форма.Элементы.КоличествоПредыдущихЛет.Доступность = Форма.ВыводитьПоказателиАналогичныхПериодовПрошлыхЛет;
	Форма.Элементы.КоличествоПредыдущихПериодов.Доступность = Форма.ВыводитьПоказателиПредыдущихПериодов;
	
	Если Форма.Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Месяц") Тогда
		Форма.Элементы.ВыводитьПоказателиАналогичныхПериодовПрошлыхЛет.Заголовок =
			НСтр("ru = 'Выводить показатели аналогичных месяцев прошлых лет';
				|en = 'Show indicators of similar months of previous years'");
		Форма.Элементы.ВыводитьПоказателиПредыдущихПериодов.Заголовок =
			НСтр("ru = 'Выводить показатели предыдущих месяцев';
				|en = 'Show indicators of previous months'");
		Форма.Элементы.КоличествоПредыдущихПериодов.Заголовок =
			НСтр("ru = 'Количество предыдущих месяцев';
				|en = 'Number of previous months'");
	ИначеЕсли Форма.Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Квартал") Тогда
		Форма.Элементы.ВыводитьПоказателиАналогичныхПериодовПрошлыхЛет.Заголовок =
			НСтр("ru = 'Выводить показатели аналогичных кварталов прошлых лет';
				|en = 'Show indicators of similar quarters of previous years'");
		Форма.Элементы.ВыводитьПоказателиПредыдущихПериодов.Заголовок =
			НСтр("ru = 'Выводить показатели предыдущих кварталов';
				|en = 'Show indicators of previous quarters'");
		Форма.Элементы.КоличествоПредыдущихПериодов.Заголовок =
			НСтр("ru = 'Количество предыдущих кварталов';
				|en = 'Number of previous quarters'");
	ИначеЕсли Форма.Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Полугодие") Тогда
		Форма.Элементы.ВыводитьПоказателиАналогичныхПериодовПрошлыхЛет.Заголовок =
			НСтр("ru = 'Выводить показатели аналогичных полугодий прошлых лет';
				|en = 'Show indicators of similar half-year periods of previous years'");
		Форма.Элементы.ВыводитьПоказателиПредыдущихПериодов.Заголовок =
			НСтр("ru = 'Выводить показатели предыдущих полугодий';
				|en = 'Show indicators for previous half-year periods'");
		Форма.Элементы.КоличествоПредыдущихПериодов.Заголовок =
			НСтр("ru = 'Количество предыдущих полугодий';
				|en = 'Number of previous half-year periods'");
	ИначеЕсли Форма.Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Год") Тогда
		Форма.Элементы.ВыводитьПоказателиАналогичныхПериодовПрошлыхЛет.Заголовок =
			НСтр("ru = 'Выводить показатели аналогичных периодов прошлых лет';
				|en = 'Show indicators of similar periods of previous years'");
		Форма.Элементы.ВыводитьПоказателиПредыдущихПериодов.Заголовок =
			НСтр("ru = 'Выводить показатели предыдущих лет';
				|en = 'Show indicators of previous years'");
		Форма.Элементы.КоличествоПредыдущихПериодов.Заголовок =
			НСтр("ru = 'Количество предыдущих лет';
				|en = 'Number of previous years'");
	Иначе
		Форма.Элементы.ВыводитьПоказателиАналогичныхПериодовПрошлыхЛет.Заголовок =
			НСтр("ru = 'Выводить показатели аналогичных периодов прошлых лет';
				|en = 'Show indicators of similar periods of previous years'");
		Форма.Элементы.ВыводитьПоказателиПредыдущихПериодов.Заголовок =
			НСтр("ru = 'Выводить показатели предыдущих периодов';
				|en = 'Show indicators of previous periods'");
		Форма.Элементы.КоличествоПредыдущихПериодов.Заголовок =
			НСтр("ru = 'Количество предыдущих периодов';
				|en = 'Number of previous periods'");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
