
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(АдресЭлементаВХранилище) Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Справочники.ЭлементыФинансовыхОтчетов.ФормаПриСозданииНаСервере(ЭтаФорма);
	Заголовок = Параметры.ВидЭлемента;

	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
	Элементы.ТолькоДляПечати.Видимость = Параметры.Свойство("ИспользоватьДляВводаПлана") 
		И Параметры.ИспользоватьДляВводаПлана;
	
	ПараметрыПодстановки = ПолучитьПараметрыПодстановкиОтчета();
	ДобавитьКомандыПараметровПодстановки(ПараметрыПодстановки);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Справочники.ЭлементыФинансовыхОтчетов.ФормаПередЗаписьюНаСервере(ЭтаФорма, ТекущийОбъект, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстовыйБлокПриИзменении(Элемент)
	
	Объект.НаименованиеДляПечати = СокрЛП(СтрПолучитьСтроку(Текст, 1));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)
	
	ДополнительныеСвойства = Новый Структура("ПредставлениеТолькоДляПечати");
	ДополнительныеСвойства.ПредставлениеТолькоДляПечати = ?(ТолькоДляПечати, 
							НСтр("ru = 'Не выводится при заполнении бюджета';
								|en = 'Not displayed while populating the budget'"), "");
	
	ФинансоваяОтчетностьКлиент.ЗавершитьРедактированиеЭлементаОтчета(ЭтаФорма, ДополнительныеСвойства);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ДобавитьКомандыПараметровПодстановки(ПараметрыПодстановки)
	
	ИмяДействияКоманды = "Подключаемый_ДобавитьШаблонПодстановки";
	
	Для Каждого ИмяПараметраИПредставление Из ПараметрыПодстановки Цикл
		
		КомандаФормы = ЭтаФорма.Команды.Добавить(ИмяПараметраИПредставление.Имя);
		КомандаФормы.Действие = ИмяДействияКоманды;
		
		НоваяКнопкаФормы = Элементы.Добавить(ИмяПараметраИПредставление.Имя, Тип("КнопкаФормы"), Элементы.ДобавитьПараметр);
		НоваяКнопкаФормы.ИмяКоманды = ИмяПараметраИПредставление.Имя;
		НоваяКнопкаФормы.Заголовок = ИмяПараметраИПредставление.Представление;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьПараметрыПодстановкиОтчета()
	
	МассивПараметров = ПараметрыПодстановкиПоВидуБюджета();
	
	Возврат МассивПараметров;
	
КонецФункции

&НаСервере
Функция ПараметрыПодстановкиПоВидуБюджета()
	
	МассивПараметров = Новый Массив;
	
	УпорядоченныеПеременные = БюджетнаяОтчетностьКлиентСервер.УпорядоченныеПеременныеБюджетныхОтчетов();
	
	БюджетыПоОрганизациям = Ложь;
	БюджетыПоПодразделениям = Ложь;
	МодельБюджетирования = Параметры.МодельБюджетирования;
	
	Если ЗначениеЗаполнено(МодельБюджетирования) Тогда
		РеквизитыМоделиБюджетирования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.МодельБюджетирования,
			Новый Структура("БюджетыПоОрганизациям,БюджетыПоПодразделениям"));
			
		Если РеквизитыМоделиБюджетирования.БюджетыПоОрганизациям Тогда
			БюджетыПоОрганизациям = Истина;
		КонецЕсли;
		
		Если РеквизитыМоделиБюджетирования.БюджетыПоПодразделениям Тогда
			БюджетыПоПодразделениям = Истина;
		КонецЕсли;
	КонецЕсли;
	
	ИспользоватьДляВводаПлана = Параметры.Свойство("ИспользоватьДляВводаПлана") И Параметры.ИспользоватьДляВводаПлана;
	
	Для Каждого ИмяИПредставление Из УпорядоченныеПеременные Цикл
		Если ИмяИПредставление.Имя = "Организация"
		   И НЕ БюджетыПоОрганизациям Тогда
			Продолжить;
		КонецЕсли;
		Если ИмяИПредставление.Имя = "Подразделение"
		   И НЕ БюджетыПоПодразделениям Тогда
			Продолжить;
		КонецЕсли;
		Если НЕ ИспользоватьДляВводаПлана
			И (ИмяИПредставление.Имя = "СтатусДокумента"
			//++ НЕ УТКА
			ИЛИ ИмяИПредставление.Имя = "Утверждающий"
			//-- НЕ УТКА
			ИЛИ ИмяИПредставление.Имя = "Ответственный") Тогда
			
			Продолжить;
		КонецЕсли;
		
		МассивПараметров.Добавить(ИмяИПредставление);
	КонецЦикла;
	
	Если Параметры.Свойство("АдресСпискаАналитик") Тогда
		ТаблицаАналитикШапки = ПолучитьИзВременногоХранилища(Параметры.АдресСпискаАналитик);
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаАналитикШапки.НомерСтроки КАК НомерСтроки,
		|	ТаблицаАналитикШапки.ВидАналитики КАК ВидАналитики
		|ПОМЕСТИТЬ АналитикиШапки
		|ИЗ
		|	&ТаблицаАналитикШапки КАК ТаблицаАналитикШапки
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	АналитикиШапки.НомерСтроки КАК НомерСтроки,
		|	АналитикиШапки.ВидАналитики КАК ВидАналитики,
		|	АналитикиСтатейБюджетов.Идентификатор КАК Идентификатор,
		|	АналитикиСтатейБюджетов.Представление КАК Представление
		|ИЗ
		|	АналитикиШапки КАК АналитикиШапки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.АналитикиСтатейБюджетов КАК АналитикиСтатейБюджетов
		|		ПО АналитикиШапки.ВидАналитики = АналитикиСтатейБюджетов.Ссылка
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
		Запрос.УстановитьПараметр("ТаблицаАналитикШапки", ТаблицаАналитикШапки);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ИмяИПредставление = Новый Структура;
			ИмяИПредставление.Вставить("Имя", Выборка.Идентификатор);
			ИмяИПредставление.Вставить("Представление", Выборка.Представление);
			
			МассивПараметров.Добавить(ИмяИПредставление);
		КонецЦикла;
	КонецЕсли;
	
	Возврат МассивПараметров;
	
КонецФункции


// Подключаемая команда. Добавляет шаблон подстановки.
// 
// Параметры:
// 	Команда - КомандаФормы -
&НаКлиенте
Процедура Подключаемый_ДобавитьШаблонПодстановки(Команда)
	 
	ВставитьТекстВЗаголовок("["+ Команда.Имя +"]");
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьТекстВЗаголовок(ТекстДляВставки, Сдвиг = 0)
	
	СтрокаНач = 0;
	СтрокаКон = 0;
	КолонкаНач = 0;
	КолонкаКон = 0;
	
	Элементы.ТекстовыйБлок.ПолучитьГраницыВыделения(СтрокаНач, КолонкаНач, СтрокаКон, КолонкаКон);
	
	Если (КолонкаКон = КолонкаНач) И (КолонкаКон + СтрДлина(ТекстДляВставки)) > Элементы.ТекстовыйБлок.Ширина / 8 Тогда
		Элементы.ТекстовыйБлок.ВыделенныйТекст = "";
	КонецЕсли;
	
	Элементы.ТекстовыйБлок.ВыделенныйТекст = ТекстДляВставки;
	
	Если Не Сдвиг = 0 Тогда
		Элементы.ТекстовыйБлок.ПолучитьГраницыВыделения(СтрокаНач, КолонкаНач, СтрокаКон, КолонкаКон);
		Элементы.ТекстовыйБлок.УстановитьГраницыВыделения(СтрокаНач, КолонкаНач - Сдвиг, СтрокаКон, КолонкаКон - Сдвиг);
	КонецЕсли;
	ТекущийЭлемент = Элементы.ТекстовыйБлок;
	
КонецПроцедуры

#КонецОбласти

