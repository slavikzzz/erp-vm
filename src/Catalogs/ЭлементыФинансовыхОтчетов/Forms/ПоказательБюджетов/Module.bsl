
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Справочники.ЭлементыФинансовыхОтчетов.ФормаПриСозданииНаСервере(ЭтотОбъект);
	ВариантРасположенияГраницыФактическихДанных = Параметры.ВариантРасположенияГраницыФактическихДанных;
	
	Если Параметры.Свойство("ПроизвольныйПоказатель") Тогда
		Элементы.ОтрицательноеЗначение.Видимость = Ложь;
	КонецЕсли;
	
	Заголовок = "";
	Если Не ЗначениеЗаполнено(ПоказательБюджетов) Тогда
		Если Параметры.Свойство("ПроизвольныйПоказатель") Тогда
			Заголовок = НСтр("ru = 'Операнд: <Показатели бюджетов, по которым есть остатки или обороты>';
							|en = 'Operand: <Items of budgets with balance or turnovers>'");
		Иначе
			Заголовок = НСтр("ru = '<Показатели бюджетов, по которым есть остатки или обороты>';
							|en = '<Items of budgets with balance or turnovers>'");
		КонецЕсли;
	Иначе
		Если Параметры.Свойство("ПроизвольныйПоказатель") Тогда
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Операнд: %1 ""%2""';
					|en = 'Operand: %1 ""%2""'"),
				Параметры.ВидЭлемента,
				ПоказательБюджетов);
		Иначе
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1 ""%2""';
					|en = '%1 ""%2""'"),
				Параметры.ВидЭлемента,
				ПоказательБюджетов);
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПоказательБюджетов) ИЛИ ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПоказательБюджетов, "УчитыватьПоКоличеству") Тогда
		Элементы.ВыводимыеПоказатели.СписокВыбора.Добавить(Перечисления.ТипыВыводимыхПоказателейБюджетногоОтчета.Количество);
		Если Не Параметры.Свойство("НастройкаЯчеек")
			И Не Параметры.Свойство("ПроизвольныйПоказатель") Тогда
			Элементы.ВыводимыеПоказатели.СписокВыбора.Добавить(Перечисления.ТипыВыводимыхПоказателейБюджетногоОтчета.КоличествоИСумма);
		КонецЕсли;
	КонецЕсли;
	
	ПривилегированныйРежимПереключатель = Число(ПривилегированныйРежим);

	Если НЕ (Параметры.Свойство("НастройкаЯчеек")
		ИЛИ Параметры.Свойство("ПроизвольныйПоказатель")) Тогда
		
		ДеревоЭлементов = ДанныеФормыВЗначение(Параметры.ЭлементыОтчета, Тип("ДеревоЗначений"));
		АдресЭлементовОтчета = ПоместитьВоВременноеХранилище(ДеревоЭлементов, УникальныйИдентификатор);
		АдресРедактируемогоЭлемента = АдресЭлементаВХранилище;
		АдресТаблицыЭлементов = Неопределено;
		
	Иначе
		
		АдресЭлементовОтчета = Параметры.АдресЭлементовОтчета;
		Если Параметры.Свойство("ПроизвольныйПоказатель") Тогда
			АдресРедактируемогоЭлемента = Параметры.АдресРедактируемогоЭлемента;
		Иначе
			АдресРедактируемогоЭлемента = АдресЭлементаВХранилище;
		КонецЕсли;
		АдресТаблицыЭлементов = Параметры.АдресТаблицыЭлементов;
	
	КонецЕсли;
	
	ПараметрыОпределенияФильтров = Новый Структура;
	ПараметрыОпределенияФильтров.Вставить("АдресРедактируемогоЭлемента", АдресРедактируемогоЭлемента);
	ПараметрыОпределенияФильтров.Вставить("АдресЭлементовОтчета", АдресЭлементовОтчета);
	Если ЗначениеЗаполнено(АдресТаблицыЭлементов) Тогда
		ПараметрыОпределенияФильтров.Вставить("АдресТаблицыЭлементов", АдресТаблицыЭлементов);
	КонецЕсли;
	
	ПараметрыДоступностиФильтров = БюджетнаяОтчетностьРасчетКэшаСервер.ПараметрыДоступностиФильтров(Неопределено, ПараметрыОпределенияФильтров);
	
	Для Каждого КлючИЗначение Из ПараметрыДоступностиФильтров Цикл
		Если КлючИЗначение.Ключ = "Сценарий" Тогда
			Элементы.ИспользоватьФильтрПоСценарий.Видимость = КлючИЗначение.Значение;
		ИначеЕсли КлючИЗначение.Ключ = "Организация" Тогда
			Элементы.ИспользоватьФильтрПоОрганизация.Видимость = КлючИЗначение.Значение;
		ИначеЕсли КлючИЗначение.Ключ = "Подразделение" Тогда
			Элементы.ИспользоватьФильтрПоПодразделение.Видимость = КлючИЗначение.Значение;
		КонецЕсли;
	КонецЦикла;
	
	Элементы.НастройкаСмещенияПериода.Заголовок = БюджетнаяОтчетностьКлиентСервер.ПредставлениеСмещения(ЭтотОбъект);
	
	Если Параметры.Свойство("РодительВидЭлемента")
	   И Параметры.РодительВидЭлемента = Перечисления.ВидыЭлементовФинансовогоОтчета.ПроизводныйПоказатель
	   И Параметры.Свойство("ИспользоватьДляВводаПлана")
	   И Параметры.ИспользоватьДляВводаПлана Тогда
		Элементы.ИсключитьДанныеВводимогоДокументаПриРасчете.Видимость = Истина;
	Иначе
		Элементы.ИсключитьДанныеВводимогоДокументаПриРасчете.Видимость = Ложь;
	КонецЕсли;
	
	СобытияФорм.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПроверкаОтбораПоПараметрамДоступности();
	Справочники.ЭлементыФинансовыхОтчетов.ФормаПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ПоказателиБюджетов" Тогда
		Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
			Прочитать();
		Иначе
			Объект.НаименованиеДляПечати = Параметр;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(АдресЭлементаВХранилище) Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаСмещенияПериодаНажатие(Элемент)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("УстановитьПериодСмещения", ЭтотОбъект);
	ПараметрыСмещения = БюджетнаяОтчетностьКлиентСервер.ПараметрыОткрытияФормыНастройкиПериода(ЭтотОбъект);
	
	ОткрытьФорму("Справочник.ЭлементыФинансовыхОтчетов.Форма.НастройкаПериодаИсточникаДанных",
															ПараметрыСмещения,,,,,ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура НеИспользоватьФильтрПриИзменении(Элемент)
	
	УстановитьОтборНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)
	
	ФинансоваяОтчетностьКлиент.ЗавершитьРедактированиеЭлементаОтчета(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтотОбъект, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПроверкаОтбораПоПараметрамДоступности()
	
	Если НЕ Модифицированность Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОпределенияФильтров = Новый Структура;
	ПараметрыОпределенияФильтров.Вставить("АдресРедактируемогоЭлемента", АдресРедактируемогоЭлемента);
	ПараметрыОпределенияФильтров.Вставить("АдресЭлементовОтчета", АдресЭлементовОтчета);
	Если ЗначениеЗаполнено(АдресТаблицыЭлементов) Тогда
		ПараметрыОпределенияФильтров.Вставить("АдресТаблицыЭлементов", АдресТаблицыЭлементов);
	КонецЕсли;
	
	ПараметрыДоступностиФильтров = БюджетнаяОтчетностьРасчетКэшаСервер.ПараметрыДоступностиФильтров(Неопределено, ПараметрыОпределенияФильтров);
	
	Для Каждого КлючИЗначение Из ПараметрыДоступностиФильтров Цикл
		СписокЭлементовОтбора = Новый СписокЗначений;
		ВыполнятьПоискЭлементовОтбора = Ложь;
		Если КлючИЗначение.Ключ = "Сценарий"
			И НЕ КлючИЗначение.Значение
			И ИспользоватьФильтрПоСценарию Тогда
			ВыполнятьПоискЭлементовОтбора = Истина;
		КонецЕсли;
		Если КлючИЗначение.Ключ = "Организация"
			И НЕ КлючИЗначение.Значение
			И ИспользоватьФильтрПоОрганизации Тогда
			ВыполнятьПоискЭлементовОтбора = Истина;
		КонецЕсли;
		Если КлючИЗначение.Ключ = "Подразделение"
			И НЕ КлючИЗначение.Значение
			И ИспользоватьФильтрПоПодразделению Тогда
			ВыполнятьПоискЭлементовОтбора = Истина;
		КонецЕсли;
		Если ВыполнятьПоискЭлементовОтбора Тогда
			СписокЭлементовОтбора = Новый СписокЗначений;
			БюджетнаяОтчетностьРасчетКэшаСервер.НайтиОтборПоИмени(
				Компоновщик.Настройки,
				КлючИЗначение.Ключ,
				СписокЭлементовОтбора,,
				Ложь);
			
			// Пересечение отборов в колонке и в доп.отборе. Флаг использования фильтра был включен.
			// Если Список элементов отбора пуст, значит пользователь очистил отбор,
			// необходимо выключить флаг использования фильтра.
			Если СписокЭлементовОтбора.Количество() = 0 Тогда
				Если КлючИЗначение.Ключ = "Сценарий" Тогда
					ИспользоватьФильтрПоСценарию = 0;
				ИначеЕсли КлючИЗначение.Ключ = "Организация" Тогда
					ИспользоватьФильтрПоОрганизации = 0;
				ИначеЕсли КлючИЗначение.Ключ = "Подразделение" Тогда
					ИспользоватьФильтрПоПодразделению = 0;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПериодСмещения(Результат, ДополнительныеПараметры) Экспорт
	
	БюджетнаяОтчетностьКлиент.УстановитьПериодСмещения(Объект, ЭтотОбъект, Результат);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборНаСервере()
	
	Справочники.ЭлементыФинансовыхОтчетов.УстановитьНастройкиОтбора(ЭтотОбъект, Компоновщик, Объект.ВидЭлемента, Компоновщик.Настройки);
	
КонецПроцедуры

&НаКлиенте
Процедура ПривилегированныйРежимПереключательПриИзменении(Элемент)
	
	ПривилегированныйРежим = Булево(ПривилегированныйРежимПереключатель);

КонецПроцедуры

#КонецОбласти

