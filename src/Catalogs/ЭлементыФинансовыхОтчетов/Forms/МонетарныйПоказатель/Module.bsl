
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Справочники.ЭлементыФинансовыхОтчетов.ФормаПриСозданииНаСервере(ЭтотОбъект);
	
	ЭтоОстаток = СтрНайти(ТипИтога,"Сальдо")> 0;
	Элементы.ГраницаПериода.Видимость = ЭтоОстаток;
	ТекущийВидИтога = ЭтоОстаток;
	Если СчетПланаСчетов <> Неопределено Тогда
		РеквизитыСчета = Новый Структура;
		РеквизитыСчета.Вставить("НаименованиеСчета", "Наименование");
		//++ НЕ УТКА
		Если ТипЗнч(СчетПланаСчетов) = МеждународныйУчетКлиентСервер.ТипПланСчетов() Тогда
			РеквизитыСчета.Вставить("ПланСчетовМеждународногоУчета", "ПланСчетов");
		КонецЕсли;
		//-- НЕ УТКА
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СчетПланаСчетов, РеквизитыСчета);
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ЗначенияРеквизитов);
		Элементы.СчетПланаСчетов.ОграничениеТипа = ФинансоваяОтчетностьСервер.ОписаниеТипаПоЗначению(СчетПланаСчетов);
	КонецЕсли;
	Элементы.ГруппаДопРеквизиты.Видимость = Параметры.ПоказатьКодСтрокиПримечание;
	Элементы.ОбратныйЗнак.Видимость = Параметры.ПоказатьКодСтрокиПримечание;
	Элементы.ВыделитьЭлемент.Видимость = Параметры.ПоказатьКодСтрокиПримечание;
	ОбновитьЗаголовокФормы();

	СобытияФорм.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Справочники.ЭлементыФинансовыхОтчетов.ФормаПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект, Отказ);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ОбновитьЗаголовокФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)

	Если ЗначениеЗаполнено(АдресЭлементаВХранилище) Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НаименованиеДляПечатиПриИзменении(Элемент)
	
	Объект.Наименование = Объект.НаименованиеДляПечати;
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
	
КонецПроцедуры

&НаКлиенте
Процедура ТипИтогаПриИзменении(Элемент)
	
	ЭтоОстаток = СтрНайти(Элемент.ТекстРедактирования,"Сальдо")> 0;
	Элементы.ГраницаПериода.Видимость = ЭтоОстаток;
	Если ТекущийВидИтога <> ЭтоОстаток Тогда
		НастроитьПолеОтбора();
	КонецЕсли;
	ТекущийВидИтога = ЭтоОстаток;
	
КонецПроцедуры

&НаКлиенте
Процедура СчетПланаСчетовПриИзменении(Элемент)
	
	СчетПланаСчетовПриИзмененииСервер();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)
	
	ФинансоваяОтчетностьКлиент.ЗавершитьРедактированиеЭлементаОтчета(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтотОбъект, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПриИзмененииРеквизитов

&НаСервере
Процедура СчетПланаСчетовПриИзмененииСервер()
	
	// По умолчанию заполняем НаименованиеДляПечати по НаименованиюСчета, 
	// пока пользователь не введет свое наименование для печати.
	НовоеНаименованиеСчета = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СчетПланаСчетов, "Наименование");
	Если НЕ ЗначениеЗаполнено(Объект.НаименованиеДляПечати)
		ИЛИ Объект.НаименованиеДляПечати = НаименованиеСчета Тогда
		Объект.НаименованиеДляПечати = НовоеНаименованиеСчета;
	КонецЕсли;
	Объект.Наименование = Объект.НаименованиеДляПечати;
	НаименованиеСчета = НовоеНаименованиеСчета;
	НастроитьПолеОтбора();
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаСервере
Процедура ОбновитьЗаголовокФормы()
	
	ПредставлениеТипа = НСтр("ru = 'Монетарный показатель';
							|en = 'Monetary indicator'");
	//++ НЕ УТКА
	Если ТипЗнч(СчетПланаСчетов) = МеждународныйУчетКлиентСервер.ТипПланСчетов() Тогда
		ПредставлениеТипа = НСтр("ru = 'Монетарный показатель международный';
								|en = 'International monetary indicator'");
	ИначеЕсли ТипЗнч(СчетПланаСчетов) = РеглУчетКлиентСервер.ТипПланСчетов() Тогда
		ПредставлениеТипа = НСтр("ru = 'Монетарный показатель регламентированный';
								|en = 'Local monetary indicator'");
	КонецЕсли;
	//-- НЕ УТКА
	
	Если НЕ ЗначениеЗаполнено(Объект.НаименованиеДляПечати) Тогда
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 (создание)';
				|en = '%1 (Create)'"),
			ПредставлениеТипа);
	Иначе
		Заголовок = Объект.НаименованиеДляПечати + " (" + ПредставлениеТипа + ")";
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьПолеОтбора()
	
	Справочники.ЭлементыФинансовыхОтчетов.УстановитьНастройкиОтбора(ЭтотОбъект,
	                                                                Компоновщик,
	                                                                Объект.ВидЭлемента,
	                                                                Компоновщик.Настройки);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
