#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	УстановкаРеквизитовИсторииПереходаНаНовыеФормулы();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	НепроверяемыеРеквизиты.Добавить("НастройкиЗаполненияСубконто.Выражение");
	
	Если Не ЭтоГруппа Тогда
		Если РучноеУточнениеПроводки Или НеОтражаетсяВМеждународномУчете Тогда
			НепроверяемыеРеквизиты.Добавить("СчетМеждународногоУчетаДт");
			НепроверяемыеРеквизиты.Добавить("СчетМеждународногоУчетаКт");
		КонецЕсли;
		
		СчетаУчета = Новый Массив();
		
		Если НЕ СчетРеглУчетаДт.Пустая() Тогда
			СчетаУчета.Добавить(СчетРеглУчетаДт);
		КонецЕсли;
		
		Если НЕ СчетРеглУчетаКт.Пустая() Тогда
			СчетаУчета.Добавить(СчетРеглУчетаКт);
		КонецЕсли;
		
		Если НЕ СчетМеждународногоУчетаДт.Пустая() Тогда
			СчетаУчета.Добавить(СчетМеждународногоУчетаДт);
		КонецЕсли;
		
		Если НЕ СчетМеждународногоУчетаКт.Пустая() Тогда
			СчетаУчета.Добавить(СчетМеждународногоУчетаКт);
		КонецЕсли;
		
		Если СчетаУчета.Количество() > 0 Тогда
			ЗначенияРеквизитаЗабалансовый = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(СчетаУчета, "Забалансовый");
			
			ОборотРеглЗабалансовый = Ложь;
			ОборотМеждународныйЗабалансовый = Ложь;
			
			Если НЕ СчетРеглУчетаДт.Пустая() И ЗначенияРеквизитаЗабалансовый[СчетРеглУчетаДт] Тогда
				ОборотРеглЗабалансовый = Истина;
				НепроверяемыеРеквизиты.Добавить("СчетРеглУчетаКт");
			КонецЕсли;
			
			Если НЕ СчетРеглУчетаКт.Пустая() И ЗначенияРеквизитаЗабалансовый[СчетРеглУчетаКт] Тогда
				ОборотРеглЗабалансовый = Истина;
				НепроверяемыеРеквизиты.Добавить("СчетРеглУчетаДт");
			КонецЕсли;
			
			Если НЕ СчетМеждународногоУчетаДт.Пустая() И ЗначенияРеквизитаЗабалансовый[СчетМеждународногоУчетаДт] Тогда
				ОборотМеждународныйЗабалансовый = Истина;
				НепроверяемыеРеквизиты.Добавить("СчетМеждународногоУчетаКт");
			КонецЕсли;
			
			Если НЕ СчетМеждународногоУчетаКт.Пустая() И ЗначенияРеквизитаЗабалансовый[СчетМеждународногоУчетаКт] Тогда
				ОборотМеждународныйЗабалансовый = Истина;
				НепроверяемыеРеквизиты.Добавить("СчетМеждународногоУчетаДт");
			КонецЕсли;
			
			Если НЕ СчетРеглУчетаДт.Пустая() И НЕ СчетРеглУчетаКт.Пустая()
				И ЗначенияРеквизитаЗабалансовый[СчетРеглУчетаДт] <> ЗначенияРеквизитаЗабалансовый[СчетРеглУчетаКт] Тогда
				ТекстСообщения = НСтр("ru = 'Оборот регламентированного учета может быть только между балансовыми или только между забалансовыми счетами.';
										|en = 'Local accounting turnover can exist only between balance accounts or only between off-balance accounts.'");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "СчетРеглУчетаДт", "Объект", Отказ);
			КонецЕсли;
			
			Если НЕ СчетМеждународногоУчетаДт.Пустая() И НЕ СчетМеждународногоУчетаКт.Пустая()
				И ЗначенияРеквизитаЗабалансовый[СчетМеждународногоУчетаДт] <> ЗначенияРеквизитаЗабалансовый[СчетМеждународногоУчетаКт] Тогда
				ТекстСообщения = НСтр("ru = 'Оборот международного учета может быть только между балансовыми или только между забалансовыми счетами.';
										|en = 'Financial accounting turnover can exist only between balance accounts or only between off-balance accounts.'");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "СчетМеждународногоУчетаДт", "Объект", Отказ);
			КонецЕсли;
			
			Если НЕ(СчетРеглУчетаДт.Пустая() И СчетРеглУчетаКт.Пустая())
				И НЕ(СчетМеждународногоУчетаДт.Пустая() И СчетМеждународногоУчетаКт.Пустая())
				И ОборотРеглЗабалансовый <> ОборотМеждународныйЗабалансовый Тогда
				ТекстСообщения = СтрШаблон(НСтр("ru = 'Для выбранного оборота регламентированного учета укажите %1 счета международного учета.';
												|en = 'For the selected local accounting turnover, specify %1 financial accounting accounts.'"),
					?(ОборотРеглЗабалансовый, НСтр("ru = 'забалансовые';
													|en = 'off-balance'"), НСтр("ru = 'балансовые';
																				|en = 'balance'")));
				ИмяРеквизита = ?(СчетМеждународногоУчетаДт.Пустая(), "СчетМеждународногоУчетаКт", "СчетМеждународногоУчетаДт");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, ИмяРеквизита, "Объект", Отказ);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановкаРеквизитовИсторииПереходаНаНовыеФормулы()
	
	Если НЕ ЭтоГруппа Тогда
		
		Для Каждого СтрокаТЧ Из НастройкиЗаполненияСубконто Цикл
			Если НЕ ЗначениеЗаполнено(СтрокаТЧ.ВыражениеИсторияПереходаНаНовыеФормулы)
				И ЗначениеЗаполнено(СтрокаТЧ.Выражение) Тогда
				СтрокаТЧ.ВыражениеИсторияПереходаНаНовыеФормулы = СтрокаТЧ.Выражение;
			КонецЕсли;
		КонецЦикла;
		УдалитьПереходНаНовыеФормулыВыполнен = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли