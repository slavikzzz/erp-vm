#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если Не ЭтоГруппа Тогда
		Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
		ПараметрыВыбораСтатейИАналитик = Справочники.ОбъектыУчетаРезервовПредстоящихРасходов.ПараметрыВыбораСтатейИАналитик(ВидРезервов);
		ДоходыИРасходыСервер.ОбработкаЗаполнения(ЭтотОбъект, ПараметрыВыбораСтатейИАналитик);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ДисконтированиеОценочногоОбязательства = Ложь;
	Если ЗначениеЗаполнено(ВидРезервов) Тогда
		ДисконтированиеОценочногоОбязательства = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидРезервов, "ДисконтированиеРезервов");
	КонецЕсли;
	
	Если ДисконтированиеОценочногоОбязательства Тогда
		ПроверяемыеРеквизиты.Добавить("КонецПериода");
	КонецЕсли;
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	НепроверяемыеРеквизиты.Добавить("ИспользованиеРезерва.АналитикаРасходов");
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
	Если Не ЭтоГруппа Тогда
		ПараметрыВыбораСтатейИАналитик = Справочники.ОбъектыУчетаРезервовПредстоящихРасходов.ПараметрыВыбораСтатейИАналитик(ВидРезервов);
		ДоходыИРасходыСервер.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, ПараметрыВыбораСтатейИАналитик);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если Не ЭтоГруппа Тогда
		ПараметрыВыбораСтатейИАналитик = Справочники.ОбъектыУчетаРезервовПредстоящихРасходов.ПараметрыВыбораСтатейИАналитик(ВидРезервов);
		ДоходыИРасходыСервер.ПередЗаписью(ЭтотОбъект, ПараметрыВыбораСтатейИАналитик);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли