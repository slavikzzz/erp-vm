#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// ТехнологияСервиса.ВыгрузкаЗагрузкаДанных

// Возвращает реквизиты справочника, которые образуют естественный ключ для элементов справочника.
//
// Возвращаемое значение:
//  Массив из Строка - имена реквизитов, образующих естественный ключ.
//
Функция ПоляЕстественногоКлюча() Экспорт
	
	Результат = Новый Массив;
	
	Результат.Добавить("Код");
	
	Возврат Результат;
	
КонецФункции

// Конец ТехнологияСервиса.ВыгрузкаЗагрузкаДанных

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура выполняет первоначальное заполнение классификатора.
Процедура НачальноеЗаполнение() Экспорт
	
	ОбновитьЗначенияКлассификатора();
	
КонецПроцедуры

Процедура ОбновитьЗначенияКлассификатора()
	
	КлассификаторТаблица = Новый ТаблицаЗначений;
	КлассификаторТаблица.Колонки.Добавить("Код", Новый ОписаниеТипов("Строка", ,Новый КвалификаторыСтроки(1)));
	КлассификаторТаблица.Колонки.Добавить("Наименование", Новый ОписаниеТипов("Строка", ,Новый КвалификаторыСтроки(100)));
	
	НоваяСтрокаКлассификатора = КлассификаторТаблица.Добавить();
	НоваяСтрокаКлассификатора.Код = "1";
	НоваяСтрокаКлассификатора.Наименование = НСтр("ru = 'Никогда не состоял(а) в браке';
													|en = 'Never married'");
	
	НоваяСтрокаКлассификатора = КлассификаторТаблица.Добавить();
	НоваяСтрокаКлассификатора.Код = "2";
	НоваяСтрокаКлассификатора.Наименование = НСтр("ru = 'Состоит в зарегистрированном браке';
													|en = 'Married'");
	
	НоваяСтрокаКлассификатора = КлассификаторТаблица.Добавить();
	НоваяСтрокаКлассификатора.Код = "3";
	НоваяСтрокаКлассификатора.Наименование = НСтр("ru = 'Состоит в незарегистрированном браке';
													|en = 'Not married'");
	
	НоваяСтрокаКлассификатора = КлассификаторТаблица.Добавить();
	НоваяСтрокаКлассификатора.Код = "4";
	НоваяСтрокаКлассификатора.Наименование = НСтр("ru = 'Вдовец (вдова)';
													|en = 'Widower (widow)'");
	
	НоваяСтрокаКлассификатора = КлассификаторТаблица.Добавить();
	НоваяСтрокаКлассификатора.Код = "5";
	НоваяСтрокаКлассификатора.Наименование = НСтр("ru = 'Разведен(а) официально (развод зарегистрирован)';
													|en = 'Officially divorced (divorce is registered)'");
	
	НоваяСтрокаКлассификатора = КлассификаторТаблица.Добавить();
	НоваяСтрокаКлассификатора.Код = "6";
	НоваяСтрокаКлассификатора.Наименование = НСтр("ru = 'Разошелся(лась)';
													|en = 'Separated'");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КлассификаторТаблица", КлассификаторТаблица);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	КлассификаторТаблица.Код КАК Код,
		|	КлассификаторТаблица.Наименование КАК Наименование
		|ПОМЕСТИТЬ ВТКлассификаторТаблица
		|ИЗ
		|	&КлассификаторТаблица КАК КлассификаторТаблица
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КлассификаторТаблица.Код КАК Код,
		|	КлассификаторТаблица.Наименование КАК Наименование,
		|	СостояниеВБраке.Ссылка КАК Ссылка
		|ИЗ
		|	ВТКлассификаторТаблица КАК КлассификаторТаблица
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СостояниеВБраке КАК СостояниеВБраке
		|		ПО КлассификаторТаблица.Код = СостояниеВБраке.Код
		|ГДЕ
		|	(СостояниеВБраке.Ссылка ЕСТЬ NULL
		|			ИЛИ КлассификаторТаблица.Наименование <> СостояниеВБраке.Наименование)";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Если ЗначениеЗаполнено(Выборка.Ссылка) Тогда
				СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();
			Иначе
				СправочникОбъект = Справочники.СостояниеВБраке.СоздатьЭлемент();
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(СправочникОбъект, Выборка, , "Ссылка");
			СправочникОбъект.ДополнительныеСвойства.Вставить("ЗаписьОбщихДанных");
			
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(СправочникОбъект);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли