
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Параметры.Свойство("ТекстАлгоритма", ТекстАлгоритма);
	Если Параметры.Свойство("ПараметрыАлгоритма") Тогда
		Для каждого СтруктураПараметра Из Параметры.ПараметрыАлгоритма Цикл
			ЗаполнитьЗначенияСвойств(ПараметрыАлгоритма.Добавить(), СтруктураПараметра);
		КонецЦикла;
		ОбновитьКоличествоПараметров(ЭтотОбъект);
	КонецЕсли;
	
	Если Параметры.Свойство("Заголовок") Тогда
		Параметры.Свойство("Заголовок", Заголовок);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		СтандартнаяОбработка = Ложь;
		Отказ = Истина;
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?';
							|en = 'The data was changed. Do you want to save the changes?'");
		ОписаниеОповещения = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПараметрыАлгоритма

&НаКлиенте
Процедура ПараметрыАлгоритмаПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ОбновитьКоличествоПараметров(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПараметрыАлгоритмаЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ПараметрыАлгоритма.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НастроитьЭлементВыбораЗначенияПараметра(Элемент, ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ПараметрыАлгоритмаЗначениеОчистка(Элемент, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ПараметрыАлгоритма.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.Значение = Неопределено;
	ТекущиеДанные.ТипЗначения = Неопределено;
	ТекущиеДанные.ТипЗначенияПредставление = Неопределено;
	
	НастроитьЭлементВыбораЗначенияПараметра(Элемент, ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)
	
	ЗавершитьРедактирование();
	
КонецПроцедуры

&НаКлиенте
Процедура Проверить(Команда)
	
	Возврат; // Пустой обработчик
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗавершитьРедактирование()
	
	Отказ = Ложь;
	
	ШаблонИмяПараметра = НСтр("ru = 'Не указано имя параметра в строке %1.';
								|en = 'A parameter name is not specified in line %1.'");
	ШаблонЗначениеПараметра = НСтр("ru = 'Не задано значение параметра ""%1"".';
									|en = 'A value of the %1 parameter is not set.'");
	ШаблонПутьКДанным = "ПараметрыАлгоритма[%1].%2";
	
	МассивПараметров = Новый Массив;
	Для Индекс = 0 По ПараметрыАлгоритма.Количество()-1 Цикл
		СтрокаПараметр = ПараметрыАлгоритма[Индекс];
		Если НЕ ЗначениеЗаполнено(СтрокаПараметр.ИмяПараметра) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				СтрШаблон(ШаблонИмяПараметра, Индекс),,
				СтрШаблон(ШаблонПутьКДанным, Индекс, "ИмяПараметра"),,
				Отказ);
			Продолжить;
		КонецЕсли;
		Если СтрокаПараметр.Значение = Неопределено Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				СтрШаблон(ШаблонЗначениеПараметра, СтрокаПараметр.ИмяПараметра),,
				СтрШаблон(ШаблонПутьКДанным, Индекс, "Значение"),,
				Отказ);
			Продолжить;
		КонецЕсли;
		МассивПараметров.Добавить(Новый Структура("ИмяПараметра,Значение", СтрокаПараметр.ИмяПараметра, СтрокаПараметр.Значение));
	КонецЦикла;
	
	Результат = Новый Структура;
	Результат.Вставить("ТекстАлгоритма", ТекстАлгоритма);
	Результат.Вставить("ПараметрыАлгоритма", МассивПараметров);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Ложь;
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЗавершитьРедактирование();
	ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьЭлементВыбораЗначенияПараметра(Элемент, ДанныеСтроки)
	
	Элемент.ВыбиратьТип          = (ДанныеСтроки.Значение = Неопределено);
	Элемент.КнопкаВыбора         = Истина;
	Элемент.КнопкаОчистки        = НЕ Элемент.ВыбиратьТип;
	Элемент.ВыборГруппИЭлементов = ГруппыИЭлементы.ГруппыИЭлементы;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьКоличествоПараметров(Форма)
	
	Форма.КоличествоПараметров = Форма.ПараметрыАлгоритма.Количество();
	
КонецПроцедуры

#КонецОбласти
