
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОписаниеДействия = Неопределено;
	Если НЕ Параметры.Свойство("ОписаниеДействия", ОписаниеДействия) Тогда
		ОписаниеДействия = УправлениеДаннымиОбИзделияхКлиентСервер.ОписаниеДействияСФайламиКонструктор();
	Иначе
		Заголовок = СтрШаблон(НСтр("ru = '%1 для состояния ""%2""';
									|en = '%1 for the ""%2"" state'"), Заголовок, ОписаниеДействия.Состояние);
	КонецЕсли;
	
	Параметры.Свойство("РасположениеДанных", РасположениеДанных);
	
	Действие = ОписаниеДействия.Действие;
	Если ДействиеПереместить(ЭтотОбъект) Тогда
		КаталогПеремещения = ОписаниеДействия.Каталог;
	Иначе
		КаталогПеремещения = РасположениеДанных;
	КонецЕсли;
	
	НастроитьЭлементыФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		СтандартнаяОбработка = Ложь;
		Отказ = Истина;
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?';
							|en = 'The data was changed. Do you want to save the changes?'");
		ОписаниеОповещения = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДействиеПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект, "Действие")
	
КонецПроцедуры

&НаКлиенте
Процедура КаталогПеремещенияПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура КаталогПеремещенияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДиалогВыбора = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	ДиалогВыбора.МножественныйВыбор = Ложь;
	ДиалогВыбора.Каталог = КаталогПеремещения;
	ДиалогВыбора.Заголовок = НСтр("ru = 'Выберите каталог расположения файлов с данными';
									|en = 'Select a data file directory'");
	ОписаниеОповещения = Новый ОписаниеОповещения("КаталогПеремещенияНачалоВыбораЗавершение", ЭтотОбъект);
	
	ФайловаяСистемаКлиент.ПоказатьДиалогВыбора(ОписаниеОповещения, ДиалогВыбора);
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)
	
	ЗавершитьРедактирование();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастроитьЭлементыФормы()
	
	//

	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЗависимыеЭлементыФормы(Форма, СписокРеквизитов = "")
	
	Элементы = Форма.Элементы;
	
	Инициализация = ПустаяСтрока(СписокРеквизитов);
	СтруктураРеквизитов = Новый Структура(СписокРеквизитов);
	
	Если СтруктураРеквизитов.Свойство("Действие")
		ИЛИ Инициализация Тогда
			
			Элементы.КаталогПеремещения.Доступность = ДействиеПереместить(Форма);
			Элементы.КаталогПеремещения.АвтоОтметкаНезаполненного = ДействиеПереместить(Форма);
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КаталогПеремещенияНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда
		КаталогПеремещения = Результат[0];
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЗавершитьРедактирование();
	ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьРедактирование()
	
	Отказ = Ложь;
	
	Если ДействиеПереместить(ЭтотОбъект) Тогда
		
		Если ПустаяСтрока(КаталогПеремещения) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				НСтр("ru = 'Необходимо указать каталог для перемещения файлов';
					|en = 'Specify a directory to move files'"),,
				"КаталогПеремещения",,
				Отказ);
		КонецЕсли;
		
		Если НРег(ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(РасположениеДанных)) =
				НРег(ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(КаталогПеремещения)) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				НСтр("ru = 'Исходный каталог расположения файлов совпадает с каталогом для перемещения';
					|en = 'The source file directory is the same as the directory for moving files'"),,
				"КаталогПеремещения",,
				Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Ложь;
	
	ОписаниеДействия.Действие = Действие;
	ОписаниеДействия.Каталог  = ?(ДействиеПереместить(ЭтотОбъект), КаталогПеремещения, "");
	
	Закрыть(ОписаниеДействия)
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ДействиеПереместить(Форма)
	
	Возврат Форма.Действие = ПредопределенноеЗначение("Перечисление.ЗагрузкаНСИДействияСФайлами.Переместить");
	
КонецФункции

#КонецОбласти