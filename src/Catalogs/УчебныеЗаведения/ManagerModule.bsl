///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2023, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

Процедура ЗаполнитьПолноеНаименование(ПараметрыОбновления = Неопределено) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.Медицина.ФедеральныйРегистрМедицинскихРаботников") Тогда
		МодульМедицинскиеРегистры = ОбщегоНазначения.ОбщийМодуль("МедицинскиеРегистры");
		Запрос = МодульМедицинскиеРегистры.ЗапросОпределитьПолныеНаименованияУчебныхЗаведений(ПараметрыОбновления);
	Иначе 
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	УчебныеЗаведения.Ссылка КАК Ссылка,
		|	УчебныеЗаведения.Наименование КАК ПолноеНаименование
		|ИЗ
		|	Справочник.УчебныеЗаведения КАК УчебныеЗаведения
		|ГДЕ
		|	УчебныеЗаведения.ПолноеНаименование = """"";
	КонецЕсли;

	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "Справочник.УчебныеЗаведения", "Ссылка", Выборка.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		Объект = Выборка.Ссылка.ПолучитьОбъект();
		Объект.ПолноеНаименование = Выборка.ПолноеНаименование;
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(Объект);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
	КонецЦикла;	
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
