#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Если Расписание.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;	
	
	ПроверитьПересечениеИнтервалов(Отказ);
	ПроверитьПоследовательностьВведенныхИнтервалов(Отказ);
КонецПроцедуры

#КонецОбласти
	

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьПересечениеИнтервалов(Отказ)
	ШаблонТекстаСообщения = НСтр("ru = 'Интервалы времени в строках расписания №%1 и №%2 пересекаются.';
								|en = 'Time intervals in the schedule lines No.%1 and No.%2 overlap.'");
	
	Для Индекс = 0 По Расписание.Количество() - 2 Цикл
		ТекущаяСтрока = Расписание[Индекс];
		
		Для ИндексПроверяемойСтроки = Индекс + 1 По Расписание.Количество() - 1 Цикл
			ПроверяемаяСтрока = Расписание[ИндексПроверяемойСтроки];
			
			Если ИнтервалыПересекаются(ТекущаяСтрока, ПроверяемаяСтрока) Тогда
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекстаСообщения, ТекущаяСтрока.НомерСтроки, ПроверяемаяСтрока.НомерСтроки);
				Поле = "Расписание[" + Формат(ТекущаяСтрока.НомерСтроки, "ЧН=0; ЧГ=") + "].ВремяНачала";
				
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, ,Отказ);
			КонецЕсли;
		КонецЦикла;			
	КонецЦикла;		
КонецПроцедуры	

Процедура ПроверитьПоследовательностьВведенныхИнтервалов(Отказ)
	ШаблонТекстаСообщения = НСтр("ru = 'Время начала интервала в строке №%1 должно быть больше чем время начала интервала в предыдущей строке.';
								|en = 'Interval start time in line No. %1 must be greater than the interval start time in the previous line.'");
	
	ПереходНаСледующиеСутки = Ложь;
	ПредыдущийИнтервал = Неопределено;
	Для Каждого ТекущийИнтервал Из Расписание Цикл 
		Если ПредыдущийИнтервал = Неопределено Тогда
			ПредыдущийИнтервал = ТекущийИнтервал;
			Продолжить;
		КонецЕсли;
		
		Если ТекущийИнтервал.ВремяНачала < ПредыдущийИнтервал.ВремяНачала Тогда
			Если ПереходНаСледующиеСутки Тогда
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекстаСообщения, ТекущийИнтервал.НомерСтроки);
				Поле = "Расписание[" + Формат(ТекущийИнтервал.НомерСтроки, "ЧН=0; ЧГ=") + "].ВремяНачала";
				
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, ,Отказ);	
			Иначе
				ПереходНаСледующиеСутки = Истина;
			КонецЕсли;	
		КонецЕсли;	
		
		ПредыдущийИнтервал = ТекущийИнтервал;
	КонецЦикла;		
КонецПроцедуры	

Функция ИнтервалыПересекаются(СтрокаИнтервала1, СтрокаИнтервала2)
	Если СтрокаИнтервала1.ВремяНачала > СтрокаИнтервала2.ВремяНачала 
		И СтрокаИнтервала1.ВремяНачала < СтрокаИнтервала2.ВремяОкончания Тогда
		
		Возврат Истина;
	ИначеЕсли СтрокаИнтервала2.ВремяНачала > СтрокаИнтервала1.ВремяНачала 
		И СтрокаИнтервала2.ВремяНачала < СтрокаИнтервала1.ВремяОкончания Тогда
		
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;			
КонецФункции	

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли