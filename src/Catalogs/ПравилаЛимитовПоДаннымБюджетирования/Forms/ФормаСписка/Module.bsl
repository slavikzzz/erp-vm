
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОтборПравил = Неопределено;
	ОтборПоВладельцу = Неопределено;
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ВладелецОпределен = Ложь;
	Если Параметры.Свойство("Отбор", ОтборПравил) Тогда
		Если ОтборПравил.Свойство("Владелец", ОтборПоВладельцу) Тогда
			Если ЗначениеЗаполнено(ОтборПоВладельцу) Тогда
				ВладелецОпределен = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ВладелецОпределен Тогда
		ВызватьИсключение НСтр("ru = 'Просмотреть список лимитов можно только в форме модели бюджетирования';
								|en = 'You can view a limit list only in the budgeting model form'");
	КонецЕсли;
	
	// Настройки видимости при запуске
	УправлениеЭлементами();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьПоБюджету(Команда)
	
	МодельБюджетирования = Неопределено;
	Для Каждого Отбор Из Список.Отбор.Элементы Цикл
		Если Строка(Отбор.ЛевоеЗначение) = "Владелец" Тогда
			МодельБюджетирования = Отбор.ПравоеЗначение;
		КонецЕсли;
	КонецЦикла;
	
	ПараметрыФормы = Новый Структура("Владелец", МодельБюджетирования);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("СозданиеЭлементовПоВидуБюджета", ЭтаФорма);
	ОткрытьФорму("Справочник.ПравилаЛимитовПоДаннымБюджетирования.Форма.ВыборЭлементовВидаБюджета", ПараметрыФормы,,,,,
																			ОписаниеОповещения,
																			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура СозданиеЭлементовПоВидуБюджета(РезультатВыбора, ДопПараметры) Экспорт
	
	Если РезультатВыбора = Истина Тогда
		
		Элементы.Список.Обновить();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЛимитыРасходаДС(Команда)
	
	ОткрытьФорму("Отчет.ЛимитыРасходаДенежныхСредствПоДаннымБюджетирования.Форма", Новый Структура("СформироватьПриОткрытии", Истина));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УправлениеЭлементами()
	Элементы.ФормаДобавитьПоБюджету.Видимость = ПравоДоступа("Добавление", Метаданные.Справочники.ПравилаЛимитовПоДаннымБюджетирования);
КонецПроцедуры

#КонецОбласти
