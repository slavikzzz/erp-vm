#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

Процедура ВыполнитьНачальноеЗаполнение() Экспорт
	
	Выборка = Справочники.ПравилаГруппировкиАктивовОбязательств.Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат;
	КонецЕсли;
	
	ПравилаГруппировки = НалогНаПрибыльБухгалтерскийУчет.НовыйПравилаГруппировкиАктивовОбязательств();
	НалогНаПрибыльБухгалтерскийУчет.ЗаполнитьПравилаГруппировкиВидовАктивовИОбязательств(ПравилаГруппировки);

	НачатьТранзакцию();
	Попытка
		Для Каждого Правило Из ПравилаГруппировки Цикл
			
			СправочникОбъект = Справочники.ПравилаГруппировкиАктивовОбязательств.СоздатьЭлемент();
			СправочникОбъект.ВидАктивовОбязательств = Правило.ВидАктивовОбязательств;
			СправочникОбъект.ОсновнойСчет = Правило.ОсновнойСчет;
			Для Каждого Счет Из Правило.ДополнительныеСчета Цикл
				СтрокаТЧ = СправочникОбъект.ДополнительныеСчета.Добавить();
				СтрокаТЧ.СчетУчета = Счет;
			КонецЦикла;
			Для Каждого Субконто Из Правило.ИсключенныеСубконто Цикл
				СтрокаТЧ = СправочникОбъект.ИсключенныеСубконто.Добавить();
				СтрокаТЧ.Субконто = Субконто;
			КонецЦикла;
			СправочникОбъект.Наименование = Правило.ВидАктивовОбязательств;
			СправочникОбъект.Записать();
		
		КонецЦикла;
	
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), Выборка.Ссылка);
		ВызватьИсключение;
	КонецПопытки;
		
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли