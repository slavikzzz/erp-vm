
#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	
	ПриЧтенииСозданииНаСервере();
	
	СобытияФорм.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	ПриЧтенииСозданииНаСервере();
	
	СобытияФорм.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидАктивовОбязательствПриИзменении(Элемент)
	
	Если Объект.Наименование = "" Тогда
		Объект.Наименование = Объект.ВидАктивовОбязательств;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыИсключенныеСубконто
&НаКлиенте
Процедура ИсключенныеСубконтоПриИзменении(Элемент)

	ОбновитьЗаголовки(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДополнительныеСчета
&НаКлиенте
Процедура ДополнительныеСчетаПриИзменении(Элемент)

	ОбновитьЗаголовки(ЭтотОбъект);
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	
	ОбновитьЗаголовки(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьЗаголовки(Форма)
	
	ЗначенияСчетов = Новый Массив;
	Для Каждого СтрокаСчета Из Форма.Объект.ДополнительныеСчета Цикл
		ЗначенияСчетов.Добавить(СтрокаСчета.СчетУчета);
	КонецЦикла;
	
	Счета= " " + ?(ЗначенияСчетов.Количество(), СтрСоединить(ЗначенияСчетов, ", "), НСтр("ru = 'не указаны';
																						|en = 'not specified'"));
	
	Форма.Элементы.ГруппаДополнительныеСчета.ЗаголовокСвернутогоОтображения = "Дополнительные счета: " + Счета;
	
	ЗначенияСубконто = Новый Массив;
	Для Каждого СтрокаСубконто Из Форма.Объект.ИсключенныеСубконто Цикл
		ЗначенияСубконто.Добавить(СтрокаСубконто.Субконто);
	КонецЦикла;
	
	Субконто= " " + ?(ЗначенияСубконто.Количество(), СтрСоединить(ЗначенияСубконто, ", "), НСтр("ru = 'не указаны';
																								|en = 'not specified'"));
	
	Форма.Элементы.ГруппаИсключенныеСубконто.ЗаголовокСвернутогоОтображения = "Исключенные субконто: " + Субконто;
	
КонецПроцедуры

#КонецОбласти
