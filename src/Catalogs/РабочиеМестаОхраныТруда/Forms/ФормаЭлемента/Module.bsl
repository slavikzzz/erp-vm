
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Первоначальное заполнение объекта.
	Если Параметры.Ключ.Пустая() Тогда
		СсылкаНаОбъект = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Объект.Ссылка).ПолучитьСсылку();
		ПриПолученииДанныхНаСервере(Параметры.ЗначениеКопирования);
		ПриИзмененииОсновныхДанных(Ложь);
	КонецЕсли;
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ПриПолученииДанныхНаСервере(ТекущийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	СсылкаНаОбъект = ТекущийОбъект.Ссылка;
	ПриПолученииДанныхНаСервере(ТекущийОбъект);
		
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	Если Параметры.Ключ.Пустая() Тогда
		ТекущийОбъект.УстановитьСсылкуНового(СсылкаНаОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	ПараметрыОповещения = Новый Структура("ВладелецФормы");
	ПараметрыОповещения.ВладелецФормы = ВладелецФормы;
	ОбщегоНазначенияКлиент.ОповеститьОбИзмененииОбъекта(Объект.Ссылка, ПараметрыОповещения);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НомерПриИзменении(Элемент)
	
	Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Объект.Номер) Тогда
		Объект.ПорядковыйНомер = Число(Объект.Номер);
	КонецЕсли;
	
	ЗаполнитьНаименование();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеПриИзменении(Элемент)
	
	ПриИзмененииОсновныхДанных();
	УстановитьНомерВручную(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДолжностьПриИзменении(Элемент)

	ПриИзмененииОсновныхДанных();
	УстановитьНомерВручную(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура НомерВручнуюПриИзменении(Элемент)
	
	ПриИзмененииОсновныхДанных();
	УстановитьОтображениеНомера(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаСервере
Процедура ЗаполнитьНаименование()
	
	НомерОсновногоРабочегоМеста = "";
	
	Объект.Наименование = РабочиеМестаОхраныТруда.НаименованиеРабочегоМеста(
		Объект.Подразделение,
		Объект.Должность,
		Объект.Номер,
		НомерОсновногоРабочегоМеста);
	
	Заголовок = Объект.Наименование;
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииОсновныхДанных(ЗаполнятьНаименование = Истина)
	
	ЗаполнитьНомер();
	СформироватьАвтоматическийНомер();
	Если ЗаполнятьНаименование Тогда
		ЗаполнитьНаименование();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНомер()
	
	Если Объект.НомерВручную Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ПорядковыйНомер) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Справочники.РабочиеМестаОхраныТруда.НомерРабочегоМестаЗанят(Объект, СсылкаНаОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ПорядковыйНомер = 1 + Справочники.РабочиеМестаОхраныТруда.МаксимальныйНомерРабочегоМеста(Объект.Владелец, Объект.Подразделение);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьДоступностьЭлементов(Форма)
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"НомерВручную",
		"Доступность",
		Форма.РабочееМестоДоступноДляРедактирования);
	
	УстановитьОтображениеНомера(Форма);
	
КонецПроцедуры	

&НаСервере
Процедура ПриПолученииДанныхНаСервере(ТекущийОбъект)
	
	РабочееМестоДоступноДляРедактирования = ПравоДоступа("Редактирование", Метаданные.Справочники.РабочиеМестаОхраныТруда);
	
	УстановитьДоступностьЭлементов(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьНомерВручную(Форма)
	
	Объект = Форма.Объект;
	
	Если ПустаяСтрока(Объект.Номер) Тогда
		Объект.НомерВручную = Истина;
		УстановитьОтображениеНомера(Форма);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьАвтоматическийНомер()
	
	Если Объект.НомерВручную Тогда
		Возврат;
	КонецЕсли;
	
	Объект.Номер = СтрЗаменить(Строка(Объект.ПорядковыйНомер), Символы.НПП, "");
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтображениеНомера(Форма)
	
	Объект = Форма.Объект;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"Номер",
		"ТолькоПросмотр",
		Не Объект.НомерВручную);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"Номер",
		"АвтоОтметкаНезаполненного",
		Объект.НомерВручную);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"Номер",
		"ОтметкаНезаполненного",
		Объект.НомерВручную И ЗначениеЗаполнено(Объект.Номер));
	
КонецПроцедуры

#КонецОбласти
