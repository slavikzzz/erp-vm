#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс	
	
#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"ПрисоединитьДополнительныеТаблицы
	|ЭтотСписок КАК ЭтотСписок
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Владельцы 
	|	ПО Владельцы.Ссылка = ЭтотСписок.Владелец
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОбособленныеПодразделения 
	|	ПО ОбособленныеПодразделения.ГоловнаяОрганизация = Владельцы.Ссылка
	|;
	|РазрешитьЧтение
	|ГДЕ
	|	ЗначениеРазрешено(Владелец)
	| ИЛИ ЗначениеРазрешено(ОбособленныеПодразделения.Ссылка)
	|;
	|РазрешитьИзменениеЕслиРазрешеноЧтение
	|ГДЕ
	|	ЗначениеРазрешено(Владелец)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция СтандартноеНаименование(Знач СтруктурнаяЕдиница) Экспорт
	
	Свойства = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтруктурнаяЕдиница, "Наименование,ОбособленноеПодразделение");
	ЭтоТОСП = Свойства.ОбособленноеПодразделение;
	
	Префикс = ?(ЭтоТОСП, НСтр("ru = 'ТОСП';
								|en = 'Territorially autonomous business unit'"),
		?(ТипЗнч(СтруктурнаяЕдиница) = Тип("СправочникСсылка.Организации"), НСтр("ru = 'Орг.';
																				|en = 'Com.'"), НСтр("ru = 'Подр.';
																										|en = 'Bus. unit'")));
	Результат = СтрШаблон(НСтр("ru = '%1 %2';
								|en = '%1 %2'"), Префикс, Свойства.Наименование);
	Возврат Результат;
	
КонецФункции

Функция ОписаниеТиповРеспондентовРосстата() Экспорт
	
	Возврат Новый ОписаниеТипов("СправочникСсылка.Организации,СправочникСсылка.ПодразделенияОрганизаций");
	
КонецФункции

#КонецОбласти

#КонецЕсли