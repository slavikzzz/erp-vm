#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.ШаблонЗаполнения <> Неопределено Тогда
		Для Каждого Смена Из Параметры.ШаблонЗаполнения Цикл
			СтрокаТаблицы = ШаблонЗаполнения.Добавить();
			СтрокаТаблицы.НомерДня = ШаблонЗаполнения.Количество();
			СтрокаТаблицы.Смена = Смена;
		КонецЦикла;
	КонецЕсли;
	
	РежимРаботы = Параметры.РежимРаботы;
	Для Каждого СтрокаСмены Из РежимРаботы.Смены Цикл
		Смены.Добавить(СтрокаСмены.Смена.Ссылка);
	КонецЦикла;
	
	ПараметрыВыбора = Новый Массив;
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("РежимРаботы", РежимРаботы));
	Элементы.ШаблонЗаполненияСмена.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Если Модифицированность Тогда 
		ТекстПредупреждения = НСтр("ru = 'Данные были изменены, внесенные изменения будут отменены.
										 |Отменить и закрыть?';
										 |en = 'Data was changed, changes will be canceled.
										 |Cancel and close? '");
		ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияПроизвольнойФормы(ЭтотОбъект, Отказ, ЗавершениеРаботы, ТекстПредупреждения, "ДействиеВыбрано");
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Ок(Команда)
	Результат = Новый Массив;
	ДействиеВыбрано = Истина;
	Для Каждого СтрокаТаблицы Из ШаблонЗаполнения Цикл
		Результат.Добавить(СтрокаТаблицы.Смена);
	КонецЦикла;	
	
	Закрыть(Результат);
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	ДействиеВыбрано = Ложь;
	Закрыть(Неопределено);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормШаблонЗаполнения

&НаКлиенте
Процедура ШаблонЗаполненияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда
		Элементы.ШаблонЗаполнения.ТекущиеДанные.НомерДня = ШаблонЗаполнения.Количество();
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ШаблонЗаполненияСменаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Если Смены.Количество() <> 0 Тогда
		СтандартнаяОбработка = Ложь;
		
		ПараметрыОткрытия = Новый Структура;
		
		НастройкиКомпоновки = Новый НастройкиКомпоновкиДанных;
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(НастройкиКомпоновки.Отбор, "Ссылка",
			ВидСравненияКомпоновкиДанных.ВСписке, Смены);
			
		ПараметрыОткрытия.Вставить("ФиксированныеНастройки", НастройкиКомпоновки);
		
		ОткрытьФорму("Справочник.СменыРаботыСотрудников.ФормаВыбора", ПараметрыОткрытия, Элемент);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
