#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Если ДанныеЗаполнения.Свойство("Родитель") И ЗначениеЗаполнено(ДанныеЗаполнения.Родитель) Тогда
			РодительГруппа = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения.Родитель, "ЭтоГруппаШаблонов");
			Если Не РодительГруппа Тогда
				ДанныеЗаполнения.Родитель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения.Родитель, "Родитель");
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЭтоГруппаШаблонов Тогда
		ПроверяемыеРеквизиты.Очистить();
	КонецЕсли;
	
	МассивНепроверяемыхРеквизитов = Новый Массив();
	
	ИсточникДанных = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Операция, "ИсточникДанных");
	ПоказателиВВалюте = МеждународныйУчетСерверПовтИсп.ПоказателиВВалюте(ИсточникДанных);
	ПоказателиКоличества = МеждународныйУчетСерверПовтИсп.ПоказателиКоличества(ИсточникДанных);
	
	Если Не МеждународныйУчетСерверПовтИсп.СвойстваСчета(СчетДебетаПоУмолчанию).Валютный
		Или ПоказателиВВалюте = Неопределено
		ИЛИ (ПоказателиВВалюте <> Неопределено И ПоказателиВВалюте.Количество() = 0) Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ИсточникСуммыВВалютеДт");
	КонецЕсли;
	
	Если Не МеждународныйУчетСерверПовтИсп.СвойстваСчета(СчетКредитаПоУмолчанию).Валютный
		ИЛИ ПоказателиВВалюте = Неопределено
		ИЛИ (ПоказателиВВалюте <> Неопределено И ПоказателиВВалюте.Количество() = 0) Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ИсточникСуммыВВалютеКт");
	КонецЕсли;
	
	ТекстСообщения = НСтр("ru = 'Для количественного счета %1 не указан источник количества.';
							|en = 'For quantitative account%1the source of quantity is not indicated.'");
	Если МеждународныйУчетСерверПовтИсп.СвойстваСчета(СчетДебетаПоУмолчанию).Количественный
		И НЕ ЗначениеЗаполнено(ИсточникКоличестваДт)Тогда
		ОшибкаСчета = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, СчетДебетаПоУмолчанию);
		ОбщегоНазначения.СообщитьПользователю(ОшибкаСчета, Ссылка);
	КонецЕсли;
	
	Если МеждународныйУчетСерверПовтИсп.СвойстваСчета(СчетКредитаПоУмолчанию).Количественный
		И НЕ ЗначениеЗаполнено(ИсточникКоличестваКт) Тогда
		ОшибкаСчета = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, СчетКредитаПоУмолчанию);
		ОбщегоНазначения.СообщитьПользователю(ОшибкаСчета, Ссылка);
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат; 
	КонецЕсли;
	
	Если НЕ ЗаполнениеСубконтоПоСоответствию Тогда
		НастройкиЗаполненияСубконто.Очистить();
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ИндексКартинки = Справочники.ШаблоныПроводокДляМеждународногоУчета.ИндексКартинки(ЭтотОбъект);
	
	УстановкаРеквизитовИсторииПереходаНаНовыеФормулы();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат; 
	КонецЕсли; 
	
	Если ПометкаУдаления Тогда
		НаборЗаписей = РегистрыСведений.ПравилаОтраженияВМеждународномУчете.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ШаблонПроводки.Установить(Ссылка);
		НаборЗаписей.Записать();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановкаРеквизитовИсторииПереходаНаНовыеФормулы()
	
	Для Каждого СтрокаТЧ Из НастройкиЗаполненияСубконто Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаТЧ.ВыражениеИсторияПереходаНаНовыеФормулы)
			И ЗначениеЗаполнено(СтрокаТЧ.Выражение) Тогда
			СтрокаТЧ.ВыражениеИсторияПереходаНаНовыеФормулы = СтрокаТЧ.Выражение;
		КонецЕсли;
	КонецЦикла;
	УдалитьПереходНаНовыеФормулыВыполнен = Истина;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли