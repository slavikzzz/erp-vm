#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если Не ПометкаУдаления Тогда
		
		Если Не ЗначениеЗаполнено(ИмяФайлаДоверенности)
			И ДополнительныеСвойства.Свойство("СформироватьФайлПриЗаписи") Тогда
			
			Если НеобходимыеДанныеЗаполнены() Тогда
				Попытка
					Доверенность = Новый ХранилищеЗначения(
						Справочники.УдалитьМашиночитаемыеДоверенностиКЭДО.ТелоДоверенности(ЭтотОбъект), Новый СжатиеДанных(9));
				Исключение
					Ошибка = ИнформацияОбОшибке();
					ЗаписьЖурналаРегистрации(
						КадровыйЭДО.ИмяСобытияЖурналаРегистрации(НСтр("ru = 'Подготовка файла МЧД';
																		|en = 'Подготовка файла МЧД'")),
						УровеньЖурналаРегистрации.Ошибка,
						Метаданные.Справочники.УдалитьМашиночитаемыеДоверенностиКЭДО,
						Ссылка,
						ПодробноеПредставлениеОшибки(Ошибка));
				КонецПопытки;
			Иначе
				Доверенность = Неопределено;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НеобходимыеДанныеЗаполнены(ВыводитьСообщения = Ложь) Экспорт
	
	Отказ = Ложь;
	
	Если Не ЗначениеЗаполнено(Доверитель) Тогда
		Если ВыводитьСообщения Тогда
			ТекстСообщения = НСтр("ru = 'Не задан доверитель';
									|en = 'Не задан доверитель'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Ссылка, "Доверитель", "Объект", Отказ);
		Иначе
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(Доверитель) = Тип("СправочникСсылка.Организации") И Не ЗначениеЗаполнено(ОтветственныйДоверителя) Тогда
		Если ВыводитьСообщения Тогда
			ТекстСообщения = НСтр("ru = 'Не задан ответственный доверителя';
									|en = 'Не задан ответственный доверителя'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Ссылка, "ОтветственныйДоверителя", "Объект", Отказ);
		Иначе
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Представитель) Тогда
		Если ВыводитьСообщения Тогда
			ТекстСообщения = НСтр("ru = 'Не задан представитель';
									|en = 'Не задан представитель'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Ссылка, "Представитель", "Объект", Отказ);
		Иначе
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(Представитель) = Тип("СправочникСсылка.Организации") И Не ЗначениеЗаполнено(ОтветственныйПредставителя) Тогда
		Если ВыводитьСообщения Тогда
			ТекстСообщения = НСтр("ru = 'Не задан ответственный представителя';
									|en = 'Не задан ответственный представителя'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Ссылка, "ОтветственныйПредставителя", "Объект", Отказ);
		Иначе
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДатаНачала)
		Или Не ЗначениеЗаполнено(ДатаОкончания)
		Или ДатаОкончания < ДатаНачала Тогда
		
		Если ВыводитьСообщения Тогда
			ТекстСообщения = НСтр("ru = 'Не корректно задан период действия доверенности';
									|en = 'Не корректно задан период действия доверенности'");
			Если Не ЗначениеЗаполнено(ДатаНачала) Тогда
				Поле = "ДатаНачала";
			Иначе
				Поле = "ДатаОкончания";
			КонецЕсли;
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Ссылка, Поле, "Объект", Отказ);
		Иначе
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Не Отказ;
	
КонецФункции

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли