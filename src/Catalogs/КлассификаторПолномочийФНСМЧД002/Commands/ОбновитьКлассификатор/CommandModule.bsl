#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриПолученииОтветаНаВопросОЗагрузкеКлассификатораФНС_МЧД002", ЭтотОбъект,
		ПараметрыВыполненияКоманды.Источник);
		
	ЗаголовокВопроса = НСтр("ru = 'Обновление классификатора';
							|en = 'Update classifier'");
	ТекстВопроса = НСтр("ru = 'Обновить классификатор полномочий ФНС (МЧД 002) сейчас?';
						|en = 'Do you want to update the FTS authority classifier (machine-readable authorization letter 002) now?'");
	Кнопки = Новый СписокЗначений;
	Кнопки.Добавить(КодВозвратаДиалога.Да);
	Кнопки.Добавить(КодВозвратаДиалога.Отмена);
	
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, Кнопки, , Кнопки[0].Значение, ЗаголовокВопроса);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПриПолученииОтветаНаВопросОЗагрузкеКлассификатораФНС_МЧД002(Ответ, Форма) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ДлительнаяОперация = ОбновитьКлассификаторНаСервере();
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ВыполнитьДействиеЗавершение", ЭтотОбъект, Форма);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Функция ОбновитьКлассификаторНаСервере()
	
	ПараметрыОперации = СинхронизацияЭДО.ЗаполнитьПараметрыСинхронизацииКлассификатораПолномочийФНС_МЧД002(Истина);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне();
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Синхронизация классификатора полномочий ФНС (МЧД 002).';
															|en = 'Synchronize the FTS authority classifier (machine-readable authorization letter 002).'");
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"СинхронизацияЭДО.СинхронизироватьКлассификаторПолномочийФНС_МЧД002", ПараметрыОперации, ПараметрыВыполнения);
	
КонецФункции

&НаКлиенте
Процедура ВыполнитьДействиеЗавершение(РезультатЗадания, Форма) Экспорт
	
	Если РезультатЗадания = Неопределено Тогда // Задание было отменено
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Загрузка отменена.';
														|en = 'Import is canceled.'"));
		Возврат;
		
	КонецЕсли;
	
	Если РезультатЗадания.Статус = "Выполнено" Тогда
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Загрузка завершена успешно.';
														|en = 'Import is completed.'"));
		Форма.Элементы.Список.Обновить();
		
	Иначе
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Ошибка синхронизации. Подробности см. в журнале регистрации.';
				|en = 'A synchronization error. For more information, see the event log.'"));
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
