#Область ОбработчикиСобытийФормы



&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Организация = Параметры.Организация;
	ЕГДС = Параметры.ЕГДС;

	УстановитьПараметрыДинамическихСписков();
	
	УправлениеВидимостью();

КонецПроцедуры


&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "Запись_ИзменениеПараметровОС2_4" Тогда
		КорзинаОС.Очистить();
	ИначеЕсли ИмяСобытия = "Запись_ИзменениеПараметровНМА2_4" Тогда
		КорзинаНМА.Очистить();
	КонецЕсли;

КонецПроцедуры



#КонецОбласти



#Область ОбработчикиСобытийЭлементовТаблицыФормыОС



&НаКлиенте
Процедура ОСВыбрать(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени(
		"Справочник.ЕдиницыГенерирующиеДенежныеСредства.Форма.ФормаВключенияВЕГДС.ОСДобавлениеВСписокВыбранного");
		
	ДобавитьВыбранныеОС(Элементы.ОС.ВыделенныеСтроки);

КонецПроцедуры



#КонецОбласти



#Область ОбработчикиСобытийЭлементовТаблицыФормыНМА

&НаКлиенте
Процедура НМАВыбрать(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени(
		"Справочник.ЕдиницыГенерирующиеДенежныеСредства.Форма.ФормаВключенияВЕГДС.НМАДобавлениеВСписокВыбранного");
		
	ДобавитьВыбранныеНМА(Элементы.НМА.ВыделенныеСтроки);

КонецПроцедуры


#КонецОбласти



#Область ОбработчикиСобытийЭлементовТаблицыФормыКорзинаОС



&НаКлиенте
Процедура КорзинаОСПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)

	ДобавитьВыбранныеОС(ПараметрыПеретаскивания.Значение);

КонецПроцедуры


// Добавить выбранные ОС.
//
// Параметры:
// ОСВыделенныеСтроки - Массив
&НаКлиенте
Процедура ДобавитьВыбранныеОС(ОСВыделенныеСтроки)

	Для Каждого СтрокаОС Из ОСВыделенныеСтроки Цикл

		ТекДанныеОС = Элементы.ОС.ДанныеСтроки(СтрокаОС);

		ФильтрПоискаОС = Новый Структура;
		ФильтрПоискаОС.Вставить("ОсновноеСредство", ТекДанныеОС.Ссылка);

		Если КорзинаОС.НайтиСтроки(ФильтрПоискаОС).Количество() = 0 Тогда
			СтрокаВыбранныхОС = КорзинаОС.Добавить();
			СтрокаВыбранныхОС.ОсновноеСредство = ТекДанныеОС.Ссылка;
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры



#КонецОбласти



#Область ОбработчикиСобытийЭлементовТаблицыФормыКорзинаНМА

&НаКлиенте
Процедура КорзинаНМАПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)

	ДобавитьВыбранныеНМА(ПараметрыПеретаскивания.Значение);

КонецПроцедуры


// Добавить выбранные НМА.
//
// Параметры:
// НМАВыделенныеСтроки - Массив
&НаКлиенте
Процедура ДобавитьВыбранныеНМА(НМАВыделенныеСтроки)

	Для Каждого СтрокаНМА Из НМАВыделенныеСтроки Цикл

		ТекДанныеНМА = Элементы.НМА.ДанныеСтроки(СтрокаНМА);

		ФильтрПоискаНМА = Новый Структура;
		ФильтрПоискаНМА.Вставить("НематериальныйАктив", ТекДанныеНМА.Ссылка);

		Если КорзинаНМА.НайтиСтроки(ФильтрПоискаНМА).Количество() = 0 Тогда
			СтрокаВыбранныхНМА = КорзинаНМА.Добавить();
			СтрокаВыбранныхНМА.НематериальныйАктив = ТекДанныеНМА.Ссылка;
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры



#КонецОбласти



#Область ОбработчикиКомандФормы



&НаКлиенте
Процедура ОСВключитьВСоставЕГДС(Команда)
	
	ВопросОПривязкеКЕГДС(КорзинаОС);

КонецПроцедуры


&НаКлиенте
Процедура НМАВключитьВСоставЕГДС(Команда)

	ВопросОПривязкеКЕГДС(КорзинаНМА);

КонецПроцедуры


&НаКлиенте
Процедура ВопросОПривязкеКЕГДС(ВыбранныеАктивы)

	Если ВыбранныеАктивы.Количество() = 0 Тогда
		ПоказатьПредупреждение(Неопределено, НСтр("ru = 'Объекты для включения в ЕГДС не выбраны.';
													|en = 'Objects to be included in the cash-generating unit are not selected.'"));
		Возврат;
	КонецЕсли;

	ТекстВопроса = НСтр("ru = 'Включить выбранные объекты в %1?';
						|en = 'Include selected objects in %1?'");
	ТекстВопросаСПараметром = СтрШаблон(ТекстВопроса, ЕГДС);

	ПараметрыПривязки = Новый Структура;
	ПараметрыПривязки.Вставить("ВыбранныеАктивы", ВыбранныеАктивы);

	ОтветНаВопрос = Новый ОписаниеОповещения("ПродолжитьПривязкуВНА", ЭтаФорма, ПараметрыПривязки);

	ПоказатьВопрос(ОтветНаВопрос, ТекстВопросаСПараметром, РежимДиалогаВопрос.ДаНет);

КонецПроцедуры


// Продолжить привязку ВНА.
//
// Параметры:
// РезультатВопроса - КодВозвратаДиалога - Результат вопроса
// ПараметрыПривязки - Структура - Параметры привязки:
// * ВыбранныеАктивы - ТаблицаЗначений - Таблица выбранных активов
&НаКлиенте
Процедура ПродолжитьПривязкуВНА(РезультатВопроса, ПараметрыПривязки) Экспорт

	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ОценкаПроизводительностиКлиент.ЗамерВремени(
	"Справочник.ЕдиницыГенерирующиеДенежныеСредства.Форма.ФормаВключенияВЕГДС.СозданиеДокументов");	

	ИзменитьПривязку(ПараметрыПривязки);

КонецПроцедуры


// Изменить привязку.
//
// Параметры:
// ПараметрыПривязки - Структура - Параметры привязки:
// * ВыбранныеАктивы - ДанныеФормыКоллекция -
&НаКлиенте
Процедура ИзменитьПривязку(ПараметрыПривязки)

	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("ВидОперации", "ИзменениеЕГДС");
	ЗначенияЗаполнения.Вставить("Дата", ПолучитьДатуСеансаНаСервере());
	ЗначенияЗаполнения.Вставить("Организация", Организация);
	ЗначенияЗаполнения.Вставить("ЕГДСФлаг", Истина);
	ЗначенияЗаполнения.Вставить("ЕГДС", ЕГДС);

	ПервыйАктив = ПараметрыПривязки.ВыбранныеАктивы.Получить(0);

	Если ПервыйАктив.Свойство("ОсновноеСредство") Тогда

		ЗначенияЗаполнения.Вставить("ОС", ПараметрыПривязки.ВыбранныеАктивы);

		ПараметрыОткрытия = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);

		ОткрытьФорму("Документ.ИзменениеПараметровОС2_4.Форма.ФормаДокумента", ПараметрыОткрытия, ЭтаФорма);

		Возврат;

	ИначеЕсли ПервыйАктив.Свойство("НематериальныйАктив") Тогда

		ЗначенияЗаполнения.Вставить("НМА", ПараметрыПривязки.ВыбранныеАктивы);

		ПараметрыОткрытия = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);

		ОткрытьФорму("Документ.ИзменениеПараметровНМА2_4.Форма.ФормаДокумента", ПараметрыОткрытия, ЭтаФорма);

		Возврат;

	КонецЕсли;

КонецПроцедуры


&НаСервере
Функция ПолучитьДатуСеансаНаСервере()

	Возврат ТекущаяДатаСеанса();
	
КонецФункции


#КонецОбласти



#Область СлужебныеПроцедурыИФункции



&НаСервере
Процедура УправлениеВидимостью()
	
	ИспользоватьНесколькоОрганизаций = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций");
	Элементы.Организация.Видимость = ИспользоватьНесколькоОрганизаций;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыДинамическихСписков()

	ОС.Параметры.УстановитьЗначениеПараметра("Организация", Организация);
	НМА.Параметры.УстановитьЗначениеПараметра("Организация", Организация);

КонецПроцедуры




#КонецОбласти
