
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Рассчитывает количество подтверждающих документов по указанной льготе.
// Отдельно считает количество действующих документов.
// 
// Параметры:
//  Льгота - СправочникСсылка.ОснованияЛьготПоИмущественнымНалогам
//  Организация - СправочникСсылка.Организации - организация, по которой нужно посчитать документы, подтверждающие льготы.
//  			 Если ОсновноеСредство не указано, то функция вернет общее количество документов по организации, включая те,
//  			 которые распространяются на конкретные объекты налогообложения данной организации. 
//  Период - дата - дата, на которую определяется действие льгот
//  ОсновноеСредство - СправочникСсылка.ОбъектыЭксплуатации - объект налогообложения, по которому нужно посчитать документы
// 
// Возвращаемое значение:
//  Структура - Состояние документов по льготе:
// * КоличествоВсего - Число - общее количество подтверждающих документов
// * КоличествоДействующихВсего - Число - количество действующих документов на переданную дату
Функция СостояниеДокументовПоЛьготе(Льгота, Организация, Период, ОсновноеСредство = Неопределено) Экспорт 

	СведенияОДокументах = Новый Структура;
	СведенияОДокументах.Вставить("КоличествоВсего", 0);
	СведенияОДокументах.Вставить("КоличествоДействующихВсего", 0);
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(СУММА(ТаблицаДокументы.КоличествоВсего), 0) КАК КоличествоВсего,
	|	ЕСТЬNULL(СУММА(ТаблицаДокументы.КоличествоДействующихВсего), 0) КАК КоличествоДействующихВсего
	|ИЗ
	|	(ВЫБРАТЬ
	|		1 КАК КоличествоВсего,
	|		ВЫБОР
	|			КОГДА ПодтверждающиеДокументы.СрокДействияОграничен
	|				ТОГДА ВЫБОР
	|					КОГДА &Период >= ПодтверждающиеДокументы.НачалоДействия
	|					И (ПодтверждающиеДокументы.ОкончаниеДействия = ДАТАВРЕМЯ(1, 1, 1)
	|					ИЛИ &Период <= ПодтверждающиеДокументы.ОкончаниеДействия)
	|						ТОГДА 1
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|			ИНАЧЕ 1
	|		КОНЕЦ КАК КоличествоДействующихВсего
	|	ИЗ
	|		Справочник.ДокументыПодтверждающиеЛьготыПоИмущественнымНалогам КАК ПодтверждающиеДокументы
	|	ГДЕ
	|		НЕ ПодтверждающиеДокументы.ПометкаУдаления
	|		И ПодтверждающиеДокументы.Владелец = &Льгота
	|		И ПодтверждающиеДокументы.Организация = &Организация
	|		И (НЕ &ЕстьОтборПоОбъекту ИЛИ НЕ ПодтверждающиеДокументы.ПрименяетсяКОтдельнымОбъектам)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		1,
	|		МАКСИМУМ(ВЫБОР
	|			КОГДА ПодтверждающиеДокументыОбъекты.Ссылка.СрокДействияОграничен
	|				ТОГДА ВЫБОР
	|					КОГДА &Период >= ПодтверждающиеДокументыОбъекты.Ссылка.НачалоДействия
	|					И (ПодтверждающиеДокументыОбъекты.Ссылка.ОкончаниеДействия = ДАТАВРЕМЯ(1, 1, 1)
	|					ИЛИ &Период <= ПодтверждающиеДокументыОбъекты.Ссылка.ОкончаниеДействия)
	|						ТОГДА 1
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|			ИНАЧЕ 1
	|		КОНЕЦ)
	|	ИЗ
	|		Справочник.ДокументыПодтверждающиеЛьготыПоИмущественнымНалогам.Объекты КАК ПодтверждающиеДокументыОбъекты
	|	ГДЕ
	|		&ЕстьОтборПоОбъекту
	|		И НЕ ПодтверждающиеДокументыОбъекты.Ссылка.ПометкаУдаления
	|		И ПодтверждающиеДокументыОбъекты.Ссылка.Владелец = &Льгота
	|		И ПодтверждающиеДокументыОбъекты.Ссылка.Организация = &Организация
	|		И ПодтверждающиеДокументыОбъекты.Ссылка.ПрименяетсяКОтдельнымОбъектам
	|		И ПодтверждающиеДокументыОбъекты.ОсновноеСредство = &ОсновноеСредство
	|	СГРУППИРОВАТЬ ПО
	|		ПодтверждающиеДокументыОбъекты.Ссылка) КАК ТаблицаДокументы";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Льгота", Льгота);
	Запрос.УстановитьПараметр("ЕстьОтборПоОбъекту", ЗначениеЗаполнено(ОсновноеСредство));
	Запрос.УстановитьПараметр("ОсновноеСредство", ОсновноеСредство);
	Запрос.УстановитьПараметр("Период", Период);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий(); // в выборке 1 запись

	ЗаполнитьЗначенияСвойств(СведенияОДокументах, Выборка);
	
	Возврат СведенияОДокументах;
	
КонецФункции

#Область Свойства

// Получает описание предопределенных наборов свойств.
//
// Параметры:
//  Наборы - ДеревоЗначений - с колонками:
//     * Имя           - Строка - Имя набора свойств. Формируется из полного имени объекта
//                       метаданных заменой символа "." на "_".
//                       Например, "Документ_ЗаказПокупателя".
//     * Идентификатор - УникальныйИдентификатор - Идентификатор ссылки предопределенного элемента.
//     * Используется  - Неопределено, Булево - Признак того, что набор свойств используется.
//                       Например, можно использовать для скрытия набора по функциональным опциям.
//                       Значение по умолчанию - Неопределено, соответствует значению Истина.
//     * ЭтоГруппа     - Булево - Истина, если набор свойств является группой.
//
Процедура ПриПолученииПредопределенныхНаборовСвойств(Наборы) Экспорт
	
	Набор = Наборы.Строки.Добавить();
	Набор.Имя = "Справочник_ДокументыПодтверждающиеЛьготыПоИмущественнымНалогам";
	Набор.Идентификатор = Новый УникальныйИдентификатор("ba87116c-eb6f-4f51-b09b-a60951ea5da4");
	Набор.Используется  = ПолучитьФункциональнуюОпцию("ИспользоватьВнеоборотныеАктивы2_4");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли