
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Ключ.Пустая() Тогда
		ПодготовитьФормуНаСервере();
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);	
	
	КлючСохраненияПоложенияОкна = ?(Объект.ПрименяетсяКОтдельнымОбъектам, "ВсяОрганизация", "ОтдельныеОбъекты");
	
	// СтандартныеПодсистемы.РаботаСФайлами
	ПараметрыГиперссылки = РаботаСФайлами.ГиперссылкаФайлов();
	ПараметрыГиперссылки.Размещение = "КоманднаяПанель";
	РаботаСФайлами.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыГиперссылки);
	// Конец СтандартныеПодсистемы.РаботаСФайлами
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
    // СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
    // Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	ПараметрыЗаписи = Новый Структура("Организация, Льгота", Объект.Организация, Объект.Владелец);
	Оповестить("Запись_ДокументыПодтверждающиеЛьготыПоИмущественнымНалогам", ПараметрыЗаписи);
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	КлючСохраненияПоложенияОкна = ?(Объект.ПрименяетсяКОтдельнымОбъектам, "ВсяОрганизация", "ОтдельныеОбъекты");
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ПолноеНаименованиеПриИзменении(Элемент)
	Объект.Наименование = Объект.ПолноеНаименование;
КонецПроцедуры

&НаКлиенте
Процедура СрокДействияОграниченПриИзменении(Элемент)
	Объект.СрокДействияОграничен = Булево(СрокДействияОграничен);
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбластьДействияПриИзменении(Элемент)
	ИзменитьОбластьДействия();
КонецПроцедуры

&НаКлиенте
Процедура ОбластьДействияНесколькоОбъектовПриИзменении(Элемент)
	ИзменитьОбластьДействия();
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ИзменитьОбластьДействия()
	Объект.ПрименяетсяКОтдельнымОбъектам = (ОбластьДействия <> 0);
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	ОбластьДействия = ?(Объект.ПрименяетсяКОтдельнымОбъектам, 1, 0);
	СрокДействияОграничен = ?(Объект.СрокДействияОграничен, 1, 0);
	
	УстановитьЗаголовок();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Объект   = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Элементы.Объекты.Видимость = Объект.ПрименяетсяКОтдельнымОбъектам;
	
	Элементы.НачалоДействия.Доступность = Объект.СрокДействияОграничен;
	Элементы.НачалоДействия.АвтоОтметкаНезаполненного = Объект.СрокДействияОграничен;
	Элементы.ОкончаниеДействия.Доступность = Объект.СрокДействияОграничен;
	Элементы.ОкончаниеДействия.АвтоОтметкаНезаполненного = Объект.СрокДействияОграничен;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовок()

	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		АвтоЗаголовок = Истина;		
	Иначе
		АвтоЗаголовок = Ложь;
		Заголовок = Нстр("ru = 'Документ, подтверждающий льготу по налогу на имущество';
						|en = 'The document that confirms the property tax relief'");
	КонецЕсли;	

КонецПроцедуры

#КонецОбласти
