
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнениеДокументов.ЗаполнитьПоСтруктуре(ЭтотОбъект, ДанныеЗаполнения);
		Если ДанныеЗаполнения.Свойство("ОсновноеСредство") Тогда
			ПрименяетсяКОтдельнымОбъектам = Истина;
			СтрокаОбъекта = Объекты.Добавить();
			СтрокаОбъекта.ОсновноеСредство = ДанныеЗаполнения.ОсновноеСредство;
		КонецЕсли;	
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Организация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если Не СрокДействияОграничен Тогда
		МассивНепроверяемыхРеквизитов.Добавить("НачалоДействия");
		МассивНепроверяемыхРеквизитов.Добавить("ОкончаниеДействия");
	КонецЕсли;	
	
	Если Не ПрименяетсяКОтдельнымОбъектам Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Объекты");
		МассивНепроверяемыхРеквизитов.Добавить("Объекты.ОсновноеСредство");
	КонецЕсли;	
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Наименование) Тогда
		Наименование = ПолноеНаименование;
	КонецЕсли; 
	
	Если Не ПрименяетсяКОтдельнымОбъектам Тогда
		Объекты.Очистить();
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
