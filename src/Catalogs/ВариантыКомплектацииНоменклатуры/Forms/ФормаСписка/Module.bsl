
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УстановитьУсловноеОформление();

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	// Варианты комплектации определяются только для товаров.
	ВладелецВариантов = Неопределено;
	Если Параметры.Свойство("Отбор") И Параметры.Отбор.Свойство("Владелец", ВладелецВариантов)
		И ЗначениеЗаполнено(ВладелецВариантов) Тогда

		ТипыНоменклатуры = Перечисления.ТипыНоменклатуры;
		ТипВладельца = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВладелецВариантов, "ТипНоменклатуры");
		Если ТипВладельца <> ТипыНоменклатуры.Товар
			И ТипВладельца <> ТипыНоменклатуры.МногооборотнаяТара Тогда
			АвтоЗаголовок = Ложь;
			Заголовок = НСтр("ru = 'Варианты комплектации используются только для товаров';
							|en = 'Kit configurations are used for goods only'");
			Элементы.Список.ТолькоПросмотр = Истина;
		КонецЕсли;
		
		Элементы.Владелец.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.Список.РежимВыбора = (Истина = Параметры.РежимВыбора);
	Элементы.Список.МножественныйВыбор = (Истина = Параметры.МножественныйВыбор);

	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Элементы.Список);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Элементы.Список);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	//

	НоменклатураСервер.УстановитьУсловноеОформлениеЕдиницИзмерения(ЭтаФорма, 
																   "ВладелецЕдиницаИзмерения", 
                                                                   "Список.Упаковка");

КонецПроцедуры

#КонецОбласти
