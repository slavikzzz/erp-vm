
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	СвойстваДоверенности   = Параметры.СвойстваДоверенности;
	ЭтоПодключениеИзПанели = Параметры.ЭтоПодключениеИзПанели;
	
	МассивКодовНалоговыхОрганов = ДокументооборотСКОВызовСервера.КодыНалоговыхОргановМЧДФНС(
		СвойстваДоверенности.СсылкаМЧДФНС);
	КоличествоНалоговыхОрганов = МассивКодовНалоговыхОрганов.Количество();
	ФИОПредставителя = ДокументооборотСКОВызовСервера.ФИОМЧДФНС(СвойстваДоверенности.СсылкаМЧДФНС,
		Перечисления.СубъектыДоверенностиНалогоплательщика.ПредставительФЛ, СвойстваДоверенности);
	Если СвойстваДоверенности.КоличествоПредставителей > 1 Тогда
		ФИОПредставителя = ФИОПредставителя + " " + СтрШаблон(
			НСтр("ru = 'и еще %1';
				|en = 'и еще %1'"),
			Строка(СвойстваДоверенности.КоличествоПредставителей - 1));
	КонецЕсли;
	
	Если КоличествоНалоговыхОрганов >= 10 Тогда
		КоличествоНалоговыхОргановТекстом = ОбщегоНазначенияЭДКОКлиентСервер.ПростоеСклонение(
			КоличествоНалоговыхОрганов,
			НСтр("ru = '%1 инспекция';
				|en = '%1 инспекция'"),
			НСтр("ru = '%1 инспекции';
				|en = '%1 инспекции'"),
			НСтр("ru = '%1 инспекций';
				|en = '%1 инспекций'"));
		КоличествоНалоговыхОргановТекстом = СтрШаблон(
			КоличествоНалоговыхОргановТекстом,
			Формат(КоличествоНалоговыхОрганов, "ЧДЦ=; ЧН="));
	КонецЕсли;
	
	МаксимальноеКоличествоСимволовЗначения = 35;
	Элементы.ЗначенияРеквизитовДоверенности.Заголовок =
		?(СтрДлина(СвойстваДоверенности.НаименованиеОрганизации) > МаксимальноеКоличествоСимволовЗначения,
			Лев(СвойстваДоверенности.НаименованиеОрганизации, МаксимальноеКоличествоСимволовЗначения - 3) + "...",
			СвойстваДоверенности.НаименованиеОрганизации) + "
		|" + НСтр("ru = 'с';
					|en = 'с'") + " "
			+ Формат(СвойстваДоверенности.ДатаВыдачи, "ДФ='дд.ММ.гггг'")
			+ ?(ЗначениеЗаполнено(СвойстваДоверенности.ДатаОкончания),
				" " + НСтр("ru = 'по';
							|en = 'по'") + " " + Формат(СвойстваДоверенности.ДатаОкончания, "ДФ='дд.ММ.гггг'"), "")
			+ ?(НЕ ЗначениеЗаполнено(СвойстваДоверенности.ДатаОкончания)
				И ЗначениеЗаполнено(СвойстваДоверенности.СрокДействия), " " + СвойстваДоверенности.СрокДействия, "") + "
		|" + ?(СтрДлина(ФИОПредставителя) > МаксимальноеКоличествоСимволовЗначения,
			Лев(ФИОПредставителя, МаксимальноеКоличествоСимволовЗначения - 3) + "...", ФИОПредставителя) + "
		|" + ?(КоличествоНалоговыхОрганов = 0, НСтр("ru = 'Все';
													|en = 'Все'"),
			?(ЗначениеЗаполнено(КоличествоНалоговыхОргановТекстом), КоличествоНалоговыхОргановТекстом,
			СтрСоединить(МассивКодовНалоговыхОрганов, ", ")));
	
	Элементы.РегистрацииВНалоговомОрганеКПП.Видимость = РегламентированнаяОтчетностьВызовСервера.ЭтоЮридическоеЛицо(
		СвойстваДоверенности.ОрганизацияСсылка);
	Если НЕ СвойстваДоверенности.ДоверенностьМожноПодставитьВРегистрацию Тогда
		Элементы.ГруппаКнопкиТаблицы.Видимость 					= Ложь;
		Элементы.РегистрацииВНалоговомОрганеПометка.Видимость 	= Ложь;
		Элементы.ФормаДа.Видимость 								= Ложь;
		Элементы.ФормаНет.Заголовок 							= НСтр("ru = 'Закрыть';
																		|en = 'Закрыть'");
		Элементы.ФормаНет.КнопкаПоУмолчанию 					= Истина;
	КонецЕсли;
	ОбновитьРегистрацииВНалоговомОргане(СвойстваДоверенности.РегистрацииВНалоговомОргане);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДекорацияСтатусДоверенностиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "доверенность" Тогда
		СтандартнаяОбработка = Ложь;
		
		ПоказатьЗначение(, СвойстваДоверенности.СсылкаМЧДФНС);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РегистрацииВНалоговомОрганеПриИзменении(Элемент)
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура РегистрацииВНалоговомОрганеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ВыбраннаяСтрока = Неопределено ИЛИ ВыбраннаяСтрока < 0
		ИЛИ ВыбраннаяСтрока >= РегистрацииВНалоговомОргане.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("РегистрацииВНалоговомОрганеВыборПослеВыбора", ЭтотОбъект);
	ПараметрыФормы = Новый Структура("Ключ", РегистрацииВНалоговомОргане[ВыбраннаяСтрока].РегистрацияСсылка);
	ОткрытьФорму(
		"Справочник.РегистрацииВНалоговомОргане.ФормаОбъекта",
		ПараметрыФормы,
		ЭтаФорма,,,,
		Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	Для каждого Стр Из РегистрацииВНалоговомОргане Цикл
		Стр.Пометка = Истина;
	КонецЦикла;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	Для каждого Стр Из РегистрацииВНалоговомОргане Цикл
		Стр.Пометка = Ложь;
	КонецЦикла;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьРегистрациюВНалоговомОргане(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ДобавитьРегистрациюВНалоговомОрганеПослеДобавления", ЭтотОбъект);
	ПараметрыЗаполнения = Новый Структура("Владелец", СвойстваДоверенности.ОрганизацияСсылка);
	ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ПараметрыЗаполнения);
	ОткрытьФорму(
		"Справочник.РегистрацииВНалоговомОргане.ФормаОбъекта",
		ПараметрыФормы,
		ЭтаФорма,,,,
		Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИзменения(Команда)
	
	ПодключениеВыполнено = ЭтоПодключениеИзПанели И (Элементы.ФормаДа.Видимость И НЕ Элементы.ФормаНет.Видимость);
	
	Если ПодключениеВыполнено Тогда
		Успешно = Истина;
		
	Иначе
		Успешно = ПодключитьМЧДФНС(СвойстваДоверенности);
		Если Успешно Тогда
			Оповестить("ПодключениеМЧДФНС",, СвойстваДоверенности.СсылкаМЧДФНС);
		КонецЕсли;
	КонецЕсли;
	
	Если Успешно Тогда
		НоваяИгнорируемаяЗарегистрированнаяМЧДФНС = СвойстваДоверенности.СсылкаМЧДФНС;
		ДокументооборотСКОВызовСервера.ЗарегистрированныеМЧДФНС(НоваяИгнорируемаяЗарегистрированнаяМЧДФНС);
		
		Если ЭтоПодключениеИзПанели И НЕ ПодключениеВыполнено Тогда
			Элементы.ГруппаКнопкиТаблицы.Видимость 					= Ложь;
			Элементы.РегистрацииВНалоговомОрганеПометка.Видимость 	= Ложь;
			Элементы.ФормаДа.Заголовок 								= НСтр("ru = 'Закрыть';
																				|en = 'Закрыть'");
			Элементы.ФормаДа.КнопкаПоУмолчанию 						= Истина;
			Элементы.ФормаНет.Видимость 							= Ложь;
			
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Успешно!';
															|en = 'Успешно!'"));
			
		Иначе
			Закрыть(Истина);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьИзменения(Команда)
	
	НоваяИгнорируемаяЗарегистрированнаяМЧДФНС = СвойстваДоверенности.СсылкаМЧДФНС;
	ДокументооборотСКОВызовСервера.ЗарегистрированныеМЧДФНС(НоваяИгнорируемаяЗарегистрированнаяМЧДФНС);
	Закрыть(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	КоличествоПомеченных = 0;
	Для каждого Стр Из Форма.РегистрацииВНалоговомОргане Цикл
		Если Стр.Пометка Тогда
			КоличествоПомеченных = КоличествоПомеченных + 1;
		КонецЕсли;
	КонецЦикла;
	
	КоличествоПомеченныхТекстом = ОбщегоНазначенияЭДКОКлиентСервер.ПростоеСклонение(
		КоличествоПомеченных,
		НСтр("ru = 'Выбрана %1';
			|en = 'Выбрана %1'"),
		НСтр("ru = 'Выбрано %1';
			|en = 'Выбрано %1'"),
		НСтр("ru = 'Выбрано %1';
			|en = 'Выбрано %1'"));
	
	КоличествоВсего = Форма.РегистрацииВНалоговомОргане.Количество();
	КоличествоВсегоТекстом = ОбщегоНазначенияЭДКОКлиентСервер.ПростоеСклонение(
		КоличествоВсего,
		НСтр("ru = '%2 инспекции';
			|en = '%2 инспекции'"),
		НСтр("ru = '%2 инспекций';
			|en = '%2 инспекций'"),
		НСтр("ru = '%2 инспекций';
			|en = '%2 инспекций'"));
	
	Форма.Элементы.ИтогПоТаблице.Заголовок = СтрШаблон(
		КоличествоПомеченныхТекстом + " " + НСтр("ru = 'из';
												|en = 'из'") + " "  + КоличествоВсегоТекстом,
		КоличествоПомеченных,
		КоличествоВсего);
	Форма.Элементы.ФормаДа.Доступность = КоличествоПомеченных <> 0;
	
КонецПроцедуры

&НаКлиенте
Процедура РегистрацииВНалоговомОрганеВыборПослеВыбора(Результат, ВыполняемоеОповещение) Экспорт
	
	ОбновитьРегистрацииВНалоговомОргане();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьРегистрациюВНалоговомОрганеПослеДобавления(Результат, ВыполняемоеОповещение) Экспорт
	
	ОбновитьРегистрацииВНалоговомОргане();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьРегистрацииВНалоговомОргане(МассивРегистрацийВНалоговомОргане = Неопределено)
	
	Если МассивРегистрацийВНалоговомОргане = Неопределено Тогда
		ТаблицаИлиМассивРегистраций = ДокументооборотСКОВызовСервера.РегистрацииВНалоговомОргане(
			СвойстваДоверенности.ОрганизацияСсылка);
	Иначе
		ТаблицаИлиМассивРегистраций = МассивРегистрацийВНалоговомОргане;
	КонецЕсли;
	
	МассивКодовНалоговыхОрганов = ДокументооборотСКОВызовСервера.КодыНалоговыхОргановМЧДФНС(
		СвойстваДоверенности.СсылкаМЧДФНС);
	
	НайденныеРегистрации = Новый Массив;
	Для каждого СвойстваРегистрации Из ТаблицаИлиМассивРегистраций Цикл
		Если МассивКодовНалоговыхОрганов.Количество() = 0
			ИЛИ МассивКодовНалоговыхОрганов.Найти(СвойстваРегистрации.КодРегистрации) <> Неопределено Тогда
			
			ПараметрыОтбора = Новый Структура("РегистрацияСсылка", СвойстваРегистрации.РегистрацияСсылка);
			МассивРегистраций = РегистрацииВНалоговомОргане.НайтиСтроки(ПараметрыОтбора);
			Если МассивРегистраций.Количество() = 0 Тогда
				РегистрацияВНалоговомОргане = РегистрацииВНалоговомОргане.Добавить();
				РегистрацияВНалоговомОргане.Пометка = Истина;
			Иначе
				РегистрацияВНалоговомОргане = МассивРегистраций[0];
			КонецЕсли;
			
			РегистрацияВНалоговомОргане.КодНалоговогоОргана = СвойстваРегистрации.КодРегистрации;
			РегистрацияВНалоговомОргане.КПП 				= СвойстваРегистрации.КППРегистрации;
			РегистрацияВНалоговомОргане.РегистрацияСсылка 	= СвойстваРегистрации.РегистрацияСсылка;
			НайденныеРегистрации.Добавить(СвойстваРегистрации.РегистрацияСсылка);
		КонецЕсли;
	КонецЦикла;
	
	КоличествоРегистраций = РегистрацииВНалоговомОргане.Количество();
	Для ИндексСКонца = 0 По КоличествоРегистраций - 1 Цикл
		ИндексРегистрации = КоличествоРегистраций - ИндексСКонца - 1;
		Если НайденныеРегистрации.Найти(РегистрацииВНалоговомОргане[ИндексРегистрации].РегистрацияСсылка) = Неопределено Тогда
			РегистрацииВНалоговомОргане.Удалить(ИндексРегистрации);
		КонецЕсли;
	КонецЦикла;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Функция ПодключитьМЧДФНС(СвойстваДоверенности)
	
	Результат = Ложь;
	
	КоличествоРегистраций = РегистрацииВНалоговомОргане.Количество();
	
	Для каждого СвойстваРегистрации Из РегистрацииВНалоговомОргане Цикл
		Если СвойстваРегистрации.Пометка Тогда
			
			Если СвойстваРегистрации.РегистрацияСсылка.Подписанты.Количество() > 0 Тогда
				
				Если ТипЗнч(СвойстваДоверенности.Представитель) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
					
					Пользователь = Мультирежим.ПользовательПоФизЛицуИзСправочникаПользователи(СвойстваДоверенности.Представитель);
					
					Если ЗначениеЗаполнено(Пользователь) Тогда
						
						ОбъектРегистрации = СвойстваРегистрации.РегистрацияСсылка.ПолучитьОбъект();
						ОбъектРегистрации.Прочитать();

						Отбор = Новый Структура();
						Отбор.Вставить("Пользователь", Пользователь);
						
						НайденныеСтроки = ОбъектРегистрации.Подписанты.НайтиСтроки(Отбор);
						Если НайденныеСтроки.Количество() > 0 Тогда
							НоваяСтрока = НайденныеСтроки[0];
						Иначе
							НоваяСтрока = ОбъектРегистрации.Подписанты.Добавить();
							НоваяСтрока.Пользователь = Пользователь;
						КонецЕсли;
						
						ЗаполнитьПредставителяВРегистрации(НоваяСтрока, СвойстваДоверенности);
						
						ОбъектРегистрации.Записать();
						
						СвойстваРегистрации.Пометка = Ложь;
						
						Результат = Истина;
						
					Иначе
						
						ТекстОшибки = НСтр("ru = 'Не удалось сопоставить доверенность регистрации %1, поскольку физ. лицо %2 не найдено в справочнике Пользователи.
                                            |Для сопоставления:
                                            |1. Зайдите в справочник Пользователи.
                                            |2. Откройте карточку пользователя, которому соответствует физ. лицо %2.
                                            |3. В поле ""Физическое лицо"" этого пользователя укажите физ. лицо %2.
                                            |4. Повторите операцию сопоставления для регистрации %1.';
                                            |en = 'Не удалось сопоставить доверенность регистрации %1, поскольку физ. лицо %2 не найдено в справочнике Пользователи.
                                            |Для сопоставления:
                                            |1. Зайдите в справочник Пользователи.
                                            |2. Откройте карточку пользователя, которому соответствует физ. лицо %2.
                                            |3. В поле ""Физическое лицо"" этого пользователя укажите физ. лицо %2.
                                            |4. Повторите операцию сопоставления для регистрации %1.'");
						
						ТекстОшибки = СтрШаблон(
							ТекстОшибки, 
							СвойстваРегистрации.РегистрацияСсылка, 
							СвойстваДоверенности.Представитель);
						
						ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
						
					КонецЕсли;
					
				Иначе
					
					ТекстОшибки = НСтр("ru = 'Не удалось сопоставить доверенность регистрации %1, поскольку невозможно определить, какой пользователь программы будет подписывать отчетность от лица %2. Зайдите в регистрацию %1 и добавьте представителя вручную';
										|en = 'Не удалось сопоставить доверенность регистрации %1, поскольку невозможно определить, какой пользователь программы будет подписывать отчетность от лица %2. Зайдите в регистрацию %1 и добавьте представителя вручную'");
						
					ТекстОшибки = СтрШаблон(
						ТекстОшибки, 
						СвойстваРегистрации.РегистрацияСсылка, 
						СвойстваДоверенности.Представитель);
					
					ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
					
					СвойстваРегистрации.Пометка = Ложь;
					
				КонецЕсли;
				
			Иначе
				
				ОбъектРегистрации = СвойстваРегистрации.РегистрацияСсылка.ПолучитьОбъект();
				ОбъектРегистрации.Прочитать();
				
				ЗаполнитьПредставителяВРегистрации(ОбъектРегистрации, СвойстваДоверенности);
				
				ОбъектРегистрации.Записать();
				
				СвойстваРегистрации.Пометка = Ложь;
				
				Результат = Истина;
				
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьПредставителяВРегистрации(Приемник, СвойстваДоверенности)
	
	ЭтоВерсия5_02 = (СвойстваДоверенности.ВерсияФормата = "5.02");
	
	Если ЭтоВерсия5_02 Тогда
		Приемник.ДокументПредставителя = СвойстваДоверенности.РегистрационныйНомерДоверенности;
		
	Иначе
		Приемник.ДокументПредставителя = НСтр("ru = 'Машиночитаемая доверенность';
												|en = 'Машиночитаемая доверенность'")
			+ ?(ЗначениеЗаполнено(СвойстваДоверенности.НомерДоверенности),
			" " + НСтр("ru = '№';
						|en = '№'") + СвойстваДоверенности.НомерДоверенности, "")
			+ ?(ЗначениеЗаполнено(СвойстваДоверенности.ДатаВыдачи), " " + НСтр("ru = 'от';
																				|en = 'от'") + " "
			+ Формат(СвойстваДоверенности.ДатаВыдачи, "ДФ='дд.ММ.гггг'") + " " + НСтр("ru = 'г.';
																						|en = 'г.'"), "");
	КонецЕсли;
	
	Приемник.Представитель = СвойстваДоверенности.Представитель;
	Приемник.УполномоченноеЛицоПредставителя = СвойстваДоверенности.ФИОПредставителя;
	Приемник.Доверенность = СвойстваДоверенности.СсылкаМЧДФНС;
	
КонецПроцедуры

#КонецОбласти
