
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОргПоУмолчанию = РегламентированнаяОтчетность.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	
	Если ЗначениеЗаполнено(ОргПоУмолчанию)
		И ?(КонтекстЭДОСервер = Неопределено, Истина,
		КонтекстЭДОСервер.СписокДопустимыхОрганизацийВОбъектахОбмена().Найти(ОргПоУмолчанию) <> Неопределено) Тогда
		
		Организация = ОргПоУмолчанию;
		СвойстваДоверителя = СвойстваОрганизации(Организация);
		Доверитель_ЮридическоеЛицо 	= СвойстваДоверителя.ЭтоЮридическоеЛицо;
		ИННДоверителя 				= СвойстваДоверителя.ИНН;
		
	Иначе
		Доверитель_ЮридическоеЛицо = Истина;
	КонецЕсли;
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("СправочникСсылка.Контрагенты"));
	МассивТипов.Добавить(Тип("СправочникСсылка.Организации"));
	МассивТипов.Добавить(Тип("СправочникСсылка.ФизическиеЛица"));
	Элементы.Представитель.ОграничениеТипа = Новый ОписаниеТипов(МассивТипов);
	
	ПредставительЯвляетсяСотрудником = Истина;
	
	УправлениеФормой(ЭтаФорма);
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.СкрытьЭлементыФормыПриИспользованииОднойОрганизации(
		ЭтаФорма, "ГруппаДоверитель");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	СвойстваДоверителя = СвойстваОрганизации(Организация);
	Доверитель_ЮридическоеЛицо 	= СвойстваДоверителя.ЭтоЮридическоеЛицо;
	ИННДоверителя 				= СвойстваДоверителя.ИНН;
	
	ПредставительЗаполнен = ЗначениеЗаполнено(Представитель)
		И Представитель <> ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка")
		И Представитель <> ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка")
		И Представитель <> ПредопределенноеЗначение("Справочник.ФизическиеЛица.ПустаяСсылка");
	
	УправлениеФормой(ЭтаФорма, ПредставительЗаполнен);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставительЯвляетсяСотрудникомПриИзменении(Элемент)
	
	ПредставительЗаполнен = ЗначениеЗаполнено(Представитель)
		И Представитель <> ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка")
		И Представитель <> ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка")
		И Представитель <> ПредопределенноеЗначение("Справочник.ФизическиеЛица.ПустаяСсылка");
	
	УправлениеФормой(ЭтаФорма, ПредставительЗаполнен);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставительПриИзменении(Элемент)
	
	Если ТипЗнч(Представитель) = Тип("СправочникСсылка.Организации")
		И Представитель <> ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка") Тогда
		
		СвойстваОрганизации = ДокументооборотСКОВызовСервера.ПолучитьСвойстваОрганизации(Представитель);
		
		Представитель_ЮридическоеЛицо = СвойстваОрганизации.ЭтоЮридическоеЛицо;
		ПредставительЮЛ_ИНН = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо, СвойстваОрганизации.ИННЮЛ, "");
		ПредставительФЛ_ИНН = ?(СвойстваОрганизации.ЭтоЮридическоеЛицо, СвойстваОрганизации.ИННРук,
			СвойстваОрганизации.ИННФЛ);
		
		УправлениеФормой(ЭтаФорма, Истина);
		
	ИначеЕсли ТипЗнч(Представитель) = Тип("СправочникСсылка.Контрагенты")
		И Представитель <> ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка") Тогда
		
		СвойстваКонтрагента = ДокументооборотСКОВызовСервера.ПолучитьСвойстваКонтрагента(Представитель);
		
		Если СвойстваКонтрагента.ЭтоЮридическоеЛицо = Истина Тогда
			Представитель_ЮридическоеЛицо = Истина;
			ПредставительЮЛ_ИНН = СвойстваКонтрагента.ИНН;
			
			УправлениеФормой(ЭтаФорма, Истина);
			
		ИначеЕсли СвойстваКонтрагента.ЭтоЮридическоеЛицо = Ложь Тогда
			Представитель_ЮридическоеЛицо = Ложь;
			ПредставительФЛ_ИНН = СвойстваКонтрагента.ИНН;
			
			УправлениеФормой(ЭтаФорма, Истина);
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Представитель) = Тип("СправочникСсылка.ФизическиеЛица")
		И Представитель <> ПредопределенноеЗначение("Справочник.ФизическиеЛица.ПустаяСсылка") Тогда
		
		СвойстваФизическогоЛица = ДокументооборотСКОВызовСервера.ПолучитьСвойстваФизическогоЛица(Представитель);
		
		Представитель_ЮридическоеЛицо = Ложь;
		ПредставительФЛ_ИНН = СвойстваФизическогоЛица.ИНН;
		
		УправлениеФормой(ЭтаФорма, Истина);
		
	Иначе
		ПредставительЮЛ_ИНН = "";
		ПредставительФЛ_ИНН = "";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Загрузить(Команда)
	
	Если НЕ СохранениеВозможно() Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("НомерДоверенности", 	НомерДоверенности);
	СтруктураДанных.Вставить("Организация", 		Организация);
	СтруктураДанных.Вставить("ИННДоверителя", 		ИННДоверителя);
	СтруктураДанных.Вставить("ИННПредставителя", 	ИННПредставителя);
	
	Закрыть(СтруктураДанных);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма, ОбновитьИННПредставителя = Ложь)
	
	Форма.Элементы.ПредставительЯвляетсяСотрудником.Видимость = НЕ Форма.Представитель_ЮридическоеЛицо;
	
	Если ОбновитьИННПредставителя Тогда
		Форма.ИННПредставителя = ?(Форма.Доверитель_ЮридическоеЛицо
			И НЕ Форма.Представитель_ЮридическоеЛицо И Форма.ПредставительЯвляетсяСотрудником, Форма.ИННДоверителя,
			?(Форма.Представитель_ЮридическоеЛицо, Форма.ПредставительЮЛ_ИНН, Форма.ПредставительФЛ_ИНН));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция СохранениеВозможно()
	
	Отказ = Ложь;
	
	ОчиститьСообщения();
	
	Если НЕ ЗначениеЗаполнено(НомерДоверенности) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не задан номер доверенности.';
														|en = 'Не задан номер доверенности.'"),,
			"НомерДоверенности",, Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ИННДоверителя) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не задан ИНН доверителя.';
														|en = 'Не задан ИНН доверителя.'"),,
			"ИННДоверителя",, Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ИННПредставителя) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не задан ИНН представителя.';
														|en = 'Не задан ИНН представителя.'"),,
			"ИННПредставителя",, Отказ);
	КонецЕсли;
	
	Возврат НЕ Отказ;
	
КонецФункции

&НаСервереБезКонтекста
Функция СвойстваОрганизации(Организация)
	
	Результат = Новый Структура;
	Результат.Вставить("ЭтоЮридическоеЛицо", 	Истина);
	Результат.Вставить("ИНН", 					"");
	
	Если ЗначениеЗаполнено(Организация) Тогда
		Результат.ЭтоЮридическоеЛицо = РегламентированнаяОтчетностьВызовСервера.ЭтоЮридическоеЛицо(Организация);
		Если Результат.ЭтоЮридическоеЛицо Тогда
			Результат.ИНН = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Организация,, "ИННЮЛ").ИННЮЛ;
			
		Иначе
			Результат.ИНН = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Организация,, "ИННФЛ").ИННФЛ;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
