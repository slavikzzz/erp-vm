#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если НЕ ЭтоГруппа Тогда
		
		ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
		
		ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Сборка;
		ВариантНазначения = Перечисления.ВариантыНазначенияСпецификации.СписокНоменклатуры;
		//++ НЕ УТКА
		Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеПроизводством") Тогда
			ВариантНазначения = Перечисления.ВариантыНазначенияСпецификации.Номенклатура;
		КонецЕсли;
		//-- НЕ УТКА
		
		Если ТипДанныхЗаполнения = Тип("СправочникСсылка.Номенклатура") Тогда
			
			СтруктураРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, "ЭтоГруппа");
			Если СтруктураРеквизитов.ЭтоГруппа = Ложь Тогда
				
				НоваяСтрока = ВыходныеИзделия.Добавить();
				НоваяСтрока.КлючСвязи          = Новый УникальныйИдентификатор;
				НоваяСтрока.Номенклатура       = ДанныеЗаполнения;
				НоваяСтрока.КоличествоУпаковок = 1;
				УправлениеДаннымиОбИзделияхКлиентСервер.ПриВводеНовойСтрокиСАвтовыбором(НоваяСтрока);
				
				ОсновноеИзделиеНоменклатура       = ДанныеЗаполнения;
				ОсновноеИзделиеКоличествоУпаковок = 1;
			
			КонецЕсли;
			
		ИначеЕсли ТипДанныхЗаполнения = Тип("Структура") Тогда
			
			Если ДанныеЗаполнения.Свойство("ТипПроизводственногоПроцесса") Тогда
				Если ТипЗнч(ДанныеЗаполнения.ТипПроизводственногоПроцесса) = Тип("Массив") 
					ИЛИ ТипЗнч(ДанныеЗаполнения.ТипПроизводственногоПроцесса) = Тип("ФиксированныйМассив") Тогда
					ТипПроизводственногоПроцесса = ДанныеЗаполнения.ТипПроизводственногоПроцесса[0];
				Иначе
					ТипПроизводственногоПроцесса = ДанныеЗаполнения.ТипПроизводственногоПроцесса;
				КонецЕсли;
			КонецЕсли;
			
			Если ДанныеЗаполнения.Свойство("Номенклатура") Тогда
				
				Если ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Сборка
					ИЛИ ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Ремонт
					ИЛИ Не ЗначениеЗаполнено(ТипПроизводственногоПроцесса) Тогда
				
					НоваяСтрока = ВыходныеИзделия.Добавить();
					НоваяСтрока.КлючСвязи          = Новый УникальныйИдентификатор;
					НоваяСтрока.Номенклатура       = ДанныеЗаполнения.Номенклатура;
					НоваяСтрока.Характеристика     = ДанныеЗаполнения.Характеристика;
					НоваяСтрока.КоличествоУпаковок = 1;
					УправлениеДаннымиОбИзделияхКлиентСервер.ПриВводеНовойСтрокиСАвтовыбором(НоваяСтрока);
					
				КонецЕсли;
				
				ОсновноеИзделиеНоменклатура       = ДанныеЗаполнения.Номенклатура;
				ОсновноеИзделиеХарактеристика     = ДанныеЗаполнения.Характеристика;
				ОсновноеИзделиеКоличествоУпаковок = 1;
				
			КонецЕсли;
			
		КонецЕсли;
		
		ПроверитьЗаполнитьВидНоменклатуры();
		
		Ответственный = Пользователи.ТекущийПользователь();
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеПроизводственногоПроцесса = Неопределено;
	Если НЕ ДополнительныеСвойства.Свойство("ОписаниеПроизводственногоПроцесса", ОписаниеПроизводственногоПроцесса) Тогда
		ОписаниеПроизводственногоПроцесса = Справочники.РесурсныеСпецификации.ОписаниеПроизводственногоПроцесса(Ссылка);
	КонецЕсли;
	
	// Дата окончания действия должна быть не меньше даты начала.
	Если ЗначениеЗаполнено(НачалоДействия) 
		И ЗначениеЗаполнено(КонецДействия) 
		И НачалоДействия > КонецДействия Тогда
		
		ТекстОшибки = НСтр("ru = 'Дата окончания действия должна быть не меньше даты начала действия.';
							|en = 'Action expiration date must be greater than the effective date.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ЭтотОбъект, "КонецДействия",, Отказ);
		
	КонецЕсли;
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
#Область ВыходныеИзделия
	МассивНепроверяемыхРеквизитов.Добавить("ВыходныеИзделия");
	МассивНепроверяемыхРеквизитов.Добавить("ВыходныеИзделия.ВидНоменклатуры");
	МассивНепроверяемыхРеквизитов.Добавить("ВыходныеИзделия.Номенклатура");
	МассивНепроверяемыхРеквизитов.Добавить("ВыходныеИзделия.КоличествоУпаковок");
	МассивНепроверяемыхРеквизитов.Добавить("ВыходныеИзделия.Характеристика");
	МассивНепроверяемыхРеквизитов.Добавить("ВыходныеИзделия.ДоляСтоимости");
	МассивНепроверяемыхРеквизитов.Добавить("ВыходныеИзделия.Спецификация");
#КонецОбласти
	
#Область ВозвратныеОтходы
	МассивНепроверяемыхРеквизитов.Добавить("ВозвратныеОтходы");
	МассивНепроверяемыхРеквизитов.Добавить("ВозвратныеОтходы.Номенклатура");
	МассивНепроверяемыхРеквизитов.Добавить("ВозвратныеОтходы.КоличествоУпаковок");
	МассивНепроверяемыхРеквизитов.Добавить("ВозвратныеОтходы.СтатьяКалькуляции");
	МассивНепроверяемыхРеквизитов.Добавить("ВозвратныеОтходы.Спецификация");
	МассивНепроверяемыхРеквизитов.Добавить("ВозвратныеОтходы.Характеристика");
#КонецОбласти
	
#Область ВходящееИзделие
//++ НЕ УТКА
	Если ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Сборка
		ИЛИ Статус <> Перечисления.СтатусыСпецификаций.Действует Тогда
//-- НЕ УТКА
		МассивНепроверяемыхРеквизитов.Добавить("ОсновноеИзделиеВидНоменклатуры");
		МассивНепроверяемыхРеквизитов.Добавить("ОсновноеИзделиеНоменклатура");
		МассивНепроверяемыхРеквизитов.Добавить("ОсновноеИзделиеКоличествоУпаковок");
//++ НЕ УТКА
	ИначеЕсли ВариантНазначения = Перечисления.ВариантыНазначенияСпецификации.ВидНоменклатуры Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ОсновноеИзделиеНоменклатура");
	Иначе
		МассивНепроверяемыхРеквизитов.Добавить("ОсновноеИзделиеВидНоменклатуры");
	КонецЕсли;
	ПроверитьЗаполнениеВходящихИзделий(Отказ);
//-- НЕ УТКА
#КонецОбласти
	
#Область Материалы
	МассивНепроверяемыхРеквизитов.Добавить("МатериалыИУслуги.Номенклатура");
	МассивНепроверяемыхРеквизитов.Добавить("МатериалыИУслуги.СпособПолученияМатериала");
	МассивНепроверяемыхРеквизитов.Добавить("МатериалыИУслуги.ИсточникПолученияПолуфабриката");
	МассивНепроверяемыхРеквизитов.Добавить("МатериалыИУслуги.КоличествоУпаковок");
	МассивНепроверяемыхРеквизитов.Добавить("МатериалыИУслуги.СтатьяКалькуляции");
	МассивНепроверяемыхРеквизитов.Добавить("МатериалыИУслуги.Характеристика");
	ПроверитьЗаполнениеМатериалов(Отказ);
#КонецОбласти

#Область Трудозатраты
	Если ЭтоТехнологическийНабор() Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Трудозатраты");
		МассивНепроверяемыхРеквизитов.Добавить("Трудозатраты.ВидРабот");
	КонецЕсли;
	МассивНепроверяемыхРеквизитов.Добавить("Трудозатраты.СтатьяКалькуляции");
	МассивНепроверяемыхРеквизитов.Добавить("Трудозатраты.Количество");
#КонецОбласти
	
//++ НЕ УТКА
#Область ПромежуточныйВыпуск
	МассивНепроверяемыхРеквизитов.Добавить("ПромежуточныйВыпуск.ЭтапОтправитель");
	МассивНепроверяемыхРеквизитов.Добавить("ПромежуточныйВыпуск.ЭтапПолучатель");
	МассивНепроверяемыхРеквизитов.Добавить("ПромежуточныйВыпуск.Номенклатура");
	МассивНепроверяемыхРеквизитов.Добавить("ПромежуточныйВыпуск.Характеристика");
#КонецОбласти
//-- НЕ УТКА

#Область Общее
	// при разборке возможно несколько последних этапов
	Если Статус <> Перечисления.СтатусыСпецификаций.Действует
		ИЛИ ТипПроизводственногоПроцесса <> Перечисления.ТипыПроизводственныхПроцессов.Разборка
		ИЛИ НЕ ОписаниеПроизводственногоПроцесса.НесколькоПоследнихЭтапов
		Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ВыходныеИзделия.ЭтапРедактирование");
		МассивНепроверяемыхРеквизитов.Добавить("ВозвратныеОтходы.ЭтапРедактирование");
	КонецЕсли;
	// при сборке возможно несколько первых этапов
	Если Статус <> Перечисления.СтатусыСпецификаций.Действует
		ИЛИ ТипПроизводственногоПроцесса <> Перечисления.ТипыПроизводственныхПроцессов.Сборка
		ИЛИ НЕ ОписаниеПроизводственногоПроцесса.НесколькоПервыхЭтапов
		Тогда
		МассивНепроверяемыхРеквизитов.Добавить("МатериалыИУслуги.ЭтапРедактирование");
		МассивНепроверяемыхРеквизитов.Добавить("Трудозатраты.ЭтапРедактирование");
	КонецЕсли;
#КонецОбласти

	ПроверитьДействующуюСпецификацию(МассивНепроверяемыхРеквизитов, Отказ);
	
	ПроверитьДополнительныеРеквизитыПроизводственногоПроцесса(Отказ);
	
	ПроверитьВыборЭтапов("ВыходныеИзделия",  "ЭтапРедактирование", Отказ);
	ПроверитьВыборЭтапов("ВозвратныеОтходы", "ЭтапРедактирование", Отказ);
	ПроверитьВыборЭтапов("МатериалыИУслуги", "ЭтапРедактирование", Отказ);
	ПроверитьВыборЭтапов("Трудозатраты",     "ЭтапРедактирование", Отказ);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если Не ЭтоГруппа Тогда
		
		Если Не ЭтоНовый() Тогда
			Блокировка = Новый БлокировкаДанных;
			Блокировка.Добавить("Справочник.ЭтапыПроизводства").УстановитьЗначение("Владелец", Ссылка);
			Блокировка.Заблокировать();
		КонецЕсли;
		
		Если ПометкаУдаления Тогда
			Статус = Перечисления.СтатусыСпецификаций.Закрыта;
		КонецЕсли;
		
		ОчиститьНеиспользуемыеДанные();
		
		Если Не ЭтоНовый() Тогда
			ПроверитьСкорректироватьЭтапыПроизводства(Отказ);
		КонецЕсли;
		
		ЗаполнитьСлужебныеРеквизиты();
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		
		УстановитьРеквизитыПоЭтапамОперациям();
	
		Если Не ЭтоНовый() Тогда
			ЗарегистрироватьРасчетДлительностиПоСпецификации();
		КонецЕсли;
		
		ИдентификаторВерсииДанных = Новый УникальныйИдентификатор();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЭтоГруппа Тогда
	
		РегистрыСведений.СпецификацииИзделий.ЗаписатьИзделияПоСпецификации(Ссылка, Статус = Перечисления.СтатусыСпецификаций.Закрыта);
		
		//++ НЕ УТКА
		Если Статус = Перечисления.СтатусыСпецификаций.Действует Тогда
			ОбновитьТребуетсяСправочноеУказаниеСерий();
		КонецЕсли;
		//-- НЕ УТКА
		
		ПолныйПересчет = Ложь;
		
		Если ДополнительныеСвойства.Свойство("РассчитатьДлительностьПроизводства", ПолныйПересчет) Тогда
			РегистрыСведений.ЗаданияКРасчетуДлительностиПроизводства.ДобавитьЗадание(
				Ссылка, ПолныйПересчет, НЕ ДополнительныеСвойства.Свойство("ЗапретитьРасчетДлительностиПроизводства"));
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Если ЭтоГруппа Тогда
		Возврат
	КонецЕсли; 
	
	Ответственный = Пользователи.ТекущийПользователь();
	Статус = Перечисления.СтатусыСпецификаций.ВРазработке;
	
	НачалоДействия = ТекущаяДатаСеанса();
	КонецДействия  = '00010101';
	
	// Очистим связь с этапами
	СписокТЧ = Новый Массив;
	СписокТЧ.Добавить("ВыходныеИзделия");
	СписокТЧ.Добавить("ВозвратныеОтходы");
	СписокТЧ.Добавить("МатериалыИУслуги");
	СписокТЧ.Добавить("Трудозатраты");
	Для каждого ИмяТЧ Из СписокТЧ Цикл
		Для каждого ДанныеСтроки Из ЭтотОбъект[ИмяТЧ] Цикл
			ДанныеСтроки.Этап = Справочники.ЭтапыПроизводства.ПустаяСсылка();
			ДанныеСтроки.ЭтапРедактирование = Справочники.ЭтапыПроизводства.ПустаяСсылка();
			//++ НЕ УТКА
			ДанныеСтроки.Операция = Справочники.ТехнологическиеОперации.ПустаяСсылка();
			ДанныеСтроки.ОперацияРедактирование = Справочники.ТехнологическиеОперации.ПустаяСсылка();
			//-- НЕ УТКА
		КонецЦикла;
	КонецЦикла;
	
	//++ НЕ УТКА
	Для каждого ДанныеСтроки Из ПромежуточныйВыпуск Цикл
		ДанныеСтроки.ЭтапОтправитель = Справочники.ЭтапыПроизводства.ПустаяСсылка();
		ДанныеСтроки.ЭтапПолучатель = Справочники.ЭтапыПроизводства.ПустаяСсылка();
		ДанныеСтроки.ОперацияОтправитель = Справочники.ТехнологическиеОперации.ПустаяСсылка();
		ДанныеСтроки.ОперацияПолучатель = Справочники.ТехнологическиеОперации.ПустаяСсылка();
	КонецЦикла;
	//-- НЕ УТКА
	
	ИдентификаторВерсииДанных = Неопределено;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПроверкаЗаполнения

#Область СтатусДействует

Процедура ПроверитьДействующуюСпецификацию(МассивНепроверяемыхРеквизитов, Отказ)

	Если Статус <> Перечисления.СтатусыСпецификаций.Действует
			ИЛИ ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьЗаполнениеВыходныхИзделийВСтатусеДействует(
		МассивНепроверяемыхРеквизитов,
		Отказ);
	
	ПроверитьЗаполнениеВозвратныхОтходовВСтатусеДействует(
		МассивНепроверяемыхРеквизитов,
		Отказ);
	
	ПроверитьЗаполнениеМатериаловВСтатусеДействует(
		МассивНепроверяемыхРеквизитов,
		Отказ);
	
	ПроверитьЗаполнениеТрудозатратВСтатусеДействует(Отказ);
	
	ПроверитьПроизводственныйПроцесс(Отказ);
	
	ПроверитьАвтовыборРасчетПоФормуламОтборПоСвойствам(Отказ);
	
	ПроверитьМаршрутныеКарты(Отказ);
	
	ПроверитьПараметрыНазначения(Отказ);
	
//++ НЕ УТКА
	Справочники.РесурсныеСпецификации.ПроверитьВложенныеСпецификации(ЭтотОбъект, Отказ);
//-- НЕ УТКА
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеВыходныхИзделийВСтатусеДействует(МассивНепроверяемыхРеквизитов, Отказ)
	
	ИмяТЧ           = "ВыходныеИзделия";
	ПредставлениеТЧ = ПредставлениеТаблицыВФорме(ИмяТЧ);
	
	ИспользоватьПараметризациюРесурсныхСпецификаций = ПолучитьФункциональнуюОпцию("ИспользоватьПараметризациюРесурсныхСпецификаций");
	
	// Проверка заполнения основного изделия
	Если Не ЗначениеЗаполнено(ВыходныеИзделия) Тогда
		
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Не введено ни одной строки в список ""%1""';
										|en = 'No line is entered into the ""%1"" list'"), ПредставлениеТЧ);
		ОбщегоНазначения.СообщитьПользователю(
			ТекстСообщения,
			ЭтотОбъект,
			ИмяТЧ,
			,
			Отказ);
			
		Возврат;
	КонецЕсли;
	
	ЭтоСборка = (ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Сборка);
	
	ВариантНазначенияВидНоменклатуры = (ВариантНазначения = Перечисления.ВариантыНазначенияСпецификации.ВидНоменклатуры);
	ВариантНазначенияСписокНоменклатуры = (ВариантНазначения = Перечисления.ВариантыНазначенияСпецификации.СписокНоменклатуры);
	
	// Проверка заполнения табличной части
	Для Индекс = 0 По ВыходныеИзделия.Количество() - 1 Цикл
		
		Если ЭтоСборка И НЕ ВариантНазначенияСписокНоменклатуры И Индекс > 0 Тогда
			Прервать;
		КонецЕсли;
		
		Строка = ВыходныеИзделия[Индекс];
		
		Если Строка.Номенклатура.Пустая()
			И НЕ ВариантНазначенияВидНоменклатуры
			И (Строка.СпособАвтовыбораНоменклатуры = Перечисления.СпособыАвтовыбораНоменклатуры.УказываетсяВНСИ
				ИЛИ НЕ ИспользоватьПараметризациюРесурсныхСпецификаций) Тогда
			
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", "Заполнение", НСтр("ru = 'Номенклатура';
																												|en = 'Items'"), Строка.НомерСтроки, ПредставлениеТЧ);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТЧ, Строка.НомерСтроки, "Номенклатура");
			
			ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект,
				Поле,
				,
				Отказ);
			
		КонецЕсли;
		
		Если Строка.ВидНоменклатуры.Пустая() И ВариантНазначенияВидНоменклатуры Тогда
				
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", "Заполнение", НСтр("ru = 'Вид номенклатуры';
																												|en = 'Item kind'"), Строка.НомерСтроки, ПредставлениеТЧ);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТЧ, Строка.НомерСтроки, "ВидНоменклатуры");
			
			ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект,
				Поле,
				,
				Отказ);
				
		КонецЕсли;
		
		Если НЕ ВариантНазначенияВидНоменклатуры Тогда
			ПроверитьЗаполнениеКоличестваВСтрокеТабЧасти(ИмяТЧ, ПредставлениеТЧ, Строка, Отказ);
		КонецЕсли;
		
		Если Строка.ОбработатьПоСпецификации И Строка.Спецификация.Пустая() Тогда
			
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", "Заполнение", НСтр("ru = 'Спецификация';
																												|en = 'Bill of materials'"), Строка.НомерСтроки, ПредставлениеТЧ);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТЧ, Строка.НомерСтроки, "Спецификация");
			
			ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект,
				Поле,
				,
				Отказ);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если НЕ (ЭтоСборка И НЕ ВариантНазначенияСписокНоменклатуры) Тогда
		ПроверитьПовторВыходныхИзделий(Отказ);
		ПроверитьЗаполнениеДолейСтоимостиВыходныхИзделия(Отказ);
	КонецЕсли;
	
	// Проверка характеристик выходных изделий
	Если ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Разборка 
		ИЛИ (ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Сборка 
			И ВыходныеИзделия.Количество() > 1) Тогда
			
		ПараметрыПроверки = НоменклатураСервер.ПараметрыПроверкиЗаполненияХарактеристик();
		ПараметрыПроверки.ИмяТЧ = ИмяТЧ;
		ПараметрыПроверки.ПредставлениеТЧ = ПредставлениеТЧ;
		
		СписокСтрок = Неопределено;
		Если ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Сборка Тогда
			
			СписокСтрок = Новый Массив;
			Для Ит = 1 По ВыходныеИзделия.Количество()-1 Цикл
				Строка = ВыходныеИзделия[Ит];
				Если Строка.СпособАвтовыбораХарактеристики = Перечисления.СпособыАвтовыбораХарактеристики.УказываетсяВНСИ
					ИЛИ НЕ ИспользоватьПараметризациюРесурсныхСпецификаций Тогда
					СписокСтрок.Добавить(Строка);
				КонецЕсли;
			КонецЦикла;
			
		ИначеЕсли ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Разборка
			И ИспользоватьПараметризациюРесурсныхСпецификаций Тогда
			
			СписокСтрок = ВыходныеИзделия.НайтиСтроки(Новый Структура("СпособАвтовыбораХарактеристики", 
					Перечисления.СпособыАвтовыбораХарактеристики.УказываетсяВНСИ));
			
		КонецЕсли;
		
		ПараметрыПроверки.СписокСтрок = СписокСтрок;
		НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект, МассивНепроверяемыхРеквизитов, Отказ, ПараметрыПроверки);
		
	КонецЕсли;
	
//++ НЕ УТКА

	// В версии 2.2 выпуск продукции по рассчитываемой стоимости возможен только на последних этапах
	Если ПроизводствоСервер.ИспользуетсяПроизводство22()
		И Не ЭтоНовый() Тогда 
		
		ВыпускающиеЭтапы = ВыпускающиеЭтапы();
		
		Если ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Разборка
			И ВыпускающиеЭтапы.Количество() > 1 Тогда
			
			Шаблон = НСтр("ru = 'Не введено ни одной строки в список ""%1"" по этапу ""%2""';
							|en = 'No lines entered into the ""%1"" list by the ""%2"" stage'");
			Для каждого ВыпускающийЭтап Из ВыпускающиеЭтапы Цикл
				
				СтруктураПоиска = Новый Структура("ЭтапРедактирование", ВыпускающийЭтап);
				Если ВыходныеИзделия.НайтиСтроки(СтруктураПоиска).ВГраница() = -1 Тогда
					
					ТекстСообщения = СтрШаблон(Шаблон, ПредставлениеТЧ, ВыпускающийЭтап);
					
					ОбщегоНазначения.СообщитьПользователю(
						ТекстСообщения,
						ЭтотОбъект,
						ИмяТЧ,
						,
						Отказ);
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
		Шаблон = НСтр("ru = 'Выпуск по рассчитываемой стоимости возможен только на последнем этапе (см. список ""%1"", строка %2)';
						|en = 'Release at calculated cost is possible only at the last stage (see the ""%1"" list, line %2)'");
		Для каждого Строка Из ВыходныеИзделия Цикл
			
			Если Строка.ЭтапРедактирование.Пустая()
				ИЛИ ВыпускающиеЭтапы.Найти(Строка.ЭтапРедактирование) <> Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ТекстСообщения = СтрШаблон(Шаблон, ПредставлениеТЧ, Формат(Строка.НомерСтроки, "ЧГ="));
			
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
				ИмяТЧ, Строка.НомерСтроки, "ЭтапРедактирование");
			
			ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект,
				Поле,
				, 
				Отказ);
			
		КонецЦикла;
		
	КонецЕсли;
//-- НЕ УТКА

КонецПроцедуры

Процедура ПроверитьЗаполнениеВозвратныхОтходовВСтатусеДействует(МассивНепроверяемыхРеквизитов, Отказ)
	
	Если ЭтоТехнологическийНабор() Тогда
		Возврат;
	КонецЕсли;
	
	ИмяТЧ           = "ВозвратныеОтходы";
	ПредставлениеТЧ = ПредставлениеТаблицыВФорме(ИмяТЧ);
	
	Для каждого Строка Из ВозвратныеОтходы Цикл
		
		Если Строка.Номенклатура.Пустая()
			И (Строка.СпособАвтовыбораНоменклатуры = Перечисления.СпособыАвтовыбораНоменклатуры.УказываетсяВНСИ
				ИЛИ НЕ ПолучитьФункциональнуюОпцию("ИспользоватьПараметризациюРесурсныхСпецификаций")) Тогда
			
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", "Заполнение", НСтр("ru = 'Номенклатура';
																												|en = 'Items'"), Строка.НомерСтроки, ПредставлениеТЧ);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТЧ, Строка.НомерСтроки, "Номенклатура");
			
			ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект,
				Поле,
				,
				Отказ);
			
		КонецЕсли;
		
		ПроверитьЗаполнениеКоличестваВСтрокеТабЧасти(ИмяТЧ, ПредставлениеТЧ, Строка, Отказ);
		
		Если Строка.ОбработатьПоСпецификации И Строка.Спецификация.Пустая() Тогда
			
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", "Заполнение", НСтр("ru = 'Спецификация';
																												|en = 'Bill of materials'"), Строка.НомерСтроки, ПредставлениеТЧ);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТЧ, Строка.НомерСтроки, "Спецификация");
			
			ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект,
				Поле,
				,
				Отказ);
			
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Строка.СтатьяКалькуляции) Тогда
		
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", "Заполнение", НСтр("ru = 'Статья калькуляции';
																												|en = 'Product cost element'"), Строка.НомерСтроки, ПредставлениеТЧ);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТЧ, Строка.НомерСтроки, "СтатьяКалькуляции");
			
			ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект,
				Поле,,
				Отказ);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ПараметрыПроверки = НоменклатураСервер.ПараметрыПроверкиЗаполненияХарактеристик();
	ПараметрыПроверки.ИмяТЧ = ИмяТЧ;
	ПараметрыПроверки.ПредставлениеТЧ = ПредставлениеТЧ;
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПараметризациюРесурсныхСпецификаций") Тогда
		СписокСтрок = ВозвратныеОтходы.НайтиСтроки(Новый Структура("СпособАвтовыбораХарактеристики", Перечисления.СпособыАвтовыбораХарактеристики.УказываетсяВНСИ));
		ПараметрыПроверки.СписокСтрок = СписокСтрок;
	КонецЕсли;
	НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект, МассивНепроверяемыхРеквизитов, Отказ, ПараметрыПроверки);
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеМатериаловВСтатусеДействует(МассивНепроверяемыхРеквизитов, Отказ)
	
	ИмяТЧ           = "МатериалыИУслуги";
	ПредставлениеТЧ = ПредставлениеТаблицыВФорме(ИмяТЧ);
	
	ИспользуетсяПроизводство22 = ПроизводствоСервер.ИспользуетсяПроизводство22();
	ИсточникСоставаНаборовВПроизводстве = УправлениеДаннымиОбИзделияхПовтИсп.ИсточникСоставаНаборовВПроизводстве();
	ТипыНоменклатуры           = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(МатериалыИУслуги.ВыгрузитьКолонку("Номенклатура"), "ТипНоменклатуры");
	
	Для каждого Строка Из МатериалыИУслуги Цикл
		
		Если Строка.Номенклатура.Пустая()
			И (Строка.СпособАвтовыбораНоменклатуры = Перечисления.СпособыАвтовыбораНоменклатуры.УказываетсяВНСИ
			
			ИЛИ Строка.СпособАвтовыбораНоменклатуры = Перечисления.СпособыАвтовыбораНоменклатуры.ЗадаетсяВСвойствеПродукции
				И Строка.ПроизводитсяВПроцессе
				И Строка.СпособПолученияМатериала = Перечисления.СпособыПолученияМатериаловВСпецификации.ПроизводитсяНаЭтапе
			
			ИЛИ Строка.СпособАвтовыбораНоменклатуры = Перечисления.СпособыАвтовыбораНоменклатуры.УточняетсяПриПроизводстве
				И Строка.ПроизводитсяВПроцессе
				
			ИЛИ НЕ ПолучитьФункциональнуюОпцию("ИспользоватьПараметризациюРесурсныхСпецификаций")
			) Тогда
			
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", "Заполнение", НСтр("ru = 'Номенклатура';
																												|en = 'Items'"), Строка.НомерСтроки, ПредставлениеТЧ);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТЧ, Строка.НомерСтроки, "Номенклатура");
			
			ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект,
				Поле,,
				Отказ);
			
		КонецЕсли;
		
		Если Строка.КоличествоУпаковок = 0
			И (ПустаяСтрока(Строка.АлгоритмРасчетаКоличества)
			ИЛИ НЕ ПолучитьФункциональнуюОпцию("ИспользоватьПараметризациюРесурсныхСпецификаций")) Тогда
			
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", "Заполнение", НСтр("ru = 'Количество';
																												|en = 'Quantity'"), Строка.НомерСтроки, ПредставлениеТЧ);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТЧ, Строка.НомерСтроки, "КоличествоУпаковок");
			
			ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект,
				Поле,,
				Отказ);
		
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Строка.СтатьяКалькуляции)
			И НЕ Строка.ПроизводитсяВПроцессе
			И НЕ (ТипыНоменклатуры.Получить(Строка.Номенклатура) = Перечисления.ТипыНоменклатуры.Набор
					И ИсточникСоставаНаборовВПроизводстве = Перечисления.ИсточникиСоставаНаборовВПроизводстве.Спецификации) Тогда
			
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", "Заполнение", НСтр("ru = 'Статья калькуляции';
																												|en = 'Product cost element'"), Строка.НомерСтроки, ПредставлениеТЧ);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТЧ, Строка.НомерСтроки, "СтатьяКалькуляции");
			
			ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект,
				Поле,,
				Отказ);
			
		КонецЕсли;
		
		Если Строка.ПроизводитсяВПроцессе И Не ЗначениеЗаполнено(Строка.ИсточникПолученияПолуфабриката) Тогда
			
			Если Строка.СпособПолученияМатериала = Перечисления.СпособыПолученияМатериаловВСпецификации.ПроизвестиПоСпецификации 
				И ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеПроизводством") Тогда // только 2.1 
				
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", "Заполнение", НСтр("ru = 'Спецификация';
																													|en = 'Bill of materials'"), Строка.НомерСтроки, ПредставлениеТЧ);
				Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТЧ, Строка.НомерСтроки, "СпособПолученияМатериалаРедактирование");
				
				ОбщегоНазначения.СообщитьПользователю(
					ТекстСообщения,
					ЭтотОбъект,
					Поле,,
					Отказ);
				
			КонецЕсли;
			
			Если Строка.СпособПолученияМатериала = Перечисления.СпособыПолученияМатериаловВСпецификации.ПроизводитсяНаЭтапе Тогда
				
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", "Заполнение", НСтр("ru = 'Этап выпуска полуфабриката';
																													|en = 'Stage of semi-finished product release'"), Строка.НомерСтроки, ПредставлениеТЧ);
				Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТЧ, Строка.НомерСтроки, "СпособПолученияМатериалаРедактирование");
			
				ОбщегоНазначения.СообщитьПользователю(
					ТекстСообщения,
					ЭтотОбъект,
					Поле,,
					Отказ);
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если Строка.СпособПолученияМатериала <> Перечисления.СпособыПолученияМатериаловВСпецификации.Обеспечивать
			И (ИспользуетсяПроизводство22 И ТипыНоменклатуры[Строка.Номенклатура] = Перечисления.ТипыНоменклатуры.МногооборотнаяТара
				ИЛИ ТипыНоменклатуры[Строка.Номенклатура] = Перечисления.ТипыНоменклатуры.Набор) Тогда
				
				ТекстСообщения = СтрШаблон(
					НСтр("ru = 'Для материала с типом ""%1"" доступно только указание способа получения ""Обеспечивать""';
						|en = 'You can only specify the ""Supply"" receipt method for the material with the ""%1"" type'"),
					ТипыНоменклатуры[Строка.Номенклатура]);
				
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
					"Колонка", "Корректность", НСтр("ru = 'Способ получения материала';
													|en = 'Method of receiving material'"), Строка.НомерСтроки, ПредставлениеТЧ, ТекстСообщения);
				Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТЧ, Строка.НомерСтроки, "СпособПолученияМатериалаРедактирование");
			
				ОбщегоНазначения.СообщитьПользователю(
					ТекстСообщения,
					ЭтотОбъект,
					Поле,,
					Отказ);
			
		КонецЕсли;
		
	КонецЦикла;
	
	// Характеристики
	ПараметрыПроверки = НоменклатураСервер.ПараметрыПроверкиЗаполненияХарактеристик();
	ПараметрыПроверки.ИмяТЧ = ИмяТЧ;
	ПараметрыПроверки.ПредставлениеТЧ = ПредставлениеТЧ;
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПараметризациюРесурсныхСпецификаций") Тогда
		СписокСтрок = МатериалыИУслуги.НайтиСтроки(Новый Структура("СпособАвтовыбораХарактеристики", Перечисления.СпособыАвтовыбораХарактеристики.УказываетсяВНСИ));
		ПараметрыПроверки.СписокСтрок = СписокСтрок;
	КонецЕсли;
	НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект, МассивНепроверяемыхРеквизитов, Отказ, ПараметрыПроверки);
	
	// Дополнительные проверки: промежуточные полуфабрикаты, производство 2.1
	ПараметрыПроверкиПромежуточныхПолуфабрикатов = УправлениеДаннымиОбИзделиях.ПолучитьПараметрыПроверкиВнутреннихПолуфабрикатов(ЭтотОбъект);
	СтруктураПроверок = Новый Структура("СоответствиеСпецификации, КратностьПроизводимогоКоличества, ЗаполнениеВнутреннихПолуфабрикатов");
	ПараметрыПроверки = Новый Структура;
	ПараметрыПроверки.Вставить("ИмяРеквизита", "СпособПолученияМатериалаРедактирование");
	ПараметрыПроверки.Вставить("СтруктураПроверок", СтруктураПроверок);
	ПараметрыПроверки.Вставить("ПараметрыПроверкиВнутреннихПолуфабрикатов", ПараметрыПроверкиПромежуточныхПолуфабрикатов);
	
	Если УправлениеДаннымиОбИзделиях.ДоступноОписаниеВероятностиПримененияМатериалов() Тогда
		СписокСтрок = МатериалыИУслуги.НайтиСтроки(Новый Структура("ПроизводитсяВПроцессе,Альтернативный",Истина,Ложь));
	Иначе
		СписокСтрок = МатериалыИУслуги.НайтиСтроки(Новый Структура("ПроизводитсяВПроцессе",Истина));
	КонецЕсли;
	
	УправлениеДаннымиОбИзделиях.ПроверитьСпецификацииПолуфабрикатов(
		СписокСтрок,
		ПараметрыПроверки,
		Отказ,
		ЭтотОбъект);
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеТрудозатратВСтатусеДействует(Отказ)
	
	Если ЭтоТехнологическийНабор() Тогда
		Возврат;
	КонецЕсли;
	
	ШаблонСтатьяКалькуляции = НСтр("ru = 'Не заполнена колонка ""Статья калькуляции"" в строке %1 списка ""Трудозатраты""';
									|en = 'Column ""Product cost element"" in line %1 of the ""Labor costs"" list is not filled in'");
	ШаблонКоличество        = НСтр("ru = 'Не заполнена колонка ""Количество"" в строке %1 списка ""Трудозатраты""';
									|en = 'Column ""Quantity"" in line %1 of the ""Labor costs"" list is not filled in'");
	
	Для каждого Строка Из Трудозатраты Цикл
		
		Если Строка.СтатьяКалькуляции.Пустая() Тогда
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонСтатьяКалькуляции, 
				Формат(Строка.НомерСтроки, "ЧГ="));
			
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Трудозатраты", Строка.НомерСтроки, "СтатьяКалькуляции");
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле,, Отказ);
			
		КонецЕсли;
		
		Если Строка.Количество = 0
			И (ПустаяСтрока(Строка.АлгоритмРасчетаКоличества) 
			ИЛИ НЕ ПолучитьФункциональнуюОпцию("ИспользоватьПараметризациюРесурсныхСпецификаций")) Тогда
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонКоличество, 
				Формат(Строка.НомерСтроки, "ЧГ="));
			
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Трудозатраты", Строка.НомерСтроки, "Количество");
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле,, Отказ);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьАвтовыборРасчетПоФормуламОтборПоСвойствам(Отказ)
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьПараметризациюРесурсныхСпецификаций") Тогда
		Возврат;
	КонецЕсли;
	
//++ НЕ УТКА
#Область Автовыбор

	// Проверим, что свойства указанные в автовыборе и условиях использования есть в продукции
	ОсновноеИзделие = УправлениеДаннымиОбИзделияхКлиентСервер.ОсновнойРеквизитОсновногоИзделияСпецификации(ЭтотОбъект);
	Если ОсновноеИзделие.Значение.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	Если ОсновноеИзделие.Имя = "ВидНоменклатуры" Тогда
		ВидИзделий = ОсновноеИзделие.Значение;
	Иначе
		ВидИзделий = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОсновноеИзделие.Значение, "ВидНоменклатуры");
	КонецЕсли;
	
	СписокВсехДоступныхСвойств = УправлениеДаннымиОбИзделиях.ПолучитьСвойстваДляАвтовыбора(ВидИзделий);
	
	МассивПроверок = Новый Массив;
	МассивПроверок.Добавить(Новый Структура("ИмяТЧ, Представление, Реквизит", "ВыходныеИзделия", ПредставлениеТаблицыВФорме("ВыходныеИзделия"), "Номенклатура"));
	МассивПроверок.Добавить(Новый Структура("ИмяТЧ, Представление, Реквизит", "ВозвратныеОтходы", ПредставлениеТаблицыВФорме("ВозвратныеОтходы"), "Номенклатура"));
	МассивПроверок.Добавить(Новый Структура("ИмяТЧ, Представление, Реквизит", "МатериалыИУслуги", ПредставлениеТаблицыВФорме("МатериалыИУслуги"), "Номенклатура"));
	
	// Настройки автоподбора материалов
	ШаблонСообщения = НСтр("ru = 'В настройке автовыбора определено, что номенклатура указывается в свойстве ""%1"", но это свойство не входит в состав свойств основного изделия (список ""%2"", строка %3).';
							|en = 'In auto selection settings, it is determined that items are specified in the ""%1"" property, but this property is not included in main product properties (the ""%2"" list, line %3).'");
	ТекстНеЗаданоСвойствоАвтовыбора = НСтр("ru = 'В настройке автовыбора определено, что номенклатура указывается в свойстве основного изделия, но свойство не задано.';
											|en = 'In auto selection settings, it is determined that items are specified in the property of the main product, but this property is not set.'");
	ТекстНеЗаданАлгоритмАвтовыбора = НСтр("ru = 'В настройке автовыбора определено, что характеристика определяется по алгоритму, но алгоритм не задан.';
											|en = 'In auto selection settings, it is determined that a variant is defined by algorithm, but the algorithm is not specified.'");
	
	Для каждого Проверка Из МассивПроверок Цикл
		
		ТабличнаяЧасть = ЭтотОбъект[Проверка.ИмяТЧ]; // СправочникТабличнаяЧасть.РесурсныеСпецификации.ВыходныеИзделия
		
		Для каждого ТекущаяСтрока Из ТабличнаяЧасть Цикл
			
			ТекстСообщения = "";
			Если ТекущаяСтрока.СпособАвтовыбораНоменклатуры = Перечисления.СпособыАвтовыбораНоменклатуры.ЗадаетсяВСвойствеПродукции Тогда
				Если ЗначениеЗаполнено(ТекущаяСтрока.СвойствоСодержащееНоменклатуру) Тогда
					Если СписокВсехДоступныхСвойств.Найти(ТекущаяСтрока.СвойствоСодержащееНоменклатуру, "Свойство") = Неопределено Тогда
						
						ЗаголовокСвойства = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущаяСтрока.СвойствоСодержащееНоменклатуру, "Заголовок");
						ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							ШаблонСообщения, 
							ЗаголовокСвойства,
							Проверка.Представление,
							Формат(ТекущаяСтрока.НомерСтроки, "ЧГ="));
							
					КонецЕсли;
				Иначе
					ТекстСообщения = ТекстНеЗаданоСвойствоАвтовыбора;
				КонецЕсли;
			ИначеЕсли ТекущаяСтрока.СпособАвтовыбораХарактеристики = Перечисления.СпособыАвтовыбораХарактеристики.ПодбираетсяПоАлгоритму 
				И НЕ ЗначениеЗаполнено(ТекущаяСтрока.АлгоритмАвтовыбораХарактеристики) Тогда
				ТекстСообщения = ТекстНеЗаданАлгоритмАвтовыбора;
			КонецЕсли;
			
			Если НЕ ПустаяСтрока(ТекстСообщения) Тогда
				Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(Проверка.ИмяТЧ, ТекущаяСтрока.НомерСтроки, Проверка.Реквизит);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле,, Отказ);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
#КонецОбласти

#Область ОтборПоСвойствам

	СписокВсехДоступныхСвойств = УправлениеДаннымиОбИзделиях.ПолучитьСвойстваДляОтбораПоСвойствам(ВидИзделий,,Ложь);

	// Настройки использования и расчета количества
	ШаблонСообщения = НСтр("ru = 'В настройках использования указаны неверные свойства (список ""%1"", строка %2).';
							|en = 'Incorrect properties are specified in usage settings (the ""%1"" list, line %2).'");
	
	МассивПроверок.Добавить(Новый Структура("ИмяТЧ, Представление, Реквизит", "Трудозатраты", ПредставлениеТаблицыВФорме("Трудозатраты"), "ВидРабот"));
	
	Для каждого Проверка Из МассивПроверок Цикл
		
		ТабличнаяЧасть = ЭтотОбъект[Проверка.ИмяТЧ]; // СправочникТабличнаяЧасть.РесурсныеСпецификации.ВыходныеИзделия
		
		Для каждого ТекущаяСтрока Из ТабличнаяЧасть Цикл
			
			СтрокиОтбор = ОтборПоСвойствам.НайтиСтроки(Новый Структура("КлючСвязи", ТекущаяСтрока.КлючСвязи));
			
			Для каждого СтрокаОтбор Из СтрокиОтбор Цикл
				
				Если СписокВсехДоступныхСвойств.Найти(СтрокаОтбор.Свойство, "Свойство") = Неопределено Тогда
					
					ТекстСообщения = СтрШаблон(ШаблонСообщения, Проверка.Представление, Формат(ТекущаяСтрока.НомерСтроки, "ЧГ="));
					
					Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(Проверка.ИмяТЧ, ТекущаяСтрока.НомерСтроки, Проверка.Реквизит);
					
					ОбщегоНазначения.СообщитьПользователю(
						ТекстСообщения, 
						ЭтотОбъект, 
						Поле,
						, 
						Отказ);
						
					Прервать;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Если НЕ ДополнительныеСвойства.Свойство("ИнтерактивнаяПроверкаПройдена") Тогда
		Справочники.ЭтапыПроизводства.ПроверитьСоответствиеОтбораПоСвойствам(Ссылка, СписокВсехДоступныхСвойств, МногоэтапныйПроизводственныйПроцесс, Отказ);
	КонецЕсли;
	
#КонецОбласти

#Область УточненияПрименения

	ВидыНастроек = Новый СписокЗначений;
	ВидыНастроек.Добавить("Номенклатура",   НСтр("ru = 'В настройках отбора по свойствам номенклатуры указаны неверные свойства.';
												|en = 'Incorrect properties are specified in filter settings by item properties.'"));
	ВидыНастроек.Добавить("Характеристика", НСтр("ru = 'В настройках отбора по свойствам характеристики указаны неверные свойства.';
												|en = 'Incorrect properties are specified in filter settings by variant properties.'"));
	
	Поле = "";
	Если ДополнительныеСвойства.Свойство("ИнтерактивнаяПроверкаПройдена") Тогда
		Поле = ?(ВариантНазначения = Перечисления.ВариантыНазначенияСпецификации.ВидНоменклатуры,
			"ОсновноеИзделиеВидНоменклатуры",
			"ОсновноеИзделиеНоменклатура");
	КонецЕсли;
	
	Для каждого Элемент Из ВидыНастроек Цикл
		
		ВидНастройки                  = Элемент.Значение;
		ВидНастройкиДоступныеСвойства = УправлениеДаннымиОбИзделиях.ПолучитьСвойстваДляОтбораПоСвойствам(ВидИзделий, ВидНастройки);
		ВидНастройкиКлючСвязи         = УправлениеДаннымиОбИзделияхКлиентСервер.ОтборПоСвойствамКлючСвязиПредопределенный(ВидНастройки);
		
		СтрокиОтбор = ОтборПоСвойствам.НайтиСтроки(Новый Структура("КлючСвязи", ВидНастройкиКлючСвязи));
		Для каждого СтрокаОтбор Из СтрокиОтбор Цикл
			
			Если ВидНастройкиДоступныеСвойства.Найти(СтрокаОтбор.Свойство, "Свойство") = Неопределено Тогда
				
				ОбщегоНазначения.СообщитьПользователю(
					Элемент.Представление,
					Ссылка,
					Поле,,
					Отказ);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;

#КонецОбласти

#Область РасчетПоФормулам

	ТабличныеЧастиОбъекта = Метаданные().ТабличныеЧасти;
	
	Отбор = Новый Структура("АлгоритмРасчетаКоличества", "");
	
	Для Ит = - МассивПроверок.ВГраница() По 0 Цикл
		Проверка = МассивПроверок[-Ит];
		Если ЭтотОбъект[Проверка.ИмяТЧ].НайтиСтроки(Отбор).Количество() = ЭтотОбъект[Проверка.ИмяТЧ].Количество() Тогда
			МассивПроверок.Удалить(-Ит);
		Иначе
			ИмяРеквизита = "КоличествоУпаковок";
			Если ТабличныеЧастиОбъекта[Проверка.ИмяТЧ].Реквизиты.Найти(ИмяРеквизита) = Неопределено Тогда
				ИмяРеквизита = "Количество";
			КонецЕсли;
			Проверка.Реквизит = ИмяРеквизита;
		КонецЕсли;
	КонецЦикла;
	
	ОписаниеИсточников = Справочники.РесурсныеСпецификации.ВыгрузитьДанныеДляКонструктораФормул(ЭтотОбъект, "");
	
	Если МассивПроверок.Количество() > 0 Тогда
		
		ПараметрыПроверки = Новый Структура("Состав, ВыводитьСообщения, ОчищатьНеНайденные", МассивПроверок, Истина, Ложь);
		
		УправлениеДаннымиОбИзделиях.ПроверитьОчиститьАлгоритмРасчетаКоличества(ЭтотОбъект, ОписаниеИсточников, ПараметрыПроверки, Отказ);
		
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.Свойство("ИнтерактивнаяПроверкаПройдена") Тогда
		Справочники.ЭтапыПроизводства.ПроверитьАлгоритмРасчетаКоличества(Ссылка, ОписаниеИсточников, МногоэтапныйПроизводственныйПроцесс, Отказ);
	КонецЕсли;
	
#КонецОбласти

//-- НЕ УТКА
	
КонецПроцедуры

Процедура ПроверитьПроизводственныйПроцесс(Отказ)
	
	Если ЭтоТехнологическийНабор() Тогда
		Возврат;
	КонецЕсли;
	
	Если ОптимальнаяПартияВыпуска <> 0
		И МинимальнаяПартияВыпуска > ОптимальнаяПартияВыпуска Тогда
	
		ТекстСообщения = НСтр("ru = 'Минимальная партия выпуска не может превышать оптимальную партию';
								|en = 'Minimum release lot cannot exceed the size of economic lot'");
		
		ОбщегоНазначения.СообщитьПользователю(
			ТекстСообщения,
			ЭтотОбъект,
			"МинимальнаяПартияВыпуска",
			,
			Отказ);
		
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ИнтерактивнаяПроверкаПройдена") Тогда
		Возврат;
	КонецЕсли;
	
	Сообщения = Новый Массив(ПолучитьСообщенияПользователю(Истина));
	
	КоличествоЭтапов = КоличествоЭтаповПроизводства();
	Если КоличествоЭтапов = 0 Тогда
		ТекстСообщения = НСтр("ru = 'Необходимо создать этап производства.';
								|en = 'Create a production stage.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,,Отказ);
	ИначеЕсли КоличествоЭтапов > 1 Тогда
		Если НЕ УправлениеДаннымиОбИзделияхКлиентСервер.МногоэтапныйПроизводственныйПроцессДоступен() Тогда
			ТекстСообщения = УправлениеДаннымиОбИзделияхКлиентСервер.МногоэтапныйПроизводственныйПроцессТекстОшибки();
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,,Отказ);
		Иначе
			ПроверитьПорядокЭтапов(Отказ);
		КонецЕсли;
	КонецЕсли;
	
	ПроверитьРеквизитыЭтапов(Отказ);
	
	ПроверитьЭтапыПроизводстваНаСтороне(Отказ);
	
	//++ НЕ УТКА
	ПроверитьРеквизитыПорядокОпераций(Отказ);
	ПроверитьЗаполнениеПромежуточногоВыпускаВСтатусеДействует(Отказ);
	//-- НЕ УТКА
	
	Если Отказ Тогда
		ЗаписатьОшибкиПроверкиПроизводственногоПроцессаВЖурналРегистрации();
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ТекстСообщенияОшибкиПроизводственногоПроцесса();
		Сообщение.УстановитьДанные(ЭтотОбъект);
		Сообщения.Добавить(Сообщение);
	КонецЕсли;
	
	Для каждого Сообщение Из Сообщения Цикл
		Сообщение.Сообщить();
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьМаршрутныеКарты(Отказ) 

//++ НЕ УТКА
	
	Если ПолучитьФункциональнуюОпцию("ХранитьОперацииВРесурсныхСпецификациях")
			ИЛИ НЕ ДополнительныеСвойства.Свойство("ПроверитьЭтапы")
			ИЛИ ЭтоТехнологическийНабор() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЭтапыПроизводства.Ссылка                            КАК Этап,
	|	ЭтапыПроизводства.Наименование                      КАК Наименование,
	|	ЭтапыПроизводства.МаршрутнаяКарта                   КАК Ссылка,
	|	ЭтапыПроизводства.МаршрутнаяКарта.Статус            КАК Статус,
	|	ЭтапыПроизводства.МаршрутнаяКарта.НачалоДействия    КАК НачалоДействия,
	|	ЭтапыПроизводства.МаршрутнаяКарта.КонецДействия     КАК КонецДействия
	|ИЗ
	|	Справочник.РесурсныеСпецификации КАК РесурсныеСпецификации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭтапыПроизводства КАК ЭтапыПроизводства
	|		ПО (ЭтапыПроизводства.Владелец = РесурсныеСпецификации.Ссылка)
	|			И (НЕ ЭтапыПроизводства.ПометкаУдаления)
	|			И (ЭтапыПроизводства.МаршрутнаяКарта <> ЗНАЧЕНИЕ(Справочник.МаршрутныеКарты.ПустаяСсылка))
	|ГДЕ
	|	РесурсныеСпецификации.Ссылка = &Спецификация
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЭтапыПроизводства.НомерЭтапа";
	
	Запрос.УстановитьПараметр("Спецификация", Ссылка);
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Справочники.РесурсныеСпецификации.ПроверитьКорректностьУказанияМаршрутнойКарты(
			НачалоДействия, КонецДействия, Выборка, Выборка.Этап, Выборка.Наименование, Отказ);
		
	КонецЦикла;
	
//-- НЕ УТКА
	
	Возврат; // обработчик пустой
	
КонецПроцедуры

Процедура ПроверитьПараметрыНазначения(Отказ)
	
//++ НЕ УТКА
	
	Если ДополнительныеСвойства.Свойство("ИнтерактивнаяПроверкаПройдена")
			ИЛИ НЕ ПолучитьФункциональнуюОпцию("ИспользоватьПараметрыНазначенияСпецификаций") Тогда
		Возврат;
	КонецЕсли;
	
	ЗначенияПараметровНазначения = Справочники.РесурсныеСпецификации.ЗначенияПараметровНазначенияСпецификаций(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Ссылка)).Получить(Ссылка);
	
	Для каждого Строка Из ЗначенияПараметровНазначения Цикл
		
		Если НЕ УправлениеДаннымиОбИзделияхПовтИсп.ПараметрНазначенияИспользуется(Строка.ВидПараметра, Строка.Реквизит, Ложь) Тогда
			Продолжить;
		КонецЕсли;
		
		УправлениеДаннымиОбИзделиях.ПроверитьЗаполнениеПараметраНазначения(
			Ссылка,
			Строка.ВидПараметра,
			Строка.Реквизит,
			Строка.СтруктураЗначения,
			Строка.ТипЗначения,
			Отказ);
		
	КонецЦикла;
	
//-- НЕ УТКА
	
	Возврат; // обработчик пустой
	
КонецПроцедуры

Процедура ПроверитьРеквизитыЭтапов(Отказ)

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЭтапыПроизводства.Ссылка
	|ИЗ
	|	Справочник.ЭтапыПроизводства КАК ЭтапыПроизводства
	|ГДЕ
	|	ЭтапыПроизводства.Владелец = &Спецификация
	|	И НЕ ЭтапыПроизводства.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("Спецификация", Ссылка);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ЭтапОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ЭтапОбъект.ДополнительныеСвойства.Вставить("СтатусСпецификации", Статус);
		Если НЕ ЭтапОбъект.ПроверитьЗаполнение() Тогда
			Отказ = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьПорядокЭтапов(Отказ)
	
	// Порядок производственного процесса
	
	СтруктураПроверок = РедакторПроизводственногоПроцесса.СтруктураПроверокПоследовательностиЭтапов(ТипПроизводственногоПроцесса);
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ИмяСправочникаОпераций",     "ЭтапыПроизводства");
	СтруктураПараметров.Вставить("ПолеНомерОперации",          "НомерЭтапа");
	СтруктураПараметров.Вставить("ПолеНомерСледующейОперации", "НомерСледующегоЭтапа");
	
	РедакторПроизводственногоПроцесса.ПоследовательностьОперацийПравильная(
			Ссылка,
			СтруктураПараметров,
			СтруктураПроверок,
			Отказ);
	
	// Порядок полуфабрикатов производимых на этапах и настройка "Планировать не ранее"
	
	Отбор = Новый Структура("ПроизводитсяВПроцессе", Истина);
	МассивСтрок = МатериалыИУслуги.НайтиСтроки(Отбор);
	Для Индекс = -МассивСтрок.ВГраница() По 0 Цикл;
		СтрокаТаблицы = МассивСтрок[-Индекс];
		Если (СтрокаТаблицы.СпособПолученияМатериала = Перечисления.СпособыПолученияМатериаловВСпецификации.ПроизвестиПоСпецификации
					И ЗначениеЗаполнено(СтрокаТаблицы.ПланироватьНеРанее)
				ИЛИ СтрокаТаблицы.СпособПолученияМатериала = Перечисления.СпособыПолученияМатериаловВСпецификации.ПроизводитсяНаЭтапе
			) Тогда
			Продолжить;
		КонецЕсли;
		МассивСтрок.Удалить(-Индекс);
	КонецЦикла;
	
	Если МассивСтрок.ВГраница() <> -1 Тогда
		
		МассивСсылок = Новый Массив;
		Для каждого СтрокаТаблицы Из МассивСтрок Цикл
			МассивСсылок.Добавить(СтрокаТаблицы.ЭтапРедактирование);
		КонецЦикла;
		
		ШаблонСообщения = НСтр("ru = 'Обнаружена неправильная последовательность этапов (список ""Материалы и работы"", строка %1).';
								|en = 'Wrong order of stages (""Materials and works"" list, line %1).'");
		
		Предшественники = Справочники.ЭтапыПроизводства.Предшественники(МассивСсылок);
		Для каждого СтрокаТаблицы Из МассивСтрок Цикл
			Если (СтрокаТаблицы.СпособПолученияМатериала = Перечисления.СпособыПолученияМатериаловВСпецификации.ПроизвестиПоСпецификации
					И Предшественники[СтрокаТаблицы.ЭтапРедактирование].Найти(СтрокаТаблицы.ПланироватьНеРанее) <> Неопределено
				ИЛИ СтрокаТаблицы.СпособПолученияМатериала = Перечисления.СпособыПолученияМатериаловВСпецификации.ПроизводитсяНаЭтапе
					И Предшественники[СтрокаТаблицы.ЭтапРедактирование].Найти(СтрокаТаблицы.ИсточникПолученияПолуфабриката) <> Неопределено
				) Тогда
				Продолжить;
			КонецЕсли;
			ТекстСообщения = СтрШаблон(ШаблонСообщения, Формат(СтрокаТаблицы.НомерСтроки, "ЧГ="));
			ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект,
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("МатериалыИУслуги", СтрокаТаблицы.НомерСтроки, "СпособПолученияМатериалаРедактирование"),
				, Отказ);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьПовторВыходныхИзделий(Отказ)
	
	ПредставлениеТЧ = ПредставлениеТаблицыВФорме("ВыходныеИзделия");
	
	Если ВыходныеИзделия.Количество() > 1 Тогда
		
		ШаблонПовторИзделия = НСтр("ru = 'Выходное изделие ""%1"" уже описано в спецификации (см. список ""%2"", строка %3). Дублирование выходных изделий в рамках одной спецификации не допускается.';
									|en = 'The ""%1"" finished product is already described in the bill of materials (see the ""%2"" list, line %3). Duplication of finished products within one bill of materials is not allowed.'");

		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("Номенклатура");
		СтруктураПоиска.Вставить("СвойствоСодержащееНоменклатуру");
		
		Для Каждого СтрокаТаблицы Из ВыходныеИзделия Цикл
			
			СтруктураПоиска.Номенклатура                   = СтрокаТаблицы.Номенклатура;
			СтруктураПоиска.СвойствоСодержащееНоменклатуру = ?(
				СтрокаТаблицы.СпособАвтовыбораНоменклатуры = Перечисления.СпособыАвтовыбораНоменклатуры.ЗадаетсяВСвойствеПродукции,
				СтрокаТаблицы.СвойствоСодержащееНоменклатуру,
				ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.ПустаяСсылка());
			
			НайденныеИзделия = ВыходныеИзделия.НайтиСтроки(СтруктураПоиска);
			Для каждого СтрокаНайдено Из НайденныеИзделия Цикл
				
				Если СтрокаТаблицы.НомерСтроки > СтрокаНайдено.НомерСтроки
					И (СтрокаТаблицы.Характеристика = СтрокаНайдено.Характеристика
						ИЛИ СтрокаТаблицы.Характеристика.Пустая()
						ИЛИ СтрокаНайдено.Характеристика.Пустая()) Тогда
						
						ПредставлениеНоменклатуры = НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
							СтрокаТаблицы.Номенклатура,
							?(СтрокаНайдено.Характеристика.Пустая(),Неопределено,СтрокаТаблицы.Характеристика));
							
						ТекстСообщения = СтрШаблон(ШаблонПовторИзделия, ПредставлениеНоменклатуры, ПредставлениеТЧ, Формат(СтрокаНайдено.НомерСтроки, "ЧГ="));
						
						Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
							"ВыходныеИзделия",
							СтрокаТаблицы.НомерСтроки,
							"Номенклатура");
							
						ОбщегоНазначения.СообщитьПользователю(
							ТекстСообщения,
							ЭтотОбъект,
							Поле,,
							Отказ);
							
						Прервать;
						
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

Процедура ПроверитьЗаполнениеДолейСтоимостиВыходныхИзделия(Отказ)
	
	// Для способов расчета "По плановой стоимости", "По объему", "По весу" проверка не выполняется
	Если СпособРаспределенияЗатратНаВыходныеИзделия <> Перечисления.СпособыРаспределенияЗатратНаВыходныеИзделия.ПоДолямСтоимости Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыРаспределенияЗатрат = Справочники.РесурсныеСпецификации.ПараметрыРаспределенияЗатрат(ЭтотОбъект);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВыходныеИзделия.ЭтапРедактирование КАК Ссылка,
	|	ВыходныеИзделия.НомерСтроки        КАК НомерСтроки,
	|
	|	ВыходныеИзделия.Номенклатура       КАК Номенклатура,
	|	ВыходныеИзделия.Характеристика     КАК Характеристика,
	|	ВыходныеИзделия.ЭтапРедактирование КАК ЭтапРедактирование,
	|
	|	ВыходныеИзделия.Упаковка           КАК Упаковка,
	|	ВыходныеИзделия.КоличествоУпаковок КАК КоличествоУпаковок,
	|
	|	ВыходныеИзделия.ДоляСтоимости      КАК ДоляСтоимости
	|
	|ПОМЕСТИТЬ ТабличнаяЧасть
	|ИЗ
	|	&ВыходныеИзделия КАК ВыходныеИзделия
	|;
	|" + ПроизводствоСервер.ТекстЗапросаПроверитьДолиСтоимостиВыходныхИзделий(ПараметрыРаспределенияЗатрат.ПоляСвязи);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ВыходныеИзделия", ВыходныеИзделия.Выгрузить());
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		
		ПредставлениеТЧ = ПредставлениеТаблицыВФорме("ВыходныеИзделия");
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл;
			
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", "Заполнение", НСтр("ru = 'Доля стоимости';
																												|en = 'Cost share'"), Выборка.НомерСтроки, ПредставлениеТЧ);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ВыходныеИзделия", Выборка.НомерСтроки, "ДоляСтоимости");
			
			ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект,
				Поле,,
				Отказ);
		
			КонецЦикла;
			
	КонецЕсли;
	
КонецПроцедуры

//++ НЕ УТКА

Процедура ПроверитьРеквизитыПорядокОпераций(Отказ)
	
	Если НЕ ПолучитьФункциональнуюОпцию("ХранитьОперацииВРесурсныхСпецификациях") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураПроверок = РедакторПроизводственногоПроцесса.СтруктураПроверокПоследовательностиОпераций();
	ТаблицаОпераций   = РедакторПроизводственногоПроцесса.ТаблицаПроверкиПорядкаЭлементовПроизводственногоПроцессаКонструктор();
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ЭтапыПроизводства.Ссылка                      КАК Этап,
	|	ЭтапыПроизводства.Подразделение               КАК ЭтапПодразделение,
	|	ЭтапыПроизводства.Подразделение.Представление КАК ЭтапПодразделениеПредставление,
	|
	|	СпрОперации.Ссылка                 КАК Операция,
	|	СпрОперации.Представление          КАК Представление,
	|	СпрОперации.НомерОперации          КАК НомерОперации,
	|	СпрОперации.НомерСледующейОперации КАК НомерСледующейОперации,
	|
	|	СпрОперации.ТехнологическийПроцесс                             КАК ТехнологическийПроцесс,
	|	СпрОперации.ТехнологическийПроцесс.Представление               КАК ТехпроцессПредставление,
	|	СпрОперации.ТехнологическийПроцесс.Статус                      КАК ТехпроцессСтатус,
	|	СпрОперации.ТехнологическийПроцесс.Подразделение               КАК ТехпроцессПодразделение,
	|	СпрОперации.ТехнологическийПроцесс.Подразделение.Представление КАК ТехпроцессПодразделениеПредставление,
	|
	|	&КорректностьПоследовательностиКонтрольныхОпераций,
	|
	|	""НомерСледующейОперации""         КАК ПолеНомерСледующейОперации,
	|	МАКСИМУМ(ВЫБОР КОГДА СпрСледующиеОперации.Ссылка ЕСТЬ NULL
	|						И СпрОперации.НомерСледующейОперации <> 0
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ) КАК НетСледующейОперации
	|
	|ИЗ
	|	Справочник.ЭтапыПроизводства КАК ЭтапыПроизводства
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТехнологическиеОперации КАК СпрОперации
	|		ПО СпрОперации.Владелец = ЭтапыПроизводства.Ссылка
	|		И НЕ СпрОперации.ПометкаУдаления
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТехнологическиеОперации КАК СпрСледующиеОперации
	|		ПО СпрОперации.НомерСледующейОперации = СпрСледующиеОперации.НомерОперации
	|			И СпрОперации.Владелец = СпрСледующиеОперации.Владелец
	|			И НЕ СпрСледующиеОперации.ПометкаУдаления
	|
	|ГДЕ
	|	ЭтапыПроизводства.Владелец = &Спецификация
	|	И НЕ ЭтапыПроизводства.ПометкаУдаления
	|	И НЕ СпрОперации.Ссылка ЕСТЬ NULL
	|
	|СГРУППИРОВАТЬ ПО
	|	ЭтапыПроизводства.Ссылка,
	|	СпрОперации.Ссылка,
	|	СпрОперации.НомерОперации,
	|	СпрОперации.НомерСледующейОперации,
	|	СпрОперации.ТехнологическийПроцесс
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЭтапыПроизводства.НомерЭтапа,
	|	СпрОперации.НомерОперации
	|
	|ИТОГИ ПО
	|	Этап
	|";
	
	ТекстКонтрольныеОперации = "";
	Если СтруктураПроверок.Свойство("КонтрольныеОперации") Тогда
		ТекстКонтрольныеОперации = РедакторПроизводственногоПроцесса.ТекстКорректностьПоследовательностиКонтрольныхОпераций();
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&КорректностьПоследовательностиКонтрольныхОпераций," , ТекстКонтрольныеОперации);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Спецификация", Ссылка);
	Запрос.Текст = ТекстЗапроса;
	
	ВыборкаЭтапы = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Этап");
	Пока ВыборкаЭтапы.Следующий() Цикл
		
		ТаблицаОпераций.Очистить();
		
		ВыборкаОперации = ВыборкаЭтапы.Выбрать();
		Пока ВыборкаОперации.Следующий() Цикл
			
			ОперацияОбъект = ВыборкаОперации.Операция.ПолучитьОбъект();
			ОперацияОбъект.ДополнительныеСвойства.Вставить("СтатусСпецификации", Статус);
			
			Если НЕ ОперацияОбъект.ПроверитьЗаполнение() Тогда
				Отказ = Истина;
			КонецЕсли;
			
			Если ОперацияОбъект.СодержитТехнологическийПроцесс Тогда
				
				Если ВыборкаЭтапы.ЭтапПодразделение <> ВыборкаОперации.ТехпроцессПодразделение Тогда
				
					ТекстСообщения = СтрШаблон(
						НСтр("ru = 'Подразделение технологического процесса (""%1"") не соответствует подразделению этапа (""%2"").';
							|en = 'The technological process business unit (""%1"") does not match the stage business unit (""%2"").'"),
						ВыборкаОперации.ТехпроцессПодразделениеПредставление,
						ВыборкаЭтапы.ЭтапПодразделениеПредставление);
						
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ВыборкаОперации.ТехнологическийПроцесс,,, Отказ);
				
				КонецЕсли;
				
				Если ВыборкаОперации.ТехпроцессСтатус <> Перечисления.СтатусыТехнологическихПроцессов.Действует Тогда
					
					ТекстСообщения = СтрШаблон(
						НСтр("ru = 'Технологический процесс ""%1"" должен иметь статус ""Действует"".';
							|en = 'Technological process ""%1"" must be in ""Valid"" status.'"),
						ВыборкаОперации.ТехпроцессПредставление);
					
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ВыборкаОперации.ТехнологическийПроцесс,,, Отказ);
					
				КонецЕсли;
				
			КонецЕсли;
			
			РедакторПроизводственногоПроцесса.ПроверитьЗаполнениеРабочихЦентровОперации(
				ОперацияОбъект, ВыборкаЭтапы.ЭтапПодразделение, ВыборкаОперации.Операция, Отказ);
			
			ЗаполнитьЗначенияСвойств(ТаблицаОпераций.Добавить(), ВыборкаОперации);
			
		КонецЦикла;
		
		Если ТаблицаОпераций.Количество() > 0
			И НЕ РедакторПроизводственногоПроцесса.ПоследовательностьСпискаОперацийПравильная(ТаблицаОпераций, СтруктураПроверок, ВыборкаЭтапы.Этап) Тогда
			Отказ = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьРеквизитыОпераций(Отказ)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТехнологическиеОперации.Ссылка
	|ИЗ
	|	Справочник.ТехнологическиеОперации КАК ТехнологическиеОперации
	|ГДЕ
	|	ТехнологическиеОперации.Спецификация = &Спецификация
	|	И НЕ ТехнологическиеОперации.ПометкаУдаления
	|";
	
	Запрос.УстановитьПараметр("Спецификация", Ссылка);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ОперацияОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ОперацияОбъект.ДополнительныеСвойства.Вставить("СтатусСпецификации", Статус);
		Если НЕ ОперацияОбъект.ПроверитьЗаполнение() Тогда
			Отказ = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеПромежуточногоВыпускаВСтатусеДействует(Отказ) Экспорт
	
	Если ВариантПромежуточногоВыпуска = Перечисления.ВариантыПромежуточногоВыпуска.ТоварДругогоКачества Тогда
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	ИСТИНА
			|ИЗ
			|	Справочник.ЭтапыПроизводства КАК ЭтапыПроизводства
			|ГДЕ
			|	ЭтапыПроизводства.Владелец = &Спецификация
			|	И НЕ ЭтапыПроизводства.ПометкаУдаления
			|
			|СГРУППИРОВАТЬ ПО
			|	ЭтапыПроизводства.НомерЭтапа
			|
			|ИМЕЮЩИЕ
			|	КОЛИЧЕСТВО(ЭтапыПроизводства.Ссылка) > 1
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ ПЕРВЫЕ 1
			|	ИСТИНА
			|ИЗ
			|	Справочник.ЭтапыПроизводства КАК ЭтапыПроизводства
			|ГДЕ
			|	ЭтапыПроизводства.Владелец = &Спецификация
			|	И НЕ ЭтапыПроизводства.ПометкаУдаления
			|	И ЭтапыПроизводства.НомерСледующегоЭтапа <> 0
			|
			|СГРУППИРОВАТЬ ПО
			|	ЭтапыПроизводства.НомерСледующегоЭтапа
			|
			|ИМЕЮЩИЕ
			|	КОЛИЧЕСТВО(ЭтапыПроизводства.Ссылка) > 1");
		Запрос.УстановитьПараметр("Спецификация", Ссылка);
		
		Если Не Запрос.Выполнить().Пустой() Тогда
			ТекстСообщения = НСтр("ru = 'При варианте промежуточного выпуска ""Продукция с качеством Ограниченно годен"" производственный процесс не должен быть расходимым (сходимым).';
									|en = 'If you use the ""Manufactured products with the Suitable to a limited extent quality"" intermediate release option, the production process cannot be divergent (convergent).'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ВариантПромежуточногоВыпуска",, Отказ);
		КонецЕсли;
		
	ИначеЕсли ВариантПромежуточногоВыпуска = Перечисления.ВариантыПромежуточногоВыпуска.НастраиваетсяВручную
		И ПромежуточныйВыпуск.Количество() <> 0 Тогда
		
		ИмяТЧ = "ПромежуточныйВыпуск";
		ПредставлениеТЧ = ПредставлениеТаблицыВФорме(ИмяТЧ);
		
		НомераСтрок = Новый Соответствие;
		ИспользоватьКачествоТоваров = ПолучитьФункциональнуюОпцию("ИспользоватьКачествоТоваров");
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	Номенклатура.Ссылка КАК Ссылка,
			|	Номенклатура.ИспользованиеХарактеристик КАК ИспользованиеХарактеристик
			|ИЗ
			|	Справочник.Номенклатура КАК Номенклатура
			|ГДЕ
			|	Номенклатура.Ссылка В (&Номенклатура)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ЭтапыПроизводства.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.ЭтапыПроизводства КАК ЭтапыПроизводства
			|ГДЕ
			|	ЭтапыПроизводства.Ссылка В (&Этапы)
			|	И ЭтапыПроизводства.НомерСледующегоЭтапа = 0");
		Запрос.УстановитьПараметр("Номенклатура", ПромежуточныйВыпуск.ВыгрузитьКолонку("Номенклатура"));
		Запрос.УстановитьПараметр("Этапы", ПромежуточныйВыпуск.ВыгрузитьКолонку("ЭтапОтправитель"));
		
		РезультатыЗапроса = Запрос.ВыполнитьПакет();
		
		ИспользованиеХарактеристик = Новый Соответствие;
		Выборка = РезультатыЗапроса[0].Выбрать();
		Пока Выборка.Следующий() Цикл
			ИспользованиеХарактеристик.Вставить(Выборка.Ссылка, Выборка.ИспользованиеХарактеристик);
		КонецЦикла;
		
		ПоследниеЭтапыСВыпуском = РезультатыЗапроса[1].Выгрузить().ВыгрузитьКолонку("Ссылка");
		
		Для Индекс = 0 По ПромежуточныйВыпуск.Количество() - 1 Цикл
			Строка = ПромежуточныйВыпуск[Индекс];
			Если Строка.ЭтапОтправитель.Пустая() Тогда
				НомерСтрокиОтправителя = Строка.НомерСтроки;
			ИначеЕсли НомераСтрок[Строка.ЭтапОтправитель] = Неопределено Тогда
				НомерСтрокиОтправителя = 1;
				НомераСтрок.Вставить(Строка.ЭтапОтправитель, 1);
			Иначе
				НомерСтрокиОтправителя = НомераСтрок[Строка.ЭтапОтправитель] + 1 ;
				НомераСтрок[Строка.ЭтапОтправитель] = НомерСтрокиОтправителя;
			КонецЕсли;
			
			Если Строка.ЭтапОтправитель.Пустая() Тогда
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
					"Колонка", "Заполнение", НСтр("ru = 'Отправитель';
													|en = 'Sender'"), НомерСтрокиОтправителя, ПредставлениеТЧ);
				Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТЧ, Строка.НомерСтроки, "ЭтапОтправитель");
				ОбщегоНазначения.СообщитьПользователю(
					ТекстСообщения, ЭтотОбъект, Поле,, Отказ);
			ИначеЕсли ПоследниеЭтапыСВыпуском.Найти(Строка.ЭтапОтправитель) <> Неопределено Тогда
				ТекстСообщения = СтрШаблон(
					НСтр("ru = 'Промежуточный выпуск не может быть на последнем этапе (строка %1 списка %2).';
						|en = 'The intermediate release cannot be in the last step (line %1 of the %2 list).'"),
					НомерСтрокиОтправителя,
					ПредставлениеТЧ);
				Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТЧ, Строка.НомерСтроки, "ЭтапПолучатель");
				ОбщегоНазначения.СообщитьПользователю(
					ТекстСообщения, Строка.ЭтапОтправитель, Поле,, Отказ);
			КонецЕсли;
			
			Если Строка.ЭтапПолучатель.Пустая() Тогда
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
					"Колонка", "Заполнение", НСтр("ru = 'Получатель';
													|en = 'Recipient'"), НомерСтрокиОтправителя, ПредставлениеТЧ);
				Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТЧ, Строка.НомерСтроки, "ЭтапПолучатель");
				ОбщегоНазначения.СообщитьПользователю(
					ТекстСообщения, ?(Строка.ЭтапОтправитель.Пустая(), ЭтотОбъект, Строка.ЭтапОтправитель),Поле,, Отказ);
			КонецЕсли;
			
			Если Строка.Номенклатура.Пустая()
				И Не ИспользоватьКачествоТоваров Тогда
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
					"Колонка", "Заполнение", НСтр("ru = 'Номенклатура';
													|en = 'Item'"), НомерСтрокиОтправителя, ПредставлениеТЧ);
				Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТЧ, Строка.НомерСтроки, "Номенклатура");
				ОбщегоНазначения.СообщитьПользователю(
					ТекстСообщения, ?(Строка.ЭтапОтправитель.Пустая(), ЭтотОбъект, Строка.ЭтапОтправитель),Поле,, Отказ);
			КонецЕсли;
			
			Если Не Строка.Номенклатура.Пустая()
				И Строка.Характеристика.Пустая()
				И (ИспользованиеХарактеристик[Строка.Номенклатура] = Перечисления.ВариантыИспользованияХарактеристикНоменклатуры.ИндивидуальныеДляНоменклатуры
					Или ИспользованиеХарактеристик[Строка.Номенклатура] = Перечисления.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеДляВидаНоменклатуры
					Или ИспользованиеХарактеристик[Строка.Номенклатура] = Перечисления.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеСДругимВидомНоменклатуры) Тогда
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
					"Колонка", "Заполнение", НСтр("ru = 'Характеристика';
													|en = 'Variant'"), НомерСтрокиОтправителя, ПредставлениеТЧ);
				Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТЧ, Строка.НомерСтроки, "Характеристика");
				ОбщегоНазначения.СообщитьПользователю(
					ТекстСообщения, ?(Строка.ЭтапОтправитель.Пустая(), ЭтотОбъект, Строка.ЭтапОтправитель),Поле,, Отказ);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

//-- НЕ УТКА

#КонецОбласти

#Область ВсеСтатусы

//++ НЕ УТКА

Процедура ПроверитьЗаполнениеВходящихИзделий(Отказ)
	
	Если ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Ремонт
		ИЛИ ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Разборка Тогда
		
		РеквизитПроверки = ?(ВариантНазначения = Перечисления.ВариантыНазначенияСпецификации.ВидНоменклатуры,
			"ОсновноеИзделиеВидНоменклатуры",
			"ОсновноеИзделиеНоменклатура");
		
		ТипНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭтотОбъект[РеквизитПроверки], "ТипНоменклатуры");
		
		Если ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Работа Тогда
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не допускается указание работы в поле ""%1""';
											|en = 'It is not allowed to specify work in the ""%1"" field'"), РеквизитПроверки);
			
			ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения,,
				РеквизитПроверки,,
				Отказ);
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

//-- НЕ УТКА

Процедура ПроверитьЗаполнениеМатериалов(Отказ)
	
	ПредставлениеТЧ = ПредставлениеТаблицыВФорме("МатериалыИУслуги");

	Для каждого Строка Из МатериалыИУслуги Цикл
		
		Если ЗначениеЗаполнено(Строка.СпособПолученияМатериала) Тогда
			
			Если УправлениеДаннымиОбИзделияхКлиентСервер.ПолуфабрикатПроизводимыйВПроцессе(Строка) И НЕ Строка.ПроизводитсяВПроцессе Тогда
				
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", "Заполнение", НСтр("ru = 'Производится в процессе';
																													|en = 'Produced in process'"), Строка.НомерСтроки, ПредставлениеТЧ);
				Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("МатериалыИУслуги", Строка.НомерСтроки, "ПроизводитсяВПроцессе");
				
				ОбщегоНазначения.СообщитьПользователю(
					ТекстСообщения,
					ЭтотОбъект,
					Поле,,
					Отказ);
				
			КонецЕсли;
			
		Иначе
			
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", "Заполнение", НСтр("ru = 'Способ получения материала';
																												|en = 'Method of receiving material'"), Строка.НомерСтроки, ПредставлениеТЧ);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("МатериалыИУслуги", Строка.НомерСтроки, "СпособПолученияМатериала");
			
			ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект,
				Поле,,
				Отказ);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьВыборЭтапов(ИмяТаблицы, РеквизитЭтап, Отказ)
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ИСТИНА
	|ИЗ
	|	Справочник.ЭтапыПроизводства КАК Т
	|ГДЕ
	|	Т.Ссылка = &Ссылка
	|	И Т.ПометкаУдаления");
	
	ПредставлениеТЧ = ПредставлениеТаблицыВФорме(ИмяТаблицы);
	ШаблонСообщения = НСтр("ru = 'Не допускается выбор этапов помеченных на удаление (список ""%1"", строка %2).';
							|en = 'Cannot select stages marked for deletion (list ""%1"", line %2).'");
	Для каждого СтрокаТаблицы Из ЭтотОбъект[ИмяТаблицы] Цикл
		
		Если СтрокаТаблицы[РеквизитЭтап].Пустая() Тогда
			Продолжить;
		КонецЕсли;
		
		Запрос.УстановитьПараметр("Ссылка", СтрокаТаблицы[РеквизитЭтап]);
		Если Запрос.Выполнить().Пустой() Тогда
			Продолжить;
		КонецЕсли;
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, ПредставлениеТЧ, Формат(СтрокаТаблицы.НомерСтроки, "ЧГ="));
		Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТаблицы, СтрокаТаблицы.НомерСтроки, РеквизитЭтап);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле,, Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Функция ВыгрузитьМатериалыИУслуги()
	
	Результат = МатериалыИУслуги.Выгрузить(, 
		"НомерСтроки, 
		|Номенклатура, 
		|Характеристика,
		|ЭтапРедактирование, 
		|ПроизводитсяВПроцессе,
		|ИсточникПолученияПолуфабриката");
	
	Результат.Индексы.Добавить("ЭтапРедактирование");
	Результат.Индексы.Добавить("ЭтапРедактирование, Номенклатура");
	Результат.Индексы.Добавить("ЭтапРедактирование, Номенклатура, Характеристика");
	
	Возврат Результат;
	
КонецФункции

Функция ВыгрузитьВыходныеИзделия()
	
	Результат = ВыходныеИзделия.Выгрузить(, 
		"НомерСтроки, 
		|Номенклатура, 
		|Характеристика,
		|ЭтапРедактирование");
	
	Результат.Индексы.Добавить("ЭтапРедактирование");
	
	Возврат Результат;
	
КонецФункции

Функция ВыгрузитьВозвратныеОтходы()
	
	Результат = ВозвратныеОтходы.Выгрузить(, 
		"НомерСтроки, 
		|Номенклатура, 
		|Характеристика,
		|ЭтапРедактирование");
	
	Результат.Индексы.Добавить("ЭтапРедактирование");
	
	Возврат Результат;
	
КонецФункции

//++ НЕ УТКА

Функция ВыгрузитьПромежуточныйВыпуск()
	
	Результат = ПромежуточныйВыпуск.Выгрузить(, 
		"ЭтапОтправитель, 
		|ЭтапПолучатель");
	
	Результат.Индексы.Добавить("ЭтапОтправитель");
	Результат.Индексы.Добавить("ЭтапПолучатель");
	
	Возврат Результат;
	
КонецФункции

//-- НЕ УТКА

Процедура ПроверитьЭтапыПроизводстваНаСтороне(Отказ) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьПроизводствоНаСтороне2_5")
		//++ Устарело_Переработка24
		И Не ПолучитьФункциональнуюОпцию("ИспользоватьПроизводствоНаСтороне")
		//-- Устарело_Переработка24
		И Истина Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА
	|ИЗ
	|	Справочник.ЭтапыПроизводства КАК Таблица
	|ГДЕ
	|	Таблица.Владелец = &Владелец
	|	И Таблица.ПроизводствоНаСтороне
	|	И НЕ Таблица.ПометкаУдаления");
	Запрос.УстановитьПараметр("Владелец", Ссылка);
	
	Если Запрос.Выполнить().Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	// Производство на стороне доступно только для типа производственного процесса "Изготовление, сборка"
	Если ТипПроизводственногоПроцесса <> Перечисления.ТипыПроизводственныхПроцессов.Сборка Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'В спецификации типа ""%1"" не допускается использовать этапы, выполняемые переработчиком';
										|en = 'Cannot use the stages performed by the subcontractor in BOM of the %1 type'"), ТипПроизводственногоПроцесса);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,,, Отказ);
		Возврат;
	КонецЕсли;
	
	// В КА не допускается в многоэтапной спецификации использовать этапы, выполняемые переработчиком
	Если МногоэтапныйПроизводственныйПроцесс И ПолучитьФункциональнуюОпцию("КомплекснаяАвтоматизация") Тогда
		ТекстСообщения = НСтр("ru = 'В многоэтапной спецификации не допускается использовать этапы, выполняемые переработчиком';
								|en = 'Cannot use stages executed by the subcontractor in multi-stage BOM'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,,, Отказ);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	МатериалыИУслуги.НомерСтроки                                                   КАК НомерСтроки,
	|	ВЫРАЗИТЬ(МатериалыИУслуги.Номенклатура КАК Справочник.Номенклатура)            КАК Номенклатура,
	|	ВЫРАЗИТЬ(МатериалыИУслуги.ЭтапРедактирование КАК Справочник.ЭтапыПроизводства) КАК ЭтапРедактирование,
	|	МатериалыИУслуги.ПроизводитсяВПроцессе                                         КАК ПроизводитсяВПроцессе,
	|	ВЫБОР
	|		КОГДА МатериалыИУслуги.ИсточникПолученияПолуфабриката ССЫЛКА Справочник.ЭтапыПроизводства
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ                                                                          КАК ПроизводитсяНаЭтапе,
	|	ВЫБОР
	|		КОГДА МатериалыИУслуги.ИсточникПолученияПолуфабриката ССЫЛКА Справочник.ЭтапыПроизводства
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ                                                                          КАК ПроизводитсяПоСпецификации
	|ПОМЕСТИТЬ ВТТаблица
	|ИЗ
	|	&МатериалыИУслуги КАК МатериалыИУслуги
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокЭтапов.Ссылка                КАК Ссылка,
	|	СписокЭтапов.ПроизводствоНаСтороне КАК ПроизводствоНаСтороне
	|ПОМЕСТИТЬ ВтСписокЭтапов
	|ИЗ
	|	Справочник.ЭтапыПроизводства КАК СписокЭтапов
	|ГДЕ
	|	СписокЭтапов.Владелец = &Спецификация
	|	И НЕ СписокЭтапов.ПометкаУдаления
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка),
	|	МАКСИМУМ(СписокЭтапов.ПроизводствоНаСтороне)
	|ИЗ
	|	Справочник.ЭтапыПроизводства КАК СписокЭтапов
	|ГДЕ
	|	СписокЭтапов.Владелец = &Спецификация
	|	И СписокЭтапов.НомерЭтапа = 1
	|	И НЕ СписокЭтапов.ПометкаУдаления
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СписокЭтапов.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МатериалыИУслуги.НомерСтроки                       КАК НомерСтроки,
	|	МатериалыИУслуги.Номенклатура                      КАК Номенклатура,
	|	ЕСТЬNULL(СписокЭтапов.ПроизводствоНаСтороне, ЛОЖЬ) КАК ПроизводствоНаСтороне,
	|	МатериалыИУслуги.ПроизводитсяВПроцессе             КАК ПроизводитсяВПроцессе,
	|	МатериалыИУслуги.ПроизводитсяНаЭтапе               КАК ПроизводитсяНаЭтапе,
	|	МатериалыИУслуги.ПроизводитсяПоСпецификации        КАК ПроизводитсяПоСпецификации
	|ПОМЕСТИТЬ ВТМатериалыИУслуги
	|ИЗ
	|	ВТТаблица КАК МатериалыИУслуги
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСписокЭтапов КАК СписокЭтапов
	|		ПО МатериалыИУслуги.ЭтапРедактирование = СписокЭтапов.Ссылка
	|;
	|
	|ВЫБРАТЬ
	|	Таблица.Ссылка               КАК Этап,
	|	Таблица.Наименование         КАК НаименованиеЭтапа,
	|	Таблица.НомерЭтапа           КАК НомерЭтапа,
	|	Таблица.НомерСледующегоЭтапа КАК НомерСледующегоЭтапа
	|ИЗ
	|	Справочник.ЭтапыПроизводства КАК Таблица
	|ГДЕ 
	|	Таблица.Владелец = &Спецификация
	|		И Таблица.ПроизводствоНаСтороне
	|		И НЕ Таблица.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерЭтапа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	0 КАК НомерСтроки
	|ГДЕ
	|	ЛОЖЬ
	//++ Устарело_Переработка24
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Т.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	ВТМатериалыИУслуги КАК Т
	|ГДЕ
	|	ВЫБОР
	|			КОГДА Т.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|				И Т.ПроизводствоНаСтороне
	|				И НЕ &ИспользуетсяТолькоПередачаВПереработку2_5
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ
	//-- Устарело_Переработка24
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	0 КАК НомерСтроки
	|ГДЕ
	|	ЛОЖЬ
	//++ Устарело_Производство21
	|
	|ОБЪЕДИНИТЬ ВСЕ
	| 
	|ВЫБРАТЬ
	|	Т.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	ВТМатериалыИУслуги КАК Т
	|ГДЕ
	|	&ИспользуетсяПроизводство21
	|	И ВЫБОР
	|		КОГДА Т.ПроизводитсяВПроцессе
	|			И Т.ПроизводствоНаСтороне
	|			И (НЕ &ИспользуетсяПроизводство22
	|				ИЛИ Т.ПроизводитсяПоСпецификации
	|					И &ИспользуетсяПроизводство22)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	//-- Устарело_Производство21
	|");
	Запрос.УстановитьПараметр("Спецификация", Ссылка);
	
	НастройкиПодсистемыПроизводство = ПроизводствоСерверПовтИсп.НастройкиПодсистемыПроизводство();
	//++ Устарело_Производство21
	Запрос.УстановитьПараметр("ИспользуетсяПроизводство21", НастройкиПодсистемыПроизводство.ИспользуетсяПроизводство21);
	//-- Устарело_Производство21
	Запрос.УстановитьПараметр("ИспользуетсяПроизводство22", НастройкиПодсистемыПроизводство.ИспользуетсяПроизводство22);
	
	//++ Устарело_Переработка24
	Запрос.УстановитьПараметр(
		"ИспользуетсяТолькоПередачаВПереработку2_5",
		НастройкиПодсистемыПроизводство.ИспользуетсяТолькоПередачаВПереработку2_5);
	//-- Устарело_Переработка24
	
	ТаблицаВыходныеИзделия  = ВыгрузитьВыходныеИзделия();
	ТаблицаВозвратныеОтходы = ВыгрузитьВозвратныеОтходы();
	ТаблицаМатериалыИУслуги = ВыгрузитьМатериалыИУслуги();
	//++ НЕ УТКА
	ТаблицаПромежуточныйВыпуск = ВыгрузитьПромежуточныйВыпуск();
	//-- НЕ УТКА
	Запрос.УстановитьПараметр("МатериалыИУслуги", ТаблицаМатериалыИУслуги);
	
	Результат = Запрос.ВыполнитьПакет();
	КоличествоПакетов = Результат.Количество();
	
	// Для этапов выполняемых на стороне должны быть описаны материалы и выходные изделия
	Если Не Результат[КоличествоПакетов - 3].Пустой() Тогда
		
		ШаблонТекстаПовторПродукции = НСтр("ru = 'Номенклатура ""%1"" не может присутствовать одновременно в продукции и в материалах (услугах).';
											|en = 'The ""%1"" products cannot be both in products and materials (services).'");
		ШаблонТекстаПовторПобочногоВыпуска = НСтр("ru = 'Номенклатура ""%1"" не может присутствовать одновременно в побочном выпуске и в материалах (услугах).';
													|en = 'The ""%1"" products cannot be both in side release and materials (services).'");
		
		Если МногоэтапныйПроизводственныйПроцесс Тогда
			
			//++ НЕ УТКА
			ПараметрыОтбора = Новый Структура;
			ПараметрыОтбора.Вставить("ЭтапРедактирование");
			
			ПараметрыОтбораОтправитель = Новый Структура;
			ПараметрыОтбораОтправитель.Вставить("ЭтапОтправитель");
			
			ПараметрыОтбораПолучатель = Новый Структура;
			ПараметрыОтбораПолучатель.Вставить("ЭтапПолучатель");
			
			ПараметрыОтбораПоПустомуЭтапу = Новый Структура;
			ПараметрыОтбораПоПустомуЭтапу.Вставить("ЭтапРедактирование", Справочники.ЭтапыПроизводства.ПустаяСсылка());
			
			ШаблонТекстаНетСтрокВТабличнойЧасти = НСтр("ru = 'Для этапа %1, выполняемого переработчиком, не введено ни одной строки в список ""%2""';
														|en = 'No lines are entered into the ""%2"" list for stage %1 performed by data subcontractor'");
			
			Выборка = Результат[КоличествоПакетов - 3].Выбрать();
			
			Пока Выборка.Следующий() Цикл
				
				ПараметрыОтбора.ЭтапРедактирование = Выборка.Этап;
				
				МассивСтрокВыходныеИзделия = ТаблицаВыходныеИзделия.НайтиСтроки(ПараметрыОтбора);
				
				Если Выборка.НомерСледующегоЭтапа = 0 Тогда
					
					ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивСтрокВыходныеИзделия,
						ТаблицаВыходныеИзделия.НайтиСтроки(ПараметрыОтбораПоПустомуЭтапу));
						
				КонецЕсли;
				
				МассивСтрокВозвратныеОтходы = ТаблицаВозвратныеОтходы.НайтиСтроки(ПараметрыОтбора);
				
				Если Выборка.НомерСледующегоЭтапа = 0 Тогда
					
					ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивСтрокВозвратныеОтходы,
						ТаблицаВозвратныеОтходы.НайтиСтроки(ПараметрыОтбораПоПустомуЭтапу));
						
				КонецЕсли;
				
				// В этапе на стороне должно быть хотя бы одно выходное изделие
				// * в старой концепции - по рассчитываемой стоимости
				// * в новой концепции: 
				// ** в последнем этапе - по рассчитываемой стоимости
				// ** в остальных этапах - по фиксируемой стоимости
				Если НастройкиПодсистемыПроизводство.ИспользуетсяПроизводство22 Тогда
					
					ЕстьПромежуточныйВыпуск = Ложь;
					Если ВариантПромежуточногоВыпуска = Перечисления.ВариантыПромежуточногоВыпуска.ТоварДругогоКачества Тогда
						ЕстьПромежуточныйВыпуск = Истина;
					ИначеЕсли ВариантПромежуточногоВыпуска = Перечисления.ВариантыПромежуточногоВыпуска.НастраиваетсяВручную Тогда
						ПараметрыОтбораОтправитель.ЭтапОтправитель = Выборка.Этап;
						МассивСтрокПромежуточныйВыпуск = ТаблицаПромежуточныйВыпуск.НайтиСтроки(ПараметрыОтбораОтправитель);
						ЕстьПромежуточныйВыпуск = МассивСтрокПромежуточныйВыпуск.ВГраница() <> -1;
					КонецЕсли;
					
					// проверяем все этапы за исключением последнего, последний проверит платформа
					Если Выборка.НомерСледующегоЭтапа <> 0
						И МассивСтрокВозвратныеОтходы.ВГраница() = -1 
						И Не ЕстьПромежуточныйВыпуск Тогда
						
						ТекстСообщения = СтрШаблон(ШаблонТекстаНетСтрокВТабличнойЧасти, Выборка.НаименованиеЭтапа, ПредставлениеТаблицыВФорме("ВозвратныеОтходы"));
						
						ОбщегоНазначения.СообщитьПользователю(
							ТекстСообщения, 
							ЭтотОбъект, 
							"ВозвратныеОтходы",, 
							Отказ); 
						
					КонецЕсли;
					
				Иначе
					
					Если МассивСтрокВыходныеИзделия.ВГраница() = -1 Тогда
						
						ТекстСообщения = СтрШаблон(ШаблонТекстаНетСтрокВТабличнойЧасти, Выборка.НаименованиеЭтапа, ПредставлениеТаблицыВФорме("ВыходныеИзделия"));
						
						ОбщегоНазначения.СообщитьПользователю(
							ТекстСообщения,
							ЭтотОбъект,
							"ВыходныеИзделия",,
							Отказ);
						
					КонецЕсли;
					
				КонецЕсли;
				
				МассивСтрокМатериалыИУслуги = ТаблицаМатериалыИУслуги.НайтиСтроки(ПараметрыОтбора);
				
				Если Выборка.НомерЭтапа = 1 Тогда
					
					ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивСтрокМатериалыИУслуги,
						ТаблицаМатериалыИУслуги.НайтиСтроки(ПараметрыОтбораПоПустомуЭтапу));
						
				КонецЕсли;
				
				ЕстьПотреблениеПромежуточногоВыпуска = Ложь;
				Если ВариантПромежуточногоВыпуска = Перечисления.ВариантыПромежуточногоВыпуска.ТоварДругогоКачества Тогда
					ЕстьПотреблениеПромежуточногоВыпуска = Истина;
				ИначеЕсли ВариантПромежуточногоВыпуска = Перечисления.ВариантыПромежуточногоВыпуска.НастраиваетсяВручную Тогда
					ПараметрыОтбораПолучатель.ЭтапПолучатель = Выборка.Этап;
					МассивСтрокПромежуточныйВыпуск = ТаблицаПромежуточныйВыпуск.НайтиСтроки(ПараметрыОтбораПолучатель);
					ЕстьПотреблениеПромежуточногоВыпуска = МассивСтрокПромежуточныйВыпуск.ВГраница() <> -1;
				КонецЕсли;
					
				// Для этапа, выполняемого переработчиком, должен быть описан хотя бы один материал
				Если МассивСтрокМатериалыИУслуги.ВГраница() = -1
					И Не ЕстьПотреблениеПромежуточногоВыпуска Тогда
					
					ТекстСообщения = СтрШаблон(ШаблонТекстаНетСтрокВТабличнойЧасти, Выборка.НаименованиеЭтапа, ПредставлениеТаблицыВФорме("МатериалыИУслуги"));
					
					ОбщегоНазначения.СообщитьПользователю(
						ТекстСообщения, 
						ЭтотОбъект, 
						"МатериалыИУслуги",, 
						Отказ);
						
				КонецЕсли;
				
				//++ Устарело_Производство21
				// В старой концепции передача одной и той же номенклатуры между этапами запрещена
				Если НастройкиПодсистемыПроизводство.ИспользуетсяПроизводство21 Тогда
					
					МассивПроверок = Новый Массив;
					
					ПараметрыПроверки = Новый Структура;
					ПараметрыПроверки.Вставить("ИмяТабличнойЧасти", "ВыходныеИзделия");
					ПараметрыПроверки.Вставить("Коллекция",         МассивСтрокВыходныеИзделия);
					ПараметрыПроверки.Вставить("ШаблонТекста",      ШаблонТекстаПовторПродукции);
					
					МассивПроверок.Добавить(ПараметрыПроверки);
					
					ПараметрыПроверки = Новый Структура;
					ПараметрыПроверки.Вставить("ИмяТабличнойЧасти", "ВозвратныеОтходы");
					ПараметрыПроверки.Вставить("Коллекция",         МассивСтрокВозвратныеОтходы);
					ПараметрыПроверки.Вставить("ШаблонТекста",      ШаблонТекстаПовторПобочногоВыпуска);
					
					МассивПроверок.Добавить(ПараметрыПроверки);
					
					Для Каждого ПараметрыПроверки Из МассивПроверок Цикл
						
						Коллекция = ПараметрыПроверки.Коллекция; // СправочникТабличнаяЧасть.РесурсныеСпецификации.ВыходныеИзделия,СправочникТабличнаяЧасть.РесурсныеСпецификации.ВозвратныеОтходы
						
						Для Каждого Строка Из Коллекция Цикл
							
							СтруктураПоиска = Новый Структура;
							СтруктураПоиска.Вставить("ЭтапРедактирование", Выборка.Этап);
							СтруктураПоиска.Вставить("Номенклатура", Строка.Номенклатура);
							
							Если ЗначениеЗаполнено(Строка.Характеристика) Тогда
								СтруктураПоиска.Вставить("Характеристика", Строка.Характеристика);
							КонецЕсли; 
							
							ЕстьДублиМатериалыИУслуги = ТаблицаМатериалыИУслуги.НайтиСтроки(СтруктураПоиска).ВГраница() <> -1;
							
							Если НЕ ЕстьДублиМатериалыИУслуги И Выборка.НомерЭтапа = 1 Тогда
								
								СтруктураПоиска.ЭтапРедактирование = Справочники.ЭтапыПроизводства.ПустаяСсылка();
								
								ЕстьДублиМатериалыИУслуги = ТаблицаМатериалыИУслуги.НайтиСтроки(СтруктураПоиска).ВГраница() <> -1;
								
							КонецЕсли;
							
							Если ЕстьДублиМатериалыИУслуги Тогда
							
								ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									ПараметрыПроверки.ШаблонТекста, 
									НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
										Строка.Номенклатура, 
										Строка.Характеристика));
								
								ОбщегоНазначения.СообщитьПользователю(
									ТекстСообщения, 
									ЭтотОбъект, 
									ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
										ПараметрыПроверки.ИмяТабличнойЧасти, 
										Строка.НомерСтроки, 
										"Номенклатура"),,
									Отказ);
								
							КонецЕсли;
							
						КонецЦикла;
						
					КонецЦикла;
					
				КонецЕсли;
				//-- Устарело_Производство21
				
			КонецЦикла;
			//-- НЕ УТКА
			
		Иначе
			
			//++ Устарело_Производство21
			// В старой концепции передача одной и той же номенклатуры между этапами запрещена
			Если НастройкиПодсистемыПроизводство.ИспользуетсяПроизводство21 Тогда
					
				МассивПроверок = Новый Массив;
				
				ПараметрыПроверки = Новый Структура;
				ПараметрыПроверки.Вставить("ИмяТабличнойЧасти", "ВыходныеИзделия");
				ПараметрыПроверки.Вставить("Коллекция",         ВыходныеИзделия);
				ПараметрыПроверки.Вставить("ШаблонТекста",      ШаблонТекстаПовторПродукции);
				
				МассивПроверок.Добавить(ПараметрыПроверки);
				
				ПараметрыПроверки = Новый Структура;
				ПараметрыПроверки.Вставить("ИмяТабличнойЧасти", "ВозвратныеОтходы");
				ПараметрыПроверки.Вставить("Коллекция",         ВозвратныеОтходы);
				ПараметрыПроверки.Вставить("ШаблонТекста",      ШаблонТекстаПовторПобочногоВыпуска);
				
				МассивПроверок.Добавить(ПараметрыПроверки);
				
				Для Каждого ПараметрыПроверки Из МассивПроверок Цикл
					
					Коллекция = ПараметрыПроверки.Коллекция; // СправочникТабличнаяЧасть.РесурсныеСпецификации.ВыходныеИзделия,СправочникТабличнаяЧасть.РесурсныеСпецификации.ВозвратныеОтходы
					
					Для Каждого Строка Из Коллекция Цикл
					
						СтруктураПоиска = Новый Структура("Номенклатура", Строка.Номенклатура);
						Если ЗначениеЗаполнено(Строка.Характеристика) Тогда
							СтруктураПоиска.Вставить("Характеристика", Строка.Характеристика);
						КонецЕсли; 
						
						Если МатериалыИУслуги.НайтиСтроки(СтруктураПоиска).ВГраница() <> -1 Тогда
							
							ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								ПараметрыПроверки.ШаблонТекста, 
								НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
									Строка.Номенклатура, 
									Строка.Характеристика));
								
							ОбщегоНазначения.СообщитьПользователю(
								ТекстСообщения, 
								ЭтотОбъект, 
								ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
									ПараметрыПроверки.ИмяТабличнойЧасти, 
									Строка.НомерСтроки, 
									"Номенклатура"),,
								Отказ);
							
						КонецЕсли;
						
					КонецЦикла;
					
				КонецЦикла;
				
			КонецЕсли;
			//-- Устарело_Производство21
			
			// Для этапа, выполняемого переработчиком, должен быть описан хотя бы один материал
			Если ТаблицаМатериалыИУслуги.Количество() = 0 Тогда
				
				ШаблонТекста = НСтр("ru = 'Не введено ни одной строки в список ""Материалы и услуги""';
									|en = 'No line is entered into the ""Materials and services"" list'");
				
				ОбщегоНазначения.СообщитьПользователю(
					ШаблонТекста, 
					ЭтотОбъект, 
					"МатериалыИУслуги",, 
					Отказ); 
					
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// В списке ""Материалы и работы"" не допускается указание работ для этапов, выполняемых переработчиком
	Если Не Результат[КоличествоПакетов - 2].Пустой() Тогда
		
		ШаблонТекста = НСтр("ru = 'Указание работ для этапов, выполняемых переработчиком, допускается при отключенном учете по версии 2.4 (см. строку %1 списка ""Материалы и работы"").';
							|en = 'Work specification for the subcontractor stages is available if accounting of version 2.4 is disabled (see the %1 line of the ""Materials and works"" list).'");
		
		Выборка = Результат[КоличествоПакетов - 2].Выбрать();
			
		Пока Выборка.Следующий() Цикл
			
			Поле           = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("МатериалыИУслуги", Выборка.НомерСтроки, "Номенклатура");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста, Выборка.НомерСтроки);
			
			ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения, 
				ЭтотОбъект, 
				Поле,, 
				Отказ);
			
		КонецЦикла;
		
	КонецЕсли;
	
	//++ Устарело_Производство21
	// В старой концепции в списке ""Материалы и работы"" не допускается указание 
	//   полуфабрикатов производимых в процессе для этапов, выполняемых переработчиком
	Если Не Результат[КоличествоПакетов - 1].Пустой() Тогда
		
		ШаблонТекста = НСтр("ru = 'В списке ""Материалы и работы"" не допускается указание полуфабрикатов производимых в процессе для этапов, выполняемых переработчиком (см. строку %1).';
							|en = 'In the ""Materials and works"" list, it is not allowed to specify semi-finished products manufactured in the process for the stages executed by the toller (see line %1).'");
		
		Выборка = Результат[КоличествоПакетов - 1].Выбрать();
			
		Пока Выборка.Следующий() Цикл
			
			Поле           = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("МатериалыИУслуги", Выборка.НомерСтроки, "Номенклатура");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста, Выборка.НомерСтроки);
			
			ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения, 
				ЭтотОбъект, 
				Поле,, 
				Отказ);
			
		КонецЦикла;
		
	КонецЕсли;
	//-- Устарело_Производство21
	
КонецПроцедуры

// Проверяет только дополнительные реквизиты при неинтерактивной работе со спецификацией.
// Для статуса спецификации "Действует" проверка выполняется в общей процедуре проверки производственного процесса.
// 
// Параметры:
//  Отказ - Булево - Отказ
Процедура ПроверитьДополнительныеРеквизитыПроизводственногоПроцесса(Отказ)
	
	Если Статус = Перечисления.СтатусыСпецификаций.Действует
			ИЛИ ДополнительныеСвойства.Свойство("ИнтерактивнаяПроверкаПройдена") Тогда
		Возврат;
	КонецЕсли;
	
	Сообщения = Новый Массив(ПолучитьСообщенияПользователю(Истина));
	
	ПроверитьРеквизитыЭтапов(Отказ);
	
	//++ НЕ УТКА
	ПроверитьРеквизитыОпераций(Отказ);
	//-- НЕ УТКА
	
	Если Отказ Тогда
		ЗаписатьОшибкиПроверкиПроизводственногоПроцессаВЖурналРегистрации();
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ТекстСообщенияОшибкиПроизводственногоПроцесса();
		Сообщение.УстановитьДанные(ЭтотОбъект);
		Сообщения.Добавить(Сообщение);
	КонецЕсли;
	
	Для каждого Сообщение Из Сообщения Цикл
		Сообщение.Сообщить();
	КонецЦикла;
	
КонецПроцедуры

Функция ТекстСообщенияОшибкиПроизводственногоПроцесса()
	
	Возврат НСтр("ru = 'Производственный процесс содержит ошибки. Воспользуйтесь проверкой и внесите необходимые исправления в форме спецификации.';
				|en = 'The production process contains errors. Use the check and make any necessary corrections in the bill of materials form.'")
	
КонецФУнкции

Процедура ЗаписатьОшибкиПроверкиПроизводственногоПроцессаВЖурналРегистрации()
	
	ИмяСобытия = НСтр("ru = 'Ресурсная спецификация.';
						|en = 'Bill of materials.'", ОбщегоНазначения.КодОсновногоЯзыка());
	ШаблонСообщения = НСтр("ru = '%1 %2';
							|en = '%1 %2'");
	
	Сообщения = ПолучитьСообщенияПользователю(Истина);
	Для каждого Сообщение Из Сообщения Цикл
		ТекстСообщения = СтрШаблон(
			ШаблонСообщения,
			Сообщение.Текст,
			?(ЗначениеЗаполнено(Сообщение.КлючДанных), "("+Сообщение.КлючДанных+")" ,""));
		ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Предупреждение,, Ссылка, ТекстСообщения);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область Прочее

Процедура ПроверитьСкорректироватьЭтапыПроизводства(Отказ)
	
	Если ДополнительныеСвойства.Свойство("ИнтерактивнаяПроверкаПройдена")
			И НЕ ЭтоТехнологическийНабор() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Этапы.Ссылка,
	|	Этапы.НомерЭтапа,
	|	Этапы.НомерСледующегоЭтапа,
	|	Этапы.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий КАК ОдновременноПроизводимоеКоличество
	|ИЗ
	|	Справочник.ЭтапыПроизводства КАК Этапы
	|ГДЕ
	|	Этапы.Владелец = &Спецификация
	|	И НЕ Этапы.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	Этапы.НомерЭтапа");
	Запрос.УстановитьПараметр("Спецификация", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	СтатусДействует = (Статус = Перечисления.СтатусыСпецификаций.Действует);
	МногоэтапныйПроизводственныйПроцесс = Выборка.Количество() > 1 И НЕ ЭтоТехнологическийНабор();
	
	Пока Выборка.Следующий() Цикл
		
		Попытка
			
			Если ЭтоТехнологическийНабор() Тогда
				
				ЭтапОбъект = Выборка.Ссылка.ПолучитьОбъект();
				ЭтапОбъект.ДополнительныеСвойства.Вставить("РазрешитьЗапись");
				ЭтапОбъект.УстановитьПометкуУдаления(Истина);
				
			Иначе
			
				ТребуетсяПеренумеровать = Не МногоэтапныйПроизводственныйПроцесс И (Выборка.НомерЭтапа <> 1 ИЛИ Выборка.НомерСледующегоЭтапа <> 0);
				
				Если СтатусДействует И Не ВыпускПроизвольнымиПорциями
					И Выборка.ОдновременноПроизводимоеКоличество <> Цел(Выборка.ОдновременноПроизводимоеКоличество) Тогда
					ТребуетсяОкруглитьКоличество = Истина;
				Иначе
					ТребуетсяОкруглитьКоличество = Ложь;
				КонецЕсли;
				
				Если ТребуетсяПеренумеровать ИЛИ ТребуетсяОкруглитьКоличество Тогда
					
					ЭтапОбъект = Выборка.Ссылка.ПолучитьОбъект(); // СправочникОбъект.ЭтапыПроизводства
					
					Если ТребуетсяПеренумеровать Тогда
						ЭтапОбъект.НомерЭтапа = 1;
						ЭтапОбъект.НомерСледующегоЭтапа = 0;
					КонецЕсли;
					
					Если ТребуетсяОкруглитьКоличество Тогда
						ЭтапОбъект.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий = Окр(ЭтапОбъект.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий);
					КонецЕсли;
					
					ЭтапОбъект.Записать();
					
				КонецЕсли;
			
			КонецЕсли;
			
		Исключение
			
			ИмяСобытия = НСтр("ru = 'Ресурсная спецификация';
								|en = 'Bill of materials'", ОбщегоНазначения.КодОсновногоЯзыка());
			ТекстШаблон = НСтр("ru = 'Не удалось скорректировать этапы ресурсной спецификации %1';
								|en = 'Cannot correct the bill of materials stages %1'");
			
			ТекстСообщения = СтрШаблон(ТекстШаблон, НСтр("ru = '(подробнее см. в журнале регистрации)';
														|en = '(for more details, see the event log)'"));
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,, Отказ);
			
			ТекстСообщения = СтрШаблон(ТекстШаблон, НСтр("ru = 'по причине:';
														|en = 'Reason:'") + " " + ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ИмяСобытия,
				УровеньЖурналаРегистрации.Предупреждение,
				,
				Ссылка,
				ТекстСообщения);
			
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗарегистрироватьРасчетДлительностиПоСпецификации() Экспорт
	
	// При изменении статуса спецификации запускаем полный пересчет вторичных данных:
	// - рассчитываем всю очередь заданий, 
	// - рассчитываем количество дней до окончания.
	
	Если Статус = Перечисления.СтатусыСпецификаций.ВРазработке
			ИЛИ ЭтоТехнологическийНабор() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА РесурсныеСпецификации.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификаций.ВРазработке)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПолныйПересчет
	|ИЗ
	|	Справочник.РесурсныеСпецификации КАК РесурсныеСпецификации
	|ГДЕ
	|	( РесурсныеСпецификации.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификаций.ВРазработке)
	|		ИЛИ НЕ (&ОптимальнаяПартияВыпуска <> 0
	|				ИЛИ &МинимальнаяПартияВыпуска <> 0
	|				ИЛИ &ЕстьПараметризацияРесурсов
	|				ИЛИ &ЕстьВложенныеСпецификации
	|				ИЛИ &ЕстьРасчетВероятности
	|				ИЛИ &ВыпускПроизвольнымиПорциями = ЛОЖЬ
	|				ИЛИ &ВариантНазначения = ЗНАЧЕНИЕ(Перечисление.ВариантыНазначенияСпецификации.СписокНоменклатуры)
	|				ИЛИ &ЕстьНекратныеНормативыВРЦ) <> (НЕ (РесурсныеСпецификации.ОптимальнаяПартияВыпуска <> 0
	|														ИЛИ РесурсныеСпецификации.МинимальнаяПартияВыпуска <> 0
	|														ИЛИ РесурсныеСпецификации.ЕстьПараметризацияРесурсов
	|														ИЛИ РесурсныеСпецификации.ЕстьВложенныеСпецификации
	|														ИЛИ РесурсныеСпецификации.ЕстьРасчетВероятности
	|														ИЛИ РесурсныеСпецификации.ВыпускПроизвольнымиПорциями = ЛОЖЬ
	|														ИЛИ РесурсныеСпецификации.ВариантНазначения = ЗНАЧЕНИЕ(Перечисление.ВариантыНазначенияСпецификации.СписокНоменклатуры)
	|														ИЛИ РесурсныеСпецификации.ЕстьНекратныеНормативыВРЦ))
	|	)
	|	И РесурсныеСпецификации.Ссылка = &Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Запрос.УстановитьПараметр("ОптимальнаяПартияВыпуска",    ОптимальнаяПартияВыпуска);
	Запрос.УстановитьПараметр("МинимальнаяПартияВыпуска",    МинимальнаяПартияВыпуска);
	Запрос.УстановитьПараметр("ЕстьПараметризацияРесурсов",  ЕстьПараметризацияРесурсов);
	Запрос.УстановитьПараметр("ЕстьВложенныеСпецификации",   ЕстьВложенныеСпецификации);
	Запрос.УстановитьПараметр("ЕстьРасчетВероятности",       ЕстьРасчетВероятности);
	Запрос.УстановитьПараметр("ЕстьНекратныеНормативыВРЦ",   ЕстьНекратныеНормативыВРЦ);
	Запрос.УстановитьПараметр("ВыпускПроизвольнымиПорциями", ВыпускПроизвольнымиПорциями);
	Запрос.УстановитьПараметр("ВариантНазначения",           ВариантНазначения);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		ДополнительныеСвойства.Вставить("РассчитатьДлительностьПроизводства", Выборка.ПолныйПересчет);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОчиститьВыборЭтапов(ТабличнаяЧасть)

	Для каждого СтрокаТаблицы Из ТабличнаяЧасть Цикл
		СтрокаТаблицы.Этап = Справочники.ЭтапыПроизводства.ПустаяСсылка();
		СтрокаТаблицы.ЭтапРедактирование = Справочники.ЭтапыПроизводства.ПустаяСсылка();
		//++ НЕ УТКА
		СтрокаТаблицы.Операция = Справочники.ТехнологическиеОперации.ПустаяСсылка();
		СтрокаТаблицы.ОперацияРедактирование = Справочники.ТехнологическиеОперации.ПустаяСсылка();
		//-- НЕ УТКА
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьСлужебныеРеквизиты()
	
	ПроверитьЗаполнитьРеквизитыОсновногоИзделия();
	
	ЗаполнитьСлужебныеРеквизитыПовтИсп(); // отдельный метод требуется для обработчика обновления
	
	Если ЕстьПараметризацияРесурсов Тогда
		РазрешитьВыборДляИзделийПобочногоВыхода = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьСлужебныеРеквизитыПовтИсп() Экспорт
	
	Если Истина
		//++ НЕ УТКА
		И (ВариантНазначения = Перечисления.ВариантыНазначенияСпецификации.Номенклатура
			ИЛИ ВариантНазначения = Перечисления.ВариантыНазначенияСпецификации.ВидНоменклатуры)
		//-- НЕ УТКА
		И ( Статус = Перечисления.СтатусыСпецификаций.ВРазработке ) Тогда
		
		ЕстьУточняемоеОсновноеИзделие      = Ложь;
//++ НЕ УТКА
		ЕстьПараметризацияРесурсов         = Ложь;
		ЕстьВложенныеСпецификации         = Ложь;
		ЕстьРасчетВероятности              = Ложь;
		ЕстьНекратныеНормативыВРЦ          = Ложь;
//-- НЕ УТКА
	
	Иначе
		
		ИтогиПоЭтапам = Новый Структура("ЕстьПараметризацияРесурсов, ЕстьНекратныеНормативыВРЦ", Ложь, Ложь);
//++ НЕ УТКА
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ИСТИНА В
		|				(ВЫБРАТЬ ПЕРВЫЕ 1
		|					ИСТИНА
		|				ИЗ
		|					Справочник.ЭтапыПроизводства.ВидыРабочихЦентров КАК Таблица
		|				ГДЕ
		|					Таблица.Ссылка.Владелец = &Спецификация
		|					И НЕ (ВЫРАЗИТЬ(Таблица.АлгоритмРасчетаКоличества КАК СТРОКА(100))) = """"
		|					И НЕ Таблица.Ссылка.ПометкаУдаления)
		|	
		|			ИЛИ ИСТИНА В
		|	
		|				(ВЫБРАТЬ ПЕРВЫЕ 1
		|					ИСТИНА
		|				ИЗ
		|					Справочник.ЭтапыПроизводства.АльтернативныеВидыРабочихЦентров КАК Таблица
		|				ГДЕ
		|					Таблица.Ссылка.Владелец = &Спецификация
		|					И НЕ (ВЫРАЗИТЬ(Таблица.АлгоритмРасчетаКоличества КАК СТРОКА(100))) = """"
		|					И НЕ Таблица.Ссылка.ПометкаУдаления)
		|
		|			ИЛИ ИСТИНА В
		|
		|				(ВЫБРАТЬ ПЕРВЫЕ 1
		|					ИСТИНА
		|				ИЗ
		|					Справочник.ЭтапыПроизводства.ОтборПоСвойствам КАК Таблица
		|				ГДЕ
		|					Таблица.Ссылка.Владелец = &Спецификация
		|					И НЕ Таблица.Ссылка.ПометкаУдаления)
		|
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЕстьПараметризацияРесурсов,
		|	ВЫБОР
		|		КОГДА ИСТИНА В
		|				(ВЫБРАТЬ ПЕРВЫЕ 1
		|					ИСТИНА
		|				ИЗ
		|					Справочник.ЭтапыПроизводства КАК Таблица
		|				ГДЕ
		|					Таблица.Владелец = &Спецификация
		|					И НЕ Таблица.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий В (0, 1)
		|					И НЕ Таблица.ПометкаУдаления)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЕстьНекратныеНормативыВРЦ");
		Запрос.УстановитьПараметр("Спецификация", Ссылка);
		
		УстановитьПривилегированныйРежим(Истина);
		ЗаполнитьЗначенияСвойств(ИтогиПоЭтапам, Запрос.Выполнить().Выгрузить()[0]);
		УстановитьПривилегированныйРежим(Ложь);
//-- НЕ УТКА
		
		// используется параметризация основного изделия спецификации
		#Область ЕстьУточняемоеОсновноеИзделие
		
		ЕстьУточняемоеОсновноеИзделие = (ВариантНазначения = Перечисления.ВариантыНазначенияСпецификации.ВидНоменклатуры)
											ИЛИ (ОсновноеИзделиеХарактеристика.Пустая()
													И Справочники.Номенклатура.ХарактеристикиИспользуются(ОсновноеИзделиеНоменклатура));
		
		#КонецОбласти
		
//++ НЕ УТКА

		// используется параметризация ресурсов спецификации
		#Область ЕстьПараметризацияРесурсов
		
		ЕстьПараметризацияРесурсов = Ложь;
		Если ПолучитьФункциональнуюОпцию("ИспользоватьПараметризациюРесурсныхСпецификаций") Тогда
			
			ЕстьПараметризацияРесурсов = ИтогиПоЭтапам.ЕстьПараметризацияРесурсов
				ИЛИ ОтборПоСвойствам.Количество();
			
			Если Не ЕстьПараметризацияРесурсов Тогда
				
				СписокТЧ = Новый Массив;
				СписокТЧ.Добавить("ВыходныеИзделия");
				СписокТЧ.Добавить("МатериалыИУслуги");
				СписокТЧ.Добавить("ВозвратныеОтходы");
				СписокТЧ.Добавить("Трудозатраты");
				
				Для каждого ИмяТЧ Из СписокТЧ Цикл
					Для каждого Строка Из ЭтотОбъект[ИмяТЧ] Цикл
						Если Не ПустаяСтрока(Строка.АлгоритмРасчетаКоличества)
							ИЛИ ИмяТЧ <> "Трудозатраты"
								И (Строка.СпособАвтовыбораНоменклатуры = Перечисления.СпособыАвтовыбораНоменклатуры.ЗадаетсяВСвойствеПродукции
									ИЛИ Строка.СпособАвтовыбораХарактеристики = Перечисления.СпособыАвтовыбораХарактеристики.ПодбираетсяПоСвойствамПродукции
									ИЛИ Строка.СпособАвтовыбораХарактеристики = Перечисления.СпособыАвтовыбораХарактеристики.ПодбираетсяПоАлгоритму) Тогда
							ЕстьПараметризацияРесурсов = Истина;
							Прервать;
						КонецЕсли;
					КонецЦикла;
					Если ЕстьПараметризацияРесурсов Тогда
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
		#КонецОбласти
		
		// используется производство полуфабрикатов в процессе или наборы
		#Область ЕстьВложенныеСпецификации
		
		ЕстьВложенныеСпецификации =
			МатериалыИУслуги.Найти(Перечисления.СпособыПолученияМатериаловВСпецификации.ПроизвестиПоСпецификации, "СпособПолученияМатериала") <> Неопределено;
		Если НЕ ЕстьВложенныеСпецификации Тогда
			ЗначенияРеквизитов = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(МатериалыИУслуги.ВыгрузитьКолонку("Номенклатура"), "ТипНоменклатуры");
			Для каждого КлючИЗначение Из ЗначенияРеквизитов Цикл
				Если КлючИЗначение.Значение = Перечисления.ТипыНоменклатуры.Набор Тогда
					ЕстьВложенныеСпецификации = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		#КонецОбласти
		
		// используется расчет вероятности применения
		#Область ЕстьРасчетВероятности
		
		ЕстьРасчетВероятности = Ложь;
		Для каждого Строка Из ВыходныеИзделия Цикл
			Если Строка.ПроцентБрака = 0 Тогда
				Продолжить;
			КонецЕсли;
			ЕстьРасчетВероятности = Истина;
			Прервать;
		КонецЦикла;
		Если Не ЕстьРасчетВероятности Тогда
			Для каждого Строка Из МатериалыИУслуги Цикл
				Если Строка.Вероятность = 0 Тогда
					Продолжить;
				КонецЕсли;
				ЕстьРасчетВероятности = Истина;
				Прервать;
			КонецЦикла;
		КонецЕсли;
		
		#КонецОбласти
		
//-- НЕ УТКА
		
		// некратный выпуск изделий
		#Область ЕстьНекратныйВыпуск
		
		ЕстьНекратныйВыпуск = Ложь;
		Если НЕ ВариантНазначения = Перечисления.ВариантыНазначенияСпецификации.ВидНоменклатуры Тогда
			Если (ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Ремонт
				ИЛИ ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Разборка) Тогда
				Количество = ОсновноеИзделиеКоличествоУпаковок * Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(ОсновноеИзделиеУпаковка, ОсновноеИзделиеНоменклатура);
				ЕстьНекратныйВыпуск = НЕ ( ВыпускПроизвольнымиПорциями ИЛИ Количество = 1 И УправлениеДаннымиОбИзделиях.ШтучноеИзделие(ОсновноеИзделиеНоменклатура));
			Иначе
				ЕстьНекратныйВыпуск = ( ВыходныеИзделия.Количество() > 1 );
				Если Не ЕстьНекратныйВыпуск Тогда
					Для каждого Строка Из ВыходныеИзделия Цикл
						Количество = Строка.КоличествоУпаковок * Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(Строка.Упаковка, Строка.Номенклатура);
						ЕстьНекратныйВыпуск = НЕ ( ВыпускПроизвольнымиПорциями ИЛИ Количество = 1 И УправлениеДаннымиОбИзделиях.ШтучноеИзделие(ОсновноеИзделиеНоменклатура));
						Прервать;
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		#КонецОбласти
		
//++ НЕ УТКА

		// некратные нормативы загрузки оборудования
		#Область ЕстьНекратныеНормативыВРЦ
			ЕстьНекратныеНормативыВРЦ = ИтогиПоЭтапам.ЕстьНекратныеНормативыВРЦ;
		#КонецОбласти
	
//-- НЕ УТКА
	КонецЕсли;
	
КонецПроцедуры

Процедура ОчиститьНеиспользуемыеДанные()
	
	ИспользоватьПараметризациюРесурсныхСпецификаций = ПолучитьФункциональнуюОпцию("ИспользоватьПараметризациюРесурсныхСпецификаций");
	
	ЭтоСборка = (ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Сборка);
	ВариантНазначенияСписокНоменклатуры = (ВариантНазначения = Перечисления.ВариантыНазначенияСпецификации.СписокНоменклатуры);
	
	Для каждого ИмяТЧ Из СтрРазделить("ВыходныеИзделия,ВозвратныеОтходы,МатериалыИУслуги",",") Цикл
		Для каждого Строка Из ЭтотОбъект[ИмяТЧ] Цикл
			Если Не ПустаяСтрока(Строка.АлгоритмРасчетаКоличества) И ИспользоватьПараметризациюРесурсныхСпецификаций Тогда
				Строка.КоличествоУпаковок = 0;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Для каждого Строка Из Трудозатраты Цикл
		Если Не ПустаяСтрока(Строка.АлгоритмРасчетаКоличества) И ИспользоватьПараметризациюРесурсныхСпецификаций Тогда
			Строка.Количество = 0;
		КонецЕсли;
	КонецЦикла;
	
	Если ЭтоСборка И НЕ ВариантНазначенияСписокНоменклатуры Тогда
		Для Индекс = -ВыходныеИзделия.Количество() + 1 По 0 Цикл
			Если Индекс = 0 Тогда
				ВыходныеИзделия[Индекс].ОписаниеИзделия = "";
			Иначе
				//++ НЕ УТКА
				УправлениеДаннымиОбИзделияхКлиентСервер.ОчиститьНастройкиАвтовыбораПоСтроке(
					ВыходныеИзделия[-Индекс], СоответствиеСвойств);
				УправлениеДаннымиОбИзделияхКлиентСервер.ОчиститьНастройкиОтбораПоСвойствамПоСтроке(
					ВыходныеИзделия[-Индекс], ОтборПоСвойствам);
				//-- НЕ УТКА
				ВыходныеИзделия.Удалить(-Индекс);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ЭтоСборка И НЕ ВариантНазначенияСписокНоменклатуры
		ИЛИ СпособРаспределенияЗатратНаВыходныеИзделия <> Перечисления.СпособыРаспределенияЗатратНаВыходныеИзделия.ПоДолямСтоимости Тогда
		Для каждого Строка Из ВыходныеИзделия Цикл
			Строка.ДоляСтоимости = 0;
		КонецЦикла;
	КонецЕсли;
	
	//++ НЕ УТКА
	Если ЭтоСборка И ВариантНазначенияСписокНоменклатуры Тогда
		УправлениеДаннымиОбИзделияхКлиентСервер.ОчиститьНастройкиУточненияПримененияСпецификации(ЭтотОбъект);
	КонецЕсли;
	//-- НЕ УТКА
	
	Для каждого Строка Из МатериалыИУслуги Цикл
		Если Строка.СпособПолученияМатериала <> Перечисления.СпособыПолученияМатериаловВСпецификации.ПроизвестиПоСпецификации Тогда
			Строка.ПланироватьНеРанее = Неопределено;
		КонецЕсли;
		Если Строка.СпособПолученияМатериала <> Перечисления.СпособыПолученияМатериаловВСпецификации.ПроизводитсяНаЭтапе Тогда
			Строка.СпецификацияРемонта = Неопределено;
		КонецЕсли;
	КонецЦикла;
	
	//++ НЕ УТКА
	Если ЭтоТехнологическийНабор() Тогда
		МинимальнаяПартияВыпуска                     = 0;
		ОптимальнаяПартияВыпуска                     = 0;
		ДопустимоеПревышениеОптимальнойПартииВыпуска = 0;
		Для каждого ИмяТЧ Из СтрРазделить("ВозвратныеОтходы,Трудозатраты",",") Цикл
			Для Индекс = 0 По ЭтотОбъект[ИмяТЧ].Количество() - 1 Цикл
				Если ИмяТЧ <> "Трудозатраты" Тогда
					УправлениеДаннымиОбИзделияхКлиентСервер.ОчиститьНастройкиАвтовыбораПоСтроке(
						ЭтотОбъект[ИмяТЧ][Индекс], СоответствиеСвойств);
				КонецЕсли;
				УправлениеДаннымиОбИзделияхКлиентСервер.ОчиститьНастройкиОтбораПоСвойствамПоСтроке(
					ЭтотОбъект[ИмяТЧ][Индекс], ОтборПоСвойствам);
			КонецЦикла;
			ЭтотОбъект[ИмяТЧ].Очистить();
		КонецЦикла;
	КонецЕсли;
	//-- НЕ УТКА
	
	Если ПометкаУдаления ИЛИ ЭтоТехнологическийНабор() Тогда
		ОчиститьВыборЭтапов(ВыходныеИзделия);
		ОчиститьВыборЭтапов(ВозвратныеОтходы);
		ОчиститьВыборЭтапов(МатериалыИУслуги);
		ОчиститьВыборЭтапов(Трудозатраты);
	КонецЕсли;
	
	//++ НЕ УТКА
	Если ВариантПромежуточногоВыпуска <> Перечисления.ВариантыПромежуточногоВыпуска.НастраиваетсяВручную
		И ПромежуточныйВыпуск.Количество() > 0 Тогда
		ПромежуточныйВыпуск.Очистить();
	КонецЕсли;
	//-- НЕ УТКА
	
КонецПроцедуры

Процедура УстановитьРеквизитыПоЭтапамОперациям()
	
	Если ЭтоНовый()
			ИЛИ ЭтоТехнологическийНабор() Тогда
		Возврат;
	КонецЕсли;
	
	ЭтапПустаяСсылка = Справочники.ЭтапыПроизводства.ПустаяСсылка();
	
	РазборкаРемонт = ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Разборка
		ИЛИ ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Ремонт;
	
	ПервыйЭтап    = Новый Структура("Ссылка, Подразделение");
	ПоследнийЭтап = Новый Структура("Ссылка, Подразделение");
	Если (РазборкаРемонт
			ИЛИ ВыходныеИзделия.Найти(ЭтапПустаяСсылка, "ЭтапРедактирование") <> Неопределено
			ИЛИ ВозвратныеОтходы.Найти(ЭтапПустаяСсылка, "ЭтапРедактирование") <> Неопределено
			ИЛИ МатериалыИУслуги.Найти(ЭтапПустаяСсылка, "ЭтапРедактирование") <> Неопределено
			ИЛИ Трудозатраты.Найти(ЭтапПустаяСсылка, "ЭтапРедактирование") <> Неопределено
		) Тогда
		Справочники.РесурсныеСпецификации.ПолучитьПервыйИПоследнийЭтап(Ссылка, ПервыйЭтап, ПоследнийЭтап, "Подразделение");
	КонецЕсли;
//++ НЕ УТКА
	ОперацияПустаяСсылка = Справочники.ТехнологическиеОперации.ПустаяСсылка();
	
	ПервыеОперации    = Новый Соответствие;
	ПоследниеОперации = Новый Соответствие;
	Если (ВыходныеИзделия.Найти(ОперацияПустаяСсылка, "ОперацияРедактирование") <> Неопределено
			ИЛИ ВозвратныеОтходы.Найти(ОперацияПустаяСсылка, "ОперацияРедактирование") <> Неопределено
			ИЛИ МатериалыИУслуги.Найти(ОперацияПустаяСсылка, "ОперацияРедактирование") <> Неопределено
			ИЛИ Трудозатраты.Найти(ОперацияПустаяСсылка, "ОперацияРедактирование") <> Неопределено
			ИЛИ ПромежуточныйВыпуск.Количество() <> 0
		) Тогда
		Справочники.РесурсныеСпецификации.ПолучитьПервыеИПоследниеОперации(Ссылка, ПервыеОперации, ПоследниеОперации);
	КонецЕсли;
//-- НЕ УТКА
		
	// Обновляет значение реквизита Этап в табличных частях
	//  - если реквизит ЭтапРедактирование не пустой то подставляет значение из него
	//  - если реквизит ЭтапРедактирование пустой то
	//		1. для изделий подставляет последний этап
	//		2. для материалов подставляет первый этап
	//
	СписокТЧ = Новый Массив;
	СписокТЧ.Добавить("ВыходныеИзделия");
	СписокТЧ.Добавить("ВозвратныеОтходы");
	СписокТЧ.Добавить("МатериалыИУслуги");
	СписокТЧ.Добавить("Трудозатраты");
	
	Для каждого ИмяТЧ Из СписокТЧ Цикл
		
		Для каждого СтрокаТЧ Из ЭтотОбъект[ИмяТЧ] Цикл
			
			Если СтрокаТЧ.ЭтапРедактирование.Пустая() Тогда
				Если ИмяТЧ = "ВыходныеИзделия" ИЛИ ИмяТЧ = "ВозвратныеОтходы" Тогда
					СтрокаТЧ.Этап = ПоследнийЭтап.Ссылка;
				Иначе
					СтрокаТЧ.Этап = ПервыйЭтап.Ссылка;
				КонецЕсли;
			Иначе
				СтрокаТЧ.Этап = СтрокаТЧ.ЭтапРедактирование;
			КонецЕсли;
			
//++ НЕ УТКА
			Если СтрокаТЧ.ОперацияРедактирование.Пустая() Тогда
				Если ИмяТЧ = "ВыходныеИзделия" ИЛИ ИмяТЧ = "ВозвратныеОтходы" Тогда
					СтрокаТЧ.Операция = ПоследниеОперации.Получить(СтрокаТЧ.Этап);
				Иначе
					СтрокаТЧ.Операция = ПервыеОперации.Получить(СтрокаТЧ.Этап);
				КонецЕсли;
			Иначе
				СтрокаТЧ.Операция = СтрокаТЧ.ОперацияРедактирование;
			КонецЕсли;
//-- НЕ УТКА
		
		КонецЦикла;
		
	КонецЦикла;
	
//++ НЕ УТКА
	Для каждого Строка Из ПромежуточныйВыпуск Цикл
		Строка.ОперацияОтправитель = ПоследниеОперации.Получить(Строка.ЭтапОтправитель);
		Строка.ОперацияПолучатель = ПервыеОперации.Получить(Строка.ЭтапПолучатель);
	КонецЦикла;
//-- НЕ УТКА
	
	Если РазборкаРемонт Тогда
		ОсновноеИзделиеЭтап = ПервыйЭтап.Ссылка;
	ИначеЕсли ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Сборка
			И ВыходныеИзделия.Количество() > 0 Тогда
		ОсновноеИзделиеЭтап = ВыходныеИзделия[0].Этап;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьЗаполнитьВидНоменклатуры()
	
	ЭтоСборка   = (ТипПроизводственногоПроцесса = ПредопределенноеЗначение("Перечисление.ТипыПроизводственныхПроцессов.Сборка"));
	ЭтоРемонт   = (ТипПроизводственногоПроцесса = ПредопределенноеЗначение("Перечисление.ТипыПроизводственныхПроцессов.Ремонт"));
	
	СписокСтрок = Новый Массив;
	СписокНоменклатуры = Новый Массив;
	
	Если (ЭтоСборка ИЛИ ЭтоРемонт) Тогда
		Для каждого Строка Из ВыходныеИзделия Цикл
			Если НЕ ЗначениеЗаполнено(Строка.ВидНоменклатуры) И ЗначениеЗаполнено(Строка.Номенклатура) Тогда
				СписокСтрок.Добавить(Строка);
				СписокНоменклатуры.Добавить(Строка.Номенклатура);
			КонецЕсли; 
		КонецЦикла;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ОсновноеИзделиеВидНоменклатуры) И ЗначениеЗаполнено(ОсновноеИзделиеНоменклатура) Тогда
		СписокСтрок.Добавить(ЭтотОбъект);
		СписокНоменклатуры.Добавить(ОсновноеИзделиеНоменклатура);
	КонецЕсли;
	
	Если СписокСтрок.Количество() > 0 Тогда
		ЗначенияРеквизита = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(СписокНоменклатуры, "ВидНоменклатуры");
		Для каждого СтрокаОбъект Из СписокСтрок Цикл
			Префикс = ?(СтрокаОбъект = ЭтотОбъект, "ОсновноеИзделие", "");
			СтрокаОбъект[Префикс+"ВидНоменклатуры"] = ЗначенияРеквизита[СтрокаОбъект[Префикс+"Номенклатура"]];
		КонецЦикла;
	КонецЕсли;
	
	СписокСтрок = Неопределено;
	
КонецПроцедуры

Функция ПроверитьЗаполнитьРеквизитыОсновногоИзделия() Экспорт
	
	ОбъектИзменен = Ложь;
	
	ЭтоСборка = (ЭтотОбъект.ТипПроизводственногоПроцесса = ПредопределенноеЗначение("Перечисление.ТипыПроизводственныхПроцессов.Сборка"));
	ЭтоРемонт = (ЭтотОбъект.ТипПроизводственногоПроцесса = ПредопределенноеЗначение("Перечисление.ТипыПроизводственныхПроцессов.Ремонт"));
	
	Если ЭтоСборка ИЛИ ЭтоРемонт Тогда
		
		СтруктураДанных = Новый Структура("ВидНоменклатуры,Номенклатура,Характеристика,Упаковка,КоличествоУпаковок");
		
		Если ЭтотОбъект.ВыходныеИзделия.Количество() > 0 Тогда
			ЗаполнитьЗначенияСвойств(СтруктураДанных, ЭтотОбъект.ВыходныеИзделия[0]);
		КонецЕсли;
		
		Для каждого КлючИЗначение Из СтруктураДанных Цикл
			РеквизитОбъекта = "ОсновноеИзделие"+КлючИЗначение.Ключ;
			Если ЭтотОбъект[РеквизитОбъекта] <> КлючИЗначение.Значение Тогда
				ЭтотОбъект[РеквизитОбъекта] = КлючИЗначение.Значение;
				ОбъектИзменен = Истина;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ОбъектИзменен;
	
КонецФункции

//++ НЕ УТКА

Процедура ОбновитьТребуетсяСправочноеУказаниеСерий()

	НаборЗаписей = РегистрыСведений.ТребуетсяСправочноеУказаниеСерий.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Спецификация.Установить(Ссылка);
	
	СтруктураПоиска = Новый Структура("ТребуетсяУказыватьСерии", Истина);
	
	// ВыходныеИзделия
	ТаблицаНоменклатура = ВыходныеИзделия.Выгрузить(СтруктураПоиска, "Номенклатура");
 	СписокНоменклатуры = ОбщегоНазначенияУТ.УдалитьПовторяющиесяЭлементыМассива(ТаблицаНоменклатура.ВыгрузитьКолонку("Номенклатура"));
	
	Для каждого НоменклатураСтроки Из СписокНоменклатуры Цикл
		Если НЕ ЗначениеЗаполнено(НоменклатураСтроки) Тогда
			Продолжить;
		КонецЕсли;
		СтроНоваяЗапись = НаборЗаписей.Добавить();
		СтроНоваяЗапись.Спецификация = Ссылка;
		СтроНоваяЗапись.Номенклатура = НоменклатураСтроки;
		СтроНоваяЗапись.ВидСтрокиСпецификации = Перечисления.ВидыСтрокДереваСпецификаций.ВыходноеИзделие;
	КонецЦикла; 
	
	// МатериалыИУслуги
	ТаблицаНоменклатура = МатериалыИУслуги.Выгрузить(СтруктураПоиска, "Номенклатура");
 	СписокНоменклатуры = ОбщегоНазначенияУТ.УдалитьПовторяющиесяЭлементыМассива(ТаблицаНоменклатура.ВыгрузитьКолонку("Номенклатура"));
	Для каждого НоменклатураСтроки Из СписокНоменклатуры Цикл
		Если НЕ ЗначениеЗаполнено(НоменклатураСтроки) Тогда
			Продолжить;
		КонецЕсли;
		СтроНоваяЗапись = НаборЗаписей.Добавить();
		СтроНоваяЗапись.Спецификация = Ссылка;
		СтроНоваяЗапись.Номенклатура = НоменклатураСтроки;
		СтроНоваяЗапись.ВидСтрокиСпецификации = Перечисления.ВидыСтрокДереваСпецификаций.Материал;
	КонецЦикла; 
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

Функция ВыпускающиеЭтапы()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЭтапыПроизводства.Ссылка
	|ИЗ
	|	Справочник.ЭтапыПроизводства КАК ЭтапыПроизводства
	|ГДЕ
	|	Владелец = &Ссылка И НЕ ПометкаУдаления И НомерСледующегоЭтапа = 0");
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку(0);
	
КонецФункции

//-- НЕ УТКА

Функция ПредставлениеТаблицыВФорме(ИмяТЧ) Экспорт
	
	ИмяСписка = "";
	
	Если ИмяТЧ = "ВыходныеИзделия"
		И ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Сборка Тогда
		
		ИмяСписка = НСтр("ru = 'Продукция';
						|en = 'Manufactured products'");
		
	Иначе
		
		ИмяСписка = Метаданные.Справочники.РесурсныеСпецификации.ТабличныеЧасти[ИмяТЧ].Синоним;
		
	КонецЕсли;
	
	Возврат ИмяСписка;
	
КонецФункции

Процедура ПроверитьЗаполнениеКоличестваВСтрокеТабЧасти(ИмяТЧ, ПредставлениеТЧ, Строка, Отказ)
	
	Если НЕ ПустаяСтрока(Строка.АлгоритмРасчетаКоличества) И ПолучитьФункциональнуюОпцию("ИспользоватьПараметризациюРесурсныхСпецификаций") Тогда
		Возврат;
	КонецЕсли;
	
	Если Строка.КоличествоУпаковок = 0 Тогда
		
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", "Заполнение", НСтр("ru = 'Количество';
																											|en = 'Quantity'"),
			Строка.НомерСтроки, ПредставлениеТЧ);
		
	Иначе
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,
		ЭтотОбъект,
		ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТЧ, Строка.НомерСтроки, "КоличествоУпаковок"),,
		Отказ);
	
КонецПроцедуры

Функция КоличествоЭтаповПроизводства()
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(*) КАК КоличествоЭтапов
	|ИЗ
	|	Справочник.ЭтапыПроизводства КАК Этапы
	|ГДЕ
	|	Этапы.Владелец = &Спецификация
	|	И НЕ Этапы.ПометкаУдаления");
	Запрос.УстановитьПараметр("Спецификация", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат 0;
	Иначе
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.КоличествоЭтапов;
	КонецЕсли;
	
КонецФункции

Функция ЭтоТехнологическийНабор()
	
	Результат = Ложь;
	
	//++ НЕ УТКА
	Если НЕ ДополнительныеСвойства.Свойство("ЭтоТехнологическийНабор") Тогда
		
		ТипНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОсновноеИзделиеВидНоменклатуры, "ТипНоменклатуры");
		
		ДополнительныеСвойства.Вставить("ЭтоТехнологическийНабор", ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Набор);
		
	КонецЕсли;
	Результат = ДополнительныеСвойства.ЭтоТехнологическийНабор;
	//-- НЕ УТКА
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
