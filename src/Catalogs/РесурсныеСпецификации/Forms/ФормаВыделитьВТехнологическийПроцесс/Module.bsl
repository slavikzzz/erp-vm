
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.ДоступностьРеквизитовСпецификации Тогда
		Элементы.ПодключитьКЭтапу.ТолькоПросмотр              = Ложь;
		Элементы.ДобавитьСвязанныеМатериалы.ТолькоПросмотр    = НЕ Параметры.ЕстьСвязанныеМатериалы;
		Элементы.ДобавитьСвязанныеТрудозатраты.ТолькоПросмотр = НЕ Параметры.ЕстьСвязанныеТрудозатраты;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.НаименованиеЭтапа) Тогда
		Элементы.ПодключитьКЭтапу.Заголовок =
			Элементы.ПодключитьКЭтапу.Заголовок + СтрШаблон(" ""%1""", Параметры.НаименованиеЭтапа);
	КонецЕсли;
	
	Параметры.Свойство("ДляВидаИзделий", ДляВидаИзделий);
	Параметры.Свойство("Подразделение",  Подразделение);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Если ПроверитьЗаполнение() Тогда
		
		СвязанныеРесурсы = Новый Структура;
		СвязанныеРесурсы.Вставить("МатериалыИУслуги", Новый Структура("ДобавитьВТехпроцесс", ДобавитьСвязанныеМатериалы));
		СвязанныеРесурсы.Вставить("Трудозатраты",     Новый Структура("ДобавитьВТехпроцесс", ДобавитьСвязанныеТрудозатраты));
		
		Результат = Новый Структура;
		Результат.Вставить("Наименование",     Наименование);
		Результат.Вставить("Подразделение",    Подразделение);
		Результат.Вставить("ДляВидаИзделий",   ДляВидаИзделий);
		Результат.Вставить("СвязанныеРесурсы", СвязанныеРесурсы);
		Результат.Вставить("ПодключитьКЭтапу", ПодключитьКЭтапу);
		
		Закрыть(Результат);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	УстановитьСнятьОтметкиЭлементов(Истина);
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	УстановитьСнятьОтметкиЭлементов(Ложь);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьСнятьОтметкиЭлементов(Отметка)
	
	МассивЭлементов = Новый Массив;
	МассивЭлементов.Добавить("ПодключитьКЭтапу");
	МассивЭлементов.Добавить("ДобавитьСвязанныеМатериалы");
	МассивЭлементов.Добавить("ДобавитьСвязанныеТрудозатраты");
	
	Для каждого ИмяЭлемента Из МассивЭлементов Цикл
		Если НЕ Элементы[ИмяЭлемента].ТолькоПросмотр Тогда
			ЭтотОбъект[ИмяЭлемента] = Отметка;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти