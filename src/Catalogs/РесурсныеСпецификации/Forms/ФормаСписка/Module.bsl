
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ДоступенОтборИспользуетсяВЗаказах = Ложь;
//++ НЕ УТКА
	ДоступенОтборИспользуетсяВЗаказах = (ПравоДоступа("Чтение", Метаданные.Документы.ЗаказНаПроизводство2_2)
//++ Устарело_Производство21
										И ПравоДоступа("Чтение", Метаданные.Документы.ЗаказНаПроизводство)
//-- Устарело_Производство21
										И ПравоДоступа("Чтение", Метаданные.Документы.ЭтапПроизводства2_2));
//-- НЕ УТКА
	
	Если Параметры.Свойство("Изделие") Тогда
		// Форма открывается из карточки номенклатуры
		ОтборНоменклатура = Параметры.Изделие;
		НеЗагружатьНастройки = Истина;
	КонецЕсли;
	
	УстановитьСвойстваИОтборыВСпискеСпецификаций();
	
	УстановитьОтборПоНоменклатуре();
	УстановитьОтборПоСтатусу(ЭтаФорма);
	
	Если Параметры.РежимВыбора Тогда
		Элементы.Список.РежимВыбора = Истина;
	КонецЕсли;
	
	НастроитьЭлементыФормы();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

	// ИнтеграцияС1СДокументооборотом
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПриСозданииНаСервере(ЭтаФорма, Элементы.ГруппаГлобальныеКоманды);
	// Конец ИнтеграцияС1СДокументооборотом
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
	РегистрыСведений.ЗаданияКРасчетуДлительностиПроизводства.ЗапуститьЗадание();
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)
	
	Если НеЗагружатьНастройки Тогда
		Настройки.Удалить("ОтборСтатус");
		Настройки.Удалить("ОтборНоменклатура");
		Настройки.Удалить("ОтборВидРабочегоЦентра");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если НеЗагружатьНастройки Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьСвойстваИОтборыВСпискеСпецификаций();
	
	УстановитьОтборПоСтатусу(ЭтаФорма);
	УстановитьОтборПоНоменклатуре();
	
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма, "Номенклатура");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборСтатусПриИзменении(Элемент)
	
	УстановитьОтборПоСтатусу(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборНоменклатураПриИзменении(Элемент)
	
	УстановитьОтборПоНоменклатуре();
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма, "Номенклатура");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборИспользуетсяКакПриИзменении(Элемент)
	
	УстановитьОтборПоНоменклатуре();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВидРабочегоЦентраПриИзменении(Элемент)
	
//++ НЕ УТКА
	УстановитьОтборПоВидуРабочегоЦентра();
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма, "ВидРабочегоЦентра");
//-- НЕ УТКА
	Возврат; // пустой обработчик
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВариантНаладкиПриИзменении(Элемент)
	
//++ НЕ УТКА
	УстановитьОтборПоВариантуНаладки();
//-- НЕ УТКА
	Возврат; // пустой обработчик
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборИспользуетсяВЗаказахПриИзменении(Элемент)
	
//++ НЕ УТКА
	УстановитьОтборПоИспользованиюВЗаказах();
//-- НЕ УТКА
	Возврат; // пустой обработчик
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбраннаяСтрока) = Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
		СтандартнаяОбработка = Ложь;
		ПоказатьЗначение(, ВыбраннаяСтрока.Ключ);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если Группа Тогда
		Возврат;
	КонецЕсли;
	
	Если Копирование Тогда
		
		Отказ = Истина;
		КопироватьРесурснуюСпецификацию();
		
	ИначеЕсли Не ОтборНоменклатура.Пустая()
		И (НЕ ЗначениеЗаполнено(ОтборИспользуетсяКак) ИЛИ ОтборИспользуетсяКак = ПредопределенноеЗначение("Перечисление.ИспользованиеНоменклатурыВНСИПроизводства.Изделие")) Тогда
		
		Отказ = Истина;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Основание", ОтборНоменклатура);
		ОткрытьФорму("Справочник.РесурсныеСпецификации.ФормаОбъекта", ПараметрыФормы);
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьДанные(Команда)
	
	УстановитьОтборПоНоменклатуре();
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаУстановитьСтатусВРазработке(Команда)
	
	УстановитьСтатусСпецификации("ВРазработке", НСтр("ru = 'В разработке';
													|en = 'Under development'"));
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаУстановитьСтатусДействует(Команда)
	
	УстановитьСтатусСпецификации("Действует", НСтр("ru = 'Действует';
													|en = 'Valid'"));
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаУстановитьСтатусЗакрыта(Команда)
	
	УстановитьСтатусСпецификации("Закрыта", НСтр("ru = 'Закрыта';
												|en = 'Closed'"));
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьРемонт(Команда)
	
	СоздатьЭлементИзСписка(ПредопределенноеЗначение("Перечисление.ТипыПроизводственныхПроцессов.Ремонт"));
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьРазборка(Команда)
	
	СоздатьЭлементИзСписка(ПредопределенноеЗначение("Перечисление.ТипыПроизводственныхПроцессов.Разборка"));
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОткрытьСпецификацииИзделия(Команда)
	
	//++ НЕ УТКА
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено ИЛИ ТипЗнч(ТекущиеДанные) = Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Команда не может быть выполнена для указанного объекта.';
										|en = 'Cannot execute the command for the object.'"));
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ТекущиеДанные.ОсновноеИзделие) = Тип("СправочникСсылка.ВидыНоменклатуры") Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Команда не может быть выполнена для спецификации на вид номенклатуры.';
										|en = 'Cannot execute the command for the bill of materials for the item kind.'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("Номенклатура,РежимСпецификацииИзделия,ТекущаяСпецификация",
								ТекущиеДанные.ОсновноеИзделие, "Полный", ТекущиеДанные.Ссылка);
	ОткрытьФорму("ОбщаяФорма.ПрименениеНоменклатурыВПроизводстве", ПараметрыФормы,, ТекущиеДанные.ОсновноеИзделие);
	
	//-- НЕ УТКА
	
	Возврат; // пустой обработчик
	
КонецПроцедуры

// ИнтеграцияС1СДокументооборотом
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(Команда, ЭтаФорма, Элементы.Список);
	
КонецПроцедуры
// Конец ИнтеграцияС1СДокументооборотом

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Список

&НаСервере
Функция СписокРасширенныхОтборовСпискаСпецификаций()
	
	СписокОтборов = Новый Массив();
	
//++ НЕ УТКА
	ИспользоватьОтборПоВидуРабочегоЦентра = (ЗначениеЗаполнено(ОтборВидРабочегоЦентра) ИЛИ ЗначениеЗаполнено(ОтборВариантНаладки));
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("ВидРабочегоЦентра", ОтборВидРабочегоЦентра);
	ПараметрыОтбора.Вставить("ВариантНаладки", ОтборВариантНаладки);
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВидыРабочихЦентров.Ссылка.Владелец КАК Ссылка
	|ИЗ
	|	Справочник.ЭтапыПроизводства.ВидыРабочихЦентров КАК ВидыРабочихЦентров
	|ГДЕ
	|	ВидыРабочихЦентров.Ссылка.Владелец = &Ссылка
	|	И (&ВидРабочегоЦентра <> ЗНАЧЕНИЕ(Справочник.ВидыРабочихЦентров.ПустаяСсылка)
	|			ИЛИ &ВариантНаладки <> ЗНАЧЕНИЕ(Справочник.ВариантыНаладки.ПустаяСсылка))
	|	И (ВидыРабочихЦентров.ВидРабочегоЦентра = &ВидРабочегоЦентра
	|			ИЛИ &ВидРабочегоЦентра = ЗНАЧЕНИЕ(Справочник.ВидыРабочихЦентров.ПустаяСсылка))
	|	И (ВидыРабочихЦентров.ВариантНаладки = &ВариантНаладки
	|			ИЛИ &ВариантНаладки = ЗНАЧЕНИЕ(Справочник.ВариантыНаладки.ПустаяСсылка))";
	
	ПроизводствоСервер.ДобавитьОтборВСписокОтборов(СписокОтборов, "ВидРабочегоЦентра", ТекстЗапроса, ПараметрыОтбора, ИспользоватьОтборПоВидуРабочегоЦентра);
//-- НЕ УТКА

//++ НЕ УТКА
	ТекстЗапроса = 
	"
	//++ Устарело_Производство21
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ТаблицаОтбора.Спецификация КАК Спецификация
	|ИЗ
	|	Документ.ЗаказНаПроизводство.Продукция КАК ТаблицаОтбора
	|		ЛЕВОЕ СОЕДИНЕНИЕ Константа.ИспользоватьУправлениеПроизводством КАК ИспользоватьПроизводство21
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ТаблицаОтбора.Спецификация = &Ссылка
	|	И ТаблицаОтбора.Ссылка.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовНаПроизводство.Закрыт)
	|	И НЕ ТаблицаОтбора.Ссылка.ПометкаУдаления
	|	И ИспользоватьПроизводство21.Значение
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ТаблицаОтбора.ИсточникПолученияПолуфабриката КАК Спецификация
	|ИЗ
	|	Документ.ЗаказНаПроизводство.МатериалыИУслуги КАК ТаблицаОтбора
	|		ЛЕВОЕ СОЕДИНЕНИЕ Константа.ИспользоватьУправлениеПроизводством КАК ИспользоватьПроизводство21
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ТаблицаОтбора.ИсточникПолученияПолуфабриката = &Ссылка
	|	И ТаблицаОтбора.Ссылка.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовНаПроизводство.Закрыт)
	|	И НЕ ТаблицаОтбора.Ссылка.ПометкаУдаления
	|	И ИспользоватьПроизводство21.Значение
	|
	|ОБЪЕДИНИТЬ ВСЕ
	//-- Устарело_Производство21
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ТаблицаОтбора.Спецификация КАК Спецификация
	|ИЗ
	|	Документ.ЗаказНаПроизводство2_2.Продукция КАК ТаблицаОтбора
	|		ЛЕВОЕ СОЕДИНЕНИЕ Константа.ИспользоватьУправлениеПроизводством2_2 КАК ИспользоватьПроизводство22
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ТаблицаОтбора.Спецификация = &Ссылка
	|	И ТаблицаОтбора.Ссылка.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовНаПроизводство2_2.Закрыт)
	|	И НЕ ТаблицаОтбора.Ссылка.ПометкаУдаления
	|	И ИспользоватьПроизводство22.Значение
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ТаблицаОтбора.Спецификация КАК Спецификация
	|ИЗ
	|	Документ.ЭтапПроизводства2_2 КАК ТаблицаОтбора
	|		ЛЕВОЕ СОЕДИНЕНИЕ Константа.ИспользоватьУправлениеПроизводством2_2 КАК ИспользоватьПроизводство22
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ТаблицаОтбора.Спецификация = &Ссылка
	|	И ТаблицаОтбора.Распоряжение.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовНаПроизводство2_2.Закрыт)
	|	И НЕ ТаблицаОтбора.Ссылка.ПометкаУдаления
	|	И ИспользоватьПроизводство22.Значение
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ТаблицаОтбора.Спецификация КАК Спецификация
	|ИЗ
	|	Документ.ЭтапПроизводства2_2.ОбеспечениеМатериаламиИРаботами КАК ТаблицаОтбора
	|		ЛЕВОЕ СОЕДИНЕНИЕ Константа.ИспользоватьУправлениеПроизводством2_2 КАК ИспользоватьПроизводство22
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ТаблицаОтбора.Спецификация = &Ссылка
	|	И ТаблицаОтбора.Ссылка.Распоряжение.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовНаПроизводство2_2.Закрыт)
	|	И НЕ ТаблицаОтбора.Ссылка.ПометкаУдаления
	|	И ИспользоватьПроизводство22.Значение";
	
	Если ОтборИспользуетсяВЗаказах = "Используется" Тогда
		ПроизводствоСервер.ДобавитьОтборВСписокОтборов(
			СписокОтборов,
			"ИспользуетсяВЗаказах",
			ТекстЗапроса,
			ОтборИспользуетсяВЗаказах,
			Истина,
			"СсылкаВСписке");
	ИначеЕсли ОтборИспользуетсяВЗаказах = "НеИспользуется" Тогда
		ПроизводствоСервер.ДобавитьОтборВСписокОтборов(
			СписокОтборов,
			"ИспользуетсяВЗаказах",
			ТекстЗапроса,
			ОтборИспользуетсяВЗаказах,
			Истина,
			"СсылкаНеВСписке");
	КонецЕсли;
//-- НЕ УТКА

	Возврат СписокОтборов;
	
КонецФункции

&НаСервере
Процедура УстановитьСвойстваИОтборыВСпискеСпецификаций()
	
	ТекстЗапроса = Справочники.РесурсныеСпецификации.ТекстЗапросаДинамическогоСпискаРесурсныхСпецификаций();
	
	СписокОтборов = СписокРасширенныхОтборовСпискаСпецификаций();
	ПроизводствоСервер.ВыполнитьПодстановкуОтборовВТекстЗапросаДинамическогоСписка(ТекстЗапроса, СписокОтборов, "СправочникРесурсныеСпецификации");
	
	СвойстваСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
	
	СвойстваСписка.ТекстЗапроса = ТекстЗапроса;
	СвойстваСписка.ОсновнаяТаблица = Список.ОсновнаяТаблица;
	СвойстваСписка.ДинамическоеСчитываниеДанных = Истина;
	
	ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Элементы.Список, СвойстваСписка);
	
	ПроизводствоСервер.УстановитьПараметрыОтбораДинамическогоСписка(Список, СписокОтборов);
	
КонецПроцедуры

#КонецОбласти

#Область Отборы

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборПоСтатусу(Форма)

	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Форма.Список, 
		"Статус", 
		Форма.ОтборСтатус, 
		ВидСравненияКомпоновкиДанных.Равно,
		, 
		ЗначениеЗаполнено(Форма.ОтборСтатус));

КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоНоменклатуре()
	
	УправлениеДаннымиОбИзделиях.УстановитьОтборПоНоменклатуреВСпискеСпецификаций(Список, ОтборНоменклатура, ОтборИспользуетсяКак);
	
КонецПроцедуры

//++ НЕ УТКА

&НаСервере
Процедура УстановитьОтборПоВидуРабочегоЦентра()
	
	УстановитьСвойстваИОтборыВСпискеСпецификаций();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоВариантуНаладки()
	
	УстановитьСвойстваИОтборыВСпискеСпецификаций();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИспользуютсяВариантыНаладки(Знач ВариантНаладки)

	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВариантНаладки, "ИспользуютсяВариантыНаладки");

КонецФункции

&НаСервере
Процедура УстановитьОтборПоИспользованиюВЗаказах()
	
	УстановитьСвойстваИОтборыВСпискеСпецификаций();
	
КонецПроцедуры

//-- НЕ УТКА

#КонецОбласти

#Область Прочее

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();
	
	Справочники.РесурсныеСпецификации.УстановитьУсловноеОформлениеСпискаРесурсныхСпецификаций(Список.УсловноеОформление);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементыФормы()
	
	Элементы.ОтборИспользуетсяВЗаказах.Видимость = ДоступенОтборИспользуетсяВЗаказах;
	
	ДоступноОписаниеТипаПроизводственногоПроцесса = УправлениеДаннымиОбИзделиях.ДоступноОписаниеТипаПроизводственногоПроцесса();
	
	Элементы.ТипПроизводственногоПроцесса.Видимость = ДоступноОписаниеТипаПроизводственногоПроцесса;
	
	Если ПравоДоступа("Добавление", Метаданные.Справочники.РесурсныеСпецификации) Тогда
		Элементы.ПодменюСоздать.Видимость = ДоступноОписаниеТипаПроизводственногоПроцесса;
		Элементы.СписокСоздать.Видимость  = НЕ ДоступноОписаниеТипаПроизводственногоПроцесса;
	КонецЕсли;
	
	ПравоРедактированияСпецификаций = ПравоДоступа("Редактирование", Метаданные.Справочники.РесурсныеСпецификации);
	Если НЕ ПравоРедактированияСпецификаций Тогда
		Элементы.СписокУстановитьСтатусВРазработке.Видимость = Ложь;
		Элементы.СписокУстановитьСтатусДействует.Видимость = Ложь;
		Элементы.СписокУстановитьСтатусЗакрыта.Видимость = Ложь;
	КонецЕсли; 
	
	//++ НЕ УТКА
	Если НЕ ПравоДоступа("Просмотр", Метаданные.ОбщиеФормы.ПрименениеНоменклатурыВПроизводстве) Тогда
		Элементы.СписокОткрытьСпецификацииИзделия.Видимость = Ложь;
	КонецЕсли;
	//-- НЕ УТКА
	
	ИзменитьВыделенныеДоступна = ПроизводствоСервер.ДоступнаКомандаИзменитьВыделенные(Метаданные.Справочники.РесурсныеСпецификации);
	Элементы.СписокИзменитьВыделенные.Видимость          = ИзменитьВыделенныеДоступна;
	Элементы.КонтекстноеМенюИзменитьВыделенные.Видимость = ИзменитьВыделенныеДоступна;
	
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЗависимыеЭлементыФормы(Форма, СписокРеквизитов = "")

	Элементы = Форма.Элементы;
	
	Инициализация = ПустаяСтрока(СписокРеквизитов);
	СтруктураРеквизитов = Новый Структура(СписокРеквизитов);
	
	Если СтруктураРеквизитов.Свойство("Номенклатура")
		ИЛИ Инициализация Тогда
		
		Форма.Элементы.ОтборИспользуетсяКак.ТолькоПросмотр = НЕ ЗначениеЗаполнено(Форма.ОтборНоменклатура);
		
	КонецЕсли;
	
//++ НЕ УТКА
	Если СтруктураРеквизитов.Свойство("ВидРабочегоЦентра")
		ИЛИ Инициализация Тогда
		
		Если НЕ Форма.ОтборВидРабочегоЦентра.Пустая() Тогда
			ИспользуютсяВариантыНаладки = ИспользуютсяВариантыНаладки(Форма.ОтборВидРабочегоЦентра);
		Иначе
			ИспользуютсяВариантыНаладки = Ложь;
		КонецЕсли;
		
		Элементы.ОтборВариантНаладки.ТолькоПросмотр = НЕ ИспользуютсяВариантыНаладки;
		
	КонецЕсли;
//-- НЕ УТКА
	
КонецПроцедуры

&НаКлиенте
Процедура КопироватьРесурснуюСпецификацию()

	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено ИЛИ ТипЗнч(ТекущиеДанные) = Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("КопироватьРесурснуюСпецификациюЗавершение", ЭтотОбъект);
	УправлениеДаннымиОбИзделияхКлиент.КопироватьРесурснуюСпецификацию(ТекущиеДанные.Ссылка, ОписаниеОповещения);

КонецПроцедуры

&НаКлиенте
Процедура КопироватьРесурснуюСпецификациюЗавершение(Ссылка, ДополнительныеПараметры) Экспорт

	Если Ссылка <> Неопределено Тогда
		Элементы.Список.ТекущаяСтрока = Ссылка;
	КонецЕсли; 

КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатусСпецификации(ЗначениеСтатуса, ПредставлениеСтатуса)

	ВыделенныеСсылки = ОбщегоНазначенияУТКлиент.ПроверитьПолучитьВыделенныеВСпискеСсылки(Элементы.Список);
	
	УправлениеДаннымиОбИзделияхКлиент.УстановитьСтатусСпецификаций(ЗначениеСтатуса, ПредставлениеСтатуса, ВыделенныеСсылки);

КонецПроцедуры

&НаКлиенте
Процедура СоздатьЭлементИзСписка(ТипПроизводственногоПроцесса)
	
	ТекущиеДанные   = Элементы.Список.ТекущиеДанные;
	ТекущийРодитель = ПредопределенноеЗначение("Справочник.РесурсныеСпецификации.ПустаяСсылка");
	
	Если Элементы.Список.Отображение = ОтображениеТаблицы.ИерархическийСписок Тогда
		ТекущийРодитель = Элементы.Список.ТекущийРодитель;
	ИначеЕсли Элементы.Список.Отображение = ОтображениеТаблицы.Дерево И ТекущиеДанные <> Неопределено Тогда
		Если ТекущиеДанные.ЭтоГруппа Тогда
			ТекущийРодитель = ТекущиеДанные.Ссылка;
		Иначе
			ТекущийРодитель = ТекущиеДанные.Родитель;
		КонецЕсли;
	КонецЕсли;
	
	ЗначенияЗаполнения = Новый Структура("ТипПроизводственногоПроцесса,Родитель",
		ТипПроизводственногоПроцесса,
		ТекущийРодитель);
	
	ОткрытьФорму("Справочник.РесурсныеСпецификации.ФормаОбъекта", Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения));
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

