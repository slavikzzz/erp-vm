
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	//++ НЕ УТКА
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	НастройкиПодсистемыПроизводство = ПроизводствоСервер.НастройкиПодсистемыПроизводство();
	Если НЕ НастройкиПодсистемыПроизводство.ИспользуетсяПроизводство Тогда 
		ВызватьИсключение НСтр("ru = 'Для открытия формы необходимо включить подсистему управления производством.';
								|en = 'Enable production management subsystem to open the form.'");
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(ЭтаФорма, НастройкиПодсистемыПроизводство);
	
	Элементы.СписокЗаказовСостояниеГрафика.Видимость = Истина;
	
	Спецификация = Параметры.Спецификация;
	
	ЗаполнитьСлужебныеРеквизиты();
	
	ЗаполнитьСписокЗаказов();

	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
	//-- НЕ УТКА
	
	НастроитьЭлементыФормы();
	
	Возврат; // пустой обработчик
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	//++ НЕ УТКА
	Если СтрНайти("Запись_ЗаказНаПроизводство, Запись_ЭтапыПроизводства, Запись_ГрафикПроизводства", ИмяСобытия) > 0
		И Источник <> "ПрименениеСпецификацииВЗаказах" Тогда
		ЗаполнитьСписокЗаказов();
	КонецЕсли; 
	//-- НЕ УТКА
	
	Возврат; // пустой обработчик
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	//++ НЕ УТКА
	Если ИсточникВыбора.ИмяФормы = "Справочник.РесурсныеСпецификации.Форма.ФормаВыбораПоНоменклатуре" Тогда
		ЗаменитьСпецификациюЗавершение(ВыбранноеЗначение);
	КонецЕсли; 
	//-- НЕ УТКА
	
	Возврат; // пустой обработчик
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокЗаказов

&НаКлиенте
Процедура СписокЗаказовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	//++ НЕ УТКА
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.СписокЗаказов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
//++ Устарело_Производство21
	ПараметрыФормы = Новый Структура("Ключ,АктивироватьСтрокуПродукции", 
						ТекущиеДанные.Заказ, ТекущиеДанные.КодСтроки);
						
	ОткрытьФорму("Документ.ЗаказНаПроизводство.ФормаОбъекта", ПараметрыФормы);
//-- Устарело_Производство21
	
	//-- НЕ УТКА
	
	Возврат; // пустой обработчик
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаЗаменитьСпецификацию(Команда)
	
//++ НЕ УТКА
	
	ВыбраныСтрокиПоКоторымПроизводствоНеЗапущено = Ложь;
	ИсключитьИзВыбораСпецификацию = Неопределено;
	
	ДанныеВыбранныхСтрок = Новый Массив;
	Для каждого ДанныеСтроки Из СписокЗаказов Цикл
		ИдентификаторСтроки = ДанныеСтроки.ПолучитьИдентификатор();
		Если Элементы.СписокЗаказов.ВыделенныеСтроки.Найти(ИдентификаторСтроки) = Неопределено Тогда
			ДанныеСтроки.Заменить = Ложь;
			Продолжить;
		КонецЕсли;
		ДанныеСтроки.Заменить = Истина;
		
		СтруктураДанных = УправлениеДаннымиОбИзделияхКлиентСервер.СтруктураДанныхОбИзделииДляВыбораСпецификации();
		СтруктураДанных.Номенклатура            = ДанныеСтроки.Номенклатура;
		СтруктураДанных.Характеристика          = ДанныеСтроки.Характеристика;
		СтруктураДанных.НачалоПроизводства      = ДанныеСтроки.НачалоПроизводства;
		СтруктураДанных.ПодразделениеДиспетчер  = ДанныеСтроки.Подразделение;
		СтруктураДанных.НаправлениеДеятельности = ДанныеСтроки.НаправлениеДеятельности;
		ДанныеВыбранныхСтрок.Добавить(СтруктураДанных);
		
		Если НЕ ДанныеСтроки.ПроизводствоЗапущено Тогда
			ВыбраныСтрокиПоКоторымПроизводствоНеЗапущено = Истина;
		КонецЕсли;
		
		Если НЕ ДанныеСтроки.ТекущаяСпецификацияБольшеНеИспользуется Тогда
			// выбрана строка для которой используется текущая спецификация
			// поэтому запретим выбор текущей спецификации для замены
			ИсключитьИзВыбораСпецификацию = Спецификация;
		КонецЕсли;
	КонецЦикла;
	
	Если ДанныеВыбранныхСтрок.Количество() = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Необходимо выбрать строки, в которых требуется выполнить замену.';
										|en = 'Select lines in which replacement should be made.'"));
		Возврат;
		
	ИначеЕсли НЕ ВыбраныСтрокиПоКоторымПроизводствоНеЗапущено Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Замена спецификации допускается только для продукции и полуфабрикатов,
											|производство которых не запущено (запланировано).';
											|en = 'BOM replacement is allowed only for products and semi-finished products 
											|whose manufacturing is not started (planned).'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыВыбораСпецификаций = УправлениеДаннымиОбИзделияхКлиентСервер.ПараметрыВыбораСпецификаций();
	
	ДоступныеТипы = ПараметрыВыбораСпецификаций.ДоступныеТипы; // Массив
	ДоступныеТипы.Добавить(ТипПроизводственногоПроцесса);
	
	ДоступныеСтатусы = ПараметрыВыбораСпецификаций.ДоступныеСтатусы; // Массив
	ДоступныеСтатусы.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыСпецификаций.Действует"));
	
	ПараметрыВыбораСпецификаций.Вставить("ИсключитьСпецификацию", ИсключитьИзВыбораСпецификацию);
	
	УправлениеДаннымиОбИзделияхКлиент.ОткрытьФормуВыбораСпецификацийПоСпискуНоменклатуры(ДанныеВыбранныхСтрок, ПараметрыВыбораСпецификаций, ЭтаФорма);
//-- НЕ УТКА
	
	Возврат; // пустой обработчик
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	//++ НЕ УТКА
	ЗаполнитьСписокЗаказов();
	//-- НЕ УТКА
	
	Возврат; // пустой обработчик
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастроитьЭлементыФормы()
	
	СтрокиЛегенды = Новый Массив;
	СтрокиЛегенды.Добавить(НСтр("ru = 'Коричневым выделена продукция или полуфабрикаты, производство которых уже запущено.';
								|en = 'Products or semi-finished products whose manufacturing has started are highlighted in brown.'"));
	
	//++ НЕ УТКА
	Если ЭтоТехнологическийНабор Тогда
		Элементы.СписокЗаказовЗаменитьСпецификацию.Видимость = Ложь;
	Иначе
	//-- НЕ УТКА
		СтрокиЛегенды.Добавить(НСтр("ru = 'Серым выделена продукция или полуфабрикаты, для которых выполнена замена спецификации и текущая спецификация больше не используется.';
									|en = 'Products or semi-finished products for which the BOM is replaced, and the current BOM is no longer used, are highlighted in gray.'"));
	//++ НЕ УТКА
	КонецЕсли;
	//-- НЕ УТКА
	
	Элементы.Легенда.Заголовок = СтрСоединить(СтрокиЛегенды, Символы.ПС);
	
КонецПроцедуры

//++ НЕ УТКА

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();
//++ Устарело_Производство21
	// Оформление продукции, для которой уже запущено производство
	#Область ПроизводствоЗапущено
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СписокЗаказов.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СписокЗаказов.ПроизводствоЗапущено");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", Новый Цвет(191, 97, 12));
	
	#КонецОбласти
	
	// Оформление продукции, для которой выполнена замена
	#Область ТекущаяСпецификацияБольшеНеИспользуется
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СписокЗаказов.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СписокЗаказов.ТекущаяСпецификацияБольшеНеИспользуется");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.НейтральноСерый);
	
	#КонецОбласти
	
	// Стандартное оформление номенклатуры
	#Область Номенклатура

	НоменклатураСервер.УстановитьУсловноеОформлениеЕдиницИзмерения(ЭтаФорма, 
																   "СписокЗаказовНоменклатураЕдиницаИзмерения", 
                                                                   "СписокЗаказов.Упаковка");

	//

	НоменклатураСервер.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(ЭтаФорма, 
																			 "СписокЗаказовХарактеристика",
																		     "СписокЗаказов.ХарактеристикиИспользуются");

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СписокЗаказовНазначение.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СписокЗаказов.Назначение");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<без назначения>';
																|en = '<No assignment>'"));
	
	#КонецОбласти
//-- Устарело_Производство21
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСлужебныеРеквизиты()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Т.ТипПроизводственногоПроцесса                                                                   КАК ТипПроизводственногоПроцесса,
	|	Т.ОсновноеИзделиеВидНоменклатуры.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Набор) КАК ЭтоТехнологическийНабор
	|ИЗ
	|	Справочник.РесурсныеСпецификации КАК Т
	|
	|ГДЕ
	|	Т.Ссылка = &Спецификация
	|";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Спецификация", Спецификация);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокЗаказов()

//++ Устарело_Производство21

	СписокЗаказов.Очистить();
	
	ТекстСписокЗаказов = 
		"ВЫБРАТЬ
		|		ТаблицаПродукция.Ссылка КАК Заказ,
		|		ТаблицаПродукция.Ссылка.Номер КАК Номер,
		|		ТаблицаПродукция.Ссылка.Дата КАК Дата,
		|		ТаблицаПродукция.Ссылка.Статус КАК Статус,
		|	
		|		ВЫБОР
		|			КОГДА ТаблицаПродукция.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовНаПроизводство.Создан)
		|				ТОГДА """"
		|			КОГДА ТаблицаПродукция.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовНаПроизводство.КПроизводству)
		|				ТОГДА ВЫБОР
		|						КОГДА ТаблицаПродукция.Ссылка.СтатусГрафикаПроизводства = ЗНАЧЕНИЕ(Перечисление.СтатусыГрафикаПроизводстваВЗаказеНаПроизводство.Рассчитан)
		|							ТОГДА &СостояниеГрафикаВРаботе
		|						КОГДА ТаблицаПродукция.Ссылка.СтатусГрафикаПроизводства = ЗНАЧЕНИЕ(Перечисление.СтатусыГрафикаПроизводстваВЗаказеНаПроизводство.ТребуетсяРассчитать)
		|							ТОГДА &СостояниеГрафикаПостроение
		|					КОНЕЦ
		|		КОНЕЦ КАК СостояниеГрафика,
		|		ТаблицаПродукция.НачатьНеРанее КАК НачалоПроизводства,
		|		ТаблицаПродукция.ДатаПотребности КАК ОкончаниеПроизводства,
		|	
		|		ТаблицаПродукция.Ссылка.Подразделение           КАК Подразделение,
		|		ТаблицаПродукция.Ссылка.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	
		|		ТаблицаПродукция.НомерСтроки КАК НомерСтроки,
		|		ТаблицаПродукция.КодСтроки КАК КодСтроки,
		|		ТаблицаПродукция.КлючСвязи КАК КлючСвязи,
		|		&ПустойКлючСвязи КАК КлючСвязиПродукция,
		|	
		|		ЛОЖЬ КАК ПроизводитсяВПроцессе,
		|		НЕОПРЕДЕЛЕНО КАК ЭтапПроизводства,
		|		"""" КАК ПредставлениеЭтапа,
		|	
		|		ТаблицаПродукция.Назначение КАК Назначение,
		|		ТаблицаПродукция.Номенклатура КАК Номенклатура,
		|		ТаблицаПродукция.Характеристика КАК Характеристика,
		|		ТаблицаПродукция.КоличествоУпаковок КАК Количество
		|	
		|	ИЗ
		|		Документ.ЗаказНаПроизводство.Продукция КАК ТаблицаПродукция
		|	ГДЕ
		|		ТаблицаПродукция.Спецификация = &Спецификация
		|		И ТаблицаПродукция.Ссылка.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовНаПроизводство.Закрыт)
		|		И НЕ ТаблицаПродукция.Ссылка.ПометкаУдаления
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТаблицаМатериалыИУслуги.Ссылка,
		|		ТаблицаМатериалыИУслуги.Ссылка.Номер,
		|		ТаблицаМатериалыИУслуги.Ссылка.Дата,
		|		ТаблицаМатериалыИУслуги.Ссылка.Статус,
		|	
		|		ВЫБОР
		|			КОГДА ТаблицаМатериалыИУслуги.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовНаПроизводство.Создан)
		|				ТОГДА """"
		|			КОГДА ТаблицаМатериалыИУслуги.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовНаПроизводство.КПроизводству)
		|				ТОГДА ВЫБОР
		|						КОГДА ТаблицаМатериалыИУслуги.Ссылка.СтатусГрафикаПроизводства = ЗНАЧЕНИЕ(Перечисление.СтатусыГрафикаПроизводстваВЗаказеНаПроизводство.Рассчитан)
		|							ТОГДА &СостояниеГрафикаВРаботе
		|						КОГДА ТаблицаМатериалыИУслуги.Ссылка.СтатусГрафикаПроизводства = ЗНАЧЕНИЕ(Перечисление.СтатусыГрафикаПроизводстваВЗаказеНаПроизводство.ТребуетсяРассчитать)
		|							ТОГДА &СостояниеГрафикаПостроение
		|					КОНЕЦ
		|		КОНЕЦ,
		|		ТаблицаПродукция.НачатьНеРанее,
		|		ТаблицаПродукция.ДатаПотребности,
		|	
		|		ТаблицаМатериалыИУслуги.Ссылка.Подразделение,
		|		ТаблицаМатериалыИУслуги.Ссылка.НаправлениеДеятельности,
		|	
		|		ТаблицаПродукция.НомерСтроки,
		|		ТаблицаПродукция.КодСтроки,
		|		ТаблицаМатериалыИУслуги.КлючСвязи,
		|		ТаблицаМатериалыИУслуги.КлючСвязиПродукция,
		|	
		|		ИСТИНА КАК ПроизводитсяВПроцессе,
		|		НЕОПРЕДЕЛЕНО КАК ЭтапПроизводства,
		|		"""" КАК ПредставлениеЭтапа,
		|	
		|		ТаблицаПродукция.Назначение,
		|		ТаблицаМатериалыИУслуги.Номенклатура,
		|		ТаблицаМатериалыИУслуги.Характеристика,
		|		ТаблицаМатериалыИУслуги.КоличествоУпаковок
		|	
		|	ИЗ
		|		Документ.ЗаказНаПроизводство.МатериалыИУслуги КАК ТаблицаМатериалыИУслуги
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.Продукция КАК ТаблицаПродукция
		|			ПО (ТаблицаПродукция.КлючСвязи = ТаблицаМатериалыИУслуги.КлючСвязиПродукция)
		|				И (ТаблицаПродукция.Ссылка = ТаблицаМатериалыИУслуги.Ссылка)
		|	ГДЕ
		|		ТаблицаМатериалыИУслуги.ИсточникПолученияПолуфабриката = &Спецификация
		|		И ТаблицаМатериалыИУслуги.Ссылка.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовНаПроизводство.Закрыт)
		|		И НЕ ТаблицаМатериалыИУслуги.Ссылка.ПометкаУдаления";
	
	Запрос = Новый Запрос;
	Запрос.Текст = СтрЗаменить("
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СписокЗаказов.Заказ                 КАК Заказ,
		|	СписокЗаказов.Номер                 КАК Номер,
		|	СписокЗаказов.Дата                  КАК Дата,
		|	СписокЗаказов.Статус                КАК Статус,
		|
		|	СписокЗаказов.СостояниеГрафика      КАК СостояниеГрафика,
		|	СписокЗаказов.НачалоПроизводства    КАК НачалоПроизводства,
		|	СписокЗаказов.ОкончаниеПроизводства КАК ОкончаниеПроизводства,
		|
		|	СписокЗаказов.Подразделение           КАК Подразделение,
		|	СписокЗаказов.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|
		|	СписокЗаказов.НомерСтроки           КАК НомерСтроки,
		|	СписокЗаказов.КодСтроки             КАК КодСтроки,
		|	СписокЗаказов.КлючСвязи             КАК КлючСвязи,
		|	СписокЗаказов.КлючСвязиПродукция    КАК КлючСвязиПродукция,
		|
		|	СписокЗаказов.ПроизводитсяВПроцессе КАК ПроизводитсяВПроцессе,
		|	СписокЗаказов.ЭтапПроизводства      КАК ЭтапПроизводства,
		|	СписокЗаказов.ПредставлениеЭтапа    КАК ПредставлениеЭтапа,
		|
		|	СписокЗаказов.Назначение                   КАК Назначение,
		|	СписокЗаказов.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры,
		|	СписокЗаказов.Номенклатура                 КАК Номенклатура,
		|	СписокЗаказов.Характеристика               КАК Характеристика,
		|	ВЫБОР
		|		КОГДА СписокЗаказов.Номенклатура.ИспользованиеХарактеристик В (
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеДляВидаНоменклатуры), 
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеСДругимВидомНоменклатуры), 
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ИндивидуальныеДляНоменклатуры))
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ                               КАК ХарактеристикиИспользуются,
		|	СписокЗаказов.Количество            КАК Количество
		|
		|ИЗ
		|	&Таблица КАК СписокЗаказов
		|
		|УПОРЯДОЧИТЬ ПО
		|	НачалоПроизводства, Заказ, ПроизводитсяВПроцессе УБЫВ", "&Таблица", "(" + ТекстСписокЗаказов + ")");
	
	Запрос.УстановитьПараметр("Спецификация", Спецификация);
	Запрос.УстановитьПараметр("ПустойКлючСвязи", ПустойКлючСвязи);
	
	Запрос.УстановитьПараметр("СостояниеГрафикаВРаботе", НСтр("ru = 'В работе';
																|en = 'In progress'"));
	Запрос.УстановитьПараметр("СостояниеГрафикаПостроение", НСтр("ru = 'Построение';
																|en = 'Build'"));
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = СписокЗаказов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
		НомерЗаказа = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Выборка.Номер);
		ПредставлениеЗаказа = СтрШаблон(НСтр("ru = '№%1 от %2 (строка %3)';
											|en = 'No. %1 from %2 (line %3)'"), 
										НомерЗаказа, 
										Формат(Выборка.Дата, "ДЛФ=D"),	
										Формат(Выборка.НомерСтроки, "ЧГ="));
		НоваяСтрока.ПредставлениеЗаказа = ПредставлениеЗаказа;
		
	КонецЦикла;
//-- Устарело_Производство21
	
	ОпределитьПроизводствоЗапущено();
	
КонецПроцедуры

&НаСервере
Процедура ОпределитьПроизводствоЗапущено()

//++ Устарело_Производство21	
	ТаблицаСписокЗаказов = СписокЗаказов.Выгрузить(, "Заказ,КодСтроки");
	ТаблицаСписокЗаказов.Свернуть("Заказ,КодСтроки");
	
	Заказы21Запущены = ПланированиеПроизводства.ЭтапыПоКоторымЗапущеноПроизводство(ТаблицаСписокЗаказов);
	
	Для каждого ДанныеСтроки Из СписокЗаказов Цикл
		
		СтруктураПоиска = Новый Структура("Распоряжение,КодСтрокиПродукция", ДанныеСтроки.Заказ, ДанныеСтроки.КодСтроки);
		ПроизводствоЗапущено = (Заказы21Запущены.НайтиСтроки(СтруктураПоиска).Количество() <> 0);
		ДанныеСтроки.ПроизводствоЗапущено = ПроизводствоЗапущено;
		
	КонецЦикла;
//-- Устарело_Производство21
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаменитьСпецификациюЗавершение(НоваяСпецификация)

	ВыполнитьЗаменуВЗаказахНаСервере(НоваяСпецификация);
	
	ОповеститьОбИзменениях = Ложь;
	
	Если ВсегоЗамен = 0 Тогда
		
		ПоказатьПредупреждение(, НСтр("ru = 'Необходимо выбрать строки, в которых требуется выполнить замену.';
										|en = 'Select lines in which replacement should be made.'"));
		Возврат;
		
	ИначеЕсли ВыполненоЗамен = ВсегоЗамен Тогда
		
		// Успешно заменили во всех заказах
		ТекстЗавершеннойОперации = НСтр("ru = 'Выполнена замена во всех выбранных строках';
										|en = 'Replacement is made in all selected lines'");
		ТекстПоясненияЗавершеннойОперации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
														НСтр("ru = 'Изменено строк заказов: %1 из %2';
															|en = 'Order lines changed: %1 out of %2'"),
														Формат(ВыполненоЗамен, "ЧГ=0"),
														Формат(ВсегоЗамен, "ЧГ=0"));
														
		ПоказатьОповещениеПользователя(ТекстЗавершеннойОперации,, ТекстПоясненияЗавершеннойОперации);
		
		ОповеститьОбИзменениях = Истина;
		
	ИначеЕсли ВыполненоЗамен = 0 Тогда
		
		// Ни в одном заказе не смогли заменить
		ПоказатьПредупреждение(,НСтр("ru = 'Не удалось выполнить замену.';
									|en = 'Cannot replace.'"));
		Возврат;
		
	Иначе
		
		// В некоторых заказах не смогли заменить
		ТекстЗавершеннойОперации = НСтр("ru = 'Замена выполнена не во всех выбранных строках';
										|en = 'Replacement is made not in all selected lines'");
		ТекстПоясненияЗавершеннойОперации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
														НСтр("ru = 'Изменено строк заказов: %1 из %2';
															|en = 'Order lines changed: %1 out of %2'"),
														Формат(ВыполненоЗамен, "ЧГ=0"),
														Формат(ВсегоЗамен, "ЧГ=0"));
														
		ПоказатьОповещениеПользователя(ТекстЗавершеннойОперации,, ТекстПоясненияЗавершеннойОперации);
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
										НСтр("ru = 'Замена выполнена не во всех строках заказов (в %1 из %2).';
											|en = 'Replacement is not made in all order lines (in %1 out of %2).'"), 
										Формат(ВыполненоЗамен, "ЧГ=0"), 
										Формат(ВсегоЗамен, "ЧГ=0"));
										
		ПоказатьПредупреждение(, ТекстСообщения);
		
		ОповеститьОбИзменениях = Истина;
		
	КонецЕсли; 
	
//++ Устарело_Производство21
	Если ОповеститьОбИзменениях Тогда
		
		Оповестить("Запись_ЗаказНаПроизводство",, "ПрименениеСпецификацииВЗаказах");
		ОповеститьОбИзменении(Тип("ДокументСсылка.ЗаказНаПроизводство"));
		
	КонецЕсли;
//-- Устарело_Производство21
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьЗаменуВЗаказахНаСервере(Знач НоваяСпецификация)

//++ Устарело_Производство21
	// Определим для каких изделий подходит данная спецификация
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Таблица.ВидНоменклатуры КАК ВидНоменклатуры,
	|	Таблица.Номенклатура    КАК Номенклатура,
	|	Таблица.Характеристика  КАК Характеристика
	|ИЗ
	|	Справочник.РесурсныеСпецификации.ВыходныеИзделия КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка = &Спецификация
	|	И Таблица.Ссылка.ТипПроизводственногоПроцесса В (ЗНАЧЕНИЕ(Перечисление.ТипыПроизводственныхПроцессов.Сборка),
	|															ЗНАЧЕНИЕ(Перечисление.ТипыПроизводственныхПроцессов.Ремонт))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Таблица.ОсновноеИзделиеВидНоменклатуры,
	|	Таблица.ОсновноеИзделиеНоменклатура,
	|	Таблица.ОсновноеИзделиеХарактеристика
	|ИЗ
	|	Справочник.РесурсныеСпецификации КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка = &Спецификация
	|	И Таблица.ТипПроизводственногоПроцесса = ЗНАЧЕНИЕ(Перечисление.ТипыПроизводственныхПроцессов.Разборка)");
	Запрос.УстановитьПараметр("Спецификация", НоваяСпецификация);
	
	ИзделияДляКоторыхПодходитСпецификация = Запрос.Выполнить().Выгрузить();
	ИзделияДляКоторыхПодходитСпецификация.Индексы.Добавить("ВидНоменклатуры,Номенклатура,Характеристика");
	
	СтруктураПоиска = Новый Структура("Заменить,ПроизводствоЗапущено", Истина, Ложь);
	ТаблицаЗаказы = СписокЗаказов.Выгрузить(СтруктураПоиска);
	ТаблицаЗаказы.Свернуть("Заказ");
	
	ВсегоЗамен = 0;
	ВыполненоЗамен = 0;
	
	Для каждого СтрокаЗаказ Из ТаблицаЗаказы Цикл
		
		ВыполненоЗаменВЗаказе = 0;
		
		// Получим данные замены в заказе
		СтруктураПоиска = Новый Структура("Заказ,Заменить,ПроизводствоЗапущено", СтрокаЗаказ.Заказ, Истина, Ложь);
		СписокСтрок = СписокЗаказов.НайтиСтроки(СтруктураПоиска);
		ДанныеЗамены21 = Новый Массив;
		СтрокиВКоторыхВыполняетсяЗамена21 = Новый Массив;
		Для каждого ДанныеСтроки Из СписокСтрок Цикл
			ВсегоЗамен = ВсегоЗамен + 1;
			
			// Нужно убедиться, что спецификация подходит для указанной строки заказа
			СтруктураПоискаПоИзделию = Новый Структура("ВидНоменклатуры,Номенклатура,Характеристика",
				ДанныеСтроки.ВидНоменклатуры,
				Справочники.Номенклатура.ПустаяСсылка(),
				Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
			
			НайденныеСтроки = ИзделияДляКоторыхПодходитСпецификация.НайтиСтроки(СтруктураПоискаПоИзделию);
			Если НайденныеСтроки.ВГраница() = -1 Тогда
				СтруктураПоискаПоИзделию.Номенклатура = ДанныеСтроки.Номенклатура;
				НайденныеСтроки = ИзделияДляКоторыхПодходитСпецификация.НайтиСтроки(СтруктураПоискаПоИзделию);
				Если НайденныеСтроки.ВГраница() = -1 Тогда
					Если ЗначениеЗаполнено(ДанныеСтроки.Характеристика) Тогда
						СтруктураПоискаПоИзделию.Характеристика = ДанныеСтроки.Характеристика;
						НайденныеСтроки = ИзделияДляКоторыхПодходитСпецификация.НайтиСтроки(СтруктураПоискаПоИзделию);
						Если НайденныеСтроки.ВГраница() = -1 Тогда
							Продолжить;
						КонецЕсли;
					Иначе
						Продолжить;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			ПараметрыЗамены = Новый Структура;
			ПараметрыЗамены.Вставить("КлючСвязи", ДанныеСтроки.КлючСвязи);
			ПараметрыЗамены.Вставить("КлючСвязиПродукция", ДанныеСтроки.КлючСвязиПродукция);
			ПараметрыЗамены.Вставить("Спецификация", НоваяСпецификация);
			
			ДанныеЗамены21.Добавить(ПараметрыЗамены);
			СтрокиВКоторыхВыполняетсяЗамена21.Добавить(ДанныеСтроки);
			
			ВыполненоЗаменВЗаказе = ВыполненоЗаменВЗаказе + 1;
			
		КонецЦикла;
		
		Если ДанныеЗамены21.Количество() <> 0 Тогда
			
			ЗаказОбъект = ДанныеСтроки.Заказ.ПолучитьОбъект();
			ЗаказОбъект.ЗаменитьСпецификации(ДанныеЗамены21);
			
			Если ЗаказОбъект.ПроверитьЗаполнение() Тогда
				
				Попытка
					
					Если ЗаказОбъект.Проведен Тогда
						ЗаказОбъект.Записать(РежимЗаписиДокумента.Проведение);
					Иначе
						ЗаказОбъект.Записать(РежимЗаписиДокумента.Запись);
					КонецЕсли;
					ВыполненоЗамен = ВыполненоЗамен + ВыполненоЗаменВЗаказе;
					
					Для каждого ДанныеСтроки Из СтрокиВКоторыхВыполняетсяЗамена21 Цикл
						ДанныеСтроки.ТекущаяСпецификацияБольшеНеИспользуется = (НоваяСпецификация <> Спецификация);
					КонецЦикла;
					
				Исключение
					
					СобытиеЖурналаРегистрации = ПроизводствоСервер.СобытиеЗаменаСпецификаций();
					
					ЗаписьЖурналаРегистрации(
						СобытиеЖурналаРегистрации, 
						УровеньЖурналаРегистрации.Ошибка,
						,
						,
						ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					
				КонецПопытки;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла; 
	//-- Устарело_Производство21
	
КонецПроцедуры

//-- НЕ УТКА

#КонецОбласти
