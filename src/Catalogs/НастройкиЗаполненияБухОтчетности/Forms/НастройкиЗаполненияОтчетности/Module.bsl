
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("НастройкаЗаполненияОтчетности") Тогда
		Настройка = Параметры.НастройкаЗаполненияОтчетности; //СправочникСсылка.НастройкиЗаполненияБухОтчетности
	КонецЕсли;
	
	НастраиваемаяБухгалтерскаяОтчетность.НастроитьКомандыГенерацииНастроек(ЭтотОбъект);
	
	ОбновитьРедакциюФормы(Настройка, РедакцияФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьЭлементыФормы("");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьЭлементыФормы(Команда)
	
	ОбновитьРедакциюФормы(Настройка, РедакцияФормы);
	
	Элементы.РедакцияФормы.Заголовок = ЗаголовокРедакцииФормы(РедакцияФормы);
	ОбновитьВидимостьЭлементов();
	
	ЗадатьОтборПоНастройке();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьСтроку(Команда)
	
	ПараметрыНовойСтроки = Новый Структура;
	ПараметрыНовойСтроки.Вставить("Родитель", Элементы.СтруктураСтрок.ТекущаяСтрока);
	ПараметрыНовойСтроки.Вставить("ОсновнаяСтрока", Неопределено);
	ПараметрыНовойСтроки.Вставить("ЭтоОсновнаяСтрока", Истина);
	ПараметрыНовойСтроки.Вставить("Регламентированная", Ложь);
	ПараметрыНовойСтроки.Вставить("ДобавлениеДопСтрок", Истина);
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ПараметрыНовойСтроки", ПараметрыНовойСтроки);
	
	ОткрытьФорму("Справочник.СтрокиБухОтчетности.ФормаОбъекта", ПараметрыОткрытия, ЭтаФорма);
	
КонецПроцедуры

 &НаКлиенте
Процедура СоздатьДопСтроку(Команда)
	
	ПараметрыОткрытия = Новый Структура;
	
	ПараметрыНовойСтроки = Новый Структура;
	ПараметрыНовойСтроки.Вставить("Родитель", Элементы.СтруктураСтрок.ТекущаяСтрока);
	ПараметрыНовойСтроки.Вставить("ОсновнаяСтрока", Элементы.ОсновныеСтроки.ТекущиеДанные.Ссылка);
	ПараметрыНовойСтроки.Вставить("ЭтоОсновнаяСтрока", Ложь);
	ПараметрыНовойСтроки.Вставить("Регламентированная", Ложь);
	ПараметрыНовойСтроки.Вставить("ДобавлениеДопСтрок", Ложь);
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ПараметрыНовойСтроки", ПараметрыНовойСтроки);

	ОткрытьФорму("Справочник.СтрокиБухОтчетности.ФормаОбъекта", ПараметрыОткрытия, ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура НастройкаПриИзменении(Элемент)
	
	ОбновитьРедакциюФормы(Настройка, РедакцияФормы);
	ОбновитьЭлементыФормы("");
	
КонецПроцедуры

&НаКлиенте
Процедура СтруктураСтрокПриАктивизацииСтроки(Элемент)
	
	Если Элементы.СтруктураСтрок.ТекущиеДанные <> Неопределено Тогда
		ОсновныеСтроки.Параметры.УстановитьЗначениеПараметра("Родитель", Элементы.СтруктураСтрок.ТекущиеДанные.Ссылка);
		НастроитьКомандыСтрок(ДоступноСозданиеОсновныхСтрок(Элементы.СтруктураСтрок.ТекущиеДанные.ФормаОтчетности));
	Иначе
		ОсновныеСтроки.Параметры.УстановитьЗначениеПараметра("Родитель", Неопределено);
		НастроитьКомандыСтрок(Ложь);
		НастроитьКомандыДопСтрок(Ложь);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОсновныеСтрокиПриАктивизацииСтроки(Элемент)
	
	Если Элементы.ОсновныеСтроки.ТекущиеДанные <> Неопределено Тогда
		ДополнительныеСтроки.Параметры.УстановитьЗначениеПараметра("ОсновнаяСтрока", Элементы.ОсновныеСтроки.ТекущиеДанные.Ссылка);
		НастроитьКомандыДопСтрок(Элементы.ОсновныеСтроки.ТекущиеДанные.ДобавлениеДопСтрок);
	Иначе
		ДополнительныеСтроки.Параметры.УстановитьЗначениеПараметра("ОсновнаяСтрока", Неопределено);
		НастроитьКомандыДопСтрок(Ложь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Программно генерируемая команда создания настроек.
// 
// Параметры:
//  Команда - КомандаКомандногоИнтерфейса
&НаКлиенте
Процедура СгенерироватьНастройкиПоУмолчанию(Команда) Экспорт
	
	НастраиваемаяБухгалтерскаяОтчетностьКлиент.СгенерироватьНастройкиПоУмолчанию(Настройка, ЭтотОбъект, Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВидимостьЭлементов()
	
	Если Настройка = ПредопределенноеЗначение("Справочник.НастройкиЗаполненияБухОтчетности.НеНастраивается") Тогда
		Элементы.СтруктураСтрок.Видимость = Ложь;
		Элементы.ОсновныеСтроки.Видимость = Ложь;
		Элементы.ДополнительныеСтроки.Видимость = Ложь;
		Элементы.КартинкаИнформация.Видимость = Истина;
		Элементы.Информация.Видимость = Истина;
	Иначе
		Элементы.СтруктураСтрок.Видимость = Истина;
		Элементы.ОсновныеСтроки.Видимость = Истина;
		Элементы.ДополнительныеСтроки.Видимость = Истина;
		Элементы.КартинкаИнформация.Видимость = Ложь;
		Элементы.Информация.Видимость = Ложь;
	КонецЕсли;
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ОсновныеСтрокиСоздатьСтроку", "Видимость", РедакцияФормы = "ФормаОтчета2025Кв1");
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьКомандыДопСтрок(ДопСтрокиДоступны)
	
	СтруктураКоманд = Новый Структура;
	
	СтруктураКоманд.Вставить("ДополнительныеСтрокиСоздатьДопСтроку");
	СтруктураКоманд.Вставить("ДополнительныеСтрокиКонтекстноеМенюСоздатьДопСтроку");
	
	Для Каждого Команда Из СтруктураКоманд Цикл
		ЭУ = Элементы.Найти(Команда.Ключ);
		Если ЭУ <> Неопределено Тогда
			ЭУ.Доступность = ДопСтрокиДоступны;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьКомандыСтрок(ДоступностьКоманды)
	
	СтруктураКоманд = Новый Структура;
	
	СтруктураКоманд.Вставить("ОсновныеСтрокиСоздатьСтроку");

	Для Каждого Команда Из СтруктураКоманд Цикл
		ЭУ = Элементы.Найти(Команда.Ключ);
		Если ЭУ <> Неопределено Тогда
			ЭУ.Доступность = ДоступностьКоманды;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗадатьОтборПоНастройке()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СтруктураСтрок, "Владелец", Настройка, ВидСравненияКомпоновкиДанных.Равно);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОбновитьРедакциюФормы(Настройка, РедакцияФормы)
	
	РедакцияФормы = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Настройка, "РедакцияФормы");
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЗаголовокРедакцииФормы(РедакцияФормы)
	
	Заголовок = "";
	
	Если РедакцияФормы = "ФормаОтчета2025Кв1" Тогда
		Заголовок = "Форма №157н, утверждена приказом Минфина России от 04.10.2023г.";
	ИначеЕсли РедакцияФормы = "ФормаОтчета2019Кв1" Тогда
		Заголовок = "Форма №66н от 02.07.2010 г., в ред. приказа Минфина России от 19.04.2019 № 61н";
	КонецЕсли;
	
	Возврат Заголовок;

КонецФункции

&НаКлиенте
Функция ДоступноСозданиеОсновныхСтрок(ФормаОтчетности)
	
	ДоступностьКоманды = Ложь;
	
	Если ФормаОтчетности = ПредопределенноеЗначение("Перечисление.ФормыБухгалтерскойОтчетности.Баланс")
		Или ФормаОтчетности = ПредопределенноеЗначение("Перечисление.ФормыБухгалтерскойОтчетности.ОФР") Тогда
		ДоступностьКоманды = Истина;
	КонецЕсли;
	
	Возврат ДоступностьКоманды;
	
КонецФункции

#КонецОбласти





