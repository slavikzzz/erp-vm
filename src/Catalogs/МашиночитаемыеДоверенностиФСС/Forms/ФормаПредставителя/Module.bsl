
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураДанных = Параметры.СтруктураДанных;
	
	Если СтруктураДанных.Свойство("ТолькоПросмотрФормы") И СтруктураДанных.ТолькоПросмотрФормы Тогда
		Элементы.ПредставительЮЛ_РоссийскаяОрганизация.ТолькоПросмотр 	= Истина;
		Элементы.ПредставительЮЛ_НаимОрг.ТолькоПросмотр 				= Истина;
		Элементы.ПредставительЮЛ_ИНН.ТолькоПросмотр 					= Истина;
		Элементы.ПредставительЮЛ_КПП.ТолькоПросмотр 					= Истина;
		Элементы.ПредставительЮЛ_ОГРН.ТолькоПросмотр 					= Истина;
		Элементы.ПредставительЮЛ_Адр.ТолькоПросмотр 					= Истина;
		Элементы.ФормаКнопкаСохранить.Доступность 						= Ложь;
	КонецЕсли;
	
	Если СтруктураДанных <> Неопределено Тогда
		ПредставительЮЛ_РоссийскаяОрганизация 	= НЕ СтруктураДанных.ПредставительЮЛ_ИностраннаяОрганизация;
		ПредставительЮЛ_НаимОрг 				= СтруктураДанных.ПредставительЮЛ_НаимОрг;
		ПредставительЮЛ_ИНН 					= СтруктураДанных.ПредставительЮЛ_ИНН;
		ПредставительЮЛ_КПП 					= СтруктураДанных.ПредставительЮЛ_КПП;
		ПредставительЮЛ_ОГРН 					= СтруктураДанных.ПредставительЮЛ_ОГРН;
		ПредставительЮЛ_Адр 					= СтруктураДанных.ПредставительЮЛ_Адр;
		ПредставительЮЛ_АдрXML 					= СтруктураДанных.ПредставительЮЛ_АдрXML;
	КонецЕсли;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПредставительЮЛ_РоссийскаяОрганизацияПриИзменении(Элемент)
	УправлениеФормой(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ПредставительЮЛ_АдрНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПредставительЮЛ_АдрНачалоВыбораЗавершение", ЭтотОбъект);
	
	ПараметрыФормы = УправлениеКонтактнойИнформациейКлиент.ПараметрыФормыКонтактнойИнформации(
		ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.ЮрАдресОрганизации"), ПредставительЮЛ_АдрXML);
	
	УправлениеКонтактнойИнформациейКлиент.ОткрытьФормуКонтактнойИнформации(ПараметрыФормы,, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставительЮЛ_АдрОчистка(Элемент, СтандартнаяОбработка)
	
	ПредставительЮЛ_АдрXML = "";
	ПредставительЮЛ_Адр = "";
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)
	
	Если НЕ СохранениеВозможно() Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("ПредставительЮЛ_ИностраннаяОрганизация", 	НЕ ПредставительЮЛ_РоссийскаяОрганизация);
	СтруктураДанных.Вставить("ПредставительЮЛ_НаимОрг", 				ПредставительЮЛ_НаимОрг);
	СтруктураДанных.Вставить("ПредставительЮЛ_ИНН", 					ПредставительЮЛ_ИНН);
	СтруктураДанных.Вставить("ПредставительЮЛ_КПП", 					ПредставительЮЛ_КПП);
	СтруктураДанных.Вставить("ПредставительЮЛ_ОГРН", 					ПредставительЮЛ_ОГРН);
	СтруктураДанных.Вставить("ПредставительЮЛ_Адр", 					ПредставительЮЛ_Адр);
	СтруктураДанных.Вставить("ПредставительЮЛ_АдрXML", 					ПредставительЮЛ_АдрXML);
	
	Закрыть(СтруктураДанных);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Форма.Элементы.ПредставительЮЛ_ОГРН.Видимость = Форма.ПредставительЮЛ_РоссийскаяОрганизация;
	
КонецПроцедуры

&НаКлиенте
Функция СохранениеВозможно()
	
	Отказ = Ложь;
	
	ОчиститьСообщения();
	
	Если НЕ ЗначениеЗаполнено(ПредставительЮЛ_НаимОрг) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не задано наименование организации.';
														|en = 'Не задано наименование организации.'"),,
			"ПредставительЮЛ_НаимОрг",, Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ПредставительЮЛ_ИНН) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не задан ИНН организации.';
														|en = 'Не задан ИНН организации.'"),,
			"ПредставительЮЛ_ИНН",, Отказ);
	ИначеЕсли НЕ РегламентированнаяОтчетностьВызовСервера.ИННСоответствуетТребованиямНаСервере(
		ПредставительЮЛ_ИНН, Ложь) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Указан некорректный ИНН организации.';
														|en = 'Указан некорректный ИНН организации.'"),,
			"ПредставительЮЛ_ИНН",, Отказ);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПредставительЮЛ_КПП) И СтрДлина(ПредставительЮЛ_КПП) <> 9 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Указан некорректный КПП организации.';
														|en = 'Указан некорректный КПП организации.'"),,
			"ПредставительЮЛ_КПП",, Отказ);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПредставительЮЛ_ОГРН) И СтрДлина(ПредставительЮЛ_ОГРН) <> 13 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Указан некорректный ОГРН организации.';
														|en = 'Указан некорректный ОГРН организации.'"),,
			"ПредставительЮЛ_ОГРН",, Отказ);
	КонецЕсли;
	
	Возврат НЕ Отказ;
	
КонецФункции

&НаКлиенте
Процедура ПредставительЮЛ_АдрНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПредставительЮЛ_АдрXML = Результат.КонтактнаяИнформация;
	ПредставительЮЛ_Адр = ДокументооборотСКОВызовСервера.ПредставлениеКонтактнойИнформации(Результат.КонтактнаяИнформация);
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти
