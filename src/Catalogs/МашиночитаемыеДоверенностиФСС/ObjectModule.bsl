#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если ПометкаУдаления ИЛИ Ссылка.ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	
	ТекстНомер = "";
	Если ЗначениеЗаполнено(НомерДоверенности) Тогда
		ТекстНомер = СтрШаблон(
			НСтр("ru = 'с идентификатором %1';
				|en = 'с идентификатором %1'"),
			НомерДоверенности);
	КонецЕсли;
	
	ТекстДатаВыдачи = "";
	Если ЗначениеЗаполнено(ДатаВыдачи) Тогда
		ТекстДатаВыдачи = Формат(ДатаВыдачи, "ДФ='дд.ММ.гггг'");
		ТекстДатаВыдачи = СтрШаблон(
			НСтр("ru = 'от %1 г.';
				|en = 'от %1 г.'"),
			ТекстДатаВыдачи);
	КонецЕсли;
	
	ТекстНаименования = СтрШаблон(
		НСтр("ru = 'Машиночитаемая доверенность (ФСС)%2%1';
			|en = 'Машиночитаемая доверенность (ФСС)%2%1'"),
		?(ЗначениеЗаполнено(ТекстНомер), " " + ТекстНомер, ""),
		?(ЗначениеЗаполнено(ТекстДатаВыдачи), " " + ТекстДатаВыдачи, ""));
	Наименование = ДокументооборотСКОКлиентСервер.ЗаменитьПФРиФССнаСФР(ТекстНаименования, Истина, Истина);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли