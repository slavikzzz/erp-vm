
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, "ВидОрганизации, Организация, КодОрганаПФР, НаименованиеТерриториальногоОрганаПФР, ИПРегистрационныйНомерПФР");
	
	Если ВидОрганизации = "ИндивидуальныйПредприниматель" Тогда
		Элементы.ГруппаОтступПФРИП.Видимость = Истина;
		Элементы.ГруппаОтступРеквизитыПФР.ОтображатьЗаголовок = Истина;
		Элементы.ГруппаОтступРеквизитыПФР.Заголовок = НСтр("ru = 'Отчетность за сотрудников:';
															|en = 'Reporting for employees:'");
	Иначе
		Элементы.ГруппаОтступПФРИП.Видимость = Ложь;
	КонецЕсли;
	
	//++ НЕ УТ
	РедактированиеПериодическихСведений.ПрочитатьЗаписьДляРедактированияВФорме(ЭтаФорма, "ИсторияРегистрацийВОрганеПФР", Организация);
	ИсторияРегистрацийВОрганеПФРПериодСтрокой = ПредставлениеПериода(ИсторияРегистрацийВОрганеПФР.Период, КонецКвартала(ИсторияРегистрацийВОрганеПФР.Период), "ФП = Истина");
	//-- НЕ УТ
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	//++ НЕ УТ
	РедактированиеПериодическихСведенийКлиент.ОбработкаОповещения(ЭтаФорма, Организация, ИмяСобытия, Параметр, Источник);
	ИсторияРегистрацийВОрганеПФРПериодСтрокой = ПредставлениеПериода(ИсторияРегистрацийВОрганеПФР.Период, КонецКвартала(ИсторияРегистрацийВОрганеПФР.Период), "ФП = Истина");
	//-- НЕ УТ
	Возврат;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("СохранитьИЗакрыть", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповещение, Отказ, ЗавершениеРаботы,, ТекстПредупреждения);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РегистрационныйНомерПФРПриИзменении(Элемент)
	//++ НЕ УТ
	РедактированиеПериодическихСведенийКлиентСервер.ОбновитьОтображениеПолейВвода(ЭтаФорма, "ИсторияРегистрацийВОрганеПФР", Организация);
	//-- НЕ УТ
	Возврат; // в УТ11 не используется
КонецПроцедуры

&НаКлиенте
Процедура ИсторияРегистрацийВОрганеПФРПериодПриИзменении(Элемент)
	//++ НЕ УТ
	ЗарплатаКадрыКлиент.ОрганизацииФормаЭлементаИсторияРегистрацийВОрганеПФРПериодПриИзменении(ЭтаФорма);
	//-- НЕ УТ
	Возврат; // в УТ11 не используется
КонецПроцедуры

&НаКлиенте
Процедура ИсторияРегистрацийВОрганеПФРПериодНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	//++ НЕ УТ
	ЗарплатаКадрыКлиент.ОрганизацииФормаЭлементаИсторияРегистрацийВОрганеПФРПериодНачалоВыбора(ЭтаФорма);
	//-- НЕ УТ
	Возврат             
КонецПроцедуры

&НаКлиенте
Процедура ИсторияРегистрацийВОрганеПФРПериодРегулирование(Элемент, Направление, СтандартнаяОбработка)
	//++ НЕ УТ
	ЗарплатаКадрыКлиент.ОрганизацииФормаЭлементаИсторияРегистрацийВОрганеПФРПериодРегулирование(ЭтаФорма, Направление);
	//-- НЕ УТ
	Возврат
КонецПроцедуры

&НаКлиенте
Процедура ИсторияРегистрацийВОрганеПФРПериодАвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	//++ НЕ УТ
	ЗарплатаКадрыКлиент.ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	//-- НЕ УТ
	Возврат
КонецПроцедуры

&НаКлиенте
Процедура ИсторияРегистрацийВОрганеПФРПериодОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, Параметры, СтандартнаяОбработка)
	//++ НЕ УТ
	ЗарплатаКадрыКлиент.ВводМесяцаОкончаниеВводаТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	//-- НЕ УТ
	Возврат
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	СохранитьИЗакрытьНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ИсторияРегистрацийВОрганеПФРИстория(Команда)
	//++ НЕ УТ
	РедактированиеПериодическихСведенийКлиент.ОткрытьИсторию("ИсторияРегистрацийВОрганеПФР", Организация, ЭтаФорма, ТолькоПросмотр);
	//-- НЕ УТ
	Возврат
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПрочитатьНаборЗаписейПериодическихСведений(ИмяРегистра, ВедущийОбъект) Экспорт
	
	РедактированиеПериодическихСведений.ПрочитатьНаборЗаписей(ЭтаФорма, ИмяРегистра, ВедущийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИЗакрыть(Результат, ДополнительныеПараметры) Экспорт 
	
	Отказ = Ложь;
	СохранитьДанныеНаСервере(Отказ);
	
	Если Не Отказ Тогда
		
		СтруктураПараметров = Новый Структура;
		//++ НЕ УТ
		СтруктураПараметров.Вставить("КодОрганаПФР", КодОрганаПФР);
		СтруктураПараметров.Вставить("НаименованиеТерриториальногоОрганаПФР", НаименованиеТерриториальногоОрганаПФР);
		СтруктураПараметров.Вставить("РегистрационныйНомерПФР", ИсторияРегистрацийВОрганеПФР.РегистрационныйНомерПФР);
		СтруктураПараметров.Вставить("ИПРегистрационныйНомерПФР", ИПРегистрационныйНомерПФР);
		СтруктураПараметров.Вставить("РегистрационныйНомерСФР", ИсторияРегистрацийВОрганеПФР.РегистрационныйНомерСФР);
		//-- НЕ УТ
		Закрыть(СтруктураПараметров);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИЗакрытьНаКлиенте(ЗакрытьФорму = Истина)

	ДополнительныеПараметры = Новый Структура("ЗакрытьФорму", ЗакрытьФорму);
	
	Оповещение = Новый ОписаниеОповещения("СохранитьИЗакрытьНаКлиентеЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	СохранитьДанные(Ложь, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИЗакрытьНаКлиентеЗавершение(Отказ, ДополнительныеПараметры) Экспорт 

	Если Не Отказ И Открыта() Тогда
		
		Модифицированность = Ложь;
		Если ДополнительныеПараметры.ЗакрытьФорму Тогда
			
			СтруктураПараметров = Новый Структура;
			//++ НЕ УТ
			СтруктураПараметров.Вставить("КодОрганаПФР", КодОрганаПФР);
			СтруктураПараметров.Вставить("НаименованиеТерриториальногоОрганаПФР", НаименованиеТерриториальногоОрганаПФР);
			СтруктураПараметров.Вставить("РегистрационныйНомерПФР", ИсторияРегистрацийВОрганеПФР.РегистрационныйНомерПФР);
			СтруктураПараметров.Вставить("ИПРегистрационныйНомерПФР", ИПРегистрационныйНомерПФР);
			СтруктураПараметров.Вставить("РегистрационныйНомерСФР", ИсторияРегистрацийВОрганеПФР.РегистрационныйНомерСФР);
			//-- НЕ УТ
			Закрыть(СтруктураПараметров);
			
		КонецЕсли;
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьДанные(Отказ, ОповещениеЗавершения = Неопределено)
	
	Если Не Модифицированность Тогда
		Если ОповещениеЗавершения <> Неопределено Тогда 
			ВыполнитьОбработкуОповещения(ОповещениеЗавершения, Отказ);
		КонецЕсли;
		Возврат;
	КонецЕсли; 
		
	//++ НЕ УТ
	ЗапроситьРежимИзмененияРегистрацийПФР(ИсторияРегистрацийВОрганеПФР.Период, Отказ, ОповещениеЗавершения);
	//-- НЕ УТ
	
КонецПроцедуры

//++ НЕ УТ

&НаКлиенте
Процедура ЗапроситьРежимИзмененияРегистрацийПФР(ДатаИзменения, Отказ, ОповещениеЗавершения)
	
	ТекстКнопкиДа = НСтр("ru = 'Изменились реквизиты пенсионного фонда';
						|en = 'Pension Fund details were changed'");
	ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'При редактировании изменены реквизиты пенсионного фонда.
						|Если исправлена прежняя запись (она была ошибочной), нажмите ""Исправлена ошибка"".
						|Если изменились сведения с %1, нажмите';
						|en = 'Attributes of Pension Fund were changed during editing.
						|If you changed the previous entry (it was incorrect), click ""Error corrected"". 
						|If data is changed from %1, click'") + " ""%2""",
			Формат(ДатаИзменения, "ДФ='д ММММ гггг ""г""'"),
			ТекстКнопкиДа);
			
	Оповещение = Новый ОписаниеОповещения("СохранитьДанныеЗавершение", ЭтотОбъект, ОповещениеЗавершения);
	РедактированиеПериодическихСведенийКлиент.ЗапроситьРежимИзмененияРегистра(ЭтаФорма, "ИсторияРегистрацийВОрганеПФР", ТекстВопроса, ТекстКнопкиДа, Отказ, Оповещение);
	
КонецПроцедуры
//-- НЕ УТ

&НаКлиенте
Процедура СохранитьДанныеЗавершение(Отказ, ОповещениеЗавершения) Экспорт  
	
	СохранитьДанныеНаСервере(Отказ);
	
	Если НЕ Отказ И ОповещениеЗавершения <> Неопределено Тогда 
		ВыполнитьОбработкуОповещения(ОповещениеЗавершения, Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьДанныеНаСервере(Отказ)
	
	Если Не ПроверитьЗаполнение() Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
		
	РедактированиеПериодическихСведений.ЗаписатьЗаписьПослеРедактированияВФорме(ЭтаФорма, "ИсторияРегистрацийВОрганеПФР", Организация);
	
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	//++ НЕ УТ
	Если ЗначениеЗаполнено(ИсторияРегистрацийВОрганеПФР.РегистрационныйНомерПФР)
		И Не ЗначениеЗаполнено(ИсторияРегистрацийВОрганеПФР.Период) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Поле ""Действует с"" не заполнено.';
					|en = 'The ""Valid from"" field is not populated.'"),,
				"ИсторияРегистрацийВОрганеПФР.Период",,
				Отказ);
	КонецЕсли;
	//-- НЕ УТ
	Возврат;
	
КонецПроцедуры

#КонецОбласти
