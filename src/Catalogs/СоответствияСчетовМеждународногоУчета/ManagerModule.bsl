#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает схему компоновки данных для настройки соответствия счетов
//
// Параметры:
// 	СчетРеглУчета - ПланСчетовСсылка.Хозрасчетный - Счет регл. учета.
//
// Возвращаемое значение:
// 	СхемаКомпоновкиДанных - СхемаКомпоновкиДанных - Схема, применяемая для настройки соответствия счетов.
//
Функция СхемыКомпоновкиДанных(СчетРеглУчета) Экспорт
	
	СхемаКомпоновкиДанных = Справочники.СоответствияСчетовМеждународногоУчета.ПолучитьМакет("СхемаКомпоновкиДанных");
	ПоляНабора = СхемаКомпоновкиДанных.НаборыДанных[0].Поля;
	
	ВидыСубконто = ФинансоваяОтчетностьПовтИсп.ВидыСубконтоСчета(СчетРеглУчета);
	Для НомерСубконто = 1 По 3 Цикл
		ПутьКДанным = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Субконто%1", НомерСубконто);
		Поле = ПоляНабора.Найти(ПутьКДанным);
		СтрокаВидСубконто = ВидыСубконто.Найти(НомерСубконто, "НомерСубконто");
		Если СтрокаВидСубконто <> Неопределено Тогда
			Поле.Заголовок = Строка(СтрокаВидСубконто.ВидСубконто);
			Поле.ТипЗначения = СтрокаВидСубконто.ТипЗначения;
		Иначе
			Поле.ОграничениеИспользования.Условие = Истина;
			Поле.ОграничениеИспользованияРеквизитов.Условие = Истина;
			Поле.ОграничениеИспользования.Поле = Истина;
			Поле.ОграничениеИспользованияРеквизитов.Поле = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат СхемаКомпоновкиДанных;
	
КонецФункции

#КонецОбласти

#КонецЕсли
