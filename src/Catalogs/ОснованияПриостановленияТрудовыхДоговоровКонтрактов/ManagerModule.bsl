#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// ТехнологияСервиса.ВыгрузкаЗагрузкаДанных

// Возвращает реквизиты справочника, которые образуют естественный ключ для элементов справочника.
//
// Возвращаемое значение:
//  Массив из Строка - имена реквизитов, образующих естественный ключ.
//
Функция ПоляЕстественногоКлюча() Экспорт
	
	Результат = Новый Массив;
	
	Результат.Добавить("Наименование");
	
	Возврат Результат;
	
КонецФункции

// Конец ТехнологияСервиса.ВыгрузкаЗагрузкаДанных

#КонецОбласти

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	ДоступныеОснования = ДоступныеНормативныеДокументыОснований();
	Если ЗначениеЗаполнено(ДоступныеОснования) Тогда
		ОтборСправочника = Новый Структура("ДокументОснование", ДоступныеОснования);
		Параметры.Вставить("Отбор", ОтборСправочника);
	КонецЕсли;
	
	ПредставленияОбщихДанных.ОбработкаПолученияДанныхВыбора(
		Справочники.ОснованияПриостановленияТрудовыхДоговоровКонтрактов, 
		ДанныеВыбора, Параметры, СтандартнаяОбработка);
	
КонецПроцедуры


#КонецЕсли

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	ПредставленияОбщихДанныхВызовСервера.ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка);
КонецПроцедуры

Функция ДоступныеНормативныеДокументыОснований() Экспорт
	
	ДоступныеДокументы = Новый Массив;
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьГосударственнуюСлужбу") Тогда
		ДоступныеДокументы.Добавить(Перечисления.НормативныеДокументыОснованийКадровыхПриказов.ТКРФ);
	КонецЕсли;
	
	Возврат ДоступныеДокументы;
	
КонецФункции

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

#Область ОбработчикиПравилРегистрации

// См. ЗарплатаКадрыРасширенныйСинхронизацияДанных.ШаблонОбработчика
Процедура ПриЗаполненииНастроекОбработчиковПравилРегистрации(Настройки) Экспорт
	ЗарплатаКадрыРасширенныйСинхронизацияДанных.ДляРегламентированныхДанных(Настройки);
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура выполняет первоначальное заполнение справочника.
//
Процедура НачальноеЗаполнение() Экспорт
	
	ИмяСправочника = Метаданные.Справочники.ОснованияПриостановленияТрудовыхДоговоровКонтрактов.Имя;
	
	ИмяМакета = ИмяМакетаКлассификатора();
	
	СоответствиеПолей = Новый Соответствие;
	СоответствиеПолей.Вставить("Наименование", "Title");
	СоответствиеПолей.Вставить("ТекстОснования", "Reason");
	
	КлючиПоиска = Новый Структура;
	КлючиПоиска.Вставить("КлючиПоискаСправочника", "Наименование");
	КлючиПоиска.Вставить("КлючиПоискаМакета", "Title");
	
	КонтрактыДоговорыСотрудников.ЗаполнитьСправочникИзМакетаКлассификатора(ИмяСправочника, ИмяМакета, СоответствиеПолей, КлючиПоиска);
	
КонецПроцедуры

Функция ИмяМакетаКлассификатора()
	Возврат "ОснованияПриостановленияТрудовыхДоговоровКонтрактов";
КонецФункции

#КонецОбласти

#КонецЕсли