#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоНовый() Тогда
		СсылкаШаблона = ДополнительныеСвойства.СсылкаШаблона;
		УстановитьСсылкуНового(ДополнительныеСвойства.СсылкаШаблона);
		Если Не ЗначениеЗаполнено(Версия) Тогда
			Версия = КабинетСотрудника.ВерсияФормата();
		КонецЕсли;
		Версия = Справочники.ШаблоныДокументов.НормализованныйНомерВерсии(Версия);
	Иначе
		СсылкаШаблона = Ссылка;
	КонецЕсли;
	
	ХешНастроек = Справочники.ШаблоныДокументов.ХешНастроек(
		ВАрхиве, МакетДокумента, СсылкаШаблона.УникальныйИдентификатор());
	
	НастройкиВарианта = Справочники.ШаблоныДокументов.JSONШаблонаДокументаПоИдентификаторуШаблона(МакетДокумента.Получить(), СсылкаШаблона.УникальныйИдентификатор());
	Настройки = Новый ХранилищеЗначения(НастройкиВарианта, Новый СжатиеДанных(9));
	
	Если Не ЭтоНовый() Тогда
		РеквизитыОбъекта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "ХешНастроек, Версия, Изменен, Настройки");
		Если ХешНастроек <> РеквизитыОбъекта.ХешНастроек Тогда
			ПрежниеНастройкиВарианта = РеквизитыОбъекта.Настройки.Получить();
			Если Справочники.ШаблоныДокументов.JSONШаблоныРазличны(ПрежниеНастройкиВарианта, НастройкиВарианта) Тогда
				СохранитьПрежнююВерсию = Истина;
				Если ЗначениеЗаполнено(ИмяПредопределенныхДанных) Тогда
					Если РеквизитыОбъекта.Изменен Тогда
						ПредопределенныеВерсии = Справочники.ШаблоныДокументов.ПредопределенныеВерсииШаблонов(ИмяПредопределенныхДанных);
						СтрокаВерсии = ПредопределенныеВерсии.Найти(Версия, "Версия");
						Если СтрокаВерсии <> Неопределено
							И Не Справочники.ШаблоныДокументов.JSONШаблоныРазличны(СтрокаВерсии.Настройки, НастройкиВарианта) Тогда
							
							Изменен = Ложь;
						КонецЕсли;
					Иначе
						СохранитьПрежнююВерсию = Ложь;
						Изменен = Истина;
					КонецЕсли;
				КонецЕсли;
				Если СохранитьПрежнююВерсию Тогда
					НоваяСтрока = ИсторияИзменений.Добавить();
					НоваяСтрока.Версия = Справочники.ШаблоныДокументов.НормализованныйНомерВерсии(РеквизитыОбъекта.Версия);
					НоваяСтрока.ДатаРегистрации = ТекущаяДатаСеанса();
					НоваяСтрока.Настройки = Новый ХранилищеЗначения(ПрежниеНастройкиВарианта, Новый СжатиеДанных(9));
				КонецЕсли;
			ИначеЕсли ЗначениеЗаполнено(ИмяПредопределенныхДанных) Тогда
				Изменен = Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли
