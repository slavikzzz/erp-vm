#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Формирует данные для публикации шаблонов в сервисе 1С:Кабинет сотрудника.
// Параметры:
// 		Ссылки - Массив
// Возвращаемое значение:
// 		ТаблицаЗначений
//
Функция ДанныеШаблоновДокументовДляПубликации(Ссылки) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокОтбора", Ссылки);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ШаблоныДокументов.Ссылка КАК Шаблон,
	|	ШаблоныДокументов.Наименование КАК НаименованиеДокумента,
	|	ШаблоныДокументов.ИмяПредопределенныхДанных КАК ИмяПредопределенныхДанных,
	|	ШаблоныДокументов.МакетДокумента КАК МакетДокумента,
	|	ШаблоныДокументов.ТребуетсяСогласование КАК ТребуетсяСогласование,
	|	ШаблоныДокументов.ВАрхиве КАК ВАрхиве,
	|	ЕСТЬNULL(КодыДокументов.Код, """") КАК КодДокументаКадровогоМероприятия,
	|	ШаблоныДокументов.ТребуетПодписания КАК ТребуетПодписания,
	|	ШаблоныДокументов.ТребуетсяНаличиеВложений КАК ТребуетсяНаличиеВложений,
	|	ШаблоныДокументов.ПометкаУдаления КАК ПометкаУдаления
	|ИЗ
	|	Справочник.ШаблоныДокументов КАК ШаблоныДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КодыДокументовКадровыхМероприятий КАК КодыДокументов
	|		ПО ШаблоныДокументов.КодДокументаКадровогоМероприятия = КодыДокументов.Ссылка
	|ГДЕ
	|	ШаблоныДокументов.Ссылка В(&СписокОтбора)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ИмяПредопределенныхДанных";
	ТаблицаДанных = Запрос.Выполнить().Выгрузить();
	ТаблицаДанных.Колонки.Добавить("ДанныеМакета", 		Новый ОписаниеТипов("Строка"));
	ТаблицаДанных.Колонки.Добавить("РазмерМакета", 		Новый ОписаниеТипов("Число"));
	ТаблицаДанных.Колонки.Добавить("УсловияОбластей", 	Новый ОписаниеТипов("Массив"));
	ТаблицаДанных.Колонки.Добавить("РеквизитыОбъектов", Новый ОписаниеТипов("Массив"));
	ТаблицаДанных.Колонки.Добавить("НеВыгружать", 		Новый ОписаниеТипов("Булево"));
	
	Для Каждого СтрокаДанных Из ТаблицаДанных Цикл
		
		// Макет табличного документа
		ДанныеМакетаДокумента = СтрокаДанных.МакетДокумента.Получить();
		Если ДанныеМакетаДокумента = Неопределено Тогда
			СтрокаДанных.НеВыгружать = Истина;
			Продолжить;
		КонецЕсли;
		
		Если ДанныеМакетаДокумента.Области.Найти("Бланк") = Неопределено Тогда
			ДанныеМакетаДокумента.Область(1, , ДанныеМакетаДокумента.ВысотаТаблицы, ).Имя = "Бланк";
		КонецЕсли;
		
		Для НомерСтроки = 1 По ДанныеМакетаДокумента.ВысотаТаблицы Цикл
			Для НомерКолонки = 1 По ДанныеМакетаДокумента.ШиринаТаблицы Цикл
				ОбластьЯчейки = ДанныеМакетаДокумента.Область(НомерСтроки, НомерКолонки, НомерСтроки, НомерКолонки);
				Если ОбластьЯчейки.Заполнение <> ТипЗаполненияОбластиТабличногоДокумента.Текст Тогда
					ПреобразоватьВШаблон = ОбластьЯчейки.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
					ОбластьЯчейки.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Текст;
					Если ПреобразоватьВШаблон Тогда
						ОбластьЯчейки.Текст = СтрШаблон("[%1]", ОбластьЯчейки.Текст);
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
		ПотокЗаписи = Новый ПотокВПамяти;
		ДанныеМакетаДокумента.Записать(ПотокЗаписи);
		ДвоичныеДанныеМакета = ПотокЗаписи.ЗакрытьИПолучитьДвоичныеДанные();
		
		СтрокаДанных.ДанныеМакета = Base64Строка(ДвоичныеДанныеМакета);
		СтрокаДанных.РазмерМакета = ДвоичныеДанныеМакета.Размер();
		
		// Условия областей
		НастройкиШаблона = ЗарплатаКадрыОтчеты.НастройкиШаблонаДляКабинетаСотрудников(
		КлючВариантаОтчета(СтрокаДанных.Шаблон.УникальныйИдентификатор()));
		
		УсловияОбластей = УсловияОбластейИзНастроек(НастройкиШаблона.Настройки, Истина);
		Если ЗначениеЗаполнено(УсловияОбластей) Тогда
			СтрокаДанных.УсловияОбластей = УсловияОбластей;
		КонецЕсли;
		
		// Реквизиты объектов
		РеквизитыОбъектов = Новый Массив;
		Для Каждого ДанныеПараметра Из НастройкиШаблона.СхемаКомпоновкиДанных.Параметры Цикл
			РеквизитыОбъектов.Добавить(ОписаниеРеквизитаШаблонаДокументов(ДанныеПараметра));
		КонецЦикла;
		
		Если ЗначениеЗаполнено(РеквизитыОбъектов) Тогда
			СтрокаДанных.РеквизитыОбъектов = РеквизитыОбъектов;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТаблицаДанных;

КонецФункции

Функция УсловияОбластейИзНастроек(Настройки, ДляВыгрузкиВКабинеты = Ложь)
	УсловияОбластей = Новый Массив;
	Если Настройки <> Неопределено Тогда
		Для Каждого ЭлементОтбора Из Настройки.Отбор.Элементы Цикл
			Если ТипЗнч(ЭлементОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
				ОписаниеУсловий = ОписаниеУсловийВыводаОбластей();
				ОписаниеУсловий.Имя 	= ?(СтрНачинаетсяС(ЭлементОтбора.Представление, "<") , "", ЭлементОтбора.Представление);
				ОписаниеУсловий.Элементы = КоллекцияОтборовОбласти(ЭлементОтбора.Элементы, ДляВыгрузкиВКабинеты);
				УсловияОбластей.Добавить(ОписаниеУсловий);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Возврат УсловияОбластей;
КонецФункции

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СравнениеВерсий") Тогда
		
		ПечатаемыйДокумент = Неопределено;
		ПараметрыПечати.Свойство("ПечатаемыйДокумент", ПечатаемыйДокумент);
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"СравнениеВерсий",
			"Сравнение версий",
			ПечатаемыйДокумент);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПолучениеДанныхДляСервиса

Функция КоллекцияОтборовОбласти(УсловияВыводаОбласти, ДляВыгрузкиВКабинеты = Ложь)
	
	КоллекцияОтборов = Новый Массив;
	Для Каждого ЭлементОтбора Из УсловияВыводаОбласти Цикл
		Если Не ЭлементОтбора.Использование Тогда
			Продолжить;
		КонецЕсли;
		Если ТипЗнч(ЭлементОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			ОписаниеЭлемента = ОписаниеОбъектаЭлементаОтбора();
			ОписаниеЭлемента.ЭтоГруппа 	= Истина;
			ОписаниеЭлемента.ТипГруппы 	= ЭлементОтбора.ТипГруппы;
			ОписаниеЭлемента.Элементы 	= КоллекцияОтборовОбласти(ЭлементОтбора.Элементы, ДляВыгрузкиВКабинеты);
		Иначе
			Если ДляВыгрузкиВКабинеты
				И (ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке
					Или ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии
					Или ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСписке
					Или ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСпискеПоИерархии) Тогда
				
				ОписаниеЭлементаГруппаИли = ОписаниеОбъектаЭлементаОтбора();
				ОписаниеЭлементаГруппаИли.ЭтоГруппа 	= Истина;
				ОписаниеЭлементаГруппаИли.ТипГруппы 	= ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
				ОписаниеЭлементаГруппаИли.Элементы		= Новый Массив;
				
				Если ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСписке
					Или ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСпискеПоИерархии Тогда
					
					ОписаниеЭлементаГруппыНе = ОписаниеОбъектаЭлементаОтбора();
					ОписаниеЭлементаГруппыНе.ЭтоГруппа 	= Истина;
					ОписаниеЭлементаГруппыНе.ТипГруппы 	= ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаНе;
					ОписаниеЭлементаГруппыНе.Элементы 	= ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ОписаниеЭлементаГруппаИли);
					
					ОписаниеЭлемента = ОписаниеЭлементаГруппыНе;
				Иначе
					ОписаниеЭлемента = ОписаниеЭлементаГруппаИли;
				КонецЕсли;
				Для каждого ЭлементСписка Из ЭлементОтбора.ПравоеЗначение Цикл
					ОписаниеЭлементаСписка = ОписаниеУсловияВыводаОбласти();
					ОписаниеЭлементаСписка.ЭтоГруппа 			= Ложь;
					ОписаниеЭлементаСписка.ЛевоеЗначение 		= Строка(ЭлементОтбора.ЛевоеЗначение);
					Если ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии
						Или ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСпискеПоИерархии Тогда
						
						ОписаниеЭлементаСписка.ВидСравнения 		= ВидСравненияКомпоновкиДанных.ВИерархии;
					Иначе
						ОписаниеЭлементаСписка.ВидСравнения 		= ВидСравненияКомпоновкиДанных.Равно;
					КонецЕсли;
					ОписаниеЭлементаСписка.ПравоеЗначение 	= ЭлементСписка.Значение;          
					НормализоватьПравоеЗначениеОтбора(ОписаниеЭлементаСписка.ПравоеЗначение, ОписаниеЭлементаСписка.ТипСсылочногоЗначения);
					ОписаниеЭлементаГруппаИли.Элементы.Добавить(ОписаниеЭлементаСписка);
				КонецЦикла;
					
			Иначе
				ОписаниеЭлемента = ОписаниеУсловияВыводаОбласти();
				ОписаниеЭлемента.ЭтоГруппа 			= Ложь;
				ОписаниеЭлемента.ЛевоеЗначение 		= Строка(ЭлементОтбора.ЛевоеЗначение);
				ОписаниеЭлемента.ВидСравнения 		= ЭлементОтбора.ВидСравнения;
				ОписаниеЭлемента.ПравоеЗначение 	= ЭлементОтбора.ПравоеЗначение;
				НормализоватьПравоеЗначениеОтбора(ОписаниеЭлемента.ПравоеЗначение, ОписаниеЭлемента.ТипСсылочногоЗначения);
			КонецЕсли;
			
		КонецЕсли;
		КоллекцияОтборов.Добавить(ОписаниеЭлемента);
	КонецЦикла; 
	
	Возврат КоллекцияОтборов;
	
КонецФункции

Процедура НормализоватьПравоеЗначениеОтбора(ПравоеЗначение, ТипСсылочногоЗначения)
	Если ЗначениеЗаполнено(ПравоеЗначение) Тогда
		ТипЗначения = ТипЗнч(ПравоеЗначение);
		Если ТипЗначения = Тип("Строка") Тогда
			СловаСтроки = СтрРазделить(ПравоеЗначение, ".");
			Если СловаСтроки.Количество() = 2 Тогда
				Если СловаСтроки[0] = "reasons" Тогда
					ПравоеЗначение = СловаСтроки[1];
					ТипСсылочногоЗначения = "reasons";
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли ОбщегоНазначения.ЭтоСсылка(ТипЗначения) Тогда
			ТипСсылочногоЗначения = ТипСсылочногоЗначения(ТипЗначения);
		ИначеЕсли ТипЗначения = Тип("СписокЗначений") Тогда
			МассивЭлементов = Новый Массив;
			Для каждого ЭлементСписка Из ПравоеЗначение Цикл
				Если ЗначениеЗаполнено(ЭлементСписка.Значение) Тогда
					МассивЭлементов.Добавить(ЭлементСписка.Значение);
				КонецЕсли;
			КонецЦикла;
			Если ЗначениеЗаполнено(МассивЭлементов) Тогда
				ПравоеЗначение = МассивЭлементов;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция ТипСсылочногоЗначения(ТипЗначения)
	Если ТипЗначения = Тип("ПеречислениеСсылка.ПолФизическогоЛица") Тогда
		Возврат "gender";
	ИначеЕсли ТипЗначения = Тип("СправочникСсылка.Должности") Тогда
		Возврат "positions";
	ИначеЕсли ТипЗначения = Тип("СправочникСсылка.ПодразделенияОрганизаций")
		Или ТипЗначения = Тип("СправочникСсылка.СтруктураПредприятия") Тогда
		
		Возврат "divisions";
	ИначеЕсли ТипЗначения = Тип("СправочникСсылка.Организации") Тогда
		Возврат "employers";
	ИначеЕсли ТипЗначения = Тип("СправочникСсылка.ШтатноеРасписание") Тогда
		Возврат "stafflist-positions";
	КонецЕсли;
	Возврат Неопределено
КонецФункции

Функция ОписаниеУсловийВыводаОбластей()
	
	Возврат Новый Структура("Имя,Элементы");

КонецФункции

Функция ОписаниеОбъектаЭлементаОтбора()

	Возврат Новый Структура("ЭтоГруппа,ТипГруппы,Элементы");

КонецФункции

Функция ОписаниеУсловияВыводаОбласти()
	
	Возврат Новый Структура("ЭтоГруппа,ЛевоеЗначение,ВидСравнения,ПравоеЗначение,ТипСсылочногоЗначения");
	
КонецФункции

Функция ОписаниеРеквизитаШаблонаДокументов(ДанныеРеквизита)
	
	ОписаниеРеквизита = Новый Структура("Имя,Представление,Тип,Обязательный,ЗначениеПоУмолчанию,ЗначениеПоУмолчаниюВторое,ДоступныеЗначения");
	ОписаниеРеквизита.Имя 			= "ПараметрыДанных" + ДанныеРеквизита.Имя;
	ОписаниеРеквизита.Представление = ДанныеРеквизита.Заголовок;
	ОписаниеРеквизита.Тип 			= КадровыйЭДО.ТипЗначенияШаблонаДокументов(ДанныеРеквизита.ТипЗначения);
	ОписаниеРеквизита.Обязательный  = ДанныеРеквизита.ЗапрещатьНезаполненныеЗначения;
	Если ТипЗнч(ДанныеРеквизита.Значение) = Тип("СтандартныйПериод") Тогда
		ОписаниеРеквизита.ЗначениеПоУмолчанию			= ДанныеРеквизита.Значение.ДатаНачала;
		ОписаниеРеквизита.ЗначениеПоУмолчаниюВторое	= НачалоДня(ДанныеРеквизита.Значение.ДатаОкончания);
	Иначе
		ОписаниеРеквизита.ЗначениеПоУмолчанию = ДанныеРеквизита.Значение;
	КонецЕсли;
	ДоступныеЗначения = ДанныеРеквизита.ПолучитьДоступныеЗначения();
	Если ЗначениеЗаполнено(ДоступныеЗначения) Тогда
		Если ОписаниеРеквизита.Тип = Перечисления.ТипыРеквизитовШаблоновДокументов.Строка Тогда
			ОписаниеРеквизита.Тип = Перечисления.ТипыРеквизитовШаблоновДокументов.Перечисление;
		КонецЕсли;
		ЗначенияРеквизита = Новый Массив;
		Для Каждого ДоступноеЗначение Из ДоступныеЗначения Цикл
			ЗначенияРеквизита.Добавить(
				Новый Структура("Представление,Значение",
					ДоступноеЗначение.Представление,
					ДоступноеЗначение.Значение));
		КонецЦикла;
		ОписаниеРеквизита.ДоступныеЗначения = ЗначенияРеквизита;
	КонецЕсли;
	
	Возврат ОписаниеРеквизита;
	
КонецФункции

#КонецОбласти

Процедура ДобавитьПолеДокумента(ПоляДокумента, ИмяПоля, ПутьКПолю, ТипЗначения, ЗначениеПоУмолчанию, ДоступноВУсловиях, ДоступныРеквизиты = Ложь, ДоступныеЗначения = Неопределено)
	
	ОписаниеПоля = Новый Структура;
	ОписаниеПоля.Вставить("ИмяПоля",				ИмяПоля);
	ОписаниеПоля.Вставить("ПутьКПолю",				ПутьКПолю);
	Если ТипЗнч(ТипЗначения) = Тип("ОписаниеТипов") Тогда
		ОписаниеПоля.Вставить("Тип",				ТипЗначения);
	Иначе
		ОписаниеПоля.Вставить("Тип",				Новый ОписаниеТипов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ТипЗначения)));
	КонецЕсли;
	ОписаниеПоля.Вставить("ЗначениеПоУмолчанию",	ЗначениеПоУмолчанию);
	ОписаниеПоля.Вставить("ДоступноВУсловиях",		ДоступноВУсловиях);
	ОписаниеПоля.Вставить("ДоступныРеквизиты",		ДоступныРеквизиты);
	ОписаниеПоля.Вставить("ДоступныеЗначения", 		ДоступныеЗначения);
	
	ПоляДокумента.Добавить(ОписаниеПоля);
	
КонецПроцедуры

Функция ДанныеШаблона(ВАрхиве, МакетДокумента, ИдентификаторСсылки)
	
	Настройки = ЗарплатаКадрыОтчеты.НастройкиШаблонаДляКабинетаСотрудников(
		КлючВариантаОтчета(ИдентификаторСсылки));
	
	ОписаниеДанных = Новый Структура;
	ОписаниеДанных.Вставить("Настройки", Настройки);
	ОписаниеДанных.Вставить("Макет", МакетДокумента);
	ОписаниеДанных.Вставить("ВАрхиве", ВАрхиве);
	
	Возврат ОписаниеДанных;
	
КонецФункции

Функция КлючВариантаОтчета(ИдентификаторСсылки) Экспорт
	
	Возврат "ШаблоныДокументов" + ИдентификаторСсылки;
	
КонецФункции

Функция ВариантОтчета(ИдентификаторСсылки, СоздатьЕслиНеНайден = Ложь) Экспорт
	
	КлючВариантаОтчета = КлючВариантаОтчета(ИдентификаторСсылки);
	ИдентификаторОтчета = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
		Метаданные.Отчеты.ШаблоныДокументовКабинетСотрудника);
	
	ВариантОтчета = ВариантыОтчетов.ВариантОтчета(ИдентификаторОтчета, КлючВариантаОтчета);
	Если ВариантОтчета = Неопределено И СоздатьЕслиНеНайден Тогда
		УстановитьПривилегированныйРежим(Истина);
		ВариантОтчета = НовыйВариантОтчета(ИдентификаторОтчета, КлючВариантаОтчета);
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Возврат ВариантОтчета;
	
КонецФункции

Функция НовыйВариантОтчета(ИдентификаторОтчета, КлючВариантаОтчета)
	
	СхемаКомпоновки = Отчеты.ШаблоныДокументовКабинетСотрудника.ПолучитьМакет("МакетКомпоновки");
	Настройки = СхемаКомпоновки.ВариантыНастроек["ПоУмолчанию"].Настройки;
	
	НастройкиВариантаОтчета = ЗарплатаКадрыОтчеты.НоваяСтруктураНастроекПечатнойФормы();
	НастройкиВариантаОтчета.Настройки = Настройки;
	
	ПредопределенныйВариант = Неопределено;
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Отчет", ИдентификаторОтчета);
	Запрос.УстановитьПараметр("КлючВарианта", "ПоУмолчанию");
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПредопределенныеВариантыОтчетов.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ПредопределенныеВариантыОтчетов КАК ПредопределенныеВариантыОтчетов
		|ГДЕ
		|	ПредопределенныеВариантыОтчетов.Отчет = &Отчет
		|	И ПредопределенныеВариантыОтчетов.КлючВарианта = &КлючВарианта
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПредопределенныеВариантыОтчетов.ПометкаУдаления";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ПредопределенныйВариант = Выборка.Ссылка;
	КонецЕсли;
	
	НовыйВариантОтчета = Справочники.ВариантыОтчетов.СоздатьЭлемент();
	НовыйВариантОтчета.Отчет = ИдентификаторОтчета;
	НовыйВариантОтчета.Настройки = Новый ХранилищеЗначения(НастройкиВариантаОтчета);
	НовыйВариантОтчета.КлючВарианта = КлючВариантаОтчета;
	НовыйВариантОтчета.Наименование = КлючВариантаОтчета;
	НовыйВариантОтчета.Описание = НСтр("ru = 'Содержит настройки для подготовки шаблона документа в 1С:Кабинет сотрудника (для служебного использования)';
										|en = 'Includes settings to prepare a document template in 1C:Employee account (for internal use)'");
	НовыйВариантОтчета.ТолькоДляАвтора = Истина;
	НовыйВариантОтчета.ТипОтчета = Перечисления.ТипыОтчетов.Внутренний;
	НовыйВариантОтчета.ПредопределенныйВариант = ПредопределенныйВариант;
	НовыйВариантОтчета.Назначение = Перечисления.НазначенияВариантовОтчетов.ДляКомпьютеровИПланшетов;
	НовыйВариантОтчета.Записать();
	
	Возврат НовыйВариантОтчета.Ссылка;
	
КонецФункции

Функция ХешНастроек(ВАрхиве, МакетДокумента, ИдентификаторСсылки) Экспорт
	Возврат ОбщегоНазначения.КонтрольнаяСуммаСтрокой(
		ОбщегоНазначения.ЗначениеВСтрокуXML(
			ДанныеШаблона(ВАрхиве, МакетДокумента, ИдентификаторСсылки)));
КонецФункции

Процедура ОбновитьШаблоны(ПараметрыОбновления = Неопределено) Экспорт
	
	Если ПараметрыОбновления = Неопределено Тогда
		МассивОбновленных = Новый Массив;
	Иначе
		
		Если ПараметрыОбновления.Свойство("МассивОбновленных") Тогда
			МассивОбновленных = ПараметрыОбновления.МассивОбновленных;
		Иначе
			МассивОбновленных = Новый Массив;
			ПараметрыОбновления.Вставить("МассивОбновленных", МассивОбновленных);
		КонецЕсли;
		
	КонецЕсли;
	
	ОбработкаЗавершена = Истина;
	Для Каждого ИмяПредопределенного Из Метаданные.Справочники.ШаблоныДокументов.ПолучитьИменаПредопределенных() Цикл
		Если МассивОбновленных.Найти(ИмяПредопределенного) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ОбработкаЗавершена = Ложь;
		ОбновитьПредопределенныйШаблонДокумента(ИмяПредопределенного, ПараметрыОбновления);
	КонецЦикла;
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(
		ПараметрыОбновления, "ОбработкаЗавершена", ОбработкаЗавершена);
	
КонецПроцедуры

Процедура ОбновитьПредопределенныйШаблонДокумента(ИмяПредопределенного, ПараметрыОбновления = Неопределено) Экспорт
	
	Если ПараметрыОбновления = Неопределено Тогда
		МассивОбновленных = Новый Массив;
	Иначе
		
		Если ПараметрыОбновления.Свойство("МассивОбновленных") Тогда
			МассивОбновленных = ПараметрыОбновления.МассивОбновленных;
		Иначе
			МассивОбновленных = Новый Массив;
			ПараметрыОбновления.Вставить("МассивОбновленных", МассивОбновленных);
		КонецЕсли;
		
	КонецЕсли;
	
	НовыйОбъект = Истина;
	СсылкаСправочника = Неопределено;
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИмяПредопределенныхДанных", ИмяПредопределенного);
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ШаблоныДокументов.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ШаблоныДокументов КАК ШаблоныДокументов
		|ГДЕ
		|	ШаблоныДокументов.ИмяПредопределенныхДанных = &ИмяПредопределенныхДанных";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		СсылкаСправочника = Выборка.Ссылка;
	КонецЕсли;
	Если СсылкаСправочника = Неопределено Тогда
		ОбъектСправочника = СоздатьЭлемент();
		ОбъектСправочника.ИмяПредопределенныхДанных = ИмяПредопределенного;
		ИдентификаторСсылки = Новый УникальныйИдентификатор;
		СсылкаСправочника = ПолучитьСсылку(ИдентификаторСсылки);
		ОбъектСправочника.УстановитьСсылкуНового(СсылкаСправочника);
		ОбъектСправочника.Наименование = НаименованиеПредопределенногоЭлемента(ИмяПредопределенного);
		ПорядокТекущейВерсии = Неопределено;
	Иначе
		НовыйОбъект = Ложь;
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(
			ПараметрыОбновления, "Справочник.ШаблоныДокументов", "Ссылка", СсылкаСправочника) Тогда
			
			Возврат;
		КонецЕсли;
		ОбъектСправочника = СсылкаСправочника.ПолучитьОбъект();
		ИдентификаторСсылки = СсылкаСправочника.УникальныйИдентификатор();
		Если ОбъектСправочника.Изменен Тогда
			МассивОбновленных.Добавить(ИмяПредопределенного);
			ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
			Возврат;
		КонецЕсли;
		ПорядокТекущейВерсии = ПорядокВерсии(ОбъектСправочника.Версия);
	КонецЕсли;
	
	// Если версия шаблона больше или такая-же как ВерсияDTO - обновление прерывается.
	ПорядокТекущейВерсияФормата = ПорядокВерсии(ТекущаяВерсияШаблонов());
	Если ПорядокТекущейВерсии <> Неопределено И ПорядокТекущейВерсии >= ПорядокТекущейВерсияФормата Тогда
		МассивОбновленных.Добавить(ИмяПредопределенного);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		Возврат;
	КонецЕсли;
	
	ВерсииШаблона = ПредопределенныеВерсииШаблонов(ИмяПредопределенного);
	СтрокаВерсииДляОбновленияШаблона = Неопределено;
	
	// Поиск подходящей версии макета шаблона документов к ВерсииDTO и текущей версии шаблона
	Для Каждого СтрокаВерсии Из ВерсииШаблона Цикл
		Если (ПорядокТекущейВерсии = Неопределено Или ПорядокТекущейВерсии < СтрокаВерсии.ПорядокВерсий)
			И ПорядокТекущейВерсияФормата >= СтрокаВерсии.ПорядокВерсий Тогда
			
			СтрокаВерсииДляОбновленияШаблона = СтрокаВерсии;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	// Если подходящей версии не нашлось процесс прерывается
	Если СтрокаВерсииДляОбновленияШаблона = Неопределено Тогда
		МассивОбновленных.Добавить(ИмяПредопределенного);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		Возврат;
	КонецЕсли;
	
	ВариантОтчетаДокумента = ВариантОтчета(ИдентификаторСсылки);
	ОбъектСправочника.Версия = СтрокаВерсииДляОбновленияШаблона.Версия;
	ОбъектСправочника.Настройки = Новый ХранилищеЗначения(СтрокаВерсииДляОбновленияШаблона.Настройки, Новый СжатиеДанных(9));
	КлючВарианта = КлючВариантаОтчета(ИдентификаторСсылки);
	МакетJSON = Новый ТекстовыйДокумент;
	МакетJSON.УстановитьТекст(СтрокаВерсииДляОбновленияШаблона.Настройки);
	ШаблонПоУмолчанию = ШаблонДокументаИзМакетаJSON(МакетJSON);
	
	МакетДокумента = ШаблонПоУмолчанию.Макет;
	ОбъектСправочника.МакетДокумента = Новый ХранилищеЗначения(МакетДокумента, Новый СжатиеДанных(9));
	
	Если ВариантОтчетаДокумента = Неопределено Тогда
		ИдентификаторОтчета = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
			Метаданные.Отчеты.ШаблоныДокументовКабинетСотрудника);
		ВариантОтчетаДокумента = НовыйВариантОтчета(ИдентификаторОтчета, КлючВарианта);
	КонецЕсли;
	ВариантОтчетаДокументаОбъект = ВариантОтчетаДокумента.ПолучитьОбъект();
	ВариантОтчетаДокументаОбъект.Настройки = Неопределено;
	ВариантОтчетаДокументаОбъект.ОбменДанными.Загрузка = Истина;
	ВариантОтчетаДокументаОбъект.Записать();
	
	ЗарплатаКадрыОтчеты.ЗапомнитьНастройкиВариантаОтчета(ВариантОтчетаДокумента, ШаблонПоУмолчанию.Настройки);
	
	ОбъектСправочника.ХешНастроек = ХешНастроек(ОбъектСправочника.ВАрхиве, МакетДокумента, ИдентификаторСсылки);
	
	ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ОбъектСправочника);
	
	// Регистрация к обмену с "1С:Кабинет сотрудника"
	Если НовыйОбъект Тогда
		ОбъектСправочника.ДополнительныеСвойства.Вставить("ЗаполнитьИсполнителейЗадач", Истина);
	КонецЕсли;
	ИнтеграцияУправлениеПерсоналомСобытия.ПриЗаписиШаблоныДокументов(ОбъектСправочника);
	
	МассивОбновленных.Добавить(ИмяПредопределенного);
	Если Не НовыйОбъект Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
	КонецЕсли;
	
КонецПроцедуры

Функция ШаблонДокументаПоУмолчанию(ИмяПредопределенного, КлючВарианта) Экспорт
	
	МакетJSON = ПолучитьМакет(ИмяПредопределенного);
	Возврат ШаблонДокументаИзМакетаJSON(МакетJSON, КлючВарианта) 
	
КонецФункции

Функция ШаблонДокументаИзХранилища(АдресШаблонаНаСервере, КлючВарианта = Неопределено) Экспорт
	
	МакетJSON = Новый ТекстовыйДокумент;
	ДанныеШаблона = ПолучитьИзВременногоХранилища(АдресШаблонаНаСервере);
	Если ТипЗнч(ДанныеШаблона) = Тип("ДвоичныеДанные") Тогда
		МакетJSON.Прочитать(ДанныеШаблона.ОткрытьПотокДляЧтения());
	Иначе
		МакетJSON.УстановитьТекст(ДанныеШаблона);
	КонецЕсли;
	
	Возврат ШаблонДокументаИзМакетаJSON(МакетJSON, КлючВарианта);
	
КонецФункции

Функция ШаблонДокументаИзМакетаJSON(МакетJSON, КлючВарианта = Неопределено)
	
	ШаблонПоУмолчанию = Новый Структура;
	
	Настройки = ЗарплатаКадрыОтчеты.НастройкиШаблонаДляКабинетаСотрудников(
		КлючВарианта, Истина);

	ШаблонДокумента = ШаблонДокументаИзJSON(МакетJSON.ПолучитьТекст());
	
	МакетДокумента = Новый ТабличныйДокумент;
	МакетДокумента.Прочитать(Base64Значение(ШаблонДокумента.Макет).ОткрытьПотокДляЧтения());
	ШаблонПоУмолчанию.Вставить("Макет", МакетДокумента);
	
	Если ШаблонДокумента.Свойство("РеквизитыДокумента")
		И ЗначениеЗаполнено(ШаблонДокумента.РеквизитыДокумента) Тогда
		
		НаборДанных = Настройки.СхемаКомпоновкиДанных.НаборыДанных[0];
		Для Каждого РеквизитДокумента Из ШаблонДокумента.РеквизитыДокумента Цикл
			
			ПолеНабораДанных = НаборДанных.Поля.Найти(РеквизитДокумента.ПутьКПолю);
			Если ПолеНабораДанных = Неопределено Тогда
				ПолеНабораДанных = НаборДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
				ПолеНабораДанных.Поле			= РеквизитДокумента.ИмяПоля;
				ПолеНабораДанных.ПутьКДанным	= РеквизитДокумента.ПутьКПолю;
			КонецЕсли;
			Если РеквизитДокумента.Свойство("Тип") Тогда
				ПолеНабораДанных.ТипЗначения	= Новый ОписаниеТипов(РеквизитДокумента.Тип);
			КонецЕсли;
			Если РеквизитДокумента.Свойство("ДоступноВУсловиях") Тогда
				ПолеНабораДанных.ОграничениеИспользования.Условие = Не РеквизитДокумента.ДоступноВУсловиях;
			Иначе
				ПолеНабораДанных.ОграничениеИспользования.Условие = Истина;
			КонецЕсли;
			ПолеНабораДанных.ОграничениеИспользования.Группировка = Истина;
			ПолеНабораДанных.ОграничениеИспользования.Порядок = Истина;
			ПолеНабораДанных.ОграничениеИспользованияРеквизитов.Группировка = Истина;
			Если РеквизитДокумента.Свойство("ДоступныРеквизиты") Тогда
				ПолеНабораДанных.ОграничениеИспользованияРеквизитов.Поле = Не РеквизитДокумента.ДоступныРеквизиты;
			Иначе
				ПолеНабораДанных.ОграничениеИспользованияРеквизитов.Поле = Истина;
			КонецЕсли;
			ПолеНабораДанных.ОграничениеИспользованияРеквизитов.Условие = Истина;
			ПолеНабораДанных.ОграничениеИспользованияРеквизитов.Порядок = Истина;
			Если РеквизитДокумента.Свойство("ДоступныеЗначения") Тогда
				ДоступныеЗначения = Новый СписокЗначений;
				Для Каждого ДоступноеЗначение Из РеквизитДокумента.ДоступныеЗначения Цикл
					ДоступныеЗначения.Добавить(ДоступноеЗначение.Значение, ДоступноеЗначение.Представление);
				КонецЦикла;
				ПолеНабораДанных.УстановитьДоступныеЗначения(ДоступныеЗначения);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ДобавитьРасширенияПолейСхемыКомпоновкиДанных(Настройки.СхемаКомпоновкиДанных);
	
	КомпоновщикНастроекКД = Новый КомпоновщикНастроекКомпоновкиДанных;
	ЗарплатаКадрыОтчеты.ИнициализироватьИЗагрузитьНастройкиВКомпоновщикКД(КомпоновщикНастроекКД, Настройки);
	
	Если ШаблонДокумента.Свойство("ВыбранныеПоля") Тогда
		Для Каждого ПутьКВыбранномуПолю Из ШаблонДокумента.ВыбранныеПоля Цикл
			НовоеПоле = КомпоновщикНастроекКД.Настройки.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
			НовоеПоле.Поле = Новый ПолеКомпоновкиДанных(ПутьКВыбранномуПолю);
		КонецЦикла;
	КонецЕсли;
	
	Если ШаблонДокумента.Свойство("УсловияВывода")
		И ЗначениеЗаполнено(ШаблонДокумента.УсловияВывода) Тогда
		
		Для Каждого УсловияВывода Из ШаблонДокумента.УсловияВывода Цикл
			ГруппаУсловийИзJSON(КомпоновщикНастроекКД.Настройки.Отбор.Элементы, УсловияВывода);
		КонецЦикла;
		
	КонецЕсли;
	
	Настройки.Настройки = КомпоновщикНастроекКД.Настройки;
	Настройки.ПользовательскиеНастройки = КомпоновщикНастроекКД.ПользовательскиеНастройки;
	
	ШаблонПоУмолчанию.Вставить("Настройки", Настройки);
	
	Возврат ШаблонПоУмолчанию;
	
КонецФункции

Процедура ДобавитьРасширенияПолейСхемыКомпоновкиДанных(Схема, ОбновитьИзСхемыПоУмолчанию = Ложь) Экспорт
	
	НаборДанных = Схема.НаборыДанных[0];
	
	Если ОбновитьИзСхемыПоУмолчанию Тогда
		ОтчетОбъект = Отчеты.ШаблоныДокументовКабинетСотрудника.Создать();
		СхемаПоУмолчанию = ОтчетОбъект.СхемаКомпоновкиДанных;
		Для Каждого Поле Из СхемаПоУмолчанию.НаборыДанных[0].Поля Цикл
			Если НаборДанных.Поля.Найти(Поле.ПутьКДанным) = Неопределено Тогда
				ПолеНабораДанных = НаборДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
				ЗаполнитьЗначенияСвойств(ПолеНабораДанных, Поле, , "ОграничениеИспользования,ОграничениеИспользованияРеквизитов");
				ЗаполнитьЗначенияСвойств(ПолеНабораДанных.ОграничениеИспользования, Поле.ОграничениеИспользования);
				ЗаполнитьЗначенияСвойств(ПолеНабораДанных.ОграничениеИспользованияРеквизитов, Поле.ОграничениеИспользованияРеквизитов);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ГруппаУсловийИзJSON(КоллекцияЭлементовОтбора, УсловияВывода, ТипГруппы = Неопределено)
	
	ГруппаОбласти = КоллекцияЭлементовОтбора.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	Если ТипГруппы = Неопределено Тогда
		ГруппаОбласти.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	Иначе
		ГруппаОбласти.ТипГруппы = ТипГруппы;
	КонецЕсли;
	
	Если УсловияВывода.Свойство("Имя") Тогда
		ГруппаОбласти.Представление = УсловияВывода.Имя;
	КонецЕсли;
	
	Для Каждого ОписаниеУсловия Из УсловияВывода.Элементы Цикл
		Если ОписаниеУсловия.ЭтоГруппа Тогда
			ГруппаУсловийИзJSON(ГруппаОбласти.Элементы, ОписаниеУсловия, Вычислить(ОписаниеУсловия.ТипГруппы));
		Иначе
			ЭлементОтбора = ГруппаОбласти.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ОписаниеУсловия.ЛевоеЗначение);
			ЭлементОтбора.ВидСравнения = Вычислить(ОписаниеУсловия.ВидСравнения);
			Если ОписаниеУсловия.Свойство("ПравоеЗначение") Тогда
				Если ОписаниеУсловия.Свойство("ТипСсылочногоЗначения") Тогда
					ТипСсылочногоЗначения = Неопределено;
					Если ОписаниеУсловия.ТипСсылочногоЗначения = "divisions" Тогда
						ТипСсылочногоЗначения = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Подразделение;
					ИначеЕсли  ОписаниеУсловия.ТипСсылочногоЗначения = "positions" Тогда
						ТипСсылочногоЗначения = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Должность;
					ИначеЕсли  ОписаниеУсловия.ТипСсылочногоЗначения = "employers" Тогда
						ТипСсылочногоЗначения = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Организация;
					ИначеЕсли  ОписаниеУсловия.ТипСсылочногоЗначения = "stafflist-positions" Тогда
						ТипСсылочногоЗначения = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ДолжностьПоШтатномуРасписанию;
					КонецЕсли;
					Если ТипСсылочногоЗначения <> Неопределено Тогда
						ПравыеЗначения = ИнтеграцияУправлениеПерсоналомОбмен.ПубличныйИдентификаторСсылка(
							ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ОписаниеУсловия.ПравоеЗначение),
							ТипСсылочногоЗначения);
						ПравоеЗначение = ПравыеЗначения.Получить(ОписаниеУсловия.ПравоеЗначение);
					ИначеЕсли ОписаниеУсловия.ТипСсылочногоЗначения = "gender" Тогда
						Если ОписаниеУсловия.ПравоеЗначение = "female" Тогда
							ПравоеЗначение = Перечисления.ПолФизическогоЛица.Женский;
						Иначе
							ПравоеЗначение = Перечисления.ПолФизическогоЛица.Мужской;
						КонецЕсли;
					КонецЕсли;
					Если Не ЗначениеЗаполнено(ПравоеЗначение) Тогда
						ПравоеЗначение = ОписаниеУсловия.ТипСсылочногоЗначения + "." + ОписаниеУсловия.ПравоеЗначение;
					КонецЕсли;
					ЭлементОтбора.ПравоеЗначение = ПравоеЗначение;
				Иначе
					ЭлементОтбора.ПравоеЗначение = ОписаниеУсловия.ПравоеЗначение;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

#Область СериализацияВJSON

Функция ШаблонДокументаИзJSON(МакетJSON) Экспорт
	
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(МакетJSON);
	
	ШаблонДокумента = ПрочитатьJSON(ЧтениеJSON);
	Возврат ШаблонДокумента;
	
КонецФункции

Функция JSONШаблонаДокумента(МакетПечатнойФормы, ТекущаяСхема, Настройки) Экспорт
	
	ОтчетОбъект = Отчеты.ШаблоныДокументовКабинетСотрудника.Создать();
	СхемаПоУмолчанию = ОтчетОбъект.СхемаКомпоновкиДанных;
	
	ПотокЗаписи = Новый ПотокВПамяти;
	МакетПечатнойФормы.Записать(ПотокЗаписи);
	ДвоичныеДанныеМакета = ПотокЗаписи.ЗакрытьИПолучитьДвоичныеДанные();
	
	ОписаниеФормы = Новый Структура;
	ОписаниеФормы.Вставить("Макет", Base64Строка(ДвоичныеДанныеМакета));
	ОписаниеФормы.Вставить("УсловияВывода", УсловияОбластейИзНастроек(Настройки));
	ОписаниеФормы.Вставить("РеквизитыДокумента", РеквизитыДокумента(ТекущаяСхема, СхемаПоУмолчанию));
	ОписаниеФормы.Вставить("ВыбранныеПоля", ВыбранныеПоляИзНастроек(Настройки));
	
	УдалитьПустыеЗначения(ОписаниеФормы);
	
	НастройкиСериализацииJSON = Новый НастройкиСериализацииJSON;
	НастройкиСериализацииJSON.ВариантЗаписиДаты = ВариантЗаписиДатыJSON.ЛокальнаяДата;
	НастройкиСериализацииJSON.ФорматСериализацииДаты = ФорматДатыJSON.ISO;
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку(Новый ПараметрыЗаписиJSON(, Символы.Таб));
	ЗаписатьJSON(ЗаписьJSON, ОписаниеФормы, НастройкиСериализацииJSON, "ПреобразованиеJSON", Справочники.ШаблоныДокументов);
	Результат = ЗаписьJSON.Закрыть();
	
	Возврат Результат;
	
КонецФункции

Функция ПреобразованиеJSON(Знач Свойство, Значение, ДополнительныеПараметры, Отказ) Экспорт
	
	Результат = Неопределено;
	Если ТипЗнч(Значение) = Тип("ТипГруппыЭлементовОтбораКомпоновкиДанных") Тогда
		Если Значение = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ Тогда
			Результат = "ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ";
		ИначеЕсли Значение = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли Тогда
			Результат = "ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли";
		ИначеЕсли Значение = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаНе Тогда
			Результат = "ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаНе";
		КонецЕсли;
	ИначеЕсли ТипЗнч(Значение) = Тип("ВидСравненияКомпоновкиДанных") Тогда
		Если Значение = ВидСравненияКомпоновкиДанных.Больше Тогда
			Результат = "ВидСравненияКомпоновкиДанных.Больше";
		ИначеЕсли Значение = ВидСравненияКомпоновкиДанных.БольшеИлиРавно Тогда
			Результат = "ВидСравненияКомпоновкиДанных.БольшеИлиРавно";
		ИначеЕсли Значение = ВидСравненияКомпоновкиДанных.ВИерархии Тогда
			Результат = "ВидСравненияКомпоновкиДанных.ВИерархии";
		ИначеЕсли Значение = ВидСравненияКомпоновкиДанных.ВСписке Тогда
			Результат = "ВидСравненияКомпоновкиДанных.ВСписке";
		ИначеЕсли Значение = ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии Тогда
			Результат = "ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии";
		ИначеЕсли Значение = ВидСравненияКомпоновкиДанных.Заполнено Тогда
			Результат = "ВидСравненияКомпоновкиДанных.Заполнено";
		ИначеЕсли Значение = ВидСравненияКомпоновкиДанных.Меньше Тогда
			Результат = "ВидСравненияКомпоновкиДанных.Меньше";
		ИначеЕсли Значение = ВидСравненияКомпоновкиДанных.МеньшеИлиРавно Тогда
			Результат = "ВидСравненияКомпоновкиДанных.МеньшеИлиРавно";
		ИначеЕсли Значение = ВидСравненияКомпоновкиДанных.НачинаетсяС Тогда
			Результат = "ВидСравненияКомпоновкиДанных.НачинаетсяС";
		ИначеЕсли Значение = ВидСравненияКомпоновкиДанных.НеВИерархии Тогда
			Результат = "ВидСравненияКомпоновкиДанных.НеВИерархии";
		ИначеЕсли Значение = ВидСравненияКомпоновкиДанных.НеВСписке Тогда
			Результат = "ВидСравненияКомпоновкиДанных.НеВСписке";
		ИначеЕсли Значение = ВидСравненияКомпоновкиДанных.НеВСпискеПоИерархии Тогда
			Результат = "ВидСравненияКомпоновкиДанных.НеВСпискеПоИерархии";
		ИначеЕсли Значение = ВидСравненияКомпоновкиДанных.НеЗаполнено Тогда
			Результат = "ВидСравненияКомпоновкиДанных.НеЗаполнено";
		ИначеЕсли Значение = ВидСравненияКомпоновкиДанных.НеНачинаетсяС Тогда
			Результат = "ВидСравненияКомпоновкиДанных.НеНачинаетсяС";
		ИначеЕсли Значение = ВидСравненияКомпоновкиДанных.НеПодобно Тогда
			Результат = "ВидСравненияКомпоновкиДанных.НеПодобно";
		ИначеЕсли Значение = ВидСравненияКомпоновкиДанных.НеРавно Тогда
			Результат = "ВидСравненияКомпоновкиДанных.НеРавно";
		ИначеЕсли Значение = ВидСравненияКомпоновкиДанных.НеСодержит Тогда
			Результат = "ВидСравненияКомпоновкиДанных.НеСодержит";
		ИначеЕсли Значение = ВидСравненияКомпоновкиДанных.Подобно Тогда
			Результат = "ВидСравненияКомпоновкиДанных.Подобно";
		ИначеЕсли Значение = ВидСравненияКомпоновкиДанных.Равно Тогда
			Результат = "ВидСравненияКомпоновкиДанных.Равно";
		ИначеЕсли Значение = ВидСравненияКомпоновкиДанных.Содержит Тогда
			Результат = "ВидСравненияКомпоновкиДанных.Содержит";
		КонецЕсли;
		
	ИначеЕсли ОбщегоНазначения.ЭтоСсылка(ТипЗнч(Значение)) Тогда
		ИдентификаторЗначения = Неопределено;
		ТипОбъектаУправлениеПерсоналом = Неопределено;
		Если ТипЗнч(Значение) = Тип("СправочникСсылка.ШтатноеРасписание") Тогда
			ТипОбъектаУправлениеПерсоналом = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ДолжностьПоШтатномуРасписанию;
		ИначеЕсли ТипЗнч(Значение) = Тип("СправочникСсылка.Должности") Тогда
			ТипОбъектаУправлениеПерсоналом = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Должность;
		ИначеЕсли ТипЗнч(Значение) = Тип("СправочникСсылка.ПодразделенияОрганизаций") Тогда
			ТипОбъектаУправлениеПерсоналом = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Подразделение;
		ИначеЕсли ТипЗнч(Значение) = Тип("СправочникСсылка.СтруктураПредприятия") Тогда
			ТипОбъектаУправлениеПерсоналом = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Подразделение;
		ИначеЕсли ТипЗнч(Значение) = Тип("СправочникСсылка.Организации") Тогда
			ТипОбъектаУправлениеПерсоналом = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Организация;
		КонецЕсли;
		Если ТипОбъектаУправлениеПерсоналом <> Неопределено Тогда
			ИдентификаторыЗначений = ИнтеграцияУправлениеПерсоналомОбмен.СсылкаПубличныйИдентификатор(
				ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Значение),
				ТипОбъектаУправлениеПерсоналом);
			ИдентификаторЗначения = ИдентификаторыЗначений.Получить(Значение);
		КонецЕсли;
		Если ИдентификаторЗначения <> Неопределено Тогда
			Результат = ИдентификаторЗначения;
		Иначе
			Если ТипЗнч(Значение) = Тип("ПеречислениеСсылка.ПолФизическогоЛица") Тогда
				Если Значение = Перечисления.ПолФизическогоЛица.Женский Тогда
					Результат = "female";
				Иначе
					Результат = "male";
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли Свойство = "ТипСсылочногоЗначения" Тогда
		Если Значение = Тип("СправочникСсылка.ПодразделенияОрганизаций")
			Или Значение = Тип("СправочникСсылка.СтруктураПредприятия") Тогда
			
			Результат = "divisions";
		ИначеЕсли Значение = Тип("ПеречислениеСсылка.ПолФизическогоЛица") Тогда
			Результат = "gender";
		ИначеЕсли Значение = Тип("СправочникСсылка.Должности") Тогда
			Результат = "positions";
		ИначеЕсли Значение = Тип("СправочникСсылка.Организации") Тогда
			Результат = "employers";
		ИначеЕсли Значение = Тип("СправочникСсылка.ШтатноеРасписание") Тогда
			Результат = "stafflist-positions";
		КонецЕсли;
	ИначеЕсли Свойство = "ДоступныеЗначения" Тогда
		Результат = Новый Массив;
		Для Каждого Элемент Из Значение Цикл
			Результат.Добавить(Новый Структура("Представление,Значение", Элемент.Представление, Элемент.Значение));
		КонецЦикла;
	ИначеЕсли ТипЗнч(Значение) = Тип("ОписаниеТипов") Тогда
		СписокТипов = Новый Массив;
		Если Значение.СодержитТип(Тип("Дата")) Тогда
			СписокТипов.Добавить("Дата");
		КонецЕсли;
		Если Значение.СодержитТип(Тип("СтандартныйПериод")) Тогда
			СписокТипов.Добавить("СтандартныйПериод");
		КонецЕсли;
		Если Значение.СодержитТип(Тип("Строка")) Тогда
			СписокТипов.Добавить("Строка");
		КонецЕсли;
		Если Значение.СодержитТип(Тип("Число")) Тогда
			СписокТипов.Добавить("Число");
		КонецЕсли;
		Если Значение.СодержитТип(Тип("Булево")) Тогда
			СписокТипов.Добавить("Булево");
		КонецЕсли;
		Если Значение.СодержитТип(Тип("ПеречислениеСсылка.ПолФизическогоЛица")) Тогда
			СписокТипов.Добавить("ПеречислениеСсылка.ПолФизическогоЛица");
		КонецЕсли;
		Если Значение.СодержитТип(Тип("СправочникСсылка.Должности")) Тогда
			СписокТипов.Добавить("СправочникСсылка.Должности");
		КонецЕсли;
		Если Значение.СодержитТип(Тип("СправочникСсылка.Организации")) Тогда
			СписокТипов.Добавить("СправочникСсылка.Организации");
		КонецЕсли;
		Если Значение.СодержитТип(Тип("СправочникСсылка.ШтатноеРасписание")) Тогда
			СписокТипов.Добавить("СправочникСсылка.ШтатноеРасписание");
		КонецЕсли;
		Если Значение.СодержитТип(Тип("СправочникСсылка.ПодразделенияОрганизаций")) Тогда
			СписокТипов.Добавить("СправочникСсылка.ПодразделенияОрганизаций");
		КонецЕсли;
		Если Значение.СодержитТип(Тип("СправочникСсылка.СтруктураПредприятия")) Тогда
			СписокТипов.Добавить("СправочникСсылка.СтруктураПредприятия");
		КонецЕсли;
		Результат = СтрСоединить(СписокТипов, ",");
	КонецЕсли;
	
	Если Результат = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтрШаблон(НСтр("ru = 'Не найдено способа конвертации значения %1 (%2)';
							|en = 'Не найдено способа конвертации значения %1 (%2)'"), Значение, ТипЗнч(Значение)));
		Отказ = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция РеквизитыДокумента(ТекущаяСхема, СхемаПоУмолчанию)
	
	Реквизиты = Новый Массив;
	
	НаборДанных = ТекущаяСхема.НаборыДанных[0];
	Для Каждого ПолеНабора Из НаборДанных.Поля Цикл
		Если СхемаПоУмолчанию.НаборыДанных[0].Поля.Найти(ПолеНабора.ПутьКДанным) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Если ТипЗнч(ПолеНабора) = Тип("ПолеНабораДанныхСхемыКомпоновкиДанных") Тогда
			ДобавитьПолеДокумента(
				Реквизиты,
				Строка(ПолеНабора.Поле),
				ПолеНабора.ПутьКДанным,
				ПолеНабора.ТипЗначения,
				,
				Не ПолеНабора.ОграничениеИспользования.Условие,
				Не ПолеНабора.ОграничениеИспользованияРеквизитов.Поле,
				ПолеНабора.ПолучитьДоступныеЗначения());
		КонецЕсли;
	КонецЦикла;
	
	Возврат Реквизиты;
	
КонецФункции

Процедура УдалитьПустыеЗначения(КоллекцияОписания)
	
	УдаляемыеКлючи = Новый Массив;
	Для Каждого Элемент Из КоллекцияОписания Цикл
		Если ТипЗнч(Элемент.Значение) = Тип("Массив") Тогда
			УдаляемыеЭлементы = Новый Массив;
			Для Каждого ЭлементМассива Из Элемент.Значение Цикл
				Если ТипЗнч(ЭлементМассива) = Тип("Структура") Тогда
					УдалитьПустыеЗначения(ЭлементМассива);
				КонецЕсли;
				Если Не ЗначениеЗаполнено(ЭлементМассива) Тогда
					УдаляемыеЭлементы.Добавить(ЭлементМассива);
				КонецЕсли;
			КонецЦикла;
			Для Каждого ЭлементМассива Из УдаляемыеЭлементы Цикл
				Элемент.Значение.Удалить(ЭлементМассива);
			КонецЦикла;
		ИначеЕсли ТипЗнч(Элемент.Значение) = Тип("Структура") Тогда
			УдалитьПустыеЗначения(Элемент.Значение);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Элемент.Значение) Тогда
			УдаляемыеКлючи.Добавить(Элемент.Ключ);
		КонецЕсли;
	КонецЦикла;
	Для Каждого Ключ Из УдаляемыеКлючи Цикл
		КоллекцияОписания.Удалить(Ключ);
	КонецЦикла;
	
КонецПроцедуры

Функция ВыбранныеПоляИзНастроек(Настройки)
	
	ВыбранныеПоля = Новый Массив;
	Для Каждого ВыбранноеПоле Из Настройки.Выбор.Элементы Цикл
		Если ТипЗнч(ВыбранноеПоле) = Тип("ВыбранноеПолеКомпоновкиДанных") Тогда
			ВыбранноеПолеСтрокой = Строка(ВыбранноеПоле.Поле);
			ВыбранныеПоля.Добавить(ВыбранноеПолеСтрокой);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ВыбранныеПоля;
	
КонецФункции

Функция JSONШаблонаДокументаПоИдентификаторуШаблона(МакетПечатнойФормы, ИдентификаторСсылки) Экспорт
	
	Настройки = ЗарплатаКадрыОтчеты.НастройкиШаблонаДляКабинетаСотрудников(
		КлючВариантаОтчета(ИдентификаторСсылки));
	
	КомпоновщикНастроекКД = Новый КомпоновщикНастроекКомпоновкиДанных;
	ЗарплатаКадрыОтчеты.ИнициализироватьИЗагрузитьНастройкиВКомпоновщикКД(КомпоновщикНастроекКД, Настройки);
	
	Возврат JSONШаблонаДокумента(МакетПечатнойФормы, Настройки.СхемаКомпоновкиДанных, КомпоновщикНастроекКД.ПолучитьНастройки());
	
КонецФункции

Функция ПустаяТаблицаОтличий() Экспорт
	
	ТаблицаОтличий = Новый ТаблицаЗначений;
	ТаблицаОтличий.Колонки.Добавить("Версия", Новый ОписаниеТипов("Строка"));
	ТаблицаОтличий.Колонки.Добавить("ДатаРегистрации", Новый ОписаниеТипов("Дата", , , Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)));
	ТаблицаОтличий.Колонки.Добавить("ПорядокВерсий", Новый ОписаниеТипов("Строка"));
	ТаблицаОтличий.Колонки.Добавить("РазделШаблона", Новый ОписаниеТипов("Строка"));
	ТаблицаОтличий.Колонки.Добавить("Изменение", Новый ОписаниеТипов("Строка"));
	ТаблицаОтличий.Колонки.Добавить("Описание", Новый ОписаниеТипов("Строка"));
	
	Возврат ТаблицаОтличий;
	
КонецФункции

Процедура ДополнитьТаблицуРазличийВерсийJSONШаблонов(МакетJSONПервый, МакетJSONВторой, ВерсияВторого, ДополняемаяТаблицаОтличий) Экспорт
	
	ТаблицаОтличий = ПустаяТаблицаОтличий();
	СобратьРазличияJSONШаблонов(МакетJSONПервый, МакетJSONВторой, Истина, ТаблицаОтличий);
	
	ТаблицаОтличий.ЗаполнитьЗначения(ВерсияВторого, "Версия");
	ЗаполнитьПорядокВерсий(ТаблицаОтличий);
	
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаОтличий, ДополняемаяТаблицаОтличий);
	
КонецПроцедуры

Функция JSONШаблоныРазличны(МакетJSONПервый, МакетJSONВторой) Экспорт
	Возврат СобратьРазличияJSONШаблонов(МакетJSONПервый, МакетJSONВторой);
КонецФункции

Функция СобратьРазличияJSONШаблонов(МакетJSONПервый, МакетJSONВторой, ПодробноеОписание = Ложь, ТаблицаОтличий = Неопределено) Экспорт
	
	Если ТаблицаОтличий = Неопределено Тогда
		ТаблицаОтличий = ПустаяТаблицаОтличий();
	КонецЕсли;
	
	Если МакетJSONПервый = МакетJSONВторой Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(МакетJSONПервый)
		Или Не ЗначениеЗаполнено(МакетJSONВторой) Тогда
		
		СтрокаИзменений = ТаблицаОтличий.Добавить();
		СтрокаИзменений.РазделШаблона = НСтр("ru = 'Шаблон';
											|en = 'Шаблон'");
		СтрокаИзменений.Описание = НСтр("ru = 'Один из сравниваемых шаблонов пуст';
										|en = 'Один из сравниваемых шаблонов пуст'");
		
		Если Не ПодробноеОписание Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	ПерваяВерсия = ШаблонДокументаИзJSON(МакетJSONПервый);
	ВтораяВерсия = ШаблонДокументаИзJSON(МакетJSONВторой);
	
	// Идентичность табличных документов
	ДанныеДокументаПервый = Base64Значение(ПерваяВерсия.Макет);
	ДанныеДокументаВторой = Base64Значение(ВтораяВерсия.Макет);
	
	МакетДокументаПервый = Новый ТабличныйДокумент;
	МакетДокументаПервый.Прочитать(ДанныеДокументаПервый.ОткрытьПотокДляЧтения());
	
	МакетДокументаВторой = Новый ТабличныйДокумент;
	МакетДокументаВторой.Прочитать(ДанныеДокументаВторой.ОткрытьПотокДляЧтения());
	
	Если ТабличныеДокументыРазличны(МакетДокументаПервый, МакетДокументаВторой, ТаблицаОтличий)
		И Не ПодробноеОписание Тогда
		
		Возврат Истина;
	КонецЕсли;
	
	Если УсловияВыводаОбластейРазличаются(
			?(ПерваяВерсия.Свойство("УсловияВывода"), ПерваяВерсия.УсловияВывода, Новый Массив),
			?(ВтораяВерсия.Свойство("УсловияВывода"), ВтораяВерсия.УсловияВывода, Новый Массив),
			ТаблицаОтличий)
		И Не ПодробноеОписание Тогда
		
		Возврат Истина
	КонецЕсли;
	
	// Идентичность набора реквизитов
	Если ДоступныеДанныеРазличны(
			?(ПерваяВерсия.Свойство("РеквизитыДокумента"), ПерваяВерсия.РеквизитыДокумента, Новый Массив),
			?(ВтораяВерсия.Свойство("РеквизитыДокумента"), ВтораяВерсия.РеквизитыДокумента, Новый Массив),
			ТаблицаОтличий)
		И Не ПодробноеОписание Тогда
		
		Возврат Истина;
	КонецЕсли;
	
	// Идентичность выбранных полей
	Если ВыбранныеПоляРазличны(
			?(ПерваяВерсия.Свойство("ВыбранныеПоля"), ПерваяВерсия.ВыбранныеПоля, Новый Массив),
			?(ВтораяВерсия.Свойство("ВыбранныеПоля"), ВтораяВерсия.ВыбранныеПоля, Новый Массив),
			ТаблицаОтличий)
		И Не ПодробноеОписание Тогда
		
		Возврат Истина;
	КонецЕсли;
	
	ЗаполнитьПорядокВерсий(ТаблицаОтличий);
	Возврат ЗначениеЗаполнено(ТаблицаОтличий);
	
КонецФункции

Функция ТабличныеДокументыРазличны(МакетДокументаПервый, МакетДокументаВторой, ТаблицаОтличий)
	
	НачальноеКоличествоОтличий = ТаблицаОтличий.Количество();
	Если МакетДокументаПервый.ВысотаТаблицы <> МакетДокументаВторой.ВысотаТаблицы
		Или МакетДокументаПервый.ШиринаТаблицы <> МакетДокументаВторой.ШиринаТаблицы Тогда
		
		СтрокаИзменений = ТаблицаОтличий.Добавить();
		СтрокаИзменений.РазделШаблона = НСтр("ru = 'Макет';
											|en = 'Макет'");
		СтрокаИзменений.Описание = НСтр("ru = 'Имеются различия в макете';
										|en = 'Имеются различия в макете'");
		
	КонецЕсли;
	
	Если МакетДокументаПервый.Области.Количество() <> МакетДокументаВторой.Области.Количество() Тогда
		
		СтрокаИзменений = ТаблицаОтличий.Добавить();
		СтрокаИзменений.РазделШаблона = НСтр("ru = 'Макет';
											|en = 'Макет'");
		СтрокаИзменений.Описание = НСтр("ru = 'Различное количество областей';
										|en = 'Различное количество областей'");
		
	КонецЕсли;
	
	СравниваемыеОбласти = Новый Структура;
	Для Каждого ОбластьВторого Из МакетДокументаВторой.Области Цикл
		
		ОбластьПервого = МакетДокументаПервый.Области.Найти(ОбластьВторого.Имя);
		Если ОбластьПервого = Неопределено Тогда
			
			СтрокаИзменений = ТаблицаОтличий.Добавить();
			СтрокаИзменений.РазделШаблона = НСтр("ru = 'Макет';
												|en = 'Макет'");
			СтрокаИзменений.Изменение = НСтр("ru = 'Добавлено';
											|en = 'Добавлено'");
			СтрокаИзменений.Описание = СтрШаблон(НСтр("ru = 'Добавлена область %1';
														|en = 'Добавлена область %1'"), ОбластьВторого.Имя);
		Иначе
			СравниваемыеОбласти.Вставить(ОбластьВторого.Имя);
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого ОбластьПервого Из МакетДокументаПервый.Области Цикл
		
		ОбластьВторого = МакетДокументаВторой.Области.Найти(ОбластьПервого.Имя);
		Если ОбластьВторого = Неопределено Тогда
			
			СтрокаИзменений = ТаблицаОтличий.Добавить();
			СтрокаИзменений.РазделШаблона = НСтр("ru = 'Макет';
												|en = 'Макет'");
			СтрокаИзменений.Изменение = НСтр("ru = 'Удалено';
											|en = 'Удалено'");
			СтрокаИзменений.Описание = СтрШаблон(НСтр("ru = 'Удалена область %1';
														|en = 'Удалена область %1'"), ОбластьПервого.Имя);
			
		Иначе
			СравниваемыеОбласти.Вставить(ОбластьПервого.Имя);
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого ОбластьПервогоМакета Из МакетДокументаПервый.Области Цикл
		
		ОбластьВторогоМакета = МакетДокументаВторой.Области.Найти(ОбластьПервогоМакета.Имя);
		Если ОбластьВторогоМакета <> Неопределено Тогда
			
			Если ОбластьПервогоМакета.Верх <> ОбластьВторогоМакета.Верх
				Или ОбластьПервогоМакета.Лево <> ОбластьВторогоМакета.Лево
				Или ОбластьПервогоМакета.Низ <> ОбластьВторогоМакета.Низ
				Или ОбластьПервогоМакета.Право <> ОбластьВторогоМакета.Право Тогда
				
				СтрокаИзменений = ТаблицаОтличий.Добавить();
				СтрокаИзменений.РазделШаблона = НСтр("ru = 'Макет';
													|en = 'Макет'");
				СтрокаИзменений.Изменение = НСтр("ru = 'Изменено';
												|en = 'Изменено'");
				СтрокаИзменений.Описание = СтрШаблон(НСтр("ru = 'Изменено размещение области %1';
															|en = 'Изменено размещение области %1'"), ОбластьПервогоМакета.Имя);
				
			КонецЕсли;
			
			Если ОбластьПервогоМакета.Низ - ОбластьПервогоМакета.Верх = ОбластьВторогоМакета.Низ - ОбластьВторогоМакета.Верх
				И ОбластьПервогоМакета.Право - ОбластьПервогоМакета.Лево = ОбластьВторогоМакета.Право - ОбластьВторогоМакета.Лево Тогда
				
				Для НомерСтроки = 0 По ОбластьПервогоМакета.Низ - ОбластьПервогоМакета.Верх Цикл
					Для НомерКолонки = 1 По МакетДокументаПервый.ШиринаТаблицы Цикл
						
						ОбластьПервого = МакетДокументаПервый.Область(ОбластьПервогоМакета.Верх + НомерСтроки, НомерКолонки, ОбластьПервогоМакета.Верх + НомерСтроки, НомерКолонки);
						ОбластьВторого = МакетДокументаВторой.Область(ОбластьВторогоМакета.Верх + НомерСтроки, НомерКолонки, ОбластьВторогоМакета.Верх + НомерСтроки, НомерКолонки);
						
						ИзмененияЯчейки = Новый Массив;
						Если ОбластьПервого.Текст <> ОбластьВторого.Текст Тогда
							ИзмененияЯчейки.Добавить(НСтр("ru = 'текст';
															|en = 'текст'"));
						КонецЕсли;
						Если ОбластьПервого.ГраницаСверху.ТипЛинии <> ОбластьВторого.ГраницаСверху.ТипЛинии Тогда
							ИзмененияЯчейки.Добавить(НСтр("ru = 'граница сверху ';
															|en = 'граница сверху '"));
						КонецЕсли;
						Если ОбластьПервого.ГраницаСНизу.ТипЛинии <> ОбластьВторого.ГраницаСНизу.ТипЛинии Тогда
							ИзмененияЯчейки.Добавить(НСтр("ru = 'граница снизу';
															|en = 'граница снизу'"));
						КонецЕсли;
						Если ОбластьПервого.ГраницаСПрава.ТипЛинии <> ОбластьВторого.ГраницаСПрава.ТипЛинии Тогда
							ИзмененияЯчейки.Добавить(НСтр("ru = 'граница справа';
															|en = 'граница справа'"));
						КонецЕсли;
						Если ОбластьПервого.ГраницаСЛева.ТипЛинии <> ОбластьВторого.ГраницаСЛева.ТипЛинии Тогда
							ИзмененияЯчейки.Добавить(НСтр("ru = 'граница слева';
															|en = 'граница слева'"));
						КонецЕсли;
						Если ОбластьПервого.Узор <> ОбластьВторого.Узор Тогда
							ИзмененияЯчейки.Добавить(НСтр("ru = 'узор';
															|en = 'узор'"));
						КонецЕсли;
						Если ЗначениеЗаполнено(ОбластьВторого.Текст) Тогда
							Если ОбластьПервого.Заполнение <> ОбластьВторого.Заполнение Тогда
								ИзмененияЯчейки.Добавить(НСтр("ru = 'заполнение';
																|en = 'заполнение'"));
							КонецЕсли;
							Если ОбластьПервого.Шрифт <> ОбластьВторого.Шрифт Тогда
								ИзмененияЯчейки.Добавить(НСтр("ru = 'шрифт';
																|en = 'шрифт'"));
							КонецЕсли;
							Если ОбластьПервого.ВертикальноеПоложение <> ОбластьВторого.ВертикальноеПоложение Тогда
								ИзмененияЯчейки.Добавить(НСтр("ru = 'вертикальное положение';
																|en = 'вертикальное положение'"));
							КонецЕсли;
							Если ОбластьПервого.ГоризонтальноеПоложение <> ОбластьВторого.ГоризонтальноеПоложение Тогда
								ИзмененияЯчейки.Добавить(НСтр("ru = 'горизонтальное положение';
																|en = 'горизонтальное положение'"));
							КонецЕсли;
						КонецЕсли;
						Если ЗначениеЗаполнено(ИзмененияЯчейки) Тогда
							СтрокаИзменений = ТаблицаОтличий.Добавить();
							СтрокаИзменений.РазделШаблона = НСтр("ru = 'Макет';
																|en = 'Макет'");
							СтрокаИзменений.Изменение = НСтр("ru = 'Изменено';
															|en = 'Изменено'");
							СтрокаИзменений.Описание = СтрШаблон(НСтр("ru = 'Область %1. Изменена ячейка (%2, %3): %4';
																		|en = 'Область %1. Изменена ячейка (%2, %3): %4'"),
								ОбластьПервогоМакета.Имя, НомерСтроки, НомерКолонки, СтрСоединить(ИзмененияЯчейки, ", "));
						КонецЕсли;
						
					КонецЦикла;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат НачальноеКоличествоОтличий < ТаблицаОтличий.Количество();
	
КонецФункции

Функция ВыбранныеПоляРазличны(ВыбранныеПоляПервого, ВыбранныеПоляВторого, ТаблицаОтличий)
	
	НачальноеКоличествоОтличий = ТаблицаОтличий.Количество();
	
	КоллекцияПервого = Новый Соответствие;
	Для Каждого Поле Из ВыбранныеПоляПервого Цикл
		КоллекцияПервого.Вставить(ВРег(Поле), Поле);
	КонецЦикла;
	
	КоллекцияВторого = Новый Соответствие;
	Для Каждого Поле Из ВыбранныеПоляВторого Цикл
		КоллекцияВторого.Вставить(ВРег(Поле), Поле);
	КонецЦикла;
	
	Для Каждого ПолеПервого Из КоллекцияПервого Цикл
		
		Поле = КоллекцияВторого.Получить(ПолеПервого.Ключ);
		Если Поле = Неопределено Тогда
			СтрокаИзменений = ТаблицаОтличий.Добавить();
			СтрокаИзменений.РазделШаблона = НСтр("ru = 'Выбранные поля';
												|en = 'Выбранные поля'");
			СтрокаИзменений.Изменение = НСтр("ru = 'Удалено';
											|en = 'Удалено'");
			СтрокаИзменений.Описание = СтрШаблон(НСтр("ru = 'Поле ""%1""';
														|en = 'Поле ""%1""'"), ПолеПервого.Значение);
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого ПолеВторого Из КоллекцияВторого Цикл
		
		Поле = КоллекцияПервого.Получить(ПолеВторого.Ключ);
		Если Поле = Неопределено Тогда
			СтрокаИзменений = ТаблицаОтличий.Добавить();
			СтрокаИзменений.РазделШаблона = НСтр("ru = 'Выбранные поля';
												|en = 'Выбранные поля'");
			СтрокаИзменений.Изменение = НСтр("ru = 'Добавлено';
											|en = 'Добавлено'");
			СтрокаИзменений.Описание = СтрШаблон(НСтр("ru = 'Поле ""%1""';
														|en = 'Поле ""%1""'"), ПолеВторого.Значение);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат НачальноеКоличествоОтличий < ТаблицаОтличий.Количество();
	
КонецФункции

Функция ДоступныеДанныеРазличны(ДоступныеДанныеПервого, ДоступныеДанныеВторого, ТаблицаОтличий)
	
	НачальноеКоличествоОтличий = ТаблицаОтличий.Количество();
	
	РеквизитыПервого = Новый Структура;
	Для Каждого РеквизитДокумента Из ДоступныеДанныеПервого Цикл
		РеквизитыПервого.Вставить(РеквизитДокумента.ИмяПоля, РеквизитДокумента);
	КонецЦикла;
	
	РеквизитыВторого = Новый Структура;
	Для Каждого РеквизитДокумента Из ДоступныеДанныеВторого Цикл
		РеквизитыВторого.Вставить(РеквизитДокумента.ИмяПоля, РеквизитДокумента);
	КонецЦикла;
	
	Для Каждого РеквизитПервого Из РеквизитыПервого Цикл
		
		Если Не РеквизитыВторого.Свойство(РеквизитПервого.Ключ) Тогда
			СтрокаИзменений = ТаблицаОтличий.Добавить();
			СтрокаИзменений.РазделШаблона = НСтр("ru = 'Доступные данные';
												|en = 'Доступные данные'");
			СтрокаИзменений.Изменение = НСтр("ru = 'Удалено';
											|en = 'Удалено'");
			СтрокаИзменений.Описание = СтрШаблон(НСтр("ru = 'Поле ""%1""';
														|en = 'Поле ""%1""'"), РеквизитПервого.Ключ);
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого РеквизитВторого Из РеквизитыВторого Цикл
		
		Если Не РеквизитыПервого.Свойство(РеквизитВторого.Ключ) Тогда
			СтрокаИзменений = ТаблицаОтличий.Добавить();
			СтрокаИзменений.РазделШаблона = НСтр("ru = 'Доступные данные';
												|en = 'Доступные данные'");
			СтрокаИзменений.Изменение = НСтр("ru = 'Добавлено';
											|en = 'Добавлено'");
			СтрокаИзменений.Описание = СтрШаблон(НСтр("ru = 'Поле ""%1""';
														|en = 'Поле ""%1""'"), РеквизитВторого.Ключ);
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого РеквизитПервого Из РеквизитыПервого Цикл
		РеквизитВторого = Неопределено;
		РеквизитыВторого.Свойство(РеквизитПервого.Ключ, РеквизитВторого);
		Если РеквизитВторого <> Неопределено Тогда
			Если РеквизитПервого.Значение.Тип <> РеквизитВторого.Тип Тогда
				СтрокаИзменений = ТаблицаОтличий.Добавить();
				СтрокаИзменений.РазделШаблона = НСтр("ru = 'Доступные данные';
													|en = 'Доступные данные'");
				СтрокаИзменений.Изменение = НСтр("ru = 'Изменено';
												|en = 'Изменено'");
				СтрокаИзменений.Описание = СтрШаблон(НСтр("ru = 'Поле ""%1"", изменен тип';
															|en = 'Поле ""%1"", изменен тип'"), РеквизитПервого.Ключ);
			КонецЕсли;
			ЗначениеПоУмолчаниюПервого = Неопределено;
			ЗначениеПоУмолчаниюВторого = Неопределено;
			Если РеквизитПервого.Значение.Свойство("ЗначениеПоУмолчанию", ЗначениеПоУмолчаниюПервого)
				<> РеквизитВторого.Свойство("ЗначениеПоУмолчанию", ЗначениеПоУмолчаниюВторого) Тогда
				
				Если ЗначениеПоУмолчаниюПервого = Неопределено Тогда
					СтрокаИзменений = ТаблицаОтличий.Добавить();
					СтрокаИзменений.РазделШаблона = НСтр("ru = 'Доступные данные';
														|en = 'Доступные данные'");
					СтрокаИзменений.Изменение = НСтр("ru = 'Изменено';
													|en = 'Изменено'");
					СтрокаИзменений.Описание = СтрШаблон(НСтр("ru = 'Поле ""%1"", добавлены значения по умолчанию';
																|en = 'Поле ""%1"", добавлены значения по умолчанию'"), РеквизитПервого.Ключ);
				Иначе
					СтрокаИзменений = ТаблицаОтличий.Добавить();
					СтрокаИзменений.РазделШаблона = НСтр("ru = 'Доступные данные';
														|en = 'Доступные данные'");
					СтрокаИзменений.Изменение = НСтр("ru = 'Изменено';
													|en = 'Изменено'");
					СтрокаИзменений.Описание = СтрШаблон(НСтр("ru = 'Поле ""%1"", удалены значения по умолчанию';
																|en = 'Поле ""%1"", удалены значения по умолчанию'"), РеквизитПервого.Ключ);
				КонецЕсли;
				
			Иначе
				Если ЗначениеПоУмолчаниюПервого <> ЗначениеПоУмолчаниюВторого Тогда
					СтрокаИзменений = ТаблицаОтличий.Добавить();
					СтрокаИзменений.РазделШаблона = НСтр("ru = 'Доступные данные';
														|en = 'Доступные данные'");
					СтрокаИзменений.Изменение = НСтр("ru = 'Изменено';
													|en = 'Изменено'");
					СтрокаИзменений.Описание = СтрШаблон(НСтр("ru = 'Поле ""%1"", значение по умолчанию';
																|en = 'Поле ""%1"", значение по умолчанию'"), РеквизитПервого.Ключ);
				КонецЕсли;
			КонецЕсли;
			ДоступныеЗначенияПервого = Неопределено;
			ДоступныеЗначенияВторого = Неопределено;
			Если РеквизитПервого.Значение.Свойство("ДоступныеЗначения", ДоступныеЗначенияПервого)
				<> РеквизитВторого.Свойство("ДоступныеЗначения", ДоступныеЗначенияВторого) Тогда
				
				Если ДоступныеЗначенияПервого = Неопределено Тогда
					СтрокаИзменений = ТаблицаОтличий.Добавить();
					СтрокаИзменений.РазделШаблона = НСтр("ru = 'Доступные данные';
														|en = 'Доступные данные'");
					СтрокаИзменений.Изменение = НСтр("ru = 'Изменено';
													|en = 'Изменено'");
					СтрокаИзменений.Описание = СтрШаблон(НСтр("ru = 'Поле ""%1"", добавлены доступные значения';
																|en = 'Поле ""%1"", добавлены доступные значения'"), РеквизитПервого.Ключ);
				Иначе
					СтрокаИзменений = ТаблицаОтличий.Добавить();
					СтрокаИзменений.РазделШаблона = НСтр("ru = 'Доступные данные';
														|en = 'Доступные данные'");
					СтрокаИзменений.Изменение = НСтр("ru = 'Изменено';
													|en = 'Изменено'");
					СтрокаИзменений.Описание = СтрШаблон(НСтр("ru = 'Поле ""%1"", очищены доступные значения';
																|en = 'Поле ""%1"", очищены доступные значения'"), РеквизитПервого.Ключ);
				КонецЕсли;
			Иначе
				Если ДоступныеЗначенияПервого <> Неопределено И ДоступныеЗначенияВторого <> Неопределено Тогда
					ДоступныеЗначенияИдентичны(РеквизитПервого.Ключ, ДоступныеЗначенияПервого, ДоступныеЗначенияВторого, ТаблицаОтличий);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат НачальноеКоличествоОтличий < ТаблицаОтличий.Количество();
	
КонецФункции

Функция ДоступныеЗначенияИдентичны(ИмяПоля, ДоступныеЗначенияПервого, ДоступныеЗначенияВторого, ТаблицаОтличий)
	
	НачальноеКоличествоОтличий = ТаблицаОтличий.Количество();
	
	Если ДоступныеЗначенияПервого <> Неопределено И ДоступныеЗначенияВторого <> Неопределено Тогда
		КоллекцияПервого = Новый Соответствие;
		Для Каждого ДоступноеЗначение Из ДоступныеЗначенияПервого Цикл
			КоллекцияПервого.Вставить(ДоступноеЗначение.Значение, ДоступноеЗначение.Представление);
		КонецЦикла;
		
		КоллекцияВторого = Новый Соответствие;
		Для Каждого ДоступноеЗначение Из ДоступныеЗначенияВторого Цикл
			КоллекцияВторого.Вставить(ДоступноеЗначение.Значение, ДоступноеЗначение.Представление);
		КонецЦикла;
		
		Для Каждого ДоступноеЗначениеПервого Из КоллекцияПервого Цикл
			
			ДоступноеЗначениеВторого = КоллекцияВторого.Получить(ДоступноеЗначениеПервого.Ключ);
			Если ДоступноеЗначениеВторого = Неопределено Тогда
				СтрокаИзменений = ТаблицаОтличий.Добавить();
				СтрокаИзменений.РазделШаблона = НСтр("ru = 'Доступные данные';
													|en = 'Доступные данные'");
				СтрокаИзменений.Изменение = НСтр("ru = 'Удалено';
												|en = 'Удалено'");
				СтрокаИзменений.Описание = СтрШаблон(НСтр("ru = 'Поле ""%1"" доступное значение ""%2""';
															|en = 'Поле ""%1"" доступное значение ""%2""'"), ИмяПоля, ДоступноеЗначениеПервого.Ключ);
			КонецЕсли;
			
		КонецЦикла;
		
		Для Каждого ДоступноеЗначениеВторого Из КоллекцияВторого Цикл
			
			ДоступноеЗначениеПервого = КоллекцияПервого.Получить(ДоступноеЗначениеВторого.Ключ);
			Если ДоступноеЗначениеПервого = Неопределено Тогда
				СтрокаИзменений = ТаблицаОтличий.Добавить();
				СтрокаИзменений.РазделШаблона = НСтр("ru = 'Выбранные поля';
													|en = 'Выбранные поля'");
				СтрокаИзменений.Изменение = НСтр("ru = 'Добавлено';
												|en = 'Добавлено'");
				СтрокаИзменений.Описание = СтрШаблон(НСтр("ru = 'Поле ""%1"" доступное значение ""%2""';
															|en = 'Поле ""%1"" доступное значение ""%2""'"), ИмяПоля, ДоступноеЗначениеВторого.Ключ);
			КонецЕсли;
			
		КонецЦикла;
		
		Для Каждого ДоступноеЗначениеПервого Из КоллекцияПервого Цикл
			
			ДоступноеЗначениеВторого = КоллекцияВторого.Получить(ДоступноеЗначениеПервого.Ключ);
			Если ДоступноеЗначениеВторого <> Неопределено
				И ДоступноеЗначениеПервого.Значение <> ДоступноеЗначениеВторого Тогда
				СтрокаИзменений = ТаблицаОтличий.Добавить();
				СтрокаИзменений.РазделШаблона = НСтр("ru = 'Доступные данные';
													|en = 'Доступные данные'");
				СтрокаИзменений.Изменение = НСтр("ru = 'Изменено';
												|en = 'Изменено'");
				СтрокаИзменений.Описание = СтрШаблон(НСтр("ru = 'Поле ""%1"" представление доступного значения ""%2""';
															|en = 'Поле ""%1"" представление доступного значения ""%2""'"), ИмяПоля, ДоступноеЗначениеПервого.Ключ);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат НачальноеКоличествоОтличий < ТаблицаОтличий.Количество();
	
КонецФункции

Функция УсловияВыводаОбластейРазличаются(УсловияВыводаПервого, УсловияВыводаВторого, ТаблицаОтличий)
	
	НачальноеКоличествоОтличий = ТаблицаОтличий.Количество();
	
	КоллекцияПервого = Новый Соответствие;
	Для Каждого УсловияВывода Из УсловияВыводаПервого Цикл
		КоллекцияПервого.Вставить(УсловияВывода.Имя, УсловияВывода.Элементы);
	КонецЦикла;
	
	КоллекцияВторого = Новый Соответствие;
	Для Каждого УсловияВывода Из УсловияВыводаВторого Цикл
		КоллекцияВторого.Вставить(УсловияВывода.Имя, УсловияВывода.Элементы);
	КонецЦикла;
	
	Для Каждого УсловияВыводаПервого Из КоллекцияПервого Цикл
		
		УсловияВыводаВторого = КоллекцияВторого.Получить(УсловияВыводаПервого.Ключ);
		Если УсловияВыводаВторого = Неопределено Тогда
			СтрокаИзменений = ТаблицаОтличий.Добавить();
			СтрокаИзменений.РазделШаблона = НСтр("ru = 'Условия вывода';
												|en = 'Условия вывода'");
			СтрокаИзменений.Изменение = НСтр("ru = 'Удалено';
											|en = 'Удалено'");
			СтрокаИзменений.Описание = СтрШаблон(НСтр("ru = 'Область ""%1"",  все условия вывода (%2)';
														|en = 'Область ""%1"",  все условия вывода (%2)'"), УсловияВыводаПервого.Ключ, УсловияВыводаПервого.Значение.Количество());
		Иначе
			ОписаниеОтличий = РезультатСравненияГруппУсловий(УсловияВыводаПервого.Значение, УсловияВыводаВторого);
			Если ЗначениеЗаполнено(ОписаниеОтличий) Тогда
				СтрокаИзменений = ТаблицаОтличий.Добавить();
				СтрокаИзменений.РазделШаблона = НСтр("ru = 'Условия вывода';
													|en = 'Условия вывода'");
				СтрокаИзменений.Изменение = НСтр("ru = 'Удалено';
												|en = 'Удалено'");
				СтрокаИзменений.Описание = СтрШаблон(НСтр("ru = 'Область ""%1"":
					|%2';
					|en = 'Область ""%1"":
					|%2'"), УсловияВыводаПервого.Ключ,
					ОписаниеОтличий);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого УсловияВыводаВторого Из КоллекцияВторого Цикл
		
		УсловияВыводаПервого = КоллекцияПервого.Получить(УсловияВыводаВторого.Ключ);
		Если УсловияВыводаПервого = Неопределено Тогда
			СтрокаИзменений = ТаблицаОтличий.Добавить();
			СтрокаИзменений.РазделШаблона = НСтр("ru = 'Условия вывода';
												|en = 'Условия вывода'");
			СтрокаИзменений.Изменение = НСтр("ru = 'Добавлено';
											|en = 'Добавлено'");
			СтрокаИзменений.Описание = СтрШаблон(НСтр("ru = 'Область ""%1"", условия вывода (%2)';
														|en = 'Область ""%1"", условия вывода (%2)'"), УсловияВыводаВторого.Ключ, УсловияВыводаВторого.Значение.Количество());
		Иначе
			ОписаниеОтличий = РезультатСравненияГруппУсловий(УсловияВыводаВторого.Значение, УсловияВыводаПервого);
			Если ЗначениеЗаполнено(ОписаниеОтличий) Тогда
				СтрокаИзменений = ТаблицаОтличий.Добавить();
				СтрокаИзменений.РазделШаблона = НСтр("ru = 'Условия вывода';
													|en = 'Условия вывода'");
				СтрокаИзменений.Изменение = НСтр("ru = 'Добавлено';
												|en = 'Добавлено'");
				СтрокаИзменений.Описание = СтрШаблон(НСтр("ru = 'Область ""%1"":
					|%2';
					|en = 'Область ""%1"":
					|%2'"), УсловияВыводаВторого.Ключ,
					ОписаниеОтличий);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат НачальноеКоличествоОтличий < ТаблицаОтличий.Количество();
	
КонецФункции

Функция РезультатСравненияГруппУсловий(ЭлементыПервой, ЭлементыВторой)
	ОписанияОтличий = Новый Массив;
	Для Каждого ЭлементУсловийПервого Из ЭлементыПервой Цикл
		УсловиеНайдено = Ложь;
		Для Каждого ЭлементУсловийВторого Из ЭлементыВторой Цикл
			Если ЭлементУсловийПервого.ЭтоГруппа = Истина
				И ЭлементУсловийВторого.ЭтоГруппа Тогда
				
				ОтличияГрупп = РезультатСравненияГруппУсловий(ЭлементУсловийПервого.Элементы, ЭлементУсловийВторого.Элементы);
				Если Не ЗначениеЗаполнено(ОтличияГрупп) Тогда
					УсловиеНайдено = Истина;
					Прервать;
				КонецЕсли;
			ИначеЕсли Не ЭлементУсловийПервого.ЭтоГруппа И Не ЭлементУсловийВторого.ЭтоГруппа Тогда
				Если ЭлементУсловийПервого.ЛевоеЗначение = ЭлементУсловийВторого.ЛевоеЗначение
					И ЭлементУсловийПервого.ВидСравнения = ЭлементУсловийВторого.ВидСравнения Тогда
					
					Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ЭлементУсловийПервого, "ПравоеЗначение")
						И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ЭлементУсловийВторого, "ПравоеЗначение") Тогда
						
						Если ЭлементУсловийПервого.ПравоеЗначение = ЭлементУсловийВторого.ПравоеЗначение Тогда
							УсловиеНайдено = Истина;
							Прервать;
						КонецЕсли;
					Иначе
						УсловиеНайдено = Истина;
						Прервать;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Если Не УсловиеНайдено Тогда
			Если Не ЭлементУсловийПервого.ЭтоГруппа Тогда
				ОписаниеОтличий = СтрШаблон("%1, %2", ЭлементУсловийПервого.ЛевоеЗначение,
					СтрЗаменить(ЭлементУсловийПервого.ВидСравнения,"ВидСравненияКомпоновкиДанных.", ""));
				Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ЭлементУсловийПервого, "ПравоеЗначение")
					И ЗначениеЗаполнено(ЭлементУсловийПервого.ПравоеЗначение) Тогда
					
					ОписаниеОтличий = СтрШаблон("%1, %2", ОписаниеОтличий, ЭлементУсловийПервого.ПравоеЗначение);
				КонецЕсли;
				ОписанияОтличий.Добавить(ОписаниеОтличий);
			Иначе
				ОписаниеОтличий = СтрШаблон("Группа %1 (%2)",
					СтрЗаменить(ЭлементУсловийПервого.ТипГруппы, "ТипГруппыЭлементовОтбораКомпоновкиДанных.Группа", ""),
					Формат(ЭлементУсловийПервого.Элементы.Количество(), "ЧН=; ЧГ="));
				ОписанияОтличий.Добавить(ОписаниеОтличий);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Возврат СтрСоединить(ОписанияОтличий, "," + Символы.ПС);
КонецФункции

#КонецОбласти

Процедура ЗаполнитьТребуетПодписания(ПараметрыОбновления = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Если ПараметрыОбновления = Неопределено Тогда
		МассивОбновленных = Новый Массив;
	Иначе
		
		Если ПараметрыОбновления.Свойство("МассивОбновленных") Тогда
			МассивОбновленных = ПараметрыОбновления.МассивОбновленных;
		Иначе
			МассивОбновленных = Новый Массив;
			ПараметрыОбновления.Вставить("МассивОбновленных", МассивОбновленных);
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МассивОбновленных", МассивОбновленных);
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	ШаблоныДокументов.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ШаблоныДокументов КАК ШаблоныДокументов
		|ГДЕ
		|	НЕ ШаблоныДокументов.Ссылка В (&МассивОбновленных)
		|	И ШаблоныДокументов.ТребуетсяСогласование
		|	И НЕ ШаблоныДокументов.ТребуетПодписания";
	
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1000","");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		МассивОбновленных.Добавить(Выборка.Ссылка);
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "Справочник.ШаблоныДокументов", "Ссылка", Выборка.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		
		СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();
		СправочникОбъект.ТребуетПодписания = Истина;
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(СправочникОбъект);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ВосстановитьНастройкиШаблонов(ПараметрыОбновления = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Отчет", ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
		Метаданные.Отчеты.ШаблоныДокументовКабинетСотрудника));
	Если ПараметрыОбновления = Неопределено Тогда
		МассивОбновленных = Новый Массив;
	Иначе
		
		Если ПараметрыОбновления.Свойство("МассивОбновленных") Тогда
			МассивОбновленных = ПараметрыОбновления.МассивОбновленных;
		Иначе
			МассивОбновленных = Новый Массив;
			ПараметрыОбновления.Вставить("МассивОбновленных", МассивОбновленных);
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МассивОбновленных", МассивОбновленных);
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	ВариантыОтчетов.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ВариантыОтчетов КАК ВариантыОтчетов
		|ГДЕ
		|	ВариантыОтчетов.Отчет = &Отчет
		|	И ВариантыОтчетов.ПометкаУдаления
		|	И ВариантыОтчетов.Наименование ПОДОБНО ""ШаблоныДокументов%""
		|	И НЕ ВариантыОтчетов.Ссылка В (&МассивОбновленных)";
	
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1000","");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 0 Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	Пока Выборка.Следующий() Цикл
		
		МассивОбновленных.Добавить(Выборка.Ссылка);
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "Справочник.ВариантыОтчетов", "Ссылка", Выборка.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		
		СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();
		СправочникОбъект.ПометкаУдаления = Ложь;
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(СправочникОбъект);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПустаяТаблицаВерсий()
	
	ТаблицаВерсий = Новый ТаблицаЗначений;
	ТаблицаВерсий.Колонки.Добавить("Версия", Новый ОписаниеТипов("Строка"));
	ТаблицаВерсий.Колонки.Добавить("ДатаРегистрации", Новый ОписаниеТипов("Дата", , , Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)));
	ТаблицаВерсий.Колонки.Добавить("Комментарий", Новый ОписаниеТипов("Строка"));
	ТаблицаВерсий.Колонки.Добавить("Настройки", Новый ОписаниеТипов("Строка"));
	ТаблицаВерсий.Колонки.Добавить("Предопределенный", Новый ОписаниеТипов("Булево"));
	
	Возврат ТаблицаВерсий;
	
КонецФункции

Функция ПредопределенныеВерсииШаблонов(ИмяПредопределенныхДанных) Экспорт
	
	ТаблицаВерсий = ПустаяТаблицаВерсий();
	Для Каждого Макет Из Метаданные.Справочники.ШаблоныДокументов.Макеты Цикл
		Если СтрНачинаетсяС(Макет.Имя, ИмяПредопределенныхДанных) Тогда
			НоваяСтрока = ТаблицаВерсий.Добавить();
			НоваяСтрока.Версия = СтрЗаменить(СтрЗаменить(Макет.Имя, ИмяПредопределенныхДанных, ""), "_", ".");
			Если СтрНачинаетсяС(НоваяСтрока.Версия, ".") Тогда
				НоваяСтрока.Версия = Сред(НоваяСтрока.Версия, 2)
			КонецЕсли;
			НоваяСтрока.Версия = НормализованныйНомерВерсии(НоваяСтрока.Версия);
			НоваяСтрока.Настройки = Справочники.ШаблоныДокументов.ПолучитьМакет(Макет.Имя).ПолучитьТекст();
			НоваяСтрока.Комментарий = Макет.Синоним;
			НоваяСтрока.Предопределенный = Истина;
		КонецЕсли;
	КонецЦикла;
	
	ЗаполнитьПорядокВерсий(ТаблицаВерсий);
	Возврат ТаблицаВерсий;
	
КонецФункции

Функция ВерсииШаблона(ШаблонДокумента) Экспорт
	
	ТаблицаВерсий = ПустаяТаблицаВерсий();
	
	ИмяПредопределенныхДанных = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ШаблонДокумента, "ИмяПредопределенныхДанных");
	Если Не ПустаяСтрока(ИмяПредопределенныхДанных) Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ПредопределенныеВерсииШаблонов(ИмяПредопределенныхДанных), ТаблицаВерсий);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ШаблонДокумента", ШаблонДокумента);
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ШаблоныДокументовИсторияИзменений.Версия КАК Версия,
		|	ШаблоныДокументовИсторияИзменений.ДатаРегистрации КАК ДатаРегистрации,
		|	ШаблоныДокументовИсторияИзменений.Комментарий КАК Комментарий,
		|	ШаблоныДокументовИсторияИзменений.Настройки КАК Настройки
		|ИЗ
		|	Справочник.ШаблоныДокументов.ИсторияИзменений КАК ШаблоныДокументовИсторияИзменений
		|ГДЕ
		|	ШаблоныДокументовИсторияИзменений.Ссылка = &ШаблонДокумента";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = ТаблицаВерсий.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		НоваяСтрока.Настройки = Выборка.Настройки.Получить();
	КонецЦикла;
	
	ЗаполнитьПорядокВерсий(ТаблицаВерсий);
	
	Возврат ТаблицаВерсий;
	
КонецФункции

Функция НормализованныйНомерВерсии(Версия) Экспорт
	
	Если Не ЗначениеЗаполнено(Версия) Тогда
		Возврат "0.0.0";
	КонецЕсли;
	
	НомераВерсии = СтрРазделить(Версия, ".");
	Для Номер = 0 По 2 Цикл
		Если Номер < НомераВерсии.Количество() Тогда
			Если Не СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(НомераВерсии[Номер]) Тогда
				ВызватьИсключение НСтр("ru = 'Не верный номер версии шаблона документов(). Номер версии шаблона документов состоит из 3-х чисел.';
										|en = 'Не верный номер версии шаблона документов(). Номер версии шаблона документов состоит из 3-х чисел.'")
			КонецЕсли;
		Иначе
			НомераВерсии.Добавить("0");
		КонецЕсли;
	КонецЦикла;
	
	Возврат СтрСоединить(НомераВерсии, ".");
	
КонецФункции

Процедура ЗаполнитьПорядокВерсий(ТаблицаСВерсией)
	
	Если ТаблицаСВерсией.Колонки.Найти("Версия") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаВерсий = ТаблицаСВерсией.Скопировать(, "Версия");
	ТаблицаВерсий.Свернуть("Версия");
	
	ПорядокВерсий = Новый Соответствие;
	Для Каждого СтрокаВерсии Из ТаблицаВерсий Цикл
		ВерсияСтроки = НормализованныйНомерВерсии(СтрокаВерсии.Версия);
		ПорядокВерсий.Вставить(ВерсияСтроки, ПорядокВерсии(ВерсияСтроки));
	КонецЦикла;
	
	Если ТаблицаСВерсией.Колонки.Найти("ПорядокВерсий") = Неопределено Тогда
		ТаблицаСВерсией.Колонки.Добавить("ПорядокВерсий", Новый ОписаниеТипов("Строка"));
	КонецЕсли;
	Для Каждого СтрокаТаблицаОтличий Из ТаблицаСВерсией Цикл
		СтрокаТаблицаОтличий.ПорядокВерсий = ПорядокВерсий.Получить(НормализованныйНомерВерсии(СтрокаТаблицаОтличий.Версия));
	КонецЦикла;
	
	Если ТаблицаСВерсией.Колонки.Найти("ДатаРегистрации") = Неопределено Тогда
		ТаблицаСВерсией.Сортировать("ПорядокВерсий Убыв");
	Иначе
		ТаблицаСВерсией.Сортировать("ПорядокВерсий Убыв, ДатаРегистрации Убыв");
	КонецЕсли;
	
КонецПроцедуры

Функция ПорядокВерсии(Знач Версия) Экспорт
	Версия = НормализованныйНомерВерсии(Версия);
	ДлинаЧасти = 3;
	ЧастиВерсии = СтрРазделить(Версия, ".");
	ЧастиВерсии[0] = СтроковыеФункцииКлиентСервер.ДополнитьСтроку(ЧастиВерсии[0], ДлинаЧасти, "0", "Слева");
	ЧастиВерсии[1] = СтроковыеФункцииКлиентСервер.ДополнитьСтроку(ЧастиВерсии[1], ДлинаЧасти, "0", "Слева");
	ЧастиВерсии[2] = СтроковыеФункцииКлиентСервер.ДополнитьСтроку(ЧастиВерсии[2], ДлинаЧасти, "0", "Слева");
	Возврат СтрСоединить(ЧастиВерсии, ".");
КонецФункции

Функция НаименованиеПредопределенногоЭлемента(ИмяПредопределенныхДанных)
	Наименование = ИмяПредопределенныхДанных;
	Для Каждого МакетСправочника Из Метаданные.Справочники.ШаблоныДокументов.Макеты Цикл
		Если СтрНачинаетсяС(МакетСправочника.Имя, "НаименованияПредопределенныхШаблонов") Тогда
			ЧтениеJSON = Новый ЧтениеJSON;
			ЧтениеJSON.УстановитьСтроку(Справочники.ШаблоныДокументов.ПолучитьМакет(МакетСправочника.Имя).ПолучитьТекст());
			НаименованияПредопределенных = ПрочитатьJSON(ЧтениеJSON);
			Если НаименованияПредопределенных.Свойство(ИмяПредопределенныхДанных) Тогда
				Наименование = НаименованияПредопределенных[ИмяПредопределенныхДанных];
				Прервать;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Возврат Наименование;
КонецФункции

#КонецОбласти

Функция ТекущаяВерсияШаблонов() Экспорт
	Версия = КабинетСотрудника.ВерсияФормата();
	Возврат НормализованныйНомерВерсии(Версия);
КонецФункции

Процедура ОбновитьВерсииШаблонов(ПараметрыОбновления = Неопределено) Экспорт
	
	Если ПараметрыОбновления = Неопределено Тогда
		МассивОбновленных = Новый Массив;
	Иначе
		
		Если ПараметрыОбновления.Свойство("МассивОбновленных") Тогда
			МассивОбновленных = ПараметрыОбновления.МассивОбновленных;
		Иначе
			МассивОбновленных = Новый Массив;
			ПараметрыОбновления.Вставить("МассивОбновленных", МассивОбновленных);
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбновленных", МассивОбновленных);
	
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	ШаблоныДокументов.Ссылка КАК Ссылка,
		|	ШаблоныДокументов.Предопределенный КАК Предопределенный,
		|	ШаблоныДокументов.ИмяПредопределенныхДанных КАК ИмяПредопределенныхДанных,
		|	ШаблоныДокументов.МакетДокумента КАК МакетДокумента
		|ИЗ
		|	Справочник.ШаблоныДокументов КАК ШаблоныДокументов
		|ГДЕ
		|	НЕ ШаблоныДокументов.Ссылка В (&МассивОбновленных)
		|	И ШаблоныДокументов.Версия = """"";
	
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1000","");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 0 Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	Пока Выборка.Следующий() Цикл
		
		МассивОбновленных.Добавить(Выборка.Ссылка);
		МакетПечатнойФормы = Выборка.МакетДокумента.Получить();
		Если МакетПечатнойФормы = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "Справочник.ШаблоныДокументов", "Ссылка", Выборка.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		
		СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();
		
		МакетТекущихНастроек = JSONШаблонаДокументаПоИдентификаторуШаблона(МакетПечатнойФормы, Выборка.Ссылка.УникальныйИдентификатор());
		СправочникОбъект.Настройки = Новый ХранилищеЗначения(МакетТекущихНастроек, Новый СжатиеДанных(9));
		
		Если Выборка.Предопределенный Тогда
			ПредопределенныеВерсии = ПредопределенныеВерсииШаблонов(Выборка.ИмяПредопределенныхДанных);
			ПодходящаяВерсия = "";
			КоличествоРазличий = Неопределено;
			Для Каждого ПредопределеннаяВерсия Из ПредопределенныеВерсии Цикл
				ТаблицаОтличий = ПустаяТаблицаОтличий();
				Если Не СобратьРазличияJSONШаблонов(ПредопределеннаяВерсия.Настройки, МакетТекущихНастроек, Истина, ТаблицаОтличий) Тогда
					ПодходящаяВерсия = ПредопределеннаяВерсия.Версия;
					КоличествоРазличий = Неопределено;
					Прервать;
				Иначе
					Если КоличествоРазличий = Неопределено
						Или КоличествоРазличий > ТаблицаОтличий.Количество() Тогда
						
						ПодходящаяВерсия = ПредопределеннаяВерсия.Версия;
						КоличествоРазличий = ТаблицаОтличий.Количество();
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			СправочникОбъект.Версия = ПодходящаяВерсия;
			Если КоличествоРазличий <> Неопределено Тогда
				СправочникОбъект.Изменен = Истина;
			КонецЕсли;
		Иначе
			СправочникОбъект.Версия = ТекущаяВерсияШаблонов();
		КонецЕсли;
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(СправочникОбъект);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;
	
КонецПроцедуры

Функция СформироватьОтчет(ШаблонДокумента, СписокВерсий) Экспорт
	
	ДокументРезультат = Новый ТабличныйДокумент;
	МакетОтчетаОРазличиях = ПолучитьМакет("МакетОтчетаОРазличиях");
	
	Область = МакетОтчетаОРазличиях.ПолучитьОбласть("Шапка");
	Область.Параметры.Заполнить(Новый Структура("Шаблон", ШаблонДокумента));
	ДокументРезультат.Вывести(Область);
	
	ОтличияВерсий = ПустаяТаблицаОтличий();
	СравниваемыеВерсии = ОтличияВерсий.СкопироватьКолонки("Версия,ДатаРегистрации,ПорядокВерсий");
	
	ВерсииШаблона = ВерсииШаблона(ШаблонДокумента);
	
	ТаблицаВерсий = Новый ТаблицаЗначений;
	ТаблицаВерсий.Колонки.Добавить("Версия", Новый ОписаниеТипов("Строка"));
	ТаблицаВерсий.Колонки.Добавить("ДатаРегистрации", Новый ОписаниеТипов("Дата", , , Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)));
	ТаблицаВерсий.Колонки.Добавить("СтруктураПоиска", Новый ОписаниеТипов());
	ТаблицаВерсий.Колонки.Добавить("ОбщийПорядок", Новый ОписаниеТипов("Число"));
	Для Каждого ЭлементСписка Из СписокВерсий Цикл
		НоваяСтрокаТаблицы = ТаблицаВерсий.Добавить();
		НоваяСтрокаТаблицы.СтруктураПоиска = ЭлементСписка;
		Если НоваяСтрокаТаблицы.СтруктураПоиска = Неопределено Тогда
			НоваяСтрокаТаблицы.ОбщийПорядок = 1;
		Иначе
			ЗаполнитьЗначенияСвойств(НоваяСтрокаТаблицы, ЭлементСписка);
		КонецЕсли;
	КонецЦикла;
	ТаблицаВерсий.Сортировать("ОбщийПорядок,Версия,ДатаРегистрации");
	
	ИсходнаяВерсия = ТаблицаВерсий[0];
	СтрокиВерсий = ВерсииШаблона.НайтиСтроки(ИсходнаяВерсия.СтруктураПоиска);
	Если ЗначениеЗаполнено(СтрокиВерсий) Тогда
		СтрокаВерсии = СтрокиВерсий[0];
		ИсходныеНастройки = СтрокаВерсии.Настройки;
		НоваяСтрокаВерсии = СравниваемыеВерсии.Добавить();
		НоваяСтрокаВерсии.Версия = СтрокаВерсии.Версия;
		НоваяСтрокаВерсии.ДатаРегистрации = СтрокаВерсии.ДатаРегистрации;
		НоваяСтрокаВерсии.ПорядокВерсий = СтрокаВерсии.ПорядокВерсий;
		
		Область = МакетОтчетаОРазличиях.ПолучитьОбласть("Заголовок|Изменения");
		Область.Параметры.Заполнить(НоваяСтрокаВерсии);
		ДокументРезультат.Вывести(Область);
		
	КонецЕсли;
	
	Для Индекс = 1 По ТаблицаВерсий.Количество() - 1 Цикл
		
		ОчереднаяВерсия = ТаблицаВерсий[Индекс];
		Если ОчереднаяВерсия.СтруктураПоиска = Неопределено Тогда
			РеквизитыШаблона = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ШаблонДокумента, "Версия, Настройки");
			ТекущиеНастройки = РеквизитыШаблона.Настройки.Получить();
			НоваяСтрокаВерсии = СравниваемыеВерсии.Добавить();
			НоваяСтрокаВерсии.Версия = РеквизитыШаблона.Версия;
			НоваяСтрокаВерсии.ДатаРегистрации = ТекущаяДатаСеанса();
			НоваяСтрокаВерсии.ПорядокВерсий = ПорядокВерсии(НоваяСтрокаВерсии.Версия);
		Иначе
			СтрокиВерсий = ВерсииШаблона.НайтиСтроки(ОчереднаяВерсия.СтруктураПоиска);
			Если ЗначениеЗаполнено(СтрокиВерсий) Тогда
				СтрокаВерсии = СтрокиВерсий[0];
				ТекущиеНастройки = СтрокаВерсии.Настройки;
				НоваяСтрокаВерсии = СравниваемыеВерсии.Добавить();
				НоваяСтрокаВерсии.Версия = СтрокаВерсии.Версия;
				НоваяСтрокаВерсии.ДатаРегистрации = СтрокаВерсии.ДатаРегистрации;
				НоваяСтрокаВерсии.ПорядокВерсий = СтрокаВерсии.ПорядокВерсий;
			КонецЕсли;
		КонецЕсли;
		
		Область = МакетОтчетаОРазличиях.ПолучитьОбласть("Заголовок|Изменения");
		Область.Параметры.Заполнить(НоваяСтрокаВерсии);
		ДокументРезультат.Присоединить(Область);
		
		ОтличияТекущихВерсий = ПустаяТаблицаОтличий();
		СобратьРазличияJSONШаблонов(ИсходныеНастройки, ТекущиеНастройки, Истина, ОтличияТекущихВерсий);
		
		ОтличияТекущихВерсий.ЗаполнитьЗначения(НоваяСтрокаВерсии.Версия, "Версия");
		ОтличияТекущихВерсий.ЗаполнитьЗначения(НоваяСтрокаВерсии.ДатаРегистрации, "ДатаРегистрации");
		ОтличияТекущихВерсий.ЗаполнитьЗначения(НоваяСтрокаВерсии.ПорядокВерсий, "ПорядокВерсий");
		
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ОтличияТекущихВерсий, ОтличияВерсий);
		
		ИсходныеНастройки = ТекущиеНастройки;
	КонецЦикла;
	ТаблицаРазделов = ОтличияВерсий.Скопировать(, "РазделШаблона,Изменение");
	ТаблицаРазделов.Свернуть("РазделШаблона,Изменение");
	
	РазделШаблона = Неопределено;
	Изменение = Неопределено;
	Для Каждого СтрокаРазделов Из ТаблицаРазделов Цикл
		
		Если РазделШаблона <> СтрокаРазделов.РазделШаблона Тогда
			Область = МакетОтчетаОРазличиях.ПолучитьОбласть("Раздел");
			Область.Параметры.Заполнить(СтрокаРазделов);
			ДокументРезультат.Вывести(Область);
			РазделШаблона = СтрокаРазделов.РазделШаблона;
			Изменение = Неопределено;
		КонецЕсли;
		
		Если Изменение <> СтрокаРазделов.Изменение Тогда
			Область = МакетОтчетаОРазличиях.ПолучитьОбласть("Изменение");
			Область.Параметры.Заполнить(СтрокаРазделов);
			ДокументРезультат.Вывести(Область);
			Изменение = СтрокаРазделов.Изменение;
		КонецЕсли;
		
		КоллекцияОтличий = Новый Соответствие;
		КоличествоОтличий = 0;
		Для Каждого СтрокаВерсии Из СравниваемыеВерсии Цикл
			СтруктураПоиска = Новый Структура("ПорядокВерсий, ДатаРегистрации");
			ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаВерсии);
			СтруктураПоиска.Вставить("РазделШаблона", СтрокаРазделов.РазделШаблона);
			СтруктураПоиска.Вставить("Изменение", СтрокаРазделов.Изменение);
			Отличия = ОтличияВерсий.НайтиСтроки(СтруктураПоиска);
			КоллекцияОтличий.Вставить(СтрокаВерсии, Отличия);
			Если КоличествоОтличий < Отличия.Количество() Тогда
				КоличествоОтличий = Отличия.Количество();
			КонецЕсли;
		КонецЦикла;
		
		Для Индекс = 0 По КоличествоОтличий - 1 Цикл
			ПерваяВерсия = Истина;
			Для Каждого СтрокаВерсии Из СравниваемыеВерсии Цикл
				Область = МакетОтчетаОРазличиях.ПолучитьОбласть("Описание|Изменения");
				
				Отличия = КоллекцияОтличий.Получить(СтрокаВерсии);
				Если Индекс < Отличия.Количество() Тогда
					СтрокаИзменений = Отличия[Индекс];
					Область.Параметры.Заполнить(СтрокаИзменений);
				КонецЕсли;
				
				Если ПерваяВерсия Тогда
					ДокументРезультат.Вывести(Область);
					ПерваяВерсия = Ложь;
				Иначе
					ДокументРезультат.Присоединить(Область);
				КонецЕсли;
		
			КонецЦикла;
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ДокументРезультат;
	
КонецФункции

Процедура ОбновитьШаблонДокументаОтсутсвие_3_0_1(ПараметрыОбновления = Неопределено) Экспорт
	
	ОбновитьПредопределенныйШаблонДокумента("Отсутствие", ПараметрыОбновления);
	
КонецПроцедуры

Процедура ОбновитьШаблоныДокументовЗаказаСправок_3_0_1(ПараметрыОбновления = Неопределено) Экспорт
	
	ОбновитьПредопределенныйШаблонДокумента("ЗапросСправки2НДФЛ", ПараметрыОбновления);
	ОбновитьПредопределенныйШаблонДокумента("ЗапросСправкиПоОстаткамОтпуска", ПараметрыОбновления);
	ОбновитьПредопределенныйШаблонДокумента("ЗапросСправкиСМестаРаботы", ПараметрыОбновления);
	
КонецПроцедуры

Процедура ЗаполнитьОбязательныеРеквизитыВариантовОтчетов(ПараметрыОбновления = Неопределено) Экспорт
	
	
	Если ПараметрыОбновления = Неопределено Тогда
		МассивОбновленных = Новый Массив;
	Иначе
		
		Если ПараметрыОбновления.Свойство("МассивОбновленных") Тогда
			МассивОбновленных = ПараметрыОбновления.МассивОбновленных;
		Иначе
			МассивОбновленных = Новый Массив;
			ПараметрыОбновления.Вставить("МассивОбновленных", МассивОбновленных);
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбновленных", МассивОбновленных);
	Запрос.УстановитьПараметр("ИдентификаторОтчета",
		ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
			Метаданные.Отчеты.ШаблоныДокументовКабинетСотрудника));
	
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	ВариантыОтчетов.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ВариантыОтчетов КАК ВариантыОтчетов
		|ГДЕ
		|	ВариантыОтчетов.Назначение = ЗНАЧЕНИЕ(Перечисление.НазначенияВариантовОтчетов.ПустаяСсылка)
		|	И ВариантыОтчетов.Отчет = &ИдентификаторОтчета";
	
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1000","");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 0 Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	Пока Выборка.Следующий() Цикл
		
		МассивОбновленных.Добавить(Выборка.Ссылка);
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(
			ПараметрыОбновления, "Справочник.ВариантыОтчетов", "Ссылка", Выборка.Ссылка) Тогда
			
			Продолжить;
		КонецЕсли;
		
		СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();
		СправочникОбъект.Назначение = Перечисления.НазначенияВариантовОтчетов.ДляКомпьютеровИПланшетов;
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(СправочникОбъект);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли