
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;

	ЗаполнитьОтборы();
	УстановитьОтборы(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборВидСобытияПриИзменении(Элемент)
	
	УстановитьОтборы(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
	ПараметрыФормы = Новый Структура;
	
	Если Копирование Тогда
		ПараметрыФормы.Вставить("ЗначениеКопирования", Элементы.Список.ТекущиеДанные.Ссылка);
	КонецЕсли;
	
	ЗначенияЗаполнения = Новый Структура;
	Для каждого ЭлементОтбора Из Список.Отбор.Элементы Цикл
		
		Если НЕ ЭлементОтбора.Использование Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно Тогда
			ЗначенияЗаполнения.Вставить(Строка(ЭлементОтбора.ЛевоеЗначение), ЭлементОтбора.ПравоеЗначение);
		КонецЕсли;
		
		Если ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке Тогда
			ЗначенияЗаполнения.Вставить(Строка(ЭлементОтбора.ЛевоеЗначение), ЭлементОтбора.ПравоеЗначение[0].Значение);
		КонецЕсли;
		
	КонецЦикла;
	
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ВидСобытияОСДоступныеЗначения = ПолучитьДоступныеЗначенияВидСобытияОС();
	
	Если ВидСобытияОСДоступныеЗначения.Количество() > 0 Тогда
		ПараметрыФормы.Вставить("ВидСобытияОСДоступныеЗначения", ВидСобытияОСДоступныеЗначения)
	КонецЕсли;
	
	ОткрытьФорму("Справочник.СобытияОС.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", Элемент.ТекущаяСтрока);
	
	ВидСобытияОСДоступныеЗначения = ПолучитьДоступныеЗначенияВидСобытияОС();
	
	Если ВидСобытияОСДоступныеЗначения.Количество() > 0 Тогда
		ПараметрыФормы.Вставить("ВидСобытияОСДоступныеЗначения", ВидСобытияОСДоступныеЗначения)
	КонецЕсли;
	
	ОткрытьФорму("Справочник.СобытияОС.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ПолучитьДоступныеЗначенияВидСобытияОС()
	
	ВидДоговораДоступныеЗначения = Новый Массив;
	
	Для каждого ЭлементОтбора Из Список.Отбор.Элементы Цикл
		
		Если НЕ ЭлементОтбора.Использование Тогда
			Продолжить;
		КонецЕсли;
		
		Если Строка(ЭлементОтбора.ЛевоеЗначение) <> "ВидСобытияОС" Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно Тогда
			ВидДоговораДоступныеЗначения.Добавить(ЭлементОтбора.ПравоеЗначение);
		КонецЕсли;
		
		Если ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке Тогда
			Для каждого ЭлементСпискаЗначений Из ЭлементОтбора.ПравоеЗначение Цикл
				ВидДоговораДоступныеЗначения.Добавить(ЭлементСпискаЗначений.Значение);
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ВидДоговораДоступныеЗначения;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьОтборы()
	
	Если НЕ Параметры.Свойство("Отбор") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("ВидСобытияОС") Тогда
		
		Если ТипЗнч(Параметры.Отбор.ВидСобытияОС) = Тип("Массив")
			ИЛИ ТипЗнч(Параметры.Отбор.ВидСобытияОС) = Тип("ФиксированныйМассив") Тогда
		
			Для Каждого Элемент Из Параметры.Отбор.ВидСобытияОС Цикл
				ОтборВидСобытия.Добавить(Элемент);
			КонецЦикла;	
			
		ИначеЕсли ТипЗнч(Параметры.Отбор.ВидСобытияОС) = Тип("СписокЗначений") Тогда
		
			Для Каждого Элемент Из Параметры.Отбор.ВидСобытияОС Цикл
				ОтборВидСобытия.Добавить(Элемент.Значение);
			КонецЦикла;	
			
		ИначеЕсли ТипЗнч(Параметры.Отбор.ВидСобытияОС) = Тип("ПеречислениеСсылка.ВидыСобытийОС") Тогда
		
			ОтборВидСобытия.Добавить(Параметры.Отбор.ВидСобытияОС);
			
		КонецЕсли;
		
		Параметры.Отбор.Удалить("ВидСобытияОС");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборы(Форма)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Форма.Список, 
		"ВидСобытияОС", 
		Форма.ОтборВидСобытия, 
		ВидСравненияКомпоновкиДанных.ВСписке,,
		ЗначениеЗаполнено(Форма.ОтборВидСобытия));
	
КонецПроцедуры

#КонецОбласти
