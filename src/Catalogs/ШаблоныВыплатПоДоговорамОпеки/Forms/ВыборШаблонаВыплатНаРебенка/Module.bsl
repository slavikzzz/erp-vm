#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.РежимВыбора Тогда
		Элементы.Список.РежимВыбора = Истина;
	Иначе 
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Параметры.Свойство("ФизическоеЛицо", ФизическоеЛицо);
	
	Если Не ЗначениеЗаполнено(ФизическоеЛицо) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ВозвращаемоеЗначениеУстановлено = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ЕстьОграниченияВыплатПоВозрастуРебенка = ЕстьОграниченияВыплатПоВозрастуРебенка(Значение);
	Если Не ЗначениеЗаполнено(Ребенок) И ЕстьОграниченияВыплатПоВозрастуРебенка Тогда
		ТекстОшибки = НСтр("ru = 'Необходимо указать ребенка, так как выбранный шаблон содержит выплаты с ограничениями по возрасту ребенка.';
							|en = 'Specify the child, since the selected template contains payments with restrictions on the child age.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки, , "Ребенок");
		Возврат;
	ИначеЕсли ЗначениеЗаполнено(Ребенок) И ЕстьОграниченияВыплатПоВозрастуРебенка И Не ДатаРожденияРебенкаУказана(Ребенок) Тогда 
		ТекстОшибки = НСтр("ru = 'Необходимо указать дату рождения ребенка, так как выбранный шаблон содержит выплаты с ограничениями по возрасту ребенка.';
							|en = 'Specify the child date of birth, since the selected template contains payments with restrictions on the child age.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки, , "Ребенок");
		Возврат;
	КонецЕсли;
	ОповеститьОВыборе(Новый Структура("Шаблон,Ребенок", Значение, Ребенок));
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ЕстьОграниченияВыплатПоВозрастуРебенка(ШаблонСсылка)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ШаблоныВыплатПоДоговорамОпекиВыплаты.Ссылка
		|ИЗ
		|	Справочник.ШаблоныВыплатПоДоговорамОпеки.Выплаты КАК ШаблоныВыплатПоДоговорамОпекиВыплаты
		|ГДЕ
		|	ШаблоныВыплатПоДоговорамОпекиВыплаты.Ссылка = &Ссылка
		|	И (ШаблоныВыплатПоДоговорамОпекиВыплаты.ВыплачиваетсяПриВозрастеОт > 0
		|			ИЛИ ШаблоныВыплатПоДоговорамОпекиВыплаты.ВыплачиваетсяПриВозрастеДо > 0)";
	
	Запрос.УстановитьПараметр("Ссылка", ШаблонСсылка);
	РезультатЗапроса = Запрос.Выполнить();
	Возврат Не РезультатЗапроса.Пустой();

КонецФункции

&НаСервереБезКонтекста
Функция ДатаРожденияРебенкаУказана(Ребенок)
	Возврат ЗначениеЗаполнено(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ребенок, "ДатаРождения"));
КонецФункции

#КонецОбласти