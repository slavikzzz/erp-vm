#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Куратор = Пользователи.ТекущийПользователь();
	Исполнитель = Пользователи.ТекущийПользователь();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если Закрыта Тогда
		
		// проверить заполнение полей
		Если Куратор <> Пользователи.ТекущийПользователь() Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Задача не закрыта, так как Вы не являетесь ее куратором.';
				|en = 'The task is not closed as you are not its supervisor.'"),
			ЭтотОбъект,
			"Куратор",
			,
			Отказ);
		КонецЕсли;	
		
		Если ОписаниеРезультата = "" Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Задача не закрыта, так как нет описания результата.';
				|en = 'Task is not closed, as there is no result description.'"),
			ЭтотОбъект,
			"ОписаниеРезультата",, Отказ);
		КонецЕсли;	
		
		Если ДатаНачала = '00010101' Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Задача не закрыта, так не определена дата ее начала.';
				|en = 'The task is not closed as its start date is not determined.'"),
			ЭтотОбъект,
			"ДатаНачала",, Отказ);
		КонецЕсли;
		
		Если ДатаОкончания = '00010101' Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Задача не закрыта, так не определена дата ее окончания.';
				|en = 'The task is not closed as its end date is not determined.'"),
			ЭтотОбъект,
			"ДатаОкончания",, Отказ);
		КонецЕсли;
		
		Если Стадия <> Перечисления.СтадииЗадач.Проверена Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Задача не закрыта, так как не проверена.';
				|en = 'Task is not closed, as it is not checked.'"),
			ЭтотОбъект,
			"Стадия",, Отказ);
		КонецЕсли;
		
		// установить дату окончания
		Если НЕ Отказ И (ДатаОкончания < ДатаНачала) Тогда
			ДатаОкончания = ТекущаяДатаСеанса();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли