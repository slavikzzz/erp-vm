
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ПараметрыЭкземпляров = Параметры.ПараметрыЭкземпляров;
	ЗаполнитьЗначенияСвойств(ЭтаФорма, ПараметрыЭкземпляров);
	Элементы.ОткрытьФормы.Видимость = ПараметрыЭкземпляров.ФлагОткрытьФормы;
	
	МеждународнаяОтчетностьСервер.ЗаполнитьСписокРесурсовОтчета(Элементы.Ресурс, ВалютыРесурсов, ПараметрыЭкземпляров.ПланыСчетов);
	СписокВыбора = Элементы.Ресурс.СписокВыбора;
	Если ПустаяСтрока(Ресурс) И СписокВыбора.Количество() Тогда
		Ресурс = Элементы.Ресурс.СписокВыбора[0].Значение;
	КонецЕсли;
	
	КратностьСумм = Перечисления.ВидыПредставленияЧисел.БезИзменений;
	
	Элементы.НаправлениеДеятельности.Видимость = Справочники.НаправленияДеятельности.ИспользуетсяУчетПоНаправлениям();

	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВыбратьПериод(Команда)

	ПараметрыВыбора = Новый Структура("НачалоПериода,КонецПериода", НачалоПериода, КонецПериода);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериода", ПараметрыВыбора, Элементы.ВыбратьПериод, , , , ОписаниеОповещения);

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериодЗавершение(РезультатВыбора, ДопПараметры) Экспорт

	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(ЭтаФорма, РезультатВыбора, "НачалоПериода,КонецПериода");

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сформировать(Команда)
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЭкземпляров = МеждународнаяОтчетностьКлиентСервер.НовыеПараметрыФормированияОтчета();
	ЗаполнитьЗначенияСвойств(ПараметрыЭкземпляров, ЭтаФорма);
	ПараметрыЭкземпляров.Ресурс = Ресурс;
	ПараметрыЭкземпляров.Валюта = ВалютыРесурсов[Ресурс];
	Закрыть(ПараметрыЭкземпляров);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти
