#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ТолькоПросмотр Тогда
		Элементы.ФормаОК.Видимость = Ложь;
		Элементы.ФормаЗакрыть.КнопкаПоУмолчанию = Истина;
	КонецЕсли;
	
	АдресНастройки = Параметры.АдресНастройки;
	ЗаполнитьНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ПеренестиИзмененияВОбъектФормыВладельца();
	Закрыть(АдресНастройки);
	
КонецПроцедуры

&НаКлиенте
Процедура ЛевоСоставФОТ(Команда)
	
	ПеренестиВНачисленияНеВключатьВФОТ(Элементы.НачисленияВключатьВФОТ.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ПравоСоставФОТ(Команда)
	
	ПеренестиВНачисленияВключатьВФОТ(Элементы.НачисленияНеВключатьВФОТ.ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНачисленияВключатьВФОТ

&НаКлиенте
Процедура НачисленияВключатьВФОТПриАктивизацииСтроки(Элемент)
	УстановитьДоступностьКоманд();
КонецПроцедуры

&НаКлиенте
Процедура НачисленияВключатьВФОТВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ОткрытьФормуПланаВидовРасчета(Элемент, Поле);
КонецПроцедуры

&НаКлиенте
Процедура НачисленияВключатьВФОТПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	СтандартнаяОбработка = Ложь;
	ПеренестиВНачисленияВключатьВФОТ(Элементы.НачисленияНеВключатьВФОТ.ТекущиеДанные);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНачисленияНеВключатьВФОТ

&НаКлиенте
Процедура НачисленияНеВключатьВФОТПриАктивизацииСтроки(Элемент)
	УстановитьДоступностьКоманд();
КонецПроцедуры

&НаКлиенте
Процедура НачисленияНеВключатьВФОТВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ОткрытьФормуПланаВидовРасчета(Элемент, Поле);
КонецПроцедуры

&НаКлиенте
Процедура НачисленияНеВключатьВФОТПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	СтандартнаяОбработка = Ложь;
	ПеренестиВНачисленияНеВключатьВФОТ(Элементы.НачисленияВключатьВФОТ.ТекущиеДанные);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьНаСервере()
	
	НачисленияВключатьВФОТ.Очистить();
	НачисленияНеВключатьВФОТ.Очистить();
	
	ТаблицаИсключаемыхНачислений = ПолучитьИзВременногоХранилища(АдресНастройки);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИсключаемыеНачисления", ТаблицаИсключаемыхНачислений.ВыгрузитьКолонку("ВидРасчета"));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Начисления.Ссылка КАК ВидРасчета
	|ИЗ
	|	ПланВидовРасчета.Начисления КАК Начисления
	|ГДЕ
	|	НЕ Начисления.Ссылка В (&ИсключаемыеНачисления)";
	
	ТаблицаВключаемыхНачислений = Запрос.Выполнить().Выгрузить();
	
	// Заполнение таблиц начислений
	НачисленияНеВключатьВФОТ.Загрузить(ТаблицаИсключаемыхНачислений);
	НачисленияВключатьВФОТ.Загрузить(ТаблицаВключаемыхНачислений);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьКоманд()
	
	ТекущиеДанные = Элементы.НачисленияВключатьВФОТ.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено И ТекущиеДанные.ТолькоПросмотр Тогда 
		ДоступностьПравоСоставФОТ = Ложь;
	Иначе
		ДоступностьПравоСоставФОТ= Истина;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ПравоСоставФОТ",
		"Доступность",
		ДоступностьПравоСоставФОТ);
	
	ТекущиеДанные = Элементы.НачисленияНеВключатьВФОТ.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено И ТекущиеДанные.ТолькоПросмотр Тогда 
		ДоступностьЛевоСоставФОТ = Ложь;
	Иначе
		ДоступностьЛевоСоставФОТ = Истина;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ЛевоСоставФОТ",
		"Доступность",
		ДоступностьЛевоСоставФОТ);
		
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуПланаВидовРасчета(Элемент, Поле)

	Если Поле.ТолькоПросмотр Тогда
		
		ТекущиеДанные = Элемент.ТекущиеДанные;
		
		Если ТекущиеДанные <> Неопределено Тогда 
			ОткрытьФорму("ПланВидовРасчета.Начисления.Форма.ФормаВидаРасчета", Новый Структура("Ключ", ТекущиеДанные.ВидРасчета));  
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВНачисленияНеВключатьВФОТ(ТекущиеДанные);

	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Если Не ТекущиеДанные.ТолькоПросмотр Тогда
		
		НоваяСтрока = НачисленияНеВключатьВФОТ.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущиеДанные);
		
		ТекущаяСтрока = НачисленияВключатьВФОТ.Индекс(ТекущиеДанные);
		НачисленияВключатьВФОТ.Удалить(ТекущаяСтрока);
		
		НачисленияНеВключатьВФОТ.Сортировать("РеквизитДопУпорядочивания");
		
	КонецЕсли;	

КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВНачисленияВключатьВФОТ(ТекущиеДанные);

	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Если Не ТекущиеДанные.ТолькоПросмотр Тогда
		
		НоваяСтрока = НачисленияВключатьВФОТ.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущиеДанные);
		
		ТекущаяСтрока = НачисленияНеВключатьВФОТ.Индекс(ТекущиеДанные);
		НачисленияНеВключатьВФОТ.Удалить(ТекущаяСтрока);
		
		НачисленияВключатьВФОТ.Сортировать("РеквизитДопУпорядочивания");
		
	КонецЕсли;	

КонецПроцедуры

&НаКлиенте
Процедура ПеренестиИзмененияВОбъектФормыВладельца()
	
	Если Модифицированность Тогда
		ПоместитьИзмененныеДанныеВоВременноеХранилище();
		Модифицированность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПоместитьИзмененныеДанныеВоВременноеХранилище()
	
	АдресНастройки = ПоместитьВоВременноеХранилище(НачисленияНеВключатьВФОТ.Выгрузить()
		, Новый УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти
