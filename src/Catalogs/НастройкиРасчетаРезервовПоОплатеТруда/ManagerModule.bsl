#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры
// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция СтруктураПараметровРасчетаРезервов() Экспорт
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Организация",   Справочники.Организации.ПустаяСсылка());
	СтруктураПараметров.Вставить("Резерв",        Справочники.Резервы.ПустаяСсылка());
	СтруктураПараметров.Вставить("ВидРезерва",    Перечисления.ВидыРезервовПоОплатеТруда.ПустаяСсылка());
	СтруктураПараметров.Вставить("НачалоПериода", Дата(1,1,1));
	СтруктураПараметров.Вставить("КонецПериода",  Дата(1,1,1));
	
	Возврат СтруктураПараметров;
	
КонецФункции

Функция ПлановыеНачисленияРезервов(Организация, Резерв, Период) Экспорт
	
	Возврат РезервыПоОплатеТруда.ПлановыеНачисленияРезервов(Организация, Резерв, Период);
	
КонецФункции

Функция ПараметрыРасчетаРезерва(Организация, Резерв, Период) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Резерв",      Резерв);
	Запрос.УстановитьПараметр("Период",      Период);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиРасчетаРезервовПоОплатеТруда.Ссылка КАК Ссылка,
	|	НастройкиРасчетаРезервовПоОплатеТруда.Организация КАК Организация,
	|	НастройкиРасчетаРезервовПоОплатеТруда.Резерв КАК Резерв,
	|	НастройкиРасчетаРезервовПоОплатеТруда.ВидРезерва КАК ВидРезерва,
	|	НастройкиРасчетаРезервовПоОплатеТруда.НачалоПериода КАК НачалоПериода,
	|	НастройкиРасчетаРезервовПоОплатеТруда.КонецПериода КАК КонецПериода
	|ИЗ
	|	Справочник.НастройкиРасчетаРезервовПоОплатеТруда КАК НастройкиРасчетаРезервовПоОплатеТруда
	|ГДЕ
	|	НастройкиРасчетаРезервовПоОплатеТруда.Организация = &Организация
	|	И НастройкиРасчетаРезервовПоОплатеТруда.Резерв = &Резерв
	|	И &Период МЕЖДУ НАЧАЛОПЕРИОДА(НастройкиРасчетаРезервовПоОплатеТруда.НачалоПериода, МЕСЯЦ) И КОНЕЦПЕРИОДА(НастройкиРасчетаРезервовПоОплатеТруда.КонецПериода, МЕСЯЦ)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ПараметрыРасчета = СтруктураПараметровРасчетаРезервов();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыРасчета, Выборка);
	КонецЕсли;
	
	Возврат ПараметрыРасчета;
	
КонецФункции

Функция ВидРезерва(Организация, Резервы) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Резерв",      Резервы);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НастройкиРасчетаРезервовПоОплатеТруда.Ссылка КАК Ссылка,
	|	НастройкиРасчетаРезервовПоОплатеТруда.ВидРезерва КАК ВидРезерва
	|ИЗ
	|	Справочник.НастройкиРасчетаРезервовПоОплатеТруда КАК НастройкиРасчетаРезервовПоОплатеТруда
	|ГДЕ
	|	НастройкиРасчетаРезервовПоОплатеТруда.Организация = &Организация
	|	И НастройкиРасчетаРезервовПоОплатеТруда.Резерв В(&Резерв)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ВидРезерва = Выборка.ВидРезерва;
	Иначе
		ВидРезерва = Перечисления.ВидыРезервовПоОплатеТруда.ПустаяСсылка();
	КонецЕсли;
	
	Возврат ВидРезерва;
	
КонецФункции

Процедура ПолучитьВТИсключаемыеНачисления(МенеджерВременныхТаблиц, Организация, Резерв, Период) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Резерв",      Резерв);
	Запрос.УстановитьПараметр("Период",      Период);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиРасчетаРезервовПоОплатеТруда.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТПараметрыРасчета
	|ИЗ
	|	Справочник.НастройкиРасчетаРезервовПоОплатеТруда КАК НастройкиРасчетаРезервовПоОплатеТруда
	|ГДЕ
	|	НастройкиРасчетаРезервовПоОплатеТруда.Организация = &Организация
	|	И НастройкиРасчетаРезервовПоОплатеТруда.Резерв = &Резерв
	|	И &Период МЕЖДУ НАЧАЛОПЕРИОДА(НастройкиРасчетаРезервовПоОплатеТруда.НачалоПериода, МЕСЯЦ) И КОНЕЦПЕРИОДА(НастройкиРасчетаРезервовПоОплатеТруда.КонецПериода, МЕСЯЦ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НастройкиРасчетаРезервовПоОплатеТрудаИсключенияИзРасчетнойБазыФондаОплатыТруда.ВидРасчета КАК ВидРасчета
	|ПОМЕСТИТЬ ВТИсключаемыеНачисления
	|ИЗ
	|	Справочник.НастройкиРасчетаРезервовПоОплатеТруда.ИсключенияИзРасчетнойБазыФондаОплатыТруда КАК НастройкиРасчетаРезервовПоОплатеТрудаИсключенияИзРасчетнойБазыФондаОплатыТруда
	|ГДЕ
	|	НастройкиРасчетаРезервовПоОплатеТрудаИсключенияИзРасчетнойБазыФондаОплатыТруда.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТПараметрыРасчета.Ссылка
	|			ИЗ
	|				ВТПараметрыРасчета)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТПараметрыРасчета";
	
	Запрос.Выполнить();
	
КонецПроцедуры

Функция НастройкаРезервов(Организация, Резерв, Период) Экспорт
	
	Возврат РезервыПоОплатеТруда.НастройкаРезервов(Организация, Резерв, Период);
	
КонецФункции

Функция СсылкаНаНастройкуРасчетаДляГоловнойОрганизации(ГоловнаяОрганизация, Резерв, Период) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", ГоловнаяОрганизация);
	Запрос.УстановитьПараметр("Резерв",      Резерв);
	Запрос.УстановитьПараметр("Период",      Период);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиРасчетаРезервовПоОплатеТруда.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.НастройкиРасчетаРезервовПоОплатеТруда КАК НастройкиРасчетаРезервовПоОплатеТруда
	|ГДЕ
	|	НастройкиРасчетаРезервовПоОплатеТруда.Организация = &Организация
	|	И НастройкиРасчетаРезервовПоОплатеТруда.Резерв = &Резерв
	|	И &Период МЕЖДУ НАЧАЛОПЕРИОДА(НастройкиРасчетаРезервовПоОплатеТруда.НачалоПериода, МЕСЯЦ) И КОНЕЦПЕРИОДА(НастройкиРасчетаРезервовПоОплатеТруда.КонецПериода, МЕСЯЦ)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Выборка.Следующий();
	
	Если Выборка.Количество() = 0 Тогда
		ВызватьИсключение НСтр("ru = 'Отсутствуют настройки для головной организации';
								|en = 'Settings for the parent company are missing'");
	Иначе
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
КонецФункции

Функция НастройкиДействующихРезервов(Организация, Период, УчитыватьПериодВыплаты = Ложь) Экспорт
	
	Возврат РезервыПоОплатеТруда.НастройкиДействующихРезервов(Организация, Период, УчитыватьПериодВыплаты);
	
КонецФункции

Функция ОписаниеНастройкиДействующихРезервов() Экспорт
	
	Возврат РезервыПоОплатеТруда.ОписаниеНастройкиДействующихРезервов();
	
КонецФункции

Функция ВидыРасчетовТекущейНастройкиРезерва(Организация, Резерв, Период) Экспорт
	
	Результат = ПлановыеНачисленияРезервов(Организация, Резерв, Период);
	ВидыРасчетовРезерва = Результат.ВыгрузитьКолонку("ВидРасчетаРезерва");
	
	Возврат ВидыРасчетовРезерва;
	
КонецФункции

Процедура СинхронизироватьНастройки(Объект, Организации = Неопределено) Экспорт
	
	ГоловнаяОрганизация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Организация, "ГоловнаяОрганизация");
	
	Если Объект.Организация <> ГоловнаяОрганизация Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", ГоловнаяОрганизация);
	Запрос.УстановитьПараметр("Резерв",              Объект.Резерв);
	Запрос.УстановитьПараметр("ВидРезерва",          Объект.ВидРезерва);
	Запрос.УстановитьПараметр("НачалоПериода",       Объект.НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",        Объект.КонецПериода);
	Запрос.УстановитьПараметр("ОтборПоОрганизации",  Организации);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Организации.Ссылка КАК Организация
	|ПОМЕСТИТЬ ВТ_Организации
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.ГоловнаяОрганизация = &ГоловнаяОрганизация
	|	И Организации.Ссылка <> &ГоловнаяОрганизация
	|	И &УсловиеПоОрганизации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НастройкиРасчетаРезервовПоОплатеТруда.Организация КАК Организация,
	|	НастройкиРасчетаРезервовПоОплатеТруда.ИспользоватьНастройкиГоловнойОрганизации КАК ИспользоватьНастройкиГоловнойОрганизации,
	|	НастройкиРасчетаРезервовПоОплатеТруда.Ссылка КАК НастройкиРасчетаРезервовПоОплатеТруда
	|ПОМЕСТИТЬ ВТ_Настройки
	|ИЗ
	|	Справочник.НастройкиРасчетаРезервовПоОплатеТруда КАК НастройкиРасчетаРезервовПоОплатеТруда
	|ГДЕ
	|	НастройкиРасчетаРезервовПоОплатеТруда.Резерв = &Резерв
	|	И НастройкиРасчетаРезервовПоОплатеТруда.ВидРезерва = &ВидРезерва
	|	И НастройкиРасчетаРезервовПоОплатеТруда.НачалоПериода = &НачалоПериода
	|	И НастройкиРасчетаРезервовПоОплатеТруда.КонецПериода = &КонецПериода
	|	И НастройкиРасчетаРезервовПоОплатеТруда.Организация В
	|			(ВЫБРАТЬ
	|				ВТ_Организации.Организация
	|			ИЗ
	|				ВТ_Организации)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Организации.Организация КАК Организация,
	|	ЕСТЬNULL(ВТ_Настройки.ИспользоватьНастройкиГоловнойОрганизации, ИСТИНА) КАК ИспользоватьНастройкиГоловнойОрганизации,
	|	ЕСТЬNULL(ВТ_Настройки.НастройкиРасчетаРезервовПоОплатеТруда, ЗНАЧЕНИЕ(Справочник.НастройкиРасчетаРезервовПоОплатеТруда.ПустаяСсылка)) КАК НастройкиРасчетаРезервовПоОплатеТруда
	|ИЗ
	|	ВТ_Организации КАК ВТ_Организации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Настройки КАК ВТ_Настройки
	|		ПО ВТ_Организации.Организация = ВТ_Настройки.Организация";
	
	Если ЗначениеЗаполнено(Организации) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоОрганизации", "Организации.Ссылка В (&ОтборПоОрганизации)");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоОрганизации", "Истина");
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если ЗначениеЗаполнено(Выборка.НастройкиРасчетаРезервовПоОплатеТруда) Тогда
			НастройкаПодразделения = Выборка.НастройкиРасчетаРезервовПоОплатеТруда.ПолучитьОбъект();
			
			Если Не Выборка.ИспользоватьНастройкиГоловнойОрганизации Тогда
				ИспользуемыеСвойства = "МетодНачисленияРезерваБУ, ФормироватьРезервНУ, МетодНачисленияРезерваНУ";
				ЗаполнитьЗначенияСвойств(НастройкаПодразделения, Объект, ИспользуемыеСвойства);
				НастройкаПодразделения.Записать();
				Продолжить;
			КонецЕсли;
			
		Иначе
			НастройкаПодразделения = Справочники.НастройкиРасчетаРезервовПоОплатеТруда.СоздатьЭлемент();
		КонецЕсли;
		
		Если Выборка.ИспользоватьНастройкиГоловнойОрганизации Тогда
			ИсключаемыеСвойства = "Организация, ИспользоватьНастройкиГоловнойОрганизации, ВидыРасчетовРезерва, ИсключенияИзРасчетнойБазыФондаОплатыТруда, ДополнительныеРеквизиты";
			Если ТипЗнч(Объект) = Тип("СправочникОбъект.НастройкиРасчетаРезервовПоОплатеТруда") Тогда
				ИсключаемыеСвойства = ИсключаемыеСвойства + ", Родитель, Владелец";
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(НастройкаПодразделения, Объект, , ИсключаемыеСвойства);
			
			НастройкаПодразделения.Организация = Выборка.Организация;
			НастройкаПодразделения.ИспользоватьНастройкиГоловнойОрганизации = Истина;
			
			ОписаниеОбъекта = Новый Структура();
			ОписаниеОбъекта.Вставить("Организация",   НастройкаПодразделения.Организация);
			ОписаниеОбъекта.Вставить("Резерв",        НастройкаПодразделения.Резерв);
			ОписаниеОбъекта.Вставить("НачалоПериода", НастройкаПодразделения.НачалоПериода);
			ОписаниеОбъекта.Вставить("КонецПериода",  НастройкаПодразделения.КонецПериода);
			
			НастройкаПодразделения.Наименование       = РезервыПоОплатеТруда.НаименованиеНастройки(ОписаниеОбъекта);
			НастройкаПодразделения.ПолноеНаименование = РезервыПоОплатеТруда.ПолноеНаименованиеНастройки(ОписаниеОбъекта);
			СкопироватьВидыРасчетовРезерва(НастройкаПодразделения, Объект.ВидыРасчетовРезерва);
			СкопироватьТабличнуюЧасть(НастройкаПодразделения, Объект, "ИсключенияИзРасчетнойБазыФондаОплатыТруда");
		КонецЕсли;
		
		НастройкаПодразделения.Записать();
		
		СкопироватьНазначенияПоказателей(НастройкаПодразделения);
		СкопироватьЗначенияПоказателей(НастройкаПодразделения);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СкопироватьТабличнуюЧасть(Приемник, Источник, ИмяТаблицы) Экспорт
	
	Приемник[ИмяТаблицы].Очистить();
	Для Каждого СтрокаТаблицы Из Источник[ИмяТаблицы] Цикл
		ЗаполнитьЗначенияСвойств(Приемник[ИмяТаблицы].Добавить(), СтрокаТаблицы);
	КонецЦикла;
	
КонецПроцедуры

Процедура СкопироватьВидыРасчетовРезерва(Приемник, ТаблицаИсточник) Экспорт
	
	Приемник.ВидыРасчетовРезерва.Очистить();
	
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаИсточник, "ВидРасчетаРезерва"), "ВидРасчета, ЭтоРасчетОтпуска, НазначениеПоказателя");
	
	ОписаниеСтроки = Новый Структура("НомерСтроки, ВидРасчета, ВидОтпуска, НазначениеПоказателя, ВидРасчетаРезерва");
	Для Каждого СтрокаТаблицы Из ТаблицаИсточник Цикл
		ОписаниеСтроки.НомерСтроки          = СтрокаТаблицы.НомерСтроки;
		ОписаниеСтроки.НазначениеПоказателя = ЗначенияРеквизитов[СтрокаТаблицы.ВидРасчетаРезерва].НазначениеПоказателя;
		ЭтоРасчетОтпуска = ЗначенияРеквизитов[СтрокаТаблицы.ВидРасчетаРезерва].ЭтоРасчетОтпуска;
		Если ЭтоРасчетОтпуска Тогда
			ОписаниеСтроки.ВидРасчета = "";
			ОписаниеСтроки.ВидОтпуска = ЗначенияРеквизитов[СтрокаТаблицы.ВидРасчетаРезерва].ВидРасчета;
		Иначе
			ОписаниеСтроки.ВидРасчета = ЗначенияРеквизитов[СтрокаТаблицы.ВидРасчетаРезерва].ВидРасчета;
			ОписаниеСтроки.ВидОтпуска = "";
		КонецЕсли;
		ЗаполнитьВидРасчетаРезерваПоОписанию(ОписаниеСтроки,
			Приемник.Организация,
			Приемник.Резерв,
			Приемник.ВидыРасчетовРезерва,
			ЭтоРасчетОтпуска);
			
		Если Не ЭтоРасчетОтпуска Тогда
			ИсключаемыеСвойства =  "Организация, Родитель, Владелец";
			ВидРасчетаРезерваОбъект = ОписаниеСтроки.ВидРасчетаРезерва.ПолучитьОбъект();
			ЗаполнитьЗначенияСвойств(ВидРасчетаРезерваОбъект, СтрокаТаблицы.ВидРасчетаРезерва, , ИсключаемыеСвойства);
			ВидРасчетаРезерваОбъект.Записать();
		КонецЕсли;
		
		НоваяСтрока = Приемник.ВидыРасчетовРезерва.Добавить();
		НоваяСтрока.ВидРасчетаРезерва = ОписаниеСтроки.ВидРасчетаРезерва;
	КонецЦикла;
	
КонецПроцедуры

Процедура СкопироватьНазначенияПоказателей(НастройкаРасчета) Экспорт
	
	НаборЗаписей = РегистрыСведений.НазначениеПоказателейРасчетаРезервов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Организация.Установить(НастройкаРасчета.Организация);
	НаборЗаписей.Отбор.ПараметрыРасчетаРезерва.Установить(НастройкаРасчета.Ссылка);
	НаборЗаписей.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", НастройкаРасчета.Организация);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПодразделенияОрганизаций.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТ_ВладельцыВидаРасчета
	|ИЗ
	|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
	|ГДЕ
	|	ПодразделенияОрганизаций.Владелец = &Организация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ШтатноеРасписание.Ссылка
	|ИЗ
	|	Справочник.ШтатноеРасписание КАК ШтатноеРасписание
	|ГДЕ
	|	ШтатноеРасписание.Владелец = &Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НазначениеПоказателейРасчетаРезервов.ВидРасчетаРезерва КАК ВидРасчетаРезерва,
	|	НазначениеПоказателейРасчетаРезервов.ВладелецВидаРасчета КАК ВладелецВидаРасчета
	|ИЗ
	|	РегистрСведений.НазначениеПоказателейРасчетаРезервов КАК НазначениеПоказателейРасчетаРезервов
	|ГДЕ
	|	НазначениеПоказателейРасчетаРезервов.ВладелецВидаРасчета В
	|			(ВЫБРАТЬ
	|				ВТ_ВладельцыВидаРасчета.Ссылка
	|			ИЗ
	|				ВТ_ВладельцыВидаРасчета)
	|
	|СГРУППИРОВАТЬ ПО
	|	НазначениеПоказателейРасчетаРезервов.ВидРасчетаРезерва,
	|	НазначениеПоказателейРасчетаРезервов.ВладелецВидаРасчета";
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	Если РезультатЗапроса.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	МассивЗначений = ОбщегоНазначения.ВыгрузитьКолонку(РезультатЗапроса, "ВидРасчетаРезерва", Истина);
	Отбор = Новый Структура("ВидРасчетаРезерва");
	Для Каждого ЗначениеМассива Из МассивЗначений Цикл
		Отбор.ВидРасчетаРезерва = ЗначениеМассива;
		МассивСтрок = РезультатЗапроса.НайтиСтроки(Отбор);
		Для Каждого СтрокаМассива Из МассивСтрок Цикл
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.Организация             = НастройкаРасчета.Организация;
			НоваяЗапись.ПараметрыРасчетаРезерва = НастройкаРасчета.Ссылка;
			НоваяЗапись.ВидРасчетаРезерва       = СтрокаМассива.ВидРасчетаРезерва;
			НоваяЗапись.ВладелецВидаРасчета     = СтрокаМассива.ВладелецВидаРасчета;
		КонецЦикла;
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

Процедура СкопироватьЗначенияПоказателей(НастройкаРасчета) Экспорт
	
	ГоловнаяОрганизация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НастройкаРасчета.Организация, "ГоловнаяОрганизация");
	
	НаборЗаписей = РегистрыСведений.ЗначенияПоказателейРасчетаРезервов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Организация.Установить(НастройкаРасчета.Организация);
	НаборЗаписей.Отбор.ПараметрыРасчетаРезерва.Установить(НастройкаРасчета.Ссылка);
	НаборЗаписей.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",         НастройкаРасчета.Организация);
	Запрос.УстановитьПараметр("ТекущаяНастройка",    НастройкаРасчета.Ссылка);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", ГоловнаяОрганизация);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПодразделенияОрганизаций.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТ_ВладельцыВидаРасчета
	|ИЗ
	|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
	|ГДЕ
	|	ПодразделенияОрганизаций.Владелец = &Организация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ШтатноеРасписание.Ссылка
	|ИЗ
	|	Справочник.ШтатноеРасписание КАК ШтатноеРасписание
	|ГДЕ
	|	ШтатноеРасписание.Владелец = &Организация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Организации.Ссылка
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.Ссылка = &ГоловнаяОрганизация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗначенияПоказателейРасчетаРезервов.ВидРасчетаРезерва КАК ВидРасчетаРезерва,
	|	ЗначенияПоказателейРасчетаРезервов.ВладелецВидаРасчета КАК ВладелецВидаРасчета,
	|	ЗначенияПоказателейРасчетаРезервов.ПоказательРасчета КАК ПоказательРасчета,
	|	ЗначенияПоказателейРасчетаРезервов.Значение КАК Значение,
	|	ВидыРасчетовРезервовПоОплатеТруда.Резерв КАК Резерв,
	|	ВидыРасчетовРезервовПоОплатеТруда.ВидРасчета КАК ВидРасчета,
	|	ВидыРасчетовРезервовПоОплатеТруда.НазначениеПоказателя КАК НазначениеПоказателя,
	|	ВидыРасчетовРезервовПоОплатеТруда.ЭтоРасчетОтпуска КАК ЭтоРасчетОтпуска,
	|	ВидыРасчетовРезервовПоОплатеТруда.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.ЗначенияПоказателейРасчетаРезервов КАК ЗначенияПоказателейРасчетаРезервов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыРасчетовРезервовПоОплатеТруда КАК ВидыРасчетовРезервовПоОплатеТруда
	|		ПО ЗначенияПоказателейРасчетаРезервов.ВидРасчетаРезерва = ВидыРасчетовРезервовПоОплатеТруда.Ссылка
	|ГДЕ
	|	ЗначенияПоказателейРасчетаРезервов.ВладелецВидаРасчета В
	|			(ВЫБРАТЬ
	|				ВТ_ВладельцыВидаРасчета.Ссылка
	|			ИЗ
	|				ВТ_ВладельцыВидаРасчета)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыРасчетовРезервовПоОплатеТруда.Организация КАК Организация,
	|	ВидыРасчетовРезервовПоОплатеТруда.Резерв КАК Резерв,
	|	ВидыРасчетовРезервовПоОплатеТруда.ВидРасчета КАК ВидРасчета,
	|	ВидыРасчетовРезервовПоОплатеТруда.НазначениеПоказателя КАК НазначениеПоказателя,
	|	ВидыРасчетовРезервовПоОплатеТруда.ЭтоРасчетОтпуска КАК ЭтоРасчетОтпуска,
	|	НастройкиРасчетаРезервовПоОплатеТрудаВидыРасчетовРезерва.ВидРасчетаРезерва КАК ВидРасчетаРезерва
	|ИЗ
	|	Справочник.НастройкиРасчетаРезервовПоОплатеТруда.ВидыРасчетовРезерва КАК НастройкиРасчетаРезервовПоОплатеТрудаВидыРасчетовРезерва
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыРасчетовРезервовПоОплатеТруда КАК ВидыРасчетовРезервовПоОплатеТруда
	|		ПО НастройкиРасчетаРезервовПоОплатеТрудаВидыРасчетовРезерва.ВидРасчетаРезерва = ВидыРасчетовРезервовПоОплатеТруда.Ссылка
	|ГДЕ
	|	НастройкиРасчетаРезервовПоОплатеТрудаВидыРасчетовРезерва.Ссылка = &ТекущаяНастройка";
	
	Результат = Запрос.ВыполнитьПакет();
	РезультатЗапросаЗначений     = Результат[1].Выгрузить();
	РезультатЗапросаВидовРасчета = Результат[2].Выгрузить();
	Если РезультатЗапросаЗначений.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	МассивЗначений = ОбщегоНазначения.ВыгрузитьКолонку(РезультатЗапросаЗначений, "ВидРасчетаРезерва", Истина);
	Отбор = Новый Структура("ВидРасчетаРезерва");
	Для Каждого ЗначениеМассива Из МассивЗначений Цикл
		Отбор.ВидРасчетаРезерва = ЗначениеМассива;
		МассивСтрок = РезультатЗапросаЗначений.НайтиСтроки(Отбор);
		Для Каждого СтрокаМассива Из МассивСтрок Цикл
			ОтборПодчиненныхСтрок = Новый Структура("Резерв, ВидРасчета, НазначениеПоказателя, ЭтоРасчетОтпуска");
			ЗаполнитьЗначенияСвойств(ОтборПодчиненныхСтрок, СтрокаМассива);
			МассивПодчиненныхСтрок = РезультатЗапросаВидовРасчета.НайтиСтроки(ОтборПодчиненныхСтрок);
			Для Каждого СтрокаМассиваПодчиненныхСтрок Из МассивПодчиненныхСтрок Цикл
				
				НоваяЗапись = НаборЗаписей.Добавить();
				НоваяЗапись.Организация             = НастройкаРасчета.Организация;
				НоваяЗапись.ПараметрыРасчетаРезерва = НастройкаРасчета.Ссылка;
				НоваяЗапись.ВидРасчетаРезерва       = СтрокаМассиваПодчиненныхСтрок.ВидРасчетаРезерва;
				НоваяЗапись.ПоказательРасчета       = СтрокаМассива.ПоказательРасчета;
				НоваяЗапись.Значение                = СтрокаМассива.Значение;
				НоваяЗапись.ВладелецВидаРасчета     = ?(ТипЗнч(СтрокаМассива.ВладелецВидаРасчета) = Тип("СправочникСсылка.Организации"),
															НастройкаРасчета.Организация, СтрокаМассива.ВладелецВидаРасчета);
				КонецЦикла;
			КонецЦикла;
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

Процедура ЗаполнитьВидРасчетаРезерваПоОписанию(СтрокаТаблицы, Организация, Резерв, ВидыРасчетовРезерва, ЭтоРасчетОтпуска) Экспорт
	
	Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ВидРасчета)
		И НЕ ЗначениеЗаполнено(СтрокаТаблицы.ВидОтпуска) Тогда
		Возврат;
	КонецЕсли;
	
	НазначениеПоказателя = ?(ЗначениеЗаполнено(СтрокаТаблицы.НазначениеПоказателя),
		СтрокаТаблицы.НазначениеПоказателя,
		Перечисления.НазначенияПоказателейРасчетаРезервовПоОплатеТруда.ДляОрганизации);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",          Организация);
	Запрос.УстановитьПараметр("Резерв",               Резерв);
	Запрос.УстановитьПараметр("ВидРасчета",           ?(ЭтоРасчетОтпуска, СтрокаТаблицы.ВидОтпуска, СтрокаТаблицы.ВидРасчета));
	Запрос.УстановитьПараметр("НазначениеПоказателя", НазначениеПоказателя);
	Запрос.УстановитьПараметр("ЭтоРасчетОтпуска",     ЭтоРасчетОтпуска);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВидыРасчетовРезервовПоОплатеТруда.Ссылка КАК Ссылка,
	|	ВидыРасчетовРезервовПоОплатеТруда.ВидРасчета КАК ВидРасчета,
	|	ВидыРасчетовРезервовПоОплатеТруда.НазначениеПоказателя КАК НазначениеПоказателя,
	|	ВидыРасчетовРезервовПоОплатеТруда.ИспользоватьСобственнуюФормулуРасчета КАК ИспользоватьСобственнуюФормулуРасчета
	|ИЗ
	|	Справочник.ВидыРасчетовРезервовПоОплатеТруда КАК ВидыРасчетовРезервовПоОплатеТруда
	|ГДЕ
	|	ВидыРасчетовРезервовПоОплатеТруда.Организация = &Организация
	|	И ВидыРасчетовРезервовПоОплатеТруда.Резерв = &Резерв
	|	И ВидыРасчетовРезервовПоОплатеТруда.ВидРасчета = &ВидРасчета
	|	И ВидыРасчетовРезервовПоОплатеТруда.ЭтоРасчетОтпуска = &ЭтоРасчетОтпуска
	|	И НЕ ВидыРасчетовРезервовПоОплатеТруда.ПометкаУдаления
	|	И ВидыРасчетовРезервовПоОплатеТруда.НазначениеПоказателя = &НазначениеПоказателя";
	
	Результат = Запрос.Выполнить();
	
	СоздатьНовыйЭлемент = Ложь;
	Если Результат.Пустой() Тогда
		СоздатьНовыйЭлемент = Истина;
	Иначе
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			ВидРасчетаРезерва = Выборка.Ссылка;
			Если СоздатьНовыйЭлемент Тогда
				Прервать;
			КонецЕсли;
			МассивСтрок = ВидыРасчетовРезерва.НайтиСтроки(Новый Структура("ВидРасчетаРезерва", Выборка.Ссылка));
			Для Каждого СтрокаМассива Из МассивСтрок Цикл
				Если СтрокаМассива.НомерСтроки <> СтрокаТаблицы.НомерСтроки Тогда
					СоздатьНовыйЭлемент = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	Если СоздатьНовыйЭлемент Тогда
		ВидРасчетаРезерва = НовыйВидРасчетаРезерва(?(ЭтоРасчетОтпуска,СтрокаТаблицы.ВидОтпуска, СтрокаТаблицы.ВидРасчета),
			СтрокаТаблицы.НазначениеПоказателя,
			Организация,
			Резерв,
			ЭтоРасчетОтпуска);
	КонецЕсли;
	
	СтрокаТаблицы.ВидРасчетаРезерва = ВидРасчетаРезерва;
	
КонецПроцедуры

Функция НовыйВидРасчетаРезерва(ВидРасчета, НазначениеПоказателя, Организация, Резерв, ЭтоРасчетОтпуска) Экспорт
	
	ОписаниеЭлемента = Новый Структура();
	ОписаниеЭлемента.Вставить("Организация",          Организация);
	ОписаниеЭлемента.Вставить("Резерв",               Резерв);
	ОписаниеЭлемента.Вставить("ВидРасчета",           ВидРасчета);
	ОписаниеЭлемента.Вставить("НазначениеПоказателя", НазначениеПоказателя);
	ОписаниеЭлемента.Вставить("ЭтоРасчетОтпуска",     ЭтоРасчетОтпуска);
	
	ВидРасчетаРезерва = Справочники.ВидыРасчетовРезервовПоОплатеТруда.СоздатьЭлементПоОписанию(ОписаниеЭлемента);
	
	Возврат ВидРасчетаРезерва;
	
КонецФункции

#КонецОбласти

#КонецЕсли