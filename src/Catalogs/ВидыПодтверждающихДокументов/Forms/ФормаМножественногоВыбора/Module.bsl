
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ОбновитьСписокВидовДокументов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиЭлементовФормы

&НаКлиенте
Процедура СписокВидовДокументовПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВидовДокументовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	ОткрытьФорму("Справочник.ВидыПодтверждающихДокументов.ФормаОбъекта",,Элементы.СписокВидовДокументов,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВидовДокументовОбработкаЗаписиНового(НовыйОбъект, Источник, СтандартнаяОбработка)
	
	ОбновитьСписокВидовДокументов(НовыйОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокВидовДокументов(ТекущийВидДокумента = Неопределено)
	
	Если ТекущийВидДокумента = Неопределено Тогда
		ТекущаяСтрока = Элементы.СписокВидовДокументов.ТекущаяСтрока;
		Если ТекущаяСтрока <> Неопределено Тогда
			ТекущиеДанные = СписокВидовДокументов.НайтиПоИдентификатору(ТекущаяСтрока);
			Если ТекущиеДанные <> Неопределено Тогда
				ТекущийВидДокумента = ТекущиеДанные.ВидДокумента;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВидыДокументов.Ссылка,
	|	ВидыДокументов.ПодтверждаетИсполнениеКонтракта
	|ИЗ
	|	Справочник.ВидыПодтверждающихДокументов КАК ВидыДокументов
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВидыДокументов.Код";
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Флаги = СписокВидовДокументов.Выгрузить();
	СписокВидовДокументов.Очистить();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока									= СписокВидовДокументов.Добавить();
		НоваяСтрока.ВидДокумента					= Выборка.Ссылка;
		НоваяСтрока.ПодтверждаетИсполнениеКонтракта = Выборка.ПодтверждаетИсполнениеКонтракта;
		
		ПредыдущаяСтрока							= Флаги.Найти(НоваяСтрока.ВидДокумента, "ВидДокумента");
		НоваяСтрока.ФлагВыбора						= ПредыдущаяСтрока <> Неопределено И ПредыдущаяСтрока.ФлагВыбора;
		Если Выборка.Ссылка = ТекущийВидДокумента Тогда
			Элементы.СписокВидовДокументов.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	
	Результат = Новый Массив;
	Для Каждого СтрокаДокументов Из СписокВидовДокументов Цикл
		Если СтрокаДокументов.ФлагВыбора Тогда
			Результат.Добавить(СтрокаДокументов.ВидДокумента);
		КонецЕсли;
	КонецЦикла;
	
	ОповеститьОВыборе(Результат);
	
КонецПроцедуры

#КонецОбласти