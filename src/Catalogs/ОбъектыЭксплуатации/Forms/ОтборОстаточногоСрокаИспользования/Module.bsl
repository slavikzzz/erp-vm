
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ОтборОстаточныйСрокИспользования = Параметры.ОтборОстаточныйСрокИспользования;
	ОтборОстаточныйСрокИспользованияВидУчета = Параметры.ОтборОстаточныйСрокИспользованияВидУчета;
	
	Если ПустаяСтрока(ОтборОстаточныйСрокИспользованияВидУчета) Тогда
		ОтборОстаточныйСрокИспользованияВидУчета = Элементы.ОтборОстаточныйСрокИспользованияВидУчета.СписокВыбора[0];
	КонецЕсли;
	
	Если НастройкиНалоговУчетныхПолитикПовтИсп.РеглУчетВНАВедетсяНезависимо() Тогда
		СписокВидовУчета = Элементы.ОтборОстаточныйСрокИспользованияВидУчета.СписокВыбора;
		СписокВидовУчета.Добавить("БУ", НСтр("ru = 'БУ';
											|en = 'AC'"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Установить(Команда)
	
	ПараметрыЗакрытия = ПолучитьПараметрыЗакрытияФормы();
	ЗаполнитьЗначенияСвойств(ПараметрыЗакрытия, ЭтотОбъект);
	
	Закрыть(ПараметрыЗакрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура Сбросить(Команда)
	
	ПараметрыЗакрытия = ПолучитьПараметрыЗакрытияФормы();
	
	Закрыть(ПараметрыЗакрытия);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьПараметрыЗакрытияФормы()

	ПараметрыЗакрытия = Новый Структура;
	
	ПараметрыЗакрытия.Вставить("ОтборОстаточныйСрокИспользования",         0);
	ПараметрыЗакрытия.Вставить("ОтборОстаточныйСрокИспользованияВидУчета", "");
	
	Возврат ПараметрыЗакрытия;

КонецФункции

#КонецОбласти
