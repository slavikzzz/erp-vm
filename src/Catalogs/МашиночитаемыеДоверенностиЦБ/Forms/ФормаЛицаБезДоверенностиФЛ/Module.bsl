
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураДанных = Параметры.СтруктураДанных;
	
	Если СтруктураДанных.Свойство("ТолькоПросмотрФормы") И СтруктураДанных.ТолькоПросмотрФормы Тогда
		Элементы.ЛицоБезДовФЛ_СНИЛС.ТолькоПросмотр 			= Истина;
		Элементы.ЛицоБезДовФЛ_Гражданство.ТолькоПросмотр 	= Истина;
		Элементы.ЛицоБезДовФЛ_Должность.ТолькоПросмотр 		= Истина;
		Элементы.ФормаКнопкаСохранить.Доступность 			= Ложь;
	КонецЕсли;
	
	Если СтруктураДанных <> Неопределено Тогда
		ЛицоБезДовФЛ_СНИЛС 			= СтруктураДанных.ЛицоБезДовФЛ_СНИЛС;
		ЛицоБезДовФЛ_Гражданство 	= СтруктураДанных.ЛицоБезДовФЛ_Гражданство;
		ЛицоБезДовФЛ_Должность 		= СтруктураДанных.ЛицоБезДовФЛ_Должность;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)
	
	Если НЕ СохранениеВозможно() Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("ЛицоБезДовФЛ_СНИЛС", 			ЛицоБезДовФЛ_СНИЛС);
	СтруктураДанных.Вставить("ЛицоБезДовФЛ_Гражданство", 	ЛицоБезДовФЛ_Гражданство);
	СтруктураДанных.Вставить("ЛицоБезДовФЛ_Должность", 		ЛицоБезДовФЛ_Должность);
	
	Закрыть(СтруктураДанных);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция СохранениеВозможно()
	
	Отказ = Ложь;
	
	ОчиститьСообщения();
	
	Если НЕ ЗначениеЗаполнено(ЛицоБезДовФЛ_СНИЛС) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не задан СНИЛС.';
														|en = 'Не задан СНИЛС.'"),,
			"ЛицоБезДовФЛ_СНИЛС",, Отказ);
	ИначеЕсли СтрДлина(ЛицоБезДовФЛ_СНИЛС) <> 14 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Указан некорректный СНИЛС.';
														|en = 'Указан некорректный СНИЛС.'"),,
			"ЛицоБезДовФЛ_СНИЛС",, Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ЛицоБезДовФЛ_Должность) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не задана должность.';
														|en = 'Не задана должность.'"),,
			"ЛицоБезДовФЛ_Должность",, Отказ);
	КонецЕсли;
	
	Возврат НЕ Отказ;
	
КонецФункции

#КонецОбласти
