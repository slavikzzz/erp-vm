#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Заголовок = ДокументооборотСКОКлиентСервер.ЗаменитьПФРиФССнаСФР(Заголовок, Истина);
	
	МестоХраненияКлюча = ЭлектроннаяПодписьВМоделиСервисаБРОВызовСервера.ОпределитьМестоХраненияКлюча();
	
	БПИНедоступен = (СокрЛП(Объект.Код) = "ЕЦП");
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	КриптографияЭДКОКлиент.ОтобразитьПредставлениеСертификата(
		МестоХраненияКлюча, 
		Элементы.Сертификат, 
		Объект.Сертификаты,
		ЭтотОбъект,
		"СертификатПредставление");
		
	КриптографияЭДКОКлиент.ОтобразитьПредставлениеСертификата(
		МестоХраненияКлюча, 
		Элементы.СертификатЭДОК, 
		Объект.СертификатыЭДОК,
		ЭтотОбъект,
		"СертификатЭДОКПредставление");
	
КонецПроцедуры
	
&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_ОрганыПФР", , Объект.Ссылка);
	
КонецПроцедуры
	
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура БПИНедоступенПриИзменении(Элемент)
	
	Если БПИНедоступен Тогда
		КодБПИ = Объект.Код;
		Объект.Код = "ЕЦП";
	ИначеЕсли СокрЛП(Объект.Код) = "ЕЦП" Тогда
		Объект.Код = КодБПИ;
	КонецЕсли;
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения(
		"СертификатНачалоВыбораЗавершение", ЭтотОбъект, Новый Структура("Элемент", Элемент));

	КриптографияЭДКОКлиент.ВыбратьСертификат(
		Оповещение, МестоХраненияКлюча, Объект.Сертификаты, "AddressBook",, Истина);
		
КонецПроцедуры

&НаКлиенте
Процедура СертификатНачалоВыбораЗавершение(Результат, ВходящийКонтекст) Экспорт
	
	Элемент = ВходящийКонтекст.Элемент;
	
	Если Результат.Выполнено Тогда
		
		Объект.Сертификаты.Очистить();
		Если ТипЗнч(Результат.ВыбранноеЗначение) = Тип("Структура") Тогда
			НовСтр = Объект.Сертификаты.Добавить();
			НовСтр.Сертификат = Результат.ВыбранноеЗначение.Отпечаток;
		Иначе
			Для Каждого Стр Из Результат.ВыбранноеЗначение Цикл
				НовСтр = Объект.Сертификаты.Добавить();
				НовСтр.Сертификат = Стр.Отпечаток;
			КонецЦикла;
		КонецЕсли;
		
		КриптографияЭДКОКлиент.ОтобразитьПредставлениеСертификата(
			МестоХраненияКлюча, 
			Элемент, 
			Объект.Сертификаты,
			ЭтотОбъект,
			"СертификатПредставление");

		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Объект.Сертификаты.Очистить();
	КриптографияЭДКОКлиент.ОтобразитьПредставлениеСертификата(
		МестоХраненияКлюча, 
		Элемент, 
		Объект.Сертификаты,
		ЭтотОбъект,
		"СертификатПредставление");
		
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Сертификаты = Новый Массив;
	Для Каждого СтрокаТаблицы Из Объект.Сертификаты Цикл
		НовыйСертификат = Новый Структура("Отпечаток", СтрокаТаблицы.Сертификат);
		КриптографияЭДКОКлиентСервер.КонтекстМоделиХраненияКлюча(МестоХраненияКлюча, НовыйСертификат);
		Сертификаты.Добавить(НовыйСертификат);
	КонецЦикла;
	
	КриптографияЭДКОКлиент.ПоказатьСертификат(Сертификаты);
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатЭДОКНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения(
		"СертификатЭДОКНачалоВыбораЗавершение", ЭтотОбъект, Новый Структура("Элемент", Элемент));

	КриптографияЭДКОКлиент.ВыбратьСертификат(
		Оповещение, МестоХраненияКлюча, Объект.СертификатыЭДОК, "AddressBook",, Истина);
		
КонецПроцедуры

&НаКлиенте
Процедура СертификатЭДОКНачалоВыбораЗавершение(Результат, ВходящийКонтекст) Экспорт
	
	Элемент = ВходящийКонтекст.Элемент;
	
	Если Результат.Выполнено Тогда
		
		Объект.СертификатыЭДОК.Очистить();
		Если ТипЗнч(Результат.ВыбранноеЗначение) = Тип("Структура") Тогда
			НовСтр = Объект.СертификатыЭДОК.Добавить();
			НовСтр.Сертификат = Результат.ВыбранноеЗначение.Отпечаток;
		Иначе
			Для Каждого Стр Из Результат.ВыбранноеЗначение Цикл
				НовСтр = Объект.СертификатыЭДОК.Добавить();
				НовСтр.Сертификат = Стр.Отпечаток;
			КонецЦикла;
		КонецЕсли;
		
		КриптографияЭДКОКлиент.ОтобразитьПредставлениеСертификата(
			МестоХраненияКлюча, 
			Элемент, 
			Объект.СертификатыЭДОК,
			ЭтотОбъект,
			"СертификатЭДОКПредставление");

		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатЭДОКОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Объект.СертификатыЭДОК.Очистить();
	КриптографияЭДКОКлиент.ОтобразитьПредставлениеСертификата(
		МестоХраненияКлюча, 
		Элемент, 
		Объект.СертификатыЭДОК,
		ЭтотОбъект,
		"СертификатЭДОКПредставление");
		
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатЭДОКОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Сертификаты = Новый Массив;
	Для Каждого СтрокаТаблицы Из Объект.СертификатыЭДОК Цикл
		НовыйСертификат = Новый Структура("Отпечаток", СтрокаТаблицы.Сертификат);
		КриптографияЭДКОКлиентСервер.КонтекстМоделиХраненияКлюча(МестоХраненияКлюча, НовыйСертификат);
		Сертификаты.Добавить(НовыйСертификат);
	КонецЦикла;
	
	КриптографияЭДКОКлиент.ПоказатьСертификат(Сертификаты);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Форма.Элементы.Код.Видимость 											= НЕ Форма.БПИНедоступен;
	Форма.Элементы.КодБПИ.Видимость 										= Форма.БПИНедоступен;
	Форма.Элементы.ОтправкаМакетовПенсионныхДел.Доступность 				= НЕ Форма.БПИНедоступен;
	Форма.Элементы.ПринимаетЗаявленияОНазначенииИДоставкеПенсии.Доступность = НЕ Форма.БПИНедоступен;
	Форма.Элементы.ГруппаСертификат.Доступность 							= НЕ Форма.БПИНедоступен;
	
КонецПроцедуры

#КонецОбласти
