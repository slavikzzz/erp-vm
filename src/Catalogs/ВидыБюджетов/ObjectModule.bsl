#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроверкаЗначенийРеквизитовПередЗаписью();
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
		
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ОтсутствуетВДанныхЗаполнения(ДанныеЗаполнения, "Владелец") Тогда
		Владелец = Справочники.МоделиБюджетирования.МодельБюджетированияПоУмолчанию();
	КонецЕсли;
	
	Если Не ЭтоГруппа Тогда
		
		Если ОтсутствуетВДанныхЗаполнения(ДанныеЗаполнения, "Периодичность") Тогда
			Периодичность = Перечисления.Периодичность.Месяц;
		КонецЕсли;
		
		Если ОтсутствуетВДанныхЗаполнения(ДанныеЗаполнения, "СпособПланирования") Тогда
			СпособПланирования = Перечисления.СпособыСоставленияБюджетов.НаФиксированныйПериод;
		КонецЕсли;
		
		Если ОтсутствуетВДанныхЗаполнения(ДанныеЗаполнения, "ВариантРасположенияГраницыФактическихДанных") Тогда
			ВариантРасположенияГраницыФактическихДанных = Перечисления.ВариантыРасположенияГраницыФактическиДанных.ВнутриПериодаБюджета;
		КонецЕсли;
		
		Если ОтсутствуетВДанныхЗаполнения(ДанныеЗаполнения, "КоличествоПериодовСкользящегоБюджета") Тогда
			КоличествоПериодовСкользящегоБюджета = 1;
		КонецЕсли;
		
		Если ОтсутствуетВДанныхЗаполнения(ДанныеЗаполнения, "СмещениеГраницыФакта") Тогда
			СмещениеГраницыФакта = 1;
		КонецЕсли;
		
		Если ОтсутствуетВДанныхЗаполнения(ДанныеЗаполнения, "ПериодичностьГраницыФакта") Тогда
			ПериодичностьГраницыФакта = Перечисления.Периодичность.Месяц;
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если Не Справочники.ГруппыДоступаВидовБюджетов.ИспользуютсяГруппыДоступа() Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ГруппаДоступа");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверкаЗначенийРеквизитовПередЗаписью()
	
	Если НЕ ЭтоГруппа
		И НЕ СпособПланирования = Перечисления.СпособыСоставленияБюджетов.Скользящий Тогда
		КоличествоПериодовСкользящегоБюджета = 1;
	КонецЕсли;
	
КонецПроцедуры

Функция ОтсутствуетВДанныхЗаполнения(ДанныеЗаполнения, ИмяРеквизита)
	Перем Значение;
	
	Если ДанныеЗаполнения = Неопределено Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если Не ДанныеЗаполнения.Свойство(ИмяРеквизита, Значение)
		ИЛИ Значение = Неопределено Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#КонецЕсли