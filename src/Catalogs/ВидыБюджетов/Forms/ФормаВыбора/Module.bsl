#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ЕстьМодельВПараметрах = Ложь;
	ЗначениеПараметра = Неопределено;
	Если Параметры.Свойство("Отбор", ЗначениеПараметра) Тогда
		Если ЗначениеПараметра.Свойство("Владелец", ЗначениеПараметра) Тогда
			ЕстьМодельВПараметрах = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если ЕстьМодельВПараметрах Тогда
		МодельБюджетирования = ЗначениеПараметра;
	Иначе
		МодельБюджетирования = МодельБюджетированияПоУмолчанию();
	КонецЕсли;
	
	Элементы.МодельБюджетирования.Видимость = Не ЕстьМодельВПараметрах;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список, "Владелец", МодельБюджетирования,,, ЗначениеЗаполнено(МодельБюджетирования));
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура МодельБюджетированияПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список, "Владелец", МодельБюджетирования,,, ЗначениеЗаполнено(МодельБюджетирования));
	ЗаписатьВыбраннуюМодельБюджетирования(МодельБюджетирования);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)

	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция МодельБюджетированияПоУмолчанию()
	
	МодельБюджетирования = ХранилищеНастроекДанныхФорм.Загрузить("Справочник.ВидыБюджетов.Форма.ФормаВыбора", "МодельБюджетирования");
	Если МодельБюджетирования = Неопределено Тогда
		МодельБюджетирования = Справочники.МоделиБюджетирования.ПустаяСсылка();
	КонецЕсли;
	Возврат МодельБюджетирования;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ЗаписатьВыбраннуюМодельБюджетирования(Модель)
	ХранилищеНастроекДанныхФорм.Сохранить("Справочник.ВидыБюджетов.Форма.ФормаВыбора", "МодельБюджетирования", Модель);
КонецПроцедуры

#КонецОбласти

