#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	ЗаполнитьНаименованиеСлужебное();
	
	ЗапомнитьРеквизитыПрежнегоСостоянияОбъекта();
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(ДанныеЗаполнения) Тогда 
		ДанныеЗаполненияНаходитсяПодОпекой = Ложь;
		Если ДанныеЗаполнения.Свойство("НаходитсяПодОпекой", ДанныеЗаполненияНаходитсяПодОпекой) Тогда
			СкрыватьВТ2 = ДанныеЗаполненияНаходитсяПодОпекой;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗаполнитьНаименованиеСлужебное() Экспорт
	Если Не ЭтоГруппа Тогда
		НаименованиеСлужебное = ФизическиеЛицаЗарплатаКадры.НаименованиеСлужебное(Наименование);
	КонецЕсли;
КонецПроцедуры

Процедура ЗаполнитьСтепеньРодства() Экспорт
	Если Не ЭтоГруппа И Не ЗначениеЗаполнено(СтепеньРодства) И ЗначениеЗаполнено(КодСвязи) Тогда
		СтепеньРодства = СтепеньРодства(Наименование, КодСвязи);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Определение степени родства "с" родственником по связи "от" родственника.
Функция СтепеньРодства(ФИО, КодСвязи)
	Если КодСвязи = "38" Или КодСвязи = "39" Тогда // Отец, Мать.
		Инициалы = СтрРазделить(ФИО, " ", Ложь);
		Если Инициалы.Количество() > 2 Тогда
			ЧастиФИО = ФизическиеЛицаЗарплатаКадрыКлиентСервер.ПустыеЧастиФИО();
			ЧастиФИО.Фамилия = Инициалы[0];
			ЧастиФИО.Имя = Инициалы[1];
			ЧастиФИО.Отчество = Инициалы[2];
			Пол = ФизическиеЛицаЗарплатаКадрыКлиентСервер.ПолПоЧастямФИО(ЧастиФИО);
			Если Пол = Перечисления.ПолФизическогоЛица.Мужской Тогда
				Возврат Справочники.СтепениРодстваФизическихЛиц.НайтиПоКоду("05"); // Сын.
			ИначеЕсли Пол = Перечисления.ПолФизическогоЛица.Женский Тогда
				Возврат Справочники.СтепениРодстваФизическихЛиц.НайтиПоКоду("06"); // Дочь.
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

Процедура ЗапомнитьРеквизитыПрежнегоСостоянияОбъекта()

	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоНовый() Тогда
		Возврат;
	КонецЕсли;
	
	ИменаРеквизитов = ОбщегоНазначения.ВыгрузитьКолонку(Метаданные.Справочники.РодственникиФизическихЛиц.Реквизиты, "Имя");
	ПрежниеЗначения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, СтрСоединить(ИменаРеквизитов, ","));
	
	ДополнительныеСвойства.Вставить("ПрежниеЗначения", ПрежниеЗначения);
	
КонецПроцедуры

#КонецОбласти



#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли