#Область ОписаниеПеременных

&НаКлиенте
Перем ПараметрыОбработчикаОжидания;

&НаКлиенте
Перем ФормаДлительнойОперации;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	СформироватьЗаголовокРеквизиты();
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура РазрешитьРедактирование(Команда)

	Закрыть(Истина);

КонецПроцедуры

&НаКлиенте
Процедура ПроверитьИспользованиеОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовУТКлиент.ПроверитьИспользованиеОбъекта(ЭтаФорма, ПараметрыОбработчикаОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеЗадания()
	
	ЗапретРедактированияРеквизитовОбъектовУТКлиент.ПроверитьВыполнениеЗадания(
		ЭтаФорма,
		ФормаДлительнойОперации,
		ПараметрыОбработчикаОжидания);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьЗаголовокРеквизиты()
	
	РеквизитыОбъекта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.Объект, "ХозяйственнаяОперация, ТипДоговора");
	
	ТекстПартнер = ПартнерыИКонтрагенты.ЗаголовокРеквизитаПартнерВЗависимостиОтХозяйственнойОперации(РеквизитыОбъекта.ХозяйственнаяОперация);
	
	ИспользоватьВариантОформленияЗакупок = ПолучитьФункциональнуюОпцию("ИспользоватьТоварыВПутиОтПоставщиков")
											Или ПолучитьФункциональнуюОпцию("ИспользоватьНеотфактурованныеПоставки");
	
	Если ИспользоватьВариантОформленияЗакупок Тогда
		ТекстЗаголовок = НСтр("ru = 'Тип взаимоотношений, %1, Контрагент, Организация,
			|Порядок оплаты, Валюта, Детализация расчетов, Оформление раздельной закупки';
			|en = 'Relationship type, %1, Counterparty, Company,
			|Payment procedure, Currency, AR/AP item, Separate purchase registration'");
	Иначе
		ТекстЗаголовок = НСтр("ru = 'Тип взаимоотношений, %1, Контрагент, Организация,
			|Порядок оплаты, Валюта, Детализация расчетов';
			|en = 'Relationship type, %1, Counterparty, Company,
			|Payment procedure, Currency, AR/AP item'");
	КонецЕсли;
	
	ИспользоватьПриемкуТоваровНаСклад	= (РеквизитыОбъекта.ТипДоговора = Перечисления.ТипыДоговоров.СПоставщиком
												Или РеквизитыОбъекта.ТипДоговора = Перечисления.ТипыДоговоров.ВвозИзЕАЭС
												Или РеквизитыОбъекта.ТипДоговора = Перечисления.ТипыДоговоров.Импорт
												Или РеквизитыОбъекта.ТипДоговора = Перечисления.ТипыДоговоров.СПоклажедателем)
											И (ИспользоватьВариантОформленияЗакупок
												Или ПолучитьФункциональнуюОпцию("ИспользоватьОрдернуюСхемуПриПоступлении"));
	
	ТекстЗаголовок = ТекстЗаголовок + ?(ИспользоватьПриемкуТоваровНаСклад,
										"," + Символы.НПП + НСтр("ru = 'Приемка товаров на склад';
																|en = 'Goods receipt at warehouse'"),
										"");
	
	Элементы.ЗаголовокРеквизиты.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстЗаголовок, ТекстПартнер);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
