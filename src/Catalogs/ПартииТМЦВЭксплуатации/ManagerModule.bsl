#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполняет реквизит "Партия" в табличной части.
// При необходимости создаются новые элементы справочника партий.
//
// Параметры:
//	Объект - ДокументОбъект.ВнутреннееПотребление, ДокументОбъект.ВводОстатков, ДокументОбъект.ВводОстатковТМЦВЭксплуатации - Документ, в котором заполняется партия.
//  РежимЗаписи - РежимЗаписиДокумента - Текущий режим записи документа.
//
Процедура ЗаполнитьПартии(Объект, РежимЗаписи) Экспорт
	
	Если РежимЗаписи <> РежимЗаписиДокумента.Проведение
		И РежимЗаписи <> РежимЗаписиДокумента.ОтменаПроведения Тогда
		Возврат;
	КонецЕсли;
	
	НачалоПримененияФСБУ5 = '000101010000';
	//++ НЕ УТ
	НачалоПримененияФСБУ5 = РеглУчетКлиентСервер.НачалоПримененияФСБУ5_2019();
	//-- НЕ УТ
	
	Если ТипЗнч(Объект) = Тип("ДокументОбъект.ВнутреннееПотребление") Тогда
		
		ТабличнаяЧасть = Объект.Товары; // Документ.ВнутреннееПотребление.Товары
		ДанныеДокумента = ТабличнаяЧасть.Выгрузить();

		ДанныеДокумента.Колонки.Добавить("ДатаНачалаЭксплуатации", Новый ОписаниеТипов("Дата"));
		ДанныеДокумента.Колонки.Добавить("СрокЭксплуатации", ОбщегоНазначения.ОписаниеТипаЧисло(5));
		
		ДанныеДокумента.ЗаполнитьЗначения(НачалоМесяца(Объект.Дата), "ДатаНачалаЭксплуатации");
		
	//++ НЕ УТ
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументОбъект.ОприходованиеТМЦВЭксплуатации") Тогда
		
		ТабличнаяЧасть = Объект.Товары; // Документ.ОприходованиеТМЦВЭксплуатации.Товары
		ДанныеДокумента = ТабличнаяЧасть.Выгрузить();

		ДанныеДокумента.Колонки.Добавить("ДатаНачалаЭксплуатации", Новый ОписаниеТипов("Дата"));
		ДанныеДокумента.Колонки.Добавить("ОсновноеСредство", Новый ОписаниеТипов("СправочникСсылка.ОбъектыЭксплуатации"));
		ДанныеДокумента.Колонки.Добавить("СтатьяРасходов", Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.СтатьиРасходов"));
		ДанныеДокумента.Колонки.Добавить("АналитикаРасходов", Метаданные.ПланыВидовХарактеристик.СтатьиРасходов.Тип);
		
		ДанныеДокумента.ЗаполнитьЗначения(НачалоМесяца(Объект.Дата), "ДатаНачалаЭксплуатации");
	//-- НЕ УТ
		
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументОбъект.ВводОстатков") Тогда
		
		ТабличнаяЧасть = Объект.ТМЦВЭксплуатации;
		ДанныеДокумента = ТабличнаяЧасть.Выгрузить();

		ДанныеДокумента.Колонки.Добавить("СрокЭксплуатации", ОбщегоНазначения.ОписаниеТипаЧисло(5));

		//++ НЕ УТ
		ДанныеДокумента.Колонки.Добавить("ОсновноеСредство", Новый ОписаниеТипов("СправочникСсылка.ОбъектыЭксплуатации"));
		//-- НЕ УТ
		
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументОбъект.ВводОстатковТМЦВЭксплуатации") Тогда
		
		ТабличнаяЧасть = Объект.ТМЦВЭксплуатации;
		ДанныеДокумента = ТабличнаяЧасть.Выгрузить();
		
		ДанныеДокумента.Колонки.Добавить("СрокЭксплуатации", ОбщегоНазначения.ОписаниеТипаЧисло(5));

	Иначе
		
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Для документа ""%1"" не предусмотрено заполнение партий';
									|en = 'Cannot fill in lots in the ""%1"" document'"), ТипЗнч(Объект));

		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
	//++ НЕ УТ
	ОчиститьИнвентарныйНомер(ДанныеДокумента);
	//-- НЕ УТ
	
	СписокПартий = Новый ТаблицаЗначений();
	СписокПартий.Колонки.Добавить("Партия", Новый ОписаниеТипов("СправочникСсылка.ПартииТМЦВЭксплуатации"));
	Для Каждого СтрокаТЧ Из ДанныеДокумента Цикл
		Если ЗначениеЗаполнено(СтрокаТЧ.Партия) Тогда
			НоваяСтрока = СписокПартий.Добавить();
			НоваяСтрока.Партия = СтрокаТЧ.Партия;
		КонецЕсли;
	КонецЦикла;
	
	Блокировка = Новый БлокировкаДанных;
	
	ЭлементБлокировки = Блокировка.Добавить("Справочник.ПартииТМЦВЭксплуатации");
	ЭлементБлокировки.ИсточникДанных = ДанныеДокумента;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("КатегорияЭксплуатации", "КатегорияЭксплуатации");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ДатаНачалаЭксплуатации", "ДатаНачалаЭксплуатации");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ИнвентарныйНомер", "ИнвентарныйНомер");
	
	Если Объект.Дата < НачалоПримененияФСБУ5 Тогда
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("СтатьяРасходов", "СтатьяРасходов");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("АналитикаРасходов", "АналитикаРасходов");
	КонецЕсли;
	
	ЭлементБлокировки = Блокировка.Добавить("Справочник.ПартииТМЦВЭксплуатации");
	ЭлементБлокировки.ИсточникДанных = СписокПартий;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ссылка", "Партия");
	
	//++ НЕ УТ
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ТМЦВЭксплуатации");
	ЭлементБлокировки.ИсточникДанных = СписокПартий;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Партия", "Партия");
	//-- НЕ УТ

	Блокировка.Заблокировать();
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(Таблица.НомерСтроки КАК ЧИСЛО) КАК НомерСтроки,
	|	ВЫРАЗИТЬ(Таблица.ДатаНачалаЭксплуатации КАК ДАТА) КАК ДатаНачалаЭксплуатации,
	|	ВЫРАЗИТЬ(Таблица.КатегорияЭксплуатации КАК Справочник.КатегорииЭксплуатации) КАК КатегорияЭксплуатации,
	|	Таблица.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	ВЫРАЗИТЬ(Таблица.СтатьяРасходов КАК ПланВидовХарактеристик.СтатьиРасходов) КАК СтатьяРасходов,
	|	Таблица.АналитикаРасходов КАК АналитикаРасходов,
	|	Таблица.СрокЭксплуатации КАК СрокЭксплуатации,
	|	ВЫРАЗИТЬ(Таблица.Партия КАК Справочник.ПартииТМЦВЭксплуатации) КАК Партия
	//++ НЕ УТ
	|	,ВЫРАЗИТЬ(Таблица.ОсновноеСредство КАК Справочник.ОбъектыЭксплуатации) КАК ОсновноеСредство
	//-- НЕ УТ
	|
	|ПОМЕСТИТЬ ДанныеДокумента
	|
	|ИЗ
	|	&ДанныеДокумента КАК Таблица
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Партия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.НомерСтроки КАК НомерСтроки,
	|	НАЧАЛОПЕРИОДА(Таблица.ДатаНачалаЭксплуатации, МЕСЯЦ) КАК ДатаНачалаЭксплуатации,
	|
	|	ВЫБОР
	|		КОГДА Таблица.СрокЭксплуатации <> 0
	|			ТОГДА НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(Таблица.ДатаНачалаЭксплуатации, МЕСЯЦ, Таблица.СрокЭксплуатации), МЕСЯЦ), ДЕНЬ)
	|		КОГДА ЕСТЬNULL(Таблица.КатегорияЭксплуатации.СрокЭксплуатации, 0) <> 0
	|			ТОГДА НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(Таблица.ДатаНачалаЭксплуатации, МЕСЯЦ, Таблица.КатегорияЭксплуатации.СрокЭксплуатации), МЕСЯЦ), ДЕНЬ)
	|		ИНАЧЕ ДАТАВРЕМЯ(1,1,1)
	|	КОНЕЦ КАК ДатаЗавершенияЭксплуатации,
	|
	|	ВЫБОР
	|		КОГДА Таблица.СрокЭксплуатации <> 0
	|			ТОГДА Таблица.СрокЭксплуатации
	|		ИНАЧЕ Таблица.КатегорияЭксплуатации.СрокЭксплуатации
	|	КОНЕЦ КАК СрокЭксплуатации,
	|
	|	Таблица.КатегорияЭксплуатации КАК КатегорияЭксплуатации,
	|	Таблица.Партия КАК Партия
	//++ НЕ УТ
	|	,Таблица.ОсновноеСредство КАК ОсновноеСредство
	//-- НЕ УТ
	|
	|ПОМЕСТИТЬ СтрокиНеТребующиеОбновления
	|
	|ИЗ
	|	ДанныеДокумента КАК Таблица
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПартииТМЦВЭксплуатации КАК СправочникПартий
	|		ПО СправочникПартий.Ссылка = Таблица.Партия
	|
	|ГДЕ
	|	&ЗаполнитьПартии
	|
	|	И НЕ СправочникПартий.ПометкаУдаления
	|	И СправочникПартий.Партия258
	|	И СправочникПартий.КатегорияЭксплуатации = Таблица.КатегорияЭксплуатации
	|
	|	И (СправочникПартий.СрокЭксплуатации = Таблица.СрокЭксплуатации
	|			И Таблица.СрокЭксплуатации <> 0
	|		ИЛИ СправочникПартий.СрокЭксплуатации = ЕСТЬNULL(Таблица.КатегорияЭксплуатации.СрокЭксплуатации, 0)
	|			И Таблица.СрокЭксплуатации = 0)
	|
	|	И СправочникПартий.ДатаНачалаЭксплуатации = Таблица.ДатаНачалаЭксплуатации
	|
	//++ НЕ УТ
	|	И (Таблица.КатегорияЭксплуатации.УчитыватьВВидеГрупповогоОС
	|			И СправочникПартий.ОсновноеСредство <> ЗНАЧЕНИЕ(Справочник.ОбъектыЭксплуатации.ПустаяСсылка)
	|			И (СправочникПартий.ОсновноеСредство = Таблица.ОсновноеСредство
	|					ИЛИ Таблица.ОсновноеСредство = ЗНАЧЕНИЕ(Справочник.ОбъектыЭксплуатации.ПустаяСсылка))
	|		ИЛИ НЕ Таблица.КатегорияЭксплуатации.УчитыватьВВидеГрупповогоОС
	|			И СправочникПартий.ОсновноеСредство = ЗНАЧЕНИЕ(Справочник.ОбъектыЭксплуатации.ПустаяСсылка))
	|
	|	И НЕ (Таблица.ДатаНачалаЭксплуатации < &НачалоПримененияФСБУ5
	|			И Таблица.КатегорияЭксплуатации.СпособПогашенияСтоимостиБУ = ЗНАЧЕНИЕ(Перечисление.СпособыПогашенияСтоимостиТМЦ.ПоНаработке)
	|			И Таблица.КатегорияЭксплуатации.ИнвентарныйУчет
	|			И СправочникПартий.ИнвентарныйНомер <> Таблица.ИнвентарныйНомер)
	|
	|	И (&Дата >= &НачалоПримененияФСБУ5
	|		ИЛИ Таблица.СтатьяРасходов = СправочникПартий.СтатьяРасходов
	|			И Таблица.АналитикаРасходов = СправочникПартий.АналитикаРасходов)
	|
	//-- НЕ УТ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Партия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.НомерСтроки КАК НомерСтроки,
	|	НАЧАЛОПЕРИОДА(Таблица.ДатаНачалаЭксплуатации, МЕСЯЦ) КАК ДатаНачалаЭксплуатации,
	|
	|	ВЫБОР
	|		КОГДА Таблица.СрокЭксплуатации <> 0
	|			ТОГДА НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(Таблица.ДатаНачалаЭксплуатации, МЕСЯЦ, Таблица.СрокЭксплуатации), МЕСЯЦ), ДЕНЬ)
	|		КОГДА ЕСТЬNULL(Таблица.КатегорияЭксплуатации.СрокЭксплуатации, 0) <> 0
	|			ТОГДА НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(Таблица.ДатаНачалаЭксплуатации, МЕСЯЦ, Таблица.КатегорияЭксплуатации.СрокЭксплуатации), МЕСЯЦ), ДЕНЬ)
	|		ИНАЧЕ ДАТАВРЕМЯ(1,1,1)
	|	КОНЕЦ КАК ДатаЗавершенияЭксплуатации,
	|
	|	ВЫБОР
	|		КОГДА Таблица.СрокЭксплуатации <> 0
	|			ТОГДА Таблица.СрокЭксплуатации
	|		ИНАЧЕ Таблица.КатегорияЭксплуатации.СрокЭксплуатации
	|	КОНЕЦ КАК СрокЭксплуатации,
	|
	|	ВЫБОР
	//++ НЕ УТ
	|		КОГДА Таблица.ДатаНачалаЭксплуатации < &НачалоПримененияФСБУ5
	|				И Таблица.КатегорияЭксплуатации.СпособПогашенияСтоимостиБУ = ЗНАЧЕНИЕ(Перечисление.СпособыПогашенияСтоимостиТМЦ.ПоНаработке)
	|				И Таблица.КатегорияЭксплуатации.ИнвентарныйУчет
	|			ТОГДА Таблица.ИнвентарныйНомер
	//-- НЕ УТ
	|		КОГДА ИСТИНА
	|			ТОГДА """"
	|	КОНЕЦ КАК ИнвентарныйНомер,
	|	Таблица.КатегорияЭксплуатации КАК КатегорияЭксплуатации,
	|	Таблица.Партия КАК Партия
	//++ НЕ УТ
	|	,Таблица.СтатьяРасходов КАК СтатьяРасходов
	|	,Таблица.АналитикаРасходов КАК АналитикаРасходов
	|	,Таблица.ОсновноеСредство КАК ОсновноеСредство
	//-- НЕ УТ
	|
	|ПОМЕСТИТЬ СтрокиТребующиеОбновления
	|
	|ИЗ
	|	ДанныеДокумента КАК Таблица
	|
	|ГДЕ
	|	&ЗаполнитьПартии
	|	И НЕ Таблица.НомерСтроки В (
	|		ВЫБРАТЬ
	|			СтрокиНеТребующиеОбновления.НомерСтроки
	|		ИЗ
	|			СтрокиНеТребующиеОбновления)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Партия
	//++ НЕ УТ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// В первом запросе выбираются партии с учетом ОС, указанным в документе
	|ВЫБРАТЬ
	|	СправочникПартий.Ссылка КАК Ссылка,
	|	СправочникПартий.ПометкаУдаления КАК ПометкаУдаления,
	|	СправочникПартий.КатегорияЭксплуатации КАК КатегорияЭксплуатации,
	|	СправочникПартий.СрокЭксплуатации КАК СрокЭксплуатации,
	|	СправочникПартий.ДатаНачалаЭксплуатации КАК ДатаНачалаЭксплуатации,
	|	СправочникПартий.ДатаЗавершенияЭксплуатации КАК ДатаЗавершенияЭксплуатации,
	|	СправочникПартий.ОсновноеСредство КАК ОсновноеСредство
	|
	|ПОМЕСТИТЬ ВсеПодходящиеПартииСУчетомОС
	|	
	|ИЗ
	|	Справочник.ПартииТМЦВЭксплуатации КАК СправочникПартий
	|ГДЕ
	|	СправочникПартий.Партия258
	|	И СправочникПартий.КатегорияЭксплуатации.УчитыватьВВидеГрупповогоОС
	|	И СправочникПартий.ОсновноеСредство <> ЗНАЧЕНИЕ(Справочник.ОбъектыЭксплуатации.ПустаяСсылка)
	|
	|	И (СправочникПартий.КатегорияЭксплуатации, 
	|		СправочникПартий.СрокЭксплуатации, 
	|		СправочникПартий.ДатаНачалаЭксплуатации, 
	|		СправочникПартий.ДатаЗавершенияЭксплуатации, 
	|		СправочникПартий.ОсновноеСредство) В (
	|
	|		ВЫБРАТЬ
	|			СтрокиТребующиеОбновления.КатегорияЭксплуатации КАК КатегорияЭксплуатации,
	|			СтрокиТребующиеОбновления.СрокЭксплуатации КАК СрокЭксплуатации,
	|			СтрокиТребующиеОбновления.ДатаНачалаЭксплуатации КАК ДатаНачалаЭксплуатации,
	|			СтрокиТребующиеОбновления.ДатаЗавершенияЭксплуатации КАК ДатаЗавершенияЭксплуатации,
	|			СтрокиТребующиеОбновления.ОсновноеСредство КАК ОсновноеСредство
	|		ИЗ
	|			СтрокиТребующиеОбновления КАК СтрокиТребующиеОбновления
	|		ГДЕ
	|			СтрокиТребующиеОбновления.ОсновноеСредство <> ЗНАЧЕНИЕ(Справочник.ОбъектыЭксплуатации.ПустаяСсылка))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Во втором запросе для каждой группы выбирается одна партия 
	|ВЫБРАТЬ
	|	СправочникПартий.Ссылка КАК Ссылка,
	|	СправочникПартий.ПометкаУдаления КАК ПометкаУдаления,
	|	СправочникПартий.КатегорияЭксплуатации КАК КатегорияЭксплуатации,
	|	СправочникПартий.СрокЭксплуатации КАК СрокЭксплуатации,
	|	СправочникПартий.ДатаНачалаЭксплуатации КАК ДатаНачалаЭксплуатации,
	|	СправочникПартий.ДатаЗавершенияЭксплуатации КАК ДатаЗавершенияЭксплуатации,
	|	СправочникПартий.ОсновноеСредство КАК ОсновноеСредство
	|	
	|ИЗ
	|	(ВЫБРАТЬ
	|		СправочникПартий.КатегорияЭксплуатации КАК КатегорияЭксплуатации,
	|		СправочникПартий.СрокЭксплуатации КАК СрокЭксплуатации,
	|		СправочникПартий.ДатаНачалаЭксплуатации КАК ДатаНачалаЭксплуатации,
	|		СправочникПартий.ДатаЗавершенияЭксплуатации КАК ДатаЗавершенияЭксплуатации,
	|		МАКСИМУМ(СправочникПартий.ОсновноеСредство) КАК ОсновноеСредство
	|	
	|	ИЗ
	|		Справочник.ПартииТМЦВЭксплуатации КАК СправочникПартий
	|	ГДЕ
	|		СправочникПартий.Партия258
	|		И СправочникПартий.КатегорияЭксплуатации.УчитыватьВВидеГрупповогоОС
	|		И СправочникПартий.ОсновноеСредство <> ЗНАЧЕНИЕ(Справочник.ОбъектыЭксплуатации.ПустаяСсылка)
	|
	|		И (СправочникПартий.КатегорияЭксплуатации, 
	|			СправочникПартий.СрокЭксплуатации, 
	|			СправочникПартий.ДатаНачалаЭксплуатации, 
	|			СправочникПартий.ДатаЗавершенияЭксплуатации) В (
	|
	|			ВЫБРАТЬ
	|				СтрокиТребующиеОбновления.КатегорияЭксплуатации КАК КатегорияЭксплуатации,
	|				СтрокиТребующиеОбновления.СрокЭксплуатации КАК СрокЭксплуатации,
	|				СтрокиТребующиеОбновления.ДатаНачалаЭксплуатации КАК ДатаНачалаЭксплуатации,
	|				СтрокиТребующиеОбновления.ДатаЗавершенияЭксплуатации КАК ДатаЗавершенияЭксплуатации
	|			ИЗ
	|				СтрокиТребующиеОбновления КАК СтрокиТребующиеОбновления
	|			ГДЕ
	|				СтрокиТребующиеОбновления.ОсновноеСредство = ЗНАЧЕНИЕ(Справочник.ОбъектыЭксплуатации.ПустаяСсылка))
	|
	|	СГРУППИРОВАТЬ ПО
	|		СправочникПартий.КатегорияЭксплуатации,
	|		СправочникПартий.СрокЭксплуатации,
	|		СправочникПартий.ДатаНачалаЭксплуатации,
	|		СправочникПартий.ДатаЗавершенияЭксплуатации
	|
	|	) КАК ПартииСУчетомОС
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПартииТМЦВЭксплуатации КАК СправочникПартий
	|	ПО СправочникПартий.КатегорияЭксплуатации = ПартииСУчетомОС.КатегорияЭксплуатации
	|		И СправочникПартий.СрокЭксплуатации = ПартииСУчетомОС.СрокЭксплуатации
	|		И СправочникПартий.ДатаНачалаЭксплуатации = ПартииСУчетомОС.ДатаНачалаЭксплуатации
	|		И СправочникПартий.Партия258
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВсеПодходящиеПартии.Ссылка) КАК Ссылка,
	|	ВсеПодходящиеПартии.ПометкаУдаления КАК ПометкаУдаления,
	|	ВсеПодходящиеПартии.КатегорияЭксплуатации КАК КатегорияЭксплуатации,
	|	ВсеПодходящиеПартии.СрокЭксплуатации КАК СрокЭксплуатации,
	|	ВсеПодходящиеПартии.ДатаНачалаЭксплуатации КАК ДатаНачалаЭксплуатации,
	|	ВсеПодходящиеПартии.ДатаЗавершенияЭксплуатации КАК ДатаЗавершенияЭксплуатации,
	|	ВсеПодходящиеПартии.ОсновноеСредство КАК ОсновноеСредство
	|
	|ПОМЕСТИТЬ ПодходящиеПартииСУчетомОС
	|	
	|ИЗ
	|	ВсеПодходящиеПартииСУчетомОС КАК ВсеПодходящиеПартии
	|	
	|ГДЕ
	|	НЕ ВсеПодходящиеПартии.ПометкаУдаления
	|
	|СГРУППИРОВАТЬ ПО
	|	ВсеПодходящиеПартии.ПометкаУдаления,
	|	ВсеПодходящиеПартии.КатегорияЭксплуатации,
	|	ВсеПодходящиеПартии.СрокЭксплуатации,
	|	ВсеПодходящиеПартии.ДатаНачалаЭксплуатации,
	|	ВсеПодходящиеПартии.ДатаЗавершенияЭксплуатации,
	|	ВсеПодходящиеПартии.ОсновноеСредство
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВсеПодходящиеПартии.Ссылка) КАК Ссылка,
	|	ВсеПодходящиеПартии.ПометкаУдаления КАК ПометкаУдаления,
	|	ВсеПодходящиеПартии.КатегорияЭксплуатации КАК КатегорияЭксплуатации,
	|	ВсеПодходящиеПартии.СрокЭксплуатации КАК СрокЭксплуатации,
	|	ВсеПодходящиеПартии.ДатаНачалаЭксплуатации КАК ДатаНачалаЭксплуатации,
	|	ВсеПодходящиеПартии.ДатаЗавершенияЭксплуатации КАК ДатаЗавершенияЭксплуатации,
	|	ВсеПодходящиеПартии.ОсновноеСредство КАК ОсновноеСредство
	|
	|ИЗ
	|	ВсеПодходящиеПартииСУчетомОС КАК ВсеПодходящиеПартии
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВсеПодходящиеПартииСУчетомОС КАК ВсеПодходящиеПартии2
	|		ПО ВсеПодходящиеПартии2.Ссылка <> ВсеПодходящиеПартии.Ссылка
	|			И НЕ ВсеПодходящиеПартии2.ПометкаУдаления
	|			И ВсеПодходящиеПартии2.КатегорияЭксплуатации = ВсеПодходящиеПартии.КатегорияЭксплуатации
	|			И ВсеПодходящиеПартии2.СрокЭксплуатации = ВсеПодходящиеПартии.СрокЭксплуатации
	|			И ВсеПодходящиеПартии2.ДатаНачалаЭксплуатации = ВсеПодходящиеПартии.ДатаНачалаЭксплуатации
	|			И ВсеПодходящиеПартии2.ДатаЗавершенияЭксплуатации = ВсеПодходящиеПартии.ДатаЗавершенияЭксплуатации
	|			И ВсеПодходящиеПартии2.ОсновноеСредство = ВсеПодходящиеПартии.ОсновноеСредство
	|
	|ГДЕ
	|	ВсеПодходящиеПартии.ПометкаУдаления
	|	И ВсеПодходящиеПартии2.Ссылка ЕСТЬ NULL
	|
	|СГРУППИРОВАТЬ ПО
	|	ВсеПодходящиеПартии.ПометкаУдаления,
	|	ВсеПодходящиеПартии.КатегорияЭксплуатации,
	|	ВсеПодходящиеПартии.СрокЭксплуатации,
	|	ВсеПодходящиеПартии.ДатаНачалаЭксплуатации,
	|	ВсеПодходящиеПартии.ДатаЗавершенияЭксплуатации,
	|	ВсеПодходящиеПартии.ОсновноеСредство
	//-- НЕ УТ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СправочникПартий.Ссылка КАК Ссылка,
	|	СправочникПартий.ПометкаУдаления КАК ПометкаУдаления,
	|	СправочникПартий.КатегорияЭксплуатации КАК КатегорияЭксплуатации,
	|	СправочникПартий.СрокЭксплуатации КАК СрокЭксплуатации,
	|	СправочникПартий.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	СправочникПартий.ДатаНачалаЭксплуатации КАК ДатаНачалаЭксплуатации,
	|	СправочникПартий.ДатаЗавершенияЭксплуатации КАК ДатаЗавершенияЭксплуатации,
	|	ВЫБОР
	|		КОГДА &Дата < &НачалоПримененияФСБУ5
	|			ТОГДА СправочникПартий.СтатьяРасходов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СтатьяРасходов,
	|	ВЫБОР
	|		КОГДА &Дата < &НачалоПримененияФСБУ5
	|			ТОГДА СправочникПартий.АналитикаРасходов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК АналитикаРасходов
	|
	|ПОМЕСТИТЬ ВсеПодходящиеПартииБезУчетаОС
	|
	|ИЗ
	|	Справочник.ПартииТМЦВЭксплуатации КАК СправочникПартий
	|ГДЕ
	|	СправочникПартий.Партия258
	//++ НЕ УТ
	|	И НЕ СправочникПартий.КатегорияЭксплуатации.УчитыватьВВидеГрупповогоОС
	|	И СправочникПартий.ОсновноеСредство = ЗНАЧЕНИЕ(Справочник.ОбъектыЭксплуатации.ПустаяСсылка)
	//-- НЕ УТ
	|
	|	И (СправочникПартий.КатегорияЭксплуатации, 
	|		СправочникПартий.СрокЭксплуатации, 
	|		СправочникПартий.ИнвентарныйНомер, 
	|		СправочникПартий.ДатаНачалаЭксплуатации, 
	|		СправочникПартий.ДатаЗавершенияЭксплуатации) В (
	|
	|		ВЫБРАТЬ
	|			СтрокиТребующиеОбновления.КатегорияЭксплуатации КАК КатегорияЭксплуатации,
	|			СтрокиТребующиеОбновления.СрокЭксплуатации КАК СрокЭксплуатации,
	|			СтрокиТребующиеОбновления.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|			СтрокиТребующиеОбновления.ДатаНачалаЭксплуатации КАК ДатаНачалаЭксплуатации,
	|			СтрокиТребующиеОбновления.ДатаЗавершенияЭксплуатации КАК ДатаЗавершенияЭксплуатации
	|		ИЗ
	|			СтрокиТребующиеОбновления КАК СтрокиТребующиеОбновления)
	|
	//++ НЕ УТ
	|	И (&Дата >= &НачалоПримененияФСБУ5
	|		ИЛИ (СправочникПартий.СтатьяРасходов, СправочникПартий.АналитикаРасходов) В (
	|
	|			ВЫБРАТЬ
	|				СтрокиТребующиеОбновления.СтатьяРасходов КАК СтатьяРасходов,
	|				СтрокиТребующиеОбновления.АналитикаРасходов КАК АналитикаРасходов
	|			ИЗ
	|				СтрокиТребующиеОбновления КАК СтрокиТребующиеОбновления))
	//-- НЕ УТ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВсеПодходящиеПартии.Ссылка) КАК Ссылка,
	|	ВсеПодходящиеПартии.ПометкаУдаления КАК ПометкаУдаления,
	|	ВсеПодходящиеПартии.КатегорияЭксплуатации КАК КатегорияЭксплуатации,
	|	ВсеПодходящиеПартии.СрокЭксплуатации КАК СрокЭксплуатации,
	|	ВсеПодходящиеПартии.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	ВсеПодходящиеПартии.ДатаНачалаЭксплуатации КАК ДатаНачалаЭксплуатации,
	|	ВсеПодходящиеПартии.ДатаЗавершенияЭксплуатации КАК ДатаЗавершенияЭксплуатации,
	|	ВсеПодходящиеПартии.СтатьяРасходов КАК СтатьяРасходов,
	|	ВсеПодходящиеПартии.АналитикаРасходов КАК АналитикаРасходов
	|
	|ПОМЕСТИТЬ ПодходящиеПартииБезУчетаОС
	|	
	|ИЗ
	|	ВсеПодходящиеПартииБезУчетаОС КАК ВсеПодходящиеПартии
	|	
	|ГДЕ
	|	НЕ ВсеПодходящиеПартии.ПометкаУдаления
	|
	|СГРУППИРОВАТЬ ПО
	|	ВсеПодходящиеПартии.ПометкаУдаления,
	|	ВсеПодходящиеПартии.КатегорияЭксплуатации,
	|	ВсеПодходящиеПартии.СрокЭксплуатации,
	|	ВсеПодходящиеПартии.ИнвентарныйНомер,
	|	ВсеПодходящиеПартии.ДатаНачалаЭксплуатации,
	|	ВсеПодходящиеПартии.ДатаЗавершенияЭксплуатации,
	|	ВсеПодходящиеПартии.СтатьяРасходов,
	|	ВсеПодходящиеПартии.АналитикаРасходов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВсеПодходящиеПартии.Ссылка) КАК Ссылка,
	|	ВсеПодходящиеПартии.ПометкаУдаления КАК ПометкаУдаления,
	|	ВсеПодходящиеПартии.КатегорияЭксплуатации КАК КатегорияЭксплуатации,
	|	ВсеПодходящиеПартии.СрокЭксплуатации КАК СрокЭксплуатации,
	|	ВсеПодходящиеПартии.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	ВсеПодходящиеПартии.ДатаНачалаЭксплуатации КАК ДатаНачалаЭксплуатации,
	|	ВсеПодходящиеПартии.ДатаЗавершенияЭксплуатации КАК ДатаЗавершенияЭксплуатации,
	|	ВсеПодходящиеПартии.СтатьяРасходов КАК СтатьяРасходов,
	|	ВсеПодходящиеПартии.АналитикаРасходов КАК АналитикаРасходов
	|
	|ИЗ
	|	ВсеПодходящиеПартииБезУчетаОС КАК ВсеПодходящиеПартии
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВсеПодходящиеПартииБезУчетаОС КАК ВсеПодходящиеПартии2
	|		ПО ВсеПодходящиеПартии2.Ссылка <> ВсеПодходящиеПартии.Ссылка
	|			И НЕ ВсеПодходящиеПартии2.ПометкаУдаления
	|			И ВсеПодходящиеПартии2.КатегорияЭксплуатации = ВсеПодходящиеПартии.КатегорияЭксплуатации
	|			И ВсеПодходящиеПартии2.СрокЭксплуатации = ВсеПодходящиеПартии.СрокЭксплуатации
	|			И ВсеПодходящиеПартии2.ИнвентарныйНомер = ВсеПодходящиеПартии.ИнвентарныйНомер
	|			И ВсеПодходящиеПартии2.ДатаНачалаЭксплуатации = ВсеПодходящиеПартии.ДатаНачалаЭксплуатации
	|			И ВсеПодходящиеПартии2.ДатаЗавершенияЭксплуатации = ВсеПодходящиеПартии.ДатаЗавершенияЭксплуатации
	|			И ВсеПодходящиеПартии2.СтатьяРасходов = ВсеПодходящиеПартии.СтатьяРасходов
	|			И ВсеПодходящиеПартии2.АналитикаРасходов = ВсеПодходящиеПартии.АналитикаРасходов
	|
	|ГДЕ
	|	ВсеПодходящиеПартии.ПометкаУдаления
	|	И ВсеПодходящиеПартии2.Ссылка ЕСТЬ NULL
	|
	|СГРУППИРОВАТЬ ПО
	|	ВсеПодходящиеПартии.ПометкаУдаления,
	|	ВсеПодходящиеПартии.КатегорияЭксплуатации,
	|	ВсеПодходящиеПартии.СрокЭксплуатации,
	|	ВсеПодходящиеПартии.ИнвентарныйНомер,
	|	ВсеПодходящиеПартии.ДатаНачалаЭксплуатации,
	|	ВсеПодходящиеПартии.ДатаЗавершенияЭксплуатации,
	|	ВсеПодходящиеПартии.СтатьяРасходов,
	|	ВсеПодходящиеПартии.АналитикаРасходов
	//++ НЕ УТ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// Неиспользуемые партии документа
	|ВЫБРАТЬ
	|	СправочникПартий.Ссылка КАК Ссылка,
	|	СправочникПартий.ПометкаУдаления КАК ПометкаУдаления,
	|	СправочникПартий.КатегорияЭксплуатации КАК КатегорияЭксплуатации,
	|	СправочникПартий.СрокЭксплуатации КАК СрокЭксплуатации,
	|	СправочникПартий.ДатаНачалаЭксплуатации КАК ДатаНачалаЭксплуатации,
	|	СправочникПартий.ДатаЗавершенияЭксплуатации КАК ДатаЗавершенияЭксплуатации,
	|	СправочникПартий.ОсновноеСредство КАК ОсновноеСредство,
	|	СправочникПартий.СтатьяРасходов КАК СтатьяРасходов,
	|	СправочникПартий.АналитикаРасходов КАК АналитикаРасходов
	|
	|ИЗ
	|	Справочник.ПартииТМЦВЭксплуатации КАК СправочникПартий
	|
	|ГДЕ
	|	СправочникПартий.Партия258
	|
		// Это партия в документе
	|	И СправочникПартий.Ссылка В (
	|		ВЫБРАТЬ
	|			ДанныеДокумента.Партия КАК Партия
	|		ИЗ
	|			ДанныеДокумента КАК ДанныеДокумента
	|
	|		ОБЪЕДИНИТЬ ВСЕ
	|
	|		ВЫБРАТЬ
	|			ДанныеДокумента.Партия КАК Партия
	|		ИЗ
	|			Документ.ВнутреннееПотребление.Товары КАК ДанныеДокумента
	|		ГДЕ
	|			ДанныеДокумента.Ссылка = &Ссылка
	|
	|		ОБЪЕДИНИТЬ ВСЕ
	|
	|		ВЫБРАТЬ
	|			ДанныеДокумента.Партия КАК Партия
	|		ИЗ
	|			Документ.ОприходованиеТМЦВЭксплуатации.Товары КАК ДанныеДокумента
	|		ГДЕ
	|			ДанныеДокумента.Ссылка = &Ссылка
	|
	|		ОБЪЕДИНИТЬ ВСЕ
	|
	|		ВЫБРАТЬ
	|			ДанныеДокумента.Партия КАК Партия
	|		ИЗ
	|			Документ.ВводОстатковТМЦВЭксплуатации.ТМЦВЭксплуатации КАК ДанныеДокумента
	|		ГДЕ
	|			ДанныеДокумента.Ссылка = &Ссылка
	|
	|		ОБЪЕДИНИТЬ ВСЕ
	|
	|		ВЫБРАТЬ
	|			ДанныеДокумента.Партия КАК Партия
	|		ИЗ
	|			Документ.ВводОстатков.ТМЦВЭксплуатации КАК ДанныеДокумента
	|		ГДЕ
	|			ДанныеДокумента.Ссылка = &Ссылка)
	|
		// Не используется в других документах
	|	И НЕ 1 В (
	|		ВЫБРАТЬ
	|			1
	|		ИЗ
	|			РегистрНакопления.ТМЦВЭксплуатации КАК ТМЦВЭксплуатации
	|		ГДЕ
	|			ТМЦВЭксплуатации.Регистратор <> &Ссылка
	|			И ТМЦВЭксплуатации.Партия = СправочникПартий.Ссылка)
	|
		// Партия не будет использоваться в документе
	|	И (НЕ (СправочникПартий.Ссылка) В (
	|			ВЫБРАТЬ
	|				ПодходящиеПартии.Ссылка
	|			ИЗ
	|				ПодходящиеПартииСУчетомОС КАК ПодходящиеПартии)
	|
	|		И НЕ (СправочникПартий.Ссылка) В (
	|			ВЫБРАТЬ
	|				ПодходящиеПартии.Ссылка
	|			ИЗ
	|				ПодходящиеПартииБезУчетаОС КАК ПодходящиеПартии)
	|
	|		И НЕ (СправочникПартий.Ссылка) В (
	|			ВЫБРАТЬ
	|				СтрокиНеТребующиеОбновления.Партия
	|			ИЗ
	|				СтрокиНеТребующиеОбновления КАК СтрокиНеТребующиеОбновления)
	|
	|		ИЛИ НЕ &ЗаполнитьПартии)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПодходящиеПартии.Ссылка КАК Ссылка,
	|	ПодходящиеПартии.ПометкаУдаления КАК ПометкаУдаления,
	|	ПодходящиеПартии.КатегорияЭксплуатации КАК КатегорияЭксплуатации,
	|	ПодходящиеПартии.СрокЭксплуатации КАК СрокЭксплуатации,
	|	"""" КАК ИнвентарныйНомер,
	|	ПодходящиеПартии.ДатаНачалаЭксплуатации КАК ДатаНачалаЭксплуатации,
	|	ПодходящиеПартии.ДатаЗавершенияЭксплуатации КАК ДатаЗавершенияЭксплуатации,
	|	ПодходящиеПартии.ОсновноеСредство КАК ОсновноеСредство,
	|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов
	|ИЗ
	|	ПодходящиеПартииСУчетомОС КАК ПодходящиеПартии
	//-- НЕ УТ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПодходящиеПартии.Ссылка КАК Ссылка,
	|	ПодходящиеПартии.ПометкаУдаления КАК ПометкаУдаления,
	|	ПодходящиеПартии.КатегорияЭксплуатации КАК КатегорияЭксплуатации,
	|	ПодходящиеПартии.СрокЭксплуатации КАК СрокЭксплуатации,
	|	ПодходящиеПартии.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	ПодходящиеПартии.ДатаНачалаЭксплуатации КАК ДатаНачалаЭксплуатации,
	|	ПодходящиеПартии.ДатаЗавершенияЭксплуатации КАК ДатаЗавершенияЭксплуатации,
	|	ПодходящиеПартии.СтатьяРасходов КАК СтатьяРасходов,
	|	ПодходящиеПартии.АналитикаРасходов КАК АналитикаРасходов
	|ИЗ
	|	ПодходящиеПартииБезУчетаОС КАК ПодходящиеПартии
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтрокиТребующиеОбновления.НомерСтроки КАК НомерСтроки,
	|	СтрокиТребующиеОбновления.КатегорияЭксплуатации КАК КатегорияЭксплуатации,
	|	СтрокиТребующиеОбновления.КатегорияЭксплуатации.УчитыватьВВидеГрупповогоОС КАК УчитыватьВВидеГрупповогоОС,
	|	СтрокиТребующиеОбновления.КатегорияЭксплуатации.ГруппаОС КАК ГруппаОС,
	|	СтрокиТребующиеОбновления.СрокЭксплуатации КАК СрокЭксплуатации,
	|	СтрокиТребующиеОбновления.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	СтрокиТребующиеОбновления.ДатаНачалаЭксплуатации КАК ДатаНачалаЭксплуатации,
	|	СтрокиТребующиеОбновления.ДатаЗавершенияЭксплуатации КАК ДатаЗавершенияЭксплуатации
	//++ НЕ УТ
	|	,СтрокиТребующиеОбновления.ОсновноеСредство КАК ОсновноеСредство
	|	,СтрокиТребующиеОбновления.СтатьяРасходов КАК СтатьяРасходов
	|	,СтрокиТребующиеОбновления.АналитикаРасходов КАК АналитикаРасходов
	//-- НЕ УТ
	|ИЗ
	|	СтрокиТребующиеОбновления КАК СтрокиТребующиеОбновления";
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("Дата", Объект.Дата);
	Запрос.УстановитьПараметр("ДанныеДокумента", ДанныеДокумента);
	Запрос.УстановитьПараметр("ЗаполнитьПартии", РежимЗаписи = РежимЗаписиДокумента.Проведение);
	Запрос.УстановитьПараметр("НачалоПримененияФСБУ5", НачалоПримененияФСБУ5);
	
	Результат = Запрос.ВыполнитьПакет();
	
	ТаблицаПартийБезУчетаОС = Результат[Результат.ВГраница()-1].Выгрузить();

	//++ НЕ УТ
	ТаблицаНеИспользуемыхПартий = Результат[Результат.ВГраница()-3].Выгрузить();
	ТаблицаПартийСУчетомОС = Результат[Результат.ВГраница()-2].Выгрузить();
	
	ИспользуемыеРеквизитыПартии = "КатегорияЭксплуатации,СрокЭксплуатации,ДатаНачалаЭксплуатации,ДатаЗавершенияЭксплуатации,ОсновноеСредство";
	СтруктураПоискаСУчетомОС = Новый Структура(ИспользуемыеРеквизитыПартии);
	ТаблицаПартийСУчетомОС.Индексы.Добавить(ИспользуемыеРеквизитыПартии);
	//-- НЕ УТ
	
	ИспользуемыеРеквизитыПартии = "КатегорияЭксплуатации,СрокЭксплуатации,ДатаНачалаЭксплуатации,ДатаЗавершенияЭксплуатации,ИнвентарныйНомер";
	//++ НЕ УТ
	Если Объект.Дата < НачалоПримененияФСБУ5 Тогда
		ИспользуемыеРеквизитыПартии = ИспользуемыеРеквизитыПартии + "," + "СтатьяРасходов,АналитикаРасходов"
	КонецЕсли;
	//-- НЕ УТ
	СтруктураПоискаБезУчетаОС = Новый Структура(ИспользуемыеРеквизитыПартии);
	ТаблицаПартийБезУчетаОС.Индексы.Добавить(ИспользуемыеРеквизитыПартии);
	//++ НЕ УТ
	ТаблицаПартийСУчетомОС.Индексы.Добавить(ИспользуемыеРеквизитыПартии);
	//-- НЕ УТ

	ПараметрыНаименования = Новый Структура("ДатаНачалаЭксплуатации,ДатаЗавершенияЭксплуатации,СрокЭксплуатации,КатегорияЭксплуатации");
	
	Выборка = Результат[Результат.ВГраница()].Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СтрокаТЧ = ТабличнаяЧасть[Выборка.НомерСтроки - 1];
		
		ТаблицаПартий = ТаблицаПартийБезУчетаОС;
		СтруктураПоиска = СтруктураПоискаБезУчетаОС;

		//++ НЕ УТ
		Если Выборка.УчитыватьВВидеГрупповогоОС Тогда
			ТаблицаПартий = ТаблицаПартийСУчетомОС;
			Если ЗначениеЗаполнено(Выборка.ОсновноеСредство) Тогда
				СтруктураПоиска = СтруктураПоискаСУчетомОС;
			Иначе
				СтруктураПоиска = СтруктураПоискаБезУчетаОС;
			КонецЕсли;
		КонецЕсли;
		//-- НЕ УТ
		
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, Выборка);
		СписокСтрок = ТаблицаПартий.НайтиСтроки(СтруктураПоиска);
		
		Если СписокСтрок.Количество() = 0 Тогда
			
			// Нужной партии нет.
			// Сначала берется неиспользуемая партия, если ее нет, то создается новая партия.
			ОбъектПартии = Неопределено;

			//++ НЕ УТ
			Если ТаблицаНеИспользуемыхПартий.Количество() <> 0 Тогда
				ОбъектПартии = ТаблицаНеИспользуемыхПартий[0].Ссылка.ПолучитьОбъект(); // СправочникОбъект.ПартииТМЦВЭксплуатации
				ОбъектПартии.ОсновноеСредство = Справочники.ОбъектыЭксплуатации.ПустаяСсылка();
				ОбъектПартии.ПометкаУдаления = Ложь;
				ТаблицаНеИспользуемыхПартий.Удалить(ТаблицаНеИспользуемыхПартий[0]);
			КонецЕсли;
			//-- НЕ УТ
			
			Если ОбъектПартии = Неопределено Тогда
				ОбъектПартии = Справочники.ПартииТМЦВЭксплуатации.СоздатьЭлемент();
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(ОбъектПартии, СтруктураПоиска);
			ОбъектПартии.Наименование = СформироватьНаименование(ОбъектПартии);
			
			//++ НЕ УТ
			Если Выборка.УчитыватьВВидеГрупповогоОС Тогда
				
				ЗаполнитьЗначенияСвойств(ПараметрыНаименования, ОбъектПартии);
				НаименованиеОС = СформироватьНаименованиеГрупповогоОС(ПараметрыНаименования);
				
				Если НЕ ЗначениеЗаполнено(ОбъектПартии.ОсновноеСредство) Тогда
				
					ОсновноеСредство = Справочники.ОбъектыЭксплуатации.СоздатьЭлемент();
					ОсновноеСредство.ТипОС = Перечисления.ТипыОС.ГрупповоеОС;
					ОсновноеСредство.Наименование = НаименованиеОС;
					ОсновноеСредство.ГруппаОС = Выборка.ГруппаОС;
					ОсновноеСредство.Записать();
					ОбъектПартии.ОсновноеСредство = ОсновноеСредство.Ссылка;
					
				ИначеЕсли НЕ ЗначениеЗаполнено(Выборка.ОсновноеСредство) Тогда
					
					// Обновление реквизитов, только если в документе не выбрано существующее ОС
					РеквизитыОС = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ОбъектПартии.ОсновноеСредство, "Наименование,ГруппаОС");
					Если РеквизитыОС.Наименование <> НаименованиеОС
						ИЛИ РеквизитыОС.ГруппаОС <> Выборка.ГруппаОС Тогда
						
						ОсновноеСредство = ОбъектПартии.ОсновноеСредство.ПолучитьОбъект();
						ОсновноеСредство.Наименование = НаименованиеОС;
						ОсновноеСредство.ГруппаОС = Выборка.ГруппаОС;
						ОсновноеСредство.Записать();
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			//-- НЕ УТ
			
			ОбъектПартии.Партия258 = Истина;
			ОбъектПартии.Записать();
			
			ДанныеПартии = ТаблицаПартий.Добавить();
			ЗаполнитьЗначенияСвойств(ДанныеПартии, СтруктураПоиска);
			ДанныеПартии.Ссылка = ОбъектПартии.Ссылка;
			ДанныеПартии.ПометкаУдаления = ОбъектПартии.ПометкаУдаления;
			
			//++ НЕ УТ
			Если Выборка.УчитыватьВВидеГрупповогоОС Тогда
				ДанныеПартии.ОсновноеСредство = ОбъектПартии.ОсновноеСредство;
			КонецЕсли;
			//-- НЕ УТ
			
		Иначе
			
			// Есть нужная партия.
			ДанныеПартии = СписокСтрок[0];
			
		КонецЕсли;
		
		Если ДанныеПартии.ПометкаУдаления Тогда
			ОбъектПартии = ДанныеПартии.Ссылка.ПолучитьОбъект(); // СправочникОбъект.ПартииТМЦВЭксплуатации
			ОбъектПартии.Наименование = СформироватьНаименование(ОбъектПартии);
			ОбъектПартии.ПометкаУдаления = Ложь;
			ОбъектПартии.Записать();
		КонецЕсли;
		
		СтрокаТЧ.Партия = ДанныеПартии.Ссылка;
			
	КонецЦикла;
	
	//++ НЕ УТ
	
	// Удаление неиспользуемых партий.
	Для Каждого ДанныеПартии Из ТаблицаНеИспользуемыхПартий Цикл
		Если НЕ ДанныеПартии.ПометкаУдаления Тогда
			ОбъектПартии = ДанныеПартии.Ссылка.ПолучитьОбъект(); // СправочникОбъект.ПартииТМЦВЭксплуатации
			ОбъектПартии.УстановитьПометкуУдаления(Истина);
		КонецЕсли;
	КонецЦикла;
	
	//-- НЕ УТ
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	//++ НЕ УТ
	
	ТМЦВЭксплуатацииВызовСервера.ОбработкаПолученияДанныхВыбораПартииТМЦВЭксплуатации(
		ДанныеВыбора, 
		Параметры, 
		СтандартнаяОбработка);
		
	//-- НЕ УТ
	
	Возврат; // в УТ пустой обработчик
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область Прочее

// Формирует наименование партии.
// 
// Параметры:
//  Объект - СправочникОбъект.ПартииТМЦВЭксплуатации - Объект
// 
// Возвращаемое значение:
//  Строка - Наименование
//
Функция СформироватьНаименование(Объект) Экспорт
	
	НаименованиеКатегории = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.КатегорияЭксплуатации, "Наименование");
	
	Если Объект.СрокЭксплуатации <> 0 Тогда
		
		Наименование = СтрШаблон(
					НСтр("ru = '%1 (с %2 по %3)';
						|en = '%1 (from %2 to %3)'"),
					СокрЛП(НаименованиеКатегории),
					Формат(Объект.ДатаНачалаЭксплуатации, НСтр("ru = 'ДФ=''МММ гггг ""г.""''';
																|en = 'DF=''MMM yyyy'''")),
					Формат(Объект.ДатаЗавершенияЭксплуатации, НСтр("ru = 'ДФ=''МММ гггг ""г.""''';
																	|en = 'DF=''MMM yyyy'''")));
					
	Иначе
		
		Наименование = СтрШаблон(
					НСтр("ru = '%1 (с %2)';
						|en = '%1 (from %2)'"),
					СокрЛП(НаименованиеКатегории),
					Формат(Объект.ДатаНачалаЭксплуатации, НСтр("ru = 'ДФ=''МММ гггг ""г.""''';
																|en = 'DF=''MMM yyyy'''")));
					
	КонецЕсли;
	
	Возврат Наименование;
	
КонецФункции

// Формирует наименование группового ОС.
// 
// Параметры:
//  Параметры - Структура - Содержит:
//  	* ДатаНачалаЭксплуатации - Дата -
//  	* ДатаЗавершенияЭксплуатации - Дата -
//  	* СрокЭксплуатации - Число -
//  	* КатегорияЭксплуатации - СправочникСсылка.КатегорииЭксплуатации -
// 
// Возвращаемое значение:
//  Строка - Наименование группового ОС.
Функция СформироватьНаименованиеГрупповогоОС(Параметры) Экспорт

	НаименованиеКатегории = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.КатегорияЭксплуатации, "Наименование");
	
	Если Параметры.СрокЭксплуатации <> 0 Тогда
		
		Наименование = СтрШаблон(
					НСтр("ru = '%1 (с %2 по %3)';
						|en = '%1 (from %2 to %3)'"),
					СокрЛП(НаименованиеКатегории),
					Формат(Параметры.ДатаНачалаЭксплуатации, НСтр("ru = 'ДФ=''МММ гггг ""г.""''';
																	|en = 'DF=''MMM yyyy'''")),
					Формат(Параметры.ДатаЗавершенияЭксплуатации, НСтр("ru = 'ДФ=''МММ гггг ""г.""''';
																		|en = 'DF=''MMM yyyy'''")));
					
	Иначе
		
		Наименование = СтрШаблон(
					НСтр("ru = '%1 (с %2)';
						|en = '%1 (from %2)'"),
					СокрЛП(НаименованиеКатегории),
					Формат(Параметры.ДатаНачалаЭксплуатации, НСтр("ru = 'ДФ=''МММ гггг ""г.""''';
																	|en = 'DF=''MMM yyyy'''")));
					
	КонецЕсли;
	
	Возврат Наименование;
	
КонецФункции

//++ НЕ УТ

Процедура ОчиститьИнвентарныйНомер(ДанныеДокумента, РеквизитыКатегорий = Неопределено)
	
	НачалоПримененияФСБУ5 = РеглУчетКлиентСервер.НачалоПримененияФСБУ5_2019();
	
	Для Каждого ДанныеСтроки Из ДанныеДокумента Цикл
		
		Если ДанныеСтроки.ДатаНачалаЭксплуатации < НачалоПримененияФСБУ5
			И ЗначениеЗаполнено(ДанныеСтроки.КатегорияЭксплуатации) Тогда
			
			Если РеквизитыКатегорий = Неопределено Тогда
				РеквизитыКатегорий = РеквизитыКатегорий(ДанныеДокумента);
			КонецЕсли;
			
			СвойстваКатегории = РеквизитыКатегорий.Получить(ДанныеСтроки.КатегорияЭксплуатации);
			Если СвойстваКатегории.СпособПогашенияСтоимостиБУ <> Перечисления.СпособыПогашенияСтоимостиТМЦ.ПоНаработке
				ИЛИ НЕ СвойстваКатегории.ИнвентарныйУчет Тогда
				ДанныеСтроки.ИнвентарныйНомер = "";
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция РеквизитыКатегорий(ДанныеДокумента)
	
	СписокКатегорий = Новый Массив;
	Для Каждого ДанныеСтроки Из ДанныеДокумента Цикл
		Если ЗначениеЗаполнено(ДанныеСтроки.КатегорияЭксплуатации) Тогда
			СписокКатегорий.Добавить(ДанныеСтроки.КатегорияЭксплуатации);
		КонецЕсли;
	КонецЦикла;
	
	РеквизитыКатегорий = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(СписокКатегорий, "СпособПогашенияСтоимостиБУ,ИнвентарныйУчет");
	
	Возврат РеквизитыКатегорий;
	
КонецФункции

//-- НЕ УТ

#КонецОбласти

#КонецОбласти

#КонецЕсли
