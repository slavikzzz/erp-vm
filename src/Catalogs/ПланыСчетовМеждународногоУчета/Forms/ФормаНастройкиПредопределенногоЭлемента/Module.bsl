#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Элементы.УчетВФункциональнойВалюте.СписокВыбора.Очистить();
	
	ПредставлениеЗначения = СтрШаблон(
		НСтр("ru = 'в валюте регламентированного учета организаций';
			|en = 'in local accounting currency of companies'"));
	Элементы.УчетВФункциональнойВалюте.СписокВыбора.Добавить(
		Перечисления.ВидыУчетаВФункциональнойВалюте.ВВалютеРегл, ПредставлениеЗначения);
	
	Если Справочники.ПланыСчетовМеждународногоУчета.ДоступенУчетВВалютеУпр() Тогда
		ПредставлениеЗначения = СтрШаблон(
			НСтр("ru = 'в валюте управленческого учета';
				|en = 'in management accounting currency'"));
		Элементы.УчетВФункциональнойВалюте.СписокВыбора.Добавить(
			Перечисления.ВидыУчетаВФункциональнойВалюте.ВВалютеУпр, ПредставлениеЗначения);
	КонецЕсли;
	
	МеждународныйПланСчетов = Справочники.ПланыСчетовМеждународногоУчета.Международный.ПолучитьОбъект(); 
		
	ЗаполнитьЗначенияСвойств(ЭтаФорма, МеждународныйПланСчетов);
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект,,,
		МеждународныйПланСчетов);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность И НЕ ЗавершениеРаботы Тогда
		Отказ = Истина;
		ОбработчикОтвета = Новый ОписаниеОповещения("ЗавершениеЗакрытияФормы", ЭтотОбъект);
		ПоказатьВопрос(ОбработчикОтвета, НСтр("ru = 'Данные были изменены. Сохранить изменения?';
												|en = 'The data was changed. Do you want to save the changes?'"), РежимДиалогаВопрос.ДаНетОтмена);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеЗакрытияФормы(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Если СохранитьДанные() Тогда
			Модифицированность = Ложь;
			Оповестить("Запись_ПланыСчетовМеждународногоУчета");
			Закрыть();
		КонецЕсли;
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьЗакрыть(Команда)
	
	Если СохранитьДанные() Тогда
		Модифицированность = Ложь;
		Оповестить("Запись_ПланыСчетовМеждународногоУчета");
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ОткрытьФорму("Справочник.ПланыСчетовМеждународногоУчета.Форма.РазблокированиеРеквизитов",,,,,,
		Новый ОписаниеОповещения("Подключаемый_РазрешитьРедактированиеРеквизитовОбъектаЗавершение", ЭтотОбъект),
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъектаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Массив") И Результат.Количество() > 0 Тогда
		ЗапретРедактированияРеквизитовОбъектовКлиент.УстановитьДоступностьЭлементовФормы(ЭтаФорма, Результат);
	КонецЕсли;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СохранитьДанные()
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат Ложь;
	КонецЕсли;

	ПланСчетов = Справочники.ПланыСчетовМеждународногоУчета.Международный.ПолучитьОбъект();
	ЗаполнитьЗначенияСвойств(ПланСчетов, ЭтаФорма, , "ЭтотОбъект");
	ПланСчетов.Записать();
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти
