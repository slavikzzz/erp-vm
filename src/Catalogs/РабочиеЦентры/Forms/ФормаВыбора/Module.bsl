
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	Если Параметры.Свойство("Отбор") Тогда 
		
		Если Параметры.Отбор.Свойство("Подразделение") Тогда
			
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
					Список,
					"ВидРабочегоЦентра.Подразделение",
					Параметры.Отбор.Подразделение);
			
			УстановитьПараметрыФункциональныхОпцийФормы(Новый Структура("Подразделение",
				Параметры.Отбор.Подразделение));
			
			Параметры.Отбор.Удалить("Подразделение");
			
		ИначеЕсли Параметры.Отбор.Свойство("ВидРабочегоЦентра") Тогда
			
			УстановитьПараметрыФункциональныхОпцийФормы(Новый Структура("Подразделение",
				ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Отбор.ВидРабочегоЦентра, "Подразделение")));
			
		КонецЕсли;

		Если Параметры.Отбор.Свойство("Участок") Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				Список, "Участок", Параметры.Отбор.Участок);
		КонецЕсли;
		
	КонецЕсли;
	
	Если Параметры.Свойство("ВыбиратьВариантНаладки") Тогда
		ВыбиратьВариантНаладки = Параметры.ВыбиратьВариантНаладки;
	Иначе
		ВыбиратьВариантНаладки = Ложь;
	КонецЕсли;
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	Если ВыбиратьВариантНаладки И ИспользуютсяВариантыНаладки(Элемент.ТекущиеДанные.ВидРабочегоЦентра) Тогда
		
		СтандартнаяОбработка = Ложь;
		
		НастройкиОтбора = Новый Структура;
		НастройкиОтбора.Вставить("Владелец", Элемент.ТекущиеДанные.ВидРабочегоЦентра);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Отбор", НастройкиОтбора);
		
		ДопПараметры = Новый Структура("РабочийЦентр", Значение);
		
		ОткрытьФорму("Справочник.ВариантыНаладки.ФормаВыбора",
			ПараметрыФормы,
			ВладелецФормы,
			,
			,
			,
			Новый ОписаниеОповещения("СписокВыборЗначенияЗавершение", ЭтотОбъект, ДопПараметры));
	
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СписокВыборЗначенияЗавершение(ВариантНаладки, ДопПараметры) Экспорт
	Если ВариантНаладки = Неопределено Тогда
		Закрыть(Неопределено);
	Иначе
		Закрыть(Новый Структура("РабочийЦентр, ВариантНаладки", ДопПараметры.РабочийЦентр, ВариантНаладки));
	КонецЕсли;
КонецПроцедуры

// Параметры:
//  ВидРабочегоЦентра - СправочникСсылка.ВидыРабочихЦентров
// 
// Возвращаемое значение:
//  Булево
&НаСервереБезКонтекста
Функция ИспользуютсяВариантыНаладки(ВидРабочегоЦентра)
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидРабочегоЦентра, "ИспользуютсяВариантыНаладки");
КонецФункции

#КонецОбласти