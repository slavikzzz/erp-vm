
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ЗаполнитьГдеУстановлена() Экспорт
	
	ТекстГдеУстановлена = Новый Массив;
		
	Если УстановленаМестнымНормативнымАктом Тогда
		
		СведенияОПунктеМестногоЗакона = Неопределено;
		Если ЗначениеЗаполнено(ОснованиеМестнойЛьготы) Тогда
			СведенияОПунктеМестногоЗакона = ЛьготыПоИмущественнымНалогамКлиентСервер.НормативныеДанныеОснованияЛьготы(
				ОснованиеМестнойЛьготы);
		КонецЕсли;	
		
		НаименованиеРегиона = АдресныйКлассификатор.НаименованиеРегионаПоКоду(КодРегиона);
		
		ТекстСсылкиНаПунктЗакона = "";
		Если СведенияОПунктеМестногоЗакона <> Неопределено 
			 И (ЗначениеЗаполнено(СведенияОПунктеМестногоЗакона.Статья)
			 Или ЗначениеЗаполнено(СведенияОПунктеМестногоЗакона.Часть)) Тогда
			
			СсылкаВЗаконе = Новый Массив;
			СсылкаВЗаконе.Добавить(СтрШаблон(Нстр("ru = 'ст.%1';
													|en = 'art. %1'"), СокрЛП(СведенияОПунктеМестногоЗакона.Статья)));
			Если ЗначениеЗаполнено(СведенияОПунктеМестногоЗакона.Часть) Тогда
				СсылкаВЗаконе.Добавить(СтрШаблон(Нстр("ru = 'ч.%1';
														|en = 'p. %1'"), СокрЛП(СведенияОПунктеМестногоЗакона.Часть)));
			КонецЕсли;	
			Если ЗначениеЗаполнено(СведенияОПунктеМестногоЗакона.Пункт) Тогда
				СсылкаВЗаконе.Добавить(СтрШаблон(Нстр("ru = 'п.%1';
														|en = 'cl. %1'"), СокрЛП(СведенияОПунктеМестногоЗакона.Пункт)));
			КонецЕсли;	
			Если ЗначениеЗаполнено(СведенияОПунктеМестногоЗакона.Подпункт) Тогда
				СсылкаВЗаконе.Добавить(СтрШаблон(Нстр("ru = 'пп.%1';
														|en = 'subcl. %1'"), СокрЛП(СведенияОПунктеМестногоЗакона.Подпункт)));
			КонецЕсли;	
			Если ЗначениеЗаполнено(СведенияОПунктеМестногоЗакона.Абзац) Тогда
				СсылкаВЗаконе.Добавить(СтрШаблон(Нстр("ru = 'абз.%1';
														|en = 'par. %1'"), СокрЛП(СведенияОПунктеМестногоЗакона.Абзац)));
			КонецЕсли;
			Если ЗначениеЗаполнено(СведенияОПунктеМестногоЗакона.Иное) Тогда
				СсылкаВЗаконе.Добавить(СокрЛП(СведенияОПунктеМестногоЗакона.Иное));
			КонецЕсли;
			
			ТекстСсылкиНаПунктЗакона = СтрСоединить(СсылкаВЗаконе, ", ");
			ТекстГдеУстановлена.Добавить(ТекстСсылкиНаПунктЗакона);
		
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТекстСсылкиНаПунктЗакона)
			 И Налог = ПредопределенноеЗначение("Перечисление.ВидыИмущественныхНалогов.ЗемельныйНалог") Тогда
			ТекстГдеУстановлена.Добавить(Нстр("ru = 'нормативного акта';
												|en = 'regulation'"));
		ИначеЕсли Налог = ПредопределенноеЗначение("Перечисление.ВидыИмущественныхНалогов.ЗемельныйНалог") Тогда
			ТекстГдеУстановлена.Добавить(Нстр("ru = 'Нормативный акт';
												|en = 'Regulation'"));
		ИначеЕсли ЗначениеЗаполнено(ТекстСсылкиНаПунктЗакона) Тогда
			ТекстГдеУстановлена.Добавить(Нстр("ru = 'закона';
												|en = 'law'"));
		Иначе
			ТекстГдеУстановлена.Добавить(Нстр("ru = 'Закон';
												|en = 'Law'"));
		КонецЕсли;
		
		Если Налог = Перечисления.ВидыИмущественныхНалогов.ЗемельныйНалог
			 И ЗначениеЗаполнено(МуниципальноеОбразование) Тогда
			СклоненнаяСтрокаМассив = ПолучитьСклоненияСтроки(МуниципальноеОбразование, "Л=ru_RU", "ПД=Родительный");
			ТекстГдеУстановлена.Добавить(СклоненнаяСтрокаМассив[0]);
		ИначеЕсли Налог = ПредопределенноеЗначение("Перечисление.ВидыИмущественныхНалогов.ЗемельныйНалог")
			 И ЗначениеЗаполнено(КодПоОКТМО) Тогда 
			ТекстГдеУстановлена.Добавить(
				СтрШаблон(Нстр("ru = 'мун. образования (ОКТМО %1)';
								|en = 'municipalities (RNCMT %1)'"), КодПоОКТМО));
		ИначеЕсли Налог = ПредопределенноеЗначение("Перечисление.ВидыИмущественныхНалогов.ЗемельныйНалог") 
			 И ЗначениеЗаполнено(НаименованиеРегиона) Тогда
			ТекстГдеУстановлена.Добавить(СтрШаблон(Нстр("ru = 'мун. образования, %1';
														|en = 'municipalities, %1'"), НаименованиеРегиона));
		ИначеЕсли Налог = ПредопределенноеЗначение("Перечисление.ВидыИмущественныхНалогов.ЗемельныйНалог") Тогда
			ТекстГдеУстановлена.Добавить(Нстр("ru = 'мун. образования';
												|en = 'municipalities'"));
		ИначеЕсли ЗначениеЗаполнено(НаименованиеРегиона) Тогда
			СклоненнаяСтрокаМассив = ПолучитьСклоненияСтроки(НаименованиеРегиона, "Л=ru_RU", "ПД=Родительный");
			ТекстГдеУстановлена.Добавить(СклоненнаяСтрокаМассив[0]);
		Иначе
			ТекстГдеУстановлена.Добавить(Нстр("ru = 'субъекта РФ';
												|en = 'RF territorial entity'"));
		КонецЕсли;	
		
	Иначе
		
		ОписаниеЛьгот = ЛьготыПоИмущественнымНалогамПовтИсп.ДанныеОЛьготахПоИмущественнымНалогам(Налог);
		ОписаниеЛьготы = ОписаниеЛьгот.ФедеральныеЛьготы[КодЛьготы];	
		Если ОписаниеЛьготы = Неопределено Тогда
			ТекстГдеУстановлена.Добавить(Нстр("ru = 'НК РФ';
												|en = 'Tax Code of the Russian Federation'"));
		Иначе	
			ТекстГдеУстановлена.Добавить(ОписаниеЛьготы.Основание);
		КонецЕсли;
		
	КонецЕсли;
	
	ГдеУстановлена = СтрСоединить(ТекстГдеУстановлена, " ");
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнениеДокументов.ЗаполнитьПоСтруктуре(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли;
	
	Если УстановленаМестнымНормативнымАктом Тогда
		Наименование = ЛьготыПоИмущественнымНалогамКлиентСервер.НаименованиеМестнойЛьготы(
			ЭтотОбъект,
			АдресныйКлассификатор.НаименованиеРегионаПоКоду(КодРегиона));
	ИначеЕсли Не УстановленаМестнымНормативнымАктом И ЗначениеЗаполнено(КодЛьготы) Тогда
		Наименование = ЛьготыПоИмущественнымНалогам.НаименованиеФедеральнойЛьготы(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если Не УстановленаМестнымНормативнымАктом Тогда
		МассивНепроверяемыхРеквизитов.Добавить("КодРегиона");
		МассивНепроверяемыхРеквизитов.Добавить("КодПоОКТМО");
		МассивНепроверяемыхРеквизитов.Добавить("ДатаНормативногоАкта");
		МассивНепроверяемыхРеквизитов.Добавить("НомерНормативногоАкта");
	КонецЕсли;	
	
	Если УстановленаМестнымНормативнымАктом И Налог <> Перечисления.ВидыИмущественныхНалогов.ЗемельныйНалог Тогда
		МассивНепроверяемыхРеквизитов.Добавить("КодПоОКТМО");
	КонецЕсли;
	
	Если ВидЛьготы <> Перечисления.ВидыЛьготПоИмущественнымНалогам.СнижениеСтавкиНаПроцент 
		 Или ВидЛьготы <> Перечисления.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаПроцент Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ПроцентУменьшения");
	КонецЕсли;	
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;

	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;

	ЗаполнитьГдеУстановлена();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли