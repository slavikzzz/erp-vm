#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// ТехнологияСервиса.ВыгрузкаЗагрузкаДанных

// Возвращает реквизиты справочника, которые образуют естественный ключ для элементов справочника.
//
// Возвращаемое значение:
//  Массив из Строка - имена реквизитов, образующих естественный ключ.
//
Функция ПоляЕстественногоКлюча() Экспорт
	
	Результат = Новый Массив;
	
	Результат.Добавить("Код");
	
	Возврат Результат;
	
КонецФункции

// Конец ТехнологияСервиса.ВыгрузкаЗагрузкаДанных

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура выполняет первоначальное заполнение классификатора.
Процедура НачальноеЗаполнение() Экспорт
	
	КлассификаторТаблица = Новый ТаблицаЗначений;
	КлассификаторТаблица.Колонки.Добавить("Код");
	КлассификаторТаблица.Колонки.Добавить("Наименование");
	
	НоваяСтрокаКлассификатора = КлассификаторТаблица.Добавить();
	НоваяСтрокаКлассификатора.Код = "1";
	НоваяСтрокаКлассификатора.Наименование = НаименованиеСтепениЧитаетИПереводит();
	
	НоваяСтрокаКлассификатора = КлассификаторТаблица.Добавить();
	НоваяСтрокаКлассификатора.Код = "2";
	НоваяСтрокаКлассификатора.Наименование = НаименованиеСтепениЧитаетИОбъясняется();
	
	НоваяСтрокаКлассификатора = КлассификаторТаблица.Добавить();
	НоваяСтрокаКлассификатора.Код = "3";
	НоваяСтрокаКлассификатора.Наименование = НаименованиеСтепениВладеетСвободно();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СтепениЗнанияЯзыка.Наименование
	               |ИЗ
	               |	Справочник.СтепениЗнанияЯзыка КАК СтепениЗнанияЯзыка";
	
	ТаблицаСуществующих = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаКлассификатора Из КлассификаторТаблица Цикл
		Если ТаблицаСуществующих.Найти(СтрокаКлассификатора.Наименование,"Наименование")  = Неопределено Тогда
			СправочникОбъект = Справочники.СтепениЗнанияЯзыка.СоздатьЭлемент();
			ЗаполнитьЗначенияСвойств(СправочникОбъект, СтрокаКлассификатора);
			СправочникОбъект.ДополнительныеСвойства.Вставить("ЗаписьОбщихДанных");
			СправочникОбъект.Записать();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция НаименованиеСтепениЧитаетИПереводит() Экспорт
	
	Возврат НСтр("ru = 'Читает и переводит со словарем';
				|en = 'Reads and translates with a dictionary'");
	
КонецФункции

Функция НаименованиеСтепениЧитаетИОбъясняется() Экспорт
	
	Возврат НСтр("ru = 'Читает и может объясняться';
				|en = 'Reads and speaks quite well'");
	
КонецФункции

Функция НаименованиеСтепениВладеетСвободно() Экспорт
	
	Возврат НСтр("ru = 'Владеет свободно';
				|en = 'Speaks fluently'");
	
КонецФункции

#КонецОбласти

#КонецЕсли