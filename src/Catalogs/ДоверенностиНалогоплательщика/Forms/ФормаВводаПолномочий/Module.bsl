
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеЭУ();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// определяем возможные полномочия в таблице
	ОпределитьПолномочиеВТаблице("02", НСтр("ru = 'представлять налоговую декларацию, другую отчетность';
											|en = 'submit tax declarations and other reporting documents'"));
	ОпределитьПолномочиеВТаблице("03", НСтр("ru = 'получать документы в налоговом органе';
											|en = 'receive documents at tax authorities'"));
	ОпределитьПолномочиеВТаблице("05", НСтр("ru = 'подписывать документы, используемые при постановке на учет и снятии с учета и сообщении сведений, установленных Налоговым кодексом Российской Федерации';
											|en = 'sign documents used for registration, deregistration, and submission of information according to the Tax Code of the Russian Federation'"));
	ОпределитьПолномочиеВТаблице("06", НСтр("ru = 'представлять документы, используемые при постановке на учет и снятии с учета и сообщении сведений, установленных Налоговым кодексом Российской Федерации';
											|en = 'submit documents used for registration, deregistration, and submission of information according to the Tax Code of the Russian Federation'"));
	ОпределитьПолномочиеВТаблице("07", НСтр("ru = 'получать от налогового органа документы, подтверждающие постановку на учет и снятие с учета';
											|en = 'receive documents that confirm registration and deregistration from tax authorities'"));
	ОпределитьПолномочиеВТаблице("08", НСтр("ru = 'подписывать документы, используемые при учете банковских счетов (в том числе открытых в банках и иных организациях финансового рынка за пределами территории Российской Федерации) и электронных средств платежа';
											|en = 'sign documents used to keep record of bank accounts (including those opened in banks and other financial market organizations outside the territory of the Russian Federation) and electronic payment facilities'"));
	ОпределитьПолномочиеВТаблице("09", НСтр("ru = 'представлять документы, используемые при учете банковских счетов (в том числе открытых в банках и иных организациях финансового рынка за пределами территории Российской Федерации) и электронных средств платежа';
											|en = 'submit documents used to keep record of bank accounts (including those opened in banks and other financial market organizations outside the territory of the Russian Federation) and electronic payment facilities'"));
	ОпределитьПолномочиеВТаблице("10", НСтр("ru = 'представлять документы по применению специальных налоговых режимов, документы по налогу на игорный бизнес';
											|en = 'submit documents on the use of special taxation treatments and gambling tax documents'"));
	ОпределитьПолномочиеВТаблице("11", НСтр("ru = 'получать в налоговом органе документы по применению специальных налоговых режимов, документы по налогу на игорный бизнес';
											|en = 'receive documents on the use of special taxation treatments and gambling tax documents at tax authorities'"));
	ОпределитьПолномочиеВТаблице("17", НСтр("ru = 'подписывать заявление на зачет/возврат налога';
											|en = 'sign applications for tax reimbursement/return'"));
	ОпределитьПолномочиеВТаблице("18", НСтр("ru = 'подписывать заявления о ввозе товаров и уплате косвенных налогов';
											|en = 'sign applications for goods import and indirect tax payment'"));
	ОпределитьПолномочиеВТаблице("19", НСтр("ru = 'получать заявления о ввозе товаров и уплате косвенных налогов';
											|en = 'receive applications for goods import and indirect tax payment'"));
	ОпределитьПолномочиеВТаблице("20", НСтр("ru = 'подписывать документы по применению специальных налоговых режимов, документы по налогу на игорный бизнес';
											|en = 'sign documents on the use of special taxation treatments and gambling tax documents'"));
	ОпределитьПолномочиеВТаблице("21", НСтр("ru = 'подписывать документы (информацию)';
											|en = 'sign documents (information)'"));
	ОпределитьПолномочиеВТаблице("22", НСтр("ru = 'представлять документы (информацию)';
											|en = 'submit documents (information)'"));
	ОпределитьПолномочиеВТаблице("23", НСтр("ru = 'подписывать, представлять и получать от налогового органа документы по регистрации (перерегистрации) контрольно-кассовой техники';
											|en = 'sign, submit and receive from tax authorities documents on the registration (reregistration) of cash registers'"));
	ОпределитьПолномочиеВТаблице("24", НСтр("ru = 'подписывать, представлять и получать от налогового органа документы по снятию контрольно-кассовой техники с регистрационного учета';
											|en = 'sign, submit and receive from tax authorities documents on the deregistration of cash registers'"));
	ОпределитьПолномочиеВТаблице("25", НСтр("ru = 'подписывать и представлять перечень автоматических устройств для расчетов';
											|en = 'sign and submit lists of automated payment machines and devices'"));
	ОпределитьПолномочиеВТаблице("28", НСтр("ru = 'подписывать и представлять отчет об операциях с товарами, подлежащими прослеживаемости, документы, содержащие реквизиты прослеживаемости, и иные документы (информацию) в рамках национальной системы прослеживаемости';
											|en = 'sign and submit a report on operations with goods subject to traceability, documents that contain traceability details, and other documents (information) within the national traceability system'"));
	ОпределитьПолномочиеВТаблице("29", НСтр("ru = 'получать от налогового органа квитанцию о присвоении регистрационного номера партии товара, подлежащего прослеживаемости';
											|en = 'receive a notice from the tax authority of assigning a registration number to the goods lot subject to traceability'"));
	ОпределитьПолномочиеВТаблице("30", НСтр("ru = 'получать документы, используемые при учете и контроле банковских счетов и электронных средств платежа';
											|en = 'receive documents used to keep records of and control bank accounts and electronic payment facilities'"));
	ОпределитьПолномочиеВТаблице("31", НСтр("ru = 'подписывать, получать и представлять документы и информацию в рамках проверки соблюдения валютного законодательства Российской Федерации';
											|en = 'sign, receive, and submit documents and information as a part of verification for compliance with the currency legislation of the Russian Federation'"));
	ОпределитьПолномочиеВТаблице("32", НСтр("ru = 'получать запросы налогового органа и представлять документы и информацию по запросам в соответствии с валютным законодательством Российской Федерации';
											|en = 'receive requests from the tax authority and submit documents and information upon requests in accordance with the currency legislation of the Russian Federation'"));
	ОпределитьПолномочиеВТаблице("33", НСтр("ru = 'подписывать и представлять отчеты по зарубежным счетам, получать уведомления налогового органа о необходимости представления исправленного (уточненного) отчета и (или) надлежащим образом оформленных подтверждающих документов';
											|en = 'sign and submit reports on foreign accounts, receive notifications from the tax authority that a corrected (updated) report and (or) properly executed supporting documents must be submitted'"));
	ОпределитьПолномочиеВТаблице("35", НСтр("ru = 'подписывать, получать и представлять документы и информацию в рамках производства по делам об административных правонарушениях за нарушения валютного законодательства Российской Федерации, в том числе процессуальные документы';
											|en = 'sign, receive, and submit documents and information upon proceedings on administrative offenses for violations of the currency legislation of the Russian Federation, including litigation documents'"));
	ОпределитьПолномочиеВТаблице("36", НСтр("ru = 'представлять заявление на получение доступа к Личному кабинету';
											|en = 'submit an application to access the personal account'"));
	ОпределитьПолномочиеВТаблице("37", НСтр("ru = 'подписывать и представлять согласие налогоплательщика (плательщика страховых взносов) на представление сведений, составляющих налоговую тайну, со стороны лица, раскрывающего сведения, составляющие налоговую тайну';
											|en = 'sign and submit the consent of a taxpayer (insurance contribution payer) for submitting information that constitutes a tax secret from a person who discloses the information that constitutes a tax secret'"));
	ОпределитьПолномочиеВТаблице("38", НСтр("ru = 'подписывать и представлять согласие налогоплательщика (плательщика страховых взносов) на представление сведений, составляющих налоговую тайну, со стороны лица, запрашивающего сведения, составляющие налоговую тайну';
											|en = 'sign and submit the consent of a taxpayer (insurance contribution payer) for submitting information that constitutes a tax secret from a person who requests the information that constitutes a tax secret'"));
	ОпределитьПолномочиеВТаблице("39", НСтр("ru = 'подписывать и представлять запрос на представление сведений, составляющих налоговую тайну';
											|en = 'sign and submit a request for submitting information that constitutes a tax secret'"));
	ОпределитьПолномочиеВТаблице("40", НСтр("ru = 'подписывать и направлять банковскую гарантию в налоговый орган';
											|en = 'sign and send a bank guarantee to the tax authority'"));
	
	// расставляем флажки
	ПолныеПолномочия = Параметры.ИсходныеДанныеСтроки.ПризнакПолныеПолномочия;
	Если НЕ ПолныеПолномочия Тогда
		Для Каждого СтрПолномочие Из Полномочия Цикл
			СтрПолномочие.Пометка = Параметры.ИсходныеДанныеСтроки["Признак" + СтрПолномочие.Код];
		КонецЦикла;
	КонецЕсли;
	
	// устанавливаем в исходное значение переключатель "Полные полномочия"
	ПереключательПолныеПолномочия = ?(ПолныеПолномочия, "0", "1");
	
	// инициализируем КПП
	ЭтоПБОЮЛ = НЕ РегламентированнаяОтчетностьВызовСервера.ЭтоЮридическоеЛицо(Параметры.Организация);
	Элементы.КПП.Видимость = НЕ ЭтоПБОЮЛ;
	КПП = Параметры.ИсходныеДанныеСтроки.КПП;
	
	// инициализируем ОКАТО
	ОКАТО = Параметры.ИсходныеДанныеСтроки.ОКАТО;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РеквизитПереключательПолныеПолномочияПриИзменении(Элемент)
	
	ПолныеПолномочия = ?(ПереключательПолныеПолномочия = "0", Истина, Ложь);
	УправлениеЭУ();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	Для Каждого Стр Из Полномочия Цикл
		Стр.Пометка = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	Для Каждого Стр Из Полномочия Цикл
		Стр.Пометка = Ложь;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ДействиеОК(Команда)
	
	Если НЕ СохранениеВозможно() Тогда
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ПризнакПолныеПолномочия", ?(ПереключательПолныеПолномочия = "0", Истина, Ложь));
	Результат.Вставить("ОКАТО", СокрЛП(ОКАТО));
	Результат.Вставить("КПП", СокрЛП(КПП));
	Для Каждого Стр Из Полномочия Цикл
		Результат.Вставить("Признак" + Стр.Код, ?(ПереключательПолныеПолномочия = "0", Ложь, Стр.Пометка));
	КонецЦикла;
	
	Закрыть(Результат);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УправлениеЭУ()
	
	Элементы.ГруппаТаблицаПолномочий.Доступность = НЕ ПолныеПолномочия;
	
КонецПроцедуры

&НаСервере
Процедура ОпределитьПолномочиеВТаблице(КодПолномочия, НаименованиеПолномочия)
	
	НовСтр = Полномочия.Добавить();
	НовСтр.Код = КодПолномочия;
	НовСтр.Наименование = НаименованиеПолномочия;
	
КонецПроцедуры

&НаКлиенте
Функция СохранениеВозможно()
	
	Отказ = Ложь;
	
	ОчиститьСообщения();
	
	// проверяем полномочия
	Если ПереключательПолныеПолномочия <> "0" Тогда
		СтрокиИстина = Полномочия.НайтиСтроки(Новый Структура("Пометка", Истина));
		Если СтрокиИстина.Количество() = 0 Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не выбран ни один вид полномочия.';
															|en = 'No power kinds are selected.'"), , "Полномочия", , Отказ);
		КонецЕсли;
	КонецЕсли;
	
	// проверяем ОКАТО
	СокрЛПОКАТО = СокрЛП(ОКАТО);
	Если СтрДлина(СокрЛПОКАТО) <> 0 И СтрДлина(СокрЛПОКАТО) <> 11 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Число цифр в ОКАТО должно быть равно 11.';
														|en = 'RNCPS must consist of 11 digits.'"), , "ОКАТО", , Отказ);
	ИначеЕсли НЕ СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(СокрЛПОКАТО) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'ОКАТО должен состоять только из цифр.';
														|en = 'RNCPS can contain digits only.'"), , "ОКАТО", , Отказ);
	КонецЕсли;
	
	// проверяем КПП
	Если Элементы.КПП.Видимость Тогда
		СокрЛПКПП = СокрЛП(КПП);
		Если СтрДлина(СокрЛПКПП) <> 0 И СтрДлина(СокрЛПКПП) <> 9 Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Число цифр в КПП должно быть равно 9.';
															|en = 'KPP must consist of 9 digits.'"), , "КПП", , Отказ);
		ИначеЕсли НЕ СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(СокрЛПКПП) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'КПП должен состоять только из цифр.';
															|en = 'KPP can contain digits only.'"), , "КПП", , Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Возврат НЕ Отказ;
	
КонецФункции

#КонецОбласти
