
#Область ОписаниеПеременных

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Организация = Параметры.Организация;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
// Код процедур и функций
#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

&НаКлиенте
Процедура ДоходыКонтагентПриИзменении(Элемент)
	
	ТекущиеДаннные = Элементы.Доходы.ТекущиеДанные;
	Если ТекущиеДаннные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущиеДаннные.Контагент) Тогда
		Свойства = ДокументооборотСКОВызовСервера.ПолучитьСвойстваКонтрагента(ТекущиеДаннные.Контагент);
		Если Свойства.ЭтоЮридическоеЛицо = Истина И ЗначениеЗаполнено(Свойства.КПП) Тогда
			ТекущиеДаннные.ИНН_КПП = Свойства.ИНН + "/" + Свойства.КПП;
		Иначе
			ТекущиеДаннные.ИНН_КПП = Свойства.ИНН;
		КонецЕсли;
	Иначе
		ТекущиеДаннные.ИНН_КПП = "";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачалоВыбораИзСправочника(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТребованияФНСКлиент.ВыбратьИзСправочника(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоходыСуммаПриИзменении(Элемент)
	ТребованияФНСКлиент.ПересчитатьИтог(ЭтотОбъект, Доходы, "Сумма", "Итого");
КонецПроцедуры

&НаКлиенте
Процедура ДоходыСуммаОчистка(Элемент, СтандартнаяОбработка)
	ТребованияФНСКлиент.ПересчитатьИтог(ЭтотОбъект, Доходы, "Сумма", "Итого");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьВПояснение(Команда)
	
	Результат = Новый Структура();
	
	ДобавитьЗначениеПараметраИзТаблицы(Результат, "Доходы", НСтр("ru = '- %Контагент (%ИНН_КПП), %Вид, %Сумма руб.';
																|en = '- %Контагент (%ИНН_КПП), %Вид, %Сумма руб.'"));
	ДобавитьЕдиничноеЗначениеПараметра(Результат, "Итого", Итого);
	
	Закрыть(Результат);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ДобавитьЗначениеПараметраИзТаблицы(Результат, ИмяТаблицы, Шаблон, Знач Замена = "")
	
	Значение = СформироватьПараметрИзТаблицы(ИмяТаблицы, Шаблон);
	ДобавитьЕдиничноеЗначениеПараметра(Результат, ИмяТаблицы, Значение);
		
КонецФункции

&НаКлиенте
Функция ДобавитьЕдиничноеЗначениеПараметра(Результат, Ключ, Значение)
	
	ТребованияФНСКлиентСервер.ДобавитьЗначениеПараметраШаблона(
		Результат, 
		Ключ, 
		Значение);
		
КонецФункции

&НаСервере
Функция СформироватьПараметрИзТаблицы(ИмяТаблицы, Знач Шаблон)
	
	Таблица = ЭтотОбъект[ИмяТаблицы];
	Возврат Справочники.ШаблоныПоясненийДляФНС.СформироватьПараметрИзТаблицы(Таблица, Шаблон);

КонецФункции

#КонецОбласти

