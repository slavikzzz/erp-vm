
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если ЗначениеЗаполнено(Объект.ИмяМакета) Тогда 
		Элементы.ВосстановитьТиповойШаблон.Видимость = Истина;
	Иначе 
		Элементы.ВосстановитьТиповойШаблон.Видимость = Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		СсылкаНаШаблон = Объект.Ссылка;
	ИначеЕсли Параметры.Свойство("ЗначениеКопирования")
			И ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда 
		СсылкаНаШаблон = Параметры.ЗначениеКопирования;
	КонецЕсли;
	
	ТекстЗагружен = Справочники.ШаблоныПоясненийДляФНС.УстановитьТекстВФорматированныйДокумент(
		СсылкаНаШаблон, 
		Шаблон,
		"ШаблонХранилище");

	Если НЕ ТекстЗагружен И ЗначениеЗаполнено(Объект.ИмяМакета) Тогда
		
		Макет = Справочники.ШаблоныПоясненийДляФНС.ПолучитьМакет(Объект.ИмяМакета);
		Вложения = Новый Структура();
		Шаблон.УстановитьHTML(Макет.ПолучитьТекст(), Вложения);
		
	КонецЕсли;
	
	Оранжевый = ЦветаСтиля.ЦветАктивности;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Справочники.ШаблоныПоясненийДляФНС.СохранитьФорматированныйДокумент(
		ТекущийОбъект, 
		Шаблон, 
		"ШаблонХранилище");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ШаблонПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НаименованиеОрганизации(Команда)
	ВставитьПараметр("{НаименованиеОрганизации}");
КонецПроцедуры

&НаКлиенте
Процедура Сегодня(Команда)
	ВставитьПараметр("{ДатаСегодня}");
КонецПроцедуры

&НаКлиенте
Процедура Руководитель(Команда)
	ВставитьПараметр("{Руководитель}");
КонецПроцедуры

&НаКлиенте
Процедура ДолжностьРуководителя(Команда)
	ВставитьПараметр("{ДолжностьРуководителя}");
КонецПроцедуры

&НаКлиенте
Процедура ИНН(Команда)
	ВставитьПараметр("{ИНН}");
КонецПроцедуры

&НаКлиенте
Процедура КПП(Команда)
	ВставитьПараметр("{КПП}");
КонецПроцедуры

&НаКлиенте
Процедура ОГРН(Команда)
	ВставитьПараметр("{ОГРН}");
КонецПроцедуры

&НаКлиенте
Процедура Адрес(Команда)
	ВставитьПараметр("{Адрес}");
КонецПроцедуры

&НаКлиенте
Процедура Телефон(Команда)
	ВставитьПараметр("{Телефон}");
КонецПроцедуры

&НаКлиенте
Процедура Требование(Команда)
	ВставитьПараметр("{Требование}");
КонецПроцедуры

&НаКлиенте
Процедура ИФНС(Команда)
	ВставитьПараметр("{ИФНС}");
КонецПроцедуры

&НаКлиенте
Процедура Исполнитель(Команда)
	ВставитьПараметр("{Исполнитель}");
КонецПроцедуры

&НаКлиенте
Процедура ВернутьШаблон(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ВосстановитьТиповойШаблонЗавершение", ЭтотОбъект);

	ТекстВопроса = НСтр("ru = 'Заменить текущий шаблон договора на типовой?';
						|en = 'Заменить текущий шаблон договора на типовой?'");
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВставитьПараметр(Значение)
	
	Структура = ТребованияФНСКлиентСервер.ШаблонЗначенияШаблона(Значение);
	
	ТребованияФНСКлиент.ВставитьПараметр(
		ЭтотОбъект,
		"Шаблон",
		Структура);
		
КонецПроцедуры

&НаСервере
Процедура ВосстановитьТиповойШаблонНаСервере()
	
	Если НЕ ЗначениеЗаполнено(Объект.ИмяМакета) Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		Макет = Справочники.ШаблоныПоясненийДляФНС.ПолучитьМакет(Объект.ИмяМакета);
	Исключение
		ТекстОшибки = НСтр("ru = 'Не удалось восстановить стандартный шаблон %1, возможно он устарел. Для создания шаблона на основе стандартного воспользуйтесь пунктом меню «Создать»-«На основе стандартного шаблона» в списке шаблонов.';
							|en = 'Не удалось восстановить стандартный шаблон %1, возможно он устарел. Для создания шаблона на основе стандартного воспользуйтесь пунктом меню «Создать»-«На основе стандартного шаблона» в списке шаблонов.'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, Объект.ИмяМакета);
		
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Возврат;
	КонецПопытки; 
	
	ТекстHTML = Макет.ПолучитьТекст();
	Вложения = Новый Структура;
	Шаблон.УстановитьHTML(ТекстHTML, Вложения);
	
	Модифицированность = Истина;

КонецПроцедуры

&НаКлиенте
Процедура ВосстановитьТиповойШаблонЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт

	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда

		ВосстановитьТиповойШаблонНаСервере();

	КонецЕсли;

КонецПроцедуры


#КонецОбласти
