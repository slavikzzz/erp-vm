#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОграничиватьОтбор = Параметры.Свойство("ВидОперации")
		И ЗначениеЗаполнено(Параметры.ВидОперации);
		
	БлокироватьОтбор = ОграничиватьОтбор
		И (Параметры.ВидОперации = Перечисления.ВидыОперацийРезервовПоОплатеТруда.Начисление
			Или Параметры.ВидОперации = Перечисления.ВидыОперацийРезервовПоОплатеТруда.Инвентаризация);
			
	Если ОграничиватьОтбор Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ТолькоРезервыПоОплатеТруда", Истина);
	КонецЕсли;
	
	Если Параметры.Свойство("МесяцНачисления")
		И ЗначениеЗаполнено(Параметры.МесяцНачисления) Тогда
		
		Если БлокироватьОтбор Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "МесяцНачисления", Параметры.МесяцНачисления);
		Иначе
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				Список, "НачалоПериода", Параметры.МесяцНачисления, ВидСравненияКомпоновкиДанных.МеньшеИлиРавно, , Истина, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Обычный);
				
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				Список, "КонецПериода", Параметры.МесяцНачисления, ВидСравненияКомпоновкиДанных.БольшеИлиРавно, , Истина, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Обычный);
		КонецЕсли;
	КонецЕсли;

	Если (Не Параметры.Отбор.Свойство("Организация") Или Не ЗначениеЗаполнено(Параметры.Отбор.Организация)) Тогда
		
		Если ОбщегоНазначенияКлиентСервер.НайтиЭлементыИГруппыОтбора(Список.Отбор, "Ссылка").Количество() = 0 Тогда
			
			ОбщегоНазначения.СообщитьПользователю(
				НСтр("ru = 'Необходимо выбрать организацию';
					|en = 'Select a company'"), , , , Отказ);
			
			Возврат;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("Организация") И ЗначениеЗаполнено(Параметры.Отбор.Организация)Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, "Организация", Параметры.Отбор.Организация, , , Истина, 
			?(БлокироватьОтбор, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Обычный));
		
		Параметры.Отбор.Удалить("Организация");
		
	КонецЕсли;
	
	СтруктураПараметровОтбора = Новый Структура();
	ЗарплатаКадры.ПриСозданииНаСервереФормыСДинамическимСписком(ЭтотОбъект, "Список",,
		СтруктураПараметровОтбора, "СписокКритерииОтбора");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки, ИспользуютсяСтандартныеНастройки)
	ЗарплатаКадры.ПроверитьПользовательскиеНастройкиДинамическогоСписка(ЭтотОбъект, Настройки);
КонецПроцедуры

&НаСервере
Процедура СписокПриОбновленииСоставаПользовательскихНастроекНаСервере(СтандартнаяОбработка)
	
	ЗарплатаКадры.ПроверитьПользовательскиеНастройкиДинамическогоСписка(ЭтотОбъект, , СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти
