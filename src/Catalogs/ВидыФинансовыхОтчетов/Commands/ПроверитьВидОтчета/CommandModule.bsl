
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Форма = ПараметрыВыполненияКоманды.Источник; // ФормаКлиентскогоПриложения - 
	Если Форма.ИмяФормы = "Справочник.ВидыФинансовыхОтчетов.Форма.ФормаЭлемента" Тогда
		Элемент = Форма.Объект; // ДанныеФормыСтруктура -
		ВидОтчета = Элемент.Ссылка;
		КомплектОтчетности = Форма.КомплектОтчетности;
		Если НЕ ЗначениеЗаполнено(ВидОтчета) ИЛИ Форма.Модифицированность Тогда
			ПоказатьВопрос(
				Новый ОписаниеОповещения("ОбработкаКомандыЗавершение", 
					ЭтотОбъект,
					Новый Структура("Форма, КомплектОтчетности, ВидОтчета", Форма, КомплектОтчетности, ВидОтчета)),
				НСтр("ru = 'Перед проверкой не сохраненные данные будут записаны.
							|Продолжить?';
							|en = 'Unsaved data will be saved before check.
							|Continue?'"),
				РежимДиалогаВопрос.ДаНет);
			Возврат;
		КонецЕсли;// данные не записаны
	КонецЕсли;// это форма элемента
	
	СформироватьОтчет(Форма, КомплектОтчетности, ВидОтчета);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработкаКомандыЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт

	Форма = ДополнительныеПараметры.Форма; // РасширениеУправляемойФормыДляСправочника - 
	КомплектОтчетности = ДополнительныеПараметры.КомплектОтчетности;
	ВидОтчета = ДополнительныеПараметры.ВидОтчета;
	Ответ = РезультатВопроса;

	Если Ответ = КодВозвратаДиалога.Да Тогда
		Форма.Записать();
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;

	СформироватьОтчет(Форма, КомплектОтчетности, ВидОтчета);

КонецПроцедуры

&НаКлиенте
Процедура СформироватьОтчет(Знач Форма, Знач КомплектОтчетности, Знач ВидОтчета)

	Если Форма.ИмяФормы = "Справочник.ВидыФинансовыхОтчетов.Форма.ФормаСписка" Тогда
		ТекущиеДанные = Форма.Элементы.Список.ТекущиеДанные;
		Если ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		Если НЕ ТекущиеДанные.ЭтоГруппа Тогда
			ВидОтчета = Форма.Элементы.Список.ТекущаяСтрока;
		КонецЕсли;// это не группа
	КонецЕсли;// это форма списка

	Если ВидОтчета <> Неопределено Тогда
		Отбор = Новый Структура("КомплектОтчетности, ВидОтчета", КомплектОтчетности, ВидОтчета);
		ПараметрыФормы = Новый Структура("Отбор, СформироватьОтчет", Отбор, Истина);
		ОткрытьФорму("Отчет.МеждународныйОтчет.Форма.ФормаОтчета", ПараметрыФормы, Форма, Истина);
	КонецЕсли;
    
КонецПроцедуры

#КонецОбласти