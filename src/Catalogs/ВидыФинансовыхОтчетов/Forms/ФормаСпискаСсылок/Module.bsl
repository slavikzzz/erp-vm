
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.РежимУдаления Тогда
		Заголовок = НСтр("ru = 'Удаление элемента отчета';
						|en = 'Remove report item'");
		Элементы.КнопкаОтмена.Заголовок = НСтр("ru = 'Отмена';
												|en = 'Cancel'");
	КонецЕсли;
	
	ДействиеСкопировать = 0;
	ДействиеПеренести = 1;
	ДействиеУдалить = 2;
	ОсновноеДействие = ДействиеСкопировать;
	
	ЭлементОтчета = Параметры.ЭлементОтчета;
	НаименованиеЭлемента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭлементОтчета, "НаименованиеДляПечати");
	РежимУдаления = Параметры.РежимУдаления;
	УдалитьВсе = Параметры.УдалитьВсе;
	ОбновитьДеревоСсылок();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьДоступность();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СкопироватьВсемПриИзменении(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиПриИзменении(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитВсеПриИзменении(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоссылокэлемента

&НаКлиенте
Процедура ДеревоСсылокЭлементаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура("Ключ",Элемент.ТекущиеДанные.ВидОтчета);
	ПараметрыФормы.Вставить("ТекущийЭлементОтчета", Элемент.ТекущиеДанные.ЭлементОтчета);
	ОткрытьФорму("Справочник.ВидыФинансовыхОтчетов.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСсылокЭлементаПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаВыполнить(Команда)
	
	Ответ = Неопределено;

	
	ПоказатьВопрос(Новый ОписаниеОповещения("КомандаВыполнитьЗавершение", ЭтотОбъект), НСтр("ru = 'Действие необратимо. Продолжить??';
																							|en = 'Action cannot be undone. Continue?'"),РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаВыполнитьЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    Ответ = РезультатВопроса;
    Если Ответ <> КодВозвратаДиалога.Да Тогда
        Возврат;
    КонецЕсли;
    
    ВыбранныйЭлемент = Неопределено;
    Если ОсновноеДействие = ДействиеПеренести Тогда
        Если Элементы.ДеревоСсылокЭлемента.ТекущаяСтрока = Неопределено Тогда
            ПоказатьПредупреждение(Неопределено, НСтр("ru = 'Укажите отчет новый источник ссылок.';
														|en = 'Specify a report new reference source.'"));
            Возврат;
        КонецЕсли;
        ТекущиеДанные = Элементы.ДеревоСсылокЭлемента.ТекущиеДанные;
        ВыбранныйЭлемент = ТекущиеДанные.ЭлементОтчета;
        Если НЕ ЗначениеЗаполнено(ВыбранныйЭлемент) Тогда
            ПодчиненныеСтроки = ФинансоваяОтчетностьКлиент.ПодчиненныеСтрокиЭлементаФормы(ТекущиеДанные);
            ВыбранныйЭлемент = ПодчиненныеСтроки[0].ЭлементОтчета;
        КонецЕсли;
    КонецЕсли;
    ВыполнитьДействие(ВыбранныйЭлемент);
    
    Закрыть(Истина);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПриИзмененииРеквизитов

&НаКлиенте
Процедура УстановитьДоступность()
	
	Если УдалитьВсе Тогда
		ОсновноеДействие = ДействиеУдалить;
		Элементы.СкопироватьВсем.Доступность = Ложь;
		Элементы.Перенести.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаСервере
Процедура ВыполнитьДействие(ВыбранныйЭлемент = Неопределено)
	
	// в выбранный элемент скопируем данные оригинала и разорвем связь
	Если ВыбранныйЭлемент <> Неопределено Тогда
		НовыйОригинал = ВыбранныйЭлемент.ПолучитьОбъект();
		ЗаполнитьДанныеЭлемента(НовыйОригинал);
		НовыйОригинал.СвязанныйЭлемент = Неопределено;
		НовыйОригинал.Записать();
	КонецЕсли;
	
	ВидыОтчетов = ФинансоваяОтчетностьСервер.ПодчиненныеСтроки(ДеревоСсылокЭлемента);
	Для Каждого ВидОтчета Из ВидыОтчетов Цикл
		ЭлементыОтчета = ФинансоваяОтчетностьСервер.ПодчиненныеСтроки(ВидОтчета);
		Для Каждого СсылкаЭлемента Из ЭлементыОтчета Цикл
			
			Если ВыбранныйЭлемент <> Неопределено И СсылкаЭлемента.ЭлементОтчета = ВыбранныйЭлемент Тогда
				Продолжить;
			КонецЕсли;
			
			ОбъектЭлемента = СсылкаЭлемента.ЭлементОтчета.ПолучитьОбъект();
			Если ОсновноеДействие = ДействиеУдалить Тогда
				ОбъектЭлемента.УстановитьПометкуУдаления(Истина);
			КонецЕсли;
			ОбъектЭлемента.СвязанныйЭлемент = ВыбранныйЭлемент;
			Если ОсновноеДействие = ДействиеСкопировать Тогда
				ЗаполнитьДанныеЭлемента(ОбъектЭлемента);
			КонецЕсли;
			ОбъектЭлемента.Записать();
			
		КонецЦикла;// по элементам одного вида отчета
	КонецЦикла;// по видам отчетов
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеЭлемента(ОбъектПриемник)
	
	ЗаполнитьЗначенияСвойств(ОбъектПриемник, ЭлементОтчета,,"Код,Владелец,Родитель,НаименованиеДляПечати,Комментарий");
	
	ВыводитьЗаголовок = ОбъектПриемник.РеквизитыВидаЭлемента.Найти(ПланыВидовХарактеристик.РеквизитыЭлементовФинансовыхОтчетов.ВыводитьЗаголовокЭлемента);
	Если ВыводитьЗаголовок <> Неопределено Тогда
		ОбъектПриемник.РеквизитыВидаЭлемента.Удалить(ВыводитьЗаголовок);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	РеквизитыВидаЭлемента.Ссылка КАК Ссылка,
	|	РеквизитыВидаЭлемента.НомерСтроки КАК НомерСтроки,
	|	РеквизитыВидаЭлемента.Реквизит КАК Реквизит,
	|	РеквизитыВидаЭлемента.Значение КАК Значение
	|ИЗ
	|	Справочник.ЭлементыФинансовыхОтчетов.РеквизитыВидаЭлемента КАК РеквизитыВидаЭлемента
	|ГДЕ
	|	(НЕ (РеквизитыВидаЭлемента.Реквизит.ИмяПредопределенныхДанных = ""КодСтрокиОтчета""
	|	ИЛИ РеквизитыВидаЭлемента.Реквизит.ИмяПредопределенныхДанных = ""Примечание"")
	|	И РеквизитыВидаЭлемента.Ссылка = &ЭлементФинансовыхОтчетовСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОперандыФормулы.НомерСтроки КАК НомерСтроки,
	|	ОперандыФормулы.Ссылка КАК Ссылка,
	|	ОперандыФормулы.Идентификатор КАК Идентификатор,
	|	ОперандыФормулы.Операнд КАК Операнд
	|ИЗ
	|	Справочник.ЭлементыФинансовыхОтчетов.ОперандыФормулы КАК ОперандыФормулы
	|ГДЕ
	|	ОперандыФормулы.Ссылка = &ЭлементФинансовыхОтчетовСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЭлементыТаблицы.НомерСтроки КАК НомерСтроки,
	|	ЭлементыТаблицы.Ссылка КАК Ссылка,
	|	ЭлементыТаблицы.Строка КАК Строка,
	|	ЭлементыТаблицы.Колонка КАК Колонка,
	|	ЭлементыТаблицы.Элемент КАК Элемент
	|ИЗ
	|	Справочник.ЭлементыФинансовыхОтчетов.ЭлементыТаблицы КАК ЭлементыТаблицы
	|ГДЕ
	|	ЭлементыТаблицы.Ссылка = &ЭлементФинансовыхОтчетовСсылка";
	
	Запрос.УстановитьПараметр("ЭлементФинансовыхОтчетовСсылка", ЭлементОтчета);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ДопРеквизиты = МассивРезультатов[0].Выгрузить(); //РеквизитыВидаЭлемента
	Для Каждого Реквизит Из ДопРеквизиты Цикл
		НоваяСтрока = ОбъектПриемник.РеквизитыВидаЭлемента.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Реквизит);
	КонецЦикла;
	ОбъектПриемник.ОперандыФормулы.Загрузить(МассивРезультатов[1].Выгрузить().Выгрузить());
	ОбъектПриемник.ЭлементыТаблицы.Загрузить(МассивРезультатов[2].Выгрузить().Выгрузить());
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДеревоСсылок()
	
	ПараметрыДерева = МеждународнаяОтчетностьКлиентСервер.НовыеПараметрыДереваЭлементов();
	ПараметрыДерева.ИмяЭлементаДерева = "ДеревоСсылокЭлемента";
	ПараметрыДерева.Вставить("ЭлементОтчета", Параметры.ЭлементОтчета);
	ПараметрыДерева.Вставить("СчетПланаСчетов", Параметры.СчетПланаСчетов);
	
	МеждународнаяОтчетностьСервер.ОбновитьДеревоСсылокЭлемента(ЭтаФорма, ПараметрыДерева);
	
	КоличествоСсылок = ПараметрыДерева.КоличествоСсылок;
	ШаблонТекста = НСтр("ru = 'На элемент ""%1""
						|обнаружены ссылки в других отчетах (%2 шт).
						|Выберите действие:';
						|en = 'References to the ""%1""
						|item are found in other reports (%2).
						|Select action:'");
	
	Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста, НаименованиеЭлемента, КоличествоСсылок);
	Элементы.Пояснение.Заголовок = Текст;
	
	Элементы.Перенести.ТолькоПросмотр = КоличествоСсылок = 1;
	Если Элементы.Перенести.ТолькоПросмотр Тогда
		ОсновноеДействие = ДействиеСкопировать;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
