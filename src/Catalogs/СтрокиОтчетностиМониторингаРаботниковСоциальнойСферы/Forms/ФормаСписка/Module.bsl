
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.РежимВыбора Тогда
		Элементы.Список.РежимВыбора = Истина;
		ЗначениеПараметра = Неопределено;
		ФормаМониторинга = Неопределено;
		Параметры.Свойство("ФормаМониторинга", ФормаМониторинга);
		Если НЕ ЗначениеЗаполнено(ФормаМониторинга)
			И Параметры.Свойство("Свойство",ЗначениеПараметра) Тогда
			ИмяСвойства = СтрЗаменить(ЗначениеПараметра.Наименование,"-","");
			Для Каждого ВидФормыМониторинга Из Перечисления.ВидыФормМониторингаРаботниковСоциальнойСферы Цикл
				Если СтрНайти(ИмяСвойства, ОбщегоНазначения.ИмяЗначенияПеречисления(ВидФормыМониторинга)) > 0 Тогда
					ФормаМониторинга = ВидФормыМониторинга;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(ФормаМониторинга) Тогда
			НастройкиСтатистики = СтатистикаПерсоналаРасширенный.НастройкиСтатистикиПерсонала();
			ФормаМониторинга = Новый Массив();
			Если НастройкиСтатистики.ИспользоватьОтчетностьМониторингаРаботниковСоциальнойСферы Тогда
				Для Каждого ФормаСтатистики Из НастройкиСтатистики Цикл
					ИмяФормыЗП = СтрЗаменить(ФормаСтатистики.Ключ,"Форма","");
					Если ИмяФормыЗП = "ИспользоватьОтчетностьМониторингаРаботниковСоциальнойСферы" Тогда
						Продолжить;
					ИначеЕсли ФормаСтатистики.Значение = Истина Тогда
						ФормаМониторинга.Добавить(ОбщегоНазначения.ПредопределенныйЭлемент("Перечисление.ВидыФормМониторингаРаботниковСоциальнойСферы."+ИмяФормыЗП));
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ФормаМониторинга) Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
				Список.Отбор,
				"ФормаМониторинга",
				ФормаМониторинга);
		КонецЕсли;
			
	КонецЕсли;
	
	ЗарплатаКадры.ПриСозданииНаСервереФормыСДинамическимСписком(ЭтотОбъект, "Список");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки)
	ЗарплатаКадры.ПроверитьПользовательскиеНастройкиДинамическогоСписка(ЭтотОбъект, Настройки);
КонецПроцедуры

&НаСервере
Процедура СписокПриОбновленииСоставаПользовательскихНастроекНаСервере(СтандартнаяОбработка)
	
	ЗарплатаКадры.ПроверитьПользовательскиеНастройкиДинамическогоСписка(ЭтотОбъект, , СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти
