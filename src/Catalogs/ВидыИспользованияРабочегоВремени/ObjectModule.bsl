#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	СтандартнаяОбработка = Истина;
	УчетРабочегоВремени.ВидыИспользованияРабочегоВремениОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, СтандартнаяОбработка);
	
	Если СтандартнаяОбработка И Не ПустаяСтрока(БуквенныйКод) Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.УстановитьПараметр("БуквенныйКод", БуквенныйКод);
		
		Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	ВидыИспользованияРабочегоВремени.Наименование,
			|	ВидыИспользованияРабочегоВремени.Ссылка
			|ИЗ
			|	Справочник.ВидыИспользованияРабочегоВремени КАК ВидыИспользованияРабочегоВремени
			|ГДЕ
			|	ВидыИспользованияРабочегоВремени.Ссылка <> &Ссылка
			|	И ВидыИспользованияРабочегоВремени.БуквенныйКод = &БуквенныйКод";
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Буквенный код ""%1"" уже используется для вида времени: ""%2"".';
					|en = 'The ""%1"" letter code is already used for time kind: ""%2"".'"), 
				БуквенныйКод, 
				Выборка.Наименование);
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "БуквенныйКод", "Объект", Отказ);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли