
&НаКлиенте
Перем КонтекстЭДОКлиент;

&НаКлиенте
Перем МассивВыбранныхДокументов;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МаксКолвоФайлов 	= ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.МаксимальнойКоличествоФайловОписи();
	МаксРазмерФайлов 	= 72*1024*1024;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ОтметитьКакПрочтенное(Объект.Ссылка);
		Заголовок = Объект.Наименование;
		ОпределитьЭтоПакетСДопДокументами(ЭтотОбъект);
		ЗаполнитьДеревоДокументов(Объект.Ссылка);
		
	Иначе
	
		Объект.ДатаСоздания = ТекущаяДатаСеанса();
		ЗаполнитьОрганизациюИзПараметров(Параметры);
		
		// Основание
		Если Не ЗначениеЗаполнено(Объект.Основание) Тогда 
			Параметры.Свойство("ОснованиеПакета", Объект.Основание);
		КонецЕсли;
		
		ОпределитьЭтоПакетСДопДокументами(ЭтотОбъект);
		Если ЗначениеЗаполнено(Объект.Основание) Тогда
			ОснованиеПриИзмененииНаСервере();
		КонецЕсли;
		
		// Копируем табличную часть.
		Если Параметры.Свойство("ЗначениеКопирования") И ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
			ЗаполнитьДеревоДокументов(Параметры.ЗначениеКопирования, Истина);
		КонецЕсли;
		
		УстановитьМодифицированность(ЭтотОбъект);
		
	КонецЕсли;
	
	Элементы.ПредставляемыеДокументы.НачальноеОтображениеДерева = НачальноеОтображениеДерева.РаскрыватьВсеУровни;
	
	СформироватьСоставМенюДобавитьИзБазы();
	
	УправлениеСтатусомИДоступностью();
	
	ОпределитьВложенияИРазмерВложений(Параметры);
	
	ИзменитьОформлениеФормы();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОрганизациюИзПараметров(Параметры)
	
	Если Параметры.Свойство("ЗначенияЗаполнения") 
		И ТипЗнч(Параметры.ЗначенияЗаполнения) = Тип("Структура") Тогда
		
		Если Параметры.ЗначенияЗаполнения.Свойство("Организация") Тогда
			Объект.Организация = Справочники.Организации.ПустаяСсылка();
			ОрганизацияОтбор = Параметры.ЗначенияЗаполнения.Организация;
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		Параметры.Свойство("Организация", Объект.Организация);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовок()
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	Заголовок = КонтекстЭДОСервер.НаименованиеОписиИсхДок(Объект);
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Заголовок = Заголовок + НСтр("ru = ' (создание)';
									|en = ' (создание)'");
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Заголовок = Объект.Наименование;
	Оповестить("Запись_ОписиИсходящихДокументовВНалоговыеОрганы", , Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// инициализируем контекст формы - контейнера клиентских методов
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииЗавершение", ЭтотОбъект);
	
	ДокументооборотСКОКлиент.ПолучитьКонтекстЭДО(ОписаниеОповещения);
	
КонецПроцедуры 

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ПроверкаЗаполнения() = Ложь Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_СканированныеДокументыДляПередачиВЭлектронномВиде" Тогда
		ЗаполнитьВычисляемыеПоляДереваДокументов(ЭтотОбъект);
		ОбновитьРазмерыОписи(Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.ПредставляемыеДокументы.Очистить();
	Для каждого ГруппаДокументов Из ДеревоДокументов.ПолучитьЭлементы() Цикл
		//Обход групп документов
		
		Для каждого СтрокаДокумент Из ГруппаДокументов.ПолучитьЭлементы() Цикл
			
			//Обход самих документов
			НоваяСтрока = ТекущийОбъект.ПредставляемыеДокументы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДокумент);
			
			НоваяСтрока.ЯвляетсяИнформацией = ГруппаДокументов.ЯвляетсяИнформацией;
			НоваяСтрока.ПорядковыйНомер 	= ГруппаДокументов.ПорядковыйНомер;
			
			НоваяСтрока.НомерДокументаОснования 		= СтрокаДокумент.НомерДокОсн;
			НоваяСтрока.ДатаДокументаОснования 			= СтрокаДокумент.ДатаДокОсн;
			
			Если СтрокаДокумент.Загружен Тогда
				
				НоваяСтрока.СсылкаНаОбъект 				= Неопределено;
				НоваяСтрока.Загружен_Номер 				= СтрокаДокумент.ЗД_Номер;
				НоваяСтрока.Загружен_Дата 				= СтрокаДокумент.ЗД_Дата;
				НоваяСтрока.Загружен_Контрагент 		= СтрокаДокумент.ЗД_Контрагент;
				НоваяСтрока.Загружен_Направление 		= СтрокаДокумент.ЗД_Направление;
				
			КонецЕсли;
			
			НоваяСтрока.КНД 							= СтрокаДокумент.КНД;
			НоваяСтрока.ПодтверждениеКНД 				= СтрокаДокумент.ПодтверждениеКНД;
			НоваяСтрока.ПодтверждениеИмяФайлаДанных 	= СтрокаДокумент.ПодтверждениеИмяФайлаДанных;
			НоваяСтрока.ПодтверждениеИмяФайлаПодписи 	= СтрокаДокумент.ПодтверждениеИмяФайлаПодписи;
			
		КонецЦикла;
	КонецЦикла;
	
	Справочники.ШаблоныПоясненийДляФНС.СохранитьФорматированныйДокумент(
		ТекущийОбъект, 
		Пояснение, 
		"ТекстовоеПояснение");
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПояснениеПриИзменении(Элемент)
	
	УстановитьМодифицированность(ЭтотОбъект);
	
	Если НЕ ЗаполненТекстФорматированногоДокумента(Пояснение) Тогда
		Объект.ШаблонМакета = Неопределено;
	КонецЕсли;
	
	ОбновитьРазмерыОписи(Объект.Ссылка);
	УстановитьВидимостьПодсказкиПроЖелтыйФон();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьПанельИнформацииНажатие(Элемент)
	БольшеНеПоказыватьИнформационнуюПанель();
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПунктТребования(ДанныеСтроки, ОписаниеОповещения)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТипПункта", ?(ДанныеСтроки.ЯвляетсяИнформацией, "2", "1"));
	ПараметрыФормы.Вставить("НомерПункта", ДанныеСтроки.ПорядковыйНомер);
	
	ОткрытьФормуВыбораПунктаТребования(ОписаниеОповещения, ПараметрыФормы);
				
КонецПроцедуры
	
&НаКлиенте
Процедура ОткрытьФормуВыбораПунктаТребования(ОписаниеОповещения, ПараметрыФормы = Неопределено)
	
	ОткрытьФорму(
		"Справочник.ОписиИсходящихДокументовВНалоговыеОрганы.Форма.ФормаВыбораПунктаТребования", 
		ПараметрыФормы, 
		ЭтотОбъект, 
		, 
		,
		, 
		ОписаниеОповещения, 
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
				
КонецПроцедуры
	
&НаКлиенте
Процедура ОткрытьФормуВыбораПунктаТребованияСПроверкой(ОписаниеОповещения, ПараметрыФормы = Неопределено)
	
	Если ЭтоПакетСДопДокументами Тогда
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, "1.01");
	Иначе
		ОткрытьФормуВыбораПунктаТребования(ОписаниеОповещения, ПараметрыФормы);
	КонецЕсли;
				
КонецПроцедуры

&НаКлиенте
Процедура ПредставляемыеДокументыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
		
	СтандартнаяОбработка = Ложь;
	
	ДанныеСтроки = Элемент.ТекущиеДанные;
	
	Если ДанныеСтроки.ЭтоГруппа Тогда
		
		ПриВыбореГруппы(Элемент, Поле, ДанныеСтроки);
		
	ИначеЕсли ТипЗнч(ДанныеСтроки.СсылкаНаОбъект) = Тип("Строка") Тогда
		//ошибка: удалилась ссылка на объект
		Возврат;
	Иначе
		
		ПриВыбореСтроки(Элемент, ДанныеСтроки, Поле);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ЭтоРедактированиеАкта(ДанныеСтроки, Поле)
		
	ЭтоСкан = ТипЗнч(ДанныеСтроки.СсылкаНаОбъект) = Тип("СправочникСсылка.СканированныеДокументыДляПередачиВЭлектронномВиде");
	ЭтоАкт  = ДанныеСтроки.ВидДокумента = ПредопределенноеЗначение("Перечисление.ВидыПредставляемыхДокументов.АктПриемкиСдачиРабот");
	ЭтоРедактированиеОснования = Поле.Имя = "ПредставляемыеДокументыПредставлениеОснования";
	
	ЭтоРедактированиеАкта = 
		ЭтоРедактированиеОснования
		И ЭтоАкт
		И НЕ ТолькоПросмотр
		И НЕ ЭтоСкан;
	
	Возврат ЭтоРедактированиеАкта;
	
КонецФункции

&НаКлиенте
Процедура ПриВыбореСтроки(Элемент, ДанныеСтроки, Поле)
		
	ЭтоРедактированиеАкта = ЭтоРедактированиеАкта(ДанныеСтроки, Поле);
	
	Если ЭтоРедактированиеАкта Тогда
		ПриВыбореАкта(Элемент, ДанныеСтроки, Поле);
	ИначеЕсли ЗначениеЗаполнено(ДанныеСтроки.СсылкаНаОбъект) Тогда
		//откроем объект по ссылке
		ОткрытьДокумент(ДанныеСтроки.СсылкаНаОбъект)
	ИначеЕсли ДанныеСтроки.Загружен Тогда
		
		Если ЭтоФайлБанковскойГарантии(ДанныеСтроки.ИмяФайлаДанных) Тогда
			ОткрытьФормуБанковскихГарантий(ДанныеСтроки.ИмяФайлаДанных, ДанныеСтроки.ИмяФайлаПодписи);
		Иначе
			ОткрытьФормуЗагруженногоДокумента(ДанныеСтроки, НЕ Элемент.ТолькоПросмотр);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриВыбореАкта(Элемент, ДанныеСтроки, Поле)
		
	ДополнительныеПараметры = Новый Структура("ИдентификаторТекущейСтроки", Элемент.ТекущаяСтрока); 

	//Описание оповещения
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"РедактированиеДоговораЗавершение", 
		ЭтотОбъект, 
		ДополнительныеПараметры);

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НомерДоговора", ДанныеСтроки.НомерДокОсн);
	ПараметрыФормы.Вставить("ДатаДоговора",  ДанныеСтроки.ДатаДокОсн);

	//Открываем форму выбора пункта требования, результат закрытия передаем в ПеремещениеВыделенныхСтрокЗавершение
	ОткрытьФорму(
		"Справочник.ОписиИсходящихДокументовВНалоговыеОрганы.Форма.ФормаРеквизитовДоговора", 
		ПараметрыФормы, 
		ЭтотОбъект,
		,
		,
		,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

&НаКлиенте
Процедура ПриВыбореГруппы(Элемент, Поле, ДанныеСтроки)
		
	Если Поле.Имя = "ПредставляемыеДокументыСоставноеПоле" И НЕ ТолькоПросмотр Тогда
		
		// выберем пункт требования и перенесем в него документы
		//Описание оповещения
		ДополнительныеПараметры = Новый Структура("ИдентификаторТекущейСтроки", Элемент.ТекущаяСтрока); 
		ОписаниеОповещения = Новый ОписаниеОповещения("ИзменитьПунктТребованияГруппыДокументов", ЭтотОбъект, ДополнительныеПараметры);
		
		Если ЭтоПакетСДопДокументами Тогда
			ВыполнитьОбработкуОповещения(ОписаниеОповещения, "1.01");
		Иначе
			ВыбратьПунктТребования(ДанныеСтроки, ОписаниеОповещения);
		КонецЕсли;
		
	Иначе
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставляемыеДокументыПередУдалением(Элемент, Отказ)
	
	ПередИнтерактивнымУдалениемВыделенныхСтрокНаСервере(Элемент.ВыделенныеСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставляемыеДокументыПослеУдаления(Элемент)
	
	УдалитьПустыеГруппыДокументов();
	Записать();
	Модифицированность = Ложь;
	ОбновитьРазмерыОписи(Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставляемыеДокументыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;	

КонецПроцедуры

&НаКлиенте
Процедура ОснованиеПриИзменении(Элемент)
	
	ОснованиеПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОснованиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ЗаполнитьОснованиеПослеВыбора", 
		ЭтотОбъект);
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("СкрытьЕГРЮЛ",    		Истина);
	ДополнительныеПараметры.Вставить("ЭтоРежимВыбора", 		Истина);
	ДополнительныеПараметры.Вставить("ОтборОрган",     		ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ФНС"));
	ДополнительныеПараметры.Вставить("ТолькоОтправленные", 	Истина);
	
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		ДополнительныеПараметры.Вставить("Организация", 	Объект.Организация);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Основание) Тогда
		ДополнитьДопПараметрыИнформациейПоОснованию(ДополнительныеПараметры);
	КонецЕсли;
	
	ОткрытьФорму(
		"ОбщаяФорма.РегламентированнаяОтчетность", 
		ДополнительныеПараметры,
		,
		Новый УникальныйИдентификатор,
		,
		,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставляемыеДокументыНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, Выполнение)
	ПараметрыПеретаскивания.ДопустимыеДействия = ДопустимыеДействияПеретаскивания.Перемещение;
КонецПроцедуры

&НаКлиенте
Процедура ПредставляемыеДокументыПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставляемыеДокументыПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	Если Строка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	//отрабатываем более логичное поведение:
	//если перемещают группы на другую группу, то не сливаем содержимое групп в целевую, а лишь выполняем стандартную обработку
	ЦелеваяСтрока = ДеревоДокументов.НайтиПоИдентификатору(Строка);
	Если ЦелеваяСтрока.ЭтоГруппа Тогда
		Для каждого ИдентификаторПереносимойСтроки Из ПараметрыПеретаскивания.Значение Цикл
			Если НЕ ДеревоДокументов.НайтиПоИдентификатору(ИдентификаторПереносимойСтроки).ЭтоГруппа Тогда
				СтандартнаяОбработка = Ложь;	
				Прервать;
			КонецЕсли;
		КонецЦикла;
	Иначе
		СтандартнаяОбработка = Ложь;	
	КонецЕсли;
	
	Если СтандартнаяОбработка Тогда
		Возврат
	КонецЕсли;
	
	Если ЦелеваяСтрока.ЭтоГруппа Тогда
		ЦелевойПунктТребования = ЦелеваяСтрока.ПорядковыйНомерВыгрузки;
	Иначе
		ЦелевойПунктТребования = ЦелеваяСтрока.ПолучитьРодителя().ПорядковыйНомерВыгрузки;
	КонецЕсли;
	
	ПеренестиВыделенныеСтрокиЗавершениеНаСервере(ЦелевойПунктТребования, ПараметрыПеретаскивания.Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставляемыеДокументыОкончаниеПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	
	УдалитьПустыеГруппыДокументов();
	ЗаполнитьВычисляемыеПоляДереваДокументов(ЭтотОбъект);
	Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставляемыеДокументыПриИзменении(Элемент)
	
	ЗаполнитьВычисляемыеПоляДереваДокументов(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставляемыеДокументыПриАктивизацииСтроки(Элемент)
	
	ДанныеСтроки = Элемент.ТекущиеДанные;
	Элементы.ПредставляемыеДокументыИзменить.Доступность = (ДанныеСтроки <> Неопределено И НЕ ДанныеСтроки.ЭтоГруппа);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеОснованияОчистка(Элемент, СтандартнаяОбработка)
	
	ПредставлениеОснования = "";
	Объект.Основание = Неопределено;
	ОснованиеПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеОснованияОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ЗначениеЗаполнено(Объект.Основание) Тогда
		ПоказатьЗначение(,Объект.Основание);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДополнитьДопПараметрыИнформациейПоОснованию(ДополнительныеПараметры)

	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	СведенияПоОбъекту = КонтекстЭДОСервер.СведенияПоОтправляемымОбъектам(Объект.Основание);
	
	ДополнительныеПараметры.Вставить("Раздел",     СведенияПоОбъекту.СтраницаЖурнала);
	ДополнительныеПараметры.Вставить("Организация", СведенияПоОбъекту.Организация);
	ДополнительныеПараметры.Вставить("Ссылка",     СведенияПоОбъекту.Ссылка);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВставитьКонтрагента(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ВставитьКонтрагента_ПослеВыбора", 
		ЭтотОбъект);
		
	ОткрытьФорму("Справочник.Контрагенты.ФормаВыбора", ,,,,,ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоШаблону(Команда)
	
	Если НЕ ПроверитьОснование() Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ЗаполнитьПоШаблону_ПослеВыбораШаблона", 
		ЭтотОбъект);
		
	ПараметрыФормы = Новый Структура("ТекущаяСтрока", Объект.ШаблонМакета);
		
	ОткрытьФорму(
		"Справочник.ШаблоныПоясненийДляФНС.ФормаВыбора",
		ПараметрыФормы,
		ЭтотОбъект,,,,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьШаблон(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ИзменитьШаблон_ПослеВыбораШаблона", 
		ЭтотОбъект);
		
	ПараметрыФормы = Новый Структура("ТекущаяСтрока", Объект.ШаблонМакета);
		
	ОткрытьФорму(
		"Справочник.ШаблоныПоясненийДляФНС.ФормаСписка",
		ПараметрыФормы,
		ЭтотОбъект,,,,
		ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрисоединенныеФайлы(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.Основание) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Сначала выберите документ-основание';
										|en = 'Сначала выберите документ-основание'"));
		Возврат;
	КонецЕсли;
	
	МассивСвойствФайлов = Новый Массив;
	ФайлыИсточников = Новый Соответствие;
	ФайлыИсточников.Вставить(Объект.Основание, МассивСвойствФайлов);
	
	КонтекстЭДОКлиент.ПолучитьИзображенияПрисоединенныхФайловИсточников(ФайлыИсточников, УникальныйИдентификатор);
	МассивСвойствФайлов = ФайлыИсточников[Объект.Основание];
	
	Если МассивСвойствФайлов.Количество() = 0 Тогда
		Текст = НСтр("ru = 'У документа %1 нет присоединенных файлов';
					|en = 'У документа %1 нет присоединенных файлов'");
		Текст = СтрШаблон(Текст, ПредставлениеОснования);
		ПоказатьПредупреждение(, Текст);
		Возврат;
	КонецЕсли;
	
	АдресМассиваСвойствФайлов = ПоместитьВоВременноеХранилище(МассивСвойствФайлов, Новый УникальныйИдентификатор);
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ЭтоПакетСДопДокументами",    ЭтоПакетСДопДокументами);
	ПараметрыФормы.Вставить("АдресМассиваСвойствФайлов",  АдресМассиваСвойствФайлов);
	ПараметрыФормы.Вставить("Организация",  			  Объект.Организация);
	ПараметрыФормы.Вставить("СведенияДокумента", 		  ПредставлениеОснования);
	
	//Описание оповещения
	ОписаниеОповещения = Новый ОписаниеОповещения("ДобавитьДокументыЗавершение", ЭтотОбъект);
	
	ОткрытьФорму("Справочник.СканированныеДокументыДляПередачиВЭлектронномВиде.Форма.ФормаЭлементаНовая", ПараметрыФормы,ЭтотОбъект,,,,ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПервичку(Команда)
	
	ВыполняемоеОповещение = Новый ОписаниеОповещения(
		"ВыбратьПервичкуЗавершение", 
		ЭтотОбъект);

	КонтекстЭДОКлиент.ВыбратьПервичныйДокументВОтветеНаТребование(ВыполняемоеОповещение, Объект.Организация,,Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьСканыИзПредыдущихОтветов(Команда)
	
	Если НЕ ПроверитьОснование() Тогда
		Возврат;
	КонецЕсли;
	
	ВыполняемоеОповещение = Новый ОписаниеОповещения(
		"ВыбратьПервичкуЗавершение", 
		ЭтотОбъект);

	КонтекстЭДОКлиент.ВыбратьСканированныйДокументВОтветеНаТребование(ВыполняемоеОповещение, Объект.Организация,,Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВыделенныеСтроки(Команда)
	
	ДанныеСтроки = Элементы.ПредставляемыеДокументы.ТекущиеДанные;
	
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;

	МассивИдентификаторовВыделенныхСтрок = Элементы.ПредставляемыеДокументы.ВыделенныеСтроки;
	
	// выберем пункт требования и перенесем в него документы
	//Описание оповещения
	ДополнительныеПараметры = Новый Структура("МассивИдентификаторовВыделенныхСтрок", МассивИдентификаторовВыделенныхСтрок); 
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПеренестиВыделенныеСтрокиЗавершение", 
		ЭтотОбъект, 
		ДополнительныеПараметры);
	
	ВыбратьПунктТребования(ДанныеСтроки, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура РазбитьНаНесколько(Команда)
	
	ТекстВопроса = НСтр("ru = 'Для продолжения необходимо сохранить введеную информацию.
						|Продолжить?';
						|en = 'Для продолжения необходимо сохранить введеную информацию.
						|Продолжить?'");
						
	ЭтоНовый = Параметры.Ключ.Пустая();
	
	Попытка
		РезультатЗаписи = Записать();
	Исключение
		РезультатЗаписи = Ложь;
	КонецПопытки;
	
	Если НЕ РезультатЗаписи Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Во время сохранения произошли ошибки! Продолжение невозможно.';
										|en = 'Во время сохранения произошли ошибки! Продолжение невозможно.'"));
		Возврат;
	КонецЕсли;

	Если ЭтоПакетСДопДокументами Тогда
		ТекстВопроса = ДокументооборотСКОКлиентСервер.ПредставлениеПакетаСДопДокументами() + " будет разбит на несколько. Продолжить?";
	Иначе
		ТекстВопроса = "Ответ на требование о представлении документов (информации) будет разбит на несколько. Продолжить?";
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("РазбитьНаНесколькоЗавершение", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьДокументы(Команда)
	
	//Параметры формы
	ПараметрыФормы = Новый Структура();
	
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		ПараметрыФормы.Вставить("ОтборОрганизация", Объект.Организация);
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не заполнено поле ""Требование""';
														|en = 'Не заполнено поле ""Требование""'"), , "Объект.Основание");
		Возврат;
	КонецЕсли;
	
	//Описание оповещения
	ОписаниеОповещения = Новый ОписаниеОповещения("ДобавитьДокументыЗавершение", ЭтотОбъект);
	
	//Открываем форму выбора документов, результат закрытия передаем в ДобавитьДокументыЗавершение
	ОткрытьФорму("РегистрСведений.ДокументыПоТребованиюФНС.Форма.ФормаПодбора", ПараметрыФормы, ЭтотОбъект, , , , ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьОтчетыПоНДС(Команда)
	
	ПараметрыОтбора = Новый Структура;
	
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		ПараметрыОтбора.Вставить("Организация", Объект.Организация);
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не заполнено поле ""Требование""';
														|en = 'Не заполнено поле ""Требование""'"), , "Объект.Основание");
		Возврат;
	КонецЕсли;
	
	//Описание оповещения
	ОписаниеОповещения = Новый ОписаниеОповещения("ДобавитьДокументыНДСЗавершение", ЭтотОбъект);

	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентПереопределяемый.ВыбратьДокументНДСДляПередачиФНС(ОписаниеОповещения, ПараметрыОтбора);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтправить(Команда)
	
	Попытка
		РезультатЗаписи = Записать();
	Исключение
		РезультатЗаписи = Ложь;
	КонецПопытки;
	
	Если НЕ РезультатЗаписи Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Во время сохранения произошли ошибки! Продолжение невозможно.';
										|en = 'Во время сохранения произошли ошибки! Продолжение невозможно.'"));
		Возврат;
	КонецЕсли;
	
	ТекстСообщения = "";
	Если НЕ ПроверкаПередОтправкой(ТекстСообщения) Тогда
		ПоказатьПредупреждение(, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	//обновление файлов перед отправкой
	
	РезультатОбновления = ОбновитьФайлыДокументовИБНаСервере();

	Если РезультатОбновления Тогда
		
		РазвернутьДеревоДокументов();
		
	Иначе
		
		ПоказатьПредупреждение(, "Не удалось обновить файлы документов базы данных!");
		Возврат;
		
	КонецЕсли;
	
	Всего = 0;
	ВАрхиве = 0;
	ПроверитьТаблицуДокументов(Всего, ВАрхиве);
	
	Если ВАрхиве > 0 Тогда 
		ВсеВАрхиве = ?(ВАрхиве = Всего, 1, 0);
		КонтекстЭДОКлиент.ПоказатьУведомлениеАрхивныхФайлов(, 22 + ВсеВАрхиве, 2, Истина);
		Возврат;
	КонецЕсли;
	
	ПредложитьДозаполнитьПараметрыПояснения();
	
КонецПроцедуры

&НаКлиенте
Функция ПредложитьДозаполнитьПараметрыПояснения()

	ЕстьТекст = ЗаполненТекстФорматированногоДокумента(Пояснение);
	Если ЕстьТекст Тогда
		ПараметрыКЗаполнению = НезаполненныеПараметры(Пояснение);
		Если ПараметрыКЗаполнению.Количество() = 0 Тогда
			
			КомандаОтправить_ПослеПроверок();
			
		Иначе
			
			ТекстВопроса = НСтр("ru = 'В текстовом пояснении обнаружены незаполненные значения:';
								|en = 'В текстовом пояснении обнаружены незаполненные значения:'");
			Для каждого Параметр Из ПараметрыКЗаполнению Цикл
				ТекстВопроса = ТекстВопроса + Символы.ПС + "- <" + Параметр + ">";
			КонецЦикла;
			
			ТекстВопроса = ТекстВопроса + Символы.ПС + НСтр("ru = '
                                                             |Перед отправкой рекомендуется завершить заполнение всех данных. Прервать отправку?';
                                                             |en = '
                                                             |Перед отправкой рекомендуется завершить заполнение всех данных. Прервать отправку?'");
			
			ОписаниеОповещения = Новый ОписаниеОповещения("КомандаОтправить_ПослеОтветаНаВопросПроПараметры", ЭтотОбъект);
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
			
		КонецЕсли;
	Иначе
		КомандаОтправить_ПослеПроверок();
	КонецЕсли;

КонецФункции

&НаКлиенте
Процедура КомандаОтправить_ПослеОтветаНаВопросПроПараметры(Ответ, ВходящийКонтекст) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		КомандаОтправить_ПослеПроверок();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтправить_ПослеПроверок() Экспорт
	
	ТребуетсяРазбитьНаНесколько = (ДопустимоеКоличествоФайлов < 0) ИЛИ (ДопустимоеКоличествоМегабайт < 0);
	
	Если ТребуетсяРазбитьНаНесколько Тогда
		
		Если ДопустимоеКоличествоФайлов < 0 Тогда
			
			ТекстВопроса = "Допустимое количество файлов превышено на " +(-ДопустимоеКоличествоФайлов) + ".";
			
		ИначеЕсли ДопустимоеКоличествоМегабайт < 0 Тогда
			
			ТекстВопроса = "Допустимый размер описи превышен на " +(-ДопустимоеКоличествоМегабайт) + " Мб.";
		
		КонецЕсли;
		
		ТекстВопроса = ТекстВопроса + "
		|Перед отправкой документ требуется разбить на несколько.";
		
		СписокКнопок = Новый СписокЗначений;
		СписокКнопок.Добавить(Истина, "Разбить");
		СписокКнопок.Добавить(Ложь, "Отмена");
		
		ОписаниеОповещения = Новый ОписаниеОповещения("КомандаОтправитьВопросРазбитьЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, СписокКнопок);
		Возврат;
		
	Иначе
		
		Если ЭтоПакетСДопДокументами Тогда
			ТекстВопроса = ДокументооборотСКОКлиентСервер.ПредставлениеПакетаСДопДокументами() + " будет отправлен в ФНС. Продолжить?";
		Иначе
			ТекстВопроса = "Ответ на требование о представлении документов (информации) будет отправлен в ФНС. Продолжить?";
		КонецЕсли;
		
		ОписаниеОповещения = Новый ОписаниеОповещения("КомандаОтправитьВопросОписьБудетОтправленаЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Возврат;
		
	КонецЕсли;
	
	УправлениеСтатусомИДоступностью();

КонецПроцедуры

&НаКлиенте
Процедура Изменить(Команда)
	
	ДанныеСтроки = Элементы.ПредставляемыеДокументы.ТекущиеДанные;
	
	Если ДанныеСтроки <> Неопределено И НЕ ДанныеСтроки.ЭтоГруппа Тогда
		
		Если ЗначениеЗаполнено(ДанныеСтроки.СсылкаНаОбъект) Тогда
			
			Если ТипЗнч(ДанныеСтроки.СсылкаНаОбъект) = Тип("СправочникСсылка.СканированныеДокументыДляПередачиВЭлектронномВиде") Тогда
				ОткрытьСкан(ДанныеСтроки.СсылкаНаОбъект);
			Иначе
				ПоказатьЗначение(, ДанныеСтроки.СсылкаНаОбъект);
			КонецЕсли;
			
		ИначеЕсли ДанныеСтроки.Загружен Тогда
			ОткрытьФормуЗагруженногоДокумента(ДанныеСтроки, Ложь);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьЭлектронныйДокумент(Команда)
	ВыбратьЭлектронныйДокументИзБазы();
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСкан(Команда)
	
	Если НЕ ПроверитьОснование() Тогда
		Возврат;
	КонецЕсли;
	
	ВыбратьИДобавитьВложения();
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьДокументыИзДругойБазы(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ДобавитьДокументыИзДругойБазыПослеВыбораВидаДокумента", 
		ЭтотОбъект);
	
	ОткрытьФорму("Справочник.ОписиИсходящихДокументовВНалоговыеОрганы.Форма.ФормаЗагрузкиИзФайлаОбмена", , ЭтотОбъект, , , , ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьДокументыИзДругойБазыПослеВыбораВидаДокумента(Результат, ВходящийКонтекст) Экспорт
	
	Если Результат = "xml" Тогда
		ДобавитьДокументыИзФайлаОбмена();
	ИначеЕсли Результат = "сканы" Тогда
		ЗагрузитьСканыИзФайлаОбмена();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура БанковскиеГарантии(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.Основание) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Заполните поле ""Основание""';
														|en = 'Заполните поле ""Основание""'"));
		Возврат;
	КонецЕсли;
	
	ОткрытьФормуБанковскихГарантий();
	
КонецПроцедуры
	
&НаКлиенте
Процедура ОткрытьФормуБанковскихГарантий(ИмяФайлаДанных = "", ИмяФайлаПодписи = "")
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("Опись",  				Объект.Ссылка);
	ДополнительныеПараметры.Вставить("ИмяФайлаДанных",  	ИмяФайлаДанных);
	ДополнительныеПараметры.Вставить("ИмяФайлаПодписи", 	ИмяФайлаПодписи);
	ДополнительныеПараметры.Вставить("ЗапретитьИзменение", 	НЕ ЭтоИзменяемоеСообщение);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"БанковскиеГарантии_ПослеВыбора", 
		ЭтотОбъект,
		ДополнительныеПараметры);
	
	ОткрытьФорму("Справочник.ОписиИсходящихДокументовВНалоговыеОрганы.Форма.БанковскиеГарантии", 
		ДополнительныеПараметры, 
		ЭтотОбъект, 
		,
		,
		,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура БанковскиеГарантии_ПослеВыбора(Результат, ВходящийКонтекст) Экспорт
	
	Пропустить = 
		Результат = Неопределено
		ИЛИ ВходящийКонтекст.ИмяФайлаДанных = Результат.XMLФайл.Имя
		ИЛИ ВходящийКонтекст.ИмяФайлаПодписи = Результат.Подпись.Имя;
	
	Если Пропустить Тогда
		Возврат;
	КонецЕсли;
	
	ДобавитьБанковскиеГарантииНаСервере("1.01", Результат);
		
	Записать();
	ОбновитьРазмерыОписи(Объект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьБанковскиеГарантииНаСервере(ПунктТребования, Результат)
	
	XMLФайл = Результат.XMLФайл;
	Подпись = Результат.Подпись;
	
	ДокументыДеревоЗначений = РеквизитФормыВЗначение("ДеревоДокументов", Тип("ДеревоЗначений"));
	СтрокиДерева = ДокументыДеревоЗначений.Строки;
	
	ЗагружаемыеДокументы = Новый Массив;
	ЗагружаемыеДокументы.Добавить(XMLФайл);
	ЗагружаемыеДокументы.Добавить(Подпись);

	Если НЕ ЕстьНовыеДокументы(ЗагружаемыеДокументы, СтрокиДерева, "Имя") Тогда
		Возврат;
	КонецЕсли;
	
	СвойстваПунктаТребования = РазложитьПунктТребования(ПунктТребования);
	ГруппаДокументов = ОпределитьГруппуДокументовПоНомеруПункта(СтрокиДерева, СвойстваПунктаТребования, ПунктТребования);
	
	НоваяСтрока = ГруппаДокументов.Строки.Добавить();
	НоваяСтрока.Загружен 			= Истина;
	НоваяСтрока.ЯвляетсяИнформацией = СвойстваПунктаТребования.ЯвляетсяИнформацией;
	НоваяСтрока.КНД 				= ДокументооборотСКОВызовСервера.КНДФайлаПоАдресу(XMLФайл.Адрес, "windows-1251");
	НоваяСтрока.ИмяФайлаДанных 		= XMLФайл.Имя;
	НоваяСтрока.СоставноеПоле  		= XMLФайл.Имя;
	НоваяСтрока.ИмяФайлаПодписи 	= Подпись.Имя;
	
	СохранитьИзменения(ДокументыДеревоЗначений);
	
	Успешно = СохранитьФайлыБанковскойГарантии(Объект.Ссылка, XMLФайл, Подпись);
	Если НЕ Успешно Тогда
		
		// Откатываем
		ГруппаДокументов.Строки.Удалить(НоваяСтрока);
		СохранитьИзменения(ДокументыДеревоЗначений);
		
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Произошла ошибка при загрузке документов из файла обмена:';
				|en = 'Произошла ошибка при загрузке документов из файла обмена:'"));
		Возврат;
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеОрганизации(Команда)
	ВставитьПараметр("НаименованиеОрганизации");
КонецПроцедуры

&НаКлиенте
Процедура Сегодня(Команда)
	ВставитьПараметр("ДатаСегодня");
КонецПроцедуры

&НаКлиенте
Процедура Руководитель(Команда)
	ВставитьПараметр("Руководитель");
КонецПроцедуры

&НаКлиенте
Процедура ДолжностьРуководителя(Команда)
	ВставитьПараметр("ДолжностьРуководителя");
КонецПроцедуры

&НаКлиенте
Процедура ИНН(Команда)
	ВставитьПараметр("ИНН");
КонецПроцедуры

&НаКлиенте
Процедура КПП(Команда)
	ВставитьПараметр("КПП");
КонецПроцедуры

&НаКлиенте
Процедура ОГРН(Команда)
	ВставитьПараметр("ОГРН");
КонецПроцедуры

&НаКлиенте
Процедура Адрес(Команда)
	ВставитьПараметр("Адрес");
КонецПроцедуры

&НаКлиенте
Процедура Телефон(Команда)
	ВставитьПараметр("Телефон");
КонецПроцедуры

&НаКлиенте
Процедура Требование(Команда)
	ВставитьПараметр("Требование");
КонецПроцедуры

&НаКлиенте
Процедура ИФНС(Команда)
	ВставитьПараметр("ИФНС");
КонецПроцедуры

&НаКлиенте
Процедура Исполнитель(Команда)
	ВставитьПараметр("Исполнитель");
КонецПроцедуры

&НаКлиенте
Процедура ВставитьПериод(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ВставитьПериод_Завершение", 
		ЭтотОбъект);
		
	ТребованияФНСКлиент.ВставитьПериод(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьОтчет(Команда)
	ВыбратьОтчет();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ФормаЗаполненияШаблона(СсылкаНаШаблон)
	
	ИмяМакета = СсылкаНаШаблон.ИмяМакета;
	Форма = Метаданные.Справочники.ШаблоныПоясненийДляФНС.Формы.Найти(ИмяМакета);
	
	Если Форма = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		Возврат ИмяМакета;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьПоШаблону_ПослеВыбораШаблона(СсылкаНаШаблон, ВходящийКонтекст) Экспорт
	
	Если СсылкаНаШаблон = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Форма = ФормаЗаполненияШаблона(СсылкаНаШаблон);
	Если Форма = Неопределено Тогда
		ЗаполнитьТекстВыбранногоШаблона(СсылкаНаШаблон);
	Иначе
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ЗаполнитьПоШаблону_ПослеЗаполненияФормы", 
			ЭтотОбъект,
			СсылкаНаШаблон);
		
		ДанныеОписи = Новый Структура("Организация, НалоговыйОрган, Основание");
		ЗаполнитьЗначенияСвойств(ДанныеОписи, Объект);
	
		ОткрытьФорму(
			"Справочник.ШаблоныПоясненийДляФНС.Форма." + Форма,
			ДанныеОписи,
			ЭтотОбъект,,,,
			ОписаниеОповещения,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоШаблону_ПослеЗаполненияФормы(ДанныеИзФормыЗаполнения, СсылкаНаШаблон) Экспорт
	
	Если ДанныеИзФормыЗаполнения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьТекстВыбранногоШаблона(СсылкаНаШаблон, ДанныеИзФормыЗаполнения);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТекстВыбранногоШаблона(СсылкаНаШаблон, ДанныеИзФормыЗаполнения = Неопределено) Экспорт
	
	Объект.ШаблонМакета = СсылкаНаШаблон;
	УстановитьМодифицированность(ЭтотОбъект);
	
	ЗаполнитьТекстШаблона(СсылкаНаШаблон, ДанныеИзФормыЗаполнения);
	УстановитьВидимостьПодсказкиПроЖелтыйФон();
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьШаблон_ПослеВыбораШаблона(Шаблон, ВходящийКонтекст) Экспорт
	
	Если Шаблон = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("Ключ", Шаблон);

	ОткрытьФорму("Справочник.ШаблоныПоясненийДляФНС.Форма.РедактированиеШаблона",ДополнительныеПараметры, ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура БольшеНеПоказыватьИнформационнуюПанель()
	
	ХранилищеОбщихНастроек.Сохранить(
		"ДокументооборотСКонтролирующимиОрганами_ПакетСДопДокументами_УбратьИнформационнуюПанель",
		,
		Истина);
	
	УстановитьВидимостьИнформационнойПанели();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьИнформационнойПанели()

	ПанельУбранаПользователем = ХранилищеОбщихНастроек.Загрузить("ДокументооборотСКонтролирующимиОрганами_ПакетСДопДокументами_УбратьИнформационнуюПанель") = Истина;
	
	Если ПанельУбранаПользователем ИЛИ НЕ ЭтоПакетСДопДокументами Тогда
		Элементы.ИнформационнаяПанель.Видимость = Ложь;
	Иначе
		Элементы.ИнформационнаяПанель.Видимость = Истина;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПервичкуЗавершение(РезультатВыбора, ВходящийКонтекст) Экспорт
	
	Если НЕ ЭтоСсылка(РезультатВыбора) 
		И ТипЗнч(РезультатВыбора) <> Тип("Массив")
		И ТипЗнч(РезультатВыбора) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(РезультатВыбора) = Тип("Массив") И РезультатВыбора.Количество() > 1 Тогда
		
		// Выбрано несколько документов
		Если РезультатВыбора.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		МассивВыбранныхДокументов = РезультатВыбора;
		ПодключитьОбработчикОжидания("Подключаемый_ДобавитьДокументыПередВыборомПунктаТребования", 0.2, Истина);
		
	Иначе
		
		Если ТипЗнч(РезультатВыбора) = Тип("Массив") И РезультатВыбора.Количество() = 1 Тогда
			РезультатВыбора = РезультатВыбора[0];
		КонецЕсли;
		
		Если ТипЗнч(РезультатВыбора) = Тип("Структура") И РезультатВыбора.Свойство("Ссылка") Тогда
			ДокументИсточник = РезультатВыбора.Ссылка;
		Иначе
			ДокументИсточник = РезультатВыбора;
		КонецЕсли;
		
		// Выбран один документ
		ЭлектронныеДокументы_ЭДО = ЭлектронныеДокументыЭДО(ДокументИсточник);
		
		Если ЭлектронныеДокументы_ЭДО.ЕстьЭлектронныйДокумент Тогда
			
			// Выбран один документ 1С-ЭДО
			
			МассивВыбранныхДокументов = Новый Массив;
			
			ТекущийДокумент = Новый Структура;
		
			ТекущийДокумент.Вставить("ВыбранныйДокумент", 	ДокументИсточник);
			ТекущийДокумент.Вставить("ВидДокументаФНС", 	ЭлектронныеДокументы_ЭДО.ВидДокументаФНС);
			
			МассивВыбранныхДокументов.Добавить(ТекущийДокумент);
			
			ПодключитьОбработчикОжидания("Подключаемый_ДобавитьДокументыПередВыборомПунктаТребования", 0.2, Истина);
			
		ИначеЕсли ЭтоСсылка(РезультатВыбора) Тогда
			
			// Выбран один документ, на основе которого надо создать сканировнаный
			
			Свойства = Новый Структура;
			Свойства.Вставить("Ссылка", 	РезультатВыбора);
			Свойства.Вставить("Описание", 	Строка(РезультатВыбора));
			
			РезультатВыбораФайлов = Новый Структура;
			РезультатВыбораФайлов.Вставить("СвойстваВыбранногоДокумента", Свойства);
			
			СоздатьСканированныйДокумент(РезультатВыбораФайлов);
			
		ИначеЕсли ТипЗнч(РезультатВыбора) = Тип("Структура") И РезультатВыбора.Свойство("ВыбранныйДокумент") Тогда
			
			// Выбран один документ из списка сканированных
			МассивВыбранныхДокументов = Новый Массив;
			МассивВыбранныхДокументов.Добавить(РезультатВыбора);
			
			ПодключитьОбработчикОжидания("Подключаемый_ДобавитьДокументыПередВыборомПунктаТребования", 0.2, Истина);
			
		ИначеЕсли ТипЗнч(РезультатВыбора) = Тип("Структура") И РезультатВыбора.Свойство("Ссылка") Тогда
			
			// Выбран один обычный документ при помощи переопределяемой 
			// ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентПереопределяемый.ВыбратьДокументИсточникДляПередачиФНС 
			
			РезультатВыбораФайлов = Новый Структура;
			РезультатВыбораФайлов.Вставить("СвойстваВыбранногоДокумента", РезультатВыбора);
			
			СоздатьСканированныйДокумент(РезультатВыбораФайлов);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЭлектронныеДокументыЭДО(ДокументИсточник) Экспорт
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	Возврат КонтекстЭДОСервер.ЭлектронныеДокументыЭДО(ДокументИсточник);
		
КонецФункции

&НаСервере
Функция ЭтоСсылка(Источник) Экспорт
	
	Возврат ОбщегоНазначения.ЭтоСсылка(ТипЗнч(Источник));
		
КонецФункции

&НаКлиенте
Процедура ЗагрузитьСканыИзФайлаОбмена()
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ИдентификаторФормыВладельца", УникальныйИдентификатор);
	
	//Описание оповещения
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузитьСканыИзФайлаОбменаЗавершение", ЭтотОбъект);
	
	ОткрытьФорму("Справочник.СканированныеДокументыДляПередачиВЭлектронномВиде.Форма.ФормаЗагрузкаДокументовИзФайлаОбмена", ПараметрыФормы, ЭтотОбъект, , , , ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСканыИзФайлаОбменаЗавершение(СтуктураРезультат, ДополнительныеПараметры) Экспорт
	
	Если СтуктураРезультат <> Неопределено Тогда
		
		МассивВыбранныхДокументов = ОтработатьРезультатДобавленияСкановНаСервере(СтуктураРезультат);
		ПодключитьОбработчикОжидания("Подключаемый_ДобавитьДокументыПередВыборомПунктаТребования", 0.2, Истина);
			
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОтработатьРезультатДобавленияСкановНаСервере(СтруктураРезультата)
	
	ДобавленныеСканы = Справочники.СканированныеДокументыДляПередачиВЭлектронномВиде.ЗагрузитьИзВнешнегоИсточникаПоСтруктуреРезультатаВыбора(СтруктураРезультата);
	
	МассивВыбранныхДокументов = Новый Массив;
	
	Для каждого ДобавленныйСкан Из ДобавленныеСканы Цикл
		
		ТекущийДокумент = Новый Структура;
		
		ТекущийДокумент.Вставить("ВыбранныйДокумент", 	ДобавленныйСкан);
		ТекущийДокумент.Вставить("ВидДокументаФНС", 	Перечисления.ВидыПредставляемыхДокументов.ИныеДокументы);
		
		МассивВыбранныхДокументов.Добавить(ТекущийДокумент);
		
	КонецЦикла;
	
	Возврат МассивВыбранныхДокументов;
		
КонецФункции

&НаСервере
Процедура СформироватьСоставМенюДобавитьИзБазы()
	
	СоответствиеВидовДокументовНДС = ПолучитьСоответствиеВидДокументаФНСТипуВыбранногоДокументаНДС();
	
	Если СоответствиеВидовДокументовНДС.Количество() = 0 Тогда
		//В прикладном решении нет ни одного объекта метаданных, соответствующего хотя бы одному из видов документов категории "Отчеты по НДС"
		Элементы.ПредставляемыеДокументыДобавитьДокументыНДС.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьВычисляемыеПоляДереваДокументов(Форма)
	
	ЕстьАкты = Ложь;
	
	ТипСкан = Тип("СправочникСсылка.СканированныеДокументыДляПередачиВЭлектронномВиде");
	
	ДеревоДокументов = Форма.ДеревоДокументов;
	Группы = ДеревоДокументов.ПолучитьЭлементы();
	
	Для каждого Группа Из Группы Цикл
		
		// это строка группы документов
		Группа.ЭтоГруппа = Истина;
		
		ТекстПорядковыйНомерВыгрузки = ?(Группа.ЯвляетсяИнформацией, "2.", "1.");
		ТекстПорядковыйНомерВыгрузки = ТекстПорядковыйНомерВыгрузки + Формат(Группа.ПорядковыйНомер, "ЧЦ=2; ЧН=; ЧВН=");
		
		Группа.ПорядковыйНомерВыгрузки = ТекстПорядковыйНомерВыгрузки;
		Группа.ИсточникДокумента = 0;
		
		ДокументыГруппы = Группа.ПолучитьЭлементы();
		
		ЗаполнитьСоставноеПоле(Группа);
		
		Для каждого Документ Из ДокументыГруппы Цикл
			
			// это строка документа
			Документ.ЭтоГруппа = Ложь;
			
			Если Документ.Загружен Тогда
				
				Документ.ИсточникДокумента = 3;
				
				Если НЕ ЗначениеЗаполнено(Документ.СоставноеПоле) Тогда
					ЗаполнитьСоставноеПоле(Документ);
				КонецЕсли;
				
			Иначе
				
				Документ.СоставноеПоле = Документ.СсылкаНаОбъект;
				Документ.ИсточникДокумента = 1;
				
			КонецЕсли;
			
			ЭтоАкт = Документ.ИсточникДокумента <> 2 
				И Документ.ВидДокумента = ПредопределенноеЗначение("Перечисление.ВидыПредставляемыхДокументов.АктПриемкиСдачиРабот");
			
			Если ЭтоАкт Тогда
				
				ЕстьАкты = Истина;
				Если НЕ ЗначениеЗаполнено(Документ.НомерДокОсн) ИЛИ НЕ ЗначениеЗаполнено(Документ.ДатаДокОсн) Тогда
					Документ.ПредставлениеОснования = "Заполнить договор";
				Иначе
					//реквизиты договора заполнены
					Документ.ПредставлениеОснования = 
						"Договор " 
						+ Строка(Документ.НомерДокОсн) 
						+ " от " 
						+ Формат(Документ.ДатаДокОсн, "ДЛФ=D");
						
				КонецЕсли;
	            
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Форма.Элементы.ПредставляемыеДокументыПредставлениеОснования.Видимость = ЕстьАкты;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоДокументов(СсылкаНаДокумент, СозданКопированием = Ложь)
	
	ДеревоОбъект  = РеквизитФормыВЗначение("ДеревоДокументов", Тип("ДеревоЗначений"));
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОписиИсходящихДокументовВНалоговыеОрганыПредставляемыеДокументы.ЯвляетсяИнформацией КАК ЯвляетсяИнформацией,
		|	ОписиИсходящихДокументовВНалоговыеОрганыПредставляемыеДокументы.ПорядковыйНомер КАК ПорядковыйНомер,
		|	ОписиИсходящихДокументовВНалоговыеОрганыПредставляемыеДокументы.ВидДокумента КАК ВидДокумента,
		|	ОписиИсходящихДокументовВНалоговыеОрганыПредставляемыеДокументы.СсылкаНаОбъект КАК СсылкаНаОбъект,
		|	ОписиИсходящихДокументовВНалоговыеОрганыПредставляемыеДокументы.Загружен КАК Загружен,
		|	ОписиИсходящихДокументовВНалоговыеОрганыПредставляемыеДокументы.Загружен_Номер КАК Загружен_Номер,
		|	ОписиИсходящихДокументовВНалоговыеОрганыПредставляемыеДокументы.Загружен_Дата КАК Загружен_Дата,
		|	ОписиИсходящихДокументовВНалоговыеОрганыПредставляемыеДокументы.Загружен_Контрагент КАК Загружен_Контрагент,
		|	ОписиИсходящихДокументовВНалоговыеОрганыПредставляемыеДокументы.КНД КАК КНД,
		|	ОписиИсходящихДокументовВНалоговыеОрганыПредставляемыеДокументы.Загружен_Направление КАК Загружен_Направление,
		|	ОписиИсходящихДокументовВНалоговыеОрганыПредставляемыеДокументы.НомерДокументаОснования КАК НомерДокументаОснования,
		|	ОписиИсходящихДокументовВНалоговыеОрганыПредставляемыеДокументы.ДатаДокументаОснования КАК ДатаДокументаОснования,
		|	ОписиИсходящихДокументовВНалоговыеОрганыПредставляемыеДокументы.ИмяФайлаДанных КАК ИмяФайлаДанных,
		|	ОписиИсходящихДокументовВНалоговыеОрганыПредставляемыеДокументы.ИмяФайлаПодписи КАК ИмяФайлаПодписи,
		|	ОписиИсходящихДокументовВНалоговыеОрганыПредставляемыеДокументы.ВерсияФайлаДанных КАК ВерсияФайлаДанных,
		|	ОписиИсходящихДокументовВНалоговыеОрганыПредставляемыеДокументы.ПодтверждениеКНД КАК ПодтверждениеКНД,
		|	ОписиИсходящихДокументовВНалоговыеОрганыПредставляемыеДокументы.ПодтверждениеИмяФайлаДанных КАК ПодтверждениеИмяФайлаДанных,
		|	ОписиИсходящихДокументовВНалоговыеОрганыПредставляемыеДокументы.ПодтверждениеИмяФайлаПодписи КАК ПодтверждениеИмяФайлаПодписи,
		|	ОписиИсходящихДокументовВНалоговыеОрганыПредставляемыеДокументы.СведенияДокументаОснования КАК СведенияДокументаОснования
		|ИЗ
		|	Справочник.ОписиИсходящихДокументовВНалоговыеОрганы.ПредставляемыеДокументы КАК ОписиИсходящихДокументовВНалоговыеОрганыПредставляемыеДокументы
		|ГДЕ
		|	ОписиИсходящихДокументовВНалоговыеОрганыПредставляемыеДокументы.Ссылка = &СсылкаНаОбъект
		|	И (&СозданКопированием
		|				И НЕ(ОписиИсходящихДокументовВНалоговыеОрганыПредставляемыеДокументы.ЯвляетсяИнформацией
		|						И ОписиИсходящихДокументовВНалоговыеОрганыПредставляемыеДокументы.ПорядковыйНомер = 0)
		|			ИЛИ НЕ &СозданКопированием)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЯвляетсяИнформацией,
		|	ПорядковыйНомер
		|ИТОГИ ПО
		|	ЯвляетсяИнформацией,
		|	ПорядковыйНомер,
		|	ВидДокумента";

	Запрос.УстановитьПараметр("СсылкаНаОбъект", СсылкаНаДокумент);
	Запрос.УстановитьПараметр("СозданКопированием", СозданКопированием);

	Результат = Запрос.Выполнить();

	ВыборкаЯвляетсяИнформацией = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ДеревоОбъект  = РеквизитФормыВЗначение("ДеревоДокументов", Тип("ДеревоЗначений"));
	
	Пока ВыборкаЯвляетсяИнформацией.Следующий() Цикл
		
		ВыборкаПорядковыйНомер = ВыборкаЯвляетсяИнформацией.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПорядковыйНомер.Следующий() Цикл
			
			// добавляем строку групп документов
			СтрокаГрупп = ДеревоОбъект.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаГрупп,ВыборкаПорядковыйНомер);

			ВыборкаВидДокумента = ВыборкаПорядковыйНомер.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаВидДокумента.Следующий() Цикл
				
				ВыборкаДетальныеЗаписи = ВыборкаВидДокумента.Выбрать();
				Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
					
					// добавляем строку документов
					НоваяСтрока = СтрокаГрупп.Строки.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаДетальныеЗаписи);
					
					НоваяСтрока.НомерДокОсн 					= ВыборкаДетальныеЗаписи.НомерДокументаОснования;
					НоваяСтрока.ДатаДокОсн 						= ВыборкаДетальныеЗаписи.ДатаДокументаОснования;
					
					Если ВыборкаДетальныеЗаписи.Загружен Тогда
						
						НоваяСтрока.ЗД_Номер 					= ВыборкаДетальныеЗаписи.Загружен_Номер;
						НоваяСтрока.ЗД_Дата 					= ВыборкаДетальныеЗаписи.Загружен_Дата;
						НоваяСтрока.ЗД_Контрагент				= ВыборкаДетальныеЗаписи.Загружен_Контрагент;
						НоваяСтрока.ЗД_Контрагент				= ВыборкаДетальныеЗаписи.Загружен_Контрагент;
						НоваяСтрока.СведенияДокументаОснования  = ВыборкаДетальныеЗаписи.СведенияДокументаОснования;
						
					КонецЕсли;
					
					НоваяСтрока.КНД 							= ВыборкаДетальныеЗаписи.КНД;
					НоваяСтрока.ПодтверждениеКНД 				= ВыборкаДетальныеЗаписи.ПодтверждениеКНД;
					НоваяСтрока.ПодтверждениеИмяФайлаДанных 	= ВыборкаДетальныеЗаписи.ПодтверждениеИмяФайлаДанных;
					НоваяСтрока.ПодтверждениеИмяФайлаПодписи 	= ВыборкаДетальныеЗаписи.ПодтверждениеИмяФайлаПодписи;
					
				КонецЦикла;
			КонецЦикла;	
		КонецЦикла;
	КонецЦикла;
	
	// Преобразование объекта прикладного типа ДеревоЗначений
	// в реквизит управляемой формы (данные формы)
	ЗначениеВРеквизитФормы(ДеревоОбъект, "ДеревоДокументов");
	
	ТекстЗагружен = Справочники.ШаблоныПоясненийДляФНС.УстановитьТекстВФорматированныйДокумент(
		СсылкаНаДокумент, 
		Пояснение,
		"ТекстовоеПояснение");

	ЗаполнитьВычисляемыеПоляДереваДокументов(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьДокумент(СсылкаНаОбъект)
	
	//проверяем на принадлежность к сканированным
	ТипСкан = Тип("СправочникСсылка.СканированныеДокументыДляПередачиВЭлектронномВиде");

	Если ТипЗнч(СсылкаНаОбъект) = ТипСкан Тогда
		ОткрытьСкан(СсылкаНаОбъект);
		Возврат;
	КонецЕсли;
	
	//проверяем на принадлежность к отчетам НДС
	СоответствиеВидДокументаФНСТипуВыбранногоДокументаНДС = ПолучитьСоответствиеВидДокументаФНСТипуВыбранногоДокументаНДС();
	
	ВидДокумента = СоответствиеВидДокументаФНСТипуВыбранногоДокументаНДС[ТипЗнч(СсылкаНаОбъект)];
	Если ВидДокумента <> Неопределено  Тогда
		ПоказатьЗначение(, СсылкаНаОбъект);
		Возврат;
	КонецЕсли;
	
	//считаем, что это ЭД
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентПереопределяемый.ОткрытьАктуальныйЭД(СсылкаНаОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСкан(Скан)
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Ключ",       Скан);
	ПараметрыФормы.Вставить("ЭтоПакетСДопДокументами", ЭтоПакетСДопДокументами);
	ПараметрыФормы.Вставить("УстановитьПриказММВ_7_6_16", Истина);
	
	ОткрытьФорму("Справочник.СканированныеДокументыДляПередачиВЭлектронномВиде.Форма.ФормаЭлементаНовая", ПараметрыФормы, ЭтотОбъект);
		
КонецПроцедуры

&НаКлиенте
Процедура РедактированиеДоговораЗавершение(СтуктураРезультат, ДополнительныеПараметры) Экспорт
	Если ТипЗнч(СтуктураРезультат) <> Тип("Структура")  Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока = ДеревоДокументов.НайтиПоИдентификатору(ДополнительныеПараметры.ИдентификаторТекущейСтроки);
	ТекущаяСтрока.НомерДокОсн = СтуктураРезультат.Номер;
	ТекущаяСтрока.ДатаДокОсн = СтуктураРезультат.Дата;
	
	ЗаполнитьВычисляемыеПоляДереваДокументов(ЭтотОбъект);
	Записать();
		
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьПунктТребованияГруппыДокументов(ВыбранныйПунктТребования, ДополнительныеПараметры) Экспорт

	Если ТипЗнч(ВыбранныйПунктТребования) = Тип("Строка") Тогда
		//Был выбран пункт требования
		
		СвойстваПунктаТребования = РазложитьПунктТребования(ВыбранныйПунктТребования);
		
		//поиск указанного пункта требования
		ЦелеваяГруппаДокументов = Неопределено;
		Для каждого ГруппаДокументов Из ДеревоДокументов.ПолучитьЭлементы() Цикл
			Если ГруппаДокументов.ПорядковыйНомерВыгрузки = ВыбранныйПунктТребования Тогда
				ЦелеваяГруппаДокументов = ГруппаДокументов;
				Прервать;
			КонецЕсли;	
		КонецЦикла;
		
		Если ЦелеваяГруппаДокументов = Неопределено Тогда
			//группы документов с выбранным пунктом требования нет, просто переименовываем текущую
			ГруппаДокументов = ДеревоДокументов.НайтиПоИдентификатору(ДополнительныеПараметры.ИдентификаторТекущейСтроки);
			
			ГруппаДокументов.ЯвляетсяИнформацией 		= СвойстваПунктаТребования.ЯвляетсяИнформацией;
			ГруппаДокументов.ПорядковыйНомер 			= СвойстваПунктаТребования.ПорядковыйНомер;
			ГруппаДокументов.ПорядковыйНомерВыгрузки 	= ВыбранныйПунктТребования;
		Иначе
			//нашлась группа документов с выбранным пунктом требования, переносим в нее документы исходной группы
			МассивИдентификаторовВыделенныхСтрок = Новый Массив;
			
			ГруппаДокументов = ДеревоДокументов.НайтиПоИдентификатору(ДополнительныеПараметры.ИдентификаторТекущейСтроки);
			Для каждого СтрокаДокумент Из ГруппаДокументов.ПолучитьЭлементы() Цикл
				МассивИдентификаторовВыделенныхСтрок.Добавить(СтрокаДокумент.ПолучитьИдентификатор());
			КонецЦикла;
			
			ПеренестиВыделенныеСтрокиЗавершениеНаСервере(ВыбранныйПунктТребования, МассивИдентификаторовВыделенныхСтрок);
			УдалитьПустыеГруппыДокументов();
		КонецЕсли;
		
		ЗаполнитьВычисляемыеПоляДереваДокументов(ЭтотОбъект);
	Иначе
		//Не был выбран пункт требования 	
	КонецЕсли;
	
	Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВыделенныеСтрокиЗавершение(ВыбранныйПунктТребования, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(ВыбранныйПунктТребования) = Тип("Строка") Тогда
		//Был выбран пункт требования
		
		МассивИдентификаторовВыделенныхСтрок = ДополнительныеПараметры.МассивИдентификаторовВыделенныхСтрок;
		ПеренестиВыделенныеСтрокиЗавершениеНаСервере(ВыбранныйПунктТребования, МассивИдентификаторовВыделенныхСтрок);
		УдалитьПустыеГруппыДокументов();
		ЗаполнитьВычисляемыеПоляДереваДокументов(ЭтотОбъект);
		Записать();
	Иначе
		//Не был выбран пункт требования 	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПеренестиВыделенныеСтрокиЗавершениеНаСервере(ВыбранныйПунктТребования, МассивИдентификаторовВыделенныхСтрок) 
	
	СвойстваПунктаТребования = РазложитьПунктТребования(ВыбранныйПунктТребования);
	
	//поиск указанного пункта требования
	ЦелеваяГруппаДокументов = Неопределено;
	Для каждого ГруппаДокументов Из ДеревоДокументов.ПолучитьЭлементы() Цикл
		Если ГруппаДокументов.ПорядковыйНомерВыгрузки = ВыбранныйПунктТребования Тогда
			ЦелеваяГруппаДокументов = ГруппаДокументов;
			Прервать;
		КонецЕсли;	
	КонецЦикла;
	
	Если ЦелеваяГруппаДокументов = Неопределено Тогда
		ЦелеваяГруппаДокументов = ДеревоДокументов.ПолучитьЭлементы().Добавить();
		ЦелеваяГруппаДокументов.ЯвляетсяИнформацией = СвойстваПунктаТребования.ЯвляетсяИнформацией;
		ЦелеваяГруппаДокументов.ПорядковыйНомер = СвойстваПунктаТребования.ПорядковыйНомер;
	КонецЕсли;
	
	// определили целевую группу документов
	
	// преобразуем массив, разложив выделенные строки групп на составляющие
	НовыйМассивИдентификаторов = Новый Массив;
	Для каждого ИдентификаторВыделеннойСтроки Из МассивИдентификаторовВыделенныхСтрок Цикл
		ВыделеннаяСтрока = ДеревоДокументов.НайтиПоИдентификатору(ИдентификаторВыделеннойСтроки);
		Если ВыделеннаяСтрока.ЭтоГруппа Тогда
			//это группа
			Для каждого СтрокаДокумента Из ВыделеннаяСтрока.ПолучитьЭлементы() Цикл
				ДополнитьМассивЗначением(НовыйМассивИдентификаторов, СтрокаДокумента.ПолучитьИдентификатор());
			КонецЦикла;
		Иначе
			//это документ
			ДополнитьМассивЗначением(НовыйМассивИдентификаторов, ИдентификаторВыделеннойСтроки);
		КонецЕсли;
	КонецЦикла;
	
	МассивУдаляемыхСтрок = Новый Массив;
	Для каждого ИдентификаторСтрокиУдаляемогоДокумента Из НовыйМассивИдентификаторов Цикл
		УдаляемаяСтрокаДокумента = ДеревоДокументов.НайтиПоИдентификатору(ИдентификаторСтрокиУдаляемогоДокумента);
		
		МассивУдаляемыхСтрок.Добавить(УдаляемаяСтрокаДокумента);
		
		НоваяСтрокаДокумента = ЦелеваяГруппаДокументов.ПолучитьЭлементы().Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаДокумента, УдаляемаяСтрокаДокумента);
		
		НоваяСтрокаДокумента.ЯвляетсяИнформацией = СвойстваПунктаТребования.ЯвляетсяИнформацией;
		НоваяСтрокаДокумента.ПорядковыйНомер = СвойстваПунктаТребования.ПорядковыйНомер;
	КонецЦикла;
	
	Для каждого УдаляемаяСтрока Из МассивУдаляемыхСтрок Цикл
		УдаляемаяСтрока.ПолучитьРодителя().ПолучитьЭлементы().Удалить(УдаляемаяСтрока);	
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуЗагруженногоДокумента(ДанныеСтроки, ДоступностьДоговора = Истина)
	
	СтруктураПараметров = ПараметрыОткрытияЗагруженногоXMLФайла();
	ЗаполнитьЗначенияСвойств(СтруктураПараметров, ДанныеСтроки);
	СтруктураПараметров.Вставить("ДоступностьДоговора", ДоступностьДоговора);
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("СтруктураПараметров", СтруктураПараметров);
	ДополнительныеПараметры.Вставить("Опись", Объект.Ссылка);
	ДополнительныеПараметры.Вставить("Заголовок", ДанныеСтроки.СоставноеПоле);
	
	ОткрытьФорму("Справочник.ОписиИсходящихДокументовВНалоговыеОрганы.Форма.ФормаЗагруженныхXMLФайлов", 
		ДополнительныеПараметры, 
		ЭтотОбъект, 
		, , , , 
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоФайлБанковскойГарантии(ИмяФайла)
	
	Возврат СтрНайти(Врег(ИмяФайла), "ON_SVBANKGAR");
	
КонецФункции
	
&НаСервере
Функция ПараметрыОткрытияЗагруженногоXMLФайла()
	
	СтруктураПараметров = Новый Структура;
	
	Дерево = РеквизитФормыВЗначение("ДеревоДокументов");
	
	Для каждого Колонка Из Дерево.Колонки Цикл
		СтруктураПараметров.Вставить(Колонка.Имя);
	КонецЦикла;
	
	Возврат СтруктураПараметров;

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СформироватьСтрокуПредставленияДокументов(КоличествоДокументов)
	
	РезультатСтрокой = СтрЗаменить(ЧислоПрописью(КоличествоДокументов, "Л = ru_RU; НП=Истина, НД=Ложь", "документ, документа, документов, м, , , , ,0"),
								ЧислоПрописью(КоличествоДокументов, "Л = ru_RU; НП=Ложь, НД=Ложь", " , , , , , , , ,0"),
								"");
	
	Возврат Формат(КоличествоДокументов, "ЧН=0; ЧГ=0") + " " + РезультатСтрокой;
	
КонецФункции

&НаСервере
Функция ОкруглитьРазмер(РазмерВБайтах)
	
	РазмерВМегабайтах = Окр(РазмерВБайтах / 1024 / 1024, 2, РежимОкругления.Окр15как20);
	
	Если РазмерВМегабайтах = 0 Тогда
		РазмерВМегабайтах = 0.01;	
	КонецЕсли;
	
	Возврат РазмерВМегабайтах;
	
КонецФункции

&НаСервере
Процедура ИзменитьОформлениеЕслиТолькоПросмотр()
	
	Если НЕ ЭтоИзменяемоеСообщение Тогда
		
		Элементы.ПредставляемыеДокументыФайлы.Видимость = Ложь;
		Элементы.ГруппаРазмерыОписи.Видимость = Ложь;
		
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ОбновитьРазмерыОписи(СсылкаНаДокумент)
	
	Если НЕ ЭтоИзменяемоеСообщение Тогда
		
		ИзменитьОформлениеЕслиТолькоПросмотр();
		Возврат;
		
	Иначе
		
		Элементы.ПредставляемыеДокументыФайлы.Видимость = Истина;
		Элементы.ГруппаРазмерыОписи.Видимость = Истина;	
	
	КонецЕсли;
	
	НадписьРазмерыОписи = Элементы.НадписьРазмерыОписи;
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	
	//Получим таблицу файлов
	ТЗДокументыФайлыРазмер = КонтекстЭДОСервер.ПолучитьТаблицуДокументовОписиИРазмерыФайлов(СсылкаНаДокумент);
	ОднаСтрока = ТЗДокументыФайлыРазмер.Количество() = 1;
	
	СуммаКолвоФайлов 	= 0;
	СуммаРазмерФайлов 	= 0;
	СоответствиеДокументФайлы = Новый Соответствие;
	
	СуммаКолвоФайловГруппы = 0;
	СуммаРазмерФайловГруппы = 0;
	
	//Сформируем соответствие файлов
	Для каждого СтрокаДокумент Из ТЗДокументыФайлыРазмер Цикл
		
		РазмерВБайтах = СтрокаДокумент.РазмерФайлов;
		КолвоФайлов = СтрокаДокумент.КоличествоФайлов;
		
		СуммаКолвоФайлов = СуммаКолвоФайлов + КолвоФайлов;
		СуммаРазмерФайлов = СуммаРазмерФайлов + РазмерВБайтах;

		СтруктураФайлы = Новый Структура;
		СтруктураФайлы.Вставить("Количество", КолвоФайлов);
		СтруктураФайлы.Вставить("Размер", РазмерВБайтах);
		
		СоответствиеДокументФайлы.Вставить(СтрокаДокумент.ИдентификаторДокумента, СтруктураФайлы);
		
	КонецЦикла;
	
	//Заполним таблицу документов данными из соответствия файлов
	Для каждого ГруппаДокументов Из ДеревоДокументов.ПолучитьЭлементы() Цикл
		
		Для каждого ЭлементГруппы Из ГруппаДокументов.ПолучитьЭлементы() Цикл
			
			Если ЗначениеЗаполнено(ЭлементГруппы.ИмяФайлаДанных) Тогда
				ИдентификаторДокумента = ЭлементГруппы.ИмяФайлаДанных;
			Иначе
				ИдентификаторДокумента = ЭлементГруппы.СсылкаНаОбъект;
			КонецЕсли; 
			
			СтруктураФайлы = СоответствиеДокументФайлы[ИдентификаторДокумента];
			Если СтруктураФайлы <> Неопределено Тогда
				РазмерВБайтах = СтруктураФайлы.Размер;
				РазмерВМегабайтах = ОкруглитьРазмер(РазмерВБайтах);
				
				ЭлементГруппы.Файлы = Формат(РазмерВМегабайтах, "ЧГ=0") + " Мб";
				ЭлементГруппы.КоличествоФайлов = СтруктураФайлы.Количество; // служебное невидимое поле
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;

	ЕстьТекст = ЗаполненТекстФорматированногоДокумента(Пояснение);
	Если ЕстьТекст Тогда
		
		РазмерПояснения = РазмерТекстовогоПоясненияПоРеквизитуФормы();
		
		СуммаКолвоФайлов = СуммаКолвоФайлов + 1;
		СуммаРазмерФайлов = СуммаРазмерФайлов + РазмерПояснения;
		
	КонецЕсли;
	
	//Заполним элементы группы размеров описи
	ДопустимоеКоличествоФайлов = МаксКолвоФайлов - СуммаКолвоФайлов;
	ДопустимоеКоличествоМегабайт = ОкруглитьРазмер(МаксРазмерФайлов - СуммаРазмерФайлов);
	ТребуетсяРазбитьНаНесколько = (ДопустимоеКоличествоФайлов < 0) ИЛИ (ДопустимоеКоличествоМегабайт < 0);
	
	СодержимоеТекста = "";
	
	Если ТребуетсяРазбитьНаНесколько Тогда
		
		ЦветТекста = Новый Цвет(255, 0, 0); // красный
		
		Если ДопустимоеКоличествоФайлов < 0 Тогда
			
			СодержимоеТекста = "Допустимое количество файлов превышено на " +(-ДопустимоеКоличествоФайлов) + ".";
			
		ИначеЕсли ДопустимоеКоличествоМегабайт < 0 Тогда
			
			СодержимоеТекста = "Допустимый размер описи превышен на " +(-ДопустимоеКоличествоМегабайт) + " Мб.";
		
		КонецЕсли;
		
		Если (ДопустимоеКоличествоФайлов < 0 ИЛИ ДопустимоеКоличествоМегабайт < 0) И ОднаСтрока Тогда
			СодержимоеТекста = СодержимоеТекста + НСтр("ru = ' Зайдите в документ и устраните нарушение';
														|en = ' Зайдите в документ и устраните нарушение'");
		КонецЕсли;
		
	Иначе
		//отправке ничего не угрожает
		
		ЦветТекста = Новый Цвет(134, 134, 134); //серый
		
		СодержимоеТекста = "Можно добавить еще " + ДопустимоеКоличествоМегабайт + " Мб.";
		
	КонецЕсли;
	
	НадписьРазмерыОписи.ЦветТекста = ЦветТекста;
	НадписьРазмерыОписи.Заголовок = СодержимоеТекста;
	
	Элементы.НадписьРазбитьНаНесколько.Видимость = ТребуетсяРазбитьНаНесколько И НЕ ОднаСтрока;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ДобавитьДокументыПередВыборомПунктаТребования()
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("МассивРезультат", МассивВыбранныхДокументов);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ДобавитьДокументыПоПунктуТребования", 
		ЭтотОбъект, 
		ДополнительныеПараметры);
	
	ОткрытьФормуВыбораПунктаТребованияСПроверкой(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьДеревоДокументов()
	
	Для Каждого СтрокаГруппы Из ДеревоДокументов.ПолучитьЭлементы() Цикл
        Элементы.ПредставляемыеДокументы.Развернуть(СтрокаГруппы.ПолучитьИдентификатор(), Истина);
	КонецЦикла;
	
КонецПроцедуры
	
&НаСервере
Функция ПолучитьТекущийДокумент()
	
	ТекущаяСтрока = Элементы.ПредставляемыеДокументы.ТекущаяСтрока;
	
	Если ТекущаяСтрока <> Неопределено Тогда
		ТекущийДокумент = ДеревоДокументов.НайтиПоИдентификатору(ТекущаяСтрока).СоставноеПоле;
	Иначе
		ТекущийДокумент = Неопределено;
	КонецЕсли;

	Возврат ТекущийДокумент;

КонецФункции 

&НаСервере
Процедура УстановитьТекущийДокумент(ПредставлениеДокумента)
	
	ИдентификаторСтроки = Неопределено;
	Для каждого ГруппаДокументов Из ДеревоДокументов.ПолучитьЭлементы() Цикл
		
		Для каждого ЭлементГруппы Из ГруппаДокументов.ПолучитьЭлементы() Цикл
			
			Если ЭлементГруппы.СоставноеПоле = ПредставлениеДокумента Тогда
				
				ИдентификаторСтроки = ЭлементГруппы.ПолучитьИдентификатор();
				Прервать;
				
			КонецЕсли; 
			
			
		КонецЦикла;
		
		Если ИдентификаторСтроки <> Неопределено Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ИдентификаторСтроки <> Неопределено Тогда
		Элементы.ПредставляемыеДокументы.ТекущаяСтрока = ИдентификаторСтроки;
	КонецЕсли;
	
КонецПроцедуры
	
&НаСервере
Функция ОбновитьФайлыДокументовИБНаСервере()
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	Если КонтекстЭДОСервер.ОбновитьФайлыДокументовИБОписиИсходящихДокументов(Объект.Ссылка) Тогда
		
		Прочитать();
		
		ПредставлениеТекущегоДокумента = ПолучитьТекущийДокумент();
		
		ДеревоДокументов.ПолучитьЭлементы().Очистить();
		ЗаполнитьДеревоДокументов(Объект.Ссылка);
		
		УстановитьТекущийДокумент(ПредставлениеТекущегоДокумента);
		
		ОбновитьРазмерыОписи(Объект.Ссылка);
		Возврат Истина;
	Иначе
		Возврат Ложь;	
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура РазбитьНаНесколькоЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ТекстСообщения = "";
	Если НЕ ПроверкаПередРазбивкой(ТекстСообщения) Тогда
		ПоказатьПредупреждение(, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ТекстПредупреждения = "";
	МассивСозданныхОписей = РазбитьНаНесколькоНаСервере(ТекстПредупреждения);
	Если МассивСозданныхОписей = Неопределено Тогда
		Если ЗначениеЗаполнено(ТекстПредупреждения) Тогда
			ПоказатьПредупреждение(, ТекстПредупреждения);
		КонецЕсли;
	    Возврат;
	Иначе
		
		Оповестить("Запись_ОписиИсходящихДокументовВНалоговыеОрганы",,Объект.Ссылка);
		
		Прочитать();

		//обновим дерево документов текущей описи   
		ДеревоДокументов.ПолучитьЭлементы().Очистить();
		ЗаполнитьДеревоДокументов(Объект.Ссылка);
		ОбновитьРазмерыОписи(Объект.Ссылка);
		
		//откроем форму групповой отправки
		СписокОписей = Новый СписокЗначений;
		СписокОписей.ЗагрузитьЗначения(МассивСозданныхОписей);
		СписокОписей.Добавить(Объект.Ссылка);
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("СписокОписей", 			СписокОписей);
		ПараметрыФормы.Вставить("Основание",    			Объект.Основание);
		ПараметрыФормы.Вставить("ЭтоПакетСДопДокументами",  ЭтоПакетСДопДокументами);
		
		ОткрытьФорму("Справочник.ОписиИсходящихДокументовВНалоговыеОрганы.Форма.ФормаГрупповойОтправки", ПараметрыФормы);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция РазбиениеВозможно(РазмерПояснения)
	
	// Проверка ситуации:
	// Размер вложения превышает 1024 Мб.
	// Разбиением на несколько описей ситуацию не исправить.
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	ТЗКритичныеФайлы = КонтекстЭДОСервер.КритичныеДокументыОписиИсходящихДокументовПриОтправке(Объект.Ссылка);
	
	ЕстьНедопустимыеФайлы = Ложь;
	Для каждого КритичныйФайл Из ТЗКритичныеФайлы Цикл
		Если КритичныйФайл.СтатусКритичности = 2 Тогда
			ЕстьНедопустимыеФайлы = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возможно = 
		НЕ ЕстьНедопустимыеФайлы 
		И РазмерПояснения < МаксРазмерФайлов;

	Возврат Возможно;
	
КонецФункции

&НаСервере
Функция ШаблонДерева()
	
	ДеревоСоздаваемыхОписей = Новый ДеревоЗначений;
	Реквизиты = Метаданные.Справочники.ОписиИсходящихДокументовВНалоговыеОрганы.ТабличныеЧасти.ПредставляемыеДокументы.Реквизиты;
	Для каждого Реквизит Из Реквизиты Цикл
		ДеревоСоздаваемыхОписей.Колонки.Добавить(Реквизит.Имя);
	КонецЦикла;

	Возврат ДеревоСоздаваемыхОписей;
	
КонецФункции

&НаСервере
Функция РаспределитьДокументыПоДереву(ДеревоСоздаваемыхОписей, ЗаполненоПояснение, РазмерПояснения)
	
	//ситации, при которых нельзя разбить проверили, начинаем разбивать
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	ТЗДокументыФайлыРазмер = КонтекстЭДОСервер.ПолучитьТаблицуДокументовОписиИРазмерыФайлов(Объект.Ссылка);
	
	СуммаКолвоФайлов 	= 0;
	СуммаРазмерФайлов 	= 0;
	
	НоваяСтрокаОпись = ДеревоСоздаваемыхОписей.Строки.Добавить();
	
	Для каждого СтрокаДокумент Из ТЗДокументыФайлыРазмер Цикл
		
		СуммаКолвоФайлов = СуммаКолвоФайлов + СтрокаДокумент.КоличествоФайлов;
		СуммаРазмерФайлов = СуммаРазмерФайлов + СтрокаДокумент.РазмерФайлов;

		Если (СуммаКолвоФайлов > МаксКолвоФайлов 
			ИЛИ СуммаРазмерФайлов > МаксРазмерФайлов) 
			И НоваяСтрокаОпись.Строки.Количество() > 0 Тогда
			
			//начинаем новую опись
			НоваяСтрокаОпись = ДеревоСоздаваемыхОписей.Строки.Добавить();
			
			СуммаКолвоФайлов = СтрокаДокумент.КоличествоФайлов;
			СуммаРазмерФайлов = СтрокаДокумент.РазмерФайлов;

		КонецЕсли;
		
		НоваяСтрокаДокумент = НоваяСтрокаОпись.Строки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаДокумент, СтрокаДокумент); 
	
	КонецЦикла;
	
	СтрокиОписей = ДеревоСоздаваемыхОписей.Строки;
	
	ТребуетсяРазбиение = 
		СтрокиОписей.Количество() > 1
		ИЛИ СуммаРазмерФайлов + ?(ЗаполненоПояснение,РазмерПояснения,0) > МаксРазмерФайлов 
		ИЛИ СуммаКолвоФайлов + ?(ЗаполненоПояснение,1,0) > МаксКолвоФайлов;
		
	Возврат ТребуетсяРазбиение;
	
КонецФункции

&НаСервере
Функция СтруктураРеквизитовОписи()
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	
	СтруктураРеквизитовОписи = КонтекстЭДОСервер.ПолучитьСтруктуруРеквизитовОписиИсходящихДокументов(Объект.Ссылка);
	ТекстПояснения = Справочники.ШаблоныПоясненийДляФНС.ПолучитьТекстИзФорматированногоДокумента(Пояснение);
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(СтруктураРеквизитовОписи, ТекстПояснения);

	Возврат СтруктураРеквизитовОписи;
	
КонецФункции	

&НаСервере
Функция РаспределитьДокументыПоОписям(ДеревоСоздаваемыхОписей, ЗаполненоПояснение)
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	
	СтруктураРеквизитовОписи = СтруктураРеквизитовОписи();
	
	ТаблицаФайловИзменяющихВладельца = Новый ТаблицаЗначений;
	ТаблицаФайловИзменяющихВладельца.Колонки.Добавить("ИмяФайла");
	ТаблицаФайловИзменяющихВладельца.Колонки.Добавить("НовыйВладелец");
	
	МассивСозданныхОписей = Новый Массив;
	// спроектировано наполнение описей, начинаем работать с метаданными
	НачатьТранзакцию();
	
	Попытка
		
		НачальнаяОписьЗаполнена = Ложь;
		
		РедактируемаяОписьСсылка = Объект.Ссылка;
		РедактируемаяОписьОбъект = Объект.Ссылка.ПолучитьОбъект();
		
		РедактируемаяОписьОбъект.ПредставляемыеДокументы.Очистить();
		РедактируемаяОписьОбъект.ТекстовоеПояснение = Неопределено;
		
		СтрокиОписей = ДеревоСоздаваемыхОписей.Строки;
		Для каждого СтрокаОпись Из СтрокиОписей Цикл
			
			Если НачальнаяОписьЗаполнена Тогда
				РедактируемаяОписьОбъект = СоздатьНовуюОпись(МассивСозданныхОписей, СтруктураРеквизитовОписи);
			КонецЕсли;
			
			//заполняем табличную часть
			Для каждого СтрокаДокумент Из СтрокаОпись.Строки Цикл
				
				НоваяСтрокаДокумент = РедактируемаяОписьОбъект.ПредставляемыеДокументы.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаДокумент, СтрокаДокумент);
				
				Если НачальнаяОписьЗаполнена И ЗначениеЗаполнено(СтрокаДокумент.ИмяФайлаДанных) Тогда
					//надо изменить владельца файла данных
					НоваяСтрокаФайл = ТаблицаФайловИзменяющихВладельца.Добавить();
					НоваяСтрокаФайл.ИмяФайла = СтрокаДокумент.ИмяФайлаДанных;
					НоваяСтрокаФайл.НовыйВладелец = РедактируемаяОписьСсылка;
					
					Если ЗначениеЗаполнено(СтрокаДокумент.ИмяФайлаПодписи) Тогда
						//надо изменить владельца файла подписи
						НоваяСтрокаФайл = ТаблицаФайловИзменяющихВладельца.Добавить();
						НоваяСтрокаФайл.ИмяФайла = СтрокаДокумент.ИмяФайлаПодписи;
						НоваяСтрокаФайл.НовыйВладелец = РедактируемаяОписьСсылка;
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;
			
			РедактируемаяОписьОбъект.Записать();
			НачальнаяОписьЗаполнена = Истина;
			
		КонецЦикла;
		
		//изменяем владельца у "переброшенных" файлов
		Успешно = КонтекстЭДОСервер.ИзменитьВладельцаФайловДокументовРеализацииПолномочийНалоговыхОрганов(
			Объект.Ссылка, 
			ТаблицаФайловИзменяющихВладельца);
			
		Если НЕ Успешно Тогда
			ОтменитьТранзакцию();
			Возврат Неопределено;
		КонецЕсли;
		
	Исключение
		
		ОтменитьТранзакцию();
		Возврат Неопределено;

	КонецПопытки;
	
	Если ЗаполненоПояснение Тогда
		СоздатьОписьСПояснением(МассивСозданныхОписей, СтруктураРеквизитовОписи);
	КонецЕсли;
	
	ЗафиксироватьТранзакцию();

	Возврат МассивСозданныхОписей;
	
КонецФункции


&НаСервере
Функция РазбитьНаНесколькоНаСервере(ТекстПредупреждения)
	
	ЗаполненоПояснение = ЗаполненТекстФорматированногоДокумента(Пояснение);
	РазмерПояснения    = РазмерТекстовогоПоясненияПоРеквизитуФормы();
	
	Возможно = РазбиениеВозможно(РазмерПояснения);
	
	Если НЕ Возможно Тогда
		ТекстПредупреждения = "Размеры некоторых файлов превышают допустимые! Требуется устранить нарушение до разбиения описи на несколько!";
		Возврат Неопределено;
	КонецЕсли;
	
	//ситации, при которых нельзя разбить проверили, начинаем разбивать
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	
	ДеревоСоздаваемыхОписей = ШаблонДерева();
	
	ТребуетсяРазбиение = РаспределитьДокументыПоДереву(
		ДеревоСоздаваемыхОписей, 
		ЗаполненоПояснение, 
		РазмерПояснения);
	
	Если НЕ ТребуетсяРазбиение Тогда
		//все файлы уместились в текущей описи, разбития не происходило
		ТекстПредупреждения = "Разделения описи не требуется.";
		Возврат Неопределено;
	КонецЕсли;
	
	МассивСозданныхОписей = РаспределитьДокументыПоОписям(
		ДеревоСоздаваемыхОписей, 
		ЗаполненоПояснение);
		
	Возврат МассивСозданныхОписей;
	
КонецФункции

&НаСервере
Функция СоздатьНовуюОпись(МассивСозданныхОписей, СтруктураРеквизитовОписи) 
	
	НоваяОписьОбъект = Справочники.ОписиИсходящихДокументовВНалоговыеОрганы.СоздатьЭлемент();
	ЗаполнитьЗначенияСвойств(НоваяОписьОбъект, СтруктураРеквизитовОписи);
	НоваяОписьОбъект.ДатаСоздания = ТекущаяДатаСеанса();
	НоваяОписьОбъект.ТекстовоеПояснение = Неопределено;
	НоваяОписьОбъект.Записать();
	
	НоваяОписьСсылка = НоваяОписьОбъект.Ссылка;
	
	МассивСозданныхОписей.Добавить(НоваяОписьСсылка);
	
	Возврат НоваяОписьОбъект;
			
КонецФункции

&НаСервере
Процедура СоздатьОписьСПояснением(МассивСозданныхОписей, СтруктураРеквизитовОписи)

	ОписьСПояснением = СоздатьНовуюОпись(МассивСозданныхОписей, СтруктураРеквизитовОписи);
	
	Справочники.ШаблоныПоясненийДляФНС.УстановитьТекстИзДанныхШаблона(СтруктураРеквизитовОписи, Пояснение);
		
	Справочники.ШаблоныПоясненийДляФНС.СохранитьФорматированныйДокумент(
		ОписьСПояснением, 
		Пояснение, 
		"ТекстовоеПояснение");
		
	ОписьСПояснением.Записать();
	
	Пояснение = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьДокументыЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыбора <> Неопределено И РезультатВыбора.Количество() > 0 Тогда
		
		МассивВыбранныхДокументов = РезультатВыбора;
		ПодключитьОбработчикОжидания("Подключаемый_ДобавитьДокументыПередВыборомПунктаТребования", 0.2, Истина);
						
	КонецЕсли;   
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьДокументыПоПунктуТребования(ВыбранныйПунктТребования, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(ВыбранныйПунктТребования) = Тип("Строка") Тогда
		//Был выбран пункт требования
		
		ОтработатьРезультатДобавленияДокументовНаСервере(ВыбранныйПунктТребования, ДополнительныеПараметры.МассивРезультат);
		
		Записать();
		ОбновитьРазмерыОписи(Объект.Ссылка);
		
		РазвернутьДеревоДокументов();
		
		УстановитьМодифицированность(ЭтотОбъект);
		
	Иначе
		//Не был выбран пункт требования 	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьМодифицированность(Форма)
	Форма.Модифицированность = Истина;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСоответствиеВидДокументаФНСТипуВыбранногоДокументаНДС()
	
	РезультатСоответствие = Новый Соответствие; 
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	Виды = КонтекстЭДОСервер.ВидыПредставляемыхДокументов();
	
	МассивВидовДокументовНДС = Новый Массив;
	Для каждого Вид Из Виды Цикл
		Если Вид.ЭтоНДС Тогда
			МассивВидовДокументовНДС.Добавить(Вид.Значение);
		КонецЕсли;
	КонецЦикла; 

	СоответствиеТиповИсточниковВидамДокументовФНС = Новый Соответствие;
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСерверПереопределяемый.ОпределитьСоответствиеТиповИсточниковВидамДокументовФНС(СоответствиеТиповИсточниковВидамДокументовФНС);
	
	Для каждого ВидДокументаНДС Из МассивВидовДокументовНДС Цикл
		МассивТипоИсточников = СоответствиеТиповИсточниковВидамДокументовФНС[ВидДокументаНДС];
		
		Если МассивТипоИсточников <> Неопределено Тогда
			Для каждого ТипИсточника Из МассивТипоИсточников Цикл
				РезультатСоответствие.Вставить(ТипИсточника, ВидДокументаНДС);
			КонецЦикла;
		КонецЕсли;	
	КонецЦикла;

	Возврат РезультатСоответствие;
	
КонецФункции 

&НаКлиенте
Процедура ДобавитьДокументыНДСЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		СоответствиеВидДокументаФНСТипуВыбранногоДокументаНДС = ПолучитьСоответствиеВидДокументаФНСТипуВыбранногоДокументаНДС();
		
		ВидДокумента = СоответствиеВидДокументаФНСТипуВыбранногоДокументаНДС[ТипЗнч(Результат)];
		
		Если ВидДокумента = Неопределено  Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Выбор отменен. Не удалось определить вид выбранного документа.';
															|en = 'Выбор отменен. Не удалось определить вид выбранного документа.'"));
			Возврат;
		КонецЕсли;
		
		МассивВыбранныхДокументов = Новый Массив;
		ТекущийДокумент = Новый Структура("ВыбранныйДокумент, ВидДокументаФНС");
		ТекущийДокумент.ВыбранныйДокумент 	= Результат;
		ТекущийДокумент.ВидДокументаФНС 	= ВидДокумента;
		
		МассивВыбранныхДокументов.Добавить(ТекущийДокумент);
		ПодключитьОбработчикОжидания("Подключаемый_ДобавитьДокументыПередВыборомПунктаТребования", 0.2, Истина);
	КонецЕсли;     
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьДокументыИзФайлаОбменаЗавершение(СтруктураВыбора, ДополнительныеПараметры) Экспорт
	
	Если СтруктураВыбора <> Неопределено Тогда
		
		// выберем пункт требования и добавим документы
		//Описание оповещения
		ДополнительныеПараметры = Новый Структура("СтруктураВыбора", СтруктураВыбора); 
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ДобавитьДокументыИзФайлаОбменаПоПунктуТребования", 
			ЭтотОбъект, 
			ДополнительныеПараметры);
		
		ОткрытьФормуВыбораПунктаТребованияСПроверкой(ОписаниеОповещения);
					
	КонецЕсли;   
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьДокументыИзФайлаОбменаПоПунктуТребования(ВыбранныйПунктТребования, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(ВыбранныйПунктТребования) = Тип("Строка") Тогда
		//Был выбран пункт требования
		
		ДобавитьДокументыФайлаОбменаНаСервере(ВыбранныйПунктТребования, ДополнительныеПараметры.СтруктураВыбора);
		
		Записать();
		ОбновитьРазмерыОписи(Объект.Ссылка);
		
	Иначе
		//Не был выбран пункт требования 	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЕстьНовыеДокументы(ЗагружаемыеДокументы, СтрокиДерева, ИмяПоля = "ФайлВыгрузкиИмя")

	ЕстьНовыеДокументы = Ложь;
	
	Для каждого ЗагружаемыйДокумент Из ЗагружаемыеДокументы Цикл
		
		//поиск дублей
		НайденнаяСтрока = СтрокиДерева.Найти(ЗагружаемыйДокумент[ИмяПоля], "ИмяФайлаДанных", Истина);
		Если НайденнаяСтрока = Неопределено Тогда
			// это новый документ
			ЕстьНовыеДокументы = Истина;
			Прервать;
		КонецЕсли;	
		
	КонецЦикла;
	
	Возврат ЕстьНовыеДокументы;

КонецФункции

&НаСервере
Процедура ДобавитьНовыйФайл(НовыеФайлы, ЗагружаемыйДокумент)
	
	// добавляем в массив НовыеФайлы имена файлов выгрузки и подписи 
	НовыеФайлы.Добавить(ЗагружаемыйДокумент.ФайлВыгрузкиИмя);
	НовыеФайлы.Добавить(ЗагружаемыйДокумент.ФайлПодписиИмя);
	
	Если ЗначениеЗаполнено(ЗагружаемыйДокумент.ПодтверждениеФайлВыгрузкиИмя) Тогда
		НовыеФайлы.Добавить(ЗагружаемыйДокумент.ПодтверждениеФайлВыгрузкиИмя);
		НовыеФайлы.Добавить(ЗагружаемыйДокумент.ПодтверждениеФайлПодписиИмя);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьСоставноеПоле(НоваяСтрока)
	
	Если НоваяСтрока.ЭтоГруппа Тогда
				
		ТекстПорядковыйНомерВыгрузки = ?(НоваяСтрока.ЯвляетсяИнформацией, "2.", "1.");
		ТекстПорядковыйНомерВыгрузки = ТекстПорядковыйНомерВыгрузки + Формат(НоваяСтрока.ПорядковыйНомер, "ЧЦ=2; ЧН=; ЧВН=");
		
		ДокументыГруппы = НоваяСтрока.ПолучитьЭлементы();
		
		КоличествоДокументов = ДокументыГруппы.Количество();
		НоваяСтрока.СоставноеПоле = 
			"Пункт требования " 
			+ ТекстПорядковыйНомерВыгрузки 
			+ " (" + СформироватьСтрокуПредставленияДокументов(КоличествоДокументов) + ")";
		
	ИначеЕсли ЗначениеЗаполнено(НоваяСтрока.ЗД_Номер) И ЗначениеЗаполнено(НоваяСтрока.ЗД_Дата) Тогда
	
		ДобавкаНаправление = ?(НоваяСтрока.ЗД_Направление = "Входящий", " (Полученный)", "");
		
		НоваяСтрока.СоставноеПоле = 
			Строка(НоваяСтрока.ВидДокумента) 
			+ " " 
			+ НоваяСтрока.ЗД_Номер 
			+ " от " 
			+ Формат(НоваяСтрока.ЗД_Дата, "ДЛФ=D") 
			+ ДобавкаНаправление;
			
	ИначеЕсли ЗначениеЗаполнено(НоваяСтрока.ИмяФайлаДанных) Тогда
			
		НоваяСтрока.СоставноеПоле = НоваяСтрока.ИмяФайлаДанных;
			
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ДобавитьДокументыФайлаОбменаНаСервере(ПунктТребования, СтруктураВыбора)
	
	ЗагружаемыеДокументы = ПолучитьИзВременногоХранилища(СтруктураВыбора.АдресТЗЗагруженныеДокументы);
	
	ДокументыДеревоЗначений = РеквизитФормыВЗначение("ДеревоДокументов", Тип("ДеревоЗначений"));
	СтрокиДерева = ДокументыДеревоЗначений.Строки;

	Если НЕ ЕстьНовыеДокументы(ЗагружаемыеДокументы, СтрокиДерева) Тогда
		//все документы уже загружены
		Возврат Истина;	
	КонецЕсли;
	
	СвойстваПунктаТребования = РазложитьПунктТребования(ПунктТребования);
	ГруппаДокументов = ОпределитьГруппуДокументовПоНомеруПункта(СтрокиДерева, СвойстваПунктаТребования, ПунктТребования);
	
	// заполняем дерево документов и массив имен новых файлов
	НовыеФайлы = Новый Массив;
	
	Для каждого ЗагружаемыйДокумент Из ЗагружаемыеДокументы Цикл
		
		//поиск дублей
		НайденнаяСтрока = СтрокиДерева.Найти(ЗагружаемыйДокумент.ФайлВыгрузкиИмя, "ИмяФайлаДанных", Истина);
		Если НайденнаяСтрока <> Неопределено Тогда
			// такой документ уже есть
			Продолжить;
		КонецЕсли;	
		
		// добавляем в массив НовыеФайлы имена файлов выгрузки и подписи 
		ДобавитьНовыйФайл(НовыеФайлы, ЗагружаемыйДокумент);
		
		НоваяСтрока = ГруппаДокументов.Строки.Добавить();
		НоваяСтрока.Загружен 			= Истина;
		НоваяСтрока.ЯвляетсяИнформацией = СвойстваПунктаТребования.ЯвляетсяИнформацией;
		НоваяСтрока.ПорядковыйНомер 	= СвойстваПунктаТребования.ПорядковыйНомер;
		НоваяСтрока.ВидДокумента 		= ЗагружаемыйДокумент.ВидДокумента;	
		НоваяСтрока.ЗД_Номер 			= ЗагружаемыйДокумент.Номер;
		НоваяСтрока.ЗД_Дата 			= ЗагружаемыйДокумент.Дата;
		НоваяСтрока.ЗД_Контрагент 		= ЗагружаемыйДокумент.Контрагент;
		НоваяСтрока.ДатаДокОсн 			= ЗагружаемыйДокумент.ДатаДокОсн;
		НоваяСтрока.НомерДокОсн 		= ЗагружаемыйДокумент.НомерДокОсн;
		НоваяСтрока.КНД 				= ЗагружаемыйДокумент.КНД;
		НоваяСтрока.ЗД_Направление 		= ЗагружаемыйДокумент.Направление;
		НоваяСтрока.ИмяФайлаДанных 		= ЗагружаемыйДокумент.ФайлВыгрузкиИмя;
		НоваяСтрока.ИмяФайлаПодписи 	= ЗагружаемыйДокумент.ФайлПодписиИмя;
		НоваяСтрока.ПодтверждениеКНД 				= ЗагружаемыйДокумент.ПодтверждениеКНД;
		НоваяСтрока.ПодтверждениеИмяФайлаДанных 	= ЗагружаемыйДокумент.ПодтверждениеФайлВыгрузкиИмя;
		НоваяСтрока.ПодтверждениеИмяФайлаПодписи 	= ЗагружаемыйДокумент.ПодтверждениеФайлПодписиИмя;
		
		ЗаполнитьСоставноеПоле(НоваяСтрока);
		
	КонецЦикла;
	
	СохранитьИзменения(ДокументыДеревоЗначений);

	ПолноеИмяФайлаОбмена = СтруктураВыбора.ПолноеИмяФайлаОбмена;
	Если НЕ ЗагрузитьФайлыИзФайлаОбменаВИБ(Объект.Ссылка, ПолноеИмяФайлаОбмена, НовыеФайлы) Тогда
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Произошла ошибка при загрузке документов из файла обмена.';
				|en = 'Произошла ошибка при загрузке документов из файла обмена.'"));
		Возврат Ложь;
	КонецЕсли; 
	
КонецФункции

&НаСервере
Процедура СохранитьИзменения(ДокументыДеревоЗначений)

	// Преобразование объекта прикладного типа ДеревоЗначений
	// в реквизит управляемой формы (данные формы)
	ЗначениеВРеквизитФормы(ДокументыДеревоЗначений, "ДеревоДокументов");

	ЗаполнитьВычисляемыеПоляДереваДокументов(ЭтотОбъект);

	// Загружаем новые файлы из пакета обмена
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Записать();
	КонецЕсли;

КонецПроцедуры 

&НаСервереБезКонтекста
Функция ПолучитьСтрокойИННКППОрганизации(СсылкаОрганизация)
	
	Если НЕ РегламентированнаяОтчетностьВызовСервера.ЭтоЮридическоеЛицо(СсылкаОрганизация) Тогда
				Сведения = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(СсылкаОрганизация, , "ИННФЛ");
				ИННКППОрганизации = СокрЛП(Сведения.ИННФЛ);
			Иначе
				Сведения = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(СсылкаОрганизация, , "ИННЮЛ, КППЮЛ");
				ИННКППОрганизации = СокрЛП(Сведения.ИННЮЛ) + СокрЛП(Сведения.КППЮЛ);
			КонецЕсли;
			
		
	Возврат ИННКППОрганизации;        
	
КонецФункции

&НаСервере
Функция РазложитьПунктТребования(ПунктТребованияСтрокой)
	
	// Пример "1.01"
	
	ЯвляетсяИнформацией = НЕ (Лев(ПунктТребованияСтрокой, 1) = "1");
	ПорядковыйНомер = Число(Прав(ПунктТребованияСтрокой, 2));
	СтруктураРезультат = Новый Структура("ЯвляетсяИнформацией, ПорядковыйНомер", ЯвляетсяИнформацией, ПорядковыйНомер);
	
	Возврат СтруктураРезультат;	
		
КонецФункции

&НаСервере
Процедура ДобавитьФайл(ВсеФайлы, Имя, Адрес)
	
	НовыйФайл = ВсеФайлы.Добавить();
	НовыйФайл.ИмяФайла 	 = Имя;
	НовыйФайл.АдресФайла = Адрес;
	
КонецПроцедуры

&НаСервере
Функция ВидыДобавляемыхДокументов(СтрокиДерева, МассивРезультат)
	
	УникальныеДокументы = Новый Массив;
	ДокументыНДС = Новый Массив;
	ДокументыЭДО = Новый Массив;
	АктыПриемки	= Новый Массив;
	
	ТипСкан = Тип("СправочникСсылка.СканированныеДокументыДляПередачиВЭлектронномВиде");
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	ВидыДокументовНДС = КонтекстЭДОСервер.ДокументЭДОпоНДС();
	ВидыДокументовЭД  = КонтекстЭДОСервер.ДокументЭДОСЭД();
	
	Для каждого ТекущийДокумент Из МассивРезультат Цикл
		
		СсылкаНаОбъект 	= ТекущийДокумент.ВыбранныйДокумент;
		ВидДокумента 	= ТекущийДокумент.ВидДокументаФНС;
		
		//поиск дублей
		НайденнаяСтрока = СтрокиДерева.Найти(СсылкаНаОбъект, "СсылкаНаОбъект", Истина);
		Если НайденнаяСтрока <> Неопределено Тогда
			// такая ссылка уже есть
			Продолжить;
		КонецЕсли;	
		
		УникальныеДокументы.Добавить(ТекущийДокумент);
		
		Если ТипЗнч(СсылкаНаОбъект) <> ТипСкан Тогда
			
			Если ВидыДокументовНДС.Найти(ВидДокумента) <> Неопределено Тогда
				
				ДокументыНДС.Добавить(СсылкаНаОбъект);
				
			ИначеЕсли ВидыДокументовЭД.Найти(ВидДокумента) <> Неопределено Тогда
				
				ДокументыЭДО.Добавить(СсылкаНаОбъект);
				
				Если ВидДокумента = Перечисления.ВидыПредставляемыхДокументов.АктПриемкиСдачиРабот Тогда
					АктыПриемки.Добавить(СсылкаНаОбъект);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("УникальныеДокументы", УникальныеДокументы);
	ДополнительныеПараметры.Вставить("ДокументыНДС", ДокументыНДС);
	ДополнительныеПараметры.Вставить("ДокументыЭДО", ДокументыЭДО);
	ДополнительныеПараметры.Вставить("АктыПриемки",  АктыПриемки);
	
	Возврат ДополнительныеПараметры;
	
КонецФункции

&НаСервере
Процедура ДобавитьДокументНДС(ВсеФайлы, ФайлыДокументаНДС, НоваяСтрока, СсылкаНаОбъект)
	
	Для каждого ФайлДокументаНДС Из ФайлыДокументаНДС Цикл
		
		Если ФайлДокументаНДС.ТипФайла = "ФайлВыгрузки" Тогда
			НоваяСтрока.ИмяФайлаДанных = ФайлДокументаНДС.ИмяФайла;
			НоваяСтрока.ВерсияФайлаДанных = СсылкаНаОбъект.ВерсияДанных;
		КонецЕсли;
		
		ДобавитьФайл(
			ВсеФайлы, 
			ФайлДокументаНДС.ИмяФайла, 
			ФайлДокументаНДС.АдресВременногоХранилища);
			
	КонецЦикла;
		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ИмяФайлаПодписи(ИмяФайла)
	
	Возврат ДокументооборотСКОКлиентСервер.ИмяФайлаЭПФайлаОписиИсходящихДокументов(ИмяФайла);
	
КонецФункции

&НаСервере
Процедура ДобавитьДокументЭДО(ВсеФайлы, ФайлыДокументаЭДО, НоваяСтрока, СсылкаНаОбъект, РеквизитыДоговоров)
	
	// сначала извлекаем имена файлов выгрузки (относительно них генерируем имена файлов подписи)
			
	ИмяФайлаВыгрузкиЭДО 	= "";
	ИмяФайлаПодтверждения 	= "";
	
	Для каждого ФайлДокументаЭДО Из ФайлыДокументаЭДО Цикл
		Если ФайлДокументаЭДО.ТипФайла = "ФайлВыгрузки" Тогда
			ИмяФайлаВыгрузкиЭДО = ФайлДокументаЭДО.ИмяФайла;	
		ИначеЕсли ФайлДокументаЭДО.ТипФайла = "ФайлПодтверждения" Тогда
			ИмяФайлаПодтвержденияЭДО = ФайлДокументаЭДО.ИмяФайла;
		КонецЕсли;
	КонецЦикла;
	
	Для каждого ФайлДокументаЭДО Из ФайлыДокументаЭДО Цикл
		
		Если ФайлДокументаЭДО.ТипФайла = "ФайлВыгрузки" Тогда
			НоваяСтрока.ИмяФайлаДанных = ИмяФайлаВыгрузкиЭДО;
			НоваяСтрока.ВерсияФайлаДанных = СсылкаНаОбъект.ВерсияДанных;
			НоваяСтрока.КНД = ФайлДокументаЭДО.КНД;
			
			ДобавитьФайл(
				ВсеФайлы, 
				НоваяСтрока.ИмяФайлаДанных, 
				ФайлДокументаЭДО.АдресВременногоХранилища);
			
		ИначеЕсли ФайлДокументаЭДО.ТипФайла = "ЭЦП" Тогда
			НоваяСтрока.ИмяФайлаПодписи = ИмяФайлаПодписи(ИмяФайлаВыгрузкиЭДО);
			
			ДобавитьФайл(
				ВсеФайлы, 
				НоваяСтрока.ИмяФайлаПодписи, 
				ФайлДокументаЭДО.АдресВременногоХранилища);
			
		ИначеЕсли ФайлДокументаЭДО.ТипФайла = "ФайлПодтверждения" Тогда
			НоваяСтрока.ПодтверждениеИмяФайлаДанных = ИмяФайлаПодтвержденияЭДО;
			НоваяСтрока.ПодтверждениеКНД = ФайлДокументаЭДО.КНД;
			
			ДобавитьФайл(
				ВсеФайлы, 
				НоваяСтрока.ПодтверждениеИмяФайлаДанных, 
				ФайлДокументаЭДО.АдресВременногоХранилища);
			
		ИначеЕсли ФайлДокументаЭДО.ТипФайла = "ЭЦППодтверждения" Тогда
			НоваяСтрока.ПодтверждениеИмяФайлаПодписи = ИмяФайлаПодписи(ИмяФайлаПодтвержденияЭДО);
			
			ДобавитьФайл(
				ВсеФайлы, 
				НоваяСтрока.ПодтверждениеИмяФайлаПодписи, 
				ФайлДокументаЭДО.АдресВременногоХранилища);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если НоваяСтрока.ВидДокумента = Перечисления.ВидыПредставляемыхДокументов.АктПриемкиСдачиРабот Тогда
		//Заполним реквизиты договора
		Если РеквизитыДоговоров <> Неопределено Тогда
			РеквизитыДоговора = РеквизитыДоговоров[СсылкаНаОбъект];
		Иначе
			РеквизитыДоговора = Неопределено;
		КонецЕсли;
		
		Если РеквизитыДоговора <> Неопределено Тогда
			НоваяСтрока.НомерДокОсн = РеквизитыДоговора.НомерДоговора;
			НоваяСтрока.ДатаДокОсн  = РеквизитыДоговора.ДатаДоговора;
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Функция ОпределитьГруппуДокументовПоНомеруПункта(СтрокиДерева, СвойстваПунктаТребования, ПунктТребования)
	
	//поиск указанного пункта требования
	ГруппаДокументов = СтрокиДерева.Найти(ПунктТребования, "ПорядковыйНомерВыгрузки", Ложь);
	Если ГруппаДокументов = Неопределено Тогда
		
		// такой группы еще нет, добавим новую строку
		ГруппаДокументов = СтрокиДерева.Добавить();
		ГруппаДокументов.ЯвляетсяИнформацией = СвойстваПунктаТребования.ЯвляетсяИнформацией;
		ГруппаДокументов.ПорядковыйНомер     = СвойстваПунктаТребования.ПорядковыйНомер;

	КонецЕсли;
	
	Возврат ГруппаДокументов;

КонецФункции

&НаСервере
Процедура ОтработатьРезультатДобавленияДокументовНаСервере(ПунктТребования, МассивРезультат)
	
	ТипСкан = Тип("СправочникСсылка.СканированныеДокументыДляПередачиВЭлектронномВиде");
	
	ДокументыДеревоЗначений = РеквизитФормыВЗначение("ДеревоДокументов", Тип("ДеревоЗначений"));
	СтрокиДерева = ДокументыДеревоЗначений.Строки;

	Виды = ВидыДобавляемыхДокументов(СтрокиДерева, МассивРезультат);
	
	Если Виды.УникальныеДокументы.Количество() = 0 Тогда
		//нет добавляемых документов
		Возврат;
	КонецЕсли;
	
	СвойстваПунктаТребования = РазложитьПунктТребования(ПунктТребования);
	
	//поиск указанного пункта требования
	ГруппаДокументов = ОпределитьГруппуДокументовПоНомеруПункта(СтрокиДерева, СвойстваПунктаТребования, ПунктТребования);
	
	ВсеФайлы = Новый ТаблицаЗначений;
	ВсеФайлы.Колонки.Добавить("ИмяФайла");
	ВсеФайлы.Колонки.Добавить("АдресФайла");
	
	// обработаем сформированные массивы
	// сформируем соответствия с файлами выгрузки
	ПереопределяемыйМодуль = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервераПереопределяемый;
	
	ФайлыДокументовНДС = ПереопределяемыйМодуль.ПолучитьФайлыВыгрузкиНДС(Виды.ДокументыНДС, УникальныйИдентификатор);
	ФайлыДокументовЭДО = ПереопределяемыйМодуль.ПолучитьФайлыВыгрузкиЭД(Виды.ДокументыЭДО, УникальныйИдентификатор);
	РеквизитыДоговоров = ПереопределяемыйМодуль.ПолучитьНомерДатаДоговораДокументов(Виды.АктыПриемки);
	
	Для каждого ТекущийДокумент Из Виды.УникальныеДокументы Цикл
		
		СсылкаНаОбъект 	= ТекущийДокумент.ВыбранныйДокумент;
		ВидДокумента 	= ТекущийДокумент.ВидДокументаФНС;
		
		НоваяСтрока = ГруппаДокументов.Строки.Добавить();
		НоваяСтрока.ЯвляетсяИнформацией = СвойстваПунктаТребования.ЯвляетсяИнформацией;
		НоваяСтрока.ПорядковыйНомер     = СвойстваПунктаТребования.ПорядковыйНомер;
		НоваяСтрока.СсылкаНаОбъект      = СсылкаНаОбъект;
		НоваяСтрока.ВидДокумента        = ВидДокумента;
		
		ЭтоСкан = ТипЗнч(СсылкаНаОбъект) = ТипСкан;
		
		Если ЭтоСкан Тогда
			Продолжить;
		КонецЕсли;
			
		Если ФайлыДокументовНДС <> Неопределено Тогда
			ФайлыДокументаНДС = ФайлыДокументовНДС.Получить(СсылкаНаОбъект);
		КонецЕсли;
		
		Если ФайлыДокументовЭДО <> Неопределено Тогда
			ФайлыДокументаЭДО = ФайлыДокументовЭДО.Получить(СсылкаНаОбъект);
		КонецЕсли;
		
		ЭтоДокументНДС = ФайлыДокументаНДС <> Неопределено;
		ЭтоДокументЭДО = ФайлыДокументаЭДО <> Неопределено;
		
		Если ЭтоДокументНДС Тогда
			ДобавитьДокументНДС(ВсеФайлы, ФайлыДокументаНДС, НоваяСтрока, СсылкаНаОбъект);
		ИначеЕсли ЭтоДокументЭДО Тогда
			ДобавитьДокументЭДО(ВсеФайлы, ФайлыДокументаЭДО, НоваяСтрока, СсылкаНаОбъект, РеквизитыДоговоров);
		КонецЕсли;
		
	КонецЦикла;
	
	СохранитьИзменения(ДокументыДеревоЗначений);

	// Загружаем новые файлы из документов ИБ	
	Если ВсеФайлы.Количество() > 0 Тогда
		
		КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
		КонтекстЭДОСервер.ПоместитьФайлыДокументовРеализацииПолномочийНалоговыхОрганов(Объект.Ссылка, ВсеФайлы);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УдалитьФайлыЗагруженныхДокументовПоМассивуИмен(ОбъектСсылка, МассивИменУдаляемыхФайлов)
	
	НаборЗаписей = РегистрыСведений.ФайлыДокументовРеализацииПолномочийНалоговыхОрганов.СоздатьНаборЗаписей(); 
	НаборЗаписей.Отбор.Документ.Установить(ОбъектСсылка); 
	
	Для каждого ИмяУдаляемогоФайла Из МассивИменУдаляемыхФайлов Цикл
		НаборЗаписей.Отбор.ИмяФайла.Установить(ИмяУдаляемогоФайла); 
		НаборЗаписей.Записать();
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ДополнитьМассивЗначением(ДополняемыйМассив, Знач ДополняемоеЗначение)
	
	Если ДополняемыйМассив.Найти(ДополняемоеЗначение) = Неопределено  Тогда
		//такого элемента еще нет
		ДополняемыйМассив.Добавить(ДополняемоеЗначение);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередИнтерактивнымУдалениемВыделенныхСтрокНаСервере(Знач МассивИдентификаторовВыделенныхСтрок)
	
	// все выделенные строки будут удалены
	// удалим файлы удаляемых документов из ИБ
	
	// преобразуем массив, разложив выделенные строки групп на составляющие
	НовыйМассивИдентификаторов = Новый Массив;
	Для каждого ИдентификаторВыделеннойСтроки Из МассивИдентификаторовВыделенныхСтрок Цикл
		ВыделеннаяСтрока = ДеревоДокументов.НайтиПоИдентификатору(ИдентификаторВыделеннойСтроки);
		Если ВыделеннаяСтрока.ЭтоГруппа Тогда
			//это группа
			Для каждого СтрокаДокумента Из ВыделеннаяСтрока.ПолучитьЭлементы() Цикл
				ДополнитьМассивЗначением(НовыйМассивИдентификаторов, СтрокаДокумента.ПолучитьИдентификатор());
			КонецЦикла;
		Иначе
			//это документ
			ДополнитьМассивЗначением(НовыйМассивИдентификаторов, ИдентификаторВыделеннойСтроки);
		КонецЕсли;
	КонецЦикла;
	
	// соберем массив МассивИменУдаляемыхФайлов
	МассивИменУдаляемыхФайлов = Новый Массив;
	
	Для каждого ИдентификаторСтрокиУдаляемогоДокумента Из НовыйМассивИдентификаторов Цикл
		СтрокаДокумента = ДеревоДокументов.НайтиПоИдентификатору(ИдентификаторСтрокиУдаляемогоДокумента);
		
		ИмяФайлаДанных 	= СтрокаДокумента.ИмяФайлаДанных;
		ИмяФайлаПодписи	= СтрокаДокумента.ИмяФайлаПодписи;
		ПодтверждениеИмяФайлаДанных 	= СтрокаДокумента.ПодтверждениеИмяФайлаДанных;
		ПодтверждениеИмяФайлаПодписи	= СтрокаДокумента.ПодтверждениеИмяФайлаПодписи;
		
		Если ЗначениеЗаполнено(ИмяФайлаДанных) Тогда
			МассивИменУдаляемыхФайлов.Добавить(ИмяФайлаДанных);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ИмяФайлаПодписи) Тогда
			МассивИменУдаляемыхФайлов.Добавить(ИмяФайлаПодписи);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПодтверждениеИмяФайлаДанных) Тогда
			МассивИменУдаляемыхФайлов.Добавить(ПодтверждениеИмяФайлаДанных);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПодтверждениеИмяФайлаПодписи) Тогда
			МассивИменУдаляемыхФайлов.Добавить(ПодтверждениеИмяФайлаПодписи);
		КонецЕсли;

		
	КонецЦикла;
	
	// Удаляем файлы из регистра сведений согласно массиву МассивИменУдаляемыхФайлов 
	Если МассивИменУдаляемыхФайлов.Количество() > 0  Тогда
		УдалитьФайлыЗагруженныхДокументовПоМассивуИмен(Объект.Ссылка, МассивИменУдаляемыхФайлов);	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьПустыеГруппыДокументов()
	
	МассивУдаляемыхГрупп = Новый Массив;
	
	Группы = ДеревоДокументов.ПолучитьЭлементы();
	Для каждого Группа Из Группы Цикл
		
		ДокументыГруппы = Группа.ПолучитьЭлементы();
		Если ДокументыГруппы.Количество() = 0 Тогда
		
			МассивУдаляемыхГрупп.Добавить(Группа);
		
		КонецЕсли;
		
	КонецЦикла;
	
	Для каждого УдаляемаяГруппа Из МассивУдаляемыхГрупп Цикл
		
		ИндексУдаляемойГруппы = Группы.Индекс(УдаляемаяГруппа);
		Группы.Удалить(ИндексУдаляемойГруппы);
		
	КонецЦикла;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗагрузитьФайлыИзФайлаОбменаВИБ(ОбъектСсылка, ПолноеИмяФайлаОбмена, МассивИменЗагружаемыхФайлов)
	
	ОставитьУникальныеЗагружаемыеФайлы(МассивИменЗагружаемыхФайлов, ОбъектСсылка);
	
	Если МассивИменЗагружаемыхФайлов.Количество() = 0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	// Извлекаем все файлы из ZIP файла обмена
	
	Попытка
		ФайлОбмена = Новый Файл(ПолноеИмяФайлаОбмена);
		
		КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
		
		КаталогОбмена = ОперацииСФайламиЭДКО.СоздатьВременныйКаталог();
		
		ЧтениеЗИП = Новый ЧтениеZipФайла(ПолноеИмяФайлаОбмена);	
		ЧтениеЗИП.ИзвлечьВсе(КаталогОбмена);
		
		// Наполняем регистр сведений ФайлыДокументовРеализацииПолномочийНалоговыхОрганов файлами
		
		НаборЗаписей = РегистрыСведений.ФайлыДокументовРеализацииПолномочийНалоговыхОрганов.СоздатьНаборЗаписей(); 
		НаборЗаписей.Отбор.Документ.Установить(ОбъектСсылка); 
		НаборЗаписей.Прочитать();
		
		Для каждого ИмяЗагружаемогоФайла Из МассивИменЗагружаемыхФайлов Цикл
			
			ПолноеИмяЗагружаемогоФайла = КаталогОбмена + ИмяЗагружаемогоФайла;
			ЗагружаемыйФайл = Новый Файл(ПолноеИмяЗагружаемогоФайла);
			РазмерФайла = ЗагружаемыйФайл.Размер();
			
			НоваяЗапись = НаборЗаписей.Добавить();
			
			НоваяЗапись.Документ = ОбъектСсылка;
			НоваяЗапись.ИмяФайла = ИмяЗагружаемогоФайла;
			НоваяЗапись.ТипСодержимого = Перечисления.ТипыСодержимогоФайлов.xml;
			НоваяЗапись.Размер = РазмерФайла;
			НоваяЗапись.Данные = Новый ХранилищеЗначения(Новый ДвоичныеДанные(ПолноеИмяЗагружаемогоФайла), Новый СжатиеДанных(9));;
			
		КонецЦикла;
		
		НаборЗаписей.Записать();
		
		// Все необходимые файлы добвлены в ИБ. Удаляем каталог обмена и файл обмена.
		УдалитьФайлы(КаталогОбмена);
		УдалитьФайлы(ПолноеИмяФайлаОбмена);
		
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;	
	
КонецФункции

&НаСервереБезКонтекста
Функция ОставитьУникальныеЗагружаемыеФайлы(ЗагружаемыеФайлы, ОбъектСсылка)
	
	СписокИменФайлов = Новый СписокЗначений;
	СписокИменФайлов.ЗагрузитьЗначения(ЗагружаемыеФайлы);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ФайлыДокументовРеализацииПолномочийНалоговыхОрганов.ИмяФайла
		|ИЗ
		|	РегистрСведений.ФайлыДокументовРеализацииПолномочийНалоговыхОрганов КАК ФайлыДокументовРеализацииПолномочийНалоговыхОрганов
		|ГДЕ
		|	ФайлыДокументовРеализацииПолномочийНалоговыхОрганов.Документ = &ОбъектСсылка
		|	И ФайлыДокументовРеализацииПолномочийНалоговыхОрганов.ИмяФайла В(&СписокИменФайлов)";

	Запрос.УстановитьПараметр("ОбъектСсылка",     ОбъектСсылка);
	Запрос.УстановитьПараметр("СписокИменФайлов", СписокИменФайлов);

	Результат = Запрос.Выполнить();

	Выборка = Результат.Выбрать();

	Пока Выборка.Следующий() Цикл
		ИндексЭлемента = ЗагружаемыеФайлы.Найти(Выборка.ИмяФайла);
		Если ИндексЭлемента <> Неопределено Тогда
			ЗагружаемыеФайлы.Удалить(ИндексЭлемента);
		КонецЕсли;
	КонецЦикла;
	
КонецФункции

&НаСервереБезКонтекста
Функция СохранитьФайлыБанковскойГарантии(ОбъектСсылка, XMLФайл, Подпись)
	
	ЗагружаемыеФайлы = Новый Массив;
	ЗагружаемыеФайлы.Добавить(XMLФайл.Имя);
	ЗагружаемыеФайлы.Добавить(Подпись.Имя);
	
	ОставитьУникальныеЗагружаемыеФайлы(ЗагружаемыеФайлы, ОбъектСсылка);
	
	Если ЗагружаемыеФайлы.Количество() = 0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	Попытка
		
		НаборЗаписей = РегистрыСведений.ФайлыДокументовРеализацииПолномочийНалоговыхОрганов.СоздатьНаборЗаписей(); 
		НаборЗаписей.Отбор.Документ.Установить(ОбъектСсылка); 
		НаборЗаписей.Прочитать();
		
		// XML-Файл
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Документ = ОбъектСсылка;
		НоваяЗапись.ИмяФайла = XMLФайл.Имя;
		НоваяЗапись.ТипСодержимого = Перечисления.ТипыСодержимогоФайлов.xml;
		НоваяЗапись.Размер = XMLФайл.Размер;
		НоваяЗапись.Данные = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(XMLФайл.Адрес), Новый СжатиеДанных(9));
		НаборЗаписей.Записать();
		
		// Подпись
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Документ = ОбъектСсылка;
		НоваяЗапись.ИмяФайла = Подпись.Имя;
		НоваяЗапись.ТипСодержимого = Перечисления.ТипыСодержимогоФайлов.sgn;
		НоваяЗапись.Размер = Подпись.Размер;
		НоваяЗапись.Данные = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(Подпись.Адрес), Новый СжатиеДанных(9));
		НаборЗаписей.Записать();
		
	Исключение
		
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ИмяСобытия  = НСтр("ru = 'Электронный документооборот с контролирующими органами. Банковская гарантия';
							|en = 'Электронный документооборот с контролирующими органами. Банковская гарантия'", ОбщегоНазначения.КодОсновногоЯзыка());
			
		ЗаписьЖурналаРегистрации(
			ИмяСобытия,
			УровеньЖурналаРегистрации.Ошибка,
			,
			,
			ТекстОшибки);
		
		Возврат Ложь;
		
	КонецПопытки;
	
	Возврат Истина;	
	
КонецФункции

&НаКлиенте
Функция ПроверкаЗаполнения()
	
	ЕстьОшибки = Ложь;
	
	Если НЕ ЗначениеЗаполнено(Объект.Основание) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не заполнено поле ""Основание""';
														|en = 'Не заполнено поле ""Основание""'"), , "Объект.Основание");
		ЕстьОшибки = Истина;
	КонецЕсли;
	
	Если ЕстьОшибки Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли; 
	
КонецФункции

&НаСервере
Процедура УстановитьПредставлениеОснования()
	
	Если ЗначениеЗаполнено(Объект.Основание) Тогда
		КонтекстЭДОСервер      = ДокументооборотСКО.ПолучитьОбработкуЭДО();
		СведенияПоОбъекту      = КонтекстЭДОСервер.СведенияПоОтправляемымОбъектам(Объект.Основание);
		ПредставлениеОснования = СведенияПоОбъекту.Наименование;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОснованиеПриИзмененииНаСервере()
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	
	ОпределитьЭтоПакетСДопДокументами(ЭтотОбъект);
	
	Если ЗначениеЗаполнено(Объект.Основание) Тогда
		
		Если ЭтоПакетСДопДокументами Тогда
			СведенияПоОбъекту     = КонтекстЭДОСервер.СведенияПоОтправляемымОбъектам(Объект.Основание);
			Объект.Организация    = СведенияПоОбъекту.Организация;
			Объект.НалоговыйОрган = Справочники.НалоговыеОрганы.НайтиПоКоду(СведенияПоОбъекту.КодКонтролирующегоОргана);
		Иначе
			Объект.ОписьВходящихДокументов = ДокументооборотСКОВызовСервера.ПолучитьОписьВходящихДокументовПоТребованию(Объект.Основание);
			Объект.Организация    = Объект.Основание.Организация;
			Объект.НалоговыйОрган = Объект.Основание.НалоговыйОрган;
		КонецЕсли;
		
	Иначе
		Объект.ОписьВходящихДокументов = Справочники.ОписиВходящихДокументовИзНалоговыхОрганов.ПустаяСсылка();
		Объект.Организация = Справочники.Организации.ПустаяСсылка();
		Объект.НалоговыйОрган = Справочники.НалоговыеОрганы.ПустаяСсылка();
	КонецЕсли;
	
	ИзменитьОформлениеФормы();
	
	УстановитьМодифицированность(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьОформлениеФормы()
	
	Элементы.БанковскиеГарантии.Видимость = ЭтоПакетСДопДокументами;
	Элементы.ГруппаКнопок3.Видимость = НЕ ЭтоПакетСДопДокументами;
	Элементы.ПрисоединенныеФайлы.Видимость = ЭтоПакетСДопДокументами;
	
	Если ЭтоПакетСДопДокументами Тогда
		Элементы.ПредставляемыеДокументы.Отображение = ОтображениеТаблицы.Список;
	Иначе
		Элементы.ПредставляемыеДокументы.Отображение = ОтображениеТаблицы.Дерево;
	КонецЕсли;
	
	УстановитьЗаголовок();
	УстановитьПредставлениеОснования();
	Элементы.ПредставляемыеДокументы.Обновить();
	УстановитьВидимостьИнформационнойПанели();
	УстановитьВидимостьПодсказкиПроЖелтыйФон();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОпределитьЭтоПакетСДопДокументами(Форма)
	
	Форма.ЭтоПакетСДопДокументами = ДокументооборотСКОВызовСервера.ЭтоПакетСДопДокументами(Форма.Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьОснованиеПослеВыбора(ВыбранноеОснование, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ВыбранноеОснование <> Неопределено Тогда
		
		Если НЕ КонтекстЭДОКлиент.ОснованиеПакетаВыбраноКорректно(ВыбранноеОснование) Тогда
			Возврат;
		КонецЕсли;
		
		Объект.Основание = ВыбранноеОснование;
		ОснованиеПриИзмененииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	КонтекстЭДОКлиент = Результат.КонтекстЭДО;
	Если КонтекстЭДОКлиент = Неопределено Тогда
		Закрыть();
	КонецЕсли;
	
	Если ЕстьБитыеСсылки Тогда
		ПоказатьПредупреждение(, "Список представляемых документов поврежден.");
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПрорисоватьСтатус(ЭтоИзменяемоеСообщение = Истина)
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	Если КонтекстЭДОСервер = Неопределено Тогда
		ЭтоИзменяемоеСообщение = Ложь;
	КонецЕсли;
	
	ПараметрыПрорисовкиПанелиОтправки = ДокументооборотСКОВызовСервера.ПараметрыПрорисовкиПанелиОтправки(Объект.Ссылка, Объект.Организация, "ФНС");
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.ПрименитьПараметрыПрорисовкиПанелиОтправки(ЭтаФорма, ПараметрыПрорисовкиПанелиОтправки);
	
КонецПроцедуры

&НаСервере
Процедура УправлениеСтатусомИДоступностью()
	             
	ЭтоИзменяемоеСообщение = Истина;
	ПроверкаБитыхСсылокПередОткрытием(ЭтоИзменяемоеСообщение);
	ПрорисоватьСтатус(ЭтоИзменяемоеСообщение);
	
	// устанавливаем картинку заголовка
	УправлениеЭУОтправка(ЭтоИзменяемоеСообщение);
	
	// регулируем ТолькоПросмотр
	УстановитьТолькоПросмотр(НЕ ЭтоИзменяемоеСообщение);
	
КонецПроцедуры

&НаСервере
Процедура ПроверкаБитыхСсылокПередОткрытием(ЭтоИзменяемоеСообщение)
	
	Для каждого ГруппаДокументов Из ДеревоДокументов.ПолучитьЭлементы() Цикл
		
		Для каждого ЭлементГруппы Из ГруппаДокументов.ПолучитьЭлементы() Цикл
			
			Если (ЭлементГруппы.СсылкаНаОбъект = Неопределено ИЛИ ТипЗнч(ЭлементГруппы.СсылкаНаОбъект) = Тип("Строка")) И ЭлементГруппы.Загружен = Ложь Тогда
				//это битая ссылка
				ЭтоИзменяемоеСообщение = Ложь;
				ЕстьБитыеСсылки = Истина;
				Возврат;
			КонецЕсли; 
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеЭУОтправка(ЭтоИзменяемоеСообщение = Неопределено)
	
	ТекстСообщения = "";
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО(ТекстСообщения);
	Если КонтекстЭДОСервер = Неопределено Тогда 
		Элементы.КнопкаОтправить.Доступность = Ложь;
		Возврат;
	КонецЕсли;
	
	// регулируем видимость кнопки Отправить
	СтатусОтправки = КонтекстЭДОСервер.ПолучитьСтатусОтправкиОбъекта(Объект.Ссылка);
	
	Если ЗначениеЗаполнено(СтатусОтправки) И СтатусОтправки <> Перечисления.СтатусыОтправки.ВКонверте Тогда
		ЭтоИзменяемоеСообщение = Ложь;
	КонецЕсли;
	
	Если НЕ ЭтоИзменяемоеСообщение Тогда
		Элементы.КнопкаОтправить.Видимость = Ложь;
		Элементы.ФормаЗаписать.ТолькоВоВсехДействиях = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьПодсказкиПроЖелтыйФон()
	
	Есть = Справочники.ШаблоныПоясненийДляФНС.ЕстьЖелтыйФон(Пояснение);
	Элементы.НадписьПроОранжевое.Видимость = Есть;

КонецПроцедуры

&НаСервере
Процедура УстановитьТолькоПросмотр(ТолькоПросмотр = Истина)
	
	Если ТолькоПросмотр Тогда
		
		Элементы.ГруппаКоманднаяПанельПредставляемыеДокументы.Видимость = НЕ ТолькоПросмотр;
		
	КонецЕсли;
	
	Элементы.ПредставляемыеДокументы.ТолькоПросмотр = ТолькоПросмотр;
	Элементы.ПредставлениеОснования.ТолькоПросмотр = ТолькоПросмотр;
	Элементы.НалоговыйОрган.ТолькоПросмотр = ТолькоПросмотр;
	
	Элементы.Пояснение.ТолькоПросмотр = ТолькоПросмотр;
	
	Элементы.КнопкиПоясненияСФоном.Видимость = НЕ ТолькоПросмотр;
	Элементы.НадписьПроОранжевое.Видимость = НЕ ТолькоПросмотр;
	
	ИзменитьОформлениеЕслиТолькоПросмотр();
	
	ОставитьОднуИзСтраниц(ТолькоПросмотр);
	
КонецПроцедуры

&НаСервере
Процедура ОставитьОднуИзСтраниц(ТолькоПросмотр = Истина)
	
	Если ТолькоПросмотр Тогда
		
		ПояснениеЗаполнено = ЗаполненТекстФорматированногоДокумента(Пояснение);
		
		Элементы.Документы.Видимость = Объект.ПредставляемыеДокументы.Количество() > 0;
		Элементы.ТекстовоеПояснение.Видимость = ПояснениеЗаполнено;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаполненТекстФорматированногоДокумента(Пояснение)

	ПояснениеЗаполнено = Справочники.ШаблоныПоясненийДляФНС.ЗаполненТекстФорматированногоДокумента(Пояснение);
	Возврат ПояснениеЗаполнено;

КонецФункции

&НаСервере
Функция ПроверкаПередОтправкой(ТекстПредупреждения)
	
	РезультатПроверки = Истина;
	
	МаксКолвоФайлов = ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.МаксимальнойКоличествоФайловОписи();
	
	ПредельноеКоличествоФайловВСтроке = МаксКолвоФайлов;
	
	Для каждого ГруппаДокументов Из ДеревоДокументов.ПолучитьЭлементы() Цикл
		
		Для каждого ЭлементГруппы Из ГруппаДокументов.ПолучитьЭлементы() Цикл
			
			Если ЭлементГруппы.КоличествоФайлов = 0 Тогда
				
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'У документа %1 отсутствуют прикрепленные файлы.';
						|en = 'У документа %1 отсутствуют прикрепленные файлы.'"), ЭлементГруппы.СоставноеПоле);
					
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
				РезультатПроверки = Ложь;
				
			ИначеЕсли ЭлементГруппы.КоличествоФайлов > ПредельноеКоличествоФайловВСтроке Тогда 
				
				ПревышеноНа = ПредельноеКоличествоФайловВСтроке - ЭлементГруппы.КоличествоФайлов;
				
				ТекстСообщения = "Допустимое количество файлов в документе ""%1"" превышено на %2."; 
				ТекстСообщения = СтрШаблон(ТекстСообщения, ЭлементГруппы.СоставноеПоле, ПревышеноНа);
				
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
				РезультатПроверки = Ложь;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Если РезультатПроверки = Ложь Тогда
		ТекстПредупреждения = "Отправка невозможна: требуется устранить нарушения."
	КонецЕсли;
	
	Возврат РезультатПроверки;
	
КонецФункции

&НаСервере
Функция ПроверкаПередРазбивкой(ТекстПредупреждения)
	
	РезультатПроверки = Истина;
	
	Для каждого ГруппаДокументов Из ДеревоДокументов.ПолучитьЭлементы() Цикл
		
		Для каждого ЭлементГруппы Из ГруппаДокументов.ПолучитьЭлементы() Цикл
			
			Если ЭлементГруппы.КоличествоФайлов = 0 Тогда
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'У документа %1 отсутствуют прикрепленные файлы.';
						|en = 'У документа %1 отсутствуют прикрепленные файлы.'"), ЭлементГруппы.СоставноеПоле);
				РезультатПроверки = Ложь;
			КонецЕсли; 
			
		КонецЦикла;
		
	КонецЦикла;
	
	Если РезультатПроверки = Ложь Тогда
		ТекстПредупреждения = "Разбивка отменена: требуется устранить нарушения."
	КонецЕсли;
	
	Возврат РезультатПроверки;
	
КонецФункции

&НаКлиенте
Процедура КомандаОтправитьВопросОписьБудетОтправленаЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеОтправкиЗавершение", ЭтотОбъект);
	
	КонтекстЭДОКлиент.ОтправкаОписиИсходящихДокументовВФНС(
		Объект.Ссылка,
		Объект.Организация,
		УникальныйИдентификатор,
		ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтправкиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	// обновляем после переноса текстового пояснения в дерево документов
	ОбновитьФайлыДокументовИБНаСервере();
	УправлениеСтатусомИДоступностью();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтправитьВопросРазбитьЗавершение(Ответ, ДополнительныеПараметры) Экспорт

	Если Ответ <> Истина Тогда
		Возврат;
	КонецЕсли;	
	
	ТекстПредупреждения = "";
	МассивСозданныхОписей = РазбитьНаНесколькоНаСервере(ТекстПредупреждения);
	Если МассивСозданныхОписей = Неопределено Тогда
		Если ЗначениеЗаполнено(ТекстПредупреждения) Тогда
			ПоказатьПредупреждение(, ТекстПредупреждения);
		КонецЕсли;
		Возврат;
	Иначе
		
		Прочитать();
		
		//обновим дерево документов текущей описи   
		ДеревоДокументов.ПолучитьЭлементы().Очистить();
		ЗаполнитьДеревоДокументов(Объект.Ссылка);
		ОбновитьРазмерыОписи(Объект.Ссылка);
		
		//откроем форму групповой отправки
		СписокОписей = Новый СписокЗначений;
		СписокОписей.ЗагрузитьЗначения(МассивСозданныхОписей);
		СписокОписей.Добавить(Объект.Ссылка);
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("СписокОписей", 			СписокОписей);
		ПараметрыФормы.Вставить("Основание",    			Объект.Основание);
		ПараметрыФормы.Вставить("ЭтоПакетСДопДокументами",  ЭтоПакетСДопДокументами);
		
		ОткрытьФорму("Справочник.ОписиИсходящихДокументовВНалоговыеОрганы.Форма.ФормаГрупповойОтправки", ПараметрыФормы, , Новый УникальныйИдентификатор);
		
	КонецЕсли;
	
	УправлениеСтатусомИДоступностью();

КонецПроцедуры

&НаСервере
Процедура ОпределитьВложенияИРазмерВложений(Параметры)

	Если Параметры.Свойство("ЗначениеКопирования") И ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		
		КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
		Вложения = КонтекстЭДОСервер.ПолучитьФайлыДокументовРеализацииПолномочийНалоговыхОрганов(Параметры.ЗначениеКопирования);
		
		Если Вложения.Количество() > 0 Тогда
			
			Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
				Записать();
			КонецЕсли;
			
			Вложения.Колонки.Добавить("АдресФайла");
			Для каждого Вложение Из Вложения Цикл
				Вложение.АдресФайла = ПоместитьВоВременноеХранилище(Вложение.Данные.Получить(), УникальныйИдентификатор);
			КонецЦикла;
			
			КонтекстЭДОСервер.ПоместитьФайлыДокументовРеализацииПолномочийНалоговыхОрганов(Объект.Ссылка, Вложения);
			
			ОбновитьРазмерыОписи(Объект.Ссылка);
			
		Иначе
			ОбновитьРазмерыОписи(Параметры.ЗначениеКопирования);
		КонецЕсли;
		
	Иначе
		ОбновитьРазмерыОписи(Объект.Ссылка);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьДокументыИзФайлаОбмена()
	
	Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не заполнено поле ""Требование""';
														|en = 'Не заполнено поле ""Требование""'"), , "Объект.Основание");
		Возврат;
	КонецЕсли;
	
	ИННКППОрганизации = ПолучитьСтрокойИННКППОрганизации(Объект.Организация);
	
	ПараметрыФормы = Новый Структура("ИННКППОрганизации, ИдентификаторФормыВладельца", ИННКППОрганизации, УникальныйИдентификатор);
	
	//Описание оповещения
	ОписаниеОповещения = Новый ОписаниеОповещения("ДобавитьДокументыИзФайлаОбменаЗавершение", ЭтотОбъект);
	
	//Открываем форму выбора сканированных документов, результат закрытия передаем в ДобавитьСканированныеДокументыЗавершение
	ОткрытьФорму("Справочник.ОписиИсходящихДокументовВНалоговыеОрганы.Форма.ФормаЗагрузкаДокументовИзФайлаОбмена", ПараметрыФормы, ЭтотОбъект, , , , ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#Область ЗагрузкаСкана

&НаКлиенте
Процедура ВыбратьИДобавитьВложения()
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ВыбратьИДобавитьВложения_ПослеВыбора", 
		ЭтотОбъект);
		
	ТребованияФНСКлиент.ВыбратьСканыВОтветНаТребование(КонтекстЭДОКлиент, УникальныйИдентификатор, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьИДобавитьВложения_ПослеВыбора(Результат, ВходящийКонтекст) Экспорт
	
	Если НЕ Результат.Выполнено Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьPDF = ОбработкаФайловPDFКлиентСервер.ЕстьPDFФайлы(Результат.ОписанияФайлов, "Имя");
	Если ЕстьPDF Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ВыбратьИДобавитьВложения_ПослеПредложенияЗагрузитьXML", 
			ЭтотОбъект, 
			Результат);
			
		ОткрытьФорму(
			"Справочник.ОписиИсходящихДокументовВНалоговыеОрганы.Форма.ЗагружайтеДокументы1СЭДОкакXMLфайлы",
			,,,,,
			ОписаниеОповещения);
		
	Иначе
		ВыбратьИДобавитьВложения_ПослеПредложенияЗагрузитьXML(НСтр("ru = 'Продолжить';
																	|en = 'Продолжить'"), Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьИДобавитьВложения_ПослеПредложенияЗагрузитьXML(Ответ, РезультатВыбораФайлов) Экспорт
	
	Если Ответ = НСтр("ru = 'Продолжить';
						|en = 'Продолжить'") Тогда
		СоздатьСканированныйДокумент(РезультатВыбораФайлов);
	ИначеЕсли Ответ = "ЗагрузитьИзДругойБазы" Тогда
		ДобавитьДокументыИзФайлаОбмена();
	ИначеЕсли Ответ = "ЗагрузитьИзЭтойБазы" Тогда
		ВыбратьЭлектронныйДокументИзБазы();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьСканированныйДокумент(РезультатВыбораФайлов)
	
	АдресРезультатаВыбораФайлов = ПоместитьВоВременноеХранилище(РезультатВыбораФайлов, Новый УникальныйИдентификатор);
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Организация", Объект.Организация);
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", 			СтруктураПараметров);
	ПараметрыФормы.Вставить("АдресРезультатаВыбораФайлов", 	АдресРезультатаВыбораФайлов);
	ПараметрыФормы.Вставить("ДобавитьВОтобранные", 			Истина);
	ПараметрыФормы.Вставить("ЭтоПакетСДопДокументами", 		ЭтоПакетСДопДокументами);
	ПараметрыФормы.Вставить("ПроверятьНедопустимыеСимволы",	Истина);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"СоздатьСканированныйДокументЗавершение", 
		ЭтотОбъект);
	
	ОткрытьФорму("Справочник.СканированныеДокументыДляПередачиВЭлектронномВиде.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект,,,,ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьСканированныйДокументЗавершение(РезультатВыбора, ВходящийКонтекст) Экспорт
	
	 Если РезультатВыбора <> Неопределено И РезультатВыбора.Количество() > 0 Тогда
		
		МассивВыбранныхДокументов = РезультатВыбора;
		ПодключитьОбработчикОжидания("Подключаемый_ДобавитьДокументыПередВыборомПунктаТребования", 0.2, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ВыбратьЭлектронныйДокументИзБазы

&НаКлиенте
Процедура ВыбратьЭлектронныйДокументИзБазы()
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ВыбратьЭлектронныйДокументИзБазыЗавершение", 
		ЭтотОбъект);
		
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("ОтборОрганизация", Объект.Организация);
	ДополнительныеПараметры.Вставить("ТолькоЭлектронныеДокументы", Истина);
		
	ОткрытьФорму("РегистрСведений.ДокументыПоТребованиюФНС.Форма.ФормаВыбора", ДополнительныеПараметры,,,,,ОписаниеОповещения);
	 
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьЭлектронныйДокументИзБазыЗавершение(РезультатВыбора, ВходящийКонтекст) Экспорт
	
	 Если РезультатВыбора <> Неопределено И РезультатВыбора.Количество() > 0 Тогда
		 
		МассивВыбранныхДокументов = РезультатВыбора;
			
		ПодключитьОбработчикОжидания("Подключаемый_ДобавитьДокументыПередВыборомПунктаТребования", 0.2, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура ПроверитьТаблицуДокументов(Всего, ВАрхиве)
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	ТЗДокументыФайлыРазмер = КонтекстЭДОСервер.ПолучитьТаблицуДокументовОписиИРазмерыФайлов(Объект.Ссылка);
	
	Всего = 0;
	ВАрхиве = 0;
	
	Для Каждого Вложение Из ТЗДокументыФайлыРазмер Цикл 
		ВАрхиве = ВАрхиве + ?(Вложение.ВАрхиве > 0, 1, 0);
		Всего = Всего + 1;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьПараметр(Имя)
	
	Если НЕ ПроверитьОснование() Тогда
		Возврат;
	КонецЕсли;
	
	Записать();
	
	Значение = ЗначениеЗаполненияШаблона(Имя);
	
	ТребованияФНСКлиент.ВставитьПараметр(
		ЭтотОбъект,
		"Пояснение",
		Значение);
		
	УстановитьВидимостьПодсказкиПроЖелтыйФон();
		
КонецПроцедуры
	
&НаКлиенте
Процедура ВставитьЗначение(Значение = "", Замена = "")
	
	Структура = ТребованияФНСКлиентСервер.ШаблонЗначенияШаблона(Значение, Замена, Новый Цвет);
	
	ТребованияФНСКлиент.ВставитьПараметр(
		ЭтотОбъект,
		"Пояснение",
		Структура);
		
	УстановитьВидимостьПодсказкиПроЖелтыйФон();
		
КонецПроцедуры
	
&НаСервере
Функция ЗначениеЗаполненияШаблона(Имя)
	
	Данные = ДанныеЗаполненияШаблона();
	Если Данные.Свойство(Имя) Тогда
		Возврат Данные[Имя];
	Иначе
		Возврат ТребованияФНСКлиентСервер.ШаблонЗначенияШаблона(, , Новый Цвет);
	КонецЕсли;
		
КонецФункции

&НаКлиенте
Процедура ВыбратьОтчет()
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ВыбратьОтчет_Завершение", 
		ЭтотОбъект);
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("ЭтоРежимВыбора", Истина);
	ДополнительныеПараметры.Вставить("Организация", Объект.Организация);
	ДополнительныеПараметры.Вставить("Раздел", ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Отчеты"));
	
	ОткрытьФорму(
		"ОбщаяФорма.РегламентированнаяОтчетность", 
		ДополнительныеПараметры,
		,
		Новый УникальныйИдентификатор,
		,
		,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
	КонецПроцедуры
	
&НаКлиенте
Процедура ВыбратьОтчет_Завершение(Отчет, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Отчет <> Неопределено Тогда
		Представленние = ПредставлениеОтчета(Отчет);
		ВставитьЗначение(Представленние, НСтр("ru = '<Отчет>';
												|en = '<Отчет>'"));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПредставлениеОтчета(Отчет)

	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	ЭтоОтчет = КонтекстЭДОСервер.СсылкаПрисутствуетВРегистреЖурналОтчетовСтатусы(Отчет);
	
	НаименованиеОтчета = ТребованияФНСВызовСервера.СвойствоОтчета(Отчет, "НаименованиеОтчета");
	
	Возврат НаименованиеОтчета;

КонецФункции

&НаКлиенте
Процедура ВставитьПериод_Завершение(ФинансовыйПериод, ДополнительныеПараметры) Экспорт
	
	ВставитьЗначение(ФинансовыйПериод);
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьКонтрагента_ПослеВыбора(Контрагент, ВходящийКонтекст) Экспорт
	
	Если Контрагент <> Неопределено Тогда
		
		Свойства = ДокументооборотСКОВызовСервера.ПолучитьСвойстваКонтрагента(Контрагент);
		ИНН = Свойства.ИНН;
		
		ПредставлениеКонтрагента = НСтр("ru = '%1 (ИНН %2)';
										|en = '%1 (ИНН %2)'");
		
		ПредставлениеКонтрагента = СтрШаблон(
			ПредставлениеКонтрагента, 
			Строка(Контрагент), 
			ИНН);
		
		ВставитьЗначение(ПредставлениеКонтрагента, "<ИНН контрагента>");
		
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Функция РазмерТекстовогоПоясненияПоРеквизитуФормы() Экспорт
			
	// сохраняем вложения в каталог выгрузки
	ДвДанные = Справочники.ШаблоныПоясненийДляФНС.ДвДанныеPDFФайлаФорматированногоДокумента(Пояснение);
	
	Если ДвДанные = Неопределено Тогда
		Возврат 0;
	КонецЕсли;
	
	Возврат ДвДанные.Размер();
	
КонецФункции

&НаКлиенте
Процедура ВставитьЧислоПрописью(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ВставитьЧислоПрописью_ПослеВводаЧисла", 
		ЭтотОбъект);
		
	Подсказка = НСтр("ru = 'Введите сумму, которую надо написать прописью';
					|en = 'Введите сумму, которую надо написать прописью'");
	
	Число = ВыделенноеЧисло();
	
	ПоказатьВводЧисла(ОписаниеОповещения, Число, Подсказка, 20, 2);
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьЧислоПрописью_ПослеВводаЧисла(Сумма, ВходящийКонтекст) Экспорт
	
	Если Сумма <> Неопределено Тогда
		
		ПараметрыПрописи = "рубль, рубля, рублей, м, копейка, копейки, копеек, ж, 2 знака";

		Представление = ЧислоПрописью(Сумма, ,ПараметрыПрописи);
		
		Результат = НСтр("ru = '%1 руб. (%2)';
						|en = '%1 руб. (%2)'");
		Результат = СтрШаблон(Результат, Сумма, НРег(Представление));
		
		ВставитьЗначение(Результат, "<Сумма прописью>");
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Функция ВыделенноеЧисло() Экспорт
	
	ЗакладкаНачала = Неопределено;
	ЗакладкаОкончания = Неопределено;
	
	Элементы.Пояснение.ПолучитьГраницыВыделения(ЗакладкаНачала, ЗакладкаОкончания);

	ПозицияНачала    = Пояснение.ПолучитьПозициюПоЗакладке(ЗакладкаНачала);
	ПозицияОкончания = Пояснение.ПолучитьПозициюПоЗакладке(ЗакладкаОкончания);
	
	Текст = Пояснение.ПолучитьТекст(ЗакладкаНачала, ЗакладкаОкончания);
	
	Число = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(Текст);
	
	Возврат Число;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьТекстШаблона(СсылкаНаШаблон, ДанныеИзФормыЗаполнения = Неопределено) Экспорт

	ДанныеЗаполнения = ДанныеЗаполненияШаблона(ДанныеИзФормыЗаполнения);

	Справочники.ШаблоныПоясненийДляФНС.УстановитьТекстВФорматированныйДокумент(
		СсылкаНаШаблон, 
		Пояснение, 
		"ШаблонХранилище");
	
	// Заменяем параметры по тексту на значения из базы.
	ЭлементыКОбнулению = Новый Массив;
	
	ЗакладкаКонцаДокумента = Пояснение.ПолучитьЗакладкуКонца();
	
	ОбластьНачалоПараметра = Пояснение.НайтиТекст("{");
	Пока ОбластьНачалоПараметра <> Неопределено Цикл
	
		ОбластьКонецПараметра = Пояснение.НайтиТекст("}", ОбластьНачалоПараметра.ЗакладкаНачала);
		Если ОбластьКонецПараметра = Неопределено Тогда
			// Для параметра не задана закрывающая скобка и он продолжается до конца текста, поэтому выходим из цикла.
			Прервать;
		КонецЕсли;
		
		МассивЭлементов = Пояснение.СформироватьЭлементы(
			ОбластьНачалоПараметра.ЗакладкаНачала, ОбластьКонецПараметра.ЗакладкаКонца);
			
		ЭлементыКОбнулению.Очистить();
		ПоследнийТекстовыйЭлемент = Неопределено;
			
		Сч = 0;
		ДлинаДобавленногоТекста = 0;
		Пока Сч < МассивЭлементов.Количество() Цикл
			
			ЭлементДокумента = МассивЭлементов[Сч];
		
			Если ТипЗнч(ЭлементДокумента) <> Тип("ТекстФорматированногоДокумента") Тогда
				Сч = Сч + 1;
				Продолжить;
			КонецЕсли;

			ИмяПараметра = ИзвлечьИмяПараметраИзЭлементов(МассивЭлементов, ЭлементыКОбнулению, Сч, ЭлементДокумента);
			
			ЗаменитьПараметрНаТекст(
				ЭлементДокумента, 
				ИмяПараметра, 
				ДанныеЗаполнения, 
				ДлинаДобавленногоТекста,
				ЭлементыКОбнулению,
				ПоследнийТекстовыйЭлемент);
			
			Сч = Сч + 1;
			
		КонецЦикла;

		УбратьЛишниеВыделения(
			ПоследнийТекстовыйЭлемент, 
			Пояснение, 
			ЗакладкаКонцаДокумента, 
			ДлинаДобавленногоТекста);
		
		// Ищем следующий параметр
		ОбластьНачалоПараметра = Пояснение.НайтиТекст("{", ОбластьНачалоПараметра.ЗакладкаНачала);
		
	КонецЦикла;
	
	ВыделитьПараметрыЖирным(Пояснение);

КонецПроцедуры

&НаСервере
Процедура ВыделитьПараметрыЖирным(Пояснение)
	
	ОбластьНачалоПараметра = Пояснение.НайтиТекст("<");
	Пока ОбластьНачалоПараметра <> Неопределено Цикл
	
		ОбластьКонецПараметра = Пояснение.НайтиТекст(">", ОбластьНачалоПараметра.ЗакладкаНачала);
		Если ОбластьКонецПараметра = Неопределено Тогда
			// Для параметра не задана закрывающая скобка и он продолжается до конца текста, поэтому выходим из цикла.
			Прервать;
		КонецЕсли;
		
		МассивЭлементов = Пояснение.СформироватьЭлементы(
			ОбластьНачалоПараметра.ЗакладкаНачала, ОбластьКонецПараметра.ЗакладкаКонца);
			
		Сч = 0;
		Пока Сч < МассивЭлементов.Количество() Цикл
			
			Элемент = МассивЭлементов[Сч];
			Элемент.Шрифт = Новый Шрифт(Элемент.Шрифт,,, Истина);
			
			Сч = Сч + 1;

		КонецЦикла;
		
		ОбластьНачалоПараметра = Пояснение.НайтиТекст("<", ОбластьКонецПараметра.ЗакладкаКонца);
		
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Функция ПроверитьОснование()
	
	Если ЗначениеЗаполнено(Объект.Основание) Тогда
		Возврат Истина;
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Выберите документ-основание';
														|en = 'Выберите документ-основание'"), ,"ПредставлениеОснования");
		Возврат Ложь;
	КонецЕсли;

КонецФункции

&НаСервере
Функция ДанныеЗаполненияШаблона(ДанныеИзФормыЗаполнения = Неопределено) Экспорт
	
	ДанныеОрганизации = Новый Структура();
	ДанныеОрганизации.Вставить("Организация", Объект.Организация);
	ДанныеОрганизации.Вставить("ПриОткрытии", Истина);
	ДанныеОрганизации.Вставить("АдресЮридический", "");
	ДанныеОрганизации.Вставить("АдресФактический", "");
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	КонтекстЭДОСервер.ЗаполнитьДанныеОрганизации(ДанныеОрганизации);
	
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ДанныеОрганизации, ДанныеОрганизации.СтруктураДанныхОрганизации, Истина);
	
	Если ДанныеИзФормыЗаполнения = Неопределено Тогда
		Данные = Новый Структура;
	Иначе
		Данные = ОбщегоНазначенияКлиентСервер.СкопироватьРекурсивно(ДанныеИзФормыЗаполнения);
	КонецЕсли;
	
	ДобавитьЗначениеПараметра(Данные, "ИНН", ДанныеОрганизации.ИННЮЛ, "<ИНН>");
	
	ДобавитьЗначениеПараметра(Данные,"КПП", ДанныеОрганизации.КППЮЛ, "<КПП>");
	
	ДобавитьЗначениеПараметра(Данные, "ОГРН", ДанныеОрганизации.ОГРН, "<ОГРН>");
	ДобавитьЗначениеПараметра(Данные, "Адрес", ДанныеОрганизации.АдресЮридическийПредставление, "<Адрес организации>");
	ДобавитьЗначениеПараметра(Данные, "Телефон", ДанныеОрганизации.ТелОрганизации, "<Телефон организации>");
	
	ДобавитьЗначениеПараметра(Данные, "НаименованиеОрганизации", ДанныеОрганизации.КраткоеНаименование, "<Наименование организации>");
	
	// ФИО и должность руководителя
	Руководитель = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервераПереопределяемый.Руководитель(Объект.Организация);
	Если ЗначениеЗаполнено(Руководитель) Тогда
		
		ФИО = ДокументооборотСКОКлиентСервер.ФамилияИнициалыПоСсылке(Руководитель);
		ДобавитьЗначениеПараметра(Данные, "Руководитель", ФИО, "<Руководитель>");
		
		// Должность руководителя
		ДанныеСотрудника = ОбработкаЗаявленийАбонента.ПолучитьДанныеСотрудника(
			Перечисления.ТипыВладельцевЭЦП.Руководитель, 
			ДанныеОрганизации, Руководитель);
			
		ДобавитьЗначениеПараметра(Данные, "ДолжностьРуководителя", ДанныеСотрудника.Должность, "<Должность руководителя>");
		
	КонецЕсли;
	
	// Исполнитель
	ФизЛицо = Мультирежим.ФизЛицоПоПользователюИзСправочникаПользователи();
	Если ЗначениеЗаполнено(ФизЛицо) Тогда
		Исполнитель = ДокументооборотСКОКлиентСервер.ФамилияИнициалыПоСсылке(ФизЛицо);
		ДобавитьЗначениеПараметра(Данные, "Исполнитель", Исполнитель, "<Исполнитель>"); 
	Иначе
		Исполнитель = Мультирежим.ИмяТекущегоПользователя();
		ДобавитьЗначениеПараметра(Данные, "Исполнитель", Исполнитель, "<Исполнитель>");
	КонецЕсли;
	
	ДобавитьЗначениеПараметра(Данные, "ИФНС", Объект.НалоговыйОрган, "<ИФНС>");
	ДобавитьЗначениеПараметра(Данные, "Требование", Объект.Основание, "<Требование>");
	
	ДатаСегодня = Формат(ТекущаяДатаСеанса(), "ДЛФ=D");
	ДобавитьЗначениеПараметра(Данные, "ДатаСегодня", ДатаСегодня);
	
	Если ТипЗнч(Объект.Основание) = Тип("СправочникСсылка.ДокументыРеализацииПолномочийНалоговыхОрганов") Тогда
		Отчет = Объект.Основание.ДокументОснование;
	Иначе
		Отчет = "";
	КонецЕсли;
	
	ДобавитьЗначениеПараметра(Данные, "Отчет", Отчет, "<Отчет>");
	
	Период = ТребованияФНСВызовСервера.СвойствоОтчета(Отчет, "ФинансовыйПериод");
		
	ДобавитьЗначениеПараметра(Данные, "Период", Период, "<Период отчета>");
	
	УчетнаяЗапись = КонтекстЭДОСервер.УчетнаяЗаписьОрганизации(Объект.Организация);
	ДобавитьЗначениеПараметра(Данные, "НаименованиеОператора", УчетнаяЗапись.СпецоператорСвязи, "<Наименование оператора>");
	
	Город = ОбработкаЗаявленийАбонента.ПолеСертификата_2_5_4_7(ДанныеОрганизации.АдресЮридическийЗначение);
	ДобавитьЗначениеПараметра(Данные, "Город", Город, "<Город>");
	
	Возврат Данные;
	
КонецФункции

&НаСервере
Процедура ДобавитьЗначениеПараметра(Данные, Ключ, Значение, Знач Замена = "")
	
	ТребованияФНСКлиентСервер.ДобавитьЗначениеПараметраШаблона(
		Данные, 
		Ключ, 
		Значение, 
		Замена);
	
КонецПроцедуры

&НаСервере
Процедура УбратьЛишниеВыделения(
		ПоследнийТекстовыйЭлемент, 
		ЭлементФормы, 
		ЗакладкаКонцаДокумента, 
		ДлинаДобавленногоТекста) Экспорт
		
	// При расположении подряд нескольких параметров между ними могут появляться 
	// пробелы или знаки препинания, закрашенные цветом Оранжевый(). 
	// Чтобы после замены всех соседних параметров эти пробелы не выделялсь, снимем у них фон.

	Если ПоследнийТекстовыйЭлемент <> Неопределено Тогда
		
		ЕстьДанные = ПоследнийТекстовыйЭлемент.ЗакладкаКонца <> ЗакладкаКонцаДокумента;
		
		Пока ЕстьДанные Цикл
			ПозицияПоследнегоТекстовогоЭлемента = ЭлементФормы.ПолучитьПозициюПоЗакладке(
				ПоследнийТекстовыйЭлемент.ЗакладкаКонца);
			
			ПозицияКонцаДокумента = ЭлементФормы.ПолучитьПозициюПоЗакладке(ЗакладкаКонцаДокумента);
			
			ПозицияПоследнегоТекстовогоЭлемента = Мин(ПозицияПоследнегоТекстовогоЭлемента + ДлинаДобавленногоТекста + 1, ПозицияКонцаДокумента);
			ЗакладкаПоПозиции = ЭлементФормы.ПолучитьЗакладкуПоПозиции(ПозицияПоследнегоТекстовогоЭлемента);
			
			МассивЭлементов = ЭлементФормы.ПолучитьЭлементы(ПоследнийТекстовыйЭлемент.ЗакладкаКонца, ЗакладкаПоПозиции);
			ПоследнийТекстовыйЭлемент = Неопределено;
			Для Каждого ЭлементДокумента Из МассивЭлементов Цикл
				Если ТипЗнч(ЭлементДокумента) <> Тип("ТекстФорматированногоДокумента") Тогда
					ПоследнийТекстовыйЭлемент = Неопределено;
					Прервать;
				КонецЕсли;
				
				Если СтрНайти(ЭлементДокумента.Текст, "{") > 0 Тогда
					// Начало нового параметра, дальше не обрабатываем
					ПоследнийТекстовыйЭлемент = Неопределено;
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			
			Если ПоследнийТекстовыйЭлемент = Неопределено Тогда
				ЕстьДанные = Ложь;
			ИначеЕсли ЭлементФормы.ПолучитьПозициюПоЗакладке(ПоследнийТекстовыйЭлемент.ЗакладкаКонца) <= ПозицияПоследнегоТекстовогоЭлемента Тогда
				ЕстьДанные = Ложь;
			Иначе
				ЕстьДанные = ПоследнийТекстовыйЭлемент.ЗакладкаКонца <> ЗакладкаКонцаДокумента;
			КонецЕсли;
			
		КонецЦикла;

	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ЗаменитьПараметрНаТекст(
			ЭлементДокумента, 
			ИмяПараметра, 
			ДанныеЗаполнения, 
			ДлинаДобавленногоТекста,
			ЭлементыКОбнулению,
			ПоследнийТекстовыйЭлемент)
			
	Значение = "<" + ИмяПараметра + ">";
			
	Если ЗначениеЗаполнено(ИмяПараметра) Тогда
				
		Если ДанныеЗаполнения.Свойство(ИмяПараметра) Тогда
			
			ЭлементДокумента.Шрифт = Новый Шрифт(ЭлементДокумента.Шрифт,,, Истина);
			Если ЗначениеЗаполнено(ДанныеЗаполнения[ИмяПараметра].Значение) Тогда
			
				Значение = ДанныеЗаполнения[ИмяПараметра].Значение;
				СброситьВыделениеЭлемента(ЭлементДокумента, ЭлементыКОбнулению, ПоследнийТекстовыйЭлемент);
			
			Иначе
				Значение = ДанныеЗаполнения[ИмяПараметра].Замена;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ЭлементДокумента.Текст = Значение;
	ДлинаДобавленногоТекста = ДлинаДобавленногоТекста + СтрДлина(Значение);
	
КонецФункции

&НаСервере
Функция ИзвлечьИмяПараметраИзЭлементов(
		МассивЭлементов, 
		ЭлементыКОбнулению, 
		Сч, 
		ЭлементДокумента, 
		СкобкаНачала = "{" , 
		СкобкаОкончания = "}",
		Обнулять = Истина)
	
	ИмяПараметра = ЭлементДокумента.Текст;
	
	// Возможно, что из-за форматирования текст параметра разбился на несколько html-элементов
	// и надо собрать их в один
	Пока Сч < МассивЭлементов.Количество() - 1
		И Прав(ИмяПараметра, 1) <> СкобкаОкончания Цикл
		
		СледующийЭлементДокумента = МассивЭлементов[Сч + 1];
		Если ТипЗнч(СледующийЭлементДокумента) <> Тип("ТекстФорматированногоДокумента") Тогда
			Прервать;
		КонецЕсли;
		
		ИмяПараметра = ИмяПараметра + СледующийЭлементДокумента.Текст;
		Сч = Сч + 1;
		
		ЭлементыКОбнулению.Добавить(СледующийЭлементДокумента);
		
	КонецЦикла;
	
	Если Лев(ИмяПараметра, 1) = СкобкаНачала Тогда
		ИмяПараметра = Сред(ИмяПараметра, 2);
	КонецЕсли;
	Если Прав(ИмяПараметра, 1) = СкобкаОкончания Тогда
		ИмяПараметра = Сред(ИмяПараметра, 1, СтрДлина(ИмяПараметра) - 1);
	КонецЕсли;
	
	Если Обнулять Тогда
		Для Каждого Элемент Из ЭлементыКОбнулению Цикл
			Элемент.Текст = "";
		КонецЦикла;
	КонецЕсли;
	
	Возврат ИмяПараметра;
	
КонецФункции

&НаСервере
Функция НезаполненныеПараметры(Пояснение)
	
	Результат = Новый Массив;
	ЭлементыКОбнулению = Новый Массив;
	
	ОбластьНачалоПараметра = Пояснение.НайтиТекст("<");
	Пока ОбластьНачалоПараметра <> Неопределено Цикл
	
		ОбластьКонецПараметра = Пояснение.НайтиТекст(">", ОбластьНачалоПараметра.ЗакладкаНачала);
		Если ОбластьКонецПараметра = Неопределено Тогда
			// Для параметра не задана закрывающая скобка и он продолжается до конца текста, поэтому выходим из цикла.
			Прервать;
		КонецЕсли;
		
		МассивЭлементов = Пояснение.СформироватьЭлементы(
			ОбластьНачалоПараметра.ЗакладкаНачала, ОбластьКонецПараметра.ЗакладкаКонца);
			
		Сч = 0;
		Пока Сч < МассивЭлементов.Количество() Цикл
			
			Элемент = МассивЭлементов[Сч];
			
			ИмяПараметра = ИзвлечьИмяПараметраИзЭлементов(
				МассивЭлементов, 
				ЭлементыКОбнулению, 
				Сч, 
				Элемент, 
				"<" , 
				">",
				Ложь);
				
			Если Результат.Найти(ИмяПараметра) = Неопределено Тогда
				Результат.Добавить(ИмяПараметра);
			КонецЕсли;
			
			Сч = Сч + 1;

		КонецЦикла;
		
		ОбластьНачалоПараметра = Пояснение.НайтиТекст("<", ОбластьКонецПараметра.ЗакладкаКонца);
		
	КонецЦикла;
	
	Возврат Результат;

КонецФункции

&НаСервере
Процедура СброситьВыделениеЭлемента(ЭлементДокумента, ЭлементыКОбнулению, ПоследнийТекстовыйЭлемент)
	
	Если ЭлементДокумента.ЦветФона = ТребованияФНСКлиентСервер.Оранжевый() Тогда
		ПоследнийТекстовыйЭлемент = ЭлементДокумента;
	КонецЕсли;
	
	Для Каждого Элемент Из ЭлементыКОбнулению Цикл
		Если Элемент.ЦветФона = ТребованияФНСКлиентСервер.Оранжевый() Тогда
			Элемент.ЦветФона = Справочники.ШаблоныПоясненийДляФНС.Белый();
			ПоследнийТекстовыйЭлемент = Элемент;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#Область ПанельОтправкиВКонтролирующиеОрганы

&НаКлиенте
Процедура ОбновитьОтправку(Команда)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОбновитьОтправкуИзПанелиОтправки(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаПротоколНажатие(Элемент)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьПротоколИзПанелиОтправки(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьНеотправленноеИзвещение(Команда)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОтправитьНеотправленноеИзвещениеИзПанелиОтправки(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыОтправкиНажатие(Элемент)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьСостояниеОтправкиИзПанелиОтправки(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура КритическиеОшибкиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьКритическиеОшибкиИзПанелиОтправки(ЭтаФорма);
КонецПроцедуры

#КонецОбласти

#КонецОбласти