#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	Если ЭтоГруппа Тогда
		НепроверяемыеРеквизиты.Добавить("Код");
	Иначе
		
		Если Не ЗначениеЗаполнено(Наименование) Тогда
			Наименование = Код;
		КонецЕсли;
		
		Если Лев(Код, 4) <> УкрупненныйКодРасходов Тогда
		
			ТекстСообщения = НСтр("ru = 'Укрупненный код расхода не соответствует детализированному коду (первые 4 символа).';
									|en = 'The consolidated code does not match the detailed code (the first 4 characters).'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "УкрупненныйКодРасходов", , Отказ);
			
		КонецЕсли;
	
	КонецЕсли;
	
	ДенежныеСредстваСерверЛокализация.ПроверитьУникальностьЗаписиСтатьиЦелевыхСредств(ЭтотОбъект, Отказ);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ДенежныеСредстваСерверЛокализация.ЗаполнитьСокращенноеОписаниеСтатьиЦелевыхСредств(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли