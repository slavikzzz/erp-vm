#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если НЕ РабочиеЦентрыИсключитьЗаполненыКорректно(Отказ) Тогда
		МассивНепроверяемыхРеквизитов.Добавить("КритерииОптимизации");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция РабочиеЦентрыИсключитьЗаполненыКорректно(Отказ)
	
	Результат = Истина;	
	
	ТекстЗапроса = ТекстЗапросаРабочиеЦентрыИсключитьЗаполненыКорректно();
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("РабочиеЦентрыИсключить", РабочиеЦентрыИсключить.Выгрузить());
	
	ВыборкаИтоги = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаИтоги.Следующий() Цикл
		Если ВыборкаИтоги.Исключить Тогда
			
			Результат = Ложь;
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Для вида рабочего центра ""%1"" все рабочие центры указаны в качестве исключаемых.';
					|en = 'All work centers are specified as excluded from the ""%1"" work center type.'"),
				ВыборкаИтоги.ВидРабочегоЦентраПредставление);
				
			СтрокаСОшибкой = РабочиеЦентрыИсключить.Найти(ВыборкаИтоги.ВидРабочегоЦентра, "ВидРабочегоЦентра");
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
				"РабочиеЦентрыИсключить",
				СтрокаСОшибкой.НомерСтроки,
				"ВидРабочегоЦентра");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
				
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ТекстЗапросаРабочиеЦентрыИсключитьЗаполненыКорректно()
	
	Результат = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РабочиеЦентрыИсключить.ВидРабочегоЦентра,
	|	РабочиеЦентрыИсключить.РабочийЦентр
	|ПОМЕСТИТЬ РабочиеЦентрыИсключить
	|ИЗ
	|	&РабочиеЦентрыИсключить КАК РабочиеЦентрыИсключить
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РабочиеЦентры.ВидРабочегоЦентра КАК ВидРабочегоЦентра,
	|	РабочиеЦентры.ВидРабочегоЦентра.Представление КАК ВидРабочегоЦентраПредставление,
	|	РабочиеЦентры.Ссылка КАК РабочийЦентр,
	|	ВЫБОР
	|		КОГДА РабочиеЦентрыИсключить.РабочийЦентр ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Исключить
	|ИЗ
	|	Справочник.РабочиеЦентры КАК РабочиеЦентры
	|		ЛЕВОЕ СОЕДИНЕНИЕ РабочиеЦентрыИсключить КАК РабочиеЦентрыИсключить
	|		ПО РабочиеЦентры.Ссылка = РабочиеЦентрыИсключить.РабочийЦентр
	|			И РабочиеЦентры.ВидРабочегоЦентра = РабочиеЦентрыИсключить.ВидРабочегоЦентра
	|ГДЕ
	|	РабочиеЦентры.ВидРабочегоЦентра В
	|			(ВЫБРАТЬ
	|				РабочиеЦентрыИсключить.ВидРабочегоЦентра
	|			ИЗ
	|				РабочиеЦентрыИсключить)
	|ИТОГИ
	|	МИНИМУМ(Исключить)
	|ПО
	|	ВидРабочегоЦентра";
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли