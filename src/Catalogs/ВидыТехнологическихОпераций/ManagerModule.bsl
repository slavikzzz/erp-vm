#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Создает временную таблицу с описанием реквизитов и сведений вида операций.
// Имя таблицы - ВТСвойстваНабора.
//
// Параметры:
//  Ссылка					 - СправочникСсылка.ВидыТехнологическихОпераций - вид операции.
//  МенеджерВременныхТаблиц	 - МенеджерВременныхТаблиц - менеджер в который будет помещена созданная временная таблица.
//  ПрочитатьРеквизиты		 - Булево - флаг необходимости получения реквизитов.
//  ПрочитатьСведения		 - Булево - флаг необходимости получения сведений.
//
Процедура СоздатьВТСвойстваНабора(Ссылка, МенеджерВременныхТаблиц, ПрочитатьРеквизиты, ПрочитатьСведения) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	СвойстваНаборов.НомерСтроки     КАК НомерСтроки,
		|	СвойстваНаборов.Свойство        КАК Свойство,
		|	СвойстваНаборов.ПометкаУдаления КАК ПометкаУдаления,
		|	ЕСТЬNULL(Свойства.Заголовок, ПРЕДСТАВЛЕНИЕ(СвойстваНаборов.Свойство)) КАК Заголовок,
		|	Свойства.ТипЗначения            КАК ТипЗначения,
		|	Свойства.ФорматСвойства         КАК ФорматСвойства,
		|	ЛОЖЬ                            КАК ЭтоДополнительноеСведение
		|ПОМЕСТИТЬ ВТСвойстваНабора
		|ИЗ
		|	Справочник.ВидыТехнологическихОпераций КАК ВидыТехнологическихОпераций
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты КАК СвойстваНаборов
		|		ПО ВидыТехнологическихОпераций.НаборСвойств = СвойстваНаборов.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК Свойства
		|		ПО (СвойстваНаборов.Свойство = Свойства.Ссылка)
		|ГДЕ
		|	ВидыТехнологическихОпераций.Ссылка = &Ссылка
		|	И &ПрочитатьРеквизиты
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СвойстваНаборов.НомерСтроки,
		|	СвойстваНаборов.Свойство,
		|	СвойстваНаборов.ПометкаУдаления,
		|	ЕСТЬNULL(Свойства.Заголовок, ПРЕДСТАВЛЕНИЕ(СвойстваНаборов.Свойство)),
		|	Свойства.ТипЗначения,
		|	Свойства.ФорматСвойства,
		|	ИСТИНА
		|ИЗ
		|	Справочник.ВидыТехнологическихОпераций КАК ВидыТехнологическихОпераций
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеСведения КАК СвойстваНаборов
		|		ПО ВидыТехнологическихОпераций.НаборСвойств = СвойстваНаборов.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК Свойства
		|		ПО (СвойстваНаборов.Свойство = Свойства.Ссылка)
		|ГДЕ
		|	ВидыТехнологическихОпераций.Ссылка = &Ссылка
		|	И &ПрочитатьСведения");
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ПрочитатьРеквизиты", ПрочитатьРеквизиты);
	Запрос.УстановитьПараметр("ПрочитатьСведения", ПрочитатьСведения);
	
	Запрос.Выполнить();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

