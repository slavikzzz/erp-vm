#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Формирует отчета в заданный табличный документ
//
// Параметры:
// 		ТабличныйДокумент - ТабличныйДокумент - Табличный документ для формирования отчета.
Процедура СформироватьОтчет(ТабличныйДокумент) Экспорт
	
	Макет = ЭтотОбъект.ПолучитьМакет("МакетОтчета");
	
	ПараметрыПроверки = Справочники.ОбъектыЭксплуатации.ПараметрыПроверкиЗаполнения();
	ПараметрыПроверки.СообщатьОшибки = Ложь;
	
	ЕстьОшибки = Ложь;
	
	Справочники.ОбъектыЭксплуатации.ПроверитьЗаполнениеОбъектаЭксплуатации(ОбъектЭксплуатации, ПараметрыПроверки, ЕстьОшибки);
	
	Ошибки = ПараметрыПроверки.ПотокОшибок;
	ОшибкиУзлов = ПараметрыПроверки.ПараметрыПроверкиУзлов.ПотокОшибок;
	
	ВывестиЗаголовокОтчета(ТабличныйДокумент, Макет);
	
	Если ЕстьОшибки Тогда
		
		ТабличныйДокумент.НачатьАвтогруппировкуСтрок();
		
		ВывестиЗаголовокТаблицы(ТабличныйДокумент, Макет);
		
		Область = Макет.ПолучитьОбласть("Строка");
		Область.Параметры.ОбъектОшибки = ОбъектЭксплуатации;
		ТабличныйДокумент.Вывести(Область, 0, Строка(ОбъектЭксплуатации), Истина);
		
		ВывестиОшибки(ТабличныйДокумент, ОбъектЭксплуатации, 0, Ошибки, Макет);
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	Узлы.Ссылка КАК ОбъектОшибки
			|ИЗ
			|	Справочник.УзлыОбъектовЭксплуатации КАК Узлы
			|ГДЕ
			|	Узлы.Родитель = &Родитель
			|	И Узлы.Владелец = &Владелец
			|	И Узлы.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыОбъектовЭксплуатации.ВЭксплуатации), ЗНАЧЕНИЕ(Перечисление.СтатусыОбъектовЭксплуатации.ПустаяСсылка))");
		Запрос.УстановитьПараметр("Родитель", Справочники.УзлыОбъектовЭксплуатации.ПустаяСсылка());
		Запрос.УстановитьПараметр("Владелец", ОбъектЭксплуатации);
		
		Результат = Запрос.Выполнить();
		Если Не Результат.Пустой() Тогда
			
			Выборка = Результат.Выбрать();
			Пока Выборка.Следующий() Цикл
				ВывестиОбъектОшибки(ТабличныйДокумент, Выборка.ОбъектОшибки, 1, ОшибкиУзлов, Макет);
			КонецЦикла;
			
		КонецЕсли;
		
		ТабличныйДокумент.ЗакончитьАвтогруппировкуСтрок();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ВывестиЗаголовокОтчета(ТабличныйДокумент, Макет)
	
	// Вывод заголовка отчета
	ШаблонЗаголовкаОтчета = НСтр("ru = 'Контроль корректности заполнения объекта эксплуатации ""%Наименование%""';
								|en = 'Control the accuracy of population of the ""%Наименование%"" asset'");
	ОбластьМакета = Макет.ПолучитьОбласть("ЗаголовокОтчета");
	ОбластьМакета.Параметры.Заголовок = СтрЗаменить(ШаблонЗаголовкаОтчета, "%Наименование%", ОбъектЭксплуатации);
	ТабличныйДокумент.Вывести(ОбластьМакета);
	
КонецПроцедуры

Процедура ВывестиЗаголовокТаблицы(ТабличныйДокумент, Макет)
	
	ОбластьМакета = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ТабличныйДокумент.Вывести(ОбластьМакета);
	
КонецПроцедуры

Процедура ВывестиОбъектОшибки(ТабличныйДокумент, ОбъектОшибки, Уровень, ПотокОшибок, Макет)
	
	Область = Макет.ПолучитьОбласть("Строка");
	Область.Параметры.ОбъектОшибки = ОбъектОшибки;
	ТабличныйДокумент.Вывести(Область, Уровень, Строка(ОбъектОшибки), Истина);
	
	ВывестиОшибки(ТабличныйДокумент, ОбъектОшибки, Уровень, ПотокОшибок, Макет);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Узлы.Ссылка КАК ОбъектОшибки
		|ИЗ
		|	Справочник.УзлыОбъектовЭксплуатации КАК Узлы
		|ГДЕ
		|	Узлы.Родитель = &Родитель
		|	И Узлы.Владелец = &Владелец
		|	И Узлы.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыОбъектовЭксплуатации.ВЭксплуатации))");
	Запрос.УстановитьПараметр("Родитель", ОбъектОшибки);
	Запрос.УстановитьПараметр("Владелец", ОбъектЭксплуатации);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		ВывестиОбъектОшибки(ТабличныйДокумент, Выборка.ОбъектОшибки, Уровень+1, ПотокОшибок, Макет);
	КонецЦикла;
	
КонецПроцедуры

Процедура ВывестиОшибки(ТабличныйДокумент, ОбъектОшибки, Уровень, ПотокОшибок, Макет)
	
	СчетчикОшибок = ПотокОшибок.СчетчикОшибок; // Соответствие
	Если СчетчикОшибок.Получить(ОбъектОшибки) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбластьОшибки = Макет.ПолучитьОбласть("Ошибка");
	Для Каждого Ошибка Из ПотокОшибок.СписокОшибок Цикл
		
		Если Ошибка.ОбъектОшибки = ОбъектОшибки Тогда
		
			ОбластьОшибки.Параметры.Заполнить(Ошибка);
			ТабличныйДокумент.Вывести(ОбластьОшибки, Уровень+1, Строка(ОбъектОшибки), Истина);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
