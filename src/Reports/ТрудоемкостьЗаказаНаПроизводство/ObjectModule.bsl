#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - См. ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	Настройки.События.ПриСозданииНаСервере = Истина;
КонецПроцедуры

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//   ЭтотОбъект - ФормаКлиентскогоПриложения - Форма отчета.
//   Отказ - Булево - Передается из параметров обработчика "как есть".
//   СтандартнаяОбработка - Передается из параметров обработчика "как есть".
//
// См. также:
//   "ФормаКлиентскогоПриложения.ПриСозданииНаСервере" в синтакс-помощнике.
//
Процедура ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка) Экспорт
	
	Параметры = ЭтотОбъект.Параметры;
	
	Если Параметры.Свойство("ПараметрКоманды")
	   И Параметры.Свойство("КлючВарианта") Тогда
		
		КлючВарианта = Параметры.КлючВарианта;
		
		Если ТипЗнч(Параметры.ПараметрКоманды) = Тип("ДокументСсылка.ЗаказНаПроизводство2_2") Тогда
		
			Если НЕ ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.ПараметрКоманды, "ДинамическаяСтруктура") Тогда
				
				ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Данный отчет применим только к заказам с включенной динамической структурой.';
															|en = 'This report is applicable only to orders with enabled dynamic structure.'"));
				Отказ = Истина;
				Возврат;
				
			КонецЕсли;
			
			ЭтотОбъект.ФормаПараметры.Отбор.Вставить("ЗаказНаПроизводство", Параметры.ПараметрКоманды);
			
		ИначеЕсли ТипЗнч(Параметры.ПараметрКоманды) = Тип("ДокументСсылка.ЭтапПроизводства2_2") Тогда
			
			Если НЕ ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.ПараметрКоманды, "ДинамическаяСтруктура") Тогда
				
				ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Данный отчет применим только к этапам с включенной динамической структурой.';
															|en = 'This report is applicable only to stages with enabled dynamic structure.'"));
				Отказ = Истина;
				Возврат;
				
			КонецЕсли;
			
			Если КлючВарианта = "ТрудоемкостьПоПартии" Тогда
				
				Партия = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.ПараметрКоманды, "ПартияПроизводства");
				ЭтотОбъект.ФормаПараметры.Отбор.Вставить("ПартияПроизводства", Партия);
				
			ИначеЕсли КлючВарианта = "ТрудоемкостьПоЭтапу" Тогда
				
				ЭтотОбъект.ФормаПараметры.Отбор.Вставить("Этап", Параметры.ПараметрКоманды);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#КонецЕсли

