#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Формирует табличную часть отчета
Процедура СформироватьОтчет(ПараметрыОтчета, АдресХранилища) Экспорт
	
	ПараметрыОтчета.СписокСформированныхЛистов.Очистить();
	
	СформироватьСтраницыИнвентарнойКнигиОС(ПараметрыОтчета);
	
	СформироватьОбложкуИнвентарнойКнигиОС(ПараметрыОтчета);
	
	ПоместитьВоВременноеХранилище(ПараметрыОтчета.СписокСформированныхЛистов, АдресХранилища);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура формирует табличный документ с обложкой инвентарной книги.
//
// Параметры:
//  ДокументРезультат - табличный документ, в который выводится
//                      обложка инвентарной книги.
//
// Возвращаемое значение:
//  Нет.
//
Процедура СформироватьОбложкуИнвентарнойКнигиОС(ПараметрыОтчета)
	
	ДокументРезультат = Новый ТабличныйДокумент;
	ДокументРезультат.АвтоМасштаб = Истина;
	
	НачалоПериода              = ПараметрыОтчета.Началопериода;
	КонецПериода               = ПараметрыОтчета.КонецПериода;
	Организация                = ПараметрыОтчета.Организация;
	Подразделение              = ПараметрыОтчета.Подразделение;
	Ответственный              = ПараметрыОтчета.Ответственный;
	СписокСформированныхЛистов = ПараметрыОтчета.СписокСформированныхЛистов;
	
	ДатаОтбора = ?(ЗначениеЗаполнено(КонецПериода), КонецПериода, ТекущаяДатаСеанса());
	
	УстановитьПривилегированныйРежим(Истина);
	Сведения = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Организация, ДатаОтбора);
	УстановитьПривилегированныйРежим(Ложь);
	ДанныеОтветственного = УправлениеВнеоборотнымиАктивамиПереопределяемый.ДанныеФизЛица(Организация, Ответственный, ДатаОтбора);
	
	ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ДокументРезультат.ПолеСверху         = 20;
	ДокументРезультат.ПолеСнизу          = 20;
	ДокументРезультат.ПолеСлева          = 10;
	ДокументРезультат.ПолеСправа         = 10;
	
	ДокументРезультат.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ИнвентарнаяКнигаОС_Обложка";
	
	Макет = ПолучитьМакет("Обложка");
	Макет.Параметры.НаименованиеОрганизации          = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(Сведения, "НаименованиеДляПечатныхФорм");
	Макет.Параметры.ОКПО                             = Сведения.КодПоОКПО;
	
	Если ЗначениеЗаполнено(НачалоПериода) Тогда
		СтрокаС = Формат(НачалоПериода, "ДЛФ=DD");
	Иначе
		СтрокаС = НСтр("ru = '""____"" ___________ ______ г.';
						|en = '""____"" ___________ ______ '");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КонецПериода) Тогда
		СтрокаПо = Формат(КонецПериода, "ДЛФ=DD");
	Иначе
		СтрокаПо = НСтр("ru = '""____"" ___________ ______ г.';
						|en = '""____"" ___________ ______ '");
	КонецЕсли;
	ШаблонСтрокаПериода = НСтр("ru = 'с %1 по %2';
								|en = 'from %1 to %2'");
	
	Макет.Параметры.Период                           = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСтрокаПериода, СтрокаС, СтрокаПо);
	Макет.Параметры.НаименованиеПодразделения        = ?(ЗначениеЗаполнено(Подразделение), Подразделение.Наименование, "");
	Макет.Параметры.КодПодразделения                 = ?(ЗначениеЗаполнено(Подразделение), Подразделение.Код, "");
	Макет.Параметры.Должность                        = ДанныеОтветственного.Должность;
	Макет.Параметры.ФамилияИО                        = ДанныеОтветственного.Фамилия + " " + Лев(ДанныеОтветственного.Имя, 1) + "." + Лев(ДанныеОтветственного.Отчество, 1) + ".";
	Макет.Параметры.ТабельныйНомер                   = ДанныеОтветственного.ТабельныйНомер;
	
	ДокументРезультат.Вывести(Макет);
	
	ДокументРезультат.ТолькоПросмотр = Истина;
	
	СписокСформированныхЛистов.Добавить(ДокументРезультат, "Обложка");
	
КонецПроцедуры

// Процедура формирует табличный документ со страницами инвентарной книги.
//
// Параметры:
//  ДокументРезультат - табличный документ, в который выводятся
//                      страницы инвентарной книги.
//
// Возвращаемое значение:
//  Нет.
//
Процедура СформироватьСтраницыИнвентарнойКнигиОС(ПараметрыОтчета)
	
	ДокументРезультат = Новый ТабличныйДокумент;
	ДокументРезультат.АвтоМасштаб = Истина;
	
	Организация                = ПараметрыОтчета.Организация;
	Подразделение              = ПараметрыОтчета.Подразделение;
	МОЛ                        = ПараметрыОтчета.МОЛ;
	СписокСформированныхЛистов = ПараметрыОтчета.СписокСформированныхЛистов;
	
	ОписаниеТекстаЗапроса = СформироватьОписаниеТекстаЗапроса(Подразделение, МОЛ, ПараметрыОтчета.Началопериода, ПараметрыОтчета.КонецПериода);
	
	Запрос = Новый Запрос(ОписаниеТекстаЗапроса.ТекстЗапроса);
	Запрос.УстановитьПараметр("НачалоПериода22", ОписаниеТекстаЗапроса.НачалоПериода22);
	Запрос.УстановитьПараметр("НачалоПериода24", ОписаниеТекстаЗапроса.НачалоПериода24);
	Запрос.УстановитьПараметр("КонецПериода22", ОписаниеТекстаЗапроса.КонецПериода22);
	Запрос.УстановитьПараметр("КонецПериода24", ОписаниеТекстаЗапроса.КонецПериода24);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("МОЛ", МОЛ);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	// Заполнение таблицы строк инвентарной книги.
	
	СтрокиИнвентарнойКниги = Новый ТаблицаЗначений();
	СтрокиИнвентарнойКниги.Колонки.Добавить("ДокументСсылка");
	СтрокиИнвентарнойКниги.Колонки.Добавить("ОсновноеСредствоСсылка");	
	СтрокиИнвентарнойКниги.Колонки.Добавить("ОсновноеСредство");
	СтрокиИнвентарнойКниги.Колонки.Добавить("ИнвентарныйНомер");
	СтрокиИнвентарнойКниги.Колонки.Добавить("СрокПолезногоИспользования");
	СтрокиИнвентарнойКниги.Колонки.Добавить("ПервоначальнаяСтоимость");
	СтрокиИнвентарнойКниги.Колонки.Добавить("ПоступилоПредставлениеДокумента");
	СтрокиИнвентарнойКниги.Колонки.Добавить("ПоступилоДатаПринятияКУчету");
	СтрокиИнвентарнойКниги.Колонки.Добавить("ПоступилоДокументПринятияКУчету");
	СтрокиИнвентарнойКниги.Колонки.Добавить("ПоступилоПодразделение");
	СтрокиИнвентарнойКниги.Колонки.Добавить("ПоступилоМОЛ");
	СтрокиИнвентарнойКниги.Колонки.Добавить("НачисленнаяАмортизацияИлиИзнос");
	СтрокиИнвентарнойКниги.Колонки.Добавить("ОстаточнаяСтоимость");
	СтрокиИнвентарнойКниги.Колонки.Добавить("ПеремещениеИлиВыбытиеПредставлениеДокумента");
	СтрокиИнвентарнойКниги.Колонки.Добавить("ПеремещениеИлиВыбытиеПодразделение");
	СтрокиИнвентарнойКниги.Колонки.Добавить("ПеремещениеИлиВыбытиеМОЛ");
	СтрокиИнвентарнойКниги.Колонки.Добавить("ПеремещениеИлиВыбытиеПричинаВыбытия");
	СтрокиИнвентарнойКниги.Колонки.Добавить("ПереоценкаДата");
	СтрокиИнвентарнойКниги.Колонки.Добавить("ПереоценкаКоэффициент");
	СтрокиИнвентарнойКниги.Колонки.Добавить("ПереоценкаВосстановительаяСтоимость");
	
	Пока Выборка.Следующий() Цикл
		
		СтрокаИнвентарнойКниги = СтрокиИнвентарнойКниги.Добавить();
		
		СтрокаИнвентарнойКниги.ОсновноеСредствоСсылка      = Выборка.ОсновноеСредствоСсылка;
		СтрокаИнвентарнойКниги.ИнвентарныйНомер            = Выборка.ИнвентарныйНомер;
		СтрокаИнвентарнойКниги.СрокПолезногоИспользования  = Выборка.СрокПолезногоИспользования;
		СтрокаИнвентарнойКниги.ДокументСсылка              = Выборка.Документ;
		
		СтрокаИнвентарнойКниги.ОсновноеСредство = 
			?(НЕ ЗначениеЗаполнено(Выборка.ОсновноеСредствоНаименованиеПолное), 
				Выборка.ОсновноеСредствоНаименование, 
				Выборка.ОсновноеСредствоНаименованиеПолное);
		
		Если Выборка.ТипДокумента = "ПринятиеКУчетуОС" Тогда
			
			// Четная страница.
			СтрокаИнвентарнойКниги.ПоступилоПредставлениеДокумента             = Выборка.ПредставлениеДокумента;
			СтрокаИнвентарнойКниги.ПоступилоДатаПринятияКУчету                 = Выборка.ДатаПринятияКУчету;
			СтрокаИнвентарнойКниги.ПоступилоДокументПринятияКУчету             = Выборка.ДокументПринятияКУчету;
			СтрокаИнвентарнойКниги.ПоступилоПодразделение                      = Выборка.ПоследнееМестонахождениеПредставление;
			СтрокаИнвентарнойКниги.ПоступилоМОЛ                                = ФизическиеЛицаКлиентСервер.ФамилияИнициалы(Выборка.ПоследнееМОЛПредставление);
			СтрокаИнвентарнойКниги.ПервоначальнаяСтоимость                     = Выборка.ПервоначальнаяСтоимость;
			СтрокаИнвентарнойКниги.НачисленнаяАмортизацияИлиИзнос              = 0;
			
			// Нечетная страница.
			СтрокаИнвентарнойКниги.ОстаточнаяСтоимость                         = 
				?(Выборка.ПорядокПогашенияСтоимости = Перечисления.ПорядокПогашенияСтоимостиОС.СписаниеПриПринятииКУчету, 
					0, 
					Выборка.ПервоначальнаяСтоимость);
					
			СтрокаИнвентарнойКниги.ПеремещениеИлиВыбытиеПредставлениеДокумента = "";
			СтрокаИнвентарнойКниги.ПеремещениеИлиВыбытиеПодразделение          = "";
			СтрокаИнвентарнойКниги.ПеремещениеИлиВыбытиеМОЛ                    = "";
			СтрокаИнвентарнойКниги.ПеремещениеИлиВыбытиеПричинаВыбытия         = "";
			
		ИначеЕсли Выборка.ТипДокумента = "РазукомплектацияОС" Тогда
			
			// Четная страница.
			СтрокаИнвентарнойКниги.ПоступилоПредставлениеДокумента             = Выборка.ПредставлениеДокумента;
			СтрокаИнвентарнойКниги.ПоступилоДатаПринятияКУчету                 = Выборка.ДатаПринятияКУчету;
			СтрокаИнвентарнойКниги.ПоступилоДокументПринятияКУчету             = Выборка.ДокументПринятияКУчету;
			СтрокаИнвентарнойКниги.ПоступилоПодразделение                      = Выборка.ПоследнееМестонахождениеПредставление;
			СтрокаИнвентарнойКниги.ПоступилоМОЛ                                = ФизическиеЛицаКлиентСервер.ФамилияИнициалы(Выборка.ПоследнееМОЛПредставление);
			СтрокаИнвентарнойКниги.ПервоначальнаяСтоимость                     = Выборка.ПервоначальнаяСтоимость;
			СтрокаИнвентарнойКниги.НачисленнаяАмортизацияИлиИзнос = 
				?(Выборка.ПорядокПогашенияСтоимости = Перечисления.ПорядокПогашенияСтоимостиОС.НачислениеИзноса, 
					Выборка.НачисленныйИзнос + Выборка.ИзносПоДокументу, 
					Выборка.НачисленнаяАмортизация + Выборка.АмортизацияПоДокументу);
			
			// Нечетная страница.
			СтрокаИнвентарнойКниги.ОстаточнаяСтоимость                         = Выборка.ВосстановительнаяСтоимость - СтрокаИнвентарнойКниги.НачисленнаяАмортизацияИлиИзнос;
			СтрокаИнвентарнойКниги.ПеремещениеИлиВыбытиеПредставлениеДокумента = "";
			СтрокаИнвентарнойКниги.ПеремещениеИлиВыбытиеПодразделение          = "";
			СтрокаИнвентарнойКниги.ПеремещениеИлиВыбытиеМОЛ                    = "";
			СтрокаИнвентарнойКниги.ПеремещениеИлиВыбытиеПричинаВыбытия         = "";
			
		ИначеЕсли Выборка.ТипДокумента = "ПереоценкаОС" Тогда
			
			// Четная страница.
			СтрокаИнвентарнойКниги.ПервоначальнаяСтоимость                     = Выборка.ПервоначальнаяСтоимость;
			СтрокаИнвентарнойКниги.НачисленнаяАмортизацияИлиИзнос = 
				?(Выборка.ПорядокПогашенияСтоимости = Перечисления.ПорядокПогашенияСтоимостиОС.НачислениеИзносаПоЕНАОФ, 
					Выборка.НачисленныйИзнос, 
					Выборка.НачисленнаяАмортизация);
			
			// Нечетная страница.
			СтрокаИнвентарнойКниги.ОстаточнаяСтоимость                         = Выборка.ВосстановительнаяСтоимость - СтрокаИнвентарнойКниги.НачисленнаяАмортизацияИлиИзнос;
			СтрокаИнвентарнойКниги.ПереоценкаДата                              = Формат(Выборка.ДатаДокумента, "ДЛФ=D");
			СтрокаИнвентарнойКниги.ПереоценкаВосстановительаяСтоимость         = Выборка.ВосстановительнаяСтоимость;
			
			ПереоценкаКоэффициент =
				?(Выборка.ВосстановительнаяСтоимость <> 0, 
					(Выборка.ВосстановительнаяСтоимость + Выборка.ИзменениеСтоимости) / Выборка.ВосстановительнаяСтоимость,
					0);
			СтрокаИнвентарнойКниги.ПереоценкаКоэффициент = Формат(ПереоценкаКоэффициент, "ЧДЦ=1; ЧН=");
			
		ИначеЕсли Выборка.ТипДокумента = "ПеремещениеОС" И Выборка.ПоследнееМестонахождение = Подразделение Тогда
			
			// Четная страница.
			СтрокаИнвентарнойКниги.ПоступилоПредставлениеДокумента             = Выборка.ПредставлениеДокумента;
			СтрокаИнвентарнойКниги.ПоступилоДатаПринятияКУчету                 = Выборка.ДатаПринятияКУчету;
			СтрокаИнвентарнойКниги.ПоступилоДокументПринятияКУчету             = "";
			СтрокаИнвентарнойКниги.ПоступилоПодразделение                      = Выборка.ПоследнееМестонахождениеПредставление;
			СтрокаИнвентарнойКниги.ПоступилоМОЛ                                = ФизическиеЛицаКлиентСервер.ФамилияИнициалы(Выборка.ПоследнееМОЛПредставление);
			СтрокаИнвентарнойКниги.ПервоначальнаяСтоимость                     = Выборка.ВосстановительнаяСтоимость;
			СтрокаИнвентарнойКниги.НачисленнаяАмортизацияИлиИзнос = 
				?(Выборка.ПорядокПогашенияСтоимости = Перечисления.ПорядокПогашенияСтоимостиОС.НачислениеИзносаПоЕНАОФ, 
					Выборка.НачисленныйИзнос + Выборка.ИзносПоДокументу, 
					Выборка.НачисленнаяАмортизация + Выборка.АмортизацияПоДокументу);
			
			// Нечетная страница.
			СтрокаИнвентарнойКниги.ОстаточнаяСтоимость                         = Выборка.ВосстановительнаяСтоимость - СтрокаИнвентарнойКниги.НачисленнаяАмортизацияИлиИзнос;
			СтрокаИнвентарнойКниги.ПеремещениеИлиВыбытиеПредставлениеДокумента = Выборка.ПредставлениеДокумента;			
			СтрокаИнвентарнойКниги.ПеремещениеИлиВыбытиеПодразделение          = Выборка.ПредпоследнееМестонахождениеПредставление;
			СтрокаИнвентарнойКниги.ПеремещениеИлиВыбытиеМОЛ                    = ФизическиеЛицаКлиентСервер.ФамилияИнициалы(Выборка.ПредпоследнееМОЛПредставление);
			СтрокаИнвентарнойКниги.ПеремещениеИлиВыбытиеПричинаВыбытия         = "";
			
		Иначе                                                         
			
			// Четная страница.
			СтрокаИнвентарнойКниги.ПоступилоПредставлениеДокумента             = "";
			СтрокаИнвентарнойКниги.ПоступилоДатаПринятияКУчету                 = "";
			СтрокаИнвентарнойКниги.ПоступилоДокументПринятияКУчету             = Неопределено;
			СтрокаИнвентарнойКниги.ПоступилоПодразделение                      = "";
			СтрокаИнвентарнойКниги.ПоступилоМОЛ                                = "";
			СтрокаИнвентарнойКниги.ПервоначальнаяСтоимость                     = Выборка.ВосстановительнаяСтоимость;
			СтрокаИнвентарнойКниги.НачисленнаяАмортизацияИлиИзнос = 
				?(Выборка.ПорядокПогашенияСтоимости = Перечисления.ПорядокПогашенияСтоимостиОС.НачислениеИзносаПоЕНАОФ, 
					Выборка.НачисленныйИзнос + Выборка.ИзносПоДокументу, 
					Выборка.НачисленнаяАмортизация + Выборка.АмортизацияПоДокументу);
			
			// Нечетная страница.
			СтрокаИнвентарнойКниги.ОстаточнаяСтоимость                         = Выборка.ВосстановительнаяСтоимость - СтрокаИнвентарнойКниги.НачисленнаяАмортизацияИлиИзнос;
			СтрокаИнвентарнойКниги.ПеремещениеИлиВыбытиеПредставлениеДокумента = Выборка.ПредставлениеДокумента;			
			СтрокаИнвентарнойКниги.ПеремещениеИлиВыбытиеПодразделение          = Выборка.ПоследнееМестонахождениеПредставление;
			СтрокаИнвентарнойКниги.ПеремещениеИлиВыбытиеМОЛ                    = ФизическиеЛицаКлиентСервер.ФамилияИнициалы(Выборка.ПоследнееМОЛПредставление);
			СтрокаИнвентарнойКниги.ПеремещениеИлиВыбытиеПричинаВыбытия         = Выборка.ПричинаСписания;
			
		КонецЕсли;
		
	КонецЦикла;
	
	// Вывод страниц инвентарной книги.
	ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ДокументРезультат.ПолеСверху         = 20;
	ДокументРезультат.ПолеСнизу          = 20;
	ДокументРезультат.ПолеСлева          = 10;
	ДокументРезультат.ПолеСправа         = 10;
	ДокументРезультат.ФиксацияСлева      = 3;
	
	ДокументРезультат.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ИнвентарнаяКнигаОС_Обложка";
	
	Макет = ПолучитьМакет("Страницы");
	
	НомерЧетнойСтраницы   = 2;
	НомерНечетнойСтраницы = НомерЧетнойСтраницы + 1;
	
	Шапка = Макет.ПолучитьОбласть("Шапка");
	Шапка.Параметры.НомерЧетнойСтраницы   = НомерЧетнойСтраницы;
	Шапка.Параметры.НомерНечетнойСтраницы = НомерНечетнойСтраницы;	
	ДокументРезультат.Вывести(Шапка);
	
	Подвал = Макет.ПолучитьОбласть("Подвал");
	
	Для каждого СтрокаИнвентарнойКниги Из СтрокиИнвентарнойКниги Цикл
		
		Строка = Макет.ПолучитьОбласть("Строка");
		
		Строка.Параметры.Заполнить(СтрокаИнвентарнойКниги);
		Строка.Параметры.НомерСтроки = СтрокиИнвентарнойКниги.Индекс(СтрокаИнвентарнойКниги) + 1;
		
		СтрокаСПодвалом = Новый Массив;
		СтрокаСПодвалом.Добавить(Строка);
		СтрокаСПодвалом.Добавить(Подвал);
		
		Если НЕ ОбщегоНазначенияБПВызовСервера.ПроверитьВыводТабличногоДокумента(ДокументРезультат, СтрокаСПодвалом) Тогда
			
			ДокументРезультат.Вывести(Подвал);
			ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
			
			НомерЧетнойСтраницы   = НомерЧетнойСтраницы + 2;
			НомерНечетнойСтраницы = НомерНечетнойСтраницы + 2;
			
			Шапка = Макет.ПолучитьОбласть("Шапка");
			Шапка.Параметры.НомерЧетнойСтраницы   = НомерЧетнойСтраницы;
			Шапка.Параметры.НомерНечетнойСтраницы = НомерНечетнойСтраницы;
			ДокументРезультат.Вывести(Шапка);
			
		КонецЕсли;
		
		ДокументРезультат.Вывести(Строка);
		
	КонецЦикла;
	
	ДокументРезультат.Вывести(Подвал);
	
	ДокументРезультат.ТолькоПросмотр = Истина;
	
	СписокСформированныхЛистов.Добавить(ДокументРезультат, "Страницы");
	
КонецПроцедуры

Функция СформироватьОписаниеТекстаЗапроса(Подразделение, МОЛ, НачалоПериода, КонецПериода)
	
	СписокЗапросов = Новый Массив;
	
	ДатаНачалаУчета = Константы.ДатаНачалаУчетаВнеоборотныхАктивов2_4.Получить();
	
	ИспользоватьВерсию22 = 
		ПолучитьФункциональнуюОпцию("ИспользоватьВнеоборотныеАктивы2_2")
		И (НЕ ЗначениеЗаполнено(НачалоПериода)
			ИЛИ НачалоПериода < ДатаНачалаУчета
			ИЛИ НЕ ЗначениеЗаполнено(ДатаНачалаУчета));
			
	ИспользоватьВерсию24 = 
		ПолучитьФункциональнуюОпцию("ИспользоватьВнеоборотныеАктивы2_4")
		И (НЕ ЗначениеЗаполнено(КонецПериода)
			ИЛИ КонецПериода >= ДатаНачалаУчета);
			
	НачалоПериода22 = НачалоПериода;
	НачалоПериода24 = ?(ИспользоватьВерсию22, ДатаНачалаУчета, НачалоПериода);
	КонецПериода22 = ?(ИспользоватьВерсию24, ДатаНачалаУчета-1, КонецПериода);
	КонецПериода24 = КонецПериода;
			
	#Область Версия22
	
	Если ИспользоватьВерсию22 Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	НачисленнаяАмортизацияОС.Субконто1 КАК ОсновноеСредство,
		|	НачисленнаяАмортизацияОС.Счет КАК СчетНачисленияАмортизации,
		|	НачисленнаяАмортизацияОС.Период КАК Период
		|ПОМЕСТИТЬ НачисленнаяАмортизацияОС
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(&НачалоПериода22, &КонецПериода22, Запись, , , ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства), Организация = &Организация) КАК НачисленнаяАмортизацияОС
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство,
		|	СчетНачисленияАмортизации,
		|	Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	НачисленнаяАмортизацияОС.Субконто1 КАК ОсновноеСредство,
		|	НачисленнаяАмортизацияОС.Счет КАК СчетНачисленияАмортизации,
		|	НачисленнаяАмортизацияОС.Период КАК Период,
		|	НачисленнаяАмортизацияОС.СуммаКонечныйОстатокКт КАК НачисленнаяАмортизация,
		|	НачисленнаяАмортизацияОС.СуммаКонечныйОстатокДт КАК НачисленныйИзнос
		|ПОМЕСТИТЬ НачисленнаяАмортизацияОС_Суммы
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(&НачалоПериода22, &КонецПериода22, Регистратор, , , ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства), Организация = &Организация) КАК НачисленнаяАмортизацияОС
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство,
		|	СчетНачисленияАмортизации,
		|	Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВосстановительнаяСтоимостьОСБУ.Субконто1 КАК ОсновноеСредство,
		|	ВосстановительнаяСтоимостьОСБУ.Счет КАК СчетУчета,
		|	ВосстановительнаяСтоимостьОСБУ.Период КАК Период,
		|	ВосстановительнаяСтоимостьОСБУ.Регистратор КАК Регистратор,
		|	ВосстановительнаяСтоимостьОСБУ.СуммаКонечныйОстатокДт КАК ВосстановительнаяСтоимость,
		|	ВосстановительнаяСтоимостьОСБУ.СуммаОборот КАК ИзменениеСтоимости
		|ПОМЕСТИТЬ ВосстановительнаяСтоимостьОСБУ
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(&НачалоПериода22, &КонецПериода22, Регистратор, , , ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства), Организация = &Организация) КАК ВосстановительнаяСтоимостьОСБУ
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство,
		|	СчетУчета,
		|	Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	АмортизацияИлиИзносПоДокументу.Субконто1 КАК ОсновноеСредство,
		|	АмортизацияИлиИзносПоДокументу.Период КАК Период,
		|	АмортизацияИлиИзносПоДокументу.Счет КАК Счет,
		|	АмортизацияИлиИзносПоДокументу.КорСчет КАК КорСчет,
		|	АмортизацияИлиИзносПоДокументу.СуммаОборотДт КАК СуммаОборотДт,
		|	АмортизацияИлиИзносПоДокументу.СуммаОборотКт КАК СуммаОборотКт
		|ПОМЕСТИТЬ АмортизацияИлиИзносПоДокументу
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода22, &КонецПериода22, Регистратор, , ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства), Организация = &Организация, , ) КАК АмортизацияИлиИзносПоДокументу
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДанныеДокумента.Ссылка КАК Ссылка,
		|	КОНЕЦПЕРИОДА(ДанныеДокумента.Дата, МЕСЯЦ) КАК ДатаДвиженийБухгалтерии,
		|	""ПринятиеКУчетуОС"" КАК ТипДокумента,
		|	"""" КАК ПричинаСписания,
		|	NULL КАК Событие
		|ПОМЕСТИТЬ ТаблицаДокументы
		|ИЗ
		|	Документ.ПринятиеКУчетуОС КАК ДанныеДокумента
		|ГДЕ
		|	ДанныеДокумента.Проведен
		|	И ДанныеДокумента.Организация = &Организация
		|	И &ОтборПоПериодуДокумента
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка,
		|	ДанныеДокумента.Дата,
		|	""ПеремещениеОС"",
		|	"""",
		|	NULL
		|ИЗ
		|	Документ.ПеремещениеОС КАК ДанныеДокумента
		|ГДЕ
		|	ДанныеДокумента.Проведен
		|	И ДанныеДокумента.Организация = &Организация
		|	И &ОтборПоПериодуДокумента
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка,
		|	ДанныеДокумента.Дата,
		|	""ПереоценкаОС"",
		|	"""",
		|	NULL
		|ИЗ
		|	Документ.ПереоценкаОС КАК ДанныеДокумента
		|ГДЕ
		|	ДанныеДокумента.Проведен
		|	И ДанныеДокумента.Организация = &Организация
		|	И &ОтборПоПериодуДокумента
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка,
		|	ДанныеДокумента.ДатаПередачи,
		|	""ПередачаОС"",
		|	"""",
		|	ДанныеДокумента.СобытиеПередачиОС
		|ИЗ
		|	Документ.ПодготовкаКПередачеОС КАК ДанныеДокумента
		|ГДЕ
		|	ДанныеДокумента.ПередачаВыполнена
		|	И ДанныеДокумента.Проведен
		|	И ДанныеДокумента.Организация = &Организация
		|	И &ОтборПоПериодуДокумента
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка,
		|	ДанныеДокумента.Дата,
		|	""СписаниеОС"",
		|	ДанныеДокумента.ПричинаСписания.Представление,
		|	NULL
		|ИЗ
		|	Документ.СписаниеОС КАК ДанныеДокумента
		|ГДЕ
		|	НЕ ДанныеДокумента.ЧастичнаяЛиквидация
		|	И ДанныеДокумента.Проведен
		|	И ДанныеДокумента.Организация = &Организация
		|	И &ОтборПоПериодуДокумента
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СобытияОСОрганизаций.ОсновноеСредство КАК ОсновноеСредство,
		|	ТаблицаДокументы.Ссылка КАК Документ,
		|	ТаблицаДокументы.ТипДокумента КАК ТипДокумента,
		|	ТаблицаДокументы.ПричинаСписания КАК ПричинаСписания,
		|	СобытияОСОрганизаций.Период КАК ДатаДокумента,
		|	ТаблицаДокументы.ДатаДвиженийБухгалтерии КАК ДатаДвиженийБухгалтерии
		|ПОМЕСТИТЬ ДвиженияОС
		|ИЗ
		|	РегистрСведений.СобытияОСОрганизаций КАК СобытияОСОрганизаций
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДокументы КАК ТаблицаДокументы
		|		ПО СобытияОСОрганизаций.Регистратор = ТаблицаДокументы.Ссылка
		|			И (СобытияОСОрганизаций.Событие = ЕСТЬNULL(ТаблицаДокументы.Событие, СобытияОСОрганизаций.Событие))
		|ГДЕ
		|	&ОтборПоПериодуРегистра
		|	И СобытияОСОрганизаций.Организация = &Организация
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияОС.ОсновноеСредство КАК ОсновноеСредство,
		|	ДвиженияОС.Документ КАК Документ,
		|	ДвиженияОС.ТипДокумента КАК ТипДОкумента,
		|	ДвиженияОС.ДатаДокумента КАК ДатаДокумента,
		|	ДвиженияОС.ДатаДвиженийБухгалтерии КАК ДатаДвиженийБухгалтерии,
		|	ДвиженияОС.ПричинаСписания КАК ПричинаСписания,
		|	МАКСИМУМ(ПервоначальныеСведенияОС.Период) КАК ДатаПоследнегоПервоначальныеСведенияОС,
		|	МАКСИМУМ(ПараметрыАмортизацииОСБУ.Период) КАК ДатаПоследнегоПараметрыАмортизацииОС,
		|	МАКСИМУМ(МестонахождениеОСПоследнее.Период) КАК ДатаПоследнегоМестонахождениеОС,
		|	МАКСИМУМ(МестонахождениеОСПредпоследнее.Период) КАК ДатаПредпоследнегоМестонахождениеОС,
		|	МАКСИМУМ(ПорядокУчетаОС.Период) КАК ДатаПоследнегоПорядокУчетаОС,
		|	МАКСИМУМ(ПараметрыЦелевогоФинансированияОС.Период) КАК ДатаПоследнегоПорядокУчетаОСЦФ
		|ПОМЕСТИТЬ ДатыПоследнихДвиженийПоРегистрамСведений
		|ИЗ
		|	ДвиженияОС КАК ДвиженияОС
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
		|		ПО ДвиженияОС.ДатаДвиженийБухгалтерии >= ПервоначальныеСведенияОС.Период
		|			И ДвиженияОС.ОсновноеСредство = ПервоначальныеСведенияОС.ОсновноеСредство
		|			И ПервоначальныеСведенияОС.ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОС КАК МестонахождениеОСПоследнее
		|		ПО ДвиженияОС.ДатаДокумента >= МестонахождениеОСПоследнее.Период
		|			И ДвиженияОС.ОсновноеСредство = МестонахождениеОСПоследнее.ОсновноеСредство
		|			И МестонахождениеОСПоследнее.Организация = &Организация
		|			И МестонахождениеОСПоследнее.ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСБУ КАК ПараметрыАмортизацииОСБУ
		|		ПО ДвиженияОС.ДатаДокумента >= ПараметрыАмортизацииОСБУ.Период
		|			И ДвиженияОС.ОсновноеСредство = ПараметрыАмортизацииОСБУ.ОсновноеСредство
		|			И ПараметрыАмортизацииОСБУ.Организация = &Организация
		|			И ПараметрыАмортизацииОСБУ.ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОС КАК МестонахождениеОСПредпоследнее
		|		ПО ДвиженияОС.ОсновноеСредство = МестонахождениеОСПредпоследнее.ОсновноеСредство
		|			И ДвиженияОС.ДатаДокумента > МестонахождениеОСПредпоследнее.Период
		|			И МестонахождениеОСПредпоследнее.Организация = &Организация
		|			И МестонахождениеОСПредпоследнее.ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОС КАК ПорядокУчетаОС
		|		ПО ДвиженияОС.ОсновноеСредство = ПорядокУчетаОС.ОсновноеСредство
		|			И ДвиженияОС.ДатаДвиженийБухгалтерии >= ПорядокУчетаОС.Период
		|			И ПорядокУчетаОС.Организация = &Организация
		|			И ПорядокУчетаОС.ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыЦелевогоФинансированияОС КАК ПараметрыЦелевогоФинансированияОС
		|		ПО ДвиженияОС.ОсновноеСредство = ПараметрыЦелевогоФинансированияОС.ОсновноеСредство
		|			И ДвиженияОС.ДатаДвиженийБухгалтерии >= ПараметрыЦелевогоФинансированияОС.Период
		|
		|СГРУППИРОВАТЬ ПО
		|	ДвиженияОС.ОсновноеСредство,
		|	ДвиженияОС.Документ,
		|	ДвиженияОС.ТипДокумента,
		|	ДвиженияОС.ДатаДокумента,
		|	ДвиженияОС.ДатаДвиженийБухгалтерии,
		|	ДвиженияОС.ПричинаСписания
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДатыПоследнихДвиженийПоРегистрамСведений.ОсновноеСредство КАК ОсновноеСредство,
		|	ДатыПоследнихДвиженийПоРегистрамСведений.Документ КАК Документ,
		|	ДатыПоследнихДвиженийПоРегистрамСведений.ТипДОкумента КАК ТипДокумента,
		|	ДатыПоследнихДвиженийПоРегистрамСведений.ДатаДокумента КАК ДатаДокумента,
		|	ДатыПоследнихДвиженийПоРегистрамСведений.ДатаДвиженийБухгалтерии КАК ДатаДвиженийБухгалтерии,
		|	ДатыПоследнихДвиженийПоРегистрамСведений.ПричинаСписания КАК ПричинаСписания,
		|	ПараметрыАмортизацииОС.СрокПолезногоИспользованияБУ КАК СрокПолезногоИспользования,
		|	ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьБУ КАК ПервоначальнаяСтоимость,
		|	ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюБУ КАК ДатаПринятияКУчету,
		|	ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюБУ КАК ДокументПринятияКУчету,
		|	ПервоначальныеСведенияОС.ПорядокУчетаБУ КАК ПорядокПогашенияСтоимости,
		|	ПорядокУчетаОС.СчетУчета КАК СчетУчета,
		|	ПорядокУчетаОС.СчетНачисленияАмортизации КАК СчетНачисленияАмортизации,
		|	ПараметрыЦелевогоФинансированияОС.СчетУчета КАК СчетУчетаЦФ,
		|	ПараметрыЦелевогоФинансированияОС.СчетАмортизации КАК СчетНачисленияАмортизацииЦФ,
		|	МестонахождениеОСПоследнее.МОЛ КАК ПоследнееМОЛ,
		|	МестонахождениеОСПоследнее.Местонахождение КАК ПоследнееМестонахождение,
		|	МестонахождениеОСПредпоследнее.МОЛ КАК ПредпоследнееМОЛ,
		|	МестонахождениеОСПредпоследнее.Местонахождение КАК ПредпоследнееМестонахождение
		|ПОМЕСТИТЬ СрезПоследнихПоРегистрамСведений
		|ИЗ
		|	ДатыПоследнихДвиженийПоРегистрамСведений КАК ДатыПоследнихДвиженийПоРегистрамСведений
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОС КАК ПорядокУчетаОС
		|		ПО ДатыПоследнихДвиженийПоРегистрамСведений.ОсновноеСредство = ПорядокУчетаОС.ОсновноеСредство
		|			И ДатыПоследнихДвиженийПоРегистрамСведений.ДатаПоследнегоПорядокУчетаОС = ПорядокУчетаОС.Период
		|			И ПорядокУчетаОС.Организация = &Организация
		|			И ПорядокУчетаОС.ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыЦелевогоФинансированияОС КАК ПараметрыЦелевогоФинансированияОС
		|		ПО ДатыПоследнихДвиженийПоРегистрамСведений.ОсновноеСредство = ПараметрыЦелевогоФинансированияОС.ОсновноеСредство
		|			И ДатыПоследнихДвиженийПоРегистрамСведений.ДатаПоследнегоПорядокУчетаОСЦФ = ПараметрыЦелевогоФинансированияОС.Период
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОС КАК МестонахождениеОСПоследнее
		|		ПО ДатыПоследнихДвиженийПоРегистрамСведений.ОсновноеСредство = МестонахождениеОСПоследнее.ОсновноеСредство
		|			И ДатыПоследнихДвиженийПоРегистрамСведений.ДатаПоследнегоМестонахождениеОС = МестонахождениеОСПоследнее.Период
		|			И МестонахождениеОСПоследнее.Организация = &Организация
		|			И МестонахождениеОСПоследнее.ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОС КАК МестонахождениеОСПредпоследнее
		|		ПО ДатыПоследнихДвиженийПоРегистрамСведений.ОсновноеСредство = МестонахождениеОСПредпоследнее.ОсновноеСредство
		|			И ДатыПоследнихДвиженийПоРегистрамСведений.ДатаПредпоследнегоМестонахождениеОС = МестонахождениеОСПредпоследнее.Период
		|			И МестонахождениеОСПредпоследнее.Организация = &Организация
		|			И МестонахождениеОСПредпоследнее.ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСБУ КАК ПараметрыАмортизацииОС
		|		ПО ДатыПоследнихДвиженийПоРегистрамСведений.ОсновноеСредство = ПараметрыАмортизацииОС.ОсновноеСредство
		|			И ДатыПоследнихДвиженийПоРегистрамСведений.ДатаПоследнегоПараметрыАмортизацииОС = ПараметрыАмортизацииОС.Период
		|			И ПараметрыАмортизацииОС.Организация = &Организация
		|			И ПараметрыАмортизацииОС.ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
		|		ПО ДатыПоследнихДвиженийПоРегистрамСведений.ОсновноеСредство = ПервоначальныеСведенияОС.ОсновноеСредство
		|			И ДатыПоследнихДвиженийПоРегистрамСведений.ДатаПоследнегоПервоначальныеСведенияОС = ПервоначальныеСведенияОС.Период
		|			И ПервоначальныеСведенияОС.ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СрезПоследнихПоРегистрамСведений.ОсновноеСредство КАК ОсновноеСредство,
		|	СрезПоследнихПоРегистрамСведений.Документ КАК Документ,
		|	СрезПоследнихПоРегистрамСведений.ТипДокумента КАК ТипДокумента,
		|	СрезПоследнихПоРегистрамСведений.ДатаДокумента КАК ДатаДокумента,
		|	СрезПоследнихПоРегистрамСведений.ДатаДвиженийБухгалтерии КАК ДатаДвиженийБухгалтерии,
		|	СрезПоследнихПоРегистрамСведений.ПричинаСписания КАК ПричинаСписания,
		|	СрезПоследнихПоРегистрамСведений.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
		|	СрезПоследнихПоРегистрамСведений.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость,
		|	СрезПоследнихПоРегистрамСведений.ПорядокПогашенияСтоимости КАК ПорядокПогашенияСтоимости,
		|	СрезПоследнихПоРегистрамСведений.ПоследнееМОЛ КАК ПоследнееМОЛ,
		|	СрезПоследнихПоРегистрамСведений.ПредпоследнееМОЛ КАК ПредпоследнееМОЛ,
		|	СрезПоследнихПоРегистрамСведений.ПоследнееМестонахождение КАК ПоследнееМестонахождение,
		|	СрезПоследнихПоРегистрамСведений.ПредпоследнееМестонахождение КАК ПредпоследнееМестонахождение,
		|	МАКСИМУМ(ВосстановительнаяСтоимостьОСБУ.Период) КАК ДатаПоследнегоДвиженияВосстановительнаяСтоимость,
		|	МАКСИМУМ(НачисленнаяАмортизацияОС.Период) КАК ДатаПоследнегоДвиженияНачисленнаяАмортизацияИлиИзнос,
		|	СрезПоследнихПоРегистрамСведений.СчетУчета КАК СчетУчета,
		|	СрезПоследнихПоРегистрамСведений.СчетУчетаЦФ КАК СчетУчетаЦФ,
		|	СрезПоследнихПоРегистрамСведений.СчетНачисленияАмортизации КАК СчетНачисленияАмортизации,
		|	СрезПоследнихПоРегистрамСведений.СчетНачисленияАмортизацииЦФ КАК СчетНачисленияАмортизацииЦФ,
		|	СрезПоследнихПоРегистрамСведений.ДатаПринятияКУчету КАК ДатаПринятияКУчету,
		|	СрезПоследнихПоРегистрамСведений.ДокументПринятияКУчету КАК ДокументПринятияКУчету
		|ПОМЕСТИТЬ СрезПоследнихПоРегистрамСведенийИБухгалтерии
		|ИЗ
		|	СрезПоследнихПоРегистрамСведений КАК СрезПоследнихПоРегистрамСведений
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВосстановительнаяСтоимостьОСБУ КАК ВосстановительнаяСтоимостьОСБУ
		|		ПО СрезПоследнихПоРегистрамСведений.ОсновноеСредство = ВосстановительнаяСтоимостьОСБУ.ОсновноеСредство
		|			И (СрезПоследнихПоРегистрамСведений.СчетУчета = ВосстановительнаяСтоимостьОСБУ.СчетУчета
		|				ИЛИ СрезПоследнихПоРегистрамСведений.СчетУчетаЦФ = ВосстановительнаяСтоимостьОСБУ.СчетУчета)
		|			И СрезПоследнихПоРегистрамСведений.ДатаДвиженийБухгалтерии > ВосстановительнаяСтоимостьОСБУ.Период
		|		ЛЕВОЕ СОЕДИНЕНИЕ НачисленнаяАмортизацияОС КАК НачисленнаяАмортизацияОС
		|		ПО СрезПоследнихПоРегистрамСведений.ОсновноеСредство = НачисленнаяАмортизацияОС.ОсновноеСредство
		|			И (СрезПоследнихПоРегистрамСведений.СчетНачисленияАмортизации = НачисленнаяАмортизацияОС.СчетНачисленияАмортизации
		|				ИЛИ СрезПоследнихПоРегистрамСведений.СчетНачисленияАмортизацииЦФ = НачисленнаяАмортизацияОС.СчетНачисленияАмортизации)
		|			И СрезПоследнихПоРегистрамСведений.ДатаДвиженийБухгалтерии > НачисленнаяАмортизацияОС.Период
		|
		|СГРУППИРОВАТЬ ПО
		|	СрезПоследнихПоРегистрамСведений.ОсновноеСредство,
		|	СрезПоследнихПоРегистрамСведений.Документ,
		|	СрезПоследнихПоРегистрамСведений.ТипДокумента,
		|	СрезПоследнихПоРегистрамСведений.ДатаДокумента,
		|	СрезПоследнихПоРегистрамСведений.ПричинаСписания,
		|	СрезПоследнихПоРегистрамСведений.СрокПолезногоИспользования,
		|	СрезПоследнихПоРегистрамСведений.ПервоначальнаяСтоимость,
		|	СрезПоследнихПоРегистрамСведений.ПорядокПогашенияСтоимости,
		|	СрезПоследнихПоРегистрамСведений.ПоследнееМОЛ,
		|	СрезПоследнихПоРегистрамСведений.ПредпоследнееМОЛ,
		|	СрезПоследнихПоРегистрамСведений.ПоследнееМестонахождение,
		|	СрезПоследнихПоРегистрамСведений.ПредпоследнееМестонахождение,
		|	СрезПоследнихПоРегистрамСведений.СчетУчета,
		|	СрезПоследнихПоРегистрамСведений.СчетУчетаЦФ,
		|	СрезПоследнихПоРегистрамСведений.СчетНачисленияАмортизации,
		|	СрезПоследнихПоРегистрамСведений.СчетНачисленияАмортизацииЦФ,
		|	СрезПоследнихПоРегистрамСведений.ДатаДвиженийБухгалтерии,
		|	СрезПоследнихПоРегистрамСведений.ДатаПринятияКУчету,
		|	СрезПоследнихПоРегистрамСведений.ДокументПринятияКУчету
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СрезПоследнихПоРегистрамСведенийИБухгалтерии.ОсновноеСредство КАК ОсновноеСредство,
		|	СрезПоследнихПоРегистрамСведенийИБухгалтерии.Документ КАК Документ,
		|	СрезПоследнихПоРегистрамСведенийИБухгалтерии.ТипДокумента КАК ТипДокумента,
		|	СрезПоследнихПоРегистрамСведенийИБухгалтерии.ДатаДокумента КАК ДатаДокумента,
		|	СрезПоследнихПоРегистрамСведенийИБухгалтерии.ПричинаСписания КАК ПричинаСписания,
		|	ЕСТЬNULL(СрезПоследнихПоРегистрамСведенийИБухгалтерии.СрокПолезногоИспользования, 0) КАК СрокПолезногоИспользования,
		|	ЕСТЬNULL(СрезПоследнихПоРегистрамСведенийИБухгалтерии.ПервоначальнаяСтоимость, 0) КАК ПервоначальнаяСтоимость,
		|	СрезПоследнихПоРегистрамСведенийИБухгалтерии.ПорядокПогашенияСтоимости КАК ПорядокПогашенияСтоимости,
		|	СрезПоследнихПоРегистрамСведенийИБухгалтерии.ПоследнееМОЛ КАК ПоследнееМОЛ,
		|	СрезПоследнихПоРегистрамСведенийИБухгалтерии.ПредпоследнееМОЛ КАК ПредпоследнееМОЛ,
		|	СрезПоследнихПоРегистрамСведенийИБухгалтерии.ПоследнееМестонахождение КАК ПоследнееМестонахождение,
		|	СрезПоследнихПоРегистрамСведенийИБухгалтерии.ПредпоследнееМестонахождение КАК ПредпоследнееМестонахождение,
		|	СрезПоследнихПоРегистрамСведенийИБухгалтерии.ДатаПринятияКУчету КАК ДатаПринятияКУчету,
		|	СрезПоследнихПоРегистрамСведенийИБухгалтерии.ДокументПринятияКУчету КАК ДокументПринятияКУчету,
		|	ЕСТЬNULL(ВосстановительнаяСтоимостьОСБУ.ВосстановительнаяСтоимость, 0) КАК ВосстановительнаяСтоимость,
		|	ЕСТЬNULL(НачисленнаяАмортизацияОС_Суммы.НачисленнаяАмортизация, 0) КАК НачисленнаяАмортизация,
		|	ЕСТЬNULL(НачисленнаяАмортизацияОС_Суммы.НачисленныйИзнос, 0) КАК НачисленныйИзнос,
		|	ЕСТЬNULL(АмортизацияИлиИзносПоДокументу.СуммаОборотКт, 0) КАК АмортизацияПоДокументу,
		|	ЕСТЬNULL(АмортизацияИлиИзносПоДокументу.СуммаОборотДт, 0) КАК ИзносПоДокументу,
		|	СрезПоследнихПоРегистрамСведенийИБухгалтерии.СчетНачисленияАмортизации КАК СчетНачисленияАмортизации,
		|	ЕСТЬNULL(ВосстановительнаяСтоимостьОСБУПоДокументу.ИзменениеСтоимости, 0) КАК ИзменениеСтоимости
		|ПОМЕСТИТЬ ИнвентарнаяКнига22
		|ИЗ
		|	СрезПоследнихПоРегистрамСведенийИБухгалтерии КАК СрезПоследнихПоРегистрамСведенийИБухгалтерии
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВосстановительнаяСтоимостьОСБУ КАК ВосстановительнаяСтоимостьОСБУ
		|		ПО СрезПоследнихПоРегистрамСведенийИБухгалтерии.ДатаПоследнегоДвиженияВосстановительнаяСтоимость = ВосстановительнаяСтоимостьОСБУ.Период
		|			И СрезПоследнихПоРегистрамСведенийИБухгалтерии.ОсновноеСредство = ВосстановительнаяСтоимостьОСБУ.ОсновноеСредство
		|			И (СрезПоследнихПоРегистрамСведенийИБухгалтерии.СчетУчета = ВосстановительнаяСтоимостьОСБУ.СчетУчета
		|				ИЛИ СрезПоследнихПоРегистрамСведенийИБухгалтерии.СчетУчетаЦФ = ВосстановительнаяСтоимостьОСБУ.СчетУчета)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВосстановительнаяСтоимостьОСБУ КАК ВосстановительнаяСтоимостьОСБУПоДокументу
		|		ПО СрезПоследнихПоРегистрамСведенийИБухгалтерии.Документ = ВосстановительнаяСтоимостьОСБУПоДокументу.Регистратор
		|			И СрезПоследнихПоРегистрамСведенийИБухгалтерии.ОсновноеСредство = ВосстановительнаяСтоимостьОСБУПоДокументу.ОсновноеСредство
		|			И (СрезПоследнихПоРегистрамСведенийИБухгалтерии.СчетУчета = ВосстановительнаяСтоимостьОСБУПоДокументу.СчетУчета
		|				ИЛИ СрезПоследнихПоРегистрамСведенийИБухгалтерии.СчетУчетаЦФ = ВосстановительнаяСтоимостьОСБУПоДокументу.СчетУчета)
		|		ЛЕВОЕ СОЕДИНЕНИЕ НачисленнаяАмортизацияОС_Суммы КАК НачисленнаяАмортизацияОС_Суммы
		|		ПО СрезПоследнихПоРегистрамСведенийИБухгалтерии.ДатаПоследнегоДвиженияНачисленнаяАмортизацияИлиИзнос = НачисленнаяАмортизацияОС_Суммы.Период
		|			И СрезПоследнихПоРегистрамСведенийИБухгалтерии.ОсновноеСредство = НачисленнаяАмортизацияОС_Суммы.ОсновноеСредство
		|			И (СрезПоследнихПоРегистрамСведенийИБухгалтерии.СчетНачисленияАмортизации = НачисленнаяАмортизацияОС_Суммы.СчетНачисленияАмортизации
		|				ИЛИ СрезПоследнихПоРегистрамСведенийИБухгалтерии.СчетНачисленияАмортизацииЦФ = НачисленнаяАмортизацияОС_Суммы.СчетНачисленияАмортизации)
		|		ЛЕВОЕ СОЕДИНЕНИЕ АмортизацияИлиИзносПоДокументу КАК АмортизацияИлиИзносПоДокументу
		|		ПО СрезПоследнихПоРегистрамСведенийИБухгалтерии.ОсновноеСредство = АмортизацияИлиИзносПоДокументу.ОсновноеСредство
		|			И СрезПоследнихПоРегистрамСведенийИБухгалтерии.ДатаДвиженийБухгалтерии = АмортизацияИлиИзносПоДокументу.Период
		|			И (СрезПоследнихПоРегистрамСведенийИБухгалтерии.СчетНачисленияАмортизации = АмортизацияИлиИзносПоДокументу.Счет
		|				ИЛИ СрезПоследнихПоРегистрамСведенийИБухгалтерии.СчетНачисленияАмортизацииЦФ = АмортизацияИлиИзносПоДокументу.Счет)
		|			И СрезПоследнихПоРегистрамСведенийИБухгалтерии.СчетНачисленияАмортизации <> АмортизацияИлиИзносПоДокументу.КорСчет
		|			И СрезПоследнихПоРегистрамСведенийИБухгалтерии.СчетНачисленияАмортизацииЦФ <> АмортизацияИлиИзносПоДокументу.КорСчет
		|			И ((СрезПоследнихПоРегистрамСведенийИБухгалтерии.ПорядокПогашенияСтоимости = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеИзноса)
		|					ИЛИ СрезПоследнихПоРегистрамСведенийИБухгалтерии.ПорядокПогашенияСтоимости = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеИзносаПоЕНАОФ))
		|					И АмортизацияИлиИзносПоДокументу.СуммаОборотДт <> 0
		|				ИЛИ (СрезПоследнихПоРегистрамСведенийИБухгалтерии.ПорядокПогашенияСтоимости <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеИзноса)
		|					ИЛИ СрезПоследнихПоРегистрамСведенийИБухгалтерии.ПорядокПогашенияСтоимости <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеИзносаПоЕНАОФ))
		|					И АмортизацияИлиИзносПоДокументу.СуммаОборотКт <> 0)
		|ГДЕ
		|	&УсловиеПодразделение
		|	И &УсловиеМОЛ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ СрезПоследнихПоРегистрамСведенийИБухгалтерии
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ СрезПоследнихПоРегистрамСведений
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ДатыПоследнихДвиженийПоРегистрамСведений
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ДвиженияОС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ТаблицаДокументы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВосстановительнаяСтоимостьОСБУ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ НачисленнаяАмортизацияОС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ НачисленнаяАмортизацияОС_Суммы
		|";
		
		Если НЕ Подразделение.Пустая() Тогда
			УсловиеПодразделение = "(СрезПоследнихПоРегистрамСведенийИБухгалтерии.ПоследнееМестонахождение = &Подразделение 
									|	ИЛИ СрезПоследнихПоРегистрамСведенийИБухгалтерии.ПредпоследнееМестонахождение = &Подразделение)";
		Иначе
			УсловиеПодразделение = "ИСТИНА";
		КонецЕсли; 
		
		Если НЕ МОЛ.Пустая() Тогда
			УсловиеМОЛ = "(СрезПоследнихПоРегистрамСведенийИБухгалтерии.ПоследнееМОЛ = &МОЛ 
							|	ИЛИ СрезПоследнихПоРегистрамСведенийИБухгалтерии.ПредпоследнееМОЛ = &МОЛ)";
		Иначе
			УсловиеМОЛ = "ИСТИНА";
		КонецЕсли; 
		
		Если ЗначениеЗаполнено(НачалоПериода22)
			И ЗначениеЗаполнено(КонецПериода22) Тогда
			
			ОтборПоПериодуРегистра = "СобытияОСОрганизаций.Период МЕЖДУ &НачалоПериода22 И &КонецПериода22";
			ОтборПоПериодуДокумента = "ДанныеДокумента.Дата МЕЖДУ &НачалоПериода22 И &КонецПериода22";
			
		ИначеЕсли ЗначениеЗаполнено(НачалоПериода22) Тогда
			
			ОтборПоПериодуРегистра = "СобытияОСОрганизаций.Период >= &НачалоПериода22";
			ОтборПоПериодуДокумента = "ДанныеДокумента.Дата >= &НачалоПериода22";
			
		ИначеЕсли ЗначениеЗаполнено(КонецПериода22) Тогда
			
			ОтборПоПериодуРегистра = "СобытияОСОрганизаций.Период <= &КонецПериода22";
			ОтборПоПериодуДокумента = "ДанныеДокумента.Дата <= &КонецПериода22";
			
		Иначе
			
			ОтборПоПериодуРегистра = "ИСТИНА";
			ОтборПоПериодуДокумента = "ИСТИНА";
			
		КонецЕсли;
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПодразделение", УсловиеПодразделение);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеМОЛ", УсловиеМОЛ);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОтборПоПериодуРегистра", ОтборПоПериодуРегистра);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОтборПоПериодуДокумента", ОтборПоПериодуДокумента);
		
		СписокЗапросов.Добавить(ТекстЗапроса);
		
	КонецЕсли;
	
	#КонецОбласти
	
	#Область Версия24
	
	Если ИспользоватьВерсию24 Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	НачисленнаяАмортизацияОС.ОсновноеСредство КАК ОсновноеСредство,
		|	НачисленнаяАмортизацияОС.Период КАК Период,
		|	-(НачисленнаяАмортизацияОС.АмортизацияРеглКонечныйОстаток + НачисленнаяАмортизацияОС.АмортизацияЦФКонечныйОстаток) КАК НачисленнаяАмортизация
		|ПОМЕСТИТЬ НачисленнаяАмортизацияОС
		|ИЗ
		|	РегистрНакопления.АмортизацияОС.ОстаткиИОбороты(&НачалоПериода24, &КонецПериода24, Регистратор, , Организация = &Организация) КАК НачисленнаяАмортизацияОС
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство,
		|	Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СтоимостьОС.ОсновноеСредство КАК ОсновноеСредство,
		|	СтоимостьОС.Период КАК Период,
		|	СтоимостьОС.Регистратор КАК Регистратор,
		|	СтоимостьОС.СтоимостьРеглКонечныйОстаток + СтоимостьОС.СтоимостьЦФКонечныйОстаток КАК ВосстановительнаяСтоимость,
		|	СтоимостьОС.СтоимостьРеглОборот + СтоимостьОС.СтоимостьЦФОборот КАК ИзменениеСтоимости
		|ПОМЕСТИТЬ СтоимостьОС
		|ИЗ
		|	РегистрНакопления.СтоимостьОС.ОстаткиИОбороты(&НачалоПериода24, &КонецПериода24, Регистратор, , Организация = &Организация) КАК СтоимостьОС
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство,
		|	Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДанныеДокумента.Ссылка КАК Ссылка,
		|	КОНЕЦПЕРИОДА(ДанныеДокумента.Дата, МЕСЯЦ) КАК ДатаДвиженийБухгалтерии,
		|	""ПринятиеКУчетуОС"" КАК ТипДокумента,
		|	"""" КАК ПричинаСписания,
		|	NULL КАК Событие
		|ПОМЕСТИТЬ ТаблицаДокументы
		|ИЗ
		|	Документ.ПринятиеКУчетуОС2_4 КАК ДанныеДокумента
		|ГДЕ
		|	ДанныеДокумента.Проведен
		|	И ДанныеДокумента.Организация = &Организация
		|	И &ОтборПоПериодуДокумента
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка КАК Ссылка,
		|	КОНЕЦПЕРИОДА(ДанныеДокумента.Дата, МЕСЯЦ) КАК ДатаДвиженийБухгалтерии,
		|	""РазукомплектацияОС"" КАК ТипДокумента,
		|	"""" КАК ПричинаСписания,
		|	NULL Событие
		|ИЗ
		|	Документ.РазукомплектацияОС КАК ДанныеДокумента
		|ГДЕ
		|	ДанныеДокумента.Проведен
		|	И ДанныеДокумента.Организация = &Организация
		|	И &ОтборПоПериодуДокумента
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка КАК Ссылка,
		|	КОНЕЦПЕРИОДА(ДанныеДокумента.Дата, МЕСЯЦ) КАК ДатаДвиженийБухгалтерии,
		|	""ОбъединениеОС"" КАК ТипДокумента,
		|	"""" КАК ПричинаСписания,
		|	NULL Событие
		|ИЗ
		|	Документ.ОбъединениеОС КАК ДанныеДокумента
		|ГДЕ
		|	ДанныеДокумента.Проведен
		|	И ДанныеДокумента.Организация = &Организация
		|	И &ОтборПоПериодуДокумента
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка,
		|	ДанныеДокумента.Дата,
		|	""ПеремещениеОС"",
		|	"""",
		|	NULL
		|ИЗ
		|	Документ.ПеремещениеОС2_4 КАК ДанныеДокумента
		|ГДЕ
		|	ДанныеДокумента.Проведен
		|	И ДанныеДокумента.Организация = &Организация
		|	И &ОтборПоПериодуДокумента
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка,
		|	ДанныеДокумента.Дата,
		|	""ПередачаОС"",
		|	"""",
		|	NULL
		|ИЗ
		|	Документ.ПодготовкаКПередачеОС2_4 КАК ДанныеДокумента
		|ГДЕ
		|	ДанныеДокумента.Проведен
		|	И ДанныеДокумента.Организация = &Организация
		|	И &ОтборПоПериодуДокумента
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка,
		|	ДанныеДокумента.Дата,
		|	""ПереоценкаОС"",
		|	"""",
		|	NULL
		|ИЗ
		|	Документ.ПереоценкаОС2_4 КАК ДанныеДокумента
		|ГДЕ
		|	ДанныеДокумента.Проведен
		|	И ДанныеДокумента.Организация = &Организация
		|	И &ОтборПоПериодуДокумента
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка,
		|	ДанныеДокумента.Дата,
		|	""СписаниеОС"",
		|	ДанныеДокумента.ПричинаСписания.Представление,
		|	NULL
		|ИЗ
		|	Документ.СписаниеОС2_4 КАК ДанныеДокумента
		|ГДЕ
		|	ДанныеДокумента.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеОСЧастичное)
		|	И ДанныеДокумента.Проведен
		|	И ДанныеДокумента.Организация = &Организация
		|	И &ОтборПоПериодуДокумента
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СобытияОСОрганизаций.ОсновноеСредство КАК ОсновноеСредство,
		|	ТаблицаДокументы.Ссылка КАК Документ,
		|	ТаблицаДокументы.ТипДокумента КАК ТипДокумента,
		|	ТаблицаДокументы.ПричинаСписания КАК ПричинаСписания,
		|	СобытияОСОрганизаций.Период КАК ДатаДокумента,
		|	ТаблицаДокументы.ДатаДвиженийБухгалтерии КАК ДатаДвиженийБухгалтерии
		|ПОМЕСТИТЬ ДвиженияОС
		|ИЗ
		|	РегистрСведений.СобытияОСОрганизаций КАК СобытияОСОрганизаций
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДокументы КАК ТаблицаДокументы
		|		ПО СобытияОСОрганизаций.Регистратор = ТаблицаДокументы.Ссылка
		|			И (СобытияОСОрганизаций.Событие = ЕСТЬNULL(ТаблицаДокументы.Событие, СобытияОСОрганизаций.Событие))
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РазукомплектацияОС КАК РазукомплектацияОС
		|		ПО РазукомплектацияОС.Ссылка = СобытияОСОрганизаций.Регистратор
		|ГДЕ
		|	&ОтборПоПериодуРегистра
		|	И СобытияОСОрганизаций.Организация = &Организация
		|	И (РазукомплектацияОС.Ссылка ЕСТЬ NULL
		|		ИЛИ РазукомплектацияОС.ОсновноеСредство <> СобытияОСОрганизаций.ОсновноеСредство)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияОС.ОсновноеСредство КАК ОсновноеСредство,
		|	ДвиженияОС.Документ КАК Документ,
		|	ДвиженияОС.ТипДокумента КАК ТипДОкумента,
		|	ДвиженияОС.ДатаДокумента КАК ДатаДокумента,
		|	ДвиженияОС.ДатаДвиженийБухгалтерии КАК ДатаДвиженийБухгалтерии,
		|	ДвиженияОС.ПричинаСписания КАК ПричинаСписания,
		|	МАКСИМУМ(ПервоначальныеСведенияОС.Период) КАК ДатаПоследнегоПервоначальныеСведенияОС,
		|	МАКСИМУМ(ПараметрыАмортизацииОСБУ.Период) КАК ДатаПоследнегоПараметрыАмортизацииОС,
		|	МАКСИМУМ(МестонахождениеОСПоследнее.Период) КАК ДатаПоследнегоМестонахождениеОС,
		|	МАКСИМУМ(МестонахождениеОСПредпоследнее.Период) КАК ДатаПредпоследнегоМестонахождениеОС,
		|	МАКСИМУМ(ПорядокУчетаОС.Период) КАК ДатаПоследнегоПорядокУчетаОС
		|ПОМЕСТИТЬ ДатыПоследнихДвиженийПоРегистрамСведений
		|ИЗ
		|	ДвиженияОС КАК ДвиженияОС
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
		|		ПО ДвиженияОС.ДатаДвиженийБухгалтерии >= ПервоначальныеСведенияОС.Период
		|			И ДвиженияОС.ОсновноеСредство = ПервоначальныеСведенияОС.ОсновноеСредство
		|			И ПервоначальныеСведенияОС.ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОС КАК МестонахождениеОСПоследнее
		|		ПО ДвиженияОС.ДатаДокумента >= МестонахождениеОСПоследнее.Период
		|			И ДвиженияОС.ОсновноеСредство = МестонахождениеОСПоследнее.ОсновноеСредство
		|			И МестонахождениеОСПоследнее.Организация = &Организация
		|			И МестонахождениеОСПоследнее.ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСБУ КАК ПараметрыАмортизацииОСБУ
		|		ПО ДвиженияОС.ДатаДокумента >= ПараметрыАмортизацииОСБУ.Период
		|			И ПараметрыАмортизацииОСБУ.ОсновноеСредство = ДвиженияОС.ОсновноеСредство
		|			И ПараметрыАмортизацииОСБУ.Организация = &Организация
		|			И ПараметрыАмортизацииОСБУ.ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОС КАК МестонахождениеОСПредпоследнее
		|		ПО ДвиженияОС.ОсновноеСредство = МестонахождениеОСПредпоследнее.ОсновноеСредство
		|			И ДвиженияОС.ДатаДокумента > МестонахождениеОСПредпоследнее.Период
		|			И МестонахождениеОСПредпоследнее.Организация = &Организация
		|			И МестонахождениеОСПредпоследнее.ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОС КАК ПорядокУчетаОС
		|		ПО ДвиженияОС.ОсновноеСредство = ПорядокУчетаОС.ОсновноеСредство
		|			И ДвиженияОС.ДатаДвиженийБухгалтерии >= ПорядокУчетаОС.Период
		|			И ПорядокУчетаОС.Организация = &Организация
		|			И ПорядокУчетаОС.ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДвиженияОС.ОсновноеСредство,
		|	ДвиженияОС.Документ,
		|	ДвиженияОС.ТипДокумента,
		|	ДвиженияОС.ДатаДокумента,
		|	ДвиженияОС.ДатаДвиженийБухгалтерии,
		|	ДвиженияОС.ПричинаСписания
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДатыПоследнихДвиженийПоРегистрамСведений.ОсновноеСредство КАК ОсновноеСредство,
		|	ДатыПоследнихДвиженийПоРегистрамСведений.Документ КАК Документ,
		|	ДатыПоследнихДвиженийПоРегистрамСведений.ТипДОкумента КАК ТипДокумента,
		|	ДатыПоследнихДвиженийПоРегистрамСведений.ДатаДокумента КАК ДатаДокумента,
		|	ДатыПоследнихДвиженийПоРегистрамСведений.ДатаДвиженийБухгалтерии КАК ДатаДвиженийБухгалтерии,
		|	ДатыПоследнихДвиженийПоРегистрамСведений.ПричинаСписания КАК ПричинаСписания,
		|	ПараметрыАмортизацииОС.СрокПолезногоИспользованияБУ КАК СрокПолезногоИспользования,
		|	ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьБУ КАК ПервоначальнаяСтоимость,
		|	ПервоначальныеСведенияОС.ПорядокУчетаБУ КАК ПорядокПогашенияСтоимости,
		|	ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюБУ КАК ДатаВводаВЭксплуатациюБУ,
		|	ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюБУ КАК ДокументВводаВЭксплуатациюБУ,
		|	МестонахождениеОСПоследнее.МОЛ КАК ПоследнееМОЛ,
		|	МестонахождениеОСПоследнее.Местонахождение КАК ПоследнееМестонахождение,
		|	МестонахождениеОСПредпоследнее.МОЛ КАК ПредпоследнееМОЛ,
		|	МестонахождениеОСПредпоследнее.Местонахождение КАК ПредпоследнееМестонахождение
		|ПОМЕСТИТЬ СрезПоследнихПоРегистрамСведений
		|ИЗ
		|	ДатыПоследнихДвиженийПоРегистрамСведений КАК ДатыПоследнихДвиженийПоРегистрамСведений
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОС КАК ПорядокУчетаОС
		|		ПО ДатыПоследнихДвиженийПоРегистрамСведений.ОсновноеСредство = ПорядокУчетаОС.ОсновноеСредство
		|			И ДатыПоследнихДвиженийПоРегистрамСведений.ДатаПоследнегоПорядокУчетаОС = ПорядокУчетаОС.Период
		|			И ПорядокУчетаОС.Организация = &Организация
		|			И ПорядокУчетаОС.ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОС КАК МестонахождениеОСПоследнее
		|		ПО ДатыПоследнихДвиженийПоРегистрамСведений.ОсновноеСредство = МестонахождениеОСПоследнее.ОсновноеСредство
		|			И ДатыПоследнихДвиженийПоРегистрамСведений.ДатаПоследнегоМестонахождениеОС = МестонахождениеОСПоследнее.Период
		|			И МестонахождениеОСПоследнее.Организация = &Организация
		|			И МестонахождениеОСПоследнее.ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОС КАК МестонахождениеОСПредпоследнее
		|		ПО ДатыПоследнихДвиженийПоРегистрамСведений.ОсновноеСредство = МестонахождениеОСПредпоследнее.ОсновноеСредство
		|			И ДатыПоследнихДвиженийПоРегистрамСведений.ДатаПредпоследнегоМестонахождениеОС = МестонахождениеОСПредпоследнее.Период
		|			И МестонахождениеОСПредпоследнее.Организация = &Организация
		|			И МестонахождениеОСПредпоследнее.ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСБУ КАК ПараметрыАмортизацииОС
		|		ПО ДатыПоследнихДвиженийПоРегистрамСведений.ОсновноеСредство = ПараметрыАмортизацииОС.ОсновноеСредство
		|			И ДатыПоследнихДвиженийПоРегистрамСведений.ДатаПоследнегоПараметрыАмортизацииОС = ПараметрыАмортизацииОС.Период
		|			И ПараметрыАмортизацииОС.Организация = &Организация
		|			И ПараметрыАмортизацииОС.ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
		|		ПО ДатыПоследнихДвиженийПоРегистрамСведений.ОсновноеСредство = ПервоначальныеСведенияОС.ОсновноеСредство
		|			И ДатыПоследнихДвиженийПоРегистрамСведений.ДатаПоследнегоПервоначальныеСведенияОС = ПервоначальныеСведенияОС.Период
		|			И ПервоначальныеСведенияОС.ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СрезПоследнихПоРегистрамСведений.ОсновноеСредство КАК ОсновноеСредство,
		|	СрезПоследнихПоРегистрамСведений.Документ КАК Документ,
		|	СрезПоследнихПоРегистрамСведений.ТипДокумента КАК ТипДокумента,
		|	СрезПоследнихПоРегистрамСведений.ДатаДокумента КАК ДатаДокумента,
		|	СрезПоследнихПоРегистрамСведений.ДатаДвиженийБухгалтерии КАК ДатаДвиженийБухгалтерии,
		|	СрезПоследнихПоРегистрамСведений.ПричинаСписания КАК ПричинаСписания,
		|	СрезПоследнихПоРегистрамСведений.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
		|	СрезПоследнихПоРегистрамСведений.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость,
		|	СрезПоследнихПоРегистрамСведений.ПорядокПогашенияСтоимости КАК ПорядокПогашенияСтоимости,
		|	СрезПоследнихПоРегистрамСведений.ПоследнееМОЛ КАК ПоследнееМОЛ,
		|	СрезПоследнихПоРегистрамСведений.ПредпоследнееМОЛ КАК ПредпоследнееМОЛ,
		|	СрезПоследнихПоРегистрамСведений.ПоследнееМестонахождение КАК ПоследнееМестонахождение,
		|	СрезПоследнихПоРегистрамСведений.ПредпоследнееМестонахождение КАК ПредпоследнееМестонахождение,
		|	СрезПоследнихПоРегистрамСведений.ДатаВводаВЭксплуатациюБУ КАК ДатаВводаВЭксплуатациюБУ,
		|	СрезПоследнихПоРегистрамСведений.ДокументВводаВЭксплуатациюБУ КАК ДокументВводаВЭксплуатациюБУ,
		|	МАКСИМУМ(СтоимостьОС.Период) КАК ДатаПоследнегоДвиженияВосстановительнаяСтоимость,
		|	МАКСИМУМ(НачисленнаяАмортизацияОС.Период) КАК ДатаПоследнегоДвиженияНачисленнаяАмортизацияИлиИзнос
		|ПОМЕСТИТЬ СрезПоследнихПоРегистрамСведенийИБухгалтерии
		|ИЗ
		|	СрезПоследнихПоРегистрамСведений КАК СрезПоследнихПоРегистрамСведений
		|		ЛЕВОЕ СОЕДИНЕНИЕ СтоимостьОС КАК СтоимостьОС
		|		ПО СрезПоследнихПоРегистрамСведений.ОсновноеСредство = СтоимостьОС.ОсновноеСредство
		|			И СрезПоследнихПоРегистрамСведений.ДатаДвиженийБухгалтерии > СтоимостьОС.Период
		|			И (НЕ СтоимостьОС.ВосстановительнаяСтоимость ЕСТЬ NULL)
		|		ЛЕВОЕ СОЕДИНЕНИЕ НачисленнаяАмортизацияОС КАК НачисленнаяАмортизацияОС
		|		ПО СрезПоследнихПоРегистрамСведений.ОсновноеСредство = НачисленнаяАмортизацияОС.ОсновноеСредство
		|			И СрезПоследнихПоРегистрамСведений.ДатаДвиженийБухгалтерии > НачисленнаяАмортизацияОС.Период
		|
		|СГРУППИРОВАТЬ ПО
		|	СрезПоследнихПоРегистрамСведений.ОсновноеСредство,
		|	СрезПоследнихПоРегистрамСведений.Документ,
		|	СрезПоследнихПоРегистрамСведений.ТипДокумента,
		|	СрезПоследнихПоРегистрамСведений.ДатаДокумента,
		|	СрезПоследнихПоРегистрамСведений.ПричинаСписания,
		|	СрезПоследнихПоРегистрамСведений.СрокПолезногоИспользования,
		|	СрезПоследнихПоРегистрамСведений.ПервоначальнаяСтоимость,
		|	СрезПоследнихПоРегистрамСведений.ПорядокПогашенияСтоимости,
		|	СрезПоследнихПоРегистрамСведений.ПоследнееМОЛ,
		|	СрезПоследнихПоРегистрамСведений.ПредпоследнееМОЛ,
		|	СрезПоследнихПоРегистрамСведений.ПоследнееМестонахождение,
		|	СрезПоследнихПоРегистрамСведений.ПредпоследнееМестонахождение,
		|	СрезПоследнихПоРегистрамСведений.ДатаДвиженийБухгалтерии,
		|	СрезПоследнихПоРегистрамСведений.ДатаВводаВЭксплуатациюБУ,
		|	СрезПоследнихПоРегистрамСведений.ДокументВводаВЭксплуатациюБУ
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СрезПоследнихПоРегистрамСведенийИБухгалтерии.ОсновноеСредство КАК ОсновноеСредство,
		|	СрезПоследнихПоРегистрамСведенийИБухгалтерии.Документ КАК Документ,
		|	СрезПоследнихПоРегистрамСведенийИБухгалтерии.ТипДокумента КАК ТипДокумента,
		|	СрезПоследнихПоРегистрамСведенийИБухгалтерии.ДатаДокумента КАК ДатаДокумента,
		|	СрезПоследнихПоРегистрамСведенийИБухгалтерии.ПричинаСписания КАК ПричинаСписания,
		|	ЕСТЬNULL(СрезПоследнихПоРегистрамСведенийИБухгалтерии.СрокПолезногоИспользования, 0) КАК СрокПолезногоИспользования,
		|	ЕСТЬNULL(СрезПоследнихПоРегистрамСведенийИБухгалтерии.ПервоначальнаяСтоимость, 0) КАК ПервоначальнаяСтоимость,
		|	СрезПоследнихПоРегистрамСведенийИБухгалтерии.ПорядокПогашенияСтоимости КАК ПорядокПогашенияСтоимости,
		|	СрезПоследнихПоРегистрамСведенийИБухгалтерии.ПоследнееМОЛ КАК ПоследнееМОЛ,
		|	СрезПоследнихПоРегистрамСведенийИБухгалтерии.ПредпоследнееМОЛ КАК ПредпоследнееМОЛ,
		|	СрезПоследнихПоРегистрамСведенийИБухгалтерии.ПоследнееМестонахождение КАК ПоследнееМестонахождение,
		|	СрезПоследнихПоРегистрамСведенийИБухгалтерии.ПредпоследнееМестонахождение КАК ПредпоследнееМестонахождение,
		|	СрезПоследнихПоРегистрамСведенийИБухгалтерии.ДатаВводаВЭксплуатациюБУ КАК ДатаПринятияКУчету,
		|	СрезПоследнихПоРегистрамСведенийИБухгалтерии.ДокументВводаВЭксплуатациюБУ КАК ДокументПринятияКУчету,
		|	ЕСТЬNULL(СтоимостьОС.ВосстановительнаяСтоимость, 0) КАК ВосстановительнаяСтоимость,
		|	ЕСТЬNULL(НачисленнаяАмортизацияОС.НачисленнаяАмортизация, 0) КАК НачисленнаяАмортизация,
		|	ЕСТЬNULL(НачисленнаяАмортизацияОС.НачисленнаяАмортизация, 0) КАК НачисленныйИзнос,
		|	0 КАК АмортизацияПоДокументу,
		|	0 КАК ИзносПоДокументу,
		|	ЕСТЬNULL(СтоимостьОСПоДокументу.ИзменениеСтоимости, 0) КАК ИзменениеСтоимости
		|ПОМЕСТИТЬ ИнвентарнаяКнига24
		|ИЗ
		|	СрезПоследнихПоРегистрамСведенийИБухгалтерии КАК СрезПоследнихПоРегистрамСведенийИБухгалтерии
		|		ЛЕВОЕ СОЕДИНЕНИЕ СтоимостьОС КАК СтоимостьОС
		|		ПО СрезПоследнихПоРегистрамСведенийИБухгалтерии.ДатаПоследнегоДвиженияВосстановительнаяСтоимость = СтоимостьОС.Период
		|			И СрезПоследнихПоРегистрамСведенийИБухгалтерии.ОсновноеСредство = СтоимостьОС.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ СтоимостьОС КАК СтоимостьОСПоДокументу
		|		ПО СрезПоследнихПоРегистрамСведенийИБухгалтерии.Документ = СтоимостьОСПоДокументу.Регистратор
		|			И СрезПоследнихПоРегистрамСведенийИБухгалтерии.ОсновноеСредство = СтоимостьОСПоДокументу.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ НачисленнаяАмортизацияОС КАК НачисленнаяАмортизацияОС
		|		ПО СрезПоследнихПоРегистрамСведенийИБухгалтерии.ОсновноеСредство = НачисленнаяАмортизацияОС.ОсновноеСредство
		|			И СрезПоследнихПоРегистрамСведенийИБухгалтерии.ДатаПоследнегоДвиженияНачисленнаяАмортизацияИлиИзнос = НачисленнаяАмортизацияОС.Период
		|ГДЕ
		|	&УсловиеПодразделение
		|	И &УсловиеМОЛ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ СрезПоследнихПоРегистрамСведенийИБухгалтерии
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ СрезПоследнихПоРегистрамСведений
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ДатыПоследнихДвиженийПоРегистрамСведений
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ДвиженияОС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ТаблицаДокументы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ СтоимостьОС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ НачисленнаяАмортизацияОС";
		
		Если НЕ Подразделение.Пустая() Тогда
			УсловиеПодразделение = "(СрезПоследнихПоРегистрамСведенийИБухгалтерии.ПоследнееМестонахождение = &Подразделение 
									|	ИЛИ СрезПоследнихПоРегистрамСведенийИБухгалтерии.ПредпоследнееМестонахождение = &Подразделение)";
		Иначе
			УсловиеПодразделение = "ИСТИНА";
		КонецЕсли; 
		
		Если НЕ МОЛ.Пустая() Тогда
			УсловиеМОЛ = "(СрезПоследнихПоРегистрамСведенийИБухгалтерии.ПоследнееМОЛ = &МОЛ 
							|	ИЛИ СрезПоследнихПоРегистрамСведенийИБухгалтерии.ПредпоследнееМОЛ = &МОЛ)";
		Иначе
			УсловиеМОЛ = "ИСТИНА";
		КонецЕсли; 
		
		Если ЗначениеЗаполнено(НачалоПериода24)
			И ЗначениеЗаполнено(КонецПериода24) Тогда
			
			ОтборПоПериодуРегистра = "СобытияОСОрганизаций.Период МЕЖДУ &НачалоПериода24 И &КонецПериода24";
			ОтборПоПериодуДокумента = "ДанныеДокумента.Дата МЕЖДУ &НачалоПериода24 И &КонецПериода24";
			
		ИначеЕсли ЗначениеЗаполнено(НачалоПериода24) Тогда
			
			ОтборПоПериодуРегистра = "СобытияОСОрганизаций.Период >= &НачалоПериода24";
			ОтборПоПериодуДокумента = "ДанныеДокумента.Дата >= &НачалоПериода24";
			
		ИначеЕсли ЗначениеЗаполнено(КонецПериода24) Тогда
			
			ОтборПоПериодуРегистра = "СобытияОСОрганизаций.Период <= &КонецПериода24";
			ОтборПоПериодуДокумента = "ДанныеДокумента.Дата <= &КонецПериода24";
			
		Иначе
			
			ОтборПоПериодуРегистра = "ИСТИНА";
			ОтборПоПериодуДокумента = "ИСТИНА";
			
		КонецЕсли;
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПодразделение", УсловиеПодразделение);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеМОЛ", УсловиеМОЛ);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОтборПоПериодуРегистра", ОтборПоПериодуРегистра);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОтборПоПериодуДокумента", ОтборПоПериодуДокумента);
		
		СписокЗапросов.Добавить(ТекстЗапроса);
		
	КонецЕсли;
	
	#КонецОбласти
	
	#Область ИнвентарнаяКнига
	
	ТекстЗапроса = "";
	
	Если ИспользоватьВерсию22 Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ИнвентарнаяКнига.ОсновноеСредство КАК ОсновноеСредствоСсылка,
		|	ВЫРАЗИТЬ(ИнвентарнаяКнига.ОсновноеСредство КАК Справочник.ОбъектыЭксплуатации).НаименованиеПолное КАК ОсновноеСредствоНаименованиеПолное,
		|	ВЫРАЗИТЬ(ИнвентарнаяКнига.ОсновноеСредство КАК Справочник.ОбъектыЭксплуатации).Наименование КАК ОсновноеСредствоНаименование,
		|	ВЫРАЗИТЬ(ИнвентарнаяКнига.ОсновноеСредство КАК Справочник.ОбъектыЭксплуатации).ИнвентарныйНомер КАК ИнвентарныйНомер,
		|	ПРЕДСТАВЛЕНИЕ(ИнвентарнаяКнига.Документ) КАК ПредставлениеДокумента,
		|	ИнвентарнаяКнига.Документ КАК Документ,
		|	ИнвентарнаяКнига.ТипДокумента КАК ТипДокумента,
		|	ИнвентарнаяКнига.ДатаДокумента КАК ДатаДокумента,
		|	ИнвентарнаяКнига.ПричинаСписания КАК ПричинаСписания,
		|	ИнвентарнаяКнига.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
		|	ИнвентарнаяКнига.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость,
		|	ИнвентарнаяКнига.ПорядокПогашенияСтоимости КАК ПорядокПогашенияСтоимости,
		|	ИнвентарнаяКнига.ПоследнееМОЛ КАК ПоследнееМОЛ,
		|	ЕСТЬNULL(ВЫРАЗИТЬ(ИнвентарнаяКнига.ПоследнееМОЛ КАК Справочник.ФизическиеЛица).Представление, """") КАК ПоследнееМОЛПредставление,
		|	ИнвентарнаяКнига.ПредпоследнееМОЛ КАК ПредпоследнееМОЛ,
		|	ЕСТЬNULL(ВЫРАЗИТЬ(ИнвентарнаяКнига.ПредпоследнееМОЛ КАК Справочник.ФизическиеЛица).Представление, """") КАК ПредпоследнееМОЛПредставление,
		|	ИнвентарнаяКнига.ПоследнееМестонахождение КАК ПоследнееМестонахождение,
		|	ЕСТЬNULL(ВЫРАЗИТЬ(ИнвентарнаяКнига.ПоследнееМестонахождение КАК Справочник.СтруктураПредприятия).Представление, """") КАК ПоследнееМестонахождениеПредставление,
		|	ИнвентарнаяКнига.ПредпоследнееМестонахождение КАК ПредпоследнееМестонахождение,
		|	ЕСТЬNULL(ВЫРАЗИТЬ(ИнвентарнаяКнига.ПредпоследнееМестонахождение КАК Справочник.СтруктураПредприятия).Представление, """") КАК ПредпоследнееМестонахождениеПредставление,
		|	ИнвентарнаяКнига.ДатаПринятияКУчету КАК ДатаПринятияКУчету,
		|	ИнвентарнаяКнига.ДокументПринятияКУчету КАК ДокументПринятияКУчету,
		|	ИнвентарнаяКнига.ВосстановительнаяСтоимость КАК ВосстановительнаяСтоимость,
		|	ИнвентарнаяКнига.НачисленнаяАмортизация КАК НачисленнаяАмортизация,
		|	ИнвентарнаяКнига.НачисленныйИзнос КАК НачисленныйИзнос,
		|	ИнвентарнаяКнига.АмортизацияПоДокументу КАК АмортизацияПоДокументу,
		|	ИнвентарнаяКнига.ИзносПоДокументу КАК ИзносПоДокументу,
		|	ИнвентарнаяКнига.ИзменениеСтоимости КАК ИзменениеСтоимости
		|ИЗ
		|	ИнвентарнаяКнига22 КАК ИнвентарнаяКнига";
		
	КонецЕсли; 
	
	Если ИспользоватьВерсию22 И ИспользоватьВерсию24 Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|ОБЪЕДИНИТЬ ВСЕ
		|";
	КонецЕсли; 
	
	Если ИспользоватьВерсию24 Тогда
		
		ТекстЗапроса = ТекстЗапроса + 
		"ВЫБРАТЬ
		|	ИнвентарнаяКнига.ОсновноеСредство КАК ОсновноеСредствоСсылка,
		|	ВЫРАЗИТЬ(ИнвентарнаяКнига.ОсновноеСредство КАК Справочник.ОбъектыЭксплуатации).НаименованиеПолное КАК ОсновноеСредствоНаименованиеПолное,
		|	ВЫРАЗИТЬ(ИнвентарнаяКнига.ОсновноеСредство КАК Справочник.ОбъектыЭксплуатации).Наименование КАК ОсновноеСредствоНаименование,
		|	ВЫРАЗИТЬ(ИнвентарнаяКнига.ОсновноеСредство КАК Справочник.ОбъектыЭксплуатации).ИнвентарныйНомер КАК ИнвентарныйНомер,
		|	ПРЕДСТАВЛЕНИЕ(ИнвентарнаяКнига.Документ) КАК ПредставлениеДокумента,
		|	ИнвентарнаяКнига.Документ КАК Документ,
		|	ИнвентарнаяКнига.ТипДокумента КАК ТипДокумента,
		|	ИнвентарнаяКнига.ДатаДокумента КАК ДатаДокумента,
		|	ИнвентарнаяКнига.ПричинаСписания КАК ПричинаСписания,
		|	ИнвентарнаяКнига.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
		|	ИнвентарнаяКнига.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость,
		|	ИнвентарнаяКнига.ПорядокПогашенияСтоимости КАК ПорядокПогашенияСтоимости,
		|	ИнвентарнаяКнига.ПоследнееМОЛ КАК ПоследнееМОЛ,
		|	ЕСТЬNULL(ВЫРАЗИТЬ(ИнвентарнаяКнига.ПоследнееМОЛ КАК Справочник.ФизическиеЛица).Представление, """") КАК ПоследнееМОЛПредставление,
		|	ИнвентарнаяКнига.ПредпоследнееМОЛ КАК ПредпоследнееМОЛ,
		|	ЕСТЬNULL(ВЫРАЗИТЬ(ИнвентарнаяКнига.ПредпоследнееМОЛ КАК Справочник.ФизическиеЛица).Представление, """") КАК ПредпоследнееМОЛПредставление,
		|	ИнвентарнаяКнига.ПоследнееМестонахождение КАК ПоследнееМестонахождение,
		|	ЕСТЬNULL(ВЫРАЗИТЬ(ИнвентарнаяКнига.ПоследнееМестонахождение КАК Справочник.СтруктураПредприятия).Представление, """") КАК ПоследнееМестонахождениеПредставление,
		|	ИнвентарнаяКнига.ПредпоследнееМестонахождение КАК ПредпоследнееМестонахождение,
		|	ЕСТЬNULL(ВЫРАЗИТЬ(ИнвентарнаяКнига.ПредпоследнееМестонахождение КАК Справочник.СтруктураПредприятия).Представление, """") КАК ПредпоследнееМестонахождениеПредставление,
		|	ИнвентарнаяКнига.ДатаПринятияКУчету КАК ДатаПринятияКУчету,
		|	ИнвентарнаяКнига.ДокументПринятияКУчету КАК ДокументПринятияКУчету,
		|	ИнвентарнаяКнига.ВосстановительнаяСтоимость КАК ВосстановительнаяСтоимость,
		|	ИнвентарнаяКнига.НачисленнаяАмортизация КАК НачисленнаяАмортизация,
		|	ИнвентарнаяКнига.НачисленныйИзнос КАК НачисленныйИзнос,
		|	ИнвентарнаяКнига.АмортизацияПоДокументу КАК АмортизацияПоДокументу,
		|	ИнвентарнаяКнига.ИзносПоДокументу КАК ИзносПоДокументу,
		|	ИнвентарнаяКнига.ИзменениеСтоимости КАК ИзменениеСтоимости
		|ИЗ
		|	ИнвентарнаяКнига24 КАК ИнвентарнаяКнига";
		
	КонецЕсли; 
	
	ТекстЗапроса = ТекстЗапроса + "
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДокумента";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
		
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(СписокЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	ОписаниеТекстаЗапроса = Новый Структура;
	ОписаниеТекстаЗапроса.Вставить("ТекстЗапроса", ТекстЗапроса);
	ОписаниеТекстаЗапроса.Вставить("НачалоПериода22", ?(НЕ ЗначениеЗаполнено(НачалоПериода22), НачалоПериода22, НачалоДня(НачалоПериода22)));
	ОписаниеТекстаЗапроса.Вставить("НачалоПериода24", ?(НЕ ЗначениеЗаполнено(НачалоПериода24), НачалоПериода24, НачалоДня(НачалоПериода24)));
	ОписаниеТекстаЗапроса.Вставить("КонецПериода22", ?(НЕ ЗначениеЗаполнено(КонецПериода22), КонецПериода22, КонецДня(КонецПериода22)));
	ОписаниеТекстаЗапроса.Вставить("КонецПериода24", ?(НЕ ЗначениеЗаполнено(КонецПериода24), КонецПериода24, КонецДня(КонецПериода24)));
	
	Возврат ОписаниеТекстаЗапроса;

КонецФункции

#КонецОбласти

#КонецЕсли
