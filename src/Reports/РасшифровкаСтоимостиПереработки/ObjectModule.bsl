#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

Перем МассивОбъектов, Период, ВидЦены, КэшированныеЗначения;

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - см. ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	Настройки.События.ПриСозданииНаСервере = Истина;
КонецПроцедуры

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//   ЭтаФорма - ФормаКлиентскогоПриложения - Форма отчета.
//   Отказ - Булево - Передается из параметров обработчика "как есть".
//   СтандартнаяОбработка - Булево - Передается из параметров обработчика "как есть".
//
// См. также:
//   "ФормаКлиентскогоПриложения.ПриСозданииНаСервере" в синтакс-помощнике.
//
Процедура ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт
	
	Параметры = ЭтаФорма.Параметры;
	
	Если Параметры.Свойство("ПараметрКоманды") Тогда
		ЭтаФорма.Параметры.Отбор.Вставить("МассивОбъектов", Параметры.ПараметрКоманды);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий
	
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПользовательскиеНастройкиМодифицированы = Ложь;
	
	УстановитьОбязательныеНастройки(ПользовательскиеНастройкиМодифицированы);
	
	// Подготовим и выведем отчет.
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	
	НастройкиКомпоновкиДанных = КомпоновщикНастроек.ПолучитьНастройки();
	
	МакетКомпоновки = КомпоновщикМакета.Выполнить(
		СхемаКомпоновкиДанных,
		НастройкиКомпоновкиДанных,
		ДанныеРасшифровки);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	
	ТаблицаМатериалов = СтоимостьПереработкиДавальческогоСырья();
	
	ВнешниеНаборыДанных = Новый Структура("ТаблицаМатериалов", ТаблицаМатериалов);
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки, Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.НачатьВывод();
	ПроцессорВывода.Вывести(ПроцессорКомпоновки, Истина);
	
	ПроцессорВывода.ЗакончитьВывод();
	
	// Сообщим форме отчета, что настройки модифицированы
	Если ПользовательскиеНастройкиМодифицированы Тогда
		КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ПользовательскиеНастройкиМодифицированы", Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции
	
Процедура УстановитьОбязательныеНастройки(ПользовательскиеНастройкиМодифицированы)
	
	МассивОбъектов = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек.ФиксированныеНастройки, "МассивОбъектов").Значение;
	
	Период = КонецДня(ТекущаяДатаСеанса());
	ПараметрДатаОтчета = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "ДатаОтчета");
	
	Если ЗначениеЗаполнено(ПараметрДатаОтчета.Значение) Тогда
		Период = КонецДня(ПараметрДатаОтчета.Значение);
	Иначе
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ДатаОтчета", Период);
		ПользовательскиеНастройкиМодифицированы = Истина;
	КонецЕсли;
	
	ПараметрДатаОтчета = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "ВидЦены");
	ВидЦены = ПараметрДатаОтчета.Значение;
	
КонецПроцедуры

Функция СтоимостьПереработкиДавальческогоСырья()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗаказДавальца2_5Продукция.Ссылка                         КАК ЗаказДавальца,
	|	ЗаказДавальца2_5Продукция.Спецификация                   КАК Спецификация,
	|	ЗаказДавальца2_5Продукция.Ссылка.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЗаказДавальца2_5Продукция.Ссылка.Дата                    КАК НачалоПроизводства,
	|	ЗаказДавальца2_5Продукция.Номенклатура                   КАК Номенклатура,
	|	ЗаказДавальца2_5Продукция.Характеристика                 КАК Характеристика,
	|	ЗаказДавальца2_5Продукция.Количество                     КАК Количество,
	|	ВЫБОР
	|		КОГДА &ВидЦены = НЕОПРЕДЕЛЕНО
	|			ТОГДА ЗаказДавальца2_5Продукция.ВидЦены
	|		ИНАЧЕ &ВидЦены
	|	КОНЕЦ                                                    КАК ВидЦены,
	|	ЗаказДавальца2_5Продукция.Ссылка.Валюта                  КАК Валюта
	|ИЗ
	|	Документ.ЗаказДавальца2_5.Продукция КАК ЗаказДавальца2_5Продукция
	|ГДЕ
	|	ЗаказДавальца2_5Продукция.Ссылка В(&МассивОбъектов)
	|	И НЕ ЗаказДавальца2_5Продукция.Отменено
	|	И НЕ ЗаказДавальца2_5Продукция.Спецификация = ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)
	//++ Устарело_Переработка24
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗаказДавальцаПродукция.Ссылка                         КАК ЗаказДавальца,
	|	ЗаказДавальцаПродукция.Спецификация                   КАК Спецификация,
	|	ЗаказДавальцаПродукция.Ссылка.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЗаказДавальцаПродукция.Ссылка.Дата                    КАК НачалоПроизводства,
	|	ЗаказДавальцаПродукция.Номенклатура                   КАК Номенклатура,
	|	ЗаказДавальцаПродукция.Характеристика                 КАК Характеристика,
	|	ЗаказДавальцаПродукция.Количество                     КАК Количество,
	|	ВЫБОР
	|		КОГДА &ВидЦены = НЕОПРЕДЕЛЕНО
	|			ТОГДА ЗаказДавальцаПродукция.Ссылка.ВидЦены
	|		ИНАЧЕ &ВидЦены
	|	КОНЕЦ КАК ВидЦены,
	|	ЗаказДавальцаПродукция.Ссылка.Валюта                  КАК Валюта
	|ИЗ
	|	Документ.ЗаказДавальца.Продукция КАК ЗаказДавальцаПродукция
	|ГДЕ
	|	ЗаказДавальцаПродукция.Ссылка В(&МассивОбъектов)
	|	И НЕ ЗаказДавальцаПродукция.Спецификация = ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)
	//-- Устарело_Переработка24
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказДавальца2_5Материалы.Ссылка            КАК ЗаказДавальца,
	|	ЗаказДавальца2_5Материалы.Спецификация      КАК Спецификация,
	|	ЗаказДавальца2_5Материалы.Номенклатура      КАК Номенклатура,
	|	ЗаказДавальца2_5Материалы.Характеристика    КАК Характеристика,
	|	СУММА(ЗаказДавальца2_5Материалы.Количество) КАК Количество
	|ИЗ
	|	Документ.ЗаказДавальца2_5.Материалы КАК ЗаказДавальца2_5Материалы
	|ГДЕ
	|	ЗаказДавальца2_5Материалы.Ссылка В(&МассивОбъектов)
	|	И НЕ ЗаказДавальца2_5Материалы.Отменено
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказДавальца2_5Материалы.Ссылка,
	|	ЗаказДавальца2_5Материалы.Спецификация,
	|	ЗаказДавальца2_5Материалы.Номенклатура,
	|	ЗаказДавальца2_5Материалы.Характеристика
	//++ Устарело_Переработка24
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗаказДавальцаМатериалы.Ссылка            КАК ЗаказДавальца,
	|	ЗаказДавальцаМатериалы.Спецификация      КАК Спецификация,
	|	ЗаказДавальцаМатериалы.Номенклатура      КАК Номенклатура,
	|	ЗаказДавальцаМатериалы.Характеристика    КАК Характеристика,
	|	СУММА(ЗаказДавальцаМатериалы.Количество) КАК Количество
	|ИЗ
	|	Документ.ЗаказДавальца.Материалы КАК ЗаказДавальцаМатериалы
	|ГДЕ
	|	ЗаказДавальцаМатериалы.Ссылка В (&МассивОбъектов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказДавальцаМатериалы.Ссылка,
	|	ЗаказДавальцаМатериалы.Спецификация,
	|	ЗаказДавальцаМатериалы.Номенклатура,
	|	ЗаказДавальцаМатериалы.Характеристика
	//-- Устарело_Переработка24
	|";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр("ВидЦены", ВидЦены);
	
	Результат = Запрос.ВыполнитьПакет();
	
	ВыборкаПродукцияЗаказа   = Результат[0].Выбрать();
	ТаблицаМатериалыДавальца = Результат[1].Выгрузить();
	
	КлючПоиска = "ЗаказДавальца, Спецификация, Номенклатура, Характеристика";
	ТаблицаМатериалыДавальца.Индексы.Добавить(КлючПоиска);
	
	ПараметрыВыборки = Справочники.РесурсныеСпецификации.ПараметрыВыборкиДанных(
		"МатериалыИУслуги, Трудозатраты",,
		Перечисления.ВариантыЗаполненияОбеспеченияПроизводства.ПоНастройкамПередачиВПроизводство);
	
	ПараметрыВыборки.СпособРасчетаМатериалов = Перечисления.СпособыРасчетаМатериалов.ВероятноеПотребление;
	ПараметрыВыборки.ПереопределениеНастройкиПартииВыпуска.Использовать = Истина;
	ПараметрыВыборки.ПереопределениеНастройкиПартииВыпуска.ВыпускПроизвольнымиПорциями = Истина;
	
	ТаблицаЗатрат = НоваяТаблицаЗатрат();
	
	Пока ВыборкаПродукцияЗаказа.Следующий() Цикл
		
		ДанныеПоНоменклатуре = Справочники.РесурсныеСпецификации.ДанныеПоНоменклатуреРасширенный();
		ЗаполнитьЗначенияСвойств(ДанныеПоНоменклатуре, ВыборкаПродукцияЗаказа);
		
		ТабличныеЧасти = Справочники.РесурсныеСпецификации.ДанныеСпецификацииСПолуфабрикатами(ДанныеПоНоменклатуре, Истина, ПараметрыВыборки);
		
		// Материалы
		ТЧМатериалы = ТабличныеЧасти.МатериалыИУслуги;
		
		ТЧМатериалы.Колонки.Добавить("ЗаказДавальца",Новый ОписаниеТипов(ТипыЗаказДавальца()));
		ТЧМатериалы.ЗаполнитьЗначения(ВыборкаПродукцияЗаказа.ЗаказДавальца, "ЗаказДавальца");
		
		ТЧМатериалы.Колонки.Добавить("Продукция",Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
		ТЧМатериалы.ЗаполнитьЗначения(ВыборкаПродукцияЗаказа.Номенклатура, "Продукция");
		
		ТЧМатериалы.Колонки.Добавить("ХарактеристикаПродукции",Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
		ТЧМатериалы.ЗаполнитьЗначения(ВыборкаПродукцияЗаказа.Характеристика, "ХарактеристикаПродукции");
		
		ТЧМатериалы.Колонки.Добавить("ВидЦены",Новый ОписаниеТипов("СправочникСсылка.ВидыЦен"));
		ТЧМатериалы.ЗаполнитьЗначения(ВыборкаПродукцияЗаказа.ВидЦены, "ВидЦены");
		
		ТЧМатериалы.Колонки.Добавить("Валюта",Новый ОписаниеТипов("СправочникСсылка.Валюты"));
		ТЧМатериалы.ЗаполнитьЗначения(ВыборкаПродукцияЗаказа.Валюта, "Валюта");
		
		ТЧМатериалы.ЗаполнитьЗначения(ВыборкаПродукцияЗаказа.Спецификация, "Спецификация");
		
		Для Каждого ТекущиеДанные Из ТЧМатериалы Цикл
			
			НоваяСтрока = ТаблицаЗатрат.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущиеДанные);
			
			НоваяСтрока.КоличествоПоСпецификации = ТекущиеДанные.Количество;
			КоличествоПоСпецификации = НоваяСтрока.КоличествоПоСпецификации;
			
			Отбор = Новый Структура(КлючПоиска);
			ЗаполнитьЗначенияСвойств(Отбор, НоваяСтрока);
			
			Для Каждого МатериалДавальца Из ТаблицаМатериалыДавальца.НайтиСтроки(Отбор) Цикл
				
				Если МатериалДавальца.Количество = 0 Тогда
					Продолжить;
				КонецЕсли;
				
				Количество = Мин(КоличествоПоСпецификации, МатериалДавальца.Количество);
				
				НоваяСтрока.КоличествоПредоставляетсяДавальцем = НоваяСтрока.КоличествоПредоставляетсяДавальцем + Количество;
				
				МатериалДавальца.Количество = МатериалДавальца.Количество - Количество;
				КоличествоПоСпецификации = КоличествоПоСпецификации - Количество;
				
				Если КоличествоПоСпецификации = 0 Тогда
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаЗатрат.ЗаказДавальца                      КАК ЗаказДавальца,
	|	ТаблицаЗатрат.Спецификация                       КАК Спецификация,
	|	ТаблицаЗатрат.Продукция                          КАК Продукция,
	|	ТаблицаЗатрат.ХарактеристикаПродукции            КАК ХарактеристикаПродукции,
	|	ВЫРАЗИТЬ(ТаблицаЗатрат.Номенклатура КАК Справочник.Номенклатура)                 КАК Номенклатура,
	|	ВЫРАЗИТЬ(ТаблицаЗатрат.Характеристика КАК Справочник.ХарактеристикиНоменклатуры) КАК Характеристика,
	|	ТаблицаЗатрат.КоличествоПоСпецификации           КАК КоличествоПоСпецификации,
	|	ТаблицаЗатрат.КоличествоПредоставляетсяДавальцем КАК КоличествоПредоставляетсяДавальцем,
	|	ТаблицаЗатрат.ВидЦены                            КАК ВидЦены,
	|	ТаблицаЗатрат.Валюта                             КАК Валюта
	|ПОМЕСТИТЬ ВТТаблицаЗатрат
	|ИЗ
	|	&ТаблицаЗатрат КАК ТаблицаЗатрат
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаЗатрат.ЗаказДавальца                      КАК ЗаказДавальца,
	|	ТаблицаЗатрат.Спецификация                       КАК Спецификация,
	|	ТаблицаЗатрат.Продукция                          КАК Продукция,
	|	ТаблицаЗатрат.ХарактеристикаПродукции            КАК ХарактеристикаПродукции,
	|	ТаблицаЗатрат.Номенклатура                       КАК Номенклатура,
	|	ТаблицаЗатрат.Характеристика                     КАК Характеристика,
	|	СУММА(ТаблицаЗатрат.КоличествоПоСпецификации)           КАК КоличествоПоСпецификации,
	|	СУММА(ТаблицаЗатрат.КоличествоПредоставляетсяДавальцем) КАК КоличествоПредоставляетсяДавальцем,
	|	ТаблицаЗатрат.Валюта                             КАК Валюта,
	|	ВЫРАЗИТЬ(ВЫБОР
	|		КОГДА &ИспользуетсяЦенообразование25
	|			ТОГДА
	|				ЦеныНоменклатуры25СрезПоследних.Цена
	|				* ВЫБОР
	|					КОГДА ТаблицаЗатрат.Валюта <> ЦеныНоменклатуры25СрезПоследних.Валюта
	|						ТОГДА ВЫБОР
	|							КОГДА ЕСТЬNULL(КурсыВалютыЦены25.КурсЗнаменатель, 0) > 0
	|								И ЕСТЬNULL(КурсыВалютыЦены25.КурсЧислитель, 0) > 0
	|								И ЕСТЬNULL(КурсыВалюты.КурсЗнаменатель, 0) > 0
	|								И ЕСТЬNULL(КурсыВалюты.КурсЧислитель, 0) > 0
	|								ТОГДА
	|									(КурсыВалютыЦены25.КурсЧислитель * КурсыВалюты.КурсЗнаменатель)
	|									/ (КурсыВалюты.КурсЧислитель * КурсыВалютыЦены25.КурсЗнаменатель)
	|							ИНАЧЕ 0
	|						КОНЕЦ
	|					ИНАЧЕ 1
	|				КОНЕЦ
	|		ИНАЧЕ
	|			ЦеныНоменклатурыСрезПоследних.Цена
	|			* ВЫБОР
	|				КОГДА ТаблицаЗатрат.Валюта <> ЦеныНоменклатурыСрезПоследних.Валюта
	|					ТОГДА ВЫБОР
	|						КОГДА ЕСТЬNULL(КурсыВалютыЦены.КурсЗнаменатель, 0) > 0
	|							И ЕСТЬNULL(КурсыВалютыЦены.КурсЧислитель, 0) > 0
	|							И ЕСТЬNULL(КурсыВалюты.КурсЗнаменатель, 0) > 0
	|							И ЕСТЬNULL(КурсыВалюты.КурсЧислитель, 0) > 0
	|							ТОГДА
	|								(КурсыВалютыЦены.КурсЧислитель * КурсыВалюты.КурсЗнаменатель)
	|								/ (КурсыВалюты.КурсЧислитель * КурсыВалютыЦены.КурсЗнаменатель)
	|							ИНАЧЕ 0
	|					КОНЕЦ
	|				ИНАЧЕ 1
	|			КОНЕЦ
	|	КОНЕЦ КАК ЧИСЛО(31,2))                           КАК Цена,
	|	СУММА(ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА &ИспользуетсяЦенообразование25
	|				ТОГДА
	|					ЦеныНоменклатуры25СрезПоследних.Цена
	|					* ВЫБОР
	|						КОГДА ТаблицаЗатрат.Валюта <> ЦеныНоменклатуры25СрезПоследних.Валюта
	|							ТОГДА ВЫБОР
	|								КОГДА ЕСТЬNULL(КурсыВалютыЦены25.КурсЗнаменатель, 0) > 0
	|									И ЕСТЬNULL(КурсыВалютыЦены25.КурсЧислитель, 0) > 0
	|									И ЕСТЬNULL(КурсыВалюты.КурсЗнаменатель, 0) > 0
	|									И ЕСТЬNULL(КурсыВалюты.КурсЧислитель, 0) > 0
	|									ТОГДА
	|										(КурсыВалютыЦены25.КурсЧислитель * КурсыВалюты.КурсЗнаменатель)
	|										/ (КурсыВалюты.КурсЧислитель * КурсыВалютыЦены25.КурсЗнаменатель)
	|								ИНАЧЕ 0
	|							КОНЕЦ
	|						ИНАЧЕ 1
	|					КОНЕЦ
	|			ИНАЧЕ
	|				ЦеныНоменклатурыСрезПоследних.Цена
	|				* ВЫБОР
	|					КОГДА ТаблицаЗатрат.Валюта <> ЦеныНоменклатурыСрезПоследних.Валюта
	|						ТОГДА ВЫБОР
	|							КОГДА ЕСТЬNULL(КурсыВалютыЦены.КурсЗнаменатель, 0) > 0
	|								И ЕСТЬNULL(КурсыВалютыЦены.КурсЧислитель, 0) > 0
	|								И ЕСТЬNULL(КурсыВалюты.КурсЗнаменатель, 0) > 0
	|								И ЕСТЬNULL(КурсыВалюты.КурсЧислитель, 0) > 0
	|								ТОГДА
	|									(КурсыВалютыЦены.КурсЧислитель * КурсыВалюты.КурсЗнаменатель)
	|									/ (КурсыВалюты.КурсЧислитель * КурсыВалютыЦены.КурсЗнаменатель)
	|								ИНАЧЕ 0
	|						КОНЕЦ
	|					ИНАЧЕ 1
	|				КОНЕЦ
	|		КОНЕЦ КАК ЧИСЛО(31,2))
	|		* (ТаблицаЗатрат.КоличествоПоСпецификации
	|			- ТаблицаЗатрат.КоличествоПредоставляетсяДавальцем)) КАК СуммаСобственныхМатериалов
	|ПОМЕСТИТЬ ТаблицаЗатрат
	|ИЗ
	|	ВТТаблицаЗатрат КАК ТаблицаЗатрат
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|	ПО (ВидыНоменклатуры.Ссылка = ТаблицаЗатрат.Номенклатура.ВидНоменклатуры)
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних(&Период, БазоваяВалюта = &БазоваяВалюта) КАК КурсыВалюты
	|	ПО КурсыВалюты.Валюта = ТаблицаЗатрат.Валюта
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&Период) КАК ЦеныНоменклатурыСрезПоследних
	|	ПО ТаблицаЗатрат.ВидЦены = ЦеныНоменклатурыСрезПоследних.ВидЦены
	|	И ТаблицаЗатрат.Номенклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура
	|	И ТаблицаЗатрат.Характеристика = ЦеныНоменклатурыСрезПоследних.Характеристика
	|	И НЕ &ИспользуетсяЦенообразование25
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних(&Период, БазоваяВалюта = &БазоваяВалюта) КАК КурсыВалютыЦены
	|	ПО КурсыВалютыЦены.Валюта       = ЦеныНоменклатурыСрезПоследних.Валюта
	|	И НЕ &ИспользуетсяЦенообразование25
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры25.СрезПоследних(&Период) КАК ЦеныНоменклатуры25СрезПоследних
	|	ПО ЦеныНоменклатуры25СрезПоследних.Номенклатура = ТаблицаЗатрат.Номенклатура
	|	И ЦеныНоменклатуры25СрезПоследних.ХарактеристикаЦО =
	|		ЕСТЬNULL(ВЫБОР
	|				КОГДА ВидыНоменклатуры.НастройкиКлючаЦенПоХарактеристике = ЗНАЧЕНИЕ(Перечисление.ВариантОтбораДляКлючаЦен.НеИспользовать)
	|					ТОГДА ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатурыДляЦенообразования.ПустаяСсылка)
	|				ИНАЧЕ ТаблицаЗатрат.Характеристика.ХарактеристикаНоменклатурыДляЦенообразования
	|			КОНЕЦ, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатурыДляЦенообразования.ПустаяСсылка))
	|	И ЦеныНоменклатуры25СрезПоследних.СерияЦО = ЗНАЧЕНИЕ(Справочник.СерииНоменклатурыДляЦенообразования.ПустаяСсылка)
	|	И ЦеныНоменклатуры25СрезПоследних.УпаковкаЦО = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|	И ТаблицаЗатрат.ВидЦены = ЦеныНоменклатуры25СрезПоследних.ВидЦены
	|	И &ИспользуетсяЦенообразование25
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних(&Период, БазоваяВалюта = &БазоваяВалюта) КАК КурсыВалютыЦены25
	|	ПО КурсыВалютыЦены25.Валюта       = ЦеныНоменклатуры25СрезПоследних.Валюта
	|	И &ИспользуетсяЦенообразование25
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗатрат.ЗаказДавальца,
	|	ТаблицаЗатрат.Спецификация,
	|	ТаблицаЗатрат.Продукция,
	|	ТаблицаЗатрат.ХарактеристикаПродукции,
	|	ТаблицаЗатрат.Номенклатура,
	|	ТаблицаЗатрат.Характеристика,
	|	ТаблицаЗатрат.ВидЦены,
	|	ТаблицаЗатрат.Валюта,
	|	ВЫРАЗИТЬ(ВЫБОР
	|		КОГДА &ИспользуетсяЦенообразование25
	|			ТОГДА
	|				ЦеныНоменклатуры25СрезПоследних.Цена
	|				* ВЫБОР
	|					КОГДА ТаблицаЗатрат.Валюта <> ЦеныНоменклатуры25СрезПоследних.Валюта
	|						ТОГДА ВЫБОР
	|							КОГДА ЕСТЬNULL(КурсыВалютыЦены25.КурсЗнаменатель, 0) > 0
	|								И ЕСТЬNULL(КурсыВалютыЦены25.КурсЧислитель, 0) > 0
	|								И ЕСТЬNULL(КурсыВалюты.КурсЗнаменатель, 0) > 0
	|								И ЕСТЬNULL(КурсыВалюты.КурсЧислитель, 0) > 0
	|								ТОГДА
	|									(КурсыВалютыЦены25.КурсЧислитель * КурсыВалюты.КурсЗнаменатель)
	|									/ (КурсыВалюты.КурсЧислитель * КурсыВалютыЦены25.КурсЗнаменатель)
	|							ИНАЧЕ 0
	|						КОНЕЦ
	|					ИНАЧЕ 1
	|				КОНЕЦ
	|		ИНАЧЕ
	|			ЦеныНоменклатурыСрезПоследних.Цена
	|			* ВЫБОР
	|				КОГДА ТаблицаЗатрат.Валюта <> ЦеныНоменклатурыСрезПоследних.Валюта
	|					ТОГДА ВЫБОР
	|						КОГДА ЕСТЬNULL(КурсыВалютыЦены.КурсЗнаменатель, 0) > 0
	|							И ЕСТЬNULL(КурсыВалютыЦены.КурсЧислитель, 0) > 0
	|							И ЕСТЬNULL(КурсыВалюты.КурсЗнаменатель, 0) > 0
	|							И ЕСТЬNULL(КурсыВалюты.КурсЧислитель, 0) > 0
	|							ТОГДА
	|								(КурсыВалютыЦены.КурсЧислитель * КурсыВалюты.КурсЗнаменатель)
	|								/ (КурсыВалюты.КурсЧислитель * КурсыВалютыЦены.КурсЗнаменатель)
	|							ИНАЧЕ 0
	|					КОНЕЦ
	|				ИНАЧЕ 1
	|			КОНЕЦ
	|	КОНЕЦ КАК ЧИСЛО(31,2))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЗаказДавальца,
	|	Спецификация,
	|	Продукция,
	|	ХарактеристикаПродукции
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаЗатрат.ЗаказДавальца                     КАК ЗаказДавальца,
	|	ТаблицаЗатрат.Продукция                         КАК Продукция,
	|	ТаблицаЗатрат.ХарактеристикаПродукции           КАК ХарактеристикаПродукции,
	|	ТаблицаЗатрат.Спецификация                      КАК Спецификация,
	|	СУММА(ТаблицаЗатрат.СуммаСобственныхМатериалов) КАК СуммаСобственныхМатериалов
	|ПОМЕСТИТЬ СуммыСобственныхМатериалов
	|ИЗ
	|	ТаблицаЗатрат КАК ТаблицаЗатрат
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ТаблицаЗатрат.ЗаказДавальца) = ТИП(Документ.ЗаказДавальца2_5)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗатрат.ЗаказДавальца,
	|	ТаблицаЗатрат.Продукция,
	|	ТаблицаЗатрат.ХарактеристикаПродукции,
	|	ТаблицаЗатрат.Спецификация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПродукция.Ссылка            КАК ЗаказДавальца,
	|	ТаблицаПродукция.Номенклатура      КАК Продукция,
	|	ТаблицаПродукция.Характеристика    КАК ХарактеристикаПродукции,
	|	ТаблицаПродукция.Спецификация      КАК Спецификация,
	|	СУММА(ТаблицаПродукция.Количество) КАК Количество,
	|	СУММА(ЕСТЬNULL(СуммыСобственныхМатериалов.СуммаСобственныхМатериалов, 0))       КАК СуммаСобственныхМатериалов,
	|	СУММА(ТаблицаПродукция.Сумма)
	|		- СУММА(ЕСТЬNULL(СуммыСобственныхМатериалов.СуммаСобственныхМатериалов, 0)) КАК СуммаУслуги,
	|	СУММА(ТаблицаПродукция.Сумма)      КАК Сумма
	|ПОМЕСТИТЬ СуммыЗаказов
	|ИЗ
	|	Документ.ЗаказДавальца2_5.Продукция КАК ТаблицаПродукция
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ СуммыСобственныхМатериалов КАК СуммыСобственныхМатериалов
	|	ПО СуммыСобственныхМатериалов.ЗаказДавальца = ТаблицаПродукция.Ссылка
	|	И СуммыСобственныхМатериалов.Продукция = ТаблицаПродукция.Номенклатура
	|	И СуммыСобственныхМатериалов.ХарактеристикаПродукции = ТаблицаПродукция.Характеристика
	|	И СуммыСобственныхМатериалов.Спецификация = ТаблицаПродукция.Спецификация
	|	
	|ГДЕ
	|	ТаблицаПродукция.Ссылка В(&МассивОбъектов)
	|	И НЕ ТаблицаПродукция.Отменено
	|	И НЕ ТаблицаПродукция.Спецификация = ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаПродукция.Ссылка,
	|	ТаблицаПродукция.Спецификация,
	|	ТаблицаПродукция.Номенклатура,
	|	ТаблицаПродукция.Характеристика
	//++ Устарело_Переработка24
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаПродукция.Ссылка КАК ЗаказДавальца,
	|	ТаблицаПродукция.Номенклатура КАК Продукция,
	|	ТаблицаПродукция.Характеристика КАК ХарактеристикаПродукции,
	|	ТаблицаПродукция.Спецификация КАК Спецификация,
	|	СУММА(ТаблицаПродукция.Количество) КАК Количество,
	|	СУММА(ТаблицаПродукция.СуммаСобственныхМатериалов) КАК СуммаСобственныхМатериалов,
	|	СУММА(ТаблицаПродукция.СуммаУслуги) КАК СуммаУслуги,
	|	СУММА(ТаблицаПродукция.Сумма) КАК Сумма
	|ИЗ
	|	Документ.ЗаказДавальца.Продукция КАК ТаблицаПродукция
	|ГДЕ
	|	ТаблицаПродукция.Ссылка В (&МассивОбъектов)
	|	И ТаблицаПродукция.Спецификация <> ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаПродукция.Ссылка,
	|	ТаблицаПродукция.Спецификация,
	|	ТаблицаПродукция.Номенклатура,
	|	ТаблицаПродукция.Характеристика
	//-- Устарело_Переработка24
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаЗатрат.ЗаказДавальца                          КАК ЗаказДавальца,
	|	ТаблицаЗатрат.Валюта                                 КАК ВалютаДокумента,
	|	ТаблицаЗатрат.Спецификация                           КАК Спецификация,
	|	ТаблицаЗатрат.Продукция                              КАК Продукция,
	|	ТаблицаЗатрат.ХарактеристикаПродукции                КАК ХарактеристикаПродукции,
	|	ТаблицаЗатрат.Номенклатура                           КАК Номенклатура,
	|	ТаблицаЗатрат.Характеристика                         КАК Характеристика,
	|	ТаблицаЗатрат.КоличествоПоСпецификации               КАК КоличествоПоСпецификации,
	|	ТаблицаЗатрат.КоличествоПредоставляетсяДавальцем     КАК КоличествоПредоставляетсяДавальцем,
	|	ТаблицаЗатрат.Цена                                   КАК Цена,
	|	ЕСТЬNULL(СуммыЗаказов.СуммаСобственныхМатериалов, 0) КАК СуммаМатериаловВыставлено,
	|	ЕСТЬNULL(СуммыЗаказов.СуммаУслуги, 0)                КАК СуммаУслугиВыставлено,
	|	ЕСТЬNULL(СуммыЗаказов.Сумма, 0)                      КАК СуммаВсегоВыставлено
	|ИЗ
	|	ТаблицаЗатрат КАК ТаблицаЗатрат
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ СуммыЗаказов КАК СуммыЗаказов
	|	ПО СуммыЗаказов.ЗаказДавальца = ТаблицаЗатрат.ЗаказДавальца
	|	И СуммыЗаказов.Спецификация = ТаблицаЗатрат.Спецификация
	|	И СуммыЗаказов.Продукция = ТаблицаЗатрат.Продукция
	|	И СуммыЗаказов.ХарактеристикаПродукции = ТаблицаЗатрат.ХарактеристикаПродукции
	|";
	
	Запрос.УстановитьПараметр("ТаблицаЗатрат", ТаблицаЗатрат);
	Запрос.УстановитьПараметр("Период", ?(ЗначениеЗаполнено(Период), Период, КонецДня(ТекущаяДатаСеанса())));
	Запрос.УстановитьПараметр("ИспользуетсяЦенообразование25", ЦенообразованиеВызовСервера.ИспользуетсяЦенообразование25());
	Запрос.УстановитьПараметр("БазоваяВалюта", ЗначениеНастроекПовтИсп.БазоваяВалютаПоУмолчанию());
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция НоваяТаблицаЗатрат()
	
	ТипыЗаказДавальца = ТипыЗаказДавальца();
	
	ТаблицаЗатрат = Новый ТаблицаЗначений;
	ТаблицаЗатрат.Колонки.Добавить("ЗаказДавальца",Новый ОписаниеТипов(ТипыЗаказДавальца));
	ТаблицаЗатрат.Колонки.Добавить("Номенклатура",Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаЗатрат.Колонки.Добавить("Характеристика",Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ТаблицаЗатрат.Колонки.Добавить("Продукция",Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаЗатрат.Колонки.Добавить("ХарактеристикаПродукции",Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ТаблицаЗатрат.Колонки.Добавить("КоличествоПоСпецификации",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15,3,ДопустимыйЗнак.Неотрицательный)));
	ТаблицаЗатрат.Колонки.Добавить("КоличествоПредоставляетсяДавальцем",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15,3,ДопустимыйЗнак.Неотрицательный)));
	ТаблицаЗатрат.Колонки.Добавить("ВидЦены",Новый ОписаниеТипов("СправочникСсылка.ВидыЦен"));
	ТаблицаЗатрат.Колонки.Добавить("Валюта",Новый ОписаниеТипов("СправочникСсылка.Валюты"));
	ТаблицаЗатрат.Колонки.Добавить("Спецификация",Новый ОписаниеТипов("СправочникСсылка.РесурсныеСпецификации"));
	
	Возврат ТаблицаЗатрат;
	
КонецФункции

Функция ТипыЗаказДавальца()
	
	ТипыЗаказДавальца = Новый Массив;
	//++ Устарело_Переработка24
	ТипыЗаказДавальца.Добавить(Тип("ДокументСсылка.ЗаказДавальца"));
	//-- Устарело_Переработка24
	ТипыЗаказДавальца.Добавить(Тип("ДокументСсылка.ЗаказДавальца2_5"));
	
	Возврат ТипыЗаказДавальца;
	
КонецФункции

#КонецОбласти

#КонецЕсли

