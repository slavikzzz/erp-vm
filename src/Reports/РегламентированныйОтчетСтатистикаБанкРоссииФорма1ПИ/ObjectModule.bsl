#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Перем ИменаРазделовИТаблицСтр;

Перем ИменаСправочниковИСсылки;

#Область СлужебныеПроцедурыИФункции

#Область ФормированиеРегламентированногоОтчета

Процедура ИнициализацияПеременныхМодуля()
	
	// В каждой группе <Имя раздела>,<Имя таблицы шаблона>.
	ИменаРазделовИТаблицСтр = "Раздел1,Inward;Раздел2,Outward;Раздел3,Fellow;Раздел4,Balance";
	
	ИменаСправочниковИСсылки = Новый СписокЗначений;
	ИменаСправочниковИСсылки.Добавить("OKOPF", "ОКОПФ",     Истина); // ОКОПФ
	ИменаСправочниковИСсылки.Добавить("OKFS",  "ОКФС",      Истина); // ОКФС
	ИменаСправочниковИСсылки.Добавить("OKSM",  "КодыСтран", Истина); // КодыСтран
	ИменаСправочниковИСсылки.Добавить("OKV",   "КодыВалют", Истина); // КодыВалют
	ИменаСправочниковИСсылки.Добавить("OVED",  "КодыВЭД",   Истина); // КодыВЭД
	
	ИменаСправочниковИСсылки.Добавить("RESP", "РезидентнаяПринадлежность", Истина); // РезидентнаяПринадлежность
	
КонецПроцедуры

#КонецОбласти

#Область ВыгрузкаРегламентированногоОтчета

Процедура ПрочитатьМетаданныеШаблона(МакетМетаданныхШаблона, МакетСписковВыбора, МетаданныеШаблона) Экспорт
	
	Если ТипЗнч(МетаданныеШаблона) <> Тип("Структура") Тогда
		МетаданныеШаблона = Новый Структура();
	КонецЕсли;
	
	Для Каждого Область Из МакетМетаданныхШаблона.Области Цикл
		Если Область.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Строки Тогда
			
			ВерхОбласти  = Область.Верх;
			НизОбласти   = Область.Низ;
			ЛевоОбласти  = 1;
			ПравоОбласти = 256;
			
			Если Область.Имя = "Question_Meta" Тогда
				
				ТаблВопросовШаблона = Новый ТаблицаЗначений;
				Для НомКол = ЛевоОбласти По ПравоОбласти Цикл
					ИмяКолонки = СокрЛП(МакетМетаданныхШаблона.Область(ВерхОбласти, НомКол).Текст);
					Если ПустаяСтрока(ИмяКолонки) Тогда
						ПравоОбласти = НомКол - 1;
						Прервать;
					КонецЕсли;
					ТаблВопросовШаблона.Колонки.Добавить(ИмяКолонки, Новый ОписаниеТипов("Строка"));
				КонецЦикла;
				
				Для НомСтр = ВерхОбласти + 1 По НизОбласти Цикл
					ЗначениеОбластиВПервойКолонке = СокрЛП(МакетМетаданныхШаблона.Область(НомСтр, ЛевоОбласти).Текст);
					Если НЕ ПустаяСтрока(ЗначениеОбластиВПервойКолонке) Тогда
						ИндКолонки = 0;
						НовСтрока = ТаблВопросовШаблона.Добавить();
						Для НомКол = ЛевоОбласти По ПравоОбласти Цикл
							ЗначениеОбласти = СокрП(МакетМетаданныхШаблона.Область(НомСтр, НомКол, НомСтр, НомКол).Текст);
							НовСтрока.Установить(ИндКолонки, ЗначениеОбласти);
							ИндКолонки = ИндКолонки + 1;
						КонецЦикла;
					КонецЕсли;
				КонецЦикла;
				
				МетаданныеШаблона.Вставить(Область.Имя, ТаблВопросовШаблона);
				
			ИначеЕсли Область.Имя = "Period_Check_Meta" Тогда
				
				ТаблПроверкиПериодаШаблона = Новый ТаблицаЗначений;
				Для НомКол = ЛевоОбласти По ПравоОбласти Цикл
					ИмяКолонки = СокрЛП(МакетМетаданныхШаблона.Область(ВерхОбласти, НомКол).Текст);
					Если ПустаяСтрока(ИмяКолонки) Тогда
						ПравоОбласти = НомКол - 1;
						Прервать;
					КонецЕсли;
					ТаблПроверкиПериодаШаблона.Колонки.Добавить(ИмяКолонки, Новый ОписаниеТипов("Строка"));
				КонецЦикла;
				
				Для НомСтр = ВерхОбласти + 1 По НизОбласти Цикл
					ЗначениеОбластиВПервойКолонке = СокрЛП(МакетМетаданныхШаблона.Область(НомСтр, ЛевоОбласти).Текст);
					Если НЕ ПустаяСтрока(ЗначениеОбластиВПервойКолонке) Тогда
						ИндКолонки = 0;
						НовСтрока = ТаблПроверкиПериодаШаблона.Добавить();
						Для НомКол = ЛевоОбласти По ПравоОбласти Цикл
							ЗначениеОбласти = СокрП(МакетМетаданныхШаблона.Область(НомСтр, НомКол, НомСтр, НомКол).Текст);
							НовСтрока.Установить(ИндКолонки, ЗначениеОбласти);
							ИндКолонки = ИндКолонки + 1;
						КонецЦикла;
					КонецЕсли;
				КонецЦикла;
				
				МетаданныеШаблона.Вставить(Область.Имя, ТаблПроверкиПериодаШаблона);
				
			ИначеЕсли Область.Имя = "Survey_Meta" Тогда
				
				СписокОпросШаблона = Новый СписокЗначений;
				
				Для НомСтр = ВерхОбласти По НизОбласти Цикл
					ЗначениеОбластиВПервойКолонке = СокрЛП(МакетМетаданныхШаблона.Область(НомСтр, ЛевоОбласти).Текст);
					Если НЕ ПустаяСтрока(ЗначениеОбластиВПервойКолонке) Тогда
						ЗначениеОбластиВоВторойКолонке = СокрП(МакетМетаданныхШаблона.Область(НомСтр, ЛевоОбласти + 1).Текст);
						СписокОпросШаблона.Добавить(ЗначениеОбластиВоВторойКолонке, ЗначениеОбластиВПервойКолонке);
					КонецЕсли;
				КонецЦикла;
				
				МетаданныеШаблона.Вставить(Область.Имя, СписокОпросШаблона);
				
			ИначеЕсли Область.Имя = "KeyValuePair_Meta" Тогда
				
				СправочникиКлючИЗначение = Новый Структура;
				
				Для НомСтр = ВерхОбласти По НизОбласти Цикл
					ЗначениеОбластиВПервойКолонке = СокрЛП(МакетМетаданныхШаблона.Область(НомСтр, ЛевоОбласти).Текст);
					Если НЕ ПустаяСтрока(ЗначениеОбластиВПервойКолонке) Тогда
						ЗначениеОбластиВоВторойКолонке = СокрП(МакетМетаданныхШаблона.Область(НомСтр, ЛевоОбласти + 1).Текст);
						СпрКоординатыОбласти = РегламентированнаяОтчетностьБанкРоссии.КоординатыОбласти(ЗначениеОбластиВоВторойКолонке);
						
						ТаблСправочникКлючИЗначение = Новый ТаблицаЗначений;
						ТаблСправочникКлючИЗначение.Колонки.Добавить("Ключ",     Новый ОписаниеТипов("Строка"));
						ТаблСправочникКлючИЗначение.Колонки.Добавить("Значение", Новый ОписаниеТипов("Строка"));
						Для НомСтрСпр = ВерхОбласти По НизОбласти Цикл
							Ключ = СокрЛП(МакетМетаданныхШаблона.Область(НомСтрСпр, СпрКоординатыОбласти.Столбец1).Текст);
							Значение = СокрЛП(МакетМетаданныхШаблона.Область(НомСтрСпр, СпрКоординатыОбласти.Столбец2).Текст);
							Если НЕ ПустаяСтрока(Ключ) Тогда
								НовСтрока = ТаблСправочникКлючИЗначение.Добавить();
								НовСтрока.Ключ = Ключ;
								НовСтрока.Значение = Значение;
							Иначе
								Прервать;
							КонецЕсли;
						КонецЦикла;
						
						СправочникиКлючИЗначение.Вставить(ЗначениеОбластиВПервойКолонке, ТаблСправочникКлючИЗначение);
					Иначе
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
				МетаданныеШаблона.Вставить(Область.Имя, СправочникиКлючИЗначение);
				
			ИначеЕсли Область.Имя = "NSI_Meta" Тогда
				
				СправочникиКлючИЗначение = Новый Структура;
				
				Для Каждого ЭлементИмяИСсылка Из ИменаСправочниковИСсылки Цикл
					
					ИмяСправочника   = ЭлементИмяИСсылка.Значение;
					СсылкаНаДиапазон = ЭлементИмяИСсылка.Представление;
					
					ТекМакетМетаданных = МакетМетаданныхШаблона;
					ВерхОбластиСпр = ВерхОбласти;
					НизОбластиСпр  = НизОбласти;
					НомерКолонкиКлюч = 1;
					НомерКолонкиЗначение = 2;
					Если ЭлементИмяИСсылка.Пометка Тогда // справочник загружаем из макета "СпискиВыбора*"
						Если МакетСписковВыбора = Неопределено Тогда
							Продолжить;
						КонецЕсли;
						ТекМакетМетаданных = МакетСписковВыбора;
						ОбластьСправочника = ТекМакетМетаданных.Область(СсылкаНаДиапазон);
						ВерхОбластиСпр = ОбластьСправочника.Верх;
						НизОбластиСпр  = ОбластьСправочника.Низ;
					Иначе
						СпрКоординатыОбласти = РегламентированнаяОтчетностьБанкРоссии.КоординатыОбласти(СсылкаНаДиапазон);
						НомерКолонкиКлюч     = СпрКоординатыОбласти.Столбец1;
						НомерКолонкиЗначение = СпрКоординатыОбласти.Столбец2;
					КонецЕсли;
					
					ТаблСправочникКлючИЗначение = Новый ТаблицаЗначений;
					ТаблСправочникКлючИЗначение.Колонки.Добавить("Ключ",     Новый ОписаниеТипов("Строка"));
					ТаблСправочникКлючИЗначение.Колонки.Добавить("Значение", Новый ОписаниеТипов("Строка"));
					
					Для НомСтрСпр = ВерхОбластиСпр + 1 По НизОбластиСпр Цикл
						Ключ = СокрЛП(ТекМакетМетаданных.Область(НомСтрСпр, НомерКолонкиКлюч).Текст);
						Значение = СокрЛП(ТекМакетМетаданных.Область(НомСтрСпр, НомерКолонкиЗначение).Текст);
						Если НЕ ПустаяСтрока(Ключ) Тогда
							НовСтрока = ТаблСправочникКлючИЗначение.Добавить();
							НовСтрока.Ключ = Ключ;
							НовСтрока.Значение = Значение;
						Иначе
							Прервать;
						КонецЕсли;
					КонецЦикла;
					
					СправочникиКлючИЗначение.Вставить(ИмяСправочника, ТаблСправочникКлючИЗначение);
					
				КонецЦикла;
				
				МетаданныеШаблона.Вставить(Область.Имя, СправочникиКлючИЗначение);
				
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	// Создадим таблицу данных листа ответов анкеты.
	ТаблОтветовШаблона = Новый ТаблицаЗначений;
	ТаблОтветовШаблона.Колонки.Добавить("ИДВопроса",      Новый ОписаниеТипов("Строка"));
	ТаблОтветовШаблона.Колонки.Добавить("ЗначениеОтвета", Новый ОписаниеТипов("Строка"));
	ТаблОтветовШаблона.Колонки.Добавить("НомСтрокиЛиста", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(6, 0)));
	ТаблОтветовШаблона.Добавить();
	
	МетаданныеШаблона.Вставить("Answer_Meta", ТаблОтветовШаблона);
	
	// Создадим таблицу данных листа ошибок анкеты.
	ТаблОшибокШаблона = Новый ТаблицаЗначений;
	ТаблОшибокШаблона.Колонки.Добавить("АдресОшибки",    Новый ОписаниеТипов("Строка"));
	ТаблОшибокШаблона.Колонки.Добавить("МестоОшибки",    Новый ОписаниеТипов("Строка"));
	ТаблОшибокШаблона.Колонки.Добавить("ОписаниеОшибки", Новый ОписаниеТипов("Строка"));
	ТаблОшибокШаблона.Добавить();
	
	МетаданныеШаблона.Вставить("Errors_Meta", ТаблОшибокШаблона);
	
КонецПроцедуры

Процедура ПодготовитьТаблицыРазделов(Форма, МетаданныеШаблона, ИменаРазделовИТаблиц)
	
	МетаданныеШаблона.Вставить("ТаблицыРазделов", Новый Структура());
	
	Для Каждого ИмяРазделаИТаблицыСтр Из ИменаРазделовИТаблиц Цикл
		ИмяРазделаИТаблицы = СтрРазделить(ИмяРазделаИТаблицыСтр, ",");
		
		ТаблицаРаздела = Новый ТаблицаЗначений;
		ТаблицаРаздела.Колонки.Добавить("НС", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(7,0)));
		ТаблицаРаздела.Колонки.Добавить("МЧ");
		
		МетаданныеШаблона.ТаблицыРазделов.Вставить(ИмяРазделаИТаблицы[1], ТаблицаРаздела);
		
		НомСтраницы = 0;
		Для Каждого Элемент Из Форма.мДанныеОтчета[ИмяРазделаИТаблицы[0]] Цикл
			
			СтраницаНеПустая = Истина;
			НомСтраницы = НомСтраницы + 1;
			
			Если ТипЗнч(Элемент) <> Тип("КлючИЗначение") Тогда
				НовСтрокаТР = ТаблицаРаздела.Добавить();
				НовСтрокаТР.НС = НомСтраницы;
				НовСтрокаТР.МЧ = ТаблицаРаздела.СкопироватьКолонки();
				
				СтраницаНеПустая = Ложь;
				СписокПоказателей = Новый СписокЗначений;
				Для Каждого ЭлементДанныхТР Из Элемент.Данные Цикл
					СписокПоказателей.Добавить(ЭлементДанныхТР, ЭлементДанныхТР.Ключ);
					Если ЗначениеЗаполнено(ЭлементДанныхТР.Значение) Тогда
						СтраницаНеПустая = Истина;
					КонецЕсли;
				КонецЦикла;
				СписокПоказателей.СортироватьПоПредставлению();
				
				Для Каждого ЭлементСписка Из СписокПоказателей Цикл
					Если ТаблицаРаздела.Колонки.Найти(ЭлементСписка.Значение.Ключ) = Неопределено Тогда
						ТаблицаРаздела.Колонки.Вставить(ТаблицаРаздела.Колонки.Количество() - 1, ЭлементСписка.Значение.Ключ);
					КонецЕсли;
					НовСтрокаТР[ЭлементСписка.Значение.Ключ] = ЭлементСписка.Значение.Значение;
				КонецЦикла;
				
				ЭлементСтруктуры = Неопределено;
				Для Каждого ЭлементМнЧ Из Элемент.ДанныеМногострочныхЧастей Цикл
					ЭлементСтруктуры = ЭлементМнЧ;
					ТаблицаМнЧ = НовСтрокаТР.МЧ;
					Прервать;
				КонецЦикла;
				Если ЭлементСтруктуры = Неопределено Тогда
					Если НЕ СтраницаНеПустая Тогда
						ТаблицаРаздела.Удалить(НовСтрокаТР);
					КонецЕсли;
					Продолжить;
				КонецЕсли;
			Иначе
				ЭлементСтруктуры = Элемент;
				ТаблицаМнЧ = ТаблицаРаздела;
			КонецЕсли;
			
			Для НомСтр = 1 По ЭлементСтруктуры.Значение.Количество() Цикл
				ДанныеСтроки = ЭлементСтруктуры.Значение[НомСтр - 1];
				
				СтрокаНеПустая = Ложь;
				НовСтрока = ТаблицаМнЧ.Добавить();
				Для Каждого ЭлементДанных Из ДанныеСтроки Цикл
					ГрафаПоказателя = СтрЗаменить(ЭлементДанных.Ключ, ЭлементСтруктуры.Ключ, "");
					Если НЕ ПустаяСтрока(ГрафаПоказателя) Тогда
						НомерГрафыПоказателя = Число(" " + ГрафаПоказателя);
						
						КоличествоКолонок = ТаблицаМнЧ.Колонки.Количество();
						Если НомерГрафыПоказателя >= КоличествоКолонок Тогда
							Для НомКол = КоличествоКолонок + 1 По НомерГрафыПоказателя Цикл
								ТаблицаМнЧ.Колонки.Добавить();
							КонецЦикла;
							ТаблицаМнЧ.Колонки.Добавить(ЭлементДанных.Ключ);
						ИначеЕсли ПустаяСтрока(ТаблицаМнЧ.Колонки[НомерГрафыПоказателя].Имя) Тогда
							ТаблицаМнЧ.Колонки[НомерГрафыПоказателя].Имя = ЭлементДанных.Ключ; 
						КонецЕсли;
						
						НовСтрока[НомерГрафыПоказателя] = ЭлементДанных.Значение;
						Если ЗначениеЗаполнено(ЭлементДанных.Значение) Тогда
							СтрокаНеПустая  = Истина;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
				
				Если НЕ СтрокаНеПустая Тогда
					ТаблицаМнЧ.Удалить(НовСтрока);
				Иначе
					НовСтрока.НС = НомСтр;
				КонецЕсли;
			КонецЦикла;
			
			Если НЕ СтраницаНеПустая И ТаблицаРаздела.Количество() > 0 И ТаблицаМнЧ.Количество() = 0 Тогда
				ТаблицаРаздела.Удалить(ТаблицаРаздела.Количество() - 1);
			КонецЕсли;
			
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Функция ВыполнитьПроверкуДанных(Форма, ПараметрыВыгрузки, ЕстьПредупреждения = Ложь) Экспорт
	
	СтатусПроверки = Истина;
	
	ИменаРазделовИТаблиц = СтрРазделить(ИменаРазделовИТаблицСтр, ";");
	
	ТаблВопросы = ПараметрыВыгрузки.МетаданныеШаблона["Question_Meta"];
	
	// Подготовка таблиц для обработки и контроля, добавление их в метаданные шаблона.
	ПодготовитьТаблицыРазделов(Форма, ПараметрыВыгрузки.МетаданныеШаблона, ИменаРазделовИТаблиц);
	
	// Обработаем вопросы анкеты для титульного листа.
	СтруктураДанныхТитульный = Форма.мДанныеОтчета.Титульный;
	ВопросыТитульный = ТаблВопросы.НайтиСтроки(Новый Структура("QUESTION_TYPE", "HEADER"));
	Для Каждого ТекВопрос Из ВопросыТитульный Цикл
		ИмяПоказателя = ТекВопрос["QUESTION_NAME_ID"];
		ЗначениеПоказателя = Неопределено;
		Если СтруктураДанныхТитульный.Свойство(ИмяПоказателя, ЗначениеПоказателя) Тогда
			Описание = "";
			РезультатПроверки = РегламентированнаяОтчетностьБанкРоссии.ВыполнитьПроверкуЗначения(
				Форма, ПараметрыВыгрузки, Описание, ТекВопрос, ЗначениеПоказателя);
			Если РезультатПроверки > 0 Тогда
				Если РезультатПроверки = 1 Тогда // Предупреждение
					ЕстьПредупреждения = Истина;
				ИначеЕсли РезультатПроверки = 2 Тогда // Ошибка
					СтатусПроверки = Ложь;
				КонецЕсли;
				
				МестоОшибки = "Титульный лист" + ": """ + ТекВопрос["QUESTION_TEXT"] + """";
				ТекстОшибки = Описание + Символы.ПС + Символы.Таб + "<" + МестоОшибки + ">";
				РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
					Форма, ТекстОшибки, "Титульный", ИмяПоказателя);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	// Обновим необходимые данные опроса, если существует поле "NamePeriod".
	Если СтруктураДанныхТитульный.Свойство("NamePeriod") Тогда
		ОбновитьДанныеОпросаПоПериоду(ПараметрыВыгрузки);
	КонецЕсли;
	
	// Обработаем вопросы анкеты для листов с таблицами.
	Для Каждого ИмяРазделаИТаблицыСтр Из ИменаРазделовИТаблиц Цикл
		
		ИмяРазделаИТаблицы = СтрРазделить(ИмяРазделаИТаблицыСтр, ",");
		
		ВопросыРаздела = ТаблВопросы.НайтиСтроки(
			Новый Структура("QUESTION_TYPE,QUESTION_ENTITY", "TABLE", ИмяРазделаИТаблицы[1]));
		
		ТаблицаРаздела = ПараметрыВыгрузки.МетаданныеШаблона.ТаблицыРазделов[ИмяРазделаИТаблицы[1]]; 
		КоличествоКолонокТаблицы = ТаблицаРаздела.Колонки.Количество();
		
		Для НомСтр = 1 По ТаблицаРаздела.Количество() Цикл
			ДанныеСтрокиТаблицы = ТаблицаРаздела[НомСтр - 1];
			СтрНомСтроки = Формат(ДанныеСтрокиТаблицы.НС, "ЧГ=");
			
			ВопросыРазделаИЗначения = Новый Соответствие;
			Для НомВопроса = 1 По ВопросыРаздела.Количество() Цикл
				ТекВопрос = ВопросыРаздела[НомВопроса - 1];
				
				НомКолонки = Число(" " + ТекВопрос["QUESTION_SORT"]);
				НомКолонки = ?(НомКолонки > 0, НомКолонки, НомВопроса);
				
				Если КоличествоКолонокТаблицы > НомКолонки Тогда
					Описание = "";
					ЗначениеОтвета = ДанныеСтрокиТаблицы[НомКолонки];
					
					РезультатПроверки = РегламентированнаяОтчетностьБанкРоссии.ВыполнитьПроверкуЗначения(
						Форма, ПараметрыВыгрузки, Описание, ТекВопрос, ЗначениеОтвета, НомСтр);
					Если РезультатПроверки > 0 Тогда
						Если РезультатПроверки = 1 Тогда // Предупреждение
							ЕстьПредупреждения = Истина;
						ИначеЕсли РезультатПроверки = 2 Тогда // Ошибка
							СтатусПроверки = Ложь;
						КонецЕсли;
						
						МестоОшибки = "Раздел " + Прав(ИмяРазделаИТаблицы[0], 1)
							+ " [строка " + СтрНомСтроки  + "]: """ + ТекВопрос["QUESTION_TEXT"] + """";
						ТекстОшибки = Описание + Символы.ПС + Символы.Таб + "<" + МестоОшибки + ">";
						РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(Форма, ТекстОшибки, ИмяРазделаИТаблицы[0],
							ТаблицаРаздела.Колонки[НомКолонки].Имя + "_" + СтрНомСтроки);
					КонецЕсли;
					
					// Преобразовываем необязательные к заполнению числовые значения в зависимости от ограничений.
					Если РегламентированнаяОтчетностьБанкРоссии.ПреобразоватьЗначениеЕслиНеобходимо(ЗначениеОтвета, ТекВопрос) Тогда
						ДанныеСтрокиТаблицы[НомКолонки] = ЗначениеОтвета;
					КонецЕсли;
					
					ВопросыРазделаИЗначения.Вставить(ТекВопрос["QUESTION_NAME_ID"],
						Новый Структура("ИмяКолонки,Значение,Результат,Вопрос",
							ТаблицаРаздела.Колонки[НомКолонки].Имя, ЗначениеОтвета, РезультатПроверки, ТекВопрос));
				КонецЕсли;
			КонецЦикла;
			
			РезультатПроверки = ВыполнитьПроверкуСтроки(Форма, ИмяРазделаИТаблицы,
				ТаблицаРаздела, ВопросыРазделаИЗначения, ПараметрыВыгрузки, НомСтр);
			Если РезультатПроверки = 1 Тогда      // предупреждения
				ЕстьПредупреждения = Истина;
			ИначеЕсли РезультатПроверки = 2 Тогда // ошибки
				СтатусПроверки = Ложь;
			ИначеЕсли РезультатПроверки = 3 Тогда // ошибки и предупреждения
				ЕстьПредупреждения = Истина;
				СтатусПроверки = Ложь;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат СтатусПроверки;
	
КонецФункции

Функция ВыполнитьПроверкуСтроки(Форма, ИмяРазделаИТаблицы,
			ТаблицаРаздела, ВопросыРазделаИЗначения, ПараметрыВыгрузки, НомСтр)
	
	РезультатПроверки = 0;
	
	ДанныеСтрокиТаблицы = ТаблицаРаздела[НомСтр - 1];
	СтрНомСтраницы = Формат(ДанныеСтрокиТаблицы.НС, "ЧГ=");
	
	// Проверка строки раздела 1.
	Если ИмяРазделаИТаблицы[1] = "Inward" Тогда
	// Проверка строки раздела 2.
	ИначеЕсли ИмяРазделаИТаблицы[1] = "Outward" Тогда
	КонецЕсли;
	
	Возврат РезультатПроверки;
	
КонецФункции

Процедура ВыполнитьПроверкуЗначения(РезультатПроверки, Описание,
									ВопросАнкеты,      ПараметрыВыгрузки,
									ЗначениеОтвета,    НомСтроки = 0) Экспорт
	
	Если СтрДлина(ЗначениеОтвета) = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ИмяПоказателя = ВопросАнкеты["QUESTION_NAME_ID"];
	
	Если РезультатПроверки > 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Проверка титульного листа.
	Если ИмяПоказателя = "inn" Тогда
		Если НЕ РегламентированнаяОтчетностьБанкРоссии.ИННСоответствуетТребованиям(ЗначениеОтвета) Тогда
			Описание = НСтр("ru = 'Некорректный код, проверьте правильность ввода!';
							|en = 'Некорректный код, проверьте правильность ввода!'");
			РезультатПроверки = 2;
		КонецЕсли;
	ИначеЕсли ИмяПоказателя = "okpo" Тогда
		Если НЕ РегламентированнаяОтчетностьБанкРоссии.КодПоОКПОСоответствуетТребованиям(ЗначениеОтвета) Тогда
			Описание = НСтр("ru = 'Некорректный код, проверьте правильность ввода!';
							|en = 'Некорректный код, проверьте правильность ввода!'");
			РезультатПроверки = 2;
		КонецЕсли;
	ИначеЕсли ИмяПоказателя = "ogrn" Тогда
		Если НЕ РегламентированнаяОтчетностьБанкРоссии.ОГРНСоответствуетТребованиям(ЗначениеОтвета) Тогда
			Описание = НСтр("ru = 'Некорректный код, проверьте правильность ввода!';
							|en = 'Некорректный код, проверьте правильность ввода!'");
			РезультатПроверки = 2;
		КонецЕсли;
	ИначеЕсли ИмяПоказателя = "executor" Тогда
		Если НЕ РегламентированнаяОтчетностьБанкРоссии.НетЦифрВСтроке(ЗначениеОтвета) Тогда
			Описание = НСтр("ru = 'Значение не должно содержать цифры';
							|en = 'Значение не должно содержать цифры'");
			РезультатПроверки = 1;
		КонецЕсли;
	ИначеЕсли ИмяПоказателя = "signer" Тогда
		Если НЕ РегламентированнаяОтчетностьБанкРоссии.НетЦифрВСтроке(ЗначениеОтвета) Тогда
			Описание = НСтр("ru = 'Значение не должно содержать цифры';
							|en = 'Значение не должно содержать цифры'");
			РезультатПроверки = 1;
		КонецЕсли;
	ИначеЕсли ИмяПоказателя = "email" Тогда
		Если НЕ РегламентированнаяОтчетностьБанкРоссии.АдресЭлектроннойПочтыСоответствуетТребованиям(ЗначениеОтвета) Тогда
			Описание = НСтр("ru = 'Некорректный адрес эл. почты';
							|en = 'Некорректный адрес эл. почты'");
			РезультатПроверки = 1;
		КонецЕсли;
	ИначеЕсли ИмяПоказателя = "lkbrEmail" Тогда
		Если НЕ РегламентированнаяОтчетностьБанкРоссии.АдресЭлектроннойПочтыСоответствуетТребованиям(ЗначениеОтвета) Тогда
			Описание = НСтр("ru = 'Некорректный адрес эл. почты';
							|en = 'Некорректный адрес эл. почты'");
			РезультатПроверки = 1;
		КонецЕсли;
	ИначеЕсли ИмяПоказателя = "website" Тогда
		Если НЕ РегламентированнаяОтчетностьБанкРоссии.АдресВебСервераСоответствуетТребованиям(ЗначениеОтвета) Тогда
			Описание = НСтр("ru = 'Некорректный адрес веб сайта';
							|en = 'Некорректный адрес веб сайта'");
			РезультатПроверки = 1;
		КонецЕсли;
	ИначеЕсли ИмяПоказателя = "phone" Тогда
		Если НЕ РегламентированнаяОтчетностьБанкРоссии.НомерТелефонаСоответствуетТребованиям(ЗначениеОтвета) Тогда
			Описание = НСтр("ru = 'Недопустимые символы в номере телефона';
							|en = 'Недопустимые символы в номере телефона'");
			РезультатПроверки = 2;
		КонецЕсли;
	ИначеЕсли ИмяПоказателя = "fax" Тогда
		Если НЕ РегламентированнаяОтчетностьБанкРоссии.НомерТелефонаСоответствуетТребованиям(ЗначениеОтвета) Тогда
			Описание = НСтр("ru = 'Недопустимые символы в номере Факса';
							|en = 'Недопустимые символы в номере Факса'");
			РезультатПроверки = 1;
		КонецЕсли;
	ИначеЕсли ИмяПоказателя = "inward" Тогда
		ТаблицаРаздела1 = ПараметрыВыгрузки.МетаданныеШаблона.ТаблицыРазделов["Inward"];
		Если ЗначениеОтвета = "Нулевой" Тогда
			Если ТаблицаРаздела1.Количество() > 0 Тогда
				Описание = НСтр("ru = 'В разделе 1 присутствуют данные';
								|en = 'В разделе 1 присутствуют данные'");
				РезультатПроверки = 2;
			КонецЕсли;
		Иначе
			Если ТаблицаРаздела1.Количество() = 0 Тогда
				Описание = НСтр("ru = 'В разделе 1 отсутствуют данные';
								|en = 'В разделе 1 отсутствуют данные'");
				РезультатПроверки = 2;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ИмяПоказателя = "outward" Тогда
		ТаблицаРаздела2 = ПараметрыВыгрузки.МетаданныеШаблона.ТаблицыРазделов["Outward"];
		Если ЗначениеОтвета = "Нулевой" Тогда
			Если ТаблицаРаздела2.Количество() > 0 Тогда
				Описание = НСтр("ru = 'В разделе 2 присутствуют данные';
								|en = 'В разделе 2 присутствуют данные'");
				РезультатПроверки = 2;
			КонецЕсли;
		Иначе
			Если ТаблицаРаздела2.Количество() = 0 Тогда
				Описание = НСтр("ru = 'В разделе 2 отсутствуют данные';
								|en = 'В разделе 2 отсутствуют данные'");
				РезультатПроверки = 2;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ИмяПоказателя = "fellow" Тогда
		ТаблицаРаздела2 = ПараметрыВыгрузки.МетаданныеШаблона.ТаблицыРазделов["Fellow"];
		Если ЗначениеОтвета = "Нулевой" Тогда
			Если ТаблицаРаздела2.Количество() > 0 Тогда
				Описание = НСтр("ru = 'В разделе 3 присутствуют данные';
								|en = 'В разделе 3 присутствуют данные'");
				РезультатПроверки = 2;
			КонецЕсли;
		Иначе
			Если ТаблицаРаздела2.Количество() = 0 Тогда
				Описание = НСтр("ru = 'В разделе 3 отсутствуют данные';
								|en = 'В разделе 3 отсутствуют данные'");
				РезультатПроверки = 2;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ИмяПоказателя = "accountingInfo" Тогда
		ТаблицаРаздела2 = ПараметрыВыгрузки.МетаданныеШаблона.ТаблицыРазделов["Balance"];
		Если ЗначениеОтвета = "Нулевой" Тогда
			Если ТаблицаРаздела2.Количество() > 0 Тогда
				Описание = НСтр("ru = 'В разделе 4 присутствуют данные';
								|en = 'В разделе 4 присутствуют данные'");
				РезультатПроверки = 2;
			КонецЕсли;
		Иначе
			Если ТаблицаРаздела2.Количество() = 0 Тогда
				Описание = НСтр("ru = 'В разделе 4 отсутствуют данные';
								|en = 'В разделе 4 отсутствуют данные'");
				РезультатПроверки = 2;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьДанныеОпросаПоПериоду(ПараметрыВыгрузки)
	
	КодПериода = "0";
	ГодПериода = "0";
	
	КодПериодичности = Число(" " + РегламентированнаяОтчетностьБанкРоссии.ЗначениеДанныхОпросаПоКлючу(
		ПараметрыВыгрузки, "PERIODTYPE"));
	
	Если КодПериодичности > 0 Тогда
		КодПериода = Строка(ПараметрыВыгрузки.ОтчетКвартал);
		ГодПериода = Формат(ПараметрыВыгрузки.ОтчетГод, "ЧГ=");
	КонецЕсли;
	
	РегламентированнаяОтчетностьБанкРоссии.ЗаписатьДанныеОпросаПоКлючу(
		ПараметрыВыгрузки, "PERIODVALUE", КодПериода);
	РегламентированнаяОтчетностьБанкРоссии.ЗаписатьДанныеОпросаПоКлючу(
		ПараметрыВыгрузки, "PERIODYEAR", ГодПериода);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

ИнициализацияПеременныхМодуля();

#КонецЕсли