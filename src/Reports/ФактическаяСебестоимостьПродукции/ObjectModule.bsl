
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - см. ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.События.ПриСозданииНаСервере = Истина;
	Настройки.События.ПередЗагрузкойНастроекВКомпоновщик = Истина;
	
КонецПроцедуры

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//   ЭтаФорма - ФормаКлиентскогоПриложения - Форма отчета.
//   Отказ - Булево - Передается из параметров обработчика "как есть".
//   СтандартнаяОбработка - Булево - Передается из параметров обработчика "как есть".
Процедура ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт
	
	Параметры = ЭтаФорма.Параметры;
	
	Если Параметры.Свойство("ПараметрКоманды")
			И Параметры.Свойство("ОписаниеКоманды")
			И Параметры.ОписаниеКоманды.Свойство("ДополнительныеПараметры") Тогда 
			
		СтруктураОтборов = Новый Структура("Регистратор", Параметры.ПараметрКоманды);
		ЭтаФорма.ФормаПараметры.Отбор = СтруктураОтборов;
		
	КонецЕсли;
	
КонецПроцедуры


Процедура ПередЗагрузкойНастроекВКомпоновщик(Контекст, КлючСхемы, КлючВарианта, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД) Экспорт
	
	Если ТипЗнч(Контекст) = Тип("ФормаКлиентскогоПриложения") Тогда
		НастроитьПараметрыОтчетаПоВариантуОтчета(КлючВарианта, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД);
		НастроитьПараметрыОтборыПоФункциональнымОпциям(НовыеНастройкиКД);
		ОтчетыСервер.ПодключитьСхему(ЭтотОбъект, Контекст, СхемаКомпоновкиДанных, КлючСхемы);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	
	ДоступныеПоляОтбора = Новый Массив;
	ДоступныеПоляУсловий = Новый Массив;
	
	СхемаРазузлования = Отчеты.ДеревоСебестоимостиПродукции.ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	НастройкиРазузлования = СхемаРазузлования.НастройкиПоУмолчанию;
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	
	НаборДанныхРазузловки = СхемаРазузлования.НаборыДанных.Найти("ВыпущеннаяПродукция");
	Если НаборДанныхРазузловки = Неопределено Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Изменилась схема разузлования продукции. Разузлование невозможно.';
				|en = 'Product explosion scheme is changed. Cannot explode.'"));
		Возврат;
		
	КонецЕсли;	
	
	ПараметрПериод = НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Период"));
	Период = ПараметрПериод.Значение; // СтандартныйПериод
	НачалоПериода = ?(ПараметрПериод.Использование, Период.ДатаНачала, НачалоГода(ТекущаяДатаСеанса()));
	КонецПериода = ?(ПараметрПериод.Использование, Период.ДатаОкончания, ТекущаяДатаСеанса());
	
	ДанныеПоСебестоимости = НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ДанныеПоСебестоимости")).Значение;
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(НастройкиРазузлования, "ДанныеПоСебестоимости", ДанныеПоСебестоимости);
	
	Если ПараметрПериод.Использование Тогда
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(НастройкиРазузлования, "Период", Период);
	КонецЕсли;
	
	Для Каждого ДоступноеПолеОтбора Из НастройкиОтчета.Отбор.ДоступныеПоляОтбора.Элементы Цикл
		ДоступныеПоляОтбора.Добавить(ДоступноеПолеОтбора.Поле);
	КонецЦикла;
	
	Для Каждого ПолеДанных Из НаборДанныхРазузловки.Поля Цикл
		
		Если ПолеДанных.ОграничениеИспользования.Условие Тогда
			Продолжить;
		КонецЕсли;
		
		ДоступныеПоляУсловий.Добавить(Новый ПолеКомпоновкиДанных(ПолеДанных.Поле));
		
	КонецЦикла;
	
	Накладные = Неопределено;
		Для Каждого ЭлементПараметров Из НастройкиОтчета.ПараметрыДанных.Элементы Цикл
		Если ЭлементПараметров.Параметр = Новый ПараметрКомпоновкиДанных("Регистратор")
			И ЭлементПараметров.Использование Тогда
			
			Накладные = ЭлементПараметров.Значение;
		КонецЕсли;
	КонецЦикла;
	
	Если Не Накладные = Неопределено Тогда
		
		ПараметрРегистратор = СхемаРазузлования.Параметры.Найти("Регистраторы");
		Если Не ПараметрРегистратор = Неопределено Тогда
			ПараметрРегистратор.Значение = Накладные;
		КонецЕсли;
				
	КонецЕсли;
		
	НастройкиРазузлования.Отбор.Элементы.Очистить();
	ЗаполнитьНастройкиОтчетаРекурсивно(НастройкиРазузлования.Отбор, НастройкиОтчета.Отбор, ДоступныеПоляУсловий);
	
	ПараметрыДереваСебестоимости = СтруктураСебестоимости.ПараметрыДереваСебестоимости();
	ПараметрыДереваСебестоимости.ДинамическоеСчитывание = Ложь;
	ПараметрыДереваСебестоимости.ТипРезультата = "МенеджерВременныхТаблиц";
	ПараметрыДереваСебестоимости.Результат = МенеджерВТ;
	
	ПараметрыУзлаДереваСебестоимости = СтруктураСебестоимости.ПараметрыУзлаДереваСебестоимости();
	ПараметрыУзлаДереваСебестоимости.ИнтерактивнаяНастройка = Истина;
	ПараметрыУзлаДереваСебестоимости.СхемаКД = СхемаРазузлования;
	ПараметрыУзлаДереваСебестоимости.НастройкиКД = НастройкиРазузлования;
	ПараметрыУзлаДереваСебестоимости.Отборы.ДанныеПоСебестоимости = ДанныеПоСебестоимости;
	ПараметрыУзлаДереваСебестоимости.Отборы.РазворачиватьДопРасходы = Истина;
	ПараметрыУзлаДереваСебестоимости.Отборы.ДатаОкончания = КонецПериода;
	
	СтруктураСебестоимости.ПостроитьДеревоСебестоимости(ПараметрыДереваСебестоимости, ПараметрыУзлаДереваСебестоимости);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	
	Запрос.УстановитьПараметр("Накладные", Накладные);
	Запрос.УстановитьПараметр("ДанныеПоСебестоимости", ДанныеПоСебестоимости);
	Запрос.УстановитьПараметр("ВалютаУправленческогоУчета", Константы.ВалютаУправленческогоУчета.Получить());
	Запрос.УстановитьПараметр("ДатаОкончания", КонецПериода);
	
		УстановленОтборПоОрганизации = ОтчетыУТКлиентСервер.ПолучитьЗначениеОтбора(
		КомпоновщикНастроек, "Организация", , Истина) <> Неопределено;
	
#Область ТекстЗапроса
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	АналитикаУчетаНоменклатуры.Номенклатура КАК Продукция,
		|	АналитикаУчетаНоменклатуры.Характеристика КАК ХарактеристикаПродукции,
		|	АналитикаУчетаНоменклатуры.Назначение КАК Назначение,
		|	Результат.ПартияПродукции КАК ПартияПродукции,
		|	Результат.ПартияЗатрата КАК ПартияПолуфабриката,
		|	Результат.Номенклатура КАК Полуфабрикат,
		|	Результат.Характеристика КАК ХарактеристикаПолуфабриката,
		|	ВЫБОР
		|		КОГДА Результат.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.ВидыСтрокДереваСебестоимости.ПартияПродукции)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоПродукция,
		|	СУММА(Результат.Количество) КАК Количество,
		|	Результат.АналитикаУчетаПродукции КАК АналитикаУчетаПродукции,
		|	Результат.Организация КАК Организация,
		|	Результат.Организация.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета,
		|	АналитикаУчетаНоменклатуры.Серия КАК Серия,
		|	СУММА(ВЫБОР
		|			КОГДА Результат.ТребуетсяРазузлование
		|				ТОГДА 0
		|			ИНАЧЕ Результат.Сумма
		|		КОНЕЦ) КАК Сумма,
		|	СУММА(ВЫБОР
		|			КОГДА Результат.ТребуетсяРазузлование
		|				ТОГДА 0
		|			ИНАЧЕ Результат.Материальные
		|		КОНЕЦ) КАК Материальные,
		|	СУММА(ВЫБОР
		|			КОГДА Результат.ТребуетсяРазузлование
		|			 ИЛИ Результат.ТребуетсяРазузлованиеДопРасходов
		|				ТОГДА 0
		|			ИНАЧЕ Результат.ДопРасходы
		|		КОНЕЦ) КАК ДопРасходы,
		|	СУММА(ВЫБОР
		|			КОГДА Результат.ТребуетсяРазузлование
		|				ТОГДА 0
		|			ИНАЧЕ Результат.Трудозатраты
		|		КОНЕЦ) КАК Трудозатраты,
		|	СУММА(ВЫБОР
		|			КОГДА Результат.ТребуетсяРазузлование
		|				ТОГДА 0
		|			ИНАЧЕ Результат.ПостатейныеПостоянные
		|		КОНЕЦ) КАК ПостатейныеПостоянные,
		|	СУММА(ВЫБОР
		|			КОГДА Результат.ТребуетсяРазузлование
		|				ТОГДА 0
		|			ИНАЧЕ Результат.ПостатейныеПеременные
		|		КОНЕЦ) КАК ПостатейныеПеременные,
		|	СУММА(ВЫБОР
		|			КОГДА Результат.ТребуетсяРазузлование
		|				ТОГДА 0
		|			ИНАЧЕ Результат.СуммаЗабалансовая
		|		КОНЕЦ) КАК Забалансовая,
		|	СУММА(ВЫБОР
		|			КОГДА Результат.ТребуетсяРазузлование
		|				ТОГДА 0
		|			ИНАЧЕ Результат.НалоговыйУчет
		|		КОНЕЦ) КАК НалоговыйУчет,
		|	СУММА(ВЫБОР
		|			КОГДА Результат.ТребуетсяРазузлование
		|				ТОГДА 0
		|			ИНАЧЕ Результат.ПостояннаяРазница
		|		КОНЕЦ) КАК ПостояннаяРазница,
		|	СУММА(ВЫБОР
		|			КОГДА Результат.ТребуетсяРазузлование
		|				ТОГДА 0
		|			ИНАЧЕ Результат.ВременнаяРазница
		|		КОНЕЦ) КАК ВременнаяРазница,
		|	СУММА(ВЫБОР
		|			КОГДА Результат.ТребуетсяРазузлование
		|				ТОГДА 0
		|			ИНАЧЕ Результат.НДД
		|		КОНЕЦ) КАК НДД
		|ПОМЕСТИТЬ КоличествоВыпуска
		|ИЗ
		|	Результат КАК Результат
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
		|		ПО Результат.АналитикаУчетаПродукции = АналитикаУчетаНоменклатуры.Ссылка
		|ГДЕ
		|	Результат.ТребуетсяРазузлование
		|	ИЛИ Результат.ТребуетсяРазузлованиеДопРасходов И Результат.ДопРасходы <> 0
		|	ИЛИ Результат.ВидСтроки В (
		|		ЗНАЧЕНИЕ(Перечисление.ВидыСтрокДереваСебестоимости.ПартияПродукции),
		|		ЗНАЧЕНИЕ(Перечисление.ВидыСтрокДереваСебестоимости.ПартияПолуфабриката))
		|
		|СГРУППИРОВАТЬ ПО
		|	Результат.Номенклатура,
		|	Результат.Характеристика,
		|	АналитикаУчетаНоменклатуры.Номенклатура,
		|	АналитикаУчетаНоменклатуры.Характеристика,
		|	ВЫБОР
		|		КОГДА Результат.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.ВидыСтрокДереваСебестоимости.ПартияПродукции)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ,
		|	Результат.АналитикаУчетаПродукции,
		|	АналитикаУчетаНоменклатуры.Назначение,
		|	Результат.ПартияЗатрата,
		|	Результат.ПартияПродукции,
		|	Результат.Организация,
		|	Результат.Организация.ВалютаРегламентированногоУчета,
		|	АналитикаУчетаНоменклатуры.Серия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КоличествоВыпуска.Продукция КАК Продукция,
		|	КоличествоВыпуска.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	КоличествоВыпуска.Назначение КАК Назначение,
		|	КоличествоВыпуска.ПартияПродукции КАК ПартияПродукции,
		|	СУММА(КоличествоВыпуска.Количество) КАК Количество,
		|	КоличествоВыпуска.АналитикаУчетаПродукции КАК АналитикаУчетаПродукции,
		|	КоличествоВыпуска.Организация КАК Организация,
		|	КоличествоВыпуска.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета,
		|	КоличествоВыпуска.Серия КАК Серия,
		|	СУММА(КоличествоВыпуска.Сумма) КАК Сумма,
		|	СУММА(КоличествоВыпуска.Материальные) КАК Материальные,
		|	СУММА(КоличествоВыпуска.ДопРасходы) КАК ДопРасходы,
		|	СУММА(КоличествоВыпуска.Трудозатраты) КАК Трудозатраты,
		|	СУММА(КоличествоВыпуска.ПостатейныеПостоянные) КАК ПостатейныеПостоянные,
		|	СУММА(КоличествоВыпуска.ПостатейныеПеременные) КАК ПостатейныеПеременные,
		|	СУММА(КоличествоВыпуска.Забалансовая) КАК Забалансовая,
		|	СУММА(КоличествоВыпуска.НалоговыйУчет) КАК НалоговыйУчет,
		|	СУММА(КоличествоВыпуска.ПостояннаяРазница) КАК ПостояннаяРазница,
		|	СУММА(КоличествоВыпуска.ВременнаяРазница) КАК ВременнаяРазница,
		|	СУММА(КоличествоВыпуска.НДД) КАК НДД
		|ПОМЕСТИТЬ ВТПродукция
		|ИЗ
		|	КоличествоВыпуска КАК КоличествоВыпуска
		|ГДЕ
		|	КоличествоВыпуска.ЭтоПродукция
		|
		|СГРУППИРОВАТЬ ПО
		|	КоличествоВыпуска.Продукция,
		|	КоличествоВыпуска.АналитикаУчетаПродукции,
		|	КоличествоВыпуска.Назначение,
		|	КоличествоВыпуска.ПартияПродукции,
		|	КоличествоВыпуска.ХарактеристикаПродукции,
		|	КоличествоВыпуска.Организация,
		|	КоличествоВыпуска.ВалютаРегламентированногоУчета,
		|	КоличествоВыпуска.Серия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.АналитикаУчетаВыходноеИзделие КАК АналитикаУчетаВыходноеИзделие,
		|	ВложенныйЗапрос.Количество КАК Количество,
		|	ВложенныйЗапрос.Сумма КАК Сумма,
		|	ВложенныйЗапрос.Материальные КАК Материальные,
		|	ВложенныйЗапрос.ДопРасходы КАК ДопРасходы,
		|	ВложенныйЗапрос.Трудозатраты КАК Трудозатраты,
		|	ВложенныйЗапрос.ПостатейныеПостоянные КАК ПостатейныеПостоянные,
		|	ВложенныйЗапрос.ПостатейныеПеременные КАК ПостатейныеПеременные,
		|	ВложенныйЗапрос.Забалансовая КАК Забалансовая,
		|	ВложенныйЗапрос.НалоговыйУчет КАК НалоговыйУчет,
		|	ВложенныйЗапрос.ПостояннаяРазница КАК ПостояннаяРазница,
		|	ВложенныйЗапрос.ВременнаяРазница КАК ВременнаяРазница,
		|	ВложенныйЗапрос.НДД КАК НДД,
		|	ВложенныйЗапрос.Затрата КАК Затрата,
		|	ВложенныйЗапрос.ХарактеристикаЗатраты КАК ХарактеристикаЗатраты,
		|	ВложенныйЗапрос.Полуфабрикат КАК Полуфабрикат,
		|	ВложенныйЗапрос.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
		|	ВложенныйЗапрос.АналитикаУчетаПродукции КАК АналитикаУчетаПродукции,
		|	ВложенныйЗапрос.ВидСтроки КАК ВидСтроки,
		|	ВложенныйЗапрос.ПартияПолуфабриката КАК ПартияПолуфабриката,
		|	ВложенныйЗапрос.ПартияПродукции КАК ПартияПродукции,
		|	ВложенныйЗапрос.ЭтоПолуфабрикат КАК ЭтоПолуфабрикат,
		|	ВложенныйЗапрос.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ВложенныйЗапрос.Организация КАК Организация,
		|	ВложенныйЗапрос.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета,
		|	ВложенныйЗапрос.Подразделение КАК Подразделение,
		|	ВложенныйЗапрос.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	ВложенныйЗапрос.ПартияЗатрата КАК ПартияЗатрата
		|ПОМЕСТИТЬ Затраты
		|ИЗ
		|	(ВЫБРАТЬ
		|		Результат.АналитикаУчетаВыходноеИзделие КАК АналитикаУчетаВыходноеИзделие,
		|		СУММА(Результат.Количество) КАК Количество,
		|		СУММА(ВЫБОР
		|				КОГДА Результат.ТребуетсяРазузлование
		|					ТОГДА 0
		|				КОГДА Результат.ТребуетсяРазузлованиеДопРасходов
		|					ТОГДА Результат.Сумма - Результат.ДопРасходы
		|				ИНАЧЕ Результат.Сумма
		|			КОНЕЦ) КАК Сумма,
		|		СУММА(ВЫБОР
		|				КОГДА Результат.ТребуетсяРазузлование
		|					ТОГДА 0
		|				ИНАЧЕ Результат.Материальные
		|			КОНЕЦ) КАК Материальные,
		|		СУММА(ВЫБОР
		|				КОГДА Результат.ТребуетсяРазузлование
		|				 ИЛИ Результат.ТребуетсяРазузлованиеДопРасходов
		|					ТОГДА 0
		|				ИНАЧЕ Результат.ДопРасходы
		|			КОНЕЦ) КАК ДопРасходы,
		|		СУММА(ВЫБОР
		|				КОГДА Результат.ТребуетсяРазузлование
		|					ТОГДА 0
		|				ИНАЧЕ Результат.Трудозатраты
		|			КОНЕЦ) КАК Трудозатраты,
		|		СУММА(ВЫБОР
		|				КОГДА Результат.ТребуетсяРазузлование
		|					ТОГДА 0
		|				ИНАЧЕ Результат.ПостатейныеПостоянные
		|			КОНЕЦ) КАК ПостатейныеПостоянные,
		|		СУММА(ВЫБОР
		|				КОГДА Результат.ТребуетсяРазузлование
		|					ТОГДА 0
		|				ИНАЧЕ Результат.ПостатейныеПеременные
		|			КОНЕЦ) КАК ПостатейныеПеременные,
		|		СУММА(ВЫБОР
		|				КОГДА Результат.ТребуетсяРазузлование
		|					ТОГДА 0
		|				ИНАЧЕ Результат.СуммаЗабалансовая
		|			КОНЕЦ) КАК Забалансовая,
		|		СУММА(ВЫБОР
		|				КОГДА Результат.ТребуетсяРазузлование
		|					ТОГДА 0
		|				ИНАЧЕ Результат.НалоговыйУчет
		|			КОНЕЦ) КАК НалоговыйУчет,
		|		СУММА(ВЫБОР
		|				КОГДА Результат.ТребуетсяРазузлование
		|					ТОГДА 0
		|				ИНАЧЕ Результат.ПостояннаяРазница
		|			КОНЕЦ) КАК ПостояннаяРазница,
		|		СУММА(ВЫБОР
		|				КОГДА Результат.ТребуетсяРазузлование
		|					ТОГДА 0
		|				ИНАЧЕ Результат.ВременнаяРазница
		|			КОНЕЦ) КАК ВременнаяРазница,
		|		СУММА(ВЫБОР
		|				КОГДА Результат.ТребуетсяРазузлование
		|					ТОГДА 0
		|				ИНАЧЕ Результат.НДД
		|			КОНЕЦ) КАК НДД,
		|		Результат.Номенклатура КАК Затрата,
		|		Результат.Характеристика КАК ХарактеристикаЗатраты,
		|		АналитикаУчетаПолуфабриката.Номенклатура КАК Полуфабрикат,
		|		АналитикаУчетаПолуфабриката.Характеристика КАК ХарактеристикаПолуфабриката,
		|		Результат.АналитикаУчетаПродукции КАК АналитикаУчетаПродукции,
		|		Результат.ВидСтроки КАК ВидСтроки,
		|		Результат.ПартияВыходноеИзделие КАК ПартияПолуфабриката,
		|		Результат.ПартияПродукции КАК ПартияПродукции,
		|		Результат.ТребуетсяРазузлование КАК ЭтоПолуфабрикат,
		|		ВЫБОР
		|			КОГДА ТИПЗНАЧЕНИЯ(Результат.Номенклатура) = ТИП(Справочник.Номенклатура)
		|				ТОГДА ВЫРАЗИТЬ(Результат.Номенклатура КАК Справочник.Номенклатура).ЕдиницаИзмерения
		|			КОГДА ТИПЗНАЧЕНИЯ(Результат.Номенклатура) = ТИП(Справочник.ВидыРаботСотрудников)
		|				ТОГДА ВЫРАЗИТЬ(Результат.Номенклатура КАК Справочник.ВидыРаботСотрудников).ЕдиницаИзмерения
		|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
		|		КОНЕЦ КАК ЕдиницаИзмерения,
		|		Результат.Организация КАК Организация,
		|		Результат.Организация.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета,
		|		Результат.Подразделение КАК Подразделение,
		|		Результат.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|		Результат.ПартияЗатрата КАК ПартияЗатрата
		|	ИЗ
		|		Результат КАК Результат
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикаУчетаПолуфабриката
		|			ПО Результат.АналитикаУчетаВыходноеИзделие = АналитикаУчетаПолуфабриката.Ссылка
		|	
		|	СГРУППИРОВАТЬ ПО
		|		Результат.Характеристика,
		|		АналитикаУчетаПолуфабриката.Номенклатура,
		|		Результат.АналитикаУчетаПродукции,
		|		АналитикаУчетаПолуфабриката.Характеристика,
		|		Результат.Подразделение,
		|		Результат.Организация,
		|		Результат.Организация.ВалютаРегламентированногоУчета,
		|		Результат.ПартияВыходноеИзделие,
		|		Результат.СтатьяКалькуляции,
		|		Результат.Номенклатура,
		|		Результат.АналитикаУчетаВыходноеИзделие,
		|		Результат.ТребуетсяРазузлование,
		|		ВЫБОР
		|			КОГДА ТИПЗНАЧЕНИЯ(Результат.Номенклатура) = ТИП(Справочник.Номенклатура)
		|				ТОГДА ВЫРАЗИТЬ(Результат.Номенклатура КАК Справочник.Номенклатура).ЕдиницаИзмерения
		|			КОГДА ТИПЗНАЧЕНИЯ(Результат.Номенклатура) = ТИП(Справочник.ВидыРаботСотрудников)
		|				ТОГДА ВЫРАЗИТЬ(Результат.Номенклатура КАК Справочник.ВидыРаботСотрудников).ЕдиницаИзмерения
		|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
		|		КОНЕЦ, // ЕдиницаИзмерения
		|		Результат.ПартияПродукции,
		|		Результат.ВидСтроки,
		|		Результат.ПартияЗатрата
		|	) КАК ВложенныйЗапрос
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТПродукция.Продукция КАК Продукция,
		|	ВТПродукция.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	ВТПродукция.Назначение КАК Назначение,
		|	ВТПродукция.ПартияПродукции КАК ПартияПродукции,
		|	ВТПродукция.Количество КАК КоличествоВыпуск,
		|	ВТПродукция.Продукция КАК Полуфабрикат,
		|	ВТПродукция.ХарактеристикаПродукции КАК ХарактеристикаПолуфабриката,
		|	Затраты.Затрата КАК Затрата,
		|	Затраты.ХарактеристикаЗатраты КАК ХарактеристикаЗатраты,
		|	Затраты.Сумма КАК Стоимость,
		|	Затраты.Материальные КАК Материальные,
		|	Затраты.ДопРасходы КАК ДопРасходы,
		|	Затраты.Трудозатраты КАК Трудозатраты,
		|	Затраты.ПостатейныеПостоянные КАК ПостатейныеПостоянные,
		|	Затраты.ПостатейныеПеременные КАК ПостатейныеПеременные,
		|	Затраты.Забалансовая КАК Забалансовая,
		|	Затраты.НалоговыйУчет КАК НалоговыйУчет,
		|	Затраты.ПостояннаяРазница КАК ПостояннаяРазница,
		|	Затраты.ВременнаяРазница КАК ВременнаяРазница,
		|	Затраты.НДД КАК НДД,
		|
		|	Затраты.Количество КАК Количество,
		|	Затраты.ЭтоПолуфабрикат КАК ЭтоПолуфабрикат,
		|	Затраты.ЕдиницаИзмерения КАК ЕдиницаИзмеренияЗатраты,
		|	Затраты.Организация КАК Организация,
		|	Затраты.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета,
		|	Затраты.Подразделение КАК Подразделение,
		|	Затраты.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	ВТПродукция.Серия КАК Серия,
		|	ВТПродукция.Сумма КАК СуммаВыпуск,
		|	ВТПродукция.Материальные КАК МатериальныеВыпуск,
		|	ВТПродукция.ДопРасходы КАК ДопРасходыВыпуск,
		|	ВТПродукция.Трудозатраты КАК ТрудозатратыВыпуск,
		|	ВТПродукция.ПостатейныеПостоянные КАК ПостатейныеПостоянныеВыпуск,
		|	ВТПродукция.ПостатейныеПеременные КАК ПостатейныеПеременныеВыпуск,
		|	ВТПродукция.Забалансовая КАК ЗабалансоваяВыпуск,
		|	ВТПродукция.НалоговыйУчет КАК НалоговыйУчетВыпуск,
		|	ВТПродукция.ПостояннаяРазница КАК ПостояннаяРазницаВыпуск,
		|	ВТПродукция.ВременнаяРазница КАК ВременнаяРазницаВыпуск,
		|	ВТПродукция.НДД КАК НДДВыпуск,
		|	Затраты.ПартияЗатрата КАК ПартияЗатрата
		|ПОМЕСТИТЬ ЗатратыВРазрезе
		|ИЗ
		|	ВТПродукция КАК ВТПродукция
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Затраты КАК Затраты
		|		ПО ВТПродукция.АналитикаУчетаПродукции = Затраты.АналитикаУчетаВыходноеИзделие
		|			И (НЕ Затраты.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.ВидыСтрокДереваСебестоимости.ПартияПродукции))
		|			И ВТПродукция.ПартияПродукции = Затраты.ПартияПродукции
		|			И ВТПродукция.ПартияПродукции = Затраты.ПартияПолуфабриката
		|			И ВТПродукция.Организация = Затраты.Организация
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КоличествоВыпуска.Продукция КАК Продукция,
		|	КоличествоВыпуска.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	КоличествоВыпуска.Назначение КАК Назначение,
		|	КоличествоВыпуска.ПартияПродукции КАК ПартияПродукции,
		|	КоличествоВыпуска.Количество КАК КоличествоВыпуск,
		|	КоличествоВыпуска.Полуфабрикат КАК Полуфабрикат,
		|	КоличествоВыпуска.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
		|	Затраты.Затрата КАК Затрата,
		|	Затраты.ХарактеристикаЗатраты КАК ХарактеристикаЗатраты,
		|	СУММА(Затраты.Сумма) КАК Стоимость,
		|	СУММА(Затраты.Материальные) КАК Материальные,
		|	СУММА(Затраты.ДопРасходы) КАК ДопРасходы,
		|	СУММА(Затраты.Трудозатраты) КАК Трудозатраты,
		|	СУММА(Затраты.ПостатейныеПостоянные) КАК ПостатейныеПостоянные,
		|	СУММА(Затраты.ПостатейныеПеременные) КАК ПостатейныеПеременные,
		|	СУММА(Затраты.Забалансовая) КАК Забалансовая,
		|	СУММА(Затраты.НалоговыйУчет) КАК НалоговыйУчет,
		|	СУММА(Затраты.ПостояннаяРазница) КАК ПостояннаяРазница,
		|	СУММА(Затраты.ВременнаяРазница) КАК ВременнаяРазница,
		|	СУММА(Затраты.НДД),
		|
		|	СУММА(Затраты.Количество) КАК Количество,
		|	Затраты.ЭтоПолуфабрикат КАК ЭтоПолуфабрикат,
		|	Затраты.ЕдиницаИзмерения КАК ЕдиницаИзмеренияЗатраты,
		|	Затраты.Организация КАК Организация,
		|	Затраты.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета,
		|	Затраты.Подразделение КАК Подразделение,
		|	Затраты.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	КоличествоВыпуска.Серия КАК Серия,
		|	0 КАК СуммаВыпуск,
		|	0 КАК МатериальныеВыпуск,
		|	0 КАК ДопРасходыВыпуск,
		|	0 КАК ТрудозатратыВыпуск,
		|	0 КАК ПостатейныеПостоянныеВыпуск,
		|	0 КАК ПостатейныеПеременныеВыпуск,
		|	0 КАК ЗабалансоваяВыпуск,
		|	0 КАК НалоговыйУчетВыпуск,
		|	0 КАК ПостояннаяРазницаВыпуск,
		|	0 КАК ВременнаяРазницаВыпуск,
		|	0 КАК НДДВыпуск,
		|	Затраты.ПартияЗатрата КАК ПартияЗатрата
		|ИЗ
		|	КоличествоВыпуска КАК КоличествоВыпуска
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Затраты КАК Затраты
		|		ПО КоличествоВыпуска.АналитикаУчетаПродукции = Затраты.АналитикаУчетаПродукции
		|			И КоличествоВыпуска.Полуфабрикат = Затраты.Полуфабрикат
		|			И КоличествоВыпуска.ХарактеристикаПолуфабриката = Затраты.ХарактеристикаПолуфабриката
		|			И КоличествоВыпуска.ПартияПолуфабриката = Затраты.ПартияПолуфабриката
		|			И КоличествоВыпуска.ПартияПродукции = Затраты.ПартияПродукции
		|			И Затраты.ПартияПродукции <> Затраты.ПартияПолуфабриката
		|			И Затраты.Организация = КоличествоВыпуска.Организация
		|ГДЕ
		|	НЕ КоличествоВыпуска.ЭтоПродукция
		|
		|СГРУППИРОВАТЬ ПО
		|	КоличествоВыпуска.Продукция,
		|	КоличествоВыпуска.ХарактеристикаПродукции,
		|	КоличествоВыпуска.Назначение,
		|	КоличествоВыпуска.Серия,
		|	КоличествоВыпуска.ПартияПродукции,
		|	КоличествоВыпуска.Количество,
		|	КоличествоВыпуска.Полуфабрикат,
		|	КоличествоВыпуска.ХарактеристикаПолуфабриката,
		|	Затраты.Затрата,
		|	Затраты.ХарактеристикаЗатраты,
		|	Затраты.ЭтоПолуфабрикат,
		|	Затраты.ЕдиницаИзмерения,
		|	Затраты.Организация,
		|	Затраты.ВалютаРегламентированногоУчета,
		|	Затраты.Подразделение,
		|	Затраты.СтатьяКалькуляции,
		|	Затраты.ПартияЗатрата
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
		|	СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
		|	СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Серия КАК Серия,
		|	СебестоимостьТоваров.Партия КАК Партия,
		|	СебестоимостьТоваров.Организация КАК Организация,
		|	СУММА(СебестоимостьТоваров.Количество) КАК Количество,
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 1
		|				ТОГДА СебестоимостьТоваров.Стоимость
		|				+ СебестоимостьТоваров.Трудозатраты
		|				+ СебестоимостьТоваров.ПостатейныеПостоянныеСНДС
		|				+ СебестоимостьТоваров.ПостатейныеПеременныеСНДС
		|				+ СебестоимостьТоваров.ДопРасходы
		|			КОГДА &ДанныеПоСебестоимости = 2
		|				ТОГДА СебестоимостьТоваров.СтоимостьБезНДС
		|				+ СебестоимостьТоваров.Трудозатраты
		|				+ СебестоимостьТоваров.ПостатейныеПостоянныеБезНДС
		|				+ СебестоимостьТоваров.ПостатейныеПеременныеБезНДС
		|				+ СебестоимостьТоваров.ДопРасходыБезНДС
		|			КОГДА &ДанныеПоСебестоимости = 3
		|				ТОГДА СебестоимостьТоваров.СтоимостьУпр
		|				+ СебестоимостьТоваров.ТрудозатратыУпр
		|				+ СебестоимостьТоваров.ПостатейныеПостоянныеУпр
		|				+ СебестоимостьТоваров.ПостатейныеПеременныеУпр
		|				+ СебестоимостьТоваров.ДопРасходыУпр
		|			КОГДА &ДанныеПоСебестоимости = 4
		|				ТОГДА СебестоимостьТоваров.СтоимостьРегл
		|				+ СебестоимостьТоваров.ТрудозатратыРегл
		|				+ СебестоимостьТоваров.ПостатейныеПостоянныеРегл
		|				+ СебестоимостьТоваров.ПостатейныеПеременныеРегл
		|				+ СебестоимостьТоваров.ДопРасходыРегл
		|		КОНЕЦ)												КАК Стоимость,
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 1
		|				ТОГДА СебестоимостьТоваров.Стоимость
		|			КОГДА &ДанныеПоСебестоимости = 2
		|				ТОГДА СебестоимостьТоваров.СтоимостьБезНДС
		|			КОГДА &ДанныеПоСебестоимости = 3
		|				ТОГДА СебестоимостьТоваров.СтоимостьУпр
		|			КОГДА &ДанныеПоСебестоимости = 4
		|				ТОГДА СебестоимостьТоваров.СтоимостьРегл
		|		КОНЕЦ)												КАК Материальные,
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 1
		|				ТОГДА СебестоимостьТоваров.ДопРасходы
		|			КОГДА &ДанныеПоСебестоимости = 2
		|				ТОГДА СебестоимостьТоваров.ДопРасходыБезНДС
		|			КОГДА &ДанныеПоСебестоимости = 3
		|				ТОГДА СебестоимостьТоваров.ДопРасходыУпр
		|			КОГДА &ДанныеПоСебестоимости = 4
		|				ТОГДА СебестоимостьТоваров.ДопРасходыРегл
		|		КОНЕЦ)												КАК ДопРасходы,
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 1
		|				ТОГДА СебестоимостьТоваров.Трудозатраты
		|			КОГДА &ДанныеПоСебестоимости = 2
		|				ТОГДА СебестоимостьТоваров.Трудозатраты
		|			КОГДА &ДанныеПоСебестоимости = 3
		|				ТОГДА СебестоимостьТоваров.ТрудозатратыУпр
		|			КОГДА &ДанныеПоСебестоимости = 4
		|				ТОГДА СебестоимостьТоваров.ТрудозатратыРегл
		|		КОНЕЦ)												КАК Трудозатраты,
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 1
		|				ТОГДА СебестоимостьТоваров.ПостатейныеПостоянныеСНДС
		|			КОГДА &ДанныеПоСебестоимости = 2
		|				ТОГДА СебестоимостьТоваров.ПостатейныеПостоянныеБезНДС
		|			КОГДА &ДанныеПоСебестоимости = 3
		|				ТОГДА СебестоимостьТоваров.ПостатейныеПостоянныеУпр
		|			КОГДА &ДанныеПоСебестоимости = 4
		|				ТОГДА СебестоимостьТоваров.ПостатейныеПостоянныеРегл
		|		КОНЕЦ)												КАК ПостатейныеПостоянные,
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 1
		|				ТОГДА СебестоимостьТоваров.ПостатейныеПеременныеСНДС
		|			КОГДА &ДанныеПоСебестоимости = 2
		|				ТОГДА СебестоимостьТоваров.ПостатейныеПеременныеБезНДС
		|			КОГДА &ДанныеПоСебестоимости = 3
		|				ТОГДА СебестоимостьТоваров.ПостатейныеПеременныеУпр
		|			КОГДА &ДанныеПоСебестоимости = 4
		|				ТОГДА СебестоимостьТоваров.ПостатейныеПеременныеРегл
		|		КОНЕЦ)												КАК ПостатейныеПеременные,
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 1
		|				ТОГДА СебестоимостьТоваров.СтоимостьЗабалансовая
		|			КОГДА &ДанныеПоСебестоимости = 2
		|				ТОГДА СебестоимостьТоваров.СтоимостьЗабалансовая
		|			КОГДА &ДанныеПоСебестоимости = 3
		|				ТОГДА СебестоимостьТоваров.СтоимостьЗабалансовая
		|			КОГДА &ДанныеПоСебестоимости = 4
		|				ТОГДА СебестоимостьТоваров.СтоимостьЗабалансоваяРегл
		|		КОНЕЦ)												КАК Забалансовая,
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 4
		|				ТОГДА СебестоимостьТоваров.СтоимостьРегл
		|					+ СебестоимостьТоваров.ДопРасходыРегл
		|					+ СебестоимостьТоваров.ТрудозатратыРегл
		|					+ СебестоимостьТоваров.ПостатейныеПостоянныеРегл
		|					+ СебестоимостьТоваров.ПостатейныеПеременныеРегл
		|					- СебестоимостьТоваров.ПостояннаяРазница
		|					- СебестоимостьТоваров.ВременнаяРазница
		|			ИНАЧЕ 0
		|		КОНЕЦ)												КАК НалоговыйУчет,
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 4
		|				ТОГДА СебестоимостьТоваров.ПостояннаяРазница
		|			ИНАЧЕ 0
		|		КОНЕЦ)												КАК ПостояннаяРазница,
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 4
		|				ТОГДА СебестоимостьТоваров.ВременнаяРазница
		|			ИНАЧЕ 0
		|		КОНЕЦ)												КАК ВременнаяРазница,
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 4
		|				ТОГДА СебестоимостьТоваров.СтоимостьНДД
		|			ИНАЧЕ 0
		|		КОНЕЦ)												КАК НДД
		|
		|ПОМЕСТИТЬ МатериалыПродукция
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
		|ГДЕ
		|	СебестоимостьТоваров.Регистратор В(&Накладные)
		|	И СебестоимостьТоваров.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И (СебестоимостьТоваров.Период <= &ДатаОкончания ИЛИ &ДатаОкончания = ДАТАВРЕМЯ(1,1,1))
		|
		|СГРУППИРОВАТЬ ПО
		|	СебестоимостьТоваров.Партия,
		|	СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Серия,
		|	СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Характеристика,
		|	СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Номенклатура,
		|	СебестоимостьТоваров.Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ФактическиеДанные.Продукция КАК Продукция,
		|	ФактическиеДанные.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	ФактическиеДанные.Назначение КАК НазначениеПродукции,
		|	ФактическиеДанные.ПартияПродукции КАК ПартияПродукции,
		|	ФактическиеДанные.Полуфабрикат КАК Полуфабрикат,
		|	ФактическиеДанные.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
		|	ФактическиеДанные.Затрата КАК Затрата,
		|	ФактическиеДанные.ХарактеристикаЗатраты КАК ХарактеристикаЗатраты,
		|	ВЫБОР
		|		КОГДА ФактическиеДанные.СуммаВыпуск > 0
		|			ТОГДА ФактическиеДанные.СуммаВыпуск
		|		ИНАЧЕ ФактическиеДанные.Стоимость
		|	КОНЕЦ КАК Стоимость,
		|	ВЫБОР
		|		КОГДА ФактическиеДанные.МатериальныеВыпуск > 0
		|			ТОГДА ФактическиеДанные.МатериальныеВыпуск
		|		ИНАЧЕ ФактическиеДанные.Материальные
		|	КОНЕЦ КАК Материальные,
		|	ВЫБОР
		|		КОГДА ФактическиеДанные.ДопРасходыВыпуск > 0
		|			ТОГДА ФактическиеДанные.ДопРасходыВыпуск
		|		ИНАЧЕ ФактическиеДанные.ДопРасходы
		|	КОНЕЦ КАК ДопРасходы,
		|	ВЫБОР
		|		КОГДА ФактическиеДанные.ТрудозатратыВыпуск > 0
		|			ТОГДА ФактическиеДанные.ТрудозатратыВыпуск
		|		ИНАЧЕ ФактическиеДанные.Трудозатраты
		|	КОНЕЦ КАК Трудозатраты,
		|	ВЫБОР
		|		КОГДА ФактическиеДанные.ПостатейныеПостоянныеВыпуск > 0
		|			ТОГДА ФактическиеДанные.ПостатейныеПостоянныеВыпуск
		|		ИНАЧЕ ФактическиеДанные.ПостатейныеПостоянные
		|	КОНЕЦ КАК ПостатейныеПостоянные,
		|	ВЫБОР
		|		КОГДА ФактическиеДанные.ПостатейныеПеременныеВыпуск > 0
		|			ТОГДА ФактическиеДанные.ПостатейныеПеременныеВыпуск
		|		ИНАЧЕ ФактическиеДанные.ПостатейныеПеременные
		|	КОНЕЦ КАК ПостатейныеПеременные,
		|	ВЫБОР
		|		КОГДА ФактическиеДанные.ЗабалансоваяВыпуск > 0
		|			ТОГДА ФактическиеДанные.ЗабалансоваяВыпуск
		|		ИНАЧЕ ФактическиеДанные.Забалансовая
		|	КОНЕЦ КАК Забалансовые,
		|	ВЫБОР
		|		КОГДА ФактическиеДанные.НалоговыйУчетВыпуск > 0
		|			ТОГДА ФактическиеДанные.НалоговыйУчетВыпуск
		|		ИНАЧЕ ФактическиеДанные.НалоговыйУчет
		|	КОНЕЦ КАК НалоговыйУчет,
		|	ВЫБОР
		|		КОГДА ФактическиеДанные.ПостояннаяРазницаВыпуск > 0
		|			ТОГДА ФактическиеДанные.ПостояннаяРазницаВыпуск
		|		ИНАЧЕ ФактическиеДанные.ПостояннаяРазница
		|	КОНЕЦ КАК ПостояннаяРазница,
		|	ВЫБОР
		|		КОГДА ФактическиеДанные.ВременнаяРазницаВыпуск > 0
		|			ТОГДА ФактическиеДанные.ВременнаяРазницаВыпуск
		|		ИНАЧЕ ФактическиеДанные.ВременнаяРазница
		|	КОНЕЦ КАК ВременнаяРазница,
		|	ВЫБОР
		|		КОГДА ФактическиеДанные.НДДВыпуск > 0
		|			ТОГДА ФактическиеДанные.НДДВыпуск
		|		ИНАЧЕ ФактическиеДанные.НДД
		|	КОНЕЦ КАК НДД,
		|	ФактическиеДанные.Количество КАК Количество,
		|	ФактическиеДанные.Продукция.ЕдиницаИзмерения КАК ЕдиницаИзмеренияПродукции,
		|	ФактическиеДанные.Полуфабрикат.ЕдиницаИзмерения КАК ЕдиницаИзмеренияПолуфабриката,
		|	ФактическиеДанные.ЕдиницаИзмеренияЗатраты КАК ЕдиницаИзмеренияЗатраты,
		|	ФактическиеДанные.Организация КАК Организация,
		|	ФактическиеДанные.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	ФактическиеДанные.Подразделение КАК Подразделение,
		|	""Продукция"" КАК ТипДанных,
		|	ФактическиеДанные.Серия КАК Серия,
		|	ФактическиеДанные.ПартияЗатрата КАК ПартияЗатраты,
		|	0 КАК Уровень,
		|	ВЫБОР
		|		КОГДА &ДанныеПоСебестоимости = 3
		|			ТОГДА ФактическиеДанные.ВалютаРегламентированногоУчета
		|		ИНАЧЕ
		|			&ВалютаУправленческогоУчета
		|	КОНЕЦ КАК Валюта
		|ИЗ
		|	ЗатратыВРазрезе КАК ФактическиеДанные
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	NULL КАК Продукция,
		|	NULL КАК ХарактеристикаПродукции,
		|	NULL КАК НазначениеПродукции,
		|	NULL КАК ПартияПродукции,
		|	NULL КАК Полуфабрикат,
		|	NULL КАК ХарактеристикаПолуфабриката,
		|	МатериалыПродукция.Номенклатура КАК Затрата,
		|	МатериалыПродукция.Характеристика КАК ХарактеристикаЗатраты,
		|	СУММА(МатериалыПродукция.Стоимость) КАК Стоимость,
		|	СУММА(МатериалыПродукция.Материальные) КАК Материальные,
		|	СУММА(МатериалыПродукция.ДопРасходы) КАК ДопРасходы,
		|	СУММА(МатериалыПродукция.Трудозатраты) КАК Трудозатраты,
		|	СУММА(МатериалыПродукция.ПостатейныеПостоянные) КАК ПостатейныеПостоянные,
		|	СУММА(МатериалыПродукция.ПостатейныеПеременные) КАК ПостатейныеПеременные,
		|	СУММА(МатериалыПродукция.Забалансовая) КАК Забалансовые,
		|	СУММА(МатериалыПродукция.НалоговыйУчет) КАК НалоговыйУчет,
		|	СУММА(МатериалыПродукция.ПостояннаяРазница) КАК ПостояннаяРазница,
		|	СУММА(МатериалыПродукция.ВременнаяРазница) КАК ВременнаяРазница,
		|	СУММА(МатериалыПродукция.НДД),
		|	СУММА(МатериалыПродукция.Количество) КАК Количество,
		|	NULL КАК ЕдиницаИзмеренияПродукции,
		|	NULL КАК ЕдиницаИзмеренияПолуфабриката,
		|	МатериалыПродукция.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмеренияЗатраты,
		|	МатериалыПродукция.Организация КАК Организация,
		|	NULL КАК СтатьяКалькуляции,
		|	NULL КАК Подразделение,
		|	""Материалы"" КАК ТипДанных,
		|	МатериалыПродукция.Серия КАК Серия,
		|	МатериалыПродукция.Партия КАК ПартияЗатраты,
		|	0 КАК Уровень,
		|	ВЫБОР
		|		КОГДА &ДанныеПоСебестоимости = 3
		|			ТОГДА МатериалыПродукция.Организация.ВалютаРегламентированногоУчета
		|		ИНАЧЕ
		|			&ВалютаУправленческогоУчета
		|	КОНЕЦ КАК Валюта
		|ИЗ
		|	МатериалыПродукция КАК МатериалыПродукция
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
		|		ПО (СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Номенклатура = МатериалыПродукция.Номенклатура)
		|			И (СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Характеристика = МатериалыПродукция.Характеристика)
		|			И (СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Серия = МатериалыПродукция.Серия)
		|			И (СебестоимостьТоваров.РазделУчета В (ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты), ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение)))
		|			И (СебестоимостьТоваров.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
		|			И (СебестоимостьТоваров.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииНаСклад), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииВПодразделение)))
		|			И (НЕ СебестоимостьТоваров.Партия = НЕОПРЕДЕЛЕНО)
		|			И (МатериалыПродукция.Партия = НЕОПРЕДЕЛЕНО ИЛИ МатериалыПродукция.Партия = СебестоимостьТоваров.Партия)
		|ГДЕ
		|	СебестоимостьТоваров.Регистратор ЕСТЬ NULL
		|
		|СГРУППИРОВАТЬ ПО
		|	МатериалыПродукция.Номенклатура,
		|	МатериалыПродукция.Характеристика,
		|	МатериалыПродукция.Организация,
		|	МатериалыПродукция.Серия,
		|	МатериалыПродукция.Номенклатура.ЕдиницаИзмерения,
		|	МатериалыПродукция.Партия,
		|	ВЫБОР
		|		КОГДА &ДанныеПоСебестоимости = 3
		|			ТОГДА МатериалыПродукция.Организация.ВалютаРегламентированногоУчета
		|		ИНАЧЕ
		|			&ВалютаУправленческогоУчета
		|	КОНЕЦ
		//++ НЕ УТКА	
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТПродукция.Продукция КАК Продукция,
		|	ВТПродукция.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	ВТПродукция.Назначение КАК НазначениеПродукции,
		|	ВТПродукция.ПартияПродукции КАК ПартияПродукции,
		|	NULL КАК Полуфабрикат,
		|	NULL КАК ХарактеристикаПолуфабриката,
		|	Затраты.Номенклатура КАК Затрата,
		|	Затраты.Характеристика КАК ХарактеристикаЗатраты,
		|	Затраты.Сумма КАК Стоимость,
		|	Затраты.Материальные КАК Материальные,
		|	Затраты.ДопРасходы КАК ДопРасходы,
		|	Затраты.Трудозатраты КАК Трудозатраты,
		|	Затраты.ПостатейныеПостоянные КАК ПостатейныеПостоянные,
		|	Затраты.ПостатейныеПеременные КАК ПостатейныеПеременные,
		|	Затраты.СуммаЗабалансовая КАК Забалансовые,
		|	Затраты.НалоговыйУчет КАК НалоговыйУчет,
		|	Затраты.ПостояннаяРазница КАК ПостояннаяРазница,
		|	Затраты.ВременнаяРазница КАК ВременнаяРазница,
		|	Затраты.НДД,
		|	Затраты.Количество КАК Количество,
		|	ВТПродукция.Продукция.ЕдиницаИзмерения КАК ЕдиницаИзмеренияПродукции,
		|	NULL КАК ЕдиницаИзмеренияПолуфабриката,
		|	Затраты.ЕдиницаИзмерения КАК ЕдиницаИзмеренияЗатраты,
		|	ВТПродукция.Организация КАК Организация,
		|	Затраты.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	Затраты.Подразделение КАК Подразделение,
		|	""МатериалыДавальца"" КАК ТипДанных,
		|	ВТПродукция.Серия КАК Серия,
		|	Затраты.ПартияЗатрата КАК ПартияЗатраты,
		|	Затраты.Уровень КАК Уровень,
		|	ВЫБОР
		|		КОГДА &ДанныеПоСебестоимости = 3
		|			ТОГДА ВТПродукция.ВалютаРегламентированногоУчета
		|		ИНАЧЕ
		|			&ВалютаУправленческогоУчета
		|	КОНЕЦ КАК Валюта
		|ИЗ
		|	ВТПродукция КАК ВТПродукция
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Результат КАК Затраты
		|		ПО ВТПродукция.АналитикаУчетаПродукции = Затраты.АналитикаУчетаПродукции
		|			И ВТПродукция.ПартияПродукции = Затраты.ПартияПродукции
		|			И (Затраты.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.ВидыСтрокДереваСебестоимости.ПартияЗатраты))
		|			И (ТИПЗНАЧЕНИЯ(Затраты.ПартияЗатрата) = ТИП(Документ.ПриемкаТоваровНаХранение)
		|						И ВЫРАЗИТЬ(Затраты.ПартияЗатрата КАК Документ.ПриемкаТоваровНаХранение).ХозяйственнаяОперация =
		|							ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеОтДавальца2_5)
		//++ Устарело_Переработка24
		|				ИЛИ ТИПЗНАЧЕНИЯ(Затраты.ПартияЗатрата) = ТИП(Документ.ПоступлениеСырьяОтДавальца)
		//-- Устарело_Переработка24
		|				ИЛИ ЛОЖЬ)
		//-- НЕ УТКА	
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ФактическиеДанные.Продукция КАК Продукция,
		|	ФактическиеДанные.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	ФактическиеДанные.Назначение КАК НазначениеПродукции,
		|	ФактическиеДанные.ПартияПродукции КАК ПартияПродукции,
		|	ФактическиеДанные.Полуфабрикат КАК Полуфабрикат,
		|	ФактическиеДанные.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
		|	СУММА(ФактическиеДанные.Количество) КАК КоличествоВыпуск,
		|	ФактическиеДанные.Организация КАК Организация,
		|	ВЫБОР
		|		КОГДА &ДанныеПоСебестоимости = 3
		|			ТОГДА ФактическиеДанные.ВалютаРегламентированногоУчета
		|		ИНАЧЕ
		|			&ВалютаУправленческогоУчета
		|	КОНЕЦ КАК Валюта
		|ИЗ
		|	КоличествоВыпуска КАК ФактическиеДанные
		|ГДЕ
		|	ФактическиеДанные.ЭтоПродукция
		|
		|СГРУППИРОВАТЬ ПО
		|	ФактическиеДанные.Продукция,
		|	ФактическиеДанные.Полуфабрикат,
		|	ФактическиеДанные.ХарактеристикаПолуфабриката,
		|	ФактическиеДанные.ПартияПродукции,
		|	ФактическиеДанные.ХарактеристикаПродукции,
		|	ФактическиеДанные.Назначение,
		|	ФактическиеДанные.Организация,
		|	ВЫБОР
		|		КОГДА &ДанныеПоСебестоимости = 3
		|			ТОГДА ФактическиеДанные.ВалютаРегламентированногоУчета
		|		ИНАЧЕ
		|			&ВалютаУправленческогоУчета
		|	КОНЕЦ
		|";
		
	Если Не УстановленОтборПоОрганизации Тогда
		ТекстЗапроса = ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов() + "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ФактическиеДанные.Организация КАК Организация
		|ИЗ
		|	КоличествоВыпуска КАК ФактическиеДанные
		|";
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
#КонецОбласти

	МассивРезультатов = Запрос.ВыполнитьПакет();
	ВнешниеНаборыДанных = Новый Структура();
	ИндексПоследнегоЗапроса = МассивРезультатов.Количество();
	
	МассивОрганизаций = Неопределено;
	Если Не УстановленОтборПоОрганизации Тогда
		ИндексПоследнегоЗапроса = ИндексПоследнегоЗапроса - 1;
		МассивОрганизаций = Новый Массив();
		ОрганизацииДляПроверки = МассивРезультатов[ИндексПоследнегоЗапроса].Выбрать();
		Пока ОрганизацииДляПроверки.Следующий() Цикл
			МассивОрганизаций.Добавить(ОрганизацииДляПроверки.Организация);
		КонецЦикла;
	КонецЕсли;
	
	ВнешниеНаборыДанных.Вставить("Выпуски", МассивРезультатов[ИндексПоследнегоЗапроса - 1].Выгрузить());
	ВнешниеНаборыДанных.Вставить("Факт", МассивРезультатов[ИндексПоследнегоЗапроса - 2].Выгрузить());
	
	ЭлементыКУдалению = Новый Массив;
	Для Каждого ЭлементОтбора Из НастройкиОтчета.Отбор.Элементы Цикл
		
		Если ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Регистратор") Тогда
			ЭлементыКУдалению.Добавить(ЭлементОтбора);
		КонецЕсли;
		
		//++ НЕ УТКА	
		Если ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Калькуляция") Тогда
			// Отчет вызван в качестве расширофвки отчета "Плановая и фактическая себестоимость".
			ЭлементыКУдалению.Добавить(ЭлементОтбора);
		КонецЕсли;
		//-- НЕ УТКА	
		
	КонецЦикла;
	
	Для Каждого УдаляемыйЭлемент Из ЭлементыКУдалению Цикл
		НастройкиОтчета.Отбор.Элементы.Удалить(УдаляемыйЭлемент);
	КонецЦикла;
	
	// Управление видимостью колонками постоянной и временной разницы.
	НайденныеКолонки = Новый Массив;
	НайтиКолонкуРекурсивно(НастройкиОтчета.Структура, НайденныеКолонки, Новый ПолеКомпоновкиДанных("ПостояннаяРазница"));
	НайтиКолонкуРекурсивно(НастройкиОтчета.Структура, НайденныеКолонки, Новый ПолеКомпоновкиДанных("ВременнаяРазница"));
	
	ОтображатьПРВР = (ДанныеПоСебестоимости = 4)
		И НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ОтображатьПРВР")).Значение;
	Для Каждого Колонка Из НайденныеКолонки Цикл
		Колонка.Использование = ОтображатьПРВР;
	КонецЦикла;
	
	// Управление видимостью колонки налогового учета.
	НайденныеКолонки = Новый Массив;
	НайтиКолонкуРекурсивно(НастройкиОтчета.Структура, НайденныеКолонки, Новый ПолеКомпоновкиДанных("НалоговыйУчет"));
	Для Каждого Колонка Из НайденныеКолонки Цикл
		Колонка.Использование = (ДанныеПоСебестоимости = 4)
			И ПолучитьФункциональнуюОпцию("ИспользоватьРеглУчет")
			И Колонка.Использование;
	КонецЦикла;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	
	ВывестиТекстПредупрежденияОбОграниченияхИспользованияОтчета(НачалоПериода,
		КонецПериода,
		ДокументРезультат,
		МассивОрганизаций);
		
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	//++ НЕ УТКА
	Если Не ТипЗнч(Накладные) = Тип("ДокументСсылка.ОтчетДавальцу2_5")
		//++ Устарело_Переработка24
		И Не ТипЗнч(Накладные) = Тип("ДокументСсылка.ОтчетДавальцу")
		//-- Устарело_Переработка24
		И Истина Тогда
	//-- НЕ УТКА	
		КомпоновкаДанныхСервер.СкрытьВспомогательныеПараметрыОтчета(
			СхемаКомпоновкиДанных, КомпоновщикНастроек, ДокументРезультат, ВспомогательныеПараметрыОтчета());
	//++ НЕ УТКА	
	КонецЕсли;
	//-- НЕ УТКА	
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Настраивает параметры отчета по варианту отчета
//
// Параметры:
//	КлючВарианта						- Строка									- имя предопределенного или уникальный идентификатор пользовательского
//																						варианта отчета.
//										- Неопределено 								- вызов для варианта расшифровки или без контекста.
//	НовыеНастройкиКД					- НастройкиКомпоновкиДанных					- настройки варианта отчета, которые будут загружены
//																						в компоновщик настроек после его инициализации.
//										- Неопределено 								- настройки варианта не надо загружать (уже загружены ранее).
//	НовыеПользовательскиеНастройкиКД	- ПользовательскиеНастройкиКомпоновкиДанных - пользовательские настройки, которые будут загружены в компоновщик
//																						настроек после его инициализации.
//										- Неопределено 								- пользовательские настройки не надо загружать (уже загружены ранее).
//
Процедура НастроитьПараметрыОтчетаПоВариантуОтчета(КлючВарианта, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД)
	
	ПараметрДанныеПоСебестоимости = СхемаКомпоновкиДанных.Параметры.Найти("ДанныеПоСебестоимости");
	
	СписокВыбора = Новый СписокЗначений;
	
	Если КлючВарианта = "ФактическаяСебестоимостьПродукцииПредприятия" Тогда
		
		СписокВыбора.Добавить(1, НСтр("ru = 'В валюте упр. учета с НДС';
										|en = 'In management accounting currency, including VAT'"));
		СписокВыбора.Добавить(2, НСтр("ru = 'В валюте упр. учета без НДС';
										|en = 'In management accounting currency, excluding VAT'"));
		
	ИначеЕсли КлючВарианта = "ФактическаяСебестоимостьПродукцииОрганизации" Тогда	
		
		Если РасчетСебестоимостиПовтИсп.УправленческийУчетОрганизаций() Тогда
			СписокВыбора.Добавить(3, НСтр("ru = 'В валюте упр. учета';
											|en = 'In management accounting currency'"));
		КонецЕсли;
		СписокВыбора.Добавить(4, НСтр("ru = 'В валюте регл. учета';
										|en = 'In local accounting currency'"));
		
	Иначе
		
		СписокВыбора.Добавить(1, НСтр("ru = 'В валюте упр. учета с НДС';
										|en = 'In management accounting currency, including VAT'"));
		СписокВыбора.Добавить(2, НСтр("ru = 'В валюте упр. учета без НДС';
										|en = 'In management accounting currency, excluding VAT'"));
		СписокВыбора.Добавить(4, НСтр("ru = 'В валюте регл. учета';
										|en = 'In local accounting currency'"));
		
	КонецЕсли;
	
	ПараметрДанныеПоСебестоимости.УстановитьДоступныеЗначения(СписокВыбора);
	
	Если НовыеНастройкиКД = Неопределено
		Или НовыеПользовательскиеНастройкиКД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗначениеПараметраДанныеПоСебестоимости = НовыеНастройкиКД.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ДанныеПоСебестоимости"));
	НастройкаДанныеПоСебестоимости = НовыеПользовательскиеНастройкиКД.Элементы.Найти(ЗначениеПараметраДанныеПоСебестоимости.ИдентификаторПользовательскойНастройки);
	Если Не НастройкаДанныеПоСебестоимости = Неопределено
		И СписокВыбора.НайтиПоЗначению(НастройкаДанныеПоСебестоимости.Значение) = Неопределено Тогда
		НастройкаДанныеПоСебестоимости.Значение = СписокВыбора[0].Значение;
	КонецЕсли;
	
	ПараметрОтображатьПРВР = НовыеНастройкиКД.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ОтображатьПРВР"));
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьРеглУчет")
	 ИЛИ НЕ НастройкиНалоговУчетныхПолитикЛокализация.ВедетсяУчетПостоянныхИВременныхРазниц(Неопределено, ТекущаяДатаСеанса()) Тогда
	 	ПараметрОтображатьПРВР.Использование = Ложь;
	 	ПараметрОтображатьПРВР.Значение = Ложь;
		ПараметрОтображатьПРВР.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	КонецЕсли;
	
КонецПроцедуры

// Заполняет отбор в СКД получателе возможными отборами.
// 
// Параметры:
//	ПриемникНастроек - ОтборКомпоновкиДанных
//	ИсточникНастроек - ОтборКомпоновкиДанных
//	ВозможныеПоляУсловия - Массив - полей компоновки данных, на которые возможно наложить отбор в приемнике настроек.
//
Процедура ЗаполнитьНастройкиОтчетаРекурсивно(ПриемникНастроек, ИсточникНастроек, ВозможныеПоляУсловия)
	
	Для Каждого ЭлементОтбора Из ИсточникНастроек.Элементы Цикл
		
		Если Не ЭлементОтбора.Использование Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТипЗнч(ЭлементОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			
			НовыйЭлемент = ПриемникНастроек.Элементы.Добавить(ТипЗнч(ЭлементОтбора));
			ЗаполнитьЗначенияСвойств(НовыйЭлемент, ЭлементОтбора);
			ЗаполнитьНастройкиОтчетаРекурсивно(НовыйЭлемент, ЭлементОтбора, ВозможныеПоляУсловия);
			
		Иначе
			
			Если СтрНайти(ЭлементОтбора.ЛевоеЗначение, ".") = 0 И ВозможныеПоляУсловия.Найти(ЭлементОтбора.ЛевоеЗначение) = Неопределено
					Или СтрНайти(ЭлементОтбора.ЛевоеЗначение, ".") > 0 И ВозможныеПоляУсловия.Найти(
					Новый ПолеКомпоновкиДанных(Лев(ЭлементОтбора.ЛевоеЗначение, СтрНайти(ЭлементОтбора.ЛевоеЗначение, ".") - 1))) = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(ПриемникНастроек.Элементы.Добавить(ТипЗнч(ЭлементОтбора)), ЭлементОтбора);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ВспомогательныеПараметрыОтчета()
	
	Параметры = Новый Массив;
	Параметры.Добавить("ТолькоМатериалыДавальца");
	
	Возврат Параметры;
	
КонецФункции

Процедура НастроитьПараметрыОтборыПоФункциональнымОпциям(КомпоновщикНастроекФормы)
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристикиНоменклатуры") Тогда
		УдаляемыеПоля = Новый Массив; // Массив из Строка
		УдаляемыеПоля.Добавить(Новый ПолеКомпоновкиДанных("ХарактеристикаПродукции"));
		УдаляемыеПоля.Добавить(Новый ПолеКомпоновкиДанных("ХарактеристикаПолуфабриката"));
		УдаляемыеПоля.Добавить(Новый ПолеКомпоновкиДанных("ХарактеристикаЗатраты"));
		РасчетСебестоимостиПрикладныеАлгоритмы.ОграничитьИспользованиеПолейСКД(УдаляемыеПоля, СхемаКомпоновкиДанных,
			КомпоновщикНастроек, "", Истина);
		КомпоновкаДанныхСервер.ОчиститьСтруктуруПоФункциональнымОпциям(КомпоновщикНастроекФормы.Структура, УдаляемыеПоля);
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры") Тогда
		УдаляемыеПоля = Новый Массив; // Массив из Строка
		УдаляемыеПоля.Добавить(Новый ПолеКомпоновкиДанных("Серия"));
		РасчетСебестоимостиПрикладныеАлгоритмы.ОграничитьИспользованиеПолейСКД(УдаляемыеПоля, СхемаКомпоновкиДанных,
			КомпоновщикНастроек, "", Истина);
		КомпоновкаДанныхСервер.ОчиститьСтруктуруПоФункциональнымОпциям(КомпоновщикНастроекФормы.Структура, УдаляемыеПоля);
	КонецЕсли;
	
	ВедетсяУчетПостоянныхИВременныхРазниц = Ложь; 
	//++ НЕ УТ
	
	//++ Локализация
	ВедетсяУчетПостоянныхИВременныхРазниц = Истина;
	//-- Локализация
	
	//-- НЕ УТ
	Если НЕ ВедетсяУчетПостоянныхИВременныхРазниц Тогда
		УдаляемыеПоля = Новый Массив; // Массив из Строка
		УдаляемыеПоля.Добавить("ПостояннаяРазница");
		УдаляемыеПоля.Добавить("ВременнаяРазница");
		УдаляемыеПоля.Добавить("НалоговыйУчет");
		РасчетСебестоимостиПрикладныеАлгоритмы.ОграничитьИспользованиеПолейСКД(УдаляемыеПоля, СхемаКомпоновкиДанных,
			КомпоновщикНастроек, "", Истина);
		КомпоновкаДанныхСервер.УдалитьЭлементОтбораИзВсехНастроекОтчета(КомпоновщикНастроекФормы, УдаляемыеПоля);
		КомпоновкаДанныхСервер.УдалитьВыбранноеПолеИзВсехНастроекОтчета(КомпоновщикНастроекФормы, УдаляемыеПоля, Истина);
	КонецЕсли;
	
	Если НЕ ПолучитьФункциональнуюОпцию("УчетПоНДД") Тогда
		УдаляемыеПоля = Новый Массив; // Массив из Строка
		УдаляемыеПоля.Добавить("НДД");
		РасчетСебестоимостиПрикладныеАлгоритмы.ОграничитьИспользованиеПолейСКД(УдаляемыеПоля, СхемаКомпоновкиДанных,
			КомпоновщикНастроек, "", Истина);
		КомпоновкаДанныхСервер.УдалитьЭлементОтбораИзВсехНастроекОтчета(КомпоновщикНастроекФормы, УдаляемыеПоля);
		КомпоновкаДанныхСервер.УдалитьВыбранноеПолеИзВсехНастроекОтчета(КомпоновщикНастроекФормы, УдаляемыеПоля, Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ВывестиТекстПредупрежденияОбОграниченияхИспользованияОтчета(НачалоПериода, КонецПериода, ДокументРезультат,
	МассивОрганизаций)
	
	ТекстПредупреждения = РасчетСебестоимостиПрикладныеАлгоритмы.ТекстПредупрежденияНеподдерживаемыеОрганизации(
		НачалоПериода, КонецПериода, КомпоновщикНастроек, МассивОрганизаций);
	
	Если Не ПустаяСтрока(ТекстПредупреждения) Тогда
		
		ТаблицаПредупреждение = Новый ТабличныйДокумент;
		ОбластьПредупреждение = ТаблицаПредупреждение.Область(1,1,1,1);
		
		ОбластьПредупреждение.Текст 	 = СокрЛП(ТекстПредупреждения);
		ОбластьПредупреждение.ЦветТекста = ЦветаСтиля.ЦветОтрицательногоЧисла;
		
		ДокументРезультат.ВставитьОбласть(
		ОбластьПредупреждение,
		ДокументРезультат.Область(1,1,1,1),
		ТипСмещенияТабличногоДокумента.ПоВертикали);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура НайтиКолонкуРекурсивно(КоллекцияЭлементов, НайденныеКолонки, ЗначениеПоиска)
	Для каждого Элемент Из КоллекцияЭлементов Цикл
		Если ТипЗнч(Элемент) = Тип("ГруппировкаКомпоновкиДанных")
			ИЛИ ТипЗнч(Элемент) = Тип("ГруппировкаТаблицыКомпоновкиДанных")
			ИЛИ ТипЗнч(Элемент) = Тип("ГруппировкаДиаграммыКомпоновкиДанных") Тогда
			Для Каждого ПолеГруппировки Из Элемент.Выбор.Элементы Цикл
				Если ТипЗнч(ПолеГруппировки) <> Тип("АвтоВыбранноеПолеКомпоновкиДанных") 
						И ПолеГруппировки.Поле = ЗначениеПоиска
				Тогда
					НайденныеКолонки.Добавить(ПолеГруппировки);
				КонецЕсли;
			КонецЦикла;
			НайтиКолонкуРекурсивно(Элемент.Структура, НайденныеКолонки, ЗначениеПоиска);
		ИначеЕсли ТипЗнч(Элемент) = Тип("ТаблицаКомпоновкиДанных") Тогда
			НайтиКолонкуРекурсивно(Элемент.Колонки, НайденныеКолонки, ЗначениеПоиска);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#КонецЕсли
