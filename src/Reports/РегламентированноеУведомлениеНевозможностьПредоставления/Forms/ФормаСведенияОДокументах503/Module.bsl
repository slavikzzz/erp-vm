#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.ПорНомДок.НайтиПоЗначению("Z.ZZ") = Неопределено Тогда
		Для Каждого Элт Из Параметры.ПорНомДок Цикл 
			НовСтр = СписокДокументов.Добавить();
			НовСтр.Валидно = СтрокаСоответствуетФорматуНомераДокумента(Элт.Значение);
			НовСтр.НомерДокумента = Элт.Значение;
		КонецЦикла;
	Иначе
		ВсеДокументы = Истина;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВсеДокументыПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокДокументов

&НаКлиенте
Процедура СписокДокументовПриИзменении(Элемент)
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	ТекущиеДанные.Валидно = СтрокаСоответствуетФорматуНомераДокумента(ТекущиеДанные.НомерДокумента);
	Если Не ТекущиеДанные.Валидно Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю("Номер документа из требования не соответствует формату");
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Ок(Команда)
	ПорНомДок = Новый СписокЗначений;
	Если ВсеДокументы Тогда 
		ПорНомДок.Добавить("Z.ZZ");
		ТекстовоеОписание = "Все документы";
	Иначе
		Для Каждого Стр Из СписокДокументов Цикл
			Если ЗначениеЗаполнено(Стр.НомерДокумента) И ПорНомДок.НайтиПоЗначению(Стр.НомерДокумента) = Неопределено Тогда 
				ПорНомДок.Добавить(Стр.НомерДокумента);
			КонецЕсли;
		КонецЦикла;
		ТекстовоеОписание = ?(ПорНомДок.Количество() = 0, "", "Список из " + ПорНомДок.Количество() + " документа/ов");
	КонецЕсли;
	Закрыть(Новый Структура("ПорНомДок, ТекстовоеОписание", ПорНомДок, ТекстовоеОписание));
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть(Неопределено);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Функция СтрокаСоответствуетФорматуНомераДокумента(Стр)
	Если Не ЗначениеЗаполнено(Стр) Или Стр = "Z.ZZ" Тогда 
		Возврат Истина;
	КонецЕсли;
	Если СтрДлина(Стр) <> 4 
		Или Сред(Стр, 2, 1) <> "."
		Или Не СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Лев(Стр, 1))
		Или Не СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Прав(Стр, 2)) Тогда 
		
		Возврат Ложь;
	КонецЕсли;
	Возврат Истина;
КонецФункции

#КонецОбласти
