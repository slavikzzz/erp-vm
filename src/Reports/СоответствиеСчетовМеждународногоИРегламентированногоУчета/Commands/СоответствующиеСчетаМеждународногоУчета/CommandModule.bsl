
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	НастройкиОтчета = НастройкиОтчета(ПараметрКоманды);
	КлючНастроек = "СоответствиеСчетовМеждународногоИРегламентированногоУчетаКонтекстОтчет";
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("КлючВарианта", "СоответствиеСчетовМеждународногоИРегламентированногоУчетаКонтекст");
	ПараметрыФормы.Вставить("КлючНазначенияИспользования", КлючНастроек);
	ПараметрыФормы.Вставить("КлючПользовательскихНастроек", КлючНастроек);
	ПараметрыФормы.Вставить("ПользовательскиеНастройки", НастройкиОтчета.ПользовательскиеНастройки);
	ПараметрыФормы.Вставить("ФиксированныеНастройки", НастройкиОтчета.ФиксированныеНастройки);
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	
	ОткрытьФорму("Отчет.СоответствиеСчетовМеждународногоИРегламентированногоУчета.Форма", 
					ПараметрыФормы, 
					ПараметрыВыполненияКоманды.Источник, 
					ПараметрыВыполненияКоманды.Уникальность, 
					ПараметрыВыполненияКоманды.Окно);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция НастройкиОтчета(Счет)
	
	// Настройки контекстного отчета
	КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных;
	ИмяСхемы = Метаданные.Отчеты.СоответствиеСчетовМеждународногоИРегламентированногоУчета.ОсновнаяСхемаКомпоновкиДанных.Имя;
	СхемаОтчета = Отчеты.СоответствиеСчетовМеждународногоИРегламентированногоУчета.ПолучитьМакет(ИмяСхемы);
	НастройкиВарианта = СхемаОтчета.ВариантыНастроек.СоответствиеСчетовМеждународногоИРегламентированногоУчетаКонтекст.Настройки;
	КомпоновщикНастроек.ЗагрузитьНастройки(НастройкиВарианта);
	ПользовательскиеНастройки = КомпоновщикНастроек.ПользовательскиеНастройки;
	
	// Установим параметры контекстного отчета
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(ПользовательскиеНастройки, "СчетПланаСчетов", Счет);
	
	Возврат КомпоновщикНастроек;
	
КонецФункции

#КонецОбласти