#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - См. ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	Настройки.События.ПриСозданииНаСервере = Истина;
	Настройки.События.ПриЗагрузкеВариантаНаСервере = Истина;
	Настройки.События.ПриЗагрузкеПользовательскихНастроекНаСервере = Истина;
КонецПроцедуры

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//   ЭтаФорма - ФормаКлиентскогоПриложения - Форма отчета.
//   Отказ - Булево - Передается из параметров обработчика "как есть".
//   СтандартнаяОбработка - Булево - Передается из параметров обработчика "как есть".
//
// См. также:
//   "ФормаКлиентскогоПриложения.ПриСозданииНаСервере" в синтакс-помощнике.
//
Процедура ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт
	
	// Дополнительные команды
	КомпоновщикНастроекФормы = ЭтаФорма.Отчет.КомпоновщикНастроек;
	Параметры = ЭтаФорма.Параметры;
	
	Если Параметры.Свойство("ПараметрКоманды") Тогда
		
		ЭтаФорма.ФормаПараметры.Отбор.Вставить("ПланПроизводства", Параметры.ПараметрКоманды);
		
		Сценарий = Параметры.ПараметрКоманды.Сценарий;
		Если НЕ Сценарий.ИспользоватьДляПланированияМатериалов Тогда
			
			ТекстСообщения = НСтр("ru = 'В сценарии ""%Сценарий%"" не включен расчет плановых потребностей!';
									|en = 'The scenario ""%Сценарий%"" does not have the planned demands calculation enabled!'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Сценарий%", Сценарий);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Параметры.Свойство("Расшифровка")
		И Параметры.Расшифровка <> Неопределено Тогда
		
		ЗаменятьВариант = Ложь;
		
		Если Параметры.Расшифровка.ПрименяемыеНастройки.Структура.Количество() > 0 Тогда
			
			ПолеНоменклатураПродукции = Новый ПолеКомпоновкиДанных("НоменклатураПродукции");
			ПолеХарактеристикаПродукции = Новый ПолеКомпоновкиДанных("ХарактеристикаПродукции");
			ПолеСпецификация = Новый ПолеКомпоновкиДанных("Спецификация");
			
			ПоляГруппировки = Параметры.Расшифровка.ПрименяемыеНастройки.Структура[0].ПоляГруппировки; // ПоляГруппировкиКомпоновкиДанных - 
			
			Для каждого Группировка Из ПоляГруппировки.Элементы Цикл
			
				Если Группировка.Поле = ПолеНоменклатураПродукции 
					ИЛИ Группировка.Поле = ПолеХарактеристикаПродукции
					ИЛИ Группировка.Поле = ПолеСпецификация Тогда
				
					ЗаменятьВариант = Истина;
					Прервать;
				
				КонецЕсли; 
			
			КонецЦикла; 
		
		КонецЕсли; 
		
		ЭтоКонтекстныйОтчет = Ложь;
		
		// заполняем параметры расшифровки
		Если ЭтоАдресВременногоХранилища(Параметры.Расшифровка.Данные) Тогда
		
			ДанныеРасшифровки = ПолучитьИзВременногоХранилища(Параметры.Расшифровка.Данные);
			
			Если НЕ ДанныеРасшифровки.Настройки.ДополнительныеСвойства.Свойство("КлючТекущегоВарианта")
				ИЛИ НЕ (ДанныеРасшифровки.Настройки.ДополнительныеСвойства.КлючТекущегоВарианта = "ПлановыеТрудозатратыКонтекст"
				ИЛИ ДанныеРасшифровки.Настройки.ДополнительныеСвойства.КлючТекущегоВарианта = "Основной") Тогда
			
				Возврат;
			
			КонецЕсли; 
			ЭтоКонтекстныйОтчет = ДанныеРасшифровки.Настройки.ДополнительныеСвойства.КлючТекущегоВарианта = "ПлановыеТрудозатратыКонтекст";
			
			ПараметрПериодичность = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(ДанныеРасшифровки, "Периодичность");
			
			Если ПараметрПериодичность <> Неопределено Тогда
				КомпоновкаДанныхКлиентСервер.УстановитьПараметр(
					КомпоновщикНастроекФормы,
					"Периодичность",
					ПараметрПериодичность.Значение);
				
				КомпоновщикНастроекФормы.ПолучитьНастройки();
			КонецЕсли;
			
		КонецЕсли; 
		
		Если НЕ ЗаменятьВариант Тогда
			Возврат;
		КонецЕсли; 
		
		Если Параметры.Свойство("КлючВарианта") Тогда
			Параметры.КлючВарианта = ?(ЭтоКонтекстныйОтчет, "РасшифровкаКонтекст", "Расшифровка");
		КонецЕсли; 
		Если Параметры.Свойство("КлючНазначенияИспользования") Тогда
			Параметры.КлючНазначенияИспользования = ?(ЭтоКонтекстныйОтчет, "РасшифровкаКонтекст", "Расшифровка");
		КонецЕсли;
		
		ЭтаФорма.КлючТекущегоВарианта = ?(ЭтоКонтекстныйОтчет, "РасшифровкаКонтекст", "Расшифровка");
		
		ЭтаФорма.РежимРасшифровки = Ложь;
		ЭтаФорма.НастройкиОтчета.РазрешеноИзменятьВарианты = Ложь;
		ЭтаФорма.НастройкиОтчета.ВариантСсылка = ВариантыОтчетов.ВариантОтчета(ЭтаФорма.НастройкиОтчета.ОтчетСсылка, ЭтаФорма.КлючТекущегоВарианта);
		
	КонецЕсли;
	

КонецПроцедуры

// Вызывается в одноименном обработчике формы отчета после выполнения кода формы.
//
Процедура ПриЗагрузкеВариантаНаСервере(ЭтаФорма, НовыеНастройкиКД) Экспорт
	
	Отчет = ЭтаФорма.Отчет;
	КомпоновщикНастроекФормы = Отчет.КомпоновщикНастроек;
	
	Если КомпоновщикНастроекФормы.Настройки.ДополнительныеСвойства.Свойство("КлючТекущегоВарианта") 
		И ((КомпоновщикНастроекФормы.Настройки.ДополнительныеСвойства.КлючТекущегоВарианта = "ПлановыеТрудозатратыКонтекст"
		И ЭтаФорма.КлючТекущегоВарианта = "РасшифровкаКонтекст")
		ИЛИ (КомпоновщикНастроекФормы.Настройки.ДополнительныеСвойства.КлючТекущегоВарианта = "Основной"
		И ЭтаФорма.КлючТекущегоВарианта = "Расшифровка")) Тогда
		
		Сценарий = Неопределено;
		КомпоновщикНастроекФормы.Настройки.ДополнительныеСвойства.Свойство("Сценарий", Сценарий);
		
		ОтчетОбъект = Отчеты.ПлановаяПотребностьВТрудовыхРесурсах.Создать();
		Вариант = ОтчетОбъект.СхемаКомпоновкиДанных.ВариантыНастроек.Найти(ЭтаФорма.КлючТекущегоВарианта);
		
		КомпоновкаДанныхКлиентСервер.ЗаполнитьЭлементы(Вариант.Настройки.ПараметрыДанных,	КомпоновщикНастроекФормы.Настройки.ПараметрыДанных);
		КомпоновкаДанныхКлиентСервер.СкопироватьЭлементы(Вариант.Настройки.Отбор,			КомпоновщикНастроекФормы.Настройки.Отбор, Ложь);
		
		// Проброс параметра периодичности при расшифровке
		ПараметрПериодичность = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроекФормы, "Периодичность");
		
		КомпоновщикНастроекФормы.ЗагрузитьНастройки(Вариант.Настройки);
		
		Если ПараметрПериодичность <> Неопределено Тогда
			КомпоновкаДанныхКлиентСервер.УстановитьПараметр(
				КомпоновщикНастроекФормы,
				"Периодичность",
				ПараметрПериодичность.Значение);
		КонецЕсли;
		
		Если Сценарий <> Неопределено Тогда
			КомпоновкаДанныхКлиентСервер.УстановитьПараметр(
				КомпоновщикНастроекФормы, 
				"Сценарий", 
				Сценарий);
		
		КонецЕсли; 
		
	КонецЕсли; 
	
	КомпоновщикНастроекФормы.Настройки.ДополнительныеСвойства.Вставить("КлючТекущегоВарианта", ЭтаФорма.КлючТекущегоВарианта);
	
	Если ЭтаФорма.ФормаПараметры.Отбор.Свойство("ПланПроизводства") Тогда
		
		КомпоновщикНастроекФормы.Настройки.ДополнительныеСвойства.Вставить(
			"Сценарий", 
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭтаФорма.ФормаПараметры.Отбор.ПланПроизводства, "Сценарий"));
		
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроекФормы, 
			"Сценарий", 
			КомпоновщикНастроекФормы.Настройки.ДополнительныеСвойства.Сценарий);
		
	КонецЕсли;
	
	ПараметрПериодичность = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроекФормы, "Периодичность");
	ПараметрСценарий = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроекФормы, "Сценарий");
	
	Если ПараметрПериодичность <> Неопределено И ПараметрПериодичность.Значение = Неопределено
		И ПараметрСценарий <> Неопределено И ЗначениеЗаполнено(ПараметрСценарий.Значение) Тогда
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроекФормы,
			"Периодичность",
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрСценарий.Значение, "Периодичность"));
	КонецЕсли;
	
	НовыеНастройкиКД = КомпоновщикНастроекФормы.Настройки;
	

КонецПроцедуры

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - форма отчета.
//   НовыеПользовательскиеНастройкиКД - ПользовательскиеНастройкиКомпоновкиДанных -
//       Пользовательские настройки для загрузки в компоновщик настроек.
Процедура ПриЗагрузкеПользовательскихНастроекНаСервере(Форма, НовыеПользовательскиеНастройкиКД) Экспорт
	
	ПараметрСценарий = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(НовыеПользовательскиеНастройкиКД, "Сценарий");
	
	Если ПараметрСценарий <> Неопределено И ПараметрСценарий.Значение <> Неопределено Тогда
		
		Сценарий = ПараметрСценарий.Значение;
		
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроек,
			"Периодичность",
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Сценарий, "Периодичность"));
		
	ИначеЕсли Форма.Параметры.Свойство("ПараметрКоманды") Тогда
		
		План = Форма.Параметры.ПараметрКоманды;
		
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроек,
			"Периодичность",
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(План, "Сценарий.Периодичность"));
		
	КонецЕсли;
	
	НовыеПользовательскиеНастройкиКД = КомпоновщикНастроек.ПолучитьНастройки();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, СтандартнаяОбработка)
	
	ПараметрПериодичность = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "Периодичность");
	ПараметрСценарий = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "Сценарий");
	
	Если ПараметрПериодичность <> Неопределено И НЕ ЗначениеЗаполнено(ПараметрПериодичность.Значение)
		И ПараметрСценарий <> Неопределено И ЗначениеЗаполнено(ПараметрСценарий.Значение) Тогда
			КомпоновкаДанныхКлиентСервер.УстановитьПараметр(
				КомпоновщикНастроек,
				"Периодичность",
				ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрСценарий.Значение, "Периодичность"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли