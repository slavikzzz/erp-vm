#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область СлужебныйПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - см. ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.РазрешеноИзменятьВарианты = Ложь;
	
КонецПроцедуры

#КонецОбласти
	
#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НастройкиОсновнойСхемы = КомпоновщикНастроек.ПолучитьНастройки();
	
	ВнешниеНаборыДанных = СформироватьВнешниеНаборыДанных(НастройкиОсновнойСхемы);
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОсновнойСхемы, ДанныеРасшифровки);	
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки);
	
	ПроцессорВыводаВТабличныйДокумент = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВыводаВТабличныйДокумент.УстановитьДокумент(ДокументРезультат);	
	ПроцессорВыводаВТабличныйДокумент.Вывести(ПроцессорКомпоновкиДанных);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СформироватьВнешниеНаборыДанных(НастройкиОсновнойСхемы)
	
	Документ = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(НастройкиОсновнойСхемы, "Документ").Значение;
	Номенклатура = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(НастройкиОсновнойСхемы, "Номенклатура").Значение;
	Характеристика = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(НастройкиОсновнойСхемы, "Характеристика").Значение;
	ВариантОтчета = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(НастройкиОсновнойСхемы, "ВариантОтчета").Значение;
	
	Если ВариантОтчета = "ПередачаТоваровХранителю" Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТаблицаДокумента.Ссылка  КАК Документ,
		|	ТаблицаТовары.Количество КАК Количество
		|ИЗ
		|	Документ.ПередачаТоваровХранителю КАК ТаблицаДокумента
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПередачаТоваровХранителю.Товары КАК ТаблицаТовары
		|	ПО ТаблицаДокумента.Ссылка = ТаблицаТовары.Ссылка
		|
		|ГДЕ
		|	ТаблицаТовары.ЗаказКлиента     = &Заказ	
		|	И ТаблицаТовары.Номенклатура   = &Номенклатура
		|	И ТаблицаТовары.Характеристика = &Характеристика
		|	И ТаблицаДокумента.Проведен";
		
	ИначеЕсли ВариантОтчета = "ПоступлениеТоваровОтХранителя" Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТаблицаДокумента.Ссылка  КАК Документ,
		|	ТаблицаТовары.Количество КАК Количество
		|ИЗ
		|	Документ.ПоступлениеТоваровОтХранителя КАК ТаблицаДокумента
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровОтХранителя.Товары КАК ТаблицаТовары
		|	ПО ТаблицаДокумента.Ссылка = ТаблицаТовары.Ссылка
		|
		|ГДЕ
		|	ТаблицаДокумента.Проведен
		|	И ТаблицаТовары.Распоряжение   = &Заказ
		|	И ТаблицаТовары.Номенклатура   = &Номенклатура
		|	И ТаблицаТовары.Характеристика = &Характеристика";
	
	//++ Устарело_Переработка24
	ИначеЕсли ВариантОтчета = "ПередачаСырьяПереработчику" Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТаблицаТовары.Ссылка КАК Документ,
		|	ТаблицаТовары.Количество КАК Количество
		|ИЗ
		|	Документ.ПередачаСырьяПереработчику КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПередачаСырьяПереработчику.Товары КАК ТаблицаТовары
		|		ПО ТаблицаДокумента.Ссылка = ТаблицаТовары.Ссылка
		|ГДЕ
		|	ТаблицаДокумента.ЗаказПереработчику = &Заказ
		|	И ТаблицаДокумента.Проведен
		|	И ТаблицаТовары.Номенклатура = &Номенклатура
		|	И ТаблицаТовары.Характеристика = &Характеристика
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаТовары.Ссылка КАК Документ,
		|	-ТаблицаТовары.Количество КАК Количество
		|ИЗ
		|	Документ.ВозвратСырьяОтПереработчика КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратСырьяОтПереработчика.Товары КАК ТаблицаТовары
		|		ПО ТаблицаДокумента.Ссылка = ТаблицаТовары.Ссылка
		|ГДЕ
		|	ТаблицаДокумента.ДокументОснование = &Заказ
		|	И ТаблицаДокумента.Проведен
		|	И ТаблицаТовары.Номенклатура = &Номенклатура
		|	И ТаблицаТовары.Характеристика = &Характеристика";
		
	ИначеЕсли ВариантОтчета = "ПоступлениеОтПереработчика" Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТаблицаТовары.Ссылка КАК Документ,
		|	ТаблицаТовары.Количество КАК Количество
		|ИЗ
		|	Документ.ПоступлениеОтПереработчика КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеОтПереработчика.Товары КАК ТаблицаТовары
		|		ПО ТаблицаДокумента.Ссылка = ТаблицаТовары.Ссылка
		|ГДЕ
		|	ТаблицаДокумента.ЗаказПереработчику = &Заказ
		|	И ТаблицаДокумента.Проведен
		|	И ТаблицаТовары.Номенклатура = &Номенклатура
		|	И ТаблицаТовары.Характеристика = &Характеристика";
		
	//-- Устарело_Переработка24
	Иначе
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	НЕОПРЕДЕЛЕНО КАК Документ,
		|	0            КАК Количество
		|ГДЕ
		|	ЛОЖЬ
		|";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Заказ", Документ);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Характеристика", Характеристика);
	
	СписокДокументов = Запрос.Выполнить().Выгрузить();
	
	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить("СписокДокументов", СписокДокументов);
	
	Возврат ВнешниеНаборыДанных;

КонецФункции

#КонецОбласти

#КонецЕсли
