#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Функция ВерсияФорматаВыгрузки(Знач НаДату = Неопределено, ВыбраннаяФорма = Неопределено) Экспорт
	
	Если НаДату = Неопределено Тогда
		НаДату = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Возврат Перечисления.ВерсииФорматовВыгрузки.Версия500;
	
КонецФункции

Функция ТаблицаФормОтчета() Экспорт
	
	ОписаниеТиповСтрока = Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(254));
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("Дата"));
	ОписаниеТиповДата = Новый ОписаниеТипов(МассивТипов, , Новый КвалификаторыДаты(ЧастиДаты.Дата));
	
	ТаблицаФормОтчета = Новый ТаблицаЗначений;
	ТаблицаФормОтчета.Колонки.Добавить("ФормаОтчета",        ОписаниеТиповСтрока);
	ТаблицаФормОтчета.Колонки.Добавить("ОписаниеОтчета",     ОписаниеТиповСтрока, "Утверждена",  20);
	ТаблицаФормОтчета.Колонки.Добавить("ДатаНачалоДействия", ОписаниеТиповДата,   "Действует с", 5);
	ТаблицаФормОтчета.Колонки.Добавить("ДатаКонецДействия",  ОписаниеТиповДата,   "         по", 5);
	ТаблицаФормОтчета.Колонки.Добавить("РедакцияФормы",      ОписаниеТиповСтрока, "Редакция формы", 20);
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета        = "ФормаОтчета2016Кв2";
	НоваяФорма.ОписаниеОтчета     = "Приложение № 1 к приказу ФНС России от 12.01.2016 № ММВ-7-3/1@.";
	НоваяФорма.РедакцияФормы      = "от 12.01.2016 № ММВ-7-3/1@.";
	НоваяФорма.ДатаНачалоДействия = '2016-05-01';
	НоваяФорма.ДатаКонецДействия  = '2019-02-28';
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета        = "ФормаОтчета2019Кв1";
	НоваяФорма.ОписаниеОтчета     = "Приложение № 1 к приказу ФНС России от 21.09.2018 № ММВ-7-3/544@.";
	НоваяФорма.РедакцияФормы      = "от 21.09.2018 № ММВ-7-3/544@.";
	НоваяФорма.ДатаНачалоДействия = '2019-03-01';
	НоваяФорма.ДатаКонецДействия  = '2020-11-30';
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета        = "ФормаОтчета2025Кв1";
	НоваяФорма.ОписаниеОтчета
	= "Утверждена приказом ФНС России от 19.04.2023 № ЕД-7-3/262@ (в ред. приказа ФНС России от 03.09.2024 № ЕД-7-3/694@).";
	НоваяФорма.РедакцияФормы      = "от 03.09.2024 № ЕД-7-3/694@.";
	НоваяФорма.ДатаНачалоДействия = '2025-01-01';
	НоваяФорма.ДатаКонецДействия  = РегламентированнаяОтчетностьКлиентСервер.ПустоеЗначениеТипа(Тип("Дата"));
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета        = "ФормаОтчета2023Кв3";
	НоваяФорма.ОписаниеОтчета     = "Приложение № 1 к приказу ФНС России от 19.04.2023 № ЕД-7-3/262@.";
	НоваяФорма.РедакцияФормы      = "от 19.04.2023 № ЕД-7-3/262@.";
	НоваяФорма.ДатаНачалоДействия = '2023-07-01';
	НоваяФорма.ДатаКонецДействия  = '2024-12-31';
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета        = "ФормаОтчета2020Кв4";
	НоваяФорма.ОписаниеОтчета     = "Приложение № 1 к приказу ФНС России от 27.08.2020 № ЕД-7-3/610@.";
	НоваяФорма.РедакцияФормы      = "от 27.08.2020 № ЕД-7-3/610@.";
	НоваяФорма.ДатаНачалоДействия = '2020-12-01';
	НоваяФорма.ДатаКонецДействия  = '2023-08-31';
	
	Возврат ТаблицаФормОтчета;
	
КонецФункции

Функция ДанныеРеглОтчета(ЭкземплярРеглОтчета) Экспорт
	
	ТаблицаДанныхРеглОтчета = ИнтерфейсыВзаимодействияБРО.НовыйТаблицаДанныхРеглОтчета();
	
	ДанныеРеглОтчета = ЭкземплярРеглОтчета.ДанныеОтчета.Получить();
	Если ТипЗнч(ДанныеРеглОтчета) <> Тип("Структура") Тогда
		Возврат ТаблицаДанныхРеглОтчета;
	КонецЕсли;
	
	Если ЭкземплярРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2025Кв1"
		ИЛИ ЭкземплярРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2023Кв3"
		ИЛИ ЭкземплярРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2020Кв4"
		ИЛИ ЭкземплярРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2019Кв1" Тогда
		
		Возврат ТаблицаДанныхРеглОтчета;
		
	ИначеЕсли ЭкземплярРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2016Кв2" Тогда
		
		Если ДанныеРеглОтчета.ПоказателиОтчета.Свойство("ПолеТабличногоДокументаРаздел11")
			И ДанныеРеглОтчета.ДанныеМногострочныхРазделов.Свойство("П0001100001") Тогда
			
			ТЗ_Р11 = ДанныеРеглОтчета.ДанныеМногострочныхРазделов.П0001100001;
			
			Если ДанныеРеглОтчета.ПоказателиОтчета.ПолеТабличногоДокументаРаздел11.Свойство("П000110001003")
				И ТЗ_Р11.Колонки.Найти("П000110000101") <> Неопределено
				И ТЗ_Р11.Колонки.Найти("П000110000102") <> Неопределено Тогда
				
				ОКТМО = ДанныеРеглОтчета.ПоказателиОтчета.ПолеТабличногоДокументаРаздел11.П000110001003;
				
				Для Каждого Стр_ТЗ_Р11 Из ТЗ_Р11 Цикл
					Сумма = ТаблицаДанныхРеглОтчета.Добавить();
					Сумма.Период = ЭкземплярРеглОтчета.ДатаНачала;
					Сумма.ОКАТО  = ОКТМО;
					Сумма.КБК    = Стр_ТЗ_Р11.П000110000101;
					Сумма.Сумма  = Стр_ТЗ_Р11.П000110000102;
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ТаблицаДанныхРеглОтчета;
	
КонецФункции

Функция ДеревоФормИФорматов() Экспорт
	
	ФормыИФорматы = Новый ДеревоЗначений;
	ФормыИФорматы.Колонки.Добавить("Код");
	ФормыИФорматы.Колонки.Добавить("ДатаПриказа");
	ФормыИФорматы.Колонки.Добавить("НомерПриказа");
	ФормыИФорматы.Колонки.Добавить("ДатаНачалаДействия");
	ФормыИФорматы.Колонки.Добавить("ДатаОкончанияДействия");
	ФормыИФорматы.Колонки.Добавить("ИмяОбъекта");
	ФормыИФорматы.Колонки.Добавить("Описание");
	
	Форма2025Кв1 = ОпределитьФормуВДеревеФормИФорматов(ФормыИФорматы,
		"1151090", '2024-09-03', "ЕД-7-3/694@", "ФормаОтчета2025Кв1");
	ОпределитьФорматВДеревеФормИФорматов(Форма2025Кв1, "5.05");
	
	Форма2023Кв3 = ОпределитьФормуВДеревеФормИФорматов(ФормыИФорматы,
		"1151090", '2023-04-19', "ЕД-7-3/262@", "ФормаОтчета2023Кв3");
	ОпределитьФорматВДеревеФормИФорматов(Форма2023Кв3, "5.04");
	
	Форма2020Кв4 = ОпределитьФормуВДеревеФормИФорматов(ФормыИФорматы,
		"1151090", '2020-08-27', "ЕД-7-3/610@", "ФормаОтчета2020Кв4");
	ОпределитьФорматВДеревеФормИФорматов(Форма2020Кв4, "5.03");
	
	Форма2019Кв1 = ОпределитьФормуВДеревеФормИФорматов(ФормыИФорматы,
		"1151090", '2018-09-21', "ММВ-7-3/544@", "ФормаОтчета2019Кв1");
	ОпределитьФорматВДеревеФормИФорматов(Форма2019Кв1, "5.02");
	
	Форма2016Кв2 = ОпределитьФормуВДеревеФормИФорматов(ФормыИФорматы,
		"1151090", '2016-01-12', "ММВ-7-3/1@", "ФормаОтчета2016Кв2");
	ОпределитьФорматВДеревеФормИФорматов(Форма2016Кв2, "5.01");
	
	Возврат ФормыИФорматы;
	
КонецФункции

Функция ОпределитьФормуВДеревеФормИФорматов(ДеревоФормИФорматов, Код, ДатаПриказа = '00010101', НомерПриказа = "",
			ИмяОбъекта = "", ДатаНачалаДействия = '00010101', ДатаОкончанияДействия = '00010101', Описание = "")
	
	НовСтр = ДеревоФормИФорматов.Строки.Добавить();
	НовСтр.Код = СокрЛП(Код);
	НовСтр.ДатаПриказа = ДатаПриказа;
	НовСтр.НомерПриказа = СокрЛП(НомерПриказа);
	НовСтр.ДатаНачалаДействия = ДатаНачалаДействия;
	НовСтр.ДатаОкончанияДействия = ДатаОкончанияДействия;
	НовСтр.ИмяОбъекта = СокрЛП(ИмяОбъекта);
	НовСтр.Описание = СокрЛП(Описание);
	Возврат НовСтр;
	
КонецФункции

Функция ОпределитьФорматВДеревеФормИФорматов(Форма, Версия, ДатаПриказа = '00010101', НомерПриказа = "",
			ДатаНачалаДействия = Неопределено, ДатаОкончанияДействия = Неопределено, ИмяОбъекта = "", Описание = "")
	
	НовСтр = Форма.Строки.Добавить();
	НовСтр.Код = СокрЛП(Версия);
	НовСтр.ДатаПриказа = ДатаПриказа;
	НовСтр.НомерПриказа = СокрЛП(НомерПриказа);
	НовСтр.ДатаНачалаДействия = ?(ДатаНачалаДействия = Неопределено, Форма.ДатаНачалаДействия, ДатаНачалаДействия);
	НовСтр.ДатаОкончанияДействия = ?(ДатаОкончанияДействия = Неопределено, Форма.ДатаОкончанияДействия, ДатаОкончанияДействия);
	НовСтр.ИмяОбъекта = СокрЛП(ИмяОбъекта);
	НовСтр.Описание = СокрЛП(Описание);
	Возврат НовСтр;
	
КонецФункции

#Область ПоказателиОценкиРискаВыезднойНалоговойПроверки

Процедура ПоказателиОценкиРискаВыезднойНалоговойПроверки(ТаблицаПоказателей, ОписаниеОтчета) Экспорт 
	
	СведенияОтчета = ОписаниеОтчета.РегламентированныйОтчет.ДанныеОтчета.Получить();
	
	ЗначениеПоказателя = 0;
	
	Если ОписаниеОтчета.ВыбраннаяФорма = "ФормаОтчета2025Кв1"
		ИЛИ ОписаниеОтчета.ВыбраннаяФорма = "ФормаОтчета2023Кв3"
		ИЛИ ОписаниеОтчета.ВыбраннаяФорма = "ФормаОтчета2020Кв4"
		ИЛИ ОписаниеОтчета.ВыбраннаяФорма = "ФормаОтчета2019Кв1" Тогда
		
	ИначеЕсли ОписаниеОтчета.ВыбраннаяФорма = "ФормаОтчета2016Кв2" Тогда
		
		ИмяРаздела = "Раздел11";
		Если СведенияОтчета.Свойство("ОкружениеСохранения") Тогда // отчет сохранен в 2.0
			ИмяРаздела = "Раздел1";
		КонецЕсли;
		
		Раздел11 = СведенияОтчета.ПоказателиОтчета["ПолеТабличногоДокумента" + ИмяРаздела];
		
		НомерСтроки = 1;
		Постфикс = "_" + РегламентированнаяОтчетность.ЧислоВСтрокуЧГ0(НомерСтроки);
		Пока Раздел11.Свойство("П000110000102" + Постфикс) Цикл
			ЗначениеПоказателя = ЗначениеПоказателя
			                   + РегламентированнаяОтчетность.ПоказательОтчета(Раздел11, "П000110000102" + Постфикс)
			                   - РегламентированнаяОтчетность.ПоказательОтчета(Раздел11, "П000110000103" + Постфикс);
			НомерСтроки = НомерСтроки + 1;
			Постфикс = "_" + РегламентированнаяОтчетность.ЧислоВСтрокуЧГ0(НомерСтроки);
		КонецЦикла;
		
	КонецЕсли;
	
	НоваяСтрока = ТаблицаПоказателей.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока, ОписаниеОтчета);
	НоваяСтрока.Показатель = "СуммаАкцизовАлкоголь";
	НоваяСтрока.ЗначениеПоказателя = ЗначениеПоказателя;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли