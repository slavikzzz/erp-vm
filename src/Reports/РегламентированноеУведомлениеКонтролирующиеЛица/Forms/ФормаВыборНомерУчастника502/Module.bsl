&НаКлиенте
Перем Оповещение Экспорт;

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура ОК(Команда)
	Если Номер = 0 И РосИн < 3 Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю("Необходимо указать номер участника");
		Возврат;
	КонецЕсли;
	
	Результат = 0;
	Если ВводМногострочнойЧасти Тогда 
		Если РосИн = 0 Тогда 
			Результат = "УИ-" + Формат(Номер, "ЧЦ=5; ЧВН=; ЧГ=");
		ИначеЕсли РосИн = 1 Тогда 
			Результат = "УР-" + Формат(Номер, "ЧЦ=5; ЧВН=; ЧГ=");
		ИначеЕсли РосИн = 2 Тогда 
			Результат = "УС-" + Формат(Номер, "ЧЦ=5; ЧВН=; ЧГ=");
		ИначеЕсли РосИн = 3 Тогда 
			Результат = "МК-00000";
		ИначеЕсли РосИн = 4 Тогда 
			Результат = "РК-00000";
		КонецЕсли;
	Иначе 
		Если РосИн = 0 Тогда 
			Результат = "ИО-" + Формат(Номер, "ЧЦ=5; ЧВН=; ЧГ=");
		ИначеЕсли РосИн = 1 Тогда 
			Результат = "РО-" + Формат(Номер, "ЧЦ=5; ЧВН=; ЧГ=");
		ИначеЕсли РосИн = 2 Тогда 
			Результат = "ФЛ-" + Формат(Номер, "ЧЦ=5; ЧВН=; ЧГ=");
		КонецЕсли;
	КонецЕсли;
	Закрыть(Новый Структура("ИмяОбласти, Результат", ИмяОбласти, Результат));
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть(Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура Очистить(Команда)
	Закрыть(Новый Структура("ИмяОбласти, Очистить", ИмяОбласти, 0));
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;

	Параметры.Свойство("ВводМногострочнойЧасти", ВводМногострочнойЧасти);
	Если Параметры.Свойство("ЗначениеОбласти") Тогда
		ЗначениеОбласти = Параметры.ЗначениеОбласти;
	КонецЕсли;
	Если Параметры.Свойство("ИмяОбласти") Тогда
		ИмяОбласти = Параметры.ИмяОбласти;
	КонецЕсли;
	
	Если ВводМногострочнойЧасти Тогда 
		Элементы.РосИн.СписокВыбора.Добавить(0, "иностранная организация");
		Элементы.РосИн.СписокВыбора.Добавить(1, "российская организация");
		Элементы.РосИн.СписокВыбора.Добавить(2, "иностранная структура");
		Элементы.РосИн.СписокВыбора.Добавить(3, "последний участника в последовательности (международной компании)");
		Элементы.РосИн.СписокВыбора.Добавить(4, "последний участника в последовательности (организации, указанной в пункте 1.1 статьи 24.2 Кодекса)");
	Иначе 
		Элементы.РосИн.СписокВыбора.Добавить(0, "иностранная организация");
		Элементы.РосИн.СписокВыбора.Добавить(1, "российская организация");
		Элементы.РосИн.СписокВыбора.Добавить(2, "физическое лицо");
	КонецЕсли;
	
	Если Параметры.Свойство("ФиксированныйТип") Тогда 
		РосИн = Параметры.ФиксированныйТип;
		Элементы.РосИн.Доступность = Ложь;
	Иначе 
		Тип = Лев(ЗначениеОбласти, 3);
		Если Тип = "ИО-" Или Тип = "УИ-" Тогда 
			РосИн = 0;
		ИначеЕсли Тип = "РО-" Или Тип = "УР-" Тогда 
			РосИн = 1;
		ИначеЕсли Тип = "ФЛ-" Или Тип = "УС-" Тогда 
			РосИн = 2;
		ИначеЕсли Тип = "МК-" Тогда 
			РосИн = 3;
		ИначеЕсли Тип = "РК-" Тогда 
			РосИн = 4;
		КонецЕсли;
	КонецЕсли;
	
	ОписаниеТипов = Новый ОписаниеТипов("Число");
	Номер = ОписаниеТипов.ПривестиЗначение(Сред(ЗначениеОбласти, 4));
КонецПроцедуры
#КонецОбласти
