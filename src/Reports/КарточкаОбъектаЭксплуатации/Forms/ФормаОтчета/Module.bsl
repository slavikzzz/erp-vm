
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("ПараметрКоманды") Тогда
		
		ТипПараметра = ТипЗнч(Параметры.ПараметрКоманды);
		
		Если ТипПараметра = Тип("Массив")
			Или ТипПараметра = Тип("СправочникСсылка.ОбъектыЭксплуатации")
			Или ТипПараметра = Тип("СправочникСсылка.УзлыОбъектовЭксплуатации") Тогда
			
			ЗаполнитьОбъектыОтчета(Параметры.ПараметрКоманды);
			
		Иначе
			
			Отказ = Истина;
			
		КонецЕсли;
		
	Иначе
		
		Отказ = Истина;
		
	КонецЕсли;
	
	Если ОбъектыОтчета.Количество() = 0 Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		ЗагрузитьНастройки();
		СформироватьОтчет();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сформировать(Команда)
	
	СохранитьНастройки();
	ОчиститьСообщения();
	СформироватьОтчет();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьОбъектыОтчета(Параметр)
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ОбъектыЭксплуатации.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ОбъектыЭксплуатации КАК ОбъектыЭксплуатации
		|ГДЕ
		|	ОбъектыЭксплуатации.Ссылка В(&Параметр)
		|	И НЕ ОбъектыЭксплуатации.ПометкаУдаления
		|	И НЕ ОбъектыЭксплуатации.ЭтоГруппа
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	УзлыОбъектовЭксплуатации.Ссылка
		|ИЗ
		|	Справочник.УзлыОбъектовЭксплуатации КАК УзлыОбъектовЭксплуатации
		|ГДЕ
		|	УзлыОбъектовЭксплуатации.Ссылка В(&Параметр)
		|	И НЕ УзлыОбъектовЭксплуатации.ПометкаУдаления");
	Запрос.УстановитьПараметр("Параметр", Параметр);
	
	ОбъектыОтчета.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры

&НаСервере
Процедура СформироватьОтчет()
	
	ТаблицаОтчета.Очистить();
	
	ОбъектОтчет = РеквизитФормыВЗначение("Отчет");
	
	Для Каждого ОбъектЭксплуатации Из ОбъектыОтчета Цикл
		
		ОбъектОтчет.СформироватьОтчет(ТаблицаОтчета, ОбъектЭксплуатации.Значение)
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройки()

	Перем Настройки;
	
	Настройки = Новый Структура(
		"ПаспортныеХарактеристики,
		|ПодчиненныеУзлы,
		|Наработки,
		|ТекущиеРемонты,
		|ЗакрытыеРемонты,
		|ТекущиеДефекты,
		|ЗакрытыеДефекты");
	ЗаполнитьЗначенияСвойств(Настройки, Отчет);
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("Отчет.КарточкаОбъектаЭксплуатации", "ФормаОтчета", Настройки);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьНастройки()
	
	Перем ЗначениеНастроек;
	ЗначениеНастроек = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("Отчет.КарточкаОбъектаЭксплуатации", "ФормаОтчета");
	
	Если ЗначениеНастроек = Неопределено Тогда
		
		Отчет.ПаспортныеХарактеристики = Истина;
		Отчет.ПодчиненныеУзлы = Истина;
		Отчет.Наработки = Истина;
		Отчет.ТекущиеРемонты = Истина;
		Отчет.ЗакрытыеРемонты = Истина;
		Отчет.ТекущиеДефекты = Истина;
		Отчет.ЗакрытыеДефекты = Истина;
		
	ИначеЕсли ТипЗнч(ЗначениеНастроек) = Тип("Структура") Тогда
		
		ЗаполнитьЗначенияСвойств(Отчет, ЗначениеНастроек);
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
