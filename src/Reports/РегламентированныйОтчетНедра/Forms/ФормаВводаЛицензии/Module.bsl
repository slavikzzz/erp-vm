
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура Очистить(Команда)
	
	ПолеВводаСерия = "";
	ПолеВводаНомер = "";
	ПолеВводаВид   = "";
	
	СформироватьПредставление();
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьПредставление()
	
	Серия = СокрЛП(ПолеВводаСерия);
	Номер = СокрЛП(ПолеВводаНомер);
	Вид   = СокрЛП(ПолеВводаВид);
	
	Если НЕ ЗначениеЗаполнено(Серия) И НЕ ЗначениеЗаполнено(Вид) И НЕ ЗначениеЗаполнено(ПолеВводаНомер) Тогда
		
		ПолеВводаПредставление = "";
		
	Иначе
		
		Серия = РегламентированнаяОтчетностьКлиентСервер.ДополнитьСтроку(Серия, 3, "0");
		
		Если ВерсияФормата = "3.00004" Тогда
			
			Номер = РегламентированнаяОтчетностьКлиентСервер.ДополнитьСтроку(Номер, 6, "0");
			
		Иначе
			
			Номер = РегламентированнаяОтчетностьКлиентСервер.ДополнитьСтроку(Номер, 5, "0");
			
		КонецЕсли;
		
		Вид = РегламентированнаяОтчетностьКлиентСервер.ДополнитьСтроку(Вид, 2, "0");
		
		ПолеВводаПредставление = Серия + "" + Номер + "" + Вид;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеВводаСерияПриИзменении(Элемент)
	
	СформироватьПредставление();
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеВводаНомерПриИзменении(Элемент)
	
	Если ВерсияФормата <> "3.00004" И СтрДлина(СокрЛП(ПолеВводаНомер)) > 5 Тогда
		
		ПолеВводаНомер = "";
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Введите номер лицензии, состоящий не более чем из 5 цифр';
								|en = 'Введите номер лицензии, состоящий не более чем из 5 цифр'");
		Сообщение.Сообщить();
		
	КонецЕсли;
	
	СформироватьПредставление();
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеВводаВидПриИзменении(Элемент)
	
	СформироватьПредставление();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	НачальноеЗначениеВыбора = Параметры.НачальноеЗначениеВыбора;
	ВерсияФормата = Параметры.ВерсияФормата;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность И Не БылаНажатаКнопкаЗакрыть Тогда
		
		Отказ = Истина;
		
		Если ЗавершениеРаботы Тогда
			
			ТекстПредупреждения = НСтр(
			"ru = 'Перед завершением работы рекомендуется сохранить измененные данные, иначе изменения будут утеряны';
			|en = 'Перед завершением работы рекомендуется сохранить измененные данные, иначе изменения будут утеряны'");
			
			Возврат;
			
		КонецЕсли;
		
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Отказаться от изменений?';
							|en = 'Данные были изменены. Отказаться от изменений?'");
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса,  РежимДиалогаВопрос.ОКОтмена);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.ОК Тогда
		
		Модифицированность = Ложь;
		
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если НЕ БылаНажатаКнопкаЗакрыть Тогда
		
		Возврат;
		
	КонецЕсли;
	
	НачальноеЗначениеВыбора = ПолеВводаПредставление;
	
КонецПроцедуры

&НаКлиенте
Процедура Ввести(Команда)
	
	БылаНажатаКнопкаЗакрыть = Истина;
	
	Закрыть(ПолеВводаПредставление);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗначениеЗаполнено(НачальноеЗначениеВыбора) Тогда
		
		Если ВерсияФормата = "3.00004" Тогда
			
			Если СтрДлина(НачальноеЗначениеВыбора) = 11 Тогда
				
				ПолеВводаСерия = Сред(НачальноеЗначениеВыбора,  1, 3);
				ПолеВводаНомер = Сред(НачальноеЗначениеВыбора,  4, 6);
				ПолеВводаВид   = Сред(НачальноеЗначениеВыбора, 10, 2);
				
				Если НЕ СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ПолеВводаНомер) Тогда
					
					ПолеВводаПредставление = НачальноеЗначениеВыбора;
					
					Сообщение = Новый СообщениеПользователю;
					Сообщение.Текст = НСтр("ru = 'Введите номер лицензии, состоящий из цифр';
											|en = 'Введите номер лицензии, состоящий из цифр'");
					Сообщение.Сообщить();
					
					ЭтаФорма.ТекущийЭлемент = ЭтаФорма.ПолеВводаНомер;
					
				Иначе
					
					ПолеВводаПредставление = ПолеВводаСерия + "" + ПолеВводаНомер + "" + ПолеВводаВид;
					
				КонецЕсли;
				
			Иначе
				
				ПолеВводаПредставление = НачальноеЗначениеВыбора;
				
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = НСтр(
				"ru = 'Введите серию, номер и вид лицензии в соответствии с правилами ввода реквизитов лицензии (11 символов без разделителей)';
				|en = 'Введите серию, номер и вид лицензии в соответствии с правилами ввода реквизитов лицензии (11 символов без разделителей)'");
				Сообщение.Сообщить();
				
			КонецЕсли;
			
		Иначе// "3.00003", "3.00002"
			
			Если СтрДлина(НачальноеЗначениеВыбора) = 10 Тогда
				
				ПолеВводаСерия = Сред(НачальноеЗначениеВыбора, 1, 3);
				ПолеВводаНомер = Сред(НачальноеЗначениеВыбора, 4, 5);
				ПолеВводаВид   = Сред(НачальноеЗначениеВыбора, 9, 2);
				
				ПолеВводаПредставление = ПолеВводаСерия + "" + ПолеВводаНомер + "" + ПолеВводаВид;
				
			Иначе
				
				ПолеВводаПредставление = НачальноеЗначениеВыбора;
				
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = НСтр(
				"ru = 'Введите серию, номер и вид лицензии в соответствии с правилами ввода реквизитов лицензии (10 символов без разделителей)';
				|en = 'Введите серию, номер и вид лицензии в соответствии с правилами ввода реквизитов лицензии (10 символов без разделителей)'");
				Сообщение.Сообщить();
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти