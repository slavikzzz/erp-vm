#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ПодключаемыеКоманды

// Определяет состав программного интерфейса для интеграции с конфигурацией.
//
// Параметры:
//   Настройки - Структура - Настройки интеграции этого объекта.
//       Описание см. в модуле ПодключаемыеКомандыПереопределяемый,
//       комментарий к процедуре ПриОпределенииСоставаНастроекПодключаемыхОбъектов,
//       раздел "Параметры процедуры ПриОпределенииНастроек".
//
Процедура ПриОпределенииНастроек(Настройки) Экспорт
	
	Настройки.Вставить("НастроитьВариантыОтчета", Истина);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.ВариантыОтчетов

// См. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.
//
Процедура НастроитьВариантыОтчета(Настройки, НастройкиОтчета) Экспорт
	
	НастройкиОтчета.ОпределитьНастройкиФормы = Истина;
	
	НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "АналитическийОтчетПоРеестрам");
	
	НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "РеестрПолучателейВыплат");
	НастройкиВарианта.Включен = Ложь;
	НастройкиВарианта.ВидимостьПоУмолчанию = Ложь;
	
	НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "РеестрПолучателейВыплатОтдельнымКатегориям");
	НастройкиВарианта.Включен = Ложь;
	НастройкиВарианта.ВидимостьПоУмолчанию = Ложь;	
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВариантыОтчетов

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "РеестрПолучателейВыплат") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"РеестрПолучателейВыплат",
			НСтр("ru = 'Реестр получателей выплат';
				|en = 'Payee registry'"),
			ПечатьФормы(МассивОбъектов, ОбъектыПечати));
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПечатьФормы(СписокДокументов, ОбъектыПечати) 
	
	ОтчетОбъект = Отчеты.РеестрыСтимулирующихВыплатМедицинскимИСоциальнымРаботникам.Создать();
	
	ДокументСсылка = СписокДокументов[0];
	
	ОтдельнымКатегориям = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "СоциальныеВыплатыОтдельнымКатегориямМедРаботников");
	ИмяВарианта = ?(ОтдельнымКатегориям, "РеестрПолучателейВыплатОтдельнымКатегориям", "РеестрПолучателейВыплат");
	
	ВариантОтчета = ОтчетОбъект.СхемаКомпоновкиДанных.ВариантыНастроек.Найти(ИмяВарианта);
	Если ВариантОтчета= Неопределено Тогда
		Возврат Новый ТабличныйДокумент;
	КонецЕсли;
	
	НастройкиОтчета = ВариантОтчета.Настройки;
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Документ", ДокументСсылка);
	СтруктураПараметров.Вставить("ВидПособия", ДокументСсылка.ВидПособия);
	
	Для Каждого ПараметрЗаполнения Из СтруктураПараметров Цикл
		
		ПараметрКомпоновкиДанных = Новый ПараметрКомпоновкиДанных(ПараметрЗаполнения.Ключ);
		ЗначениеПараметра = НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(ПараметрКомпоновкиДанных);
		Если ЗначениеПараметра <> Неопределено Тогда
			ЗначениеПараметра.Значение = ПараметрЗаполнения.Значение;
			ЗначениеПараметра.Использование = Истина;
		Иначе
			НовыйПараметр = НастройкиОтчета.ПараметрыДанных.Элементы.Добавить();
			НовыйПараметр.Параметр = ПараметрКомпоновкиДанных;
			НовыйПараметр.Значение = ПараметрЗаполнения.Значение;
			НовыйПараметр.Использование = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	ОтчетОбъект.КомпоновщикНастроек.ЗагрузитьНастройки(НастройкиОтчета);
	
	ДокументРезультат = Новый ТабличныйДокумент;
	ОтчетОбъект.СкомпоноватьРезультат(ДокументРезультат);
	Возврат ДокументРезультат;
	
КонецФункции

#КонецОбласти

#КонецЕсли