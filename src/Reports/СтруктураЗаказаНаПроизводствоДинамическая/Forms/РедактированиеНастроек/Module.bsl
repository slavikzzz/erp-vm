
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Перем ПараметрыПеремещенияТоваров;
	
	Если Не Параметры.Свойство("ПараметрыПеремещенияТоваров", ПараметрыПеремещенияТоваров) Или ТипЗнч(ПараметрыПеремещенияТоваров) <> Тип("Структура") Тогда
		
		Шаблон = НСтр("ru = 'Не передан служебный параметр %Парам1%';
						|en = 'Internal parameter %Парам1% is not passed'");
		
		Шаблон = СтрЗаменить(Шаблон, "%Парам1%", "ПараметрыПеремещенияТоваров");
		
		ВызватьИсключение Шаблон;
		
	КонецЕсли;
	
	Если Не ПараметрыПеремещенияТоваров.Свойство("ВариантОформления", ВариантОформления) Тогда
		
		ВариантОформления = Перечисления.ВариантыОформленияДокументовПеремещения.ПеремещениеТоваров;
		
	КонецЕсли;
	
	Если Не ПараметрыПеремещенияТоваров.Свойство("ТолькоВНаличии", ТолькоВНаличии) Тогда
		
		ТолькоВНаличии = Истина;
		
	КонецЕсли;
	
	НастроитьЭлементыФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВариантОформленияПриИзменении(Элемент)
	
	ТолькоВНаличии = ?(ВариантОформления = ПредопределенноеЗначение("Перечисление.ВариантыОформленияДокументовПеремещения.ПеремещениеТоваров"), Истина, ТолькоВНаличии);
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма, "ВариантОформления");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Применить(Команда)
	
	Результат = Новый Структура();
	
	ПараметрыПеремещенияТоваров = Новый Структура("ВариантОформления, ТолькоВНаличии", ВариантОформления, ТолькоВНаличии);
	Результат.Вставить("ПараметрыПеремещенияТоваров", ПараметрыПеремещенияТоваров);
	
	Закрыть(Результат);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

&НаСервере
Процедура НастроитьЭлементыФормы()
	
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЗависимыеЭлементыФормы(Форма, СписокРеквизитов = "")
	
	Элементы = Форма.Элементы;

	Инициализация = ПустаяСтрока(СписокРеквизитов);
	СтруктураРеквизитов = Новый Структура(СписокРеквизитов);
	
	Если СтруктураРеквизитов.Свойство("ВариантОформления")
		ИЛИ Инициализация Тогда
		Элементы.ТолькоВНаличии.Доступность = Форма.ВариантОформления = ПредопределенноеЗначение("Перечисление.ВариантыОформленияДокументовПеремещения.ЗаказНаПеремещение");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
