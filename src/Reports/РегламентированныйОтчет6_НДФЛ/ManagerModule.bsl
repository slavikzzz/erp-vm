#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ВерсияФорматаВыгрузки(Знач НаДату = Неопределено, ВыбраннаяФорма = Неопределено) Экспорт
	
	Если НаДату = Неопределено Тогда
		НаДату = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Возврат Перечисления.ВерсииФорматовВыгрузки.Версия500;
	
КонецФункции

Функция ТаблицаФормОтчета() Экспорт
	
	ОписаниеТиповСтрока = Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(254));
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("Дата"));
	ОписаниеТиповДата = Новый ОписаниеТипов(МассивТипов, , Новый КвалификаторыДаты(ЧастиДаты.Дата));
	
	ТаблицаФормОтчета = Новый ТаблицаЗначений;
	ТаблицаФормОтчета.Колонки.Добавить("ФормаОтчета",        ОписаниеТиповСтрока);
	ТаблицаФормОтчета.Колонки.Добавить("ОписаниеОтчета",     ОписаниеТиповСтрока, "Утверждена",  20);
	ТаблицаФормОтчета.Колонки.Добавить("ДатаНачалоДействия", ОписаниеТиповДата,   "Действует с", 5);
	ТаблицаФормОтчета.Колонки.Добавить("ДатаКонецДействия",  ОписаниеТиповДата,   "         по", 5);
	ТаблицаФормОтчета.Колонки.Добавить("РедакцияФормы",      ОписаниеТиповСтрока, "Редакция формы", 20);
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета        = "ФормаОтчета2024Кв2";
	НоваяФорма.ОписаниеОтчета
	= "Утверждена приказом ФНС России от 19.09.2023 № ЕД-7-11/649@ (в ред. приказа ФНС России от 09.01.2024 № ЕД-7-11/1@).";
	НоваяФорма.РедакцияФормы      = "от 09.01.2024 № ЕД-7-11/1@.";
	НоваяФорма.ДатаНачалоДействия = '20240101';
	НоваяФорма.ДатаКонецДействия  = РегламентированнаяОтчетностьКлиентСервер.ПустоеЗначениеТипа(Тип("Дата"));
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета        = "ФормаОтчета2024Кв1";
	НоваяФорма.ОписаниеОтчета     = "Утверждена приказом ФНС России от 19.09.2023 № ЕД-7-11/649@.";
	НоваяФорма.РедакцияФормы      = "от 19.09.2023 № ЕД-7-11/649@.";
	НоваяФорма.ДатаНачалоДействия = '20240101';
	НоваяФорма.ДатаКонецДействия  = '20240630';
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета        = "ФормаОтчета2023Кв1";
	НоваяФорма.ОписаниеОтчета
	= "Приложение № 1 к приказу ФНС России от 15.10.2020 № ЕД-7-11/753@ (в ред. приказа ФНС России от 29.09.2022 № ЕД-7-11/881@).";
	НоваяФорма.РедакцияФормы      = "от 29.09.2022 № ЕД-7-11/881@.";
	НоваяФорма.ДатаНачалоДействия = '20230101';
	НоваяФорма.ДатаКонецДействия  = '20231231';
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета        = "ФормаОтчета2021Кв4";
	НоваяФорма.ОписаниеОтчета
	= "Приложение № 1 к приказу ФНС России от 15.10.2020 № ЕД-7-11/753@ (в ред. приказа ФНС России от 28.09.2021 № ЕД-7-11/845@).";
	НоваяФорма.РедакцияФормы      = "от 28.09.2021 № ЕД-7-11/845@.";
	НоваяФорма.ДатаНачалоДействия = '20211001';
	НоваяФорма.ДатаКонецДействия  = '20221231';
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета        = "ФормаОтчета2021Кв1";
	НоваяФорма.ОписаниеОтчета     = "Приложение № 1 к приказу ФНС России от 15.10.2020 № ЕД-7-11/753@.";
	НоваяФорма.РедакцияФормы      = "от 15.10.2020 № ЕД-7-11/753@.";
	НоваяФорма.ДатаНачалоДействия = '20210101';
	НоваяФорма.ДатаКонецДействия  = '20211231';
	
	Возврат ТаблицаФормОтчета;
	
КонецФункции

Функция ДанныеРеглОтчета(ЭкземплярРеглОтчета) Экспорт
	
	Возврат Неопределено;
	
КонецФункции

Функция ДеревоФормИФорматов() Экспорт
	
	ФормыИФорматы = Новый ДеревоЗначений;
	ФормыИФорматы.Колонки.Добавить("Код");
	ФормыИФорматы.Колонки.Добавить("ДатаПриказа");
	ФормыИФорматы.Колонки.Добавить("НомерПриказа");
	ФормыИФорматы.Колонки.Добавить("ДатаНачалаДействия");
	ФормыИФорматы.Колонки.Добавить("ДатаОкончанияДействия");
	ФормыИФорматы.Колонки.Добавить("ИмяОбъекта");
	ФормыИФорматы.Колонки.Добавить("Описание");
	
	ФормаОтчета2024Кв2 = ОпределитьФормуВДеревеФормИФорматов(ФормыИФорматы,
	"1151100", '2024-01-09', "ЕД-7-11/1@", "ФормаОтчета2024Кв2");
	ОпределитьФорматВДеревеФормИФорматов(ФормаОтчета2024Кв2, "5.05");
	
	ФормаОтчета2024Кв1 = ОпределитьФормуВДеревеФормИФорматов(ФормыИФорматы,
	"1151100", '2023-09-19', "ЕД-7-11/649@", "ФормаОтчета2024Кв1");
	ОпределитьФорматВДеревеФормИФорматов(ФормаОтчета2024Кв1, "5.04");
	
	ФормаОтчета2023Кв1 = ОпределитьФормуВДеревеФормИФорматов(ФормыИФорматы,
	"1151100", '2022-09-29', "ЕД-7-11/881@", "ФормаОтчета2023Кв1");
	ОпределитьФорматВДеревеФормИФорматов(ФормаОтчета2023Кв1, "5.03");
	
	ФормаОтчета2021Кв4 = ОпределитьФормуВДеревеФормИФорматов(ФормыИФорматы,
	"1151100", '2021-09-28', "ЕД-7-11/845@", "ФормаОтчета2021Кв4");
	ОпределитьФорматВДеревеФормИФорматов(ФормаОтчета2021Кв4, "5.02");
	
	ФормаОтчета2021Кв1 = ОпределитьФормуВДеревеФормИФорматов(ФормыИФорматы,
	"1151100", '2020-10-15', "ЕД-7-11/753@", "ФормаОтчета2021Кв1");
	ОпределитьФорматВДеревеФормИФорматов(ФормаОтчета2021Кв1, "5.01");
	
	Возврат ФормыИФорматы;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОпределитьФормуВДеревеФормИФорматов(ДеревоФормИФорматов, Код, ДатаПриказа = '00010101', НомерПриказа = "",
			ИмяОбъекта = "", ДатаНачалаДействия = '00010101', ДатаОкончанияДействия = '00010101', Описание = "")
	
	НовСтр = ДеревоФормИФорматов.Строки.Добавить();
	НовСтр.Код = СокрЛП(Код);
	НовСтр.ДатаПриказа = ДатаПриказа;
	НовСтр.НомерПриказа = СокрЛП(НомерПриказа);
	НовСтр.ДатаНачалаДействия = ДатаНачалаДействия;
	НовСтр.ДатаОкончанияДействия = ДатаОкончанияДействия;
	НовСтр.ИмяОбъекта = СокрЛП(ИмяОбъекта);
	НовСтр.Описание = СокрЛП(Описание);
	Возврат НовСтр;
	
КонецФункции

Функция ОпределитьФорматВДеревеФормИФорматов(Форма, Версия, ДатаПриказа = '00010101', НомерПриказа = "",
			ДатаНачалаДействия = Неопределено, ДатаОкончанияДействия = Неопределено, ИмяОбъекта = "", Описание = "")
	
	НовСтр = Форма.Строки.Добавить();
	НовСтр.Код = СокрЛП(Версия);
	НовСтр.ДатаПриказа = ДатаПриказа;
	НовСтр.НомерПриказа = СокрЛП(НомерПриказа);
	НовСтр.ДатаНачалаДействия = ?(ДатаНачалаДействия = Неопределено, Форма.ДатаНачалаДействия, ДатаНачалаДействия);
	НовСтр.ДатаОкончанияДействия = ?(ДатаОкончанияДействия = Неопределено, Форма.ДатаОкончанияДействия, ДатаОкончанияДействия);
	НовСтр.ИмяОбъекта = СокрЛП(ИмяОбъекта);
	НовСтр.Описание = СокрЛП(Описание);
	Возврат НовСтр;
	
КонецФункции

Функция СправкиОДоходахИСуммахНалогаФизическихЛицИзОтчетов6_НДФЛ(СтруктураПараметров) Экспорт
	
	МассивДанныхОтчетов = Новый Массив;
	
	ИсточникОтчета = "РегламентированныйОтчет6_НДФЛ";
	
	ДатаОкончания = НачалоДня(КонецКвартала(СтруктураПараметров.ДатаОкончания));
	
	ИмяФормы = Неопределено;
	СтруктураПараметров.Свойство("ИмяФормыОтчета", ИмяФормы);
	
	Если ИмяФормы = Неопределено Тогда
		ИмяФормы = РегламентированнаяОтчетностьВызовСервера.ИмяФормыРеглОтчетаДействующейВОтчетномПериоде(
		ИсточникОтчета, ДатаОкончания);
	КонецЕсли;
	
	Если ИмяФормы = Неопределено Тогда
		Возврат МассивДанныхОтчетов;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИсточникОтчета", ИсточникОтчета);
	Запрос.УстановитьПараметр("ДатаОкончания",  ДатаОкончания);
	Запрос.УстановитьПараметр("Организация",    СтруктураПараметров.Организация);
	Запрос.УстановитьПараметр("ИмяФормы",       ИмяФормы);
	
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РегламентированныйОтчет.Ссылка,
	|	РегламентированныйОтчет.Вид КАК Вид
	|ИЗ
	|	Документ.РегламентированныйОтчет КАК РегламентированныйОтчет
	|ГДЕ
	|	РегламентированныйОтчет.ИсточникОтчета = &ИсточникОтчета
	|	И РегламентированныйОтчет.Организация = &Организация
	|	И РегламентированныйОтчет.ДатаОкончания = &ДатаОкончания
	|	И НЕ РегламентированныйОтчет.ПометкаУдаления
	|	И ВЫРАЗИТЬ(РегламентированныйОтчет.ВыбраннаяФорма КАК СТРОКА(18)) = &ИмяФормы
	|
	|УПОРЯДОЧИТЬ ПО
	|	Вид ВОЗР";
	
	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		
		Если ВРег(СокрЛП(ИмяФормы)) = ВРег("ФормаОтчета2024Кв2")
			ИЛИ ВРег(СокрЛП(ИмяФормы)) = ВРег("ФормаОтчета2024Кв1")
			ИЛИ ВРег(СокрЛП(ИмяФормы)) = ВРег("ФормаОтчета2023Кв1")
			ИЛИ ВРег(СокрЛП(ИмяФормы)) = ВРег("ФормаОтчета2021Кв4")
			ИЛИ ВРег(СокрЛП(ИмяФормы)) = ВРег("ФормаОтчета2021Кв1") Тогда
			
			Пока Выборка.Следующий() Цикл
				
				ДанныеОтчета = Выборка.Ссылка.ДанныеОтчета.Получить();
				
				Если НЕ ДанныеОтчета.Свойство("ОкружениеСохранения") Тогда
					
					ДанныеТитульногоЛиста = ДанныеОтчета.ПоказателиОтчета.ПолеТабличногоДокументаТитульный;
					
					СтруктураДанныхОтчета = Новый Структура;
					СтруктураДанныхОтчета.Вставить("НомерКорректировки", Выборка.Вид);
					СтруктураДанныхОтчета.Вставить("КПП", СокрЛП(ДанныеТитульногоЛиста["КПП"]));
					СтруктураДанныхОтчета.Вставить("ОКТМО", СокрЛП(ДанныеТитульногоЛиста["ОКТМО"]));
					СтруктураДанныхОтчета.Вставить("СправкиОДоходахИСуммахНалогаФизическихЛиц",
					ДанныеОтчета.ДанныеМногоуровневыхРазделов["Приложение1"]);
					
					МассивДанныхОтчетов.Добавить(СтруктураДанныхОтчета);
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат МассивДанныхОтчетов;
	
КонецФункции

#КонецОбласти

#КонецЕсли