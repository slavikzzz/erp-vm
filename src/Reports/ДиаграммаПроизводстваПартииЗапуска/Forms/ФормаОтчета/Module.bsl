
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Параметры.Свойство("СтатусГрафика", СтатусГрафика) Тогда
		СтатусГрафика = СтатусРабочийГрафик();
	КонецЕсли;
	
	Если Параметры.Свойство("ПартияПроизводства") Тогда
		ПартияПроизводства = Параметры.ПартияПроизводства;
	ИначеЕсли Параметры.Свойство("ПараметрКоманды")
		И ТипЗнч(Параметры.ПараметрКоманды) = Тип("ДокументСсылка.ЭтапПроизводства2_2") Тогда
		
		ПартияПроизводства = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			Параметры.ПараметрКоманды, "ПартияПроизводства");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПартияПроизводства) Тогда
		ЗаполнитьВыходныеИзделия();
		УстановитьЗаголовокФормы();
		СформироватьОтчетВФоне();
	Иначе
		ТекстСообщения = НСтр("ru = 'Непосредственное формирование этого отчета не предусмотрено. Перейти к этому отчету можно из отчетов ""Диаграмма производства заказа"" или ""Структура заказа на производство"".';
								|en = 'Direct generation of this report is not provided. You can access the report from reports ""Order production chart"" or ""Production order structure"".'");
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НачатьОжиданиеДлительнойОперации Тогда
		
		ПодключитьОбработчикОжидания("НачатьОжиданиеДлительнойОперации", 0.1, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если НЕ ДлительнаяОперация = Неопределено Тогда
		ОтменитьДлительнуюОперацию(ДлительнаяОперация.ИдентификаторЗадания);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сформировать(Команда)
	
	ЗапуститьФормированиеОтчета();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ФормированиеОтчета

&НаКлиенте
Процедура ЗапуститьФормированиеОтчета()
	
	СформироватьОтчетВФоне();
	
	Если НачатьОжиданиеДлительнойОперации Тогда
		
		НачатьОжиданиеДлительнойОперации();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьОжиданиеДлительнойОперации()
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("СформироватьОтчетВФонеЗавершение", ЭтотОбъект);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
		ДлительнаяОперация,
		ОповещениеОЗавершении,
		ПараметрыОжидания);
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаДлительнаяОперация;
	
	НачатьОжиданиеДлительнойОперации = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьОтчетВФоне()
	
	Если НЕ ДлительнаяОперация = Неопределено Тогда
		ОтменитьДлительнуюОперацию(ДлительнаяОперация.ИдентификаторЗадания);
	КонецЕсли;
	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("ПартияПроизводства", ПартияПроизводства);
	ПараметрыПроцедуры.Вставить("СтатусГрафика", СтатусГрафика);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания =
		НСтр("ru = 'Формирование расшифровки отчета ""Диаграмма графика производства заказа""';
			|en = 'Generate drill-down for the ""Order production schedule chart"" report'");
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(
		"Отчеты.ДиаграммаПроизводстваПартииЗапуска.СформироватьОтчет",
		ПараметрыПроцедуры,
		ПараметрыВыполнения);
	
	Если ДлительнаяОперация.Статус = "Выполняется" Тогда
		
		НачатьОжиданиеДлительнойОперации = Истина;
		
	Иначе
		
		ОбработатьРезультатФормированияОтчетаВФоне(ЭтотОбъект, ДлительнаяОперация);
		
		НачатьОжиданиеДлительнойОперации = Ложь;
		ДлительнаяОперация = Неопределено;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьОтчетВФонеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ДлительнаяОперация = Неопределено;
	
	Если Результат = Неопределено Тогда
		
		ОчиститьИПоказатьДиаграмму(ЭтотОбъект);
		
	Иначе
		
		ОбработатьРезультатФормированияОтчетаВФоне(ЭтотОбъект, Результат);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбработатьРезультатФормированияОтчетаВФоне(Форма, Результат)
	
	Если Результат.Статус = "Выполнено" Тогда
		
		НоваяДиаграмма = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если ТипЗнч(НоваяДиаграмма) = Тип("ДиаграммаГанта") Тогда
			Форма.ДиаграммаГанта = НоваяДиаграмма;
		КонецЕсли;
		
		Форма.Элементы.ГруппаСтраницы.ТекущаяСтраница = Форма.Элементы.СтраницаДиаграмма;
		
	Иначе
		
		ОчиститьИПоказатьДиаграмму(Форма);
		
		Если Результат.Статус = "Ошибка" Тогда
			
			ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОчиститьИПоказатьДиаграмму(Форма)
	
	Форма.ДиаграммаГанта.Очистить();
	Форма.Элементы.ГруппаСтраницы.ТекущаяСтраница = Форма.Элементы.СтраницаДиаграмма;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОтменитьДлительнуюОперацию(ИдентификаторЗадания)
	
	ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаСервере
Процедура ЗаполнитьВыходныеИзделия()
	
	ОписаниеНХ = Новый Массив;
	
	Если Параметры.Свойство("МассивВыходныеИзделия") Тогда
		
		Для каждого Номенклатура Из Параметры.МассивВыходныеИзделия Цикл
			
			Если ОписаниеНХ.Количество() > 0 Тогда
				ОписаниеНХ.Добавить(", ");
			КонецЕсли;
			
			ОписаниеНоменклатура = Новый ФорматированнаяСтрока(
				ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "Представление"),
				,
				,
				,
				ПолучитьНавигационнуюСсылку(Номенклатура));
				
			ОписаниеНХ.Добавить(ОписаниеНоменклатура);
		
		КонецЦикла;
		
	КонецЕсли;
	
	Если ОписаниеНХ.Количество() > 0 Тогда
		
		ВыходныеИзделия = Новый ФорматированнаяСтрока(ОписаниеНХ);
		
	Иначе
	
		Элементы.ВыходныеИзделия.Видимость = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовокФормы()
	
	Если СтатусГрафика = СтатусРабочийГрафик() Тогда
		
		Заголовок = НСтр("ru = 'График производства партии запуска';
						|en = 'Starting lot production schedule'");
		
	ИначеЕсли СтатусГрафика = СтатусПредварительныйГрафик() Тогда
		
		Заголовок = НСтр("ru = 'График производства партии запуска (планирование)';
						|en = 'Starting lot production schedule (creating)'");
		
	ИначеЕсли СтатусГрафика = СтатусМодельГрафика() Тогда
		
		Заголовок = НСтр("ru = 'График производства партии запуска (модель)';
						|en = 'Starting lot production schedule (model)'");
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СтатусРабочийГрафик()
	
	Возврат РегистрыСведений.ГрафикЭтаповПроизводства2_2.СтатусРабочийГрафик();
	
КонецФункции

&НаСервере
Функция СтатусПредварительныйГрафик()
	
	Возврат РегистрыСведений.ГрафикЭтаповПроизводства2_2.СтатусПредварительныйГрафик();
	
КонецФункции

&НаСервере
Функция СтатусМодельГрафика()
	
	Возврат РегистрыСведений.ГрафикЭтаповПроизводства2_2.СтатусМодельГрафика();
	
КонецФункции

#КонецОбласти

#КонецОбласти
