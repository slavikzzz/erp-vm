#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ТипЗнч(Параметры.ДанныеДляРедактирования) = Тип("ХранилищеЗначения") Тогда 
		ЗагрузкаДанных(Параметры.ДанныеДляРедактирования);
	КонецЕсли;
	
	ОтчетностьМайнеров.СправочникиМайнеровВТаблицыНаФорме(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Асинх Процедура ПрОтсПулПриИзменении(Элемент)
	Если ПрОтсПул И АдрИдентифПул.Количество() > 0 Тогда 
		Если КодВозвратаДиалога.Да = Ждать ВопросАсинх("Очистить сведения об идентификаторах пула?", РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Нет) Тогда 
			АдрИдентифПул.Очистить();
		Иначе
			ПрОтсПул = Ложь;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	ОчиститьСообщения();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыАдрИдентиф

&НаКлиенте
Процедура АдрИдентифПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Если АдрИдентиф.Количество() >= 500 Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю("Допускается не более 500 строк с адресами");
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура АдрИдентифПередУдалением(Элемент, Отказ)
	Отказ = Истина;
	Для Каждого Элт Из ОбщегоНазначенияКлиентСервер.СкопироватьМассив(Элемент.ВыделенныеСтроки) Цикл 
		АдрИдентиф.Удалить(АдрИдентиф.НайтиПоИдентификатору(Элт));
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыАдрИдентифПул

&НаКлиенте
Процедура АдрИдентифПулПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	ПрОтсПул = ?(НоваяСтрока Или Копирование, Ложь, ПрОтсПул);
КонецПроцедуры

&НаКлиенте
Процедура АдрИдентифПулПередУдалением(Элемент, Отказ)
	Отказ = Истина;
	Для Каждого Элт Из ОбщегоНазначенияКлиентСервер.СкопироватьМассив(Элемент.ВыделенныеСтроки) Цикл 
		АдрИдентифПул.Удалить(АдрИдентифПул.НайтиПоИдентификатору(Элт));
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСвВалют

&НаКлиенте
Процедура СвВалютКодВалютНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	ВыборИзСписка("Валюты", "КодВалют", Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура СвВалютКодВалютЗачисНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	ВыборИзСписка("Валюты", "КодВалютЗачис", Элемент, СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СгруппироватьВалюты(Команда)
	СгруппироватьВалютыНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура Проверить(Команда)
	ОчиститьСообщения();
	Для Каждого Стр Из СвВалют Цикл 
		Если СтрДлина(СокрЛП(Стр.КодВалют)) <> 4 Тогда 
			ОбщегоНазначенияКлиент.СообщитьПользователю("Код валюты должен содержать 4 символа", , "СвВалютКодВалют");
			Элементы.Страницы.ТекущаяСтраница = Элементы.СтрСвВалют;
			Элементы.СвВалют.ТекущаяСтрока = Стр.ПолучитьИдентификатор();
			Возврат;
		ИначеЕсли ЗначениеЗаполнено(СокрЛП(Стр.КодВалютЗачис)) И СтрДлина(СокрЛП(Стр.КодВалютЗачис)) <> 4 Тогда 
			ОбщегоНазначенияКлиент.СообщитьПользователю("Код зачисленной валюты должен содержать 4 символа", , "СвВалютКодВалютЗачис");
			Элементы.Страницы.ТекущаяСтраница = Элементы.СтрСвВалют;
			Элементы.СвВалют.ТекущаяСтрока = Стр.ПолучитьИдентификатор();
			Возврат;
		ИначеЕсли Не ЗначениеЗаполнено(Стр.Сумма) Тогда 
			ОбщегоНазначенияКлиент.СообщитьПользователю("Необходимо указать сумму", , "СвВалютСумма");
			Элементы.Страницы.ТекущаяСтраница = Элементы.СтрСвВалют;
			Элементы.СвВалют.ТекущаяСтрока = Стр.ПолучитьИдентификатор();
			Возврат;
		ИначеЕсли Не ЗначениеЗаполнено(Стр.СтатЗачис) Тогда 
			ОбщегоНазначенияКлиент.СообщитьПользователю("Необходимо указать статус", , "СвВалютСтатЗачис");
			Элементы.Страницы.ТекущаяСтраница = Элементы.СтрСвВалют;
			Элементы.СвВалют.ТекущаяСтрока = Стр.ПолучитьИдентификатор();
			Возврат;
		КонецЕсли;
	КонецЦикла;
	Для Каждого Стр Из АдрИдентиф Цикл 
		Если Не ЗначениеЗаполнено(Стр.АдрИдентиф) Тогда 
			ОбщегоНазначенияКлиент.СообщитьПользователю("Адрес-идентификатор майнера должен быть заполнен", , "АдрИдентифАдрИдентиф");
			Элементы.Страницы.ТекущаяСтраница = Элементы.СтрАдрИдентиф;
			Элементы.АдрИдентиф.ТекущаяСтрока = Стр.ПолучитьИдентификатор();
			Возврат;
		КонецЕсли;
	КонецЦикла;
	Для Каждого Стр Из АдрИдентифПул Цикл 
		Если Не ЗначениеЗаполнено(Стр.АдрИдентифПул) Тогда 
			ОбщегоНазначенияКлиент.СообщитьПользователю("Адрес-идентификатор пула майнеров должен быть заполнен", , "АдрИдентифПулАдрИдентифПул");
			Элементы.Страницы.ТекущаяСтраница = Элементы.СтрАдрИдентифПул;
			Элементы.АдрИдентифПул.ТекущаяСтрока = Стр.ПолучитьИдентификатор();
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
	Если АдрИдентиф.Количество() = 0 Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю("Адрес-идентификатор майнера должен быть заполнен", , "АдрИдентифАдрИдентиф");
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтрАдрИдентиф;
		Возврат;
	КонецЕсли;
	Если СвВалют.Количество() = 0 Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю("Валюты должны быть заполнены", , "АдрИдентифАдрИдентиф");
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтрСвВалют;
		Возврат;
	Иначе
		ВсеКлючи = Новый Соответствие;
		Для Каждого Стр Из СвВалют Цикл
			КлючСтроки = "_" + Стр.КодВалют + "_" + Стр.КодВалютЗачис + "_" + Стр.СтатЗачис + "_";
			Если ВсеКлючи[КлючСтроки] = Истина Тогда
				Элементы.Страницы.ТекущаяСтраница = Элементы.СтрСвВалют;
				Элементы.СвВалют.ТекущаяСтрока = Стр.ПолучитьИдентификатор();
				ОбщегоНазначенияКлиент.СообщитьПользователю("Обнаружены дублирующиеся строки, их следует сгруппировать");
				Возврат;
			КонецЕсли;
			
			ВсеКлючи.Вставить(КлючСтроки, Истина);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Ок(Команда)
	Если Не ЗаполненоКорректно() Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю("Заполнение некорректно, выполните проверку заполнения");
	Иначе
		Закрыть(ОписаниеРезультата());
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Добавить(Команда)
	Если СвВалют.Количество() >= 500 Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю("Допускается не более 500 строк с валютами");
	Иначе
		Элементы.СвВалют.ТекущаяСтрока = СвВалют.Добавить().ПолучитьИдентификатор();
		ВыборИзСписка("Валюты", "КодВалют", Элементы.СвВалютКодВалют, Ложь);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Асинх Процедура Удалить(Команда)
	Если Элементы.СвВалют.ТекущиеДанные <> Неопределено И
		КодВозвратаДиалога.Да = Ждать ВопросАсинх("Удалить сведения?", РежимДиалогаВопрос.ДаНетОтмена) Тогда 
		
		Для Каждого Элт Из ОбщегоНазначенияКлиентСервер.СкопироватьМассив(Элементы.СвВалют.ВыделенныеСтроки) Цикл 
			СвВалют.Удалить(СвВалют.НайтиПоИдентификатору(Элт));
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Асинх Процедура ОчиститьВалюты(Команда)
	Если ЗначениеЗаполнено(СвВалют.Количество()) 
		И КодВозвратаДиалога.Да = Ждать ВопросАсинх("Очистить сведения по валютам?", РежимДиалогаВопрос.ДаНетОтмена) Тогда
		СвВалют.Очистить();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Асинх Процедура ОчиститьАдрИдентиф(Команда)
	Если ЗначениеЗаполнено(АдрИдентиф.Количество()) 
		И КодВозвратаДиалога.Да = Ждать ВопросАсинх("Очистить идентификаторы майнера?", РежимДиалогаВопрос.ДаНетОтмена) Тогда
		АдрИдентиф.Очистить();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Асинх Процедура ОчиститьАдрИдентифПул(Команда)
	Если ЗначениеЗаполнено(АдрИдентифПул.Количество()) 
		И КодВозвратаДиалога.Да = Ждать ВопросАсинх("Очистить идентификаторы пула?", РежимДиалогаВопрос.ДаНетОтмена) Тогда
		АдрИдентифПул.Очистить();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗагрузкаДанных(ДанныеДляРедактирования)
	ОтчетностьМайнеров.JSONВРеквизитыФормы(ЭтотОбъект, ДанныеДляРедактирования.Получить());
КонецПроцедуры

&НаКлиенте
Функция ЗаполненоКорректно()
	Для Каждого Стр Из СвВалют Цикл 
		Если (СтрДлина(СокрЛП(Стр.КодВалют)) <> 4) Или (Не ЗначениеЗаполнено(Стр.Сумма)) Или (Не ЗначениеЗаполнено(Стр.СтатЗачис))
			Или (ЗначениеЗаполнено(СокрЛП(Стр.КодВалютЗачис)) И СтрДлина(СокрЛП(Стр.КодВалютЗачис)) <> 4) Тогда 
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	Для Каждого Стр Из АдрИдентиф Цикл 
		Если Не ЗначениеЗаполнено(Стр.АдрИдентиф) Тогда 
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	Для Каждого Стр Из АдрИдентифПул Цикл 
		Если Не ЗначениеЗаполнено(Стр.АдрИдентифПул) Тогда 
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	ВсеКлючи = Новый Соответствие;
	ЕстьДубли = Ложь;
	Для Каждого Стр Из СвВалют Цикл
		КлючСтроки = "_" + Стр.КодВалют + "_" + Стр.КодВалютЗачис + "_" + Стр.СтатЗачис + "_";
		Если ВсеКлючи[КлючСтроки] = Истина Тогда
			ЕстьДубли = Истина;
			Прервать;
		КонецЕсли;
		ВсеКлючи.Вставить(КлючСтроки, Истина);
	КонецЦикла;
	
	Возврат ЗначениеЗаполнено(АдрИдентиф.Количество()) И ЗначениеЗаполнено(СвВалют.Количество()) И (Не ЕстьДубли);
КонецФункции

&НаСервере
Функция ОписаниеРезультата()
	РезультатЗакрытия = Новый Структура("Описание, Данные", "", Неопределено);
	Если ЗначениеЗаполнено(СвВалют.Количество()) Тогда 
		РезультатЗакрытия.Описание = "Информация о " + Формат(СвВалют.Количество(), "ЧГ=") + " валютах";
	КонецЕсли;
	Если ЗначениеЗаполнено(АдрИдентиф.Количество() + АдрИдентифПул.Количество()) Тогда
		ВсегоАдресов = Формат(АдрИдентиф.Количество() + АдрИдентифПул.Количество(), "ЧГ=");
		РезультатЗакрытия.Описание = РезультатЗакрытия.Описание + 
			?(ЗначениеЗаполнено(РезультатЗакрытия.Описание), " и " + ВсегоАдресов + " адресах", "Информация о " + ВсегоАдресов + " адресах")
	КонецЕсли;
	
	ОписаниеJSON = ВалютыУведомлениеМайнераВJSON();
	РезультатЗакрытия.Данные = Новый ХранилищеЗначения(ОписаниеJSON, Новый СжатиеДанных(9));
	Возврат РезультатЗакрытия;
КонецФункции

&НаСервере
Функция ВалютыУведомлениеМайнераВJSON()
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписьJSON.ЗаписатьНачалоОбъекта();
	
	ОтчетностьМайнеров.ЗаписатьИмяЗначениеСвойства(ЗаписьJSON, "version", "1.0.0");
	ОтчетностьМайнеров.ЗаписатьИмяЗначениеСвойства(ЗаписьJSON, "Форма", "ФормаСвДобытВалют");
	ОтчетностьМайнеров.ЗаписатьИмяЗначениеСвойства(ЗаписьJSON, "schema", "minerCryptoSchemas.xsd");
	ОтчетностьМайнеров.ЗаписатьПакетПоказателей(Новый Структура("ПрОтсПул", ПрОтсПул), ЗаписьJSON);
	ОтчетностьМайнеров.ЗаписатьТаблицуПоказателей(ЭтотОбъект, ЗаписьJSON, "СвВалют");
	ОтчетностьМайнеров.ЗаписатьТаблицуПоказателей(ЭтотОбъект, ЗаписьJSON, "АдрИдентиф");
	ОтчетностьМайнеров.ЗаписатьТаблицуПоказателей(ЭтотОбъект, ЗаписьJSON, "АдрИдентифПул");
	
	ЗаписьJSON.ЗаписатьКонецОбъекта();
	Результат = ЗаписьJSON.Закрыть();
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура ВыборИзСписка(ИмяСписка, ИмяКолонки, Элемент, СтандартнаяОбработка) Экспорт 
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Заголовок", "Выбор кода");
	ПараметрыФормы.Вставить("ТаблицаЗначений", ЭтотОбъект[ИмяСписка]);
	ПараметрыФормы.Вставить("СтруктураДляПоиска", Новый Структура("Код", Элемент.Родитель.ТекущиеДанные[ИмяКолонки]));
	
	ДополнительныеПараметры = Новый Структура("Форма, Элемент, ИмяКолонки", ЭтотОбъект, Элемент, ИмяКолонки);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыборИзСпискаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ОткрытьФорму("Обработка.ОбщиеОбъектыРеглОтчетности.Форма.ФормаВыбораЗначенияИзТаблицы", 
		ПараметрыФормы, ЭтотОбъект,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ВыборИзСпискаЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	ДополнительныеПараметры.Элемент.Родитель.ТекущиеДанные[ДополнительныеПараметры.ИмяКолонки] = РезультатВыбора.Код;
КонецПроцедуры

&НаСервере
Процедура СгруппироватьВалютыНаСервере()
	ТаблицаСвВалют = РеквизитФормыВЗначение("СвВалют");
	ТаблицаСвВалют.Свернуть("КодВалют,КодВалютЗачис,СтатЗачис", "Сумма");
	ЗначениеВРеквизитФормы(ТаблицаСвВалют, "СвВалют");
КонецПроцедуры

#КонецОбласти
