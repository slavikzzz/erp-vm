
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СхемаКомпоновкиДанных.НаборыДанных.НаборДанных.Запрос = ПолучитьТекстЗапроса();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьТекстЗапроса()
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДокументРаспределения.Организация КАК Организация,
		|	ДокументРаспределения.Подразделение КАК Подразделение,
		|	ДокументРаспределения.Ссылка КАК Документ,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПроизводство,
		|	"""" КАК НомерЗаказаНаПроизводство,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаЗаказаНаПроизводство,
		|	ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка) КАК Этап,
		|	ТЧВыпускиБезРаспоряжения.Номенклатура КАК Продукция,
		|	ТЧВыпускиБезРаспоряжения.Характеристика КАК ХарактеристикаПродукции,
		|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Полуфабрикат,
		|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК ХарактеристикаПолуфабриката,
		|	ДокументРаспределения.Номенклатура КАК Номенклатура,
		|	ДокументРаспределения.Характеристика КАК Характеристика,
		|	ДокументРаспределения.Серия КАК Серия,
		|	0 КАК Произведено,
		|	0 КАК Норматив,
		|	0 КАК Отклонение,
		|	ТЧВыпускиБезРаспоряжения.Количество КАК РаспределеноНаПроизводство,
		|	ДокументРаспределения.Назначение КАК Назначение
		|ИЗ
		|	Документ.РаспределениеПроизводственныхЗатрат КАК ДокументРаспределения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеПроизводственныхЗатрат.ВыпускиБезРаспоряжения КАК ТЧВыпускиБезРаспоряжения
		|		ПО ДокументРаспределения.Ссылка = ТЧВыпускиБезРаспоряжения.Ссылка
		|ГДЕ
		|	ДокументРаспределения.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ДокументРаспределения.Проведен
		|	И ТЧВыпускиБезРаспоряжения.Количество <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗатратыСписанияПоНормативам.Организация,
		|	ЗатратыСписанияПоНормативам.Подразделение,
		|	СписаниеЗатратНаВыпуск.Ссылка,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПроизводство,
		|	"""",
		|	ДАТАВРЕМЯ(1, 1, 1),
		|	ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка),
		|	СписаниеЗатратНаВыпуск.Номенклатура,
		|	СписаниеЗатратНаВыпуск.Характеристика,
		|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка),
		|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка),
		|	ЗатратыСписанияПоНормативам.Номенклатура,
		|	ЗатратыСписанияПоНормативам.Характеристика,
		|	ЗатратыСписанияПоНормативам.Серия,
		|	СписаниеЗатратНаВыпуск.Количество,
		|	ЗатратыСписанияПоНормативам.КоличествоРасход,
		|	0,
		|	0,
		|	ЗатратыСписанияПоНормативам.Назначение
		|ИЗ
		|	РегистрНакопления.МатериалыИРаботыВПроизводстве.Обороты(, , Регистратор, ) КАК ЗатратыСписанияПоНормативам
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеЗатратНаВыпуск КАК СписаниеЗатратНаВыпуск
		|		ПО (СписаниеЗатратНаВыпуск.Ссылка = ЗатратыСписанияПоНормативам.Регистратор)
		|ГДЕ
		|	ЗатратыСписанияПоНормативам.Регистратор ССЫЛКА Документ.СписаниеЗатратНаВыпуск
		|
		//++ НЕ УТКА
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗатратыОбороты.Организация,
		|	ЗатратыОбороты.Подразделение,
		|	МаршрутныйЛист.Ссылка,
		|	МаршрутныйЛист.Распоряжение,
		|	МаршрутныйЛист.Распоряжение.Номер,
		|	МаршрутныйЛист.Распоряжение.Дата,
		|	ВЫБОР
		|		КОГДА МаршрутныйЛист.Этап.Владелец.МногоэтапныйПроизводственныйПроцесс
		|			ТОГДА МаршрутныйЛист.Этап
		|		ИНАЧЕ МаршрутныйЛист.Этап.Владелец
		|	КОНЕЦ,
		|	МаршрутныйЛист.Номенклатура,
		|	МаршрутныйЛист.Характеристика,
		|	МаршрутныйЛист.НоменклатураПолуфабриката,
		|	МаршрутныйЛист.ХарактеристикаПолуфабриката,
		|	ЗатратыОбороты.Номенклатура,
		|	ЗатратыОбороты.Характеристика,
		|	ЗатратыОбороты.Серия,
		|	МаршрутныйЛист.Произведено,
		|	ЗатратыОбороты.КоличествоРасход,
		|	0,
		|	0,
		|	ЗатратыОбороты.Назначение
		|ИЗ
		|	РегистрНакопления.МатериалыИРаботыВПроизводстве.Обороты(, , Регистратор, ) КАК ЗатратыОбороты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛистПроизводства КАК МаршрутныйЛист
		|		ПО (МаршрутныйЛист.Ссылка = ЗатратыОбороты.Регистратор)
		|ГДЕ
		|	ЗатратыОбороты.КоличествоРасход < 0
		|	И ЗатратыОбороты.Регистратор ССЫЛКА Документ.МаршрутныйЛистПроизводства
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	МаршрутныйЛист.Организация,
		|	МаршрутныйЛист.Подразделение,
		|	ТаблицаМатериалы.Ссылка,
		|	МаршрутныйЛист.Распоряжение,
		|	МаршрутныйЛист.Распоряжение.Номер,
		|	МаршрутныйЛист.Распоряжение.Дата,
		|	ВЫБОР
		|		КОГДА МаршрутныйЛист.Этап.Владелец.МногоэтапныйПроизводственныйПроцесс
		|			ТОГДА МаршрутныйЛист.Этап
		|		ИНАЧЕ МаршрутныйЛист.Этап.Владелец
		|	КОНЕЦ,
		|	МаршрутныйЛист.Номенклатура,
		|	МаршрутныйЛист.Характеристика,
		|	МаршрутныйЛист.НоменклатураПолуфабриката,
		|	МаршрутныйЛист.ХарактеристикаПолуфабриката,
		|	ТаблицаМатериалы.Номенклатура,
		|	ТаблицаМатериалы.Характеристика,
		|	ТаблицаМатериалы.Серия,
		|	МаршрутныйЛист.Произведено,
		|	ТаблицаМатериалы.Количество,
		|	ТаблицаМатериалы.КоличествоОтклонение,
		|	0,
		|	ТаблицаМатериалы.Назначение
		|ИЗ
		|	Документ.МаршрутныйЛистПроизводства.МатериалыИУслуги КАК ТаблицаМатериалы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛистПроизводства КАК МаршрутныйЛист
		|		ПО ТаблицаМатериалы.Ссылка = МаршрутныйЛист.Ссылка
		|			И (МаршрутныйЛист.ФактическоеОкончание МЕЖДУ &НачалоПериода И &КонецПериода)
		|			И (МаршрутныйЛист.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыМаршрутныхЛистовПроизводства.Выполнен))
		|			И (ТаблицаМатериалы.Количество + ТаблицаМатериалы.КоличествоОтклонение > 0)
		|			И (МаршрутныйЛист.Проведен)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДокументРаспределения.Организация,
		|	ДокументРаспределения.Подразделение,
		|	ДокументРаспределения.Ссылка,
		|	ТЧЭтапыГрафикаПроизводства.ЗаказНаПроизводство,
		|	ТЧЭтапыГрафикаПроизводства.ЗаказНаПроизводство.Номер,
		|	ТЧЭтапыГрафикаПроизводства.ЗаказНаПроизводство.Дата,
		|	ВЫБОР
		|		КОГДА ТЧЭтапыГрафикаПроизводства.Этап.Владелец.МногоэтапныйПроизводственныйПроцесс
		|			ТОГДА ТЧЭтапыГрафикаПроизводства.Этап
		|		ИНАЧЕ ТЧЭтапыГрафикаПроизводства.Этап.Владелец
		|	КОНЕЦ,
		|	ТЧПродукцияЗаказа.Номенклатура,
		|	ТЧПродукцияЗаказа.Характеристика,
		|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка),
		|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка),
		|	ДокументРаспределения.Номенклатура,
		|	ДокументРаспределения.Характеристика,
		|	ДокументРаспределения.Серия,
		|	0,
		|	0,
		|	0,
		|	ТЧЭтапыГрафикаПроизводства.Количество,
		|	ДокументРаспределения.Назначение
		|ИЗ
		|	Документ.РаспределениеПроизводственныхЗатрат КАК ДокументРаспределения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеПроизводственныхЗатрат.ЭтапыГрафикаПроизводства КАК ТЧЭтапыГрафикаПроизводства
		|		ПО ДокументРаспределения.Ссылка = ТЧЭтапыГрафикаПроизводства.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.Продукция КАК ТЧПродукцияЗаказа
		|		ПО (ТЧПродукцияЗаказа.Ссылка = ТЧЭтапыГрафикаПроизводства.ЗаказНаПроизводство)
		|			И (ТЧПродукцияЗаказа.КодСтроки = ТЧЭтапыГрафикаПроизводства.КодСтрокиПродукция)
		|ГДЕ
		|	ДокументРаспределения.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ДокументРаспределения.Проведен
		|	И ТЧЭтапыГрафикаПроизводства.Количество <> 0
		//-- НЕ УТКА
		|";
		
	//++ НЕ УТКА
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "НЕОПРЕДЕЛЕНО КАК ЗаказНаПроизводство", "ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство.ПустаяСсылка) КАК ЗаказНаПроизводство");
	//-- НЕ УТКА
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецЕсли

