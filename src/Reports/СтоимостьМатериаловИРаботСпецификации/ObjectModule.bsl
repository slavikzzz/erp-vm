//++ Устарело_Производство21
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

Перем Основание, ВидЦены, Валюта, Период, КэшированныеЗначения;

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - См. ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	Настройки.События.ПриСозданииНаСервере = Истина;
	Настройки.События.ПередЗагрузкойНастроекВКомпоновщик = Истина;
КонецПроцедуры

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   Отказ - Булево - Передается из параметров обработчика "как есть".
//   СтандартнаяОбработка - Булево - Передается из параметров обработчика "как есть".
//
// См. также:
//   "ФормаКлиентскогоПриложения.ПриСозданииНаСервере" в синтакс-помощнике.
//
Процедура ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	Параметры = Форма.Параметры;
	
	Если Параметры.Свойство("ПараметрКоманды") Тогда
		
		ТипПараметра = ТипЗнч(Параметры.ПараметрКоманды);
		
		Если ТипПараметра = Тип("СправочникСсылка.РесурсныеСпецификации") Тогда
			Параметры.КлючВарианта = "ОценкаРесурснойСпецификацииКонтекст";
		Иначе
			Параметры.КлючВарианта = "ОценкаСпецификацииЗаказаКонтекст";
		КонецЕсли;
		
		Форма.ФормаПараметры.КлючНазначенияИспользования = Параметры.ПараметрКоманды;
		Форма.ФормаПараметры.Отбор.Вставить("Основание", Параметры.ПараметрКоманды);
		
	КонецЕсли;
	
КонецПроцедуры

// Вызывается перед загрузкой новых настроек. Используется для изменения схемы компоновки.
//   Например, если схема отчета зависит от ключа варианта или параметров отчета.
//   Чтобы изменения схемы вступили в силу следует вызывать метод ОтчетыСервер.ПодключитьСхему().
//
// Параметры:
//   Контекст - Произвольный - 
//       Параметры контекста, в котором используется отчет.
//       Используется для передачи в параметрах метода ОтчетыСервер.ПодключитьСхему().
//   КлючСхемы - Строка -
//       Идентификатор текущей схемы компоновщика настроек.
//       По умолчанию не заполнен (это означает что компоновщик инициализирован на основании основной схемы).
//       Используется для оптимизации, чтобы переинициализировать компоновщик как можно реже).
//       Может не использоваться если переинициализация выполняется безусловно.
//   КлючВарианта - Строка, Неопределено -
//       Имя предопределенного или уникальный идентификатор пользовательского варианта отчета.
//       Неопределено когда вызов для варианта расшифровки или без контекста.
//   НовыеНастройкиКД - НастройкиКомпоновкиДанных, Неопределено -
//       Настройки варианта отчета, которые будут загружены в компоновщик настроек после его инициализации.
//       Неопределено когда настройки варианта не надо загружать (уже загружены ранее).
//   НовыеПользовательскиеНастройкиКД - ПользовательскиеНастройкиКомпоновкиДанных, Неопределено -
//       Пользовательские настройки, которые будут загружены в компоновщик настроек после его инициализации.
//       Неопределено когда пользовательские настройки не надо загружать (уже загружены ранее).
//
// Примеры:
// 1. Компоновщик отчета инициализируется на основании схемы из общих макетов:
//	Если КлючСхемы <> "1" Тогда
//		КлючСхемы = "1";
//		СхемаКД = ПолучитьОбщийМакет("МояОбщаяСхемаКомпоновки");
//		ОтчетыСервер.ПодключитьСхему(ЭтотОбъект, Контекст, СхемаКД, КлючСхемы);
//	КонецЕсли;
//
// 2. Схема зависит от значения параметра, выведенного в пользовательские настройки отчета:
//	Если ТипЗнч(НовыеПользовательскиеНастройкиКД) = Тип("ПользовательскиеНастройкиКомпоновкиДанных") Тогда
//		ПолноеИмяОбъектаМетаданных = "";
//		Для Каждого ЭлементКД Из НовыеПользовательскиеНастройкиКД.Элементы Цикл
//			Если ТипЗнч(ЭлементКД) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
//				ИмяПараметра = Строка(ЭлементКД.Параметр);
//				Если ИмяПараметра = "ОбъектМетаданных" Тогда
//					ПолноеИмяОбъектаМетаданных = ЭлементКД.Значение;
//				КонецЕсли;
//			КонецЕсли;
//		КонецЦикла;
//		Если КлючСхемы <> ПолноеИмяОбъектаМетаданных Тогда
//			КлючСхемы = ПолноеИмяОбъектаМетаданных;
//			СхемаКД = Новый СхемаКомпоновкиДанных;
//			// Наполнение схемы...
//			ОтчетыСервер.ПодключитьСхему(ЭтотОбъект, Контекст, СхемаКД, КлючСхемы);
//		КонецЕсли;
//	КонецЕсли;
//
Процедура ПередЗагрузкойНастроекВКомпоновщик(Контекст, КлючСхемы, КлючВарианта, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД) Экспорт
	
	Если ТипЗнч(Контекст) = Тип("ФормаКлиентскогоПриложения") Тогда
		
		Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоВидовЦен") Тогда
			Возврат;
		КонецЕсли;
		
		КомпоновщикНастроекФормы = Контекст.Отчет.КомпоновщикНастроек;
		
		ПараметрВидЦены = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроекФормы.ПользовательскиеНастройки, "ВидЦены");
		ВидЦены = ПараметрВидЦены.Значение;
		
		Если Не ЗначениеЗаполнено(ВидЦены) Тогда
			
			ВидЦены = Константы.ВидЦеныПлановойСтоимостиМатериаловРабот.Получить();
			
			КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроекФормы.ПользовательскиеНастройки, "ВидЦены", ВидЦены);
			НовыеПользовательскиеНастройкиКД = КомпоновщикНастроекФормы.ПользовательскиеНастройки;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий
	
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПользовательскиеНастройкиМодифицированы = Ложь;
	
	УстановитьОбязательныеНастройки(ПользовательскиеНастройкиМодифицированы);
	
	// Подготовим и выведем отчет.
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	
	НастройкиКомпоновкиДанных = КомпоновщикНастроек.ПолучитьНастройки();
	
	МакетКомпоновки = КомпоновщикМакета.Выполнить(
		СхемаКомпоновкиДанных,
		НастройкиКомпоновкиДанных,
		ДанныеРасшифровки);
		
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	
	НаборДанных = СтоимостьМатериаловИРабот();

	ВнешниеНаборыДанных = Новый Структура("НаборДанных", НаборДанных);
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки, Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.НачатьВывод();
	ПроцессорВывода.Вывести(ПроцессорКомпоновки, Истина);
	
	ПроцессорВывода.ЗакончитьВывод();
	
	// Сообщим форме отчета, что настройки модифицированы
	Если ПользовательскиеНастройкиМодифицированы Тогда
		КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ПользовательскиеНастройкиМодифицированы", Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции
	
Процедура УстановитьОбязательныеНастройки(ПользовательскиеНастройкиМодифицированы)
	
	ПараметрВидЦены = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "ВидЦены");
	
	Если Не ЗначениеЗаполнено(ПараметрВидЦены.Значение) Тогда
		ПараметрВидЦены.Значение = Константы.ВидЦеныПлановойСтоимостиМатериаловРабот.Получить();
		Если Не ЗначениеЗаполнено(ПараметрВидЦены.Значение) Тогда
			ПараметрВидЦены.Значение = Справочники.ВидыЦен.ВидЦеныПоУмолчанию(ПараметрВидЦены.Значение);
		КонецЕсли;
	КонецЕсли;
	
	ВидЦены = ПараметрВидЦены.Значение;
	
	ПараметрОснование = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "Основание");
	Основание = ПараметрОснование.Значение;
	
	Валюта = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидЦены, "ВалютаЦены");
	
	ПараметрВалюта = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "Валюта");
	Если ПараметрВалюта.Значение <> Валюта Тогда
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "Валюта", Валюта);
		ПользовательскиеНастройкиМодифицированы = Истина;
	КонецЕсли;
	
	Период = КонецДня(ТекущаяДатаСеанса());
	ПараметрДатаОтчета = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "ДатаОтчета");
	
	Если ТипЗнч(ПараметрДатаОтчета.Значение) = Тип("СтандартнаяДатаНачала") И ЗначениеЗаполнено(ПараметрДатаОтчета.Значение.Дата) Тогда
		Период = КонецДня(ПараметрДатаОтчета.Значение.Дата);
	ИначеЕсли ТипЗнч(ПараметрДатаОтчета.Значение) = Тип("Дата") И ЗначениеЗаполнено(ПараметрДатаОтчета.Значение) Тогда
		Период = КонецДня(ПараметрДатаОтчета.Значение);
	КонецЕсли;
	
КонецПроцедуры

Функция СтоимостьМатериаловИРабот()
	
	ТипПараметра = ТипЗнч(Основание);
	
	Запрос = Новый Запрос;
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Если ТипПараметра = Тип("СправочникСсылка.РесурсныеСпецификации") Тогда
		ДанныеСпецификацииВМенеджерВременныхТаблиц(МенеджерВременныхТаблиц);
	Иначе
		ДанныеЗаказаВМенеджерВременныхТаблиц(МенеджерВременныхТаблиц);
	КонецЕсли;
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("Основание", Основание);
	Запрос.УстановитьПараметр("ВидЦены", ВидЦены);
	
	Если ЦенообразованиеВызовСервера.ИспользуетсяЦенообразование25() Тогда
		Запрос.Текст = ТекстЗапросаСтоимостьМатериаловИРабот2_5();
	Иначе
		Запрос.Текст = ТекстЗапросаСтоимостьМатериаловИРабот();
	КонецЕсли;
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат Результат;
	
КонецФункции

Процедура ДанныеСпецификацииВМенеджерВременныхТаблиц(МенеджерВременныхТаблиц)
	
	ДанныеПоНоменклатуре = Справочники.РесурсныеСпецификации.ДанныеОсновногоИзделияСпецификации(Основание);

	ПараметрыВыборки = Справочники.РесурсныеСпецификации.ПараметрыВыборкиДанных();
	ПараметрыВыборки.ВариантЗаполненияОбеспечения = Перечисления.ВариантыЗаполненияОбеспеченияПроизводства.ПоНастройкамПередачиВПроизводство;
	ПараметрыВыборки.ОкруглятьКоличествоШтучныхТоваров = Ложь;
	ПараметрыВыборки.ПереопределениеНастройкиПартииВыпуска.Использовать = Истина;
	ПараметрыВыборки.ПереопределениеНастройкиПартииВыпуска.ВыпускПроизвольнымиПорциями = Истина;
	
	ТабличныеЧасти = Справочники.РесурсныеСпецификации.ДанныеСпецификацииСПолуфабрикатами(
		ДанныеПоНоменклатуре,
		Истина,
		ПараметрыВыборки);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ВыходныеИзделия", ТабличныеЧасти.ВыходныеИзделия);
	Запрос.УстановитьПараметр("ВозвратныеОтходы", ТабличныеЧасти.ВозвратныеОтходы);
	Запрос.УстановитьПараметр("МатериалыИУслуги", ТабличныеЧасти.МатериалыИУслуги);
	Запрос.УстановитьПараметр("Трудозатраты", ТабличныеЧасти.Трудозатраты);
	Запрос.УстановитьПараметр("Спецификация", Основание);
	Запрос.УстановитьПараметр("Период", Период);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство.ПустаяСсылка) КАК ЗаказНаПроизводство,
	|	0 КАК КодСтрокиЗаказаНаПроизводство,
	|	&Спецификация КАК Спецификация,
	|	ВыходныеИзделия.НомерСтроки КАК НомерСтроки,
	|	ВыходныеИзделия.Номенклатура КАК Номенклатура,
	|	ВыходныеИзделия.Характеристика КАК Характеристика,
	|	ВыходныеИзделия.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВыходныеИзделия.ДоляСтоимости КАК ДоляСтоимости,
	|	&Период КАК ДатаПотребности,
	|	&Период КАК НачатьНеРанее,
	|	ВыходныеИзделия.Количество КАК КоличествоПоЗаказу,
	|	ВыходныеИзделия.Количество КАК КоличествоПоСпецификации
	|ПОМЕСТИТЬ СпецификацияВыходныеИзделия
	|ИЗ
	|	&ВыходныеИзделия КАК ВыходныеИзделия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МатериалыИУслуги.Номенклатура КАК Номенклатура,
	|	МатериалыИУслуги.Характеристика КАК Характеристика,
	|	МатериалыИУслуги.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	МатериалыИУслуги.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	МатериалыИУслуги.ПрименениеМатериала КАК Описание,
	|	МатериалыИУслуги.Количество КАК Количество,
	|	МатериалыИУслуги.ИспользуетсяАвтовыбор КАК ИспользуетсяАвтовыбор,
	|	&Спецификация КАК Спецификация,
	|	ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство.ПустаяСсылка) КАК ЗаказНаПроизводство,
	|	0 КАК КодСтрокиЗаказаНаПроизводство,
	|	&Период КАК НачатьНеРанее
	|ПОМЕСТИТЬ ВТСпецификацияМатериалы
	|ИЗ
	|	&МатериалыИУслуги КАК МатериалыИУслуги
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДД.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ ИспользуетсяАвтовыбор
	|ИЗ
	|	ВТСпецификацияМатериалы КАК ДД
	|ГДЕ
	|	ДД.ИспользуетсяАвтовыбор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВозвратныеОтходы.Номенклатура КАК Номенклатура,
	|	ВозвратныеОтходы.Характеристика КАК Характеристика,
	|	ВозвратныеОтходы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВозвратныеОтходы.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	ВозвратныеОтходы.ОписаниеИзделия КАК Описание,
	|	ВозвратныеОтходы.Количество КАК Количество,
	|	&Спецификация КАК Спецификация,
	|	ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство.ПустаяСсылка) КАК ЗаказНаПроизводство,
	|	0 КАК КодСтрокиЗаказаНаПроизводство,
	|	&Период КАК НачатьНеРанее
	|ПОМЕСТИТЬ ВТСпецификацияОтходы
	|ИЗ
	|	&ВозвратныеОтходы КАК ВозвратныеОтходы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Трудозатраты.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	Трудозатраты.ВидРабот КАК ВидРабот,
	|	Трудозатраты.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК Характеристика,
	|	Трудозатраты.НазначениеРабот КАК Описание,
	|	Трудозатраты.Количество КАК Количество,
	|	&Спецификация КАК Спецификация,
	|	ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство.ПустаяСсылка) КАК ЗаказНаПроизводство,
	|	0 КАК КодСтрокиЗаказаНаПроизводство,
	|	&Период КАК НачатьНеРанее
	|ПОМЕСТИТЬ ВТСпецификацияТрудозатраты
	|ИЗ
	|	&Трудозатраты КАК Трудозатраты";
	
	Запрос.Выполнить();
	
	Если РасчетСебестоимостиПрикладныеАлгоритмы.РазмерВременнойТаблицы(МенеджерВременныхТаблиц,"ИспользуетсяАвтовыбор") > 0 Тогда
		ВызватьИсключение НСтр("ru = 'Некоторые позиции материалов зависят от свойств продукции. Рекомендуется
		|сформировать отчет по заказу на производство по выбранной спецификации с указанной продукцией и характеристикой.';
		|en = 'Some material items depend on product properties. It is recommended
		|that you generate a production order report by selected specification with specified products and characteristic.'");
	КонецЕсли;
	
КонецПроцедуры

Процедура ДанныеЗаказаВМенеджерВременныхТаблиц(МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ЗаказНапроизводство", Основание);
	
	Запрос.Текст = Документы.ЗаказНаПроизводство.ТекстЗапросаПоТабличнымЧастямЗаказа();
	
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ТекстЗапросаСтоимостьМатериаловИРабот()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВТСпецификацияМатериалы.КодСтрокиЗаказаНаПроизводство КАК КодСтрокиЗаказаНаПроизводство,
	|	ВТСпецификацияМатериалы.Номенклатура,
	|	ВТСпецификацияМатериалы.Характеристика,
	|	ВТСпецификацияМатериалы.ЕдиницаИзмерения,
	|	ВТСпецификацияМатериалы.СтатьяКалькуляции,
	|	ВТСпецификацияМатериалы.Описание КАК Описание,
	|	ВТСпецификацияМатериалы.Количество
	|ПОМЕСТИТЬ ВТМатериальные
	|ИЗ
	|	ВТСпецификацияМатериалы КАК ВТСпецификацияМатериалы
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТСпецификацияОтходы.КодСтрокиЗаказаНаПроизводство,
	|	ВТСпецификацияОтходы.Номенклатура,
	|	ВТСпецификацияОтходы.Характеристика,
	|	ВТСпецификацияОтходы.ЕдиницаИзмерения,
	|	ВТСпецификацияОтходы.СтатьяКалькуляции,
	|	ВТСпецификацияОтходы.Описание,
	|	ВТСпецификацияОтходы.Количество
	|ИЗ
	|	ВТСпецификацияОтходы КАК ВТСпецификацияОтходы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТМатериальные.КодСтрокиЗаказаНаПроизводство КАК КодСтрокиЗаказаНаПроизводство,
	|	ВТМатериальные.Номенклатура КАК Затрата,
	|	ВТМатериальные.Характеристика КАК Характеристика,
	|	ВТМатериальные.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	ВТМатериальные.Описание КАК Описание,
	|	ВТМатериальные.Количество КАК КоличествоСпецификация,
	|	ВТМатериальные.Количество * ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Цена, 0) / ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки1, 1) КАК СуммаСпецификация,
	|	ВТМатериальные.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Цена, 0) / ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки1, 1) КАК Цена
	|ПОМЕСТИТЬ СуммыЗатрат
	|ИЗ
	|	ВТМатериальные КАК ВТМатериальные
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
	|				&Период,
	|				(Номенклатура, Характеристика) В
	|						(ВЫБРАТЬ
	|							Т.Номенклатура,
	|							Т.Характеристика
	|						ИЗ
	|							ВТМатериальные КАК Т)
	|					И ВидЦены = &ВидЦены) КАК ЦеныНоменклатурыСрезПоследних
	|		ПО ВТМатериальные.Номенклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура
	|			И ВТМатериальные.Характеристика = ЦеныНоменклатурыСрезПоследних.Характеристика
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТСпецификацияТрудозатраты.КодСтрокиЗаказаНаПроизводство,
	|	ВТСпецификацияТрудозатраты.ВидРабот,
	|	ВТСпецификацияТрудозатраты.Характеристика,
	|	ВТСпецификацияТрудозатраты.СтатьяКалькуляции,
	|	ВТСпецификацияТрудозатраты.Описание,
	|	ВТСпецификацияТрудозатраты.Количество,
	|	ВТСпецификацияТрудозатраты.Количество * ЕСТЬNULL(Расценки.Расценка, 0),
	|	ВТСпецификацияТрудозатраты.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЕСТЬNULL(Расценки.Расценка, 0)
	|ИЗ
	|	ВТСпецификацияТрудозатраты КАК ВТСпецификацияТрудозатраты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РасценкиРаботСотрудников.СрезПоследних(
	|				&Период,
	|				ВидРабот В
	|					(ВЫБРАТЬ
	|						Т.ВидРабот
	|					ИЗ
	|						ВТСпецификацияТрудозатраты КАК Т)) КАК Расценки
	|		ПО ВТСпецификацияТрудозатраты.ВидРабот = Расценки.ВидРабот
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СуммыЗатрат.КодСтрокиЗаказаНаПроизводство,
	|	СуммыЗатрат.Затрата,
	|	СуммыЗатрат.Характеристика,
	|	СуммыЗатрат.СтатьяКалькуляции,
	|	СуммыЗатрат.Описание,
	|	СуммыЗатрат.КоличествоСпецификация КАК Количество,
	|	СуммыЗатрат.ЕдиницаИзмерения,
	|	СуммыЗатрат.Цена КАК Цена,
	|	&Основание,
	|	ВЫБОР КОГДА СуммыЗатрат.СтатьяКалькуляции.ТипЗатрат = Значение(Перечисление.ТипыЗатрат.ВозвратныеОтходы) ТОГДА -1*СуммыЗатрат.СуммаСпецификация ИНАЧЕ СуммыЗатрат.СуммаСпецификация КОНЕЦ КАК Сумма
	|ИЗ
	|	СуммыЗатрат КАК СуммыЗатрат";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаКоэффициентУпаковки1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ЦеныНоменклатурыСрезПоследних.Упаковка",
			"ЦеныНоменклатурыСрезПоследних.Номенклатура"));

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаСтоимостьМатериаловИРабот2_5()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВТСпецификацияМатериалы.КодСтрокиЗаказаНаПроизводство КАК КодСтрокиЗаказаНаПроизводство,
	|	ВТСпецификацияМатериалы.Номенклатура,
	|	ВТСпецификацияМатериалы.Характеристика,
	|	ЕСТЬNULL(ВЫБОР
	|		КОГДА
	|			ВидыНоменклатуры.НастройкиКлючаЦенПоХарактеристике = ЗНАЧЕНИЕ(Перечисление.ВариантОтбораДляКлючаЦен.НеИспользовать)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатурыДляЦенообразования.ПустаяСсылка)
	|		ИНАЧЕ ВТСпецификацияМатериалы.Характеристика.ХарактеристикаНоменклатурыДляЦенообразования
	|	КОНЕЦ, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатурыДляЦенообразования.ПустаяСсылка)) КАК ХарактеристикаЦО,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатурыДляЦенообразования.ПустаяСсылка) КАК СерияЦО,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК УпаковкаЦО,
	|	ВТСпецификацияМатериалы.ЕдиницаИзмерения,
	|	ВТСпецификацияМатериалы.СтатьяКалькуляции,
	|	ВТСпецификацияМатериалы.Описание КАК Описание,
	|	ВТСпецификацияМатериалы.Количество
	|ПОМЕСТИТЬ ВТМатериальные
	|ИЗ
	|	ВТСпецификацияМатериалы КАК ВТСпецификацияМатериалы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|		ПО (ВидыНоменклатуры.Ссылка = ВТСпецификацияМатериалы.Номенклатура.ВидНоменклатуры)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТСпецификацияОтходы.КодСтрокиЗаказаНаПроизводство,
	|	ВТСпецификацияОтходы.Номенклатура,
	|	ВТСпецификацияОтходы.Характеристика,
	|	ЕСТЬNULL(ВЫБОР
	|		КОГДА
	|			ВидыНоменклатуры.НастройкиКлючаЦенПоХарактеристике = ЗНАЧЕНИЕ(Перечисление.ВариантОтбораДляКлючаЦен.НеИспользовать)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатурыДляЦенообразования.ПустаяСсылка)
	|		ИНАЧЕ ВТСпецификацияОтходы.Характеристика.ХарактеристикаНоменклатурыДляЦенообразования
	|	КОНЕЦ, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатурыДляЦенообразования.ПустаяСсылка)) КАК ХарактеристикаЦО,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатурыДляЦенообразования.ПустаяСсылка) КАК СерияЦО,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК УпаковкаЦО,
	|	ВТСпецификацияОтходы.ЕдиницаИзмерения,
	|	ВТСпецификацияОтходы.СтатьяКалькуляции,
	|	ВТСпецификацияОтходы.Описание,
	|	ВТСпецификацияОтходы.Количество
	|ИЗ
	|	ВТСпецификацияОтходы КАК ВТСпецификацияОтходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|		ПО (ВидыНоменклатуры.Ссылка = ВТСпецификацияОтходы.Номенклатура.ВидНоменклатуры)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТМатериальные.КодСтрокиЗаказаНаПроизводство КАК КодСтрокиЗаказаНаПроизводство,
	|	ВТМатериальные.Номенклатура КАК Затрата,
	|	ВТМатериальные.Характеристика КАК Характеристика,
	|	ВТМатериальные.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	ВТМатериальные.Описание КАК Описание,
	|	ВТМатериальные.Количество КАК КоличествоСпецификация,
	|	ВТМатериальные.Количество * ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Цена, 0) /
	|		ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки1, 1) КАК СуммаСпецификация,
	|	ВТМатериальные.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Цена, 0) / ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки1, 1) КАК Цена
	|ПОМЕСТИТЬ СуммыЗатрат
	|ИЗ
	|	ВТМатериальные КАК ВТМатериальные
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры25.СрезПоследних(&Период, (Номенклатура, ХарактеристикаЦО, СерияЦО,
	|			УпаковкаЦО) В
	|			(ВЫБРАТЬ
	|				Т.Номенклатура,
	|				Т.ХарактеристикаЦО,
	|				Т.СерияЦО,
	|				Т.УпаковкаЦО
	|			ИЗ
	|				ВТМатериальные КАК Т)
	|		И ВидЦены = &ВидЦены) КАК ЦеныНоменклатурыСрезПоследних
	|		ПО ВТМатериальные.Номенклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура
	|		И ВТМатериальные.ХарактеристикаЦО = ЦеныНоменклатурыСрезПоследних.ХарактеристикаЦО
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТСпецификацияТрудозатраты.КодСтрокиЗаказаНаПроизводство,
	|	ВТСпецификацияТрудозатраты.ВидРабот,
	|	ВТСпецификацияТрудозатраты.Характеристика,
	|	ВТСпецификацияТрудозатраты.СтатьяКалькуляции,
	|	ВТСпецификацияТрудозатраты.Описание,
	|	ВТСпецификацияТрудозатраты.Количество,
	|	ВТСпецификацияТрудозатраты.Количество * ЕСТЬNULL(Расценки.Расценка, 0),
	|	ВТСпецификацияТрудозатраты.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЕСТЬNULL(Расценки.Расценка, 0)
	|ИЗ
	|	ВТСпецификацияТрудозатраты КАК ВТСпецификацияТрудозатраты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РасценкиРаботСотрудников.СрезПоследних(&Период, ВидРабот В
	|			(ВЫБРАТЬ
	|				Т.ВидРабот
	|			ИЗ
	|				ВТСпецификацияТрудозатраты КАК Т)) КАК Расценки
	|		ПО ВТСпецификацияТрудозатраты.ВидРабот = Расценки.ВидРабот
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СуммыЗатрат.КодСтрокиЗаказаНаПроизводство,
	|	СуммыЗатрат.Затрата,
	|	СуммыЗатрат.Характеристика,
	|	СуммыЗатрат.СтатьяКалькуляции,
	|	СуммыЗатрат.Описание,
	|	СуммыЗатрат.КоличествоСпецификация КАК Количество,
	|	СуммыЗатрат.ЕдиницаИзмерения,
	|	СуммыЗатрат.Цена КАК Цена,
	|	&Основание,
	|	ВЫБОР
	|		КОГДА СуммыЗатрат.СтатьяКалькуляции.ТипЗатрат = Значение(Перечисление.ТипыЗатрат.ВозвратныеОтходы)
	|			ТОГДА -1 * СуммыЗатрат.СуммаСпецификация
	|		ИНАЧЕ СуммыЗатрат.СуммаСпецификация
	|	КОНЕЦ КАК Сумма
	|ИЗ
	|	СуммыЗатрат КАК СуммыЗатрат";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаКоэффициентУпаковки1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ЦеныНоменклатурыСрезПоследних.Упаковка",
			"ЦеныНоменклатурыСрезПоследних.Номенклатура"));

	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти 

#КонецЕсли
//-- Устарело_Производство21
