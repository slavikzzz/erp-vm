#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("РезультатПроверки", Отчет.РезультатПроверки);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Отчет.РезультатПроверки = Неопределено Тогда
	
		ПоказатьПредупреждение(, НСтр("ru = 'Отчет не предназначен для непосредственного использования';
										|en = 'Отчет не предназначен для непосредственного использования'"), 5);
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьПроверкиКС" Тогда
	
		Отчет.РезультатПроверки = Параметр.РезультатПроверки;
		СкомпоноватьРезультат();
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	
	СтандартнаяОбработка = Ложь;
	СтруктураЯчейки = Новый Структура;
	
	РезультатОбработкаРасшифровкиНаСервере(Расшифровка, СтруктураЯчейки);
	
	Если ЭтаФорма.ВладелецФормы = Неопределено 
		ИЛИ (ЭтаФорма.ВладелецФормы.КлючУникальности <> СтруктураЯчейки.СсылкаНаОтчет
		И ТипЗнч(СтруктураЯчейки.СсылкаНаОтчет) <> Тип("ДокументСсылка.УведомлениеОСпецрежимахНалогообложения")) Тогда
		
		ПараметрыФормы = Новый Структура;
		
		ЗаполнитьПараметрыНаСервере(ПараметрыФормы, СтруктураЯчейки.СсылкаНаОтчет);
		
		ПараметрыФормы.Вставить("ДоступенМеханизмПечатиРеглОтчетностиСДвухмернымШтрихкодомPDF417", 
			РегламентированнаяОтчетностьКлиент.ДоступенМеханизмПечатиРеглОтчетностиСДвухмернымШтрихкодомPDF417());
		ПараметрыФормы.Вставить("НеОтображатьПредупреждение", Истина);
		
		Попытка
			
			ПараметрыФормы.Вставить("мСохраненныйДок", СтруктураЯчейки.СсылкаНаОтчет);
			ВариантОтчета = ?(ПараметрыФормы.ЭтоВнешнийОтчет, "ВнешнийОтчет.", "Отчет.");
			ПараметрыФормы.Удалить("ЭтоВнешнийОтчет");
						
			ФормаОтчета = ОткрытьФорму(ВариантОтчета + ПараметрыФормы.ИсточникОтчета + ".Форма." 
				+ ПараметрыФормы.мВыбраннаяФорма, ПараметрыФормы, , СтруктураЯчейки.СсылкаНаОтчет);
			
		Исключение
			
			СтрокаОписания = ОписаниеОшибки();
			ПоказатьПредупреждение(,НСтр("ru = 'Внимание! Устаревшая редакция формы отчета не поддерживается текущей версией конфигурации.';
										|en = 'Внимание! Устаревшая редакция формы отчета не поддерживается текущей версией конфигурации.'"));
			
			Возврат;
			
		КонецПопытки;
		
	Иначе
	
		ФормаОтчета = ЭтаФорма.ВладелецФормы;
		ФормаОтчета.Открыть();
	
	КонецЕсли;
	
	ФормаОтчета.Активизировать();
	ФормаОтчета.АктивизироватьЯчейку(СтруктураЯчейки);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура РезультатОбработкаРасшифровкиНаСервере(Расшифровка, СтруктураЯчейки)
	
	КоллекцияРасшифровки = ПолучитьИзВременногоХранилища(ДанныеРасшифровки);
	
	ПоляРасшифровки = КоллекцияРасшифровки.Элементы[Расшифровка].ПолучитьПоля();
	
	Если ПоляРасшифровки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли; 
	
	СтруктураРасшифровки = Новый Структура;
	
	Для каждого ЗначениеРасшифровки Из ПоляРасшифровки Цикл
		СтруктураРасшифровки.Вставить(ЗначениеРасшифровки.Поле, ЗначениеРасшифровки.Значение); 
	КонецЦикла; 
	
	СтруктураЯчейки.Вставить("СсылкаНаОтчет"	, СтруктураРасшифровки.СсылкаНаОтчет);
	СтруктураЯчейки.Вставить("Раздел"			, СтруктураРасшифровки.ИмяРаздела);
	СтруктураЯчейки.Вставить("Страница"			, СтруктураРасшифровки.НомерСтраницы); 
	СтруктураЯчейки.Вставить("Строка"			, "");
	СтруктураЯчейки.Вставить("Графа"			, "");
	СтруктураЯчейки.Вставить("СтрокаПП"			, "");
	СтруктураЯчейки.Вставить("ИмяЯчейки"		, СтруктураРасшифровки.ИмяПоказателя);
	СтруктураЯчейки.Вставить("Описание"			, "Не задано значение");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПараметрыНаСервере(ПараметрыФормы, СсылкаНаОтчет)
	
	ПараметрыФормы.Вставить("мДатаНачалаПериодаОтчета"	, НачалоДня(СсылкаНаОтчет.ДатаНачала));
	ПараметрыФормы.Вставить("мДатаКонцаПериодаОтчета"	, КонецДня(СсылкаНаОтчет.ДатаОкончания));
	ПараметрыФормы.Вставить("мПериодичность"			, СсылкаНаОтчет.Периодичность);
	ПараметрыФормы.Вставить("Организация"				, СсылкаНаОтчет.Организация);
	ПараметрыФормы.Вставить("мВыбраннаяФорма"			, СсылкаНаОтчет.ВыбраннаяФорма);
	ПараметрыФормы.Вставить("ИсточникОтчета"			, СсылкаНаОтчет.ИсточникОтчета);
	ПараметрыФормы.Вставить("ЭтоВнешнийОтчет"			, 
		РегламентированнаяОтчетность.ЭтоВнешнийОтчет(СсылкаНаОтчет.ИсточникОтчета));
	
КонецПроцедуры

#КонецОбласти
