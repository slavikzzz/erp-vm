#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("АдресТаблицыКопий");
	ДополнительныеПараметры.Вставить("МакетСКП");
	ДополнительныеПараметры.Вставить("ВариантСКП");
	ДополнительныеПараметры.Вставить("ДанныеРасшифровки");
	
	ЗаполнитьЗначенияСвойств(ДополнительныеПараметры, Параметры);
	
	СкомпоноватьОтчетНаСервере();
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РезультатПриАктивизации(Элемент)
	
	Если НЕ Элемент.ТекущаяОбласть.Текст = "Пометить на удаление" Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("Расшифровка", Элемент.ТекущаяОбласть.Расшифровка);
	
	РезультатПриАктивизацииНаСервере(ДополнительныеПараметры);
	
	КнопкиВопроса = РежимДиалогаВопрос.ОКОтмена;
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопроса", ЭтотОбъект, ДополнительныеПараметры);
	
	ПоказатьВопрос(Оповещение, НСтр("ru = 'Пометить на удаление " + ДополнительныеПараметры.СсылкаНаОтчет 
		+ " от " + Формат(ДополнительныеПараметры.ДатаПодписи, "ДФ=dd.MM.yyyy") + "?'"), КнопкиВопроса);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СкомпоноватьОтчетНаСервере()
	
	ТаблицаРезультата 	= ПолучитьИзВременногоХранилища(ДополнительныеПараметры.АдресТаблицыКопий);
	
	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить("ТаблицаРезультата", ТаблицаРезультата);
	
	СхемаКомпоновкиДанных = Отчеты.ПроверкаКонтрольныхСоотношений.ПолучитьМакет("ПоискКопий_1");
	Настройки = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;

	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных;
	
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, ДанныеРасшифровки);
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки);
	
	ДополнительныеПараметры.Вставить("ДанныеРасшифровки", ПоместитьВоВременноеХранилище(ДанныеРасшифровки, 
		УникальныйИдентификатор));
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(Результат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
КонецПроцедуры 

&НаСервереБезКонтекста
Процедура РезультатПриАктивизацииНаСервере(ДополнительныеПараметры)
	
	КоллекцияРасшифровки = ПолучитьИзВременногоХранилища(ДополнительныеПараметры.ДанныеРасшифровки);
	
	ПоляРасшифровки = КоллекцияРасшифровки.Элементы[ДополнительныеПараметры.Расшифровка].ПолучитьПоля();
	
	СтруктураРасшифровки = Новый Структура;
	
	Для каждого ЗначениеРасшифровки Из ПоляРасшифровки Цикл
		СтруктураРасшифровки.Вставить(ЗначениеРасшифровки.Поле, ЗначениеРасшифровки.Значение); 
	КонецЦикла; 
	
	ДополнительныеПараметры.Вставить("СсылкаНаОтчет"	, СтруктураРасшифровки.Ссылка);
	ДополнительныеПараметры.Вставить("ДатаПодписи"		, СтруктураРасшифровки.ДатаПодписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопроса(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Отмена Тогда
        Возврат;
		
	Иначе 
		Результат = ПометитьОтчетНаУдаление(ДополнительныеПараметры);
		
		Если Результат Тогда
		
			Оповестить("ПрограммнаяПометкаУдаленияОтчета");
			Закрыть("ПрограммнаяПометкаУдаленияОтчета");
		
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПометитьОтчетНаУдаление(ДополнительныеПараметры)
	
	Попытка
	
		ДокументОбъект = ДополнительныеПараметры.СсылкаНаОтчет.ПолучитьОбъект();
		ДокументОбъект.ПометкаУдаления = Истина;
		ДокументОбъект.Записать();
		
		Возврат Истина;
	
	Исключение
		
		Сообщить("Не удалось пометить на удаление выбранный объект!");
		
		Возврат Ложь;
	
	КонецПопытки;

КонецФункции 

#КонецОбласти
