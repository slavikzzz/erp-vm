
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	НастройкиОтчета = НастройкиОтчета(ПараметрКоманды);
	КлючНастроек = "ИспользованиеСчетаКонтекстныйОтчет";
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("КлючВарианта", "ИспользованиеСчетаКонтекст");
	ПараметрыФормы.Вставить("КлючНазначенияИспользования", КлючНастроек);
	ПараметрыФормы.Вставить("КлючПользовательскихНастроек", КлючНастроек);
	ПараметрыФормы.Вставить("ПользовательскиеНастройки", НастройкиОтчета.ПользовательскиеНастройки);
	ПараметрыФормы.Вставить("ФиксированныеНастройки", НастройкиОтчета.ФиксированныеНастройки);
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	
	ОткрытьФорму("Отчет.ИспользованиеСчетовИСубконтоМеждународныйУчет.Форма", 
					ПараметрыФормы, 
					ПараметрыВыполненияКоманды.Источник, 
					ПараметрыВыполненияКоманды.Уникальность, 
					ПараметрыВыполненияКоманды.Окно);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция НастройкиОтчета(Счет)
	
	// Настройки контекстного отчета
	КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных;
	ИмяСхемы = Метаданные.Отчеты.ИспользованиеСчетовИСубконтоМеждународныйУчет.ОсновнаяСхемаКомпоновкиДанных.Имя;
	СхемаОтчета = Отчеты.ИспользованиеСчетовИСубконтоМеждународныйУчет.ПолучитьМакет(ИмяСхемы);
	НастройкиВарианта = СхемаОтчета.ВариантыНастроек.ИспользованиеСчетаКонтекст.Настройки;
	КомпоновщикНастроек.ЗагрузитьНастройки(НастройкиВарианта);
	ПользовательскиеНастройки = КомпоновщикНастроек.ПользовательскиеНастройки;
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ЭтоМеждународный", Ложь);
	
	// Установим параметры контекстного отчета
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(ПользовательскиеНастройки, "СчетПланаСчетов", Счет);
	
	Возврат КомпоновщикНастроек;
	
КонецФункции

#КонецОбласти