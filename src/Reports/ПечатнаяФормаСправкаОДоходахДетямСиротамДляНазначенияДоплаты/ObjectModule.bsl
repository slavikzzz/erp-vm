#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Попытка
		
		РезультатКомпоновки = ЗарплатаКадрыОтчеты.РезультатКомпоновкиМакетаПечатнойФормы(ЭтотОбъект, ДанныеРасшифровки, ,
			Отчеты.ПечатнаяФормаСправкаОДоходахДетямСиротамДляНазначенияДоплаты.ВнешниеНаборыДанных());
		
		Отчеты.ПечатнаяФормаСправкаОДоходахДетямСиротамДляНазначенияДоплаты.Сформировать(ДокументРезультат, РезультатКомпоновки);
		
	Исключение
		
		ИнформацияОшибки = ИнформацияОбОшибке();
		ВызватьИсключение НСтр("ru = 'В настройку формирования справки о среднем заработке внесены критичные изменения. Печатная форма не будет сформирована';
								|en = 'Critical changes have been made to the average salary certificate generation setting. Print form cannot be generated.'") + ". " + КраткоеПредставлениеОшибки(ИнформацияОшибки);
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Процедура ИнициализироватьОтчет(КлючВарианта = Неопределено) Экспорт
	
	ЗарплатаКадрыОбщиеНаборыДанных.ЗаполнитьОбщиеИсточникиДанныхОтчета(ЭтотОбъект,
		ЗарплатаКадрыОтчеты.ПоляПредставленийКадровыхДанныхСотрудниковОтчетовПечатныхФорм());
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыРасширеннаяПодсистемы") Тогда
		Для Каждого НаборДанных Из СхемаКомпоновкиДанных.НаборыДанных Цикл
			Если ТипЗнч(НаборДанных) = Тип("НаборДанныхЗапросСхемыКомпоновкиДанных") Тогда
				НаборДанных.Запрос = СтрЗаменить(НаборДанных.Запрос,
					"НЕОПРЕДЕЛЕНО КАК ГрафикРаботы,",
					"КадровыеДанныеСотрудников.ГрафикРаботы КАК ГрафикРаботы,");
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Для общей формы "Форма отчета" подсистемы "Варианты отчетов".
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.События.ПередЗагрузкойНастроекВКомпоновщик = Истина;
	
КонецПроцедуры

// Вызывается перед загрузкой новых настроек. Используется для изменения схемы компоновки.
//
Процедура ПередЗагрузкойНастроекВКомпоновщик(Контекст, КлючСхемы, КлючВарианта, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД) Экспорт
	
	ЗарплатаКадрыОтчеты.ИнициализироватьОтчетПечатнойФормы(Контекст, ЭтотОбъект, КлючСхемы, КлючВарианта, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД);
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли