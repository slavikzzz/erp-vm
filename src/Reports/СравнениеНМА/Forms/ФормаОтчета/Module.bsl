
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("ПараметрКоманды") Тогда
		
		ТипПараметра = ТипЗнч(Параметры.ПараметрКоманды);
		
		Если ТипПараметра = Тип("Массив")
			Или ТипПараметра = Тип("СправочникСсылка.НематериальныеАктивы") Тогда
			
			ЗаполнитьОбъектыОтчета(Параметры.ПараметрКоманды);
			
		Иначе
			Отказ = Истина;
		КонецЕсли;
	Иначе
		Отказ = Истина;
	КонецЕсли;
	
	Если Список.Количество() = 0 Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если Список.Количество() = 1 Тогда
		Элементы.ТолькоРазличия.Видимость = Ложь;
		ТолькоРазличия = Ложь;
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		СформироватьОтчет();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ТолькоРазличияПриИзменении(Элемент)
	СформироватьОтчет();
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	СформироватьОтчет();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СформироватьОтчет()
	
	ОбъектОтчет = РеквизитФормыВЗначение("Отчет");
	ОбъектОтчет.СформироватьОтчет(ТабличныйДокумент, Список, ТолькоРазличия);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОбъектыОтчета(Параметр)
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Объекты.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.НематериальныеАктивы КАК Объекты
		|ГДЕ
		|	Объекты.Ссылка В(&Параметр)
		|	И НЕ Объекты.ПометкаУдаления
		|	И НЕ Объекты.ЭтоГруппа");
	
	Запрос.УстановитьПараметр("Параметр", Параметр);
	
	Список.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры

#КонецОбласти