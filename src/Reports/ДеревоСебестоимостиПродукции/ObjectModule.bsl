#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - см. ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.События.ПередЗагрузкойНастроекВКомпоновщик = Истина;
	Настройки.Вставить("РазрешеноИзменятьСтруктуру", Ложь);
	
КонецПроцедуры

Процедура ПередЗагрузкойНастроекВКомпоновщик(Контекст, КлючСхемы, КлючВарианта, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД) Экспорт
	
	Если ТипЗнч(Контекст) = Тип("ФормаКлиентскогоПриложения") Тогда
		НастроитьПараметрыОтчетаПоВариантуОтчета(КлючВарианта, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД);
		ОтчетыСервер.ПодключитьСхему(ЭтотОбъект, Контекст, СхемаКомпоновкиДанных, КлючСхемы);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Настраивает параметры отчета по варианту отчета
//
// Параметры:
//	КлючВарианта						- Строка									- имя предопределенного или уникальный идентификатор пользовательского
//																						варианта отчета.
//										- Неопределено 								- вызов для варианта расшифровки или без контекста.
//	НовыеНастройкиКД					- НастройкиКомпоновкиДанных					- настройки варианта отчета, которые будут загружены
//																						в компоновщик настроек после его инициализации.
//										- Неопределено 								- настройки варианта не надо загружать (уже загружены ранее).
//	НовыеПользовательскиеНастройкиКД	- ПользовательскиеНастройкиКомпоновкиДанных - пользовательские настройки, которые будут загружены в компоновщик
//																						настроек после его инициализации.
//										- Неопределено 								- пользовательские настройки не надо загружать (уже загружены ранее).
//
Процедура НастроитьПараметрыОтчетаПоВариантуОтчета(КлючВарианта, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД)
	
	ПараметрДанныеПоСебестоимости = СхемаКомпоновкиДанных.Параметры.Найти("ДанныеПоСебестоимости");
	
	СписокВыбора = Новый СписокЗначений;
	
	Если КлючВарианта = "ДеревоСебестоимостиПродукцииПредприятия" Тогда
		
		СписокВыбора.Добавить(1, НСтр("ru = 'В валюте упр. учета с НДС';
										|en = 'In management accounting currency, including VAT'"));
		СписокВыбора.Добавить(2, НСтр("ru = 'В валюте упр. учета без НДС';
										|en = 'In management accounting currency, excluding VAT'"));
		
	ИначеЕсли КлючВарианта = "ДеревоСебестоимостиПродукцииОрганизации" Тогда	
		
		Если РасчетСебестоимостиПовтИсп.УправленческийУчетОрганизаций() Тогда
			СписокВыбора.Добавить(3, НСтр("ru = 'В валюте упр. учета';
											|en = 'In management accounting currency'"));
		КонецЕсли;
		СписокВыбора.Добавить(4, НСтр("ru = 'В валюте регл. учета';
										|en = 'In local accounting currency'"));
		
	Иначе
		
		СписокВыбора.Добавить(1, НСтр("ru = 'В валюте упр. учета с НДС';
										|en = 'In management accounting currency, including VAT'"));
		СписокВыбора.Добавить(2, НСтр("ru = 'В валюте упр. учета без НДС';
										|en = 'In management accounting currency, excluding VAT'"));
		СписокВыбора.Добавить(4, НСтр("ru = 'В валюте регл. учета';
										|en = 'In local accounting currency'"));
		
	КонецЕсли;
	
	ПараметрДанныеПоСебестоимости.УстановитьДоступныеЗначения(СписокВыбора);
	
	Если НовыеНастройкиКД = Неопределено
		Или НовыеПользовательскиеНастройкиКД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗначениеПараметраДанныеПоСебестоимости = НовыеНастройкиКД.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ДанныеПоСебестоимости"));
	НастройкаДанныеПоСебестоимости = НовыеПользовательскиеНастройкиКД.Элементы.Найти(ЗначениеПараметраДанныеПоСебестоимости.ИдентификаторПользовательскойНастройки);
	Если Не НастройкаДанныеПоСебестоимости = Неопределено
		И СписокВыбора.НайтиПоЗначению(НастройкаДанныеПоСебестоимости.Значение) = Неопределено Тогда
		НастройкаДанныеПоСебестоимости.Значение = СписокВыбора[0].Значение;
	КонецЕсли;
	
	ПараметрОтображатьПРВР = НовыеНастройкиКД.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ОтображатьПРВР"));
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьРеглУчет")
	 ИЛИ НЕ НастройкиНалоговУчетныхПолитикЛокализация.ВедетсяУчетПостоянныхИВременныхРазниц(Неопределено, ТекущаяДатаСеанса()) Тогда
	 	ПараметрОтображатьПРВР.Использование = Ложь;
	 	ПараметрОтображатьПРВР.Значение = Ложь;
		ПараметрОтображатьПРВР.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли