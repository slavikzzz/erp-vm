#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция-конструктор для хранения настроек отчета в универсальном формате.
//

// Пустые параметры компоновки отчета.
// 
// Возвращаемое значение:
//  Структура - см. БухгалтерскиеОтчеты.ПустыеПараметрыКомпоновкиОтчета();
Функция ПустыеПараметрыКомпоновкиОтчета() Экспорт
	
	// Общая структура настроек.
	ПараметрыОтчета = БухгалтерскиеОтчеты.ПустыеПараметрыКомпоновкиОтчета();
	
	ПараметрыОтчета.ИдентификаторОтчета   = "ОборотноСальдоваяВедомость";
	ПараметрыОтчета.УменьшенныйАвтоотступ = Истина;
	
	БухгалтерскиеОтчеты.ДобавитьПоказателиВПараметры(ПараметрыОтчета, ПолучитьНаборПоказателей());
	ОтчетОбъект = Отчеты.ОборотноСальдоваяВедомость.Создать();
	ПараметрыОтчета.Вставить("Группировка", ОтчетОбъект.Группировка.ВыгрузитьКолонки());
	ПараметрыОтчета.Вставить("РазвернутоеСальдо", ОтчетОбъект.РазвернутоеСальдо.ВыгрузитьКолонки());
	// 0 = В одной колонке, 1 = В отдельных колонках
	ПараметрыОтчета.Вставить("РазмещениеДополнительныхПолей",
		БухгалтерскиеОтчетыКлиентСервер.РазмещениеДополнительныхПолей().ВместеСВладельцем);
	ПараметрыОтчета.Вставить("ДополнительныеПоля", ОтчетОбъект.ДополнительныеПоля.ВыгрузитьКолонки());

	ПараметрыОтчета.Вставить("ПоСубсчетам", Ложь); // детализация до субсчетов
	ПараметрыОтчета.Вставить("ВыводитьЗабалансовыеСчета", Ложь);

	Возврат ПараметрыОтчета;
	
КонецФункции

// Заполняет структуру настроек универсального формата из реквизитов формы.
//
// Параметры:
//  ПараметрыОтчета - Структура - см. ПустыеПараметрыКомпоновкиОтчета()
//  Форма - УправляемаяФорма - содержит основновной реквизит Отчет .
//
Процедура ЗаполнитьПараметрыИзФормы(ПараметрыОтчета, Форма) Экспорт
	
	БухгалтерскиеОтчеты.ЗаполнитьПараметрыИзФормы(ПараметрыОтчета, Форма);
	
	Отчет = Форма.Отчет;

	ПараметрыОтчета.ПоСубсчетам = Отчет.ПоСубсчетам;
	ПараметрыОтчета.ВыводитьЗабалансовыеСчета = Отчет.ВыводитьЗабалансовыеСчета;
	
КонецПроцедуры

// Инициализирует набор параметров, задающих флаги выполнения дополнительных действий над сущностями, обрабатываемыми
// в процессе формирования отчета.
//
// Возвращаемое значение:
//   Структура   - флаги, задающие необходимость дополнительных действий.
//
Функция ПолучитьПараметрыИсполненияОтчета() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ИспользоватьПередКомпоновкойМакета", Истина);
	Результат.Вставить("ИспользоватьПослеКомпоновкиМакета",  Истина);
	Результат.Вставить("ИспользоватьПослеВыводаРезультата",  Истина);
	Результат.Вставить("ИспользоватьДанныеРасшифровки",      Истина);
	Результат.Вставить("ИспользоватьПередВыводомЭлементаРезультата", Истина);

	Возврат Результат;

КонецФункции

// Формирует текст, выводимый в заголовке отчета.
//
// Параметры:
//  ПараметрыОтчета - Структура - см. ПодготовитьПараметрыОтчета() в ФормаОтчета.
//
// Возвращаемое значение:
//   Строка      - текст заголовка с учетом периода.
//
Функция ПолучитьТекстЗаголовка(ПараметрыОтчета) Экспорт 
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Оборотно-сальдовая ведомость%1';
			|en = 'Trial balance%1'"),
		БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(ПараметрыОтчета.НачалоПериода, ПараметрыОтчета.КонецПериода));
	
КонецФункции

// В процедуре можно доработать компоновщик перед выводом в отчет. Изменения сохранены не будут.
//
// Параметры:
//  ПараметрыОтчета - Структура - см. ПодготовитьПараметрыОтчета() в ФормаОтчета.
//  Схема        - СхемаКомпоновкиДанных - описание получаемых данных.
//  КомпоновщикНастроек - КомпоновщикНастроекКомпоновкиДанных - связь настроек компоновки данных и схемы компоновки.
//
Процедура ПередКомпоновкойМакета(ПараметрыОтчета, Схема, КомпоновщикНастроек) Экспорт
	
	ПараметрыАлгоритма = ПараметрыАлгоритма();
	
	ПодготовитьНаборыДанных(ПараметрыОтчета, ПараметрыАлгоритма);
	
	// Для развернутого сальдо модифицируем выражения полей итога, а также укажем группировки, по которым они должны рассчитываться.
	Если ПараметрыАлгоритма.ВыводитьРазвернутоеСальдо Тогда
		
		СоздатьИтогиРазвернутогоСальдо(ПараметрыАлгоритма);
		
	КонецЕсли;
	
	БухгалтерскиеОтчеты.УстановитьМинимальнуюШиринуПоказателей(ПараметрыАлгоритма.Схема, ПолучитьНаборПоказателей());
	
	Схема = ПараметрыАлгоритма.Схема;
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(Схема));
	
	Для Каждого Параметр Из КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы Цикл
		Параметр.Использование = Истина;
	КонецЦикла;
	
	// Проверим список исключаемых счетов: возможно некоторые субсчета стоит из него исключить.
	// Запрос детализации строится с указанием конкретных видов субконто в параметре виртуальной таблицы.
	// Поэтому, если у какого-либо субсчета детализируемого счета нет нужного набора субконто, 
	// он будет исключен из запроса детализации. Такие субсчета не нужно исключать из запроса по счетам.
	СчетаИсключенныеИзЗапросаПоСчетам = СчетаССовпадающимНаборомСубконто(
		ПараметрыАлгоритма.СчетаИсключенныеИзЗапросаПоСчетам,
		ПараметрыАлгоритма.КоличествоСубконтоДетализацииПоСчетам);
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаИсключенныеИзЗапросаПоСчетам, ПараметрыАлгоритма.СчетаДляОтдельногоЗапроса);
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СчетаИсключенныеИзЗапросаПоСчетам", СчетаИсключенныеИзЗапросаПоСчетам);
	
	Если ЗначениеЗаполнено(ПараметрыОтчета.НачалоПериода) Тогда
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "НачалоПериода", НачалоДня(ПараметрыОтчета.НачалоПериода));
	КонецЕсли;
	Если ЗначениеЗаполнено(ПараметрыОтчета.КонецПериода) Тогда
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "КонецПериода", КонецДня(ПараметрыОтчета.КонецПериода));
	КонецЕсли;
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ВыводитьЗабалансовыеСчета", ПараметрыОтчета.ВыводитьЗабалансовыеСчета);
	
	БухгалтерскиеОтчетыПереопределяемый.УстановитьПараметрыВалют(Схема, КомпоновщикНастроек, ПараметрыОтчета);
	
	// Формирование структуры отчета.
	ПараметрыАлгоритма.Вставить("СписокСчетовПоСубсчетам", БухгалтерскиеОтчетыВызовСервера.ПолучитьСписокСчетовПоСубсчетам(ПараметрыАлгоритма.НастройкаГруппировки));
	СформироватьСтруктуруОтчета(КомпоновщикНастроек, ПараметрыОтчета, ПараметрыАлгоритма);	

КонецПроцедуры

// В процедуре можно уточнить особенности вывода данных в отчет.
//
// Параметры:
//  ПараметрыОтчета - Структура - см. ПодготовитьПараметрыОтчета() в ФормаОтчета.
//  МакетКомпоновки - МакетКомпоновкиДанных - описание выводимых данных.
//
Процедура ПослеКомпоновкиМакета(ПараметрыОтчета, МакетКомпоновки) Экспорт

	// Определим количество группировок по субконто.
	КоличествоГруппировок = 0;
	
	Для Каждого СтрокаТаблицы Из ПараметрыОтчета.Группировка Цикл
		Если СтрокаТаблицы.Использование Тогда
			КоличествоСубконто = СтрЧислоВхождений(СтрокаТаблицы.ПоСубконто, "+");
			КоличествоГруппировок = Макс(КоличествоГруппировок, КоличествоСубконто);
		КонецЕсли;
	КонецЦикла;

	// Для того чтобы отобразить сумму в валюте после каждой группировки подставляется еще групировка по валюте,
	// удваивая тем самым общее количество группировок отчета. Это нужно учесть.
	КоличествоГруппировок = КоличествоГруппировок + ?(ПараметрыОтчета.ПоказательВалютнаяСумма, КоличествоГруппировок, 0);

	КоличествоСтрокПараметров = 0;
	БухгалтерскиеОтчетыПереопределяемый.ВысотаВыводимыхПараметров(ПараметрыОтчета.СхемаКомпоновкиДанных, КоличествоСтрокПараметров);
	КоличествоСтрокШапки = Макс(КоличествоГруппировок, 1);
	ПараметрыОтчета.Вставить("ВысотаШапки", КоличествоСтрокПараметров + КоличествоСтрокШапки + 1);

	// Обработка шапки отчета.
	// Шапка отчета состоит и 4 макетов, макет шапки таблицы, и 3 макета группировок колонок.
	// Поместим ссылки на них в массив для дальнейшей обработки.
	МакетШапкиТаблицы = БухгалтерскиеОтчеты.ПолучитьМакетШапки(МакетКомпоновки, , ?(МакетКомпоновки.Тело.Количество() > 1, "Подвал", "Заголовок"));
	
	МакетыШапкиОтчета = Новый Массив;
	МакетыШапкиОтчета.Добавить(МакетШапкиТаблицы.Имя);

	ПараметрыПоиска = БухгалтерскиеОтчеты.ПараметрыПоискаВТелеМакетаКомпоновки();
	ПараметрыПоиска.СвойствоДляИдентификации = "ТипЭлемента";
	ТекущаяТаблицаМакетаКомпоновкиДанных = БухгалтерскиеОтчеты.ПодобратьЭлементыИзТелаМакета(
		МакетКомпоновки, "ТаблицаМакетаКомпоновкиДанных" , ПараметрыПоиска);
	Для Каждого Колонка Из ТекущаяТаблицаМакетаКомпоновкиДанных.Колонки Цикл

		Для Каждого ТелоГруппировки Из Колонка.Тело Цикл
			МакетыШапкиОтчета.Добавить(ТелоГруппировки.Макет);
		КонецЦикла;

	КонецЦикла;

	// Удалим лишние строки из шапки отчета.
	МассивДляУдаления = Новый Массив;
	
	Для Каждого ИмяМакетаШапкиОтчета Из МакетыШапкиОтчета Цикл
		
		МакетШапкиОтчета = МакетКомпоновки.Макеты[ИмяМакетаШапкиОтчета];
		
		
		Для Индекс = КоличествоСтрокШапки + 1 По МакетШапкиОтчета.Макет.Количество() - 1 Цикл
			
			МассивДляУдаления.Добавить(МакетШапкиОтчета.Макет[Индекс]);
			
		КонецЦикла;
		
		Для Каждого Элемент Из МассивДляУдаления Цикл
			МакетШапкиОтчета.Макет.Удалить(Элемент);
		КонецЦикла;
		
		// Если группировка только по счету, объединим ячейки заголовка таблицы.
		Если МакетШапкиОтчета = МакетШапкиТаблицы И КоличествоСтрокШапки = 1 Тогда
			
			Для Каждого Ячейка Из МакетШапкиОтчета.Макет[МакетШапкиОтчета.Макет.Количество() - 1].Ячейки Цикл
				
				Оформление = Ячейка.Оформление.Элементы.Найти("ОбъединятьПоВертикали");
				Оформление.Значение = Истина;
				Оформление.Использование = Истина;
				
			КонецЦикла;
			
		КонецЕсли;
			
	КонецЦикла;
	
	// Для обработки итогов запомним имена различных макетов.
	
	// Соответствие макетов колонкам отчета понадобится позже для подсчета итогов.
	СоответствиеМакетовКолонкамОтчета = Новый Соответствие;
	МакетыРесурсовПодвалаОтчета = Новый Массив;
	ЗаполнитьМакетыРесурсовПодвалаОтчета(ТекущаяТаблицаМакетаКомпоновкиДанных, МакетыРесурсовПодвалаОтчета, СоответствиеМакетовКолонкамОтчета);
	МакетыРесурсовСчета = Новый Массив;
	ЗаполнитьМакетыРесурсовГруппировки(ТекущаяТаблицаМакетаКомпоновкиДанных.Строки, МакетыРесурсовСчета, СоответствиеМакетовКолонкамОтчета, "Счет");

	ПараметрыПоиска = БухгалтерскиеОтчеты.ПараметрыПоискаВТелеМакетаКомпоновки();
	ПараметрыПоиска.МножественныйПодбор      = Истина;
	ПараметрыПоиска.СвойствоДляИдентификации = "ПолеГруппировки";
	ПараметрыПоиска.ВозвращаемыйТип          = "Макет";
	МакетГруппировкиСчет = БухгалтерскиеОтчеты.ПодобратьЭлементыИзТелаМакета(МакетКомпоновки, "Счет", ПараметрыПоиска);
	МассивИменМакетовСчет = Новый Массив;
	Для Каждого МакетСчет Из МакетГруппировкиСчет Цикл
		МассивИменМакетовСчет.Добавить(МакетСчет.Имя);
	КонецЦикла;

	// Макеты группировки и ресурсов группировки по валюте будем использовать для определения принадлежности элемента Макет макета компоновки.
	МакетГруппировкиВалюта = БухгалтерскиеОтчеты.ПодобратьЭлементыИзТелаМакета(МакетКомпоновки, "Валюта", ПараметрыПоиска);
	МассивИменМакетовВалюта = Новый Массив;
	Для Каждого МакетВалюта Из МакетГруппировкиВалюта Цикл
		МассивИменМакетовВалюта.Добавить(МакетВалюта.Имя);
	КонецЦикла;
	
	МакетРесурсовГруппировкиВалюта = Новый Массив;
	ЗаполнитьМакетыРесурсовГруппировки(ТекущаяТаблицаМакетаКомпоновкиДанных.Строки, МакетРесурсовГруппировкиВалюта, СоответствиеМакетовКолонкамОтчета, "Валюта", Истина);

	// Если в отчете включена валютная сумма, то ее нужно показывать только по тем счетам, где есть валюта.
	// Для того чтобы убрать вывод показателя валюта, там где он не нужен, переберем все макеты и удалим строки,
	// предназначенные для вывода валютных сумм, у всех макетов кроме макетов группировки по валюте и макетов ресурсов
	// группировки по валюте.
	Если ПараметрыОтчета.ПоказательВалютнаяСумма Тогда
		Для Каждого Макет Из МакетКомпоновки.Макеты Цикл 
			
			// Пропускаем макеты не соответствующего типа.
			Если ТипЗнч(Макет) = Тип("ОписаниеМакетаОбластиМакетаКомпоновкиДанных")
				И ТипЗнч(Макет.Макет) <> Тип("МакетОбластиКомпоновкиДанных") Тогда
				Продолжить;
			КонецЕсли;
			
			// Пропускаем макеты шапки.
			Если МакетыШапкиОтчета.Найти(Макет.Имя) = Неопределено Тогда
				
				Если МассивИменМакетовВалюта.Найти(Макет.Имя) <> Неопределено 
				    ИЛИ МакетРесурсовГруппировкиВалюта.Найти(Макет.Имя) <> Неопределено Тогда
					
					Если ПараметрыОтчета.ПоказательКонтроль Тогда
						Макет.Макет.Удалить(Макет.Макет.Количество() - 2);
					КонецЕсли;
					
				ИначеЕсли Макет.Макет.Количество() > 1 Тогда // Последний не удаляем.
					
					Макет.Макет.Удалить(Макет.Макет.Количество() - 1);
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Создадим структуру для хранения итогов.
	ЗначенияПоказателей = Новый Структура();
	
	// Определим количество показателей выводимых в отчет.
	КоличествоПоказателей 	= БухгалтерскиеОтчетыВызовСервера.КоличествоПоказателей(ПараметрыОтчета);
	// На верхнем уровне структура. Ключу структуры соответсвует имя макета колонки, значению - 2-х мерный массив.
	// Размерность первого измерения массива соответсвует количеству показателей отчета.
	// Размерность второго измерения равна 2, что соответсвует дебету и кредиту.
	Для Каждого Колонка Из ТекущаяТаблицаМакетаКомпоновкиДанных.Колонки Цикл
		
		// Создаем структуру.
		ЗначенияПоказателей.Вставить(Колонка.Тело[0].Макет, Новый Массив(КоличествоПоказателей, 2));
		
		// Заполняем значения массива нолями.
		Для Каждого Массив Из ЗначенияПоказателей[Колонка.Тело[0].Макет] Цикл
			Для Индекс = 0 По 1 Цикл
				Массив[Индекс] = 0;
			КонецЦикла;
		КонецЦикла;
		
	КонецЦикла;

	// Создадим структуру для хранения временных данных, которые нужны для расчета итогов при выводе отчета.
	ВременныеДанныеОтчета = Новый Структура;

	// Упаковываем временные данные в структуру.
	ВременныеДанныеОтчета.Вставить("МакетыРесурсовСчета",               МакетыРесурсовСчета);
	ВременныеДанныеОтчета.Вставить("МакетыРесурсовПодвалаОтчета",       МакетыРесурсовПодвалаОтчета);
	ВременныеДанныеОтчета.Вставить("СоответствиеМакетовКолонкамОтчета", СоответствиеМакетовКолонкамОтчета);
	ВременныеДанныеОтчета.Вставить("МакетВалюта",                       МассивИменМакетовВалюта);
	ВременныеДанныеОтчета.Вставить("МакетШапкиОтчета",                  МакетыШапкиОтчета);
	ВременныеДанныеОтчета.Вставить("МакетСчет",                         МассивИменМакетовСчет);
	ВременныеДанныеОтчета.Вставить("КоличествоПоказателей",             КоличествоПоказателей);
	ВременныеДанныеОтчета.Вставить("ЗначенияПоказателей",               ЗначенияПоказателей);
	ВременныеДанныеОтчета.Вставить("ТекущийСчет",                       Неопределено);
	ВременныеДанныеОтчета.Вставить("ПредыдущийКорневойСчет",            Неопределено);
	ВременныеДанныеОтчета.Вставить("МакетыПредыдущегоКорневогоСчета",   Новый Массив);
	
	ПараметрыОтчета.Вставить("ВременныеДанныеОтчета", ВременныеДанныеОтчета);
	
КонецПроцедуры

// В процедуре можно уточнить особенности вывода в отчет отдельного элемента в структуре данных.
//
// Параметры:
//  ПараметрыОтчета - Структура - см. ПодготовитьПараметрыОтчета() в ФормаОтчета.
//  МакетКомпоновки - МакетКомпоновкиДанных - описание выводимых данных.
//  ДанныеРасшифровки - ДанныеРасшифровкиКомпоновкиДанных - описание расшифровки для элемента в структуре данных.
//  ЭлементРезультата - ЭлементРезультатаКомпоновкиДанных - описание элемента в структуре данных.
//  Отказ        - Булево - если Истина, то не выводить эти данные в отчет.
//
Процедура ПередВыводомЭлементаРезультата(ПараметрыОтчета, МакетКомпоновки, ДанныеРасшифровки, ЭлементРезультата, Отказ = Ложь) Экспорт
	
	// Отсекаем валютные группировки на счетах, по которым не ведется
	// валютный учет.
	Если ЭлементРезультата.ЗначенияПараметров.Количество() > 0
		И ЭлементРезультата.ЗначенияПараметров.Найти("П1") <> Неопределено
		И ЗначениеЗаполнено(ЭлементРезультата.Макет)
		И ПараметрыОтчета.ВременныеДанныеОтчета.МакетВалюта.Найти(ЭлементРезультата.Макет) <> Неопределено
		И ЭлементРезультата.ЗначенияПараметров.П1.Значение = Null Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;

	Если ПараметрыОтчета.ПоказательВалютнаяСумма
		И ПараметрыОтчета.ВременныеДанныеОтчета.КоличествоПоказателей > 1 Тогда
		КоличествоПоказателей = ПараметрыОтчета.ВременныеДанныеОтчета.КоличествоПоказателей - 1;
	Иначе
		КоличествоПоказателей = ПараметрыОтчета.ВременныеДанныеОтчета.КоличествоПоказателей;
	КонецЕсли;

	// Обрабатываем элементы, содержащие корневые счета.
	Если ЭлементРезультата.ЗначенияПараметров.Количество() > 0 
		И ЭлементРезультата.ЗначенияПараметров.Найти("П1") <> Неопределено
		И ЗначениеЗаполнено(ЭлементРезультата.Макет)
		И ПараметрыОтчета.ВременныеДанныеОтчета.МакетШапкиОтчета.Найти(ЭлементРезультата.Макет) = Неопределено
		И ПараметрыОтчета.ВременныеДанныеОтчета.МакетыРесурсовПодвалаОтчета.Найти(ЭлементРезультата.Макет) = Неопределено Тогда

		// Запомним значение счета.
		Если ПараметрыОтчета.ВременныеДанныеОтчета.МакетСчет.Найти(ЭлементРезультата.Макет) <> Неопределено Тогда
			
			ПараметрыОтчета.ВременныеДанныеОтчета.ТекущийСчет = Неопределено;
			
			Для Каждого Параметр Из ЭлементРезультата.ЗначенияПараметров Цикл
				Если ТипЗнч(Параметр.Значение) = Тип("ИдентификаторРасшифровкиКомпоновкиДанных") Тогда
					
					ПараметрыОтчета.ВременныеДанныеОтчета.ТекущийСчет = ДанныеРасшифровки.Элементы[Параметр.Значение].ПолучитьПоля()[0].Значение;;
					Прервать;
					
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
		// Накапливаем суммы по корневым счетам.
		Если ПараметрыОтчета.ВременныеДанныеОтчета.МакетыРесурсовСчета.Найти(ЭлементРезультата.Макет) <> Неопределено 
			И ЗначениеЗаполнено(ПараметрыОтчета.ВременныеДанныеОтчета.ТекущийСчет)
			И Не ЗначениеЗаполнено(ПараметрыОтчета.ВременныеДанныеОтчета.ТекущийСчет.Родитель) 
			И МакетКомпоновки.Макеты[ЭлементРезультата.Макет].Макет.Количество() > 0 Тогда
			
			КолонкаРезультата = ПараметрыОтчета.ВременныеДанныеОтчета.СоответствиеМакетовКолонкамОтчета.Получить(ЭлементРезультата.Макет);
			Если КолонкаРезультата = Неопределено Тогда
				Возврат;
			КонецЕсли;

			Если ПараметрыОтчета.ВременныеДанныеОтчета.ТекущийСчет = ПараметрыОтчета.ВременныеДанныеОтчета.ПредыдущийКорневойСчет Тогда
				
				Если ПараметрыОтчета.ВременныеДанныеОтчета.МакетыПредыдущегоКорневогоСчета.Найти(КолонкаРезультата) = Неопределено Тогда
					// Новая группа показателей по той же строке.
					ПараметрыОтчета.ВременныеДанныеОтчета.МакетыПредыдущегоКорневогоСчета.Добавить(КолонкаРезультата);
					
				Иначе
					// Были движения по счету-группе, поэтому внутри группы вывелся сам счет-группа. В итоги это не должно попасть.
					Возврат;
					
				КонецЕсли;
				
			Иначе
				// Далее идет другой счет (или субсчет).
				ПараметрыОтчета.ВременныеДанныеОтчета.ПредыдущийКорневойСчет = ПараметрыОтчета.ВременныеДанныеОтчета.ТекущийСчет;
				ПараметрыОтчета.ВременныеДанныеОтчета.МакетыПредыдущегоКорневогоСчета = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(КолонкаРезультата);

			КонецЕсли;

			Для ИндексЭлемента = 0 По КоличествоПоказателей - 1 Цикл
				
				СтрокаМакета = МакетКомпоновки.Макеты[ЭлементРезультата.Макет].Макет[ИндексЭлемента];
				
				Для Каждого Ячейка Из СтрокаМакета.Ячейки Цикл
					
					Если Ячейка.Элементы.Количество() = 0 Тогда
						Продолжить;
					КонецЕсли;
					
					Для Каждого Элемент Из Ячейка.Элементы Цикл
						
						ИмяПараметра = Строка(Элемент.Значение);
						ПараметрРезультата = ЭлементРезультата.ЗначенияПараметров.Найти(ИмяПараметра);
						Если ПараметрРезультата = Неопределено Или ПараметрРезультата.Значение = Null Тогда
							Продолжить;
						КонецЕсли;
						
						Если ИндексЭлемента = (КоличествоПоказателей - 1)
						   И ПараметрыОтчета.ПоказательКонтроль
						   И Не ПараметрыОтчета.ВременныеДанныеОтчета.ТекущийСчет.НалоговыйУчет Тогда
							Продолжить;
						КонецЕсли;
						
						ИндексЯчейки = СтрокаМакета.Ячейки.Индекс(Ячейка);
						ИтогПоКолонке = ПараметрыОтчета.ВременныеДанныеОтчета.ЗначенияПоказателей[КолонкаРезультата];
						ТекущееЗначениеИтога = ИтогПоКолонке[ИндексЭлемента][ИндексЯчейки];
						ИтогПоКолонке[ИндексЭлемента][ИндексЯчейки] = ТекущееЗначениеИтога + ПараметрРезультата.Значение;
						
					КонецЦикла;
					
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЕсли; 

	// Проставляем накопленные суммы в подвал отчета.
	ИначеЕсли ПараметрыОтчета.ВременныеДанныеОтчета.МакетыРесурсовПодвалаОтчета.Найти(ЭлементРезультата.Макет) <> Неопределено Тогда

		КолонкаРезультата = ПараметрыОтчета.ВременныеДанныеОтчета.СоответствиеМакетовКолонкамОтчета.Получить(ЭлементРезультата.Макет);

		Если КолонкаРезультата <> Неопределено Тогда

			Для Каждого СтрокаМакета Из МакетКомпоновки.Макеты[ЭлементРезультата.Макет].Макет Цикл

				ИндексЭлемента = МакетКомпоновки.Макеты[ЭлементРезультата.Макет].Макет.Индекс(СтрокаМакета);

				Для Каждого Ячейка Из СтрокаМакета.Ячейки Цикл

					ИндексЯчейки =  СтрокаМакета.Ячейки.Индекс(Ячейка);

					Для Каждого Элемент Из Ячейка.Элементы Цикл

						ИмяПараметра = Строка(Элемент.Значение);
						ПараметрЭлемента = ЭлементРезультата.ЗначенияПараметров.Найти(ИмяПараметра);

						// Если параметр определен в макете, но его нет в элементе результата.
						Если ПараметрЭлемента = Неопределено Тогда
							
							// Добавляем параметра в элемент результата.
							ПараметрЭлемента = ЭлементРезультата.ЗначенияПараметров.Добавить();
							ПараметрЭлемента.Имя = ИмяПараметра;
							
						КонецЕсли;
						
						ПараметрЭлемента.Значение = ПараметрыОтчета.ВременныеДанныеОтчета.ЗначенияПоказателей[КолонкаРезультата][ИндексЭлемента][ИндексЯчейки];
						
					КонецЦикла;

				КонецЦикла;

			КонецЦикла;

		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

// В процедуре можно изменить табличный документ после вывода в него данных.
//
// Параметры:
//  ПараметрыОтчета - Структура - см. ПодготовитьПараметрыОтчета() в ФормаОтчета.
//  Результат    - ТабличныйДокумент - сформированный отчет.
//
Процедура ПослеВыводаРезультата(ПараметрыОтчета, Результат) Экспорт

	БухгалтерскиеОтчетыВызовСервера.ОбработкаРезультатаОтчета(ПараметрыОтчета.ИдентификаторОтчета, Результат);
	
	Если Результат.Области.Найти("Заголовок") = Неопределено Тогда
		Результат.ФиксацияСверху = ПараметрыОтчета.ВысотаШапки;
	Иначе
		Результат.ФиксацияСверху = Результат.Области.Заголовок.Низ + ПараметрыОтчета.ВысотаШапки;
	КонецЕсли;
	
	Результат.ФиксацияСлева = 0;
	
КонецПроцедуры

// Задает набор показателей, которые позволяет анализировать отчет.
//
// Возвращаемое значение:
//   Массив      - основные суммовые показатели отчета.
//
Функция ПолучитьНаборПоказателей() Экспорт
	
	НаборПоказателей = Новый Массив;
	НаборПоказателей.Добавить("БУ");
	НаборПоказателей.Добавить("НУ");
	НаборПоказателей.Добавить("ПР");
	НаборПоказателей.Добавить("ВР");
	НаборПоказателей.Добавить("Контроль");
	НаборПоказателей.Добавить("ВалютнаяСумма");
	
	БухгалтерскиеОтчетыПереопределяемый.ДополнительныеПоказателиБухгалтерскихОтчетов(НаборПоказателей);
	
	Возврат НаборПоказателей;
	
КонецФункции

#КонецОбласти

#Область РасшифровкаСтандартныхОтчетов

// Заполняет настройки расшифровки (см. БухгалтерскиеОтчетыКлиентСервер.НовыйУниверсальныеНастройки) для переданного экземпляра отчета.
//
// Параметры:
//  Настройки				 - Структура								 - Настройки расшифровки отчета, которые нужно заполнить (см. БухгалтерскиеОтчетыКлиентСервер.НовыйУниверсальныеНастройки).
//  Объект					 - ОтчетОбъект								 - Отчет из данных которого нудно собрать универсальные настройки.
//  ДанныеРасшифровки		 - ДанныеРасшифровкиКомпоновкиДанных		 - Данные расшифровки отчета.
//  ИдентификаторРасшифровки - ИдентификаторРасшифровкиКомпоновкиДанных  - Идентификатор расшифровки из ячейки для которой вызвана расшифровка.
//  РеквизитыРасшифровки	 - Структура								 - Реквизиты отчета полученные из контекста расшифровываемой ячейки.
//
Процедура ЗаполнитьНастройкиРасшифровки(Настройки, Объект, ДанныеРасшифровки, ИдентификаторРасшифровки, РеквизитыРасшифровки) Экспорт

	БухгалтерскиеОтчетыРасшифровка.ЗаполнитьНастройкиРасшифровкиПоДаннымСтандартногоОтчета(Настройки, ДанныеРасшифровки, ИдентификаторРасшифровки, Объект, РеквизитыРасшифровки);
	
	Счет = Неопределено;
	Настройки.ТаблицаГруппировка.Очистить();
	Если Настройки.Свойство("Счет", Счет) Тогда
		НужноПоменятьНумерациюСубконто = Ложь;
		СтрокаГруппировкиПоСчету = Объект.Группировка.Найти(Счет, "Счет");
		
		Если СтрокаГруппировкиПоСчету = Неопределено Тогда
			
			СтрокаГруппировкиПоСчету = Объект.Группировка.Найти(Счет.Родитель, "Счет");
			НужноПоменятьНумерациюСубконто = Истина;
			
		КонецЕсли;
		
		Если СтрокаГруппировкиПоСчету <> Неопределено Тогда
			
			ДанныеСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Счет);
			
			СтрокаПоСубконто = СтрокаГруппировкиПоСчету.ПоСубконто;
			
			// В СтрокаПоСубконто, храниться порядок и признак использования видов субконто счета, в следующем виде:
			// "+1-2" - значение приведенной строки указывает на то, что у счета 2 вида субконто и Субконто1 используется,
			// а у Субконто2 флаг использования снят.
			// Ниже произведем анализ подобной строки и установим группировки в соответствии с ее содержанием.
			
			КартаВидовСубконто = Новый Соответствие;
			НомерСубконтоВЗапросе = 1;
			ТипЧисло = Новый ОписаниеТипов("Число");
			КоличествоСубконто = СтрДлина(СтрокаПоСубконто) / 2; 
			Для Индекс = 1 По КоличествоСубконто Цикл
				ИндексСубконто = Сред(СтрокаПоСубконто, Индекс*2, 1);
				Если ИндексСубконто = "0" Тогда
					
					СтрокаГруппировки = БухгалтерскиеОтчетыКлиентСервер.НовыйСтрокаГруппировки();
					СтрокаГруппировки.Поле = 			"Подразделение";
					СтрокаГруппировки.Представление = 	НСтр("ru = 'Подразделение';
																|en = 'Business unit'");
					СтрокаГруппировки.Использование = 	?(Сред(СтрокаПоСубконто, Индекс * 2 - 1, 1) = "+", Истина, Ложь);
					СтрокаГруппировки.ТипГруппировки = 	0;
					
					Настройки.ТаблицаГруппировка.Добавить(СтрокаГруппировки);
					
				Иначе
					НомерСубконто = Сред(СтрокаПоСубконто, Индекс*2, 1);
					СтрокаГруппировки = БухгалтерскиеОтчетыКлиентСервер.НовыйСтрокаГруппировки();
					СтрокаГруппировки.Поле = 			"Субконто" + НомерСубконто;
					СтрокаГруппировки.Представление = 	ДанныеСчета["ВидСубконто" + Сред(СтрокаПоСубконто, Индекс*2, 1) + "Наименование"];
					СтрокаГруппировки.Использование = 	?(Сред(СтрокаПоСубконто, Индекс * 2 - 1, 1) = "+", Истина, Ложь);
					СтрокаГруппировки.ТипГруппировки = 	0;
					
					Настройки.ТаблицаГруппировка.Добавить(СтрокаГруппировки);
					
					НомерСубконтоЧислом = ТипЧисло.ПривестиЗначение(НомерСубконто);
															
				КонецЕсли;
			КонецЦикла;
			
			Если НужноПоменятьНумерациюСубконто Тогда
				
				БухгалтерскиеОтчетыРасшифровка.ПоменятьНумерациюСубконтоВТабличнойЧасти("ТаблицаГруппировка", Настройки, Счет.Родитель, Счет);
				
			КонецЕсли;
			
		КонецЕсли;
		
		СтрокаРазвернутогоСальдоПоСчету = Объект.РазвернутоеСальдо.Найти(Счет, "Счет");
		
		Если СтрокаРазвернутогоСальдоПоСчету = Неопределено Тогда
			
			СтрокаРазвернутогоСальдоПоСчету = Объект.РазвернутоеСальдо.Найти(Счет.Родитель, "Счет");
			
		КонецЕсли;
		
		Если СтрокаРазвернутогоСальдоПоСчету <> Неопределено И СтрокаРазвернутогоСальдоПоСчету.Использование Тогда
			
			СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Счет);
			Настройки.РазвернутоеСальдо = СвойстваСчета.Вид = ВидСчета.АктивноПассивный;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Адаптирует переданные настройки для данного вида отчетов.
// Перед применением настроек расшифровки может возникнуть необходимость учесть особенности этого вида отчетов.
//
// Параметры:
//  Настройки	 - Структура - Настройки которые нужно адаптировать (см. БухгалтерскиеОтчетыКлиентСервер.НовыйУниверсальныеНастройки).
//
Процедура АдаптироватьНастройки(Настройки) Экспорт
	
КонецПроцедуры

// Устанавливает, какими отчетами и при каких условиях может быть расшифрован этот вид отчетов.
//
// Параметры:
//  Правила - ТаблицаЗначений с правилами расшифровки этого отчета см. БухгалтерскиеОтчетыРасшифровка.НовыйПравилаРасшифровки().
//
Процедура ЗаполнитьПравилаРасшифровки(Правила) Экспорт
	
	Правило = Правила.Добавить();
	Правило.Отчет = "ОборотноСальдоваяВедомостьПоСчету";
	БухгалтерскиеОтчетыРасшифровка.ДобавитьТребуемыйРеквизитРасшифровки(Правило.ТребуемыеРеквизиты, "Счет", "Счет");
	Правило.ШаблонПредставления = НСтр("ru = 'ОСВ по счету [Счет]';
										|en = 'Trial balance on [Счет] account'");
	
	Правило = Правила.Добавить();
	Правило.Отчет = "КарточкаСчета";
	БухгалтерскиеОтчетыРасшифровка.ДобавитьТребуемыйРеквизитРасшифровки(Правило.ТребуемыеРеквизиты, "Счет", "Счет");
	Правило.ШаблонПредставления = НСтр("ru = 'Карточка счета [Счет]';
										|en = '[Счет] account card'");
	
	Правило = Правила.Добавить();
	Правило.Отчет = "АнализСчета";
	БухгалтерскиеОтчетыРасшифровка.ДобавитьТребуемыйРеквизитРасшифровки(Правило.ТребуемыеРеквизиты, "Счет", "Счет");
	Правило.ШаблонПредставления = НСтр("ru = 'Анализ счета [Счет]';
										|en = '[Счет] account analysis'");
	
	Правило = Правила.Добавить();
	Правило.Отчет = "ОборотыСчета";
	БухгалтерскиеОтчетыРасшифровка.ДобавитьТребуемыйРеквизитРасшифровки(Правило.ТребуемыеРеквизиты, "Счет", "Счет");
	БухгалтерскиеОтчетыРасшифровка.ДобавитьТребуемыйРеквизитРасшифровки(Правило.ТребуемыеРеквизиты, "Периодичность", "", 9);
	Правило.ШаблонПредставления = НСтр("ru = 'Обороты счета [Счет] по месяцам';
										|en = '[Счет] account turnovers by months'");
	
	Правило = Правила.Добавить();
	Правило.Отчет = "ОборотыСчета";
	БухгалтерскиеОтчетыРасшифровка.ДобавитьТребуемыйРеквизитРасшифровки(Правило.ТребуемыеРеквизиты, "Счет", "Счет");
	БухгалтерскиеОтчетыРасшифровка.ДобавитьТребуемыйРеквизитРасшифровки(Правило.ТребуемыеРеквизиты, "Периодичность", "", 6);
	Правило.ШаблонПредставления = НСтр("ru = 'Обороты счета [Счет] по дням';
										|en = '[Счет] account turnovers by days'");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьСтруктуруОтчета(КомпоновщикНастроек, ПараметрыОтчета, ПараметрыАлгоритма)
	
	// Очищаем структуру отчета, для того чтобы сформировать ее по установленным настройкам.
	КомпоновщикНастроек.Настройки.Структура.Очистить();
	КомпоновщикНастроек.Настройки.Выбор.Элементы.Очистить();
	
	БухгалтерскиеОтчетыКлиентСервер.ДобавитьВыбранноеПоле(КомпоновщикНастроек.Настройки.Выбор, "Счет");
	
	// Основой структуры отчета будет таблица.
	Структура = КомпоновщикНастроек.Настройки.Структура.Добавить(Тип("ТаблицаКомпоновкиДанных"));
	
	Структура.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));

	// Далее добавляем колонки в таблицу.
	СоздатьКолонкиОтчета(Структура.Колонки, ПараметрыОтчета);
	
	УсловноеОформлениеАвтоотступа = Неопределено;
	Для каждого ЭлементОформления Из КомпоновщикНастроек.Настройки.УсловноеОформление.Элементы Цикл
		Если ЭлементОформления.Представление = НСтр("ru = 'Уменьшенный автоотступ';
													|en = 'Reduced auto indent'") Тогда
			УсловноеОформлениеАвтоотступа = ЭлементОформления;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если УсловноеОформлениеАвтоотступа = Неопределено Тогда
		УсловноеОформлениеАвтоотступа = КомпоновщикНастроек.Настройки.УсловноеОформление.Элементы.Добавить();
		УсловноеОформлениеАвтоотступа.Представление = НСтр("ru = 'Уменьшенный автоотступ';
															|en = 'Reduced auto indent'");
		УсловноеОформлениеАвтоотступа.Оформление.УстановитьЗначениеПараметра("Автоотступ", 1);
		УсловноеОформлениеАвтоотступа.Использование = Ложь;
		УсловноеОформлениеАвтоотступа.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ;
	Иначе
		УсловноеОформлениеАвтоотступа.Поля.Элементы.Очистить();
	КонецЕсли;
	
	// Добавим в структуру отчета 2 группировки по счету с разным отбором и параметрами.
	// Группировка по балансовым счетам.
	ГруппировкаПоБалансовымСчетам = Структура.Строки.Добавить();
	
	// Заполняем основные параметры группировки.
	ПолеГруппировки = ГруппировкаПоБалансовымСчетам.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
	ПолеГруппировки.Использование  = Истина;
	ПолеГруппировки.Поле           = Новый ПолеКомпоновкиДанных("Счет");
	ПолеГруппировки.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.Иерархия;
	ГруппировкаПоБалансовымСчетам.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
	ГруппировкаПоБалансовымСчетам.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
	
	// Добавим автоотступ.
	ПолеОформления = УсловноеОформлениеАвтоотступа.Поля.Элементы.Добавить();
	ПолеОформления.Поле = ПолеГруппировки.Поле;

	// Установка отбора на выводимый уровень иерархии счета.
	ГруппаЭлементовОтбора = ГруппировкаПоБалансовымСчетам.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаЭлементовОтбора.Применение = ТипПримененияОтбораКомпоновкиДанных.Иерархия;
	ГруппаЭлементовОтбора.ТипГруппы  = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ГруппаЭлементовОтбора, "ПараметрыДанных.ПоСубсчетам", Истина);
	БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ГруппаЭлементовОтбора, "СистемныеПоля.УровеньВГруппировке", 1);
	БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ГруппаЭлементовОтбора, "Счет", ПараметрыАлгоритма.СписокСчетовПоСубсчетам, ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии, ПараметрыАлгоритма.СписокСчетовПоСубсчетам.Количество() > 0);
	
	// Устанавливаем отбор по признаку Забалансовый.
	БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ГруппировкаПоБалансовымСчетам.Отбор, "Счет.Забалансовый", Ложь);
	
	// Отключим вывод отбора.
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметрВывода(ГруппировкаПоБалансовымСчетам, "ВыводитьОтбор", ТипВыводаТекстаКомпоновкиДанных.НеВыводить);
	
	// Группировка по забалансовым счетам.
	ГруппировкаПоЗабалансовымСчетам = Структура.Строки.Добавить();
	
	// Заполняем основные параметры группировки.
	ПолеГруппировки = ГруппировкаПоЗабалансовымСчетам.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
	ПолеГруппировки.Использование  = Истина;
	ПолеГруппировки.Поле           = Новый ПолеКомпоновкиДанных("Счет");
	ПолеГруппировки.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.Иерархия;
	ГруппировкаПоЗабалансовымСчетам.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
	ГруппировкаПоЗабалансовымСчетам.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
	
	// Добавим автоотступ.
	ПолеОформления = УсловноеОформлениеАвтоотступа.Поля.Элементы.Добавить();
	ПолеОформления.Поле = ПолеГруппировки.Поле;
	
	// Установка отбора на выводимый уровень иерархии счета.
	ГруппаЭлементовОтбора = ГруппировкаПоЗабалансовымСчетам.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаЭлементовОтбора.Применение = ТипПримененияОтбораКомпоновкиДанных.Иерархия;
	ГруппаЭлементовОтбора.ТипГруппы  = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ГруппаЭлементовОтбора, "ПараметрыДанных.ПоСубсчетам", Истина);
	БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ГруппаЭлементовОтбора, "СистемныеПоля.УровеньВГруппировке", 1);
	БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ГруппаЭлементовОтбора, "Счет", ПараметрыАлгоритма.СписокСчетовПоСубсчетам, ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии, ПараметрыАлгоритма.СписокСчетовПоСубсчетам.Количество() > 0);
	
	// Устанавливаем отбор по признаку Забалансовый.
	БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ГруппировкаПоЗабалансовымСчетам.Отбор, "Счет.Забалансовый", Истина);
	
	// Отключим вывод отбора и итогов.
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметрВывода(ГруппировкаПоЗабалансовымСчетам, "ВыводитьОтбор", ТипВыводаТекстаКомпоновкиДанных.НеВыводить);
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметрВывода(ГруппировкаПоЗабалансовымСчетам, "РасположениеОбщихИтогов", РасположениеИтоговКомпоновкиДанных.Нет);
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ПоСубсчетам", ПараметрыОтчета.ПоСубсчетам);
	
	// Добавленные группировки сложим в массив для удобства дальнейшей доработки структуры.
	ГруппировкиПоСчетам = Новый Массив;
	ГруппировкиПоСчетам.Добавить(ГруппировкаПоБалансовымСчетам);
	ГруппировкиПоСчетам.Добавить(ГруппировкаПоЗабалансовымСчетам);
	
	// Для валютных сумм, добавляем группировку по валюте.
	Если ПараметрыОтчета.ПоказательВалютнаяСумма Тогда
		
		Для Каждого Группировка Из ГруппировкиПоСчетам Цикл
			
			Структура = Группировка.Структура.Добавить();
			ПолеГруппировки = Структура.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
			ПолеГруппировки.Использование  = Истина;
			ПолеГруппировки.Поле           = Новый ПолеКомпоновкиДанных("Валюта");
			ПолеГруппировки.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.Элементы;
			
			Структура.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
			Структура.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
			
			// Устанавливаем отбор по признаку Валютный.
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(Структура.Отбор, "Счет.Валютный", Истина);
			// Отключим вывод отбора и итогов.
			БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметрВывода(Структура, "ВыводитьОтбор", ТипВыводаТекстаКомпоновкиДанных.НеВыводить);
			
			ПолеОформления = УсловноеОформлениеАвтоотступа.Поля.Элементы.Добавить();
			ПолеОформления.Поле = ПолеГруппировки.Поле;
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Добавляем группировки для разворота по субконто.
	Если ПараметрыАлгоритма.ЕстьДетализацияПоСубконто Тогда
		
		Для Каждого Группировка Из ГруппировкиПоСчетам Цикл
			
			Структура = Группировка;
			
			Для Индекс = 1 По ПараметрыАлгоритма.КоличествоСубконтоДетализации Цикл 
				
				Структура = Структура.Структура.Добавить();
				ПолеГруппировки = Структура.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
				ПолеГруппировки.Использование  = Истина;
				ПолеГруппировки.Поле           = Новый ПолеКомпоновкиДанных("Субконто" + Индекс);
				ПолеГруппировки.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.Элементы;
				
				Структура.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
				Структура.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
				
				БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(Структура.Отбор, ПолеГруппировки.Поле,"", ВидСравненияКомпоновкиДанных.Заполнено);
				БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметрВывода(Структура, "ВыводитьОтбор", ТипВыводаТекстаКомпоновкиДанных.НеВыводить);
				
				ПолеОформления = УсловноеОформлениеАвтоотступа.Поля.Элементы.Добавить();
				ПолеОформления.Поле = ПолеГруппировки.Поле;
				
				Если ПараметрыОтчета.ПоказательВалютнаяСумма Тогда
					ГруппировкаПоВалюте = Структура.Структура.Добавить();
					ПолеГруппировки = ГруппировкаПоВалюте.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
					ПолеГруппировки.Использование  = Истина;
					ПолеГруппировки.Поле           = Новый ПолеКомпоновкиДанных("Валюта");
					ПолеГруппировки.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.Элементы;
					
					ГруппировкаПоВалюте.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
					ГруппировкаПоВалюте.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
					
					// Устанавливаем отбор по признаку Валютный.
					БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ГруппировкаПоВалюте.Отбор, "Счет.Валютный", Истина);
					
					// Отключим вывод отбора и итогов.
					БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметрВывода(ГруппировкаПоВалюте, "ВыводитьОтбор", ТипВыводаТекстаКомпоновкиДанных.НеВыводить);
					
					ПолеОформления = УсловноеОформлениеАвтоотступа.Поля.Элементы.Добавить();
					ПолеОформления.Поле = ПолеГруппировки.Поле;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Дополнительные данные.
	БухгалтерскиеОтчетыВызовСервера.ДобавитьДополнительныеПоля(ПараметрыОтчета, КомпоновщикНастроек);
	
	// Добавляем отбор по организации.
	БухгалтерскиеОтчетыВызовСервера.ДобавитьОтборПоОрганизации(ПараметрыОтчета, КомпоновщикНастроек);

	БухгалтерскиеОтчетыВызовСервера.ДобавитьОтборДляПоказателяКонтроль(ПараметрыОтчета, КомпоновщикНастроек);
	
	// Добавляем отбор по ресурсам отчета.
	ДобавитьОтборПоРесурсам(ПараметрыОтчета, КомпоновщикНастроек);
	
	// Оформление итогов.
	ДобавитьОформлениеИтогов(ПараметрыОтчета, КомпоновщикНастроек);
	
	// У счетов по которым не ведется налоговый учет, уберем показатели НУ,ПР, ВР и контроль.
	// Для этого условным оформлением установим высоту строк для этих показателе равной 1.
	// Позже при обработке таблицы результата такие строки будут удалены.
	// Добавляем элемент условного оформления.
	УсловноеОформление = КомпоновщикНастроек.Настройки.УсловноеОформление.Элементы.Добавить();
	
	// Указываем оформляемые поля.
	Поле = УсловноеОформление.Поля.Элементы.Добавить();
	Поле.Поле = Новый ПолеКомпоновкиДанных("Показатели.НУ");
	Поле = УсловноеОформление.Поля.Элементы.Добавить();
	Поле.Поле = Новый ПолеКомпоновкиДанных("Показатели.ПР");
	Поле = УсловноеОформление.Поля.Элементы.Добавить();
	Поле.Поле = Новый ПолеКомпоновкиДанных("Показатели.ВР");
	Поле = УсловноеОформление.Поля.Элементы.Добавить();
	Поле.Поле = Новый ПолеКомпоновкиДанных("Показатели.Контроль");
	Поле = УсловноеОформление.Поля.Элементы.Добавить();
	
	// Включим использование этого элемента условного оформления только в группировке и иерархической группировке.
	УсловноеОформление.ИспользоватьВЗаголовке                         = ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать;
	УсловноеОформление.ИспользоватьВЗаголовкеОбщегоИтога              = ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать;
	УсловноеОформление.ИспользоватьВЗаголовкеПолей                    = ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать;
	УсловноеОформление.ИспользоватьВЗаголовкеПолейРесурсов            = ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать;
	УсловноеОформление.ИспользоватьВЗаголовкеПолейРесурсовОбщегоИтога = ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать;
	УсловноеОформление.ИспользоватьВОбщемИтоге                        = ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать;
	УсловноеОформление.ИспользоватьВОтборе                            = ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать;
	УсловноеОформление.ИспользоватьВПараметрах                        = ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать;
	
	ГруппаОтбора = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(УсловноеОформление.Отбор.Элементы,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ГруппаОтбора, "Счет",
		ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.Вспомогательный"), ВидСравненияКомпоновкиДанных.Равно);
	БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ГруппаОтбора, "Счет.НалоговыйУчет", Ложь);
	
	// Устанавливаем параметр оформления.
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(УсловноеОформление.Оформление, "МаксимальнаяВысота", 1);
	
	Если УсловноеОформлениеАвтоотступа.Поля.Элементы.Количество() = 0 Тогда
		УсловноеОформлениеАвтоотступа.Использование = Ложь;
	КонецЕсли;

КонецПроцедуры

// Для списка счетов подбирает субсчета, у которых имеются такие же виды субконто как требуются для счетов.
//
// Параметры:
//  СчетаГруппы  - СписокЗначений - счета, явно используемые в настройках.
//  СубконтоДетализацииПоСчетам - Соответствие - набор требуемых видов субконто для каждого счета.
//
// Возвращаемое значение:
//   СписокЗначений - счета, у всех из которых есть требуемые субконто.
//
Функция СчетаССовпадающимНаборомСубконто(СчетаГруппы, СубконтоДетализацииПоСчетам);

	Если СчетаГруппы.Количество() = 0 Тогда
		Возврат СчетаГруппы;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ХозрасчетныйВидыСубконто.ВидСубконто КАК ВидСубконто,
	|	ХозрасчетныйВидыСубконто.НомерСтроки КАК НомерСубконто,
	|	ХозрасчетныйВидыСубконто.Ссылка КАК Счет
	|ИЗ
	|	ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто
	|ГДЕ
	|	ХозрасчетныйВидыСубконто.Ссылка В(&СчетаГруппы)");
	Запрос.УстановитьПараметр("СчетаГруппы", СчетаГруппы);
	СчетаИСубконто = Запрос.Выполнить().Выгрузить();
	СчетаИСубконто.Индексы.Добавить("Счет");
	
	ШаблонЗапроса = 
	"ВЫБРАТЬ
	|	ХозрасчетныйВидыСубконто.Ссылка
	|ИЗ
	|	ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто
	|ГДЕ
	|	ХозрасчетныйВидыСубконто.Ссылка В ИЕРАРХИИ(&[Счет])
	|	И ХозрасчетныйВидыСубконто.ВидСубконто В (&[ВидСубконто1], &[ВидСубконто2], &[ВидСубконто3])
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйВидыСубконто.Ссылка
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(ХозрасчетныйВидыСубконто.Ссылка) = &[КоличествоСубконто]";

	Разделитель = "
	|
	|ОБЪЕДИНИТЬ
	|
	|";
	
	Запрос.Текст = "";
	НужноВыполнятьЗапрос = Ложь;
	СчетаИсключение = Новый Массив;
	Для Каждого ИсключенныйСчет Из СчетаГруппы Цикл
		
		Счет = ИсключенныйСчет.Значение;
		ВидыСубконто = СубконтоДетализацииПоСчетам.Получить(Счет);
		Если ВидыСубконто = Неопределено Тогда
			Продолжить;
		КонецЕсли;
			
		Если БухгалтерскийУчетПовтИсп.СчетаВИерархии(Счет).Количество() > 1 Тогда
			
			ИндексСчета = СчетаГруппы.Индекс(ИсключенныйСчет);
			
			Запрос.УстановитьПараметр("Счет" + ИндексСчета, Счет);
			
			ПараметрыШаблона = Новый Структура();
			ПараметрыШаблона.Вставить("Счет", "Счет" + ИндексСчета);
			
			ВидыСубконтоСчета = СчетаИСубконто.НайтиСтроки(Новый Структура("Счет", Счет));
			Для Каждого ВидСубконто Из ВидыСубконтоСчета Цикл
				
				Если ВидыСубконто.НайтиПоЗначению(ВидСубконто.ВидСубконто) <> Неопределено Тогда
					
					Запрос.УстановитьПараметр("ВидСубконто" + ВидСубконто.НомерСубконто + "_" + ИндексСчета, ВидСубконто.ВидСубконто);
					ПараметрыШаблона.Вставить("ВидСубконто" + ВидСубконто.НомерСубконто, "ВидСубконто" + ВидСубконто.НомерСубконто + "_" + ИндексСчета);
					
				КонецЕсли;
				
			КонецЦикла;

			Запрос.УстановитьПараметр("КоличествоСубконто_" + ИндексСчета, ВидыСубконто.Количество());
			ПараметрыШаблона.Вставить("КоличествоСубконто", "КоличествоСубконто_" + ИндексСчета);
			
			Запрос.Текст = Запрос.Текст + СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонЗапроса, ПараметрыШаблона) + Разделитель;
			
			НужноВыполнятьЗапрос = Истина;
			
		Иначе
			
			СчетаИсключение.Добавить(Счет);
			
		КонецЕсли;
		
	КонецЦикла;
	
	СтруктураПараметров = Новый Структура("ВидСубконто1, ВидСубконто2, ВидСубконто3", "ВидСубконто1", "ВидСубконто2", "ВидСубконто3");
	Запрос.Текст = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(Запрос.Текст, СтруктураПараметров);
	Запрос.УстановитьПараметр("ВидСубконто1", Неопределено);
	Запрос.УстановитьПараметр("ВидСубконто2", Неопределено);
	Запрос.УстановитьПараметр("ВидСубконто3", Неопределено);
	
	СписокСчетов = Новый СписокЗначений;
	Если НужноВыполнятьЗапрос Тогда
		
		Запрос.Текст = Лев(Запрос.Текст, СтрДлина(Запрос.Текст) - 13);
		СписокСчетов.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
		
	КонецЕсли;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СписокСчетов, СчетаИсключение);
		
	Возврат СписокСчетов;
	
КонецФункции

Функция ЗаполнитьМакетыРесурсовГруппировки(Таблица, МассивМакетов, СоответствиеМакетовКолонкамОтчета, ПолеГруппировки = Неопределено, ВключатьМакетыВложенныхГруппировок = Ложь, ПрочитатьМакетыРесурсов = Ложь)
	
	// Перебираем все элементы макета.
	Для Каждого Группировка Из Таблица Цикл
		
		Если ТипЗнч(Группировка) = Тип("ГруппировкаТаблицыМакетаКомпоновкиДанных") Тогда
			// Если это группировка, проверим поле группировки.
			Если ПолеГруппировки = Неопределено
			 Или Группировка.Группировка.Количество() > 0 И Группировка.Группировка[0].ИмяПоля = ПолеГруппировки Тогда
				
				Если ВключатьМакетыВложенныхГруппировок Тогда
					// Перебираем вложенные группировки. Условие по полю группировки в них не накладываем.
					ЗаполнитьМакетыРесурсовГруппировки(Группировка.Тело, МассивМакетов, СоответствиеМакетовКолонкамОтчета,,,Истина);
				Иначе
					ЗаполнитьМакетыРесурсовГруппировки(Группировка.Тело, МассивМакетов, СоответствиеМакетовКолонкамОтчета, ПолеГруппировки, ВключатьМакетыВложенныхГруппировок, Истина);
				КонецЕсли;
				
				// Перебираем иерархию группировки.
				Для Каждого ТелоИерархии Из Группировка.ТелоИерархии Цикл
					
					Если ТипЗнч(ТелоИерархии) = Тип("МакетГруппировкиТаблицыМакетаКомпоновкиДанных") Тогда
						
						Для Каждого МакетРесурсов Из ТелоИерархии.МакетРесурсов Цикл
							// Помещаем макеты в массив.
							МассивМакетов.Добавить(МакетРесурсов.Макет);
							// Добавляем соответствие макетов ресурсов и колонок отчета.
							СоответствиеМакетовКолонкамОтчета.Вставить(МакетРесурсов.Макет, МакетРесурсов.МакетГруппировки);
							
						КонецЦикла;
						
					КонецЕсли;
				
				КонецЦикла;
				
			Иначе
				// Если эта группировка не подошла по условию проверим вложенные группировки.
				ЗаполнитьМакетыРесурсовГруппировки(Группировка.Тело, МассивМакетов, СоответствиеМакетовКолонкамОтчета, ПолеГруппировки, ВключатьМакетыВложенныхГруппировок);

			КонецЕсли;
			
		ИначеЕсли ТипЗнч(Группировка) = Тип("МакетГруппировкиТаблицыМакетаКомпоновкиДанных") И ПрочитатьМакетыРесурсов Тогда
			
			Для Каждого МакетРесурсов Из Группировка.МакетРесурсов Цикл
				
				// Помещаем макеты в массив.
				МассивМакетов.Добавить(МакетРесурсов.Макет);
				// Добавляем соответствие макетов ресурсов и колонок отчета.
				СоответствиеМакетовКолонкамОтчета.Вставить(МакетРесурсов.Макет, МакетРесурсов.МакетГруппировки);
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецФункции

Функция ЗаполнитьМакетыРесурсовПодвалаОтчета(Таблица, МассивМакетов, СоответствиеМакетовКолонкамОтчета)

	Для Каждого Группировка Из Таблица.Строки Цикл

		Для Каждого МакетРесурсов Из Группировка.МакетПодвала.МакетРесурсов Цикл

			МассивМакетов.Добавить(МакетРесурсов.Макет);

			СоответствиеМакетовКолонкамОтчета.Вставить(МакетРесурсов.Макет, МакетРесурсов.МакетГруппировки);

		КонецЦикла;

	КонецЦикла;

КонецФункции

Процедура ДобавитьОтборПоРесурсам(ПараметрыОтчета, КомпоновщикНастроек)
	
	// При выводе показателей Контроль или СверкаНУ строки в отчет выводим только в том случае,
	// если по ним есть хотя бы одна ненулевая сумма.
	// По остальным показателям (БУ, НУ, ПР, ВР, Валютная сумма) этой проверки не требуется,
	// т.к. виртуальные таблицы регистра бухгалтерии самостоятельно отфильтровывают полностью нулевые строки.
	// Исключение может составлять случай, когда по счету-группе в целом суммы нулевые, а по его субсчетам
	// есть противоположные по знаку обороты, которые в итоге свертываются в нуль. 
	// Такие обнулившиеся на верхнем уровне строки правильнее показывать, чтобы пользователь видел, что движения были.

	Если Не ПараметрыОтчета.ПоказательКонтроль
	   И Не ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОтчета, "ПоказательСверкаНУ", Ложь) Тогда
		Возврат;
	КонецЕсли;

	КоличествоВыбранныхПоказателей = 0;
	Для Каждого ИмяПоказателя Из ПолучитьНаборПоказателей() Цикл
		Если ПараметрыОтчета["Показатель" + ИмяПоказателя] Тогда
			КоличествоВыбранныхПоказателей = КоличествоВыбранныхПоказателей + 1;
		КонецЕсли;
	КонецЦикла;
	
	Если КоличествоВыбранныхПоказателей > 1 Тогда
		// Выбрано несколько показателей, фильтр не накладываем.
		Возврат;
	КонецЕсли;

	УсловноеОформление = КомпоновщикНастроек.Настройки.УсловноеОформление.Элементы.Добавить();
	
	// Указываем оформляемые поля.
	Поле = УсловноеОформление.Поля.Элементы.Добавить();
	Поле.Поле = Новый ПолеКомпоновкиДанных("Счет");
	
	УсловноеОформление.ИспользоватьВЗаголовке = ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать;
	УсловноеОформление.ИспользоватьВЗаголовкеПолей = ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать;
	
	// Устанавливаем параметр оформления.
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(УсловноеОформление.Оформление, "МаксимальнаяВысота", 1);
	
	Группа = УсловноеОформление.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	Группа.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	ИмяПоля = ?(ПараметрыОтчета.ПоказательКонтроль, "Контроль", "СверкаНУ");

	БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(Группа, "СальдоНаНачалоПериода." + ИмяПоля + "НачальныйОстатокДт", 0, ВидСравненияКомпоновкиДанных.Равно);
	
	БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(Группа, "СальдоНаНачалоПериода." + ИмяПоля + "НачальныйОстатокКт", 0, ВидСравненияКомпоновкиДанных.Равно);
	
	БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(Группа, "СальдоНаКонецПериода." + ИмяПоля + "КонечныйОстатокДт", 0, ВидСравненияКомпоновкиДанных.Равно);
	
	БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(Группа, "СальдоНаКонецПериода." + ИмяПоля + "КонечныйОстатокКт", 0, ВидСравненияКомпоновкиДанных.Равно);
			
	БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(Группа, "ОборотыЗаПериод." + ИмяПоля + "ОборотДт", 0, ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(Группа, "ОборотыЗаПериод." + ИмяПоля + "ОборотКт", 0, ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
КонецПроцедуры

Процедура ДобавитьОформлениеИтогов(ПараметрыОтчета, КомпоновщикНастроек)
	
	УсловноеОформление = КомпоновщикНастроек.Настройки.УсловноеОформление.Элементы.Добавить();
	
	УсловноеОформление.ИспользоватьВЗаголовке 					= ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать;
	УсловноеОформление.ИспользоватьВЗаголовкеПолей 				= ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать;
	УсловноеОформление.ИспользоватьВГруппировке 				= ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать;
	УсловноеОформление.ИспользоватьВЗаголовке 					= ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать;
	УсловноеОформление.ИспользоватьВЗаголовкеПолей 				= ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать;
	УсловноеОформление.ИспользоватьВИерархическойГруппировке 	= ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать;
	УсловноеОформление.ИспользоватьВОтборе 						= ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать;
	УсловноеОформление.ИспользоватьВПараметрах 					= ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать;
	
	ПолужирныйШрифт = Новый Шрифт(,10,Истина);
	
	// Устанавливаем параметр оформления.
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(УсловноеОформление.Оформление, "Шрифт", ПолужирныйШрифт);
КонецПроцедуры

Функция СчетаВИерархии(СчетГруппа, ОграничениеПоСубсчетам = Неопределено)
	
	Если Не ЗначениеЗаполнено(СчетГруппа) Тогда
		Возврат Новый Массив;
	КонецЕсли;

	// В отличие от функции БухгалтерскийУчет.СформироватьМассивСубсчетов() в данном случае для нас важно отсечь счета,
	// по которым не должно быть проводок в информационной базе, т.к. может сложиться ситуация, когда у счета-группы будут
	// субсчета, сами являющиеся группами.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СчетГруппа", СчетГруппа);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Хозрасчетный.Ссылка КАК Счет
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный КАК Хозрасчетный_Субсчета
	|		ПО Хозрасчетный.Ссылка = Хозрасчетный_Субсчета.Родитель
	|ГДЕ
	|	Хозрасчетный.Ссылка В ИЕРАРХИИ(&СчетГруппа)
	|	И Хозрасчетный_Субсчета.Родитель ЕСТЬ NULL";
	Результат = Запрос.Выполнить();
	
	Если ОграничениеПоСубсчетам = Неопределено Тогда
		
		СчетаДляОтдельногоЗапроса = Результат.Выгрузить().ВыгрузитьКолонку("Счет");
		
	Иначе
		// Добавляем только субсчета, относящиеся к данной группе настроек.
		СчетаДляОтдельногоЗапроса = Новый Массив;
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			Если ОграничениеПоСубсчетам[Выборка.Счет] = Истина Тогда
				СчетаДляОтдельногоЗапроса.Добавить(Выборка.Счет);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат СчетаДляОтдельногоЗапроса;

КонецФункции

Функция ИерархияСчетовСписка(Счета)
	
	Иерархия = Новый Соответствие;
	
	Для Каждого ПроверяемыйСчет Из Счета Цикл
		Для Каждого Счет Из Счета Цикл
			Если ПроверяемыйСчет.ПринадлежитЭлементу(Счет) Тогда
				Иерархия.Вставить(Счет, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПроверяемыйСчет));
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат Иерархия;
	
КонецФункции

Процедура СоздатьКолонкиОтчета(КолонкиСтруктурыОтчета, ПараметрыОтчета)
	
	// Если нужно, добавляем отдельную колонку для показателей (БУ,НУ,ПР,ВР и т.д.).
	КоличествоПоказателей = БухгалтерскиеОтчетыВызовСервера.КоличествоПоказателей(ПараметрыОтчета);
	
	Если КоличествоПоказателей > 1 Тогда
		
		Колонка = КолонкиСтруктурыОтчета.Добавить();
		Колонка.Имя = "Показатели";
		ГруппаПоказатели = Колонка.Выбор.Элементы.Добавить(Тип("ГруппаВыбранныхПолейКомпоновкиДанных"));
		ГруппаПоказатели.Заголовок     = БухгалтерскиеОтчеты.ЗаголовокГруппыПоказателей();
		ГруппаПоказатели.Использование = Истина;
		ГруппаПоказатели.Расположение  = РасположениеПоляКомпоновкиДанных.Вертикально;
		
		Для Каждого ИмяПоказателя Из ПараметрыОтчета.НаборПоказателей Цикл
			Если ПараметрыОтчета["Показатель" + ИмяПоказателя] Тогда 
				БухгалтерскиеОтчетыКлиентСервер.ДобавитьВыбранноеПоле(ГруппаПоказатели, "Показатели." + ИмяПоказателя);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	// Для начального сальдо, оборотов и конечного сальдо, создадим по отдельной колонке
	// в каждой колонке будет 2 ячейки дебет и кредит.
	
	// Колонка Начальное сальдо.
	Колонка = КолонкиСтруктурыОтчета.Добавить();
	Колонка.Имя = "СальдоНаНачалоПериода";
	ГруппаСальдоНаНачало = Колонка.Выбор.Элементы.Добавить(Тип("ГруппаВыбранныхПолейКомпоновкиДанных"));
	ГруппаСальдоНаНачало.Заголовок     = НСтр("ru = 'Сальдо на начало периода';
												|en = 'Opening balance'");
	ГруппаСальдоНаНачало.Использование = Истина;
	ГруппаСальдоНаНачалоДт = ГруппаСальдоНаНачало.Элементы.Добавить(Тип("ГруппаВыбранныхПолейКомпоновкиДанных"));
	ГруппаСальдоНаНачалоДт.Заголовок     = НСтр("ru = 'Дебет';
												|en = 'Debit'");
	ГруппаСальдоНаНачалоДт.Использование = Истина;
	ГруппаСальдоНаНачалоДт.Расположение  = РасположениеПоляКомпоновкиДанных.Вертикально;
	ГруппаСальдоНаНачалоКт = ГруппаСальдоНаНачало.Элементы.Добавить(Тип("ГруппаВыбранныхПолейКомпоновкиДанных"));
	ГруппаСальдоНаНачалоКт.Заголовок     = НСтр("ru = 'Кредит';
												|en = 'Credit'");
	ГруппаСальдоНаНачалоКт.Использование = Истина;
	ГруппаСальдоНаНачалоКт.Расположение  = РасположениеПоляКомпоновкиДанных.Вертикально;
	
	// Колонка Обороты.
	Колонка = КолонкиСтруктурыОтчета.Добавить();
	Колонка.Имя = "ОборотыЗаПериод";
	ГруппаОбороты = Колонка.Выбор.Элементы.Добавить(Тип("ГруппаВыбранныхПолейКомпоновкиДанных"));
	ГруппаОбороты.Заголовок     = НСтр("ru = 'Обороты за период';
										|en = 'Turnover for a period'");
	ГруппаОбороты.Использование = Истина;
	ГруппаОборотыДт = ГруппаОбороты.Элементы.Добавить(Тип("ГруппаВыбранныхПолейКомпоновкиДанных"));
	ГруппаОборотыДт.Заголовок     = НСтр("ru = 'Дебет';
										|en = 'Debit'");
	ГруппаОборотыДт.Использование = Истина;
	ГруппаОборотыДт.Расположение  = РасположениеПоляКомпоновкиДанных.Вертикально;
	ГруппаОборотыКт = ГруппаОбороты.Элементы.Добавить(Тип("ГруппаВыбранныхПолейКомпоновкиДанных"));
	ГруппаОборотыКт.Заголовок     = НСтр("ru = 'Кредит';
										|en = 'Credit'");
	ГруппаОборотыКт.Использование = Истина;
	ГруппаОборотыКт.Расположение  = РасположениеПоляКомпоновкиДанных.Вертикально;
	
	// Колонка Конечное сальдо.
	Колонка = КолонкиСтруктурыОтчета.Добавить();
	Колонка.Имя = "СальдоНаКонецПериода";
	ГруппаСальдоНаКонец = Колонка.Выбор.Элементы.Добавить(Тип("ГруппаВыбранныхПолейКомпоновкиДанных"));
	ГруппаСальдоНаКонец.Заголовок     = НСтр("ru = 'Сальдо на конец периода';
											|en = 'Closing balance'");
	ГруппаСальдоНаКонец.Использование = Истина;
	ГруппаСальдоНаКонецДт = ГруппаСальдоНаКонец.Элементы.Добавить(Тип("ГруппаВыбранныхПолейКомпоновкиДанных"));
	ГруппаСальдоНаКонецДт.Заголовок     = НСтр("ru = 'Дебет';
												|en = 'Debit'");
	ГруппаСальдоНаКонецДт.Использование = Истина;
	ГруппаСальдоНаКонецДт.Расположение  = РасположениеПоляКомпоновкиДанных.Вертикально;
	ГруппаСальдоНаКонецКт = ГруппаСальдоНаКонец.Элементы.Добавить(Тип("ГруппаВыбранныхПолейКомпоновкиДанных"));
	ГруппаСальдоНаКонецКт.Заголовок     = НСтр("ru = 'Кредит';
												|en = 'Credit'");
	ГруппаСальдоНаКонецКт.Использование = Истина;
	ГруппаСальдоНаКонецКт.Расположение  = РасположениеПоляКомпоновкиДанных.Вертикально;
	
	// Заполняем созданные колонки показателями.
	Для Каждого ИмяПоказателя Из ПараметрыОтчета.НаборПоказателей Цикл
		Если ПараметрыОтчета["Показатель" + ИмяПоказателя] Тогда 
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьВыбранноеПоле(ГруппаСальдоНаНачалоДт, "СальдоНаНачалоПериода." + ИмяПоказателя + "НачальныйОстатокДт");
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьВыбранноеПоле(ГруппаСальдоНаНачалоКт, "СальдоНаНачалоПериода." + ИмяПоказателя + "НачальныйОстатокКт");
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьВыбранноеПоле(ГруппаОборотыДт,        "ОборотыЗаПериод."       + ИмяПоказателя + "ОборотДт");
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьВыбранноеПоле(ГруппаОборотыКт,        "ОборотыЗаПериод."       + ИмяПоказателя + "ОборотКт");
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьВыбранноеПоле(ГруппаСальдоНаКонецДт,  "СальдоНаКонецПериода."  + ИмяПоказателя + "КонечныйОстатокДт");
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьВыбранноеПоле(ГруппаСальдоНаКонецКт,  "СальдоНаКонецПериода."  + ИмяПоказателя + "КонечныйОстатокКт");
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура СоздатьИтогиРазвернутогоСальдо(ПараметрыАлгоритма)
	
	Схема = ПараметрыАлгоритма.Схема;
	ПоляИтогаОтчета = Схема.ПоляИтога;
	ТекстУсловие = ПараметрыАлгоритма.ТекстУсловие;
	
	МассивПоказателей = Новый Массив;
	МассивПоказателей.Добавить("БУ");
	МассивПоказателей.Добавить("НУ");
	МассивПоказателей.Добавить("ПР");
	МассивПоказателей.Добавить("ВР");
	МассивПоказателей.Добавить("Контроль");	
	
	БухгалтерскиеОтчетыПереопределяемый.ДополнительныеПоказателиБухгалтерскихОтчетов(МассивПоказателей);
	
	СтороныПроводки = Новый Массив;
	СтороныПроводки.Добавить("Дт");
	СтороныПроводки.Добавить("Кт");
	
	СтороныОстатка = Новый Структура;
	СтороныОстатка.Вставить("СальдоНаНачалоПериода", "Начальный");
	СтороныОстатка.Вставить("СальдоНаКонецПериода", "Конечный");
	
	// Для итогов не по субконто изменяем область действия полей итогов.
	Для Каждого ИмяПоказателя Из МассивПоказателей Цикл
		Для Каждого СторонаПроводки Из СтороныПроводки Цикл
			Для Каждого СторонаОстатка Из СтороныОстатка Цикл
				
				ИмяПоляИтога = СтрШаблон("%1.%2%3Остаток%4", СторонаОстатка.Ключ, ИмяПоказателя, СторонаОстатка.Значение, СторонаПроводки);
				Если ИмяПоказателя = "СверкаНУ" Тогда
					// Для случая обычного сальдо в СКД записано выражение итогов. Целью этого выражения является отнесение суммы итога
					// по СверкаНУ к той же стороне проводки, что и суммы по НУ.
					// Для случая развернутого сальдо нет необходимости определять сторону проводки, к которой отнести СверкаНУ, т.к.
					// допустимо появление сумм с обоих сторон.
					Для Каждого ПолеИтога Из ПоляИтогаОтчета Цикл
						
						Если ПолеИтога.Группировки.Количество() = 1 И ПолеИтога.Группировки[0] = "ОбщийИтог" Тогда
							// Есть два поля итога с разными областями группировок. Пропускаем общие итоги.
							Продолжить;
						КонецЕсли;
						Если ПолеИтога.ПутьКДанным = ИмяПоляИтога Тогда
							ПолеИтога.Группировки.Очистить();
							Прервать;
						КонецЕсли;
						
					КонецЦикла;
					
				Иначе // поиск единственного поля итога по имени
					
					ПолеИтога = ПоляИтогаОтчета.Найти(ИмяПоляИтога);
					
				КонецЕсли;
				
				ПолеИтога.Выражение = СтрШаблон("Выбор Когда %1 Тогда Сумма(%2.%3%4РазвернутыйОстаток%5) Иначе Сумма(%2.%3%4Остаток%5) Конец",
					ТекстУсловие, СторонаОстатка.Ключ, ИмяПоказателя, СторонаОстатка.Значение, СторонаПроводки);
				
				ПолеИтога.Группировки.Добавить("Счет");
				ПолеИтога.Группировки.Добавить("Счет Иерархия");
				ПолеИтога.Группировки.Добавить("Валюта");
			
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	// Для каждого субконто добавляем свои поля итогов, которые рассчитываются только по конкретному субконто.
	Для СчетчикСубконто = 1 По 3 Цикл
		
		ТекстУсловие = "Ложь ИЛИ ";
		СчетчикСчетов = 1;
		// Составим текст условия для субконто <СчетчикСубконто>.
		Для Каждого СчетСразворотом Из ПараметрыАлгоритма.СчетаСРазворотомПоСубконто[СчетчикСубконто - 1] Цикл
			
			ТекстУсловие = ТекстУсловие + "Счет = &СчетСРазворотомПоСубконто" + СчетчикСубконто + "_" + СчетчикСчетов + " ИЛИ ";
			
			// Установка параметра СчетСРазворотомПоСубконто.
			БухгалтерскиеОтчетыВызовСервера.СкопироватьПараметрСхемыКомпоновкиДанных(Схема, "СчетСРазворотомПоСубконто" + + СчетчикСубконто + "_" + СчетчикСчетов, "СчетСРазворотомПоСубконто", СчетСразворотом);
			
			СчетчикСчетов = СчетчикСчетов + 1;
			
		КонецЦикла;
		
		ТекстУсловие = Лев(ТекстУсловие, СтрДлина(ТекстУсловие) - 4);
		
		Для Каждого ИмяПоказателя Из МассивПоказателей Цикл
			Для Каждого СторонаПроводки Из СтороныПроводки Цикл
				Для Каждого СторонаОстатка Из СтороныОстатка Цикл
			
					ПолеИтога = ПоляИтогаОтчета.Добавить();
					ПолеИтога.ПутьКДанным = СтрШаблон("%1.%2%3Остаток%4", СторонаОстатка.Ключ, ИмяПоказателя, СторонаОстатка.Значение, СторонаПроводки);
					ПолеИтога.Выражение = СтрШаблон("Выбор Когда %1 Тогда Сумма(%2.%3%4РазвернутыйОстаток%5) Иначе Сумма(%2.%3%4Остаток%5) Конец",
						ТекстУсловие, СторонаОстатка.Ключ, ИмяПоказателя, СторонаОстатка.Значение, СторонаПроводки);
					ПолеИтога.Группировки.Добавить("Субконто" + СчетчикСубконто);
				
				КонецЦикла;
		    КонецЦикла;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПараметрыАлгоритма()

	ПараметрыАлгоритма = Новый Структура;
	ПараметрыАлгоритма.Вставить("Схема",                             ПолучитьМакет("СхемаКомпоновкиДанных")); // Получаем схему компоновки - эталон
	ПараметрыАлгоритма.Вставить("СчетаИсключенныеИзЗапросаПоСчетам", Новый СписокЗначений);
	ПараметрыАлгоритма.Вставить("СписокВсехСчетовРазвернутоеСальдо", Новый СписокЗначений);
	ПараметрыАлгоритма.Вставить("ЕстьДетализацияПоСубконто",         Ложь);
	ПараметрыАлгоритма.Вставить("ЕстьРазворотПоСубконто",            Ложь);
	ПараметрыАлгоритма.Вставить("ВыводитьРазвернутоеСальдо",         Ложь);
	ПараметрыАлгоритма.Вставить("НастройкаГруппировки",              Неопределено);
	ПараметрыАлгоритма.Вставить("ТекстУсловие",                      "Ложь ИЛИ ");
	
	// Для настройки развернутого сальдо может понадобиться информация о счетах и
	// количестве субконто, по которым нужна детализация.
	ПараметрыАлгоритма.Вставить("КоличествоСубконтоДетализации",         0);
	ПараметрыАлгоритма.Вставить("КоличествоСубконтоДетализацииПоСчетам", Новый Соответствие);	
	
	// Создадим массив, в который будем складывать счета, по которым хотим видеть развернутое сальдо
	// по разным субконто. Этот массив будем использовать для генерации выражений полей итогов СКД.
	СчетаСРазворотомПоСубконто = Новый Массив;
	Для инд = 1 По 3 Цикл
		СчетаСРазворотомПоСубконто.Добавить(Новый Массив);
	КонецЦикла;
	ПараметрыАлгоритма.Вставить("СчетаСРазворотомПоСубконто", СчетаСРазворотомПоСубконто);	
	ПараметрыАлгоритма.Вставить("СчетаДляОтдельногоЗапроса",  Новый Массив);

	Возврат ПараметрыАлгоритма;
	
КонецФункции

Процедура ПодготовитьНаборыДанных(ПараметрыОтчета, ПараметрыАлгоритма)
	
	ДоработатьНаборДанных_ПоСубконто(ПараметрыОтчета, ПараметрыАлгоритма);	
	
	ДоработатьНаборДанных_ПоСубконтоРазвернутое(ПараметрыОтчета, ПараметрыАлгоритма);
	
	Схема = ПараметрыАлгоритма.Схема;
	
	Если Не ПараметрыАлгоритма.ЕстьДетализацияПоСубконто Тогда
		Схема.НаборыДанных.НаборДанныхОбъединение.Элементы.Удалить(Схема.НаборыДанных.НаборДанныхОбъединение.Элементы.ПоСубконто);
	КонецЕсли;
	Если Не ПараметрыАлгоритма.ВыводитьРазвернутоеСальдо
	 Или НЕ ПараметрыАлгоритма.ЕстьРазворотПоСубконто Тогда
		Схема.НаборыДанных.НаборДанныхОбъединение.Элементы.Удалить(Схема.НаборыДанных.НаборДанныхОбъединение.Элементы.ПоСубконтоРазвернутое);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДоработатьНаборДанных_ПоСубконто(ПараметрыОтчета, ПараметрыАлгоритма)
	
	НаборДанных = ПараметрыАлгоритма.Схема.НаборыДанных.НаборДанныхОбъединение.Элементы.ПоСубконто; // Набор "ПоСубконто".
	ТекстЗапроса = НаборДанных.Запрос;
	НаборДанных.Запрос = "";
	
	// Определим текст запроса СКД который далее заменим.
	ТекстЗапросДетализацииПоСубконто       = БухгалтерскиеОтчетыВызовСервера.ПолучитьТекстПоМаркерам(ТекстЗапроса, "//Начало ЗапросПоСубконто Детализация", "//Конец ЗапросПоСубконто Детализация");
	ТекстУсловиеСчетаДетализацииПоСубконто = БухгалтерскиеОтчетыВызовСервера.ПолучитьТекстПоМаркерам(ТекстЗапроса, "//Начало УсловиеСчета Детализация", "//Конец УсловиеСчета Детализация");
	ТекстСубконтоДетализацииПоСубконто     = БухгалтерскиеОтчетыВызовСервера.ПолучитьТекстПоМаркерам(ТекстЗапроса, "//Начало Субконто Детализация", "//Конец Субконто Детализация");
	
	// Определяем параметры, на основании которых будем формировать тексты запросов.
	ПараметрыФормируемыхЗапросов = Новый ТаблицаЗначений;
	ПараметрыФормируемыхЗапросов.Колонки.Добавить("Счет",                   Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ПараметрыФормируемыхЗапросов.Колонки.Добавить("СчетаВИерархии",         Новый ОписаниеТипов("Массив")); // только перечисленные субсчета
	ПараметрыФормируемыхЗапросов.Колонки.Добавить("СубконтоДетализации",    Новый ОписаниеТипов("СписокЗначений"));
	ПараметрыФормируемыхЗапросов.Колонки.Добавить("СубконтоДетализации1",   Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.ВидыСубконтоХозрасчетные"));
	ПараметрыФормируемыхЗапросов.Колонки.Добавить("СубконтоДетализации2",   Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.ВидыСубконтоХозрасчетные"));
	ПараметрыФормируемыхЗапросов.Колонки.Добавить("СубконтоДетализации3",   Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.ВидыСубконтоХозрасчетные"));
	ПараметрыФормируемыхЗапросов.Колонки.Добавить("ПоСубсчетам",            Новый ОписаниеТипов("Булево"));
	
	СписокВсехСчетовГруппировка  = Новый СписокЗначений;
	СчетаИсключенныеИзЗапросаПоСчетам = ПараметрыАлгоритма.СчетаИсключенныеИзЗапросаПоСчетам;

	// Оставим уникальные строки со счетами.
	НастройкаГруппировки = БухгалтерскиеОтчеты.УникальныеНастройкиОтчетаПоСчетам(ПараметрыОтчета.Группировка);

	Для Каждого СтрокаТаблицы Из НастройкаГруппировки Цикл
		
		СубконтоДетализации = Новый СписокЗначений;
		СубконтоДетализацииИндекс = Новый Массив;
			
		ДанныеСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаТаблицы.Счет);
		
		КоличествоСубконто = СтрДлина(СтрокаТаблицы.ПоСубконто) / 2;
		Для Индекс = 1 По КоличествоСубконто Цикл
			
			ПозицияНомераСубконтоВКоде = Индекс * 2;
			ВидСубконтоСчета           = ДанныеСчета["ВидСубконто" + Сред(СтрокаТаблицы.ПоСубконто, ПозицияНомераСубконтоВКоде, 1)];
			НужнаДетализацияПоСубконто = (Сред(СтрокаТаблицы.ПоСубконто, ПозицияНомераСубконтоВКоде - 1, 1) = "+");
			
			Если НужнаДетализацияПоСубконто Тогда
				СубконтоДетализации.Добавить(ВидСубконтоСчета);
				СубконтоДетализацииИндекс.Добавить(Индекс);
			КонецЕсли;
			
		КонецЦикла;
			
		Если СубконтоДетализации.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ОписаниеПодзапроса = ПараметрыФормируемыхЗапросов.Добавить();
		ОписаниеПодзапроса.Счет                = СтрокаТаблицы.Счет;
		ОписаниеПодзапроса.ПоСубсчетам         = СтрокаТаблицы.ПоСубсчетам;
		ОписаниеПодзапроса.СубконтоДетализации = СубконтоДетализации.Скопировать();
		Для Каждого ИндексПсевдонима Из СубконтоДетализацииИндекс Цикл
			ОписаниеПодзапроса["СубконтоДетализации" + ИндексПсевдонима] = ДанныеСчета["ВидСубконто" + ИндексПсевдонима];
		КонецЦикла;
		ОписаниеПодзапроса.СчетаВИерархии      = СтрокаТаблицы.СчетаВИерархии;
		
		СписокВсехСчетовГруппировка.Добавить(СтрокаТаблицы.Счет);
	
	КонецЦикла;

	// Сворачиваем копию таблицы запросов, получая тем самым минимальный набор запросов которые надо выполнить.
	Колонки = "ПоСубсчетам,СубконтоДетализации1,СубконтоДетализации2,СубконтоДетализации3";
	ЗапросыДляФормирования = ПараметрыФормируемыхЗапросов.Скопировать(, Колонки);
	ЗапросыДляФормирования.Свернуть(Колонки);
	
	СчетаГруппировки = НастройкаГруппировки.ВыгрузитьКолонку("Счет");
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаИсключенныеИзЗапросаПоСчетам, СчетаГруппировки);
	
	ИерархияСчетовНастройкиГруппировка = ИерархияСчетовСписка(СчетаГруппировки);
	
	// Формируем тексты запросов.
	КоличествоСубконтоДетализацииПоСчетам = ПараметрыАлгоритма.КоличествоСубконтоДетализацииПоСчетам;
	НужноКорректироватьЗапросСКД = Ложь;
	ОтборСтрок = Новый Структура(Колонки);
	ИндексПодзапроса = 0;
	СчетаБезСубконто = Новый Массив;
	
	Для Каждого ОписаниеПодзапроса Из ЗапросыДляФормирования Цикл
		
		// Формируем список счетов по которым будет выполнен запрос.
		СчетаПодзапроса = Новый Массив;
		ОграничениеПоСубсчетам = Новый Соответствие; // только перечисленные субсчета
		ЗаполнитьЗначенияСвойств(ОтборСтрок, ОписаниеПодзапроса, Колонки);
		СтрокиДетализацииСПодходящимиПараметрами = ПараметрыФормируемыхЗапросов.НайтиСтроки(ОтборСтрок);
		Для Каждого СтрокаДетализации Из СтрокиДетализацииСПодходящимиПараметрами Цикл
			
			СчетаПодзапроса.Добавить(СтрокаДетализации.Счет);
			Для Каждого Счет Из СтрокаДетализации.СчетаВИерархии Цикл
				ОграничениеПоСубсчетам.Вставить(Счет, Истина);
			КонецЦикла;
			
		КонецЦикла;
		
		// Список субконто возьмем из нулевой строки. Он одинаковый у всех.
		СубконтоДетализации = СтрокиДетализацииСПодходящимиПараметрами[0].СубконтоДетализации;
		
		ИндексПодзапроса = ИндексПодзапроса + 1;
		
		Если ОписаниеПодзапроса.ПоСубсчетам ИЛИ ПараметрыОтчета.ПоСубсчетам Тогда
			
			Для Каждого Счет Из БухгалтерскийУчет.СформироватьМассивСубсчетов(СчетаПодзапроса) Цикл
				Если ОграничениеПоСубсчетам[Счет] = Истина Тогда
					// Добавляем только субсчета, относящиеся к данной группе настроек.
					КоличествоСубконтоДетализацииПоСчетам.Вставить(Счет, СубконтоДетализации);
				КонецЕсли;
			КонецЦикла;
			
		Иначе
			
			Для каждого Счет Из СчетаПодзапроса Цикл
				Если ОграничениеПоСубсчетам[Счет] = Истина Тогда
					// Добавляем только субсчета, относящиеся к данной группе настроек.
					КоличествоСубконтоДетализацииПоСчетам.Вставить(Счет, СубконтоДетализации);
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		ПараметрыАлгоритма.Вставить("КоличествоСубконтоДетализации", Макс(ПараметрыАлгоритма.КоличествоСубконтоДетализации, СубконтоДетализации.Количество()));

		// Формируем текст параметра УсловиеСчета запроса детализации по субконто.
		ТекстДляПодстановкиУсловиеСчетаДетализацииПоСубконто = "Счет В (&СчетДетализации" + ИндексПодзапроса + ")
												   |				И (НЕ Счет.Забалансовый
												   |					ИЛИ &ВыводитьЗабалансовыеСчета)";
										  
		// Формируем текст параметра Субконто запроса детализации по субконто.
		ТекстДляПодстановкиСубконтоДетализацииПоСубконто = "&СубконтоДетализации" + ИндексПодзапроса;
		
		// Формируем текст запроса для счета детализации.
		ТекстДляПодстановкиЗапросДетализацииПоСубконто = СтрЗаменить(ТекстЗапросДетализацииПоСубконто, ТекстУсловиеСчетаДетализацииПоСубконто, ТекстДляПодстановкиУсловиеСчетаДетализацииПоСубконто);
		ТекстДляПодстановкиЗапросДетализацииПоСубконто = СтрЗаменить(ТекстДляПодстановкиЗапросДетализацииПоСубконто, ТекстСубконтоДетализацииПоСубконто, ТекстДляПодстановкиСубконтоДетализацииПоСубконто);
		
		// Доработка текста запроса СКД.
		// Для первого запроса секция "{Выбрать..." должна быть. 
		Если НужноКорректироватьзапросСКД Тогда
			
			ПозицияОткрывающейСкобки = СтрНайти(ТекстДляПодстановкиЗапросДетализацииПоСубконто, "{");
			Если ПозицияОткрывающейСкобки <> 0 Тогда
				
				ПозицияЗакрывающейСкобки = СтрНайти(ТекстДляПодстановкиЗапросДетализацииПоСубконто, "}", , ПозицияОткрывающейСкобки);
				ТекстДляПодстановкиЗапросДетализацииПоСубконто = Лев(ТекстДляПодстановкиЗапросДетализацииПоСубконто, ПозицияОткрывающейСкобки - 1)
					+ Сред(ТекстДляПодстановкиЗапросДетализацииПоСубконто, ПозицияЗакрывающейСкобки + 1);
				
			КонецЕсли;
				
		Иначе
			// Для последующих запросов секцию "{Выбрать..." нужно убирать.
			НужноКорректироватьзапросСКД = Истина;
		КонецЕсли;
		
		// Корректировка текста запроса в зависимости от количества указанных видов субконто.
		Если ИндексПодзапроса > 1 Тогда
			// В первом запросе объединения должны присутствовать псевдонимы полей. Для последующих запросов удаляем псевдонимы.
			Для ИндексПсевдонима = 1 По 3 Цикл
				ТекстДляПодстановкиЗапросДетализацииПоСубконто = СтрЗаменить(ТекстДляПодстановкиЗапросДетализацииПоСубконто, " КАК Субконто" + ИндексПсевдонима + "Представление", "");
				ТекстДляПодстановкиЗапросДетализацииПоСубконто = СтрЗаменить(ТекстДляПодстановкиЗапросДетализацииПоСубконто, " КАК Субконто" + ИндексПсевдонима, "");
			КонецЦикла;
			
			// Обнуляем субконто и его представление не указанные в запросе.
			Для ИндексСубконто = СубконтоДетализации.Количество() + 1 По 3 Цикл
				ТекстДляПодстановкиЗапросДетализацииПоСубконто = СтрЗаменить(ТекстДляПодстановкиЗапросДетализацииПоСубконто, "ПРЕДСТАВЛЕНИЕССЫЛКИ(ОстаткиИОбороты.Субконто" + ИндексСубконто + ")", """""");
				ТекстДляПодстановкиЗапросДетализацииПоСубконто = СтрЗаменить(ТекстДляПодстановкиЗапросДетализацииПоСубконто, "ОстаткиИОбороты.Субконто" + ИндексСубконто, "Null");
			КонецЦикла;
			
		Иначе
			// Обнуляем субконто и его представление не указанные в запросе.
			Для ИндексСубконто = СубконтоДетализации.Количество() + 1 По 3 Цикл
				ТекстДляПодстановкиЗапросДетализацииПоСубконто = СтрЗаменить(ТекстДляПодстановкиЗапросДетализацииПоСубконто, "ПРЕДСТАВЛЕНИЕССЫЛКИ(ОстаткиИОбороты.Субконто" + ИндексСубконто + ") КАК Субконто" + ИндексСубконто + "Представление" , """"" КАК Субконто" + ИндексСубконто + "Представление");
				ТекстДляПодстановкиЗапросДетализацииПоСубконто = СтрЗаменить(ТекстДляПодстановкиЗапросДетализацииПоСубконто, "ОстаткиИОбороты.Субконто" + ИндексСубконто + " КАК Субконто" + ИндексСубконто, "Null КАК Субконто" + ИндексСубконто);
			КонецЦикла;
		КонецЕсли;
		
		// Если среди субсчетов, есть счета со свойствами отличными от счета верхнего уровня, то удалим эти субсчета.
		СчетаИсключенныеИзЗапросаГруппировка = ОбщегоНазначенияКлиентСервер.РазностьМассивов(СписокВсехСчетовГруппировка.ВыгрузитьЗначения(), СчетаПодзапроса);
		Для каждого Счет Из СчетаПодзапроса Цикл
			Исключения = ИерархияСчетовНастройкиГруппировка.Получить(Счет);
			Если Исключения <> Неопределено Тогда
				Для Каждого СчетИсключения Из Исключения Цикл
					// Если параметры группировки настроены так,
					// что счет и его субсчет могут быть отнесены к одному подзапросу,
					// то исключать субсчет не следует.
					Если СчетаПодзапроса.Найти(СчетИсключения) = Неопределено Тогда
						СчетаИсключенныеИзЗапросаГруппировка.Добавить(СчетИсключения);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;

		СчетаГруппировки = БухгалтерскийУчет.СформироватьМассивСубсчетов(СчетаПодзапроса);

		СубсчетаПодзапроса = ОбщегоНазначенияКлиентСервер.РазностьМассивов(СчетаГруппировки, СчетаИсключенныеИзЗапросаГруппировка);
		
		СубсчетаСписок = Новый СписокЗначений;
		СубсчетаСписок.ЗагрузитьЗначения(СубсчетаПодзапроса);
		
		БухгалтерскиеОтчетыВызовСервера.СкопироватьПараметрСхемыКомпоновкиДанных(ПараметрыАлгоритма.Схема, "СчетДетализации" + ИндексПодзапроса, "СчетДетализации", СубсчетаСписок);
		
		// Добавление и установка значения параметра СубконтоДетализации{Индекс}.
		БухгалтерскиеОтчетыВызовСервера.СкопироватьПараметрСхемыКомпоновкиДанных(ПараметрыАлгоритма.Схема, "СубконтоДетализации" + ИндексПодзапроса, "СубконтоДетализации", СубконтоДетализации);

		// Доработка запроса набора данных.
		НаборДанных.Запрос = НаборДанных.Запрос + ТекстДляПодстановкиЗапросДетализацииПоСубконто
												+ Символы.ПС + Символы.ПС + "ОБЪЕДИНИТЬ ВСЕ" + Символы.ПС + Символы.ПС;

		СписокСчетовГруппировки = Новый СписокЗначений;
		СписокСчетовГруппировки.ЗагрузитьЗначения(СчетаПодзапроса);
		
		// У некоторых субсчетов может быть отличный состав субконто от родительского счета. Определим такие счета
		// и создадим для них отдельный запрос.
		СчетаСПодходящимСоставомСубконто = СчетаССовпадающимНаборомСубконто(СписокСчетовГруппировки, КоличествоСубконтоДетализацииПоСчетам);
		
		// Подготовим список счетов для отдельного запроса. Возьмем список всех счетов группировки, включая их субсчета и
		// вычтем из них счета с подходящим составом субконто.
		СчетаДляОтдельногоЗапроса = СчетаВИерархии(СчетаПодзапроса, ОграничениеПоСубсчетам); 
		СчетаДляОтдельногоЗапроса = ОбщегоНазначенияКлиентСервер.РазностьМассивов(СчетаДляОтдельногоЗапроса, СчетаСПодходящимСоставомСубконто.ВыгрузитьЗначения());
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаБезСубконто, СчетаДляОтдельногоЗапроса, Истина);
		
	КонецЦикла;
	
	// Сформируем отдельный запрос для счетов без субконто
	Если СчетаБезСубконто.Количество() > 0 Тогда
		
		// Формируем текст параметра УсловиеСчета запроса детализации по субконто.
		ТекстДляПодстановкиУсловиеСчетаДетализацииПоСубконто = "Счет В (&СчетДетализации_БезСубконто)
			|				И (НЕ Счет.Забалансовый
			|					ИЛИ &ВыводитьЗабалансовыеСчета)";
		
		// Формируем текст параметра Субконто запроса детализации по субконто.
		ТекстДляПодстановкиСубконтоДетализацииПоСубконто = "";
		
		// Формируем текст запроса для счета детализации.
		ТекстДляПодстановкиЗапросДетализацииПоСубконто = СтрЗаменить(ТекстЗапросДетализацииПоСубконто, ТекстУсловиеСчетаДетализацииПоСубконто, ТекстДляПодстановкиУсловиеСчетаДетализацииПоСубконто);
		ТекстДляПодстановкиЗапросДетализацииПоСубконто = СтрЗаменить(ТекстДляПодстановкиЗапросДетализацииПоСубконто, ТекстСубконтоДетализацииПоСубконто, ТекстДляПодстановкиСубконтоДетализацииПоСубконто);
		
		ПозицияОткрывающейСкобки = СтрНайти(ТекстДляПодстановкиЗапросДетализацииПоСубконто, "{");
		Если ПозицияОткрывающейСкобки <> 0 Тогда
			
			ПозицияЗакрывающейСкобки = СтрНайти(ТекстДляПодстановкиЗапросДетализацииПоСубконто, "}", , ПозицияОткрывающейСкобки);
			ТекстДляПодстановкиЗапросДетализацииПоСубконто = Лев(ТекстДляПодстановкиЗапросДетализацииПоСубконто, ПозицияОткрывающейСкобки - 1)
				+ Сред(ТекстДляПодстановкиЗапросДетализацииПоСубконто, ПозицияЗакрывающейСкобки + 1);
			
		КонецЕсли;
		
		// Корректировка текста запроса в зависимости от количества указанных видов субконто.
		// В первом запросе объединения должны присутствовать псевдонимы полей. Для последующих запросов удаляем псевдонимы.
		Для ИндексПсевдонима = 1 По 3 Цикл
			ТекстДляПодстановкиЗапросДетализацииПоСубконто = СтрЗаменить(ТекстДляПодстановкиЗапросДетализацииПоСубконто, " КАК Субконто" + ИндексПсевдонима + "Представление", "");
			ТекстДляПодстановкиЗапросДетализацииПоСубконто = СтрЗаменить(ТекстДляПодстановкиЗапросДетализацииПоСубконто, " КАК Субконто" + ИндексПсевдонима, "");
		КонецЦикла;
		
		СубсчетаСписок = Новый СписокЗначений;
		СубсчетаСписок.ЗагрузитьЗначения(СчетаБезСубконто);
		
		БухгалтерскиеОтчетыВызовСервера.СкопироватьПараметрСхемыКомпоновкиДанных(ПараметрыАлгоритма.Схема, "СчетДетализации_БезСубконто", "СчетДетализации", СубсчетаСписок);
		
		// Доработка запроса набора данных.
		НаборДанных.Запрос = НаборДанных.Запрос + ТекстДляПодстановкиЗапросДетализацииПоСубконто
		+ Символы.ПС + Символы.ПС + "ОБЪЕДИНИТЬ ВСЕ" + Символы.ПС + Символы.ПС;
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПараметрыАлгоритма.СчетаДляОтдельногоЗапроса, СчетаБезСубконто);
		
	КонецЕсли;
	
	// Дорабатываем текст запроса СКД в соответствии с количеством детализируемых субконто.
	// Получаем список полей-субконто, которые требуются в запросе.
	ТекстДляПодстановкиЗапросДетализацииПоСубконто = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве("{ВЫБРАТЬ");
	Для Индекс = 1 По ПараметрыАлгоритма.КоличествоСубконтоДетализации Цикл
		
		ТекстДляПодстановкиЗапросДетализацииПоСубконто.Добавить(СтрШаблон("
		|	Субконто%1Представление", Формат(Индекс, "ЧГ=0")));
		ТекстДляПодстановкиЗапросДетализацииПоСубконто.Добавить(",");
		
	КонецЦикла;
	ТекстДляПодстановкиЗапросДетализацииПоСубконто[ТекстДляПодстановкиЗапросДетализацииПоСубконто.ВГраница()] = "}";
	ТекстЗапросаСКД = СтрСоединить(ТекстДляПодстановкиЗапросДетализацииПоСубконто);
	// Вырезаем из запроса текущий полный набор полей и вставляем только требуемые.
	ПозицияОткрывающейСкобки = СтрНайти(НаборДанных.Запрос, "{");
	Если ПозицияОткрывающейСкобки <> 0 Тогда
		
		ПозицияЗакрывающейСкобки = СтрНайти(НаборДанных.Запрос, "}", , ПозицияОткрывающейСкобки);
		НаборДанных.Запрос = Лев(НаборДанных.Запрос, ПозицияОткрывающейСкобки - 1)
			+ ТекстЗапросаСКД
			+ Сред(НаборДанных.Запрос, ПозицияЗакрывающейСкобки + 1);
		
	КонецЕсли;
	
	// Удаляем Ключевую фразу " ОБЪЕДИНИТЬ ВСЕ " в конце запроса.
	НаборДанных.Запрос = Лев(НаборДанных.Запрос, СтрДлина(НаборДанных.Запрос) - 16);
	
	ПараметрыАлгоритма.ЕстьДетализацияПоСубконто = (ЗапросыДляФормирования.Количество() > 0);
	ПараметрыАлгоритма.НастройкаГруппировки      = НастройкаГруппировки;
	
КонецПроцедуры

Процедура ДоработатьНаборДанных_ПоСубконтоРазвернутое(ПараметрыОтчета, ПараметрыАлгоритма)
	
	НаборДанных = ПараметрыАлгоритма.Схема.НаборыДанных.НаборДанныхОбъединение.Элементы.ПоСубконтоРазвернутое; // Набор "ПоСубконтоРазвернутое".
	СчетаИсключенныеИзЗапросаПоСчетам = ПараметрыАлгоритма.СчетаИсключенныеИзЗапросаПоСчетам;
	ТекстЗапроса = НаборДанных.Запрос;
	НаборДанных.Запрос = "";
	
	ТекстЗапросПоСубконтоРазвернутое       = БухгалтерскиеОтчетыВызовСервера.ПолучитьТекстПоМаркерам(ТекстЗапроса, "//Начало ЗапросПоСубконто РазвернутоеСальдо", "//Конец ЗапросПоСубконто РазвернутоеСальдо");
	ТекстУсловиеСчетаПоСубконтоРазвернутое = БухгалтерскиеОтчетыВызовСервера.ПолучитьТекстПоМаркерам(ТекстЗапроса, "//Начало УсловиеСчета РазвернутоеСальдо"    , "//Конец УсловиеСчета РазвернутоеСальдо");
	ТекстСубконтоПоСубконтоРазвернутое     = БухгалтерскиеОтчетыВызовСервера.ПолучитьТекстПоМаркерам(ТекстЗапроса, "//Начало Субконто РазвернутоеСальдо"        , "//Конец Субконто РазвернутоеСальдо");
	
	// Оставим уникальные строки со счетами.
	НастройкаРазвернутоеСальдо = БухгалтерскиеОтчеты.УникальныеНастройкиОтчетаПоСчетам(ПараметрыОтчета.РазвернутоеСальдо);

	СписокВсехСчетовРазвернутоеСальдо  = ПараметрыАлгоритма.СписокВсехСчетовРазвернутоеСальдо;
	СчетаБезСубконтоСразвернутымСальдо = Новый Массив;
	ЕстьРазворотПоСубконто             = Ложь;

	// Определяем параметры, на основании которых будем формировать тексты запросов.
	ПараметрыФормируемыхЗапросов = Новый ТаблицаЗначений;
	ПараметрыФормируемыхЗапросов.Колонки.Добавить("Счет",                                   Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ПараметрыФормируемыхЗапросов.Колонки.Добавить("СубконтоРазвернутоеСальдо",              Новый ОписаниеТипов("СписокЗначений"));
	ПараметрыФормируемыхЗапросов.Колонки.Добавить("СубконтоРазвернутоеСальдо1",             Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.ВидыСубконтоХозрасчетные"));
	ПараметрыФормируемыхЗапросов.Колонки.Добавить("СубконтоРазвернутоеСальдо2",             Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.ВидыСубконтоХозрасчетные"));
	ПараметрыФормируемыхЗапросов.Колонки.Добавить("СубконтоРазвернутоеСальдо3",             Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.ВидыСубконтоХозрасчетные"));
	ПараметрыФормируемыхЗапросов.Колонки.Добавить("ПоСубсчетам",                            Новый ОписаниеТипов("Булево"));
	ПараметрыФормируемыхЗапросов.Колонки.Добавить("ЕстьРазворотПоСубконтоТекущегоСчета",    Новый ОписаниеТипов("Булево"));
	ПараметрыФормируемыхЗапросов.Колонки.Добавить("КоличествоСубконтоДетализируемыхПоСчету",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(1, 0, ДопустимыйЗнак.Неотрицательный)));
	
	// Массив, в который будем складывать счета, по которым хотим видеть развернутое сальдо
	// по разным субконто. Этот массив будем использовать для генерации выражений полей итогов СКД.
	СчетаСРазворотомПоСубконто = ПараметрыАлгоритма.СчетаСРазворотомПоСубконто;
	
	Для Каждого СтрокаТаблицы Из НастройкаРазвернутоеСальдо Цикл
		
		ОписаниеПодзапроса = ПараметрыФормируемыхЗапросов.Добавить();
		ОписаниеПодзапроса.Счет        = СтрокаТаблицы.Счет;
		ОписаниеПодзапроса.ПоСубсчетам = СтрокаТаблицы.ПоСубсчетам;

		СубконтоРазвернутоеСальдо = Новый СписокЗначений;
		ЕстьРазворотПоСубконтоТекущегоСчета = Ложь;
		ДанныеСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаТаблицы.Счет);
		Для ИндексСубконто = 1 По ДанныеСчета.КоличествоСубконто Цикл
			Если СтрНайти(СтрокаТаблицы.ПоСубконто, "+" + ИндексСубконто) = 0 Тогда // Не нужен разворот по субконто.
				Продолжить;
			КонецЕсли;
			
			// Дополняем список счетов с разворотом по определенному субконто.
			Если СтрокаТаблицы.ПоСубсчетам Тогда
				Для Каждого Счет Из СтрокаТаблицы.СчетаВИерархии Цикл
					СчетаСразворотомПоСубконто[ИндексСубконто - 1].Добавить(Счет);
				КонецЦикла;
			Иначе
				СчетаСразворотомПоСубконто[ИндексСубконто - 1].Добавить(СтрокаТаблицы.Счет);
			КонецЕсли;
			
			// Задаем набор субконто необходимый для разворота.
			СубконтоРазвернутоеСальдо.Очистить();
			Для ИндексПсевдонима = 1 По ИндексСубконто Цикл
				СубконтоРазвернутоеСальдо.Добавить(ДанныеСчета["ВидСубконто" + ИндексПсевдонима]);
				ОписаниеПодзапроса["СубконтоРазвернутоеСальдо" + ИндексПсевдонима] = ДанныеСчета["ВидСубконто" + ИндексПсевдонима];
			КонецЦикла;
			
			ЕстьРазворотПоСубконтоТекущегоСчета = Истина;
			ЕстьРазворотПоСубконто = Истина;
			
		КонецЦикла;
		
		СубконтоДетализируемыеПоСчету = ПараметрыАлгоритма.КоличествоСубконтоДетализацииПоСчетам.Получить(СтрокаТаблицы.Счет);
		КоличествоСубконтоДетализируемыхПоСчету = ?(СубконтоДетализируемыеПоСчету = Неопределено, 0, СубконтоДетализируемыеПоСчету.Количество());
		
		ОписаниеПодзапроса.КоличествоСубконтоДетализируемыхПоСчету = КоличествоСубконтоДетализируемыхПоСчету;
		ОписаниеПодзапроса.СубконтоРазвернутоеСальдо               = СубконтоРазвернутоеСальдо.Скопировать();
		ОписаниеПодзапроса.ЕстьРазворотПоСубконтоТекущегоСчета     = ЕстьРазворотПоСубконтоТекущегоСчета;
		
		СписокВсехСчетовРазвернутоеСальдо.Добавить(СтрокаТаблицы.Счет);
		
		СчетаИсключенныеИзЗапросаПоСчетам.Добавить(СтрокаТаблицы.Счет);
		
	КонецЦикла;
	
	// Сворачиваем копию таблицы запросов, получая тем самым минимальный набор запросов, которые надо выполнить.
	Колонки = "ПоСубсчетам,ЕстьРазворотПоСубконтоТекущегоСчета,КоличествоСубконтоДетализируемыхПоСчету,СубконтоРазвернутоеСальдо1,СубконтоРазвернутоеСальдо2,СубконтоРазвернутоеСальдо3";
	ЗапросыДляФормирования = ПараметрыФормируемыхЗапросов.Скопировать(, Колонки);
	ЗапросыДляФормирования.Свернуть(Колонки);	
	
	ПолныйСписокСубконтоДетализацииПоСчетам = Новый Соответствие;
	
	СчетаРазвернутогоСальдо = НастройкаРазвернутоеСальдо.ВыгрузитьКолонку("Счет");
	
	ИерархияСчетовНастройкиРазвернутогоСальдо = ИерархияСчетовСписка(СчетаРазвернутогоСальдо);
	
	ТекстУсловие = ПараметрыАлгоритма.ТекстУсловие;
	ОтборСтрок = Новый Структура(Колонки);
	ИндексПодзапроса = 0;
	Для Каждого ОписаниеПодзапроса Из ЗапросыДляФормирования Цикл
		
		// Формируем список счетов, по которым будет выполнен запрос.
		СчетаПодзапроса = Новый Массив;
		ЗаполнитьЗначенияСвойств(ОтборСтрок, ОписаниеПодзапроса, Колонки);
		СтрокиРазвернутогоСальдоСПодходящимиПараметрами = ПараметрыФормируемыхЗапросов.НайтиСтроки(ОтборСтрок);
		Для Каждого СтрокаРазвернутогоСальдо Из СтрокиРазвернутогоСальдоСПодходящимиПараметрами Цикл
			СчетаПодзапроса.Добавить(СтрокаРазвернутогоСальдо.Счет);
		КонецЦикла;
		
		// Субсчета для параметра СчетРазвернутоеСальдо (пока что без учета исключений).
		СчетаРазвернутоеСальдо = БухгалтерскийУчет.СформироватьМассивСубсчетов(СчетаПодзапроса);
		
		// Список субконто возьмем из нулевой строки. Он одинаковый у всех.
		СубконтоРазвернутоеСальдо = СтрокиРазвернутогоСальдоСПодходящимиПараметрами[0].СубконтоРазвернутоеСальдо;
		СубконтоРазвернутоеСальдоКоличество = СубконтоРазвернутоеСальдо.Количество();
		
		ИндексПодзапроса = ИндексПодзапроса + 1;
		
		// Тексты для итогов по ресурсам.
		ИндексСубСчета = 0;
		СчетаИтогов = ?(ОписаниеПодзапроса.ПоСубсчетам, СчетаРазвернутоеСальдо, СчетаПодзапроса);
		Для Каждого Счет Из СчетаИтогов Цикл
			
			ПолныйСписокСубконтоДетализацииПоСчетам.Вставить(Счет, СубконтоРазвернутоеСальдо);
			
			БухгалтерскиеОтчетыВызовСервера.СкопироватьПараметрСхемыКомпоновкиДанных(ПараметрыАлгоритма.Схема, "СчетРазвернутоеСальдоИтоги" + ИндексПодзапроса  + "_" + ИндексСубСчета, "СчетРазвернутоеСальдоИтоги", Счет);
			ТекстУсловие = ТекстУсловие + "Счет = &СчетРазвернутоеСальдоИтоги" + ИндексПодзапроса + "_" + ИндексСубСчета + " ИЛИ ";
			ИндексСубСчета = ИндексСубСчета + 1;
			
		КонецЦикла;
		
		// Счета без разворота по субконто будут обработаны общим запросом, поэтому отдельный запрос не формируем.
		// Модифицируема текст условия для вычисления развернутого сальдо и добавляем соответсвующий параметр.
		Если Не ОписаниеПодзапроса.ЕстьРазворотПоСубконтоТекущегоСчета Тогда
			// Счета, по которым нужно развернутое сальдо, но не в разрезе субконто, нельзя исключать из запроса по счетам. Запомним их.
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаБезСубконтоСразвернутымСальдо,
				?(ОписаниеПодзапроса.ПоСубсчетам, СчетаВИерархии(СчетаПодзапроса), СчетаПодзапроса));

			СубсчетаСписок = Новый СписокЗначений;
			СубсчетаСписок.ЗагрузитьЗначения(СчетаРазвернутоеСальдо);
			БухгалтерскиеОтчетыВызовСервера.СкопироватьПараметрСхемыКомпоновкиДанных(ПараметрыАлгоритма.Схема, "СчетРазвернутоеСальдо" + ИндексПодзапроса, "СчетРазвернутоеСальдо", СубсчетаСписок);
			Продолжить;
		КонецЕсли;
					
		// Формируем текст параметра УсловиеСчета запроса детализации по субконто.
		ТекстДляПодстановкиУсловиеСчетаПоСубконтоРазвернутое = "Счет В (&СчетРазвернутоеСальдо" + ИндексПодзапроса + ")
													|				И (НЕ Счет.Забалансовый
													|					ИЛИ &ВыводитьЗабалансовыеСчета)";
		
		// Формируем текст параметра Субконто запроса по субконто развернутое.
		ТекстДляПодстановкиСубконтоПоСубконтоРазвернутое = "&СубконтоРазвернутый" + ИндексПодзапроса;
		
		// Если среди субсчетов, есть счета со свойствами отличными от счета верхнего уровня, то удалим эти субсчета.
		СчетаИсключенныеИзЗапросаПоСчетамРазвернутое = ОбщегоНазначенияКлиентСервер.РазностьМассивов(СписокВсехСчетовРазвернутоеСальдо.ВыгрузитьЗначения(), СчетаПодзапроса);
		Для каждого Счет Из СчетаПодзапроса Цикл
			Исключения = ИерархияСчетовНастройкиРазвернутогоСальдо.Получить(Счет);
			Если Исключения <> Неопределено Тогда
				Для Каждого СчетИсключения Из Исключения Цикл
					// Если параметры развернутого сальдо настроены так,
					// что счет и его субсчет могут быть отнесены к одному подзапросу,
					// то исключать субсчет не следует.
					Если СчетаПодзапроса.Найти(СчетИсключения) = Неопределено Тогда
						СчетаИсключенныеИзЗапросаПоСчетамРазвернутое.Добавить(СчетИсключения);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
		
		СчетаРазвернутоеСальдо = ОбщегоНазначенияКлиентСервер.РазностьМассивов(СчетаРазвернутоеСальдо, СчетаИсключенныеИзЗапросаПоСчетамРазвернутое);
		
		// Установка параметра СчетРазвернутоеСальдо.
		СубсчетаСписок = Новый СписокЗначений;
		СубсчетаСписок.ЗагрузитьЗначения(СчетаРазвернутоеСальдо);
		БухгалтерскиеОтчетыВызовСервера.СкопироватьПараметрСхемыКомпоновкиДанных(ПараметрыАлгоритма.Схема, "СчетРазвернутоеСальдо" + ИндексПодзапроса, "СчетРазвернутоеСальдо", СубсчетаСписок);
		
		// Установка параметра "СубконтоДетализацииРазвернутый.
		БухгалтерскиеОтчетыВызовСервера.СкопироватьПараметрСхемыКомпоновкиДанных(ПараметрыАлгоритма.Схема, "СубконтоРазвернутый" + ИндексПодзапроса, "СубконтоРазвернутый", СубконтоРазвернутоеСальдо);
		
		// Формируем текст запроса для счета детализации.
		ТекстДляПодстановкиЗапросПоСубконтоРазвернутое = ТекстЗапросПоСубконтоРазвернутое;
		ТекстДляПодстановкиЗапросПоСубконтоРазвернутое = СтрЗаменить(ТекстДляПодстановкиЗапросПоСубконтоРазвернутое, ТекстУсловиеСчетаПоСубконтоРазвернутое, ТекстДляПодстановкиУсловиеСчетаПоСубконтоРазвернутое);
		ТекстДляПодстановкиЗапросПоСубконтоРазвернутое = СтрЗаменить(ТекстДляПодстановкиЗапросПоСубконтоРазвернутое, ТекстСубконтоПоСубконтоРазвернутое, ТекстДляПодстановкиСубконтоПоСубконтоРазвернутое);
		
		// Для разворота сальдо по нужному количеству субконто, включим их во вложенном запросе.
		Для ИндексСубконто = 1 По СубконтоРазвернутоеСальдоКоличество Цикл
			
			ТекстДляПодстановкиЗапросПоСубконтоРазвернутое = СтрЗаменить(ТекстДляПодстановкиЗапросПоСубконтоРазвернутое,
															"//Null КАК Субконто" + ИндексСубконто,
															"ОстаткиИОбороты.Субконто" + ИндексСубконто + " КАК Субконто" + ИндексСубконто);
			
		КонецЦикла;
		
		// Для детализации могут потребоваться не все субконто, по которым разворачиваем.
		// Поэтому в конечном запросе берем КоличествоСубконтоДетализации.
		Для ИндексСубконто = 1 По ПараметрыАлгоритма.КоличествоСубконтоДетализации Цикл
			
			Если ИндексСубконто > ОписаниеПодзапроса.КоличествоСубконтоДетализируемыхПоСчету
			 Или ИндексСубконто > СубконтоРазвернутоеСальдоКоличество Тогда
				ТекстПодстановки = "Null КАК Субконто" + ИндексСубконто;
			Иначе
				ТекстПодстановки = "ВложенныйЗапрос.Субконто" + ИндексСубконто + " КАК Субконто" + ИндексСубконто;
			КонецЕсли;
			ТекстДляПодстановкиЗапросПоСубконтоРазвернутое = СтрЗаменить(ТекстДляПодстановкиЗапросПоСубконтоРазвернутое, "//Null КАК ВложенныйЗапрос.Субконто" + ИндексСубконто, ТекстПодстановки);
			
		КонецЦикла;
		
		// Доработка запроса набора данных.
		НаборДанных.Запрос = НаборДанных.Запрос + ТекстДляПодстановкиЗапросПоСубконтоРазвернутое
												+ Символы.ПС + Символы.ПС + "ОБЪЕДИНИТЬ ВСЕ" + Символы.ПС + Символы.ПС;
			
	КонецЦикла;
	
	// Вычтем из счетов без развернутого сальдо по субконто те, по которым развернутое сальдо по субконто все-таки включено
	// отдельной настройкой.
	Для ИндексСубконто = 0 По 2 Цикл
		СчетаБезСубконтоСРазвернутымСальдо = ОбщегоНазначенияКлиентСервер.РазностьМассивов(СчетаБезСубконтоСРазвернутымСальдо, СчетаСРазворотомПоСубконто[ИндексСубконто]);
	КонецЦикла;
	
	// Среди счетов с развернутым сальдо могут быть субсчета, по которым не удасться получить развернутое сальдо.
	// У некоторых субсчетов может быть отличный состав субконто от родительского счета. Определим такие счета
	// и создадим для них отдельный запрос.
	СчетаСПодходящимСоставомСубконто = СчетаССовпадающимНаборомСубконто(СписокВсехСчетовРазвернутоеСальдо, ПолныйСписокСубконтоДетализацииПоСчетам);
	
	// Подготовим список счетов для отдельного запроса по развернутому сальдо. Возьмем список всех счетов с развернутым
	// сальдо, включая их субсчета. Вычтем из них счета с подходящим составом субконто.
	СчетаДляОтдельногоЗапроса = СчетаВИерархии(СписокВсехСчетовРазвернутоеСальдо.ВыгрузитьЗначения());
	СчетаДляОтдельногоЗапроса = ОбщегоНазначенияКлиентСервер.РазностьМассивов(СчетаДляОтдельногоЗапроса, СчетаСПодходящимСоставомСубконто.ВыгрузитьЗначения());
	// Добавляем счета, у которых явно указано, что сальдо не разворачивается по субконто.
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаДляОтдельногоЗапроса, СчетаБезСубконтоСРазвернутымСальдо);
	
	// Сформируем отдельный запрос для таких счетов.
	Если СчетаДляОтдельногоЗапроса.Количество() > 0 Тогда
		
		СписокСчетаБезСубконтоСРазвернутымСальдо = Новый СписокЗначений;
		СписокСчетаБезСубконтоСРазвернутымСальдо.ЗагрузитьЗначения(СчетаДляОтдельногоЗапроса);
		
		// Установка параметра СчетРазвернутоеСальдо.
		БухгалтерскиеОтчетыВызовСервера.СкопироватьПараметрСхемыКомпоновкиДанных(ПараметрыАлгоритма.Схема, "СчетРазвернутоеСальдо_БезСубконто", "СчетРазвернутоеСальдо", СписокСчетаБезСубконтоСРазвернутымСальдо);
		
		// Формируем текст параметра УсловиеСчета запроса детализации по субконто.
		ТекстДляПодстановкиУсловиеСчетаПоСубконтоРазвернутое = "Счет В (&СчетРазвернутоеСальдо_БезСубконто)
													|				И (НЕ Счет.Забалансовый
													|					ИЛИ &ВыводитьЗабалансовыеСчета)";
		
		// Формируем текст параметра Субконто запроса по субконто развернутое.
		ТекстДляПодстановкиСубконтоПоСубконтоРазвернутое = "";
		
		// Формируем текст запроса для счета детализации.
		ТекстДляПодстановкиЗапросПоСубконтоРазвернутое = ТекстЗапросПоСубконтоРазвернутое;
		ТекстДляПодстановкиЗапросПоСубконтоРазвернутое = СтрЗаменить(ТекстДляПодстановкиЗапросПоСубконтоРазвернутое, ТекстУсловиеСчетаПоСубконтоРазвернутое, ТекстДляПодстановкиУсловиеСчетаПоСубконтоРазвернутое);
		ТекстДляПодстановкиЗапросПоСубконтоРазвернутое = СтрЗаменить(ТекстДляПодстановкиЗапросПоСубконтоРазвернутое, ТекстСубконтоПоСубконтоРазвернутое, ТекстДляПодстановкиСубконтоПоСубконтоРазвернутое);
		
		// Добавляем нужное количество субконто.
		Для Индекс = 1 По ПараметрыАлгоритма.КоличествоСубконтоДетализации Цикл
			ТекстДляПодстановкиЗапросПоСубконтоРазвернутое = СтрЗаменить(ТекстДляПодстановкиЗапросПоСубконтоРазвернутое, "//Null КАК ВложенныйЗапрос.Субконто" + Индекс, "Null КАК Субконто" + Индекс);
		КонецЦикла;
		
		// Доработка запроса набора данных.
		НаборДанных.Запрос = НаборДанных.Запрос + ТекстДляПодстановкиЗапросПоСубконтоРазвернутое;
		
		// Взводим флаг, хотя формально разврота по субконто может и не быть
		ЕстьРазворотПоСубконто = Истина;
		
	Иначе
		
		// Удаляем Ключевую фразу " ОБЪЕДИНИТЬ ВСЕ " в конце запроса.
		НаборДанных.Запрос = Лев(НаборДанных.Запрос, СтрДлина(НаборДанных.Запрос) - 16);
	
	КонецЕсли;
	
	ПараметрыАлгоритма.ТекстУсловие              = Лев(ТекстУсловие, СтрДлина(ТекстУсловие) - 4);
	ПараметрыАлгоритма.ВыводитьРазвернутоеСальдо = (ЗапросыДляФормирования.Количество() > 0);
	ПараметрыАлгоритма.ЕстьРазворотПоСубконто    = ЕстьРазворотПоСубконто;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли