#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

Перем ДатаНачалаНП, ДатаКонцаНП;

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = ложь;
	ДокументРезультат.Очистить();
	
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	
	ЗначениеПараметра = НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Период"));
	Если ЗначениеПараметра <> Неопределено Тогда
		ДатаНачалаНП = Дата(ЗначениеПараметра.Значение.ДатаНачала);
		ДатаКонцаНП  = Дата(ЗначениеПараметра.Значение.ДатаОкончания);
	КонецЕсли;
	
	Если ДатаНачалаНП = '00010101' Тогда
		ДатаНачалаНП = НачалоМесяца(ТекущаяДатаСеанса());
	КонецЕсли;
	Если ДатаКонцаНП = '00010101' Тогда
		ДатаКонцаНП = КонецМесяца(ТекущаяДатаСеанса());
	КонецЕсли;
	
	НастройкиОтчета.Структура.Очистить();
	
	Группировка = НастройкиОтчета.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
	Группировка.ПоляГруппировки.Элементы.Очистить();
	Группировка.Выбор.Элементы.Очистить();
	Группировка.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
	
	НастройкиОтчета.Выбор.Элементы.Очистить();
	ОтчетыСервер.ДобавитьВыбранноеПоле(НастройкиОтчета.Выбор, "Организация");
	ОтчетыСервер.ДобавитьВыбранноеПоле(НастройкиОтчета.Выбор, "ГоловнаяОрганизация");
	ОтчетыСервер.ДобавитьВыбранноеПоле(НастройкиОтчета.Выбор, "ФизическоеЛицо");
	
	МакетКомпоновки = ЗарплатаКадрыОтчеты.МакетКомпоновкиДанныхДляКоллекцииЗначений(СхемаКомпоновкиДанных, НастройкиОтчета);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , , Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	
	ТаблицаЗначений =  Новый ТаблицаЗначений;
	ПроцессорВывода.УстановитьОбъект(ТаблицаЗначений);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	Если ТаблицаЗначений.Количество() > 0 Тогда
		СформироватьМакет(ДокументРезультат, ТаблицаЗначений, ДанныеРасшифровки);
	КонецЕсли;
	
	ДопСвойства = КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства;
	ДопСвойства.Вставить("ОтчетПустой", ТаблицаЗначений.Количество() = 0);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ИнициализироватьОтчет() Экспорт
	
	ЗарплатаКадрыОбщиеНаборыДанных.ЗаполнитьОбщиеИсточникиДанныхОтчета(ЭтотОбъект);
	
КонецПроцедуры

// Для общей формы "Форма отчета" подсистемы "Варианты отчетов".
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.События.ПередЗагрузкойНастроекВКомпоновщик = Истина;
	
КонецПроцедуры

// Вызывается перед загрузкой новых настроек. Используется для изменения схемы компоновки.
//
Процедура ПередЗагрузкойНастроекВКомпоновщик(Контекст, КлючСхемы, КлючВарианта, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД) Экспорт
	
	Если КлючСхемы <> КлючВарианта Тогда
		
		ИнициализироватьОтчет();
		ОтчетыСервер.ПодключитьСхему(ЭтотОбъект, Контекст, СхемаКомпоновкиДанных, КлючСхемы);
		
		КлючСхемы = КлючВарианта;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьМакет(ДокументРезультат, ФизЛицаОрганизаций, ДанныеРасшифровки)
	
	// Создание запроса и установка всех необходимых параметров.
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц; 
	Запрос.УстановитьПараметр("ТаблицаФизЛицОрганизаций", ФизЛицаОрганизаций);
	Запрос.УстановитьПараметр("НачалоПериода", ДатаНачалаНП);
	Запрос.УстановитьПараметр("КонецПериода",  ДатаКонцаНП);
	Запрос.УстановитьПараметр("ДатаОбъединенияВзносов",	УчетСтраховыхВзносов.ДатаОбъединенияВзносов());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаФизЛиц.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ТаблицаФизЛиц.Организация КАК Организация,
	|	ТаблицаФизЛиц.ГоловнаяОрганизация КАК ГоловнаяОрганизация
	|ПОМЕСТИТЬ ВТФизЛицаОрганизаций
	|ИЗ
	|	&ТаблицаФизЛицОрганизаций КАК ТаблицаФизЛиц
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизическоеЛицо,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ФизЛицаОрганизаций.Организация КАК Организация
	|ИЗ
	|	ВТФизЛицаОрганизаций КАК ФизЛицаОрганизаций";
	СписокОрганизаций = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Организация");
	
	Организация = Неопределено;
	Если СписокОрганизаций.Количество() = 1 Тогда
		Организация = СписокОрганизаций[0];
	КонецЕсли;	
		
	// список начислений не облагаемых ФСС НС
	НеЯвляетсяОбъектомПоФСС_НС = Новый Массив;
	НеЯвляетсяОбъектомПоФСС_НС.Добавить(Справочники.ВидыДоходовПоСтраховымВзносам.АвторскиеОткрытия);
	НеЯвляетсяОбъектомПоФСС_НС.Добавить(Справочники.ВидыДоходовПоСтраховымВзносам.АвторскиеАудиовизуальныеПроизведения);
	НеЯвляетсяОбъектомПоФСС_НС.Добавить(Справочники.ВидыДоходовПоСтраховымВзносам.АвторскиеДругиеМузыкальныеПроизведения);
	НеЯвляетсяОбъектомПоФСС_НС.Добавить(Справочники.ВидыДоходовПоСтраховымВзносам.АвторскиеЛитературныеПроизведения);
	НеЯвляетсяОбъектомПоФСС_НС.Добавить(Справочники.ВидыДоходовПоСтраховымВзносам.АвторскиеМузыкальноСценическиеПроизведение);
	НеЯвляетсяОбъектомПоФСС_НС.Добавить(Справочники.ВидыДоходовПоСтраховымВзносам.АвторскиеНаучныеТруды);
	НеЯвляетсяОбъектомПоФСС_НС.Добавить(Справочники.ВидыДоходовПоСтраховымВзносам.АвторскиеСкульптуры);
	НеЯвляетсяОбъектомПоФСС_НС.Добавить(Справочники.ВидыДоходовПоСтраховымВзносам.АвторскиеГрафическиеПроизведения);
	НеЯвляетсяОбъектомПоФСС_НС.Добавить(Справочники.ВидыДоходовПоСтраховымВзносам.АвторскиеИсполнениеПроизведений);
	НеЯвляетсяОбъектомПоФСС_НС.Добавить(Справочники.ВидыДоходовПоСтраховымВзносам.ДоговорыГПХ);
	НеЯвляетсяОбъектомПоФСС_НС.Добавить(Справочники.ВидыДоходовПоСтраховымВзносам.НеЯвляетсяОбъектом);
	Запрос.УстановитьПараметр("НеЯвляетсяОбъектомПоФСС_НС", НеЯвляетсяОбъектомПоФСС_НС);
	
	ПособияКромеНСиПЗ = Новый Массив;
	ПособияКромеНСиПЗ.Добавить(Перечисления.ПереченьПособийСоциальногоСтрахования.Нетрудоспособность);
	ПособияКромеНСиПЗ.Добавить(Перечисления.ПереченьПособийСоциальногоСтрахования.БеременностьРоды);
	ПособияКромеНСиПЗ.Добавить(Перечисления.ПереченьПособийСоциальногоСтрахования.ПоУходуЗаРебенком);
	ПособияКромеНСиПЗ.Добавить(Перечисления.ПереченьПособийСоциальногоСтрахования.ПриПостановкеНаУчетВРанниеСрокиБеременности);
	ПособияКромеНСиПЗ.Добавить(Перечисления.ПереченьПособийСоциальногоСтрахования.ПриРожденииРебенка);
	ПособияКромеНСиПЗ.Добавить(Перечисления.ПереченьПособийСоциальногоСтрахования.ВСвязиСоСмертью);
	ПособияКромеНСиПЗ.Добавить(Перечисления.ВидыОсобыхНачисленийИУдержаний.ПособиеПриПостановкеНаУчетВРанниеСрокиБеременности);
	ПособияКромеНСиПЗ.Добавить(Перечисления.ВидыОсобыхНачисленийИУдержаний.ПособиеПриРожденииРебенка);
	ПособияКромеНСиПЗ.Добавить(Перечисления.ВидыОсобыхНачисленийИУдержаний.ПособиеНаПогребение);
	ПособияКромеНСиПЗ.Добавить(Перечисления.ВидыОсобыхНачисленийИУдержаний.ПособиеНаПогребениеСотруднику);
	ПособияКромеНСиПЗ.Добавить(Перечисления.ПереченьПособийСоциальногоСтрахования.ДополнительныеВыходныеДниПоУходуЗаДетьмиИнвалидами);
	ПособияКромеНСиПЗ.Добавить(Перечисления.ПереченьПособийСоциальногоСтрахования.СтраховыеВзносыПоДопВыходнымПоУходуЗаДетьмиИнвалидами);
	Запрос.УстановитьПараметр("ПособияКромеНСиПЗ", ПособияКромеНСиПЗ);
	ПособияНСиПЗ = Новый Массив;
	ПособияНСиПЗ.Добавить(Перечисления.ПереченьПособийСоциальногоСтрахования.НетрудоспособностьНесчастныйСлучай);
	ПособияНСиПЗ.Добавить(Перечисления.ПереченьПособийСоциальногоСтрахования.НетрудоспособностьПрофзаболевание);
	ПособияНСиПЗ.Добавить(Перечисления.ПереченьПособийСоциальногоСтрахования.ДополнительныйОтпускПослеНесчастныхСлучаев);
	Запрос.УстановитьПараметр("ПособияНСиПЗ", ПособияНСиПЗ);
	ДополнительныеПособия = Новый ТаблицаЗначений;
	
	УчетСтраховыхВзносов.СформироватьВТРасширенныеСведенияОДоходахИВзносах(?(ДатаНачалаНП = НачалоГода(ДатаНачалаНП), ДобавитьМесяц(ДатаНачалаНП, -1), ДатаНачалаНП), ДатаКонцаНП, Организация, Запрос.МенеджерВременныхТаблиц, Истина, Истина, Истина, Истина, Истина, Истина,,, Макс(ТекущаяДатаСеанса(), ДатаКонцаНП), ДатаНачалаНП < УчетСтраховыхВзносов.ДатаПередачиАдминистрированияВзносовФНС());
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ФизЛицаОрганизаций.ФизическоеЛицо
	|ИЗ
	|	ВТФизЛицаОрганизаций КАК ФизЛицаОрганизаций
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ИсчисленныеСтраховыеВзносы КАК ИсчисленныеСтраховыеВзносы
	|		ПО ФизЛицаОрганизаций.ФизическоеЛицо = ИсчисленныеСтраховыеВзносы.ФизическоеЛицо
	|			И ФизЛицаОрганизаций.Организация = ИсчисленныеСтраховыеВзносы.Организация
	|			И ФизЛицаОрганизаций.ГоловнаяОрганизация = ИсчисленныеСтраховыеВзносы.ГоловнаяОрганизация
	|ГДЕ
	|	НАЧАЛОПЕРИОДА(ИсчисленныеСтраховыеВзносы.ДатаПолученияДохода, КВАРТАЛ) < НАЧАЛОПЕРИОДА(ИсчисленныеСтраховыеВзносы.Период, КВАРТАЛ)
	|	И ИсчисленныеСтраховыеВзносы.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ФизЛицаОрганизаций.ФизическоеЛицо
	|ИЗ
	|	ВТФизЛицаОрганизаций КАК ФизЛицаОрганизаций
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СведенияОДоходахСтраховыеВзносы КАК СведенияОДоходахСтраховыеВзносы
	|		ПО ФизЛицаОрганизаций.ФизическоеЛицо = СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо
	|			И ФизЛицаОрганизаций.Организация = СведенияОДоходахСтраховыеВзносы.Организация
	|			И ФизЛицаОрганизаций.ГоловнаяОрганизация = СведенияОДоходахСтраховыеВзносы.ГоловнаяОрганизация
	|ГДЕ
	|	НАЧАЛОПЕРИОДА(СведенияОДоходахСтраховыеВзносы.ДатаПолученияДохода, КВАРТАЛ) < НАЧАЛОПЕРИОДА(СведенияОДоходахСтраховыеВзносы.Период, КВАРТАЛ)
	|	И СведенияОДоходахСтраховыеВзносы.Период МЕЖДУ &НачалоПериода И &КонецПериода";
	
	Если ДатаНачалаНП < УчетСтраховыхВзносов.ДатаПередачиАдминистрированияВзносовФНС() И Не Запрос.Выполнить().Пустой() Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СведенияОДоходах.Организация КАК Организация,
		|	СведенияОДоходах.Подразделение КАК Подразделение,
		|	СведенияОДоходах.ФизическоеЛицо КАК ФизическоеЛицо,
		|	СведенияОДоходах.Период КАК МесяцПолученияДохода,
		|	СведенияОДоходах.ПериодРегистрации КАК МесяцРегистрацииДохода,
		|	ВЫБОР
		|		КОГДА СведенияОДоходах.Начисление ЕСТЬ NULL
		|			ТОГДА СведенияОДоходах.ВидДохода
		|		КОГДА СведенияОДоходах.Начисление = НЕОПРЕДЕЛЕНО
		|			ТОГДА СведенияОДоходах.ВидДохода
		|		ИНАЧЕ СведенияОДоходах.Начисление
		|	КОНЕЦ КАК ВидРасчета,
		|	СведенияОДоходах.Инвалид КАК Инвалид,
		|	СведенияОДоходах.ВидЗастрахованногоЛица КАК ВидЗастрахованногоЛица,
		|	СведенияОДоходах.НачислятьНакопительнуюЧасть КАК НачислятьНакопительнуюЧасть,
		|	СведенияОДоходах.ВидТарифаСтраховыхВзносов КАК ВидТарифаСтраховыхВзносов,
		|	ОписаниеТарифов.НачислятьВзносыСПревышения КАК НачислятьВзносыСПревышения,
		|	ОписаниеТарифов.СтавкаСПревышенияПредела КАК СтавкаСПревышенияПредела,
		|	СведенияОДоходах.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией КАК ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
		|	СведенияОДоходах.КлассУсловийТруда КАК КлассУсловийТруда,
		|	СведенияОДоходах.ЗастрахованФССкакИностранец КАК ЗастрахованФССкакИностранец,
		|	СведенияОДоходах.ИспользоватьЛьготуИнвалидамПоФСС_НС КАК ИспользоватьЛьготуИнвалидамПоФСС_НС,
		|	СУММА(СведенияОДоходах.Сумма) КАК НачисленоВсего,
		|	СУММА(СведенияОДоходах.СуммаПФР) КАК НачисленоВсегоПФР,
		|	СУММА(СведенияОДоходах.СуммаФОМС) КАК НачисленоВсегоФОМС,
		|	СУММА(СведенияОДоходах.СуммаФСС) КАК НачисленоВсегоФСС,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.НеЯвляетсяОбъектом)
		|				ТОГДА 0
		|			КОГДА НЕ СведенияОДоходах.ЗастрахованПФР
		|				ТОГДА 0
		|			ИНАЧЕ СведенияОДоходах.НеОблагаетсяПФР
		|		КОНЕЦ) КАК НеОблагаетсяПФР,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.НеЯвляетсяОбъектом)
		|				ТОГДА 0
		|			КОГДА НЕ СведенияОДоходах.ЗастрахованФОМС
		|				ТОГДА 0
		|			ИНАЧЕ СведенияОДоходах.НеОблагаетсяФОМС
		|		КОНЕЦ) КАК НеОблагаетсяФОМС,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.НеЯвляетсяОбъектом)
		|				ТОГДА 0
		|			КОГДА НЕ СведенияОДоходах.ЗастрахованФСС
		|					И НЕ СведенияОДоходах.ЗастрахованФССкакИностранец
		|				ТОГДА 0
		|			ИНАЧЕ СведенияОДоходах.НеОблагаетсяФСС
		|		КОНЕЦ) КАК НеОблагаетсяФСС,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.НеЯвляетсяОбъектом)
		|				ТОГДА 0
		|			ИНАЧЕ СведенияОДоходах.НеОблагаетсяФСС_НС
		|		КОНЕЦ) КАК НеОблагаетсяФСС_НС,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.НеЯвляетсяОбъектом)
		|				ТОГДА СведенияОДоходах.Сумма
		|			КОГДА НЕ СведенияОДоходах.ЗастрахованПФР
		|				ТОГДА СведенияОДоходах.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК НеЯвляетсяОбъектомИДоходыНезастрахованныхПФР,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.НеЯвляетсяОбъектом)
		|				ТОГДА СведенияОДоходах.Сумма
		|			КОГДА НЕ СведенияОДоходах.ЗастрахованФОМС
		|				ТОГДА СведенияОДоходах.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК НеЯвляетсяОбъектомИДоходыНезастрахованныхФОМС,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.НеЯвляетсяОбъектом)
		|				ТОГДА СведенияОДоходах.Сумма
		|			КОГДА НЕ СведенияОДоходах.ЗастрахованФСС
		|					И НЕ СведенияОДоходах.ЗастрахованФССкакИностранец
		|				ТОГДА СведенияОДоходах.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК НеЯвляетсяОбъектомИДоходыНезастрахованныхФСС,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ВидДохода В (&НеЯвляетсяОбъектомПоФСС_НС)
		|				ТОГДА СведенияОДоходах.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК НеЯвляетсяОбъектомФСС_НС,
		|	СУММА(СведенияОДоходах.СуммаПревысившаяПределПФР) КАК СуммаПревысившаяПределПФР,
		|	СУММА(СведенияОДоходах.СуммаПревысившаяПределФОМС) КАК СуммаПревысившаяПределФОМС,
		|	СУММА(СведенияОДоходах.СуммаПревысившаяПределФСС) КАК СуммаПревысившаяПределФСС,
		|	СУММА(СведенияОДоходах.БазаПФР - СведенияОДоходах.СуммаПревысившаяПределПФР) КАК БазаПФР,
		|	СУММА(СведенияОДоходах.БазаПФР) КАК БазаПФРЗанятыхНаРаботахСДосрочнойПенсией,
		|	СУММА(СведенияОДоходах.БазаФОМС - СведенияОДоходах.СуммаПревысившаяПределФОМС) КАК БазаФОМС,
		|	СУММА(СведенияОДоходах.БазаФСС - СведенияОДоходах.СуммаПревысившаяПределФСС) КАК БазаФСС,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ЗастрахованФССкакИностранец
		|				ТОГДА СведенияОДоходах.БазаФСС - СведенияОДоходах.СуммаПревысившаяПределФСС
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК БазаФССИностранцев,
		|	СУММА(СведенияОДоходах.БазаФСС_НС) КАК БазаФСС_НС,
		|	СУММА(СведенияОДоходах.ПФРПоСуммарномуТарифу) КАК ПФРПоСуммарномуТарифу,
		|	СУММА(СведенияОДоходах.ПФРСтраховая) КАК ПФРСтраховая,
		|	СУММА(СведенияОДоходах.ПФРССуммПревышения) КАК ПФРССуммПревышения,
		|	СУММА(СведенияОДоходах.ОПСпоМеждународнымДоговорам) КАК ОПСпоМеждународнымДоговорам,
		|	СУММА(СведенияОДоходах.ОПСпоМеждународнымДоговорамСПревышения) КАК ОПСпоМеждународнымДоговорамСПревышения,
		|	СУММА(СведенияОДоходах.ОМСпоМеждународнымДоговорам) КАК ОМСпоМеждународнымДоговорам,
		|	СУММА(СведенияОДоходах.ОМСпоМеждународнымДоговорамСПревышения) КАК ОМСпоМеждународнымДоговорамСПревышения,
		|	СУММА(СведенияОДоходах.ОССпоМеждународнымДоговорам) КАК ОССпоМеждународнымДоговорам,
		|	СУММА(СведенияОДоходах.ОССпоМеждународнымДоговорамСПревышения) КАК ОССпоМеждународнымДоговорамСПревышения,
		|	СУММА(СведенияОДоходах.ПФРНакопительная) КАК ПФРНакопительная,
		|	СУММА(СведенияОДоходах.ФФОМС) КАК ФФОМС,
		|	СУММА(СведенияОДоходах.ТФОМС) КАК ТФОМС,
		|	СУММА(СведенияОДоходах.ФСС) КАК ФСС,
		|	СУММА(СведенияОДоходах.ФССНесчастныеСлучаи) КАК ФССНесчастныеСлучаи,
		|	СУММА(СведенияОДоходах.ПФРЗаЗанятыхНаРаботахСДосрочнойПенсией) КАК ПФРЗаЗанятыхНаРаботахСДосрочнойПенсией,
		|	СУММА(СведенияОДоходах.ПФРНаДоплатуЛетчикам) КАК ПФРПоДополнительномуТарифу,
		|	СУММА(СведенияОДоходах.ПФРНаДоплатуШахтерам) КАК ПФРНаДоплатуКПенсииШахтерам,
		|	СУММА(СведенияОДоходах.ПособияЗаСчетФСС) КАК ПособиеЗаСчетФСС,
		|	СУММА(СведенияОДоходах.ПособияЗаСчетФСС_НС) КАК ПособиеЗаСчетФСС_НС
		|ПОМЕСТИТЬ ВТДанныеБезОпоздавшихИсправлений
		|ИЗ
		|	ВТРасширенныеСведенияОДоходахИВзносах КАК СведенияОДоходах
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОписаниеТарифов КАК ОписаниеТарифов
		|		ПО СведенияОДоходах.ВидТарифаСтраховыхВзносов = ОписаниеТарифов.ВидТарифа
		|			И (ОписаниеТарифов.РасчетныйПериод = ГОД(СведенияОДоходах.Период))
		|ГДЕ
		|	СведенияОДоходах.ПериодРегистрации МЕЖДУ &НачалоПериода И &КонецПериода
		|
		|СГРУППИРОВАТЬ ПО
		|	СведенияОДоходах.ФизическоеЛицо,
		|	СведенияОДоходах.НачислятьНакопительнуюЧасть,
		|	СведенияОДоходах.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
		|	СведенияОДоходах.КлассУсловийТруда,
		|	СведенияОДоходах.ЗастрахованФССкакИностранец,
		|	СведенияОДоходах.ИспользоватьЛьготуИнвалидамПоФСС_НС,
		|	СведенияОДоходах.Организация,
		|	СведенияОДоходах.Подразделение,
		|	СведенияОДоходах.ВидТарифаСтраховыхВзносов,
		|	ОписаниеТарифов.НачислятьВзносыСПревышения,
		|	ОписаниеТарифов.СтавкаСПревышенияПредела,
		|	СведенияОДоходах.Инвалид,
		|	СведенияОДоходах.ВидЗастрахованногоЛица,
		|	ВЫБОР
		|		КОГДА СведенияОДоходах.Начисление ЕСТЬ NULL
		|			ТОГДА СведенияОДоходах.ВидДохода
		|		КОГДА СведенияОДоходах.Начисление = НЕОПРЕДЕЛЕНО
		|			ТОГДА СведенияОДоходах.ВидДохода
		|		ИНАЧЕ СведенияОДоходах.Начисление
		|	КОНЕЦ,
		|	СведенияОДоходах.Период,
		|	СведенияОДоходах.ПериодРегистрации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТРасширенныеСведенияОДоходахИВзносах
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТРазмерыТарифныхСтавок
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТДанныеОбИнвалидности
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТИспользуемыеТарифы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТОписаниеТарифов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(ДатыПолученияДохода.ДатаПолученияДохода) КАК ПоздняяДатаПолученияДохода,
		|	МИНИМУМ(ДатыПолученияДохода.ДатаПолученияДохода) КАК РанняяДатаПолученияДохода
		|ИЗ
		|	(ВЫБРАТЬ
		|		ИсчисленныеСтраховыеВзносы.ДатаПолученияДохода КАК ДатаПолученияДохода
		|	ИЗ
		|		ВТФизЛицаОрганизаций КАК ФизЛицаОрганизаций
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ИсчисленныеСтраховыеВзносы КАК ИсчисленныеСтраховыеВзносы
		|			ПО ФизЛицаОрганизаций.ФизическоеЛицо = ИсчисленныеСтраховыеВзносы.ФизическоеЛицо
		|				И ФизЛицаОрганизаций.Организация = ИсчисленныеСтраховыеВзносы.Организация
		|				И ФизЛицаОрганизаций.ГоловнаяОрганизация = ИсчисленныеСтраховыеВзносы.ГоловнаяОрганизация
		|	ГДЕ
		|		ИсчисленныеСтраховыеВзносы.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		СведенияОДоходахСтраховыеВзносы.ДатаПолученияДохода
		|	ИЗ
		|		ВТФизЛицаОрганизаций КАК ФизЛицаОрганизаций
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СведенияОДоходахСтраховыеВзносы КАК СведенияОДоходахСтраховыеВзносы
		|			ПО ФизЛицаОрганизаций.ФизическоеЛицо = СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо
		|				И ФизЛицаОрганизаций.Организация = СведенияОДоходахСтраховыеВзносы.Организация
		|				И ФизЛицаОрганизаций.ГоловнаяОрганизация = СведенияОДоходахСтраховыеВзносы.ГоловнаяОрганизация
		|	ГДЕ
		|		СведенияОДоходахСтраховыеВзносы.Период МЕЖДУ &НачалоПериода И &КонецПериода) КАК ДатыПолученияДохода";
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() И ЗначениеЗаполнено(Выборка.РанняяДатаПолученияДохода) Тогда
			УточненнаяДатаНачалаНП = Мин(ДатаНачалаНП, Выборка.РанняяДатаПолученияДохода);
			УточненнаяДатаКонцаНП = Макс(ДатаКонцаНП, Выборка.ПоздняяДатаПолученияДохода);
		Иначе 	
			УточненнаяДатаНачалаНП = ДатаНачалаНП;
			УточненнаяДатаКонцаНП = ДатаКонцаНП;
		КонецЕсли;
		
		УчетСтраховыхВзносов.СформироватьВТРасширенныеСведенияОДоходахИВзносах(УточненнаяДатаНачалаНП, УточненнаяДатаКонцаНП, Организация, Запрос.МенеджерВременныхТаблиц, Истина, Истина, Истина, Истина, Истина, Истина,,, Макс(ТекущаяДатаСеанса(), УточненнаяДатаКонцаНП));
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СведенияОДоходах.Организация КАК Организация,
		|	СведенияОДоходах.Подразделение КАК Подразделение,
		|	СведенияОДоходах.ФизическоеЛицо КАК ФизическоеЛицо,
		|	СведенияОДоходах.Период КАК МесяцПолученияДохода,
		|	СведенияОДоходах.ПериодРегистрации КАК МесяцРегистрацииДохода,
		|	ВЫБОР
		|		КОГДА СведенияОДоходах.Начисление ЕСТЬ NULL
		|			ТОГДА СведенияОДоходах.ВидДохода
		|		КОГДА СведенияОДоходах.Начисление = НЕОПРЕДЕЛЕНО
		|			ТОГДА СведенияОДоходах.ВидДохода
		|		ИНАЧЕ СведенияОДоходах.Начисление
		|	КОНЕЦ КАК ВидРасчета,
		|	СведенияОДоходах.Инвалид КАК Инвалид,
		|	СведенияОДоходах.ВидЗастрахованногоЛица КАК ВидЗастрахованногоЛица,
		|	СведенияОДоходах.НачислятьНакопительнуюЧасть КАК НачислятьНакопительнуюЧасть,
		|	СведенияОДоходах.ВидТарифаСтраховыхВзносов КАК ВидТарифаСтраховыхВзносов,
		|	ОписаниеТарифов.НачислятьВзносыСПревышения КАК НачислятьВзносыСПревышения,
		|	ОписаниеТарифов.СтавкаСПревышенияПредела КАК СтавкаСПревышенияПредела,
		|	СведенияОДоходах.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией КАК ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
		|	СведенияОДоходах.КлассУсловийТруда КАК КлассУсловийТруда,
		|	СведенияОДоходах.ЗастрахованФССкакИностранец КАК ЗастрахованФССкакИностранец,
		|	СведенияОДоходах.ИспользоватьЛьготуИнвалидамПоФСС_НС КАК ИспользоватьЛьготуИнвалидамПоФСС_НС,
		|	СУММА(СведенияОДоходах.Сумма) КАК НачисленоВсего,
		|	СУММА(СведенияОДоходах.СуммаПФР) КАК НачисленоВсегоПФР,
		|	СУММА(СведенияОДоходах.СуммаФОМС) КАК НачисленоВсегоФОМС,
		|	СУММА(СведенияОДоходах.СуммаФСС) КАК НачисленоВсегоФСС,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.НеЯвляетсяОбъектом)
		|				ТОГДА 0
		|			КОГДА НЕ СведенияОДоходах.ЗастрахованПФР
		|				ТОГДА 0
		|			ИНАЧЕ СведенияОДоходах.НеОблагаетсяПФР
		|		КОНЕЦ) КАК НеОблагаетсяПФР,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.НеЯвляетсяОбъектом)
		|				ТОГДА 0
		|			КОГДА НЕ СведенияОДоходах.ЗастрахованФОМС
		|				ТОГДА 0
		|			ИНАЧЕ СведенияОДоходах.НеОблагаетсяФОМС
		|		КОНЕЦ) КАК НеОблагаетсяФОМС,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.НеЯвляетсяОбъектом)
		|				ТОГДА 0
		|			КОГДА НЕ СведенияОДоходах.ЗастрахованФСС
		|					И НЕ СведенияОДоходах.ЗастрахованФССкакИностранец
		|				ТОГДА 0
		|			ИНАЧЕ СведенияОДоходах.НеОблагаетсяФСС
		|		КОНЕЦ) КАК НеОблагаетсяФСС,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.НеЯвляетсяОбъектом)
		|				ТОГДА 0
		|			ИНАЧЕ СведенияОДоходах.НеОблагаетсяФСС_НС
		|		КОНЕЦ) КАК НеОблагаетсяФСС_НС,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.НеЯвляетсяОбъектом)
		|				ТОГДА СведенияОДоходах.Сумма
		|			КОГДА НЕ СведенияОДоходах.ЗастрахованПФР
		|				ТОГДА СведенияОДоходах.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК НеЯвляетсяОбъектомИДоходыНезастрахованныхПФР,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.НеЯвляетсяОбъектом)
		|				ТОГДА СведенияОДоходах.Сумма
		|			КОГДА НЕ СведенияОДоходах.ЗастрахованФОМС
		|				ТОГДА СведенияОДоходах.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК НеЯвляетсяОбъектомИДоходыНезастрахованныхФОМС,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.НеЯвляетсяОбъектом)
		|				ТОГДА СведенияОДоходах.Сумма
		|			КОГДА НЕ СведенияОДоходах.ЗастрахованФСС
		|					И НЕ СведенияОДоходах.ЗастрахованФССкакИностранец
		|				ТОГДА СведенияОДоходах.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК НеЯвляетсяОбъектомИДоходыНезастрахованныхФСС,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ВидДохода В (&НеЯвляетсяОбъектомПоФСС_НС)
		|				ТОГДА СведенияОДоходах.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК НеЯвляетсяОбъектомФСС_НС,
		|	СУММА(СведенияОДоходах.СуммаПревысившаяПределПФР) КАК СуммаПревысившаяПределПФР,
		|	СУММА(СведенияОДоходах.СуммаПревысившаяПределФОМС) КАК СуммаПревысившаяПределФОМС,
		|	СУММА(СведенияОДоходах.СуммаПревысившаяПределФСС) КАК СуммаПревысившаяПределФСС,
		|	СУММА(СведенияОДоходах.БазаПФР - СведенияОДоходах.СуммаПревысившаяПределПФР) КАК БазаПФР,
		|	СУММА(СведенияОДоходах.БазаПФР) КАК БазаПФРЗанятыхНаРаботахСДосрочнойПенсией,
		|	СУММА(СведенияОДоходах.БазаФОМС - СведенияОДоходах.СуммаПревысившаяПределФОМС) КАК БазаФОМС,
		|	СУММА(СведенияОДоходах.БазаФСС - СведенияОДоходах.СуммаПревысившаяПределФСС) КАК БазаФСС,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ЗастрахованФССкакИностранец
		|				ТОГДА СведенияОДоходах.БазаФСС - СведенияОДоходах.СуммаПревысившаяПределФСС
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК БазаФССИностранцев,
		|	СУММА(СведенияОДоходах.БазаФСС_НС) КАК БазаФСС_НС,
		|	СУММА(СведенияОДоходах.ПФРПоСуммарномуТарифу) КАК ПФРПоСуммарномуТарифу,
		|	СУММА(СведенияОДоходах.ПФРСтраховая) КАК ПФРСтраховая,
		|	СУММА(СведенияОДоходах.ПФРССуммПревышения) КАК ПФРССуммПревышения,
		|	СУММА(СведенияОДоходах.ПФРНакопительная) КАК ПФРНакопительная,
		|	СУММА(СведенияОДоходах.ОПСпоМеждународнымДоговорам) КАК ОПСпоМеждународнымДоговорам,
		|	СУММА(СведенияОДоходах.ОПСпоМеждународнымДоговорамСПревышения) КАК ОПСпоМеждународнымДоговорамСПревышения,
		|	СУММА(СведенияОДоходах.ОМСпоМеждународнымДоговорам) КАК ОМСпоМеждународнымДоговорам,
		|	СУММА(СведенияОДоходах.ОМСпоМеждународнымДоговорамСПревышения) КАК ОМСпоМеждународнымДоговорамСПревышения,
		|	СУММА(СведенияОДоходах.ОССпоМеждународнымДоговорам) КАК ОССпоМеждународнымДоговорам,
		|	СУММА(СведенияОДоходах.ОССпоМеждународнымДоговорамСПревышения) КАК ОССпоМеждународнымДоговорамСПревышения,
		|	СУММА(СведенияОДоходах.ФФОМС) КАК ФФОМС,
		|	СУММА(СведенияОДоходах.ТФОМС) КАК ТФОМС,
		|	СУММА(СведенияОДоходах.ФСС) КАК ФСС,
		|	СУММА(СведенияОДоходах.ФССНесчастныеСлучаи) КАК ФССНесчастныеСлучаи,
		|	СУММА(СведенияОДоходах.ПФРЗаЗанятыхНаРаботахСДосрочнойПенсией) КАК ПФРЗаЗанятыхНаРаботахСДосрочнойПенсией,
		|	СУММА(СведенияОДоходах.ПФРНаДоплатуЛетчикам) КАК ПФРПоДополнительномуТарифу,
		|	СУММА(СведенияОДоходах.ПФРНаДоплатуШахтерам) КАК ПФРНаДоплатуКПенсииШахтерам,
		|	СУММА(СведенияОДоходах.ПособияЗаСчетФСС) КАК ПособиеЗаСчетФСС,
		|	СУММА(СведенияОДоходах.ПособияЗаСчетФСС_НС) КАК ПособиеЗаСчетФСС_НС,
		|	ЛОЖЬ КАК ВключатьВПервичнуюОтчетностьПФР,
		|	ЗНАЧЕНИЕ(Справочник.СамостоятельныеКлассификационныеЕдиницы.ПустаяСсылка) КАК СКЕ,
		|	0 КАК СтавкаНаНСиПЗ
		|ПОМЕСТИТЬ ВТДанныеДляОтчета
		|ИЗ
		|	ВТРасширенныеСведенияОДоходахИВзносах КАК СведенияОДоходах
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОписаниеТарифов КАК ОписаниеТарифов
		|		ПО СведенияОДоходах.ВидТарифаСтраховыхВзносов = ОписаниеТарифов.ВидТарифа
		|			И (ОписаниеТарифов.РасчетныйПериод = ГОД(СведенияОДоходах.Период))
		|ГДЕ
		|	СведенияОДоходах.ПериодРегистрации МЕЖДУ &НачалоПериода И &КонецПериода
		|	И НЕ СведенияОДоходах.ВключатьВПервичнуюОтчетностьПФР
		|
		|СГРУППИРОВАТЬ ПО
		|	СведенияОДоходах.ФизическоеЛицо,
		|	СведенияОДоходах.НачислятьНакопительнуюЧасть,
		|	СведенияОДоходах.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
		|	СведенияОДоходах.КлассУсловийТруда,
		|	СведенияОДоходах.ЗастрахованФССкакИностранец,
		|	СведенияОДоходах.ИспользоватьЛьготуИнвалидамПоФСС_НС,
		|	СведенияОДоходах.Организация,
		|	СведенияОДоходах.Подразделение,
		|	СведенияОДоходах.ВидТарифаСтраховыхВзносов,
		|	ОписаниеТарифов.НачислятьВзносыСПревышения,
		|	ОписаниеТарифов.СтавкаСПревышенияПредела,
		|	СведенияОДоходах.Инвалид,
		|	СведенияОДоходах.ВидЗастрахованногоЛица,
		|	ВЫБОР
		|		КОГДА СведенияОДоходах.Начисление ЕСТЬ NULL
		|			ТОГДА СведенияОДоходах.ВидДохода
		|		КОГДА СведенияОДоходах.Начисление = НЕОПРЕДЕЛЕНО
		|			ТОГДА СведенияОДоходах.ВидДохода
		|		ИНАЧЕ СведенияОДоходах.Начисление
		|	КОНЕЦ,
		|	СведенияОДоходах.Период,
		|	СведенияОДоходах.ПериодРегистрации
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеБезОпоздавшихИсправлений.Организация,
		|	ДанныеБезОпоздавшихИсправлений.Подразделение,
		|	ДанныеБезОпоздавшихИсправлений.ФизическоеЛицо,
		|	ДанныеБезОпоздавшихИсправлений.МесяцПолученияДохода,
		|	ДанныеБезОпоздавшихИсправлений.МесяцРегистрацииДохода,
		|	ДанныеБезОпоздавшихИсправлений.ВидРасчета,
		|	ДанныеБезОпоздавшихИсправлений.Инвалид,
		|	ДанныеБезОпоздавшихИсправлений.ВидЗастрахованногоЛица,
		|	ДанныеБезОпоздавшихИсправлений.НачислятьНакопительнуюЧасть,
		|	ДанныеБезОпоздавшихИсправлений.ВидТарифаСтраховыхВзносов,
		|	ДанныеБезОпоздавшихИсправлений.НачислятьВзносыСПревышения,
		|	ДанныеБезОпоздавшихИсправлений.СтавкаСПревышенияПредела,
		|	ДанныеБезОпоздавшихИсправлений.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
		|	ДанныеБезОпоздавшихИсправлений.КлассУсловийТруда,
		|	ДанныеБезОпоздавшихИсправлений.ЗастрахованФССкакИностранец,
		|	ДанныеБезОпоздавшихИсправлений.ИспользоватьЛьготуИнвалидамПоФСС_НС,
		|	ДанныеБезОпоздавшихИсправлений.НачисленоВсего,
		|	ДанныеБезОпоздавшихИсправлений.НачисленоВсегоПФР,
		|	ДанныеБезОпоздавшихИсправлений.НачисленоВсегоФОМС,
		|	ДанныеБезОпоздавшихИсправлений.НачисленоВсегоФСС,
		|	ДанныеБезОпоздавшихИсправлений.НеОблагаетсяПФР,
		|	ДанныеБезОпоздавшихИсправлений.НеОблагаетсяФОМС,
		|	ДанныеБезОпоздавшихИсправлений.НеОблагаетсяФСС,
		|	ДанныеБезОпоздавшихИсправлений.НеОблагаетсяФСС_НС,
		|	ДанныеБезОпоздавшихИсправлений.НеЯвляетсяОбъектомИДоходыНезастрахованныхПФР,
		|	ДанныеБезОпоздавшихИсправлений.НеЯвляетсяОбъектомИДоходыНезастрахованныхФОМС,
		|	ДанныеБезОпоздавшихИсправлений.НеЯвляетсяОбъектомИДоходыНезастрахованныхФСС,
		|	ДанныеБезОпоздавшихИсправлений.НеЯвляетсяОбъектомФСС_НС,
		|	ДанныеБезОпоздавшихИсправлений.СуммаПревысившаяПределПФР,
		|	ДанныеБезОпоздавшихИсправлений.СуммаПревысившаяПределФОМС,
		|	ДанныеБезОпоздавшихИсправлений.СуммаПревысившаяПределФСС,
		|	ДанныеБезОпоздавшихИсправлений.БазаПФР,
		|	ДанныеБезОпоздавшихИсправлений.БазаПФРЗанятыхНаРаботахСДосрочнойПенсией,
		|	ДанныеБезОпоздавшихИсправлений.БазаФОМС,
		|	ДанныеБезОпоздавшихИсправлений.БазаФСС,
		|	ДанныеБезОпоздавшихИсправлений.БазаФССИностранцев,
		|	ДанныеБезОпоздавшихИсправлений.БазаФСС_НС,
		|	ДанныеБезОпоздавшихИсправлений.ПФРПоСуммарномуТарифу,
		|	ДанныеБезОпоздавшихИсправлений.ПФРСтраховая,
		|	ДанныеБезОпоздавшихИсправлений.ПФРССуммПревышения,
		|	ДанныеБезОпоздавшихИсправлений.ПФРНакопительная,
		|	ДанныеБезОпоздавшихИсправлений.ОПСпоМеждународнымДоговорам,
		|	ДанныеБезОпоздавшихИсправлений.ОПСпоМеждународнымДоговорамСПревышения,
		|	ДанныеБезОпоздавшихИсправлений.ОМСпоМеждународнымДоговорам,
		|	ДанныеБезОпоздавшихИсправлений.ОМСпоМеждународнымДоговорамСПревышения,
		|	ДанныеБезОпоздавшихИсправлений.ОССпоМеждународнымДоговорам,
		|	ДанныеБезОпоздавшихИсправлений.ОССпоМеждународнымДоговорамСПревышения,
		|	ДанныеБезОпоздавшихИсправлений.ФФОМС,
		|	ДанныеБезОпоздавшихИсправлений.ТФОМС,
		|	ДанныеБезОпоздавшихИсправлений.ФСС,
		|	ДанныеБезОпоздавшихИсправлений.ФССНесчастныеСлучаи,
		|	ДанныеБезОпоздавшихИсправлений.ПФРЗаЗанятыхНаРаботахСДосрочнойПенсией,
		|	ДанныеБезОпоздавшихИсправлений.ПФРПоДополнительномуТарифу,
		|	ДанныеБезОпоздавшихИсправлений.ПФРНаДоплатуКПенсииШахтерам,
		|	ДанныеБезОпоздавшихИсправлений.ПособиеЗаСчетФСС,
		|	ДанныеБезОпоздавшихИсправлений.ПособиеЗаСчетФСС_НС,
		|	ИСТИНА,
		|	ЗНАЧЕНИЕ(Справочник.СамостоятельныеКлассификационныеЕдиницы.ПустаяСсылка),
		|	0
		|ИЗ
		|	ВТДанныеБезОпоздавшихИсправлений КАК ДанныеБезОпоздавшихИсправлений";

	Иначе
		
		РасчетЗарплаты.СоздатьВТДополнительныеСвойстваНачислений(Запрос.МенеджерВременныхТаблиц);
		
		// Дополнительно прочитаем возмещаемые ФСС суммы страховых взносов с оплаты дополнительных выходных дней по уходу за детьми-инвалидами.
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Пособия.Организация КАК Организация
		|ИЗ
		|	РегистрНакопления.ПособияПоСоциальномуСтрахованию КАК Пособия
		|ГДЕ
		|	Пособия.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И (Пособия.Организация, Пособия.ВидПособияСоциальногоСтрахования, Пособия.ФизическоеЛицо) В
		|			(ВЫБРАТЬ
		|				ФизЛицаОрганизаций.Организация КАК Организация,
		|				ЗНАЧЕНИЕ(Перечисление.ПереченьПособийСоциальногоСтрахования.СтраховыеВзносыПоДопВыходнымПоУходуЗаДетьмиИнвалидами) КАК ВидПособияСоциальногоСтрахования,
		|				ФизЛицаОрганизаций.ФизическоеЛицо КАК ФизическоеЛицо
		|			ИЗ
		|				ВТФизЛицаОрганизаций КАК ФизЛицаОрганизаций)";
		Если Не Запрос.Выполнить().Пустой() Тогда
			Запрос.Текст = 
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ФизЛицаОрганизаций.Организация КАК Организация,
			|	ФизЛицаОрганизаций.ФизическоеЛицо КАК ФизическоеЛицо
			|ПОМЕСТИТЬ ВТФизическиеЛицаОтбор
			|ИЗ
			|	ВТФизЛицаОрганизаций КАК ФизЛицаОрганизаций
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	СведенияОДоходах.ФизическоеЛицо КАК ФизическоеЛицо,
			|	СведенияОДоходах.ПериодРегистрации КАК Период,
			|	МИНИМУМ(СведенияОДоходах.Инвалид) КАК Инвалид,
			|	МИНИМУМ(СведенияОДоходах.ВидЗастрахованногоЛица) КАК ВидЗастрахованногоЛица,
			|	МИНИМУМ(СведенияОДоходах.НачислятьНакопительнуюЧасть) КАК НачислятьНакопительнуюЧасть,
			|	МИНИМУМ(СведенияОДоходах.ЗастрахованФССкакИностранец) КАК ЗастрахованФССкакИностранец,
			|	МИНИМУМ(СведенияОДоходах.ИспользоватьЛьготуИнвалидамПоФСС_НС) КАК ИспользоватьЛьготуИнвалидамПоФСС_НС
			|ПОМЕСТИТЬ ВТДанныеФизлиц
			|ИЗ
			|	ВТРасширенныеСведенияОДоходахИВзносах КАК СведенияОДоходах
			|
			|СГРУППИРОВАТЬ ПО
			|	СведенияОДоходах.ФизическоеЛицо,
			|	СведенияОДоходах.ПериодРегистрации
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ПособияПоСоциальномуСтрахованиюОбороты.Организация КАК Организация,
			|	ПособияПоСоциальномуСтрахованиюОбороты.ФизическоеЛицо КАК ФизическоеЛицо,
			|	НАЧАЛОПЕРИОДА(ПособияПоСоциальномуСтрахованиюОбороты.Период, МЕСЯЦ) КАК МесяцПолученияДохода,
			|	НАЧАЛОПЕРИОДА(ПособияПоСоциальномуСтрахованиюОбороты.Период, МЕСЯЦ) КАК МесяцРегистрацииДохода,
			|	ПособияПоСоциальномуСтрахованиюОбороты.ВидПособияСоциальногоСтрахования КАК ВидРасчета,
			|	ПособияПоСоциальномуСтрахованиюОбороты.СуммаВсегоОборот КАК ПособиеЗаСчетФСС,
			|	ДанныеФизлиц.Инвалид КАК Инвалид,
			|	ДанныеФизлиц.ВидЗастрахованногоЛица КАК ВидЗастрахованногоЛица,
			|	ДанныеФизлиц.НачислятьНакопительнуюЧасть КАК НачислятьНакопительнуюЧасть,
			|	ДанныеФизлиц.ЗастрахованФССкакИностранец КАК ЗастрахованФССкакИностранец,
			|	ДанныеФизлиц.ИспользоватьЛьготуИнвалидамПоФСС_НС КАК ИспользоватьЛьготуИнвалидамПоФСС_НС,
			|	ИспользуемыеТарифы.ВидТарифаСтраховыхВзносов КАК ВидТарифаСтраховыхВзносов,
			|	ОписаниеТарифов.НачислятьВзносыСПревышения КАК НачислятьВзносыСПревышения,
			|	ОписаниеТарифов.СтавкаСПревышенияПредела КАК СтавкаСПревышенияПредела,
			|	0 КАК ФСС,
			|	0 КАК БазаПФР,
			|	0 КАК БазаПФРЗанятыхНаРаботахСДосрочнойПенсией,
			|	0 КАК БазаФОМС,
			|	0 КАК БазаФСС,
			|	0 КАК БазаФССИностранцев,
			|	0 КАК НачисленоВсего,
			|	0 КАК НачисленоВсегоПФР,
			|	0 КАК НачисленоВсегоФОМС,
			|	0 КАК НачисленоВсегоФСС,
			|	0 КАК НеОблагаетсяПФР,
			|	0 КАК НеОблагаетсяФОМС,
			|	0 КАК НеОблагаетсяФСС,
			|	0 КАК НеЯвляетсяОбъектомИДоходыНезастрахованныхПФР,
			|	0 КАК НеЯвляетсяОбъектомИДоходыНезастрахованныхФОМС,
			|	0 КАК НеЯвляетсяОбъектомИДоходыНезастрахованныхФСС,
			|	0 КАК ПФРЗаЗанятыхНаРаботахСДосрочнойПенсией,
			|	0 КАК ПФРНаДоплатуКПенсииШахтерам,
			|	0 КАК ПФРНакопительная,
			|	0 КАК ПФРПоДополнительномуТарифу,
			|	0 КАК ПФРПоСуммарномуТарифу,
			|	0 КАК ПФРССуммПревышения,
			|	0 КАК ПФРСтраховая,
			|	0 КАК СтавкаПФР,
			|	0 КАК СтавкаПФРНакопительная,
			|	0 КАК СтавкаСуммарныйТариф,
			|	0 КАК СтавкаТФОМС,
			|	0 КАК СтавкаФСС,
			|	0 КАК СтавкаФССсИностранцев,
			|	0 КАК СтавкаФФОМС,
			|	0 КАК СуммаПревысившаяПределПФР,
			|	0 КАК СуммаПревысившаяПределФОМС,
			|	0 КАК СуммаПревысившаяПределФСС,
			|	0 КАК ТФОМС,
			|	0 КАК ФФОМС
			|ИЗ
			|	(ВЫБРАТЬ
			|		ПособияПоСоциальномуСтрахованиюОбороты.Организация КАК Организация,
			|		ПособияПоСоциальномуСтрахованиюОбороты.ФизическоеЛицо КАК ФизическоеЛицо,
			|		НАЧАЛОПЕРИОДА(ПособияПоСоциальномуСтрахованиюОбороты.Период, МЕСЯЦ) КАК Период,
			|		ПособияПоСоциальномуСтрахованиюОбороты.ВидПособияСоциальногоСтрахования КАК ВидПособияСоциальногоСтрахования,
			|		СУММА(ПособияПоСоциальномуСтрахованиюОбороты.СуммаВсего) КАК СуммаВсегоОборот
			|	ИЗ
			|		РегистрНакопления.ПособияПоСоциальномуСтрахованию КАК ПособияПоСоциальномуСтрахованиюОбороты
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТФизическиеЛицаОтбор КАК Отбор
			|			ПО (ПособияПоСоциальномуСтрахованиюОбороты.Период МЕЖДУ &НачалоПериода И &КонецПериода)
			|				И ПособияПоСоциальномуСтрахованиюОбороты.Организация = Отбор.Организация
			|				И ПособияПоСоциальномуСтрахованиюОбороты.ФизическоеЛицо = Отбор.ФизическоеЛицо
			|				И (ПособияПоСоциальномуСтрахованиюОбороты.ВидПособияСоциальногоСтрахования = ЗНАЧЕНИЕ(Перечисление.ПереченьПособийСоциальногоСтрахования.СтраховыеВзносыПоДопВыходнымПоУходуЗаДетьмиИнвалидами))
			|	
			|	СГРУППИРОВАТЬ ПО
			|		ПособияПоСоциальномуСтрахованиюОбороты.Организация,
			|		ПособияПоСоциальномуСтрахованиюОбороты.ФизическоеЛицо,
			|		НАЧАЛОПЕРИОДА(ПособияПоСоциальномуСтрахованиюОбороты.Период, МЕСЯЦ),
			|		ПособияПоСоциальномуСтрахованиюОбороты.ВидПособияСоциальногоСтрахования) КАК ПособияПоСоциальномуСтрахованиюОбороты
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИспользуемыеТарифы КАК ИспользуемыеТарифы
			|		ПО ПособияПоСоциальномуСтрахованиюОбороты.Организация.ГоловнаяОрганизация = ИспользуемыеТарифы.ГоловнаяОрганизация
			|			И ПособияПоСоциальномуСтрахованиюОбороты.Период = ИспользуемыеТарифы.Период
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОписаниеТарифов КАК ОписаниеТарифов
			|		ПО (ИспользуемыеТарифы.ВидТарифаСтраховыхВзносов = ОписаниеТарифов.ВидТарифа)
			|			И (ОписаниеТарифов.РасчетныйПериод = ГОД(ПособияПоСоциальномуСтрахованиюОбороты.Период))
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеФизлиц КАК ДанныеФизлиц
			|		ПО ПособияПоСоциальномуСтрахованиюОбороты.ФизическоеЛицо = ДанныеФизлиц.ФизическоеЛицо
			|			И ПособияПоСоциальномуСтрахованиюОбороты.Период = ДанныеФизлиц.Период
			|ГДЕ
			|	ПособияПоСоциальномуСтрахованиюОбороты.СуммаВсегоОборот <> 0";
			ДополнительныеПособия = Запрос.Выполнить().Выгрузить();
		КонецЕсли;

		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СведенияОДоходах.Организация КАК Организация,
		|	СведенияОДоходах.Подразделение КАК Подразделение,
		|	СведенияОДоходах.ФизическоеЛицо КАК ФизическоеЛицо,
		|	СведенияОДоходах.Период КАК МесяцПолученияДохода,
		|	СведенияОДоходах.ПериодРегистрации КАК МесяцРегистрацииДохода,
		|	ВЫБОР
		|		КОГДА СведенияОДоходах.Начисление ЕСТЬ NULL
		|			ТОГДА СведенияОДоходах.ВидДохода
		|		КОГДА СведенияОДоходах.Начисление = НЕОПРЕДЕЛЕНО
		|			ТОГДА СведенияОДоходах.ВидДохода
		|		ИНАЧЕ СведенияОДоходах.Начисление
		|	КОНЕЦ КАК ВидРасчета,
		|	СведенияОДоходах.Инвалид КАК Инвалид,
		|	СведенияОДоходах.ВидЗастрахованногоЛица КАК ВидЗастрахованногоЛица,
		|	СведенияОДоходах.НачислятьНакопительнуюЧасть КАК НачислятьНакопительнуюЧасть,
		|	СведенияОДоходах.ВидТарифаСтраховыхВзносов КАК ВидТарифаСтраховыхВзносов,
		|	ОписаниеТарифов.НачислятьВзносыСПревышения КАК НачислятьВзносыСПревышения,
		|	ОписаниеТарифов.СтавкаСПревышенияПредела КАК СтавкаСПревышенияПредела,
		|	СведенияОДоходах.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией КАК ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
		|	СведенияОДоходах.КлассУсловийТруда КАК КлассУсловийТруда,
		|	СведенияОДоходах.ЗастрахованФССкакИностранец КАК ЗастрахованФССкакИностранец,
		|	СведенияОДоходах.ИспользоватьЛьготуИнвалидамПоФСС_НС КАК ИспользоватьЛьготуИнвалидамПоФСС_НС,
		|	ВЫБОР
		|		КОГДА СведенияОДоходах.СКЕ ССЫЛКА Справочник.СамостоятельныеКлассификационныеЕдиницы
		|			ТОГДА СведенияОДоходах.СКЕ
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СамостоятельныеКлассификационныеЕдиницы.ПустаяСсылка)
		|	КОНЕЦ КАК СКЕ,
		|	СведенияОДоходах.СтавкаНаНСиПЗ КАК СтавкаНаНСиПЗ,
		|	СУММА(СведенияОДоходах.Сумма) КАК НачисленоВсего,
		|	СУММА(СведенияОДоходах.СуммаПФР) КАК НачисленоВсегоПФР,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.Период < &ДатаОбъединенияВзносов
		|				ТОГДА СведенияОДоходах.СуммаФОМС
		|			КОГДА СведенияОДоходах.ВидДохода.ВходитВБазуФОМС
		|					И НЕ СведенияОДоходах.ВходитВБазуФОМС
		|				ТОГДА 0
		|			ИНАЧЕ СведенияОДоходах.СуммаФОМС
		|		КОНЕЦ) КАК НачисленоВсегоФОМС,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.Период < &ДатаОбъединенияВзносов
		|				ТОГДА СведенияОДоходах.СуммаФСС
		|			КОГДА СведенияОДоходах.ВидДохода.ВходитВБазуФСС
		|					И НЕ СведенияОДоходах.ВходитВБазуФСС
		|				ТОГДА 0
		|			ИНАЧЕ СведенияОДоходах.СуммаФСС
		|		КОНЕЦ) КАК НачисленоВсегоФСС,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.НеЯвляетсяОбъектом)
		|				ТОГДА 0
		|			КОГДА НЕ СведенияОДоходах.ЗастрахованПФР
		|				ТОГДА 0
		|			ИНАЧЕ СведенияОДоходах.НеОблагаетсяПФР
		|		КОНЕЦ) КАК НеОблагаетсяПФР,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.НеЯвляетсяОбъектом)
		|				ТОГДА 0
		|			КОГДА НЕ СведенияОДоходах.ЗастрахованФОМС
		|				ТОГДА 0
		|			КОГДА СведенияОДоходах.Период < &ДатаОбъединенияВзносов
		|				ТОГДА СведенияОДоходах.НеОблагаетсяФОМС
		|			КОГДА СведенияОДоходах.ВидДохода.ВходитВБазуФОМС
		|					И НЕ СведенияОДоходах.ВходитВБазуФОМС
		|				ТОГДА 0
		|			ИНАЧЕ СведенияОДоходах.НеОблагаетсяФОМС
		|		КОНЕЦ) КАК НеОблагаетсяФОМС,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.НеЯвляетсяОбъектом)
		|				ТОГДА 0
		|			КОГДА НЕ СведенияОДоходах.ЗастрахованФСС
		|					И НЕ СведенияОДоходах.ЗастрахованФССкакИностранец
		|				ТОГДА 0
		|			КОГДА СведенияОДоходах.Период < &ДатаОбъединенияВзносов
		|				ТОГДА СведенияОДоходах.НеОблагаетсяФСС
		|			КОГДА СведенияОДоходах.ВидДохода.ВходитВБазуФСС
		|					И НЕ СведенияОДоходах.ВходитВБазуФСС
		|				ТОГДА 0
		|			ИНАЧЕ СведенияОДоходах.НеОблагаетсяФСС
		|		КОНЕЦ) КАК НеОблагаетсяФСС,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.НеЯвляетсяОбъектом)
		|				ТОГДА 0
		|			ИНАЧЕ СведенияОДоходах.НеОблагаетсяФСС_НС
		|		КОНЕЦ) КАК НеОблагаетсяФСС_НС,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.НеЯвляетсяОбъектом)
		|				ТОГДА СведенияОДоходах.СуммаПФР
		|			КОГДА НЕ СведенияОДоходах.ЗастрахованПФР
		|				ТОГДА СведенияОДоходах.СуммаПФР
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК НеЯвляетсяОбъектомИДоходыНезастрахованныхПФР,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.НеЯвляетсяОбъектом)
		|				ТОГДА СведенияОДоходах.СуммаФОМС
		|			КОГДА НЕ СведенияОДоходах.ЗастрахованФОМС
		|				ТОГДА СведенияОДоходах.СуммаФОМС
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК НеЯвляетсяОбъектомИДоходыНезастрахованныхФОМС,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.НеЯвляетсяОбъектом)
		|				ТОГДА СведенияОДоходах.СуммаФСС
		|			КОГДА НЕ СведенияОДоходах.ЗастрахованФСС
		|					И НЕ СведенияОДоходах.ЗастрахованФССкакИностранец
		|				ТОГДА СведенияОДоходах.СуммаФСС
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК НеЯвляетсяОбъектомИДоходыНезастрахованныхФСС,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ВидДохода В (&НеЯвляетсяОбъектомПоФСС_НС)
		|				ТОГДА СведенияОДоходах.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК НеЯвляетсяОбъектомФСС_НС,
		|	СУММА(СведенияОДоходах.СуммаПревысившаяПределПФР) КАК СуммаПревысившаяПределПФР,
		|	СУММА(СведенияОДоходах.СуммаПревысившаяПределФОМС) КАК СуммаПревысившаяПределФОМС,
		|	СУММА(СведенияОДоходах.СуммаПревысившаяПределФСС) КАК СуммаПревысившаяПределФСС,
		|	СУММА(СведенияОДоходах.БазаПФР - СведенияОДоходах.СуммаПревысившаяПределПФР) КАК БазаПФР,
		|	СУММА(СведенияОДоходах.БазаПФР) КАК БазаПФРЗанятыхНаРаботахСДосрочнойПенсией,
		|	СУММА(СведенияОДоходах.БазаФОМС - СведенияОДоходах.СуммаПревысившаяПределФОМС) КАК БазаФОМС,
		|	СУММА(СведенияОДоходах.БазаФСС - СведенияОДоходах.СуммаПревысившаяПределФСС) КАК БазаФСС,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ЗастрахованФССкакИностранец
		|				ТОГДА СведенияОДоходах.БазаФСС - СведенияОДоходах.СуммаПревысившаяПределФСС
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК БазаФССИностранцев,
		|	СУММА(СведенияОДоходах.БазаФСС_НС) КАК БазаФСС_НС,
		|	СУММА(СведенияОДоходах.ПФРПоСуммарномуТарифу) КАК ПФРПоСуммарномуТарифу,
		|	СУММА(СведенияОДоходах.ПФРСтраховая) КАК ПФРСтраховая,
		|	СУММА(СведенияОДоходах.ПФРССуммПревышения) КАК ПФРССуммПревышения,
		|	СУММА(СведенияОДоходах.ПФРНакопительная) КАК ПФРНакопительная,
		|	СУММА(СведенияОДоходах.ОПСпоМеждународнымДоговорам) КАК ОПСпоМеждународнымДоговорам,
		|	СУММА(СведенияОДоходах.ОПСпоМеждународнымДоговорамСПревышения) КАК ОПСпоМеждународнымДоговорамСПревышения,
		|	СУММА(СведенияОДоходах.ОМСпоМеждународнымДоговорам) КАК ОМСпоМеждународнымДоговорам,
		|	СУММА(СведенияОДоходах.ОМСпоМеждународнымДоговорамСПревышения) КАК ОМСпоМеждународнымДоговорамСПревышения,
		|	СУММА(СведенияОДоходах.ОССпоМеждународнымДоговорам) КАК ОССпоМеждународнымДоговорам,
		|	СУММА(СведенияОДоходах.ОССпоМеждународнымДоговорамСПревышения) КАК ОССпоМеждународнымДоговорамСПревышения,
		|	СУММА(СведенияОДоходах.ФФОМС) КАК ФФОМС,
		|	СУММА(СведенияОДоходах.ТФОМС) КАК ТФОМС,
		|	СУММА(СведенияОДоходах.ФСС) КАК ФСС,
		|	СУММА(СведенияОДоходах.ФССНесчастныеСлучаи) КАК ФССНесчастныеСлучаи,
		|	СУММА(СведенияОДоходах.ПФРЗаЗанятыхНаРаботахСДосрочнойПенсией) КАК ПФРЗаЗанятыхНаРаботахСДосрочнойПенсией,
		|	СУММА(СведенияОДоходах.ПФРНаДоплатуЛетчикам) КАК ПФРПоДополнительномуТарифу,
		|	СУММА(СведенияОДоходах.ПФРНаДоплатуШахтерам) КАК ПФРНаДоплатуКПенсииШахтерам,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.НеОблагаетсяЦеликомПрокуроров)
		|					И ЕСТЬNULL(СвойстваНачисленийДляУчетаСтраховыхВзносов.ВидПособияСоциальногоСтрахования, НЕОПРЕДЕЛЕНО) В (&ПособияКромеНСиПЗ)
		|				ТОГДА СведенияОДоходах.Сумма
		|			КОГДА СведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.НеОблагаетсяЦеликомПрокуроров)
		|					И СведенияОДоходах.Начисление В (&ПособияКромеНСиПЗ)
		|				ТОГДА СведенияОДоходах.Сумма
		|			ИНАЧЕ СведенияОДоходах.ПособияЗаСчетФСС
		|		КОНЕЦ) КАК ПособиеЗаСчетФСС,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.НеОблагаетсяЦеликомПрокуроров)
		|					И ЕСТЬNULL(СвойстваНачисленийДляУчетаСтраховыхВзносов.ВидПособияСоциальногоСтрахования, НЕОПРЕДЕЛЕНО) В (&ПособияНСиПЗ)
		|				ТОГДА СведенияОДоходах.Сумма
		|			ИНАЧЕ СведенияОДоходах.ПособияЗаСчетФСС_НС
		|		КОНЕЦ) КАК ПособиеЗаСчетФСС_НС,
		|	ИСТИНА КАК ВключатьВПервичнуюОтчетностьПФР
		|ПОМЕСТИТЬ ВТДанныеДляОтчета
		|ИЗ
		|	ВТРасширенныеСведенияОДоходахИВзносах КАК СведенияОДоходах
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОписаниеТарифов КАК ОписаниеТарифов
		|		ПО СведенияОДоходах.ВидТарифаСтраховыхВзносов = ОписаниеТарифов.ВидТарифа
		|			И (ОписаниеТарифов.РасчетныйПериод = ГОД(СведенияОДоходах.Период))
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДополнительныеСвойстваНачислений КАК СвойстваНачисленийДляУчетаСтраховыхВзносов
		|		ПО (ВЫБОР
		|				КОГДА СведенияОДоходах.Начисление ЕСТЬ NULL
		|					ТОГДА СведенияОДоходах.ВидДохода
		|				КОГДА СведенияОДоходах.Начисление = НЕОПРЕДЕЛЕНО
		|					ТОГДА СведенияОДоходах.ВидДохода
		|				ИНАЧЕ СведенияОДоходах.Начисление
		|			КОНЕЦ = СвойстваНачисленийДляУчетаСтраховыхВзносов.Начисление)
		|ГДЕ
		|	СведенияОДоходах.ПериодРегистрации МЕЖДУ &НачалоПериода И &КонецПериода
		|
		|СГРУППИРОВАТЬ ПО
		|	СведенияОДоходах.ФизическоеЛицо,
		|	СведенияОДоходах.НачислятьНакопительнуюЧасть,
		|	СведенияОДоходах.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
		|	СведенияОДоходах.КлассУсловийТруда,
		|	СведенияОДоходах.ЗастрахованФССкакИностранец,
		|	СведенияОДоходах.ИспользоватьЛьготуИнвалидамПоФСС_НС,
		|	СведенияОДоходах.Организация,
		|	СведенияОДоходах.Подразделение,
		|	СведенияОДоходах.ВидТарифаСтраховыхВзносов,
		|	ОписаниеТарифов.НачислятьВзносыСПревышения,
		|	ОписаниеТарифов.СтавкаСПревышенияПредела,
		|	ВЫБОР
		|		КОГДА СведенияОДоходах.СКЕ ССЫЛКА Справочник.СамостоятельныеКлассификационныеЕдиницы
		|			ТОГДА СведенияОДоходах.СКЕ
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СамостоятельныеКлассификационныеЕдиницы.ПустаяСсылка)
		|	КОНЕЦ,
		|	СведенияОДоходах.СтавкаНаНСиПЗ,
		|	СведенияОДоходах.Инвалид,
		|	СведенияОДоходах.ВидЗастрахованногоЛица,
		|	ВЫБОР
		|		КОГДА СведенияОДоходах.Начисление ЕСТЬ NULL
		|			ТОГДА СведенияОДоходах.ВидДохода
		|		КОГДА СведенияОДоходах.Начисление = НЕОПРЕДЕЛЕНО
		|			ТОГДА СведенияОДоходах.ВидДохода
		|		ИНАЧЕ СведенияОДоходах.Начисление
		|	КОНЕЦ,
		|	СведенияОДоходах.Период,
		|	СведенияОДоходах.ПериодРегистрации";
	КонецЕсли;
	Запрос.Выполнить();
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДляОтчета.Организация,
	|	ДанныеДляОтчета.МесяцПолученияДохода КАК Период
	|ПОМЕСТИТЬ ВТОрганизацииПериоды
	|ИЗ
	|	ВТДанныеДляОтчета КАК ДанныеДляОтчета";
		
	Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеДляОтчета.Организация КАК Организация,
	|	ДанныеДляОтчета.Подразделение КАК Подразделение,
	|	ДанныеДляОтчета.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ДанныеДляОтчета.МесяцПолученияДохода КАК МесяцПолученияДохода,
	|	ДанныеДляОтчета.МесяцРегистрацииДохода КАК МесяцРегистрацииДохода,
	|	ДанныеДляОтчета.ВключатьВПервичнуюОтчетностьПФР КАК ВключатьВПервичнуюОтчетностьПФР,
	|	ДанныеДляОтчета.ВидРасчета КАК ВидРасчета,
	|	ДанныеДляОтчета.ВидТарифаСтраховыхВзносов КАК ВидТарифаСтраховыхВзносов,
	|	ДанныеДляОтчета.НачислятьВзносыСПревышения КАК НачислятьВзносыСПревышения,
	|	ДанныеДляОтчета.СтавкаСПревышенияПредела КАК СтавкаСПревышенияПредела,
	|	ДанныеДляОтчета.НачислятьНакопительнуюЧасть КАК НачислятьНакопительнуюЧасть,
	|	ДанныеДляОтчета.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией КАК ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	ДанныеДляОтчета.КлассУсловийТруда КАК КлассУсловийТруда,
	|	ДанныеДляОтчета.ЗастрахованФССкакИностранец КАК ЗастрахованФССкакИностранец,
	|	ДанныеДляОтчета.ИспользоватьЛьготуИнвалидамПоФСС_НС КАК ИспользоватьЛьготуИнвалидамПоФСС_НС,
	|	ДанныеДляОтчета.ВидЗастрахованногоЛица КАК СтатусЗастрахованногоЛица,
	|	СУММА(ДанныеДляОтчета.НачисленоВсего) КАК НачисленоВсего,
	|	СУММА(ДанныеДляОтчета.НачисленоВсегоПФР) КАК НачисленоВсегоПФР,
	|	СУММА(ДанныеДляОтчета.НачисленоВсегоФОМС) КАК НачисленоВсегоФОМС,
	|	СУММА(ДанныеДляОтчета.НачисленоВсегоФСС) КАК НачисленоВсегоФСС,
	|	СУММА(ДанныеДляОтчета.НеОблагаетсяПФР) КАК НеОблагаетсяПФР,
	|	СУММА(ДанныеДляОтчета.НеОблагаетсяФОМС) КАК НеОблагаетсяФОМС,
	|	СУММА(ДанныеДляОтчета.НеОблагаетсяФСС) КАК НеОблагаетсяФСС,
	|	СУММА(ДанныеДляОтчета.НеЯвляетсяОбъектомИДоходыНезастрахованныхПФР) КАК НеЯвляетсяОбъектомИДоходыНезастрахованныхПФР,
	|	СУММА(ДанныеДляОтчета.НеЯвляетсяОбъектомИДоходыНезастрахованныхФОМС) КАК НеЯвляетсяОбъектомИДоходыНезастрахованныхФОМС,
	|	СУММА(ДанныеДляОтчета.НеЯвляетсяОбъектомИДоходыНезастрахованныхФСС) КАК НеЯвляетсяОбъектомИДоходыНезастрахованныхФСС,
	|	СУММА(ДанныеДляОтчета.СуммаПревысившаяПределПФР) КАК СуммаПревысившаяПределПФР,
	|	СУММА(ДанныеДляОтчета.СуммаПревысившаяПределФОМС) КАК СуммаПревысившаяПределФОМС,
	|	СУММА(ДанныеДляОтчета.СуммаПревысившаяПределФСС) КАК СуммаПревысившаяПределФСС,
	|	СУММА(ДанныеДляОтчета.БазаПФР) КАК БазаПФР,
	|	СУММА(ДанныеДляОтчета.БазаПФРЗанятыхНаРаботахСДосрочнойПенсией) КАК БазаПФРЗанятыхНаРаботахСДосрочнойПенсией,
	|	СУММА(ДанныеДляОтчета.БазаФОМС) КАК БазаФОМС,
	|	СУММА(ДанныеДляОтчета.БазаФСС) КАК БазаФСС,
	|	СУММА(ДанныеДляОтчета.БазаФССИностранцев) КАК БазаФССИностранцев,
	|	СУММА(ДанныеДляОтчета.ПФРПоСуммарномуТарифу) КАК ПФРПоСуммарномуТарифу,
	|	СУММА(ДанныеДляОтчета.ПФРСтраховая) КАК ПФРСтраховая,
	|	СУММА(ДанныеДляОтчета.ПФРССуммПревышения) КАК ПФРССуммПревышения,
	|	СУММА(ДанныеДляОтчета.ПФРНакопительная) КАК ПФРНакопительная,
	|	СУММА(ДанныеДляОтчета.ОПСпоМеждународнымДоговорам) КАК ОПСпоМеждународнымДоговорам,
	|	СУММА(ДанныеДляОтчета.ОПСпоМеждународнымДоговорамСПревышения) КАК ОПСпоМеждународнымДоговорамСПревышения,
	|	СУММА(ДанныеДляОтчета.ОМСпоМеждународнымДоговорам) КАК ОМСпоМеждународнымДоговорам,
	|	СУММА(ДанныеДляОтчета.ОМСпоМеждународнымДоговорамСПревышения) КАК ОМСпоМеждународнымДоговорамСПревышения,
	|	СУММА(ДанныеДляОтчета.ОССпоМеждународнымДоговорам) КАК ОССпоМеждународнымДоговорам,
	|	СУММА(ДанныеДляОтчета.ОССпоМеждународнымДоговорамСПревышения) КАК ОССпоМеждународнымДоговорамСПревышения,
	|	СУММА(ДанныеДляОтчета.ФФОМС) КАК ФФОМС,
	|	СУММА(ДанныеДляОтчета.ТФОМС) КАК ТФОМС,
	|	СУММА(ДанныеДляОтчета.ФСС) КАК ФСС,
	|	СУММА(ДанныеДляОтчета.ПФРПоДополнительномуТарифу) КАК ПФРПоДополнительномуТарифу,
	|	СУММА(ДанныеДляОтчета.ПФРНаДоплатуКПенсииШахтерам) КАК ПФРНаДоплатуКПенсииШахтерам,
	|	СУММА(ДанныеДляОтчета.ПФРЗаЗанятыхНаРаботахСДосрочнойПенсией) КАК ПФРЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	СУММА(ДанныеДляОтчета.ПособиеЗаСчетФСС) КАК ПособиеЗаСчетФСС,
	|	РазмерыТарифныхСтавок.ПФР КАК СтавкаПФР,
	|	РазмерыТарифныхСтавок.ПФРНакопительная КАК СтавкаПФРНакопительная,
	|	РазмерыТарифныхСтавок.ФСС КАК СтавкаФСС,
	|	РазмерыТарифныхСтавок.ФССсИностранцев КАК СтавкаФССсИностранцев,
	|	РазмерыТарифныхСтавок.ФФОМС КАК СтавкаФФОМС,
	|	РазмерыТарифныхСтавок.ТФОМС КАК СтавкаТФОМС,
	|	РазмерыТарифныхСтавок.ПФР + РазмерыТарифныхСтавок.ФСС + РазмерыТарифныхСтавок.ФФОМС + РазмерыТарифныхСтавок.ТФОМС КАК СтавкаСуммарныйТариф,
	|	ДанныеДляОтчета.ВидЗастрахованногоЛица В (ЗНАЧЕНИЕ(Перечисление.ВидыЗастрахованныхЛицОбязательногоСтрахования.ВременноПребывающиеИностранцыСтрахуемыеНаОПС), ЗНАЧЕНИЕ(Перечисление.ВидыЗастрахованныхЛицОбязательногоСтрахования.ВременноПребывающиеИностранцыСтрахуемыеФСС))
	|		И ДанныеДляОтчета.МесяцПолученияДохода >= &ДатаОбъединенияВзносов КАК ИсчислениеПоМеждународнымДоговорам
	|ИЗ
	|	ВТДанныеДляОтчета КАК ДанныеДляОтчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРазмерыТарифныхСтавок КАК РазмерыТарифныхСтавок
	|		ПО ДанныеДляОтчета.ВидТарифаСтраховыхВзносов = РазмерыТарифныхСтавок.ВидТарифа
	|			И (НАЧАЛОПЕРИОДА(ДанныеДляОтчета.МесяцПолученияДохода, ГОД) = РазмерыТарифныхСтавок.Период)
	|ГДЕ
	|	(ДанныеДляОтчета.ФСС <> 0
	|			ИЛИ ДанныеДляОтчета.ФФОМС <> 0
	|			ИЛИ ДанныеДляОтчета.ПФРССуммПревышения <> 0
	|			ИЛИ ДанныеДляОтчета.ПФРПоСуммарномуТарифу <> 0
	|			ИЛИ ДанныеДляОтчета.ПФРНакопительная <> 0
	|			ИЛИ ДанныеДляОтчета.ПФРСтраховая <> 0
	|			ИЛИ ДанныеДляОтчета.ПФРПоДополнительномуТарифу <> 0
	|			ИЛИ ДанныеДляОтчета.ПФРНаДоплатуКПенсииШахтерам <> 0
	|			ИЛИ ДанныеДляОтчета.ПФРЗаЗанятыхНаРаботахСДосрочнойПенсией <> 0
	|			ИЛИ ДанныеДляОтчета.НачисленоВсего <> 0
	|			ИЛИ ДанныеДляОтчета.ОПСпоМеждународнымДоговорам <> 0
	|			ИЛИ ДанныеДляОтчета.ОПСпоМеждународнымДоговорамСПревышения <> 0
	|			ИЛИ ДанныеДляОтчета.ОМСпоМеждународнымДоговорам <> 0
	|			ИЛИ ДанныеДляОтчета.ОМСпоМеждународнымДоговорамСПревышения <> 0
	|			ИЛИ ДанныеДляОтчета.ОССпоМеждународнымДоговорам <> 0
	|			ИЛИ ДанныеДляОтчета.ОССпоМеждународнымДоговорамСПревышения <> 0)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДляОтчета.Организация,
	|	ДанныеДляОтчета.Подразделение,
	|	ДанныеДляОтчета.ФизическоеЛицо,
	|	ДанныеДляОтчета.ВидРасчета,
	|	ДанныеДляОтчета.ВидТарифаСтраховыхВзносов,
	|	ДанныеДляОтчета.НачислятьВзносыСПревышения,
	|	ДанныеДляОтчета.СтавкаСПревышенияПредела,
	|	ДанныеДляОтчета.НачислятьНакопительнуюЧасть,
	|	ДанныеДляОтчета.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	ДанныеДляОтчета.КлассУсловийТруда,
	|	ДанныеДляОтчета.ЗастрахованФССкакИностранец,
	|	ДанныеДляОтчета.ИспользоватьЛьготуИнвалидамПоФСС_НС,
	|	ДанныеДляОтчета.МесяцПолученияДохода,
	|	ДанныеДляОтчета.ВключатьВПервичнуюОтчетностьПФР,
	|	ДанныеДляОтчета.МесяцРегистрацииДохода,
	|	ДанныеДляОтчета.ВидЗастрахованногоЛица,
	|	РазмерыТарифныхСтавок.ПФР,
	|	РазмерыТарифныхСтавок.ПФРНакопительная,
	|	РазмерыТарифныхСтавок.ФСС,
	|	РазмерыТарифныхСтавок.ФССсИностранцев,
	|	РазмерыТарифныхСтавок.ФФОМС,
	|	РазмерыТарифныхСтавок.ТФОМС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДляОтчета.Организация КАК Организация,
	|	ДанныеДляОтчета.Подразделение КАК Подразделение,
	|	ДанныеДляОтчета.СКЕ КАК СКЕ,
	|	ДанныеДляОтчета.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ДанныеДляОтчета.МесяцПолученияДохода КАК МесяцПолученияДохода,
	|	ДанныеДляОтчета.МесяцРегистрацииДохода КАК МесяцРегистрацииДохода,
	|	ДанныеДляОтчета.ВидРасчета КАК ВидРасчета,
	|	ДанныеДляОтчета.Инвалид КАК Инвалид,
	|	ДанныеДляОтчета.ВидЗастрахованногоЛица КАК СтатусЗастрахованногоЛица,
	|	ДанныеДляОтчета.ИспользоватьЛьготуИнвалидамПоФСС_НС КАК ИспользоватьЛьготуИнвалидамПоФСС_НС,
	|	СУММА(ДанныеДляОтчета.НачисленоВсего) КАК НачисленоВсего,
	|	СУММА(ДанныеДляОтчета.НеОблагаетсяФСС_НС) КАК НеОблагаетсяФСС_НС,
	|	СУММА(ДанныеДляОтчета.НеЯвляетсяОбъектомФСС_НС) КАК НеЯвляетсяОбъектомФСС_НС,
	|	СУММА(ДанныеДляОтчета.БазаФСС_НС) КАК БазаФСС_НС,
	|	СУММА(ДанныеДляОтчета.ФССНесчастныеСлучаи) КАК ФССНесчастныеСлучаи,
	|	СУММА(ДанныеДляОтчета.ПособиеЗаСчетФСС_НС) КАК ПособиеЗаСчетФСС_НС,
	|	РазмерыТарифныхСтавок.ФСС КАК СтавкаФСС,
	|	ДанныеДляОтчета.СтавкаНаНСиПЗ КАК СтавкаФСС_НС
	|ИЗ
	|	ВТДанныеДляОтчета КАК ДанныеДляОтчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРазмерыТарифныхСтавок КАК РазмерыТарифныхСтавок
	|		ПО ДанныеДляОтчета.ВидТарифаСтраховыхВзносов = РазмерыТарифныхСтавок.ВидТарифа
	|			И (НАЧАЛОПЕРИОДА(ДанныеДляОтчета.МесяцПолученияДохода, ГОД) = РазмерыТарифныхСтавок.Период)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДляОтчета.Организация,
	|	ДанныеДляОтчета.Подразделение,
	|	ДанныеДляОтчета.ФизическоеЛицо,
	|	ДанныеДляОтчета.СКЕ,
	|	ДанныеДляОтчета.ВидРасчета,
	|	ДанныеДляОтчета.Инвалид,
	|	ДанныеДляОтчета.МесяцПолученияДохода,
	|	ДанныеДляОтчета.МесяцРегистрацииДохода,
	|	ДанныеДляОтчета.ВидЗастрахованногоЛица,
	|	ДанныеДляОтчета.ИспользоватьЛьготуИнвалидамПоФСС_НС,
	|	РазмерыТарифныхСтавок.ФСС,
	|	РазмерыТарифныхСтавок.ФССсИностранцев,
	|	ДанныеДляОтчета.СтавкаНаНСиПЗ";
	
	МассивРезультатовЗапроса = Запрос.ВыполнитьПакет();
	
	КоличествоРезультатов = МассивРезультатовЗапроса.Количество();
	ТаблицаВзносыФСС_НС = МассивРезультатовЗапроса[КоличествоРезультатов-1].Выгрузить();
	ТаблицаВзносы = МассивРезультатовЗапроса[КоличествоРезультатов-2].Выгрузить();
	ТаблицаВзносыФСС = ТаблицаВзносы;
	Если ЗначениеЗаполнено(ДополнительныеПособия) Тогда
		ТаблицаВзносыФСС = ТаблицаВзносыФСС.Скопировать();
		ТаблицаВзносыФСС.Колонки.ВидРасчета.Имя = "Образец";
		ТаблицаВзносыФСС.Колонки.Добавить("ВидРасчета", Новый ОписаниеТипов(ТаблицаВзносыФСС.Колонки.Образец.ТипЗначения, "ПеречислениеСсылка.ПереченьПособийСоциальногоСтрахования"));
		ТаблицаВзносыФСС.ЗагрузитьКолонку(ТаблицаВзносыФСС.ВыгрузитьКолонку("Образец"),"ВидРасчета");
		ТаблицаВзносыФСС.Колонки.Удалить("Образец");
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ДополнительныеПособия, ТаблицаВзносыФСС);
	КонецЕсли;
	
	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить("ДанныеПФР", ТаблицаВзносы);
	ВнешниеНаборыДанных.Вставить("ДанныеФОМС", ТаблицаВзносы);
	ВнешниеНаборыДанных.Вставить("ДанныеФСС", ТаблицаВзносыФСС);
	ВнешниеНаборыДанных.Вставить("ДанныеФСС_НС", ТаблицаВзносыФСС_НС);
	
	НастройкиКомпоновки = КомпоновщикНастроек.ПолучитьНастройки();
	
	ОтборПоОрганизации = Неопределено;
	Для Каждого ЭлементОтбора Из НастройкиКомпоновки.Отбор.Элементы Цикл
		
		Если ТипЗнч(ЭлементОтбора) <> Тип("ЭлементОтбораКомпоновкиДанных") Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ЭлементОтбора.Использование Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Организация") Тогда
			ОтборПоОрганизации = ЭлементОтбора.ПравоеЗначение;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ОтборПоОрганизации = Неопределено Тогда
		
		ИспользуетсяТрудШахтеров = ПолучитьФункциональнуюОпцию("ИспользуетсяТрудШахтеров");
		ИспользуетсяТрудЧленовЛетныхЭкипажей = ПолучитьФункциональнуюОпцию("ИспользуетсяТрудЧленовЛетныхЭкипажей");
		ИспользуетсяСКЕ = ПолучитьФункциональнуюОпцию("ИспользоватьСамостоятельныеКлассификационныеЕдиницы");
		
	Иначе
		
		Если ТипЗнч(ОтборПоОрганизации) = Тип("СправочникСсылка.Организации") Тогда
			
			ПараметрыФО = Новый Структура("Организация", ОтборПоОрганизации);
			
			ИспользуетсяТрудШахтеров = ПолучитьФункциональнуюОпцию("ИспользуетсяТрудШахтеров", ПараметрыФО);
			ИспользуетсяТрудЧленовЛетныхЭкипажей = ПолучитьФункциональнуюОпцию("ИспользуетсяТрудЧленовЛетныхЭкипажей", ПараметрыФО);
			ИспользуетсяСКЕ = ПолучитьФункциональнуюОпцию("ИспользоватьСамостоятельныеКлассификационныеЕдиницы", ПараметрыФО);
			
		Иначе
			
			ИспользуетсяТрудШахтеров = Ложь;
			ИспользуетсяТрудЧленовЛетныхЭкипажей = Ложь;
			ИспользуетсяСКЕ = Ложь;
			
			Если ТипЗнч(ОтборПоОрганизации) = Тип("СписокЗначений") Тогда
				СписокОрганизаций = ОтборПоОрганизации.ВыгрузитьЗначения();
			Иначе
				СписокОрганизаций = ОтборПоОрганизации;
			КонецЕсли;
			
			Для Каждого Организация Из СписокОрганизаций Цикл
				
				ПараметрыФО = Новый Структура("Организация", Организация);
				
				Если ПолучитьФункциональнуюОпцию("ИспользуетсяТрудШахтеров", ПараметрыФО) Тогда
					ИспользуетсяТрудШахтеров = Истина;
				КонецЕсли;
				
				Если ПолучитьФункциональнуюОпцию("ИспользуетсяТрудЧленовЛетныхЭкипажей", ПараметрыФО) Тогда
					ИспользуетсяТрудЧленовЛетныхЭкипажей = Истина;
				КонецЕсли;
				
				Если ПолучитьФункциональнуюОпцию("ИспользоватьСамостоятельныеКлассификационныеЕдиницы", ПараметрыФО) Тогда
					ИспользуетсяСКЕ = Истина;
				КонецЕсли;
				
				Если ИспользуетсяТрудШахтеров И ИспользуетсяТрудЧленовЛетныхЭкипажей И ИспользуетсяСКЕ Тогда
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ИспользуетсяТрудШахтеров Или Не ИспользуетсяТрудЧленовЛетныхЭкипажей Тогда
		ДанныеПФР = СхемаКомпоновкиДанных.ВложенныеСхемыКомпоновкиДанных.Найти("ДанныеПФР");
		Если ДанныеПФР <> Неопределено Тогда
			
			Если Не ИспользуетсяТрудШахтеров Тогда
				Поле = ДанныеПФР.Схема.НаборыДанных.ДанныеПФР.Поля.Найти("ПФРНаДоплатуКПенсииШахтерам");
				Если Поле <> Неопределено Тогда
					ДанныеПФР.Схема.НаборыДанных.ДанныеПФР.Поля.Удалить(Поле);
				КонецЕсли;
			КонецЕсли;
			Если Не ИспользуетсяТрудЧленовЛетныхЭкипажей Тогда
				Поле = ДанныеПФР.Схема.НаборыДанных.ДанныеПФР.Поля.Найти("ПФРПоДополнительномуТарифу");
				Если Поле <> Неопределено Тогда
					ДанныеПФР.Схема.НаборыДанных.ДанныеПФР.Поля.Удалить(Поле);
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	Если ИспользуетсяСКЕ Тогда
		ДанныеФСС_НС = СхемаКомпоновкиДанных.ВложенныеСхемыКомпоновкиДанных.Найти("ДанныеФСС_НС");
		Если ДанныеФСС_НС <> Неопределено Тогда
			Поле = ДанныеФСС_НС.Схема.НаборыДанных.ДанныеФСС_НС.Поля.Найти("СКЕ");
			Если Поле <> Неопределено Тогда
				Поле.Заголовок = НСтр("ru = 'Тариф/СКЕ';
										|en = 'Tariff/ICU'")
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	МакетКомпоновки = ЗарплатаКадрыОтчеты.МакетКомпоновкиДанных(СхемаКомпоновкиДанных, НастройкиКомпоновки);
		
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки, истина);
	
	УстановитьПараметрыВыводаТабличногоДокумента(ДокументРезультат);
	
КонецПроцедуры

Процедура УстановитьПараметрыВыводаТабличногоДокумента(ДокументРезультат)
	ДокументРезультат.Автомасштаб 			= 	Истина;
	ДокументРезультат.ОриентацияСтраницы 	= 	ОриентацияСтраницы.Портрет;
	ДокументРезультат.ТолькоПросмотр		= 	Истина;
	ДокументРезультат.ПолеСверху			= 	5;
	ДокументРезультат.ПолеСнизу				= 	0;
	ДокументРезультат.ПолеСлева				= 	15;
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли