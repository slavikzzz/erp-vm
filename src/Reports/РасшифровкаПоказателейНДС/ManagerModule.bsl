#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ПолучитьПараметрыИсполненияОтчета() Экспорт
	
	Структура = Новый Структура;
	Структура.Вставить("ИспользоватьПередКомпоновкойМакета", Истина);
	Структура.Вставить("ИспользоватьВнешниеНаборыДанных",    Истина);
	Структура.Вставить("ИспользоватьПослеВыводаРезультата",  Истина);
	Структура.Вставить("ИспользоватьДанныеРасшифровки",      Истина);
	Структура.Вставить("ИспользоватьПривилегированныйРежим", Ложь);
	Возврат Структура;
	
КонецФункции

Функция ПолучитьВнешниеНаборыДанных(ПараметрыОтчета, МакетКомпоновки) Экспорт
	
	СтруктураВнешнихНаборовДанных = Новый Структура();
	
	Запрос =  ПолучитьПараметрыЗапроса(ПараметрыОтчета);
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИдентификаторМакета = ПараметрыОтчета.ИдентификаторМакета;
	Если ИдентификаторМакета = "РасшифровкаНачислениеРеализацияСложныйУчетНДС" Тогда
		ПолучитьНаборДанныхНачислениеРеализацияСложныйУчетНДС(Запрос, СтруктураВнешнихНаборовДанных);
	ИначеЕсли ИдентификаторМакета = "РасшифровкаНачислениеАвансыПолученныеУпрощенныйУчетНДС" Тогда
		ПолучитьНаборДанныхНачислениеАвансыПолученныеУпрощенныйУчетНДС(Запрос, СтруктураВнешнихНаборовДанных);
	ИначеЕсли ИдентификаторМакета = "РасшифровкаНачислениеИсполнениеОНА" Тогда
		ПолучитьНаборДанныхНачислениеНалоговыйАгент(Запрос, СтруктураВнешнихНаборовДанных);
	ИначеЕсли ИдентификаторМакета = "РасшифровкаНачислениеВосстановлениеСАвансовВыданныхУпрощенныйУчетНДС" Тогда 
		ПолучитьНаборДанныхНачислениеВосстановлениеСАвансовВыданныхУпрощенныйУчетНДС(Запрос, СтруктураВнешнихНаборовДанных);
	ИначеЕсли ИдентификаторМакета = "РасшифровкаНачислениеСМРХозСпособом" Тогда
		ПолучитьНаборДанныхНачислениеСМРХозСпособом(Запрос, СтруктураВнешнихНаборовДанных);
	ИначеЕсли ИдентификаторМакета = "РасшифровкаНачислениеРеализация0" Тогда
		ПолучитьНаборДанныхНачислениеРеализация0(Запрос, СтруктураВнешнихНаборовДанных);
	ИначеЕсли ИдентификаторМакета = "РасшифровкаСтроки130Декларации" Тогда
		ПолучитьНаборДанныхВычетПоПриобретеннымЦенностямРасшифровка130СтрокиДекларации(Запрос, СтруктураВнешнихНаборовДанных);
	ИначеЕсли ИдентификаторМакета = "РасшифровкаВычетПриобретенныеЦенности" Тогда
		ПолучитьНаборДанныхВычетПоПриобретеннымЦенностям(Запрос, СтруктураВнешнихНаборовДанных);
	ИначеЕсли ИдентификаторМакета = "РасшифровкаВычетИсполнениеОНА" Тогда
		ПолучитьНаборДанныхВычетНалоговыйАгент(Запрос, СтруктураВнешнихНаборовДанных);
	ИначеЕсли ИдентификаторМакета = "РасшифровкаВычетСАвансовПолученныхУпрощенныйУчетНДС" Тогда
		ПолучитьНаборДанныхВычетСАвансовПолученныхУпрощенныйУчетНДС(Запрос, СтруктураВнешнихНаборовДанных);
	ИначеЕсли ИдентификаторМакета = "РасшифровкаВычетАвансыВыданныеУпрощенныйУчетНДС" Тогда
		ПолучитьНаборДанныхВычетАвансыВыданныеУпрощенныйУчетНДС(Запрос, СтруктураВнешнихНаборовДанных);
	ИначеЕсли ИдентификаторМакета = "РасшифровкаВычетПоПриобретеннымЦенностям0" Тогда
		ПолучитьНаборДанныхВычетПоПриобретеннымЦенностям0(Запрос, СтруктураВнешнихНаборовДанных);
	ИначеЕсли ИдентификаторМакета = "РасшифровкаВычетИсполнениеОНА0" Тогда
		ПолучитьНаборДанныхВычетНалоговыйАгент0(Запрос, СтруктураВнешнихНаборовДанных);
	ИначеЕсли ИдентификаторМакета = "РасшифровкаНачислениеВосстановлениеДругиеОперации" Тогда
		ПолучитьНаборДанныхНачислениеВосстановлениеДругиеОперации(Запрос, СтруктураВнешнихНаборовДанных);
	КонецЕсли;
	
	Если СтруктураВнешнихНаборовДанных.Количество() = 0 Тогда
		ВызватьИсключение НСтр("ru = 'Отчет не предназначен для интерактивного формирования. Воспользуйтесь отчетом ""Анализ состояния учета по НДС""';
								|en = 'The report cannot be generated interactively. Use the ""VAT accounting state analysis"" report'");
	КонецЕсли;
	
	Возврат СтруктураВнешнихНаборовДанных["ВнешнийНаборДанных" + СтрЗаменить(ПараметрыОтчета.ИдентификаторМакета,"Расшифровка", "")];

КонецФункции	

Функция ПолучитьТекстЗаголовка(ПараметрыОтчета, ОрганизацияВНачале = Истина) Экспорт 
	
	ТекстЗаголовок = ПараметрыОтчета.СхемаКомпоновкиДанных.ВариантыНастроек[ПараметрыОтчета.ИдентификаторМакета].Представление;
	
	// Переопределим заголовок расшифровки для декларации по НДС с 1 января 2015 года
	Если ПараметрыОтчета.ИдентификаторМакета = "РасшифровкаСтроки130Декларации" И ПараметрыОтчета.НачалоПериода >= '20150101' Тогда
		ТекстЗаголовок = СтрЗаменить(ТекстЗаголовок, "130", "120");
	КонецЕсли;
	
	Возврат ТекстЗаголовок + БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(ПараметрыОтчета.НачалоПериода, ПараметрыОтчета.КонецПериода);
	
КонецФункции

Процедура ПослеВыводаРезультата(ПараметрыОтчета, Результат) Экспорт
	
	БухгалтерскиеОтчетыВызовСервера.ОбработкаРезультатаОтчета(ПараметрыОтчета.ИдентификаторОтчета, Результат);

	Если Результат.Области.Найти("Заголовок") <> Неопределено Тогда
		Результат.ФиксацияСверху = Результат.Области.Заголовок.Низ;
	КонецЕсли;
	Результат.ФиксацияСлева = 0;	
КонецПроцедуры

// В процедуре можно доработать компоновщик перед выводом в отчет
// Изменения сохранены не будут.
Процедура ПередКомпоновкойМакета(ПараметрыОтчета, Схема, КомпоновщикНастроек) Экспорт
	
	// Получим предопределенные настройки отчета
	ОсновнаяСхемаКомпоновкиДанных = ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	ПредопределенныеНастройки = ОсновнаяСхемаКомпоновкиДанных.ВариантыНастроек[ПараметрыОтчета.ИдентификаторМакета].Настройки;
	
	// Очищаем структуру отчета
	КомпоновщикНастроек.Настройки.Структура.Очистить();
	КомпоновщикНастроек.Настройки.Выбор.Элементы.Очистить();
	
	// Найдем элемент офрмления Автоотступ
	УсловноеОформлениеАвтоотступа = Неопределено;
	Для каждого ЭлементОформления Из КомпоновщикНастроек.Настройки.УсловноеОформление.Элементы Цикл
		Если ЭлементОформления.Представление = "Уменьшенный автоотступ" Тогда
			УсловноеОформлениеАвтоотступа = ЭлементОформления;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	// Если автоотступ не задан добавляем его
	// если задан очистим элеметы к которым он применяется
	// дальше при добавлениии группировок элементы буду добавлены.
	Если УсловноеОформлениеАвтоотступа = Неопределено Тогда
		УсловноеОформлениеАвтоотступа = КомпоновщикНастроек.Настройки.УсловноеОформление.Элементы.Добавить();
		УсловноеОформлениеАвтоотступа.Представление = "Уменьшенный автоотступ";
		УсловноеОформлениеАвтоотступа.Оформление.УстановитьЗначениеПараметра("Автоотступ", 1);
		УсловноеОформлениеАвтоотступа.Использование = Ложь;
		УсловноеОформлениеАвтоотступа.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ;
	Иначе
		УсловноеОформлениеАвтоотступа.Поля.Элементы.Очистить();
	КонецЕсли;
	
	// В структуру отчета добавляем таблицу
	Таблица = КомпоновщикНастроек.Настройки.Структура.Добавить(Тип("ТаблицаКомпоновкиДанных"));	
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметрВывода(Таблица, "ВертикальноеРасположениеОбщихИтогов", РасположениеИтоговКомпоновкиДанных.Конец); 
	
	// В предопределенных настройках указаны ресурсы которые должны быть выведены в отчет
	// В доступных полях можно узнать является ли поле ресурсом, и проверить целесобразность вывода поля.
	ДоступныеПоля = КомпоновщикНастроек.Настройки.ДоступныеПоляВыбора;
	
	Для Каждого ВыбранноеПоле Из ПредопределенныеНастройки.Выбор.Элементы Цикл
		
		Если ВыбранноеПоле.Использование И ТипЗнч(ВыбранноеПоле) <> Тип("АвтоВыбранноеПолеКомпоновкиДанных") Тогда
			
			// Ищем выбранное поле среди доступных
			СвойстваПоля = ДоступныеПоля.НайтиПоле(ВыбранноеПоле.Поле);	
			
			// Если поле доступно и это ресурс, добавляем
			// поля не ресурсы задаются в таблице "Дополнительные данные".
			Если СвойстваПоля <> Неопределено И СвойстваПоля.Ресурс Тогда
				
				Колонка = Таблица.Колонки.Добавить();
				Колонка.Имя = ВыбранноеПоле.Поле; 
				БухгалтерскиеОтчетыКлиентСервер.ДобавитьВыбранноеПоле(Колонка.Выбор, ВыбранноеПоле.Поле);
				
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	// Для того чтобы имитировать обращение к объекту КоллекцияЭлементовСтруктурыТаблицыКомпоновкиДанных 
	// как к ГруппировкаТаблицыКомпоновкиДанных поместим коллекцию в структуру.
	Структура =  Новый Структура("Структура",Таблица.Строки);	
	
	// Перебираем таблицу группировок
	Для Каждого ПолеВыбраннойГруппировки Из ПараметрыОтчета.Группировка Цикл 
		Если ПолеВыбраннойГруппировки.Использование Тогда
			Структура = Структура.Структура.Добавить();
			ПолеГруппировки = Структура.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
			ПолеГруппировки.Использование  = Истина;
			ПолеГруппировки.Поле           = Новый ПолеКомпоновкиДанных(ПолеВыбраннойГруппировки.Поле);
			Если ПолеВыбраннойГруппировки.ТипГруппировки = 1 Тогда
				ПолеГруппировки.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.Иерархия;
			ИначеЕсли ПолеВыбраннойГруппировки.ТипГруппировки = 2 Тогда
				ПолеГруппировки.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.ТолькоИерархия;
			Иначе
				ПолеГруппировки.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.Элементы;
			КонецЕсли;
			
			Структура.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
			Структура.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
			
			ПолеОформления = УсловноеОформлениеАвтоотступа.Поля.Элементы.Добавить();
			ПолеОформления.Поле = ПолеГруппировки.Поле;
			
		КонецЕсли;
	КонецЦикла;
	
	// Добавляем предопределенные недоступные группировки (всегда в конце)
	ПредопределеннаяГруппировка = ПредопределенныеНастройки.Структура[0].Строки;
	
	Пока ПредопределеннаяГруппировка.Количество() > 0 Цикл
		
		Если ПредопределеннаяГруппировка[0].РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный Тогда
			
			Если ПредопределеннаяГруппировка[0].Состояние = СостояниеЭлементаНастройкиКомпоновкиДанных.Включен Тогда
				Структура = Структура.Структура.Добавить();
				
				// Поля группировок
				Для Каждого ПолеПредопределеннойГруппировки Из ПредопределеннаяГруппировка[0].ПоляГруппировки.Элементы Цикл
					Если ТипЗнч(ПолеПредопределеннойГруппировки) = Тип("АвтоПолеГруппировкиКомпоновкиДанных") Тогда
						ПолеГруппировки = Структура.ПоляГруппировки.Элементы.Добавить(Тип("АвтоПолеГруппировкиКомпоновкиДанных"));
					Иначе
						Если БухгалтерскиеОтчетыКлиентСервер.НайтиГруппировку(Таблица.Строки, ПолеПредопределеннойГруппировки.Поле) = Неопределено Тогда
							ПолеГруппировки = Структура.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
							ЗаполнитьЗначенияСвойств(ПолеГруппировки,ПолеПредопределеннойГруппировки);
							ПолеОформления = УсловноеОформлениеАвтоотступа.Поля.Элементы.Добавить();
							ПолеОформления.Поле = ПолеГруппировки.Поле;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;	
				
				//Отборы
				Для Каждого ОтборПредопределеннойГруппировки Из ПредопределеннаяГруппировка[0].Отбор.Элементы Цикл
					ОтборГруппировки =  Структура.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
					ЗаполнитьЗначенияСвойств(ОтборГруппировки,ОтборПредопределеннойГруппировки);
				КонецЦикла;	
				
				// Условное оформление
				Для Каждого УсловноеОформлениеПредопределеннойГруппировки Из ПредопределеннаяГруппировка[0].УсловноеОформление.Элементы Цикл
					УсловноеОформлениеГруппировки =  Структура.УсловноеОформление.Элементы.Добавить();
					ЗаполнитьЗначенияСвойств(УсловноеОформлениеГруппировки,УсловноеОформлениеПредопределеннойГруппировки);
					
					//Отбор
					Для Каждого ОтборПредопределенногоОформления Из УсловноеОформлениеПредопределеннойГруппировки.Отбор.Элементы Цикл
						ОтборОформления =  УсловноеОформлениеГруппировки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
						ЗаполнитьЗначенияСвойств(ОтборОформления,ОтборПредопределенногоОформления);
					КонецЦикла;	
					
					// Параметры оформления
					Для ИндексПараметра = 0 По УсловноеОформлениеПредопределеннойГруппировки.Оформление.Элементы.Количество() - 1 Цикл
						ОформлениеПредопределеннаяГруппировка = УсловноеОформлениеПредопределеннойГруппировки.Оформление.Элементы[ИндексПараметра];
						ОформлениеГруппировки =  УсловноеОформлениеГруппировки.Оформление.Элементы[ИндексПараметра];
						ЗаполнитьЗначенияСвойств(ОформлениеГруппировки,ОформлениеПредопределеннаяГруппировка);
					КонецЦикла;	
					
					Для Каждого ПредопределенноеОформляемоеПолеГруппировки Из УсловноеОформлениеПредопределеннойГруппировки.Поля.Элементы Цикл
						ОформляемоеПоле = УсловноеОформлениеГруппировки.Поля.Элементы.Добавить();
						ЗаполнитьЗначенияСвойств(ОформляемоеПоле,ПредопределенноеОформляемоеПолеГруппировки);
					КонецЦикла;	
					
				КонецЦикла;	
				
				//Выбор
				Для Каждого ВыборПредопределеннойГруппировки Из ПредопределеннаяГруппировка[0].Выбор.Элементы Цикл
					Если ТипЗнч(ВыборПредопределеннойГруппировки) = Тип("АвтоВыбранноеПолеКомпоновкиДанных") Тогда
						Структура.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));	
					Иначе
						Если БухгалтерскиеОтчетыКлиентСервер.НайтиГруппировку(Таблица.Строки, ВыборПредопределеннойГруппировки.Поле) = Неопределено Тогда
							ВыборГруппировки =  Структура.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
							ЗаполнитьЗначенияСвойств(ВыборГруппировки,ВыборПредопределеннойГруппировки);
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;	
				
				//Порядок
				Для Каждого ПорядокПредопределеннойГруппировки Из ПредопределеннаяГруппировка[0].Порядок.Элементы Цикл
					Если ТипЗнч(ПорядокПредопределеннойГруппировки) = Тип("АвтоЭлементПорядкаКомпоновкиДанных") Тогда
						Структура.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));	
					Иначе	
						ПорядокГруппировки =  Структура.Порядок.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
						ЗаполнитьЗначенияСвойств(ПорядокГруппировки,ПорядокПредопределеннойГруппировки);
					КонецЕсли;
				КонецЦикла;	
				
				// Параметры вывода
				Для ИндексПараметра = 0 По ПредопределеннаяГруппировка[0].ПараметрыВывода.Элементы.Количество() - 1 Цикл
					ПараметрыВыводаПредопределеннаяГруппировка = ПредопределеннаяГруппировка[0].ПараметрыВывода.Элементы[ИндексПараметра];
					ПараметрыВыводаГруппировки =  Структура.ПараметрыВывода.Элементы[ИндексПараметра];
					ЗаполнитьЗначенияСвойств(ПараметрыВыводаГруппировки,ПараметрыВыводаПредопределеннаяГруппировка);
				КонецЦикла;	
				
			КонецЕсли;
		КонецЕсли;
		
		ПредопределеннаяГруппировка = ПредопределеннаяГруппировка[0].Структура;
		
	КонецЦикла;
	
	// Дополнительные данные
	БухгалтерскиеОтчетыВызовСервера.ДобавитьДополнительныеПоля(ПараметрыОтчета, КомпоновщикНастроек);
	
	Если УсловноеОформлениеАвтоотступа.Поля.Элементы.Количество() = 0 Тогда
		УсловноеОформлениеАвтоотступа.Использование = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьНаборПоказателей() Экспорт
	
	НаборПоказателей = Новый Массив;
	Возврат НаборПоказателей;
	
КонецФункции	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Создает запрос, устанавливает параметры запроса 
//
//
// Возвращаемое значение:
//   Запрос   - заготовка запроса, с установленными параметрами.
//
Функция ПолучитьПараметрыЗапроса(ПараметрыОтчета) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ВидыЦенностейАвансыВсе = Новый Массив;
	ВидыЦенностейАвансыВсе.Добавить(Перечисления.ВидыЦенностей.АвансыВыданные);
	ВидыЦенностейАвансыВсе.Добавить(Перечисления.ВидыЦенностей.АвансыПолученные);
	ВидыЦенностейАвансыВсе.Добавить(Перечисления.ВидыЦенностей.АвансыПолученные0);
	ВидыЦенностейАвансыВсе.Добавить(Перечисления.ВидыЦенностей.ВозвратАвансовПолученных);
	
	ВидыЦенностейАвансыПолученные = Новый Массив;
	ВидыЦенностейАвансыПолученные.Добавить(Перечисления.ВидыЦенностей.АвансыПолученные);
	ВидыЦенностейАвансыПолученные.Добавить(Перечисления.ВидыЦенностей.АвансыПолученные0);
	ВидыЦенностейАвансыПолученные.Добавить(Перечисления.ВидыЦенностей.ВозвратАвансовПолученных);
	
	Если ПараметрыОтчета.ЭтоРасшифровкаДекларации Тогда
		ВидыЦенностейАвансыПолученные.Добавить(Перечисления.ВидыЦенностей.АвансыПолученныеНалоговыйАгент);
	КонецЕсли;
	
	ВидыЦенностейАвансыВыданные = Новый Массив;
	ВидыЦенностейАвансыВыданные.Добавить(Перечисления.ВидыЦенностей.АвансыВыданные);
	
	Если ПараметрыОтчета.ЭтоРасшифровкаДекларации Тогда
		ВидыЦенностейАвансыВыданные.Добавить(Перечисления.ВидыЦенностей.АвансыВыданныеНалоговыйАгент);
	КонецЕсли;
	
	ВидыЦенностейНалоговыйАгент = Перечисления.ВидыЦенностей.МассивВидовЦенностиНалоговыйАгент(Истина, Истина);
	НалоговыйАгентРеализацияТоваров = Перечисления.ВидыЦенностей.МассивВидовЦенностиНалоговыйАгентРеализацияТоваров();
	
	СписокВидовСубконто = Новый СписокЗначений;
	СписокВидовСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	СписокВидовСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры);
	
	СписокВидовСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами);
	СчетВыручкиЕНВД = ПланыСчетов.Хозрасчетный.ВыручкаЕНВД;
	
	ВидыСубконтоКонтрагентыДоговоры = Новый Массив;
	ВидыСубконтоКонтрагентыДоговоры.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	ВидыСубконтоКонтрагентыДоговоры.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры);
	
	ВедетсяУчетОбособленныхПодразделений = Истина;
	
	Если ВедетсяУчетОбособленныхПодразделений И ПараметрыОтчета.ВключатьОбособленныеПодразделения Тогда
		
		СписокОрганизаций = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьСписокОбособленныхПодразделений(ПараметрыОтчета.Организация);
		
	Иначе
		
		СписокОрганизаций = Новый СписокЗначений;
		СписокОрганизаций.Добавить(ПараметрыОтчета.Организация);
		
	КонецЕсли;	
	
	// С 01.10.2014 при реализации без НДС счета-фактуры не выставляются,	
	// кроме случая освобождения по ст.145 и 145.1.
	ПрименяетсяОсвобождениеОтУплатыНДС = УчетнаяПолитика.ПрименяетсяОсвобождениеОтУплатыНДС(
		ПараметрыОтчета.Организация, ПараметрыОтчета.КонецПериода);
		
	ОрганизацииНеНаЛьготномНалогообложении = УчетНДСРФ.ОрганизацииНеНаЛьготномНалогообложении(СписокОрганизаций, НачалоДня(ПараметрыОтчета.НачалоПериода));
	//	Установка параметров запроса
	
	//	Необходимо проверить остатки по регистру НДС начисленный после регламентных документов.
	Запрос.УстановитьПараметр("НачалоСледующегоПериода", 					КонецДня(ПараметрыОтчета.КонецПериода) + 1);
	Запрос.УстановитьПараметр("НачалоПериода",								НачалоДня(ПараметрыОтчета.НачалоПериода));
	Запрос.УстановитьПараметр("КонецПериодаГраница", 						Новый Граница(КонецДня(ПараметрыОтчета.КонецПериода), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("КонецПериода", 								КонецДня(ПараметрыОтчета.КонецПериода));	
	Запрос.УстановитьПараметр("Организация",								СписокОрганизаций); 
	Запрос.УстановитьПараметр("ОрганизацииНеНаЛьготномНалогообложении",		ОрганизацииНеНаЛьготномНалогообложении);
	Запрос.УстановитьПараметр("ПустаяСсылкаНаСчетФактуруВыданный", 			Документы.СчетФактураВыданный.ПустаяСсылка());
	Запрос.УстановитьПараметр("ВидыСубконто",								СписокВидовСубконто);
	Запрос.УстановитьПараметр("ВидыСубконтоКонтрагентыДоговоры",			ВидыСубконтоКонтрагентыДоговоры);
	Запрос.УстановитьПараметр("ВидыЦенностейНалоговыйАгент",				ВидыЦенностейНалоговыйАгент); 
	Запрос.УстановитьПараметр("НалоговыйАгентРеализацияТоваров",			НалоговыйАгентРеализацияТоваров);
	Запрос.УстановитьПараметр("ВидыЦенностейАвансыВсе", 					ВидыЦенностейАвансыВсе);
	Запрос.УстановитьПараметр("ВидыЦенностейАвансыПолученные",				ВидыЦенностейАвансыПолученные);
	Запрос.УстановитьПараметр("ВидыЦенностейАвансыВыданные",				ВидыЦенностейАвансыВыданные);
	Запрос.УстановитьПараметр("ВыручкаЕНВД",								СчетВыручкиЕНВД);
	Запрос.УстановитьПараметр("ПустаяНоменклатура", 						Справочники.Номенклатура.ПустаяСсылка());
	Запрос.УстановитьПараметр("ДатаНачалаДействияПостановления735", 		'2014-10-01'); // Постановление Правительства РФ от 30.07.2014 № 735
	Запрос.УстановитьПараметр("ПрименяетсяОсвобождениеОтУплатыНДС", 		ПрименяетсяОсвобождениеОтУплатыНДС); 
	Запрос.УстановитьПараметр("НоваяАрхитектураВзаиморасчетов", 			ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов")); 
	
	//	Тексты ошибок
	
	Запрос.УстановитьПараметр("ТекстОтраженыВКнигеПродаж", 	  НСтр("ru = 'Отражены в книге продаж';
																		|en = 'Recorded in the sales ledger'"));
	Запрос.УстановитьПараметр("ТекстНеОтраженыВКнигеПродаж",  НСтр("ru = 'Не отражены в книге продаж';
																	|en = 'Not recorded in the sales ledger'"));	
	Запрос.УстановитьПараметр("ТекстОтраженыВКнигеПокупок",   НСтр("ru = 'Отражены в книге покупок';
																	|en = 'Recorded in the purchase ledger'"));	
	Запрос.УстановитьПараметр("ТекстНеОтраженыВКнигеПокупок", НСтр("ru = 'Не отражены в книге покупок';
																	|en = 'Not recorded in the purchase ledger'"));	
	
	Запрос.УстановитьПараметр("ТекстБезОшибок", НСтр("ru = 'Без ошибок';
													|en = 'Without errors'"));
				
	Запрос.УстановитьПараметр("ТекстОшибкаРеализацияЕНВДСНДС", 
		НСтр("ru = 'Ошибка: Начислен НДС при реализации по деятельности, облагаемой ЕНВД';
			|en = 'Error: VAT is accrued while selling under the activity subject to VAT'"));
		
	Запрос.УстановитьПараметр("ТекстОшибкаРеализацияБезСФ", 
		НСтр("ru = 'Ошибка: Не выписан счет-фактура при реализации';
			|en = 'Error: Tax invoice is not issued on sale'"));
	
	Запрос.УстановитьПараметр("ТекстНДСНеУчитывается", 
		НСтр("ru = 'Не установлен флажок ""Учитывать НДС""';
			|en = 'Check box ""Record VAT"" is not selected'"));
	
	Запрос.УстановитьПараметр("ТекстНДСВключенВСтоимость", 
		НСтр("ru = 'НДС включен в стоимость';
			|en = 'VAT is included in the cost'"));
	
	Запрос.УстановитьПараметр("ТекстНачислениеНеОтраженоВКнигеПродаж", 
		НСтр("ru = 'Начисление не отражено в книге продаж';
			|en = 'Accrual is not recorded in the sales ledger'"));
	
	Запрос.УстановитьПараметр("ТекстНеОтраженВКнигеПродажАвансПоКомиссии",
		НСтр("ru = 'Счет-фактура на аванс комитента (не отражается книге продаж)';
			|en = 'Tax invoice for consignor advance (not recorded in the sales ledger)'"));
	
	Запрос.УстановитьПараметр("ТекстКорректировкаКнигиПродаж", 
		НСтр("ru = 'Произведена корректировка документов, уже отраженных в книге продаж';
			|en = 'Documents already recorded in the sales ledger are adjusted'"));
	
	Запрос.УстановитьПараметр("ТекстПолученныйАвансБезСФ",
		НСтр("ru = 'Не выписан счет-фактура на полученный аванс';
			|en = 'Tax invoice for the received advance is not issued'"));
		
	Запрос.УстановитьПараметр("ТекстПолученныйАвансПоСтавке0ИлиБезНДС",
		НСтр("ru = 'Авансы по ставке 0% или без НДС';
			|en = 'Advances at a rate of 0% or VAT-exempt'"));
		
	Запрос.УстановитьПараметр("ТекстСФНаАвансПоНепроведенномуПоручению", 
		НСтр("ru = 'Ошибка: Счет-фактура на аванс выписан по непроведенному платежному поручению';
			|en = 'Error: Tax invoice for advance is issued for the unposted payment order'"));	
		
	Запрос.УстановитьПараметр("ТекстСФНаПолученныйАвансНеПроведен", 
		НСтр("ru = 'Счет-фактура на полученный аванс выписан, но не проведен';
			|en = 'Tax invoice for received advance is issued but not posted'"));
		
	Запрос.УстановитьПараметр("ТекстСФНалоговогоАгентаНеВыписан", 
		НСтр("ru = 'Счет-фактура налогового агента не выписан';
			|en = 'Tax invoice of tax agent is not issued'"));
			
	Запрос.УстановитьПараметр("ТекстСФНалоговогоАгентаНеПроведен", 
		НСтр("ru = 'Счет-фактура налогового агента выписан, но не проведен';
			|en = 'Tax invoice of tax agent is issued but not posted'"));	
		
	Запрос.УстановитьПараметр("ТекстВыданныйАвансНеОтраженВКнигеПокупок", 
		НСтр("ru = 'НДС с выданного аванса не отражен в книге покупок';
			|en = 'VAT from the issued advance is not recorded in the purchase ledger'"));	
		
	Запрос.УстановитьПараметр("ТекстОплаченныеЦенностиНеПоступили", 
		НСтр("ru = 'Оплаченные ценности пока не поступили';
			|en = 'Paid assets have not been delivered yet'"));	
		
	Запрос.УстановитьПараметр("ТекстНДСПолностьюВосстановлен", 
		НСтр("ru = 'НДС полностью восстановлен';
			|en = 'VAT is fully restored'"));	
		
	Запрос.УстановитьПараметр("ТекстНДСВосстановленЧастично", 
		НСтр("ru = 'НДС восстановлен частично';
			|en = 'VAT is partially restored'"));	
				  		
	Запрос.УстановитьПараметр("ТекстОбъектНеПринятКУчету", 
		НСтр("ru = 'Объект ОС или НМА пока не принят к учету';
			|en = 'FA or IA object is not recognized yet'"));	
		
	Запрос.УстановитьПараметр("ТекстНеПодтвержденаСтавка", 
		НСтр("ru = 'Не подтверждена ставка 0%';
			|en = '0% rate is not confirmed'"));
		
	Запрос.УстановитьПараметр("ТекстНеПодтвержденаОплатаВБюджет", 
		НСтр("ru = 'Не подтверждена оплата НДС в бюджет';
			|en = 'VAT payment to budget is not confirmed'"));
		
	Запрос.УстановитьПараметр("ТекстНеПроизведенаОплатаПоставщику", 
		НСтр("ru = 'Не произведена оплата электронных услуг поставщику или не выполнены другие условия вычета';
			|en = 'Digital services are not paid to the supplier or other terms of deduction are not complied with'"));
		
	Запрос.УстановитьПараметр("ТекстНеПроизведенаОплатаВБюджет", 
		НСтр("ru = 'Не произведена оплата НДС в бюджет';
			|en = 'VAT payment to budget is not made'"));
		
	Запрос.УстановитьПараметр("ТекстОтсутствуетСФПолученный", 
		НСтр("ru = 'Отсутствует счет-фактура полученный';
			|en = 'No tax invoice received'"));	
		
	Запрос.УстановитьПараметр("ТекстСФПолученныйНеПроведен", 
		НСтр("ru = 'Счет-фактура полученный не проведен';
			|en = 'Received tax invoice is not posted'"));	
		
	Запрос.УстановитьПараметр("ТекстВычетНДСЗаблокирован", 
		НСтр("ru = 'Установлена блокировка вычета НДС';
			|en = 'VAT deduction is blocked'"));	
		
	Запрос.УстановитьПараметр("ТекстВычетНеОтраженВКнигеПокупок", 
		НСтр("ru = 'Вычет не отражен в книге покупок';
			|en = 'The deduction is not recorded in the purchase ledger'"));	
		
	Запрос.УстановитьПараметр("ТекстКорректировкаКнигиПокупок", 
		НСтр("ru = 'Произведена корректировка документов, уже отраженных в книге покупок';
			|en = 'Documents already recorded in the purchase ledger are adjusted'"));
		
	Запрос.УстановитьПараметр("ТекстПоступлениеНеОплаченоПоставщику", 
		НСтр("ru = 'Поступление по договору с исполнением обязанностей налогового агента не оплачено поставщику';
			|en = 'Receipt under the contract with performance of tax agent duties is not paid to the supplier'"));	
		
	Запрос.УстановитьПараметр("ТекстНДСНеУплаченВБюджет", 
		НСтр("ru = 'НДС, начисленный при исполнении обязанностей налогового агента, не уплачен в бюджет';
			|en = 'VAT accrued while performing tax agent duties is not paid to the budget'"));	
		
	Запрос.УстановитьПараметр("ТекстПолученныйАвансНеОтраженВКнигеПродаж", 
		НСтр("ru = 'НДС с полученного аванса не отражен в книге продаж';
			|en = 'VAT from the received advance is not recorded in the sales ledger'"));	
		
	Запрос.УстановитьПараметр("ТекстОплаченныеЦенностиНеРеализованы", 
		НСтр("ru = 'Оплаченные ценности пока не реализованы';
			|en = 'Paid assets have not been sold yet'"));	
		
	Запрос.УстановитьПараметр("ТекстНДСПринятКВычетуПолностью", 
		НСтр("ru = 'НДС принят к вычету полностью';
			|en = 'VAT is fully accepted for deduction'"));	
		
	Запрос.УстановитьПараметр("ТекстНДСПринятКВычетуЧастично", 
		НСтр("ru = 'НДС принят к вычету частично';
			|en = 'VAT is partially accepted for deduction'"));	
		
	Запрос.УстановитьПараметр("ТекстВыданныйАвансБезСФ", 
		НСтр("ru = 'Отсутствует счет-фактура на выданный аванс';
			|en = 'No tax invoice for the issued advance'"));	
		
	Запрос.УстановитьПараметр("ТекстСФНаВыданныйАвансНеПроведен", 
		НСтр("ru = 'Счет-фактура на выданный аванс не проведен';
			|en = 'Tax invoice for issued advance is not posted'"));	
		
	Запрос.УстановитьПараметр("ТекстНДССписанНаРасходы", 
		НСтр("ru = 'НДС списан на расходы';
			|en = 'VAT is written off as expenses'"));	
		
	Запрос.УстановитьПараметр("ТекстВычетНеОтраженДоПодтвержденияСтавки0", 
		НСтр("ru = 'Вычет НДС не может быть отражен до подтверждения ставки 0%';
			|en = 'VAT deduction cannot be recorded till the rate of 0% is approved'"));
		
	Запрос.УстановитьПараметр("ТекстСноска", НСтр("ru = '*';
													|en = '*'"));
		
	Запрос.УстановитьПараметр("ТекстНетСноски", "");
	
	Запрос.УстановитьПараметр("ТекстОбъектНеПринятКУчетуОбъектСтроительства", 
		НСтр("ru = 'Объект строительства пока не принят к учету';
			|en = 'Construction object is not recognized yet'"));
	
	Запрос.УстановитьПараметр("НесколькоПлатежей",
		НСтр("ru = '<несколько платежей>';
			|en = '<several payments>'"));
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Хозрасчетный.Ссылка КАК Счет
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.Ссылка В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоАвансамПолученным), ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоАвансамПолученнымВал), ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоАвансамПолученнымУЕ))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Хозрасчетный.Ссылка КАК Счет
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.Ссылка В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоАвансамВыданным), ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоАвансамВыданнымВал), ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоАвансамВыданнымУЕ))";
	
	Результат = Запрос.ВыполнитьПакет();
	Запрос.УстановитьПараметр("СписокСчетовАвансовПолученных", Результат[0].Выгрузить().ВыгрузитьКолонку("Счет"));
	Запрос.УстановитьПараметр("СписокСчетовАвансовВыданных",   Результат[1].Выгрузить().ВыгрузитьКолонку("Счет"));
	
	
	Запрос.УстановитьПараметр("СобытияНеОтражаемыеВРазделеНачисленияПоРеализации", 
		Отчеты.АнализСостоянияНалоговогоУчетаПоНДС.СобытияНеОтражаемыеВРазделеРеализация());

	ВидыЦенностейНеОтражаемыеВРазделеНачисленияПоРеализации = Новый Массив();
	
	Если НЕ ПараметрыОтчета.ЭтоРасшифровкаДекларации Тогда
		
		ВидыЦенностейНеОтражаемыеВРазделеНачисленияПоРеализации.Добавить(Перечисления.ВидыЦенностей.СМРСобственнымиСилами);
	
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
			ВидыЦенностейНеОтражаемыеВРазделеНачисленияПоРеализации,
			ВидыЦенностейАвансыВсе);

		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
			ВидыЦенностейНеОтражаемыеВРазделеНачисленияПоРеализации,
			ВидыЦенностейНалоговыйАгент);
			
	КонецЕсли;
		
	Запрос.УстановитьПараметр("ВидыЦенностейНеОтражаемыеВРазделеНачисленияПоРеализации",
		ВидыЦенностейНеОтражаемыеВРазделеНачисленияПоРеализации);
		
	Возврат Запрос;
	
КонецФункции

// Выполняет запрос к базе данных для получения набора данных, необходимого для формирования расшифровки по
//	начисленному НДС по реализации в случае сложного учета НДС.
//
// Параметры
//  Запрос  - Запрос - запрос к базе данных
//  СтруктураВнешнихНаборов - структура внешних источников данных для расшифровки.
//
// Возвращаемое значение:
//   Структура   - структура параметров для формирования карты отчета.
//
Процедура ПолучитьНаборДанныхНачислениеРеализацияСложныйУчетНДС(Запрос, СтруктураВнешнихНаборов) 
	
	//	Документы включенные в книгу продаж (НДС по реализации)
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КнигаПродаж.СчетФактура КАК СчетФактура,
	|	ВЫБОР
	|		КОГДА КнигаПродаж.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ТоварыНалоговыйАгент)
	|				И КнигаПродаж.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС20)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС20_120)
	|		КОГДА КнигаПродаж.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ТоварыНалоговыйАгент)
	|				И КнигаПродаж.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18_118)
	|		КОГДА КнигаПродаж.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ТоварыНалоговыйАгент)
	|				И КнигаПродаж.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС7)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС7_107)
	|		КОГДА КнигаПродаж.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ТоварыНалоговыйАгент)
	|				И КнигаПродаж.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС5)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС5_105)
	|		ИНАЧЕ КнигаПродаж.СтавкаНДС
	|	КОНЕЦ КАК СтавкаНДС,
	|	КнигаПродаж.СуммаБезНДСОборот КАК Сумма,
	|	КнигаПродаж.НДСОборот КАК СуммаНДС,
	|	КнигаПродаж.Событие,
	|	КнигаПродаж.ВидЦенности,
	|	КнигаПродаж.Регистратор,
	|	КнигаПродаж.ЗаписьДополнительногоЛиста КАК ЗаписьДополнительногоЛиста,
	|	КнигаПродаж.ИсправленныйСчетФактура КАК ИсправленныйСчетФактура
	|ПОМЕСТИТЬ ВТСоставКнигиПродаж
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			&НачалоПериода,
	|			&КонецПериодаГраница,
	|			Запись,
	|			Организация В (&Организация)
	|				И НЕ ВидЦенности В (&ВидыЦенностейНеОтражаемыеВРазделеНачисленияПоРеализации)
	|				И НЕ Событие В (&СобытияНеОтражаемыеВРазделеНачисленияПоРеализации)
	|				И НЕ ЗаписьДополнительногоЛиста) КАК КнигаПродаж
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КнигаПродаж.СчетФактура,
	|	ВЫБОР
	|		КОГДА КнигаПродаж.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ТоварыНалоговыйАгент)
	|				И КнигаПродаж.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС20)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС20_120)
	|		КОГДА КнигаПродаж.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ТоварыНалоговыйАгент)
	|				И КнигаПродаж.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18_118)
	|		КОГДА КнигаПродаж.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ТоварыНалоговыйАгент)
	|				И КнигаПродаж.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС7)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС7_107)
	|		КОГДА КнигаПродаж.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ТоварыНалоговыйАгент)
	|				И КнигаПродаж.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС5)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС5_105)
	|		ИНАЧЕ КнигаПродаж.СтавкаНДС
	|	КОНЕЦ КАК СтавкаНДС,
	|	КнигаПродаж.СуммаБезНДСОборот,
	|	КнигаПродаж.НДСОборот,
	|	КнигаПродаж.Событие,
	|	КнигаПродаж.ВидЦенности,
	|	КнигаПродаж.Регистратор,
	|	КнигаПродаж.ЗаписьДополнительногоЛиста,
	|	КнигаПродаж.ИсправленныйСчетФактура
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			&НачалоПериода,
	|			,
	|			Запись,
	|			Организация В (&Организация)
	|				И НЕ ВидЦенности В (&ВидыЦенностейНеОтражаемыеВРазделеНачисленияПоРеализации)
	|				И НЕ Событие В (&СобытияНеОтражаемыеВРазделеНачисленияПоРеализации)
	|				И (ЗаписьДополнительногоЛиста
	|					И (КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода))) КАК КнигаПродаж
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСНачисленныйОбороты.СчетФактура КАК ДокументРегистратор,
	|	НДСНачисленныйОбороты.СтавкаНДС,
	|	НДСНачисленныйОбороты.ВидЦенности,
	|	2 КАК РангОшибки
	|ПОМЕСТИТЬ ВТОперацииСОшибками
	|ИЗ
	|	ВТСоставКнигиПродаж КАК НДСНачисленныйОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		ПО (НДСНачисленныйОбороты.СчетФактура = СчетФактураВыданныйДокументыОснования.ДокументОснование
	|				ИЛИ НДСНачисленныйОбороты.СчетФактура = СчетФактураВыданныйДокументыОснования.Ссылка)
	|			И СчетФактураВыданныйДокументыОснования.Ссылка.Проведен
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураКомиссионеру КАК СчетФактураКомиссионеру
	|		ПО (НДСНачисленныйОбороты.СчетФактура = СчетФактураКомиссионеру.ДокументОснование
	|				ИЛИ НДСНачисленныйОбороты.СчетФактура = СчетФактураКомиссионеру.Ссылка)
	|			И СчетФактураКомиссионеру.Ссылка.Проведен
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученныйНалоговыйАгент КАК СчетФактураПолученныйНалоговыйАгент
	|		ПО (НДСНачисленныйОбороты.СчетФактура = СчетФактураПолученныйНалоговыйАгент.Ссылка)
	|			И СчетФактураПолученныйНалоговыйАгент.Ссылка.Проведен
	|	
	|ГДЕ
	|	ЕСТЬNULL(СчетФактураВыданныйДокументыОснования.Ссылка,
	|		ЕСТЬNULL(СчетФактураКомиссионеру.Ссылка, СчетФактураПолученныйНалоговыйАгент.Ссылка)) ЕСТЬ NULL
	|	И НЕ НДСНачисленныйОбороты.СчетФактура ССЫЛКА Документ.СводнаяСправкаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТОперацииСОшибками.ДокументРегистратор,
	|	СУММА(ВЫБОР
	|			КОГДА ВТОперацииСОшибками.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоСтрокБезНДС,
	|	КОЛИЧЕСТВО(*) КАК ВсегоСтрок
	|ПОМЕСТИТЬ ВТ_ОперацииПоСтавкеБезНДС
	|ИЗ
	|	ВТОперацииСОшибками КАК ВТОперацииСОшибками
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТОперацииСОшибками.ДокументРегистратор
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВТОперацииСОшибками.ДокументРегистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТСоставКнигиПродаж.СчетФактура КАК СчетФактура,
	|	ВТСоставКнигиПродаж.СтавкаНДС,
	|	ВТОперацииСОшибками.ДокументРегистратор,
	|	СУММА(ВТСоставКнигиПродаж.Сумма) КАК Сумма,
	|	СУММА(ВТСоставКнигиПродаж.СуммаНДС) КАК СуммаНДС,
	|	МИНИМУМ(ВЫБОР
	|			КОГДА ВТОперацииСОшибками.РангОшибки <> 2
	|				ТОГДА ВТОперацииСОшибками.РангОшибки
	|			КОГДА &ПрименяетсяОсвобождениеОтУплатыНДС
	|				ТОГДА ВТОперацииСОшибками.РангОшибки
	|			КОГДА ВТ_ОперацииПоСтавкеБезНДС.ВсегоСтрок = ВТ_ОперацииПоСтавкеБезНДС.КоличествоСтрокБезНДС
	|				ТОГДА NULL
	|			ИНАЧЕ ВТОперацииСОшибками.РангОшибки
	|		КОНЕЦ) КАК РангОшибки,
	|	ВТСоставКнигиПродаж.Событие,
	|	ВТСоставКнигиПродаж.ВидЦенности,
	|	ВТСоставКнигиПродаж.Регистратор,
	|	ВТСоставКнигиПродаж.ЗаписьДополнительногоЛиста,
	|	ВТСоставКнигиПродаж.ИсправленныйСчетФактура
	|ПОМЕСТИТЬ ВТПроведеноРанжированиеОшибок
	|ИЗ
	|	ВТСоставКнигиПродаж КАК ВТСоставКнигиПродаж
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОперацииСОшибками КАК ВТОперацииСОшибками
	|		ПО ВТСоставКнигиПродаж.СчетФактура = ВТОперацииСОшибками.ДокументРегистратор
	|			И ВТСоставКнигиПродаж.СтавкаНДС = ВТОперацииСОшибками.СтавкаНДС
	|			И ВТСоставКнигиПродаж.ВидЦенности = ВТОперацииСОшибками.ВидЦенности
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОперацииПоСтавкеБезНДС КАК ВТ_ОперацииПоСтавкеБезНДС
	|		ПО ВТСоставКнигиПродаж.СчетФактура = ВТ_ОперацииПоСтавкеБезНДС.ДокументРегистратор
	|	
	|СГРУППИРОВАТЬ ПО
	|	ВТСоставКнигиПродаж.СчетФактура,
	|	ВТСоставКнигиПродаж.СтавкаНДС,
	|	ВТОперацииСОшибками.ДокументРегистратор,
	|	ВТСоставКнигиПродаж.Событие,
	|	ВТСоставКнигиПродаж.ВидЦенности,
	|	ВТСоставКнигиПродаж.Регистратор,
	|	ВТСоставКнигиПродаж.ЗаписьДополнительногоЛиста,
	|	ВТСоставКнигиПродаж.ИсправленныйСчетФактура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетФактураВыданныйДокументыОснования.ДокументОснование КАК ДокументОснование,
	|	СчетФактураВыданныйДокументыОснования.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТСчетФактура
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|ГДЕ
	|	СчетФактураВыданныйДокументыОснования.Ссылка.Организация В(&Организация)
	|	И СчетФактураВыданныйДокументыОснования.Ссылка.Проведен
	|	И НЕ СчетФактураВыданныйДокументыОснования.Ссылка.Перевыставленный
	|
	|СГРУППИРОВАТЬ ПО
	|	СчетФактураВыданныйДокументыОснования.ДокументОснование,
	|	СчетФактураВыданныйДокументыОснования.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СчетФактураВыданный.ДокументОснование,
	|	СчетФактураВыданный.Ссылка
	|ИЗ
	|	Документ.СчетФактураКомиссионеру КАК СчетФактураВыданный
	|ГДЕ
	|	СчетФактураВыданный.Ссылка.Организация В(&Организация)
	|	И СчетФактураВыданный.Ссылка.Проведен
	|
	|СГРУППИРОВАТЬ ПО
	|	СчетФактураВыданный.ДокументОснование,
	|	СчетФактураВыданный.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЕСТЬNULL(СчетаФактураИсправления.Ссылка, СчетаФактуры.Ссылка) КАК СчетФактура,
	|	ВТПроведеноРанжированиеОшибок.СтавкаНДС,
	|	ВТПроведеноРанжированиеОшибок.СчетФактура КАК ДокументРегистратор,
	|	ВТПроведеноРанжированиеОшибок.Сумма,
	|	ВТПроведеноРанжированиеОшибок.СуммаНДС,
	|	1 КАК КоличествоДокументов,
	|	ВЫБОР
	|		КОГДА ВТПроведеноРанжированиеОшибок.РангОшибки ЕСТЬ NULL 
	|			ТОГДА &ТекстБезОшибок
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ВТПроведеноРанжированиеОшибок.РангОшибки = 1
	|					ТОГДА &ТекстОшибкаРеализацияЕНВДСНДС
	|				КОГДА ВТПроведеноРанжированиеОшибок.РангОшибки = 2
	|					ТОГДА &ТекстОшибкаРеализацияБезСФ
	|			КОНЕЦ
	|	КОНЕЦ КАК ПризнакНаличияОшибок,
	|	ВЫБОР
	|		КОГДА ВТПроведеноРанжированиеОшибок.РангОшибки = 0
	|			ТОГДА &ТекстСноска
	|		ИНАЧЕ &ТекстНетСноски
	|	КОНЕЦ КАК СимволСноски,
	|	&ТекстОтраженыВКнигеПродаж КАК ПризнакОтраженияНДС,
	|	ВЫБОР
	|		КОГДА ВТПроведеноРанжированиеОшибок.РангОшибки ЕСТЬ NULL 
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК КоличествоОшибок,
	|	ВТПроведеноРанжированиеОшибок.Событие,
	|	ВТПроведеноРанжированиеОшибок.ВидЦенности,
	|	ВТПроведеноРанжированиеОшибок.ЗаписьДополнительногоЛиста
	|ИЗ
	|	ВТПроведеноРанжированиеОшибок КАК ВТПроведеноРанжированиеОшибок
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСчетФактура КАК СчетаФактуры
	|		ПО (ВТПроведеноРанжированиеОшибок.СчетФактура = СчетаФактуры.ДокументОснование
	|				ИЛИ ВТПроведеноРанжированиеОшибок.СчетФактура = СчетаФактуры.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСчетФактура КАК СчетаФактураИсправления
	|		ПО (ВТПроведеноРанжированиеОшибок.ИсправленныйСчетФактура = СчетаФактураИсправления.ДокументОснование
	|				ИЛИ ВТПроведеноРанжированиеОшибок.ИсправленныйСчетФактура = СчетаФактураИсправления.Ссылка)
	|";
	
	НачисленияПоРеализацииВключенные = Запрос.Выполнить();
					
	//	Документы не включенные в книгу продаж
	НачисленияПоРеализацииНеВключенныеНДСНеОтражен = Новый ТаблицаЗначений;
	
	ВнешнийНаборДанныхНачислениеРеализация = Новый Структура;
	ВнешнийНаборДанныхНачислениеРеализация.Вставить("НачисленияПоРеализацииВключенные",	
													НачисленияПоРеализацииВключенные);
	ВнешнийНаборДанныхНачислениеРеализация.Вставить("НачисленияПоРеализацииНеВключенныеНДСНеОтражен",	
													НачисленияПоРеализацииНеВключенныеНДСНеОтражен);
					
	СтруктураВнешнихНаборов.Вставить("ВнешнийНаборДанныхНачислениеРеализацияСложныйУчетНДС", ВнешнийНаборДанныхНачислениеРеализация);
		
КонецПроцедуры // ПолучитьНаборДанныхНачислениеРеализацияСложныйУчетНДС()

// Выполняет запрос к базе данных для получения набора данных, необходимого для формирования расшифровки по
//	начисленному НДС с полученных авансов в случае ведения упрощенного учета НДС.
//
// Параметры
//  Запрос  - Запрос - запрос к базе данных
//  СтруктураВнешнихНаборов - структура внешних источников данных для расшифровки.
//
// Возвращаемое значение:
//   Структура   - структура параметров для формирования карты отчета.
//
Процедура ПолучитьНаборДанныхНачислениеАвансыПолученныеУпрощенныйУчетНДС(Запрос, СтруктураВнешнихНаборов)
	
	ПараметрыРасчетаАвансов = УчетНДСУПСлужебный.ИнициализироватьПараметрыПодготовкиРасчетовАвансовВЦеляхНДС();
	
	ПараметрыРасчетаАвансов.ДатаНачала    = Запрос.Параметры.НачалоПериода;
	ПараметрыРасчетаАвансов.ДатаОкончания = Запрос.Параметры.КонецПериода;
	ПараметрыРасчетаАвансов.Организации   = Запрос.Параметры.Организация;
	
	УчетНДСУПСлужебный.ПодготовитьВТ_АвансыПолученные(
			Запрос.МенеджерВременныхТаблиц,
			ПараметрыРасчетаАвансов);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СоставКнигиПродаж.СчетФактура КАК СчетФактура,
	|	СоставКнигиПродаж.СтавкаНДС,
	|	СоставКнигиПродаж.СуммаБезНДСОборот,
	|	СоставКнигиПродаж.НДСОборот,
	|	СоставКнигиПродаж.ВидЦенности,
	|	СоставКнигиПродаж.Событие,
	|	СоставКнигиПродаж.Регистратор,
	|	СоставКнигиПродаж.ЗаписьДополнительногоЛиста
	|ПОМЕСТИТЬ ВТСоставКнигиПродаж
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			&НачалоПериода,
	|			&КонецПериодаГраница,
	|			Авто,
	|			Организация В (&Организация)
	|				И ВидЦенности В (&ВидыЦенностейАвансыПолученные)
	|				И НЕ Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.НалогИсчисляетПокупатель)
	|				И НЕ ЗаписьДополнительногоЛиста) КАК СоставКнигиПродаж
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СоставКнигиПродаж.СчетФактура,
	|	СоставКнигиПродаж.СтавкаНДС,
	|	СоставКнигиПродаж.СуммаБезНДСОборот,
	|	СоставКнигиПродаж.НДСОборот,
	|	СоставКнигиПродаж.ВидЦенности,
	|	СоставКнигиПродаж.Событие,
	|	СоставКнигиПродаж.Регистратор,
	|	СоставКнигиПродаж.ЗаписьДополнительногоЛиста
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			&НачалоПериода,
	|			,
	|			Авто,
	|			Организация В (&Организация)
	|				И ВидЦенности В (&ВидыЦенностейАвансыПолученные)
	|				И НЕ Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.НалогИсчисляетПокупатель)
	|				И ЗаписьДополнительногоЛиста
	|				И (КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода)) КАК СоставКнигиПродаж
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Расчеты.Договор           КАК ДоговорКонтрагента,
	|	Расчеты.РасчетныйДокумент КАК СчетФактура,
	|	ВЫБОР
	|		КОГДА Расчеты.Договор.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)
	|			ТОГДА &СтавкаНДСПоУмолчанию
	|		КОГДА НЕ Расчеты.Договор.СтавкаНДС.РасчетнаяСтавка
	|			ТОГДА Расчеты.Договор.СтавкаНДС.СоответствующаяРасчетнаяСтавка
	|		ИНАЧЕ
	|			Расчеты.Договор.СтавкаНДС
	|	КОНЕЦ КАК СтавкаНДСПредположительная,
	|	СУММА(Расчеты.Сумма) КАК Сумма,
	|	СУММА(ВЫБОР
	|		КОГДА Расчеты.Договор.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)
	|			ТОГДА (ВЫРАЗИТЬ(Расчеты.Сумма - Расчеты.Сумма * &ПроцентНДС / (100 + &ПроцентНДС) КАК ЧИСЛО(31,2)))
	|		КОГДА Расчеты.Договор.СтавкаНДС.Ставка > 0
	|			ТОГДА (ВЫРАЗИТЬ(Расчеты.Сумма - Расчеты.Сумма * Расчеты.Договор.СтавкаНДС.Ставка / (100 + Расчеты.Договор.СтавкаНДС.Ставка) КАК ЧИСЛО(31, 2)))
	|		ИНАЧЕ ВЫРАЗИТЬ(Расчеты.Сумма КАК ЧИСЛО(31,2))
	|	КОНЕЦ) КАК СуммаБезНДС,
	|
	|	СУММА(ВЫБОР
	|		КОГДА Расчеты.Договор.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)
	|			ТОГДА (ВЫРАЗИТЬ(Расчеты.Сумма * &ПроцентНДС / (100 + &ПроцентНДС) КАК ЧИСЛО(31,2)))
	|		КОГДА Расчеты.Договор.СтавкаНДС.Ставка > 0
	|			ТОГДА (ВЫРАЗИТЬ(Расчеты.Сумма * Расчеты.Договор.СтавкаНДС.Ставка / (100 + Расчеты.Договор.СтавкаНДС.Ставка) КАК ЧИСЛО(31, 2)))
	|		ИНАЧЕ (ВЫРАЗИТЬ(Расчеты.Сумма КАК ЧИСЛО(31, 2)))
	|	КОНЕЦ) КАК СуммаНДС
	|
	|ПОМЕСТИТЬ ВТХозрасчетныйВсеОстаткиАвансыПолученные
	|ИЗ
	|	БазаНДС_АвансыПолученные КАК Расчеты
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеРасчетныхДокументов
	|		ПО Расчеты.РасчетныйДокумент = ДанныеРасчетныхДокументов.Документ
	|			И Расчеты.Организация    = ДанныеРасчетныхДокументов.Организация
	|ГДЕ
	|	Расчеты.ДатаПогашения = ДАТАВРЕМЯ(1,1,1)
	|
	|СГРУППИРОВАТЬ ПО
	|	Расчеты.Договор,
	|	Расчеты.РасчетныйДокумент,
	|	ДанныеРасчетныхДокументов.Дата,
	|	ВЫБОР
	|		КОГДА Расчеты.Договор.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)
	|			ТОГДА &СтавкаНДСПоУмолчанию
	|		КОГДА НЕ Расчеты.Договор.СтавкаНДС.РасчетнаяСтавка
	|			ТОГДА Расчеты.Договор.СтавкаНДС.СоответствующаяРасчетнаяСтавка
	|		ИНАЧЕ
	|			Расчеты.Договор.СтавкаНДС
	|	КОНЕЦ
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Платежи.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА Платежи.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)
	|			ТОГДА &СтавкаНДСПоУмолчанию
	|		КОГДА НЕ Платежи.СтавкаНДС.РасчетнаяСтавка
	|			ТОГДА Платежи.СтавкаНДС.СоответствующаяРасчетнаяСтавка
	|		ИНАЧЕ
	|			Платежи.СтавкаНДС
	|	КОНЕЦ КАК СтавкаНДС,
	|	СУММА(Платежи.Сумма) КАК Сумма,
	|	СУММА(Платежи.СуммаНДС) КАК СуммаНДС,
	|	СУММА(Платежи.Сумма - Платежи.СуммаНДС) КАК СуммаБезНДС
	|ПОМЕСТИТЬ СуммыПлатежей
	|ИЗ
	|	Документ.ПоступлениеБезналичныхДенежныхСредств.РасшифровкаПлатежа КАК Платежи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеБезналичныхДенежныхСредств КАК ДанныеРасчетныхДокументов
	|		ПО Платежи.Ссылка = ДанныеРасчетныхДокументов.Ссылка
	|ГДЕ
	|	Платежи.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТХозрасчетныйВсеОстаткиАвансыПолученные.СчетФактура
	|			ИЗ
	|				ВТХозрасчетныйВсеОстаткиАвансыПолученные)
	|
	|СГРУППИРОВАТЬ ПО
	|	Платежи.Ссылка,
	|	Платежи.СтавкаНДС,
	|	ДанныеРасчетныхДокументов.Дата
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Платежи.Ссылка,
	|	ВЫБОР
	|		КОГДА Платежи.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)
	|			ТОГДА &СтавкаНДСПоУмолчанию
	|		КОГДА НЕ Платежи.СтавкаНДС.РасчетнаяСтавка
	|			ТОГДА Платежи.СтавкаНДС.СоответствующаяРасчетнаяСтавка
	|		ИНАЧЕ
	|			Платежи.СтавкаНДС
	|	КОНЕЦ КАК СтавкаНДС,
	|	СУММА(Платежи.Сумма) КАК Сумма,
	|	СУММА(Платежи.СуммаНДС) КАК СуммаНДС,
	|	СУММА(Платежи.Сумма - Платежи.СуммаНДС) КАК СуммаБезНДС
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер.РасшифровкаПлатежа КАК Платежи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриходныйКассовыйОрдер КАК ДанныеРасчетныхДокументов
	|		ПО Платежи.Ссылка = ДанныеРасчетныхДокументов.Ссылка
	|ГДЕ
	|	Платежи.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТХозрасчетныйВсеОстаткиАвансыПолученные.СчетФактура
	|			ИЗ
	|				ВТХозрасчетныйВсеОстаткиАвансыПолученные)
	|
	|СГРУППИРОВАТЬ ПО
	|	Платежи.Ссылка,
	|	Платежи.СтавкаНДС,
	|	ДанныеРасчетныхДокументов.Дата
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Платежи.Ссылка,
	|	ВЫБОР
	|		КОГДА Платежи.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)
	|			ТОГДА &СтавкаНДСПоУмолчанию
	|		КОГДА НЕ Платежи.СтавкаНДС.РасчетнаяСтавка
	|			ТОГДА Платежи.СтавкаНДС.СоответствующаяРасчетнаяСтавка
	|		ИНАЧЕ
	|			Платежи.СтавкаНДС
	|	КОНЕЦ КАК СтавкаНДС,
	|	СУММА(Платежи.Сумма) КАК Сумма,
	|	СУММА(Платежи.СуммаНДС) КАК СуммаНДС,
	|	СУММА(Платежи.Сумма - Платежи.СуммаНДС) КАК СуммаБезНДС
	|ИЗ
	|	Документ.ОперацияПоПлатежнойКарте.РасшифровкаПлатежа КАК Платежи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОперацияПоПлатежнойКарте КАК ДанныеРасчетныхДокументов
	|		ПО Платежи.Ссылка = ДанныеРасчетныхДокументов.Ссылка
	|ГДЕ
	|	Платежи.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТХозрасчетныйВсеОстаткиАвансыПолученные.СчетФактура
	|			ИЗ
	|				ВТХозрасчетныйВсеОстаткиАвансыПолученные)
	|
	|СГРУППИРОВАТЬ ПО
	|	Платежи.Ссылка,
	|	Платежи.СтавкаНДС,
	|	ДанныеРасчетныхДокументов.Дата
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(СуммыПлатежей.Сумма) КАК Сумма,
	|	СуммыПлатежей.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТОбщиеСуммыПлатежей
	|ИЗ
	|	СуммыПлатежей КАК СуммыПлатежей
	|
	|СГРУППИРОВАТЬ ПО
	|	СуммыПлатежей.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТАвансыПолученныеВременнаяТаблица.СчетФактура,
	|	ЕСТЬNULL(СуммыПлатежей.СтавкаНДС, ВТАвансыПолученныеВременнаяТаблица.СтавкаНДСПредположительная) КАК СтавкаНДС,
	|	ВЫРАЗИТЬ(ВЫБОР
	|		КОГДА СуммыПлатежей.СуммаБезНДС ЕСТЬ NULL 
	|			ТОГДА ВТАвансыПолученныеВременнаяТаблица.СуммаБезНДС
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ОбщиеСуммыПлатежей.Сумма > ВТАвансыПолученныеВременнаяТаблица.Сумма
	|					ТОГДА СуммыПлатежей.СуммаБезНДС * ВТАвансыПолученныеВременнаяТаблица.Сумма / ОбщиеСуммыПлатежей.Сумма
	|				ИНАЧЕ СуммыПлатежей.СуммаБезНДС
	|			КОНЕЦ
	|	КОНЕЦ КАК ЧИСЛО(31,2)) КАК СуммаБезНДС,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА СуммыПлатежей.СуммаНДС ЕСТЬ NULL 
	|				ТОГДА ВТАвансыПолученныеВременнаяТаблица.СуммаНДС
	|			ИНАЧЕ ВЫБОР
	|					КОГДА СуммыПлатежей.СтавкаНДС.Ставка = 0
	|						ТОГДА 0
	|					КОГДА ОбщиеСуммыПлатежей.Сумма > ВТАвансыПолученныеВременнаяТаблица.Сумма
	|						ТОГДА СуммыПлатежей.СуммаНДС * ВТАвансыПолученныеВременнаяТаблица.Сумма / ОбщиеСуммыПлатежей.Сумма
	|					ИНАЧЕ СуммыПлатежей.СуммаНДС
	|				КОНЕЦ
	|		КОНЕЦ КАК ЧИСЛО(31,2)) КАК СуммаНДС
	|ПОМЕСТИТЬ ВТАвансыПолученные
	|ИЗ
	|	ВТХозрасчетныйВсеОстаткиАвансыПолученные КАК ВТАвансыПолученныеВременнаяТаблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ СуммыПлатежей КАК СуммыПлатежей
	|		ПО ВТАвансыПолученныеВременнаяТаблица.СчетФактура = СуммыПлатежей.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОбщиеСуммыПлатежей КАК ОбщиеСуммыПлатежей
	|		ПО ВТАвансыПолученныеВременнаяТаблица.СчетФактура = ОбщиеСуммыПлатежей.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВТАвансыПолученныеВременнаяТаблица.СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АвансыПолученные.СчетФактура,
	|	АвансыПолученные.СтавкаНДС,
	|	АвансыПолученные.СуммаБезНДС - ЕСТЬNULL(ВТСоставКнигиПродаж.СуммаБезНДСОборот, 0) КАК СуммаБезНДС,
	|	АвансыПолученные.СуммаНДС - ЕСТЬNULL(ВТСоставКнигиПродаж.НДСОборот, 0) КАК СуммаНДС,
	|	ВТСоставКнигиПродаж.ВидЦенности,
	|	ВТСоставКнигиПродаж.Событие
	|ПОМЕСТИТЬ НевосстановленныеАвансы
	|ИЗ
	|	ВТАвансыПолученные КАК АвансыПолученные
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСоставКнигиПродаж КАК ВТСоставКнигиПродаж
	|		ПО АвансыПолученные.СчетФактура = ВТСоставКнигиПродаж.СчетФактура
	|			И АвансыПолученные.СтавкаНДС.ПеречислениеСтавкаНДС = ВТСоставКнигиПродаж.СтавкаНДС
	|ГДЕ
	|	АвансыПолученные.СуммаНДС - ЕСТЬNULL(ВТСоставКнигиПродаж.НДСОборот, 0) > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	АвансыПолученные.СчетФактура,
	|	АвансыПолученные.СтавкаНДС,
	|	АвансыПолученные.СуммаБезНДС - ЕСТЬNULL(ВТСоставКнигиПродаж.СуммаБезНДСОборот, 0),
	|	АвансыПолученные.СуммаНДС - ЕСТЬNULL(ВТСоставКнигиПродаж.НДСОборот, 0),
	|	ВТСоставКнигиПродаж.ВидЦенности,
	|	ВТСоставКнигиПродаж.Событие
	|ИЗ
	|	ВТАвансыПолученные КАК АвансыПолученные
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСоставКнигиПродаж КАК ВТСоставКнигиПродаж
	|		ПО АвансыПолученные.СчетФактура = ВТСоставКнигиПродаж.СчетФактура
	|			И АвансыПолученные.СтавкаНДС.ПеречислениеСтавкаНДС = ВТСоставКнигиПродаж.СтавкаНДС
	|ГДЕ
	|	АвансыПолученные.СуммаНДС = 0
	|	И (АвансыПолученные.СтавкаНДС <> ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)
	|		ИЛИ АвансыПолученные.СтавкаНДС.Ставка = 0)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АвансыПолученные.СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТХозрасчетныйВсеОстаткиАвансыПолученные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТь БазаНДС_АвансыПолученные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТОбщиеСуммыПлатежей
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетФактураВыданныйАванс.Проведен,
	|	СчетФактураВыданныйАванс.ДокументОснование.ПометкаУдаления,
	|	СчетФактураВыданныйАванс.Ссылка,
	|	СчетФактураВыданныйАванс.ДокументОснование КАК ДокументОснование,
	|	МИНИМУМ(ВЫБОР
	|			КОГДА СчетФактураВыданныйАвансАвансы.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК КомиссионныйТовар
	|ПОМЕСТИТЬ СчетФактураВыданныйДокументыОснования
	|ИЗ
	|	Документ.СчетФактураВыданныйАванс.Авансы КАК СчетФактураВыданныйАвансАвансы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданныйАванс КАК СчетФактураВыданныйАванс
	|		ПО СчетФактураВыданныйАвансАвансы.Ссылка = СчетФактураВыданныйАванс.Ссылка
	|ГДЕ
	|	СчетФактураВыданныйАванс.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И СчетФактураВыданныйАванс.Организация В(&Организация)
	|
	|СГРУППИРОВАТЬ ПО
	|	СчетФактураВыданныйАванс.Проведен,
	|	СчетФактураВыданныйАванс.ДокументОснование.ПометкаУдаления,
	|	СчетФактураВыданныйАванс.Ссылка,
	|	СчетФактураВыданныйАванс.ДокументОснование
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОстатки.СчетФактура КАК ДокументРегистратор,
	|	ХозрасчетныйОстатки.СуммаБезНДС КАК Сумма,
	|	ХозрасчетныйОстатки.СуммаНДС КАК СуммаНДС,
	|	ХозрасчетныйОстатки.СтавкаНДС КАК СтавкаНДС,
	|	СчетФактураВыданныйДокументыОснования.Ссылка КАК СчетФактура,
	|	ВЫБОР
	|		КОГДА СчетФактураВыданныйДокументыОснования.КомиссионныйТовар = ИСТИНА
	|			ТОГДА &ТекстНеОтраженВКнигеПродажАвансПоКомиссии
	|		ИНАЧЕ &ТекстНеОтраженыВКнигеПродаж
	|	КОНЕЦ КАК ПризнакОтраженияНДС,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОстатки.СтавкаНДС.Ставка = 0
	|				ИЛИ ХозрасчетныйОстатки.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)
	|			ТОГДА &ТекстПолученныйАвансПоСтавке0ИлиБезНДС
	|		КОГДА СчетФактураВыданныйДокументыОснования.Ссылка ЕСТЬ NULL 
	|			ТОГДА &ТекстПолученныйАвансБезСФ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА СчетФактураВыданныйДокументыОснования.КомиссионныйТовар = ИСТИНА
	|					ТОГДА &ТекстБезОшибок
	|				КОГДА СчетФактураВыданныйДокументыОснования.Проведен
	|					ТОГДА &ТекстНачислениеНеОтраженоВКнигеПродаж
	|				ИНАЧЕ &ТекстСФНаПолученныйАвансНеПроведен
	|			КОНЕЦ
	|	КОНЕЦ КАК ПризнакНаличияОшибок,
	|	0 КАК СуммаВключенные,
	|	ХозрасчетныйОстатки.СуммаНДС КАК СуммаНеВключенные,
	|	1 КАК КоличествоДокументов,
	|	ХозрасчетныйОстатки.ВидЦенности,
	|	ХозрасчетныйОстатки.Событие,
	|	ЛОЖЬ КАК ЗаписьДополнительногоЛиста,
	|	1 КАК НомерСтроки,
	|	ЕСТЬNULL(СчетФактураВыданныйДокументыОснования.ДокументОснованиеПометкаУдаления, ЛОЖЬ) КАК ДокументОснованиеПометкаУдаления
	|ПОМЕСТИТЬ ДанныеОтчета
	|ИЗ
	|	НевосстановленныеАвансы КАК ХозрасчетныйОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ СчетФактураВыданныйДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		ПО ХозрасчетныйОстатки.СчетФактура = СчетФактураВыданныйДокументыОснования.ДокументОснование
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТСоставКнигиПродаж.СчетФактура,
	|	ВТСоставКнигиПродаж.СуммаБезНДСОборот,
	|	ВТСоставКнигиПродаж.НДСОборот,
	|	ВТСоставКнигиПродаж.СтавкаНДС,
	|	СчетФактураВыданныйДокументыОснования.Ссылка,
	|	&ТекстОтраженыВКнигеПродаж,
	|	&ТекстБезОшибок,
	|	ВТСоставКнигиПродаж.НДСОборот,
	|	0,
	|	1,
	|	ВТСоставКнигиПродаж.ВидЦенности,
	|	ВТСоставКнигиПродаж.Событие,
	|	ВТСоставКнигиПродаж.ЗаписьДополнительногоЛиста,
	|	1,
	|	ЕСТЬNULL(СчетФактураВыданныйДокументыОснования.ДокументОснованиеПометкаУдаления, ЛОЖЬ)
	|ИЗ
	|	ВТСоставКнигиПродаж КАК ВТСоставКнигиПродаж
	|		ЛЕВОЕ СОЕДИНЕНИЕ СчетФактураВыданныйДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		ПО ВТСоставКнигиПродаж.СчетФактура = СчетФактураВыданныйДокументыОснования.ДокументОснование
	|			И ВТСоставКнигиПродаж.Регистратор = СчетФактураВыданныйДокументыОснования.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеОтчета.ДокументРегистратор,
	|	ДанныеОтчета.Сумма,
	|	ДанныеОтчета.СуммаНДС,
	|	ДанныеОтчета.СтавкаНДС,
	|	ДанныеОтчета.СчетФактура,
	|	ДанныеОтчета.ПризнакОтраженияНДС,
	|	ДанныеОтчета.ПризнакНаличияОшибок,
	|	ДанныеОтчета.СуммаВключенные,
	|	ДанныеОтчета.СуммаНеВключенные,
	|	ДанныеОтчета.КоличествоДокументов,
	|	ДанныеОтчета.ВидЦенности,
	|	ДанныеОтчета.Событие,
	|	ДанныеОтчета.ЗаписьДополнительногоЛиста,
	|	ДанныеОтчета.НомерСтроки
	|ИЗ
	|	ДанныеОтчета КАК ДанныеОтчета
	|ГДЕ
	|	НЕ ДанныеОтчета.ДокументОснованиеПометкаУдаления";
	
	ПодстановкаСтавкаНДС = УчетНДСРФ.ТекстВыбораСтавкиНДСПоУмолчанию(Запрос.Параметры, "ДанныеРасчетныхДокументов.Дата", Истина);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СтавкаНДСПоУмолчанию", ПодстановкаСтавкаНДС);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПроцентНДС", УчетНДСРФ.ТекстВыбораПроцентаНДС(ПодстановкаСтавкаНДС));
	
	Запрос.Текст = ТекстЗапроса;
	
	НачислениеАвансыПолученныеУпрощенныйУчетНДС = Запрос.Выполнить();
	
	ВнешнийНаборДанныхНачислениеАвансыПолученныеУпрощенныйУчетНДС = Новый Структура;
	
	ВнешнийНаборДанныхНачислениеАвансыПолученныеУпрощенныйУчетНДС.Вставить("НачислениеАвансыПолученныеУпрощенныйУчетНДС",
																		  	НачислениеАвансыПолученныеУпрощенныйУчетНДС);
	СтруктураВнешнихНаборов.Вставить("ВнешнийНаборДанныхНачислениеАвансыПолученныеУпрощенныйУчетНДС",
										ВнешнийНаборДанныхНачислениеАвансыПолученныеУпрощенныйУчетНДС);

КонецПроцедуры // ПолучитьНаборДанныхНачислениеАвансыПолученныеУпрощенныйУчетНДС()

// Выполняет запрос к базе данных для получения набора данных, необходимого для формирования расшифровки по
//	начисленному НДС при исполнении обязанностей налогового агента.
//
// Параметры
//  Запрос  - Запрос - запрос к базе данных
//  СтруктураВнешнихНаборов - структура внешних источников данных для расшифровки.
//
// Возвращаемое значение:
//   Структура   - структура параметров для формирования карты отчета.
//
Процедура ПолучитьНаборДанныхНачислениеНалоговыйАгент(Запрос, СтруктураВнешнихНаборов) 
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСЗаписиКнигиПродажОбороты.СчетФактура КАК СчетФактура,
	|	НДСЗаписиКнигиПродажОбороты.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	НДСЗаписиКнигиПродажОбороты.Покупатель КАК Покупатель,
	|	НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот КАК СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот КАК НДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.Событие,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПродажОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПродажОбороты.НомерСтроки,
	|	НДСЗаписиКнигиПродажОбороты.СтавкаНДС
	|ПОМЕСТИТЬ ВТНДСЗаписиКнигиПродажОбороты
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			&НачалоПериода,
	|			&КонецПериодаГраница,
	|			Запись,
	|			Организация В (&Организация)
	|				И ВидЦенности В (&ВидыЦенностейНалоговыйАгент)
	|				И НЕ ЗаписьДополнительногоЛиста) КАК НДСЗаписиКнигиПродажОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПродажОбороты.СчетФактура,
	|	НДСЗаписиКнигиПродажОбороты.ДоговорКонтрагента,
	|	НДСЗаписиКнигиПродажОбороты.Покупатель,
	|	НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.Событие,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПродажОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПродажОбороты.НомерСтроки,
	|	НДСЗаписиКнигиПродажОбороты.СтавкаНДС
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			&НачалоПериода,
	|			,
	|			Запись,
	|			Организация В (&Организация)
	|				И ВидЦенности В (&ВидыЦенностейНалоговыйАгент)
	|				И (ЗаписьДополнительногоЛиста
	|					И (КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода))) КАК НДСЗаписиКнигиПродажОбороты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Расчеты.Период КАК Период,
	|	Расчеты.Регистратор КАК Регистратор,
	|	Расчеты.Регистратор КАК ДокументОснование,
	|	АналитикаУчетаПоПартнерамДанные.Контрагент КАК Контрагент,
	|	Расчеты.Валюта КАК Валюта,
	|	ВЫРАЗИТЬ(Расчеты.ОбъектРасчетов.Договор КАК Справочник.ДоговорыКонтрагентов) КАК Договор,
	|	СУММА(Расчеты.СуммаРегл) КАК СуммаОборот,
	|	ВЫРАЗИТЬ(СУММА(Расчеты.СуммаРегл * &ПроцентНДС /100) КАК ЧИСЛО(31,2)) КАК СуммаНДСОборот
	|ПОМЕСТИТЬ ОплатыПоставщику
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками КАК Расчеты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерамДанные
	|		ПО Расчеты.АналитикаУчетаПоПартнерам = АналитикаУчетаПоПартнерамДанные.КлючАналитики
	|			И (АналитикаУчетаПоПартнерамДанные.Организация В (&Организация))
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО Расчеты.ОбъектРасчетов.Договор = ДоговорыКонтрагентов.Ссылка
	|			И (ДоговорыКонтрагентов.УчетАгентскогоНДС)
	|ГДЕ
	|	Расчеты.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И (Расчеты.Регистратор ССЫЛКА Документ.СписаниеБезналичныхДенежныхСредств
	|			ИЛИ Расчеты.Регистратор ССЫЛКА Документ.РасходныйКассовыйОрдер)
	|	И Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И ЕСТЬNULL(ДоговорыКонтрагентов.УчетАгентскогоНДС, 
	|		Расчеты.ОбъектРасчетов.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.НалоговыйАгентПоНДС))
	|
	|СГРУППИРОВАТЬ ПО
	|	Расчеты.Период,
	|	Расчеты.Регистратор,
	|	ВЫРАЗИТЬ(Расчеты.ОбъектРасчетов.Договор КАК Справочник.ДоговорыКонтрагентов),
	|	АналитикаУчетаПоПартнерамДанные.Контрагент,
	|	Расчеты.Валюта
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетФактураВыданный.Ссылка КАК Ссылка,
	|	СчетФактураВыданный.Проведен КАК Проведен,
	|	ВЫБОР
	|		КОГДА СчетФактураВыданный.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ СчетФактураВыданный.Договор
	|	КОНЕЦ КАК ДоговорКонтрагента,
	|	СчетФактураВыданный.ДокументОснование КАК ДокументОснование
	|ПОМЕСТИТЬ ВТСчетФактураВыданный
	|ИЗ
	|	Документ.СчетФактураНалоговыйАгент КАК СчетФактураВыданный
	|ГДЕ
	|	СчетФактураВыданный.Организация В(&Организация)
	|	И СчетФактураВыданный.ПометкаУдаления = ЛОЖЬ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДоговорКонтрагента,
	|	ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОплатыПоставщику.ДокументОснование КАК ДокументОплаты,
	|	ОплатыПоставщику.Регистратор КАК ДокументРегистратор,
	|	ЕСТЬNULL(НДСЗаписиКнигиПродажОбороты.СтавкаНДС, &СтавкаНДСПоУмолчанию) КАК СтавкаНДС,
	|	СУММА(ЕСТЬNULL(ОплатыПоставщику.СуммаОборот, ЕСТЬNULL(НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот, 0))) КАК Сумма,
	|	СУММА(ЕСТЬNULL(ОплатыПоставщику.СуммаНДСОборот, ЕСТЬNULL(НДСЗаписиКнигиПродажОбороты.НДСОборот, 0))) КАК СуммаНДС,
	|	ЕСТЬNULL(СчетФактураВыданный.Ссылка, НЕОПРЕДЕЛЕНО) КАК СчетФактура,
	|	ИСТИНА КАК ЭтоСчетФактураВыданный,
	|	ВЫБОР
	|		КОГДА НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот ЕСТЬ NULL
	|			ТОГДА ВЫБОР
	|					КОГДА СчетФактураВыданный.Ссылка ЕСТЬ NULL
	|						ТОГДА &ТекстСФНалоговогоАгентаНеВыписан
	|					ИНАЧЕ ВЫБОР
	|							КОГДА СчетФактураВыданный.Проведен
	|								ТОГДА &ТекстНачислениеНеОтраженоВКнигеПродаж
	|							ИНАЧЕ &ТекстСФНалоговогоАгентаНеПроведен
	|						КОНЕЦ
	|				КОНЕЦ
	|		ИНАЧЕ &ТекстБезОшибок
	|	КОНЕЦ КАК ПризнакНаличияОшибок,
	|	ВЫБОР
	|		КОГДА НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот ЕСТЬ NULL
	|			ТОГДА &ТекстНеОтраженыВКнигеПродаж
	|		ИНАЧЕ &ТекстОтраженыВКнигеПродаж
	|	КОНЕЦ КАК ПризнакОтраженияНДС,
	|	СУММА(1) КАК КоличествоДокументов,
	|	СУММА(ВЫБОР
	|			КОГДА НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот ЕСТЬ NULL
	|				ТОГДА 0
	|			ИНАЧЕ НДСЗаписиКнигиПродажОбороты.НДСОборот
	|		КОНЕЦ) КАК СуммаВключенныхВБазу,
	|	СУММА(ВЫБОР
	|			КОГДА НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот ЕСТЬ NULL
	|				ТОГДА ОплатыПоставщику.СуммаНДСОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаНеВключенныхВБазу,
	|	ОплатыПоставщику.Контрагент КАК Контрагент,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПродажОбороты.Событие,
	|	ОплатыПоставщику.Договор КАК ДоговорКонтрагента,
	|	НДСЗаписиКнигиПродажОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПродажОбороты.НомерСтроки
	|ИЗ
	|	ОплатыПоставщику КАК ОплатыПоставщику
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСчетФактураВыданный КАК СчетФактураВыданный
	|		ПО ОплатыПоставщику.ДокументОснование = СчетФактураВыданный.ДокументОснование
	|			И ОплатыПоставщику.Договор = СчетФактураВыданный.ДоговорКонтрагента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНДСЗаписиКнигиПродажОбороты КАК НДСЗаписиКнигиПродажОбороты
	|		ПО ОплатыПоставщику.ДокументОснование = НДСЗаписиКнигиПродажОбороты.СчетФактура
	|			И ОплатыПоставщику.Договор = НДСЗаписиКнигиПродажОбороты.ДоговорКонтрагента
	|ГДЕ
	|	НЕ ЕСТЬNULL(НДСЗаписиКнигиПродажОбороты.ВидЦенности, ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)) В (&НалоговыйАгентРеализацияТоваров)
	|
	|СГРУППИРОВАТЬ ПО
	|	ОплатыПоставщику.ДокументОснование,
	|	ЕСТЬNULL(НДСЗаписиКнигиПродажОбороты.СтавкаНДС, &СтавкаНДСПоУмолчанию),
	|	ЕСТЬNULL(СчетФактураВыданный.Ссылка, НЕОПРЕДЕЛЕНО),
	|	НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот,
	|	ОплатыПоставщику.Контрагент,
	|	ОплатыПоставщику.Договор,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПродажОбороты.Событие,
	|	НДСЗаписиКнигиПродажОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПродажОбороты.НомерСтроки,
	|	ВЫБОР
	|		КОГДА НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот ЕСТЬ NULL
	|			ТОГДА ВЫБОР
	|					КОГДА СчетФактураВыданный.Ссылка ЕСТЬ NULL
	|						ТОГДА &ТекстСФНалоговогоАгентаНеВыписан
	|					ИНАЧЕ ВЫБОР
	|							КОГДА СчетФактураВыданный.Проведен
	|								ТОГДА &ТекстНачислениеНеОтраженоВКнигеПродаж
	|							ИНАЧЕ &ТекстСФНалоговогоАгентаНеПроведен
	|						КОНЕЦ
	|				КОНЕЦ
	|		ИНАЧЕ &ТекстБезОшибок
	|	КОНЕЦ,
	|	ОплатыПоставщику.Регистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СчетФактураВыданный.ДокументОснование КАК ДокументОплаты,
	|	НДСЗаписиКнигиПродажОбороты.СчетФактура КАК ДокументРегистратор,
	|	НДСЗаписиКнигиПродажОбороты.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот) КАК Сумма,
	|	СУММА(НДСЗаписиКнигиПродажОбороты.НДСОборот) КАК СуммаНДС,
	|	ЕСТЬNULL(СчетФактураВыданный.Ссылка, НЕОПРЕДЕЛЕНО) КАК СчетФактура,
	|	ИСТИНА КАК ЭтоСчетФактураВыданный,
	|	ВЫБОР
	|		КОГДА СчетФактураВыданный.Ссылка ЕСТЬ NULL
	|			ТОГДА &ТекстСФНалоговогоАгентаНеВыписан
	|		ИНАЧЕ &ТекстБезОшибок
	|	КОНЕЦ КАК ПризнакНаличияОшибок,
	|	ВЫБОР
	|		КОГДА НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот ЕСТЬ NULL
	|			ТОГДА &ТекстНеОтраженыВКнигеПродаж
	|		ИНАЧЕ &ТекстОтраженыВКнигеПродаж
	|	КОНЕЦ КАК ПризнакОтраженияНДС,
	|	СУММА(1) КАК КоличествоДокументов,
	|	СУММА(НДСЗаписиКнигиПродажОбороты.НДСОборот) КАК СуммаВключенныхВБазу,
	|	0 КАК СуммаНеВключенныхВБазу,
	|	НДСЗаписиКнигиПродажОбороты.Покупатель КАК Контрагент,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПродажОбороты.Событие,
	|	НДСЗаписиКнигиПродажОбороты.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	НДСЗаписиКнигиПродажОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПродажОбороты.НомерСтроки
	|ИЗ
	|	ВТНДСЗаписиКнигиПродажОбороты КАК НДСЗаписиКнигиПродажОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОплатыПоставщику КАК ОплатыПоставщику
	|		ПО ОплатыПоставщику.ДокументОснование = НДСЗаписиКнигиПродажОбороты.СчетФактура
	|			И ОплатыПоставщику.Договор = НДСЗаписиКнигиПродажОбороты.ДоговорКонтрагента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСчетФактураВыданный КАК СчетФактураВыданный
	|		ПО НДСЗаписиКнигиПродажОбороты.СчетФактура = СчетФактураВыданный.ДокументОснование
	|			И НДСЗаписиКнигиПродажОбороты.ДоговорКонтрагента = СчетФактураВыданный.ДоговорКонтрагента
	|ГДЕ
	|	НЕ НДСЗаписиКнигиПродажОбороты.ВидЦенности В (&НалоговыйАгентРеализацияТоваров)
	|	И ОплатыПоставщику.Договор ЕСТЬ NULL
	|
	|СГРУППИРОВАТЬ ПО
	|	СчетФактураВыданный.ДокументОснование,
	|	НДСЗаписиКнигиПродажОбороты.СтавкаНДС,
	|	ЕСТЬNULL(СчетФактураВыданный.Ссылка, НЕОПРЕДЕЛЕНО),
	|	НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.Покупатель,
	|	НДСЗаписиКнигиПродажОбороты.ДоговорКонтрагента,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПродажОбороты.Событие,
	|	НДСЗаписиКнигиПродажОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПродажОбороты.НомерСтроки,
	|	ВЫБОР
	|		КОГДА СчетФактураВыданный.Ссылка ЕСТЬ NULL
	|			ТОГДА &ТекстСФНалоговогоАгентаНеВыписан
	|		ИНАЧЕ &ТекстБезОшибок
	|	КОНЕЦ,
	|	НДСЗаписиКнигиПродажОбороты.СчетФактура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СчетФактураВыданный.ДокументОснование,
	|	СчетФактураВыданный.ДокументОснование,
	|	НДСЗаписиКнигиПродажОбороты.СтавкаНДС,
	|	СУММА(НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот),
	|	СУММА(НДСЗаписиКнигиПродажОбороты.НДСОборот),
	|	ЕСТЬNULL(СчетФактураВыданный.Ссылка, НЕОПРЕДЕЛЕНО),
	|	ИСТИНА,
	|	ВЫБОР
	|		КОГДА НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот ЕСТЬ NULL
	|			ТОГДА ВЫБОР
	|					КОГДА СчетФактураВыданный.Ссылка ЕСТЬ NULL
	|						ТОГДА &ТекстСФНалоговогоАгентаНеВыписан
	|					ИНАЧЕ ВЫБОР
	|							КОГДА СчетФактураВыданный.Проведен
	|								ТОГДА &ТекстНачислениеНеОтраженоВКнигеПродаж
	|							ИНАЧЕ &ТекстСФНалоговогоАгентаНеПроведен
	|						КОНЕЦ
	|				КОНЕЦ
	|		ИНАЧЕ &ТекстБезОшибок
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот ЕСТЬ NULL
	|			ТОГДА &ТекстНеОтраженыВКнигеПродаж
	|		ИНАЧЕ &ТекстОтраженыВКнигеПродаж
	|	КОНЕЦ,
	|	СУММА(1),
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот,
	|	0,
	|	НДСЗаписиКнигиПродажОбороты.Покупатель,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПродажОбороты.Событие,
	|	НЕОПРЕДЕЛЕНО,
	|	НДСЗаписиКнигиПродажОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПродажОбороты.НомерСтроки
	|ИЗ
	|	ВТНДСЗаписиКнигиПродажОбороты КАК НДСЗаписиКнигиПродажОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСчетФактураВыданный КАК СчетФактураВыданный
	|		ПО НДСЗаписиКнигиПродажОбороты.СчетФактура = СчетФактураВыданный.ДокументОснование
	|ГДЕ
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.НалоговыйАгентКомитент)
	|
	|СГРУППИРОВАТЬ ПО
	|	СчетФактураВыданный.ДокументОснование,
	|	НДСЗаписиКнигиПродажОбороты.СчетФактура,
	|	НДСЗаписиКнигиПродажОбороты.СтавкаНДС,
	|	ЕСТЬNULL(СчетФактураВыданный.Ссылка, НЕОПРЕДЕЛЕНО),
	|	НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.Покупатель,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПродажОбороты.Событие,
	|	НДСЗаписиКнигиПродажОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПродажОбороты.НомерСтроки,
	|	ВЫБОР
	|		КОГДА НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот ЕСТЬ NULL
	|			ТОГДА ВЫБОР
	|					КОГДА СчетФактураВыданный.Ссылка ЕСТЬ NULL
	|						ТОГДА &ТекстСФНалоговогоАгентаНеВыписан
	|					ИНАЧЕ ВЫБОР
	|							КОГДА СчетФактураВыданный.Проведен
	|								ТОГДА &ТекстНачислениеНеОтраженоВКнигеПродаж
	|							ИНАЧЕ &ТекстСФНалоговогоАгентаНеПроведен
	|						КОНЕЦ
	|				КОНЕЦ
	|		ИНАЧЕ &ТекстБезОшибок
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НЕОПРЕДЕЛЕНО,
	|	НалоговыйАгентРеализацияТоваров.СчетФактура,
	|	НалоговыйАгентРеализацияТоваров.СтавкаНДС,
	|	СУММА(НалоговыйАгентРеализацияТоваров.СуммаБезНДСОборот),
	|	СУММА(НалоговыйАгентРеализацияТоваров.НДСОборот),
	|	ЕСТЬNULL(СчетФактураПолученный.Ссылка, НалоговыйАгентРеализацияТоваров.СчетФактура),
	|	ЛОЖЬ,
	|	&ТекстБезОшибок,
	|	&ТекстОтраженыВКнигеПродаж,
	|	СУММА(1),
	|	НалоговыйАгентРеализацияТоваров.НДСОборот,
	|	0,
	|	НалоговыйАгентРеализацияТоваров.Покупатель,
	|	НалоговыйАгентРеализацияТоваров.ВидЦенности,
	|	НалоговыйАгентРеализацияТоваров.Событие,
	|	НЕОПРЕДЕЛЕНО,
	|	НалоговыйАгентРеализацияТоваров.ЗаписьДополнительногоЛиста,
	|	НалоговыйАгентРеализацияТоваров.НомерСтроки
	|ИЗ
	|	ВТНДСЗаписиКнигиПродажОбороты КАК НалоговыйАгентРеализацияТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученныйАванс КАК СчетФактураПолученный
	|		ПО НалоговыйАгентРеализацияТоваров.СчетФактура = СчетФактураПолученный.ДокументОснование
	|			И НЕ СчетФактураПолученный.ПометкаУдаления
	|ГДЕ
	|	НалоговыйАгентРеализацияТоваров.ВидЦенности В(&НалоговыйАгентРеализацияТоваров)
	|
	|СГРУППИРОВАТЬ ПО
	|	НалоговыйАгентРеализацияТоваров.СчетФактура,
	|	ЕСТЬNULL(СчетФактураПолученный.Ссылка, НалоговыйАгентРеализацияТоваров.СчетФактура),
	|	НалоговыйАгентРеализацияТоваров.СтавкаНДС,
	|	НалоговыйАгентРеализацияТоваров.СуммаБезНДСОборот,
	|	НалоговыйАгентРеализацияТоваров.НДСОборот,
	|	НалоговыйАгентРеализацияТоваров.Покупатель,
	|	НалоговыйАгентРеализацияТоваров.ВидЦенности,
	|	НалоговыйАгентРеализацияТоваров.Событие,
	|	НалоговыйАгентРеализацияТоваров.ЗаписьДополнительногоЛиста,
	|	НалоговыйАгентРеализацияТоваров.НомерСтроки";
	
	ПодстановкаСтавкаНДС = УчетНДСРФ.ТекстВыбораСтавкиНДСПоУмолчанию(Запрос.Параметры, "ОплатыПоставщику.Период", Истина);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&СтавкаНДСПоУмолчанию", ПодстановкаСтавкаНДС);
	ПодстановкаСтавкаНДС = УчетНДСРФ.ТекстВыбораСтавкиНДСПоУмолчанию(Запрос.Параметры, "Расчеты.Период", Истина);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПроцентНДС", УчетНДСРФ.ТекстВыбораПроцентаНДС(ПодстановкаСтавкаНДС));
	
	НачисленияИсполнениеОНА = Запрос.Выполнить();
	ВнешнийНаборДанныхНачислениеИсполнениеОНА = Новый Структура;
	ВнешнийНаборДанныхНачислениеИсполнениеОНА.Вставить("НачисленияИсполнениеОНА", НачисленияИсполнениеОНА);
	
	СтруктураВнешнихНаборов.Вставить("ВнешнийНаборДанныхНачислениеИсполнениеОНА", ВнешнийНаборДанныхНачислениеИсполнениеОНА);
	
КонецПроцедуры

// Выполняет запрос к базе данных для получения набора данных, необходимого для формирования расшифровки по
//	начисленному НДС с выданных авансов (по факту поступления) в случае ведения упрощенного учета НДС.
//
// Параметры
//  Запрос  - Запрос - запрос к базе данных
//  СтруктураВнешнихНаборов - структура внешних источников данных для расшифровки.
//
// Возвращаемое значение:
//   Структура   - структура параметров для формирования карты отчета.
//
Процедура ПолучитьНаборДанныхНачислениеВосстановлениеСАвансовВыданныхУпрощенныйУчетНДС(Запрос, СтруктураВнешнихНаборов) 

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Субконто1 КАК Субконто1
	|ПОМЕСТИТЬ ВТХозрасчетныйОстаткиНДСпоАвансамИПредоплатамВыданным
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&НачалоСледующегоПериода, 
	|											Счет В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСпоАвансамИПредоплатамВыданным)),
	|											ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СФПолученные), 
	|											Организация В (&ОрганизацииНеНаЛьготномНалогообложении)) КАК ХозрасчетныйОстатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Субконто1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Субконто1 КАК Субконто1,
	|	ХозрасчетныйОстатки.СуммаОстатокДт
	|ПОМЕСТИТЬ ВТХозрасчетныйОстаткиНДСпоАвансамПоСпискуСчетов
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&НачалоСледующегоПериода, 
	|											Счет В (&СписокСчетовАвансовВыданных), 
	|											ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами), 
	|											Организация В (&ОрганизацииНеНаЛьготномНалогообложении)) КАК ХозрасчетныйОстатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Субконто1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПокупокОбороты.Организация КАК Организация,
	|	НДСЗаписиКнигиПокупокОбороты.СчетФактура КАК СчетФактура,
	|	НДСЗаписиКнигиПокупокОбороты.ВидЦенности КАК ВидЦенности,
	|	НДСЗаписиКнигиПокупокОбороты.СтавкаНДС КАК СтавкаНДС,
	|	НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПокупокОбороты.НДСОборот,
	|	НДСЗаписиКнигиПокупокОбороты.Событие,
	|	НДСЗаписиКнигиПокупокОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПокупокОбороты.НомерСтроки
	|ПОМЕСТИТЬ ВТЗаписиКнигиПокупокОборотыСАвансовВыданныхУпрощенныйУчет
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			,
	|			&КонецПериодаГраница,
	|			Запись,
	|			Организация В (&ОрганизацииНеНаЛьготномНалогообложении)
	|				И СчетФактура В
	|					(ВЫБРАТЬ
	|						ХозрасчетныйОстатки.Субконто1 КАК СчетФактура
	|					ИЗ
	|						ВТХозрасчетныйОстаткиНДСпоАвансамИПредоплатамВыданным КАК ХозрасчетныйОстатки)
	|				И ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.АвансыВыданные)
	|				И НЕ ЗаписьДополнительногоЛиста) КАК НДСЗаписиКнигиПокупокОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПокупокОбороты.Организация,
	|	НДСЗаписиКнигиПокупокОбороты.СчетФактура,
	|	НДСЗаписиКнигиПокупокОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПокупокОбороты.СтавкаНДС,
	|	НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПокупокОбороты.НДСОборот,
	|	НДСЗаписиКнигиПокупокОбороты.Событие,
	|	НДСЗаписиКнигиПокупокОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПокупокОбороты.НомерСтроки
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			,
	|			,
	|			Запись,
	|			Организация В (&ОрганизацииНеНаЛьготномНалогообложении)
	|				И СчетФактура В
	|					(ВЫБРАТЬ
	|						ХозрасчетныйОстатки.Субконто1 КАК СчетФактура
	|					ИЗ
	|						ВТХозрасчетныйОстаткиНДСпоАвансамИПредоплатамВыданным КАК ХозрасчетныйОстатки)
	|				И ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.АвансыВыданные)
	|				И (ЗаписьДополнительногоЛиста
	|					И (КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода))) КАК НДСЗаписиКнигиПокупокОбороты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	СчетФактура,
	|	ВидЦенности,
	|	СтавкаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПродажОбороты.Покупатель,
	|	НДСЗаписиКнигиПродажОбороты.ДоговорКонтрагента,
	|	НДСЗаписиКнигиПродажОбороты.СчетФактура КАК СчетФактура,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности КАК ВидЦенности,
	|	НДСЗаписиКнигиПродажОбороты.СтавкаНДС КАК СтавкаНДС,
	|	НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот
	|ПОМЕСТИТЬ ВТЗаписиКнигиПродажОборотыСАвансовВыданныхУпрощенныйУчет
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			,
	|			&КонецПериодаГраница,
	|			,
	|			Организация В (&ОрганизацииНеНаЛьготномНалогообложении)
	|				И СчетФактура В
	|					(ВЫБРАТЬ
	|						ХозрасчетныйОстатки.Субконто1 КАК СчетФактура
	|					ИЗ
	|						ВТХозрасчетныйОстаткиНДСпоАвансамИПредоплатамВыданным КАК ХозрасчетныйОстатки)
	|				И ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.АвансыВыданные)
	|				И НЕ ЗаписьДополнительногоЛиста) КАК НДСЗаписиКнигиПродажОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПродажОбороты.Покупатель,
	|	НДСЗаписиКнигиПродажОбороты.ДоговорКонтрагента,
	|	НДСЗаписиКнигиПродажОбороты.СчетФактура,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПродажОбороты.СтавкаНДС,
	|	НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			,
	|			,
	|			,
	|			Организация В (&ОрганизацииНеНаЛьготномНалогообложении)
	|				И СчетФактура В
	|					(ВЫБРАТЬ
	|						ХозрасчетныйОстатки.Субконто1 КАК СчетФактура
	|					ИЗ
	|						ВТХозрасчетныйОстаткиНДСпоАвансамИПредоплатамВыданным КАК ХозрасчетныйОстатки)
	|				И ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.АвансыВыданные)
	|				И (ЗаписьДополнительногоЛиста
	|					И (КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода))) КАК НДСЗаписиКнигиПродажОбороты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура,
	|	ВидЦенности,
	|	СтавкаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(НДСЗаписиКнигиПокупокОбороты.СчетФактура, НДСЗаписиКнигиПродажОбороты.СчетФактура) КАК ДокументРегистратор,
	|	СУММА(ЕСТЬNULL(НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот, 0) - ЕСТЬNULL(НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот, 0)) КАК Сумма,
	|	СУММА(ЕСТЬNULL(НДСЗаписиКнигиПокупокОбороты.НДСОборот, 0) - ЕСТЬNULL(НДСЗаписиКнигиПродажОбороты.НДСОборот, 0)) КАК СуммаНДС,
	|	ХозрасчетныйОстатки.СуммаОстатокДт,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот, 0) - ЕСТЬNULL(НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот, 0) + ЕСТЬNULL(НДСЗаписиКнигиПокупокОбороты.НДСОборот, 0) - ЕСТЬNULL(НДСЗаписиКнигиПродажОбороты.НДСОборот, 0) - ХозрасчетныйОстатки.СуммаОстатокДт = 0
	|			ТОГДА &ТекстОплаченныеЦенностиНеПоступили
	|		ИНАЧЕ &ТекстНачислениеНеОтраженоВКнигеПродаж
	|	КОНЕЦ КАК ПризнакНаличияОшибок,
	|	&ТекстНеОтраженыВКнигеПродаж КАК ПризнакОтраженияНДС,
	|	1 КАК КоличествоДокументов,
	|	ЕСТЬNULL(НДСЗаписиКнигиПродажОбороты.СтавкаНДС, ВЫРАЗИТЬ(&СтавкаНДСПоУмолчанию КАК Справочник.СтавкиНДС).ПеречислениеСтавкаНДС) КАК СтавкаНДС,
	|	НДСЗаписиКнигиПокупокОбороты.Событие,
	|	НДСЗаписиКнигиПокупокОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПокупокОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПокупокОбороты.НомерСтроки
	|ИЗ
	|	ВТЗаписиКнигиПокупокОборотыСАвансовВыданныхУпрощенныйУчет КАК НДСЗаписиКнигиПокупокОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаписиКнигиПродажОборотыСАвансовВыданныхУпрощенныйУчет КАК НДСЗаписиКнигиПродажОбороты
	|		ПО НДСЗаписиКнигиПокупокОбороты.СчетФактура = НДСЗаписиКнигиПродажОбороты.СчетФактура
	|			И НДСЗаписиКнигиПокупокОбороты.ВидЦенности = НДСЗаписиКнигиПродажОбороты.ВидЦенности
	|			И НДСЗаписиКнигиПокупокОбороты.СтавкаНДС = НДСЗаписиКнигиПродажОбороты.СтавкаНДС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТХозрасчетныйОстаткиНДСпоАвансамПоСпискуСчетов КАК ХозрасчетныйОстатки
	|		ПО НДСЗаписиКнигиПокупокОбороты.СчетФактура = ХозрасчетныйОстатки.Субконто1
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеРасчетныхДокументов
	|		ПО НДСЗаписиКнигиПокупокОбороты.СчетФактура = ДанныеРасчетныхДокументов.Документ
	|			И НДСЗаписиКнигиПокупокОбороты.Организация = ДанныеРасчетныхДокументов.Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСЗаписиКнигиПродажОбороты.Покупатель,
	|	НДСЗаписиКнигиПродажОбороты.ДоговорКонтрагента,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПокупокОбороты.НДСОборот,
	|	ХозрасчетныйОстатки.СуммаОстатокДт,
	|	НДСЗаписиКнигиПокупокОбороты.Событие,
	|	НДСЗаписиКнигиПокупокОбороты.ВидЦенности,
	|	ЕСТЬNULL(НДСЗаписиКнигиПродажОбороты.СтавкаНДС, ВЫРАЗИТЬ(&СтавкаНДСПоУмолчанию КАК Справочник.СтавкиНДС).ПеречислениеСтавкаНДС),
	|	НДСЗаписиКнигиПокупокОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПокупокОбороты.НомерСтроки,
	|	ЕСТЬNULL(НДСЗаписиКнигиПокупокОбороты.СчетФактура, НДСЗаписиКнигиПродажОбороты.СчетФактура),
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот, 0) - ЕСТЬNULL(НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот, 0) + ЕСТЬNULL(НДСЗаписиКнигиПокупокОбороты.НДСОборот, 0) - ЕСТЬNULL(НДСЗаписиКнигиПродажОбороты.НДСОборот, 0) - ХозрасчетныйОстатки.СуммаОстатокДт = 0
	|			ТОГДА &ТекстОплаченныеЦенностиНеПоступили
	|		ИНАЧЕ &ТекстНачислениеНеОтраженоВКнигеПродаж
	|	КОНЕЦ";
	
	ПодстановкаСтавкаНДС = УчетНДСРФ.ТекстВыбораСтавкиНДСПоУмолчанию(Запрос.Параметры, "ДанныеРасчетныхДокументов.Дата", Истина);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&СтавкаНДСПоУмолчанию", ПодстановкаСтавкаНДС);
	
	ВосстановлениеСАвансовВыданныхУпрощенныйУчетНДСНеВключенные = Запрос.Выполнить();
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НДСЗаписиКнигиПродаж.СчетФактура КАК СчетФактура,
	|	НДСЗаписиКнигиПродаж.СтавкаНДС,
	|	НДСЗаписиКнигиПродаж.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПродаж.НДСОборот,
	|	НДСЗаписиКнигиПродаж.Событие,
	|	НДСЗаписиКнигиПродаж.ВидЦенности,
	|	НДСЗаписиКнигиПродаж.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПродаж.НомерСтроки
	|ПОМЕСТИТЬ ВТНДСЗаписиКнигиПродажОбороты
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			&НачалоПериода,
	|			&КонецПериодаГраница,
	|			Запись,
	|			Организация В (&ОрганизацииНеНаЛьготномНалогообложении)
	|				И НЕ ЗаписьДополнительногоЛиста) КАК НДСЗаписиКнигиПродаж
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПродаж.СчетФактура,
	|	НДСЗаписиКнигиПродаж.СтавкаНДС,
	|	НДСЗаписиКнигиПродаж.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПродаж.НДСОборот,
	|	НДСЗаписиКнигиПродаж.Событие,
	|	НДСЗаписиКнигиПродаж.ВидЦенности,
	|	НДСЗаписиКнигиПродаж.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПродаж.НомерСтроки
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			&НачалоПериода,
	|			,
	|			Запись,
	|			Организация В (&ОрганизацииНеНаЛьготномНалогообложении)
	|				И (ЗаписьДополнительногоЛиста
	|					И (КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода))) КАК НДСЗаписиКнигиПродаж
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПродаж.СчетФактура КАК ДокументРегистратор,
	|	НДСЗаписиКнигиПродаж.СуммаБезНДСОборот КАК Сумма,
	|	НДСЗаписиКнигиПродаж.НДСОборот КАК СуммаНДС,
	|	ХозрасчетныйОстатки.СуммаОстатокДт,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОстатки.СуммаОстатокДт ЕСТЬ NULL 
	|			ТОГДА &ТекстНДСПолностьюВосстановлен
	|		ИНАЧЕ &ТекстНДСВосстановленЧастично
	|	КОНЕЦ КАК ПризнакНаличияОшибок,
	|	&ТекстОтраженыВКнигеПродаж КАК ПризнакОтраженияНДС,
	|	1 КАК КоличествоДокументов,
	|	НДСЗаписиКнигиПродаж.СтавкаНДС КАК СтавкаНДС,
	|	НДСЗаписиКнигиПродаж.ВидЦенности,
	|	НДСЗаписиКнигиПродаж.Событие,
	|	НДСЗаписиКнигиПродаж.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПродаж.НомерСтроки
	|ИЗ
	|	ВТНДСЗаписиКнигиПродажОбороты КАК НДСЗаписиКнигиПродаж
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТХозрасчетныйОстаткиНДСпоАвансамПоСпискуСчетов КАК ХозрасчетныйОстатки
	|		ПО НДСЗаписиКнигиПродаж.СчетФактура = ХозрасчетныйОстатки.Субконто1";	
	
	Запрос.Текст = ТекстЗапроса;
	
	ВосстановлениеСАвансовВыданныхУпрощенныйУчетНДСВключенные = Запрос.Выполнить();
		
	ВнешнийНаборДанныхНачислениеВосстановлениеСАвансовВыданныхУпрощенныйУчетНДС = Новый Структура;
	
	ВнешнийНаборДанныхНачислениеВосстановлениеСАвансовВыданныхУпрощенныйУчетНДС.Вставить("ВосстановлениеСАвансовВыданныхУпрощенныйУчетНДСНеВключенные",
																					     	ВосстановлениеСАвансовВыданныхУпрощенныйУчетНДСНеВключенные);
	ВнешнийНаборДанныхНачислениеВосстановлениеСАвансовВыданныхУпрощенныйУчетНДС.Вставить("ВосстановлениеСАвансовВыданныхУпрощенныйУчетНДСВключенные",
																						 	ВосстановлениеСАвансовВыданныхУпрощенныйУчетНДСВключенные);
		
	СтруктураВнешнихНаборов.Вставить("ВнешнийНаборДанныхНачислениеВосстановлениеСАвансовВыданныхУпрощенныйУчетНДС",
									 	ВнешнийНаборДанныхНачислениеВосстановлениеСАвансовВыданныхУпрощенныйУчетНДС);
	
КонецПроцедуры // ПолучитьНаборДанныхНачислениеВосстановлениеСАвансовВыданныхУпрощенныйУчетНДС()

// Выполняет запрос к базе данных для получения набора данных, необходимого для формирования расшифровки по
//	начисленному НДС при выполнении строительно-монтажных работ хозяйственным способом.
//
// Параметры
//  Запрос  - Запрос - запрос к базе данных
//  СтруктураВнешнихНаборов - структура внешних источников данных для расшифровки.
//
// Возвращаемое значение:
//   Структура   - структура параметров для формирования карты отчета.
//
Процедура ПолучитьНаборДанныхНачислениеСМРХозСпособом(Запрос, СтруктураВнешнихНаборов) 
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСЗаписиКнигиПродажОбороты.СчетФактура КАК СчетФактура,
	|	НДСЗаписиКнигиПродажОбороты.СтавкаНДС,
	|	НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.Событие,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПродажОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПродажОбороты.НомерСтроки
	|ПОМЕСТИТЬ ЗаписиКнигиПродажВЧастиСМР
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			&НачалоПериода,
	|			&КонецПериодаГраница,
	|			Запись,
	|			Организация В (&Организация)
	|				И ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.СМРСобственнымиСилами)
	|				И НЕ ЗаписьДополнительногоЛиста) КАК НДСЗаписиКнигиПродажОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПродажОбороты.СчетФактура,
	|	НДСЗаписиКнигиПродажОбороты.СтавкаНДС,
	|	НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.Событие,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПродажОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПродажОбороты.НомерСтроки
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			&НачалоПериода,
	|			,
	|			Запись,
	|			Организация В (&Организация)
	|				И ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.СМРСобственнымиСилами)
	|				И (ЗаписьДополнительногоЛиста
	|					И (КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода))) КАК НДСЗаписиКнигиПродажОбороты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПродажОбороты.СчетФактура КАК ДокументРегистратор,
	|	СчетаФактурыВыданные.Ссылка КАК СчетФактура,
	|	НДСЗаписиКнигиПродажОбороты.СтавкаНДС,
	|	НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот КАК Сумма,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот КАК СуммаНДС,
	|	&ТекстОтраженыВКнигеПродаж КАК ПризнакОтраженияНДС,
	|	1 КАК КоличествоДокументов,
	|	0 КАК НеВключенныйНДС,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот КАК ВключенныйНДС,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПродажОбороты.Событие,
	|	НДСЗаписиКнигиПродажОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПродажОбороты.НомерСтроки
	|ИЗ
	|	ЗаписиКнигиПродажВЧастиСМР КАК НДСЗаписиКнигиПродажОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетаФактурыВыданные
	|		ПО (СчетаФактурыВыданные.ДокументОснование = НДСЗаписиКнигиПродажОбороты.СчетФактура)";
	
	НачислениеСМРХозСпособом = Запрос.Выполнить();
	
	ВнешнийНаборДанныхНачислениеСМРХозСпособом = Новый Структура;
	ВнешнийНаборДанныхНачислениеСМРХозСпособом.Вставить("НачислениеСМРХозСпособом",	НачислениеСМРХозСпособом);
	
	СтруктураВнешнихНаборов.Вставить("ВнешнийНаборДанныхНачислениеСМРХозСпособом", 
									 ВнешнийНаборДанныхНачислениеСМРХозСпособом);
	
	
КонецПроцедуры // ПолучитьНаборДанныхНачисление()

// Выполняет запрос к базе данных для получения набора данных, необходимого для формирования расшифровки по
//	начислению НДС в случае реализации по ставке НДС 0%.
//
// Параметры
//  Запрос  - Запрос - запрос к базе данных
//  СтруктураВнешнихНаборов - структура внешних источников данных для расшифровки.
//
// Возвращаемое значение:
//   Структура   - структура параметров для формирования карты отчета.
//
Процедура ПолучитьНаборДанныхНачислениеРеализация0(Запрос, СтруктураВнешнихНаборов) 

	ТекстЗапросаНеподтвержденныеРеализации0 = УчетНДСПереопределяемый.ТекстЗапросаНеподтвержденныеРеализации0();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КнигаПродаж.СчетФактура КАК ДокументРегистратор,
	|	КнигаПродаж.СтавкаНДС,
	|	СУММА(КнигаПродаж.СуммаБезНДС) КАК Сумма,
	|	СУММА(КнигаПродаж.НДС) КАК СуммаНДС,
	|	&ТекстБезОшибок КАК ПризнакНаличияОшибок,
	|	&ТекстОтраженыВКнигеПродаж КАК ПризнакОтраженияНДС,
	|	1 КАК КоличествоДокументов,
	|	СУММА(КнигаПродаж.НДС) КАК СуммаВключенные,
	|	КнигаПродаж.ВидЦенности,
	|	КнигаПродаж.Событие КАК Событие,
	|	КнигаПродаж.ЗаписьДополнительногоЛиста КАК ЗаписьДополнительногоЛиста,
	|	КнигаПродаж.ВидДеятельностиНДС
	|ПОМЕСТИТЬ ВТОтгрузкаПоСтавке0
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж КАК КнигаПродаж
	|ГДЕ
	|	КнигаПродаж.Организация В(&Организация)
	|	И КнигаПродаж.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И НЕ КнигаПродаж.ЗаписьДополнительногоЛиста
	|	И КнигаПродаж.Событие В (ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.ПодтвержденаСтавка0), ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.НеПодтвержденаСтавка0))
	|	И КнигаПродаж.Активность
	|	И НЕ КнигаПродаж.ВидЦенности В (&ВидыЦенностейАвансыВсе, &ВидыЦенностейНалоговыйАгент)
	|
	|СГРУППИРОВАТЬ ПО
	|	КнигаПродаж.Организация,
	|	КнигаПродаж.СчетФактура,
	|	КнигаПродаж.СтавкаНДС,
	|	КнигаПродаж.ВидЦенности,
	|	КнигаПродаж.Событие,
	|	КнигаПродаж.ЗаписьДополнительногоЛиста,
	|	КнигаПродаж.ВидДеятельностиНДС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КнигаПродаж.СчетФактура,
	|	КнигаПродаж.СтавкаНДС,
	|	СУММА(КнигаПродаж.СуммаБезНДС),
	|	СУММА(КнигаПродаж.НДС),
	|	&ТекстБезОшибок,
	|	&ТекстОтраженыВКнигеПродаж,
	|	1,
	|	СУММА(КнигаПродаж.НДС),
	|	КнигаПродаж.ВидЦенности,
	|	КнигаПродаж.Событие,
	|	КнигаПродаж.ЗаписьДополнительногоЛиста,
	|	КнигаПродаж.ВидДеятельностиНДС
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж КАК КнигаПродаж
	|ГДЕ
	|	КнигаПродаж.Организация В(&Организация)
	|	И КнигаПродаж.Период >= &НачалоПериода
	|	И КнигаПродаж.Событие В (ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.ПодтвержденаСтавка0), ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.НеПодтвержденаСтавка0))
	|	И КнигаПродаж.Активность
	|	И КнигаПродаж.ЗаписьДополнительногоЛиста
	|	И КнигаПродаж.КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода
	|	И НЕ КнигаПродаж.ВидЦенности В (&ВидыЦенностейАвансыВсе, &ВидыЦенностейНалоговыйАгент)
	|
	|СГРУППИРОВАТЬ ПО
	|	КнигаПродаж.Организация,
	|	КнигаПродаж.СчетФактура,
	|	КнигаПродаж.СтавкаНДС,
	|	КнигаПродаж.ВидЦенности,
	|	КнигаПродаж.Событие,
	|	КнигаПродаж.ЗаписьДополнительногоЛиста,
	|	КнигаПродаж.ВидДеятельностиНДС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументРегистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НалоговаяБазаПоОтгрузкам.СчетФактура,
	|	НалоговаяБазаПоОтгрузкам.ДокументОтгрузки
	|ПОМЕСТИТЬ ДокументыОтгрузки
	|ИЗ
	|	(" + ТекстЗапросаНеподтвержденныеРеализации0 + ") КАК НалоговаяБазаПоОтгрузкам
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НалоговаяБазаПоОтгрузкам.СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ДокументыОтгрузки.ДокументОтгрузки, ВТОтгрузкаПоСтавке0.ДокументРегистратор) КАК ДокументОтгрузки,
	|	ВТОтгрузкаПоСтавке0.ДокументРегистратор КАК ДокументРегистратор,
	|	ВТОтгрузкаПоСтавке0.СтавкаНДС КАК СтавкаНДС,
	|	ВТОтгрузкаПоСтавке0.Сумма КАК Сумма,
	|	ВТОтгрузкаПоСтавке0.СуммаНДС КАК СуммаНДС,
	|	ВТОтгрузкаПоСтавке0.ПризнакНаличияОшибок КАК ПризнакНаличияОшибок,
	|	ВТОтгрузкаПоСтавке0.ПризнакОтраженияНДС КАК ПризнакОтраженияНДС,
	|	ВТОтгрузкаПоСтавке0.КоличествоДокументов КАК КоличествоДокументов,
	|	ВТОтгрузкаПоСтавке0.СуммаВключенные КАК СуммаВключенные,
	|	ВТОтгрузкаПоСтавке0.ВидЦенности КАК ВидЦенности,
	|	ВТОтгрузкаПоСтавке0.Событие КАК Событие,
	|	ВТОтгрузкаПоСтавке0.ЗаписьДополнительногоЛиста КАК ЗаписьДополнительногоЛиста,
	|	ВТОтгрузкаПоСтавке0.ВидДеятельностиНДС КАК ВидДеятельностиНДС
	|ПОМЕСТИТЬ НалоговаяБазаПоОтгрузкам
	|ИЗ
	|	ВТОтгрузкаПоСтавке0 КАК ВТОтгрузкаПоСтавке0
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДокументыОтгрузки КАК ДокументыОтгрузки
	|		ПО ВТОтгрузкаПоСтавке0.ДокументРегистратор = ДокументыОтгрузки.СчетФактура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЕСТЬNULL(ДокументыОтгрузки.ДокументОтгрузки, ВТОтгрузкаПоСтавке0.ДокументРегистратор)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НалоговаяБазаПоОтгрузкам.ДокументРегистратор КАК ДокументРегистратор,
	|	НалоговаяБазаПоОтгрузкам.СтавкаНДС КАК СтавкаНДС,
	|	НалоговаяБазаПоОтгрузкам.Сумма КАК Сумма,
	|	НалоговаяБазаПоОтгрузкам.СуммаНДС КАК СуммаНДС,
	|	НалоговаяБазаПоОтгрузкам.ПризнакНаличияОшибок КАК ПризнакНаличияОшибок,
	|	НалоговаяБазаПоОтгрузкам.ПризнакОтраженияНДС КАК ПризнакОтраженияНДС,
	|	НалоговаяБазаПоОтгрузкам.КоличествоДокументов КАК КоличествоДокументов,
	|	НалоговаяБазаПоОтгрузкам.СуммаВключенные КАК СуммаВключенные,
	|	НалоговаяБазаПоОтгрузкам.ВидЦенности КАК ВидЦенности,
	|	НалоговаяБазаПоОтгрузкам.Событие КАК Событие,
	|	НалоговаяБазаПоОтгрузкам.ЗаписьДополнительногоЛиста КАК ЗаписьДополнительногоЛиста,
	|	ВЫБОР
	|		КОГДА НалоговаяБазаПоОтгрузкам.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.Космос)
	|			ТОГДА ""1010409""
	|			ИНАЧЕ ЕСТЬNULL(ТаможенныеДекларацииПоЭкспорту.КодОперации, ""1011410"")
	|	КОНЕЦ КАК КодВидаОперации
	|ИЗ
	|	НалоговаяБазаПоОтгрузкам КАК НалоговаяБазаПоОтгрузкам
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияТаможенныхДекларацийЭкспорт КАК ТаможенныеДекларацииПоЭкспорту
	|		ПО НалоговаяБазаПоОтгрузкам.ДокументОтгрузки = ТаможенныеДекларацииПоЭкспорту.ДокументОтгрузки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ НалоговаяБазаПоОтгрузкам
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДокументыОтгрузки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТОтгрузкаПоСтавке0";
	
	НачислениеРеализация0 = Запрос.Выполнить();
		
	ВнешнийНаборДанныхНачислениеРеализация0 = Новый Структура;
	ВнешнийНаборДанныхНачислениеРеализация0.Вставить("НачислениеРеализация0",	НачислениеРеализация0);
	
	СтруктураВнешнихНаборов.Вставить("ВнешнийНаборДанныхНачислениеРеализация0", ВнешнийНаборДанныхНачислениеРеализация0);
	
КонецПроцедуры // ПолучитьНаборДанныхНачислениеРеализация0()

// Выполняет запрос к базе данных для получения набора данных, необходимого для формирования расшифровки по
//	вычету НДС по приобретенным ценностям.
//
// Параметры
//  Запрос  - Запрос - запрос к базе данных
//  СтруктураВнешнихНаборов - структура внешних источников данных для расшифровки.
//
// Возвращаемое значение:
//   Структура   - структура параметров для формирования карты отчета.
//
Процедура ПолучитьНаборДанныхВычетПоПриобретеннымЦенностямРасшифровка130СтрокиДекларации(Запрос, СтруктураВнешнихНаборов) 
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА СоставКнигиПокупок.ИсправленныйСчетФактура = НЕОПРЕДЕЛЕНО
	|			ТОГДА СоставКнигиПокупок.СчетФактура
	|		ИНАЧЕ СоставКнигиПокупок.ИсправленныйСчетФактура
	|	КОНЕЦ КАК ДокументРегистратор,
	|	СоставКнигиПокупок.СтавкаНДС,
	|	СУММА(СоставКнигиПокупок.СуммаБезНДСОборот) КАК СуммаБезНДСОборот,
	|	СУММА(СоставКнигиПокупок.НДСОборот) КАК НДСОборот,
	|	СоставКнигиПокупок.ЗаписьДополнительногоЛиста,
	|	СоставКнигиПокупок.НомерСтроки,
	|	СоставКнигиПокупок.Событие,
	|	СоставКнигиПокупок.ВидЦенности
	|ПОМЕСТИТЬ ВТСоставКнигиПокупок
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			&НачалоПериода,
	|			&КонецПериодаГраница,
	|			Запись,
	|			Организация В (&Организация)
	|				И Событие <> ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.ПредъявленНДСКВычету0)
	|				И НЕ ЗаписьДополнительногоЛиста) КАК СоставКнигиПокупок
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА СоставКнигиПокупок.ИсправленныйСчетФактура = НЕОПРЕДЕЛЕНО
	|			ТОГДА СоставКнигиПокупок.СчетФактура
	|		ИНАЧЕ СоставКнигиПокупок.ИсправленныйСчетФактура
	|	КОНЕЦ,
	|	СоставКнигиПокупок.ЗаписьДополнительногоЛиста,
	|	СоставКнигиПокупок.НомерСтроки,
	|	СоставКнигиПокупок.СтавкаНДС,
	|	СоставКнигиПокупок.Событие,
	|	СоставКнигиПокупок.ВидЦенности
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА СоставКнигиПокупок.ИсправленныйСчетФактура = НЕОПРЕДЕЛЕНО
	|			ТОГДА СоставКнигиПокупок.СчетФактура
	|		ИНАЧЕ СоставКнигиПокупок.ИсправленныйСчетФактура
	|	КОНЕЦ,
	|	СоставКнигиПокупок.СтавкаНДС,
	|	СУММА(СоставКнигиПокупок.СуммаБезНДСОборот),
	|	СУММА(СоставКнигиПокупок.НДСОборот),
	|	СоставКнигиПокупок.ЗаписьДополнительногоЛиста,
	|	СоставКнигиПокупок.НомерСтроки,
	|	СоставКнигиПокупок.Событие,
	|	СоставКнигиПокупок.ВидЦенности
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			&НачалоПериода,
	|			,
	|			Запись,
	|			Организация В (&Организация)
	|				И Событие <> ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.ПредъявленНДСКВычету0)
	|				И (ЗаписьДополнительногоЛиста
	|					И (КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода))) КАК СоставКнигиПокупок
	|
	|СГРУППИРОВАТЬ ПО
	|	СоставКнигиПокупок.СтавкаНДС,
	|	СоставКнигиПокупок.ЗаписьДополнительногоЛиста,
	|	СоставКнигиПокупок.НомерСтроки,
	|	ВЫБОР
	|		КОГДА СоставКнигиПокупок.ИсправленныйСчетФактура = НЕОПРЕДЕЛЕНО
	|			ТОГДА СоставКнигиПокупок.СчетФактура
	|		ИНАЧЕ СоставКнигиПокупок.ИсправленныйСчетФактура
	|	КОНЕЦ,
	|	СоставКнигиПокупок.Событие,
	|	СоставКнигиПокупок.ВидЦенности
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументРегистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетаФактурыДокументы.СчетФактура КАК СчетФактура,
	|	СчетаФактурыДокументы.ССылка КАК ССылка,
	|	СчетаФактурыДокументы.ДокументОснование КАК ДокументОснование,
	|	СчетаФактурыДокументы.СчетФактураПроведен КАК СчетФактураПроведен,
	|	СчетаФактурыДокументы.СчетФактураДата КАК СчетФактураДата,
	|	СчетаФактурыДокументы.НомерВходящегоСчетаФактуры,
	|	СчетаФактурыДокументы.ДатаВходящегоСчетаФактуры
	|ПОМЕСТИТЬ ВременнаяТаблицаСчетФактураПолученный
	|ИЗ
	|	(ВЫБРАТЬ
	|		СчетФактураПолученный.Ссылка КАК СчетФактура,
	|		СчетФактураПолученный.Ссылка КАК ССылка,
	|		СчетФактураПолученный.Ссылка КАК ДокументОснование,
	|		СчетФактураПолученный.Проведен КАК СчетФактураПроведен,
	|		СчетФактураПолученный.Дата КАК СчетФактураДата,
	|		СчетФактураПолученный.Номер КАК НомерВходящегоСчетаФактуры,
	|		СчетФактураПолученный.ДатаСоставления КАК ДатаВходящегоСчетаФактуры
	|	ИЗ
	|		Документ.СчетФактураПолученный КАК СчетФактураПолученный
	|	ГДЕ
	|		СчетФактураПолученный.Ссылка.Проведен
	|		И НЕ СчетФактураПолученный.Ссылка.ПометкаУдаления
	|		И СчетФактураПолученный.Ссылка.Организация В(&Организация)
	|		И СчетФактураПолученный.Ссылка В
	|				(ВЫБРАТЬ
	|					ВТСоставКнигиПокупок.ДокументРегистратор
	|				ИЗ
	|					ВТСоставКнигиПокупок)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		СчетФактураПолученный.ДокументОснование,
	|		СчетФактураПолученный.Ссылка,
	|		СчетФактураПолученный.ДокументОснование,
	|		СчетФактураПолученный.Ссылка.Проведен,
	|		СчетФактураПолученный.Ссылка.Дата,
	|		СчетФактураПолученный.Ссылка.Номер,
	|		СчетФактураПолученный.Ссылка.Дата
	|	ИЗ
	|		Документ.СчетФактураПолученный.ДокументыОснования КАК СчетФактураПолученный
	|	ГДЕ
	|		СчетФактураПолученный.Ссылка.Проведен
	|		И НЕ СчетФактураПолученный.Ссылка.ПометкаУдаления
	|		И СчетФактураПолученный.Ссылка.Организация В(&Организация)
	|		И СчетФактураПолученный.ДокументОснование В
	|				(ВЫБРАТЬ
	|					ВТСоставКнигиПокупок.ДокументРегистратор
	|				ИЗ
	|					ВТСоставКнигиПокупок)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВозвратТоваровОтПокупателя.Ссылка,
	|		ВозвратТоваровОтПокупателя.Ссылка,
	|		ВозвратТоваровОтПокупателя.Ссылка,
	|		ВозвратТоваровОтПокупателя.Проведен,
	|		ВозвратТоваровОтПокупателя.Дата,
	|		ВозвратТоваровОтПокупателя.НомерРасходногоКассовогоОрдера,
	|		ВозвратТоваровОтПокупателя.ДатаРасходногоКассовогоОрдера
	|	ИЗ
	|		Документ.ВозвратТоваровОтКлиента КАК ВозвратТоваровОтПокупателя
	|	ГДЕ
	|		ВозвратТоваровОтПокупателя.ДокументРеализации ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|		И ВозвратТоваровОтПокупателя.НомерРасходногоКассовогоОрдера <> """"
	|		И ВозвратТоваровОтПокупателя.ДатаРасходногоКассовогоОрдера <> ДАТАВРЕМЯ(1, 1, 1)
	|		И ВозвратТоваровОтПокупателя.Проведен
	|		И НЕ ВозвратТоваровОтПокупателя.ПометкаУдаления
	|		И ВозвратТоваровОтПокупателя.Организация В(&Организация)
	|		И ВозвратТоваровОтПокупателя.Ссылка В
	|				(ВЫБРАТЬ
	|					ВТСоставКнигиПокупок.ДокументРегистратор
	|				ИЗ
	|					ВТСоставКнигиПокупок)) КАК СчетаФактурыДокументы
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетФактураВыданный.Ссылка КАК СчетФактура,
	|	СчетФактураВыданный.Ссылка КАК Ссылка,
	|	СчетФактураВыданный.Ссылка КАК ДокументОснование,
	|	СчетФактураВыданный.ДокументОснование КАК СчетФактураДокументОснование,
	|	СчетФактураВыданный.Проведен КАК СчетФактураПроведен,
	|	СчетФактураВыданный.Дата КАК ДатаСчетаФактуры,
	|	СчетФактураВыданный.Номер КАК НомерСчетаФактуры
	|ПОМЕСТИТЬ ВременнаяТаблицаСчетФактураВыданный
	|ИЗ
	|	Документ.СчетФактураВыданный КАК СчетФактураВыданный
	|ГДЕ
	|	СчетФактураВыданный.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТСоставКнигиПокупок.ДокументРегистратор
	|			ИЗ
	|				ВТСоставКнигиПокупок)
	|	И СчетФактураВыданный.Проведен
	|	И НЕ СчетФактураВыданный.ПометкаУдаления
	|	И СчетФактураВыданный.Организация В(&Организация)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СчетФактураВыданный.ДокументОснование,
	|	СчетФактураВыданный.Ссылка,
	|	СчетФактураВыданный.ДокументОснование,
	|	СчетФактураВыданный.Ссылка.ДокументОснование,
	|	СчетФактураВыданный.Ссылка.Проведен,
	|	СчетФактураВыданный.Ссылка.Дата,
	|	СчетФактураВыданный.Ссылка.Номер
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданный
	|ГДЕ
	|	СчетФактураВыданный.ДокументОснование В
	|			(ВЫБРАТЬ
	|				ВТСоставКнигиПокупок.ДокументРегистратор
	|			ИЗ
	|				ВТСоставКнигиПокупок)
	|	И СчетФактураВыданный.Ссылка.Проведен
	|	И НЕ СчетФактураВыданный.Ссылка.ПометкаУдаления
	|	И СчетФактураВыданный.Ссылка.Организация В(&Организация)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТСоставКнигиПокупок.ДокументРегистратор,
	|	ВТСоставКнигиПокупок.СтавкаНДС,
	|	ВТСоставКнигиПокупок.СуммаБезНДСОборот КАК Сумма,
	|	ВТСоставКнигиПокупок.НДСОборот КАК СуммаНДС,
	|	&ТекстБезОшибок КАК ПризнакНаличияОшибок,
	|	&ТекстНетСноски КАК СимволСноски,
	|	ВЫБОР
	|		КОГДА ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.СчетФактураВыданный
	|				ИЛИ ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|			ТОГДА ВТСоставКнигиПокупок.ДокументРегистратор
	|		КОГДА ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.ОтчетКомиссионера
	|			ТОГДА СчетФактураПолученныйДокументыОснования.ССылка
	|		ИНАЧЕ ЕСТЬNULL(СчетФактураПолученныйДокументыОснования.ССылка, СчетФактураВыданныйДокументыОснования.Ссылка)
	|	КОНЕЦ КАК СчетФактура,
	|	&ТекстОтраженыВКнигеПокупок КАК ПризнакОтраженияНДС,
	|	1 КАК КоличествоДокументов,
	|	ВЫБОР
	|		КОГДА ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.СчетФактураВыданный
	|				ИЛИ ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|			ТОГДА ДанныеПервичныхДокументов.НомерРегистратора
	|		КОГДА ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.ОтчетКомиссионера
	|			ТОГДА СчетФактураПолученныйДокументыОснования.НомерВходящегоСчетаФактуры
	|		ИНАЧЕ ЕСТЬNULL(СчетФактураПолученныйДокументыОснования.НомерВходящегоСчетаФактуры, СчетФактураВыданныйДокументыОснования.НомерСчетаФактуры)
	|	КОНЕЦ КАК НомерВходящегоДокумента,
	|	ВЫБОР
	|		КОГДА ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.СчетФактураВыданный
	|				ИЛИ ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|			ТОГДА ДанныеПервичныхДокументов.ДатаРегистратора
	|		КОГДА ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.ОтчетКомиссионера
	|			ТОГДА СчетФактураПолученныйДокументыОснования.ДатаВходящегоСчетаФактуры
	|		ИНАЧЕ ЕСТЬNULL(СчетФактураПолученныйДокументыОснования.ДатаВходящегоСчетаФактуры, СчетФактураВыданныйДокументыОснования.ДатаСчетаФактуры)
	|	КОНЕЦ КАК ДатаВходящегоДокумента,
	|	0 КАК КоличествоОшибок,
	|	ВТСоставКнигиПокупок.ЗаписьДополнительногоЛиста,
	|	ВТСоставКнигиПокупок.НомерСтроки,
	|	ВТСоставКнигиПокупок.Событие,
	|	ВТСоставКнигиПокупок.ВидЦенности
	|ИЗ
	|	ВТСоставКнигиПокупок КАК ВТСоставКнигиПокупок
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСчетФактураПолученный КАК СчетФактураПолученныйДокументыОснования
	|		ПО ВТСоставКнигиПокупок.ДокументРегистратор = СчетФактураПолученныйДокументыОснования.ДокументОснование
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСчетФактураВыданный КАК СчетФактураВыданныйДокументыОснования
	|		ПО ВТСоставКнигиПокупок.ДокументРегистратор = СчетФактураВыданныйДокументыОснования.ДокументОснование
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО (ДанныеПервичныхДокументов.Организация В (&Организация))
	|			И ВТСоставКнигиПокупок.ДокументРегистратор = ДанныеПервичныхДокументов.Документ";

	ВычетПоПриобретеннымЦенностямРасшифровка130СтрокиДекларации = Запрос.Выполнить();
	
	ВнешнийНаборДанныхВычетПриобретенныеЦенности = Новый Структура;
	ВнешнийНаборДанныхВычетПриобретенныеЦенности.Вставить("ВычетПоПриобретеннымЦенностямРасшифровка130СтрокиДекларации", 
		ВычетПоПриобретеннымЦенностямРасшифровка130СтрокиДекларации);
	
	СтруктураВнешнихНаборов.Вставить("ВнешнийНаборДанныхСтроки130Декларации", 
									 ВнешнийНаборДанныхВычетПриобретенныеЦенности);
	
КонецПроцедуры

// Выполняет запрос к базе данных для получения набора данных, необходимого для формирования расшифровки по
//	вычету НДС по приобретенным ценностям.
//
// Параметры
//  Запрос  - Запрос - запрос к базе данных
//  СтруктураВнешнихНаборов - структура внешних источников данных для расшифровки.
//
// Возвращаемое значение:
//   Структура   - структура параметров для формирования карты отчета.
//
Процедура ПолучитьНаборДанныхВычетПоПриобретеннымЦенностям(Запрос, СтруктураВнешнихНаборов) 
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА СоставКнигиПокупок.Регистратор ССЫЛКА Документ.ВозвратТоваровОтКлиента
	|			ТОГДА СоставКнигиПокупок.Регистратор
	|		КОГДА СоставКнигиПокупок.ИсправленныйСчетФактура = НЕОПРЕДЕЛЕНО
	|			ТОГДА СоставКнигиПокупок.СчетФактура
	|		ИНАЧЕ СоставКнигиПокупок.ИсправленныйСчетФактура
	|	КОНЕЦ КАК ДокументРегистратор,
	|	СоставКнигиПокупок.СтавкаНДС,
	|	СУММА(СоставКнигиПокупок.СуммаБезНДСОборот) КАК СуммаБезНДСОборот,
	|	СУММА(СоставКнигиПокупок.НДСОборот) КАК НДСОборот,
	|	СоставКнигиПокупок.ЗаписьДополнительногоЛиста,
	|	СоставКнигиПокупок.НомерСтроки,
	|	СоставКнигиПокупок.Событие,
	|	СоставКнигиПокупок.ВидЦенности
	|ПОМЕСТИТЬ ВТСоставКнигиПокупок
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			&НачалоПериода,
	|			&КонецПериодаГраница,
	|			Запись,
	|			Организация В (&Организация)
	|				И НЕ ВидЦенности В (&ВидыЦенностейАвансыВсе, &ВидыЦенностейНалоговыйАгент)
	|				И Событие <> ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.ПредъявленНДСКВычету0)
	|				И НЕ ЗаписьДополнительногоЛиста) КАК СоставКнигиПокупок
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА СоставКнигиПокупок.Регистратор ССЫЛКА Документ.ВозвратТоваровОтКлиента
	|			ТОГДА СоставКнигиПокупок.Регистратор
	|		КОГДА СоставКнигиПокупок.ИсправленныйСчетФактура = НЕОПРЕДЕЛЕНО
	|			ТОГДА СоставКнигиПокупок.СчетФактура
	|		ИНАЧЕ СоставКнигиПокупок.ИсправленныйСчетФактура
	|	КОНЕЦ,
	|	СоставКнигиПокупок.ЗаписьДополнительногоЛиста,
	|	СоставКнигиПокупок.НомерСтроки,
	|	СоставКнигиПокупок.СтавкаНДС,
	|	СоставКнигиПокупок.Событие,
	|	СоставКнигиПокупок.ВидЦенности
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА СоставКнигиПокупок.ИсправленныйСчетФактура = НЕОПРЕДЕЛЕНО
	|			ТОГДА СоставКнигиПокупок.СчетФактура
	|		ИНАЧЕ СоставКнигиПокупок.ИсправленныйСчетФактура
	|	КОНЕЦ,
	|	СоставКнигиПокупок.СтавкаНДС,
	|	СУММА(СоставКнигиПокупок.СуммаБезНДСОборот),
	|	СУММА(СоставКнигиПокупок.НДСОборот),
	|	СоставКнигиПокупок.ЗаписьДополнительногоЛиста,
	|	СоставКнигиПокупок.НомерСтроки,
	|	СоставКнигиПокупок.Событие,
	|	СоставКнигиПокупок.ВидЦенности
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			&НачалоПериода,
	|			,
	|			Запись,
	|			Организация В (&Организация)
	|				И НЕ ВидЦенности В (&ВидыЦенностейАвансыВсе, &ВидыЦенностейНалоговыйАгент)
	|				И Событие <> ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.ПредъявленНДСКВычету0)
	|				И (ЗаписьДополнительногоЛиста
	|					И (КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода))) КАК СоставКнигиПокупок
	|
	|СГРУППИРОВАТЬ ПО
	|	СоставКнигиПокупок.СтавкаНДС,
	|	СоставКнигиПокупок.ЗаписьДополнительногоЛиста,
	|	СоставКнигиПокупок.НомерСтроки,
	|	ВЫБОР
	|		КОГДА СоставКнигиПокупок.ИсправленныйСчетФактура = НЕОПРЕДЕЛЕНО
	|			ТОГДА СоставКнигиПокупок.СчетФактура
	|		ИНАЧЕ СоставКнигиПокупок.ИсправленныйСчетФактура
	|	КОНЕЦ,
	|	СоставКнигиПокупок.Событие,
	|	СоставКнигиПокупок.ВидЦенности
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументРегистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетаФактурыДокументы.СчетФактура КАК СчетФактура,
	|	СчетаФактурыДокументы.Ссылка КАК Ссылка,
	|	СчетаФактурыДокументы.ДокументОснование КАК ДокументОснование,
	|	СчетаФактурыДокументы.СчетФактураПроведен КАК СчетФактураПроведен,
	|	СчетаФактурыДокументы.СчетФактураДата КАК СчетФактураДата,
	|	СчетаФактурыДокументы.НомерВходящегоСчетаФактуры,
	|	СчетаФактурыДокументы.ДатаВходящегоСчетаФактуры
	|ПОМЕСТИТЬ ВременнаяТаблицаСчетФактураПолученный
	|ИЗ
	|	(ВЫБРАТЬ
	|		СчетФактураПолученный.Ссылка КАК СчетФактура,
	|		СчетФактураПолученный.Ссылка КАК ССылка,
	|		СчетФактураПолученный.Ссылка КАК ДокументОснование,
	|		СчетФактураПолученный.Проведен КАК СчетФактураПроведен,
	|		СчетФактураПолученный.Дата КАК СчетФактураДата,
	|		СчетФактураПолученный.Номер КАК НомерВходящегоСчетаФактуры,
	|		СчетФактураПолученный.ДатаСоставления КАК ДатаВходящегоСчетаФактуры
	|	ИЗ
	|		Документ.СчетФактураПолученный КАК СчетФактураПолученный
	|	ГДЕ
	|		НЕ СчетФактураПолученный.Ссылка.ПометкаУдаления
	|		И СчетФактураПолученный.Ссылка.Организация В(&Организация)
	|		И СчетФактураПолученный.Ссылка В
	|				(ВЫБРАТЬ
	|					ВТСоставКнигиПокупок.ДокументРегистратор
	|				ИЗ
	|					ВТСоставКнигиПокупок)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		СчетФактураПолученный.ДокументОснование,
	|		СчетФактураПолученный.Ссылка,
	|		СчетФактураПолученный.ДокументОснование,
	|		СчетФактураПолученный.Ссылка.Проведен,
	|		СчетФактураПолученный.Ссылка.Дата,
	|		СчетФактураПолученный.Ссылка.Номер,
	|		СчетФактураПолученный.Ссылка.Дата
	|	ИЗ
	|		Документ.СчетФактураПолученный.ДокументыОснования КАК СчетФактураПолученный
	|	ГДЕ
	|		НЕ СчетФактураПолученный.Ссылка.ПометкаУдаления
	|		И СчетФактураПолученный.Ссылка.Организация В(&Организация)
	|		И СчетФактураПолученный.ДокументОснование В
	|				(ВЫБРАТЬ
	|					ВТСоставКнигиПокупок.ДокументРегистратор
	|				ИЗ
	|					ВТСоставКнигиПокупок)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВозвратТоваровОтКлиента.Ссылка,
	|		ВозвратТоваровОтКлиента.Ссылка,
	|		ВозвратТоваровОтКлиента.Ссылка,
	|		ВозвратТоваровОтКлиента.Проведен,
	|		ВозвратТоваровОтКлиента.Дата,
	|		ВозвратТоваровОтКлиента.НомерРасходногоКассовогоОрдера,
	|		ВозвратТоваровОтКлиента.ДатаРасходногоКассовогоОрдера
	|	ИЗ
	|		Документ.ВозвратТоваровОтКлиента КАК ВозвратТоваровОтКлиента
	|	ГДЕ
	|		ВозвратТоваровОтКлиента.ДокументРеализации ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|		И ВозвратТоваровОтКлиента.НомерРасходногоКассовогоОрдера <> """"
	|		И ВозвратТоваровОтКлиента.ДатаРасходногоКассовогоОрдера <> ДАТАВРЕМЯ(1, 1, 1)
	|		И ВозвратТоваровОтКлиента.Проведен
	|		И НЕ ВозвратТоваровОтКлиента.ПометкаУдаления
	|		И ВозвратТоваровОтКлиента.Организация В(&Организация)
	|		И ВозвратТоваровОтКлиента.Ссылка В
	|				(ВЫБРАТЬ
	|					ВТСоставКнигиПокупок.ДокументРегистратор
	|				ИЗ
	|					ВТСоставКнигиПокупок)) КАК СчетаФактурыДокументы
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетФактураВыданный.Ссылка КАК СчетФактура,
	|	СчетФактураВыданный.Ссылка КАК Ссылка,
	|	СчетФактураВыданный.Ссылка КАК ДокументОснование,
	|	СчетФактураВыданный.ДокументОснование КАК СчетФактураДокументОснование,
	|	СчетФактураВыданный.Проведен КАК СчетФактураПроведен,
	|	СчетФактураВыданный.Дата КАК ДатаСчетаФактуры,
	|	СчетФактураВыданный.Номер КАК НомерСчетаФактуры
	|ПОМЕСТИТЬ ВременнаяТаблицаСчетФактураВыданный
	|ИЗ
	|	Документ.СчетФактураВыданный КАК СчетФактураВыданный
	|ГДЕ
	|	СчетФактураВыданный.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТСоставКнигиПокупок.ДокументРегистратор
	|			ИЗ
	|				ВТСоставКнигиПокупок)
	|	И ТИПЗНАЧЕНИЯ(СчетФактураВыданный.ДокументОснование) В (ТИП(Документ.КорректировкаРеализации),ТИП(Документ.ВозвратТоваровОтКлиента))
	|	И НЕ СчетФактураВыданный.ПометкаУдаления
	|	И СчетФактураВыданный.Организация В(&Организация)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СчетФактураВыданный.ДокументОснование,
	|	СчетФактураВыданный.Ссылка,
	|	СчетФактураВыданный.ДокументОснование,
	|	СчетФактураВыданный.Ссылка.ДокументОснование,
	|	СчетФактураВыданный.Ссылка.Проведен,
	|	СчетФактураВыданный.Ссылка.Дата,
	|	СчетФактураВыданный.Ссылка.Номер
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданный
	|ГДЕ
	|	СчетФактураВыданный.ДокументОснование В
	|			(ВЫБРАТЬ
	|				ВТСоставКнигиПокупок.ДокументРегистратор
	|			ИЗ
	|				ВТСоставКнигиПокупок)
	|	И ТИПЗНАЧЕНИЯ(СчетФактураВыданный.ДокументОснование) В (ТИП(Документ.КорректировкаРеализации),ТИП(Документ.ВозвратТоваровОтКлиента))
	|	И НЕ СчетФактураВыданный.Ссылка.ПометкаУдаления
	|	И СчетФактураВыданный.Ссылка.Организация В(&Организация)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СчетФактураВыданный.ДокументОснование,
	|	СчетФактураВыданный.Ссылка,
	|	СчетФактураВыданный.ДокументОснование,
	|	СчетФактураВыданный.Ссылка.ДокументОснование,
	|	СчетФактураВыданный.Ссылка.Проведен,
	|	СчетФактураВыданный.Ссылка.Дата,
	|	СчетФактураВыданный.Ссылка.Номер
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданный
	|ГДЕ
	|	СчетФактураВыданный.ДокументОснование В
	|			(ВЫБРАТЬ
	|				ВТСоставКнигиПокупок.ДокументРегистратор
	|			ИЗ
	|				ВТСоставКнигиПокупок)
	|	И СчетФактураВыданный.ДокументОснование ССЫЛКА Документ.ПередачаТоваровМеждуОрганизациями
	|	И НЕ СчетФактураВыданный.Ссылка.ПометкаУдаления
	|	И СчетФактураВыданный.Ссылка.Контрагент В(&Организация)
	|	
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТСоставКнигиПокупок.ДокументРегистратор,
	|	ВТСоставКнигиПокупок.СтавкаНДС,
	|	ВТСоставКнигиПокупок.СуммаБезНДСОборот КАК Сумма,
	|	ВТСоставКнигиПокупок.НДСОборот КАК СуммаНДС,
	|	&ТекстБезОшибок КАК ПризнакНаличияОшибок,
	|	&ТекстНетСноски КАК СимволСноски,
	|	ВЫБОР
	|		КОГДА ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.СчетФактураВыданный
	|				ИЛИ ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.ТаможеннаяДекларацияИмпорт
	|				ИЛИ ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|			ТОГДА ВТСоставКнигиПокупок.ДокументРегистратор
	|		КОГДА ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.ОтчетКомиссионера
	|			ТОГДА СчетФактураПолученныйДокументыОснования.Ссылка
	|		ИНАЧЕ ЕСТЬNULL(СчетФактураПолученныйДокументыОснования.Ссылка, СчетФактураВыданныйДокументыОснования.Ссылка)
	|	КОНЕЦ КАК СчетФактура,
	|	&ТекстОтраженыВКнигеПокупок КАК ПризнакОтраженияНДС,
	|	1 КАК КоличествоДокументов,
	|	ВЫБОР
	|		КОГДА ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.СчетФактураВыданный
	|				ИЛИ ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.ТаможеннаяДекларацияИмпорт
	|				ИЛИ ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|			ТОГДА ДанныеПервичныхДокументов.НомерРегистратора
	|		КОГДА ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.ОтчетКомиссионера
	|			ТОГДА СчетФактураПолученныйДокументыОснования.НомерВходящегоСчетаФактуры
	|		ИНАЧЕ ЕСТЬNULL(СчетФактураПолученныйДокументыОснования.НомерВходящегоСчетаФактуры, СчетФактураВыданныйДокументыОснования.НомерСчетаФактуры)
	|	КОНЕЦ КАК НомерВходящегоДокумента,
	|	ВЫБОР
	|		КОГДА ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.СчетФактураВыданный
	|				ИЛИ ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.ТаможеннаяДекларацияИмпорт
	|				ИЛИ ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|			ТОГДА ДанныеПервичныхДокументов.ДатаРегистратора
	|		КОГДА ВТСоставКнигиПокупок.ДокументРегистратор ССЫЛКА Документ.ОтчетКомиссионера
	|			ТОГДА СчетФактураПолученныйДокументыОснования.ДатаВходящегоСчетаФактуры
	|		ИНАЧЕ ЕСТЬNULL(СчетФактураПолученныйДокументыОснования.ДатаВходящегоСчетаФактуры, СчетФактураВыданныйДокументыОснования.ДатаСчетаФактуры)
	|	КОНЕЦ КАК ДатаВходящегоДокумента,
	|	0 КАК КоличествоОшибок,
	|	ВТСоставКнигиПокупок.ЗаписьДополнительногоЛиста,
	|	ВТСоставКнигиПокупок.НомерСтроки,
	|	ВТСоставКнигиПокупок.Событие,
	|	ВТСоставКнигиПокупок.ВидЦенности
	|ИЗ
	|	ВТСоставКнигиПокупок КАК ВТСоставКнигиПокупок
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСчетФактураПолученный КАК СчетФактураПолученныйДокументыОснования
	|		ПО ВТСоставКнигиПокупок.ДокументРегистратор = СчетФактураПолученныйДокументыОснования.ДокументОснование
	|			И СчетФактураПолученныйДокументыОснования.СчетФактураПроведен
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСчетФактураВыданный КАК СчетФактураВыданныйДокументыОснования
	|		ПО ВТСоставКнигиПокупок.ДокументРегистратор = СчетФактураВыданныйДокументыОснования.ДокументОснование
	|			И СчетФактураВыданныйДокументыОснования.СчетФактураПроведен
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО (ДанныеПервичныхДокументов.Организация В (&Организация))
	|			И ВТСоставКнигиПокупок.ДокументРегистратор = ДанныеПервичныхДокументов.Документ";
	
	ВычетПоПриобретеннымЦенностямВключенные = Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСПредъявленныйОстатки.СчетФактура КАК ДокументРегистратор,
	|	НДСПредъявленныйОстатки.СтавкаНДС КАК СтавкаНДС,
	|	МАКСИМУМ(НДСПредъявленныйОстатки.ВидЦенности) КАК ВидЦенности,
	|	СУММА(НДСПредъявленныйОстатки.СуммаБезНДСОстаток) КАК СуммаБезНДС,
	|	СУММА(НДСПредъявленныйОстатки.НДСОстаток) КАК СуммаНДС
	|ПОМЕСТИТЬ НДСПредъявленныйОстатки
	|ИЗ
	|	РегистрНакопления.НДСПредъявленный.Остатки(
	|			&НачалоСледующегоПериода,
	|			Организация В (&Организация)
	|			И НЕ ВидЦенности В (&ВидыЦенностейАвансыВсе, &ВидыЦенностейНалоговыйАгент)
	|			И ВидДеятельностиНДС <> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт)) КАК НДСПредъявленныйОстатки
	|СГРУППИРОВАТЬ ПО
	|	НДСПредъявленныйОстатки.СчетФактура,
	|	НДСПредъявленныйОстатки.СтавкаНДС
	|;
	|
	|ВЫБРАТЬ
	|	БлокировкаВычетаНДССчетаФактуры.СчетФактура КАК СчетФактура,
	|	МАКСИМУМ(БлокировкаВычетаНДССчетаФактуры.Ссылка.Дата) КАК ДатаПоследнегоДокумента
	|ПОМЕСТИТЬ ДатыПоследнихОперацийПоБлокировке
	|ИЗ
	|	Документ.БлокировкаВычетаНДС.СчетаФактуры КАК БлокировкаВычетаНДССчетаФактуры
	|ГДЕ
	|	НЕ БлокировкаВычетаНДССчетаФактуры.Ссылка.ПометкаУдаления
	|	И БлокировкаВычетаНДССчетаФактуры.Ссылка.Дата <= &КонецПериода
	|	И БлокировкаВычетаНДССчетаФактуры.Ссылка.Организация В (&Организация)
	|СГРУППИРОВАТЬ ПО
	|	БлокировкаВычетаНДССчетаФактуры.СчетФактура
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура,
	|	ДатаПоследнегоДокумента
	|;
	|
	|ВЫБРАТЬ
	|	БлокировкаВычетаНДССчетаФактуры.СчетФактура КАК СчетФактура,
	|	МАКСИМУМ(БлокировкаВычетаНДССчетаФактуры.Ссылка) КАК Ссылка
	|ПОМЕСТИТЬ ВТ_СрезПоследнихДокументовБлокировки
	|ИЗ
	|	Документ.БлокировкаВычетаНДС.СчетаФактуры КАК БлокировкаВычетаНДССчетаФактуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДатыПоследнихОперацийПоБлокировке
	|		ПО ДатыПоследнихОперацийПоБлокировке.СчетФактура = БлокировкаВычетаНДССчетаФактуры.СчетФактура
	|			И ДатыПоследнихОперацийПоБлокировке.ДатаПоследнегоДокумента = БлокировкаВычетаНДССчетаФактуры.Ссылка.Дата
	|ГДЕ
	|	НЕ БлокировкаВычетаНДССчетаФактуры.Ссылка.ПометкаУдаления
	|СГРУППИРОВАТЬ ПО
	|	БлокировкаВычетаНДССчетаФактуры.СчетФактура
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура,
	|	Ссылка
	|;
	|
	|ВЫБРАТЬ
	|	ЗаблокированныеСФ.СчетФактура КАК СчетФактура
	|ПОМЕСТИТЬ БлокировкаВычета
	|ИЗ
	|	Документ.БлокировкаВычетаНДС.СчетаФактуры КАК ЗаблокированныеСФ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СрезПоследнихДокументовБлокировки КАК ВТ_СрезПоследнихДокументовБлокировки
	|		ПО ЗаблокированныеСФ.СчетФактура = ВТ_СрезПоследнихДокументовБлокировки.СчетФактура
	|			И ЗаблокированныеСФ.Ссылка = ВТ_СрезПоследнихДокументовБлокировки.Ссылка
	|ГДЕ
	|	(ЗаблокированныеСФ.СрокБлокировки = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ИЛИ ЗаблокированныеСФ.СрокБлокировки > &КонецПериода)
	|	И ЗаблокированныеСФ.Ссылка.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияБлокировкиВычетаНДС.Установлена)
	|;
	|
	|ВЫБРАТЬ
	|	НДСПредъявленныйОстатки.ДокументРегистратор КАК ДокументРегистратор,
	|	НДСПредъявленныйОстатки.СтавкаНДС КАК СтавкаНДС,
	|	НДСПредъявленныйОстатки.СуммаНДС КАК СуммаНДС,
	|	НДСПредъявленныйОстатки.СуммаБезНДС КАК Сумма,
	|	1 КАК КоличествоДокументов,
	|	ВЫБОР
	|		КОГДА НДСПредъявленныйОстатки.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ЭлектронныеУслуги)
	|			ТОГДА &ТекстНеПроизведенаОплатаПоставщику
	|		КОГДА НЕ ЗаявлениеОВвозеТоваров.Ссылка ЕСТЬ NULL
	|			И (ПолученоПодтверждение.ДатаПодтвержденияОплаты ЕСТЬ NULL
	|					ИЛИ ПолученоПодтверждение.ДатаПодтвержденияОплаты > &КонецПериода) ТОГДА
	|			  &ТекстНеПодтвержденаОплатаВБюджет
	|		КОГДА НЕ ТаможеннаяДекларацияИмпорт.Ссылка ЕСТЬ NULL
	|			И (ОплаченоВБюджет.ДатаПодтвержденияОплаты ЕСТЬ NULL
	|					ИЛИ ОплаченоВБюджет.ДатаПодтвержденияОплаты > &КонецПериода) ТОГДА
	|			  &ТекстНеПроизведенаОплатаВБюджет
	|		КОГДА СчетФактураПолученныйДокументыОснования.Ссылка ЕСТЬ NULL 
	|			ТОГДА &ТекстОтсутствуетСФПолученный
	|		КОГДА НЕ БлокировкаВычета.СчетФактура ЕСТЬ NULL
	|			ТОГДА &ТекстВычетНДСЗаблокирован
	|		КОГДА НЕ СчетФактураПолученныйДокументыОснования.СчетФактураПроведен
	|			ТОГДА &ТекстСФПолученныйНеПроведен
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК ПризнакНаличияОшибок,
	|	&ТекстНеОтраженыВКнигеПокупок КАК ПризнакОтраженияНДС,
	|	ВЫБОР
	|		КОГДА НДСПредъявленныйОстатки.ДокументРегистратор ССЫЛКА Документ.СчетФактураВыданный
	|				ИЛИ НДСПредъявленныйОстатки.ДокументРегистратор ССЫЛКА Документ.ТаможеннаяДекларацияИмпорт
	|				ИЛИ НДСПредъявленныйОстатки.ДокументРегистратор ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|			ТОГДА НДСПредъявленныйОстатки.ДокументРегистратор
	|		КОГДА НДСПредъявленныйОстатки.ДокументРегистратор Ссылка Документ.ОтчетКомиссионера
	|			ТОГДА СчетФактураПолученныйДокументыОснования.Ссылка
	|		ИНАЧЕ ЕСТЬNULL(СчетФактураПолученныйДокументыОснования.ССылка, СчетФактураВыданныйДокументыОснования.Ссылка)
	|	КОНЕЦ КАК СчетФактура,
	|	ВЫБОР
	|		КОГДА НДСПредъявленныйОстатки.ДокументРегистратор ССЫЛКА Документ.СчетФактураВыданный
	|				ИЛИ НДСПредъявленныйОстатки.ДокументРегистратор ССЫЛКА Документ.ТаможеннаяДекларацияИмпорт
	|				ИЛИ НДСПредъявленныйОстатки.ДокументРегистратор ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|			ТОГДА ДанныеПервичныхДокументов.НомерРегистратора
	|		КОГДА НДСПредъявленныйОстатки.ДокументРегистратор ССЫЛКА Документ.ОтчетКомиссионера
	|			ТОГДА СчетФактураПолученныйДокументыОснования.НомерВходящегоСчетаФактуры
	|		ИНАЧЕ ЕСТЬNULL(СчетФактураПолученныйДокументыОснования.НомерВходящегоСчетаФактуры, СчетФактураВыданныйДокументыОснования.НомерСчетаФактуры)
	|	КОНЕЦ КАК НомерВходящегоДокумента,
	|	ВЫБОР
	|		КОГДА НДСПредъявленныйОстатки.ДокументРегистратор ССЫЛКА Документ.СчетФактураВыданный
	|				ИЛИ НДСПредъявленныйОстатки.ДокументРегистратор ССЫЛКА Документ.ТаможеннаяДекларацияИмпорт
	|				ИЛИ НДСПредъявленныйОстатки.ДокументРегистратор ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|			ТОГДА ДанныеПервичныхДокументов.ДатаРегистратора
	|		КОГДА НДСПредъявленныйОстатки.ДокументРегистратор ССЫЛКА Документ.ОтчетКомиссионера
	|			ТОГДА СчетФактураПолученныйДокументыОснования.ДатаВходящегоСчетаФактуры
	|		ИНАЧЕ ЕСТЬNULL(СчетФактураПолученныйДокументыОснования.ДатаВходящегоСчетаФактуры, СчетФактураВыданныйДокументыОснования.ДатаСчетаФактуры)
	|	КОНЕЦ КАК ДатаВходящегоДокумента
	|ИЗ
	|	НДСПредъявленныйОстатки КАК НДСПредъявленныйОстатки
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		ВременнаяТаблицаСчетФактураПолученный КАК СчетФактураПолученныйДокументыОснования
	|	ПО 
	|		НДСПредъявленныйОстатки.ДокументРегистратор = СчетФактураПолученныйДокументыОснования.ДокументОснование
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		ВременнаяТаблицаСчетФактураВыданный КАК СчетФактураВыданныйДокументыОснования
	|	ПО 
	|		НДСПредъявленныйОстатки.ДокументРегистратор = СчетФактураВыданныйДокументыОснования.ДокументОснование
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Документ.ЗаявлениеОВвозеТоваров КАК ЗаявлениеОВвозеТоваров
	|	ПО 
	|		НДСПредъявленныйОстатки.ДокументРегистратор = ЗаявлениеОВвозеТоваров.Ссылка
	|		И НЕ ЗаявлениеОВвозеТоваров.ПометкаУдаления
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		РегистрСведений.ПодтверждениеОплатыНДСВБюджет КАК ПолученоПодтверждение
	|	ПО 
	|		ЗаявлениеОВвозеТоваров.Ссылка = ПолученоПодтверждение.СчетФактура
	|		И ПолученоПодтверждение.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияОплатыНДСВБюджет.ПолученоПодтверждение)
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Документ.ТаможеннаяДекларацияИмпорт КАК ТаможеннаяДекларацияИмпорт
	|	ПО 
	|		НДСПредъявленныйОстатки.ДокументРегистратор = ТаможеннаяДекларацияИмпорт.Ссылка
	|		И НЕ ТаможеннаяДекларацияИмпорт.ПометкаУдаления
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		РегистрСведений.ПодтверждениеОплатыНДСВБюджет КАК ОплаченоВБюджет
	|	ПО 
	|		ТаможеннаяДекларацияИмпорт.Ссылка = ОплаченоВБюджет.СчетФактура
	|		И ОплаченоВБюджет.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияОплатыНДСВБюджет.Оплачено)
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		БлокировкаВычета КАК БлокировкаВычета
	|	ПО
	|		НДСПредъявленныйОстатки.ДокументРегистратор = БлокировкаВычета.СчетФактура
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|	ПО 
	|		(ДанныеПервичныхДокументов.Организация В (&Организация))
	|		И НДСПредъявленныйОстатки.ДокументРегистратор = ДанныеПервичныхДокументов.Документ
	|";
	
	ВычетПоПриобретеннымЦенностямНеВключенныеРегламенты = Запрос.Выполнить();
	
	ВнешнийНаборДанныхВычетПриобретенныеЦенности = Новый Структура;
	ВнешнийНаборДанныхВычетПриобретенныеЦенности.Вставить("ВычетПоПриобретеннымЦенностямВключенные", ВычетПоПриобретеннымЦенностямВключенные);
	ВнешнийНаборДанныхВычетПриобретенныеЦенности.Вставить("ВычетПоПриобретеннымЦенностямНеВключенныеРегламенты", ВычетПоПриобретеннымЦенностямНеВключенныеРегламенты);
	ВнешнийНаборДанныхВычетПриобретенныеЦенности.Вставить("ВычетПоПриобретеннымЦенностямНеВключенныеПервичка", Новый ТаблицаЗначений);
	
	СтруктураВнешнихНаборов.Вставить("ВнешнийНаборДанныхВычетПриобретенныеЦенности", 
									 ВнешнийНаборДанныхВычетПриобретенныеЦенности);
	
КонецПроцедуры // ПолучитьНаборДанныхВычетПоПриобретеннымЦенностям()

// Выполняет запрос к базе данных для получения набора данных, необходимого для формирования расшифровки по
//	вычету НДС при исполнении обязанностей налогового агента.
//
// Параметры
//  Запрос  - Запрос - запрос к базе данных
//  СтруктураВнешнихНаборов - структура внешних источников данных для расшифровки.
//
// Возвращаемое значение:
//   Структура   - структура параметров для формирования карты отчета.
//
Процедура ПолучитьНаборДанныхВычетНалоговыйАгент(Запрос, СтруктураВнешнихНаборов) 
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСЗаписиКнигиПокупокОбороты.СчетФактура,
	|	НДСЗаписиКнигиПокупокОбороты.СтавкаНДС,
	|	НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПокупокОбороты.НДСОборот,
	|	НДСЗаписиКнигиПокупокОбороты.ДокументОплаты КАК ДокументОплаты,
	|	НДСЗаписиКнигиПокупокОбороты.Событие,
	|	НДСЗаписиКнигиПокупокОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПокупокОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПокупокОбороты.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ НДСЗаписиКнигиПокупокОборотыНалоговыйАгентВычеты
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			&НачалоПериода,
	|			&КонецПериодаГраница,
	|			Запись,
	|			ВидЦенности В (&ВидыЦенностейНалоговыйАгент)
	|				И Организация В (&Организация)
	|				И Событие <> ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.ПредъявленНДСКВычету0)
	|				И НЕ ЗаписьДополнительногоЛиста) КАК НДСЗаписиКнигиПокупокОбороты
	|ГДЕ
	|	НЕ НДСЗаписиКнигиПокупокОбороты.Регистратор ССЫЛКА Документ.ЗаписьКнигиПокупок
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПокупокОбороты.СчетФактура,
	|	НДСЗаписиКнигиПокупокОбороты.СтавкаНДС,
	|	НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПокупокОбороты.НДСОборот,
	|	НДСЗаписиКнигиПокупокОбороты.ДокументОплаты,
	|	НДСЗаписиКнигиПокупокОбороты.Событие,
	|	НДСЗаписиКнигиПокупокОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПокупокОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПокупокОбороты.НомерСтроки
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			&НачалоПериода,
	|			,
	|			Запись,
	|			ВидЦенности В (&ВидыЦенностейНалоговыйАгент)
	|				И Организация В (&Организация)
	|				И Событие <> ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.ПредъявленНДСКВычету0)
	|				И (ЗаписьДополнительногоЛиста
	|					И (КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода))) КАК НДСЗаписиКнигиПокупокОбороты
	|ГДЕ
	|	НЕ НДСЗаписиКнигиПокупокОбороты.Регистратор ССЫЛКА Документ.ЗаписьКнигиПокупок
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПокупокОбороты.СчетФактура,
	|	НДСЗаписиКнигиПокупокОбороты.СтавкаНДС,
	|	НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПокупокОбороты.НДСОборот,
	|	ЗаписьКнигиПокупокДокументыОплаты.ДокументОплаты,
	|	НДСЗаписиКнигиПокупокОбороты.Событие,
	|	НДСЗаписиКнигиПокупокОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПокупокОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПокупокОбороты.НомерСтроки
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			&НачалоПериода,
	|			&КонецПериодаГраница,
	|			Запись,
	|			ВидЦенности В (&ВидыЦенностейНалоговыйАгент)
	|				И Организация В (&Организация)
	|				И Событие <> ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.ПредъявленНДСКВычету0)
	|				И НЕ ЗаписьДополнительногоЛиста) КАК НДСЗаписиКнигиПокупокОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаписьКнигиПокупок.ДокументыОплаты КАК ЗаписьКнигиПокупокДокументыОплаты
	|		ПО НДСЗаписиКнигиПокупокОбороты.Регистратор = ЗаписьКнигиПокупокДокументыОплаты.Ссылка
	|ГДЕ
	|	НДСЗаписиКнигиПокупокОбороты.Регистратор ССЫЛКА Документ.ЗаписьКнигиПокупок
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСЗаписиКнигиПокупокОбороты.СчетФактура,
	|	НДСЗаписиКнигиПокупокОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПокупокОбороты.Событие,
	|	НДСЗаписиКнигиПокупокОбороты.ЗаписьДополнительногоЛиста,
	|	ЗаписьКнигиПокупокДокументыОплаты.ДокументОплаты,
	|	НДСЗаписиКнигиПокупокОбороты.СтавкаНДС,
	|	НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПокупокОбороты.НДСОборот,
	|	НДСЗаписиКнигиПокупокОбороты.НомерСтроки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОплаты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПодтверждениеОплатыНДСВБюджет.СчетФактура КАК СчетФактура,
	|	КОЛИЧЕСТВО(ПодтверждениеОплатыНДСВБюджет.СчетФактура) КАК КоличествоПлатежей,
	|	МАКСИМУМ(ПодтверждениеОплатыНДСВБюджет.СтрокаПлатежноРасчетныеДокументы) КАК СтрокаПлатежноРасчетныеДокументы
	|ПОМЕСТИТЬ втОплатыВБюджет
	|ИЗ
	|	РегистрСведений.ПодтверждениеОплатыНДСВБюджет КАК ПодтверждениеОплатыНДСВБюджет
	|ГДЕ
	|	ПодтверждениеОплатыНДСВБюджет.СчетФактура В (ВЫБРАТЬ РАЗЛИЧНЫЕ Т.СчетФактура ИЗ НДСЗаписиКнигиПокупокОборотыНалоговыйАгентВычеты КАК Т)
	|	И ПодтверждениеОплатыНДСВБюджет.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияОплатыНДСВБюджет.Оплачено)
	|СГРУППИРОВАТЬ ПО
	|	ПодтверждениеОплатыНДСВБюджет.СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПокупокОбороты.СчетФактура КАК ДокументРегистратор,
	|	НДСЗаписиКнигиПокупокОбороты.СтавкаНДС,
	|	НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот КАК Сумма,
	|	НДСЗаписиКнигиПокупокОбороты.НДСОборот КАК СуммаНДС,
	|	НДСЗаписиКнигиПокупокОбороты.ДокументОплаты КАК ДокументОплатыПоставщику,
	|	НДСЗаписиКнигиПокупокОбороты.СчетФактура КАК СчетФактура,
	|	ВЫБОР
	|		КОГДА ОплатыВБюджет.КоличествоПлатежей = 1 ТОГДА
	|			ОплатыВБюджет.СтрокаПлатежноРасчетныеДокументы
	|	ИНАЧЕ
	|		&НесколькоПлатежей
	|	КОНЕЦ КАК ДокументОплатыВБюджет,
	|	1 КАК КоличествоДокументов,
	|	&ТекстОтраженыВКнигеПокупок КАК ПризнакОтраженияНДС,
	|	&ТекстБезОшибок КАК ПризнакНаличияОшибок,
	|	НДСЗаписиКнигиПокупокОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПокупокОбороты.Событие,
	|	НДСЗаписиКнигиПокупокОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПокупокОбороты.НомерСтроки
	|ИЗ
	|	НДСЗаписиКнигиПокупокОборотыНалоговыйАгентВычеты КАК НДСЗаписиКнигиПокупокОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОплатыВБюджет КАК ОплатыВБюджет
	|		ПО ОплатыВБюджет.СчетФактура = НДСЗаписиКнигиПокупокОбороты.СчетФактура";
	
	ВычетПриИсполненииОНАВключенные = Запрос.Выполнить();
	
	ВнешнийНаборДанныхВычетИсполнениеОНА = Новый Структура;
	ВнешнийНаборДанныхВычетИсполнениеОНА.Вставить("ВычетПриИсполненииОНАВключенные",   ВычетПриИсполненииОНАВключенные);
	ВнешнийНаборДанныхВычетИсполнениеОНА.Вставить("ВычетПриИсполненииОНАНеВключенные", Новый ТаблицаЗначений);
	
	СтруктураВнешнихНаборов.Вставить("ВнешнийНаборДанныхВычетИсполнениеОНА", ВнешнийНаборДанныхВычетИсполнениеОНА);
		
КонецПроцедуры // ПолучитьНаборДанныхВычетИсполнениеОНА()

// Выполняет запрос к базе данных для получения набора данных, необходимого для формирования расшифровки по
//	вычету НДС с полученных авансов (по факту реализации) в случае ведения упрощенного учета НДС.
//
// Параметры
//  Запрос  - Запрос - запрос к базе данных
//  СтруктураВнешнихНаборов - структура внешних источников данных для расшифровки.
//
// Возвращаемое значение:
//   Структура   - структура параметров для формирования карты отчета.
//
Процедура ПолучитьНаборДанныхВычетСАвансовПолученныхУпрощенныйУчетНДС(Запрос, СтруктураВнешнихНаборов) 
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Субконто1 КАК Субконто1,
	|	ХозрасчетныйОстатки.Субконто2 КАК Субконто2
	|ПОМЕСТИТЬ ВТХозрасчетныйОстаткиСАвансовПолученныхУпрощенныйУчет
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&НачалоСледующегоПериода, Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСПоАвансамИПредоплатам), , Организация В (&Организация)) КАК ХозрасчетныйОстатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Субконто1, Субконто2
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПродажОбороты.СчетФактура КАК СчетФактура,
	|	НДСЗаписиКнигиПродажОбороты.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности КАК ВидЦенности,
	|	НДСЗаписиКнигиПродажОбороты.СтавкаНДС КАК СтавкаНДС,
	|	НДСЗаписиКнигиПродажОбороты.Покупатель,
	|	НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.Событие,
	|	НДСЗаписиКнигиПродажОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПродажОбороты.Исправление
	|ПОМЕСТИТЬ ВТНДСЗаписиКнигиПродажОборотыСАвансовПолученныхУпрощенныйУчет
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			,
	|			&КонецПериодаГраница,
	|			Авто,
	|			Организация В (&Организация)
	|				И (Покупатель, СчетФактура) В
	|					(ВЫБРАТЬ
	|						ХозрасчетныйОстатки.Субконто1 КАК Покупатель,
	|						ХозрасчетныйОстатки.Субконто2 КАК СчетФактура
	|					ИЗ
	|						ВТХозрасчетныйОстаткиСАвансовПолученныхУпрощенныйУчет КАК ХозрасчетныйОстатки)
	|				И ВидЦенности В (&ВидыЦенностейАвансыПолученные)
	|				И НЕ ЗаписьДополнительногоЛиста) КАК НДСЗаписиКнигиПродажОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПродажОбороты.СчетФактура,
	|	НДСЗаписиКнигиПродажОбороты.ДоговорКонтрагента,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПродажОбороты.СтавкаНДС,
	|	НДСЗаписиКнигиПродажОбороты.Покупатель,
	|	НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.Событие,
	|	НДСЗаписиКнигиПродажОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПродажОбороты.Исправление
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			,
	|			,
	|			Авто,
	|			Организация В (&Организация)
	|				И (Покупатель, СчетФактура) В
	|					(ВЫБРАТЬ
	|						ХозрасчетныйОстатки.Субконто1 КАК Покупатель,
	|						ХозрасчетныйОстатки.Субконто2 КАК СчетФактура
	|					ИЗ
	|						ВТХозрасчетныйОстаткиСАвансовПолученныхУпрощенныйУчет КАК ХозрасчетныйОстатки)
	|				И ВидЦенности В (&ВидыЦенностейАвансыПолученные)
	|				И (ЗаписьДополнительногоЛиста
	|					И (КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода))) КАК НДСЗаписиКнигиПродажОбороты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура,
	|	ДоговорКонтрагента,
	|	СтавкаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПокупокОбороты.СчетФактура КАК СчетФактура,
	|	НДСЗаписиКнигиПокупокОбороты.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	НДСЗаписиКнигиПокупокОбороты.Поставщик КАК Поставщик,
	|	НДСЗаписиКнигиПокупокОбороты.ВидЦенности КАК ВидЦенности,
	|	НДСЗаписиКнигиПокупокОбороты.СтавкаНДС КАК СтавкаНДС,
	|	НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПокупокОбороты.НДСОборот
	|ПОМЕСТИТЬ ВТНДСЗаписиКнигиПокупокОборотыСАвансовПолученныхУпрощенныйУчет
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			,
	|			&КонецПериодаГраница,
	|			,
	|			Организация В (&Организация)
	|				И (Поставщик, СчетФактура) В
	|					(ВЫБРАТЬ
	|						ХозрасчетныйОстатки.Субконто1 КАК Поставщик,
	|						ХозрасчетныйОстатки.Субконто2 КАК СчетФактура
	|					ИЗ
	|						ВТХозрасчетныйОстаткиСАвансовПолученныхУпрощенныйУчет КАК ХозрасчетныйОстатки)
	|				И ВидЦенности В (&ВидыЦенностейАвансыПолученные)
	|				И НЕ ЗаписьДополнительногоЛиста) КАК НДСЗаписиКнигиПокупокОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПокупокОбороты.СчетФактура,
	|	НДСЗаписиКнигиПокупокОбороты.ДоговорКонтрагента,
	|	НДСЗаписиКнигиПокупокОбороты.Поставщик,
	|	НДСЗаписиКнигиПокупокОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПокупокОбороты.СтавкаНДС,
	|	НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПокупокОбороты.НДСОборот
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			,
	|			,
	|			,
	|			Организация В (&Организация)
	|				И (Поставщик, СчетФактура) В
	|					(ВЫБРАТЬ
	|						ХозрасчетныйОстатки.Субконто1 КАК Поставщик,
	|						ХозрасчетныйОстатки.Субконто2 КАК СчетФактура
	|					ИЗ
	|						ВТХозрасчетныйОстаткиСАвансовПолученныхУпрощенныйУчет КАК ХозрасчетныйОстатки)
	|				И ВидЦенности В (&ВидыЦенностейАвансыПолученные)
	|				И (ЗаписьДополнительногоЛиста
	|					И (КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода))) КАК НДСЗаписиКнигиПокупокОбороты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура,
	|	ДоговорКонтрагента,
	|	Поставщик,
	|	СтавкаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Субконто1 КАК Субконто1,
	|	ХозрасчетныйОстатки.Субконто2 КАК Субконто2,
	|	ХозрасчетныйОстатки.СуммаОстатокКт
	|ПОМЕСТИТЬ ХозрасчетныйОстаткиСАвансовПолученныхУпрощенныйУчет
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&НачалоСледующегоПериода, Счет В (&СписокСчетовАвансовПолученных),, Организация В (&Организация)) КАК ХозрасчетныйОстатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Субконто1,Субконто2
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПродажОбороты.СтавкаНДС КАК СтавкаНДС,
	|	НДСЗаписиКнигиПродажОбороты.СчетФактура КАК ДокументРегистратор,
	|	НДСЗаписиКнигиПродажОбороты.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	СчетФактураВыданныйАванс.Ссылка КАК СчетФактура,
	|	СУММА(ЕСТЬNULL(НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот, 0) - ЕСТЬNULL(НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот, 0)) КАК Сумма,
	|	СУММА(ЕСТЬNULL(НДСЗаписиКнигиПродажОбороты.НДСОборот, 0) - ЕСТЬNULL(НДСЗаписиКнигиПокупокОбороты.НДСОборот, 0)) КАК СуммаНДС,
	|	ХозрасчетныйОстатки.СуммаОстатокКт,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот, 0) - ЕСТЬNULL(НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот, 0) + ЕСТЬNULL(НДСЗаписиКнигиПродажОбороты.НДСОборот, 0) - ЕСТЬNULL(НДСЗаписиКнигиПокупокОбороты.НДСОборот, 0) - ХозрасчетныйОстатки.СуммаОстатокКт = 0
	|			ТОГДА &ТекстОплаченныеЦенностиНеРеализованы
	|		ИНАЧЕ &ТекстВычетНеОтраженВКнигеПокупок
	|	КОНЕЦ КАК ПризнакНаличияОшибок,
	|	&ТекстНеОтраженыВКнигеПокупок КАК ПризнакОтраженияНДС,
	|	1 КАК КоличествоДокументов,
	|	НДСЗаписиКнигиПродажОбороты.Событие,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПродажОбороты.ЗаписьДополнительногоЛиста
	|ИЗ
	|	ВТНДСЗаписиКнигиПродажОборотыСАвансовПолученныхУпрощенныйУчет КАК НДСЗаписиКнигиПродажОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНДСЗаписиКнигиПокупокОборотыСАвансовПолученныхУпрощенныйУчет КАК НДСЗаписиКнигиПокупокОбороты
	|		ПО (НДСЗаписиКнигиПокупокОбороты.СчетФактура = НДСЗаписиКнигиПродажОбороты.СчетФактура)
	|			И (НДСЗаписиКнигиПокупокОбороты.ДоговорКонтрагента = НДСЗаписиКнигиПродажОбороты.ДоговорКонтрагента)
	|			И (НДСЗаписиКнигиПокупокОбороты.Поставщик = НДСЗаписиКнигиПродажОбороты.Покупатель)
	|			И (НДСЗаписиКнигиПокупокОбороты.СтавкаНДС = НДСЗаписиКнигиПродажОбороты.СтавкаНДС)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ХозрасчетныйОстаткиСАвансовПолученныхУпрощенныйУчет КАК ХозрасчетныйОстатки
	|		ПО НДСЗаписиКнигиПродажОбороты.СчетФактура = ХозрасчетныйОстатки.Субконто2
	|		 	И НДСЗаписиКнигиПродажОбороты.Покупатель = ХозрасчетныйОстатки.Субконто1
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданныйАванс КАК СчетФактураВыданныйАванс
	|		ПО НДСЗаписиКнигиПродажОбороты.СчетФактура = СчетФактураВыданныйАванс.ДокументОснование
	|			И НДСЗаписиКнигиПродажОбороты.Исправление = СчетФактураВыданныйАванс.Исправление
	|			И НДСЗаписиКнигиПродажОбороты.Покупатель = СчетФактураВыданныйАванс.Контрагент
	|ГДЕ
	|	ЕСТЬNULL(НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот, 0) - ЕСТЬNULL(НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот, 0) <> 0
	|	И ЕСТЬNULL(НДСЗаписиКнигиПродажОбороты.НДСОборот, 0) - ЕСТЬNULL(НДСЗаписиКнигиПокупокОбороты.НДСОборот, 0) <> 0
	|	И НЕ СчетФактураВыданныйАванс.Ссылка.ПометкаУдаления
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСЗаписиКнигиПродажОбороты.Покупатель,
	|	НДСЗаписиКнигиПродажОбороты.ДоговорКонтрагента,
	|	НДСЗаписиКнигиПродажОбороты.СчетФактура,
	|	СчетФактураВыданныйАванс.Ссылка,
	|	НДСЗаписиКнигиПродажОбороты.СтавкаНДС,
	|	НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПокупокОбороты.НДСОборот,
	|	ХозрасчетныйОстатки.СуммаОстатокКт,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПродажОбороты.Событие,
	|	НДСЗаписиКнигиПродажОбороты.ЗаписьДополнительногоЛиста,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот, 0) - ЕСТЬNULL(НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот, 0) + ЕСТЬNULL(НДСЗаписиКнигиПродажОбороты.НДСОборот, 0) - ЕСТЬNULL(НДСЗаписиКнигиПокупокОбороты.НДСОборот, 0) - ХозрасчетныйОстатки.СуммаОстатокКт = 0
	|			ТОГДА &ТекстОплаченныеЦенностиНеРеализованы
	|		ИНАЧЕ &ТекстВычетНеОтраженВКнигеПокупок
	|	КОНЕЦ";
	
	ВычетСАвансовПолученныхУпрощенныйУчетНДСНеВключенные = Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСЗаписиКнигиПокупок.СчетФактура КАК СчетФактура,
	|	НДСЗаписиКнигиПокупок.Поставщик,
	|	НДСЗаписиКнигиПокупок.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	НДСЗаписиКнигиПокупок.СтавкаНДС,
	|	НДСЗаписиКнигиПокупок.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПокупок.НДСОборот,
	|	НДСЗаписиКнигиПокупок.Событие,
	|	НДСЗаписиКнигиПокупок.ВидЦенности,
	|	НДСЗаписиКнигиПокупок.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПокупок.НомерСтроки
	|ПОМЕСТИТЬ ВТНДСЗаписиКнигиПокупокВЧастиАвансовПолученных
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			&НачалоПериода,
	|			&КонецПериодаГраница,
	|			Запись,
	|			Организация В (&Организация)
	|				И ВидЦенности В (&ВидыЦенностейАвансыПолученные)
	|				И НЕ ЗаписьДополнительногоЛиста) КАК НДСЗаписиКнигиПокупок
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПокупок.СчетФактура,
	|	НДСЗаписиКнигиПокупок.Поставщик,
	|	НДСЗаписиКнигиПокупок.ДоговорКонтрагента,
	|	НДСЗаписиКнигиПокупок.СтавкаНДС,
	|	НДСЗаписиКнигиПокупок.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПокупок.НДСОборот,
	|	НДСЗаписиКнигиПокупок.Событие,
	|	НДСЗаписиКнигиПокупок.ВидЦенности,
	|	НДСЗаписиКнигиПокупок.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПокупок.НомерСтроки
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			&НачалоПериода,
	|			,
	|			Запись,
	|			Организация В (&Организация)
	|				И ВидЦенности В (&ВидыЦенностейАвансыПолученные)
	|				И (ЗаписьДополнительногоЛиста
	|					И (КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода))) КАК НДСЗаписиКнигиПокупок
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетФактураВыданныйДокументыОснования.Ссылка,
	|	СчетФактураВыданныйДокументыОснования.Ссылка.Контрагент КАК Контрагент,
	|	СчетФактураВыданныйИсправительный.Ссылка КАК СчетФактураИсправительный,
	|	СчетФактураВыданныйДокументыОснования.ДокументОснование
	|ПОМЕСТИТЬ ВТСчетФактураВыданныйДокументыОснования
	|ИЗ
	|	Документ.СчетФактураВыданныйАванс КАК СчетФактураВыданныйДокументыОснования
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданныйАванс КАК СчетФактураВыданныйИсправительный
	|		ПО (СчетФактураВыданныйИсправительный.СчетФактураОснование = СчетФактураВыданныйДокументыОснования.Ссылка)
	|			И (СчетФактураВыданныйИсправительный.Исправление)
	|			И (СчетФактураВыданныйИсправительный.Проведен)
	|ГДЕ
	|	НЕ СчетФактураВыданныйДокументыОснования.Исправление
	|	И СчетФактураВыданныйДокументыОснования.Проведен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НДСЗаписиКнигиПокупок.СтавкаНДС КАК СтавкаНДС,
	|	НДСЗаписиКнигиПокупок.СчетФактура КАК ДокументРегистратор,
	|	НДСЗаписиКнигиПокупок.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ЕСТЬNULL(СчетФактураВыданныйДокументыОснования.СчетФактураИсправительный, СчетФактураВыданныйДокументыОснования.Ссылка) КАК СчетФактура,
	|	НДСЗаписиКнигиПокупок.СуммаБезНДСОборот КАК Сумма,
	|	НДСЗаписиКнигиПокупок.НДСОборот КАК СуммаНДС,
	|	ХозрасчетныйОстатки.СуммаОстатокКт,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОстатки.СуммаОстатокКт ЕСТЬ NULL 
	|			ТОГДА &ТекстНДСПринятКВычетуПолностью
	|		ИНАЧЕ &ТекстНДСПринятКВычетуЧастично
	|	КОНЕЦ КАК ПризнакНаличияОшибок,
	|	&ТекстОтраженыВКнигеПокупок КАК ПризнакОтраженияНДС,
	|	1 КАК КоличествоДокументов,
	|	НДСЗаписиКнигиПокупок.ВидЦенности,
	|	НДСЗаписиКнигиПокупок.Событие,
	|	НДСЗаписиКнигиПокупок.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПокупок.НомерСтроки
	|ИЗ
	|	ВТНДСЗаписиКнигиПокупокВЧастиАвансовПолученных КАК НДСЗаписиКнигиПокупок
	|		ЛЕВОЕ СОЕДИНЕНИЕ ХозрасчетныйОстаткиСАвансовПолученныхУпрощенныйУчет КАК ХозрасчетныйОстатки
	|		ПО НДСЗаписиКнигиПокупок.СчетФактура = ХозрасчетныйОстатки.Субконто1
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСчетФактураВыданныйДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		ПО (НДСЗаписиКнигиПокупок.СчетФактура = СчетФактураВыданныйДокументыОснования.ДокументОснование
	|				ИЛИ НДСЗаписиКнигиПокупок.СчетФактура = СчетФактураВыданныйДокументыОснования.Ссылка)
	|				И НДСЗаписиКнигиПокупок.Поставщик = СчетФактураВыданныйДокументыОснования.Контрагент
	|ГДЕ
	|	СчетФактураВыданныйДокументыОснования.Ссылка ЕСТЬ NULL 
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НДСЗаписиКнигиПокупок.СтавкаНДС КАК СтавкаНДС,
	|	НДСЗаписиКнигиПокупок.СчетФактура КАК ДокументРегистратор,
	|	НДСЗаписиКнигиПокупок.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ЕСТЬNULL(СчетФактураВыданныйДокументыОснования.СчетФактураИсправительный, СчетФактураВыданныйДокументыОснования.Ссылка) КАК СчетФактура,
	|	НДСЗаписиКнигиПокупок.СуммаБезНДСОборот КАК Сумма,
	|	НДСЗаписиКнигиПокупок.НДСОборот КАК СуммаНДС,
	|	ХозрасчетныйОстатки.СуммаОстатокКт,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОстатки.СуммаОстатокКт ЕСТЬ NULL 
	|			ТОГДА &ТекстНДСПринятКВычетуПолностью
	|		ИНАЧЕ &ТекстНДСПринятКВычетуЧастично
	|	КОНЕЦ КАК ПризнакНаличияОшибок,
	|	&ТекстОтраженыВКнигеПокупок КАК ПризнакОтраженияНДС,
	|	1 КАК КоличествоДокументов,
	|	НДСЗаписиКнигиПокупок.ВидЦенности,
	|	НДСЗаписиКнигиПокупок.Событие,
	|	НДСЗаписиКнигиПокупок.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПокупок.НомерСтроки
	|ИЗ
	|	ВТНДСЗаписиКнигиПокупокВЧастиАвансовПолученных КАК НДСЗаписиКнигиПокупок
	|		ЛЕВОЕ СОЕДИНЕНИЕ ХозрасчетныйОстаткиСАвансовПолученныхУпрощенныйУчет КАК ХозрасчетныйОстатки
	|		ПО НДСЗаписиКнигиПокупок.СчетФактура = ХозрасчетныйОстатки.Субконто1
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСчетФактураВыданныйДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		ПО (НДСЗаписиКнигиПокупок.СчетФактура = СчетФактураВыданныйДокументыОснования.ДокументОснование
	|				ИЛИ НДСЗаписиКнигиПокупок.СчетФактура = СчетФактураВыданныйДокументыОснования.Ссылка)
	|				И НДСЗаписиКнигиПокупок.Поставщик = СчетФактураВыданныйДокументыОснования.Контрагент
	|ГДЕ
	|	СчетФактураВыданныйДокументыОснования.Ссылка.Проведен";
	
	ВычетСАвансовПолученныхУпрощенныйУчетНДСВключенные = Запрос.Выполнить();
	
	ВнешнийНаборДанныхВычетСАвансовПолученныхУпрощенныйУчетНДС = Новый Структура;
	
	ВнешнийНаборДанныхВычетСАвансовПолученныхУпрощенныйУчетНДС.Вставить("ВычетСАвансовПолученныхУпрощенныйУчетНДСВключенные",
	ВычетСАвансовПолученныхУпрощенныйУчетНДСВключенные);
	
	ВнешнийНаборДанныхВычетСАвансовПолученныхУпрощенныйУчетНДС.Вставить("ВычетСАвансовПолученныхУпрощенныйУчетНДСНеВключенные",
	ВычетСАвансовПолученныхУпрощенныйУчетНДСНеВключенные);
	
	СтруктураВнешнихНаборов.Вставить("ВнешнийНаборДанныхВычетСАвансовПолученныхУпрощенныйУчетНДС",
	ВнешнийНаборДанныхВычетСАвансовПолученныхУпрощенныйУчетНДС);
		
КонецПроцедуры // ПолучитьНаборДанныхВычетСАвансовПолученныхУпрощенныйУчетНДС()

// Выполняет запрос к базе данных для получения набора данных, необходимого для формирования расшифровки по
//	вычету НДС с выданных авансов в случае ведения упрощенного учета НДС.
//
// Параметры
//  Запрос  - Запрос - запрос к базе данных
//  СтруктураВнешнихНаборов - структура внешних источников данных для расшифровки.
//
// Возвращаемое значение:
//   Структура   - структура параметров для формирования карты отчета.
//
Процедура ПолучитьНаборДанныхВычетАвансыВыданныеУпрощенныйУчетНДС(Запрос, СтруктураВнешнихНаборов) 
	
	ПараметрыРасчетаАвансов = УчетНДСУПСлужебный.ИнициализироватьПараметрыПодготовкиРасчетовАвансовВЦеляхНДС();
	
	ПараметрыРасчетаАвансов.ДатаНачала    = Запрос.Параметры.НачалоПериода;
	ПараметрыРасчетаАвансов.ДатаОкончания = Запрос.Параметры.КонецПериода;
	ПараметрыРасчетаАвансов.Организации   = Запрос.Параметры.Организация;  
	
	ПараметрыРасчетаАвансов.Организации_АвансыВыданные          = Запрос.Параметры.ОрганизацииНеНаЛьготномНалогообложении;
	ПараметрыРасчетаАвансов.ЗаполнятьОрганизации_АвансыВыданные = Ложь;
	
	УчетНДСУПСлужебный.ПодготовитьВТ_АвансыВыданные_Возникновения(
			Запрос.МенеджерВременныхТаблиц,
			ПараметрыРасчетаАвансов);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	АвансыВыданные.ДокументРегистратор                                    КАК ДокументРегистратор,
	|	&СтавкаНДСПоУмолчанию                                                 КАК СтавкаНДСПредположительная,
	|	СУММА(ВЫРАЗИТЬ(АвансыВыданные.СуммаАванса
	|			- АвансыВыданные.СуммаАванса
	|				* &ПроцентНДС / (100 + &ПроцентНДС) КАК ЧИСЛО(31,2)))     КАК СуммаБезНДС,
	|	СУММА(ВЫРАЗИТЬ(АвансыВыданные.СуммаАванса
	|					* &ПроцентНДС / (100 + &ПроцентНДС) КАК ЧИСЛО(31,2))) КАК СуммаНДС
	|ПОМЕСТИТЬ ВТАвансыВыданныеВременнаяТаблица
	|ИЗ
	|	АвансыВыданные_Возникновения КАК АвансыВыданные
	|
	|СГРУППИРОВАТЬ ПО
	|	АвансыВыданные.ДокументРегистратор,
	|	АвансыВыданные.ДатаАванса
	|
	|ИМЕЮЩИЕ
	|	СУММА(АвансыВыданные.СуммаАванса) > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументРегистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ АвансыВыданные_Возникновения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасшифровкаПлатежа.Ссылка КАК Ссылка,
	|	РасшифровкаПлатежа.СтавкаНДС.СоответствующаяРасчетнаяСтавка КАК СтавкаНДС,
	|	СУММА(РасшифровкаПлатежа.СуммаНДС) КАК СуммаНДС,
	|	СУММА(РасшифровкаПлатежа.Сумма - РасшифровкаПлатежа.СуммаНДС) КАК СуммаБезНДС
	|ПОМЕСТИТЬ СуммыПлатежей
	|ИЗ
	|	Документ.СписаниеБезналичныхДенежныхСредств.РасшифровкаПлатежа КАК РасшифровкаПлатежа
	|ГДЕ
	|	РасшифровкаПлатежа.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТАвансыВыданныеВременнаяТаблица.ДокументРегистратор
	|			ИЗ
	|				ВТАвансыВыданныеВременнаяТаблица)
	|
	|СГРУППИРОВАТЬ ПО
	|	РасшифровкаПлатежа.Ссылка,
	|	РасшифровкаПлатежа.СтавкаНДС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасшифровкаПлатежа.Ссылка,
	|	РасшифровкаПлатежа.СтавкаНДС.СоответствующаяРасчетнаяСтавка,
	|	СУММА(РасшифровкаПлатежа.СуммаНДС),
	|	СУММА(РасшифровкаПлатежа.Сумма - РасшифровкаПлатежа.СуммаНДС)
	|ИЗ
	|	Документ.РасходныйКассовыйОрдер.РасшифровкаПлатежа КАК РасшифровкаПлатежа
	|ГДЕ
	|	РасшифровкаПлатежа.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТАвансыВыданныеВременнаяТаблица.ДокументРегистратор
	|			ИЗ
	|				ВТАвансыВыданныеВременнаяТаблица)
	|
	|СГРУППИРОВАТЬ ПО
	|	РасшифровкаПлатежа.Ссылка,
	|	РасшифровкаПлатежа.СтавкаНДС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТАвансыВыданныеВременнаяТаблица.ДокументРегистратор КАК ДокументРегистратор,
	|	ЕСТЬNULL(СуммыПлатежей.СтавкаНДС, ВТАвансыВыданныеВременнаяТаблица.СтавкаНДСПредположительная) КАК СтавкаНДС,
	|	ЕСТЬNULL(СуммыПлатежей.СуммаБезНДС, ВТАвансыВыданныеВременнаяТаблица.СуммаБезНДС) КАК Сумма,
	|	ЕСТЬNULL(СуммыПлатежей.СуммаНДС, ВТАвансыВыданныеВременнаяТаблица.СуммаНДС) КАК СуммаНДС
	|ПОМЕСТИТЬ ВТАвансыВыданные
	|ИЗ
	|	ВТАвансыВыданныеВременнаяТаблица КАК ВТАвансыВыданныеВременнаяТаблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ СуммыПлатежей КАК СуммыПлатежей
	|		ПО ВТАвансыВыданныеВременнаяТаблица.ДокументРегистратор = СуммыПлатежей.Ссылка
	|ГДЕ
	|	ЕСТЬNULL(СуммыПлатежей.СуммаНДС, ВТАвансыВыданныеВременнаяТаблица.СуммаНДС) > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументРегистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПокупокОбороты.СчетФактура КАК ДокументРегистратор,
	|	НДСЗаписиКнигиПокупокОбороты.СтавкаНДС КАК СтавкаНДС,
	|	НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот КАК Сумма,
	|	НДСЗаписиКнигиПокупокОбороты.НДСОборот КАК СуммаНДС,
	|	НДСЗаписиКнигиПокупокОбороты.Событие,
	|	НДСЗаписиКнигиПокупокОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПокупокОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПокупокОбороты.ДоговорКонтрагента,
	|	НДСЗаписиКнигиПокупокОбороты.НомерСтроки,
	|	ВЫБОР
	|		КОГДА НДСЗаписиКнигиПокупокОбороты.ИсправленныйСчетФактура = НЕОПРЕДЕЛЕНО
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ИсправленныйСчетФактура
	|ПОМЕСТИТЬ СоставКнигиПокупокВЧастиАвансов
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			&НачалоПериода,
	|			&КонецПериодаГраница,
	|			Запись,
	|			Организация В (&Организация)
	|				И ВидЦенности В (&ВидыЦенностейАвансыВыданные)
	|				И НЕ ЗаписьДополнительногоЛиста) КАК НДСЗаписиКнигиПокупокОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПокупокОбороты.СчетФактура,
	|	НДСЗаписиКнигиПокупокОбороты.СтавкаНДС,
	|	НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПокупокОбороты.НДСОборот,
	|	НДСЗаписиКнигиПокупокОбороты.Событие,
	|	НДСЗаписиКнигиПокупокОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПокупокОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПокупокОбороты.ДоговорКонтрагента,
	|	НДСЗаписиКнигиПокупокОбороты.НомерСтроки,
	|	ВЫБОР
	|		КОГДА НДСЗаписиКнигиПокупокОбороты.ИсправленныйСчетФактура = НЕОПРЕДЕЛЕНО
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			&НачалоПериода,
	|			,
	|			Запись,
	|			Организация В (&Организация)
	|				И ВидЦенности В (&ВидыЦенностейАвансыВыданные)
	|				И (ЗаписьДополнительногоЛиста
	|					И (КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода))) КАК НДСЗаписиКнигиПокупокОбороты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументРегистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АвансыВыданные.ДокументРегистратор КАК ДокументРегистратор,
	|	СчетФактураПолученныйАванс.Ссылка КАК СчетФактура,
	|	ЕСТЬNULL(СоставКнигиПокупокВЧастиАвансов.СтавкаНДС, АвансыВыданные.СтавкаНДС) КАК СтавкаНДС,
	|	ЕСТЬNULL(СоставКнигиПокупокВЧастиАвансов.Сумма, АвансыВыданные.Сумма) КАК Сумма,
	|	ЕСТЬNULL(СоставКнигиПокупокВЧастиАвансов.СуммаНДС, АвансыВыданные.СуммаНДС) КАК СуммаНДС,
	|	ВЫБОР
	|		КОГДА СчетФактураПолученныйАванс.Ссылка ЕСТЬ NULL 
	|			ТОГДА &ТекстВыданныйАвансБезСФ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА СчетФактураПолученныйАванс.Ссылка.Проведен
	|					ТОГДА ВЫБОР
	|							КОГДА СоставКнигиПокупокВЧастиАвансов.ДокументРегистратор ЕСТЬ NULL 
	|								ТОГДА &ТекстВычетНеОтраженВКнигеПокупок
	|							ИНАЧЕ &ТекстБезОшибок
	|						КОНЕЦ
	|				ИНАЧЕ &ТекстСФНаВыданныйАвансНеПроведен
	|			КОНЕЦ
	|	КОНЕЦ КАК ПризнакНаличияОшибок,
	|	ВЫБОР
	|		КОГДА СоставКнигиПокупокВЧастиАвансов.ДокументРегистратор ЕСТЬ NULL 
	|			ТОГДА &ТекстНеОтраженыВКнигеПокупок
	|		ИНАЧЕ &ТекстОтраженыВКнигеПокупок
	|	КОНЕЦ КАК ПризнакОтраженияНДС,
	|	ВЫБОР
	|		КОГДА СоставКнигиПокупокВЧастиАвансов.ДокументРегистратор ЕСТЬ NULL 
	|			ТОГДА ЕСТЬNULL(СоставКнигиПокупокВЧастиАвансов.СуммаНДС, АвансыВыданные.СуммаНДС)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаНеВключеннаяВБазу,
	|	ВЫБОР
	|		КОГДА СоставКнигиПокупокВЧастиАвансов.ДокументРегистратор ЕСТЬ NULL 
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(СоставКнигиПокупокВЧастиАвансов.СуммаНДС, АвансыВыданные.СуммаНДС)
	|	КОНЕЦ КАК СуммаВключеннаяВБазу,
	|	1 КАК КоличествоДокументов,
	|	ЕСТЬNULL(СоставКнигиПокупокВЧастиАвансов.ВидЦенности, ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)) КАК ВидЦенности,
	|	ЕСТЬNULL(СоставКнигиПокупокВЧастиАвансов.Событие, ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.ПустаяСсылка)) КАК Событие,
	|	ЕСТЬNULL(СоставКнигиПокупокВЧастиАвансов.ЗаписьДополнительногоЛиста, ЛОЖЬ) КАК ЗаписьДополнительногоЛиста,
	|	СоставКнигиПокупокВЧастиАвансов.НомерСтроки
	|ИЗ
	|	ВТАвансыВыданные КАК АвансыВыданные
	|		ЛЕВОЕ СОЕДИНЕНИЕ СоставКнигиПокупокВЧастиАвансов КАК СоставКнигиПокупокВЧастиАвансов
	|		ПО АвансыВыданные.ДокументРегистратор = СоставКнигиПокупокВЧастиАвансов.ДокументРегистратор
	|			И АвансыВыданные.СтавкаНДС.ПеречислениеСтавкаНДС = СоставКнигиПокупокВЧастиАвансов.СтавкаНДС
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученныйАванс КАК СчетФактураПолученныйАванс
	|		ПО (СоставКнигиПокупокВЧастиАвансов.ДокументРегистратор = СчетФактураПолученныйАванс.ДокументОснование)
	|			И (НЕ СчетФактураПолученныйАванс.ПометкаУдаления)
	|			И (СоставКнигиПокупокВЧастиАвансов.ИсправленныйСчетФактура = СчетФактураПолученныйАванс.Исправление)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СоставКнигиПокупокВЧастиАвансов.ДокументРегистратор КАК ДокументРегистратор,
	|	СчетФактураПолученныйАванс.Ссылка КАК СчетФактура,
	|	СоставКнигиПокупокВЧастиАвансов.СтавкаНДС КАК СтавкаНДС,
	|	СоставКнигиПокупокВЧастиАвансов.Сумма КАК Сумма,
	|	СоставКнигиПокупокВЧастиАвансов.СуммаНДС КАК СуммаНДС,
	|	ВЫБОР
	|		КОГДА СчетФактураПолученныйАванс.Ссылка ЕСТЬ NULL 
	|			ТОГДА &ТекстВыданныйАвансБезСФ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА СчетФактураПолученныйАванс.Ссылка.Проведен
	|					ТОГДА  &ТекстБезОшибок
	|				ИНАЧЕ &ТекстСФНаВыданныйАвансНеПроведен
	|			КОНЕЦ
	|	КОНЕЦ КАК ПризнакНаличияОшибок,
	|	&ТекстОтраженыВКнигеПокупок КАК ПризнакОтраженияНДС,
	|	0  КАК СуммаНеВключеннаяВБазу,
	|	СоставКнигиПокупокВЧастиАвансов.СуммаНДС КАК СуммаВключеннаяВБазу,
	|	1 КАК КоличествоДокументов,
	|	СоставКнигиПокупокВЧастиАвансов.ВидЦенности КАК ВидЦенности,
	|	СоставКнигиПокупокВЧастиАвансов.Событие КАК Событие,
	|	СоставКнигиПокупокВЧастиАвансов.ЗаписьДополнительногоЛиста КАК ЗаписьДополнительногоЛиста,
	|	СоставКнигиПокупокВЧастиАвансов.НомерСтроки
	|ИЗ
	|	СоставКнигиПокупокВЧастиАвансов КАК СоставКнигиПокупокВЧастиАвансов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТАвансыВыданные КАК АвансыВыданные
	|		ПО АвансыВыданные.ДокументРегистратор = СоставКнигиПокупокВЧастиАвансов.ДокументРегистратор
	|			И АвансыВыданные.СтавкаНДС.ПеречислениеСтавкаНДС = СоставКнигиПокупокВЧастиАвансов.СтавкаНДС
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученныйАванс КАК СчетФактураПолученныйАванс
	|		ПО (СоставКнигиПокупокВЧастиАвансов.ДокументРегистратор = СчетФактураПолученныйАванс.ДокументОснование)
	|			И (НЕ СчетФактураПолученныйАванс.ПометкаУдаления)
	|			И (СоставКнигиПокупокВЧастиАвансов.ИсправленныйСчетФактура = СчетФактураПолученныйАванс.Исправление)
	|ГДЕ
	|	АвансыВыданные.ДокументРегистратор ЕСТЬ NULL
	|";
	
	ПодстановкаСтавкаНДС = УчетНДСРФ.ТекстВыбораСтавкиНДСПоУмолчанию(Запрос.Параметры, "АвансыВыданные.ДатаАванса", Истина);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&СтавкаНДСПоУмолчанию", ПодстановкаСтавкаНДС);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПроцентНДС", УчетНДСРФ.ТекстВыбораПроцентаНДС(ПодстановкаСтавкаНДС));
	
	ВычетАвансыВыданныеУпрощенныйУчетНДС = Запрос.Выполнить();
	
	ВнешнийНаборДанныхВычетАвансыВыданныеУпрощенныйУчетНДС = Новый Структура;
	
	ВнешнийНаборДанныхВычетАвансыВыданныеУпрощенныйУчетНДС.Вставить("ВычетАвансыВыданныеУпрощенныйУчетНДС",
																		ВычетАвансыВыданныеУпрощенныйУчетНДС);
		
	СтруктураВнешнихНаборов.Вставить("ВнешнийНаборДанныхВычетАвансыВыданныеУпрощенныйУчетНДС",
									 	ВнешнийНаборДанныхВычетАвансыВыданныеУпрощенныйУчетНДС);
	
КонецПроцедуры // ПолучитьНаборДанныхВычетАвансыВыданныеУпрощенныйУчетНДС()

// Выполняет запрос к базе данных для получения набора данных, необходимого для формирования расшифровки по
//	вычету НДС по приобретенным ценностям, реализованным по ставке НДС 0%.
//
// Параметры
//  Запрос  - Запрос - запрос к базе данных
//  СтруктураВнешнихНаборов - структура внешних источников данных для расшифровки.
//
// Возвращаемое значение:
//   Структура   - структура параметров для формирования карты отчета.
//
Процедура ПолучитьНаборДанныхВычетПоПриобретеннымЦенностям0(Запрос, СтруктураВнешнихНаборов) 
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СоставКнигиПокупок.Организация,
	|	СоставКнигиПокупок.СчетФактура КАК СчетФактура,
	|	СоставКнигиПокупок.ДокументОтгрузки КАК ДокументОтгрузки,
	|	СоставКнигиПокупок.СтавкаНДС,
	|	СУММА(СоставКнигиПокупок.СуммаБезНДС),
	|	СУММА(СоставКнигиПокупок.НДС),
	|	СоставКнигиПокупок.Событие,
	|	СоставКнигиПокупок.ВидЦенности,
	|	СоставКнигиПокупок.ЗаписьДополнительногоЛиста,
	|	СоставКнигиПокупок.НомерСтроки,
	|	СоставКнигиПокупок.ВидДеятельностиНДС
	|ПОМЕСТИТЬ ВТСоставКнигиПокупокПоПриобретеннымЦенностям0
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок КАК СоставКнигиПокупок
	|ГДЕ
	|	СоставКнигиПокупок.Организация В(&Организация)
	|	И НЕ СоставКнигиПокупок.ЗаписьДополнительногоЛиста
	|	И СоставКнигиПокупок.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И СоставКнигиПокупок.Событие В (ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.ПредъявленНДСКВычету0),
	|									ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.НеПодтвержденаСтавка0))
	|	И СоставКнигиПокупок.Активность
	|	И НЕ СоставКнигиПокупок.ВидЦенности В (&ВидыЦенностейАвансыВсе)
	|	И НЕ СоставКнигиПокупок.ВидЦенности В (&ВидыЦенностейНалоговыйАгент)
	|
	|СГРУППИРОВАТЬ ПО
	|	СоставКнигиПокупок.Организация,
	|	СоставКнигиПокупок.СчетФактура,
	|	СоставКнигиПокупок.ДокументОтгрузки,
	|	СоставКнигиПокупок.СтавкаНДС,
	|	СоставКнигиПокупок.Событие,
	|	СоставКнигиПокупок.ВидЦенности,
	|	СоставКнигиПокупок.ЗаписьДополнительногоЛиста,
	|	СоставКнигиПокупок.НомерСтроки,
	|	СоставКнигиПокупок.ВидДеятельностиНДС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СоставКнигиПокупок.Организация,
	|	СоставКнигиПокупок.СчетФактура,
	|	СоставКнигиПокупок.ДокументОтгрузки КАК ДокументОтгрузки,
	|	СоставКнигиПокупок.СтавкаНДС,
	|	СУММА(СоставКнигиПокупок.СуммаБезНДС),
	|	СУММА(СоставКнигиПокупок.НДС),
	|	СоставКнигиПокупок.Событие,
	|	СоставКнигиПокупок.ВидЦенности,
	|	СоставКнигиПокупок.ЗаписьДополнительногоЛиста,
	|	СоставКнигиПокупок.НомерСтроки,
	|	СоставКнигиПокупок.ВидДеятельностиНДС
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок КАК СоставКнигиПокупок
	|ГДЕ
	|	СоставКнигиПокупок.Организация В(&Организация)
	|	И СоставКнигиПокупок.ЗаписьДополнительногоЛиста
	|	И СоставКнигиПокупок.Период >= &НачалоПериода 
	|	И СоставКнигиПокупок.КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода
	|	И СоставКнигиПокупок.Событие В (ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.ПредъявленНДСКВычету0),
	|									ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.НеПодтвержденаСтавка0))
	|	И НЕ СоставКнигиПокупок.ВидЦенности В (&ВидыЦенностейАвансыВсе)
	|	И НЕ СоставКнигиПокупок.ВидЦенности В (&ВидыЦенностейНалоговыйАгент)
	|
	|СГРУППИРОВАТЬ ПО
	|	СоставКнигиПокупок.Организация,
	|	СоставКнигиПокупок.СчетФактура,
	|	СоставКнигиПокупок.ДокументОтгрузки,
	|	СоставКнигиПокупок.СтавкаНДС,
	|	СоставКнигиПокупок.Событие,
	|	СоставКнигиПокупок.ВидЦенности,
	|	СоставКнигиПокупок.ЗаписьДополнительногоЛиста,
	|	СоставКнигиПокупок.НомерСтроки,
	|	СоставКнигиПокупок.ВидДеятельностиНДС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетаФактурыДокументы.СчетФактура КАК СчетФактура,
	|	СчетаФактурыДокументы.Ссылка КАК Ссылка,
	|	СчетаФактурыДокументы.ДокументОснование КАК ДокументОснование,
	|	СчетаФактурыДокументы.СчетФактураПроведен КАК СчетФактураПроведен,
	|	СчетаФактурыДокументы.СчетФактураДата КАК СчетФактураДата,
	|	СчетаФактурыДокументы.НомерВходящегоСчетаФактуры,
	|	СчетаФактурыДокументы.ДатаВходящегоСчетаФактуры
	|ПОМЕСТИТЬ ВременнаяТаблицаСчетФактураПолученный
	|ИЗ
	|	(ВЫБРАТЬ
	|		СчетФактураПолученный.Ссылка КАК СчетФактура,
	|		СчетФактураПолученный.Ссылка КАК ССылка,
	|		СчетФактураПолученный.Ссылка КАК ДокументОснование,
	|		СчетФактураПолученный.Проведен КАК СчетФактураПроведен,
	|		СчетФактураПолученный.Дата КАК СчетФактураДата,
	|		СчетФактураПолученный.Номер КАК НомерВходящегоСчетаФактуры,
	|		СчетФактураПолученный.ДатаСоставления КАК ДатаВходящегоСчетаФактуры
	|	ИЗ
	|		Документ.СчетФактураПолученный КАК СчетФактураПолученный
	|	ГДЕ
	|		НЕ СчетФактураПолученный.Ссылка.ПометкаУдаления
	|		И СчетФактураПолученный.Ссылка.Организация В(&Организация)
	|		И СчетФактураПолученный.Ссылка В
	|				(ВЫБРАТЬ
	|					ВТСоставКнигиПокупокПоПриобретеннымЦенностям0.СчетФактура
	|				ИЗ
	|					ВТСоставКнигиПокупокПоПриобретеннымЦенностям0)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		СчетФактураПолученный.ДокументОснование,
	|		СчетФактураПолученный.Ссылка,
	|		СчетФактураПолученный.ДокументОснование,
	|		СчетФактураПолученный.Ссылка.Проведен,
	|		СчетФактураПолученный.Ссылка.Дата,
	|		СчетФактураПолученный.Ссылка.Номер,
	|		СчетФактураПолученный.Ссылка.Дата
	|	ИЗ
	|		Документ.СчетФактураПолученный.ДокументыОснования КАК СчетФактураПолученный
	|	ГДЕ
	|		НЕ СчетФактураПолученный.Ссылка.ПометкаУдаления
	|		И СчетФактураПолученный.Ссылка.Организация В(&Организация)
	|		И СчетФактураПолученный.ДокументОснование В
	|				(ВЫБРАТЬ
	|					ВТСоставКнигиПокупокПоПриобретеннымЦенностям0.СчетФактура
	|				ИЗ
	|					ВТСоставКнигиПокупокПоПриобретеннымЦенностям0)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВозвратТоваровОтКлиента.Ссылка,
	|		ВозвратТоваровОтКлиента.Ссылка,
	|		ВозвратТоваровОтКлиента.Ссылка,
	|		ВозвратТоваровОтКлиента.Проведен,
	|		ВозвратТоваровОтКлиента.Дата,
	|		ВозвратТоваровОтКлиента.НомерРасходногоКассовогоОрдера,
	|		ВозвратТоваровОтКлиента.ДатаРасходногоКассовогоОрдера
	|	ИЗ
	|		Документ.ВозвратТоваровОтКлиента КАК ВозвратТоваровОтКлиента
	|	ГДЕ
	|		ВозвратТоваровОтКлиента.ДокументРеализации ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|		И ВозвратТоваровОтКлиента.НомерРасходногоКассовогоОрдера <> """"
	|		И ВозвратТоваровОтКлиента.ДатаРасходногоКассовогоОрдера <> ДАТАВРЕМЯ(1, 1, 1)
	|		И НЕ ВозвратТоваровОтКлиента.ПометкаУдаления
	|		И ВозвратТоваровОтКлиента.Организация В(&Организация)
	|		И ВозвратТоваровОтКлиента.Ссылка В
	|				(ВЫБРАТЬ
	|					ВТСоставКнигиПокупокПоПриобретеннымЦенностям0.СчетФактура
	|				ИЗ
	|					ВТСоставКнигиПокупокПоПриобретеннымЦенностям0)) КАК СчетаФактурыДокументы
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОснование,
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаможенныеДекларацииПоЭкспорту.ДокументОтгрузки,
	|	ТаможенныеДекларацииПоЭкспорту.Организация,
	|	ТаможенныеДекларацииПоЭкспорту.КодОперации
	|ПОМЕСТИТЬ СведенияТаможенныхДекларацийЭкспорт
	|ИЗ
	|	РегистрСведений.СведенияТаможенныхДекларацийЭкспорт КАК ТаможенныеДекларацииПоЭкспорту
	|ГДЕ
	|	ТаможенныеДекларацииПоЭкспорту.Организация В(&Организация)
	|	И ТаможенныеДекларацииПоЭкспорту.ДокументОтгрузки В
	|			(ВЫБРАТЬ
	|				ВТСоставКнигиПокупокПоПриобретеннымЦенностям0.ДокументОтгрузки
	|			ИЗ
	|				ВТСоставКнигиПокупокПоПриобретеннымЦенностям0)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТаможенныеДекларацииПоЭкспорту.Организация,
	|	ТаможенныеДекларацииПоЭкспорту.ДокументОтгрузки
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СоставКнигиПокупок.СчетФактура КАК ДокументРегистратор,
	|	NULL КАК ДокументОтгрузки,
	|	ВЫБОР
	|		КОГДА СоставКнигиПокупок.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.Космос)
	|			ТОГДА ""1010409""
	|			ИНАЧЕ ЕСТЬNULL(СведенияТаможенныхДекларацийЭкспорт.КодОперации, ""1011410"")
	|	КОНЕЦ КАК КодВидаОперации,
	|	СоставКнигиПокупок.СтавкаНДС КАК СтавкаНДС,
	|	СоставКнигиПокупок.СуммаБезНДС КАК Сумма,
	|	СоставКнигиПокупок.НДС КАК СуммаНДС,
	|	&ТекстБезОшибок КАК ПризнакНаличияОшибок,
	|	&ТекстОтраженыВКнигеПокупок КАК ПризнакОтраженияНДС,
	|	1 КАК КоличествоДокументов,
	|	СоставКнигиПокупок.НДС КАК СуммаВключенные,
	|	0 КАК СуммаНеВключенные,
	|	СоставКнигиПокупок.СчетФактура.Номер КАК НомерВходящегоДокумента,
	|	СоставКнигиПокупок.СчетФактура.Дата КАК ДатаВходящегоДокумента,
	|	СоставКнигиПокупок.СчетФактура КАК СчетФактура,
	|	СоставКнигиПокупок.Событие,
	|	СоставКнигиПокупок.ВидЦенности,
	|	СоставКнигиПокупок.ЗаписьДополнительногоЛиста,
	|	СоставКнигиПокупок.НомерСтроки
	|ИЗ
	|	ВТСоставКнигиПокупокПоПриобретеннымЦенностям0 КАК СоставКнигиПокупок
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСчетФактураПолученный КАК СчетФактураПолученныйДокументыОснования
	|		ПО (СоставКнигиПокупок.СчетФактура = СчетФактураПолученныйДокументыОснования.ДокументОснование
	|				ИЛИ СоставКнигиПокупок.СчетФактура = СчетФактураПолученныйДокументыОснования.Ссылка)
	|			И СчетФактураПолученныйДокументыОснования.СчетФактураПроведен
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		ПО (СоставКнигиПокупок.СчетФактура = СчетФактураВыданныйДокументыОснования.ДокументОснование
	|				ИЛИ СоставКнигиПокупок.СчетФактура = СчетФактураВыданныйДокументыОснования.Ссылка)
	|			И СчетФактураВыданныйДокументыОснования.Ссылка.Проведен
	|			И НЕ СчетФактураВыданныйДокументыОснования.Ссылка.Исправление
	|		ЛЕВОЕ СОЕДИНЕНИЕ СведенияТаможенныхДекларацийЭкспорт КАК СведенияТаможенныхДекларацийЭкспорт
	|		ПО СоставКнигиПокупок.ДокументОтгрузки = СведенияТаможенныхДекларацийЭкспорт.ДокументОтгрузки
	|			И СоставКнигиПокупок.Организация = СведенияТаможенныхДекларацийЭкспорт.Организация
	|";
	
	ВычетПоПриобретеннымЦенностям0 = Запрос.Выполнить().Выгрузить();

	ВнешнийНаборДанныхВычетПоПриобретеннымЦенностям0 = Новый Структура;
	ВнешнийНаборДанныхВычетПоПриобретеннымЦенностям0.Вставить("ВычетПоПриобретеннымЦенностям0", 
															  ВычетПоПриобретеннымЦенностям0);
	
	СтруктураВнешнихНаборов.Вставить("ВнешнийНаборДанныхВычетПоПриобретеннымЦенностям0", 
									 ВнешнийНаборДанныхВычетПоПриобретеннымЦенностям0);
		
КонецПроцедуры // ПолучитьНаборДанныхВычетПоПриобретеннымЦенностям0()

// Выполняет запрос к базе данных для получения набора данных, необходимого для формирования расшифровки по
//	вычету НДС при исполнении обязанностей налогового агента при реализации ценностей по ставке НДС 0%.
//
// Параметры
//  Запрос  - Запрос - запрос к базе данных
//  СтруктураВнешнихНаборов - структура внешних источников данных для расшифровки.
//
// Возвращаемое значение:
//   Структура   - структура параметров для формирования карты отчета.
//
Процедура ПолучитьНаборДанныхВычетНалоговыйАгент0(Запрос, СтруктураВнешнихНаборов) 
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СоставКнигиПокупок.СчетФактура КАК СчетФактура,
	|	СоставКнигиПокупок.СтавкаНДС,
	|	СоставКнигиПокупок.ДокументОплаты КАК ДокументОплаты,
	|	СоставКнигиПокупок.СуммаБезНДСОборот,
	|	СоставКнигиПокупок.НДСОборот,
	|	СоставКнигиПокупок.Событие,
	|	СоставКнигиПокупок.ВидЦенности,
	|	СоставКнигиПокупок.ЗаписьДополнительногоЛиста
	|ПОМЕСТИТЬ ВТСоставКнигиПокупокНалоговыйАгент0
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			&НачалоПериода,
	|			&КонецПериодаГраница,
	|			,
	|			Организация В (&Организация)
	|				И ВидЦенности В (&ВидыЦенностейНалоговыйАгент)
	|				И Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.ПредъявленНДСКВычету0)) КАК СоставКнигиПокупок
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОплаты
	|;
	|
	|ВЫБРАТЬ
	|	СоставКнигиПокупок.СчетФактура КАК ДокументРегистратор,
	|	СоставКнигиПокупок.СтавкаНДС КАК СтавкаНДС,
	|	NULL КАК ДокументОтгрузки,
	|	&ТекстБезОшибок КАК ПризнакНаличияОшибок,
	|	&ТекстОтраженыВКнигеПокупок КАК ПризнакОтраженияНДС,
	|	СоставКнигиПокупок.СуммаБезНДСОборот КАК Сумма,
	|	СоставКнигиПокупок.НДСОборот КАК СуммаНДС,
	|	1 КАК КоличествоДокументов,
	|	СчетФактураВыданныйДокументыОснования.Ссылка КАК СчетФактура,
	|	СоставКнигиПокупок.НДСОборот КАК СуммаВключенные,
	|	0 КАК СуммаНеВключенные,
	|	СоставКнигиПокупок.ВидЦенности КАК ВидЦенности,
	|	СоставКнигиПокупок.Событие КАК Событие,
	|	СоставКнигиПокупок.ЗаписьДополнительногоЛиста  КАК ЗаписьДополнительногоЛиста
	|ИЗ
	|	ВТСоставКнигиПокупокНалоговыйАгент0 КАК СоставКнигиПокупок
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		ПО СоставКнигиПокупок.ДокументОплаты = СчетФактураВыданныйДокументыОснования.ДокументОснование";
	
	ВычетИсполнениеОНА0 = Запрос.Выполнить();
	
	ВнешнийНаборДанныхВычетИсполнениеОНА0 = Новый Структура;
	ВнешнийНаборДанныхВычетИсполнениеОНА0.Вставить("ВычетИсполнениеОНА0",ВычетИсполнениеОНА0);
	
	СтруктураВнешнихНаборов.Вставить("ВнешнийНаборДанныхВычетИсполнениеОНА0", ВнешнийНаборДанныхВычетИсполнениеОНА0);
	
КонецПроцедуры // ПолучитьНаборДанныхВычетИсполнениеОНА0()

// Выполняет запрос к базе данных для получения набора данных, необходимого для формирования расшифровки по
//	начислению НДС по прочим операциям в случае ведения сложного учета НДС.
//
// Параметры
//  Запрос  - Запрос - запрос к базе данных
//  СтруктураВнешнихНаборов - структура внешних источников данных для расшифровки.
//
// Возвращаемое значение:
//   Структура   - структура параметров для формирования карты отчета.
//
Процедура ПолучитьНаборДанныхНачислениеВосстановлениеДругиеОперации(Запрос, СтруктураВнешнихНаборов) 

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСЗаписиКнигиПродажОбороты.СчетФактура КАК ДокументРегистратор,
	|	НДСЗаписиКнигиПродажОбороты.СтавкаНДС,
	|	НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот КАК Сумма,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот КАК СуммаНДС,
	|	&ТекстОтраженыВКнигеПродаж КАК ПризнакОтраженияНДС,
	|	&ТекстБезОшибок КАК ПризнакНаличияОшибок,
	|	1 КАК КоличествоДокументов,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот КАК СуммаВключенные,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПродажОбороты.Событие КАК Событие,
	|	НДСЗаписиКнигиПродажОбороты.ЗаписьДополнительногоЛиста КАК ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПродажОбороты.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			&НачалоПериода,
	|			&КонецПериодаГраница,
	|			Запись,
	|			Организация В (&Организация)
	|				И Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.ВосстановлениеНДС)
	|				И ВидЦенности <> ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.АвансыВыданные)
	|				И ВидЦенности <> ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.АвансыВыданныеНалоговыйАгент)
	|				И ВидЦенности <> ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ТоварыНалоговыйАгент)
	|				И НЕ ЗаписьДополнительногоЛиста) КАК НДСЗаписиКнигиПродажОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПродажОбороты.СчетФактура,
	|	НДСЗаписиКнигиПродажОбороты.СтавкаНДС,
	|	НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот,
	|	&ТекстОтраженыВКнигеПродаж,
	|	&ТекстБезОшибок,
	|	1,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПродажОбороты.Событие,
	|	НДСЗаписиКнигиПродажОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПродажОбороты.НомерСтроки
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			&НачалоПериода,
	|			,
	|			Запись,
	|			Организация В (&Организация)
	|				И Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.ВосстановлениеНДС)
	|				И ВидЦенности <> ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.АвансыВыданные)
	|				И ВидЦенности <> ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.АвансыВыданныеНалоговыйАгент)
	|				И ВидЦенности <> ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ТоварыНалоговыйАгент)
	|				И (ЗаписьДополнительногоЛиста
	|					И (КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода))) КАК НДСЗаписиКнигиПродажОбороты";
	
	НачислениеВосстановлениеДругиеОперации = Запрос.Выполнить();
	
	ВнешнийНаборДанныхНачислениеВосстановлениеДругиеОперации = Новый Структура;
	ВнешнийНаборДанныхНачислениеВосстановлениеДругиеОперации.Вставить("НачислениеВосстановлениеДругиеОперации", 
																	  НачислениеВосстановлениеДругиеОперации);
	
	СтруктураВнешнихНаборов.Вставить("ВнешнийНаборДанныхНачислениеВосстановлениеДругиеОперации",
									 ВнешнийНаборДанныхНачислениеВосстановлениеДругиеОперации);
	
КонецПроцедуры // ПолучитьНаборДанныхНачислениеВосстановлениеДругиеОперации()

#КонецОбласти

#КонецЕсли