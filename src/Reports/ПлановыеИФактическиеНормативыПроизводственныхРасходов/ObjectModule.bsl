#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - см. ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.События.ПриСозданииНаСервере = Истина;
	
КонецПроцедуры

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//   ЭтаФорма - ФормаКлиентскогоПриложения - Форма отчета.
//   Отказ - Булево - Передается из параметров обработчика "как есть".
//   СтандартнаяОбработка - Булево - Передается из параметров обработчика "как есть".
Процедура ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт
	
	Параметры = ЭтаФорма.Параметры;
	Если Параметры.Свойство("ПараметрКоманды")
			И Параметры.Свойство("ПараметрыОтчет")
			И Параметры.ПараметрыОтчет.Свойство("ДополнительныеПараметры") Тогда 
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	НаборДанных = СхемаКомпоновкиДанных.НаборыДанных.Найти("НаборДанных1");
	
	НаборДанных25 = СхемаКомпоновкиДанных.НаборыДанных.Найти("НаборДанных2_5");
	Если ЦенообразованиеВызовСервера.ИспользуетсяЦенообразование25() Тогда
		НаборДанных.Запрос = НаборДанных25.Запрос;
	КонецЕсли;
	СхемаКомпоновкиДанных.НаборыДанных.Удалить(НаборДанных25);
	
	НастройкиКомпоновки = КомпоновщикНастроек.ПолучитьНастройки();
	
	НаборДанных.Запрос = СтрЗаменить(НаборДанных.Запрос, "&Объем", Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаОбъемУпаковки("СН.ЕдиницаИзмерения", "СН"));
	НаборДанных.Запрос = СтрЗаменить(НаборДанных.Запрос, "&Вес", Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки("СН.ЕдиницаИзмерения", "СН"));
	
	Период = НастройкиКомпоновки.ПараметрыДанных.НайтиЗначениеПараметра(
		Новый ПараметрКомпоновкиДанных("СтандартныйПериод")).Значение; // СтандартныйПериод
	НачалоПериода = Период.ДатаНачала;
	КонецПериода = Период.ДатаОкончания;
	
	ТекстПериодов = "";
	
	Период = КонецМесяца(НачалоПериода) + 1;
	ШаблонОбъединения = "
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|	&Период
		|";
	
	Пока НачалоМесяца(КонецПериода) >= Период Цикл
		
		ТекстПериодов = ТекстПериодов + СтрЗаменить(ШаблонОбъединения, "&Период", 
			Формат(Период, "ДФ='""ДатаВремя(""гггг"",""М"",""д"")""'"));
		Период = КонецМесяца(Период) + 1;
		
	КонецЦикла;
	
	НаборДанных.Запрос = СтрЗаменить(НаборДанных.Запрос, "//**01", ТекстПериодов);
	
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ТекстПостоянныеРасходы", НСтр("ru = 'Постоянные расходы';
																										|en = 'Fixed costs'"));
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ТекстПеременныеРасходы", НСтр("ru = 'Переменные расходы';
																										|en = 'Variable costs'"));
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "Норматив", НСтр("ru = 'Норматив';
																							|en = 'Standard'"));
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ВеличинаБазы", НСтр("ru = 'Величина базы распределения';
																								|en = 'Allocation base volume'"));
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СуммаРасхода", НСтр("ru = 'Сумма расхода';
																								|en = 'Expense amount'"));
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "БазаНеЗадана", НСтр("ru = 'База не задана';
																								|en = 'Base is not specified'"));
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ПредставлениеШтуки", НСтр("ru = 'шт.';
																									|en = 'pcs.'"));
	
КонецПроцедуры

#КонецЕсли