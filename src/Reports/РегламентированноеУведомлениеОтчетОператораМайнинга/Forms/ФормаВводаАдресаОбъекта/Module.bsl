#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ТипЗнч(Параметры.Данные) = Тип("ХранилищеЗначения") Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры.Данные.Получить());
		Элементы.ДекорацияАдрес.Заголовок = АдресСтрока;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДекорацияАдресНажатие(Элемент)
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Заголовок", "Ввод адреса объекта инфраструктуры");
	ПараметрыФормы.Вставить("ВидКонтактнойИнформации", ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.ФактАдресОрганизации"));
	ПараметрыФормы.Вставить("КонтактнаяИнформация", АдресJSON);
	ПараметрыФормы.Вставить("ЗначенияПолей", АдресJSON);
	
	ОО = Новый ОписаниеОповещения("ДекорацияАдресНажатиеЗавершение", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ОбщийМодуль("УправлениеКонтактнойИнформациейКлиент").ОткрытьФормуКонтактнойИнформации(ПараметрыФормы, , ОО);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Ок(Команда)
	Если ЗначениеЗаполнено(АдресСтрока) И ЗначениеЗаполнено(ИдНом) Тогда 
		Закрыть(ОписаниеРезультата());
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю("Необходимо заполнить адрес");
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ДекорацияАдресНажатиеЗавершение(Результат, Параметры) Экспорт
	АдресВРеквизитыФормы(Результат);
КонецПроцедуры

&НаСервере
Процедура АдресВРеквизитыФормы(Результат)
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		Если ЗначениеЗаполнено(Результат.Значение) Тогда 
			СтруктураАдресаЗаписи = РаботаСАдресами.СведенияОбАдресеПриказМинфинаРФ171н(Результат.Значение, Новый Структура("КодыАдреса", Истина));
			
			АдресJSON = Результат.Значение;
			АдресСтрока = СтруктураАдресаЗаписи.Представление;
			Элементы.ДекорацияАдрес.Заголовок = СтруктураАдресаЗаписи.Представление;
			ИдНом = СтруктураАдресаЗаписи.ИдентификаторАдресногоОбъекта;
		Иначе
			АдресJSON = "";
			АдресСтрока = "";
			Элементы.ДекорацияАдрес.Заголовок = "<Введите адрес>";
			КадастрНом = "";
			ИдНом = "";
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ОписаниеРезультата()
	Данные = Новый Структура;
	Данные.Вставить("АдресJSON", АдресJSON);
	Данные.Вставить("АдресСтрока", АдресСтрока);
	Данные.Вставить("ИдНом", ИдНом);
	Данные.Вставить("КадастрНом", КадастрНом);
	Возврат Новый Структура("Описание, Данные, Тип", АдресСтрока, Новый ХранилищеЗначения(Данные), "1");
КонецФункции

#КонецОбласти
