
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	Если Параметры.Свойство("КлючОбъекта") Тогда
		КлючОбъекта = Параметры.КлючОбъекта;
	КонецЕсли;
	Если Параметры.Свойство("Настройки") Тогда
		Настройки = Параметры.Настройки;
	КонецЕсли;
	Если Параметры.Свойство("КлючТекущейНастройки") Тогда
		КлючТекущейНастройки = Параметры.КлючТекущейНастройки;
	КонецЕсли;	
	
	ПользовательИБ = ИмяПользователяИБ(Пользователи.ТекущийПользователь());
	
	ПерезаполнитьСписокНастроек();
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	КлючНастройки = СформироватьСвободноеНаименование(КлючТекущейНастройки, СписокНастроек);
	Элементы.ЧтоБудетДальше.ТекущаяСтраница = Элементы.Новый;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокНастроекПриАктивизацииСтроки(Элемент)

	ТекущиеДанные = Элементы.СписокНастроек.ТекущиеДанные;
	Если Не ТекущиеДанные = Неопределено Тогда
		КлючНастройки = ТекущиеДанные.Значение;
		Элементы.ЧтоБудетДальше.ТекущаяСтраница = Элементы.Перезапись;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КлючНастройкиПриИзменении(Элемент)
	
	Если СписокНастроек.НайтиПоЗначению(КлючНастройки) = Неопределено Тогда
		Элементы.ЧтоБудетДальше.ТекущаяСтраница = Элементы.Новый;
	Иначе
		Элементы.ЧтоБудетДальше.ТекущаяСтраница = Элементы.Перезапись;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СохранитьНастройку(Команда)
	
	Отказ = Ложь;
	
	Если Не ЗначениеЗаполнено(КлючНастройки) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Поле ""Наименование"" не заполнено';
				|en = '""Description"" is not filled in'"),
			,
			"ЭтаФорма.КлючНастройки");
		Отказ = Истина;
	КонецЕсли;

	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	СохранитьНастройкуНаСервере(КлючОбъекта, КлючНастройки, Настройки);
	Закрыть(КлючНастройки);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокНастроекУдалить(Команда)
	
	ТекущиеДанные = Элементы.СписокНастроек.ТекущиеДанные;
	Настройка = ТекущиеДанные.Значение;
	СписокНастроекУдалитьНаСервере(КлючОбъекта, Настройка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ИмяПользователяИБ(Пользователь)
	Если Не ЗначениеЗаполнено(Пользователь) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	НайденныйПользовательИБ = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Пользователь, "ИдентификаторПользователяИБ"));
	Если НайденныйПользовательИБ = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат НайденныйПользовательИБ.Имя;
КонецФункции

&НаСервереБезКонтекста
Процедура СохранитьНастройкуНаСервере(КлючОбъекта, КлючНастройки, Настройки)
	
	ОбщегоНазначения.ХранилищеСистемныхНастроекСохранить(КлючОбъекта, КлючНастройки, Настройки); 
	
КонецПроцедуры

&НаСервере
Процедура СписокНастроекУдалитьНаСервере(КлючОбъекта, Настройка)
	
	ОбщегоНазначения.ХранилищеСистемныхНастроекУдалить(КлючОбъекта, Настройка, ПользовательИБ);	
	ПерезаполнитьСписокНастроек();
	
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьСписокНастроек()
	
	СписокНастроек = ХранилищеСистемныхНастроек.ПолучитьСписок(КлючОбъекта);
	ТекущееЗначение = СписокНастроек.НайтиПоЗначению(КлючТекущейНастройки);
	Если Не ТекущееЗначение = Неопределено Тогда
		Индекс = СписокНастроек.Индекс(ТекущееЗначение);
		Элементы.СписокНастроек.ТекущаяСтрока = Индекс;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СформироватьСвободноеНаименование(КлючНастройки, СписокНастроек)
	
	Если СписокНастроек.Количество() = 0 Тогда
		СвободноеНаименование = НСтр("ru = 'Основной';
									|en = 'Main'");
		Возврат СвободноеНаименование;
	КонецЕсли;
	
	ШаблонИмениНастройки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '%1 - копия';
			|en = '%1 - copy'"),
		КлючНастройки);
	
	СвободноеНаименование = ШаблонИмениНастройки;
	НайденноеЗначение = СписокНастроек.НайтиПоЗначению(СвободноеНаименование);
	Если НайденноеЗначение = Неопределено Тогда
		Возврат СвободноеНаименование;
	КонецЕсли;
	
	НомерВарианта = 1;
	Пока Истина Цикл
		НомерВарианта = НомерВарианта + 1;
		СвободноеНаименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 (%2)';
				|en = '%1 (%2)'"),
			ШаблонИмениНастройки,
			Формат(НомерВарианта, ""));
		НайденноеЗначение = СписокНастроек.НайтиПоЗначению(СвободноеНаименование);
		Если НайденноеЗначение = Неопределено Тогда
			Возврат СвободноеНаименование;
		КонецЕсли;
	КонецЦикла;
	
КонецФункции

#КонецОбласти