#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьТаблицуПримененияФорматов() Экспорт 
	Результат = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюТаблицуПримененияФорматов();
	
	Стр = Результат.Добавить();
	Стр.ИмяФормы = "Форма2023_1";
	Стр.КНД = "1150086";
	Стр.ВерсияФормата = "5.02";
	
	Возврат Результат;
КонецФункции

Функция ДанноеУведомлениеДоступноДляОрганизации() Экспорт 
	Возврат Истина;
КонецФункции

Функция ДанноеУведомлениеДоступноДляИП() Экспорт 
	Возврат Истина;
КонецФункции

Функция ПолучитьОсновнуюФорму() Экспорт 
	Возврат "";
КонецФункции

Функция ПолучитьФормуПоУмолчанию() Экспорт 
	Возврат "";
КонецФункции

Функция ПолучитьТаблицуФорм() Экспорт 
	Результат = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюТаблицуФормУведомления();
	
	Стр = Результат.Добавить();
	Стр.ИмяФормы = "Форма2023_1";
	Стр.ОписаниеФормы = "Форма по приказу ФНС России от 30.11.2022 № ЕД-7-8/1134@";
	Стр.ДатаНачала = '20230101';
	Стр.ДатаКонца = '20991130';
	
	Возврат Результат;
КонецФункции

Функция ЭлектронноеПредставление(Объект, ИмяФормы, УникальныйИдентификатор) Экспорт
	Если ИмяФормы = "Форма2023_1" Тогда
		Возврат ЭлектронноеПредставление_Форма2023_1(Объект, УникальныйИдентификатор);
	КонецЕсли;
	Возврат Неопределено;
КонецФункции

Функция ПроверитьДокумент(Объект, ИмяФормы, УникальныйИдентификатор) Экспорт
КонецФункции

Функция СформироватьСписокЛистов(Объект) Экспорт
	Если Объект.ИмяФормы = "Форма2023_1" Тогда 
		Возврат УведомлениеОСпецрежимахНалогообложения.ПечатьВСледующихВерсиях(Объект);
	КонецЕсли;
КонецФункции

Функция ПроверитьДокументСВыводомВТаблицу(Объект, ИмяФормы, УникальныйИдентификатор) Экспорт 
	Если ИмяФормы = "Форма2023_1" Тогда 
		Возврат ПроверитьДокументСВыводомВТаблицу_Форма2023_1(
			УведомлениеОСпецрежимахНалогообложения.ДанныеУведомленияДляВыгрузки(Объект), УникальныйИдентификатор);
	КонецЕсли;
КонецФункции

Функция ИдентификаторФайлаЭлектронногоПредставления_Форма2023_1(СведенияОтправки)
	Префикс = "UT_PREDOTSR";
	Возврат Документы.УведомлениеОСпецрежимахНалогообложения.ИдентификаторФайлаЭлектронногоПредставления(Префикс, СведенияОтправки);
КонецФункции

Процедура ПроверитьТитульная_2023(Данные, ТаблицаОшибок)
	Титульная = Данные.ДанныеУведомления.Титульная;
	ЭтоЮЛ = РегламентированнаяОтчетностьПереопределяемый.ЭтоЮридическоеЛицо(Данные.Организация);
	УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаИННКПП(ЭтоЮЛ, Титульная, ТаблицаОшибок);
	УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаКодаНО(Титульная.КодНО, ТаблицаОшибок, "Титульная");
	Если Не ЗначениеЗаполнено(Титульная.НаимОрг) Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
			"Не указано наименование организации", "Титульная", "НаимОрг"));
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Титульная.ДАТА_ПОДПИСИ) Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
			"Не указана дата формирования документа", "Титульная", "ДАТА_ПОДПИСИ"));
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Титульная.ПРИЗНАК_НП_ПОДВАЛ) Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
			"Не указан признак подписанта", "Титульная", "ПРИЗНАК_НП_ПОДВАЛ"));
	КонецЕсли;
	Если Титульная.ПРИЗНАК_НП_ПОДВАЛ = "2" И Не ЗначениеЗаполнено(Титульная.НаимДок) Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
			"Не указан документ подписанта", "Титульная", "НаимДок"));
	КонецЕсли;
	УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаПодписантаНалоговойОтчетности(Данные, ТаблицаОшибок, "Титульная", 
		((Титульная.ПРИЗНАК_НП_ПОДВАЛ = "1" И ЭтоЮЛ) Или Титульная.ПРИЗНАК_НП_ПОДВАЛ = "2"));
	
	Если Не ЗначениеЗаполнено(Титульная.ПрПредост) Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
			"Не указан признак предоставления", "Титульная", "ПрПредост"));
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Титульная.ПрХодатПриост) Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
			"Не указан признак ходатайства", "Титульная", "ПрХодатПриост"));
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Титульная.ПрВыпУсл) Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
			"Не указан признак обязательства", "Титульная", "ПрВыпУсл"));
	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьОтсрРасср_2023(Данные, ТаблицаОшибок)
	Титульная = Данные.ДанныеУведомления.Титульная;
	ОтсрРассрИК = Данные.ДанныеУведомления.ОтсрРассрИК;
	ОтсрРасср = Данные.ДанныеУведомления.ОтсрРасср;
	ЭтоЮЛ = РегламентированнаяОтчетностьПереопределяемый.ЭтоЮридическоеЛицо(Данные.Организация);
	
	ОтсрРассрИК_Заполнено = Ложь;
	Для Каждого КЗ Из ОтсрРассрИК Цикл 
		ОтсрРассрИК_Заполнено = ОтсрРассрИК_Заполнено 
			Или (ЗначениеЗаполнено(ОтсрРассрИК[КЗ.Ключ]) И СтрНайти(КЗ.Ключ, "___") = 0);
	КонецЦикла;
	ОтсрРасср_Заполнено = УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(ОтсрРасср);
	
	Если (Титульная.ПрПредост = "3" Или Не ОтсрРасср_Заполнено) И Не ОтсрРассрИК_Заполнено Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
			"Необходимо заполнить лист 1 со сведениями о предоставлении отсрочки", "ОтсрРассрИК", ""));
	КонецЕсли;
	Если Титульная.ПрПредост = "3" И ОтсрРасср_Заполнено Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
			"Лист 2 не заполняется при признаке ""3""", "ОтсрРасср", ""));
	КонецЕсли;
	Если ОтсрРассрИК_Заполнено Тогда
		Если Не ЗначениеЗаполнено(ОтсрРассрИК.ПодПункт) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указан подпункт", "ОтсрРассрИК", "ПодПункт"));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ОтсрРассрИК.Пункт) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указан пункт", "ОтсрРассрИК", "Пункт"));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ОтсрРассрИК.Статья) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указана статья", "ОтсрРассрИК", "Статья"));
		КонецЕсли;
		Если Данные.ОТЧ.ПривестиЗначение(ОтсрРассрИК.Срок) = 0 Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указан / неправильно указан срок", "ОтсрРассрИК", "Срок"));
		КонецЕсли;
		Если Титульная.ПрПредост = "2" И Не ЗначениеЗаполнено(ОтсрРассрИК.ПрУплРасср) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указан признак уплаты", "ОтсрРассрИК", "ПрУплРасср"));
		ИначеЕсли (Титульная.ПрПредост = "1" Или Титульная.ПрПредост = "3") И ЗначениеЗаполнено(ОтсрРассрИК.ПрУплРасср) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"При коде ""1"" и ""3"" признак уплаты не указывается", "ОтсрРассрИК", "ПрУплРасср"));
		КонецЕсли;
		
		Инд = 0;
		Для Каждого Стр Из Данные.ДанныеДопСтрокБД.МнгСтр1 Цикл 
			Инд = Инд + 1;
			УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаКПП(Стр, ТаблицаОшибок, "ОтсрРассрИК", "КПП", ЭтоЮЛ, Инд);
			УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаКБК(Стр, ТаблицаОшибок, "ОтсрРассрИК", "КБК", Истина, Инд);
			УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаОКТМО(Стр, ТаблицаОшибок, "ОтсрРассрИК", "ОКТМО", Истина, Инд);
			Если Не ЗначениеЗаполнено(Стр.СрокУпл) Тогда 
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указан срок уплаты", "ОтсрРассрИК", "СрокУпл___" + Формат(Инд, "ЧГ=")));
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Если ОтсрРасср_Заполнено Тогда
		Если Не ЗначениеЗаполнено(ОтсрРасср.ПодПункт) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указан подпункт", "ОтсрРасср", "ПодПункт"));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ОтсрРасср.Пункт) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указан пункт", "ОтсрРасср", "Пункт"));
		КонецЕсли;
		Если Данные.ОТЧ.ПривестиЗначение(ОтсрРасср.Срок) = 0 Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указан / неправильно указан срок", "ОтсрРасср", "Срок"));
		КонецЕсли;
		Если Титульная.ПрПредост = "2" И Не ЗначениеЗаполнено(ОтсрРасср.ПрУплРасср) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указан признак уплаты", "ОтсрРасср", "ПрУплРасср"));
		ИначеЕсли Титульная.ПрПредост = "1" И ЗначениеЗаполнено(ОтсрРасср.ПрУплРасср) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"При коде ""1"" признак уплаты не указывается", "ОтсрРасср", "ПрУплРасср"));
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьСвПоруч_2023(Данные, ТаблицаОшибок)
	СпОбеспИсп = Данные.ДанныеУведомления.СпОбеспИсп;
	
	Для Каждого Признак Из СтрРазделить("ПрЗалогДвИмущ,ПрЗалогНеДвИмущ,ПрПоруч,ПрБанкГарант", ",") Цикл 
		Если Не ЗначениеЗаполнено(СпОбеспИсп[Признак]) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указан признак", "СпОбеспИсп", Признак));
		КонецЕсли;
	КонецЦикла;
	Если СпОбеспИсп.ПрЗалогДвИмущ = "0" И СпОбеспИсп.ПрЗалогНеДвИмущ = "0"
		И СпОбеспИсп.ПрПоруч = "0" И СпОбеспИсп.ПрБанкГарант = "0" Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
			"Хотя бы один из признаков должен быть равен 1", "СпОбеспИсп", "ПрЗалогДвИмущ"));
	КонецЕсли;
	
	Для Каждого Элт Из Данные.ДанныеМногостраничныхРазделов.СвДвижИмущ Цикл 
		СвДвижИмущ = Элт.Значение;
		Если УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(СвДвижИмущ) Тогда 
			Если Не ЗначениеЗаполнено(СвДвижИмущ.НаимДвижИмущ) Тогда 
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указано наименование имущества", "СвДвижИмущ", "НаимДвижИмущ", СвДвижИмущ.УИД));
			КонецЕсли;
		ИначеЕсли СпОбеспИсп.ПрЗалогДвИмущ = "1" Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Страница с движимым имуществом не заполнена", "СвДвижИмущ", "", СвДвижИмущ.УИД));
		КонецЕсли;
	КонецЦикла;
	Для Каждого Элт Из Данные.ДанныеМногостраничныхРазделов.СвНеДвижИмущ Цикл 
		СвНеДвижИмущ = Элт.Значение;
		Если УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(СвНеДвижИмущ) Тогда 
			Если Не ЗначениеЗаполнено(СвНеДвижИмущ.НаимНеДвижИмущ) Тогда 
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указано наименование имущества", "СвНеДвижИмущ", "НаимНеДвижИмущ", СвНеДвижИмущ.УИД));
			КонецЕсли;
			Если Не ЗначениеЗаполнено(СвНеДвижИмущ.НомКадастрЗУ) Тогда 
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указан кадастровый номер", "СвНеДвижИмущ", "НомКадастрЗУ", СвНеДвижИмущ.УИД));
			КонецЕсли;
			Если Не ЗначениеЗаполнено(СвНеДвижИмущ.ПрСтоим) Тогда 
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указан признак", "СвНеДвижИмущ", "ПрСтоим", СвНеДвижИмущ.УИД));
			КонецЕсли;
		ИначеЕсли СпОбеспИсп.ПрЗалогНеДвИмущ = "1" Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Страница с недвижимым имуществом не заполнена", "СвНеДвижИмущ", "", СвНеДвижИмущ.УИД));
		КонецЕсли;
	КонецЦикла;
	Для Каждого Элт Из Данные.ДанныеМногостраничныхРазделов.СвПоруч Цикл 
		СвПоруч = Элт.Значение;
		Если УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(СвПоруч) Тогда
			Если СвПоруч.ЮрФиз = "1" Тогда 
				УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаИНН(СвПоруч, ТаблицаОшибок, "СвПоруч", "ИННЮЛ", Истина, Истина);
				Если Не ЗначениеЗаполнено(СвПоруч.НаимОрг) Тогда 
					ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
						"Не указано наименование организации", "СвПоруч", "НаимОрг", СвПоруч.УИД));
				КонецЕсли;
			ИначеЕсли СвПоруч.ЮрФиз = "2" Тогда
				УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаИНН(СвПоруч, ТаблицаОшибок, "СвПоруч", "ИННФЛ", Истина, Ложь);
				Если Не ЗначениеЗаполнено(СвПоруч.Фамилия) Тогда 
					ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
						"Не указана фамилия", "СвПоруч", "Фамилия", СвПоруч.УИД));
				КонецЕсли;
				Если Не ЗначениеЗаполнено(СвПоруч.Имя) Тогда 
					ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
						"Не указано имя", "СвПоруч", "Имя", СвПоруч.УИД));
				КонецЕсли;
			Иначе
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указан / неправильно указан признак", "СвПоруч", "ЮрФиз", СвПоруч.УИД));
			КонецЕсли;
		ИначеЕсли СпОбеспИсп.ПрПоруч = "1" Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Страница с поручителями не заполнена", "СвПоруч", "", СвПоруч.УИД));
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Функция ПроверитьДокументСВыводомВТаблицу_Форма2023_1(Данные, УникальныйИдентификатор)
	ТаблицаОшибок = Новый СписокЗначений;
	УведомлениеОСпецрежимахНалогообложения.ПроверкаАктуальностиФормыПриВыгрузке(
		Данные.Объект.ИмяФормы, ТаблицаОшибок, ПолучитьТаблицуФорм());
	
	ПроверитьТитульная_2023(Данные, ТаблицаОшибок);
	ПроверитьОтсрРасср_2023(Данные, ТаблицаОшибок);
	ПроверитьСвПоруч_2023(Данные, ТаблицаОшибок);
	
	УведомлениеОСпецрежимахНалогообложения.ПроверкаДатВУведомлении(Данные, ТаблицаОшибок);
	УведомлениеОСпецрежимахНалогообложения.ПолнаяПроверкаЗаполненныхПоказателейНаСоотвествиеСписку("СпискиВыбора2023_1",
		"СхемаВыгрузкиФорма2023_1", Данные.Объект.ИмяОтчета, ТаблицаОшибок, Данные);
	Возврат ТаблицаОшибок;
КонецФункции

Функция ОсновныеСведенияЭлектронногоПредставления_Форма2023_1(Объект, УникальныйИдентификатор)
	ОсновныеСведения = Документы.УведомлениеОСпецрежимахНалогообложения.НачальнаяИнициализацияОбщихРеквизитовВыгрузки(Объект);
	ОсновныеСведения.Вставить("КолФайл", 0);
	Данные = Объект.ДанныеУведомления.Получить();
	ИдентификаторФайла = ИдентификаторФайлаЭлектронногоПредставления_Форма2023_1(ОсновныеСведения);
	ОсновныеСведения.Вставить("ИдФайл", ИдентификаторФайла);
	
	Возврат ОсновныеСведения;
КонецФункции

Функция ЭлектронноеПредставление_Форма2023_1(Объект, УникальныйИдентификатор)
	СписокФайлов = Новый СписокЗначений;
	ИменаФайлов = Новый СписокЗначений;
	
	СведенияЭлектронногоПредставления = УведомлениеОСпецрежимахНалогообложения.СведенияЭлектронногоПредставления();
	ДанныеУведомления = УведомлениеОСпецрежимахНалогообложения.ДанныеУведомленияДляВыгрузки(Объект);
	Ошибки = ПроверитьДокументСВыводомВТаблицу_Форма2023_1(ДанныеУведомления, УникальныйИдентификатор);
	УведомлениеОСпецрежимахНалогообложения.СообщитьОшибкиПриПроверкеВыгрузки(Объект, Ошибки, ДанныеУведомления);
	
	ОсновныеСведения = ОсновныеСведенияЭлектронногоПредставления_Форма2023_1(Объект, УникальныйИдентификатор);
	СтруктураВыгрузки = Документы.УведомлениеОСпецрежимахНалогообложения.ИзвлечьСтруктуруXMLУведомления(Объект.ИмяОтчета, "СхемаВыгрузкиФорма2023_1");
	УведомлениеОСпецрежимахНалогообложения.ТиповоеЗаполнениеДанными(ДанныеУведомления, ОсновныеСведения, СтруктураВыгрузки);
	УведомлениеОСпецрежимахНалогообложения.ВыгрузитьДеревоВТаблицу(СтруктураВыгрузки, ОсновныеСведения, СведенияЭлектронногоПредставления);
	Возврат СведенияЭлектронногоПредставления;
КонецФункции

#КонецОбласти
#КонецЕсли
