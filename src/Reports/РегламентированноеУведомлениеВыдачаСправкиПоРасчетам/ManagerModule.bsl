#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьТаблицуПримененияФорматов() Экспорт 
	Результат = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюТаблицуПримененияФорматов();
	
	Стр = Результат.Добавить();
	Стр.ИмяФормы = "Форма2025_1";
	Стр.КНД = "1114237";
	Стр.ВерсияФормата = "5.03";
	
	Стр = Результат.Добавить();
	Стр.ИмяФормы = "Форма2023_2";
	Стр.КНД = "1166507";
	Стр.ВерсияФормата = "5.01";
	
	Возврат Результат;
КонецФункции

Функция ДанноеУведомлениеДоступноДляОрганизации() Экспорт 
	Возврат Истина;
КонецФункции

Функция ДанноеУведомлениеДоступноДляИП() Экспорт 
	Возврат Истина;
КонецФункции

Функция ПолучитьОсновнуюФорму() Экспорт 
	Возврат "";
КонецФункции

Функция ПолучитьФормуПоУмолчанию() Экспорт 
	Возврат "";
КонецФункции

Функция ПолучитьТаблицуФорм() Экспорт 
	Результат = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюТаблицуФормУведомления();
	
	Стр = Результат.Добавить();
	Стр.ИмяФормы = "Форма2025_1";
	Стр.ОписаниеФормы = "В соответствии с приказом ФНС России от 30.01.2025 № ЕД-7-8/61@ в формате 5.03";
	Стр.ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ВыдачаСправкиПоРасчетам;
	Стр.ДатаНачала = '20250508';
	Стр.ДатаКонца = '20991231';
	
	Стр = Результат.Добавить();
	Стр.ИмяФормы = "Форма2023_2";
	Стр.ОписаниеФормы = "В соответствии с письмом ФНС России от --.--.---- № -------/----@.";
	Стр.ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.СведенияПоЗадолженности;
	
	Возврат Результат;
КонецФункции

Функция СформироватьВыгрузкуИПолучитьДанные(ДокОбъект) Экспорт 
	Выгрузка = ДокОбъект.ВыгрузитьДокумент();
	Если Выгрузка = Неопределено Тогда 
		Возврат Неопределено;
	КонецЕсли;
	СтруктураВыгрузки = Новый Структура("ТестВыгрузки,КодировкаВыгрузки", Выгрузка[0].ТестВыгрузки, Выгрузка[0].КодировкаВыгрузки);
	
	Если ДокОбъект.ИмяФормы = "Форма2023_2" Тогда 
		СтруктураВыгрузки.Вставить("Данные", УведомлениеОСпецрежимахНалогообложения.ПолучитьМакетДвоичныхДанных(ДокОбъект.ИмяОтчета, "TIFF_2023_2"));
		СтруктураВыгрузки.Вставить("ИмяФайла", "1166507_5.01000_01.tif");
		Возврат СтруктураВыгрузки;
	ИначеЕсли ДокОбъект.ИмяФормы = "Форма2025_1" Тогда 
		СтруктураВыгрузки.Вставить("Данные", УведомлениеОСпецрежимахНалогообложения.ПолучитьМакетДвоичныхДанных(ДокОбъект.ИмяОтчета, "TIFF_2025_1"));
		СтруктураВыгрузки.Вставить("ИмяФайла", "1114237_5.03000_03.tif");
		Возврат СтруктураВыгрузки;
	КонецЕсли;
КонецФункции

Функция ЭлектронноеПредставление(Объект, ИмяФормы, УникальныйИдентификатор) Экспорт
	Если ИмяФормы = "Форма2023_2" Тогда
		Возврат ЭлектронноеПредставление_Форма2023_2(Объект, УникальныйИдентификатор);
	ИначеЕсли ИмяФормы = "Форма2025_1" Тогда
		Возврат ЭлектронноеПредставление_Форма2025_1(Объект, УникальныйИдентификатор);
	КонецЕсли;
КонецФункции

Функция СформироватьСписокЛистов(Объект) Экспорт
	Если Объект.ИмяФормы = "Форма2020_1" Тогда 
		Возврат СформироватьСписокЛистовФорма2020_1(Объект);
	ИначеЕсли Объект.ИмяФормы = "Форма2023_1" Тогда 
		Возврат СформироватьСписокЛистовФорма2023_1(Объект);
	ИначеЕсли Объект.ИмяФормы = "Форма2023_2" Тогда 
		Возврат СформироватьСписокЛистовФорма2023_2(Объект);
	ИначеЕсли Объект.ИмяФормы = "Форма2025_1" Тогда 
		Возврат СформироватьСписокЛистовФорма2025_1(Объект);
	КонецЕсли;
КонецФункции

Функция ПроверитьДокументСВыводомВТаблицу(Объект, ИмяФормы, УникальныйИдентификатор) Экспорт 
	Если ИмяФормы = "Форма2023_2" Тогда 
		Возврат ПроверитьДокументСВыводомВТаблицу_Форма2023_2(
			Объект, УведомлениеОСпецрежимахНалогообложения.ДанныеУведомленияДляВыгрузки(Объект), УникальныйИдентификатор);
	ИначеЕсли ИмяФормы = "Форма2025_1" Тогда 
		Возврат ПроверитьДокументСВыводомВТаблицу_Форма2025_1(
			Объект, УведомлениеОСпецрежимахНалогообложения.ДанныеУведомленияДляВыгрузки(Объект), УникальныйИдентификатор);
	КонецЕсли;
КонецФункции

Функция ИдентификаторФайлаЭлектронногоПредставления_Форма2025_1(СведенияОтправки)
	Префикс = "SR_VIDSPRSOSTISP";
	Возврат Документы.УведомлениеОСпецрежимахНалогообложения.ИдентификаторФайлаЭлектронногоПредставления(Префикс, СведенияОтправки);
КонецФункции

Функция ИдентификаторФайлаЭлектронногоПредставления_Форма2023_2(СведенияОтправки)
	Префикс = "SR_SVEDDOLGENS";
	Возврат Документы.УведомлениеОСпецрежимахНалогообложения.ИдентификаторФайлаЭлектронногоПредставления(Префикс, СведенияОтправки);
КонецФункции

Функция ПроверитьДокументСВыводомВТаблицу_Форма2023_2(Объект, Данные, УникальныйИдентификатор)
	ТаблицаОшибок = Новый СписокЗначений;
	УведомлениеОСпецрежимахНалогообложения.ПроверкаАктуальностиФормыПриВыгрузке(
		Данные.Объект.ИмяФормы, ТаблицаОшибок, ПолучитьТаблицуФорм());
	УведомлениеОСпецрежимахНалогообложения.ПроверкаДатВУведомлении(Данные, ТаблицаОшибок);
	УведомлениеОСпецрежимахНалогообложения.ПолнаяПроверкаЗаполненныхПоказателейНаСоотвествиеСписку(
		"СпискиВыбора2023_2", "СхемаВыгрузкиФорма2023_2", Данные.Объект.ИмяОтчета, ТаблицаОшибок, Данные);
	Титульная = Данные.ДанныеУведомления.Титульная;
	
	Если РегламентированнаяОтчетностьПереопределяемый.ЭтоЮридическоеЛицо(Объект.Организация) Тогда
		Если Титульная.ПрЗаяв <> "1" Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Организация должна указывать признак заявителя 1", "Титульная", "ПрЗаяв"));
		КонецЕсли;
		УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаИННКПП(Истина, Титульная, ТаблицаОшибок);
	Иначе
		Если Титульная.ПрЗаяв <> "2" И Титульная.ПрЗаяв <> "3" Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Физическое лицо/ИП должно указывать признак заявителя 2 или 3", "Титульная", "ПрЗаяв"));
		КонецЕсли;
		Если Титульная.ПрЗаяв = "3" Тогда 
			УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаИННКПП(Ложь, Титульная, ТаблицаОшибок);
		ИначеЕсли Титульная.ПрЗаяв = "2" Тогда
			Если ЗначениеЗаполнено(Титульная.ИНН) Тогда 
				УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаИНН(Титульная, ТаблицаОшибок, "Титульная", "ИНН", Ложь, Ложь);
			Иначе 
				Если Не ЗначениеЗаполнено(Титульная.КодВидДок)
					Или СтрДлина(Титульная.КодВидДок) <> 2
					Или Не СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Титульная.КодВидДок) Тогда 
					ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
						"Не указан / неправильно указан код документа", "Титульная", "КодВидДок"));
				КонецЕсли;
				Если Не ЗначениеЗаполнено(Титульная.СерНомДок) Тогда 
					ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
						"Не указана серия и номер документа", "Титульная", "СерНомДок"));
				КонецЕсли;
				Если Не ЗначениеЗаполнено(Титульная.ДатаДок) Тогда 
					ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
						"Не указана дата выдачи документа", "Титульная", "ДатаДок"));
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаКодаНО(Титульная.КодНО, ТаблицаОшибок, "Титульная");
	Если Не ЗначениеЗаполнено(Титульная.НаимОрг) Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
			"Не указано наименование организации", "Титульная", "НаимОрг"));
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Титульная.ДАТА_ПОДПИСИ) Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
			"Не указана дата подписи", "Титульная", "ДАТА_ПОДПИСИ"));
	КонецЕсли;
	
	УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаПодписантаНалоговойОтчетности(Данные, ТаблицаОшибок, "Титульная", Истина);
	Если Титульная.ПРИЗНАК_НП_ПОДВАЛ = "2" И (Не ЗначениеЗаполнено(Титульная.НаимДок)) Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
			"Необходимо указать документ представителя", "Титульная", "НаимДок"));
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Титульная.ПрВыдДок) Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
			"Не указан признак документа, который заявитель просит выдать", "Титульная", "ПрВыдДок"));
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Титульная.СпособПредстСвед) Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
			"Не указан способ представления сведений", "Титульная", "СпособПредстСвед"));
	КонецЕсли;
	Если Титульная.ПрВыдДок = "1" Тогда 
		Если Не ЗначениеЗаполнено(Титульная.ДатаСвед) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указана дата сведений", "Титульная", "ДатаСвед"));
		КонецЕсли;
		Если ЗначениеЗаполнено(Титульная.ДатаНачПериод) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Для признака документа ""1"" дата начала/окончания не указывается", "Титульная", "ДатаНачПериод"));
		КонецЕсли;
		Если ЗначениеЗаполнено(Титульная.ДатаКонПериод) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Для признака документа ""1"" дата начала/окончания не указывается", "Титульная", "ДатаКонПериод"));
		КонецЕсли;
	ИначеЕсли Титульная.ПрВыдДок = "2" Тогда 
		Если ЗначениеЗаполнено(Титульная.ДатаСвед) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Для признака документа ""2"" дата сведений не указывается", "Титульная", "ДатаСвед"));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Титульная.ДатаНачПериод) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указана дата начала периода", "Титульная", "ДатаНачПериод"));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Титульная.ДатаКонПериод) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указана дата окончания периода", "Титульная", "ДатаКонПериод"));
		КонецЕсли;
		Если ЗначениеЗаполнено(Титульная.ДатаНачПериод) 
			И ЗначениеЗаполнено(Титульная.ДатаКонПериод)
			И Год(Титульная.ДатаНачПериод) <> Год(Титульная.ДатаКонПериод) Тогда 
			
			Если Не ЗначениеЗаполнено(Титульная.ДатаКонПериод) Тогда 
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Годы начала и окончания периода должны совпадать", "Титульная", "ДатаКонПериод"));
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ТаблицаОшибок;
КонецФункции

Функция ПроверитьДокументСВыводомВТаблицу_Форма2025_1(Объект, Данные, УникальныйИдентификатор)
	ТаблицаОшибок = Новый СписокЗначений;
	УведомлениеОСпецрежимахНалогообложения.ПроверкаАктуальностиФормыПриВыгрузке(
		Данные.Объект.ИмяФормы, ТаблицаОшибок, ПолучитьТаблицуФорм());
	УведомлениеОСпецрежимахНалогообложения.ПроверкаДатВУведомлении(Данные, ТаблицаОшибок);
	УведомлениеОСпецрежимахНалогообложения.ПолнаяПроверкаЗаполненныхПоказателейНаСоотвествиеСписку(
		"СпискиВыбора2025_1", "СхемаВыгрузкиФорма2025_1", Данные.Объект.ИмяОтчета, ТаблицаОшибок, Данные);
	Титульная = Данные.ДанныеУведомления.Титульная;
	
	ЭтоЮЛ = РегламентированнаяОтчетностьПереопределяемый.ЭтоЮридическоеЛицо(Объект.Организация);
	Если ЭтоЮЛ Тогда
		УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаИННКПП(Истина, Титульная, ТаблицаОшибок);
	Иначе
		Если Титульная.ПрЗаяв = "3" Тогда 
			УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаИННКПП(Ложь, Титульная, ТаблицаОшибок);
		ИначеЕсли Титульная.ПрЗаяв = "2" Тогда
			Если ЗначениеЗаполнено(Титульная.ИНН) Тогда 
				УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаИНН(Титульная, ТаблицаОшибок, "Титульная", "ИНН", Ложь, Ложь);
			ИначеЕсли УведомлениеОСпецрежимахНалогообложения.БлокЗаполнен(Титульная, "КодВидДок,СерНомДок,ДатаДок") Тогда 
				Если Не ЗначениеЗаполнено(Титульная.КодВидДок) Тогда 
					ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
						"Не указан код документа", "Титульная", "КодВидДок"));
				КонецЕсли;
				Если Не ЗначениеЗаполнено(Титульная.СерНомДок) Тогда 
					ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
						"Не указана серия и номер документа", "Титульная", "СерНомДок"));
				КонецЕсли;
				Если Не ЗначениеЗаполнено(Титульная.ДатаДок) Тогда 
					ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
						"Не указана дата выдачи документа", "Титульная", "ДатаДок"));
				КонецЕсли;
			ИначеЕсли Не СтрПодобнаПоРегулярномуВыражению(Титульная.ЕРН, "[0-9]{3}-[0-9]{3}-[0-9]{3}-[0-9]{3}") Тогда 
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указан / неправильно указан номер ЕРН", "Титульная", "ЕРН"));
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаКодаНО(Титульная.КодНО, ТаблицаОшибок, "Титульная");
	Если Не ЗначениеЗаполнено(Титульная.НаимОрг) Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
			"Не указано наименование организации", "Титульная", "НаимОрг"));
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Титульная.ДАТА_ПОДПИСИ) Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
			"Не указана дата подписи", "Титульная", "ДАТА_ПОДПИСИ"));
	КонецЕсли;
	
	УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаПодписантаНалоговойОтчетности(
		Данные, ТаблицаОшибок, "Титульная", ЭтоЮЛ Или (Титульная.ПРИЗНАК_НП_ПОДВАЛ = "2"));
	Если Титульная.ПРИЗНАК_НП_ПОДВАЛ = "2" И (Не ЗначениеЗаполнено(Титульная.НаимДок)) Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
			"Необходимо указать документ представителя", "Титульная", "НаимДок"));
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Титульная.ПрЗаяв) Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
			"Не указан признак заявителя", "Титульная", "ПрЗаяв"));
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Титульная.ПрСправк) Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
			"Не указан признак вида справки", "Титульная", "ПрСправк"));
	КонецЕсли;
	
	Если Титульная.ПрСправк = "3" Тогда 
		Если Не ЗначениеЗаполнено(Титульная.ДатаНач) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указана начальная дата периода по запросу информации", "Титульная", "ДатаНач"));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Титульная.ДатаКон) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указана конечная дата периода по запросу информации", "Титульная", "ДатаКон"));
		КонецЕсли;
		Если Год(Титульная.ДатаНач) <> Год(Титульная.ДатаКон) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Год начала и окончания периода должен совпадать", "Титульная", "ДатаКон"));
		КонецЕсли;
	ИначеЕсли Титульная.ПрСправк = "1" Тогда 
		Если Не ЗначениеЗаполнено(Титульная.ДеталСвед) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указан признак", "Титульная", "ДеталСвед"));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Титульная.ИнфПрНач) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указан признак", "Титульная", "ИнфПрНач"));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Титульная.РасчСуммПени) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указан признак", "Титульная", "РасчСуммПени"));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Титульная.ДеталНачСумм) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указан признак", "Титульная", "ДеталНачСумм"));
		КонецЕсли;
		
		Если Титульная.ДеталНачСумм = "1" Или УведомлениеОСпецрежимахНалогообложения.БлокЗаполнен(Титульная, "ПрПредДетал,Квартал,Год") Тогда 
			Если Не ЗначениеЗаполнено(Титульная.ПрПредДетал) Тогда 
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указан признак", "Титульная", "ПрПредДетал"));
			КонецЕсли;
			Если Не ЗначениеЗаполнено(Титульная.Квартал) Тогда 
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указан квартал", "Титульная", "Квартал"));
			КонецЕсли;
			УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаГода(Титульная, ТаблицаОшибок, "Титульная", "Год", Истина);
		КонецЕсли;
		
		Если Титульная.ИнфПрНач = "1" Или Титульная.ДеталНачСумм = "1" Тогда 
			Если Не ЗначениеЗаполнено(Титульная.ПрКБК) Тогда 
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указан признак", "Титульная", "ПрКБК"));
			ИначеЕсли Титульная.ПрКБК = "2" Тогда
				Для Каждого Стр Из Данные.ДанныеДопСтрокБД.МнгСтр1 Цикл 
					УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаКБК(
						Стр, ТаблицаОшибок, "ЗаявВыдСпрСостИсп", "КБК", Истина, Данные.ДанныеДопСтрокБД.МнгСтр1.Индекс(Стр) + 1);
				КонецЦикла;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(Титульная.ПрКПП) Тогда 
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указан признак", "Титульная", "ПрКПП"));
			ИначеЕсли Титульная.ПрКПП = "2" Тогда 
				Для Каждого Стр Из Данные.ДанныеДопСтрокБД.МнгСтр2 Цикл 
					УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаКПП(
						Стр, ТаблицаОшибок, "ЗаявВыдСпрСостИсп", "КПП", Истина, Данные.ДанныеДопСтрокБД.МнгСтр2.Индекс(Стр) + 1);
				КонецЦикла;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(Титульная.ПрОКТМО) Тогда 
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указан признак", "Титульная", "ПрОКТМО"));
			ИначеЕсли Титульная.ПрОКТМО = "2" Тогда 
				Для Каждого Стр Из Данные.ДанныеДопСтрокБД.МнгСтр3 Цикл 
					УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаОКТМО(
						Стр, ТаблицаОшибок, "ЗаявВыдСпрСостИсп", "ОКТМО", Истина, Данные.ДанныеДопСтрокБД.МнгСтр3.Индекс(Стр) + 1);
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ТаблицаОшибок;
КонецФункции

Функция ОсновныеСведенияЭлектронногоПредставления_Форма2023_2(Объект, УникальныйИдентификатор)
	ОсновныеСведения = Документы.УведомлениеОСпецрежимахНалогообложения.НачальнаяИнициализацияОбщихРеквизитовВыгрузки(Объект);
	Данные = Объект.ДанныеУведомления.Получить();
	ОсновныеСведения.Вставить("ПрЗаяв", Данные.ДанныеУведомления.Титульная.ПрЗаяв);
	ИдентификаторФайла = ИдентификаторФайлаЭлектронногоПредставления_Форма2023_2(ОсновныеСведения);
	ОсновныеСведения.Вставить("ИдФайл", ИдентификаторФайла);
	Возврат ОсновныеСведения;
КонецФункции

Функция ОсновныеСведенияЭлектронногоПредставления_Форма2025_1(Объект, УникальныйИдентификатор)
	ОсновныеСведения = Документы.УведомлениеОСпецрежимахНалогообложения.НачальнаяИнициализацияОбщихРеквизитовВыгрузки(Объект);
	Данные = Объект.ДанныеУведомления.Получить();
	
	Титульная = Данные.ДанныеУведомления.Титульная;
	ОсновныеСведения.Вставить("ПрЗаяв", Титульная.ПрЗаяв);
	ОсновныеСведения.Вставить("УдЛичнЗаполн", УведомлениеОСпецрежимахНалогообложения.БлокЗаполнен(Титульная, "КодВидДок,СерНомДок,ДатаДок"));
	ОсновныеСведения.Вставить("ПрСправк", Титульная.ПрСправк);
	ОсновныеСведения.Вставить("ПрКБК", Титульная.ПрКБК);
	ОсновныеСведения.Вставить("ПрКПП", Титульная.ПрКПП);
	ОсновныеСведения.Вставить("ПрОКТМО", Титульная.ПрОКТМО);
	ОсновныеСведения.Вставить("ИнфПрНач", Титульная.ИнфПрНач);
	ОсновныеСведения.Вставить("ДеталНачСумм", Титульная.ДеталНачСумм);
	
	ИдентификаторФайла = ИдентификаторФайлаЭлектронногоПредставления_Форма2025_1(ОсновныеСведения);
	ОсновныеСведения.Вставить("ИдФайл", ИдентификаторФайла);
	Возврат ОсновныеСведения;
КонецФункции

Функция ЭлектронноеПредставление_Форма2023_2(Объект, УникальныйИдентификатор)
	СведенияЭлектронногоПредставления = УведомлениеОСпецрежимахНалогообложения.СведенияЭлектронногоПредставления();
	ДанныеУведомления = УведомлениеОСпецрежимахНалогообложения.ДанныеУведомленияДляВыгрузки(Объект);
	Ошибки = ПроверитьДокументСВыводомВТаблицу_Форма2023_2(Объект, ДанныеУведомления, УникальныйИдентификатор);
	УведомлениеОСпецрежимахНалогообложения.СообщитьОшибкиПриПроверкеВыгрузки(Объект, Ошибки, ДанныеУведомления);
	
	ОсновныеСведения = ОсновныеСведенияЭлектронногоПредставления_Форма2023_2(Объект, УникальныйИдентификатор);
	СтруктураВыгрузки = Документы.УведомлениеОСпецрежимахНалогообложения.ИзвлечьСтруктуруXMLУведомления(Объект.ИмяОтчета, "СхемаВыгрузкиФорма2023_2");
	УведомлениеОСпецрежимахНалогообложения.ТиповоеЗаполнениеДанными(ДанныеУведомления, ОсновныеСведения, СтруктураВыгрузки);
	УведомлениеОСпецрежимахНалогообложения.ВыгрузитьДеревоВТаблицу(СтруктураВыгрузки, ОсновныеСведения, СведенияЭлектронногоПредставления);
	Возврат СведенияЭлектронногоПредставления;
КонецФункции

Функция ЭлектронноеПредставление_Форма2025_1(Объект, УникальныйИдентификатор)
	СведенияЭлектронногоПредставления = УведомлениеОСпецрежимахНалогообложения.СведенияЭлектронногоПредставления();
	ДанныеУведомления = УведомлениеОСпецрежимахНалогообложения.ДанныеУведомленияДляВыгрузки(Объект);
	Ошибки = ПроверитьДокументСВыводомВТаблицу_Форма2025_1(Объект, ДанныеУведомления, УникальныйИдентификатор);
	УведомлениеОСпецрежимахНалогообложения.СообщитьОшибкиПриПроверкеВыгрузки(Объект, Ошибки, ДанныеУведомления);
	
	ОсновныеСведения = ОсновныеСведенияЭлектронногоПредставления_Форма2025_1(Объект, УникальныйИдентификатор);
	СтруктураВыгрузки = Документы.УведомлениеОСпецрежимахНалогообложения.ИзвлечьСтруктуруXMLУведомления(Объект.ИмяОтчета, "СхемаВыгрузкиФорма2025_1");
	УведомлениеОСпецрежимахНалогообложения.ТиповоеЗаполнениеДанными(ДанныеУведомления, ОсновныеСведения, СтруктураВыгрузки);
	УведомлениеОСпецрежимахНалогообложения.ВыгрузитьДеревоВТаблицу(СтруктураВыгрузки, ОсновныеСведения, СведенияЭлектронногоПредставления);
	Возврат СведенияЭлектронногоПредставления;
КонецФункции

Функция СформироватьСписокЛистовФорма2020_1(Объект) Экспорт
	СписокИспользуемыхМакетов = УведомлениеОСпецрежимахНалогообложения.ЗагрузитьМакетыИзАрхива(Объект, , "БланкиПечати");
	Листы = Новый СписокЗначений;
	
	ПечатнаяФорма = УведомлениеОСпецрежимахНалогообложения.НовыйПустойЛист();
	СтруктураПараметров = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Ссылка, "ДанныеУведомления").Получить();
	ИННКПП = УведомлениеОСпецрежимахНалогообложения.ТиповаяСтруктураИННКППДляПечати(Объект, СтруктураПараметров.ДанныеУведомления.Титульная);
	
	НомСтр = 0;
	УведомлениеОСпецрежимахНалогообложения.НапечататьСтруктуру(Объект.ИмяОтчета,
		СтруктураПараметров.ДанныеУведомления["Титульная"], НомСтр, 
		УведомлениеОСпецрежимахНалогообложения.ПустойМакетИзСписка(Объект, СписокИспользуемыхМакетов, "Печать_Форма2020_1"), ПечатнаяФорма, ИННКПП);
	ПечатнаяФорма.Области.Адрес1.Текст = СтруктураПараметров.ДанныеУведомления.Титульная.Адрес1;
	ПечатнаяФорма.Области.Адрес2.Текст = СтруктураПараметров.ДанныеУведомления.Титульная.Адрес2;
	
	УведомлениеОСпецрежимахНалогообложения.ПоложитьПФВСписокЛистов(Объект, Листы, ПечатнаяФорма, НомСтр, Ложь);
	Возврат Листы;
КонецФункции

Функция СформироватьСписокЛистовФорма2023_1(Объект) Экспорт
	СписокИспользуемыхМакетов = УведомлениеОСпецрежимахНалогообложения.ЗагрузитьМакетыИзАрхива(Объект, , "БланкиПечати");
	Листы = Новый СписокЗначений;
	
	ПечатнаяФорма = УведомлениеОСпецрежимахНалогообложения.НовыйПустойЛист();
	СтруктураПараметров = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Ссылка, "ДанныеУведомления").Получить();
	ИННКПП = УведомлениеОСпецрежимахНалогообложения.ТиповаяСтруктураИННКППДляПечати(Объект, СтруктураПараметров.ДанныеУведомления.Титульная);
	
	НомСтр = 0;
	УведомлениеОСпецрежимахНалогообложения.НапечататьСтруктуру(Объект.ИмяОтчета,
		СтруктураПараметров.ДанныеУведомления["Титульная"], НомСтр, 
		УведомлениеОСпецрежимахНалогообложения.ПустойМакетИзСписка(Объект, СписокИспользуемыхМакетов, "Печать_Форма2023_1"), ПечатнаяФорма, ИННКПП);
	УведомлениеОСпецрежимахНалогообложения.ПоложитьПФВСписокЛистов(Объект, Листы, ПечатнаяФорма, НомСтр, Ложь);
	Возврат Листы;
КонецФункции

Функция СформироватьСписокЛистовФорма2023_2(Объект) Экспорт
	СписокИспользуемыхМакетов = УведомлениеОСпецрежимахНалогообложения.ЗагрузитьМакетыИзАрхива(Объект, , "БланкиПечати");
	Листы = Новый СписокЗначений;
	
	ПечатнаяФорма = УведомлениеОСпецрежимахНалогообложения.НовыйПустойЛист();
	СтруктураПараметров = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Ссылка, "ДанныеУведомления").Получить();
	ИННКПП = УведомлениеОСпецрежимахНалогообложения.ТиповаяСтруктураИННКППДляПечати(Объект, СтруктураПараметров.ДанныеУведомления.Титульная);
	
	НомСтр = 0;
	УведомлениеОСпецрежимахНалогообложения.НапечататьСтруктуру(Объект.ИмяОтчета,
		СтруктураПараметров.ДанныеУведомления["Титульная"], НомСтр,
		УведомлениеОСпецрежимахНалогообложения.ПустойМакетИзСписка(Объект, СписокИспользуемыхМакетов, "Печать_Форма2023_2"), ПечатнаяФорма, ИННКПП);
	УведомлениеОСпецрежимахНалогообложения.ПоложитьПФВСписокЛистов(Объект, Листы, ПечатнаяФорма, НомСтр, Ложь);
	Возврат Листы;
КонецФункции

Функция СформироватьСписокЛистовФорма2025_1(Объект) Экспорт
	СписокИспользуемыхМакетов = УведомлениеОСпецрежимахНалогообложения.ЗагрузитьМакетыИзАрхива(Объект, , "БланкиПечати");
	Листы = Новый СписокЗначений;
	
	ПечатнаяФорма = УведомлениеОСпецрежимахНалогообложения.НовыйПустойЛист();
	СтруктураПараметров = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Ссылка, "ДанныеУведомления").Получить();
	ИННКПП = УведомлениеОСпецрежимахНалогообложения.ТиповаяСтруктураИННКППДляПечати(Объект, СтруктураПараметров.ДанныеУведомления.Титульная);
	
	Если СтрПодобнаПоРегулярномуВыражению(СтруктураПараметров.ДанныеУведомления.Титульная.ЕРН, "[0-9]{3}-[0-9]{3}-[0-9]{3}-[0-9]{3}") Тогда 
		СтруктураПараметров.ДанныеУведомления.Титульная.ЕРН = СтрЗаменить(СтруктураПараметров.ДанныеУведомления.Титульная.ЕРН, "-", "");
	Иначе
		СтруктураПараметров.ДанныеУведомления.Титульная.ЕРН = "";
	КонецЕсли;
	
	НомСтр = 0;
	УведомлениеОСпецрежимахНалогообложения.НапечататьСтруктуру(Объект.ИмяОтчета, СтруктураПараметров.ДанныеУведомления["Титульная"], НомСтр,
		УведомлениеОСпецрежимахНалогообложения.ПустойМакетИзСписка(Объект, СписокИспользуемыхМакетов, "Печать_Форма2025_1_Титульная1"),
		ПечатнаяФорма, ИННКПП);
	УведомлениеОСпецрежимахНалогообложения.ПоложитьПФВСписокЛистов(Объект, Листы, ПечатнаяФорма, НомСтр, Ложь);
	
	УведомлениеОСпецрежимахНалогообложения.НапечататьСтруктуру(Объект.ИмяОтчета, СтруктураПараметров.ДанныеУведомления["Титульная"], НомСтр,
		УведомлениеОСпецрежимахНалогообложения.ПустойМакетИзСписка(Объект, СписокИспользуемыхМакетов, "Печать_Форма2025_1_Титульная2"),
		ПечатнаяФорма, ИННКПП);
	УведомлениеОСпецрежимахНалогообложения.ПоложитьПФВСписокЛистов(Объект, Листы, ПечатнаяФорма, НомСтр, Ложь);
	
	УведомлениеОСпецрежимахНалогообложения.НапечататьСтраницыСМногострочнойЧастью(
		Объект, Листы, СтруктураПараметров,"ЗаявВыдСпрСостИсп", "МнгСтр1",
		УведомлениеОСпецрежимахНалогообложения.ПустойМакетИзСписка(Объект, СписокИспользуемыхМакетов, "Печать_Форма2025_1_КБК"),
		НомСтр, ИННКПП);
	
	УведомлениеОСпецрежимахНалогообложения.НапечататьСтраницыСМногострочнойЧастью(
		Объект, Листы, СтруктураПараметров,"ЗаявВыдСпрСостИсп", "МнгСтр2",
		УведомлениеОСпецрежимахНалогообложения.ПустойМакетИзСписка(Объект, СписокИспользуемыхМакетов, "Печать_Форма2025_1_КПП"),
		НомСтр, ИННКПП);
		
	УведомлениеОСпецрежимахНалогообложения.НапечататьСтраницыСМногострочнойЧастью(
		Объект, Листы, СтруктураПараметров,"ЗаявВыдСпрСостИсп", "МнгСтр3",
		УведомлениеОСпецрежимахНалогообложения.ПустойМакетИзСписка(Объект, СписокИспользуемыхМакетов, "Печать_Форма2025_1_ОКТМО"),
		НомСтр, ИННКПП);
	
	Возврат Листы;
КонецФункции

#КонецОбласти
#КонецЕсли
