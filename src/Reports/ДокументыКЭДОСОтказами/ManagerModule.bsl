#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВариантыОтчетов

// См. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.
//
Процедура НастроитьВариантыОтчета(Настройки, НастройкиОтчета) Экспорт
	
	НастройкиОтчета.ОпределитьНастройкиФормы = Истина;
	
	НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "ДокументыСОтказамиПодписания");
	НастройкиВарианта.Описание = НСтр("ru = 'Отчет показывает документы КЭДО с которыми сотрудники не согласны';
										|en = 'Отчет показывает документы КЭДО с которыми сотрудники не согласны'");
	НастройкиВарианта.ВидимостьПоУмолчанию = Ложь;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВариантыОтчетов

#КонецОбласти

#КонецОбласти
	
#Область СлужебныйПрограммныйИнтерфейс

Процедура ДобавитьКомандыФормированияОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	Если Не ПравоДоступа("Просмотр", Метаданные.Отчеты.ДокументыКЭДОСОтказами) Тогда
		Возврат;
	КонецЕсли;
	
	Команда = КомандыОтчетов.Добавить();
	Команда.Представление      = НСтр("ru = 'С отказами подписания';
										|en = 'С отказами подписания'");
	Команда.Важность           = "Обычное";
	Команда.Менеджер           = "Отчет.ДокументыКЭДОСОтказами";
	Команда.КлючВарианта       = "ДокументыСОтказамиПодписания";
	Команда.СочетаниеКлавиш    = Новый СочетаниеКлавиш(Клавиша.A, Ложь, Истина, Истина);
	Команда.Подменю            = "ДокументыКЭДОСотрудников";
	Команда.Картинка           = БиблиотекаКартинок.Отчет;
	Команда.Назначение         = "ДляСписка";
	
КонецПроцедуры

Функция ВнешниеНаборыДанных() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	КомментарииФайловДокументовКЭДО.ПрисоединенныйФайл КАК ПрисоединенныйФайл,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(КомментарииФайловДокументовКЭДО.Исполнитель) = ТИП(Справочник.ФизическиеЛица)
		|			ТОГДА КомментарииФайловДокументовКЭДО.Исполнитель
		|		ИНАЧЕ КомментарииФайловДокументовКЭДО.Исполнитель.ФизическоеЛицо
		|	КОНЕЦ КАК Исполнитель,
		|	КомментарииФайловДокументовКЭДО.Комментарий КАК Комментарий,
		|	КомментарииФайловДокументовКЭДО.ДатаКомментария КАК ДатаКомментария
		|ИЗ
		|	РегистрСведений.КомментарииФайловДокументовКЭДО КАК КомментарииФайловДокументовКЭДО";
	
	УстановитьПривилегированныйРежим(Истина);
	ВнешниеНаборы = Новый Структура("КомментарииЭлектронныхДокументов",  Запрос.Выполнить().Выгрузить());
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ВнешниеНаборы
	
КонецФункции

#КонецОбласти
	
#КонецЕсли