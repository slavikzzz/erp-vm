#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.Свойство("UIDФорма1СОтчетность", UIDФорма1СОтчетность);
	Параметры.Свойство("ВидУведомления", ВидУведомления);
	
	Если Параметры.Свойство("Ключ") И ЗначениеЗаполнено(Объект.Ссылка) Тогда 
		Организация = Объект.Организация;
	Иначе
		Организация = Параметры.Организация;
		Параметры.Свойство("Организация", Объект.Организация);
	КонецЕсли;
	
	Заголовок = ВидУведомления;
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.ДатаПодписи = ТекущаяДатаСеанса();
		Объект.ВидУведомления = ВидУведомления;
		Заголовок = Заголовок + " (создание)";
	КонецЕсли;
	
	Разложение = СтрРазделить(ИмяФормы, ".");
	Объект.ИмяФормы = Разложение[3];
	Объект.ИмяОтчета = Разложение[1];
	
	РегламентированнаяОтчетность.ДобавитьКнопкуПрисоединенныеФайлы(ЭтотОбъект);
	РегламентированнаяОтчетностьКлиентСервер.ПриИнициализацииФормыРегламентированногоОтчета(ЭтотОбъект);
	СформироватьМакетНаСервере();
	ЗагрузитьДанные();
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Элементы.НаименованиеЭтапа.Заголовок = "В работе";
	Иначе
		Статус = РегламентированнаяОтчетность.СохраненныйСтатусОтправкиУведомления(Объект.Ссылка);
		Если Статус <> Неопределено Тогда
			Элементы.НаименованиеЭтапа.Заголовок
			= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1';
																			|en = '%1'"), Статус);
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда 
		ЗаблокироватьДанныеДляРедактирования(Объект.Ссылка, , УникальныйИдентификатор);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		ПриЗакрытииНаСервере();
	КонецЕсли;
	Оповестить("Запись_УведомлениеОСпецрежимахНалогообложения",,Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	РегламентированнаяОтчетностьКлиент.ПередЗакрытиемРегламентированногоОтчета(ЭтотОбъект, Отказ, СтандартнаяОбработка, ЗавершениеРаботы, ТекстПредупреждения);
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("Запись_УведомлениеОСпецрежимахНалогообложения", ПараметрыЗаписи, Объект.Ссылка);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПредставлениеУведомленияПриИзмененииСодержимогоОбласти(Элемент, Область)
	ДанныеСтраницы[Область.Имя] = Область.Значение;
	Модифицированность = Истина;
	
	Если ВидУведомления = ПредопределенноеЗначение("Перечисление.ВидыУведомленийОСпецрежимахНалогообложения.ОсвобождениеОтУплатыНДС")
		Или ВидУведомления = ПредопределенноеЗначение("Перечисление.ВидыУведомленийОСпецрежимахНалогообложения.ОсвобождениеОтНДСПриЕСХН") Тогда
		
		ПредставлениеУведомленияПриИзмененииСодержимогоОбласти_Освобождение(Область);
	ИначеЕсли ВидУведомления = ПредопределенноеЗначение("Перечисление.ВидыУведомленийОСпецрежимахНалогообложения.Уведомление1НачалоУплаты") Тогда 
		ПредставлениеУведомленияПриИзмененииСодержимогоОбласти_Уведомление1НачалоУплаты(Область);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеУведомленияВыбор(Элемент, Область, СтандартнаяОбработка)
	Если Область.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник
		И Область.СодержитЗначение = Истина
		И ТипЗнч(Область.Значение) = Тип("Булево") Тогда 
	
		СтандартнаяОбработка = Ложь;
		Область.Значение = Не Область.Значение;
		Модифицированность = Истина;
	КонецЕсли;
	
	Если Объект.ВидУведомления = ПредопределенноеЗначение("Перечисление.ВидыУведомленийОСпецрежимахНалогообложения.ЗачетСФР") Тогда 
		ПредставлениеУведомленияВыбор_ЗачетСФР(Элемент, Область, СтандартнаяОбработка);
	ИначеЕсли Объект.ВидУведомления = ПредопределенноеЗначение("Перечисление.ВидыУведомленийОСпецрежимахНалогообложения.ОбработкаПерсональныхДанных")
		Или Объект.ВидУведомления = ПредопределенноеЗначение("Перечисление.ВидыУведомленийОСпецрежимахНалогообложения.ИзменениеСведенийОбработкаПерсональныхДанных")
		Или Объект.ВидУведомления = ПредопределенноеЗначение("Перечисление.ВидыУведомленийОСпецрежимахНалогообложения.ПрекращениеОбработкиПерсональныхДанных") Тогда
		ПредставлениеУведомленияВыбор_ПерсональныеДанные(Элемент, Область, СтандартнаяОбработка)
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаНаименованиеСтатусаНажатие(Элемент)
	ПараметрыИзменения = Новый Структура;
	ПараметрыИзменения.Вставить("Форма", ЭтотОбъект);
	ПараметрыИзменения.Вставить("Организация", Объект.Организация);
	ПараметрыИзменения.Вставить("КонтролирующийОрган",
		ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ПустаяСсылка"));
	ПараметрыИзменения.Вставить("ТекстВопроса", НСтр("ru = 'Вы уверены, что уведомление уже сдано?';
													|en = 'Вы уверены, что уведомление уже сдано?'"));
	
	РегламентированнаяОтчетностьКлиент.ИзменитьСтатусОтправки(ПараметрыИзменения);
КонецПроцедуры

&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)
	СохранитьДанные();
	Оповестить("Запись_УведомлениеОСпецрежимахНалогообложения",,Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	СохранитьДанные();
	Оповестить("Запись_УведомлениеОСпецрежимахНалогообложения",,Объект.Ссылка);
	Закрыть(Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура ПечатьБРО(Команда)
	ПечатьБРОНаСервере();
	РегламентированнаяОтчетностьКлиент.ОткрытьФормуПредварительногоПросмотра(
		ЭтотОбъект, "Открыть", Ложь, ЭтотОбъект["СтруктураРеквизитовУведомления"].СписокПечатаемыхЛистов);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗагрузитьДанные()
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ЗаполнитьТитульный();
		
		Если Параметры.Свойство("ЗначениеКопирования")
			И ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда 
			
			СтруктураПараметров = Параметры.ЗначениеКопирования.ДанныеУведомления.Получить();
			Для Каждого Обл Из ПредставлениеУведомления.Области Цикл 
				Если Обл.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник
					И Обл.СодержитЗначение = Истина Тогда 
					
					СтруктураПараметров.Титульный.Свойство(Обл.Имя, Обл.Значение);
					ДанныеСтраницы.Вставить(Обл.Имя, Обл.Значение);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	СтруктураПараметров = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Ссылка, "ДанныеУведомления").Получить();
	Для Каждого Обл Из ПредставлениеУведомления.Области Цикл 
		Если Обл.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник
			И Обл.СодержитЗначение = Истина Тогда 
			
			СтруктураПараметров.Титульный.Свойство(Обл.Имя, Обл.Значение);
			ДанныеСтраницы.Вставить(Обл.Имя, Обл.Значение);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура СохранитьДанные() Экспорт
	Если ЗначениеЗаполнено(Объект.Ссылка) И Не Модифицированность Тогда 
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.ВидУведомления = ВидУведомления;
		Объект.Организация = Организация;
		Объект.Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Для Каждого Обл Из ПредставлениеУведомления.Области Цикл 
		Если Обл.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник
			И Обл.СодержитЗначение = Истина Тогда 
			
			ДанныеСтраницы[Обл.Имя] = Обл.Значение;
		КонецЕсли;
	КонецЦикла;
	
	СтруктураПараметров = Новый Структура("Титульный, ИмяЭкраннойФормы", ДанныеСтраницы, ИмяЭкраннойФормы);
	УведомлениеОСпецрежимахНалогообложения.СохранитьДанные(ЭтотОбъект, СтруктураПараметров);
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТитульный_ВозмещениеПроизводственныхРасходов()
	Если РегламентированнаяОтчетностьПереопределяемый.ЭтоЮридическоеЛицо(Организация) Тогда 
		СтрокаСведений = "НаимЮЛПол,ТелОрганизации,БанкСчетНомер,БанкСчетНаимБанка,БанкСчетБИКБанка,АдресЭлектроннойПочтыОрганизации";
		СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Организация, Объект.ДатаПодписи, СтрокаСведений);
		
		ПредставлениеУведомления.Области["Данные1"].Значение = СведенияОбОрганизации.НаимЮЛПол;
		ПредставлениеУведомления.Области["Данные26"].Значение = СведенияОбОрганизации.ТелОрганизации;
		ПредставлениеУведомления.Области["Данные27"].Значение = СведенияОбОрганизации.АдресЭлектроннойПочтыОрганизации;
		ПредставлениеУведомления.Области["Данные11"].Значение = СведенияОбОрганизации.БанкСчетНомер;
		ПредставлениеУведомления.Области["Данные10"].Значение = СведенияОбОрганизации.БанкСчетНаимБанка;
		ПредставлениеУведомления.Области["Данные12"].Значение = СведенияОбОрганизации.БанкСчетБИКБанка;
	Иначе 
		СтрокаСведений = "ФИО,ТелДом,БанкСчетНомер,БанкСчетНаимБанка,БанкСчетБИКБанка,АдресЭлектроннойПочтыОрганизации";
		СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Организация, Объект.ДатаПодписи, СтрокаСведений);
		
		ПредставлениеУведомления.Области["Данные1"].Значение = СведенияОбОрганизации.ФИО;
		ПредставлениеУведомления.Области["Данные26"].Значение = СведенияОбОрганизации.ТелДом;
		ПредставлениеУведомления.Области["Данные27"].Значение = СведенияОбОрганизации.АдресЭлектроннойПочтыОрганизации;
		ПредставлениеУведомления.Области["Данные11"].Значение = СведенияОбОрганизации.БанкСчетНомер;
		ПредставлениеУведомления.Области["Данные10"].Значение = СведенияОбОрганизации.БанкСчетНаимБанка;
		ПредставлениеУведомления.Области["Данные12"].Значение = СведенияОбОрганизации.БанкСчетБИКБанка;
	КонецЕсли;
	
	ПредставлениеУведомления.Области["ДатаПодписи"].Значение = Объект.ДатаПодписи;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТитульный_ПрибытиеГражданинаРФ()
	Если РегламентированнаяОтчетностьПереопределяемый.ЭтоЮридическоеЛицо(Организация) Тогда 
		СтрокаСведений = "НаимЮЛПол,ПолныйАдрЮР";
		СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Организация, Объект.ДатаПодписи, СтрокаСведений);
		ПредставлениеУведомления.Области["ОсновныеСведения1"].Значение = СведенияОбОрганизации.НаимЮЛПол;
		ПредставлениеУведомления.Области["ОсновныеСведения2"].Значение = СведенияОбОрганизации.ПолныйАдрЮР;
	Иначе 
		СтрокаСведений = "ФИО";
		СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Организация, Объект.ДатаПодписи, СтрокаСведений);
		ПредставлениеУведомления.Области["ОсновныеСведения1"].Значение = СведенияОбОрганизации.ФИО;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТитульный_УбытиеИностранца()
	Если РегламентированнаяОтчетностьПереопределяемый.ЭтоЮридическоеЛицо(Организация) Тогда 
		СтрокаСведений = "НаимЮЛПол,ПолныйАдрЮР,ИННЮЛ";
		СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Организация, Объект.ДатаПодписи, СтрокаСведений);
		ПредставлениеУведомления.Области["НаимОрг"].Значение = СведенияОбОрганизации.НаимЮЛПол;
		ПредставлениеУведомления.Области["АдресОрг"].Значение = СведенияОбОрганизации.ПолныйАдрЮР;
		ПредставлениеУведомления.Области["ИНН"].Значение = СведенияОбОрганизации.ИННЮЛ;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТитульный_Освобождение()
	Если РегламентированнаяОтчетностьПереопределяемый.ЭтоЮридическоеЛицо(Организация) Тогда 
		СтрокаСведений = "ИННЮЛ,КППЮЛ,НаимЮЛПол,ПолныйАдрЮР,ТелОрганизации";
		ДП = ?(ЗначениеЗаполнено(Объект.ДатаПодписи), Объект.ДатаПодписи, ТекущаяДатаСеанса());
		СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Организация, ДП, СтрокаСведений);
		ПредставлениеУведомления.Области["НПЛ"].Значение = СведенияОбОрганизации.НаимЮЛПол;
		ПредставлениеУведомления.Области["ИННКПП"].Значение = СведенияОбОрганизации.ИННЮЛ + "/" + СведенияОбОрганизации.КППЮЛ;
		ПредставлениеУведомления.Области["АддрТел1"].Значение = СведенияОбОрганизации.ПолныйАдрЮР;
		ПредставлениеУведомления.Области["АддрТел2"].Значение = СведенияОбОрганизации.ТелОрганизации;
		ПредставлениеУведомления.Области["НаименованиеНПЛ"].Значение = СведенияОбОрганизации.НаимЮЛПол;
		ПредставлениеУведомления.Области["ДатаОт"].Значение = Объект.ДатаПодписи;
		
		КИ = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Организация, Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации, ТекущаяДатаСеанса(), Ложь);
		Если ТипЗнч(КИ) = Тип("ТаблицаЗначений")
			И КИ.Количество() = 1
			И КИ.Колонки.Найти("Значение") <> Неопределено Тогда 
			
			ПредставлениеУведомления.Области["АддрТел1"].Значение = КИ[0].Представление;
		КонецЕсли;
	Иначе 
		СтрокаСведений = "ИННФЛ,ФИО,ТелДом,АдрПрописки";
		ДП = ?(ЗначениеЗаполнено(Объект.ДатаПодписи), Объект.ДатаПодписи, ТекущаяДатаСеанса());
		СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Организация, ДП, СтрокаСведений);
		ПредставлениеУведомления.Области["НПЛ"].Значение = СведенияОбОрганизации.ФИО;
		ПредставлениеУведомления.Области["ИННКПП"].Значение = СведенияОбОрганизации.ИННФЛ;
		ПредставлениеУведомления.Области["АддрТел1"].Значение = СведенияОбОрганизации.АдрПрописки;
		ПредставлениеУведомления.Области["АддрТел2"].Значение = СведенияОбОрганизации.ТелДом;
		ПредставлениеУведомления.Области["НаименованиеНПЛ"].Значение = СведенияОбОрганизации.ФИО;
		ПредставлениеУведомления.Области["ДатаОт"].Значение = Объект.ДатаПодписи;
		
		КИ = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Организация, Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации, ТекущаяДатаСеанса(), Ложь);
		Если ТипЗнч(КИ) = Тип("ТаблицаЗначений")
			И КИ.Количество() = 1
			И КИ.Колонки.Найти("Значение") <> Неопределено Тогда 
			
			ПредставлениеУведомления.Области["АддрТел1"].Значение = КИ[0].Представление;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТитульный_ЗаявлениеНаПриемДокументовФСС()
	Если РегламентированнаяОтчетностьПереопределяемый.ЭтоЮридическоеЛицо(Организация) Тогда 
		СтрокаСведений = "ИННЮЛ,КППЮЛ,НаимЮЛПол,ПолныйАдрЮР,ТелОрганизации";
		ДП = ?(ЗначениеЗаполнено(Объект.ДатаПодписи), Объект.ДатаПодписи, ТекущаяДатаСеанса());
		СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Организация, ДП, СтрокаСведений);
		ПредставлениеУведомления.Области["Страхователь"].Значение = СведенияОбОрганизации.НаимЮЛПол;
		ПредставлениеУведомления.Области["Реквизит3"].Значение = СведенияОбОрганизации.ИННЮЛ;
		ПредставлениеУведомления.Области["Реквизит4"].Значение = СведенияОбОрганизации.КППЮЛ;
		ПредставлениеУведомления.Области["ДатаОт"].Значение = Объект.ДатаПодписи;
	Иначе 
		СтрокаСведений = "ИННФЛ,ФИО,ТелДом,АдрПрописки";
		ДП = ?(ЗначениеЗаполнено(Объект.ДатаПодписи), Объект.ДатаПодписи, ТекущаяДатаСеанса());
		СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Организация, ДП, СтрокаСведений);
		ПредставлениеУведомления.Области["Страхователь"].Значение = СведенияОбОрганизации.ФИО;
		ПредставлениеУведомления.Области["Реквизит3"].Значение = СведенияОбОрганизации.ИННФЛ;
		ПредставлениеУведомления.Области["ДатаОт"].Значение = Объект.ДатаПодписи;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТитульный_ВозвратСуммыНВОС()
	Если РегламентированнаяОтчетностьПереопределяемый.ЭтоЮридическоеЛицо(Организация) Тогда 
		СтрокаСведений = "ИННЮЛ,КППЮЛ,НаимЮЛПол,ПолныйАдрЮР,ТелОрганизации";
		ДП = ?(ЗначениеЗаполнено(Объект.ДатаПодписи), Объект.ДатаПодписи, ТекущаяДатаСеанса());
		СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Организация, ДП, СтрокаСведений);
		ПредставлениеУведомления.Области["НаименованиеПриродопользователя"].Значение = СведенияОбОрганизации.НаимЮЛПол + " ИНН:" + СведенияОбОрганизации.ИННЮЛ + " КПП:" + СведенияОбОрганизации.КППЮЛ + " Адрес:" + СведенияОбОрганизации.ПолныйАдрЮР;
		ПредставлениеУведомления.Области["ДатаОт"].Значение = Объект.ДатаПодписи;
	Иначе 
		СтрокаСведений = "ИННФЛ,ФИО,ТелДом,АдрПрописки";
		ДП = ?(ЗначениеЗаполнено(Объект.ДатаПодписи), Объект.ДатаПодписи, ТекущаяДатаСеанса());
		СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Организация, ДП, СтрокаСведений);
		ПредставлениеУведомления.Области["НаименованиеПриродопользователя"].Значение = СведенияОбОрганизации.ФИО + " ИНН:" + СведенияОбОрганизации.ИННФЛ + " Адрес:" + СведенияОбОрганизации.АдрПрописки;
		ПредставлениеУведомления.Области["ДатаОт"].Значение = Объект.ДатаПодписи;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТитульный_ЗачетСуммыНВОС()
	Если РегламентированнаяОтчетностьПереопределяемый.ЭтоЮридическоеЛицо(Организация) Тогда 
		СтрокаСведений = "ИННЮЛ,КППЮЛ,НаимЮЛПол,ПолныйАдрЮР,ТелОрганизации";
		ДП = ?(ЗначениеЗаполнено(Объект.ДатаПодписи), Объект.ДатаПодписи, ТекущаяДатаСеанса());
		СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Организация, ДП, СтрокаСведений);
		ПредставлениеУведомления.Области["НаименованиеПриродопользователя"].Значение = СведенияОбОрганизации.НаимЮЛПол + " ИНН:" + СведенияОбОрганизации.ИННЮЛ + " КПП:" + СведенияОбОрганизации.КППЮЛ + " Адрес:" + СведенияОбОрганизации.ПолныйАдрЮР;
		ПредставлениеУведомления.Области["ДатаОт"].Значение = Объект.ДатаПодписи;
	Иначе 
		СтрокаСведений = "ИННФЛ,ФИО,ТелДом,АдрПрописки";
		ДП = ?(ЗначениеЗаполнено(Объект.ДатаПодписи), Объект.ДатаПодписи, ТекущаяДатаСеанса());
		СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Организация, ДП, СтрокаСведений);
		ПредставлениеУведомления.Области["НаименованиеПриродопользователя"].Значение = СведенияОбОрганизации.ФИО + " ИНН:" + СведенияОбОрганизации.ИННФЛ + " Адрес:" + СведенияОбОрганизации.АдрПрописки;
		ПредставлениеУведомления.Области["ДатаОт"].Значение = Объект.ДатаПодписи;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТитульный_Уведомление1_2()
	РегистрацияВИФНС = Документы.УведомлениеОСпецрежимахНалогообложения.РегистрацияВФНСОрганизации(Объект.Организация);
	РеквНо = РегистрацияВНОСервер.ДанныеРегистрации(РегистрацияВИФНС);
	ПредставлениеУведомления.Области["НаименованиеНО"].Значение = РеквНо.Наименование;
	ПредставлениеУведомления.Области["КодНО"].Значение = РеквНо.Код;
	
	СтрокаСведений = "ИННЮЛ,КППЮЛ,НаимЮЛПол,ПолныйАдрЮР,ТелОрганизации,ФИОРук,ТелРук";
	ДП = ?(ЗначениеЗаполнено(Объект.ДатаПодписи), Объект.ДатаПодписи, ТекущаяДатаСеанса());
	СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Организация, ДП, СтрокаСведений);
	ПредставлениеУведомления.Области["ИНН"].Значение = СведенияОбОрганизации.ИННЮЛ;
	ПредставлениеУведомления.Области["КПП"].Значение = СведенияОбОрганизации.КППЮЛ;
	ПредставлениеУведомления.Области["НаименованиеОрг2"].Значение = СведенияОбОрганизации.НаимЮЛПол;
	ПредставлениеУведомления.Области["ФИОРук"].Значение = СведенияОбОрганизации.ФИОРук;
	ПредставлениеУведомления.Области["ТлфРук"].Значение = СведенияОбОрганизации.ТелРук;
	ПредставлениеУведомления.Области["ДатаОт"].Значение = Объект.ДатаПодписи;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТитульный_ОбработкаПерсональныхДанных()
	Если РегламентированнаяОтчетностьПереопределяемый.ЭтоЮридическоеЛицо(Организация) Тогда 
		СтрокаСведений = "ИННЮЛ,НаимЮЛПол,НаимЮЛСокр,АдрПочт,ОГРН,ДолжнРук,ФамилияРук,ИмяРук,ОтчествоРук";
		ДП = ?(ЗначениеЗаполнено(Объект.ДатаПодписи), Объект.ДатаПодписи, ТекущаяДатаСеанса());
		СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Организация, ДП, СтрокаСведений);
		ПредставлениеУведомления.Области["ФИО_Должность"].Значение = СведенияОбОрганизации.ФамилияРук + " " 
			+ СведенияОбОрганизации.ИмяРук + " " + СведенияОбОрганизации.ОтчествоРук + ", " + СведенияОбОрганизации.ДолжнРук;
		ПредставлениеУведомления.Области["Данные1"].Значение = СведенияОбОрганизации.НаимЮЛПол + " / "
			+ СведенияОбОрганизации.НаимЮЛСокр + " ИНН:" + СведенияОбОрганизации.ИННЮЛ + " ОГРН:" + СведенияОбОрганизации.ОГРН;
		ПредставлениеУведомления.Области["Данные2"].Значение = СведенияОбОрганизации.АдрПочт;
	Иначе 
		СтрокаСведений = "ИННФЛ,ФИО,АдрПрописки,ОГРН";
		ДП = ?(ЗначениеЗаполнено(Объект.ДатаПодписи), Объект.ДатаПодписи, ТекущаяДатаСеанса());
		СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Организация, ДП, СтрокаСведений);
		ПредставлениеУведомления.Области["ФИО_Должность"].Значение = СведенияОбОрганизации.ФИО + ", индивидуальный предприниматель";
		ПредставлениеУведомления.Области["Данные1"].Значение = СведенияОбОрганизации.ФИО 
			+ " ИНН:" + СведенияОбОрганизации.ИННФЛ + " ОГРН:" + СведенияОбОрганизации.ОГРН;
		ПредставлениеУведомления.Области["Данные2"].Значение = СведенияОбОрганизации.АдрПрописки;
	КонецЕсли;
	
	ПредставлениеУведомления.Области["ДатаПодписи"].Значение = ДП;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТитульный_ЗачетВозвратСФР()
	ДП = ?(ЗначениеЗаполнено(Объект.ДатаПодписи), Объект.ДатаПодписи, ТекущаяДатаСеанса());
	Если РегламентированнаяОтчетностьПереопределяемый.ЭтоЮридическоеЛицо(Организация) Тогда 
		СтрокаСведений = "ИННЮЛ,КППЮЛ,НаимЮЛПол,ПолныйАдрЮР,ТелОрганизации,ФИОРук,ДолжнРук,ТелРук,ФИОБух,ТелБух,РегНомПФР";
		СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Организация, ДП, СтрокаСведений);
		ПредставлениеУведомления.Области["Страхователь1"].Значение = СведенияОбОрганизации.НаимЮЛПол;
		ПредставлениеУведомления.Области["ИНН"].Значение = СведенияОбОрганизации.ИННЮЛ;
		ПредставлениеУведомления.Области["КПП"].Значение = СведенияОбОрганизации.КППЮЛ;
		ПредставлениеУведомления.Области["Адрес"].Значение = СведенияОбОрганизации.ПолныйАдрЮР;
	Иначе 
		СтрокаСведений = "ИННФЛ,ФИО,ТелДом,АдрПрописки,ФИОРук,ДолжнРук,ТелРук,ФИОБух,ТелБух,РегНомПФР";
		СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Организация, ДП, СтрокаСведений);
		ПредставлениеУведомления.Области["Страхователь1"].Значение = СведенияОбОрганизации.ФИО;
		ПредставлениеУведомления.Области["ИНН"].Значение = СведенияОбОрганизации.ИННФЛ;
		ПредставлениеУведомления.Области["Адрес"].Значение = СведенияОбОрганизации.АдрПрописки;
	КонецЕсли;
	
	ПредставлениеУведомления.Области["ФИОРук"].Значение = СведенияОбОрганизации.ФИОРук;
	ПредставлениеУведомления.Области["ДолжнРук"].Значение = СведенияОбОрганизации.ДолжнРук;
	ПредставлениеУведомления.Области["ТлфРук"].Значение = СведенияОбОрганизации.ТелРук;
	ПредставлениеУведомления.Области["ФИОБух"].Значение = СведенияОбОрганизации.ФИОБух;
	ПредставлениеУведомления.Области["ТлфБух"].Значение = СведенияОбОрганизации.ТелБух;
	ПредставлениеУведомления.Области["РегНомерСФР"].Значение = СведенияОбОрганизации.РегНомПФР;
	ПредставлениеУведомления.Области["ДАТА_ПОДПИСИ"].Значение = Объект.ДатаПодписи;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТитульный_Экосбор()
	ДП = ?(ЗначениеЗаполнено(Объект.ДатаПодписи), Объект.ДатаПодписи, ТекущаяДатаСеанса());
	Если РегламентированнаяОтчетностьПереопределяемый.ЭтоЮридическоеЛицо(Организация) Тогда 
		СтрокаСведений = "ИННЮЛ,КППЮЛ,НаимЮЛПол,ПолныйАдрЮР,ОКОПФ";
		СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Организация, ДП, СтрокаСведений);
		Представление = СведенияОбОрганизации.НаимЮЛПол + " ИНН: " + СведенияОбОрганизации.ИННЮЛ + " КПП: " + СведенияОбОрганизации.КППЮЛ
			+ " Адрес: " + СведенияОбОрганизации.ПолныйАдрЮР + " ОКОПФ: " + СведенияОбОрганизации.ОКОПФ;
		Обл = ПредставлениеУведомления.Области.Найти("НаимРеквОрг");
		Если ТипЗнч(Обл) = Тип("ОбластьЯчеекТабличногоДокумента") Тогда 
			Обл.Значение = Представление;
		КонецЕсли;
		Обл = ПредставлениеУведомления.Области.Найти("НаимЮЛРекв");
		Если ТипЗнч(Обл) = Тип("ОбластьЯчеекТабличногоДокумента") Тогда 
			Обл.Значение = Представление;
		КонецЕсли;
	Иначе 
		СтрокаСведений = "ИННФЛ,ФИО,АдрПрописки,ОКОПФ";
		СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Организация, ДП, СтрокаСведений);
		Представление = СведенияОбОрганизации.ФИО + " ИНН: " + СведенияОбОрганизации.ИННФЛ
			+ " Адрес: " + СведенияОбОрганизации.АдрПрописки + " ОКОПФ: " + СведенияОбОрганизации.ОКОПФ;
		Обл = ПредставлениеУведомления.Области.Найти("НаимРеквОрг");
		Если ТипЗнч(Обл) = Тип("ОбластьЯчеекТабличногоДокумента") Тогда 
			Обл.Значение = Представление;
		КонецЕсли;
		Обл = ПредставлениеУведомления.Области.Найти("НаимФЛРекв");
		Если ТипЗнч(Обл) = Тип("ОбластьЯчеекТабличногоДокумента") Тогда 
			Обл.Значение = Представление;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТитульный()
	Если ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ОсвобождениеОтУплатыНДС
		Или ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ОсвобождениеОтНДСПриЕСХН Тогда 
		
		ЗаполнитьТитульный_Освобождение();
	ИначеЕсли ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ВозвратСуммыНВОС Тогда 
		ЗаполнитьТитульный_ВозвратСуммыНВОС();
	ИначеЕсли ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ЗачетСуммыНВОС Тогда 
		ЗаполнитьТитульный_ЗачетСуммыНВОС();
	ИначеЕсли ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ЗаявлениеНаПриемДокументовФСС Тогда
		ЗаполнитьТитульный_ЗаявлениеНаПриемДокументовФСС();
	ИначеЕсли ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.Уведомление1НачалоУплаты
		Или ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.Уведомление2ОтказОтУплаты Тогда
		ЗаполнитьТитульный_Уведомление1_2();
	ИначеЕсли ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ОбработкаПерсональныхДанных Тогда
		ЗаполнитьТитульный_ОбработкаПерсональныхДанных();
	ИначеЕсли ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ИзменениеСведенийОбработкаПерсональныхДанных Тогда
		ЗаполнитьТитульный_ОбработкаПерсональныхДанных();
	ИначеЕсли ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ПрекращениеОбработкиПерсональныхДанных Тогда
		ЗаполнитьТитульный_ОбработкаПерсональныхДанных();
	ИначеЕсли ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ВозмещениеПроизводственныхРасходов Тогда
		ЗаполнитьТитульный_ВозмещениеПроизводственныхРасходов();
	ИначеЕсли ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ПрибытиеГражданинаРФ Тогда
		ЗаполнитьТитульный_ПрибытиеГражданинаРФ();
	ИначеЕсли ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ЗачетСФР
		Или ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ВозвратСФР Тогда
		ЗаполнитьТитульный_ЗачетВозвратСФР();
	ИначеЕсли ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.УбытиеИностранца
		Или ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ПрибытиеИностранца Тогда
		ЗаполнитьТитульный_УбытиеИностранца();
	ИначеЕсли ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ВозвратЭкосбор
		Или ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ЗачетЭкосбор
		Или ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.СверкаЭкосбор Тогда
		ЗаполнитьТитульный_Экосбор();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ПолучитьИмяЭкраннойФормы()
	Если ЗначениеЗаполнено(ИмяЭкраннойФормы) Тогда 
		Возврат ИмяЭкраннойФормы;
	КонецЕсли;
	
	Если ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ОбработкаПерсональныхДанных Тогда 
		Возврат ?(Год(Объект.Дата) >= 2023 Или Не ЗначениеЗаполнено(Объект.Ссылка), "Форма_ОбработкаПерсональныхДанных_2023", "Форма_ОбработкаПерсональныхДанных");
	Иначе 
		Возврат СоответствиеИменЭкраннойФормы()[ВидУведомления];
	КонецЕсли;
КонецФункции

&НаСервере
Функция СоответствиеИменЭкраннойФормы()
	Результат = Новый Соответствие;
	Результат.Вставить(Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ОсвобождениеОтУплатыНДС, "Форма_ОсвобождениеОтУплатыНДС");
	Результат.Вставить(Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ВозвратСуммыНВОС, "Форма_ВозвратСуммыНВОС");
	Результат.Вставить(Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ЗачетСуммыНВОС, "Форма_ЗачетСуммыНВОС");
	Результат.Вставить(Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ЗаявлениеНаПриемДокументовФСС, "Форма_ЗаявлениеНаПриемДокументовФСС");
	Результат.Вставить(Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.Уведомление1НачалоУплаты, "Форма_Уведомление1НачалоУплаты");
	Результат.Вставить(Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.Уведомление2ОтказОтУплаты, "Форма_Уведомление2ОтказОтУплаты");
	Результат.Вставить(Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ОсвобождениеОтНДСПриЕСХН, "Форма_ОсвобождениеОтНДСПриЕСХН");
	Результат.Вставить(Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ПрибытиеГражданинаРФ, "Форма_ПрибытиеГражданинаРФ");
	Результат.Вставить(Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.УбытиеИностранца, "Форма_УбытиеИностранца");
	Результат.Вставить(Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ПрибытиеИностранца, "Форма_ПрибытиеИностранца");
	Результат.Вставить(Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ЗачетСФР, "Форма_ЗачетСФР");
	Результат.Вставить(Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ВозвратСФР, "Форма_ВозвратСФР");
	Результат.Вставить(Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ИзменениеСведенийОбработкаПерсональныхДанных, "Форма_ИзменениеСведенийОбработкаПерсональныхДанных");
	Результат.Вставить(Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ПрекращениеОбработкиПерсональныхДанных, "Форма_ПрекращениеОбработкиПерсональныхДанных");
	Результат.Вставить(Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ВозмещениеПроизводственныхРасходов, "Форма_ВозмещениеПроизводственныхРасходов");
	Результат.Вставить(Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ЗачетЭкосбор, "Форма_ЗачетЭкосбор");
	Результат.Вставить(Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ВозвратЭкосбор, "Форма_ВозвратЭкосбор");
	Результат.Вставить(Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.СверкаЭкосбор, "Форма_СверкаЭкосбор");
	Результат.Вставить(Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ОбработкаПерсональныхДанных, "Форма_ОбработкаПерсональныхДанных_2023");
	Возврат Результат;
КонецФункции

&НаСервере
Процедура СформироватьМакетНаСервере()
	ДанныеСтраницы = Новый Структура;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда 
		СтруктураПараметров = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Ссылка, "ДанныеУведомления").Получить();
		СтруктураПараметров.Свойство("ИмяЭкраннойФормы", ИмяЭкраннойФормы);
	КонецЕсли;
	Макет = УведомлениеОСпецрежимахНалогообложения.ПолучитьМакетТабличногоДокумента(ЭтотОбъект, ПолучитьИмяЭкраннойФормы());
	Область = Макет.ПолучитьОбласть("Титульный");
	ПредставлениеУведомления.Очистить();
	ПредставлениеУведомления.Вывести(Область);
	ПредставлениеУведомления.ВыделенныеОбласти.Очистить();
	ТекущийЭлемент = Элементы.ПредставлениеУведомления;
	Элементы.ПредставлениеУведомления.ТекущаяОбласть = ПредставлениеУведомления.Область(1,1,1,1);
	
	Для Каждого Обл Из ПредставлениеУведомления.Области Цикл 
		Если Обл.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник
			И Обл.СодержитЗначение = Истина Тогда 
			
			ДанныеСтраницы.Вставить(Обл.Имя);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеУведомленияПриИзмененииСодержимогоОбласти_Освобождение(Область)
	Если ВРег(Область.Имя) = "ДАТАОТ" Тогда 
		Объект.ДатаПодписи = Область.Значение;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеУведомленияПриИзмененииСодержимогоОбласти_Уведомление1НачалоУплаты(Область)
	Если ВРег(Область.Имя) = "ДАТАОТ" Тогда 
		Объект.ДатаПодписи = Область.Значение;
	КонецЕсли;
	
	Если Область.СодержитЗначение = Истина
		И ТипЗнч(Область.Значение) = Тип("Число")
		И СтрНачинаетсяС(ВРег(Область.Имя), "СУММА") Тогда 
		
		Области = ПредставлениеУведомления.Области;
		Области.СуммаИтогоМес1.Значение = Области.Сумма1Мес1.Значение + Области.Сумма2Мес1.Значение + Области.Сумма3Мес1.Значение;
		Области.СуммаИтогоМес2.Значение = Области.Сумма1Мес2.Значение + Области.Сумма2Мес2.Значение + Области.Сумма3Мес2.Значение;
		Области.СуммаИтогоМес3.Значение = Области.Сумма1Мес3.Значение + Области.Сумма2Мес3.Значение + Области.Сумма3Мес3.Значение;
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеУведомленияВыбор_ЗачетСФР(Элемент, Область, СтандартнаяОбработка)
	Если СтрНачинаетсяС(Область.Имя, "ПодчОбл") Тогда 
		СмежнаяОбласть = ПредставлениеУведомления.Области.Найти(СтрЗаменить(Область.Имя, "ПодчОбл", "ПодчФОбл"));
		СмежнаяОбласть.Значение = Не СмежнаяОбласть.Значение;
		ДанныеСтраницы[СмежнаяОбласть.Имя] = СмежнаяОбласть.Значение;
		Модифицированность = Истина;
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеУведомленияВыбор_ПерсональныеДанные(Элемент, Область, СтандартнаяОбработка)
	Если Область.Имя = "ГиперссылкаНаСайт" Тогда 
		СтандартнаяОбработка = Ложь;
		ПерейтиПоНавигационнойСсылке("https://pd.rkn.gov.ru/operators-registry/notification/form/");
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриЗакрытииНаСервере()
	СохранитьДанные();
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНаКлиенте(Автосохранение = Ложь,ВыполняемоеОповещение = Неопределено) Экспорт 
	
	СохранитьДанные();
	Если ВыполняемоеОповещение <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ВыполняемоеОповещение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОткрытьПрисоединенныеФайлы(Команда)
	
	РегламентированнаяОтчетностьКлиент.СохранитьУведомлениеИОткрытьФормуПрисоединенныеФайлы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПечатьБРОНаСервере()
	УведомлениеОСпецрежимахНалогообложения.ПечатьУведомленияБРО(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти
