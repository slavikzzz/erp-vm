#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВариантыОтчетов

// См. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.
//
Процедура НастроитьВариантыОтчета(Настройки, НастройкиОтчета) Экспорт
	
	НастройкиОтчета.ОпределитьНастройкиФормы = Истина;
	
	НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "Т6");
	НастройкиВарианта.Описание = НСтр("ru = 'Унифицированная форма № Т-6';
										|en = 'Unified form No. T-6'");
	НастройкиВарианта.Включен = Ложь;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВариантыОтчетов

#КонецОбласти

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ДополнительныеПараметры = Неопределено;
	Если ПараметрыПечати.Свойство("ИдентификаторыСтрокСотрудников") Тогда
		СписокОтборов = Новый Массив;
		ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(СписокОтборов, "СсылкаНаОбъект.ДанныеОтпусков.ИдентификаторСтрокиСотрудника", 
			ВидСравненияКомпоновкиДанных.ВСписке, ПараметрыПечати.ИдентификаторыСтрокСотрудников);
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Отбор", СписокОтборов);
	КонецЕсли;
	
	ЗарплатаКадрыОтчеты.ВывестиВКоллекциюПечатнуюФорму("Отчет.ПечатнаяФормаТ6",
		МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода, ВнешниеНаборыДанных(МассивОбъектов), ДополнительныеПараметры);
	
КонецПроцедуры

Процедура Сформировать(ДокументРезультат, РезультатКомпоновки, ОбъектыПечати = Неопределено, ПараметрыПечати = Неопределено) Экспорт
	
	Если РезультатКомпоновки.ОтчетПустой Тогда
		Возврат;
	КонецЕсли;
	
	КадровыйУчет.ВывестиНаПечатьТ6(
		ПараметрыПечати,
		ДокументРезультат,
		РезультатКомпоновки.ДанныеОтчета.Строки,
		РезультатКомпоновки.МакетПечатнойФормы,
		РезультатКомпоновки.ИдентификаторыМакета,
		ОбъектыПечати);
	
КонецПроцедуры

Функция ВнешниеНаборыДанных(МассивОбъектов = Неопределено) Экспорт
	
	ВнешниеНаборы = Новый Структура;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	
	Если МассивОбъектов = Неопределено Тогда
		Запрос.УстановитьПараметр("МассивОбъектов", Новый Массив);
	Иначе
		Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	КонецЕсли;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ОсновнойОтпуск.СсылкаНаОбъект КАК СсылкаНаОбъект,
		|	ОсновнойОтпуск.Сотрудник КАК Сотрудник,
		|	ЗНАЧЕНИЕ(Справочник.ВидыОтпусков.Основной) КАК ВидОтпуска,
		|	ОсновнойОтпуск.ДатаНачала КАК ДатаНачала,
		|	ОсновнойОтпуск.ДатаОкончания КАК ДатаОкончания,
		|	СУММА(ОсновнойОтпуск.ОплаченоДней) КАК ОплаченоДней
		|ИЗ
		|	(ВЫБРАТЬ
		|		Отпуск.Ссылка КАК СсылкаНаОбъект,
		|		Отпуск.Сотрудник КАК Сотрудник,
		|		Отпуск.ДатаНачалаОсновногоОтпуска КАК ДатаНачала,
		|		Отпуск.ДатаОкончанияОсновногоОтпуска КАК ДатаОкончания,
		|		СУММА(ОтпускНачисления.ОплаченоДней) КАК ОплаченоДней
		|	ИЗ
		|		Документ.Отпуск КАК Отпуск
		|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.Отпуск.Начисления КАК ОтпускНачисления
		|			ПО Отпуск.Ссылка = ОтпускНачисления.Ссылка
		|				И Отпуск.ВидРасчетаОсновногоОтпуска = ОтпускНачисления.Начисление
		|				И Отпуск.ДатаНачалаОсновногоОтпуска <= ОтпускНачисления.ДатаНачала
		|				И Отпуск.ДатаОкончанияОсновногоОтпуска >= ОтпускНачисления.ДатаОкончания
		|	ГДЕ
		|		Отпуск.Ссылка В(&МассивОбъектов)
		|		И Отпуск.ПредоставитьОсновнойОтпуск
		|		И ЕСТЬNULL(ОтпускНачисления.ОплаченоДней, 0) > 0
		|	
		|	СГРУППИРОВАТЬ ПО
		|		Отпуск.Ссылка,
		|		Отпуск.Сотрудник,
		|		Отпуск.ДатаНачалаОсновногоОтпуска,
		|		Отпуск.ДатаОкончанияОсновногоОтпуска
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Отпуск.Ссылка,
		|		Отпуск.Сотрудник,
		|		Отпуск.ДатаНачалаОсновногоОтпуска,
		|		Отпуск.ДатаОкончанияОсновногоОтпуска,
		|		СУММА(ОтпускНачисления.ОплаченоДней)
		|	ИЗ
		|		Документ.Отпуск КАК Отпуск
		|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.Отпуск.Начисления КАК ОтпускНачисления
		|			ПО Отпуск.Ссылка = ОтпускНачисления.Ссылка
		|				И Отпуск.ВидРасчетаКомпенсацииОсновногоОтпуска = ОтпускНачисления.Начисление
		|				И Отпуск.ПериодРегистрации = ОтпускНачисления.ДатаНачала
		|	ГДЕ
		|		Отпуск.Ссылка В(&МассивОбъектов)
		|		И Отпуск.ПредоставитьКомпенсациюОсновногоОтпуска
		|		И ЕСТЬNULL(ОтпускНачисления.ОплаченоДней, 0) > 0
		|	
		|	СГРУППИРОВАТЬ ПО
		|		Отпуск.Ссылка,
		|		Отпуск.Сотрудник,
		|		Отпуск.ДатаНачалаОсновногоОтпуска,
		|		Отпуск.ДатаОкончанияОсновногоОтпуска) КАК ОсновнойОтпуск
		|
		|СГРУППИРОВАТЬ ПО
		|	ОсновнойОтпуск.СсылкаНаОбъект,
		|	ОсновнойОтпуск.Сотрудник,
		|	ОсновнойОтпуск.ДатаНачала,
		|	ОсновнойОтпуск.ДатаОкончания
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ОтпускДополнительныеОтпуска.Ссылка,
		|	ОтпускДополнительныеОтпуска.Ссылка.Сотрудник,
		|	ОтпускДополнительныеОтпуска.ВидОтпуска,
		|	ОтпускДополнительныеОтпуска.ДатаНачала,
		|	ОтпускДополнительныеОтпуска.ДатаОкончания,
		|	СУММА(ОтпускНачисления.ОплаченоДней)
		|ИЗ
		|	Документ.Отпуск.ДополнительныеОтпуска КАК ОтпускДополнительныеОтпуска
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Отпуск.Начисления КАК ОтпускНачисления
		|		ПО ОтпускДополнительныеОтпуска.Ссылка = ОтпускНачисления.Ссылка
		|			И ОтпускДополнительныеОтпуска.ВидРасчета = ОтпускНачисления.Начисление
		|			И ОтпускДополнительныеОтпуска.ДатаНачала <= ОтпускНачисления.ДатаНачала
		|			И ОтпускДополнительныеОтпуска.ДатаОкончания >= ОтпускНачисления.ДатаОкончания
		|ГДЕ
		|	ОтпускДополнительныеОтпуска.Ссылка В(&МассивОбъектов)
		|	И ОтпускДополнительныеОтпуска.Ссылка.ПредоставитьДополнительныйОтпуск
		|	И ОтпускДополнительныеОтпуска.КоличествоДней > 0
		|	И ЕСТЬNULL(ОтпускНачисления.ОплаченоДней, 0) > 0
		|
		|СГРУППИРОВАТЬ ПО
		|	ОтпускДополнительныеОтпуска.Ссылка,
		|	ОтпускДополнительныеОтпуска.Ссылка.Сотрудник,
		|	ОтпускДополнительныеОтпуска.ВидОтпуска,
		|	ОтпускДополнительныеОтпуска.ДатаНачала,
		|	ОтпускДополнительныеОтпуска.ДатаОкончания
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ОтпускДополнительныеОтпуска.Ссылка,
		|	ОтпускДополнительныеОтпуска.Ссылка.Сотрудник,
		|	ОтпускДополнительныеОтпуска.ВидОтпуска,
		|	ОтпускДополнительныеОтпуска.ДатаНачала,
		|	ОтпускДополнительныеОтпуска.ДатаОкончания,
		|	СУММА(ОтпускНачисления.ОплаченоДней)
		|ИЗ
		|	Документ.Отпуск.ДополнительныеОтпуска КАК ОтпускДополнительныеОтпуска
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Отпуск.Начисления КАК ОтпускНачисления
		|		ПО ОтпускДополнительныеОтпуска.Ссылка = ОтпускНачисления.Ссылка
		|			И ОтпускДополнительныеОтпуска.ВидРасчетаКомпенсации = ОтпускНачисления.Начисление
		|			И ОтпускДополнительныеОтпуска.Ссылка.ПериодРегистрации = ОтпускНачисления.ДатаНачала
		|ГДЕ
		|	ОтпускДополнительныеОтпуска.Ссылка В(&МассивОбъектов)
		|	И ОтпускДополнительныеОтпуска.Ссылка.ПредоставитьДополнительныйОтпуск
		|	И ОтпускДополнительныеОтпуска.КоличествоДнейКомпенсации > 0
		|	И ЕСТЬNULL(ОтпускНачисления.ОплаченоДней, 0) > 0
		|
		|СГРУППИРОВАТЬ ПО
		|	ОтпускДополнительныеОтпуска.Ссылка,
		|	ОтпускДополнительныеОтпуска.Ссылка.Сотрудник,
		|	ОтпускДополнительныеОтпуска.ВидОтпуска,
		|	ОтпускДополнительныеОтпуска.ДатаНачала,
		|	ОтпускДополнительныеОтпуска.ДатаОкончания";
	
	ВнешниеНаборы.Вставить("ОплаченныеДниОтпусков", Запрос.Выполнить().Выгрузить());
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ВнешниеНаборы;
	
КонецФункции

#КонецОбласти

#КонецЕсли