#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ВерсияФорматаВыгрузки(Знач НаДату = Неопределено, ВыбраннаяФорма = Неопределено) Экспорт
	
	Если НаДату = Неопределено Тогда
		НаДату = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Если НаДату >= '20080101' Тогда
		Возврат Перечисления.ВерсииФорматовВыгрузки.Версия500;
	ИначеЕсли НаДату > '20050101' Тогда
		Возврат Перечисления.ВерсииФорматовВыгрузки.Версия300;
	КонецЕсли;
	
КонецФункции

Функция ТаблицаФормОтчета() Экспорт
	
	ОписаниеТиповСтрока = Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(0));
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("Дата"));
	ОписаниеТиповДата = Новый ОписаниеТипов(МассивТипов, , Новый КвалификаторыДаты(ЧастиДаты.Дата));
	
	ТаблицаФормОтчета = Новый ТаблицаЗначений;
	ТаблицаФормОтчета.Колонки.Добавить("ФормаОтчета",        ОписаниеТиповСтрока);
	ТаблицаФормОтчета.Колонки.Добавить("ОписаниеОтчета",     ОписаниеТиповСтрока, "Утверждена",  20);
	ТаблицаФормОтчета.Колонки.Добавить("ДатаНачалоДействия", ОписаниеТиповДата,   "Действует с", 5);
	ТаблицаФормОтчета.Колонки.Добавить("ДатаКонецДействия",  ОписаниеТиповДата,   "         по", 5);
	ТаблицаФормОтчета.Колонки.Добавить("РедакцияФормы",      ОписаниеТиповСтрока, "Редакция формы", 20);
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета        = "ФормаОтчета2008Кв1";
	НоваяФорма.ОписаниеОтчета     = "Приложение № 1 Утверждено Приказом Минфина РФ от 20.02.2008 № 27н.";
	НоваяФорма.РедакцияФормы      = "от 20.02.2008 г. № 27н.";
	НоваяФорма.ДатаНачалоДействия = '2008-01-01';
	НоваяФорма.ДатаКонецДействия  = '2010-12-31';
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета        = "ФормаОтчета2011Кв3";
	НоваяФорма.ОписаниеОтчета     = "Приложение № 1 к приказу ФНС России от 24.11.2011 № ММВ-7-11/895.";
	НоваяФорма.РедакцияФормы      = "от 24.11.2011 № ММВ-7-11/895.";
	НоваяФорма.ДатаНачалоДействия = '2011-01-01';
	НоваяФорма.ДатаКонецДействия  = '2012-12-31';
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета        = "ФормаОтчета2013Кв4";
	НоваяФорма.ОписаниеОтчета     = "Приложение № 1 к приказу ФНС России от 05.11.2013 № ММВ-7-11/478@.";
	НоваяФорма.РедакцияФормы      = "от 05.11.2013 № ММВ-7-11/478@.";
	НоваяФорма.ДатаНачалоДействия = '2013-01-01';
	НоваяФорма.ДатаКонецДействия  = '2016-12-31';
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета        = "ФормаОтчета2017Кв4";
	НоваяФорма.ОписаниеОтчета     = "Приложение № 1 к приказу ФНС России от 31.03.2017 № ММВ-7-21/271@.";
	НоваяФорма.РедакцияФормы      = "от 31.03.2017 № ММВ-7-21/271@.";
	НоваяФорма.ДатаНачалоДействия = '2017-01-01';
	НоваяФорма.ДатаКонецДействия  = '2018-12-31';
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета        = "ФормаОтчета2019Кв4";
	НоваяФорма.ОписаниеОтчета     = "Приложение № 1 к приказу ФНС России от 14.08.2019 № СА-7-21/405@.";
	НоваяФорма.РедакцияФормы      = "от 14.08.2019 № СА-7-21/405@.";
	НоваяФорма.ДатаНачалоДействия = '2019-01-01';
	НоваяФорма.ДатаКонецДействия  = '2019-12-31';
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета        = "ФормаОтчета2019Кв1";
	НоваяФорма.ОписаниеОтчета
	= "Приложение № 1 к приказу ФНС России от 31.03.2017 № ММВ-7-21/271@ (в ред. приказа ФНС России от 04.10.2018 № ММВ-7-21/575@).";
	НоваяФорма.РедакцияФормы      = "от 04.10.2018 № ММВ-7-21/575@.";
	НоваяФорма.ДатаНачалоДействия = '2019-01-01';
	НоваяФорма.ДатаКонецДействия  = '2019-12-31';
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета        = "ФормаОтчета2022Кв4";
	НоваяФорма.ОписаниеОтчета
	= "Утверждена приказом ФНС России от 24.08.2022 № ЕД-7-21/766@ (в ред. приказа ФНС России от 16.08.2023 № ЕД-7-21/544@).";
	НоваяФорма.РедакцияФормы      = "от 16.08.2023 № ЕД-7-21/544@.";
	НоваяФорма.ДатаНачалоДействия = '2022-01-01';
	НоваяФорма.ДатаКонецДействия  = '2023-12-31';
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета = "ФормаОтчета2022Кв1";// "ФормаОтчета2022Кв1" не применялась и замещена формой "ФормаОтчета2022Кв4"
	НоваяФорма.ОписаниеОтчета
	= "Приложение № 1 к приказу ФНС России от 14.08.2019 № СА-7-21/405@ (в ред. приказа ФНС России от 28.01.2022 № ЕД-7-21/53@).";
	НоваяФорма.РедакцияФормы = "от 28.01.2022 № ЕД-7-21/53@.";
	НоваяФорма.ДатаНачалоДействия = '2023-01-01';
	НоваяФорма.ДатаКонецДействия  = '2023-01-01';
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета        = "ФормаОтчета2021Кв4";
	НоваяФорма.ОписаниеОтчета
	= "Приложение № 1 к приказу ФНС России от 14.08.2019 № СА-7-21/405@ (в ред. приказа ФНС России от 18.06.2021 № ЕД-7-21/574@).";
	НоваяФорма.РедакцияФормы      = "от 18.06.2021 № ЕД-7-21/574@.";
	НоваяФорма.ДатаНачалоДействия = '2021-01-01';
	НоваяФорма.ДатаКонецДействия  = '2022-12-31';
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета        = "ФормаОтчета2020Кв4_1";
	НоваяФорма.ОписаниеОтчета
	= "Приложение № 1 к приказу ФНС России от 14.08.2019 № СА-7-21/405@ (в ред. приказа ФНС России от 09.12.2020 № КЧ-7-21/889@).";
	НоваяФорма.РедакцияФормы      = "от 09.12.2020 № КЧ-7-21/889@.";
	НоваяФорма.ДатаНачалоДействия = '2020-01-01';
	НоваяФорма.ДатаКонецДействия  = '2021-12-31';
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета        = "ФормаОтчета2020Кв4";
	НоваяФорма.ОписаниеОтчета
	= "Приложение № 1 к приказу ФНС России от 14.08.2019 № СА-7-21/405@ (в ред. приказа ФНС России от 28.07.2020 № ЕД-7-21/475@).";
	НоваяФорма.РедакцияФормы      = "от 28.07.2020 № ЕД-7-21/475@.";
	НоваяФорма.ДатаНачалоДействия = '2020-01-01';
	НоваяФорма.ДатаКонецДействия  = '2021-12-31';
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета        = "ФормаОтчета2024Кв4";
	НоваяФорма.ОписаниеОтчета
	= "Утверждена приказом ФНС России от 24.08.2022 № ЕД-7-21/766@ (в ред. приказа ФНС России от 27.09.2024 № БВ-7-21/805@).";
	НоваяФорма.РедакцияФормы      = "от 27.09.2024 № БВ-7-21/805@.";
	НоваяФорма.ДатаНачалоДействия = '2024-01-01';
	НоваяФорма.ДатаКонецДействия  = РегламентированнаяОтчетностьКлиентСервер.ПустоеЗначениеТипа(Тип("Дата"));
	
	Возврат ТаблицаФормОтчета;
	
КонецФункции

Функция ДанныеРеглОтчета(ЭкземплярРеглОтчета) Экспорт
	
	ТаблицаДанныхРеглОтчета = ИнтерфейсыВзаимодействияБРО.НовыйТаблицаДанныхРеглОтчета();
	
	Если ЭкземплярРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2024Кв4"
		ИЛИ ЭкземплярРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2022Кв4"
		ИЛИ ЭкземплярРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2022Кв1"
		ИЛИ ЭкземплярРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2021Кв4"
		ИЛИ ЭкземплярРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2020Кв4_1"
		ИЛИ ЭкземплярРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2020Кв4"
		ИЛИ ЭкземплярРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2019Кв4"
		ИЛИ ЭкземплярРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2019Кв1"
		ИЛИ ЭкземплярРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2017Кв4" Тогда
		
		ДанныеРеглОтчета = ЭкземплярРеглОтчета.ДанныеОтчета.Получить();
		Если ТипЗнч(ДанныеРеглОтчета) <> Тип("Структура") Тогда
			Возврат ТаблицаДанныхРеглОтчета;
		КонецЕсли;
		
		Если ДанныеРеглОтчета.Свойство("ДанныеМногоуровневыхРазделов") Тогда
			
			ДанныеМногострочнойЧастиРаздела1
			= ДанныеРеглОтчета.ДанныеМногоуровневыхРазделов["Раздел1"].Строки[0].ДанныеМногострочныхЧастей["П0000100"];
			
			Для каждого СтрокаМнЧ Из ДанныеМногострочнойЧастиРаздела1.Строки Цикл
				Сумма = ТаблицаДанныхРеглОтчета.Добавить();
				Сумма.Период = ЭкземплярРеглОтчета.ДатаОкончания;
				Сумма.ОКАТО  = СтрокаМнЧ.Данные.П000010001003;
				Сумма.КБК    = СтрокаМнЧ.Данные.П000010002003;
				Сумма.Сумма  = СтрокаМнЧ.Данные.П000010003003;
			КонецЦикла;
			
		ИначеЕсли ДанныеРеглОтчета.Свойство("ДанныеМногострочныхРазделов") Тогда // отчет сохранен в 2.0
			
			ДанныеМногострочнойЧастиРаздела1 = ДанныеРеглОтчета.ДанныеМногострочныхРазделов.П00001;
			
			Для каждого СтрокаМнЧ Из ДанныеМногострочнойЧастиРаздела1 Цикл
				Сумма = ТаблицаДанныхРеглОтчета.Добавить();
				Сумма.Период = ЭкземплярРеглОтчета.ДатаОкончания;
				Сумма.ОКАТО  = СтрокаМнЧ.П000010001003;
				Сумма.КБК    = СтрокаМнЧ.П000010002003;
				Сумма.Сумма  = СтрокаМнЧ.П000010003003;
			КонецЦикла;
			
		КонецЕсли;
		
		ТаблицаДанныхРеглОтчета.Свернуть("ВидНалога,Период,ОКАТО,КБК,Аванс,Агент", "Сумма");
		
	ИначеЕсли ЭкземплярРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2013Кв4" Тогда
		
		ДанныеРеглОтчета = ЭкземплярРеглОтчета.ДанныеОтчета.Получить();
		Если ТипЗнч(ДанныеРеглОтчета) <> Тип("Структура") Тогда
			Возврат ТаблицаДанныхРеглОтчета;
		КонецЕсли;
		
		Если ДанныеРеглОтчета.ДанныеМногострочныхРазделов.Свойство("П00001000") Тогда
			
			Для Каждого СтрТаблицы Из ДанныеРеглОтчета.ДанныеМногострочныхРазделов.П00001000 Цикл
				Сумма = ТаблицаДанныхРеглОтчета.Добавить();
				Сумма.Период = ЭкземплярРеглОтчета.ДатаОкончания;
				Сумма.ОКАТО  = СтрТаблицы.П000010001003;
				Сумма.КБК    = СтрТаблицы.П000010002003;
				Сумма.Сумма  = СтрТаблицы.П000010003003;
			КонецЦикла;
			
		КонецЕсли;
		
	ИначеЕсли ЭкземплярРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2011Кв3" Тогда
		
		ДанныеРеглОтчета = ЭкземплярРеглОтчета.ДанныеОтчета.Получить();
		Если ТипЗнч(ДанныеРеглОтчета) <> Тип("Структура") Тогда
			Возврат ТаблицаДанныхРеглОтчета;
		КонецЕсли;
		
		Если ДанныеРеглОтчета.ДанныеМногостраничныхРазделов.Свойство("Раздел1") Тогда
			
			Раздел1 = ДанныеРеглОтчета.ДанныеМногостраничныхРазделов.Раздел1;
			
			Период = ЭкземплярРеглОтчета.ДатаОкончания;
			КодСтрокиОКАТО = "П000010001000_";
			КодСтрокиКБК   = "П000010002000_";
			КодСтрокиСумма = "П000010003000_";
			
			Для Каждого Страница Из Раздел1 Цикл
				
				НалогКУплате = Страница.Данные;
				
				Для НомерПоказателя = 1 По 6 Цикл
					Сумма = ТаблицаДанныхРеглОтчета.Добавить();
					Сумма.Период = Период;
					Сумма.ОКАТО  = НалогКУплате[КодСтрокиОКАТО + НомерПоказателя];
					Сумма.КБК    = НалогКУплате[КодСтрокиКБК + НомерПоказателя];
					Сумма.Сумма  = НалогКУплате[КодСтрокиСумма+ НомерПоказателя];
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ТаблицаДанныхРеглОтчета;
	
КонецФункции

Функция ДеревоФормИФорматов() Экспорт
	
	ФормыИФорматы = Новый ДеревоЗначений;
	ФормыИФорматы.Колонки.Добавить("Код");
	ФормыИФорматы.Колонки.Добавить("ДатаПриказа");
	ФормыИФорматы.Колонки.Добавить("НомерПриказа");
	ФормыИФорматы.Колонки.Добавить("ДатаНачалаДействия");
	ФормыИФорматы.Колонки.Добавить("ДатаОкончанияДействия");
	ФормыИФорматы.Колонки.Добавить("ИмяОбъекта");
	ФормыИФорматы.Колонки.Добавить("Описание");
	
	Форма20080101 = ОпределитьФормуВДеревеФормИФорматов(ФормыИФорматы,
	"1152026", '2008-02-20', "27н", "ФормаОтчета2008Кв1");
	ОпределитьФорматВДеревеФормИФорматов(Форма20080101, "5.01");
	
	Форма20110101 = ОпределитьФормуВДеревеФормИФорматов(ФормыИФорматы,
	"1152026", '2011-11-24', "ММВ-7-11/895", "ФормаОтчета2011Кв3");
	ОпределитьФорматВДеревеФормИФорматов(Форма20110101, "5.02");
	
	Форма20130101 = ОпределитьФормуВДеревеФормИФорматов(ФормыИФорматы,
	"1152026", '2013-11-05', "ММВ-7-11/478@", "ФормаОтчета2013Кв4");
	ОпределитьФорматВДеревеФормИФорматов(Форма20130101, "5.03");
	
	Форма20170101 = ОпределитьФормуВДеревеФормИФорматов(ФормыИФорматы,
	"1152026", '2017-03-31', "ММВ-7-21/271@", "ФормаОтчета2017Кв4");
	ОпределитьФорматВДеревеФормИФорматов(Форма20170101, "5.04");
	
	Форма20190101 = ОпределитьФормуВДеревеФормИФорматов(ФормыИФорматы,
	"1152026", '2018-10-04', "ММВ-7-21/575@", "ФормаОтчета2019Кв1");
	ОпределитьФорматВДеревеФормИФорматов(Форма20190101, "5.05");
	
	Форма2019Кв4 = ОпределитьФормуВДеревеФормИФорматов(ФормыИФорматы,
	"1152026", '2019-08-14', "СА-7-21/405@", "ФормаОтчета2019Кв4");
	ОпределитьФорматВДеревеФормИФорматов(Форма2019Кв4, "5.06");
	
	Форма2020Кв4 = ОпределитьФормуВДеревеФормИФорматов(ФормыИФорматы,
	"1152026", '2020-07-28', "ЕД-7-21/475@", "ФормаОтчета2020Кв4");
	ОпределитьФорматВДеревеФормИФорматов(Форма2020Кв4, "5.07");
	
	Форма2020Кв4_1 = ОпределитьФормуВДеревеФормИФорматов(ФормыИФорматы,
	"1152026", '2020-12-09', "КЧ-7-21/889@", "ФормаОтчета2020Кв4_1");
	ОпределитьФорматВДеревеФормИФорматов(Форма2020Кв4_1, "5.08");
	
	Форма2021Кв4 = ОпределитьФормуВДеревеФормИФорматов(ФормыИФорматы,
	"1152026", '2021-06-18', "ЕД-7-21/574@", "ФормаОтчета2021Кв4");
	ОпределитьФорматВДеревеФормИФорматов(Форма2021Кв4, "5.09");
	
	Форма2022Кв4 = ОпределитьФормуВДеревеФормИФорматов(ФормыИФорматы,
	"1152026", '2022-08-24', "ЕД-7-21/766@", "ФормаОтчета2022Кв4");
	ОпределитьФорматВДеревеФормИФорматов(Форма2022Кв4, "5.11");// версия 5.10 не применялась и замещена версией 5.11
	
	Форма2024Кв4 = ОпределитьФормуВДеревеФормИФорматов(ФормыИФорматы,
	"1152026", '2024-09-27', "БВ-7-21/805@", "ФормаОтчета2024Кв4");
	ОпределитьФорматВДеревеФормИФорматов(Форма2024Кв4, "5.12");
	
	Возврат ФормыИФорматы;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОпределитьФормуВДеревеФормИФорматов(ДеревоФормИФорматов, Код, ДатаПриказа = '00010101', НомерПриказа = "",
			ИмяОбъекта = "", ДатаНачалаДействия = '00010101', ДатаОкончанияДействия = '00010101', Описание = "")
	
	НовСтр = ДеревоФормИФорматов.Строки.Добавить();
	НовСтр.Код = СокрЛП(Код);
	НовСтр.ДатаПриказа = ДатаПриказа;
	НовСтр.НомерПриказа = СокрЛП(НомерПриказа);
	НовСтр.ДатаНачалаДействия = ДатаНачалаДействия;
	НовСтр.ДатаОкончанияДействия = ДатаОкончанияДействия;
	НовСтр.ИмяОбъекта = СокрЛП(ИмяОбъекта);
	НовСтр.Описание = СокрЛП(Описание);
	Возврат НовСтр;
	
КонецФункции

Функция ОпределитьФорматВДеревеФормИФорматов(Форма, Версия, ДатаПриказа = '00010101', НомерПриказа = "",
			ДатаНачалаДействия = Неопределено, ДатаОкончанияДействия = Неопределено, ИмяОбъекта = "", Описание = "")
	
	НовСтр = Форма.Строки.Добавить();
	НовСтр.Код = СокрЛП(Версия);
	НовСтр.ДатаПриказа = ДатаПриказа;
	НовСтр.НомерПриказа = СокрЛП(НомерПриказа);
	НовСтр.ДатаНачалаДействия = ?(ДатаНачалаДействия = Неопределено, Форма.ДатаНачалаДействия, ДатаНачалаДействия);
	НовСтр.ДатаОкончанияДействия = ?(ДатаОкончанияДействия = Неопределено, Форма.ДатаОкончанияДействия, ДатаОкончанияДействия);
	НовСтр.ИмяОбъекта = СокрЛП(ИмяОбъекта);
	НовСтр.Описание = СокрЛП(Описание);
	Возврат НовСтр;
	
КонецФункции

#КонецОбласти

#Область ПоказателиОценкиРискаВыезднойНалоговойПроверки

Процедура ПоказателиОценкиРискаВыезднойНалоговойПроверки(ТаблицаПоказателей, ОписаниеОтчета) Экспорт 
	
	СведенияОтчета = ОписаниеОтчета.РегламентированныйОтчет.ДанныеОтчета.Получить();
	
	ЗначениеПоказателя = 0;
	
	Если ОписаниеОтчета.ВыбраннаяФорма = "ФормаОтчета2024Кв4"
		ИЛИ ОписаниеОтчета.ВыбраннаяФорма = "ФормаОтчета2022Кв4"
		ИЛИ ОписаниеОтчета.ВыбраннаяФорма = "ФормаОтчета2022Кв1"
		ИЛИ ОписаниеОтчета.ВыбраннаяФорма = "ФормаОтчета2021Кв4"
		ИЛИ ОписаниеОтчета.ВыбраннаяФорма = "ФормаОтчета2020Кв4_1"
		ИЛИ ОписаниеОтчета.ВыбраннаяФорма = "ФормаОтчета2020Кв4"
		ИЛИ ОписаниеОтчета.ВыбраннаяФорма = "ФормаОтчета2019Кв4" Тогда
		
		Если СведенияОтчета.Свойство("ОкружениеСохранения") Тогда // отчет сохранен в 2.0
		Иначе
			ДанныеМногострочнойЧастиРаздела1
			= СведенияОтчета.ДанныеМногоуровневыхРазделов["Раздел1"].Строки[0].ДанныеМногострочныхЧастей["П0000100"];
			Для Каждого СтрокаМнЧ Из ДанныеМногострочнойЧастиРаздела1.Строки Цикл
				ЗначениеПоказателя = ЗначениеПоказателя + РегламентированнаяОтчетность.ПоказательОтчета(СтрокаМнЧ.Данные, "П000010002103");
			КонецЦикла;
		КонецЕсли;
		
	ИначеЕсли ОписаниеОтчета.ВыбраннаяФорма = "ФормаОтчета2019Кв1"
		ИЛИ ОписаниеОтчета.ВыбраннаяФорма = "ФормаОтчета2017Кв4" Тогда
		
		Если СведенияОтчета.Свойство("ОкружениеСохранения") Тогда // отчет сохранен в 2.0
			
			Для Каждого Лист Из СведенияОтчета.ДанныеМногостраничныхРазделов.Раздел2 Цикл
				ДанныеЛиста = Лист.Данные;
				ЗначениеПоказателя = ЗначениеПоказателя
				                   + РегламентированнаяОтчетность.ПоказательОтчета(ДанныеЛиста, "П000020022003")
				                   - РегламентированнаяОтчетность.ПоказательОтчета(ДанныеЛиста, "П000020025003");
			КонецЦикла;
			
			Для Каждого Лист Из СведенияОтчета.ДанныеМногостраничныхРазделов.Раздел3 Цикл
				ДанныеЛиста = Лист.Данные;
				ЗначениеПоказателя = ЗначениеПоказателя
				                   + РегламентированнаяОтчетность.ПоказательОтчета(ДанныеЛиста, "П000030010003")
				                   - РегламентированнаяОтчетность.ПоказательОтчета(ДанныеЛиста, "П000030013003");
			КонецЦикла;
			
		Иначе
			
			ДеревоРаздела = СведенияОтчета.ДанныеМногоуровневыхРазделов["Раздел2"];
			Для Каждого Лист Из ДеревоРаздела.Строки Цикл
				ДанныеЛиста = Лист.Данные;
				ЗначениеПоказателя = ЗначениеПоказателя
				                   + РегламентированнаяОтчетность.ПоказательОтчета(ДанныеЛиста, "П000020022003")
				                   - РегламентированнаяОтчетность.ПоказательОтчета(ДанныеЛиста, "П000020025003");
			КонецЦикла;
			
			ДеревоРаздела = СведенияОтчета.ДанныеМногоуровневыхРазделов["Раздел3"];
			Для Каждого Лист Из ДеревоРаздела.Строки Цикл
				ДанныеЛиста = Лист.Данные;
				ЗначениеПоказателя = ЗначениеПоказателя
				                   + РегламентированнаяОтчетность.ПоказательОтчета(ДанныеЛиста, "П000030010003")
				                   - РегламентированнаяОтчетность.ПоказательОтчета(ДанныеЛиста, "П000030013003");
			КонецЦикла;
			
		КонецЕсли;
		
	Иначе
		
		Для Каждого Лист Из СведенияОтчета.ДанныеМногостраничныхРазделов.Раздел2 Цикл
			ДанныеЛиста = Лист.Данные;
			ЗначениеПоказателя = ЗначениеПоказателя
			                   + РегламентированнаяОтчетность.ПоказательОтчета(ДанныеЛиста, "П000020022003")
			                   - РегламентированнаяОтчетность.ПоказательОтчета(ДанныеЛиста, "П000020025003");
		КонецЦикла;
		
		Для Каждого Лист Из СведенияОтчета.ДанныеМногостраничныхРазделов.Раздел3 Цикл
			ДанныеЛиста = Лист.Данные;
			ЗначениеПоказателя = ЗначениеПоказателя
			                   + РегламентированнаяОтчетность.ПоказательОтчета(ДанныеЛиста, "П000030010003")
			                   - РегламентированнаяОтчетность.ПоказательОтчета(ДанныеЛиста, "П000030013003");
		КонецЦикла;
		
	КонецЕсли;
	
	НоваяСтрока = ТаблицаПоказателей.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока, ОписаниеОтчета);
	НоваяСтрока.Показатель = "СуммаНалогаНаИмущество";
	НоваяСтрока.ЗначениеПоказателя = ЗначениеПоказателя;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли