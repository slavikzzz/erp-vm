#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
Функция ПолучитьТаблицуПримененияФорматов() Экспорт 
	Результат = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюТаблицуПримененияФорматов();
	
	Стр = Результат.Добавить();
	Стр.ИмяФормы = "Форма2022_1";
	Стр.КНД = "1111654";
	Стр.ВерсияФормата = "5.01";
	
	Возврат Результат;
КонецФункции

Функция ДанноеУведомлениеДоступноДляОрганизации() Экспорт 
	Возврат Истина;
КонецФункции

Функция ДанноеУведомлениеДоступноДляИП() Экспорт 
	Возврат Ложь;
КонецФункции

Функция ПолучитьОсновнуюФорму() Экспорт 
	Возврат "";
КонецФункции

Функция ПолучитьФормуПоУмолчанию() Экспорт 
	Возврат "";
КонецФункции

Функция ПолучитьТаблицуФорм() Экспорт 
	Результат = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюТаблицуФормУведомления();
	
	Стр = Результат.Добавить();
	Стр.ИмяФормы = "Форма2022_1";
	Стр.ОписаниеФормы = "Форма по приказу ФНС России 03.08.2022 № ЕД-7-13/715@";
	Стр.ДатаНачала = '20221001';
	Стр.ДатаКонца = '20991231';
	
	Возврат Результат;
КонецФункции

Функция СформироватьВыгрузкуИПолучитьДанные(ДокОбъект) Экспорт 
	Выгрузка = ДокОбъект.ВыгрузитьДокумент();
	Если Выгрузка = Неопределено Тогда 
		Возврат Неопределено;
	КонецЕсли;
	СтруктураВыгрузки = Новый Структура("ТестВыгрузки,КодировкаВыгрузки", Выгрузка[0].ТестВыгрузки, Выгрузка[0].КодировкаВыгрузки);
	
	Если ДокОбъект.ИмяФормы = "Форма2022_1" Тогда 
		СтруктураВыгрузки.Вставить("Данные", УведомлениеОСпецрежимахНалогообложения.ПолучитьМакетДвоичныхДанных(ДокОбъект.ИмяОтчета, "TIFF_2022_1"));
		СтруктураВыгрузки.Вставить("ИмяФайла", "1111654_5.01000_01.tif");
		Возврат СтруктураВыгрузки;
	КонецЕсли;
КонецФункции

Функция ЭлектронноеПредставление(Объект, ИмяФормы, УникальныйИдентификатор) Экспорт
	Если ИмяФормы = "Форма2022_1" Тогда
		Возврат ЭлектронноеПредставление_Форма2022_1(Объект, УникальныйИдентификатор);
	КонецЕсли;
	Возврат Неопределено;
КонецФункции

Функция ПроверитьДокумент(Объект, ИмяФормы, УникальныйИдентификатор) Экспорт
	Если ИмяФормы = "Форма2022_1" Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Электронный формат для данной формы не опубликован';
													|en = 'Электронный формат для данной формы не опубликован'"));
		Возврат Неопределено;
	КонецЕсли;
КонецФункции

Функция СформироватьСписокЛистов(Объект) Экспорт
	Если Объект.ИмяФормы = "Форма2022_1" Тогда 
		Возврат СформироватьСписокЛистовФорма2022_1(Объект);
	КонецЕсли;
КонецФункции

Функция ПроверитьДокументСВыводомВТаблицу(Объект, ИмяФормы, УникальныйИдентификатор) Экспорт 
	Если ИмяФормы = "Форма2022_1" Тогда 
		Возврат ПроверитьДокументСВыводомВТаблицу_Форма2022_1(
			УведомлениеОСпецрежимахНалогообложения.ДанныеУведомленияДляВыгрузки(Объект), УникальныйИдентификатор);
	КонецЕсли;
КонецФункции

Функция ИдентификаторФайлаЭлектронногоПредставления_Форма2022_1(СведенияОтправки)
	Префикс = "UT_SOBLUSLMHK";
	Возврат Документы.УведомлениеОСпецрежимахНалогообложения.ИдентификаторФайлаЭлектронногоПредставления(Префикс, СведенияОтправки);
КонецФункции

Функция ПроверитьДокументСВыводомВТаблицу_Форма2022_1(Данные, УникальныйИдентификатор)
	ТаблицаОшибок = Новый СписокЗначений;
	УведомлениеОСпецрежимахНалогообложения.ПроверкаАктуальностиФормыПриВыгрузке(
		Данные.Объект.ИмяФормы, ТаблицаОшибок, ПолучитьТаблицуФорм());
	
	Титульная = Данные.ДанныеУведомления.Титульная;
	УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаКодаНО(Титульная.КодНО, ТаблицаОшибок, "Титульная");
	УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаГода(Титульная, ТаблицаОшибок, "Титульная", "ОтчетГод", Истина);
	
	Если Не ЗначениеЗаполнено(Титульная.НаимОрг) Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
			"Не указано наименование организации", "Титульная", "НаимОрг"));
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Титульная.ДАТА_ПОДПИСИ) Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
			"Не указана дата формирования документа", "Титульная", "ДАТА_ПОДПИСИ"));
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Титульная.Период) Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
			"Не указан период", "Титульная", "Период"));
	КонецЕсли;
	
	УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаИННКПП(Истина, Титульная, ТаблицаОшибок);
	Если Не ЗначениеЗаполнено(Титульная.ПРИЗНАК_НП_ПОДВАЛ) Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
			"Не указан признак подписанта", "Титульная", "ПРИЗНАК_НП_ПОДВАЛ"));
	КонецЕсли;
	УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаПодписантаНалоговойОтчетности(Данные, ТаблицаОшибок, "Титульная", Истина);
	
	Если Титульная.ПРИЗНАК_НП_ПОДВАЛ = "2" И Не ЗначениеЗаполнено(Титульная.НаимДок) Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
			"Не указан документ подписанта", "Титульная", "НаимДок"));
	КонецЕсли;
	
	НеПроверяемыеСтраницы = Новый Соответствие;
	Проверить_СвКонтрЛиц(Данные, ТаблицаОшибок);
	Проверить_СвСоблУслМХКПериод(Данные, ТаблицаОшибок);
	Проверить_ОснПрКонтрЛиц(Данные, ТаблицаОшибок);
	Проверить_СвПринОфисСобств(Данные, ТаблицаОшибок);
	Проверить_СвПринОфисИное(Данные, ТаблицаОшибок);
	Проверить_РаскрКонтрЛицМККосв(Данные, ТаблицаОшибок, НеПроверяемыеСтраницы);
	Проверить_РаскрУчМК(Данные, ТаблицаОшибок, НеПроверяемыеСтраницы);
	Проверить_СвИО(Данные, ТаблицаОшибок, НеПроверяемыеСтраницы);
	Проверить_СвРосОрг(Данные, ТаблицаОшибок, НеПроверяемыеСтраницы);
	Проверить_СведИСБезЮЛ(Данные, ТаблицаОшибок, НеПроверяемыеСтраницы);
	
	УведомлениеОСпецрежимахНалогообложения.ПроверкаДатВУведомлении(Данные, ТаблицаОшибок);
	УведомлениеОСпецрежимахНалогообложения.ПолнаяПроверкаЗаполненныхПоказателейНаСоотвествиеСписку(
		"СпискиВыбора_2022", "СхемаВыгрузкиФорма_2022", Данные.Объект.ИмяОтчета, ТаблицаОшибок, Данные);
	Возврат ТаблицаОшибок;
КонецФункции

Процедура Проверить_СвКонтрЛиц(Данные, ТаблицаОшибок)
	Для Каждого Стр0 Из Данные.ДанныеМногостраничныхРазделов.СвСоблУслМХК Цикл
		УИДРодителя = Стр0.Значение.УИД;
		КоличествоЗаполненных = 0;
		Для Каждого Стр Из Данные.ДанныеМногостраничныхРазделов.СвИОКонтрЛиц Цикл
			ДанныеТекущейСтраницы = Стр.Значение;
			Если ДанныеТекущейСтраницы.УИДРодителя <> УИДРодителя
				Или Не УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(ДанныеТекущейСтраницы) Тогда 
				Продолжить;
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(ДанныеТекущейСтраницы["ПрИнфКонтрЛиц"]) Тогда
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указан признак", "СвИОКонтрЛиц", "ПрИнфКонтрЛиц", ДанныеТекущейСтраницы.УИД));
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ДанныеТекущейСтраницы["НомерКонтрЛиц"]) Тогда
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указан номер контролирующего лица", "СвИОКонтрЛиц", "НомерКонтрЛиц", ДанныеТекущейСтраницы.УИД));
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ДанныеТекущейСтраницы["НаимОрг"]) Тогда
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указано наименование организации", "СвИОКонтрЛиц", "НаимОрг", ДанныеТекущейСтраницы.УИД));
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ДанныеТекущейСтраницы["НаимОргЛат"]) Тогда
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указано наименование организации в латинской транскрипции", "СвИОКонтрЛиц", "НаимОргЛат", ДанныеТекущейСтраницы.УИД));
			КонецЕсли;
			УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаОКСМ(ДанныеТекущейСтраницы, ТаблицаОшибок, "СвИОКонтрЛиц", "СтрРег", Истина);
			
			КоличествоЗаполненных = КоличествоЗаполненных + 1;
			Прервать;
		КонецЦикла;
		
		Для Каждого Стр Из Данные.ДанныеМногостраничныхРазделов.СвРосОКонтрЛиц Цикл
			ДанныеТекущейСтраницы = Стр.Значение;
			Если ДанныеТекущейСтраницы.УИДРодителя <> УИДРодителя
				Или Не УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(ДанныеТекущейСтраницы) Тогда 
				Продолжить;
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(ДанныеТекущейСтраницы["ПрИнфКонтрЛиц"]) Тогда
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указан признак", "СвРосОКонтрЛиц", "ПрИнфКонтрЛиц", ДанныеТекущейСтраницы.УИД));
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ДанныеТекущейСтраницы["НомерКонтрЛиц"]) Тогда
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указан номер контролирующего лица", "СвРосОКонтрЛиц", "НомерКонтрЛиц", ДанныеТекущейСтраницы.УИД));
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ДанныеТекущейСтраницы["НаимОрг"]) Тогда
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указано наименование организации", "СвРосОКонтрЛиц", "НаимОрг", ДанныеТекущейСтраницы.УИД));
			КонецЕсли;
			УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаИНН(ДанныеТекущейСтраницы, ТаблицаОшибок, "СвРосОКонтрЛиц", "ИННЮЛ", Истина, Истина);
			УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаКПП(ДанныеТекущейСтраницы, ТаблицаОшибок, "СвРосОКонтрЛиц", "КПП", Истина);
			УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаОГРН(ДанныеТекущейСтраницы, ТаблицаОшибок, "СвРосОКонтрЛиц", "ОГРН", Истина, Истина);
			
			КоличествоЗаполненных = КоличествоЗаполненных + 1;
			Прервать;
		КонецЦикла;
		
		Для Каждого Стр Из Данные.ДанныеМногостраничныхРазделов.СвФЛКонтрЛиц Цикл
			ДанныеТекущейСтраницы = Стр.Значение;
			Если ДанныеТекущейСтраницы.УИДРодителя <> УИДРодителя
				Или Не УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(ДанныеТекущейСтраницы) Тогда 
				Продолжить;
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(ДанныеТекущейСтраницы["ПрИнфКонтрЛиц"]) Тогда
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указан признак", "СвФЛКонтрЛиц", "ПрИнфКонтрЛиц", ДанныеТекущейСтраницы.УИД));
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ДанныеТекущейСтраницы["НомерКонтрЛиц"]) Тогда
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указан номер контролирующего лица", "СвФЛКонтрЛиц", "НомерКонтрЛиц", ДанныеТекущейСтраницы.УИД));
			КонецЕсли;
			УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаИНН(ДанныеТекущейСтраницы, ТаблицаОшибок, "СвРосОКонтрЛиц", "ИННФЛ", Ложь, Ложь);
			Если Не ЗначениеЗаполнено(ДанныеТекущейСтраницы["ПрГражд"]) Тогда
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указан признак гражданства", "СвФЛКонтрЛиц", "ПрГражд", ДанныеТекущейСтраницы.УИД));
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ДанныеТекущейСтраницы["ПрРезидент"]) Тогда
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указан признак налогового резиденства", "СвФЛКонтрЛиц", "ПрРезидент", ДанныеТекущейСтраницы.УИД));
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ДанныеТекущейСтраницы["Пол"]) Тогда
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указан пол", "СвФЛКонтрЛиц", "Пол", ДанныеТекущейСтраницы.УИД));
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ДанныеТекущейСтраницы["Пол"]) Тогда
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указан пол", "СвФЛКонтрЛиц", "Пол", ДанныеТекущейСтраницы.УИД));
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ДанныеТекущейСтраницы["ДатаРожд"]) Тогда
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указана дата рождения", "СвФЛКонтрЛиц", "ДатаРожд", ДанныеТекущейСтраницы.УИД));
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ДанныеТекущейСтраницы["МестоРожд"]) Тогда
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указано место рождения", "СвФЛКонтрЛиц", "МестоРожд", ДанныеТекущейСтраницы.УИД));
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ДанныеТекущейСтраницы["Фамилия"]) Тогда
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указана фамилия", "СвФЛКонтрЛиц", "Фамилия", ДанныеТекущейСтраницы.УИД));
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ДанныеТекущейСтраницы["Имя"]) Тогда
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указано имя", "СвФЛКонтрЛиц", "Имя", ДанныеТекущейСтраницы.УИД));
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ДанныеТекущейСтраницы["Имя"]) Тогда
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указано имя", "СвФЛКонтрЛиц", "Имя", ДанныеТекущейСтраницы.УИД));
			КонецЕсли;
			УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаОКСМ(ДанныеТекущейСтраницы, 
				ТаблицаОшибок, "СвФЛКонтрЛиц", "ОКСМРез", ДанныеТекущейСтраницы["ПрРезидент"] = "2");
			УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаОКСМ(ДанныеТекущейСтраницы, 
				ТаблицаОшибок, "СвФЛКонтрЛиц", "ОКСМГр", ДанныеТекущейСтраницы["ПрГражд"] = "2");
			УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаОКСМ(ДанныеТекущейСтраницы, 
				ТаблицаОшибок, "СвФЛКонтрЛиц", "СтрРегФЛ", ЗначениеЗаполнено(ДанныеТекущейСтраницы["КодФЛНПРег"]));
				
				Если Не ЗначениеЗаполнено(ДанныеТекущейСтраницы["УдЛичнФЛ_КодВидДок"]) Тогда
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указан код вида документа", "СвФЛКонтрЛиц", "УдЛичнФЛ_КодВидДок", ДанныеТекущейСтраницы.УИД));
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ДанныеТекущейСтраницы["УдЛичнФЛ_СерНомДок"]) Тогда
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указаны серия/номер документа", "СвФЛКонтрЛиц", "УдЛичнФЛ_СерНомДок", ДанныеТекущейСтраницы.УИД));
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ДанныеТекущейСтраницы["УдЛичнФЛ_ДатаДок"]) Тогда
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указана дата выдачи документа", "СвФЛКонтрЛиц", "УдЛичнФЛ_ДатаДок", ДанныеТекущейСтраницы.УИД));
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ДанныеТекущейСтраницы["УдЛичнФЛ_ВыдДок"]) Тогда
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указан орган, выдавший документ", "СвФЛКонтрЛиц", "УдЛичнФЛ_ВыдДок", ДанныеТекущейСтраницы.УИД));
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ДанныеТекущейСтраницы["СвДокПодтвРег_КодВидДок"])
				Или ЗначениеЗаполнено(ДанныеТекущейСтраницы["СвДокПодтвРег_СерНомДок"])
				Или ЗначениеЗаполнено(ДанныеТекущейСтраницы["СвДокПодтвРег_ДатаДок"])
				Или ЗначениеЗаполнено(ДанныеТекущейСтраницы["СвДокПодтвРег_ВыдДок"]) Тогда 
			
				Если Не ЗначениеЗаполнено(ДанныеТекущейСтраницы["СвДокПодтвРег_КодВидДок"]) Тогда
					ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
						"Не указан код вида документа", "СвФЛКонтрЛиц", "СвДокПодтвРег_КодВидДок", ДанныеТекущейСтраницы.УИД));
				КонецЕсли;
				Если Не ЗначениеЗаполнено(ДанныеТекущейСтраницы["СвДокПодтвРег_СерНомДок"]) Тогда
					ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
						"Не указаны серия/номер документа", "СвФЛКонтрЛиц", "СвДокПодтвРег_СерНомДок", ДанныеТекущейСтраницы.УИД));
				КонецЕсли;
				Если Не ЗначениеЗаполнено(ДанныеТекущейСтраницы["СвДокПодтвРег_ДатаДок"]) Тогда
					ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
						"Не указана дата выдачи документа", "СвФЛКонтрЛиц", "СвДокПодтвРег_ДатаДок", ДанныеТекущейСтраницы.УИД));
				КонецЕсли;
				Если Не ЗначениеЗаполнено(ДанныеТекущейСтраницы["СвДокПодтвРег_ВыдДок"]) Тогда
					ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
						"Не указан орган, выдавший документ", "СвФЛКонтрЛиц", "СвДокПодтвРег_ВыдДок", ДанныеТекущейСтраницы.УИД));
				КонецЕсли;
			КонецЕсли;
			
			КоличествоЗаполненных = КоличествоЗаполненных + 1;
			Прервать;
		КонецЦикла;
		
		Если КоличествоЗаполненных <> 1 Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Необходимо заполнить строго один из листов А.1, А.2 или А.3", "СвСоблУслМХК", "", УИДРодителя));
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура Проверить_ОснПрКонтрЛиц(Данные, ТаблицаОшибок)
	Для Каждого Стр Из Данные.ДанныеМногостраничныхРазделов.ОснПрКонтрЛиц Цикл 
		ОснПрКонтрЛиц = Стр.Значение;
		Для Инд = 1 По 5 Цикл
			Если Не ЗначениеЗаполнено(ОснПрКонтрЛиц["Код20" + Инд]) Тогда
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указан код 20" + Инд, "ОснПрКонтрЛиц", "Код20" + Инд, ОснПрКонтрЛиц.УИД));
			КонецЕсли;
		КонецЦикла;
		Для Инд = 1 По 4 Цикл
			Если Не ЗначениеЗаполнено(ОснПрКонтрЛиц["Код30" + Инд]) Тогда
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указан код 30" + Инд, "ОснПрКонтрЛиц", "Код30" + Инд, ОснПрКонтрЛиц.УИД));
			КонецЕсли;
		КонецЦикла;
		Если Не ЗначениеЗаполнено(ОснПрКонтрЛиц["ПрИнфКонтрЛиц"]) Тогда
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указан признак раскрытия информации", "ОснПрКонтрЛиц", "ПрИнфКонтрЛиц", ОснПрКонтрЛиц.УИД));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ОснПрКонтрЛиц["ОснКонтрЛиц"]) Тогда
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указано основание", "ОснПрКонтрЛиц", "ОснКонтрЛиц", ОснПрКонтрЛиц.УИД));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ОснПрКонтрЛиц["НомерКонтрЛиц"]) Тогда
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указан номер контролирующего лица", "ОснПрКонтрЛиц", "НомерКонтрЛиц", ОснПрКонтрЛиц.УИД));
		КонецЕсли;
		
		Если (ОснПрКонтрЛиц.Код201 = "0" И ЗначениеЗаполнено(ОснПрКонтрЛиц.ДоляПрямУч))
			Или (ОснПрКонтрЛиц.Код201 = "1" И Не ЗначениеЗаполнено(ОснПрКонтрЛиц.ДоляПрямУч)) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"При Код201=1 доля прямого участия должна быть заполнена, при Код201=0 не заполняется",
				"ОснПрКонтрЛиц", "ДоляПрямУч", ОснПрКонтрЛиц.УИД));
		КонецЕсли;
		Если (ОснПрКонтрЛиц.Код202 = "0" И ЗначениеЗаполнено(ОснПрКонтрЛиц.ДоляКосУч))
			Или (ОснПрКонтрЛиц.Код202 = "1" И Не ЗначениеЗаполнено(ОснПрКонтрЛиц.ДоляКосУч)) Тогда
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"При Код202=1 доля косвенного участия должна быть заполнена, при Код202=0 не заполняется", "ОснПрКонтрЛиц",
				"ДоляКосУч", ОснПрКонтрЛиц.УИД));
		КонецЕсли;
		Если (ОснПрКонтрЛиц.Код203 = "0" И ЗначениеЗаполнено(ОснПрКонтрЛиц.ДоляСмешУч))
			Или (ОснПрКонтрЛиц.Код203 = "1" И Не ЗначениеЗаполнено(ОснПрКонтрЛиц.ДоляСмешУч)) Тогда
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"При Код203=1 доля смешанного участия должна быть заполнена, при Код203=0 не заполняется",
				"ОснПрКонтрЛиц", "ДоляСмешУч", ОснПрКонтрЛиц.УИД));
		КонецЕсли;
		Если (ОснПрКонтрЛиц.Код204 = "0" И ЗначениеЗаполнено(ОснПрКонтрЛиц.ДоляУчСупДет))
			Или (ОснПрКонтрЛиц.Код204 = "1" И Не ЗначениеЗаполнено(ОснПрКонтрЛиц.ДоляУчСупДет)) Тогда
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"При Код204=1 доля совместного участия должна быть заполнена, при Код204=0 не заполняется",
				"ОснПрКонтрЛиц", "ДоляУчСупДет", ОснПрКонтрЛиц.УИД));
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура Проверить_СвПринОфисСобств(Данные, ТаблицаОшибок)
	Для Каждого Стр Из Данные.ДанныеМногостраничныхРазделов.СвПринОфисСобств Цикл 
		СвПринОфисСобств = Стр.Значение;
		Если Не УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(СвПринОфисСобств) Тогда 
			Продолжить;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СвПринОфисСобств["НаимОб"]) Тогда
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указано наименование объекта", "СвПринОфисСобств", "НаимОб", СвПринОфисСобств.УИД));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(СвПринОфисСобств["КадастрНомер"]) Тогда
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указан кадастровый номер", "СвПринОфисСобств", "КадастрНомер", СвПринОфисСобств.УИД));
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура Проверить_СвПринОфисИное(Данные, ТаблицаОшибок)
	Для Каждого Стр Из Данные.ДанныеМногостраничныхРазделов.СвПринОфисИное Цикл 
		СвПринОфисИное = Стр.Значение;
		Если Не УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(СвПринОфисИное) Тогда 
			Продолжить;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СвПринОфисИное["НаимОб"]) Тогда
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указано наименование объекта", "СвПринОфисИное", "НаимОб", СвПринОфисИное.УИД));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(СвПринОфисИное["КадастрНомер"]) Тогда
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указан кадастровый номер", "СвПринОфисИное", "КадастрНомер", СвПринОфисИное.УИД));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(СвПринОфисИное["Основание"]) Тогда
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указано основание", "СвПринОфисИное", "Основание", СвПринОфисИное.УИД));
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура Проверить_СвСоблУслМХКПериод(Данные, ТаблицаОшибок)
	Для Каждого Стр Из Данные.ДанныеМногостраничныхРазделов.СвСоблУслМХКПериод Цикл 
		СвСоблУслМХКПериод = Стр.Значение;
		Для Каждого Элт Из СтрРазделить("КонтрЛицРезРФ,КонтрЛицГрРФ,ДоляНачПериод,ДоляДохПериод,"
			+"ДоляРасхПериод,УпрТерРФ,ШтатРабДох,ОфисТерСпецРайон,МКОтвечУсл", ",") Цикл 
			
			Если Не ЗначениеЗаполнено(СвСоблУслМХКПериод[Элт]) Тогда
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указан код", "СвСоблУслМХКПериод", Элт, СвСоблУслМХКПериод.УИД));
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

Процедура Проверить_РаскрКонтрЛицМККосв(Данные, ТаблицаОшибок, НеПроверяемыеСтраницы)
	Для Каждого Стр Из Данные.ДанныеМногостраничныхРазделов.РаскрКонтрЛицМККосв Цикл 
		РаскрКонтрЛицМККосв = Стр.Значение;
		Если Не УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(РаскрКонтрЛицМККосв) Тогда
			НеПроверяемыеСтраницы.Вставить(РаскрКонтрЛицМККосв.УИД, Истина);
			Продолжить;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(РаскрКонтрЛицМККосв["ПрИнфКонтрЛиц"]) Тогда
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указан код", "РаскрКонтрЛицМККосв", "ПрИнфКонтрЛиц", РаскрКонтрЛицМККосв.УИД));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(РаскрКонтрЛицМККосв["НомерКонтрЛиц"]) Тогда
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указан номер контролирующего лица", "РаскрКонтрЛицМККосв", "НомерКонтрЛиц", РаскрКонтрЛицМККосв.УИД));
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура Проверить_РаскрУчМК(Данные, ТаблицаОшибок, НеПроверяемыеСтраницы)
	Для Каждого Стр Из Данные.ДанныеМногостраничныхРазделов.РаскрУчМК Цикл 
		РаскрУчМК = Стр.Значение;
		Если НеПроверяемыеСтраницы[РаскрУчМК.УИДРодителя] = Истина
			Или Не УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(РаскрУчМК) Тогда
			НеПроверяемыеСтраницы.Вставить(РаскрУчМК.УИД, Истина);
			Продолжить;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(РаскрУчМК["НомерПосл"]) Тогда
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указан номер последовательности", "РаскрУчМК", "НомерПосл", РаскрУчМК.УИД));
		КонецЕсли;
		
		Инд = 0;
		Для Каждого МнгСтр Из Данные.ДанныеДопСтрокБД.МнгСтр Цикл 
			Если МнгСтр.УИД <> РаскрУчМК.УИД Тогда 
				Продолжить;
			КонецЕсли;
			
			Инд = Инд + 1;
			Если Не ЗначениеЗаполнено(МнгСтр.НомерУчПосл) Тогда 
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указан номер участника", "РаскрУчМК", "НомерУчПосл___" + Формат(Инд, "ЧГ="), РаскрУчМК.УИД));
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

Процедура Проверить_СвИО(Данные, ТаблицаОшибок, НеПроверяемыеСтраницы)
	Для Каждого Стр Из Данные.ДанныеМногостраничныхРазделов.СвИО Цикл 
		СвИО = Стр.Значение;
		Если НеПроверяемыеСтраницы[СвИО.УИДРодителя] = Истина
			Или Не УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(СвИО) Тогда
			НеПроверяемыеСтраницы.Вставить(СвИО.УИД, Истина);
			Продолжить;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СвИО["ПрИнфКонтрЛиц"]) Тогда
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указан признак", "СвИО", "ПрИнфКонтрЛиц", СвИО.УИД));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(СвИО["НомерПосл"]) Тогда
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указан номер последовательности", "СвИО", "НомерПосл", СвИО.УИД));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(СвИО["НомерУчПосл"]) Тогда
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указан номер участника", "СвИО", "НомерУчПосл", СвИО.УИД));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(СвИО["НаимОрг"]) Тогда
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указано наименование структуры", "СвИО", "НаимОрг", СвИО.УИД));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(СвИО["НаимОргЛат"]) Тогда
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указано наименование структуры в латинской транскрипции", "СвИО", "НаимОргЛат", СвИО.УИД));
		КонецЕсли;
		УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаОКСМ(СвИО, ТаблицаОшибок, "СвИО", "СтрРег", Истина);
	КонецЦикла;
КонецПроцедуры

Процедура Проверить_СвРосОрг(Данные, ТаблицаОшибок, НеПроверяемыеСтраницы)
	Для Каждого Стр Из Данные.ДанныеМногостраничныхРазделов.СвРосОрг Цикл 
		СвРосОрг = Стр.Значение;
		Если НеПроверяемыеСтраницы[СвРосОрг.УИДРодителя] = Истина
			Или Не УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(СвРосОрг) Тогда
			НеПроверяемыеСтраницы.Вставить(СвРосОрг.УИД, Истина);
			Продолжить;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СвРосОрг["ПрИнфКонтрЛиц"]) Тогда
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указан признак", "СвРосОрг", "ПрИнфКонтрЛиц", СвРосОрг.УИД));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(СвРосОрг["НомерПосл"]) Тогда
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указан номер последовательности", "СвРосОрг", "НомерПосл", СвРосОрг.УИД));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(СвРосОрг["НомерУчПосл"]) Тогда
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указан номер участника", "СвРосОрг", "НомерУчПосл", СвРосОрг.УИД));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(СвРосОрг["НаимОрг"]) Тогда
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указано наименование", "СвРосОрг", "НаимОрг", СвРосОрг.УИД));
		КонецЕсли;
		УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаИНН(СвРосОрг, ТаблицаОшибок, "СвРосОрг", "ИННЮЛ", Истина, Истина);
		УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаКПП(СвРосОрг, ТаблицаОшибок, "СвРосОрг", "КПП", Истина);
		УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаОГРН(СвРосОрг, ТаблицаОшибок, "СвРосОрг", "ОГРН", Истина, Истина);
	КонецЦикла;
КонецПроцедуры

Процедура Проверить_СведИСБезЮЛ(Данные, ТаблицаОшибок, НеПроверяемыеСтраницы)
	Для Каждого Стр Из Данные.ДанныеМногостраничныхРазделов.СведИСБезЮЛ Цикл 
		СведИСБезЮЛ = Стр.Значение;
		Если НеПроверяемыеСтраницы[СведИСБезЮЛ.УИДРодителя] = Истина
			Или Не УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(СведИСБезЮЛ) Тогда
			НеПроверяемыеСтраницы.Вставить(СведИСБезЮЛ.УИД, Истина);
			Продолжить;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СведИСБезЮЛ["ПрИнфКонтрЛиц"]) Тогда
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указан признак", "СведИСБезЮЛ", "ПрИнфКонтрЛиц", СведИСБезЮЛ.УИД));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(СведИСБезЮЛ["НомерПосл"]) Тогда
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указан номер последовательности", "СведИСБезЮЛ", "НомерПосл", СведИСБезЮЛ.УИД));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(СведИСБезЮЛ["НомерУчПосл"]) Тогда
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указан номер участника", "СведИСБезЮЛ", "НомерУчПосл", СведИСБезЮЛ.УИД));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(СведИСБезЮЛ["НаимОрг"]) Тогда
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указано наименование структуры", "СведИСБезЮЛ", "НаимОрг", СведИСБезЮЛ.УИД));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(СведИСБезЮЛ["НаимОргЛат"]) Тогда
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указано наименование структуры в латинской транскрипции", "СведИСБезЮЛ", "НаимОргЛат", СведИСБезЮЛ.УИД));
		КонецЕсли;
		УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаОКСМ(СведИСБезЮЛ, ТаблицаОшибок, "СведИСБезЮЛ", "СтрРег", Истина);
		Если Не ЗначениеЗаполнено(СведИСБезЮЛ["ОргФорм"]) Тогда
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указан код организационной формы", "СведИСБезЮЛ", "ОргФорм", СведИСБезЮЛ.УИД));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(СведИСБезЮЛ["ДатаУчрИС"]) Тогда
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указана дата учреждения структуры", "СведИСБезЮЛ", "ДатаУчрИС", СведИСБезЮЛ.УИД));
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Функция ОсновныеСведенияЭлектронногоПредставления_Форма2022_1(Объект, УникальныйИдентификатор)
	ОсновныеСведения = Документы.УведомлениеОСпецрежимахНалогообложения.НачальнаяИнициализацияОбщихРеквизитовВыгрузки(Объект);
	ИдентификаторФайла = ИдентификаторФайлаЭлектронногоПредставления_Форма2022_1(ОсновныеСведения);
	ОсновныеСведения.Вставить("ИдФайл", ИдентификаторФайла);
	Возврат ОсновныеСведения;
КонецФункции

Функция ЭлектронноеПредставление_Форма2022_1(Объект, УникальныйИдентификатор)
	СведенияЭлектронногоПредставления = УведомлениеОСпецрежимахНалогообложения.СведенияЭлектронногоПредставления();
	ДанныеУведомления = УведомлениеОСпецрежимахНалогообложения.ДанныеУведомленияДляВыгрузки(Объект);
	Ошибки = ПроверитьДокументСВыводомВТаблицу_Форма2022_1(ДанныеУведомления, УникальныйИдентификатор);
	УведомлениеОСпецрежимахНалогообложения.СообщитьОшибкиПриПроверкеВыгрузки(Объект, Ошибки, ДанныеУведомления);
	
	ДополнитьПараметры(ДанныеУведомления);
	ОсновныеСведения = ОсновныеСведенияЭлектронногоПредставления_Форма2022_1(Объект, УникальныйИдентификатор);
	СтруктураВыгрузки = Документы.УведомлениеОСпецрежимахНалогообложения.ИзвлечьСтруктуруXMLУведомления(Объект.ИмяОтчета, "СхемаВыгрузкиФорма_2022");
	УведомлениеОСпецрежимахНалогообложения.ТиповоеЗаполнениеДанными(ДанныеУведомления, ОсновныеСведения, СтруктураВыгрузки);
	УведомлениеОСпецрежимахНалогообложения.ВыгрузитьДеревоВТаблицу(СтруктураВыгрузки, ОсновныеСведения, СведенияЭлектронногоПредставления);
	Возврат СведенияЭлектронногоПредставления;
КонецФункции

Процедура ДополнитьПараметры(ДанныеУведомления)
	Для Каждого Стр Из ДанныеУведомления.ДанныеМногостраничныхРазделов.СвИОКонтрЛиц Цикл 
		Данные = Стр.Значение;
		Если УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(Данные) Тогда 
			Данные.Вставить("НомерКонтрЛицСтр", "ИО-" + Формат(Данные.НомерКонтрЛиц, "ЧЦ=5; ЧН=00000; ЧВН=; ЧГ="));
		КонецЕсли;
	КонецЦикла;
	Для Каждого Стр Из ДанныеУведомления.ДанныеМногостраничныхРазделов.СвРосОКонтрЛиц Цикл 
		Данные = Стр.Значение;
		Если УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(Данные) Тогда 
			Данные.Вставить("НомерКонтрЛицСтр", "РО-" + Формат(Данные.НомерКонтрЛиц, "ЧЦ=5; ЧН=00000; ЧВН=; ЧГ="));
		КонецЕсли;
	КонецЦикла;
	Для Каждого Стр Из ДанныеУведомления.ДанныеМногостраничныхРазделов.СвФЛКонтрЛиц Цикл 
		Данные = Стр.Значение;
		Если УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(Данные) Тогда 
			Данные.Вставить("НомерКонтрЛицСтр", "ФЛ-" + Формат(Данные.НомерКонтрЛиц, "ЧЦ=5; ЧН=00000; ЧВН=; ЧГ="));
		КонецЕсли;
	КонецЦикла;
	Для Каждого Стр Из ДанныеУведомления.ДанныеМногостраничныхРазделов.СвИО Цикл 
		Данные = Стр.Значение;
		Если УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(Данные) Тогда 
			Данные.Вставить("НомерУчПослСтр", "УИ-" + Формат(Данные.НомерУчПосл, "ЧЦ=5; ЧН=00000; ЧВН=; ЧГ="));
			Данные.Вставить("НомерПослСтр", Формат(Данные.НомерПосл, "ЧЦ=5; ЧН=00000; ЧВН=; ЧГ="));
		КонецЕсли;
	КонецЦикла;
	Для Каждого Стр Из ДанныеУведомления.ДанныеМногостраничныхРазделов.РаскрУчМК Цикл 
		Данные = Стр.Значение;
		Если УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(Данные) Тогда 
			Данные.Вставить("НомерПослСтр", Формат(Данные.НомерПосл, "ЧЦ=5; ЧН=00000; ЧВН=; ЧГ="));
		КонецЕсли;
	КонецЦикла;
	Для Каждого Стр Из ДанныеУведомления.ДанныеМногостраничныхРазделов.СвРосОрг Цикл 
		Данные = Стр.Значение;
		Если УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(Данные) Тогда 
			Данные.Вставить("НомерУчПослСтр", "УР-" + Формат(Данные.НомерУчПосл, "ЧЦ=5; ЧН=00000; ЧВН=; ЧГ="));
			Данные.Вставить("НомерПослСтр", Формат(Данные.НомерПосл, "ЧЦ=5; ЧН=00000; ЧВН=; ЧГ="));
		КонецЕсли;
	КонецЦикла;
	Для Каждого Стр Из ДанныеУведомления.ДанныеМногостраничныхРазделов.СведИСБезЮЛ Цикл 
		Данные = Стр.Значение;
		Если УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(Данные) Тогда 
			Данные.Вставить("НомерУчПослСтр", "УС-" + Формат(Данные.НомерУчПосл, "ЧЦ=5; ЧН=00000; ЧВН=; ЧГ="));
			Данные.Вставить("НомерПослСтр", Формат(Данные.НомерПосл, "ЧЦ=5; ЧН=00000; ЧВН=; ЧГ="));
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Стр Из ДанныеУведомления.ДанныеМногостраничныхРазделов.РаскрКонтрЛицМККосв Цикл 
		Данные = Стр.Значение;
		Если Не УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(Данные) Тогда 
			Данные["УИД"] = Новый УникальныйИдентификатор;
		КонецЕсли;
	КонецЦикла;
	Для Каждого Стр Из ДанныеУведомления.ДанныеМногостраничныхРазделов.РаскрУчМК Цикл 
		Данные = Стр.Значение;
		Если Не УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(Данные) Тогда 
			Данные["УИД"] = Новый УникальныйИдентификатор;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Функция СформироватьСписокЛистовФорма2022_1(Объект) Экспорт
	Листы = Новый СписокЗначений;
	ПечатнаяФорма = УведомлениеОСпецрежимахНалогообложения.НовыйПустойЛист();
	СтруктураПараметров = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Ссылка, "ДанныеУведомления").Получить();
	ИННКПП = УведомлениеОСпецрежимахНалогообложения.ТиповаяСтруктураИННКППДляПечати(Объект, СтруктураПараметров.ДанныеУведомления.Титульная);
	
	НомСтр = 0;
	УведомлениеОСпецрежимахНалогообложения.НапечататьСтруктуру(Объект.ИмяОтчета,
		СтруктураПараметров.ДанныеУведомления["Титульная"], НомСтр, "Печать_Форма2022_Титульная", ПечатнаяФорма, ИННКПП);
	УведомлениеОСпецрежимахНалогообложения.ПоложитьПФВСписокЛистов(Объект, Листы, ПечатнаяФорма, НомСтр);
	
	СтруктураСтраницыЛистВ = Новый Структура;
	СтруктураСтраницыЛистВ.Вставить("ПрИнфКонтрЛиц");
	СтруктураСтраницыЛистВ.Вставить("НомерКонтрЛиц");
	СтруктураСтраницыЛистВ.Вставить("ДоляУчКосвИт");
	СтруктураСтраницыЛистВ.Вставить("НомерПосл");
	СтруктураСтраницыЛистВ.Вставить("ДоляКосвПослИт");
	Для Инд = 1 По 10 Цикл
		СтруктураСтраницыЛистВ.Вставить("НомерУчПосл_" + Инд);
		СтруктураСтраницыЛистВ.Вставить("ДоляУчПрям_" + Инд);
		СтруктураСтраницыЛистВ.Вставить("ДоляУчКосв_" + Инд);
	КонецЦикла;
	
	НомСтр = 1;
	Для Каждого Стр Из СтруктураПараметров.ДанныеМногостраничныхРазделов.СвСоблУслМХК Цикл
		УИДОтбора = Стр.Значение.УИД;
		Данные = ОбщегоНазначения.СкопироватьРекурсивно(СтруктураПараметров);
		
		Для Каждого Элт Из СтрРазделить("СвПринОфисСобств,СвПринОфисИное,СвСоблУслМХКПериод,СвИОКонтрЛиц,СвРосОКонтрЛиц,"
			+ "СвФЛКонтрЛиц,ОснПрКонтрЛиц,РаскрКонтрЛицМККосв,СвДохРасхМХК", ",") Цикл 
			
			Итер = 0;
			Пока Итер < Данные.ДанныеМногостраничныхРазделов[Элт].Количество() Цикл 
				Если Данные.ДанныеМногостраничныхРазделов[Элт][Итер].Значение.УИДРодителя = УИДОтбора Тогда 
					Итер = Итер + 1;
				Иначе
					Данные.ДанныеМногостраничныхРазделов[Элт].Удалить(Итер);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
		Для Каждого СтраницаДляПечати Из Данные.ДанныеМногостраничныхРазделов.СвСоблУслМХКПериод Цикл 
			Если УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(СтраницаДляПечати.Значение) Тогда 
				УведомлениеОСпецрежимахНалогообложения.НапечататьСтруктуру(Объект.ИмяОтчета,
					СтраницаДляПечати.Значение, НомСтр, "Печать_Форма2022_СвСоблУслМХКПериод", ПечатнаяФорма, ИННКПП);
				УведомлениеОСпецрежимахНалогообложения.ПоложитьПФВСписокЛистов(Объект, Листы, ПечатнаяФорма, НомСтр);
			КонецЕсли;
		КонецЦикла;
		Для Каждого СтраницаДляПечати Из Данные.ДанныеМногостраничныхРазделов.СвИОКонтрЛиц Цикл 
			Если УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(СтраницаДляПечати.Значение) Тогда 
				СтраницаДляПечати.Значение.НомерКонтрЛиц = Формат(СтраницаДляПечати.Значение.НомерКонтрЛиц, "ЧЦ=5; ЧВН=; ЧГ=");
				УведомлениеОСпецрежимахНалогообложения.НапечататьСтруктуру(Объект.ИмяОтчета,
					СтраницаДляПечати.Значение, НомСтр, "Печать_Форма2022_СвИОКонтрЛиц", ПечатнаяФорма, ИННКПП);
				УведомлениеОСпецрежимахНалогообложения.ПоложитьПФВСписокЛистов(Объект, Листы, ПечатнаяФорма, НомСтр);
			КонецЕсли;
		КонецЦикла;
		Для Каждого СтраницаДляПечати Из Данные.ДанныеМногостраничныхРазделов.СвРосОКонтрЛиц Цикл
			Если УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(СтраницаДляПечати.Значение) Тогда 
				СтраницаДляПечати.Значение.НомерКонтрЛиц = Формат(СтраницаДляПечати.Значение.НомерКонтрЛиц, "ЧЦ=5; ЧВН=; ЧГ=");
				УведомлениеОСпецрежимахНалогообложения.НапечататьСтруктуру(Объект.ИмяОтчета,
					СтраницаДляПечати.Значение, НомСтр, "Печать_Форма2022_СвРосОКонтрЛиц", ПечатнаяФорма, ИННКПП);
				УведомлениеОСпецрежимахНалогообложения.ПоложитьПФВСписокЛистов(Объект, Листы, ПечатнаяФорма, НомСтр);
			КонецЕсли;
		КонецЦикла;
		Для Каждого СтраницаДляПечати Из Данные.ДанныеМногостраничныхРазделов.СвФЛКонтрЛиц Цикл
			Если УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(СтраницаДляПечати.Значение) Тогда 
				СтраницаДляПечати.Значение.НомерКонтрЛиц = Формат(СтраницаДляПечати.Значение.НомерКонтрЛиц, "ЧЦ=5; ЧВН=; ЧГ=");
				УведомлениеОСпецрежимахНалогообложения.НапечататьСтруктуру(Объект.ИмяОтчета,
					СтраницаДляПечати.Значение, НомСтр, "Печать_Форма2022_СвФЛКонтрЛиц", ПечатнаяФорма, ИННКПП);
				УведомлениеОСпецрежимахНалогообложения.ПоложитьПФВСписокЛистов(Объект, Листы, ПечатнаяФорма, НомСтр);
			КонецЕсли;
		КонецЦикла;
		Для Каждого СтраницаДляПечати Из Данные.ДанныеМногостраничныхРазделов.ОснПрКонтрЛиц Цикл 
			Если УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(СтраницаДляПечати.Значение) Тогда 
				УведомлениеОСпецрежимахНалогообложения.НапечататьСтруктуру(Объект.ИмяОтчета,
					СтраницаДляПечати.Значение, НомСтр, "Печать_Форма2022_ОснПрКонтрЛиц", ПечатнаяФорма, ИННКПП);
				УведомлениеОСпецрежимахНалогообложения.ПоложитьПФВСписокЛистов(Объект, Листы, ПечатнаяФорма, НомСтр);
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого Стр0 Из Данные.ДанныеМногостраничныхРазделов.РаскрКонтрЛицМККосв Цикл 
			Для Каждого Стр1 Из СтруктураПараметров.ДанныеМногостраничныхРазделов.РаскрУчМК Цикл
				Если Стр1.Значение.УИДРодителя <> Стр0.Значение.УИД
					Или Не УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(Стр1.Значение)
					Или Не УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(Стр0.Значение)Тогда 
					Продолжить;
				КонецЕсли;
				
				МнгСтрСтроки = СтруктураПараметров.ДанныеДопСтрокБД.МнгСтр.НайтиСтроки(Новый Структура("УИД", Стр1.Значение.УИД));
				КоличествоСтраниц = Цел(МнгСтрСтроки.Количество() / 10) + ?(МнгСтрСтроки.Количество() % 10 = 0, 0, 1);
				Для Инд = 1 По КоличествоСтраниц Цикл 
					Для Каждого КЗ Из СтруктураСтраницыЛистВ Цикл 
						СтруктураСтраницыЛистВ[КЗ.Ключ] = Неопределено;
					КонецЦикла;
					
					ЗаполнитьЗначенияСвойств(СтруктураСтраницыЛистВ, Стр0.Значение);
					ЗаполнитьЗначенияСвойств(СтруктураСтраницыЛистВ, Стр1.Значение);
					СтруктураСтраницыЛистВ.НомерПосл = Формат(СтруктураСтраницыЛистВ.НомерПосл, "ЧЦ=5; ЧВН=; ЧГ=");
					Для Итер = 0 По 9 Цикл 
						ПозицияВТаблице = (10 * (Инд - 1)) + Итер;
						Если ПозицияВТаблице > МнгСтрСтроки.ВГраница() Тогда 
							Прервать;
						КонецЕсли;
						
						СтруктураСтраницыЛистВ["НомерУчПосл_" + (Итер + 1)] = МнгСтрСтроки[ПозицияВТаблице]["НомерУчПосл"];
						СтруктураСтраницыЛистВ["ДоляУчПрям_" + (Итер + 1)] = МнгСтрСтроки[ПозицияВТаблице]["ДоляУчПрям"];
						СтруктураСтраницыЛистВ["ДоляУчКосв_" + (Итер + 1)] = МнгСтрСтроки[ПозицияВТаблице]["ДоляУчКосв"];
					КонецЦикла;
					
					УведомлениеОСпецрежимахНалогообложения.НапечататьСтруктуру(Объект.ИмяОтчета,
						СтруктураСтраницыЛистВ, НомСтр, "Печать_Форма2022_РаскрУчМК", ПечатнаяФорма, ИННКПП);
					УведомлениеОСпецрежимахНалогообложения.ПоложитьПФВСписокЛистов(Объект, Листы, ПечатнаяФорма, НомСтр);
				КонецЦикла;
				
				Для Каждого Стр2 Из СтруктураПараметров.ДанныеМногостраничныхРазделов.СвИО Цикл
					Если Стр2.Значение.УИДРодителя <> Стр1.Значение.УИД
						Или Не УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(Стр2.Значение) Тогда 
						Продолжить;
					КонецЕсли;
					
					Стр2.Значение.НомерПосл = Формат(Стр2.Значение.НомерПосл, "ЧЦ=5; ЧВН=; ЧГ=");
					Стр2.Значение.НомерУчПосл = Формат(Стр2.Значение.НомерУчПосл, "ЧЦ=5; ЧВН=; ЧГ=");
					УведомлениеОСпецрежимахНалогообложения.НапечататьСтруктуру(Объект.ИмяОтчета,
						Стр2.Значение, НомСтр, "Печать_Форма2022_СвИО", ПечатнаяФорма, ИННКПП);
					УведомлениеОСпецрежимахНалогообложения.ПоложитьПФВСписокЛистов(Объект, Листы, ПечатнаяФорма, НомСтр);
				КонецЦикла;
				Для Каждого Стр2 Из СтруктураПараметров.ДанныеМногостраничныхРазделов.СвРосОрг Цикл
					Если Стр2.Значение.УИДРодителя <> Стр1.Значение.УИД
						Или Не УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(Стр2.Значение) Тогда 
						Продолжить;
					КонецЕсли;
					
					Стр2.Значение.НомерПосл = Формат(Стр2.Значение.НомерПосл, "ЧЦ=5; ЧВН=; ЧГ=");
					Стр2.Значение.НомерУчПосл = Формат(Стр2.Значение.НомерУчПосл, "ЧЦ=5; ЧВН=; ЧГ=");
					УведомлениеОСпецрежимахНалогообложения.НапечататьСтруктуру(Объект.ИмяОтчета,
						Стр2.Значение, НомСтр, "Печать_Форма2022_СвРосОрг", ПечатнаяФорма, ИННКПП);
					УведомлениеОСпецрежимахНалогообложения.ПоложитьПФВСписокЛистов(Объект, Листы, ПечатнаяФорма, НомСтр);
				КонецЦикла;
				Для Каждого Стр2 Из СтруктураПараметров.ДанныеМногостраничныхРазделов.СведИСБезЮЛ Цикл
					Если Стр2.Значение.УИДРодителя <> Стр1.Значение.УИД
						Или Не УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(Стр2.Значение) Тогда 
						Продолжить;
					КонецЕсли;
					
					Стр2.Значение.НомерПосл = Формат(Стр2.Значение.НомерПосл, "ЧЦ=5; ЧВН=; ЧГ=");
					Стр2.Значение.НомерУчПосл = Формат(Стр2.Значение.НомерУчПосл, "ЧЦ=5; ЧВН=; ЧГ=");
					УведомлениеОСпецрежимахНалогообложения.НапечататьСтруктуру(Объект.ИмяОтчета,
						Стр2.Значение, НомСтр, "Печать_Форма2022_СведИСБезЮЛ", ПечатнаяФорма, ИННКПП);
					УведомлениеОСпецрежимахНалогообложения.ПоложитьПФВСписокЛистов(Объект, Листы, ПечатнаяФорма, НомСтр);
				КонецЦикла;
			КонецЦикла;
			Прервать;
		КонецЦикла;
		
		Для Каждого СтраницаДляПечати Из Данные.ДанныеМногостраничныхРазделов.СвДохРасхМХК Цикл
			Если УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(СтраницаДляПечати.Значение) Тогда 
				УведомлениеОСпецрежимахНалогообложения.НапечататьСтруктуру(Объект.ИмяОтчета,
					СтраницаДляПечати.Значение, НомСтр, "Печать_Форма2022_СвДохРасхМХК", ПечатнаяФорма, ИННКПП);
				УведомлениеОСпецрежимахНалогообложения.ПоложитьПФВСписокЛистов(Объект, Листы, ПечатнаяФорма, НомСтр);
			КонецЕсли;
		КонецЦикла;
		УведомлениеОСпецрежимахНалогообложения.ТиповаяПечатьНесколькихСтраницНаОдинБланк(
			Объект, Листы, Данные, "СвПринОфисСобств", 3, "Печать_Форма2022_СвПринОфисСобств", НомСтр, ИННКПП);
		УведомлениеОСпецрежимахНалогообложения.ТиповаяПечатьНесколькихСтраницНаОдинБланк(
			Объект, Листы, Данные, "СвПринОфисИное", 2, "Печать_Форма2022_СвПринОфисИное", НомСтр, ИННКПП);
	КонецЦикла;
	
	Возврат Листы;
КонецФункции
#КонецОбласти
#КонецЕсли
