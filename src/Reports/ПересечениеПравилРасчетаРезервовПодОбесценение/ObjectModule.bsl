#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	ПараметрыОтчета = ПолучитьПараметрыОтчета(НастройкиОтчета);
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Если РасчетСебестоимостиПовтИсп.ПартионныйУчетВерсии22(ПараметрыОтчета.ПериодОтчета) Тогда
	
		Если ПараметрыОтчета.ВариантФормирования = 1 Тогда
			РезультатЗапросаПоПравилам = ПолучитьДействующиеПравила(ПараметрыОтчета.ПериодОтчета, ПараметрыОтчета.Организация);
		Иначе
			РезультатЗапросаПоПравилам = ПолучитьГруппыПотенциальноПересекающихсяПравил(ПараметрыОтчета.ПериодОтчета, ПараметрыОтчета.Организация, ПараметрыОтчета.ВариантФормирования);
		КонецЕсли;
		
		Если РезультатЗапросаПоПравилам.Пустой() Тогда
			СформироватьПустуюТаблицуДляСКД(МенеджерВременныхТаблиц);
		Иначе
			Если ПараметрыОтчета.ВариантФормирования = 1 Тогда
				СформироватьВременнуюТаблицуДляСКДБезПоискаПересечений(МенеджерВременныхТаблиц, РезультатЗапросаПоПравилам, ПараметрыОтчета.ПериодОтчета);
			Иначе	
				СформироватьВременнуюТаблицуДляСКД(МенеджерВременныхТаблиц, РезультатЗапросаПоПравилам, ПараметрыОтчета.ПериодОтчета);
			КонецЕсли;
		КонецЕсли;

	Иначе
		СформироватьПустуюТаблицуДляСКД(МенеджерВременныхТаблиц);
	КонецЕсли;
		
	// Скомпонуем и выведем результат запроса в табличный документ
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);

	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки,, ДанныеРасшифровки,,, МенеджерВременныхТаблиц);

	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьПараметрыОтчета(НастройкиОтчета)
	
	СтруктураПараметров = Новый Структура("Организация,ПериодОтчета,ВариантФормирования");
	
	ПараметрыДанных = НастройкиОтчета.ПараметрыДанных;
	ПараметрОрганизация = ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Организация"));
	ПараметрПериод = ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ПериодОтчета"));
	ПараметрВариант = ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ВариантФормирования"));
	
	Если НЕ ПараметрОрганизация.Использование ИЛИ НЕ ЗначениеЗаполнено(ПараметрОрганизация.Значение) Тогда
		СтруктураПараметров.Организация = ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка");
	Иначе
		СтруктураПараметров.Организация = ПараметрОрганизация.Значение;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ПараметрПериод.Значение) Тогда
		СтруктураПараметров.ПериодОтчета = КонецМесяца(ТекущаяДатаСеанса());
	Иначе
		СтруктураПараметров.ПериодОтчета = КонецМесяца(ПараметрПериод.Значение);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ПараметрВариант.Значение) Тогда
		СтруктураПараметров.ВариантФормирования = 1;
	Иначе
		СтруктураПараметров.ВариантФормирования = ПараметрВариант.Значение;
	КонецЕсли;
	
	Возврат СтруктураПараметров;
	
КонецФункции

Функция ПолучитьДействующиеПравила(ПериодОтчета, Организация)
	
	ЗапросПравил = Новый Запрос(ТекстЗапросаДействующихПравил());
	ЗапросПравил.УстановитьПараметр("ПериодОтчета", ПериодОтчета);
	ЗапросПравил.УстановитьПараметр("Организация", Организация);
	ЗапросПравил.УстановитьПараметр("УправленческийУчетОрганизаций", РасчетСебестоимостиПовтИсп.УправленческийУчетОрганизаций(ПериодОтчета));
		
	Возврат ЗапросПравил.Выполнить();
	
КонецФункции
	
Функция ПолучитьГруппыПотенциальноПересекающихсяПравил(ПериодОтчета, Организация, ВариантФормирования)
	
	ЗапросГруппПравил = Новый Запрос;
	ТекстЗапроса = ТекстЗапросаГруппПравил();
	
	Если ВариантФормирования = 2 Тогда
		
		// При варианте формирования "Все пересечения" не следует выполнять группировку по приоритету
		
		Схема = Новый СхемаЗапроса;
		Схема.УстановитьТекстЗапроса(ТекстЗапроса);
		
		ПоляЗапроса = Схема.ПакетЗапросов[0].Операторы[0].ВыбираемыеПоля;
		ПолеПриоритет = ПоляЗапроса.Индекс(ПоляЗапроса.Найти("ПравилаФормированияРезервовПодОбесценениеЗапасов.Приоритет"));
		ПоляЗапроса.Установить(ПолеПриоритет, Новый ВыражениеСхемыЗапроса("-1")); 
		
		ТекстЗапроса = Схема.ПолучитьТекстЗапроса();
		
	КонецЕсли;
	
	ЗапросГруппПравил.Текст = ТекстЗапроса;	
	ЗапросГруппПравил.УстановитьПараметр("ПериодОтчета", ПериодОтчета);
	ЗапросГруппПравил.УстановитьПараметр("Организация", Организация);
	ЗапросГруппПравил.УстановитьПараметр("УправленческийУчетОрганизаций", РасчетСебестоимостиПовтИсп.УправленческийУчетОрганизаций(ПериодОтчета));
	
	Возврат ЗапросГруппПравил.Выполнить();
	
КонецФункции

Процедура СформироватьПустуюТаблицуДляСКД(МенеджерВременныхТаблиц)
	
	ЗапросПустойВременнойТаблицы = Новый Запрос;
	ЗапросПустойВременнойТаблицы.Текст = ТекстЗапросаПустойИтоговойТаблицы();
	ЗапросПустойВременнойТаблицы.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ЗапросПустойВременнойТаблицы.Выполнить();
		
КонецПроцедуры

Процедура СформироватьВременнуюТаблицуДляСКДБезПоискаПересечений(МенеджерВременныхТаблиц, РезультатЗапросаПоПравилам, ПериодОтчета)
	
	ВыборкаПравил = РезультатЗапросаПоПравилам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	КоличествоВременныхТаблиц = Справочники.ПравилаФормированияРезервовПодОбесценениеЗапасов.СоздатьВременныеТаблицыОстатковПоПравилам(ВыборкаПравил, ПериодОтчета, МенеджерВременныхТаблиц);
	
	ПараметрыПомещения = Новый Структура;
	ПараметрыПомещения.Вставить("ИмяВременнойТаблицы", "ТаблицаОстатковПоПравилу");
	ПараметрыПомещения.Вставить("НомерВременнойТаблицы", КоличествоВременныхТаблиц);
	Справочники.ПравилаФормированияРезервовПодОбесценениеЗапасов.ОбъединитьВременныеТаблицыОстатковВИтоговую(МенеджерВременныхТаблиц, ПараметрыПомещения);
		
КонецПроцедуры

Процедура СформироватьВременнуюТаблицуДляСКД(МенеджерВременныхТаблиц, РезультатЗапросаПоГруппамПравил, ПериодОтчета)
	
	ВыборкаГруппПравил = РезультатЗапросаПоГруппамПравил.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	НомерОбъединеннойТаблицы = 0;
	
	Пока ВыборкаГруппПравил.Следующий() Цикл
		
		// Сформируем временную таблицу пересекающихся записей в рамках группы
		// потенциально дублирующихся правил
		
		НомерОбъединеннойТаблицы = НомерОбъединеннойТаблицы + 1;
		ВыборкаПравил = ВыборкаГруппПравил.Выбрать();
		
		КоличествоВременныхТаблиц = Справочники.ПравилаФормированияРезервовПодОбесценениеЗапасов.СоздатьВременныеТаблицыОстатковПоПравилам(ВыборкаПравил, ПериодОтчета, МенеджерВременныхТаблиц);
	
		ОбъединенныйЗапрос = СформироватьОбъединенныйЗапросПоГруппеПравил(КоличествоВременныхТаблиц, НомерОбъединеннойТаблицы);
		ОбъединенныйЗапрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		ОбъединенныйЗапрос.Выполнить();
			
	КонецЦикла;
	
	// Объединим пересекающиеся записи, полученные по всем группам
	
	ПараметрыПомещения = Новый Структура;
	ПараметрыПомещения.Вставить("ИмяВременнойТаблицы", "ПересекающиесяОстатки");
	ПараметрыПомещения.Вставить("НомерВременнойТаблицы", НомерОбъединеннойТаблицы);
	Справочники.ПравилаФормированияРезервовПодОбесценениеЗапасов.ОбъединитьВременныеТаблицыОстатковВИтоговую(МенеджерВременныхТаблиц, ПараметрыПомещения);
		
КонецПроцедуры

Функция СформироватьОбъединенныйЗапросПоГруппеПравил(МаксимальныйНомерВременнойТаблицы, НомерОбъединеннойТаблицы)
	
	ОбъединенныйЗапрос = Новый Запрос;
	Схема = Новый СхемаЗапроса;
	Схема.УстановитьТекстЗапроса(ТекстЗапросаОбъединеннияПоГруппеПравил());
	
	ПоследнийПакетЗапроса = Схема.ПакетЗапросов[Схема.ПакетЗапросов.Количество() - 1];
	ПоследнийПакетЗапроса.ТаблицаДляПомещения = Справочники.ПравилаФормированияРезервовПодОбесценениеЗапасов.ИмяВременнойТаблицы("ПересекающиесяОстатки", НомерОбъединеннойТаблицы);
	
	Справочники.ПравилаФормированияРезервовПодОбесценениеЗапасов.СформироватьОператорыОбъединенияВременныхТаблиц(Схема, "ТаблицаОстатковПоПравилу", МаксимальныйНомерВременнойТаблицы); 
	
	Для НомерВременнойТаблицы = 1 По МаксимальныйНомерВременнойТаблицы Цикл

		ИмяВременнойТаблицы = Справочники.ПравилаФормированияРезервовПодОбесценениеЗапасов.ИмяВременнойТаблицы("ТаблицаОстатковПоПравилу", НомерВременнойТаблицы);
		ЗапросУничтоженияВТ = Схема.ПакетЗапросов.Добавить(Тип("ЗапросУничтоженияТаблицыСхемыЗапроса"));
		ЗапросУничтоженияВТ.ИмяТаблицы = ИмяВременнойТаблицы;
		
	КонецЦикла;
	
	ЗапросУничтоженияВТ = Схема.ПакетЗапросов.Добавить(Тип("ЗапросУничтоженияТаблицыСхемыЗапроса"));
	ЗапросУничтоженияВТ.ИмяТаблицы = "ОбъединениеТаблицОстатков";
	ЗапросУничтоженияВТ = Схема.ПакетЗапросов.Добавить(Тип("ЗапросУничтоженияТаблицыСхемыЗапроса"));
	ЗапросУничтоженияВТ.ИмяТаблицы = "ЗаписиСНесколькимиПравилами";
		
	ОбъединенныйЗапрос.Текст = Схема.ПолучитьТекстЗапроса();
	
	Возврат ОбъединенныйЗапрос;
	
КонецФункции

Функция ТекстЗапросаДействующихПравил()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПравилаФормированияРезервовПодОбесценениеЗапасов.Ссылка КАК Правило,
	|	ПравилаФормированияРезервовПодОбесценениеЗапасов.Владелец КАК Организация,
	|	ПравилаФормированияРезервовПодОбесценениеЗапасов.Приоритет КАК Приоритет,
	|	ПравилаФормированияРезервовПодОбесценениеЗапасов.РегламентированныйУчет КАК РегламентированныйУчет,
	|	(ВЫБОР
	|		КОГДА &УправленческийУчетОрганизаций
	|			ТОГДА ПравилаФормированияРезервовПодОбесценениеЗапасов.УправленческийУчет
	|		ИНАЧЕ ЛОЖЬ КОНЕЦ) КАК УправленческийУчет,
	|	ПравилаФормированияРезервовПодОбесценениеЗапасов.ХранилищеНастроекКомпоновкиДанных КАК НастройкиКомпоновкиДанных
	|ПОМЕСТИТЬ ДействующиеПравила
	|ИЗ
	|	Справочник.ПравилаФормированияРезервовПодОбесценениеЗапасов КАК ПравилаФормированияРезервовПодОбесценениеЗапасов
	|ГДЕ
	|	НЕ ПравилаФормированияРезервовПодОбесценениеЗапасов.ПометкаУдаления
	|	И ВЫБОР
	|			КОГДА ПравилаФормированияРезервовПодОбесценениеЗапасов.НачалоДействия = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ПравилаФормированияРезервовПодОбесценениеЗапасов.НачалоДействия <= КОНЕЦПЕРИОДА(&ПериодОтчета, МЕСЯЦ)
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА ПравилаФормированияРезервовПодОбесценениеЗапасов.КонецДействия = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ПравилаФормированияРезервовПодОбесценениеЗапасов.КонецДействия >= НАЧАЛОПЕРИОДА(&ПериодОтчета, МЕСЯЦ)
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА &Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ПравилаФормированияРезервовПодОбесценениеЗапасов.Владелец = &Организация
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДействующиеПравила.Организация КАК Организация,
	|	ДействующиеПравила.Приоритет КАК Приоритет,
	|	ИСТИНА КАК РегламентированныйУчет,
	|	ЛОЖЬ КАК УправленческийУчет,
	|	ДействующиеПравила.Правило КАК Правило,
	|	ДействующиеПравила.НастройкиКомпоновкиДанных КАК НастройкиКомпоновкиДанных
	|ИЗ
	|	ДействующиеПравила КАК ДействующиеПравила
	|ГДЕ
	|	ДействующиеПравила.РегламентированныйУчет
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДействующиеПравила.Организация КАК Организация,
	|	ДействующиеПравила.Приоритет КАК Приоритет,
	|	ЛОЖЬ КАК РегламентированныйУчет,
	|	ИСТИНА КАК УправленческийУчет,
	|	ДействующиеПравила.Правило КАК Правило,
	|	ДействующиеПравила.НастройкиКомпоновкиДанных КАК НастройкиКомпоновкиДанных
	|ИЗ
	|	ДействующиеПравила КАК ДействующиеПравила
	|ГДЕ
	|	ДействующиеПравила.УправленческийУчет";
	
	Возврат ТекстЗапроса
	
КонецФункции

Функция ТекстЗапросаГруппПравил()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПравилаФормированияРезервовПодОбесценениеЗапасов.Ссылка КАК Правило,
	|	ПравилаФормированияРезервовПодОбесценениеЗапасов.Владелец КАК Организация,
	|	ПравилаФормированияРезервовПодОбесценениеЗапасов.Приоритет КАК Приоритет,
	|	ПравилаФормированияРезервовПодОбесценениеЗапасов.РегламентированныйУчет КАК РегламентированныйУчет,
	|	(ВЫБОР
	|		КОГДА &УправленческийУчетОрганизаций
	|			ТОГДА ПравилаФормированияРезервовПодОбесценениеЗапасов.УправленческийУчет
	|		ИНАЧЕ ЛОЖЬ КОНЕЦ) КАК УправленческийУчет,
	|	ПравилаФормированияРезервовПодОбесценениеЗапасов.ХранилищеНастроекКомпоновкиДанных КАК НастройкиКомпоновкиДанных
	|ПОМЕСТИТЬ ДействующиеПравила
	|ИЗ
	|	Справочник.ПравилаФормированияРезервовПодОбесценениеЗапасов КАК ПравилаФормированияРезервовПодОбесценениеЗапасов
	|ГДЕ
	|	НЕ ПравилаФормированияРезервовПодОбесценениеЗапасов.ПометкаУдаления
	|	И ВЫБОР
	|			КОГДА ПравилаФормированияРезервовПодОбесценениеЗапасов.НачалоДействия = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ПравилаФормированияРезервовПодОбесценениеЗапасов.НачалоДействия <= КОНЕЦПЕРИОДА(&ПериодОтчета, МЕСЯЦ)
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА ПравилаФормированияРезервовПодОбесценениеЗапасов.КонецДействия = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ПравилаФормированияРезервовПодОбесценениеЗапасов.КонецДействия >= НАЧАЛОПЕРИОДА(&ПериодОтчета, МЕСЯЦ)
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА &Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ПравилаФормированияРезервовПодОбесценениеЗапасов.Владелец = &Организация
	|		КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Приоритет,
	|	РегламентированныйУчет,
	|	УправленческийУчет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДействующиеПравила.Организация КАК Организация,
	|	ДействующиеПравила.Приоритет КАК Приоритет,
	|	ИСТИНА КАК РегламентированныйУчет
	|ПОМЕСТИТЬ НаборыРеквизитовСНесколькимиПравилами
	|ИЗ
	|	ДействующиеПравила КАК ДействующиеПравила
	|ГДЕ
	|	ДействующиеПравила.РегламентированныйУчет
	|
	|СГРУППИРОВАТЬ ПО
	|	ДействующиеПравила.Организация,
	|	ДействующиеПравила.Приоритет
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДействующиеПравила.Правило) > 1
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДействующиеПравила.Организация,
	|	ДействующиеПравила.Приоритет,
	|	ЛОЖЬ
	|ИЗ
	|	ДействующиеПравила КАК ДействующиеПравила
	|ГДЕ
	|	ДействующиеПравила.УправленческийУчет
	|
	|СГРУППИРОВАТЬ ПО
	|	ДействующиеПравила.Организация,
	|	ДействующиеПравила.Приоритет
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДействующиеПравила.Правило) > 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НаборыРеквизитовСНесколькимиПравилами.Организация КАК Организация,
	|	НаборыРеквизитовСНесколькимиПравилами.Приоритет КАК Приоритет,
	|	НаборыРеквизитовСНесколькимиПравилами.РегламентированныйУчет КАК РегламентированныйУчет,
	|	АВТОНОМЕРЗАПИСИ() КАК ИдентификаторНабора
	|ПОМЕСТИТЬ НаборыСИдентификаторами
	|ИЗ
	|	НаборыРеквизитовСНесколькимиПравилами КАК НаборыРеквизитовСНесколькимиПравилами
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Приоритет,
	|	РегламентированныйУчет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НаборыСИдентификаторами.ИдентификаторНабора КАК ИдентификаторНабора,
	|	НаборыСИдентификаторами.Организация КАК Организация,
	|	НаборыСИдентификаторами.Приоритет КАК Приоритет,
	|	НаборыСИдентификаторами.РегламентированныйУчет КАК РегламентированныйУчет,
	|	НЕ НаборыСИдентификаторами.РегламентированныйУчет КАК УправленческийУчет,
	|	ДействующиеПравила.Правило КАК Правило,
	|	ДействующиеПравила.НастройкиКомпоновкиДанных КАК НастройкиКомпоновкиДанных
	|ИЗ
	|	ДействующиеПравила КАК ДействующиеПравила
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НаборыСИдентификаторами КАК НаборыСИдентификаторами
	|		ПО ДействующиеПравила.Организация = НаборыСИдентификаторами.Организация
	|			И ДействующиеПравила.Приоритет = НаборыСИдентификаторами.Приоритет
	|			И (ВЫБОР
	|				КОГДА НаборыСИдентификаторами.РегламентированныйУчет
	|					ТОГДА ДействующиеПравила.РегламентированныйУчет
	|				ИНАЧЕ ДействующиеПравила.УправленческийУчет
	|			КОНЕЦ)
	|ИТОГИ
	|	МАКСИМУМ(Организация),
	|	МАКСИМУМ(Приоритет),
	|	МАКСИМУМ(РегламентированныйУчет)
	|ПО
	|	ИдентификаторНабора";
	
	Возврат ТекстЗапроса
				
КонецФункции

Функция ТекстЗапросаОбъединеннияПоГруппеПравил()
	
	// Поля итоговой выборки должны совпадать с полями в:
	// - Справочник.ПравилаФормированияРезервовПодОбесценениеЗапасов.ТекстЗапросаПомещенияВоВременнуюТаблицу()
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаОстатков.Период КАК Период,
	|	ТаблицаОстатков.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаОстатков.РазделУчета КАК РазделУчета,
	|	ТаблицаОстатков.Склад КАК Склад,
	|	ТаблицаОстатков.ВидЗапасов КАК ВидЗапасов,
	|	ТаблицаОстатков.ТипЗапасов КАК ТипЗапасов,
	|	ТаблицаОстатков.Организация КАК Организация,
	|	ТаблицаОстатков.Партия КАК Партия,
	|	ТаблицаОстатков.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	ТаблицаОстатков.АналитикаФинансовогоУчета КАК АналитикаФинансовогоУчета,
	|	ТаблицаОстатков.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
	|	ЕСТЬNULL(ТаблицаОстатков.АналитикаУчетаПоПартнерам, ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка)) КАК АналитикаУчетаПоПартнерам,
	|	ТаблицаОстатков.Правило КАК Правило,
	|	ТаблицаОстатков.Приоритет КАК Приоритет,
	|	ТаблицаОстатков.ВидЦены КАК ВидЦены,
	|	ТаблицаОстатков.РегламентированныйУчет КАК РегламентированныйУчет,
	|	ТаблицаОстатков.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ТаблицаОстатков.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации,
	|	ТаблицаОстатков.РасчетСебестоимости КАК РасчетСебестоимости,
	|	ТаблицаОстатков.РезервПодОбесценениеРегл КАК РезервПодОбесценениеРегл,
	|	ТаблицаОстатков.РезервПодОбесценениеУпр КАК РезервПодОбесценениеУпр,
	|	ТаблицаОстатков.РезервПодОбесценениеРеглВыручка КАК РезервПодОбесценениеРеглВыручка,
	|	ТаблицаОстатков.РезервПодОбесценениеУпрВыручка КАК РезервПодОбесценениеУпрВыручка,
	|	ТаблицаОстатков.ЦенаУказана КАК ЦенаУказана,
	|	ТаблицаОстатков.КоличествоОстаток КАК КоличествоОстаток,
	|	ТаблицаОстатков.СебестоимостьРегл КАК СебестоимостьРегл,
	|	ТаблицаОстатков.СебестоимостьУпр КАК СебестоимостьУпр,
	|	ТаблицаОстатков.ЧистаяСтоимостьПродажиРегл КАК ЧистаяСтоимостьПродажиРегл,
	|	ТаблицаОстатков.ЧистаяСтоимостьПродажиУпр КАК ЧистаяСтоимостьПродажиУпр,
	|	ТаблицаОстатков.ВидДвижения КАК ВидДвижения	
	|ПОМЕСТИТЬ ОбъединениеТаблицОстатков
	|ИЗ
	|	ТаблицаОстатков КАК ТаблицаОстатков
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаФинансовогоУчета,
	|	АналитикаУчетаПартий,
	|	Партия,
	|	ВидДеятельностиНДС,
	|	АналитикаУчетаПоПартнерам,
	|	ВидЗапасов,
	|	АналитикаУчетаНоменклатуры,
	|	РазделУчета,
	|	ТипЗапасов,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОбъединениеТаблицОстатков.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ОбъединениеТаблицОстатков.РазделУчета КАК РазделУчета,
	|	ОбъединениеТаблицОстатков.ВидЗапасов КАК ВидЗапасов,
	|	ОбъединениеТаблицОстатков.ТипЗапасов КАК ТипЗапасов,
	|	ОбъединениеТаблицОстатков.Организация КАК Организация,
	|	ОбъединениеТаблицОстатков.Партия КАК Партия,
	|	ОбъединениеТаблицОстатков.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	ОбъединениеТаблицОстатков.АналитикаФинансовогоУчета КАК АналитикаФинансовогоУчета,
	|	ОбъединениеТаблицОстатков.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
	|	ЕСТЬNULL(ОбъединениеТаблицОстатков.АналитикаУчетаПоПартнерам, ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка)) КАК АналитикаУчетаПоПартнерам
	|ПОМЕСТИТЬ ЗаписиСНесколькимиПравилами
	|ИЗ
	|	ОбъединениеТаблицОстатков КАК ОбъединениеТаблицОстатков
	|
	|СГРУППИРОВАТЬ ПО
	|	ОбъединениеТаблицОстатков.РазделУчета,
	|	ОбъединениеТаблицОстатков.АналитикаУчетаНоменклатуры,
	|	ОбъединениеТаблицОстатков.ВидЗапасов,
	|	ОбъединениеТаблицОстатков.ТипЗапасов,
	|	ОбъединениеТаблицОстатков.АналитикаФинансовогоУчета,
	|	ОбъединениеТаблицОстатков.АналитикаУчетаПартий,
	|	ОбъединениеТаблицОстатков.ВидДеятельностиНДС,
	|	ОбъединениеТаблицОстатков.Партия,
	|	ОбъединениеТаблицОстатков.АналитикаУчетаПоПартнерам,
	|	ОбъединениеТаблицОстатков.Организация,
	|	ОбъединениеТаблицОстатков.РегламентированныйУчет
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ОбъединениеТаблицОстатков.Правило) > 1
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаПартий,
	|	Организация,
	|	Партия,
	|	АналитикаФинансовогоУчета,
	|	РазделУчета,
	|	ВидЗапасов,
	|	ВидДеятельностиНДС,
	|	ТипЗапасов,
	|	АналитикаУчетаПоПартнерам,
	|	АналитикаУчетаНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОбъединениеТаблицОстатков.Период КАК Период,
	|	ОбъединениеТаблицОстатков.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ОбъединениеТаблицОстатков.РазделУчета КАК РазделУчета,
	|	ОбъединениеТаблицОстатков.Склад КАК Склад,
	|	ОбъединениеТаблицОстатков.ВидЗапасов КАК ВидЗапасов,
	|	ОбъединениеТаблицОстатков.ТипЗапасов КАК ТипЗапасов,
	|	ОбъединениеТаблицОстатков.Организация КАК Организация,
	|	ОбъединениеТаблицОстатков.Партия КАК Партия,
	|	ОбъединениеТаблицОстатков.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	ОбъединениеТаблицОстатков.АналитикаФинансовогоУчета КАК АналитикаФинансовогоУчета,
	|	ОбъединениеТаблицОстатков.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
	|	ОбъединениеТаблицОстатков.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ОбъединениеТаблицОстатков.Правило КАК Правило,
	|	ОбъединениеТаблицОстатков.Приоритет КАК Приоритет,
	|	ОбъединениеТаблицОстатков.ВидЦены КАК ВидЦены,
	|	ОбъединениеТаблицОстатков.РегламентированныйУчет КАК РегламентированныйУчет,
	|	ОбъединениеТаблицОстатков.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ОбъединениеТаблицОстатков.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации,
	|	ОбъединениеТаблицОстатков.РасчетСебестоимости КАК РасчетСебестоимости,
	|	ОбъединениеТаблицОстатков.РезервПодОбесценениеРегл КАК РезервПодОбесценениеРегл,
	|	ОбъединениеТаблицОстатков.РезервПодОбесценениеУпр КАК РезервПодОбесценениеУпр,
	|	ОбъединениеТаблицОстатков.РезервПодОбесценениеРеглВыручка КАК РезервПодОбесценениеРеглВыручка,
	|	ОбъединениеТаблицОстатков.РезервПодОбесценениеУпрВыручка КАК РезервПодОбесценениеУпрВыручка,
	|	ОбъединениеТаблицОстатков.ЦенаУказана КАК ЦенаУказана,
	|	ОбъединениеТаблицОстатков.КоличествоОстаток КАК КоличествоОстаток,
	|	ОбъединениеТаблицОстатков.СебестоимостьРегл КАК СебестоимостьРегл,
	|	ОбъединениеТаблицОстатков.СебестоимостьУпр КАК СебестоимостьУпр,
	|	ОбъединениеТаблицОстатков.ЧистаяСтоимостьПродажиРегл КАК ЧистаяСтоимостьПродажиРегл,
	|	ОбъединениеТаблицОстатков.ЧистаяСтоимостьПродажиУпр КАК ЧистаяСтоимостьПродажиУпр,
	|	ОбъединениеТаблицОстатков.ВидДвижения КАК ВидДвижения
	|ПОМЕСТИТЬ ПересекающиесяОстатки
	|ИЗ
	|	ОбъединениеТаблицОстатков КАК ОбъединениеТаблицОстатков
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЗаписиСНесколькимиПравилами КАК ЗаписиСНесколькимиПравилами
	|		ПО ОбъединениеТаблицОстатков.АналитикаУчетаНоменклатуры = ЗаписиСНесколькимиПравилами.АналитикаУчетаНоменклатуры
	|			И ОбъединениеТаблицОстатков.РазделУчета = ЗаписиСНесколькимиПравилами.РазделУчета
	|			И ОбъединениеТаблицОстатков.ВидЗапасов = ЗаписиСНесколькимиПравилами.ВидЗапасов
	|			И ОбъединениеТаблицОстатков.ТипЗапасов = ЗаписиСНесколькимиПравилами.ТипЗапасов
	|			И ОбъединениеТаблицОстатков.Организация = ЗаписиСНесколькимиПравилами.Организация
	|			И ОбъединениеТаблицОстатков.Партия = ЗаписиСНесколькимиПравилами.Партия
	|			И ОбъединениеТаблицОстатков.АналитикаУчетаПартий = ЗаписиСНесколькимиПравилами.АналитикаУчетаПартий
	|			И ОбъединениеТаблицОстатков.АналитикаФинансовогоУчета = ЗаписиСНесколькимиПравилами.АналитикаФинансовогоУчета
	|			И ОбъединениеТаблицОстатков.ВидДеятельностиНДС = ЗаписиСНесколькимиПравилами.ВидДеятельностиНДС
	|			И ОбъединениеТаблицОстатков.АналитикаУчетаПоПартнерам = ЗаписиСНесколькимиПравилами.АналитикаУчетаПоПартнерам
	|";
	
	Возврат ТекстЗапроса
				
КонецФункции

Функция ТекстЗапросаПустойИтоговойТаблицы()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК АналитикаУчетаНоменклатуры,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
	|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК ВидЗапасов,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПустаяСсылка) КАК ТипЗапасов,
	|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК Партия,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаФинансовогоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК ВидДеятельностиНДС,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка) КАК АналитикаУчетаПоПартнерам,
	|	ЗНАЧЕНИЕ(Справочник.ПравилаФормированияРезервовПодОбесценениеЗапасов.ПустаяСсылка) КАК Правило,
	|	0 КАК Приоритет,
	|	ЗНАЧЕНИЕ(Справочник.ВидыЦен.ПустаяСсылка) КАК ВидЦены,
	|	ЛОЖЬ КАК РегламентированныйУчет
	|ПОМЕСТИТЬ ВременнаяТаблицаОстатков
	|ГДЕ
	|	ЛОЖЬ";
	
	Возврат ТекстЗапроса
				
КонецФункции

#КонецОбласти

#КонецЕсли