#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	НастройкиОсновнойСхемы = КомпоновщикНастроек.ПолучитьНастройки();
	ЗначенияОтбораДанных = ПолучитьЗначенияОтбораДанных(НастройкиОсновнойСхемы);
	
	ТекстСообщения = НСтр("ru = 'Отчет не поддерживает получение данных до даты начала учета внеоборотных активов версии 2.4 - %1.
                           |Необходимо воспользоваться отчетом ""Регламентированный учет"" - ""Справки-расчеты"" - ""Справка-расчет амортизации"".';
                           |en = 'Report does not support data retrieval prior to the commencement date of accounting of non-current assets 2.4 -%1.
                           | You need to use ""Local accounting"" report - ""Detailed calculations"" - ""Detailed calculation of depreciation"".'");

	Если НЕ ЗначениеЗаполнено(ЗначенияОтбораДанных.ДокументРасчетаАмортизации) Тогда
		ВнеоборотныеАктивыСлужебный.ПроверитьПериодОтчетаВерсии24(
			ЗначенияОтбораДанных.НачалоПериода, 
			ЗначенияОтбораДанных.ОкончаниеПериода, 
			ТекстСообщения,
			Отказ); 
	КонецЕсли; 
		
	Если НЕ ЗначенияОтбораДанных.ПоказатьРасчетБУ
		И НЕ ЗначенияОтбораДанных.ПоказатьРасчетНУ
		И НЕ ЗначенияОтбораДанных.ПоказатьРасчетУУ Тогда
		ТекстСообщения = НСтр("ru = 'Необходимо выбрать какие данные нужно получить: БУ, НУ, УУ.';
								|en = 'You need to choose what data you want to get: AC, TA, MA.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,, Отказ); 
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НастройкиОсновнойСхемы = КомпоновщикНастроек.ПолучитьНастройки();
	
	УстановитьТекстЗапроса(СхемаКомпоновкиДанных);
	
	ЗначенияОтбораДанных = ПолучитьЗначенияОтбораДанных(НастройкиОсновнойСхемы);
	
	ОбесценениеВНАСервер.УстановитьВидимостьДанныхОбесцененияВОтчете(НастройкиОсновнойСхемы);
	
	УстановитьПараметрыОтчета(ЗначенияОтбораДанных, НастройкиОсновнойСхемы);
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОсновнойСхемы, ДанныеРасшифровки);	
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, Неопределено, ДанныеРасшифровки, Истина);
	
	ПроцессорВыводаВТабличныйДокумент = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВыводаВТабличныйДокумент.УстановитьДокумент(ДокументРезультат);	
	ПроцессорВыводаВТабличныйДокумент.Вывести(ПроцессорКомпоновкиДанных);
	
	ДобавитьИнформационноеСообщениеВШапку(ДокументРезультат, ЗначенияОтбораДанных);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - См. ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	Настройки.События.ПриСозданииНаСервере = Истина;
	Настройки.События.ПриЗагрузкеВариантаНаСервере = Истина;
КонецПроцедуры

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//   ЭтаФорма - ФормаКлиентскогоПриложения - Форма отчета:
//   	* Параметры - Структура - может содержать свойства:
//				** ПараметрКоманды - Произвольный -
//				** ОписаниеКоманды - Структура - 
//   Отказ - Булево - Передается из параметров обработчика "как есть".
//   СтандартнаяОбработка - Булево - Передается из параметров обработчика "как есть".
//
// См. также:
//   "ФормаКлиентскогоПриложения.ПриСозданииНаСервере" в синтакс-помощнике.
//
Процедура ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт
	
	Параметры = ЭтаФорма.Параметры;
	
	Если Параметры.Свойство("ПараметрКоманды")
		И Параметры.Свойство("ОписаниеКоманды")
		И Параметры.ОписаниеКоманды.Свойство("ДополнительныеПараметры") Тогда 
		
		ЭтаФорма.ФормаПараметры.КлючНазначенияИспользования = Параметры.ОписаниеКоманды.ДополнительныеПараметры.ИмяКоманды;
		Параметры.КлючНазначенияИспользования = Параметры.ОписаниеКоманды.ДополнительныеПараметры.ИмяКоманды;
		
		Если Параметры.ОписаниеКоманды.ДополнительныеПараметры.ИмяКоманды = "СправкаРасчетПоОС" Тогда
			ЭтаФорма.ФормаПараметры.Отбор.Вставить("ОсновноеСредство", Параметры.ПараметрКоманды);
			Параметры.КлючВарианта = "СправкаРасчетАмортизацииОС";
		ИначеЕсли Параметры.ОписаниеКоманды.ДополнительныеПараметры.ИмяКоманды = "СправкаРасчетПоДокументуАмортизацииОС" Тогда
			ЭтаФорма.ФормаПараметры.Отбор.Вставить("ДокументРасчетаАмортизации", Параметры.ПараметрКоманды);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   НовыеНастройкиКД - НастройкиКомпоновкиДанных - Настройки для загрузки в компоновщик настроек.
//
// См. синтакс-помощник "Расширение управляемой формы для отчета.ПриЗагрузкеВариантаНаСервере" в синтакс-помощнике.
//
Процедура ПриЗагрузкеВариантаНаСервере(Форма, НовыеНастройкиКД) Экспорт
	
	Отчет = Форма.Отчет;
	КомпоновщикНастроекФормы = Отчет.КомпоновщикНастроек; // КомпоновщикНастроекКомпоновкиДанных -
	
	УстановитьПериодОтчета(Форма, КомпоновщикНастроекФормы);
	
	НовыеНастройкиКД = КомпоновщикНастроекФормы.Настройки;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьЗначенияОтбораДанных(НастройкиОсновнойСхемы)

	ЭлементыОтбора = НастройкиОсновнойСхемы.Отбор.Элементы;
	
	ЗначенияОтбораДанных = Новый Структура;
	
	ЗначенияОтбораДанных.Вставить("СписокОрганизаций", ОтчетыУТСервер.ЗначениеОтбора("Организация", ЭлементыОтбора));
	Если ЗначенияОтбораДанных.СписокОрганизаций.Количество() = 0 Тогда
		ЗначенияОтбораДанных.СписокОрганизаций = ВнеоборотныеАктивыСлужебный.СписокРазрешенныхОрганизаций();
	КонецЕсли;
	
	ЗначенияОтбораДанных.Вставить("СписокОС", ОтчетыУТСервер.ЗначениеОтбора("ОсновноеСредство", ЭлементыОтбора));
	ЗначенияОтбораДанных.Вставить("ДокументРасчетаАмортизации", ОтчетыУТСервер.ЗначениеОтбора("ДокументРасчетаАмортизации", ЭлементыОтбора));
	
	ОтчетыУТСервер.ПериодОтчета(НастройкиОсновнойСхемы, ЗначенияОтбораДанных);
	
	ПоказатьРасчетБУ = ОтчетыУТСервер.ЗначениеПараметра(НастройкиОсновнойСхемы, "ПоказатьРасчетБУ", Ложь);
	ПоказатьРасчетНУ = ОтчетыУТСервер.ЗначениеПараметра(НастройкиОсновнойСхемы, "ПоказатьРасчетНУ", Ложь);
	ПоказатьРасчетУУ = ОтчетыУТСервер.ЗначениеПараметра(НастройкиОсновнойСхемы, "ПоказатьРасчетУУ", Ложь);
	
	ЗначенияОтбораДанных.Вставить("ПоказатьРасчетБУ", ПоказатьРасчетБУ);
	ЗначенияОтбораДанных.Вставить("ПоказатьРасчетНУ", ПоказатьРасчетНУ);
	ЗначенияОтбораДанных.Вставить("ПоказатьРасчетУУ", ПоказатьРасчетУУ);
	
	Возврат ЗначенияОтбораДанных;
	
КонецФункции

Процедура УстановитьПараметрыОтчета(ЗначенияОтбораДанных, НастройкиОсновнойСхемы)

	Если ЗначениеЗаполнено(ЗначенияОтбораДанных.НачалоПериода) Тогда
		НачалоГода = НачалоГода(ЗначенияОтбораДанных.НачалоПериода);
	Иначе
		НачалоГода = '000101010000';
	КонецЕсли;
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(НастройкиОсновнойСхемы, "НачалоГода", НачалоГода);
	
	Если ЗначениеЗаполнено(ЗначенияОтбораДанных.ОкончаниеПериода) Тогда
		КонецГода = КонецГода(ЗначенияОтбораДанных.ОкончаниеПериода);
	Иначе
		КонецГода = '000101010000';
	КонецЕсли;
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(НастройкиОсновнойСхемы, "КонецГода", КонецГода);
	
КонецПроцедуры

Процедура УстановитьТекстЗапроса(СхемаКомпоновкиДанных)

	СписокЗапросов = Новый Массив;
	
	#Область ВТ_Амортизация
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВЫБОР
	|		КОГДА ТабАмортизация.ПериодНачисления <> ДАТАВРЕМЯ(1,1,1)
	|			ТОГДА ТабАмортизация.ПериодНачисления
	|		ИНАЧЕ ТабАмортизация.Период
	|	КОНЕЦ КАК Период,
	|
	|	ТабАмортизация.Период КАК ПериодРасчета,
	|
	|	ВЫБОР
	|		КОГДА ТабАмортизация.ПериодНачисления <> ДАТАВРЕМЯ(1,1,1)
	|			ТОГДА НАЧАЛОПЕРИОДА(ТабАмортизация.ПериодНачисления, ГОД)
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(ТабАмортизация.Период, ГОД)
	|	КОНЕЦ КАК НачалоГода,
	|
	|	ВЫБОР
	|		КОГДА ТабАмортизация.ПериодНачисления <> ДАТАВРЕМЯ(1,1,1)
	|			ТОГДА НАЧАЛОПЕРИОДА(ТабАмортизация.ПериодНачисления, МЕСЯЦ)
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(ТабАмортизация.Период, МЕСЯЦ)
	|	КОНЕЦ КАК НачалоМесяца,
	|
	|	ТабАмортизация.Организация КАК Организация,
	|	ТабАмортизация.Организация.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ТабАмортизация.ОсновноеСредство КАК ОсновноеСредство,
	|
	|	ТабАмортизация.Амортизация КАК АмортизацияУУ,
	|	ТабАмортизация.АмортизацияРегл + ТабАмортизация.АмортизацияЦФ КАК АмортизацияБУ,
	|	ТабАмортизация.АмортизацияНУ + ТабАмортизация.АмортизацияНУЦФ КАК АмортизацияНУ,
	|	ТабАмортизация.АмортизацияПР + ТабАмортизация.АмортизацияПРЦФ КАК АмортизацияПР,
	|	ТабАмортизация.АмортизацияВР + ТабАмортизация.АмортизацияВРЦФ КАК АмортизацияВР,
	|
	|	ТабАмортизация.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериодУУ,
	|	ТабАмортизация.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериодУУ,
	|	ТабАмортизация.РесурсВыработанныйЗаПрошлыйПериодБУ КАК РесурсВыработанныйЗаПрошлыйПериодБУ,
	|	ТабАмортизация.РесурсВыработанныйЗаТекущийПериодБУ КАК РесурсВыработанныйЗаТекущийПериодБУ,
	|
	|	ТабАмортизация.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизацииУУ,
	|	ТабАмортизация.СтоимостьДляВычисленияАмортизацииРегл КАК СтоимостьДляВычисленияАмортизацииБУ,
	|
	|	ВЫБОР
	|		КОГДА ТабАмортизация.ПорядокУчетаУУ = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПустаяСсылка)
	|				И ТабАмортизация.Амортизация <> 0
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)
	|		ИНАЧЕ ТабАмортизация.ПорядокУчетаУУ 
	|	КОНЕЦ КАК ПорядокУчетаУУ,
	|
	|	ВЫБОР
	|		КОГДА ТабАмортизация.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПустаяСсылка)
	|				И (ТабАмортизация.АмортизацияРегл + ТабАмортизация.АмортизацияЦФ) <> 0
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамРегл)
	|		ИНАЧЕ ТабАмортизация.ПорядокУчетаБУ 
	|	КОНЕЦ КАК ПорядокУчетаБУ,
	|
	|	ВЫБОР
	|		КОГДА ТабАмортизация.ПорядокНачисленияАмортизацииУУ = ЗНАЧЕНИЕ(Перечисление.ПорядокНачисленияАмортизации.ПустаяСсылка)
	|				И ТабАмортизация.Амортизация <> 0
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокНачисленияАмортизации.СоСледующегоМесяца)
	|		ИНАЧЕ ТабАмортизация.ПорядокНачисленияАмортизацииУУ 
	|	КОНЕЦ КАК ПорядокНачисленияАмортизацииУУ,
	|
	|	ВЫБОР
	|		КОГДА ТабАмортизация.ПорядокНачисленияАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокНачисленияАмортизации.ПустаяСсылка)
	|				И (ТабАмортизация.АмортизацияРегл + ТабАмортизация.АмортизацияЦФ) <> 0
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокНачисленияАмортизации.СоСледующегоМесяца)
	|		ИНАЧЕ ТабАмортизация.ПорядокНачисленияАмортизацииБУ 
	|	КОНЕЦ КАК ПорядокНачисленияАмортизацииБУ,
	|
	|	ТабАмортизация.ЭтоДоначисление КАК ЭтоДоначислениеУУ,
	|	ТабАмортизация.ЭтоДоначислениеБУ КАК ЭтоДоначислениеБУ
	|
	|ПОМЕСТИТЬ ВТ_Амортизация
	|
	|ИЗ
	|	РегистрНакопления.АмортизацияОС КАК ТабАмортизация
	|
	|ГДЕ
	|	(ТабАмортизация.Период >= &НачалоПериода
	|			ИЛИ &НачалоПериода = ДАТАВРЕМЯ(1, 1, 1))
	|	И (ТабАмортизация.Период <= &КонецПериода
	|			ИЛИ &КонецПериода = ДАТАВРЕМЯ(1, 1, 1))
	|	И ТабАмортизация.Регистратор ССЫЛКА Документ.АмортизацияОС2_4
	|	И ТабАмортизация.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|
	|{ГДЕ
	|	ТабАмортизация.Организация.* КАК Организация,
	|	ТабАмортизация.ОсновноеСредство.* КАК ОсновноеСредство,
	|	(ВЫРАЗИТЬ(ТабАмортизация.Регистратор КАК Документ.АмортизацияОС2_4)) КАК ДокументРасчетаАмортизации,
	|	ТабАмортизация.Амортизация КАК АмортизацияУУ,
	|	(ТабАмортизация.АмортизацияРегл + ТабАмортизация.АмортизацияЦФ) КАК АмортизацияБУ,
	|	(ТабАмортизация.АмортизацияНУ + ТабАмортизация.АмортизацияНУЦФ) КАК АмортизацияНУ,
	|	(ТабАмортизация.АмортизацияПР + ТабАмортизация.АмортизацияПРЦФ) КАК АмортизацияПР,
	|	(ТабАмортизация.АмортизацияВР + ТабАмортизация.АмортизацияВРЦФ) КАК АмортизацияВР}
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	ОсновноеСредство,
	|	Организация";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область УчетнаяПолитика
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_Амортизация.Период КАК Период,
	|	ВТ_Амортизация.Организация КАК Организация,
	|	МАКСИМУМ(ТаблицаПоследнийПериод.Период) КАК ПериодСрезаПоследних
	|
	|ПОМЕСТИТЬ УчетнаяПолитикаБУ_СрезПоследних
	|
	|ИЗ
	|	ВТ_Амортизация КАК ВТ_Амортизация
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаБухУчета КАК ТаблицаПоследнийПериод
	|		ПО (ТаблицаПоследнийПериод.Организация = ВТ_Амортизация.Организация)
	|			И (ТаблицаПоследнийПериод.Период <= ВТ_Амортизация.Период)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Амортизация.Период,
	|	ВТ_Амортизация.Организация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСрезПоследних.Период КАК Период,
	|	ТаблицаСрезПоследних.Организация КАК Организация,
	|	ТаблицаРегистраНаДату.ПорядокНачисленияАмортизацииОС КАК ПорядокНачисленияАмортизации
	|
	|ПОМЕСТИТЬ УчетнаяПолитикаБУ
	|
	|ИЗ
	|	УчетнаяПолитикаБУ_СрезПоследних КАК ТаблицаСрезПоследних
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаБухУчета КАК ТаблицаРегистраНаДату
	|		ПО (ТаблицаРегистраНаДату.Организация = ТаблицаСрезПоследних.Организация)
	|			И (ТаблицаРегистраНаДату.Период = ТаблицаСрезПоследних.ПериодСрезаПоследних)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ УчетнаяПолитикаБУ_СрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Амортизация.Период КАК Период,
	|	ВТ_Амортизация.Организация КАК Организация,
	|	МАКСИМУМ(ТаблицаПоследнийПериод.Период) КАК ПериодСрезаПоследних
	|
	|ПОМЕСТИТЬ УчетнаяПолитикаУУ_СрезПоследних
	|
	|ИЗ
	|	ВТ_Амортизация КАК ВТ_Амортизация
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета КАК ТаблицаПоследнийПериод
	|		ПО (ТаблицаПоследнийПериод.Организация = ВТ_Амортизация.Организация)
	|			И (ТаблицаПоследнийПериод.Период <= ВТ_Амортизация.Период)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Амортизация.Период,
	|	ВТ_Амортизация.Организация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСрезПоследних.Период КАК Период,
	|	ТаблицаСрезПоследних.Организация КАК Организация,
	|	ТаблицаРегистраНаДату.ПорядокНачисленияАмортизацииОС КАК ПорядокНачисленияАмортизации
	|
	|ПОМЕСТИТЬ УчетнаяПолитикаУУ
	|
	|ИЗ
	|	УчетнаяПолитикаУУ_СрезПоследних КАК ТаблицаСрезПоследних
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета КАК ТаблицаРегистраНаДату
	|		ПО (ТаблицаРегистраНаДату.Организация = ТаблицаСрезПоследних.Организация)
	|			И (ТаблицаРегистраНаДату.Период = ТаблицаСрезПоследних.ПериодСрезаПоследних)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ УчетнаяПолитикаУУ_СрезПоследних";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ПериодыПараметровБУ
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫБОР
	|		КОГДА (Амортизация.ПорядокНачисленияАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокНачисленияАмортизации.СДатыПринятияКУчету)
	|				ИЛИ Амортизация.ПорядокНачисленияАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокНачисленияАмортизации.ПустаяСсылка)
	|					 И ЕСТЬNULL(УчетнаяПолитикаБУ.ПорядокНачисленияАмортизации, ЗНАЧЕНИЕ(Перечисление.ПорядокНачисленияАмортизации.СДатыПринятияКУчету)) = ЗНАЧЕНИЕ(Перечисление.ПорядокНачисленияАмортизации.СДатыПринятияКУчету))
	|			ТОГДА Амортизация.ПериодРасчета
	|		ИНАЧЕ КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(Амортизация.Период, МЕСЯЦ, -1), МЕСЯЦ)
	|	КОНЕЦ КАК ПериодПараметров,
	|
	|	КОНЕЦПЕРИОДА(Амортизация.ПериодРасчета, МЕСЯЦ) КАК КонецПериодаПараметров,
	|
	|	Амортизация.Период КАК Период,
	|	Амортизация.Организация КАК Организация,
	|	Амортизация.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	Амортизация.ОсновноеСредство КАК ОсновноеСредство
	|
	|ПОМЕСТИТЬ ПериодыПараметровБУ
	|
	|ИЗ
	|	ВТ_Амортизация КАК Амортизация
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ УчетнаяПолитикаБУ КАК УчетнаяПолитикаБУ
	|		ПО УчетнаяПолитикаБУ.Организация = Амортизация.Организация
	|			И УчетнаяПолитикаБУ.Период = Амортизация.Период
	|ГДЕ
	|	(Амортизация.АмортизацияБУ <> 0
	|			ИЛИ Амортизация.АмортизацияНУ <> 0
	|			ИЛИ Амортизация.АмортизацияПР <> 0
	|			ИЛИ Амортизация.АмортизацияВР <> 0)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Организация,
	|	ГоловнаяОрганизация,
	|	ПериодПараметров";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область ПериодыПараметровУУ
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫБОР
	|		КОГДА (Амортизация.ПорядокНачисленияАмортизацииУУ = ЗНАЧЕНИЕ(Перечисление.ПорядокНачисленияАмортизации.СДатыПринятияКУчету)
	|				ИЛИ Амортизация.ПорядокНачисленияАмортизацииУУ = ЗНАЧЕНИЕ(Перечисление.ПорядокНачисленияАмортизации.ПустаяСсылка)
	|						И ЕСТЬNULL(УчетнаяПолитика.ПорядокНачисленияАмортизации, ЗНАЧЕНИЕ(Перечисление.ПорядокНачисленияАмортизации.СДатыПринятияКУчету)) = ЗНАЧЕНИЕ(Перечисление.ПорядокНачисленияАмортизации.СДатыПринятияКУчету))
	|			ТОГДА Амортизация.ПериодРасчета
	|		ИНАЧЕ КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(Амортизация.Период, МЕСЯЦ, -1), МЕСЯЦ)
	|	КОНЕЦ КАК ПериодПараметров,
	|
	|	КОНЕЦПЕРИОДА(Амортизация.ПериодРасчета, МЕСЯЦ) КАК КонецПериодаПараметров,
	|	Амортизация.Период КАК Период,
	|	Амортизация.Организация КАК Организация,
	|	Амортизация.ОсновноеСредство КАК ОсновноеСредство
	|
	|ПОМЕСТИТЬ ПериодыПараметровУУ
	|
	|ИЗ
	|	ВТ_Амортизация КАК Амортизация
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ УчетнаяПолитикаУУ КАК УчетнаяПолитика
	|		ПО УчетнаяПолитика.Организация = Амортизация.Организация
	|			И УчетнаяПолитика.Период = Амортизация.Период
	|ГДЕ
	|	Амортизация.АмортизацияУУ <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Организация,
	|	ПериодПараметров";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ПериодыПараметровНаНачалоМесяца
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(Амортизация.Период, МЕСЯЦ, -1), МЕСЯЦ) КАК ПериодПараметров,
	|	КОНЕЦПЕРИОДА(Амортизация.ПериодРасчета, МЕСЯЦ) КАК КонецПериодаПараметров,
	|	Амортизация.Период КАК Период,
	|	Амортизация.Организация КАК Организация,
	|	Амортизация.ОсновноеСредство КАК ОсновноеСредство
	|
	|ПОМЕСТИТЬ ПериодыПараметровНаНачалоМесяца
	|
	|ИЗ
	|	ВТ_Амортизация КАК Амортизация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Организация,
	|	ПериодПараметров";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область ПараметрыРасчета
	
	// УУ
	ВнеоборотныеАктивыСлужебный.ДобавитьТекстЗапросаТаблицыСправкиРасчетАмортизации(
		"ПорядокУчетаОС", "ПорядокУчетаУпр", "ПериодыПараметровУУ", "ОсновноеСредство", СписокЗапросов);
	
	ВнеоборотныеАктивыСлужебный.ДобавитьТекстЗапросаТаблицыСправкиРасчетАмортизации(
		"ПараметрыАмортизацииОСУУ", "ПараметрыАмортизацииОСУУ", "ПериодыПараметровУУ", "ОсновноеСредство", СписокЗапросов);
		
	// БУ
	ВнеоборотныеАктивыСлужебный.ДобавитьТекстЗапросаТаблицыСправкиРасчетАмортизации(
		"ПервоначальныеСведенияОС", "ПервоначальныеСведенияБУ", "ПериодыПараметровБУ", "ОсновноеСредство", СписокЗапросов);
	
	ВнеоборотныеАктивыСлужебный.ДобавитьТекстЗапросаТаблицыСправкиРасчетАмортизации(
		"ПорядокУчетаОС", "ПорядокУчетаРегл", "ПериодыПараметровБУ", "ОсновноеСредство", СписокЗапросов);
		
	ВнеоборотныеАктивыСлужебный.ДобавитьТекстЗапросаТаблицыСправкиРасчетАмортизации(
		"ПараметрыАмортизацииОСБУ", "ПараметрыАмортизацииБУ", "ПериодыПараметровБУ", "ОсновноеСредство", СписокЗапросов);
		
	// НУ
	ВнеоборотныеАктивыСлужебный.ДобавитьТекстЗапросаТаблицыСправкиРасчетАмортизации(
		"ПервоначальныеСведенияОС", "ПервоначальныеСведенияНУ", "ПериодыПараметровНаНачалоМесяца", "ОсновноеСредство", СписокЗапросов);
	
	ВнеоборотныеАктивыСлужебный.ДобавитьТекстЗапросаТаблицыСправкиРасчетАмортизации(
		"ПорядокУчетаОСБУ", "ПорядокУчетаБУ_НУ", "ПериодыПараметровНаНачалоМесяца", "ОсновноеСредство", СписокЗапросов);
	
	ВнеоборотныеАктивыСлужебный.ДобавитьТекстЗапросаТаблицыСправкиРасчетАмортизации(
		"ПараметрыАмортизацииОСБУ", "ПараметрыАмортизацииБУ_НУ", "ПериодыПараметровНаНачалоМесяца", "ОсновноеСредство", СписокЗапросов);
		
	#КонецОбласти

	#Область ГрафикиАмортизацииОС
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ПараметрыАмортизацииБУ.Период КАК Период,
	|	ПараметрыАмортизацииБУ.Организация КАК Организация,
	|	ПараметрыАмортизацииБУ.ОсновноеСредство КАК ОсновноеСредство,
	|	ПараметрыАмортизацииБУ.ГрафикАмортизации КАК ГрафикАмортизации,
	|
	|	&КоэффициентПериода КАК КоэффициентПериода
	|
	|ПОМЕСТИТЬ ГрафикиАмортизацииОС
	|
	|ИЗ
	|	ПараметрыАмортизацииБУ КАК ПараметрыАмортизацииБУ
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ГодовыеГрафикиАмортизацииОС КАК ГодовыеГрафикиАмортизацииОС
	|		ПО ГодовыеГрафикиАмортизацииОС.Ссылка = ПараметрыАмортизацииБУ.ГрафикАмортизации
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	ОсновноеСредство,
	|	Организация";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&КоэффициентПериода", ВнеоборотныеАктивыСлужебный.ТекстПоляКоэффициентПериода("ПараметрыАмортизацииБУ.Период"));
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти

	#Область ВТ_НастройкиУчетаНалогаНаПрибыль
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПериодыПараметров.Период КАК Период,
	|	ПериодыПараметров.ПериодПараметров КАК ПериодПараметров,
	|	ПериодыПараметров.Организация КАК Организация,
	|	ПериодыПараметров.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	МАКСИМУМ(ТаблицаПоследнийПериод.Период) КАК ПериодСрезаПоследних
	|
	|ПОМЕСТИТЬ НастройкиУчетаНалогаНаПрибыль_СрезПоследних
	|
	|ИЗ
	|	ПериодыПараметровБУ КАК ПериодыПараметров
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиУчетаНалогаНаПрибыль КАК ТаблицаПоследнийПериод
	|		ПО (ТаблицаПоследнийПериод.Организация = ПериодыПараметров.ГоловнаяОрганизация)
	|			И (ТаблицаПоследнийПериод.Период <= ПериодыПараметров.ПериодПараметров)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПериодыПараметров.Период,
	|	ПериодыПараметров.ПериодПараметров,
	|	ПериодыПараметров.Организация,
	|	ПериодыПараметров.ГоловнаяОрганизация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ГоловнаяОрганизация,
	|	ПериодСрезаПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСрезПоследних.Период КАК Период,
	|	ТаблицаСрезПоследних.Организация КАК Организация,
	|	ТаблицаРегистраНаДату.МетодНачисленияАмортизацииНУ КАК МетодНачисленияАмортизацииНУ
	|
	|ПОМЕСТИТЬ ВТ_НастройкиУчетаНалогаНаПрибыль
	|
	|ИЗ
	|	НастройкиУчетаНалогаНаПрибыль_СрезПоследних КАК ТаблицаСрезПоследних
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиУчетаНалогаНаПрибыль КАК ТаблицаРегистраНаДату
	|		ПО (ТаблицаРегистраНаДату.Организация = ТаблицаСрезПоследних.ГоловнаяОрганизация)
	|			И (ТаблицаРегистраНаДату.Период = ТаблицаСрезПоследних.ПериодСрезаПоследних)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Период;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ НастройкиУчетаНалогаНаПрибыль_СрезПоследних";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область СтоимостьАмортизация_НаНачалоГода
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СписокОС.ОсновноеСредство КАК ОсновноеСредство
	|
	|ПОМЕСТИТЬ втСписокОСУменьшаемогоОстатка
	|
	|ИЗ
	|	ПервоначальныеСведенияБУ КАК СписокОС
	|
	|ГДЕ
	|	СписокОС.МетодНачисленияАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	СписокОС.ОсновноеСредство
	|ИЗ
	|	ПараметрыАмортизацииОСУУ КАК СписокОС
	|ГДЕ
	|	СписокОС.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(СтоимостьОС.Период, ГОД) КАК НачалоГода,
	|	СтоимостьОС.Организация КАК Организация,
	|	СтоимостьОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СтоимостьОС.СтоимостьНачальныйОстаток КАК Стоимость,
	|	СтоимостьОС.СтоимостьРеглНачальныйОстаток + СтоимостьОС.СтоимостьЦФНачальныйОстаток КАК СтоимостьРегл
	|
	|ПОМЕСТИТЬ ВТ_СтоимостьОС_НаНачалоГода
	|
	|ИЗ
	|	РегистрНакопления.СтоимостьОС.ОстаткиИОбороты(
	|			&НачалоГода {(&НачалоГода)},
	|			&КонецГода {(&КонецГода)},
	|			Год,
	|			,
	|			ОсновноеСредство В
	|				(ВЫБРАТЬ
	|					СписокОС.ОсновноеСредство
	|				ИЗ
	|					втСписокОСУменьшаемогоОстатка КАК СписокОС)) КАК СтоимостьОС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Организация,
	|	НачалоГода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(АмортизацияОС.Период, ГОД) КАК НачалоГода,
	|	АмортизацияОС.Организация КАК Организация,
	|	АмортизацияОС.ОсновноеСредство КАК ОсновноеСредство,
	|	АмортизацияОС.АмортизацияНачальныйОстаток КАК АмортизацияУУ,
	|	-(АмортизацияОС.АмортизацияРеглНачальныйОстаток + АмортизацияОС.АмортизацияЦФНачальныйОстаток) КАК АмортизацияРегл
	|
	|ПОМЕСТИТЬ ВТ_АмортизацияОС_НаНачалоГода
	|
	|ИЗ
	|	РегистрНакопления.АмортизацияОС.ОстаткиИОбороты(
	|			&НачалоГода {(&НачалоГода)},
	|			&КонецГода {(&КонецГода)},
	|			Год,
	|			,
	|			ОсновноеСредство В
	|				(ВЫБРАТЬ
	|					СписокОС.ОсновноеСредство
	|				ИЗ
	|					втСписокОСУменьшаемогоОстатка КАК СписокОС)) КАК АмортизацияОС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Организация,
	|	НачалоГода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втСписокОСУменьшаемогоОстатка";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область СтоимостьАмортизация_НаНачалоМесяца
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СтоимостьОС.Период КАК Период,
	|	СтоимостьОС.Организация КАК Организация,
	|	СтоимостьОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СУММА(СтоимостьОС.СтоимостьНУ) КАК СтоимостьНУ,
	|	СУММА(СтоимостьОС.АмортизационнаяПремия) КАК АмортизационнаяПремия
	|
	|ПОМЕСТИТЬ ВТ_СтоимостьОС_ЗаПериод
	|
	|ИЗ
	|	(ВЫБРАТЬ
	|		СтоимостьОС.Период КАК Период,
	|		СтоимостьОС.Организация КАК Организация,
	|		СтоимостьОС.ОсновноеСредство КАК ОсновноеСредство,
	|		СтоимостьОС.СтоимостьНУКонечныйОстаток + СтоимостьОС.СтоимостьНУЦФКонечныйОстаток КАК СтоимостьНУ,
	|		СтоимостьОС.АмортизационнаяПремияКонечныйОстаток КАК АмортизационнаяПремия
	|	ИЗ
	|		РегистрНакопления.СтоимостьОС.ОстаткиИОбороты(
	|				&НачалоПрошлогоПериода {(&НачалоПрошлогоПериода)},
	|				&КонецПериода {(&КонецПериода)},
	|				Месяц,
	|				,
	|				ОсновноеСредство В
	|					(ВЫБРАТЬ
	|						Амортизация.ОсновноеСредство
	|					ИЗ
	|						ВТ_Амортизация КАК Амортизация)) КАК СтоимостьОС
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА ПринятиеКУчету.ПринятиеКУчетуПрошлойДатой
	|				ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ПринятиеКУчету.ДатаПринятияКУчету, ДЕНЬ), ДЕНЬ, -1)
	|			ИНАЧЕ ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ПринятиеКУчету.Дата, ДЕНЬ), ДЕНЬ, -1) 
	|		КОНЕЦ КАК Период,
	|
	|		ПринятиеКУчету.Организация КАК Организация,
	|		ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|		ТаблицаОС.СтоимостьНУ КАК СтоимостьНУ,
	|		0 КАК АмортизационнаяПремия
	|
	|	ИЗ
	|		Документ.ПринятиеКУчетуОС2_4 КАК ПринятиеКУчету
	|
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОС2_4.ОС КАК ТаблицаОС
	|			ПО ТаблицаОС.Ссылка = ПринятиеКУчету.Ссылка
	|
	|	ГДЕ
	|		(ПринятиеКУчету.Дата >= &НачалоПериода
	|			ИЛИ &НачалоПериода = ДАТАВРЕМЯ(1,1,1))
	|		И (ПринятиеКУчету.Дата <= &КонецПериода
	|		ИЛИ &КонецПериода = ДАТАВРЕМЯ(1,1,1))
	|
	|		И ПринятиеКУчету.Проведен
	|		И ПринятиеКУчету.ОтражатьВНУ
	|		И ПринятиеКУчету.ПринятиеКУчетуПрошлойДатой
	|
	|	) КАК СтоимостьОС
	|
	|СГРУППИРОВАТЬ ПО
	|	СтоимостьОС.Период,
	|	СтоимостьОС.Организация,
	|	СтоимостьОС.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Амортизация.Период КАК Период,
	|	ВТ_Амортизация.Организация КАК Организация,
	|	ВТ_Амортизация.ОсновноеСредство КАК ОсновноеСредство,
	|	МАКСИМУМ(ВТ_СтоимостьОС_ЗаПериод.Период) КАК ПериодСрезаПоследних
	|
	|ПОМЕСТИТЬ ВТ_СтоимостьОС_НаНачалоМесяца_СрезПоследних
	|
	|ИЗ
	|	ВТ_Амортизация КАК ВТ_Амортизация
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СтоимостьОС_ЗаПериод КАК ВТ_СтоимостьОС_ЗаПериод
	|		ПО (ВТ_СтоимостьОС_ЗаПериод.ОсновноеСредство = ВТ_Амортизация.ОсновноеСредство)
	|			И (ВТ_СтоимостьОС_ЗаПериод.Организация = ВТ_Амортизация.Организация)
	|			И (ВТ_СтоимостьОС_ЗаПериод.Период < ВТ_Амортизация.НачалоМесяца)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Амортизация.Период,
	|	ВТ_Амортизация.Организация,
	|	ВТ_Амортизация.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Организация,
	|	ПериодСрезаПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПоследнийПериод.Период КАК Период,
	|	ТаблицаПоследнийПериод.Организация КАК Организация,
	|	ТаблицаПоследнийПериод.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаРегистраНаДату.СтоимостьНУ КАК СтоимостьНУ,
	|	ТаблицаРегистраНаДату.АмортизационнаяПремия КАК АмортизационнаяПремия
	|
	|ПОМЕСТИТЬ ВТ_СтоимостьОС_НаНачалоМесяца
	|
	|ИЗ
	|	ВТ_СтоимостьОС_НаНачалоМесяца_СрезПоследних КАК ТаблицаПоследнийПериод
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СтоимостьОС_ЗаПериод КАК ТаблицаРегистраНаДату
	|		ПО (ТаблицаРегистраНаДату.ОсновноеСредство = ТаблицаПоследнийПериод.ОсновноеСредство)
	|			И (ТаблицаРегистраНаДату.Организация = ТаблицаПоследнийПериод.Организация)
	|			И (ТаблицаРегистраНаДату.Период = ТаблицаПоследнийПериод.ПериодСрезаПоследних)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Организация,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_СтоимостьОС_ЗаПериод
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_СтоимостьОС_НаНачалоМесяца_СрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АмортизацияОС.Период КАК Период,
	|	АмортизацияОС.Организация КАК Организация,
	|	АмортизацияОС.ОсновноеСредство КАК ОсновноеСредство,
	|	-(АмортизацияОС.АмортизацияНУКонечныйОстаток + АмортизацияОС.АмортизацияНУЦФКонечныйОстаток) КАК АмортизацияНУ
	|
	|ПОМЕСТИТЬ ВТ_АмортизацияОС_ЗаПериод
	|
	|ИЗ
	|	РегистрНакопления.АмортизацияОС.ОстаткиИОбороты(
	|			&НачалоПрошлогоПериода {(&НачалоПрошлогоПериода)},
	|			&КонецПериода {(&КонецПериода)},
	|			Месяц,
	|			,
	|			ОсновноеСредство В
	|				(ВЫБРАТЬ
	|					Амортизация.ОсновноеСредство
	|				ИЗ
	|					ВТ_Амортизация КАК Амортизация)) КАК АмортизацияОС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Амортизация.Период КАК Период,
	|	ВТ_Амортизация.Организация КАК Организация,
	|	ВТ_Амортизация.ОсновноеСредство КАК ОсновноеСредство,
	|	МАКСИМУМ(ВТ_АмортизацияОС_ЗаПериод.Период) КАК ПериодСрезаПоследних
	|
	|ПОМЕСТИТЬ ВТ_АмортизацияОС_НаНачалоМесяца_СрезПоследних
	|
	|ИЗ
	|	ВТ_Амортизация КАК ВТ_Амортизация
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АмортизацияОС_ЗаПериод КАК ВТ_АмортизацияОС_ЗаПериод
	|		ПО (ВТ_АмортизацияОС_ЗаПериод.ОсновноеСредство = ВТ_Амортизация.ОсновноеСредство)
	|			И (ВТ_АмортизацияОС_ЗаПериод.Организация = ВТ_Амортизация.Организация)
	|			И (ВТ_АмортизацияОС_ЗаПериод.Период < ВТ_Амортизация.НачалоМесяца)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Амортизация.Период,
	|	ВТ_Амортизация.Организация,
	|	ВТ_Амортизация.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Организация,
	|	ПериодСрезаПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПоследнийПериод.Период КАК Период,
	|	ТаблицаПоследнийПериод.Организация КАК Организация,
	|	ТаблицаПоследнийПериод.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаРегистраНаДату.АмортизацияНУ КАК АмортизацияНУ
	|
	|ПОМЕСТИТЬ ВТ_АмортизацияОС_НаНачалоМесяца
	|
	|ИЗ
	|	ВТ_АмортизацияОС_НаНачалоМесяца_СрезПоследних КАК ТаблицаПоследнийПериод
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АмортизацияОС_ЗаПериод КАК ТаблицаРегистраНаДату
	|		ПО (ТаблицаРегистраНаДату.ОсновноеСредство = ТаблицаПоследнийПериод.ОсновноеСредство)
	|			И (ТаблицаРегистраНаДату.Организация = ТаблицаПоследнийПериод.Организация)
	|			И (ТаблицаРегистраНаДату.Период = ТаблицаПоследнийПериод.ПериодСрезаПоследних)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Организация,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_АмортизацияОС_ЗаПериод
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_АмортизацияОС_НаНачалоМесяца_СрезПоследних";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ВТ_АмортизацияОСПриПереходеКЛинейномуМетоду
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НелинейныйМетод.Организация КАК Организация,
	|	ЕСТЬNULL(МИНИМУМ(ЛинейныйМетод.Период), ДАТАВРЕМЯ(2009, 1, 1)) КАК Дата
	|
	|ПОМЕСТИТЬ ДатыПереходаКЛинейномуМетоду
	|
	|ИЗ
	|	РегистрСведений.НастройкиУчетаНалогаНаПрибыль.СрезПоследних(
	|		&КонецПериода{&КонецПериода}, 
	|		МетодНачисленияАмортизацииНУ = ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизации.Нелинейный)) КАК НелинейныйМетод
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиУчетаНалогаНаПрибыль КАК ЛинейныйМетод
	|		ПО ЛинейныйМетод.Организация = НелинейныйМетод.Организация
	|			И ЛинейныйМетод.МетодНачисленияАмортизацииНУ = ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизации.Линейный)
	|			И ЛинейныйМетод.Период >= ДАТАВРЕМЯ(2009, 1, 1)
	|			И ЛинейныйМетод.Период > НелинейныйМетод.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	НелинейныйМетод.Организация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация
	|;
	|	
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АмортизацияОС.Организация КАК Организация,
	|	АмортизацияОС.ОсновноеСредство КАК ОсновноеСредство,
	|
	|	СУММА(ВЫБОР КОГДА АмортизацияОС.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ТОГДА -АмортизацияОС.АмортизацияНУ
	|		ИНАЧЕ АмортизацияОС.АмортизацияНУ
	|	КОНЕЦ) КАК АмортизацияНУ
	|
	|ПОМЕСТИТЬ ВТ_АмортизацияОСПриПереходеКЛинейномуМетоду
	|
	|ИЗ
	|
	|	ДатыПереходаКЛинейномуМетоду КАК ДатыПереходаКЛинейномуМетоду
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.АмортизацияОС КАК АмортизацияОС
	|		ПО АмортизацияОС.Организация.ГоловнаяОрганизация = ДатыПереходаКЛинейномуМетоду.Организация
	|			И АмортизацияОС.Период < ДатыПереходаКЛинейномуМетоду.Дата
	|			И АмортизацияОС.Активность
	|
	|ГДЕ
	|	ДатыПереходаКЛинейномуМетоду.Дата > ДАТАВРЕМЯ(2009, 1, 1)
	|	И (АмортизацияОС.Организация, АмортизацияОС.ОсновноеСредство) В
	|			(ВЫБРАТЬ
	|				СписокНМА.Организация,
	|				СписокНМА.ОсновноеСредство
	|			ИЗ
	|				ВТ_Амортизация КАК СписокНМА)
	|
	|СГРУППИРОВАТЬ ПО
	|	АмортизацияОС.Организация,
	|	АмортизацияОС.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Организация";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область Выработка
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Амортизация.Период КАК Период,
	|	Амортизация.Организация КАК Организация,
	|	Амортизация.ОсновноеСредство КАК ОсновноеСредство,
	|	ЕСТЬNULL(ПорядокУчетаУпр.ПоказательНаработки, ПорядокУчетаРегл.ПоказательНаработки) КАК ПоказательНаработки,
	|	КОНЕЦПЕРИОДА(Амортизация.Период, МЕСЯЦ) КАК КонецМесяца,
	|	ВЫБОР
	|		КОГДА МЕСЯЦ(ПервоначальныеСведенияБУ.ДатаВводаВЭксплуатациюБУ) = МЕСЯЦ(Амортизация.Период) - 1
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПринятоКУчетуВПрошломМесяце
	|
	|ПОМЕСТИТЬ ВТ_СписокОС_Выработка
	|
	|ИЗ
	|	ВТ_Амортизация КАК Амортизация
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПервоначальныеСведенияБУ КАК ПервоначальныеСведенияБУ
	|		ПО (ПервоначальныеСведенияБУ.Организация = Амортизация.Организация)
	|			И (ПервоначальныеСведенияБУ.ОсновноеСредство = Амортизация.ОсновноеСредство)
	|			И (ПервоначальныеСведенияБУ.Период = Амортизация.Период)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаУпр КАК ПорядокУчетаУпр
	|		ПО (ПорядокУчетаУпр.ОсновноеСредство = Амортизация.ОсновноеСредство)
	|			И (ПорядокУчетаУпр.Период = Амортизация.Период)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаРегл КАК ПорядокУчетаРегл
	|		ПО (ПорядокУчетаРегл.ОсновноеСредство = Амортизация.ОсновноеСредство)
	|			И (ПорядокУчетаРегл.Период = Амортизация.Период)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииОСУУ КАК ПараметрыАмортизацииОСУУ
	|		ПО (ПараметрыАмортизацииОСУУ.Организация = Амортизация.Организация)
	|			И (ПараметрыАмортизацииОСУУ.ОсновноеСредство = Амортизация.ОсновноеСредство)
	|			И (ПараметрыАмортизацииОСУУ.Период = Амортизация.Период)
	|ГДЕ
	|	(ПервоначальныеСведенияБУ.МетодНачисленияАмортизацииБУ В (
	|			ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции), 
	|			ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПоЕНАОФНа1000кмПробега))
	|		ИЛИ ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ПоказательНаработки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СписокОС.Период КАК Период,
	|	НАЧАЛОПЕРИОДА(СписокОС.Период, МЕСЯЦ) КАК НачалоМесяца,
	|	СписокОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СписокОС.ПоказательНаработки КАК ПоказательНаработки
	|
	|ПОМЕСТИТЬ ВТ_СписокОС_НеПринятоКУчетуВПрошломМесяце
	|
	|ИЗ
	|	ВТ_СписокОС_Выработка КАК СписокОС
	|
	|ГДЕ
	|	НЕ СписокОС.ПринятоКУчетуВПрошломМесяце
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ПоказательНаработки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СписокОС.Период КАК Период,
	|	НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(СписокОС.Период, МЕСЯЦ, -1), МЕСЯЦ) КАК НачалоМесяца,
	|	СписокОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СписокОС.ПоказательНаработки КАК ПоказательНаработки
	|
	|ПОМЕСТИТЬ ВТ_СписокОС_ПринятоКУчетуВПрошломМесяце
	|
	|ИЗ
	|	ВТ_СписокОС_Выработка КАК СписокОС
	|
	|ГДЕ
	|	СписокОС.ПринятоКУчетуВПрошломМесяце
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ПоказательНаработки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПериодыПараметров.Период КАК Период,
	|	ПериодыПараметров.ОсновноеСредство КАК ОсновноеСредство,
	|	ПериодыПараметров.ПоказательНаработки КАК ПоказательНаработки,
	|	МАКСИМУМ(ТаблицаПоследнийПериод.Период) КАК ПериодСрезаПоследних
	|
	|ПОМЕСТИТЬ НаработкиОбъектовЭксплуатации_СрезПоследних_НаНачалоМесяца
	|
	|ИЗ
	|	ВТ_СписокОС_НеПринятоКУчетуВПрошломМесяце КАК ПериодыПараметров
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НаработкиОбъектовЭксплуатации КАК ТаблицаПоследнийПериод
	|		ПО (ТаблицаПоследнийПериод.ОбъектЭксплуатации = ПериодыПараметров.ОсновноеСредство)
	|			И (ТаблицаПоследнийПериод.ПоказательНаработки = ПериодыПараметров.ПоказательНаработки)
	|			И (ТаблицаПоследнийПериод.Период < ПериодыПараметров.НачалоМесяца)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПериодыПараметров.Период,
	|	ПериодыПараметров.ОсновноеСредство,
	|	ПериодыПараметров.ПоказательНаработки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ПоказательНаработки,
	|	ПериодСрезаПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСрезПоследних.Период КАК Период,
	|	ТаблицаСрезПоследних.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаРегистраНаДату.ПоказательНаработки КАК ПоказательНаработки,
	|	ТаблицаРегистраНаДату.Значение КАК ОбъемВыработки
	|
	|ПОМЕСТИТЬ ВТ_ВыработкаОС_НаНачалоМесяца
	|ИЗ
	|	НаработкиОбъектовЭксплуатации_СрезПоследних_НаНачалоМесяца КАК ТаблицаСрезПоследних
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НаработкиОбъектовЭксплуатации КАК ТаблицаРегистраНаДату
	|		ПО (ТаблицаРегистраНаДату.ОбъектЭксплуатации = ТаблицаСрезПоследних.ОсновноеСредство)
	|			И (ТаблицаРегистраНаДату.ПоказательНаработки = ТаблицаСрезПоследних.ПоказательНаработки)
	|			И (ТаблицаРегистраНаДату.Период = ТаблицаСрезПоследних.ПериодСрезаПоследних)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ПоказательНаработки,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ НаработкиОбъектовЭксплуатации_СрезПоследних_НаНачалоМесяца
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПериодыПараметров.Период КАК Период,
	|	ПериодыПараметров.ОсновноеСредство КАК ОсновноеСредство,
	|	ПериодыПараметров.ПоказательНаработки КАК ПоказательНаработки,
	|	МАКСИМУМ(ТаблицаПоследнийПериод.Период) КАК ПериодСрезаПоследних
	|
	|ПОМЕСТИТЬ НаработкиОбъектовЭксплуатации_СрезПоследних_НаКонецМесяца
	|
	|ИЗ
	|	ВТ_СписокОС_Выработка КАК ПериодыПараметров
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НаработкиОбъектовЭксплуатации КАК ТаблицаПоследнийПериод
	|		ПО (ТаблицаПоследнийПериод.ОбъектЭксплуатации = ПериодыПараметров.ОсновноеСредство)
	|			И (ТаблицаПоследнийПериод.ПоказательНаработки = ПериодыПараметров.ПоказательНаработки)
	|			И (ТаблицаПоследнийПериод.Период <= ПериодыПараметров.КонецМесяца)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПериодыПараметров.Период,
	|	ПериодыПараметров.ОсновноеСредство,
	|	ПериодыПараметров.ПоказательНаработки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ПоказательНаработки,
	|	ПериодСрезаПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСрезПоследних.Период КАК Период,
	|	ТаблицаСрезПоследних.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаРегистраНаДату.ПоказательНаработки КАК ПоказательНаработки,
	|	ТаблицаРегистраНаДату.Значение КАК ОбъемВыработки,
	|	СписокОС.ПринятоКУчетуВПрошломМесяце КАК ПринятоКУчетуВПрошломМесяце
	|
	|ПОМЕСТИТЬ ВТ_ВыработкаОС_НаКонецМесяца
	|
	|ИЗ
	|	НаработкиОбъектовЭксплуатации_СрезПоследних_НаКонецМесяца КАК ТаблицаСрезПоследних
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НаработкиОбъектовЭксплуатации КАК ТаблицаРегистраНаДату
	|		ПО (ТаблицаРегистраНаДату.ОбъектЭксплуатации = ТаблицаСрезПоследних.ОсновноеСредство)
	|			И (ТаблицаРегистраНаДату.ПоказательНаработки = ТаблицаСрезПоследних.ПоказательНаработки)
	|			И (ТаблицаРегистраНаДату.Период = ТаблицаСрезПоследних.ПериодСрезаПоследних)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СписокОС_Выработка КАК СписокОС
	|		ПО (СписокОС.ОсновноеСредство = ТаблицаСрезПоследних.ОсновноеСредство)
	|			И (СписокОС.ПоказательНаработки = ТаблицаСрезПоследних.ПоказательНаработки)
	|			И (СписокОС.Период = ТаблицаСрезПоследних.Период)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ПоказательНаработки,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ НаработкиОбъектовЭксплуатации_СрезПоследних_НаКонецМесяца
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПериодыПараметров.Период КАК Период,
	|	ПериодыПараметров.ОсновноеСредство КАК ОсновноеСредство,
	|	ПериодыПараметров.ПоказательНаработки КАК ПоказательНаработки,
	|	МАКСИМУМ(ТаблицаПоследнийПериод.Период) КАК ПериодСрезаПоследних
	|
	|ПОМЕСТИТЬ НаработкиОбъектовЭксплуатации_СрезПоследних_НаНачалоПредыдущегоМесяца
	|
	|ИЗ
	|	ВТ_СписокОС_ПринятоКУчетуВПрошломМесяце КАК ПериодыПараметров
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НаработкиОбъектовЭксплуатации КАК ТаблицаПоследнийПериод
	|		ПО (ТаблицаПоследнийПериод.ОбъектЭксплуатации = ПериодыПараметров.ОсновноеСредство)
	|			И (ТаблицаПоследнийПериод.ПоказательНаработки = ПериодыПараметров.ПоказательНаработки)
	|			И (ТаблицаПоследнийПериод.Период < ПериодыПараметров.НачалоМесяца)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПериодыПараметров.Период,
	|	ПериодыПараметров.ОсновноеСредство,
	|	ПериодыПараметров.ПоказательНаработки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ПоказательНаработки,
	|	ПериодСрезаПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСрезПоследних.Период КАК Период,
	|	ТаблицаСрезПоследних.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаРегистраНаДату.ПоказательНаработки КАК ПоказательНаработки,
	|	ТаблицаРегистраНаДату.Значение КАК ОбъемВыработки
	|
	|ПОМЕСТИТЬ ВТ_ВыработкаОС_НаНачалоПредыдущегоМесяца
	|
	|ИЗ
	|	НаработкиОбъектовЭксплуатации_СрезПоследних_НаНачалоПредыдущегоМесяца КАК ТаблицаСрезПоследних
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НаработкиОбъектовЭксплуатации КАК ТаблицаРегистраНаДату
	|		ПО (ТаблицаРегистраНаДату.ОбъектЭксплуатации = ТаблицаСрезПоследних.ОсновноеСредство)
	|			И (ТаблицаРегистраНаДату.ПоказательНаработки = ТаблицаСрезПоследних.ПоказательНаработки)
	|			И (ТаблицаРегистраНаДату.Период = ТаблицаСрезПоследних.ПериодСрезаПоследних)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ПоказательНаработки,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ НаработкиОбъектовЭксплуатации_СрезПоследних_НаНачалоПредыдущегоМесяца
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(НаработкиПериодНачало.Период, НаработкиПериодОкончание.Период) КАК Период,
	|	ЕСТЬNULL(НаработкиПериодНачало.ОсновноеСредство, НаработкиПериодОкончание.ОсновноеСредство) КАК ОсновноеСредство,
	|	ЕСТЬNULL(НаработкиПериодОкончание.ОбъемВыработки, 0) - ЕСТЬNULL(НаработкиПериодНачало.ОбъемВыработки, 0) КАК ОбъемВыработки,
	|	ЕСТЬNULL(НаработкиПериодНачало.ОбъемВыработки, 0) КАК ОбъемВыработкиНаНачалоМесяца
	|
	|ПОМЕСТИТЬ ВТ_ВыработкаОС
	|
	|ИЗ
	|	ВТ_ВыработкаОС_НаНачалоМесяца КАК НаработкиПериодНачало
	|
	|		ПОЛНОЕ СОЕДИНЕНИЕ ВТ_ВыработкаОС_НаКонецМесяца КАК НаработкиПериодОкончание
	|		ПО НаработкиПериодНачало.ОсновноеСредство = НаработкиПериодОкончание.ОсновноеСредство
	|			И НаработкиПериодНачало.ПоказательНаработки = НаработкиПериодОкончание.ПоказательНаработки
	|			И НаработкиПериодНачало.Период = НаработкиПериодОкончание.Период
	|ГДЕ
	|	НЕ НаработкиПериодОкончание.ПринятоКУчетуВПрошломМесяце
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЕСТЬNULL(НаработкиПериодНачало.Период, НаработкиПериодОкончание.Период),
	|	ЕСТЬNULL(НаработкиПериодНачало.ОсновноеСредство, НаработкиПериодОкончание.ОсновноеСредство),
	|	ЕСТЬNULL(НаработкиПериодОкончание.ОбъемВыработки, 0) - ЕСТЬNULL(НаработкиПериодНачало.ОбъемВыработки, 0),
	|	0
	|ИЗ
	|	ВТ_ВыработкаОС_НаНачалоПредыдущегоМесяца КАК НаработкиПериодНачало
	|
	|		ПОЛНОЕ СОЕДИНЕНИЕ ВТ_ВыработкаОС_НаКонецМесяца КАК НаработкиПериодОкончание
	|		ПО НаработкиПериодНачало.ОсновноеСредство = НаработкиПериодОкончание.ОсновноеСредство
	|			И НаработкиПериодНачало.ПоказательНаработки = НаработкиПериодОкончание.ПоказательНаработки
	|			И НаработкиПериодНачало.Период = НаработкиПериодОкончание.Период
	|ГДЕ
	|	НаработкиПериодОкончание.ПринятоКУчетуВПрошломМесяце
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Период";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область Амортизация
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Амортизация.Период КАК Период,
	|	Амортизация.Период КАК ПериодБУ,
	|	Амортизация.Организация КАК Организация,
	|	Амортизация.ОсновноеСредство КАК ОсновноеСредство,
	|
	|	ВТ_ВыработкаОС.ОбъемВыработки КАК Выработка,
	|
	// Параметры УУ
	|	Амортизация.АмортизацияУУ КАК АмортизацияУУ,
	|
	|	Амортизация.СтоимостьДляВычисленияАмортизацииУУ КАК СтоимостьДляВычисленияАмортизацииУУ,
	|	Амортизация.РесурсВыработанныйЗаПрошлыйПериодУУ КАК РесурсВыработанныйЗаПрошлыйПериодУУ,
	|	Амортизация.РесурсВыработанныйЗаТекущийПериодУУ КАК РесурсВыработанныйЗаТекущийПериодУУ,
	|	Амортизация.ПорядокУчетаУУ КАК ПорядокУчетаУУ,
	|	Амортизация.ЭтоДоначислениеУУ КАК ЭтоДоначислениеУУ,
	|
	|	ВЫБОР
	|		КОГДА Амортизация.ЭтоДоначислениеУУ
	|				И НАЧАЛОПЕРИОДА(Амортизация.ПериодРасчета, МЕСЯЦ) = НАЧАЛОПЕРИОДА(Амортизация.Период, МЕСЯЦ)
	|			ТОГДА 0
	|		ИНАЧЕ ПараметрыАмортизацииОСУУ.ЛиквидационнаяСтоимость
	|	КОНЕЦ КАК ЛиквидационнаяСтоимость,
	|
	|	ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации КАК МетодНачисленияАмортизацииУУ,
	|	ПараметрыАмортизацииОСУУ.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ОбъемПродукцииРаботДляВычисленияАмортизацииУУ,
	|	ПараметрыАмортизацииОСУУ.СрокИспользованияДляВычисленияАмортизации КАК СрокИспользованияДляВычисленияАмортизацииУУ,
	|	ПараметрыАмортизацииОСУУ.КоэффициентУскорения КАК КоэффициентУскоренияУУ,
	|	ЕСТЬNULL(ВТ_СтоимостьОС_НаНачалоГода.Стоимость, 0) - ЕСТЬNULL(ВТ_АмортизацияОС_НаНачалоГода.АмортизацияУУ, 0) КАК СтоимостьНаНачалоГодаУУ,
	|
	|	ВЫБОР
	|		КОГДА ((ГОД(Амортизация.Период) - ГОД(НАЧАЛОПЕРИОДА(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения, МЕСЯЦ))) * 12 + МЕСЯЦ(Амортизация.Период) - МЕСЯЦ(НАЧАЛОПЕРИОДА(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения, МЕСЯЦ))) / 12 < 1
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(((ГОД(Амортизация.Период) - ГОД(НАЧАЛОПЕРИОДА(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения, МЕСЯЦ))) * 12 + МЕСЯЦ(Амортизация.Период) - МЕСЯЦ(НАЧАЛОПЕРИОДА(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения, МЕСЯЦ))) / 12 - 0.5 КАК ЧИСЛО(4, 0))
	|	КОНЕЦ КАК ЧислоПрошедшихЛетУУ,
	|
	|	ВЫБОР
	|		КОГДА ГОД(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения) <> ГОД(Амортизация.НачалоМесяца)
	|			ТОГДА 13 - МЕСЯЦ(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения) + 12 * (ГОД(Амортизация.НачалоМесяца) - ГОД(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения) - 1)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОтработанныхМесяцевПолныхЛетУУ,
	|
	|	ВЫБОР
	|		КОГДА ПараметрыАмортизацииОСУУ.СрокИспользованияДляВычисленияАмортизации / 12 < 1
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(ПараметрыАмортизацииОСУУ.СрокИспользованияДляВычисленияАмортизации / 12 - 0.5 КАК ЧИСЛО(4, 0))
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ПараметрыАмортизацииОСУУ.СрокИспользованияДляВычисленияАмортизации / 12 - ВЫБОР
	|				КОГДА ПараметрыАмортизацииОСУУ.СрокИспользованияДляВычисленияАмортизации / 12 < 1
	|					ТОГДА 0
	|				ИНАЧЕ ВЫРАЗИТЬ(ПараметрыАмортизацииОСУУ.СрокИспользованияДляВычисленияАмортизации / 12 - 0.5 КАК ЧИСЛО(4, 0))
	|			КОНЕЦ > 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ЧислоЛетПользованияУУ,
	|
	// Параметры БУ
	|	Амортизация.АмортизацияБУ КАК АмортизацияБУ,
	|	Амортизация.АмортизацияПР КАК АмортизацияПР,
	|	Амортизация.АмортизацияВР КАК АмортизацияВР,
	|
	|	Амортизация.СтоимостьДляВычисленияАмортизацииБУ КАК СтоимостьДляВычисленияАмортизацииБУ,
	|	Амортизация.РесурсВыработанныйЗаПрошлыйПериодБУ КАК РесурсВыработанныйЗаПрошлыйПериодБУ,
	|	Амортизация.РесурсВыработанныйЗаТекущийПериодБУ КАК РесурсВыработанныйЗаТекущийПериодБУ,
	|	Амортизация.ПорядокУчетаБУ КАК ПорядокУчетаБУ,
	|	Амортизация.ЭтоДоначислениеБУ КАК ЭтоДоначислениеБУ,
	|
	|	ВЫБОР
	|		КОГДА Амортизация.ЭтоДоначислениеБУ
	|				И НАЧАЛОПЕРИОДА(Амортизация.ПериодРасчета, МЕСЯЦ) = НАЧАЛОПЕРИОДА(Амортизация.Период, МЕСЯЦ)
	|			ТОГДА 0
	|		ИНАЧЕ ПараметрыАмортизацииБУ.ЛиквидационнаяСтоимость
	|	КОНЕЦ КАК ЛиквидационнаяСтоимостьРегл,
	|
	|	ПараметрыАмортизацииБУ.КоэффициентУскорения КАК КоэффициентУскоренияБУ,
	|	ПараметрыАмортизацииБУ.СрокИспользованияДляВычисленияАмортизации КАК СрокИспользованияДляВычисленияАмортизацииБУ,
	|	ПараметрыАмортизацииБУ.КоэффициентАмортизацииБУ КАК КоэффициентАмортизацииБУ,
	|	ПараметрыАмортизацииБУ.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ОбъемПродукцииРаботДляВычисленияАмортизацииБУ,
	|	ПервоначальныеСведенияБУ.МетодНачисленияАмортизацииБУ КАК МетодНачисленияАмортизацииБУ,
	|
	|	ЕСТЬNULL(ВТ_СтоимостьОС_НаНачалоГода.СтоимостьРегл, 0) - ЕСТЬNULL(ВТ_АмортизацияОС_НаНачалоГода.АмортизацияРегл, 0) КАК СтоимостьНаНачалоГодаБУ,
	|
	|	ВЫБОР
	|		КОГДА ((ГОД(Амортизация.Период) - ГОД(НАЧАЛОПЕРИОДА(ПараметрыАмортизацииБУ.ДатаПоследнегоИзменения, МЕСЯЦ))) * 12 + МЕСЯЦ(Амортизация.Период) - МЕСЯЦ(НАЧАЛОПЕРИОДА(ПараметрыАмортизацииБУ.ДатаПоследнегоИзменения, МЕСЯЦ))) / 12 < 1
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(((ГОД(Амортизация.Период) - ГОД(НАЧАЛОПЕРИОДА(ПараметрыАмортизацииБУ.ДатаПоследнегоИзменения, МЕСЯЦ))) * 12 + МЕСЯЦ(Амортизация.Период) - МЕСЯЦ(НАЧАЛОПЕРИОДА(ПараметрыАмортизацииБУ.ДатаПоследнегоИзменения, МЕСЯЦ))) / 12 - 0.5 КАК ЧИСЛО(4, 0))
	|	КОНЕЦ КАК ЧислоПрошедшихЛетБУ,
	|
	|	ВЫБОР
	|		КОГДА ГОД(ПараметрыАмортизацииБУ.ДатаПоследнегоИзменения) <> ГОД(Амортизация.НачалоМесяца)
	|			ТОГДА 13 - МЕСЯЦ(ПараметрыАмортизацииБУ.ДатаПоследнегоИзменения) + 12 * (ГОД(Амортизация.НачалоМесяца) - ГОД(ПараметрыАмортизацииБУ.ДатаПоследнегоИзменения) - 1)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОтработанныхМесяцевПолныхЛетБУ,
	|
	|	ВЫБОР
	|		КОГДА ПараметрыАмортизацииБУ.СрокИспользованияДляВычисленияАмортизации / 12 < 1
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(ПараметрыАмортизацииБУ.СрокИспользованияДляВычисленияАмортизации / 12 - 0.5 КАК ЧИСЛО(4, 0))
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ПараметрыАмортизацииБУ.СрокИспользованияДляВычисленияАмортизации / 12 - ВЫБОР
	|				КОГДА ПараметрыАмортизацииБУ.СрокИспользованияДляВычисленияАмортизации / 12 < 1
	|					ТОГДА 0
	|				ИНАЧЕ ВЫРАЗИТЬ(ПараметрыАмортизацииБУ.СрокИспользованияДляВычисленияАмортизации / 12 - 0.5 КАК ЧИСЛО(4, 0))
	|			КОНЕЦ > 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ЧислоЛетПользованияБУ,
	|
	|	ВЫБОР
	|		КОГДА ПараметрыАмортизацииБУ.ГрафикАмортизации <> ЗНАЧЕНИЕ(Справочник.ГодовыеГрафикиАмортизацииОС.ПустаяСсылка)
	|			ТОГДА ВЫБОР
	|					КОГДА ПервоначальныеСведенияБУ.МетодНачисленияАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.Линейный)
	|						ТОГДА ВЫРАЗИТЬ(ГрафикиАмортизацииОС.КоэффициентПериода * 12 КАК ЧИСЛО(15, 6))
	|					ИНАЧЕ ГрафикиАмортизацииОС.КоэффициентПериода
	|				КОНЕЦ
	|		КОГДА ПервоначальныеСведенияБУ.МетодНачисленияАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПоЕНАОФ)
	|				ИЛИ ПервоначальныеСведенияБУ.МетодНачисленияАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПоСуммеЧиселЛетИспользования)
	|			ТОГДА ВЫРАЗИТЬ(1 / 12 КАК ЧИСЛО(15, 6))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоэффициентПериода,
	|
	// Параметры НУ
	|	Амортизация.АмортизацияНУ КАК АмортизацияНУ,
	|
	|	ЕСТЬNULL(СтоимостьНаНачалоМесяца.СтоимостьНУ, 0) КАК СтоимостьНаНачалоМесяцаНУ,
	|	ЕСТЬNULL(СтоимостьНаНачалоМесяца.АмортизационнаяПремия, 0) КАК АмортизационнаяПремия,
	|	ЕСТЬNULL(АмортизацияНаНачалоМесяца.АмортизацияНУ, 0) КАК АмортизацияНаНачалоМесяцаНУ,
	|
	|	ЕСТЬNULL(ПараметрыАмортизацииБУ_НУ.СпециальныйКоэффициент, 1) КАК СпециальныйКоэффициентНУ,
	|	ЕСТЬNULL(ПараметрыАмортизацииБУ_НУ.СрокПолезногоИспользованияНУ, 0) КАК СрокПолезногоИспользованияНУ,
	|
	|	&НормаАмортизации КАК НормаАмортизации,
	|
	|	ВЫБОР
	|		КОГДА ПорядокУчетаБУ_НУ.АмортизационнаяГруппа В (
	|						ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ВосьмаяГруппа), 
	|						ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ДевятаяГруппа), 
	|						ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ДесятаяГруппа))
	|
	|					И ВЫРАЗИТЬ(Амортизация.ОсновноеСредство КАК Справочник.ОбъектыЭксплуатации).ГруппаОС В (
	|						ЗНАЧЕНИЕ(Перечисление.ГруппыОС.Здания), 
	|						ЗНАЧЕНИЕ(Перечисление.ГруппыОС.Сооружения))
	|
	|				ИЛИ ПорядокУчетаБУ_НУ.АмортизационнаяГруппа = ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ОтдельнаяГруппа)
	|
	|			ТОГДА 0
	|		КОГДА ГОД(ЕСТЬNULL(ДатыПереходаКЛинейномуМетоду.Дата, ДАТАВРЕМЯ(2009, 1, 1))) = 2009
	|			ТОГДА ЕСТЬNULL(ПервоначальныеСведенияНУ.АмортизацияДо2009, 0)
	|		КОГДА ЕСТЬNULL(ДатыПереходаКЛинейномуМетоду.Дата, ДАТАВРЕМЯ(2009, 1, 1)) <= ЕСТЬNULL(ПервоначальныеСведенияНУ.ДатаВводаВЭксплуатациюБУ, ДАТАВРЕМЯ(1,1,1))
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(АмортизацияОСПриПереходеКЛинейномуМетоду.АмортизацияНУ, 0)
	|	КОНЕЦ КАК АмортизацияПриПереходеКЛинейномуМетоду,
	|
	|	ВЫБОР
	|		КОГДА ПорядокУчетаБУ_НУ.АмортизационнаяГруппа В (
	|						ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ВосьмаяГруппа), 
	|						ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ДевятаяГруппа), 
	|						ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ДесятаяГруппа))
	|
	|					И ВЫРАЗИТЬ(Амортизация.ОсновноеСредство КАК Справочник.ОбъектыЭксплуатации).ГруппаОС В (
	|						ЗНАЧЕНИЕ(Перечисление.ГруппыОС.Здания), 
	|						ЗНАЧЕНИЕ(Перечисление.ГруппыОС.Сооружения))
	|
	|				ИЛИ ПорядокУчетаБУ_НУ.АмортизационнаяГруппа = ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ОтдельнаяГруппа)
	|
	|			ТОГДА 0
	|		КОГДА ГОД(ЕСТЬNULL(ДатыПереходаКЛинейномуМетоду.Дата, ДАТАВРЕМЯ(2009, 1, 1))) = 2009
	|			ТОГДА ЕСТЬNULL(ПервоначальныеСведенияНУ.ФактическийСрокИспользованияДо2009, 0)
	|		КОГДА ЕСТЬNULL(ДатыПереходаКЛинейномуМетоду.Дата, ДАТАВРЕМЯ(2009, 1, 1)) <= ЕСТЬNULL(ПервоначальныеСведенияНУ.ДатаВводаВЭксплуатациюБУ, ДАТАВРЕМЯ(1,1,1))
	|			ТОГДА 0
	|		ИНАЧЕ (ГОД(ЕСТЬNULL(ДатыПереходаКЛинейномуМетоду.Дата, ДАТАВРЕМЯ(2009, 1, 1))) 
	|				- ГОД(ПервоначальныеСведенияНУ.ДатаВводаВЭксплуатациюБУ)) * 12 
	|			+ (МЕСЯЦ(ЕСТЬNULL(ДатыПереходаКЛинейномуМетоду.Дата, ДАТАВРЕМЯ(2009, 1, 1))) 
	|				- МЕСЯЦ(ПервоначальныеСведенияНУ.ДатаВводаВЭксплуатациюБУ)) - 1
	|	КОНЕЦ КАК ФактическийСрокПриПереходеКЛинейномуМетоду,
	|
	|	ВЫБОР
	|		КОГДА ПорядокУчетаБУ_НУ.АмортизационнаяГруппа В (
	|						ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ВосьмаяГруппа), 
	|						ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ДевятаяГруппа), 
	|						ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ДесятаяГруппа))
	|
	|					И ВЫРАЗИТЬ(Амортизация.ОсновноеСредство КАК Справочник.ОбъектыЭксплуатации).ГруппаОС В (
	|						ЗНАЧЕНИЕ(Перечисление.ГруппыОС.Здания), 
	|						ЗНАЧЕНИЕ(Перечисление.ГруппыОС.Сооружения))
	|
	|				ИЛИ ПорядокУчетаБУ_НУ.АмортизационнаяГруппа = ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ОтдельнаяГруппа)
	|
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизации.Линейный)
	|
	|		ИНАЧЕ ЕСТЬNULL(ВТ_НастройкиУчетаНалогаНаПрибыль.МетодНачисленияАмортизацииНУ, ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизации.Линейный))
	|
	|	КОНЕЦ КАК МетодНачисленияАмортизацииНУ
	|
	|ИЗ
	|	ВТ_Амортизация КАК Амортизация
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыЭксплуатации КАК ОбъектыЭксплуатации
	|		ПО (ОбъектыЭксплуатации.Ссылка = Амортизация.ОсновноеСредство)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаБУ_НУ КАК ПорядокУчетаБУ_НУ
	|		ПО (ПорядокУчетаБУ_НУ.Период = Амортизация.Период)
	|			И (ПорядокУчетаБУ_НУ.ОсновноеСредство = Амортизация.ОсновноеСредство)
	|			И (ПорядокУчетаБУ_НУ.Организация = Амортизация.Организация)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииБУ КАК ПараметрыАмортизацииБУ
	|		ПО (ПараметрыАмортизацииБУ.Период = Амортизация.Период)
	|			И (ПараметрыАмортизацииБУ.ОсновноеСредство = Амортизация.ОсновноеСредство)
	|			И (ПараметрыАмортизацииБУ.Организация = Амортизация.Организация)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииБУ_НУ КАК ПараметрыАмортизацииБУ_НУ
	|		ПО (ПараметрыАмортизацииБУ_НУ.Период = Амортизация.Период)
	|			И (ПараметрыАмортизацииБУ_НУ.ОсновноеСредство = Амортизация.ОсновноеСредство)
	|			И (ПараметрыАмортизацииБУ_НУ.Организация = Амортизация.Организация)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииОСУУ КАК ПараметрыАмортизацииОСУУ
	|		ПО (ПараметрыАмортизацииОСУУ.Период = Амортизация.Период)
	|			И (ПараметрыАмортизацииОСУУ.ОсновноеСредство = Амортизация.ОсновноеСредство)
	|			И (ПараметрыАмортизацииОСУУ.Организация = Амортизация.Организация)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПервоначальныеСведенияБУ КАК ПервоначальныеСведенияБУ
	|		ПО (ПервоначальныеСведенияБУ.Период = Амортизация.Период)
	|			И (ПервоначальныеСведенияБУ.ОсновноеСредство = Амортизация.ОсновноеСредство)
	|			И (ПервоначальныеСведенияБУ.Организация = Амортизация.Организация)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПервоначальныеСведенияНУ КАК ПервоначальныеСведенияНУ
	|		ПО (ПервоначальныеСведенияНУ.Период = Амортизация.Период)
	|			И (ПервоначальныеСведенияНУ.ОсновноеСредство = Амортизация.ОсновноеСредство)
	|			И (ПервоначальныеСведенияНУ.Организация = Амортизация.Организация)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ГрафикиАмортизацииОС КАК ГрафикиАмортизацииОС
	|		ПО (ГрафикиАмортизацииОС.Период = Амортизация.Период)
	|			И (ГрафикиАмортизацииОС.ОсновноеСредство = Амортизация.ОсновноеСредство)
	|			И (ГрафикиАмортизацииОС.Организация = Амортизация.Организация)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВыработкаОС КАК ВТ_ВыработкаОС
	|		ПО (ВТ_ВыработкаОС.Период = Амортизация.Период)
	|			И (ВТ_ВыработкаОС.ОсновноеСредство = Амортизация.ОсновноеСредство)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьОС_НаНачалоГода КАК ВТ_СтоимостьОС_НаНачалоГода
	|		ПО (ВТ_СтоимостьОС_НаНачалоГода.НачалоГода = Амортизация.НачалоГода)
	|			И (ВТ_СтоимостьОС_НаНачалоГода.ОсновноеСредство = Амортизация.ОсновноеСредство)
	|			И (ВТ_СтоимостьОС_НаНачалоГода.Организация = Амортизация.Организация)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияОС_НаНачалоГода КАК ВТ_АмортизацияОС_НаНачалоГода
	|		ПО (ВТ_АмортизацияОС_НаНачалоГода.НачалоГода = Амортизация.НачалоГода)
	|			И (ВТ_АмортизацияОС_НаНачалоГода.ОсновноеСредство = Амортизация.ОсновноеСредство)
	|			И (ВТ_АмортизацияОС_НаНачалоГода.Организация = Амортизация.Организация)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьОС_НаНачалоМесяца КАК СтоимостьНаНачалоМесяца
	|		ПО (СтоимостьНаНачалоМесяца.Период = Амортизация.Период)
	|			И (СтоимостьНаНачалоМесяца.Организация = Амортизация.Организация)
	|			И (СтоимостьНаНачалоМесяца.ОсновноеСредство = Амортизация.ОсновноеСредство)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияОС_НаНачалоМесяца КАК АмортизацияНаНачалоМесяца
	|		ПО (АмортизацияНаНачалоМесяца.Период = Амортизация.Период)
	|			И (АмортизацияНаНачалоМесяца.Организация = Амортизация.Организация)
	|			И (АмортизацияНаНачалоМесяца.ОсновноеСредство = Амортизация.ОсновноеСредство)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДатыПереходаКЛинейномуМетоду КАК ДатыПереходаКЛинейномуМетоду
	|		ПО (ДатыПереходаКЛинейномуМетоду.Организация = Амортизация.Организация)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияОСПриПереходеКЛинейномуМетоду КАК АмортизацияОСПриПереходеКЛинейномуМетоду
	|		ПО (АмортизацияОСПриПереходеКЛинейномуМетоду.ОсновноеСредство = Амортизация.ОсновноеСредство)
	|			И (АмортизацияОСПриПереходеКЛинейномуМетоду.Организация = Амортизация.Организация)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_НастройкиУчетаНалогаНаПрибыль КАК ВТ_НастройкиУчетаНалогаНаПрибыль
	|		ПО (ВТ_НастройкиУчетаНалогаНаПрибыль.Период = Амортизация.Период)
	|			И (ВТ_НастройкиУчетаНалогаНаПрибыль.Организация = Амортизация.Организация)
	|";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти

	ТекстЗапроса = СтрСоединить(СписокЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	ТекстЗапроса = СтрЗаменить(
		ТекстЗапроса, 
		"&НормаАмортизации", 
		РасчетАмортизацииОСЛокализация.ТекстПоляНормаАмортизации("ПорядокУчетаБУ_НУ.АмортизационнаяГруппа"));
		
	СхемаКомпоновкиДанных.НаборыДанных.Амортизация.Запрос = ТекстЗапроса;
	
КонецПроцедуры

Процедура УстановитьПериодОтчета(Форма, КомпоновщикНастроекФормы)

	ПараметрПериод = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроекФормы, "ПериодОтчета"); 
	ПараметрПериод = ПараметрПериод.Значение; // СтандартныйПериод - 
	
	Если Форма.Параметры.КлючНазначенияИспользования = "СправкаРасчетПоДокументуАмортизацииОС" Тогда
		
		ДокументРасчетаАмортизации = ОтчетыУТСервер.ЗначениеОтбора(
										"ДокументРасчетаАмортизации", 
										КомпоновщикНастроекФормы.Настройки.Отбор.Элементы);
										
		Если ЗначениеЗаполнено(ДокументРасчетаАмортизации) Тогда
			ДатаДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументРасчетаАмортизации[0], "Дата");
			
			ПараметрПериод.ДатаНачала = НачалоМесяца(ДатаДокумента);
			ПараметрПериод.ДатаОкончания = КонецМесяца(ДатаДокумента);
		КонецЕсли; 
		
	Иначе	
		
		Если НЕ ЗначениеЗаполнено(ПараметрПериод.ДатаНачала) Тогда
			
			ПараметрПериод.ДатаНачала = Константы.ДатаНачалаУчетаВнеоборотныхАктивов2_4.Получить();
			
		КонецЕсли; 
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура ДобавитьИнформационноеСообщениеВШапку(ТабДок, ЗначенияОтбораДанных)

	СписокПроблем = Новый Массив;
	
	ПроверитьАктуальностьРасчетаАмортизации(ЗначенияОтбораДанных, СписокПроблем);
	ПроверитьНаличиеПараметровРасчетаАмортизации(ЗначенияОтбораДанных, СписокПроблем);
	
	Если СписокПроблем.Количество() = 0 Тогда
		Возврат;
	КонецЕсли; 
	
	СписокПроблем = ОбщегоНазначенияУТ.УдалитьПовторяющиесяЭлементыМассива(СписокПроблем);
	
	ТекстСообщения = СтрСоединить(СписокПроблем, Символы.ПС);

	ТабДок.Область("R1C1").Текст = ТекстСообщения;
	ТабДок.Область("R1C1").ЦветТекста = Метаданные.ЭлементыСтиля.ЦветТекстаПроблема.Значение;
	ТабДок.Область(1, 1, 1, ).ВысотаСтроки = 0;
	
КонецПроцедуры

Процедура ПроверитьАктуальностьРасчетаАмортизации(ЗначенияОтбораДанных, СписокПроблем)

	РегистрыСведений.ПакетыАмортизацииОС.СоздатьПакетыАмортизации(ЗначенияОтбораДанных.СписокОрганизаций);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДокументАмортизация.Организация.Представление КАК ОрганизацияПредставление
	|ИЗ
	|	Документ.АмортизацияОС2_4 КАК ДокументАмортизация
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗаданияКРасчетуАмортизацииОС КАК ЗаданияКРасчету
	|		ПО (ЗаданияКРасчету.Организация = ДокументАмортизация.Организация)
	|			И (ЗаданияКРасчету.Месяц <= ДокументАмортизация.Дата)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПакетыАмортизацииОС КАК ПакетыАмортизацииОС
	|		ПО ПакетыАмортизацииОС.Организация = ЗаданияКРасчету.Организация
	|			И ПакетыАмортизацииОС.НомерПакета = ЗаданияКРасчету.НомерПакета
	|			И &ОтборПоСпискуОС
	|ГДЕ
	|	ДокументАмортизация.Организация В(&СписокОрганизаций)
	|	И (НЕ &ОтборПоСпискуОС ИЛИ ПакетыАмортизацииОС.ОсновноеСредство В(&СписокОС))
	|	И (НЕ &ОтборПоДокументу ИЛИ ДокументАмортизация.Ссылка В(&ДокументРасчетаАмортизации))
	|	И ДокументАмортизация.Проведен
	|	И (ДокументАмортизация.Дата >= &НачалоПериода
	|			ИЛИ &НачалоПериода = ДАТАВРЕМЯ(1, 1, 1))
	|	И (ДокументАмортизация.Дата <= &ОкончаниеПериода
	|			ИЛИ &ОкончаниеПериода = ДАТАВРЕМЯ(1, 1, 1))";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("СписокОрганизаций", ЗначенияОтбораДанных.СписокОрганизаций);
	Запрос.УстановитьПараметр("НачалоПериода", ЗначенияОтбораДанных.НачалоПериода);
	Запрос.УстановитьПараметр("ОкончаниеПериода", ЗначенияОтбораДанных.ОкончаниеПериода);
	Запрос.УстановитьПараметр("СписокОС", ЗначенияОтбораДанных.СписокОС);
	Запрос.УстановитьПараметр("ОтборПоСпискуОС", ЗначениеЗаполнено(ЗначенияОтбораДанных.СписокОС));
	Запрос.УстановитьПараметр("ДокументРасчетаАмортизации", ЗначенияОтбораДанных.ДокументРасчетаАмортизации);
	Запрос.УстановитьПараметр("ОтборПоДокументу", ЗначениеЗаполнено(ЗначенияОтбораДанных.ДокументРасчетаАмортизации));
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли; 
	
	СписокПроблем.Добавить(НСтр("ru = 'Расчет амортизации не актуален:';
								|en = 'Depreciation calculation is not relevant:'"));
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		ТекстСообщения = НСтр("ru = '- для организации ""%1"" необходимо выполнить закрытие месяца';
								|en = '- for the company ""%1"" you must perform a month-end closing'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, Выборка.ОрганизацияПредставление);
		СписокПроблем.Добавить(ТекстСообщения); 
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьНаличиеПараметровРасчетаАмортизации(ЗначенияОтбораДанных, СписокПроблем)

	Если НЕ ЗначенияОтбораДанных.ПоказатьРасчетУУ Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	АмортизацияЗаписи.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.АмортизацияОС КАК АмортизацияЗаписи
	|ГДЕ
	|	АмортизацияЗаписи.Организация В(&СписокОрганизаций)
	|	И (НЕ &ОтборПоДокументу ИЛИ АмортизацияЗаписи.Регистратор В(&ДокументРасчетаАмортизации))
	|	И (НЕ &ОтборПоСпискуОС ИЛИ АмортизацияЗаписи.ОсновноеСредство В(&СписокОС))
	|	И АмортизацияЗаписи.Регистратор ССЫЛКА Документ.АмортизацияОС2_4
	|	И АмортизацияЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И (АмортизацияЗаписи.Период >= &НачалоПериода
	|			ИЛИ &НачалоПериода = ДАТАВРЕМЯ(1, 1, 1))
	|	И (АмортизацияЗаписи.Период <= &ОкончаниеПериода
	|			ИЛИ &ОкончаниеПериода = ДАТАВРЕМЯ(1, 1, 1))
	|	И АмортизацияЗаписи.РесурсВыработанныйЗаТекущийПериод = 0
	|	И АмортизацияЗаписи.СтоимостьДляВычисленияАмортизации = 0
	|	И АмортизацияЗаписи.Амортизация <> 0";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("СписокОрганизаций", ЗначенияОтбораДанных.СписокОрганизаций);
	Запрос.УстановитьПараметр("НачалоПериода", ЗначенияОтбораДанных.НачалоПериода);
	Запрос.УстановитьПараметр("ОкончаниеПериода", ЗначенияОтбораДанных.ОкончаниеПериода);
	Запрос.УстановитьПараметр("СписокОС", ЗначенияОтбораДанных.СписокОС);
	Запрос.УстановитьПараметр("ОтборПоСпискуОС", ЗначениеЗаполнено(ЗначенияОтбораДанных.СписокОС));
	Запрос.УстановитьПараметр("ДокументРасчетаАмортизации", ЗначенияОтбораДанных.ДокументРасчетаАмортизации);
	Запрос.УстановитьПараметр("ОтборПоДокументу", ЗначениеЗаполнено(ЗначенияОтбораДанных.ДокументРасчетаАмортизации));
	
	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
		ТекстСообщения = НСтр("ru = 'Параметры расчета амортизации УУ могут быть незаполнены (подробнее см. справку)';
								|en = 'Parameters for calculating depreciation MA may be not filled (for more details, see Help)'");
		СписокПроблем.Добавить(ТекстСообщения); 
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли 