
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - см. ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.События.ПередЗагрузкойНастроекВКомпоновщик = Истина;
	
КонецПроцедуры

// Вызывается перед загрузкой новых настроек. Используется для изменения схемы компоновки.
//
// Параметры:
//	Контекст							- Произвольный								- параметры контекста, в котором используется отчет.
//	КлючСхемы							- Строка									- идентификатор текущей схемы компоновщика настроек.
//	КлючВарианта						- Строка									- имя предопределенного или уникальный идентификатор пользовательского
//																						варианта отчета.
//										- Неопределено 								- вызов для варианта расшифровки или без контекста.
//	НовыеНастройкиКД					- НастройкиКомпоновкиДанных					- настройки варианта отчета, которые будут загружены
//																						в компоновщик настроек после его инициализации.
//										- Неопределено 								- настройки варианта не надо загружать (уже загружены ранее).
//	НовыеПользовательскиеНастройкиКД	- ПользовательскиеНастройкиКомпоновкиДанных - пользовательские настройки, которые будут загружены в компоновщик
//																						настроек после его инициализации.
//										- Неопределено 								- пользовательские настройки не надо загружать (уже загружены ранее).
//
Процедура ПередЗагрузкойНастроекВКомпоновщик(Контекст, КлючСхемы, КлючВарианта, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД) Экспорт
	
	Если ТипЗнч(Контекст) = Тип("ФормаКлиентскогоПриложения") Тогда
		
		Если НовыеПользовательскиеНастройкиКД = Неопределено Тогда
			ПользовательскиеНастройки = Контекст.Отчет.КомпоновщикНастроек.ПользовательскиеНастройки; // ПользовательскиеНастройкиКомпоновкиДанных -
			НастроитьПараметрыОтчетаПоВариантуОтчета(Контекст.НастройкиОтчета, НовыеНастройкиКД, ПользовательскиеНастройки);
		Иначе
			НастроитьПараметрыОтчетаПоВариантуОтчета(Контекст.НастройкиОтчета, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД);
		КонецЕсли;
		
		НастроитьПараметрыОтборыПоФункциональнымОпциям(НовыеНастройкиКД);
		
		ОтчетыСервер.ПодключитьСхему(ЭтотОбъект, Контекст, СхемаКомпоновкиДанных, КлючСхемы);
		
		КомпоновщикНастроекФормы = Контекст.Отчет.КомпоновщикНастроек;
		Параметры = Контекст.Параметры;
		
		Если Параметры.Свойство("ПользовательскиеПараметры") Тогда
			КомпоновкаДанныхКлиентСервер.ДобавитьПараметрыВПользовательскиеНастройки(КомпоновщикНастроекФормы, Параметры.ПользовательскиеПараметры);
			НовыеПользовательскиеНастройкиКД = КомпоновщикНастроекФормы.ПользовательскиеНастройки;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	
	НастроитьПользовательскиеНастройкиПоФункциональнымОпциям(КомпоновщикНастроек);
	
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	
	НастройкиОтчета.ПараметрыДанных.УстановитьЗначениеПараметра("Постатейные",
		НСтр("ru = 'Постатейные';
			|en = 'Itemized'"));
	НастройкиОтчета.ПараметрыДанных.УстановитьЗначениеПараметра("НачальныйОстаток",
		НСтр("ru = 'Начальный остаток';
			|en = 'Opening balance'"));
	НастройкиОтчета.ПараметрыДанных.УстановитьЗначениеПараметра("Регистрация",
		НСтр("ru = 'Закупка услуг, регистрация, отражение зарплаты';
			|en = 'Service purchase, registration, payroll'"));
	НастройкиОтчета.ПараметрыДанных.УстановитьЗначениеПараметра("РаспределениеНаПроизводство",
		НСтр("ru = 'Распределение на производство';
			|en = 'Allocation to production'"));
	НастройкиОтчета.ПараметрыДанных.УстановитьЗначениеПараметра("Выбытие",
		НСтр("ru = 'Списание на другие статьи, выбытие';
			|en = 'Write off to other items, retirement'"));
	НастройкиОтчета.ПараметрыДанных.УстановитьЗначениеПараметра("Перенос",
		НСтр("ru = 'Перенос на другой объект возникновения затрат';
			|en = 'Allocate to another cost center'"));
	НастройкиОтчета.ПараметрыДанных.УстановитьЗначениеПараметра("КонечныйОстаток",
		НСтр("ru = 'Конечный остаток';
			|en = 'Closing balance'"));
	
	ПараметрПериодОтчета = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(НастройкиОтчета, "Период");
	ПараметрПериодОтчетаЗначение = ПараметрПериодОтчета.Значение; //СтандартныйПериод
	БезОтбораПоПериоду = Не ПараметрПериодОтчета.Использование;
	БезОтбораПоПериодуНачало = БезОтбораПоПериоду ИЛИ НЕ ЗначениеЗаполнено(ПараметрПериодОтчетаЗначение.ДатаНачала);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(НастройкиОтчета, "БезОтбораПоПериоду", БезОтбораПоПериоду);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(НастройкиОтчета, "БезОтбораПоПериодуНачало", БезОтбораПоПериодуНачало);
	
	ТекстЗапроса = ЗатратыСервер.ТекстЗапросаПроизводственныеЗатратыПостатейные();
	
	СхемаКомпоновкиДанных.НаборыДанных.Основной.Запрос = ТекстЗапроса.ТекстЗапроса;
	
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);
	
	ЗатратыСервер.ДобавитьОтборПоВыбраннымРесурсам(ТекстЗапроса.Ресурсы, МакетКомпоновки.НаборыДанных.Основной);
	
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , ДанныеРасшифровки, Истина, Ложь);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	ТабличныйДокумент = ПроцессорВывода.ЗакончитьВывод();
	
	// удалим служебные тексты
	
	ЗаменяемыйТекстРасширенный = "%СлужебныйОтбор% И
						|";
	ЗаменяемыйТекстБезРасширения = "%СлужебныйОтбор%";
	
	ОбластьРасширенная = ТабличныйДокумент.НайтиТекст(ЗаменяемыйТекстРасширенный);
	ОбластьБезРасширения = ТабличныйДокумент.НайтиТекст(ЗаменяемыйТекстБезРасширения);
	Если ОбластьРасширенная <> Неопределено Тогда
		ОбластьРасширенная.Текст = СтрЗаменить(ОбластьРасширенная.Текст, ЗаменяемыйТекстРасширенный, "");
	ИначеЕсли ОбластьБезРасширения <> Неопределено Тогда
		ОбластьБезРасширения.Текст = СтрЗаменить(ОбластьБезРасширения.Текст, ЗаменяемыйТекстБезРасширения, "");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Настраивает доступные значения у параметра компоновки данных "Данные по себестоимости" в зависимости от варианта.
// 
// Параметры:
//	Документ - ДокументСсылка.РаспределениеПрочихЗатрат - расшифровывающийся документ.
//	НовыеНастройкиКД - НастройкиКомпоновкиДанных - переопределяемые настройки отчета.
//	НовыеПользовательскиеНастройкиКД - ПользовательскиеНастройкиКомпоновкиДанных - настройки схемы компоновки данных.
Процедура НастроитьПараметрыОтчетаПоВариантуОтчета(НастройкиОтчета, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД)
		
	КлючиВариантов = КлючиПредопределенныхВариантов();
	ПредопределенныйВариант = ПолучитьПредопределенныйВариант(НастройкиОтчета.ВариантСсылка);
	КлючПредопределенногоВарианта = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПредопределенныйВариант, "КлючВарианта");
	Если Не ЗначениеЗаполнено(ПредопределенныйВариант) 
		Или КлючиВариантов.Найти(КлючПредопределенногоВарианта) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрДанныеОтчета = СхемаКомпоновкиДанных.Параметры.Найти("ДанныеПоСебестоимости");
	ПараметрПоПредприятию = СхемаКомпоновкиДанных.Параметры.Найти("ПоПредприятию");
	
	СписокВыбора = Новый СписокЗначений;
	
	Если КлючПредопределенногоВарианта = "ДвижениеПостатейныхРасходовПоПредприятию" Тогда
		
		СписокВыбора.Добавить(1, НСтр("ru = 'В валюте упр. учета с НДС';
										|en = 'In management accounting currency, including VAT'"));
		СписокВыбора.Добавить(2, НСтр("ru = 'В валюте упр. учета без НДС';
										|en = 'In management accounting currency, excluding VAT'"));
		ПараметрПоПредприятию.Значение = Истина;
		
	Иначе
		
		Если РасчетСебестоимостиПовтИсп.УправленческийУчетОрганизаций() Тогда
			СписокВыбора.Добавить(3, НСтр("ru = 'В валюте упр. учета';
											|en = 'In management accounting currency'"));
		КонецЕсли;
		СписокВыбора.Добавить(4, НСтр("ru = 'В валюте регл. учета';
										|en = 'In local accounting currency'"));
		ПараметрПоПредприятию.Значение = Ложь;
		
	КонецЕсли;
	
	ПараметрДанныеОтчета.УстановитьДоступныеЗначения(СписокВыбора);
	
	Если НовыеНастройкиКД = Неопределено
		Или НовыеПользовательскиеНастройкиКД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗначениеПараметраДанныеОтчета = НовыеНастройкиКД.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ДанныеПоСебестоимости"));
	НастройкаДанныеОтчета = НовыеПользовательскиеНастройкиКД.Элементы.Найти(ЗначениеПараметраДанныеОтчета.ИдентификаторПользовательскойНастройки);
	
	Если Не НастройкаДанныеОтчета = Неопределено
		И СписокВыбора.НайтиПоЗначению(НастройкаДанныеОтчета.Значение) = Неопределено Тогда
		НастройкаДанныеОтчета.Значение = СписокВыбора[0].Значение;
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьПредопределенныйВариант(Знач Вариант)
	
	КлючиВариантов = КлючиПредопределенныхВариантов();
	
	// Если вариант не задан, то возьмем первый из списка по умолчанию
	Если Вариант = Неопределено Тогда
		Возврат Новый Структура("КлючВарианта", КлючиВариантов[0]);
	КонецЕсли;
	
	Пока КлючиВариантов.Найти(Вариант.КлючВарианта) = Неопределено
		И ЗначениеЗаполнено(Вариант.Родитель) Цикл
		Вариант = Вариант.Родитель;
	КонецЦикла;
	
	Возврат Вариант;
	
КонецФункции

Функция КлючиПредопределенныхВариантов()
	
	КлючиВариантов = Новый Массив;
	КлючиВариантов.Добавить("ДвижениеПостатейныхРасходов");
	КлючиВариантов.Добавить("ДвижениеПостатейныхРасходовПоПредприятию");
	
	Возврат КлючиВариантов;
	
КонецФункции

Процедура НастроитьПараметрыОтборыПоФункциональнымОпциям(КомпоновщикНастроекФормы)
	
	Если ПолучитьФункциональнуюОпцию("КомплекснаяАвтоматизация") Тогда
		КомпоновкаДанныхСервер.УдалитьЭлементОтбораИзВсехНастроекОтчета(КомпоновщикНастроекФормы, "ЗаказНаПроизводство");
	КонецЕсли;
	
КонецПроцедуры

Процедура НастроитьПользовательскиеНастройкиПоФункциональнымОпциям(КомпоновщикНастроекФормы)
	
	ИспользоватьУчетПоОбъектамВозникновенияЗатрат = Ложь;
	//++ НЕ УТ
	ИспользоватьУчетПоОбъектамВозникновенияЗатрат = ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОбъектамВозникновенияЗатрат");
	//-- НЕ УТ
	Если Не ИспользоватьУчетПоОбъектамВозникновенияЗатрат Тогда
		КомпоновкаДанныхСервер.УдалитьВыбранноеПолеИзВсехНастроекОтчета(КомпоновщикНастроекФормы,
			"ТранзитМеждуОВЗСтоимость");
		КомпоновкаДанныхСервер.УдалитьВыбранноеПолеИзВсехНастроекОтчета(КомпоновщикНастроекФормы,
			"ТранзитМеждуОВЗПР");
		КомпоновкаДанныхСервер.УдалитьВыбранноеПолеИзВсехНастроекОтчета(КомпоновщикНастроекФормы,
			"ТранзитМеждуОВЗВР");
	КонецЕсли;
	
	ВедетсяУчетПостоянныхИВременныхРазниц = Ложь; 
	//++ НЕ УТ
	
	//++ Локализация
	ВедетсяУчетПостоянныхИВременныхРазниц = Истина;
	//-- Локализация
	
	//-- НЕ УТ
	Если НЕ ВедетсяУчетПостоянныхИВременныхРазниц Тогда
		УдаляемыеПоля = Новый Массив;
		УдаляемыеПоля.Добавить("НачальныйОстатокНУ");
		УдаляемыеПоля.Добавить("НачальныйОстатокВР");
		УдаляемыеПоля.Добавить("НачальныйОстатокПР");
		УдаляемыеПоля.Добавить("ПоступилоНУ");
		УдаляемыеПоля.Добавить("ПоступилоВР");
		УдаляемыеПоля.Добавить("ПоступилоПР");
		УдаляемыеПоля.Добавить("ИзрасходованоНаПроизводствоНУ");
		УдаляемыеПоля.Добавить("ИзрасходованоНаПроизводствоВР");
		УдаляемыеПоля.Добавить("ИзрасходованоНаПроизводствоПР");
		УдаляемыеПоля.Добавить("ПрочееСписаниеНУ");
		УдаляемыеПоля.Добавить("ПрочееСписаниеВР");
		УдаляемыеПоля.Добавить("ПрочееСписаниеПР");
		УдаляемыеПоля.Добавить("ТранзитМеждуОВЗНУ");
		УдаляемыеПоля.Добавить("ТранзитМеждуОВЗВР");
		УдаляемыеПоля.Добавить("ТранзитМеждуОВЗПР");
		УдаляемыеПоля.Добавить("КонечныйОстатокНУ");
		УдаляемыеПоля.Добавить("КонечныйОстатокВР");
		УдаляемыеПоля.Добавить("КонечныйОстатокПР");
		
		РасчетСебестоимостиПрикладныеАлгоритмы.ОграничитьИспользованиеПолейСКД(УдаляемыеПоля, СхемаКомпоновкиДанных,
			КомпоновщикНастроек, "", Истина);
		КомпоновкаДанныхСервер.УдалитьЭлементОтбораИзВсехНастроекОтчета(КомпоновщикНастроекФормы, УдаляемыеПоля);
		КомпоновкаДанныхСервер.УдалитьВыбранноеПолеИзВсехНастроекОтчета(КомпоновщикНастроекФормы, УдаляемыеПоля, Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли