#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Формирует отчет в табличном документе по указанному объекту основного средства
//
// Параметры:
// 		ТабличныйДокумент - ТабличныйДокумент - Табличный документ, в котором необходимо сформировать отчет
// 		Список - Массив, СписокЗначений - список объектов типа <СправочникСсылка.ОбъектыЭксплуатации> по которым необходимо сформировать отчет
// 		ТолькоРазличия - Булево - Признак формирования отчета только по различиям.
//
Процедура СформироватьОтчет(ТабличныйДокумент, Список, ТолькоРазличия=Ложь) Экспорт
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.Очистить();
	
	РеквизитыСравнения = РеквизитыСравнения();
	
	Запрос = Новый Запрос(ТекстЗапросаСравнения(РеквизитыСравнения));
	Запрос.УстановитьПараметр("Список", Список);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Макет = ЭтотОбъект.ПолучитьМакет("Макет");
	
	ОблЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОблОбъект = Макет.ПолучитьОбласть("Объект");
	ОблРаздел = Макет.ПолучитьОбласть("Раздел");
	ОблРеквизит = Макет.ПолучитьОбласть("Реквизит");
	ОблРеквизитРазличные = Макет.ПолучитьОбласть("РеквизитРазличные");
	
	ТабличныйДокумент.Вывести(ОблЗаголовок);
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если ТолькоРазличия И Не Выборка.ЕстьРазличия Тогда
			Продолжить;
		КонецЕсли;
		
		ОблОбъект.Параметры.Заполнить(Выборка);
		ТабличныйДокумент.Вывести(ОблОбъект);
		
		ТабличныйДокумент.НачатьГруппуСтрок();
		
		Для Каждого Раздел Из РеквизитыСравнения Цикл
			
			ОблРаздел.Параметры.Заполнить(Раздел);
			ТабличныйДокумент.Вывести(ОблРаздел);
			
			ТабличныйДокумент.НачатьГруппуСтрок();
			
			Для Каждого Реквизит Из Раздел.Реквизиты Цикл
				Обл = ?(Выборка[Реквизит.Имя + "Сравнение"], ОблРеквизит, ОблРеквизитРазличные);
				Обл.Параметры.Синоним = Реквизит.Синоним;
				Обл.Параметры.ЗначениеБУ = Формат(Выборка[Реквизит.Имя + "БУ"], Реквизит.Формат);
				Обл.Параметры.ЗначениеМФУ = Формат(Выборка[Реквизит.Имя + "МФУ"], Реквизит.Формат);
				ТабличныйДокумент.Вывести(Обл);
			КонецЦикла;
			
			ТабличныйДокумент.ЗакончитьГруппуСтрок();
			
		КонецЦикла;
		
		ТабличныйДокумент.ЗакончитьГруппуСтрок();
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция РеквизитыСравнения()
	
	ОписанияРазделов = Новый Массив;
	
	ОписаниеРаздела = Новый Структура;
	ОписаниеРаздела.Вставить("Синоним", НСтр("ru = 'Основные сведения';
											|en = 'Main data'"));
	ОписаниеРаздела.Вставить("Реквизиты", Новый Массив);
	ОписаниеРаздела.Реквизиты.Добавить(ОписаниеРеквизита("Состояние", НСтр("ru = 'Состояние';
																			|en = 'Status'")));
	ОписаниеРаздела.Реквизиты.Добавить(ОписаниеРеквизита("Организация", НСтр("ru = 'Организация';
																			|en = 'Company'")));
	ОписаниеРаздела.Реквизиты.Добавить(ОписаниеРеквизита("ИнвентарныйНомер", НСтр("ru = 'Инвентарный номер';
																					|en = 'Inventory number'")));
	ОписаниеРаздела.Реквизиты.Добавить(ОписаниеРеквизита("Группа", НСтр("ru = 'Группа ОС';
																		|en = 'Fixed assets group'"), Ложь));
	ОписаниеРаздела.Реквизиты.Добавить(ОписаниеРеквизита("ДатаПринятияКУчету", НСтр("ru = 'Дата принятия к учету';
																					|en = 'Date of recognition'"),,"ДЛФ=DD"));
	ОписаниеРаздела.Реквизиты.Добавить(ОписаниеРеквизита("СчетУчета", НСтр("ru = 'Счет учета';
																			|en = 'Ledger account'"), Ложь));
	ОписаниеРаздела.Реквизиты.Добавить(ОписаниеРеквизита("ПервоначальнаяСтоимость", НСтр("ru = 'Первоначальная стоимость';
																						|en = 'Initial cost'"),, "ЧДЦ=2"));
	ОписаниеРаздела.Реквизиты.Добавить(ОписаниеРеквизита("ЛиквидационнаяСтоимость", НСтр("ru = 'Ликвидационная стоимость';
																						|en = 'Residual value'"), Ложь, "ЧДЦ=2"));
	ОписанияРазделов.Добавить(ОписаниеРаздела);
	
	ОписаниеРаздела = Новый Структура;
	ОписаниеРаздела.Вставить("Синоним", НСтр("ru = 'Параметры амортизации';
											|en = 'Depreciation parameters'"));
	ОписаниеРаздела.Вставить("Реквизиты", Новый Массив);
	ОписаниеРаздела.Реквизиты.Добавить(ОписаниеРеквизита("МетодНачисленияАмортизации", НСтр("ru = 'Метод начисления';
																							|en = 'Depreciation method'")));
	ОписаниеРаздела.Реквизиты.Добавить(ОписаниеРеквизита("СчетАмортизации", НСтр("ru = 'Счет начисления';
																				|en = 'Accrual account'"), Ложь));
	ОписаниеРаздела.Реквизиты.Добавить(ОписаниеРеквизита("СрокИспользования", НСтр("ru = 'Срок полезного использования';
																					|en = 'Useful life'")));
	ОписаниеРаздела.Реквизиты.Добавить(ОписаниеРеквизита("ПоказательНаработки", НСтр("ru = 'Показатель наработки';
																					|en = 'Asset activity measure'")));
	ОписаниеРаздела.Реквизиты.Добавить(ОписаниеРеквизита("ОбъемНаработки", НСтр("ru = 'Предполагаемый объем наработки (продукции или выполненных работ)';
																				|en = 'Estimated running time (of products or performed works)'"),, "ЧДЦ=2"));
	ОписанияРазделов.Добавить(ОписаниеРаздела);
	
	Возврат ОписанияРазделов;
	
КонецФункции

Функция ОписаниеРеквизита(Имя, Синоним, Сравнивать=Истина, Формат="")
	
	ОписаниеРеквизита = Новый Структура;
	ОписаниеРеквизита.Вставить("Имя", Имя);
	ОписаниеРеквизита.Вставить("Синоним", Синоним);
	ОписаниеРеквизита.Вставить("Сравнивать", Сравнивать);
	ОписаниеРеквизита.Вставить("Формат", Формат);
	
	Возврат ОписаниеРеквизита;
	
КонецФункции

Функция ТекстЗапросаСравнения(РеквизитыСравнения)
	
	ТекстЗапроса = 
		ТекстЗапросаДанныеБУ()
		+ ТекстЗапросаДанныеМФУ()
		+ "
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Объекты.Ссылка КАК ОсновноеСредство,
		|	Объекты.Наименование КАК Наименование,
		|	ВЫБОР
		|		КОГДА ЛОЖЬ
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЕстьРазличия
		|ИЗ
		|	Справочник.ОбъектыЭксплуатации КАК Объекты
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеБУ КАК ДанныеБУ
		|		ПО Объекты.Ссылка = ДанныеБУ.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеМФУ КАК ДанныеМФУ
		|		ПО Объекты.Ссылка = ДанныеМФУ.ОсновноеСредство
		|ГДЕ
		|	Объекты.Ссылка В(&Список)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Наименование";
	
	ДопПоля = "";
	
	ДопУсловия = "";
	
	Для Каждого Раздел Из РеквизитыСравнения Цикл
		
		Для Каждого Реквизит Из Раздел.Реквизиты Цикл
			
			ДопПоля = ДопПоля + СтрЗаменить(
				",
				|	ДанныеБУ.% КАК %БУ,
				|	ДанныеМФУ.% КАК %МФУ",
				"%",
				Реквизит.Имя
			);
			
			Если Реквизит.Сравнивать Тогда
				ДопПоля = ДопПоля + СтрЗаменить(
					",
					|	ВЫБОР КОГДА ЕСТЬNULL(ДанныеБУ.%, НЕОПРЕДЕЛЕНО) = ЕСТЬNULL(ДанныеМФУ.%, НЕОПРЕДЕЛЕНО) ТОГДА ИСТИНА ИНАЧЕ ЛОЖЬ КОНЕЦ КАК %Сравнение",
					"%",
					Реквизит.Имя
				);
				ДопУсловия = ДопУсловия + СтрЗаменить(
					"
					|			ИЛИ ЕСТЬNULL(ДанныеБУ.%, НЕОПРЕДЕЛЕНО) <> ЕСТЬNULL(ДанныеМФУ.%, НЕОПРЕДЕЛЕНО)",
					"%",
					Реквизит.Имя
				);
			Иначе
				ДопПоля = ДопПоля + СтрЗаменить(
					",
					|	ИСТИНА КАК %Сравнение",
					"%",
					Реквизит.Имя
				);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "КАК Наименование", "КАК Наименование" + ДопПоля);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "КОГДА ЛОЖЬ", "КОГДА ЛОЖЬ" + ДопУсловия);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДанныеБУ()
	
	Возврат
	"ВЫБРАТЬ
	|	ПервоначальныеСведения.ОсновноеСредство КАК Актив,
	|	ПервоначальныеСведения.Организация КАК Организация,
	|	ПервоначальныеСведения.ОсновноеСредство.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	ПервоначальныеСведения.ОсновноеСредство.ГруппаОС КАК ГруппаОС,
	|	ПервоначальныеСведения.ПервоначальнаяСтоимостьБУ КАК ПервоначальнаяСтоимость,
	|	ПервоначальныеСведения.МетодНачисленияАмортизацииБУ КАК МетодНачисленияАмортизации
	|ПОМЕСТИТЬ ПервоначальныеСведения
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияОС.СрезПоследних(, ДатаИсправления = ДАТАВРЕМЯ(1,1,1) И ОсновноеСредство В (&Список)) КАК ПервоначальныеСведения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Актив,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТекущиеСостояния.ОсновноеСредство КАК Актив,
	|	ТекущиеСостояния.СостояниеБУ КАК Состояние
	|ПОМЕСТИТЬ ТекущиеСостояния
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОСБУ.СрезПоследних(
	|			,
	|			(Организация, ОсновноеСредство) В
	|				(ВЫБРАТЬ
	|					ПервоначальныеСведения.Организация,
	|					ПервоначальныеСведения.Актив
	|				ИЗ
	|					ПервоначальныеСведения КАК ПервоначальныеСведения)) КАК ТекущиеСостояния
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТекущиеСостояния.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СостоянияПринятия.ОсновноеСредство КАК Актив,
	|	НАЧАЛОПЕРИОДА(СостоянияПринятия.Период, ДЕНЬ) КАК Дата
	|ПОМЕСТИТЬ ПринятияКУчету
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОСБУ.СрезПоследних(
	|			,
	|			(Организация, ОсновноеСредство, СостояниеБУ) В
	|				(ВЫБРАТЬ
	|					ПервоначальныеСведения.Организация,
	|					ПервоначальныеСведения.Актив,
	|					ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету) КАК Состояние
	|				ИЗ
	|					ПервоначальныеСведения КАК ПервоначальныеСведения)) КАК СостоянияПринятия
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СостоянияПринятия.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПорядокУчетаОС.ОсновноеСредство КАК Актив,
	|	ПорядокУчетаОС.ПоказательНаработки КАК ПоказательНаработки,
	|	ЕСТЬNULL(СчетУчетаСтоимости.СчетУчета, ПорядокУчетаОС.СчетУчета) КАК СчетУчета,
	|	ЕСТЬNULL(СчетУчетаСтоимости.СчетУчета, ПорядокУчетаОС.СчетНачисленияАмортизации) КАК СчетНачисленияАмортизации
	|ПОМЕСТИТЬ ПорядокУчетаОС
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОС.СрезПоследних(
	|			,
	|			ОсновноеСредство В
	|				(ВЫБРАТЬ
	|					ПервоначальныеСведения.Актив
	|				ИЗ
	|					ПервоначальныеСведения КАК ПервоначальныеСведения)) КАК ПорядокУчетаОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияНаСчетахУчета КАК СчетУчетаСтоимости
	|		ПО СчетУчетаСтоимости.ВидСчета = ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СтоимостьВНА)
	|		И СчетУчетаСтоимости.АналитикаУчета = ПорядокУчетаОС.ГруппаФинансовогоУчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияНаСчетахУчета КАК СчетУчетаАмортизации
	|		ПО СчетУчетаАмортизации.ВидСчета = ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АмортизацияВНА)
	|		И СчетУчетаАмортизации.АналитикаУчета = ПорядокУчетаОС.ГруппаФинансовогоУчета
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПорядокУчетаОС.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПараметрыАмортизации.ОсновноеСредство КАК Актив,
	|	ПараметрыАмортизации.СрокИспользованияДляВычисленияАмортизации КАК СрокИспользования,
	|	ПараметрыАмортизации.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ОбъемНаработки
	|ПОМЕСТИТЬ ПараметрыАмортизации
	|ИЗ
	|	РегистрСведений.ПараметрыАмортизацииОСБУ.СрезПоследних(
	|			,
	|			ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|			И (Организация, ОсновноеСредство) В
	|				(ВЫБРАТЬ
	|					ПервоначальныеСведения.Организация,
	|					ПервоначальныеСведения.Актив
	|				ИЗ
	|					ПервоначальныеСведения КАК ПервоначальныеСведения)) КАК ПараметрыАмортизации
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПараметрыАмортизации.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Активы.Ссылка КАК ОсновноеСредство,
	|	ЕСТЬNULL(ТекущиеСостояния.Состояние, ЗНАЧЕНИЕ(Перечисление.СостоянияОС.НеПринятоКУчету)) КАК Состояние,
	|	ПервоначальныеСведения.Организация КАК Организация,
	|	ПервоначальныеСведения.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	ПервоначальныеСведения.МетодНачисленияАмортизации КАК МетодНачисленияАмортизации,
	|	ПорядокУчетаОС.СчетУчета КАК СчетУчета,
	|	ПорядокУчетаОС.СчетНачисленияАмортизации КАК СчетАмортизации,
	|	ПринятияКУчету.Дата КАК ДатаПринятияКУчету,
	|	ПервоначальныеСведения.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость,
	|	0 КАК ЛиквидационнаяСтоимость,
	|	ПараметрыАмортизации.СрокИспользования КАК СрокИспользования,
	|	ПорядокУчетаОС.ПоказательНаработки КАК ПоказательНаработки,
	|	ПараметрыАмортизации.ОбъемНаработки КАК ОбъемНаработки,
	|	ПервоначальныеСведения.ГруппаОС КАК Группа
	|ПОМЕСТИТЬ ДанныеБУ
	|ИЗ
	|	Справочник.ОбъектыЭксплуатации КАК Активы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПервоначальныеСведения КАК ПервоначальныеСведения
	|		ПО Активы.Ссылка = ПервоначальныеСведения.Актив
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТекущиеСостояния КАК ТекущиеСостояния
	|		ПО Активы.Ссылка = ТекущиеСостояния.Актив
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПринятияКУчету КАК ПринятияКУчету
	|		ПО Активы.Ссылка = ПринятияКУчету.Актив
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОС КАК ПорядокУчетаОС
	|		ПО Активы.Ссылка = ПорядокУчетаОС.Актив
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизации КАК ПараметрыАмортизации
	|		ПО Активы.Ссылка = ПараметрыАмортизации.Актив
	|ГДЕ
	|	Активы.Ссылка В(&Список)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПервоначальныеСведения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТекущиеСостояния
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПринятияКУчету
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПорядокУчетаОС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПараметрыАмортизации" + ";";
	
КонецФункции

Функция ТекстЗапросаДанныеМФУ()
	
	Возврат
	"ВЫБРАТЬ
	|	ТекущиеСостояния.ОсновноеСредство КАК Актив,
	|	ТекущиеСостояния.Организация,
	|	ТекущиеСостояния.ИнвентарныйНомер,
	|	ТекущиеСостояния.СчетУчета,
	|	ТекущиеСостояния.ВидАктива,
	|	ТекущиеСостояния.ЛиквидационнаяСтоимость,
	|	ТекущиеСостояния.ПорядокУчета,
	|	ТекущиеСостояния.МетодНачисленияАмортизации,
	|	ТекущиеСостояния.СчетАмортизации,
	|	ТекущиеСостояния.СрокИспользования,
	|	ТекущиеСостояния.ПоказательНаработки,
	|	ТекущиеСостояния.ОбъемНаработки,
	|	ЕСТЬNULL(ТекущиеСостояния.Состояние, ЗНАЧЕНИЕ(Перечисление.СостоянияОС.НеПринятоКУчету)) КАК Состояние,
	|	ЕСТЬNULL(СостоянияСписания.Период, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаДоПринятияКУчету
	|ПОМЕСТИТЬ ТекущиеСостояния
	|ИЗ
	|	РегистрСведений.ОсновныеСредстваМеждународныйУчет.СрезПоследних(, ОсновноеСредство В (&Список)) КАК ТекущиеСостояния
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСредстваМеждународныйУчет.СрезПоследних(
	|				,
	|				ОсновноеСредство В (&Список)
	|					И Состояние <> ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)) КАК СостоянияСписания
	|		ПО ТекущиеСостояния.ОсновноеСредство = СостоянияСписания.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Актив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ДвиженияПринятияКУчету.Период) КАК ДатаПринятияКУчету,
	|	ДвиженияПринятияКУчету.ОсновноеСредство КАК Актив
	|ПОМЕСТИТЬ ПринятияКУчету
	|ИЗ
	|	ТекущиеСостояния КАК ТекущиеСостояния
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСредстваМеждународныйУчет КАК ДвиженияПринятияКУчету
	|		ПО ТекущиеСостояния.Актив = ДвиженияПринятияКУчету.ОсновноеСредство
	|			И ТекущиеСостояния.ДатаДоПринятияКУчету < ДвиженияПринятияКУчету.Период
	|ГДЕ
	|	ДвиженияПринятияКУчету.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДвиженияПринятияКУчету.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Актив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетУчета.Субконто1 КАК Актив,
	|	СчетУчета.СуммаОстаток КАК Стоимость
	|ПОМЕСТИТЬ ПервоначальнаяСтоимость
	|ИЗ
	|	РегистрБухгалтерии.Международный.Остатки(
	|			,
	|			Счет В
	|				(ВЫБРАТЬ
	|					ТекущиеСостояния.СчетУчета
	|				ИЗ
	|					ТекущиеСостояния КАК ТекущиеСостояния),
	|			,
	|			(Организация, Субконто1) В
	|				(ВЫБРАТЬ
	|					ТекущиеСостояния.Организация,
	|					ТекущиеСостояния.Актив
	|				ИЗ
	|					ТекущиеСостояния КАК ТекущиеСостояния)) КАК СчетУчета
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Актив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Активы.Ссылка КАК ОсновноеСредство,
	|	ЕСТЬNULL(ТекущиеСостояния.Состояние, ЗНАЧЕНИЕ(Перечисление.СостоянияОС.НеПринятоКУчету)) КАК Состояние,
	|	ТекущиеСостояния.Организация КАК Организация,
	|	ТекущиеСостояния.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	ТекущиеСостояния.СчетУчета КАК СчетУчета,
	|	ТекущиеСостояния.МетодНачисленияАмортизации КАК МетодНачисленияАмортизации,
	|	ТекущиеСостояния.СчетАмортизации КАК СчетАмортизации,
	|	НАЧАЛОПЕРИОДА(ПринятияКУчету.ДатаПринятияКУчету, ДЕНЬ) КАК ДатаПринятияКУчету,
	|	ПервоначальнаяСтоимость.Стоимость КАК ПервоначальнаяСтоимость,
	|	ТекущиеСостояния.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость,
	|	ТекущиеСостояния.СрокИспользования КАК СрокИспользования,
	|	ТекущиеСостояния.ПоказательНаработки КАК ПоказательНаработки,
	|	ТекущиеСостояния.ОбъемНаработки КАК ОбъемНаработки,
	|	Активы.ГруппаОСМеждународныйУчет КАК Группа
	|ПОМЕСТИТЬ ДанныеМФУ
	|ИЗ
	|	Справочник.ОбъектыЭксплуатации КАК Активы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТекущиеСостояния КАК ТекущиеСостояния
	|		ПО Активы.Ссылка = ТекущиеСостояния.Актив
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПринятияКУчету КАК ПринятияКУчету
	|		ПО Активы.Ссылка = ПринятияКУчету.Актив
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость
	|		ПО Активы.Ссылка = ПервоначальнаяСтоимость.Актив
	|ГДЕ
	|	Активы.Ссылка В(&Список)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТекущиеСостояния
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПринятияКУчету
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПервоначальнаяСтоимость"+";";
	
КонецФункции

#КонецОбласти

#КонецЕсли