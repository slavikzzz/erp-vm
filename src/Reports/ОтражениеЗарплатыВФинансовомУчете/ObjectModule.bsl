#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

Перем Организация, НачалоПериода, КонецПериода;

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПользовательскиеНастройкиМодифицированы = Ложь;
	
	УстановитьОбязательныеНастройки(ПользовательскиеНастройкиМодифицированы);
	
	// Подготовим и выведем отчет.
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	
	НастройкиКомпоновкиДанных = КомпоновщикНастроек.ПолучитьНастройки();
	
	НастроитьСоставКолонок(НастройкиКомпоновкиДанных);
	
	НаборыДанных = ИнициализироватьВнешнийНаборДанных();
	
	МакетКомпоновки = КомпоновщикМакета.Выполнить(
		СхемаКомпоновкиДанных,
		НастройкиКомпоновкиДанных,
		ДанныеРасшифровки);
		
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, НаборыДанных, ДанныеРасшифровки, Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.НачатьВывод();
	ПроцессорВывода.Вывести(ПроцессорКомпоновки, Истина);
	ПроцессорВывода.ЗакончитьВывод();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Для общей формы "Форма отчета" подсистемы "Варианты отчетов".
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
КонецПроцедуры

Процедура УстановитьОбязательныеНастройки(ПользовательскиеНастройкиМодифицированы)
	
	Организация = Справочники.Организации.ПустаяСсылка();
	
	ПараметрОрганизация = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "Организация");
	Если ПараметрОрганизация <> Неопределено Тогда
		Если ПараметрОрганизация.Использование Тогда
			Организация = ПараметрОрганизация.Значение;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		СтрокаСообщения = НСтр("ru = 'Не установлено значение параметра ""Организация"".';
								|en = 'Value of the ""Company"" parameter is not set.'");
		ВызватьИсключение СтрокаСообщения;
	КонецЕсли;
	
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	ЗначениеПараметра = НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Период"));
	Если ЗначениеПараметра <> Неопределено Тогда
		НачалоПериода = Дата(ЗначениеПараметра.Значение.ДатаНачала);
		КонецПериода  = Дата(ЗначениеПараметра.Значение.ДатаОкончания);
	КонецЕсли;
	
	Если НачалоПериода = '00010101' Тогда
		НачалоПериода = НачалоМесяца(ТекущаяДатаСеанса());
	КонецЕсли;
	Если КонецПериода = '00010101' Тогда
		КонецПериода = КонецМесяца(ТекущаяДатаСеанса());
	КонецЕсли;
	
	Валюта = Константы.ВалютаРегламентированногоУчета.Получить();
	ПараметрВалюта = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "Валюта");
	Если ПараметрВалюта.Значение <> Валюта Тогда
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "Валюта", Валюта);
		ПользовательскиеНастройкиМодифицированы = Истина;
	КонецЕсли;
	
	// Сообщим форме отчета, что настройки модифицированы
	Если ПользовательскиеНастройкиМодифицированы Тогда
		КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ПользовательскиеНастройкиМодифицированы", Истина);
	КонецЕсли;
	
КонецПроцедуры

Функция ИнициализироватьВнешнийНаборДанных()
	
	СтрокаСписокТаблиц = "НачисленнаяЗарплатаИВзносы, НачисленныйНДФЛ, УдержаннаяЗарплата, НачисленныеПроцентыПоЗаймам";
	СписокТаблиц = Новый СписокЗначений;
	СписокТаблиц.Добавить("НачисленнаяЗарплатаИВзносы");
	СписокТаблиц.Добавить("НачисленныйНДФЛ");
	СписокТаблиц.Добавить("УдержаннаяЗарплата");
	СписокТаблиц.Добавить("НачисленныеПроцентыПоЗаймам");
	
	ТекущийМесяц = НачалоПериода;
	
	ИнициализацияТаблиц = Истина;
	
	РезультирующаяТаблицаНачислений = Неопределено;
	РезультирующаяТаблицаНДФЛ       = Неопределено;
	РезультирующаяТаблицаУдержаний  = Неопределено;
	РезультирующаяТаблицаПроцентов  = Неопределено;
	
	Пока ТекущийМесяц <= НачалоМесяца(КонецПериода) Цикл
		
		СтруктураПараметров = ОтражениеЗарплатыВБухучетеРасширенный.ПараметрыДляЗаполненияТаблицДокумента();
		СтруктураПараметров.Организация 		= Организация;
		СтруктураПараметров.ПериодРегистрации 	= ТекущийМесяц;
		
		СтруктураПараметров.Вставить("СтрокаСписокТаблиц", СтрокаСписокТаблиц);
		
		АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено);
		
		Документы.ОтражениеЗарплатыВФинансовомУчете.ПодготовитьДанныеДляЗаполнения(СтруктураПараметров, АдресХранилища);
		
		Параметры = Новый Структура;
		Параметры.Вставить("АдресХранилища",          АдресХранилища);
		Параметры.Вставить("СтрокаСписокТаблиц",      СтрокаСписокТаблиц);
		Параметры.Вставить("СписокТаблиц",            СписокТаблиц);
		Параметры.Вставить("ЗаполнятьАналитики",      Ложь);
		Параметры.Вставить("ТекстОповещения",         );
		Параметры.Вставить("ПояснениеОповещения",     );
		Параметры.Вставить("ЕстьДанныеДляЗаполнения", Ложь);
		Параметры.Вставить("Сводно",                  Ложь);
		
		Результат = ТаблицыОтражения();
		
		Документы.ОтражениеЗарплатыВФинансовомУчете.ЗаполнениеПослеВыполненияДлительнойОперации(Результат, Параметры, Ложь);
		
		Если ИнициализацияТаблиц Тогда
			
			РезультирующаяТаблицаНачислений     = Результат.НачисленнаяЗарплатаИВзносыПоФизлицам;
			РезультирующаяТаблицаНДФЛ           = Результат.НачисленныйНДФЛ;
			РезультирующаяТаблицаУдержаний      = Результат.УдержаннаяЗарплата;
			РезультирующаяТаблицаПроцентов      = Результат.НачисленныеПроцентыПоЗаймам;
			
			ИнициализацияТаблиц = Ложь;
			
		Иначе
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(Результат.НачисленнаяЗарплатаИВзносыПоФизлицам,   РезультирующаяТаблицаНачислений);
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(Результат.НачисленныйНДФЛ,       РезультирующаяТаблицаНДФЛ);
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(Результат.УдержаннаяЗарплата,    РезультирующаяТаблицаУдержаний);
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(Результат.НачисленныеПроцентыПоЗаймам,    РезультирующаяТаблицаПроцентов);
		КонецЕсли;
		
		ТекущийМесяц = ДобавитьМесяц(ТекущийМесяц, 1);
		
	КонецЦикла;

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Документы.Ссылка КАК Ссылка,
		|	Документы.Сводно КАК Сводно,
		|	Документы.Организация
		|ПОМЕСТИТЬ ВТДокументы
		|ИЗ
		|	Документ.ОтражениеЗарплатыВФинансовомУчете КАК Документы
		|ГДЕ
		|	Документы.Проведен
		|	И Документы.ПериодРегистрации МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Документы.Организация = &Организация
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	&Организация,
		|	НачисленнаяЗарплата.ОблагаетсяЕНВД,
		|	НачисленнаяЗарплата.ПодразделениеПредприятия,
		|	НачисленнаяЗарплата.СпособОтраженияЗарплатыВБухучете,
		|	НачисленнаяЗарплата.ФизическоеЛицо,
		|	НачисленнаяЗарплата.ВидОперации,
		|	НачисленнаяЗарплата.Сумма,
		|	НачисленнаяЗарплата.ВзносыПоЕдиномуТарифу,
		|	НачисленнаяЗарплата.ПФРСтраховая,
		|	НачисленнаяЗарплата.ПФРНакопительная,
		|	НачисленнаяЗарплата.ПФРДоПредельнойВеличины,
		|	НачисленнаяЗарплата.ПФРСПревышения,
		|	НачисленнаяЗарплата.ПФРНаДоплатуЛетчикам,
		|	НачисленнаяЗарплата.ПФРНаДоплатуШахтерам,
		|	НачисленнаяЗарплата.ПФРЗаЗанятыхНаПодземныхИВредныхРаботах,
		|	НачисленнаяЗарплата.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботах,
		|	НачисленнаяЗарплата.ОПСпоМеждународнымДоговорам,
		|	НачисленнаяЗарплата.ОПСпоМеждународнымДоговорамСПревышения,
		|	НачисленнаяЗарплата.ОМСпоМеждународнымДоговорам,
		|	НачисленнаяЗарплата.ОМСпоМеждународнымДоговорамСПревышения,
		|	НачисленнаяЗарплата.ОССпоМеждународнымДоговорам,
		|	НачисленнаяЗарплата.ОССпоМеждународнымДоговорамСПревышения,
		|	НачисленнаяЗарплата.ФСС,
		|	НачисленнаяЗарплата.ФФОМС,
		|	НачисленнаяЗарплата.ТФОМС,
		|	НачисленнаяЗарплата.ФССНесчастныеСлучаи
		|ПОМЕСТИТЬ ВТНачислено
		|ИЗ
		|	&НачисленнаяЗарплата КАК НачисленнаяЗарплата
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НачисленныйНДФЛ.ПодразделениеПредприятия,
		|	НачисленныйНДФЛ.ВидОперации,
		|	НачисленныйНДФЛ.ФизическоеЛицо,
		|	НачисленныйНДФЛ.РегистрацияВНалоговомОргане,
		|	НачисленныйНДФЛ.Сумма КАК НДФЛНачислено,
		|	0 КАК НДФЛОтражено
		|ПОМЕСТИТЬ ВТНДФЛ
		|ИЗ
		|	&НачисленныйНДФЛ КАК НачисленныйНДФЛ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НачисленныеПроцентыПоЗаймам.ПодразделениеПредприятия,
		|	НачисленныеПроцентыПоЗаймам.ФизическоеЛицо,
		|	НачисленныеПроцентыПоЗаймам.Сумма КАК ПроцентыНачислено,
		|	0 КАК ПроцентыОтражено
		|ПОМЕСТИТЬ ВТПроценты
		|ИЗ
		|	&НачисленныеПроцентыПоЗаймам КАК НачисленныеПроцентыПоЗаймам
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	УдержаннаяЗарплата.ПодразделениеПредприятия,
		|	УдержаннаяЗарплата.ВидОперации,
		|	УдержаннаяЗарплата.ФизическоеЛицо,
		|	УдержаннаяЗарплата.Контрагент,
		|	УдержаннаяЗарплата.Сумма КАК УдержанияНачислено,
		|	0 КАК УдержанияОтражено
		|ПОМЕСТИТЬ ВТУдержания
		|ИЗ
		|	&УдержаннаяЗарплата КАК УдержаннаяЗарплата
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТДокументы.Организация,
		|	НачисленияПоФизлицам.ПодразделениеПредприятия,
		|	НачисленияПоФизлицам.ФизическоеЛицо,
		|	НачисленияПоФизлицам.ВидОперации,
		|	НачисленияПоФизлицам.СпособОтраженияЗарплатыВБухучете,
		|	НачисленияПоФизлицам.ОблагаетсяЕНВД,
		|	НачисленияПоФизлицам.Сумма,
		|	НачисленияПоФизлицам.ВзносыПоЕдиномуТарифу,
		|	НачисленияПоФизлицам.ПФРСтраховая,
		|	НачисленияПоФизлицам.ПФРНакопительная,
		|	НачисленияПоФизлицам.ПФРДоПредельнойВеличины,
		|	НачисленияПоФизлицам.ПФРСПревышения,
		|	НачисленияПоФизлицам.ПФРНаДоплатуЛетчикам,
		|	НачисленияПоФизлицам.ПФРНаДоплатуШахтерам,
		|	НачисленияПоФизлицам.ПФРЗаЗанятыхНаПодземныхИВредныхРаботах,
		|	НачисленияПоФизлицам.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботах,
		|	НачисленияПоФизлицам.ОПСпоМеждународнымДоговорам,
		|	НачисленияПоФизлицам.ОПСпоМеждународнымДоговорамСПревышения,
		|	НачисленияПоФизлицам.ОМСпоМеждународнымДоговорам,
		|	НачисленияПоФизлицам.ОМСпоМеждународнымДоговорамСПревышения,
		|	НачисленияПоФизлицам.ОССпоМеждународнымДоговорам,
		|	НачисленияПоФизлицам.ОССпоМеждународнымДоговорамСПревышения,
		|	НачисленияПоФизлицам.ФСС,
		|	НачисленияПоФизлицам.ФФОМС,
		|	НачисленияПоФизлицам.ТФОМС,
		|	НачисленияПоФизлицам.ФССНесчастныеСлучаи
		|ПОМЕСТИТЬ ВТОтражено
		|ИЗ
		|	ВТДокументы КАК ВТДокументы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтражениеЗарплатыВФинансовомУчете.НачисленнаяЗарплатаИВзносыПоФизлицам КАК НачисленияПоФизлицам
		|		ПО ВТДокументы.Ссылка = НачисленияПоФизлицам.Ссылка
		|ГДЕ
		|	НЕ ВТДокументы.Сводно
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТДокументы.Организация,
		|	НачисленияСводно.ПодразделениеПредприятия,
		|	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка),
		|	НачисленияСводно.ВидОперации,
		|	НачисленияСводно.СпособОтраженияЗарплатыВБухучете,
		|	НачисленияСводно.ОблагаетсяЕНВД,
		|	НачисленияСводно.Сумма,
		|	НачисленияСводно.ВзносыПоЕдиномуТарифу,
		|	НачисленияСводно.ПФРСтраховая,
		|	НачисленияСводно.ПФРНакопительная,
		|	НачисленияСводно.ПФРДоПредельнойВеличины,
		|	НачисленияСводно.ПФРСПревышения,
		|	НачисленияСводно.ПФРНаДоплатуЛетчикам,
		|	НачисленияСводно.ПФРНаДоплатуШахтерам,
		|	НачисленияСводно.ПФРЗаЗанятыхНаПодземныхИВредныхРаботах,
		|	НачисленияСводно.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботах,
		|	НачисленияСводно.ОПСпоМеждународнымДоговорам,
		|	НачисленияСводно.ОПСпоМеждународнымДоговорамСПревышения,
		|	НачисленияСводно.ОМСпоМеждународнымДоговорам,
		|	НачисленияСводно.ОМСпоМеждународнымДоговорамСПревышения,
		|	НачисленияСводно.ОССпоМеждународнымДоговорам,
		|	НачисленияСводно.ОССпоМеждународнымДоговорамСПревышения,
		|	НачисленияСводно.ФСС,
		|	НачисленияСводно.ФФОМС,
		|	НачисленияСводно.ТФОМС,
		|	НачисленияСводно.ФССНесчастныеСлучаи
		|ИЗ
		|	ВТДокументы КАК ВТДокументы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтражениеЗарплатыВФинансовомУчете.НачисленнаяЗарплатаИВзносы КАК НачисленияСводно
		|		ПО ВТДокументы.Ссылка = НачисленияСводно.Ссылка
		|ГДЕ
		|	ВТДокументы.Сводно
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТНачислено.Организация,
		|	ВТНачислено.ОблагаетсяЕНВД,
		|	ВТНачислено.ПодразделениеПредприятия,
		|	ВТНачислено.СпособОтраженияЗарплатыВБухучете,
		|	ВТНачислено.ФизическоеЛицо,
		|	ВТНачислено.ВидОперации,
		|	ВТНачислено.Сумма,
		|	ВТНачислено.ВзносыПоЕдиномуТарифу,
		|	ВТНачислено.ПФРСтраховая,
		|	ВТНачислено.ПФРНакопительная,
		|	ВТНачислено.ПФРДоПредельнойВеличины,
		|	ВТНачислено.ПФРСПревышения,
		|	ВТНачислено.ПФРНаДоплатуЛетчикам,
		|	ВТНачислено.ПФРНаДоплатуШахтерам,
		|	ВТНачислено.ПФРЗаЗанятыхНаПодземныхИВредныхРаботах,
		|	ВТНачислено.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботах,
		|	ВТНачислено.ОПСпоМеждународнымДоговорам,
		|	ВТНачислено.ОПСпоМеждународнымДоговорамСПревышения,
		|	ВТНачислено.ОМСпоМеждународнымДоговорам,
		|	ВТНачислено.ОМСпоМеждународнымДоговорамСПревышения,
		|	ВТНачислено.ОССпоМеждународнымДоговорам,
		|	ВТНачислено.ОССпоМеждународнымДоговорамСПревышения,
		|	ВТНачислено.ФСС,
		|	ВТНачислено.ФССНесчастныеСлучаи,
		|	ВТНачислено.ФФОМС,
		|	ВТНачислено.ТФОМС,
		|	0 КАК СуммаОтражено,
		|	0 КАК ВзносыПоЕдиномуТарифуОтражено,
		|	0 КАК ПФРСтраховаяОтражено,
		|	0 КАК ПФРНакопительнаяОтражено,
		|	0 КАК ПФРДоПредельнойВеличиныОтражено,
		|	0 КАК ПФРСПревышенияОтражено,
		|	0 КАК ПФРНаДоплатуЛетчикамОтражено,
		|	0 КАК ПФРНаДоплатуШахтерамОтражено,
		|	0 КАК ПФРЗаЗанятыхНаПодземныхИВредныхРаботахОтражено,
		|	0 КАК ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахОтражено,
		|	0 КАК ОПСпоМеждународнымДоговорамОтражено,
		|	0 КАК ОПСпоМеждународнымДоговорамСПревышенияОтражено,
		|	0 КАК ОМСпоМеждународнымДоговорамОтражено,
		|	0 КАК ОМСпоМеждународнымДоговорамСПревышенияОтражено,
		|	0 КАК ОССпоМеждународнымДоговорамОтражено,
		|	0 КАК ОССпоМеждународнымДоговорамСПревышенияОтражено,
		|	0 КАК ФССОтражено,
		|	0 КАК ФССНесчастныеСлучаиОтражено,
		|	0 КАК ФФОМСОтражено,
		|	0 КАК ТФОМСОтражено
		|ПОМЕСТИТЬ ВТНачисленнаяЗарплата
		|ИЗ
		|	ВТНачислено КАК ВТНачислено
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТОтражено.Организация,
		|	ВТОтражено.ОблагаетсяЕНВД,
		|	ВТОтражено.ПодразделениеПредприятия,
		|	ВТОтражено.СпособОтраженияЗарплатыВБухучете,
		|	ВТОтражено.ФизическоеЛицо,
		|	ВТОтражено.ВидОперации,
		|	0 КАК Сумма,
		|	0 КАК ВзносыПоЕдиномуТарифу,
		|	0 КАК ПФРСтраховая,
		|	0 КАК ПФРНакопительная,
		|	0 КАК ПФРДоПредельнойВеличины,
		|	0 КАК ПФРСПревышения,
		|	0 КАК ПФРНаДоплатуЛетчикам,
		|	0 КАК ПФРНаДоплатуШахтерам,
		|	0 КАК ПФРЗаЗанятыхНаПодземныхИВредныхРаботах,
		|	0 КАК ПФРЗаЗанятыхНаТяжелыхИПрочихРаботах,
		|	0 КАК ОПСпоМеждународнымДоговорам,
		|	0 КАК ОПСпоМеждународнымДоговорамСПревышения,
		|	0 КАК ОМСпоМеждународнымДоговорам,
		|	0 КАК ОМСпоМеждународнымДоговорамСПревышения,
		|	0 КАК ОССпоМеждународнымДоговорам,
		|	0 КАК ОССпоМеждународнымДоговорамСПревышения,
		|	0 КАК ФСС,
		|	0 КАК ФССНесчастныеСлучаи,
		|	0 КАК ФФОМС,
		|	0 КАК ТФОМС,
		|	ВТОтражено.Сумма,
		|	ВТОтражено.ВзносыПоЕдиномуТарифу,
		|	ВТОтражено.ПФРСтраховая,
		|	ВТОтражено.ПФРНакопительная,
		|	ВТОтражено.ПФРДоПредельнойВеличины,
		|	ВТОтражено.ПФРСПревышения,
		|	ВТОтражено.ПФРНаДоплатуЛетчикам,
		|	ВТОтражено.ПФРНаДоплатуШахтерам,
		|	ВТОтражено.ПФРЗаЗанятыхНаПодземныхИВредныхРаботах,
		|	ВТОтражено.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботах,
		|	ВТОтражено.ОПСпоМеждународнымДоговорам,
		|	ВТОтражено.ОПСпоМеждународнымДоговорамСПревышения,
		|	ВТОтражено.ОМСпоМеждународнымДоговорам,
		|	ВТОтражено.ОМСпоМеждународнымДоговорамСПревышения,
		|	ВТОтражено.ОССпоМеждународнымДоговорам,
		|	ВТОтражено.ОССпоМеждународнымДоговорамСПревышения,
		|	ВТОтражено.ФСС,
		|	ВТОтражено.ФССНесчастныеСлучаи,
		|	ВТОтражено.ФФОМС,
		|	ВТОтражено.ТФОМС
		|ИЗ
		|	ВТОтражено КАК ВТОтражено
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТНДФЛ.ПодразделениеПредприятия,
		|	ВТНДФЛ.ВидОперации,
		|	ВТНДФЛ.ФизическоеЛицо,
		|	ВТНДФЛ.РегистрацияВНалоговомОргане,
		|	ВТНДФЛ.НДФЛНачислено,
		|	ВТНДФЛ.НДФЛОтражено
		|ПОМЕСТИТЬ ВТНДФЛОбъединение
		|ИЗ
		|	ВТНДФЛ КАК ВТНДФЛ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ОтраженныйНДФЛ.ПодразделениеПредприятия,
		|	ОтраженныйНДФЛ.ВидОперации,
		|	ОтраженныйНДФЛ.ФизическоеЛицо,
		|	ОтраженныйНДФЛ.РегистрацияВНалоговомОргане,
		|	0,
		|	ОтраженныйНДФЛ.Сумма
		|ИЗ
		|	Документ.ОтражениеЗарплатыВФинансовомУчете.НачисленныйНДФЛ КАК ОтраженныйНДФЛ
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДокументы КАК ВТДокументы
		|		ПО ОтраженныйНДФЛ.Ссылка = ВТДокументы.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТПроценты.ПодразделениеПредприятия,
		|	ВТПроценты.ФизическоеЛицо,
		|	ВТПроценты.ПроцентыНачислено,
		|	ВТПроценты.ПроцентыОтражено
		|ПОМЕСТИТЬ ВТПроцентыОбъединение
		|ИЗ
		|	ВТПроценты КАК ВТПроценты
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ОтражениеЗарплатыВФинансовомУчетеНачисленныеПроцентыПоЗаймам.ПодразделениеПредприятия,
		|	ОтражениеЗарплатыВФинансовомУчетеНачисленныеПроцентыПоЗаймам.ФизическоеЛицо,
		|	0,
		|	ОтражениеЗарплатыВФинансовомУчетеНачисленныеПроцентыПоЗаймам.Сумма
		|ИЗ
		|	Документ.ОтражениеЗарплатыВФинансовомУчете.НачисленныеПроцентыПоЗаймам КАК ОтражениеЗарплатыВФинансовомУчетеНачисленныеПроцентыПоЗаймам
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДокументы КАК ВТДокументы
		|		ПО ОтражениеЗарплатыВФинансовомУчетеНачисленныеПроцентыПоЗаймам.Ссылка = ВТДокументы.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТУдержания.ПодразделениеПредприятия,
		|	ВТУдержания.ВидОперации,
		|	ВТУдержания.ФизическоеЛицо,
		|	ВТУдержания.Контрагент,
		|	ВТУдержания.УдержанияНачислено,
		|	ВТУдержания.УдержанияОтражено
		|ПОМЕСТИТЬ ВТУдержанияОбъединение
		|ИЗ
		|	ВТУдержания КАК ВТУдержания
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	УдержаннаяЗарплатаПринятаяКУчету.ПодразделениеПредприятия,
		|	УдержаннаяЗарплатаПринятаяКУчету.ВидОперации,
		|	УдержаннаяЗарплатаПринятаяКУчету.ФизическоеЛицо,
		|	УдержаннаяЗарплатаПринятаяКУчету.Контрагент,
		|	0,
		|	УдержаннаяЗарплатаПринятаяКУчету.Сумма
		|ИЗ
		|	Документ.ОтражениеЗарплатыВФинансовомУчете.УдержаннаяЗарплата КАК УдержаннаяЗарплатаПринятаяКУчету
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДокументы КАК ВТДокументы
		|		ПО УдержаннаяЗарплатаПринятаяКУчету.Ссылка = ВТДокументы.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТНачисленнаяЗарплата.Организация,
		|	ВТНачисленнаяЗарплата.ОблагаетсяЕНВД,
		|	ВТНачисленнаяЗарплата.ПодразделениеПредприятия,
		|	ВТНачисленнаяЗарплата.СпособОтраженияЗарплатыВБухучете,
		|	ВТНачисленнаяЗарплата.ФизическоеЛицо,
		|	ВТНачисленнаяЗарплата.ВидОперации,
		|	СУММА(ВТНачисленнаяЗарплата.Сумма) КАК Сумма,
		|	СУММА(ВТНачисленнаяЗарплата.ВзносыПоЕдиномуТарифу) КАК ВзносыПоЕдиномуТарифу,
		|	СУММА(ВТНачисленнаяЗарплата.ПФРСтраховая) КАК ПФРСтраховая,
		|	СУММА(ВТНачисленнаяЗарплата.ПФРНакопительная) КАК ПФРНакопительная,
		|	СУММА(ВТНачисленнаяЗарплата.ПФРДоПредельнойВеличины) КАК ПФРДоПредельнойВеличины,
		|	СУММА(ВТНачисленнаяЗарплата.ПФРСПревышения) КАК ПФРСПревышения,
		|	СУММА(ВТНачисленнаяЗарплата.ПФРНаДоплатуЛетчикам) КАК ПФРНаДоплатуЛетчикам,
		|	СУММА(ВТНачисленнаяЗарплата.ПФРНаДоплатуШахтерам) КАК ПФРНаДоплатуШахтерам,
		|	СУММА(ВТНачисленнаяЗарплата.ПФРЗаЗанятыхНаПодземныхИВредныхРаботах) КАК ПФРЗаЗанятыхНаПодземныхИВредныхРаботах,
		|	СУММА(ВТНачисленнаяЗарплата.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботах) КАК ПФРЗаЗанятыхНаТяжелыхИПрочихРаботах,
		|	СУММА(ВТНачисленнаяЗарплата.ОПСпоМеждународнымДоговорам) КАК ОПСпоМеждународнымДоговорам,
		|	СУММА(ВТНачисленнаяЗарплата.ОПСпоМеждународнымДоговорамСПревышения) КАК ОПСпоМеждународнымДоговорамСПревышения,
		|	СУММА(ВТНачисленнаяЗарплата.ОМСпоМеждународнымДоговорам) КАК ОМСпоМеждународнымДоговорам,
		|	СУММА(ВТНачисленнаяЗарплата.ОМСпоМеждународнымДоговорамСПревышения) КАК ОМСпоМеждународнымДоговорамСПревышения,
		|	СУММА(ВТНачисленнаяЗарплата.ОССпоМеждународнымДоговорам) КАК ОССпоМеждународнымДоговорам,
		|	СУММА(ВТНачисленнаяЗарплата.ОССпоМеждународнымДоговорамСПревышения) КАК ОССпоМеждународнымДоговорамСПревышения,
		|	СУММА(ВТНачисленнаяЗарплата.ФСС) КАК ФСС,
		|	СУММА(ВТНачисленнаяЗарплата.ФССНесчастныеСлучаи) КАК ФССНесчастныеСлучаи,
		|	СУММА(ВТНачисленнаяЗарплата.ФФОМС) КАК ФФОМС,
		|	СУММА(ВТНачисленнаяЗарплата.ТФОМС) КАК ТФОМС,
		|	СУММА(ВТНачисленнаяЗарплата.СуммаОтражено) КАК СуммаОтражено,
		|	СУММА(ВТНачисленнаяЗарплата.ВзносыПоЕдиномуТарифуОтражено) КАК ВзносыПоЕдиномуТарифуОтражено,
		|	СУММА(ВТНачисленнаяЗарплата.ПФРСтраховаяОтражено) КАК ПФРСтраховаяОтражено,
		|	СУММА(ВТНачисленнаяЗарплата.ПФРНакопительнаяОтражено) КАК ПФРНакопительнаяОтражено,
		|	СУММА(ВТНачисленнаяЗарплата.ПФРДоПредельнойВеличиныОтражено) КАК ПФРДоПредельнойВеличиныОтражено,
		|	СУММА(ВТНачисленнаяЗарплата.ПФРСПревышенияОтражено) КАК ПФРСПревышенияОтражено,
		|	СУММА(ВТНачисленнаяЗарплата.ПФРНаДоплатуЛетчикамОтражено) КАК ПФРНаДоплатуЛетчикамОтражено,
		|	СУММА(ВТНачисленнаяЗарплата.ПФРНаДоплатуШахтерамОтражено) КАК ПФРНаДоплатуШахтерамОтражено,
		|	СУММА(ВТНачисленнаяЗарплата.ПФРЗаЗанятыхНаПодземныхИВредныхРаботахОтражено) КАК ПФРЗаЗанятыхНаПодземныхИВредныхРаботахОтражено,
		|	СУММА(ВТНачисленнаяЗарплата.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахОтражено) КАК ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахОтражено,
		|	СУММА(ВТНачисленнаяЗарплата.ОПСпоМеждународнымДоговорамОтражено) КАК ОПСпоМеждународнымДоговорамОтражено,
		|	СУММА(ВТНачисленнаяЗарплата.ОПСпоМеждународнымДоговорамСПревышенияОтражено) КАК ОПСпоМеждународнымДоговорамСПревышенияОтражено,
		|	СУММА(ВТНачисленнаяЗарплата.ОМСпоМеждународнымДоговорамОтражено) КАК ОМСпоМеждународнымДоговорамОтражено,
		|	СУММА(ВТНачисленнаяЗарплата.ОМСпоМеждународнымДоговорамСПревышенияОтражено) КАК ОМСпоМеждународнымДоговорамСПревышенияОтражено,
		|	СУММА(ВТНачисленнаяЗарплата.ОССпоМеждународнымДоговорамОтражено) КАК ОССпоМеждународнымДоговорамОтражено,
		|	СУММА(ВТНачисленнаяЗарплата.ОССпоМеждународнымДоговорамСПревышенияОтражено) КАК ОССпоМеждународнымДоговорамСПревышенияОтражено,
		|	СУММА(ВТНачисленнаяЗарплата.ФССОтражено) КАК ФССОтражено,
		|	СУММА(ВТНачисленнаяЗарплата.ФССНесчастныеСлучаиОтражено) КАК ФССНесчастныеСлучаиОтражено,
		|	СУММА(ВТНачисленнаяЗарплата.ФФОМСОтражено) КАК ФФОМСОтражено,
		|	СУММА(ВТНачисленнаяЗарплата.ТФОМСОтражено) КАК ТФОМСОтражено,
		|	ВЫБОР
		|		КОГДА СУММА(ВТНачисленнаяЗарплата.Сумма) - СУММА(ВТНачисленнаяЗарплата.СуммаОтражено)
		|			+ СУММА(ВТНачисленнаяЗарплата.ПФРСтраховая) - СУММА(ВТНачисленнаяЗарплата.ПФРСтраховаяОтражено)
		|			+ СУММА(ВТНачисленнаяЗарплата.ПФРНакопительная) - СУММА(ВТНачисленнаяЗарплата.ПФРНакопительнаяОтражено)
		|			+ СУММА(ВТНачисленнаяЗарплата.ПФРДоПредельнойВеличины) - СУММА(ВТНачисленнаяЗарплата.ПФРДоПредельнойВеличиныОтражено)
		|			+ СУММА(ВТНачисленнаяЗарплата.ПФРСПревышения) - СУММА(ВТНачисленнаяЗарплата.ПФРСПревышенияОтражено)
		|			+ СУММА(ВТНачисленнаяЗарплата.ПФРНаДоплатуЛетчикам) - СУММА(ВТНачисленнаяЗарплата.ПФРНаДоплатуЛетчикамОтражено)
		|			+ СУММА(ВТНачисленнаяЗарплата.ПФРНаДоплатуШахтерам) - СУММА(ВТНачисленнаяЗарплата.ПФРНаДоплатуШахтерамОтражено)
		|			+ СУММА(ВТНачисленнаяЗарплата.ПФРЗаЗанятыхНаПодземныхИВредныхРаботах) - СУММА(ВТНачисленнаяЗарплата.ПФРЗаЗанятыхНаПодземныхИВредныхРаботахОтражено)
		|			+ СУММА(ВТНачисленнаяЗарплата.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботах) - СУММА(ВТНачисленнаяЗарплата.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахОтражено)
		|			+ СУММА(ВТНачисленнаяЗарплата.ОПСпоМеждународнымДоговорам) - СУММА(ВТНачисленнаяЗарплата.ОПСпоМеждународнымДоговорамОтражено)
		|			+ СУММА(ВТНачисленнаяЗарплата.ОПСпоМеждународнымДоговорамСПревышения) - СУММА(ВТНачисленнаяЗарплата.ОПСпоМеждународнымДоговорамСПревышенияОтражено)
		|			+ СУММА(ВТНачисленнаяЗарплата.ОМСпоМеждународнымДоговорам) - СУММА(ВТНачисленнаяЗарплата.ОМСпоМеждународнымДоговорамОтражено)
		|			+ СУММА(ВТНачисленнаяЗарплата.ОМСпоМеждународнымДоговорамСПревышения) - СУММА(ВТНачисленнаяЗарплата.ОМСпоМеждународнымДоговорамСПревышенияОтражено)
		|			+ СУММА(ВТНачисленнаяЗарплата.ОССпоМеждународнымДоговорам) - СУММА(ВТНачисленнаяЗарплата.ОССпоМеждународнымДоговорамОтражено)
		|			+ СУММА(ВТНачисленнаяЗарплата.ОССпоМеждународнымДоговорамСПревышения) - СУММА(ВТНачисленнаяЗарплата.ОССпоМеждународнымДоговорамСПревышенияОтражено)
		|			+ СУММА(ВТНачисленнаяЗарплата.ФСС) - СУММА(ВТНачисленнаяЗарплата.ФССОтражено)
		|			+ СУММА(ВТНачисленнаяЗарплата.ФССНесчастныеСлучаи) - СУММА(ВТНачисленнаяЗарплата.ФССНесчастныеСлучаиОтражено)
		|			+ СУММА(ВТНачисленнаяЗарплата.ФФОМС) - СУММА(ВТНачисленнаяЗарплата.ФФОМСОтражено)
		|			+ СУММА(ВТНачисленнаяЗарплата.ТФОМС) - СУММА(ВТНачисленнаяЗарплата.ТФОМСОтражено) <> 0
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ТребуетсяОтражение,
		|	""НачисленнаяЗарплата"" КАК ИдентификаторНабора
		|ИЗ
		|	ВТНачисленнаяЗарплата КАК ВТНачисленнаяЗарплата
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТНачисленнаяЗарплата.ПодразделениеПредприятия,
		|	ВТНачисленнаяЗарплата.ФизическоеЛицо,
		|	ВТНачисленнаяЗарплата.ОблагаетсяЕНВД,
		|	ВТНачисленнаяЗарплата.СпособОтраженияЗарплатыВБухучете,
		|	ВТНачисленнаяЗарплата.ВидОперации,
		|	ВТНачисленнаяЗарплата.Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТНДФЛОбъединение.ПодразделениеПредприятия,
		|	ВТНДФЛОбъединение.ВидОперации,
		|	ВТНДФЛОбъединение.ФизическоеЛицо,
		|	ВТНДФЛОбъединение.РегистрацияВНалоговомОргане,
		|	СУММА(ВТНДФЛОбъединение.НДФЛНачислено) КАК НДФЛНачислено,
		|	СУММА(ВТНДФЛОбъединение.НДФЛОтражено) КАК НДФЛОтражено,
		|	ВЫБОР
		|		КОГДА СУММА(ВТНДФЛОбъединение.НДФЛНачислено) - СУММА(ВТНДФЛОбъединение.НДФЛОтражено) <> 0
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ТребуетсяОтражение,
		|	&Организация,
		|	""НачисленныйНДФЛ"" КАК ИдентификаторНабора
		|ИЗ
		|	ВТНДФЛОбъединение КАК ВТНДФЛОбъединение
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТНДФЛОбъединение.ФизическоеЛицо,
		|	ВТНДФЛОбъединение.РегистрацияВНалоговомОргане,
		|	ВТНДФЛОбъединение.ПодразделениеПредприятия,
		|	ВТНДФЛОбъединение.ВидОперации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТУдержанияОбъединение.ПодразделениеПредприятия,
		|	ВТУдержанияОбъединение.ВидОперации,
		|	ВТУдержанияОбъединение.ФизическоеЛицо,
		|	ВТУдержанияОбъединение.Контрагент,
		|	СУММА(ВТУдержанияОбъединение.УдержанияНачислено) КАК УдержанияНачислено,
		|	СУММА(ВТУдержанияОбъединение.УдержанияОтражено) КАК УдержанияОтражено,
		|	ВЫБОР
		|		КОГДА СУММА(ВТУдержанияОбъединение.УдержанияНачислено) - СУММА(ВТУдержанияОбъединение.УдержанияОтражено) <> 0
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ТребуетсяОтражение,
		|	&Организация,
		|	""УдержаннаяЗарплата"" КАК ИдентификаторНабора
		|ИЗ
		|	ВТУдержанияОбъединение КАК ВТУдержанияОбъединение
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТУдержанияОбъединение.ВидОперации,
		|	ВТУдержанияОбъединение.ПодразделениеПредприятия,
		|	ВТУдержанияОбъединение.ФизическоеЛицо,
		|	ВТУдержанияОбъединение.Контрагент
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТПроцентыОбъединение.ПодразделениеПредприятия,
		|	ВТПроцентыОбъединение.ФизическоеЛицо,
		|	&Организация,
		|	""НачисленныеПроцентыПоЗаймам"" КАК ИдентификаторНабора,
		|	СУММА(ВТПроцентыОбъединение.ПроцентыНачислено) КАК ПроцентыНачислено,
		|	СУММА(ВТПроцентыОбъединение.ПроцентыОтражено) КАК ПроцентыОтражено,
		|	ВЫБОР
		|		КОГДА СУММА(ВТПроцентыОбъединение.ПроцентыНачислено) - СУММА(ВТПроцентыОбъединение.ПроцентыОтражено) <> 0
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ТребуетсяОтражение
		|ИЗ
		|	ВТПроцентыОбъединение КАК ВТПроцентыОбъединение
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТПроцентыОбъединение.ФизическоеЛицо,
		|	ВТПроцентыОбъединение.ПодразделениеПредприятия";
		
	Запрос.УстановитьПараметр("НачисленнаяЗарплата",   РезультирующаяТаблицаНачислений);
	Запрос.УстановитьПараметр("НачисленныйНДФЛ",       РезультирующаяТаблицаНДФЛ);
	Запрос.УстановитьПараметр("УдержаннаяЗарплата",    РезультирующаяТаблицаУдержаний);
	Запрос.УстановитьПараметр("НачисленныеПроцентыПоЗаймам",    РезультирующаяТаблицаПроцентов);
	Запрос.УстановитьПараметр("Организация",           Организация);
	Запрос.УстановитьПараметр("НачалоПериода",         НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",          КонецПериода);
	
	Результат = Запрос.ВыполнитьПакет();
	
	НаборыДанных = Новый Структура;
	НаборыДанных.Вставить("НачисленнаяЗарплата", Результат[10].Выгрузить());
	НаборыДанных.Вставить("НачисленныйНДФЛ",     Результат[11].Выгрузить());
	НаборыДанных.Вставить("УдержаннаяЗарплата",  Результат[12].Выгрузить());
	НаборыДанных.Вставить("НачисленныеПроцентыПоЗаймам",  Результат[13].Выгрузить());
	
	Возврат НаборыДанных;
	
КонецФункции

Функция ТаблицыОтражения()
	
	НачисленнаяЗарплатаИВзносы = Новый ТаблицаЗначений;
	Реквизиты = Метаданные.Документы.ОтражениеЗарплатыВФинансовомУчете.ТабличныеЧасти.НачисленнаяЗарплатаИВзносы.Реквизиты;
	Для Каждого Реквизит Из Реквизиты Цикл
		НачисленнаяЗарплатаИВзносы.Колонки.Добавить(Реквизит.Имя, Реквизит.Тип);
	КонецЦикла;
	
	НачисленнаяЗарплатаИВзносыПоФизлицам = Новый ТаблицаЗначений;
	Реквизиты = Метаданные.Документы.ОтражениеЗарплатыВФинансовомУчете.ТабличныеЧасти.НачисленнаяЗарплатаИВзносыПоФизлицам.Реквизиты;
	Для Каждого Реквизит Из Реквизиты Цикл
		НачисленнаяЗарплатаИВзносыПоФизлицам.Колонки.Добавить(Реквизит.Имя, Реквизит.Тип);
	КонецЦикла;
	
	НачисленныйНДФЛ = Новый ТаблицаЗначений;
	Реквизиты = Метаданные.Документы.ОтражениеЗарплатыВФинансовомУчете.ТабличныеЧасти.НачисленныйНДФЛ.Реквизиты;
	Для Каждого Реквизит Из Реквизиты Цикл
		НачисленныйНДФЛ.Колонки.Добавить(Реквизит.Имя, Реквизит.Тип);
	КонецЦикла;
	
	УдержаннаяЗарплата = Новый ТаблицаЗначений;
	Реквизиты = Метаданные.Документы.ОтражениеЗарплатыВФинансовомУчете.ТабличныеЧасти.УдержаннаяЗарплата.Реквизиты;
	Для Каждого Реквизит Из Реквизиты Цикл
		УдержаннаяЗарплата.Колонки.Добавить(Реквизит.Имя, Реквизит.Тип);
	КонецЦикла;
	
	НачисленныеПроцентыПоЗаймам = Новый ТаблицаЗначений;
	Реквизиты = Метаданные.Документы.ОтражениеЗарплатыВФинансовомУчете.ТабличныеЧасти.НачисленныеПроцентыПоЗаймам.Реквизиты;
	Для Каждого Реквизит Из Реквизиты Цикл
		НачисленныеПроцентыПоЗаймам.Колонки.Добавить(Реквизит.Имя, Реквизит.Тип);
	КонецЦикла;
	
	Результат = Новый Структура();
	Результат.Вставить("НачисленнаяЗарплатаИВзносы",           НачисленнаяЗарплатаИВзносы);
	Результат.Вставить("НачисленнаяЗарплатаИВзносыПоФизлицам", НачисленнаяЗарплатаИВзносыПоФизлицам);
	Результат.Вставить("НачисленныйНДФЛ",                      НачисленныйНДФЛ);
	Результат.Вставить("УдержаннаяЗарплата",                   УдержаннаяЗарплата);
	Результат.Вставить("НачисленныеПроцентыПоЗаймам",          НачисленныеПроцентыПоЗаймам);
	
	Возврат Результат;
	
КонецФункции

Процедура НастроитьСоставКолонок(НастройкиКомпоновкиДанных)
	
	ДатаОтменыТФОМС = УчетСтраховыхВзносовКлиентСервер.ДатаОтменыТФОМС();
	ДатаОбъединенияСтраховойИНакопительной = УчетСтраховыхВзносовКлиентСервер.ДатаОбъединенияСтраховойИНакопительнойЧастейВзносовПФР();
	ДатаПоявленияВзносовНаДоплатуШахтерам = УчетСтраховыхВзносовКлиентСервер.ДатаВводаВзносовНаДоплатуШахтерам();
	ДатаПоявленияВзносовТяжВред = УчетСтраховыхВзносовКлиентСервер.ДатаВводаВзносовЗаЗанятыхНаРаботахСДосрочнойПенсией();
	
	НеиспользуемыеПоля = Новый Массив;
	
	Если НачалоПериода >= ДатаОтменыТФОМС Тогда
		НеиспользуемыеПоля.Добавить(Новый ПолеКомпоновкиДанных("ТФОМС"));
		НеиспользуемыеПоля.Добавить(Новый ПолеКомпоновкиДанных("ТФОМСКОтражению"));
	КонецЕсли;
	
	Если НачалоПериода >= ДатаОбъединенияСтраховойИНакопительной Тогда
		НеиспользуемыеПоля.Добавить(Новый ПолеКомпоновкиДанных("ПФРСтраховая"));
		НеиспользуемыеПоля.Добавить(Новый ПолеКомпоновкиДанных("ПФРСтраховаяКОтражению"));
		НеиспользуемыеПоля.Добавить(Новый ПолеКомпоновкиДанных("ПФРНакопительная"));
		НеиспользуемыеПоля.Добавить(Новый ПолеКомпоновкиДанных("ПФРНакопительнаяКОтражению"));
	КонецЕсли;
	
	Если НачалоПериода < ДатаОбъединенияСтраховойИНакопительной Тогда
		НеиспользуемыеПоля.Добавить(Новый ПолеКомпоновкиДанных("ПФРПоСуммарномуТарифу"));
		НеиспользуемыеПоля.Добавить(Новый ПолеКомпоновкиДанных("ПФРПоСуммарномуТарифуКОтражению"));
	КонецЕсли;
	
	Если НачалоПериода < ДатаПоявленияВзносовНаДоплатуШахтерам Или Не ПолучитьФункциональнуюОпцию("ИспользуетсяТрудШахтеров") Тогда
		НеиспользуемыеПоля.Добавить(Новый ПолеКомпоновкиДанных("ПФРНаДоплатуШахтерам"));
		НеиспользуемыеПоля.Добавить(Новый ПолеКомпоновкиДанных("ПФРНаДоплатуШахтерамКОтражению"));
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользуетсяТрудЧленовЛетныхЭкипажей") Тогда
		НеиспользуемыеПоля.Добавить(Новый ПолеКомпоновкиДанных("ПФРНаДоплатуЛетчикам"));
		НеиспользуемыеПоля.Добавить(Новый ПолеКомпоновкиДанных("ПФРНаДоплатуЛетчикамКОтражению"));
	КонецЕсли;

	Если НачалоПериода < ДатаПоявленияВзносовТяжВред Или Не ПолучитьФункциональнуюОпцию("ИспользуютсяРаботыСДосрочнойПенсией") Тогда
		НеиспользуемыеПоля.Добавить(Новый ПолеКомпоновкиДанных("ПФРЗаЗанятыхНаПодземныхИВредныхРаботах"));
		НеиспользуемыеПоля.Добавить(Новый ПолеКомпоновкиДанных("ПФРЗаЗанятыхНаПодземныхИВредныхРаботахКОтражению"));
		НеиспользуемыеПоля.Добавить(Новый ПолеКомпоновкиДанных("ПФРЗаЗанятыхНаТяжелыхИПрочихРаботах"));
		НеиспользуемыеПоля.Добавить(Новый ПолеКомпоновкиДанных("ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахКОтражению"));
	КонецЕсли;
	
	МассивПолей = Новый Массив;
	
	Для Каждого ЭлементСтруктуры Из НастройкиКомпоновкиДанных.Структура Цикл
		
		ДобавитьВыбранныеПоляВМассив(
			ЭлементСтруктуры.Выбор.Элементы,
			МассивПолей,
			Ложь);
		
	КонецЦикла;
	
	Для Каждого ВыбранноеПоле Из МассивПолей Цикл
		
		Если НеиспользуемыеПоля.Найти(ВыбранноеПоле.Поле) <> Неопределено Тогда
			ВыбранноеПоле.Использование = Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьВыбранныеПоляВМассив(ЭлементСтруктуры, МассивПолей, ТолькоГруппы = Ложь)
	
	Если ТипЗнч(ЭлементСтруктуры) = Тип("КомпоновщикНастроекКомпоновкиДанных") Тогда
		ВыбранныеПоля = ЭлементСтруктуры.Настройки.Выбор;
	Иначе
		ВыбранныеПоля = ЭлементСтруктуры;
	КонецЕсли;
	
	Для каждого Элемент Из ЭлементСтруктуры Цикл
		Если ТипЗнч(Элемент) = Тип("АвтоВыбранноеПолеКомпоновкиДанных") Тогда
			Продолжить;
		ИначеЕсли ТипЗнч(Элемент) = Тип("ГруппаВыбранныхПолейКомпоновкиДанных") Тогда
			Если ТолькоГруппы Тогда
				МассивПолей.Добавить(Элемент);
			КонецЕсли;
			ДобавитьВыбранныеПоляВМассив(Элемент.Элементы, МассивПолей, ТолькоГруппы);
		Иначе
			Если Не ТолькоГруппы Тогда
				МассивПолей.Добавить(Элемент);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

