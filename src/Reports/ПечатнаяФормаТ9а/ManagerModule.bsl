#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВариантыОтчетов

// См. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.
//
Процедура НастроитьВариантыОтчета(Настройки, НастройкиОтчета) Экспорт
	
	НастройкиОтчета.ОпределитьНастройкиФормы = Истина;
	
	НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "Т9а");
	НастройкиВарианта.Описание = НСтр("ru = 'Унифицированная форма № Т-9а';
										|en = 'Unified form No. T-9a'");
	НастройкиВарианта.Включен = Ложь;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВариантыОтчетов

#КонецОбласти

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ЗарплатаКадрыОтчеты.ВывестиВКоллекциюПечатнуюФорму("Отчет.ПечатнаяФормаТ9а",
		МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	
КонецПроцедуры

Процедура Сформировать(ДокументРезультат, РезультатКомпоновки, ОбъектыПечати = Неопределено, ПараметрыПечати = Неопределено) Экспорт
	
	Если РезультатКомпоновки.ОтчетПустой Тогда
		Возврат;
	КонецЕсли;
	
	ДокументРезультат.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Командировка_Т9а";
	ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ДокументРезультат.АвтоМасштаб = Истина;
	
	Для Каждого ДанныеНаПечать Из РезультатКомпоновки.ДанныеОтчета.Строки Цикл
		
		Если ДокументРезультат.ВысотаТаблицы > 0 Тогда
			ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПерваяСтрокаПечатнойФормы = ДокументРезультат.ВысотаТаблицы + 1;
		
		ДанныеПользовательскихПолейШапки = ЗарплатаКадрыОтчеты.ЗначенияЗаполненияПользовательскихПолей(РезультатКомпоновки.ИдентификаторыМакета, ДанныеНаПечать);
		
		СчетчикБланков = 1;
		СчетчикКолонок = 1;
		Для Каждого ДанныеДетальныхЗаписей Из ДанныеНаПечать.Строки Цикл
			
			ДанныеПользовательскихПолей = ЗарплатаКадрыОтчеты.ЗначенияЗаполненияПользовательскихПолей(РезультатКомпоновки.ИдентификаторыМакета, ДанныеДетальныхЗаписей);
			
			Если СчетчикКолонок = 1 Тогда
				
				Если СчетчикБланков > 1 Тогда
					
					ЗарплатаКадрыОтчеты.ВывестиВДокументРезультатОбластиМакета(
						ДокументРезультат,
						РезультатКомпоновки.МакетПечатнойФормы,
						"Подвал",
						ДанныеНаПечать,
						ДанныеПользовательскихПолейШапки);
					
					ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
					
				КонецЕсли;
				
				ОбластьШапка = РезультатКомпоновки.МакетПечатнойФормы.ПолучитьОбласть("Шапка");
				
				ЗарплатаКадрыОтчеты.ЗаполнитьПараметрыОбластиМакета(
					ОбластьШапка,
					ДанныеНаПечать,
					ДанныеПользовательскихПолейШапки);
				
				Если СчетчикБланков > 1 Тогда
					ОбластьШапка.Параметры.СсылкаНаОбъектНомерНаПечать = Строка(ОбластьШапка.Параметры.СсылкаНаОбъектНомерНаПечать) + "/" + СчетчикБланков;
				КонецЕсли;
				
				ДокументРезультат.Вывести(ОбластьШапка);
				
				ЗарплатаКадрыОтчеты.ВывестиВДокументРезультатОбластиМакета(
					ДокументРезультат,
					РезультатКомпоновки.МакетПечатнойФормы,
					"Строка|Заголовки",
					ДанныеНаПечать,
					ДанныеДетальныхЗаписей,
					ДанныеПользовательскихПолей);
				
				СчетчикБланков = СчетчикБланков + 1;
				
			КонецЕсли;
			
			ОбластьСтрокаКолонка = РезультатКомпоновки.МакетПечатнойФормы.ПолучитьОбласть("Строка|Колонка");
			
			ЗарплатаКадрыОтчеты.ЗаполнитьПараметрыОбластиМакета(
				ОбластьСтрокаКолонка,
				ДанныеНаПечать,
				ДанныеДетальныхЗаписей,
				ДанныеПользовательскихПолей);
			
			ДокументРезультат.Присоединить(ОбластьСтрокаКолонка);
			
			СчетчикКолонок = СчетчикКолонок + 1;
			Если СчетчикКолонок = 4 Тогда
				СчетчикКолонок = 1;
			КонецЕсли;
			
		КонецЦикла;
		
		ЗарплатаКадрыОтчеты.ВывестиВДокументРезультатОбластиМакета(
			ДокументРезультат,
			РезультатКомпоновки.МакетПечатнойФормы,
			"Подвал",
			ДанныеНаПечать,
			ДанныеПользовательскихПолейШапки);
		
		Если ОбъектыПечати <> Неопределено Тогда
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ДокументРезультат, ПерваяСтрокаПечатнойФормы, ОбъектыПечати, ДанныеНаПечать.СсылкаНаОбъект);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли