#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Формирует отчет.
//
// Параметры:
//  Параметры  - Структура - параметры формирования отчета:
//		* ЭтапПроизводства - ДокументСсылка.ЭтапПроизводства2_2 - диагностируемый этап.
//		* СтатусГрафика - Число - статус диагностируемого графика.
//  АдресХранилища - Строка - адрес хранилища, в которое будет помещен результат формирования отчета.
//
Процедура СформироватьОтчет(Параметры, АдресХранилища) Экспорт
	
	ДиаграммаГанта = Новый ДиаграммаГанта;
	
	НастроитьОбщиеСвойстваДиаграммы(ДиаграммаГанта);
	
	ЭтапПроизводства = Параметры.ЭтапПроизводства;
	СтатусГрафика = Параметры.СтатусГрафика;
	Границы = Новый Структура("Начало, Окончание", '39991231', '00010101');
	
	ВыбратьДанныеИЗаполнитьДиаграмму(ДиаграммаГанта, ЭтапПроизводства, СтатусГрафика, Границы);
	
	НастроитьШкалуВремениДиаграммы(ДиаграммаГанта, Границы);
	
	ПоместитьВоВременноеХранилище(ДиаграммаГанта, АдресХранилища);
	
КонецПроцедуры

#Область КомандыПодменюОтчеты

// Добавляет команду отчета в список команд.
// 
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//
// Возвращаемое значение:
//  - СтрокаТаблицыЗначений.
//  - Неопределено.
//
Функция ДобавитьКомандуСмежныеЭтапыПроизводства(КомандыОтчетов) Экспорт

	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.ДиаграммаСмежныхЭтаповПроизводства)
		И УправлениеПроизводствомПовтИсп.ИспользуетсяГрафикПроизводства() Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		КомандаОтчет.Идентификатор      = Метаданные.Отчеты.ДиаграммаСмежныхЭтаповПроизводства.ПолноеИмя();
		КомандаОтчет.Представление      = НСтр("ru = 'Смежные этапы';
												|en = 'Adjacent stages'");
		КомандаОтчет.ИмяФормы           = "Отчет.ДиаграммаСмежныхЭтаповПроизводства.Форма";
		КомандаОтчет.МножественныйВыбор = Ложь;
		КомандаОтчет.Важность           = "Обычное";
		
		Возврат КомандаОтчет;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ВыбратьДанныеИЗаполнитьДиаграмму(ДиаграммаГанта, ЭтапПроизводства, СтатусГрафика, Границы)
	
	ДиаграммаГанта.Обновление = Ложь;
	
	Данные = СмежныеЭтапыИЗависимости(ЭтапПроизводства, СтатусГрафика);
	
	Если Данные.Этапы.Количество() > 0 Тогда
		
		Серия = СерияПоУмолчанию(ДиаграммаГанта);
		
		Интервалы = Новый Соответствие;
		Для каждого Строка Из Данные.Этапы Цикл
			
			Границы.Начало = МИН(Границы.Начало, Строка.Начало);
			Границы.Окончание = МАКС(Границы.Окончание, Строка.Окончание);
			
			Точка = УстановитьТочку(ДиаграммаГанта, ЭтапПроизводства, Строка);
			Значение = ДиаграммаГанта.ПолучитьЗначение(Точка, Серия);
			Интервал = ДобавитьИнтервал(Значение, Строка);
			
			Интервалы.Вставить(Строка.ЭтапПроизводства, Интервал);
			
		КонецЦикла;
		
		// Связи между этапами.
		Для каждого Строка Из Данные.Зависимости Цикл
			
			Интервал = Интервалы.Получить(Строка.ЭтапПроизводства); // ИнтервалДиаграммыГанта - 
			СледующийИнтервал = Интервалы.Получить(Строка.СледующийЭтап);
			
			Если НЕ Интервал = Неопределено
				И НЕ СледующийИнтервал = Неопределено Тогда
				
				Связь = Интервал.Добавить(СледующийИнтервал);
				Связь.ТипСвязи = ТипСвязиДиаграммыГанта.КонецНачало;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ДиаграммаГанта.Обновление = Ложь;
	
КонецПроцедуры

// Возвращает смежные этапы и зависимости
//
// Параметры:
//  ЭтапПроизводства - ДокументСсылка.ЭтапПроизводства2_2 - этап.
//  СтатусГрафика - Число - статус графика производства. 
//
// Возвращаемое значение:
//  Структура - содержит:
//   * Этапы - ТаблицаЗначений - содержит:
//   * Зависимости - ТаблицаЗначений - 
//
Функция СмежныеЭтапыИЗависимости(ЭтапПроизводства, СтатусГрафика)
	
	ТекстыЗапросовПакета = Новый Массив;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТСвязиЭтапов.Этап КАК ЭтапПроизводства
	|ПОМЕСТИТЬ ВТЭтапы
	|ИЗ
	|	ВТСвязиЭтапов КАК ВТСвязиЭтапов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТСвязиЭтапов.СледующийЭтап
	|ИЗ
	|	ВТСвязиЭтапов КАК ВТСвязиЭтапов";
	ТекстыЗапросовПакета.Добавить(ТекстЗапроса);
	
	Если СтатусГрафика = СтатусРабочийГрафик() Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	Т.ЭтапПроизводства КАК ЭтапПроизводства,
		|	Т.НачалоЭтапа КАК Начало,
		|	Т.ОкончаниеЭтапа КАК Окончание,
		|	&ПредставлениеЭтапа КАК ПредставлениеЭтапа,
		|	ВЫБОР
		|		КОГДА Т.ЭтапПроизводства.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Завершен)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Завершен,
		|	Т.ЭтапПроизводства.РучноеРазмещениеВГрафике КАК РучноеРазмещение
		|ИЗ
		|	РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК Т
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НормативныйГрафикЭтаповПроизводства КАК НормативныйГрафикЭтаповПроизводства
		|		ПО Т.ЭтапПроизводства = НормативныйГрафикЭтаповПроизводства.ЭтапПроизводства
		|ГДЕ
		|	Т.ЭтапПроизводства В
		|			(ВЫБРАТЬ
		|				ВТЭтапы.ЭтапПроизводства
		|			ИЗ
		|				ВТЭтапы)
		|	И Т.СтатусГрафика = &СтатусГрафика
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЕСТЬNULL(НормативныйГрафикЭтаповПроизводства.ДлительностьДоЗапуска, -Т.ЭтапПроизводства.ДлительностьДоВыпуска)";
		
	Иначе
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ГрафикЭтаповПроизводства2_2.ЭтапПроизводства,
		|	ГрафикЭтаповПроизводства2_2.НачалоЭтапа КАК Начало,
		|	ГрафикЭтаповПроизводства2_2.ОкончаниеЭтапа КАК Окончание
		|ПОМЕСТИТЬ ВТГрафикПоСтатусу
		|ИЗ
		|	РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК ГрафикЭтаповПроизводства2_2
		|ГДЕ
		|	ГрафикЭтаповПроизводства2_2.ЭтапПроизводства В
		|			(ВЫБРАТЬ
		|				ВТЭтапы.ЭтапПроизводства
		|			ИЗ
		|				ВТЭтапы)
		|	И ГрафикЭтаповПроизводства2_2.СтатусГрафика = &СтатусГрафика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.ЭтапПроизводства КАК ЭтапПроизводства,
		|	Т.Начало КАК Начало,
		|	Т.Окончание КАК Окончание,
		|	&ПредставлениеЭтапа КАК ПредставлениеЭтапа,
		|	Т.Завершен КАК Завершен,
		|	Т.РучноеРазмещение КАК РучноеРазмещение
		|ИЗ
		|	(ВЫБРАТЬ
		|		ВТГрафикПоСтатусу.ЭтапПроизводства КАК ЭтапПроизводства,
		|		ВТГрафикПоСтатусу.Начало КАК Начало,
		|		ВТГрафикПоСтатусу.Окончание КАК Окончание,
		|		ВЫБОР
		|			КОГДА ВТГрафикПоСтатусу.ЭтапПроизводства.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Завершен)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ КАК Завершен,
		|		ВТГрафикПоСтатусу.ЭтапПроизводства.РучноеРазмещениеВГрафике КАК РучноеРазмещение
		|	ИЗ
		|		ВТГрафикПоСтатусу КАК ВТГрафикПоСтатусу
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ГрафикЭтаповПроизводства2_2.ЭтапПроизводства,
		|		ГрафикЭтаповПроизводства2_2.НачалоЭтапа,
		|		ГрафикЭтаповПроизводства2_2.ОкончаниеЭтапа,
		|		ВЫБОР
		|			КОГДА ГрафикЭтаповПроизводства2_2.ЭтапПроизводства.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Завершен)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ,
		|		ГрафикЭтаповПроизводства2_2.ЭтапПроизводства.РучноеРазмещениеВГрафике
		|	ИЗ
		|		РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК ГрафикЭтаповПроизводства2_2
		|	ГДЕ
		|		ГрафикЭтаповПроизводства2_2.ЭтапПроизводства В
		|				(ВЫБРАТЬ
		|					ВТЭтапы.ЭтапПроизводства
		|				ИЗ
		|					ВТЭтапы)
		|		И НЕ ГрафикЭтаповПроизводства2_2.ЭтапПроизводства В
		|					(ВЫБРАТЬ
		|						ВТГрафикПоСтатусу.ЭтапПроизводства
		|					ИЗ
		|						ВТГрафикПоСтатусу)
		|		И ГрафикЭтаповПроизводства2_2.СтатусГрафика = &СтатусРабочийГрафик) КАК Т
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НормативныйГрафикЭтаповПроизводства КАК НормативныйГрафикЭтаповПроизводства
		|		ПО Т.ЭтапПроизводства = НормативныйГрафикЭтаповПроизводства.ЭтапПроизводства
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЕСТЬNULL(НормативныйГрафикЭтаповПроизводства.ДлительностьДоЗапуска, -Т.ЭтапПроизводства.ДлительностьДоВыпуска)";
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(
		ТекстЗапроса,
		"&ПредставлениеЭтапа",
		Документы.ЭтапПроизводства2_2.ТекстЗапросаПредставлениеЭтапа("Т.ЭтапПроизводства"));
	
	ТекстыЗапросовПакета.Добавить(ТекстЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТСвязиЭтапов.Этап КАК ЭтапПроизводства,
	|	ВТСвязиЭтапов.СледующийЭтап
	|ИЗ
	|	ВТСвязиЭтапов КАК ВТСвязиЭтапов";
	ТекстыЗапросовПакета.Добавить(ТекстЗапроса);
	
	Разделитель =
	"
	|;
	|/////////////////////////////////////////////////////////////
	|";
	ТекстЗапроса = СтрСоединить(ТекстыЗапросовПакета, Разделитель);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Документы.ЭтапПроизводства2_2.СоздатьВТСвязиЭтапов(
		МенеджерВременныхТаблиц,
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ЭтапПроизводства),
		Ложь);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ЭтапПроизводства", ЭтапПроизводства);
	Запрос.УстановитьПараметр("СтатусГрафика", СтатусГрафика);
	Запрос.УстановитьПараметр("СтатусРабочийГрафик", СтатусРабочийГрафик());
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	МаксИндекс = МассивРезультатов.ВГраница();
	
	Результат = Новый Структура;
	Результат.Вставить("Этапы", МассивРезультатов[МаксИндекс-1].Выгрузить());
	Результат.Вставить("Зависимости", МассивРезультатов[МаксИндекс].Выгрузить());
	
	Возврат Результат;
	
КонецФункции

Функция СерияПоУмолчанию(ДиаграммаГанта)
	
	Возврат ДиаграммаГанта.УстановитьСерию("ГрафикПроизводства");
	
КонецФункции

Функция УстановитьТочку(ДиаграммаГанта, ЭтапПроизводства, Данные)
	
	Результат = ДиаграммаГанта.УстановитьТочку(Данные.ЭтапПроизводства);
	Результат.Текст = Данные.ПредставлениеЭтапа;
	Результат.Расшифровка = Данные.ЭтапПроизводства;
	
	Если Данные.ЭтапПроизводства = ЭтапПроизводства Тогда
		Результат.Шрифт = Новый Шрифт(,, Истина);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ДобавитьИнтервал(Значение, Данные)
	
	Результат = Значение.Добавить();
	Результат.Начало = Данные.Начало;
	Результат.Конец = Данные.Окончание;
	Результат.Текст = ТекстИнтервалаДиаграммы(
		Данные.Начало, Данные.Окончание);
	Результат.Расшифровка = Данные.ЭтапПроизводства;
	Результат.Цвет = ЦветИнтервала(Данные.Завершен, Данные.РучноеРазмещение);
	
	Возврат Результат;
	
КонецФункции

Процедура НастроитьОбщиеСвойстваДиаграммы(ДиаграммаГанта)
	
	ДиаграммаГанта.АвтоОпределениеПолногоИнтервала = Ложь;
	ДиаграммаГанта.ПоддержкаМасштаба = ПоддержкаМасштабаДиаграммыГанта.ВсеДанные;
	ДиаграммаГанта.Окантовка = Ложь;
	ДиаграммаГанта.ОтображатьЛегенду = Ложь;
	ДиаграммаГанта.ВертикальнаяПрокрутка = Истина;
	ДиаграммаГанта.ОтображатьПустыеЗначения = Ложь;
	ДиаграммаГанта.ОтображатьЗаголовок = Ложь;
	ДиаграммаГанта.ОтображениеТекстаЗначения = ОтображениеТекстаЗначенияДиаграммыГанта.НеОтображать;
	ДиаграммаГанта.ОбластьПостроения.Право = 1;
	
КонецПроцедуры

Процедура НастроитьШкалуВремениДиаграммы(ДиаграммаГанта, Границы)
	
	Если НЕ ЗначениеЗаполнено(Границы.Окончание) Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиШкалы = УправлениеПроизводствомКлиентСервер.НастройкиШкалыДиаграммыГантаВРежимеВсеДанные(
		Границы.Начало, Границы.Окончание, 9);
	
	ШкалаВремениЭлементы = ДиаграммаГанта.ОбластьПостроения.ШкалаВремени.Элементы;
	
	Для Индекс = 1 По ШкалаВремениЭлементы.Количество()-1 Цикл
		ШкалаВремениЭлементы.Удалить(ШкалаВремениЭлементы[Индекс]);
	КонецЦикла;
	
	ЭлементШкалы = ШкалаВремениЭлементы.Добавить();
	ЭлементШкалы.Единица = НастройкиШкалы.Единица;
	ЭлементШкалы.Формат = НастройкиШкалы.Формат;
	
	Если ШкалаВремениЭлементы.Количество() = 2 Тогда
		ШкалаВремениЭлементы.Удалить(ШкалаВремениЭлементы[0]);
	КонецЕсли;
	
	ДиаграммаГанта.УстановитьПолныйИнтервал(
		НастройкиШкалы.НачалоПолногоИнтервала,
		НастройкиШкалы.ОкончаниеПолногоИнтервала);
	
КонецПроцедуры

Функция СтатусРабочийГрафик()
	
	Возврат РегистрыСведений.ГрафикЭтаповПроизводства2_2.СтатусРабочийГрафик();
	
КонецФункции

Функция ТекстИнтервалаДиаграммы(Начало, Окончание)
	
	ФорматнаяСтрока = УправлениеПроизводством.ФорматнаяСтрокаДляДатыГрафикаПроизводства();
	ФорматНачало = Формат(Начало, ФорматнаяСтрока);
	ФорматОкончание = Формат(Окончание, ФорматнаяСтрока);
	
	Если ФорматНачало = ФорматОкончание Тогда
		Результат = ФорматНачало;
	Иначе
		Результат = ФорматНачало + " - " + ФорматОкончание;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ЦветИнтервала(ЭтапЗавершен, РучноеРазмещение)
	
	Если ЭтапЗавершен Тогда
		Результат = ЦветаСтиля.ЦветФонаЭтапЗавершен;
	Иначе
		Если НЕ РучноеРазмещение Тогда
			Результат = ЦветаСтиля.ЦветФонаЭтапНеЗавершенЗапланированАвтоматически;
		Иначе
			Результат = ЦветаСтиля.ЦветФонаЭтапНеЗавершенЗапланированВручную;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли