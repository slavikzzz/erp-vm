&НаСервере
Перем мОбъектОтчета;

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ПолеТабличногоДокументаФормаОтчета.Очистить();
	
	СформироватьСтруктуруРеквизитовФормы();
	
	СтруктураРеквизитовФормы.ТекстДляЗаголовочнойЧасти = "";
	СтруктураРеквизитовФормы.мСоставПоказателей = "СоставПоказателей2018Кв1";
	
	ИмяРаздела = "ПолеТабличногоДокументаФормаОтчета";
	СтруктураРеквизитовФормы.НаимТекущегоРаздела = ИмяРаздела;
	
	СтруктураРеквизитовФормы.СписокПечатаемыхЛистов = Новый СписокЗначений;
	
	СтруктураРеквизитовФормы.мВерсияФормы = "14/11/2018";
	СтруктураРеквизитовФормы.мКодОтчета = "0609012";
	СтруктураРеквизитовФормы.мИдГруппы1 = "П0000020001";
	СтруктураРеквизитовФормы.мИдГруппы2 = "П0000040001";
	
	КолИсхСтрокИдГруппы1 = 20;
	КолИсхСтрокИдГруппы2 = 5;
	
	КолКолонокИдГруппы1 = 5;
	КолКолонокИдГруппы2 = 12;
	
	мСтруктураМногострочныхРазделов = Новый Структура;
	мСтруктураМногострочныхРазделов.Вставить(СтруктураРеквизитовФормы.мИдГруппы1, СтруктураРеквизитовФормы.мИдГруппы1);
	мСтруктураМногострочныхРазделов.Вставить(СтруктураРеквизитовФормы.мИдГруппы2, СтруктураРеквизитовФормы.мИдГруппы2);
	
	мСтруктураИсхКолвоСтрокРазделов = Новый Структура;
	мСтруктураИсхКолвоСтрокРазделов.Вставить(СтруктураРеквизитовФормы.мИдГруппы1, КолИсхСтрокИдГруппы1);
	мСтруктураИсхКолвоСтрокРазделов.Вставить(СтруктураРеквизитовФормы.мИдГруппы2, КолИсхСтрокИдГруппы2);
	
	мСтруктураКолвоКолонокРазделов = Новый Структура;
	мСтруктураКолвоКолонокРазделов.Вставить(СтруктураРеквизитовФормы.мИдГруппы1, КолКолонокИдГруппы1);
	мСтруктураКолвоКолонокРазделов.Вставить(СтруктураРеквизитовФормы.мИдГруппы2, КолКолонокИдГруппы2);
	
	СтруктураРеквизитовФормы.мСоответствияМнгЧ = Новый Соответствие;
	СтруктураРеквизитовФормы.мСоответствияМнгЧ.Вставить(СтруктураРеквизитовФормы.мИдГруппы1, "МногострочнаяЧасть1");
	СтруктураРеквизитовФормы.мСоответствияМнгЧ.Вставить(СтруктураРеквизитовФормы.мИдГруппы2, "МногострочнаяЧасть2");
	
	СтруктураРеквизитовФормы.мПечатныеФормы = Новый СписокЗначений;
	
	ПолеТабличногоДокументаФормаОтчета.Вывести(ОбъектОтчета(ИмяФормы).ПолучитьМакет("ФормаОтчета2018Кв1_ФормаОтчета"));
	
	РегламентированнаяОтчетность.СформироватьСтруктуруЦветовРаскраски(ЭтотОбъект);
	
	мСтруктураВариантыЗаполнения = Новый Структура;
	
	ПорядкиОкругленияОтчетности = Новый Структура;
	ПорядкиОкругленияОтчетности.Вставить("ПеречислениеПорядкиОкругленияОтчетностиОкр1",
										 Перечисления.ПорядкиОкругленияОтчетности.Окр1);
	ПорядкиОкругленияОтчетности.Вставить("ПеречислениеПорядкиОкругленияОтчетностиОкр1000",
										 Перечисления.ПорядкиОкругленияОтчетности.Окр1000);
	ПорядкиОкругленияОтчетности.Вставить("ПеречислениеПорядкиОкругленияОтчетностиОкр1000000",
										 Перечисления.ПорядкиОкругленияОтчетности.Окр1000000);
	
	СоответствиеЕдиницыИзмеренияДелителю = Новый Структура;
	СоответствиеЕдиницыИзмеренияДелителю.Вставить("Окр1", 1);
	СоответствиеЕдиницыИзмеренияДелителю.Вставить("Окр1000", 1000);
	СоответствиеЕдиницыИзмеренияДелителю.Вставить("Окр1000000", 1000000);

	СтруктураРеквизитовФормы.мВыбраннаяФорма          = Параметры.мВыбраннаяФорма;
	СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета  = Параметры.мДатаКонцаПериодаОтчета;
	СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета = Параметры.мДатаНачалаПериодаОтчета;
	СтруктураРеквизитовФормы.мПериодичность           = Параметры.мПериодичность;
	СтруктураРеквизитовФормы.мСкопированаФорма        = Параметры.мСкопированаФорма;
	СтруктураРеквизитовФормы.мСохраненныйДок          = Параметры.мСохраненныйДок;
	СтруктураРеквизитовФормы.Организация              = Параметры.Организация;
	Если ЗначениеЗаполнено(СтруктураРеквизитовФормы.Организация) Тогда 
		СтруктураРеквизитовФормы.ЭтоЮрЛицо =
			РегламентированнаяОтчетностьПереопределяемый.ЭтоЮридическоеЛицо(СтруктураРеквизитовФормы.Организация);
	КонецЕсли;
	
	ДатаПодписи = ТекущаяДатаСеанса();
	
	СтруктураРеквизитовФормы.СправочникиВидыКонтактнойИнформацииПочтовыйАдресОрганизации = 
		Справочники.ВидыКонтактнойИнформации.ПочтовыйАдресОрганизации;
	
	Инициализация(Параметры.БезОткрытияФормы);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьСтруктуруРеквизитовФормы()
	
	СтруктураРеквизитовФормы = 
		РегламентированнаяОтчетность.СформироватьСтруктуруОбязательныхРеквизитовСтатистическогоОтчета();
		
	СтруктураРеквизитовФормы.Вставить("мИдГруппы1");
	СтруктураРеквизитовФормы.Вставить("мИдГруппы2");
	СтруктураРеквизитовФормы.Вставить("ЭтоЮрЛицо");
	
КонецПроцедуры

&НаСервере
Процедура Инициализация(БезОткрытияФормы = Ложь) Экспорт
	СформироватьСпискиВыбора();
	РегламентированнаяОтчетность.ЗаполнитьНачальныеНастройки(ЭтотОбъект);
	ФормироватьСтруктуруСтраницОтчета();
	ФормироватьСтруктуруСвойствСтраниц();
	
	СтруктураРеквизитовФормы.мАвтоВыборКодов = Ложь;
	СтруктураРеквизитовФормы.мБезОткрытияФормы = БезОткрытияФормы;
	СтруктураРеквизитовФормы.мРежимПечати = Ложь;
	СтруктураРеквизитовФормы.мВПрограммеИзмененаОрганизация = Ложь;

	ТекТабличноеПоле = ПолеТабличногоДокументаФормаОтчета;

	СтруктураРеквизитовФормы.ВидДокумента = 0;
	НомерКорректировки = 1;
	СтруктураРеквизитовФормы.мАдресвФорматеДляВыгрузки = "";
	
	РегламентированнаяОтчетность.НачальныеОперацииСМногострочнымиБлоками(ЭтотОбъект);
	
	Если СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено Тогда
		СтруктураРеквизитовФормы.ЕдиницаИзмерения = 
			ПорядкиОкругленияОтчетности.ПеречислениеПорядкиОкругленияОтчетностиОкр1;
		СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения = 0;
		
		Если СтруктураРеквизитовФормы.мСкопированаФорма <> Неопределено Тогда
			ВосстановитьСохраненныеДанные();
			_Приложения.ПолучитьЭлементы().Очистить();
			ИмяФайлаДоверенности = "";
			Доверенность = Справочники.РегламентированныйОтчетПрисоединенныеФайлы.ПустаяСсылка();
			ХешДоверенности = "";
		КонецЕсли;
		
		Модифицированность = Истина;
	Иначе
		ВосстановитьСохраненныеДанные();
		
		Если ЗначениеЗаполнено(СтруктураРеквизитовФормы.мСкопированаФорма) Тогда
			СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено;
			_Приложения.ПолучитьЭлементы().Очистить();
			ИмяФайлаДоверенности = "";
			Доверенность = Справочники.РегламентированныйОтчетПрисоединенныеФайлы.ПустаяСсылка();
			ХешДоверенности = "";
		КонецЕсли;
		
		Если НЕ БезОткрытияФормы И НЕ СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено Тогда
			РегламентированнаяОтчетность.БлокировкаИлиЗапретРедактированияОтчета(ЭтотОбъект);
		КонецЕсли;
	КонецЕсли;

	Если СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено Тогда
		НомерКорректировки = 0;
	Иначе
		НомерКорректировки = СтруктураРеквизитовФормы.мСохраненныйДок.Вид;
	КонецЕсли;
	
	ТекТабличноеПоле.Области.НомерКорректировки.Значение = НомерКорректировки;
	ПолеТабличногоДокументаФормаОтчета.Области.ДатаСоставленияОтчета.Значение = ДатаПодписи;
	
	ПоказатьПериод();

	Если СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено
		Или СтруктураРеквизитовФормы.мВПрограммеИзмененаОрганизация Тогда
		ЗаполнитьСведенияОбОрганизацииНаСервере();
	КонецЕсли;
	
	СтруктураРеквизитовФормы.НаимОрганизации = СтруктураРеквизитовФормы.Организация.Наименование;
	
	ТекущийЭлемент = Элементы["ПолеТабличногоДокументаФормаОтчета"];
	
	РегламентированнаяОтчетность.ДобавитьКнопкуПрисоединенныеФайлы(ЭтотОбъект);
	
	РегламентированнаяОтчетностьКлиентСервер.ПриИнициализацииФормыРегламентированногоОтчета(ЭтотОбъект, "РПН");
	РегламентированнаяОтчетность.УстановитьЦветФорматДоступностьЯчеек(ЭтотОбъект);
	ИзменитьТаблицуВариантовЗаполненияНаСервере();
	
	Если Не ЗначениеЗаполнено(СтруктураРеквизитовФормы.мСохраненныйДок) Тогда
		ЗаполнитьЗагрязняющиеВещества();
	КонецЕсли;
	ОтображениеГиперссылокДоверенности(ЭтотОбъект);
	
	// ПроцессыОбработкиДокументов
	//
	РегламентированнаяОтчетность.ПриИнициализации(ЭтотОбъект,
		?(ЗначениеЗаполнено(СтруктураРеквизитовФормы.мСохраненныйДок),
			СтруктураРеквизитовФормы.мСохраненныйДок,
			СтруктураРеквизитовФормы.ПустаяСсылкаНаРегламентированныйОтчет));
	//
	// ПроцессыОбработкиДокументов
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьПериод()
	
	ПолеТабличногоДокументаФормаОтчета.Области.ПериодОтчета.Значение = "за "
		+ НРег(Формат(СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета, "ДФ = 'гггг'")) + " г.";
	СтруктураРеквизитовФормы.СтрПериодОтчета =
		НРег(Формат(СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета, "ДФ = 'гггг'")) + " г.";
	
КонецПроцедуры

&НаСервере
Процедура ВосстановитьСохраненныеДанные()
	Перем ДанныеМногострочныхРазделов;
	Перем ДанныеВариановАвтоЗаполнения;
	Перем ПоказателиОтчета;
	Перем ВерсияФормы;

	Если СтруктураРеквизитовФормы.Организация <> СтруктураРеквизитовФормы.мСохраненныйДок.Организация
	   И СтруктураРеквизитовФормы.Организация <> РегламентированнаяОтчетностьКлиентСервер.ПустоеЗначениеТипа("СправочникСсылка.Организации")
	   И СтруктураРеквизитовФормы.Организация <> Неопределено Тогда
		СтруктураРеквизитовФормы.мВПрограммеИзмененаОрганизация = Истина;
	Иначе
		СтруктураРеквизитовФормы.Организация = СтруктураРеквизитовФормы.мСохраненныйДок.Организация;
	КонецЕсли;
	
	ДатаПодписи = СтруктураРеквизитовФормы.мСохраненныйДок.ДатаПодписи;
	СтруктураРеквизитовФормы.ЕдиницаИзмерения = СтруктураРеквизитовФормы.мСохраненныйДок.ЕдиницаИзмерения;
	СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения = СтруктураРеквизитовФормы.мСохраненныйДок.ТочностьЕдиницыИзмерения;
	Комментарий = СтруктураРеквизитовФормы.мСохраненныйДок.Комментарий;

	СписокСохранения = СтруктураРеквизитовФормы.мСохраненныйДок.ДанныеОтчета.Получить();
	Если СписокСохранения.Свойство("ДанныеВариантовАвтоЗаполнения", ДанныеВариановАвтоЗаполнения) Тогда
		
		Если НЕ ДанныеВариановАвтоЗаполнения = Неопределено Тогда
			
			мСтруктураВариантыЗаполнения.Очистить();
			
			Для Каждого ЭлементСтруктуры Из ДанныеВариановАвтоЗаполнения Цикл
				
				мСтруктураВариантыЗаполнения.Вставить(ЭлементСтруктуры.Ключ, ЭлементСтруктуры.Ключ);
				
				ЭтотОбъект["ТаблицаВариантыЗаполнения" + ЭлементСтруктуры.Ключ].Очистить();
				
				Для Каждого ЭлементМассива Из ЭлементСтруктуры.Значение Цикл
					
					ТаблВарЗаполнения =
						ЭтотОбъект["ТаблицаВариантыЗаполнения" + ЭлементСтруктуры.Ключ].Добавить().ТаблицаВариантовЗаполнения;
					
					ЗначениеВДанныеФормы(ЭлементМассива, ТаблВарЗаполнения);
					
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;

	СписокСохранения.Свойство("ВерсияФормы", ВерсияФормы);
	
	СписокСохранения.Свойство("ФлОтклВыборЗначений", СтруктураРеквизитовФормы.мАвтоВыборКодов);
	СтруктураРеквизитовФормы.мАвтоВыборКодов = ?(СтруктураРеквизитовФормы.мАвтоВыборКодов = Неопределено,
		Ложь, СтруктураРеквизитовФормы.мАвтоВыборКодов);
	СписокСохранения.Свойство("ФлажокОтклАвтоРасчет", СтруктураРеквизитовФормы.ФлажокОтклАвтоРасчет);
	СтруктураРеквизитовФормы.ФлажокОтклАвтоРасчет = ?(СтруктураРеквизитовФормы.ФлажокОтклАвтоРасчет = Неопределено,
		Ложь, СтруктураРеквизитовФормы.ФлажокОтклАвтоРасчет);
		
	СписокСохранения.Свойство("Доверенность", Доверенность);
	СписокСохранения.Свойство("ИмяФайлаДоверенности", ИмяФайлаДоверенности);
	СписокСохранения.Свойство("ХешДоверенности", ХешДоверенности);
	Если СписокСохранения.Свойство("_Приложения") Тогда 
		ЗначениеВДанныеФормы(СписокСохранения._Приложения, _Приложения);
	КонецЕсли;
	
	Если СписокСохранения.Свойство("ДанныеМногострочныхРазделов", ДанныеМногострочныхРазделов)
		И ДанныеМногострочныхРазделов <> Неопределено Тогда
		
		Для Каждого ЭлементСтруктуры Из ДанныеМногострочныхРазделов Цикл
			
			НайденноеЗначение = Неопределено;
			
			мСтруктураМногострочныхРазделов.Свойство(ЭлементСтруктуры.Ключ, НайденноеЗначение);
			
			Если НЕ НайденноеЗначение = Неопределено Тогда
				
				ЗначениеВДанныеФормы(ЭлементСтруктуры.Значение, ЭтотОбъект[НайденноеЗначение]);
				
				ВывестиРазделВТабличныйДокументНаСервере(НайденноеЗначение, ЭтотОбъект[НайденноеЗначение]);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	СписокСохранения.Свойство( "ПоказателиОтчета", ПоказателиОтчета );
	
	Для Каждого ПоказателиСтраницы Из ПоказателиОтчета Цикл
		ИмяТекТабличноеПоле = ПоказателиСтраницы.Ключ;
		ТекТабличноеПоле    = ЭтотОбъект[ИмяТекТабличноеПоле];
		ПоказателиТаблПоле  = ПоказателиСтраницы.Значение;

		Для Каждого Показатель Из ПоказателиТаблПоле Цикл
			ИмяПоказателя       = Показатель.Ключ;
			ЗначениеПоказателя  = Показатель.Значение;
			РегламентированнаяОтчетность.УстановитьЗначениеИФорматВПоле(ТекТабличноеПоле, ИмяПоказателя, ЗначениеПоказателя);
		КонецЦикла;
	КонецЦикла;

	СтруктураРеквизитовФормы.мАдресвФорматеДляВыгрузки = ПолеТабличногоДокументаФормаОтчета.Области.ОргАдрес.Значение;
	ПолеТабличногоДокументаФормаОтчета.Области.ОргАдрес.Значение =
		РегламентированнаяОтчетностьКлиентСервер.ПредставлениеАдресаВФормате9Запятых(
		СтруктураРеквизитовФормы.мАдресвФорматеДляВыгрузки);
	Если ЗначениеЗаполнено(СтруктураРеквизитовФормы.Организация) Тогда 
		СтруктураРеквизитовФормы.ЭтоЮрЛицо
			= РегламентированнаяОтчетностьПереопределяемый.ЭтоЮридическоеЛицо(СтруктураРеквизитовФормы.Организация);
	КонецЕсли;

	РегламентированнаяОтчетность.ОперацииПриВосстановленииРегламентированногоОтчета(ЭтотОбъект);

	Модифицированность = Ложь;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСведенияОбОрганизацииНаСервере(ВПрограммеИзмененаДатаПодписи = Ложь)
	
	РегламентированнаяОтчетностьКлиентСервер.ЗаполнитьСведенияОбОрганизацииДляОтчетаСтатистики(
		ЭтотОбъект, ВПрограммеИзмененаДатаПодписи);
		
	Если ЗначениеЗаполнено(СтруктураРеквизитовФормы.Организация) Тогда 
		Сведения = "ИННЮЛ,ОГРН,НаимЮЛПол,НаимЮЛСокр,КППЮЛ,ОКПО,ОКВЭД,ФИОРук,ДолжнРук,ОКТМО,ОргИндекс,"
			+ "АдрЮР,ФИОРук,ДолжнРук,ТелОрганизации,АдресЭлектроннойПочтыОрганизации";
		ОргСведения = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(СтруктураРеквизитовФормы.Организация, ДатаПодписи, Сведения);
		ПолеТабличногоДокументаФормаОтчета.Области.inn.Значение = ОргСведения.ИННЮЛ;
		ПолеТабличногоДокументаФормаОтчета.Области.ogrn.Значение = ОргСведения.ОГРН;
		ПолеТабличногоДокументаФормаОтчета.Области.kpp.Значение = ОргСведения.КППЮЛ;
		ПолеТабличногоДокументаФормаОтчета.Области.full_name_organization.Значение = ОргСведения.НаимЮЛПол;
		ПолеТабличногоДокументаФормаОтчета.Области.short_name_organization.Значение = ОргСведения.НаимЮЛСокр;
		ПолеТабличногоДокументаФормаОтчета.Области.code_okpo.Значение = ОргСведения.ОКПО;
		ПолеТабличногоДокументаФормаОтчета.Области.okved.Значение = ОргСведения.ОКВЭД;
		ПолеТабличногоДокументаФормаОтчета.Области.ceo_full_name.Значение = ОргСведения.ФИОРук;
		ПолеТабличногоДокументаФормаОтчета.Области.ceo_position.Значение = ОргСведения.ДолжнРук;
		ПолеТабличногоДокументаФормаОтчета.Области.post_address_post.Значение = ОргСведения.ОргИндекс;
		ПолеТабличногоДокументаФормаОтчета.Области.post_address_street.Значение = ОргСведения.АдрЮР;
		ПолеТабличногоДокументаФормаОтчета.Области.legal_address_post.Значение = ОргСведения.ОргИндекс;
		ПолеТабличногоДокументаФормаОтчета.Области.legal_address_street.Значение = ОргСведения.АдрЮР;
		ПолеТабличногоДокументаФормаОтчета.Области.code_okpo.Значение = ОргСведения.ОКПО;
		ПолеТабличногоДокументаФормаОтчета.Области.ceo_full_name.Значение = ОргСведения.ФИОРук;
		ПолеТабличногоДокументаФормаОтчета.Области.ceo_position.Значение = ОргСведения.ДолжнРук;
		ПолеТабличногоДокументаФормаОтчета.Области.phone.Значение = ОргСведения.ТелОрганизации;
		ПолеТабличногоДокументаФормаОтчета.Области.email.Значение = ОргСведения.АдресЭлектроннойПочтыОрганизации;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСведенияОбОрганизацииНаКлиенте(ВПрограммеИзмененаДатаПодписи = Ложь)
	
	РегламентированнаяОтчетностьКлиентСервер.ЗаполнитьСведенияОбОрганизацииДляОтчетаСтатистики(
		ЭтотОбъект, ВПрограммеИзмененаДатаПодписи);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьОтчет(Команда)
	
	СохранитьНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНаКлиенте(Автосохранение = Ложь, ВыполняемоеОповещение = Неопределено) Экспорт
	
	Вариант = СтруктураРеквизитовФормы.ВидДокумента * НомерКорректировки;
	ПронумероватьСтрокиДляВыгрузки();
	
	Если НЕ РегламентированнаяОтчетностьКлиент.ПриЗаписиРегламентированногоОтчетаНаКлиенте(
		ЭтотОбъект, , Автосохранение, Вариант) Тогда
		Возврат;
	КонецЕсли;
	
	ПодобныйОтчетСуществует = Ложь;
	ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки = Ложь;
	
	ПоказателиОтчета = Новый Структура();
	
	#Если ВебКлиент Тогда
		ЗаполнитьПоказателиОтчетаНаСервере(ПоказателиОтчета);
	#Иначе
		ЗаполнитьПоказателиОтчетаНаКлиенте(ПоказателиОтчета);
	#КонецЕсли
	
	РезультатСохранения = ПередСохранением(, ПодобныйОтчетСуществует, Вариант,
		ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, Автосохранение, ПоказателиОтчета);
	
	ВидДокументаНомерКорректировкиИзменен = Неопределено;
	
	Если ПодобныйОтчетСуществует И Автосохранение Тогда
		Возврат;
	КонецЕсли;
	
	НуженВопросПередСохранением
		= (ПодобныйОтчетСуществует ИЛИ ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки);
	
	Если НуженВопросПередСохранением Тогда
		СохранитьНаКлиентеСВопросом(Автосохранение, Вариант, ВыполняемоеОповещение,
			ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, ПодобныйОтчетСуществует, ПоказателиОтчета);
	Иначе		
		ПослеСохраненияНаКлиенте(ВыполняемоеОповещение, РезультатСохранения);		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНаКлиентеСВопросом(Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, ПодобныйОтчетСуществует, ПоказателиОтчета)
	
	Если ПодобныйОтчетСуществует Тогда
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Да, "Сохранить");
		Кнопки.Добавить(КодВозвратаДиалога.Нет, "Отмена");
		
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Отчет с видом %1 уже существует.
		|Сохранить отчет с таким же видом?';
		|en = 'Отчет с видом %1 уже существует.
		|Сохранить отчет с таким же видом?'"), ?(Вариант = 0, """Первичный""", """Корректирующий/" + Вариант + """"));
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Автосохранение", Автосохранение);
		ДополнительныеПараметры.Вставить("Вариант", Вариант);
		ДополнительныеПараметры.Вставить("ВыполняемоеОповещение", ВыполняемоеОповещение);
		ДополнительныеПараметры.Вставить("ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки",
			ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки);
		ДополнительныеПараметры.Вставить("ПоказателиОтчета", ПоказателиОтчета);
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ВопросСохранитьОтчетСТакимЖеВидомЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, Кнопки, , Кнопки.Получить(1).Значение);
		
	Иначе
		
		СохранитьНаКлиентеСВопросомПродолжение(Автосохранение, Вариант, ВыполняемоеОповещение,
			ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, ПоказателиОтчета);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросСохранитьОтчетСТакимЖеВидомЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Автосохранение = ДополнительныеПараметры.Автосохранение;
	Вариант = ДополнительныеПараметры.Вариант;
	ВыполняемоеОповещение = ДополнительныеПараметры.ВыполняемоеОповещение;
	ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки =
		ДополнительныеПараметры.ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки;
	ПоказателиОтчета = ДополнительныеПараметры.ПоказателиОтчета;
	
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		
		Возврат;
		
	КонецЕсли;
	
	СохранитьНаКлиентеСВопросомПродолжение(Автосохранение, Вариант, ВыполняемоеОповещение,
		ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, ПоказателиОтчета);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНаКлиентеСВопросомПродолжение(Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, ПоказателиОтчета)
	
	Если ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки Тогда
		
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ВидОтчета", Вариант);
		
		ФормаВопроса = РегламентированнаяОтчетностьКлиент.ПолучитьОбщуюФормуПоИмени(
		"ВопросПриИзмененииВидаДокументаНомераКорректировки", ПараметрыФормы);
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Автосохранение", Автосохранение);
		ДополнительныеПараметры.Вставить("Вариант", Вариант);
		ДополнительныеПараметры.Вставить("ВыполняемоеОповещение", ВыполняемоеОповещение);
		ДополнительныеПараметры.Вставить("ПоказателиОтчета", ПоказателиОтчета);
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
		"ВопросПриИзмененииВидаДокументаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		
		ФормаВопроса.ОписаниеОповещенияОЗакрытии = ОписаниеОповещения;
		ФормаВопроса.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		ФормаВопроса.Открыть();
		
	Иначе
		Состояние(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Сохраняется %1...';
				|en = 'Сохраняется %1...'"), Заголовок), , , БиблиотекаКартинок.Записать);
		РезультатСохранения = Сохранить(Автосохранение, , Вариант, Ложь, ПоказателиОтчета);
		ПослеСохраненияНаКлиенте(ВыполняемоеОповещение, РезультатСохранения);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВопросПриИзмененииВидаДокументаЗавершение(КодВозврата, ДополнительныеПараметры) Экспорт
	
	Автосохранение = ДополнительныеПараметры.Автосохранение;
	Вариант = ДополнительныеПараметры.Вариант;
	ВыполняемоеОповещение = ДополнительныеПараметры.ВыполняемоеОповещение;
	ПоказателиОтчета = ДополнительныеПараметры.ПоказателиОтчета;
	
	Если КодВозврата = КодВозвратаДиалога.Да
		ИЛИ КодВозврата = КодВозвратаДиалога.Нет Тогда
		
		ВидДокументаНомерКорректировкиИзменен = ?(КодВозврата = КодВозвратаДиалога.Да, Истина, Ложь);
		
	Иначе
		
		Возврат;
		
	КонецЕсли;
	
	Состояние(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Сохраняется %1...';
			|en = 'Сохраняется %1...'"), Заголовок), , , БиблиотекаКартинок.Записать);
	РезультатСохранения = Сохранить(Автосохранение, , Вариант, ВидДокументаНомерКорректировкиИзменен, ПоказателиОтчета);
	ПослеСохраненияНаКлиенте(ВыполняемоеОповещение, РезультатСохранения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеСохраненияНаКлиенте(ВыполняемоеОповещение, РезультатСохранения)
	
	КлючУникальности = СтруктураРеквизитовФормы.мСохраненныйДок;
	
	Если РезультатСохранения Тогда
		
		РегламентированнаяОтчетностьКлиент.ПослеЗаписиРегламентированногоОтчета(ЭтотОбъект);
		
		Если ВыполняемоеОповещение <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ВыполняемоеОповещение);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПередСохранением(КодИФНС = "не применимо", ПодобныйОтчетСуществует="", Вариант="", ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки="", Автосохранение="", ПоказателиОтчета="")
	
	Если РегламентированнаяОтчетность.БылиИзмененыКлючевыеРеквизитыОтчета(ЭтотОбъект, КодИФНС)
		И РегламентированнаяОтчетность.СуществуетДокументСАналогичнымиРеквизитами(ЭтотОбъект, КодИФНС) Тогда
		
		ПодобныйОтчетСуществует = Истина;
		
	КонецЕсли;
	
	СтруктураРеквизитовФормы.мЗаписываетсяНовыйДокумент	= (СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено
		Или СтруктураРеквизитовФормы.мСкопированаФорма <> Неопределено);
	
	НовыйИлиСкопированныйДокумент = (СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено
		Или СтруктураРеквизитовФормы.мСкопированаФорма <> Неопределено);
	
	Если Вариант <> Неопределено И Вариант <> СтруктураРеквизитовФормы.мВариант И НЕ НовыйИлиСкопированныйДокумент Тогда
		
		ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки = Истина;
		
	КонецЕсли;
	
	Если ПодобныйОтчетСуществует 
		Или ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки Тогда
	
		Возврат Ложь;
	
	КонецЕсли;
	
	Возврат Сохранить(Автосохранение, КодИФНС, Вариант, , ПоказателиОтчета);
		
КонецФункции

&НаСервере
Функция Сохранить(Автосохранение = Ложь, КодИФНС = "не применимо", Вариант = Неопределено, ВидДокументаНомерКорректировкиИзменен = Неопределено, ПоказателиОтчета = Неопределено) Экспорт
	
	ДанныеДляРазблокирования = Неопределено;
	
	Если НЕ СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено Тогда
		Если НЕ СтруктураРеквизитовФормы.мБезОткрытияФормы Тогда
			ДанныеДляРазблокирования = Новый Структура("Ключ,ИдФормы",
			СтруктураРеквизитовФормы.мСохраненныйДок, УникальныйИдентификатор);
		КонецЕсли;
		СтруктураРеквизитовФормы.мСохраненныйДок = СтруктураРеквизитовФормы.мСохраненныйДок.ПолучитьОбъект();
	КонецЕсли;
	
	Если НЕ РегламентированнаяОтчетность.ПриЗаписиРегламентированногоОтчетаНаСервере(ЭтотОбъект, КодИФНС,
		Автосохранение, Вариант, ВидДокументаНомерКорректировкиИзменен, СтруктураРеквизитовФормы.мСохраненныйДок) Тогда
		СтруктураРеквизитовФормы.мСохраненныйДок = СтруктураРеквизитовФормы.мСохраненныйДок.Ссылка;
		Возврат Ложь;
	КонецЕсли;
	
	ОтчетМетаданные = Метаданные.Отчеты[Сред(Лев(СтрЗаменить(ИмяФормы, "Внешний", ""),
		СтрНайти(СтрЗаменить(ИмяФормы, "Внешний", ""), ".Форма.") - 1), 7)];
		
	ВидОтчетности = Перечисления.ВидыОтчетности.РегламентированнаяОтчетность;
		
	ПредставлениеВида = РегламентированнаяОтчетность.ПредставлениеВидаДокумента(Вариант);
	
	СтруктураРеквизитовФормы.мСохраненныйДок.ФорматВыгрузки 		  = "Автоматически";
	СтруктураРеквизитовФормы.мСохраненныйДок.ИсточникОтчета			  = ОтчетМетаданные.Имя;
	СтруктураРеквизитовФормы.мСохраненныйДок.НаименованиеОтчета		  = ОтчетМетаданные.ОсновнаяФорма.Синоним;
	СтруктураРеквизитовФормы.мСохраненныйДок.ДатаНачала               = СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета;
	СтруктураРеквизитовФормы.мСохраненныйДок.ДатаОкончания            = СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета;
	СтруктураРеквизитовФормы.мСохраненныйДок.Периодичность            = СтруктураРеквизитовФормы.мПериодичность;
	СтруктураРеквизитовФормы.мСохраненныйДок.ВыбраннаяФорма           = СтруктураРеквизитовФормы.мВыбраннаяФорма;
	СтруктураРеквизитовФормы.мСохраненныйДок.Организация              = СтруктураРеквизитовФормы.Организация;
	СтруктураРеквизитовФормы.мСохраненныйДок.ДатаПодписи              = ДатаПодписи;
	СтруктураРеквизитовФормы.мСохраненныйДок.ЕдиницаИзмерения         = Неопределено;
	СтруктураРеквизитовФормы.мСохраненныйДок.ТочностьЕдиницыИзмерения = СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения;
	СтруктураРеквизитовФормы.мСохраненныйДок.ВидОтчетности            = ВидОтчетности;
	СтруктураРеквизитовФормы.мСохраненныйДок.Вид                      = Вариант;
	СтруктураРеквизитовФормы.мСохраненныйДок.ПредставлениеВида		  = ПредставлениеВида;
	СтруктураРеквизитовФормы.мСохраненныйДок.Комментарий              = Комментарий;
	
	СтруктураРеквизитовФормы.мВариант = Вариант;

	СписокСохранения = Новый Структура();
	
	СписокСохранения.Вставить("ПоказателиОтчета", ПоказателиОтчета);
	ДанныеМногострочныхРазделов = Новый Структура;
	
	Для Каждого ЭлементСтруктуры Из мСтруктураМногострочныхРазделов Цикл
		
		ДанныеМногострочныхРазделов.Вставить(
			ЭлементСтруктуры.Ключ, ДанныеФормыВЗначение(ЭтотОбъект[ЭлементСтруктуры.Значение], Тип("ТаблицаЗначений")));
		
	КонецЦикла;
	
	СписокСохранения.Вставить("ДанныеМногострочныхРазделов", ДанныеМногострочныхРазделов);
	СписокСохранения.Вставить("ДанныеВариантовАвтоЗаполнения", Неопределено);
	СписокСохранения.Вставить("ВерсияФормы", СтруктураРеквизитовФормы.мВерсияФормы);
	СписокСохранения.Вставить("ФлОтклВыборЗначений", СтруктураРеквизитовФормы.мАвтоВыборКодов);
	СписокСохранения.Вставить("ФлажокОтклАвтоРасчет", СтруктураРеквизитовФормы.ФлажокОтклАвтоРасчет);
	СписокСохранения.Вставить("Доверенность", Доверенность);
	СписокСохранения.Вставить("ИмяФайлаДоверенности", ИмяФайлаДоверенности);
	СписокСохранения.Вставить("ХешДоверенности", ХешДоверенности);
	СписокСохранения.Вставить("_Приложения", ДанныеФормыВЗначение(_Приложения, Тип("ДеревоЗначений")));

	ХранилищеДанных = Новый ХранилищеЗначения(СписокСохранения);
	СтруктураРеквизитовФормы.мСохраненныйДок.ДанныеОтчета = ХранилищеДанных;

	// ПроцессыОбработкиДокументов
	//
	РегламентированнаяОтчетность.ПередЗаписьюНаСервере(ЭтотОбъект,
		СтруктураРеквизитовФормы.мСохраненныйДок, Новый Структура);
	//
	// ПроцессыОбработкиДокументов
	
	Попытка
		СтруктураРеквизитовФормы.мСохраненныйДок.Записать();
		РегламентированнаяОтчетность.СохранитьСтатусОтправкиРеглОтчета(ЭтотОбъект);
	Исключение
		
		Если НЕ Автосохранение Тогда
			ОбщегоНазначения.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1';
																												|en = '%1'"),
				РегламентированнаяОтчетностьКлиентСервер.СформироватьТекстСообщения(ОписаниеОшибки())));
		КонецЕсли;
		
		СтруктураРеквизитовФормы.мСохраненныйДок = СтруктураРеквизитовФормы.мСохраненныйДок.Ссылка;
		
		Возврат Ложь;
		
	КонецПопытки;
	
	// ПроцессыОбработкиДокументов
	//
	РегламентированнаяОтчетность.ПослеЗаписиНаСервере(ЭтотОбъект,
		СтруктураРеквизитовФормы.мСохраненныйДок, Новый Структура);
	//
	// ПроцессыОбработкиДокументов
	
	Модифицированность = Ложь;
	
	СтруктураРеквизитовФормы.мСохраненныйДок = СтруктураРеквизитовФормы.мСохраненныйДок.Ссылка;
	
	Если НЕ СтруктураРеквизитовФормы.мБезОткрытияФормы
       И НЕ СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено Тогда
				   
    	ЗаблокироватьДанныеДляРедактирования(
			СтруктураРеквизитовФормы.мСохраненныйДок, , УникальныйИдентификатор);

    КонецЕсли;
	
	Если ДанныеДляРазблокирования <> Неопределено Тогда
		РазблокироватьДанныеДляРедактирования(ДанныеДляРазблокирования.Ключ, ДанныеДляРазблокирования.ИдФормы);
	КонецЕсли;
	
	Возврат Истина;

КонецФункции

&НаКлиенте
Функция СобратьДанныеТекущегоТаблПоляНаКлиенте(ИмяТабличногоПоля) Экспорт
	
	Возврат РегламентированнаяОтчетностьКлиентСервер.СобратьДанныеТекущегоТаблПоля(ЭтотОбъект, ИмяТабличногоПоля);
	
КонецФункции

&НаСервере
Функция СобратьДанныеТекущегоТаблПоляНаСервере(ИмяТабличногоПоля) Экспорт
	
	Возврат РегламентированнаяОтчетностьКлиентСервер.СобратьДанныеТекущегоТаблПоля(ЭтотОбъект, ИмяТабличногоПоля);

КонецФункции

&НаСервере
Процедура ЗаполнитьПоказателиОтчетаНаСервере(ПоказателиОтчета)
	
	РегламентированнаяОтчетностьКлиентСервер.ЗаполнитьПоказателиОтчета(ЭтотОбъект, ПоказателиОтчета);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоказателиОтчетаНаКлиенте(ПоказателиОтчета)
	
	РегламентированнаяОтчетностьКлиентСервер.ЗаполнитьПоказателиОтчета(ЭтотОбъект, ПоказателиОтчета);
	
КонецПроцедуры

&НаКлиенте
Процедура ПронумероватьСтрокиДляПечати()
	Для Стр = 1 По 300 Цикл
		ИмяОбласти = "П000002000101_" + Стр;
		Если ПолеТабличногоДокументаФормаОтчета.Области.Найти(ИмяОбласти) = Неопределено Тогда 
			Прервать;
		КонецЕсли;
		ПолеТабличногоДокументаФормаОтчета.Области[ИмяОбласти].Значение = ?(Стр <= 20, "" + (200 + Стр), "");
	КонецЦикла;
	
	Для Стр = 1 По 80 Цикл
		ИмяОбласти = "П000004000100_" + Стр;
		Если ПолеТабличногоДокументаФормаОтчета.Области.Найти(ИмяОбласти) = Неопределено Тогда 
			Прервать;
		КонецЕсли;
		ПолеТабличногоДокументаФормаОтчета.Области[ИмяОбласти].Значение = ?(Стр <= 5, "" + (400 + Стр), "");
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПронумероватьСтрокиДляВыгрузки()
	ОбновитьСтруктуруМногострочногоРаздела(ЭтотОбъект, "П0000020001");
	ОбновитьСтруктуруМногострочногоРаздела(ЭтотОбъект, "П0000040001");
	
	Для Стр = 1 По 300 Цикл
		ИмяОбласти = "П000002000100_" + Стр;
		Обл = ПолеТабличногоДокументаФормаОтчета.Области.Найти(ИмяОбласти);
		Если Обл = Неопределено Тогда 
			Прервать;
		КонецЕсли;
		Обл.Значение = 2000 + Стр;
	КонецЦикла;
	
	Для Стр = 1 По 80 Цикл
		ИмяОбласти = "П000004000191_" + Стр;
		Обл = ПолеТабличногоДокументаФормаОтчета.Области.Найти(ИмяОбласти);
		Если Обл = Неопределено Тогда 
			Прервать;
		КонецЕсли;
		Обл.Значение = 400 + Стр;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗагрязняющиеВещества()
	ЗагрязняющиеВещества = РегламентированнаяОтчетностьКлиентСервер.НайтиСвойстваПоказателя(
		ЭтотОбъект, "ЗагрязняющиеВещества").ТаблицаЗначений;
	Вещество = ЗагрязняющиеВещества.НайтиСтроки(Новый Структура("Код", "703"));
	Если Вещество.Количество() > 0 Тогда
		ПолеТабличногоДокументаФормаОтчета.Области["П000002000102_1"].Значение = Вещество[0].Код;
		ПолеТабличногоДокументаФормаОтчета.Области["П000002000103_1"].Значение = Вещество[0].Название;
	КонецЕсли;
	
	Вещество = ЗагрязняющиеВещества.НайтиСтроки(Новый Структура("Код", "322"));
	Если Вещество.Количество() > 0 Тогда
		ПолеТабличногоДокументаФормаОтчета.Области["П000002000102_2"].Значение = Вещество[0].Код;
		ПолеТабличногоДокументаФормаОтчета.Области["П000002000103_2"].Значение = Вещество[0].Название;
	КонецЕсли;
	
	Вещество = ЗагрязняющиеВещества.НайтиСтроки(Новый Структура("Код", "410"));
	Если Вещество.Количество() > 0 Тогда
		ПолеТабличногоДокументаФормаОтчета.Области["П000002000102_3"].Значение = Вещество[0].Код;
		ПолеТабличногоДокументаФормаОтчета.Области["П000002000103_3"].Значение = Вещество[0].Название;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСтроку(Команда)
	
	#Если ВебКлиент Тогда
		
		ДобавитьСтрокуНаСервере("ФормаОтчета");
		
	#Иначе
		
		ДобавитьСтрокуНаКлиенте("ФормаОтчета");
		
	#КонецЕсли
	
	ПронумероватьСтрокиДляПечати();
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьСтрокуНаСервере(КолонкаРазделыОтчетаСокрНаим)
	
	ВставитьДополнительнуюСтроку(ЭтотОбъект,КолонкаРазделыОтчетаСокрНаим);

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСтрокуНаКлиенте(КолонкаРазделыОтчетаСокрНаим)
	
	ВставитьДополнительнуюСтроку(ЭтотОбъект, КолонкаРазделыОтчетаСокрНаим);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ВставитьДополнительнуюСтрокуВГруппу(Форма, ТекущаяСтраница, ИмяГруппы)
	Перем СтруктураГруппы;
	Перем МаксимальноеКоличествоСтрокГруппы;
	
	Если ИмяГруппы = "П0000020001" Тогда
		Если Форма.ПолеТабличногоДокументаФормаОтчета.Области.Найти("П000002000101_300") <> Неопределено Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ИмяГруппы = "П0000040001" Тогда 
		Если Форма.ПолеТабличногоДокументаФормаОтчета.Области.Найти("П000004000101_80") <> Неопределено Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Форма.мСтруктураИсхКолвоСтрокРазделов.Свойство(ИмяГруппы, МаксимальноеКоличествоСтрокГруппы);
	
	ТекТабличноеПолеИмя = "ПолеТабличногоДокументаФормаОтчета";
	ТекТабличноеПоле = Форма[ТекТабличноеПолеИмя];
	Форма.мСтруктураМногострочныхРазделов.Свойство(ИмяГруппы, СтруктураГруппы);
	СтруктураГруппы = Форма[СтруктураГруппы];

	КолСтрок = СтруктураГруппы.Количество();
	КолСтрокСтр = Формат(КолСтрок, "ЧГ=0");
	КолСтрокСтр1 = Формат(КолСтрок + 1, "ЧГ=0");
	ИмяПервойЯчейкиПоследнейСтроки = ИмяГруппы + "01_" + КолСтрокСтр;

	ОбластьЯчейки = ТекТабличноеПоле.Области[ИмяПервойЯчейкиПоследнейСтроки];

	НомерВерхЯчейкиОбласти = ОбластьЯчейки.Верх;
	НомерНижнЯчейкиОбласти = ОбластьЯчейки.Низ;
	СдвигСтрок = НомерНижнЯчейкиОбласти - НомерВерхЯчейкиОбласти + 1;

	НомерВерхЯчейкиНовойОбласти = НомерВерхЯчейкиОбласти + СдвигСтрок;
	НомерНижнЯчейкиНовойОбласти = НомерНижнЯчейкиОбласти + СдвигСтрок;

	РегламентированнаяОтчетностьВызовСервера.СпециальногоДобавленияСтроки(
		ТекТабличноеПоле, ТекТабличноеПолеИмя, ИмяГруппы, НомерВерхЯчейкиОбласти, НомерНижнЯчейкиОбласти);
	
	Форма[ТекТабличноеПолеИмя] = ТекТабличноеПоле;
	
	Для НомерГрафы = 0 По 93 Цикл
		Если НомерГрафы >= 10 И НомерГрафы <= 90 Тогда 
			Продолжить;
		КонецЕсли;
		
		ИмяКолонки = ИмяГруппы + Формат(НомерГрафы, "ЧЦ=2; ЧВН=; ЧН=00");
		
		Если СтруктураГруппы[0].Свойство(ИмяКолонки) Тогда
			
			ИмяОбластиЯчейки    = ИмяКолонки + "_" + КолСтрокСтр;
			ИмяНовОбластиЯчейки = ИмяКолонки + "_" + КолСтрокСтр1;
			
			ОбластьЯчейки = ТекТабличноеПоле.Области[ИмяОбластиЯчейки];
			ОбластьЯчейки.Имя = ИмяНовОбластиЯчейки;
			
			НомерВерхСтрокиЯчейки  = ОбластьЯчейки.Верх - СдвигСтрок;
			НомерНижнСтрокиЯчейки  = ОбластьЯчейки.Низ  - СдвигСтрок;
			
			НомерЛевоКолонкиЯчейки = ОбластьЯчейки.Лево;
			НомерПравКолонкиЯчейки = ОбластьЯчейки.Право;
			
			ОбластьЯчейкиИсходнойОбласти = ТекТабличноеПоле.Область(
				НомерВерхСтрокиЯчейки, НомерЛевоКолонкиЯчейки, НомерНижнСтрокиЯчейки, НомерПравКолонкиЯчейки);
			ОбластьЯчейкиИсходнойОбласти.Имя = ИмяОбластиЯчейки;
			
			Если НомерГрафы = 1 Тогда
				НовОбластьЯчейкиУдалитьСтроку = ТекТабличноеПоле.Область(ОбластьЯчейки.Верх, 2, ОбластьЯчейки.Верх, 2);
				НовОбластьЯчейкиУдалитьСтроку.Имя = "УдалитьСтроку" + ИмяНовОбластиЯчейки;
				ИсхОбластьЯчейкиУдалитьСтроку = ТекТабличноеПоле.Область(
					ОбластьЯчейкиИсходнойОбласти.Верх, 2, ОбластьЯчейкиИсходнойОбласти.Верх, 2);
				ИсхОбластьЯчейкиУдалитьСтроку.Имя = "УдалитьСтроку" + ИмяОбластиЯчейки;
			КонецЕсли;
			
			Если ОбластьЯчейки.СодержитЗначение = Истина Тогда
				ОбластьЯчейки.Очистить();
			Иначе
				ИсходныйНомер        = Число("0" + ОбластьЯчейки.Текст);
				НовыйПорядковыйНомер = ИсходныйНомер + 1;
				
				ОбластьЯчейки.Текст = 
					?(НЕ НовыйПорядковыйНомер > МаксимальноеКоличествоСтрокГруппы, Строка(НовыйПорядковыйНомер), "");
				
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	Если ИмяГруппы = "П0000020001" Тогда
		ИмяТекущейОбласти = ИмяГруппы + "02_" + КолСтрокСтр1;
	Иначе
		ИмяТекущейОбласти = ИмяГруппы + "01_" + КолСтрокСтр1;
	КонецЕсли;
	Форма.Элементы[ТекТабличноеПолеИмя].ТекущаяОбласть = ТекТабличноеПоле.Области[ИмяТекущейОбласти];
	Форма.ТекущийЭлемент = Форма.Элементы[ТекТабличноеПолеИмя];

	СтруктураГруппы.Добавить();
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ВставитьДополнительнуюСтроку(Форма, ТекущаяСтраница)
	
	Перем СтруктураГруппы;
	Перем МаксимальноеКоличествоСтрокГруппы;

	#Если НаКлиенте Тогда
		ИмяГруппы = Форма.ОпределитьИдМногострочногоРазделаПоАктивнойСтраницеНаКлиенте(ТекущаяСтраница);
	#Иначе
		ИмяГруппы = Форма.ОпределитьИдМногострочногоРазделаПоАктивнойСтраницеНаСервере(ТекущаяСтраница);
	#КонецЕсли
		
	Если ИмяГруппы = Неопределено Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Не выбран многострочный раздел, в который необходимо добавить строку.';
								|en = 'Не выбран многострочный раздел, в который необходимо добавить строку.'");
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	ВставитьДополнительнуюСтрокуВГруппу(Форма, ТекущаяСтраница, ИмяГруппы);
	Форма.Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура УдалитьСтроку(Команда)
	
	ТекТабличноеПоле = ЭтотОбъект["ПолеТабличногоДокументаФормаОтчета"];
	
	ТекущаяОбласть = ТекТабличноеПоле.ТекущаяОбласть;
	ИмяГруппы      = РазделТекущейОбласти(ЭтотОбъект, ТекущаяОбласть);
	
	Если ИмяГруппы = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не выбрана удаляемая строка многострочного раздела.';
														|en = 'Не выбрана удаляемая строка многострочного раздела.'"));
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = НСтр("ru = 'Удалить выбранную строку?';
						|en = 'Удалить выбранную строку?'");
	ОписаниеОповещения = Новый ОписаниеОповещения("УдалитьСтрокуЗавершение", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьСтрокуЗавершение(Ответ, ДополнительныеПараметры) Экспорт

	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
		
	#Если ВебКлиент Тогда
		
		УдалитьДополнительнуюСтрокуНаСервере("ФормаОтчета");
		
	#Иначе
		
		УдалитьДополнительнуюСтрокуНаКлиенте("ФормаОтчета");
		
	#КонецЕсли
	
	РасчетНаКлиенте("", "");
	ПронумероватьСтрокиДляПечати();
	
КонецПроцедуры

&НаСервере
Процедура УдалитьДополнительнуюСтрокуНаСервере(ТекущаяСтраница)
	
	УдалитьДополнительнуюСтроку(ЭтотОбъект, , , ТекущаяСтраница);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьДополнительнуюСтрокуНаКлиенте(ТекущаяСтраница)
	
	УдалитьДополнительнуюСтроку(ЭтотОбъект, , , ТекущаяСтраница);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УдалитьДополнительнуюСтрокуИзГруппы(Форма, СпециальноеДобавление, ИмяКолонкиНомера, ТекущаяСтраница, ИмяГруппы, НомУдаляемойСтроки, НомерСтрокиТаблицы)
	Перем ИсходноеКоличествоСтрокГруппы;
	Перем СтруктураГруппы;
	
	Форма.мСтруктураИсхКолвоСтрокРазделов.Свойство(ИмяГруппы, ИсходноеКоличествоСтрокГруппы);
	
	Форма.мСтруктураМногострочныхРазделов.Свойство(ИмяГруппы, СтруктураГруппы);
	
	ТекТабличноеПолеИмя = "ПолеТабличногоДокументаФормаОтчета";
	ТекТабличноеПоле = Форма["ПолеТабличногоДокументаФормаОтчета"];
	
	ОбновитьСтруктуруМногострочногоРаздела(Форма, ИмяГруппы);
	СтруктураГруппы = Форма[СтруктураГруппы];
	Если СтруктураГруппы.Количество() > ИсходноеКоличествоСтрокГруппы Тогда
		ИмяПервойЯчейкиУдаляемойСтроки = ИмяГруппы + "01_" + НомУдаляемойСтроки;
		
		ОбластьЯчейки = ТекТабличноеПоле.Области[ИмяПервойЯчейкиУдаляемойСтроки];
		НомерВерхЯчейкиОбласти = ОбластьЯчейки.Верх;
		НомерНижнЯчейкиОбласти = ОбластьЯчейки.Верх;
		
		РегламентированнаяОтчетностьВызовСервера.СпециальногоУдаленияСтроки(
			ТекТабличноеПоле, ТекТабличноеПолеИмя, ИмяГруппы, НомерВерхЯчейкиОбласти, НомерНижнЯчейкиОбласти);
		
		Форма[ТекТабличноеПолеИмя] = ТекТабличноеПоле;
		
		КолвоСдвигаемыхСтрок = СтруктураГруппы.Количество() - НомУдаляемойСтроки;
		
		Если КолвоСдвигаемыхСтрок > 0 Тогда
			
			Для НСтр = НомУдаляемойСтроки + 1 По СтруктураГруппы.Количество() Цикл
				
				ТекСтрока = СтруктураГруппы[НСтр-1];
				
				Для НомерГрафы = 0 По 93 Цикл
					Если НомерГрафы >= 10 И НомерГрафы <= 90 Тогда 
						Продолжить;
					КонецЕсли;
					
					КолонкаИмя = ИмяГруппы + Формат(НомерГрафы, "ЧЦ=2; ЧВН=; ЧН=00");
					
					Если СтруктураГруппы[0].Свойство(КолонкаИмя) Тогда
						
						ИмяСдвинутойЯчейки = КолонкаИмя + "_" + НСтр;
						НовоеИмяСдвинутойЯчейки = КолонкаИмя + "_" + (НСтр - 1);
						
						ОбластьСдвинутойЯчейки     = ТекТабличноеПоле.Области[ИмяСдвинутойЯчейки];
						ОбластьСдвинутойЯчейки.Имя = НовоеИмяСдвинутойЯчейки;
						
						Если НомерГрафы = 1 Тогда
							ОбластьЯчейкиУдалитьСтроку = ТекТабличноеПоле.Область(
								ОбластьСдвинутойЯчейки.Верх, 2, ОбластьСдвинутойЯчейки.Верх, 2);
							ОбластьЯчейкиУдалитьСтроку.Имя = "УдалитьСтроку" + НовоеИмяСдвинутойЯчейки;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
				
			КонецЦикла;
		КонецЕсли;
		
		СтруктураГруппы.Удалить(НомерСтрокиТаблицы);
	Иначе
		СтруктураГруппы.Удалить(НомерСтрокиТаблицы);
		ДобавляемаяСтрока = СтруктураГруппы.Добавить();
		
		Для Каждого СтрокаТаблицы Из СтруктураГруппы Цикл
			
			НомерСтрокиВТаблице = СтруктураГруппы.Индекс(СтрокаТаблицы);
			НомерСтрокиВФормеТаблицы = Формат(НомерСтрокиВТаблице + 1, "ЧГ=0");
			
			Для НомерГрафы = 0 По 93 Цикл
				Если НомерГрафы >= 10 И НомерГрафы <= 90 Тогда 
					Продолжить;
				КонецЕсли;
				
				КолонкаТаблицыИмя = ИмяГруппы + Формат(НомерГрафы, "ЧЦ=2; ЧВН=; ЧН=00");
				
				Если СтруктураГруппы[0].Свойство(КолонкаТаблицыИмя) Тогда
					
					ИмяЯчейки                = КолонкаТаблицыИмя + "_" + НомерСтрокиВФормеТаблицы;
					ОбластьЯчейкиТаблицы     = ТекТабличноеПоле.Области[ИмяЯчейки];
					
				КонецЕсли;
			КонецЦикла;

		КонецЦикла;
		
		#Если НаКлиенте Тогда
			Форма.ВывестиРазделВТабличныйДокументНаКлиенте(ИмяГруппы, СтруктураГруппы);
		#Иначе
			Форма.ВывестиРазделВТабличныйДокументНаСервере(ИмяГруппы, СтруктураГруппы);
		#КонецЕсли
	КонецЕсли;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УдалитьДополнительнуюСтроку(Форма, СпециальноеДобавление = Ложь, ИмяКолонкиНомера = Неопределено, ТекущаяСтраница = Неопределено) Экспорт
	
	Перем ИсходноеКоличествоСтрокГруппы;
	Перем СтруктураГруппы;
	
	#Если НаКлиенте Тогда
		ИмяГруппы = Форма.ОпределитьИдМногострочногоРазделаПоАктивнойСтраницеНаКлиенте(ТекущаяСтраница);
	#Иначе
		ИмяГруппы = Форма.ОпределитьИдМногострочногоРазделаПоАктивнойСтраницеНаСервере(ТекущаяСтраница);
	#КонецЕсли
	
	ИмяЯчейки = Форма.Элементы["ПолеТабличногоДокументаФормаОтчета"].ТекущаяОбласть.Имя;
	НомУдаляемойСтроки = Число( Сред( ИмяЯчейки, СтрНайти( ИмяЯчейки, "_") + 1));
	НомерСтрокиТаблицы = НомУдаляемойСтроки - 1;
	
	УдалитьДополнительнуюСтрокуИзГруппы(Форма, СпециальноеДобавление, ИмяКолонкиНомера,
		ТекущаяСтраница, ИмяГруппы, НомУдаляемойСтроки, НомерСтрокиТаблицы);
	Форма.Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Функция ОпределитьИдМногострочногоРазделаПоАктивнойСтраницеНаКлиенте(ИмяЛиста = Неопределено) Экспорт
	
	ИдГрупп = Новый Структура;
	
	Возврат ОпределитьИдМногострочногоРазделаПоАктивнойСтранице(ЭтотОбъект, ИмяЛиста, ИмяЛиста, ИдГрупп);

КонецФункции

&НаСервере
Функция ОпределитьИдМногострочногоРазделаПоАктивнойСтраницеНаСервере(ИмяЛиста = Неопределено) Экспорт
	
	ИдГрупп = Новый Структура;
	
	Возврат ОпределитьИдМногострочногоРазделаПоАктивнойСтранице(ЭтотОбъект, ИмяЛиста, ИмяЛиста, ИдГрупп);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ОпределитьИдМногострочногоРазделаПоАктивнойСтранице(Форма, ИмяЛиста = Неопределено, ТекущаяСтраница = Неопределено, ИдГрупп = Неопределено)
	
	Перем ИмяГруппы;

	Если ИмяЛиста = Неопределено Тогда
		ИмяТекущейСтраницыПанели = ТекущаяСтраница.Имя;
	Иначе
		ИмяТекущейСтраницыПанели = ИмяЛиста;
	КонецЕсли;

	Если ИмяТекущейСтраницыПанели = "ФормаОтчета" Тогда
		
		ИмяГруппы = РазделТекущейОбласти(Форма, Форма.Элементы.ПолеТабличногоДокументаФормаОтчета.ТекущаяОбласть);
		
	КонецЕсли;

	Возврат ИмяГруппы;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьСтруктуруМногострочногоРаздела(Форма, ИмяГруппы) Экспорт
	
	Перем СтруктураГруппы;
	
	Форма.мСтруктураМногострочныхРазделов.Свойство(ИмяГруппы, СтруктураГруппы);
	
	СтруктураГруппы = Форма[СтруктураГруппы];
	
	ТекТабличноеПолеИмя = "ПолеТабличногоДокументаФормаОтчета";
	
	ТекТабличноеПоле = Форма[ТекТабличноеПолеИмя];
	
	Для НСтр = 1 По СтруктураГруппы.Количество() Цикл
		ТекСтрокаТаблицы = СтруктураГруппы[НСтр - 1];
		НСтрСтр = Формат(НСтр, "ЧГ=0");
		
		Для НомерГрафы = 0 По 93 Цикл
			Если НомерГрафы >= 10 И НомерГрафы <= 90 Тогда 
				Продолжить;
			КонецЕсли;
			
			ИмяКолонки = ИмяГруппы + Формат(НомерГрафы, "ЧЦ=2; ЧВН=; ЧН=00");
			
			Если СтруктураГруппы[0].Свойство(ИмяКолонки) Тогда
				
				ИмяЯчейки     = ИмяКолонки + "_" + НСтрСтр;
				ОбластьЯчейки = ТекТабличноеПоле.Области[ИмяЯчейки];
				
				Если ОбластьЯчейки.СодержитЗначение = Истина Тогда
					ЗначениеЯчейки = ОбластьЯчейки.Значение;
				Иначе
					ЗначениеЯчейки = ОбластьЯчейки.Текст;
				КонецЕсли;
				
				ТекСтрокаТаблицы[ИмяКолонки] = ЗначениеЯчейки;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ВывестиРазделВТабличныйДокументНаСервере(ИмяГруппы, СтруктураГруппы) Экспорт
	
	ВывестиРазделВТабличныйДокумент(ЭтотОбъект, ИмяГруппы, СтруктураГруппы);
	
КонецПроцедуры

&НаКлиенте
Процедура ВывестиРазделВТабличныйДокументНаКлиенте(ИмяГруппы, СтруктураГруппы) Экспорт
	
	ВывестиРазделВТабличныйДокумент(ЭтотОбъект, ИмяГруппы, СтруктураГруппы);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ВывестиРазделВТабличныйДокумент(Форма, ИмяГруппы, СтруктураГруппы)
	
	Перем ИсходноеКоличествоСтрокГруппы;

	Форма.мСтруктураИсхКолвоСтрокРазделов.Свойство(ИмяГруппы, ИсходноеКоличествоСтрокГруппы);

	ТекущееКоличествоСтрокГруппы = СтруктураГруппы.Количество();
	
	ТекТабличноеПолеИмя = "ПолеТабличногоДокументаФормаОтчета";
	
	ТекТабличноеПоле = Форма[ТекТабличноеПолеИмя];
	
	Если ТекущееКоличествоСтрокГруппы > ИсходноеКоличествоСтрокГруппы Тогда
		
		РегламентированнаяОтчетностьКлиентСервер.ДобавитьНеобходимоеКоличествоДопСтрок(
			Форма, ТекТабличноеПоле, ТекТабличноеПолеИмя, ИсходноеКоличествоСтрокГруппы,
			ТекущееКоличествоСтрокГруппы, ИмяГруппы, СтруктураГруппы);
		
		Форма[ТекТабличноеПолеИмя] = ТекТабличноеПоле;
		
	КонецЕсли;
	
	ТекТабличноеПоле = Форма[ТекТабличноеПолеИмя];

	Для Каждого СтрокаТаблицы Из СтруктураГруппы Цикл
		
		Для НомерГрафы = 0 По 93 Цикл
			Если НомерГрафы >= 10 И НомерГрафы <= 90 Тогда 
				Продолжить;
			КонецЕсли;
			
			КолонкаТаблицыИмя = ИмяГруппы + Формат(НомерГрафы, "ЧЦ=2; ЧВН=; ЧН=00");
			
			Если СтруктураГруппы[0].Свойство(КолонкаТаблицыИмя) Тогда
				
				НомерСтрокиВТаблице      = СтруктураГруппы.Индекс(СтрокаТаблицы);
				НомерСтрокиВФормеТаблицы = НомерСтрокиВТаблице + 1;
				ИмяЯчейки                = КолонкаТаблицыИмя + "_" + НомерСтрокиВФормеТаблицы;
				ОбластьЯчейкиТаблицы     = ТекТабличноеПоле.Области[ИмяЯчейки];
				ЗначениеЯчейки           = СтрокаТаблицы[КолонкаТаблицыИмя];
				
				Если ОбластьЯчейкиТаблицы.СодержитЗначение Тогда
					ОбластьЯчейкиТаблицы.Значение = ЗначениеЯчейки;
					
				Иначе
					
					ОбластьЯчейкиТаблицы.Текст    = ЗначениеЯчейки;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	ОтображениеГиперссылокДоверенности(Форма);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если СтрНайти(Заголовок, СтруктураРеквизитовФормы.СтрПериодОтчета) = 0 Тогда
		Заголовок = Заголовок + " за " + СтруктураРеквизитовФормы.СтрПериодОтчета;
	КонецЕсли;
	
	ОргСтр = " (" + СтруктураРеквизитовФормы.НаимОрганизации + ")";
	Если СтрНайти(Заголовок, ОргСтр) = 0 Тогда
		Заголовок = Заголовок + ОргСтр;
	КонецЕсли;
	
	ПронумероватьСтрокиДляПечати();
	
	Заголовок = Заголовок + ?(СтрНайти(ИмяФормы, "Внешний") > 0, " - внешний отчет", "");
	
	ПодключитьОбработчикОжидания("ВыполнитьПроверкуПередОткрытием", 0.1, Истина);

КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПроверкуПередОткрытием()
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииЗавершение", ЭтотОбъект);
	Отказ = Ложь;
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередОткрытиемФормыРегламентированногоОтчета(
		ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Модифицированность = Ложь;
		Закрыть();
		Возврат;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	РегламентированнаяОтчетностьКлиент.ПередЗакрытиемРегламентированногоОтчета(
		ЭтотОбъект, Отказ, СтандартнаяОбработка, ЗавершениеРаботы, ТекстПредупреждения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
				
		Возврат;
		
	КонецЕсли;
	
	ПриЗакрытииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗакрытииНаСервере()
	
	РегламентированнаяОтчетность.ПриЗакрытииРегламентированногоОтчета(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеТабличногоДокументаФормаОтчетаВыбор(Элемент, Область, СтандартнаяОбработка)
	
	// ПроцессыОбработкиДокументов
	//
	Если Элементы.ПолеТабличногоДокументаФормаОтчета.Защита Тогда
		
		Возврат;
		
	КонецЕсли;
	//
	// ПроцессыОбработкиДокументов
	
	Если Область.Имя = "ОргАдрес" Тогда
		РегламентированнаяОтчетностьКлиент.ОбработкаАдресаВСтатистическомОтчете(ЭтотОбъект, Область, СтандартнаяОбработка);
	ИначеЕсли СтрЧислоВхождений(Область.Имя, "ДобавитьСтроку") > 0 Тогда
		СтандартнаяОбработка = Ложь;
		ДобавитьСтроку("");
	ИначеЕсли СтрЧислоВхождений(Область.Имя, "УдалитьСтроку") > 0 Тогда
		УдалитьСтроку("");
	ИначеЕсли Область.Имя = "is_separate" 
		Или Область.Имя = "is_ceo"
		Или Область.Имя = "is_non_resident" Тогда
		Область.Значение = Не Область.Значение;
		СтандартнаяОбработка = Ложь;
		Модифицированность = Истина;
	ИначеЕсли Область.Имя = "ФайлДоверенностиДобавить" Тогда
		Если Не ЗначениеЗаполнено(Доверенность) Тогда 
			Если Не ЗначениеЗаполнено(СтруктураРеквизитовФормы.мСохраненныйДок) Или Модифицированность Тогда 
				ТекстВопроса = "Перед добавлением доверенности необходимо сохранить отчет. Продолжить?";
				ОО = Новый ОписаниеОповещения("ВопросДоверенностьЗавершение", ЭтотОбъект);
				ПоказатьВопрос(ОО, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
			Иначе
				ВопросДоверенностьЗавершение(КодВозвратаДиалога.Да, Неопределено);
			КонецЕсли;
		Иначе
			РаботаСФайламиКлиент.ОткрытьФайл(ДанныеФайла(Доверенность), Ложь);
		КонецЕсли;
	ИначеЕсли Область.Имя = "ФайлДоверенностиУдалить" И Область.Гиперссылка = Истина И ЗначениеЗаполнено(Доверенность) Тогда
		УдалитьФайлДоверенности();
	ИначеЕсли Область.Имя = "ОргРПН" Или Область.Имя = "КодОргРПН" Тогда
		ПараметрВводаПоля = РегламентированнаяОтчетностьКлиентСервер.НайтиСвойстваПоказателя(ЭтотОбъект, "ОрганыРПН");
		ОбластьДляВвода     = ПолеТабличногоДокументаФормаОтчета.Области["КодОргРПН"];
		КопироватьДанныеФормы(ПараметрВводаПоля.ТаблицаЗначений, ТЗВыбора);
		ВвестиПоказатель(Элемент, ОбластьДляВвода, "", Ложь, ПараметрВводаПоля.ТекстПриВыборе,
			ТЗВыбора, СтандартнаяОбработка, "ОргРПН");
	ИначеЕсли Область.Имя = "region_oktmo" Или Область.Имя = "region_name" Тогда
		ПараметрВводаПоля = РегламентированнаяОтчетностьКлиентСервер.НайтиСвойстваПоказателя(ЭтотОбъект, "ОКТМОРегионы");
		ОбластьДляВвода = ПолеТабличногоДокументаФормаОтчета.Области["region_oktmo"];
		ВвестиПоказатель(Элемент, ОбластьДляВвода, ПараметрВводаПоля.Показатель, Ложь, ПараметрВводаПоля.ТекстПриВыборе,
			ПараметрВводаПоля.ТаблицаЗначений, СтандартнаяОбработка, "region_name");

	ИначеЕсли Область.Имя = "post_address_municipality_oktmo" Или Область.Имя = "post_address_municipality_name" Тогда
		ПараметрВводаПоля = РегламентированнаяОтчетностьКлиентСервер.НайтиСвойстваПоказателя(ЭтотОбъект, "ОКТМО");
		ОбластьДляВвода = ПолеТабличногоДокументаФормаОтчета.Области["post_address_municipality_oktmo"];
		ВвестиПоказатель(Элемент, ОбластьДляВвода, ПараметрВводаПоля.Показатель, Ложь, ПараметрВводаПоля.ТекстПриВыборе,
			ПараметрВводаПоля.ТаблицаЗначений, СтандартнаяОбработка, "post_address_municipality_name");
	ИначеЕсли Область.Имя = "legal_address_municipality_oktmo" Или Область.Имя = "legal_address_municipality_name" Тогда
		ПараметрВводаПоля = РегламентированнаяОтчетностьКлиентСервер.НайтиСвойстваПоказателя(ЭтотОбъект, "ОКТМО");
		ОбластьДляВвода = ПолеТабличногоДокументаФормаОтчета.Области["legal_address_municipality_oktmo"];
		ВвестиПоказатель(Элемент, ОбластьДляВвода, ПараметрВводаПоля.Показатель, Ложь, ПараметрВводаПоля.ТекстПриВыборе,
			ПараметрВводаПоля.ТаблицаЗначений, СтандартнаяОбработка, "legal_address_municipality_name");
	ИначеЕсли Область.Имя = "territory_org_code" Или Область.Имя = "territory_org_name" Тогда
		ПараметрВводаПоля = РегламентированнаяОтчетностьКлиентСервер.НайтиСвойстваПоказателя(ЭтотОбъект, "РПН");
		ОбластьДляВвода = ПолеТабличногоДокументаФормаОтчета.Области["territory_org_code"];
		ВвестиПоказатель(Элемент, ОбластьДляВвода, ПараметрВводаПоля.Показатель, Ложь, ПараметрВводаПоля.ТекстПриВыборе,
			ПараметрВводаПоля.ТаблицаЗначений, СтандартнаяОбработка, "territory_org_name");
			
	ИначеЕсли Область.Имя = "onvos_oktmo" 
		Или Область.Имя = "onvos_oktmo_name"
		Или Область.Имя = "КодОКТМООНВ" Тогда
		ПараметрВводаПоля = РегламентированнаяОтчетностьКлиентСервер.НайтиСвойстваПоказателя(ЭтотОбъект, "ОКТМО");
		ОбластьДляВвода = ПолеТабличногоДокументаФормаОтчета.Области["onvos_oktmo"];
		ВвестиПоказатель(Элемент, ОбластьДляВвода, ПараметрВводаПоля.Показатель, Ложь, ПараметрВводаПоля.ТекстПриВыборе,
			ПараметрВводаПоля.ТаблицаЗначений, СтандартнаяОбработка, "onvos_oktmo_name");
			
	ИначеЕсли Область.Имя = "not_registered_reason_id" Или Область.Имя = "not_registered_reason_name" Тогда
		ПараметрВводаПоля = РегламентированнаяОтчетностьКлиентСервер.НайтиСвойстваПоказателя(ЭтотОбъект, "НеРегистрация");
		ОбластьДляВвода = ПолеТабличногоДокументаФормаОтчета.Области["not_registered_reason_id"];
		ВвестиПоказатель(Элемент, ОбластьДляВвода, ПараметрВводаПоля.Показатель, Ложь, ПараметрВводаПоля.ТекстПриВыборе,
			ПараметрВводаПоля.ТаблицаЗначений, СтандартнаяОбработка, "not_registered_reason_name");
	ИначеЕсли Область.Имя = "okved" Тогда
		ПараметрВводаПоля = РегламентированнаяОтчетностьКлиентСервер.НайтиСвойстваПоказателя(ЭтотОбъект, "ОКВЭД");
		ВвестиПоказатель(Элемент, Область, ПараметрВводаПоля.Показатель, Ложь, ПараметрВводаПоля.ТекстПриВыборе,
			ПараметрВводаПоля.ТаблицаЗначений, СтандартнаяОбработка, Неопределено);
	ИначеЕсли (Не (Область.Защита = Истина)) И СтрНайти(Область.Имя, "_") = 14 Тогда 
		НомерСтроки = Сред(Область.Имя, 15);
		
		Если СтрНайти(Область.Имя, "П000002000102_") = 1 Или СтрНайти(Область.Имя, "П000002000103_") = 1 Тогда
			ПараметрВводаПоля = РегламентированнаяОтчетностьКлиентСервер.НайтиСвойстваПоказателя(ЭтотОбъект,
				"ЗагрязняющиеВещества");
			ОбластьДляВвода = ПолеТабличногоДокументаФормаОтчета.Области["П000002000102_" + НомерСтроки];
			ИмяСмежнойОбласти = ПолеТабличногоДокументаФормаОтчета.Области["П000002000103_" + НомерСтроки].Имя;
		ИначеЕсли СтрНайти(Область.Имя, "П000004000103_") = 1 Тогда
			ПараметрВводаПоля = РегламентированнаяОтчетностьКлиентСервер.НайтиСвойстваПоказателя(ЭтотОбъект, "ТипМероприятия");
			ОбластьДляВвода = ПолеТабличногоДокументаФормаОтчета.Области["П000004000103_" + НомерСтроки];
			ИмяСмежнойОбласти = ПолеТабличногоДокументаФормаОтчета.Области["П000004000193_" + НомерСтроки].Имя;
		ИначеЕсли СтрНайти(Область.Имя, "П000004000104_") = 1 Тогда
			ПараметрВводаПоля = РегламентированнаяОтчетностьКлиентСервер.НайтиСвойстваПоказателя(ЭтотОбъект,
				"ПризнакМероприятия");
			ОбластьДляВвода = Область;
		Иначе 
			Возврат;
		КонецЕсли;
		
		КодПоказателя = ПараметрВводаПоля.Показатель;
		КопироватьДанныеФормы(ПараметрВводаПоля.ТаблицаЗначений, ТЗВыбора);
		ВвестиПоказатель(Элемент, ОбластьДляВвода, КодПоказателя, Ложь, ПараметрВводаПоля.ТекстПриВыборе,
			ТЗВыбора, СтандартнаяОбработка, ИмяСмежнойОбласти);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОтображениеГиперссылокДоверенности(Форма)
	ФайлДоверенностиУдалить = Форма.ПолеТабличногоДокументаФормаОтчета.Области["ФайлДоверенностиУдалить"];
	ФайлДоверенностиДобавить = Форма.ПолеТабличногоДокументаФормаОтчета.Области["ФайлДоверенностиДобавить"];
	Если ЗначениеЗаполнено(Форма.Доверенность) Тогда 
		ФайлДоверенностиУдалить.Гиперссылка = Истина;
		ФайлДоверенностиУдалить.Текст = "х";
		ФайлДоверенностиДобавить.Текст = Форма.ИмяФайлаДоверенности;
	Иначе
		ФайлДоверенностиУдалить.Гиперссылка = Ложь;
		ФайлДоверенностиУдалить.Текст = "";
		ФайлДоверенностиДобавить.Текст = "Добавить файл доверенности";
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура УдалитьФайлДоверенности()
	Попытка
		ПрисоединенныйФайлОбъект = Доверенность.ПолучитьОбъект();
		Если ПравоДоступа("Удаление", Метаданные.Справочники.РегламентированныйОтчетПрисоединенныеФайлы) Тогда 
			ПрисоединенныйФайлОбъект.Удалить();
		Иначе
			ПрисоединенныйФайлОбъект.ПометкаУдаления = Истина;
			ПрисоединенныйФайлОбъект.Записать();
		КонецЕсли;
		Доверенность = Справочники.РегламентированныйОтчетПрисоединенныеФайлы.ПустаяСсылка();
		ХешДоверенности = "";
		ПолеТабличногоДокументаФормаОтчета.Области["attorney_file_attachment_file"].Значение = "";
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю("При установке файла доверенности произошли ошибки");
		Возврат;
	КонецПопытки;
	
	ПоказателиОтчета = Новый Структура;
	ЗаполнитьПоказателиОтчетаНаСервере(ПоказателиОтчета);
	Сохранить(,,,,ПоказателиОтчета);
	ОтображениеГиперссылокДоверенности(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ВопросДоверенностьЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Если Модифицированность Или Не ЗначениеЗаполнено(СтруктураРеквизитовФормы.мСохраненныйДок) Тогда
			ОО = Новый ОписаниеОповещения("ПослеСохраненияЗавершениеЗавершение", ЭтотОбъект);
			СохранитьНаКлиенте(, ОО);
		Иначе
			ПослеСохраненияЗавершениеЗавершение(Неопределено, Неопределено);
		КонецЕсли;
	Иначе
		Возврат;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеСохраненияЗавершениеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	АдресФайла  = "";
	ВыбИмяФайла = "";
	ОО = Новый ОписаниеОповещения("ДобавитьДоверенностьЗавершение", ЭтотОбъект);
	
	Попытка
		НачатьПомещениеФайла(ОО, АдресФайла, ВыбИмяФайла, Истина, УникальныйИдентификатор);
	Исключение
		ШаблонСообщения = НСтр("ru = 'При загрузке файла возникла ошибка.
									 |%1';
									 |en = 'При загрузке файла возникла ошибка.
									 |%1'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонСообщения, КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		ПоказатьПредупреждение(, ТекстСообщения, , НСтр("ru = 'Ошибка';
														|en = 'Ошибка'"));
	КонецПопытки;
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьДоверенностьЗавершение(Результат, АдресФайла, ВыбИмяФайла, Парам) Экспорт
	Если НЕ Результат Тогда
		Возврат;
	КонецЕсли;
	
	ИмяФайлаНРЕГ = НРег(ВыбИмяФайла);
	Если Не СтрЗаканчиваетсяНа(ИмяФайлаНРЕГ, ".jpeg")
		И Не СтрЗаканчиваетсяНа(ИмяФайлаНРЕГ, ".jpg")
		И Не СтрЗаканчиваетсяНа(ИмяФайлаНРЕГ, ".jpe")
		И Не СтрЗаканчиваетсяНа(ИмяФайлаНРЕГ, ".png")
		И Не СтрЗаканчиваетсяНа(ИмяФайлаНРЕГ, ".pdf")
		И Не СтрЗаканчиваетсяНа(ИмяФайлаНРЕГ, ".tif")
		И Не СтрЗаканчиваетсяНа(ИмяФайлаНРЕГ, ".tiff")
		И Не СтрЗаканчиваетсяНа(ИмяФайлаНРЕГ, ".doc")
		И Не СтрЗаканчиваетсяНа(ИмяФайлаНРЕГ, ".docx")
		И Не СтрЗаканчиваетсяНа(ИмяФайлаНРЕГ, ".docm")
		И Не СтрЗаканчиваетсяНа(ИмяФайлаНРЕГ, ".xls")
		И Не СтрЗаканчиваетсяНа(ИмяФайлаНРЕГ, ".xlsx")
		И Не СтрЗаканчиваетсяНа(ИмяФайлаНРЕГ, ".xlsm")
		И Не СтрЗаканчиваетсяНа(ИмяФайлаНРЕГ, ".7z")
		И Не СтрЗаканчиваетсяНа(ИмяФайлаНРЕГ, ".zip")
		И Не СтрЗаканчиваетсяНа(ИмяФайлаНРЕГ, ".rar") Тогда 
		
		ОбщегоНазначенияКлиент.СообщитьПользователю("Файл должен иметь одно из расширений: .jpeg, .jpg, .jpe, .png, .pdf, "
			+ ".tif, .tiff, .doc, .docx, .docm, .xls, .xlsx, .xlsm, .7z, .zip, .rar");
		Возврат;
	КонецЕсли;
	
	ОО = Новый ОписаниеОповещения("ПодписатьГОСТ2012Завершение", ЭтотОбъект, Новый Структура("СтрокаПоиска, АдресФайла", ВыбИмяФайла, АдресФайла));
	МестоХраненияКлюча = КриптографияЭДКОКлиентСервер.КонтекстМоделиХраненияКлюча(СтруктураРеквизитовФормы.Организация);
	КриптографияЭДКОКлиент.ХешироватьДанные(ОО, МестоХраненияКлюча, АдресФайла,, Истина,, "GOST R 34.10-2012-256", Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПодписатьГОСТ2012Завершение(Результат, ДополнительныеПараметры) Экспорт
	Если ТипЗнч(Результат) = Тип("Структура") и Результат.Свойство("Хеш") Тогда 
		ХешДоверенности = НРег(Результат.Хеш);
	Иначе
		ХешДоверенности = "";
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ХешДоверенности) Тогда 
		Возврат;
	КонецЕсли;
	
	Каталог = "";
	СтрокаПоиска = ДополнительныеПараметры.СтрокаПоиска;
	АдресФайла = ДополнительныеПараметры.АдресФайла;

	РазделительПути = ПолучитьРазделительПути();
	Пока СтрДлина(СтрокаПоиска) > 0 Цикл
		Если Прав(СтрокаПоиска, 1) = РазделительПути Тогда
			Каталог = Лев(СтрокаПоиска, СтрДлина(СтрокаПоиска));
			Прервать;
		Иначе
			СтрокаПоиска = Лев(СтрокаПоиска, СтрДлина(СтрокаПоиска) - 1);
		КонецЕсли;
	КонецЦикла;
	
	Попытка
		ЗаполнитьСтруктуруДанныхФайла(АдресФайла, ДополнительныеПараметры.СтрокаПоиска, Каталог);
	Исключение
		ШаблонСообщения = НСтр("ru = 'При загрузке файла возникла ошибка.
									 |%1';
									 |en = 'При загрузке файла возникла ошибка.
									 |%1'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонСообщения, КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		ПоказатьПредупреждение(, ТекстСообщения, , НСтр("ru = 'Ошибка';
														|en = 'Ошибка'"));
		Возврат;
	КонецПопытки;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтруктуруДанныхФайла(АдресФайла, ПолноеИмяФайла, Каталог) Экспорт 
	МодульРаботаСФайлами = ОбщегоНазначения.ОбщийМодуль("РаботаСФайлами");
	ИмяФайла = СтрЗаменить(ПолноеИмяФайла, Каталог, "");
	ИмяБезРасширения = Лев(ИмяФайла, СтрНайти(ИмяФайла, ".", НаправлениеПоиска.СКонца) - 1);
	ПараметрыФайла = Новый Структура;
	ПараметрыФайла.Вставить("ВладелецФайлов", СтруктураРеквизитовФормы.мСохраненныйДок);
	ПараметрыФайла.Вставить("Автор", Неопределено);
	ПараметрыФайла.Вставить("ИмяБезРасширения", ИмяБезРасширения);
	ПараметрыФайла.Вставить("РасширениеБезТочки", СтрЗаменить(ИмяФайла, ИмяБезРасширения + ".", ""));
	ПараметрыФайла.Вставить("ВремяИзменения", Неопределено);
	ПараметрыФайла.Вставить("ВремяИзмененияУниверсальное", Неопределено);
	ПараметрыФайла.Вставить("Служебный", Истина);
	Доверенность = МодульРаботаСФайлами.ДобавитьФайл(ПараметрыФайла, АдресФайла, , "Файл создан автоматически из формы уведомления, редактирование запрещено.");
	ИмяФайлаДоверенности = ИмяФайла;
	ПолеТабличногоДокументаФормаОтчета.Области["attorney_file_attachment_file"].Значение = ИмяФайла;
	ПоказателиОтчета = Новый Структура;
	ЗаполнитьПоказателиОтчетаНаСервере(ПоказателиОтчета);
	Сохранить(,,,,ПоказателиОтчета);
	ОтображениеГиперссылокДоверенности(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуКонтактнойИнформацииЗавершение(Результат, Параметры) Экспорт
	
	РегламентированнаяОтчетностьКлиент.ОбновитьАдресВТабличномДокументеСтатистическойОтчетности(Результат,
		Параметры.Область, СтруктураРеквизитовФормы);
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеТабличногоДокументаФормаОтчетаПриИзмененииСодержимогоОбласти(Элемент, Область)
	
	// ПроцессыОбработкиДокументов
	//
	Если Элементы.ПолеТабличногоДокументаФормаОтчета.Защита Тогда
		
		Возврат;
		
	КонецЕсли;
	//
	// ПроцессыОбработкиДокументов
	
	Если Область.Имя = "ДатаСоставленияОтчета" Тогда 
		ДатаПодписи = Область.Значение;
		ЗаполнитьСведенияОбОрганизацииНаКлиенте(Истина);
		Модифицированность = Истина;
		Возврат;
	ИначеЕсли Область.Имя = "НомерКорректировки" Тогда 
		Если Область.Значение = 0 Тогда 
			СтруктураРеквизитовФормы.ВидДокумента = 0;
			НомерКорректировки = 0;
		Иначе 
			СтруктураРеквизитовФормы.ВидДокумента = 1;
			НомерКорректировки = Область.Значение;
		КонецЕсли;
		Модифицированность = Истина;
		Возврат;
	ИначеЕсли Область.Имя = "КодОНВ"
		Или Область.Имя = "КодОКТМООНВ"
		Или Область.Имя = "КодОКВЭД2ОНВ" Тогда 
		
		ПроставитьКодыВЯчейки(Область, ПолеТабличногоДокументаФормаОтчета);
		ПолеТабличногоДокументаФормаОтчета.Области["onvos_code"].Значение = ПолеТабличногоДокументаФормаОтчета.Области["КодОНВ"].Значение;
		ПолеТабличногоДокументаФормаОтчета.Области["onvos_oktmo"].Значение = ПолеТабличногоДокументаФормаОтчета.Области["КодОКТМООНВ"].Значение;
		ПолеТабличногоДокументаФормаОтчета.Области["onvos_okved"].Значение = ПолеТабличногоДокументаФормаОтчета.Области["КодОКВЭД2ОНВ"].Значение;
		Модифицированность = Истина;
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура("Область, Элемент, Форма", Область, Элемент, ЭтотОбъект);
	РегламентированнаяОтчетностьКлиент.ПредставлениеНуля(ДополнительныеПараметры);
	
	РасчетНаКлиенте("", "");
	
	Если СтруктураРеквизитовФормы.мАвтоВыборКодов Тогда
		ИмяОбласти = Область.Имя;
		Если СтрНайти(ИмяОбласти, "П000004000103_") = 1 Тогда
			ПолеТабличногоДокументаФормаОтчета.Области["П000004000193_" + Сред(ИмяОбласти, 15)].Значение = 0;
		КонецЕсли;
	КонецЕсли;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеТабличногоДокументаФормаОтчетаПриИзмененииСодержимогоОбластиЗавершение(СписокЭлемент, ДополнительныеПараметры) Экспорт
	
	Область = ДополнительныеПараметры.Область;
	
	Если СписокЭлемент <> Неопределено  Тогда
		
		Область.Формат = РегламентированнаяОтчетностьКлиентСервер.ЗаменитьПредставлениеНуляВСтрокеФормата(
			Область.Формат, СписокЭлемент.Значение);
		
	КонецЕсли;
	
	РасчетНаКлиенте("", "");
	
	Если СтруктураРеквизитовФормы.мАвтоВыборКодов Тогда
		ИмяОбласти = Область.Имя;
		Если СтрНайти(ИмяОбласти, "П000004000103_") = 1 Тогда
			ПолеТабличногоДокументаФормаОтчета.Области["П000004000193_" + Сред(ИмяОбласти, 15)].Значение = 0;
		КонецЕсли;
	КонецЕсли;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура РасчетНаКлиенте(ИмяСтраницыПанели = "", ОбластьИмя = "") Экспорт

	Расчет(ЭтотОбъект, ИмяСтраницыПанели, ОбластьИмя);

КонецПроцедуры

&НаСервере
Процедура РасчетНаСервере(ИмяСтраницыПанели = "", ОбластьИмя = "") Экспорт

	Расчет(ЭтотОбъект, ИмяСтраницыПанели, ОбластьИмя);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура Расчет(Форма, ИмяТекущейСтраницыПанели = "", ОбластьИмя = "")
	
	Если (Форма.СтруктураРеквизитовФормы.ФлажокОтклАвтоРасчет = Истина) Тогда
		Возврат;
	КонецЕсли;
	
	Области = Форма.ПолеТабличногоДокументаФормаОтчета.Области;
	
	Для Гр = 3 По 8 Цикл 
		Области["П00010030" + Гр].Значение = Области["П00010040" + Гр].Значение
												+ Области["П00010050" + Гр].Значение
												+ Области["П00010060" + Гр].Значение
												+ Области["П00010070" + Гр].Значение
												+ Области["П00010080" + Гр].Значение
												+ Области["П00010090" + Гр].Значение;
		Области["П00010010" + Гр].Значение = Области["П00010020" + Гр].Значение
												+ Области["П00010030" + Гр].Значение;
	КонецЦикла;
	
	Для Стр = 1 По 9 Цикл
		Области["П000100"+Стр+"08"].Значение = Области["П000100"+Стр+"03"].Значение
														+ Области["П000100"+Стр+"05"].Значение
														- Области["П000100"+Стр+"06"].Значение;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	СтруктураРеквизитовФормы.мВПрограммеИзмененаОрганизация = Истина;
	ЗаполнитьСведенияОбОрганизацииНаКлиенте();
	СтруктураРеквизитовФормы.мВПрограммеИзмененаОрганизация = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуНастроек(Команда)
	
	ОткрытьФормуВыбораСтраниц("ВыбратьДляНастройки");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораСтраниц(ВариантВыбора)
	
	мПараметры = Новый Структура;
	
	мПараметры.Вставить("ПроверкаСоотношений", Ложь);
	мПараметры.Вставить("Автосохранение", Ложь);
	мПараметры.Вставить("СчетчикСтраниц", Ложь);
	мПараметры.Вставить("ПараметрыОтображенияСумм", Ложь);
	мПараметры.Вставить("ИмеетсяРеквизитДеревоВыбранныхСтраниц", Ложь);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ФлажокОтклАвтоРасчет", СтруктураРеквизитовФормы.ФлажокОтклАвтоРасчет);
	ПараметрыФормы.Вставить("мАвтоВыборКодов", СтруктураРеквизитовФормы.мАвтоВыборКодов);
	ПараметрыФормы.Вставить("мПараметры", мПараметры);
	
	ФормаНастройкиОтчета = РегламентированнаяОтчетностьКлиент.ПолучитьОбщуюФормуПоИмени("НастройкиОтчета",
		ПараметрыФормы, ЭтотОбъект);
	ФормаНастройкиОтчета.Элементы.Страницы.ПодчиненныеЭлементы.СвойстваРазделов.Видимость = Ложь;
	
	ЗначениеАвторасчета = СтруктураРеквизитовФормы.ФлажокОтклАвтоРасчет;
	
	ДополнительныеПараметры = Новый Структура("ЗначениеАвторасчета", ЗначениеАвторасчета);
	ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытьФормуВыбораСтраницЗавершение", ЭтотОбъект,
		ДополнительныеПараметры);
	
	ФормаНастройкиОтчета.ОписаниеОповещенияОЗакрытии = ОписаниеОповещения;
	ФормаНастройкиОтчета.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ФормаНастройкиОтчета.Открыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораСтраницЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	ЗначениеАвторасчета = ДополнительныеПараметры.ЗначениеАвторасчета;

	Модифицированность = Истина;
	
	Если ЗначениеАвторасчета <> СтруктураРеквизитовФормы.ФлажокОтклАвтоРасчет Тогда
		
		ИзменитьТаблицуВариантовЗаполненияНаСервере();
		
		Расчет(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОтчет(Команда)
	
	ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Внимание! Будут очищены все показатели отчета.%1Продолжить операцию?';
			|en = 'Внимание! Будут очищены все показатели отчета.%1Продолжить операцию?'"), Символы.ПС);
	ОписаниеОповещения = Новый ОписаниеОповещения("ОчиститьОтчетЗавершение", ЭтотОбъект);
	
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОтчетЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Нет Тогда       
		Возврат;      
	Иначе       
		Очистить();       
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Очистить() Экспорт
	
	ОчиститьТабличноеПолеНаСервере();
	ПронумероватьСтрокиДляПечати();
	
	Модифицированность = Истина;

КонецПроцедуры

&НаСервере
Процедура ОчиститьТабличноеПолеНаСервере()
	
	Перем СписокПоказателей;

	ВыбТабПоле = ПолеТабличногоДокументаФормаОтчета;
	
	СписокПоказателей = Новый СписокЗначений;
	СписокПоказателей.Добавить(ВыбТабПоле.Области["ПериодОтчета"].Значение,				"ПериодОтчета");
	СписокПоказателей.Добавить(ВыбТабПоле.Области["ДатаСоставленияОтчета"].Значение,	"ДатаСоставленияОтчета");
	СписокПоказателей.Добавить(ВыбТабПоле.Области["ОргНазв"].Значение,					"ОргНазв");
	СписокПоказателей.Добавить(ВыбТабПоле.Области["ОргАдрес"].Значение,					"ОргАдрес");
	СписокПоказателей.Добавить(ВыбТабПоле.Области["ОргКодОКПО"].Значение,				"ОргКодОКПО");
	СписокПоказателей.Добавить(ВыбТабПоле.Области["ОргКодНазв"].Значение,				"ОргКодНазв");
	СписокПоказателей.Добавить(ВыбТабПоле.Области["ОргКодНазв1"].Значение,				"ОргКодНазв1");
	СписокПоказателей.Добавить(ВыбТабПоле.Области["ОргКодЗнач"].Значение,				"ОргКодЗнач");
	СписокПоказателей.Добавить(ВыбТабПоле.Области["ОргКодЗнач1"].Значение,              "ОргКодЗнач1");
	СписокПоказателей.Добавить(ВыбТабПоле.Области["ОргДолжностьИсп"].Значение,          "ОргДолжностьИсп");
	СписокПоказателей.Добавить(ВыбТабПоле.Области["ОргТелефонИсп"].Значение,            "ОргТелефонИсп");
	СписокПоказателей.Добавить(ВыбТабПоле.Области["ОргИсполнитель"].Значение,           "ОргИсполнитель");
	СписокПоказателей.Добавить(ВыбТабПоле.Области["НомерКорректировки"].Значение,       "НомерКорректировки");
	СписокПоказателей.Добавить(ВыбТабПоле.Области["ОргАдресЭлектроннойПочты"].Значение, "ОргАдресЭлектроннойПочты");
	СписокПоказателей.Добавить(ВыбТабПоле.Области["КодОНВ"].Значение,					"КодОНВ");
	СписокПоказателей.Добавить(ВыбТабПоле.Области["КодОКТМООНВ"].Значение,              "КодОКТМООНВ");
	СписокПоказателей.Добавить(ВыбТабПоле.Области["КодОКВЭД2ОНВ"].Значение,             "КодОКВЭД2ОНВ");
	СписокПоказателей.Добавить(ВыбТабПоле.Области["ОргПоле1"].Значение,                 "ОргПоле1");
	СписокПоказателей.Добавить(ВыбТабПоле.Области["ОргПоле2"].Значение,                 "ОргПоле2");
	СписокПоказателей.Добавить(ВыбТабПоле.Области["ОргПолеЗнач1"].Значение,             "ОргПолеЗнач1");
	СписокПоказателей.Добавить(ВыбТабПоле.Области["ОргПолеЗнач2"].Значение,             "ОргПолеЗнач2");

	ВыбТабПоле.Очистить();
	ВыбТабПоле.Вывести(ОбъектОтчета(ИмяФормы).ПолучитьМакет("ФормаОтчета2018Кв1_ФормаОтчета"));
	
	ПолеТабличногоДокументаФормаОтчета = ВыбТабПоле;
	
	РегламентированнаяОтчетность.НачальныеОперацииСМногострочнымиБлоками(ЭтотОбъект);
	
	Для каждого ЭлементСписка Из СписокПоказателей Цикл
		ВыбТабПоле.Области[ЭлементСписка.Представление].Значение = ЭлементСписка.Значение;
	КонецЦикла;
	
	ИзменитьТаблицуВариантовЗаполненияНаСервере();
	ПроставитьКодыВЯчейки(ПолеТабличногоДокументаФормаОтчета.Области["КодОНВ"], ПолеТабличногоДокументаФормаОтчета);
	ПроставитьКодыВЯчейки(ПолеТабличногоДокументаФормаОтчета.Области["КодОКТМООНВ"], ПолеТабличногоДокументаФормаОтчета);
	ПроставитьКодыВЯчейки(ПолеТабличногоДокументаФормаОтчета.Области["КодОКВЭД2ОНВ"], ПолеТабличногоДокументаФормаОтчета);
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура АктивизироватьЯчейку(Ячейка) Экспорт
	
	ИмяПоляТабличногоДокумента = "ПолеТабличногоДокументаФормаОтчета";
	
	НайденнаяОбласть = ЭтотОбъект[ИмяПоляТабличногоДокумента].Области.Найти(Ячейка.ИмяЯчейки);
	
	Если НЕ НайденнаяОбласть = Неопределено Тогда
		
		Элементы[ИмяПоляТабличногоДокумента].ТекущаяОбласть = НайденнаяОбласть;
		
	ИначеЕсли Ячейка.СтрокаПП <> Неопределено И ЗначениеЗаполнено(Ячейка.СтрокаПП) Тогда
		
		НайденнаяОбласть = ЭтотОбъект[ИмяПоляТабличногоДокумента].Области.Найти(Ячейка.ИмяЯчейки + Ячейка.СтрокаПП);
		
		Если НЕ НайденнаяОбласть = Неопределено Тогда
			
			Элементы[ИмяПоляТабличногоДокумента].ТекущаяОбласть = НайденнаяОбласть;
			
		Иначе
			
			НайденнаяОбласть = ЭтотОбъект[ИмяПоляТабличногоДокумента].Области.Найти(Ячейка.ИмяЯчейки + "_" + Ячейка.СтрокаПП);
			
			Если НЕ НайденнаяОбласть = Неопределено Тогда
				
				Элементы[ИмяПоляТабличногоДокумента].ТекущаяОбласть = НайденнаяОбласть;
				
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		НайденнаяОбласть = ЭтотОбъект[ИмяПоляТабличногоДокумента].Области.Найти(Ячейка.ИмяЯчейки + "1");
		
		Если НЕ НайденнаяОбласть = Неопределено Тогда
			
			Элементы[ИмяПоляТабличногоДокумента].ТекущаяОбласть = НайденнаяОбласть;
			
		Иначе
			
			НайденнаяОбласть = ЭтотОбъект[ИмяПоляТабличногоДокумента].Области.Найти(Ячейка.ИмяЯчейки + "_1");
			
			Если НЕ НайденнаяОбласть = Неопределено Тогда
				
				Элементы[ИмяПоляТабличногоДокумента].ТекущаяОбласть = НайденнаяОбласть;
				
			Иначе
				
				Элементы[ИмяПоляТабличногоДокумента].ТекущаяОбласть =
					ЭтотОбъект[ИмяПоляТабличногоДокумента].Область(Ячейка.ИмяЯчейки);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьБланк(Команда)
	
	Отказ = Ложь;
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("Команда", Команда);
	ОписаниеОповещения = Новый ОписаниеОповещения("ПоказатьБланкЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередПечатьюРегламентированногоОтчета(
		ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьБланкЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	Команда = ДополнительныеПараметры.Команда;
		
	Если Команда <> Неопределено Тогда
		Печать(Команда.Имя);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Печать(ВидПечати, НеИзФормыОтчета = Ложь) Экспорт
	
	Состояние(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '%1. Формируется печатная форма...';
			|en = '%1. Формируется печатная форма...'"), Заголовок), , , БиблиотекаКартинок.Печать);
	
	Если НЕ ПечатьНаСервере(ВидПечати) Тогда
		Возврат;
	КонецЕсли;
	
	РегламентированнаяОтчетностьКлиент.ОткрытьФормуПредварительногоПросмотра(
		ЭтотОбъект, ВидПечати, Неопределено, СтруктураРеквизитовФормы.СписокПечатаемыхЛистов);
	
КонецПроцедуры

&НаСервере
Функция ПечатьНаСервере(ВидПечати)
	Если НЕ РегламентированнаяОтчетность.ПринтерДоступен() Тогда
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Перед формированием печатных форм необходимо определить в системе принтер и задать его в качестве используемого по умолчанию!';
				|en = 'Перед формированием печатных форм необходимо определить в системе принтер и задать его в качестве используемого по умолчанию!'"));
		Возврат Ложь;
	КонецЕсли;
	
	ТабличныйДокумент = ЭтотОбъект["ПолеТабличногоДокументаФормаОтчета"];
	Области = ПолеТабличногоДокументаФормаОтчета.Области;
	
	СтруктураРеквизитовФормы.мПечатныеФормы.Очистить();
	
	ОбщиеРеквизитыРазделов = Новый Структура;
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("НаименованиеЛиста",  "Форма 2-ТП (воздух)");
	СтруктураПараметров.Вставить("ОриентацияЛиста",    "Ландшафт");
	СтруктураПараметров.Вставить("ИмяСтраницы",        "2ТПВоздух");
	Структурапараметров.Вставить("ИмяОбластиТаблПоля", "ПолеТабличногоДокументаФормаОтчета");
	СтруктураПараметров.Вставить("ВидПечати",          ВидПечати);
	
	ПечатнаяФорма = РегламентированнаяОтчетность.НоваяСтраница();
	ПечатнаяФорма.Вывести(ТабличныйДокумент.ПолучитьОбласть("ОсновнаяЧасть1а"));
	ПечатнаяФорма.Вывести(ТабличныйДокумент.ПолучитьОбласть("ОсновнаяЧасть1б"));
	СтруктураРеквизитовФормы.мПечатныеФормы.Добавить(ПечатнаяФорма, "Форма 2-ТП (воздух)");
	
	ПечатнаяФорма = РегламентированнаяОтчетность.НоваяСтраница();
	ПечатнаяФорма.Вывести(ТабличныйДокумент.ПолучитьОбласть("ОсновнаяЧасть2"));
	СтруктураРеквизитовФормы.мПечатныеФормы.Добавить(ПечатнаяФорма, "Форма 2-ТП (воздух)");
	
	ПечатнаяФорма = РегламентированнаяОтчетность.НоваяСтраница();
	ПечатнаяФорма.Вывести(ТабличныйДокумент.ПолучитьОбласть("ОсновнаяЧасть3Заголовок"));
	ПечатнаяФорма.Вывести(ТабличныйДокумент.ПолучитьОбласть("ОсновнаяЧасть3Шапка"));
	
	ВерхМногострочнойЧасти = Области["ОсновнаяЧасть3Шапка"].Низ + 1;
	НизМногострочнойЧасти = Области["ДобавлениеСтроки1"].Верх - 1;
	ТекущийВерх = ВерхМногострочнойЧасти;
	Пока ТекущийВерх <= НизМногострочнойЧасти Цикл
		ТекущаяОбластьМногострочнойЧасти = ПолеТабличногоДокументаФормаОтчета.ПолучитьОбласть(ТекущийВерх,,ТекущийВерх,);
		МассивПроверкиВывода = Новый Массив;
		МассивПроверкиВывода.Добавить(ТекущаяОбластьМногострочнойЧасти);
		МассивПроверкиВывода.Добавить(ТабличныйДокумент.ПолучитьОбласть("ОсновнаяЧасть3Подвал"));
		
		Если ПечатнаяФорма.ПроверитьВывод(МассивПроверкиВывода) Тогда
			ПечатнаяФорма.Вывести(ТекущаяОбластьМногострочнойЧасти);
		Иначе
			СтруктураРеквизитовФормы.мПечатныеФормы.Добавить(ПечатнаяФорма, "Форма 2-ТП (воздух)");
			ПечатнаяФорма = РегламентированнаяОтчетность.НоваяСтраница();
			ПечатнаяФорма.Вывести(ТабличныйДокумент.ПолучитьОбласть("ОсновнаяЧасть3Шапка"));
			ПечатнаяФорма.Вывести(ТекущаяОбластьМногострочнойЧасти);
		КонецЕсли;
		
		ТекущийВерх = ТекущийВерх + 1;
	КонецЦикла;
	ПечатнаяФорма.Вывести(ТабличныйДокумент.ПолучитьОбласть("ОсновнаяЧасть3Подвал"));
	СтруктураРеквизитовФормы.мПечатныеФормы.Добавить(ПечатнаяФорма, "Форма 2-ТП (воздух)");
	
	ПечатнаяФорма = РегламентированнаяОтчетность.НоваяСтраница();
	ПечатнаяФорма.Вывести(ТабличныйДокумент.ПолучитьОбласть("ОсновнаяЧасть4Заголовок"));
	ПечатнаяФорма.Вывести(ТабличныйДокумент.ПолучитьОбласть("ОсновнаяЧасть4Шапка"));
	ВерхМногострочнойЧасти = Области["ОсновнаяЧасть4Шапка"].Низ + 1;
	НизМногострочнойЧасти = Области["ДобавлениеСтроки2"].Верх - 1;
	ТекущийВерх = ВерхМногострочнойЧасти;
	Пока ТекущийВерх <= НизМногострочнойЧасти Цикл
		ТекущаяОбластьМногострочнойЧасти = ПолеТабличногоДокументаФормаОтчета.ПолучитьОбласть(ТекущийВерх,,ТекущийВерх,);
		МассивПроверкиВывода = Новый Массив;
		МассивПроверкиВывода.Добавить(ТекущаяОбластьМногострочнойЧасти);
		МассивПроверкиВывода.Добавить(ТабличныйДокумент.ПолучитьОбласть("ОсновнаяЧасть4Подвал"));
		
		Если ПечатнаяФорма.ПроверитьВывод(МассивПроверкиВывода) Тогда
			ПечатнаяФорма.Вывести(ТекущаяОбластьМногострочнойЧасти);
		Иначе
			СтруктураРеквизитовФормы.мПечатныеФормы.Добавить(ПечатнаяФорма, "Форма 2-ТП (воздух)");
			ПечатнаяФорма = РегламентированнаяОтчетность.НоваяСтраница();
			ПечатнаяФорма.Вывести(ТабличныйДокумент.ПолучитьОбласть("ОсновнаяЧасть4Шапка"));
			ПечатнаяФорма.Вывести(ТекущаяОбластьМногострочнойЧасти);
		КонецЕсли;
		
		ТекущийВерх = ТекущийВерх + 1;
	КонецЦикла;
	ПечатнаяФорма.Вывести(ТабличныйДокумент.ПолучитьОбласть("ОсновнаяЧасть4Подвал"));
	СтруктураРеквизитовФормы.мПечатныеФормы.Добавить(ПечатнаяФорма, "Форма 2-ТП (воздух)");
	
	ПечатнаяФорма = РегламентированнаяОтчетность.НоваяСтраница();
	ПечатнаяФорма.Вывести(ТабличныйДокумент.ПолучитьОбласть("ОсновнаяЧасть5"));
	СтруктураРеквизитовФормы.мПечатныеФормы.Добавить(ПечатнаяФорма, "Форма 2-ТП (воздух)");
	
	РегламентированнаяОтчетность.УдалитьНомерКорректировки(СтруктураРеквизитовФормы.мПечатныеФормы[0].Значение);
	Инд = 1;
	Пока Инд < СтруктураРеквизитовФормы.мПечатныеФормы.Количество() Цикл 
		Лист = СтруктураРеквизитовФормы.мПечатныеФормы[Инд].Значение;
		Инд = Инд + 1;
		Для Каждого Область Из Лист.Области Цикл 
			Если СтрНайти(Область.Имя, "УдалитьСтрокуП0") = 1 Тогда 
				Область.Текст = "";
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	РегламентированнаяОтчетностьКлиентСервер.ПроставлениеНомеровЛистов(
		ЭтотОбъект, , СтруктураРеквизитовФормы.СписокПечатаемыхЛистов);
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура СформироватьСпискиВыбора()
	
	МакетСоставаПоказателей = ОбъектОтчета(ИмяФормы).ПолучитьМакет("Списки2018Кв1");
	
	Массив = Новый Массив;
	Массив.Добавить(Тип("Строка"));
	Строка16  = Новый ОписаниеТипов(Массив, , Новый КвалификаторыСтроки(16,  ДопустимаяДлина.Переменная));
	Строка512 = Новый ОписаниеТипов(Массив, , Новый КвалификаторыСтроки(512, ДопустимаяДлина.Переменная));
	
	КоллекцияСписковВыбора = Новый Соответствие;
	Для Каждого Область Из МакетСоставаПоказателей.Области Цикл
		Если Область.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Строки Тогда
			ВерхОбласти = Область.Верх;
			НизОбласти = Область.Низ;
			ТаблСписка = Новый ТаблицаЗначений;
			ТаблСписка.Колонки.Добавить("Код",     Строка16   ,,
				МакетСоставаПоказателей.Область(ВерхОбласти, 1, ВерхОбласти, 1).ШиринаКолонки);
			ТаблСписка.Колонки.Добавить("Название",Строка512  ,,
				МакетСоставаПоказателей.Область(ВерхОбласти, 2, ВерхОбласти, 2).ШиринаКолонки);
			ТаблСписка.Колонки.Добавить("Идентификатор",Строка16  ,,
				МакетСоставаПоказателей.Область(ВерхОбласти, 2, ВерхОбласти, 2).ШиринаКолонки);
			Для НомСтр = ВерхОбласти По НизОбласти Цикл
				КодПоказателя = СокрП(МакетСоставаПоказателей.Область(НомСтр, 1).Текст);
				Если КодПоказателя <> "###" Тогда
					НовСтрока = ТаблСписка.Добавить();
					НовСтрока.Код = КодПоказателя;
					НовСтрока.Название = СокрП(МакетСоставаПоказателей.Область(НомСтр, 2).Текст);
					НовСтрока.Идентификатор = СокрП(МакетСоставаПоказателей.Область(НомСтр, 3).Текст);
				КонецЕсли;
			КонецЦикла;
			КоллекцияСписковВыбора.Вставить(Область.Имя, ТаблСписка);
		КонецЕсли;
	КонецЦикла;
	
	СвойстваПоказателей.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("S_ZAGRV", КоллекцияСписковВыбора["S_ZAGRV"]);
	Запрос.УстановитьПараметр("PRIL_ZAGRV_1", КоллекцияСписковВыбора["PRIL_ZAGRV_1"]);
	Запрос.УстановитьПараметр("S_MERPOYMV", КоллекцияСписковВыбора["S_MERPOYMV"]);
	Запрос.УстановитьПараметр("S_OZENVIPMER", КоллекцияСписковВыбора["S_OZENVIPMER"]);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	S_ZAGRV.Код КАК Код,
	|	S_ZAGRV.Название КАК Название,
	|	S_ZAGRV.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_S_ZAGRV
	|ИЗ
	|	&S_ZAGRV КАК S_ZAGRV
	|ИНДЕКСИРОВАТЬ ПО
	|	S_ZAGRV.Код
	|;
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	PRIL_ZAGRV_1.Код КАК Код
	|ПОМЕСТИТЬ ВТ_PRIL_ZAGRV_1
	|ИЗ &PRIL_ZAGRV_1 КАК PRIL_ZAGRV_1
	|ИНДЕКСИРОВАТЬ ПО
	|	PRIL_ZAGRV_1.Код
	|;
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	S_MERPOYMV.Код КАК Код,
	|	S_MERPOYMV.Название КАК Название
	|ПОМЕСТИТЬ ВТ_S_MERPOYMV
	|ИЗ
	|	&S_MERPOYMV КАК S_MERPOYMV
	|;
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	S_OZENVIPMER.Код КАК Код,
	|	S_OZENVIPMER.Название КАК Название
	|ПОМЕСТИТЬ ВТ_S_OZENVIPMER
	|ИЗ
	|	&S_OZENVIPMER КАК S_OZENVIPMER
	|;
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	S_ZAGRV.Код КАК Код,
	|	S_ZAGRV.Название КАК Название,
	|	S_ZAGRV.Идентификатор КАК Идентификатор
	|ИЗ
	|	ВТ_S_ZAGRV КАК S_ZAGRV
	|ГДЕ
	|	S_ZAGRV.Код В (ВЫБРАТЬ PRIL_ZAGRV_1.Код ИЗ ВТ_PRIL_ZAGRV_1 КАК PRIL_ZAGRV_1)
	|;
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	S_MERPOYMV.Код КАК Код,
	|	S_MERPOYMV.Название КАК Название
	|ИЗ
	|	ВТ_S_MERPOYMV КАК S_MERPOYMV
	|;
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	S_OZENVIPMER.Код КАК Код,
	|	S_OZENVIPMER.Название КАК Название
	|ИЗ
	|	ВТ_S_OZENVIPMER КАК S_OZENVIPMER";
	
	Результат = Запрос.ВыполнитьПакет();
	ЗагрязняющиеВещества = Результат[4].Выгрузить();
	ТипМероприятия = Результат[5].Выгрузить();
	ПризнакМероприятия = Результат[6].Выгрузить();
	
	НоваяСтрокаТЗВыбора = ЗагрязняющиеВещества.Вставить(0);
	НоваяСтрокаТЗВыбора.Код = "";
	НоваяСтрокаТЗВыбора.Название = "Пустое значение";
	НоваяСтрокаТЗВыбора = ТипМероприятия.Вставить(0);
	НоваяСтрокаТЗВыбора.Код = "";
	НоваяСтрокаТЗВыбора.Название = "Пустое значение";
	НоваяСтрокаТЗВыбора = ПризнакМероприятия.Вставить(0);
	НоваяСтрокаТЗВыбора.Код = "";
	НоваяСтрокаТЗВыбора.Название = "Пустое значение";
	
	РегламентированнаяОтчетность.ДобавитьСтрокуОписанияВвода(СвойстваПоказателей, "ЗагрязняющиеВещества", 512, ,
		"Выбор кода загрязняющего вещества", ЗагрязняющиеВещества);
	РегламентированнаяОтчетность.ДобавитьСтрокуОписанияВвода(СвойстваПоказателей, "ТипМероприятия", 512, ,
		"Выбор типа мероприятия", ТипМероприятия);
	РегламентированнаяОтчетность.ДобавитьСтрокуОписанияВвода(СвойстваПоказателей, "ПризнакМероприятия", 512, ,
		"Выбор признака", ПризнакМероприятия);
	РегламентированнаяОтчетность.ДобавитьСтрокуОписанияВвода(СвойстваПоказателей, "LANDS_DISTURB_TYPE", 512, ,
		"Причина нарушения земель", КоллекцияСписковВыбора["LANDS_DISTURB_TYPE"]);
	РегламентированнаяОтчетность.ДобавитьСтрокуОписанияВвода(СвойстваПоказателей, "ORG_SCALE", 512, ,
		"Размер предприятия", КоллекцияСписковВыбора["ORG_SCALE"]);
	РегламентированнаяОтчетность.ДобавитьСтрокуОписанияВвода(СвойстваПоказателей, "FISH_CAT", 512, ,
		"Категория рыбного значения", КоллекцияСписковВыбора["FISH_CAT"]);
	РегламентированнаяОтчетность.ДобавитьСтрокуОписанияВвода(СвойстваПоказателей, "OBJ_EXPLOITATION", 512, ,
		"Эксплуатация", КоллекцияСписковВыбора["OBJ_EXPLOITATION"]);
	РегламентированнаяОтчетность.ДобавитьСтрокуОписанияВвода(СвойстваПоказателей, "ОрганыРПН", 512, ,
		"Выбор органа РосПриродНадзора", КоллекцияСписковВыбора["ОрганыРПН"]);
	РегламентированнаяОтчетность.ДобавитьСтрокуОписанияВвода(СвойстваПоказателей, "НеРегистрация", 512, ,
		"Причина отсутствия регистрации", КоллекцияСписковВыбора["НеРегистрация"]);
	РегламентированнаяОтчетность.ЗагрузитьОбщиеСправочникиРПН(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ВвестиПоказатель(Элемент, Область, КодПоказателя, Знач ЭтоСумма, Знач ТекстВыбора, ТаблицаВыбора, СтандартнаяОбработка, ОбластьНаименование = "") Экспорт
	
	Если ТаблицаВыбора = Неопределено Или СтруктураРеквизитовФормы.мАвтоВыборКодов Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	Если ТипЗнч(Область.Значение) = Тип("Число") Тогда 
		ИсходноеЗначение = Формат(Область.Значение, "ЧГ=0");
	Иначе
		ИсходноеЗначение = Область.Значение;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Заголовок",          ТекстВыбора);
	ПараметрыФормы.Вставить("ТаблицаЗначений",    ТаблицаВыбора);
	ПараметрыФормы.Вставить("СтруктураДляПоиска", Новый Структура("Код", ИсходноеЗначение));
	ПараметрыФормы.Вставить("ВключитьВидимостьНадписиРосстат", Истина);
	
	ДополнительныеПараметры = Новый Структура("Область, ОбластьНаименование", Область, ОбластьНаименование);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВвестиПоказательЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ОткрытьФорму("Обработка.ОбщиеОбъектыРеглОтчетности.Форма.ФормаВыбораЗначенияИзТаблицы",
		ПараметрыФормы, ЭтотОбъект,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ВвестиПоказательЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Область = ДополнительныеПараметры.Область;
	ОбластьНаименование = ДополнительныеПараметры.ОбластьНаименование;
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Область.Значение) = Тип("Число") Тогда 
		ОТ = Новый ОписаниеТипов("Число");
		Область.Значение = ОТ.ПривестиЗначение(РезультатВыбора["Код"]);
	КонецЕсли;
	
	Если ОбластьНаименование <> Неопределено Тогда
		РезультатВыбораНазвание = РезультатВыбора.Название;
		Если РезультатВыбораНазвание = "Пустое значение" Тогда 
			РезультатВыбораНазвание = "";
		КонецЕсли;
		ПолеТабличногоДокументаФормаОтчета.Области[ОбластьНаименование].Значение = РезультатВыбораНазвание;
	КонецЕсли;
	
	Область.Значение = РезультатВыбора["Код"];
	Если Область.Имя = "onvos_oktmo" Тогда 
		Обл = ПолеТабличногоДокументаФормаОтчета.Области["КодОКТМООНВ"];
		Обл.Значение = РезультатВыбора["Код"];
		ПроставитьКодыВЯчейки(Обл, ПолеТабличногоДокументаФормаОтчета);
	КонецЕсли;
	
	Если СтрНайти(Область.Имя, "П000004000104_") = 1 Тогда
		Если РезультатВыбора["Код"] = "0" Тогда
			Область.Формат = "ЧН=0";
		ИначеЕсли РезультатВыбора["Код"] = "" Тогда 
			Область.Формат = "";
		КонецЕсли;
	КонецЕсли;
	
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СравнитьОтчеты(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("СравнитьОтчетыЗавершение", ЭтотОбъект);
	
	Если Модифицированность Тогда
		СохранитьНаКлиенте(, ОписаниеОповещения);
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайла(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузитьИзФайлаЗавершение", ЭтотОбъект);
	
	Отказ = Ложь;
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередЗагрузкойРегламентированногоОтчета(
		ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура Выгрузить(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузитьЗавершение", ЭтотОбъект);

	Отказ = Ложь;
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередВыгрузкойРегламентированногоОтчета(
		ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура СравнитьОтчетыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	РегламентированнаяОтчетностьСравнениеКлиент.СравнитьОтчеты(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Функция СравнениеОтчетов(ДанныеДляСравнения) Экспорт
	
	Если Модифицированность Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(СтрШаблон(НСтр("ru = 'Отчет ""%1"" не сохранен.';
																	|en = 'Отчет ""%1"" не сохранен.'"), Заголовок));
		Возврат Ложь;
	КонецЕсли;
	
	Возврат СравнениеОтчетовНаСервере(ДанныеДляСравнения);
	
КонецФункции

&НаСервере
Функция СравнениеОтчетовНаСервере(ДанныеДляСравнения)
	
	ПараметрыОтчета = СформироватьСтруктуруПараметров();
	
	ДеревоДляСравнения = РегламентированнаяОтчетность.ПолучитьДеревоВыгрузки(ЭтотОбъект, "СхемаВыгрузкиXML2018Кв1");
	
	Результат = РегламентированнаяОтчетностьСравнение.СравнениеОтчетов(
		ЭтотОбъект, ПараметрыОтчета, ДанныеДляСравнения, ДеревоДляСравнения);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ЗагрузитьИзФайлаЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ЗагрузитьИзФайлаОтчет();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайлаОтчет() Экспорт
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузитьИзФайлаОтчетЗавершение", ЭтотОбъект);
	
	Если Модифицированность Тогда
		СохранитьНаКлиенте(,ОписаниеОповещения);
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайлаОтчетЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	РегламентированнаяОтчетностьЗагрузкаКлиент.ЗагрузитьИзФайлаОтчет(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Функция ЗагрузкаОтчетаИзФайла(ДанныеДляЗагрузки) Экспорт
	
	Если Модифицированность Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(СтрШаблон(НСтр("ru = 'Отчет ""%1"" не сохранен.';
																	|en = 'Отчет ""%1"" не сохранен.'"), Заголовок));
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ ЗагрузкаОтчетаИзФайлаНаСервере(ДанныеДляЗагрузки) Тогда
		Возврат Ложь;
	КонецЕсли;
		
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция ЗагрузкаОтчетаИзФайлаНаСервере(ДанныеДляЗагрузки)
	
	ПараметрыОтчета = СформироватьСтруктуруПараметров();
	
	ДеревоДляЗагрузки = РегламентированнаяОтчетность.ПолучитьДеревоВыгрузки(ЭтотОбъект, "СхемаВыгрузкиXML2018Кв1");
	
	Если НЕ РегламентированнаяОтчетностьЗагрузка.ЗагрузкаОтчетаИзФайла(
		ЭтотОбъект, ПараметрыОтчета, ДанныеДляЗагрузки, ДеревоДляЗагрузки) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ВыгрузитьЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузитьИзФормыПослеСохраненияЗавершение", ЭтотОбъект);
	Если Модифицированность Тогда
		СохранитьНаКлиенте(, ОписаниеОповещения);
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьИзФормыПослеСохраненияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ПараметрыВыгрузки = Неопределено;
	ТекстВыгрузки = Неопределено;
	Если НЕ УниверсальнаяВыгрузкаXMLПроверкаВозможностиВыгрузкиНаСервере(ТекстВыгрузки, ПараметрыВыгрузки) Тогда
		ТекстВопроса = НСтр("ru = 'В ходе выгрузки были найдены ошибки.';
							|en = 'В ходе выгрузки были найдены ошибки.'");
		КнопкиВопроса = Новый СписокЗначений;
		КнопкиВопроса.Добавить(КодВозвратаДиалога.Да, "Продолжить выгрузку");
		КнопкиВопроса.Добавить(КодВозвратаДиалога.Нет, "Показать ошибки");
		ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузитьИзФормыЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, КнопкиВопроса, , КодВозвратаДиалога.Нет);
	Иначе
		ВыгрузитьОтчет();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьИзФормыЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ВыгрузитьОтчет(Истина);
	Иначе
		СпДокОсн = Новый СписокЗначений;
		СпДокОсн.Добавить(СтруктураРеквизитовФормы.мСохраненныйДок);
		ОшибкиОтчета = Новый Массив;
		РегламентированнаяОтчетностьКлиент.ПроверитьРегламентированныеОтчеты(СпДокОсн, ОшибкиОтчета);
		РегламентированнаяОтчетностьКлиент.ОткрытьФормуНавигацииПоОшибкамВыгрузки(ОшибкиОтчета);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыгрузку(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПроверитьВыгрузкуЗавершение", ЭтотОбъект);

	Отказ = Ложь;
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередВыгрузкойРегламентированногоОтчета(
		ОписаниеОповещения, ЭтотОбъект, Отказ);
		
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыгрузкуЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьВыгрузкуОтчета();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыгрузкуОтчета()
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПроверитьВыгрузкуОтчетаЗавершение", ЭтотОбъект);
	
	Если НЕ ЗначениеЗаполнено(СтруктураРеквизитовФормы.Организация) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Организация не выбрана! Выгрузка невозможна.';
														|en = 'Организация не выбрана! Выгрузка невозможна.'"));
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		СохранитьНаКлиенте(,ОписаниеОповещения);
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура ПроверитьВыгрузкуОтчетаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ФормаВыгрузкиРеглОтчета = ПолучитьФорму(
		"Документ.ВыгрузкаРегламентированныхОтчетов.Форма.ФормаДокумента", , ЭтотОбъект);
	
	СпДокОсн = Новый СписокЗначений;
	СпДокОсн.Добавить(СтруктураРеквизитовФормы.мСохраненныйДок);
	
	ФормаВыгрузкиРеглОтчета.Проверить(СпДокОсн);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьОтчет(ВыгружатьСОшибками = Ложь) Экспорт
	
	ДополнительныеПараметры = Новый Структура("ВыгружатьСОшибками", ВыгружатьСОшибками);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузитьОтчетЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	Если Модифицированность Тогда
		СохранитьНаКлиенте(, ОписаниеОповещения);
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура ВыгрузитьОтчетЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ПронумероватьСтрокиДляВыгрузки();
	ФормаВыгрузкиРеглОтчета = ПолучитьФорму(
		"Документ.ВыгрузкаРегламентированныхОтчетов.Форма.ФормаДокумента", , ЭтотОбъект);
	
	СпДокОсн = Новый СписокЗначений;
	СпДокОсн.Добавить(СтруктураРеквизитовФормы.мСохраненныйДок);
	
	ФормаВыгрузкиРеглОтчета.СформироватьИЗаписать(СпДокОсн,,,,,,,,,ДополнительныеПараметры.ВыгружатьСОшибками);
	
КонецПроцедуры

&НаСервере
Функция ПроверитьПоказатели() Экспорт
	
	ДеревоВыгрузки = РегламентированнаяОтчетность.ПолучитьДеревоВыгрузки(ЭтотОбъект, "СхемаВыгрузкиXML2018Кв1");
	ПараметрыВыгрузки = СформироватьСтруктуруПараметров();
	КоллекцияСписковВыбора = "";
	Если НЕ ПроверитьВозможностьВыгрузки(ПараметрыВыгрузки, ДеревоВыгрузки) Тогда
		Возврат Ложь;
	КонецЕсли;
	ЗаполнитьДанными(ДеревоВыгрузки, ПараметрыВыгрузки);
	Если НЕ РегламентированнаяОтчетность.ПроверитьДанные(ЭтотОбъект, ДеревоВыгрузки) Тогда
		Возврат Ложь;
	КонецЕсли;
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Функция УниверсальнаяВыгрузка(ТекстВыгрузки = Неопределено, ПроверятьБлокировки = Истина, ИмяФайлаВыгрузки = "", ВыгружатьСОшибками = Ложь) Экспорт
	
	Перем ПараметрыВыгрузки;
	
	Если Модифицированность Тогда
		
		Если СтрНайти(Заголовок, СтруктураРеквизитовФормы.СтрПериодОтчета) = 0 Тогда
			ТекстЗаголовка = Заголовок + " за " + СтруктураРеквизитовФормы.СтрПериодОтчета;
		Иначе
			ТекстЗаголовка = Заголовок;
		КонецЕсли;
		
		ОргСтр = " (" + СтруктураРеквизитовФормы.НаимОрганизации + ")";
		Если СтрНайти(Заголовок, ОргСтр) = 0 Тогда
			ТекстЗаголовка = ТекстЗаголовка + ОргСтр;
		КонецЕсли;
		
		ОбщегоНазначенияКлиент.СообщитьПользователю("Отчет """ + ТекстЗаголовка + """ не сохранен.");
		Возврат Ложь;
		
	КонецЕсли;
	
	Возврат УниверсальнаяВыгрузкаНаСервере(ВыгружатьСОшибками, ТекстВыгрузки, ПараметрыВыгрузки, ИмяФайлаВыгрузки);
	
КонецФункции

&НаСервере
Функция УниверсальнаяВыгрузкаНаСервере(ВыгружатьСОшибками, ТекстВыгрузки, ПараметрыВыгрузки, ИмяФайлаВыгрузки)
	Если НЕ ВыгружатьСОшибками
		И НЕ УниверсальнаяВыгрузкаXMLПроверкаВозможностиВыгрузкиНаСервере(ТекстВыгрузки, ПараметрыВыгрузки) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ УниверсальнаяВыгрузкаXMLПроверкаДанныхНаСервере(ТекстВыгрузки, ПараметрыВыгрузки) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ИмяФайлаВыгрузки = ПараметрыВыгрузки.ИмяФайла;
	
	Возврат Истина;
КонецФункции

&НаСервере
Функция УниверсальнаяВыгрузкаXMLПроверкаВозможностиВыгрузкиНаСервере(ТекстВыгрузки, ПараметрыВыгрузки) Экспорт
	
	ПараметрыВыгрузки = СформироватьСтруктуруПараметров();
	ДеревоВыгрузки = РегламентированнаяОтчетность.ПолучитьДеревоВыгрузки(ЭтотОбъект, "СхемаВыгрузкиXML2018Кв1");
	Если НЕ ПроверитьВозможностьВыгрузки(ПараметрыВыгрузки, ДеревоВыгрузки) Тогда
		Возврат Ложь;
	КонецЕсли;
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция ПустаяСтруктураВыгрузки()
	Возврат Новый Структура("АдресФайлаВыгрузки, ИмяФайлаВыгрузки, КодировкаФайлаВыгрузки, ТипФайлаВыгрузки");
КонецФункции

&НаСервере
Функция УниверсальнаяВыгрузкаXMLПроверкаДанныхНаСервере(ТекстВыгрузки, ПараметрыВыгрузки) Экспорт
	
	Если ПараметрыВыгрузки = Неопределено Тогда 
		ПараметрыВыгрузки = СформироватьСтруктуруПараметров();
	КонецЕсли;
	ДеревоВыгрузки = РегламентированнаяОтчетность.ПолучитьДеревоВыгрузки(ЭтотОбъект, "СхемаВыгрузкиXML2018Кв1");
	ЗаполнитьДанными(ДеревоВыгрузки, ПараметрыВыгрузки);
	Если НЕ РегламентированнаяОтчетность.ПроверитьДанные(ЭтотОбъект, ДеревоВыгрузки) Тогда
		КоллекцияСписковВыбора = "";
		Возврат Ложь;
	КонецЕсли;
	
	ТекстВыгрузки = РегламентированнаяОтчетность.ВыгрузитьДеревоВXML(ДеревоВыгрузки, ПараметрыВыгрузки);
	РезультатВыгрузки = Новый Массив;
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("xml");
	ЗаписьТекста = Новый ЗаписьТекста(ИмяВременногоФайла, "utf-8");
	ЗаписьТекста.Записать(ТекстВыгрузки);
	ЗаписьТекста.Закрыть();
	ЗаписьТекста = Неопределено;
	ФайлВыгрузки = Новый ДвоичныеДанные(ИмяВременногоФайла);
	УдалитьФайлы(ФайлВыгрузки);
	
	ОсновнаяВыгрузка = ПустаяСтруктураВыгрузки();
	ОсновнаяВыгрузка.АдресФайлаВыгрузки = ПоместитьВоВременноеХранилище(ФайлВыгрузки, Новый УникальныйИдентификатор);
	ОсновнаяВыгрузка.ИмяФайлаВыгрузки = ПараметрыВыгрузки.ИмяФайла;
	ОсновнаяВыгрузка.КодировкаФайлаВыгрузки = "utf-8";
	ОсновнаяВыгрузка.ТипФайлаВыгрузки = "ОсновныеСведения";
	РезультатВыгрузки.Добавить(ОсновнаяВыгрузка);
	
	Если ЗначениеЗаполнено(Доверенность) Тогда
		Приложение = ПустаяСтруктураВыгрузки();
		Приложение.АдресФайлаВыгрузки = ПоместитьВоВременноеХранилище(РаботаСФайлами.ДвоичныеДанныеФайла(Доверенность), Новый УникальныйИдентификатор);
		Приложение.ИмяФайлаВыгрузки = ИмяФайлаДоверенности;
		Приложение.КодировкаФайлаВыгрузки = "";
		Приложение.ТипФайлаВыгрузки = "2ТПВОЗДУХ.ПРИЛОЖЕНИЕ";
		РезультатВыгрузки.Добавить(Приложение);
	КонецЕсли;
	
	Для Каждого Стр Из _Приложения.ПолучитьЭлементы() Цикл 
		Для Каждого СтрФайл Из Стр.ПолучитьЭлементы() Цикл
			Приложение = ПустаяСтруктураВыгрузки();
			Приложение.АдресФайлаВыгрузки = ПоместитьВоВременноеХранилище(РаботаСФайлами.ДвоичныеДанныеФайла(СтрФайл.ПрисоединенныйФайл), Новый УникальныйИдентификатор);
			Приложение.ИмяФайлаВыгрузки = СтрФайл.ОписаниеДокумента;
			Приложение.КодировкаФайлаВыгрузки = "";
			Приложение.ТипФайлаВыгрузки = "2ТПВОЗДУХ.ПРИЛОЖЕНИЕ";
			РезультатВыгрузки.Добавить(Приложение);
		КонецЦикла;
	КонецЦикла;
	
	ТекстВыгрузки = РезультатВыгрузки;

	КоллекцияСписковВыбора = "";
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция ПроверитьВозможностьВыгрузки(ПараметрыВыгрузки, ДеревоВыгрузки)
	СтатусПроверки = Истина;
	
	Возврат СтатусПроверки;
КонецФункции

&НаСервере
Процедура ЗаполнитьДанными(ДеревоВыгрузки, ПараметрыВыгрузки)
	ДополнитьПараметрыВыгрузки(ПараметрыВыгрузки);
	ОбработатьУсловныеЭлементы(ОбщегоНазначения.СкопироватьРекурсивно(ПараметрыВыгрузки), ДеревоВыгрузки);
	ЗаполнитьДаннымиУзел(ПараметрыВыгрузки, ДеревоВыгрузки);
	ОбработатьУсловныеЭлементы(ПараметрыВыгрузки, ДеревоВыгрузки);
	РегламентированнаяОтчетность.ОтсечьНезаполненныеНеобязательныеУзлыСтатистики(ДеревоВыгрузки);
	
	Если ПараметрыВыгрузки.ЕстьПриложения Тогда 
		Узел_response = РегламентированнаяОтчетность.ПолучитьПодчиненныйЭлемент(ДеревоВыгрузки, "response");
		Узел_data = РегламентированнаяОтчетность.ПолучитьПодчиненныйЭлемент(Узел_response, "data");
		Узел_service_request = РегламентированнаяОтчетность.ПолучитьПодчиненныйЭлемент(Узел_data, "service_request");
		Узел_attached_documents = РегламентированнаяОтчетность.ПолучитьПодчиненныйЭлемент(Узел_service_request, "attached_documents");
		Узел_attached_document = РегламентированнаяОтчетность.ПолучитьПодчиненныйЭлемент(Узел_attached_documents, "attached_document");
		
		Для Каждого Стр Из _Приложения.ПолучитьЭлементы() Цикл 
			НовыйУзел_attached_document = РегламентированнаяОтчетность.НовыйУзелИзПрототипа(Узел_attached_document);
			
			РегламентированнаяОтчетность.УстановитьЗначениеЭлемента(НовыйУзел_attached_document, "number", Стр.Номер);
			РегламентированнаяОтчетность.УстановитьЗначениеЭлемента(НовыйУзел_attached_document, "name", Стр.Название);
			РегламентированнаяОтчетность.УстановитьЗначениеЭлемента(НовыйУзел_attached_document, "comment", Стр.Комментарий);
			РегламентированнаяОтчетность.УстановитьЗначениеЭлемента(НовыйУзел_attached_document, "date", Формат(Стр.Дата, "ДФ='yyyy-MM-dd HH:mm:ss'; ДП=' '"));
			
			Узел_documents_attachments_files = РегламентированнаяОтчетность.ПолучитьПодчиненныйЭлемент(НовыйУзел_attached_document, "documents_attachments_files");
			Узел_documents_attachments_file = РегламентированнаяОтчетность.ПолучитьПодчиненныйЭлемент(Узел_documents_attachments_files, "documents_attachments_file");
			Для Каждого СтрФайл Из Стр.ПолучитьЭлементы() Цикл
				НовыйУзел_documents_attachments_file = РегламентированнаяОтчетность.НовыйУзелИзПрототипа(Узел_documents_attachments_file);
				РегламентированнаяОтчетность.УстановитьЗначениеЭлемента(НовыйУзел_documents_attachments_file, "uuid", Строка(Новый УникальныйИдентификатор));
				РегламентированнаяОтчетность.УстановитьЗначениеЭлемента(НовыйУзел_documents_attachments_file, "gost", СтрФайл.Комментарий);
				РегламентированнаяОтчетность.УстановитьЗначениеЭлемента(НовыйУзел_documents_attachments_file, "name", СтрФайл.ОписаниеДокумента);
				РегламентированнаяОтчетность.УстановитьЗначениеЭлемента(НовыйУзел_documents_attachments_file, "file_name", СтрФайл.ОписаниеДокумента);
				РегламентированнаяОтчетность.УстановитьЗначениеЭлемента(НовыйУзел_documents_attachments_file, "extension", СтрФайл.Название);
			КонецЦикла;
			РегламентированнаяОтчетность.УдалитьУзел(Узел_documents_attachments_file);
		КонецЦикла;
		РегламентированнаяОтчетность.УдалитьУзел(Узел_attached_document);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОбработатьУсловныеЭлементы(Знач ПараметрыВыгрузки, Узел)
	мДанныеОтчета = СтруктураРеквизитовФормы.мСохраненныйДок.ДанныеОтчета.Получить().ПоказателиОтчета;
	Если ТипЗнч(Узел) <> Тип("ДеревоЗначений") Тогда
		Если Узел.Код = ПараметрыВыгрузки.ИмяКлючевогоУзлаСодержательнойЧасти Тогда
			Возврат;
		КонецЕсли;
		РегламентированнаяОтчетность.ДополнитьСтруктуруПараметров(мДанныеОтчета, ПараметрыВыгрузки, Узел);
	КонецЕсли;
	КоличествоСтрок = Узел.Строки.Количество();
	Для Инд = 1 По КоличествоСтрок Цикл
		ТекСтр = Узел.Строки.Получить(КоличествоСтрок - Инд);
		Если НЕ ПустаяСтрока(ТекСтр.Условие) Тогда
			Если НЕ РегламентированнаяОтчетность.УсловиеВыполнено(ЭтотОбъект, ПараметрыВыгрузки, ТекСтр.Условие, "", Узел) Тогда
				Узел.Строки.Удалить(ТекСтр);
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		ОбработатьУсловныеЭлементы(ПараметрыВыгрузки, ТекСтр);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДаннымиУзел(ПараметрыВыгрузки, Узел, НомерСтроки = Неопределено)
	
	СтрокиУзла = Новый Массив;
	Для Каждого Стр Из Узел.Строки Цикл
		СтрокиУзла.Добавить(Стр);
	КонецЦикла;
	
	Для Каждого Стр из СтрокиУзла Цикл
		
		Если ПустаяСтрока(Стр.ЗначениеПоУмолчанию) Тогда
			Если НЕ ПустаяСтрока(Стр.Ключ) Тогда
				ПолныйКодПоказателя = Стр.Ключ + ?(ЗначениеЗаполнено(НомерСтроки), "_" + Формат(НомерСтроки, "ЧГ="), "");
				ЗначениеПоказателя = Неопределено;
				Если ПараметрыВыгрузки.Свойство(ПолныйКодПоказателя, ЗначениеПоказателя) Тогда
					ФорматПредставленияНуля = Неопределено;
					Если ПараметрыВыгрузки.Свойство("ЧН" + ПолныйКодПоказателя, ФорматПредставленияНуля) Тогда
						РегламентированнаяОтчетность.ВывестиПоказательСтатистикиВXML(Стр, ЗначениеПоказателя, ФорматПредставленияНуля);
					Иначе
						РегламентированнаяОтчетность.ВывестиПоказательСтатистикиВXML(Стр, ЗначениеПоказателя);
					КонецЕсли;
				Иначе
					РегламентированнаяОтчетность.ВывестиПоказательСтатистикиВXML(Стр, "");
				КонецЕсли;
			Иначе
				Стр.Значение = "";
			КонецЕсли;
		ИначеЕсли Лев(Стр.ЗначениеПоУмолчанию, 1) = "&" Тогда
			РегламентированнаяОтчетность.ВывестиПоказательСтатистикиВXML(Стр, ПараметрыВыгрузки[Сред(Стр.ЗначениеПоУмолчанию, 2)]);
		Иначе
			Стр.Значение = Стр.ЗначениеПоУмолчанию;
			Если НЕ ПустаяСтрока(Стр.Ключ) Тогда
				ПолныйКодПоказателя = Стр.Ключ + ?(ЗначениеЗаполнено(НомерСтроки), "_" + Формат(НомерСтроки, "ЧГ="), "");
				ЗначениеПоказателя = Неопределено;
				Если ПараметрыВыгрузки.Свойство(ПолныйКодПоказателя, ЗначениеПоказателя) Тогда
					ФорматПредставленияНуля = Неопределено;
					Если ПараметрыВыгрузки.Свойство("ЧН" + ПолныйКодПоказателя, ФорматПредставленияНуля) Тогда
						РегламентированнаяОтчетность.ВывестиПоказательСтатистикиВXML(Стр, ЗначениеПоказателя, ФорматПредставленияНуля);
					Иначе
						РегламентированнаяОтчетность.ВывестиПоказательСтатистикиВXML(Стр, ЗначениеПоказателя);
					КонецЕсли;
				Иначе
					РегламентированнаяОтчетность.ВывестиПоказательСтатистикиВXML(Стр, "");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если Стр.Тип = "С" ИЛИ Стр.Тип = "C" Тогда
			Если Стр.Многострочность Тогда 
				НомСтр = 1;
				Пока РегламентированнаяОтчетностьКлиентСервер.СвойствоОпределено(ПараметрыВыгрузки, Стр.Ключ + Формат(НомСтр, "ЧГ=")) Цикл
					Если Не ЗначениеЗаполнено(ПараметрыВыгрузки[Стр.Ключ + Формат(НомСтр, "ЧГ=")]) Тогда 
						НомСтр = НомСтр + 1;
						Продолжить;
					КонецЕсли;
					УзелСоответствующийСтроке = РегламентированнаяОтчетность.СкопироватьУзел(Узел, Стр);
					ЗаполнитьДаннымиУзел(ПараметрыВыгрузки, УзелСоответствующийСтроке, НомСтр);
					НомСтр = НомСтр + 1;
				КонецЦикла;
				РегламентированнаяОтчетность.УдалитьУзел(Стр);
			Иначе
				Если ЗначениеЗаполнено(Стр.ЗначениеПоУмолчанию) Тогда
					РегламентированнаяОтчетность.ВывестиПоказательСтатистикиВXML(Стр, ПараметрыВыгрузки[Сред(Стр.ЗначениеПоУмолчанию, 2)]);
				КонецЕсли;
				ЗаполнитьДаннымиУзел(ПараметрыВыгрузки, Стр, НомерСтроки);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция СформироватьИмяФайла(ПараметрыВыгрузки)
	Перем ПолученноеЗначение;
	
	ИмяФайлаXML = "2tp_air"
				+ ".xml";
	
	Возврат ИмяФайлаXML;
КонецФункции

&НаСервере
Функция СформироватьСтруктуруПараметров()
	Перем ПолученноеЗначение;
	ТабДокумент = СтруктураРеквизитовФормы.мСохраненныйДок.ДанныеОтчета.Получить().ПоказателиОтчета.ПолеТабличногоДокументаФормаОтчета;
	
	СтрокаСведений = "ОКВЭД2, ОКАТО, ОКПО, ОГРН, ТипНП, ДатаРегистрации, ИННЮЛ, КППЮЛ, НаимЮЛПол, АдрЮР, АдрФакт, АдрПочт, ";
	СтрокаСведений = СтрокаСведений + "ОргИндекс, ОргСубъект, ОргРайон, ОргГород, ОргУлица, ОргДом, ОргКорпус, ОргКвартира, ";
	СтрокаСведений = СтрокаСведений + "БанкСчетНомер, БанкСчетНаимБанка, БанкСчетКоррСчетБанка, БанкСчетБИКБанка, ";
	СтрокаСведений = СтрокаСведений + "ТелОрганизации, ФаксОрганизации, ЭлектроннаяПочта, ФИОРук, ТелРук, ФИОБух, ТелБух";
	СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(СтруктураРеквизитовФормы.Организация, СтруктураРеквизитовФормы.мСохраненныйДок.ДатаПодписи, СтрокаСведений);
	
	ПараметрыВыгрузки = Новый Структура;
	ПараметрыВыгрузки.Вставить("Документ", СтруктураРеквизитовФормы.мСохраненныйДок);
	ПараметрыВыгрузки.Вставить("ИмяФайла", СформироватьИмяФайла(ПараметрыВыгрузки));
	ПараметрыВыгрузки.Вставить("ИмяКлючевогоУзлаСодержательнойЧасти", "RPT_2TP_WASTE_FACT_SECTION_");
	
	Для Каждого КЗ Из ТабДокумент Цикл 
		Если Не ПараметрыВыгрузки.Свойство(КЗ.Ключ) Тогда 
			ПараметрыВыгрузки.Вставить(КЗ.Ключ, КЗ.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПараметрыВыгрузки;
КонецФункции

&НаСервере
Процедура ДополнитьПараметрыВыгрузки(ПараметрыВыгрузки)
	ПараметрыВыгрузки.Вставить("UUID", Строка(Новый УникальныйИдентификатор));
	ПараметрыВыгрузки.Вставить("ВерсПрог", РегламентированнаяОтчетность.НазваниеИВерсияПрограммы());
	ПараметрыВыгрузки.Вставить("ДатаФормированияФайла", Формат(ТекущаяДатаСеанса(), "ДФ='yyyy-MM-dd HH:mm:ss'"));
	ПараметрыВыгрузки.Вставить("ОтчетныйГод", Формат(СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета, "ДФ=yyyy"));
	ПараметрыВыгрузки.Вставить("is_ceo_en", ?(ПолеТабличногоДокументаФормаОтчета.Области.is_ceo.Значение = Истина, "true", "false"));
	ПараметрыВыгрузки.Вставить("is_non_resident_en", ?(ПолеТабличногоДокументаФормаОтчета.Области.is_non_resident.Значение = Истина, "true", "false"));
	ПараметрыВыгрузки.Вставить("is_separate_en", ?(ПолеТабличногоДокументаФормаОтчета.Области.is_separate.Значение = Истина, "true", "false"));
	ПараметрыВыгрузки.Вставить("register_date_str", Формат(ПолеТабличногоДокументаФормаОтчета.Области.register_date.Значение, "ДФ=yyyy-MM-dd"));
	ПараметрыВыгрузки.Вставить("egrul_egrip_date_str", Формат(ПолеТабличногоДокументаФормаОтчета.Области.egrul_egrip_date.Значение, "ДФ=yyyy-MM-dd"));
	ПараметрыВыгрузки.Вставить("ЕстьПриложения", (_Приложения.ПолучитьЭлементы().Количество() > 0));
	ПараметрыВыгрузки.Вставить("НеРегистрация", ЗначениеЗаполнено(ПолеТабличногоДокументаФормаОтчета.Области.not_registered_reason_id.Значение));
	Если Не ЗначениеЗаполнено(ПараметрыВыгрузки.onvos_code)
		И (ПараметрыВыгрузки.declared_object_category = "4" Или ПараметрыВыгрузки.declared_object_category = "IV") Тогда 
		ПараметрыВыгрузки.onvos_code = "АА-0000-000000-П";
	КонецЕсли;
	
	Для Инд = 1 По 9 Цикл 
		Для Гр = 1 По 8 Цикл 
			Если Гр > 2 Тогда 
				ПараметрыВыгрузки.Вставить("П90000001010" + Гр + "_" + Инд, ПолеТабличногоДокументаФормаОтчета.Области["П000100" + Инд + "0" + Гр].Значение);
			Иначе
				ПараметрыВыгрузки.Вставить("П90000001010" + Гр + "_" + Инд, "");
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	ПараметрыВыгрузки["П900000010101_1"] = "pollutant_total";
	ПараметрыВыгрузки["П900000010101_2"] = "pollutant_solid";
	ПараметрыВыгрузки["П900000010101_3"] = "pollutant_liquid";
	ПараметрыВыгрузки["П900000010101_4"] = "pollutant_sulfur_dioxide";
	ПараметрыВыгрузки["П900000010101_5"] = "pollutant_carbon_monoxide";
	ПараметрыВыгрузки["П900000010101_6"] = "pollutant_nitrogen_oxide";
	ПараметрыВыгрузки["П900000010101_7"] = "pollutant_hydrocarbon";
	ПараметрыВыгрузки["П900000010101_8"] = "pollutant_volatile_organic_compounds";
	ПараметрыВыгрузки["П900000010101_9"] = "pollutant_other";
	ПараметрыВыгрузки["П900000010102_1"] = "Всего";
	ПараметрыВыгрузки["П900000010102_2"] = "в том числе твердых";
	ПараметрыВыгрузки["П900000010102_3"] = "в том числе газообразные и жидкие";
	ПараметрыВыгрузки["П900000010102_4"] = "из них: диоксид серы";
	ПараметрыВыгрузки["П900000010102_5"] = "оксид углерода";
	ПараметрыВыгрузки["П900000010102_6"] = "оксид азота (в пересчете на NO2)";
	ПараметрыВыгрузки["П900000010102_7"] = "углеводороды (без летучих органических соединений)";
	ПараметрыВыгрузки["П900000010102_8"] = "летучие органические соединения (ЛОС)";
	ПараметрыВыгрузки["П900000010102_9"] = "прочие газообразные и жидкие";
	
	Инд = 0;
	Итер = 0;
	Раздел2Заполнен = Ложь;
	Пока Истина Цикл 
		Инд = Инд + 1;
		Если ПолеТабличногоДокументаФормаОтчета.Области.Найти("П000002000101_" + Формат(Инд, "ЧГ=")) = Неопределено Тогда 
			Прервать;
		КонецЕсли;
		КодВещества = ПолеТабличногоДокументаФормаОтчета.Области.Найти("П000002000102_" + Формат(Инд, "ЧГ=")).Значение;
		Если ЗначениеЗаполнено(КодВещества) И ТипЗнч(КодВещества) = Тип("Число") Тогда 
			КодВещества = Формат(КодВещества, "ЧЦ=4; ЧВН=; ЧГ=");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(КодВещества) Тогда 
			Раздел2Заполнен = Истина;
			Итер = Итер + 1;
			ПараметрыВыгрузки.Вставить("П800000010101_" + Формат(Итер, "ЧГ="), "id");
			ПараметрыВыгрузки.Вставить("П800000010103_" + Формат(Итер, "ЧГ="), КодВещества);
			ПараметрыВыгрузки.Вставить("П800000010102_" + Формат(Итер, "ЧГ="), 
				ПолеТабличногоДокументаФормаОтчета.Области.Найти("П000002000103_" + Формат(Инд, "ЧГ=")).Значение);
			ПараметрыВыгрузки.Вставить("П800000010104_" + Формат(Итер, "ЧГ="), 
				ПолеТабличногоДокументаФормаОтчета.Области.Найти("П000002000104_" + Формат(Инд, "ЧГ=")).Значение);
		КонецЕсли;
	КонецЦикла;
	ПараметрыВыгрузки.Вставить("Раздел2Заполнен", Раздел2Заполнен);
	
	Инд = 0;
	Итер = 0;
	Раздел4Заполнен = Ложь;
	Пока Истина Цикл 
		Инд = Инд + 1;
		Если ПолеТабличногоДокументаФормаОтчета.Области.Найти("П000004000101_" + Формат(Инд, "ЧГ=")) = Неопределено Тогда 
			Прервать;
		КонецЕсли;
		Если ЗначениеЗаполнено(ПолеТабличногоДокументаФормаОтчета.Области.Найти("П000004000101_" + Формат(Инд, "ЧГ=")).Значение) Тогда 
			Раздел4Заполнен = Истина;
			Итер = Итер + 1;
			ПараметрыВыгрузки.Вставить("П700000010101_" + Формат(Итер, "ЧГ="), 
				ПолеТабличногоДокументаФормаОтчета.Области.Найти("П000004000101_" + Формат(Инд, "ЧГ=")).Значение);
			ПараметрыВыгрузки.Вставить("П700000010102_" + Формат(Итер, "ЧГ="), 
				ПолеТабличногоДокументаФормаОтчета.Области.Найти("П000004000102_" + Формат(Инд, "ЧГ=")).Значение);
			ПараметрыВыгрузки.Вставить("П700000010107_" + Формат(Итер, "ЧГ="), 
				ПолеТабличногоДокументаФормаОтчета.Области.Найти("П000004000107_" + Формат(Инд, "ЧГ=")).Значение);
			ПараметрыВыгрузки.Вставить("П700000010108_" + Формат(Итер, "ЧГ="), 
				ПолеТабличногоДокументаФормаОтчета.Области.Найти("П000004000108_" + Формат(Инд, "ЧГ=")).Значение);
			ПараметрыВыгрузки.Вставить("П700000010106_" + Формат(Итер, "ЧГ="), 
				ПолеТабличногоДокументаФормаОтчета.Области.Найти("П000004000106_" + Формат(Инд, "ЧГ=")).Значение);
			ПараметрыВыгрузки.Вставить("П700000010105_" + Формат(Итер, "ЧГ="), 
				ПолеТабличногоДокументаФормаОтчета.Области.Найти("П000004000105_" + Формат(Инд, "ЧГ=")).Значение);
			ПараметрыВыгрузки.Вставить("П700000010103_" + Формат(Итер, "ЧГ="), 
				ПолеТабличногоДокументаФормаОтчета.Области.Найти("П000004000103_" + Формат(Инд, "ЧГ=")).Значение);
			Если ПолеТабличногоДокументаФормаОтчета.Области.Найти("П000004000104_" + Формат(Инд, "ЧГ=")).Значение = 1 Тогда 
				ПараметрыВыгрузки.Вставить("П700000010104_" + Формат(Итер, "ЧГ="), "true");
			Иначе
				ПараметрыВыгрузки.Вставить("П700000010104_" + Формат(Итер, "ЧГ="), "false");
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	ПараметрыВыгрузки.Вставить("Раздел4Заполнен", Раздел4Заполнен);
	
	ПараметрыВыгрузки.Вставить("П600000010101_5", "pollutant_hydrocarbon");
	ПараметрыВыгрузки.Вставить("П600000010102_5", "Углеводороды с учетом ЛОС (исключая метан)");
	ПараметрыВыгрузки.Вставить("П600000010103_5", ПолеТабличногоДокументаФормаОтчета.Области["П000500504"].Значение);
	ПараметрыВыгрузки.Вставить("П600000010104_5", ПолеТабличногоДокументаФормаОтчета.Области["П000500505"].Значение);
	ПараметрыВыгрузки.Вставить("П600000010101_4", "pollutant_nitrogen_oxide");
	ПараметрыВыгрузки.Вставить("П600000010102_4", "Оксид азота (в пересчете на NO2)");
	ПараметрыВыгрузки.Вставить("П600000010103_4", ПолеТабличногоДокументаФормаОтчета.Области["П000500404"].Значение);
	ПараметрыВыгрузки.Вставить("П600000010104_4", ПолеТабличногоДокументаФормаОтчета.Области["П000500405"].Значение);
	ПараметрыВыгрузки.Вставить("П600000010101_3", "pollutant_carbon_monoxide");
	ПараметрыВыгрузки.Вставить("П600000010102_3", "Оксид углерода");
	ПараметрыВыгрузки.Вставить("П600000010103_3", ПолеТабличногоДокументаФормаОтчета.Области["П000500304"].Значение);
	ПараметрыВыгрузки.Вставить("П600000010104_3", ПолеТабличногоДокументаФормаОтчета.Области["П000500305"].Значение);
	ПараметрыВыгрузки.Вставить("П600000010101_2", "pollutant_sulfur_dioxide");
	ПараметрыВыгрузки.Вставить("П600000010102_2", "Диоксид серы");
	ПараметрыВыгрузки.Вставить("П600000010103_2", ПолеТабличногоДокументаФормаОтчета.Области["П000500204"].Значение);
	ПараметрыВыгрузки.Вставить("П600000010104_2", ПолеТабличногоДокументаФормаОтчета.Области["П000500205"].Значение);
	ПараметрыВыгрузки.Вставить("П600000010101_1", "pollutant_solid");
	ПараметрыВыгрузки.Вставить("П600000010102_1", "Твердые вещества");
	ПараметрыВыгрузки.Вставить("П600000010103_1", ПолеТабличногоДокументаФормаОтчета.Области["П000500104"].Значение);
	ПараметрыВыгрузки.Вставить("П600000010104_1", ПолеТабличногоДокументаФормаОтчета.Области["П000500105"].Значение);
	
	ПараметрыВыгрузки.Вставить("ЕстьДоверенность", ЗначениеЗаполнено(Доверенность));
	ПараметрыВыгрузки.Вставить("Доверенность_УИД", Строка(Новый УникальныйИдентификатор));
	ПараметрыВыгрузки.Вставить("Доверенность_Файл", ИмяФайлаДоверенности);
	ПараметрыВыгрузки.Вставить("Доверенность_Хеш", ХешДоверенности);
	
	ИндексТочки = СтрНайти(ИмяФайлаДоверенности, ".", НаправлениеПоиска.СКонца);
	Если ИндексТочки = 0 Тогда 
		ПараметрыВыгрузки.Вставить("Доверенность_БезРасширения", ИмяФайлаДоверенности);
		ПараметрыВыгрузки.Вставить("Доверенность_Расширение", );
	Иначе
		ПараметрыВыгрузки.Вставить("Доверенность_БезРасширения", Лев(ИмяФайлаДоверенности, ИндексТочки - 1));
		ПараметрыВыгрузки.Вставить("Доверенность_Расширение", Сред(ИмяФайлаДоверенности, ИндексТочки + 1));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьПакет(Команда)
	
	Если НЕ ЗначениеЗаполнено(СтруктураРеквизитовФормы.Организация) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Организация не выбрана! Выгрузка невозможна.';
														|en = 'Организация не выбрана! Выгрузка невозможна.'"));
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузитьПакетПослеСохранения", ЭтотОбъект);
	
	Если Модифицированность Тогда
		СохранитьНаКлиенте(, ОписаниеОповещения);
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьПакетПослеСохранения(Результат, ДополнительныеПараметры) Экспорт
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузитьПакетПослеПолученияКонтекстаЭДО", ЭтотОбъект);
	ДокументооборотСКОКлиент.ПолучитьКонтекстЭДО(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьПакетПослеПолученияКонтекстаЭДО(Результат, ДополнительныеПараметры) Экспорт
	
	КонтекстЭДОКлиент = Результат.КонтекстЭДО;
	
	ДополнительныеПараметры = Новый Структура("КонтекстЭДОКлиент", КонтекстЭДОКлиент);
	ОписаниеОповещения =
		Новый ОписаниеОповещения("ВыгрузитьПакетПослеВыгрузкиПакета", ЭтотОбъект, ДополнительныеПараметры);
	КонтекстЭДОКлиент.ВыгрузитьПакетДляОтправкиВРПН(ЭтотОбъект, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьПакетПослеВыгрузкиПакета(Результат, ДополнительныеПараметры) Экспорт
	
	КонтекстЭДОКлиент = ДополнительныеПараметры.КонтекстЭДОКлиент;
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяВременногоФайлаПакета = Результат.РезультатВыгрузки;
	
	Если ЗначениеЗаполнено(ИмяВременногоФайлаПакета) Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузитьПакетПослеСохраненияФайлаНаКлиентеЗавершение", ЭтотОбъект);
		КонтекстЭДОКлиент.СохранитьФайлНаКлиенте(ИмяВременногоФайлаПакета, ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьПакетПослеСохраненияФайлаНаКлиентеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ Результат Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Ошибка при выгрузке пакета';
														|en = 'Ошибка при выгрузке пакета'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЧерезПредставителя(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьЧерезПредставителяЗавершение", ЭтотОбъект);

	Отказ = Ложь;
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередВыгрузкойРегламентированногоОтчета(
		ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЧерезПредставителяЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	РегламентированнаяОтчетностьКлиент.ПриНажатииНаКнопкуОтправкиЧерезПредставителяВРегламентированномОтчете(
		ЭтотОбъект, "РПН");
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВИнтернете(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПроверитьВИнтернетеЗавершение", ЭтотОбъект);

	Отказ = Ложь;
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередВыгрузкойРегламентированногоОтчета(
		ОписаниеОповещения, ЭтотОбъект, Отказ);
		
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВИнтернетеЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	РегламентированнаяОтчетностьКлиент.ПроверитьВИнтернете(ЭтотОбъект, "РПН");
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьТаблицуВариантовЗаполненияНаСервере()
	
	ИзменитьТаблицуВариантовЗаполнения(ЭтотОбъект, НЕ СтруктураРеквизитовФормы.ФлажокОтклАвтоРасчет);
	РегламентированнаяОтчетность.ПолучитьСведенияОПоказателяхОтчета(
		ЭтотОбъект, Новый Структура("Период", СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета));
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ИзменитьТаблицуВариантовЗаполнения(Форма, ПолныйРучнойВвод = Ложь)
	
	мДеревоСтраницОтчета = Форма.мДеревоСтраницОтчета;
	ИмяСтраницы = мДеревоСтраницОтчета.ПолучитьЭлементы()[0].ИмяСтраницы;
	
	ТаблицаВариантыЗаполнения = РегламентированнаяОтчетностьВызовСервера.ПолучитьТаблицуВариантовЗаполненияНаСервере(
		Форма, ИмяСтраницы);
	
	Если ТаблицаВариантыЗаполнения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ПолныйРучнойВвод Тогда
		
		Для Каждого Строка Из ТаблицаВариантыЗаполнения Цикл
			Если Строка.ВариантЗаполнения = 4 Тогда
				Строка.ВариантЗаполнения = 0;
			КонецЕсли;
		КонецЦикла;
		
		Форма.УстановитьЦветФонаЯчейкиНаСервере(,,Форма.СтруктураРеквизитовФормы.НаимТекущегоРаздела);
		
	ИначеЕсли ПолныйРучнойВвод Тогда
		
		Для Каждого Стр Из ТаблицаВариантыЗаполнения Цикл
			Если Стр.ВариантЗаполнения = 0 Тогда
				Стр.ВариантЗаполнения = 4
			КонецЕсли;
		КонецЦикла;
		
		Форма.УстановитьЦветФонаЯчейкиНаСервере(,,Форма.СтруктураРеквизитовФормы.НаимТекущегоРаздела);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ФормироватьСтруктуруСтраницОтчета() 
	
	мДеревоСтраницОтчета.ПолучитьЭлементы().Очистить();
	
	СтрокаУровня1 = мДеревоСтраницОтчета.ПолучитьЭлементы().Добавить();
	СтрокаУровня1.ИмяСтраницы        = "ПолеТабличногоДокументаФормаОтчета";
	СтрокаУровня1.Представление      =
		"Сведения о производстве, отгрузке продукции и балансе производственных мощностей";
	СтрокаУровня1.ОриентацияСтраницы = "Ландшафт";
	СтрокаУровня1.ВыводНаПечать      = 1;
	СтрокаУровня1.ПоказатьСтраницу   = 1;
	СтрокаУровня1.ВыгрузитьСтраницу  = 2;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЦветФонаЯчейкиНаКлиенте(ИмяЯчейки = Неопределено, ВариантЗаполнения = Неопределено, ИмяТекущейСтраницыПанели = Неопределено) Экспорт
	
	УстановитьЦветФонаЯчейки(ЭтотОбъект, ИмяЯчейки, ВариантЗаполнения, ИмяТекущейСтраницыПанели);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЦветФонаЯчейкиНаСервере(ИмяЯчейки = Неопределено, ВариантЗаполнения = Неопределено, ИмяТекущейСтраницыПанели = Неопределено) Экспорт
	
	УстановитьЦветФонаЯчейки(ЭтотОбъект, ИмяЯчейки, ВариантЗаполнения, ИмяТекущейСтраницыПанели);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЦветФонаЯчейки(Форма, ИмяЯчейки = Неопределено, ВариантЗаполнения = Неопределено, ИмяТекущейСтраницыПанели = Неопределено) Экспорт
	Перем ЦветФона;
	Перем Комментарий;
	
	Если Форма.СтруктураРеквизитовФормы.мБезОткрытияФормы ИЛИ Форма.СтруктураРеквизитовФормы.мРежимПечати Тогда
		Возврат;
	КонецЕсли;
	
	Если ИмяЯчейки <> Неопределено Тогда
		#Если НаКлиенте Тогда
			ИмяСтраницыПанели =
				РегламентированнаяОтчетностьКлиент.ОпределитьСтраницуПанелиПоКодуПоказателяНаКлиенте(
				Форма, ИмяЯчейки);
		#Иначе
			ИмяСтраницыПанели =
				РегламентированнаяОтчетностьВызовСервера.ОпределитьСтраницуПанелиПоКодуПоказателяНаСервере(
				Форма, ИмяЯчейки);
		#КонецЕсли
		Если ИмяСтраницыПанели = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ИмяПоляТаблДокумента = "ПолеТабличногоДокументаФормаОтчета";
		
		Ключ = "_" + ВариантЗаполнения;
		Форма.мСтруктураЦветовРаскраски.Свойство(Ключ, ЦветФона);
		
		ЗначениеВКвадратах = РегламентированнаяОтчетностьКлиентСервер.ОбластьЯвляетсяЯчейкойСоставногоПоля(Форма, ИмяЯчейки);
		
		Если ВариантЗаполнения = 4 ИЛИ ВариантЗаполнения = 5 Тогда
			
			Если ЗначениеВКвадратах Тогда
				РегламентированнаяОтчетностьКлиентСервер.СвойстваГрупповыхЗначений(Форма, ИмяЯчейки, "УстановитьЗащиту");
			Иначе
				Форма[ИмяПоляТаблДокумента].Области[ИмяЯчейки].Защита = Истина;
			КонецЕсли;
			
		Иначе
			
			Если ЗначениеВКвадратах Тогда
				РегламентированнаяОтчетностьКлиентСервер.СвойстваГрупповыхЗначений(Форма, ИмяЯчейки, "СнятьЗащиту");
			Иначе
				Форма[ИмяПоляТаблДокумента].Области[ИмяЯчейки].Защита = Ложь;
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЗначениеВКвадратах Тогда
			РегламентированнаяОтчетностьКлиентСервер.СвойстваГрупповыхЗначений(Форма, ИмяЯчейки,
				"ПроставитьЦветРаскраски", ЦветФона);
		Иначе
			Форма[ИмяПоляТаблДокумента].Области[ИмяЯчейки].ЦветФона = ЦветФона;
		КонецЕсли;
		
	Иначе
		ТаблицаВариантыЗаполнения =
			РегламентированнаяОтчетностьВызовСервера.ПолучитьТаблицуВариантовЗаполненияНаСервере(
			Форма,	ИмяТекущейСтраницыПанели);
		
		Если ТаблицаВариантыЗаполнения = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Для Каждого Строка Из ТаблицаВариантыЗаполнения Цикл
			ИмяПоказателя            = Строка.КодПоказателя;
			ВариантЗаполнения        = Строка.ВариантЗаполнения;
			Комментарий              = Строка.Комментарий;
			КодПоказателяПоСтруктуре = Строка.КодПоказателяПоСтруктуре;
			
			КолвоСтрокВМногострочнойЧасти =
				РегламентированнаяОтчетностьКлиентСервер.ПоказательВходитВМногострочнуюЧасть(Форма, ИмяПоказателя);
			
			Если КолвоСтрокВМногострочнойЧасти > 0 Тогда
				Многострочность = Истина;
				БазоваяЧасть = ИмяПоказателя;
				ОписанияБлоков = РегламентированнаяОтчетностьКлиентСервер.ПолучитьСвойствоРаздела(Форма,
					ИмяТекущейСтраницыПанели, "МногострочностьВРазделе");
				
				Если ТипЗнч(ОписанияБлоков) = Тип("Число") Тогда
					ВБлокеВсеПоказателиЗаполняются = Истина;
				ИначеЕсли ТипЗнч(ОписанияБлоков) = Тип("ДанныеФормыКоллекция") Тогда
					ИмяГруппы = Сред(ИмяПоказателя, 1, 11);
					ВБлокеВсеПоказателиЗаполняются =
						НЕ ОписанияБлоков.НайтиСтроки(Новый Структура("ИдГруппы", ИмяГруппы))[0].НезаполняемыеПоказателиЕсть;
				КонецЕсли;
				
			Иначе
				Многострочность = Ложь;
				ВБлокеВсеПоказателиЗаполняются = Истина;
				КолвоСтрокВМногострочнойЧасти = 1;
			КонецЕсли;
			
			Для Инд = 1 по КолвоСтрокВМногострочнойЧасти Цикл
				
				Если Многострочность Тогда
					ИмяПоказателя = БазоваяЧасть + "_" + Формат(Инд, "ЧГ=0");
				КонецЕсли;
				
				ТекОбластьЯчеек = Форма.ПолеТабличногоДокументаФормаОтчета.Области[ИмяПоказателя];
				Если (Не ПустаяСтрока(Комментарий)) ИЛИ (Не ПустаяСтрока(ТекОбластьЯчеек.Примечание.Текст)) Тогда
					ТекОбластьЯчеек.Примечание.Текст = Комментарий;
				КонецЕсли;
				
				Ключ = "_" + ВариантЗаполнения;
				Форма.мСтруктураЦветовРаскраски.Свойство(Ключ, ЦветФона);
				
				ЗначениеВКвадратах = ?(ПустаяСтрока(КодПоказателяПоСтруктуре), Ложь, Истина);
				
				Если ВариантЗаполнения = 4 ИЛИ ВариантЗаполнения = 5 Тогда
					
					Если ЗначениеВКвадратах Тогда
						РегламентированнаяОтчетностьКлиентСервер.СвойстваГрупповыхЗначений(Форма, ИмяПоказателя, "УстановитьЗащиту");
					Иначе
						Форма.ПолеТабличногоДокументаФормаОтчета.Области[ИмяПоказателя].Защита = Истина;
					КонецЕсли;
					
				Иначе
					
					Если ЗначениеВКвадратах Тогда
						РегламентированнаяОтчетностьКлиентСервер.СвойстваГрупповыхЗначений(Форма, ИмяПоказателя, "СнятьЗащиту");
					Иначе
						Форма.ПолеТабличногоДокументаФормаОтчета.Области[ИмяПоказателя].Защита = Ложь;
					КонецЕсли;
				КонецЕсли;
				
				Если ЗначениеВКвадратах Тогда
					РегламентированнаяОтчетностьКлиентСервер.СвойстваГрупповыхЗначений(Форма, ИмяПоказателя,
						"ПроставитьЦветРаскраски", ЦветФона);
				Иначе
					Форма.ПолеТабличногоДокументаФормаОтчета.Области[ИмяПоказателя].ЦветФона = ЦветФона;
				КонецЕсли;
			КонецЦикла;
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ФормироватьСтруктуруСвойствСтраниц() Экспорт
	
	мСвойстваРазделовДекларации.ПолучитьЭлементы().Очистить();
	
	СтрокаУровня1 = мСвойстваРазделовДекларации.ПолучитьЭлементы().Добавить();
	
	СтрокаУровня1.ИмяСтраницы                          = "ПолеТабличногоДокументаФормаОтчета";
	СтрокаУровня1.МногострочностьВРазделе              = 5;
	СтрокаУровня1.МногостраничностьВРазделе            = Ложь;
	СтрокаУровня1.ОчищатьРаздел                        = Ложь;
	СтрокаУровня1.ИмяПредставления                     = "";
	СтрокаУровня1.ПредставлениеДанных                  = Ложь;
	СтрокаУровня1.РазделОбязателенДляВыгрузки          = Неопределено;
	СтрокаУровня1.НазваниеПанелиТабличногоПоляРаздела  = "ОсновнаяПанель";
	СтрокаУровня1.СохранятьМногострКакТЗ               = Неопределено;
	СтрокаУровня1.РазделЯвляетсяАвтозаполняемым        = Ложь;
	
КонецПроцедуры 

&НаСервере
Процедура УстановитьФорматВыводаВПоляхВвода(Форма, НаимРаздела = "") Экспорт
	Перем СтруктураГруппы;
	
	Если Форма.СтруктураРеквизитовФормы.ЕдиницаИзмерения =
		Форма.ПорядкиОкругленияОтчетности.ПеречислениеПорядкиОкругленияОтчетностиОкр1 Тогда
		Форма.СтруктураРеквизитовФормы.мСтрокаФормата = "; ЧДЦ = "
			+ Форма.СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения + "; ЧРД=,; ЧН=-; ЧС = 0";
	ИначеЕсли Форма.СтруктураРеквизитовФормы.ЕдиницаИзмерения =
		Форма.ПорядкиОкругленияОтчетности.ПеречислениеПорядкиОкругленияОтчетностиОкр1000 Тогда
		Форма.СтруктураРеквизитовФормы.мСтрокаФормата = "; ЧДЦ = "
			+ Форма.СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения + "; ЧРД=,; ЧН=-; ЧС = 3";
	ИначеЕсли Форма.СтруктураРеквизитовФормы.ЕдиницаИзмерения =
		Форма.ПорядкиОкругленияОтчетности.ПеречислениеПорядкиОкругленияОтчетностиОкр1000000 Тогда
		Форма.СтруктураРеквизитовФормы.мСтрокаФормата = "; ЧДЦ = "
			+ Форма.СтруктураРеквизитовФормы.ТочностьЕдиницыИзмерения + "; ЧРД=,; ЧН=-; ЧС = 6";
	КонецЕсли;
	
	Для Каждого СтрПоказатели Из Форма.мТаблицаСоставПоказателей Цикл
		
		Если НЕ ПустаяСтрока(НаимРаздела) И НаимРаздела <> СтрПоказатели.ИмяПоляТаблДокумента Тогда
			Продолжить;
		КонецЕсли;
		
		КодПоказателя				= СокрП(СтрПоказатели.КодПоказателяПоСоставу);
		ПризнМногострочность		= СокрП(СтрПоказатели.ПризнМногострочности);
		КодПоказателяПоСтруктуре	= СокрП(СтрПоказатели.КодПоказателяПоСтруктуре);
		КодПоказателяПоФорме		= СокрП(СтрПоказатели.КодПоказателяПоФорме);
		ТипДанныхПоказателя			= СокрП(СтрПоказатели.ТипДанныхПоказателя);
		ИмяПоказателя				= ?(ПустаяСтрока(СтрПоказатели.КодПоказателяПоФорме), КодПоказателя, КодПоказателяПоФорме);
		
		Если ПустаяСтрока(КодПоказателя)
		 ИЛИ КодПоказателя = "==="
		 ИЛИ КодПоказателя = "###"
		 ИЛИ Лев(КодПоказателя, 2) = "//" Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТипДанныхПоказателя = "Е" И ПустаяСтрока(КодПоказателяПоСтруктуре) Тогда
			ТекТабличноеПоле = Форма.ПолеТабличногоДокументаФормаОтчета;
			Если ПризнМногострочность <> "/" Тогда
				ФорматируемаяОбласть = ТекТабличноеПоле.Области[ИмяПоказателя];
				ДопустимыйЗнакПоказателя = ФорматируемаяОбласть.ТипЗначения.КвалификаторыЧисла.ДопустимыйЗнак;
				ЧислоЕЗнак = Форма.СтруктураРеквизитовФормы.мОписаниеТиповЧислоЕ;
				
				Если ДопустимыйЗнакПоказателя = ДопустимыйЗнак.Неотрицательный Тогда
					ЧислоЕЗнак = Новый ОписаниеТипов("Число",
						Новый КвалификаторыЧисла(ЧислоЕЗнак.КвалификаторыЧисла.Разрядность,
							ЧислоЕЗнак.КвалификаторыЧисла.РазрядностьДробнойЧасти, ДопустимыйЗнакПоказателя));
				КонецЕсли;
				ФорматируемаяОбласть.ТипЗначения = ЧислоЕЗнак;
				ФорматируемаяОбласть.Формат = "ЧЦ = "
					+ ЧислоЕЗнак.КвалификаторыЧисла.Разрядность
					+ Форма.СтруктураРеквизитовФормы.мСтрокаФормата;
			Иначе
				НСтр = 1;
				Пока ТекТабличноеПоле.Области.Найти(ИмяПоказателя + "_" + Формат(НСтр, "ЧГ=0")) <> Неопределено Цикл
					ФорматируемаяОбласть = ТекТабличноеПоле.Области[ИмяПоказателя + "_" + Формат(НСтр, "ЧГ=0")];
					ДопустимыйЗнакПоказателя = ФорматируемаяОбласть.ТипЗначения.КвалификаторыЧисла.ДопустимыйЗнак;
					ЧислоЕЗнак = Форма.СтруктураРеквизитовФормы.мОписаниеТиповЧислоЕ;
					Если ДопустимыйЗнакПоказателя = ДопустимыйЗнак.Неотрицательный Тогда
						ЧислоЕЗнак = Новый ОписаниеТипов("Число",
							Новый КвалификаторыЧисла(ЧислоЕЗнак.КвалификаторыЧисла.Разрядность,
								ЧислоЕЗнак.КвалификаторыЧисла.РазрядностьДробнойЧасти, ДопустимыйЗнакПоказателя));
					КонецЕсли;
					ФорматируемаяОбласть.ТипЗначения = ЧислоЕЗнак;
					ФорматируемаяОбласть.Формат = "ЧЦ = "
						+ ЧислоЕЗнак.КвалификаторыЧисла.Разрядность
						+ Форма.СтруктураРеквизитовФормы.мСтрокаФормата;
					НСтр = НСтр + 1;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#Область ПанельОтправкиВКонтролирующиеОрганы

&НаКлиенте
Процедура ОбновитьОтправку(Команда)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОбновитьОтправкуИзПанелиОтправки(ЭтотОбъект, "РПН");
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаПротоколНажатие(Элемент)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьПротоколИзПанелиОтправки(ЭтотОбъект, "РПН");
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьНеотправленноеИзвещение(Команда)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОтправитьНеотправленноеИзвещениеИзПанелиОтправки(
		ЭтотОбъект, "РПН");
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыОтправкиНажатие(Элемент)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьСостояниеОтправкиИзПанелиОтправки(ЭтотОбъект, "РПН");
КонецПроцедуры

&НаКлиенте
Процедура КритическиеОшибкиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьКритическиеОшибкиИзПанелиОтправки(ЭтотОбъект, "РПН");
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаНаименованиеЭтапаНажатие(Элемент)
	
	ПараметрыИзменения = Новый Структура;
	ПараметрыИзменения.Вставить("Форма", ЭтотОбъект);
	ПараметрыИзменения.Вставить("Организация", СтруктураРеквизитовФормы.Организация);
	ПараметрыИзменения.Вставить("КонтролирующийОрган",
		ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.РПН"));
	
	РегламентированнаяОтчетностьКлиент.ИзменитьСтатусОтправки(ПараметрыИзменения);
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ОтправитьВКонтролирующийОрган(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПроверкаВозможностиВыгрузкиРегламентированногоОтчетаПередОтправкой", ЭтотОбъект);
	Отказ = Ложь;
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередОтправкойРегламентированногоОтчета(
		ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаВозможностиВыгрузкиРегламентированногоОтчетаПередОтправкой(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьВКонтролирующийОрганЗавершение", ЭтотОбъект);
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередВыгрузкойРегламентированногоОтчета(
		ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьВКонтролирующийОрганЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	РегламентированнаяОтчетностьКлиент.ПриНажатииНаКнопкуОтправкиВКонтролирующийОрган(ЭтотОбъект, "РПН");
	
КонецПроцедуры

&НаКлиенте
Процедура  ЗаписатьИЗакрыть(Команда)
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма", ЭтотОбъект);
	Оповещение = Новый ОписаниеОповещения("ПослеСохраненияФормыЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	СохранитьНаКлиенте(, Оповещение);
КонецПроцедуры

&НаКлиенте
Процедура ПослеСохраненияФормыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	ДополнительныеПараметры.Форма.Закрыть();
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция РазделТекущейОбласти(Форма, ТекущаяОбласть)

	Если ТекущаяОбласть = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ИмяТекущейОбласти = СтрЗаменить(ТекущаяОбласть.Имя, "ДобавитьСтрокуЗначок", "ДобавитьСтроку");
	Если ИмяТекущейОбласти = "ДобавитьСтроку1" Тогда
		Возврат Форма.СтруктураРеквизитовФормы.мИдГруппы1;
	ИначеЕсли ИмяТекущейОбласти = "ДобавитьСтроку2" Тогда
		Возврат Форма.СтруктураРеквизитовФормы.мИдГруппы2;
	ИначеЕсли СтрНайти(ИмяТекущейОбласти, "УдалитьСтроку") > 0 Тогда
		ИмяТекущейОбласти = СтрЗаменить(ИмяТекущейОбласти, "УдалитьСтроку", "");
	КонецЕсли;
	ИмяАктивногоРаздела = Лев(ИмяТекущейОбласти, 13);
	
	Для Каждого ЭлементСтруктуры Из Форма.мСтруктураМногострочныхРазделов Цикл
		ИмяГруппы = ЭлементСтруктуры.Ключ;
		СтруктураГруппы = Форма[ЭлементСтруктуры.Значение];
		
		Если СтруктураГруппы[0].Свойство(ИмяАктивногоРаздела) Тогда
			Возврат ИмяГруппы;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

&НаКлиенте
Процедура ПолеТабличногоДокументаФормаОтчетаПриАктивизации(Элемент)
	
	// ПроцессыОбработкиДокументов
	//
	Если Элементы.ПолеТабличногоДокументаФормаОтчета.Защита Тогда
		
		Возврат;
		
	КонецЕсли;
	//
	// ПроцессыОбработкиДокументов
	
	Элементы.ТабличныйДокументКонтекстноеМенюУдалитьСтроку.Доступность = Ложь;
	Элементы.ТабличныйДокументКонтекстноеМенюДобавитьСтроку.Доступность = Ложь;
	
	ИмяГруппы = РазделТекущейОбласти(ЭтотОбъект, Элемент.ТекущаяОбласть);
	
	Если ЗначениеЗаполнено(ИмяГруппы) Тогда 
		ТестовоеИмя = ИмяГруппы + "01_" + Строка(мСтруктураИсхКолвоСтрокРазделов[ИмяГруппы] + 1);
		Элементы.ТабличныйДокументКонтекстноеМенюДобавитьСтроку.Доступность = Истина;
		Если СтрЧислоВхождений(Элемент.ТекущаяОбласть.Имя, "ДобавитьСтроку") = 0 И
			ПолеТабличногоДокументаФормаОтчета.Области.Найти(ТестовоеИмя) <> Неопределено Тогда
			Элементы.ТабличныйДокументКонтекстноеМенюУдалитьСтроку.Доступность = Истина;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура РазрешитьРедактированиеРеквизитовОбъекта() Экспорт
	
	РегламентированнаяОтчетность.РазрешитьРедактированиеРеквизитовОтчета(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	РегламентированнаяОтчетностьКлиент.РазрешитьРедактированиеРеквизитовОтчета(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОткрытьПрисоединенныеФайлы(Команда)
	
	РегламентированнаяОтчетностьКлиент.СохранитьОтчетИОткрытьФормуПрисоединенныеФайлы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаСервере
Функция ОбъектОтчета(ЭтаФормаИмя) Экспорт
	
	Если мОбъектОтчета = Неопределено Тогда 
		мОбъектОтчета = РегламентированнаяОтчетностьВызовСервера.ОбъектОтчета(ЭтаФормаИмя);
	КонецЕсли;
	
	Возврат мОбъектОтчета;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ПроставитьКодыВЯчейки(Область, ТабДок)
	Значение = СтрЗаменить(Область.Значение, ".", "");
	Для Инд = 1 По 5 Цикл
		О1 = ТабДок.Области.Найти(Область.Имя + Инд + "_1");
		Если О1 = Неопределено Тогда 
			Возврат;
		КонецЕсли;
		Для Кол = 1 По 16 Цикл 
			О1 = ТабДок.Области.Найти(Область.Имя + Инд + "_" + Кол);
			Если О1 = Неопределено Тогда 
				Прервать;
			КонецЕсли;
			О1.Значение = Сред(Значение, Кол, 1);
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

#Область ОбработчикиСобытийФормы

#Область ОбработчикиСобытийПроцессыОбработкиДокументов

&НаКлиенте
Процедура Подключаемый_ВыполнитьЗадачуПоОбработкеДокументаОповещение(Контекст, ДополнительныеПараметры) Экспорт
	
	ВыполнитьЗадачуПоОбработкеДокументаНаСервере(Контекст);
		
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры, Контекст);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьЗадачуПоОбработкеДокумента(Команда)
	
	РегламентированнаяОтчетностьКлиент.ВыполнитьЗадачу(
		ЭтотОбъект,
		Команда,
		?(ЗначениеЗаполнено(СтруктураРеквизитовФормы.мСохраненныйДок),
			СтруктураРеквизитовФормы.мСохраненныйДок,
			СтруктураРеквизитовФормы.ПустаяСсылкаНаРегламентированныйОтчет));
	
КонецПроцедуры

#КонецОбласти

#Область ПроцессыОбработкиДокументов

&НаКлиенте
Процедура Подключаемый_КомментарийНаправившегоОткрытие(Элемент, СтандартнаяОбработка)
	
	РегламентированнаяОтчетностьКлиент.КомментарийНаправившегоОткрытие(ЭтотОбъект, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КомментарийСледующемуНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РегламентированнаяОтчетностьКлиент.КомментарийСледующемуНачалоВыбора(
		ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПроцессыОбработкиДокументов

&НаКлиенте
Процедура Записать(ПараметрыЗаписи) Экспорт
	
	СохранитьНаКлиенте();
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьЗадачуПоОбработкеДокументаНаСервере(Контекст)
	
	РегламентированнаяОтчетность.ВыполнитьЗадачу(ЭтотОбъект, Контекст, СтруктураРеквизитовФормы.мСохраненныйДок);
	
КонецПроцедуры

&НаСервере
Процедура УправлениеДоступностьюРеквизитов(БлокируемыеЭлементы, ТолькоПросмотр) Экспорт
	
	РегламентированнаяОтчетность.УправлениеДоступностьюРеквизитов(ЭтотОбъект, БлокируемыеЭлементы, ТолькоПросмотр);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

&НаКлиенте
Процедура ДоступностьКнопокПрисоединенныеФайлы()
	ТекущиеДанные = Элементы.ПриложенияКОтчету.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Элементы.ПриложенияКОтчетуУдалить.Доступность = Ложь;
		Элементы.ПриложенияКОтчетуДобавитьФайл.Доступность = Ложь;
	Иначе
		Элементы.ПриложенияКОтчетуУдалить.Доступность = Истина;
		Элементы.ПриложенияКОтчетуДобавитьФайл.Доступность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция УровеньГруппировки(ТекущиеДанные)
	Возврат ?(ТекущиеДанные.ПолучитьРодителя() = Неопределено, 0, 1 + УровеньГруппировки(ТекущиеДанные.ПолучитьРодителя()));
КонецФункции

&НаКлиенте
Процедура ДобавитьДокумент(Команда)
	ПараметрыВводаРеквизитов = Новый Структура("Добавление, Редактирование, ТекущиеДанные", Истина, Ложь, Неопределено);
	ОписаниеОповещения = Новый ОписаниеОповещения("ДобавитьДокументЗавершение", ЭтотОбъект, ПараметрыВводаРеквизитов);
	
	ФормаВводаРеквизитовДокумента = ПолучитьФорму("ОбщаяФорма.ФормаВводаРеквизитовДокументаРПН", ПараметрыВводаРеквизитов, ЭтотОбъект);
	ФормаВводаРеквизитовДокумента.ОписаниеОповещенияОЗакрытии = ОписаниеОповещения;
	ФормаВводаРеквизитовДокумента.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ФормаВводаРеквизитовДокумента.Открыть();
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьДокументЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		Если ДополнительныеПараметры.Добавление Тогда 
			ЗаполнитьЗначенияСвойств(_Приложения.ПолучитьЭлементы().Добавить(), Результат);
		ИначеЕсли ДополнительныеПараметры.Редактирование Тогда
			ЗаполнитьЗначенияСвойств(ДополнительныеПараметры.ТекущиеДанные, Результат);
		КонецЕсли;
		
		Модифицированность = Истина;
	КонецЕсли;
	ДоступностьКнопокПрисоединенныеФайлы();
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьФайл(Команда)
	ТекущиеДанные = Элементы.ПриложенияКОтчету.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если УровеньГруппировки(ТекущиеДанные) = 1 Тогда
		ТекущиеДанные = ТекущиеДанные.ПолучитьРодителя();
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СтруктураРеквизитовФормы.мСохраненныйДок) Или Модифицированность Тогда 
		ТекстВопроса = "Перед добавлением приложения необходимо сохранить отчет. Продолжить?";
		ОО = Новый ОписаниеОповещения("ВопросФайлЗавершение", ЭтотОбъект, Новый Структура("ТекущиеДанные", ТекущиеДанные));
		ПоказатьВопрос(ОО, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
	Иначе
		ВопросФайлЗавершение(КодВозвратаДиалога.Да, Новый Структура("ТекущиеДанные", ТекущиеДанные));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВопросФайлЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Если Модифицированность Или Не ЗначениеЗаполнено(СтруктураРеквизитовФормы.мСохраненныйДок) Тогда
			ОО = Новый ОписаниеОповещения("ПослеСохраненияФайлЗавершениеЗавершение", ЭтотОбъект, ДополнительныеПараметры);
			СохранитьНаКлиенте(, ОО);
		Иначе
			ПослеСохраненияФайлЗавершениеЗавершение(Неопределено, ДополнительныеПараметры);
		КонецЕсли;
	Иначе
		Возврат;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеСохраненияФайлЗавершениеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	АдресФайла  = "";
	ВыбИмяФайла = "";
	ОО = Новый ОписаниеОповещения("ДобавитьФайлЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	Попытка
		НачатьПомещениеФайла(ОО, АдресФайла, ВыбИмяФайла, Истина, УникальныйИдентификатор);
	Исключение
		ШаблонСообщения = НСтр("ru = 'При загрузке файла возникла ошибка.
									 |%1';
									 |en = 'При загрузке файла возникла ошибка.
									 |%1'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонСообщения, КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		ПоказатьПредупреждение(, ТекстСообщения, , НСтр("ru = 'Ошибка';
														|en = 'Ошибка'"));
	КонецПопытки;
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьФайлЗавершение(Результат, АдресФайла, ВыбИмяФайла, ДополнительныеПараметры) Экспорт
	Если НЕ Результат Тогда
		Возврат;
	КонецЕсли;
	
	ИмяФайлаНРЕГ = НРег(ВыбИмяФайла);
	Если Не СтрЗаканчиваетсяНа(ИмяФайлаНРЕГ, ".jpeg")
		И Не СтрЗаканчиваетсяНа(ИмяФайлаНРЕГ, ".jpg")
		И Не СтрЗаканчиваетсяНа(ИмяФайлаНРЕГ, ".jpe")
		И Не СтрЗаканчиваетсяНа(ИмяФайлаНРЕГ, ".png")
		И Не СтрЗаканчиваетсяНа(ИмяФайлаНРЕГ, ".pdf")
		И Не СтрЗаканчиваетсяНа(ИмяФайлаНРЕГ, ".tif")
		И Не СтрЗаканчиваетсяНа(ИмяФайлаНРЕГ, ".tiff")
		И Не СтрЗаканчиваетсяНа(ИмяФайлаНРЕГ, ".doc")
		И Не СтрЗаканчиваетсяНа(ИмяФайлаНРЕГ, ".docx")
		И Не СтрЗаканчиваетсяНа(ИмяФайлаНРЕГ, ".docm")
		И Не СтрЗаканчиваетсяНа(ИмяФайлаНРЕГ, ".xls")
		И Не СтрЗаканчиваетсяНа(ИмяФайлаНРЕГ, ".xlsx")
		И Не СтрЗаканчиваетсяНа(ИмяФайлаНРЕГ, ".xlsm")
		И Не СтрЗаканчиваетсяНа(ИмяФайлаНРЕГ, ".7z")
		И Не СтрЗаканчиваетсяНа(ИмяФайлаНРЕГ, ".zip")
		И Не СтрЗаканчиваетсяНа(ИмяФайлаНРЕГ, ".rar") Тогда 
		
		ОбщегоНазначенияКлиент.СообщитьПользователю("Файл должен иметь одно из расширений: .jpeg, .jpg, .jpe, .png, .pdf, "
			+ ".tif, .tiff, .doc, .docx, .docm, .xls, .xlsx, .xlsm, .7z, .zip, .rar");
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("СтрокаПоиска", ВыбИмяФайла);
	ДополнительныеПараметры.Вставить("АдресФайла", АдресФайла);
	ОО = Новый ОписаниеОповещения("ПодписатьФайлГОСТ2012Завершение", ЭтотОбъект, ДополнительныеПараметры);
	МестоХраненияКлюча = КриптографияЭДКОКлиентСервер.КонтекстМоделиХраненияКлюча(СтруктураРеквизитовФормы.Организация);
	КриптографияЭДКОКлиент.ХешироватьДанные(ОО, МестоХраненияКлюча, АдресФайла,, Истина,, "GOST R 34.10-2012-256", Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПодписатьФайлГОСТ2012Завершение(Результат, ДополнительныеПараметры) Экспорт
	Если ТипЗнч(Результат) = Тип("Структура") и Результат.Свойство("Хеш") Тогда 
		НовСтрКомментарий = НРег(Результат.Хеш);
	Иначе
		НовСтрКомментарий = "";
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(НовСтрКомментарий) Тогда 
		Возврат;
	КонецЕсли;
	НовСтр = ДополнительныеПараметры.ТекущиеДанные.ПолучитьЭлементы().Добавить();
	НовСтр.Комментарий = НовСтрКомментарий;
	
	Каталог = "";
	СтрокаПоиска = ДополнительныеПараметры.СтрокаПоиска;
	АдресФайла = ДополнительныеПараметры.АдресФайла;

	РазделительПути = ПолучитьРазделительПути();
	Пока СтрДлина(СтрокаПоиска) > 0 Цикл
		Если Прав(СтрокаПоиска, 1) = РазделительПути Тогда
			Каталог = Лев(СтрокаПоиска, СтрДлина(СтрокаПоиска));
			Прервать;
		Иначе
			СтрокаПоиска = Лев(СтрокаПоиска, СтрДлина(СтрокаПоиска) - 1);
		КонецЕсли;
	КонецЦикла;
	
	Попытка
		ЗаполнитьСтруктуруДанныхПриложения(АдресФайла, ДополнительныеПараметры.СтрокаПоиска, Каталог, НовСтр.ПолучитьИдентификатор());
		Элементы.ПриложенияКОтчету.Развернуть(ДополнительныеПараметры.ТекущиеДанные.ПолучитьИдентификатор());
		ДоступностьКнопокПрисоединенныеФайлы();
	Исключение
		ШаблонСообщения = НСтр("ru = 'При загрузке файла возникла ошибка.
									 |%1';
									 |en = 'При загрузке файла возникла ошибка.
									 |%1'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонСообщения, КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		ПоказатьПредупреждение(, ТекстСообщения, , НСтр("ru = 'Ошибка';
														|en = 'Ошибка'"));
		Возврат;
	КонецПопытки;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтруктуруДанныхПриложения(АдресФайла, ПолноеИмяФайла, Каталог, НовСтр) Экспорт 
	НовСтрСтр = _Приложения.НайтиПоИдентификатору(НовСтр);
	МодульРаботаСФайлами = ОбщегоНазначения.ОбщийМодуль("РаботаСФайлами");
	ИмяФайла = СтрЗаменить(ПолноеИмяФайла, Каталог, "");
	ИмяБезРасширения = Лев(ИмяФайла, СтрНайти(ИмяФайла, ".", НаправлениеПоиска.СКонца) - 1);
	ПараметрыФайла = Новый Структура;
	ПараметрыФайла.Вставить("ВладелецФайлов", СтруктураРеквизитовФормы.мСохраненныйДок);
	ПараметрыФайла.Вставить("Автор", Неопределено);
	ПараметрыФайла.Вставить("ИмяБезРасширения", ИмяБезРасширения);
	ПараметрыФайла.Вставить("РасширениеБезТочки", СтрЗаменить(ИмяФайла, ИмяБезРасширения + ".", ""));
	ПараметрыФайла.Вставить("ВремяИзменения", Неопределено);
	ПараметрыФайла.Вставить("ВремяИзмененияУниверсальное", Неопределено);
	ПараметрыФайла.Вставить("Служебный", Истина);
	ПрисоединенныйФайл = МодульРаботаСФайлами.ДобавитьФайл(ПараметрыФайла, АдресФайла, , "Файл создан автоматически из формы уведомления, редактирование запрещено.");
	ПоказателиОтчета = Новый Структура;
	ЗаполнитьПоказателиОтчетаНаСервере(ПоказателиОтчета);
	
	НовСтрСтр.ПрисоединенныйФайл = ПрисоединенныйФайл;
	НовСтрСтр.ОписаниеДокумента = ИмяФайла;
	НовСтрСтр.Название = СтрЗаменить(ИмяФайла, ИмяБезРасширения + ".", "");
	Сохранить(,,,,ПоказателиОтчета);
КонецПроцедуры

&НаКлиенте
Процедура Удалить(Команда)
	ТекущиеДанные = Элементы.ПриложенияКОтчету.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	УдалитьПрисоединенныйФайл(ТекущиеДанные.ПолучитьИдентификатор());
	ДоступностьКнопокПрисоединенныеФайлы();
КонецПроцедуры

&НаСервере
Процедура УдалитьПрисоединенныйФайл(ИдВДереве)
	ТекущиеДанные = _Приложения.НайтиПоИдентификатору(ИдВДереве);
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ПравоДоступаУдаление = ПравоДоступа("Удаление", Метаданные.Справочники.РегламентированныйОтчетПрисоединенныеФайлы);
	Если УровеньГруппировки(ТекущиеДанные) = 0 Тогда
		Для Каждого Стр Из ТекущиеДанные.ПолучитьЭлементы() Цикл
			ПрисоединенныйФайлОбъект = Стр.ПрисоединенныйФайл.ПолучитьОбъект();
			Если ПравоДоступаУдаление Тогда 
				ПрисоединенныйФайлОбъект.Удалить();
			Иначе
				ПрисоединенныйФайлОбъект.ПометкаУдаления = Истина;
				ПрисоединенныйФайлОбъект.Записать();
			КонецЕсли;
		КонецЦикла;
		_Приложения.ПолучитьЭлементы().Удалить(ТекущиеДанные);
	Иначе
		ПрисоединенныйФайлОбъект = ТекущиеДанные.ПрисоединенныйФайл.ПолучитьОбъект();
		Если ПравоДоступаУдаление Тогда 
			ПрисоединенныйФайлОбъект.Удалить();
		Иначе
			ПрисоединенныйФайлОбъект.ПометкаУдаления = Истина;
			ПрисоединенныйФайлОбъект.Записать();
		КонецЕсли;
		ТекущиеДанные.ПолучитьРодителя().ПолучитьЭлементы().Удалить(ТекущиеДанные);
	КонецЕсли;
	
	ПоказателиОтчета = Новый Структура;
	ЗаполнитьПоказателиОтчетаНаСервере(ПоказателиОтчета);
	Сохранить(,,,,ПоказателиОтчета);
КонецПроцедуры

&НаКлиенте
Процедура ПриложенияКОтчетуВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ТекущиеДанные = _Приложения.НайтиПоИдентификатору(ВыбраннаяСтрока);
	Если УровеньГруппировки(ТекущиеДанные) = 0 Тогда
		ПараметрыВводаРеквизитов = Новый Структура("Добавление, Редактирование, ТекущиеДанные", Ложь, Истина, ТекущиеДанные);
		ПараметрыОткрытияФормы = Новый Структура;
		ПараметрыОткрытияФормы.Вставить("Номер", ТекущиеДанные.Номер);
		ПараметрыОткрытияФормы.Вставить("Название", ТекущиеДанные.Название);
		ПараметрыОткрытияФормы.Вставить("Комментарий", ТекущиеДанные.Комментарий);
		ПараметрыОткрытияФормы.Вставить("Дата", ТекущиеДанные.Дата);
		ОписаниеОповещения = Новый ОписаниеОповещения("ДобавитьДокументЗавершение", ЭтотОбъект, ПараметрыВводаРеквизитов);
		
		ФормаВводаРеквизитовДокумента = ПолучитьФорму("ОбщаяФорма.ФормаВводаРеквизитовДокументаРПН", ПараметрыОткрытияФормы, ЭтотОбъект);
		ФормаВводаРеквизитовДокумента.ОписаниеОповещенияОЗакрытии = ОписаниеОповещения;
		ФормаВводаРеквизитовДокумента.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		ФормаВводаРеквизитовДокумента.Открыть();
	Иначе
		РаботаСФайламиКлиент.ОткрытьФайл(ДанныеФайла(ТекущиеДанные.ПрисоединенныйФайл), Ложь);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриложенияКОтчетуПриАктивизацииСтроки(Элемент)
	ДоступностьКнопокПрисоединенныеФайлы();
КонецПроцедуры

&НаСервере
Функция ДанныеФайла(ПрисоединенныйФайл)
	Возврат РаботаСФайлами.ДанныеФайла(ПрисоединенныйФайл);
КонецФункции
