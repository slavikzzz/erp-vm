
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция НайтиГруппировку(Структура, Имя)
	
	Для каждого Элемент Из Структура Цикл
		
		Для каждого Поле Из Элемент.ПоляГруппировки.Элементы Цикл
			Если Не ТипЗнч(Поле) = Тип("АвтоПолеГруппировкиКомпоновкиДанных") Тогда
				Если Поле.Поле = Новый ПолеКомпоновкиДанных(Имя) Тогда
					Возврат Элемент;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Если Элемент.Структура.Количество() = 0 Тогда
			Возврат Неопределено;
		Иначе
			Группировка = НайтиГруппировку(Элемент.Структура, Имя);
		КонецЕсли;
	КонецЦикла;
	Возврат Группировка;
	
КонецФункции

Процедура ДобавитьВыбранноеПолеИлиГруппу(Поле,Выбор)
	
	Если ТипЗнч(Поле) = Тип("ВыбранноеПолеКомпоновкиДанных") Тогда
		ВыборГруппировки =  Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
		ЗаполнитьЗначенияСвойств(ВыборГруппировки,Поле);
	ИначеЕсли ТипЗнч(Поле) = Тип("ГруппаВыбранныхПолейКомпоновкиДанных") Тогда
		ВыборГруппировки =  Выбор.Элементы.Добавить(Тип("ГруппаВыбранныхПолейКомпоновкиДанных"));
		ЗаполнитьЗначенияСвойств(ВыборГруппировки,Поле);
		Для Каждого ПолеГруппы Из Поле.Элементы  Цикл
			ДобавитьВыбранноеПолеИлиГруппу(ПолеГруппы,ВыборГруппировки);
		КонецЦикла;	
	КонецЕсли;
	
КонецПроцедуры

// Функция возвращает параметры отчета, см. БухгалтерскиеОтчетыВызовСервера.СформироватьОтчет
// Возвращаемое значение:
//	Структура
//
Функция ПолучитьПараметрыИсполненияОтчета() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ИспользоватьПередКомпоновкойМакета", Истина);
	Результат.Вставить("ИспользоватьПослеВыводаРезультата",  Истина);
	Результат.Вставить("ИспользоватьДанныеРасшифровки",      Истина);
	Результат.Вставить("ИспользоватьВнешниеНаборыДанных",    Истина);
	Результат.Вставить("ИспользоватьПривилегированныйРежим", Истина);
	
	Возврат Результат;
													
КонецФункции

// Функция возвращает параметры отчета, см. БухгалтерскиеОтчетыВызовСервера.СформироватьОтчет
// Возвращаемое значение:
//	Структура
//
Функция ПолучитьВнешниеНаборыДанных(ПараметрыОтчета, МакетКомпоновки) Экспорт
	
	СписокДоступныхОрганизаций = ОбщегоНазначенияБПВызовСервераПовтИсп.ВсеОрганизацииДанныеКоторыхДоступныПоRLS(Ложь);
	Если ЗначениеЗаполнено(ПараметрыОтчета.Организация) И СписокДоступныхОрганизаций.Найти(ПараметрыОтчета.Организация) = Неопределено Тогда
		ВызватьИсключение НСтр("ru = 'Нет доступа к данным по организации для получения внешнего набора данных';
								|en = 'No access to data by company to receive an external data set'");
	КонецЕсли;
	
	Настройки = ПараметрыОтчета.НастройкиКомпоновкиДанных;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВерсияУведомления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрыОтчета.Уведомление, "ВерсияУведомления");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Документы.УведомлениеОКонтролируемыхСделках.ПолучитьВременныеТаблицыДляЗаполненияУведомления(ПараметрыОтчета.Уведомление);
	
	Если ВерсияУведомления >= КонтролируемыеСделкиКлиентСервер.ВерсияУведомления_2019() Тогда
		Запрос.Текст = ТекстЗапросаКонтролируемыхСделок2019();
	Иначе
		Запрос.Текст = ТекстЗапросаКонтролируемыхСделок2012();
	КонецЕсли;
	
	СделкиУведомления = Запрос.Выполнить().Выгрузить();
	
	ДополнитьТаблицуПричинамиВключенияВУведомление(СделкиУведомления, ВерсияУведомления);
	
	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить("КонтролируемыеСделки", СделкиУведомления);
	
	Возврат ВнешниеНаборыДанных;	
КонецФункции

// Функция возвращает заголовок отчета, см. БухгалтерскиеОтчетыВызовСервера.ВывестиЗаголовокОтчета
// Возвращаемое значение:
//	Строка
//
Функция ПолучитьТекстЗаголовка(ПараметрыОтчета, ОрганизацияВНачале = Истина) Экспорт 
	
	Возврат НСтр("ru = 'Контролируемые сделки для включения в уведомление';
				|en = 'Controlled transactions to include in the notification'");
	
КонецФункции

// В процедуре можно доработать компоновщик перед выводом в отчет
// Изменения сохранены не будут.
Процедура ПередКомпоновкойМакета(ПараметрыОтчета, Схема, КомпоновщикНастроек) Экспорт
	
	ПредопределенныеНастройки = ПараметрыОтчета.СхемаКомпоновкиДанных.ВариантыНастроек[0].Настройки;
	
	КомпоновщикНастроек.Настройки.Структура.Очистить();
	КомпоновщикНастроек.Настройки.Выбор.Элементы.Очистить();
		
	Таблица = КомпоновщикНастроек.Настройки.Структура.Добавить(Тип("ТаблицаКомпоновкиДанных"));
		
	КолонкаСумма = Таблица.Колонки.Добавить();
	КолонкаСумма.Имя           = "Сумма";
	КолонкаСумма.Использование = Истина;
	БухгалтерскиеОтчетыКлиентСервер.ДобавитьВыбранноеПоле(КолонкаСумма.Выбор, "СуммаБезНДСВРублях");			
	
	Структура =  Новый Структура("Структура",Таблица.Строки);	
	
	КоличествоГруппировок = 0;
	Для Каждого ПолеВыбраннойГруппировки Из ПараметрыОтчета.Группировка Цикл 
		Если ПолеВыбраннойГруппировки.Использование Тогда
			Структура = Структура.Структура.Добавить();
			
			ПолеГруппировки = Структура.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
			ПолеГруппировки.Использование  = Истина;
			ПолеГруппировки.Поле           = Новый ПолеКомпоновкиДанных(ПолеВыбраннойГруппировки.Поле);
			
			Если ПолеВыбраннойГруппировки.ТипГруппировки = 1 Тогда
				ПолеГруппировки.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.Иерархия;
			ИначеЕсли ПолеВыбраннойГруппировки.ТипГруппировки = 2 Тогда
				ПолеГруппировки.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.ТолькоИерархия;
			Иначе
				ПолеГруппировки.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.Элементы;
			КонецЕсли;
			
			Структура.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
			ПолеСуммы = Структура.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
			
			
			Структура.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
						
			КоличествоГруппировок = КоличествоГруппировок + 1;
		КонецЕсли;
	КонецЦикла;
	
	// Добавляем предопределенные недоступные группировки (всегда в конце)
	
	ПредопределеннаяГруппировка = ПредопределенныеНастройки.Структура[0].Строки;
	Пока ПредопределеннаяГруппировка.Количество() > 0 Цикл
		
		Если ПредопределеннаяГруппировка[0].ПоляГруппировки.Элементы.Количество() > 0 Тогда
			
			Если ПредопределеннаяГруппировка[0].РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный И ПредопределеннаяГруппировка[0].Состояние = СостояниеЭлементаНастройкиКомпоновкиДанных.Включен Тогда
				Структура = Структура.Структура.Добавить();
				
				// Поля группировок
				Для Каждого ПолеПредопределеннойГруппировки Из ПредопределеннаяГруппировка[0].ПоляГруппировки.Элементы Цикл
					Если ТипЗнч(ПолеПредопределеннойГруппировки) = Тип("АвтоПолеГруппировкиКомпоновкиДанных") Тогда
						ПолеГруппировки = Структура.ПоляГруппировки.Элементы.Добавить(Тип("АвтоПолеГруппировкиКомпоновкиДанных"));
					Иначе
						Если НайтиГруппировку(Таблица.Строки, ПолеПредопределеннойГруппировки.Поле) = Неопределено Тогда
							ПолеГруппировки = Структура.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
							ЗаполнитьЗначенияСвойств(ПолеГруппировки,ПолеПредопределеннойГруппировки);
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;	
				
				//Отборы
				Для Каждого ОтборПредопределеннойГруппировки Из ПредопределеннаяГруппировка[0].Отбор.Элементы Цикл
					ОтборГруппировки =  Структура.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
					ЗаполнитьЗначенияСвойств(ОтборГруппировки,ОтборПредопределеннойГруппировки);
				КонецЦикла;	
				
				// Условное оформление
				Для Каждого УсловноеОформлениеПредопределеннойГруппировки Из ПредопределеннаяГруппировка[0].УсловноеОформление.Элементы Цикл
					УсловноеОформлениеГруппировки =  Структура.УсловноеОформление.Элементы.Добавить();
					ЗаполнитьЗначенияСвойств(УсловноеОформлениеГруппировки,УсловноеОформлениеПредопределеннойГруппировки);
					
					//Отбор
					Для Каждого ОтборПредопределенногоОформления Из УсловноеОформлениеПредопределеннойГруппировки.Отбор.Элементы Цикл
						ОтборОформления =  УсловноеОформлениеГруппировки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
						ЗаполнитьЗначенияСвойств(ОтборОформления,ОтборПредопределенногоОформления);
					КонецЦикла;	
					
					// Параметры оформления
					Для ИндексПараметра = 0 По УсловноеОформлениеПредопределеннойГруппировки.Оформление.Элементы.Количество() - 1 Цикл
						ОформлениеПредопределеннаяГруппировка = УсловноеОформлениеПредопределеннойГруппировки.Оформление.Элементы[ИндексПараметра];
						ОформлениеГруппировки =  УсловноеОформлениеГруппировки.Оформление.Элементы[ИндексПараметра];
						ЗаполнитьЗначенияСвойств(ОформлениеГруппировки,ОформлениеПредопределеннаяГруппировка);
					КонецЦикла;	
					
					Для Каждого ПредопределенноеОформляемоеПолеГруппировки Из УсловноеОформлениеПредопределеннойГруппировки.Поля.Элементы Цикл
						ОформляемоеПоле = УсловноеОформлениеГруппировки.Поля.Элементы.Добавить();
						ЗаполнитьЗначенияСвойств(ОформляемоеПоле,ПредопределенноеОформляемоеПолеГруппировки);
					КонецЦикла;	
					
				КонецЦикла;	
				
				//Выбор
				Для Каждого ВыборПредопределеннойГруппировки Из ПредопределеннаяГруппировка[0].Выбор.Элементы Цикл
					Если ТипЗнч(ВыборПредопределеннойГруппировки) = Тип("АвтоВыбранноеПолеКомпоновкиДанных") Тогда
						Структура.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));	
					Иначе
						Если НайтиГруппировку(Таблица.Строки, ВыборПредопределеннойГруппировки.Поле) = Неопределено Тогда
							ДобавитьВыбранноеПолеИлиГруппу(ВыборПредопределеннойГруппировки,Структура.Выбор);	
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;	
				
				//Порядок
				Для Каждого ПорядокПредопределеннойГруппировки Из ПредопределеннаяГруппировка[0].Порядок.Элементы Цикл
					Если ТипЗнч(ПорядокПредопределеннойГруппировки) = Тип("АвтоЭлементПорядкаКомпоновкиДанных") Тогда
						Структура.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));	
					Иначе	
						ПорядокГруппировки =  Структура.Порядок.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
						ЗаполнитьЗначенияСвойств(ПорядокГруппировки,ПорядокПредопределеннойГруппировки);
					КонецЕсли;
				КонецЦикла;	
				
				// Параметры вывода
				Для ИндексПараметра = 0 По ПредопределеннаяГруппировка[0].ПараметрыВывода.Элементы.Количество() - 1 Цикл
					ПараметрыВыводаПредопределеннаяГруппировка = ПредопределеннаяГруппировка[0].ПараметрыВывода.Элементы[ИндексПараметра];
					ПараметрыВыводаГруппировки =  Структура.ПараметрыВывода.Элементы[ИндексПараметра];
					ЗаполнитьЗначенияСвойств(ПараметрыВыводаГруппировки,ПараметрыВыводаПредопределеннаяГруппировка);
				КонецЦикла;	
				
			КонецЕсли;
		КонецЕсли;
		
		ПредопределеннаяГруппировка = ПредопределеннаяГруппировка[0].Структура;
		
	КонецЦикла;
	
	// Дополнительные данные
	БухгалтерскиеОтчетыВызовСервера.ДобавитьДополнительныеПоля(ПараметрыОтчета, КомпоновщикНастроек);
					
КонецПроцедуры

// Процедура выполняет обработку результата отчета, см. БухгалтерскиеОтчетыВызовСервера.СформироватьОтчет.
//
Процедура ПослеВыводаРезультата(ПараметрыОтчета, Результат) Экспорт
	
	БухгалтерскиеОтчетыВызовСервера.ОбработкаРезультатаОтчета(ПараметрыОтчета.ИдентификаторОтчета, Результат);
	
	Если Результат.Области.Найти("Заголовок") = Неопределено Тогда
		Результат.ФиксацияСверху = 0;
	Иначе
		Результат.ФиксацияСверху = Результат.Области.Заголовок.Низ;
	КонецЕсли;
	Результат.ФиксацияСлева = 0;
	
КонецПроцедуры

// Функция возвращает показатели отчета, см. БухгалтерскиеОтчетыВызовСервера.ОбработкаПроверкиЗаполнения
// Возвращаемое значение:
//	Массив - массив значений строкового типа.
//
Функция ПолучитьНаборПоказателей() Экспорт
	
	НаборПоказателей = Новый Массив;
	НаборПоказателей.Добавить("БУ");
	НаборПоказателей.Добавить("НУ");
	НаборПоказателей.Добавить("ПР");
	НаборПоказателей.Добавить("ВР");
	
	Возврат НаборПоказателей;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаКонтролируемыхСделок2012()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	КонтролируемыеСделки.Организация КАК Организация,
	|	КонтролируемыеСделки.ОтчетныйГод КАК ОтчетныйГод,
	|	КонтролируемыеСделки.ГоловнойКонтрагент КАК Контрагент,
	|	КонтролируемыеСделки.Контрагент КАК ОбособленноеПодразделениеКонтрагента,
	|	КонтролируемыеСделки.Договор КАК Договор,
	|	КонтролируемыеСделки.ПредметСделки КАК ПредметСделки,
	|	КонтролируемыеСделки.СделкаВнешнейТорговлиТоваромМировойБиржевойТорговли КАК СделкаВнешнейТорговлиТоваромМировойБиржевойТорговли,
	|	СУММА(КонтролируемыеСделки.СуммаБезНДСВРублях) КАК СуммаБезНДСВРублях,
	|	КонтролируемыеСделки.КонтрагентЗарегистрированВРФ КАК КонтрагентЗарегистрированВРФ,
	|	КонтролируемыеСделки.ЗарегистрированВСтранеСЛьготнымНалогообложением КАК ЗарегистрированВСтранеСЛьготнымНалогообложением,
	|	КонтролируемыеСделки.ТипВзаимозависимости КАК ТипВзаимозависимости,
	|	КонтролируемыеСделки.СделкаОблагаетсяЕНВД КАК СделкаОблагаетсяЕНВД,
	|	КонтролируемыеСделки.СделкаКонтролируетсяПоРазнымСтавкамНалогаНаПрибыль КАК СделкаКонтролируетсяПоРазнымСтавкамНалогаНаПрибыль,
	|	КонтролируемыеСделки.СделкаСПлательщикомЕСХН КАК СделкаСПлательщикомЕСХН,
	|	КонтролируемыеСделки.СделкаКонтролируетсяПоНалогуНаПрибыль КАК СделкаКонтролируетсяПоНалогуНаПрибыль,
	|	КонтролируемыеСделки.СделкаКонтролируетсяПоНДПИ КАК СделкаКонтролируетсяПоНДПИ,
	|	КонтролируемыеСделки.СделкаМорскогоМесторождения КАК СделкаМорскогоМесторождения,
	|	КонтролируемыеСделки.СделкаКонтролируетсяПоИнвестиционнымПроектам КАК СделкаКонтролируетсяПоИнвестиционнымПроектам,
	|	КонтролируемыеСделки.СделкаКонтролируетсяПоОсвобождениюНДС КАК СделкаКонтролируетсяПоОсвобождениюНДС,
	|	КонтролируемыеСделки.СделкаКонтролируетсяПоИнвестиционномуВычету КАК СделкаКонтролируетсяПоИнвестиционномуВычету,
	|	КонтролируемыеСделки.СделкаКонтролируетсяПоДополнительномуДоходуОтСырья КАК СделкаКонтролируетсяПоДополнительномуДоходуОтСырья,
	|	ВЫБОР
	|		КОГДА КонтролируемыеСделки.ТипПредметаСделки = ЗНАЧЕНИЕ(Перечисление.ТипыПредметовКонтролируемыхСделок.Товар)
	|			ТОГДА ЕСТЬNULL(ДоговорыУчастниковКонтролируемыхСделок.КодУсловийПоставки, """")
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК КодУсловийПоставки,
	|	ЕСТЬNULL(ДоговорыУчастниковКонтролируемыхСделок.СпособОпределенияЦеныСделки, ЗНАЧЕНИЕ(Справочник.СпособыОпределенияЦенКонтролируемыхСделок.ПустаяСсылка)) КАК СпособОпределенияЦеныСделки,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ДоговорыУчастниковКонтролируемыхСделок.КодНаименованияСделки, """") = """"
	|			ТОГДА ВЫБОР
	|					КОГДА КонтролируемыеСделки.ТипПредметаСделки = ЗНАЧЕНИЕ(Перечисление.ТипыПредметовКонтролируемыхСделок.РаботаУслуга)
	|						ТОГДА ""019""
	|					ИНАЧЕ ""015""
	|				КОНЕЦ
	|		ИНАЧЕ ЕСТЬNULL(ДоговорыУчастниковКонтролируемыхСделок.КодНаименованияСделки, """")
	|	КОНЕЦ КАК КодНаименованияСделки,
	|	КонтролируемыеСделки.СделкаКонтролируетсяПоРегистрацииВОЭЗ КАК СделкаКонтролируетсяПоРегистрацииВОЭЗ,
	|	КонтролируемыеСделки.СтороныВзаимозависимыПоКодексу КАК СтороныВзаимозависимыПоКодексу,
	|	КонтролируемыеСделки.СделкаНеЯвляетсяКонтролируемойПоП4 КАК СделкаНеЯвляетсяКонтролируемойПоП4,
	|	КонтролируемыеСделки.ТипВзаимозависимости = ЗНАЧЕНИЕ(Перечисление.ТипыВзаимозависимости.НеВзаимозависимыйПосредник) КАК НеВзаимозависимыйПосредник,
	|	ВЫБОР
	|		КОГДА НЕ КонтрагентыПоМинимальнойСумме.ГоловнойКонтрагент ЕСТЬ NULL
	|				И (НЕ КонтрагентыКонтролируемыеКакПрочиеЗависимыеЛица.ГоловнойКонтрагент ЕСТЬ NULL
	|					ИЛИ НЕ КонтрагентыКонтролируемыеПоОбщейСумме.ГоловнойКонтрагент ЕСТЬ NULL
	|					ИЛИ НЕ КонтрагентыКонтролируемыеПоНДПИ.ГоловнойКонтрагент ЕСТЬ NULL
	|					ИЛИ НЕ КонтрагентыКонтролируемыеПоСпецРежиму.ГоловнойКонтрагент ЕСТЬ NULL
	|					ИЛИ НЕ КонтрагентыКонтролируемыеПоПрибыли.ГоловнойКонтрагент ЕСТЬ NULL
	|					ИЛИ НЕ КонтрагентыКонтролируемыеПоОЭЗ.ГоловнойКонтрагент ЕСТЬ NULL
	|					ИЛИ НЕ КонтрагентыКонтролируемыеПоРегистрацииНеВРФ.ГоловнойКонтрагент ЕСТЬ NULL
	|					ИЛИ НЕ КонтрагентыКонтролируемыеПоМорскомуМесторождению.ГоловнойКонтрагент ЕСТЬ NULL
	|					ИЛИ НЕ КонтрагентыКонтролируемыеПоИнвестиционнымПроектам.ГоловнойКонтрагент ЕСТЬ NULL
	|					ИЛИ НЕ КонтрагентыКонтролируемыеПоОсвобождениюНДС.ГоловнойКонтрагент ЕСТЬ NULL
	|					ИЛИ НЕ КонтрагентыКонтролируемыеПоРегистрацииНеВРФ.ГоловнойКонтрагент ЕСТЬ NULL
	|					ИЛИ НЕ КонтрагентыКонтролируемыеПоИнвестиционномуВычету.ГоловнойКонтрагент ЕСТЬ NULL)
	|				И НЕ КонтролируемыеСделки.СделкаНеЯвляетсяКонтролируемойПоП4
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СделкаПопалаВУведомление,
	|	НЕ КонтрагентыКонтролируемыеПоОбщейСумме.ГоловнойКонтрагент ЕСТЬ NULL КАК СуммаДоходовПоСделкеПревышаетОбщийПредел
	|ИЗ
	|	КонтролируемыеСделки КАК КонтролируемыеСделки
	|		ЛЕВОЕ СОЕДИНЕНИЕ КонтрагентыКонтролируемыеПоОбщейСумме КАК КонтрагентыКонтролируемыеПоОбщейСумме
	|		ПО КонтролируемыеСделки.ГоловнойКонтрагент = КонтрагентыКонтролируемыеПоОбщейСумме.ГоловнойКонтрагент
	|		ЛЕВОЕ СОЕДИНЕНИЕ КонтрагентыКонтролируемыеПоНДПИ КАК КонтрагентыКонтролируемыеПоНДПИ
	|		ПО КонтролируемыеСделки.ГоловнойКонтрагент = КонтрагентыКонтролируемыеПоНДПИ.ГоловнойКонтрагент
	|			И (КонтролируемыеСделки.СделкаКонтролируетсяПоНДПИ)
	|		ЛЕВОЕ СОЕДИНЕНИЕ КонтрагентыКонтролируемыеПоСпецРежиму КАК КонтрагентыКонтролируемыеПоСпецРежиму
	|		ПО КонтролируемыеСделки.ГоловнойКонтрагент = КонтрагентыКонтролируемыеПоСпецРежиму.ГоловнойКонтрагент
	|			И (КонтролируемыеСделки.СделкаОблагаетсяЕНВД
	|				ИЛИ КонтролируемыеСделки.СделкаСПлательщикомЕСХН)
	|		ЛЕВОЕ СОЕДИНЕНИЕ КонтрагентыКонтролируемыеПоПрибыли КАК КонтрагентыКонтролируемыеПоПрибыли
	|		ПО КонтролируемыеСделки.ГоловнойКонтрагент = КонтрагентыКонтролируемыеПоПрибыли.ГоловнойКонтрагент
	|			И (КонтролируемыеСделки.СделкаКонтролируетсяПоНалогуНаПрибыль)
	|		ЛЕВОЕ СОЕДИНЕНИЕ КонтрагентыКонтролируемыеПоОЭЗ КАК КонтрагентыКонтролируемыеПоОЭЗ
	|		ПО КонтролируемыеСделки.ГоловнойКонтрагент = КонтрагентыКонтролируемыеПоОЭЗ.ГоловнойКонтрагент
	|			И (КонтролируемыеСделки.СделкаКонтролируетсяПоРегистрацииВОЭЗ)
	|		ЛЕВОЕ СОЕДИНЕНИЕ КонтрагентыКонтролируемыеПоМорскомуМесторождению КАК КонтрагентыКонтролируемыеПоМорскомуМесторождению
	|		ПО КонтролируемыеСделки.ГоловнойКонтрагент = КонтрагентыКонтролируемыеПоМорскомуМесторождению.ГоловнойКонтрагент
	|			И (КонтролируемыеСделки.СделкаМорскогоМесторождения)
	|		ЛЕВОЕ СОЕДИНЕНИЕ КонтрагентыКонтролируемыеПоИнвестиционнымПроектам КАК КонтрагентыКонтролируемыеПоИнвестиционнымПроектам
	|		ПО КонтролируемыеСделки.ГоловнойКонтрагент = КонтрагентыКонтролируемыеПоИнвестиционнымПроектам.ГоловнойКонтрагент
	|			И (КонтролируемыеСделки.СделкаКонтролируетсяПоИнвестиционнымПроектам)
	|		ЛЕВОЕ СОЕДИНЕНИЕ КонтрагентыКонтролируемыеПоОсвобождениюНДС КАК КонтрагентыКонтролируемыеПоОсвобождениюНДС
	|		ПО КонтролируемыеСделки.ГоловнойКонтрагент = КонтрагентыКонтролируемыеПоОсвобождениюНДС.ГоловнойКонтрагент
	|			И (КонтролируемыеСделки.СделкаКонтролируетсяПоОсвобождениюНДС)
	|		ЛЕВОЕ СОЕДИНЕНИЕ КонтрагентыКонтролируемыеПоИнвестиционномуВычету КАК КонтрагентыКонтролируемыеПоИнвестиционномуВычету
	|		ПО КонтролируемыеСделки.ГоловнойКонтрагент = КонтрагентыКонтролируемыеПоИнвестиционномуВычету.ГоловнойКонтрагент
	|			И (КонтролируемыеСделки.СделкаКонтролируетсяПоИнвестиционномуВычету)
	|		ЛЕВОЕ СОЕДИНЕНИЕ КонтрагентыКонтролируемыеПоРегистрацииНеВРФ КАК КонтрагентыКонтролируемыеПоРегистрацииНеВРФ
	|		ПО КонтролируемыеСделки.ГоловнойКонтрагент = КонтрагентыКонтролируемыеПоРегистрацииНеВРФ.ГоловнойКонтрагент
	|			И (КонтролируемыеСделки.ЗарегистрированВСтранеСЛьготнымНалогообложением
	|				ИЛИ КонтролируемыеСделки.СделкаВнешнейТорговлиТоваромМировойБиржевойТорговли)
	|		ЛЕВОЕ СОЕДИНЕНИЕ КонтрагентыКонтролируемыеКакПрочиеЗависимыеЛица КАК КонтрагентыКонтролируемыеКакПрочиеЗависимыеЛица
	|		ПО КонтролируемыеСделки.ГоловнойКонтрагент = КонтрагентыКонтролируемыеКакПрочиеЗависимыеЛица.ГоловнойКонтрагент
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДоговорыУчастниковКонтролируемыхСделок КАК ДоговорыУчастниковКонтролируемыхСделок
	|		ПО КонтролируемыеСделки.Организация = ДоговорыУчастниковКонтролируемыхСделок.Организация
	|			И КонтролируемыеСделки.Контрагент = ДоговорыУчастниковКонтролируемыхСделок.Контрагент
	|			И КонтролируемыеСделки.Договор = ДоговорыУчастниковКонтролируемыхСделок.ДоговорКонтрагента
	|		ЛЕВОЕ СОЕДИНЕНИЕ КонтрагентыПоМинимальнойСумме КАК КонтрагентыПоМинимальнойСумме
	|		ПО КонтролируемыеСделки.ГоловнойКонтрагент = КонтрагентыПоМинимальнойСумме.ГоловнойКонтрагент
	|
	|СГРУППИРОВАТЬ ПО
	|	КонтролируемыеСделки.ГоловнойКонтрагент,
	|	КонтролируемыеСделки.Контрагент,
	|	КонтролируемыеСделки.Договор,
	|	КонтролируемыеСделки.ПредметСделки,
	|	КонтролируемыеСделки.Организация,
	|	КонтролируемыеСделки.ОтчетныйГод,
	|	КонтролируемыеСделки.СделкаВнешнейТорговлиТоваромМировойБиржевойТорговли,
	|	КонтролируемыеСделки.КонтрагентЗарегистрированВРФ,
	|	КонтролируемыеСделки.ЗарегистрированВСтранеСЛьготнымНалогообложением,
	|	КонтролируемыеСделки.СделкаКонтролируетсяПоРазнымСтавкамНалогаНаПрибыль,
	|	КонтролируемыеСделки.ТипВзаимозависимости,
	|	КонтролируемыеСделки.СделкаОблагаетсяЕНВД,
	|	КонтролируемыеСделки.СделкаСПлательщикомЕСХН,
	|	КонтролируемыеСделки.СделкаКонтролируетсяПоНалогуНаПрибыль,
	|	КонтролируемыеСделки.СделкаКонтролируетсяПоНДПИ,
	|	КонтролируемыеСделки.СделкаМорскогоМесторождения,
	|	КонтролируемыеСделки.СделкаКонтролируетсяПоИнвестиционнымПроектам,
	|	КонтролируемыеСделки.СделкаКонтролируетсяПоОсвобождениюНДС,
	|	КонтролируемыеСделки.СделкаКонтролируетсяПоИнвестиционномуВычету,
	|	КонтролируемыеСделки.СделкаКонтролируетсяПоДополнительномуДоходуОтСырья,
	|	ВЫБОР
	|		КОГДА КонтролируемыеСделки.ТипПредметаСделки = ЗНАЧЕНИЕ(Перечисление.ТипыПредметовКонтролируемыхСделок.Товар)
	|			ТОГДА ЕСТЬNULL(ДоговорыУчастниковКонтролируемыхСделок.КодУсловийПоставки, """")
	|		ИНАЧЕ """"
	|	КОНЕЦ,
	|	ЕСТЬNULL(ДоговорыУчастниковКонтролируемыхСделок.СпособОпределенияЦеныСделки, ЗНАЧЕНИЕ(Справочник.СпособыОпределенияЦенКонтролируемыхСделок.ПустаяСсылка)),
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ДоговорыУчастниковКонтролируемыхСделок.КодНаименованияСделки, """") = """"
	|			ТОГДА ВЫБОР
	|					КОГДА КонтролируемыеСделки.ТипПредметаСделки = ЗНАЧЕНИЕ(Перечисление.ТипыПредметовКонтролируемыхСделок.РаботаУслуга)
	|						ТОГДА ""019""
	|					ИНАЧЕ ""015""
	|				КОНЕЦ
	|		ИНАЧЕ ЕСТЬNULL(ДоговорыУчастниковКонтролируемыхСделок.КодНаименованияСделки, """")
	|	КОНЕЦ,
	|	КонтролируемыеСделки.СделкаКонтролируетсяПоРегистрацииВОЭЗ,
	|	КонтролируемыеСделки.СтороныВзаимозависимыПоКодексу,
	|	КонтролируемыеСделки.СделкаНеЯвляетсяКонтролируемойПоП4,
	|	КонтролируемыеСделки.ТипВзаимозависимости = ЗНАЧЕНИЕ(Перечисление.ТипыВзаимозависимости.НеВзаимозависимыйПосредник),
	|	НЕ КонтрагентыКонтролируемыеПоОбщейСумме.ГоловнойКонтрагент ЕСТЬ NULL,
	|	ВЫБОР
	|		КОГДА НЕ КонтрагентыПоМинимальнойСумме.ГоловнойКонтрагент ЕСТЬ NULL
	|				И (НЕ КонтрагентыКонтролируемыеКакПрочиеЗависимыеЛица.ГоловнойКонтрагент ЕСТЬ NULL
	|					ИЛИ НЕ КонтрагентыКонтролируемыеПоОбщейСумме.ГоловнойКонтрагент ЕСТЬ NULL
	|					ИЛИ НЕ КонтрагентыКонтролируемыеПоНДПИ.ГоловнойКонтрагент ЕСТЬ NULL
	|					ИЛИ НЕ КонтрагентыКонтролируемыеПоСпецРежиму.ГоловнойКонтрагент ЕСТЬ NULL
	|					ИЛИ НЕ КонтрагентыКонтролируемыеПоПрибыли.ГоловнойКонтрагент ЕСТЬ NULL
	|					ИЛИ НЕ КонтрагентыКонтролируемыеПоОЭЗ.ГоловнойКонтрагент ЕСТЬ NULL
	|					ИЛИ НЕ КонтрагентыКонтролируемыеПоРегистрацииНеВРФ.ГоловнойКонтрагент ЕСТЬ NULL
	|					ИЛИ НЕ КонтрагентыКонтролируемыеПоМорскомуМесторождению.ГоловнойКонтрагент ЕСТЬ NULL
	|					ИЛИ НЕ КонтрагентыКонтролируемыеПоИнвестиционнымПроектам.ГоловнойКонтрагент ЕСТЬ NULL
	|					ИЛИ НЕ КонтрагентыКонтролируемыеПоОсвобождениюНДС.ГоловнойКонтрагент ЕСТЬ NULL
	|					ИЛИ НЕ КонтрагентыКонтролируемыеПоРегистрацииНеВРФ.ГоловнойКонтрагент ЕСТЬ NULL
	|					ИЛИ НЕ КонтрагентыКонтролируемыеПоИнвестиционномуВычету.ГоловнойКонтрагент ЕСТЬ NULL)
	|				И НЕ КонтролируемыеСделки.СделкаНеЯвляетсяКонтролируемойПоП4
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаКонтролируемыхСделок2019()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	КонтролируемыеСделки.Организация КАК Организация,
	|	КонтролируемыеСделки.ОтчетныйГод КАК ОтчетныйГод,
	|	КонтролируемыеСделки.ГоловнойКонтрагент КАК Контрагент,
	|	КонтролируемыеСделки.Контрагент КАК ОбособленноеПодразделениеКонтрагента,
	|	КонтролируемыеСделки.Договор КАК Договор,
	|	КонтролируемыеСделки.ПредметСделки КАК ПредметСделки,
	|	КонтролируемыеСделки.СделкаВнешнейТорговлиТоваромМировойБиржевойТорговли КАК СделкаВнешнейТорговлиТоваромМировойБиржевойТорговли,
	|	СУММА(КонтролируемыеСделки.СуммаБезНДСВРублях) КАК СуммаБезНДСВРублях,
	|	КонтролируемыеСделки.КонтрагентЗарегистрированВРФ КАК КонтрагентЗарегистрированВРФ,
	|	КонтролируемыеСделки.ЗарегистрированВСтранеСЛьготнымНалогообложением КАК ЗарегистрированВСтранеСЛьготнымНалогообложением,
	|	КонтролируемыеСделки.ТипВзаимозависимости КАК ТипВзаимозависимости,
	|	КонтролируемыеСделки.СделкаОблагаетсяЕНВД КАК СделкаОблагаетсяЕНВД,
	|	КонтролируемыеСделки.СделкаКонтролируетсяПоРазнымСтавкамНалогаНаПрибыль КАК СделкаКонтролируетсяПоРазнымСтавкамНалогаНаПрибыль,
	|	КонтролируемыеСделки.СделкаСПлательщикомЕСХН КАК СделкаСПлательщикомЕСХН,
	|	КонтролируемыеСделки.СделкаКонтролируетсяПоНалогуНаПрибыль КАК СделкаКонтролируетсяПоНалогуНаПрибыль,
	|	КонтролируемыеСделки.СделкаКонтролируетсяПоНДПИ КАК СделкаКонтролируетсяПоНДПИ,
	|	КонтролируемыеСделки.СделкаМорскогоМесторождения КАК СделкаМорскогоМесторождения,
	|	КонтролируемыеСделки.СделкаКонтролируетсяПоИнвестиционнымПроектам КАК СделкаКонтролируетсяПоИнвестиционнымПроектам,
	|	КонтролируемыеСделки.СделкаКонтролируетсяПоОсвобождениюНДС КАК СделкаКонтролируетсяПоОсвобождениюНДС,
	|	КонтролируемыеСделки.СделкаКонтролируетсяПоИнвестиционномуВычету КАК СделкаКонтролируетсяПоИнвестиционномуВычету,
	|	КонтролируемыеСделки.СделкаКонтролируетсяПоДополнительномуДоходуОтСырья КАК СделкаКонтролируетсяПоДополнительномуДоходуОтСырья,
	|	ВЫБОР
	|		КОГДА КонтролируемыеСделки.ТипПредметаСделки = ЗНАЧЕНИЕ(Перечисление.ТипыПредметовКонтролируемыхСделок.Товар)
	|			ТОГДА ЕСТЬNULL(ДоговорыУчастниковКонтролируемыхСделок.КодУсловийПоставки, """")
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК КодУсловийПоставки,
	|	ЕСТЬNULL(ДоговорыУчастниковКонтролируемыхСделок.СпособОпределенияЦеныСделки, ЗНАЧЕНИЕ(Справочник.СпособыОпределенияЦенКонтролируемыхСделок.ПустаяСсылка)) КАК СпособОпределенияЦеныСделки,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ДоговорыУчастниковКонтролируемыхСделок.КодНаименованияСделки, """") = """"
	|			ТОГДА ВЫБОР
	|					КОГДА КонтролируемыеСделки.ТипПредметаСделки = ЗНАЧЕНИЕ(Перечисление.ТипыПредметовКонтролируемыхСделок.РаботаУслуга)
	|						ТОГДА ""019""
	|					ИНАЧЕ ""015""
	|				КОНЕЦ
	|		ИНАЧЕ ЕСТЬNULL(ДоговорыУчастниковКонтролируемыхСделок.КодНаименованияСделки, """")
	|	КОНЕЦ КАК КодНаименованияСделки,
	|	КонтролируемыеСделки.СделкаКонтролируетсяПоРегистрацииВОЭЗ КАК СделкаКонтролируетсяПоРегистрацииВОЭЗ,
	|	КонтролируемыеСделки.СтороныВзаимозависимыПоКодексу КАК СтороныВзаимозависимыПоКодексу,
	|	КонтролируемыеСделки.ТипВзаимозависимости = ЗНАЧЕНИЕ(Перечисление.ТипыВзаимозависимости.НеВзаимозависимыйПосредник) КАК НеВзаимозависимыйПосредник,
	|	КонтролируемыеСделки.СделкаНеЯвляетсяКонтролируемойПоП4 КАК СделкаНеЯвляетсяКонтролируемойПоП4,
	|	НЕ КонтрагентыПоМинимальнойСумме.ГоловнойКонтрагент ЕСТЬ NULL
	|		И (НЕ КонтрагентыРФКонтролируемыеПоУсловию.ГоловнойКонтрагент ЕСТЬ NULL
	|			ИЛИ НЕ КонтрагентыКонтролируемыеКакПрочиеЗависимыеЛица.ГоловнойКонтрагент ЕСТЬ NULL
	|			ИЛИ НЕ КонтрагентыКонтролируемыеПоРегистрацииНеВРФ.ГоловнойКонтрагент ЕСТЬ NULL)
	|		И НЕ КонтролируемыеСделки.СделкаНеЯвляетсяКонтролируемойПоП4 КАК СделкаПопалаВУведомление,
	|	НЕ КонтрагентыРФКонтролируемыеПоУсловию.ГоловнойКонтрагент ЕСТЬ NULL КАК СуммаДоходовПоСделкеПревышаетОбщийПредел
	|ИЗ
	|	КонтролируемыеСделки КАК КонтролируемыеСделки
	|		ЛЕВОЕ СОЕДИНЕНИЕ КонтрагентыРФКонтролируемыеПоУсловию КАК КонтрагентыРФКонтролируемыеПоУсловию
	|		ПО КонтролируемыеСделки.ГоловнойКонтрагент = КонтрагентыРФКонтролируемыеПоУсловию.ГоловнойКонтрагент
	|			И (КонтролируемыеСделки.СделкаКонтролируетсяПоРазнымСтавкамНалогаНаПрибыль
	|				ИЛИ КонтролируемыеСделки.СделкаКонтролируетсяПоНДПИ
	|				ИЛИ КонтролируемыеСделки.СделкаОблагаетсяЕНВД
	|				ИЛИ КонтролируемыеСделки.СделкаСПлательщикомЕСХН
	|				ИЛИ КонтролируемыеСделки.СделкаКонтролируетсяПоНалогуНаПрибыль
	|				ИЛИ КонтролируемыеСделки.СделкаМорскогоМесторождения
	|				ИЛИ КонтролируемыеСделки.СделкаКонтролируетсяПоОсвобождениюНДС
	|				ИЛИ КонтролируемыеСделки.СделкаКонтролируетсяПоИнвестиционномуВычету
	|				ИЛИ КонтролируемыеСделки.СделкаКонтролируетсяПоДополнительномуДоходуОтСырья)
	|		ЛЕВОЕ СОЕДИНЕНИЕ КонтрагентыКонтролируемыеПоРегистрацииНеВРФ КАК КонтрагентыКонтролируемыеПоРегистрацииНеВРФ
	|		ПО КонтролируемыеСделки.ГоловнойКонтрагент = КонтрагентыКонтролируемыеПоРегистрацииНеВРФ.ГоловнойКонтрагент
	|			И (КонтролируемыеСделки.ЗарегистрированВСтранеСЛьготнымНалогообложением
	|				ИЛИ КонтролируемыеСделки.СделкаВнешнейТорговлиТоваромМировойБиржевойТорговли)
	|		ЛЕВОЕ СОЕДИНЕНИЕ КонтрагентыКонтролируемыеКакПрочиеЗависимыеЛица КАК КонтрагентыКонтролируемыеКакПрочиеЗависимыеЛица
	|		ПО КонтролируемыеСделки.ГоловнойКонтрагент = КонтрагентыКонтролируемыеКакПрочиеЗависимыеЛица.ГоловнойКонтрагент
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДоговорыУчастниковКонтролируемыхСделок КАК ДоговорыУчастниковКонтролируемыхСделок
	|		ПО КонтролируемыеСделки.Организация = ДоговорыУчастниковКонтролируемыхСделок.Организация
	|			И КонтролируемыеСделки.Контрагент = ДоговорыУчастниковКонтролируемыхСделок.Контрагент
	|			И КонтролируемыеСделки.Договор = ДоговорыУчастниковКонтролируемыхСделок.ДоговорКонтрагента
	|		ЛЕВОЕ СОЕДИНЕНИЕ КонтрагентыПоМинимальнойСумме КАК КонтрагентыПоМинимальнойСумме
	|		ПО КонтролируемыеСделки.ГоловнойКонтрагент = КонтрагентыПоМинимальнойСумме.ГоловнойКонтрагент
	|
	|СГРУППИРОВАТЬ ПО
	|	КонтролируемыеСделки.ГоловнойКонтрагент,
	|	КонтролируемыеСделки.Контрагент,
	|	КонтролируемыеСделки.Договор,
	|	КонтролируемыеСделки.ПредметСделки,
	|	КонтролируемыеСделки.Организация,
	|	КонтролируемыеСделки.ОтчетныйГод,
	|	КонтролируемыеСделки.СделкаВнешнейТорговлиТоваромМировойБиржевойТорговли,
	|	КонтролируемыеСделки.КонтрагентЗарегистрированВРФ,
	|	КонтролируемыеСделки.ЗарегистрированВСтранеСЛьготнымНалогообложением,
	|	КонтролируемыеСделки.ТипВзаимозависимости,
	|	КонтролируемыеСделки.СделкаОблагаетсяЕНВД,
	|	КонтролируемыеСделки.СделкаКонтролируетсяПоРазнымСтавкамНалогаНаПрибыль,
	|	КонтролируемыеСделки.СделкаСПлательщикомЕСХН,
	|	КонтролируемыеСделки.СделкаКонтролируетсяПоНалогуНаПрибыль,
	|	КонтролируемыеСделки.СделкаКонтролируетсяПоНДПИ,
	|	КонтролируемыеСделки.СделкаМорскогоМесторождения,
	|	КонтролируемыеСделки.СделкаКонтролируетсяПоИнвестиционнымПроектам,
	|	КонтролируемыеСделки.СделкаКонтролируетсяПоОсвобождениюНДС,
	|	КонтролируемыеСделки.СделкаКонтролируетсяПоИнвестиционномуВычету,
	|	КонтролируемыеСделки.СделкаКонтролируетсяПоДополнительномуДоходуОтСырья,
	|	ВЫБОР
	|		КОГДА КонтролируемыеСделки.ТипПредметаСделки = ЗНАЧЕНИЕ(Перечисление.ТипыПредметовКонтролируемыхСделок.Товар)
	|			ТОГДА ЕСТЬNULL(ДоговорыУчастниковКонтролируемыхСделок.КодУсловийПоставки, """")
	|		ИНАЧЕ """"
	|	КОНЕЦ,
	|	ЕСТЬNULL(ДоговорыУчастниковКонтролируемыхСделок.СпособОпределенияЦеныСделки, ЗНАЧЕНИЕ(Справочник.СпособыОпределенияЦенКонтролируемыхСделок.ПустаяСсылка)),
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ДоговорыУчастниковКонтролируемыхСделок.КодНаименованияСделки, """") = """"
	|			ТОГДА ВЫБОР
	|					КОГДА КонтролируемыеСделки.ТипПредметаСделки = ЗНАЧЕНИЕ(Перечисление.ТипыПредметовКонтролируемыхСделок.РаботаУслуга)
	|						ТОГДА ""019""
	|					ИНАЧЕ ""015""
	|				КОНЕЦ
	|		ИНАЧЕ ЕСТЬNULL(ДоговорыУчастниковКонтролируемыхСделок.КодНаименованияСделки, """")
	|	КОНЕЦ,
	|	КонтролируемыеСделки.СделкаКонтролируетсяПоРегистрацииВОЭЗ,
	|	КонтролируемыеСделки.СтороныВзаимозависимыПоКодексу,
	|	КонтролируемыеСделки.ТипВзаимозависимости = ЗНАЧЕНИЕ(Перечисление.ТипыВзаимозависимости.НеВзаимозависимыйПосредник),
	|	КонтролируемыеСделки.СделкаНеЯвляетсяКонтролируемойПоП4,
	|	НЕ КонтрагентыРФКонтролируемыеПоУсловию.ГоловнойКонтрагент ЕСТЬ NULL,
	|	НЕ КонтрагентыПоМинимальнойСумме.ГоловнойКонтрагент ЕСТЬ NULL
	|		И (НЕ КонтрагентыРФКонтролируемыеПоУсловию.ГоловнойКонтрагент ЕСТЬ NULL
	|			ИЛИ НЕ КонтрагентыКонтролируемыеКакПрочиеЗависимыеЛица.ГоловнойКонтрагент ЕСТЬ NULL
	|			ИЛИ НЕ КонтрагентыКонтролируемыеПоРегистрацииНеВРФ.ГоловнойКонтрагент ЕСТЬ NULL)
	|		И НЕ КонтролируемыеСделки.СделкаНеЯвляетсяКонтролируемойПоП4";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ДополнитьТаблицуПричинамиВключенияВУведомление(СделкиУведомления, ВерсияУведомления)
	
	СделкиУведомления.Колонки.Добавить("ПричинаПопаданияВКонтролируемыеСделки", ОбщегоНазначения.ОписаниеТипаСтрока(10));
	СделкиУведомления.Колонки.Добавить("ПричинаВключения", ОбщегоНазначения.ОписаниеТипаСтрока(250));
	
	Для Каждого Сделка Из СделкиУведомления Цикл
		
		Если ВерсияУведомления >= КонтролируемыеСделкиКлиентСервер.ВерсияУведомления_2019() Тогда
			ЗаполнитьПричинуВключения2019(Сделка);
		Иначе
			ЗаполнитьПричинуВключения2012(Сделка);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПричинуВключения2012(Сделка)
	
	Если Сделка.СделкаНеЯвляетсяКонтролируемойПоП4 Тогда
		Сделка.ПричинаПопаданияВКонтролируемыеСделки = "0";
		Сделка.ПричинаВключения = НСтр("ru = 'Сделка не является контролируемой по п.4 ст. 105.14 НК РФ';
										|en = 'Сделка не является контролируемой по п.4 ст. 105.14 НК РФ'");
		Возврат;
	КонецЕсли;
	
	ПричинаПопаданияВКонтролируемыеСделки = "";
	ПричиныВключенияВУведомление = Новый Массив;
	
	Если Сделка.СуммаДоходовПоСделкеПревышаетОбщийПредел Тогда
		ПричинаПопаданияВКонтролируемыеСделки = "2";
		ПричиныВключенияВУведомление.Добавить(НСтр("ru = 'На общих основаниях (пп.1 п.2 ст. 105.14 НК РФ)';
													|en = 'On a regular basis (subcl. 1, cl 2, art. 105.14 of the TC of the RF) '"));
	КонецЕсли;
	
	Если Сделка.СделкаКонтролируетсяПоНДПИ Тогда
		ПричинаПопаданияВКонтролируемыеСделки = "3";
		ПричиныВключенияВУведомление.Добавить(НСтр("ru = 'По признаку уплаты НДПИ (пп.2 п.2 ст. 105.14 НК РФ)';
													|en = 'By MET payment (subcl. 2 cl. 2 art. 105.14 of the TC of the RF)'"));
	КонецЕсли;
	
	Если Сделка.СделкаОблагаетсяЕНВД Или Сделка.СделкаСПлательщикомЕСХН Тогда
		ПричинаПопаданияВКонтролируемыеСделки = "4";
		ПричиныВключенияВУведомление.Добавить(НСтр("ru = 'По признаку применения спец. режимов (пп.3 п.2 ст. 105.14 НК РФ)';
													|en = 'By application of special treatments (subcl. 3, cl. 105.14 art.105.14 of the TC of the RF)'"));
	КонецЕсли;
	
	Если Сделка.СделкаКонтролируетсяПоНалогуНаПрибыль Тогда
		ПричинаПопаданияВКонтролируемыеСделки = "5";
		ПричиныВключенияВУведомление.Добавить(НСтр("ru = 'По признаку уплаты налога на прибыль (пп.4 п.2 ст. 105.14 НК РФ)';
													|en = 'By payment of profit tax (subcl. 4 cl. 2 art. 105.14 of the TC of the RF)'"));
	КонецЕсли;
	
	Если Сделка.СделкаКонтролируетсяПоРегистрацииВОЭЗ Тогда
		ПричинаПопаданияВКонтролируемыеСделки = "6";
		ПричиныВключенияВУведомление.Добавить(НСтр("ru = 'По признаку особой экономической зоны (пп.5 п.2 ст. 105.14 НК РФ)';
													|en = 'By special economic zone (subcl. 5 cl. 2 art. 105.14 of the TC of the RF)'"));
	КонецЕсли;
	
	Если Сделка.СделкаМорскогоМесторождения Тогда
		ПричинаПопаданияВКонтролируемыеСделки = "7";
		ПричиныВключенияВУведомление.Добавить(НСтр("ru = 'По признаку морского месторождения (пп.6 п.2 ст. 105.14 НК РФ)';
													|en = 'On the basis of the offshore field (subcl. 6 cl. 2 art. 105.14 of the TC of the RF)'"));
	КонецЕсли;
	
	Если Сделка.СделкаКонтролируетсяПоИнвестиционнымПроектам Тогда
		ПричинаПопаданияВКонтролируемыеСделки = "8";
		ПричиныВключенияВУведомление.Добавить(НСтр("ru = 'По признаку регионального инвестиционного проекта (пп.7 п.2 ст. 105.14 НК РФ)';
													|en = 'On the basis of the regional investment project (subcl. 7 cl. 2 art. 105.14 of the TC of the RF)'"));
	КонецЕсли;
	
	Если Сделка.СделкаКонтролируетсяПоОсвобождениюНДС Тогда
		ПричинаПопаданияВКонтролируемыеСделки = "9";
		ПричиныВключенияВУведомление.Добавить(НСтр("ru = 'По признаку освобождения от НДС (пп.8 п.2 ст. 105.14 НК РФ)';
													|en = 'On the basis of VAT exemption (subcl. 8 cl. 2 art. 105.14 of the TC of the RF)'"));
	КонецЕсли;
	
	Если Сделка.СделкаКонтролируетсяПоИнвестиционномуВычету Тогда
		ПричинаПопаданияВКонтролируемыеСделки = "10";
		ПричиныВключенияВУведомление.Добавить(НСтр("ru = 'По признаку применения инвестиционного вычета (пп.9 п.2 ст. 105.14 НК РФ)';
													|en = 'On the basis of the application of the investment deduction (subcl. 9 cl. 2 art. 105.14 of the TC of the RF)'"));
	КонецЕсли;
	
	Если Сделка.СделкаВнешнейТорговлиТоваромМировойБиржевойТорговли Тогда
		ПричинаПопаданияВКонтролируемыеСделки = "11";
		ПричиныВключенияВУведомление.Добавить(НСтр("ru = 'Торговля товарами мировой биржевой торговли';
													|en = 'Sale of commodities of international marketplace trading'"));
	КонецЕсли;
	
	Если Сделка.ЗарегистрированВСтранеСЛьготнымНалогообложением Тогда
		ПричинаПопаданияВКонтролируемыеСделки = "12";
		ПричиныВключенияВУведомление.Добавить(НСтр("ru = 'Сделка с офшором';
													|en = 'Transaction with offshore company'"));
	КонецЕсли;
	
	Если Сделка.СтороныВзаимозависимыПоКодексу И Сделка.КонтрагентЗарегистрированВРФ Тогда
		ПричинаПопаданияВКонтролируемыеСделки = "13";
		ПричиныВключенияВУведомление.Добавить(НСтр("ru = 'Сделки с российскими взаимозависимыми лицами';
													|en = 'Transactions with the Russian interdependent persons'"));
	КонецЕсли;
	
	Если Сделка.ТипВзаимозависимости = Перечисления.ТипыВзаимозависимости.НеВзаимозависимыйПосредник Тогда
		ПричинаПопаданияВКонтролируемыеСделки = "14";
		ПричиныВключенияВУведомление.Добавить(НСтр("ru = 'Не взаимозависимый посредник';
													|en = 'Not interdependent intermediary'"));
	КонецЕсли;
	
	Если Сделка.СтороныВзаимозависимыПоКодексу И Не Сделка.КонтрагентЗарегистрированВРФ Тогда
		ПричинаПопаданияВКонтролируемыеСделки = "15";
		ПричиныВключенияВУведомление.Добавить(НСтр("ru = 'Сделка с иностранным взаимозависимым лицом';
													|en = 'Transaction with a foreign interdependent person'"));
	КонецЕсли;
	
	Если ПричиныВключенияВУведомление.Количество() > 1 Тогда
		ПричинаПопаданияВКонтролируемыеСделки = "1";
	КонецЕсли;
	
	Сделка.ПричинаПопаданияВКонтролируемыеСделки = ПричинаПопаданияВКонтролируемыеСделки;
	Сделка.ПричинаВключения = СтрСоединить(ПричиныВключенияВУведомление, "," + " ");
	
КонецПроцедуры

Процедура ЗаполнитьПричинуВключения2019(Сделка)
	
	Если Сделка.СделкаНеЯвляетсяКонтролируемойПоП4 Тогда
		Сделка.ПричинаПопаданияВКонтролируемыеСделки = "0";
		Сделка.ПричинаВключения = НСтр("ru = 'Сделка не является контролируемой по п.4 ст. 105.14 НК РФ';
										|en = 'Сделка не является контролируемой по п.4 ст. 105.14 НК РФ'");
		Возврат;
	КонецЕсли;
	
	ПричинаПопаданияВКонтролируемыеСделки = "";
	ПричиныВключенияВУведомление = Новый Массив;
	
	Если Сделка.СделкаКонтролируетсяПоРазнымСтавкамНалогаНаПрибыль Тогда
		ПричинаПопаданияВКонтролируемыеСделки = "2";
		ПричиныВключенияВУведомление.Добавить(НСтр("ru = 'По признаку разных ставок налога на прибыль (пп.1 п.2 ст. 105.14 НК РФ)';
													|en = 'By different profit tax rates (subcl. 1 cl. 2 art. 105.14 of the TC of the RF)'"));
	КонецЕсли;
	
	Если Сделка.СделкаКонтролируетсяПоНДПИ Тогда
		ПричинаПопаданияВКонтролируемыеСделки = "3";
		ПричиныВключенияВУведомление.Добавить(НСтр("ru = 'По признаку уплаты НДПИ (пп.2 п.2 ст. 105.14 НК РФ)';
													|en = 'By MET payment (subcl. 2 cl. 2 art. 105.14 of the TC of the RF)'"));
	КонецЕсли;
	
	Если Сделка.СделкаОблагаетсяЕНВД Или Сделка.СделкаСПлательщикомЕСХН Тогда
		ПричинаПопаданияВКонтролируемыеСделки = "4";
		ПричиныВключенияВУведомление.Добавить(НСтр("ru = 'По признаку применения спец. режимов (пп.3 п.2 ст. 105.14 НК РФ)';
													|en = 'By application of special treatments (subcl. 3, cl. 105.14 art.105.14 of the TC of the RF)'"));
	КонецЕсли;
	
	Если Сделка.СделкаКонтролируетсяПоНалогуНаПрибыль Тогда
		ПричинаПопаданияВКонтролируемыеСделки = "5";
		ПричиныВключенияВУведомление.Добавить(НСтр("ru = 'По признаку уплаты налога на прибыль (пп.4 п.2 ст. 105.14 НК РФ)';
													|en = 'By payment of profit tax (subcl. 4 cl. 2 art. 105.14 of the TC of the RF)'"));
	КонецЕсли;
	
	Если Сделка.СделкаМорскогоМесторождения Тогда
		ПричинаПопаданияВКонтролируемыеСделки = "6";
		ПричиныВключенияВУведомление.Добавить(НСтр("ru = 'По признаку морского месторождения (пп.6 п.2 ст. 105.14 НК РФ)';
													|en = 'On the basis of the offshore field (subcl. 6 cl. 2 art. 105.14 of the TC of the RF)'"));
	КонецЕсли;
	
	Если Сделка.СделкаКонтролируетсяПоОсвобождениюНДС Тогда
		ПричинаПопаданияВКонтролируемыеСделки = "7";
		ПричиныВключенияВУведомление.Добавить(НСтр("ru = 'По признаку освобождения от НДС (пп.8 п.2 ст. 105.14 НК РФ)';
													|en = 'On the basis of VAT exemption (subcl. 8 cl. 2 art. 105.14 of the TC of the RF)'"));
	КонецЕсли;
	
	Если Сделка.СделкаКонтролируетсяПоИнвестиционномуВычету Тогда
		ПричинаПопаданияВКонтролируемыеСделки = "9";
		ПричиныВключенияВУведомление.Добавить(НСтр("ru = 'По признаку применения инвестиционного вычета (пп.9 п.2 ст. 105.14 НК РФ)';
													|en = 'On the basis of the application of the investment deduction (subcl. 9 cl. 2 art. 105.14 of the TC of the RF)'"));
	КонецЕсли;
	
	Если Сделка.СделкаКонтролируетсяПоДополнительномуДоходуОтСырья Тогда
		ПричинаПопаданияВКонтролируемыеСделки = "10";
		ПричиныВключенияВУведомление.Добавить(НСтр("ru = 'По признаку плательщика налога на доп.доход от добычи углеводородного сырья (пп.10 п.2 ст. 105.14 НК РФ)';
													|en = 'On the basis of payer of tax on additional income from the production of hydrocarbons (subcl. 10 cl. 2 art. 105.14 of the TC of the RF)'"));
	КонецЕсли;
	
	Если Сделка.СделкаВнешнейТорговлиТоваромМировойБиржевойТорговли Тогда
		ПричинаПопаданияВКонтролируемыеСделки = "11";
		ПричиныВключенияВУведомление.Добавить(НСтр("ru = 'Торговля товарами мировой биржевой торговли';
													|en = 'Sale of commodities of international marketplace trading'"));
	КонецЕсли;
	
	Если Сделка.ЗарегистрированВСтранеСЛьготнымНалогообложением Тогда
		ПричинаПопаданияВКонтролируемыеСделки = "12";
		ПричиныВключенияВУведомление.Добавить(НСтр("ru = 'Сделка с офшором';
													|en = 'Transaction with offshore company'"));
	КонецЕсли;
	
	Если Сделка.ТипВзаимозависимости = Перечисления.ТипыВзаимозависимости.НеВзаимозависимыйПосредник Тогда
		ПричинаПопаданияВКонтролируемыеСделки = "13";
		ПричиныВключенияВУведомление.Добавить(НСтр("ru = 'Не взаимозависимый посредник';
													|en = 'Not interdependent intermediary'"));
	КонецЕсли;
	
	Если Сделка.СтороныВзаимозависимыПоКодексу И Не Сделка.КонтрагентЗарегистрированВРФ Тогда
		ПричинаПопаданияВКонтролируемыеСделки = "14";
		ПричиныВключенияВУведомление.Добавить(НСтр("ru = 'Сделка с иностранным взаимозависимым лицом';
													|en = 'Transaction with a foreign interdependent person'"));
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПричинаПопаданияВКонтролируемыеСделки)
		И Сделка.СтороныВзаимозависимыПоКодексу И Сделка.КонтрагентЗарегистрированВРФ Тогда
		// Взаимозависимые лица теперь не включаются в уведомление без особых причин.
		// Поэтому, если нет причин для включения, то запишем в причины попадания, что это взаимозависимое лицо.
		// Это нужно, чтобы не было пустых группировок - иначе отчет выглядит некрасиво.
		ПричинаПопаданияВКонтролируемыеСделки = "15";
		ПричиныВключенияВУведомление.Добавить(НСтр("ru = 'Сделки с российскими взаимозависимыми лицами';
													|en = 'Transactions with the Russian interdependent persons'"));
	КонецЕсли;
	
	Если ПричиныВключенияВУведомление.Количество() > 1 Тогда
		ПричинаПопаданияВКонтролируемыеСделки = "1";
	КонецЕсли;
	
	Сделка.ПричинаПопаданияВКонтролируемыеСделки = ПричинаПопаданияВКонтролируемыеСделки;
	Сделка.ПричинаВключения = СтрСоединить(ПричиныВключенияВУведомление, "," + " ");
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли