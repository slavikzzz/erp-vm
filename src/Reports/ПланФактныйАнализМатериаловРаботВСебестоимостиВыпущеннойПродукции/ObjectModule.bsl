#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий
	
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	НаборДанныхРезультат = СхемаКомпоновкиДанных.НаборыДанных.Результат; //НаборДанныхОбъединениеСхемыКомпоновкиДанных 
	
	НаборДанных25 = СхемаКомпоновкиДанных.НаборыДанных.Найти("План2_5");
	Если ЦенообразованиеВызовСервера.ИспользуетсяЦенообразование25() Тогда
		НаборДанныхРезультат.Элементы.План.Запрос = НаборДанных25.Запрос;
	КонецЕсли;
	СхемаКомпоновкиДанных.НаборыДанных.Удалить(НаборДанных25);
	
	ТекстЗапроса = НаборДанныхРезультат.Элементы.План.Запрос;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаКоэффициентУпаковки1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ВыходныеИзделияПлан.Упаковка",
			"ВыходныеИзделияПлан.Номенклатура"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаКоэффициентУпаковки2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ВЫРАЗИТЬ(МатериалыИУслугиПлан.Упаковка КАК Справочник.УпаковкиЕдиницыИзмерения)", 
			"МатериалыИУслугиПлан.Номенклатура"));
			
	НаборДанныхРезультат.Элементы.План.Запрос = ТекстЗапроса;
	
	СегментыСервер.ВключитьОтборПоСегментуНоменклатурыВСКД(КомпоновщикНастроек);
	
	ПараметрВидЦены = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ВидЦеныПлановойСтоимостиМатериаловРабот"));
	Если ПараметрВидЦены <> Неопределено Тогда
		ПараметрВидЦены.Значение = Константы.ВидЦеныПлановойСтоимостиМатериаловРабот.Получить();
		ПараметрВидЦены.Использование = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#КонецЕсли