#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Функция ВерсияФорматаВыгрузки(Знач НаДату = Неопределено, ВыбраннаяФорма = Неопределено) Экспорт
	
	Возврат Перечисления.ВерсииФорматовВыгрузки.Версия500;
	
КонецФункции

Функция ТаблицаФормОтчета() Экспорт
	
	ОписаниеТиповСтрока = Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(254));
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("Дата"));
	ОписаниеТиповДата = Новый ОписаниеТипов(МассивТипов, , Новый КвалификаторыДаты(ЧастиДаты.Дата));
	
	ТаблицаФормОтчета = Новый ТаблицаЗначений;
	ТаблицаФормОтчета.Колонки.Добавить("ФормаОтчета",        ОписаниеТиповСтрока);
	ТаблицаФормОтчета.Колонки.Добавить("ОписаниеОтчета",     ОписаниеТиповСтрока, "Утверждена",  20);
	ТаблицаФормОтчета.Колонки.Добавить("ДатаНачалоДействия", ОписаниеТиповДата,   "Действует с", 5);
	ТаблицаФормОтчета.Колонки.Добавить("ДатаКонецДействия",  ОписаниеТиповДата,   "         по", 5);
	ТаблицаФормОтчета.Колонки.Добавить("РедакцияФормы",      ОписаниеТиповСтрока, "Редакция формы", 20);
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета        = "ФормаОтчета2016Кв2";
	НоваяФорма.ОписаниеОтчета     = "Утверждена приказом ФНС России от 12.01.2016 № ММВ-7-3/1@.";
	НоваяФорма.РедакцияФормы      = "от 12.01.2016 № ММВ-7-3/1@.";
	НоваяФорма.ДатаНачалоДействия = '2016-05-01';
	НоваяФорма.ДатаКонецДействия  = '2021-01-31';
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета        = "ФормаОтчета2021Кв1";
	НоваяФорма.ОписаниеОтчета     = "Утверждена приказом ФНС России от 13.10.2020 № ЕД-7-3/747@.";
	НоваяФорма.РедакцияФормы      = "от 13.10.2020 № ЕД-7-3/747@.";
	НоваяФорма.ДатаНачалоДействия = '2021-02-01';
	НоваяФорма.ДатаКонецДействия  = '2022-04-30';
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета        = "ФормаОтчета2022Кв2";
	НоваяФорма.ОписаниеОтчета     = "Утверждена приказом ФНС России от 12.01.2022 № ЕД-7-3/8@.";
	НоваяФорма.РедакцияФормы      = "от 12.01.2022 № ЕД-7-3/8@.";
	НоваяФорма.ДатаНачалоДействия = '2022-05-01';
	НоваяФорма.ДатаКонецДействия  = '2023-02-28';
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета        = "ФормаОтчета2023Кв1";
	НоваяФорма.ОписаниеОтчета
	= "Утверждена приказом ФНС России от 12.01.2022 № ЕД-7-3/8@ (в ред. приказа ФНС России от 12.10.2022 № ЕД-7-3/923@).";
	НоваяФорма.РедакцияФормы      = "от 12.10.2022 № ЕД-7-3/923@.";
	НоваяФорма.ДатаНачалоДействия = '2023-03-01';
	НоваяФорма.ДатаКонецДействия  = '2025-05-31';
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета        = "ФормаОтчета2025Кв2";
	НоваяФорма.ОписаниеОтчета     = "Утверждена приказом ФНС России от 31.01.2025 № ЕД-7-3/65@.";
	НоваяФорма.РедакцияФормы      = "от 31.01.2025 № ЕД-7-3/65@.";
	НоваяФорма.ДатаНачалоДействия = '2025-06-01';
	НоваяФорма.ДатаКонецДействия  = РегламентированнаяОтчетностьКлиентСервер.ПустоеЗначениеТипа(Тип("Дата"));
	
	Возврат ТаблицаФормОтчета;
	
КонецФункции

Функция ДанныеРеглОтчета(ЭкземплярРеглОтчета) Экспорт
	
	ТаблицаДанныхРеглОтчета = ИнтерфейсыВзаимодействияБРО.НовыйТаблицаДанныхРеглОтчета();
	
	ДанныеРеглОтчета = ЭкземплярРеглОтчета.ДанныеОтчета.Получить();
	Если ТипЗнч(ДанныеРеглОтчета) <> Тип("Структура") Тогда
		Возврат ТаблицаДанныхРеглОтчета;
	КонецЕсли;
	
	Если ЭкземплярРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2025Кв2"
		ИЛИ ЭкземплярРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2023Кв1"
		ИЛИ ЭкземплярРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2022Кв2"
		ИЛИ ЭкземплярРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2021Кв1" Тогда
		
		Если ДанныеРеглОтчета.Свойство("ОкружениеСохранения") Тогда
			
			Возврат ТаблицаДанныхРеглОтчета; // отчет сохранен в 2.0
			
		ИначеЕсли ДанныеРеглОтчета.Свойство("ДанныеМногоуровневыхРазделов") Тогда
			
			Для каждого СтраницаРаздела1 Из ДанныеРеглОтчета.ДанныеМногоуровневыхРазделов["Раздел1"].Строки Цикл
				
				ОКТМО = СтраницаРаздела1.Данные["П000010001003"];
				
				ДанныеМногострочнойЧастиРаздела1 = СтраницаРаздела1.ДанныеМногострочныхЧастей["П00001М1"];
				
				Для каждого СтрокаМнЧ Из ДанныеМногострочнойЧастиРаздела1.Строки Цикл
					
					Сумма = ТаблицаДанныхРеглОтчета.Добавить();
					Сумма.Период = ЭкземплярРеглОтчета.ДатаОкончания;
					Сумма.ОКАТО  = ОКТМО;
					Сумма.КБК    = СтрокаМнЧ.Данные.П00001М102000;
					Сумма.Сумма  = СтрокаМнЧ.Данные.П00001М103000;
					
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЕсли;
		
	ИначеЕсли ЭкземплярРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2016Кв2" Тогда
		
		Если ДанныеРеглОтчета.ДанныеМногостраничныхРазделов.Свойство("Раздел1") Тогда
			
			ПерваяСтраницаРаздела1 = ДанныеРеглОтчета.ДанныеМногостраничныхРазделов.Раздел1[0];
			
			Если ПерваяСтраницаРаздела1.Данные.Свойство("П000010001003")
				И ПерваяСтраницаРаздела1.ДанныеДопСтрок.Колонки.Найти("П000010000101") <> Неопределено
				И ПерваяСтраницаРаздела1.ДанныеДопСтрок.Колонки.Найти("П000010000102") <> Неопределено Тогда
				
				Для Каждого Страница Из ДанныеРеглОтчета.ДанныеМногостраничныхРазделов.Раздел1 Цикл
					ОКТМО = Страница.Данные["П000010001003"];
					Для Каждого ДопСтрока Из Страница.ДанныеДопСтрок Цикл
						Сумма = ТаблицаДанныхРеглОтчета.Добавить();
						Сумма.Период = ЭкземплярРеглОтчета.ДатаНачала;
						Сумма.ОКАТО  = ОКТМО;
						Сумма.КБК    = ДопСтрока["П000010000101"];
						Сумма.Сумма  = ДопСтрока["П000010000102"];
					КонецЦикла;
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ТаблицаДанныхРеглОтчета;
	
КонецФункции

Функция ДеревоФормИФорматов() Экспорт
	
	ФормыИФорматы = Новый ДеревоЗначений;
	ФормыИФорматы.Колонки.Добавить("Код");
	ФормыИФорматы.Колонки.Добавить("ДатаПриказа");
	ФормыИФорматы.Колонки.Добавить("НомерПриказа");
	ФормыИФорматы.Колонки.Добавить("ДатаНачалаДействия");
	ФормыИФорматы.Колонки.Добавить("ДатаОкончанияДействия");
	ФормыИФорматы.Колонки.Добавить("ИмяОбъекта");
	ФормыИФорматы.Колонки.Добавить("Описание");
	
	Форма2016Кв2 = ОпределитьФормуВДеревеФормИФорматов(ФормыИФорматы,
	"1151089", '2016-01-12', "ММВ-7-3/1@", "ФормаОтчета2016Кв2");
	ОпределитьФорматВДеревеФормИФорматов(Форма2016Кв2, "5.01");
	
	Форма2021Кв1 = ОпределитьФормуВДеревеФормИФорматов(ФормыИФорматы,
	"1151089", '2020-10-13', "ЕД-7-3/747@", "ФормаОтчета2021Кв1");
	ОпределитьФорматВДеревеФормИФорматов(Форма2021Кв1, "5.02");
	
	Форма2022Кв2 = ОпределитьФормуВДеревеФормИФорматов(ФормыИФорматы,
	"1151089", '2022-01-12', "ЕД-7-3/8@", "ФормаОтчета2022Кв2");
	ОпределитьФорматВДеревеФормИФорматов(Форма2022Кв2, "5.03");
	
	Форма2023Кв1 = ОпределитьФормуВДеревеФормИФорматов(ФормыИФорматы,
	"1151089", '2022-10-12', "ЕД-7-3/923@", "ФормаОтчета2023Кв1");
	ОпределитьФорматВДеревеФормИФорматов(Форма2023Кв1, "5.04");
	
	Форма2025Кв2 = ОпределитьФормуВДеревеФормИФорматов(ФормыИФорматы,
	"1151089", '2025-01-31', "ЕД-7-3/65@", "ФормаОтчета2025Кв2");
	ОпределитьФорматВДеревеФормИФорматов(Форма2025Кв2, "5.05");
	
	Возврат ФормыИФорматы;
	
КонецФункции

Функция ОпределитьФормуВДеревеФормИФорматов(ДеревоФормИФорматов, Код, ДатаПриказа = '00010101', НомерПриказа = "",
			ИмяОбъекта = "", ДатаНачалаДействия = '00010101', ДатаОкончанияДействия = '00010101', Описание = "")
	
	НовСтр = ДеревоФормИФорматов.Строки.Добавить();
	НовСтр.Код = СокрЛП(Код);
	НовСтр.ДатаПриказа = ДатаПриказа;
	НовСтр.НомерПриказа = СокрЛП(НомерПриказа);
	НовСтр.ДатаНачалаДействия = ДатаНачалаДействия;
	НовСтр.ДатаОкончанияДействия = ДатаОкончанияДействия;
	НовСтр.ИмяОбъекта = СокрЛП(ИмяОбъекта);
	НовСтр.Описание = СокрЛП(Описание);
	Возврат НовСтр;
	
КонецФункции

Функция ОпределитьФорматВДеревеФормИФорматов(Форма, Версия, ДатаПриказа = '00010101', НомерПриказа = "",
			ДатаНачалаДействия = Неопределено, ДатаОкончанияДействия = Неопределено, ИмяОбъекта = "", Описание = "")
	
	НовСтр = Форма.Строки.Добавить();
	НовСтр.Код = СокрЛП(Версия);
	НовСтр.ДатаПриказа = ДатаПриказа;
	НовСтр.НомерПриказа = СокрЛП(НомерПриказа);
	НовСтр.ДатаНачалаДействия = ?(ДатаНачалаДействия = Неопределено, Форма.ДатаНачалаДействия, ДатаНачалаДействия);
	НовСтр.ДатаОкончанияДействия = ?(ДатаОкончанияДействия = Неопределено, Форма.ДатаОкончанияДействия, ДатаОкончанияДействия);
	НовСтр.ИмяОбъекта = СокрЛП(ИмяОбъекта);
	НовСтр.Описание = СокрЛП(Описание);
	Возврат НовСтр;
	
КонецФункции

#Область ПоказателиОценкиРискаВыезднойНалоговойПроверки

Процедура ПоказателиОценкиРискаВыезднойНалоговойПроверки(ТаблицаПоказателей, ОписаниеОтчета) Экспорт 
	
	СведенияОтчета = ОписаниеОтчета.РегламентированныйОтчет.ДанныеОтчета.Получить();
	
	ЗначениеПоказателя = 0;
	
	Если ОписаниеОтчета.ВыбраннаяФорма = "ФормаОтчета2025Кв2"
		ИЛИ ОписаниеОтчета.ВыбраннаяФорма = "ФормаОтчета2023Кв1"
		ИЛИ ОписаниеОтчета.ВыбраннаяФорма = "ФормаОтчета2022Кв2"
		ИЛИ ОписаниеОтчета.ВыбраннаяФорма = "ФормаОтчета2021Кв1" Тогда
		
	ИначеЕсли ОписаниеОтчета.ВыбраннаяФорма = "ФормаОтчета2016Кв2" Тогда
		
		Для Каждого Лист Из СведенияОтчета.ДанныеМногостраничныхРазделов.Раздел1 Цикл
			ДанныеЛиста = Лист.Данные;
			НомерСтроки = 1;
			Постфикс = "_" + РегламентированнаяОтчетность.ЧислоВСтрокуЧГ0(НомерСтроки);
			Пока ДанныеЛиста.Свойство("П000010000102" + Постфикс) Цикл
				ЗначениеПоказателя = ЗначениеПоказателя
				+ РегламентированнаяОтчетность.ПоказательОтчета(ДанныеЛиста, "П000010000102" + Постфикс)
				- РегламентированнаяОтчетность.ПоказательОтчета(ДанныеЛиста, "П000010000103" + Постфикс);
				НомерСтроки = НомерСтроки + 1;
				Постфикс = "_" + РегламентированнаяОтчетность.ЧислоВСтрокуЧГ0(НомерСтроки);
			КонецЦикла;
		КонецЦикла;
		
	КонецЕсли;
	
	НоваяСтрока = ТаблицаПоказателей.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока, ОписаниеОтчета);
	НоваяСтрока.Показатель = "СуммаАкцизовГСМ";
	НоваяСтрока.ЗначениеПоказателя = ЗначениеПоказателя;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли