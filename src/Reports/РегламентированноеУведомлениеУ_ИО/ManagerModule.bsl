#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьТаблицуПримененияФорматов() Экспорт 
	Результат = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюТаблицуПримененияФорматов();
	
	Стр = Результат.Добавить();
	Стр.ИмяФормы = "Форма2019_1";
	Стр.КНД = "1120411";
	Стр.ВерсияФормата = "5.02";
	
	Возврат Результат;
КонецФункции

Функция ДанноеУведомлениеДоступноДляОрганизации() Экспорт 
	Возврат Истина;
КонецФункции

Функция ДанноеУведомлениеДоступноДляИП() Экспорт 
	Возврат Истина;
КонецФункции

Функция ПолучитьОсновнуюФорму() Экспорт 
	Возврат "";
КонецФункции

Функция ПолучитьФормуПоУмолчанию() Экспорт 
	Возврат "";
КонецФункции

Функция ПолучитьТаблицуФорм() Экспорт 
	Результат = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюТаблицуФормУведомления();
	
	Стр = Результат.Добавить();
	Стр.ИмяФормы = "Форма2019_1";
	Стр.ОписаниеФормы = "Приказ ФНС России от 05.07.2019 № ММВ-7-13/338@";
	
	Возврат Результат;
КонецФункции

Функция СформироватьСписокЛистов(Объект) Экспорт
	Если Объект.ИмяФормы = "Форма2019_1" Тогда 
		Возврат СформироватьСписокЛистовФорма2019_1(Объект);
	КонецЕсли;
КонецФункции

Функция СформироватьВыгрузкуИПолучитьДанные(ДокОбъект) Экспорт 
КонецФункции

Функция ЭлектронноеПредставление(Объект, ИмяФормы, УникальныйИдентификатор) Экспорт
	Если ИмяФормы = "Форма2019_1" Тогда
		Возврат ЭлектронноеПредставление_Форма2019_1(Объект, УникальныйИдентификатор);
	КонецЕсли;
КонецФункции

Функция ПроверитьДокументСВыводомВТаблицу(Объект, ИмяФормы, УникальныйИдентификатор) Экспорт 
	Если ИмяФормы = "Форма2019_1" Тогда 
		Возврат ПроверитьДокументСВыводомВТаблицу_Форма2019_1(
			УведомлениеОСпецрежимахНалогообложения.ДанныеУведомленияДляВыгрузки(Объект), УникальныйИдентификатор);
	КонецЕсли;
КонецФункции

Функция ИдентификаторФайлаЭлектронногоПредставления(СведенияОтправки)
	Префикс = "UT_UVUCHINO";
	Возврат Документы.УведомлениеОСпецрежимахНалогообложения.ИдентификаторФайлаЭлектронногоПредставления(Префикс, СведенияОтправки);
КонецФункции

Функция ОсновныеСведенияЭлектронногоПредставления_Форма2019_1(Объект, УникальныйИдентификатор)
	ОсновныеСведения = Документы.УведомлениеОСпецрежимахНалогообложения.НачальнаяИнициализацияОбщихРеквизитовВыгрузки(Объект);
	Данные = Объект.ДанныеУведомления.Получить();
	Титульный = Данные.ДанныеУведомления.Титульная;
	
	ОсновныеСведения.Вставить("ПрНП", Титульный.ПрНП);
	ОсновныеСведения.Вставить("ОснУвед", Титульный.ОснУвед);
	ОсновныеСведения.Вставить("НомКорр", Формат(?(ЗначениеЗаполнено(Титульный.НомерКорректировки), Титульный.НомерКорректировки, 0), "ЧН=0"));
	ОсновныеСведения.Вставить("EmailПодп", Титульный.ЭлАдрес);
	ОсновныеСведения.Вставить("ИННПодп", Титульный.ИННФЛ);
	ОсновныеСведения.Вставить("ТлфПодп", Титульный.Тлф);
	ОсновныеСведения.Вставить("НаимДок", Титульный.НаимДок);
	ОсновныеСведения.Вставить("ПрПодп",  Титульный.ПРИЗНАК_НП_ПОДВАЛ);
	ОсновныеСведения.Вставить("НаимДок", Титульный.НаимДок);
	ОсновныеСведения.Вставить("ДатаДок", Формат(Титульный.ДАТА_ПОДПИСИ, "ДФ=dd.MM.yyyy"));
	
	ИдентификаторФайла = ИдентификаторФайлаЭлектронногоПредставления(ОсновныеСведения);
	ОсновныеСведения.Вставить("ИдФайл", ИдентификаторФайла);
	
	Возврат ОсновныеСведения;
КонецФункции

Функция ЭлектронноеПредставление_Форма2019_1(Объект, УникальныйИдентификатор)
	СведенияЭлектронногоПредставления = УведомлениеОСпецрежимахНалогообложения.СведенияЭлектронногоПредставления();
	ДанныеУведомления = УведомлениеОСпецрежимахНалогообложения.ДанныеУведомленияДляВыгрузки(Объект);
	Ошибки = ПроверитьДокументСВыводомВТаблицу_Форма2019_1(ДанныеУведомления, УникальныйИдентификатор);
	УведомлениеОСпецрежимахНалогообложения.СообщитьОшибкиПриПроверкеВыгрузки(Объект, Ошибки, ДанныеУведомления);
	
	ОсновныеСведения = ОсновныеСведенияЭлектронногоПредставления_Форма2019_1(Объект, УникальныйИдентификатор);
	СтруктураВыгрузки = Документы.УведомлениеОСпецрежимахНалогообложения.ИзвлечьСтруктуруXMLУведомления(Объект.ИмяОтчета, "СхемаВыгрузкиФорма2019_1");
	ДополнитьПараметры(ДанныеУведомления);
	УведомлениеОСпецрежимахНалогообложения.ТиповоеЗаполнениеДанными(ДанныеУведомления, ОсновныеСведения, СтруктураВыгрузки);
	УведомлениеОСпецрежимахНалогообложения.ВыгрузитьДеревоВТаблицу(СтруктураВыгрузки, ОсновныеСведения, СведенияЭлектронногоПредставления);
	Возврат СведенияЭлектронногоПредставления;
КонецФункции

Процедура ДополнитьПараметры(Параметры)
	Если Не ЗначениеЗаполнено(Параметры.ДанныеУведомления.Титульная.НомерКорректировки) Тогда 
		Параметры.ДанныеУведомления.Титульная.НомерКорректировки = "0";
	КонецЕсли;
	
	Если Параметры.ДанныеУведомления.Свойство("ЛистСведФЛ") Тогда 
		ПарамСтр = Параметры.ДанныеУведомления.ЛистСведФЛ;
		ПарамСтр.Вставить("ДатаДок", Формат(ПарамСтр.ДатаДокДата, "ДФ=dd.MM.yyyy; ДП=' '"));
		ПарамСтр.Вставить("ДатаДок1", Формат(ПарамСтр.ДатаДокДата1, "ДФ=dd.MM.yyyy; ДП=' '"));
		ПарамСтр.Вставить("ДатаРожд", Формат(ПарамСтр.ДатаРождДата, "ДФ=dd.MM.yyyy; ДП=' '"));
	КонецЕсли;
	
	Для Каждого Стр Из Параметры.ДанныеМногостраничныхРазделов.ЛистА Цикл
		ПарамСтр = Стр.Значение;
		Если Не ЗначениеЗаполнено(ПарамСтр.НомерКонтрЛицЧисло) Тогда 
			Для Каждого КЗ Из ПарамСтр Цикл 
				ПарамСтр[КЗ.Ключ] = Неопределено;
			КонецЦикла;
		ИначеЕсли УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(ПарамСтр) Тогда
			ПарамСтр.Вставить("НомерИО", "ИО-" + Формат(ПарамСтр.НомерКонтрЛицЧисло, "ЧЦ=5; ЧВН=; ЧГ="));
			ПарамСтр.Вставить("ДатаИзмДолиУчСтр", Формат(ПарамСтр.ДатаИзмДолиУч, "ДФ=dd.MM.yyyy; ДП=' '"));
			ПарамСтр.Вставить("ДатаКонУчСтр", Формат(ПарамСтр.ДатаКонУч, "ДФ=dd.MM.yyyy; ДП=' '"));
			ПарамСтр.Вставить("ДатаВознУчСтр", Формат(ПарамСтр.ДатаВознУч, "ДФ=dd.MM.yyyy; ДП=' '"));
			ПарамСтр.Вставить("ДатаРегИОСтр", Формат(ПарамСтр.ДатаРегИО, "ДФ=dd.MM.yyyy; ДП=' '"));
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Стр Из Параметры.ДанныеМногостраничныхРазделов.ЛистБ Цикл
		ПарамСтр = Стр.Значение;
		Если Не ЗначениеЗаполнено(ПарамСтр.НомерРосОргЧисло) Тогда 
			Для Каждого КЗ Из ПарамСтр Цикл 
				ПарамСтр[КЗ.Ключ] = Неопределено;
			КонецЦикла;
		ИначеЕсли УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(ПарамСтр) Тогда
			ПарамСтр.Вставить("НомерРО", "РО-" + Формат(ПарамСтр.НомерРосОргЧисло, "ЧЦ=5; ЧВН=; ЧГ="));
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Стр Из Параметры.ДанныеМногостраничныхРазделов.ЛистВ Цикл
		ПарамСтр = Стр.Значение;
		Если Не ЗначениеЗаполнено(ПарамСтр.НомерИСЧисло) Тогда 
			Для Каждого КЗ Из ПарамСтр Цикл 
				ПарамСтр[КЗ.Ключ] = Неопределено;
			КонецЦикла;
		ИначеЕсли УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(ПарамСтр) Тогда
			ПарамСтр.Вставить("НомерИС", "ИС-" + Формат(ПарамСтр.НомерИСЧисло, "ЧЦ=5; ЧВН=; ЧГ="));
			ПарамСтр.Вставить("ДатаУчрСтр", Формат(ПарамСтр.ДатаУчр, "ДФ=dd.MM.yyyy; ДП=' '"));
			ПарамСтр.Вставить("ДатаКонУчСтр", Формат(ПарамСтр.ДатаКонУч, "ДФ=dd.MM.yyyy; ДП=' '"));
			ПарамСтр.Вставить("ДатаВознУчСтр", Формат(ПарамСтр.ДатаВознУч, "ДФ=dd.MM.yyyy; ДП=' '"));
		КонецЕсли;
	КонецЦикла;
	
	Параметры.ДанныеМногостраничныхРазделов.Вставить("Лист_Г0", Новый СписокЗначений);
	Параметры.ДанныеМногостраничныхРазделов.Вставить("Лист_Г00", Новый СписокЗначений);
	Параметры.ДанныеМногостраничныхРазделов.Вставить("Лист_Г000", Новый СписокЗначений);
	
	С_Г0 = Новый Соответствие;
	Для Каждого Стр Из Параметры.ДанныеМногостраничныхРазделов.ЛистГ Цикл
		ПарамСтр = Стр.Значение;
		Если ЗначениеЗаполнено(ПарамСтр.НомерИОЧисло) И УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(ПарамСтр) Тогда
			НомерИО = "ИО-" + Формат(ПарамСтр.НомерИОЧисло, "ЧЦ=5; ЧВН=; ЧГ=");
			Если С_Г0[НомерИО] = Неопределено Тогда 
				Стр_Г0 = Новый Структура("УИД,НаимОргЛат,ДоляУчКосвИт,НомерИО",Новый УникальныйИдентификатор, ПарамСтр.НаимОргЛат, ПарамСтр.ДоляУчКосвИт, НомерИО);
				С_Г0[НомерИО] = Стр_Г0;
				Параметры.ДанныеМногостраничныхРазделов.Лист_Г0.Добавить(Стр_Г0);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	С_Г00 = Новый Соответствие;
	Для Каждого Стр Из Параметры.ДанныеМногостраничныхРазделов.ЛистГ Цикл
		ПарамСтр = Стр.Значение;
		Если ЗначениеЗаполнено(ПарамСтр.НомерИОЧисло) И УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(ПарамСтр) Тогда
			НомерИО = "ИО-" + Формат(ПарамСтр.НомерИОЧисло, "ЧЦ=5; ЧВН=; ЧГ=");
			НомерПосл = Формат(ПарамСтр.НомерПосл, "ЧЦ=5; ЧВН=; ЧГ=");
			Если С_Г00[НомерИО + НомерПосл] = Неопределено Тогда 
				Стр_Г00 = Новый Структура("УИД,УИДРодителя,НомерПосл,ДоляКосвПослИт", Новый УникальныйИдентификатор, С_Г0[НомерИО].УИД, НомерПосл, ПарамСтр.ДоляКосвПослИт);
				С_Г00[НомерИО + НомерПосл] = Стр_Г00;
				Параметры.ДанныеМногостраничныхРазделов.Лист_Г00.Добавить(Стр_Г00);
				
				Для Каждого СтрМнг Из Параметры.ДанныеДопСтрокБД.МнгСтр.НайтиСтроки(Новый Структура("УИД", ПарамСтр.УИД)) Цикл 
					Стр_Г000 = Новый Структура("УИД,УИДРодителя,НомерУч,ДоляУчПрям,ДоляУчКосв", Новый УникальныйИдентификатор, С_Г00[НомерИО + НомерПосл].УИД, СтрМнг.НомерУч, СтрМнг.ДоляУчПрям, СтрМнг.ДоляУчКосв);
					Параметры.ДанныеМногостраничныхРазделов.Лист_Г000.Добавить(Стр_Г000);
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Функция СформироватьСписокЛистовФорма2019_1(Объект) Экспорт
	СписокИспользуемыхМакетов = УведомлениеОСпецрежимахНалогообложения.ЗагрузитьМакетыИзАрхива(Объект, "БланкиПечати_");
	Листы = Новый СписокЗначений;
	
	ПечатнаяФорма = УведомлениеОСпецрежимахНалогообложения.НовыйПустойЛист();
	СтруктураПараметров = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Ссылка, "ДанныеУведомления").Получить();
	ИННКПП = УведомлениеОСпецрежимахНалогообложения.ТиповаяСтруктураИННКППДляПечати(Объект, СтруктураПараметров.ДанныеУведомления.Титульная);
	
	НомСтр = 0;
	УведомлениеОСпецрежимахНалогообложения.НапечататьСтруктуру(Объект.ИмяОтчета, СтруктураПараметров.ДанныеУведомления["Титульная"], НомСтр,
		УведомлениеОСпецрежимахНалогообложения.ПустойМакетИзСписка(Объект, СписокИспользуемыхМакетов, "Печать_Форма2019_1_Титульная"), ПечатнаяФорма, ИННКПП);
	УведомлениеОСпецрежимахНалогообложения.ПоложитьПФВСписокЛистов(Объект, Листы, ПечатнаяФорма, НомСтр);
	
	Если Не РегламентированнаяОтчетностьПереопределяемый.ЭтоЮридическоеЛицо(Объект.Организация) Тогда 
		УведомлениеОСпецрежимахНалогообложения.НапечататьСтруктуру(Объект.ИмяОтчета, СтруктураПараметров.ДанныеУведомления["ЛистСведФЛ"], НомСтр,
			УведомлениеОСпецрежимахНалогообложения.ПустойМакетИзСписка(Объект, СписокИспользуемыхМакетов, "Печать_Форма2019_1_ЛистСведФЛ"), ПечатнаяФорма, ИННКПП);
		УведомлениеОСпецрежимахНалогообложения.ПоложитьПФВСписокЛистов(Объект, Листы, ПечатнаяФорма, НомСтр);
	КонецЕсли;
	
	Для Каждого Стр Из СтруктураПараметров.ДанныеМногостраничныхРазделов.ЛистА Цикл 
		Если УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(Стр.Значение) Тогда 
			Стр.Значение.НомерКонтрЛицЧисло = Формат(Стр.Значение.НомерКонтрЛицЧисло, "ЧЦ=5; ЧН=00000; ЧВН=; ЧГ=");
			УведомлениеОСпецрежимахНалогообложения.НапечататьСтруктуру(Объект.ИмяОтчета, Стр.Значение, НомСтр,
				УведомлениеОСпецрежимахНалогообложения.ПустойМакетИзСписка(Объект, СписокИспользуемыхМакетов, "Печать_Форма2019_1_ЛистА"), ПечатнаяФорма, ИННКПП);
			УведомлениеОСпецрежимахНалогообложения.ПоложитьПФВСписокЛистов(Объект, Листы, ПечатнаяФорма, НомСтр);
		КонецЕсли;
	КонецЦикла;
	Для Каждого Стр Из СтруктураПараметров.ДанныеМногостраничныхРазделов.ЛистБ Цикл 
		Если УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(Стр.Значение) Тогда
			Стр.Значение.НомерРосОргЧисло = Формат(Стр.Значение.НомерРосОргЧисло, "ЧЦ=5; ЧН=00000; ЧВН=; ЧГ=");
			УведомлениеОСпецрежимахНалогообложения.НапечататьСтруктуру(Объект.ИмяОтчета, Стр.Значение, НомСтр,
				УведомлениеОСпецрежимахНалогообложения.ПустойМакетИзСписка(Объект, СписокИспользуемыхМакетов, "Печать_Форма2019_1_ЛистБ"), ПечатнаяФорма, ИННКПП);
			УведомлениеОСпецрежимахНалогообложения.ПоложитьПФВСписокЛистов(Объект, Листы, ПечатнаяФорма, НомСтр);
		КонецЕсли;
	КонецЦикла;
	Для Каждого Стр Из СтруктураПараметров.ДанныеМногостраничныхРазделов.ЛистВ Цикл 
		Если УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(Стр.Значение) Тогда
			Стр.Значение.НомерИСЧисло = Формат(Стр.Значение.НомерИСЧисло, "ЧЦ=5; ЧН=00000; ЧВН=; ЧГ=");
			УведомлениеОСпецрежимахНалогообложения.НапечататьСтруктуру(Объект.ИмяОтчета, Стр.Значение, НомСтр,
				УведомлениеОСпецрежимахНалогообложения.ПустойМакетИзСписка(Объект, СписокИспользуемыхМакетов, "Печать_Форма2019_1_ЛистВ"), ПечатнаяФорма, ИННКПП);
			УведомлениеОСпецрежимахНалогообложения.ПоложитьПФВСписокЛистов(Объект, Листы, ПечатнаяФорма, НомСтр);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Стр Из СтруктураПараметров.ДанныеМногостраничныхРазделов.ЛистГ Цикл 
		Если УведомлениеОСпецрежимахНалогообложения.СтраницаЗаполнена(Стр.Значение) Тогда
			Стр.Значение.НомерИОЧисло = Формат(Стр.Значение.НомерИОЧисло, "ЧЦ=5; ЧН=00000; ЧВН=; ЧГ=");
		КонецЕсли;
	КонецЦикла;
	УведомлениеОСпецрежимахНалогообложения.НапечататьСтраницыСМногострочнойЧастью(Объект, Листы, СтруктураПараметров,
		"ЛистГ", "МнгСтр", УведомлениеОСпецрежимахНалогообложения.ПустойМакетИзСписка(Объект, СписокИспользуемыхМакетов, "Печать_Форма2019_1_ЛистГ"), НомСтр, ИННКПП);
	
	Возврат Листы;
КонецФункции

Функция ПроверитьДокументСВыводомВТаблицу_Форма2019_1(Данные, УникальныйИдентификатор)
	ТаблицаОшибок = Новый СписокЗначений;
	УведомлениеОСпецрежимахНалогообложения.ПроверкаАктуальностиФормыПриВыгрузке(
		Данные.Объект.ИмяФормы, ТаблицаОшибок, ПолучитьТаблицуФорм());
	
	Титульная = Данные.ДанныеУведомления.Титульная;
	
	Если Не ЗначениеЗаполнено(Титульная.ПРИЗНАК_НП_ПОДВАЛ) Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
			"Не указан признак подписанта", "Титульная", "ПРИЗНАК_НП_ПОДВАЛ"));
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Титульная.ДАТА_ПОДПИСИ) Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
			"Не указана дата подписи", "Титульная", "ДАТА_ПОДПИСИ"));
	КонецЕсли;
	УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаКодаНО(Титульная.КодНО, ТаблицаОшибок, "Титульная");
	Если Не ЗначениеЗаполнено(Титульная.ОснУвед) Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
			"Не указано основание подачи уведомления", "Титульная", "ОснУвед"));
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Титульная.ПрНП) Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
			"Не указан признак налогоплательщика", "Титульная", "ПрНП"));
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Титульная.НаимДок) И (Титульная.ПРИЗНАК_НП_ПОДВАЛ = "2" Или Титульная.ПРИЗНАК_НП_ПОДВАЛ = "4") Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
			"Не указан документ (обязателен при подаче представителем)", "Титульная", "НаимДок"));
	КонецЕсли;
	Если (Титульная.ПРИЗНАК_НП_ПОДВАЛ = "2" Или Титульная.ПРИЗНАК_НП_ПОДВАЛ = "3" Или Титульная.ПРИЗНАК_НП_ПОДВАЛ = "4") Тогда 
		УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаПодписантаНалоговойОтчетности(Данные, ТаблицаОшибок, "Титульная", Истина);
	Иначе
		УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаПодписантаНалоговойОтчетности(Данные, ТаблицаОшибок, "Титульная", Ложь);
	КонецЕсли;
	УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаИНН(Титульная, ТаблицаОшибок, "Титульная", "ИННФЛ", Ложь, Ложь);
	
	Если РегламентированнаяОтчетностьПереопределяемый.ЭтоЮридическоеЛицо(Данные.Организация) Тогда 
		УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаИННКПП(Истина, Титульная, ТаблицаОшибок);
		Если Не ЗначениеЗаполнено(Титульная.Наименование) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указано наименование организации", "Титульная", "Наименование"));
		КонецЕсли;
	Иначе
		УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаИНН(Титульная, ТаблицаОшибок, "Титульная", "ИНН", Ложь, Ложь);
		
		ЛистСведФЛ = Данные.ДанныеУведомления.ЛистСведФЛ;
		Если Не ЗначениеЗаполнено(ЛистСведФЛ.Фамилия) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указана фамилия", "ЛистСведФЛ", "Фамилия"));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ЛистСведФЛ.Имя) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указано имя", "ЛистСведФЛ", "Имя"));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ЛистСведФЛ.ДатаРождДата) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указана дата рождения", "ЛистСведФЛ", "ДатаРождДата"));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ЛистСведФЛ.МестоРожд) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указано место рождения", "ЛистСведФЛ", "МестоРожд"));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ЛистСведФЛ.ПрГражд) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указана признак гражданства", "ЛистСведФЛ", "ПрГражд"));
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ЛистСведФЛ.КодВидДок) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указан код документа", "ЛистСведФЛ", "КодВидДок"));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ЛистСведФЛ.ДатаДокДата) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указана дата выдачи документа", "ЛистСведФЛ", "ДатаДокДата"));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ЛистСведФЛ.СерНомДок) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указана серия и номер документа", "ЛистСведФЛ", "СерНомДок"));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ЛистСведФЛ.ВыдДок) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указан орган, выдавший документ", "ЛистСведФЛ", "ВыдДок"));
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ЛистСведФЛ.КодВидДок1)
			Или ЗначениеЗаполнено(ЛистСведФЛ.ДатаДокДата1)
			Или ЗначениеЗаполнено(ЛистСведФЛ.СерНомДок1)
			Или ЗначениеЗаполнено(ЛистСведФЛ.ВыдДок1) Тогда 
		
			Если Не ЗначениеЗаполнено(ЛистСведФЛ.КодВидДок1) Тогда 
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указан код документа", "ЛистСведФЛ", "КодВидДок1"));
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ЛистСведФЛ.ДатаДокДата1) Тогда 
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указана дата выдачи документа", "ЛистСведФЛ", "ДатаДокДата1"));
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ЛистСведФЛ.СерНомДок1) Тогда 
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указана серия и номер документа", "ЛистСведФЛ", "СерНомДок1"));
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ЛистСведФЛ.ВыдДок1) Тогда 
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указан орган, выдавший документ", "ЛистСведФЛ", "ВыдДок1"));
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	НаличиеАВ = Ложь;
	Для Каждого Лист Из Данные.ДанныеМногостраничныхРазделов.ЛистА Цикл
		ЛистТек = Лист.Значение;
		Если Не ЗначениеЗаполнено(ЛистТек.НомерКонтрЛицЧисло) Тогда 
			Продолжить;
		КонецЕсли;
		
		НаличиеАВ = Истина;
		Если Не ЗначениеЗаполнено(ЛистТек.НаимОргЛат) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указано наименование организации (лист А)", "ЛистА", "НаимОргЛат", ЛистТек.УИД));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ЛистТек.СтрРег) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указана страна регистрации (лист А)", "ЛистА", "СтрРег", ЛистТек.УИД));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ЛистТек.РегНомер) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указан регистрационный номер (лист А)", "ЛистА", "РегНомер", ЛистТек.УИД));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ЛистТек.Участие) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указан признак участия (лист А)", "ЛистА", "Участие", ЛистТек.УИД));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ЛистТек.ДатаВознУч) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указана дата (лист А)", "ЛистА", "ДатаВознУч", ЛистТек.УИД));
		КонецЕсли;
		
		Если Титульная.ОснУвед = "4" И ЛистТек.ДатаВознУч <> Дата('20150515') Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"При основании подачи уведомления ""4"" дата должна принимать значение 15.05.2015 (лист А)", "ЛистА", "ДатаВознУч", ЛистТек.УИД));
		КонецЕсли;
		Если (Титульная.ОснУвед = "2" Или Титульная.ОснУвед = "7") И Не ЗначениеЗаполнено(ЛистТек.ДатаИзмДолиУч) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"При основании подачи уведомления ""2"" или ""7"" должна быть указана дата изменения (лист А)", "ЛистА", "ДатаИзмДолиУч", ЛистТек.УИД));
		КонецЕсли;
		Если (Титульная.ОснУвед = "3") И Не ЗначениеЗаполнено(ЛистТек.ДатаКонУч) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"При основании подачи уведомления ""3"" должна быть указана дата окончания (лист А)", "ЛистА", "ДатаКонУч", ЛистТек.УИД));
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Лист Из Данные.ДанныеМногостраничныхРазделов.ЛистБ Цикл
		ЛистТек = Лист.Значение;
		Если Не ЗначениеЗаполнено(ЛистТек.НомерРосОргЧисло) Тогда 
			Продолжить;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ЛистТек.НаимОрг) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указано наименование организации (лист Б)", "ЛистБ", "НаимОрг", ЛистТек.УИД));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ЛистТек.ОГРН) Или Не РегламентированныеДанныеКлиентСервер.ОГРНСоответствуетТребованиям(ЛистТек.ОГРН, Истина, "") Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указан / неправильно указан ОГРН (лист Б)", "ЛистБ", "ОГРН", ЛистТек.УИД));
		КонецЕсли;
		УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаИНН(ЛистТек, ТаблицаОшибок, "ЛистБ", "ИННЮЛ", Истина, Истина);
		УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаКПП(ЛистТек, ТаблицаОшибок, "ЛистБ", "КПП", Истина);
	КонецЦикла;
	
	Для Каждого Лист Из Данные.ДанныеМногостраничныхРазделов.ЛистВ Цикл
		ЛистТек = Лист.Значение;
		Если Не ЗначениеЗаполнено(ЛистТек.НомерИСЧисло) Тогда 
			Продолжить;
		КонецЕсли;
		
		НаличиеАВ = Истина;
		Если Не ЗначениеЗаполнено(ЛистТек.Учредит) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указан признак учредителя (лист В)", "ЛистВ", "Учредит", ЛистТек.УИД));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ЛистТек.КонтрЛиц) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указан признак контролирующего лица (лист В)", "ЛистВ", "КонтрЛиц", ЛистТек.УИД));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ЛистТек.ОргФорм) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указан признак организационной формы (лист В)", "ЛистВ", "ОргФорм", ЛистТек.УИД));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ЛистТек.НаимОргЛат) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указано наименование структуры (лист В)", "ЛистВ", "НаимОргЛат", ЛистТек.УИД));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ЛистТек.ДатаУчр) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указана дата учереждения структуры (лист В)", "ЛистВ", "ДатаУчр", ЛистТек.УИД));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ЛистТек.ДатаВознУч) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указана дата возникновения участия (лист В)", "ЛистВ", "ДатаВознУч", ЛистТек.УИД));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ЛистТек.ДоляУч) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указана доля участия (лист В)", "ЛистВ", "ДоляУч", ЛистТек.УИД));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ЛистТек.СтранаУчрИС) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указана страна регистрации (лист В)", "ЛистВ", "СтранаУчрИС", ЛистТек.УИД));
		КонецЕсли;
		
		Если Титульная.ОснУвед = "4" И ЛистТек.ДатаВознУч <> Дата('20150515') Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"При основании подачи уведомления ""4"" дата должна принимать значение 15.05.2015 (лист В)", "ЛистВ", "ДатаВознУч", ЛистТек.УИД));
		КонецЕсли;
		Если (Титульная.ОснУвед = "3") И Не ЗначениеЗаполнено(ЛистТек.ДатаКонУч) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"При основании подачи уведомления ""3"" должна быть указана дата окончания (лист В)", "ЛистВ", "ДатаКонУч", ЛистТек.УИД));
		КонецЕсли;
	КонецЦикла;
	
	Если Не НаличиеАВ Тогда
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
			"Не заполнены сведения об иностранных организациях", "ЛистА", "НомерКонтрЛицЧисло", Данные.ДанныеМногостраничныхРазделов.ЛистА[0].Значение.УИД));
	КонецЕсли;
	
	Для Каждого Лист Из Данные.ДанныеМногостраничныхРазделов.ЛистГ Цикл
		ЛистТек = Лист.Значение;
		Если Не ЗначениеЗаполнено(ЛистТек.НомерИОЧисло) Тогда 
			Продолжить;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ЛистТек.НаимОргЛат) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указано наименование организации (лист Г)", "ЛистГ", "НаимОргЛат", ЛистТек.УИД));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ЛистТек.НомерПосл) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указан номер последовательности (лист Г)", "ЛистГ", "НомерПосл", ЛистТек.УИД));
		КонецЕсли;
		
		Инд = 0;
		Для Каждого СтрМнг Из Данные.ДанныеДопСтрокБД.МнгСтр.НайтиСтроки(Новый Структура("УИД", ЛистТек.УИД)) Цикл 
			Инд = Инд + 1;
			Если Не ЗначениеЗаполнено(СтрМнг.НомерУч) Тогда 
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указан номер участника последовательности (лист Г)", "ЛистГ", "НомерУч___" + Формат(Инд, "ЧГ="), ЛистТек.УИД));
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	УведомлениеОСпецрежимахНалогообложения.ПроверкаДатВУведомлении(Данные, ТаблицаОшибок);
	УведомлениеОСпецрежимахНалогообложения.ПолнаяПроверкаЗаполненныхПоказателейНаСоотвествиеСписку(
		"СпискиВыбора2019_1", "СхемаВыгрузкиФорма2019_1", Данные.Объект.ИмяОтчета, ТаблицаОшибок, Данные);
	Возврат ТаблицаОшибок;
КонецФункции

Функция ПечатьСразу(Объект, ИмяФормы) Экспорт
КонецФункции

Функция СформироватьМакет(Объект, ИмяФормы) Экспорт
	Если ИмяФормы = "Форма2015_1" Тогда
		Возврат СформироватьМакет_Форма2015_1(Объект);
	КонецЕсли;
КонецФункции

Функция СформироватьМакет_Форма2015_1(Объект)
	ПечатнаяФорма = Новый ТабличныйДокумент;
	ПечатнаяФорма.АвтоМасштаб = Истина;
	ПечатнаяФорма.ПолеСверху = 0;
	ПечатнаяФорма.ПолеСнизу = 0;
	ПечатнаяФорма.ПолеСлева = 0;
	ПечатнаяФорма.ПолеСправа = 0;
	ПечатнаяФорма.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_УведомлениеОСпецрежимах_"+Объект.ВидУведомления.Метаданные().Имя;
	
	МакетУведомления = Отчеты[Объект.ИмяОтчета].ПолучитьМакет("Печать_MXL_Форма2015_1");
	СтруктураПараметров = Объект.ДанныеУведомления.Получить();
	ПараметрыМакета = МакетУведомления.Параметры;
	
	НомерСтраницы = 2 + ?(РегламентированнаяОтчетностьПереопределяемый.ЭтоЮридическоеЛицо(Объект.Организация),0,1);
	Титульный = СтруктураПараметров.Титульный[0];
	СведФЛ = СтруктураПараметров.СведФЛ;
	СведРО = СтруктураПараметров.СведРО;
	СведИО = СтруктураПараметров.СведИО;
	СведУч = СтруктураПараметров.СведУч;
	СведИС = СтруктураПараметров.СведИС;
	СведИСУч = СтруктураПараметров.СведИСУч;
	МногострочнаяЧасть = СтруктураПараметров.МногострочнаяЧасть;
	
#Область ПечатьЛистФЛ
	Если Не РегламентированнаяОтчетностьПереопределяемый.ЭтоЮридическоеЛицо(Объект.Организация) Тогда 
		СведФЛ = СведФЛ[0];
		ОбластьЛистФЛ = МакетУведомления.ПолучитьОбласть("ЛистФЛ");
		ПараметрыМакета = ОбластьЛистФЛ.Параметры;
		
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Титульный.П_ИНН, "ИНН_", ПараметрыМакета, 12);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(СведФЛ.ФАМИЛИЯ_ИП, "Фамилия_", ПараметрыМакета, 35);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(СведФЛ.ИМЯ_ИП, "Имя_", ПараметрыМакета, 35);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(СведФЛ.ОТЧЕСТВО_ИП, "Отчество_", ПараметрыМакета, 35);
		ПараметрыМакета.Пол = СведФЛ.Пол;
		Документы.УведомлениеОСпецрежимахНалогообложения.ДатаВПараметрыМакета(СведФЛ.ДатаРождения, "ДатаРожд_", ПараметрыМакета);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(СведФЛ.МестоРождения, "МестоРожд_", ПараметрыМакета, 80);
		ПараметрыМакета.Гражданство = СведФЛ.Гражданство;
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(СведФЛ.КодСтрИностранец, "КодСтрИнГр_", ПараметрыМакета, 3);
		
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(СведФЛ.КодВидДок1, "КодВидДок_", ПараметрыМакета, 2);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(СведФЛ.СерНомДок1, "СерНомДок_", ПараметрыМакета, 19);
		Документы.УведомлениеОСпецрежимахНалогообложения.ДатаВПараметрыМакета(СведФЛ.ДатаВыдочиДок1, "ДатаВыдачиДок_", ПараметрыМакета);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(СведФЛ.КемВыданДок1, "ВыданДок_", ПараметрыМакета, 35);
		
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(СведФЛ.КодВидДок2, "КодВидДокРег_", ПараметрыМакета, 2);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(СведФЛ.СерНомДок2, "СерНомДокРег_", ПараметрыМакета, 19);
		Документы.УведомлениеОСпецрежимахНалогообложения.ДатаВПараметрыМакета(СведФЛ.ДатаВыдочиДок2, "ДатаВыдачиДокРег_", ПараметрыМакета);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(СведФЛ.КемВыданДок2, "ВыданДокРег_", ПараметрыМакета, 35);
		
		ПараметрыМакета.ПрМЖ = СведФЛ.МЖРФ;
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(СведФЛ.Индекс, "Индекс_", ПараметрыМакета, 6);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(СведФЛ.КодРегиона, "КодСубъекта_", ПараметрыМакета, 2);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(СведФЛ.Район, "Район_", ПараметрыМакета, 33);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(СведФЛ.Город, "Город_", ПараметрыМакета, 33);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(СведФЛ.НаселенныйПункт, "НаселенныйПункт_", ПараметрыМакета, 33);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(СведФЛ.Улица, "Улица_", ПараметрыМакета, 33);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(СведФЛ.Дом, "Дом_", ПараметрыМакета, 8);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(СведФЛ.Корпус, "Корпус_", ПараметрыМакета, 8);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(СведФЛ.Квартира, "Квартира_", ПараметрыМакета, 8);
		
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(СведФЛ.КодСтраныРегистрации, "КодСтрИнГрМЖ_", ПараметрыМакета, 3);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(СведФЛ.МЖИнГос, "МестоПрож_", ПараметрыМакета, 120);
		
		ПечатнаяФорма.Вывести(ОбластьЛистФЛ);
		ПечатнаяФорма.ВывестиГоризонтальныйРазделительСтраниц();
	КонецЕсли;
#КонецОбласти

#Область ПечатьЛистА
	НомЛистА = 1;
	Для Каждого Свед Из СведИО Цикл
		Если Не ЗначениеЗаполнено(Свед.ИО_Номер) Тогда 
			Продолжить;
		КонецЕсли;
		
		ОбластьЛистА = МакетУведомления.ПолучитьОбласть("ЛистА");
		ПараметрыМакета = ОбластьЛистА.Параметры;
		
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Титульный.П_ИНН, "ИНН_", ПараметрыМакета, 12);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Титульный.П_КПП, "КПП_", ПараметрыМакета, 9);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Формат(НомерСтраницы, "ЧЦ=3; ЧВН=; ЧГ="), "СТР_", ПараметрыМакета, 3);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Формат(НомЛистА, "ЧЦ=3; ЧВН=; ЧГ="), "СТРА_", ПараметрыМакета, 3);
		
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Формат(Свед.ИО_Номер, "ЧЦ=5; ЧВН=; ЧГ="), "НомА_", ПараметрыМакета, 5);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Свед.ИО_ПолноеНаименование_RUS, "ОрганизацияНазваниеRUS_", ПараметрыМакета, 160);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Свед.ИО_ПолноеНаименование_EN, "ОрганизацияНазваниеEN_", ПараметрыМакета, 160);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Свед.КодСтранаРегистрации, "КодСтр_", ПараметрыМакета, 3);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Свед.РегНом1, "РегНомСтрРег_", ПараметрыМакета, 80);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Свед.КодНом1, "КодНплСтрРег_", ПараметрыМакета, 80);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Свед.ИнАдр1, "АдрСтрРег_", ПараметрыМакета, 120);
		ПараметрыМакета.ПрУчастие = Свед.ПрУч1;
		Документы.УведомлениеОСпецрежимахНалогообложения.ДатаВПараметрыМакета(Свед.ДатаНач1, "ДатаВознПрава_", ПараметрыМакета);
		Документы.УведомлениеОСпецрежимахНалогообложения.ДатаВПараметрыМакета(Свед.ДатаКон1, "ДатаУтрПрава_", ПараметрыМакета);
		Документы.УведомлениеОСпецрежимахНалогообложения.ЧислоСРазделителемВПараметрыМакета(Свед.ДоляУчастия1, 3, 5, "ДоляУчастия_", ПараметрыМакета);
		
		ПечатнаяФорма.Вывести(ОбластьЛистА);
		ПечатнаяФорма.ВывестиГоризонтальныйРазделительСтраниц();
		НомерСтраницы = НомерСтраницы + 1;
		НомЛистА = НомЛистА + 1;
	КонецЦикла;
#КонецОбласти

#Область ПечатьЛистБ
	НомЛистБ = 1;
	Для Каждого Свед Из СведРО Цикл
		Если Не ЗначениеЗаполнено(Свед.РО_Номер) Тогда 
			Продолжить;
		КонецЕсли;
		
		ОбластьЛистБ = МакетУведомления.ПолучитьОбласть("ЛистБ");
		ПараметрыМакета = ОбластьЛистБ.Параметры;
		
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Титульный.П_ИНН, "ИНН_", ПараметрыМакета, 12);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Титульный.П_КПП, "КПП_", ПараметрыМакета, 9);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Формат(НомерСтраницы, "ЧЦ=3; ЧВН=; ЧГ="), "СТР_", ПараметрыМакета, 3);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Формат(НомЛистБ, "ЧЦ=3; ЧВН=; ЧГ="), "СТРА_", ПараметрыМакета, 3);
		
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Формат(Свед.РО_Номер, "ЧЦ=5; ЧВН=; ЧГ="), "НомА_", ПараметрыМакета, 5);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Свед.ОГРН2, "ОГРНРО_", ПараметрыМакета, 13);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Свед.ИНН2, "ИННРО_", ПараметрыМакета, 10);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Свед.КПП2, "КППРО_", ПараметрыМакета, 9);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Свед.РО_ПолноеНаименование, "ПолнНазвРО_", ПараметрыМакета, 160);
		
		ПечатнаяФорма.Вывести(ОбластьЛистБ);
		ПечатнаяФорма.ВывестиГоризонтальныйРазделительСтраниц();
		НомерСтраницы = НомерСтраницы + 1;
		НомЛистБ = НомЛистБ + 1;
	КонецЦикла;
#КонецОбласти

#Область ПечатьЛистВ
	НомЛистВ = 1;
	НомЛистВ1 = 1;
	Для Каждого Свед Из СведИС Цикл
		Если Не ЗначениеЗаполнено(Свед.НомерСтруктуры) Тогда 
			Продолжить;
		КонецЕсли;
		
		ОбластьЛистВ = МакетУведомления.ПолучитьОбласть("ЛистВ");
		ПараметрыМакета = ОбластьЛистВ.Параметры;
		
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Титульный.П_ИНН, "ИНН_", ПараметрыМакета, 12);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Титульный.П_КПП, "КПП_", ПараметрыМакета, 9);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Формат(НомерСтраницы, "ЧЦ=3; ЧВН=; ЧГ="), "СТР_", ПараметрыМакета, 3);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Формат(НомЛистВ, "ЧЦ=3; ЧВН=; ЧГ="), "СТРА_", ПараметрыМакета, 3);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Формат(Свед.НомерСтруктуры, "ЧЦ=5; ЧВН=; ЧГ="), "НомА_", ПараметрыМакета, 5);
		ПараметрыМакета.ОргФормаКод = Свед.ПрСтр;
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Свед.ИС_ПолноеНаименование_RUS, "ОрганизацияНазваниеRUS_", ПараметрыМакета, 160);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Свед.ИС_ПолноеНаименование_EN, "ОрганизацияНазваниеEN_", ПараметрыМакета, 160);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Свед.ИС_ДокНаименование_RUS, "НаимРеквДокRUS_", ПараметрыМакета, 80);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Свед.ИС_ДокНаименование_EN, "НаимРеквДокEN_", ПараметрыМакета, 80);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Свед.КодСтраныСтр, "КодСтр_", ПараметрыМакета, 3);
		Документы.УведомлениеОСпецрежимахНалогообложения.ДатаВПараметрыМакета(Свед.ДатаРегСтр, "ДатаРегСтр_", ПараметрыМакета);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Свед.РегНомСтр, "РегНомИд_", ПараметрыМакета, 20);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Свед.СведСтр, "ИныеСвед_", ПараметрыМакета, 240);
		
		ПечатнаяФорма.Вывести(ОбластьЛистВ);
		ПечатнаяФорма.ВывестиГоризонтальныйРазделительСтраниц();
		НомерСтраницы = НомерСтраницы + 1;
		НомЛистВ = НомЛистВ + 1;
		
		СведИСУчТек = СведИСУч.НайтиСтроки(Новый Структура("UID_parent", Свед.UID));
		Для Каждого ЛистВ1 Из СведИСУчТек Цикл
			ОбластьЛистВ1 = МакетУведомления.ПолучитьОбласть("ЛистВ1");
			ПараметрыМакета = ОбластьЛистВ1.Параметры;
			
			Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Титульный.П_ИНН, "ИНН_", ПараметрыМакета, 12);
			Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Титульный.П_КПП, "КПП_", ПараметрыМакета, 9);
			Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Формат(НомерСтраницы, "ЧЦ=3; ЧВН=; ЧГ="), "СТР_", ПараметрыМакета, 3);
			Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Формат(НомЛистВ1, "ЧЦ=3; ЧВН=; ЧГ="), "СТРА_", ПараметрыМакета, 3);
			Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Формат(Свед.НомерСтруктуры, "ЧЦ=5; ЧВН=; ЧГ="), "НомА_", ПараметрыМакета, 5);
			ПараметрыМакета.УчКод = Формат(ЛистВ1.ПрУч, "ЧН=0");
			ПараметрыМакета.КонКод = Формат(ЛистВ1.ПрКон, "ЧН=0");
			ПараметрыМакета.ДохКод = Формат(ЛистВ1.ПрДох, "ЧН=0");
			ПараметрыМакета.УчИнСтр = ЛистВ1.УчИнСтр;
			Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(ЛистВ1.ИС_Статус_RUS, "ОрганизацияНазваниеRUS_", ПараметрыМакета, 160);
			Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(ЛистВ1.ИС_Статус_EN, "ОрганизацияНазваниеEN_", ПараметрыМакета, 160);
			Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(ЛистВ1.ИС_НаимДок, "ИныеСвед_", ПараметрыМакета, 280);
			Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(ЛистВ1.ИС_Основания, "Описание_", ПараметрыМакета, 120);
			Документы.УведомлениеОСпецрежимахНалогообложения.ДатаВПараметрыМакета(ЛистВ1.ДатаНачИС, "ДатаВознУч_", ПараметрыМакета);
			Документы.УведомлениеОСпецрежимахНалогообложения.ДатаВПараметрыМакета(ЛистВ1.ДатаКонИС, "ДатаКонУч_", ПараметрыМакета);
			Документы.УведомлениеОСпецрежимахНалогообложения.ЧислоСРазделителемВПараметрыМакета(ЛистВ1.ДоляУчастияИС, 3, 5, "ДоляУч_", ПараметрыМакета);
			
			ПечатнаяФорма.Вывести(ОбластьЛистВ1);
			ПечатнаяФорма.ВывестиГоризонтальныйРазделительСтраниц();
			НомерСтраницы = НомерСтраницы + 1;
			НомЛистВ1 = НомЛистВ1 + 1;
		КонецЦикла;
	КонецЦикла;
#КонецОбласти

#Область ПечатьЛистГ
	НомЛистГ = 1;
	Для Каждого Свед Из СведУч Цикл
		Если Не ЗначениеЗаполнено(Свед.НомерУчастника) Тогда 
			Продолжить;
		КонецЕсли;
		
		ОбластьЛистГ = МакетУведомления.ПолучитьОбласть("ЛистГ_Верх");
		ОбластьЛистГМнг = МакетУведомления.ПолучитьОбласть("ЛистГ_Мнг");
		ОбластьНиз = МакетУведомления.ПолучитьОбласть("ЛистГ_Низ");
		
		ПараметрыЛистГ = ОбластьЛистГ.Параметры;
		МногострочнаяЧастьТекущая = МногострочнаяЧасть.НайтиСтроки(Новый Структура("UID", Свед.UID));
		
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Титульный.П_ИНН, "ИНН_", ПараметрыЛистГ, 12);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Титульный.П_КПП, "КПП_", ПараметрыЛистГ, 9);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Сред(Свед.НомерУчастника, 4), "НомА_", ПараметрыЛистГ, 5);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Свед.ПолноеНаименование, "ПолнНазвВ_", ПараметрыЛистГ, 160);
		Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Формат(Свед.НомерГруппы, "ЧЦ=5; ЧВН=; ЧГ="), "НомГрВ_", ПараметрыЛистГ, 5);
		Документы.УведомлениеОСпецрежимахНалогообложения.ЧислоСРазделителемВПараметрыМакета(Свед.ДоляКосвУчастия, 3, 5, "ДолУчКосвВ_", ПараметрыЛистГ);
		Документы.УведомлениеОСпецрежимахНалогообложения.ЧислоСРазделителемВПараметрыМакета(Свед.ДоляКосвУчастияГр, 3, 5, "ДолУчКосвГрВ_", ПараметрыЛистГ);
		
		СтрокВМногострочке = МногострочнаяЧастьТекущая.Количество();
		КоличествоЛистовГ = Цел(СтрокВМногострочке / 15) + ?(СтрокВМногострочке % 15 = 0,0,1);
		
		Для Инд = 0 По КоличествоЛистовГ - 1 Цикл
			Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Формат(НомерСтраницы, "ЧЦ=3; ЧВН=; ЧГ="), "СТР_", ПараметрыЛистГ, 3);
			Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Формат(НомЛистГ, "ЧЦ=3; ЧВН=; ЧГ="), "СТРА_", ПараметрыЛистГ, 3);
			ПечатнаяФорма.Вывести(ОбластьЛистГ);
			
			Для ИндВн = 0 По 14 Цикл
				ОбластьЛистГМнг = МакетУведомления.ПолучитьОбласть("ЛистГ_Мнг");
				ПарамМнг = ОбластьЛистГМнг.Параметры;
				Сдвиг = Инд * 15 + ИндВн;
				
				Если Сдвиг < СтрокВМногострочке Тогда 
					НУ = СтрЗаменить(МногострочнаяЧастьТекущая[Сдвиг].НомерУчастника,"-","");
					Если ЗначениеЗаполнено(НУ) Тогда 
						Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(НУ, "НомУчГ_", ПарамМнг, 7);
						Документы.УведомлениеОСпецрежимахНалогообложения.ЧислоСРазделителемВПараметрыМакета(МногострочнаяЧастьТекущая[Сдвиг].ДоляПрУчастия, 3, 5, "ДоляПрямУчГ_", ПарамМнг);
						Документы.УведомлениеОСпецрежимахНалогообложения.ЧислоСРазделителемВПараметрыМакета(МногострочнаяЧастьТекущая[Сдвиг].ДоляКосвУчастия, 3, 5, "ДоляКосвУчГ_", ПарамМнг);
					КонецЕсли;
				КонецЕсли;
				ПечатнаяФорма.Вывести(ОбластьЛистГМнг);
			КонецЦикла;
			
			ПечатнаяФорма.Вывести(ОбластьНиз);
			ПечатнаяФорма.ВывестиГоризонтальныйРазделительСтраниц();
			НомЛистГ = НомЛистГ + 1;
			НомерСтраницы = НомерСтраницы + 1;
		КонецЦикла;
	КонецЦикла;
#КонецОбласти

#Область ПечатьЛистТитульный
	ОбластьЛистТитульный = МакетУведомления.ПолучитьОбласть("Титульный");
	ПараметрыМакета = ОбластьЛистТитульный.Параметры;
	
	Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Титульный.П_ИНН, "ИНН_", ПараметрыМакета, 12);
	Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Титульный.П_КПП, "КПП_", ПараметрыМакета, 9);
	Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Титульный.КОД_НО, "КодНО_", ПараметрыМакета, 4);
	Документы.УведомлениеОСпецрежимахНалогообложения.ЧислоВПараметрыМакета(Формат(Титульный.НомерКорректировки, "ЧН=0"), "НомКорр_", ПараметрыМакета, 3);
	ПараметрыМакета.Нпл = Титульный.ПрНпл;
	ПараметрыМакета.Основание = Титульный.Основание;
	Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Титульный.НАИМЕНОВАНИЕ_ОРГАНИЗАЦИИ, "ПолнНазвВ_", ПараметрыМакета, 160);
	Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Титульный.ОГРН, "ОГРН_", ПараметрыМакета, 13);
	Документы.УведомлениеОСпецрежимахНалогообложения.ЧислоВПараметрыМакета(НомерСтраницы - 1, "Страниц_", ПараметрыМакета, 3);
	
	ПараметрыМакета.ПРИЗНАК_НП_ПОДВАЛ = Титульный.ПРИЗНАК_НП_ПОДВАЛ;
	Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Объект.ПодписантФамилия, "ОргПодписантФамилия_", ПараметрыМакета, 20);
	Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Объект.ПодписантИмя, "ОргПодписантИмя_", ПараметрыМакета, 20);
	Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Объект.ПодписантОтчество, "ОргПодписантОтчество_", ПараметрыМакета, 20);
	Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Титульный.ИНН_ПОДПИСАНТА, "ИНН_ПОДПИСАНТА_", ПараметрыМакета, 12);
	Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Титульный.ТЕЛЕФОН, "Телефон_", ПараметрыМакета, 20);
	ПараметрыМакета.EMAIL_ПОДПИСАНТА = Титульный.EMAIL_ПОДПИСАНТА;
	Документы.УведомлениеОСпецрежимахНалогообложения.СтрокаВПараметрыМакета(Титульный.ДОКУМЕНТ_ПРЕДСТАВИТЕЛЯ, "ДокументПредставителя_", ПараметрыМакета, 40);
	Документы.УведомлениеОСпецрежимахНалогообложения.ДатаВПараметрыМакета(Титульный.ДАТА_ПОДПИСИ, "ДатаПодписи_", ПараметрыМакета);
	
	ПечатнаяФормаИтоговая = Новый ТабличныйДокумент;
	ПечатнаяФормаИтоговая.АвтоМасштаб = Истина;
	ПечатнаяФормаИтоговая.ПолеСверху = 0;
	ПечатнаяФормаИтоговая.ПолеСнизу = 0;
	ПечатнаяФормаИтоговая.ПолеСлева = 0;
	ПечатнаяФормаИтоговая.ПолеСправа = 0;
	ПечатнаяФормаИтоговая.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_УведомлениеОСпецрежимах_"+Объект.ВидУведомления.Метаданные().Имя;
	
	ПечатнаяФормаИтоговая.Вывести(ОбластьЛистТитульный);
	ПечатнаяФормаИтоговая.ВывестиГоризонтальныйРазделительСтраниц();
	ПечатнаяФормаИтоговая.Вывести(ПечатнаяФорма);
	ПечатнаяФорма = Неопределено;
#КонецОбласти

	Возврат ПечатнаяФормаИтоговая;
КонецФункции

#КонецОбласти
#КонецЕсли