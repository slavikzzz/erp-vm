#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	Если СхемаКомпоновкиДанных = Неопределено Тогда
		СтандартнаяОбработка = Ложь
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура СформироватьОтчетРасшифровку(Параметры, ДокументРезультат) Экспорт
	
	Перем ДанныеРасшифровки;
	
	ИмяПоля = Лев(Параметры.ИДИменПоказателей[0], 13);
	ДатаНачалаНП = НачалоДня(Параметры.ДатаНачалаПериодаОтчета);
	ДатаКонцаНП  = КонецДня(Параметры.ДатаКонцаПериодаОтчета);
	Организация = Параметры.Организация;
	
	ПараметрыОтбораРегистрацийВНО = Новый Структура;
	Если Параметры.Свойство("КодНО") Тогда
		ПараметрыОтбораРегистрацийВНО.Вставить("КодНО", Параметры.КодНО);
	КонецЕсли;
	Если Параметры.Свойство("ОКТМО") Тогда
		ПараметрыОтбораРегистрацийВНО.Вставить("ОКТМО", Параметры.ОКТМО);
	КонецЕсли;
	Если Параметры.Свойство("КПП") Тогда
		ПараметрыОтбораРегистрацийВНО.Вставить("КПП", Параметры.КПП);
		ПараметрыОтбораРегистрацийВНО.Вставить("ИскатьПоПервымСимволамОКТМО", Истина);
	КонецЕсли;
	РегистрацияВНалоговомОргане = РегламентированнаяОтчетность.РегистрацииОрганизацииВНалоговомОргане(Организация, ПараметрыОтбораРегистрацийВНО);
	НомерКорректировки = 0;
	Если Параметры.Свойство("НомерКорректировки") Тогда
		НомерКорректировки = Параметры.НомерКорректировки;
	КонецЕсли;
	
	ИсточникРасшифровки = ОбщегоНазначения.ОбщийМодуль(Параметры.ИсточникРасшифровки);
	
	// Получаем данные расшифровки.
	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить(Параметры.ИмяСКД); // Имя внешнего набора данных совпадает с ключем варианта.
		
	ИсточникРасшифровки.РасчетПоказателей6НДФЛ(Параметры.ИмяРасчета, ДатаНачалаНП, ДатаКонцаНП, ?(Параметры.ДатаПодписи > ДатаКонцаНП,Параметры.ДатаПодписи, ДатаКонцаНП), Организация, РегистрацияВНалоговомОргане, ВнешниеНаборыДанных, Истина, НомерКорректировки);
	
	// Удалим строки с нулевым значением показателя.
	Данные = ВнешниеНаборыДанных[Параметры.ИмяСКД];
	ВсегоСтрок = Данные.Количество();
	Для Сч = 1 По ВсегоСтрок Цикл
		СтрокаДанных = Данные[ВсегоСтрок - Сч];
		Если Не ЗначениеЗаполнено(СтрокаДанных[ИмяПоля]) Тогда
			Данные.Удалить(СтрокаДанных)
		КонецЕсли;
	КонецЦикла;
	
	// Подготовим табличку для проверки с учетом отборов.
	ДанныеДляПроверки = Неопределено;
	Если Параметры.Свойство("ЗначениеТекущегоПоказателя") Тогда
		ДанныеДляПроверки = Данные.Скопировать();
	КонецЕсли;
	
	// Настраиваем СКД и выводим отчет.
	СхемаКомпоновкиДанных = ПолучитьМакет(Параметры.ИмяСКД);
	
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных));
	КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.ВариантыНастроек[Параметры.ИмяСКД].Настройки);
	НастройкиКомпоновки = КомпоновщикНастроек.Настройки;
	
	Параметр = НастройкиКомпоновки.ПараметрыДанных.Элементы.Найти("НеуказанноеНачисление");
	Если Параметр <> Неопределено Тогда
		Параметр.Значение = НСтр("ru = 'Начисление не указано';
								|en = 'Accrual is not specified'")
	КонецЕсли;
	ОтчетыСервер.ДобавитьВыбранноеПоле(НастройкиКомпоновки, ИмяПоля, ?(Параметры.Свойство("ЗаголовокПоля") И ЗначениеЗаполнено(Параметры.ЗаголовокПоля), Параметры.ЗаголовокПоля, ""));
	Если Параметры.Свойство("КБК") И ЗначениеЗаполнено(Параметры.КБК) Тогда
		ДобавитьОтборПоПараметру(НастройкиКомпоновки, "КБК", Параметры.КБК, ДанныеДляПроверки)
	КонецЕсли;
	Если Параметры.Свойство("СрокПеречисленияНалога") И ЗначениеЗаполнено(Параметры.СрокПеречисленияНалога) Тогда
		ДобавитьОтборПоПараметру(НастройкиКомпоновки, "СрокПеречисленияНалога", Параметры.СрокПеречисленияНалога, ДанныеДляПроверки)
	КонецЕсли;
	Если Параметры.Свойство("ДатаВозвратаНалога") И ЗначениеЗаполнено(Параметры.ДатаВозвратаНалога) Тогда
		ДобавитьОтборПоПараметру(НастройкиКомпоновки, "ДатаВозврата", Параметры.ДатаВозвратаНалога, ДанныеДляПроверки)
	КонецЕсли;
	Если Параметры.Свойство("СтавкаНалога") И ЗначениеЗаполнено(Параметры.СтавкаНалога) Тогда
		ДобавитьОтборПоПараметру(НастройкиКомпоновки, "СтавкаНалога", Параметры.СтавкаНалога, ДанныеДляПроверки)
	КонецЕсли;

	Если ДанныеДляПроверки <> Неопределено Тогда
		ЗарплатаКадры.ВывестиПредупреждениеОРасхожденииПоказателяСРасшифровкой(Параметры.ЗначениеТекущегоПоказателя, ДанныеДляПроверки.Итог(ИмяПоля), ДокументРезультат);
		ДанныеДляПроверки = Неопределено;
	КонецЕсли;
	
	МакетКомпоновки = ЗарплатаКадрыОтчетыБазовый.ЗагрузитьНастройкиРасшифровки(Параметры, КомпоновщикНастроек, СхемаКомпоновкиДанных, ДанныеРасшифровки);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки, Истина);
	
	ЗарплатаКадрыОтчетыБазовый.СохранитьПараметрыРасшифровки(Параметры, СхемаКомпоновкиДанных, ДанныеРасшифровки);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьОтборПоПараметру(НастройкиКомпоновки, ИмяОтбора, ЗначениеОтбора, ДанныеДляПроверки)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(НастройкиКомпоновки.Отбор, ИмяОтбора, ЗначениеОтбора, ВидСравненияКомпоновкиДанных.Равно, , Истина);
	Если ДанныеДляПроверки <> Неопределено И ДанныеДляПроверки.Колонки.Найти(ИмяОтбора) <> Неопределено Тогда
		ДанныеДляПроверки = ДанныеДляПроверки.Скопировать(ДанныеДляПроверки.НайтиСтроки(Новый Структура(ИмяОтбора, ЗначениеОтбора)))
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли