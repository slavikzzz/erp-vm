
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСписокРежимовОтображения();
	ЗагрузитьНастройкиФормы();
	
	Если НЕ Параметры.Свойство("СтатусГрафика", СтатусГрафика) Тогда
		
		СтатусГрафика = СтатусРабочийГрафик();
		
	КонецЕсли;
	
	Если Параметры.Свойство("ПараметрКоманды", Распоряжение) Тогда
		
		СформироватьОтчетВФоне();
		
	Иначе
		
		ДлительнаяОперация = Неопределено;
		
	КонецЕсли;
	
	УстановитьЗаголовокФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НачатьОжиданиеДлительнойОперации Тогда
		
		ПодключитьОбработчикОжидания("НачатьОжиданиеДлительнойОперации", 0.1, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если НЕ ДлительнаяОперация = Неопределено Тогда
		ОтменитьДлительнуюОперацию(ДлительнаяОперация.ИдентификаторЗадания);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РаспоряжениеПриИзменении(Элемент)
	
	СохранитьНастройкиФормыКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура РежимОтображенияПриИзменении(Элемент)
	
	СохранитьНастройкиФормыКлиент();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовДиаграммыФормыДиаграммаГанта

&НаКлиенте
Процедура ДиаграммаГантаОбработкаРасшифровки(Элемент, Расшифровки, СтандартнаяОбработка, Дата)
	
	Если ТипЗнч(Расшифровки) = Тип("Массив")
		И Расшифровки.Количество() > 1 Тогда
		
		ЗначениеРасшифровки  = Расшифровки[1];
		
		Если ТипЗнч(ЗначениеРасшифровки) = Тип("Структура") Тогда
			
			СтандартнаяОбработка = Ложь;
			
			ПараметрыФормы = ЗначениеРасшифровки;
			ПараметрыФормы.Вставить("Распоряжение", Распоряжение);
			ПараметрыФормы.Вставить("СтатусГрафика", СтатусГрафика);
			
			ОткрытьФорму(
				"Отчет.ДиаграммаПроизводстваПартииЗапуска.ФормаОбъекта",
				ПараметрыФормы,
				ЭтотОбъект,
				Новый УникальныйИдентификатор);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сформировать(Команда)
	
	ЗапуститьФормированиеОтчета();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Настройки

&НаСервере
Процедура ЗагрузитьНастройкиФормы()
	
	Настройки = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		КлючФормы(),
		КлючНастроекФормы());
	
	Если ТипЗнч(Настройки) = Тип("Структура") Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Настройки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройкиФормыКлиент()
	
	Настройки = Новый Структура;
	Настройки.Вставить("Распоряжение", Распоряжение);
	Настройки.Вставить("РежимОтображения", РежимОтображения);
	
	СохранитьНастройкиФормыСервер(Настройки);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьНастройкиФормыСервер(Настройки)
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		КлючФормы(),
		КлючНастроекФормы(),
		Настройки);
	
КонецПроцедуры
	
&НаКлиентеНаСервереБезКонтекста
Функция КлючФормы()
	
	Возврат "Отчет.ДиаграммаПроизводстваЗаказа.ФормаОтчета";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция КлючНастроекФормы()
	
	Возврат "Основные";
	
КонецФункции

#КонецОбласти

#Область ФормированиеОтчета

&НаКлиенте
Процедура ЗапуститьФормированиеОтчета()
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	СформироватьОтчетВФоне();
	
	Если НачатьОжиданиеДлительнойОперации Тогда
		
		НачатьОжиданиеДлительнойОперации();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьОжиданиеДлительнойОперации()
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("СформироватьОтчетВФонеЗавершение", ЭтотОбъект);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
		ДлительнаяОперация,
		ОповещениеОЗавершении,
		ПараметрыОжидания);
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаДлительнаяОперация;
	
	НачатьОжиданиеДлительнойОперации = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьОтчетВФоне()
	
	Если НЕ ДлительнаяОперация = Неопределено Тогда
		ОтменитьДлительнуюОперацию(ДлительнаяОперация.ИдентификаторЗадания);
	КонецЕсли;
	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("Распоряжение", Распоряжение);
	ПараметрыПроцедуры.Вставить("РежимОтображения", РежимОтображения);
	ПараметрыПроцедуры.Вставить("СтатусГрафика", СтатусГрафика);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Формирование отчета ""Диаграмма графика производства заказа""';
															|en = 'Generate the ""Order production schedule chart"" report'");
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(
		"Отчеты.ДиаграммаПроизводстваЗаказа.СформироватьОтчет",
		ПараметрыПроцедуры,
		ПараметрыВыполнения);
	
	Если ДлительнаяОперация.Статус = "Выполняется" Тогда
		
		НачатьОжиданиеДлительнойОперации = Истина;
		
	Иначе
		
		ОбработатьРезультатФормированияОтчетаВФоне(ЭтотОбъект, ДлительнаяОперация);
		
		НачатьОжиданиеДлительнойОперации = Ложь;
		ДлительнаяОперация = Неопределено;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьОтчетВФонеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ДлительнаяОперация = Неопределено;
	
	Если Результат = Неопределено Тогда
		
		ОчиститьИПоказатьДиаграмму(ЭтотОбъект);
		
	Иначе
		
		ОбработатьРезультатФормированияОтчетаВФоне(ЭтотОбъект, Результат);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбработатьРезультатФормированияОтчетаВФоне(Форма, Результат)
	
	Если Результат.Статус = "Выполнено" Тогда
		
		НоваяДиаграмма = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если ТипЗнч(НоваяДиаграмма) = Тип("ДиаграммаГанта") Тогда
			Форма.ДиаграммаГанта = НоваяДиаграмма;
		КонецЕсли;
		
		Форма.Элементы.ГруппаСтраницы.ТекущаяСтраница = Форма.Элементы.СтраницаДиаграмма;
		
	Иначе
		
		ОчиститьИПоказатьДиаграмму(Форма);
		
		Если Результат.Статус = "Ошибка" Тогда
			
			ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОчиститьИПоказатьДиаграмму(Форма)
	
	Форма.ДиаграммаГанта.Очистить();
	Форма.Элементы.ГруппаСтраницы.ТекущаяСтраница = Форма.Элементы.СтраницаДиаграмма;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОтменитьДлительнуюОперацию(ИдентификаторЗадания)
	
	ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаСервере
Процедура ЗаполнитьСписокРежимовОтображения()
	
	СписокРежимов = Отчеты.ДиаграммаПроизводстваЗаказа.РежимыОтображения();
	
	Для каждого Элемент Из СписокРежимов Цикл
		
		Элементы.РежимОтображения.СписокВыбора.Добавить(
			Элемент.Значение,
			Элемент.Представление);
			
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовокФормы()
	
	Если СтатусГрафика = СтатусРабочийГрафик() Тогда
		
		Заголовок = НСтр("ru = 'График производства заказа';
						|en = 'Order production schedule'");
		
	ИначеЕсли СтатусГрафика = СтатусПредварительныйГрафик() Тогда
		
		Заголовок = НСтр("ru = 'График производства заказа (планирование)';
						|en = 'Order production schedule (planning)'");
		
	ИначеЕсли СтатусГрафика = СтатусМодельГрафика() Тогда
		
		Заголовок = НСтр("ru = 'График производства заказа (модель)';
						|en = 'Order production schedule (model)'");
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СтатусРабочийГрафик()
	
	Возврат РегистрыСведений.ГрафикЭтаповПроизводства2_2.СтатусРабочийГрафик();
	
КонецФункции

&НаСервере
Функция СтатусПредварительныйГрафик()
	
	Возврат РегистрыСведений.ГрафикЭтаповПроизводства2_2.СтатусПредварительныйГрафик();
	
КонецФункции

&НаСервере
Функция СтатусМодельГрафика()
	
	Возврат РегистрыСведений.ГрафикЭтаповПроизводства2_2.СтатусМодельГрафика();
	
КонецФункции

#КонецОбласти

#КонецОбласти
