#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Проверки = Новый Структура;
	Проверки.Вставить("КорректностьПериода", Истина);
	
	БухгалтерскиеОтчетыВызовСервера.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, Проверки);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КонецПериода) Тогда
		
		ОписаниеНалоговогоПериодаНачало = НалоговыйУчет.БлижайшийНалоговыйПериод(НачалоПериода, Организация);
		ОписаниеНалоговогоПериодаКонец  = НалоговыйУчет.БлижайшийНалоговыйПериод(КонецПериода,  Организация);
		
		Если ОписаниеНалоговогоПериодаНачало.Период <> ОписаниеНалоговогоПериодаКонец.Период Тогда
			
			ТекстСообщения = НСтр("ru = 'Укажите даты начала и окончания в пределах одного налогового периода (года)';
									|en = 'Specify start and end dates within one fiscal period (year)'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "Отчет.НачалоПериода",, Отказ);
			
		КонецЕсли;
		
	КонецЕсли;
		
	Если ИмяРасшифровки = "Схема0" И НЕ ВключатьОбособленныеПодразделения Тогда		
		СписокОП = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьСписокОбособленныхПодразделений(Организация);
		
		Если СписокОП.Количество() > 1 Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Для расшифровки блока ""Налог"" выберите в поле ""Организация""
				|значение ""%1 с обособленными подразделениями"".';
				|en = 'To drill down the ""Tax"" section, select ""%1 with branch offices"" 
				|in the ""Company"" field. '"),
				Строка(ОбщегоНазначенияБПВызовСервераПовтИсп.ГоловнаяОрганизация(Организация)));
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "ПолеОрганизация",, Отказ);			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ИмяРасшифровки = "Схема0" И ЗначениеЗаполнено(КонецПериода) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ЕСТЬNULL(НастройкиУчетаНалогаНаПрибыль.РаздельныйУчетПоНалогуНаПрибыль, ЛОЖЬ) КАК ИспользуетсяРаздельныйУчетПоНалогуНаПрибыль,
			|	ЕСТЬNULL(УчетнаяПолитикаБухУчета.ВариантУчетаОтложенногоНалога, ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаОтложенногоНалога.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаОтложенногоНалога.БалансовыйМетод) КАК ВедетсяБалансовыйМетод
			|ИЗ
			|	Справочник.Организации КАК Организации
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиУчетаНалогаНаПрибыль.СрезПоследних(&КонецПериода, Организация = &Организация) КАК НастройкиУчетаНалогаНаПрибыль
			|		ПО Организации.Ссылка = НастройкиУчетаНалогаНаПрибыль.Организация
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаБухУчета.СрезПоследних(&КонецПериода, Организация = &Организация) КАК УчетнаяПолитикаБухУчета
			|		ПО Организации.Ссылка = УчетнаяПолитикаБухУчета.Организация
			|ГДЕ
			|	Организации.Ссылка = &Организация";
		
		Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
		Запрос.УстановитьПараметр("Организация",  Организация);
		
		РезультатЗапроса = Запрос.Выполнить();
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		Если Выборка.ИспользуетсяРаздельныйУчетПоНалогуНаПрибыль Тогда
			ТекстСообщения = НСтр("ru = 'Расшифровка блока ""Налог"" недоступна при использовании раздельного учета по налогу на прибыль.';
									|en = 'You cannot drill down the ""Tax"" section if you use separate accounting by income tax.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "ПолеОрганизация",, Отказ);
		КонецЕсли;
		
		Если Выборка.ВедетсяБалансовыйМетод Тогда
			ТекстСообщения = НСтр("ru = 'Расшифровка блока ""Налог"" недоступна при использовании балансового метода учета по ПБУ 18.';
									|en = 'You cannot drill down the ""Tax"" section if you use the balancing method of accounting in accordance with PBU 18.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "ПолеОрганизация",, Отказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли