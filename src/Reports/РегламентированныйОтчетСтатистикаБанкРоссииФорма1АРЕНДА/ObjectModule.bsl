#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Перем ИменаРазделовИТаблицСписок;

Перем ИменаСправочниковИСсылкиСписок;

#Область СлужебныеПроцедурыИФункции

#Область ФормированиеРегламентированногоОтчета

Процедура ИнициализацияПеременныхМодуля()
	
	ИменаРазделовИТаблицСписок     = Новый СписокЗначений;
	ИменаСправочниковИСсылкиСписок = Новый СписокЗначений;
	
	ИспользуемаяФормаОтчета = "ФормаОтчета2022Кв1";
	
	// В каждой группе <Имя раздела>,<Имя таблицы шаблона>.
	ИменаРазделовИТаблицСтр = "Раздел1,RENT";
	ИменаРазделовИТаблицСписок.Добавить(ИменаРазделовИТаблицСтр, ИспользуемаяФормаОтчета);
	
	ИменаСправочниковИСсылки = Новый СписокЗначений;
	ИменаСправочниковИСсылки.Добавить("OKVED",   "ОКВЭД2", Истина); // ОКВЭД2
	ИменаСправочниковИСсылки.Добавить("OKOPF",   "F:G");            // ОКОПФ
	ИменаСправочниковИСсылки.Добавить("OKFS",    "H:I");            // ОКФС
	ИменаСправочниковИСсылки.Добавить("REGION",  "J:K");            // Регионы
	ИменаСправочниковИСсылки.Добавить("OKSM",    "L:M");            // КодыСтран
	ИменаСправочниковИСсылки.Добавить("OKV",     "N:P");            // КодыВалют
	ИменаСправочниковИСсылкиСписок.Добавить(ИменаСправочниковИСсылки, ИспользуемаяФормаОтчета);
	
	ИспользуемаяФормаОтчета = "ФормаОтчета2023Кв3";
	
	// В каждой группе <Имя раздела>,<Имя таблицы шаблона>.
	ИменаРазделовИТаблицСтр = "Раздел1,RENT;Раздел2,RENT2";
	ИменаРазделовИТаблицСписок.Добавить(ИменаРазделовИТаблицСтр, ИспользуемаяФормаОтчета);
	
	ИменаСправочниковИСсылки = Новый СписокЗначений;
	ИменаСправочниковИСсылки.Добавить("OKVED",   "ОКВЭД2", Истина); // ОКВЭД2
	ИменаСправочниковИСсылки.Добавить("OKOPF",   "F:G");            // ОКОПФ
	ИменаСправочниковИСсылки.Добавить("OKFS",    "H:I");            // ОКФС
	ИменаСправочниковИСсылки.Добавить("REGION",  "J:K");            // Регионы
	ИменаСправочниковИСсылки.Добавить("OKSM",    "L:M");            // КодыСтран
	ИменаСправочниковИСсылки.Добавить("OKV",     "N:P");            // КодыВалют
	
	ИменаСправочниковИСсылки.Добавить("OKTMO",   "ОКТМО",  Истина); // ОКТМО
	
	ИменаСправочниковИСсылкиСписок.Добавить(ИменаСправочниковИСсылки, ИспользуемаяФормаОтчета);
	
КонецПроцедуры

#КонецОбласти

#Область ВыгрузкаРегламентированногоОтчета

Процедура ПрочитатьМетаданныеШаблона(МакетМетаданныхШаблона, МакетСписковВыбора, МетаданныеШаблона,
									 ВыбраннаяФормаОтчета) Экспорт
	
	Если ТипЗнч(МетаданныеШаблона) <> Тип("Структура") Тогда
		МетаданныеШаблона = Новый Структура();
	КонецЕсли;
	
	Для Каждого Область Из МакетМетаданныхШаблона.Области Цикл
		Если Область.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Строки Тогда
			
			ВерхОбласти  = Область.Верх;
			НизОбласти   = Область.Низ;
			ЛевоОбласти  = 1;
			ПравоОбласти = 256;
			
			Если Область.Имя = "Question_Meta" Тогда
				
				ТаблВопросовШаблона = Новый ТаблицаЗначений;
				Для НомКол = ЛевоОбласти По ПравоОбласти Цикл
					ИмяКолонки = СокрЛП(МакетМетаданныхШаблона.Область(ВерхОбласти, НомКол).Текст);
					Если ПустаяСтрока(ИмяКолонки) Тогда
						ПравоОбласти = НомКол - 1;
						Прервать;
					КонецЕсли;
					ТаблВопросовШаблона.Колонки.Добавить(ИмяКолонки, Новый ОписаниеТипов("Строка"));
				КонецЦикла;
				
				Для НомСтр = ВерхОбласти + 1 По НизОбласти Цикл
					ЗначениеОбластиВПервойКолонке = СокрЛП(МакетМетаданныхШаблона.Область(НомСтр, ЛевоОбласти).Текст);
					Если НЕ ПустаяСтрока(ЗначениеОбластиВПервойКолонке) Тогда
						ИндКолонки = 0;
						НовСтрока = ТаблВопросовШаблона.Добавить();
						Для НомКол = ЛевоОбласти По ПравоОбласти Цикл
							ЗначениеОбласти = СокрП(МакетМетаданныхШаблона.Область(НомСтр, НомКол, НомСтр, НомКол).Текст);
							НовСтрока.Установить(ИндКолонки, ЗначениеОбласти);
							ИндКолонки = ИндКолонки + 1;
						КонецЦикла;
					КонецЕсли;
				КонецЦикла;
				
				МетаданныеШаблона.Вставить(Область.Имя, ТаблВопросовШаблона);
				
			ИначеЕсли Область.Имя = "Period_Check_Meta" Тогда
				
				ТаблПроверкиПериодаШаблона = Новый ТаблицаЗначений;
				Для НомКол = ЛевоОбласти По ПравоОбласти Цикл
					ИмяКолонки = СокрЛП(МакетМетаданныхШаблона.Область(ВерхОбласти, НомКол).Текст);
					Если ПустаяСтрока(ИмяКолонки) Тогда
						ПравоОбласти = НомКол - 1;
						Прервать;
					КонецЕсли;
					ТаблПроверкиПериодаШаблона.Колонки.Добавить(ИмяКолонки, Новый ОписаниеТипов("Строка"));
				КонецЦикла;
				
				Для НомСтр = ВерхОбласти + 1 По НизОбласти Цикл
					ЗначениеОбластиВПервойКолонке = СокрЛП(МакетМетаданныхШаблона.Область(НомСтр, ЛевоОбласти).Текст);
					Если НЕ ПустаяСтрока(ЗначениеОбластиВПервойКолонке) Тогда
						ИндКолонки = 0;
						НовСтрока = ТаблПроверкиПериодаШаблона.Добавить();
						Для НомКол = ЛевоОбласти По ПравоОбласти Цикл
							ЗначениеОбласти = СокрП(МакетМетаданныхШаблона.Область(НомСтр, НомКол, НомСтр, НомКол).Текст);
							НовСтрока.Установить(ИндКолонки, ЗначениеОбласти);
							ИндКолонки = ИндКолонки + 1;
						КонецЦикла;
					КонецЕсли;
				КонецЦикла;
				
				МетаданныеШаблона.Вставить(Область.Имя, ТаблПроверкиПериодаШаблона);
				
			ИначеЕсли Область.Имя = "Survey_Meta" Тогда
				
				СписокОпросШаблона = Новый СписокЗначений;
				
				Для НомСтр = ВерхОбласти По НизОбласти Цикл
					ЗначениеОбластиВПервойКолонке = СокрЛП(МакетМетаданныхШаблона.Область(НомСтр, ЛевоОбласти).Текст);
					Если НЕ ПустаяСтрока(ЗначениеОбластиВПервойКолонке) Тогда
						ЗначениеОбластиВоВторойКолонке = СокрП(МакетМетаданныхШаблона.Область(НомСтр, ЛевоОбласти + 1).Текст);
						СписокОпросШаблона.Добавить(ЗначениеОбластиВоВторойКолонке, ЗначениеОбластиВПервойКолонке);
					КонецЕсли;
				КонецЦикла;
				
				МетаданныеШаблона.Вставить(Область.Имя, СписокОпросШаблона);
			
			ИначеЕсли Область.Имя = "KeyValuePair_Meta" Тогда
				
				СправочникиКлючИЗначение = Новый Структура;
				
				Для НомСтр = ВерхОбласти По НизОбласти Цикл
					ЗначениеОбластиВПервойКолонке = СокрЛП(МакетМетаданныхШаблона.Область(НомСтр, ЛевоОбласти).Текст);
					Если НЕ ПустаяСтрока(ЗначениеОбластиВПервойКолонке) Тогда
						ЗначениеОбластиВоВторойКолонке = СокрП(МакетМетаданныхШаблона.Область(НомСтр, ЛевоОбласти + 1).Текст);
						СпрКоординатыОбласти = РегламентированнаяОтчетностьБанкРоссии.КоординатыОбласти(ЗначениеОбластиВоВторойКолонке);
						
						ТаблСправочникКлючИЗначение = Новый ТаблицаЗначений;
						ТаблСправочникКлючИЗначение.Колонки.Добавить("Ключ",     Новый ОписаниеТипов("Строка"));
						ТаблСправочникКлючИЗначение.Колонки.Добавить("Значение", Новый ОписаниеТипов("Строка"));
						Для НомСтрСпр = ВерхОбласти По НизОбласти Цикл
							Ключ = СокрЛП(МакетМетаданныхШаблона.Область(НомСтрСпр, СпрКоординатыОбласти.Столбец1).Текст);
							Значение = СокрЛП(МакетМетаданныхШаблона.Область(НомСтрСпр, СпрКоординатыОбласти.Столбец2).Текст);
							Если НЕ ПустаяСтрока(Ключ) Тогда
								НовСтрока = ТаблСправочникКлючИЗначение.Добавить();
								НовСтрока.Ключ = Ключ;
								НовСтрока.Значение = Значение;
							Иначе
								Прервать;
							КонецЕсли;
						КонецЦикла;
						
						СправочникиКлючИЗначение.Вставить(ЗначениеОбластиВПервойКолонке, ТаблСправочникКлючИЗначение);
					Иначе
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
				МетаданныеШаблона.Вставить(Область.Имя, СправочникиКлючИЗначение);
				
			ИначеЕсли Область.Имя = "NSI_Meta" Тогда
				
				ИменаСправочниковИСсылкиЭлемент = РегламентированнаяОтчетностьБанкРоссии.НайтиЭлементСпискаПоПредставлению(
					ИменаСправочниковИСсылкиСписок, ВыбраннаяФормаОтчета);
				Если ИменаСправочниковИСсылкиЭлемент = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				ИменаСправочниковИСсылки = ИменаСправочниковИСсылкиЭлемент.Значение;
				
				СправочникиКлючИЗначение = Новый Структура;
				
				Для Каждого ЭлементИмяИСсылка Из ИменаСправочниковИСсылки Цикл
					
					ИмяСправочника   = ЭлементИмяИСсылка.Значение;
					СсылкаНаДиапазон = ЭлементИмяИСсылка.Представление;
					
					ТекМакетМетаданных = МакетМетаданныхШаблона;
					ВерхОбластиСпр = ВерхОбласти;
					НизОбластиСпр  = НизОбласти;
					НомерКолонкиКлюч = 1;
					НомерКолонкиЗначение = 2;
					Если ЭлементИмяИСсылка.Пометка Тогда // справочник загружаем из макета "СпискиВыбора*"
						Если МакетСписковВыбора = Неопределено Тогда
							Продолжить;
						КонецЕсли;
						ТекМакетМетаданных = МакетСписковВыбора;
						ОбластьСправочника = ТекМакетМетаданных.Области.Найти(СсылкаНаДиапазон);
						Если ОбластьСправочника = Неопределено Тогда
							Продолжить;
						КонецЕсли;
						ВерхОбластиСпр = ОбластьСправочника.Верх;
						НизОбластиСпр  = ОбластьСправочника.Низ;
						// Выполним следующие действия, если справочник размещен в сжатом макете.
						Ключ = СокрЛП(ТекМакетМетаданных.Область(ВерхОбластиСпр, НомерКолонкиКлюч).Текст);
						Если Лев(Ключ, 1) = "@" Тогда
							Если НРег(Прав(Ключ, 3) = "zip") Тогда
								ИмяСжатогоМакета = Сред(Ключ, 2);
								ИмяОбласти = СокрЛП(ТекМакетМетаданных.Область(ВерхОбластиСпр, НомерКолонкиЗначение).Текст);
								ТекМакетМетаданных = РаспакованныйМакетМетаданных(ИмяСжатогоМакета, ИмяОбласти, ВерхОбластиСпр, НизОбластиСпр);
							Иначе
								Продолжить;
							КонецЕсли;
						КонецЕсли;
					Иначе
						СпрКоординатыОбласти = РегламентированнаяОтчетностьБанкРоссии.КоординатыОбласти(СсылкаНаДиапазон);
						НомерКолонкиКлюч     = СпрКоординатыОбласти.Столбец1;
						НомерКолонкиЗначение = СпрКоординатыОбласти.Столбец2;
					КонецЕсли;
					
					ТаблСправочникКлючИЗначение = Новый ТаблицаЗначений;
					ТаблСправочникКлючИЗначение.Колонки.Добавить("Ключ",     Новый ОписаниеТипов("Строка"));
					ТаблСправочникКлючИЗначение.Колонки.Добавить("Значение", Новый ОписаниеТипов("Строка"));
					
					Для НомСтрСпр = ВерхОбластиСпр + 1 По НизОбластиСпр Цикл
						Ключ = СокрЛП(ТекМакетМетаданных.Область(НомСтрСпр, НомерКолонкиКлюч).Текст);
						Значение = СокрЛП(ТекМакетМетаданных.Область(НомСтрСпр, НомерКолонкиЗначение).Текст);
						Если НЕ ПустаяСтрока(Ключ) Тогда
							НовСтрока = ТаблСправочникКлючИЗначение.Добавить();
							НовСтрока.Ключ = Ключ;
							НовСтрока.Значение = Значение;
						Иначе
							Прервать;
						КонецЕсли;
					КонецЦикла;
					
					СправочникиКлючИЗначение.Вставить(ИмяСправочника, ТаблСправочникКлючИЗначение);
					
				КонецЦикла;
				
				МетаданныеШаблона.Вставить(Область.Имя, СправочникиКлючИЗначение);
				
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	// Создадим таблицу данных листа ответов анкеты.
	ТаблОтветовШаблона = Новый ТаблицаЗначений;
	ТаблОтветовШаблона.Колонки.Добавить("ИДВопроса",      Новый ОписаниеТипов("Строка"));
	ТаблОтветовШаблона.Колонки.Добавить("ЗначениеОтвета", Новый ОписаниеТипов("Строка"));
	ТаблОтветовШаблона.Колонки.Добавить("НомСтрокиЛиста", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(6, 0)));
	ТаблОтветовШаблона.Добавить();
	
	МетаданныеШаблона.Вставить("Answer_Meta", ТаблОтветовШаблона);
	
	// Создадим таблицу данных листа ошибок анкеты.
	ТаблОшибокШаблона = Новый ТаблицаЗначений;
	ТаблОшибокШаблона.Колонки.Добавить("АдресОшибки",    Новый ОписаниеТипов("Строка"));
	ТаблОшибокШаблона.Колонки.Добавить("МестоОшибки",    Новый ОписаниеТипов("Строка"));
	ТаблОшибокШаблона.Колонки.Добавить("ОписаниеОшибки", Новый ОписаниеТипов("Строка"));
	ТаблОшибокШаблона.Добавить();
	
	МетаданныеШаблона.Вставить("Errors_Meta", ТаблОшибокШаблона);
	
КонецПроцедуры

Процедура ПодготовитьТаблицыРазделов(Форма, МетаданныеШаблона, ИменаРазделовИТаблиц)
	
	МетаданныеШаблона.Вставить("ТаблицыРазделов", Новый Структура());
	
	Для Каждого ИмяРазделаИТаблицыСтр Из ИменаРазделовИТаблиц Цикл
		ИмяРазделаИТаблицы = СтрРазделить(ИмяРазделаИТаблицыСтр, ",");
		
		ТаблицаРаздела = Новый ТаблицаЗначений;
		ТаблицаРаздела.Колонки.Добавить("НС", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(7,0)));
		
		МетаданныеШаблона.ТаблицыРазделов.Вставить(ИмяРазделаИТаблицы[1], ТаблицаРаздела);
		
		Для Каждого ЭлементСтруктуры Из Форма.мДанныеОтчета[ИмяРазделаИТаблицы[0]] Цикл
			
			Для НомСтр = 1 По ЭлементСтруктуры.Значение.Количество() Цикл
				ДанныеСтроки = ЭлементСтруктуры.Значение[НомСтр - 1];
				
				СтрокаНеПустая = Ложь;
				НовСтрока = ТаблицаРаздела.Добавить();
				Для Каждого ЭлементДанных Из ДанныеСтроки Цикл
					ГрафаПоказателя = СтрЗаменить(ЭлементДанных.Ключ, ЭлементСтруктуры.Ключ, "");
					Если НЕ ПустаяСтрока(ГрафаПоказателя) Тогда
						НомерГрафыПоказателя = Число(" " + ГрафаПоказателя);
						
						КоличествоКолонок = ТаблицаРаздела.Колонки.Количество();
						Если НомерГрафыПоказателя >= КоличествоКолонок Тогда
							Для НомКол = КоличествоКолонок + 1 По НомерГрафыПоказателя Цикл
								ТаблицаРаздела.Колонки.Добавить();
							КонецЦикла;
							ТаблицаРаздела.Колонки.Добавить(ЭлементДанных.Ключ);
						ИначеЕсли ПустаяСтрока(ТаблицаРаздела.Колонки[НомерГрафыПоказателя].Имя) Тогда
							ТаблицаРаздела.Колонки[НомерГрафыПоказателя].Имя = ЭлементДанных.Ключ; 
						КонецЕсли;
						
						НовСтрока[НомерГрафыПоказателя] = ЭлементДанных.Значение;
						Если ЗначениеЗаполнено(ЭлементДанных.Значение) Тогда
							СтрокаНеПустая  = Истина;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
				
				Если НЕ СтрокаНеПустая Тогда
					ТаблицаРаздела.Удалить(НовСтрока);
				Иначе
					НовСтрока.НС = НомСтр;
				КонецЕсли;
			КонецЦикла;
			
			Прервать;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Функция РаспакованныйМакетМетаданных(ИмяСжатогоМакета, ИмяОбласти, ВерхОбластиСпр, НизОбластиСпр)
	
	АрхивМакетаМетаданных = ПолучитьМакет(ИмяСжатогоМакета);
	
	Попытка
		ЧтениеАрхиваМетаданных = Новый ЧтениеZipФайла(АрхивМакетаМетаданных.ОткрытьПотокДляЧтения());
	Исключение
		ВызватьИсключение НСтр("ru = 'В макете отсутствуют сжатые данные.';
								|en = 'В макете отсутствуют сжатые данные.'");
	КонецПопытки;
	
	МакетМетаданных = Новый ТабличныйДокумент;

	ИмяВременнойПапкиZIP = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(ПолучитьИмяВременногоФайла());
	СоздатьКаталог(ИмяВременнойПапкиZIP);
	
	ИмяФайлаМакетаМетаданных = ИмяВременнойПапкиZIP + ЧтениеАрхиваМетаданных.Элементы[0].Имя;
	
	ЧтениеАрхиваМетаданных.Извлечь(ЧтениеАрхиваМетаданных.Элементы[0], ИмяВременнойПапкиZIP,
		РежимВосстановленияПутейФайловZIP.НеВосстанавливать);
	ЧтениеАрхиваМетаданных.Закрыть();
	
	МакетМетаданных.Прочитать(ИмяФайлаМакетаМетаданных);
	
	Если ЗначениеЗаполнено(ИмяОбласти) Тогда
		ОбластьСправочника = МакетМетаданных.Область(ИмяОбласти);
	Иначе
		ОбластьСправочника = МакетМетаданных.Область();
	КонецЕсли;
	ВерхОбластиСпр = ОбластьСправочника.Верх;
	НизОбластиСпр  = ОбластьСправочника.Низ;
	
	УдалитьФайлы(ИмяВременнойПапкиZIP);
	
	Возврат МакетМетаданных;
	
КонецФункции

Функция ВыполнитьПроверкуДанных(Форма, ПараметрыВыгрузки, ЕстьПредупреждения = Ложь) Экспорт
	
	ИменаРазделовИТаблицЭлемент = РегламентированнаяОтчетностьБанкРоссии.НайтиЭлементСпискаПоПредставлению(
		ИменаРазделовИТаблицСписок, Форма.СтруктураРеквизитовФормы.мВыбраннаяФорма);
	Если ИменаРазделовИТаблицЭлемент = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	ИменаРазделовИТаблицСтр = ИменаРазделовИТаблицЭлемент.Значение;
	
	СтатусПроверки = Истина;
	
	ИменаРазделовИТаблиц = СтрРазделить(ИменаРазделовИТаблицСтр, ";");
	
	ТаблВопросы = ПараметрыВыгрузки.МетаданныеШаблона["Question_Meta"];
	
	// Подготовка таблиц для обработки и контроля, добавление их в метаданные шаблона.
	ПодготовитьТаблицыРазделов(Форма, ПараметрыВыгрузки.МетаданныеШаблона, ИменаРазделовИТаблиц);
	
	// Обработаем вопросы анкеты для титульного листа.
	СтруктураДанныхТитульный = Форма.мДанныеОтчета.Титульный;
	ВопросыТитульный = ТаблВопросы.НайтиСтроки(Новый Структура("QUESTION_TYPE", "HEADER"));
	Для Каждого ТекВопрос Из ВопросыТитульный Цикл
		ИмяПоказателя = ТекВопрос["QUESTION_NAME_ID"];
		ЗначениеПоказателя = Неопределено;
		Если СтруктураДанныхТитульный.Свойство(ИмяПоказателя, ЗначениеПоказателя) Тогда
			Описание = "";
			РезультатПроверки = РегламентированнаяОтчетностьБанкРоссии.ВыполнитьПроверкуЗначения(
				Форма, ПараметрыВыгрузки, Описание, ТекВопрос, ЗначениеПоказателя);
			Если РезультатПроверки > 0 Тогда
				Если РезультатПроверки = 1 Тогда // Предупреждение
					ЕстьПредупреждения = Истина;
				ИначеЕсли РезультатПроверки = 2 Тогда // Ошибка
					СтатусПроверки = Ложь;
				КонецЕсли;
				
				МестоОшибки = "Титульный лист" + ": """ + ТекВопрос["QUESTION_TEXT"] + """";
				ТекстОшибки = Описание + Символы.ПС + Символы.Таб + "<" + МестоОшибки + ">";
				РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(
					Форма, ТекстОшибки, "Титульный", ИмяПоказателя);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	// Обновим необходимые данные опроса, если существует поле "NamePeriod".
	Если СтруктураДанныхТитульный.Свойство("NamePeriod") Тогда
		ОбновитьДанныеОпросаПоПериоду(ПараметрыВыгрузки);
	КонецЕсли;
	
	// Обработаем вопросы анкеты для листов с таблицами.
	Для Каждого ИмяРазделаИТаблицыСтр Из ИменаРазделовИТаблиц Цикл
		
		ИмяРазделаИТаблицы = СтрРазделить(ИмяРазделаИТаблицыСтр, ",");
		
		ВопросыРаздела = ТаблВопросы.НайтиСтроки(
			Новый Структура("QUESTION_TYPE,QUESTION_ENTITY", "TABLE", ИмяРазделаИТаблицы[1]));
		
		ТаблицаРаздела = ПараметрыВыгрузки.МетаданныеШаблона.ТаблицыРазделов[ИмяРазделаИТаблицы[1]]; 
		КоличествоКолонокТаблицы = ТаблицаРаздела.Колонки.Количество();
		
		Для НомСтр = 1 По ТаблицаРаздела.Количество() Цикл
			ДанныеСтрокиТаблицы = ТаблицаРаздела[НомСтр - 1];
			СтрНомСтроки = Формат(ДанныеСтрокиТаблицы.НС, "ЧГ=");
			
			ВопросыРазделаИЗначения = Новый Соответствие;
			Для НомВопроса = 1 По ВопросыРаздела.Количество() Цикл
				ТекВопрос = ВопросыРаздела[НомВопроса - 1];
				
				НомКолонки = Число(" " + ТекВопрос["QUESTION_SORT"]);
				НомКолонки = ?(НомКолонки > 0, НомКолонки, НомВопроса);
				
				Если КоличествоКолонокТаблицы > НомКолонки Тогда
					Описание = "";
					ЗначениеОтвета = ДанныеСтрокиТаблицы[НомКолонки];
					
					РезультатПроверки = РегламентированнаяОтчетностьБанкРоссии.ВыполнитьПроверкуЗначения(
						Форма, ПараметрыВыгрузки, Описание, ТекВопрос, ЗначениеОтвета, НомСтр);
					Если РезультатПроверки > 0 Тогда
						Если РезультатПроверки = 1 Тогда // Предупреждение
							ЕстьПредупреждения = Истина;
						ИначеЕсли РезультатПроверки = 2 Тогда // Ошибка
							СтатусПроверки = Ложь;
						КонецЕсли;
						
						МестоОшибки = "Раздел " + Прав(ИмяРазделаИТаблицы[0], 1)
							+ " [строка " + СтрНомСтроки  + "]: """ + ТекВопрос["QUESTION_TEXT"] + """";
						ТекстОшибки = Описание + Символы.ПС + Символы.Таб + "<" + МестоОшибки + ">";
						РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(Форма, ТекстОшибки, ИмяРазделаИТаблицы[0],
							ТаблицаРаздела.Колонки[НомКолонки].Имя + "_" + СтрНомСтроки);
					КонецЕсли;
					
					ВопросыРазделаИЗначения.Вставить(ТекВопрос["QUESTION_NAME_ID"],
						Новый Структура("ИмяКолонки,Значение,Результат,Вопрос",
							ТаблицаРаздела.Колонки[НомКолонки].Имя, ЗначениеОтвета, РезультатПроверки, ТекВопрос));
				КонецЕсли;
			КонецЦикла;
			
			РезультатПроверки = ВыполнитьПроверкуСтроки(Форма, ИмяРазделаИТаблицы,
				ТаблицаРаздела, ВопросыРазделаИЗначения, ПараметрыВыгрузки, НомСтр);
			Если РезультатПроверки = 1 Тогда      // предупреждения
				ЕстьПредупреждения = Истина;
			ИначеЕсли РезультатПроверки = 2 Тогда // ошибки
				СтатусПроверки = Ложь;
			ИначеЕсли РезультатПроверки = 3 Тогда // ошибки и предупреждения
				ЕстьПредупреждения = Истина;
				СтатусПроверки = Ложь;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат СтатусПроверки;
	
КонецФункции

Функция ВыполнитьПроверкуСтроки(Форма, ИмяРазделаИТаблицы,
			ТаблицаРаздела, ВопросыРазделаИЗначения, ПараметрыВыгрузки, НомСтр)
	
	РезультатПроверки = 0;
	
	ДанныеСтрокиТаблицы = ТаблицаРаздела[НомСтр - 1];
	СтрНомСтроки = Формат(ДанныеСтрокиТаблицы.НС, "ЧГ=");
	
	// Проверка строки раздела 1.
	Если ИмяРазделаИТаблицы[1] = "RENT" Тогда
		
		ЗначенияВопросаПрим  = ВопросыРазделаИЗначения["Comment"];
		
		ЗначенияВопросаПГ  = ВопросыРазделаИЗначения["PaymentFrequencyMain"];
		ЗначенияВопросаПП  = ВопросыРазделаИЗначения["PaymentFrequencyPercent"];
		
		Если ЗначенияВопросаПрим <> Неопределено И ЗначенияВопросаПрим.Результат = 0 Тогда
			Если ((ЗначенияВопросаПГ <> Неопределено И СокрЛП(ЗначенияВопросаПГ.Значение) = "4")
			  ИЛИ (ЗначенияВопросаПП <> Неопределено И СокрЛП(ЗначенияВопросаПП.Значение) = "4"))
				 И ПустаяСтрока(ЗначенияВопросаПрим.Значение) Тогда
				Описание = НСтр("ru='ОШИБКА: При выборе ''4. Другая периодичность'' в вопросах "
					+ "''Характеристики платежей по договору (периодичность платежей по договору (Основной долг | Проценты)'' "
					+ "следует указать ""Примечание""'");
				РезультатПроверки = 2;
				
				ЗаписатьСообщениеОбОшибкеВСтроке(Форма, ПараметрыВыгрузки,
					ЗначенияВопросаПрим, Описание, ИмяРазделаИТаблицы[0], СтрНомСтроки, НомСтр);
			КонецЕсли;
		КонецЕсли;
		
		Если ПараметрыВыгрузки.ИмяМакетаШаблона = "ШаблонБанка2022Кв1" Тогда
			
			ЗначенияВопросаГ15 = ВопросыРазделаИЗначения["DebtBalanceM1"];
			ЗначенияВопросаГ16 = ВопросыРазделаИЗначения["ReRentSumM1"];
			ЗначенияВопросаГ17 = ВопросыРазделаИЗначения["PaymentM1"];
			ЗначенияВопросаГ18 = ВопросыРазделаИЗначения["RevaluationM1"];
			ЗначенияВопросаГ19 = ВопросыРазделаИЗначения["OtherChangesM1"];
			ЗначенияВопросаГ20 = ВопросыРазделаИЗначения["DebtBalanceM2"];
			
			// графа 15 + графа 16 - графа 17 + графа 18 + графа 19 = графа 20.
			Если ЗначенияВопросаГ20 <> Неопределено И ЗначенияВопросаГ20.Результат = 0 Тогда
				Если (ЗначенияВопросаГ15 <> Неопределено И ЗначенияВопросаГ15.Результат = 0)
				   И (ЗначенияВопросаГ16 <> Неопределено И ЗначенияВопросаГ16.Результат = 0)
				   И (ЗначенияВопросаГ17 <> Неопределено И ЗначенияВопросаГ17.Результат = 0)
				   И (ЗначенияВопросаГ18 <> Неопределено И ЗначенияВопросаГ18.Результат = 0)
				   И (ЗначенияВопросаГ19 <> Неопределено И ЗначенияВопросаГ19.Результат = 0) Тогда
				   Если ЗначенияВопросаГ15.Значение +  ЗначенияВопросаГ16.Значение
					  - ЗначенияВопросаГ17.Значение +  ЗначенияВопросаГ18.Значение
					  + ЗначенияВопросаГ19.Значение <> ЗначенияВопросаГ20.Значение Тогда
						Описание = НСтр("ru = 'ОШИБКА: Не выполняется условие: ''графа 15 + графа 16 - графа 17 + графа 18 + графа 19 = графа 20''';
										|en = 'ОШИБКА: Не выполняется условие: ''графа 15 + графа 16 - графа 17 + графа 18 + графа 19 = графа 20'''");
						РезультатПроверки = РезультатПроверки + ?(РезультатПроверки < 2, 2, 0);
						
						ЗаписатьСообщениеОбОшибкеВСтроке(Форма, ПараметрыВыгрузки,
							ЗначенияВопросаГ20, Описание, ИмяРазделаИТаблицы[0], СтрНомСтроки, НомСтр);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			ЗначенияВопросаГ20 = ВопросыРазделаИЗначения["DebtBalanceM2"];
			ЗначенияВопросаГ21 = ВопросыРазделаИЗначения["ReRentSumM2"];
			ЗначенияВопросаГ22 = ВопросыРазделаИЗначения["PaymentM2"];
			ЗначенияВопросаГ23 = ВопросыРазделаИЗначения["RevaluationM2"];
			ЗначенияВопросаГ24 = ВопросыРазделаИЗначения["OtherChangesM2"];
			ЗначенияВопросаГ25 = ВопросыРазделаИЗначения["DebtBalanceM3"];
			
			// графа 20 + графа 21 - графа 22 + графа 23 + графа 24 = графа 25.
			Если ЗначенияВопросаГ25 <> Неопределено И ЗначенияВопросаГ25.Результат = 0 Тогда
				Если (ЗначенияВопросаГ20 <> Неопределено И ЗначенияВопросаГ20.Результат = 0)
				   И (ЗначенияВопросаГ21 <> Неопределено И ЗначенияВопросаГ21.Результат = 0)
				   И (ЗначенияВопросаГ22 <> Неопределено И ЗначенияВопросаГ22.Результат = 0)
				   И (ЗначенияВопросаГ23 <> Неопределено И ЗначенияВопросаГ23.Результат = 0)
				   И (ЗначенияВопросаГ24 <> Неопределено И ЗначенияВопросаГ24.Результат = 0) Тогда
				   Если ЗначенияВопросаГ20.Значение +  ЗначенияВопросаГ21.Значение
					  - ЗначенияВопросаГ22.Значение +  ЗначенияВопросаГ23.Значение
					  + ЗначенияВопросаГ24.Значение <> ЗначенияВопросаГ25.Значение Тогда
						Описание = НСтр("ru = 'ОШИБКА: Не выполняется условие: ''графа 20 + графа 21 - графа 22 + графа 23 + графа 24 = графа 25''';
										|en = 'ОШИБКА: Не выполняется условие: ''графа 20 + графа 21 - графа 22 + графа 23 + графа 24 = графа 25'''");
						РезультатПроверки = РезультатПроверки + ?(РезультатПроверки < 2, 2, 0);
						
						ЗаписатьСообщениеОбОшибкеВСтроке(Форма, ПараметрыВыгрузки,
							ЗначенияВопросаГ25, Описание, ИмяРазделаИТаблицы[0], СтрНомСтроки, НомСтр);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			ЗначенияВопросаГ25 = ВопросыРазделаИЗначения["DebtBalanceM3"];
			ЗначенияВопросаГ26 = ВопросыРазделаИЗначения["ReRentSumM3"];
			ЗначенияВопросаГ27 = ВопросыРазделаИЗначения["PaymentM3"];
			ЗначенияВопросаГ28 = ВопросыРазделаИЗначения["RevaluationM3"];
			ЗначенияВопросаГ29 = ВопросыРазделаИЗначения["OtherChangesM3"];
			ЗначенияВопросаГ30 = ВопросыРазделаИЗначения["DebtBalanceFinal"];
			
			// графа 25 + графа 26 - графа 27 + графа 28 + графа 29 = графа 30.
			Если ЗначенияВопросаГ30 <> Неопределено И ЗначенияВопросаГ30.Результат = 0 Тогда
				Если (ЗначенияВопросаГ25 <> Неопределено И ЗначенияВопросаГ25.Результат = 0)
				   И (ЗначенияВопросаГ26 <> Неопределено И ЗначенияВопросаГ26.Результат = 0)
				   И (ЗначенияВопросаГ27 <> Неопределено И ЗначенияВопросаГ27.Результат = 0)
				   И (ЗначенияВопросаГ28 <> Неопределено И ЗначенияВопросаГ28.Результат = 0)
				   И (ЗначенияВопросаГ29 <> Неопределено И ЗначенияВопросаГ29.Результат = 0) Тогда
				   Если ЗначенияВопросаГ25.Значение +  ЗначенияВопросаГ26.Значение
					  - ЗначенияВопросаГ27.Значение +  ЗначенияВопросаГ28.Значение
					  + ЗначенияВопросаГ29.Значение <> ЗначенияВопросаГ30.Значение Тогда
						Описание = НСтр("ru = 'ОШИБКА: Не выполняется условие: ''графа 25 + графа 26 - графа 27 + графа 28 + графа 29 = графа 30''';
										|en = 'ОШИБКА: Не выполняется условие: ''графа 25 + графа 26 - графа 27 + графа 28 + графа 29 = графа 30'''");
						РезультатПроверки = РезультатПроверки + ?(РезультатПроверки < 2, 2, 0);
						
						ЗаписатьСообщениеОбОшибкеВСтроке(Форма, ПараметрыВыгрузки,
							ЗначенияВопросаГ30, Описание, ИмяРазделаИТаблицы[0], СтрНомСтроки, НомСтр);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
		Иначе
			
			ЗначенияВопросаГ16 = ВопросыРазделаИЗначения["DebtBalanceM1"];
			ЗначенияВопросаГ17 = ВопросыРазделаИЗначения["ReRentSumM1"];
			ЗначенияВопросаГ18 = ВопросыРазделаИЗначения["PaymentM1"];
			ЗначенияВопросаГ19 = ВопросыРазделаИЗначения["RevaluationM1"];
			ЗначенияВопросаГ20 = ВопросыРазделаИЗначения["OtherChangesM1"];
			ЗначенияВопросаГ21 = ВопросыРазделаИЗначения["DebtBalanceM2"];
			
			// графа 21 = графа 16 + графа 17 - графа 18 + графа 19 + графа 20.
			Если ЗначенияВопросаГ21 <> Неопределено И ЗначенияВопросаГ21.Результат = 0 Тогда
				Если (ЗначенияВопросаГ16 <> Неопределено И ЗначенияВопросаГ16.Результат = 0)
				   И (ЗначенияВопросаГ17 <> Неопределено И ЗначенияВопросаГ17.Результат = 0)
				   И (ЗначенияВопросаГ18 <> Неопределено И ЗначенияВопросаГ18.Результат = 0)
				   И (ЗначенияВопросаГ19 <> Неопределено И ЗначенияВопросаГ19.Результат = 0)
				   И (ЗначенияВопросаГ20 <> Неопределено И ЗначенияВопросаГ20.Результат = 0) Тогда
				   Если ЗначенияВопросаГ16.Значение +  ЗначенияВопросаГ17.Значение
					  - ЗначенияВопросаГ18.Значение +  ЗначенияВопросаГ19.Значение
					  + ЗначенияВопросаГ20.Значение <> ЗначенияВопросаГ21.Значение Тогда
						Описание = НСтр("ru = 'ОШИБКА: Не выполняется условие: ''графа 21 = графа 16 + графа 17 - графа 18 + графа 19 + графа 20''';
										|en = 'ОШИБКА: Не выполняется условие: ''графа 21 = графа 16 + графа 17 - графа 18 + графа 19 + графа 20'''");
						РезультатПроверки = РезультатПроверки + ?(РезультатПроверки < 2, 2, 0);
						
						ЗаписатьСообщениеОбОшибкеВСтроке(Форма, ПараметрыВыгрузки,
							ЗначенияВопросаГ21, Описание, ИмяРазделаИТаблицы[0], СтрНомСтроки, НомСтр);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			ЗначенияВопросаГ21 = ВопросыРазделаИЗначения["DebtBalanceM2"];
			ЗначенияВопросаГ22 = ВопросыРазделаИЗначения["ReRentSumM2"];
			ЗначенияВопросаГ23 = ВопросыРазделаИЗначения["PaymentM2"];
			ЗначенияВопросаГ24 = ВопросыРазделаИЗначения["RevaluationM2"];
			ЗначенияВопросаГ25 = ВопросыРазделаИЗначения["OtherChangesM2"];
			ЗначенияВопросаГ26 = ВопросыРазделаИЗначения["DebtBalanceM3"];
			
			// графа 21 + графа 22 - графа 23 + графа 24 + графа 25 = графа 26.
			Если ЗначенияВопросаГ26 <> Неопределено И ЗначенияВопросаГ26.Результат = 0 Тогда
				Если (ЗначенияВопросаГ21 <> Неопределено И ЗначенияВопросаГ21.Результат = 0)
				   И (ЗначенияВопросаГ22 <> Неопределено И ЗначенияВопросаГ22.Результат = 0)
				   И (ЗначенияВопросаГ23 <> Неопределено И ЗначенияВопросаГ23.Результат = 0)
				   И (ЗначенияВопросаГ24 <> Неопределено И ЗначенияВопросаГ24.Результат = 0)
				   И (ЗначенияВопросаГ25 <> Неопределено И ЗначенияВопросаГ25.Результат = 0) Тогда
				   Если ЗначенияВопросаГ21.Значение +  ЗначенияВопросаГ22.Значение
					  - ЗначенияВопросаГ23.Значение +  ЗначенияВопросаГ24.Значение
					  + ЗначенияВопросаГ25.Значение <> ЗначенияВопросаГ26.Значение Тогда
						Описание = НСтр("ru = 'ОШИБКА: Не выполняется условие: ''графа 21 + графа 22 - графа 23 + графа 24 +  графа 25 = графа 26''';
										|en = 'ОШИБКА: Не выполняется условие: ''графа 21 + графа 22 - графа 23 + графа 24 +  графа 25 = графа 26'''");
						РезультатПроверки = РезультатПроверки + ?(РезультатПроверки < 2, 2, 0);
						
						ЗаписатьСообщениеОбОшибкеВСтроке(Форма, ПараметрыВыгрузки,
							ЗначенияВопросаГ26, Описание, ИмяРазделаИТаблицы[0], СтрНомСтроки, НомСтр);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			ЗначенияВопросаГ26 = ВопросыРазделаИЗначения["DebtBalanceM3"];
			ЗначенияВопросаГ27 = ВопросыРазделаИЗначения["ReRentSumM3"];
			ЗначенияВопросаГ28 = ВопросыРазделаИЗначения["PaymentM3"];
			ЗначенияВопросаГ29 = ВопросыРазделаИЗначения["RevaluationM3"];
			ЗначенияВопросаГ30 = ВопросыРазделаИЗначения["OtherChangesM3"];
			ЗначенияВопросаГ31 = ВопросыРазделаИЗначения["DebtBalanceFinal"];
			
			// графа 26 + графа 27 - графа 28 + графа 29 + графа 30 = графа 31.
			Если ЗначенияВопросаГ31 <> Неопределено И ЗначенияВопросаГ31.Результат = 0 Тогда
				Если (ЗначенияВопросаГ26 <> Неопределено И ЗначенияВопросаГ26.Результат = 0)
				   И (ЗначенияВопросаГ27 <> Неопределено И ЗначенияВопросаГ27.Результат = 0)
				   И (ЗначенияВопросаГ28 <> Неопределено И ЗначенияВопросаГ28.Результат = 0)
				   И (ЗначенияВопросаГ29 <> Неопределено И ЗначенияВопросаГ29.Результат = 0)
				   И (ЗначенияВопросаГ30 <> Неопределено И ЗначенияВопросаГ30.Результат = 0) Тогда
				   Если ЗначенияВопросаГ26.Значение +  ЗначенияВопросаГ27.Значение
					  - ЗначенияВопросаГ28.Значение +  ЗначенияВопросаГ29.Значение
					  + ЗначенияВопросаГ30.Значение <> ЗначенияВопросаГ31.Значение Тогда
						Описание = НСтр("ru = 'ОШИБКА: Не выполняется условие: ''графа 26 + графа 27 - графа 28 + графа 29 + графа 30 = графа 31''';
										|en = 'ОШИБКА: Не выполняется условие: ''графа 26 + графа 27 - графа 28 + графа 29 + графа 30 = графа 31'''");
						РезультатПроверки = РезультатПроверки + ?(РезультатПроверки < 2, 2, 0);
						
						ЗаписатьСообщениеОбОшибкеВСтроке(Форма, ПараметрыВыгрузки,
							ЗначенияВопросаГ31, Описание, ИмяРазделаИТаблицы[0], СтрНомСтроки, НомСтр);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
	// Проверка строки раздела 2.
	ИначеЕсли ИмяРазделаИТаблицы[1] = "RENT2" Тогда
		
		ЗначенияВопросаПрим  = ВопросыРазделаИЗначения["Comment_rent2"];
		
		ЗначенияВопросаПГ  = ВопросыРазделаИЗначения["PaymentFrequencyMain_rent2"];
		ЗначенияВопросаПП  = ВопросыРазделаИЗначения["PaymentFrequencyPercent_rent2"];
		
		Если ЗначенияВопросаПрим <> Неопределено И ЗначенияВопросаПрим.Результат = 0 Тогда
			Если ((ЗначенияВопросаПГ <> Неопределено И СокрЛП(ЗначенияВопросаПГ.Значение) = "4")
			  ИЛИ (ЗначенияВопросаПП <> Неопределено И СокрЛП(ЗначенияВопросаПП.Значение) = "4"))
				 И ПустаяСтрока(ЗначенияВопросаПрим.Значение) Тогда
				Описание = НСтр("ru='ОШИБКА: При выборе ''4. Другая периодичность'' в вопросах "
					+ "''Характеристики платежей по договору (периодичность платежей по договору (Основной долг | Проценты)'' "
					+ "следует указать ""Примечание""'");
				РезультатПроверки = 2;
				
				ЗаписатьСообщениеОбОшибкеВСтроке(Форма, ПараметрыВыгрузки,
					ЗначенияВопросаПрим, Описание, ИмяРазделаИТаблицы[0], СтрНомСтроки, НомСтр);
			КонецЕсли;
		КонецЕсли;
		
		ЗначенияВопросаГ16 = ВопросыРазделаИЗначения["DebtBalanceM1_rent2"];
		ЗначенияВопросаГ17 = ВопросыРазделаИЗначения["ReRentSumM1_rent2"];
		ЗначенияВопросаГ18 = ВопросыРазделаИЗначения["PaymentM1_rent2"];
		ЗначенияВопросаГ19 = ВопросыРазделаИЗначения["RevaluationM1_rent2"];
		ЗначенияВопросаГ20 = ВопросыРазделаИЗначения["OtherChangesM1_rent2"];
		ЗначенияВопросаГ21 = ВопросыРазделаИЗначения["DebtBalanceM2_rent2"];
		
		// графа 19 = графа 21 - графа 16 - графа 17 + графа 18 - графа 20.
		// графа 21 = графа 16 + графа 17 - графа 18 + графа 19 + графа 20.
		Если ЗначенияВопросаГ21 <> Неопределено И ЗначенияВопросаГ21.Результат = 0 Тогда
			Если (ЗначенияВопросаГ16 <> Неопределено И ЗначенияВопросаГ16.Результат = 0)
			   И (ЗначенияВопросаГ17 <> Неопределено И ЗначенияВопросаГ17.Результат = 0)
			   И (ЗначенияВопросаГ18 <> Неопределено И ЗначенияВопросаГ18.Результат = 0)
			   И (ЗначенияВопросаГ19 <> Неопределено И ЗначенияВопросаГ19.Результат = 0)
			   И (ЗначенияВопросаГ20 <> Неопределено И ЗначенияВопросаГ20.Результат = 0) Тогда
			   Если ЗначенияВопросаГ16.Значение +  ЗначенияВопросаГ17.Значение
				  - ЗначенияВопросаГ18.Значение +  ЗначенияВопросаГ19.Значение
				  + ЗначенияВопросаГ20.Значение <> ЗначенияВопросаГ21.Значение Тогда
					Описание = НСтр("ru = 'ОШИБКА: Не выполняется условие: ''графа 21 = графа 16 + графа 17 - графа 18 + графа 19 + графа 20''';
									|en = 'ОШИБКА: Не выполняется условие: ''графа 21 = графа 16 + графа 17 - графа 18 + графа 19 + графа 20'''");
					Описание = Описание + Символы.ПС
							 + НСтр("ru = 'ОШИБКА: Не выполняется условие: ''графа 19 = графа 21 - графа 16 - графа 17 + графа 18 - графа 20''';
									|en = 'ОШИБКА: Не выполняется условие: ''графа 19 = графа 21 - графа 16 - графа 17 + графа 18 - графа 20'''");
					РезультатПроверки = РезультатПроверки + ?(РезультатПроверки < 2, 2, 0);
					
					ЗаписатьСообщениеОбОшибкеВСтроке(Форма, ПараметрыВыгрузки,
						ЗначенияВопросаГ21, Описание, ИмяРазделаИТаблицы[0], СтрНомСтроки, НомСтр);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		ЗначенияВопросаГ21 = ВопросыРазделаИЗначения["DebtBalanceM2_rent2"];
		ЗначенияВопросаГ22 = ВопросыРазделаИЗначения["ReRentSumM2_rent2"];
		ЗначенияВопросаГ23 = ВопросыРазделаИЗначения["PaymentM2_rent2"];
		ЗначенияВопросаГ24 = ВопросыРазделаИЗначения["RevaluationM2_rent2"];
		ЗначенияВопросаГ25 = ВопросыРазделаИЗначения["OtherChangesM2_rent2"];
		ЗначенияВопросаГ26 = ВопросыРазделаИЗначения["DebtBalanceM3_rent2"];
		
		// графа 24 = графа 26 - графа 21 - графа 22 + графа 23 - графа 25.
		// графа 26 = графа 21 + графа 22 - графа 23 + графа 24 + графа 25.
		Если ЗначенияВопросаГ26 <> Неопределено И ЗначенияВопросаГ26.Результат = 0 Тогда
			Если (ЗначенияВопросаГ21 <> Неопределено И ЗначенияВопросаГ21.Результат = 0)
			   И (ЗначенияВопросаГ22 <> Неопределено И ЗначенияВопросаГ22.Результат = 0)
			   И (ЗначенияВопросаГ23 <> Неопределено И ЗначенияВопросаГ23.Результат = 0)
			   И (ЗначенияВопросаГ24 <> Неопределено И ЗначенияВопросаГ24.Результат = 0)
			   И (ЗначенияВопросаГ25 <> Неопределено И ЗначенияВопросаГ25.Результат = 0) Тогда
			   Если ЗначенияВопросаГ21.Значение +  ЗначенияВопросаГ22.Значение
				  - ЗначенияВопросаГ23.Значение +  ЗначенияВопросаГ24.Значение
				  + ЗначенияВопросаГ25.Значение <> ЗначенияВопросаГ26.Значение Тогда
					Описание = НСтр("ru = 'ОШИБКА: Не выполняется условие: ''графа 26 = графа 21 + графа 22 - графа 23 + графа 24 + графа 25''';
									|en = 'ОШИБКА: Не выполняется условие: ''графа 26 = графа 21 + графа 22 - графа 23 + графа 24 + графа 25'''");
					Описание = Описание + Символы.ПС
							 + НСтр("ru = 'ОШИБКА: Не выполняется условие: ''графа 24 = графа 26 - графа 21 - графа 22 + графа 23 - графа 25''';
									|en = 'ОШИБКА: Не выполняется условие: ''графа 24 = графа 26 - графа 21 - графа 22 + графа 23 - графа 25'''");
					РезультатПроверки = РезультатПроверки + ?(РезультатПроверки < 2, 2, 0);
					
					ЗаписатьСообщениеОбОшибкеВСтроке(Форма, ПараметрыВыгрузки,
						ЗначенияВопросаГ26, Описание, ИмяРазделаИТаблицы[0], СтрНомСтроки, НомСтр);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		ЗначенияВопросаГ26 = ВопросыРазделаИЗначения["DebtBalanceM3_rent2"];
		ЗначенияВопросаГ27 = ВопросыРазделаИЗначения["ReRentSumM3_rent2"];
		ЗначенияВопросаГ28 = ВопросыРазделаИЗначения["PaymentM3_rent2"];
		ЗначенияВопросаГ29 = ВопросыРазделаИЗначения["RevaluationM3_rent2"];
		ЗначенияВопросаГ30 = ВопросыРазделаИЗначения["OtherChangesM3_rent2"];
		ЗначенияВопросаГ31 = ВопросыРазделаИЗначения["DebtBalanceFinal_rent2"];
		
		// графа 31 = графа 26 + графа 27 - графа 28 + графа 29 + графа 30.
		// графа 29 = графа 31 - графа 26 - графа 27 + графа 28 - графа 30.
		Если ЗначенияВопросаГ31 <> Неопределено И ЗначенияВопросаГ31.Результат = 0 Тогда
			Если (ЗначенияВопросаГ26 <> Неопределено И ЗначенияВопросаГ26.Результат = 0)
			   И (ЗначенияВопросаГ27 <> Неопределено И ЗначенияВопросаГ27.Результат = 0)
			   И (ЗначенияВопросаГ28 <> Неопределено И ЗначенияВопросаГ28.Результат = 0)
			   И (ЗначенияВопросаГ29 <> Неопределено И ЗначенияВопросаГ29.Результат = 0)
			   И (ЗначенияВопросаГ30 <> Неопределено И ЗначенияВопросаГ30.Результат = 0) Тогда
			   Если ЗначенияВопросаГ26.Значение +  ЗначенияВопросаГ27.Значение
				  - ЗначенияВопросаГ28.Значение +  ЗначенияВопросаГ29.Значение
				  + ЗначенияВопросаГ30.Значение <> ЗначенияВопросаГ31.Значение Тогда
					Описание = НСтр("ru = 'ОШИБКА: Не выполняется условие: ''графа 31 = графа 26 + графа 27 - графа 28 + графа 29 + графа 30''';
									|en = 'ОШИБКА: Не выполняется условие: ''графа 31 = графа 26 + графа 27 - графа 28 + графа 29 + графа 30'''");
					Описание = Описание + Символы.ПС
							 + НСтр("ru = 'ОШИБКА: Не выполняется условие: ''графа 29 = графа 31 - графа 26 - графа 27 + графа 28 - графа 30''';
									|en = 'ОШИБКА: Не выполняется условие: ''графа 29 = графа 31 - графа 26 - графа 27 + графа 28 - графа 30'''");
							РезультатПроверки = РезультатПроверки + ?(РезультатПроверки < 2, 2, 0);
					
					ЗаписатьСообщениеОбОшибкеВСтроке(Форма, ПараметрыВыгрузки,
						ЗначенияВопросаГ31, Описание, ИмяРазделаИТаблицы[0], СтрНомСтроки, НомСтр);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат РезультатПроверки;
	
КонецФункции

Процедура ВыполнитьПроверкуЗначения(РезультатПроверки, Описание,
									ВопросАнкеты,      ПараметрыВыгрузки,
									ЗначениеОтвета,    НомСтроки = 0) Экспорт
	
	Если СтрДлина(ЗначениеОтвета) = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ИмяПоказателя = ВопросАнкеты["QUESTION_NAME_ID"];
	
	Если РезультатПроверки > 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Проверка титульного листа.
	Если ИмяПоказателя = "Inn" Тогда
		Если НЕ РегламентированнаяОтчетностьБанкРоссии.ИННСоответствуетТребованиям(ЗначениеОтвета) Тогда
			Описание = НСтр("ru = 'Некорректный код, проверьте правильность ввода!';
							|en = 'Некорректный код, проверьте правильность ввода!'");
			РезультатПроверки = 2;
		КонецЕсли;
	ИначеЕсли ИмяПоказателя = "Okpo" Тогда
		Если НЕ РегламентированнаяОтчетностьБанкРоссии.КодПоОКПОСоответствуетТребованиям(ЗначениеОтвета) Тогда
			Описание = НСтр("ru = 'Некорректный код, проверьте правильность ввода!';
							|en = 'Некорректный код, проверьте правильность ввода!'");
			РезультатПроверки = 2;
		КонецЕсли;
	ИначеЕсли ИмяПоказателя = "Ogrn" Тогда
		Если НЕ РегламентированнаяОтчетностьБанкРоссии.ОГРНСоответствуетТребованиям(ЗначениеОтвета) Тогда
			Описание = НСтр("ru = 'Некорректный код, проверьте правильность ввода!';
							|en = 'Некорректный код, проверьте правильность ввода!'");
			РезультатПроверки = 2;
		КонецЕсли;
	ИначеЕсли ИмяПоказателя = "OKTMO" Тогда
		Если НЕ РегламентированнаяОтчетностьБанкРоссии.ОКТМОСоответствуетТребованиям(ЗначениеОтвета) Тогда
			Описание = НСтр("ru = 'Некорректный код ОКТМО';
							|en = 'Некорректный код ОКТМО'");
			РезультатПроверки = 2;
		КонецЕсли;
	ИначеЕсли ИмяПоказателя = "FIOUser" Тогда
		Если НЕ РегламентированнаяОтчетностьБанкРоссии.НетЦифрВСтроке(ЗначениеОтвета) Тогда
			Описание = НСтр("ru = 'Значение не должно содержать цифры';
							|en = 'Значение не должно содержать цифры'");
			РезультатПроверки = 1;
		КонецЕсли;
	ИначеЕсли ИмяПоказателя = "SignFIO" Тогда
		Если НЕ РегламентированнаяОтчетностьБанкРоссии.НетЦифрВСтроке(ЗначениеОтвета) Тогда
			Описание = НСтр("ru = 'Значение не должно содержать цифры';
							|en = 'Значение не должно содержать цифры'");
			РезультатПроверки = 1;
		КонецЕсли;
	ИначеЕсли ИмяПоказателя = "EmailUser" Тогда
		Если НЕ РегламентированнаяОтчетностьБанкРоссии.АдресЭлектроннойПочтыСоответствуетТребованиям(ЗначениеОтвета) Тогда
			Описание = НСтр("ru = 'Некорректный адрес эл. почты';
							|en = 'Некорректный адрес эл. почты'");
			РезультатПроверки = 1;
		КонецЕсли;
	ИначеЕсли ИмяПоказателя = "EmailRespondent" Тогда
		Если НЕ РегламентированнаяОтчетностьБанкРоссии.АдресЭлектроннойПочтыСоответствуетТребованиям(ЗначениеОтвета) Тогда
			Описание = НСтр("ru = 'Некорректный адрес эл. почты';
							|en = 'Некорректный адрес эл. почты'");
			РезультатПроверки = 1;
		КонецЕсли;
	ИначеЕсли ИмяПоказателя = "Web" Тогда
		Если НЕ РегламентированнаяОтчетностьБанкРоссии.АдресВебСервераСоответствуетТребованиям(ЗначениеОтвета) Тогда
			Описание = НСтр("ru = 'Некорректный адрес веб сайта';
							|en = 'Некорректный адрес веб сайта'");
			РезультатПроверки = 1;
		КонецЕсли;
	ИначеЕсли ИмяПоказателя = "Phone" Тогда
		Если НЕ РегламентированнаяОтчетностьБанкРоссии.НомерТелефонаСоответствуетТребованиям(ЗначениеОтвета) Тогда
			Описание = НСтр("ru = 'Недопустимые символы в номере телефона';
							|en = 'Недопустимые символы в номере телефона'");
			РезультатПроверки = 2;
		КонецЕсли;
	ИначеЕсли ИмяПоказателя = "Fax" Тогда
		Если НЕ РегламентированнаяОтчетностьБанкРоссии.НомерТелефонаСоответствуетТребованиям(ЗначениеОтвета) Тогда
			Описание = НСтр("ru = 'Недопустимые символы в номере Факса';
							|en = 'Недопустимые символы в номере Факса'");
			РезультатПроверки = 1;
		КонецЕсли;
		
	ИначеЕсли ИмяПоказателя = "Razdel1" ИЛИ ИмяПоказателя = "ReportType" Тогда
		ТаблицаРаздела1 = ПараметрыВыгрузки.МетаданныеШаблона.ТаблицыРазделов["RENT"];
		Если ЗначениеОтвета = "00" Тогда
			Если ТаблицаРаздела1.Количество() > 0 Тогда
				Описание = НСтр("ru = 'В разделе 1 присутствуют записи в таблице';
								|en = 'В разделе 1 присутствуют записи в таблице'");
				РезультатПроверки = 2;
			КонецЕсли;
		Иначе
			Если ТаблицаРаздела1.Количество() = 0 Тогда
				Описание = НСтр("ru = 'В разделе 1 отсутствуют записи в таблице';
								|en = 'В разделе 1 отсутствуют записи в таблице'");
				РезультатПроверки = 2;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ИмяПоказателя = "Razdel2" Тогда
		ТаблицаРаздела1 = ПараметрыВыгрузки.МетаданныеШаблона.ТаблицыРазделов["RENT2"];
		Если ЗначениеОтвета = "00" Тогда
			Если ТаблицаРаздела1.Количество() > 0 Тогда
				Описание = НСтр("ru = 'В разделе 2 присутствуют записи в таблице';
								|en = 'В разделе 2 присутствуют записи в таблице'");
				РезультатПроверки = 2;
			КонецЕсли;
		Иначе
			Если ТаблицаРаздела1.Количество() = 0 Тогда
				Описание = НСтр("ru = 'В разделе 2 отсутствуют записи в таблице';
								|en = 'В разделе 2 отсутствуют записи в таблице'");
				РезультатПроверки = 2;
			КонецЕсли;
		КонецЕсли;
		
	// Проверка раздела 1.
	ИначеЕсли ИмяПоказателя = "RentEndDate" Тогда
		Если НРег(ЗначениеОтвета) <> "бессрочная" Тогда
			Если НЕ РегламентированнаяОтчетностьБанкРоссии.СтрокаПредставляетЛокДату(ЗначениеОтвета) Тогда
				Описание = НСтр("ru = 'Значение должно быть датой в формате (ДД.ММ.ГГГГГ) либо ""Бессрочная""';
								|en = 'Значение должно быть датой в формате (ДД.ММ.ГГГГГ) либо ""Бессрочная""'");
				РезультатПроверки = 2;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ИмяПоказателя = "PaymentPercentage" И ПараметрыВыгрузки.ИмяМакетаШаблона = "ШаблонБанка2022Кв1" Тогда
		ДесРазделитель = Сред(Строка(1/2), 2, 1);
		Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(СтрЗаменить(ЗначениеОтвета, ДесРазделитель, "")) Тогда
			Если Число(" " + ЗначениеОтвета) < 0 Тогда
				Описание = НСтр("ru = 'Положительные значения в процентном формате, или коды ФС и СО';
								|en = 'Положительные значения в процентном формате, или коды ФС и СО'");
				РезультатПроверки = 2;
			КонецЕсли;
		Иначе
			Если ЗначениеОтвета <> "ФС" И ЗначениеОтвета <> "СО" Тогда
				Описание = НСтр("ru = 'Положительные значения в процентном формате, или коды ФС и СО';
								|en = 'Положительные значения в процентном формате, или коды ФС и СО'");
				РезультатПроверки = 2;
			КонецЕсли;
		КонецЕсли;
		
	// Проверка раздела 2.
	ИначеЕсли ИмяПоказателя = "RentEndDate_rent2" Тогда
		Если НРег(ЗначениеОтвета) <> "бессрочная" Тогда
			Если НЕ РегламентированнаяОтчетностьБанкРоссии.СтрокаПредставляетЛокДату(ЗначениеОтвета) Тогда
				Описание = НСтр("ru = 'Значение должно быть датой в формате (ДД.ММ.ГГГГГ) либо ""Бессрочная""';
								|en = 'Значение должно быть датой в формате (ДД.ММ.ГГГГГ) либо ""Бессрочная""'");
				РезультатПроверки = 2;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ИмяПоказателя = "PaymentPercentage_rent2" И ПараметрыВыгрузки.ИмяМакетаШаблона = "ШаблонБанка2022Кв1" Тогда
		ДесРазделитель = Сред(Строка(1/2), 2, 1);
		Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(СтрЗаменить(ЗначениеОтвета, ДесРазделитель, "")) Тогда
			Если Число(" " + ЗначениеОтвета) < 0 Тогда
				Описание = НСтр("ru = 'Положительные значения в процентном формате, или коды ФС и СО';
								|en = 'Положительные значения в процентном формате, или коды ФС и СО'");
				РезультатПроверки = 2;
			КонецЕсли;
		Иначе
			Если ЗначениеОтвета <> "ФС" И ЗначениеОтвета <> "СО" Тогда
				Описание = НСтр("ru = 'Положительные значения в процентном формате, или коды ФС и СО';
								|en = 'Положительные значения в процентном формате, или коды ФС и СО'");
				РезультатПроверки = 2;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьДанныеОпросаПоПериоду(ПараметрыВыгрузки)
	
	КодПериода = "0";
	ГодПериода = "0";
	
	КодПериодичности = Число(" " + РегламентированнаяОтчетностьБанкРоссии.ЗначениеДанныхОпросаПоКлючу(
		ПараметрыВыгрузки, "PERIODTYPE"));
	
	Если КодПериодичности > 0 Тогда
		КодПериода = Строка(ПараметрыВыгрузки.ОтчетКвартал);
		ГодПериода = Формат(ПараметрыВыгрузки.ОтчетГод, "ЧГ=");
	КонецЕсли;
	
	РегламентированнаяОтчетностьБанкРоссии.ЗаписатьДанныеОпросаПоКлючу(
		ПараметрыВыгрузки, "PERIODVALUE", КодПериода);
	РегламентированнаяОтчетностьБанкРоссии.ЗаписатьДанныеОпросаПоКлючу(
		ПараметрыВыгрузки, "PERIODYEAR", ГодПериода);
	
КонецПроцедуры

Процедура ЗаписатьСообщениеОбОшибкеВСтроке(Форма, ПараметрыВыгрузки,
		ЗначенияВопроса, Описание, ИмяРаздела, СтрНомСтр, НомСтрНовТаб)
	
	РегламентированнаяОтчетностьБанкРоссии.ЗаписатьСообщениеОбОшибке(
		ЗначенияВопроса.Вопрос, ПараметрыВыгрузки, Описание, НомСтрНовТаб);
	
	МестоОшибки = "Раздел " + Прав(ИмяРаздела, 1)
		+ " [строка " + СтрНомСтр + "]: """ + ЗначенияВопроса.Вопрос["QUESTION_TEXT"] + """";
	ТекстОшибки = Описание + Символы.ПС + Символы.Таб + "<" + МестоОшибки + ">";
	РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(Форма, ТекстОшибки, ИмяРаздела,
		ЗначенияВопроса.ИмяКолонки + "_" + СтрНомСтр);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

ИнициализацияПеременныхМодуля();

#КонецЕсли