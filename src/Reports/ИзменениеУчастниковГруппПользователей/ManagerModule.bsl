///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВариантыОтчетов

// См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.
Процедура ПередДобавлениемКомандОтчетов(КомандыОтчетов, Параметры, СтандартнаяОбработка) Экспорт
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ВариантыОтчетов")
	 Или Не ПравоДоступа("Просмотр", Метаданные.Отчеты.ИзменениеУчастниковГруппПользователей)
	 Или СтандартныеПодсистемыСервер.ЭтоБазоваяВерсияКонфигурации() Тогда
		Возврат;
	КонецЕсли;
	
	Представление = Неопределено;
	
	Если Параметры.ИмяФормы = "Справочник.Пользователи.Форма.ФормаСписка" Тогда
		Представление = НСтр("ru = 'Изменение участников групп пользователей';
							|en = 'Changes in user group membership'");
		КлючВарианта = "ИзменениеУчастниковГруппПользователей";
		
	ИначеЕсли Параметры.ИмяФормы = "Справочник.Пользователи.Форма.ФормаЭлемента" Тогда
		Представление = НСтр("ru = 'Изменение участия в группах пользователей';
							|en = 'Change in user group membership'");
		КлючВарианта = "ИзменениеУчастниковГруппПользователей";
		
	ИначеЕсли Параметры.ИмяФормы = "Справочник.ВнешниеПользователи.Форма.ФормаСписка" Тогда
		Представление = НСтр("ru = 'Изменение участников групп внешних пользователей';
							|en = 'Changes in external user group membership'");
		КлючВарианта = "ИзменениеУчастниковГруппВнешнихПользователей";
		
	ИначеЕсли Параметры.ИмяФормы = "Справочник.ВнешниеПользователи.Форма.ФормаЭлемента" Тогда
		Представление = НСтр("ru = 'Изменение участия в группах внешних пользователей';
							|en = 'Change in external user group membership'");
		КлючВарианта = "ИзменениеУчастниковГруппВнешнихПользователей";
	КонецЕсли;
	
	Если Представление = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Команда = КомандыОтчетов.Добавить();
	Команда.Представление = Представление;
	Команда.Менеджер = "Отчет.ИзменениеУчастниковГруппПользователей";
	Команда.КлючВарианта = КлючВарианта;
	Команда.ТолькоВоВсехДействиях = Истина;
	Команда.Важность = "СмТакже";
	
	Если КлючВарианта = "ИзменениеУчастниковГруппВнешнихПользователей" Тогда
		Команда.ФункциональныеОпции = "ИспользоватьВнешнихПользователей";
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//   Настройки - см. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.Настройки.
//   НастройкиОтчета - см. ВариантыОтчетов.ОписаниеОтчета.
//
Процедура НастроитьВариантыОтчета(Настройки, НастройкиОтчета) Экспорт
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ВариантыОтчетов") Тогда
		Возврат;
	КонецЕсли;
	
	МодульВариантыОтчетов = ОбщегоНазначения.ОбщийМодуль("ВариантыОтчетов");
	НастройкиОтчета.ОпределитьНастройкиФормы = Истина;
	НастройкиОтчета.ГруппироватьПоОтчету = Ложь;
	
	НастройкиВарианта = МодульВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "ИзменениеУчастниковГруппПользователей");
	НастройкиВарианта.Описание =
		НСтр("ru = 'Выводит изменения состава групп пользователей с учетом иерархии за указанный период по событиям журнала регистрации.';
			|en = 'Reads the event log and shows you the changes in the membership of user groups for the given time period considering the group hierarchy.'");
	
	НастройкиВарианта = МодульВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "ИзменениеУчастниковГруппВнешнихПользователей");
	НастройкиВарианта.ФункциональныеОпции.Добавить("ИспользоватьВнешнихПользователей");
	НастройкиВарианта.Описание =
		НСтр("ru = 'Выводит изменения состава групп внешних пользователей с учетом иерархии за указанный период по событиям журнала регистрации.';
			|en = 'Reads the event log and shows you the changes in the membership of external user groups for the given time period considering the group hierarchy.'");
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВариантыОтчетов

#КонецОбласти

#КонецОбласти

#КонецЕсли
