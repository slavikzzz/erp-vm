#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВариантыОтчетов

// См. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.
//
Процедура НастроитьВариантыОтчета(Настройки, НастройкиОтчета) Экспорт
	
	НастройкиОтчета.ОпределитьНастройкиФормы = Истина;
	
	НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "Т6а");
	НастройкиВарианта.Описание = НСтр("ru = 'Унифицированная форма № Т-6а';
										|en = 'Unified form No. T-6a'");
	НастройкиВарианта.Включен = Ложь;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВариантыОтчетов

#КонецОбласти

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ЗарплатаКадрыОтчеты.ВывестиВКоллекциюПечатнуюФорму("Отчет.ПечатнаяФормаТ6а",
		МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	
КонецПроцедуры

Процедура Сформировать(ДокументРезультат, РезультатКомпоновки, ОбъектыПечати = Неопределено, ПараметрыПечати = Неопределено) Экспорт
	
	Если РезультатКомпоновки.ОтчетПустой Тогда
		Возврат;
	КонецЕсли;
	
	ПоВидамОтпусков = Ложь;
	Если ПараметрыПечати <> Неопределено И ПараметрыПечати.Свойство("ПоВидамОтпусков") Тогда 
		ПоВидамОтпусков = Истина;
	КонецЕсли;
	
	ДокументРезультат.КлючПараметровПечати = "ПараметрыПечати_Т6а";
	ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ДокументРезультат.АвтоМасштаб = Истина;
	
	Для Каждого ДанныеСсылок Из РезультатКомпоновки.ДанныеОтчета.Строки Цикл
		
		Если ДокументРезультат.ВысотаТаблицы > 0 Тогда
			ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПерваяСтрокаПечатнойФормы = ДокументРезультат.ВысотаТаблицы + 1;
		
		ДанныеПользовательскихПолейСсылки = ЗарплатаКадрыОтчеты.ЗначенияЗаполненияПользовательскихПолей(РезультатКомпоновки.ИдентификаторыМакета, ДанныеСсылок);
		
		ЗарплатаКадрыОтчеты.ВывестиВДокументРезультатОбластиМакета(
			ДокументРезультат, РезультатКомпоновки.МакетПечатнойФормы, "Шапка",
			ДанныеСсылок,
			ДанныеПользовательскихПолейСсылки);
		
		ОбластьШапкиТаблицы = РезультатКомпоновки.МакетПечатнойФормы.ПолучитьОбласть("ШапкаТаблицы");
		ЗарплатаКадрыОтчеты.ЗаполнитьПараметрыОбластиМакета(ОбластьШапкиТаблицы,
			ДанныеСсылок,
			ДанныеПользовательскихПолейСсылки);
		
		ДокументРезультат.Вывести(ОбластьШапкиТаблицы);
		
		ОбластьПодвал = РезультатКомпоновки.МакетПечатнойФормы.ПолучитьОбласть("Подвал");
		ЗарплатаКадрыОтчеты.ЗаполнитьПараметрыОбластиМакета(ОбластьПодвал,
			ДанныеСсылок,
			ДанныеПользовательскихПолейСсылки);
			
		ДанныеОтпусков = Неопределено;	
		ДанныеСтроки = Неопределено;	
		НомерСтроки = Неопределено;
		
		Для Каждого ДанныеДетальныхЗаписей Из ДанныеСсылок.Строки Цикл
			
			Если НомерСтроки = Неопределено Или НомерСтроки <> ДанныеДетальныхЗаписей.СсылкаНаОбъектДанныеОтпусковНомерСтроки Или ПоВидамОтпусков Тогда 
			
				Если НомерСтроки <> Неопределено Тогда
					ВывестиСтрокуОтчета(РезультатКомпоновки, ДокументРезультат, ОбластьШапкиТаблицы, ОбластьПодвал, ДанныеСсылок, ДанныеСтроки, ДанныеОтпусков);
				КонецЕсли;
				
				ДанныеОтпусков = Новый Структура("СсылкаНаОбъектДанныеОтпусковНаименованиеОсновногоОтпускаНаПечать, СсылкаНаОбъектДанныеОтпусковКоличествоДнейОсновногоОтпускаНаПечать,
					|ДополнительныйОтпускПредставление, СсылкаНаОбъектДанныеОтпусковНачалоПериодаЗаКоторыйПредоставляетсяОтпуск,
					|СсылкаНаОбъектДанныеОтпусковКонецПериодаЗаКоторыйПредоставляетсяОтпуск, СсылкаНаОбъектДанныеОтпусковКоличествоДней,
					|СсылкаНаОбъектДанныеОтпусковДатаНачала, СсылкаНаОбъектДанныеОтпусковДатаОкончания, СсылкаНаОбъектДанныеОтпусковОснование");
			
			КонецЕсли;
			
			Если ДанныеДетальныхЗаписей.СсылкаНаОбъектДанныеОтпусковОсновной Тогда 
				ДанныеОтпусков.СсылкаНаОбъектДанныеОтпусковНаименованиеОсновногоОтпускаНаПечать = ДанныеДетальныхЗаписей.СсылкаНаОбъектДанныеОтпусковНаименованиеОсновногоОтпускаНаПечать;
				ДанныеОтпусков.СсылкаНаОбъектДанныеОтпусковКоличествоДнейОсновногоОтпускаНаПечать = ДанныеДетальныхЗаписей.СсылкаНаОбъектДанныеОтпусковКоличествоДнейОсновногоОтпускаНаПечать;
				ДанныеОтпусков.СсылкаНаОбъектДанныеОтпусковНачалоПериодаЗаКоторыйПредоставляетсяОтпуск = ДанныеДетальныхЗаписей.СсылкаНаОбъектДанныеОтпусковНачалоПериодаЗаКоторыйПредоставляетсяОтпуск;
				ДанныеОтпусков.СсылкаНаОбъектДанныеОтпусковКонецПериодаЗаКоторыйПредоставляетсяОтпуск = ДанныеДетальныхЗаписей.СсылкаНаОбъектДанныеОтпусковКонецПериодаЗаКоторыйПредоставляетсяОтпуск;
				ДанныеОтпусков.СсылкаНаОбъектДанныеОтпусковОснование = ДанныеДетальныхЗаписей.СсылкаНаОбъектДанныеОтпусковОснование;
			Иначе 
				ДанныеОтпусков.ДополнительныйОтпускПредставление = Строка(ДанныеОтпусков.ДополнительныйОтпускПредставление)
					+ ?(ЗначениеЗаполнено(ДанныеОтпусков.ДополнительныйОтпускПредставление), ", ", " ")
					+ ДанныеДетальныхЗаписей.СсылкаНаОбъектДанныеОтпусковНаименованиеДополнительногоОтпускаНаПечать
					+ ДанныеДетальныхЗаписей.СсылкаНаОбъектДанныеОтпусковКоличествоДнейДополнительногоОтпускаНаПечать;
				Если Не ЗначениеЗаполнено(ДанныеОтпусков.СсылкаНаОбъектДанныеОтпусковНачалоПериодаЗаКоторыйПредоставляетсяОтпуск) Тогда 
					ДанныеОтпусков.СсылкаНаОбъектДанныеОтпусковНачалоПериодаЗаКоторыйПредоставляетсяОтпуск = ДанныеДетальныхЗаписей.СсылкаНаОбъектДанныеОтпусковНачалоПериодаЗаКоторыйПредоставляетсяОтпуск;
					ДанныеОтпусков.СсылкаНаОбъектДанныеОтпусковКонецПериодаЗаКоторыйПредоставляетсяОтпуск = ДанныеДетальныхЗаписей.СсылкаНаОбъектДанныеОтпусковКонецПериодаЗаКоторыйПредоставляетсяОтпуск;
				КонецЕсли;
				Если Не ЗначениеЗаполнено(ДанныеОтпусков.СсылкаНаОбъектДанныеОтпусковОснование) Тогда 
					ДанныеОтпусков.СсылкаНаОбъектДанныеОтпусковОснование = ДанныеДетальныхЗаписей.СсылкаНаОбъектДанныеОтпусковОснование;
				КонецЕсли;
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(ДанныеОтпусков.СсылкаНаОбъектДанныеОтпусковДатаНачала) Или Не ЗначениеЗаполнено(ДанныеОтпусков.СсылкаНаОбъектДанныеОтпусковДатаОкончания) Тогда 
				ДанныеОтпусков.СсылкаНаОбъектДанныеОтпусковДатаНачала = ДанныеДетальныхЗаписей.СсылкаНаОбъектДанныеОтпусковДатаНачала;
				ДанныеОтпусков.СсылкаНаОбъектДанныеОтпусковДатаОкончания = ДанныеДетальныхЗаписей.СсылкаНаОбъектДанныеОтпусковДатаОкончания;
			ИначеЕсли ЗначениеЗаполнено(ДанныеДетальныхЗаписей.СсылкаНаОбъектДанныеОтпусковДатаНачала) И ЗначениеЗаполнено(ДанныеДетальныхЗаписей.СсылкаНаОбъектДанныеОтпусковДатаОкончания) Тогда 
				ДанныеОтпусков.СсылкаНаОбъектДанныеОтпусковДатаНачала = Мин(ДанныеОтпусков.СсылкаНаОбъектДанныеОтпусковДатаНачала, ДанныеДетальныхЗаписей.СсылкаНаОбъектДанныеОтпусковДатаНачала);
				ДанныеОтпусков.СсылкаНаОбъектДанныеОтпусковДатаОкончания = Макс(ДанныеОтпусков.СсылкаНаОбъектДанныеОтпусковДатаОкончания, ДанныеДетальныхЗаписей.СсылкаНаОбъектДанныеОтпусковДатаОкончания);
			КонецЕсли;
			
			ДанныеОтпусков.СсылкаНаОбъектДанныеОтпусковКоличествоДней = ?(ЗначениеЗаполнено(ДанныеОтпусков.СсылкаНаОбъектДанныеОтпусковКоличествоДней),
				ДанныеОтпусков.СсылкаНаОбъектДанныеОтпусковКоличествоДней + ДанныеДетальныхЗаписей.СсылкаНаОбъектДанныеОтпусковКоличествоДней + ДанныеДетальныхЗаписей.СсылкаНаОбъектДанныеОтпусковКоличествоДнейКомпенсации, 
				ДанныеДетальныхЗаписей.СсылкаНаОбъектДанныеОтпусковКоличествоДней + ДанныеДетальныхЗаписей.СсылкаНаОбъектДанныеОтпусковКоличествоДнейКомпенсации);
				
			ДанныеСтроки = ДанныеДетальныхЗаписей;
			НомерСтроки = ДанныеДетальныхЗаписей.СсылкаНаОбъектДанныеОтпусковНомерСтроки;
			
		КонецЦикла;
		
		Если НомерСтроки <> Неопределено Тогда
			ВывестиСтрокуОтчета(РезультатКомпоновки, ДокументРезультат, ОбластьШапкиТаблицы, ОбластьПодвал, ДанныеСсылок, ДанныеСтроки, ДанныеОтпусков);
		КонецЕсли;
		
		ДокументРезультат.Вывести(ОбластьПодвал);
		
		Если ОбъектыПечати <> Неопределено Тогда
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ДокументРезультат, ПерваяСтрокаПечатнойФормы, ОбъектыПечати, ДанныеСсылок.СсылкаНаОбъект);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ВывестиСтрокуОтчета(РезультатКомпоновки, ДокументРезультат, ОбластьШапкиТаблицы, ОбластьПодвал, ДанныеСсылок, ДанныеСтроки, ДанныеОтпусков)
	
	ДанныеПользовательскихПолей = ЗарплатаКадрыОтчеты.ЗначенияЗаполненияПользовательскихПолей(РезультатКомпоновки.ИдентификаторыМакета, ДанныеСтроки);
	
	ОбластьСтроки = РезультатКомпоновки.МакетПечатнойФормы.ПолучитьОбласть("Строка");
	ЗарплатаКадрыОтчеты.ЗаполнитьПараметрыОбластиМакета(ОбластьСтроки,
		ДанныеСсылок,
		ДанныеСтроки,
		ДанныеОтпусков,
		ДанныеПользовательскихПолей);
	
	СписокУмещаемыхОбластей = Новый Массив;
	СписокУмещаемыхОбластей.Добавить(ОбластьСтроки);
	СписокУмещаемыхОбластей.Добавить(ОбластьПодвал);
	
	Если Не ДокументРезультат.ПроверитьВывод(СписокУмещаемыхОбластей) Тогда
		
		ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
		ДокументРезультат.Вывести(ОбластьШапкиТаблицы);
		
	КонецЕсли;
	
	ДокументРезультат.Вывести(ОбластьСтроки);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли