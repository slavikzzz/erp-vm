#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область КомандыПодменюОтчеты

// Добавляет команду отчета в список команд.
// 
// Параметры:
//   КомандыОтчетов - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
// Возвращаемое значение:
//   СтрокаТаблицыЗначений - новая команда отчета.
Функция ДобавитьКомандуОтчета(КомандыОтчетов) Экспорт

	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.АнализРаспределенияНДС) Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		КомандаОтчет.Менеджер = Метаданные.Отчеты.АнализРаспределенияНДС.ПолноеИмя();
		КомандаОтчет.Представление = НСтр("ru = 'Анализ распределения НДС';
											|en = 'VAT allocation analysis'");
		КомандаОтчет.МножественныйВыбор = Истина;
		КомандаОтчет.Важность = "Обычное";
		КомандаОтчет.КлючВарианта = "АнализРаспределенияНДС";
		
		Возврат КомандаОтчет;
		
	КонецЕсли;

	Возврат Неопределено;

КонецФункции

#КонецОбласти

// Выполняет запрос и формирует табличный документ-результат отчета
// в соответствии с настройками, заданными значениями реквизитов отчета.
//
// Параметры:
//	ДокументРезультат - табличный документ, формируемый отчетом
//	ПоказыватьЗаголовок - признак видимости строк с заголовком отчета
//	ВысотаЗаголовка - параметр, через который возвращается высота заголовка в строках 
//  ДокументОбъект - если указан, то печать отчета по документу.
//
Процедура СформироватьОтчет(Знач ПараметрыОтчета, АдресХранилища) Экспорт
	
	ДокументРезультат = Новый ТабличныйДокумент;
	
	ДокументРезультат.ОриентацияСтраницы	= ОриентацияСтраницы.Ландшафт;
	ДокументРезультат.АвтоМасштаб			= Истина;
	
	Макет = Отчеты.АнализРаспределенияНДС.ПолучитьМакет("Таблица");
	
	ОбластьЗаголовок  = Макет.ПолучитьОбласть("Заголовок");
	
	Заголовок = НСтр("ru = 'Анализ распределения сумм НДС';
					|en = 'VAT amount allocation analysis'");
	
	РеквизитыДокументаРаспределения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПараметрыОтчета.ДокументРаспределения, "Организация, Дата");
	ДокументРаспределенияЗаполнен = ЗначениеЗаполнено(ПараметрыОтчета.ДокументРаспределения);
	
	НачалоПериода = ?(НЕ ДокументРаспределенияЗаполнен, НачалоПериода, НачалоКвартала(РеквизитыДокументаРаспределения.Дата));
	КонецПериода  = ?(НЕ ДокументРаспределенияЗаполнен, КонецПериода, КонецКвартала(РеквизитыДокументаРаспределения.Дата));
	
	ОбластьЗаголовок.Параметры.Период = "Период: " + ПредставлениеПериода(НачалоДня(ПараметрыОтчета.НачалоПериода), КонецДня(ПараметрыОтчета.КонецПериода)) + ?(НЕ ПараметрыОтчета.ВыручкаПоДокументам, "", " по документу """ + ПараметрыОтчета.ДокументРаспределения + """");

	Организация = ?(ДокументРаспределенияЗаполнен, РеквизитыДокументаРаспределения.Организация, ПараметрыОтчета.Организация);
	СведенияОбОрганизации	= БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(
		Организация,
		ПараметрыОтчета.КонецПериода);
		
	ПредставлениеОрганизации = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбОрганизации, "НаименованиеДляПечатныхФорм,");
	ОбластьЗаголовок.Параметры.НазваниеОрганизации = ПредставлениеОрганизации;
	
	ОбластьЗаголовок.Параметры.Заголовок	= Заголовок;
	
	ОбластьЗаголовок.Параметры.ИННОрганизации	= СокрЛП(СведенияОбОрганизации.ИНН)
												+ ?(ЗначениеЗаполнено(СведенияОбОрганизации.КПП)," / ", "")
												+ СокрЛП(СведенияОбОрганизации.КПП);
	
	ДокументРезультат.Вывести(ОбластьЗаголовок);
	
	ВыводитьОбщийЭкспорт = 
		НЕ УчетНДСРФ.УпрощенныйПорядокВычетаНДСПоНесырьевомуЭкспорту(ПараметрыОтчета.НачалоПериода)
		И ПолучитьФункциональнуюОпцию("ИспользоватьПродажиНаЭкспорт");
		
	ВыводитьЭкспортНесырьевыхТоваров = 
		УчетНДСРФ.УпрощенныйПорядокВычетаНДСПоНесырьевомуЭкспорту(ПараметрыОтчета.КонецПериода)
		И ПолучитьФункциональнуюОпцию("ИспользоватьПродажиНаЭкспортНесырьевыхТоваров");
		
	ВыводитьЭкспортСырьевыхТоваровУслуг = 
		УчетНДСРФ.УпрощенныйПорядокВычетаНДСПоНесырьевомуЭкспорту(ПараметрыОтчета.КонецПериода)
		И ПолучитьФункциональнуюОпцию("ИспользоватьПродажиНаЭкспортСырьевыхТоваровУслуг");
		
	ВыводитьКосмос = УчетНДСУП.ПараметрыУчетаПоОрганизации(Организация, НачалоПериода).УчетНДСВКосмическойДеятельности;
		
	ПараметрыОтчета.Вставить("ВыводитьОбщийЭкспорт",                ВыводитьОбщийЭкспорт);
	ПараметрыОтчета.Вставить("ВыводитьЭкспортНесырьевыхТоваров",    ВыводитьЭкспортНесырьевыхТоваров);
	ПараметрыОтчета.Вставить("ВыводитьЭкспортСырьевыхТоваровУслуг", ВыводитьЭкспортСырьевыхТоваровУслуг);
	ПараметрыОтчета.Вставить("ВыводитьКосмос",                      ВыводитьКосмос);
	
	ВыборкаПериодыБазы = ПериодыБазы(ПараметрыОтчета);
	Пока ВыборкаПериодыБазы.Следующий() Цикл
		
		ПериодБазы = ВыборкаПериодыБазы.ПериодБазы; 
		
		ВывестиВОтчетДанныеПоБазеРаспределения(ПериодБазы, ПараметрыОтчета, ДокументРезультат, Макет);
		
		ВывестиВОтчетДанныеПоРаспределениюКосвенныхРасходов(ПериодБазы, ПараметрыОтчета, ДокументРезультат, Макет);
		//++ НЕ УТ
		ВывестиВОтчетДанныеПоРаспределениюОС(ПериодБазы, ПараметрыОтчета, ДокументРезультат, Макет);
		//-- НЕ УТ
	КонецЦикла;
	
	
	ПоместитьВоВременноеХранилище(ДокументРезультат, АдресХранилища);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПериодыБазы(ПараметрыОтчета)
	
	Запрос = Новый Запрос;
	
	Если НЕ ПараметрыОтчета.ВыручкаПоДокументам Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВЫБОР 
		|		КОГДА ПартииНДСКРаспределению.ПериодБазы <> ДАТАВРЕМЯ(1,1,1)
		|			ТОГДА КОНЕЦПЕРИОДА(ПартииНДСКРаспределению.ПериодБазы, МЕСЯЦ)
		|		ИНАЧЕ КОНЕЦПЕРИОДА(ПартииНДСКРаспределению.Период, МЕСЯЦ)
		|	КОНЕЦ КАК ПериодБазы
		|ИЗ
		|	РегистрНакопления.ПартииНДСКРаспределению КАК ПартииНДСКРаспределению
		|ГДЕ
		|	ПартииНДСКРаспределению.Организация = &Организация
		|	И ПартииНДСКРаспределению.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ПартииНДСКРаспределению.Регистратор ССЫЛКА Документ.РаспределениеНДС
		|";
		
	Иначе
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВЫБОР 
		|		КОГДА ПартииНДСКРаспределению.ПериодБазы <> ДАТАВРЕМЯ(1,1,1)
		|			ТОГДА КОНЕЦПЕРИОДА(ПартииНДСКРаспределению.ПериодБазы, МЕСЯЦ)
		|		ИНАЧЕ КОНЕЦПЕРИОДА(ПартииНДСКРаспределению.Период, МЕСЯЦ)
		|	КОНЕЦ КАК ПериодБазы
		|ИЗ
		|	РегистрНакопления.ПартииНДСКРаспределению КАК ПартииНДСКРаспределению
		|ГДЕ
		|	ПартииНДСКРаспределению.Регистратор = &Ссылка
		|";
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Организация", 	ПараметрыОтчета.Организация);
	Запрос.УстановитьПараметр("НачалоПериода", 	ПараметрыОтчета.НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", 	КонецДня(ПараметрыОтчета.КонецПериода));
	Запрос.УстановитьПараметр("Ссылка", 		ПараметрыОтчета.ДокументРаспределения);
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

// Выполняет запрос и формирует таблицу соответствующую базе распределения
// косвенных расходов.
//
Процедура ВывестиВОтчетДанныеПоБазеРаспределения(ПериодБазы, ПараметрыОтчета, ДокументРезультат, Макет)
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РаспределениеНДС.Ссылка КАК ДокументРаспределения,
	|	СУММА(РаспределениеНДС.ВыручкаЕНВД) КАК ВыручкаЕНВД,
	|	СУММА(РаспределениеНДС.ВыручкаНеНДС) КАК ВыручкаНеНДС,
	|	СУММА(РаспределениеНДС.ВыручкаНДС0) КАК ВыручкаНДС0,
	|	СУММА(РаспределениеНДС.ВыручкаНДС0СырьевыеТоварыУслуги) КАК ВыручкаНДС0СырьевыеТоварыУслуги,
	|	СУММА(РаспределениеНДС.ВыручкаНДС0НесырьевыеТовары) КАК ВыручкаНДС0НесырьевыеТовары,
	|	СУММА(РаспределениеНДС.ВыручкаНДС + РаспределениеНДС.ВыручкаНеРФ + РаспределениеНДС.ВыручкаНДСПроизводствоСДЦ) КАК ВыручкаНДС,
	|	СУММА(РаспределениеНДС.ВыручкаНДС0Космос) КАК ВыручкаНДС0Космос,
	|	СУММА(РаспределениеНДС.ВыручкаЕНВД)
	|		+ СУММА(РаспределениеНДС.ВыручкаНеНДС)
	|		+ СУММА(РаспределениеНДС.ВыручкаНДС0СырьевыеТоварыУслуги)
	|		+ СУММА(РаспределениеНДС.ВыручкаНДС0НесырьевыеТовары)
	|		+ СУММА(РаспределениеНДС.ВыручкаНДС)
	|		+ СУММА(РаспределениеНДС.ВыручкаНеРФ)
	|		+ СУММА(РаспределениеНДС.ВыручкаНДС0Космос)
	|		+ СУММА(РаспределениеНДС.ВыручкаНДСПроизводствоСДЦ) КАК ВсегоВыручка
	|ИЗ
	|	Документ.РаспределениеНДС КАК РаспределениеНДС
	|ГДЕ
	|	РаспределениеНДС.Организация = &Организация
	|	И РаспределениеНДС.Проведен
	|	И КОНЕЦПЕРИОДА(РаспределениеНДС.Дата, МЕСЯЦ) = &ПериодБазы
	|
	|СГРУППИРОВАТЬ ПО
	|	РаспределениеНДС.Ссылка";
		
	
	Запрос.УстановитьПараметр("Организация", 	ПараметрыОтчета.Организация);
	Запрос.УстановитьПараметр("ПериодБазы", 	ПериодБазы);
	
	Результат = Запрос.Выполнить().Выбрать();	
	
	СтруктураПараметровВывода = Новый Структура;
	СтруктураПараметровВывода.Вставить("ОбщийЭкспорт",                ПараметрыОтчета.ВыводитьОбщийЭкспорт);
	СтруктураПараметровВывода.Вставить("ЭкспортНесырьевыхТоваров",    ПараметрыОтчета.ВыводитьЭкспортНесырьевыхТоваров);
	СтруктураПараметровВывода.Вставить("ЭкспортСырьевыхТоваровУслуг", ПараметрыОтчета.ВыводитьЭкспортСырьевыхТоваровУслуг);
	СтруктураПараметровВывода.Вставить("Космос",                      ПараметрыОтчета.ВыводитьКосмос);
	
	СтруктураДопОбласей = Новый Структура;
	СтруктураДопОбласей.Вставить("ПараметрыБазыРаспределения");
	СтруктураДопОбласей.Вставить("СтрокаБазыРаспределения");
	
	Для каждого ДопОбласть Из СтруктураДопОбласей Цикл
		МассивДопОбластейШапки = Новый Массив;
		Для каждого ПараметрВывода Из СтруктураПараметровВывода Цикл
			Если НЕ ПараметрВывода.Значение Тогда
				Продолжить;
			КонецЕсли;
			МассивДопОбластейШапки.Добавить(Макет.ПолучитьОбласть(ДопОбласть.Ключ + "|" + ПараметрВывода.Ключ));
		КонецЦикла;
		СтруктураДопОбласей[ДопОбласть.Ключ] = МассивДопОбластейШапки;
	КонецЦикла;

	ОбластьШапкаТаблицы  = Макет.ПолучитьОбласть("ПараметрыБазыРаспределения|Основное");
	ПериодБазыПредставление = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(НачалоКвартала(ПериодБазы), ПериодБазы, Истина);
	ОбластьШапкаТаблицы.Параметры.ПериодБазы = СтрЗаменить(ПериодБазыПредставление, НСтр("ru = 'квартал';
																						|en = 'quarter'"), НСтр("ru = 'квартала';
																												|en = 'quarter'"));
	ДокументРезультат.Вывести(ОбластьШапкаТаблицы);
	Для каждого Область Из СтруктураДопОбласей["ПараметрыБазыРаспределения"] Цикл
		ДокументРезультат.Присоединить(Область);
	КонецЦикла;
	
	Результат.Сбросить();
	Пока Результат.Следующий() Цикл
		
		ОбластьШапкаТаблицы  = Макет.ПолучитьОбласть("СтрокаБазыРаспределения|Основное");
		ОбластьШапкаТаблицы.Параметры.Заполнить(Результат);
		ДокументРезультат.Вывести(ОбластьШапкаТаблицы);
		
		Для каждого Область Из СтруктураДопОбласей["СтрокаБазыРаспределения"] Цикл
			Область.Параметры.Заполнить(Результат);
			ДокументРезультат.Присоединить(Область);
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

// Выполняет запрос к регистру НДСПоКосвеннымРасходам
// и выбирает информацию по косвенным расходам текущего периода, которые были
// распределены на соответствующие ставки реализации.
//
Процедура ВывестиВОтчетДанныеПоРаспределениюКосвенныхРасходов(ПериодБазы, ПараметрыОтчета, ДокументРезультат, Макет)
	
	// Используем АВТОУПОРЯДОЧИВАНИЕ, так как наследуем логику из конфигурации Бухгалтерия предприятия
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЕСТЬNULL(СчетФактураПолученный.Ссылка,ЕСТЬNULL(ИнойДокументПодтвержденияНДС.Ссылка,СчетФактураПолученныйНА.Ссылка)) КАК СчетФактураПолученный,
	|	ПартииНДСКРаспределению.ДокументПоступленияРасходов КАК ДокументПоступления
	|ПОМЕСТИТЬ ТаблицаСчетовФактурИсходная
	|ИЗ
	|	РегистрНакопления.ПартииНДСКРаспределению КАК ПартииНДСКРаспределению
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученный.ДокументыОснования КАК СчетФактураПолученный
	|		ПО 
	|			ПартииНДСКРаспределению.ДокументПоступленияРасходов = СчетФактураПолученный.ДокументОснование
	|			И СчетФактураПолученный.Ссылка.Проведен
	|			И НЕ СчетФактураПолученный.Ссылка.Исправление
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученныйНалоговыйАгент КАК СчетФактураПолученныйНА
	|		ПО 
	|			ПартииНДСКРаспределению.ДокументПоступленияРасходов = СчетФактураПолученныйНА.Ссылка
	|			И СчетФактураПолученныйНА.Проведен
	|			И НЕ СчетФактураПолученныйНА.Исправление
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИнойДокументПодтвержденияНДС КАК ИнойДокументПодтвержденияНДС
	|		ПО 
	|			ПартииНДСКРаспределению.ДокументПоступленияРасходов = ИнойДокументПодтвержденияНДС.Ссылка
	|			И ИнойДокументПодтвержденияНДС.Проведен
	|ГДЕ
	|	ПартииНДСКРаспределению.Регистратор ССЫЛКА Документ.РаспределениеНДС
	|	И ПартииНДСКРаспределению.Организация = &Организация
	|	И ПартииНДСКРаспределению.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактураПолученный
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСчетовФактурИсходная.СчетФактураПолученный КАК СчетФактураПолученный,
	|	ТаблицаСчетовФактурИсходная.ДокументПоступления КАК ДокументПоступления,
	|	ВЫБОР
	|		КОГДА ДокументыСчетФактураПолученный.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА &ПредставлениеСчетФактураПолученный
	|		КОГДА ДокументыСчетФактураПолученныйНалоговыйАгент.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА &ПредставлениеСчетФактураПолученныйНА
	|		КОГДА ДокументыИнойДокументПодтвержденияНДС.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА &ПредставлениеИнойДокументПодтвержденияНДС
	|	КОНЕЦ КАК ПредставлениеСчетаФактуры,
	|	ВЫБОР
	|		КОГДА ДокументыСчетФактураПолученный.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА ДокументыСчетФактураПолученный.Номер
	|		КОГДА ДокументыСчетФактураПолученныйНалоговыйАгент.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА ДокументыСчетФактураПолученныйНалоговыйАгент.Номер
	|		КОГДА ДокументыИнойДокументПодтвержденияНДС.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА ДокументыИнойДокументПодтвержденияНДС.НомерВходящегоДокумента
	|	КОНЕЦ КАК НомерСчетаФактуры,
	|	ВЫБОР
	|		КОГДА ДокументыСчетФактураПолученный.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА ДокументыСчетФактураПолученный.ДатаСоставления
	|		КОГДА ДокументыСчетФактураПолученныйНалоговыйАгент.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА ДокументыСчетФактураПолученныйНалоговыйАгент.ДатаСоставления
	|		КОГДА ДокументыИнойДокументПодтвержденияНДС.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА ДокументыИнойДокументПодтвержденияНДС.ДатаВходящегоДокумента
	|	КОНЕЦ КАК ДатаСчетаФактуры
	|ПОМЕСТИТЬ ТаблицаСчетовФактур
	|ИЗ
	|	ТаблицаСчетовФактурИсходная КАК ТаблицаСчетовФактурИсходная
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИнойДокументПодтвержденияНДС КАК ДокументыИнойДокументПодтвержденияНДС
	|		ПО ТаблицаСчетовФактурИсходная.СчетФактураПолученный = ДокументыИнойДокументПодтвержденияНДС.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученный КАК ДокументыСчетФактураПолученный
	|		ПО ТаблицаСчетовФактурИсходная.СчетФактураПолученный = ДокументыСчетФактураПолученный.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученныйНалоговыйАгент КАК ДокументыСчетФактураПолученныйНалоговыйАгент
	|		ПО ТаблицаСчетовФактурИсходная.СчетФактураПолученный = ДокументыСчетФактураПолученныйНалоговыйАгент.Ссылка
	|			И ТаблицаСчетовФактурИсходная.ДокументПоступления = ДокументыСчетФактураПолученныйНалоговыйАгент.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументПоступления
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПартииНДСКРаспределению.Регистратор КАК ДокументРаспределения,
	|	ЕСТЬNULL(ТаблицаСчетовФактур.СчетФактураПолученный, НЕОПРЕДЕЛЕНО) КАК СчетФактураПолученный,
	|	ПартииНДСКРаспределению.ДокументПоступленияРасходов КАК ДокументПоступления,
	|	СУММА(ПартииНДСКРаспределению.СтоимостьРегл) КАК ВсегоСумма,
	|	СУММА(ПартииНДСКРаспределению.НДСРегл) КАК ВсегоНДС,
	|	СУММА(ВЫБОР
	|			КОГДА ПартииНДСКРаспределению.КорВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|				ТОГДА ПартииНДСКРаспределению.СтоимостьРегл
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БезНДССумма,
	|	СУММА(ВЫБОР
	|			КОГДА ПартииНДСКРаспределению.КорВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|				ТОГДА ПартииНДСКРаспределению.НДСРегл
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БезНДС,
	|	СУММА(ВЫБОР
	|			КОГДА ПартииНДСКРаспределению.КорВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД)
	|				ТОГДА ПартииНДСКРаспределению.СтоимостьРегл
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ЕНВДСумма,
	|	СУММА(ВЫБОР
	|			КОГДА ПартииНДСКРаспределению.КорВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД)
	|				ТОГДА ПартииНДСКРаспределению.НДСРегл
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ЕНВДНДС,
	|	СУММА(ВЫБОР
	|			КОГДА ПартииНДСКРаспределению.КорВидДеятельностиНДС В (ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС),
	|																ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.РеализацияРаботУслугНаЭкспорт),
	|																ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДСВСтранеЕАЭС),
	|																ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПроизводствоСДЦ))
	|				ТОГДА ПартииНДСКРаспределению.СтоимостьРегл
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Сумма,
	|	СУММА(ВЫБОР
	|			КОГДА ПартииНДСКРаспределению.КорВидДеятельностиНДС В (ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС),
	|																ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.РеализацияРаботУслугНаЭкспорт),
	|																ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДСВСтранеЕАЭС),
	|																ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПроизводствоСДЦ))
	|				ТОГДА ПартииНДСКРаспределению.НДСРегл
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НДС,
	|	СУММА(ВЫБОР
	|			КОГДА ПартииНДСКРаспределению.КорВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт)
	|				ТОГДА ПартииНДСКРаспределению.СтоимостьРегл
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НДС0Сумма,
	|	СУММА(ВЫБОР
	|			КОГДА ПартииНДСКРаспределению.КорВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт)
	|				ТОГДА ПартииНДСКРаспределению.НДСРегл
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НДС0,
	|	СУММА(ВЫБОР
	|			КОГДА ПартииНДСКРаспределению.КорВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров)
	|				ТОГДА ПартииНДСКРаспределению.СтоимостьРегл
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НДС0СуммаНесырьевыеТовары,
	|	СУММА(ВЫБОР
	|			КОГДА ПартииНДСКРаспределению.КорВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров)
	|				ТОГДА ПартииНДСКРаспределению.НДСРегл
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НДС0НесырьевыеТовары,
	|	СУММА(ВЫБОР
	|			КОГДА ПартииНДСКРаспределению.КорВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг)
	|				ТОГДА ПартииНДСКРаспределению.СтоимостьРегл
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НДС0СуммаСырьевыеТоварыУслуги,
	|	СУММА(ВЫБОР
	|			КОГДА ПартииНДСКРаспределению.КорВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг)
	|				ТОГДА ПартииНДСКРаспределению.НДСРегл
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НДС0СырьевыеТоварыУслуги,
	|	СУММА(ВЫБОР
	|			КОГДА ПартииНДСКРаспределению.КорВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.Космос)
	|				ТОГДА ПартииНДСКРаспределению.СтоимостьРегл
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НДС0СуммаКосмос,
	|	СУММА(ВЫБОР
	|			КОГДА ПартииНДСКРаспределению.КорВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.Космос)
	|				ТОГДА ПартииНДСКРаспределению.НДСРегл
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НДС0Космос,
	|	ТаблицаСчетовФактур.ПредставлениеСчетаФактуры,
	|	ТаблицаСчетовФактур.НомерСчетаФактуры,
	|	ТаблицаСчетовФактур.ДатаСчетаФактуры
	|ИЗ
	|	РегистрНакопления.ПартииНДСКРаспределению КАК ПартииНДСКРаспределению
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСчетовФактур КАК ТаблицаСчетовФактур
	|		ПО ПартииНДСКРаспределению.ДокументПоступленияРасходов = ТаблицаСчетовФактур.ДокументПоступления
	|ГДЕ
	|	ПартииНДСКРаспределению.Регистратор ССЫЛКА Документ.РаспределениеНДС
	|	И ПартииНДСКРаспределению.Организация = &Организация
	|	И ПартииНДСКРаспределению.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ВЫБОР 
	|		КОГДА ПартииНДСКРаспределению.ПериодБазы <> ДАТАВРЕМЯ(1,1,1)
	|			ТОГДА КОНЕЦПЕРИОДА(ПартииНДСКРаспределению.ПериодБазы, МЕСЯЦ)
	|		ИНАЧЕ КОНЕЦПЕРИОДА(ПартииНДСКРаспределению.Период, МЕСЯЦ)
	|	КОНЕЦ = &ПериодБазы
	//++ НЕ УТ
	|	И НЕ (ЕСТЬNULL(ПартииНДСКРаспределению.СтатьяРасходов.ВариантРаспределенияРасходовРегл,ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.ПустаяСсылка)) 
	|			= ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|			ИЛИ ПартииНДСКРаспределению.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию))
	//-- НЕ УТ
	|
	|СГРУППИРОВАТЬ ПО
	|	ПартииНДСКРаспределению.Регистратор,
	|	ЕСТЬNULL(ТаблицаСчетовФактур.СчетФактураПолученный, НЕОПРЕДЕЛЕНО),
	|	ПартииНДСКРаспределению.ДокументПоступленияРасходов,
	|	ТаблицаСчетовФактур.ПредставлениеСчетаФактуры,
	|	ТаблицаСчетовФактур.НомерСчетаФактуры,
	|	ТаблицаСчетовФактур.ДатаСчетаФактуры
	|
	|ИМЕЮЩИЕ
	|	СУММА(ПартииНДСКРаспределению.НДСРегл) <> 0
	|
	|ИТОГИ ПО
	|	ОБЩИЕ,
	|	ДокументРаспределения
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("Организация", 	ПараметрыОтчета.Организация);
	Запрос.УстановитьПараметр("НачалоПериода", 	ПараметрыОтчета.НачалоПериода);
	Запрос.УстановитьПараметр("ПериодБазы", 	ПериодБазы);
	Запрос.УстановитьПараметр("КонецПериода",  	КонецДня(ПараметрыОтчета.КонецПериода));
	Запрос.УстановитьПараметр("КонецПериодаГраница", Новый Граница(КонецДня(ПараметрыОтчета.КонецПериода), ВидГраницы.Включая));
	
	Запрос.УстановитьПараметр("ПредставлениеСчетФактураПолученный", Метаданные.Документы.СчетФактураПолученный.Синоним);
	Запрос.УстановитьПараметр("ПредставлениеСчетФактураПолученныйНА", Метаданные.Документы.СчетФактураПолученныйНалоговыйАгент.Синоним);
	Запрос.УстановитьПараметр("ПредставлениеИнойДокументПодтвержденияНДС", Метаданные.Документы.ИнойДокументПодтвержденияНДС.Синоним);
	
	ВыборкаЗатраты = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Если ВыборкаЗатраты.Количество() > 0 Тогда
		
		ОбластьШапка= Макет.ПолучитьОбласть("ШапкаТаблицыЗатраты|Основное");
		ОбластьДокументОсновное = Макет.ПолучитьОбласть("СтрокаДокументРаспределенияЗатраты|Основное");
		ОбластьСтрокаОсновное   = Макет.ПолучитьОбласть("СтрокаЗатраты|Основное");
		ОбластьИтогиОсновное    = Макет.ПолучитьОбласть("ИтогиЗатраты|Основное");
		
		СтруктураПараметровВывода = Новый Структура;
		СтруктураПараметровВывода.Вставить("ОбщийЭкспорт",                ПараметрыОтчета.ВыводитьОбщийЭкспорт);
		СтруктураПараметровВывода.Вставить("ЭкспортНесырьевыхТоваров",    ПараметрыОтчета.ВыводитьЭкспортНесырьевыхТоваров);
		СтруктураПараметровВывода.Вставить("ЭкспортСырьевыхТоваровУслуг", ПараметрыОтчета.ВыводитьЭкспортСырьевыхТоваровУслуг);
		СтруктураПараметровВывода.Вставить("Космос",                      ПараметрыОтчета.ВыводитьКосмос);
		
		СтруктураДопОбласей = Новый Структура;
		СтруктураДопОбласей.Вставить("ШапкаТаблицыЗатраты");
		СтруктураДопОбласей.Вставить("СтрокаДокументРаспределенияЗатраты");
		СтруктураДопОбласей.Вставить("СтрокаЗатраты");
		СтруктураДопОбласей.Вставить("ИтогиЗатраты");
		
		Для каждого ДопОбласть Из СтруктураДопОбласей Цикл
			МассивДопОбластейШапки = Новый Массив;
			Для каждого ПараметрВывода Из СтруктураПараметровВывода Цикл
				Если НЕ ПараметрВывода.Значение Тогда
					Продолжить;
				КонецЕсли;
				МассивДопОбластейШапки.Добавить(Макет.ПолучитьОбласть(ДопОбласть.Ключ + "|" + ПараметрВывода.Ключ));
			КонецЦикла;
			СтруктураДопОбласей[ДопОбласть.Ключ] = МассивДопОбластейШапки;
		КонецЦикла;
		
		ОбластьШапка.Параметры.НаименованиеСекции = НСтр("ru = 'Результаты распределения НДС';
														|en = 'VAT allocation results'");		
		ДокументРезультат.Вывести(ОбластьШапка);
		
		Для каждого Область Из СтруктураДопОбласей["ШапкаТаблицыЗатраты"] Цикл
			ДокументРезультат.Присоединить(Область);
		КонецЦикла;
		
		ШаблонПредставления = НСтр("ru = '%1 №%2 от %3';
									|en = '%1 No%2 dated %3'");
		
		Пока ВыборкаЗатраты.Следующий() Цикл
			
			ВыборкаДокументы = ВыборкаЗатраты.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаДокументы.Следующий() Цикл
				
				ОбластьДокументОсновное.Параметры.Заполнить(ВыборкаДокументы);
				ДокументРезультат.Вывести(ОбластьДокументОсновное);
				
				Для каждого Область Из СтруктураДопОбласей["СтрокаДокументРаспределенияЗатраты"] Цикл
					Область.Параметры.Заполнить(ВыборкаДокументы);
					ДокументРезультат.Присоединить(Область);
				КонецЦикла;
					
				ВыборкаСтроки = ВыборкаДокументы.Выбрать();
				Пока ВыборкаСтроки.Следующий() Цикл
					
					ОбластьСтрокаОсновное.Параметры.Заполнить(ВыборкаСтроки);
					
					Если ЗначениеЗаполнено(ВыборкаСтроки.СчетФактураПолученный) Тогда
						ОбластьСтрокаОсновное.Параметры.ПредставлениеСчетаФактуры = СтрШаблон(ШаблонПредставления,
																					ВыборкаСтроки.ПредставлениеСчетаФактуры,
																					ВыборкаСтроки.НомерСчетаФактуры,
																					Формат(ВыборкаСтроки.ДатаСчетаФактуры, "ДЛФ=D"));
					КонецЕсли;
					
					ДокументРезультат.Вывести(ОбластьСтрокаОсновное);
					
					Для каждого Область Из СтруктураДопОбласей["СтрокаЗатраты"] Цикл
						Область.Параметры.Заполнить(ВыборкаСтроки);
						ДокументРезультат.Присоединить(Область);
					КонецЦикла;
					
				КонецЦикла;
				
			КонецЦикла;
			
			ОбластьИтогиОсновное.Параметры.Заполнить(ВыборкаЗатраты);
			ДокументРезультат.Вывести(ОбластьИтогиОсновное);
			
			Для каждого Область Из СтруктураДопОбласей["ИтогиЗатраты"] Цикл
				Область.Параметры.Заполнить(ВыборкаЗатраты);
				ДокументРезультат.Присоединить(Область);
			КонецЦикла;
			
		КонецЦикла;
	
	КонецЕсли;
	
КонецПроцедуры

//++ НЕ УТ
Процедура ВывестиВОтчетДанныеПоРаспределениюОС(ПериодБазы, ПараметрыОтчета, ДокументРезультат, Макет)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЕСТЬNULL(СчетФактураПолученный.Ссылка,СчетФактураПолученныйНА.Ссылка) КАК СчетФактураПолученный,
	|	ПартииНДСКРаспределению.ДокументПоступленияРасходов КАК ДокументПоступления
	|ПОМЕСТИТЬ ТаблицаСчетовФактурИсходная
	|ИЗ
	|	РегистрНакопления.ПартииНДСКРаспределению КАК ПартииНДСКРаспределению
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученный.ДокументыОснования КАК СчетФактураПолученный
	|		ПО 
	|			ПартииНДСКРаспределению.ДокументПоступленияРасходов = СчетФактураПолученный.ДокументОснование
	|			И СчетФактураПолученный.Ссылка.Проведен
	|			И НЕ СчетФактураПолученный.Ссылка.Исправление
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученныйНалоговыйАгент КАК СчетФактураПолученныйНА
	|		ПО 
	|			ПартииНДСКРаспределению.ДокументПоступленияРасходов = СчетФактураПолученныйНА.Ссылка
	|			И СчетФактураПолученныйНА.Проведен
	|			И НЕ СчетФактураПолученныйНА.Исправление
	|ГДЕ
	|	ПартииНДСКРаспределению.Регистратор ССЫЛКА Документ.РаспределениеНДС
	|	И ПартииНДСКРаспределению.Организация = &Организация
	|	И ПартииНДСКРаспределению.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактураПолученный
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСчетовФактурИсходная.СчетФактураПолученный КАК СчетФактураПолученный,
	|	ТаблицаСчетовФактурИсходная.ДокументПоступления КАК ДокументПоступления,
	|	ВЫБОР
	|		КОГДА ДокументыСчетФактураПолученный.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА &ПредставлениеСчетФактураПолученный
	|		КОГДА ДокументыСчетФактураПолученныйНалоговыйАгент.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА &ПредставлениеСчетФактураПолученныйНА
	|		КОГДА ДокументыИнойДокументПодтвержденияНДС.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА &ПредставлениеИнойДокументПодтвержденияНДС
	|	КОНЕЦ КАК ПредставлениеСчетаФактуры,
	|	ВЫБОР
	|		КОГДА ДокументыСчетФактураПолученный.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА ДокументыСчетФактураПолученный.Номер
	|		КОГДА ДокументыСчетФактураПолученныйНалоговыйАгент.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА ДокументыСчетФактураПолученныйНалоговыйАгент.Номер
	|		КОГДА ДокументыИнойДокументПодтвержденияНДС.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА ДокументыИнойДокументПодтвержденияНДС.НомерВходящегоДокумента
	|	КОНЕЦ КАК НомерСчетаФактуры,
	|	ВЫБОР
	|		КОГДА ДокументыСчетФактураПолученный.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА ДокументыСчетФактураПолученный.ДатаСоставления
	|		КОГДА ДокументыСчетФактураПолученныйНалоговыйАгент.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА ДокументыСчетФактураПолученныйНалоговыйАгент.ДатаСоставления
	|		КОГДА ДокументыИнойДокументПодтвержденияНДС.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА ДокументыИнойДокументПодтвержденияНДС.ДатаВходящегоДокумента
	|	КОНЕЦ КАК ДатаСчетаФактуры
	|ПОМЕСТИТЬ ТаблицаСчетовФактур
	|ИЗ
	|	ТаблицаСчетовФактурИсходная КАК ТаблицаСчетовФактурИсходная
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИнойДокументПодтвержденияНДС КАК ДокументыИнойДокументПодтвержденияНДС
	|		ПО ТаблицаСчетовФактурИсходная.СчетФактураПолученный = ДокументыИнойДокументПодтвержденияНДС.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученный КАК ДокументыСчетФактураПолученный
	|		ПО ТаблицаСчетовФактурИсходная.СчетФактураПолученный = ДокументыСчетФактураПолученный.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученныйНалоговыйАгент КАК ДокументыСчетФактураПолученныйНалоговыйАгент
	|		ПО ТаблицаСчетовФактурИсходная.СчетФактураПолученный = ДокументыСчетФактураПолученныйНалоговыйАгент.Ссылка
	|			И ТаблицаСчетовФактурИсходная.ДокументПоступления = ДокументыСчетФактураПолученныйНалоговыйАгент.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументПоступления
	|
	|;
	|	
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПартииНДСКРаспределению.АналитикаРасходов КАК ОсновноеСредство
	|ПОМЕСТИТЬ ТаблицаОС
	|ИЗ
	|	РегистрНакопления.ПартииНДСКРаспределению КАК ПартииНДСКРаспределению
	|ГДЕ
	|	ПартииНДСКРаспределению.Регистратор ССЫЛКА Документ.РаспределениеНДС
	|	И ПартииНДСКРаспределению.Организация = &Организация
	|	И ПартииНДСКРаспределению.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ПартииНДСКРаспределению.АналитикаРасходов ССЫЛКА Справочник.ОбъектыЭксплуатации
	|
	|СГРУППИРОВАТЬ ПО
	|	ПартииНДСКРаспределению.АналитикаРасходов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПартииПрочихРасходов.АналитикаРасходов КАК НематериальныйАктив
	|ПОМЕСТИТЬ ТаблицаНМА
	|ИЗ
	|	РегистрНакопления.ПартииПрочихРасходов КАК ПартииПрочихРасходов
	|ГДЕ
	|	ПартииПрочихРасходов.Регистратор ССЫЛКА Документ.РаспределениеНДС
	|	И ПартииПрочихРасходов.Организация = &Организация
	|	И ПартииПрочихРасходов.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ПартииПрочихРасходов.АналитикаРасходов ССЫЛКА Справочник.НематериальныеАктивы
	|
	|СГРУППИРОВАТЬ ПО
	|	ПартииПрочихРасходов.АналитикаРасходов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПартииНДСКРаспределению.Регистратор КАК ДокументРаспределения,
	|	ЕСТЬNULL(ТаблицаСчетовФактур.СчетФактураПолученный, НЕОПРЕДЕЛЕНО) КАК СчетФактураПолученный,
	|	ПартииНДСКРаспределению.ДокументПоступленияРасходов КАК ДокументПоступления,
	|	ВЫБОР
	|		КОГДА ПартииНДСКРаспределению.АналитикаРасходов ССЫЛКА Справочник.ОбъектыЭксплуатации
	|			ТОГДА ""ОС""
	|		КОГДА ПартииНДСКРаспределению.АналитикаРасходов ССЫЛКА Справочник.НематериальныеАктивы
	|			ТОГДА ""НМА""
	|	КОНЕЦ КАК ВидАктива,
	|	ПартииНДСКРаспределению.АналитикаРасходов КАК ОС_НМА,
	|	МАКСИМУМ(ВЫРАЗИТЬ(ПартииНДСКРаспределению.АналитикаРасходов КАК Справочник.ОбъектыЭксплуатации).ИнвентарныйНомер) КАК ИнвентарныйНомер,
	|	МАКСИМУМ(ВЫБОР
	|		КОГДА ПартииНДСКРаспределению.АналитикаРасходов ССЫЛКА Справочник.ОбъектыЭксплуатации
	|			ТОГДА ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюБУ
	|		КОГДА ПартииНДСКРаспределению.АналитикаРасходов ССЫЛКА Справочник.НематериальныеАктивы
	|			ТОГДА ПервоначальныеСведенияНМА.ДокументПринятияКУчетуБУ
	|	КОНЕЦ) КАК Состояние,
	|	СУММА(ПартииНДСКРаспределению.СтоимостьРегл) КАК ВсегоСумма,
	|	СУММА(ПартииНДСКРаспределению.НДСРегл) КАК ВсегоНДС,
	|	СУММА(ВЫБОР
	|			КОГДА ПартииНДСКРаспределению.КорВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|				ИЛИ ПартииНДСКРаспределению.НДСРегл < 0 И ПартииНДСКРаспределению.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|				ТОГДА ПартииНДСКРаспределению.СтоимостьРегл
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БезНДССумма,
	|	СУММА(ВЫБОР
	|			КОГДА ПартииНДСКРаспределению.КорВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|				ИЛИ ПартииНДСКРаспределению.НДСРегл < 0 И ПартииНДСКРаспределению.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|				ТОГДА ПартииНДСКРаспределению.НДСРегл
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БезНДС,
	|	СУММА(ВЫБОР
	|			КОГДА ПартииНДСКРаспределению.КорВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД)
	|				ИЛИ ПартииНДСКРаспределению.НДСРегл < 0 И ПартииНДСКРаспределению.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД)
	|				ТОГДА ПартииНДСКРаспределению.СтоимостьРегл
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ЕНВДСумма,
	|	СУММА(ВЫБОР
	|			КОГДА ПартииНДСКРаспределению.КорВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД)
	|				ИЛИ ПартииНДСКРаспределению.НДСРегл < 0 И ПартииНДСКРаспределению.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД)
	|				ТОГДА ПартииНДСКРаспределению.НДСРегл
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ЕНВДНДС,
	|	СУММА(ВЫБОР
	|			КОГДА ПартииНДСКРаспределению.КорВидДеятельностиНДС В (ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС),
	|																ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.РеализацияРаботУслугНаЭкспорт),
	|																ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДСВСтранеЕАЭС),
	|																ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПроизводствоСДЦ))
	|				ИЛИ ПартииНДСКРаспределению.НДСРегл < 0 И ПартииНДСКРаспределению.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|				ТОГДА ПартииНДСКРаспределению.СтоимостьРегл
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Сумма,
	|	СУММА(ВЫБОР
	|			КОГДА ПартииНДСКРаспределению.КорВидДеятельностиНДС В (ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС),
	|																ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.РеализацияРаботУслугНаЭкспорт),
	|																ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДСВСтранеЕАЭС),
	|																ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПроизводствоСДЦ))
	|				ИЛИ ПартииНДСКРаспределению.НДСРегл < 0 И ПартииНДСКРаспределению.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|				ТОГДА ПартииНДСКРаспределению.НДСРегл
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НДС,
	|	СУММА(ВЫБОР
	|			КОГДА ПартииНДСКРаспределению.КорВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт)
	|				ТОГДА ПартииНДСКРаспределению.СтоимостьРегл
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НДС0Сумма,
	|	СУММА(ВЫБОР
	|			КОГДА ПартииНДСКРаспределению.КорВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт)
	|				ТОГДА ПартииНДСКРаспределению.НДСРегл
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НДС0,
	|	СУММА(ВЫБОР
	|			КОГДА ПартииНДСКРаспределению.КорВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров)
	|				ТОГДА ПартииНДСКРаспределению.СтоимостьРегл
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НДС0СуммаНесырьевыеТовары,
	|	СУММА(ВЫБОР
	|			КОГДА ПартииНДСКРаспределению.КорВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров)
	|				ТОГДА ПартииНДСКРаспределению.НДСРегл
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НДС0НесырьевыеТовары,
	|	СУММА(ВЫБОР
	|			КОГДА ПартииНДСКРаспределению.КорВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг)
	|				ТОГДА ПартииНДСКРаспределению.СтоимостьРегл
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НДС0СуммаСырьевыеТоварыУслуги,
	|	СУММА(ВЫБОР
	|			КОГДА ПартииНДСКРаспределению.КорВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг)
	|				ТОГДА ПартииНДСКРаспределению.НДСРегл
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НДС0СырьевыеТоварыУслуги,
	|	СУММА(ВЫБОР
	|			КОГДА ПартииНДСКРаспределению.КорВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.Космос)
	|				ТОГДА ПартииНДСКРаспределению.СтоимостьРегл
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НДС0СуммаКосмос,
	|	СУММА(ВЫБОР
	|			КОГДА ПартииНДСКРаспределению.КорВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.Космос)
	|				ТОГДА ПартииНДСКРаспределению.НДСРегл
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НДС0Космос,
	|		ТаблицаСчетовФактур.ПредставлениеСчетаФактуры КАК ПредставлениеСчетаФактуры,
	|		ТаблицаСчетовФактур.НомерСчетаФактуры         КАК НомерСчетаФактуры,
	|		ТаблицаСчетовФактур.ДатаСчетаФактуры          КАК ДатаСчетаФактуры
	|ИЗ
	|	РегистрНакопления.ПартииНДСКРаспределению КАК ПартииНДСКРаспределению
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСчетовФактур КАК ТаблицаСчетовФактур
	|		ПО ПартииНДСКРаспределению.ДокументПоступленияРасходов = ТаблицаСчетовФактур.ДокументПоступления
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОС.СрезПоследних(
	|				&КонецПериода, 
	|				ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|					И Организация = &Организация) КАК ПервоначальныеСведенияОС
	|		ПО (ПартииНДСКРаспределению.АналитикаРасходов ССЫЛКА Справочник.ОбъектыЭксплуатации)
	|			И ПартииНДСКРаспределению.АналитикаРасходов = ПервоначальныеСведенияОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияНМА.СрезПоследних(&КонецПериода, Организация = &Организация) КАК ПервоначальныеСведенияНМА
	|		ПО (ПартииНДСКРаспределению.АналитикаРасходов ССЫЛКА Справочник.НематериальныеАктивы)
	|			И ПартииНДСКРаспределению.АналитикаРасходов = ПервоначальныеСведенияНМА.НематериальныйАктив
	|ГДЕ
	|	ПартииНДСКРаспределению.Регистратор ССЫЛКА Документ.РаспределениеНДС
	|	И ПартииНДСКРаспределению.Организация = &Организация
	|	И ПартииНДСКРаспределению.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ВЫБОР 
	|		КОГДА ПартииНДСКРаспределению.ПериодБазы <> ДАТАВРЕМЯ(1,1,1)
	|			ТОГДА КОНЕЦПЕРИОДА(ПартииНДСКРаспределению.ПериодБазы, МЕСЯЦ)
	|		ИНАЧЕ КОНЕЦПЕРИОДА(ПартииНДСКРаспределению.Период, МЕСЯЦ)
	|	КОНЕЦ = &ПериодБазы
	|	И (ЕСТЬNULL(ПартииНДСКРаспределению.СтатьяРасходов.ВариантРаспределенияРасходовРегл,ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.ПустаяСсылка))
	|			= ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|			ИЛИ ПартииНДСКРаспределению.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию))
	|
	|СГРУППИРОВАТЬ ПО
	|	ПартииНДСКРаспределению.Регистратор,
	|	ЕСТЬNULL(ТаблицаСчетовФактур.СчетФактураПолученный, НЕОПРЕДЕЛЕНО),
	|	ПартииНДСКРаспределению.ДокументПоступленияРасходов,
	|	ВЫБОР
	|		КОГДА ПартииНДСКРаспределению.АналитикаРасходов ССЫЛКА Справочник.ОбъектыЭксплуатации
	|			ТОГДА ""ОС""
	|		КОГДА ПартииНДСКРаспределению.АналитикаРасходов ССЫЛКА Справочник.НематериальныеАктивы
	|			ТОГДА ""НМА""
	|	КОНЕЦ,
	|	ПартииНДСКРаспределению.АналитикаРасходов,
	|	ТаблицаСчетовФактур.ПредставлениеСчетаФактуры,
	|	ТаблицаСчетовФактур.НомерСчетаФактуры,
	|	ТаблицаСчетовФактур.ДатаСчетаФактуры
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВЫБОР
	|			КОГДА ПартииНДСКРаспределению.НДСРегл < 0
	|				ТОГДА 0
	|			ИНАЧЕ ПартииНДСКРаспределению.НДСРегл
	|		КОНЕЦ) <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	NULL,
	|	ЕСТЬNULL(ТаблицаСчетовФактур.СчетФактураПолученный, НЕОПРЕДЕЛЕНО) КАК СчетФактураПолученный,
	|	ПартииНДСКРаспределениюОстатки.ДокументПоступленияРасходов,
	|	ВЫБОР
	|		КОГДА ПартииНДСКРаспределениюОстатки.АналитикаРасходов ССЫЛКА Справочник.ОбъектыЭксплуатации
	|			ТОГДА ""ОС""
	|		КОГДА ПартииНДСКРаспределениюОстатки.АналитикаРасходов ССЫЛКА Справочник.НематериальныеАктивы
	|			ТОГДА ""НМА""
	|	КОНЕЦ,
	|	ПартииНДСКРаспределениюОстатки.АналитикаРасходов,
	|	ВЫРАЗИТЬ(ПартииНДСКРаспределениюОстатки.АналитикаРасходов КАК Справочник.ОбъектыЭксплуатации).ИнвентарныйНомер,
	|	ВЫБОР
	|		КОГДА ПартииНДСКРаспределениюОстатки.АналитикаРасходов ССЫЛКА Справочник.ОбъектыЭксплуатации
	|			ТОГДА ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюБУ
	|		КОГДА ПартииНДСКРаспределениюОстатки.АналитикаРасходов ССЫЛКА Справочник.НематериальныеАктивы
	|			ТОГДА ПервоначальныеСведенияНМА.ДокументПринятияКУчетуБУ
	|	КОНЕЦ,
	|	ПартииНДСКРаспределениюОстатки.СтоимостьРеглОстаток,
	|	ПартииНДСКРаспределениюОстатки.НДСРеглОстаток,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	ТаблицаСчетовФактур.ПредставлениеСчетаФактуры КАК ПредставлениеСчетаФактуры,
	|	ТаблицаСчетовФактур.НомерСчетаФактуры         КАК НомерСчетаФактуры,
	|	ТаблицаСчетовФактур.ДатаСчетаФактуры          КАК ДатаСчетаФактуры
	|ИЗ
	|	РегистрНакопления.ПартииНДСКРаспределению.Остатки(
	|			&КонецПериодаГраница,
	|			Организация = &Организация
	|				И (ЕСТЬNULL(СтатьяРасходов.ВариантРаспределенияРасходовРегл,ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.ПустаяСсылка))
	|						= ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|						ИЛИ ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию))) КАК ПартииНДСКРаспределениюОстатки
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСчетовФактур КАК ТаблицаСчетовФактур
	|		ПО ПартииНДСКРаспределениюОстатки.ДокументПоступленияРасходов = ТаблицаСчетовФактур.ДокументПоступления
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОС.СрезПоследних(
	|				&КонецПериода, 
	|				ДатаИсправления = ДАТАВРЕМЯ(1,1,1) 
	|					И Организация = &Организация) КАК ПервоначальныеСведенияОС
	|		ПО (ПартииНДСКРаспределениюОстатки.АналитикаРасходов ССЫЛКА Справочник.ОбъектыЭксплуатации)
	|			И ПартииНДСКРаспределениюОстатки.АналитикаРасходов = ПервоначальныеСведенияОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияНМА.СрезПоследних(&КонецПериода, Организация = &Организация) КАК ПервоначальныеСведенияНМА
	|		ПО (ПартииНДСКРаспределениюОстатки.АналитикаРасходов ССЫЛКА Справочник.НематериальныеАктивы)
	|			И ПартииНДСКРаспределениюОстатки.АналитикаРасходов = ПервоначальныеСведенияНМА.НематериальныйАктив
	|
	|ГДЕ
	|	ПартииНДСКРаспределениюОстатки.НДСРеглОстаток <> 0
	|
	|ИТОГИ ПО
	|	ОБЩИЕ,
	|	ДокументРаспределения,
	|	ОС_НМА
	|";
	
	Запрос.УстановитьПараметр("Организация", 	ПараметрыОтчета.Организация);
	Запрос.УстановитьПараметр("НачалоПериода", 	ПараметрыОтчета.НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",  	КонецДня(ПараметрыОтчета.КонецПериода));
	Запрос.УстановитьПараметр("ПериодБазы",  	ПериодБазы);
	Запрос.УстановитьПараметр("КонецПериодаГраница", Новый Граница(КонецДня(ПараметрыОтчета.КонецПериода), ВидГраницы.Включая));
	
	Запрос.УстановитьПараметр("ПредставлениеСчетФактураПолученный",        Метаданные.Документы.СчетФактураПолученный.Синоним);
	Запрос.УстановитьПараметр("ПредставлениеСчетФактураПолученныйНА",      Метаданные.Документы.СчетФактураПолученныйНалоговыйАгент.Синоним);
	Запрос.УстановитьПараметр("ПредставлениеИнойДокументПодтвержденияНДС", Метаданные.Документы.ИнойДокументПодтвержденияНДС.Синоним);
	
	
	ВыборкаОбщие = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	Если ВыборкаОбщие.Количество() > 0 Тогда
		
		ОбластьШапка     = Макет.ПолучитьОбласть("ШапкаОС_НМА|Основное");
		ОбластьДокумент  = Макет.ПолучитьОбласть("ДокументРаспределенияОС_НМА|Основное");
		ОбластьОС_НМА    = Макет.ПолучитьОбласть("ОС_НМА|Основное");
		ОбластьСтрока    = Макет.ПолучитьОбласть("СтрокаОС_НМА|Основное");
		ОбластьИтоги     = Макет.ПолучитьОбласть("ИтогиОС_НМА|Основное");
		
		СтруктураПараметровВывода = Новый Структура;
		СтруктураПараметровВывода.Вставить("ОбщийЭкспорт", ПараметрыОтчета.ВыводитьОбщийЭкспорт);
		СтруктураПараметровВывода.Вставить("ЭкспортНесырьевыхТоваров", ПараметрыОтчета.ВыводитьЭкспортНесырьевыхТоваров);
		СтруктураПараметровВывода.Вставить("ЭкспортСырьевыхТоваровУслуг", ПараметрыОтчета.ВыводитьЭкспортСырьевыхТоваровУслуг);
		СтруктураПараметровВывода.Вставить("Космос", ПараметрыОтчета.ВыводитьКосмос);
		
		СтруктураДопОбласей = Новый Структура;
		СтруктураДопОбласей.Вставить("ШапкаОС_НМА");
		СтруктураДопОбласей.Вставить("ДокументРаспределенияОС_НМА");
		СтруктураДопОбласей.Вставить("ОС_НМА");
		СтруктураДопОбласей.Вставить("СтрокаОС_НМА");
		СтруктураДопОбласей.Вставить("ИтогиОС_НМА");
		
		Для каждого ДопОбласть Из СтруктураДопОбласей Цикл
			МассивДопОбластейШапки = Новый Массив;
			Для каждого ПараметрВывода Из СтруктураПараметровВывода Цикл
				Если НЕ ПараметрВывода.Значение Тогда
					Продолжить;
				КонецЕсли;
				МассивДопОбластейШапки.Добавить(Макет.ПолучитьОбласть(ДопОбласть.Ключ + "|" + ПараметрВывода.Ключ));
			КонецЦикла;
			СтруктураДопОбласей[ДопОбласть.Ключ] = МассивДопОбластейШапки;
		КонецЦикла;
		
		ОбластьШапка.Параметры.НаименованиеСекции = НСтр("ru = 'Результаты распределения НДС (в части ОС и НМА)';
														|en = 'VAT allocation results (in FA and IA)'");
		ДокументРезультат.Вывести(ОбластьШапка);
		Для каждого Область Из СтруктураДопОбласей["ШапкаОС_НМА"] Цикл
			ДокументРезультат.Присоединить(Область);
		КонецЦикла;
		
		ВыборкаОбщие.Следующий();
		
		ШаблонПредставления = НСтр("ru = '%1 №%2 от %3';
									|en = '%1 No%2 dated %3'");
		
		ВыборкаДокументРаспределения = ВыборкаОбщие.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаДокументРаспределения.Следующий() Цикл
			
			ОбластьДокумент.Параметры.Заполнить(ВыборкаДокументРаспределения);
			Если Не ЗначениеЗаполнено(ВыборкаДокументРаспределения.ДокументРаспределения) Тогда
				ОбластьДокумент.Параметры.ДокументРаспределения = НСтр("ru = 'Не распределено';
																		|en = 'Not allocated'");
			КонецЕсли;
			ДокументРезультат.Вывести(ОбластьДокумент);
			
			Для каждого Область Из СтруктураДопОбласей["ДокументРаспределенияОС_НМА"] Цикл
				Область.Параметры.Заполнить(ВыборкаДокументРаспределения);
				ДокументРезультат.Присоединить(Область);
			КонецЦикла;
			
			ВыборкаОС_НМА = ВыборкаДокументРаспределения.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаОС_НМА.Следующий() Цикл
				
				ОбластьОС_НМА.Параметры.Заполнить(ВыборкаОС_НМА);
				Если Не ЗначениеЗаполнено(ВыборкаОС_НМА.Состояние) Тогда
					ОбластьОС_НМА.Параметры.ОС_НМА = "" + ВыборкаОС_НМА.ОС_НМА + " (" + НСтр("ru = 'Ожидается принятие к учету';
																							|en = 'Awaiting recognition'") + ")";
				Иначе
					ОбластьОС_НМА.Параметры.ОС_НМА = "" + ВыборкаОС_НМА.ОС_НМА + Символы.ПС + ВыборкаОС_НМА.Состояние;
				КонецЕсли;
				ДокументРезультат.Вывести(ОбластьОС_НМА);
				
				Для каждого Область Из СтруктураДопОбласей["ОС_НМА"] Цикл
					Область.Параметры.Заполнить(ВыборкаОС_НМА);
					ДокументРезультат.Присоединить(Область);
				КонецЦикла;
				
				ВыборкаСтрокиОС = ВыборкаОС_НМА.Выбрать();
				Пока ВыборкаСтрокиОС.Следующий() Цикл
					ОбластьСтрока.Параметры.Заполнить(ВыборкаСтрокиОС);
						
					Если ЗначениеЗаполнено(ВыборкаСтрокиОС.СчетФактураПолученный) Тогда
						ОбластьСтрока.Параметры.ПредставлениеСчетаФактуры = СтрШаблон(ШаблонПредставления,
																			ВыборкаСтрокиОС.ПредставлениеСчетаФактуры,
																			ВыборкаСтрокиОС.НомерСчетаФактуры,
																			Формат(ВыборкаСтрокиОС.ДатаСчетаФактуры, "ДЛФ=D"));
					КонецЕсли;

					ДокументРезультат.Вывести(ОбластьСтрока);
					Для каждого Область Из СтруктураДопОбласей["СтрокаОС_НМА"] Цикл
						Область.Параметры.Заполнить(ВыборкаСтрокиОС);
						ДокументРезультат.Присоединить(Область);
					КонецЦикла;
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
		
		ОбластьИтоги.Параметры.Заполнить(ВыборкаОбщие);
		ДокументРезультат.Вывести(ОбластьИтоги);
		Для каждого Область Из СтруктураДопОбласей["ИтогиОС_НМА"] Цикл
			Область.Параметры.Заполнить(ВыборкаОбщие);
			ДокументРезультат.Присоединить(Область);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры
//-- НЕ УТ

#КонецОбласти

#КонецЕсли