#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	МеждународнаяОтчетностьСервер.ПриКомпоновкеРезультата(СхемаКомпоновкиДанных, КомпоновщикНастроек, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка);
	
	МеждународнаяОтчетностьСервер.ОбъединитьЗаголовкиПолей(ДокументРезультат, НСтр("ru = 'Дебет';
																					|en = 'Debit'"));
	МеждународнаяОтчетностьСервер.ОбъединитьЗаголовкиПолей(ДокументРезультат, НСтр("ru = 'Кредит';
																					|en = 'Credit'"));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Для общей формы "Форма отчета" подсистемы "Варианты отчетов".
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.События.ПередЗагрузкойНастроекВКомпоновщик = Истина;
	
КонецПроцедуры

// Вызывается перед загрузкой новых настроек. Используется для изменения схемы компоновки.
//
Процедура ПередЗагрузкойНастроекВКомпоновщик(Контекст, КлючСхемы, КлючВарианта, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоПлановСчетовВМеждународномУчете") Тогда
		ПолеСчет = СхемаКомпоновкиДанных.НаборыДанных["ОсновнойНаборДанных"].Поля.Найти("Счет"); // ПолеНабораДанныхСхемыКомпоновкиДанных - 
		ПараметрыРедактирования =  ПолеСчет.ПараметрыРедактирования.Элементы;
		ПолеПараметраПланСчетов = Новый ПолеКомпоновкиДанных("ПараметрыДанных.ПланСчетов");
		Для Каждого ПараметрРедактирования Из ПараметрыРедактирования Цикл
			Если ПараметрРедактирования.Параметр = Новый ПараметрКомпоновкиДанных("СвязиПараметровВыбора") Тогда
				ПараметрРедактирования.Использование = Ложь;
				Для Каждого СвязьПараметровВыбора Из ПараметрРедактирования.Значение Цикл
					Если СвязьПараметровВыбора.Поле = ПолеПараметраПланСчетов Тогда
						ПараметрРедактирования.Значение.Удалить(СвязьПараметровВыбора);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
		ОтчетыСервер.ПодключитьСхему(ЭтотОбъект, Контекст, СхемаКомпоновкиДанных, КлючСхемы);
	КонецЕсли;
	Если ТипЗнч(Контекст) = Тип("ФормаКлиентскогоПриложения") И 
			Контекст.Параметры.Свойство("ДополнительныеПараметры") Тогда
			ДополнительныеПараметры = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Контекст.Параметры,"ДополнительныеПараметры");
			Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура") Тогда 
				ГруппировкаПоПериоду = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "ГруппировкаПоПериоду");
				Если ГруппировкаПоПериоду <> Неопределено Тогда
					Поле = ГруппировкаПоПериоду;
					Если НовыеНастройкиКД.Структура.Количество() = 1 И ТипЗнч(НовыеНастройкиКД.Структура[0]) = Тип("ТаблицаКомпоновкиДанных") Тогда
						ТаблицаКомпоновки = НовыеНастройкиКД.Структура[0];
						ТаблицаКомпоновки.Строки.Очистить();
						Строка = ТаблицаКомпоновки.Строки.Добавить(); // ГруппировкаТаблицыКомпоновкиДанных -
						СтруктураГруппировки = Строка.Структура; // КоллекцияЭлементовСтруктурыТаблицыКомпоновкиДанных -
						НоваяГруппировка = СтруктураГруппировки.Добавить(); // ГруппировкаТаблицыКомпоновкиДанных -
						НоваяГруппировка.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
						НоваяГруппировка.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
						ПолеГруппировки = НоваяГруппировка.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
						ПолеГруппировки.Поле = Новый ПолеКомпоновкиДанных(Поле);
					Иначе
						НовыеНастройкиКД.Структура.Очистить();  
						КомпоновкаДанныхКлиентСервер.ДобавитьГруппировку(НовыеНастройкиКД, Новый ПолеКомпоновкиДанных(Поле));
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
