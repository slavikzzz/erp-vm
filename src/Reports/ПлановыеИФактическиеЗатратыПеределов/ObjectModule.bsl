
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - см. ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.События.ПриЗагрузкеВариантаНаСервере = Истина;
	Настройки.События.ПередЗагрузкойНастроекВКомпоновщик = Истина;
	
КонецПроцедуры

Процедура ПриЗагрузкеВариантаНаСервере(ЭтаФорма, НовыеНастройкиКД) Экспорт
	
	Отчет = ЭтаФорма.Отчет;
	КомпоновщикНастроекФормы = Отчет.КомпоновщикНастроек;
	
	// Изменение настроек по функциональным опциям.
	НастроитьПараметрыОтборыПоФункциональнымОпциям(КомпоновщикНастроекФормы);
	
	НовыеНастройкиКД = КомпоновщикНастроекФормы.Настройки;
	
КонецПроцедуры

Процедура ПередЗагрузкойНастроекВКомпоновщик(Контекст, КлючСхемы, КлючВарианта, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД) Экспорт
	
	Если ТипЗнч(Контекст) = Тип("ФормаКлиентскогоПриложения") Тогда
		НастроитьПараметрыОтчетаПоВариантуОтчета(КлючВарианта, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД);
		ОтчетыСервер.ПодключитьСхему(ЭтотОбъект, Контекст, СхемаКомпоновкиДанных, КлючСхемы);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	ДоступныеПоляУсловий = Новый Массив;
	СхемаРазузлования = Отчеты.ДеревоСебестоимостиПродукции.ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	НастройкиРазузлования = СхемаРазузлования.НастройкиПоУмолчанию;
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	
	НастройкиРазузлования.Отбор.Элементы.Очистить();
	НаборДанныхРазузловки = СхемаРазузлования.НаборыДанных.Найти("ВыпущеннаяПродукция");
	
	Если НаборДанныхРазузловки = Неопределено Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Изменилась схема разузлования продукции. Разузлование невозможно.';
				|en = 'Product explosion scheme is changed. Cannot explode.'"));
		Возврат;
		
	КонецЕсли;
	
	Для Каждого ПолеДанных Из НаборДанныхРазузловки.Поля Цикл
		
		Если ПолеДанных.ОграничениеИспользования.Условие Тогда
			Продолжить;
		КонецЕсли;
		
		ДоступныеПоляУсловий.Добавить(Новый ПолеКомпоновкиДанных(ПолеДанных.Поле));
		
	КонецЦикла;
	
	ЗаполнитьНастройкиОтчетаРекурсивно(НастройкиРазузлования.Отбор, НастройкиОтчета.Отбор, ДоступныеПоляУсловий);
	
	ПараметрПериод = НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Период"));
	Период = ПараметрПериод.Значение; // СтандартныйПериод
	НачалоПериода = ?(ПараметрПериод.Использование, Период.ДатаНачала, НачалоГода(ТекущаяДатаСеанса()));
	КонецПериода = ?(ПараметрПериод.Использование, Период.ДатаОкончания, ТекущаяДатаСеанса());
	
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(НастройкиРазузлования, "Период", ПараметрПериод.Значение);
	
	ДанныеПоСебестоимости = НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ДанныеПоСебестоимости")).Значение;
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(НастройкиРазузлования, "ДанныеПоСебестоимости", ДанныеПоСебестоимости);
	
	ПараметрыДереваСебестоимости = СтруктураСебестоимости.ПараметрыДереваСебестоимости();
	ПараметрыДереваСебестоимости.ДинамическоеСчитывание = Ложь;
	ПараметрыДереваСебестоимости.ТипРезультата = "МенеджерВременныхТаблиц";
	ПараметрыДереваСебестоимости.Результат = МенеджерВТ;
	
	ПараметрыУзлаДереваСебестоимости = СтруктураСебестоимости.ПараметрыУзлаДереваСебестоимости();
	ПараметрыУзлаДереваСебестоимости.ИнтерактивнаяНастройка = Истина;
	ПараметрыУзлаДереваСебестоимости.НастройкиКД = НастройкиРазузлования;
	ПараметрыУзлаДереваСебестоимости.Отборы.ДанныеПоСебестоимости = ДанныеПоСебестоимости;
	ПараметрыУзлаДереваСебестоимости.Отборы.РазворачиватьДопРасходы = Ложь;
		
	СтруктураСебестоимости.ПостроитьДеревоСебестоимости(ПараметрыДереваСебестоимости, ПараметрыУзлаДереваСебестоимости);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.УстановитьПараметр("ДанныеПоСебестоимости", ДанныеПоСебестоимости);
	Запрос.УстановитьПараметр("ПредставлениеФакта", НСтр("ru = 'ФКП = %1';
														|en = 'FKP = %1'"));
	Запрос.УстановитьПараметр("ПредставлениеПлана", НСтр("ru = 'ПКП = %1';
														|en = 'PKP = %1'"));
	
#Область ТекстЗапроса
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Результат.АналитикаУчетаПродукции.Номенклатура КАК Продукция,
		|	Результат.АналитикаУчетаПродукции.Характеристика КАК ХарактеристикаПродукции,
		|	Результат.АналитикаУчетаПродукции.Назначение КАК Назначение,
		|	Результат.ПартияПродукции КАК ПартияПродукции,
		|	Результат.Организация КАК Организация,
		|	Результат.АналитикаУчетаПродукции.Серия КАК Серия,
		|	Результат.ПартияЗатрата КАК ПартияПолуфабриката,
		|	ВЫРАЗИТЬ(Результат.Номенклатура КАК Справочник.Номенклатура) КАК Полуфабрикат,
		|	Результат.Характеристика КАК ХарактеристикаПолуфабриката,
		|	ВЫБОР
		|		КОГДА Результат.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.ВидыСтрокДереваСебестоимости.ПартияПродукции)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоПродукция,
		|	СУММА(Результат.Количество) КАК Количество,
		|	Результат.АналитикаУчетаПродукции КАК АналитикаУчетаПродукции
		|ПОМЕСТИТЬ КоличествоВыпуска
		|ИЗ
		|	Результат КАК Результат
		|ГДЕ
		|	Результат.ТребуетсяРазузлование
		|
		|СГРУППИРОВАТЬ ПО
		|	ВЫРАЗИТЬ(Результат.Номенклатура КАК Справочник.Номенклатура),
		|	Результат.Характеристика,
		|	Результат.АналитикаУчетаПродукции.Номенклатура,
		|	Результат.АналитикаУчетаПродукции.Характеристика,
		|	Результат.АналитикаУчетаПродукции,
		|	Результат.АналитикаУчетаПродукции.Назначение,
		|	Результат.ПартияЗатрата,
		|	Результат.Организация,
		|	Результат.ПартияПродукции,
		|	ВЫБОР
		|		КОГДА Результат.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.ВидыСтрокДереваСебестоимости.ПартияПродукции)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ,
		|	Результат.АналитикаУчетаПродукции.Серия
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Продукция,
		|	ХарактеристикаПродукции,
		|	Назначение,
		|	ПартияПродукции,
		|	Организация,
		|	Серия
		|;
		|
		|ВЫБРАТЬ
		|	КоличествоВыпуска.Продукция КАК Продукция,
		|	КоличествоВыпуска.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	КоличествоВыпуска.Назначение КАК Назначение,
		|	КоличествоВыпуска.ПартияПродукции КАК ПартияПродукции,
		|	КоличествоВыпуска.ПартияПолуфабриката КАК ПартияПолуфабриката,
		|	КоличествоВыпуска.Полуфабрикат КАК Полуфабрикат,
		|	КоличествоВыпуска.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
		|	КоличествоВыпуска.ЭтоПродукция КАК ЭтоПродукция,
		|	КоличествоВыпуска.Количество КАК Количество,
		|	КоличествоВыпуска.АналитикаУчетаПродукции КАК АналитикаУчетаПродукции,
		|	КоличествоВыпуска.Организация КАК Организация,
		|	КоличествоВыпуска.Серия КАК Серия
		|ПОМЕСТИТЬ КоличествоВыпускаЗатраты
		|ИЗ
		|	КоличествоВыпуска КАК КоличествоВыпуска
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	АналитикаУчетаПродукции,
		|	ХарактеристикаПолуфабриката,
		|	ПартияПолуфабриката,
		|	ПартияПродукции
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КоличествоВыпуска.Продукция КАК Продукция,
		|	КоличествоВыпуска.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	КоличествоВыпуска.Назначение КАК Назначение,
		|	КоличествоВыпуска.ПартияПродукции КАК ПартияПродукции,
		|	СУММА(КоличествоВыпуска.Количество) КАК Количество,
		|	КоличествоВыпуска.АналитикаУчетаПродукции КАК АналитикаУчетаПродукции,
		|	КоличествоВыпуска.Организация КАК Организация,
		|	КоличествоВыпуска.Серия КАК Серия
		|ПОМЕСТИТЬ ВТПродукция
		|ИЗ
		|	КоличествоВыпуска КАК КоличествоВыпуска
		|ГДЕ
		|	КоличествоВыпуска.ЭтоПродукция
		|
		|СГРУППИРОВАТЬ ПО
		|	КоличествоВыпуска.Продукция,
		|	КоличествоВыпуска.АналитикаУчетаПродукции,
		|	КоличествоВыпуска.Назначение,
		|	КоличествоВыпуска.ПартияПродукции,
		|	КоличествоВыпуска.ХарактеристикаПродукции,
		|	КоличествоВыпуска.Организация,
		|	КоличествоВыпуска.Серия
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	АналитикаУчетаПродукции,
		|	ПартияПродукции,
		|	Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Результат.АналитикаУчетаВыходноеИзделие КАК АналитикаУчетаВыходноеИзделие,
		|	Результат.Количество КАК Количество,
		|	ВЫБОР
		|		КОГДА Результат.ТребуетсяРазузлование
		|			ТОГДА 0
		|		ИНАЧЕ Результат.Сумма
		|	КОНЕЦ КАК Сумма,
		|	ВЫБОР
		|		КОГДА Результат.ТребуетсяРазузлование
		|			ТОГДА 0
		|		ИНАЧЕ Результат.СуммаЗабалансовая
		|	КОНЕЦ КАК СуммаЗабалансовая,
		|	Результат.Номенклатура КАК Затрата,
		|	Результат.Характеристика КАК ХарактеристикаЗатраты,
		|	Результат.АналитикаУчетаВыходноеИзделие.Номенклатура КАК Полуфабрикат,
		|	Результат.АналитикаУчетаВыходноеИзделие.Характеристика КАК ХарактеристикаПолуфабриката,
		|	Результат.АналитикаУчетаПродукции КАК АналитикаУчетаПродукции,
		|	Результат.ТипЗатрат КАК ТипЗатрат,
		|	Результат.ПартияВыходноеИзделие КАК ПартияПолуфабриката,
		|	Результат.ПартияПродукции КАК ПартияПродукции,
		|	ВЫБОР
		|		КОГДА Результат.Количество = 0
		|			ТОГДА 0
		|		ИНАЧЕ Результат.Сумма / Результат.Количество
		|	КОНЕЦ КАК Цена,
		|	Результат.ТребуетсяРазузлование КАК ЭтоПолуфабрикат,
		|	Результат.Подразделение КАК Подразделение,
		|	Результат.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	Результат.ВидСтроки КАК ВидСтроки,
		|	Результат.Уровень КАК Уровень
		|ПОМЕСТИТЬ Затраты
		|ИЗ
		|	Результат КАК Результат
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ПартияПродукции,
		|	АналитикаУчетаПродукции,
		|	ПартияПолуфабриката,
		|	ХарактеристикаПолуфабриката
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТПродукция.Продукция КАК Продукция,
		|	ВТПродукция.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	ВТПродукция.Назначение КАК Назначение,
		|	ВТПродукция.ПартияПродукции КАК ПартияПродукции,
		|	ВТПродукция.Количество КАК КоличествоВыпуск,
		|	ВТПродукция.Продукция КАК Полуфабрикат,
		|	ВТПродукция.ХарактеристикаПродукции КАК ХарактеристикаПолуфабриката,
		|	Затраты.Затрата КАК Затрата,
		|	Затраты.ХарактеристикаЗатраты КАК ХарактеристикаЗатраты,
		|	Затраты.Сумма КАК Стоимость,
		|	Затраты.СуммаЗабалансовая КАК СтоимостьЗабалансовая,
		|	Затраты.Количество КАК Количество,
		|	Затраты.Цена КАК Цена,
		|	Затраты.ЭтоПолуфабрикат КАК ЭтоПолуфабрикат,
		|	Затраты.Подразделение КАК Подразделение,
		|	ВТПродукция.Организация КАК Организация,
		|	Затраты.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	ВТПродукция.Серия КАК Серия,
		|	Затраты.Уровень КАК Уровень
		|ПОМЕСТИТЬ ЗатратыВРазрезе
		|ИЗ
		|	ВТПродукция КАК ВТПродукция
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Затраты КАК Затраты
		|		ПО ВТПродукция.АналитикаУчетаПродукции = Затраты.АналитикаУчетаВыходноеИзделие
		|			И (НЕ Затраты.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.ВидыСтрокДереваСебестоимости.ПартияПродукции))
		|			И ВТПродукция.ПартияПродукции = Затраты.ПартияПродукции
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КоличествоВыпуска.Продукция,
		|	КоличествоВыпуска.ХарактеристикаПродукции,
		|	КоличествоВыпуска.Назначение,
		|	КоличествоВыпуска.ПартияПродукции,
		|	КоличествоВыпуска.Количество,
		|	КоличествоВыпуска.Полуфабрикат,
		|	КоличествоВыпуска.ХарактеристикаПолуфабриката,
		|	Затраты.Затрата,
		|	Затраты.ХарактеристикаЗатраты,
		|	СУММА(Затраты.Сумма),
		|	СУММА(Затраты.СуммаЗабалансовая),
		|	СУММА(Затраты.Количество),
		|	СРЕДНЕЕ(Затраты.Цена),
		|	Затраты.ЭтоПолуфабрикат,
		|	Затраты.Подразделение,
		|	КоличествоВыпуска.Организация,
		|	Затраты.СтатьяКалькуляции,
		|	КоличествоВыпуска.Серия,
		|	Затраты.Уровень
		|ИЗ
		|	КоличествоВыпускаЗатраты КАК КоличествоВыпуска
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Затраты КАК Затраты
		|		ПО КоличествоВыпуска.АналитикаУчетаПродукции = Затраты.АналитикаУчетаПродукции
		|			И КоличествоВыпуска.Полуфабрикат = Затраты.Полуфабрикат
		|			И КоличествоВыпуска.ХарактеристикаПолуфабриката = Затраты.ХарактеристикаПолуфабриката
		|			И КоличествоВыпуска.ПартияПолуфабриката = Затраты.ПартияПолуфабриката
		|			И КоличествоВыпуска.ПартияПродукции = Затраты.ПартияПродукции
		|ГДЕ
		|	НЕ КоличествоВыпуска.ЭтоПродукция
		|
		|СГРУППИРОВАТЬ ПО
		|	КоличествоВыпуска.Продукция,
		|	КоличествоВыпуска.ХарактеристикаПродукции,
		|	КоличествоВыпуска.Назначение,
		|	КоличествоВыпуска.ПартияПродукции,
		|	КоличествоВыпуска.Количество,
		|	КоличествоВыпуска.Полуфабрикат,
		|	КоличествоВыпуска.ХарактеристикаПолуфабриката,
		|	Затраты.Затрата,
		|	Затраты.ХарактеристикаЗатраты,
		|	Затраты.ЭтоПолуфабрикат,
		|	Затраты.Подразделение,
		|	КоличествоВыпуска.Организация,
		|	Затраты.СтатьяКалькуляции,
		|	КоличествоВыпуска.Серия,
		|	Затраты.Уровень
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Продукция,
		|	ХарактеристикаПродукции,
		|	Назначение,
		|	ПартияПродукции,
		|	Серия,
		|	Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВТПродукция.Продукция КАК Продукция,
		|	ВТПродукция.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	ВТПродукция.Назначение КАК НазначениеПродукции,
		|	ВТПродукция.ПартияПродукции КАК Партия,
		|	Выпуски.Период КАК Период,
		|	ВТПродукция.Организация КАК Организация
		|ПОМЕСТИТЬ ДатыВыпусков
		|ИЗ
		|	ВТПродукция КАК ВТПродукция
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ВыпускПродукции КАК Выпуски
		|		ПО ВТПродукция.Организация = Выпуски.Организация
		|			И ВТПродукция.АналитикаУчетаПродукции = Выпуски.АналитикаУчетаНоменклатуры
		|			И ВТПродукция.ПартияПродукции = Выпуски.Регистратор
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Продукция,
		|	ХарактеристикаПродукции,
		|	НазначениеПродукции,
		|	Партия,
		|	Период,
		|	Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(Множители.Калькуляция.Дата) КАК ДатаКалькуляции,
		|	ДатыВыпусков.Продукция КАК Продукция,
		|	ДатыВыпусков.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	ДатыВыпусков.НазначениеПродукции КАК НазначениеПродукции,
		|	ДатыВыпусков.Партия КАК Партия,
		|	ИСТИНА КАК КалькуляцияЗаказа,
		|	ДатыВыпусков.Организация КАК Организация
		|ПОМЕСТИТЬ КалькуляцииЗаказов
		|ИЗ
		|	ДатыВыпусков КАК ДатыВыпусков
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МножителиПартийИПолуфабрикатов КАК Множители
		|		ПО ДатыВыпусков.Продукция = Множители.Продукция
		|			И ДатыВыпусков.ХарактеристикаПродукции = Множители.ХарактеристикаПродукции
		|			И ДатыВыпусков.НазначениеПродукции = Множители.Назначение
		|			И ДатыВыпусков.Партия = Множители.ПартияСпецификацияПродукции
		|			И ДатыВыпусков.Период >= Множители.Калькуляция.Дата
		|			И ДатыВыпусков.Организация = Множители.Калькуляция.Организация
		|
		|СГРУППИРОВАТЬ ПО
		|	ДатыВыпусков.Продукция,
		|	ДатыВыпусков.ХарактеристикаПродукции,
		|	ДатыВыпусков.НазначениеПродукции,
		|	ДатыВыпусков.Партия,
		|	ДатыВыпусков.Организация
		|	
		|ИНДЕКСИРОВАТЬ ПО
		|	Продукция,
		|	ХарактеристикаПродукции,
		|	НазначениеПродукции,
		|	Партия,
		|	Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(Множители.Калькуляция.Дата) КАК ДатаКалькуляции,
		|	ДатыВыпусков.Продукция КАК Продукция,
		|	ДатыВыпусков.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	ДатыВыпусков.НазначениеПродукции КАК НазначениеПродукции,
		|	ДатыВыпусков.Партия КАК Партия,
		|	ЛОЖЬ КАК КалькуляцияЗаказа,
		|	ДатыВыпусков.Организация КАК Организация
		|ПОМЕСТИТЬ КалькуляцииСпецификаций
		|ИЗ
		|	ДатыВыпусков КАК ДатыВыпусков
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МножителиПартийИПолуфабрикатов КАК Множители
		|		ПО ДатыВыпусков.Продукция = Множители.Продукция
		|			И ДатыВыпусков.ХарактеристикаПродукции = Множители.ХарактеристикаПродукции
		|			И ДатыВыпусков.Период >= Множители.Калькуляция.Дата
		|			И (ТИПЗНАЧЕНИЯ(Множители.ПартияСпецификацияПродукции) = ТИП(Справочник.РесурсныеСпецификации))
		|			И ДатыВыпусков.Организация = Множители.Калькуляция.Организация
		|		ЛЕВОЕ СОЕДИНЕНИЕ КалькуляцииЗаказов КАК КалькуляцииЗаказов
		|		ПО ДатыВыпусков.Продукция = КалькуляцииЗаказов.Продукция
		|			И ДатыВыпусков.ХарактеристикаПродукции = КалькуляцииЗаказов.ХарактеристикаПродукции
		|			И ДатыВыпусков.НазначениеПродукции = КалькуляцииЗаказов.НазначениеПродукции
		|			И ДатыВыпусков.Партия = КалькуляцииЗаказов.Партия
		|			И ДатыВыпусков.Организация = КалькуляцииЗаказов.Организация
		|ГДЕ
		|	КалькуляцииЗаказов.ДатаКалькуляции ЕСТЬ NULL
		|
		|СГРУППИРОВАТЬ ПО
		|	ДатыВыпусков.Продукция,
		|	ДатыВыпусков.ХарактеристикаПродукции,
		|	ДатыВыпусков.НазначениеПродукции,
		|	ДатыВыпусков.Партия,
		|	ДатыВыпусков.Организация
		|// не требуется индекс
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КалькуляцииЗаказов.ДатаКалькуляции КАК ДатаКалькуляции,
		|	КалькуляцииЗаказов.Продукция КАК Продукция,
		|	КалькуляцииЗаказов.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	КалькуляцииЗаказов.НазначениеПродукции КАК НазначениеПродукции,
		|	КалькуляцииЗаказов.Партия КАК Партия,
		|	КалькуляцииЗаказов.КалькуляцияЗаказа КАК КалькуляцияЗаказа,
		|	КалькуляцииЗаказов.Организация КАК Организация
		|ПОМЕСТИТЬ ПоследниеКалькуляции
		|ИЗ
		|	КалькуляцииЗаказов КАК КалькуляцииЗаказов
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КалькуляцииСпецификаций.ДатаКалькуляции,
		|	КалькуляцииСпецификаций.Продукция,
		|	КалькуляцииСпецификаций.ХарактеристикаПродукции,
		|	КалькуляцииСпецификаций.НазначениеПродукции,
		|	КалькуляцииСпецификаций.Партия,
		|	КалькуляцииСпецификаций.КалькуляцияЗаказа,
		|	КалькуляцииСпецификаций.Организация
		|ИЗ
		|	КалькуляцииСпецификаций КАК КалькуляцииСпецификаций
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Продукция,
		|	ХарактеристикаПродукции,
		|	НазначениеПродукции,
		|	Партия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(Множители.Калькуляция) КАК Калькуляция,
		|	МАКСИМУМ(
		|		ВЫБОР
		|			КОГДА ТИПЗНАЧЕНИЯ(Множители.ПартияСпецификацияПродукции) = ТИП(Справочник.РесурсныеСпецификации)
		|				ТОГДА Множители.ПартияСпецификацияПродукции
		|			ИНАЧЕ
		|				ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)
		|		КОНЕЦ) КАК СпецификацияПлановойКалькуляции,
		|	ПоследниеКалькуляции.Продукция КАК Продукция,
		|	ПоследниеКалькуляции.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	ПоследниеКалькуляции.НазначениеПродукции КАК НазначениеПродукции,
		|	ПоследниеКалькуляции.Партия КАК Партия,
		|	ПоследниеКалькуляции.КалькуляцияЗаказа КАК КалькуляцияЗаказа,
		|	КоличествоВыпуска.Количество КАК КоличествоПродукцииФакт,
		|	ПоследниеКалькуляции.Организация КАК Организация,
		|	КоличествоВыпуска.Серия КАК Серия
		|ПОМЕСТИТЬ КалькуляцииПродукции
		|ИЗ
		|	ПоследниеКалькуляции КАК ПоследниеКалькуляции
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МножителиПартийИПолуфабрикатов КАК Множители
		|		ПО ПоследниеКалькуляции.Продукция = Множители.Продукция
		|			И ПоследниеКалькуляции.ХарактеристикаПродукции = Множители.ХарактеристикаПродукции
		|			И (ВЫБОР
		|				КОГДА ПоследниеКалькуляции.КалькуляцияЗаказа
		|					ТОГДА ПоследниеКалькуляции.НазначениеПродукции = Множители.Назначение
		|				ИНАЧЕ ИСТИНА
		|			КОНЕЦ)
		|			И ПоследниеКалькуляции.ДатаКалькуляции = Множители.Калькуляция.Дата
		|			И (ВЫБОР
		|				КОГДА ПоследниеКалькуляции.КалькуляцияЗаказа
		|					ТОГДА ПоследниеКалькуляции.Партия = Множители.ПартияСпецификацияПродукции
		|				ИНАЧЕ ИСТИНА
		|			КОНЕЦ)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КоличествоВыпуска КАК КоличествоВыпуска
		|		ПО ПоследниеКалькуляции.Продукция = КоличествоВыпуска.Продукция
		|			И ПоследниеКалькуляции.ХарактеристикаПродукции = КоличествоВыпуска.ХарактеристикаПродукции
		|			И ПоследниеКалькуляции.НазначениеПродукции = КоличествоВыпуска.Назначение
		|			И ПоследниеКалькуляции.Партия = КоличествоВыпуска.ПартияПродукции
		|			И (КоличествоВыпуска.ЭтоПродукция)
		|
		|СГРУППИРОВАТЬ ПО
		|	ПоследниеКалькуляции.Продукция,
		|	ПоследниеКалькуляции.ХарактеристикаПродукции,
		|	ПоследниеКалькуляции.НазначениеПродукции,
		|	ПоследниеКалькуляции.Партия,
		|	ПоследниеКалькуляции.КалькуляцияЗаказа,
		|	КоличествоВыпуска.Количество,
		|	ПоследниеКалькуляции.Организация,
		|	КоличествоВыпуска.Серия
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Продукция,
		|	ХарактеристикаПродукции,
		|	НазначениеПродукции,
		|	Партия,
		|	Серия,
		|	Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	КалькуляцииПродукции.Продукция КАК Продукция,
		|	КалькуляцииПродукции.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	КалькуляцииПродукции.НазначениеПродукции КАК НазначениеПродукции,
		|	КалькуляцииПродукции.Партия КАК Партия,
		|	КалькуляцииПродукции.Серия КАК Серия,
		|	КалькуляцииПродукции.Организация КАК Организация,
		|	КалькуляцииПродукции.Калькуляция КАК Калькуляция,
		|	КалькуляцииПродукции.КалькуляцияЗаказа КАК КалькуляцияЗаказа
		|ПОМЕСТИТЬ КалькуляцииПродукцииИЗаказа
		|ИЗ
		|	КалькуляцииПродукции КАК КалькуляцииПродукции
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Продукция,
		|	ХарактеристикаПродукции,
		|	НазначениеПродукции,
		|	Партия,
		|	Серия,
		|	Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗатратыВРазрезе.Продукция КАК Продукция,
		|	ЗатратыВРазрезе.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	ЗатратыВРазрезе.Назначение КАК Назначение,
		|	ЗатратыВРазрезе.ПартияПродукции КАК ПартияПродукции,
		|	ЗатратыВРазрезе.Полуфабрикат КАК Полуфабрикат,
		|	ЗатратыВРазрезе.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
		|	ЗатратыВРазрезе.Серия КАК Серия,
		|	ЗатратыВРазрезе.ЭтоПолуфабрикат КАК ЭтоПолуфабрикат,
		|	КалькуляцииПродукцииИЗаказа.Калькуляция КАК Калькуляция,
		|	ЗатратыВРазрезе.Затрата КАК Затрата,
		|	ЗатратыВРазрезе.ХарактеристикаЗатраты КАК ХарактеристикаЗатраты,
		|	КалькуляцииПродукцииИЗаказа.КалькуляцияЗаказа КАК КалькуляцияЗаказа,
		|	ЗатратыВРазрезе.Подразделение КАК Подразделение,
		|	ЗатратыВРазрезе.Организация КАК Организация,
		|	ЗатратыВРазрезе.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	ЗатратыВРазрезе.Стоимость КАК Стоимость,
		|	ЗатратыВРазрезе.СтоимостьЗабалансовая КАК СтоимостьЗабалансовая,
		|	ЗатратыВРазрезе.Количество КАК Количество,
		|	ЗатратыВРазрезе.КоличествоВыпуск КАК КоличествоВыпуск,
		|	ЗатратыВРазрезе.Цена КАК Цена,
		|	ЗатратыВРазрезе.Уровень КАК Уровень
		|ПОМЕСТИТЬ ФактическиеДанные
		|ИЗ
		|	ЗатратыВРазрезе КАК ЗатратыВРазрезе
		|		ЛЕВОЕ СОЕДИНЕНИЕ КалькуляцииПродукцииИЗаказа КАК КалькуляцииПродукцииИЗаказа
		|		ПО ЗатратыВРазрезе.Продукция = КалькуляцииПродукцииИЗаказа.Продукция
		|			И ЗатратыВРазрезе.ХарактеристикаПродукции = КалькуляцииПродукцииИЗаказа.ХарактеристикаПродукции
		|			И ЗатратыВРазрезе.Назначение = КалькуляцииПродукцииИЗаказа.НазначениеПродукции
		|			И ЗатратыВРазрезе.ПартияПродукции = КалькуляцииПродукцииИЗаказа.Партия
		|			И ЗатратыВРазрезе.Организация = КалькуляцииПродукцииИЗаказа.Организация
		|			И ЗатратыВРазрезе.Серия = КалькуляцииПродукцииИЗаказа.Серия
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Продукция,
		|	ХарактеристикаПродукции,
		|	Назначение,
		|	ПартияПродукции,
		|	Полуфабрикат,
		|	ХарактеристикаПолуфабриката,
		|	Серия,
		|	ЭтоПолуфабрикат,
		|	Калькуляция
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КалькуляцииПродукции.Калькуляция КАК Калькуляция,
		|	КалькуляцииПродукции.Продукция КАК Продукция,
		|	КалькуляцииПродукции.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	КалькуляцииПродукции.НазначениеПродукции КАК Назначение,
		|	КалькуляцииПродукции.Партия КАК ПартияСпецификацияПродукции,
		|	МножителиПартийИПолуфабрикатов.ПартияСпецификацияПродукции КАК КалькуляционнаяПартияПродукции,
		|	МножителиПартийИПолуфабрикатов.Назначение КАК КалькуляционноеНазначениеПродукции,
		|	КалькуляцииПродукции.КоличествоПродукцииФакт / МножителиПартийИПолуфабрикатов.КоличествоПолуфабриката КАК ДоляСтоимости,
		|	КалькуляцииПродукции.КалькуляцияЗаказа КАК КалькуляцияЗаказа,
		|	КалькуляцииПродукции.Серия КАК Серия
		|ПОМЕСТИТЬ ДолиСтоимостиПлановыхДанных
		|ИЗ
		|	КалькуляцииПродукции КАК КалькуляцииПродукции
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МножителиПартийИПолуфабрикатов КАК МножителиПартийИПолуфабрикатов
		|		ПО КалькуляцииПродукции.Продукция = МножителиПартийИПолуфабрикатов.Продукция
		|			И КалькуляцииПродукции.ХарактеристикаПродукции = МножителиПартийИПолуфабрикатов.ХарактеристикаПродукции
		|			И КалькуляцииПродукции.Калькуляция = МножителиПартийИПолуфабрикатов.Калькуляция
		|			И (ВЫБОР
		|				КОГДА КалькуляцииПродукции.КалькуляцияЗаказа
		|					ТОГДА МножителиПартийИПолуфабрикатов.Назначение = КалькуляцииПродукции.НазначениеПродукции
		|				ИНАЧЕ ИСТИНА
		|			КОНЕЦ)
		|			И (ВЫБОР
		|				КОГДА КалькуляцииПродукции.КалькуляцияЗаказа
		|					ТОГДА МножителиПартийИПолуфабрикатов.ПартияСпецификацияПродукции = КалькуляцииПродукции.Партия
		|				ИНАЧЕ МножителиПартийИПолуфабрикатов.ПартияСпецификацияПродукции = КалькуляцииПродукции.СпецификацияПлановойКалькуляции
		|			КОНЕЦ)
		|			И (МножителиПартийИПолуфабрикатов.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДолиСтоимостиПлановыхДанных.Калькуляция КАК Калькуляция,
		|	ДолиСтоимостиПлановыхДанных.Продукция КАК Продукция,
		|	ДолиСтоимостиПлановыхДанных.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	ДолиСтоимостиПлановыхДанных.Назначение КАК Назначение,
		|	ДолиСтоимостиПлановыхДанных.ПартияСпецификацияПродукции КАК ПартияСпецификацияПродукции,
		|	МножителиПартийИПолуфабрикатов.Числитель * ДолиСтоимостиПлановыхДанных.ДоляСтоимости КАК Числитель,
		|	МножителиПартийИПолуфабрикатов.Знаменатель КАК Знаменатель,
		|	МножителиПартийИПолуфабрикатов.КоличествоПолуфабриката * ДолиСтоимостиПлановыхДанных.ДоляСтоимости КАК КоличествоПолуфабриката,
		|	МножителиПартийИПолуфабрикатов.ПартияСпецификацияПродукции КАК КалькуляционнаяПартияПродукции,
		|	МножителиПартийИПолуфабрикатов.Полуфабрикат КАК Полуфабрикат,
		|	МножителиПартийИПолуфабрикатов.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
		|	МножителиПартийИПолуфабрикатов.ПартияСпецификацияПолуфабриката КАК ПартияСпецификацияПолуфабриката,
		|	ВЫБОР
		|		КОГДА &ДанныеПоСебестоимости = 1
		|			ТОГДА МножителиПартийИПолуфабрикатов.Стоимость
		|		КОГДА &ДанныеПоСебестоимости = 2
		|			ТОГДА МножителиПартийИПолуфабрикатов.СтоимостьБезНДС
		|		КОГДА &ДанныеПоСебестоимости = 3
		|			ТОГДА МножителиПартийИПолуфабрикатов.СтоимостьБезНДС
		|		КОГДА &ДанныеПоСебестоимости = 4
		|			ТОГДА МножителиПартийИПолуфабрикатов.СтоимостьРегл
		|	КОНЕЦ * ДолиСтоимостиПлановыхДанных.ДоляСтоимости КАК Стоимость,
		|	ВЫБОР
		|		КОГДА &ДанныеПоСебестоимости = 4
		|			ТОГДА МножителиПартийИПолуфабрикатов.СтоимостьЗабалансоваяРегл
		|		ИНАЧЕ МножителиПартийИПолуфабрикатов.СтоимостьЗабалансовая
		|	КОНЕЦ * ДолиСтоимостиПлановыхДанных.ДоляСтоимости КАК СтоимостьЗабалансовая,
		|	МножителиПартийИПолуфабрикатов.ПартияВыпущена КАК ПартияВыпущена,
		|	МножителиПартийИПолуфабрикатов.Калькуляция.Организация КАК Организация,
		|	ДолиСтоимостиПлановыхДанных.Серия КАК Серия
		|ПОМЕСТИТЬ МножителиПолуфабрикатов
		|ИЗ
		|	ДолиСтоимостиПлановыхДанных КАК ДолиСтоимостиПлановыхДанных
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МножителиПартийИПолуфабрикатов КАК МножителиПартийИПолуфабрикатов
		|		ПО ДолиСтоимостиПлановыхДанных.Продукция = МножителиПартийИПолуфабрикатов.Продукция
		|			И ДолиСтоимостиПлановыхДанных.ХарактеристикаПродукции = МножителиПартийИПолуфабрикатов.ХарактеристикаПродукции
		|			И ДолиСтоимостиПлановыхДанных.Калькуляция = МножителиПартийИПолуфабрикатов.Калькуляция
		|			И (МножителиПартийИПолуфабрикатов.Назначение = ДолиСтоимостиПлановыхДанных.КалькуляционноеНазначениеПродукции)
		|			И (МножителиПартийИПолуфабрикатов.ПартияСпецификацияПродукции = ДолиСтоимостиПлановыхДанных.КалькуляционнаяПартияПродукции)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МножителиПолуфабрикатов.Калькуляция КАК Калькуляция,
		|	МножителиПолуфабрикатов.Продукция КАК Продукция,
		|	МножителиПолуфабрикатов.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	МножителиПолуфабрикатов.Назначение КАК Назначение,
		|	МножителиПолуфабрикатов.ПартияСпецификацияПродукции КАК ПартияСпецификацияПродукции,
		|	МножителиПолуфабрикатов.КалькуляционнаяПартияПродукции КАК КалькуляционнаяПартияПродукции,
		|	МножителиПолуфабрикатов.Числитель КАК Числитель,
		|	МножителиПолуфабрикатов.Знаменатель КАК Знаменатель,
		|	МножителиПолуфабрикатов.КоличествоПолуфабриката КАК КоличествоПолуфабриката,
		|	МножителиПолуфабрикатов.ПартияВыпущена КАК ПартияВыпущена,
		|	МножителиПолуфабрикатов.Организация КАК Организация,
		|	МножителиПолуфабрикатов.Серия КАК Серия
		|ПОМЕСТИТЬ МножителиПродукции
		|ИЗ
		|	МножителиПолуфабрикатов КАК МножителиПолуфабрикатов
		|ГДЕ
		|	МножителиПолуфабрикатов.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МножителиПродукции.Калькуляция КАК Калькуляция,
		|	МножителиПродукции.Продукция КАК Продукция,
		|	МножителиПродукции.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	МножителиПродукции.Назначение КАК Назначение,
		|	МножителиПродукции.ПартияСпецификацияПродукции КАК ПартияСпецификацияПродукции,
		|	МатериальныеЗатраты.Номенклатура КАК Затрата,
		|	МатериальныеЗатраты.Характеристика КАК Характеристика,
		|	МатериальныеЗатраты.ЭтоПолуфабрикат КАК ЭтоПолуфабрикат,
		|	МатериальныеЗатраты.Количество * МножителиПродукции.Числитель / МножителиПродукции.Знаменатель КАК Количество,
		|	ВЫБОР
		|		КОГДА &ДанныеПоСебестоимости = 1
		|			ТОГДА МатериальныеЗатраты.Стоимость
		|		КОГДА &ДанныеПоСебестоимости = 2
		|			ТОГДА МатериальныеЗатраты.СтоимостьБезНДС
		|		КОГДА &ДанныеПоСебестоимости = 3
		|			ТОГДА МатериальныеЗатраты.СтоимостьБезНДС
		|		КОГДА &ДанныеПоСебестоимости = 4
		|			ТОГДА МатериальныеЗатраты.СтоимостьРегл
		|	КОНЕЦ * МножителиПродукции.Числитель / МножителиПродукции.Знаменатель КАК Стоимость,
		|	ВЫБОР
		|		КОГДА &ДанныеПоСебестоимости = 4
		|			ТОГДА МатериальныеЗатраты.СтоимостьЗабалансоваяРегл
		|		ИНАЧЕ МатериальныеЗатраты.СтоимостьЗабалансовая
		|	КОНЕЦ * МножителиПродукции.Числитель / МножителиПродукции.Знаменатель КАК СтоимостьЗабалансовая,
		|	МатериальныеЗатраты.Подразделение КАК Подразделение,
		|	МатериальныеЗатраты.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	МножителиПродукции.Серия КАК Серия
		|ПОМЕСТИТЬ ПлановыеЗатратыПродукции
		|ИЗ
		|	МножителиПродукции КАК МножителиПродукции
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПлановыеМатериальныеЗатраты КАК МатериальныеЗатраты
		|		ПО МножителиПродукции.КалькуляционнаяПартияПродукции = МатериальныеЗатраты.ПартияСпецификация
		|			И МножителиПродукции.Калькуляция = МатериальныеЗатраты.Калькуляция
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	МножителиПродукции.Калькуляция,
		|	МножителиПродукции.Продукция,
		|	МножителиПродукции.ХарактеристикаПродукции,
		|	МножителиПродукции.Назначение,
		|	МножителиПродукции.ПартияСпецификацияПродукции,
		|	ПлановыеТрудозатраты.ВидРабот,
		|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка),
		|	ЛОЖЬ,
		|	ПлановыеТрудозатраты.Количество * МножителиПродукции.Числитель / МножителиПродукции.Знаменатель,
		|	ВЫБОР
		|		КОГДА &ДанныеПоСебестоимости = 4
		|			ТОГДА ПлановыеТрудозатраты.СтоимостьРегл
		|		ИНАЧЕ ПлановыеТрудозатраты.Стоимость
		|	КОНЕЦ * МножителиПродукции.Числитель / МножителиПродукции.Знаменатель,
		|	0,
		|	ПлановыеТрудозатраты.Подразделение,
		|	ПлановыеТрудозатраты.СтатьяКалькуляции,
		|	МножителиПродукции.Серия
		|ИЗ
		|	МножителиПродукции КАК МножителиПродукции
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПлановыеТрудозатраты КАК ПлановыеТрудозатраты
		|		ПО МножителиПродукции.КалькуляционнаяПартияПродукции = ПлановыеТрудозатраты.ПартияСпецификация
		|			И МножителиПродукции.Калькуляция = ПлановыеТрудозатраты.Калькуляция
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	МножителиПродукции.Калькуляция,
		|	МножителиПродукции.Продукция,
		|	МножителиПродукции.ХарактеристикаПродукции,
		|	МножителиПродукции.Назначение,
		|	МножителиПродукции.ПартияСпецификацияПродукции,
		|	ПлановыеПрочиеЗатраты.СтатьяРасходов,
		|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка),
		|	ЛОЖЬ,
		|	0,
		|	ВЫБОР
		|		КОГДА &ДанныеПоСебестоимости = 1
		|			ТОГДА ПлановыеПрочиеЗатраты.Стоимость
		|		КОГДА &ДанныеПоСебестоимости = 2
		|			ТОГДА ПлановыеПрочиеЗатраты.СтоимостьБезНДС
		|		КОГДА &ДанныеПоСебестоимости = 3
		|			ТОГДА ПлановыеПрочиеЗатраты.СтоимостьБезНДС
		|		КОГДА &ДанныеПоСебестоимости = 4
		|			ТОГДА ПлановыеПрочиеЗатраты.СтоимостьРегл
		|	КОНЕЦ * МножителиПродукции.Числитель / МножителиПродукции.Знаменатель,
		|	0,
		|	ПлановыеПрочиеЗатраты.Подразделение,
		|	ПлановыеПрочиеЗатраты.СтатьяКалькуляции,
		|	МножителиПродукции.Серия
		|ИЗ
		|	МножителиПродукции КАК МножителиПродукции
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПлановыеПрочиеЗатраты КАК ПлановыеПрочиеЗатраты
		|		ПО МножителиПродукции.КалькуляционнаяПартияПродукции = ПлановыеПрочиеЗатраты.ПартияСпецификация
		|			И МножителиПродукции.Калькуляция = ПлановыеПрочиеЗатраты.Калькуляция
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МножителиПолуфабрикатов.Калькуляция КАК Калькуляция,
		|	МножителиПолуфабрикатов.Продукция КАК Продукция,
		|	МножителиПолуфабрикатов.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	МножителиПолуфабрикатов.Назначение КАК Назначение,
		|	МножителиПолуфабрикатов.ПартияСпецификацияПродукции КАК ПартияСпецификацияПродукции,
		|	МножителиПолуфабрикатов.Полуфабрикат КАК Полуфабрикат,
		|	МножителиПолуфабрикатов.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
		|	ПлановыеМатериальныеЗатраты.Номенклатура КАК Номенклатура,
		|	ПлановыеМатериальныеЗатраты.Характеристика КАК Характеристика,
		|	ПлановыеМатериальныеЗатраты.ЭтоПолуфабрикат КАК ЭтоПолуфабрикат,
		|	СУММА(ЕСТЬNULL(ПлановыеМатериальныеЗатраты.Количество, 0) * МножителиПолуфабрикатов.Числитель / МножителиПолуфабрикатов.Знаменатель) КАК Количество,
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 1
		|				ТОГДА ЕСТЬNULL(ПлановыеМатериальныеЗатраты.Стоимость, 0)
		|			КОГДА &ДанныеПоСебестоимости = 2
		|				ТОГДА ЕСТЬNULL(ПлановыеМатериальныеЗатраты.СтоимостьБезНДС, 0)
		|			КОГДА &ДанныеПоСебестоимости = 3
		|				ТОГДА ЕСТЬNULL(ПлановыеМатериальныеЗатраты.СтоимостьБезНДС, 0)
		|			КОГДА &ДанныеПоСебестоимости = 4
		|				ТОГДА ЕСТЬNULL(ПлановыеМатериальныеЗатраты.СтоимостьРегл, 0)
		|		КОНЕЦ * МножителиПолуфабрикатов.Числитель / МножителиПолуфабрикатов.Знаменатель) КАК Стоимость,
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 4
		|				ТОГДА ПлановыеМатериальныеЗатраты.СтоимостьЗабалансоваяРегл
		|			ИНАЧЕ ПлановыеМатериальныеЗатраты.СтоимостьЗабалансовая
		|		КОНЕЦ * МножителиПолуфабрикатов.Числитель / МножителиПолуфабрикатов.Знаменатель) КАК СтоимостьЗабалансовая,
		|	ПлановыеМатериальныеЗатраты.Подразделение КАК Подразделение,
		|	ПлановыеМатериальныеЗатраты.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	0 КАК Цена,
		|	МножителиПолуфабрикатов.Серия КАК Серия
		|ПОМЕСТИТЬ ПлановыеЗатратыПолуфабрикатов
		|ИЗ
		|	МножителиПолуфабрикатов КАК МножителиПолуфабрикатов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПлановыеМатериальныеЗатраты КАК ПлановыеМатериальныеЗатраты
		|		ПО МножителиПолуфабрикатов.Калькуляция = ПлановыеМатериальныеЗатраты.Калькуляция
		|			И МножителиПолуфабрикатов.ПартияСпецификацияПолуфабриката = ПлановыеМатериальныеЗатраты.ПартияСпецификация
		|ГДЕ
		|	НЕ МножителиПолуфабрикатов.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	МножителиПолуфабрикатов.Калькуляция,
		|	МножителиПолуфабрикатов.Продукция,
		|	МножителиПолуфабрикатов.ХарактеристикаПродукции,
		|	МножителиПолуфабрикатов.ПартияСпецификацияПродукции,
		|	МножителиПолуфабрикатов.Назначение,
		|	МножителиПолуфабрикатов.Полуфабрикат,
		|	МножителиПолуфабрикатов.ХарактеристикаПолуфабриката,
		|	ПлановыеМатериальныеЗатраты.Номенклатура,
		|	ПлановыеМатериальныеЗатраты.Характеристика,
		|	ПлановыеМатериальныеЗатраты.ЭтоПолуфабрикат,
		|	ПлановыеМатериальныеЗатраты.Подразделение,
		|	ПлановыеМатериальныеЗатраты.СтатьяКалькуляции,
		|	МножителиПолуфабрикатов.Серия
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	МножителиПолуфабрикатов.Калькуляция,
		|	МножителиПолуфабрикатов.Продукция,
		|	МножителиПолуфабрикатов.ХарактеристикаПродукции,
		|	МножителиПолуфабрикатов.Назначение,
		|	МножителиПолуфабрикатов.ПартияСпецификацияПродукции,
		|	МножителиПолуфабрикатов.Полуфабрикат,
		|	МножителиПолуфабрикатов.ХарактеристикаПолуфабриката,
		|	ПлановыеТрудозатраты.ВидРабот,
		|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка),
		|	ЛОЖЬ,
		|	СУММА(ЕСТЬNULL(ПлановыеТрудозатраты.Количество, 0) * МножителиПолуфабрикатов.Числитель / МножителиПолуфабрикатов.Знаменатель),
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 4
		|				ТОГДА ЕСТЬNULL(ПлановыеТрудозатраты.СтоимостьРегл, 0)
		|			ИНАЧЕ ЕСТЬNULL(ПлановыеТрудозатраты.Стоимость, 0)
		|		КОНЕЦ * МножителиПолуфабрикатов.Числитель / МножителиПолуфабрикатов.Знаменатель),
		|	0,
		|	ПлановыеТрудозатраты.Подразделение,
		|	ПлановыеТрудозатраты.СтатьяКалькуляции,
		|	0,
		|	МножителиПолуфабрикатов.Серия
		|ИЗ
		|	МножителиПолуфабрикатов КАК МножителиПолуфабрикатов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПлановыеТрудозатраты КАК ПлановыеТрудозатраты
		|		ПО МножителиПолуфабрикатов.Калькуляция = ПлановыеТрудозатраты.Калькуляция
		|			И МножителиПолуфабрикатов.ПартияСпецификацияПолуфабриката = ПлановыеТрудозатраты.ПартияСпецификация
		|ГДЕ
		|	НЕ МножителиПолуфабрикатов.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	МножителиПолуфабрикатов.Калькуляция,
		|	МножителиПолуфабрикатов.Продукция,
		|	МножителиПолуфабрикатов.ХарактеристикаПродукции,
		|	МножителиПолуфабрикатов.Назначение,
		|	МножителиПолуфабрикатов.ПартияСпецификацияПродукции,
		|	МножителиПолуфабрикатов.Полуфабрикат,
		|	МножителиПолуфабрикатов.ХарактеристикаПолуфабриката,
		|	ПлановыеТрудозатраты.ВидРабот,
		|	ПлановыеТрудозатраты.Подразделение,
		|	ПлановыеТрудозатраты.СтатьяКалькуляции,
		|	МножителиПолуфабрикатов.Серия
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	МножителиПолуфабрикатов.Калькуляция,
		|	МножителиПолуфабрикатов.Продукция,
		|	МножителиПолуфабрикатов.ХарактеристикаПродукции,
		|	МножителиПолуфабрикатов.Назначение,
		|	МножителиПолуфабрикатов.ПартияСпецификацияПродукции,
		|	МножителиПолуфабрикатов.Полуфабрикат,
		|	МножителиПолуфабрикатов.ХарактеристикаПолуфабриката,
		|	ПлановыеПрочиеЗатраты.СтатьяРасходов,
		|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка),
		|	ЛОЖЬ,
		|	0,
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 1
		|				ТОГДА ЕСТЬNULL(ПлановыеПрочиеЗатраты.Стоимость, 0)
		|			КОГДА &ДанныеПоСебестоимости = 2
		|				ТОГДА ЕСТЬNULL(ПлановыеПрочиеЗатраты.СтоимостьБезНДС, 0)
		|			КОГДА &ДанныеПоСебестоимости = 3
		|				ТОГДА ЕСТЬNULL(ПлановыеПрочиеЗатраты.СтоимостьБезНДС, 0)
		|			КОГДА &ДанныеПоСебестоимости = 4
		|				ТОГДА ЕСТЬNULL(ПлановыеПрочиеЗатраты.СтоимостьРегл, 0)
		|		КОНЕЦ * МножителиПолуфабрикатов.Числитель / МножителиПолуфабрикатов.Знаменатель),
		|	0,
		|	ПлановыеПрочиеЗатраты.Подразделение,
		|	ПлановыеПрочиеЗатраты.СтатьяКалькуляции,
		|	0,
		|	МножителиПолуфабрикатов.Серия
		|ИЗ
		|	МножителиПолуфабрикатов КАК МножителиПолуфабрикатов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПлановыеПрочиеЗатраты КАК ПлановыеПрочиеЗатраты
		|		ПО МножителиПолуфабрикатов.Калькуляция = ПлановыеПрочиеЗатраты.Калькуляция
		|			И МножителиПолуфабрикатов.ПартияСпецификацияПолуфабриката = ПлановыеПрочиеЗатраты.ПартияСпецификация
		|ГДЕ
		|	НЕ МножителиПолуфабрикатов.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	МножителиПолуфабрикатов.Калькуляция,
		|	МножителиПолуфабрикатов.Продукция,
		|	МножителиПолуфабрикатов.ХарактеристикаПродукции,
		|	МножителиПолуфабрикатов.Назначение,
		|	МножителиПолуфабрикатов.ПартияСпецификацияПродукции,
		|	МножителиПолуфабрикатов.Полуфабрикат,
		|	МножителиПолуфабрикатов.ХарактеристикаПолуфабриката,
		|	ПлановыеПрочиеЗатраты.СтатьяРасходов,
		|	ПлановыеПрочиеЗатраты.Подразделение,
		|	ПлановыеПрочиеЗатраты.СтатьяКалькуляции,
		|	МножителиПолуфабрикатов.Серия
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	МножителиПолуфабрикатов.Калькуляция,
		|	МножителиПолуфабрикатов.Продукция,
		|	МножителиПолуфабрикатов.ХарактеристикаПродукции,
		|	МножителиПолуфабрикатов.Назначение,
		|	МножителиПолуфабрикатов.ПартияСпецификацияПродукции,
		|	МножителиПолуфабрикатов.Полуфабрикат,
		|	МножителиПолуфабрикатов.ХарактеристикаПолуфабриката,
		|	ЗатратыВРазрезе.Затрата,
		|	ЗатратыВРазрезе.ХарактеристикаЗатраты,
		|	ЗатратыВРазрезе.ЭтоПолуфабрикат,
		|	ЗатратыВРазрезе.Количество,
		|	СУММА(ЗатратыВРазрезе.Стоимость),
		|	СУММА(ЗатратыВРазрезе.СтоимостьЗабалансовая),
		|	ЗатратыВРазрезе.Подразделение,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка),
		|	СРЕДНЕЕ(ЗатратыВРазрезе.Цена),
		|	МножителиПолуфабрикатов.Серия
		|ИЗ
		|	МножителиПолуфабрикатов КАК МножителиПолуфабрикатов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЗатратыВРазрезе КАК ЗатратыВРазрезе
		|		ПО МножителиПолуфабрикатов.Продукция = ЗатратыВРазрезе.Продукция
		|			И МножителиПолуфабрикатов.ХарактеристикаПродукции = ЗатратыВРазрезе.ХарактеристикаПродукции
		|			И МножителиПолуфабрикатов.Назначение = ЗатратыВРазрезе.Назначение
		|			И МножителиПолуфабрикатов.ПартияСпецификацияПродукции = ЗатратыВРазрезе.ПартияПродукции
		|			И МножителиПолуфабрикатов.Полуфабрикат = ЗатратыВРазрезе.Полуфабрикат
		|			И МножителиПолуфабрикатов.ХарактеристикаПолуфабриката = ЗатратыВРазрезе.ХарактеристикаПолуфабриката
		|			И МножителиПолуфабрикатов.Серия = ЗатратыВРазрезе.Серия
		|ГДЕ
		|	МножителиПолуфабрикатов.ПартияВыпущена
		|
		|СГРУППИРОВАТЬ ПО
		|	МножителиПолуфабрикатов.Калькуляция,
		|	МножителиПолуфабрикатов.Продукция,
		|	МножителиПолуфабрикатов.ХарактеристикаПродукции,
		|	МножителиПолуфабрикатов.Назначение,
		|	МножителиПолуфабрикатов.ПартияСпецификацияПродукции,
		|	МножителиПолуфабрикатов.Полуфабрикат,
		|	МножителиПолуфабрикатов.ХарактеристикаПолуфабриката,
		|	ЗатратыВРазрезе.Затрата,
		|	ЗатратыВРазрезе.ХарактеристикаЗатраты,
		|	ЗатратыВРазрезе.ЭтоПолуфабрикат,
		|	ЗатратыВРазрезе.Количество,
		|	ЗатратыВРазрезе.Подразделение,
		|	МножителиПолуфабрикатов.Серия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПлановыеЗатратыПродукции.Калькуляция КАК Калькуляция,
		|	ПлановыеЗатратыПродукции.Продукция КАК Продукция,
		|	ПлановыеЗатратыПродукции.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	ПлановыеЗатратыПродукции.Назначение КАК Назначение,
		|	ПлановыеЗатратыПродукции.ПартияСпецификацияПродукции КАК ПартияСпецификацияПродукции,
		|	ПлановыеЗатратыПродукции.Продукция КАК Полуфабрикат,
		|	ПлановыеЗатратыПродукции.ХарактеристикаПродукции КАК ХарактеристикаПолуфабриката,
		|	ПлановыеЗатратыПродукции.Затрата КАК Затрата,
		|	ПлановыеЗатратыПродукции.Характеристика КАК ХарактеристикаЗатраты,
		|	ПлановыеЗатратыПродукции.Количество КАК Количество,
		|	ВЫБОР
		|		КОГДА ПлановыеЗатратыПродукции.ЭтоПолуфабрикат
		|			ТОГДА 0
		|		ИНАЧЕ ПлановыеЗатратыПродукции.Стоимость
		|	КОНЕЦ КАК Стоимость,
		|	ВЫБОР
		|		КОГДА ПлановыеЗатратыПродукции.ЭтоПолуфабрикат
		|			ТОГДА 0
		|		ИНАЧЕ ПлановыеЗатратыПродукции.СтоимостьЗабалансовая
		|	КОНЕЦ КАК СтоимостьЗабалансовая,
		|	ВЫБОР
		|		КОГДА ПлановыеЗатратыПродукции.Количество = 0
		|			ТОГДА 0
		|		ИНАЧЕ ПлановыеЗатратыПродукции.Стоимость / ПлановыеЗатратыПродукции.Количество
		|	КОНЕЦ КАК ЦенаПлан,
		|	ПлановыеЗатратыПродукции.ЭтоПолуфабрикат КАК ЭтоПолуфабрикат,
		|	ПлановыеЗатратыПродукции.Подразделение КАК Подразделение,
		|	ПлановыеЗатратыПродукции.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	ПлановыеЗатратыПродукции.Серия КАК Серия
		|ПОМЕСТИТЬ ПлановыеДанные
		|ИЗ
		|	ПлановыеЗатратыПродукции КАК ПлановыеЗатратыПродукции
		|ГДЕ
		|	НЕ ПлановыеЗатратыПродукции.Затрата ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПлановыеЗатратыПолуфабрикатов.Калькуляция,
		|	ПлановыеЗатратыПолуфабрикатов.Продукция,
		|	ПлановыеЗатратыПолуфабрикатов.ХарактеристикаПродукции,
		|	ПлановыеЗатратыПолуфабрикатов.Назначение,
		|	ПлановыеЗатратыПолуфабрикатов.ПартияСпецификацияПродукции,
		|	ПлановыеЗатратыПолуфабрикатов.Полуфабрикат,
		|	ПлановыеЗатратыПолуфабрикатов.ХарактеристикаПолуфабриката,
		|	ПлановыеЗатратыПолуфабрикатов.Номенклатура,
		|	ПлановыеЗатратыПолуфабрикатов.Характеристика,
		|	ПлановыеЗатратыПолуфабрикатов.Количество,
		|	ВЫБОР
		|		КОГДА ПлановыеЗатратыПолуфабрикатов.ЭтоПолуфабрикат
		|			ТОГДА 0
		|		ИНАЧЕ ПлановыеЗатратыПолуфабрикатов.Стоимость
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ПлановыеЗатратыПолуфабрикатов.ЭтоПолуфабрикат
		|			ТОГДА 0
		|		ИНАЧЕ ПлановыеЗатратыПолуфабрикатов.СтоимостьЗабалансовая
		|	КОНЕЦ КАК СтоимостьЗабалансовая,
		|	ВЫБОР
		|		КОГДА ПлановыеЗатратыПолуфабрикатов.Количество = 0
		|				ИЛИ НЕ ПлановыеЗатратыПолуфабрикатов.Цена = 0
		|			ТОГДА ПлановыеЗатратыПолуфабрикатов.Цена
		|		ИНАЧЕ ПлановыеЗатратыПолуфабрикатов.Стоимость / ПлановыеЗатратыПолуфабрикатов.Количество
		|	КОНЕЦ,
		|	ПлановыеЗатратыПолуфабрикатов.ЭтоПолуфабрикат,
		|	ПлановыеЗатратыПолуфабрикатов.Подразделение,
		|	ПлановыеЗатратыПолуфабрикатов.СтатьяКалькуляции,
		|	ПлановыеЗатратыПолуфабрикатов.Серия
		|ИЗ
		|	ПлановыеЗатратыПолуфабрикатов КАК ПлановыеЗатратыПолуфабрикатов
		|ГДЕ
		|	НЕ ПлановыеЗатратыПолуфабрикатов.Номенклатура ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПлановыеДанные.Калькуляция КАК Калькуляция,
		|	ПлановыеДанные.Продукция КАК Продукция,
		|	ПлановыеДанные.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	ПлановыеДанные.Назначение КАК Назначение,
		|	ПлановыеДанные.ПартияСпецификацияПродукции КАК ПартияСпецификацияПродукции,
		|	ПлановыеДанные.Полуфабрикат КАК Полуфабрикат,
		|	ПлановыеДанные.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
		|	ПлановыеДанные.ЭтоПолуфабрикат КАК ЭтоПолуфабрикат,
		|	ПлановыеДанные.Серия КАК Серия,
		|	СРЕДНЕЕ(ПлановыеДанные.ЦенаПлан) КАК ЦенаПлан,
		|	ПлановыеДанные.Затрата КАК Затрата,
		|	ПлановыеДанные.ХарактеристикаЗатраты КАК ХарактеристикаЗатраты
		|ПОМЕСТИТЬ ПлановыеЦеныПолуфабрикатов
		|ИЗ
		|	ПлановыеДанные КАК ПлановыеДанные
		|
		|СГРУППИРОВАТЬ ПО
		|	ПлановыеДанные.Калькуляция,
		|	ПлановыеДанные.Продукция,
		|	ПлановыеДанные.ХарактеристикаПродукции,
		|	ПлановыеДанные.Назначение,
		|	ПлановыеДанные.ПартияСпецификацияПродукции,
		|	ПлановыеДанные.Полуфабрикат,
		|	ПлановыеДанные.ХарактеристикаПолуфабриката,
		|	ПлановыеДанные.ЭтоПолуфабрикат,
		|	ПлановыеДанные.Серия,
		|	ПлановыеДанные.Затрата,
		|	ПлановыеДанные.ХарактеристикаЗатраты
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Продукция,
		|	ХарактеристикаПродукции,
		|	Назначение,
		|	ПартияСпецификацияПродукции,
		|	Полуфабрикат,
		|	ХарактеристикаПолуфабриката,
		|	Серия,
		|	ЭтоПолуфабрикат,
		|	Калькуляция
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.Калькуляция КАК Калькуляция,
		|	ВложенныйЗапрос.Продукция КАК Продукция,
		|	ВложенныйЗапрос.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	ВложенныйЗапрос.Назначение КАК НазначениеПродукции,
		|	ВложенныйЗапрос.ПартияПродукции КАК ПартияПродукции,
		|	ВложенныйЗапрос.Полуфабрикат КАК Полуфабрикат,
		|	ВложенныйЗапрос.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
		|	ВложенныйЗапрос.Затрата КАК Затрата,
		|	ВложенныйЗапрос.ХарактеристикаЗатраты КАК ХарактеристикаЗатраты,
		|	СУММА(ВложенныйЗапрос.Стоимость) КАК Стоимость,
		|	СУММА(ВложенныйЗапрос.СтоимостьЗабалансовая) КАК СтоимостьЗабалансоваяФакт,
		|	СУММА(ВложенныйЗапрос.Количество) КАК Количество,
		|	СУММА(ВложенныйЗапрос.КоличествоПлан) КАК КоличествоПлан,
		|	СУММА(ВложенныйЗапрос.СтоимостьПлан) КАК СтоимостьПлан,
		|	СУММА(ВложенныйЗапрос.СтоимостьЗабалансоваяПлан) КАК СтоимостьЗабалансоваяПлан,
		|	МАКСИМУМ(ВложенныйЗапрос.ЦенаПлан) КАК ЦенаПлан,
		|	МАКСИМУМ(ВложенныйЗапрос.ЦенаФакт) КАК ЦенаФакт,
		|	ВложенныйЗапрос.ЭтоПолуфабрикат КАК ЭтоПолуфабрикат,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(ВложенныйЗапрос.Калькуляция) = ТИП(Документ.ПлановаяКалькуляция2_2)
		|				И НЕ ВложенныйЗапрос.Калькуляция = ЗНАЧЕНИЕ(Документ.ПлановаяКалькуляция2_2.ПустаяСсылка)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК КалькуляцияНайдена,
		|	ВложенныйЗапрос.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	ВложенныйЗапрос.Организация КАК Организация,
		|	ВложенныйЗапрос.Подразделение КАК Подразделение,
		|	ВложенныйЗапрос.ТипЗатрат КАК ТипЗатрат,
		|	ВложенныйЗапрос.Серия КАК Серия,
		|	МИНИМУМ(ВложенныйЗапрос.Уровень) КАК Уровень,
		|	ЕСТЬNULL(Номенклатура.ЕдиницаИзмерения, ЕСТЬNULL(ВидыРаботСотрудников.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка))) КАК ЕдиницаИзмеренияЗатраты
		|ИЗ
		|	(ВЫБРАТЬ
		|		ФактическиеДанные.Калькуляция КАК Калькуляция,
		|		ФактическиеДанные.Продукция КАК Продукция,
		|		ФактическиеДанные.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|		ФактическиеДанные.Назначение КАК Назначение,
		|		ФактическиеДанные.ПартияПродукции КАК ПартияПродукции,
		|		ФактическиеДанные.Полуфабрикат КАК Полуфабрикат,
		|		ФактическиеДанные.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
		|		ФактическиеДанные.Затрата КАК Затрата,
		|		ФактическиеДанные.ХарактеристикаЗатраты КАК ХарактеристикаЗатраты,
		|		СРЕДНЕЕ(ВЫРАЗИТЬ(ФактическиеДанные.Цена КАК ЧИСЛО(31,2))) КАК ЦенаФакт,
		|		СУММА(ВЫРАЗИТЬ(ФактическиеДанные.Стоимость КАК ЧИСЛО(31,2))) КАК Стоимость,
		|		СУММА(ВЫРАЗИТЬ(ФактическиеДанные.СтоимостьЗабалансовая КАК ЧИСЛО(31,2))) КАК СтоимостьЗабалансовая,
		|		СУММА(ВЫРАЗИТЬ(ФактическиеДанные.Количество КАК ЧИСЛО(15, 3))) КАК Количество,
		|		МАКСИМУМ(ВЫБОР
		|				КОГДА ТИПЗНАЧЕНИЯ(ФактическиеДанные.Затрата) = ТИП(ПланВидовХарактеристик.СтатьиРасходов)
		|					ТОГДА 0
		|				ИНАЧЕ ВЫРАЗИТЬ(ЕСТЬNULL(ПлановыеЦеныПолуфабрикатов.ЦенаПлан, 0) КАК ЧИСЛО(31,2))
		|			КОНЕЦ) КАК ЦенаПлан,
		|		0 КАК КоличествоПлан,
		|		0 КАК СтоимостьПлан,
		|		0 КАК СтоимостьЗабалансоваяПлан,
		|		ФактическиеДанные.ЭтоПолуфабрикат КАК ЭтоПолуфабрикат,
		|		ФактическиеДанные.Подразделение КАК Подразделение,
		|		ФактическиеДанные.Организация КАК Организация,
		|		ФактическиеДанные.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|		ВЫБОР
		|			КОГДА ТИПЗНАЧЕНИЯ(ФактическиеДанные.Затрата) = ТИП(Справочник.ВидыРаботСотрудников)
		|				ТОГДА ""Трудозатраты""
		|			КОГДА ТИПЗНАЧЕНИЯ(ФактическиеДанные.Затрата) = ТИП(ПланВидовХарактеристик.СтатьиРасходов)
		|				ТОГДА ""Постатейные""
		|			КОГДА ТИПЗНАЧЕНИЯ(ФактическиеДанные.Затрата) = ТИП(Справочник.Номенклатура)
		|					И ФактическиеДанные.ЭтоПолуфабрикат
		|				ТОГДА ""Полуфабрикат""
		|			ИНАЧЕ ""Материальные""
		|		КОНЕЦ КАК ТипЗатрат,
		|		ФактическиеДанные.Серия КАК Серия,
		|		ФактическиеДанные.Уровень КАК Уровень
		|	ИЗ
		|		ФактическиеДанные КАК ФактическиеДанные
		|			ЛЕВОЕ СОЕДИНЕНИЕ ПлановыеЦеныПолуфабрикатов КАК ПлановыеЦеныПолуфабрикатов
		|			ПО ФактическиеДанные.Продукция = ПлановыеЦеныПолуфабрикатов.Продукция
		|				И ФактическиеДанные.ХарактеристикаПродукции = ПлановыеЦеныПолуфабрикатов.ХарактеристикаПродукции
		|				И ФактическиеДанные.Назначение = ПлановыеЦеныПолуфабрикатов.Назначение
		|				И ФактическиеДанные.ПартияПродукции = ПлановыеЦеныПолуфабрикатов.ПартияСпецификацияПродукции
		|				И ФактическиеДанные.Полуфабрикат = ПлановыеЦеныПолуфабрикатов.Полуфабрикат
		|				И ФактическиеДанные.ХарактеристикаПолуфабриката = ПлановыеЦеныПолуфабрикатов.ХарактеристикаПолуфабриката
		|				И ФактическиеДанные.Серия = ПлановыеЦеныПолуфабрикатов.Серия
		|				И ФактическиеДанные.ЭтоПолуфабрикат = ПлановыеЦеныПолуфабрикатов.ЭтоПолуфабрикат
		|				И ФактическиеДанные.Калькуляция = ПлановыеЦеныПолуфабрикатов.Калькуляция
		|				И ФактическиеДанные.Затрата = ПлановыеЦеныПолуфабрикатов.Затрата
		|				И ФактическиеДанные.ХарактеристикаЗатраты = ПлановыеЦеныПолуфабрикатов.ХарактеристикаЗатраты
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ФактическиеДанные.Калькуляция,
		|		ФактическиеДанные.Продукция,
		|		ФактическиеДанные.ХарактеристикаПродукции,
		|		ФактическиеДанные.Назначение,
		|		ФактическиеДанные.ПартияПродукции,
		|		ФактическиеДанные.Полуфабрикат,
		|		ФактическиеДанные.ХарактеристикаПолуфабриката,
		|		ФактическиеДанные.Затрата,
		|		ФактическиеДанные.ХарактеристикаЗатраты,
		|		ФактическиеДанные.ЭтоПолуфабрикат,
		|		ФактическиеДанные.Подразделение,
		|		ФактическиеДанные.Организация,
		|		ФактическиеДанные.СтатьяКалькуляции,
		|		ВЫБОР
		|			КОГДА ТИПЗНАЧЕНИЯ(ФактическиеДанные.Затрата) = ТИП(Справочник.ВидыРаботСотрудников)
		|				ТОГДА ""Трудозатраты""
		|			КОГДА ТИПЗНАЧЕНИЯ(ФактическиеДанные.Затрата) = ТИП(ПланВидовХарактеристик.СтатьиРасходов)
		|				ТОГДА ""Постатейные""
		|			КОГДА ТИПЗНАЧЕНИЯ(ФактическиеДанные.Затрата) = ТИП(Справочник.Номенклатура)
		|					И ФактическиеДанные.ЭтоПолуфабрикат
		|				ТОГДА ""Полуфабрикат""
		|			ИНАЧЕ ""Материальные""
		|		КОНЕЦ,
		|		ФактическиеДанные.Серия,
		|		ФактическиеДанные.Уровень
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ПлановыеДанные.Калькуляция,
		|		ПлановыеДанные.Продукция,
		|		ПлановыеДанные.ХарактеристикаПродукции,
		|		ПлановыеДанные.Назначение,
		|		ПлановыеДанные.ПартияСпецификацияПродукции,
		|		ПлановыеДанные.Полуфабрикат,
		|		ПлановыеДанные.ХарактеристикаПолуфабриката,
		|		ПлановыеДанные.Затрата,
		|		ПлановыеДанные.ХарактеристикаЗатраты,
		|		0,
		|		0,
		|		0,
		|		0,
		|		ВЫРАЗИТЬ(ПлановыеДанные.ЦенаПлан КАК ЧИСЛО(31,2)),
		|		ВЫРАЗИТЬ(ПлановыеДанные.Количество КАК ЧИСЛО(15, 3)),
		|		ВЫРАЗИТЬ(ПлановыеДанные.Стоимость КАК ЧИСЛО(31,2)),
		|		ВЫРАЗИТЬ(ПлановыеДанные.СтоимостьЗабалансовая КАК ЧИСЛО(31,2)),
		|		ПлановыеДанные.ЭтоПолуфабрикат,
		|		ПлановыеДанные.Подразделение,
		|		ПлановыеДанные.Калькуляция.Организация,
		|		ПлановыеДанные.СтатьяКалькуляции,
		|		ВЫБОР
		|			КОГДА ТИПЗНАЧЕНИЯ(ПлановыеДанные.Затрата) = ТИП(Справочник.ВидыРаботСотрудников)
		|				ТОГДА ""Трудозатраты""
		|			КОГДА ТИПЗНАЧЕНИЯ(ПлановыеДанные.Затрата) = ТИП(ПланВидовХарактеристик.СтатьиРасходов)
		|				ТОГДА ""Постатейные""
		|			КОГДА ТИПЗНАЧЕНИЯ(ПлановыеДанные.Затрата) = ТИП(Справочник.Номенклатура)
		|					И ПлановыеДанные.ЭтоПолуфабрикат
		|				ТОГДА ""Полуфабрикат""
		|			ИНАЧЕ ""Материальные""
		|		КОНЕЦ,
		|		ПлановыеДанные.Серия,
		|		9999
		|	ИЗ
		|		ПлановыеДанные КАК ПлановыеДанные) КАК ВложенныйЗапрос
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура
		|		ПО ВложенныйЗапрос.Затрата = Номенклатура.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыРаботСотрудников КАК ВидыРаботСотрудников
		|		ПО ВложенныйЗапрос.Затрата = ВидыРаботСотрудников.Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.ХарактеристикаПродукции,
		|	ВложенныйЗапрос.Продукция,
		|	ВложенныйЗапрос.ХарактеристикаПолуфабриката,
		|	ВложенныйЗапрос.Назначение,
		|	ВложенныйЗапрос.Затрата,
		|	ВложенныйЗапрос.ПартияПродукции,
		|	ВложенныйЗапрос.Калькуляция,
		|	ВложенныйЗапрос.Полуфабрикат,
		|	ВложенныйЗапрос.ХарактеристикаЗатраты,
		|	ВложенныйЗапрос.ЭтоПолуфабрикат,
		|	ВложенныйЗапрос.СтатьяКалькуляции,
		|	ВложенныйЗапрос.Организация,
		|	ВложенныйЗапрос.Подразделение,
		|	ВложенныйЗапрос.ТипЗатрат,
		|	ВложенныйЗапрос.Серия,
		|	ЕСТЬNULL(Номенклатура.ЕдиницаИзмерения, ЕСТЬNULL(ВидыРаботСотрудников.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.Калькуляция КАК Калькуляция,
		|	ВложенныйЗапрос.Продукция КАК Продукция,
		|	ВложенныйЗапрос.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	ВложенныйЗапрос.Назначение КАК НазначениеПродукции,
		|	ВложенныйЗапрос.ПартияПродукции КАК ПартияПродукции,
		|	ВложенныйЗапрос.Полуфабрикат КАК Полуфабрикат,
		|	ВложенныйЗапрос.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
		|	СУММА(ВложенныйЗапрос.КоличествоВыпуск) КАК КоличествоВыпуск,
		|	&ПредставлениеФакта КАК ПредставлениеФакта,
		|	&ПредставлениеПлана КАК ПредставлениеПлана,
		|	МАКСИМУМ(ВложенныйЗапрос.КоличествоВыпускПлан) КАК КоличествоВыпускПлан,
		|	ВложенныйЗапрос.Серия КАК Серия
		|ИЗ
		|	(ВЫБРАТЬ
		|		КалькуляцииПродукцииИЗаказа.Калькуляция КАК Калькуляция,
		|		КоличествоВыпуска.Продукция КАК Продукция,
		|		КоличествоВыпуска.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|		КоличествоВыпуска.Назначение КАК Назначение,
		|		КоличествоВыпуска.ПартияПродукции КАК ПартияПродукции,
		|		КоличествоВыпуска.Полуфабрикат КАК Полуфабрикат,
		|		КоличествоВыпуска.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
		|		СУММА(ВЫРАЗИТЬ(КоличествоВыпуска.Количество КАК ЧИСЛО(15, 3))) КАК КоличествоВыпуск,
		|		0 КАК КоличествоВыпускПлан,
		|		КоличествоВыпуска.Серия КАК Серия
		|	ИЗ
		|		КоличествоВыпуска КАК КоличествоВыпуска
		|		ЛЕВОЕ СОЕДИНЕНИЕ КалькуляцииПродукцииИЗаказа КАК КалькуляцииПродукцииИЗаказа
		|		ПО КалькуляцииПродукцииИЗаказа.Продукция = КоличествоВыпуска.Продукция
		|			И КалькуляцииПродукцииИЗаказа.ХарактеристикаПродукции = КоличествоВыпуска.ХарактеристикаПродукции
		|			И КалькуляцииПродукцииИЗаказа.Партия = КоличествоВыпуска.ПартияПродукции
		|			И КалькуляцииПродукцииИЗаказа.Организация = КоличествоВыпуска.Организация
		|			И КалькуляцииПродукцииИЗаказа.Серия = КоличествоВыпуска.Серия
		|			И КалькуляцииПродукцииИЗаказа.НазначениеПродукции = КоличествоВыпуска.Назначение
		|	
		|	СГРУППИРОВАТЬ ПО
		|		КалькуляцииПродукцииИЗаказа.Калькуляция,
		|		КоличествоВыпуска.Продукция,
		|		КоличествоВыпуска.ХарактеристикаПродукции,
		|		КоличествоВыпуска.Назначение,
		|		КоличествоВыпуска.ПартияПродукции,
		|		КоличествоВыпуска.Полуфабрикат,
		|		КоличествоВыпуска.ХарактеристикаПолуфабриката,
		|		КоличествоВыпуска.Серия
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		МножителиПолуфабрикатов.Калькуляция,
		|		МножителиПолуфабрикатов.Продукция,
		|		МножителиПолуфабрикатов.ХарактеристикаПродукции,
		|		МножителиПолуфабрикатов.Назначение,
		|		МножителиПолуфабрикатов.ПартияСпецификацияПродукции,
		|		МножителиПолуфабрикатов.Полуфабрикат,
		|		МножителиПолуфабрикатов.ХарактеристикаПолуфабриката,
		|		0,
		|		СУММА(ВЫРАЗИТЬ(МножителиПолуфабрикатов.КоличествоПолуфабриката КАК ЧИСЛО(15, 3))),
		|		МножителиПолуфабрикатов.Серия
		|	ИЗ
		|		МножителиПолуфабрикатов КАК МножителиПолуфабрикатов
		|	ГДЕ
		|		НЕ МножителиПолуфабрикатов.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|		
		|	СГРУППИРОВАТЬ ПО
		|		МножителиПолуфабрикатов.Калькуляция,
		|		МножителиПолуфабрикатов.Продукция,
		|		МножителиПолуфабрикатов.ХарактеристикаПродукции,
		|		МножителиПолуфабрикатов.Назначение,
		|		МножителиПолуфабрикатов.ПартияСпецификацияПродукции,
		|		МножителиПолуфабрикатов.Полуфабрикат,
		|		МножителиПолуфабрикатов.ХарактеристикаПолуфабриката,
		|		МножителиПолуфабрикатов.Серия
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		МножителиПродукции.Калькуляция,
		|		МножителиПродукции.Продукция,
		|		МножителиПродукции.ХарактеристикаПродукции,
		|		МножителиПродукции.Назначение,
		|		МножителиПродукции.ПартияСпецификацияПродукции,
		|		МножителиПродукции.Продукция,
		|		МножителиПродукции.ХарактеристикаПродукции,
		|		0,
		|		СУММА(ВЫРАЗИТЬ(МножителиПродукции.КоличествоПолуфабриката КАК ЧИСЛО(15, 3))),
		|		МножителиПродукции.Серия
		|	ИЗ
		|		МножителиПродукции КАК МножителиПродукции
		|	
		|	СГРУППИРОВАТЬ ПО
		|		МножителиПродукции.Калькуляция,
		|		МножителиПродукции.Продукция,
		|		МножителиПродукции.ХарактеристикаПродукции,
		|		МножителиПродукции.Назначение,
		|		МножителиПродукции.ПартияСпецификацияПродукции,
		|		МножителиПродукции.Серия) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.ХарактеристикаПродукции,
		|	ВложенныйЗапрос.Продукция,
		|	ВложенныйЗапрос.ХарактеристикаПолуфабриката,
		|	ВложенныйЗапрос.Назначение,
		|	ВложенныйЗапрос.ПартияПродукции,
		|	ВложенныйЗапрос.Калькуляция,
		|	ВложенныйЗапрос.Полуфабрикат,
		|	ВложенныйЗапрос.Серия";
	
#КонецОбласти

	МассивРезультатов = Запрос.ВыполнитьПакет();
	ВнешниеНаборыДанных = Новый Структура();
	ВнешниеНаборыДанных.Вставить("Факт", МассивРезультатов[МассивРезультатов.Количество() - 2].Выгрузить());
	ВнешниеНаборыДанных.Вставить("Выпуски", МассивРезультатов[МассивРезультатов.Количество() - 1].Выгрузить());
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки, Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	
	ВывестиТекстПредупрежденияОбОграниченияхИспользованияОтчета(НачалоПериода,
		КонецПериода,
		ДокументРезультат);
		
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
			
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Настраивает параметры отчета по варианту отчета
//
// Параметры:
//	КлючВарианта						- Строка									- имя предопределенного или уникальный идентификатор пользовательского
//																						варианта отчета.
//										- Неопределено 								- вызов для варианта расшифровки или без контекста.
//	НовыеНастройкиКД					- НастройкиКомпоновкиДанных					- настройки варианта отчета, которые будут загружены
//																						в компоновщик настроек после его инициализации.
//										- Неопределено 								- настройки варианта не надо загружать (уже загружены ранее).
//	НовыеПользовательскиеНастройкиКД	- ПользовательскиеНастройкиКомпоновкиДанных - пользовательские настройки, которые будут загружены в компоновщик
//																						настроек после его инициализации.
//										- Неопределено 								- пользовательские настройки не надо загружать (уже загружены ранее).
//
Процедура НастроитьПараметрыОтчетаПоВариантуОтчета(КлючВарианта, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД)
	
	ПараметрДанныеПоСебестоимости = СхемаКомпоновкиДанных.Параметры.Найти("ДанныеПоСебестоимости");
	
	СписокВыбора = Новый СписокЗначений;
	
	Если КлючВарианта = "ПлановыеИФактическиеЗатратыПеределовПредприятия" Тогда
		
		СписокВыбора.Добавить(1, НСтр("ru = 'В валюте упр. учета с НДС';
										|en = 'In management accounting currency, including VAT'"));
		СписокВыбора.Добавить(2, НСтр("ru = 'В валюте упр. учета без НДС';
										|en = 'In management accounting currency, excluding VAT'"));
		
	ИначеЕсли КлючВарианта = "ПлановыеИФактическиеЗатратыПеределовОрганизации" Тогда	
		
		Если РасчетСебестоимостиПовтИсп.УправленческийУчетОрганизаций() Тогда
			СписокВыбора.Добавить(3, НСтр("ru = 'В валюте упр. учета';
											|en = 'In management accounting currency'"));
		КонецЕсли;
		СписокВыбора.Добавить(4, НСтр("ru = 'В валюте регл. учета';
										|en = 'In local accounting currency'"));
		
	Иначе
		
		СписокВыбора.Добавить(1, НСтр("ru = 'В валюте упр. учета с НДС';
										|en = 'In management accounting currency, including VAT'"));
		СписокВыбора.Добавить(2, НСтр("ru = 'В валюте упр. учета без НДС';
										|en = 'In management accounting currency, excluding VAT'"));
		СписокВыбора.Добавить(4, НСтр("ru = 'В валюте регл. учета';
										|en = 'In local accounting currency'"));
		
	КонецЕсли;
	
	ПараметрДанныеПоСебестоимости.УстановитьДоступныеЗначения(СписокВыбора);
	
	Если НовыеНастройкиКД = Неопределено
		Или НовыеПользовательскиеНастройкиКД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗначениеПараметраДанныеПоСебестоимости = НовыеНастройкиКД.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ДанныеПоСебестоимости"));
	НастройкаДанныеПоСебестоимости = НовыеПользовательскиеНастройкиКД.Элементы.Найти(ЗначениеПараметраДанныеПоСебестоимости.ИдентификаторПользовательскойНастройки);
	Если Не НастройкаДанныеПоСебестоимости = Неопределено
		И СписокВыбора.НайтиПоЗначению(НастройкаДанныеПоСебестоимости.Значение) = Неопределено Тогда
		НастройкаДанныеПоСебестоимости.Значение = СписокВыбора[0].Значение;
	КонецЕсли;
	
КонецПроцедуры

// Заполняет отбор в СКД получателе возможными отборами.
// 
// Параметры:
//	ПриемникНастроек - ОтборКомпоновкиДанных
//	ИсточникНастроек - ОтборКомпоновкиДанных
//	ВозможныеПоляУсловия - Массив - полей компоновки данных, на которые возможно наложить отбор в приемнике настроек.
//
Процедура ЗаполнитьНастройкиОтчетаРекурсивно(ПриемникНастроек, ИсточникНастроек, ВозможныеПоляУсловия)
	
	Для Каждого ЭлементОтбора Из ИсточникНастроек.Элементы Цикл
		
		Если Не ЭлементОтбора.Использование Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТипЗнч(ЭлементОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			
			НовыйЭлемент = ПриемникНастроек.Элементы.Добавить(ТипЗнч(ЭлементОтбора));
			ЗаполнитьЗначенияСвойств(НовыйЭлемент, ЭлементОтбора);
			ЗаполнитьНастройкиОтчетаРекурсивно(НовыйЭлемент, ЭлементОтбора, ВозможныеПоляУсловия);
			
		Иначе
			
			Если СтрНайти(ЭлементОтбора.ЛевоеЗначение, ".") = 0 И ВозможныеПоляУсловия.Найти(ЭлементОтбора.ЛевоеЗначение) = Неопределено
					Или СтрНайти(ЭлементОтбора.ЛевоеЗначение, ".") > 0 И ВозможныеПоляУсловия.Найти(
					Новый ПолеКомпоновкиДанных(Лев(ЭлементОтбора.ЛевоеЗначение, СтрНайти(ЭлементОтбора.ЛевоеЗначение, ".") - 1))) = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(ПриемникНастроек.Элементы.Добавить(ТипЗнч(ЭлементОтбора)), ЭлементОтбора);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура НастроитьПараметрыОтборыПоФункциональнымОпциям(КомпоновщикНастроекФормы)
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристикиНоменклатуры") Тогда
		КомпоновкаДанныхСервер.УдалитьЭлементОтбораИзВсехНастроекОтчета(КомпоновщикНастроекФормы, "ХарактеристикаПродукции");
	КонецЕсли;
	
	КомпоновкаДанныхСервер.ОчиститьСтруктуруПоФункциональнымОпциям(КомпоновщикНастроекФормы.Настройки.Структура, УдаляемыеПоляПоФО());
	
КонецПроцедуры

Функция УдаляемыеПоляПоФО()
	
	УдаляемыеПоля = Новый Массив;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристикиНоменклатуры") Тогда
		
		УдаляемыеПоля.Добавить(Новый ПолеКомпоновкиДанных("ХарактеристикаПродукции"));
		УдаляемыеПоля.Добавить(Новый ПолеКомпоновкиДанных("ХарактеристикаПолуфабриката"));
		УдаляемыеПоля.Добавить(Новый ПолеКомпоновкиДанных("ХарактеристикаЗатраты"));
		
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры") Тогда
		УдаляемыеПоля.Добавить(Новый ПолеКомпоновкиДанных("Серия"));
	КонецЕсли;
	
	Возврат УдаляемыеПоля;
	
КонецФункции

Процедура ВывестиТекстПредупрежденияОбОграниченияхИспользованияОтчета(НачалоПериода, КонецПериода, ДокументРезультат)
	
	ТекстПредупреждения = РасчетСебестоимостиПрикладныеАлгоритмы.ТекстПредупрежденияНеподдерживаемыеОрганизации(НачалоПериода, КонецПериода, КомпоновщикНастроек);
	Если Не ПустаяСтрока(ТекстПредупреждения) Тогда
		
		ТаблицаПредупреждение = Новый ТабличныйДокумент;
		ОбластьПредупреждение = ТаблицаПредупреждение.Область(1,1,1,1);
		
		ОбластьПредупреждение.Текст 	 = СокрЛП(ТекстПредупреждения);
		ОбластьПредупреждение.ЦветТекста = ЦветаСтиля.ЦветОтрицательногоЧисла;
		
		ДокументРезультат.ВставитьОбласть(
		ОбластьПредупреждение,
		ДокументРезультат.Область(1,1,1,1),
		ТипСмещенияТабличногоДокумента.ПоВертикали);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли