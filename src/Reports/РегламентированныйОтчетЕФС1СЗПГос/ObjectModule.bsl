#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область Переменные

Перем мПолноеИмяФайлаВнешнейОбработки Экспорт;

Перем мВерсияОтчета Экспорт;

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ФормированиеРегламентированногоОтчета

Функция Сформировать_АвтоматическиРеглОтчет(ПараметрыАвтоформирования, КорректирующийОтчет) Экспорт
	
	ИмяФормы = ПараметрыАвтоформирования.ИмяФормыОтчета;
	
	ПодготовленныйРеглОтчет = ПодготовленныйРеглОтчет(ИмяФормы, ПараметрыАвтоформирования, КорректирующийОтчет);
	
	РазделыОтчета       = ПодготовленныйРеглОтчет.РазделыОтчета;
	ДанныеРеглОтчета    = ПодготовленныйРеглОтчет.ДанныеРеглОтчета;
	ДеревоСтраницОтчета = ПодготовленныйРеглОтчет.ДеревоСтраницОтчета;
	
	ПараметрыЗаписи = Новый Структура;
	ПараметрыЗаписи.Вставить("ИсточникОтчета", ПараметрыАвтоформирования.ИсточникОтчета);
	ПараметрыЗаписи.Вставить("ИмяФормыОтчета", ПараметрыАвтоформирования.ИмяФормыОтчета);
	ПараметрыЗаписи.Вставить("Организация", ПараметрыАвтоформирования.Организация);
	ПараметрыЗаписи.Вставить("ДатаНачала", ПараметрыАвтоформирования.ДатаНачала);
	ПараметрыЗаписи.Вставить("ДатаОкончания", ПараметрыАвтоформирования.ДатаОкончания);
	ПараметрыЗаписи.Вставить("СохраняемыйРеглОтчет", ПараметрыАвтоформирования.СсылкаНаСохрРеглОтчет);
	ПараметрыЗаписи.Вставить("Ошибки", ПараметрыАвтоформирования.Ошибки);
	ПараметрыЗаписи.Вставить("ДатаПодписи", ДанныеРеглОтчета.ДатаПодписи);
	ПараметрыЗаписи.Вставить("ЕдиницаИзмерения", Перечисления.ПорядкиОкругленияОтчетности.Окр1);
	ПараметрыЗаписи.Вставить("ТочностьЕдиницыИзмерения", 0);
	ПараметрыЗаписи.Вставить("Комментарий", "");
	ПараметрыЗаписи.Вставить("СтруктураМногоуровневыхРазделов", ДанныеРеглОтчета.СтруктураМногоуровневыхРазделов);
	ПараметрыЗаписи.Вставить("СтруктураРеквизитовФормы", ДанныеРеглОтчета.СтруктураРеквизитовФормы);
	ПараметрыЗаписи.Вставить("ВерсияФормы", ДанныеРеглОтчета.СтруктураРеквизитовФормы.мВерсияФормы);
	ПараметрыЗаписи.Вставить("ФлОтклВыборЗначений", Ложь);
	ПараметрыЗаписи.Вставить("ФлПечататьСтраницыНаОтдельныхЛистах", Ложь);
	ПараметрыЗаписи.Вставить("Автозаполнение", Ложь);
	ПараметрыЗаписи.Вставить("ДеревоСтраницОтчета", ДеревоСтраницОтчета);
	ПараметрыЗаписи.Вставить("Вариант", ?(КорректирующийОтчет, 1, 0));
	ПараметрыЗаписи.Вставить("РазделыОтчета", РазделыОтчета);
	ПараметрыЗаписи.Вставить("НаименованиеОтчета", ДанныеРеглОтчета.СтруктураРеквизитовФормы.НаименованиеОтчета);
	
	Возврат ЗаписатьРеглОтчет(ИмяФормы, ПараметрыЗаписи);
	
КонецФункции

Функция ПодготовленныйРеглОтчет(ИмяФормы, ПараметрыАвтоформирования, КорректирующийОтчет)
	
	РазделыОтчета = РазделыРеглОтчета(ИмяФормы);
	
	ДанныеРеглОтчета = Новый Структура;
	ДанныеРеглОтчета.Вставить("ДатаПодписи", ТекущаядатаСеанса());
	
	ПараметрыАвтоФормирования.Вставить("ДатаПодписи", ДанныеРеглОтчета.ДатаПодписи);
	
	ПараметрыАвтоФормирования.Вставить("НомерКорректировки", ?(КорректирующийОтчет, 1, 0));
	
	ЗаполнитьРеглОтчет(ИмяФормы,
					   ПараметрыАвтоформирования,
					   КорректирующийОтчет,
					   РазделыОтчета,
					   ДанныеРеглОтчета);
		
	РазделыОтчета.Титульный.НомерКорректировки = ПараметрыАвтоФормирования.НомерКорректировки;
	
	РазделыОтчета.Титульный.КПП = ПараметрыАвтоформирования.Организация.КПП;
	
	РазделыОтчета.Титульный.ДатаПодписи = ДанныеРеглОтчета.ДатаПодписи;
	
	ДанныеРеглОтчета.СтруктураРеквизитовФормы.ВидДокумента = ?(КорректирующийОтчет, 1, 0);
	
	ЗаполнитьСведенияОбОрганизации(ИмяФормы,
								   ДанныеРеглОтчета.СтруктураРеквизитовФормы,
								   ДанныеРеглОтчета.ДатаПодписи,
								   РазделыОтчета.Титульный);
	
	ДеревоСтраницОтчета = СтруктураСтраницОтчета(ИмяФормы, ПараметрыАвтоформирования.ДатаОкончания);
	
	ДанныеРеглОтчета.Вставить("ДеревоСтраницОтчета", ДеревоСтраницОтчета);
	
	ПодготовленныйРеглОтчет = Новый Структура;
	ПодготовленныйРеглОтчет.Вставить("РазделыОтчета",       РазделыОтчета);
	ПодготовленныйРеглОтчет.Вставить("ДанныеРеглОтчета",    ДанныеРеглОтчета);
	ПодготовленныйРеглОтчет.Вставить("ДеревоСтраницОтчета", ДеревоСтраницОтчета);
	
	Возврат ПодготовленныйРеглОтчет;
	
КонецФункции

Процедура СформироватьСтруктуруРеквизитовФормы(ИмяФормы, СтруктураРеквизитовФормы) Экспорт
	
	СтруктураРеквизитовФормы = Новый Структура;
	
	СтруктураРеквизитовФормы.Вставить("мВерсияФормы", "01/01/" + Лев(Прав(СокрЛП(ИмяФормы), 7), 4));
	СтруктураРеквизитовФормы.Вставить("НаименованиеОтчета",
	"ЕФС-1: Сведения о заработной плате и условиях осуществления деятельности работников государственных (муниципальных) учреждений");
	СтруктураРеквизитовФормы.Вставить("ФлажокОтклАвтоРасчет", Ложь);
	СтруктураРеквизитовФормы.Вставить("ОтображатьКнопкуЗаполнить", Ложь);
	СтруктураРеквизитовФормы.Вставить("ОтображатьКнопкуРасшифровать", Ложь);
	СтруктураРеквизитовФормы.Вставить("АвтозаполняемыеПоказатели", Новый Структура);
	СтруктураРеквизитовФормы.Вставить("ВыводитьСтраницуВТаблДокНаСервере");
	СтруктураРеквизитовФормы.Вставить("НаимТекущегоРаздела");
	СтруктураРеквизитовФормы.Вставить("мАвтоВыборКодов");
	СтруктураРеквизитовФормы.Вставить("мБезОткрытияФормы");
	СтруктураРеквизитовФормы.Вставить("мЗаписываетсяНовыйДокумент");
	СтруктураРеквизитовФормы.Вставить("СписокПечатаемыхЛистов", Новый СписокЗначений);
	СтруктураРеквизитовФормы.Вставить("мПечатныеформы", Новый СписокЗначений);
	СтруктураРеквизитовФормы.Вставить("мРежимПечати");
	СтруктураРеквизитовФормы.Вставить("мСчетчикСтраниц", Ложь);
	СтруктураРеквизитовФормы.Вставить("мИдГруппыР113М1");
	СтруктураРеквизитовФормы.Вставить("мИдГруппыР113М2");
	СтруктураРеквизитовФормы.Вставить("мИдГруппыР113М3");
	СтруктураРеквизитовФормы.Вставить("мИдГруппыР113М4");
	СтруктураРеквизитовФормы.Вставить("мИдГруппыР113М5");
	СтруктураРеквизитовФормы.Вставить("мИдГруппыР113М6");
	СтруктураРеквизитовФормы.Вставить("ДлинаИмениГруппы");
	СтруктураРеквизитовФормы.Вставить("ДлинаИмениГрафы");
	СтруктураРеквизитовФормы.Вставить("ТочностьЕдиницыИзмерения");
	СтруктураРеквизитовФормы.Вставить("ВидДокумента");
	СтруктураРеквизитовФормы.Вставить("ЕдиницаИзмерения");
	СтруктураРеквизитовФормы.Вставить("мВариант");
	СтруктураРеквизитовФормы.Вставить("мСтрокаФормата");
	СтруктураРеквизитовФормы.Вставить("мВыбраннаяФорма");
	СтруктураРеквизитовФормы.Вставить("мДатаКонцаПериодаОтчета");
	СтруктураРеквизитовФормы.Вставить("мДатаНачалаПериодаОтчета");
	СтруктураРеквизитовФормы.Вставить("мПериодичность");
	СтруктураРеквизитовФормы.Вставить("мСкопированаФорма");
	СтруктураРеквизитовФормы.Вставить("мСохраненныйДок");
	СтруктураРеквизитовФормы.Вставить("Организация");
	СтруктураРеквизитовФормы.Вставить("ИП");
	СтруктураРеквизитовФормы.Вставить("НаимОрганизации");
	СтруктураРеквизитовФормы.Вставить("СтрПериодОтчета");
	СтруктураРеквизитовФормы.Вставить("ТекущаяСтрокаРазделовОтчета");
	СтруктураРеквизитовФормы.Вставить("НомераСтрокМногоуровнегоРаздела");
	СтруктураРеквизитовФормы.Вставить("СтруктураМногострочныхЧастей");
	СтруктураРеквизитовФормы.Вставить("ИдентификаторЗадания");
	СтруктураРеквизитовФормы.Вставить("РезультатЗапускаАвтозаполненияПриСоздании");
	СтруктураРеквизитовФормы.Вставить("АдресВоВременномХранилище");
	СтруктураРеквизитовФормы.Вставить("ТекстДляЗаголовочнойЧасти");
	СтруктураРеквизитовФормы.Вставить("СформироватьФормуОтчетаАвтоматически");
	СтруктураРеквизитовФормы.Вставить("КодОтчетногоПериода");
	
	СтруктураРеквизитовФормы.Вставить("ФлПечататьСтраницыНаОтдельныхЛистах", Ложь);
	
	Если ВРег(СокрЛП(ИмяФормы)) = ВРег("ФормаОтчета2024Кв1") Тогда
		
		СтруктураРеквизитовФормы.Вставить("Титульный_ИменаЗависимыхПоказателей", СтрРазделить(
		"РегНомер,НаимОрг,ИНН,КПП,ОКФС,ОКОГУ,ОКПО,ОКВЭД,ОГРН,КодКатСтрахФЛ,Телефон,ЭлПочта", ",", Ложь));
		
	Иначе// "ФормаОтчета2023Кв1"
		
		СтруктураРеквизитовФормы.Вставить("Титульный_ИменаЗависимыхПоказателей", СтрРазделить(
		"РегНомер,НаимОрг,ИНН,КПП,ОКФС,ОКОГУ,ОКПО,ОКВЭД,ОГРН,Телефон,ЭлПочта", ",", Ложь));
		
	КонецЕсли;
	
	// ПроцессыОбработкиДокументов
	//
	СтруктураРеквизитовФормы.Вставить("ПустаяСсылкаНаРегламентированныйОтчет",
	Документы.РегламентированныйОтчет.ПустаяСсылка());
	//
	// ПроцессыОбработкиДокументов
	
КонецПроцедуры

Процедура ИнициализироватьМногострочныеЧасти(ИмяФормы, СтруктураРеквизитовФормы) Экспорт
	
	// Свойства, общие для всех многострочных групп.
	СтруктураРеквизитовФормы.ДлинаИмениГруппы = 8;
	СтруктураРеквизитовФормы.ДлинаИмениГрафы  = 13;
	
	// Структура с описанием свойств каждой группы в отдельности.
	СтруктураМногострочныхЧастей = Новый Структура;
	
	// Идентификаторы многострочных групп.
	СтруктураРеквизитовФормы.мИдГруппыР113М1 = "П00113М1";
	СтруктураРеквизитовФормы.мИдГруппыР113М2 = "П00113М2";
	СтруктураРеквизитовФормы.мИдГруппыР113М3 = "П00113М3";
	СтруктураРеквизитовФормы.мИдГруппыР113М4 = "П00113М4";
	СтруктураРеквизитовФормы.мИдГруппыР113М5 = "П00113М5";
	СтруктураРеквизитовФормы.мИдГруппыР113М6 = "П00113М6";
	
	// Группа П00113М1.
	ИменаГраф = Новый Массив;
	ИменаГраф.Добавить("П00113М100001");
	ИменаГраф.Добавить("П00113М100002");
	ИменаГраф.Добавить("П00113М100003");
	ИменаГраф.Добавить("П00113М100004");
	ИменаГраф.Добавить("П00113М100005");
	ИменаГраф.Добавить("П00113М100006");
	ИменаГраф.Добавить("П00113М100007");
	ИменаГраф.Добавить("П00113М100008");
	ИменаПодчиненныхГрупп = Новый Массив;
	ИменаПодчиненныхГрупп.Добавить(СтруктураРеквизитовФормы.мИдГруппыР113М2);
	СтруктураМногострочныхЧастей.Вставить(СтруктураРеквизитовФормы.мИдГруппыР113М1, Новый Структура(
	"ИменаГраф,ИсхКолСтрок,УровеньПодчинения,ИменаПодчиненныхГрупп", ИменаГраф, 1, 0, ИменаПодчиненныхГрупп));
	
	// Группа П00113М2.
	ИменаГраф = Новый Массив;
	ИменаГраф.Добавить("П00113М200001");
	ИменаГраф.Добавить("П00113М200002");
	ИменаГраф.Добавить("П00113М200003");
	ИменаГраф.Добавить("П00113М200004");
	ИменаГраф.Добавить("П00113М200005");
	ИменаГраф.Добавить("П00113М200006");
	ИменаГраф.Добавить("П00113М200007");
	ИменаГраф.Добавить("П00113М200008");
	ИменаГраф.Добавить("П00113М200009");
	ИменаГраф.Добавить("П00113М200010");
	ИменаГраф.Добавить("П00113М200011");
	ИменаГраф.Добавить("П00113М200012");
	ИменаГраф.Добавить("П00113М200013");
	ИменаГраф.Добавить("П00113М200014");
	ИменаГраф.Добавить("П00113М200015");
	ИменаГраф.Добавить("П00113М200016");
	ИменаГраф.Добавить("П00113М200017");
	ИменаГраф.Добавить("П00113М200018");
	ИменаГраф.Добавить("П00113М200019");
	ИменаГраф.Добавить("П00113М200020");
	ИменаГраф.Добавить("П00113М200021");
	ИменаГраф.Добавить("П00113М200022");
	
	Если ВРег(СокрЛП(ИмяФормы)) = ВРег("ФормаОтчета2024Кв1") Тогда
		
		ИменаГраф.Добавить("П00113М200023");
		
	КонецЕсли;
	
	ИменаПодчиненныхГрупп = Новый Массив;
	ИменаПодчиненныхГрупп.Добавить(СтруктураРеквизитовФормы.мИдГруппыР113М3);
	ИменаПодчиненныхГрупп.Добавить(СтруктураРеквизитовФормы.мИдГруппыР113М4);
	ИменаПодчиненныхГрупп.Добавить(СтруктураРеквизитовФормы.мИдГруппыР113М5);
	ИменаПодчиненныхГрупп.Добавить(СтруктураРеквизитовФормы.мИдГруппыР113М6);
	СтруктураМногострочныхЧастей.Вставить(СтруктураРеквизитовФормы.мИдГруппыР113М2, Новый Структура(
	"ИменаГраф,ИсхКолСтрок,УровеньПодчинения,ИменаПодчиненныхГрупп", ИменаГраф, 1, 1, ИменаПодчиненныхГрупп));
	
	// Группа П00113М3.
	ИменаГраф = Новый Массив;
	ИменаГраф.Добавить("П00113М300001");
	ИменаГраф.Добавить("П00113М300002");
	ИменаПодчиненныхГрупп = Новый Массив;
	СтруктураМногострочныхЧастей.Вставить(СтруктураРеквизитовФормы.мИдГруппыР113М3, Новый Структура(
	"ИменаГраф,ИсхКолСтрок,УровеньПодчинения,ИменаПодчиненныхГрупп", ИменаГраф, 1, 2, ИменаПодчиненныхГрупп));
	
	// Группа П00113М4.
	ИменаГраф = Новый Массив;
	ИменаГраф.Добавить("П00113М400001");
	ИменаПодчиненныхГрупп = Новый Массив;
	СтруктураМногострочныхЧастей.Вставить(СтруктураРеквизитовФормы.мИдГруппыР113М4, Новый Структура(
	"ИменаГраф,ИсхКолСтрок,УровеньПодчинения,ИменаПодчиненныхГрупп", ИменаГраф, 1, 2, ИменаПодчиненныхГрупп));
	
	// Группа П00113М5.
	ИменаГраф = Новый Массив;
	ИменаГраф.Добавить("П00113М500001");
	ИменаГраф.Добавить("П00113М500002");
	ИменаПодчиненныхГрупп = Новый Массив;
	СтруктураМногострочныхЧастей.Вставить(СтруктураРеквизитовФормы.мИдГруппыР113М5, Новый Структура(
	"ИменаГраф,ИсхКолСтрок,УровеньПодчинения,ИменаПодчиненныхГрупп", ИменаГраф, 1, 2, ИменаПодчиненныхГрупп));
	
	// Группа П00113М6.
	ИменаГраф = Новый Массив;
	ИменаГраф.Добавить("П00113М600001");
	ИменаГраф.Добавить("П00113М600002");
	ИменаПодчиненныхГрупп = Новый Массив;
	СтруктураМногострочныхЧастей.Вставить(СтруктураРеквизитовФормы.мИдГруппыР113М6, Новый Структура(
	"ИменаГраф,ИсхКолСтрок,УровеньПодчинения,ИменаПодчиненныхГрупп", ИменаГраф, 1, 2, ИменаПодчиненныхГрупп));
	
	СтруктураРеквизитовФормы.СтруктураМногострочныхЧастей = СтруктураМногострочныхЧастей;
	
КонецПроцедуры

Процедура ИнициализироватьМногоуровневыеРазделы(ИмяФормы,
												СтруктураРеквизитовФормы,
												СтруктураМногоуровневыхРазделов) Экспорт
	
	СтруктураРеквизитовФормы.НомераСтрокМногоуровнегоРаздела = Новый Массив;
	
	// Формирование структуры свойств многоуровневых разделов.
	//
	СтруктураМногоуровневыхРазделов = Новый Структура;
	
	СтруктураРаздела = Новый Структура;
	СтруктураРаздела.Вставить("Многостраничность");
	ИменаГруппМнЧ = Новый Массив;
	ИменаГруппМнЧ.Добавить(СтруктураРеквизитовФормы.мИдГруппыР113М1);
	СтруктураРаздела.Вставить("ИменаГруппМнЧ", ИменаГруппМнЧ);
	СтруктураМногоуровневыхРазделов.Вставить("Раздел1", СтруктураРаздела);
	
	// Формирование начальной структуры дерева разделов
	// с заполнением каждого уровня пустыми данными.
	//
	ДеревоДанных = Новый ДеревоЗначений;
	ДеревоДанных.Колонки.Добавить("Данные");
	ДеревоДанных.Колонки.Добавить("ДанныеМногострочныхЧастей");
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	Для каждого Элем Из СтруктураМногоуровневыхРазделов Цикл
		Если Элем.Значение.Свойство("Подчиненность") Тогда
			Продолжить;
		КонецЕсли;
		
		ДеревоДанныхРаздела = ДеревоДанных.Скопировать();
		ТекВетка = ДеревоДанныхРаздела.Строки.Добавить();
		
		ТабличныйДокумент.Очистить();
		ТабличныйДокумент.Вывести(ЭтотОбъект.ПолучитьМакет(ИмяФормы + "_" + Элем.Ключ));
		
		ДанныеТаблДокумента = ДанныеТабличногоДокумента(ИмяФормы,
														Элем.Ключ,
														СтруктураМногоуровневыхРазделов,
														ТабличныйДокумент,
														СтруктураРеквизитовФормы);
		
		ТекВетка.Данные = ДанныеТаблДокумента.Данные;
		ТекВетка.ДанныеМногострочныхЧастей = ДанныеТаблДокумента.ДанныеМногострочныхЧастей;
		
		ИнициализироватьПодчиненныеРазделы(ИмяФормы,
										   Элем.Ключ,
										   ТекВетка,
										   СтруктураМногоуровневыхРазделов,
										   ТабличныйДокумент,
										   СтруктураРеквизитовФормы);
		
		Если НЕ СтруктураРеквизитовФормы.Свойство("АдресВоВрХранилищеДеревоДанных" + Элем.Ключ) Тогда
			СтруктураРеквизитовФормы.Вставить("АдресВоВрХранилищеДеревоДанных" + Элем.Ключ, Неопределено);
		КонецЕсли;
		
		РегламентированнаяОтчетность.ПоместитьВКэш(ДеревоДанныхРаздела, Новый УникальныйИдентификатор,
		СтруктураРеквизитовФормы["АдресВоВрХранилищеДеревоДанных" + Элем.Ключ]);
	КонецЦикла;
	
КонецПроцедуры

Функция ДанныеТабличногоДокумента(ИмяФормы,
								  НаимРаздела,
								  СтруктураМногоуровневыхРазделов,
								  ТабличныйДокумент,
								  СтруктураРеквизитовФормы) Экспорт
	
	ДанныеТаблДокумента = Новый Структура;
	
	// Многоуровневые разделы.
	//
	Если СтруктураМногоуровневыхРазделов.Свойство(НаимРаздела) Тогда
		
		СтруктураРаздела = СтруктураМногоуровневыхРазделов[НаимРаздела];
		
		ИменаГруппМнЧ = Неопределено;
		СтруктураРаздела.Свойство("ИменаГруппМнЧ", ИменаГруппМнЧ);
		РазделСодержитМнЧ = (ИменаГруппМнЧ <> Неопределено);
		
		СтруктураДанных = Новый Структура();
		Для Каждого ТекущаяОбласть Из ТабличныйДокумент.Области Цикл
			Если СтрЧислоВхождений(Тип(ТекущаяОбласть), "Рисунок") = 0
				И ТекущаяОбласть.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник
				И ТекущаяОбласть.СодержитЗначение = Истина Тогда
				Если РазделСодержитМнЧ Тогда
					Если РегламентированнаяОтчетностьКлиентСервер.МногострочнаяЧастьТекущейОбласти(СтруктураРеквизитовФормы,
																					ТекущаяОбласть.Имя) = Неопределено Тогда
						СтруктураДанных.Вставить(ТекущаяОбласть.Имя, ТекущаяОбласть.Значение);
					КонецЕсли;
				Иначе
					СтруктураДанных.Вставить(ТекущаяОбласть.Имя, ТекущаяОбласть.Значение);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		ДанныеТаблДокумента.Вставить("Данные", СтруктураДанных);
		
		СтруктураДанныхМногострочныхЧастей = Новый Структура;
		Если РазделСодержитМнЧ Тогда
			Для каждого ИмяГруппыМнЧ Из СтруктураРаздела["ИменаГруппМнЧ"] Цикл
				ЭлемСтруктуры = СтруктураРеквизитовФормы.СтруктураМногострочныхЧастей[ИмяГруппыМнЧ];
				Если ЭлемСтруктуры["УровеньПодчинения"] = 0 Тогда
					ДеревоДанных = Новый ДеревоЗначений;
					ДеревоДанных.Колонки.Добавить("Данные");
					ДеревоДанных.Колонки.Добавить("ДанныеМногострочныхЧастей");
					СформироватьДеревоМногострочнойЧастиПоДаннымТабличногоДокумента(
						ИмяФормы, СтруктураРеквизитовФормы, ТабличныйДокумент, ИмяГруппыМнЧ, ДеревоДанных);
					СтруктураДанныхМногострочныхЧастей.Вставить(ИмяГруппыМнЧ, ДеревоДанных);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		ДанныеТаблДокумента.Вставить("ДанныеМногострочныхЧастей", СтруктураДанныхМногострочныхЧастей);
		
	// Обычные разделы.
	//
	Иначе
		
		Для Инд = 0 По ТабличныйДокумент.Области.Количество() - 1 Цикл
			
			ТекОбласть = ТабличныйДокумент.Области[Инд];
			
			Если СтрЧислоВхождений(Тип(ТекОбласть), "Рисунок") = 0
				И ТекОбласть.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник
				И ТекОбласть.СодержитЗначение = Истина Тогда
				
				ДанныеТаблДокумента.Вставить(ТекОбласть.Имя, ТекОбласть.Значение);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ДанныеТаблДокумента;
	
КонецФункции

Процедура ИнициализироватьПодчиненныеРазделы(ИмяФормы,
											 Знач ИмяРаздела,
											 Ветка,
											 СтруктураМногоуровневыхРазделов,
											 ТабличныйДокумент,
											 СтруктураРеквизитовФормы) Экспорт
	
	Если СтруктураМногоуровневыхРазделов[ИмяРаздела].Свойство("ИменаПодчиненныхРазделов") Тогда
		
		ИменаПодчРазделов = СтруктураМногоуровневыхРазделов[ИмяРаздела]["ИменаПодчиненныхРазделов"];
		Для каждого ИмяПодчРаздела Из ИменаПодчРазделов Цикл
			ПодчВетка = Ветка.Строки.Добавить().Строки.Добавить();
			
			ТабличныйДокумент.Очистить();
			ТабличныйДокумент.Вывести(ЭтотОбъект.ПолучитьМакет(ИмяФормы + "_" + ИмяПодчРаздела));
			
			ДанныеТаблДокумента = ДанныеТабличногоДокумента(ИмяФормы,
															ИмяПодчРаздела,
															СтруктураМногоуровневыхРазделов,
															ТабличныйДокумент,
															СтруктураРеквизитовФормы);
									
			ПодчВетка.Данные = ДанныеТаблДокумента.Данные;
			ПодчВетка.ДанныеМногострочныхЧастей = ДанныеТаблДокумента.ДанныеМногострочныхЧастей;
			
			ИнициализироватьПодчиненныеРазделы(ИмяФормы,
											   ИмяПодчРаздела,
											   ПодчВетка,
											   СтруктураМногоуровневыхРазделов,
											   ТабличныйДокумент,
											   СтруктураРеквизитовФормы);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ИнициализироватьОбычныеРазделы(ИмяФормы,
										 СтруктураРеквизитовФормы,
										 СтруктураМногоуровневыхРазделов,
										 СтруктураДанныхРазделов,
										 МассивИменРазделов = Неопределено) Экспорт
	
	Если МассивИменРазделов = Неопределено Тогда
		
		МассивИменРазделов = Новый Массив;
		МассивИменРазделов.Добавить("Титульный");
		
	КонецЕсли;
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	Для Каждого ИмяРаздела Из МассивИменРазделов Цикл
		
		ТабличныйДокумент.Очистить();
		ТабличныйДокумент.Вывести(ЭтотОбъект.ПолучитьМакет(ИмяФормы + "_" + ИмяРаздела));
		
		СохранитьДанныеРаздела(ИмяФормы,
							   СтруктураРеквизитовФормы,
							   СтруктураМногоуровневыхРазделов,
							   СтруктураДанныхРазделов,
							   ТабличныйДокумент,
							   ИмяРаздела);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СохранитьДанныеРаздела(ИмяФормы,
								 СтруктураРеквизитовФормы,
								 СтруктураМногоуровневыхРазделов,
								 СтруктураДанныхРазделов,
								 ТабличныйДокумент,
								 НаимРаздела,
								 ВеткаРаздела = Неопределено) Экспорт
	
	// Многоуровневые разделы.
	Если СтруктураМногоуровневыхРазделов.Свойство(НаимРаздела) Тогда
		
		Если ВеткаРаздела = Неопределено Тогда
			
			СтруктураРаздела = СтруктураМногоуровневыхРазделов[НаимРаздела];
			
			ИмяГлРаздела = Неопределено;
			СтруктураРаздела.Свойство("ИмяГлавногоРаздела", ИмяГлРаздела);
			ИмяГлРаздела = ?(ИмяГлРаздела = Неопределено, НаимРаздела, ИмяГлРаздела);
			
			ДеревоДанныхРаздела = ПолучитьИзВременногоХранилища(
				СтруктураРеквизитовФормы["АдресВоВрХранилищеДеревоДанных" + ИмяГлРаздела]);
			
			ТекВетка = ДеревоДанныхРаздела;
			
			Для Каждого НомерСтроки Из СтруктураРеквизитовФормы.НомераСтрокМногоуровнегоРаздела Цикл
				ТекВетка = ТекВетка.Строки[НомерСтроки - 1];
			КонецЦикла;
			
		Иначе
			
			ТекВетка = ВеткаРаздела;
			
		КонецЕсли;
		
		ДанныеТаблДокумента = ДанныеТабличногоДокумента(ИмяФормы,
														НаимРаздела,
														СтруктураМногоуровневыхРазделов,
														ТабличныйДокумент,
														СтруктураРеквизитовФормы);
		
		ТекВетка.Данные = ДанныеТаблДокумента.Данные;
		ТекВетка.ДанныеМногострочныхЧастей = ДанныеТаблДокумента.ДанныеМногострочныхЧастей;
		
		РегламентированнаяОтчетность.ПоместитьВКэш(ДеревоДанныхРаздела, Новый УникальныйИдентификатор,
		СтруктураРеквизитовФормы["АдресВоВрХранилищеДеревоДанных" + ИмяГлРаздела]);
		
		ДеревоДанныхРаздела = Неопределено;
		
	// Обычные разделы.
	Иначе
		
		СтруктураДанныхРазделов["СтруктураДанных" + НаимРаздела]
			= ДанныеТабличногоДокумента(ИмяФормы,
										НаимРаздела,
		 						  		СтруктураМногоуровневыхРазделов,
										ТабличныйДокумент,
										СтруктураРеквизитовФормы);
		
	КонецЕсли;
	
КонецПроцедуры

Функция СтруктураСтраницОтчета(ИмяФормы, ДатаКонцаПериодаОтчета = Неопределено) Экспорт
	
	ДеревоСтраницОтчета = Новый ДеревоЗначений;
	
	ДеревоСтраницОтчета.Колонки.Добавить("Представление",
		Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(100)));
	ДеревоСтраницОтчета.Колонки.Добавить("ИмяСтраницы",
		Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(100)));
	ДеревоСтраницОтчета.Колонки.Добавить("ИмяПоляТабличногоДокум",
		Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(100)));
	ДеревоСтраницОтчета.Колонки.Добавить("ИмяОбластиТабличногоПоля",
		Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(100)));
	ДеревоСтраницОтчета.Колонки.Добавить("ОриентацияСтраницы");
	ДеревоСтраницОтчета.Колонки.Добавить("ВыводНаПечать",
		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(1, 0)));
	ДеревоСтраницОтчета.Колонки.Добавить("ПоказатьСтраницу",
		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(1, 0)));
	ДеревоСтраницОтчета.Колонки.Добавить("ВыгрузитьСтраницу",
		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(1, 0)));
	
	ДеревоСтраницОтчета.Строки.Очистить();
	
	СтрокаУровня1 = ДеревоСтраницОтчета.Строки.Добавить();
	СтрокаУровня1.ИмяСтраницы        = "Титульный";
	СтрокаУровня1.Представление      = "Титульный лист";
	СтрокаУровня1.ОриентацияСтраницы = "Портрет";
	СтрокаУровня1.ВыводНаПечать      = 1;
	СтрокаУровня1.ПоказатьСтраницу   = 1;
	СтрокаУровня1.ВыгрузитьСтраницу  = 2;
	
	СтрокаУровня1 = ДеревоСтраницОтчета.Строки.Добавить();
	СтрокаУровня1.ИмяСтраницы        = "Раздел1";
	СтрокаУровня1.Представление      = "Сведения";
	СтрокаУровня1.ОриентацияСтраницы = "Ландшафт";
	СтрокаУровня1.ВыводНаПечать      = 1;
	СтрокаУровня1.ПоказатьСтраницу   = 1;
	СтрокаУровня1.ВыгрузитьСтраницу  = 2;
	
	Возврат ДеревоСтраницОтчета;
	
КонецФункции

Функция СтруктураДанныхРазделов(ИмяФормы) Экспорт
	
	СтруктураДанныхРазделов = Новый Структура;
	
	СтруктураДанныхРазделов.Вставить("СтруктураДанныхТитульный", Новый Структура);
	
	Возврат СтруктураДанныхРазделов;
	
КонецФункции

Процедура ЗаполнитьЗначенияПоУмолчанию(ИмяФормы,
									   СтруктураРеквизитовФормы,
									   СтруктураДанныхРазделов) Экспорт
	
	СтруктураДанныхРазделов.СтруктураДанныхТитульный["ПризнакСтрахователя"] = "V";
	
КонецПроцедуры

Процедура ЗаполнитьКодОтчетногоПериода(ИмяФормы, СтруктураРеквизитовФормы) Экспорт
	
	Возврат;
	
КонецПроцедуры

Процедура ЗаполнитьСведенияОбОрганизации(ИмяФормы,
										 СтруктураРеквизитовФормы,
										 ДатаПодписи,
										 СтруктураДанныхТитульный,
										 СтруктураДанныхФизЛицо = Неопределено,
										 ВПрограммеИзмененаДатаПодписи = Ложь) Экспорт
	
	Если НЕ ВПрограммеИзмененаДатаПодписи Тогда
		
		СтрокаСведений = "РегНомерСФР, КодОрганаПФР, РегНомПФР, НаимЮЛПол, НаимЮЛСокр, ФИО, ИННЮЛ, КППЮЛ, ОКФС, ОКПО,"
		+ " ОКВЭД2, ОГРН, ТелОрганизации, АдресЭлектроннойПочтыОрганизации";
		
		СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(
		СтруктураРеквизитовФормы.Организация, ДатаПодписи, СтрокаСведений);
		
		Если Год(СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета) >= 2024
			И СведенияОбОрганизации.Свойство("РегНомерСФР")
			И ТипЗнч(СведенияОбОрганизации.РегНомерСФР) = Тип("Строка")
			И ЗначениеЗаполнено(СведенияОбОрганизации.РегНомерСФР) Тогда
			
			РегНом = Лев(СокрЛП(СведенияОбОрганизации.РегНомерСФР), 10);
			
		Иначе
			
			РегНом = СокрЛП(СведенияОбОрганизации.РегНомПФР);
			
			Если СтрДлина(РегНом) > 10 И СтрДлина(РегНом) < 14 Тогда
				Если Сред(РегНом, 4, 1) <> "-" Тогда
					РегНом = Лев(РегНом, 3) + "-" + Сред(РегНом, 4);
				КонецЕсли;
				Если Сред(РегНом, 8, 1) <> "-" Тогда
					РегНом = Лев(РегНом, 7) + "-" + Сред(РегНом, 8);
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
		Если СтрДлина(РегНом) = 10 И СтроковыеФункцииКлиентсервер.ТолькоЦифрыВСтроке(РегНом) Тогда
			
			КодОргана = "";
			
		Иначе
			
			КодОргана = СокрЛП(СведенияОбОрганизации.КодОрганаПФР);
			
			КодОргана = СтрЗаменить(КодОргана, "-", "");
			КодОргана = Лев(КодОргана, 6);
			
		КонецЕсли;
		
		Если СтруктураРеквизитовФормы.ИП Тогда
			НаименованиеОрганизации = СокрЛП(СведенияОбОрганизации.ФИО);
		Иначе
			Если ЗначениеЗаполнено(СведенияОбОрганизации.НаимЮЛСокр) Тогда
				НаименованиеОрганизации = СокрЛП(СведенияОбОрганизации.НаимЮЛСокр);
			Иначе
				НаименованиеОрганизации = СокрЛП(СведенияОбОрганизации.НаимЮлПол);
			КонецЕсли;
		КонецЕсли;
		
		ИНН   = СокрЛП(СведенияОбОрганизации.ИННЮЛ);
		КПП   = СокрЛП(СведенияОбОрганизации.КППЮЛ);
		ОКФС  = СокрЛП(СведенияОбОрганизации.ОКФС);
		ОКПО  = СокрЛП(СведенияОбОрганизации.ОКПО);
		ОКВЭД = СокрЛП(СведенияОбОрганизации.ОКВЭД2);
		ОГРН  = СокрЛП(СведенияОбОрганизации.ОГРН);
		
		Тел = СокрЛП(СведенияОбОрганизации.ТелОрганизации);
		Тел = ?(СтрНачинаетсяС(Тел, "+7"), "8" + Сред(Тел, 3), Тел);
		ТелДлина = СтрДлина(Тел);
		Телефон = "";
		Для Ном = 1 По ТелДлина Цикл
			ТекСимв = Сред(Тел, Ном, 1);
			Если СтроковыеФункцииКлиентсервер.ТолькоЦифрыВСтроке(ТекСимв) Тогда
				Телефон = Телефон + ТекСимв;
			КонецЕсли;
		КонецЦикла;
		Телефон = Лев(Телефон, 15);
		
		АдресЭлектроннойПочтыОрганизации = СокрЛП(СведенияОбОрганизации.АдресЭлектроннойПочтыОрганизации);
		
		Если ТипЗнч(СтруктураДанныхТитульный) = Тип("ТабличныйДокумент") Тогда
			
			ТДО = СтруктураДанныхТитульный.Области;
			
			ТДО.КодОргана.Значение = КодОргана;
			
			ТДО.Предст_РегНомер.Значение = РегНом;
			
			ТДО.Предст_НаимОрг.Значение = НаименованиеОрганизации;
			
			ТДО.Предст_ИНН.Значение   = ИНН;
			ТДО.Предст_КПП.Значение   = КПП;
			ТДО.Предст_ОКФС.Значение  = ОКФС;
			ТДО.Предст_ОКПО.Значение  = ОКПО;
			ТДО.Предст_ОКВЭД.Значение = ОКВЭД;
			ТДО.Предст_ОГРН.Значение  = ОГРН;
			
			ТДО.Предст_Телефон.Значение = Телефон;
			ТДО.Предст_ЭлПочта.Значение = АдресЭлектроннойПочтыОрганизации;
			
			ТДО.ДатаПодписи.Значение = ДатаПодписи;
			
		ИначеЕсли ТипЗнч(СтруктураДанныхТитульный) = Тип("Структура") Тогда
			
			СтруктураДанныхТитульный.КодОргана = КодОргана;
			
			СтруктураДанныхТитульный.Предст_РегНомер = РегНом;
			
			СтруктураДанныхТитульный.Предст_НаимОрг = НаименованиеОрганизации;
			
			СтруктураДанныхТитульный.Предст_ИНН   = ИНН;
			СтруктураДанныхТитульный.Предст_КПП   = КПП;
			СтруктураДанныхТитульный.Предст_ОКФС  = ОКФС;
			СтруктураДанныхТитульный.Предст_ОКПО  = ОКПО;
			СтруктураДанныхТитульный.Предст_ОКВЭД = ОКВЭД;
			СтруктураДанныхТитульный.Предст_ОГРН  = ОГРН;
			
			СтруктураДанныхТитульный.Предст_Телефон = Телефон;
			СтруктураДанныхТитульный.Предст_ЭлПочта = АдресЭлектроннойПочтыОрганизации;
			
			СтруктураДанныхТитульный.ДатаПодписи = ДатаПодписи;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаполнитьСведенияОПодписантах(ИмяФормы, СтруктураРеквизитовФормы, СтруктураДанныхТитульный, ДатаПодписи);
	
КонецПроцедуры

Процедура ЗаполнитьСведенияОПодписантах(ИмяФормы,
										СтруктураРеквизитовФормы,
										СтруктураДанныхТитульный,
										ДатаПодписи,
										СсылкаНаРегистрациюВНО = Неопределено) Экспорт
	
	Если РегламентированнаяОтчетностьВызовСервера.ЭтоЮридическоеЛицо(СтруктураРеквизитовФормы.Организация) Тогда
		
		СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(
		СтруктураРеквизитовФормы.Организация, ДатаПодписи, "ДолжнРук, ФамилияРук, ИмяРук, ОтчествоРук");
		
		Должн = СокрЛП(СведенияОбОрганизации.ДолжнРук);
		
		Фам = СокрЛП(СведенияОбОрганизации.ФамилияРук);
		Имя = СокрЛП(СведенияОбОрганизации.ИмяРук);
		Отч = СокрЛП(СведенияОбОрганизации.ОтчествоРук);
		
		
	Иначе
		
		СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(
		СтруктураРеквизитовФормы.Организация, ДатаПодписи, "ФамилияИП, ИмяИП, ОтчествоИП");
		
		Должн = "Индивидуальный предприниматель";
		
		Фам = СокрЛП(СведенияОбОрганизации.ФамилияИП);
		Имя = СокрЛП(СведенияОбОрганизации.ИмяИП);
		Отч = СокрЛП(СведенияОбОрганизации.ОтчествоИП);
		
	КонецЕсли;
	
	Если ТипЗнч(СтруктураДанныхТитульный) = Тип("ТабличныйДокумент") Тогда
		
		ТДО = СтруктураДанныхТитульный.Области;
		
		Если ЗначениеЗаполнено(ТДО.ПризнакСтрахователя.Значение) Тогда
			
			ТДО.Рук_Должность.Значение = Должн;
			
			ТДО.Рук_Фамилия.Значение = Фам;
			ТДО.Рук_Имя.Значение = Имя;
			ТДО.Рук_Отчество.Значение = Отч;
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(СтруктураДанныхТитульный) = Тип("Структура") Тогда
		
		Если ЗначениеЗаполнено(СтруктураДанныхТитульный.ПризнакСтрахователя) Тогда
			
			СтруктураДанныхТитульный.Рук_Должность = Должн;
			
			СтруктураДанныхТитульный.Рук_Фамилия = Фам;
			СтруктураДанныхТитульный.Рук_Имя = Имя;
			СтруктураДанныхТитульный.Рук_Отчество = Отч;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция РеквизитЭлементаСправочника(ИмяФормы, Ссылка) Экспорт
	
	Если Ссылка.Метаданные().Реквизиты.Найти("РегистрацияВНалоговомОргане") <> Неопределено Тогда
		Возврат Ссылка.РегистрацияВНалоговомОргане;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция РазделыРеглОтчета(ИмяФормы) Экспорт
	
	РазделыОтчета = Новый Структура;
	РазделыОтчета.Вставить("Титульный", Новый Структура);
	РазделыОтчета.Вставить("ДанныеМногоуровневыхРазделов", Новый Структура);
	
	Возврат РазделыОтчета;
	
КонецФункции

Функция ЗаписатьДокумент(ИмяФормы, ПараметрыЗаписи) Экспорт
	
	ДокументРегламентированныйОтчет = ПараметрыЗаписи.СохраняемыйРеглОтчет;
	
	ДокументРегламентированныйОтчет.ИсточникОтчета           = ПараметрыЗаписи.ИсточникОтчета;
	ДокументРегламентированныйОтчет.НаименованиеОтчета       = ПараметрыЗаписи.НаименованиеОтчета;
	ДокументРегламентированныйОтчет.ДатаНачала               = ПараметрыЗаписи.ДатаНачала;
	ДокументРегламентированныйОтчет.ДатаОкончания            = ПараметрыЗаписи.ДатаОкончания;
	ДокументРегламентированныйОтчет.Периодичность            = Перечисления.Периодичность.Квартал;
	ДокументРегламентированныйОтчет.ВыбраннаяФорма           = ПараметрыЗаписи.ИмяФормыОтчета;
	ДокументРегламентированныйОтчет.Организация              = ПараметрыЗаписи.Организация;
	ДокументРегламентированныйОтчет.ДатаПодписи              = ПараметрыЗаписи.ДатаПодписи;
	ДокументРегламентированныйОтчет.ЕдиницаИзмерения         = ПараметрыЗаписи.ЕдиницаИзмерения;
	ДокументРегламентированныйОтчет.ТочностьЕдиницыИзмерения = ПараметрыЗаписи.ТочностьЕдиницыИзмерения;
	ДокументРегламентированныйОтчет.ВидОтчетности            = Перечисления.ВидыОтчетности.РегламентированнаяОтчетность;
	ДокументРегламентированныйОтчет.Комментарий              = ПараметрыЗаписи.Комментарий;
	ДокументРегламентированныйОтчет.Вид                      = ПараметрыЗаписи.Вариант;
	ДокументРегламентированныйОтчет.ФорматВыгрузки           = "Автоматически";
	ДокументРегламентированныйОтчет.ПредставлениеВида
	= РегламентированнаяОтчетность.ПредставлениеВидаДокумента(ДокументРегламентированныйОтчет.Вид);
	
	ПоказателиОтчета = Новый Структура;
	ПоказателиОтчета.Вставить("ПолеТабличногоДокументаТитульный", ПараметрыЗаписи.РазделыОтчета.Титульный);
	
	СписокСохранения = Новый Структура;
	СписокСохранения.Вставить("ПоказателиОтчета", ПоказателиОтчета);
	
	// Вставляем данные многоуровневых разделов.
	ДанныеМногоуровневыхРазделов = Новый Структура;
	Для Каждого Элем Из ПараметрыЗаписи.СтруктураМногоуровневыхРазделов Цикл
		СтруктураРаздела = Элем.Значение;
		Если НЕ СтруктураРаздела.Свойство("Подчиненность") Тогда
			ДанныеМногоуровневыхРазделов.Вставить(Элем.Ключ, ПолучитьИзВременногоХранилища(
			ПараметрыЗаписи.СтруктураРеквизитовФормы["АдресВоВрХранилищеДеревоДанных" + Элем.Ключ]));
		КонецЕсли;
	КонецЦикла;
	СписокСохранения.Вставить("ДанныеМногоуровневыхРазделов",        ДанныеМногоуровневыхРазделов);
	СписокСохранения.Вставить("ВерсияФормы",                         ПараметрыЗаписи.ВерсияФормы);
	СписокСохранения.Вставить("ФлОтклВыборЗначений",                 ПараметрыЗаписи.ФлОтклВыборЗначений);
	СписокСохранения.Вставить("ФлПечататьСтраницыНаОтдельныхЛистах", ПараметрыЗаписи.ФлПечататьСтраницыНаОтдельныхЛистах);
	СписокСохранения.Вставить("Автозаполнение",                      ПараметрыЗаписи.Автозаполнение);
	
	ДокументРегламентированныйОтчет.ДанныеОтчета = Новый ХранилищеЗначения(СписокСохранения);
	
	ДокументРегламентированныйОтчет.ДеревоНастройкиСтраниц = Новый ХранилищеЗначения(ПараметрыЗаписи.ДеревоСтраницОтчета);
	
	Попытка
		
		ДокументРегламентированныйОтчет.Записать();
		
		ДокументРегламентированныйОтчет.Разблокировать();
		
	Исключение
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1';
																						|en = '%1'"),
		РегламентированнаяОтчетностьКлиентСервер.СформироватьТекстСообщения(ОписаниеОшибки()));
		
		РегламентированнаяОтчетность.ДобавитьОшибкуФормированияРеглОтчета(ПараметрыЗаписи, ТекстСообщения);
		
		ДокументРегламентированныйОтчет.Разблокировать();
		
		Возврат Ложь;
		
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

Процедура ИнициализироватьМногоуровневыйРаздел(ИмяФормы,
											   СтруктураРеквизитовФормы,
											   СтруктураМногоуровневыхРазделов,
											   ИмяРаздела) Экспорт
	
	УдалитьИзВременногоХранилища(СтруктураРеквизитовФормы["АдресВоВрХранилищеДеревоДанных" + ИмяРаздела]);
	
	ДеревоДанныхРаздела = Новый ДеревоЗначений;
	ДеревоДанныхРаздела.Колонки.Добавить("Данные");
	ДеревоДанныхРаздела.Колонки.Добавить("ДанныеМногострочныхЧастей");
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	ТекВетка = ДеревоДанныхРаздела.Строки.Добавить();
	
	ТабличныйДокумент.Очистить();
	ТабличныйДокумент.Вывести(ЭтотОбъект.ПолучитьМакет(ИмяФормы + "_" + ИмяРаздела));
	
	ДанныеТаблДокумента = ДанныеТабличногоДокумента(ИмяФормы,
													ИмяРаздела,
													СтруктураМногоуровневыхРазделов,
													ТабличныйДокумент,
													СтруктураРеквизитовФормы);
	
	ТекВетка.Данные = ДанныеТаблДокумента.Данные;
	ТекВетка.ДанныеМногострочныхЧастей = ДанныеТаблДокумента.ДанныеМногострочныхЧастей;
	
	ИнициализироватьПодчиненныеРазделы(ИмяФормы,
									   ИмяРаздела,
									   ТекВетка,
									   СтруктураМногоуровневыхРазделов,
									   ТабличныйДокумент,
									   СтруктураРеквизитовФормы);
	
	Если НЕ СтруктураРеквизитовФормы.Свойство("АдресВоВрХранилищеДеревоДанных" + ИмяРаздела) Тогда
		СтруктураРеквизитовФормы.Вставить("АдресВоВрХранилищеДеревоДанных" + ИмяРаздела, Неопределено);
	КонецЕсли;
	
	РегламентированнаяОтчетность.ПоместитьВКэш(ДеревоДанныхРаздела, Новый УникальныйИдентификатор,
	СтруктураРеквизитовФормы["АдресВоВрХранилищеДеревоДанных" + ИмяРаздела]);
	
КонецПроцедуры

Процедура СформироватьДеревоМногострочнойЧастиПоДаннымТабличногоДокумента(ИмяФормы,
																		  СтруктураРеквизитовФормы,
																		  ТабличныйДокумент,
																		  ИмяГруппы,
																		  ДеревоДанных,
																		  Координаты = "")
	
	Если НЕ СтруктураРеквизитовФормы.СтруктураМногострочныхЧастей.Свойство(ИмяГруппы) Тогда
		Возврат;
	КонецЕсли;
	
	МассивИменГраф = СтруктураРеквизитовФормы.СтруктураМногострочныхЧастей[ИмяГруппы]["ИменаГраф"];
	ИмяПервойГрафы = МассивИменГраф[0];
	
	Инд_Число = 1;
	Пока ТабличныйДокумент.Области.Найти(ИмяПервойГрафы + Координаты
		+ РегламентированнаяОтчетностьКлиентСервер.СтрокаСИндексомПоказателя(Инд_Число)) <> Неопределено Цикл
		НовВетка = ДеревоДанных.Строки.Добавить();
		
		// Заполнение данными из текущей строки многострочной части.
		Данные = Новый Структура;
		Инд = РегламентированнаяОтчетностьКлиентСервер.СтрокаСИндексомПоказателя(Инд_Число);
		Для каждого ИмяГрафы Из МассивИменГраф Цикл
			Обл = ТабличныйДокумент.Области.Найти(ИмяГрафы + Координаты + Инд);
			Если Обл <> Неопределено Тогда
				Данные.Вставить(ИмяГрафы, Обл.Значение);
			КонецЕсли;
		КонецЦикла;
		НовВетка.Данные = Данные;
		
		// Заполнение данными из строк многострочных частей, подчиненных текущей строке многострочной части.
		ДанныеМногострочныхЧастей = Новый Структура;
		ИменаПодчГрупп = СтруктураРеквизитовФормы.СтруктураМногострочныхЧастей[ИмяГруппы]["ИменаПодчиненныхГрупп"];
		Для каждого ИмяПодчГруппы Из ИменаПодчГрупп Цикл
			ДеревоПодчСтрок = Новый ДеревоЗначений;
			ДеревоПодчСтрок.Колонки.Добавить("Данные");
			ДеревоПодчСтрок.Колонки.Добавить("ДанныеМногострочныхЧастей");
			ПодчКоординаты = Координаты + Инд;
			СформироватьДеревоМногострочнойЧастиПоДаннымТабличногоДокумента(
			 ИмяФормы, СтруктураРеквизитовФормы, ТабличныйДокумент, ИмяПодчГруппы, ДеревоПодчСтрок, ПодчКоординаты);
			ДанныеМногострочныхЧастей.Вставить(ИмяПодчГруппы, ДеревоПодчСтрок);
		КонецЦикла;
		НовВетка.ДанныеМногострочныхЧастей = ДанныеМногострочныхЧастей;
		
		Инд_Число = Инд_Число + 1;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьРеглОтчет(ИмяФормы,
							 ПараметрыАвтоформирования,
							 КорректирующийОтчет,
							 РазделыОтчета,
							 ДанныеРеглОтчета)
	
	ИсточникОтчета = ПараметрыАвтоформирования.ИсточникОтчета;
	ИмяФормыОтчета = ПараметрыАвтоформирования.ИмяФормыОтчета;
	
	Организация = ПараметрыАвтоформирования.Организация;
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("ДатаНачала", ПараметрыАвтоформирования.ДатаНачала);
	ДопПараметры.Вставить("ДатаОкончания", ПараметрыАвтоформирования.ДатаОкончания);
	
	ДанныеРеглОтчета.Вставить("СтруктураРеквизитовФормы");
	ДанныеРеглОтчета.Вставить("СтруктураМногоуровневыхРазделов");
	
	СтруктураДанныхРазделов = СтруктураДанныхРазделов(ИмяФормы);
	
	СформироватьСтруктуруРеквизитовФормы(ИмяФормы, ДанныеРеглОтчета.СтруктураРеквизитовФормы);
	
	ЗаполнитьКодОтчетногоПериода(ИмяФормы, ДанныеРеглОтчета.СтруктураРеквизитовФормы);
	
	ИнициализироватьМногострочныеЧасти(ИмяФормы, ДанныеРеглОтчета.СтруктураРеквизитовФормы);
	
	ИнициализироватьМногоуровневыеРазделы(ИмяФормы,
										  ДанныеРеглОтчета.СтруктураРеквизитовФормы,
										  ДанныеРеглОтчета.СтруктураМногоуровневыхРазделов);
	
	ИнициализироватьОбычныеРазделы(ИмяФормы,
								   ДанныеРеглОтчета.СтруктураРеквизитовФормы,
								   ДанныеРеглОтчета.СтруктураМногоуровневыхРазделов,
								   СтруктураДанныхРазделов);
	
	ДанныеРеглОтчета.СтруктураРеквизитовФормы.Организация = ПараметрыАвтоформирования.Организация;
	
	ДанныеРеглОтчета.СтруктураРеквизитовФормы.ИП = НЕ РегламентированнаяОтчетностьВызовСервера.ЭтоЮридическоеЛицо(
	ДанныеРеглОтчета.СтруктураРеквизитовФормы.Организация);
	
	ДанныеРеглОтчета.СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета = ПараметрыАвтоформирования.ДатаОкончания;
	
	ЗаполнитьЗначенияПоУмолчанию(ИмяФормы, ДанныеРеглОтчета.СтруктураРеквизитовФормы, СтруктураДанныхРазделов);
	
	РазделыОтчета.Титульный = СтруктураДанныхРазделов.СтруктураДанныхТитульный;
	
	РегламентированнаяОтчетность.ПоместитьВКэш(Неопределено, Новый УникальныйИдентификатор,
	ДанныеРеглОтчета.СтруктураРеквизитовФормы.АдресВоВременномХранилище);
	
	ПараметрыОтчета = Новый Структура();
	ПараметрыОтчета.Вставить("Организация",             ПараметрыАвтоформирования.Организация);
	ПараметрыОтчета.Вставить("ДатаНачалаПериодаОтчета", ПараметрыАвтоформирования.ДатаНачала);
	ПараметрыОтчета.Вставить("ДатаКонцаПериодаОтчета",  ПараметрыАвтоформирования.ДатаОкончания);
	ПараметрыОтчета.Вставить("НомерКорректировки",      ПараметрыАвтоформирования.НомерКорректировки);
	ПараметрыОтчета.Вставить("ДатаПодписи",             ПараметрыАвтоформирования.ДатаПодписи);
	ПараметрыОтчета.Вставить("АдресВоВременномХранилище",
	ДанныеРеглОтчета.СтруктураРеквизитовФормы.АдресВоВременномХранилище);
	
	Контейнер = Новый Структура;
	Контейнер.Вставить("Титульный", РазделыОтчета.Титульный);
	
	Для Каждого Раздел Из ДанныеРеглОтчета.СтруктураМногоуровневыхРазделов Цикл
		
		Если НЕ Раздел.Значение.Свойство("Подчиненность") Тогда
			
			Контейнер.Вставить(Раздел.Ключ, ПолучитьИзВременногоХранилища(
			ДанныеРеглОтчета.СтруктураРеквизитовФормы["АдресВоВрХранилищеДеревоДанных" + Раздел.Ключ]));
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ПараметрыАвтоформирования.Свойство("ЗаполнятьВнешнимиДанными")
	   И ПараметрыАвтоформирования.ЗаполнятьВнешнимиДанными = Истина Тогда
		
		ЗаполнитьРеглОтчетВнешнимиДанными(ИмяФормы,
										  Контейнер,
										  ПараметрыАвтоформирования.ВнешниеДанные);
		
	Иначе
		
		РегламентированнаяОтчетностьПереопределяемый.ЗаполнитьОтчет(ИсточникОтчета,
																	ИмяФормыОтчета,
																	ПараметрыОтчета,
																	Контейнер);
		
		РассчитатьИтоги(
		ИмяФормы, Контейнер, ДанныеРеглОтчета.СтруктураРеквизитовФормы, ДанныеРеглОтчета.СтруктураМногоуровневыхРазделов);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьРеглОтчетВнешнимиДанными(ИмяФормы, Приемник, Источник)
	
	Возврат;
	
КонецПроцедуры

Процедура РассчитатьИтоги(ИмяФормы, Контейнер, СтруктураРеквизитовФормы, СтруктураМногоуровневыхРазделов)
	
	Возврат;
	
КонецПроцедуры

Функция ЗаписатьРеглОтчет(ИмяФормы, ПараметрыЗаписи)
	
	Если НЕ ПараметрыЗаписи.СохраняемыйРеглОтчет = Неопределено Тогда
		
		Если ТипЗнч(ПараметрыЗаписи.СохраняемыйРеглОтчет) = Тип("ДокументСсылка.РегламентированныйОтчет")
			И НЕ ПараметрыЗаписи.СохраняемыйРеглОтчет.Пустая() Тогда
			
			ДокументРегламентированныйОтчет = ПараметрыЗаписи.СохраняемыйРеглОтчет.ПолучитьОбъект();
			
			РегламентированнаяОтчетность.ЗаблокироватьДокумент(ДокументРегламентированныйОтчет);
			
		ИначеЕсли ТипЗнч(ПараметрыЗаписи.СохраняемыйРеглОтчет) = Тип("ДокументОбъект.РегламентированныйОтчет") Тогда
			
			ДокументРегламентированныйОтчет = ПараметрыЗаписи.СохраняемыйРеглОтчет;
			
			РегламентированнаяОтчетность.ЗаблокироватьДокумент(ДокументРегламентированныйОтчет);
			
		Иначе
			
			РегламентированнаяОтчетность.ДобавитьОшибкуФормированияРеглОтчета(ПараметрыЗаписи,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Неверный тип входящего параметра (%1) при автоматическом формировании формы %2';
				|en = 'Неверный тип входящего параметра (%1) при автоматическом формировании формы %2'"),
			ТипЗнч(ПараметрыЗаписи.СохраняемыйРеглОтчет), ПараметрыЗаписи.НаименованиеОтчета));
			
			Возврат Неопределено;
			
		КонецЕсли
		
	Иначе
		
		ДокументРегламентированныйОтчет = Документы.РегламентированныйОтчет.СоздатьДокумент();
		
		РегламентированнаяОтчетность.ЗаблокироватьДокумент(ДокументРегламентированныйОтчет);
		
		ДокументРегламентированныйОтчет.Дата = ПараметрыЗаписи.ДатаОкончания;
		ДокументРегламентированныйОтчет.УстановитьВремя();
		ДокументРегламентированныйОтчет.УстановитьНовыйНомер();
		
	КонецЕсли;
	
	ПараметрыЗаписи.СохраняемыйРеглОтчет = ДокументРегламентированныйОтчет;
	
	Если ЗаписатьДокумент(ИмяФормы, ПараметрыЗаписи) = Ложь Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ДокументРегламентированныйОтчет.Ссылка;
	
КонецФункции

#КонецОбласти

#Область ВыгрузкаРегламентированногоОтчета

Функция УниверсальнаяВыгрузка(Параметры) Экспорт
	
	Перем ДанныеОтчета;
	Перем СтруктураРеквизитовФормы;
	Перем СтруктураМногоуровневыхРазделов;
	
	СохраненныйОтчет  = Параметры.СохраненныйОтчет;
	ИсточникОтчета    = Параметры.ИсточникОтчета;
	ИмяФормы	      = Параметры.ВыбраннаяФорма;
	РезультатВыгрузки = Параметры.РезультатВыгрузки;
	
	СформироватьСтруктуруРеквизитовФормы(ИмяФормы, СтруктураРеквизитовФормы);
	
	ИнициализироватьМногострочныеЧасти(ИмяФормы, СтруктураРеквизитовФормы);
	
	ИнициализироватьМногоуровневыеРазделы(
	ИмяФормы, СтруктураРеквизитовФормы, СтруктураМногоуровневыхРазделов);
	
	СтруктураРеквизитовФормы.мСохраненныйДок = СохраненныйОтчет;
	
	СформироватьСтруктуруПоказателей(
	ИмяФормы, ДанныеОтчета, СтруктураРеквизитовФормы, СтруктураМногоуровневыхРазделов);
	
	ПараметрыВыгрузки = СформироватьСтруктуруПараметров(ИмяФормы, ДанныеОтчета, СтруктураРеквизитовФормы);
	
	ДеревоВыгрузки = ИзвлечьСтруктуруXML(ИмяФормы);
	
	Если НЕ ПроверитьВозможностьВыгрузки(ИмяФормы,
										 ПараметрыВыгрузки,
										 ДеревоВыгрузки,
										 РезультатВыгрузки,
										 СтруктураРеквизитовФормы,
										 СтруктураМногоуровневыхРазделов) Тогда
		
		РезультатВыгрузки.Вставить("РеглОтчетВыгружен", Ложь);
		
		Возврат РезультатВыгрузки;
		
	КонецЕсли;
	
	ЗаполнитьДанными(ИмяФормы,
					 ДанныеОтчета,
					 ПараметрыВыгрузки,
					 ДеревоВыгрузки,
					 РезультатВыгрузки,
					 СтруктураРеквизитовФормы,
					 СтруктураМногоуровневыхРазделов);
	
	КодировкаФайлаВыгрузки = "utf-8";
	
	ТекстФайлаВыгрузки = ВыгрузитьДеревоВXML(ИмяФормы,
											 ДеревоВыгрузки,
											 ПараметрыВыгрузки,
											 КодировкаФайлаВыгрузки,
											 СтруктураРеквизитовФормы,
											 СтруктураМногоуровневыхРазделов);
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("xml");
	ЗаписьТекста = Новый ЗаписьТекста(ИмяВременногоФайла, КодировкаФайлаВыгрузки);
	ЗаписьТекста.Записать(ТекстФайлаВыгрузки);
	ЗаписьТекста.Закрыть();
	ЗаписьТекста = Неопределено;
	
	ФайлВыгрузки = Новый ДвоичныеДанные(ИмяВременногоФайла);
	
	АдресВоВременномХранилище = ПоместитьВоВременноеХранилище(ФайлВыгрузки, Новый УникальныйИдентификатор);
	
	УдалитьФайлы(ИмяВременногоФайла);
	
	РезультатВыгрузки.Вставить("ИмяФайлаВыгрузки", ПараметрыВыгрузки.ИдФайл + ".xml");
	РезультатВыгрузки.Вставить("АдресФайлаВыгрузки", АдресВоВременномХранилище);
	РезультатВыгрузки.Вставить("КодировкаФайлаВыгрузки", КодировкаФайлаВыгрузки);
	РезультатВыгрузки.Вставить("ТипФайлаВыгрузки", "ЕФС1СЗПГос");
	РезультатВыгрузки.Вставить("РеглОтчетВыгружен", Истина);
	
	Возврат РезультатВыгрузки;
	
КонецФункции

Процедура СформироватьСтруктуруПоказателей(ИмяФормы,
										   ДанныеОтчета,
										   СтруктураРеквизитовФормы,
										   СтруктураМногоуровневыхРазделов) Экспорт
	
	ДанныеОтчета = Новый Структура;
	
	СохрСтруктура = СохраненныеДанныеОтчета(
	ИмяФормы, СтруктураРеквизитовФормы, СтруктураМногоуровневыхРазделов);
	
	ПоказателиОтчета = СохрСтруктура.ПоказателиОтчета;
	
	Для Каждого Раздел Из ПоказателиОтчета Цикл
		
		ДанныеОтчета.Вставить(СтрЗаменить(Раздел.Ключ, "ПолеТабличногоДокумента", ""),
		РегламентированнаяОтчетность.СвернутьДанныеТабличногоПоля(Раздел.Значение));
		
	КонецЦикла;
	
	Для Каждого Раздел Из СтруктураМногоуровневыхРазделов Цикл
		
		ДанныеОтчета.Вставить(Раздел.Ключ);
		
	КонецЦикла;
	
КонецПроцедуры

Функция СохраненныеДанныеОтчета(ИмяФормы,
								СтруктураРеквизитовФормы,
								СтруктураМногоуровневыхРазделов) Экспорт
	
	СохраненныеДанныеОтчета = СтруктураРеквизитовФормы.мСохраненныйДок.ДанныеОтчета.Получить();
	
	Если ВРег(СокрЛП(ИмяФормы)) = ВРег("ФормаОтчета2023Кв1") Тогда
		
		ДанныеТитЛиста = СохраненныеДанныеОтчета.ПоказателиОтчета.ПолеТабличногоДокументаТитульный;
		Если НЕ ДанныеТитЛиста.Свойство("КодОргана") Тогда
			ДанныеТитЛиста.Вставить("КодОргана", "");
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СохраненныеДанныеОтчета;
	
КонецФункции

Функция СформироватьСтруктуруПараметров(ИмяФормы,
										ДанныеОтчета,
										СтруктураРеквизитовФормы) Экспорт
	
	Титульный = ДанныеОтчета.Титульный;
	
	ПараметрыВыгрузки = Новый Структура;
	
	ПараметрыВыгрузки.Вставить("КодОргана", СокрЛП(Титульный.КодОргана));
	
	ПараметрыВыгрузки.Вставить("СтраховательПредставляетЗаСебя", ЗначениеЗаполнено(Титульный["ПризнакСтрахователя"]));
	
	Для каждого ИмяПок Из СтруктураРеквизитовФормы["Титульный_ИменаЗависимыхПоказателей"] Цикл
		ПараметрыВыгрузки.Вставить("Предст_" + ИмяПок, СокрЛП(Титульный["Предст_" + ИмяПок]));
		ПараметрыВыгрузки.Вставить(ИмяПок, ?(ПараметрыВыгрузки.СтраховательПредставляетЗаСебя, "", СокрЛП(Титульный[ИмяПок])));
	КонецЦикла;
	
	Если ПараметрыВыгрузки.СтраховательПредставляетЗаСебя Тогда
		
		ПараметрыВыгрузки.Вставить("ОГРН", "");
		ПараметрыВыгрузки.Вставить("ОГРНИП", "");
		
	Иначе
		
		ОГРН = СокрЛП(Титульный["ОГРН"]);
		Если СтрДлина(СокрЛП(Титульный["ИНН"])) = 12 Тогда
			ПараметрыВыгрузки.Вставить("ОГРН", "");
			ПараметрыВыгрузки.Вставить("ОГРНИП", ОГРН);
		Иначе
			ПараметрыВыгрузки.Вставить("ОГРН", ОГРН);
			ПараметрыВыгрузки.Вставить("ОГРНИП", "");
		КонецЕсли;
		
	КонецЕсли;
	
	Предст_ОГРН = СокрЛП(Титульный["Предст_ОГРН"]);
	Если СтрДлина(СокрЛП(Титульный["Предст_ИНН"])) = 12 Тогда
		ПараметрыВыгрузки.Вставить("Предст_ОГРН", "");
		ПараметрыВыгрузки.Вставить("Предст_ОГРНИП", Предст_ОГРН);
	Иначе
		ПараметрыВыгрузки.Вставить("Предст_ОГРН", Предст_ОГРН);
		ПараметрыВыгрузки.Вставить("Предст_ОГРНИП", "");
	КонецЕсли;
	
	ПараметрыВыгрузки.Вставить("Рук_Фамилия",  СокрЛП(Титульный.Рук_Фамилия));
	ПараметрыВыгрузки.Вставить("Рук_Имя",      СокрЛП(Титульный.Рук_Имя));
	ПараметрыВыгрузки.Вставить("Рук_Отчество", СокрЛП(Титульный.Рук_Отчество));
	
	ПараметрыВыгрузки.Вставить("Рук_Должность", СокрЛП(Титульный.Рук_Должность));
	
	ПараметрыВыгрузки.Вставить("ДатаЗаполнения", Формат(Титульный.ДатаПодписи, "ДФ=""гггг-ММ-дд"""));
	
	ПараметрыВыгрузки.Вставить("ГУИД", Строка(Новый УникальныйИдентификатор));
	
	ДатаВремя = ТекущаяДатаСеанса();
	Смещение_ЧетвертиЧаса = Окр(Число(ТекущаяДатаСеанса() - ТекущаяУниверсальнаяДата()) / 900);
	Смещение_Часы = Цел(Смещение_ЧетвертиЧаса / 4);
	ДобавкаЧасов = ?(Смещение_Часы >= 0, "+", "") + Формат(Смещение_Часы, "ЧГ=;ЧЦ=2;ЧВН=;ЧН=00");
	ДобавкаМинут = СтрЗаменить(Формат((Смещение_ЧетвертиЧаса - Смещение_Часы * 4) * 15, "ЧГ=;ЧЦ=2;ЧВН=;ЧН=00"), "-", "");
	ПараметрыВыгрузки.Вставить("ДатаВремя",
	Формат(ДатаВремя, "ДФ=""гггг-ММ-дд'T'ЧЧ:мм:сс""") + ДобавкаЧасов + ":" + ДобавкаМинут);
	
	ПараметрыВыгрузки.Вставить("ПрограммаПодготовки",
	РегламентированнаяОтчетность.НазваниеПрограммы() + " " + РегламентированнаяОтчетность.ВерсияПрограммы());
	
	ПараметрыВыгрузки.Вставить("ДатаПодписи", Титульный.ДатаПодписи);
	
	ПараметрыВыгрузки.Вставить("ИмяКлючевогоУзлаСодержательнойЧасти", "СЗВ");
	
	ПараметрыВыгрузки.Вставить("ИдФайл", ИдентификаторФайлаВыгрузки(ИмяФормы, ПараметрыВыгрузки));
	
	Возврат ПараметрыВыгрузки;
	
КонецФункции

Функция ИзвлечьСтруктуруXML(ИмяФормы) Экспорт
	
	ДеревоСтруктуры = Новый ДеревоЗначений;
	ДеревоСтруктуры.Колонки.Добавить("Код");
	ДеревоСтруктуры.Колонки.Добавить("Тип");
	ДеревоСтруктуры.Колонки.Добавить("Формат");
	ДеревоСтруктуры.Колонки.Добавить("МинРазмерность");
	ДеревоСтруктуры.Колонки.Добавить("МаксРазмерность");
	ДеревоСтруктуры.Колонки.Добавить("Обязательность");
	ДеревоСтруктуры.Колонки.Добавить("Многостраничность");
	ДеревоСтруктуры.Колонки.Добавить("Многострочность");
	ДеревоСтруктуры.Колонки.Добавить("Раздел");
	ДеревоСтруктуры.Колонки.Добавить("Ключ");
	ДеревоСтруктуры.Колонки.Добавить("Условие");
	ДеревоСтруктуры.Колонки.Добавить("ЗначениеПоУмолчанию");
	ДеревоСтруктуры.Колонки.Добавить("Значение");
	ДеревоСтруктуры.Колонки.Добавить("Представление");
	ДеревоСтруктуры.Колонки.Добавить("Показатели");
	
	Если ВРег(СокрЛП(ИмяФормы)) = ВРег("ФормаОтчета2024Кв1") Тогда
		
		Макет = ЭтотОбъект.ПолучитьМакет("СхемаВыгрузки2024Кв1");
		
	Иначе// "ФормаОтчета2023Кв1"
		
		Макет = ЭтотОбъект.ПолучитьМакет("СхемаВыгрузки2023Кв1");
		
	КонецЕсли;
	
	ВысотаТаблицы = Макет.ВысотаТаблицы;
	
	УчтенныеГруппы = Новый Соответствие;
	
	Для Уровень = 0 По Макет.КоличествоУровнейГруппировокСтрок() - 1 Цикл
		Макет.ПоказатьУровеньГруппировокСтрок(Уровень);
		Для НомерСтроки = 2 По ВысотаТаблицы Цикл
			НомСтр = ВысотаТаблицы - НомерСтроки + 2;
			Если Макет.Область(НомСтр, 0, НомСтр, 0).Видимость И УчтенныеГруппы.Получить(НомСтр) = Неопределено Тогда
				
				РодительскийУзел = ДеревоСтруктуры;
				Если Уровень <> 0 Тогда
					Для Инд = 1 По НомСтр - 2 Цикл
						Узел = УчтенныеГруппы.Получить(НомСтр - Инд);
						Если Узел <> Неопределено Тогда
							РодительскийУзел = Узел;
							Прервать;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
				
				НовСтр = РодительскийУзел.Строки.Вставить(0);
				НовСтр.Код = СокрЛП(Макет.Область(НомСтр, 1, НомСтр, 1).Текст);
				НовСтр.Раздел = СокрЛП(Макет.Область(НомСтр, 2, НомСтр, 2).Текст);
				НовСтр.Ключ = СокрЛП(Макет.Область(НомСтр, 3, НомСтр, 3).Текст);
				НовСтр.Тип = СокрЛП(Макет.Область(НомСтр, 4, НомСтр, 4).Текст);
				НовСтр.Формат = СокрЛП(Макет.Область(НомСтр, 5, НомСтр, 5).Текст);
				МинРазмерность = СокрЛП(Макет.Область(НомСтр, 6, НомСтр, 6).Текст);
				НовСтр.МинРазмерность = ?(ПустаяСтрока(МинРазмерность), ?(НовСтр.Формат = "N", 99999, 0), Число(МинРазмерность));
				МаксРазмерность = СокрЛП(Макет.Область(НомСтр, 7, НомСтр, 7).Текст);
				НовСтр.МаксРазмерность = ?(ПустаяСтрока(МаксРазмерность), 99999, Число(МаксРазмерность));
				НовСтр.Обязательность = СокрЛП(Макет.Область(НомСтр, 8, НомСтр, 8).Текст);
				НовСтр.Многостраничность = НЕ ПустаяСтрока(Макет.Область(НомСтр, 9, НомСтр, 9).Текст);
				НовСтр.Многострочность = НЕ ПустаяСтрока(Макет.Область(НомСтр, 10, НомСтр, 10).Текст);
				НовСтр.Условие = СокрЛП(Макет.Область(НомСтр, 11, НомСтр, 11).Текст);
				НовСтр.ЗначениеПоУмолчанию = СокрЛП(Макет.Область(НомСтр, 12, НомСтр, 12).Текст);
				НовСтр.Представление = СокрЛП(Макет.Область(НомСтр, 13, НомСтр, 13).Текст);
				
				УчтенныеГруппы.Вставить(НомСтр, НовСтр);
				
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат ДеревоСтруктуры;
	
КонецФункции

Функция ПроверитьВозможностьВыгрузки(ИмяФормы,
									 ПараметрыВыгрузки,
									 ДеревоВыгрузки,
									 РезультатВыгрузки,
									 СтруктураРеквизитовФормы,
									 СтруктураМногоуровневыхРазделов) Экспорт
	
	СтатусПроверки = Истина;
	
	// Сведения о страхователе.
	//
	НачСообщ = "В Сведениях о страхователе ";
	
	ТекстСообщ = "";
	Если НЕ ЗначениеЗаполнено(ПараметрыВыгрузки.Предст_РегНомер) Тогда
		СообщитьВТС(ИмяФормы, РезультатВыгрузки, НачСообщ + "не задан регистрационный номер", "Титульный", "Предст_РегНомер");
		СтатусПроверки = Ложь;
	ИначеЕсли НЕ РегистрационныйНомерСоответствуетТребованиям(ИмяФормы, ПараметрыВыгрузки.Предст_РегНомер, ТекстСообщ) Тогда
		СообщитьВТС(ИмяФормы, РезультатВыгрузки,
		НачСообщ + "задан некорректный регистрационный номер. " + ТекстСообщ, "Титульный", "Предст_РегНомер");
		СтатусПроверки = Ложь;
	ИначеЕсли СтрДлина(ПараметрыВыгрузки.Предст_РегНомер) = 14 Тогда
		Если НЕ ЗначениеЗаполнено(ПараметрыВыгрузки.КодОргана) Тогда
			СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Не задан код территориального органа, принимающего ЕФС-1",
			"Титульный", "КодОргана");
			СтатусПроверки = Ложь;
		ИначеЕсли СтрДлина(ПараметрыВыгрузки.КодОргана) <> 6
			ИЛИ НЕ СтроковыеФункцииКлиентсервер.ТолькоЦифрыВСтроке(ПараметрыВыгрузки.КодОргана) Тогда
			СообщитьВТС(ИмяФормы, РезультатВыгрузки,
			"Задан код территориального органа, принимающего ЕФС-1, не состоящий из 6 цифр", "Титульный", "КодОргана");
			СтатусПроверки = Ложь;
		КонецЕсли;
	ИначеЕсли СтрДлина(ПараметрыВыгрузки.Предст_РегНомер) = 10 Тогда
		Если ЗначениеЗаполнено(ПараметрыВыгрузки.КодОргана) И НЕ (СтрДлина(ПараметрыВыгрузки.КодОргана) = 6
			И СтроковыеФункцииКлиентсервер.ТолькоЦифрыВСтроке(ПараметрыВыгрузки.КодОргана)) Тогда
			СообщитьВТС(ИмяФормы, РезультатВыгрузки,
			"Задан код территориального органа, принимающего ЕФС-1, не состоящий из 6 цифр. "
			+ "Код территориального органа указывать необязательно, если регистрационный номер страхователя состоит из 10 цифр",
			"Титульный", "КодОргана");
			СтатусПроверки = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ПараметрыВыгрузки.Предст_НаимОрг) Тогда
		СообщитьВТС(ИмяФормы, РезультатВыгрузки, НачСообщ + "не задано наименование организации, обособленного "
		+ "подразделения/фамилия, имя, отчество (при наличии) индивидуального предпринимателя, физического лица)",
		"Титульный", "Предст_НаимОрг");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ПараметрыВыгрузки.Предст_ИНН) Тогда
		СообщитьВТС(ИмяФормы, РезультатВыгрузки, НачСообщ + "не задан ИНН", "Титульный", "Предст_ИНН");
		СтатусПроверки = Ложь;
	ИначеЕсли (СтрДлина(ПараметрыВыгрузки.Предст_ИНН) <> 10 И СтрДлина(ПараметрыВыгрузки.Предст_ИНН) <> 12)
		ИЛИ НЕ СтроковыеФункцииКлиентсервер.ТолькоЦифрыВСтроке(ПараметрыВыгрузки.Предст_ИНН) Тогда
		СообщитьВТС(ИмяФормы, РезультатВыгрузки, НачСообщ + "задан ИНН, не состоящий из 10 или 12 цифр",
		"Титульный", "Предст_ИНН");
		СтатусПроверки = Ложь;
	Иначе
		Если СтрДлина(ПараметрыВыгрузки.Предст_ИНН) = 10 Тогда
			Если НЕ ЗначениеЗаполнено(ПараметрыВыгрузки.Предст_КПП) Тогда
				СообщитьВТС(ИмяФормы, РезультатВыгрузки, НачСообщ + "не задан КПП", "Титульный", "Предст_КПП");
				СтатусПроверки = Ложь;
			ИначеЕсли СтрДлина(ПараметрыВыгрузки.Предст_КПП) <> 9 Тогда
				СообщитьВТС(ИмяФормы, РезультатВыгрузки, НачСообщ + "задан КПП, не состоящий из 9 символов",
				"Титульный", "Предст_КПП");
				СтатусПроверки = Ложь;
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(ПараметрыВыгрузки.Предст_ОГРН) Тогда
				СообщитьВТС(ИмяФормы, РезультатВыгрузки, НачСообщ + "не задан ОГРН. Если ОГРН отсутствует, укажите 0 (ноль)",
				"Титульный", "Предст_ОГРН");
				СтатусПроверки = Ложь;
			ИначеЕсли НЕ (ПараметрыВыгрузки.Предст_ОГРН = "0" ИЛИ (СтрДлина(ПараметрыВыгрузки.Предст_ОГРН) = 13
				И СтроковыеФункцииКлиентсервер.ТолькоЦифрыВСтроке(ПараметрыВыгрузки.Предст_ОГРН))) Тогда
				СообщитьВТС(ИмяФормы, РезультатВыгрузки, НачСообщ
				+ "задан ОГРН, не состоящий из 13 цифр. Если ОГРН отсутствует, укажите 0 (ноль)",
				"Титульный", "Предст_ОГРН");
				СтатусПроверки = Ложь;
			КонецЕсли;
		ИначеЕсли СтрДлина(ПараметрыВыгрузки.Предст_ИНН) = 12 Тогда
			Если ЗначениеЗаполнено(ПараметрыВыгрузки.Предст_КПП) Тогда
				СообщитьВТС(ИмяФормы, РезультатВыгрузки, НачСообщ + "КПП не должен быть задан при ИНН, состоящем из 12 цифр",
				"Титульный", "Предст_КПП");
				СтатусПроверки = Ложь;
			КонецЕсли;
			Если ЗначениеЗаполнено(ПараметрыВыгрузки.Предст_ОГРНИП) И (СтрДлина(ПараметрыВыгрузки.Предст_ОГРНИП) <> 15
				ИЛИ НЕ СтроковыеФункцииКлиентсервер.ТолькоЦифрыВСтроке(ПараметрыВыгрузки.Предст_ОГРНИП)) Тогда
				СообщитьВТС(ИмяФормы, РезультатВыгрузки, НачСообщ + "задан ОГРНИП, не состоящий из 15 цифр",
				"Титульный", "Предст_ОГРН");
				СтатусПроверки = Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ПараметрыВыгрузки.Предст_ОКФС) Тогда
		СообщитьВТС(ИмяФормы, РезультатВыгрузки, НачСообщ + "не задан ОКФС", "Титульный", "Предст_ОКФС");
		СтатусПроверки = Ложь;
	Иначе
		ВозмКоды = ВозможныеЗначенияИзСпискаВыбораПоказателя(ИмяФормы, "КодыПоОКФС");
		Если ВозмКоды.МассивЗначений <> Неопределено
			И ВозмКоды.МассивЗначений.Найти(ПараметрыВыгрузки.Предст_ОКФС) = Неопределено Тогда
			СообщитьВТС(ИмяФормы, РезультатВыгрузки,
			НачСообщ + "задан некорректный ОКФС. Возможны значения: " + ВозмКоды.СтрокаЗначений, "Титульный", "Предст_ОКФС");
			СтатусПроверки = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ПараметрыВыгрузки.Предст_ОКОГУ) Тогда
		СообщитьВТС(ИмяФормы, РезультатВыгрузки, НачСообщ + "не задан ОКОГУ", "Титульный", "Предст_ОКОГУ");
		СтатусПроверки = Ложь;
	ИначеЕсли СтрДлина(ПараметрыВыгрузки.Предст_ОКОГУ) <> 7
		ИЛИ НЕ СтроковыеФункцииКлиентсервер.ТолькоЦифрыВСтроке(ПараметрыВыгрузки.Предст_ОКОГУ) Тогда
		СообщитьВТС(ИмяФормы, РезультатВыгрузки, НачСообщ + "задан ОКОГУ, не состоящий из 7 цифр",
		"Титульный", "Предст_ОКОГУ");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ПараметрыВыгрузки.Предст_ОКПО) Тогда
		СообщитьВТС(ИмяФормы, РезультатВыгрузки, НачСообщ + "не задан ОКПО", "Титульный", "Предст_ОКПО");
		СтатусПроверки = Ложь;
	ИначеЕсли НЕ (СтрДлина(ПараметрыВыгрузки.Предст_ОКПО) = 8
		ИЛИ СтрДлина(ПараметрыВыгрузки.Предст_ОКПО) = 10 ИЛИ СтрДлина(ПараметрыВыгрузки.Предст_ОКПО) = 14)
		ИЛИ НЕ СтроковыеФункцииКлиентсервер.ТолькоЦифрыВСтроке(ПараметрыВыгрузки.Предст_ОКПО) Тогда
		СообщитьВТС(ИмяФормы, РезультатВыгрузки, НачСообщ + "задан ОКПО, не состоящий из 8, 10 или 14 цифр",
		"Титульный", "Предст_ОКПО");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыВыгрузки.Предст_ОКВЭД)
		И НЕ РегламентированнаяОтчетностьКлиентСервер.ОКВЭДСоответствуетТребованиям(ПараметрыВыгрузки.Предст_ОКВЭД) Тогда
		СообщитьВТС(ИмяФормы, РезультатВыгрузки, "задан код по ОКВЭД, не соответствующий одному из шаблонов: "
		+ """XX"", ""XX.X"", ""XX.XX"", ""XX.XX.X"", ""XX.XX.XX"" (X - любая цифра)", "Титульный", "Предст_ОКВЭД");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	Если ВРег(СокрЛП(ИмяФормы)) = ВРег("ФормаОтчета2024Кв1") Тогда
		
		ВозмКоды = ВозможныеЗначенияИзСпискаВыбораПоказателя(ИмяФормы, "КодыКатСтрахФЛ");
		
		Если СтрДлина(ПараметрыВыгрузки.Предст_ИНН) = 12 Тогда
			
			Если НЕ ЗначениеЗаполнено(ПараметрыВыгрузки.Предст_КодКатСтрахФЛ) Тогда
				СообщитьВТС(ИмяФормы, РезультатВыгрузки, НачСообщ
				+ "не задан код категории страхователя - физического лица", "Титульный", "Предст_КодКатСтрахФЛ");
				СтатусПроверки = Ложь;
			ИначеЕсли ВозмКоды.МассивЗначений <> Неопределено
				И ВозмКоды.МассивЗначений.Найти(ПараметрыВыгрузки.Предст_КодКатСтрахФЛ) = Неопределено Тогда
				СообщитьВТС(ИмяФормы, РезультатВыгрузки, НачСообщ
				+ "задан некорректный код категории страхователя - физического лица. Возможны значения: "
				+ ВозмКоды.СтрокаЗначений, "Титульный", "Предст_КодКатСтрахФЛ");
				СтатусПроверки = Ложь;
			КонецЕсли;
			
		Иначе
				
			Если ЗначениеЗаполнено(ПараметрыВыгрузки.Предст_КодКатСтрахФЛ)
				И ВозмКоды.МассивЗначений <> Неопределено
				И ВозмКоды.МассивЗначений.Найти(ПараметрыВыгрузки.Предст_КодКатСтрахФЛ) = Неопределено Тогда
				СообщитьВТС(ИмяФормы, РезультатВыгрузки, НачСообщ
				+ "задан некорректный код категории страхователя - физического лица. Возможны значения: "
				+ ВозмКоды.СтрокаЗначений, "Титульный", "Предст_КодКатСтрахФЛ");
				СтатусПроверки = Ложь;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыВыгрузки.Предст_Телефон)
		И НЕ СтроковыеФункцииКлиентсервер.ТолькоЦифрыВСтроке(ПараметрыВыгрузки.Предст_Телефон) Тогда
		СообщитьВТС(ИмяФормы, РезультатВыгрузки, НачСообщ + "задан номер контактного телефона, не состоящий только из цифр",
		"Титульный", "Предст_Телефон");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	ТекстСообщ = "";
	Если ЗначениеЗаполнено(ПараметрыВыгрузки.Предст_ЭлПочта)
		И НЕ АдресЭлектроннойПочтыСоответствуетТребованиям(ИмяФормы, ПараметрыВыгрузки.Предст_ЭлПочта, ТекстСообщ) Тогда
		СообщитьВТС(ИмяФормы, РезультатВыгрузки, НачСообщ + "задан некорректный адрес электронной почты. " + ТекстСообщ,
		"Титульный", "Предст_ЭлПочта");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	// Сведения о страхователе, за которого представляются сведения.
	//
	Если НЕ ПараметрыВыгрузки.СтраховательПредставляетЗаСебя Тогда
		
		НачСообщ = "В Сведениях о страхователе, за которого представляются сведения, ";
		
		ТекстСообщ = "";
		Если НЕ ЗначениеЗаполнено(ПараметрыВыгрузки.РегНомер) Тогда
			СообщитьВТС(ИмяФормы, РезультатВыгрузки, НачСообщ + "не задан регистрационный номер", "Титульный", "РегНомер");
			СтатусПроверки = Ложь;
		ИначеЕсли НЕ РегистрационныйНомерСоответствуетТребованиям(ИмяФормы, ПараметрыВыгрузки.РегНомер, ТекстСообщ) Тогда
			СообщитьВТС(ИмяФормы, РезультатВыгрузки,
			НачСообщ + "задан некорректный регистрационный номер. " + ТекстСообщ, "Титульный", "РегНомер");
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ПараметрыВыгрузки.НаимОрг) Тогда
			СообщитьВТС(ИмяФормы, РезультатВыгрузки, НачСообщ + "не задано наименование организации, обособленного "
			+ "подразделения/фамилия, имя, отчество (при наличии) индивидуального предпринимателя, физического лица)",
			"Титульный", "НаимОрг");
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ПараметрыВыгрузки.ИНН) Тогда
			СообщитьВТС(ИмяФормы, РезультатВыгрузки, НачСообщ + "не задан ИНН", "Титульный", "ИНН");
			СтатусПроверки = Ложь;
		ИначеЕсли (СтрДлина(ПараметрыВыгрузки.ИНН) <> 10 И СтрДлина(ПараметрыВыгрузки.ИНН) <> 12)
			ИЛИ НЕ СтроковыеФункцииКлиентсервер.ТолькоЦифрыВСтроке(ПараметрыВыгрузки.ИНН) Тогда
			СообщитьВТС(ИмяФормы, РезультатВыгрузки, НачСообщ + "задан ИНН, не состоящий из 10 или 12 цифр",
			"Титульный", "ИНН");
			СтатусПроверки = Ложь;
		Иначе
			Если СтрДлина(ПараметрыВыгрузки.ИНН) = 10 Тогда
				Если НЕ ЗначениеЗаполнено(ПараметрыВыгрузки.КПП) Тогда
					СообщитьВТС(ИмяФормы, РезультатВыгрузки, НачСообщ + "не задан КПП", "Титульный", "КПП");
					СтатусПроверки = Ложь;
				ИначеЕсли СтрДлина(ПараметрыВыгрузки.КПП) <> 9 Тогда
					СообщитьВТС(ИмяФормы, РезультатВыгрузки, НачСообщ + "задан КПП, не состоящий из 9 символов",
					"Титульный", "КПП");
					СтатусПроверки = Ложь;
				КонецЕсли;
				Если НЕ ЗначениеЗаполнено(ПараметрыВыгрузки.ОГРН) Тогда
					СообщитьВТС(ИмяФормы, РезультатВыгрузки, НачСообщ + "не задан ОГРН. Если ОГРН отсутствует, укажите 0 (ноль)",
					"Титульный", "ОГРН");
					СтатусПроверки = Ложь;
				ИначеЕсли НЕ (ПараметрыВыгрузки.ОГРН = "0" ИЛИ (СтрДлина(ПараметрыВыгрузки.ОГРН) = 13
					И СтроковыеФункцииКлиентсервер.ТолькоЦифрыВСтроке(ПараметрыВыгрузки.ОГРН))) Тогда
					СообщитьВТС(ИмяФормы, РезультатВыгрузки, НачСообщ
					+ "задан ОГРН, не состоящий из 13 цифр. Если ОГРН отсутствует, укажите 0 (ноль)",
					"Титульный", "ОГРН");
					СтатусПроверки = Ложь;
				КонецЕсли;
			ИначеЕсли СтрДлина(ПараметрыВыгрузки.ИНН) = 12 Тогда
				Если ЗначениеЗаполнено(ПараметрыВыгрузки.КПП) Тогда
					СообщитьВТС(ИмяФормы, РезультатВыгрузки, НачСообщ + "КПП не должен быть задан при ИНН, состоящем из 12 цифр",
					"Титульный", "КПП");
					СтатусПроверки = Ложь;
				КонецЕсли;
				Если ЗначениеЗаполнено(ПараметрыВыгрузки.ОГРНИП) И (СтрДлина(ПараметрыВыгрузки.ОГРНИП) <> 15
					ИЛИ НЕ СтроковыеФункцииКлиентсервер.ТолькоЦифрыВСтроке(ПараметрыВыгрузки.ОГРНИП)) Тогда
					СообщитьВТС(ИмяФормы, РезультатВыгрузки, НачСообщ + "задан ОГРНИП, не состоящий из 15 цифр",
					"Титульный", "ОГРН");
					СтатусПроверки = Ложь;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ПараметрыВыгрузки.ОКФС) Тогда
			СообщитьВТС(ИмяФормы, РезультатВыгрузки, НачСообщ + "не задан ОКФС", "Титульный", "ОКФС");
			СтатусПроверки = Ложь;
		Иначе
			ВозмКоды = ВозможныеЗначенияИзСпискаВыбораПоказателя(ИмяФормы, "КодыПоОКФС");
			Если ВозмКоды.МассивЗначений <> Неопределено
				И ВозмКоды.МассивЗначений.Найти(ПараметрыВыгрузки.ОКФС) = Неопределено Тогда
				СообщитьВТС(ИмяФормы, РезультатВыгрузки,
				НачСообщ + "задан некорректный ОКФС. Возможны значения: " + ВозмКоды.СтрокаЗначений, "Титульный", "ОКФС");
				СтатусПроверки = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ПараметрыВыгрузки.ОКОГУ) Тогда
			СообщитьВТС(ИмяФормы, РезультатВыгрузки, НачСообщ + "не задан ОКОГУ", "Титульный", "ОКОГУ");
			СтатусПроверки = Ложь;
		ИначеЕсли СтрДлина(ПараметрыВыгрузки.ОКОГУ) <> 7
			ИЛИ НЕ СтроковыеФункцииКлиентсервер.ТолькоЦифрыВСтроке(ПараметрыВыгрузки.ОКОГУ) Тогда
			СообщитьВТС(ИмяФормы, РезультатВыгрузки, НачСообщ + "задан ОКОГУ, не состоящий из 7 цифр",
			"Титульный", "ОКОГУ");
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ПараметрыВыгрузки.ОКПО) Тогда
			СообщитьВТС(ИмяФормы, РезультатВыгрузки, НачСообщ + "не задан ОКПО", "Титульный", "ОКПО");
			СтатусПроверки = Ложь;
		ИначеЕсли НЕ (СтрДлина(ПараметрыВыгрузки.ОКПО) = 8
			ИЛИ СтрДлина(ПараметрыВыгрузки.ОКПО) = 10 ИЛИ СтрДлина(ПараметрыВыгрузки.ОКПО) = 14)
			ИЛИ НЕ СтроковыеФункцииКлиентсервер.ТолькоЦифрыВСтроке(ПараметрыВыгрузки.ОКПО) Тогда
			СообщитьВТС(ИмяФормы, РезультатВыгрузки, НачСообщ + "задан ОКПО, не состоящий из 8, 10 или 14 цифр",
			"Титульный", "ОКПО");
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПараметрыВыгрузки.ОКВЭД)
			И НЕ РегламентированнаяОтчетностьКлиентСервер.ОКВЭДСоответствуетТребованиям(ПараметрыВыгрузки.ОКВЭД) Тогда
			СообщитьВТС(ИмяФормы, РезультатВыгрузки, "задан код по ОКВЭД, не соответствующий одному из шаблонов: "
			+ """XX"", ""XX.X"", ""XX.XX"", ""XX.XX.X"", ""XX.XX.XX"" (X - любая цифра)", "Титульный", "ОКВЭД");
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		Если ВРег(СокрЛП(ИмяФормы)) = ВРег("ФормаОтчета2024Кв1") Тогда
			
			ВозмКоды = ВозможныеЗначенияИзСпискаВыбораПоказателя(ИмяФормы, "КодыКатСтрахФЛ");
			
			Если СтрДлина(ПараметрыВыгрузки.ИНН) = 12 Тогда
				
				Если НЕ ЗначениеЗаполнено(ПараметрыВыгрузки.КодКатСтрахФЛ) Тогда
					СообщитьВТС(ИмяФормы, РезультатВыгрузки, НачСообщ
					+ "не задан код категории страхователя - физического лица", "Титульный", "КодКатСтрахФЛ");
					СтатусПроверки = Ложь;
				ИначеЕсли ВозмКоды.МассивЗначений <> Неопределено
					И ВозмКоды.МассивЗначений.Найти(ПараметрыВыгрузки.КодКатСтрахФЛ) = Неопределено Тогда
					СообщитьВТС(ИмяФормы, РезультатВыгрузки, НачСообщ
					+ "задан некорректный код категории страхователя - физического лица. Возможны значения: "
					+ ВозмКоды.СтрокаЗначений, "Титульный", "КодКатСтрахФЛ");
					СтатусПроверки = Ложь;
				КонецЕсли;
				
			Иначе
				
				Если ЗначениеЗаполнено(ПараметрыВыгрузки.КодКатСтрахФЛ)
					И ВозмКоды.МассивЗначений <> Неопределено
					И ВозмКоды.МассивЗначений.Найти(ПараметрыВыгрузки.КодКатСтрахФЛ) = Неопределено Тогда
					СообщитьВТС(ИмяФормы, РезультатВыгрузки, НачСообщ
					+ "задан некорректный код категории страхователя - физического лица. Возможны значения: "
					+ ВозмКоды.СтрокаЗначений, "Титульный", "КодКатСтрахФЛ");
					СтатусПроверки = Ложь;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПараметрыВыгрузки.Телефон)
			И НЕ СтроковыеФункцииКлиентсервер.ТолькоЦифрыВСтроке(ПараметрыВыгрузки.Телефон) Тогда
			СообщитьВТС(ИмяФормы, РезультатВыгрузки, НачСообщ + "задан номер контактного телефона, не состоящий только из цифр",
			"Титульный", "Телефон");
			СтатусПроверки = Ложь;
		КонецЕсли;
		
		ТекстСообщ = "";
		Если ЗначениеЗаполнено(ПараметрыВыгрузки.ЭлПочта)
			И НЕ АдресЭлектроннойПочтыСоответствуетТребованиям(ИмяФормы, ПараметрыВыгрузки.ЭлПочта, ТекстСообщ) Тогда
			СообщитьВТС(ИмяФормы, РезультатВыгрузки, НачСообщ + "задан некорректный адрес электронной почты. " + ТекстСообщ,
			"Титульный", "ЭлПочта");
			СтатусПроверки = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	//Сведения о руководителе (уполномоченном представителе) страхователя.
	//
	Если НЕ ЗначениеЗаполнено(ПараметрыВыгрузки.Рук_Должность) Тогда
		СообщитьВТС(ИмяФормы, РезультатВыгрузки,
		"Не задано наименование должности руководителя (уполномоченного представителя) страхователя",
		"Титульный", "Рук_Должность");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ПараметрыВыгрузки.Рук_Фамилия) И НЕ ЗначениеЗаполнено(ПараметрыВыгрузки.Рук_Имя) Тогда
		СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Не заданы фамилия и имя руководителя (уполномоченного представителя) "
		+ "страхователя. Должно быть указано хотя бы одно из значений: фамилия или имя", "Титульный", "Рук_Фамилия");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ПараметрыВыгрузки.ДатаПодписи) Тогда
		СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Не задана дата подписи", "Титульный", "ДатаПодписи");
		СтатусПроверки = Ложь;
	ИначеЕсли Год(ПараметрыВыгрузки.ДатаПодписи) < 2023 Тогда
		СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Дата подписи не должна быть ранее 2023 года", "Титульный", "ДатаПодписи");
		СтатусПроверки = Ложь;
	ИначеЕсли НачалоДня(ПараметрыВыгрузки.ДатаПодписи) > НачалоДня(ТекущаяДатаСеанса() + 86400) Тогда
		СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Дата подписи не должна быть позже текущей даты более чем на 1 день",
		"Титульный", "ДатаПодписи");
		СтатусПроверки = Ложь;
	КонецЕсли;
	
	// Раздел 1.
	//
	СохрСтруктура = СохраненныеДанныеОтчета(ИмяФормы, СтруктураРеквизитовФормы, СтруктураМногоуровневыхРазделов);
	ДеревоРаздела = СохрСтруктура.ДанныеМногоуровневыхРазделов["Раздел1"];
	СохрСтруктура = Неопределено;
	
	Если НЕ ЗаполненРаздел(ИмяФормы, ДеревоРаздела, "Раздел1",
		СтруктураРеквизитовФормы, СтруктураМногоуровневыхРазделов) Тогда
		
		СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Не заполнены сведения о трудовой (иной) деятельности, страховом стаже, "
		+ "заработной плате и дополнительных страховых взносах на накопительную пенсию", "Раздел1", "П000110000101", "1");
		СтатусПроверки = Ложь;
		
	Иначе
		
		// Подраздел 1 раздела 1.
		//
		Возм_СтатусыЗЛ = ВозможныеЗначенияИзСпискаВыбораПоказателя(ИмяФормы, "СтатусыЗЛ");
		Возм_Дни       = ВозможныеЗначенияИзСпискаВыбораПоказателя(ИмяФормы, "Дни");
		Возм_Месяцы    = ВозможныеЗначенияИзСпискаВыбораПоказателя(ИмяФормы, "Месяцы");
		Возм_КТО       = ВозможныеЗначенияИзСпискаВыбораПоказателя(ИмяФормы, "КодыТипаОрганизации");
		Возм_ТипыСвед  = ВозможныеЗначенияИзСпискаВыбораПоказателя(ИмяФормы, "ТипыСведений");
		Возм_КодыДолжн = ВозможныеЗначенияИзСпискаВыбораПоказателя(ИмяФормы, "КодыДолжности");
		Возм_КатегПерс = ВозможныеЗначенияИзСпискаВыбораПоказателя(ИмяФормы, "КодыКатегорийПерсонала");
		Возм_ВидыДог   = ВозможныеЗначенияИзСпискаВыбораПоказателя(ИмяФормы, "ВидыДоговора");
		Возм_СрокиДог  = ВозможныеЗначенияИзСпискаВыбораПоказателя(ИмяФормы, "СрокиДоговора");
		Возм_УченСтеп  = ВозможныеЗначенияИзСпискаВыбораПоказателя(ИмяФормы, "УченыеСтепени");
		Возм_УченЗван  = ВозможныеЗначенияИзСпискаВыбораПоказателя(ИмяФормы, "УченыеЗвания");
		Возм_ПочетЗван = ВозможныеЗначенияИзСпискаВыбораПоказателя(ИмяФормы, "ПочетныеЗвания");
		Возм_ПрофКвГр  = ВозможныеЗначенияИзСпискаВыбораПоказателя(ИмяФормы, "КодыПрофКвалифГруппы");
		Возм_КвалифУр  = ВозможныеЗначенияИзСпискаВыбораПоказателя(ИмяФормы, "КвалифУровни");
		Возм_СпОцУслТр = ВозможныеЗначенияИзСпискаВыбораПоказателя(ИмяФормы, "КодыСпецОценкиУслТруда");
		Возм_КвалКатАт = ВозможныеЗначенияИзСпискаВыбораПоказателя(ИмяФормы, "КвалифКатегПоИтАттестации");
		Возм_СистОплТр = ВозможныеЗначенияИзСпискаВыбораПоказателя(ИмяФормы, "СистемаОплатыТруда");
		Возм_КодыУслЗ  = ВозможныеЗначенияИзСпискаВыбораПоказателя(ИмяФормы, "КодыУсловийЗанятости");
		Возм_ЗнКодУслЗ = ВозможныеЗначенияИзСпискаВыбораПоказателя(ИмяФормы, "ЗначенияКодаУсловийЗанятости");
		Возм_КодыСпЧР  = ВозможныеЗначенияИзСпискаВыбораПоказателя(ИмяФормы, "КодыСпецЧасовРаботы");
		Возм_КодыВыпл  = ВозможныеЗначенияИзСпискаВыбораПоказателя(ИмяФормы, "КодыВыплат");
		
		Если ВРег(СокрЛП(ИмяФормы)) = ВРег("ФормаОтчета2024Кв1") Тогда
			
			Возм_ПрВклВРасчетСрЧисл = ВозможныеЗначенияИзСпискаВыбораПоказателя(ИмяФормы, "ПрВклВРасчетСрЧисл");
			
		КонецЕсли;
		
		ПредставленияСтраницСНеуникальнымиСНИЛС = Новый Массив;
		
		ТаблСНИЛС = Новый ТаблицаЗначений;
		ТаблСНИЛС.Колонки.Добавить("СНИЛС");
		ТаблСНИЛС.Колонки.Добавить("ИндексСтраницы");
		ТаблСНИЛС.Колонки.Добавить("СчетчикСтраниц");
		
		Для каждого Ветка Из ДеревоРаздела.Строки Цикл
			
			Данные = Ветка.Данные;
			НомерСтраницы = Формат(ДеревоРаздела.Строки.Индекс(Ветка) + 1, "ЧГ=");
			
			ФамилияЗЛ  = СокрЛП(Данные["П000110000101"]);
			ИмяЗЛ      = СокрЛП(Данные["П000110000201"]);
			ОтчествоЗЛ = СокрЛП(Данные["П000110000301"]);
			СНИЛС      = СокрЛП(Данные["П000110000401"]);
			
			СтруктПарам = Новый Структура;
			СтруктПарам.Вставить("ПроверкаВыгрузкиСтраницы");
			СтруктПарам.Вставить("Фам", ФамилияЗЛ);
			СтруктПарам.Вставить("Имя", ИмяЗЛ);
			СтруктПарам.Вставить("Отч", ОтчествоЗЛ);
			СтруктПарам.Вставить("СНИЛС", СНИЛС);
			СтруктПарам.Вставить("НомерСтраницы", НомерСтраницы);
			ПредстСтраницы
			= РегламентированнаяОтчетностьКлиентСервер.ПредставлениеСтраницыРазделаРеглОтчетаСодержащегоДанныеФизическихЛиц(
			СтруктПарам);
			
			Если НЕ ЗначениеЗаполнено(ФамилияЗЛ) И НЕ ЗначениеЗаполнено(ИмяЗЛ) Тогда
				СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
				+ ": не заданы фамилия и имя застрахованного лица. Должно быть указано хотя бы одно из значений: фамилия или имя",
				"Раздел1", "П000110000101", НомерСтраницы);
				СтатусПроверки = Ложь;
			КонецЕсли;
			
			ТекстСообщения = "";
			Если НЕ ЗначениеЗаполнено(СНИЛС) Тогда
				СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
				+ ": не задан СНИЛС застрахованного лица", "Раздел1", "П000110000401", НомерСтраницы);
				СтатусПроверки = Ложь;
			ИначеЕсли НЕ СНИЛССоответствуетТребованиям(ИмяФормы, СНИЛС, ТекстСообщения) Тогда
				СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
				+ ": неверно задан СНИЛС застрахованного лица." + ?(ЗначениеЗаполнено(ТекстСообщения),
				" " + ТекстСообщения, ""), "Раздел1", "П000110000401", НомерСтраницы);
				СтатусПроверки = Ложь;
			Иначе
				НоваяСтрокаТаблСНИЛС = ТаблСНИЛС.Добавить();
				НоваяСтрокаТаблСНИЛС.СНИЛС = СНИЛС;
				НоваяСтрокаТаблСНИЛС.ИндексСтраницы = ДеревоРаздела.Строки.Индекс(Ветка);
				НоваяСтрокаТаблСНИЛС.СчетчикСтраниц = 1;
			КонецЕсли;
			
			ЗначПок = СокрЛП(Данные["П000110000501"]);
			Если ЗначениеЗаполнено(ЗначПок) Тогда
				ТекстСообщения = "";
				Если НЕ ИННФЛСоответствуетТребованиям(ИмяФормы, ЗначПок, ТекстСообщения) Тогда
					СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
					+ ": неверно задан ИНН застрахованного лица" + ?(ЗначениеЗаполнено(ТекстСообщения),
					". " + ТекстСообщения, ""), "Раздел1", "П000110000501", НомерСтраницы);
					СтатусПроверки = Ложь;
				КонецЕсли;
			КонецЕсли;
			
			ЗначПок = СокрЛП(Данные["П000110000701"]);
			Если НЕ ЗначениеЗаполнено(ЗначПок) Тогда
				СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
				+ ": не задан статус ЗЛ", "Раздел1", "П000110000701", НомерСтраницы);
				СтатусПроверки = Ложь;
			ИначеЕсли Возм_СтатусыЗЛ.МассивЗначений.Найти(ЗначПок) = Неопределено Тогда
				СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
				+ ": некорректно задан статус ЗЛ. Возможны значения: " + Возм_СтатусыЗЛ.СтрокаЗначений,
				"Раздел1", "П000110000701", НомерСтраницы);
				СтатусПроверки = Ложь;
			КонецЕсли;
			
			ЗначПок = СокрЛП(Данные["П000110000801"]);
			Если НЕ ЗначениеЗаполнено(ЗначПок) Тогда
				СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
				+ ": не задано гражданство (код страны) застрахованного лица", "Раздел1", "П000110000801", НомерСтраницы);
				СтатусПроверки = Ложь;
			ИначеЕсли СтрДлина(ЗначПок) <> 3 ИЛИ НЕ СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ЗначПок) Тогда
				СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
				+ ": задано значение гражданства (кода страны) застрахованного лица, не состоящее из 3 цифр",
				"Раздел1", "П000110000801", НомерСтраницы);
				СтатусПроверки = Ложь;
			КонецЕсли;
			
			ДР = Данные["П000110000601"];
			ДеньОсобойДР  = СокрЛП(Данные["П000110000901"]);
			МесяцОсобойДР = СокрЛП(Данные["П000110000902"]);
			ГодОсобойДР   = СокрЛП(Данные["П000110000903"]);
			Если НЕ ЗначениеЗаполнено(ДР) И НЕ ЗначениеЗаполнено(ДеньОсобойДР)
				И НЕ ЗначениеЗаполнено(МесяцОсобойДР) И НЕ ЗначениеЗаполнено(ГодОсобойДР) Тогда
				СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы + " не задана "
				+ "дата рождения или дата рождения ""особая"" застрахованного лица (без месяца и(или) дня месяца рождения)",
				"Раздел1", "П000110000601", НомерСтраницы);
				СтатусПроверки = Ложь;
			ИначеЕсли ЗначениеЗаполнено(ДР) Тогда
				Если НЕ ДатаВходитВРазрешенныйПериод(ИмяФормы, ДР) Тогда
					СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
					+ " задана дата рождения застрахованного лица, выходящая за пределы периода с 01.01.1900 по 31.12.2099",
					"Раздел1", "П000110000601", НомерСтраницы);
					СтатусПроверки = Ложь;
				КонецЕсли;
			Иначе
				Если ЗначениеЗаполнено(ДеньОсобойДР) И Возм_Дни.МассивЗначений.Найти(ДеньОсобойДР) = Неопределено Тогда
					СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
					+ ": некорректно задан день в дате рождения ""особой"" застрахованного лица. Возможны значения: "
					+ Возм_Дни.СтрокаЗначений, "Раздел1", "П000110000901", НомерСтраницы);
					СтатусПроверки = Ложь;
				КонецЕсли;
				Если ЗначениеЗаполнено(МесяцОсобойДР) И Возм_Месяцы.МассивЗначений.Найти(МесяцОсобойДР) = Неопределено Тогда
					СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
					+ ": некорректно задан месяц в дате рождения ""особой"" застрахованного лица. Возможны значения: "
					+ Возм_Месяцы.СтрокаЗначений, "Раздел1", "П000110000902", НомерСтраницы);
					СтатусПроверки = Ложь;
				КонецЕсли;
				Если ЗначениеЗаполнено(ДеньОсобойДР) И ЗначениеЗаполнено(МесяцОсобойДР) И ЗначениеЗаполнено(ГодОсобойДР) Тогда
					СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы + " должна быть задана дата рождения, "
					+ "если известны день, месяц и год рождения застрахованного лица", "Раздел1", "П000110000601", НомерСтраницы);
					СтатусПроверки = Ложь;
				ИначеЕсли НЕ ЗначениеЗаполнено(ГодОсобойДР) Тогда
					СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
					+ ": не задан год в дате рождения ""особой"" застрахованного лица", "Раздел1", "П000110000903", НомерСтраницы);
					СтатусПроверки = Ложь;
				ИначеЕсли СтрДлина(ГодОсобойДР) <> 4 ИЛИ НЕ СтроковыеФункцииКлиентсервер.ТолькоЦифрыВСтроке(ГодОсобойДР) Тогда
					СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
					+ " задан год в дате рождения ""особой"" застрахованного лица, не состоящий из 4 цифр",
					"Раздел1", "П000110000903", НомерСтраницы);
					СтатусПроверки = Ложь;
				КонецЕсли;
			КонецЕсли;
			
			// Подраздел 1.3 раздела 1.
			//
			ДеревоМнЧ_1 = Ветка.ДанныеМногострочныхЧастей[СтруктураРеквизитовФормы.мИдГруппыР113М1];
			
			Для каждого СтрокаМнЧ_1 Из ДеревоМнЧ_1.Строки Цикл
				
				НомерСтроки = Формат(ДеревоМнЧ_1.Строки.Индекс(СтрокаМнЧ_1) + 1, "ЧГ=");
				
				Дан_1 = СтрокаМнЧ_1.Данные;
				
				МесОтчПериода = Неопределено;
				ГодОтчПериода = Неопределено;
				
				ЗначПок = СокрЛП(Дан_1["П00113М100001"]);
				Если НЕ ЗначениеЗаполнено(ЗначПок) Тогда
					СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы + ": не задан месяц отчетного периода",
					"Раздел1", "П00113М100001_" + НомерСтроки, НомерСтраницы);
					СтатусПроверки = Ложь;
				ИначеЕсли Возм_Месяцы.МассивЗначений.Найти(ЗначПок) = Неопределено Тогда
					СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
					+ ": некорректно задан месяц отчетного периода. Возможны значения: " + Возм_Месяцы.СтрокаЗначений,
					"Раздел1", "П00113М100001_" + НомерСтроки, НомерСтраницы);
					СтатусПроверки = Ложь;
				Иначе
					МесОтчПериода = Число(ЗначПок);
				КонецЕсли;
				
				ЗначПок = СокрЛП(Дан_1["П00113М100002"]);
				Если НЕ ЗначениеЗаполнено(ЗначПок) Тогда
					СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы + ": не задан год отчетного периода",
					"Раздел1", "П00113М100002_" + НомерСтроки, НомерСтраницы);
					СтатусПроверки = Ложь;
				ИначеЕсли СтрДлина(ЗначПок) <> 4 ИЛИ НЕ СтроковыеФункцииКлиентсервер.ТолькоЦифрыВСтроке(ЗначПок) Тогда
					СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
					+ ": задан год отчетного периода, не состоящий из 4 цифр",
					"Раздел1", "П00113М100002_" + НомерСтроки, НомерСтраницы);
					СтатусПроверки = Ложь;
				Иначе
					ГодОтчПериода = Число(ЗначПок);
				КонецЕсли;
				
				Если ТипЗнч(МесОтчПериода) = Тип("Число") И ТипЗнч(ГодОтчПериода) = Тип("Число")
					И НачалоДня(Дата(ГодОтчПериода, МесОтчПериода, 1)) > НачалоДня(ПараметрыВыгрузки.ДатаПодписи) Тогда
					СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
					+ ": отчетный период не может быть позже даты подписи, указанной на Титульном листе",
					"Раздел1", "П00113М100001_" + НомерСтроки, НомерСтраницы);
					СтатусПроверки = Ложь;
				КонецЕсли;
				
				МесКорОтмПериода = Неопределено;
				ГодКорОтмПериода = Неопределено;
				
				ТипСвед = НРег(СокрЛП(Дан_1["П00113М100003"]));
				Если НЕ ЗначениеЗаполнено(ТипСвед) Тогда
					СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы + ": не задан тип сведений",
					"Раздел1", "П00113М100003_" + НомерСтроки, НомерСтраницы);
					СтатусПроверки = Ложь;
				ИначеЕсли Возм_ТипыСвед.МассивЗначений.Найти(ТипСвед) = Неопределено Тогда
					СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
					+ ": некорректно задан тип сведений. Возможны значения: " + Возм_ТипыСвед.СтрокаЗначений,
					"Раздел1", "П00113М100003_" + НомерСтроки, НомерСтраницы);
					СтатусПроверки = Ложь;
				ИначеЕсли ТипСвед = "корректирующие" ИЛИ ТипСвед = "отменяющие" Тогда
					
					ЗначПок = СокрЛП(Дан_1["П00113М100005"]);
					Если НЕ ЗначениеЗаполнено(ЗначПок) Тогда
						СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
						+ ": не задан месяц корректируемого (отменяемого) периода",
						"Раздел1", "П00113М100005_" + НомерСтроки, НомерСтраницы);
						СтатусПроверки = Ложь;
					ИначеЕсли Возм_Месяцы.МассивЗначений.Найти(ЗначПок) = Неопределено Тогда
						СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
						+ ": некорректно задан месяц корректируемого (отменяемого) периода. Возможны значения: "
						+ Возм_Месяцы.СтрокаЗначений, "Раздел1", "П00113М100005_" + НомерСтроки, НомерСтраницы);
						СтатусПроверки = Ложь;
					Иначе
						МесКорОтмПериода = Число(ЗначПок);
					КонецЕсли;
					
					ЗначПок = СокрЛП(Дан_1["П00113М100006"]);
					Если НЕ ЗначениеЗаполнено(ЗначПок) Тогда
						СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
						+ ": не задан год корректируемого (отменяемого) периода",
						"Раздел1", "П00113М100006_" + НомерСтроки, НомерСтраницы);
						СтатусПроверки = Ложь;
					ИначеЕсли СтрДлина(ЗначПок) <> 4 ИЛИ НЕ СтроковыеФункцииКлиентсервер.ТолькоЦифрыВСтроке(ЗначПок) Тогда
						СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
						+ ": задан год корректируемого (отменяемого) периода, не состоящий из 4 цифр",
						"Раздел1", "П00113М100006_" + НомерСтроки, НомерСтраницы);
						СтатусПроверки = Ложь;
					Иначе
						ГодКорОтмПериода = Число(ЗначПок);
					КонецЕсли;
					
				КонецЕсли;
				
				ЗначПок = СокрЛП(Дан_1["П00113М100004"]);
				Если НЕ ЗначениеЗаполнено(ЗначПок) Тогда
					СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы + ": не задан КТО организации",
					"Раздел1", "П00113М100004_" + НомерСтроки, НомерСтраницы);
					СтатусПроверки = Ложь;
				ИначеЕсли Возм_КТО.МассивЗначений.Найти(ЗначПок) = Неопределено Тогда
					СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
					+ ": некорректно задан КТО организации. Возможны значения: " + Возм_КТО.СтрокаЗначений,
					"Раздел1", "П00113М100004_" + НомерСтроки, НомерСтраницы);
					СтатусПроверки = Ложь;
				КонецЕсли;
				
				ЗначПок = СокрЛП(Дан_1["П00113М100007"]);
				Если ЗначениеЗаполнено(ЗначПок) И (СтрДлина(ЗначПок) <> 14
					ИЛИ НЕ СтроковыеФункцииКлиентсервер.ТолькоЦифрыВСтроке(ЗначПок)) Тогда
					СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
					+ ": задан идентификационный номер ТОСП, не состоящий из 14 цифр",
					"Раздел1", "П00113М100007_" + НомерСтроки, НомерСтраницы);
					СтатусПроверки = Ложь;
				КонецЕсли;
				
				// Сведения об условиях занятости и заработной плате.
				//
				ДеревоМнЧ_2 = СтрокаМнЧ_1.ДанныеМногострочныхЧастей[СтруктураРеквизитовФормы.мИдГруппыР113М2];
				
				Если ТипСвед = "исходные" ИЛИ ТипСвед = "корректирующие" ИЛИ ЗаполненаМногострочнаяЧасть(ИмяФормы,
					ДеревоМнЧ_2, "Раздел1", СтруктураРеквизитовФормы, СтруктураМногоуровневыхРазделов) Тогда
					
					Для каждого СтрокаМнЧ_2 Из ДеревоМнЧ_2.Строки Цикл
						
						Постфикс_2 = НомерСтроки + "_" + Формат(ДеревоМнЧ_2.Строки.Индекс(СтрокаМнЧ_2) + 1, "ЧГ=");
						
						Дан_2 = СтрокаМнЧ_2.Данные;
						
						НачДата = Дан_2["П00113М200002"];
						КонДата = Дан_2["П00113М200003"];
						
						Если ЗначениеЗаполнено(НачДата) И ЗначениеЗаполнено(КонДата) Тогда
							Если НачалоДня(НачДата) > НачалоДня(КонДата) Тогда
								СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
								+ ": дата начала периода работы не должна быть позднее даты окончания",
								"Раздел1", "П00113М200002_" + Постфикс_2, НомерСтраницы);
								СтатусПроверки = Ложь;
							КонецЕсли;
							Если ТипСвед = "исходные" Тогда
								Если ЗначениеЗаполнено(МесОтчПериода) И ЗначениеЗаполнено(ГодОтчПериода) Тогда
									Если Месяц(НачДата) <> МесОтчПериода ИЛИ Год(НачДата) <> ГодОтчПериода Тогда
										СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
										+ ": дата начала периода работы должна находиться в пределах периода, указанного в поле ""Отчетный период""",
										"Раздел1", "П00113М200002_" + Постфикс_2, НомерСтраницы);
										СтатусПроверки = Ложь;
									КонецЕсли;
									Если Месяц(КонДата) <> МесОтчПериода ИЛИ Год(КонДата) <> ГодОтчПериода Тогда
										СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
										+ ": дата окончания периода работы должна находиться в пределах периода, указанного в поле ""Отчетный период""",
										"Раздел1", "П00113М200003_" + Постфикс_2, НомерСтраницы);
										СтатусПроверки = Ложь;
									КонецЕсли;
								КонецЕсли;
							ИначеЕсли ТипСвед = "корректирующие" ИЛИ ТипСвед = "отменяющие" Тогда
								Если ЗначениеЗаполнено(МесКорОтмПериода) И ЗначениеЗаполнено(ГодКорОтмПериода) Тогда
									Если Месяц(НачДата) <> МесКорОтмПериода ИЛИ Год(НачДата) <> ГодКорОтмПериода Тогда
										СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы + ": дата начала периода работы "
										+ "должна находиться в пределах периода, указанного в поле ""Корректируемый (отменяемый) период""",
										"Раздел1", "П00113М200002_" + Постфикс_2, НомерСтраницы);
										СтатусПроверки = Ложь;
									КонецЕсли;
									Если Месяц(КонДата) <> МесКорОтмПериода ИЛИ Год(КонДата) <> ГодКорОтмПериода Тогда
										СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы + ": дата окончания периода работы "
										+ "должна находиться в пределах периода, указанного в поле ""Корректируемый (отменяемый) период""",
										"Раздел1", "П00113М200003_" + Постфикс_2, НомерСтраницы);
										СтатусПроверки = Ложь;
									КонецЕсли;
								КонецЕсли;
							КонецЕсли;
						Иначе
							Если НЕ ЗначениеЗаполнено(НачДата) Тогда
								СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
								+ ": не задана дата начала периода работы в отчетном месяце",
								"Раздел1", "П00113М200002_" + Постфикс_2, НомерСтраницы);
								СтатусПроверки = Ложь;
							КонецЕсли;
							Если НЕ ЗначениеЗаполнено(КонДата) Тогда
								СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
								+ ": не задана дата окончания периода работы в отчетном месяце",
								"Раздел1", "П00113М200003_" + Постфикс_2, НомерСтраницы);
								СтатусПроверки = Ложь;
							КонецЕсли;
						КонецЕсли;
						
						Если ВРег(СокрЛП(ИмяФормы)) = ВРег("ФормаОтчета2024Кв1") Тогда
							
							ЗначПок = СокрЛП(Дан_2["П00113М200023"]);
							Если НЕ ЗначениеЗаполнено(ЗначПок) Тогда
								СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
								+ ": не задан признак невключения работника в расчет cреднесписочной численности",
								"Раздел1", "П00113М200023_" + Постфикс_2, НомерСтраницы);
								СтатусПроверки = Ложь;
							ИначеЕсли Возм_ПрВклВРасчетСрЧисл.МассивЗначений.Найти(ЗначПок) = Неопределено Тогда
								СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
								+ ": некорректно задан признак невключения работника в расчет cреднесписочной численности. Возможны значения: "
								+ Возм_ПрВклВРасчетСрЧисл.СтрокаЗначений, "Раздел1", "П00113М200023_" + Постфикс_2, НомерСтраницы);
								СтатусПроверки = Ложь;
							КонецЕсли;
							
						КонецЕсли;
						
						ЗначПок = СокрЛП(Дан_2["П00113М200004"]);
						Если НЕ ЗначениеЗаполнено(ЗначПок) Тогда
							СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
							+ ": не задано наименование структурного подразделения",
							"Раздел1", "П00113М200004_" + Постфикс_2, НомерСтраницы);
							СтатусПроверки = Ложь;
						КонецЕсли;
						
						ЗначПок = СокрЛП(Дан_2["П00113М200006"]);
						Если НЕ ЗначениеЗаполнено(ЗначПок) Тогда
							СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
							+ ": не задан код наименования должности (профессии)",
							"Раздел1", "П00113М200006_" + Постфикс_2, НомерСтраницы);
							СтатусПроверки = Ложь;
						ИначеЕсли Возм_КодыДолжн.МассивЗначений.Найти(ЗначПок) = Неопределено Тогда
							СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
							+ ": некорректно задан код наименования должности (профессии)",
							"Раздел1", "П00113М200006_" + Постфикс_2, НомерСтраницы);
							СтатусПроверки = Ложь;
						КонецЕсли;
						
						ЗначПок = СокрЛП(Дан_2["П00113М200007"]);
						Если НЕ ЗначениеЗаполнено(ЗначПок) Тогда
							СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
							+ ": не задан код категории персонала", "Раздел1", "П00113М200007_" + Постфикс_2, НомерСтраницы);
							СтатусПроверки = Ложь;
						ИначеЕсли Возм_КатегПерс.МассивЗначений.Найти(ЗначПок) = Неопределено Тогда
							СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
							+ ": некорректно задан код категории персонала. Возможны значения: " + Возм_КатегПерс.СтрокаЗначений,
							"Раздел1", "П00113М200007_" + Постфикс_2, НомерСтраницы);
							СтатусПроверки = Ложь;
						КонецЕсли;
						
						ЗначПок = СокрЛП(Дан_2["П00113М200008"]);
						Если НЕ ЗначениеЗаполнено(ЗначПок) Тогда
							СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
							+ ": не задан вид договора", "Раздел1", "П00113М200008_" + Постфикс_2, НомерСтраницы);
							СтатусПроверки = Ложь;
						ИначеЕсли Возм_ВидыДог.МассивЗначений.Найти(ЗначПок) = Неопределено Тогда
							СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
							+ ": некорректно задан вид договора. Возможны значения: " + Возм_ВидыДог.СтрокаЗначений,
							"Раздел1", "П00113М200008_" + Постфикс_2, НомерСтраницы);
							СтатусПроверки = Ложь;
						КонецЕсли;
						
						ЗначПок = СокрЛП(Дан_2["П00113М200009"]);
						Если НЕ ЗначениеЗаполнено(ЗначПок) Тогда
							СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
							+ ": не задан срок договора", "Раздел1", "П00113М200009_" + Постфикс_2, НомерСтраницы);
							СтатусПроверки = Ложь;
						ИначеЕсли Возм_СрокиДог.МассивЗначений.Найти(ЗначПок) = Неопределено Тогда
							СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
							+ ": некорректно задан срок договора. Возможны значения: " + Возм_СрокиДог.СтрокаЗначений,
							"Раздел1", "П00113М200009_" + Постфикс_2, НомерСтраницы);
							СтатусПроверки = Ложь;
						КонецЕсли;
						
						Если НЕ ЗначениеЗаполнено(СокрЛП(Дан_2["П00113М200010"])) Тогда
							
							Если НЕ ЗначениеЗаполнено(СокрЛП(Дан_2["П00113М200011"]))
								И НЕ ЗначениеЗаполнено(СокрЛП(Дан_2["П00113М200012"]))
								И НЕ ЗначениеЗаполнено(СокрЛП(Дан_2["П00113М200013"])) Тогда
								
								СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
								+ ": либо должны быть заданы сведения об ученой степени и(или) ученом звании и(или) почетном звании, либо "
								+ "должен быть заполнен признак их отсутствия", "Раздел1", "П00113М200010_" + Постфикс_2, НомерСтраницы);
								СтатусПроверки = Ложь;
								
							Иначе
								
								ЗначПок = СокрЛП(Дан_2["П00113М200011"]);
								Если ЗначениеЗаполнено(ЗначПок) И Возм_УченСтеп.МассивЗначений.Найти(ЗначПок) = Неопределено Тогда
									СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
									+ ": некорректно задана ученая степень. Возможны значения: " + Возм_УченСтеп.СтрокаЗначений,
									"Раздел1", "П00113М200011_" + Постфикс_2, НомерСтраницы);
									СтатусПроверки = Ложь;
								КонецЕсли;
								
								ЗначПок = СокрЛП(Дан_2["П00113М200012"]);
								Если ЗначениеЗаполнено(ЗначПок) И Возм_УченЗван.МассивЗначений.Найти(ЗначПок) = Неопределено Тогда
									СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
									+ ": некорректно задано ученое звание. Возможны значения: " + Возм_УченЗван.СтрокаЗначений,
									"Раздел1", "П00113М200012_" + Постфикс_2, НомерСтраницы);
									СтатусПроверки = Ложь;
								КонецЕсли;
								
								ЗначПок = СокрЛП(Дан_2["П00113М200013"]);
								Если ЗначениеЗаполнено(ЗначПок) И Возм_ПочетЗван.МассивЗначений.Найти(ЗначПок) = Неопределено Тогда
									СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
									+ ": некорректно задано почетное звание. Возможны значения: " + Возм_ПочетЗван.СтрокаЗначений,
									"Раздел1", "П00113М200013_" + Постфикс_2, НомерСтраницы);
									СтатусПроверки = Ложь;
								КонецЕсли;
								
							КонецЕсли;
							
						КонецЕсли;
						
						ЗначПок = СокрЛП(Дан_2["П00113М200014"]);
						Если НЕ ЗначениеЗаполнено(ЗначПок) Тогда
							СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
							+ ": не задан код профессиональной квалификационной группы",
							"Раздел1", "П00113М200014_" + Постфикс_2, НомерСтраницы);
							СтатусПроверки = Ложь;
						ИначеЕсли Возм_ПрофКвГр.МассивЗначений.Найти(ЗначПок) = Неопределено Тогда
							СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
							+ ": некорректно задан код профессиональной квалификационной группы",
							"Раздел1", "П00113М200014_" + Постфикс_2, НомерСтраницы);
							СтатусПроверки = Ложь;
						КонецЕсли;
						
						ЗначПок = СокрЛП(Дан_2["П00113М200015"]);
						Если НЕ ЗначениеЗаполнено(ЗначПок) Тогда
							СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
							+ ": не задан квалификационный уровень", "Раздел1", "П00113М200015_" + Постфикс_2, НомерСтраницы);
							СтатусПроверки = Ложь;
						ИначеЕсли Возм_КвалифУр.МассивЗначений.Найти(ЗначПок) = Неопределено Тогда
							СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
							+ ": некорректно задан квалификационный уровень. Возможны значения: "
							+ Возм_КвалифУр.СтрокаЗначений, "Раздел1", "П00113М200015_" + Постфикс_2, НомерСтраницы);
							СтатусПроверки = Ложь;
						КонецЕсли;
						
						ЗначПок = СокрЛП(Дан_2["П00113М200016"]);
						Если НЕ ЗначениеЗаполнено(ЗначПок) Тогда
							СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
							+ ": не задан класс (подкласс) условий труда по степени вредности и (или) опасности",
							"Раздел1", "П00113М200016_" + Постфикс_2, НомерСтраницы);
							СтатусПроверки = Ложь;
						ИначеЕсли Возм_СпОцУслТр.МассивЗначений.Найти(ЗначПок) = Неопределено Тогда
							СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
							+ ": некорректно задан класс (подкласс) условий труда по степени вредности и (или) опасности. Возможны "
							+ "значения: " + Возм_СпОцУслТр.СтрокаЗначений, "Раздел1", "П00113М200016_" + Постфикс_2, НомерСтраницы);
							СтатусПроверки = Ложь;
						КонецЕсли;
						
						ЗначПок = СокрЛП(Дан_2["П00113М200017"]);
						Если НЕ ЗначениеЗаполнено(ЗначПок) Тогда
							СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
							+ ": не задана квалификационная категория по итогам аттестации",
							"Раздел1", "П00113М200017_" + Постфикс_2, НомерСтраницы);
							СтатусПроверки = Ложь;
						ИначеЕсли Возм_КвалКатАт.МассивЗначений.Найти(ЗначПок) = Неопределено Тогда
							СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
							+ ": некорректно задана квалификационная категория по итогам аттестации. Возможны значения: "
							+ Возм_КвалКатАт.СтрокаЗначений, "Раздел1", "П00113М200017_" + Постфикс_2, НомерСтраницы);
							СтатусПроверки = Ложь;
						КонецЕсли;
						
						ЗначПок = СокрЛП(Дан_2["П00113М200018"]);
						Если НЕ ЗначениеЗаполнено(ЗначПок) Тогда
							СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
							+ ": не задана система оплаты труда", "Раздел1", "П00113М200018_" + Постфикс_2, НомерСтраницы);
							СтатусПроверки = Ложь;
						ИначеЕсли Возм_СистОплТр.МассивЗначений.Найти(ЗначПок) = Неопределено Тогда
							СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
							+ ": некорректно задана система оплаты труда. Возможны значения: "
							+ Возм_СистОплТр.СтрокаЗначений, "Раздел1", "П00113М200018_" + Постфикс_2, НомерСтраницы);
							СтатусПроверки = Ложь;
						КонецЕсли;
						
						Если Дан_2["П00113М200019"] < 0 ИЛИ Дан_2["П00113М200019"] > 372 Тогда
							СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
							+ ": некорректно задана норма рабочих часов (выступлений, постановок). Возможны значения: 0-372",
							"Раздел1", "П00113М200019_" + Постфикс_2, НомерСтраницы);
							СтатусПроверки = Ложь;
						КонецЕсли;
						
						Если Дан_2["П00113М200020"] < 0 ИЛИ Дан_2["П00113М200020"] > 496 Тогда
							СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
							+ ": некорректно задано количество фактически отработанных рабочих часов (выступлений, постановок). "
							+ "Возможны значения: 0-496", "Раздел1", "П00113М200020_" + Постфикс_2, НомерСтраницы);
							СтатусПроверки = Ложь;
						КонецЕсли;
						
						Если Дан_2["П00113М200021"] < 0.010 ИЛИ Дан_2["П00113М200021"] > 2 Тогда
							СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
							+ ": некорректно задано число занятых штатных единиц по должности (профессии). "
							+ "Возможны значения: 0,010-2,000", "Раздел1", "П00113М200021_" + Постфикс_2, НомерСтраницы);
							СтатусПроверки = Ложь;
						КонецЕсли;
						
						Если Дан_2["П00113М200022"] < 0 Тогда
							СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
							+ ": общая сумма всех выплат (ИТОГО) не должна быть отрицательной",
							"Раздел1", "П00113М200022_" + Постфикс_2, НомерСтраницы);
							СтатусПроверки = Ложь;
						КонецЕсли;
						
						// Сведения о специальных (отраслевых) условиях занятости.
						//
						ДеревоМнЧ_3 = СтрокаМнЧ_2.ДанныеМногострочныхЧастей[СтруктураРеквизитовФормы.мИдГруппыР113М3];
						
						Для каждого СтрокаМнЧ_3 Из ДеревоМнЧ_3.Строки Цикл
							
							Постфикс_3 = Постфикс_2 + "_" + Формат(ДеревоМнЧ_3.Строки.Индекс(СтрокаМнЧ_3) + 1, "ЧГ=");
							
							Дан_3 = СтрокаМнЧ_3.Данные;
							
							КодУЗ = СокрЛП(Дан_3["П00113М300001"]);
							Если НЕ ЗначениеЗаполнено(КодУЗ) Тогда
								СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
								+ ": не задан код специальных (отраслевых) условий занятости работника",
								"Раздел1", "П00113М300001_" + Постфикс_3, НомерСтраницы);
								СтатусПроверки = Ложь;
							ИначеЕсли Возм_КодыУслЗ.МассивЗначений.Найти(КодУЗ) = Неопределено Тогда
								СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
								+ ": некорректно задан код специальных (отраслевых) условий занятости работника. Возможны значения: "
								+ Возм_КодыУслЗ.СтрокаЗначений, "Раздел1", "П00113М300001_" + Постфикс_3, НомерСтраницы);
								СтатусПроверки = Ложь;
							КонецЕсли;
							
							ЗначПок = СокрЛП(Дан_3["П00113М300002"]);
							Если НЕ ЗначениеЗаполнено(ЗначПок) Тогда
								СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
								+ ": не задано значение кода специальных (отраслевых) условий занятости работника",
								"Раздел1", "П00113М300002_" + Постфикс_3, НомерСтраницы);
								СтатусПроверки = Ложь;
							Иначе
								Если СтрРазделить("0,ВМП,УОМП,ПС,ФМП,РМБ,РСМП,ФАП", ",", Ложь).Найти(КодУЗ) <> Неопределено Тогда
									Возм_Зн = ВозможныеЗначенияИзСпискаВыбораПоказателя(ИмяФормы, "ЗначенияКода" + КодУЗ);
									Если Возм_Зн.МассивЗначений.Найти(ЗначПок) = Неопределено Тогда
										СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
										+ ": некорректно задано значение для кода " + КодУЗ
										+ " специальных (отраслевых) условий занятости работника. " + ?(Возм_Зн.МассивЗначений.Количество() = 1,
										"Возможно значение", "Возможны значения") + ": " + Возм_Зн.СтрокаЗначений,
										"Раздел1", "П00113М300002_" + Постфикс_3, НомерСтраницы);
										СтатусПроверки = Ложь;
									КонецЕсли;
								ИначеЕсли Возм_ЗнКодУслЗ.МассивЗначений.Найти(ЗначПок) = Неопределено Тогда
									СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
									+ ": некорректно задано значение кода специальных (отраслевых) условий занятости работника. Возможны "
									+ "значения: " + Возм_ЗнКодУслЗ.СтрокаЗначений, "Раздел1", "П00113М300002_" + Постфикс_3, НомерСтраницы);
									СтатусПроверки = Ложь;
								КонецЕсли;
							КонецЕсли;
							
						КонецЦикла;
						
						// Размер фиксированной части в соответствии с трудовым договором (оклад, сдельная расценка
						// за единицу выработки, часовая тарифная ставка, размер разовой концертной ставки).
						//
						ДеревоМнЧ_4 = СтрокаМнЧ_2.ДанныеМногострочныхЧастей[СтруктураРеквизитовФормы.мИдГруппыР113М4];
						
						Если ДеревоМнЧ_4.Строки.Количество() > 1 Тогда
							
							Постфикс_4 = Постфикс_2 + "_2";
							
							СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
							+ ": не должно быть задано более одного значения размера фиксированной части в соответствии с трудовым договором"
							+ " (оклада, сдельной расценки за единицу выработки, часовой тарифной ставки, размера разовой концертной ставки)",
							"Раздел1", "П00113М400001_" + Постфикс_4, НомерСтраницы);
							СтатусПроверки = Ложь;
							
						Иначе
							
							СтрокаМнЧ_4 = ДеревоМнЧ_4.Строки[0];
							
							Постфикс_4 = Постфикс_2 + "_1";
							
							Дан_4 = СтрокаМнЧ_4.Данные;
							
							Если Дан_4["П00113М400001"] < 0 Тогда
								СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
								+ ": размер фиксированной части в соответствии с трудовым договором (оклад, сдельная расценка за единицу "
								+ "выработки, часовая тарифная ставка, размер разовой концертной ставки) не должен быть отрицательным",
								"Раздел1", "П00113М400001_" + Постфикс_4, НомерСтраницы);
								СтатусПроверки = Ложь;
							КонецЕсли;
							
						КонецЕсли;
						
						// Сведения о количестве специальных часов работы.
						//
						ДеревоМнЧ_5 = СтрокаМнЧ_2.ДанныеМногострочныхЧастей[СтруктураРеквизитовФормы.мИдГруппыР113М5];
						
						Для каждого СтрокаМнЧ_5 Из ДеревоМнЧ_5.Строки Цикл
							
							Постфикс_5 = Постфикс_2 + "_" + Формат(ДеревоМнЧ_5.Строки.Индекс(СтрокаМнЧ_5) + 1, "ЧГ=");
							
							Дан_5 = СтрокаМнЧ_5.Данные;
							
							КодСЧР = СокрЛП(Дан_5["П00113М500001"]);
							КолСЧР = Дан_5["П00113М500002"];
							
							КодСЧР = СокрЛП(Дан_5["П00113М500001"]);
							Если НЕ ЗначениеЗаполнено(КодСЧР) Тогда
								СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
								+ ": не задан код специальных часов работы", "Раздел1", "П00113М500001_" + Постфикс_5, НомерСтраницы);
								СтатусПроверки = Ложь;
							ИначеЕсли Возм_КодыСпЧР.МассивЗначений.Найти(КодСЧР) = Неопределено Тогда
								СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
								+ ": некорректно задан код специальных часов работы. Возможны значения: " + Возм_КодыСпЧР.СтрокаЗначений,
								"Раздел1", "П00113М500001_" + Постфикс_5, НомерСтраницы);
								СтатусПроверки = Ложь;
							КонецЕсли;
							
							Если КолСЧР < 0 Тогда
								СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
								+ ": количество специальных часов работы не должно быть отрицательным",
								"Раздел1", "П00113М500002_" + Постфикс_5, НомерСтраницы);
								СтатусПроверки = Ложь;
							КонецЕсли;
							
						КонецЦикла;
						
						// Сведения о заработной плате.
						//
						ДеревоМнЧ_6 = СтрокаМнЧ_2.ДанныеМногострочныхЧастей[СтруктураРеквизитовФормы.мИдГруппыР113М6];
						
						Для каждого СтрокаМнЧ_6 Из ДеревоМнЧ_6.Строки Цикл
							
							Постфикс_6 = Постфикс_2 + "_" + Формат(ДеревоМнЧ_6.Строки.Индекс(СтрокаМнЧ_6) + 1, "ЧГ=");
							
							Дан_6 = СтрокаМнЧ_6.Данные;
							
							КодВыпл = СокрЛП(Дан_6["П00113М600001"]);
							СумВыпл = Дан_6["П00113М600002"];
							
							Если НЕ ЗначениеЗаполнено(КодВыпл) Тогда
								СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
								+ ": не задан код выплаты", "Раздел1", "П00113М600001_" + Постфикс_6, НомерСтраницы);
								СтатусПроверки = Ложь;
							ИначеЕсли Возм_КодыВыпл.МассивЗначений.Найти(КодВыпл) = Неопределено Тогда
								СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
								+ ": некорректно задан код выплаты. Возможны значения: " + Возм_КодыВыпл.СтрокаЗначений,
								"Раздел1", "П00113М600001_" + Постфикс_6, НомерСтраницы);
								СтатусПроверки = Ложь;
							КонецЕсли;
							
							Если СумВыпл < 0 Тогда
								СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения -> " + ПредстСтраницы
								+ ": сумма выплаты не должна быть отрицательной", "Раздел1", "П00113М600002_" + Постфикс_6, НомерСтраницы);
								СтатусПроверки = Ложь;
							КонецЕсли;
							
						КонецЦикла;
						
					КонецЦикла;
					
				КонецЕсли;
				
			КонецЦикла;
			
			Если РезультатВыгрузки.ТаблицаСообщений.Количество() >= 100 Тогда
				Возврат СтатусПроверки;
			КонецЕсли;
			
		КонецЦикла;
		
		// Проверка уникальности СНИЛС в пределах подраздела 1 раздела 1.
		//
		Табл = ТаблСНИЛС.Скопировать();
		Табл.Свернуть("СНИЛС", "СчетчикСтраниц");
		
		ИндексПервойСтраницыСНеуникальнымСНИЛС = Неопределено;
		
		Если ТаблСНИЛС.Количество() <> Табл.Количество() Тогда
			
			Для каждого СтрокаТабл Из Табл Цикл
				
				Если СтрокаТабл.СчетчикСтраниц > 1 Тогда
					
					СтрокиТаблСНИЛС = ТаблСНИЛС.НайтиСтроки(
					Новый Структура("СНИЛС", СтрокаТабл.СНИЛС));
					
					Для каждого СтрокаТаблСНИЛС Из СтрокиТаблСНИЛС Цикл
						
						Данные = ДеревоРаздела.Строки[СтрокаТаблСНИЛС.ИндексСтраницы].Данные;
						
						ФамилияЗЛ  = СокрЛП(Данные["П000110000101"]);
						ИмяЗЛ      = СокрЛП(Данные["П000110000201"]);
						ОтчествоЗЛ = СокрЛП(Данные["П000110000301"]);
						СНИЛС      = СокрЛП(Данные["П000110000401"]);
						
						СтруктПарам = Новый Структура;
						СтруктПарам.Вставить("Фам", Данные["П000110000101"]);
						СтруктПарам.Вставить("Имя", Данные["П000110000201"]);
						СтруктПарам.Вставить("Отч", Данные["П000110000301"]);
						СтруктПарам.Вставить("СНИЛС", Данные["П000110000401"]);
						СтруктПарам.Вставить("НомерСтраницы", СтрокаТаблСНИЛС.ИндексСтраницы + 1);
						ПредставленияСтраницСНеуникальнымиСНИЛС.Добавить(""""
						+ РегламентированнаяОтчетностьКлиентСервер.ПредставлениеСтраницыРазделаРеглОтчетаСодержащегоДанныеФизическихЛиц(
						СтруктПарам) + """");
						
						Если ИндексПервойСтраницыСНеуникальнымСНИЛС = Неопределено Тогда
							ИндексПервойСтраницыСНеуникальнымСНИЛС = СтрокаТаблСНИЛС.ИндексСтраницы;
						КонецЕсли;
						
					КонецЦикла;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
		Если ПредставленияСтраницСНеуникальнымиСНИЛС.Количество() > 0 Тогда
			
			СообщитьВТС(ИмяФормы, РезультатВыгрузки, "Сведения: заданы неуникальные СНИЛС на страницах "
			+ СтрСоединить(ПредставленияСтраницСНеуникальнымиСНИЛС, ", ") + ".",
			"Раздел1", "П000110000401", Формат(ИндексПервойСтраницыСНеуникальнымСНИЛС + 1, "ЧГ="));
			СтатусПроверки = Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СтатусПроверки;
	
КонецФункции

Функция ЗаполненРаздел(ИмяФормы,
					   Знач ДеревоРаздела,
					   Знач ИмяРаздела,
					   СтруктураРеквизитовФормы,
					   СтруктураМногоуровневыхРазделов,
					   ДопПараметры = Неопределено) Экспорт
	
	Для каждого СтраницаРаздела Из ДеревоРаздела.Строки Цикл
		Если ЗаполненаСтраницаРаздела(ИмяФормы,
									  СтраницаРаздела,
									  ИмяРаздела,
									  СтруктураРеквизитовФормы,
									  СтруктураМногоуровневыхРазделов,
									  ДопПараметры) Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

Функция ЗаполненаСтраницаРаздела(ИмяФормы,
								 Знач СтраницаРаздела,
								 Знач ИмяРаздела,
								 СтруктураРеквизитовФормы,
								 СтруктураМногоуровневыхРазделов,
								 ДопПараметры = Неопределено)
	
	ПоказателиНеанализируемыеНаЗаполненность
	= ПоказателиНеанализируемыеНаЗаполненность(ИмяФормы, ИмяРаздела, ДопПараметры);
	
	Данные = СтраницаРаздела.Данные;
	Для каждого Пок Из Данные Цикл
		Если ЗначениеЗаполнено(Пок.Значение) И ПоказателиНеанализируемыеНаЗаполненность.Найти(Пок.Ключ) = Неопределено Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если СтруктураМногоуровневыхРазделов[ИмяРаздела].Свойство("ИменаГруппМнЧ") Тогда
		ИменаГруппМнЧ = СтруктураМногоуровневыхРазделов[ИмяРаздела]["ИменаГруппМнЧ"];
		Для каждого ИмяГруппы Из ИменаГруппМнЧ Цикл
			ДеревоМнЧ = СтраницаРаздела.ДанныеМногострочныхЧастей[ИмяГруппы];
			Если ЗаполненаМногострочнаяЧасть(ИмяФормы,
											 ДеревоМнЧ,
											 ИмяРаздела,
											 СтруктураРеквизитовФормы,
											 СтруктураМногоуровневыхРазделов,
											 ДопПараметры) Тогда
				Возврат Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если СтруктураМногоуровневыхРазделов[ИмяРаздела].Свойство("ИменаПодчиненныхРазделов") Тогда
		ИменаПодчРазделов = СтруктураМногоуровневыхРазделов[ИмяРаздела]["ИменаПодчиненныхРазделов"];
		Для ИндПодчРаздела = 0 По ИменаПодчРазделов.Количество() - 1 Цикл
			ДеревоПодчРаздела = СтраницаРаздела.Строки[ИндПодчРаздела];
			ИмяПодчРаздела = ИменаПодчРазделов[ИндПодчРаздела];
			Если ЗаполненРаздел(ИмяФормы,
								ДеревоПодчРаздела,
								ИмяПодчРаздела,
								СтруктураРеквизитовФормы,
								СтруктураМногоуровневыхРазделов,
								ДопПараметры) Тогда
				Возврат Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ЗаполненаМногострочнаяЧасть(ИмяФормы,
									Знач ДеревоМнЧ,
									Знач ИмяРаздела,
									СтруктураРеквизитовФормы,
									СтруктураМногоуровневыхРазделов,
									ДопПараметры = Неопределено)
	
	Для Каждого СтрокаМнЧ Из ДеревоМнЧ.Строки Цикл
		Если ЗаполненаСтрокаМногострочнойЧасти(ИмяФормы,
											   СтрокаМнЧ,
											   ИмяРаздела,
											   СтруктураРеквизитовФормы,
											   СтруктураМногоуровневыхРазделов,
											   ДопПараметры) Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

Функция ЗаполненаСтрокаМногострочнойЧасти(ИмяФормы,
										  Знач СтрокаМнЧ,
										  Знач ИмяРаздела,
										  СтруктураРеквизитовФормы,
										  СтруктураМногоуровневыхРазделов,
										  ДопПараметры = Неопределено)
	
	ИмяГруппыМнЧ = Неопределено;
	
	ПоказателиНеанализируемыеНаЗаполненность
	= ПоказателиНеанализируемыеНаЗаполненность(ИмяФормы, ИмяРаздела, ДопПараметры);
	
	Для каждого Пок Из СтрокаМнЧ.Данные Цикл
		Если НЕ ЗначениеЗаполнено(ИмяГруппыМнЧ) Тогда
			ИмяГруппыМнЧ = Лев(Пок.Ключ, СтруктураРеквизитовФормы.ДлинаИмениГруппы);
		КонецЕсли;
		Если ЗначениеЗаполнено(Пок.Значение) И ПоказателиНеанализируемыеНаЗаполненность.Найти(Пок.Ключ) = Неопределено Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ИмяГруппыМнЧ) Тогда
		ИменаПодчГруппМнЧ = СтруктураРеквизитовФормы.СтруктураМногострочныхЧастей[ИмяГруппыМнЧ]["ИменаПодчиненныхГрупп"];
		Для каждого ИмяПодчГруппыМнЧ Из ИменаПодчГруппМнЧ Цикл
			ДанныеПодчМнЧ = СтрокаМнЧ.ДанныеМногострочныхЧастей[ИмяПодчГруппыМнЧ];
			Для Каждого СтрокаПодчМнЧ Из ДанныеПодчМнЧ.Строки Цикл
				Если ЗаполненаСтрокаМногострочнойЧасти(ИмяФормы,
													   СтрокаПодчМнЧ,
													   ИмяРаздела,
													   СтруктураРеквизитовФормы,
													   СтруктураМногоуровневыхРазделов,
													   ДопПараметры) Тогда
					Возврат Истина;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ПоказателиНеанализируемыеНаЗаполненность(ИмяФормы, Знач ИмяРаздела, ДопПараметры = Неопределено)
	
	ИменаПоказателей = Новый Массив;
	
	Возврат ИменаПоказателей;
	
КонецФункции

Функция РегистрационныйНомерСоответствуетТребованиям(ИмяФормы, Знач РегНомер, ТекстСообщения)
	
	ТекстСообщенияОбщий = НСтр(
	"ru = 'Регистрационный номер должен соответствовать одному из шаблонов: ""ХХХ-ХХХ-ХХХХХХ"", ""ХХХХХХХХХХ"" (X - любая цифра)';
	|en = 'Регистрационный номер должен соответствовать одному из шаблонов: ""ХХХ-ХХХ-ХХХХХХ"", ""ХХХХХХХХХХ"" (X - любая цифра)'");
	
	РегНомер = СокрЛП(РегНомер);
	
	Если СтрДлина(РегНомер) = 14 Тогда
		
		Для Ном = 1 По 14 Цикл
			
			ТекСимв = Сред(РегНомер, Ном, 1);
			
			Если Ном = 4 ИЛИ Ном = 8 Тогда
				РегНомерСоответствуетТребованиям = ТекСимв = "-";
			Иначе
				РегНомерСоответствуетТребованиям = СтрНайти("1234567890", ТекСимв) > 0;
			КонецЕсли;
			
			Если НЕ РегНомерСоответствуетТребованиям Тогда
				
				ТекстСообщения = ТекстСообщенияОбщий;
				
			КонецЕсли;
			
		КонецЦикла;
		
	ИначеЕсли СтрДлина(РегНомер) = 10 Тогда
		
		Для Ном = 1 По 10 Цикл
			
			ТекСимв = Сред(РегНомер, Ном, 1);
			
			РегНомерСоответствуетТребованиям = СтрНайти("1234567890", ТекСимв) > 0;
			
			Если НЕ РегНомерСоответствуетТребованиям Тогда
				
				ТекстСообщения = ТекстСообщенияОбщий;
				
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		
		ТекстСообщения = ТекстСообщенияОбщий;
		
	КонецЕсли;
	
	Возврат ?(ТекстСообщения = "", Истина, Ложь);
	
КонецФункции

Функция АдресЭлектроннойПочтыСоответствуетТребованиям(ИмяФормы, Знач ЭлПочта, ТекстСообщения)
	
	ЭлПочта = СокрЛП(ЭлПочта);
	
	Если СтрНайти(ЭлПочта, "@") = 0 Тогда
		
		ТекстСообщения = НСтр("ru = 'Отсутствует символ ""@""';
								|en = 'Отсутствует символ ""@""'");
		
	ИначеЕсли СтрНайти(ЭлПочта, ".") = 0 Тогда
		
		ТекстСообщения = НСтр("ru = 'Отсутствует символ "".""';
								|en = 'Отсутствует символ "".""'");
		
	ИначеЕсли СтрНачинаетсяС(ЭлПочта, "@") Тогда
		
		ТекстСообщения = НСтр("ru = 'Адрес не должен начинаться с символа ""@""';
								|en = 'Адрес не должен начинаться с символа ""@""'");
		
	ИначеЕсли СтрЗаканчиваетсяНа(ЭлПочта, ".") Тогда
		
		ТекстСообщения = НСтр("ru = 'Адрес не должен заканчиваться символом "".""';
								|en = 'Адрес не должен заканчиваться символом "".""'");
		
	Иначе
		
		ПозицияСимв1_ПервыйСлева  = СтрНайти(ЭлПочта, "@");
		ПозицияСимв2_ПервыйСправа = СтрНайти(ЭлПочта, ".", НаправлениеПоиска.СКонца);
		
		Если ПозицияСимв1_ПервыйСлева > ПозицияСимв2_ПервыйСправа ИЛИ СтрНайти(ЭлПочта, "@.") > 0 Тогда
			
			ТекстСообщения = НСтр(
			"ru = 'Символ ""."" должен содержаться справа от символа ""@"", но не должен следовать сразу за ним';
			|en = 'Символ ""."" должен содержаться справа от символа ""@"", но не должен следовать сразу за ним'");
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ?(ТекстСообщения = "", Истина, Ложь);
	
КонецФункции

Функция СНИЛССоответствуетТребованиям(ИмяФормы, Знач СНИЛС, ТекстСообщения)
	
	ТекстСообщенияОбщий = НСтр(
	"ru = 'СНИЛС должен соответствовать одному из шаблонов: ""ХХХ-ХХХ-ХХХ-ХХ"", ""ХХХ-ХХХ-ХХХ ХХ"" (X - любая цифра)';
	|en = 'СНИЛС должен соответствовать одному из шаблонов: ""ХХХ-ХХХ-ХХХ-ХХ"", ""ХХХ-ХХХ-ХХХ ХХ"" (X - любая цифра)'");
	
	СНИЛС = СокрЛП(СНИЛС);
	
	Если СтрДлина(СНИЛС) <> 14 Тогда
		
		ТекстСообщения = ТекстСообщенияОбщий;
		
		Возврат Ложь;
		
	Иначе
		
		Для Ном = 1 По 14 Цикл
			
			ТекСимв = Сред(СНИЛС, Ном, 1);
			
			Если Ном = 4 ИЛИ Ном = 8 Тогда
				СНИЛССоответствуетТребованиям = ТекСимв = "-";
			ИначеЕсли Ном = 12 Тогда
				СНИЛССоответствуетТребованиям = (ТекСимв = " " ИЛИ ТекСимв = "-");
			Иначе
				СНИЛССоответствуетТребованиям = СтрНайти("1234567890", ТекСимв) > 0;
			КонецЕсли;
			
			Если НЕ СНИЛССоответствуетТребованиям Тогда
				
				ТекстСообщения = ТекстСообщенияОбщий;
				
				Возврат Ложь;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция ИННФЛСоответствуетТребованиям(ИмяФормы, Знач ИНН, ТекстСообщения)
	
	Если НЕ РегламентированныеДанныеКлиентСервер.ИННСоответствуетТребованиям(ИНН, Ложь, ТекстСообщения) Тогда
		
		Если ЗначениеЗаполнено(ТекстСообщения) И Прав(ТекстСообщения, 1) = "." Тогда
			ТекстСообщения = Лев(ТекстСообщения, СтрДлина(ТекстСообщения) - 1);
		КонецЕсли;
		
		Возврат Ложь;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция ДатаВходитВРазрешенныйПериод(ИмяФормы,
									 ПровДата,
									 ЛеваяГраница = '19000101',
									 ПраваяГраница = '20991231') Экспорт
	
	Возврат ПровДата >= ЛеваяГраница И ПровДата <= ПраваяГраница;
	
КонецФункции

Функция ВозможныеЗначенияИзСпискаВыбораПоказателя(ИмяФормы, ИмяГруппыЗначений)
	
	Если ВРег(СокрЛП(ИмяФормы)) = ВРег("ФормаОтчета2024Кв1") Тогда
		
		МакетСоставаПоказателей = ЭтотОбъект.ПолучитьМакет("СпискиВыбора2024Кв1");
		
	Иначе// "ФормаОтчета2023Кв1"
		
		МакетСоставаПоказателей = ЭтотОбъект.ПолучитьМакет("СпискиВыбора2023Кв1");
		
	КонецЕсли;
	
	МассивЗначений = Неопределено;
	СтрокаЗначений = Неопределено;
	
	МассивЧисел = Новый Массив;
	
	ПереченьЦелыхУникальныхЧисел = Истина;
	
	Область = МакетСоставаПоказателей.Области.Найти(ИмяГруппыЗначений);
	Если Область <> Неопределено Тогда
		МассивЗначений = Новый Массив;
		ВерхОбласти = Область.Верх;
		НизОбласти  = Область.Низ;
		Для НомСтр = ВерхОбласти По НизОбласти Цикл
			ТекЗначение = СокрП(МакетСоставаПоказателей.Область(НомСтр, 1).Текст);
			Если ЗначениеЗаполнено(ТекЗначение) Тогда
				Если ПереченьЦелыхУникальныхЧисел Тогда
					ПровЗначение = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(ТекЗначение);
					Если ТипЗнч(ПровЗначение) = Тип("Число") И Формат(ПровЗначение, "ЧГ=") = Формат(Цел(ПровЗначение), "ЧГ=")
						И МассивЧисел.Найти(ПровЗначение) = Неопределено Тогда
						МассивЧисел.Добавить(ПровЗначение);
					Иначе
						ПереченьЦелыхУникальныхЧисел = Ложь;
					КонецЕсли;
				КонецЕсли;
				МассивЗначений.Добавить(ТекЗначение);
			КонецЕсли;
		КонецЦикла;
		Если ПереченьЦелыхУникальныхЧисел Тогда
			СтрокаЗначений = ПредставлениеПеречняЦелыхУникальныхЧисел(МассивЧисел);
		Иначе
			СтрокаЗначений = СтрСоединить(МассивЗначений, ", ");
		КонецЕсли;
	КонецЕсли;
	
	Возврат Новый Структура("МассивЗначений, СтрокаЗначений", МассивЗначений, СтрокаЗначений);
	
КонецФункции

Функция ПредставлениеПеречняЦелыхУникальныхЧисел(Знач МассивЧисел, Разделитель = ", ", Выводить123Как1_3 = Истина)
	
	Если МассивЧисел.Количество() = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	Результат = "";
	
	Если Выводить123Как1_3 Тогда
		
		ПредЗнач = Неопределено;
		ПредПредЗнач = Неопределено;
		СледЗнач = Неопределено;
		
		НачИнтервала = Неопределено;
		
		Для НомСч = 0 По МассивЧисел.Количество() - 1 Цикл
			
			ТекЗнач = МассивЧисел[НомСч];
			
			ПредЗнач = ?(НомСч = 0, Неопределено, МассивЧисел[НомСч - 1]);
			
			ПредПредЗнач = ?(НомСч = 0 ИЛИ НомСч = 1, Неопределено, МассивЧисел[НомСч - 2]);
			
			СледЗнач = ?(НомСч = МассивЧисел.Количество() - 1, Неопределено, МассивЧисел[НомСч + 1]);
			
			ЭтоВнутренняяЧастьИнтервала
			= ПредЗнач <> Неопределено И СледЗнач <> Неопределено И ПредЗнач + 1 = ТекЗнач И ТекЗнач + 1 = СледЗнач;
			
			ЭтоКонецИнтервала
			= (СледЗнач = Неопределено ИЛИ (СледЗнач <> Неопределено И ТекЗнач + 1 <> СледЗнач))
			И ПредПредЗнач <> Неопределено И ПредЗнач + 1 = ТекЗнач И ПредПредЗнач + 1 = ПредЗнач;
			
			Если ЭтоКонецИнтервала Тогда
				Результат = Результат + "-" + Формат(ТекЗнач, "ЧГ=");
			ИначеЕсли НЕ ЭтоВнутренняяЧастьИнтервала Тогда
				Результат = Результат + ?(Результат = "", "", Разделитель) + Формат(ТекЗнач, "ЧГ=;ЧН=");
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		
		Для НомСч = 0 По МассивЧисел.Количество() - 1 Цикл
			
			ТекЗнач = МассивЧисел[НомСч];
			МассивЧисел[НомСч] = Формат(ТекЗнач, "ЧГ=");
			
		КонецЦикла;
		
		Результат = СтрСоединить(МассивЧисел, Разделитель);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура СообщитьВТС(ИмяФормы, Владелец, Описание, Раздел, ИмяЯчейки, ФИО = "") Экспорт
	
	РегламентированнаяОтчетность.СообщитьВТаблицуСообщений(Владелец, Описание, Раздел, ИмяЯчейки, ФИО);
	
КонецПроцедуры

Процедура ЗаполнитьДанными(ИмяФормы,
						   ДанныеОтчета,
						   ПараметрыВыгрузки,
						   ДеревоВыгрузки,
						   РезультатВыгрузки,
						   СтруктураРеквизитовФормы,
						   СтруктураМногоуровневыхРазделов) Экспорт
	
	ОбработатьУсловныеЭлементы(ИмяФормы,
							   ДанныеОтчета,
							   ПараметрыВыгрузки,
							   ДеревоВыгрузки,
							   РезультатВыгрузки);
	
	ЗаполнитьДаннымиУзел(ИмяФормы,
						 СтруктураРеквизитовФормы,
						 СтруктураМногоуровневыхРазделов,
						 ПараметрыВыгрузки,
						 ДеревоВыгрузки);
	
	ОтсечьНезаполненныеНеобязательныеУзлы(ИмяФормы, ДеревоВыгрузки, ПараметрыВыгрузки);
	
КонецПроцедуры

Функция ИдентификаторФайлаВыгрузки(ИмяФормы, ПараметрыВыгрузки)
	
	Если СтрДлина(ПараметрыВыгрузки.Предст_РегНомер) = 10 Тогда
		
		Возврат "ПФР_" + ПараметрыВыгрузки.Предст_РегНомер
		+ "_ЕФС-1_" + Формат(ТекущаяДатаСеанса(), "ДФ=ггггММдд") + "_" + ПараметрыВыгрузки.ГУИД;
		
	Иначе
		
		Возврат "ПФР_" + ПараметрыВыгрузки.Предст_РегНомер + "_" + ПараметрыВыгрузки.КодОргана
		+ "_ЕФС-1_" + Формат(ТекущаяДатаСеанса(), "ДФ=ггггММдд") + "_" + ПараметрыВыгрузки.ГУИД;
		
	КонецЕсли;
	
КонецФункции

Процедура ОбработатьУсловныеЭлементы(ИмяФормы,
									 ДанныеОтчета,
									 Знач ПараметрыВыгрузки,
									 Узел,
									 РезультатВыгрузки)
	
	Если ТипЗнч(Узел) <> Тип("ДеревоЗначений") Тогда
		Если Узел.Код = ПараметрыВыгрузки.ИмяКлючевогоУзлаСодержательнойЧасти Тогда
			Возврат;
		КонецЕсли;
		РегламентированнаяОтчетность.ДополнитьСтруктуруПараметров(ДанныеОтчета, ПараметрыВыгрузки, Узел);
	КонецЕсли;
	КоличествоСтрок = Узел.Строки.Количество();
	Для Инд = 1 По КоличествоСтрок Цикл
		ТекСтр = Узел.Строки.Получить(КоличествоСтрок - Инд);
		Если НЕ ПустаяСтрока(ТекСтр.Условие) Тогда
			Если НЕ РегламентированнаяОтчетность.УсловиеВыполнено(РезультатВыгрузки, ПараметрыВыгрузки, ТекСтр.Условие) Тогда
				Узел.Строки.Удалить(ТекСтр);
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		ОбработатьУсловныеЭлементы(ИмяФормы,
								   ДанныеОтчета,
								   ПараметрыВыгрузки,
								   ТекСтр,
								   РезультатВыгрузки);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьДаннымиУзел(ИмяФормы,
							   СтруктураРеквизитовФормы,
							   СтруктураМногоуровневыхРазделов,
							   ПараметрыВыгрузки,
							   Узел,
							   НомерСтроки = Неопределено)
	
	СтрокиУзла = Новый Массив;
	Для Каждого Стр Из Узел.Строки Цикл
		СтрокиУзла.Добавить(Стр);
	КонецЦикла;
	
	Для Каждого Стр из СтрокиУзла Цикл
		Если Стр.Тип = "С" ИЛИ Стр.Тип = "C" Тогда // учет обоих вариантов: кириллицы и латиницы
			Если РегламентированнаяОтчетностьКлиентСервер.СвойствоОпределено(ПараметрыВыгрузки,
				"ИмяКлючевогоУзлаСодержательнойЧасти") И Стр.Код = ПараметрыВыгрузки.ИмяКлючевогоУзлаСодержательнойЧасти Тогда
				ЗаполнитьСодержательнуюЧасть(ИмяФормы,
											 СтруктураРеквизитовФормы,
											 СтруктураМногоуровневыхРазделов,
											 Стр,
											 ПараметрыВыгрузки);
			ИначеЕсли Стр.Многострочность Тогда
				НомСтр = 1;
				ПодчиненныйЭлемент = ПолучитьПервыйПодчиненныйУзелСЗаполненнымКлючом(ИмяФормы, Стр);
				КлючПодчиненногоЭлемента = ПодчиненныйЭлемент.Ключ;
				Пока РегламентированнаяОтчетностьКлиентСервер.СвойствоОпределено(ПараметрыВыгрузки,
						КлючПодчиненногоЭлемента + "_" + Формат(НомСтр, "ЧГ=")) Цикл
					УзелСоответствующийСтроке = СкопироватьУзел(ИмяФормы, Узел, Стр);
					ЗаполнитьДаннымиУзел(ИмяФормы,
										 СтруктураРеквизитовФормы,
										 СтруктураМногоуровневыхРазделов,
										 ПараметрыВыгрузки,
										 УзелСоответствующийСтроке,
										 НомСтр);
					НомСтр = НомСтр + 1;
				КонецЦикла;
			Иначе
				ЗаполнитьДаннымиУзел(ИмяФормы,
									 СтруктураРеквизитовФормы,
									 СтруктураМногоуровневыхРазделов,
									 ПараметрыВыгрузки,
									 Стр,
									 НомерСтроки);
			КонецЕсли;
		Иначе
			Если ПустаяСтрока(Стр.ЗначениеПоУмолчанию) Тогда
				Если НЕ ПустаяСтрока(Стр.Ключ) Тогда
					ПолныйКодПоказателя = Стр.Ключ + ?(ЗначениеЗаполнено(НомерСтроки), "_" + Формат(НомерСтроки, "ЧГ="), "");
					ЗначениеПоказателя = Неопределено;
					Если ПараметрыВыгрузки.Свойство(ПолныйКодПоказателя, ЗначениеПоказателя) Тогда
						ВывестиПоказательВXML(ИмяФормы,Стр, ЗначениеПоказателя);
					Иначе
						ВывестиПоказательВXML(ИмяФормы, Стр, "");
					КонецЕсли;
				Иначе
					Стр.Значение = "";
				КонецЕсли;
			ИначеЕсли Лев(Стр.ЗначениеПоУмолчанию, 1) = "&" Тогда
				ВывестиПоказательВXML(ИмяФормы, Стр, ПараметрыВыгрузки[Сред(Стр.ЗначениеПоУмолчанию, 2)]);
			Иначе
				Стр.Значение = Стр.ЗначениеПоУмолчанию;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОтсечьНезаполненныеНеобязательныеУзлы(ИмяФормы, Узел, ПараметрыВыгрузки)
	
	КоличествоСтрок = Узел.Строки.Количество();
	Для Инд = 1 По КоличествоСтрок Цикл
		Стр = Узел.Строки.Получить(КоличествоСтрок - Инд);
		ОтсечьНезаполненныеНеобязательныеУзлы(ИмяФормы, Стр, ПараметрыВыгрузки);
	КонецЦикла;
	
	Если ТипЗнч(Узел) <> Тип("ДеревоЗначений") Тогда
		Если (СтрНайти(Узел.Обязательность, "Н") <> 0 ИЛИ СтрНайти(Узел.Обязательность, "H") <> 0)
			И УзелПуст(ИмяФормы, Узел, ПараметрыВыгрузки) Тогда // учтем оба варианта: кириллицу и латиницу
			РегламентированнаяОтчетность.УдалитьУзел(Узел);
		ИначеЕсли (СтрНайти(Узел.Обязательность, "М") <> 0
			ИЛИ СтрНайти(Узел.Обязательность, "M") <> 0) // учтем оба варианта: кириллицу и латиницу
			И УзелПуст(ИмяФормы, Узел, ПараметрыВыгрузки) И ?(СтрНайти(Узел.Обязательность, "О") <> 0
			ИЛИ СтрНайти(Узел.Обязательность, "O") <> 0,
			РегламентированнаяОтчетность.ИмеютсяАналогичныеСоседниеУзлыКлюч(Узел), Истина) Тогда
			РегламентированнаяОтчетность.УдалитьУзел(Узел);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьСодержательнуюЧасть(ИмяФормы,
									   СтруктураРеквизитовФормы,
									   СтруктураМногоуровневыхРазделов,
									   УзелРодитель,
									   ПараметрыВыгрузки)
	
	// Сведения.
	// Случай заполнения содержательной части в процедуре "ЗаписатьУзелДереваВXML".
	
	Возврат;
	
КонецПроцедуры

Функция ПолучитьПервыйПодчиненныйУзелСЗаполненнымКлючом(ИмяФормы, Родитель)
	
	Для Каждого Стр из Родитель.Строки Цикл
		Если ЗначениеЗаполнено(Стр.Ключ) Тогда
			Возврат Стр;
		КонецЕсли;
	КонецЦикла;
	Возврат Неопределено;
	
КонецФункции

Функция СкопироватьУзел(ИмяФормы, Родитель, Узел, Знач МаксИндекс = Неопределено)
	
	// Нахождение узла с максимальным индексом и с тем же кодом,
	// что и у копируемого, и добавление нового сразу после найденного.
	Если МаксИндекс = Неопределено Тогда
		СтрокиСТемЖеКодом = Родитель.Строки.НайтиСтроки(Новый Структура("Код", Узел.Код), Ложь);
		МаксИндекс = - 1;
		КолСтрокСТемЖеКодом = СтрокиСТемЖеКодом.Количество();
		Если КолСтрокСТемЖеКодом > 0 Тогда
			МаксИндекс = Родитель.Строки.Индекс(СтрокиСТемЖеКодом[КолСтрокСТемЖеКодом - 1]);
		КонецЕсли;
	КонецЕсли;
	
	ИндексСоздаваемогоУзла = МаксИндекс + 1;
	Если ИндексСоздаваемогоУзла >= Родитель.Строки.Количество() ИЛИ ИндексСоздаваемогоУзла = 0 Тогда
		// Аналогичный узел не найден или найденный узел - последний.
		НовыйУзел = Родитель.Строки.Добавить();
	Иначе
		НовыйУзел = Родитель.Строки.Вставить(ИндексСоздаваемогоУзла);
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(НовыйУзел, Узел, , "Родитель, Строки");
	
	Для Каждого Стр из Узел.Строки Цикл
		СкопироватьУзел(ИмяФормы, НовыйУзел, Стр, Узел.Строки.Количество() - 1);
	КонецЦикла;
	
	Возврат НовыйУзел;
	
КонецФункции

Процедура ВывестиПоказательВXML(ИмяФормы, Узел, ЗначениеПоказателя)
	
	МинШирина = Узел.МинРазмерность;
	МаксШирина = Узел.МаксРазмерность;
	
	Если Узел.Формат = "T" ИЛИ Узел.Формат = "Т" Тогда // учтем оба варианта: кириллицу и латиницу
		Если ТипЗнч(ЗначениеПоказателя) = Тип("Дата") Тогда
			ЗначениеПоказателяСтр = Формат(ЗначениеПоказателя, "ДФ=dd.MM.yyyy");
		Иначе
			ЗначениеПоказателяСтр = СокрЛП(ЗначениеПоказателя);
		КонецЕсли;
		Узел.Значение = ?(МаксШирина < СтрДлина(ЗначениеПоказателяСтр),
		СокрЛП(Лев(ЗначениеПоказателяСтр, МаксШирина)), ЗначениеПоказателяСтр);
	ИначеЕсли Узел.Формат = "N" Тогда
		СтрокаФормата = "ЧРД=.;ЧН=0;ЧГ=;";
		Если Узел.МаксРазмерность <> 0 И Узел.МаксРазмерность <> 99999 Тогда
			СтрокаФормата = СтрокаФормата + "ЧЦ=" + Формат(Узел.МаксРазмерность, "ЧГ=") + ";";
		КонецЕсли;
		Если Узел.МинРазмерность <> 99999 Тогда
			СтрокаФормата = СтрокаФормата + "ЧДЦ=" + Формат(Узел.МинРазмерность, "ЧГ=") + ";";
		КонецЕсли;
		Узел.Значение = СокрЛП(Формат(ЗначениеПоказателя, СтрокаФормата));
	КонецЕсли;
	
КонецПроцедуры

Функция УзелПуст(ИмяФормы, Узел, ПараметрыВыгрузки)
	
	Для Каждого Стр из Узел.Строки Цикл
		Если НЕ УзелПуст(ИмяФормы, Стр, ПараметрыВыгрузки) Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Если Узел.Формат = "S" Тогда
		Возврат Истина;
	ИначеЕсли Узел.Формат = "N" Тогда
		Возврат Узел.Значение = "0" ИЛИ (НЕ ЗначениеЗаполнено(Узел.Значение));
	Иначе
		Возврат НЕ ЗначениеЗаполнено(Узел.Значение);
	КонецЕсли;
	
КонецФункции

Функция ВыгрузитьДеревоВXML(ИмяФормы,
							ДеревоВыгрузки,
							ПараметрыВыгрузки,
							КодировкаФайлаВыгрузки,
							СтруктураРеквизитовФормы,
							СтруктураМногоуровневыхРазделов)
	
	ПотокXML = РегламентированнаяОтчетность.СоздатьНовыйПотокXML(КодировкаФайлаВыгрузки);
	
	ЗаписатьУзелДереваВXML(ИмяФормы, ДеревоВыгрузки, ПотокXML, ПараметрыВыгрузки,
	СтруктураРеквизитовФормы, СтруктураМногоуровневыхРазделов);
	
	ТекстДляЗаписи = ПотокXML.Закрыть();
	
	Возврат ТекстДляЗаписи;
	
КонецФункции

Процедура ЗаписатьУзелДереваВXML(ИмяФормы,
								 СтрокаДерева,
								 ПотокXML,
								 ПараметрыВыгрузки,
								 СтруктураРеквизитовФормы,
								 СтруктураМногоуровневыхРазделов)
	
	Если ТипЗнч(СтрокаДерева) = Тип("ДеревоЗначений") Тогда
		ПотокXML.ЗаписатьНачалоЭлемента("ЭДПФР");
		
		Если ВРег(СокрЛП(ИмяФормы)) = ВРег("ФормаОтчета2024Кв1") Тогда
			
			ПотокXML.ЗаписатьАтрибут("xmlns", "http://пф.рф/ЕФС-1/2024-01-01");
			
		Иначе// "ФормаОтчета2023Кв1"
			
			ПотокXML.ЗаписатьАтрибут("xmlns", "http://пф.рф/ЕФС-1/2022-09-22");
			
		КонецЕсли;
		
		ПотокXML.ЗаписатьАтрибут("xmlns:УТ2", "http://пф.рф/УТ/2017-08-21");
		
		Если ВРег(СокрЛП(ИмяФормы)) = ВРег("ФормаОтчета2024Кв1") Тогда
			
			ПотокXML.ЗаписатьАтрибут("xmlns:ЕФС", "http://пф.рф/ВС/ЕФС/2024-01-01");
			
		Иначе// "ФормаОтчета2023Кв1"
			
			ПотокXML.ЗаписатьАтрибут("xmlns:ЕФС", "http://пф.рф/ВС/ЕФС/2022-09-22");
			
		КонецЕсли;
		
		ПотокXML.ЗаписатьАтрибут("xmlns:ВС2", "http://пф.рф/ВС/типы/2017-10-23");
		ПотокXML.ЗаписатьАтрибут("xmlns:АФ5", "http://пф.рф/АФ/2018-12-07");
		ПотокXML.ЗаписатьАтрибут("xmlns:xsi", "http://www.w3.org/2001/XMLSchema-instance");
		Для каждого Стр Из СтрокаДерева.Строки Цикл
			ЗаписатьУзелДереваВXML(ИмяФормы, Стр, ПотокXML, ПараметрыВыгрузки,
			СтруктураРеквизитовФормы, СтруктураМногоуровневыхРазделов);
		КонецЦикла;
		ПотокXML.ЗаписатьКонецЭлемента();
	Иначе
		Если СтрокаДерева.Тип = "А" ИЛИ СтрокаДерева.Тип = "A" Тогда
			ПотокXML.ЗаписатьАтрибут(СтрокаДерева.Код, Строка(СтрокаДерева.Значение));
			
		ИначеЕсли СтрокаДерева.Код = "СЗВ" Тогда
			
			СохрСтруктура = СохраненныеДанныеОтчета(ИмяФормы, СтруктураРеквизитовФормы, СтруктураМногоуровневыхРазделов);
			ДеревоРаздела = СохрСтруктура.ДанныеМногоуровневыхРазделов["Раздел1"];
			СохрСтруктура = Неопределено;
			
			Если ЗаполненРаздел(ИмяФормы, ДеревоРаздела, "Раздел1",
				СтруктураРеквизитовФормы, СтруктураМногоуровневыхРазделов) Тогда
				
				ПотокXML.ЗаписатьНачалоЭлемента("СЗВ");
				
				Для каждого Ветка Из ДеревоРаздела.Строки Цикл
					
					Дан = Ветка.Данные;
					
					ПотокXML.ЗаписатьНачалоЭлемента("ЗЛ");
					
					ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "УТ2:СНИЛС", Дан["П000110000401"]);
					ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "ИНН",       Дан["П000110000501"], Ложь);
					
					ПотокXML.ЗаписатьНачалоЭлемента("УТ2:ФИО");
					ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "УТ2:Фамилия",  Дан["П000110000101"], Ложь);
					ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "УТ2:Имя",      Дан["П000110000201"], Ложь);
					ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "УТ2:Отчество", Дан["П000110000301"], Ложь);
					ПотокXML.ЗаписатьКонецЭлемента();// "УТ2:ФИО"
					
					ДатаРожд = Дан["П000110000601"];
					Если ЗначениеЗаполнено(ДатаРожд) Тогда
						ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "УТ2:ДатаРождения", ДатаРожд);
					Иначе
						ПотокXML.ЗаписатьНачалоЭлемента("УТ2:ДатаРожденияОсобая");
						ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "УТ2:День",  Дан["П000110000901"], Ложь);
						ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "УТ2:Месяц", Дан["П000110000902"], Ложь);
						ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "УТ2:Год",   Дан["П000110000903"]);
						ПотокXML.ЗаписатьКонецЭлемента();// "УТ2:ДатаРожденияОсобая"
					КонецЕсли;
					
					ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "СтатусЗЛ",    Дан["П000110000701"]);
					ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "Гражданство", Дан["П000110000801"]);
					
					ДеревоМнЧ_1 = Ветка.ДанныеМногострочныхЧастей[СтруктураРеквизитовФормы.мИдГруппыР113М1];
					
					Для каждого СтрокаМнЧ_1 Из ДеревоМнЧ_1.Строки Цикл
						
						Дан_1 = СтрокаМнЧ_1.Данные;
						
						ПотокXML.ЗаписатьНачалоЭлемента("СЗПГос");
						
						ПотокXML.ЗаписатьНачалоЭлемента("ОтчетныйПериод");
						ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "Месяц", Дан_1["П00113М100001"]);
						ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "Год",   Дан_1["П00113М100002"]);
						ПотокXML.ЗаписатьКонецЭлемента();// "ОтчетныйПериод"
						
						ЗначПок = СокрЛП(Дан_1["П00113М100003"]);
						ТипСвед = "";
						ТипСвед = ?(НРег(ЗначПок) = "исходные",       "1", ТипСвед);
						ТипСвед = ?(НРег(ЗначПок) = "корректирующие", "2", ТипСвед);
						ТипСвед = ?(НРег(ЗначПок) = "отменяющие",     "3", ТипСвед);
						ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "Тип", ТипСвед);
						
						Если ТипСвед = "2" ИЛИ ТипСвед = "3" Тогда
							ПотокXML.ЗаписатьНачалоЭлемента("КоррОтмПериод");
							ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "Месяц", Дан_1["П00113М100005"]);
							ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "Год",   Дан_1["П00113М100006"]);
							ПотокXML.ЗаписатьКонецЭлемента();// "КоррОтмПериод"
						КонецЕсли;
						
						ПотокXML.ЗаписатьНачалоЭлемента("Организация");
						ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "ЕФС:КодКТО", Дан_1["П00113М100004"]);
						ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "ТОСП",       Дан_1["П00113М100007"], Ложь);
						ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "OID",        Дан_1["П00113М100008"], Ложь);
						ПотокXML.ЗаписатьКонецЭлемента();// "Организация"
						
						ДеревоМнЧ_2 = СтрокаМнЧ_1.ДанныеМногострочныхЧастей[СтруктураРеквизитовФормы.мИдГруппыР113М2];
						
						Если ТипСвед = "1" ИЛИ ТипСвед = "2" ИЛИ ЗаполненаМногострочнаяЧасть(ИмяФормы, ДеревоМнЧ_2,
							"Раздел1", СтруктураРеквизитовФормы, СтруктураМногоуровневыхРазделов) Тогда
							
							Для каждого СтрокаМнЧ_2 Из ДеревоМнЧ_2.Строки Цикл
								
								Дан_2 = СтрокаМнЧ_2.Данные;
								
								ПотокXML.ЗаписатьНачалоЭлемента("УЗиЗП");
								
								ПотокXML.ЗаписатьНачалоЭлемента("Период");
								ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "УТ2:С",  Дан_2["П00113М200002"]);
								ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "УТ2:По", Дан_2["П00113М200003"]);
								ПотокXML.ЗаписатьКонецЭлемента();// "Период"
								
								ПотокXML.ЗаписатьНачалоЭлемента("СтруктурноеПодразделение");
								ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "Наименование", Дан_2["П00113М200004"]);
								ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "OID",          Дан_2["П00113М200005"], Ложь);
								ПотокXML.ЗаписатьКонецЭлемента();// "СтруктурноеПодразделение"
								
								ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "ЕФС:КодДолжности", Дан_2["П00113М200006"]);
								ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "ЕФС:КодККП",       Дан_2["П00113М200007"]);
								
								ПотокXML.ЗаписатьНачалоЭлемента("Звание");
								НетЗваний = ЗначениеЗаполнено(СокрЛП(Дан_2["П00113М200010"]));
								Если НетЗваний Тогда
									ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "НетЗваний", "0");
								Иначе
									ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "УченаяСтепень",   Дан_2["П00113М200011"], Ложь);
									ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "ЕФС:КодУчЗвание", Дан_2["П00113М200012"], Ложь);
									ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "ЕФС:ПчЗвание",    Дан_2["П00113М200013"], Ложь);
								КонецЕсли;
								ПотокXML.ЗаписатьКонецЭлемента();// "Звание"
								
								ДеревоМнЧ_3 = СтрокаМнЧ_2.ДанныеМногострочныхЧастей[СтруктураРеквизитовФормы.мИдГруппыР113М3];
								Для каждого СтрокаМнЧ_3 Из ДеревоМнЧ_3.Строки Цикл
									
									Дан_3 = СтрокаМнЧ_3.Данные;
									
									ПотокXML.ЗаписатьНачалоЭлемента("СоУЗ");
									ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "Код",      Дан_3["П00113М300001"]);
									ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "Значение", Дан_3["П00113М300002"]);
									ПотокXML.ЗаписатьКонецЭлемента();// "СоУЗ"
									
								КонецЦикла;
								
								ПотокXML.ЗаписатьНачалоЭлемента("Договор");
								ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "Вид",  Дан_2["П00113М200008"]);
								ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "Срок", Дан_2["П00113М200009"]);
								ПотокXML.ЗаписатьКонецЭлемента();// "Договор"
								
								ПотокXML.ЗаписатьНачалоЭлемента("Квалификация");
								
								ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "ЕФС:КодКУ",     Дан_2["П00113М200014"]);
								ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "Уровень",       Дан_2["П00113М200015"]);
								ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "Вредность",     СтрЗаменить(Дан_2["П00113М200016"], ",", "."));
								ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "Аттестация",    Дан_2["П00113М200017"]);
								ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "ШтатЕд",        Формат(Дан_2["П00113М200021"], "ЧДЦ=3; ЧРД=."));
								ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "СистемаОплаты", Дан_2["П00113М200018"]);
								
								ДеревоМнЧ_4 = СтрокаМнЧ_2.ДанныеМногострочныхЧастей[СтруктураРеквизитовФормы.мИдГруппыР113М4];
								Для каждого СтрокаМнЧ_4 Из ДеревоМнЧ_4.Строки Цикл
									
									Дан_4 = СтрокаМнЧ_4.Данные;
									
									ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "РазмерФЧ", Дан_4["П00113М400001"]);
									
								КонецЦикла;
								
								ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "РабВремяНорма", Дан_2["П00113М200019"]);
								ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "РабВремяФакт",  Дан_2["П00113М200020"]);
								
								ДеревоМнЧ_5 = СтрокаМнЧ_2.ДанныеМногострочныхЧастей[СтруктураРеквизитовФормы.мИдГруппыР113М5];
								Для каждого СтрокаМнЧ_5 Из ДеревоМнЧ_5.Строки Цикл
									
									Дан_5 = СтрокаМнЧ_5.Данные;
									
									ПотокXML.ЗаписатьНачалоЭлемента("КолСпецЧасовРаб");
									ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "КодСпецЧ",   Дан_5["П00113М500001"]);
									ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "КоличСпецЧ", Дан_5["П00113М500002"]);
									ПотокXML.ЗаписатьКонецЭлемента();// "КолСпецЧасовРаб"
									
								КонецЦикла;
								
								ДеревоМнЧ_6 = СтрокаМнЧ_2.ДанныеМногострочныхЧастей[СтруктураРеквизитовФормы.мИдГруппыР113М6];
								Для каждого СтрокаМнЧ_6 Из ДеревоМнЧ_6.Строки Цикл
									
									Дан_6 = СтрокаМнЧ_6.Данные;
									
									ПотокXML.ЗаписатьНачалоЭлемента("СведОЗаработПлате");
									ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "ЕФС:КодВыплаты", Дан_6["П00113М600001"]);
									ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "Сумма",          Дан_6["П00113М600002"]);
									ПотокXML.ЗаписатьКонецЭлемента();// "СведОЗаработПлате"
									
								КонецЦикла;
								
								ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "Итого", Дан_2["П00113М200022"]);
								
								ПотокXML.ЗаписатьКонецЭлемента();// "Квалификация"
								
								Если ВРег(СокрЛП(ИмяФормы)) = ВРег("ФормаОтчета2024Кв1") Тогда
									
									ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, "РабНеВклРасчЧисл", Дан_2["П00113М200023"]);
									
								КонецЕсли;
								
								ПотокXML.ЗаписатьКонецЭлемента();// "УЗиЗП"
								
							КонецЦикла;
							
						КонецЕсли;
						
						ПотокXML.ЗаписатьКонецЭлемента();// "СЗПГос"
						
					КонецЦикла;
					
					ПотокXML.ЗаписатьКонецЭлемента();// "ЗЛ"
					
				КонецЦикла;
				
				ПотокXML.ЗаписатьКонецЭлемента();// "СЗВ"
				
			КонецЕсли;
			
			ДеревоРаздела = Неопределено;
			
		Иначе
			ПотокXML.ЗаписатьНачалоЭлемента(СтрокаДерева.Код);
			Для каждого Лист из СтрокаДерева.Строки Цикл
				ЗаписатьУзелДереваВXML(ИмяФормы, Лист, ПотокXML, ПараметрыВыгрузки,
				СтруктураРеквизитовФормы, СтруктураМногоуровневыхРазделов);
			КонецЦикла;
			ПотокXML.ЗаписатьТекст(Строка(СтрокаДерева.Значение));
			ПотокXML.ЗаписатьКонецЭлемента();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПотокXML_ВывестиЭлемент(ИмяФормы, ПотокXML, ИмяЭлемента, ЗначениеЭлемента, ОбязательностьЭлемента = Истина)
	
	Если ОбязательностьЭлемента ИЛИ ЗначениеЗаполнено(ЗначениеЭлемента) Тогда
		
		Если ТипЗнч(ЗначениеЭлемента) = Тип("Строка") Тогда
			ЗначЭлемента = СокрЛП(ЗначениеЭлемента);
		ИначеЕсли ТипЗнч(ЗначениеЭлемента) = Тип("Дата") Тогда
			ЗначЭлемента = Формат(ЗначениеЭлемента, "ДФ=""гггг-ММ-дд""");
		ИначеЕсли ТипЗнч(ЗначениеЭлемента) = Тип("Число") Тогда
			ЗначЭлемента = Формат(ЗначениеЭлемента, "ЧДЦ=2; ЧРД=.; ЧН=0; ЧГ=");
		Конецесли;
		
		ПотокXML.ЗаписатьНачалоЭлемента(ИмяЭлемента);
		ПотокXML.ЗаписатьТекст(ЗначЭлемента);
		ПотокXML.ЗаписатьКонецЭлемента();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ФормированиеПечатногоБланка

Функция ПечатныйБланк(ПараметрыПечати) Экспорт
	
	Перем СтруктураРеквизитовФормы;
	Перем СтруктураМногоуровневыхРазделов;
	Перем ДанныеРазделов;
	Перем ВидПечати;
	Перем ДеревоВыбранныхСтраниц;
	Перем ДанныеМногоуровневыхРазделов;
	
	ИмяФормы = ПараметрыПечати.ВыбраннаяФорма;
	
	ПечатныйБланкФормируетсяВФорматеPDF = Ложь;
	
	Если НЕ ПараметрыПечати.Свойство("СтруктураРеквизитовФормы", СтруктураРеквизитовФормы) Тогда
		СформироватьСтруктуруРеквизитовФормы(ИмяФормы, СтруктураРеквизитовФормы);
		ИнициализироватьМногострочныеЧасти(ИмяФормы, СтруктураРеквизитовФормы);
		ПараметрыПечати.Свойство("СохраненныйОтчет", СтруктураРеквизитовФормы.мСохраненныйДок);
		СтруктураРеквизитовФормы.ИП = НЕ РегламентированнаяОтчетностьВызовСервера.ЭтоЮридическоеЛицо(
		ПараметрыПечати.СохраненныйОтчет.Организация);
	КонецЕсли;
	
	Если НЕ ПараметрыПечати.Свойство("СтруктураМногоуровневыхРазделов", СтруктураМногоуровневыхРазделов) Тогда
		ИнициализироватьМногоуровневыеРазделы(ИмяФормы, СтруктураРеквизитовФормы, СтруктураМногоуровневыхРазделов);
	КонецЕсли;
	
	Если НЕ ПараметрыПечати.Свойство("ДеревоВыбранныхСтраниц", ДеревоВыбранныхСтраниц) Тогда
		ДеревоВыбранныхСтраниц = ПараметрыПечати.СохраненныйОтчет.ДеревоНастройкиСтраниц.Получить();
		ПечатныйБланкФормируетсяВФорматеPDF = Истина;
	КонецЕсли;
	
	Если НЕ ПараметрыПечати.Свойство("ВидПечати", ВидПечати) Тогда
		ВидПечати = "";
	КонецЕсли;
	
	Если НЕ ПараметрыПечати.Свойство("ДанныеРазделов", ДанныеРазделов) Тогда
		
		ДанныеРазделов = Новый Структура;
		СохрСтруктура = СохраненныеДанныеОтчета(ИмяФормы, СтруктураРеквизитовФормы, СтруктураМногоуровневыхРазделов);
		ПоказателиОтчета = СохрСтруктура.ПоказателиОтчета;
		
		Для Каждого Раздел Из ПоказателиОтчета Цикл
			ДанныеРазделов.Вставить(СтрЗаменить(Раздел.Ключ, "ПолеТабличногоДокумента", ""),
			РегламентированнаяОтчетность.СвернутьДанныеТабличногоПоля(Раздел.Значение));
		КонецЦикла;
		
		Если СохрСтруктура.Свойство("ДанныеМногоуровневыхРазделов", ДанныеМногоуровневыхРазделов) Тогда
			Для Каждого ЭлементСтруктуры Из ДанныеМногоуровневыхРазделов Цикл
				РегламентированнаяОтчетность.ПоместитьВКэш(ЭлементСтруктуры.Значение, Новый УникальныйИдентификатор,
				СтруктураРеквизитовФормы["АдресВоВрХранилищеДеревоДанных" + ЭлементСтруктуры.Ключ]);
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
	Для Каждого СтраницаОтчета Из ДеревоВыбранныхСтраниц.Строки Цикл
		
		Если СтраницаОтчета.ВыводНаПечать = 0 Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("НаименованиеЛиста",  СтраницаОтчета.Представление);
		СтруктураПараметров.Вставить("ОриентацияЛиста",    СтраницаОтчета.ОриентацияСтраницы);
		СтруктураПараметров.Вставить("ИмяСтраницы",        СтраницаОтчета.ИмяСтраницы);
		Структурапараметров.Вставить("ИмяОбластиТаблПоля", СтраницаОтчета.ИмяОбластиТабличногоПоля);
		Структурапараметров.Вставить("ВидПечати",          ВидПечати);
		
		ВывестиНаПечатьЛист(ИмяФормы, СтруктураПараметров, СтруктураРеквизитовФормы, ДанныеРазделов);
		
	КонецЦикла;
	
	РегламентированнаяОтчетностьКлиентСервер.ПроставлениеНомеровЛистов(
	СтруктураРеквизитовФормы, , СтруктураРеквизитовФормы.СписокПечатаемыхЛистов);
	
	Если НЕ ПечатныйБланкФормируетсяВФорматеPDF Тогда
		Возврат "";
	КонецЕсли;
	
	ПакетОтображаемыхДокументов = Новый ПакетОтображаемыхДокументов;
	
	Для Каждого Лист Из СтруктураРеквизитовФормы.СписокПечатаемыхЛистов Цикл
		ПакетОтображаемыхДокументов.Состав.Добавить(Лист.Значение[0]);
	КонецЦикла;
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("pdf");
	
	ПакетОтображаемыхДокументов.ЗаписатьФайлДляПечати(ИмяВременногоФайла);
	
	ФайлПечатногоБланка = Новый ДвоичныеДанные(ИмяВременногоФайла);
	
	АдресВоВременномХранилище = ПоместитьВоВременноеХранилище(ФайлПечатногоБланка, Новый УникальныйИдентификатор);
	
	УдалитьФайлы(ИмяВременногоФайла);
	
	ИмяФайлаПечатногоБланка = СтруктураРеквизитовФормы.НаименованиеОтчета + НСтр("ru = ' за ';
																				|en = ' за '")
	+ ПредставлениеПериода(НачалоДня(СтруктураРеквизитовФормы.мСохраненныйДок.ДатаНачала),
	КонецДня(СтруктураРеквизитовФормы.мСохраненныйДок.ДатаОкончания), "ФП = Истина")
	+ " (" + СтруктураРеквизитовФормы.мСохраненныйДок.Организация + ").pdf";
	
	ПечатныйБланк = Новый Структура;
	ПечатныйБланк.Вставить("АдресПечатногоБланка",    АдресВоВременномХранилище);
	ПечатныйБланк.Вставить("ИмяФайлаПечатногоБланка", ИмяФайлаПечатногоБланка);
	
	Возврат ПечатныйБланк;
	
КонецФункции

Процедура ВывестиНаПечатьЛист(ИмяФормы, СтруктураПараметров, СтруктураРеквизитовФормы, ДанныеРазделов)
	
	ИмяТекРаздела = СтруктураПараметров.ИмяСтраницы;
	
	Если ВРег(СокрЛП(ИмяФормы)) = ВРег("ФормаОтчета2024Кв1") Тогда
		
		Макет = ЭтотОбъект.ПолучитьМакет("ПечатныйБланк2024Кв1");
		
	Иначе// "ФормаОтчета2023Кв1"
		
		Макет = ЭтотОбъект.ПолучитьМакет("ПечатныйБланк2023Кв1");
		
	КонецЕсли;
	
	Если ИмяТекРаздела = "Титульный" Тогда
		
		ПечФорма = Макет.Получитьобласть("Титул");
		ПечФорма.ПолеСверху = 5;
		ПечФорма.ПолеСправа = 5;
		ПечФорма.ПолеСнизу  = 5;
		ПечФорма.ПолеСлева  = 15;
		ПечФорма.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
		ПечФорма.Автомасштаб = Истина;
		
		ПечФорма.Параметры.Заполнить(ДанныеРазделов.Титульный);
		
		Если ВРег(СокрЛП(ИмяФормы)) = ВРег("ФормаОтчета2024Кв1") Тогда
			
			ИменаПоказателей = СтрРазделить("ИНН,КПП,ОКФС,ОКОГУ,ОКПО,ОКВЭД,ОГРН,КодКатСтрахФЛ,Телефон", ",", Ложь);
			
		Иначе// "ФормаОтчета2024Кв1"
			
			ИменаПоказателей = СтрРазделить("ИНН,КПП,ОКФС,ОКОГУ,ОКПО,ОКВЭД,ОГРН,Телефон", ",", Ложь);
			
		КонецЕсли;
		
		ЗначенияВЗнакоместах = Новый Структура;
		Для каждого ИмяПок Из ИменаПоказателей Цикл
			
			КолЗнакомест = 0;
			КолЗнакомест = ?(ИмяПок = "ИНН",  12, КолЗнакомест);
			КолЗнакомест = ?(ИмяПок = "КПП",   9, КолЗнакомест);
			КолЗнакомест = ?(ИмяПок = "ОКФС",  2, КолЗнакомест);
			КолЗнакомест = ?(ИмяПок = "ОКОГУ", 7, КолЗнакомест);
			КолЗнакомест = ?(ИмяПок = "ОКПО", 14, КолЗнакомест);
			КолЗнакомест = ?(ИмяПок = "ОКВЭД", 6, КолЗнакомест);
			КолЗнакомест = ?(ИмяПок = "ОГРН" ИЛИ ИмяПок = "Телефон", 15, КолЗнакомест);
			
			Если ВРег(СокрЛП(ИмяФормы)) = ВРег("ФормаОтчета2024Кв1") Тогда
				
				КолЗнакомест = ?(ИмяПок = "КодКатСтрахФЛ", 4, КолЗнакомест);
				
			КонецЕсли;
			
			Предст_ИмяПок = "Предст_" + ИмяПок;
			ЗначПок = СокрЛП(ДанныеРазделов.Титульный[Предст_ИмяПок]);
			Если Предст_ИмяПок = "Предст_ИНН" Тогда
				ЗначПок = ?(СтрДлина(ЗначПок) = 10, ЗначПок + "--", ЗначПок);
			ИначеЕсли Предст_ИмяПок = "Предст_ОКПО" Тогда
				ЗначПок = СтроковыеФункцииКлиентСервер.ДополнитьСтроку(ЗначПок, 14, " ", "Справа");
			ИначеЕсли Предст_ИмяПок = "Предст_ОКВЭД" Тогда
				ЗначПок = СтрЗаменить(ЗначПок, ".", "");
			ИначеЕсли Предст_ИмяПок = "Предст_ОГРН" Тогда
				ЗначПок = ?(СтрДлина(ЗначПок) = 13, "00" + ЗначПок, ЗначПок);
			КонецЕсли;
			Для НомПоз = 1 По КолЗнакомест Цикл
				ЗначенияВЗнакоместах.Вставить(Предст_ИмяПок + НомПоз, Сред(ЗначПок, НомПоз, 1));
			КонецЦикла;
			
			ЗначПок = СокрЛП(ДанныеРазделов.Титульный[ИмяПок]);
			Если ИмяПок = "ИНН" Тогда
				ЗначПок = ?(СтрДлина(ЗначПок) = 10, ЗначПок + "--", ЗначПок);
			ИначеЕсли Предст_ИмяПок = "ОКПО" Тогда
				ЗначПок = СтроковыеФункцииКлиентСервер.ДополнитьСтроку(ЗначПок, 14, " ", "Справа");
			ИначеЕсли ИмяПок = "ОКВЭД" Тогда
				ЗначПок = СтрЗаменить(ЗначПок, ".", "");
			ИначеЕсли ИмяПок = "ОГРН" Тогда
				ЗначПок = ?(СтрДлина(ЗначПок) = 13, "00" + ЗначПок, ЗначПок);
			ИначеЕсли ИмяПок = "Телефон" Тогда
				Продолжить;
			КонецЕсли;
			Для НомПоз = 1 По КолЗнакомест Цикл
				ЗначенияВЗнакоместах.Вставить(ИмяПок + НомПоз, Сред(ЗначПок, НомПоз, 1));
			КонецЦикла;
			
		КонецЦикла;
		ПечФорма.Параметры.Заполнить(ЗначенияВЗнакоместах);
		
		Фам = СокрЛП(ДанныеРазделов.Титульный["Рук_Фамилия"]);
		Имя = СокрЛП(ДанныеРазделов.Титульный["Рук_Имя"]);
		Отч = СокрЛП(ДанныеРазделов.Титульный["Рук_Отчество"]);
		ФИОРук = "";
		Если ЗначениеЗаполнено(Фам) Тогда
			ФИОРук = Фам;
		КонецЕсли;
		Если ЗначениеЗаполнено(Имя) Тогда
			ФИОРук = ?(ЗначениеЗаполнено(ФИОРук), ФИОРук + " " + Имя, Имя);
		КонецЕсли;
		Если ЗначениеЗаполнено(Отч) Тогда
			ФИОРук = ?(ЗначениеЗаполнено(ФИОРук), ФИОРук + " " + Отч, Отч);
		КонецЕсли;
		ПечФорма.Параметры.ФИОРук = ФИОРук;
		
		ДатаПодписи_Мас = СтрРазделить(Формат(ДанныеРазделов.Титульный["ДатаПодписи"], "ДФ=""дд ММММ гггг"""), " ", Ложь);
		Если ДатаПодписи_Мас.Количество() = 3 Тогда
			ПечФорма.Параметры.ДатаПодписи_Ч = ДатаПодписи_Мас[0];
			ПечФорма.Параметры.ДатаПодписи_М = ДатаПодписи_Мас[1];
			ПечФорма.Параметры.ДатаПодписи_Г = ДатаПодписи_Мас[2];
		КонецЕсли;
		
		СтруктураПараметров.Вставить("НомерСтраницы", 1);
		РегламентированнаяОтчетность.ВывестиНаПечатьЛистОбщая("",
		СтруктураПараметров, ПечФорма, СтруктураРеквизитовФормы.мПечатныеФормы);
		
	ИначеЕсли ИмяТекРаздела = "Раздел1" Тогда
		
		НомерСтраницы = 1;
		
		ПечФорма = Макет.Получитьобласть("ШапкаРазд1");
		ПечФорма.ПолеСверху = 15;
		ПечФорма.ПолеСправа = 5;
		ПечФорма.ПолеСнизу  = 5;
		ПечФорма.ПолеСлева  = 5;
		ПечФорма.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
		ПечФорма.Автомасштаб = Истина;
		
		СтруктураПараметров.Вставить("ОриентацияЛиста", "Ландшафт");
		
		Подр1        = Макет.Получитьобласть("Подр1");
		Подр11       = Макет.Получитьобласть("Подр11");
		Подр12       = Макет.Получитьобласть("Подр12");
		Подр13       = Макет.Получитьобласть("Подр13");
		ШапкаТабл    = Макет.Получитьобласть("ШапкаТабл");
		ПодшапкаТабл = Макет.Получитьобласть("ПодшапкаТабл");
		СтрокаТабл   = Макет.Получитьобласть("СтрокаТабл");
		
		Если ВРег(СокрЛП(ИмяФормы)) = ВРег("ФормаОтчета2024Кв1") Тогда
			
			Сноска = Макет.Получитьобласть("Сноска");
			
		КонецЕсли;
		
		МассивПроверки = Новый Массив;
		
		ДеревоРаздела = ПолучитьИзВременногоХранилища(СтруктураРеквизитовФормы["АдресВоВрХранилищеДеревоДанныхРаздел1"]);
		
		Для каждого Ветка Из ДеревоРаздела.Строки Цикл
			
			Если СтруктураРеквизитовФормы.ФлПечататьСтраницыНаОтдельныхЛистах И ДеревоРаздела.Строки.Индекс(Ветка) > 0 Тогда
				
				НомерСтраницы = НомерСтраницы + 1;
				СтруктураПараметров.Вставить("НомерСтраницы", НомерСтраницы);
				РегламентированнаяОтчетность.ВывестиНаПечатьЛистОбщая("",
				СтруктураПараметров, ПечФорма, СтруктураРеквизитовФормы.мПечатныеФормы);
				
				ПечФорма.Очистить();
				
			КонецЕсли;
			
			Дан = Ветка.Данные;
			
			Для ИндПарам = 0 По Подр1.Параметры.Количество() - 1 Цикл
				Подр1.Параметры[ИндПарам] = Неопределено;
			КонецЦикла;
			
			Подр1.Параметры.Заполнить(Дан);
			
			ИменаПоказателей = СтрРазделить("П000110000501,П000110000701,П000110000801", ",", Ложь);
			ЗначенияВЗнакоместах = Новый Структура;
			Для каждого ИмяПок Из ИменаПоказателей Цикл
				
				КолЗнакомест = 0;
				КолЗнакомест = ?(ИмяПок = "П000110000501", 12, КолЗнакомест);
				КолЗнакомест = ?(ИмяПок = "П000110000701",  4, КолЗнакомест);
				КолЗнакомест = ?(ИмяПок = "П000110000801",  3, КолЗнакомест);
				
				ЗначПок = СокрЛП(Дан[ИмяПок]);
				Для НомПоз = 1 По КолЗнакомест Цикл
					ЗначенияВЗнакоместах.Вставить(ИмяПок + "_" + НомПоз, Сред(ЗначПок, НомПоз, 1));
				КонецЦикла;
				
			КонецЦикла;
			Подр1.Параметры.Заполнить(ЗначенияВЗнакоместах);
			
			ДатаРожд_Мас = СтрРазделить(Формат(Дан["П000110000601"], "ДФ=""дд ММММ гггг"""), " ", Ложь);
			Если ДатаРожд_Мас.Количество() = 3 Тогда
				Подр1.Параметры.ДатаРожд_Ч = ДатаРожд_Мас[0];
				Подр1.Параметры.ДатаРожд_М = ДатаРожд_Мас[1];
				Подр1.Параметры.ДатаРожд_Г = ДатаРожд_Мас[2];
			Иначе
				Подр1.Параметры.ДатаРожд_Ч = ?(ЗначениеЗаполнено(Дан["П000110000901"]),
				СтроковыеФункцииКлиентСервер.ДополнитьСтроку(СокрЛП(Дан["П000110000901"]), 2, "0", "Слева"), "");
				ДатаРожд_М
				= СтроковыеФункцииКлиентСервер.ДополнитьСтроку(СокрЛП(Дан["П000110000902"]), 2, "0", "Слева");
				Если СтрРазделить("01,02,03,04,05,06,07,08,09,10,11,12", ",", Ложь).Найти(ДатаРожд_М) <> Неопределено Тогда
					Подр1.Параметры.ДатаРожд_М = НРег(Формат(СтроковыеФункцииКлиентСервер.СтрокаВДату(
					"01." + ДатаРожд_М + ".2000", ЧастиДаты.Дата), "ДФ=""ММММ"""));
				КонецЕсли;
				Подр1.Параметры.ДатаРожд_Г = СокрЛП(Дан["П000110000903"]);
			КонецЕсли;
			
			МассивПроверки.Очистить();
			МассивПроверки.Добавить(Подр1);
			
			Если НЕ ПечФорма.ПроверитьВывод(МассивПроверки) Тогда
				
				НомерСтраницы = НомерСтраницы + 1;
				СтруктураПараметров.Вставить("НомерСтраницы", НомерСтраницы);
				РегламентированнаяОтчетность.ВывестиНаПечатьЛистОбщая("",
				СтруктураПараметров, ПечФорма, СтруктураРеквизитовФормы.мПечатныеФормы);
				
				ПечФорма.Очистить();
				
			КонецЕсли;
			
			ПечФорма.Вывести(Подр1);
			ПечФормаПодр1 = ПечФорма.Область("Подр1");
			ПечФормаПодр1.Имя = "_" + Формат(ПечФормаПодр1.Верх, "ЧГ=");
			
			МассивПроверки.Очистить();
			МассивПроверки.Добавить(Подр11);
			
			Если НЕ ПечФорма.ПроверитьВывод(МассивПроверки) Тогда
				
				НомерСтраницы = НомерСтраницы + 1;
				СтруктураПараметров.Вставить("НомерСтраницы", НомерСтраницы);
				РегламентированнаяОтчетность.ВывестиНаПечатьЛистОбщая("",
				СтруктураПараметров, ПечФорма, СтруктураРеквизитовФормы.мПечатныеФормы);
				
				ПечФорма.Очистить();
				
			КонецЕсли;
			
			ПечФорма.Вывести(Подр11);
			ПечФормаПодр11 = ПечФорма.Область("Подр11");
			ПечФормаПодр11.Имя = "_" + Формат(ПечФормаПодр11.Верх, "ЧГ=");
			
			МассивПроверки.Очистить();
			МассивПроверки.Добавить(Подр12);
			
			Если НЕ ПечФорма.ПроверитьВывод(МассивПроверки) Тогда
				
				НомерСтраницы = НомерСтраницы + 1;
				СтруктураПараметров.Вставить("НомерСтраницы", НомерСтраницы);
				РегламентированнаяОтчетность.ВывестиНаПечатьЛистОбщая("",
				СтруктураПараметров, ПечФорма, СтруктураРеквизитовФормы.мПечатныеФормы);
				
				ПечФорма.Очистить();
				
			КонецЕсли;
			
			ПечФорма.Вывести(Подр12);
			ПечФормаПодр12 = ПечФорма.Область("Подр12");
			ПечФормаПодр12.Имя = "_" + Формат(ПечФормаПодр12.Верх, "ЧГ=");
			
			ДеревоМнЧ_1 = Ветка.ДанныеМногострочныхЧастей[СтруктураРеквизитовФормы.мИдГруппыР113М1];
			
			Для каждого СтрокаМнЧ_1 Из ДеревоМнЧ_1.Строки Цикл
				
				Дан_1 = СтрокаМнЧ_1.Данные;
				
				Для ИндПарам = 0 По Подр13.Параметры.Количество() - 1 Цикл
					Подр13.Параметры[ИндПарам] = Неопределено;
				КонецЦикла;
				
				Подр13.Параметры.Заполнить(Дан_1);
				
				ИменаПоказателей = СтрРазделить(
				"П00113М100001,П00113М100002,П00113М100004,П00113М100005,П00113М100006,П00113М100007", ",", Ложь);
				ЗначенияВЗнакоместах = Новый Структура;
				Для каждого ИмяПок Из ИменаПоказателей Цикл
					
					КолЗнакомест = 0;
					КолЗнакомест = ?(ИмяПок = "П00113М100001" ИЛИ ИмяПок = "П00113М100005", 2, КолЗнакомест);
					КолЗнакомест = ?(ИмяПок = "П00113М100002" ИЛИ ИмяПок = "П00113М100006", 4, КолЗнакомест);
					КолЗнакомест = ?(ИмяПок = "П00113М100004",  6, КолЗнакомест);
					КолЗнакомест = ?(ИмяПок = "П00113М100007", 14, КолЗнакомест);
					
					ЗначПок = СокрЛП(Дан_1[ИмяПок]);
					Если (ИмяПок = "П00113М100001" ИЛИ ИмяПок = "П00113М100005") И ЗначениеЗаполнено(ЗначПок) Тогда
						ЗначПок = СтроковыеФункцииКлиентСервер.ДополнитьСтроку(ЗначПок, 2, "0", "Слева");
					КонецЕсли;
					Для НомПоз = 1 По КолЗнакомест Цикл
						ЗначенияВЗнакоместах.Вставить(ИмяПок + "_" + НомПоз, Сред(ЗначПок, НомПоз, 1));
					КонецЦикла;
					
				КонецЦикла;
				Подр13.Параметры.Заполнить(ЗначенияВЗнакоместах);
				
				ТипСвед = НРег(СокрЛП(Дан_1["П00113М100003"]));
				Если ТипСвед = "исходные" Тогда
					Подр13.Параметры.Исх = "Х";
				ИначеЕсли ТипСвед = "корректирующие" Тогда
					Подр13.Параметры.Кор = "Х";
				ИначеЕсли ТипСвед = "отменяющие" Тогда
					Подр13.Параметры.Отм = "Х";
				КонецЕсли;
				
				МассивПроверки.Очистить();
				МассивПроверки.Добавить(Подр13);
				
				Если НЕ ПечФорма.ПроверитьВывод(МассивПроверки) Тогда
					
					НомерСтраницы = НомерСтраницы + 1;
					СтруктураПараметров.Вставить("НомерСтраницы", НомерСтраницы);
					РегламентированнаяОтчетность.ВывестиНаПечатьЛистОбщая("",
					СтруктураПараметров, ПечФорма, СтруктураРеквизитовФормы.мПечатныеФормы);
					
					ПечФорма.Очистить();
					
				КонецЕсли;
				
				ПечФорма.Вывести(Подр13);
				ПечФормаПодр13 = ПечФорма.Область("Подр13");
				ПечФормаПодр13.Имя = "_" + Формат(ПечФормаПодр13.Верх, "ЧГ=");
				
				ДеревоМнЧ_2 = СтрокаМнЧ_1.ДанныеМногострочныхЧастей[СтруктураРеквизитовФормы.мИдГруппыР113М2];
				
				Для каждого СтрокаМнЧ_2 Из ДеревоМнЧ_2.Строки Цикл
					
					ЭтоПерваяСтрокаТабл = ДеревоМнЧ_2.Строки.Индекс(СтрокаМнЧ_2) = 0;
					
					ДеревоМнЧ_3 = СтрокаМнЧ_2.ДанныеМногострочныхЧастей[СтруктураРеквизитовФормы.мИдГруппыР113М3];
					ДеревоМнЧ_4 = СтрокаМнЧ_2.ДанныеМногострочныхЧастей[СтруктураРеквизитовФормы.мИдГруппыР113М4];
					ДеревоМнЧ_5 = СтрокаМнЧ_2.ДанныеМногострочныхЧастей[СтруктураРеквизитовФормы.мИдГруппыР113М5];
					ДеревоМнЧ_6 = СтрокаМнЧ_2.ДанныеМногострочныхЧастей[СтруктураРеквизитовФормы.мИдГруппыР113М6];
					
					КолПодстрокТабл = Макс(
					ДеревоМнЧ_3.Строки.Количество(), ДеревоМнЧ_4.Строки.Количество(),
					ДеревоМнЧ_5.Строки.Количество(), ДеревоМнЧ_6.Строки.Количество());
					
					Для НомПодстрокиТабл = 1 По КолПодстрокТабл Цикл
						
						Для ИндПарам = 0 По СтрокаТабл.Параметры.Количество() - 1 Цикл
							СтрокаТабл.Параметры[ИндПарам] = Неопределено;
						КонецЦикла;
						
						Если НомПодстрокиТабл = 1 Тогда
							
							Дан_2 = СтрокаМнЧ_2.Данные;
							СтрокаТабл.Параметры.Заполнить(Дан_2);
							
							ЗначПок = СокрЛП(Дан_2["П00113М200004"]) + "; " + СокрЛП(Дан_2["П00113М200005"]);
							ЗначПок = ?(Лев(ЗначПок, 2) = "; ", Сред(ЗначПок, 3), ЗначПок);
							СтрокаТабл.Параметры.П00113М200004 = ?(Прав(ЗначПок, 2) = "; ", Лев(ЗначПок, СтрДлина(ЗначПок) - 2), ЗначПок);
							
							Если ЗначениеЗаполнено(Дан_2["П00113М200010"]) Тогда
								СтрокаТабл.Параметры.П00113М200011 = "0";
							Иначе
								ЗначПок = СокрЛП(Дан_2["П00113М200011"]) + "; "
								+ СокрЛП(Дан_2["П00113М200012"]) + "; " + СокрЛП(Дан_2["П00113М200013"]);
								ЗначПок = СтрЗаменить(ЗначПок, "; ;", ";");
								ЗначПок = ?(Лев(ЗначПок, 2) = "; ", Сред(ЗначПок, 3), ЗначПок);
								СтрокаТабл.Параметры.П00113М200011 = ?(Прав(ЗначПок, 2) = "; ", Лев(ЗначПок, СтрДлина(ЗначПок) - 2), ЗначПок);
							КонецЕсли;
							
						КонецЕсли;
						
						Если НомПодстрокиТабл <= ДеревоМнЧ_3.Строки.Количество() Тогда
							Дан_3 = ДеревоМнЧ_3.Строки[НомПодстрокиТабл - 1].Данные;
							СтрокаТабл.Параметры.Заполнить(Дан_3);
						КонецЕсли;
						
						Если НомПодстрокиТабл <= ДеревоМнЧ_4.Строки.Количество() Тогда
							Дан_4 = ДеревоМнЧ_4.Строки[НомПодстрокиТабл - 1].Данные;
							СтрокаТабл.Параметры.Заполнить(Дан_4);
						КонецЕсли;
						
						Если НомПодстрокиТабл <= ДеревоМнЧ_5.Строки.Количество() Тогда
							Дан_5 = ДеревоМнЧ_5.Строки[НомПодстрокиТабл - 1].Данные;
							СтрокаТабл.Параметры.Заполнить(Дан_5);
						КонецЕсли;
						
						Если НомПодстрокиТабл <= ДеревоМнЧ_6.Строки.Количество() Тогда
							Дан_6 = ДеревоМнЧ_6.Строки[НомПодстрокиТабл - 1].Данные;
							СтрокаТабл.Параметры.Заполнить(Дан_6);
						КонецЕсли;
						
						МассивПроверки.Очистить();
						Если ЭтоПерваяСтрокаТабл И НомПодстрокиТабл = 1 Тогда
							МассивПроверки.Добавить(ШапкаТабл);
							МассивПроверки.Добавить(ПодшапкаТабл);
						КонецЕсли;
						МассивПроверки.Добавить(СтрокаТабл);
						
						Если НЕ ПечФорма.ПроверитьВывод(МассивПроверки) Тогда
							
							НомерСтраницы = НомерСтраницы + 1;
							СтруктураПараметров.Вставить("НомерСтраницы", НомерСтраницы);
							РегламентированнаяОтчетность.ВывестиНаПечатьЛистОбщая("",
							СтруктураПараметров, ПечФорма, СтруктураРеквизитовФормы.мПечатныеФормы);
							
							ПечФорма.Очистить();
							
							Если НЕ (ЭтоПерваяСтрокаТабл И НомПодстрокиТабл = 1) Тогда
								ПечФорма.Вывести(ПодшапкаТабл);
								ПечФормаПодшапкаТабл = ПечФорма.Область("ПодшапкаТабл");
								ПечФормаПодшапкаТабл.Имя = "_" + Формат(ПечФормаПодшапкаТабл.Верх, "ЧГ=");
							КонецЕсли;
							
						КонецЕсли;
						
						Если ЭтоПерваяСтрокаТабл И НомПодстрокиТабл = 1 Тогда
							ПечФорма.Вывести(ШапкаТабл);
							ПечФормаШапкаТабл = ПечФорма.Область("ШапкаТабл");
							ПечФормаШапкаТабл.Имя = "_" + Формат(ПечФормаШапкаТабл.Верх, "ЧГ=");
							ПечФорма.Вывести(ПодшапкаТабл);
							ПечФормаПодшапкаТабл = ПечФорма.Область("ПодшапкаТабл");
							ПечФормаПодшапкаТабл.Имя = "_" + Формат(ПечФормаПодшапкаТабл.Верх, "ЧГ=");
						КонецЕсли;
						ПечФорма.Вывести(СтрокаТабл);
						ПечФормаСтрокаТабл = ПечФорма.Область("СтрокаТабл");
						ПечФормаСтрокаТабл.Имя = "_" + Формат(ПечФормаСтрокаТабл.Верх, "ЧГ=");
						
					КонецЦикла;
					
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЦикла;
		
		Если ВРег(СокрЛП(ИмяФормы)) = ВРег("ФормаОтчета2024Кв1") Тогда
			
			МассивПроверки.Очистить();
			МассивПроверки.Добавить(Сноска);
			
			Если НЕ ПечФорма.ПроверитьВывод(МассивПроверки) Тогда
				
				НомерСтраницы = НомерСтраницы + 1;
				СтруктураПараметров.Вставить("НомерСтраницы", НомерСтраницы);
				РегламентированнаяОтчетность.ВывестиНаПечатьЛистОбщая("",
				СтруктураПараметров, ПечФорма, СтруктураРеквизитовФормы.мПечатныеФормы);
				
				ПечФорма.Очистить();
				
			КонецЕсли;
			
			ПечФорма.Вывести(Сноска);
			
		КонецЕсли;
		
		НомерСтраницы = НомерСтраницы + 1;
		СтруктураПараметров.Вставить("НомерСтраницы", НомерСтраницы);
		РегламентированнаяОтчетность.ВывестиНаПечатьЛистОбщая("",
		СтруктураПараметров, ПечФорма, СтруктураРеквизитовФормы.мПечатныеФормы);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

Процедура ВставитьДопСтроку(ИмяФормы,
							СтруктураРеквизитовФормы,
							ТабличныйДокумент,
							Знач ИмяГруппы,
							Знач ИмяОбластиПреемника,
							ЭтоАвтоВставкаПодчиненнойМнЧ = Ложь) Экспорт
	
	НачИндекс = "_1";
	
	Если СтрНайти(ИмяОбластиПреемника, "ДобавлениеСтроки" + ИмяГруппы) > 0 Тогда
		
		ИскомаяОбласть = ТабличныйДокумент.Области.Найти("МногострочнаяЧасть" + ИмяГруппы);
		
		ВремТаблДок = ТабличныйДокумент.ПолучитьОбласть(ИскомаяОбласть.Имя);
		ВставляемаяОбласть = ВремТаблДок.Область(ИскомаяОбласть.Имя);
		ВставляемаяОбласть.Имя = "";
		
		НовКоординаты = РегламентированнаяОтчетностьКлиентСервер.СтрокаСКоординатамиПоказателя(ИмяОбластиПреемника);
		
		КолУровнейПодчиненности = СтрЧислоВхождений(НовКоординаты, "_");
		НачКоординаты = "";
		Для НомУр = 1 По КолУровнейПодчиненности Цикл
			НачКоординаты = НачКоординаты + НачИндекс;
		КонецЦикла;
		
		ПреИндекс_Число = РегламентированнаяОтчетностьКлиентСервер.ИндексПоказателя(ИмяОбластиПреемника);
		НовИндекс_Число = ?(ЭтоАвтоВставкаПодчиненнойМнЧ, 1, ПреИндекс_Число + 1);
		
		ПреИндекс = РегламентированнаяОтчетностьКлиентСервер.СтрокаСИндексомПоказателя(ПреИндекс_Число);
		НовИндекс = РегламентированнаяОтчетностьКлиентСервер.СтрокаСИндексомПоказателя(НовИндекс_Число);
		
		НачПостфикс = ИмяГруппы + НачКоординаты + НачИндекс;
		ПреПостфикс = ИмяГруппы + НовКоординаты + ПреИндекс;
		НовПостфикс = ИмяГруппы + НовКоординаты + НовИндекс;
		
		МассивИменГраф = СтруктураРеквизитовФормы.СтруктураМногострочныхЧастей[ИмяГруппы]["ИменаГраф"];
		
		Для каждого ИмяГрафы Из МассивИменГраф Цикл
			
			НачИмяОбл = ИмяГрафы + НачКоординаты + НачИндекс;
			НовИмяОбл = ИмяГрафы + НовКоординаты + НовИндекс;
			
			ОбластьЯчейки = ВремТаблДок.Области[НачИмяОбл];
			ОбластьЯчейки.Имя = НовИмяОбл;
			
			Если ОбластьЯчейки.СодержитЗначение = Истина Тогда
				ОбластьЯчейки.Очистить();
			КонецЕсли;
			
		КонецЦикла;
		
		Обл = ВремТаблДок.Области.Найти("УдалитьСтроку" + НачПостфикс);
		Если Обл <> Неопределено Тогда
			Обл.Имя = "УдалитьСтроку" + НовПостфикс;
		КонецЕсли;
		
		Если ЭтоАвтоВставкаПодчиненнойМнЧ Тогда
			Обл = ВремТаблДок.Области.Найти("УдалитьСтроку" + НовПостфикс);
			Если Обл <> Неопределено Тогда
				Обл.Гиперссылка = Ложь;
				Обл.Текст = "";
			КонецЕсли;
		Иначе
			Обл = ВремТаблДок.Области.Найти("УдалитьСтроку" + НовПостфикс);
			Если Обл <> Неопределено Тогда
				Обл.Гиперссылка = Истина;
				Обл.Текст = "х";
				Если НовИндекс_Число = 2 Тогда
					Если Обл <> Неопределено Тогда
						Обл = ТабличныйДокумент.Области.Найти("УдалитьСтроку" + ПреПостфикс);
						Обл.Гиперссылка = Истина;
						Обл.Текст = "х";
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Обл = ТабличныйДокумент.Области.Найти("ДобавитьСтроку" + ПреПостфикс);
		Если Обл <> Неопределено Тогда
			Обл.Имя = "ДобавитьСтроку" + НовПостфикс;
		КонецЕсли;
		Обл = ТабличныйДокумент.Области.Найти("ДобавитьСтрокуЗначок" + ПреПостфикс);
		Если Обл <> Неопределено Тогда
			Обл.Имя = "ДобавитьСтрокуЗначок" + НовПостфикс;
		КонецЕсли;
		
		ОблПреемник = ТабличныйДокумент.Области.Найти(ИмяОбластиПреемника);
		ОблПреемник.Имя = "ДобавлениеСтроки" + НовПостфикс;
		
		// Изменения в оформлении ячеек: установка значения "НетЛинии"/"Сплошная" для
		// свойства "ГраницаСнизу" (в случае задания номеров специальных колонок):
		НомераСпецКолонок = Неопределено;
		СтруктураРеквизитовФормы.СтруктураМногострочныхЧастей[ИмяГруппы].Свойство(
			"КолонкиСИзмНижнГраницей", НомераСпецКолонок);
		Если НомераСпецКолонок <> Неопределено Тогда
			Для каждого НомКолонки Из НомераСпецКолонок Цикл
				// Удаление линии в определенных колонках последней (до добавления) строки.
				Если НЕ ЭтоАвтоВставкаПодчиненнойМнЧ Тогда
					Обл = ТабличныйДокумент.Область(
						ОблПреемник.Верх - 1, НомКолонки, ОблПреемник.Верх - 1, НомКолонки);
					Если Обл <> Неопределено Тогда
						Обл.ГраницаСнизу = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.НетЛинии);
					КонецЕсли;
				КонецЕсли;
				// Установка линии в определенных колонках добавляемой строки.
				Обл = ВремТаблДок.Область(
					ВставляемаяОбласть.Низ, НомКолонки, ВставляемаяОбласть.Низ, НомКолонки);
				Если Обл <> Неопределено Тогда
					Обл.ГраницаСнизу = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		ОблПреемник_ = ТабличныйДокумент.Область(
			ОблПреемник.Верх, , ОблПреемник.Верх + ИскомаяОбласть.Низ - ИскомаяОбласть.Верх, );
		ТабличныйДокумент.ВставитьОбласть(
			ВставляемаяОбласть, ОблПреемник_, ТипСмещенияТабличногоДокумента.ПоВертикали);
		
		Если НЕ ЭтоАвтоВставкаПодчиненнойМнЧ Тогда
			// Установка текущей области после добавления строки.
			НовИмяТекОбл = МассивИменГраф[0] + НовКоординаты + НовИндекс;
			НовТекОбл = ТабличныйДокумент.Области[НовИмяТекОбл];
			ТабличныйДокумент.ТекущаяОбласть = НовТекОбл;
		КонецЕсли;
		
	КонецЕсли;
	
	ИменаПодчиненныхГрупп = СтруктураРеквизитовФормы.СтруктураМногострочныхЧастей[
		ИмяГруппы]["ИменаПодчиненныхГрупп"];
	Для каждого ИмяПодчГруппы Из ИменаПодчиненныхГрупп Цикл
		НовКоординатыДляПодчОбласти = НовКоординаты + НовИндекс;
		
		КолУровнейПодчиненности = СтрЧислоВхождений(НовКоординатыДляПодчОбласти, "_");
		НачКоординатыДляПодчОбласти = "";
		Для НомУр = 1 По КолУровнейПодчиненности Цикл
			НачКоординатыДляПодчОбласти = НачКоординатыДляПодчОбласти + НачИндекс;
		КонецЦикла;
		
		НачИндВставляемойОбласти = НачИндекс;
		
		ИскомаяОбласть = ТабличныйДокумент.Области.Найти(
			"ДобавлениеСтроки" + ИмяПодчГруппы + НачКоординатыДляПодчОбласти + НачИндВставляемойОбласти);
		
		Если ИскомаяОбласть = Неопределено Тогда
			НачИндВставляемойОбласти = 1;
			Пока ТабличныйДокумент.Области.Найти("ДобавлениеСтроки" + ИмяПодчГруппы
				+ НачКоординатыДляПодчОбласти + "_" + Формат(НачИндВставляемойОбласти, "ЧГ=")) = Неопределено Цикл
				НачИндВставляемойОбласти = НачИндВставляемойОбласти + 1;
			КонецЦикла;
			НачИндВставляемойОбласти = "_" + Формат(НачИндВставляемойОбласти, "ЧГ=");
			ИскомаяОбласть = ТабличныйДокумент.Области.Найти(
				"ДобавлениеСтроки" + ИмяПодчГруппы + НачКоординатыДляПодчОбласти + НачИндВставляемойОбласти);
		КонецЕсли;
		
		ВремТабличныйДокумент = ТабличныйДокумент.ПолучитьОбласть(ИскомаяОбласть.Имя);
		ВставляемаяОбласть = ВремТабличныйДокумент.Область(ИскомаяОбласть.Имя);
		Для каждого ОбластьЯчейки Из ВремТабличныйДокумент.Области Цикл
			ОбластьЯчейки.Имя = СтрЗаменить(ОбластьЯчейки.Имя,
				ИмяПодчГруппы + НачКоординатыДляПодчОбласти + НачИндВставляемойОбласти,
				ИмяПодчГруппы + НовКоординатыДляПодчОбласти + НачИндекс);
		КонецЦикла;
		ИмяОбластиПреемникаДляПодчОбласти = СтрЗаменить(ИмяОбластиПреемника, ПреПостфикс, НовПостфикс);
		ОбластьПреемник = ТабличныйДокумент.Области.Найти(ИмяОбластиПреемникаДляПодчОбласти);
		
		ТабличныйДокумент.ВставитьОбласть(ВставляемаяОбласть, ОбластьПреемник, ТипСмещенияТабличногоДокумента.ПоВертикали);
		
		ИмяОбластиПреемникаДляПодчОбласти = "ДобавлениеСтроки" + ИмяПодчГруппы + НовКоординатыДляПодчОбласти + НачИндекс;
		
		ВставитьДопСтроку(ИмяФормы,
						  СтруктураРеквизитовФормы,
						  ТабличныйДокумент,
						  ИмяПодчГруппы,
						  ИмяОбластиПреемникаДляПодчОбласти,
						  Истина);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли