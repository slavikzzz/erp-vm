#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Организация = Параметры.Организация;
	НачалоПериода = Параметры.НачалоПериода;
	КонецПериода = Параметры.КонецПериода;
	НаправлениеДеятельности = Параметры.НаправлениеДеятельности;
	ЗаголовокПоказателя = Параметры.ЗаголовокПоказателя;
	КодПредставление = Параметры.КодПредставление;
	КодПоказателя = Параметры.КодПоказателя;
	Графа = Параметры.Графа;
	АдресВременногоХранилищаРасшифровки = Параметры.АдресВременногоХранилищаРасшифровки;

	ДанныеРасшифровки = ПолучитьИзВременногоХранилища(АдресВременногоХранилищаРасшифровки);
	ДанныеОборотов = ДанныеРасшифровки.ДанныеОборотов;
	
	Заполнить(ДанныеОборотов);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТабличныйДокументОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	
	СтандартнаяОбработка = Ложь;	
	ИсточникДанных = Расшифровка.ИсточникДанных;
	ИндексОборота = Расшифровка.ИндексОборота;

	Если ИсточникДанных = "КассаФакт" Тогда
		ИмяФормыРасшифровки = "Отчет.ПодтверждениеРасходовПриКазначейскомСопровожденииРасшифровка.Форма";
	ИначеЕсли ИсточникДанных = "ДанныеБух" Тогда
		ИмяФормыРасшифровки = "Отчет.ОборотыСчета.Форма.ФормаОтчета";
	КонецЕсли;

	ПараметрыФормыРасшифровки = ПараметрыФормыРасшифровкиОборота(ИсточникДанных, ИндексОборота);
	
	ОткрытьФорму(ИмяФормыРасшифровки, ПараметрыФормыРасшифровки,, Истина)

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПараметрыФормыРасшифровкиОборота(ИсточникДанных, ИндексОборота)

	ПараметрыФормы = Новый Структура;

	ДанныеРасшифровки = ПолучитьИзВременногоХранилища(АдресВременногоХранилищаРасшифровки);
	ПравилаПоПоказателю = ДанныеРасшифровки.ПравилаПоПоказателю;
	
	Правило = ПравилаПоПоказателю.Найти(ИсточникДанных, "ИсточникДанных");
	Если Правило <> Неопределено Тогда
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("КодПоказателя", КодПоказателя);
		ДополнительныеПараметры.Вставить("КодПредставление", КодПредставление);
		ДополнительныеПараметры.Вставить("ЗаголовокПоказателя", ЗаголовокПоказателя);
		ДополнительныеПараметры.Вставить("Графа", Графа);
		
		ОтборОборотов = Правило.ОтборОборотов; //СписокЗначений
		СтруктураОтбораОборотов = ОтборОборотов.Получить(ИндексОборота).Значение;
		
		Если ИсточникДанных = "КассаФакт" Тогда
			ПараметрыФормы = Отчеты.ПодтверждениеРасходовПриКазначейскомСопровождении.ПараметрыФормыРасшифровкиПоказателяКассы(ЭтаФорма, СтруктураОтбораОборотов, ДополнительныеПараметры);
		ИначеЕсли ИсточникДанных = "ДанныеБух" Тогда
			ПараметрыФормы = Отчеты.ПодтверждениеРасходовПриКазначейскомСопровождении.ПараметрыФормыРасшифровкиПоказателяБух(ЭтаФорма, СтруктураОтбораОборотов);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ПараметрыФормы;
	
КонецФункции

&НаСервере
Процедура Заполнить(ДанныеОборотов)
	
	Макет = Отчеты.ПодтверждениеРасходовПриКазначейскомСопровождении.ПолучитьМакет("РасшифровкаОборотов");
	
	СекцияЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	СекцияИтог      = Макет.ПолучитьОбласть("Итог");
	СекцияПлюс      = Макет.ПолучитьОбласть("Плюс");
	СекцияМинус     = Макет.ПолучитьОбласть("Минус");
	СекцияСлагаемое = Макет.ПолучитьОбласть("Слагаемое");
	
	ШаблонЗаголовка = НСтр("ru = 'Показатель %1 ""%2"", графа ""%3"" - расшифровка значения';
							|en = 'Indicator %1 ""%2"", column ""%3"" - value details'");
	ЗаголовокРасшифровки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонЗаголовка, КодПредставление, ЗаголовокПоказателя, Графа);
	ЗаголовокРасшифровки = СтрЗаменить(ЗаголовокРасшифровки, Символы.ПС, "");
	
	ПолеРасшифровки.Вывести(СекцияЗаголовок);
	ПолеРасшифровки.Области.НаименованиеЗаголовок.Значение = ЗаголовокРасшифровки;

	ИтоговаяСумма = 0;
	Для Каждого Строка Из ДанныеОборотов Цикл
		
		Если Строка.Множитель = "-" Тогда
			ПолеРасшифровки.Вывести(СекцияМинус);
		Иначе
			ПолеРасшифровки.Вывести(СекцияПлюс);
		КонецЕсли;
		
		ПолеРасшифровки.Вывести(СекцияСлагаемое);
		
		ПолеРасшифровки.Области.СлагаемоеНаименование.Значение = Строка.Оборот;
		ПолеРасшифровки.Области.СлагаемоеСумма.Значение = Строка.Сумма;
		
		РасшифровкаОбласти = Новый Структура;
		РасшифровкаОбласти.Вставить("ИсточникДанных", Строка.ИсточникДанных);
		РасшифровкаОбласти.Вставить("ИндексОборота", Строка.ИндексОборота);
		ПолеРасшифровки.Области.СлагаемоеСумма.Расшифровка = РасшифровкаОбласти;
		
		ИтоговаяСумма = ИтоговаяСумма + Строка.Сумма;
		
	КонецЦикла;
	
	ПолеРасшифровки.Вывести(СекцияИтог);
	ПолеРасшифровки.Области.ИтогСумма.Значение = ИтоговаяСумма;

КонецПроцедуры

#КонецОбласти