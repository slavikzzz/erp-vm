#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

Перем мПолноеИмяФайлаВнешнейОбработки Экспорт; // для варианта внешнего отчета
Перем мВерсияОтчета Экспорт; // для варианта внешнего отчета

// Структура реквизитов формы отчета.
Перем СтруктураРеквизитовФормы;

// Соответствие, содержащее кеш проверенных по классификатору адресов.
// Процесс проверки может включать http запрос и занимать много времени.
// При этом адреса часто повторяются.
Перем СоответствиеКешАдресов;

// Массивы кодов из макета Списки.
Перем СписокКодовДеятельности, СписокКодовПродукции;
// Структура адреса из схемы xsd, если отличается от стандартной.
Перем ОписаниеСтруктурыАдресаИзОтчета;
// Признак режима отладки.
Перем РежимОтладки;
// Предельное количество ошибок, по достижении которого дальнейшая проверка прекращается.
Перем МаксимальноеКоличествоОшибок, ТекущееКоличествоОшибок;
Перем МаксимальноеКоличествоПредупреждений, ТекущееКоличествоПредупреждений;
// Дата начала и конца отчета.
Перем ДатаНачалаОтчета, ДатаКонцаОтчета;
// Границы дат по xsd.
Перем НижняяГраницаДатыПоXSD, ВерхняяГраницаДатыПоXSD;

// Данные обособленных подразделений разделов.
Перем ДанныеОП;

// Справочники.
Перем СправочникиОтчета;

// Документ.РегламентированныйОтчет к которому привязан отчет.
Перем ДокументСсылкаАлкоОтчет;

// Структура получаемая функцией ПолучитьСтруктуруСервисРегистров()
// модуля формы отчета.
Перем СтруктураСервисРегистров;

// Объект метаданных отчета.
Перем ОбъектЭтогоОтчета;

// Переменные, полученные из реквизитов формы отчета.
Перем СтруктураДанныхТитульный, мДеревоВыбранныхСтраниц, ДополнительныеДанные,
	НомерКорректировки, ДатаПодписи;

// Переменные, используемые при проверке и выгрузке отчета.
Перем ДеревоВыгрузки, ПараметрыВыгрузки, СтатусПроверки, ИмяФормыОбъекта;

// Переменные, полученные из реквизитов формы отчета.
Перем ТаблицаСтраницыЛицензии, ТаблицаСтраницыДекларация;

// Таблица значений, в которую записываются найденные при проверке выгрузки ошибки.
Перем ТаблицаСообщений;

// Таблица значений, в который записываются информационные сообщения при проверке выгрузки ошибки.
Перем ТаблицаСообщенийДляСведения;

// Счетчики для справочников отчета.
Перем СчетчикИдПроизвИмп, СчетчикИдПолучателей, СчетчикИдЛицензий;

// Данные движений продукции с привязкой к производителю-импортеру.
Перем ДанныеДвижений;

// Вспомогательные переменные.
Перем ВсегоСтрокПоРазделу1, ВсегоСтрокПоРазделу2, НужноОбработатьСтрок,
	ОбработаноСтрок, КолСтрокПриПроверкеЗаполнения;

// Содержит процент выполнения процесса проверки или выгрузки, который соответсвует проверке заполнения разделов.
// Для процесса проверки с ошибками - 100%, проверка без ошибок - 90%, при выгрузке без ошибок - 84%.
Перем ПроцентБезЗавершающейПроверкиДанных, ПроцентБезКонвертацииАрхивации;

// Адрес временного хранилища для обмена вспомогательной информацией (процент выполнения, этап).
Перем ВспомогательныйАдрес;
// Содержит шаг информирования о проценте выполнения процесса.
Перем ШагИнформирования;

// Признак отсутствия ошибок при проверке.
Перем НетОшибокПроверки;

// Версия БСП.
Перем ВерсияБСПЧислом;

#КонецОбласти

#Область ПрограммныйИнтерфейс

#Область ОбработкаПриЗаполнении

// В зависимости от имени регистра обрабатывает поля ресурсов, измерений, реквизитов записи
// или поля структуры, строки таблицы значений и вычисляет значения при необходимости.
// Используется при импорте таблиц значений в регистр сведений отчета.
// Совпадает с процедурой ОбработкаЗаписи(), но сделана отдельной для возможности при необходимости
// поменять работу заполнения регистра только лишь поменяв отчет, не трогая глобальные модули.
// Это нужно для поддержки выпуска внешних отчетов в случае серьезных ошибок.
//
// Параметры:
//
//	 	ИмяРегистра 				- Строка - Имя регистра, запись которого обрабатывается.
// 		СтрокаТЗ					- Запись регистра сведений, строка таблицы значений или Структура. 
//									Должна соответствовать измерениям, ресурсам и реквизитам регистра 
//									сведений с именем ИмяРегистра.
//		ВышеБРО_1_1_11_0			- Булево. Признак, что текущая версия БРО выше 1.1.11.0
//		ДатаКонцаПериодаОтчета		- Дата.
//
Процедура ОбработкаСтрокиТаблицыСтарогоФормата(ИмяРегистра, СтрокаТЗ,
		ВышеБРО_1_1_11_0 = Неопределено, ДатаКонцаПериодаОтчета = Неопределено) Экспорт

	ОбработкаЗаписи(ИмяРегистра, СтрокаТЗ, ВышеБРО_1_1_11_0, ДатаКонцаПериодаОтчета);

КонецПроцедуры
#КонецОбласти

#Область ОбработкаПриЗаписиИзФормЗаписиРегистров

// В зависимости от имени регистра обрабатывает поля ресурсов, измерений, реквизитов записи
// или поля структуры, строки таблицы значений и вычисляет значения при необходимости.
//
// Параметры:
//
//	 	ИмяРегистра 				- Строка - Имя регистра, запись которого обрабатывается.
// 		Запись 						- Запись регистра сведений, строка таблицы значений или Структура. 
//									Должна соответствовать измерениям, ресурсам и реквизитам регистра 
//									сведений с именем ИмяРегистра.
//		ВышеБРО_1_1_11_0			- Булево. Признак, что текущая версия БРО выше 1.1.11.0
//		ДатаКонцаПериодаОтчета		- Дата.
//
Процедура ОбработкаЗаписи(ИмяРегистра, Запись,
		ВышеБРО_1_1_11_0 = Неопределено, ДатаКонцаПериодаОтчета = Неопределено) Экспорт

	Если ВышеБРО_1_1_11_0 = Неопределено Тогда
		ВышеБРО_1_1_11_0 = РегламентированнаяОтчетностьАЛКОВызовСервера.СравнениеВерсииПодсистемы("РегламентированнаяОтчетность", "1.1.11.0");
	КонецЕсли;

	Если ЗначениеЗаполнено(Запись.Документ) И ВышеБРО_1_1_11_0 Тогда
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Запись.Документ, "Организация");
		Запись.Организация = ЗначенияРеквизитов.Организация;
	КонецЕсли;

	Если ИмяРегистра = "СведенияРеглОтчетАлкоПрил21Раздел1Поставки" 
		ИЛИ ИмяРегистра = "СведенияРеглОтчетАлкоПрил21Раздел2Возвраты" Тогда

		ИдГруппы = ?(ИмяРегистра = "СведенияРеглОтчетАлкоПрил21Раздел1Поставки", "П0000100003", "П0000200003");
		ИдГруппыСокр = ?(ИмяРегистра = "СведенияРеглОтчетАлкоПрил21Раздел1Поставки", "П00001", "П00002");
		
		// Производитель
		Запись[ИдГруппы + "02"] = СокрЛП(Запись[ИдГруппы + "02"]);
		КодПродукции = Запись[ИдГруппы + "02"];

		Запись[ИдГруппы + "03"] = СокрЛП(Запись[ИдГруппы + "03"]);
		Наименование = Запись[ИдГруппы + "03"];

		Запись[ИдГруппы + "04"] = СтрЗаменить(Запись[ИдГруппы + "04"], " ", "");
		ИНН = Запись[ИдГруппы + "04"];

		Запись[ИдГруппы + "05"] = СтрЗаменить(Запись[ИдГруппы + "05"], " ", "");
		КПП = Запись[ИдГруппы + "05"];

		Если НЕ ЗначениеЗаполнено(Запись.ПроизводительИмпортер) Тогда
			
			Запись.ПроизводительИмпортер = ОпределитьПроизводителяИмпортера(ИНН, КПП, Наименование, КодПродукции);
			
			Если ЗначениеЗаполнено(Запись.ПроизводительИмпортер) Тогда
				Запись[ИдГруппы + "03"] = Наименование;
				Запись[ИдГруппы + "05"] = КПП;
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Запись.ПроизводительИмпортер) Тогда
			
			// Заполним адрес если нет представления адреса.
			// Если есть - адресные данные XML либо введены в форме записи регистра,
			// либо при импорте данных старого формата формы будут импортированы
			// из Дополнительных данных страниц отчета.
			СтруктураРезультата = РегламентированнаяОтчетностьАЛКО.ОбработкаСохраненияАдресаКонтрагента(
									Запись.ПроизводительИмпортер, 
									Запись[ИдГруппы + "03АдресXML"], 									
									ДатаКонцаПериодаОтчета,
									Истина);
			
			Запись[ИдГруппы + "03АдресXML"] 	= СтруктураРезультата.ПолеСтруктурыАдреса;
			Запись[ИдГруппы + "03Адрес"]		= СтруктураРезультата.Представление;
			
		Иначе 
			РегламентированнаяОтчетностьАЛКО.ОбработкаСохраненияАдреса(
							Запись[ИдГруппы + "03АдресXML"], Запись[ИдГруппы + "03Адрес"]);			
		КонецЕсли;
		
		СтруктураХешИдПроизводителя = РегламентированнаяОтчетностьАЛКО.ПолучитьВсеИдПроизводителя(
							КодПродукции, Наименование, ИНН, КПП);
		ЗаполнитьЗначенияСвойств(Запись, СтруктураХешИдПроизводителя);

		// Получатель
		Запись[ИдГруппы + "06"] = СокрЛП(Запись[ИдГруппы + "06"]);
		Наименование = Запись[ИдГруппы + "06"];

		Запись[ИдГруппы + "08"] = СтрЗаменить(Запись[ИдГруппы + "08"], " ", "");
		ИНН = Запись[ИдГруппы + "08"];

		Запись[ИдГруппы + "09"] = СтрЗаменить(Запись[ИдГруппы + "09"], " ", "");
		КПП = Запись[ИдГруппы + "09"];

		Если НЕ ЗначениеЗаполнено(Запись.Получатель) Тогда
			
			Запись.Получатель = ОпределитьПолучателя(ИНН, КПП, Наименование);
			
			Если ЗначениеЗаполнено(Запись.Получатель) Тогда
				Запись[ИдГруппы + "06"] = Наименование;
				Запись[ИдГруппы + "09"] = КПП;
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Запись.Получатель) Тогда
			
			// Заполним адрес если нет представления адреса.
			// Если есть - адресные данные XML либо введены в форме записи регистра,
			// либо при импорте данных старого формата формы будут импортированы
			// из Дополнительных данных страниц отчета.
			СтруктураРезультата = РегламентированнаяОтчетностьАЛКО.ОбработкаСохраненияАдресаКонтрагента(
									Запись.Получатель, 
									Запись[ИдГруппы + "07XML"], 									
									ДатаКонцаПериодаОтчета,
									Истина);
			
			Запись[ИдГруппы + "07XML"] 	= СтруктураРезультата.ПолеСтруктурыАдреса;
			Запись[ИдГруппы + "07"]		= СтруктураРезультата.Представление;
			
		Иначе 
			РегламентированнаяОтчетностьАЛКО.ОбработкаСохраненияАдреса(
							Запись[ИдГруппы + "07XML"], Запись[ИдГруппы + "07"]);			
		КонецЕсли;

		// Лицензия
		
		Запись[ИдГруппыСокр + "СерияНомер"] = СокрЛП(Запись[ИдГруппыСокр + "СерияНомер"]);
		СерияНомер = Запись[ИдГруппыСокр + "СерияНомер"];
		
		Запись[ИдГруппыСокр + "КемВыдана"] = СокрЛП(Запись[ИдГруппыСокр + "КемВыдана"]);
		КемВыдана = Запись[ИдГруппыСокр + "КемВыдана"];
		
		Запись[ИдГруппы + "10"] = СокрЛП(Запись[ИдГруппы + "10"]);
		ВидДеятельности = Запись[ИдГруппы + "10"];
		
		ДатаВыдачи = Запись[ИдГруппыСокр + "ДатаВыдачи"];
		ДатаОкончания = Запись[ИдГруппыСокр + "ДатаОкончания"];
		
		СтруктураСведений = Новый Структура;

		СтруктураСведений.Вставить("Наименование", Наименование);
		СтруктураСведений.Вставить("ИНН", ИНН);
		СтруктураСведений.Вставить("КПП", КПП);
		СтруктураСведений.Вставить("ПредставлениеАдреса", Запись[ИдГруппы + "07"]);
		СтруктураСведений.Вставить("Номер", "");
		СтруктураСведений.Вставить("Серия", "");
		СтруктураСведений.Вставить("ДатаВыдачи", "");
		СтруктураСведений.Вставить("ДатаОкончания", "");
		СтруктураСведений.Вставить("КемВыдана", ВидДеятельности);

		СтруктураХешИдПолучателя = РегламентированнаяОтчетностьАЛКО.ПолучитьВсеИдПоставщика(СтруктураСведений);
		// Идентифицируем получателя как набор Наименование, ИНН, КПП, Адрес и ВидДеятельности по лицензии.
		Запись.ХешИдПолучателя = СтруктураХешИдПолучателя.ХешИдЛицензии;
		
		СтруктураСведений.Вставить("Наименование", Наименование);
		СтруктураСведений.Вставить("ИНН", ИНН);
		СтруктураСведений.Вставить("КПП", КПП);
		СтруктураСведений.Вставить("ПредставлениеАдреса", Запись[ИдГруппы + "07"]);
		СтруктураСведений.Вставить("Номер", СерияНомер);
		СтруктураСведений.Вставить("Серия", "");
		СтруктураСведений.Вставить("ДатаВыдачи", ДатаВыдачи);
		СтруктураСведений.Вставить("ДатаОкончания", ДатаОкончания);
		СтруктураСведений.Вставить("КемВыдана", КемВыдана);

		СтруктураХешИдПолучателя = РегламентированнаяОтчетностьАЛКО.ПолучитьВсеИдПоставщика(СтруктураСведений);
		
		Запись.ХешИдЛицензии = СтруктураХешИдПолучателя.ХешИдЛицензии;

	КонецЕсли;
	
	Если ИмяРегистра = "СведенияРеглОтчетАлкоЛицензии" Тогда		
		РегламентированнаяОтчетностьАЛКО.ОбработкаЗаписиРегистраАлкоЛицензии(Запись);		
	КонецЕсли;

КонецПроцедуры

Функция ОпределитьПроизводителяИмпортера(ИНН, КПП, ПолноеНаименование,
		КодПродукции) Экспорт

	Возврат РегламентированнаяОтчетностьАЛКО.ОпределитьПроизводителяИмпортераАЛКО(ИНН, КПП, ПолноеНаименование, КодПродукции);
КонецФункции

Функция ОпределитьПолучателя(ИНН, КПП, ПолноеНаименование) Экспорт

	Возврат РегламентированнаяОтчетностьАЛКО.ОпределитьКонтрагентаИзЛицензийПоставщиковАЛКО(ИНН, КПП, ПолноеНаименование);
КонецФункции
#КонецОбласти

#Область Расчет

// Производит расчет "по строке" полей ресурсов Записи. 
//
// Параметры:
//
//	 	ИдГруппы 				- Строка - Имя табличной группы, к которой относится регистр сведений.
// 		Запись 					- Запись регистра сведений	- Запись, которая обрабатывается.
//		КварталОтчета			- Число - Номер квартала отчета. Нужен в некоторых отчетах, в остальных
//								параметр для унификации вызова процедуры Расчет для любых отчетов
//								из процедур модуля РегламентированнаяОтчетностьАЛКО.
//
Процедура Расчет(ИдГруппы, Запись, КварталОтчета = Неопределено, ДатаКонцаПериодаОтчета = Неопределено) Экспорт
	// Для Приложения 4 расчета по строке нет.
	Возврат;
КонецПроцедуры
#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Служебные

Процедура СообщитьОбОшибкеЗаполнения(ДокументСсылка,
		ТаблицаСообщенийОбОшибках, СтатусПроверкиОтчета = Ложь, СтруктураОшибки,
		МаксимальноеКоличествоОшибок = Неопределено, ТекущееКоличествоОшибок = Неопределено)

	РегламентированнаяОтчетностьАЛКО.СообщитьОбОшибкеЗаполнения(
		ДокументСсылка, ТаблицаСообщенийОбОшибках, СтатусПроверкиОтчета, СтруктураОшибки, РежимОтладки,
		МаксимальноеКоличествоОшибок, ТекущееКоличествоОшибок);

КонецПроцедуры

Функция ПолучитьИмяРегистраСведений(ИдГруппы)

	Возврат РегламентированнаяОтчетностьАЛКОКлиентСервер.ПолучитьИмяРегистраСведенийАЛКО(ИдГруппы, СтруктураРеквизитовФормы);

КонецФункции

Функция ПолучитьКоличествоСтрокПоСтраницеГруппы(ИдГруппы, ИндексСтраницы, БезПомеченныхНаУдаление = Истина)
	
	ИмяРегистраСведений = ПолучитьИмяРегистраСведений(ИдГруппы);
	СервисРегистровГруппы = СтруктураСервисРегистров[ИмяРегистраСведений];
		 
	Возврат РегламентированнаяОтчетностьАЛКОКлиентСервер.ПолучитьКоличествоСтрокПоСтраницеИзСервисРегистровГруппы(
						СервисРегистровГруппы, ИндексСтраницы, БезПомеченныхНаУдаление);
	
КонецФункции

Функция ПолучитьКоличествоСтрокПоГруппе(ИдГруппы, БезПомеченныхНаУдаление = Истина)
	
	ИмяРегистраСведений = ПолучитьИмяРегистраСведений(ИдГруппы);
	СервисРегистровГруппы = СтруктураСервисРегистров[ИмяРегистраСведений];			
	
	Возврат РегламентированнаяОтчетностьАЛКОКлиентСервер.ПолучитьКоличествоСтрокПоГруппеИзСервисРегистровГруппы(
						СервисРегистровГруппы, БезПомеченныхНаУдаление);
	
КонецФункции
					
Функция ЗначениеПоКлючуСтруктуры(Ключ, Структура)

	Возврат РегламентированнаяОтчетностьАЛКОКлиентСервер.ЗначениеПоКлючуСтруктуры(Ключ, Структура);

КонецФункции

Функция ИзвлечьСтруктуруXML()

	Возврат РегламентированнаяОтчетностьАЛКО.ИзвлечьСтруктуруXMLАЛКО(ДатаКонцаОтчета, ИмяФормыОбъекта, ОбъектЭтогоОтчета);

КонецФункции
#КонецОбласти


#Область СлужебныеРасчет


#КонецОбласти

#Область ПроверитьВыгрузку

Функция ПроверитьЗаполнениеТитульный(СтатусПроверки)
	
	СтруктураПараметров = Новый Структура;
	
	СтруктураПараметров.Вставить("ОписаниеСтруктурыАдресаИзОтчета", ОписаниеСтруктурыАдресаИзОтчета);
	
	СтруктураПараметров.Вставить("МаксимальноеКоличествоОшибок", МаксимальноеКоличествоОшибок);
	СтруктураПараметров.Вставить("ТекущееКоличествоОшибок", ТекущееКоличествоОшибок);
	СтруктураПараметров.Вставить("МаксимальноеКоличествоПредупреждений", МаксимальноеКоличествоПредупреждений);
	СтруктураПараметров.Вставить("ТекущееКоличествоПредупреждений", ТекущееКоличествоПредупреждений);
	

	Результат = РегламентированнаяОтчетностьАЛКО.ВерноеЗаполнениеТитульный(
					ДокументСсылкаАлкоОтчет, ТаблицаСообщений, ТаблицаСообщенийДляСведения, 
					СтатусПроверки, ПараметрыВыгрузки, СтруктураПараметров, РежимОтладки, СоответствиеКешАдресов);
								
	СтруктураПараметров.Свойство("ТекущееКоличествоОшибок", ТекущееКоличествоОшибок);
	СтруктураПараметров.Свойство("ТекущееКоличествоПредупреждений", ТекущееКоличествоПредупреждений);
			
	Возврат Результат;

КонецФункции

Функция ЛицензируемаяДеятельность()
	
	Возврат РегламентированнаяОтчетностьАЛКО.ЛицензируемаяДеятельностьАЛКО(ТаблицаСтраницыЛицензии, "П000000000105");

КонецФункции

Функция УказанаЛицензия(ДанныеЛиста)

	Возврат РегламентированнаяОтчетностьАЛКО.УказанаЛицензия(ДанныеЛиста, "П000000000105");

КонецФункции

Процедура ПроверитьЗаполнениеЛицензии(СтатусПроверки)

	СтруктураСведенийРазделаЛицензий = Новый Структура;

	// Заполняются ли в разделе лицензии декларанта - Истина, или только адреса мест деятельности - Ложь.
	СтруктураСведенийРазделаЛицензий.Вставить("ЕстьЛицензииВРазделеЛицензии", Истина);

	// Если не передавать - по умолчанию будет установлен "П000000000105".
	СтруктураСведенийРазделаЛицензий.Вставить("ИмяПоляКодВидаДеятельности", "П000000000105");

	// Нижняя и верхняя границы даты по схеме XSD. 
	// Если не задана - по умолчанию устанавливается в '1000.01.01 00:00.00' и '2099.12.31 23:59.59' соответственно.	
	СтруктураСведенийРазделаЛицензий.Вставить("НижняяГраницаДатыПоXSD", НижняяГраницаДатыПоXSD);
	СтруктураСведенийРазделаЛицензий.Вставить("ВерхняяГраницаДатыПоXSD", ВерхняяГраницаДатыПоXSD);

	// Даты начала и конца отчета.	
	СтруктураСведенийРазделаЛицензий.Вставить("ДатаНачалаОтчета", ДатаНачалаОтчета);
	СтруктураСведенийРазделаЛицензий.Вставить("ДатаКонцаОтчета", ДатаКонцаОтчета);

	// Массив кодов видов деятельности из макета Списки нужного периода.
	СтруктураСведенийРазделаЛицензий.Вставить("СписокКодовДеятельности", СписокКодовДеятельности);

	СтруктураСведенийРазделаЛицензий.Вставить("МаксимальноеКоличествоОшибок", МаксимальноеКоличествоОшибок);
	СтруктураСведенийРазделаЛицензий.Вставить("ТекущееКоличествоОшибок", ТекущееКоличествоОшибок);
	СтруктураСведенийРазделаЛицензий.Вставить("МаксимальноеКоличествоПредупреждений", МаксимальноеКоличествоПредупреждений);
	СтруктураСведенийРазделаЛицензий.Вставить("ТекущееКоличествоПредупреждений", ТекущееКоличествоПредупреждений);
	
	РегламентированнаяОтчетностьАЛКО.ВерноеЗаполнениеРазделаЛицензии(ДокументСсылкаАлкоОтчет, СтатусПроверки, ТаблицаСтраницыЛицензии, ТаблицаСообщений, ТаблицаСообщенийДляСведения, СтруктураСведенийРазделаЛицензий, РежимОтладки);

	СтруктураСведенийРазделаЛицензий.Свойство("ТекущееКоличествоОшибок", ТекущееКоличествоОшибок);
	СтруктураСведенийРазделаЛицензий.Свойство("ТекущееКоличествоПредупреждений", ТекущееКоличествоПредупреждений);
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеДекларации(СтатусПроверки)

	ИмяРаздела = "Декларация";
	СтрВКакомРазделе = "В разделе Декларация";
	ПоказательОП = "ОбособленноеПодразделение";
	
	// Необходимость проверки определяется переменной ЕстьАктивныеСтрокиВТаблице.
	ИдГруппыРаздел1 = СтруктураРеквизитовФормы.мИдГруппы12;	
	ИдГруппыРаздел2 = СтруктураРеквизитовФормы.мИдГруппы22;
		
	ДанныеРаздела = ТаблицаСтраницыДекларация;

	ИмяРегистраРаздел1 = ПолучитьИмяРегистраСведений(ИдГруппыРаздел1);
	СервисРегистровРаздел1 = СтруктураСервисРегистров[ИмяРегистраРаздел1];
	
	ИмяРегистраРаздел2 = ПолучитьИмяРегистраСведений(ИдГруппыРаздел2);
	СервисРегистровРаздел2 = СтруктураСервисРегистров[ИмяРегистраРаздел2];
		
	НомерЛиста = 0;
	Для Каждого ЛистРаздела Из ДанныеРаздела Цикл

		НомерЛиста = НомерЛиста + 1;

		СтруктураСведенийПроверки = Новый Структура;
		СтруктураСведенийПроверки.Вставить("ПараметрыВыгрузки", ПараметрыВыгрузки);
		СтруктураСведенийПроверки.Вставить("ИмяРаздела", ИмяРаздела);
		СтруктураСведенийПроверки.Вставить("СтрВКакомРазделе", СтрВКакомРазделе);
		СтруктураСведенийПроверки.Вставить("НомерЛиста", НомерЛиста);
		СтруктураСведенийПроверки.Вставить("ЛистРаздела", ЛистРаздела);

		// Если есть заполненные строки при незаполненном ОП - нужно выдать ошибку.
		КолАктивныхСтрокРаздел1 = ПолучитьКоличествоСтрокПоСтраницеГруппы(ИдГруппыРаздел1, НомерЛиста - 1, Истина);
		КолАктивныхСтрокРаздел2 = ПолучитьКоличествоСтрокПоСтраницеГруппы(ИдГруппыРаздел2, НомерЛиста - 1, Истина);
		
		ЕстьАктивныеСтрокиВТаблице = (КолАктивныхСтрокРаздел1 + КолАктивныхСтрокРаздел2 > 0);

		СтруктураСведенийПроверки.Вставить("ПроверятьНаПустоту", ЕстьАктивныеСтрокиВТаблице);

		СтруктураСведенийПроверки.Вставить("ОписаниеСтруктурыАдресаИзОтчета", ОписаниеСтруктурыАдресаИзОтчета);
		
		СтруктураСведенийПроверки.Вставить("МаксимальноеКоличествоОшибок", МаксимальноеКоличествоОшибок);
		СтруктураСведенийПроверки.Вставить("ТекущееКоличествоОшибок", ТекущееКоличествоОшибок);
		СтруктураСведенийПроверки.Вставить("МаксимальноеКоличествоПредупреждений", МаксимальноеКоличествоПредупреждений);
		СтруктураСведенийПроверки.Вставить("ТекущееКоличествоПредупреждений", ТекущееКоличествоПредупреждений);
	
		СведенияОбОП = Неопределено;

		РегламентированнаяОтчетностьАЛКО.ВерноеЗаполнениеОП(
					ДокументСсылкаАлкоОтчет, ТаблицаСообщений, ТаблицаСообщенийДляСведения, СтатусПроверки, 
					СтруктураСведенийПроверки, СведенияОбОП, РежимОтладки, СоответствиеКешАдресов);

		СтруктураСведенийПроверки.Свойство("ТекущееКоличествоОшибок", ТекущееКоличествоОшибок);
		СтруктураСведенийПроверки.Свойство("ТекущееКоличествоПредупреждений", ТекущееКоличествоПредупреждений);
		
		СтрокаГде = РегламентированнаяОтчетностьАЛКО.СтрокаГдеСообщенийОбОшибках(СтрВКакомРазделе, НомерЛиста, Неопределено, Неопределено, РежимОтладки);

		ИндексСтраницыРегистраРаздел1 = СервисРегистровРаздел1[НомерЛиста - 1].ИндексСтраницыРегистра;
		ИндексСтраницыРегистраРаздел2 = СервисРегистровРаздел2[НомерЛиста - 1].ИндексСтраницыРегистра;
		
		ИдОП = "";
				
		Если ТипЗнч(ЛистРаздела.ДополнительныеДанные[0].Значение) = Тип("Структура")
				И ЛистРаздела.ДополнительныеДанные[0].Значение.Свойство("СведенияОбОП", СведенияОбОП) Тогда

			Если ЗначениеЗаполнено(СведенияОбОП.КПП)
					или ЗначениеЗаполнено(СведенияОбОП.Наименование) Тогда

				Наименование_ОП = СведенияОбОП.Наименование;
				КПП_ОП = СведенияОбОП.КПП;
				ИдОП = ИдентификаторОП(СведенияОбОП);

				// Контроль записей на уникальность в Разделе("КПП ОП" + "Наименование ОП").
				Если НЕ ДанныеОП["Декларация"].Получить(ИдОП) = Неопределено Тогда

				// Такое ОП уже есть.
					ЛистПовтора = ДанныеОП["Декларация"][ИдОП]["НомерЛиста"];
					ЛистПовтораСтроковый = РегламентированнаяОтчетностьАЛКОКлиентСервер.СтрокаЧГ0(ЛистПовтора);
					ТекстОписанияОшибки = СтрокаГде
						+ " обособленное подразделение повторяет заполненное на листе "
						+ ЛистПовтораСтроковый + "!
						|ОП: " + Наименование_ОП + ?(ПустаяСтрока(КПП_ОП), "", ", (КПП "
						+ КПП_ОП + ")")
						+ ?(НЕ ЗначениеЗаполнено(СведенияОбОП.ПредставлениеАдреса), "", "
						|Адрес: " + СведенияОбОП.ПредставлениеАдреса);

					СтруктураОшибки = Новый Структура;
					СтруктураОшибки.Вставить("ТекстОписанияОшибки", ТекстОписанияОшибки);
					СтруктураОшибки.Вставить("Раздел", ИмяРаздела);
					СтруктураОшибки.Вставить("Показатель", ПоказательОП);
					СтруктураОшибки.Вставить("ЛистРаздела", НомерЛиста);
					СтруктураОшибки.Вставить("ТипОшибки", "ПовторОП");

					СообщитьОбОшибкеЗаполнения(ДокументСсылкаАлкоОтчет, ТаблицаСообщений, СтатусПроверки, СтруктураОшибки, 
												МаксимальноеКоличествоОшибок, ТекущееКоличествоОшибок);

				Иначе

					ДанныеОП["Декларация"].Вставить(ИдОП, Новый Соответствие);

					КопияСведенияОбОП = ОбщегоНазначенияКлиентСервер.СкопироватьРекурсивно(СведенияОбОП);

					ДанныеОП["Декларация"][ИдОП].Вставить("ОП", КопияСведенияОбОП);

					ДанныеОП["Декларация"][ИдОП].Вставить("НомерЛиста", НомерЛиста);

					ДанныеОП["Декларация"][ИдОП].Вставить("ИндексСтраницыРегистраРаздел1", ИндексСтраницыРегистраРаздел1);
					ДанныеОП["Декларация"][ИдОП].Вставить("ИндексСтраницыРегистраРаздел2", ИндексСтраницыРегистраРаздел2);
					
					ДанныеОП["Декларация"][ИдОП].Вставить("КолСтрокПоРаздел1", 0);
					ДанныеОП["Декларация"][ИдОП].Вставить("КолСтрокПоРаздел2", 0);
					
					ДанныеДвижений.Вставить(ИдОП, Новый Соответствие);

				КонецЕсли;
			
			КонецЕсли;
					
		КонецЕсли;

		Если КолАктивныхСтрокРаздел1 > 0 Тогда
			ПроверитьЗаполнениеТаблицы(СтатусПроверки, 
					ИдГруппыРаздел1, НомерЛиста, ИдОП);
		КонецЕсли;
		
		Если КолАктивныхСтрокРаздел2 > 0 Тогда

		// Проверка Раздела 2 не отличается от Раздела 1.
			ПроверитьЗаполнениеТаблицы(СтатусПроверки, 
					ИдГруппыРаздел2, НомерЛиста, ИдОП);
		КонецЕсли;

		Если НетОшибокПроверки и (ТекущееКоличествоОшибок > 0) Тогда

			НетОшибокПроверки = Ложь;
			ПроцентБезЗавершающейПроверкиДанных = 100;
			НужноОбработатьСтрок = НужноОбработатьСтрок / 2;

		КонецЕсли;

		Если ТекущееКоличествоОшибок > МаксимальноеКоличествоОшибок Тогда
			ОбработаноСтрок = НужноОбработатьСтрок;			
			Возврат;
		КонецЕсли;

	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеТаблицы(СтатусПроверки, ИдГруппы,
		НомерЛиста, ИдОП = "")

	ПроверятьАдресаПроизводителей = СтруктураРеквизитовФормы.ПроверятьАдресаПроизводителей;
	
	ИндексСтраницы = НомерЛиста - 1;

	ИмяТаблицы = ?(ИдГруппы = СтруктураРеквизитовФормы.мИдГруппы12, "Раздел 1", "Раздел 2");
	ИдГруппыСокр = ?(ИдГруппы = СтруктураРеквизитовФормы.мИдГруппы12, "П00001", "П00002");
	
	СокрИмяТаблицы = СтрЗаменить(ИмяТаблицы, " ", "");
	
	ЕстьИдОП = НЕ ПустаяСтрока(СтрЗаменить(ИдОП, "\", ""));

	// Опеределим регистр сведений.
	ИмяРегистраСведений = ПолучитьИмяРегистраСведений(ИдГруппы);

	СервисРегистров = СтруктураСервисРегистров[ИмяРегистраСведений];
	ИндексСтраницыРегистра = СервисРегистров[ИндексСтраницы].ИндексСтраницыРегистра;
	ИДДокИндСтраницы = РегламентированнаяОтчетностьАЛКО.ПолучитьИдДокИндСтраницы(ДокументСсылкаАлкоОтчет, ИндексСтраницыРегистра);

	ДанныеДвиженийПоОП = Неопределено;
	Если ЕстьИдОП Тогда
		
		ДанныеДвиженийПоОП = ДанныеДвижений[ИдОП];
		
		Ключ = "ИДДокИндСтраницы" + СокрИмяТаблицы;
		ДанныеОП["Декларация"][ИдОП].Вставить(Ключ, ИДДокИндСтраницы);
		
	КонецЕсли;

	ТекущийИндексСтроки = 0;
	КолСтрокРаздела = 0;

	// Зафиксируем имена ключевых полей.
	ИмяКодаВидаПродукции = ИдГруппы + "02";

	ИмяНаименованияПроизводителя = ИдГруппы + "03";
	ИмяАдресаПроизводителя = ИдГруппы + "03Адрес";
	ИмяАдресаXMLПроизводителя = ИдГруппы + "03АдресXML";
	ИмяИННПроизводителя = ИдГруппы + "04";
	ИмяКПППроизводителя = ИдГруппы + "05";

	ИмяНаименованияПолучателя = ИдГруппы + "06";
	ИмяАдресаПолучателя = ИдГруппы + "07";
	ИмяАдресаXMLПолучателя = ИдГруппы + "07XML";
	ИмяИННПолучателя = ИдГруппы + "08";
	ИмяКПППолучателя = ИдГруппы + "09";

	ИмяВидаДеятельностиЛицензии = ИдГруппы + "10";
	
	ИмяДатыТТН = ИдГруппы + "11";
	ИмяНомерТТН = ИдГруппы + "12";
	ИмяНомерГТД = ИдГруппы + "13";
	ИмяОбъемПоТТН = ИдГруппы + "14";

	ИмяРаздела = "Декларация";
	СтрВКакомРазделе = "В разделе Декларация";
	КоличествоКолонокВРазделе = 14;
	
	КоличествоПервых = 1000;

	Пока Истина Цикл

		СтруктураНастроек = Новый Структура;
		СтруктураНастроек.Вставить("КоличествоПервых", КоличествоПервых);
		СтруктураНастроек.Вставить("ТекущийИндексСтроки", ТекущийИндексСтроки);
		
		РезЗапросаДанных = РегламентированнаяОтчетностьАЛКО.ЗаполнитьПоСтраницеРазделаАЛКО(ИдГруппы, ИндексСтраницы, СтруктураРеквизитовФормы, , ИДДокИндСтраницы, Ложь, СтруктураНастроек);

		Если РезЗапросаДанных.Количество() = 0 Тогда
			Прервать;
		КонецЕсли;

		Для каждого СтрокаДекларации Из РезЗапросаДанных Цикл

			НомерСтроки = СтрокаДекларации.ИндексСтроки;
			ТекущийИндексСтроки = СтрокаДекларации.ИндексСтроки;
			КолСтрокРаздела = КолСтрокРаздела + 1;

			НомерСтрокиСтроковый = РегламентированнаяОтчетностьАЛКОКлиентСервер.СтрокаЧГ0(НомерСтроки);

			СтрокаЗаполнена = РегламентированнаяОтчетностьАЛКО.СтрокаДекларацииЗаполнена(СтрокаДекларации, ИдГруппы, КоличествоКолонокВРазделе);

			СтрокаГде = РегламентированнаяОтчетностьАЛКО.СтрокаГдеСообщенийОбОшибках(СтрВКакомРазделе, НомерЛиста, НомерСтрокиСтроковый, ИмяТаблицы, РежимОтладки);

			Если НЕ СтрокаЗаполнена Тогда

				ТекстОписанияОшибки = СтрокаГде + " не заполнено ни одно поле!";

				ИмяПоказателя = ИмяКодаВидаПродукции + "_" + НомерСтрокиСтроковый;

				СтруктураОшибки = Новый Структура;
				СтруктураОшибки.Вставить("ТекстОписанияОшибки", ТекстОписанияОшибки);
				СтруктураОшибки.Вставить("Раздел", ИмяРаздела);
				СтруктураОшибки.Вставить("Показатель", ИмяПоказателя);
				СтруктураОшибки.Вставить("ЛистРаздела", НомерЛиста);
				СтруктураОшибки.Вставить("ТипОшибки", "ПустаяСтрока");

				СообщитьОбОшибкеЗаполнения(ДокументСсылкаАлкоОтчет, ТаблицаСообщений, СтатусПроверки, СтруктураОшибки,
											МаксимальноеКоличествоОшибок, ТекущееКоличествоОшибок);

				Продолжить;
			КонецЕсли;

			ОбработаноСтрок = ОбработаноСтрок + 1;

			ИмяПоказателя = ИмяКодаВидаПродукции + "_" + НомерСтрокиСтроковый;
			КодВидаПродукции = СокрЛП(СтрокаДекларации[ИмяКодаВидаПродукции]);

			СтруктураИнформацииВидаПродукции = Новый Структура;

			СтруктураИнформацииВидаПродукции.Вставить("ИмяПоказателя", ИмяПоказателя);
			СтруктураИнформацииВидаПродукции.Вставить("ИмяРаздела", ИмяРаздела);
			СтруктураИнформацииВидаПродукции.Вставить("НомерЛиста", НомерЛиста);
			СтруктураИнформацииВидаПродукции.Вставить("СписокКодов", СписокКодовПродукции);
			СтруктураИнформацииВидаПродукции.Вставить("СтрокаГде", СтрокаГде);
			СтруктураИнформацииВидаПродукции.Вставить("СтрВКакомРазделе", СтрВКакомРазделе);
			СтруктураИнформацииВидаПродукции.Вставить("НомерСтрокиСтроковый", НомерСтрокиСтроковый);
			СтруктураИнформацииВидаПродукции.Вставить("ИмяТаблицы", ИмяТаблицы);
			
			СтруктураИнформацииВидаПродукции.Вставить("МаксимальноеКоличествоОшибок", МаксимальноеКоличествоОшибок);
			СтруктураИнформацииВидаПродукции.Вставить("ТекущееКоличествоОшибок", ТекущееКоличествоОшибок);
			СтруктураИнформацииВидаПродукции.Вставить("МаксимальноеКоличествоПредупреждений", МаксимальноеКоличествоПредупреждений);
			СтруктураИнформацииВидаПродукции.Вставить("ТекущееКоличествоПредупреждений", ТекущееКоличествоПредупреждений);

			СтруктураИнформацииВидаПродукции.Вставить("МинДлинаКода", 3);
			СтруктураИнформацииВидаПродукции.Вставить("МаксДлинаКода", 5);
			
			РегламентированнаяОтчетностьАЛКО.ВерныйКодВидаПродукции(
					ДокументСсылкаАлкоОтчет, ТаблицаСообщений, ТаблицаСообщенийДляСведения, СтатусПроверки, 
					КодВидаПродукции, СтруктураИнформацииВидаПродукции, РежимОтладки);

			СтруктураИнформацииВидаПродукции.Свойство("ТекущееКоличествоОшибок", ТекущееКоличествоОшибок);
			СтруктураИнформацииВидаПродукции.Свойство("ТекущееКоличествоПредупреждений", ТекущееКоличествоПредупреждений);

			////////////////////////////////////////////////////////////////////////////////
			// Контроль Наименования-ИНН-КПП производителя-импортера.
			ИмяПоказателяНаименованияПроизводителя = ИмяНаименованияПроизводителя + "_"
				+ НомерСтрокиСтроковый;
			НаименованиеПроизводителя = СокрЛП(СтрокаДекларации[ИмяНаименованияПроизводителя]);

			ИмяПоказателяИНН = ИмяИННПроизводителя + "_" + НомерСтрокиСтроковый;
			ИННПроизводителя = СокрЛП(СтрокаДекларации[ИмяИННПроизводителя]);

			ИмяПоказателяКПП = ИмяКПППроизводителя + "_" + НомерСтрокиСтроковый;
			КПППроизводителя = СокрЛП(СтрокаДекларации[ИмяКПППроизводителя]);
			
			// Данные адреса для определения принадлежности.
			ИмяПоказателяАдреса = ИмяАдресаПроизводителя + "_" + НомерСтрокиСтроковый;
			
			АдресПроизводителя = СокрЛП(СтрокаДекларации[ИмяАдресаПроизводителя]);
			АдресПроизводителяXML = СокрЛП(СтрокаДекларации[ИмяАдресаXMLПроизводителя]);
			
			СтруктураАдресПроизводителя = РегламентированнаяОтчетностьАЛКО.СтруктураАдресаИзСтрокиСтруктурыХранения(
																	АдресПроизводителяXML);
																					
			КодСтраныПроизводителя = СтруктураАдресПроизводителя.КодСтраны;
						
			РоссийскийРезидент = Ложь;
			РезидентЕАЭС = Ложь;
	
			РегламентированнаяОтчетностьАЛКО.ОпределитьПринадлежностьЕАЭСиРФ(ИННПроизводителя, КПППроизводителя, НаименованиеПроизводителя, 
											КодСтраныПроизводителя, РоссийскийРезидент, РезидентЕАЭС, РежимОтладки);

			////////////////////////////////////////////////////////////////////////////////								
			// Контроль Адреса производителя.
			
			РодПадежАдресЧего = "производителя/импортера";
			
			Если ПроверятьАдресаПроизводителей Тогда
				
				Обязательный = Истина;
				ТолькоРоссия = РоссийскийРезидент;
				ТолькоЕАЭС = (НЕ ТолькоРоссия);
				ТолькоИностранныйКромеЕАЭС = Ложь;
				ЛюбойИностранный = Ложь;
				
				ВспомогательнаяСтруктураДанных = Новый Структура;

				ВспомогательнаяСтруктураДанных.Вставить("ИмяРаздела", ИмяРаздела);
				ВспомогательнаяСтруктураДанных.Вставить("СтрокаГде", СтрокаГде);
				ВспомогательнаяСтруктураДанных.Вставить("СтрВКакомРазделе", СтрВКакомРазделе);
				ВспомогательнаяСтруктураДанных.Вставить("Показатель", ИмяПоказателяАдреса);

				// Поля, заполненны только при проверке строчных и многостраничных разделов.
				ВспомогательнаяСтруктураДанных.Вставить("НомерЛиста", НомерЛиста);
				ВспомогательнаяСтруктураДанных.Вставить("НомерСтрокиСтроковый", НомерСтрокиСтроковый);
				ВспомогательнаяСтруктураДанных.Вставить("ИмяТаблицы", ИмяТаблицы);
				ВспомогательнаяСтруктураДанных.Вставить("РодПадежАдресЧего", РодПадежАдресЧего);

				ВспомогательнаяСтруктураДанных.Вставить("ОписаниеСтруктурыАдресаИзОтчета", ОписаниеСтруктурыАдресаИзОтчета);

				ВспомогательнаяСтруктураДанных.Вставить("Обязательный", Обязательный);
				ВспомогательнаяСтруктураДанных.Вставить("ТолькоРоссия", ТолькоРоссия);
				ВспомогательнаяСтруктураДанных.Вставить("ТолькоЕАЭС", ТолькоЕАЭС);
				ВспомогательнаяСтруктураДанных.Вставить("ТолькоИностранныйКромеЕАЭС", ТолькоИностранныйКромеЕАЭС);
				ВспомогательнаяСтруктураДанных.Вставить("ЛюбойИностранный", ЛюбойИностранный);
				
				// Адрес формально не выгружается - поэтому все ошибки адреса выдаем как предупреждение.
				ВспомогательнаяСтруктураДанных.Вставить("МаксимальноеКоличествоОшибок", МаксимальноеКоличествоПредупреждений);
				ВспомогательнаяСтруктураДанных.Вставить("ТекущееКоличествоОшибок", 0);
				ВспомогательнаяСтруктураДанных.Вставить("МаксимальноеКоличествоПредупреждений", МаксимальноеКоличествоПредупреждений);
				ВспомогательнаяСтруктураДанных.Вставить("ТекущееКоличествоПредупреждений", ТекущееКоличествоПредупреждений);

				// Адрес формально не выгружается - поэтому все ошибки адреса выдаем как предупреждение.
				ПроверкаАдресаПройдена = РегламентированнаяОтчетностьАЛКО.ВерныйАдрес(ДокументСсылкаАлкоОтчет, ТаблицаСообщенийДляСведения, 
											ТаблицаСообщенийДляСведения, Истина, СтруктураАдресПроизводителя, 
											ВспомогательнаяСтруктураДанных, РежимОтладки, СоответствиеКешАдресов);
											
				ТекущееКоличествоОшибокАдреса = 0;
				ВспомогательнаяСтруктураДанных.Свойство("ТекущееКоличествоОшибок", ТекущееКоличествоОшибокАдреса);
				ВспомогательнаяСтруктураДанных.Свойство("ТекущееКоличествоПредупреждений", ТекущееКоличествоПредупреждений);

				ТекущееКоличествоПредупреждений = ТекущееКоличествоПредупреждений + ТекущееКоличествоОшибокАдреса;
				
			КонецЕсли;
			//
			////////////////////////////////////////////////////////////////////////////////
			
			РодПадежЧейИННКПП = РодПадежАдресЧего;
			
			МожетБытьПустойИНН = Ложь;
			МожетБытьПустойКПП = Истина;
			КонтрольОП = Ложь;
			РоссийскийТолькоЮЛ = Неопределено;
			РоссийскийТолькоФЛ = Неопределено;
			
			ТолькоРФ = (НЕ РоссийскийРезидент = Неопределено) И РоссийскийРезидент;
			ТолькоЕАЭС = НЕ ТолькоРФ;
			
			МинДлинаИННпоXSD = 1;
			МаксДлинаИННпоXSD = 20;
			
			КПП56СимволыМогутБытьБуквами = Ложь;
			
			ИННLiteТип = (СтрДлина(ИННПроизводителя) = 10) И ТолькоРФ;
			КППLiteТип = (СтрДлина(ИННПроизводителя) = 10) И ТолькоРФ;

			СтруктураСведенийИННКПП = Новый Структура;

			СтруктураСведенийИННКПП.Вставить("ИмяРаздела", ИмяРаздела);

			СтруктураСведенийИННКПП.Вставить("СтрокаГде", СтрокаГде);
			СтруктураСведенийИННКПП.Вставить("СтрВКакомРазделе", СтрВКакомРазделе);
			СтруктураСведенийИННКПП.Вставить("НомерСтрокиСтроковый", НомерСтрокиСтроковый);
			СтруктураСведенийИННКПП.Вставить("ИмяТаблицы", ИмяТаблицы);

			СтруктураСведенийИННКПП.Вставить("НомерЛиста", НомерЛиста);
			СтруктураСведенийИННКПП.Вставить("КонтрольОП", КонтрольОП);

			СтруктураСведенийИННКПП.Вставить("ИмяПоказателяНаименования", ИмяПоказателяНаименованияПроизводителя);
			СтруктураСведенийИННКПП.Вставить("Наименование", НаименованиеПроизводителя);

			СтруктураСведенийИННКПП.Вставить("ИмяПоказателяИНН", ИмяПоказателяИНН);
			СтруктураСведенийИННКПП.Вставить("ИНН", ИННПроизводителя);
			СтруктураСведенийИННКПП.Вставить("ИмяПоказателяКПП", ИмяПоказателяКПП);
			СтруктураСведенийИННКПП.Вставить("КПП", КПППроизводителя);
			СтруктураСведенийИННКПП.Вставить("РодПадежЧейИННКПП", РодПадежЧейИННКПП);

			СтруктураСведенийИННКПП.Вставить("МожетБытьПустойИНН", МожетБытьПустойИНН);
			СтруктураСведенийИННКПП.Вставить("МожетБытьПустойКПП", МожетБытьПустойКПП);

			СтруктураСведенийИННКПП.Вставить("РоссийскийТолькоЮЛ", РоссийскийТолькоЮЛ);
			СтруктураСведенийИННКПП.Вставить("РоссийскийТолькоФЛ", РоссийскийТолькоФЛ);
			СтруктураСведенийИННКПП.Вставить("КПП56СимволыМогутБытьБуквами", КПП56СимволыМогутБытьБуквами);
			СтруктураСведенийИННКПП.Вставить("ИННLiteТип", ИННLiteТип);
			СтруктураСведенийИННКПП.Вставить("КППLiteТип", КППLiteТип);
			
			СтруктураСведенийИННКПП.Вставить("МаксимальноеКоличествоОшибок", МаксимальноеКоличествоОшибок);
			СтруктураСведенийИННКПП.Вставить("ТекущееКоличествоОшибок", ТекущееКоличествоОшибок);
			СтруктураСведенийИННКПП.Вставить("МаксимальноеКоличествоПредупреждений", МаксимальноеКоличествоПредупреждений);
			СтруктураСведенийИННКПП.Вставить("ТекущееКоличествоПредупреждений", ТекущееКоличествоПредупреждений);

			СтруктураСведенийИННКПП.Вставить("КодСтраны", КодСтраныПроизводителя);
			СтруктураСведенийИННКПП.Вставить("ТолькоЕАЭС", ТолькоЕАЭС);
			СтруктураСведенийИННКПП.Вставить("ТолькоРФ", ТолькоРФ); 
			СтруктураСведенийИННКПП.Вставить("МаксДлинаИННпоXSD", МаксДлинаИННпоXSD); 	// По умолчанию 20.
			СтруктураСведенийИННКПП.Вставить("МинДлинаИННпоXSD", МинДлинаИННпоXSD);		// По умолчанию 1.
			
			РегламентированнаяОтчетностьАЛКО.ВерныеНаименованиеИННКПП(ДокументСсылкаАлкоОтчет, 
					ТаблицаСообщений, ТаблицаСообщенийДляСведения, СтатусПроверки, СтруктураСведенийИННКПП, 
					РоссийскийРезидент, РежимОтладки);

			СтруктураСведенийИННКПП.Свойство("ТекущееКоличествоОшибок", ТекущееКоличествоОшибок);
			СтруктураСведенийИННКПП.Свойство("ТекущееКоличествоПредупреждений", ТекущееКоличествоПредупреждений);

			//
			////////////////////////////////////////////////////////////////////////////////
			ПроизводительРоссийский = РоссийскийРезидент;
			Если ПроизводительРоссийский Тогда			
				КодСтраныПроизводителя = "643";			
			КонецЕсли;
			
			////////////////////////////////////////////////////////////////////////////////
			// Контроль Наименования-ИНН-КПП получателя.
			ИмяПоказателяНаименованияПолучателя = ИмяНаименованияПолучателя + "_"
				+ НомерСтрокиСтроковый;
			НаименованиеПолучателя = СокрЛП(СтрокаДекларации[ИмяНаименованияПолучателя]);

			ИмяПоказателяИНН = ИмяИННПолучателя + "_" + НомерСтрокиСтроковый;
			ИННПолучателя = СокрЛП(СтрокаДекларации[ИмяИННПолучателя]);

			ИмяПоказателяКПП = ИмяКПППолучателя + "_" + НомерСтрокиСтроковый;
			КПППолучателя = СокрЛП(СтрокаДекларации[ИмяКПППолучателя]);

			РодПадежЧейИННКПП = "получателя";
			
			ИмяПоказателяАдреса = ИмяАдресаПолучателя + "_" + НомерСтрокиСтроковый;
			
			АдресПолучателя = СокрЛП(СтрокаДекларации[ИмяАдресаПолучателя]);
			АдресПолучателяXML = СокрЛП(СтрокаДекларации[ИмяАдресаXMLПолучателя]);
			
			СтруктураАдресПолучателя = РегламентированнаяОтчетностьАЛКО.СтруктураАдресаИзСтрокиСтруктурыХранения(
																	АдресПолучателяXML);
																					
			КодСтраныПолучателя = СтруктураАдресПолучателя.КодСтраны;
						
			РоссийскийРезидент = Ложь;
			РезидентЕАЭС = Ложь;
	
			РегламентированнаяОтчетностьАЛКО.ОпределитьПринадлежностьЕАЭСиРФ(ИННПолучателя, КПППолучателя, НаименованиеПолучателя,
											КодСтраныПолучателя, РоссийскийРезидент, РезидентЕАЭС, РежимОтладки);
											
			////////////////////////////////////////////////////////////////////////////////								
			// Контроль Адреса получателя.
			
			РодПадежАдресЧего = РодПадежЧейИННКПП;
			Обязательный = Истина;
			ТолькоРоссия = РоссийскийРезидент;
			ТолькоЕАЭС = РезидентЕАЭС;
			ТолькоИностранныйКромеЕАЭС = Ложь;
			ЛюбойИностранный = Ложь;
			
			ВспомогательнаяСтруктураДанных = Новый Структура;

			ВспомогательнаяСтруктураДанных.Вставить("ИмяРаздела", ИмяРаздела);
			ВспомогательнаяСтруктураДанных.Вставить("СтрокаГде", СтрокаГде);
			ВспомогательнаяСтруктураДанных.Вставить("СтрВКакомРазделе", СтрВКакомРазделе);
			ВспомогательнаяСтруктураДанных.Вставить("Показатель", ИмяПоказателяАдреса);

			// Поля, заполненны только при проверке строчных и многостраничных разделов.
			ВспомогательнаяСтруктураДанных.Вставить("НомерЛиста", НомерЛиста);
			ВспомогательнаяСтруктураДанных.Вставить("НомерСтрокиСтроковый", НомерСтрокиСтроковый);
			ВспомогательнаяСтруктураДанных.Вставить("ИмяТаблицы", ИмяТаблицы);
			ВспомогательнаяСтруктураДанных.Вставить("РодПадежАдресЧего", РодПадежАдресЧего);

			ВспомогательнаяСтруктураДанных.Вставить("ОписаниеСтруктурыАдресаИзОтчета", ОписаниеСтруктурыАдресаИзОтчета);

			ВспомогательнаяСтруктураДанных.Вставить("Обязательный", Обязательный);
			ВспомогательнаяСтруктураДанных.Вставить("ТолькоРоссия", ТолькоРоссия);
			ВспомогательнаяСтруктураДанных.Вставить("ТолькоЕАЭС", ТолькоЕАЭС);
			ВспомогательнаяСтруктураДанных.Вставить("ТолькоИностранныйКромеЕАЭС", ТолькоИностранныйКромеЕАЭС);
			ВспомогательнаяСтруктураДанных.Вставить("ЛюбойИностранный", ЛюбойИностранный);
			
			ВспомогательнаяСтруктураДанных.Вставить("МаксимальноеКоличествоОшибок", МаксимальноеКоличествоОшибок);
			ВспомогательнаяСтруктураДанных.Вставить("ТекущееКоличествоОшибок", ТекущееКоличествоОшибок);
			ВспомогательнаяСтруктураДанных.Вставить("МаксимальноеКоличествоПредупреждений", МаксимальноеКоличествоПредупреждений);
			ВспомогательнаяСтруктураДанных.Вставить("ТекущееКоличествоПредупреждений", ТекущееКоличествоПредупреждений);

			ПроверкаАдресаПройдена = РегламентированнаяОтчетностьАЛКО.ВерныйАдрес(ДокументСсылкаАлкоОтчет, ТаблицаСообщений, 
										ТаблицаСообщенийДляСведения, Истина, СтруктураАдресПолучателя, 
										ВспомогательнаяСтруктураДанных, РежимОтладки, СоответствиеКешАдресов);
										
			ВспомогательнаяСтруктураДанных.Свойство("ТекущееКоличествоОшибок", ТекущееКоличествоОшибок);
			ВспомогательнаяСтруктураДанных.Свойство("ТекущееКоличествоПредупреждений", ТекущееКоличествоПредупреждений);

			//
			////////////////////////////////////////////////////////////////////////////////
											
			
			МожетБытьПустойИНН = НЕ ((НЕ РоссийскийРезидент = Неопределено) И РоссийскийРезидент);
			МожетБытьПустойКПП = Истина;
			КонтрольОП = Ложь;
			РоссийскийТолькоЮЛ = Неопределено;
			РоссийскийТолькоФЛ = Неопределено;
			
			ТолькоРФ = (НЕ РоссийскийРезидент = Неопределено) И РоссийскийРезидент;
			ТолькоЕАЭС = РезидентЕАЭС;
			МинДлинаИННпоXSD = 1;
			МаксДлинаИННпоXSD = 20;
			
			КПП56СимволыМогутБытьБуквами = Ложь;
			
			ИННLiteТип = (СтрДлина(ИННПолучателя) = 10) И ТолькоРФ;
			КППLiteТип = (СтрДлина(ИННПолучателя) = 10) И ТолькоРФ;

			СтруктураСведенийИННКПП = Новый Структура;

			СтруктураСведенийИННКПП.Вставить("ИмяРаздела", ИмяРаздела);

			СтруктураСведенийИННКПП.Вставить("СтрокаГде", СтрокаГде);
			СтруктураСведенийИННКПП.Вставить("СтрВКакомРазделе", СтрВКакомРазделе);
			СтруктураСведенийИННКПП.Вставить("НомерСтрокиСтроковый", НомерСтрокиСтроковый);
			СтруктураСведенийИННКПП.Вставить("ИмяТаблицы", ИмяТаблицы);

			СтруктураСведенийИННКПП.Вставить("НомерЛиста", НомерЛиста);
			СтруктураСведенийИННКПП.Вставить("КонтрольОП", КонтрольОП);

			СтруктураСведенийИННКПП.Вставить("ИмяПоказателяНаименования", ИмяПоказателяНаименованияПолучателя);
			СтруктураСведенийИННКПП.Вставить("Наименование", НаименованиеПолучателя);

			СтруктураСведенийИННКПП.Вставить("ИмяПоказателяИНН", ИмяПоказателяИНН);
			СтруктураСведенийИННКПП.Вставить("ИНН", ИННПолучателя);
			СтруктураСведенийИННКПП.Вставить("ИмяПоказателяКПП", ИмяПоказателяКПП);
			СтруктураСведенийИННКПП.Вставить("КПП", КПППолучателя);
			СтруктураСведенийИННКПП.Вставить("РодПадежЧейИННКПП", РодПадежЧейИННКПП);

			СтруктураСведенийИННКПП.Вставить("МожетБытьПустойИНН", МожетБытьПустойИНН);
			СтруктураСведенийИННКПП.Вставить("МожетБытьПустойКПП", МожетБытьПустойКПП);

			СтруктураСведенийИННКПП.Вставить("РоссийскийТолькоЮЛ", РоссийскийТолькоЮЛ);
			СтруктураСведенийИННКПП.Вставить("РоссийскийТолькоФЛ", РоссийскийТолькоФЛ);
			СтруктураСведенийИННКПП.Вставить("КПП56СимволыМогутБытьБуквами", КПП56СимволыМогутБытьБуквами);
			СтруктураСведенийИННКПП.Вставить("ИННLiteТип", ИННLiteТип);
			СтруктураСведенийИННКПП.Вставить("КППLiteТип", КППLiteТип);
			
			СтруктураСведенийИННКПП.Вставить("МаксимальноеКоличествоОшибок", МаксимальноеКоличествоОшибок);
			СтруктураСведенийИННКПП.Вставить("ТекущееКоличествоОшибок", ТекущееКоличествоОшибок);
			СтруктураСведенийИННКПП.Вставить("МаксимальноеКоличествоПредупреждений", МаксимальноеКоличествоПредупреждений);
			СтруктураСведенийИННКПП.Вставить("ТекущееКоличествоПредупреждений", ТекущееКоличествоПредупреждений);

			СтруктураСведенийИННКПП.Вставить("КодСтраны", КодСтраныПолучателя);
			СтруктураСведенийИННКПП.Вставить("ТолькоЕАЭС", ТолькоЕАЭС);
			СтруктураСведенийИННКПП.Вставить("ТолькоРФ", ТолькоРФ); 
			СтруктураСведенийИННКПП.Вставить("МаксДлинаИННпоXSD", МаксДлинаИННпоXSD); 	// По умолчанию 20.
			СтруктураСведенийИННКПП.Вставить("МинДлинаИННпоXSD", МинДлинаИННпоXSD);		// По умолчанию 1.
			
			НетПроблемИННКПППолучателя = РегламентированнаяОтчетностьАЛКО.ВерныеНаименованиеИННКПП(ДокументСсылкаАлкоОтчет,
						ТаблицаСообщений, ТаблицаСообщенийДляСведения, СтатусПроверки, СтруктураСведенийИННКПП, 
						РоссийскийРезидент, РежимОтладки);

			СтруктураСведенийИННКПП.Свойство("ТекущееКоличествоОшибок", ТекущееКоличествоОшибок);
			СтруктураСведенийИННКПП.Свойство("ТекущееКоличествоПредупреждений", ТекущееКоличествоПредупреждений);
			
			//
			////////////////////////////////////////////////////////////////////////////////
			ПолучательРоссийский = РоссийскийРезидент;
			Если ПолучательРоссийский Тогда			
				КодСтраныПолучателя = "643";			
			КонецЕсли; 

			////////////////////////////////////////////////////////////////////////////////
			// Контроль лицензии.
			ВидДеятельностиЛицензии = СокрЛП(СтрокаДекларации[ИмяВидаДеятельностиЛицензии]);
				
			ЛицензияЗаполнена = Ложь;
			ВидДеятельностиЗаполнен = ЗначениеЗаполнено(ВидДеятельностиЛицензии);

			// Если есть ошибки по ИНН/КПП нет смысла проверять лицензию, 
			// корректность ее заполнения зависит от корректности данных ИНН/КПП.
			Если НетПроблемИННКПППолучателя Тогда

				// Поля лицензии НЕобязательны по схеме выгрузки.
				СерияЛиц = "";
				НомерЛиц = СокрЛП(СтрокаДекларации[ИдГруппыСокр + "СерияНомер"]);
				ДатаВыдачиЛиц = СтрокаДекларации[ИдГруппыСокр + "ДатаВыдачи"];
				ДатаОкончанияЛиц = СтрокаДекларации[ИдГруппыСокр + "ДатаОкончания"];
				КемВыданаЛиц = СокрЛП(СтрокаДекларации[ИдГруппыСокр + "КемВыдана"]);
				
				ЛицензияЗаполнена = ЗначениеЗаполнено(НомерЛиц) 
									ИЛИ ЗначениеЗаполнено(ДатаВыдачиЛиц)
									ИЛИ ЗначениеЗаполнено(ДатаОкончанияЛиц)
									ИЛИ ЗначениеЗаполнено(КемВыданаЛиц);

				Если ЛицензияЗаполнена ИЛИ ДатаКонцаОтчета < Дата("20210401000101") Тогда
				
					РодительскийПадежЧьяЛицензия = РодПадежЧейИННКПП;

					СтруктураСведенийЛицензий = Новый Структура;

					СтруктураСведенийЛицензий.Вставить("ИмяРаздела", ИмяРаздела);
					СтруктураСведенийЛицензий.Вставить("СтрВКакомРазделе", СтрВКакомРазделе);
					СтруктураСведенийЛицензий.Вставить("НомерЛиста", НомерЛиста);
					СтруктураСведенийЛицензий.Вставить("СтрокаГде", СтрокаГде);
					СтруктураСведенийЛицензий.Вставить("НомерСтрокиСтроковый", НомерСтрокиСтроковый);
					СтруктураСведенийЛицензий.Вставить("ИмяТаблицы", ИмяТаблицы);

					СтруктураСведенийЛицензий.Вставить("РоссийскийРезидент", ПолучательРоссийский);

					СтруктураСведенийЛицензий.Вставить("НетПроблемИННКПП", НетПроблемИННКПППолучателя);

					СтруктураСведенийЛицензий.Вставить("ИмяСерияЛицензии", ИдГруппыСокр + "СерияНомер"
						+ "_" + НомерСтрокиСтроковый);
					СтруктураСведенийЛицензий.Вставить("СерияЛицензии", СерияЛиц);
					СтруктураСведенийЛицензий.Вставить("ИмяНомерЛицензии", ИдГруппыСокр + "СерияНомер"
						+ "_" + НомерСтрокиСтроковый);
					СтруктураСведенийЛицензий.Вставить("НомерЛицензии", НомерЛиц);

					СтруктураСведенийЛицензий.Вставить("ИмяДатаВыдачиЛицензии", ИдГруппыСокр + "ДатаВыдачи"
						+ "_" + НомерСтрокиСтроковый);
					СтруктураСведенийЛицензий.Вставить("ДатаВыдачиЛицензии", ДатаВыдачиЛиц);
					СтруктураСведенийЛицензий.Вставить("ИмяДатаОкончанияЛицензии", ИдГруппыСокр + "ДатаОкончания"
						+ "_" + НомерСтрокиСтроковый);
					СтруктураСведенийЛицензий.Вставить("ДатаОкончанияЛицензии", ДатаОкончанияЛиц);

					СтруктураСведенийЛицензий.Вставить("КемВыданаОбязателен", Истина);
					СтруктураСведенийЛицензий.Вставить("ИмяКемВыданаЛицензия", ИдГруппыСокр + "КемВыдана"
						+ "_" + НомерСтрокиСтроковый);
					СтруктураСведенийЛицензий.Вставить("КемВыданаЛицензия", КемВыданаЛиц);

					СтруктураСведенийЛицензий.Вставить("ДатаНачалаОтчета", ДатаНачалаОтчета);
					СтруктураСведенийЛицензий.Вставить("ДатаКонцаОтчета", ДатаКонцаОтчета);

					СтруктураСведенийЛицензий.Вставить("НижняяГраницаДатыПоXSD", НижняяГраницаДатыПоXSD);
					СтруктураСведенийЛицензий.Вставить("ВерхняяГраницаДатыПоXSD", ВерхняяГраницаДатыПоXSD);

					СтруктураСведенийЛицензий.Вставить("РодительскийПадежЧьяЛицензия", РодительскийПадежЧьяЛицензия);

					СтруктураСведенийЛицензий.Вставить("КодОбязателен", Ложь);
					СтруктураСведенийЛицензий.Вставить("Код", Неопределено);
					СтруктураСведенийЛицензий.Вставить("ИмяКод", Неопределено);
					СтруктураСведенийЛицензий.Вставить("СписокКодов", Неопределено);
					
					СтруктураСведенийЛицензий.Вставить("МаксимальноеКоличествоОшибок", МаксимальноеКоличествоОшибок);
					СтруктураСведенийЛицензий.Вставить("ТекущееКоличествоОшибок", ТекущееКоличествоОшибок);
					СтруктураСведенийЛицензий.Вставить("МаксимальноеКоличествоПредупреждений", МаксимальноеКоличествоПредупреждений);
					СтруктураСведенийЛицензий.Вставить("ТекущееКоличествоПредупреждений", ТекущееКоличествоПредупреждений);

					РегламентированнаяОтчетностьАЛКО.ВернаяЛицензия(ДокументСсылкаАлкоОтчет, ТаблицаСообщений, 
								ТаблицаСообщенийДляСведения, СтатусПроверки, СтруктураСведенийЛицензий, 
								ЛицензияЗаполнена, РежимОтладки);

					СтруктураСведенийЛицензий.Свойство("ТекущееКоличествоОшибок", ТекущееКоличествоОшибок);
					СтруктураСведенийЛицензий.Свойство("ТекущееКоличествоПредупреждений", ТекущееКоличествоПредупреждений);
					
				КонецЕсли; 
				
			КонецЕсли;
			//
			////////////////////////////////////////////////////////////////////////////////

			////////////////////////////////////////////////////////////////////////////////
			// Контроль ТТН.
			ЗаполненаТТН = Ложь;

			ТипНакладной = "ТТН"; // ТипНакладной для Уведомления "Уведомление".
			ОбязателенКЗаполнению = Истина;
			НомерТТНОбязателен = Ложь;
			НомерГТДОбязателен = Ложь;
			НомерГТДилиНомерТТНОбязателен = Истина;
			ОбъемПоТТНОбязателен = Истина;

			СтруктураСведенийНакладной = Новый Структура;

			СтруктураСведенийНакладной.Вставить("ИмяРаздела", ИмяРаздела);
			СтруктураСведенийНакладной.Вставить("СтрВКакомРазделе", СтрВКакомРазделе);
			СтруктураСведенийНакладной.Вставить("НомерЛиста", НомерЛиста);
			СтруктураСведенийНакладной.Вставить("СтрокаГде", СтрокаГде);
			СтруктураСведенийНакладной.Вставить("НомерСтрокиСтроковый", НомерСтрокиСтроковый);
			СтруктураСведенийНакладной.Вставить("ИмяТаблицы", ИмяТаблицы);

			СтруктураСведенийНакладной.Вставить("ИмяДатыТТН", ИмяДатыТТН + "_"
				+ НомерСтрокиСтроковый);
			СтруктураСведенийНакладной.Вставить("ДатаТТН", СтрокаДекларации[ИмяДатыТТН]);
			
			СтруктураСведенийНакладной.Вставить("ИмяНомерТТН", ИмяНомерТТН + "_"
				+ НомерСтрокиСтроковый);
			СтруктураСведенийНакладной.Вставить("НомерТТН", СтрокаДекларации[ИмяНомерТТН]);
			СтруктураСведенийНакладной.Вставить("ИмяНомерГТД", ИмяНомерГТД + "_"
				+ НомерСтрокиСтроковый);
			СтруктураСведенийНакладной.Вставить("НомерТТНОбязателен", НомерТТНОбязателен);
			
			СтруктураСведенийНакладной.Вставить("НомерГТДилиНомерТТНОбязателен", НомерГТДилиНомерТТНОбязателен);
			
			СтруктураСведенийНакладной.Вставить("НомерГТД", СтрокаДекларации[ИмяНомерГТД]);
			СтруктураСведенийНакладной.Вставить("ИмяОбъемПоТТН", ИмяОбъемПоТТН + "_"
				+ НомерСтрокиСтроковый);
			СтруктураСведенийНакладной.Вставить("НомерГТДОбязателен", НомерГТДОбязателен);
			
			СтруктураСведенийНакладной.Вставить("ОбъемПоТТН", СтрокаДекларации[ИмяОбъемПоТТН]);

			СтруктураСведенийНакладной.Вставить("ДатаНачалаОтчета", ДатаНачалаОтчета);
			СтруктураСведенийНакладной.Вставить("ДатаКонцаОтчета", ДатаКонцаОтчета);
			СтруктураСведенийНакладной.Вставить("НижняяГраницаДатыПоXSD", НижняяГраницаДатыПоXSD);
			СтруктураСведенийНакладной.Вставить("ВерхняяГраницаДатыПоXSD", ВерхняяГраницаДатыПоXSD);

			СтруктураСведенийНакладной.Вставить("Обязательный", ОбязателенКЗаполнению);

			СтруктураСведенийНакладной.Вставить("МаксимальноеКоличествоОшибок", МаксимальноеКоличествоОшибок);
			СтруктураСведенийНакладной.Вставить("ТекущееКоличествоОшибок", ТекущееКоличествоОшибок);
			СтруктураСведенийНакладной.Вставить("МаксимальноеКоличествоПредупреждений", МаксимальноеКоличествоПредупреждений);
			СтруктураСведенийНакладной.Вставить("ТекущееКоличествоПредупреждений", ТекущееКоличествоПредупреждений);

			РегламентированнаяОтчетностьАЛКО.ВернаяНакладная(ДокументСсылкаАлкоОтчет, ТаблицаСообщений, ТаблицаСообщенийДляСведения, СтатусПроверки, СтруктураСведенийНакладной, ТипНакладной, ЗаполненаТТН, РежимОтладки);

			СтруктураСведенийНакладной.Свойство("ТекущееКоличествоОшибок", ТекущееКоличествоОшибок);
			СтруктураСведенийНакладной.Свойство("ТекущееКоличествоПредупреждений", ТекущееКоличествоПредупреждений);
			
			//
			////////////////////////////////////////////////////////////////////////////////

			// Расчета в разделе нет. Проверка на отсутствие отрицательных чисел - при проверке ТТН. 
						
			// Заполняем справочники и структуру движений.
			Если ЕстьИдОП И ТекущееКоличествоОшибок = 0 Тогда

				ИдКодПродПроизв = "Х" + СтрокаДекларации.ХешИдКодПродПроизв;
				ИдПроизв = "Х" + СтрокаДекларации.ХешИдПроизводителя;
				
				Если СправочникиОтчета["ПроизводителиИмпортеры"].Получить(ИдПроизв) = Неопределено Тогда

					СчетчикИдПроизвИмп = СчетчикИдПроизвИмп + 1;
					СправочникиОтчета["ПроизводителиИмпортеры"].Вставить(ИдПроизв, Новый Соответствие);
					СправочникиОтчета["ПроизводителиИмпортеры"][ИдПроизв].Вставить("ИдПроизвИмп", СчетчикИдПроизвИмп);
					СправочникиОтчета["ПроизводителиИмпортеры"][ИдПроизв].Вставить("П000000000004", НаименованиеПроизводителя);
					СправочникиОтчета["ПроизводителиИмпортеры"][ИдПроизв].Вставить("П000000000005", ИННПроизводителя);
					СправочникиОтчета["ПроизводителиИмпортеры"][ИдПроизв].Вставить("П000000000006", КПППроизводителя);

				КонецЕсли;
				
				ТекущийПроизводитель = СправочникиОтчета["ПроизводителиИмпортеры"][ИдПроизв];
				
				// Ведущие нули в строке вызывают ошибку при попытке назначения ключем.
				КодВидаПродукцииКлюч = "Х" + КодВидаПродукции;

				Если ДанныеДвиженийПоОП.Получить(КодВидаПродукцииКлюч) = Неопределено Тогда

					ДанныеДвиженийПоОП.Вставить(КодВидаПродукцииКлюч, Новый Соответствие);
					ДанныеДвиженийПоОП[КодВидаПродукцииКлюч].Вставить("КодПродукции", КодВидаПродукции);
					ДанныеДвиженийПоОП[КодВидаПродукцииКлюч].Вставить("КодПродПроизв", Новый Соответствие);

				КонецЕсли;
				ДанныеДвижений_ПоОП_ПоКоду = ДанныеДвиженийПоОП[КодВидаПродукцииКлюч]["КодПродПроизв"];
				
				Если ДанныеДвижений_ПоОП_ПоКоду.Получить(ИдКодПродПроизв) = Неопределено Тогда
					
					ДанныеДвижений_ПоОП_ПоКоду.Вставить(ИдКодПродПроизв, Новый Соответствие);
					
					ДанныеДвижений_ПоОП_ПоКоду[ИдКодПродПроизв].Вставить("ИдПроизвИмп", ТекущийПроизводитель["ИдПроизвИмп"]);
					ДанныеДвижений_ПоОП_ПоКоду[ИдКодПродПроизв].Вставить("Получатели", Новый Соответствие);
					
				КонецЕсли;
				
				ДанныеДвижений_ПоПолучателям = ДанныеДвижений_ПоОП_ПоКоду[ИдКодПродПроизв]["Получатели"];
						
				// ХешИдЛицензии включает в себя ХешИдПолучателя,  т.е для одного получателя
				// с разными видами деятельности по лицензии ХешИдЛицензии будет разным,
				// и тем более будет разным для разных получателей.
				// Поэтому в качестве уникального идентификатора будем импользовать ХешИдЛицензии,
				// в силу того, что в справочнике Получатели теперь не отражаются лицензии, а только
				// виды деятельности, причем только один вид, а не как раньше в 11 декларации - можно 
				// было указать несколько лицензий.
				ИдПолучателя = "Х" + СтрокаДекларации.ХешИдПолучателя;
				ИдЛицензии = "Х" + СтрокаДекларации.ХешИдЛицензии;

				Если СправочникиОтчета["Получатели"].Получить(ИдПолучателя) = Неопределено Тогда

					СчетчикИдПолучателей = СчетчикИдПолучателей + 1;

					СправочникиОтчета["Получатели"].Вставить(ИдПолучателя, Новый Соответствие);

					СправочникиОтчета["Получатели"][ИдПолучателя].Вставить("ИдКонтр", СчетчикИдПолучателей);
					СправочникиОтчета["Получатели"][ИдПолучателя].Вставить("П000000000007", НаименованиеПолучателя);
					СправочникиОтчета["Получатели"][ИдПолучателя].Вставить("Лицензии", Новый Соответствие);
					
					Если ПолучательРоссийский Тогда
						// Россия.
						СправочникиОтчета["Получатели"][ИдПолучателя].Вставить("П000000000008", СтруктураАдресПолучателя);
						
						СправочникиОтчета["Получатели"][ИдПолучателя].Вставить("ЮЛ", ЗначениеЗаполнено(КПППолучателя));
						
						СправочникиОтчета["Получатели"][ИдПолучателя].Вставить("П000000000009", ИННПолучателя);						
						СправочникиОтчета["Получатели"][ИдПолучателя].Вставить("П000000000010", КПППолучателя);	
						
						Если ВидДеятельностиЗаполнен Тогда					
							СправочникиОтчета["Получатели"][ИдПолучателя].Вставить("П000000000011", ВидДеятельностиЛицензии);					
						КонецЕсли;
						
					Иначе
						
						СправочникиОтчета["Получатели"][ИдПолучателя].Вставить("П000000000081", КодСтраныПолучателя);
						СправочникиОтчета["Получатели"][ИдПолучателя].Вставить("П000000000082", АдресПолучателя);
						СправочникиОтчета["Получатели"][ИдПолучателя].Вставить("Номер", ИННПолучателя);
						
					КонецЕсли; 
					
				КонецЕсли;

				ТекущийПолучатель = СправочникиОтчета["Получатели"][ИдПолучателя];
				
				Если ЛицензияЗаполнена Тогда
							
					Лицензии = ТекущийПолучатель["Лицензии"];
					Если Лицензии.Получить(ИдЛицензии) = Неопределено Тогда
						
						СчетчикИдЛицензий = СчетчикИдЛицензий + 1;
						Лицензии.Вставить(ИдЛицензии, Новый Соответствие);
						Лицензии[ИдЛицензии].Вставить("ИдЛицензии", СчетчикИдЛицензий);
						Лицензии[ИдЛицензии].Вставить("П000000000011", НомерЛиц); // Номер.
						Лицензии[ИдЛицензии].Вставить("П000000000012", ДатаВыдачиЛиц); // Дата выдачи.
						Лицензии[ИдЛицензии].Вставить("П000000000013", ДатаОкончанияЛиц); // Дата окончания.
						Лицензии[ИдЛицензии].Вставить("П000000000014", КемВыданаЛиц); // Кем.
														
					КонецЕсли;
					
					ТекущаяЛицензия = ТекущийПолучатель["Лицензии"][ИдЛицензии];
					
				КонецЕсли;
								
				
				ИдДвижения = ИдПолучателя + ИдЛицензии;
				
				Если ДанныеДвижений_ПоПолучателям.Получить(ИдДвижения) = Неопределено Тогда
					
					ДанныеДвижений_ПоПолучателям.Вставить(ИдДвижения, Новый Соответствие);

					СтруктураПолучателя = ДанныеДвижений_ПоПолучателям[ИдДвижения];

					СтруктураПолучателя.Вставить("ХешИдПолучателя", СтрокаДекларации.ХешИдПолучателя);
					СтруктураПолучателя.Вставить("ХешИдЛицензии", СтрокаДекларации.ХешИдЛицензии);
					СтруктураПолучателя.Вставить("ИдПолучателя", ТекущийПолучатель["ИдКонтр"]);
					Если ЛицензияЗаполнена Тогда
						СтруктураПолучателя.Вставить("ИдЛицензии", ТекущаяЛицензия["ИдЛицензии"]);
					КонецЕсли;
					
					СтруктураПолучателя.Вставить("Поставка", Новый Соответствие);
					СтруктураПолучателя.Вставить("Возврат", Новый Соответствие);
					
				КонецЕсли;	

				СтруктураПолучателя = ДанныеДвижений_ПоПолучателям[ИдДвижения];
				
				ПоставкаИлиВозврат = ?(ИдГруппы = СтруктураРеквизитовФормы.мИдГруппы12, "Поставка", "Возврат");
				СтруктураНакладных = СтруктураПолучателя[ПоставкаИлиВозврат];
				
				КлючСтроки = "Х" + НомерСтрокиСтроковый;
				
				СтруктураНакладных.Вставить(КлючСтроки, Новый Соответствие);
				
				СтруктураНакладной = СтруктураНакладных[КлючСтроки];
				
				СтруктураНакладной.Вставить("П000000000012", СтрокаДекларации[ИмяДатыТТН]);
				СтруктураНакладной.Вставить("П000000000013", СтрокаДекларации[ИмяНомерТТН]);
				СтруктураНакладной.Вставить("П000000000014", СтрокаДекларации[ИмяНомерГТД]);
				СтруктураНакладной.Вставить("П000000000015", СтрокаДекларации[ИмяОбъемПоТТН]);
				
			КонецЕсли;

			Если НетОшибокПроверки и (ТекущееКоличествоОшибок > 0) Тогда
				НетОшибокПроверки = Ложь;
				ПроцентБезЗавершающейПроверкиДанных = 100;
				НужноОбработатьСтрок = НужноОбработатьСтрок / 2;
			КонецЕсли;

			Если ТекущееКоличествоОшибок > МаксимальноеКоличествоОшибок Тогда
				ОбработаноСтрок = НужноОбработатьСтрок;
				Возврат;
			КонецЕсли;

			// Информируем о ходе выполнения.
			Если (НужноОбработатьСтрок > 0) И (ОбработаноСтрок % ШагИнформирования = 0) Тогда

				РегламентированнаяОтчетностьАлко.СообщитьПроцентВыполнения("Проверка заполнения Декларации. " + ИмяТаблицы + ".", 
						ОбработаноСтрок, НужноОбработатьСтрок, ПроцентБезЗавершающейПроверкиДанных, РежимОтладки);

			КонецЕсли;

		КонецЦикла;

		// Информируем о ходе выполнения.
		Если НужноОбработатьСтрок > 0 Тогда

			РегламентированнаяОтчетностьАлко.СообщитьПроцентВыполнения("Проверка заполнения Декларации. " + ИмяТаблицы + ".", 
					ОбработаноСтрок, НужноОбработатьСтрок, ПроцентБезЗавершающейПроверкиДанных, РежимОтладки);

		КонецЕсли;

	КонецЦикла;
	
	Если ЕстьИдОП Тогда
		Ключ = "КолСтрокПо" + СокрИмяТаблицы;
		ДанныеОП["Декларация"][ИдОП].Вставить(Ключ, КолСтрокРаздела);
	КонецЕсли;
	
КонецПроцедуры

Функция ОтчетЗаполненКорректно()

	СтатусПроверки = Истина;

	ПроверитьЗаполнениеТитульный(СтатусПроверки);

	ПроверитьЗаполнениеЛицензии(СтатусПроверки);

	Если ТекущееКоличествоОшибок < МаксимальноеКоличествоОшибок Тогда

	// информируем о ходе выполнения				
		Если НужноОбработатьСтрок > 0 Тогда

			РегламентированнаяОтчетностьАлко.СообщитьПроцентВыполнения("Проверка заполнения Декларации.", 
					ОбработаноСтрок, НужноОбработатьСтрок, ПроцентБезЗавершающейПроверкиДанных, РежимОтладки);

		КонецЕсли;

		ПроверитьЗаполнениеДекларации(СтатусПроверки);

	КонецЕсли;

	КолСтрокПриПроверкеЗаполнения = ОбработаноСтрок;

	Возврат СтатусПроверки;

КонецФункции

Процедура ОтсечьНезаполненныеНеобязательныеУзлы(Узел)

	РегламентированнаяОтчетностьАЛКО.ОтсечьНезаполненныеНеобязательныеУзлыАЛКО(Узел);

КонецПроцедуры

Процедура ОбработатьУсловныеЭлементы(Знач ПараметрыВыгрузки, Узел)

	РегламентированнаяОтчетностьАЛКО.ОбработатьУсловныеЭлементыАЛКО(ПараметрыВыгрузки, Узел, ДокументСсылкаАлкоОтчет, ТаблицаСообщений, СтатусПроверки, ВерсияБСПЧислом);

КонецПроцедуры

Функция ПолучитьПодчиненныйЭлемент(Узел, КодЭлемента)

	Возврат РегламентированнаяОтчетностьАЛКО.ПолучитьПодчиненныйЭлементАЛКО(Узел, КодЭлемента);

КонецФункции

Функция НовыйУзелИзПрототипа(ПрототипУзла)

	Возврат РегламентированнаяОтчетностьАЛКО.НовыйУзелИзПрототипаАЛКО(ПрототипУзла);

КонецФункции

Процедура УстановитьЗначениеЭлемента(УзелРодитель, ИмяЭлемента,
		ЗначениеЭлемента)

	РегламентированнаяОтчетностьАЛКО.УстановитьЗначениеЭлементаАЛКО(УзелРодитель, ИмяЭлемента, ЗначениеЭлемента);

КонецПроцедуры

Функция ИдентификаторОП(СведенияОП)

	Возврат РегламентированнаяОтчетностьАЛКО.ИдентификаторОП_АЛКО(СведенияОП);

КонецФункции

Процедура ВывестиПоказательВXML(Узел, ЗначениеПоказателя)

	РегламентированнаяОтчетностьАЛКО.ВывестиПоказательВXML_АЛКО(Узел, ЗначениеПоказателя);

КонецПроцедуры

Функция ПроверитьДанные(Узел)

	Возврат РегламентированнаяОтчетностьАЛКО.ПроверитьДанныеАЛКО(Узел, ДокументСсылкаАлкоОтчет, ТаблицаСообщений, СтатусПроверки);

КонецФункции

Процедура ЗаполнитьСправочникиДекларации(УзелСправочники)

	ПрототипУзла_ПроизводителиИмпортеры = ПолучитьПодчиненныйЭлемент(УзелСправочники, "ПроизводителиИмпортеры");

	Для Каждого ЭлементСправочникиПроизводителиИмпортеры Из СправочникиОтчета["ПроизводителиИмпортеры"] Цикл

		СтрокаПроизводителяИмпортера = ЭлементСправочникиПроизводителиИмпортеры.Значение;

		НовыйУзелПроизводителиИмпортеры = НовыйУзелИзПрототипа(ПрототипУзла_ПроизводителиИмпортеры);

		УстановитьЗначениеЭлемента(НовыйУзелПроизводителиИмпортеры, "ИдПроизвИмп", СтрокаПроизводителяИмпортера["ИдПроизвИмп"]);
		УстановитьЗначениеЭлемента(НовыйУзелПроизводителиИмпортеры, "П000000000004", СтрокаПроизводителяИмпортера["П000000000004"]);
		УстановитьЗначениеЭлемента(НовыйУзелПроизводителиИмпортеры, "П000000000005", СтрокаПроизводителяИмпортера["П000000000005"]);
		УстановитьЗначениеЭлемента(НовыйУзелПроизводителиИмпортеры, "П000000000006", СтрокаПроизводителяИмпортера["П000000000006"]);

	КонецЦикла;
	
	СправочникиОтчета["ПроизводителиИмпортеры"].Очистить();
	
	РегламентированнаяОтчетность.УдалитьУзел(ПрототипУзла_ПроизводителиИмпортеры);

	ПрототипУзла_Получатели = ПолучитьПодчиненныйЭлемент(УзелСправочники, "Контрагенты");

	Для Каждого ЭлементСправочникиПолучатели Из СправочникиОтчета["Получатели"] Цикл

		СтрокаПолучателя = ЭлементСправочникиПолучатели.Значение;

		Узел_Получатели = НовыйУзелИзПрототипа(ПрототипУзла_Получатели);

		УстановитьЗначениеЭлемента(Узел_Получатели, "ИдКонтр", СтрокаПолучателя["ИдКонтр"]);
		УстановитьЗначениеЭлемента(Узел_Получатели, "П000000000007", СтрокаПолучателя["П000000000007"]);
		
		Узел_Резидент = ПолучитьПодчиненныйЭлемент(Узел_Получатели, "Резидент");
		Узел_Иностр = ПолучитьПодчиненныйЭлемент(Узел_Получатели, "Иностр");
		
		Если СтрокаПолучателя.Получить("ЮЛ") = Неопределено Тогда
		
			// Иностранец.
			РегламентированнаяОтчетность.УдалитьУзел(Узел_Резидент);
			
			УстановитьЗначениеЭлемента(Узел_Иностр, "П000000000081", СтрокаПолучателя["П000000000081"]);
			УстановитьЗначениеЭлемента(Узел_Иностр, "П000000000082", СтрокаПолучателя["П000000000082"]);
			УстановитьЗначениеЭлемента(Узел_Иностр, "Номер", СтрокаПолучателя["Номер"]);
						
		Иначе
			// Россия.
			РегламентированнаяОтчетность.УдалитьУзел(Узел_Иностр);
			
			Лицензии = СтрокаПолучателя.Получить("Лицензии");
			Если Лицензии.Количество() > 0 Тогда
			
				ПрототипУзла_Лицензии = ПолучитьПодчиненныйЭлемент(Узел_Резидент, "Лицензии");
				
				Для Каждого ЛицензияКлючЗначение Из Лицензии Цикл

					Лицензия = ЛицензияКлючЗначение.Значение;

					Узел_Лицензии = НовыйУзелИзПрототипа(ПрототипУзла_Лицензии);
					
					Узел_Лицензия = ПолучитьПодчиненныйЭлемент(Узел_Лицензии, "Лицензия");
					
					Для каждого ПоляЛицензии Из Лицензия Цикл
					
						УстановитьЗначениеЭлемента(Узел_Лицензия, ПоляЛицензии.Ключ, ПоляЛицензии.Значение);
					
					КонецЦикла; 
					
				КонецЦикла;
				
				РегламентированнаяОтчетность.УдалитьУзел(ПрототипУзла_Лицензии);
				
			КонецЕсли; 
			
			Узел_Адрес = ПолучитьПодчиненныйЭлемент(Узел_Резидент, "П000000000008");
			СтруктураАдреса = СтрокаПолучателя["П000000000008"];
			
			УстановитьЗначениеЭлемента(Узел_Адрес, "КодСтраны", 	СтруктураАдреса.КодСтраны);
			УстановитьЗначениеЭлемента(Узел_Адрес, "Индекс", 		СтруктураАдреса.Индекс);
			УстановитьЗначениеЭлемента(Узел_Адрес, "КодРегион", 	СтруктураАдреса.КодРегиона);
			УстановитьЗначениеЭлемента(Узел_Адрес, "Район", 		СтруктураАдреса.Район);
			УстановитьЗначениеЭлемента(Узел_Адрес, "Город", 		СтруктураАдреса.Город);
			УстановитьЗначениеЭлемента(Узел_Адрес, "НаселПункт", 	СтруктураАдреса.НаселенныйПункт);
			УстановитьЗначениеЭлемента(Узел_Адрес, "Улица", 		СтруктураАдреса.Улица);
			УстановитьЗначениеЭлемента(Узел_Адрес, "Дом", 			СтруктураАдреса.Дом);
			УстановитьЗначениеЭлемента(Узел_Адрес, "Корпус",	 	СтруктураАдреса.Корпус);
			УстановитьЗначениеЭлемента(Узел_Адрес, "Литера", 		СтруктураАдреса.Литера);
			УстановитьЗначениеЭлемента(Узел_Адрес, "Кварт", 		СтруктураАдреса.Квартира);
			
			Узел_ЮЛ = ПолучитьПодчиненныйЭлемент(Узел_Резидент, "ЮЛ");
			Узел_ФЛ = ПолучитьПодчиненныйЭлемент(Узел_Резидент, "ФЛ");
						
			Если СтрокаПолучателя["ЮЛ"] Тогда
				УстановитьЗначениеЭлемента(Узел_ЮЛ, "П000000000009", СтрокаПолучателя["П000000000009"]);
				УстановитьЗначениеЭлемента(Узел_ЮЛ, "П000000000010", СтрокаПолучателя["П000000000010"]);
				РегламентированнаяОтчетность.УдалитьУзел(Узел_ФЛ);
			Иначе
				УстановитьЗначениеЭлемента(Узел_ФЛ, "П000000000009", СтрокаПолучателя["П000000000009"]);
				РегламентированнаяОтчетность.УдалитьУзел(Узел_ЮЛ);			
			КонецЕсли;
			
			Если НЕ ПустаяСтрока(СтрокаПолучателя["П000000000011"]) Тогда
				УстановитьЗначениеЭлемента(Узел_Резидент, "П000000000011", СтрокаПолучателя["П000000000011"]);		
			КонецЕсли;
			
		КонецЕсли; 
		
	КонецЦикла;
	
	СправочникиОтчета["Получатели"].Очистить();

	РегламентированнаяОтчетность.УдалитьУзел(ПрототипУзла_Получатели);

КонецПроцедуры

Процедура ЗаполнитьСведенияДекларации(НовыйУзелОбъемОборота, ИдОП)
	

	ОбразецУзлаОборот = ПолучитьПодчиненныйЭлемент(НовыйУзелОбъемОборота, "Оборот");

	ДанныеДвиженийПоОП = ДанныеДвижений[ИдОП];

	НомерППКодаВидаПродукции = 0;

	Для Каждого ЭлементДвиженияПоОП Из ДанныеДвиженийПоОП Цикл
		
		КодВидаПродукцииКлюч = ЭлементДвиженияПоОП.Ключ;
		ЭлементДвиженияПоОП_ПоКоду = ЭлементДвиженияПоОП.Значение;
		
		КодВидаПродукции = ЭлементДвиженияПоОП_ПоКоду["КодПродукции"];

		НовыйУзелОборот = НовыйУзелИзПрототипа(ОбразецУзлаОборот);

		НомерППКодаВидаПродукции = НомерППКодаВидаПродукции + 1;

		УстановитьЗначениеЭлемента(НовыйУзелОборот, "ПN", НомерППКодаВидаПродукции);
		УстановитьЗначениеЭлемента(НовыйУзелОборот, "П000000000003", КодВидаПродукции);

		ОбразецУзлаСведПроизвИмпорт = ПолучитьПодчиненныйЭлемент(НовыйУзелОборот, "СведПроизвИмпорт");

		

		ДанныеДвижений_ПоОП_ПоКоду = ЭлементДвиженияПоОП_ПоКоду["КодПродПроизв"];

		НомерППСведенийОПроизводителеИмпортере = 0;

		Для Каждого Элемент_ДанныеДвижений_ПоОП_ПоКоду Из ДанныеДвижений_ПоОП_ПоКоду Цикл

			ИдКодПродПроизв = Элемент_ДанныеДвижений_ПоОП_ПоКоду.Ключ;
			СтрокаСведенийОПроизводителеИмпортере = Элемент_ДанныеДвижений_ПоОП_ПоКоду.Значение;

			НовыйУзелСведПроизвИмпорт = НовыйУзелИзПрототипа(ОбразецУзлаСведПроизвИмпорт);

			НомерППСведенийОПроизводителеИмпортере = НомерППСведенийОПроизводителеИмпортере
				+ 1;
			 
			ИдПроизвИмп = СтрокаСведенийОПроизводителеИмпортере["ИдПроизвИмп"];

			УстановитьЗначениеЭлемента(НовыйУзелСведПроизвИмпорт, "ПN", НомерППСведенийОПроизводителеИмпортере);
			УстановитьЗначениеЭлемента(НовыйУзелСведПроизвИмпорт, "ИдПроизвИмп", ИдПроизвИмп);

			// Сведения о получателях.
			ОбразецУзлаПолучатель = ПолучитьПодчиненныйЭлемент(НовыйУзелСведПроизвИмпорт, "Получатель");

			Получатели = СтрокаСведенийОПроизводителеИмпортере["Получатели"];
			НомерПППолучателя = 0;

			Для Каждого ЭлементПолучателя Из Получатели Цикл

				НовыйУзелПолучатель = НовыйУзелИзПрототипа(ОбразецУзлаПолучатель);

				НомерПППолучателя = НомерПППолучателя + 1;

				СтруктураПолучателя = ЭлементПолучателя.Значение;

				ИдПолучателя = СтруктураПолучателя["ИдПолучателя"];
				ИдЛицензии = СтруктураПолучателя["ИдЛицензии"];
				
				УстановитьЗначениеЭлемента(НовыйУзелПолучатель, "ПN", НомерПППолучателя);
				УстановитьЗначениеЭлемента(НовыйУзелПолучатель, "ИдПолучателя", ИдПолучателя);
				Если ИдЛицензии <> Неопределено Тогда				
					УстановитьЗначениеЭлемента(НовыйУзелПолучатель, "ИдЛицензии", ИдЛицензии)				
				КонецЕсли; 
				
				
				/////////////////////////////////////////////////////////////////
				// Сведения о поставках продукции.
				ОбразецУзлаПоставка = ПолучитьПодчиненныйЭлемент(НовыйУзелПолучатель, "Поставка");
				Поставки = СтруктураПолучателя["Поставка"];
				
				Для Каждого СтрокаПродукции Из Поставки Цикл

					НовыйУзелПоставка = НовыйУзелИзПрототипа(ОбразецУзлаПоставка);
					
					СтрокаПоставки = СтрокаПродукции.Значение;
					
					//Заполняем ключи с П000000000012 по П000000000015.
					Для Счетчик = 12 По 15 Цикл
	
						КолонкаСтрокаПродукции = Формат(Счетчик, "ЧЦ=2; ЧН=; ЧГ=0; ЧВН="); // 12, ..15.
												
						КлючВыгрузки = "П0000000000" + КолонкаСтрокаПродукции;
											   	
						УстановитьЗначениеЭлемента(НовыйУзелПоставка, КлючВыгрузки, СтрокаПоставки[КлючВыгрузки]);
						
					КонецЦикла;
					
					ОбработаноСтрок = ОбработаноСтрок + 1;

				КонецЦикла;
				
				РегламентированнаяОтчетность.УдалитьУзел(ОбразецУзлаПоставка);
				//
				/////////////////////////////////////////////////////////////////
				
				/////////////////////////////////////////////////////////////////
				// Сведения о возвратах продукции.
				ОбразецУзлаВозврат = ПолучитьПодчиненныйЭлемент(НовыйУзелПолучатель, "Возврат");
				Возвраты = СтруктураПолучателя["Возврат"];
				
				Для Каждого СтрокаПродукции Из Возвраты Цикл

					НовыйУзелВозврат = НовыйУзелИзПрототипа(ОбразецУзлаВозврат);
					
					СтрокаПоставки = СтрокаПродукции.Значение;

					//Заполняем ключи с П000000000016 по П000000000019.
					Для Счетчик = 12 По 15 Цикл
	
						КолонкаСтрокаПродукции = Формат(Счетчик, "ЧЦ=2; ЧН=; ЧГ=0; ЧВН="); // 12, ..15.
						КолонкаВыгрузки = Формат(Счетчик + 4, "ЧЦ=2; ЧН=; ЧГ=0; ЧВН="); // 16, ..19.
						
						// П000000000012 по П000000000015.
						КлючСтрокаПоставки = "П0000000000" + КолонкаСтрокаПродукции;
						// П000000000016 .. П000000000019.
						КлючВыгрузки = "П0000000000" + КолонкаВыгрузки;
											   	
						УстановитьЗначениеЭлемента(НовыйУзелВозврат, КлючВыгрузки, СтрокаПоставки[КлючСтрокаПоставки]);
						
					КонецЦикла;
					
					ОбработаноСтрок = ОбработаноСтрок + 1;

				КонецЦикла;
				
				РегламентированнаяОтчетность.УдалитьУзел(ОбразецУзлаВозврат);
				//
				/////////////////////////////////////////////////////////////////
				
				Получатели.Удалить(ЭлементПолучателя.Ключ);

			КонецЦикла;

			РегламентированнаяОтчетность.УдалитьУзел(ОбразецУзлаПолучатель);
			СтрокаСведенийОПроизводителеИмпортере.Удалить("Получатели");

			ДанныеДвижений_ПоОП_ПоКоду.Удалить(ИдКодПродПроизв);

		КонецЦикла;

		РегламентированнаяОтчетность.УдалитьУзел(ОбразецУзлаСведПроизвИмпорт);
		ДанныеДвиженийПоОП.Удалить(КодВидаПродукцииКлюч);

		// Информируем о ходе выполнения.
		Если НужноОбработатьСтрок > 0 Тогда

			РегламентированнаяОтчетностьАлко.СообщитьПроцентВыполнения("Заполнение данными дерева выгрузки.", 
					ОбработаноСтрок, НужноОбработатьСтрок, ПроцентБезЗавершающейПроверкиДанных, РежимОтладки);

		КонецЕсли;

	КонецЦикла;

	РегламентированнаяОтчетность.УдалитьУзел(ОбразецУзлаОборот);

КонецПроцедуры

Процедура ЗаполнитьСодержательнуюЧасть(УзелРодитель, КорневойУзел,
		ПараметрыВыгрузки)

	УзелСправочники = ПолучитьПодчиненныйЭлемент(КорневойУзел, "Справочники");
	
	ЗаполнитьСправочникиДекларации(УзелСправочники);
	
	Узел_Документ = УзелРодитель;

	Узел_Организация = ПолучитьПодчиненныйЭлемент(Узел_Документ, "Организация");
	
	Узел_Деятельность = ПолучитьПодчиненныйЭлемент(Узел_Организация, "Деятельность");
	
	Если НЕ Узел_Деятельность = Неопределено Тогда
	
		// Формат Выгрузки 4.40
		Узел_Лицензируемая = ПолучитьПодчиненныйЭлемент(Узел_Деятельность, "Лицензируемая");
		Узел_Нелицензируемая = ПолучитьПодчиненныйЭлемент(Узел_Деятельность, "Нелицензируемая");

		ДанныеОтчетаЛицензии = ТаблицаСтраницыЛицензии;
		ДопДанные = ДанныеОтчетаЛицензии[0].ДополнительныеДанные[0].Значение;

		ЭтоЛицензируемаяДеятельность = ЛицензируемаяДеятельность();

		Если ЭтоЛицензируемаяДеятельность Тогда
			
			РегламентированнаяОтчетность.УдалитьУзел(Узел_Нелицензируемая);
			ПрототипУзла_Лицензия = ПолучитьПодчиненныйЭлемент(Узел_Лицензируемая, "Лицензия");
			
			Для Каждого Страница Из ДанныеОтчетаЛицензии Цикл
				
				ДанныеСтраницы = Страница.Данные[0].Значение;
				
				Если Не УказанаЛицензия(ДанныеСтраницы) Тогда
					Продолжить;
				КонецЕсли;
				
				ДополнительныеДанныеСтраницы = Страница.ДополнительныеДанные[0].Значение;
				Узел_Лицензия = НовыйУзелИзПрототипа(ПрототипУзла_Лицензия);
				УстановитьЗначениеЭлемента(Узел_Лицензия, "ВидДеят", ЗначениеПоКлючуСтруктуры("П000000000105", ДополнительныеДанныеСтраницы));
				УстановитьЗначениеЭлемента(Узел_Лицензия, "СерНомЛиц", ДанныеСтраницы.П000000000101
					+ " " + ДанныеСтраницы.П000000000102);
				УстановитьЗначениеЭлемента(Узел_Лицензия, "ДатаНачЛиц", ДанныеСтраницы.П000000000103);
				
				// Заполнение для разных вариантов XSD.
				// Ошибки не будет - перед заполнением проверяется наличие элемента.
				УстановитьЗначениеЭлемента(Узел_Лицензия, "ДатаОкончЛиц", ДанныеСтраницы.П000000000104);								
				УстановитьЗначениеЭлемента(Узел_Лицензия, "ДатаОконЛиц", ДанныеСтраницы.П000000000104);
								
			КонецЦикла;
			РегламентированнаяОтчетность.УдалитьУзел(ПрототипУзла_Лицензия);
			
		Иначе
			РегламентированнаяОтчетность.УдалитьУзел(Узел_Лицензируемая);
			УстановитьЗначениеЭлемента(Узел_Нелицензируемая, "ВидДеят", ЗначениеПоКлючуСтруктуры("П000000000105", ДопДанные));
		КонецЕсли;
	КонецЕсли;

	ПрототипУзла_ОбъемОборота = ПолучитьПодчиненныйЭлемент(Узел_Документ, "ОбъемОборота");

	Для Каждого ЭлементОП Из ДанныеОП["Декларация"] Цикл

		ИдОП = ЭлементОП.Ключ;
		СтруктураОП = ЭлементОП.Значение;

		СведенияОбОП = СтруктураОП["ОП"];

		// Заполняем сведения по ОП.
		НазваниеОП = СведенияОбОП.Наименование;
		КПП = СведенияОбОП.КПП;

		Если ПустаяСтрока(НазваниеОП) И ПустаяСтрока(КПП) Тогда
			Продолжить;
		КонецЕсли;

		Узел_ОбъемОборота = НовыйУзелИзПрототипа(ПрототипУзла_ОбъемОборота);

		// Заполнение для разных вариантов XSD.
		// Ошибки не будет - перед заполнением проверяется наличие элемента.
		УстановитьЗначениеЭлемента(Узел_ОбъемОборота, "Наим", НазваниеОП);				
		УстановитьЗначениеЭлемента(Узел_ОбъемОборота, "НаимЮЛ", НазваниеОП);		
				
		Если ЗначениеЗаполнено(КПП) Тогда
			УстановитьЗначениеЭлемента(Узел_ОбъемОборота, "КППЮЛ", КПП);
		КонецЕсли;

		Узел_АдрОрг = ПолучитьПодчиненныйЭлемент(Узел_ОбъемОборота, "АдрОрг");
		УстановитьЗначениеЭлемента(Узел_АдрОрг, "КодСтраны", СведенияОбОП.КодСтраны);
		УстановитьЗначениеЭлемента(Узел_АдрОрг, "Индекс", СведенияОбОП.Индекс);
		УстановитьЗначениеЭлемента(Узел_АдрОрг, "КодРегион", СведенияОбОП.КодРегиона);
		УстановитьЗначениеЭлемента(Узел_АдрОрг, "Район", СведенияОбОП.Район);
		УстановитьЗначениеЭлемента(Узел_АдрОрг, "Город", СведенияОбОП.Город);
		УстановитьЗначениеЭлемента(Узел_АдрОрг, "НаселПункт", СведенияОбОП.НаселенныйПункт);
		УстановитьЗначениеЭлемента(Узел_АдрОрг, "Улица", СведенияОбОП.Улица);
		УстановитьЗначениеЭлемента(Узел_АдрОрг, "Дом", СведенияОбОП.Дом);
		УстановитьЗначениеЭлемента(Узел_АдрОрг, "Корпус", СведенияОбОП.Корпус);
		УстановитьЗначениеЭлемента(Узел_АдрОрг, "Литера", СведенияОбОП.Литера);
		УстановитьЗначениеЭлемента(Узел_АдрОрг, "Кварт", СведенияОбОП.Квартира);

		ЗаполнитьСведенияДекларации(Узел_ОбъемОборота, ИдОП);
				
	КонецЦикла;

	// Для пустой декларации надо выгрузить хотя бы один узел ОбъемОборота.
	// В качестве ОП - сама организация.
	Если ДанныеОП["Декларация"].Количество() = 0 Тогда

	// Заполняем сведения по ОП.
		НазваниеОргСокр = ПараметрыВыгрузки.НаимСокр;
		КПП = "";
		ПараметрыВыгрузки.Свойство("КППЮЛ", КПП);

		Узел_ОбъемОборота = НовыйУзелИзПрототипа(ПрототипУзла_ОбъемОборота);

		
		// Заполнение для разных вариантов XSD.
		// Ошибки не будет - перед заполнением проверяется наличие элемента.
		УстановитьЗначениеЭлемента(Узел_ОбъемОборота, "Наим", НазваниеОргСокр);				
		УстановитьЗначениеЭлемента(Узел_ОбъемОборота, "НаимЮЛ", НазваниеОргСокр);
		
		Если ЗначениеЗаполнено(КПП) Тогда
			УстановитьЗначениеЭлемента(Узел_ОбъемОборота, "КППЮЛ", КПП);
		КонецЕсли;

		АдресОрганизации = ПараметрыВыгрузки.АдресОрганизации;
		Узел_АдрОрг = ПолучитьПодчиненныйЭлемент(Узел_ОбъемОборота, "АдрОрг");
		УстановитьЗначениеЭлемента(Узел_АдрОрг, "КодСтраны", АдресОрганизации.КодСтраны);
		УстановитьЗначениеЭлемента(Узел_АдрОрг, "Индекс", АдресОрганизации.Индекс);
		УстановитьЗначениеЭлемента(Узел_АдрОрг, "КодРегион", АдресОрганизации.КодРегиона);
		УстановитьЗначениеЭлемента(Узел_АдрОрг, "Район", АдресОрганизации.Район);
		УстановитьЗначениеЭлемента(Узел_АдрОрг, "Город", АдресОрганизации.Город);
		УстановитьЗначениеЭлемента(Узел_АдрОрг, "НаселПункт", АдресОрганизации.НаселенныйПункт);
		УстановитьЗначениеЭлемента(Узел_АдрОрг, "Улица", АдресОрганизации.Улица);
		УстановитьЗначениеЭлемента(Узел_АдрОрг, "Дом", АдресОрганизации.Дом);
		УстановитьЗначениеЭлемента(Узел_АдрОрг, "Корпус", АдресОрганизации.Корпус);
		УстановитьЗначениеЭлемента(Узел_АдрОрг, "Литера", АдресОрганизации.Литера);
		УстановитьЗначениеЭлемента(Узел_АдрОрг, "Кварт", АдресОрганизации.Квартира);
		
	КонецЕсли;

	РегламентированнаяОтчетность.УдалитьУзел(ПрототипУзла_ОбъемОборота);

	КолСтрокЗаполнено = ОбработаноСтрок - КолСтрокПриПроверкеЗаполнения;

	Если КолСтрокЗаполнено <> КолСтрокПриПроверкеЗаполнения Тогда

	// Выгружено не то количество, что есть по данным заполнения отчета.
		ТекстОписанияОшибки = "В выгрузку попало меньшее количество строк, чем заполнено в Декларации!
			|Немедленно свяжитесь с техподдержкой, желательно выслать выгрузку 
			|Вашей информационной базы в виде файла .dt";

		СтруктураОшибки = Новый Структура;
		СтруктураОшибки.Вставить("ТекстОписанияОшибки", ТекстОписанияОшибки);
		СтруктураОшибки.Вставить("Раздел", "Титульный");
		СтруктураОшибки.Вставить("Показатель", Неопределено);
		СтруктураОшибки.Вставить("ЛистРаздела", Неопределено);
		СтруктураОшибки.Вставить("ТипОшибки", "НеВсяИнформацияПопалаВВыгрузку");

		СообщитьОбОшибкеЗаполнения(ДокументСсылкаАлкоОтчет, ТаблицаСообщений, СтатусПроверки, СтруктураОшибки,
									МаксимальноеКоличествоОшибок, ТекущееКоличествоОшибок);

	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьДаннымиУзел(ПараметрыВыгрузки, Узел,
		НомерСтроки = Неопределено)

	Для Каждого Стр из Узел.Строки Цикл
		Если Стр.Тип = "С" ИЛИ Стр.Тип = "C" Тогда // учтем оба варианта: кириллицу и латиницу
			Если ПараметрыВыгрузки.Свойство("ИмяКлючевогоУзлаСодержательнойЧасти")
					И Стр.Код = ПараметрыВыгрузки.ИмяКлючевогоУзлаСодержательнойЧасти Тогда
				ЗаполнитьДаннымиУзел(ПараметрыВыгрузки, Стр);
				ЗаполнитьСодержательнуюЧасть(Стр, Узел, ПараметрыВыгрузки);
			Иначе
				ЗаполнитьДаннымиУзел(ПараметрыВыгрузки, Стр);
			КонецЕсли;
		Иначе
			Если ПустаяСтрока(Стр.ЗначениеПоУмолчанию) Тогда

				Если НЕ ПустаяСтрока(Стр.Ключ) Тогда

					НайденКлюч = Истина;
					КлючВыгрузки = Строка(Стр.Ключ);
					ПозицияТочки = СтрНайти(КлючВыгрузки, ".");
					СтруктураПараметровВыгрузки = ПараметрыВыгрузки;

					Пока ПозицияТочки > 0 Цикл

						ПромежуточныйКлюч = Лев(КлючВыгрузки, ПозицияТочки - 1);
						КлючВыгрузки = Сред(КлючВыгрузки, ПозицияТочки + 1);

						Если СтруктураПараметровВыгрузки.Свойство(ПромежуточныйКлюч) Тогда
							СтруктураПараметровВыгрузки = СтруктураПараметровВыгрузки[ПромежуточныйКлюч];
						Иначе
							НайденКлюч = Ложь;
							Прервать;
						КонецЕсли;

						ПозицияТочки = СтрНайти(КлючВыгрузки, ".");

					КонецЦикла;

					Если НайденКлюч Тогда
						
						ЗначениеПараметра = Неопределено;
                    	СтруктураПараметровВыгрузки.Свойство(КлючВыгрузки, ЗначениеПараметра);
						
						Если ЗначениеЗаполнено(ЗначениеПараметра) Тогда
							ВывестиПоказательВXML(Стр, ЗначениеПараметра);
						Иначе	
							Стр.Значение = "";
						КонецЕсли; 
                    	
					Иначе
						Стр.Значение = "";
					КонецЕсли;

				Иначе
					Стр.Значение = "";
				КонецЕсли;

			ИначеЕсли Лев(Стр.ЗначениеПоУмолчанию, 1) = "&" Тогда

				Ключ = Сред(Стр.ЗначениеПоУмолчанию, 2);

				НайденКлюч = Истина;
				КлючВыгрузки = Строка(Ключ);
				ПозицияТочки = СтрНайти(КлючВыгрузки, ".");
				СтруктураПараметровВыгрузки = ПараметрыВыгрузки;

				Пока ПозицияТочки > 0 Цикл

					ПромежуточныйКлюч = Лев(КлючВыгрузки, ПозицияТочки - 1);
					КлючВыгрузки = Сред(КлючВыгрузки, ПозицияТочки + 1);

					Если СтруктураПараметровВыгрузки.Свойство(ПромежуточныйКлюч) Тогда
						СтруктураПараметровВыгрузки = СтруктураПараметровВыгрузки[ПромежуточныйКлюч];
					Иначе
						НайденКлюч = Ложь;
						Прервать;
					КонецЕсли;

					ПозицияТочки = СтрНайти(КлючВыгрузки, ".");

				КонецЦикла;

				Если НайденКлюч Тогда
						
					ЗначениеПараметра = Неопределено;
                	СтруктураПараметровВыгрузки.Свойство(КлючВыгрузки, ЗначениеПараметра);
					
					Если ЗначениеЗаполнено(ЗначениеПараметра) Тогда
						ВывестиПоказательВXML(Стр, ЗначениеПараметра);
					Иначе	
						Стр.Значение = "";
					КонецЕсли; 
                	
				Иначе
					Стр.Значение = "";
				КонецЕсли;

			Иначе
				ВывестиПоказательВXML(Стр, Стр.ЗначениеПоУмолчанию);				
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

Процедура ЗаполнитьДанными()

	Если НужноОбработатьСтрок > 0 Тогда

		РегламентированнаяОтчетностьАлко.СообщитьПроцентВыполнения("Заполнение данными дерева выгрузки.", 
				ОбработаноСтрок, НужноОбработатьСтрок, ПроцентБезЗавершающейПроверкиДанных, РежимОтладки);

	КонецЕсли;

	ОбработатьУсловныеЭлементы(ПараметрыВыгрузки, ДеревоВыгрузки); // формируем точную структуру XML-макета
	ЗаполнитьДаннымиУзел(ПараметрыВыгрузки, ДеревоВыгрузки); // заполняем дерево данными
	Если НужноОбработатьСтрок > 0 Тогда

		РегламентированнаяОтчетностьАлко.СообщитьПроцентВыполнения("Заполнение данными дерева выгрузки.", 
				ОбработаноСтрок, НужноОбработатьСтрок, ПроцентБезЗавершающейПроверкиДанных, РежимОтладки);

	КонецЕсли;

	ОтсечьНезаполненныеНеобязательныеУзлы(ДеревоВыгрузки);

	Если НужноОбработатьСтрок > 0 Тогда

		РегламентированнаяОтчетностьАлко.СообщитьПроцентВыполнения("Заполнение данными дерева выгрузки.", 
				ПроцентБезЗавершающейПроверкиДанных, 100, , РежимОтладки);

	КонецЕсли;

КонецПроцедуры

Функция ПроверитьВыгрузку(ПараметрыДокумента, ЭтоВыгрузка = Ложь) Экспорт

	ОбъектЭтогоОтчета = ЭтотОбъект;
	
	ВерсияБСПЧислом = РегламентированнаяОтчетностьАлко.ПолучитьВерсиюБСПЧислом();

	РежимОтладки = ОбщегоНазначенияКлиентСервер.РежимОтладки();

	ДокументСсылкаАлкоОтчет = ПараметрыДокумента.ДокументСсылка;

	ИмяФормыОбъекта = ПараметрыДокумента.ИмяФормыОбъекта;
	ВспомогательныйАдрес = ПараметрыДокумента.ВспомогательныйАдрес;
	ПараметрыФормы = ПараметрыДокумента.ПараметрыФормы;
	ПараметрыВыгрузки = ПараметрыДокумента.ПараметрыВыгрузки;

	СоответствиеКешАдресов = Новый Соответствие;

	// Таблица результата.
	ТаблицаСообщений = РегламентированнаяОтчетностьАлко.СоздатьТаблицуСообщенийОбОшибках();
	ТаблицаСообщенийДляСведения = РегламентированнаяОтчетностьАлко.СоздатьТаблицуСообщенийОбОшибках();

	ТаблицаСтраницыЛицензии = ПараметрыФормы.ТаблицаСтраницыЛицензии;
	ТаблицаСтраницыДекларация = ПараметрыФормы.ТаблицаСтраницыДекларация;
	
	СтруктураРеквизитовФормы = ПараметрыФормы.СтруктураРеквизитовФормы;
	СтруктураСервисРегистров = ПараметрыФормы.СтруктураСервисРегистров;
	
	ОбработаноСтрок = 0;
	ШагИнформирования = 200;
	НетОшибокПроверки = Истина;

	ДатаНачалаОтчета = СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета;
	ДатаКонцаОтчета = СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета;
	НижняяГраницаДатыПоXSD = Неопределено;
	ВерхняяГраницаДатыПоXSD = Неопределено;
	
	МаксимальноеКоличествоОшибок = 1000;
	ТекущееКоличествоОшибок = 0;
	МаксимальноеКоличествоПредупреждений = 1000; 
	ТекущееКоличествоПредупреждений = 0;
	
	ДеревоВыгрузки = ИзвлечьСтруктуруXML();

	ВсегоСтрокПоРазделу1 = ПолучитьКоличествоСтрокПоГруппе(СтруктураРеквизитовФормы.мИдГруппы12, Истина);
	ВсегоСтрокПоРазделу2 = ПолучитьКоличествоСтрокПоГруппе(СтруктураРеквизитовФормы.мИдГруппы22, Истина);
	
	ДанныеДвижений = Новый Соответствие;
	
	ДанныеОП = Новый Соответствие;
	ДанныеОП.Вставить("Декларация", Новый Соответствие);
		
	СправочникиОтчета = Новый Соответствие;
	СправочникиОтчета.Вставить("ПроизводителиИмпортеры", Новый Соответствие);
	СправочникиОтчета.Вставить("Получатели", Новый Соответствие);
		
	СчетчикИдПроизвИмп = 0;
	СчетчикИдПолучателей = 0;
	СчетчикИдЛицензий = 0;
	
	// Один проход при проверке заполнения,
	// второй - при заполнении выгрузки - все вместе 90%
	// на завершающую проверку данных отводим 10%.
	НужноОбработатьСтрок = 2 * (ВсегоСтрокПоРазделу1 + ВсегоСтрокПоРазделу2);
	// При выгрузке проверка заполнения строк и заполнение дерева выгрузки составляет примерно 84%.
	// При проверке выгрузки - примерно 90% от общего времени выполнения.
	ПроцентБезЗавершающейПроверкиДанных = ?(ЭтоВыгрузка, 84, 90);
	

	// ОписаниеСтруктурыАдресаИзОтчета	- Структура.
	//		Поля структуры соответствуют структуре, получаемой при вызове
	//		функции ПолучитьПустуюСтруктуруАдреса(). 
	//		Может не содержать поля УникальныйНомерФИАС, ДополнительныеКоды, АдресXML, КодСтраны
	//		Страна, КодРегиона, Регион, ТипДома, ТипКорпуса, ТипКвартиры, ПредставлениеАдреса.
	//		При наличии перечисленных полей - они будут удалены!
	//		
	//		Значениями всех полей структуры является структура вида:
	//			* ПредставлениеПоля - осмысленное название поля адреса.
	//			* МинДлина - минимальная длина строки поля адреса, к которому относится структура.
	//			* МаксДлина- максимальная длина строки поля адреса, к которому относится структура.
	//			* ТолькоЦифры - Булево. Признак что в поле адреса должны быть только цифры.
	//
	// Эта структура используется в РегламентированнаяОтчетностьАЛКО.ПроверитьАдрес() для контроля типа и длины полей.
	// Если не указана - в РегламентированнаяОтчетностьАЛКО.ПроверитьАдрес() используется структура по умолчанию,
	// на момент написания кода структура по умолчанию соответствует описанию адреса во всех схемах XSD ФСРАР.
	//
	// Если в будущем описание адреса для конкретных отчетов изменится - нужно заполнять структуру ОписаниеСтруктурыАдресаИзОтчета
	// для этого отчета. Если описание изменится для всех отчетов - нужно менять описание структуры в
	// РегламентированнаяОтчетностьАЛКО.ПроверитьАдрес().
	ОписаниеСтруктурыАдресаИзОтчета = Неопределено;
	
	ИмяОбластиКодовМакета = "ВидыДеятельности";
	КоллекцияСписковВыбора = Неопределено;
	СписокКодовДеятельности = РегламентированнаяОтчетностьАЛКО.МассивКодовИзМакетаСписки(ИмяОбластиКодовМакета, 
			ДатаКонцаОтчета, ИмяФормыОбъекта, ОбъектЭтогоОтчета, , КоллекцияСписковВыбора);
	ИмяОбластиКодовМакета = "ВидыПродукции";
	СписокКодовПродукции = РегламентированнаяОтчетностьАЛКО.МассивКодовИзМакетаСписки(ИмяОбластиКодовМакета, 
			ДатаКонцаОтчета, ИмяФормыОбъекта, ОбъектЭтогоОтчета, , КоллекцияСписковВыбора);

	Если НЕ ОтчетЗаполненКорректно() Тогда

		Результат = Новый Структура;
		Результат.Вставить("ТаблицаСообщений", ТаблицаСообщений);
		Результат.Вставить("ТаблицаСообщенийДляСведения", ТаблицаСообщенийДляСведения);
		Возврат Результат;

	КонецЕсли;

	// Информируем о ходе выполнения.
	Если НужноОбработатьСтрок > 0 Тогда

		РегламентированнаяОтчетностьАлко.СообщитьПроцентВыполнения("Заполнение данными дерева выгрузки.", 
				ОбработаноСтрок, НужноОбработатьСтрок, ПроцентБезЗавершающейПроверкиДанных, РежимОтладки);

	КонецЕсли;

	ЗаполнитьДанными();

	Если ТекущееКоличествоОшибок > 0 Тогда

		Результат = Новый Структура;
		Результат.Вставить("ТаблицаСообщений", ТаблицаСообщений);
		Результат.Вставить("ТаблицаСообщенийДляСведения", ТаблицаСообщенийДляСведения);
		Возврат Результат;

	КонецЕсли;

	// Информируем о ходе выполнения.				
	Если НужноОбработатьСтрок > 0 Тогда

		РегламентированнаяОтчетностьАлко.СообщитьПроцентВыполнения("Проверка данных по схеме выгрузки.", 
				ПроцентБезЗавершающейПроверкиДанных, 100, , РежимОтладки);

	КонецЕсли;

	Если НЕ ПроверитьДанные(ДеревоВыгрузки) Тогда
	// Информируем о ходе выполнения.
		Если НужноОбработатьСтрок > 0 Тогда

			РегламентированнаяОтчетностьАлко.СообщитьПроцентВыполнения("", 100, 100, , РежимОтладки);

		КонецЕсли;

		Результат = Новый Структура;
		Результат.Вставить("ТаблицаСообщений", ТаблицаСообщений);
		Результат.Вставить("ТаблицаСообщенийДляСведения", ТаблицаСообщенийДляСведения);
		Возврат Результат;

	КонецЕсли;

	// Информируем о ходе выполнения.				
	Если НужноОбработатьСтрок > 0 Тогда

		РегламентированнаяОтчетностьАлко.СообщитьПроцентВыполнения(?(ЭтоВыгрузка, "Запись выгрузки в XML.", ""), 
				?(ЭтоВыгрузка, 93, 100), 100, , РежимОтладки);

	КонецЕсли;

	Результат = Новый Структура;
	Результат.Вставить("ТаблицаСообщений", ТаблицаСообщений);
	Результат.Вставить("ТаблицаСообщенийДляСведения", ТаблицаСообщенийДляСведения);

	Возврат Результат;

КонецФункции

Функция СписокРазделовОшибок() Экспорт
	
	СписокРазделов = Новый Массив;
	СписокРазделов.Добавить("Титульный");
	СписокРазделов.Добавить("Лицензии");
	СписокРазделов.Добавить("Декларация");
	
	Возврат СписокРазделов;

КонецФункции
#КонецОбласти

#Область Выгрузка

Функция ВыгрузитьОтчет(ПараметрыДокумента) Экспорт

	ЭтоВыгрузка = Истина;

	Результат = ПроверитьВыгрузку(ПараметрыДокумента, ЭтоВыгрузка);

	Если ТекущееКоличествоОшибок > 0 Тогда
	// Есть сообщения о ошибках
		Возврат Результат;
	КонецЕсли;

	// Проверка прошла успешно.
	// Пишем XML в файл.
	ИмяФайлаXML = Неопределено;

	// расширение по умолчанию - .tmp
	ИмяФайлаXML = ПолучитьИмяВременногоФайла();

	РегламентированнаяОтчетностьАЛКО.ВыгрузитьДеревоВXML(ДеревоВыгрузки, ПараметрыВыгрузки, ИмяФайлаXML);

	// информируем о ходе выполнения				
	Если НужноОбработатьСтрок > 0 Тогда

		РегламентированнаяОтчетностьАлко.СообщитьПроцентВыполнения("", 100, 100, , РежимОтладки);

	КонецЕсли;

	Результат.Вставить("ИмяФайлаXML", ИмяФайлаXML);

	Возврат Результат;

КонецФункции
#КонецОбласти

#Область Печать

Функция ПечататьРаздел(СтрокаРаздела)

	// Если в настройках взведен флаг печати, то добавляем раздел в список печатаемых и выходим.
	Если СтрокаРаздела.ВыводНаПечать <> 0 Тогда
		СтруктураРеквизитовФормы.мПечатаемыеРазделы.Добавить(СтрокаРаздела.ИмяСтраницы);
		Возврат 1;
	КонецЕсли;

	Возврат 0;

КонецФункции

Функция ПечатьОтчета(ПараметрыДокумента) Экспорт

	ОбъектЭтогоОтчета = ЭтотОбъект;
	
	ДокументСсылкаАлкоОтчет = ПараметрыДокумента.ДокументСсылка;

	ИмяФормыОбъекта = ПараметрыДокумента.ИмяФормыОбъекта;
	ВспомогательныйАдрес = ПараметрыДокумента.ВспомогательныйАдрес;
	ПараметрыФормы = ПараметрыДокумента.ПараметрыФормы;

	ВидПечати = ПараметрыДокумента.ВидПечати;

	ТаблицаСтраницыЛицензии = ПараметрыФормы.ТаблицаСтраницыЛицензии;
	ТаблицаСтраницыДекларация = ПараметрыФормы.ТаблицаСтраницыДекларация;
	
	СтруктураСервисРегистров = ПараметрыФормы.СтруктураСервисРегистров;

	СтруктураРеквизитовФормы = ПараметрыФормы.СтруктураРеквизитовФормы;

	ВсегоСтрокПоЛицензиям = ПолучитьКоличествоСтрокПоГруппе(СтруктураРеквизитовФормы.мИдГруппы01, Истина);
	ВсегоСтрокПоРазделу1 = ПолучитьКоличествоСтрокПоГруппе(СтруктураРеквизитовФормы.мИдГруппы12, Истина);
	ВсегоСтрокПоРазделу2 = ПолучитьКоличествоСтрокПоГруппе(СтруктураРеквизитовФормы.мИдГруппы22, Истина);

	// Один проход при выводе в целом по организации,
	// второй - при выводе по ОП
	// Лицензии выводятся один раз.
	// Все это в 2 кратном размере, поскольку сначала вся информация отбирается в запросе.
	НужноОбработатьСтрок = (2 * (ВсегоСтрокПоРазделу1 + ВсегоСтрокПоРазделу2)
		+ ВсегоСтрокПоЛицензиям) * 2;

	ОбработаноСтрок = 0;

	ТипЭкспорта = ?(СтруктураРеквизитовФормы.Свойство("ТипЭкспорта"),
					СтруктураРеквизитовФормы.ТипЭкспорта, Неопределено);
	
	ПроцентБезКонвертацииАрхивации = 
		РегламентированнаяОтчетностьАЛКОКлиентСервер.ПроцентБезКонвертацииАрхивации(ВидПечати, ТипЭкспорта);

	СтруктураДанныхТитульный = ПараметрыФормы.СтруктураДанныхТитульный;
	мДеревоВыбранныхСтраниц = ПараметрыФормы.мДеревоВыбранныхСтраниц;
	НомерКорректировки = ПараметрыФормы.НомерКорректировки;
	ДополнительныеДанные = ПараметрыФормы.ДополнительныеДанные;
	ДатаПодписи = ПараметрыФормы.ДатаПодписи;

	СтруктураПараметров = Новый Структура;

	Для Каждого СтрокаУровня1 Из мДеревоВыбранныхСтраниц.Строки Цикл

		Пометка = ПечататьРаздел(СтрокаУровня1);

		ИмяСтраницыПанели = СтрокаУровня1.ИмяСтраницы;
		НаименованиеЛиста = СтрокаУровня1.Представление;
		ОриентацияЛиста = СтрокаУровня1.ОриентацияСтраницы;
		ИмяОбластиТаблПоля = СтрокаУровня1.ИмяОбластиТабличногоПоля;

		Если Пометка = 0 Тогда
			Продолжить;
		КонецЕсли;

		СтруктураПараметров.Очистить();
		СтруктураПараметров.Вставить("НаименованиеЛиста", НаименованиеЛиста);
		СтруктураПараметров.Вставить("ОриентацияЛиста", ОриентацияЛиста);
		СтруктураПараметров.Вставить("ИмяСтраницы", ИмяСтраницыПанели);
		СтруктураПараметров.Вставить("ИмяРаздела", ИмяСтраницыПанели);
		Структурапараметров.Вставить("ИмяОбластиТаблПоля", ИмяОбластиТаблПоля);
		СтруктураПараметров.Вставить("ВидПечати", ВидПечати);
		СтруктураПараметров.Вставить("ИмяФормыОбъекта", ИмяФормыОбъекта);

		Если ИмяСтраницыПанели = "Титульный" Тогда

			ВывестиНаПечатьТитульный(СтруктураПараметров);

		ИначеЕсли ИмяСтраницыПанели = "Декларация" Тогда

			СтруктураПараметров.Вставить("ИмяРаздела", "Раздел1");
			ВывестиНаПечатьРазделДекларации(СтруктураПараметров);
			
			СтруктураПараметров.Вставить("ИмяРаздела", "Раздел2");
			ВывестиНаПечатьРазделДекларации(СтруктураПараметров);
					
		ИначеЕсли ИмяСтраницыПанели = "Лицензии" Тогда

			ВывестиНаПечатьРазделЛицензии(СтруктураПараметров);

		КонецЕсли;

	КонецЦикла;

	// Информируем о ходе выполнения.				
	Если НужноОбработатьСтрок > 0 Тогда

		РегламентированнаяОтчетностьАлко.СообщитьПроцентВыполнения(
				?(ВидПечати = "ПечатьФайлZIP", "Конвертация в выбранный формат и архивация.", ""), 
				НужноОбработатьСтрок, НужноОбработатьСтрок, ПроцентБезКонвертацииАрхивации, РежимОтладки);

	КонецЕсли;

	Возврат СтруктураРеквизитовФормы;

КонецФункции

Процедура ВывестиНаПечатьТитульный(СтруктураПараметровСтраницы)

	НастройкиСтраницы = Новый Структура;

	НастройкиСтраницы.Вставить("ОтображатьЗаголовки", Ложь);
	НастройкиСтраницы.Вставить("ОтображатьСетку", Ложь);
	НастройкиСтраницы.Вставить("ЧерноБелыйПросмотр", Истина);
	НастройкиСтраницы.Вставить("ЧерноБелаяПечать", Истина);
	НастройкиСтраницы.Вставить("Автомасштаб", Истина);
	НастройкиСтраницы.Вставить("ОриентацияСтраницы", ОриентацияСтраницы.Портрет);
	НастройкиСтраницы.Вставить("ПолеСверху", 5);
	НастройкиСтраницы.Вставить("ПолеСнизу", 5);
	НастройкиСтраницы.Вставить("ПолеСлева", 5);
	НастройкиСтраницы.Вставить("ПолеСправа", 5);

	СтруктураПараметровСтраницы.Вставить("НастройкиСтраницы", НастройкиСтраницы);
	СтруктураПараметровСтраницы.Вставить("СтруктураДанныхТитульный", СтруктураДанныхТитульный);
	СтруктураПараметровСтраницы.Вставить("ДокументСсылкаАлкоОтчет", ДокументСсылкаАлкоОтчет);

	РегламентированнаяОтчетностьАЛКО.ВывестиНаПечатьТитульный(СтруктураПараметровСтраницы, СтруктураРеквизитовФормы, ОбъектЭтогоОтчета);

КонецПроцедуры

Процедура ВывестиНаПечатьРазделЛицензии(СтруктураПараметровСтраницы)

	НастройкиСтраницы = Новый Структура;

	НастройкиСтраницы.Вставить("ОтображатьЗаголовки", Ложь);
	НастройкиСтраницы.Вставить("ОтображатьСетку", Ложь);
	НастройкиСтраницы.Вставить("ЧерноБелыйПросмотр", Истина);
	НастройкиСтраницы.Вставить("ЧерноБелаяПечать", Истина);
	НастройкиСтраницы.Вставить("Автомасштаб", Истина);
	НастройкиСтраницы.Вставить("ОриентацияСтраницы", ОриентацияСтраницы.Портрет);
	НастройкиСтраницы.Вставить("ПолеСверху", 10);
	НастройкиСтраницы.Вставить("ПолеСнизу", 5);
	НастройкиСтраницы.Вставить("ПолеСлева", 5);
	НастройкиСтраницы.Вставить("ПолеСправа", 5);

	СтруктураПараметровСтраницы.Вставить("НастройкиСтраницы", НастройкиСтраницы);
	СтруктураПараметровСтраницы.Вставить("СтруктураДанныхТитульный", СтруктураДанныхТитульный);
	СтруктураПараметровСтраницы.Вставить("СтруктураСервисРегистров", СтруктураСервисРегистров);
	СтруктураПараметровСтраницы.Вставить("ТаблицаСтраницыЛицензии", ТаблицаСтраницыЛицензии);
	СтруктураПараметровСтраницы.Вставить("НужноОбработатьСтрок", НужноОбработатьСтрок);
	СтруктураПараметровСтраницы.Вставить("ПроцентБезКонвертацииАрхивации", ПроцентБезКонвертацииАрхивации);
	СтруктураПараметровСтраницы.Вставить("ДокументСсылкаАлкоОтчет", ДокументСсылкаАлкоОтчет);

	РегламентированнаяОтчетностьАЛКО.ВывестиНаПечатьРазделЛицензии(СтруктураПараметровСтраницы, СтруктураРеквизитовФормы, 
																	ОбработаноСтрок, ОбъектЭтогоОтчета, РежимОтладки);

КонецПроцедуры

Процедура ВывестиНаПечатьРазделДекларации(СтруктураПараметровСтраницы)

// В структуре СтруктураПараметровСтраницы уже установлены свойства 
	// ИмяФормыОбъекта, ИмяРаздела, НаименованиеЛиста - в процедуре ПечатьОтчета().

	СтруктураПараметровСтраницы.Вставить("ДокументСсылкаАлкоОтчет", ДокументСсылкаАлкоОтчет);
	СтруктураПараметровСтраницы.Вставить("СтруктураДанныхТитульный", СтруктураДанныхТитульный);
	СтруктураПараметровСтраницы.Вставить("СтруктураСервисРегистров", СтруктураСервисРегистров);
	СтруктураПараметровСтраницы.Вставить("ДополнительныеДанные", ДополнительныеДанные);
	
	СтруктураПараметровСтраницы.Вставить("ТаблицаСтраницыРаздела", ТаблицаСтраницыДекларация);
	ИдГруппы = ?(СтруктураПараметровСтраницы.ИмяРаздела = "Раздел1", 
					СтруктураРеквизитовФормы.мИдГруппы12, СтруктураРеквизитовФормы.мИдГруппы22);
	СтруктураПараметровСтраницы.Вставить("ИдГруппыРаздела", ИдГруппы);

	// Настройки листа.
	НастройкиСтраницы = Новый Структура;
	НастройкиСтраницы.Вставить("ОтображатьЗаголовки", Ложь);
	НастройкиСтраницы.Вставить("ОтображатьСетку", Ложь);
	НастройкиСтраницы.Вставить("ЧерноБелыйПросмотр", Истина);
	НастройкиСтраницы.Вставить("ЧерноБелаяПечать", Истина);
	НастройкиСтраницы.Вставить("Автомасштаб", Истина);
	НастройкиСтраницы.Вставить("ОриентацияСтраницы", ОриентацияСтраницы.Ландшафт);
	НастройкиСтраницы.Вставить("ПолеСверху", 10);
	НастройкиСтраницы.Вставить("ПолеСнизу", 5);
	НастройкиСтраницы.Вставить("ПолеСлева", 5);
	НастройкиСтраницы.Вставить("ПолеСправа", 5);

	СтруктураПараметровСтраницы.Вставить("НастройкиСтраницы", НастройкиСтраницы);

	// Высоты в стандартных строках с установленным свойством "Автовысота" (11,25).
	// Вычисляются методом пробной печати заведомо большого количества строк.
	// Например ВысотаСтраницыВСтроках = 120 и строк в отчете 200, значения короткие чтобы в одну строку 
	// помещались в поле.
	// После печати считается сколько поместилось на странице, с учетом что некоторые строки высотой в 2-3
	// "стандартных" - это и есть правильная ВысотаСтраницыВСтроках. 
	// Лучше ставить с небольшим запасом, в меньшую сторону. Если строк на листе вышло 100, лучше поставить 95.
	// Остальные высоты считаются по макету - сколько стандартных строк по высоте занимают.
	СтруктураВысот = Новый Структура;
	СтруктураВысот.Вставить("ВысотаСтраницыВСтроках", 95);
	СтруктураВысот.Вставить("Высота_НомерСтраницыВСтроках", 1);
	СтруктураВысот.Вставить("Высота_ЗаголовокЛистаВСтроках", 2);
	СтруктураВысот.Вставить("Высота_ПустаяСтрокаВСтроках", 1);
	СтруктураВысот.Вставить("Высота_ЗаголовокТаблицыВСтроках", 5);
	СтруктураВысот.Вставить("Высота_СтрокаОПВСтроках", 2);
	СтруктураВысот.Вставить("Высота_ПодвалВСтроках", 1);

	СтруктураПараметровСтраницы.Вставить("СтруктураВысот", СтруктураВысот);
	
	НастройкиАлгоритмаПечати = Новый Структура;

	/////////////////////////////////////////////////////////////
	// Общие настройки печати по разделу.
	/////////////////////////////////////////////////////////////

	НастройкиАлгоритмаПечати.Вставить("ИмяОбластиПустаяСтрока", "ПустаяСтрока");
	НастройкиАлгоритмаПечати.Вставить("ИмяОбластиПодвал", "Подвал");
	НастройкиАлгоритмаПечати.Вставить("ИмяОбластиОргДиректор", "ОргДиректор");
	НастройкиАлгоритмаПечати.Вставить("ИмяОбластиОргБухгалтер", "ОргБухгалтер");
	НастройкиАлгоритмаПечати.Вставить("ВыводитьСводнуюТаблицуПоОрганизацииВЦелом", Истина);
	НастройкиАлгоритмаПечати.Вставить("ВыводитьВозвратыЗаДекларацией", Ложь);
	// Описание заголовка и шапки таблицы.
	НастройкиАлгоритмаПечати.Вставить("ИмяОбластиНомерСтраницы", "НомерСтраницы");
	НастройкиАлгоритмаПечати.Вставить("ИмяОбластиЗаголовокЛиста", "ЗаголовокЛиста");
	НастройкиАлгоритмаПечати.Вставить("ИмяОбластиЗаголовокТаблицы", "ЗаголовокТаблицы");

	// СписокПереносимыхПолей - список, значения которого - количество стандартных символов, помещающихся в 
	// в одну строчку по длине поля ,при шрифте и величине шрифта, установленных для поля в макете в области
	// "СтрокаТаблицы" или аналогичном.	
	// В представлении элементов списка хранится имена ИмяПоляПереносимойСтроки, т.е поля,
	// в котором слова могут переносится на следующую строку, если не хватает длины, или номера колонок, т.е
	// в представлении можно указывать либо полное имя поля регистра сведений вида ИдГруппы + НомерКолонки,
	// либо просто НомерКолонки.
	// Строка "СтрокаТаблицы" или аналогичная должна иметь настройку АвтовысотаСтроки = Истина.
	//	

	// Действуют и на общую часть по организации и на часть по ОП.
	СписокПереносимыхПолей = Новый СписокЗначений;
	СписокПереносимыхПолей.Добавить(26, "01");
	СписокПереносимыхПолей.Добавить(26, "03");
	СписокПереносимыхПолей.Добавить(26, "06");
	СписокПереносимыхПолей.Добавить(26, "07");
	СписокПереносимыхПолей.Добавить(26, "10");
	СписокПереносимыхПолей.Добавить(26, "12");
	СписокПереносимыхПолей.Добавить(26, "13");

	НастройкиАлгоритмаПечати.Вставить("СписокПереносимыхПолей", СписокПереносимыхПолей);
	
	// СоответствиеСоставныхПолей -  
	// Значения - массив номеров колонок или окончаний имен ресурсов и реквизитов регистров после ИдГруппы,
	// которые нужно выводить. Значение элемента с нулевом индексом - разделительный символ между полями.
	// Ключ - номер колонки в которой нужно выводить несколько полей регистра сведений. 
	
	МассивКолонок = Новый Массив;
	МассивКолонок.Добавить(Символы.ПС);
	МассивКолонок.Добавить("03");
	МассивКолонок.Добавить("03Адрес");
	
	СоответствиеСоставныхПолей = Новый Соответствие;
	СоответствиеСоставныхПолей.Вставить("03", МассивКолонок);
	
	НастройкиАлгоритмаПечати.Вставить("СоответствиеСоставныхПолей", СоответствиеСоставныхПолей);

	// Описание областей макета, выводящих строки общей информации по ОП и организации.
	НастройкиАлгоритмаПечати.Вставить("ИмяОбластиСтрокаОП", "СтрокаОП");
	НастройкиАлгоритмаПечати.Вставить("ИмяОбластиСтрокаОрг", "СтрокаОП");

	// Описание областей макета, выводящих строки общей информации по ОП и организации. 
	НастройкиАлгоритмаПечати.Вставить("ИмяОбластиЗаголовокРеквизитовОП", "ЗаголовокРеквизитов");
	НастройкиАлгоритмаПечати.Вставить("ИмяОбластиЗаголовокРеквизитовОрг", "ЗаголовокРеквизитов");

	// Текст пояснения к общим сведениям по ОП и организации.
	НастройкиАлгоритмаПечати.Вставить("ЗаголовокРеквизитовОП", 
		"Сведения по обособленному подразделению (с указанием ИНН, КПП и адреса)/
		|объекту торговли сельскохозяйственного товаропроизводителя (с указанием ИНН, КПП и адреса)");
	НастройкиАлгоритмаПечати.Вставить("ЗаголовокРеквизитовОрг", "Сведения по организации / сельскохозяйственному товаропроизводителю");

	
	// Имя области в которой выводится общая информация по ОП и Организации.
	НастройкиАлгоритмаПечати.Вставить("ИмяОбластиРеквизитовОП", "ОП");
	НастройкиАлгоритмаПечати.Вставить("ИмяОбластиРеквизитовОрг", "ОП");

	// Информация по колонкам группировки, суммирования и запрету вывода.
	// Используется при выводе строк по организации - выводить "как есть" или сворачивать
	// по определенным полям.
	//
	// Пустая строка колонок группировки и суммирования означает автоопределение 
	// - все что не числа - колонки группировки.
	// - все что числа - колонки суммирования.
	//
	// Значение "Не сворачивать" или "НеСворачивать" в колонках группировки вместо списка колонок
	// предотвращает сворачивание, независимо от значения колонок суммирования.

	НастройкиАлгоритмаПечати.Вставить("КолонкиГруппировокОрг", "");
	НастройкиАлгоритмаПечати.Вставить("КолонкиСуммированияОрг", "");
	НастройкиАлгоритмаПечати.Вставить("КолонкиНеВыводитьОрг", "");

	/////////////////////////////////////////////////////////////
	// Алгоритм печати строк ОП.
	/////////////////////////////////////////////////////////////

	НастройкиАлгоритмаПечатиСтрокОП = Новый Структура;
	НастройкиАлгоритмаПечатиСтрокОП.Вставить("ИдГруппыОбластиСтрокаМакета", "П0000100003");
	НастройкиАлгоритмаПечатиСтрокОП.Вставить("ИмяОбластиНомера", "П000010000300");
	НастройкиАлгоритмаПечатиСтрокОП.Вставить("ИмяОбластиСтрокаТаблицы", "СтрокаТаблицы");

	СписокСтруктурИнформацииИтогов = Новый СписокЗначений;

	СтруктураИнформацииИтога = Новый Структура;
	СтруктураИнформацииИтога.Вставить("ИдГруппыОбластейМакетаИтоги", "П0000100004");
	СтруктураИнформацииИтога.Вставить("ИмяОбластиСтрокаИтогов", "СтрокаИтого");
	СтруктураИнформацииИтога.Вставить("ИмяОбластиРазмещенияПояснения", "ОписаниеИтого");
	СтруктураИнформацииИтога.Вставить("ТекстПояснения", "Итого по коду %#02#%");
	СтруктураИнформацииИтога.Вставить("КолонкиГруппировок", "02");
	СтруктураИнформацииИтога.Вставить("КолонкиСуммирования", "14");

	СписокСтруктурИнформацииИтогов.Добавить(СтруктураИнформацииИтога, "1");

	СтруктураИнформацииИтога2 = Новый Структура;
	СтруктураИнформацииИтога2.Вставить("ИдГруппыОбластейМакетаИтоги", "П0000100004");
	СтруктураИнформацииИтога2.Вставить("ИмяОбластиСтрокаИтогов", "СтрокаИтого");
	СтруктураИнформацииИтога2.Вставить("ИмяОбластиРазмещенияПояснения", "ОписаниеИтого");
	СтруктураИнформацииИтога2.Вставить("ТекстПояснения", "ИТОГО");
	СтруктураИнформацииИтога2.Вставить("КолонкиГруппировок", "");
	СтруктураИнформацииИтога2.Вставить("КолонкиСуммирования", "14");

	СписокСтруктурИнформацииИтогов.Добавить(СтруктураИнформацииИтога2, "2");

	НастройкиАлгоритмаПечатиСтрокОП.Вставить("СписокСтруктурИнформацииИтогов", СписокСтруктурИнформацииИтогов);

	НастройкиАлгоритмаПечати.Вставить("НастройкиАлгоритмаПечатиСтрокОП", НастройкиАлгоритмаПечатиСтрокОП);

	/////////////////////////////////////////////////////////////
	// Алгоритм печати строк Организации.
	/////////////////////////////////////////////////////////////

	НастройкиАлгоритмаПечатиСтрокОрг = Новый Структура;
	НастройкиАлгоритмаПечатиСтрокОрг.Вставить("ИдГруппыОбластиСтрокаМакета", "П0000100003");
	НастройкиАлгоритмаПечатиСтрокОрг.Вставить("ИмяОбластиНомера", "П000010000300");
	НастройкиАлгоритмаПечатиСтрокОрг.Вставить("ИмяОбластиСтрокаТаблицы", "СтрокаТаблицы");

	СписокСтруктурИнформацииИтоговОрг = Новый СписокЗначений;

	СтруктураИнформацииИтога = Новый Структура;
	СтруктураИнформацииИтога.Вставить("ИдГруппыОбластейМакетаИтоги", "П0000100004");
	СтруктураИнформацииИтога.Вставить("ИмяОбластиСтрокаИтогов", "СтрокаИтого");
	СтруктураИнформацииИтога.Вставить("ИмяОбластиРазмещенияПояснения", "ОписаниеИтого");
	СтруктураИнформацииИтога.Вставить("ТекстПояснения", "Итого по коду %#02#%");
	СтруктураИнформацииИтога.Вставить("КолонкиГруппировок", "02");
	СтруктураИнформацииИтога.Вставить("КолонкиСуммирования", "14");

	СписокСтруктурИнформацииИтоговОрг.Добавить(СтруктураИнформацииИтога, "1");

	СтруктураИнформацииИтога2 = Новый Структура;
	СтруктураИнформацииИтога2.Вставить("ИдГруппыОбластейМакетаИтоги", "П0000100004");
	СтруктураИнформацииИтога2.Вставить("ИмяОбластиСтрокаИтогов", "СтрокаИтого");
	СтруктураИнформацииИтога2.Вставить("ИмяОбластиРазмещенияПояснения", "ОписаниеИтого");
	СтруктураИнформацииИтога2.Вставить("ТекстПояснения", "ИТОГО");
	СтруктураИнформацииИтога2.Вставить("КолонкиГруппировок", "");
	СтруктураИнформацииИтога2.Вставить("КолонкиСуммирования", "14");

	СписокСтруктурИнформацииИтоговОрг.Добавить(СтруктураИнформацииИтога2, "2");

	НастройкиАлгоритмаПечатиСтрокОрг.Вставить("СписокСтруктурИнформацииИтогов", СписокСтруктурИнформацииИтоговОрг);

	НастройкиАлгоритмаПечати.Вставить("НастройкиАлгоритмаПечатиСтрокОрг", НастройкиАлгоритмаПечатиСтрокОрг);

	/////////////////////////////////////////////////////////////
	// Конец настроек алгоритма печати
	/////////////////////////////////////////////////////////////

	СтруктураПараметровСтраницы.Вставить("НастройкиАлгоритмаПечати", НастройкиАлгоритмаПечати);

	// Заполнение данных для показа шкалы прогресса.
	СтруктураПараметровСтраницы.Вставить("НужноОбработатьСтрок", НужноОбработатьСтрок);
	СтруктураПараметровСтраницы.Вставить("ПроцентБезКонвертацииАрхивации", ПроцентБезКонвертацииАрхивации);


	РегламентированнаяОтчетностьАЛКО.ВывестиНаПечатьРазделДекларации(СтруктураПараметровСтраницы, СтруктураРеквизитовФормы, 
																		ОбработаноСтрок, ОбъектЭтогоОтчета, РежимОтладки);

КонецПроцедуры
																	
#КонецОбласти

#Область ВнешнийОтчет

// Процедуры и функции этой области используются только в случае
// публикации в виде внешнего отчета.

#Область ПрограммныйИнтерфейсВнешнийОтчет

Функция ВерсияФорматаВыгрузки(Знач НаДату = Неопределено, ВыбраннаяФорма = Неопределено) Экспорт
	
	Если НаДату = Неопределено Тогда
		НаДату = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Возврат Перечисления.ВерсииФорматовВыгрузки.Версия401алко;
		
КонецФункции

Функция ТаблицаФормОтчета() Экспорт
	
	ОписаниеТиповСтрока = Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(0));
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("Дата"));
	ОписаниеТиповДата = Новый ОписаниеТипов(МассивТипов, , Новый КвалификаторыДаты(ЧастиДаты.Дата));
	
	ТаблицаФормОтчета = Новый ТаблицаЗначений;
	ТаблицаФормОтчета.Колонки.Добавить("ФормаОтчета",        ОписаниеТиповСтрока);
	ТаблицаФормОтчета.Колонки.Добавить("ОписаниеОтчета",     ОписаниеТиповСтрока, "Утверждена",  20);
	ТаблицаФормОтчета.Колонки.Добавить("ДатаНачалоДействия", ОписаниеТиповДата,   "Действует с", 5);
	ТаблицаФормОтчета.Колонки.Добавить("ДатаКонецДействия",  ОписаниеТиповДата,   "         по", 5);
	ТаблицаФормОтчета.Колонки.Добавить("РедакцияФормы",      ОписаниеТиповСтрока, "Редакция формы", 20);
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета        = "ФормаОтчета2021Кв1";
	НоваяФорма.ОписаниеОтчета     = "Приложение № 3 к Порядку представления в форме электронного документа "
									+ "деклараций об объеме производства, оборота и (или) использования "
									+ "этилового спирта, алкогольной и спиртосодержащей продукции, "
									+ "об использовании производственных мощностей производителями пива и "
									+ "пивных напитков сидра, пуаре, медовухи "
									+ "(Утверждено приказом ФСРАР РФ от 17.12.2020 г. № 396).";
	НоваяФорма.РедакцияФормы	  = "от 17.12.2020 № 396.";
	НоваяФорма.ДатаНачалоДействия = '20210101';
	НоваяФорма.ДатаКонецДействия  = РегламентированнаяОтчетностьКлиентСервер.ПустоеЗначениеТипа(Тип("Дата"));
	
	
	Возврат ТаблицаФормОтчета;
	
КонецФункции

Функция ДанныеРеглОтчета(ЭкземплярРеглОтчета) Экспорт
	
	Возврат Неопределено;
	
КонецФункции

Функция ДеревоФормИФорматов() Экспорт
	
	ФормыИФорматы = Новый ДеревоЗначений;
	ФормыИФорматы.Колонки.Добавить("Код");
	ФормыИФорматы.Колонки.Добавить("ДатаПриказа");
	ФормыИФорматы.Колонки.Добавить("НомерПриказа");
	ФормыИФорматы.Колонки.Добавить("ДатаНачалаДействия");
	ФормыИФорматы.Колонки.Добавить("ДатаОкончанияДействия");
	ФормыИФорматы.Колонки.Добавить("ИмяОбъекта");
	ФормыИФорматы.Колонки.Добавить("Описание");
	
	Форма20210101 = ОпределитьФормуВДеревеФормИФорматов(ФормыИФорматы, "1152039", '2020-12-17', "396", "ФормаОтчета2021Кв1");
	ОпределитьФорматВДеревеФормИФорматов(Форма20210101, "4.40", , , '2021-01-01');
	
	
	Возврат ФормыИФорматы;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункцииВнешнийОтчет

Функция ОпределитьФормуВДеревеФормИФорматов(ДеревоФормИФорматов, Код,
		ДатаПриказа = '00010101', НомерПриказа = "", ИмяОбъекта = "",
		ДатаНачалаДействия = '00010101', ДатаОкончанияДействия = '00010101',
		Описание = "")

	НовСтр = ДеревоФормИФорматов.Строки.Добавить();
	НовСтр.Код = СокрЛП(Код);
	НовСтр.ДатаПриказа = ДатаПриказа;
	НовСтр.НомерПриказа = СокрЛП(НомерПриказа);
	НовСтр.ДатаНачалаДействия = ДатаНачалаДействия;
	НовСтр.ДатаОкончанияДействия = ДатаОкончанияДействия;
	НовСтр.ИмяОбъекта = СокрЛП(ИмяОбъекта);
	НовСтр.Описание = СокрЛП(Описание);
	Возврат НовСтр;

КонецФункции

Функция ОпределитьФорматВДеревеФормИФорматов(Форма, Версия,
		ДатаПриказа = '00010101', НомерПриказа = "",
		ДатаНачалаДействия = Неопределено, ДатаОкончанияДействия = Неопределено,
		ИмяОбъекта = "", Описание = "")

	НовСтр = Форма.Строки.Добавить();
	НовСтр.Код = СокрЛП(Версия);
	НовСтр.ДатаПриказа = ДатаПриказа;
	НовСтр.НомерПриказа = СокрЛП(НомерПриказа);
	НовСтр.ДатаНачалаДействия = ?(ДатаНачалаДействия = Неопределено, Форма.ДатаНачалаДействия, ДатаНачалаДействия);
	НовСтр.ДатаОкончанияДействия = ?(ДатаОкончанияДействия = Неопределено, Форма.ДатаОкончанияДействия, ДатаОкончанияДействия);
	НовСтр.ИмяОбъекта = СокрЛП(ИмяОбъекта);
	НовСтр.Описание = СокрЛП(Описание);
	Возврат НовСтр;

КонецФункции
#КонецОбласти

мВерсияОтчета = "БПКОРП 3.0.42.54.4";

#КонецОбласти

#КонецОбласти

#КонецЕсли
