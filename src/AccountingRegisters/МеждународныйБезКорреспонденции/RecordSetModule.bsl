
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполняет субконто проводки из переданных данных проводки
//
// Параметры:
//  Проводка - РегистрБухгалтерииЗапись.МеждународныйБезКорреспонденции - проводка у которой требуется заполнить субконто
//  Данные   - КлючИЗначение Из Структура - структура содержащая данные субконто:
//             * Ключ - Строка - Поля проводки субконто - "ВидСубконто1,Субконто1...."
//             * Значение - Произвольный - Значение полей субконто
//
Процедура УстановитьСубконто(Проводка, Данные) Экспорт
	
	ИмяСчет = "Счет";
	ИмяСубконто = "Субконто";
	ИмяВидаСубконто = "ВидСубконто";

	Счет = Проводка[ИмяСчет];
	СвойстваСчета = МеждународныйУчетСерверПовтИсп.СвойстваСчета(Счет);
	Для Номер = 1 По СвойстваСчета.КоличествоСубконто Цикл
		ВидСубконто = Данные[ИмяВидаСубконто + Номер];
		Проводка.Субконто[ВидСубконто] = Данные[ИмяСубконто + Номер];
	КонецЦикла;
	
КонецПроцедуры

// Добавляет переданную проводку в текущий набор записей
//
// Параметры:
//  Проводка - РегистрБухгалтерииЗапись.МеждународныйБезКорреспонденции, Структура - добавляемая проводка
//  ТекстСодержания - Строка - текст содержания проводки.
//
Процедура ДобавитьПроводку(Проводка, ТекстСодержания = "") Экспорт
	
	НоваяПроводка = Добавить();
	ЗаполнитьЗначенияСвойств(НоваяПроводка, Проводка);
	Если НЕ ПустаяСтрока(ТекстСодержания) Тогда
		НоваяПроводка.Содержание = ТекстСодержания;
	КонецЕсли;
	Для Каждого Субконто Из Проводка.Субконто Цикл
		НоваяПроводка.Субконто[Субконто.Ключ] = Субконто.Значение;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ПустыеСуммыПроводки = Ложь;
	Для Каждого Запись Из ЭтотОбъект Цикл
		Если Запись.Сумма = 0 И Запись.СуммаПредставления = 0 Тогда
			ПустыеСуммыПроводки = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ ПустыеСуммыПроводки Тогда
		МассивНепроверяемыхРеквизитов = Новый Массив;
		МассивНепроверяемыхРеквизитов.Добавить("Сумма");
		МассивНепроверяемыхРеквизитов.Добавить("СуммаПредставления");
		ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты,МассивНепроверяемыхРеквизитов);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Регистратор = Отбор.Регистратор.Значение;
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	ФормироватьКорректировочныеПроводки = ЕстьНеобходимостьФормированияКорректировочныхПроводок(Регистратор, МенеджерВременныхТаблиц);
	Текст = НСтр("ru = 'Установлена дата запрета изменения проводок по международному учету.
				|Отмена проведения документа запрещена.';
				|en = 'A period-end closing date for financial accounting entries is set.
				|You cannot unpost the document.'");
	
	Если ТипЗнч(Регистратор) = Тип("ДокументСсылка.ОперацияМеждународный") Тогда
		ВводитьПериодПоСтрокам = ДополнительныеСвойства.Свойство("ВводитьПериодПоСтрокам");
		Если ФормироватьКорректировочныеПроводки И НЕ ВводитьПериодПоСтрокам Тогда
			Текст = Текст + Символы.ПС + Символы.ПС
				+ НСтр("ru = 'Корректировать проводки документа ""Операция (международный учет)"" в закрытом периоде по международному учету
				|допускается только с включенным флагом ""Вводить дату в строках"" на закладке ""Основное"".';
				|en = 'You can make adjustments to the ""Transaction (financial accounting)"" document entries for the closed period of international accounting
				|only if the ""Enter date in lines"" check box on the Main tab is selected.'");
			ВызватьИсключение Текст;
		КонецЕсли;
	КонецЕсли;
	
	Если ФормироватьКорректировочныеПроводки Тогда
		
		ОтменаПроведения = ДополнительныеСвойства.Свойство("СвойстваДокумента")
			И ДополнительныеСвойства.СвойстваДокумента.РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения;
		
		ОчиститьНаборЗаписей = Ложь;
		
		Если Количество() = 0 И НЕ ОтменаПроведения Тогда
			Прочитать();
			ОчиститьНаборЗаписей = Истина;
		КонецЕсли;
		
		НетАктивныхЗаписей = Количество() = 0 ИЛИ НЕ ЭтотОбъект[0].Активность;
		
		Если ОтменаПроведения ИЛИ НетАктивныхЗаписей Тогда
			ЗаписыватьПустойНабор = Ложь;
			Если НЕ ДополнительныеСвойства.Свойство("ЗаписыватьПустойНабор", ЗаписыватьПустойНабор) ИЛИ НЕ ЗаписыватьПустойНабор Тогда
				ВызватьИсключение Текст;
			КонецЕсли;
		КонецЕсли;
		
		Если ОчиститьНаборЗаписей Тогда
			Очистить();
		КонецЕсли;
		
		СформироватьКорректировочныеПроводки(Регистратор, МенеджерВременныхТаблиц);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьКорректировочныеПроводки(Регистратор, МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос(ТекстЗапросаКорректировочныхПроводок());
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Документ", Регистратор);
	Запрос.УстановитьПараметр("НаборДвижений", Выгрузить());
	
	Очистить();
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяПроводка = Добавить();
		ЗаполнитьЗначенияСвойств(НоваяПроводка, Выборка);
		УстановитьСубконто(НоваяПроводка, Выборка);
	КонецЦикла;
	
КонецПроцедуры

Функция ТекстЗапросаКорректировочныхПроводок()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	НаборДвижений.ВидДвижения,
	|	НаборДвижений.Период,
	|	НаборДвижений.Регистратор,
	|	НаборДвижений.ПланСчетов,
	|	НаборДвижений.Организация,
	|	НаборДвижений.Счет,
	|	НаборДвижений.Подразделение,
	|	НаборДвижений.НаправлениеДеятельности,
	|	НаборДвижений.ВидСубконто1,
	|	НаборДвижений.Субконто1,
	|	НаборДвижений.ВидСубконто2,
	|	НаборДвижений.Субконто2,
	|	НаборДвижений.ВидСубконто3,
	|	НаборДвижений.Субконто3,
	|	НаборДвижений.Валюта,
	|	НаборДвижений.ВалютнаяСумма,
	|	НаборДвижений.Количество,
	|	НаборДвижений.Сумма,
	|	НаборДвижений.СуммаПредставления,
	|	НаборДвижений.ШаблонПроводки
	|ПОМЕСТИТЬ НаборДвижений
	|ИЗ
	|	&НаборДвижений КАК НаборДвижений
	|ИНДЕКСИРОВАТЬ ПО
	|	Счет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НаборДвижений.Счет КАК Счет
	|ПОМЕСТИТЬ СчетаНабора
	|ИЗ
	|	НаборДвижений КАК НаборДвижений
	|ИНДЕКСИРОВАТЬ ПО
	|	Счет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыСубконто.Ссылка КАК Счет,
	|	ВидыСубконто.НомерСтроки КАК НомерСубконто,
	|	ВидыСубконто.ВидСубконто КАК ВидСубконто
	|ПОМЕСТИТЬ СубконтоСчетов
	|ИЗ
	|	ПланСчетов.Международный.ВидыСубконто КАК ВидыСубконто
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ СчетаНабора КАК СчетаНабора
	|		ПО ВидыСубконто.Ссылка = СчетаНабора.Счет
	|ИНДЕКСИРОВАТЬ ПО
	|	Счет,
	|	НомерСубконто
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НаборДвижений.ВидДвижения,
	|	НаборДвижений.Период,
	|	НаборДвижений.Регистратор,
	|	НаборДвижений.ПланСчетов,
	|	НаборДвижений.Организация,
	|	НаборДвижений.Счет,
	|	НаборДвижений.Подразделение,
	|	НаборДвижений.НаправлениеДеятельности,
	|	ЕСТЬNULL(СубконтоСчетов1.ВидСубконто, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)) КАК ВидСубконто1,
	|	ВЫБОР
	|		КОГДА СубконтоСчетов1.ВидСубконто = НаборДвижений.ВидСубконто1
	|			ТОГДА НаборДвижений.Субконто1
	|		КОГДА СубконтоСчетов1.ВидСубконто = НаборДвижений.ВидСубконто2
	|			ТОГДА НаборДвижений.Субконто2
	|		КОГДА СубконтоСчетов1.ВидСубконто = НаборДвижений.ВидСубконто3
	|			ТОГДА НаборДвижений.Субконто3
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Субконто1,
	|	ЕСТЬNULL(СубконтоСчетов2.ВидСубконто, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)) КАК ВидСубконто2,
	|	ВЫБОР
	|		КОГДА СубконтоСчетов2.ВидСубконто = НаборДвижений.ВидСубконто1
	|			ТОГДА НаборДвижений.Субконто1
	|		КОГДА СубконтоСчетов2.ВидСубконто = НаборДвижений.ВидСубконто2
	|			ТОГДА НаборДвижений.Субконто2
	|		КОГДА СубконтоСчетов2.ВидСубконто = НаборДвижений.ВидСубконто3
	|			ТОГДА НаборДвижений.Субконто3
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Субконто2,
	|	ЕСТЬNULL(СубконтоСчетов3.ВидСубконто, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)) КАК ВидСубконто3,
	|	ВЫБОР
	|		КОГДА СубконтоСчетов3.ВидСубконто = НаборДвижений.ВидСубконто1
	|			ТОГДА НаборДвижений.Субконто1
	|		КОГДА СубконтоСчетов3.ВидСубконто = НаборДвижений.ВидСубконто2
	|			ТОГДА НаборДвижений.Субконто2
	|		КОГДА СубконтоСчетов3.ВидСубконто = НаборДвижений.ВидСубконто3
	|			ТОГДА НаборДвижений.Субконто3
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Субконто3,
	|	НаборДвижений.Валюта,
	|	НаборДвижений.ВалютнаяСумма,
	|	НаборДвижений.Количество,
	|	НаборДвижений.Сумма,
	|	НаборДвижений.СуммаПредставления,
	|	НаборДвижений.ШаблонПроводки,
	|	ВЫБОР
	|		КОГДА НаборДвижений.Период > ЕстьNULL(ДатыЗапретаПоОрганизациям.ДатаЗапрета, ДатаВремя(1,1,1)) 
	|			ТОГДА НаборДвижений.Период
	|		ИНАЧЕ ДОБАВИТЬКДАТЕ(ДатыЗапретаПоОрганизациям.ДатаЗапрета, СЕКУНДА, 1)
	|	КОНЕЦ КАК ПериодКорректировки
	|ПОМЕСТИТЬ НовыеПроводки
	|ИЗ
	|	НаборДвижений КАК НаборДвижений
	|	ЛЕВОЕ СОЕДИНЕНИЕ СубконтоСчетов КАК СубконтоСчетов1
	|		ПО НаборДвижений.Счет = СубконтоСчетов1.Счет
	|		 И СубконтоСчетов1.НомерСубконто = 1
	|	ЛЕВОЕ СОЕДИНЕНИЕ СубконтоСчетов КАК СубконтоСчетов2
	|		ПО НаборДвижений.Счет = СубконтоСчетов2.Счет
	|		 И СубконтоСчетов2.НомерСубконто = 2
	|	ЛЕВОЕ СОЕДИНЕНИЕ СубконтоСчетов КАК СубконтоСчетов3
	|		ПО НаборДвижений.Счет = СубконтоСчетов3.Счет
	|		 И СубконтоСчетов3.НомерСубконто = 3
	|	ЛЕВОЕ СОЕДИНЕНИЕ ДатыЗапретаПоОрганизациям КАК ДатыЗапретаПоОрганизациям
	|		ПО НаборДвижений.Организация = ДатыЗапретаПоОрганизациям.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсходныеПроводки.ВидДвижения,
	|	ИсходныеПроводки.Период,
	|	ИсходныеПроводки.Регистратор,
	|	ИсходныеПроводки.ПланСчетов,
	|	ИсходныеПроводки.Организация,
	|	ИсходныеПроводки.Счет,
	|	ЕСТЬNULL(ИсходныеПроводки.Подразделение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)) КАК Подразделение,
	|	ЕСТЬNULL(ИсходныеПроводки.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности,
	|	ЕСТЬNULL(ИсходныеПроводки.ВидСубконто1, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)) КАК ВидСубконто1,
	|	ЕСТЬNULL(ИсходныеПроводки.Субконто1, НЕОПРЕДЕЛЕНО) КАК Субконто1,
	|	ЕСТЬNULL(ИсходныеПроводки.ВидСубконто2, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)) КАК ВидСубконто2,
	|	ЕСТЬNULL(ИсходныеПроводки.Субконто2, НЕОПРЕДЕЛЕНО) КАК Субконто2,
	|	ЕСТЬNULL(ИсходныеПроводки.ВидСубконто3, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)) КАК ВидСубконто3,
	|	ЕСТЬNULL(ИсходныеПроводки.Субконто3, НЕОПРЕДЕЛЕНО) КАК Субконто3,
	|	ЕСТЬNULL(ИсходныеПроводки.Валюта, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)) КАК Валюта,
	|	ЕСТЬNULL(ИсходныеПроводки.ВалютнаяСумма, 0) КАК ВалютнаяСумма,
	|	ЕСТЬNULL(ИсходныеПроводки.Количество, 0) КАК Количество,
	|	ИсходныеПроводки.Сумма,
	|	ИсходныеПроводки.СуммаПредставления,
	|	ИсходныеПроводки.ШаблонПроводки,
	|	ИсходныеПроводки.ТипПроводки,
	|	ИсходныеПроводки.Содержание,
	|	ДОБАВИТЬКДАТЕ(ДатыЗапретаПоОрганизациям.ДатаЗапрета, СЕКУНДА, 1) КАК ПериодКорректировки
	|ПОМЕСТИТЬ ИсходныеПроводки
	|ИЗ
	|	РегистрБухгалтерии.МеждународныйБезКорреспонденции.ДвиженияССубконто(, , Регистратор В (&Документ), , ) КАК ИсходныеПроводки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДатыЗапретаПоОрганизациям КАК ДатыЗапретаПоОрганизациям
	|		ПО ИсходныеПроводки.Организация = ДатыЗапретаПоОрганизациям.Организация
	|			И ИсходныеПроводки.Период <= ДатыЗапретаПоОрганизациям.ДатаЗапрета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.ВидДвижения,
	|	ВложенныйЗапрос.Период,
	|	ВложенныйЗапрос.Регистратор,
	|	ВложенныйЗапрос.ПланСчетов,
	|	ВложенныйЗапрос.Организация,
	|	ВложенныйЗапрос.Счет,
	|	ВложенныйЗапрос.Подразделение,
	|	ВложенныйЗапрос.НаправлениеДеятельности,
	|	ВложенныйЗапрос.ВидСубконто1,
	|	ВложенныйЗапрос.Субконто1,
	|	ВложенныйЗапрос.ВидСубконто2,
	|	ВложенныйЗапрос.Субконто2,
	|	ВложенныйЗапрос.ВидСубконто3,
	|	ВложенныйЗапрос.Субконто3,
	|	ВложенныйЗапрос.Валюта,
	|	СУММА(ВложенныйЗапрос.ВалютнаяСумма) КАК ВалютнаяСумма,
	|	СУММА(ВложенныйЗапрос.Количество) КАК Количество,
	|	СУММА(ВложенныйЗапрос.Сумма) КАК Сумма,
	|	СУММА(ВложенныйЗапрос.СуммаПредставления) КАК СуммаПредставления,
	|	ВложенныйЗапрос.ШаблонПроводки КАК ШаблонПроводки,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПроводокМеждународныйУчет.Корректировка) КАК ТипПроводки
	|ПОМЕСТИТЬ КорректировочныеПроводки
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПроводкиСторно.ВидДвижения КАК ВидДвижения,
	|		ПроводкиСторно.ПериодКорректировки КАК Период,
	|		ПроводкиСторно.Регистратор КАК Регистратор,
	|		ПроводкиСторно.ПланСчетов КАК ПланСчетов,
	|		ПроводкиСторно.Организация КАК Организация,
	|		ПроводкиСторно.Счет КАК Счет,
	|		ПроводкиСторно.Подразделение КАК Подразделение,
	|		ПроводкиСторно.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ПроводкиСторно.ВидСубконто1 КАК ВидСубконто1,
	|		ПроводкиСторно.Субконто1 КАК Субконто1,
	|		ПроводкиСторно.ВидСубконто2 КАК ВидСубконто2,
	|		ПроводкиСторно.Субконто2 КАК Субконто2,
	|		ПроводкиСторно.ВидСубконто3 КАК ВидСубконто3,
	|		ПроводкиСторно.Субконто3 КАК Субконто3,
	|		ПроводкиСторно.Валюта КАК Валюта,
	|		-ПроводкиСторно.ВалютнаяСумма КАК ВалютнаяСумма,
	|		-ПроводкиСторно.Количество КАК Количество,
	|		-ПроводкиСторно.Сумма КАК Сумма,
	|		-ПроводкиСторно.СуммаПредставления КАК СуммаПредставления,
	|		ПроводкиСторно.ШаблонПроводки КАК ШаблонПроводки
	|	ИЗ
	|		ИсходныеПроводки КАК ПроводкиСторно
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НовыеПроводки.ВидДвижения,
	|		НовыеПроводки.ПериодКорректировки,
	|		НовыеПроводки.Регистратор,
	|		НовыеПроводки.ПланСчетов,
	|		НовыеПроводки.Организация,
	|		НовыеПроводки.Счет,
	|		НовыеПроводки.Подразделение,
	|		НовыеПроводки.НаправлениеДеятельности,
	|		НовыеПроводки.ВидСубконто1,
	|		НовыеПроводки.Субконто1,
	|		НовыеПроводки.ВидСубконто2,
	|		НовыеПроводки.Субконто2,
	|		НовыеПроводки.ВидСубконто3,
	|		НовыеПроводки.Субконто3,
	|		НовыеПроводки.Валюта,
	|		НовыеПроводки.ВалютнаяСумма,
	|		НовыеПроводки.Количество,
	|		НовыеПроводки.Сумма,
	|		НовыеПроводки.СуммаПредставления,
	|		НовыеПроводки.ШаблонПроводки
	|	ИЗ
	|		НовыеПроводки КАК НовыеПроводки) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.ВидДвижения,
	|	ВложенныйЗапрос.Период,
	|	ВложенныйЗапрос.Регистратор,
	|	ВложенныйЗапрос.ПланСчетов,
	|	ВложенныйЗапрос.Организация,
	|	ВложенныйЗапрос.Счет,
	|	ВложенныйЗапрос.Подразделение,
	|	ВложенныйЗапрос.НаправлениеДеятельности,
	|	ВложенныйЗапрос.ВидСубконто1,
	|	ВложенныйЗапрос.Субконто1,
	|	ВложенныйЗапрос.ВидСубконто2,
	|	ВложенныйЗапрос.Субконто2,
	|	ВложенныйЗапрос.ВидСубконто3,
	|	ВложенныйЗапрос.Субконто3,
	|	ВложенныйЗапрос.Валюта,
	|	ВложенныйЗапрос.ШаблонПроводки
	|	
	|ИМЕЮЩИЕ
	|	СУММА(ВложенныйЗапрос.Сумма) <> 0
	|	ИЛИ СУММА(ВложенныйЗапрос.Количество) <> 0
	|	ИЛИ СУММА(ВложенныйЗапрос.ВалютнаяСумма) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсходныеПроводки.ВидДвижения,
	|	ИсходныеПроводки.Период,
	|	ИсходныеПроводки.Регистратор,
	|	ИсходныеПроводки.ПланСчетов,
	|	ИсходныеПроводки.Организация,
	|	ИсходныеПроводки.Счет,
	|	ИсходныеПроводки.Подразделение,
	|	ИсходныеПроводки.НаправлениеДеятельности,
	|	ИсходныеПроводки.ВидСубконто1,
	|	ИсходныеПроводки.Субконто1,
	|	ИсходныеПроводки.ВидСубконто2,
	|	ИсходныеПроводки.Субконто2,
	|	ИсходныеПроводки.ВидСубконто3,
	|	ИсходныеПроводки.Субконто3,
	|	ИсходныеПроводки.Валюта,
	|	ИсходныеПроводки.ВалютнаяСумма,
	|	ИсходныеПроводки.Количество,
	|	ИсходныеПроводки.Сумма,
	|	ИсходныеПроводки.СуммаПредставления,
	|	ИсходныеПроводки.ШаблонПроводки,
	|	ИсходныеПроводки.ТипПроводки,
	|	ИсходныеПроводки.Содержание
	|ИЗ
	|	ИсходныеПроводки КАК ИсходныеПроводки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировочныеПроводки.ВидДвижения,
	|	КорректировочныеПроводки.Период,
	|	КорректировочныеПроводки.Регистратор,
	|	КорректировочныеПроводки.ПланСчетов,
	|	КорректировочныеПроводки.Организация,
	|	КорректировочныеПроводки.Счет,
	|	КорректировочныеПроводки.Подразделение,
	|	КорректировочныеПроводки.НаправлениеДеятельности,
	|	КорректировочныеПроводки.ВидСубконто1,
	|	КорректировочныеПроводки.Субконто1,
	|	КорректировочныеПроводки.ВидСубконто2,
	|	КорректировочныеПроводки.Субконто2,
	|	КорректировочныеПроводки.ВидСубконто3,
	|	КорректировочныеПроводки.Субконто3,
	|	КорректировочныеПроводки.Валюта,
	|	КорректировочныеПроводки.ВалютнаяСумма,
	|	КорректировочныеПроводки.Количество,
	|	КорректировочныеПроводки.Сумма,
	|	КорректировочныеПроводки.СуммаПредставления,
	|	КорректировочныеПроводки.ШаблонПроводки,
	|	КорректировочныеПроводки.ТипПроводки,
	|	""""
	|ИЗ
	|	КорректировочныеПроводки КАК КорректировочныеПроводки
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПроверкиДатыЗапрета()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Организации.Ссылка КАК Организация,
	|	ВЫБОР
	|		КОГДА НЕ ДатыЗапретаПоОрганизации.ДатаЗапрета ЕСТЬ NULL 
	|			ТОГДА КОНЕЦПЕРИОДА(ДатыЗапретаПоОрганизации.ДатаЗапрета, ДЕНЬ)
	|		КОГДА НЕ ДатыЗапретаОбщая.ДатаЗапрета ЕСТЬ NULL 
	|			ТОГДА КОНЕЦПЕРИОДА(ДатыЗапретаОбщая.ДатаЗапрета, ДЕНЬ)
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|	КОНЕЦ КАК ДатаЗапрета
	|ПОМЕСТИТЬ ДатыЗапретаПоОрганизациям
	|ИЗ
	|	Справочник.Организации КАК Организации
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДатыЗапретаФормированияПроводокМеждународныйУчет КАК ДатыЗапретаПоОрганизации
	|		ПО Организации.Ссылка = ДатыЗапретаПоОрганизации.Организация
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДатыЗапретаФормированияПроводокМеждународныйУчет КАК ДатыЗапретаОбщая
	|		ПО (ДатыЗапретаОбщая.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
	|ГДЕ
	|	ВЫБОР
	|		КОГДА НЕ ДатыЗапретаПоОрганизации.ДатаЗапрета ЕСТЬ NULL 
	|			ТОГДА КОНЕЦПЕРИОДА(ДатыЗапретаПоОрганизации.ДатаЗапрета, ДЕНЬ)
	|		КОГДА НЕ ДатыЗапретаОбщая.ДатаЗапрета ЕСТЬ NULL 
	|			ТОГДА КОНЕЦПЕРИОДА(ДатыЗапретаОбщая.ДатаЗапрета, ДЕНЬ)
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|	КОНЕЦ <> ДАТАВРЕМЯ(1, 1, 1)
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ДатаЗапрета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПериодыНабора.Период КАК Период,
	|	ПериодыНабора.Организация КАК Организация
	|ПОМЕСТИТЬ ПериодыНабора
	|ИЗ
	|	&ПериодыНабора КАК ПериодыНабора
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА
	|ИЗ
	|	РегистрБухгалтерии.МеждународныйБезКорреспонденции КАК ИсходныеПроводки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДатыЗапретаПоОрганизациям КАК ДатыЗапретаПоОрганизациям
	|		ПО ИсходныеПроводки.Организация = ДатыЗапретаПоОрганизациям.Организация
	|		 И ИсходныеПроводки.Период <= ДатыЗапретаПоОрганизациям.ДатаЗапрета
	|ГДЕ
	|	ИсходныеПроводки.Регистратор = &Документ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА
	|ИЗ
	|	ПериодыНабора КАК ПериодыНабора
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДатыЗапретаПоОрганизациям КАК ДатыЗапретаПоОрганизациям
	|		ПО ПериодыНабора.Организация = ДатыЗапретаПоОрганизациям.Организация
	|		 И ПериодыНабора.Период <= ДатыЗапретаПоОрганизациям.ДатаЗапрета
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ЕстьНеобходимостьФормированияКорректировочныхПроводок(Регистратор, МенеджерВременныхТаблиц)
	
	ПериодыНабора = ЭтотОбъект.Выгрузить(, "Период,Организация");
	ПериодыНабора.Свернуть("Период,Организация");
	
	Запрос = Новый Запрос(ТекстЗапросаПроверкиДатыЗапрета());
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Документ", Регистратор);
	Запрос.УстановитьПараметр("ПериодыНабора", ПериодыНабора);
	Результат = Запрос.Выполнить();
	
	Возврат Не Результат.Пустой();
	
КонецФункции

#КонецОбласти

#КонецЕсли