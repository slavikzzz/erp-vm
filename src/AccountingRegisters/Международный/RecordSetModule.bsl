
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполняет субконто проводки из переданных данных проводки
//
// Параметры:
//  Проводка - РегистрБухгалтерииЗаписьИмяРегистраБухгалтерии - проводка у которой требуется заполнить субконто
//  Данные   - Структура - структура содержащая данные субконто
//             ключ структуры указывает имя поля проводки - "ВидСубконтоДт1,СубконтоДт1...."
//             значение структуры соответствующее значение поля проводки
//  ВидДвижения - Строка- "Дт" или "Кт".
//
Процедура УстановитьСубконто(Проводка, Данные, ВидДвижения) Экспорт
	
	Если ВидДвижения = "Дт" Тогда
		ИмяСчет = "СчетДт";
		ИмяСубконто = "СубконтоДт";
		ИмяВидаСубконто = "ВидСубконтоДт";
	Иначе
		ИмяСчет = "СчетКт";
		ИмяСубконто = "СубконтоКт";
		ИмяВидаСубконто = "ВидСубконтоКт";
	КонецЕсли;
	
	Счет = Проводка[ИмяСчет];
	СвойстваСчета = МеждународныйУчетСерверПовтИсп.СвойстваСчета(Счет);
	Для Номер = 1 По СвойстваСчета.КоличествоСубконто Цикл
		ВидСубконто = Данные[ИмяВидаСубконто + Номер];
		Если ВидДвижения = "Дт" Тогда
			Проводка.СубконтоДт[ВидСубконто] = Данные[ИмяСубконто + Номер];
		Иначе
			Проводка.СубконтоКт[ВидСубконто] = Данные[ИмяСубконто + Номер];
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Добавляет переданную проводку в текущий набор записей
//
// Параметры:
//  Проводка - РегистрБухгалтерииЗапись.Международный, Структура - добавляемая проводка
//  ТекстСодержания - Строка - текст содержания проводки.
//
Процедура ДобавитьПроводоку(Проводка, ТекстСодержания = "") Экспорт
	
	НоваяПроводка = ЭтотОбъект.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяПроводка, Проводка);
	Если НЕ ПустаяСтрока(ТекстСодержания) Тогда
		НоваяПроводка.Содержание = ТекстСодержания;
	КонецЕсли;
	Для Каждого Субконто Из Проводка.СубконтоДт Цикл
		НоваяПроводка.СубконтоДт[Субконто.Ключ] = Субконто.Значение;
	КонецЦикла;
	Для Каждого Субконто Из Проводка.СубконтоКт Цикл
		НоваяПроводка.СубконтоКт[Субконто.Ключ] = Субконто.Значение;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ПустыеСуммыПроводки = Ложь;
	Для Каждого Запись Из ЭтотОбъект Цикл
		Если Запись.Сумма = 0 И Запись.СуммаПредставления = 0 Тогда
			ПустыеСуммыПроводки = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ ПустыеСуммыПроводки Тогда
		МассивНепроверяемыхРеквизитов = Новый Массив;
		МассивНепроверяемыхРеквизитов.Добавить("Сумма");
		МассивНепроверяемыхРеквизитов.Добавить("СуммаПредставления");
		ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты,МассивНепроверяемыхРеквизитов);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Регистратор = ЭтотОбъект.Отбор.Регистратор.Значение;
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	ФормироватьКорректировочныеПроводки = ЕстьНеобходимостьФормированияКорректировочныхПроводок(Регистратор, МенеджерВременныхТаблиц);
	Текст = НСтр("ru = 'Установлена дата запрета изменения проводок по международному учету.
				|Отмена проведения документа запрещена.';
				|en = 'A period-end closing date for financial accounting entries is set.
				|You cannot unpost the document.'");
	
	Если ТипЗнч(Регистратор) = Тип("ДокументСсылка.ОперацияМеждународный") Тогда
		ВводитьПериодПоСтрокам = ДополнительныеСвойства.Свойство("ВводитьПериодПоСтрокам");
		Если ФормироватьКорректировочныеПроводки И НЕ ВводитьПериодПоСтрокам Тогда
			Текст = Текст + Символы.ПС + Символы.ПС
				+ НСтр("ru = 'Корректировать проводки документа ""Операция (международный учет)"" в закрытом периоде по международному учету
				|допускается только с включенным флагом ""Вводить дату в строках"" на закладке ""Основное"".';
				|en = 'You can make adjustments to the ""Transaction (financial accounting)"" document entries for the closed period of international accounting
				|only if the ""Enter date in lines"" check box on the Main tab is selected.'");
			ВызватьИсключение Текст;
		КонецЕсли;
	КонецЕсли;
	
	Если ФормироватьКорректировочныеПроводки Тогда
		
		ОтменаПроведения = ДополнительныеСвойства.Свойство("СвойстваДокумента")
			И ДополнительныеСвойства.СвойстваДокумента.РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения;
		
		ОчиститьНаборЗаписей = Ложь;
		
		Если Количество() = 0 И НЕ ОтменаПроведения Тогда
			Прочитать();
			ОчиститьНаборЗаписей = Истина;
		КонецЕсли;
		
		НетАктивныхЗаписей = Количество() = 0 ИЛИ НЕ ЭтотОбъект[0].Активность;
		
		Если ОтменаПроведения ИЛИ НетАктивныхЗаписей Тогда
			ЗаписыватьПустойНабор = Ложь;
			Если НЕ ДополнительныеСвойства.Свойство("ЗаписыватьПустойНабор", ЗаписыватьПустойНабор) ИЛИ НЕ ЗаписыватьПустойНабор Тогда
				ВызватьИсключение Текст;
			КонецЕсли;
		КонецЕсли;
		
		Если ОчиститьНаборЗаписей Тогда
			Очистить();
		КонецЕсли;
		
		СформироватьКорректировочныеПроводки(Регистратор, МенеджерВременныхТаблиц);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьКорректировочныеПроводки(Регистратор, МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос(ТекстЗапросаКорректировочныхПроводок());
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Документ", Регистратор);
	Запрос.УстановитьПараметр("НаборДвижений", Выгрузить());
	
	Очистить();
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяПроводка = Добавить();
		ЗаполнитьЗначенияСвойств(НоваяПроводка, Выборка);
		УстановитьСубконто(НоваяПроводка, Выборка, "Дт");
		УстановитьСубконто(НоваяПроводка, Выборка, "Кт");
	КонецЦикла;
	
КонецПроцедуры

Функция ТекстЗапросаКорректировочныхПроводок()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	НаборДвижений.Период,
	|	НаборДвижений.Регистратор,
	|	НаборДвижений.ПланСчетов,
	|	НаборДвижений.Организация,
	|	НаборДвижений.СчетДт,
	|	НаборДвижений.ПодразделениеДт,
	|	НаборДвижений.НаправлениеДеятельностиДт,
	|	НаборДвижений.ВидСубконтоДт1,
	|	НаборДвижений.СубконтоДт1,
	|	НаборДвижений.ВидСубконтоДт2,
	|	НаборДвижений.СубконтоДт2,
	|	НаборДвижений.ВидСубконтоДт3,
	|	НаборДвижений.СубконтоДт3,
	|	НаборДвижений.ВалютаДт,
	|	НаборДвижений.ВалютнаяСуммаДт,
	|	НаборДвижений.КоличествоДт,
	|	НаборДвижений.СчетКт,
	|	НаборДвижений.ПодразделениеКт,
	|	НаборДвижений.НаправлениеДеятельностиКт,
	|	НаборДвижений.ВидСубконтоКт1,
	|	НаборДвижений.СубконтоКт1,
	|	НаборДвижений.ВидСубконтоКт2,
	|	НаборДвижений.СубконтоКт2,
	|	НаборДвижений.ВидСубконтоКт3,
	|	НаборДвижений.СубконтоКт3,
	|	НаборДвижений.ВалютаКт,
	|	НаборДвижений.ВалютнаяСуммаКт,
	|	НаборДвижений.КоличествоКт,
	|	НаборДвижений.Сумма,
	|	НаборДвижений.СуммаПредставления,
	|	НаборДвижений.ШаблонПроводки
	|ПОМЕСТИТЬ НаборДвижений
	|ИЗ
	|	&НаборДвижений КАК НаборДвижений
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетДт,
	|	СчетКт
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НаборДвижений.СчетДт КАК Счет
	|ПОМЕСТИТЬ СчетаНабора
	|ИЗ
	|	НаборДвижений КАК НаборДвижений
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НаборДвижений.СчетКт КАК Счет
	|ИЗ
	|	НаборДвижений КАК НаборДвижений
	|ИНДЕКСИРОВАТЬ ПО
	|	Счет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыСубконто.Ссылка КАК Счет,
	|	ВидыСубконто.НомерСтроки КАК НомерСубконто,
	|	ВидыСубконто.ВидСубконто КАК ВидСубконто
	|ПОМЕСТИТЬ СубконтоСчетов
	|ИЗ
	|	ПланСчетов.Международный.ВидыСубконто КАК ВидыСубконто
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ СчетаНабора КАК СчетаНабора
	|		ПО ВидыСубконто.Ссылка = СчетаНабора.Счет
	|ИНДЕКСИРОВАТЬ ПО
	|	Счет,
	|	НомерСубконто
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НаборДвижений.Период,
	|	НаборДвижений.Регистратор,
	|	НаборДвижений.ПланСчетов,
	|	НаборДвижений.Организация,
	|	НаборДвижений.СчетДт,
	|	НаборДвижений.ПодразделениеДт,
	|	НаборДвижений.НаправлениеДеятельностиДт,
	|	ЕСТЬNULL(СубконтоСчетовДт1.ВидСубконто, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)) КАК ВидСубконтоДт1,
	|	ВЫБОР
	|		КОГДА СубконтоСчетовДт1.ВидСубконто = НаборДвижений.ВидСубконтоДт1
	|			ТОГДА НаборДвижений.СубконтоДт1
	|		КОГДА СубконтоСчетовДт1.ВидСубконто = НаборДвижений.ВидСубконтоДт2
	|			ТОГДА НаборДвижений.СубконтоДт2
	|		КОГДА СубконтоСчетовДт1.ВидСубконто = НаборДвижений.ВидСубконтоДт3
	|			ТОГДА НаборДвижений.СубконтоДт3
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СубконтоДт1,
	|	ЕСТЬNULL(СубконтоСчетовДт2.ВидСубконто, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)) КАК ВидСубконтоДт2,
	|	ВЫБОР
	|		КОГДА СубконтоСчетовДт2.ВидСубконто = НаборДвижений.ВидСубконтоДт1
	|			ТОГДА НаборДвижений.СубконтоДт1
	|		КОГДА СубконтоСчетовДт2.ВидСубконто = НаборДвижений.ВидСубконтоДт2
	|			ТОГДА НаборДвижений.СубконтоДт2
	|		КОГДА СубконтоСчетовДт2.ВидСубконто = НаборДвижений.ВидСубконтоДт3
	|			ТОГДА НаборДвижений.СубконтоДт3
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СубконтоДт2,
	|	ЕСТЬNULL(СубконтоСчетовДт3.ВидСубконто, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)) КАК ВидСубконтоДт3,
	|	ВЫБОР
	|		КОГДА СубконтоСчетовДт3.ВидСубконто = НаборДвижений.ВидСубконтоДт1
	|			ТОГДА НаборДвижений.СубконтоДт1
	|		КОГДА СубконтоСчетовДт3.ВидСубконто = НаборДвижений.ВидСубконтоДт2
	|			ТОГДА НаборДвижений.СубконтоДт2
	|		КОГДА СубконтоСчетовДт3.ВидСубконто = НаборДвижений.ВидСубконтоДт3
	|			ТОГДА НаборДвижений.СубконтоДт3
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СубконтоДт3,
	|	НаборДвижений.ВалютаДт,
	|	НаборДвижений.ВалютнаяСуммаДт,
	|	НаборДвижений.КоличествоДт,
	|	НаборДвижений.СчетКт,
	|	НаборДвижений.ПодразделениеКт,
	|	НаборДвижений.НаправлениеДеятельностиКт,
	|	ЕСТЬNULL(СубконтоСчетовКт1.ВидСубконто, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)) КАК ВидСубконтоКт1,
	|	ВЫБОР
	|		КОГДА СубконтоСчетовКт1.ВидСубконто = НаборДвижений.ВидСубконтоКт1
	|			ТОГДА НаборДвижений.СубконтоКт1
	|		КОГДА СубконтоСчетовКт1.ВидСубконто = НаборДвижений.ВидСубконтоКт2
	|			ТОГДА НаборДвижений.СубконтоКт2
	|		КОГДА СубконтоСчетовКт1.ВидСубконто = НаборДвижений.ВидСубконтоКт3
	|			ТОГДА НаборДвижений.СубконтоКт3
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СубконтоКт1,
	|	ЕСТЬNULL(СубконтоСчетовКт2.ВидСубконто, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)) КАК ВидСубконтоКт2,
	|	ВЫБОР
	|		КОГДА СубконтоСчетовКт2.ВидСубконто = НаборДвижений.ВидСубконтоКт1
	|			ТОГДА НаборДвижений.СубконтоКт1
	|		КОГДА СубконтоСчетовКт2.ВидСубконто = НаборДвижений.ВидСубконтоКт2
	|			ТОГДА НаборДвижений.СубконтоКт2
	|		КОГДА СубконтоСчетовКт2.ВидСубконто = НаборДвижений.ВидСубконтоКт3
	|			ТОГДА НаборДвижений.СубконтоКт3
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СубконтоКт2,
	|	ЕСТЬNULL(СубконтоСчетовКт3.ВидСубконто, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)) КАК ВидСубконтоКт3,
	|	ВЫБОР
	|		КОГДА СубконтоСчетовКт3.ВидСубконто = НаборДвижений.ВидСубконтоКт1
	|			ТОГДА НаборДвижений.СубконтоКт1
	|		КОГДА СубконтоСчетовКт3.ВидСубконто = НаборДвижений.ВидСубконтоКт2
	|			ТОГДА НаборДвижений.СубконтоКт2
	|		КОГДА СубконтоСчетовКт3.ВидСубконто = НаборДвижений.ВидСубконтоКт3
	|			ТОГДА НаборДвижений.СубконтоКт3
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СубконтоКт3,
	|	НаборДвижений.ВалютаКт,
	|	НаборДвижений.ВалютнаяСуммаКт,
	|	НаборДвижений.КоличествоКт,
	|	НаборДвижений.Сумма,
	|	НаборДвижений.СуммаПредставления,
	|	НаборДвижений.ШаблонПроводки,
	|	ВЫБОР
	|		КОГДА НаборДвижений.Период > ЕстьNULL(ДатыЗапретаПоОрганизациям.ДатаЗапрета, ДатаВремя(1,1,1)) 
	|			ТОГДА НаборДвижений.Период
	|		ИНАЧЕ ДОБАВИТЬКДАТЕ(ДатыЗапретаПоОрганизациям.ДатаЗапрета, СЕКУНДА, 1)
	|	КОНЕЦ КАК ПериодКорректировки
	|ПОМЕСТИТЬ НовыеПроводки
	|ИЗ
	|	НаборДвижений КАК НаборДвижений
	|	ЛЕВОЕ СОЕДИНЕНИЕ СубконтоСчетов КАК СубконтоСчетовДт1
	|		ПО НаборДвижений.СчетДт = СубконтоСчетовДт1.Счет
	|		 И СубконтоСчетовДт1.НомерСубконто = 1
	|	ЛЕВОЕ СОЕДИНЕНИЕ СубконтоСчетов КАК СубконтоСчетовДт2
	|		ПО НаборДвижений.СчетДт = СубконтоСчетовДт2.Счет
	|		 И СубконтоСчетовДт2.НомерСубконто = 2
	|	ЛЕВОЕ СОЕДИНЕНИЕ СубконтоСчетов КАК СубконтоСчетовДт3
	|		ПО НаборДвижений.СчетДт = СубконтоСчетовДт3.Счет
	|		 И СубконтоСчетовДт3.НомерСубконто = 3
	|	ЛЕВОЕ СОЕДИНЕНИЕ СубконтоСчетов КАК СубконтоСчетовКт1
	|		ПО НаборДвижений.СчетКт = СубконтоСчетовКт1.Счет
	|		 И СубконтоСчетовКт1.НомерСубконто = 1
	|	ЛЕВОЕ СОЕДИНЕНИЕ СубконтоСчетов КАК СубконтоСчетовКт2
	|		ПО НаборДвижений.СчетКт = СубконтоСчетовКт2.Счет
	|		 И СубконтоСчетовКт2.НомерСубконто = 2
	|	ЛЕВОЕ СОЕДИНЕНИЕ СубконтоСчетов КАК СубконтоСчетовКт3
	|		ПО НаборДвижений.СчетКт = СубконтоСчетовКт3.Счет
	|		 И СубконтоСчетовКт3.НомерСубконто = 3
	|	ЛЕВОЕ СОЕДИНЕНИЕ ДатыЗапретаПоОрганизациям КАК ДатыЗапретаПоОрганизациям
	|		ПО НаборДвижений.Организация = ДатыЗапретаПоОрганизациям.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсходныеПроводки.Период,
	|	ИсходныеПроводки.Регистратор,
	|	ИсходныеПроводки.ПланСчетов,
	|	ИсходныеПроводки.Организация,
	|	ИсходныеПроводки.СчетДт,
	|	ЕСТЬNULL(ИсходныеПроводки.ПодразделениеДт, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)) КАК ПодразделениеДт,
	|	ЕСТЬNULL(ИсходныеПроводки.НаправлениеДеятельностиДт, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельностиДт,
	|	ЕСТЬNULL(ИсходныеПроводки.ВидСубконтоДт1, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)) КАК ВидСубконтоДт1,
	|	ЕСТЬNULL(ИсходныеПроводки.СубконтоДт1, НЕОПРЕДЕЛЕНО) КАК СубконтоДт1,
	|	ЕСТЬNULL(ИсходныеПроводки.ВидСубконтоДт2, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)) КАК ВидСубконтоДт2,
	|	ЕСТЬNULL(ИсходныеПроводки.СубконтоДт2, НЕОПРЕДЕЛЕНО) КАК СубконтоДт2,
	|	ЕСТЬNULL(ИсходныеПроводки.ВидСубконтоДт3, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)) КАК ВидСубконтоДт3,
	|	ЕСТЬNULL(ИсходныеПроводки.СубконтоДт3, НЕОПРЕДЕЛЕНО) КАК СубконтоДт3,
	|	ЕСТЬNULL(ИсходныеПроводки.ВалютаДт, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)) КАК ВалютаДт,
	|	ЕСТЬNULL(ИсходныеПроводки.ВалютнаяСуммаДт, 0) КАК ВалютнаяСуммаДт,
	|	ЕСТЬNULL(ИсходныеПроводки.КоличествоДт, 0) КАК КоличествоДт,
	|	ИсходныеПроводки.СчетКт,
	|	ЕСТЬNULL(ИсходныеПроводки.ПодразделениеКт, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)) КАК ПодразделениеКт,
	|	ЕСТЬNULL(ИсходныеПроводки.НаправлениеДеятельностиКт, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельностиКт,
	|	ЕСТЬNULL(ИсходныеПроводки.ВидСубконтоКт1, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)) КАК ВидСубконтоКт1,
	|	ЕСТЬNULL(ИсходныеПроводки.СубконтоКт1, НЕОПРЕДЕЛЕНО) КАК СубконтоКт1,
	|	ЕСТЬNULL(ИсходныеПроводки.ВидСубконтоКт2, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)) КАК ВидСубконтоКт2,
	|	ЕСТЬNULL(ИсходныеПроводки.СубконтоКт2, НЕОПРЕДЕЛЕНО) КАК СубконтоКт2,
	|	ЕСТЬNULL(ИсходныеПроводки.ВидСубконтоКт3, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)) КАК ВидСубконтоКт3,
	|	ЕСТЬNULL(ИсходныеПроводки.СубконтоКт3, НЕОПРЕДЕЛЕНО) КАК СубконтоКт3,
	|	ЕСТЬNULL(ИсходныеПроводки.ВалютаКт, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)) КАК ВалютаКт,
	|	ЕСТЬNULL(ИсходныеПроводки.ВалютнаяСуммаКт, 0) КАК ВалютнаяСуммаКт,
	|	ЕСТЬNULL(ИсходныеПроводки.КоличествоКт, 0) КАК КоличествоКт,
	|	ИсходныеПроводки.Сумма,
	|	ИсходныеПроводки.СуммаПредставления,
	|	ИсходныеПроводки.ШаблонПроводки,
	|	ИсходныеПроводки.ТипПроводки,
	|	ИсходныеПроводки.Содержание,
	|	ДОБАВИТЬКДАТЕ(ДатыЗапретаПоОрганизациям.ДатаЗапрета, СЕКУНДА, 1) КАК ПериодКорректировки
	|ПОМЕСТИТЬ ИсходныеПроводки
	|ИЗ
	|	РегистрБухгалтерии.Международный.ДвиженияССубконто(, , Регистратор В (&Документ), , ) КАК ИсходныеПроводки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДатыЗапретаПоОрганизациям КАК ДатыЗапретаПоОрганизациям
	|		ПО ИсходныеПроводки.Организация = ДатыЗапретаПоОрганизациям.Организация
	|			И ИсходныеПроводки.Период <= ДатыЗапретаПоОрганизациям.ДатаЗапрета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Период,
	|	ВложенныйЗапрос.Регистратор,
	|	ВложенныйЗапрос.ПланСчетов,
	|	ВложенныйЗапрос.Организация,
	|	ВложенныйЗапрос.СчетДт,
	|	ВложенныйЗапрос.ПодразделениеДт,
	|	ВложенныйЗапрос.НаправлениеДеятельностиДт,
	|	ВложенныйЗапрос.ВидСубконтоДт1,
	|	ВложенныйЗапрос.СубконтоДт1,
	|	ВложенныйЗапрос.ВидСубконтоДт2,
	|	ВложенныйЗапрос.СубконтоДт2,
	|	ВложенныйЗапрос.ВидСубконтоДт3,
	|	ВложенныйЗапрос.СубконтоДт3,
	|	ВложенныйЗапрос.ВалютаДт,
	|	ВложенныйЗапрос.СчетКт,
	|	ВложенныйЗапрос.ПодразделениеКт,
	|	ВложенныйЗапрос.НаправлениеДеятельностиКт,
	|	ВложенныйЗапрос.ВидСубконтоКт1,
	|	ВложенныйЗапрос.СубконтоКт1,
	|	ВложенныйЗапрос.ВидСубконтоКт2,
	|	ВложенныйЗапрос.СубконтоКт2,
	|	ВложенныйЗапрос.ВидСубконтоКт3,
	|	ВложенныйЗапрос.СубконтоКт3,
	|	ВложенныйЗапрос.ВалютаКт,
	|	СУММА(ВложенныйЗапрос.ВалютнаяСуммаДт) КАК ВалютнаяСуммаДт,
	|	СУММА(ВложенныйЗапрос.ВалютнаяСуммаКт) КАК ВалютнаяСуммаКт,
	|	СУММА(ВложенныйЗапрос.КоличествоДт) КАК КоличествоДт,
	|	СУММА(ВложенныйЗапрос.КоличествоКт) КАК КоличествоКт,
	|	СУММА(ВложенныйЗапрос.Сумма) КАК Сумма,
	|	СУММА(ВложенныйЗапрос.СуммаПредставления) КАК СуммаПредставления,
	|	ВложенныйЗапрос.ШаблонПроводки КАК ШаблонПроводки,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПроводокМеждународныйУчет.Корректировка) КАК ТипПроводки
	|ПОМЕСТИТЬ КорректировочныеПроводки
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПроводкиСторно.ПериодКорректировки КАК Период,
	|		ПроводкиСторно.Регистратор КАК Регистратор,
	|		ПроводкиСторно.ПланСчетов КАК ПланСчетов,
	|		ПроводкиСторно.Организация КАК Организация,
	|		ПроводкиСторно.СчетДт КАК СчетДт,
	|		ПроводкиСторно.ПодразделениеДт КАК ПодразделениеДт,
	|		ПроводкиСторно.НаправлениеДеятельностиДт КАК НаправлениеДеятельностиДт,
	|		ПроводкиСторно.ВидСубконтоДт1 КАК ВидСубконтоДт1,
	|		ПроводкиСторно.СубконтоДт1 КАК СубконтоДт1,
	|		ПроводкиСторно.ВидСубконтоДт2 КАК ВидСубконтоДт2,
	|		ПроводкиСторно.СубконтоДт2 КАК СубконтоДт2,
	|		ПроводкиСторно.ВидСубконтоДт3 КАК ВидСубконтоДт3,
	|		ПроводкиСторно.СубконтоДт3 КАК СубконтоДт3,
	|		ПроводкиСторно.ВалютаДт КАК ВалютаДт,
	|		ПроводкиСторно.СчетКт КАК СчетКт,
	|		ПроводкиСторно.ПодразделениеКт КАК ПодразделениеКт,
	|		ПроводкиСторно.НаправлениеДеятельностиКт КАК НаправлениеДеятельностиКт,
	|		ПроводкиСторно.ВидСубконтоКт1 КАК ВидСубконтоКт1,
	|		ПроводкиСторно.СубконтоКт1 КАК СубконтоКт1,
	|		ПроводкиСторно.ВидСубконтоКт2 КАК ВидСубконтоКт2,
	|		ПроводкиСторно.СубконтоКт2 КАК СубконтоКт2,
	|		ПроводкиСторно.ВидСубконтоКт3 КАК ВидСубконтоКт3,
	|		ПроводкиСторно.СубконтоКт3 КАК СубконтоКт3,
	|		ПроводкиСторно.ВалютаКт КАК ВалютаКт,
	|		-ПроводкиСторно.ВалютнаяСуммаДт КАК ВалютнаяСуммаДт,
	|		-ПроводкиСторно.ВалютнаяСуммаКт КАК ВалютнаяСуммаКт,
	|		-ПроводкиСторно.КоличествоДт КАК КоличествоДт,
	|		-ПроводкиСторно.КоличествоКт КАК КоличествоКт,
	|		-ПроводкиСторно.Сумма КАК Сумма,
	|		-ПроводкиСторно.СуммаПредставления КАК СуммаПредставления,
	|		ПроводкиСторно.ШаблонПроводки КАК ШаблонПроводки
	|	ИЗ
	|		ИсходныеПроводки КАК ПроводкиСторно
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НовыеПроводки.ПериодКорректировки,
	|		НовыеПроводки.Регистратор,
	|		НовыеПроводки.ПланСчетов,
	|		НовыеПроводки.Организация,
	|		НовыеПроводки.СчетДт,
	|		НовыеПроводки.ПодразделениеДт,
	|		НовыеПроводки.НаправлениеДеятельностиДт,
	|		НовыеПроводки.ВидСубконтоДт1,
	|		НовыеПроводки.СубконтоДт1,
	|		НовыеПроводки.ВидСубконтоДт2,
	|		НовыеПроводки.СубконтоДт2,
	|		НовыеПроводки.ВидСубконтоДт3,
	|		НовыеПроводки.СубконтоДт3,
	|		НовыеПроводки.ВалютаДт,
	|		НовыеПроводки.СчетКт,
	|		НовыеПроводки.ПодразделениеКт,
	|		НовыеПроводки.НаправлениеДеятельностиКт,
	|		НовыеПроводки.ВидСубконтоКт1,
	|		НовыеПроводки.СубконтоКт1,
	|		НовыеПроводки.ВидСубконтоКт2,
	|		НовыеПроводки.СубконтоКт2,
	|		НовыеПроводки.ВидСубконтоКт3,
	|		НовыеПроводки.СубконтоКт3,
	|		НовыеПроводки.ВалютаКт,
	|		НовыеПроводки.ВалютнаяСуммаДт,
	|		НовыеПроводки.ВалютнаяСуммаКт,
	|		НовыеПроводки.КоличествоДт,
	|		НовыеПроводки.КоличествоКт,
	|		НовыеПроводки.Сумма,
	|		НовыеПроводки.СуммаПредставления,
	|		НовыеПроводки.ШаблонПроводки
	|	ИЗ
	|		НовыеПроводки КАК НовыеПроводки) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Период,
	|	ВложенныйЗапрос.Регистратор,
	|	ВложенныйЗапрос.ПланСчетов,
	|	ВложенныйЗапрос.Организация,
	|	ВложенныйЗапрос.СчетДт,
	|	ВложенныйЗапрос.ПодразделениеДт,
	|	ВложенныйЗапрос.НаправлениеДеятельностиДт,
	|	ВложенныйЗапрос.ВидСубконтоДт1,
	|	ВложенныйЗапрос.СубконтоДт1,
	|	ВложенныйЗапрос.ВидСубконтоДт2,
	|	ВложенныйЗапрос.СубконтоДт2,
	|	ВложенныйЗапрос.ВидСубконтоДт3,
	|	ВложенныйЗапрос.СубконтоДт3,
	|	ВложенныйЗапрос.ВалютаДт,
	|	ВложенныйЗапрос.СчетКт,
	|	ВложенныйЗапрос.ПодразделениеКт,
	|	ВложенныйЗапрос.НаправлениеДеятельностиКт,
	|	ВложенныйЗапрос.ВидСубконтоКт1,
	|	ВложенныйЗапрос.СубконтоКт1,
	|	ВложенныйЗапрос.ВидСубконтоКт2,
	|	ВложенныйЗапрос.СубконтоКт2,
	|	ВложенныйЗапрос.ВидСубконтоКт3,
	|	ВложенныйЗапрос.СубконтоКт3,
	|	ВложенныйЗапрос.ВалютаКт,
	|	ВложенныйЗапрос.ШаблонПроводки
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВложенныйЗапрос.Сумма) <> 0
	|	ИЛИ СУММА(ВложенныйЗапрос.ВалютнаяСуммаДт) <> 0
	|	ИЛИ СУММА(ВложенныйЗапрос.ВалютнаяСуммаКт) <> 0
	|	ИЛИ СУММА(ВложенныйЗапрос.КоличествоДт) <> 0
	|	ИЛИ СУММА(ВложенныйЗапрос.КоличествоКт) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсходныеПроводки.Период,
	|	ИсходныеПроводки.Регистратор,
	|	ИсходныеПроводки.ПланСчетов,
	|	ИсходныеПроводки.Организация,
	|	ИсходныеПроводки.СчетДт,
	|	ИсходныеПроводки.ПодразделениеДт,
	|	ИсходныеПроводки.НаправлениеДеятельностиДт,
	|	ИсходныеПроводки.ВидСубконтоДт1,
	|	ИсходныеПроводки.СубконтоДт1,
	|	ИсходныеПроводки.ВидСубконтоДт2,
	|	ИсходныеПроводки.СубконтоДт2,
	|	ИсходныеПроводки.ВидСубконтоДт3,
	|	ИсходныеПроводки.СубконтоДт3,
	|	ИсходныеПроводки.СчетКт,
	|	ИсходныеПроводки.ПодразделениеКт,
	|	ИсходныеПроводки.НаправлениеДеятельностиКт,
	|	ИсходныеПроводки.ВидСубконтоКт1,
	|	ИсходныеПроводки.СубконтоКт1,
	|	ИсходныеПроводки.ВидСубконтоКт2,
	|	ИсходныеПроводки.СубконтоКт2,
	|	ИсходныеПроводки.ВидСубконтоКт3,
	|	ИсходныеПроводки.СубконтоКт3,
	|	ИсходныеПроводки.ВалютаДт,
	|	ИсходныеПроводки.ВалютаКт,
	|	ИсходныеПроводки.ВалютнаяСуммаДт,
	|	ИсходныеПроводки.ВалютнаяСуммаКт,
	|	ИсходныеПроводки.КоличествоДт,
	|	ИсходныеПроводки.КоличествоКт,
	|	ИсходныеПроводки.Сумма,
	|	ИсходныеПроводки.СуммаПредставления,
	|	ИсходныеПроводки.ШаблонПроводки,
	|	ИсходныеПроводки.ТипПроводки,
	|	ИсходныеПроводки.Содержание
	|ИЗ
	|	ИсходныеПроводки КАК ИсходныеПроводки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировочныеПроводки.Период,
	|	КорректировочныеПроводки.Регистратор,
	|	КорректировочныеПроводки.ПланСчетов,
	|	КорректировочныеПроводки.Организация,
	|	КорректировочныеПроводки.СчетДт,
	|	КорректировочныеПроводки.ПодразделениеДт,
	|	КорректировочныеПроводки.НаправлениеДеятельностиДт,
	|	КорректировочныеПроводки.ВидСубконтоДт1,
	|	КорректировочныеПроводки.СубконтоДт1,
	|	КорректировочныеПроводки.ВидСубконтоДт2,
	|	КорректировочныеПроводки.СубконтоДт2,
	|	КорректировочныеПроводки.ВидСубконтоДт3,
	|	КорректировочныеПроводки.СубконтоДт3,
	|	КорректировочныеПроводки.СчетКт,
	|	КорректировочныеПроводки.ПодразделениеКт,
	|	КорректировочныеПроводки.НаправлениеДеятельностиКт,
	|	КорректировочныеПроводки.ВидСубконтоКт1,
	|	КорректировочныеПроводки.СубконтоКт1,
	|	КорректировочныеПроводки.ВидСубконтоКт2,
	|	КорректировочныеПроводки.СубконтоКт2,
	|	КорректировочныеПроводки.ВидСубконтоКт3,
	|	КорректировочныеПроводки.СубконтоКт3,
	|	КорректировочныеПроводки.ВалютаДт,
	|	КорректировочныеПроводки.ВалютаКт,
	|	КорректировочныеПроводки.ВалютнаяСуммаДт,
	|	КорректировочныеПроводки.ВалютнаяСуммаКт,
	|	КорректировочныеПроводки.КоличествоДт,
	|	КорректировочныеПроводки.КоличествоКт,
	|	КорректировочныеПроводки.Сумма,
	|	КорректировочныеПроводки.СуммаПредставления,
	|	КорректировочныеПроводки.ШаблонПроводки,
	|	КорректировочныеПроводки.ТипПроводки,
	|	""""
	|ИЗ
	|	КорректировочныеПроводки КАК КорректировочныеПроводки
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПроверкиДатыЗапрета()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Организации.Ссылка КАК Организация,
	|	ВЫБОР
	|		КОГДА НЕ ДатыЗапретаПоОрганизации.ДатаЗапрета ЕСТЬ NULL 
	|			ТОГДА КОНЕЦПЕРИОДА(ДатыЗапретаПоОрганизации.ДатаЗапрета, ДЕНЬ)
	|		КОГДА НЕ ДатыЗапретаОбщая.ДатаЗапрета ЕСТЬ NULL 
	|			ТОГДА КОНЕЦПЕРИОДА(ДатыЗапретаОбщая.ДатаЗапрета, ДЕНЬ)
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|	КОНЕЦ КАК ДатаЗапрета
	|ПОМЕСТИТЬ ДатыЗапретаПоОрганизациям
	|ИЗ
	|	Справочник.Организации КАК Организации
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДатыЗапретаФормированияПроводокМеждународныйУчет КАК ДатыЗапретаПоОрганизации
	|		ПО Организации.Ссылка = ДатыЗапретаПоОрганизации.Организация
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДатыЗапретаФормированияПроводокМеждународныйУчет КАК ДатыЗапретаОбщая
	|		ПО (ДатыЗапретаОбщая.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
	|ГДЕ
	|	ВЫБОР
	|		КОГДА НЕ ДатыЗапретаПоОрганизации.ДатаЗапрета ЕСТЬ NULL 
	|			ТОГДА КОНЕЦПЕРИОДА(ДатыЗапретаПоОрганизации.ДатаЗапрета, ДЕНЬ)
	|		КОГДА НЕ ДатыЗапретаОбщая.ДатаЗапрета ЕСТЬ NULL 
	|			ТОГДА КОНЕЦПЕРИОДА(ДатыЗапретаОбщая.ДатаЗапрета, ДЕНЬ)
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|	КОНЕЦ <> ДАТАВРЕМЯ(1, 1, 1)
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ДатаЗапрета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПериодыНабора.Период КАК Период,
	|	ПериодыНабора.Организация КАК Организация
	|ПОМЕСТИТЬ ПериодыНабора
	|ИЗ
	|	&ПериодыНабора КАК ПериодыНабора
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА
	|ИЗ
	|	РегистрБухгалтерии.Международный КАК ИсходныеПроводки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДатыЗапретаПоОрганизациям КАК ДатыЗапретаПоОрганизациям
	|		ПО ИсходныеПроводки.Организация = ДатыЗапретаПоОрганизациям.Организация
	|		 И ИсходныеПроводки.Период <= ДатыЗапретаПоОрганизациям.ДатаЗапрета
	|ГДЕ
	|	ИсходныеПроводки.Регистратор = &Документ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА
	|ИЗ
	|	ПериодыНабора КАК ПериодыНабора
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДатыЗапретаПоОрганизациям КАК ДатыЗапретаПоОрганизациям
	|		ПО ПериодыНабора.Организация = ДатыЗапретаПоОрганизациям.Организация
	|		 И ПериодыНабора.Период <= ДатыЗапретаПоОрганизациям.ДатаЗапрета
	|";
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ЕстьНеобходимостьФормированияКорректировочныхПроводок(Регистратор, МенеджерВременныхТаблиц)
	
	ПериодыНабора = ЭтотОбъект.Выгрузить(, "Период,Организация");
	ПериодыНабора.Свернуть("Период,Организация");
	
	Запрос = Новый Запрос(ТекстЗапросаПроверкиДатыЗапрета());
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Документ", Регистратор);
	Запрос.УстановитьПараметр("ПериодыНабора", ПериодыНабора);
	Результат = Запрос.Выполнить();
	
	Возврат Не Результат.Пустой();
	
КонецФункции

#КонецОбласти

#КонецЕсли