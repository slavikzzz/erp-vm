#Область ОписаниеПеременных

&НаКлиенте
Перем КонтекстЭДОКлиент;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ПоказыватьКнопки = Параметры.Свойство("ПоказыватьКнопки") И Параметры.ПоказыватьКнопки;
	Элементы.ФлагБольшеНеПоказывать.Видимость = ПоказыватьКнопки;
	Элементы.Интервал2.Видимость = ПоказыватьКнопки;
	Элементы.Интервал3.Ширина = ?(ПоказыватьКнопки, 18, 0);
	
	Если ПоказыватьКнопки Тогда
		ПроверитьОтказалисьЛиОтПоказа(Отказ);
	КонецЕсли;
	
	Организация = Параметры.Организация;
	
	ИмяМакетаПредложения = "МакетФормыПредложенияОПодключении";
	МакетHTML = ПолучитьОбщийМакет(ИмяМакетаПредложения).ПолучитьТекст();
	ДокументHTML = ПолучитьДокументHTML(МакетHTML);
	СтраницаHTML = ПолучитьТекстHTML(ДокументHTML);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЭтаФорма.ТекущийЭлемент = Элементы.ОбратныйЗвонок;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииЗавершение", ЭтотОбъект);
	ДокументооборотСКОКлиент.ПолучитьКонтекстЭДО(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Элементы.ФлагБольшеНеПоказывать.Видимость И ФлагБольшеНеПоказывать Тогда 
		СохранитьЗначениеБольшеНеПоказыватьНаСервере(Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПрезентацияПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	ЭтаФорма.ТекущийЭлемент = Элементы.ФлагБольшеНеПоказывать;
	Если ДанныеСобытия.element.id = "gwd-taparea_1" Тогда
		ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("https://portal.1c.ru/applications/1C-Reporting");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПроверитьОтказалисьЛиОтПоказа(Отказ)
	
	БольшеНеПоказывать 	= ХранилищеОбщихНастроек.Загрузить(
		"Обработка.ОбщиеОбъектыРеглОтчетности", 
		"НеПоказыватьПредложениеОформитьЗаявлениеНаПодключение");
	
	Если БольшеНеПоказывать = Истина Тогда
		Отказ = Истина;
	КонецЕсли;
	
	ВремяПоследнегоНапоминания 	= ХранилищеОбщихНастроек.Загрузить(
		"Обработка.ОбщиеОбъектыРеглОтчетности", 
		"ВремяПоследнегоНапоминания");
	
	Если ВремяПоследнегоНапоминания <> Неопределено 
		И ВремяПоследнегоНапоминания + 4 * 60 * 60 > ТекущаяДатаСеанса() Тогда
		
		Отказ = Истина;
		
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Процедура СохранитьЗначениеБольшеНеПоказыватьНаСервере(БольшеНеПоказывать)
	
	ХранилищеОбщихНастроек.Сохранить(
		"Обработка.ОбщиеОбъектыРеглОтчетности", 
		"НеПоказыватьПредложениеОформитьЗаявлениеНаПодключение", 
		БольшеНеПоказывать);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьДокументHTML(МакетHTML)

	ЧтениеHTML = Новый ЧтениеHTML;
	ЧтениеHTML.УстановитьСтроку(МакетHTML, "UTF-8");
	
	ПостроительDOM = Новый ПостроительDOM;
	ДокументHTML = ПостроительDOM.Прочитать(ЧтениеHTML);
	ЧтениеHTML.Закрыть();
	
	Возврат ДокументHTML;
	
КонецФункции

&НаСервере
Функция ПолучитьТекстHTML(ДокументHTML)
	
	ЗапиcьHTML = Новый ЗаписьHTML;
	ЗапиcьHTML.УстановитьСтроку();
	ЗаписьDOM = Новый ЗаписьDOM;
	ЗаписьDOM.Записать(ДокументHTML, ЗапиcьHTML);

	Возврат ЗапиcьHTML.Закрыть();
	
КонецФункции

&НаКлиенте
Процедура ПриОткрытииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	КонтекстЭДОКлиент = Результат.КонтекстЭДО;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьсяНажатие(Элемент)
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьФормуМастераЗаявленияНаПодключение(
		, , ,
		ПредопределенноеЗначение("Перечисление.ТипыЗаявленияАбонентаСпецоператораСвязи.Первичное")
		);
		
КонецПроцедуры

&НаКлиенте
Процедура Декорация1Нажатие(Элемент)
	
	ИмяФормыОбращенияВТехподдержку = "ОбщаяФорма.ОбращениеВТехподдержку";
	ОткрытьФорму(ИмяФормыОбращенияВТехподдержку);
	
КонецПроцедуры

#КонецОбласти








