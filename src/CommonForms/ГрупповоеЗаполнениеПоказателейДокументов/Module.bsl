
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РасширенноеРедактирование = Параметры.РасширенноеРедактирование;
	МассивНачислений = Неопределено;
	Параметры.Свойство("МассивНачислений", МассивНачислений);
	Если МассивНачислений <> Неопределено Тогда 
		ДополнитьФормуНачислениями(МассивНачислений);
		ЗаполнитьНачисления(МассивНачислений);
		УстановитьДоступностьЗначенийНачислений(ЭтотОбъект);
	КонецЕсли;
	
	МассивПоказателей = Параметры.МассивПоказателей;
	Если МассивПоказателей.Количество() > 0 Или МассивНачислений = Неопределено Тогда
		ДополнитьФормуПоказателями(МассивПоказателей);
		ЗаполнитьПоказатели(МассивПоказателей);
	КонецЕсли;
	
	КлючСохраненияПоложенияОкна = Строка(КоличествоПоказателей + КоличествоНачислений);
	
	УстановитьДоступностьЗначенийПоказателей(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура Подключаемый_ИспользованиеПоказательПриИзменении(Элемент)
	
	УстановитьДоступностьЗначенийПоказателей(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ИспользованиеНачислениеПриИзменении(Элемент)
	
	УстановитьДоступностьЗначенийНачислений(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПоказательДействие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НачислениеДействие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	НачисленияПоказатели = Новый Структура;
	
	ЗначенияПоказателей = Новый Соответствие;
	Для Сч = 1 По КоличествоПоказателей Цикл
		Если ЭтотОбъект["Показатель" + Сч + "Использование"] Тогда 
			Если РасширенноеРедактирование Тогда
				ДействиеСПоказателем = Новый Структура;
				ДействиеСПоказателем.Вставить("Действие", ЭтотОбъект["Показатель" + Сч + "Действие"]);
				ДействиеСПоказателем.Вставить("Значение", ЭтотОбъект["Показатель" + Сч + "Значение"]);
				ДействиеСПоказателем.Вставить("Округление", ЭтотОбъект["Показатель" + Сч + "Округление"]);
				ЗначенияПоказателей.Вставить(ЭтотОбъект["Показатель" + Сч], ДействиеСПоказателем);
			Иначе
				ЗначенияПоказателей.Вставить(ЭтотОбъект["Показатель" + Сч], ЭтотОбъект["Показатель" + Сч + "Значение"]);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	НачисленияПоказатели.Вставить("Показатели", ЗначенияПоказателей);
	
	РазмерНачислений = Новый Соответствие;
	Для Сч = 1 По КоличествоНачислений Цикл
		Если ЭтотОбъект["Начисление" + Сч + "Использование"] Тогда 
			Если РасширенноеРедактирование Тогда
				ДействиеСНачислением = Новый Структура;
				ДействиеСНачислением.Вставить("Действие", ЭтотОбъект["Начисление" + Сч + "Действие"]);
				ДействиеСНачислением.Вставить("Значение", ЭтотОбъект["Начисление" + Сч + "Значение"]);
				ДействиеСНачислением.Вставить("Округление", ЭтотОбъект["Начисление" + Сч + "Округление"]);
				РазмерНачислений.Вставить(ЭтотОбъект["Начисление" + Сч], ДействиеСНачислением);
			Иначе
				РазмерНачислений.Вставить(ЭтотОбъект["Начисление" + Сч], ЭтотОбъект["Начисление" + Сч + "Значение"]);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	НачисленияПоказатели.Вставить("Начисления", РазмерНачислений);
	
	Оповестить("ИзмененыПоказателиДокумента", НачисленияПоказатели, ЭтотОбъект);
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ДополнитьФормуПоказателями(МассивПоказателей)

	КоличествоПоказателей = МассивПоказателей.Количество();
	ФиксированнаяСумма = КоличествоПоказателей = 0;
	
	РеквизитыПоказателей = Новый Соответствие;
	Если КоличествоПоказателей <> 0 Тогда 
		РеквизитыПоказателей = ЗарплатаКадрыРасширенный.СведенияОПоказателяхРасчетаЗарплаты(МассивПоказателей);
	КонецЕсли;
	
	КоличествоПоказателей = ?(ФиксированнаяСумма, 1, КоличествоПоказателей);
	
	МассивИменРеквизитовФормы = Новый Массив;
	ЗарплатаКадры.ЗаполнитьМассивИменРеквизитовФормы(ЭтотОбъект, МассивИменРеквизитовФормы);
	
	ДобавляемыеРеквизиты = Новый Массив;
	
	Для Сч = 1 По КоличествоПоказателей Цикл
		Если ФиксированнаяСумма Тогда
			НаименованиеПоказателя = НСтр("ru = 'Сумма';
											|en = 'Amount'");
			ДобавитьРеквизитыПоказателейИНачислений(Истина, НаименованиеПоказателя, ДобавляемыеРеквизиты, Сч);
			Продолжить;
		КонецЕсли;
		
		Показатель = МассивПоказателей[Сч - 1];
		Если ЗначениеЗаполнено(РеквизитыПоказателей[Показатель].КраткоеНаименование) Тогда
			НаименованиеПоказателя = РеквизитыПоказателей[Показатель].КраткоеНаименование;
		Иначе
			НаименованиеПоказателя = РеквизитыПоказателей[Показатель].Наименование;
		КонецЕсли;
		
		ДобавитьРеквизитыПоказателейИНачислений(Истина, НаименованиеПоказателя, ДобавляемыеРеквизиты, Сч);
	КонецЦикла;
	
	ЗарплатаКадры.ИзменитьРеквизитыФормы(ЭтотОбъект, ДобавляемыеРеквизиты, МассивИменРеквизитовФормы);
	ТипЧисло = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2));
	
	Для Сч = 1 По КоличествоПоказателей Цикл
		Если ФиксированнаяСумма Тогда
			Показатель = Неопределено;
			ТипПоказателя = ТипЧисло;
		Иначе
			Показатель = МассивПоказателей[Сч - 1];
			ТипПоказателя = РеквизитыПоказателей[Показатель].ТипПоказателяПриРасчете;
		КонецЕсли;
		ДобавитьЭлементыПоказателейИНачислений(Истина, Показатель, ТипПоказателя, Сч);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДополнитьФормуНачислениями(МассивНачислений)

	КоличествоНачислений = МассивНачислений.Количество();
	
	Если КоличествоНачислений = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	РеквизитыНачислений = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивНачислений, "Наименование, КраткоеНаименование");
	
	МассивИменРеквизитовФормы = Новый Массив;
	ЗарплатаКадры.ЗаполнитьМассивИменРеквизитовФормы(ЭтотОбъект, МассивИменРеквизитовФормы);
	
	ДобавляемыеРеквизиты = Новый Массив;
	
	Для Сч = 1 По КоличествоНачислений Цикл
		
		Начисление = МассивНачислений[Сч-1];
		Если ЗначениеЗаполнено(РеквизитыНачислений[Начисление].КраткоеНаименование) Тогда
			НаименованиеНачисления = ЗначениеЗаполнено(РеквизитыНачислений[Начисление].КраткоеНаименование);
		Иначе
			НаименованиеНачисления = РеквизитыНачислений[Начисление].Наименование;
		КонецЕсли;
		ДобавитьРеквизитыПоказателейИНачислений(Ложь, НаименованиеНачисления, ДобавляемыеРеквизиты, Сч);
	КонецЦикла;
	
	ЗарплатаКадры.ИзменитьРеквизитыФормы(ЭтотОбъект, ДобавляемыеРеквизиты, МассивИменРеквизитовФормы);
	ТипЧисло = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2));
	
	Для Сч = 1 По КоличествоНачислений Цикл
		Начисление = МассивНачислений[Сч-1];
		ДобавитьЭлементыПоказателейИНачислений(Ложь, Начисление, ТипЧисло, Сч);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьРеквизитыПоказателейИНачислений(ЭтоПоказатель, НаименованиеПоказателя, ДобавляемыеРеквизиты, Сч)
	
	Если ЭтоПоказатель Тогда
		ИмяРеквизита = "Показатель";
		ПоказательРеквизит = Новый РеквизитФормы(ИмяРеквизита + Сч, Новый ОписаниеТипов("СправочникСсылка.ПоказателиРасчетаЗарплаты"));
		ДобавляемыеРеквизиты.Добавить(ПоказательРеквизит);
	Иначе
		ИмяРеквизита = "Начисление";
		ПоказательРеквизит = Новый РеквизитФормы(ИмяРеквизита + Сч, Новый ОписаниеТипов("ПланВидовРасчетаСсылка.Начисления"));
		ДобавляемыеРеквизиты.Добавить(ПоказательРеквизит);
	КонецЕсли;
	
	ЗначениеРеквизит = Новый РеквизитФормы(ИмяРеквизита + Сч + "Значение", Справочники.ПоказателиРасчетаЗарплаты.ОписаниеТиповЗначенияПоказателяРасчетаЗарплаты());
	ДобавляемыеРеквизиты.Добавить(ЗначениеРеквизит);
		
	ИспользованиеПоказателяРеквизит = Новый РеквизитФормы(ИмяРеквизита + Сч + "Использование", Новый ОписаниеТипов("Булево"),, НаименованиеПоказателя);
	ДобавляемыеРеквизиты.Добавить(ИспользованиеПоказателяРеквизит);
	
	Если РасширенноеРедактирование Тогда
		ДействиеРеквизит = Новый РеквизитФормы(ИмяРеквизита + Сч + "Действие", Новый ОписаниеТипов("Строка"));
		ДобавляемыеРеквизиты.Добавить(ДействиеРеквизит);
		
		ДействиеРеквизит = Новый РеквизитФормы(ИмяРеквизита + Сч + "Округление", Новый ОписаниеТипов("СправочникСсылка.СпособыОкругленияПриРасчетеЗарплаты"));
		ДобавляемыеРеквизиты.Добавить(ДействиеРеквизит);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьЭлементыПоказателейИНачислений(ЭтоПоказатель, Показатель, ТипПоказателя, Сч)
	
	ИмяРеквизита = ?(ЭтоПоказатель, "Показатель", "Начисление");
	Если Элементы.Найти(ИмяРеквизита + Сч + "Использование") <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Использование = Элементы.Добавить(ИмяРеквизита + Сч + "Использование", Тип("ПолеФормы"), Элементы.ПоказателиЛеваяКолонка);
	Использование.Вид = ВидПоляФормы.ПолеФлажка;
	Использование.ПутьКДанным = ИмяРеквизита + Сч + "Использование";
	Использование.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
	Использование.УстановитьДействие("ПриИзменении", "Подключаемый_Использование"+ ИмяРеквизита +"ПриИзменении");
	
	Значение = Элементы.Добавить(ИмяРеквизита + Сч + "Значение", Тип("ПолеФормы"), Элементы.ПоказателиПраваяКолонка);
	Значение.Вид = ВидПоляФормы.ПолеВвода;
	Значение.ПутьКДанным = ИмяРеквизита + Сч + "Значение";
	Значение.РастягиватьПоГоризонтали = Ложь;
	Значение.Ширина = 16;
	Значение.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	Если РасширенноеРедактирование Тогда
		ТипПоказателя = Новый ОписаниеТипов(ТипПоказателя, "Число",,Новый КвалификаторыЧисла(15, 4));
	КонецЕсли;
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, ИмяРеквизита + Сч + "Значение", "ОграничениеТипа", ТипПоказателя);
		
	Если РасширенноеРедактирование Тогда
		Действие = Элементы.Добавить(ИмяРеквизита + Сч + "Действие", Тип("ПолеФормы"), Элементы.ПоказателиСредняяКолонка);
		Действие.Вид = ВидПоляФормы.ПолеВвода;
		Действие.ПутьКДанным = ИмяРеквизита + Сч + "Действие";
		Действие.РастягиватьПоГоризонтали = Ложь;
		Действие.Ширина = 16;
		Действие.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		Действие.РежимВыбораИзСписка = Истина;
		Действие.РедактированиеТекста = Ложь;
		ЗаполнитьСписокДействийСПоказателями(Действие.СписокВыбора);
		ЭтаФорма[ИмяРеквизита + Сч + "Действие"] = Действие.СписокВыбора[0].Значение;
		Действие.УстановитьДействие("Очистка", "Подключаемый_"+ ИмяРеквизита + "Действие");
		
		Округление = Элементы.Добавить(ИмяРеквизита + Сч + "Округление", Тип("ПолеФормы"), Элементы.ПоказателиОкругление);
		Округление.Вид = ВидПоляФормы.ПолеВвода;
		Округление.ПутьКДанным = ИмяРеквизита + Сч + "Округление";
		Округление.РастягиватьПоГоризонтали = Ложь;
		Округление.Ширина = 16;
		Округление.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		Если ЭтоПоказатель Тогда
			ЭтаФорма[ИмяРеквизита + Сч + "Округление"] = Справочники.ПоказателиРасчетаЗарплаты.ОкруглениеПоУмолчаниюДляПоказателя(Показатель)
		Иначе
			ЭтаФорма[ИмяРеквизита + Сч + "Округление"] = Справочники.СпособыОкругленияПриРасчетеЗарплаты.ПоУмолчанию();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоказатели(МассивПоказателей)
	
	Сч = 1;
	Для Каждого Показатель Из МассивПоказателей Цикл 
		ЭтотОбъект["Показатель" + Сч] = Показатель;
		Сч = Сч + 1;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьЗначенийПоказателей(Форма)
	
	Для Сч = 1 По Форма.КоличествоПоказателей Цикл
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, 
			"Показатель" + Сч + "Значение", "Доступность", Форма["Показатель" + Сч + "Использование"]);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, 
			"Показатель" + Сч + "Действие", "Доступность", Форма["Показатель" + Сч + "Использование"]);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, 
			"Показатель" + Сч + "Округление", "Доступность", Форма["Показатель" + Сч + "Использование"]);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНачисления(МассивНачислений)
	
	Сч = 1;
	Для Каждого Начисление Из МассивНачислений Цикл 
		ЭтотОбъект["Начисление" + Сч] = Начисление;
		Сч = Сч + 1;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьЗначенийНачислений(Форма)
	
	Для Сч = 1 По Форма.КоличествоНачислений Цикл
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, 
			"Начисление" + Сч + "Значение", "Доступность", Форма["Начисление" + Сч + "Использование"]);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, 
			"Начисление" + Сч + "Действие", "Доступность", Форма["Начисление" + Сч + "Использование"]);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, 
			"Начисление" + Сч + "Округление", "Доступность", Форма["Начисление" + Сч + "Использование"]);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокДействийСПоказателями(СписокДействий)
	
	СписокДействий.Добавить("ФиксЗначение", "Фикс. значение");
	СписокДействий.Добавить("Прибавить", "Прибавить");
	СписокДействий.Добавить("Умножить", "Умножить на");
	
КонецПроцедуры

#КонецОбласти
