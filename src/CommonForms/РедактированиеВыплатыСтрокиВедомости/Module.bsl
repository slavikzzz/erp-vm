#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПолучитьПараметры();
	
	ВидыДоходовИсполнительногоПроизводстваКлиентСервер.НастроитьКолонкуВидДоходаДляВыплатыЗарплаты(
		ЭтотОбъект,
		"Выплаты",
		"ВыплатыВидДохода",
		"Выплаты.ВидДоходаИсполнительногоПроизводства",
		ДатаВыплаты,
		Истина);
	
	Заголовок = СтрШаблон(НСтр("ru = '%1 : выплаты';
								|en = '%1 : payments'"), Строка(ФизическоеЛицо));
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("ВыбратьИЗакрыть", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповещение, Отказ, ЗавершениеРаботы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЗарплата

&НаКлиенте
Процедура ВыплатыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока И Не Копирование Тогда
		ЗаполнитьЗначенияСвойств(Элемент.ТекущиеДанные, ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ВыбратьИЗакрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПолучитьПараметры()
	
	Параметры.Свойство("ИдентификаторСтроки",	ИдентификаторСтроки);
	Выплаты.Загрузить(ПолучитьИзВременногоХранилища(Параметры.АдресВХранилищеВыплатыПоСтроке));
	
	Параметры.Свойство("ФизическоеЛицо",                       ФизическоеЛицо);
	Параметры.Свойство("Организация",                          Организация);
	Параметры.Свойство("ПериодРегистрации",                    ПериодРегистрации);
	Параметры.Свойство("СтатьяФинансирования",                 СтатьяФинансирования);
	Параметры.Свойство("СтатьяРасходов",                       СтатьяРасходов);
	Параметры.Свойство("ВидДоходаИсполнительногоПроизводства", ВидДоходаИсполнительногоПроизводства);
	
КонецПроцедуры


&НаКлиенте
Процедура ВыбратьИЗакрыть(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ПроверитьЗаполнение() Тогда
		
		РезультатыРедактирования = Новый Структура;
		РезультатыРедактирования.Вставить("Модифицированность", Модифицированность);
		РезультатыРедактирования.Вставить("ИдентификаторСтроки", ИдентификаторСтроки);
		РезультатыРедактирования.Вставить("АдресВХранилищеВыплатыПоСтроке", АдресВХранилищеВыплатыПоСтроке());
		
		Модифицированность = Ложь;
		Закрыть(РезультатыРедактирования)
		
	КонецЕсли
	
КонецПроцедуры

&НаСервере
Функция АдресВХранилищеВыплатыПоСтроке()
	
	ВыплатыФизлицу = Выплаты.Выгрузить();
	ВыплатыФизлицу.ЗаполнитьЗначения(ИдентификаторСтроки, "ИдентификаторСтроки");
	
	Возврат ПоместитьВоВременноеХранилище(ВыплатыФизлицу, УникальныйИдентификатор);
	
КонецФункции	

#КонецОбласти
