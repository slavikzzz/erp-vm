
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Параметры.Свойство("СписокПредметов") Тогда
		ВызватьИсключение НСтр("ru = 'Не заполнен список физических лиц, для которых необходимо установить напоминание.';
								|en = 'List of individuals for whom a reminder should be set is not filled in.'");
		Возврат;
	КонецЕсли; 
	
	ПриПолученииДанныхНаСервере(Параметры.СписокПредметов);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НапомнитьОДнеРожденияПриИзменении(Элемент)
	УстановитьДоступностьКоличествоДней(ЭтаФорма);
	УстановитьВидимостьДекорацияКолокольчик(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура КоличествоДнейПриИзменении(Элемент)
	УстановитьЗаголовокКоличествоДней(ЭтаФорма);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	СоздатьУдалитьНапоминания();
	ПоказатьОповещениеОбОшибочных();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриПолученииДанныхНаСервере(СписокПредметов)

	ЗаполнитьПредметыНапоминаний(СписокПредметов);
	
	Если ПредметыНапоминаний.Количество() = 0 Тогда
		ВызватьИсключение НСтр("ru = 'Нет сотрудников с заполненными датами рождения - напоминания не могут быть установлены.';
								|en = 'No employees with populated birth dates. Reminders cannot be set.'");
	КонецЕсли;
	
	УстановитьРеквизитыНапоминанияОДнеРождения();
	
	УстановитьСвойстваЭлементовФормы();

КонецПроцедуры

&НаСервере
Процедура УстановитьРеквизитыНапоминанияОДнеРождения()
	
	Если ПредметыНапоминаний.Количество() = 0 Тогда
		НапомнитьОДнеРождения = Истина;
		Возврат;
	КонецЕсли;
	
	ФизическоеЛицо = ПредметыНапоминаний[0].ФизическоеЛицо;
	МассивКлючейРегистра = ПолучитьНапоминанияНаСервере(ФизическоеЛицо);
	
	НапомнитьОДнеРождения = (МассивКлючейРегистра.Количество() > 0);
	УстановитьКоличествоДнейНапоминания(?(МассивКлючейРегистра.Количество() > 0, МассивКлючейРегистра[0], Неопределено));
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваЭлементовФормы()
	
	УстановитьНадписьШапки();
	
	УстановитьЗаголовокКоличествоДней(ЭтаФорма);
	
	УстановитьВидимостьДекорацияКолокольчик(ЭтаФорма);
	УстановитьДоступностьКоличествоДней(ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьДекорацияКолокольчик(Форма)

	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ДекорацияКолокольчикВкл",
		"Видимость",
		Форма.НапомнитьОДнеРождения);

	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ДекорацияКолокольчикВыкл",
		"Видимость",
		Не Форма.НапомнитьОДнеРождения);
		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьКоличествоДней(Форма)

	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"КоличествоДней",
		"Доступность",
		Форма.НапомнитьОДнеРождения);

КонецПроцедуры

&НаСервере
Процедура УстановитьНадписьШапки()

	Если ПредметыНапоминаний.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ПредметыНапоминаний.Количество() = 1 Тогда
		ФамилияИнициалы = ПредметыНапоминаний[0].ФамилияИОВРодительномПадеже;
		НадписьШапка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'У %1 день рождения %2.';
				|en = '%1 has a birthday %2.'"),
				ФамилияИнициалы,
				Формат(ПредметыНапоминаний[0].ДатаРождения, "ДФ = ""д ММММ"""));
	Иначе
		НадписьШапка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'У %1 дни рождения.';
				|en = '%1 have birthdays.'"),
			СтрокаФизическихЛицИДнейРождений());
	КонецЕсли;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗаголовокКоличествоДней(Форма)

	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"КоличествоДней",
		"Заголовок",
		СтроковыеФункцииКлиентСервер.ФормаМножественногоЧисла("день", "дня", "дней", Форма.КоличествоДней));

КонецПроцедуры

&НаКлиенте
Процедура СоздатьУдалитьНапоминания()
	
	// В любом случае очищаем уже сделанные напоминания.
	УдалитьНапоминания();
	
	Если НапомнитьОДнеРождения Тогда
		СоздатьНапоминания();
		СохранитьНастройкуНаСервере();
	КонецЕсли;
	
	ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.НапоминанияПользователя"));
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНапоминания()
	
	Для Каждого ПредметНапоминания Из ПредметыНапоминаний Цикл
		ТекстНапоминания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 у %2 день рождения.';
				|en = '%2 has a birthday on %1.'"),
				Формат(ПредметНапоминания.ДатаРождения, "ДФ = ""д ММММ"""),
				ПредметНапоминания.ФИОВРодительномПадеже);
		НапоминанияПользователяКлиент.НапомнитьОЕжегодномСобытииПредмета(
			ТекстНапоминания,
			КоличествоДней * ЗарплатаКадрыКлиентСервер.ДлительностьСутокВСекундах(),
			ПредметНапоминания.ФизическоеЛицо,
			"ДатаРождения");
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьНапоминания()

	МассивКлючейРегистра = ПолучитьНапоминанияНаСервере();
	УдалитьНапоминанияНаСервере(МассивКлючейРегистра);
	
	Для каждого КлючЗаписиРегистра Из МассивКлючейРегистра Цикл
		НапоминанияПользователяКлиент.УдалитьЗаписьИзКэшаОповещений(КлючЗаписиРегистра);
		Оповестить("Запись_НапоминанияПользователя", Новый Структура, КлючЗаписиРегистра);
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьОповещениеОбОшибочных()
	
	Если ОшибочныеПредметыНапоминаний.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ОшибочныеПредметыНапоминаний.Количество() = 1 Тогда
		ТекстОповещения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось создать напоминание для %1';
				|en = 'Cannot create reminder for %1'"),
			ОшибочныеПредметыНапоминаний[0].ФИОВРодительномПадеже);
	Иначе
		ТекстОповещения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось создать напоминания для %1-х сотрудников';
				|en = 'Cannot create reminders for %1 employees'"),
			ОшибочныеПредметыНапоминаний.Количество());
	КонецЕсли;
	
	ПоказатьОповещениеПользователя(НСтр("ru = 'Не заполнена дата рождения.';
										|en = 'Birth date is required.'"),, ТекстОповещения,БиблиотекаКартинок.Ошибка32);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьНапоминанияНаСервере(ФизическоеЛицо = Неопределено)

	Если ФизическоеЛицо = Неопределено Тогда
		МассивПредметовНапоминаний = ПредметыНапоминаний.Выгрузить(,"ФизическоеЛицо").ВыгрузитьКолонку("ФизическоеЛицо");
	Иначе
		МассивПредметовНапоминаний = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФизическоеЛицо);
	КонецЕсли;
	
	Возврат КадровыйУчетРасширенный.НапоминанияОДняхРождения(МассивПредметовНапоминаний);
	
КонецФункции

&НаСервереБезКонтекста
Процедура УдалитьНапоминанияНаСервере(МассивКлючейРегистра)
	
	Для каждого КлючРегистраНапоминаний Из МассивКлючейРегистра Цикл
		НапоминанияПользователяСлужебный.ОтключитьНапоминание(КлючРегистраНапоминаний, Ложь);
	КонецЦикла; 
	
КонецПроцедуры

&НаСервере
Процедура УстановитьКоличествоДнейНапоминания(КлючРегистра)

	КоличествоДнейНапоминания = Неопределено;
	
	Если КлючРегистра <> Неопределено Тогда
		КоличествоДнейНапоминания = КоличествоДнейНапоминания(КлючРегистра);
	КонецЕсли;
	
	Если КоличествоДнейНапоминания = Неопределено Тогда
		КоличествоДней = ПоследнееУстановленноеКоличествоДней();
	Иначе
		КоличествоДней = КоличествоДнейНапоминания;
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция КоличествоДнейНапоминания(КлючРегистра)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НапоминанияПользователя.ИнтервалВремениНапоминания КАК ИнтервалВремениНапоминания
		|ИЗ
		|	РегистрСведений.НапоминанияПользователя КАК НапоминанияПользователя
		|ГДЕ
		|	НапоминанияПользователя.Пользователь = &Пользователь
		|	И НапоминанияПользователя.ВремяСобытия = &ВремяСобытия
		|	И НапоминанияПользователя.Источник = &Источник";
	
	Запрос.УстановитьПараметр("ВремяСобытия", КлючРегистра.ВремяСобытия);
	Запрос.УстановитьПараметр("Источник", КлючРегистра.Источник);
	Запрос.УстановитьПараметр("Пользователь", КлючРегистра.Пользователь);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.ИнтервалВремениНапоминания/86400;

КонецФункции

&НаСервереБезКонтекста
Функция ПоследнееУстановленноеКоличествоДней()
	Возврат ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("НастройкиПользователя", "ПоследнееУстановленноеКоличествоДней", 5);
КонецФункции

&НаСервере
Процедура ЗаполнитьПредметыНапоминаний(АдресПредметовНапоминаний)

	СписокПредметов = ПолучитьИзВременногоХранилища(АдресПредметовНапоминаний);
	Если СписокПредметов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьПредметыНапоминанийПоФизическимЛицам(СписокПредметов);
	ЗаполнитьПредметыНапоминанийПоСотрудникам(СписокПредметов);
	
	ОтделитьОшибочноЗаполненныеПредметы();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПредметыНапоминанийПоФизическимЛицам(СписокПредметов)
	
	Если ТипЗнч(СписокПредметов[0]) <> Тип("СправочникСсылка.ФизическиеЛица") Тогда
		Возврат;
	КонецЕсли;
	
	КадровыеДанные = КадровыйУчет.КадровыеДанныеФизическихЛиц(Истина, СписокПредметов, "ФИО, ФамилияИО, Пол, ДатаРождения");
	
	Для Каждого СтрокаДанных Из КадровыеДанные Цикл
		ПредметНапоминания = ПредметыНапоминаний.Добавить();
		ПредметНапоминания.ФизическоеЛицо = СтрокаДанных.ФизическоеЛицо;
		ПредметНапоминания.ДатаРождения = СтрокаДанных.ДатаРождения;
		ФизическиеЛицаЗарплатаКадры.Просклонять(СтрокаДанных.ФИО, 2, ПредметНапоминания.ФИОВРодительномПадеже, СтрокаДанных.Пол, СтрокаДанных.ФизическоеЛицо);
		ФизическиеЛицаЗарплатаКадры.Просклонять(СтрокаДанных.ФамилияИО, 2, ПредметНапоминания.ФамилияИОВРодительномПадеже, СтрокаДанных.Пол, СтрокаДанных.ФизическоеЛицо);
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПредметыНапоминанийПоСотрудникам(СписокПредметов)
	
	Если ТипЗнч(СписокПредметов[0]) <> Тип("СправочникСсылка.Сотрудники") Тогда
		Возврат;
	КонецЕсли;
	
	КадровыеДанные = КадровыйУчет.КадровыеДанныеСотрудников(Истина, СписокПредметов, "ФИО, ФамилияИО, Пол, ДатаРождения");
	
	Для Каждого СтрокаДанных Из КадровыеДанные Цикл
		ПредметНапоминания = ПредметыНапоминаний.Добавить();
		ПредметНапоминания.ФизическоеЛицо = СтрокаДанных.ФизическоеЛицо;
		ПредметНапоминания.ДатаРождения = СтрокаДанных.ДатаРождения;
		ФизическиеЛицаЗарплатаКадры.Просклонять(СтрокаДанных.ФИО, 2, ПредметНапоминания.ФИОВРодительномПадеже, СтрокаДанных.Пол, СтрокаДанных.ФизическоеЛицо);
		ФизическиеЛицаЗарплатаКадры.Просклонять(СтрокаДанных.ФамилияИО, 2, ПредметНапоминания.ФамилияИОВРодительномПадеже, СтрокаДанных.Пол, СтрокаДанных.ФизическоеЛицо);
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ОтделитьОшибочноЗаполненныеПредметы()

	МассивОшибочныхСтрок = Новый Массив;
	
	Для каждого ПредметНапоминания Из ПредметыНапоминаний Цикл
		Если Не ЗначениеЗаполнено(ПредметНапоминания.ДатаРождения) Тогда
			МассивОшибочныхСтрок.Добавить(ПредметНапоминания);
		КонецЕсли;
	КонецЦикла;
	
	Для каждого ОшибочнаяСтрока Из МассивОшибочныхСтрок Цикл
		ПредметыНапоминаний.Удалить(ОшибочнаяСтрока);
		ЗаполнитьЗначенияСвойств(ОшибочныеПредметыНапоминаний.Добавить(), ОшибочнаяСтрока);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкуНаСервере()
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("НастройкиПользователя", "ПоследнееУстановленноеКоличествоДней", КоличествоДней);
КонецПроцедуры

&НаСервере
Функция СтрокаФизическихЛицИДнейРождений()

	ИтоговаяСтрока = "";
	
	КоличествоВыводимыхСотрудников = ?(ПредметыНапоминаний.Количество() > 3, 2, ПредметыНапоминаний.Количество());
	Для Сч = 0 По КоличествоВыводимыхСотрудников - 1 Цикл
		Если Сч = 1 Или (Сч = 2 И КоличествоВыводимыхСотрудников = 3) Тогда
			ИтоговаяСтрока = ИтоговаяСтрока + ", ";
		КонецЕсли;
		ФамилияИнициалы = ПредметыНапоминаний[Сч].ФамилияИОВРодительномПадеже;
		ИтоговаяСтрока = ИтоговаяСтрока + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 (%2)';
				|en = '%1 (%2)'"),
			ФамилияИнициалы,
			Формат(ПредметыНапоминаний[Сч].ДатаРождения, "ДФ = ""д ММММ"""));
	КонецЦикла;
	
	Если ПредметыНапоминаний.Количество() > 3 Тогда
		ИтоговаяСтрока = ИтоговаяСтрока + " " + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'и еще у %1 сотрудников';
				|en = 'and other %1 employees'"), ПредметыНапоминаний.Количество() - 2);
	КонецЕсли;
	
	Возврат ИтоговаяСтрока;
	
КонецФункции

#КонецОбласти