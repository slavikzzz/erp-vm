#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	СписокСвойств = ГруппыЗатратКлиентСервер.ПоляГруппЗатрат();
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры, СписокСвойств);
	
	Если ЗначениеЗаполнено(ГруппаЗатрат) Тогда
		Заголовок = ГруппаЗатрат;
	Иначе
		Заголовок = ГруппыЗатратСервер.ПредставлениеГруппыЗатрат(Параметры);
	КонецЕсли;
	
	ТолькоПросмотрЭлементов = Не ПравоДоступа("Изменение", Метаданные.Справочники.ПартииПроизводства);
	
	СписокПолей = ГруппыЗатратСервер.ПереченьПолейПоГруппировкеЗатрат(Документ, Параметры.ГруппировкаЗатрат);
	ТекстНеИспользуется = НСтр("ru = '<не используется>';
								|en = '<not used>'");
	
	МассивЭлементов = Новый Массив;
	МассивЭлементов.Добавить("ГруппировкаЗатрат");
	МассивЭлементов.Добавить("Номенклатура");
	МассивЭлементов.Добавить("Характеристика");
	МассивЭлементов.Добавить("Серия");
	МассивЭлементов.Добавить("Спецификация");
	МассивЭлементов.Добавить("Назначение");
	
	Для Каждого ТекЭлемент Из МассивЭлементов Цикл
		Элементы[ТекЭлемент].ТолькоПросмотр = ТолькоПросмотрЭлементов;
		Если СписокПолей.НайтиПоЗначению(ТекЭлемент) = Неопределено Тогда
			Элементы[ТекЭлемент].ПодсказкаВвода = ТекстНеИспользуется;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти