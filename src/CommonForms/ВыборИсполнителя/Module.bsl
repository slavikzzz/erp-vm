
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Параметры.Свойство("Подразделение", ПодразделениеИсходное);
	Параметры.Свойство("Организация",   Организация);
	Параметры.Свойство("Исполнитель",   ИсполнительИсходный);
	
	Дата = Дата(1,1,1);
	Параметры.Свойство("Дата", Дата);
	
	ТолькоРаботники = Ложь;
	Параметры.Свойство("ТолькоРаботники", ТолькоРаботники);
	
	Если НЕ ЗначениеЗаполнено(Дата) Тогда
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	РежимВыбораПоДокументу = Неопределено;
	Если ЗначениеЗаполнено(ИсполнительИсходный) Тогда
		Если ТипЗнч(ИсполнительИсходный) = Тип("СправочникСсылка.Бригады") И НЕ ТолькоРаботники Тогда
			Элементы.Бригады.ТекущаяСтрока = ИсполнительИсходный;
			РежимВыбораПоДокументу = "Бригады";
			ИспользоватьБригадныеНаряды = Истина;
		Иначе
			Элементы.Работники.ТекущаяСтрока = ИсполнительИсходный;
			РежимВыбораПоДокументу = "Работники";
			ИспользоватьПерсональныеНаряды = Истина;
		КонецЕсли;
	КонецЕсли;
	
	РежимВыбораПоПодразделению = Неопределено;
	Если ЗначениеЗаполнено(ПодразделениеИсходное) Тогда
		
		ПараметрыПодразделения = ПроизводствоСервер.ПараметрыПроизводственногоПодразделения(ПодразделениеИсходное);
		
		Если ПараметрыПодразделения.ИспользоватьБригадныеНаряды И НЕ ТолькоРаботники Тогда
			РежимВыбораПоПодразделению = "Бригады";
			ИспользоватьБригадныеНаряды = Истина;
		КонецЕсли;
		
		Если ПараметрыПодразделения.ИспользоватьПерсональныеНаряды Тогда
			РежимВыбораПоПодразделению = "Работники";
			ИспользоватьПерсональныеНаряды = Истина;
		КонецЕсли;
		
	Иначе
		ИспользоватьБригадныеНаряды =  НЕ ТолькоРаботники;
		ИспользоватьПерсональныеНаряды = Истина;
		РежимВыбораПоПодразделению = "Работники";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РежимВыбораПоДокументу) Тогда
		РежимВыбора = РежимВыбораПоДокументу;
	Иначе
		РежимВыбора = РежимВыбораПоПодразделению;
	КонецЕсли;
	
	Подразделение = ПодразделениеИсходное;
	
	ИспользоватьНачислениеЗарплаты = ПолучитьФункциональнуюОпцию("ИспользоватьНачислениеЗарплаты");
	
	Элементы.ГруппаТолькоРаботающие.Видимость = ИспользоватьНачислениеЗарплаты;
	Элементы.ГруппаОрганизация.Видимость = Ложь;
	
	//++ Локализация
	Если ПроизводствоСервер.ИспользуетсяУчетТрудозатратВРазрезеСотрудников(Дата) Тогда
		УстановитьОтборПоОрганизации();
	КонецЕсли;
	//-- Локализация
	
	ОперативныйУчетПроизводстваЛокализация.ДобавитьДанныеДляПодбораРаботников(ЭтотОбъект, Дата);
	
	УстановитьОтборПоПодразделению();
	
	УстановитьВидимость(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	//++ Локализация
	Дата = Дата(1,1,1);
	Параметры.Свойство("Дата", Дата);
	
	Если НЕ ЗначениеЗаполнено(Дата) Тогда
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Если ИспользоватьНачислениеЗарплаты ИЛИ ПроизводствоСервер.ИспользуетсяУчетТрудозатратВРазрезеСотрудников(Дата) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Работники, "ТолькоРаботающие", ТолькоРаботающие);
	КонецЕсли;
	//-- Локализация
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПодразделениеПриИзменении(Элемент)
	
	УстановитьОтборПоПодразделению();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	УстановитьОтборПоОрганизации();
КонецПроцедуры

&НаКлиенте
Процедура РежимВыбораПриИзменении(Элемент)
	УстановитьВидимость(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ТолькоРаботающиеПриИзменении(Элемент)
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Работники, "ТолькоРаботающие", ТолькоРаботающие);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыБригады

&НаКлиенте
Процедура БригадыВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	ОповеститьОВыборе(Элементы.Бригады.ТекущаяСтрока);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыРаботники

&НаКлиенте
Процедура РаботникиВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	Результат = Элементы.Работники.ТекущаяСтрока;
	
	//++ Локализация
	Если ТипЗнч(Результат) = Тип("РегистрСведенийКлючЗаписи.ДанныеДляПодбораСотрудников") Тогда
		Результат = СотрудникПоКлючуЗаписи(Результат);
	КонецЕсли;
	//-- Локализация
	
	ОповеститьОВыборе(Результат);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимость(Форма)
	
	Форма.Элементы.РежимВыбора.Видимость = Форма.ИспользоватьБригадныеНаряды И Форма.ИспользоватьПерсональныеНаряды;
	
	Если Форма.РежимВыбора = "Бригады" Тогда
		Форма.Элементы.ГруппаСтраницы.ТекущаяСтраница = Форма.Элементы.ГруппаБригады;
		Форма.Элементы.БригадыВыбрать.КнопкаПоУмолчанию = Истина;
		Форма.Элементы.ТолькоРаботающие.Видимость = Ложь;
		Форма.Элементы.Организация.Видимость = Ложь;
	Иначе
		Форма.Элементы.ГруппаСтраницы.ТекущаяСтраница = Форма.Элементы.ГруппаРаботники;
		Форма.Элементы.РаботникиВыбрать.КнопкаПоУмолчанию = Истина;
		Форма.Элементы.ТолькоРаботающие.Видимость = Истина;
		Форма.Элементы.Организация.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоПодразделению()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Бригады, 
		"Подразделение", 
		Подразделение, 
		ВидСравненияКомпоновкиДанных.Равно,
		, // Представление - автоматически
		ЗначениеЗаполнено(Подразделение));
	
	Если (ИспользоватьНачислениеЗарплаты
		ИЛИ ПроизводствоСервер.ИспользуетсяУчетТрудозатратВРазрезеСотрудников(ТекущаяДатаСеанса()))
		И ИспользоватьПерсональныеНаряды Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Работники, 
			"Подразделение", 
			Подразделение,
			ВидСравненияКомпоновкиДанных.Равно,
			, // Представление - автоматически
			ЗначениеЗаполнено(Подразделение));
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоОрганизации()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Бригады, 
		"Организация", 
		Организация, 
		ВидСравненияКомпоновкиДанных.Равно,
		, // Представление - автоматически
		ЗначениеЗаполнено(Организация));
	
	Если ПроизводствоСервер.ИспользуетсяУчетТрудозатратВРазрезеСотрудников(ТекущаяДатаСеанса())
		И ИспользоватьПерсональныеНаряды Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Работники, 
			"Организация", 
			Организация,
			ВидСравненияКомпоновкиДанных.Равно,
			, // Представление - автоматически
			ЗначениеЗаполнено(Организация));
		
	КонецЕсли;
	
КонецПроцедуры

//++ Локализация
&НаСервереБезКонтекста
Функция СотрудникПоКлючуЗаписи(Результат)
	Возврат Результат.Сотрудник;
КонецФункции
//-- Локализация

#КонецОбласти
