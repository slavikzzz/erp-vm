
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НастройкиSmartway = БронированиеКомандировок.НовыеНастройкиСистемыБронирования();
	БронированиеКомандировок.ЗаполнитьНастройкиСистемыБронирования(НастройкиSmartway, Перечисления.СистемыБронированияКомандировок.Smartway);
	
	ЛогинSmartway = НастройкиSmartway.Логин;
	Если ЗначениеЗаполнено(НастройкиSmartway.Пароль) Тогда
		ПарольSmartway = УникальныйИдентификатор;
	КонецЕсли;
	
	НастройкиSmartwayИзменены = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЛоготипSmartwayНажатие(Элемент)
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("https://smartway.today");
КонецПроцедуры

&НаКлиенте
Процедура НастройкиSmartwayПриИзменении(Элемент)
	НастройкиSmartwayИзменены = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Отказ = Ложь;
	ПроверитьНастройкиSmartway(Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	СохранитьИзмененияНаСервере();
	
	Оповестить("СохранениеНастроекДоступаКСервисамБронированияКомандировок", , ВладелецФормы);
	
	ЗаголовокОповещения = НСтр("ru = 'Бронирование командировок';
								|en = 'Book business trips'");
	ПояснениеОповещения = НСтр("ru = 'Настройки доступа к сервисам бронирования командировок сохранены.';
								|en = 'Settings of access to business trip booking services are saved.'");
	ПоказатьОповещениеПользователя(ЗаголовокОповещения, , ПояснениеОповещения, БиблиотекаКартинок.Информация);
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СохранитьИзмененияНаСервере()
	
	Если Не НастройкиSmartwayИзменены Тогда
		Возврат;
	КонецЕсли;
		
	НовыеНастройки = БронированиеКомандировок.НовыеНастройкиСистемыБронирования();
	НовыеНастройки.Логин = ЛогинSmartway;
	НовыеНастройки.Пароль = ПарольSmartway;
	
	БронированиеКомандировок.СохранитьНастройкиСистемыБронирования(НовыеНастройки, Перечисления.СистемыБронированияКомандировок.Smartway);
	
	НастройкиSmartwayИзменены = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьНастройкиSmartway(Отказ)
	
	Если ЗначениеЗаполнено(ЛогинSmartway) И Не ЗначениеЗаполнено(ПарольSmartway) Тогда
		ТекстСообщения = НСтр("ru = 'Пароль Smartway не заполнен.';
								|en = 'Smartway password is not filled in.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , , "ПарольSmartway", Отказ);
	КонецЕсли;
		
	Если ЗначениеЗаполнено(ПарольSmartway) И Не ЗначениеЗаполнено(ЛогинSmartway) Тогда
		ТекстСообщения = НСтр("ru = 'Логин Smartway не заполнен.';
								|en = 'Smartway username is not filled in.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , , "ЛогинSmartway", Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
