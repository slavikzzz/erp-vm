#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	Элементы.ГруппыЗатратГруппаЗатрат.Заголовок =
		ПереработкаНаСторонеКлиентСервер.ЗаголовокПоляГруппыЗатрат2_5(Параметры.ПоляГруппыЗатрат);
	
	ГруппыЗатрат.Загрузить(ПолучитьИзВременногоХранилища(Параметры.АдресХранилищаГруппЗатрат));
	
	Для Каждого СтрокаГруппа Из ГруппыЗатрат Цикл
		
		Если Параметры.НомерГруппыЗатрат = СтрокаГруппа.НомерГруппыЗатрат Тогда
			Элементы.ГруппыЗатрат.ТекущаяСтрока = СтрокаГруппа.ПолучитьИдентификатор();
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

&НаКлиенте
Процедура СписокГруппВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОбработатьВыборГруппы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьГруппу(Команда)
	
	ОбработатьВыборГруппы();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработатьВыборГруппы()

	ТекущиеДанные = Элементы.ГруппыЗатрат.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыВыбора = Новый Структура;
	ПараметрыВыбора.Вставить("ГруппаЗатрат", ТекущиеДанные.ГруппаЗатрат);
	ПараметрыВыбора.Вставить("НомерГруппыЗатрат", ТекущиеДанные.НомерГруппыЗатрат);
	ПараметрыВыбора.Вставить("Спецификация", ТекущиеДанные.Спецификация);
	ПараметрыВыбора.Вставить("НазначениеОбеспечения", ТекущиеДанные.НазначениеОбеспечения);
	ПараметрыВыбора.Вставить("ЕстьИмпортноеИзделие", ТекущиеДанные.ЕстьИмпортноеИзделие);
	//++ НЕ УТКА
	ПараметрыВыбора.Вставить("Распоряжение", ТекущиеДанные.Распоряжение);
	ПараметрыВыбора.Вставить("Этап", ТекущиеДанные.Этап);
	ПараметрыВыбора.Вставить("Заказ", ТекущиеДанные.Заказ);
	ПараметрыВыбора.Вставить("СпособРаспределенияЗатратНаВыходныеИзделия", ТекущиеДанные.СпособРаспределенияЗатратНаВыходныеИзделия);
	ПараметрыВыбора.Вставить("ПараметрыЗаполненияНазначений", ТекущиеДанные.ПараметрыЗаполненияНазначений);
	ПараметрыВыбора.Вставить("СписокВыбораНазначений", ТекущиеДанные.СписокВыбораНазначений);
	ПараметрыВыбора.Вставить("ПроизводствоИзДавальческогоСырья", ТекущиеДанные.ПроизводствоИзДавальческогоСырья);
	//-- НЕ УТКА
	
	Закрыть(ПараметрыВыбора);
	
КонецПроцедуры

#КонецОбласти
