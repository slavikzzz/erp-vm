
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;

	ЗначенияЗаполнения = Параметры.ЗначенияЗаполнения;
	
	Дата = ?(ЗначенияЗаполнения.Дата <> '000101010000', ЗначенияЗаполнения.Дата, ТекущаяДатаСеанса());
	НалоговаяЛьгота = ЗначенияЗаполнения.НалоговаяЛьгота;
	ЛьготнаяСтавка = ЗначенияЗаполнения.ЛьготнаяСтавка;
	ПроцентУменьшения = ЗначенияЗаполнения.ПроцентУменьшения;
	СуммаУменьшения = ЗначенияЗаполнения.СуммаУменьшения;

	Если НалоговаяЛьгота = ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.ОсвобождениеОтНалогообложения") Тогда
		
		КодНалоговойЛьготыОсвобождениеОтНалогообложения = ЗначенияЗаполнения.КодНалоговойЛьготы;
		РегиональныйКодЛьготыОсвобождениеОтНалогообложения = ЗначенияЗаполнения.РегиональныйКодЛьготы;
		
	ИначеЕсли НалоговаяЛьгота = ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.СнижениеНалоговойСтавки") Тогда
		
		КодНалоговойЛьготыСнижениеНалоговойСтавки = ЗначенияЗаполнения.КодНалоговойЛьготы;
		РегиональныйКодЛьготыСнижениеНалоговойСтавки = ЗначенияЗаполнения.РегиональныйКодЛьготы;
		
	ИначеЕсли НалоговаяЛьгота = ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаВПроцентах") Тогда
		
		КодНалоговойЛьготыУменьшениеСуммыНалогаВПроцентах = ЗначенияЗаполнения.КодНалоговойЛьготы;
		РегиональныйКодЛьготыУменьшениеСуммыНалогаВПроцентах = ЗначенияЗаполнения.РегиональныйКодЛьготы;
		
	ИначеЕсли НалоговаяЛьгота = ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаНаСумму") Тогда
		
		КодНалоговойЛьготыУменьшениеСуммыНалогаНаСумму = ЗначенияЗаполнения.КодНалоговойЛьготы;
		
	КонецЕсли;

	НастроитьЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?';
							|en = 'The data has changed. Do you want to save the changes?'");
		
		ОповещениеОЗакрытии = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект, Отказ);
		
		ПоказатьВопрос(ОповещениеОЗакрытии, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	Если НалоговаяЛьгота <> Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.СнижениеНалоговойСтавки Тогда
		НепроверяемыеРеквизиты.Добавить("ЛьготнаяСтавка");
	КонецЕсли;
	Если НалоговаяЛьгота <> Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаВПроцентах Тогда
		НепроверяемыеРеквизиты.Добавить("ПроцентУменьшения");
	КонецЕсли;
	Если НалоговаяЛьгота <> Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаНаСумму Тогда
		НепроверяемыеРеквизиты.Добавить("СуммаУменьшения");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НалоговаяЛьготаПриИзменении(Элемент)
	
	// Сохраним значения
	Если НалоговаяЛьгота = ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.ОсвобождениеОтНалогообложения") Тогда
		
		КодНалоговойЛьготыОсвобождениеОтНалогообложения = "";
		РегиональныйКодЛьготыОсвобождениеОтНалогообложения = "";
			
	ИначеЕсли НалоговаяЛьгота = ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.СнижениеНалоговойСтавки") Тогда
		
		КодНалоговойЛьготыСнижениеНалоговойСтавки = "";
		РегиональныйКодЛьготыСнижениеНалоговойСтавки = "";
		ЛьготнаяСтавка = 0;
		
	ИначеЕсли НалоговаяЛьгота = ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаВПроцентах") Тогда
		
		КодНалоговойЛьготыУменьшениеСуммыНалогаВПроцентах = "";
		РегиональныйКодЛьготыУменьшениеСуммыНалогаВПроцентах = "";
		ПроцентУменьшения = 0;
		
	ИначеЕсли НалоговаяЛьгота = ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаНаСумму") Тогда
		
		КодНалоговойЛьготыУменьшениеСуммыНалогаНаСумму = "";
		СуммаУменьшения = 0;
		
	КонецЕсли;
	
	НастроитьЭлементыФормы(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура КодЛьготыОсвобождениеОтНалогообложенияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	ВыборКода("КодНалоговойЛьготыОсвобождениеОтНалогообложения");
	
КонецПроцедуры

&НаКлиенте
Процедура КодЛьготыУменьшениеСуммыНалогаНаСуммуНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	ВыборКода("КодНалоговойЛьготыУменьшениеСуммыНалогаНаСумму");

КонецПроцедуры

&НаКлиенте
Процедура РегиональныйКодЛьготыОсвобождениеОтНалогообложенияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ВыбратьРегиональныйКодЛьготы("РегиональныйКодЛьготыОсвобождениеОтНалогообложения");
	
КонецПроцедуры

&НаКлиенте
Процедура РегиональныйКодЛьготыСнижениеНалоговойСтавкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ВыбратьРегиональныйКодЛьготы("РегиональныйКодЛьготыСнижениеНалоговойСтавки");

КонецПроцедуры

&НаКлиенте
Процедура РегиональныйКодЛьготыУменьшениеСуммыНалогаВПроцентахНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ВыбратьРегиональныйКодЛьготы("РегиональныйКодЛьготыУменьшениеСуммыНалогаВПроцентах");

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)

	ЗаписатьДанные();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

&НаКлиенте
Процедура ВыборКода(ИмяКода)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяКода", ИмяКода);

	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ВыборКодаЗавершение", ЭтотОбъект, ДополнительныеПараметры);

	ИмущественныеНалогиКлиент.ВыбратьКодИзКлассификатора(
		ИмяКода, 
		"ЛьготыПоТранспортномуНалогу", 
		"ПараметрыНачисленияТранспортногоНалога", 
		Дата, 
		ЭтотОбъект[ИмяКода], 
		ОповещениеОЗакрытии);
		
КонецПроцедуры

&НаКлиенте
Процедура ВыборКодаЗавершение(РезультатЗакрытия, ДопПараметры) Экспорт
	
	Если РезультатЗакрытия <> Неопределено Тогда
		
		Модифицированность = Истина;
		
		ЭтотОбъект[ДопПараметры.ИмяКода] = РезультатЗакрытия;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьРегиональныйКодЛьготы(ИмяКода)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ОснованиеЛьготы", ЭтотОбъект[ИмяКода]);
	
	ДополнительныеПараметры = Новый Структура("Льгота", ИмяКода);
	Оповещение = Новый ОписаниеОповещения("ВыбратьРегиональныйКодЛьготыЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ОткрытьФорму(
		"Обработка.ОбщиеОбъектыРеглОтчетности.Форма.ФормаВводаОснованияЛьготы", 
		ПараметрыФормы, 
		ЭтотОбъект,,,, 
		Оповещение, 
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьРегиональныйКодЛьготыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	ЭтотОбъект[ДополнительныеПараметры.Льгота] = Результат;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЭлементыФормы(Форма)
	
	НалоговаяЛьгота = Форма.НалоговаяЛьгота;
	Элементы = Форма.Элементы;
	
	Элементы.ГруппаКодЛьготыОсвобождениеОтНалогообложения.Доступность = 
		НалоговаяЛьгота = ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.ОсвобождениеОтНалогообложения");
		
	СнижениеНалоговойСтавки = 
		НалоговаяЛьгота = ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.СнижениеНалоговойСтавки");
	Элементы.ЛьготнаяСтавка.Доступность = СнижениеНалоговойСтавки;
	Элементы.ГруппаКодЛьготыСнижениеНалоговойСтавки.Доступность = СнижениеНалоговойСтавки;
	
	УменьшениеСуммыНалогаВПроцентах = 
		НалоговаяЛьгота = ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаВПроцентах");
	Элементы.ПроцентУменьшения.Доступность = УменьшениеСуммыНалогаВПроцентах;
	Элементы.ГруппаКодЛьготыУменьшениеСуммыНалогаВПроцентах.Доступность = УменьшениеСуммыНалогаВПроцентах;
		
	УменьшениеСуммыНалогаНаСумму = 
		НалоговаяЛьгота = ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаНаСумму");
	Элементы.СуммаУменьшения.Доступность = УменьшениеСуммыНалогаНаСумму;
	Элементы.КодНалоговойЛьготыУменьшениеСуммыНалогаНаСумму.Доступность = УменьшениеСуммыНалогаНаСумму;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Результат, Отказ) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		ЗаписатьДанные(Отказ);
		
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		
		Модифицированность = Ложь;
		Закрыть();
		
	КонецЕсли;

КонецПроцедуры	

&НаКлиенте
Процедура ЗаписатьДанные(Отказ = Ложь)
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЛьготы = Новый Структура(
	"НалоговаяЛьгота,
	|ЛьготнаяСтавка,
	|ПроцентУменьшения,
	|СуммаУменьшения,
	|КодНалоговойЛьготы,
	|РегиональныйКодЛьготы");

	ЗаполнитьЗначенияСвойств(ПараметрыЛьготы, ЭтотОбъект);
	
	Если НалоговаяЛьгота = ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.ОсвобождениеОтНалогообложения") Тогда
		
		ПараметрыЛьготы.КодНалоговойЛьготы = КодНалоговойЛьготыОсвобождениеОтНалогообложения;
		ПараметрыЛьготы.РегиональныйКодЛьготы = РегиональныйКодЛьготыОсвобождениеОтНалогообложения;
		
	ИначеЕсли НалоговаяЛьгота = ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.СнижениеНалоговойСтавки") Тогда
		
		ПараметрыЛьготы.КодНалоговойЛьготы = КодНалоговойЛьготыСнижениеНалоговойСтавки;
		ПараметрыЛьготы.РегиональныйКодЛьготы = РегиональныйКодЛьготыСнижениеНалоговойСтавки;
		
	ИначеЕсли НалоговаяЛьгота = ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаВПроцентах") Тогда
		
		ПараметрыЛьготы.КодНалоговойЛьготы = КодНалоговойЛьготыУменьшениеСуммыНалогаВПроцентах;
		ПараметрыЛьготы.РегиональныйКодЛьготы = РегиональныйКодЛьготыУменьшениеСуммыНалогаВПроцентах;
		
	ИначеЕсли НалоговаяЛьгота = ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаНаСумму") Тогда
		
		ПараметрыЛьготы.КодНалоговойЛьготы = КодНалоговойЛьготыУменьшениеСуммыНалогаНаСумму;
		ПараметрыЛьготы.РегиональныйКодЛьготы = "";
		
	Иначе
		
		ПараметрыЛьготы.КодНалоговойЛьготы = "";
		ПараметрыЛьготы.РегиональныйКодЛьготы = "";
		
	КонецЕсли;
	
	Модифицированность = Ложь;
	
	Закрыть(ПараметрыЛьготы);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
