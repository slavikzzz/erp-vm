
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Организация = Параметры.Организация;
	ОсновныеСредства = Параметры.ОсновныеСредства;
	АдресОстатковПрослеживаемыхТоваров =  Параметры.АдресОстатковПрослеживаемыхТоваров;
		
	ЗаполнитьТаблицуОстатков();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПеренестиВДокументВыполнить()

	ПоместитьТоварыВХранилище();
	Закрыть(КодВозвратаДиалога.OK);
	
	ОповеститьОВыборе(АдресОстатковПрослеживаемыхТоваров);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьСтрокиВыполнить()

	Для Каждого СтрокаТаблицы Из ТаблицаОстатков Цикл
		СтрокаТаблицы.Выбран = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьСтрокиВыполнить()

	Для Каждого СтрокаТаблицы Из ТаблицаОстатков Цикл
		СтрокаТаблицы.Выбран = Ложь
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВыделенныеСтроки(Команда)
	
	МассивСтрок = Элементы.ТаблицаОстатков.ВыделенныеСтроки;
	Для Каждого НомерСтроки Из МассивСтрок Цикл
		СтрокаТаблицы = ТаблицаОстатков.НайтиПоИдентификатору(НомерСтроки);
		Если СтрокаТаблицы <> Неопределено Тогда
			СтрокаТаблицы.Выбран = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьВыделенныеСтроки(Команда)
	
	МассивСтрок = Элементы.ТаблицаОстатков.ВыделенныеСтроки;
	Для Каждого НомерСтроки Из МассивСтрок Цикл
		СтрокаТаблицы = ТаблицаОстатков.НайтиПоИдентификатору(НомерСтроки);
		Если СтрокаТаблицы <> Неопределено Тогда
			СтрокаТаблицы.Выбран = Ложь;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

&НаСервере
Процедура ПоместитьТоварыВХранилище() 
	
	Отбор = Новый Структура("Выбран", Истина);
	Остатки =
		ТаблицаОстатков.Выгрузить(
			Отбор,
			"ОсновноеСредство,НомерГТД,КодТНВЭД,КоличествоПоРНПТ");
	
	ПоместитьВоВременноеХранилище(Остатки, АдресОстатковПрослеживаемыхТоваров);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуОстатков()
	
	Запрос = Новый Запрос();		
	Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|ПрослеживаемыеТоварыВСоставеОСОстатки.РНПТ КАК НомерГТД,
			|ПрослеживаемыеТоварыВСоставеОСОстатки.КодТНВЭД КАК КодТНВЭД,
			|ПрослеживаемыеТоварыВСоставеОСОстатки.ОсновноеСредство КАК ОсновноеСредство,
			|ЕСТЬNULL(ПрослеживаемыеТоварыВСоставеОСОстатки.КоличествоПоРНПТОстаток, 0) КАК КоличествоПоРНПТ,
			|ЕСТЬNULL(ПрослеживаемыеТоварыВСоставеОСОстатки.КоличествоПоРНПТОстаток, 0) КАК ОстатокПоРНПТ
			|ИЗ
			|РегистрНакопления.ПрослеживаемыеТоварыВСоставеОС.Остатки(,Организация = &Организация &Условие1
			| &Условие2) КАК ПрослеживаемыеТоварыВСоставеОСОстатки
			|ГДЕ
			|ПрослеживаемыеТоварыВСоставеОСОстатки.КоличествоПоРНПТОстаток > 0
			|";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Если ЗначениеЗаполнено(КодТНВЭД) Тогда	
		Запрос.УстановитьПараметр("КодТНВЭД", КодТНВЭД);	
		Текст = СтрЗаменить(Текст,"&Условие1","И КодТНВЭД = &КодТНВЭД");
	Иначе
		 Текст = СтрЗаменить(Текст,"&Условие1","");
	КонецЕсли;
	 
	Если ЗначениеЗаполнено(ОсновныеСредства) И  ОсновныеСредства.Количество()>0 Тогда
		Запрос.УстановитьПараметр("ОсновныеСредства", ОсновныеСредства);
		Текст = СтрЗаменить(Текст,"&Условие2","И ОсновноеСредство В (&ОсновныеСредства)");
	Иначе
		Текст = СтрЗаменить(Текст,"&Условие2","");
	КонецЕсли;
	
	Запрос.Текст = Текст;
	
	УстановитьПривилегированныйРежим(Истина);
	ТаблицаОстатков.Загрузить(Запрос.Выполнить().Выгрузить());
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
