
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Параметры.Сервис) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.Заголовок) Тогда
		АвтоЗаголовок = Ложь;
		Заголовок     = Параметры.Заголовок;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Параметры.Банки)
	 Или Параметры.Сервис = Перечисления.СервисыОбменаСБанками.ФинансоваяОтчетность Тогда
	 
		ТекстПодробногоОписания = СтроковыеФункции.ФорматированнаяСтрока(Параметры.ПодробноеОписание);
		
	Иначе
		
		ШаблонОписания = НСтр("ru = '%1
                          |
                          |%2';
                          |en = '%1
                          |
                          |%2'");
		Если Параметры.Банки.Количество() = 1 Тогда
			
			БанкиПолучатели = Параметры.Банки[0].Значение;
			
		Иначе
			
			СписокТребующих = Параметры.Банки.ВыгрузитьЗначения();
			СписокТребующих.Вставить(0, НСтр("ru = 'Требуют:';
											|en = 'Require:'"));
			БанкиПолучатели = СтрСоединить(СписокТребующих, Символы.ПС + "♦ ");
				
		КонецЕсли;
		
		ТекстПодробногоОписания = СтроковыеФункции.ФорматированнаяСтрока(ШаблонОписания, Параметры.ПодробноеОписание, БанкиПолучатели);
			
	КонецЕсли;
	ПодробноеОписание.УстановитьФорматированнуюСтроку(ТекстПодробногоОписания);
	
	Элементы.ГруппаШаблонФайла.Видимость    = ЗначениеЗаполнено(Параметры.ИмяФайлаШаблона);
	Элементы.ДекорацияШаблонФайла.Заголовок = Параметры.ИмяФайлаШаблона;

КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОбразецФайлаНажатие(Элемент)
	
	ПараметрыШаблона = УниверсальныйОбменСБанкамиФормыКлиентСервер.ПараметрыФормыОписания();
	ПараметрыШаблона.Сервис          = Параметры.Сервис;
	ПараметрыШаблона.Характеристика  = Параметры.Характеристика;
	ПараметрыШаблона.Банки           = Параметры.Банки;
	ПараметрыШаблона.ИмяФайлаШаблона = Параметры.ИмяФайлаШаблона;
	
	УниверсальныйОбменСБанкамиФормыКлиент.СохранитьФайлШаблона(ПараметрыШаблона); 	
	
КонецПроцедуры

#КонецОбласти