#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ТипЗнч(Параметры.СписокФайлов) <> Тип("Массив")
		Или Параметры.СписокФайлов.Количество() = 0 Тогда
		
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	СписокФайлов.ЗагрузитьЗначения(Параметры.СписокФайлов);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Отказ = Истина;
	
	Оповещение = Новый ОписаниеОповещения("ПослеСохраненияФайлов", ЭтотОбъект);
	КадровыйЭДОКлиент.СохранитьПечатныеФормыНаДиск(СписокФайлов.ВыгрузитьЗначения(), Оповещение);
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПослеСохраненияФайлов(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		УдалитьФайлыИзОбработки();
		
		Если ОписаниеОповещенияОЗакрытии <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ОписаниеОповещенияОЗакрытии, Истина);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьФайлыИзОбработки()
	
	РегистрыСведений.ЗапланированныеДействияСФайламиДокументовКЭДО.УдалитьФайлыИзОбработки(
		СписокФайлов.ВыгрузитьЗначения(), Перечисления.ДействияСФайламиДокументовКЭДО.ЗаписатьНаДиск);
	
КонецПроцедуры

#КонецОбласти
