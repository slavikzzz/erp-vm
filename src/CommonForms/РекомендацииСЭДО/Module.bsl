&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("Действие", Действие); 
	Параметры.Свойство("ДанныеОшибок", ДанныеОшибок);
	Если ТипЗнч(ДанныеОшибок) = Тип("Структура")
		И ДанныеОшибок.ОрганизацииСОшибками.Количество() > 0  Тогда
		ТекущаяОрганизация = ДанныеОшибок.ОрганизацииСОшибками[0];
	КонецЕсли;
	
	НачалоПолученияНезагруженных = ТекущаяДата() - 30 * 24 * 60 * 60;
	
	Если Действие = Неопределено
		Или Действие = "ПолучениеСписка" 
		Или Действие = "ПолучениеСообщений" Тогда
		ЗаполнитьТаблицуНезагруженныхСообщений(НачалоПолученияНезагруженных); 
	КонецЕсли;   
	
	Если Действие = Неопределено
		Или Действие = "ОтправкаДокумента" Тогда
		ЗаполнитьИдентификаторыТребованийБезОтвета(); 
		ЗаполнитьИдентификаторыПроактивБезОтвета();
	КонецЕсли;
	
	ТекстыТаблицНайденныхПроблем = ТекстыТаблицНайденныхПроблем();
	
	ЕстьПроблемыПроактив = ?(ИдентификаторыНеполученныхПроактив.Количество() + ИдентификаторыПроактивБезОтвета.Количество() > 0, Истина, Ложь);
	ЕстьПроблемыТребования = ?(ИдентификаторыНеполученныхТребований.Количество() + ИдентификаторыТребованийБезОтвета.Количество() > 0, Истина, Ложь);  
	
	ЕстьПроблемы = ЕстьПроблемыПроактив Или ЕстьПроблемыТребования;
	
	Элементы.ПодробноHTML.Высота = 8 
		+ ?(ЕстьПроблемыПроактив, 2, 0) 
		+ ?(ЕстьПроблемыТребования, 6, 0) 
		+ ИдентификаторыНеполученныхПроактив.Количество() * 1.52
		+ ИдентификаторыНеполученныхТребований.Количество() * 1.52;
	
	ПодробноHTML = "<!DOCTYPE HTML>
			|<html>
			| <head>
			|  <meta charset=""utf-8"">
			|  <style>
			|  body {
			|	font-family: 'Arial'; 
			|	font-size: 10pt;    
			|	margin: 0;
			|}  
			|html {
			|   overflow: hidden;
			|}          
			|p {
			|    display: block;
			|    margin-block-start: 1em;
			|    margin-block-end: 1em;
			|    margin-inline-start: 0px;
			|    margin-inline-end: 0px;
			|}
			|#container {
			|   max-width: 850px;  
			|	padding-right: 50px;
			|	padding-right: 20px;
   			|	padding-left: 20px;
			|}
			|#textBlock {
			|   float: right;
			|}
			|#leftcol {
			|    width: 50px;
			|}
			|#leftcol img {
			|	width: 50px;
			|	height: 50px;
			|	float: left; 
			|	margin: 10px 10px 0px 0px;
			|}      
			|table {
			|	border-color: gray;
			|	border-width: 0.5px;
			|	border-spacing: 2px;
			|	box-sizing: border-box;
			|	display: table;
			|	border-collapse: collapse;
			|	border-style: solid;
			|}    
			|tbody {
			|   display: table-row-group;
			|   vertical-align: middle;
			|   border-color: gray;  
			|	border-width: 0.5px;
			|}
			|td {
			|	padding: 5px; 
			|	height: 12px;
			|	font-size: 9pt;
			|}    
			|#tabheader {
			|	margin-bottom: 3px;
			|	padding-top: 10px;
			|}
			|#colorblock {
			|	background: #FFFAD9;
			|}
			|#bottom {
			|    clear: both;
			|}
			|  </style>
			| </head>
			|<body>
			|<div id=""container"">
			|    <div id=""textBlock"">  
			|	<div id=""leftcol"">
			|   	<img alt="""" src=""data:image/svg+xml;base64,PHN2ZyB3aWR0aD0iNjUiIGhlaWdodD0iNjQiIHZpZXdCb3g9IjAgMCA2NSA2NCIgZmlsbD0iI0YzODAwMCIgeG1sbnM9Imh0dHA6Ly93d3cudzMub3JnLzIwMDAvc3ZnIj4KPHBhdGggZmlsbC1ydWxlPSJldmVub2RkIiBkPSJNMzQgNUwzIDYxSDY0LjVMMzQgNVpNMzMuOTg1NyA5LjE1NTI4TDYuMzkzMTQgNTlINjEuMTMzM0wzMy45ODU3IDkuMTU1MjhaIi8+CjxwYXRoIGQ9Ik0zMyAyN0gzNVY0M0gzM1YyN1oiLz4KPHBhdGggZD0iTTM2IDUwQzM2IDUxLjEwNDYgMzUuMTA0NiA1MiAzNCA1MkMzMi44OTU0IDUyIDMyIDUxLjEwNDYgMzIgNTBDMzIgNDguODk1NCAzMi44OTU0IDQ4IDM0IDQ4QzM1LjEwNDYgNDggMzYgNDguODk1NCAzNiA1MFoiLz4KPC9zdmc+Cg=="" />
			|	</div>
			|    <p>Возникли ошибки при работе с СЭДО СФР."
				+ ?(ЕстьПроблемы, " Ниже перечислены проблемы с документами СЭДО, обнаруженные при анализе данных базы:", "") + "</p>" 
				+ ?(ЕстьПроблемыПроактив, "
			|<p id=""tabheader""><strong>Запросы недостающих сведений по проактивным выплатам</strong></p>
			|<table border=""1"" style=""border-collapse: collapse; width: 100%;"">
			|<tbody>
			|<tr>
			|<td style=""width: 25%; background-color: #fafafa;""><strong>Организация</strong></td>
			|<td style=""width: 35%; background-color: #fafafa;""><strong>Вид документа</strong></td>
			|<td style=""width: 25%; background-color: #fafafa;""><strong>Идентификатор СФР</strong></td>
			|<td style=""width: 15%; background-color: #fafafa;""><strong>Проблема</strong></td>
			|</tr>" + ТекстыТаблицНайденныхПроблем.ТекстТаблицыПроактив + "
			|</tbody>
			|</table>", "")
				+ ?(ЕстьПроблемыТребования, "
			|<br>
			|<p id=""tabheader""><strong>Требования СФР</strong></p>
			|<table border=""1"" style=""border-collapse: collapse; width: 100%;"">
			|<tbody>
			|<tr>
			|<td style=""width: 25%; background-color: #fafafa;""><strong>Организация</strong></td>
			|<td style=""width: 35%; background-color: #fafafa;""><strong>Вид документа</strong></td>
			|<td style=""width: 25%; background-color: #fafafa;""><strong>Идентификатор СФР</strong></td>
			|<td style=""width: 15%; background-color: #fafafa;""><strong>Проблема</strong></td>
			|</tr>" + ТекстыТаблицНайденныхПроблем.ТекстТаблицыТребования + "
			|</tbody>
			|</table>", "") + "
			|<br>
			|<div id=""colorblock"">
			|<p><strong>Рекомендации</strong></p>
			|<p>Рекомендуется подключить обмен СЭДО через оператора. Для подключения Вы можете отправить <a href = ""ОткрытьЗаявление"">заявление на подключение к 1С-Отчетности</a>.</p>
			|</div>
			|</div>
			|</body>
			|</html>";
				

КонецПроцедуры

&НаСервере
Функция ТекстыТаблицНайденныхПроблем()  
	
	Результат = Новый Структура("ТекстТаблицыПроактив, ТекстТаблицыТребования", "", "");
	
	Для Каждого СтрокаТаблицы Из ТаблицаЗапросов Цикл
		Если СтрокаТаблицы.НомерПроблемы = 1 Тогда
			Результат.ТекстТаблицыПроактив = Результат.ТекстТаблицыПроактив + "
				|<tr>
				|<td style=""width: 25%;"">" + СтрокаТаблицы.Организация + "</td>
				|<td style=""width: 35%;"">" + СтрокаТаблицы.ВидДокумента + "</td>
				|<td style=""width: 25%;"">" + СтрокаТаблицы.ИдентификаторФСС + "</td>
				|<td style=""width: 15%;""><span style=""color: #e12828;"">Не удалось получить</span></td>
				|</tr>";	
		ИначеЕсли СтрокаТаблицы.НомерПроблемы = 3 Тогда
			Результат.ТекстТаблицыТребования = Результат.ТекстТаблицыТребования + "
				|<tr>
				|<td style=""width: 25%;"">" + СтрокаТаблицы.Организация + "</td>
				|<td style=""width: 35%;"">" + СтрокаТаблицы.ВидДокумента + "</td>
				|<td style=""width: 25%;"">" + СтрокаТаблицы.ИдентификаторФСС + "</td>
				|<td style=""width: 15%;""><span style=""color: #e12828;"">Не удалось получить</span></td>
				|</tr>";
		ИначеЕсли СтрокаТаблицы.НомерПроблемы = 4 Тогда
			Результат.ТекстТаблицыТребования = Результат.ТекстТаблицыТребования + "
				|<tr>
				|<td style=""width: 25%;"">" + СтрокаТаблицы.Организация + "</td>
				|<td style=""width: 35%;"">" + СтрокаТаблицы.ВидДокумента + "</td>
				|<td style=""width: 25%;"">" + СтрокаТаблицы.ИдентификаторФСС + "</td>
				|<td style=""width: 15%;""><span style=""color: #e12828;"">Не удалось отправить ответ</span></td>
				|</tr>";
		КонецЕсли;
	КонецЦикла;

	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьТаблицуНезагруженныхСообщений(ДатаНачала, ТипыСообщенияСЭДО = Неопределено)

	ТаблицаЗапросов.Очистить();
	ИдентификаторыНеполученныхПроактив.Очистить();
	ИдентификаторыНеполученныхТребований.Очистить();
	
	СоответствиеКодВидДокумента = СоответствиеКодВидДокумента();
		
	Запрос = Новый Запрос;  
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);	
	Если ТипыСообщенияСЭДО <> Неопределено Тогда
		Запрос.УстановитьПараметр("Все", Ложь);
		Запрос.УстановитьПараметр("ТипыСообщенияСЭДО", ТипыСообщенияСЭДО);
	Иначе                 
		Запрос.УстановитьПараметр("Все", Истина);
		Запрос.УстановитьПараметр("ТипыСообщенияСЭДО", Новый Массив);
	КонецЕсли;
	
	НастройкиВызова = Новый Структура;
	НастройкиВызова.Вставить("БезУчетаПользователя", Истина);
	ОрганизацииСОбменом = ЭлектронныйДокументооборотСФСС.ОрганизацииИспользующиеОбменФСС();
	ОрганизацииДляЗапроса = Новый Массив;
	Для каждого Организация Из ОрганизацииСОбменом Цикл
		СвойстваОбменаПоСЭДОЧерезОператора =
			ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.СвойстваОбменаПоСЭДОЧерезОператора(
				Организация, ,
				НастройкиВызова);
		Если СвойстваОбменаПоСЭДОЧерезОператора.ОбменПоСЭДОНапрямую Тогда
			ОрганизацииДляЗапроса.Добавить(Организация);
		КонецЕсли;
	КонецЦикла;
	Если ОрганизацииДляЗапроса.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	Запрос.УстановитьПараметр("Организации", ОрганизацииДляЗапроса);
	
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
					|	ВходящиеСообщенияСЭДОФСС.Организация КАК Организация,
					|	ВходящиеСообщенияСЭДОФСС.Идентификатор КАК ИдентификаторФСС,
					|	ВходящиеСообщенияСЭДОФСС.Тип КАК ТипСообщенияСЭДО
					|ИЗ
					|	РегистрСведений.ВходящиеСообщенияСЭДОФСС КАК ВходящиеСообщенияСЭДОФСС
					|ГДЕ
					|	ВходящиеСообщенияСЭДОФСС.Новое = ИСТИНА
					|	И ВходящиеСообщенияСЭДОФСС.Дата >= &ДатаНачала
					|	И ВходящиеСообщенияСЭДОФСС.Организация В (&Организации)
					|	И (ВходящиеСообщенияСЭДОФСС.Тип В (&ТипыСообщенияСЭДО)
	               	|		ИЛИ &Все = ИСТИНА)";
	
	Выборка = Запрос.Выполнить().Выбрать();  
	
	КодыПроактив = КодыПроактив();
	КодыТребований = КодыТребований();
	
	Пока Выборка.Следующий() Цикл
		СтрокаЗапроса = ТаблицаЗапросов.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаЗапроса, Выборка);
		СтрокаЗапроса.ВидДокумента = СоответствиеКодВидДокумента.Получить(Строка(Выборка.ТипСообщенияСЭДО));
		
		Если КодыПроактив.Найти(Строка(Выборка.ТипСообщенияСЭДО)) <> Неопределено Тогда
			ТекущаяОрганизация = Выборка.Организация;
			ИдентификаторыНеполученныхПроактив.Добавить(Выборка.ИдентификаторФСС);
			СтрокаЗапроса.НомерПроблемы = 1;
		КонецЕсли;
		
		Если КодыТребований.Найти(Строка(Выборка.ТипСообщенияСЭДО)) <> Неопределено Тогда
			ТекущаяОрганизация = Выборка.Организация;
			ИдентификаторыНеполученныхТребований.Добавить(Выборка.ИдентификаторФСС);
			СтрокаЗапроса.НомерПроблемы = 3;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИдентификаторыТребованийБезОтвета(Знач МинимальнаяДатаОтвета = Неопределено)
	
	ИдентификаторыТребованийБезОтвета.Очистить();    
	
	МаксимальнаяДатаОтправкиОтвета = ТекущаяДата() + 30 * 24 * 60 * 60;
	
	Если МинимальнаяДатаОтвета = Неопределено Тогда
		МинимальнаяДатаОтвета = ТекущаяДата() - 3 * 24 * 60 * 60;  
	КонецЕсли;
	
	Запрос = Новый Запрос;	
	Запрос.УстановитьПараметр("МаксимальнаяДатаОтправкиОтвета", МаксимальнаяДатаОтправкиОтвета);
	Запрос.УстановитьПараметр("МинимальнаяДатаОтвета", МинимальнаяДатаОтвета);
	
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ВходящийДокументСЭДОФСС.ИдентификаторСообщения КАК ИдентификаторФСС, 
				   |	ВходящийДокументСЭДОФСС.Организация КАК Организация,
				   |	ВходящийДокументСЭДОФСС.ВидДокументаФСС КАК ВидДокумента,
	               |	МАКСИМУМ(ОтветСтрахователяНаТребованиеПроверкиФСС.ИдентификаторСообщенияОтвета) КАК ИдентификаторСообщенияОтвета,
	               |	МАКСИМУМ(ОтветСтрахователяНаТребованиеПроверкиФСС.ДатаОтправкиВФСС) КАК ДатаОтправкиВФСС,
	               |	МАКСИМУМ(ОтветСтрахователяНаТребованиеПроверкиФСС.Дата) КАК Дата
	               |ИЗ
	               |	Документ.ОтветСтрахователяНаТребованиеПроверкиФСС КАК ОтветСтрахователяНаТребованиеПроверкиФСС
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВходящийДокументСЭДОФСС КАК ВходящийДокументСЭДОФСС
	               |		ПО ОтветСтрахователяНаТребованиеПроверкиФСС.Основание = ВходящийДокументСЭДОФСС.Ссылка
	               |ГДЕ
	               |	ВходящийДокументСЭДОФСС.МаксимальнаяДатаОтправкиОтвета <= &МаксимальнаяДатаОтправкиОтвета
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВходящийДокументСЭДОФСС.ИдентификаторСообщения,
				   |	ВходящийДокументСЭДОФСС.Организация,
				   |	ВходящийДокументСЭДОФСС.ВидДокументаФСС
	               |
	               |ИМЕЮЩИЕ
	               |	МАКСИМУМ(ОтветСтрахователяНаТребованиеПроверкиФСС.ИдентификаторСообщенияОтвета) = """" И
	               |	МАКСИМУМ(ОтветСтрахователяНаТребованиеПроверкиФСС.ДатаОтправкиВФСС) = ДАТАВРЕМЯ(1, 1, 1) И
	               |	МАКСИМУМ(ОтветСтрахователяНаТребованиеПроверкиФСС.Дата) > &МинимальнаяДатаОтвета";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл     
		СтрокаЗапроса = ТаблицаЗапросов.Добавить();	  
		ЗаполнитьЗначенияСвойств(СтрокаЗапроса, Выборка);
		СтрокаЗапроса.НомерПроблемы = 4;
		
		ИдентификаторыТребованийБезОтвета.Добавить(Выборка.ИдентификаторФСС);	
	КонецЦикла;	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИдентификаторыПроактивБезОтвета()
	
	ИдентификаторыПроактивБезОтвета.Очистить();
	
	ЭлектронныйДокументооборотСФССПереопределяемый.ЗаполнитьИдентификаторыПроактивБезОтвета(ТаблицаЗапросов, ИдентификаторыПроактивБезОтвета);	
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция КодыТребований()
	
	Результат = Новый Массив;    
	
	Результат.Добавить("303");
	Результат.Добавить("308");
	Результат.Добавить("310");
	Результат.Добавить("312"); 
	
	Возврат Результат;
		
КонецФункции 

&НаКлиентеНаСервереБезКонтекста
Функция КодыПроактив()
	
	Результат = Новый Массив;    
	
	Результат.Добавить("100"); 
	
	Возврат Результат;
		
КонецФункции
  
&НаСервере
Функция СоответствиеКодВидДокумента()
	
	Результат = Новый Соответствие;
	
	МакетПеречняДокументов = Обработки.ДокументооборотСКонтролирующимиОрганами.ПолучитьМакет("ПереченьДокументовОбрабатываемыхСЭДОФСС");
	
	Попытка
		ЧтениеXML = Новый ЧтениеXML;
		БуферДанных = ПолучитьБуферДвоичныхДанныхИзДвоичныхДанных(МакетПеречняДокументов);
		ЧтениеXML.ОткрытьПоток(Новый ПотокВПамяти(БуферДанных));
		ДанныеОтвета = ФабрикаXDTO.ПрочитатьXML(ЧтениеXML);
		ЧтениеXML.Закрыть();
	Исключение
		Возврат Результат;
	КонецПопытки;
		
	Если ДанныеОтвета.Свойства().Получить("dicList") <> Неопределено Тогда
		УзелСписок = ДанныеОтвета.dicList;
		
		Если УзелСписок.Свойства().Получить("dic") <> Неопределено Тогда
			Для К = 0 По УзелСписок.dic.Количество() - 1 Цикл
				УзелСообщения = ДанныеОтвета.dicList.dic[К];
				ЭлементТипСообщения = УзелСообщения.code;
				ПредставлениеДокумента = УзелСообщения.name; 
				
				Результат.Вставить(ЭлементТипСообщения, ПредставлениеДокумента);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;  
	
	// Почему-то нет в макете
	Результат.Вставить("330", "Запрос исторических сообщений за дату абонентом");
	Результат.Вставить("331", "Запрос исторических сообщений за дату оператором");
	Результат.Вставить("332", "Ответ на запрос исторических данных");
	
	Возврат Результат; 
	
КонецФункции

&НаКлиенте
Процедура ПодробноHTMLПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	СсылкаДляПерехода = Неопределено;
	ДанныеСобытия.Свойство("Href", СсылкаДляПерехода);
	
	Если СсылкаДляПерехода <> Неопределено Тогда
		СтандартнаяОбработка = Ложь;
		Если СтрНайти(СсылкаДляПерехода, "ОткрытьЗаявление") > 0 Тогда
			ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьФормуМастераЗаявленияНаПодключение(
				ТекущаяОрганизация, ЭтотОбъект, ,
				ПредопределенноеЗначение("Перечисление.ТипыЗаявленияАбонентаСпецоператораСвязи.Первичное"), , ,Истина);
		Иначе
			ОбщегоНазначенияКлиент.ОткрытьНавигационнуюСсылку(СсылкаДляПерехода);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
