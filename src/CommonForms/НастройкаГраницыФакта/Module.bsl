
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ВидБюджета = Параметры.ВидБюджета;
	НачалоПериода = Параметры.НачалоПериода;
	ГраницаФактДанных = Параметры.ГраницаФактДанных;
	ГраницаФактДанныхПоУмолчанию = Справочники.ВидыБюджетов.ГраницаФактическихДанныхПоВидуБюджета(ВидБюджета, НачалоПериода);
	ТекстЗаголовка = НСтр("ru = 'Использовать границу факт.данных по-умолчанию (%1)';
							|en = 'Use the default actual data offset (%1)'");
	ТекстЗаголовка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстЗаголовка, Формат(ГраницаФактДанныхПоУмолчанию, "ДЛФ=D"));
	Элементы.ИспользоватьПоУмолчанию.СписокВыбора[0].Представление = ТекстЗаголовка;
	
	Если ГраницаФактДанных <> ГраницаФактДанныхПоУмолчанию Тогда
		ВариантГраницыФактДанных = 1;
	КонецЕсли;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаГраницыФактаРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РегулированиеНаСервере(Направление);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)
	
	Если ПроверитьЗаполнение() Тогда
		Если ВариантГраницыФактДанных = 1 Тогда
			Закрыть(ГраницаФактДанных);
		Иначе
			Закрыть(ГраницаФактДанныхПоУмолчанию);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантПриИзменении(Элемент)
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура РегулированиеНаСервере(Направление)
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВидБюджета, "Периодичность");
	
	Если Направление > 0 Тогда
		
		НоваяДата = НачалоДня(БюджетированиеКлиентСервер.ДатаКонцаПериода(ГраницаФактДанных, Реквизиты.Периодичность));
		Если НоваяДата > ГраницаФактДанных Тогда
			ГраницаФактДанных = НоваяДата;
			Возврат;
		КонецЕсли;
		
	Иначе
		
		НоваяДата = НачалоДня(БюджетированиеКлиентСервер.ДатаНачалаПериода(ГраницаФактДанных, Реквизиты.Периодичность) - 1);
		Если НоваяДата < ГраницаФактДанных Тогда
			ГраницаФактДанных = НоваяДата;
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	ГраницаФактДанных = БюджетированиеКлиентСервер.ДатаКонцаПериода(
		БюджетированиеКлиентСервер.ДобавитьИнтервал(НоваяДата, Реквизиты.Периодичность, Направление), Реквизиты.Периодичность);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Форма.Элементы.ДатаГраницыФакта.Доступность = Форма.ВариантГраницыФактДанных;
	
КонецПроцедуры

#КонецОбласти

