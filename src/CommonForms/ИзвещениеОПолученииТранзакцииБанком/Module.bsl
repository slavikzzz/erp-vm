#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РезультатПолученияПротокола = Параметры.РезультатПолученияПротокола;
	
	Если НЕ РезультатПолученияПротокола.Выполнено Тогда
		Возврат;
	КонецЕсли;
	
	Наименование = УниверсальныйОбменСБанками.ПредставлениеПредмета(
		Перечисления.СервисыОбменаСБанками.ФинансоваяОтчетность,
		Параметры.Предмет);
		
	Протокол = РезультатПолученияПротокола.ТекстПротокола;
	Предмет  = РезультатПолученияПротокола.Предмет;
	
	ЗаполнитьДанныеВШапкеФормы();
	УправлениеЭУ(Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НЕ РезультатПолученияПротокола.Выполнено Тогда
		ПоказатьПредупреждение(, РезультатПолученияПротокола.ОписаниеОшибки,, НСтр("ru = 'Ошибка получения извещения о получении';
																					|en = 'An error occurred while receiving the receipt notification'"));
		Закрыть(Ложь);
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаСохранитьНажатие(Элемент)
	
	ДанныеПротокола = РезультатПолученияПротокола.ДанныеПротокола;
	ДанныеПодписи = РезультатПолученияПротокола.ДанныеПодписи;
	
	ФинОтчетностьВБанкиКлиент.СохранитьПротокол("ИзвещениеОПолучении",
		Банк,
		Организация,
		ДанныеПротокола,
		ДанныеПодписи);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УправлениеЭУ(Отказ = Ложь)
	
	Элементы.Наименование.Видимость = НЕ ПустаяСтрока(Наименование);
	
	РазобратьПротокол();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеВШапкеФормы()

	Элементы.Наименование.Заголовок	= Наименование;
	
	// Отправитель
	Если ЗначениеЗаполнено(Организация) Тогда
		Элементы.Организация.Заголовок = Организация;
	Иначе
		Элементы.Организация.Видимость 				= Ложь;
		Элементы.ЗаголовокОрганизация.Видимость 	= Ложь;
	КонецЕсли;
	
	// Получатель
	Если ЗначениеЗаполнено(Банк) Тогда
		Элементы.Банк.Заголовок				= Банк;
	Иначе
		Элементы.ЗаголовокБанк.Видимость 	= Ложь;
		Элементы.Банк.Видимость 			= Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура РазобратьПротокол()
	
	Если СтрНайти(Протокол, "ИзвещениеОПолучении") = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Чтение = Новый ЧтениеXML;
	Чтение.УстановитьСтроку(Протокол);
	ДанныеПротокола = ФабрикаXDTO.ПрочитатьXML(Чтение);
	
	ИдентификаторБанка = ДанныеПротокола.Банк.Идентификатор;
	Банк = УниверсальныйОбменСБанками.НайтиБанкПоИдентификатору(ИдентификаторБанка);
	ИННОрганизации = ДанныеПротокола.Организация.ИНН;
	Организация = УниверсальныйОбменСБанками.НайтиОрганизациюПоИНН(ИННОрганизации);
	
	Если НЕ ПустаяСтрока(ДанныеПротокола.ДатаПолучения) Тогда
		ДатаПолучения = XMLЗначение(Тип("Дата"), ДанныеПротокола.ДатаПолучения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти