#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Сотрудник = Параметры.Сотрудник;
	ЗаполнитьДокументыИндексации();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДокументыИндексации

&НаКлиенте
Процедура ДокументыИндексацииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ДанныеСтроки = Элементы.ДокументыИндексации.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено Или Не ЗначениеЗаполнено(ДанныеСтроки.Документ) Тогда 
		Возврат;
	КонецЕсли;
	
	ПоказатьЗначение(, ДанныеСтроки.Документ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьДокументыИндексации()
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КоэффициентИндексацииЗаработка.Период КАК Период,
		|	КоэффициентИндексацииЗаработка.Регистратор КАК Документ
		|ПОМЕСТИТЬ ВТДокументыИндексации
		|ИЗ
		|	РегистрСведений.КоэффициентИндексацииЗаработка КАК КоэффициентИндексацииЗаработка
		|ГДЕ
		|	КоэффициентИндексацииЗаработка.Сотрудник = &Сотрудник
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КоэффициентыПовышения.Период,
		|	КоэффициентыПовышения.Регистратор
		|ИЗ
		|	РегистрСведений.КоэффициентыПовышенияЗаработкаУчитываемыеПриИндексации КАК КоэффициентыПовышения
		|ГДЕ
		|	КоэффициентыПовышения.Сотрудник = &Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДокументыИндексации.Документ КАК Документ
		|ИЗ
		|	ВТДокументыИндексации КАК ДокументыИндексации
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДокументыИндексации.Период";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл 
		ЗаполнитьЗначенияСвойств(ДокументыИндексации.Добавить(), Выборка);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
