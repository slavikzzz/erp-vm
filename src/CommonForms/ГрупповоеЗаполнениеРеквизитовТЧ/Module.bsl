
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МассивОписанийРеквизитов = Неопределено;
	Параметры.Свойство("МассивОписанийРеквизитов", МассивОписанийРеквизитов);
	Параметры.Свойство("ИмяТЧ", ИмяТЧ);
	Если МассивОписанийРеквизитов <> Неопределено Тогда
		ДополнитьФормуРеквизитами(МассивОписанийРеквизитов);
		ЗаполнитьЗначенияРеквизитов(МассивОписанийРеквизитов);
	КонецЕсли;
	
	КлючСохраненияПоложенияОкна = Строка(КоличествоРеквизитов);
	
	УстановитьДоступностьЗначенийРеквизитов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура Подключаемый_ИспользованиеРеквизитПриИзменении(Элемент)
	
	УстановитьДоступностьЗначенийРеквизитов();
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	РеквизитыТЧ = Новый Структура;
	РеквизитыТЧ.Вставить("ИмяТЧ", ЭтотОбъект.ИмяТЧ);

	ЗначенияРеквизитов = Новый Структура;
	Для Каждого Реквизит из МассивРеквизитов Цикл
		ИмяРеквизита = Реквизит.Значение;
		Если ЭтотОбъект[ИмяРеквизита + "Использование"] Тогда 
			ЗначенияРеквизитов.Вставить(ИмяРеквизита, ЭтотОбъект[ИмяРеквизита]);
		КонецЕсли;
	КонецЦикла;
	РеквизитыТЧ.Вставить("ЗначенияРеквизитов", ЗначенияРеквизитов);
	
	Оповестить("ИзмененыРеквизитыДокумента", РеквизитыТЧ, ЭтотОбъект);
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ДополнитьФормуРеквизитами(МассивОписанийРеквизитов)

	КоличествоРеквизитов = МассивОписанийРеквизитов.Количество();
	
	МассивИменРеквизитовФормы = Новый Массив;
	ЗарплатаКадры.ЗаполнитьМассивИменРеквизитовФормы(ЭтотОбъект, МассивИменРеквизитовФормы);
	
	ДобавляемыеРеквизиты = Новый Массив;
	Для Каждого ОписаниеРеквизита Из МассивОписанийРеквизитов Цикл
		ДобавитьРеквизитыФормы(ОписаниеРеквизита.ИмяРеквизита, ОписаниеРеквизита.ТипРеквизита, ОписаниеРеквизита.Наименование, ДобавляемыеРеквизиты);
	КонецЦикла;
	
	ЗарплатаКадры.ИзменитьРеквизитыФормы(ЭтотОбъект, ДобавляемыеРеквизиты, МассивИменРеквизитовФормы);
	
	Для Каждого ОписаниеРеквизита Из МассивОписанийРеквизитов Цикл
		ДобавитьЭлементыРеквизитов(ОписаниеРеквизита.ИмяРеквизита, ОписаниеРеквизита.ТипРеквизита, ОписаниеРеквизита.Наименование, ОписаниеРеквизита.ЗначениеРеквизита);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьРеквизитыФормы(ИмяРеквизита, ТипРеквизита, НаименованиеРеквизита, ДобавляемыеРеквизиты)
	ПоказательРеквизит = Новый РеквизитФормы(ИмяРеквизита, ТипРеквизита);
	ДобавляемыеРеквизиты.Добавить(ПоказательРеквизит);
	
	ИспользованиеПоказателяРеквизит = Новый РеквизитФормы(ИмяРеквизита  + "Использование", Новый ОписаниеТипов("Булево"),, НаименованиеРеквизита);
	ДобавляемыеРеквизиты.Добавить(ИспользованиеПоказателяРеквизит);
	
	МассивРеквизитов.Добавить(ИмяРеквизита);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьЭлементыРеквизитов(ИмяРеквизита, ТипРеквизита, Представление, ЗначениеРеквизита = Неопределено)
	
	Если Элементы.Найти(ИмяРеквизита + "Использование") <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Использование = Элементы.Добавить(ИмяРеквизита + "Использование", Тип("ПолеФормы"), Элементы.ПоказателиЛеваяКолонка);
	Использование.Вид = ВидПоляФормы.ПолеФлажка;
	Использование.ПутьКДанным = ИмяРеквизита + "Использование";
	Использование.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
	Использование.УстановитьДействие("ПриИзменении", "Подключаемый_ИспользованиеРеквизитПриИзменении");
	
	Значение = Элементы.Добавить(ИмяРеквизита, Тип("ПолеФормы"), Элементы.ПоказателиПраваяКолонка);
	Значение.Вид = ВидПоляФормы.ПолеВвода;
	Значение.ПутьКДанным = ИмяРеквизита;
	Значение.Ширина = 16;
	Значение.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, ИмяРеквизита, "ОграничениеТипа", ТипРеквизита);
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗначенияРеквизитов(МассивОписанийРеквизитов)
	
	Для Каждого ОписаниеРеквизита Из МассивОписанийРеквизитов Цикл 
		ЭтотОбъект[ОписаниеРеквизита.ИмяРеквизита] = ОписаниеРеквизита.ЗначениеРеквизита;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьЗначенийРеквизитов()

	Для Каждого Реквизит из МассивРеквизитов Цикл
		ИмяРеквизита = Реквизит.Значение;
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(ЭтотОбъект.Элементы, 
			ИмяРеквизита, "Доступность", ЭтотОбъект[ИмяРеквизита + "Использование"]);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
