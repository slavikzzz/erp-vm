
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	СписокЗначений.ТипЗначения = Параметры.ТипЗначения;
	СписокЗначений.ЗагрузитьЗначения(Параметры.Значения);
	
	Если ЗначениеЗаполнено(Параметры.Заголовок) Тогда
		Заголовок = Параметры.Заголовок;
		АвтоЗаголовок = Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.ПараметрыВыбора) Тогда
		МассивОтборов = Новый Массив;
		Для каждого КлючИЗначение Из Параметры.ПараметрыВыбора Цикл
			МассивОтборов.Добавить(Новый ПараметрВыбора("Отбор." + КлючИЗначение.Ключ, КлючИЗначение.Значение));
		КонецЦикла;
		ПараметрыВыбора = Новый ФиксированныйМассив(МассивОтборов);
		Элементы.СписокЗначенийЗначение.ПараметрыВыбора = ПараметрыВыбора;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)
	
	ВозвращаемоеЗначение = СписокЗначений.ВыгрузитьЗначения();
	Закрыть(ВозвращаемоеЗначение);
	
КонецПроцедуры

#КонецОбласти