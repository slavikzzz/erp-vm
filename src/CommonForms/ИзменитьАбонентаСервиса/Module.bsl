#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ТекущийИдентификаторПользователяИП", ТекущийИдентификаторПользователяИП);
	Параметры.Свойство("ТекущийИдентификаторАбонентаИП", ТекущийИдентификаторАбонентаИП);
	Параметры.Свойство("РазделениеВключено", РазделениеВключено);
	Если Не ЗначениеЗаполнено(ТекущийИдентификаторПользователяИП) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаДлительнаяОперация;
	НачатьПроверкуИдентификатора();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Новый ОписаниеОповещения("ПриЗавершенииЗадания", ЭтотОбъект), ПараметрыОжидания);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)
	
	Закрыть(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть(Ложь);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПриЗавершенииЗадания(Результат, ИмяЗадания) Экспорт

	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ТекстСообщения = НСтр("ru = 'При выполнении фонового задания возникла ошибка:';
								|en = 'An error occurred while executing a background job:'") + Символы.ПС + Результат.КраткоеПредставлениеОшибки;
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	ПроверкаИдентификатораЗавершение(Результат.АдресРезультата);

КонецПроцедуры

&НаСервере
Процедура НачатьПроверкуИдентификатора()
	
	ИмяМетода 		= "КабинетСотрудникаМенеджерСервиса.ПолучитьАбонентовИнтернетПоддержкиФоновоеЗадание";
	
	ПараметрыВыполненияВФоне = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполненияВФоне.ЗапуститьВФоне = Истина;
	ПараметрыВыполненияВФоне.ОжидатьЗавершение = 0;
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(ИмяМетода, Неопределено, ПараметрыВыполненияВФоне);
	
КонецПроцедуры

&НаСервере
Процедура ПроверкаИдентификатораЗавершение(АдресРезультата)

	Результат = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	ТекстСообщения = "";
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		ТекстСообщения = НСтр("ru = 'Не удалось проверить пользователя Интернет-поддержки';
								|en = 'Cannot verify online support user'");
	ИначеЕсли Не ЗначениеЗаполнено(Результат.АбонентыИП) Тогда
		ТекстСообщения = НСтр("ru = 'Не удалось проверить пользователя Интернет-поддержки';
								|en = 'Cannot verify online support user'");
	ИначеЕсли Результат.СообщениеОбОшибке <> Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Не удалось проверить пользователя Интернет-поддержки:';
								|en = 'Cannot verify online support user:'") + Символы.ПС + Результат.СообщениеОбОшибке;
	КонецЕсли;
	
	Если Не ПустаяСтрока(ТекстСообщения) Тогда
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Если РазделениеВключено Тогда
		АбонентНеИзменился = (СокрЛП(ТекущийИдентификаторАбонентаИП) = СокрЛП(Результат.АбонентыИП[0].Идентификатор));
	Иначе
		АбонентНеИзменился = (СокрЛП(ТекущийИдентификаторПользователяИП) = СокрЛП(Результат.АбонентыИП[0].Идентификатор));
	КонецЕсли;
	Если АбонентНеИзменился Тогда
		Если РазделениеВключено Тогда
			ТекстЗаголовка = НСтр("ru = 'Абонент уже является текущим пользователем Интернет-поддержки.';
									|en = 'The subscriber is already the current online support user.'");
		Иначе
			ТекстЗаголовка = НСтр("ru = 'Абонент является текущим пользователем Интернет-поддержки, для изменения необходимо подключить Интернет-поддержку для другого пользователя.';
									|en = 'The subscriber is a current online support user, connect online support for another user to edit.'");
		КонецЕсли;
		Элементы.ДекорацияИзменениеНеДоступно.Заголовок = ТекстЗаголовка;
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаИзменениеНеДоступно;
	Иначе
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаИзменениеАбонента;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти



