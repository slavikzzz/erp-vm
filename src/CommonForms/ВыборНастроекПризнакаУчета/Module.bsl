
&НаКлиенте
Процедура Выбрать(Команда)
	
	ОбработатьВыбор(Элементы.ДеревоВыбора.ТекущаяСтрока);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("Валюта") Тогда
		РежимРаботы = 2;
		Заголовок = НСтр("ru = 'Способ определения валюты';
						|en = 'Currency determination method'");
	ИначеЕсли Параметры.Свойство("Количество") Тогда
		РежимРаботы = 1;
		Заголовок = НСтр("ru = 'Способ определения ед. измерения';
						|en = 'Method of determining a unit of measure'");
	Иначе
		ВызватьИсключение НСтр("ru = 'Неизвестный режим работы';
								|en = 'Unknown operation mode'");
	КонецЕсли;
	
	ТипыЗначения = Параметры.ТипыЗначения;
	
	КэшКартинок = Новый Структура("Элемент, Справочник", БиблиотекаКартинок.Элемент, БиблиотекаКартинок.Справочник);
	
	Дерево = РеквизитФормыВЗначение("ДеревоВыбора");
	
	Типы = ТипыЗначения.Типы();
	Если Типы.Количество() = 1 Тогда
		НоваяСтрока = Дерево.Строки.Добавить();
		НоваяСтрока.Представление = Типы[0];
		НоваяСтрока.Картинка = КэшКартинок.Справочник;
		СтрокиДерева = НоваяСтрока.Строки;
	Иначе
		СтрокиДерева = Дерево.Строки;
	КонецЕсли;
	
	ЗаполнитьРеквизитыАналитики(СтрокиДерева, ТипыЗначения, КэшКартинок);
	УдалитьНеиспользуемыеВетви(СтрокиДерева);
	
	ЗначениеВРеквизитФормы(Дерево, "ДеревоВыбора");
	
КонецПроцедуры

&НаСервере
Процедура УдалитьНеиспользуемыеВетви(Строки)
	
	Удалить = Новый Массив;
	Для Каждого Строка Из Строки Цикл
		Если НЕ Строка.Строки.Итог("Искомый", Истина)
			И НЕ Строка.Искомый Тогда
			Удалить.Добавить(Строка);
		Иначе
			УдалитьНеиспользуемыеВетви(Строка.Строки);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ЭлементМассива Из Удалить Цикл
		Строки.Удалить(ЭлементМассива);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыАналитики(ЭлементыСтрокиРодитель, ОписаниеТипов, КэшКартинок, Уровень = 0)
	
	Если Уровень = 4 Тогда
		Возврат;
	КонецЕсли;
	
	ТипыРеквизита = ОписаниеТипов.Типы();
	
	Для Каждого ТипРеквизита Из ТипыРеквизита Цикл
		
		МетаданныеЭлемента = Метаданные.НайтиПоТипу(ТипРеквизита);
		Если МетаданныеЭлемента = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТипыРеквизита.Количество() = 1 Тогда
			ЭлементыСтроки = ЭлементыСтрокиРодитель;
		Иначе
			НоваяСтрока = ЭлементыСтрокиРодитель.Добавить();
			НоваяСтрока.Представление = ТипРеквизита;
			НоваяСтрока.Значение = "<тип>";
			НоваяСтрока.Картинка = КэшКартинок.Справочник;
			ЭлементыСтроки = НоваяСтрока.Строки;
		КонецЕсли;
				
		Попытка
			Реквизиты = МетаданныеЭлемента.Реквизиты;
		Исключение
			Продолжить;
		КонецПопытки;
		
		МассивВалют = Новый Массив;
		Для Каждого Реквизит Из Реквизиты Цикл
			
			Типы = Реквизит.Тип.Типы();
			
			Если Типы.Найти(Тип("СправочникСсылка.ШаблоныЭтикетокИЦенников")) <> Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ЕстьИскомыйТип = Ложь;
			Если Типы.Количество() = 1 Тогда
				ИскомыйТип = ?(РежимРаботы = 1, Тип("СправочникСсылка.УпаковкиЕдиницыИзмерения"), Тип("СправочникСсылка.Валюты"));
				ЕстьИскомыйТип = Типы.Найти(ИскомыйТип) <> Неопределено;
			КонецЕсли;
			
			НоваяСтрока = ЭлементыСтроки.Добавить();
			НоваяСтрока.Представление = Реквизит.Синоним;
			НоваяСтрока.Значение = Реквизит.Имя;
			НоваяСтрока.Искомый = ЕстьИскомыйТип;
			НоваяСтрока.Картинка = КэшКартинок.Элемент;
			
			Если ЕстьИскомыйТип Тогда
				Продолжить;
			КонецЕсли;
			
			ЭлементыРеквизита = НоваяСтрока.Строки;
			
			ЗаполнитьРеквизитыАналитики(ЭлементыРеквизита, Реквизит.Тип, КэшКартинок, Уровень + 1);
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЭлементыДерева = ДеревоВыбора.ПолучитьЭлементы();
	Если ЭлементыДерева.Количество() Тогда
		ИДПервойСтроки = ЭлементыДерева[0].ПолучитьИдентификатор();
		Элементы.ДеревоВыбора.Развернуть(ИДПервойСтроки);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ДеревоВыбораВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбработатьВыбор(ВыбраннаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыбор(ВыбраннаяСтрока)
	
	СтрокаДерева = ДеревоВыбора.НайтиПоИдентификатору(ВыбраннаяСтрока);
	СтрокаПодходитДляВыбора = СтрокаДерева.Искомый ИЛИ ТипЗнч(СтрокаДерева.Значение) = Тип("СправочникСсылка.УпаковкиЕдиницыИзмерения");
	Если Не СтрокаПодходитДляВыбора Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Строка не может быть выбрана!';
										|en = 'Line cannot be selected.'"));
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(СтрокаДерева.Значение) = Тип("СправочникСсылка.УпаковкиЕдиницыИзмерения") Тогда
		Значение = СтрокаДерева.Значение;
		Представление = СтрокаДерева.ПредставлениеВариантаВыбора;
	Иначе
		Родитель = СтрокаДерева;
		Значение = "";
		Представление = "";
		Пока Родитель <> Неопределено Цикл
			Если Родитель.Значение <> "<тип>"
				И ЗначениеЗаполнено(Родитель.Значение) Тогда
				Значение = Родитель.Значение + ?(НЕ ПустаяСтрока(Значение), ".", "") + Значение;
				Представление = "[" + Родитель.Представление + "]" + 
					?(НЕ ПустаяСтрока(Представление), ".", "") + Представление;
			КонецЕсли;
			Родитель = Родитель.ПолучитьРодителя();
		КонецЦикла;
	КонецЕсли;
	
	Закрыть(Новый Структура("Значение, Представление", Значение, Представление));
	
КонецПроцедуры