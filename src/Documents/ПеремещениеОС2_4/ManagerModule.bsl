#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	МеханизмыДокумента.Добавить("ОсновныеСредства");
	МеханизмыДокумента.Добавить("РеестрДокументов");
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	МеханизмыДокумента.Добавить("ОбесценениеВНА");
	МеханизмыДокумента.Добавить("ОборотныеРегистрыУправленческогоУчета");
	
	ПеремещениеОСЛокализация.ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента);
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка, ДокументОбъект - ссылка на документ или объект, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура:
//     * Ключ - Строка - Имя таблицы.
//     * Значение - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Если ТипЗнч(Документ) = Тип("ДокументОбъект.ПеремещениеОС2_4") Тогда
		ДокументСсылка = Документ.Ссылка;
	Иначе
		ДокументСсылка = Документ;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда

		ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДопПараметры);
		
		ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры);
		
		ТекстЗапросаТаблицаМестонахождениеОС(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПервоначальныеСведенияОС(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПорядокУчетаОСБУ(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПорядокУчетаОСУУ(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПараметрыАмортизацииОСУУ(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаУзлыКомпонентыАмортизации(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДвиженияСоставЕГДС(ТекстыЗапроса, Регистры);
		
		//++ НЕ УТКА
		ТекстЗапросаТаблицаОтражениеДокументовВМеждународномУчете(ТекстыЗапроса, Регистры);
		//-- НЕ УТКА
		
		ПеремещениеОСЛокализация.ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры);
	
	КонецЕсли;
	
	ТаблицыДвижений = ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
	Возврат ТаблицыДвижений;
	
КонецФункции

// Формирует таблицы движений при отложенном проведении.
//
// Параметры:
//  МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Содержит временные таблицы, которые используются при формировании движений.
//
// Возвращаемое значение:
//  см. ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения
//
Функция ТаблицыОтложенногоФормированияДвижений(МенеджерВременныхТаблиц) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ПолучитьДанныеДокумента(Запрос);

	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения();
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла; 

	ТекстыЗапроса = Новый СписокЗначений;
	
	ТекстЗапросаТаблицаСтоимостьОС(ТекстыЗапроса);
	ТекстЗапросаТаблицаАмортизацияОС(ТекстыЗапроса);
	ТекстЗапросаТаблицаДвиженияДоходыРасходыПрочиеАктивыПассивы(ТекстыЗапроса);
	ТекстЗапросаТаблицаДвиженияПоПрочимАктивамПассивам(ТекстыЗапроса);
	
	// Регистры сведений для которых используется СрезПоследних
	ТекстЗапросаТаблицаПервоначальныеСведенияОС(ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПорядокУчетаОСБУ(ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПорядокУчетаОСУУ(ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПараметрыАмортизацииОСУУ(ТекстыЗапроса, Неопределено);
	
	//++ НЕ УТКА
	ТекстЗапросаТаблицаОтражениеДокументовВМеждународномУчете(ТекстыЗапроса, Неопределено);
	//-- НЕ УТКА
	
	ПеремещениеОСЛокализация.ТаблицыОтложенногоФормированияДвижений(ТекстыЗапроса);
	
	ТаблицыДвижений = ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса);
	
	Возврат ТаблицыДвижений;
	
КонецФункции

#КонецОбласти

#Область Команды

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Документы.ИзменениеПараметровОС2_4.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	ПеремещениеОСЛокализация.ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры);

КонецПроцедуры

// Добавляет команду создания документа "Перемещение ОС".
//
// Параметры:
//  КомандыСозданияНаОсновании - См. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Порядок	- Число - Порядок команды.
//
// Возвращаемое значение:
//	СтрокаТаблицыЗначений - добавленная команда
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании, Порядок = 0) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.ПеремещениеОС2_4) Тогда
		
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.ПеремещениеОС2_4.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.ПеремещениеОС2_4);
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьВнеоборотныеАктивы2_4";
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		КомандаСоздатьНаОсновании.Порядок = Порядок;
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	ПеремещениеОСЛокализация.ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры);

КонецПроцедуры

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Вычисляет значения свойств при изменении значений реквизитов.
// Используется в форме документа и при проверке заполнения.
//
// Параметры:
//  Объект					 - ДокументОбъект.ПринятиеКУчетуОС2_4, ДанныеФормыСтруктура - Данные документа.
//  ЕстьСобственныеОС        - Булево - Истина, если в документе есть собственные ОС.
//  ИзмененныеРеквизиты		 - Строка - Список измененных реквизитов.
//
// Возвращаемое значение:
//  ТаблицаЗначений - Содержит новые значения свойств реквизитов:
//					*ИмяРеквизита	- Строка	- Имя реквизита объекта
//					*ИмяЭлемента	- Строка	- Имя элемента формы
//					*Свойство	- Строка	- Свойство, например, "Видимость", "ТолькоПросмотр"
//					*Значение	- Булево	- Значение свойства.
//
Функция ЗначенияСвойствЗависимыхРеквизитов(Объект, ЕстьСобственныеОС, ИзмененныеРеквизиты = "") Экспорт

	СтруктураИзмененныхРеквизитов = Новый Структура(ИзмененныеРеквизиты);
	ОбновитьВсе = СтруктураИзмененныхРеквизитов.Количество() = 0;
	
	ПараметрыРеквизитовОбъекта = Новый Массив;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("ЕстьСобственныеОС") 
		ИЛИ ОбновитьВсе Тогда
		
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"НачислениеАмортизации", 
			"НачислениеАмортизации", 
			"Видимость", 
			ЕстьСобственныеОС, 
			ПараметрыРеквизитовОбъекта);
		
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"СтатьяРасходов", 
			"СтатьяРасходов", 
			"Видимость", 
			ЕстьСобственныеОС, 
			ПараметрыРеквизитовОбъекта);
		
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"АналитикаРасходов", 
			"АналитикаРасходов", 
			"Видимость", 
			ЕстьСобственныеОС, 
			ПараметрыРеквизитовОбъекта);
			
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"ИзменяетсяОтражениеРасходовПоНалогу", 
			"ИзменяетсяОтражениеРасходовПоНалогу", 
			"Видимость", 
			ЕстьСобственныеОС, 
			ПараметрыРеквизитовОбъекта);
		
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"СтатьяРасходовНалог", 
			"СтатьяРасходовНалог", 
			"Видимость", 
			ЕстьСобственныеОС, 
			ПараметрыРеквизитовОбъекта);
			
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"АналитикаРасходовНалог", 
			"АналитикаРасходовНалог", 
			"Видимость", 
			ЕстьСобственныеОС, 
			ПараметрыРеквизитовОбъекта);
	
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"АдресМестонахожденияПолучателя", 
			"АдресМестонахожденияПолучателя", 
			"Видимость", 
			ЕстьСобственныеОС, 
			ПараметрыРеквизитовОбъекта);
		              
	КонецЕсли;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("НачислениеАмортизации") 
		ИЛИ ОбновитьВсе Тогда
		
		СтатьяРасходовДоступна = (Объект.НачислениеАмортизации = 1);
		АналитикаРасходовДоступна = (Объект.НачислениеАмортизации = 1);
		
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"СтатьяРасходов", 
			"СтатьяРасходов", 
			"ТолькоПросмотрБезОтметкиНезаполненного", 
			НЕ СтатьяРасходовДоступна, 
			ПараметрыРеквизитовОбъекта);
		
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"АналитикаРасходов", 
			"АналитикаРасходов", 
			"ТолькоПросмотр", 
			НЕ АналитикаРасходовДоступна, 
			ПараметрыРеквизитовОбъекта);
		
	КонецЕсли;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("ХозяйственнаяОперация") 
		ИЛИ ОбновитьВсе Тогда
		
		ЭтоПеремещениеУзлов = Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеремещениеУзлов;
		
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"ОС.ОсновноеСредствоВладелец", "ОСОсновноеСредствоВладелец", "Видимость", ЭтоПеремещениеУзлов, ПараметрыРеквизитовОбъекта);
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"ОС.ОсновноеСредствоВладелецТекущее", "ОСОсновноеСредствоВладелецТекущее", "Видимость", ЭтоПеремещениеУзлов, ПараметрыРеквизитовОбъекта);
		
		Если ЭтоПеремещениеУзлов Тогда
			ЗаголовокОсновногоСредства = НСтр("ru = 'ОС-узел';
												|en = 'Fixed asset sub-asset'");
		Иначе
			ЗаголовокОсновногоСредства = НСтр("ru = 'Основное средство';
												|en = 'Fixed asset'");
		КонецЕсли;
		
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"ОС.ОсновноеСредство", "ОСОсновноеСредство", "Заголовок", ЗаголовокОсновногоСредства, ПараметрыРеквизитовОбъекта);
		
	КонецЕсли;

	ПеремещениеОСЛокализация.ЗначенияСвойствЗависимыхРеквизитов(Объект, ЕстьСобственныеОС, СтруктураИзмененныхРеквизитов, ПараметрыРеквизитовОбъекта);
	
	Возврат ПараметрыРеквизитовОбъекта;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт
	
	ИсточникиДанных = Новый Соответствие;
	Возврат ИсточникиДанных;
	
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.ПеремещениеОС2_4";
	
	ЗначенияПараметров = ЗначенияПараметровПроведения();
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("НомерНаПечать", """""");
	ПереопределениеРасчетаПараметров.Вставить("ИнформацияПоОтправителю",  """""");
	ПереопределениеРасчетаПараметров.Вставить("ИнформацияПоПолучателю", """""");
	
	ВЗапросеЕстьИсточник = Истина;
	ТекстыЗапросаВременныхТаблиц = Новый Соответствие;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	ИначеЕсли ИмяРегистра = "ДокументыПоОС" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	Иначе
		
		ТекстЗапроса = ПеремещениеОСЛокализация.АдаптированныйТекстЗапросаДвиженийПоРегистру(
							ИмяРегистра, ПереопределениеРасчетаПараметров, СинонимТаблицыДокумента, ТекстыЗапросаВременныхТаблиц);
		
	КонецЕсли;
				
	Если ТекстЗапроса = Неопределено Тогда
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';
								|en = 'In document %ПолноеИмяДокумента%, adaptation of request for generating records of register %ИмяРегистра% is not implemented.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов"
		ИЛИ ИмяРегистра = "ДокументыПоОС" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ВЗапросеЕстьИсточник,
										ПереопределениеРасчетаПараметров);
	Иначе	
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ПереопределениеРасчетаПараметров,
										ТекстыЗапросаВременныхТаблиц);
	КонецЕсли; 

	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДопПараметры)
	
	ПроведениеДокументов.ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДопПараметры);
	
	СформироватьТаблицуВтСписокДокументов(Запрос);
	ПолучитьДанныеДокумента(Запрос);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Период КАК Период,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.ОрганизацияПолучатель КАК ОрганизацияПолучатель,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ДанныеДокумента.ПодразделениеПолучатель КАК ПодразделениеПолучатель
	|	
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения(Реквизиты);
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла; 
	
КонецПроцедуры

Процедура СформироватьТаблицуВтСписокДокументов(Запрос)
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СписокДокументов.Ссылка КАК Ссылка,
	|	СписокДокументов.Организация КАК Организация,
	|	СписокДокументов.Дата КАК Период
	|
	|ПОМЕСТИТЬ ВтСписокДокументов
	|
	|ИЗ
	|	Документ.ПеремещениеОС2_4 КАК СписокДокументов
	|
	|ГДЕ
	|	СписокДокументов.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|";
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПолучитьДанныеДокумента(Запрос)
	
	СписокЗапросов = Новый Массив;
	
	#Область ДанныеДокументаРеквизиты
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                         КАК Ссылка,
	|	ДанныеДокумента.Дата                           КАК Период,
	|	ДанныеДокумента.Номер                          КАК Номер,
	|	ДанныеДокумента.Организация                    КАК Организация,
	|	ДанныеДокумента.ОрганизацияПолучатель          КАК ОрганизацияПолучатель,
	|	ДанныеДокумента.МОЛПолучатель                  КАК МОЛПолучатель,
	|	ДанныеДокумента.Ответственный                  КАК Ответственный,
	|	ДанныеДокумента.Комментарий                    КАК Комментарий,
	|	ДанныеДокумента.Проведен                       КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления                КАК ПометкаУдаления,
	|	ДанныеДокумента.ОтражатьВРеглУчете             КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете              КАК ОтражатьВУпрУчете,
	|	ДанныеДокумента.НачислениеАмортизации          КАК НачислятьАмортизацию,
	|	ДанныеДокумента.СтатьяРасходов                 КАК СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходов              КАК АналитикаРасходов,
	|	ДанныеДокумента.СтатьяРасходовНалог            КАК СтатьяРасходовНалог,
	|	ДанныеДокумента.АналитикаРасходовНалог         КАК АналитикаРасходовНалог,
	|	ДанныеДокумента.Подразделение                  КАК Подразделение,
	|	ДанныеДокумента.ПодразделениеПолучатель        КАК ПодразделениеПолучатель,
	|	ДанныеДокумента.ХозяйственнаяОперация          КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.ДокументОснование              КАК ДокументОснование,
	|	ДанныеДокумента.СобытиеОС                      КАК СобытиеОС,
	|	ДанныеДокумента.ИзменяетсяОтражениеРасходовПоНалогу    КАК ИзменяетсяОтражениеРасходовПоНалогу,
	|	ДанныеДокумента.АдресМестонахожденияПолучателя         КАК АдресМестонахождения,
	|	ДанныеДокумента.АдресМестонахожденияПолучателяЗначение КАК АдресМестонахожденияЗначение,
	|	ДанныеДокумента.ЕГДСПолучатель КАК ЕГДСПолучатель,
	|
	|	НастройкиХозяйственныхОпераций.Ссылка КАК НастройкаХозяйственнойОперации,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеСтоимостиОСвДругуюОрганизацию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИзменениеПараметровСтоимостиОС)
	|	КОНЕЦ КАК ХО_Стоимость_Отправитель,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеСтоимостиОСизДругойОрганизации)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИзменениеПараметровСтоимостиОС)
	|	КОНЕЦ КАК ХО_Стоимость_Получатель,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеАмортизацииОСвДругуюОрганизацию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИзменениеПараметровАмортизацииОС)
	|	КОНЕЦ КАК ХО_Амортизация_Отправитель,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеАмортизацииОСизДругойОрганизации)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИзменениеПараметровАмортизацииОС)
	|	КОНЕЦ КАК ХО_Амортизация_Получатель,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеРезерваПереоценкиОСвДругуюОрганизацию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИзменениеРезерваПереоценкиСтоимостиОС)
	|	КОНЕЦ КАК ХО_РезервПереоценкиСтоимости_Отправитель,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеРезерваПереоценкиОСизДругойОрганизации)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИзменениеРезерваПереоценкиСтоимостиОС)
	|	КОНЕЦ КАК ХО_РезервПереоценкиСтоимости_Получатель,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПеремещениеСтоимостиОСвДругуюОрганизацию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ИзменениеПараметровСтоимостиОС)
	|	КОНЕЦ КАК НастройкаХО_Стоимость_Отправитель,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПеремещениеСтоимостиОСизДругойОрганизации)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ИзменениеПараметровСтоимостиОС)
	|	КОНЕЦ КАК НастройкаХО_Стоимость_Получатель,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПеремещениеАмортизацииОСвДругуюОрганизацию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ИзменениеПараметровАмортизацииОС)
	|	КОНЕЦ КАК НастройкаХО_Амортизация_Отправитель,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПеремещениеАмортизацииОСизДругойОрганизации)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ИзменениеПараметровАмортизацииОС)
	|	КОНЕЦ КАК НастройкаХО_Амортизация_Получатель,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПеремещениеРезерваПереоценкиОСвДругуюОрганизацию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ИзменениеРезерваПереоценкиСтоимостиОС)
	|	КОНЕЦ КАК НастройкаХО_РезервПереоценкиСтоимости_Отправитель,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПеремещениеРезерваПереоценкиОСизДругойОрганизации)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ИзменениеРезерваПереоценкиСтоимостиОС)
	|	КОНЕЦ КАК НастройкаХО_РезервПереоценкиСтоимости_Получатель
	|
	|ПОМЕСТИТЬ ДанныеДокументаРеквизиты
	|
	|ИЗ
	|	Документ.ПеремещениеОС2_4 КАК ДанныеДокумента
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НастройкиХозяйственныхОпераций КАК НастройкиХозяйственныхОпераций
	|		ПО ДанныеДокумента.ХозяйственнаяОперация = НастройкиХозяйственныхОпераций.ХозяйственнаяОперация
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка В (
	|		ВЫБРАТЬ
	|			ВтСписокДокументов.Ссылка
	|		ИЗ
	|			ВтСписокДокументов КАК ВтСписокДокументов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ДанныеДокументаТаблицаОС
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаДокумента.ОсновноеСредство КАК ОбъектУчета,
	|	ТаблицаДокумента.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаДокумента.ОсновноеСредствоВладелец КАК ОсновноеСредствоВладелец
	|
	|ПОМЕСТИТЬ ДанныеДокументаТаблицаОС
	|
	|ИЗ
	|	Документ.ПеремещениеОС2_4.ОС КАК ТаблицаДокумента
	|
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В (
	|		ВЫБРАТЬ
	|			ВтСписокДокументов.Ссылка
	|		ИЗ
	|			ВтСписокДокументов КАК ВтСписокДокументов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	ОсновноеСредство
	|";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	Запрос.Текст = СтрСоединить(СписокЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)

	ЗначенияПараметровПроведения = Новый Структура;
	ЗначенияПараметровПроведения.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ПеремещениеОС2_4"));
	ЗначенияПараметровПроведения.Вставить("НазваниеДокумента", НСтр("ru = 'Перемещение ОС';
																	|en = 'Transfer fixed assets'"));
	ЗначенияПараметровПроведения.Вставить("СтатьяАП_ОС", ПланыВидовХарактеристик.СтатьиАктивовПассивов.ОсновныеСредства);
	ЗначенияПараметровПроведения.Вставить("СтатьяАП_ПрибыльУбытки", ПланыВидовХарактеристик.СтатьиАктивовПассивов.ПрибылиИУбытки);
	
	Если Реквизиты <> Неопределено Тогда
		
		ЗначенияПараметровПроведения.Вставить("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
		
		ВнеоборотныеАктивыСлужебный.УстановитьЗначенияПараметровПроведенияПриПеремещении(Реквизиты, ЗначенияПараметровПроведения);
		
	КонецЕсли; 
	
	Возврат ЗначенияПараметровПроведения;
	
КонецФункции

Функция ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДокументыПоОС";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(ТаблицаОС.НомерСтроки-1, 0)            КАК НомерЗаписи,
	|	ДанныеДокумента.Ссылка                          КАК Ссылка,
	|	ДанныеДокумента.Дата                            КАК Дата,
	|	ДанныеДокумента.Организация                     КАК Организация,
	|	ДанныеДокумента.Подразделение                   КАК Подразделение,
	|	&ИдентификаторМетаданных                        КАК ТипСсылки,
	|	ДанныеДокумента.ХозяйственнаяОперация           КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Проведен                        КАК Проведен,
	|	ЛОЖЬ                                            КАК ДополнительнаяЗапись,
	|	ДанныеДокумента.ОтражатьВРеглУчете              КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете               КАК ОтражатьВУпрУчете,
	|	ДанныеДокумента.СобытиеОС                       КАК СобытиеОС,
	|	ТаблицаОС.ОсновноеСредство                      КАК ОсновноеСредство
	|
	|ИЗ
	|	Документ.ПеремещениеОС2_4 КАК ДанныеДокумента
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПеремещениеОС2_4.ОС КАК ТаблицаОС
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ТаблицаОС.НомерСтроки-1, 0)            КАК НомерЗаписи,
	|	ДанныеДокумента.Ссылка                          КАК Ссылка,
	|	ДанныеДокумента.Дата                            КАК Дата,
	|	ДанныеДокумента.ОрганизацияПолучатель           КАК Организация,
	|	ДанныеДокумента.ПодразделениеПолучатель         КАК Подразделение,
	|	&ИдентификаторМетаданных                        КАК ТипСсылки,
	|	ДанныеДокумента.ХозяйственнаяОперация           КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Проведен                        КАК Проведен,
	|	ИСТИНА                                          КАК ДополнительнаяЗапись,
	|	ДанныеДокумента.ОтражатьВРеглУчете              КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете               КАК ОтражатьВУпрУчете,
	|	ДанныеДокумента.СобытиеОС                       КАК СобытиеОС,
	|	ТаблицаОС.ОсновноеСредство                      КАК ОсновноеСредство
	|
	|ИЗ
	|	Документ.ПеремещениеОС2_4 КАК ДанныеДокумента
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПеремещениеОС2_4.ОС КАК ТаблицаОС
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|	И ДанныеДокумента.ОрганизацияПолучатель <> ДанныеДокумента.Организация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ТаблицаОС.НомерСтроки-1, 0)            КАК НомерЗаписи,
	|	ДанныеДокумента.Ссылка                          КАК Ссылка,
	|	ДанныеДокумента.Дата                            КАК Дата,
	|	ДанныеДокумента.Организация                     КАК Организация,
	|	ДанныеДокумента.Подразделение                   КАК Подразделение,
	|	&ИдентификаторМетаданных                        КАК ТипСсылки,
	|	ДанныеДокумента.ХозяйственнаяОперация           КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Проведен                        КАК Проведен,
	|	ИСТИНА                                          КАК ДополнительнаяЗапись,
	|	ДанныеДокумента.ОтражатьВРеглУчете              КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете               КАК ОтражатьВУпрУчете,
	|	ДанныеДокумента.СобытиеОС                       КАК СобытиеОС,
	|	ТаблицаОС.ОсновноеСредствоВладелец              КАК ОсновноеСредство
	|
	|ИЗ
	|	Документ.ПеремещениеОС2_4 КАК ДанныеДокумента
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПеремещениеОС2_4.ОС КАК ТаблицаОС
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПеремещениеОС2_4.ОС КАК ТаблицаОСПредыдущие
	|		ПО ТаблицаОСПредыдущие.Ссылка = ТаблицаОС.Ссылка
	|			И ТаблицаОСПредыдущие.ОсновноеСредствоВладелец = ТаблицаОС.ОсновноеСредствоВладелец
	|			И ТаблицаОСПредыдущие.НомерСтроки < ТаблицаОС.НомерСтроки
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеУзлов)
	|	И ТаблицаОС.ОсновноеСредствоВладелец <> ЗНАЧЕНИЕ(Справочник.ОбъектыЭксплуатации.ПустаяСсылка)
	|	И ТаблицаОСПредыдущие.Ссылка ЕСТЬ NULL
	|";	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Дата КАК ДатаДокументаИБ,
	|	ДанныеДокумента.Номер КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных КАК ТипСсылки,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ДанныеДокумента.Ответственный КАК Ответственный,
	|	ДанныеДокумента.Комментарий КАК Комментарий,
	|	ДанныеДокумента.Проведен КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления КАК ПометкаУдаления,
	|	ЛОЖЬ КАК ДополнительнаяЗапись,
	|	&ИнформацияПоПолучателю КАК Дополнительно,
	|	ДанныеДокумента.Дата КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать КАК НомерПервичногоДокумента,
	|	ЛОЖЬ КАК СторноИсправление,
	|	НЕОПРЕДЕЛЕНО КАК СторнируемыйДокумент,
	|	НЕОПРЕДЕЛЕНО КАК ИсправляемыйДокумент,
	|	ДанныеДокумента.Дата   КАК ДатаОтраженияВУчете,
	|	НЕОПРЕДЕЛЕНО КАК Приоритет
	|
	|ИЗ
	|	Документ.ПеремещениеОС2_4 КАК ДанныеДокумента
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка,
	|	ДанныеДокумента.Дата,
	|	ДанныеДокумента.Номер,
	|	&ИдентификаторМетаданных,
	|	ДанныеДокумента.ОрганизацияПолучатель,
	|	ДанныеДокумента.ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО,
	|	ДанныеДокумента.ПодразделениеПолучатель,
	|	ДанныеДокумента.Ответственный,
	|	ДанныеДокумента.Комментарий,
	|	ДанныеДокумента.Проведен,
	|	ДанныеДокумента.ПометкаУдаления,
	|	ИСТИНА,
	|	&ИнформацияПоОтправителю,
	|	ДанныеДокумента.Дата,
	|	&НомерНаПечать,
	|	ЛОЖЬ КАК СторноИсправление,
	|	НЕОПРЕДЕЛЕНО КАК СторнируемыйДокумент,
	|	НЕОПРЕДЕЛЕНО КАК ИсправляемыйДокумент,
	|	ДанныеДокумента.Дата,
	|	НЕОПРЕДЕЛЕНО КАК Приоритет
	|
	|ИЗ
	|	Документ.ПеремещениеОС2_4 КАК ДанныеДокумента
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|	И ДанныеДокумента.ОрганизацияПолучатель <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ТекстЗапросаТаблицаМестонахождениеОС(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "МестонахождениеОС";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтРасширеннаяТаблицаОС(ТекстыЗапроса);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтМестонахождениеОС(ТекстыЗапроса, "ВтРасширеннаяТаблицаОС");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                       КАК Регистратор,
	|	ДанныеДокумента.Период                       КАК Период,
	|
	|	ВЫБОР КОГДА ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|		ТОГДА ДанныеДокумента.ОрганизацияПолучатель
	|		ИНАЧЕ ДанныеДокумента.Организация
	|	КОНЕЦ                                        КАК Организация,
	|
	|	ТаблицаОС.ОсновноеСредство                   КАК ОсновноеСредство,
	|	ДанныеДокумента.МОЛПолучатель                КАК МОЛ,
	|	ДанныеДокумента.ПодразделениеПолучатель      КАК Местонахождение,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.АдресМестонахождения <> """"
	|			ТОГДА ДанныеДокумента.АдресМестонахождения
	|		ИНАЧЕ ВтМестонахождениеОС.АдресМестонахождения
	|	КОНЕЦ КАК АдресМестонахождения,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.АдресМестонахождения <> """"
	|			ТОГДА ДанныеДокумента.АдресМестонахожденияЗначение
	|		ИНАЧЕ ВтМестонахождениеОС.АдресМестонахожденияЗначение
	|	КОНЕЦ КАК АдресМестонахожденияЗначение,
	|
	|	ВтМестонахождениеОС.Арендатор                      КАК Арендатор,
	|	ВтМестонахождениеОС.ПодразделениеАрендатора        КАК ПодразделениеАрендатора,
	|	ВтМестонахождениеОС.МОЛАрендатора                  КАК МОЛАрендатора,
	|
	|	ДанныеДокумента.ОтражатьВРеглУчете                 КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете                  КАК ОтражатьВУпрУчете
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасширеннаяТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтМестонахождениеОС КАК ВтМестонахождениеОС
	|		ПО ВтМестонахождениеОС.Ссылка = ТаблицаОС.Ссылка
	|			И ВтМестонахождениеОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПервоначальныеСведенияОС(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПервоначальныеСведенияОС";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтРасширеннаяТаблицаОС(ТекстыЗапроса);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПервоначальныеСведенияОС(ТекстыЗапроса, "ВтРасширеннаяТаблицаОС");
	
	ТекстЗапроса = ПеремещениеОСЛокализация.ТекстЗапросаТаблицаПервоначальныеСведенияОС();
	
	Если ТекстЗапроса = Неопределено Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка КАК Регистратор,
		|	ДанныеДокумента.Период КАК Период,
		|	ДанныеДокумента.ОрганизацияПолучатель КАК Организация,
		|	ПервоначальныеСведенияОС.ОсновноеСредство КАК ОсновноеСредство,
		|
		|	ПервоначальныеСведенияОС.ПорядокУчетаУУ КАК ПорядокУчетаУУ,
		|	ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьБУ КАК ПервоначальнаяСтоимостьБУ,
		|	ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьУУ КАК ПервоначальнаяСтоимостьУУ,
		|	ПервоначальныеСведенияОС.Контрагент КАК Контрагент,
		|	ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюБУ КАК ДатаВводаВЭксплуатациюБУ,
		|	ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюУУ КАК ДатаВводаВЭксплуатациюУУ,
		|	ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюУУ КАК ДокументВводаВЭксплуатациюУУ,
		|	ПервоначальныеСведенияОС.ДокументСнятияСУчетаУУ КАК ДокументСнятияСУчетаУУ
		|
		|ИЗ
		|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
		|		ПО ПервоначальныеСведенияОС.Ссылка = ДанныеДокумента.Ссылка
		|
		|ГДЕ
		|	ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
		|";
	
	КонецЕсли;
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПорядокУчетаОСБУ(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПорядокУчетаОСБУ";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтРасширеннаяТаблицаОС(ТекстыЗапроса);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПорядокУчетаОСБУ(ТекстыЗапроса, "ВтРасширеннаяТаблицаОС");
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПервоначальныеСведенияОС(ТекстыЗапроса, "ВтРасширеннаяТаблицаОС");
	
	ТекстЗапроса = ПеремещениеОСЛокализация.ТекстЗапросаТаблицаПорядокУчетаОСБУ(ТекстыЗапроса);
	
	Если ТекстЗапроса = Неопределено Тогда
	
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка КАК Регистратор,
		|	ДанныеДокумента.Период КАК Период,
		|	ДанныеДокумента.Организация КАК Организация,
		|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
		|
		|	ПорядокУчета.СостояниеБУ КАК СостояниеБУ,
		|
		// НачислятьАмортизациюБУ
		|	ВЫБОР 
		|		КОГДА ДанныеДокумента.НачислятьАмортизацию = 0 
		|			ТОГДА ПорядокУчета.НачислятьАмортизациюБУ
		|		КОГДА ДанныеДокумента.НачислятьАмортизацию = 1 
		|			И (ПорядокУчета.НачислятьАмортизациюБУ
		|				ИЛИ ПервоначальныеСведенияОС.ПорядокУчетаБУ В (
		|					ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеАмортизации),
		|					ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.АмортизацияНачислена)))
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК НачислятьАмортизациюБУ,
		|
		// СтатьяРасходовБУ
		|	ВЫБОР ДанныеДокумента.НачислятьАмортизацию
		|		КОГДА 0 ТОГДА ПорядокУчета.СтатьяРасходовБУ
		|		КОГДА 1 ТОГДА ДанныеДокумента.СтатьяРасходов
		|		КОГДА 2 ТОГДА НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК СтатьяРасходовБУ,
		|
		// АналитикаРасходовБУ
		|	ВЫБОР ДанныеДокумента.НачислятьАмортизацию
		|		КОГДА 0 ТОГДА ПорядокУчета.АналитикаРасходовБУ
		|		КОГДА 1 ТОГДА ДанныеДокумента.АналитикаРасходов
		|		КОГДА 2 ТОГДА НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК АналитикаРасходовБУ,
		|
		// СпособОтраженияРасходовБУ
		|	ВЫБОР ДанныеДокумента.НачислятьАмортизацию
		|		КОГДА 0 ТОГДА ПорядокУчета.СпособОтраженияРасходовБУ
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК СпособОтраженияРасходовБУ
		|
		|ИЗ
		|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
		| 
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасширеннаяТаблицаОС КАК ТаблицаОС
		|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПорядокУчетаОСБУ КАК ПорядокУчета
		|		ПО ПорядокУчета.Ссылка = ТаблицаОС.Ссылка
		|			И ПорядокУчета.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
		|		ПО ПервоначальныеСведенияОС.Ссылка = ТаблицаОС.Ссылка
		|			И ПервоначальныеСведенияОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
		|
		|ГДЕ
		|	ДанныеДокумента.ОтражатьВРеглУчете
		|	И ДанныеДокумента.НачислятьАмортизацию <> 0
		|";
				
	КонецЕсли;
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПорядокУчетаОСУУ(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПорядокУчетаОСУУ";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтРасширеннаяТаблицаОС(ТекстыЗапроса);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПорядокУчетаОСУУ(ТекстыЗапроса, "ВтРасширеннаяТаблицаОС");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Регистратор,
	|	ДанныеДокумента.Период КАК Период,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ВтПорядокУчетаОСУУ.ОсновноеСредство КАК ОсновноеСредство,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.СнятоСУчета) КАК Состояние,
	|	ЛОЖЬ КАК НачислятьАмортизациюУУ,
	|	ЛОЖЬ КАК СтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
	|	ВтПорядокУчетаОСУУ.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|	ВтПорядокУчетаОСУУ.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ВтПорядокУчетаОСУУ.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПорядокУчетаОСУУ КАК ВтПорядокУчетаОСУУ
	|		ПО ВтПорядокУчетаОСУУ.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ОтражатьВУпрУчете
	|	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Регистратор,
	|	ДанныеДокумента.Период КАК Период,
	|
	|	ВЫБОР КОГДА ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|		ТОГДА ДанныеДокумента.ОрганизацияПолучатель
	|		ИНАЧЕ ДанныеДокумента.Организация
	|	КОНЕЦ КАК Организация,
	|
	|	ВтПорядокУчетаОСУУ.ОсновноеСредство КАК ОсновноеСредство,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету) КАК Состояние,
	|
	|	ВЫБОР ДанныеДокумента.НачислятьАмортизацию
	|		КОГДА 0 ТОГДА ВтПорядокУчетаОСУУ.НачислятьАмортизациюУУ
	|		КОГДА 1 ТОГДА ИСТИНА
	|		КОГДА 2 ТОГДА ЛОЖЬ
	|	КОНЕЦ КАК НачислятьАмортизациюУУ,
	|
	|	ВЫБОР ДанныеДокумента.НачислятьАмортизацию
	|		КОГДА 0 ТОГДА ВтПорядокУчетаОСУУ.СтатьяРасходов
	|		КОГДА 1 ТОГДА ДанныеДокумента.СтатьяРасходов
	|		КОГДА 2 ТОГДА НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СтатьяРасходов,
	|
	|	ВЫБОР ДанныеДокумента.НачислятьАмортизацию
	|		КОГДА 0 ТОГДА ВтПорядокУчетаОСУУ.АналитикаРасходов
	|		КОГДА 1 ТОГДА ДанныеДокумента.АналитикаРасходов
	|		КОГДА 2 ТОГДА НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК АналитикаРасходов,
	|
	|	ВЫБОР ДанныеДокумента.НачислятьАмортизацию
	|		КОГДА 0 ТОГДА ВтПорядокУчетаОСУУ.СпособОтраженияРасходов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СпособОтраженияРасходов,
	|
	|	ВЫБОР ДанныеДокумента.НачислятьАмортизацию
	|		КОГДА 0 ТОГДА ВтПорядокУчетаОСУУ.ПередаватьРасходыВДругуюОрганизацию
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПередаватьРасходыВДругуюОрганизацию,
	|
	|	ВЫБОР ДанныеДокумента.НачислятьАмортизацию
	|		КОГДА 0 ТОГДА ВтПорядокУчетаОСУУ.ОрганизацияПолучательРасходов
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|	КОНЕЦ КАК ОрганизацияПолучательРасходов
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПорядокУчетаОСУУ КАК ВтПорядокУчетаОСУУ
	|		ПО ВтПорядокУчетаОСУУ.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ОтражатьВУпрУчете
	|	И (ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|			ИЛИ ДанныеДокумента.НачислятьАмортизацию <> 0)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПараметрыАмортизацииОСУУ(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПараметрыАмортизацииОСУУ";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтРасширеннаяТаблицаОС(ТекстыЗапроса);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыАмортизацииОСУУ(ТекстыЗапроса, "ВтРасширеннаяТаблицаОС");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Регистратор,
	|	ДанныеДокумента.Период КАК Период,
	|	ДанныеДокумента.ОрганизацияПолучатель КАК Организация,
	|
	|	втПараметрыАмортизацииОСУУ.ОсновноеСредство КАК ОсновноеСредство,
	|	втПараметрыАмортизацииОСУУ.СрокИспользования КАК СрокИспользования,
	|	втПараметрыАмортизацииОСУУ.СрокИспользованияДляВычисленияАмортизации КАК СрокИспользованияДляВычисленияАмортизации,
	|	втПараметрыАмортизацииОСУУ.КоэффициентУскорения КАК КоэффициентУскорения,
	|	втПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации КАК МетодНачисленияАмортизации,
	|	втПараметрыАмортизацииОСУУ.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость,
	|	втПараметрыАмортизацииОСУУ.ЛиквидационнаяСтоимостьРегл КАК ЛиквидационнаяСтоимостьРегл,
	|	втПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения КАК ДатаПоследнегоИзменения,
	|	втПараметрыАмортизацииОСУУ.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	втПараметрыАмортизацииОСУУ.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|	втПараметрыАмортизацииОСУУ.СтоимостьДляВычисленияАмортизацииРегл КАК СтоимостьДляВычисленияАмортизацииРегл
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПараметрыАмортизацииОСУУ КАК втПараметрыАмортизацииОСУУ
	|		ПО втПараметрыАмортизацииОСУУ.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|	И ДанныеДокумента.ОтражатьВУпрУчете
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаСтоимостьОС(ТекстыЗапроса)

	ИмяРегистра = "СтоимостьОС";
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтРасширеннаяТаблицаОС(ТекстыЗапроса);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыУчетаОС(ТекстыЗапроса, "ВтРасширеннаяТаблицаОС");
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтАрендованныеОС(ТекстыЗапроса, "ВтРасширеннаяТаблицаОС");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)      КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                      КАК Регистратор,
	|	ДанныеДокумента.Период                      КАК Период,
	|	ДанныеДокумента.ХО_Стоимость_Отправитель    КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация                 КАК Организация,
	|	ТаблицаОС.ОсновноеСредство                  КАК ОсновноеСредство,
	|	ДанныеДокумента.Подразделение               КАК Подразделение,
	|	ВтПараметрыУчетаОС.ГруппаФинансовогоУчета   КАК ГруппаФинансовогоУчета,
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	ВтПараметрыУчетаОС.Арендатор                КАК Арендатор,
	|
	|	СтоимостьОС.Стоимость                       КАК Стоимость,
	|	СтоимостьОС.СтоимостьРегл                   КАК СтоимостьРегл,
	|	СтоимостьОС.СтоимостьНУ                     КАК СтоимостьНУ,
	|	СтоимостьОС.СтоимостьПР                     КАК СтоимостьПР,
	|	СтоимостьОС.СтоимостьВР                     КАК СтоимостьВР,
	|	СтоимостьОС.СтоимостьЦФ                     КАК СтоимостьЦФ,
	|	СтоимостьОС.СтоимостьНУЦФ                   КАК СтоимостьНУЦФ,
	|	СтоимостьОС.СтоимостьПРЦФ                   КАК СтоимостьПРЦФ,
	|	СтоимостьОС.СтоимостьВРЦФ                   КАК СтоимостьВРЦФ,
	|	СтоимостьОС.АмортизационнаяПремия           КАК АмортизационнаяПремия,
	|	СтоимостьОС.ЗалоговаяСтоимость              КАК ЗалоговаяСтоимость,
	|	СтоимостьОС.ПредварительнаяСтоимость        КАК ПредварительнаяСтоимость,
	|	СтоимостьОС.ПредварительнаяСтоимостьРегл    КАК ПредварительнаяСтоимостьРегл,
	|	СтоимостьОС.РезервПереоценкиСтоимости       КАК РезервПереоценкиСтоимости,
	|	СтоимостьОС.РезервПереоценкиСтоимостиРегл   КАК РезервПереоценкиСтоимостиРегл,
	|	СтоимостьОС.НеУчитываемаяСтоимостьНУ        КАК НеУчитываемаяСтоимостьНУ,
	|	0                                           КАК ПриростСтоимости,
	|	0                                           КАК ПриростСтоимостиРегл,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|			ТОГДА ДанныеДокумента.ОрганизацияПолучатель
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                       КАК КорОрганизация,
	|
	|	ДанныеДокумента.ПодразделениеПолучатель     КАК КорПодразделение,
	|
	|	ВЫБОР
	|		КОГДА НЕ АрендованныеОС.Контрагент ЕСТЬ NULL
	|			ТОГДА АрендованныеОС.Контрагент
	|		ИНАЧЕ ВтПараметрыУчетаОС.Арендатор
	|	КОНЕЦ КАК КорКонтрагент,
	|
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности  КАК КорНаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                КАК КорСтатьяРасходов,
	|	Неопределено                                КАК КорАналитикаРасходов,
	|	ВтПараметрыУчетаОС.ГруппаФинансовогоУчета   КАК КорГруппаФинансовогоУчета,
	|	ДанныеДокумента.ОтражатьВРеглУчете          КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете           КАК ОтражатьВУпрУчете,
	|	
	|	ТаблицаОС.ИдентификаторСтроки               КАК ИдентификаторФинЗаписи,
	|	ДанныеДокумента.НастройкаХО_Стоимость_Отправитель КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасширеннаяТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПараметрыУчетаОС КАК ВтПараметрыУчетаОС
	|		ПО ВтПараметрыУчетаОС.Ссылка = ТаблицаОС.Ссылка
	|			И ВтПараметрыУчетаОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьОС
	|		ПО СтоимостьОС.Ссылка = ТаблицаОС.Ссылка
	|			И СтоимостьОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втАрендованныеОС КАК АрендованныеОС
	|		ПО АрендованныеОС.Ссылка = ТаблицаОС.Ссылка
	|			И АрендованныеОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|			И АрендованныеОС.Состояние В (
	|				ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ВАренде),
	|				ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКЗабалансовомуУчету))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)      КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                      КАК Регистратор,
	|	ДанныеДокумента.Период                      КАК Период,
	|	ДанныеДокумента.ХО_Стоимость_Получатель     КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.ОрганизацияПолучатель       КАК Организация,
	|	ТаблицаОС.ОсновноеСредство                  КАК ОсновноеСредство,
	|	ДанныеДокумента.ПодразделениеПолучатель     КАК Подразделение,
	|	ВтПараметрыУчетаОС.ГруппаФинансовогоУчета   КАК ГруппаФинансовогоУчета,
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	ВтПараметрыУчетаОС.Арендатор                КАК Арендатор,
	|
	|	СтоимостьОС.Стоимость                       КАК Стоимость,
	|	СтоимостьОС.СтоимостьРегл                   КАК СтоимостьРегл,
	|	СтоимостьОС.СтоимостьНУ                     КАК СтоимостьНУ,
	|	СтоимостьОС.СтоимостьПР                     КАК СтоимостьПР,
	|	СтоимостьОС.СтоимостьВР                     КАК СтоимостьВР,
	|	СтоимостьОС.СтоимостьЦФ                     КАК СтоимостьЦФ,
	|	СтоимостьОС.СтоимостьНУЦФ                   КАК СтоимостьНУЦФ,
	|	СтоимостьОС.СтоимостьПРЦФ                   КАК СтоимостьПРЦФ,
	|	СтоимостьОС.СтоимостьВРЦФ                   КАК СтоимостьВРЦФ,
	|	СтоимостьОС.АмортизационнаяПремия           КАК АмортизационнаяПремия,
	|	СтоимостьОС.ЗалоговаяСтоимость              КАК ЗалоговаяСтоимость,
	|	СтоимостьОС.ПредварительнаяСтоимость        КАК ПредварительнаяСтоимость,
	|	СтоимостьОС.ПредварительнаяСтоимостьРегл    КАК ПредварительнаяСтоимостьРегл,
	|	СтоимостьОС.РезервПереоценкиСтоимости       КАК РезервПереоценкиСтоимости,
	|	СтоимостьОС.РезервПереоценкиСтоимостиРегл   КАК РезервПереоценкиСтоимостиРегл,
	|	СтоимостьОС.НеУчитываемаяСтоимостьНУ        КАК НеУчитываемаяСтоимостьНУ,
	|	0                                           КАК ПриростСтоимости,
	|	0                                           КАК ПриростСтоимостиРегл,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|			ТОГДА ДанныеДокумента.Организация
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                       КАК КорОрганизация,
	|
	|	ДанныеДокумента.Подразделение               КАК КорПодразделение,
	|
	|	ВЫБОР
	|		КОГДА НЕ АрендованныеОС.Контрагент ЕСТЬ NULL
	|			ТОГДА АрендованныеОС.Контрагент
	|		ИНАЧЕ ВтПараметрыУчетаОС.Арендатор
	|	КОНЕЦ КАК КорКонтрагент,
	|
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности  КАК КорНаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                КАК КорСтатьяРасходов,
	|	Неопределено                                КАК КорАналитикаРасходов,
	|	ВтПараметрыУчетаОС.ГруппаФинансовогоУчета   КАК КорГруппаФинансовогоУчета,
	|	ДанныеДокумента.ОтражатьВРеглУчете          КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете           КАК ОтражатьВУпрУчете,
	|	
	|	ТаблицаОС.ИдентификаторСтроки               КАК ИдентификаторФинЗаписи,
	|	ДанныеДокумента.НастройкаХО_Стоимость_Получатель КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасширеннаяТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПараметрыУчетаОС КАК ВтПараметрыУчетаОС
	|		ПО ВтПараметрыУчетаОС.Ссылка = ТаблицаОС.Ссылка
	|			И ВтПараметрыУчетаОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьОС
	|		ПО СтоимостьОС.Ссылка = ТаблицаОС.Ссылка
	|			И СтоимостьОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втАрендованныеОС КАК АрендованныеОС
	|		ПО АрендованныеОС.Ссылка = ТаблицаОС.Ссылка
	|			И АрендованныеОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|			И АрендованныеОС.Состояние В (
	|				ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ВАренде),
	|				ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКЗабалансовомуУчету))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)      КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                      КАК Регистратор,
	|	ДанныеДокумента.Период                      КАК Период,
	|	ДанныеДокумента.ХО_РезервПереоценкиСтоимости_Получатель КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация                 КАК Организация,
	|	ТаблицаОС.ОсновноеСредство                  КАК ОсновноеСредство,
	|	НЕОПРЕДЕЛЕНО                                КАК Подразделение,
	|	ВтПараметрыУчетаОС.ГруппаФинансовогоУчета   КАК ГруппаФинансовогоУчета,
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                КАК Арендатор,
	|
	|	0                                           КАК Стоимость,
	|	0                                           КАК СтоимостьРегл,
	|	0                                           КАК СтоимостьНУ,
	|	0                                           КАК СтоимостьПР,
	|	0                                           КАК СтоимостьВР,
	|	0                                           КАК СтоимостьЦФ,
	|	0                                           КАК СтоимостьНУЦФ,
	|	0                                           КАК СтоимостьПРЦФ,
	|	0                                           КАК СтоимостьВРЦФ,
	|	0                                           КАК АмортизационнаяПремия,
	|	0                                           КАК ЗалоговаяСтоимость,
	|	0                                           КАК ПредварительнаяСтоимость,
	|	0                                           КАК ПредварительнаяСтоимостьРегл,
	|	0                                           КАК РезервПереоценкиСтоимости,
	|	0                                           КАК РезервПереоценкиСтоимостиРегл,
	|	0                                           КАК НеУчитываемаяСтоимостьНУ,
	|
	|	СтоимостьОС.ПриростСтоимости                КАК ПриростСтоимости,
	|	СтоимостьОС.ПриростСтоимостиРегл            КАК ПриростСтоимостиРегл,
	|
	|	ДанныеДокумента.ОрганизацияПолучатель       КАК КорОрганизация,
	|	НЕОПРЕДЕЛЕНО                                КАК КорПодразделение,
	|
	|	ВЫБОР 
	|		КОГДА СтоимостьОС.ЗабалансовыйУчет
	|			ТОГДА ВтПараметрыУчетаОС.Арендатор
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                       КАК КорКонтрагент,
	|
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности  КАК КорНаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                КАК КорСтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                                КАК КорАналитикаРасходов,
	|	ВтПараметрыУчетаОС.ГруппаФинансовогоУчета   КАК КорГруппаФинансовогоУчета,
	|	ДанныеДокумента.ОтражатьВРеглУчете          КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете           КАК ОтражатьВУпрУчете,
	|	
	|	ТаблицаОС.ИдентификаторСтроки               КАК ИдентификаторФинЗаписи,
	|	ДанныеДокумента.НастройкаХО_РезервПереоценкиСтоимости_Получатель КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасширеннаяТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПараметрыУчетаОС КАК ВтПараметрыУчетаОС
	|		ПО ВтПараметрыУчетаОС.Ссылка = ТаблицаОС.Ссылка
	|			И ВтПараметрыУчетаОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьОС
	|		ПО СтоимостьОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И СтоимостьОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.Организация <> ДанныеДокумента.ОрганизацияПолучатель
	|	И (СтоимостьОС.ПриростСтоимости <> 0 
	|		ИЛИ СтоимостьОС.ПриростСтоимостиРегл <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)      КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                      КАК Регистратор,
	|	ДанныеДокумента.Период                      КАК Период,
	|	ДанныеДокумента.ХО_РезервПереоценкиСтоимости_Отправитель КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.ОрганизацияПолучатель       КАК Организация,
	|	ТаблицаОС.ОсновноеСредство                  КАК ОсновноеСредство,
	|	НЕОПРЕДЕЛЕНО                                КАК Подразделение,
	|	ВтПараметрыУчетаОС.ГруппаФинансовогоУчета   КАК ГруппаФинансовогоУчета,
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                КАК Арендатор,
	|
	|	0                                           КАК Стоимость,
	|	0                                           КАК СтоимостьРегл,
	|	0                                           КАК СтоимостьНУ,
	|	0                                           КАК СтоимостьПР,
	|	0                                           КАК СтоимостьВР,
	|	0                                           КАК СтоимостьЦФ,
	|	0                                           КАК СтоимостьНУЦФ,
	|	0                                           КАК СтоимостьПРЦФ,
	|	0                                           КАК СтоимостьВРЦФ,
	|	0                                           КАК АмортизационнаяПремия,
	|	0                                           КАК ЗалоговаяСтоимость,
	|	0                                           КАК ПредварительнаяСтоимость,
	|	0                                           КАК ПредварительнаяСтоимостьРегл,
	|	0                                           КАК РезервПереоценкиСтоимости,
	|	0                                           КАК РезервПереоценкиСтоимостиРегл,
	|	0                                           КАК НеУчитываемаяСтоимостьНУ,
	|
	|	СтоимостьОС.ПриростСтоимости                КАК ПриростСтоимости,
	|	СтоимостьОС.ПриростСтоимостиРегл            КАК ПриростСтоимостиРегл,
	|
	|	ДанныеДокумента.Организация                 КАК КорОрганизация,
	|	ДанныеДокумента.Подразделение               КАК КорПодразделение,
	|
	|	ВЫБОР 
	|		КОГДА СтоимостьОС.ЗабалансовыйУчет
	|			ТОГДА ВтПараметрыУчетаОС.Арендатор
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                       КАК КорКонтрагент,
	|
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности  КАК КорНаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                КАК КорСтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                                КАК КорАналитикаРасходов,
	|	ВтПараметрыУчетаОС.ГруппаФинансовогоУчета   КАК КорГруппаФинансовогоУчета,
	|	ДанныеДокумента.ОтражатьВРеглУчете          КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете           КАК ОтражатьВУпрУчете,
	|	
	|	ТаблицаОС.ИдентификаторСтроки               КАК ИдентификаторФинЗаписи,
	|	ДанныеДокумента.НастройкаХО_РезервПереоценкиСтоимости_Отправитель КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасширеннаяТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПараметрыУчетаОС КАК ВтПараметрыУчетаОС
	|		ПО ВтПараметрыУчетаОС.Ссылка = ТаблицаОС.Ссылка
	|			И ВтПараметрыУчетаОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьОС
	|		ПО СтоимостьОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И СтоимостьОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.Организация <> ДанныеДокумента.ОрганизацияПолучатель
	|	И (СтоимостьОС.ПриростСтоимости <> 0 
	|		ИЛИ СтоимостьОС.ПриростСтоимостиРегл <> 0)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаАмортизацияОС(ТекстыЗапроса)

	ИмяРегистра = "АмортизацияОС";
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтРасширеннаяТаблицаОС(ТекстыЗапроса);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыУчетаОС(ТекстыЗапроса, "ВтРасширеннаяТаблицаОС");

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)          КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                          КАК Регистратор,
	|	ДанныеДокумента.Период                          КАК Период,
	|	ДанныеДокумента.ХО_Амортизация_Отправитель      КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация                     КАК Организация,
	|	ТаблицаОС.ОсновноеСредство                      КАК ОсновноеСредство,
	|	ДанныеДокумента.Подразделение                   КАК Подразделение,
	|	ВтПараметрыУчетаОС.ГруппаФинансовогоУчета       КАК ГруппаФинансовогоУчета,
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                    КАК Контрагент,
	|	АмортизацияОС.Амортизация                       КАК Амортизация,
	|	АмортизацияОС.АмортизацияРегл                   КАК АмортизацияРегл,
	|	АмортизацияОС.АмортизацияНУ                     КАК АмортизацияНУ,
	|	АмортизацияОС.АмортизацияПР                     КАК АмортизацияПР,
	|	АмортизацияОС.АмортизацияВР                     КАК АмортизацияВР,
	|	АмортизацияОС.АмортизацияЦФ                     КАК АмортизацияЦФ,
	|	АмортизацияОС.АмортизацияНУЦФ                   КАК АмортизацияНУЦФ,
	|	АмортизацияОС.АмортизацияПРЦФ                   КАК АмортизацияПРЦФ,
	|	АмортизацияОС.АмортизацияВРЦФ                   КАК АмортизацияВРЦФ,
	|	АмортизацияОС.РезервПереоценкиАмортизации       КАК РезервПереоценкиАмортизации,
	|	АмортизацияОС.РезервПереоценкиАмортизацииРегл   КАК РезервПереоценкиАмортизацииРегл,
	|	ДанныеДокумента.ОрганизацияПолучатель           КАК КорОрганизация,
	|	ДанныеДокумента.ПодразделениеПолучатель         КАК КорПодразделение,
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности      КАК КорНаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                    КАК КорСтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                                    КАК КорАналитикаРасходов,
	|	ВтПараметрыУчетаОС.ГруппаФинансовогоУчета       КАК КорГруппаФинансовогоУчета,
	|	АмортизацияОС.НачислятьИзнос                    КАК НачислятьИзнос,
	|	
	|	ТаблицаОС.ИдентификаторСтроки                   КАК ИдентификаторФинЗаписи,
	|	ДанныеДокумента.НастройкаХО_Амортизация_Отправитель КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасширеннаяТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПараметрыУчетаОС КАК ВтПараметрыУчетаОС
	|		ПО ВтПараметрыУчетаОС.Ссылка = ТаблицаОС.Ссылка
	|			И ВтПараметрыУчетаОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияОС
	|		ПО АмортизацияОС.Ссылка = ТаблицаОС.Ссылка
	|			И АмортизацияОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)          КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                          КАК Регистратор,
	|	ДанныеДокумента.Период                          КАК Период,
	|	ДанныеДокумента.ХО_Амортизация_Получатель       КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.ОрганизацияПолучатель           КАК Организация,
	|	ТаблицаОС.ОсновноеСредство                      КАК ОсновноеСредство,
	|	ДанныеДокумента.ПодразделениеПолучатель         КАК Подразделение,
	|	ВтПараметрыУчетаОС.ГруппаФинансовогоУчета       КАК ГруппаФинансовогоУчета,
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                    КАК Контрагент,
	|	АмортизацияОС.Амортизация                       КАК Амортизация,
	|	АмортизацияОС.АмортизацияРегл                   КАК АмортизацияРегл,
	|	АмортизацияОС.АмортизацияНУ                     КАК АмортизацияНУ,
	|	АмортизацияОС.АмортизацияПР                     КАК АмортизацияПР,
	|	АмортизацияОС.АмортизацияВР                     КАК АмортизацияВР,
	|	АмортизацияОС.АмортизацияЦФ                     КАК АмортизацияЦФ,
	|	АмортизацияОС.АмортизацияНУЦФ                   КАК АмортизацияНУЦФ,
	|	АмортизацияОС.АмортизацияПРЦФ                   КАК АмортизацияПРЦФ,
	|	АмортизацияОС.АмортизацияВРЦФ                   КАК АмортизацияВРЦФ,
	|	АмортизацияОС.РезервПереоценкиАмортизации       КАК РезервПереоценкиАмортизации,
	|	АмортизацияОС.РезервПереоценкиАмортизацииРегл   КАК РезервПереоценкиАмортизацииРегл,
	|	ДанныеДокумента.Организация                     КАК КорОрганизация,
	|	ДанныеДокумента.Подразделение                   КАК КорПодразделение,
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности      КАК КорНаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                    КАК КорСтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                                    КАК КорАналитикаРасходов,
	|	ВтПараметрыУчетаОС.ГруппаФинансовогоУчета       КАК КорГруппаФинансовогоУчета,
	|	АмортизацияОС.НачислятьИзнос                    КАК НачислятьИзнос,
	|	
	|	ТаблицаОС.ИдентификаторСтроки                   КАК ИдентификаторФинЗаписи,
	|	ДанныеДокумента.НастройкаХО_Амортизация_Получатель КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасширеннаяТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПараметрыУчетаОС КАК ВтПараметрыУчетаОС
	|		ПО ВтПараметрыУчетаОС.Ссылка = ТаблицаОС.Ссылка
	|			И ВтПараметрыУчетаОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияОС
	|		ПО АмортизацияОС.Ссылка = ТаблицаОС.Ссылка
	|			И АмортизацияОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаДвиженияДоходыРасходыПрочиеАктивыПассивы(ТекстыЗапроса)

	ИмяРегистра = "ДвиженияДоходыРасходыПрочиеАктивыПассивы";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыУчетаОС(ТекстыЗапроса, "ВтРасширеннаяТаблицаОС");
	
	ТекстЗапроса =
	"// Перемещение амортизации для отправителя
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка            КАК Регистратор,
	|	ДанныеДокумента.Период            КАК Период,
	|	ДанныеДокумента.ХО_Амортизация_Отправитель КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация       КАК Организация,
	|
	|	ДанныеДокумента.Подразделение     КАК Подразделение,
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&СтатьяАП_ОС КАК Статья,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаРасходов,
	|	ТаблицаОС.ОсновноеСредство        КАК АналитикаАктивовПассивов,
	|	ВтПараметрыУчетаОС.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|
	|	ДанныеДокумента.ПодразделениеПолучатель КАК КорПодразделение,
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаРасходов,
	|	ТаблицаОС.ОсновноеСредство        КАК КорАналитикаАктивовПассивов,
	|	ВтПараметрыУчетаОС.ГруппаФинансовогоУчета  КАК КорГруппаФинансовогоУчета,
	|
	|	ДанныеДокумента.ОрганизацияПолучатель КАК КорОрганизация,
	|
	|	АмортизацияОС.Амортизация         КАК Сумма,
	|	АмортизацияОС.Амортизация         КАК СуммаУпр,
	|
	|	АмортизацияОС.АмортизацияРегл 
	|	 + АмортизацияОС.АмортизацияЦФ КАК СуммаРегл
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасширеннаяТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПараметрыУчетаОС КАК ВтПараметрыУчетаОС
	|		ПО ВтПараметрыУчетаОС.Ссылка = ТаблицаОС.Ссылка
	|			И ВтПараметрыУчетаОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияОС
	|		ПО АмортизацияОС.Ссылка = ТаблицаОС.Ссылка
	|			И АмортизацияОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Перемещение амортизации для получателя
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка            КАК Регистратор,
	|	ДанныеДокумента.Период            КАК Период,
	|	ДанныеДокумента.ХО_Амортизация_Получатель КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.ОрганизацияПолучатель КАК Организация,
	|
	|	ДанныеДокумента.ПодразделениеПолучатель КАК Подразделение,
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК Статья,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаРасходов,
	|	ТаблицаОС.ОсновноеСредство        КАК АналитикаАктивовПассивов,
	|	ВтПараметрыУчетаОС.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|
	|	ДанныеДокумента.Подразделение     КАК КорПодразделение,
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаРасходов,
	|	ТаблицаОС.ОсновноеСредство        КАК КорАналитикаАктивовПассивов,
	|	ВтПараметрыУчетаОС.ГруппаФинансовогоУчета  КАК КорГруппаФинансовогоУчета,
	|
	|	ДанныеДокумента.Организация       КАК КорОрганизация,
	|
	|	АмортизацияОС.Амортизация         КАК Сумма,
	|	АмортизацияОС.Амортизация         КАК СуммаУпр,
	|	АмортизацияОС.АмортизацияРегл + АмортизацияОС.АмортизацияЦФ КАК СуммаРегл
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасширеннаяТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПараметрыУчетаОС КАК ВтПараметрыУчетаОС
	|		ПО ВтПараметрыУчетаОС.Ссылка = ТаблицаОС.Ссылка
	|			И ВтПараметрыУчетаОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияОС
	|		ПО АмортизацияОС.Ссылка = ТаблицаОС.Ссылка
	|			И АмортизацияОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|
	|ГДЕ
	|	ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Перемещение стоимости для отправителя
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка            КАК Регистратор,
	|	ДанныеДокумента.Период            КАК Период,
	|	ДанныеДокумента.ХО_Стоимость_Отправитель КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация       КАК Организация,
	|
	|	ДанныеДокумента.Подразделение     КАК Подразделение,
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК Статья,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаРасходов,
	|	ТаблицаОС.ОсновноеСредство        КАК АналитикаАктивовПассивов,
	|	ВтПараметрыУчетаОС.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|
	|	ДанныеДокумента.ПодразделениеПолучатель КАК КорПодразделение,
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаРасходов,
	|	ТаблицаОС.ОсновноеСредство        КАК КорАналитикаАктивовПассивов,
	|	ВтПараметрыУчетаОС.ГруппаФинансовогоУчета  КАК КорГруппаФинансовогоУчета,
	|
	|	ДанныеДокумента.ОрганизацияПолучатель КАК КорОрганизация,
	|
	|	СтоимостьОС.Стоимость             КАК Сумма,
	|	СтоимостьОС.Стоимость             КАК СуммаУпр,
	|
	|	СтоимостьОС.СтоимостьРегл 
	|	 + СтоимостьОС.СтоимостьЦФ КАК СуммаРегл
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасширеннаяТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПараметрыУчетаОС КАК ВтПараметрыУчетаОС
	|		ПО ВтПараметрыУчетаОС.Ссылка = ТаблицаОС.Ссылка
	|			И ВтПараметрыУчетаОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьОС
	|		ПО СтоимостьОС.Ссылка = ТаблицаОС.Ссылка
	|			И СтоимостьОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Перемещение стоимости для получателя
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка            КАК Регистратор,
	|	ДанныеДокумента.Период            КАК Период,
	|	ДанныеДокумента.ХО_Стоимость_Получатель КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.ОрганизацияПолучатель КАК Организация,
	|
	|	ДанныеДокумента.ПодразделениеПолучатель КАК Подразделение,
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК Статья,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаРасходов,
	|	ТаблицаОС.ОсновноеСредство        КАК АналитикаАктивовПассивов,
	|	ВтПараметрыУчетаОС.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|
	|	ДанныеДокумента.Подразделение     КАК КорПодразделение,
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаРасходов,
	|	ТаблицаОС.ОсновноеСредство        КАК КорАналитикаАктивовПассивов,
	|	ВтПараметрыУчетаОС.ГруппаФинансовогоУчета  КАК КорГруппаФинансовогоУчета,
	|
	|	ДанныеДокумента.Организация       КАК КорОрганизация,
	|
	|	СтоимостьОС.Стоимость             КАК Сумма,
	|	СтоимостьОС.Стоимость             КАК СуммаУпр,
	|
	|	СтоимостьОС.СтоимостьРегл
	|	 + СтоимостьОС.СтоимостьЦФ КАК СуммаРегл
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасширеннаяТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПараметрыУчетаОС КАК ВтПараметрыУчетаОС
	|		ПО ВтПараметрыУчетаОС.Ссылка = ТаблицаОС.Ссылка
	|			И ВтПараметрыУчетаОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьОС
	|		ПО СтоимостьОС.Ссылка = ТаблицаОС.Ссылка
	|			И СтоимостьОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|
	|ГДЕ
	|	ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаДвиженияПоПрочимАктивамПассивам(ТекстыЗапроса)
	
	ИмяРегистра = "ДвиженияПоПрочимАктивамПассивам";
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыУчетаОС(ТекстыЗапроса, "ВтРасширеннаяТаблицаОС");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка            КАК Регистратор,
	|	ДанныеДокумента.Период            КАК Период,
	|	ДанныеДокумента.Организация       КАК Организация,
	|	ДанныеДокумента.Подразделение     КАК Подразделение,
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РасчетыПоВыделенномуИмуществу) КАК Статья,
	|	ДанныеДокумента.ОрганизацияПолучатель КАК Аналитика,
	|	НЕОПРЕДЕЛЕНО                      КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Дебет) КАК ДебетКредит,
	|
	|	СтоимостьОС.СтоимостьРегл + СтоимостьОС.СтоимостьЦФ КАК СуммаРегл,
	|	СтоимостьОС.СтоимостьПР + СтоимостьОС.СтоимостьПРЦФ КАК ПостояннаяРазница,
	|	СтоимостьОС.СтоимостьВР + СтоимостьОС.СтоимостьВРЦФ КАК ВременнаяРазница,
	|	СтоимостьОС.Стоимость КАК СуммаУпр,
	|	СтоимостьОС.Стоимость КАК СуммаСНДС,
	|	СтоимостьОС.Стоимость КАК СуммаБезНДС,
	|	
	|	ТаблицаОС.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ДанныеДокумента.НастройкаХО_Стоимость_Отправитель КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасширеннаяТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПараметрыУчетаОС КАК ВтПараметрыУчетаОС
	|		ПО ВтПараметрыУчетаОС.Ссылка = ТаблицаОС.Ссылка
	|			И ВтПараметрыУчетаОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьОС
	|		ПО СтоимостьОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И СтоимостьОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|	И ДанныеДокумента.Организация <> ДанныеДокумента.ОрганизацияПолучатель
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка            КАК Регистратор,
	|	ДанныеДокумента.Период            КАК Период,
	|	ДанныеДокумента.ОрганизацияПолучатель КАК Организация,
	|	ДанныеДокумента.ПодразделениеПолучатель КАК Подразделение,
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РасчетыПоВыделенномуИмуществу) КАК Статья,
	|	ДанныеДокумента.Организация       КАК Аналитика,
	|	НЕОПРЕДЕЛЕНО                      КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Кредит) КАК ДебетКредит,
	|
	|	СтоимостьОС.СтоимостьРегл + СтоимостьОС.СтоимостьЦФ КАК СуммаРегл,
	|	СтоимостьОС.СтоимостьПР + СтоимостьОС.СтоимостьПРЦФ КАК ПостояннаяРазница,
	|	СтоимостьОС.СтоимостьВР + СтоимостьОС.СтоимостьВРЦФ КАК ВременнаяРазница,
	|	СтоимостьОС.Стоимость КАК СуммаУпр,
	|	СтоимостьОС.Стоимость КАК СуммаСНДС,
	|	СтоимостьОС.Стоимость КАК СуммаБезНДС,
	|	
	|	ТаблицаОС.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ДанныеДокумента.НастройкаХО_Стоимость_Получатель КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасширеннаяТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПараметрыУчетаОС КАК ВтПараметрыУчетаОС
	|		ПО ВтПараметрыУчетаОС.Ссылка = ТаблицаОС.Ссылка
	|			И ВтПараметрыУчетаОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьОС
	|		ПО СтоимостьОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И СтоимостьОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|	И ДанныеДокумента.Организация <> ДанныеДокумента.ОрганизацияПолучатель
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка            КАК Регистратор,
	|	ДанныеДокумента.Период            КАК Период,
	|	ДанныеДокумента.Организация       КАК Организация,
	|	ДанныеДокумента.Подразделение     КАК Подразделение,
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РасчетыПоВыделенномуИмуществу) КАК Статья,
	|	ДанныеДокумента.ОрганизацияПолучатель КАК Аналитика,
	|	НЕОПРЕДЕЛЕНО                      КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Кредит) КАК ДебетКредит,
	|
	|	АмортизацияОС.АмортизацияРегл + АмортизацияОС.АмортизацияЦФ КАК СуммаРегл,
	|	АмортизацияОС.АмортизацияПР + АмортизацияОС.АмортизацияПРЦФ КАК ПостояннаяРазница,
	|	АмортизацияОС.АмортизацияВР + АмортизацияОС.АмортизацияВРЦФ КАК ВременнаяРазница,
	|	АмортизацияОС.Амортизация КАК СуммаУпр,
	|	АмортизацияОС.Амортизация КАК СуммаСНДС,
	|	АмортизацияОС.Амортизация КАК СуммаБезНДС,
	|	
	|	ТаблицаОС.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ДанныеДокумента.НастройкаХО_Амортизация_Отправитель КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасширеннаяТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПараметрыУчетаОС КАК ВтПараметрыУчетаОС
	|		ПО ВтПараметрыУчетаОС.Ссылка = ТаблицаОС.Ссылка
	|			И ВтПараметрыУчетаОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияОС
	|		ПО АмортизацияОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И АмортизацияОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|	И ДанныеДокумента.Организация <> ДанныеДокумента.ОрганизацияПолучатель
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка            КАК Регистратор,
	|	ДанныеДокумента.Период            КАК Период,
	|	ДанныеДокумента.ОрганизацияПолучатель КАК Организация,
	|	ДанныеДокумента.ПодразделениеПолучатель КАК Подразделение,
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РасчетыПоВыделенномуИмуществу) КАК Статья,
	|	ДанныеДокумента.Организация       КАК Аналитика,
	|	НЕОПРЕДЕЛЕНО                      КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Дебет) КАК ДебетКредит,
	|
	|	АмортизацияОС.АмортизацияРегл + АмортизацияОС.АмортизацияЦФ КАК СуммаРегл,
	|	АмортизацияОС.АмортизацияПР + АмортизацияОС.АмортизацияПРЦФ КАК ПостояннаяРазница,
	|	АмортизацияОС.АмортизацияВР + АмортизацияОС.АмортизацияВРЦФ КАК ВременнаяРазница,
	|	АмортизацияОС.Амортизация КАК СуммаУпр,
	|	АмортизацияОС.Амортизация КАК СуммаСНДС,
	|	АмортизацияОС.Амортизация КАК СуммаБезНДС,
	|	
	|	ТаблицаОС.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ДанныеДокумента.НастройкаХО_Амортизация_Получатель КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасширеннаяТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПараметрыУчетаОС КАК ВтПараметрыУчетаОС
	|		ПО ВтПараметрыУчетаОС.Ссылка = ТаблицаОС.Ссылка
	|			И ВтПараметрыУчетаОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияОС
	|		ПО АмортизацияОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И АмортизацияОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|	И ДанныеДокумента.Организация <> ДанныеДокумента.ОрганизацияПолучатель
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаУзлыКомпонентыАмортизации(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "УзлыКомпонентыАмортизации";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Регистратор,
	|	ДанныеДокумента.Период КАК Период,
	|	ТаблицаОС.ОсновноеСредство КАК УзелКомпонент,
	|	ТаблицаОС.ОсновноеСредствоВладелец КАК ОсновноеСредство
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеУзлов)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

//++ НЕ УТКА

Функция ТекстЗапросаТаблицаОтражениеДокументовВМеждународномУчете(ТекстыЗапроса, Регистры = Неопределено)
	
	ИмяРегистра = "ОтражениеДокументовВМеждународномУчете";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Регистратор,
	|	ДанныеДокумента.Период КАК Период,
	|	ДанныеДокумента.Ссылка КАК Документ,
	|	ДанныеДокумента.Организация КАК Организация,
	|	НАЧАЛОПЕРИОДА(ДанныеДокумента.Период, ДЕНЬ) КАК ДатаОтражения,
	|	ДанныеДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Регистратор,
	|	ДанныеДокумента.Период КАК Период,
	|	ДанныеДокумента.Ссылка КАК Документ,
	|	ДанныеДокумента.ОрганизацияПолучатель КАК Организация,
	|	НАЧАЛОПЕРИОДА(ДанныеДокумента.Период, ДЕНЬ) КАК ДатаОтражения,
	|	ДанныеДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|ГДЕ
	|	ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

//-- НЕ УТКА

Функция ТекстЗапросаТаблицаДвиженияСоставЕГДС(ТекстыЗапроса, Регистры = Неопределено)
	
	ИмяРегистра = "СоставЕГДС";

	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьОбесценениеВНА") Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"// Перемещение ОС в другую ЕГДС.
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка         КАК Регистратор,
	|	ДанныеДокумента.Период         КАК Период,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОрганизацияПолучатель = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ДанныеДокумента.Организация
	|		ИНАЧЕ ДанныеДокумента.ОрганизацияПолучатель
	|	КОНЕЦ                       КАК Организация,
	|
	|	ТаблицаОС.ОсновноеСредство         КАК ВнеоборотныйАктив,
	|	ДанныеДокумента.ЕГДСПолучатель             КАК ЕГДС
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|ОБЪЕДИНИТЬ
	|
	|// Очистка ЕГДС по организации отправителю 
	|// при перемещении в подразделение с выделенным балансом.
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка         КАК Регистратор,
	|	ДанныеДокумента.Период         КАК Период,
	|	ДанныеДокумента.Организация    КАК Организация,
	|	ТаблицаОС.ОсновноеСредство     КАК ВнеоборотныйАктив,
	|	ЗНАЧЕНИЕ(Справочник.ЕдиницыГенерирующиеДенежныеСредства.ПустаяСсылка) КАК ЕГДС
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ОрганизацияПолучатель <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|	И ДанныеДокумента.ОрганизацияПолучатель <> ДанныеДокумента.Организация
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область ПроведениеПоРеглУчету

Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт

	Возврат ПеремещениеОСЛокализация.ТекстЗапросаВТОтраженияВРеглУчете();

КонецФункции

Функция ТекстОтраженияВРеглУчете() Экспорт

	Возврат ПеремещениеОСЛокализация.ТекстОтраженияВРеглУчете();

КонецФункции

#КонецОбласти

#Область Печать

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	ПеремещениеОСЛокализация.ДобавитьКомандыПечати(КомандыПечати);

КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПеремещениеОСЛокализация.Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);

КонецПроцедуры

#КонецОбласти

#Область Прочее

Функция ПараметрыВыбораСтатейИАналитик() Экспорт
	
	ПараметрыВыбораСтатейИАналитик = Новый Массив;
	
	// СтатьяРасходов
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья = "СтатьяРасходов";
	ПараметрыВыбора.ДоступностьПоОперации = Неопределено;
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходов";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходов");
	
	ПараметрыВыбораСтатейИАналитик.Добавить(ПараметрыВыбора);
	
	//
	ПеремещениеОСЛокализация.ДополнитьПараметрыВыбораСтатейИАналитик(ПараметрыВыбораСтатейИАналитик);
	
	Возврат ПараметрыВыбораСтатейИАналитик;
	
КонецФункции

Функция ЕстьСобственныеОС(Объект) Экспорт

	ЕстьСобственныеОС = Истина;
	
	//++ Локализация
	
	Если Объект.ОС.Количество() = 0 Тогда
		Возврат Истина;
	КонецЕсли;

	ТекстЗапроса = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА
	|ИЗ
	|	Справочник.ОбъектыЭксплуатации КАК ОбъектыЭксплуатации
	|ГДЕ
	|	ОбъектыЭксплуатации.Ссылка В(&СписокОС)
	|	И НЕ ОбъектыЭксплуатации.Ссылка В
	|				(ВЫБРАТЬ
	|					АрендованныеОС.ОсновноеСредство КАК ОсновноеСредство
	|				ИЗ
	|					РегистрСведений.АрендованныеОС.СрезПоследних(
	|						&Дата,
	|						ОсновноеСредство В (&СписокОС)) КАК АрендованныеОС
	|				ГДЕ
	|					АрендованныеОС.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКЗабалансовомуУчету))";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("Дата", Объект.Дата);
	Запрос.УстановитьПараметр("СписокОС", Объект.ОС.Выгрузить().ВыгрузитьКолонку("ОсновноеСредство"));
	
	Результат = Запрос.Выполнить();
	ЕстьСобственныеОС = НЕ Результат.Пустой();
	
	//-- Локализация
	
	Возврат ЕстьСобственныеОС;
	
КонецФункции
 
#КонецОбласти

#КонецОбласти

#КонецЕсли
