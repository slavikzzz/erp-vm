#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ВходящийДокументСЭДОФСС") Тогда
		
		// Определяем тип сообщения ответа.
		СоответствиеТипов = Новый Соответствие;
		СоответствиеТипов.Вставить(303, 313);
		СоответствиеТипов.Вставить(308, 314);
		ТипЗапроса = Число(ДанныеЗаполнения.ТипСообщения);
		ТипСообщенияОтвета = СоответствиеТипов[ТипЗапроса];
		Если НЕ ЗначениеЗаполнено(ТипСообщенияОтвета) Тогда
			Шаблон = НСтр("'Не удалось определить тип ответа на требование с типом %1'");
			ОписаниеОшибки = СтрШаблон(Шаблон, ТипЗапроса);
			ВызватьИсключение(ОписаниеОшибки);
		КонецЕсли;
		
		ТекстЗапроса = "ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПредоставленныеДокументы.НаборДокументов КАК НаборДокументов
		|ИЗ
		|	Документ.ОтветСтрахователяНаТребованиеПроверкиФСС.ПредоставленныеДокументы КАК ПредоставленныеДокументы
		|ГДЕ
		|	ПредоставленныеДокументы.Ссылка.Основание = &Основание
		|
		|УПОРЯДОЧИТЬ ПО
		|	НаборДокументов УБЫВ";
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапроса;
		Запрос.УстановитьПараметр("Основание", ДанныеЗаполнения);
		Выборка = Запрос.Выполнить();
		
		Если НЕ Выборка.Пустой() Тогда
			ИдентификаторНабораДокументов = Выборка.Выгрузить()[0].НаборДокументов + 1;
		Иначе
			ИдентификаторНабораДокументов = 1;
		КонецЕсли;
		
		Основание = ДанныеЗаполнения;
		РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			ДанныеЗаполнения, "Ответственный, Организация");
		Ответственный = РеквизитыОснования.Ответственный;
		Организация = РеквизитыОснования.Организация;
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	ВходящийДокументСЭДОФССЗапрашиваемыеДокументы.ВидДокумента КАК ВидДокумента,
		               |	ВходящийДокументСЭДОФССЗапрашиваемыеДокументы.КоличествоДокументов КАК КоличествоДокументов
		               |ИЗ
		               |	Документ.ВходящийДокументСЭДОФСС.ЗапрашиваемыеДокументы КАК ВходящийДокументСЭДОФССЗапрашиваемыеДокументы
		               |ГДЕ
		               |	ВходящийДокументСЭДОФССЗапрашиваемыеДокументы.Ссылка = &Ссылка";
		Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
		РезультатЗапроса = Запрос.Выполнить();
		
		Если НЕ РезультатЗапроса.Пустой() Тогда
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				НоваяСтрока = ПредоставленныеДокументы.Добавить();
				НоваяСтрока.ДатаДокумента = ТекущаяДатаСеанса();
				НоваяСтрока.НаборДокументов = ИдентификаторНабораДокументов;
				НоваяСтрока.ВидДокумента = Выборка.ВидДокумента;
				НоваяСтрока.КоличествоДокументовВоВложении = Выборка.КоличествоДокументов;
			КонецЦикла;
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
