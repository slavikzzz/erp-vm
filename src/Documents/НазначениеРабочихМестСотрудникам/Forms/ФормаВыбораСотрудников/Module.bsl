
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Подразделение = Параметры.Подразделение;
	Должность = Параметры.Должность;
	
	Для Каждого ДанныеСотрудника Из ПолучитьИзВременногоХранилища(Параметры.АдресСотрудников) Цикл
		ЗаполнитьЗначенияСвойств(Сотрудники.Добавить(), ДанныеСотрудника);
	КонецЦикла;
	АдресСотрудников = "";
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если АдресСотрудников <> ""
		И Не ЗавершениеРаботы Тогда
		
		Структура = Новый Структура("АдресСотрудников", АдресСотрудников);
		Структура.Вставить("РабочееМестоВНовойСтроке", РабочееМестоВНовойСтроке);
		ОповеститьОВыборе(Структура);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	ПоместитьСотрудниковВХранилище();
	Закрыть(КодВозвратаДиалога.OK);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВсе(Команда)
	Для Каждого СтрокаСотрудника Из Сотрудники Цикл
		СтрокаСотрудника.Выбран = Истина;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсе(Команда)
	Для Каждого СтрокаСотрудника Из Сотрудники Цикл
		СтрокаСотрудника.Выбран = Ложь;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПоместитьСотрудниковВХранилище();
	// В исходной строке рабочего места должен остаться хотя бы один сотрудник
	Если Сотрудники.НайтиСтроки(Новый Структура("Выбран",Ложь)).Количество() > 0
		  И Сотрудники.НайтиСтроки(Новый Структура("Выбран",Истина)).Количество() > 0 Тогда
		ТаблицаВыбранных = РеквизитФормыВЗначение("Сотрудники").Скопировать(Новый Структура("Выбран", Истина));
		АдресСотрудников = ПоместитьВоВременноеХранилище(ТаблицаВыбранных, УникальныйИдентификатор);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
