#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКопировании(ОбъектКопирования)
	
	ИнициализироватьДокумент();
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ИнициализироватьДокумент();
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Массив") Тогда
		
		Если ДанныеЗаполнения.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		ПроверитьВозможностьВводаНаОсновании(ДанныеЗаполнения);
		
		Если ТипЗнч(ДанныеЗаполнения[0]) = Тип("СправочникСсылка.РесурсныеСпецификации") Тогда
			ЗаполнитьПоСпецификациям(ДанныеЗаполнения);
		ИначеЕсли ТипЗнч(ДанныеЗаполнения[0]) = Тип("ДокументСсылка.ЗаказНаПроизводство2_2") Тогда
			ЗаполнитьПоЗаказу(ДанныеЗаполнения);
		ИначеЕсли ТипЗнч(ДанныеЗаполнения[0]) = Тип("ДокументСсылка.ЭтапПроизводства2_2") Тогда
			ЗаполнитьПоЭтапу(ДанныеЗаполнения);
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаполнениеОбъектовПоСтатистике.ЗаполнитьРеквизитыОбъекта(ЭтотОбъект, ДанныеЗаполнения);
	
	Если Не ЗначениеЗаполнено(Ответственный) Тогда
		Ответственный = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	ПараметрыПроверки = НоменклатураСервер.ПараметрыПроверкиЗаполненияХарактеристик();
	ПараметрыПроверки.ИмяТЧ = "ОбъектыКалькуляции";
	
	НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(
		ЭтотОбъект,
		МассивНепроверяемыхРеквизитов,
		Отказ,
		ПараметрыПроверки);
		
	ПараметрыПроверки = НоменклатураСервер.ПараметрыПроверкиЗаполненияКоличества();
	ПараметрыПроверки.ИмяТЧ = "ОбъектыКалькуляции";
	НоменклатураСервер.ПроверитьЗаполнениеКоличества(ЭтотОбъект, ПроверяемыеРеквизиты, Отказ, ПараметрыПроверки);
	
	Если ОбъектКалькуляции = Перечисления.ОбъектыКалькуляции.ЗаказНаПроизводство Тогда
		
		ТекстОшибки = НСтр("ru = 'Перед расчетом требуется запланировать %1';
							|en = 'It is required to plan %1 before calculation'");
		
		Заказы = ОбъектыКалькуляции.ВыгрузитьКолонку("Объект");
		СтатусыЗаказов = Документы.ЗаказНаПроизводство2_2.ЗаказыЗапланированы(Заказы);
		Для Каждого КлючИЗначение Из СтатусыЗаказов Цикл
			
			Если КлючИЗначение.Значение Тогда
				Продолжить;
			КонецЕсли;
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, КлючИЗначение.Ключ),
				Ссылка,
				
				,
				,
				Отказ);
			
		КонецЦикла;
		
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ПриЗаписиДокумента(ЭтотОбъект, Отказ);
	
	Если ПроведениеДокументов.СвойстваДокумента(ЭтотОбъект).РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		
		ЗаписатьОчередьФоновогоПроведения(Истина);
		ЗапуститьФоновуюОбработкуДвижений(Ссылка);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеДокументов.ОбработкаПроведенияДокумента(ЭтотОбъект, Отказ);
	
	ЗаписатьОчередьФоновогоПроведения();
	
	ЗапуститьФоновуюОбработкуДвижений(Ссылка);
		
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеДокументов.ОбработкаУдаленияПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ИнициализироватьДокумент()
	
	Организация 	= ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	Автор			= Пользователи.ТекущийПользователь();
	
КонецПроцедуры

Процедура ЗаписатьОчередьФоновогоПроведения(ОтменаПроведения = Ложь)
	
	Очередь = Новый ТаблицаЗначений;
	Очередь.Колонки.Добавить("Калькуляция", Новый ОписаниеТипов("ДокументСсылка.ПлановаяКалькуляция2_2"));
	Очередь.Колонки.Добавить("ОтменаПроведения", Новый ОписаниеТипов("Булево"));
	
	СтрокаОчередь = Очередь.Добавить();
	СтрокаОчередь.Калькуляция = Ссылка;
	СтрокаОчередь.ОтменаПроведения = ОтменаПроведения;
	
	РегистрыСведений.ОчередьРасчетаПлановыхКалькуляций.ЗаписатьОчередь(Очередь, Ссылка);
	РегистрыСведений.СостоянияПлановыхКалькуляций.УстановитьСостояние(Ссылка, 
		Перечисления.СостоянияРасчетаПлановойКалькуляции.Рассчитывается);
	
КонецПроцедуры

Процедура ЗапуститьФоновуюОбработкуДвижений(Калькуляция)
	
	Документы.ПлановаяКалькуляция2_2.ЗапускВыполненияФоновогоПроведения(Калькуляция);
	
КонецПроцедуры

Процедура ЗаполнитьПоСпецификациям(МассивОбъектов)
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Изделия.Номенклатура КАК Номенклатура,
		|	Изделия.Характеристика КАК Характеристика,
		|	СУММА(ВЫРАЗИТЬ(Изделия.КоличествоУпаковок * ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) КАК ЧИСЛО(15,3))) КАК Количество,
		|	СУММА(Изделия.КоличествоУпаковок) КАК КоличествоУпаковок,
		|	Изделия.Упаковка КАК Упаковка,
		|	Изделия.Ссылка КАК Объект
		|ИЗ
		|	Справочник.РесурсныеСпецификации.ВыходныеИзделия КАК Изделия
		|ГДЕ
		|	Изделия.Ссылка В(&МассивОбъектов)
		|
		|СГРУППИРОВАТЬ ПО
		|	Изделия.Номенклатура,
		|	Изделия.Упаковка,
		|	Изделия.Характеристика,
		|	Изделия.Ссылка";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"Изделия.Упаковка",
			"Изделия.Номенклатура"));
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	ОбъектыКалькуляции.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

Процедура ЗаполнитьПоЗаказу(ДанныеЗаполнения)
	
	ОбъектКалькуляции = Перечисления.ОбъектыКалькуляции.ЗаказНаПроизводство;
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказНаПроизводство2_2Продукция.Номенклатура КАК Номенклатура,
		|	ЗаказНаПроизводство2_2Продукция.Характеристика КАК Характеристика,
		|	ЗаказНаПроизводство2_2Продукция.Упаковка КАК Упаковка,
		|	СУММА(ЗаказНаПроизводство2_2Продукция.КоличествоУпаковок) КАК КоличествоУпаковок,
		|	СУММА(ЗаказНаПроизводство2_2Продукция.Количество) КАК Количество,
		|	ЗаказНаПроизводство2_2.Ссылка КАК Объект,
		|	ЗаказНаПроизводство2_2.Подразделение КАК ПодразделениеДиспетчер,
		|	ЗаказНаПроизводство2_2Продукция.Назначение КАК Назначение,
		|	ЗаказНаПроизводство2_2.Организация КАК Организация
		|ИЗ
		|	Документ.ЗаказНаПроизводство2_2.Продукция КАК ЗаказНаПроизводство2_2Продукция
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство2_2 КАК ЗаказНаПроизводство2_2
		|		ПО ЗаказНаПроизводство2_2Продукция.Ссылка = ЗаказНаПроизводство2_2.Ссылка
		|			И (НЕ ЗаказНаПроизводство2_2Продукция.Отменено)
		|ГДЕ
		|	ЗаказНаПроизводство2_2.Ссылка В(&МассивОбъектов)
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗаказНаПроизводство2_2Продукция.Характеристика,
		|	ЗаказНаПроизводство2_2Продукция.Упаковка,
		|	ЗаказНаПроизводство2_2.Ссылка,
		|	ЗаказНаПроизводство2_2Продукция.Номенклатура,
		|	ЗаказНаПроизводство2_2Продукция.Назначение,
		|	ЗаказНаПроизводство2_2.Подразделение,
		|	ЗаказНаПроизводство2_2.Организация";
	
	Запрос.УстановитьПараметр("МассивОбъектов", ДанныеЗаполнения);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ПодразделениеДиспетчер = Выборка.ПодразделениеДиспетчер;
		Организация = Выборка.Организация;
		НовыйОбъект = ОбъектыКалькуляции.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйОбъект, Выборка);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПоЭтапу(ДанныеЗаполнения)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЭтапПроизводства2_2.Распоряжение
		|ИЗ
		|	Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства2_2
		|ГДЕ
		|	ЭтапПроизводства2_2.Ссылка В(&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", ДанныеЗаполнения);
	
	ЗаполнитьПоЗаказу(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Распоряжение"));
	
КонецПроцедуры

Процедура ПроверитьВозможностьВводаНаОсновании(Основания)
	
	Если ТипЗнч(Основания[0]) = Тип("СправочникСсылка.РесурсныеСпецификации") Тогда
		ТекстЗапроса = 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	Реквизиты.Ссылка КАК Ссылка,
			|	ВЫБОР
			|		КОГДА Реквизиты.ТипПроизводственногоПроцесса = ЗНАЧЕНИЕ(Перечисление.ТипыПроизводственныхПроцессов.Разборка)
			|			ТОГДА 0
			|		ИНАЧЕ
			|			1
			|	КОНЕЦ КАК НомерОшибки
			|ИЗ
			|	Справочник.РесурсныеСпецификации КАК Реквизиты
			|ГДЕ
			|	(Реквизиты.ТипПроизводственногоПроцесса = ЗНАЧЕНИЕ(Перечисление.ТипыПроизводственныхПроцессов.Разборка)
			|		ИЛИ Реквизиты.ВариантНазначения = ЗНАЧЕНИЕ(Перечисление.ВариантыНазначенияСпецификации.ВидНоменклатуры))
			|	И Реквизиты.Ссылка В(&Основания)";
	ИначеЕсли ТипЗнч(Основания[0]) = Тип("ДокументСсылка.ЗаказНаПроизводство2_2") Тогда
		ТекстЗапроса = 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	ЗаказНаПроизводство2_2.Ссылка КАК Ссылка,
			|	0 КАК НомерОшибки
			|ИЗ
			|	Документ.ЗаказНаПроизводство2_2 КАК ЗаказНаПроизводство2_2
			|ГДЕ
			|	ЗаказНаПроизводство2_2.ТипПроизводственногоПроцесса = ЗНАЧЕНИЕ(Перечисление.ТипыПроизводственныхПроцессов.Разборка)
			|	И ЗаказНаПроизводство2_2.Ссылка В(&Основания)";
	Иначе
		ТекстЗапроса = 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	ЗаказНаПроизводство2_2.Ссылка КАК Ссылка,
			|	0 КАК НомерОшибки
			|ИЗ
			|	Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства2_2
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство2_2 КАК ЗаказНаПроизводство2_2
			|		ПО ЭтапПроизводства2_2.Распоряжение = ЗаказНаПроизводство2_2.Ссылка
			|ГДЕ
			|	ЗаказНаПроизводство2_2.ТипПроизводственногоПроцесса = ЗНАЧЕНИЕ(Перечисление.ТипыПроизводственныхПроцессов.Разборка)
			|	И ЭтапПроизводства2_2.Ссылка В(&Основания)";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Основания", Основания);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Если Не Выборка.Следующий() Тогда
		Возврат;
	КонецЕсли;
	
	Если Выборка.НомерОшибки = 0 Тогда
		ТекстИсключения = НСтр("ru = 'Калькуляция разборки, утилизации не поддерживается.';
								|en = 'Reverse kitting and disposal costing is not supported.'");
	Иначе
		ТекстИсключения = НСтр("ru = 'На основании спецификации для вида номенклатуры ввод калькуляции недоступен.';
								|en = 'Costing entry based on bill of materials is not available for the item kind.'");
	КонецЕсли;
	
	ВызватьИсключение ТекстИсключения;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
