#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Выгружает документ и возвращает свойства файла выгрузки.
//
// Параметры:
//  УникальныйИдентификатор - Строка.
//
// Возвращаемое значение:
//	 - Неопределено  - если не удалось сформировать файл выгрузки.
//	 - Массив из Структура:
//		* АдресФайлаВыгрузки - Строка - адрес двоичных данных файла выгрузки во временном хранилище.
//		* ИмяФайлаВыгрузки - Строка - короткое имя файла выгрузки (с расширением).
//
Функция ВыгрузитьДокумент(УникальныйИдентификатор = Неопределено) Экспорт
	
	Результат = Неопределено;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОсновныеСведенияВыгрузки = Документы.УведомлениеОКонтролируемыхСделках.ПолучитьОсновныеСведенияУведомленияДляВыгрузки(Ссылка);
	
	СодержаниеВыгрузки = ЭлектронноеПредставление(ОсновныеСведенияВыгрузки);
	
	Для Каждого ЭлементВыгрузки Из СодержаниеВыгрузки Цикл
		
		Если Результат = Неопределено Тогда
			Результат = Новый Массив;
		КонецЕсли;
		
		ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
		СохраняемыйФайл = Новый ТекстовыйДокумент;
		СохраняемыйФайл.УстановитьТекст(ЭлементВыгрузки.ТекстФайла);
		СохраняемыйФайл.Записать(ИмяВременногоФайла, ЭлементВыгрузки.КодировкаТекста);
		
		ФайлВыгрузки = Новый ДвоичныеДанные(ИмяВременногоФайла);
		Если УникальныйИдентификатор <> Неопределено Тогда
			СсылкаНаДвоичныеДанныеФайла = ПоместитьВоВременноеХранилище(ФайлВыгрузки, УникальныйИдентификатор);
		Иначе
			СсылкаНаДвоичныеДанныеФайла = ПоместитьВоВременноеХранилище(ФайлВыгрузки, Новый УникальныйИдентификатор());
		КонецЕсли;
		
		СтруктураВыгрузки = Новый Структура;
		СтруктураВыгрузки.Вставить("АдресФайлаВыгрузки", СсылкаНаДвоичныеДанныеФайла);
		СтруктураВыгрузки.Вставить("ИмяФайлаВыгрузки", ЭлементВыгрузки.ИмяФайла);
		
		Результат.Добавить(СтруктураВыгрузки);
		
		Попытка
			УдалитьФайлы(ИмяВременногоФайла);
		Исключение
			ИмяСобытия = НСтр("ru = 'Уведомление о контролируемых сделках. Выгрузка документа.';
								|en = 'Уведомление о контролируемых сделках. Выгрузка документа.'", ОбщегоНазначения.КодОсновногоЯзыка());
			ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка, , , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Выгружает документ и возвращает свойства файла выгрузки.
// 
// Параметры:
//  УникальныйИдентификатор - Строка - 
// 
// Возвращаемое значение:
//	 - Неопределено  - если не удалось сформировать файл выгрузки.
//	 - Массив из Структура:
//		* АдресФайлаВыгрузки - Строка - адрес двоичных данных файла выгрузки во временном хранилище.
//		* ИмяФайлаВыгрузки - Строка - короткое имя файла выгрузки (с расширением).
Функция ВыгрузитьДокументСРазделениемНаФайлы(УникальныйИдентификатор = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОсновныеСведенияВыгрузки = Документы.УведомлениеОКонтролируемыхСделках.ПолучитьОсновныеСведенияУведомленияДляВыгрузки(Ссылка);
	
	Если ОсновныеСведенияВыгрузки.ВерсияУведомления >= КонтролируемыеСделкиКлиентСервер.ВерсияУведомления_2024() Тогда
		
		ФайлыВХранилище = ЭлектронноеПредставление505(ОсновныеСведенияВыгрузки, УникальныйИдентификатор);
		
	ИначеЕсли ОсновныеСведенияВыгрузки.ВерсияУведомления >= КонтролируемыеСделкиКлиентСервер.ВерсияУведомления_2018() Тогда
		
		ФайлыВХранилище = ЭлектронноеПредставление503_504(ОсновныеСведенияВыгрузки, УникальныйИдентификатор);
		
	Иначе
		
		ФайлыВХранилище = ЭлектронноеПредставление502(ОсновныеСведенияВыгрузки, УникальныйИдентификатор);
		
	КонецЕсли;
	
	Возврат ФайлыВХранилище;
	
КонецФункции

// Выгружает документ и возвращает свойства файла выгрузки.
// Поддерживаемая версия уведомления: 5.02.
// Параметры:
//  УникальныйИдентификатор - Строка - 
// 
// Возвращаемое значение:
//	 - Неопределено  - если не удалось сформировать файл выгрузки.
//	 - Массив из Структура:
//		* АдресФайлаВыгрузки - Строка - адрес двоичных данных файла выгрузки во временном хранилище.
//		* ИмяФайлаВыгрузки - Строка - короткое имя файла выгрузки (с расширением).
Функция ВыгрузитьДокумент502(УникальныйИдентификатор = Неопределено) Экспорт
	
	// Функция нужна для выгрузки рег.отчетности
	Возврат ВыгрузитьДокументСРазделениемНаФайлы(УникальныйИдентификатор);
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения)
	
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Организация = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

	Запрос = Новый Запрос();
	Запрос.Параметры.Вставить("Ссылка", Ссылка);
	Запрос.Параметры.Вставить("Организация", Организация);
	Запрос.Параметры.Вставить("ОтчетныйГод", ОтчетныйГод);
	Запрос.Параметры.Вставить("НомерКорректировки", НомерКорректировки);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УведомлениеОКонтролируемыхСделках.Ссылка,
	|	УведомлениеОКонтролируемыхСделках.Номер,
	|	УведомлениеОКонтролируемыхСделках.Дата
	|ИЗ
	|	Документ.УведомлениеОКонтролируемыхСделках КАК УведомлениеОКонтролируемыхСделках
	|ГДЕ
	|	УведомлениеОКонтролируемыхСделках.Организация = &Организация
	|	И УведомлениеОКонтролируемыхСделках.ОтчетныйГод = &ОтчетныйГод
	|	И УведомлениеОКонтролируемыхСделках.НомерКорректировки = &НомерКорректировки
	|	И УведомлениеОКонтролируемыхСделках.Ссылка <> &Ссылка";
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Если НомерКорректировки > 0 Тогда
			ТекстИсключения = НСтр("ru = 'Корректировка №%НомерКорректировки% уведомления о контролируемых сделках за %ОтчетныйГод% уже существует';
									|en = 'Adjustment No. %НомерКорректировки% of notification of controlled transactions for %ОтчетныйГод% already exists'");
		Иначе
			ТекстИсключения = НСтр("ru = 'Уведомление о контролируемых сделках за %ОтчетныйГод% уже существует';
									|en = 'Controlled transaction notification for %ОтчетныйГод% already exists'");
		КонецЕсли;
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%НомерКорректировки%", НомерКорректировки);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ОтчетныйГод%", Формат(ОтчетныйГод, "ДФ=yyyy"));
		ВызватьИсключение(ТекстИсключения);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ВерсияУведомления) Тогда
		ВерсияУведомления = Документы.УведомлениеОКонтролируемыхСделках.ВерсияУведомленияПоОтчетномуГоду(ОтчетныйГод);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Формирует сведения необходимые для сохранения и передачи файла (файлов) электронного
// представления документа.
//
// Параметры:
//  ОсновныеСведенияВыгрузки - Структура - структура сведений выгрузки.
//
// Возвращаемое значение:
//  ТаблицаЗначений - сведения электронного представления документа, включающие в себя
//                    имя файла(файлов), текст(тексты) и кодировку представления.
//
Функция ЭлектронноеПредставление(ОсновныеСведенияВыгрузки)
	
	ПроизвольнаяСтрока = Новый ОписаниеТипов("Строка");
	
	СведенияЭлектронногоПредставления = Новый ТаблицаЗначений;
	СведенияЭлектронногоПредставления.Колонки.Добавить("ИмяФайла", ПроизвольнаяСтрока);
	СведенияЭлектронногоПредставления.Колонки.Добавить("ТекстФайла", ПроизвольнаяСтрока);
	СведенияЭлектронногоПредставления.Колонки.Добавить("КодировкаТекста", ПроизвольнаяСтрока);
	
	СтруктураВыгрузки = ИзвлечьСтруктуруXML();
	ЗаполнитьДанными(СтруктураВыгрузки, ОсновныеСведенияВыгрузки);
	
	Текст = ВыгрузитьДеревоВXML(СтруктураВыгрузки, ОсновныеСведенияВыгрузки);
	
	СтрокаСведенийЭлектронногоПредставления = СведенияЭлектронногоПредставления.Добавить();
	СтрокаСведенийЭлектронногоПредставления.ИмяФайла = ОсновныеСведенияВыгрузки.ИдФайл + ".xml";
	СтрокаСведенийЭлектронногоПредставления.ТекстФайла = Текст;
	СтрокаСведенийЭлектронногоПредставления.КодировкаТекста = "windows-1251";
	
	Если СведенияЭлектронногоПредставления.Количество() = 0 Тогда
		СведенияЭлектронногоПредставления = Неопределено;
	КонецЕсли;
	Возврат СведенияЭлектронногоПредставления;
	
КонецФункции

// Формирует сведения необходимые для сохранения и передачи файла (файлов) электронного
// представления документа.
//
// Возвращаемое значение:
//  ТаблицаЗначений - сведения электронного представления документа, включающие в себя
//                    имя файла(файлов), текст(тексты) и кодировку представления.
//
Функция ЭлектронноеПредставление502(ОсновныеСведенияВыгрузки, УникальныйИдентификатор = Неопределено)
	
	Контекст = КонтекстВыполненияВыгрузки(ОсновныеСведенияВыгрузки);
	
	ВременныйПакетФайлов = Новый Массив;
	
	ИдФайл = Контекст.ИдФайлПерв;
	
	ФайлВыгрузки = НовыйФайлВыгрузки_502(Контекст, ИдФайл);
	
	ЛистыУведомления = КонтролируемыеСделки.ПолучитьЛистыУведомления(ЭтотОбъект.Ссылка);
	
	УвеличитьОтступ(Контекст);
	
	Для Каждого Лист1А Из ЛистыУведомления.ЛистыРаздела1А Цикл
		ТекстБлокаСделки = ТекстПоСделке502(Контекст, ЛистыУведомления, Лист1А);
		
		РазмерБлокаСделки = РазмерВБайтахWin1251(ТекстБлокаСделки);
		
		Если (Контекст.ТекущийРазмер + РазмерБлокаСделки + Контекст.РазмерЗавершающихТэгов) > Контекст.РазмерРазделения Тогда
			ФайлВыгрузки.Записать(Контекст.ЗавершающиеТэги);
			ФайлВыгрузки.Закрыть();
			ВременныйПакетФайлов.Добавить("TMP_" + ИдФайл + ".xml");
			
			ИдФайл = Документы.УведомлениеОКонтролируемыхСделках.ИдентификаторФайлаЭлектронногоПредставления(Контекст.ОсновныеСведенияВыгрузки);
			ФайлВыгрузки = НовыйФайлВыгрузки_502(Контекст, ИдФайл);
		КонецЕсли;
		
		ФайлВыгрузки.Записать(ТекстБлокаСделки);
		Контекст.Вставить("ТекущийРазмер", Контекст.ТекущийРазмер + РазмерБлокаСделки);
	КонецЦикла;
	
	ФайлВыгрузки.Записать(Контекст.ЗавершающиеТэги);
	ФайлВыгрузки.Закрыть();
	ВременныйПакетФайлов.Добавить("TMP_" + ИдФайл + ".xml");
	
	ОкончательныйПакетФайлов = Неопределено;
	
	ВсегоФайлов = ВременныйПакетФайлов.Количество();
	
	Для Инд = 1 По ВсегоФайлов Цикл
		Если ОкончательныйПакетФайлов = Неопределено Тогда
			ОкончательныйПакетФайлов = Новый Массив;
		КонецЕсли;
		
		ИмяФайла = ВременныйПакетФайлов[Инд - 1];
		ВременныйФайл = Новый ЧтениеТекста(Контекст.ВременныйКаталог + ИмяФайла, "windows-1251");
		
		СтрокаОбъявления = ВременныйФайл.ПрочитатьСтроку();
		СтрокаТэгаФайл = ВременныйФайл.ПрочитатьСтроку();
		
		СтрокаТэгаФайл = СтрЗаменить(СтрокаТэгаФайл, "QQQQQ", XMLСтрока(ВсегоФайлов));
		СтрокаТэгаФайл = СтрЗаменить(СтрокаТэгаФайл, "NNNNN", XMLСтрока(Инд));
		
		ИмяИтоговогоФайла = СтрЗаменить(ИмяФайла, "TMP_", "");
		
		ИтоговыйФайл = Новый ЗаписьТекста(Контекст.ВременныйКаталог + ИмяИтоговогоФайла, "windows-1251");
		ИтоговыйФайл.ЗаписатьСтроку(СтрокаОбъявления);
		ИтоговыйФайл.Записать(СтрокаТэгаФайл);
		
		Пока Истина Цикл
			СледующаяСтрока = ВременныйФайл.ПрочитатьСтроку();
			Если СледующаяСтрока = Неопределено Тогда
				Прервать;
			КонецЕсли;
			СледующаяСтрока = Символы.ПС + СледующаяСтрока;
			ИтоговыйФайл.Записать(СледующаяСтрока);
		КонецЦикла;
		
		ВременныйФайл.Закрыть();
		ВременныйФайл = Неопределено; // Для сборщика мусора
		
		ИтоговыйФайл.Закрыть();
		ИтоговыйФайл = Неопределено; // Для сборщика мусора
		
		ФайлВыгрузки = Новый ДвоичныеДанные(Контекст.ВременныйКаталог + ИмяИтоговогоФайла);
		Если УникальныйИдентификатор <> Неопределено Тогда
			СсылкаНаДвоичныеДанныеФайла = ПоместитьВоВременноеХранилище(ФайлВыгрузки, УникальныйИдентификатор);
		Иначе
			СсылкаНаДвоичныеДанныеФайла = ПоместитьВоВременноеХранилище(ФайлВыгрузки, Новый УникальныйИдентификатор());
		КонецЕсли;
		
		СтруктураВыгрузки = Новый Структура;
		СтруктураВыгрузки.Вставить("АдресФайлаВыгрузки", СсылкаНаДвоичныеДанныеФайла);
		СтруктураВыгрузки.Вставить("ИмяФайлаВыгрузки", ИмяИтоговогоФайла);
		
		ОкончательныйПакетФайлов.Добавить(СтруктураВыгрузки);
		
		УдалитьФайлы(Контекст.ВременныйКаталог, ИмяФайла);
		УдалитьФайлы(Контекст.ВременныйКаталог, ИмяИтоговогоФайла);
		
	КонецЦикла;
	
	Возврат ОкончательныйПакетФайлов;
	
КонецФункции

// Формирует сведения необходимые для сохранения и передачи файла (файлов) электронного
// представления документа в формате 5.03 и 5.04.
//
// Возвращаемое значение:
//  ТаблицаЗначений - сведения электронного представления документа, включающие в себя
//                    имя файла(файлов), текст(тексты) и кодировку представления.
//
Функция ЭлектронноеПредставление503_504(ОсновныеСведенияВыгрузки, УникальныйИдентификатор = Неопределено)
	
	Контекст = КонтекстВыполненияВыгрузки(ОсновныеСведенияВыгрузки);
	
	ВременныйПакетФайлов = Новый Массив;
	
	ИдФайл = Контекст.ИдФайлПерв;
	
	ФайлВыгрузки = НовыйФайлВыгрузки_503_504(Контекст, ИдФайл);
	
	ЛистыУведомления = КонтролируемыеСделки.ПолучитьЛистыУведомления(ЭтотОбъект.Ссылка);
	
	УвеличитьОтступ(Контекст);
	
	Для Каждого Лист1А ИЗ ЛистыУведомления.ЛистыРаздела1А Цикл
		ТекстБлокаСделки = ТекстПоСделке503_504(Контекст, ЛистыУведомления, Лист1А);
		
		РазмерБлокаСделки = РазмерВБайтахWin1251(ТекстБлокаСделки);
		
		Если (Контекст.ТекущийРазмер + РазмерБлокаСделки + Контекст.РазмерЗавершающихТэгов) > Контекст.РазмерРазделения Тогда
			ФайлВыгрузки.Записать(Контекст.ЗавершающиеТэги);
			ФайлВыгрузки.Закрыть();
			ВременныйПакетФайлов.Добавить("TMP_" + ИдФайл + ".xml");
			
			ИдФайл = Документы.УведомлениеОКонтролируемыхСделках.ИдентификаторФайлаЭлектронногоПредставления(Контекст.ОсновныеСведенияВыгрузки);
			ФайлВыгрузки = НовыйФайлВыгрузки_503_504(Контекст, ИдФайл);
		КонецЕсли;
		
		ФайлВыгрузки.Записать(ТекстБлокаСделки);
		Контекст.Вставить("ТекущийРазмер", Контекст.ТекущийРазмер + РазмерБлокаСделки);
	КонецЦикла;
	
	ФайлВыгрузки.Записать(Контекст.ЗавершающиеТэги);
	ФайлВыгрузки.Закрыть();
	ВременныйПакетФайлов.Добавить("TMP_" + ИдФайл + ".xml");
	
	ОкончательныйПакетФайлов = Неопределено;
	
	ВсегоФайлов = ВременныйПакетФайлов.Количество();
	
	Для Инд = 1 По ВсегоФайлов Цикл
		Если ОкончательныйПакетФайлов = Неопределено Тогда
			ОкончательныйПакетФайлов = Новый Массив;
		КонецЕсли;
		
		ИмяФайла = ВременныйПакетФайлов[Инд - 1];
		ВременныйФайл = Новый ЧтениеТекста(Контекст.ВременныйКаталог + ИмяФайла, "windows-1251");
		
		СтрокаОбъявления = ВременныйФайл.ПрочитатьСтроку();
		СтрокаТэгаФайл = ВременныйФайл.ПрочитатьСтроку();
		
		СтрокаТэгаФайл = СтрЗаменить(СтрокаТэгаФайл, "IDDQD",
			?(ВсегоФайлов = 1, Контекст.ИдФайлПерв, Контекст.ИдФайлИсх));
		
		СтрокаТэгаФайл = СтрЗаменить(СтрокаТэгаФайл, "QQQQQ", XMLСтрока(ВсегоФайлов));
		СтрокаТэгаФайл = СтрЗаменить(СтрокаТэгаФайл, "NNNNN", XMLСтрока(Инд));
		
		ИмяИтоговогоФайла = СтрЗаменить(ИмяФайла, "TMP_", "");
		
		ИтоговыйФайл = Новый ЗаписьТекста(Контекст.ВременныйКаталог + ИмяИтоговогоФайла, "windows-1251");
		ИтоговыйФайл.ЗаписатьСтроку(СтрокаОбъявления);
		ИтоговыйФайл.Записать(СтрокаТэгаФайл);
		
		Пока Истина Цикл
			СледующаяСтрока = ВременныйФайл.ПрочитатьСтроку();
			Если СледующаяСтрока = Неопределено Тогда
				Прервать;
			КонецЕсли;
			СледующаяСтрока = Символы.ПС + СледующаяСтрока;
			ИтоговыйФайл.Записать(СледующаяСтрока);
		КонецЦикла;
		
		ВременныйФайл.Закрыть();
		ВременныйФайл = Неопределено; // Для сборщика мусора
		
		ИтоговыйФайл.Закрыть();
		ИтоговыйФайл = Неопределено; // Для сборщика мусора
		
		ФайлВыгрузки = Новый ДвоичныеДанные(Контекст.ВременныйКаталог + ИмяИтоговогоФайла);
		Если УникальныйИдентификатор <> Неопределено Тогда
			СсылкаНаДвоичныеДанныеФайла = ПоместитьВоВременноеХранилище(ФайлВыгрузки, УникальныйИдентификатор);
		Иначе
			СсылкаНаДвоичныеДанныеФайла = ПоместитьВоВременноеХранилище(ФайлВыгрузки, Новый УникальныйИдентификатор());
		КонецЕсли;
		
		СтруктураВыгрузки = Новый Структура;
		СтруктураВыгрузки.Вставить("АдресФайлаВыгрузки", СсылкаНаДвоичныеДанныеФайла);
		СтруктураВыгрузки.Вставить("ИмяФайлаВыгрузки", ИмяИтоговогоФайла);
		
		ОкончательныйПакетФайлов.Добавить(СтруктураВыгрузки);
		
		УдалитьФайлы(Контекст.ВременныйКаталог, ИмяФайла);
		УдалитьФайлы(Контекст.ВременныйКаталог, ИмяИтоговогоФайла);
		
	КонецЦикла;
	
	Возврат ОкончательныйПакетФайлов;
	
КонецФункции

// Формирует сведения необходимые для сохранения и передачи файла (файлов) электронного
// представления документа в формате 5.05.
//
// Возвращаемое значение:
//  ТаблицаЗначений - сведения электронного представления документа, включающие в себя
//                    имя файла(файлов), текст(тексты) и кодировку представления.
//
Функция ЭлектронноеПредставление505(ОсновныеСведенияВыгрузки, УникальныйИдентификатор = Неопределено)
	
	Контекст = КонтекстВыполненияВыгрузки(ОсновныеСведенияВыгрузки);
	
	ВременныйПакетФайлов = Новый Массив;
	
	ИдФайл = Контекст.ИдФайлПерв;
	
	ФайлВыгрузки = НовыйФайлВыгрузки_505(Контекст, ИдФайл);
	
	ЛистыУведомления = КонтролируемыеСделки.ПолучитьЛистыУведомления(ЭтотОбъект.Ссылка);
	
	УвеличитьОтступ(Контекст);
	
	Для Каждого Лист1А ИЗ ЛистыУведомления.ЛистыРаздела1А Цикл
		ТекстБлокаСделки = ТекстПоСделке505(Контекст, ЛистыУведомления, Лист1А);
		
		РазмерБлокаСделки = РазмерВБайтахWin1251(ТекстБлокаСделки);
		
		Если (Контекст.ТекущийРазмер + РазмерБлокаСделки + Контекст.РазмерЗавершающихТэгов) > Контекст.РазмерРазделения Тогда
			ФайлВыгрузки.Записать(Контекст.ЗавершающиеТэги);
			ФайлВыгрузки.Закрыть();
			ВременныйПакетФайлов.Добавить("TMP_" + ИдФайл + ".xml");
			
			ИдФайл = Документы.УведомлениеОКонтролируемыхСделках.ИдентификаторФайлаЭлектронногоПредставления(Контекст.ОсновныеСведенияВыгрузки);
			ФайлВыгрузки = НовыйФайлВыгрузки_505(Контекст, ИдФайл);
		КонецЕсли;
		
		ФайлВыгрузки.Записать(ТекстБлокаСделки);
		Контекст.Вставить("ТекущийРазмер", Контекст.ТекущийРазмер + РазмерБлокаСделки);
	КонецЦикла;
	
	ФайлВыгрузки.Записать(Контекст.ЗавершающиеТэги);
	ФайлВыгрузки.Закрыть();
	ВременныйПакетФайлов.Добавить("TMP_" + ИдФайл + ".xml");
	
	ПакетФайлов = СформироватьОкончательныйПакетФайлов(Контекст, ВременныйПакетФайлов, УникальныйИдентификатор);
	ОчиститьКонтекст(Контекст);
	Возврат ПакетФайлов;
	
КонецФункции

// Очищает файлы во временном каталоге контекста выполнения выгрузки.
// 
// Параметры:
//  Контекст - Структура - структура измерений:
//  * ВременныйКаталог         - Строка -путь временного каталога
//  * ЗавершающиеТэги          - Строка - описание завершающих тегов.
//  * ИдФайлИсх                - Строка
//  * ИдФайлПерв               - Строка
//  * ОсновныеСведенияВыгрузки - Структура - описание структуры выгрузки.
//  * Отступ                   - Строка - описание табуляции.
//  * РазмерЗавершающихТэгов   - Число - размер завершающих тегов.
//  * РазмерРазделения         - Число
//  * ТекущийРазмер            - Число
//  ВременныйПакетФайлов       - Массив из Строка - массив названий временных файлов xml.
//  УникальныйИдентификатор    - УникальныйИдентификатор - ункальный идентификатор временного хранилища файла.
//
Процедура ОчиститьКонтекст(Контекст)
	
	УдалитьФайлы(Контекст.ВременныйКаталог);
	
КонецПроцедуры

// Возвращает массив окончательных пакетов файлов.
// 
// Параметры:
//  Контекст - Структура - структура измерений:
//  * ВременныйКаталог         - Строка -путь временного каталога
//  * ЗавершающиеТэги          - Строка - описание завершающих тегов.
//  * ИдФайлИсх                - Строка
//  * ИдФайлПерв               - Строка
//  * ОсновныеСведенияВыгрузки - Структура - описание структуры выгрузки.
//  * Отступ                   - Строка - описание табуляции.
//  * РазмерЗавершающихТэгов   - Число - размер завершающих тегов.
//  * РазмерРазделения         - Число
//  * ТекущийРазмер            - Число
//  ВременныйПакетФайлов       - Массив из Строка - массив названий временных файлов xml.
//  УникальныйИдентификатор    - УникальныйИдентификатор - ункальный идентификатор временного хранилища файла.
//  Возвращаемое значение:
//  Массив из Структура - массив структур окончательных пакетов файлов. Поля структуры:
//  * АдресФайлаВыгрузки       - Строка - адрес во временном хранилище на ссылку двоичных данных файла.
//  * ИмяФайлаВыгрузки         - Строка - имя файла выгрузки.
//
Функция СформироватьОкончательныйПакетФайлов(Контекст, ВременныйПакетФайлов, УникальныйИдентификатор)
	
	ОкончательныйПакетФайлов = Неопределено;
	
	ВсегоФайлов = ВременныйПакетФайлов.Количество();
	
	Для Инд = 1 По ВсегоФайлов Цикл
		Если ОкончательныйПакетФайлов = Неопределено Тогда
			ОкончательныйПакетФайлов = Новый Массив;
		КонецЕсли;
		
		ИмяФайла = ВременныйПакетФайлов[Инд - 1];
		ВременныйФайл = Новый ЧтениеТекста(Контекст.ВременныйКаталог + ИмяФайла, "windows-1251");
		
		СтрокаОбъявления = ВременныйФайл.ПрочитатьСтроку();
		СтрокаТэгаФайл = ВременныйФайл.ПрочитатьСтроку();
		
		СтрокаТэгаФайл = СтрЗаменить(СтрокаТэгаФайл, "IDDQD",
			?(ВсегоФайлов = 1, Контекст.ИдФайлПерв, Контекст.ИдФайлИсх));
		
		СтрокаТэгаФайл = СтрЗаменить(СтрокаТэгаФайл, "QQQQQ", XMLСтрока(ВсегоФайлов));
		СтрокаТэгаФайл = СтрЗаменить(СтрокаТэгаФайл, "NNNNN", XMLСтрока(Инд));
		
		ИмяИтоговогоФайла = СтрЗаменить(ИмяФайла, "TMP_", "");
		
		ИтоговыйФайл = Новый ЗаписьТекста(Контекст.ВременныйКаталог + ИмяИтоговогоФайла, "windows-1251");
		ИтоговыйФайл.ЗаписатьСтроку(СтрокаОбъявления);
		ИтоговыйФайл.Записать(СтрокаТэгаФайл);
		
		Пока Истина Цикл
			СледующаяСтрока = ВременныйФайл.ПрочитатьСтроку();
			Если СледующаяСтрока = Неопределено Тогда
				Прервать;
			КонецЕсли;
			СледующаяСтрока = Символы.ПС + СледующаяСтрока;
			ИтоговыйФайл.Записать(СледующаяСтрока);
		КонецЦикла;
		
		ВременныйФайл.Закрыть();
		ВременныйФайл = Неопределено; // Для сборщика мусора
		
		ИтоговыйФайл.Закрыть();
		ИтоговыйФайл = Неопределено; // Для сборщика мусора
		
		ФайлВыгрузки = Новый ДвоичныеДанные(Контекст.ВременныйКаталог + ИмяИтоговогоФайла);
		Если УникальныйИдентификатор <> Неопределено Тогда
			СсылкаНаДвоичныеДанныеФайла = ПоместитьВоВременноеХранилище(ФайлВыгрузки, УникальныйИдентификатор);
		Иначе
			СсылкаНаДвоичныеДанныеФайла = ПоместитьВоВременноеХранилище(ФайлВыгрузки, Новый УникальныйИдентификатор());
		КонецЕсли;
		
		СтруктураВыгрузки = Новый Структура;
		СтруктураВыгрузки.Вставить("АдресФайлаВыгрузки", СсылкаНаДвоичныеДанныеФайла);
		СтруктураВыгрузки.Вставить("ИмяФайлаВыгрузки", ИмяИтоговогоФайла);
		
		ОкончательныйПакетФайлов.Добавить(СтруктураВыгрузки);
		
		УдалитьФайлы(Контекст.ВременныйКаталог, ИмяФайла);
		УдалитьФайлы(Контекст.ВременныйКаталог, ИмяИтоговогоФайла);
		
	КонецЦикла;
	
	Возврат ОкончательныйПакетФайлов;
	
КонецФункции

// Возвращает массив окончательных пакетов файлов.
// Поддерживаемый формат уведомления: 5.05.
// Параметры:
//  Контекст - Структура - структура измерений:
//  * ВременныйКаталог         - Строка -путь временного каталога
//  * ЗавершающиеТэги          - Строка - описание завершающих тегов.
//  * ИдФайлИсх                - Строка
//  * ИдФайлПерв               - Строка
//  * ОсновныеСведенияВыгрузки - Структура - описание структуры выгрузки.
//  * Отступ                   - Строка - описание табуляции.
//  * РазмерЗавершающихТэгов   - Число - размер завершающих тегов.
//  * РазмерРазделения         - Число
//  * ТекущийРазмер            - Число
//  ИдентификаторФайла         - Строка - идентификатор файла.
//  Возвращаемое значение:
//  ЗаписьТекста - файл выгрузки.
//
Функция НовыйФайлВыгрузки_505(Контекст, ИдентификаторФайла)
	
	ОсновныеСведенияВыгрузки = Контекст.ОсновныеСведенияВыгрузки;
	
	Контекст.Вставить("Отступ", "");
	
	ТекстФайла = Новый ТекстовыйДокумент;
	
	ТекстФайла.ДобавитьСтроку("<?xml version=""1.0"" encoding=""windows-1251""?>");
	
	Шаблон_Файл = "<Файл xmlns:xsi=""http://www.w3.org/2001/XMLSchema-instance"" %ИдФайл% %ВерсПрог% %ВерсФорм% %ИдФайлИсх% %ИдФайлПерв% КолФайл=""QQQQQ"" НомФайл=""NNNNN"">";
	
	УстановитьАтрибут(Шаблон_Файл, "ИдФайл", ИдентификаторФайла);
	УстановитьАтрибут(Шаблон_Файл, "ВерсПрог", ОсновныеСведенияВыгрузки.ВерсПрог);
	УстановитьАтрибут(Шаблон_Файл, "ВерсФорм", "5.05");
	УстановитьАтрибут(Шаблон_Файл, "ИдФайлИсх", "IDDQD");
	УстановитьАтрибут(Шаблон_Файл, "ИдФайлПерв", Контекст.ИдФайлПерв);
	
	ТекстФайла.ДобавитьСтроку(Контекст.Отступ + Шаблон_Файл);
	
	УвеличитьОтступ(Контекст);
	
	Шаблон_Документ = "<Документ КНД=""1110025"" %ДатаДок% %ОтчетГод% %КодНО% %НомКорр% %ПоМесту%>";
	УстановитьАтрибут(Шаблон_Документ, "ДатаДок", ОсновныеСведенияВыгрузки.ДатаДок);
	УстановитьАтрибут(Шаблон_Документ, "ОтчетГод", ОсновныеСведенияВыгрузки.ОтчетГод);
	УстановитьАтрибут(Шаблон_Документ, "КодНО", ОсновныеСведенияВыгрузки.КодНО);
	УстановитьАтрибут(Шаблон_Документ, "НомКорр", ОсновныеСведенияВыгрузки.НомКорр);
	УстановитьАтрибут(Шаблон_Документ, "ПоМесту", ОсновныеСведенияВыгрузки.ПоМесту);
	
	ТекстФайла.ДобавитьСтроку(Контекст.Отступ + Шаблон_Документ);
	
	УвеличитьОтступ(Контекст);
	
	Шаблон_СвНП = "<СвНП %ОКТМО% %Тлф% %E-mail%>";
	УстановитьАтрибут(Шаблон_СвНП, "ОКТМО", ОсновныеСведенияВыгрузки.ОКТМО);
	УстановитьНеобязательныйАтрибут(Шаблон_СвНП, "Тлф", ОсновныеСведенияВыгрузки.Тлф);
	УстановитьНеобязательныйАтрибут(Шаблон_СвНП, "E-mail", ОсновныеСведенияВыгрузки.ЭлПочта);
	
	ТекстФайла.ДобавитьСтроку(Контекст.Отступ + Шаблон_СвНП);
	
	УвеличитьОтступ(Контекст);
	
	Если ОсновныеСведенияВыгрузки.ЭтоПБОЮЛ Тогда
		ЗаполнитьСведенияОФизлице_503_504_505(Контекст, ТекстФайла);
	Иначе
		ЗаполнитьСведенияОрганизации(Контекст, ТекстФайла);
	КонецЕсли;
	
	УменьшитьОтступ(Контекст);
	
	ТекстФайла.ДобавитьСтроку(Контекст.Отступ + "</СвНП>");
	
	Шаблон_Подписант = "<Подписант %ПрПодп%>";
	УстановитьАтрибут(Шаблон_Подписант, "ПрПодп", ОсновныеСведенияВыгрузки.ПрПодп);
	
	ТекстФайла.ДобавитьСтроку(Контекст.Отступ + Шаблон_Подписант);
	
	УвеличитьОтступ(Контекст);
	
	Если ЕстьЗаполненныеРеквизиты(ОсновныеСведенияВыгрузки, "ПодпФамилия, ПодпИмя, ПодпОтчество") Тогда
		Шаблон_ФИО = "<ФИО %Фамилия% %Имя% %Отчество%/>";
		УстановитьАтрибут(Шаблон_ФИО, "Фамилия", ОсновныеСведенияВыгрузки.ПодпФамилия);
		УстановитьАтрибут(Шаблон_ФИО, "Имя", ОсновныеСведенияВыгрузки.ПодпИмя);
		УстановитьНеобязательныйАтрибут(Шаблон_ФИО, "Отчество", ОсновныеСведенияВыгрузки.ПодпОтчество);
		
		ТекстФайла.ДобавитьСтроку(Контекст.Отступ + Шаблон_ФИО);
	КонецЕсли;
	
	Если ЕстьЗаполненныеРеквизиты(ОсновныеСведенияВыгрузки, "НаимДокПодп, НаимОргПодп") Тогда
		Шаблон_СвПред = "<СвПред %НаимДок%/>";
		УстановитьАтрибут(Шаблон_СвПред, "НаимДок", ОсновныеСведенияВыгрузки.НаимДокПодп);
		
		ТекстФайла.ДобавитьСтроку(Контекст.Отступ + Шаблон_СвПред);
	КонецЕсли;
	
	УменьшитьОтступ(Контекст);
	
	ТекстФайла.ДобавитьСтроку(Контекст.Отступ + "</Подписант>");
	
	ТекстФайла.ДобавитьСтроку(Контекст.Отступ + "<УвКонтрСд>");
	
	ТекстФайла = ТекстФайла.ПолучитьТекст();
	
	Контекст.Вставить("ТекущийРазмер", РазмерВБайтахWin1251(ТекстФайла));
	
	ФайлВыгрузки = Новый ЗаписьТекста(Контекст.ВременныйКаталог + "TMP_" + ИдентификаторФайла + ".xml", "windows-1251");
	ФайлВыгрузки.Записать(ТекстФайла);
	
	Возврат ФайлВыгрузки;
	
КонецФункции

// Контекст выполнения выгрузки.
// 
// Параметры:
//  ОсновныеСведенияВыгрузки - Структура - Основные сведения выгрузки
// 
// Возвращаемое значение:
//  Структура
Функция КонтекстВыполненияВыгрузки(ОсновныеСведенияВыгрузки)
	
	ИдФайлИсх = ОсновныеСведенияВыгрузки.ИдФайл;
	ИдФайлПерв = Документы.УведомлениеОКонтролируемыхСделках.ИдентификаторФайлаЭлектронногоПредставления(ОсновныеСведенияВыгрузки);
	
	РазмерРазделения = 100 * 1024 * 1024; // 100 Мб
	
	ЗавершающиеТэги = "		</УвКонтрСд>
	                  |	</Документ>
	                  |</Файл>";
	РазмерЗавершающихТэгов = РазмерВБайтахWin1251(ЗавершающиеТэги);
	
	Контекст = Новый Структура;
	Контекст.Вставить("ОсновныеСведенияВыгрузки", ОсновныеСведенияВыгрузки);
	
	Контекст.Вставить("РазмерРазделения", РазмерРазделения);
	
	Контекст.Вставить("ИдФайлИсх", ИдФайлИсх);
	Контекст.Вставить("ИдФайлПерв", ИдФайлПерв);
	
	Контекст.Вставить("ЗавершающиеТэги", ЗавершающиеТэги);
	Контекст.Вставить("РазмерЗавершающихТэгов", РазмерЗавершающихТэгов);
	
	Контекст.Вставить("Отступ", "");
	
	ВременныйКаталог = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(ПолучитьИмяВременногоФайла());
	СоздатьКаталог(ВременныйКаталог);
	Контекст.Вставить("ВременныйКаталог", ВременныйКаталог);
	
	Возврат Контекст;
	
КонецФункции

// Размер в байтах win1251.
// 
// Параметры:
//  ИзмеряемыйТекст - Строка - Измеряемый текст
// 
// Возвращаемое значение:
// 	Число
//  
Функция РазмерВБайтахWin1251(ИзмеряемыйТекст)
	
	КоличествоВхожденийПС = СтрЧислоВхождений(ИзмеряемыйТекст, Символы.ПС);
	Результат = СтрДлина(ИзмеряемыйТекст) + КоличествоВхожденийПС; // Перевод строки кодируется 2 байтами
	
	Возврат Результат;
	
КонецФункции

// Возвращает массив окончательных пакетов файлов.
// Поддерживаемый формат уведомления: 5.02.
// Параметры:
//  Контекст - Структура - структура измерений:
//  * ВременныйКаталог         - Строка -путь временного каталога
//  * ЗавершающиеТэги          - Строка - описание завершающих тегов.
//  * ИдФайлИсх                - Строка
//  * ИдФайлПерв               - Строка
//  * ОсновныеСведенияВыгрузки - Структура - описание структуры выгрузки.
//  * Отступ                   - Строка - описание табуляции.
//  * РазмерЗавершающихТэгов   - Число - размер завершающих тегов.
//  * РазмерРазделения         - Число
//  * ТекущийРазмер            - Число
//  ИдентификаторФайла         - Строка - идентификатор файла.
//  Возвращаемое значение:
//  ЗаписьТекста - файл выгрузки.
//
Функция НовыйФайлВыгрузки_502(Контекст, ИдентификаторФайла)
	
	ОсновныеСведенияВыгрузки = Контекст.ОсновныеСведенияВыгрузки;
	
	Контекст.Вставить("Отступ", "");
	
	ТекстФайла = Новый ТекстовыйДокумент;
	
	ТекстФайла.ДобавитьСтроку("<?xml version=""1.0"" encoding=""windows-1251""?>");
	
	Шаблон_Файл = "<Файл xmlns:xsi=""http://www.w3.org/2001/XMLSchema-instance"" %ИдФайл% %ВерсПрог% ВерсФорм=""5.02"" %ИдФайлИсх% %ИдФайлПерв% КолФайл=""QQQQQ"" НомФайл=""NNNNN"">";
	УстановитьАтрибут(Шаблон_Файл, "ИдФайл", ИдентификаторФайла);
	УстановитьАтрибут(Шаблон_Файл, "ВерсПрог", ОсновныеСведенияВыгрузки.ВерсПрог);
	УстановитьАтрибут(Шаблон_Файл, "ИдФайлИсх", Контекст.ИдФайлИсх);
	УстановитьАтрибут(Шаблон_Файл, "ИдФайлПерв", Контекст.ИдФайлПерв);
	
	ТекстФайла.ДобавитьСтроку(Контекст.Отступ + Шаблон_Файл);
	
	УвеличитьОтступ(Контекст);
	
	Шаблон_Документ = "<Документ КНД=""1110025"" %ДатаДок% %ОтчетГод% %КодНО% %НомКорр% %ПоМесту%>";
	УстановитьАтрибут(Шаблон_Документ, "ДатаДок", ОсновныеСведенияВыгрузки.ДатаДок);
	УстановитьАтрибут(Шаблон_Документ, "ОтчетГод", ОсновныеСведенияВыгрузки.ОтчетГод);
	УстановитьАтрибут(Шаблон_Документ, "КодНО", ОсновныеСведенияВыгрузки.КодНО);
	УстановитьАтрибут(Шаблон_Документ, "НомКорр", ОсновныеСведенияВыгрузки.НомКорр);
	УстановитьАтрибут(Шаблон_Документ, "ПоМесту", ОсновныеСведенияВыгрузки.ПоМесту);
	
	ТекстФайла.ДобавитьСтроку(Контекст.Отступ + Шаблон_Документ);
	
	УвеличитьОтступ(Контекст);
	
	Шаблон_СвНП = "<СвНП %ОКАТО% %ОКВЭД% %Тлф% %E-mail%>";
	УстановитьАтрибут(Шаблон_СвНП, "ОКАТО", ?(ОтчетныйГод < Дата(2012,12,31), ОсновныеСведенияВыгрузки.ОКАТО, ОсновныеСведенияВыгрузки.ОКТМО));
	УстановитьНеобязательныйАтрибут(Шаблон_СвНП, "ОКВЭД", ОсновныеСведенияВыгрузки.ОКВЭД);
	УстановитьНеобязательныйАтрибут(Шаблон_СвНП, "Тлф", ОсновныеСведенияВыгрузки.Тлф);
	УстановитьНеобязательныйАтрибут(Шаблон_СвНП, "E-mail", ОсновныеСведенияВыгрузки.ЭлПочта);
	
	ТекстФайла.ДобавитьСтроку(Контекст.Отступ + Шаблон_СвНП);
	
	УвеличитьОтступ(Контекст);
	
	Если ОсновныеСведенияВыгрузки.ЭтоПБОЮЛ Тогда
		ЗаполнитьСведенияОФизлице_502(Контекст, ТекстФайла);
	Иначе
		ЗаполнитьСведенияОрганизации(Контекст, ТекстФайла);
	КонецЕсли;
	
	УменьшитьОтступ(Контекст);
	
	ТекстФайла.ДобавитьСтроку(Контекст.Отступ + "</СвНП>");
	
	Шаблон_Подписант = "<Подписант %ПрПодп%>";
	УстановитьАтрибут(Шаблон_Подписант, "ПрПодп", ОсновныеСведенияВыгрузки.ПрПодп);
	
	ТекстФайла.ДобавитьСтроку(Контекст.Отступ + Шаблон_Подписант);
	
	УвеличитьОтступ(Контекст);
	
	Если ЕстьЗаполненныеРеквизиты(ОсновныеСведенияВыгрузки, "ПодпФамилия, ПодпИмя, ПодпОтчество") Тогда
		Шаблон_ФИО = "<ФИО %Фамилия% %Имя% %Отчество%/>";
		УстановитьАтрибут(Шаблон_ФИО, "Фамилия", ОсновныеСведенияВыгрузки.ПодпФамилия);
		УстановитьАтрибут(Шаблон_ФИО, "Имя", ОсновныеСведенияВыгрузки.ПодпИмя);
		УстановитьНеобязательныйАтрибут(Шаблон_ФИО, "Отчество", ОсновныеСведенияВыгрузки.ПодпОтчество);
		
		ТекстФайла.ДобавитьСтроку(Контекст.Отступ + Шаблон_ФИО);
	КонецЕсли;
	
	Если ЕстьЗаполненныеРеквизиты(ОсновныеСведенияВыгрузки, "НаимДокПодп, НаимОргПодп") Тогда
		Шаблон_СвПред = "<СвПред %НаимДок% %НаимОрг%/>";
		УстановитьАтрибут(Шаблон_СвПред, "НаимДок", ОсновныеСведенияВыгрузки.НаимДокПодп);
		УстановитьНеобязательныйАтрибут(Шаблон_СвПред, "НаимОрг", ?(ОсновныеСведенияВыгрузки.Свойство("НаимОргПодп"), ОсновныеСведенияВыгрузки.НаимОргПодп, ""));
		
		ТекстФайла.ДобавитьСтроку(Контекст.Отступ + Шаблон_СвПред);
	КонецЕсли;
	
	УменьшитьОтступ(Контекст);
	
	ТекстФайла.ДобавитьСтроку(Контекст.Отступ + "</Подписант>");
	
	ТекстФайла.ДобавитьСтроку(Контекст.Отступ + "<УвКонтрСд>");
	
	ТекстФайла = ТекстФайла.ПолучитьТекст();
	
	Контекст.Вставить("ТекущийРазмер", РазмерВБайтахWin1251(ТекстФайла));
	
	ФайлВыгрузки = Новый ЗаписьТекста(Контекст.ВременныйКаталог + "TMP_" + ИдентификаторФайла + ".xml", "windows-1251");
	ФайлВыгрузки.Записать(ТекстФайла);
	
	Возврат ФайлВыгрузки;
	
КонецФункции

// Возвращает массив окончательных пакетов файлов.
// Поддерживаемые форматы уведомления: 5.03, 5.04.
// Параметры:
//  Контекст - Структура - структура измерений:
//  * ВременныйКаталог         - Строка -путь временного каталога
//  * ЗавершающиеТэги          - Строка - описание завершающих тегов.
//  * ИдФайлИсх                - Строка
//  * ИдФайлПерв               - Строка
//  * ОсновныеСведенияВыгрузки - Структура - описание структуры выгрузки.
//  * Отступ                   - Строка - описание табуляции.
//  * РазмерЗавершающихТэгов   - Число - размер завершающих тегов.
//  * РазмерРазделения         - Число
//  * ТекущийРазмер            - Число
//  ИдентификаторФайла         - Строка - идентификатор файла.
//  Возвращаемое значение:
//  ЗаписьТекста - файл выгрузки.
//
Функция НовыйФайлВыгрузки_503_504(Контекст, ИдентификаторФайла)
	
	ОсновныеСведенияВыгрузки = Контекст.ОсновныеСведенияВыгрузки;
	
	Контекст.Вставить("Отступ", "");
	
	ТекстФайла = Новый ТекстовыйДокумент;
	
	ТекстФайла.ДобавитьСтроку("<?xml version=""1.0"" encoding=""windows-1251""?>");
	
	Шаблон_Файл = "<Файл xmlns:xsi=""http://www.w3.org/2001/XMLSchema-instance"" %ИдФайл% %ВерсПрог% %ВерсФорм% %ИдФайлИсх% %ИдФайлПерв% КолФайл=""QQQQQ"" НомФайл=""NNNNN"">";
	
	УстановитьАтрибут(Шаблон_Файл, "ИдФайл", ИдентификаторФайла);
	УстановитьАтрибут(Шаблон_Файл, "ВерсПрог", ОсновныеСведенияВыгрузки.ВерсПрог);
	ВерсияФорматаСтрокой = ?(ОсновныеСведенияВыгрузки.ВерсияУведомления 
		>= КонтролируемыеСделкиКлиентСервер.ВерсияУведомления_2019(), "5.04", "5.03");
	УстановитьАтрибут(Шаблон_Файл, "ВерсФорм", ВерсияФорматаСтрокой);
	УстановитьАтрибут(Шаблон_Файл, "ИдФайлИсх", "IDDQD");
	УстановитьАтрибут(Шаблон_Файл, "ИдФайлПерв", Контекст.ИдФайлПерв);
	
	ТекстФайла.ДобавитьСтроку(Контекст.Отступ + Шаблон_Файл);
	
	УвеличитьОтступ(Контекст);
	
	Шаблон_Документ = "<Документ КНД=""1110025"" %ДатаДок% %ОтчетГод% %КодНО% %НомКорр% %ПоМесту%>";
	УстановитьАтрибут(Шаблон_Документ, "ДатаДок", ОсновныеСведенияВыгрузки.ДатаДок);
	УстановитьАтрибут(Шаблон_Документ, "ОтчетГод", ОсновныеСведенияВыгрузки.ОтчетГод);
	УстановитьАтрибут(Шаблон_Документ, "КодНО", ОсновныеСведенияВыгрузки.КодНО);
	УстановитьАтрибут(Шаблон_Документ, "НомКорр", ОсновныеСведенияВыгрузки.НомКорр);
	УстановитьАтрибут(Шаблон_Документ, "ПоМесту", ОсновныеСведенияВыгрузки.ПоМесту);
	
	ТекстФайла.ДобавитьСтроку(Контекст.Отступ + Шаблон_Документ);
	
	УвеличитьОтступ(Контекст);
	
	Шаблон_СвНП = "<СвНП %ОКТМО% %Тлф% %E-mail%>";
	УстановитьАтрибут(Шаблон_СвНП, "ОКТМО", ОсновныеСведенияВыгрузки.ОКТМО);
	УстановитьНеобязательныйАтрибут(Шаблон_СвНП, "Тлф", ОсновныеСведенияВыгрузки.Тлф);
	УстановитьНеобязательныйАтрибут(Шаблон_СвНП, "E-mail", ОсновныеСведенияВыгрузки.ЭлПочта);
	
	ТекстФайла.ДобавитьСтроку(Контекст.Отступ + Шаблон_СвНП);
	
	УвеличитьОтступ(Контекст);
	
	Если ОсновныеСведенияВыгрузки.ЭтоПБОЮЛ Тогда
		ЗаполнитьСведенияОФизлице_503_504_505(Контекст, ТекстФайла);
	Иначе
		ЗаполнитьСведенияОрганизации(Контекст, ТекстФайла);
	КонецЕсли;
	
	УменьшитьОтступ(Контекст);
	
	ТекстФайла.ДобавитьСтроку(Контекст.Отступ + "</СвНП>");
	
	Шаблон_Подписант = "<Подписант %ПрПодп%>";
	УстановитьАтрибут(Шаблон_Подписант, "ПрПодп", ОсновныеСведенияВыгрузки.ПрПодп);
	
	ТекстФайла.ДобавитьСтроку(Контекст.Отступ + Шаблон_Подписант);
	
	УвеличитьОтступ(Контекст);
	
	Если ЕстьЗаполненныеРеквизиты(ОсновныеСведенияВыгрузки, "ПодпФамилия, ПодпИмя, ПодпОтчество") Тогда
		Шаблон_ФИО = "<ФИО %Фамилия% %Имя% %Отчество%/>";
		УстановитьАтрибут(Шаблон_ФИО, "Фамилия", ОсновныеСведенияВыгрузки.ПодпФамилия);
		УстановитьАтрибут(Шаблон_ФИО, "Имя", ОсновныеСведенияВыгрузки.ПодпИмя);
		УстановитьНеобязательныйАтрибут(Шаблон_ФИО, "Отчество", ОсновныеСведенияВыгрузки.ПодпОтчество);
		
		ТекстФайла.ДобавитьСтроку(Контекст.Отступ + Шаблон_ФИО);
	КонецЕсли;
	
	Если ЕстьЗаполненныеРеквизиты(ОсновныеСведенияВыгрузки, "НаимДокПодп, НаимОргПодп") Тогда
		Шаблон_СвПред = "<СвПред %НаимДок% %НаимОрг%/>";
		УстановитьАтрибут(Шаблон_СвПред, "НаимДок", ОсновныеСведенияВыгрузки.НаимДокПодп);
		УстановитьНеобязательныйАтрибут(Шаблон_СвПред, "НаимОрг", ?(ОсновныеСведенияВыгрузки.Свойство("НаимОргПодп"), ОсновныеСведенияВыгрузки.НаимОргПодп, ""));
		
		ТекстФайла.ДобавитьСтроку(Контекст.Отступ + Шаблон_СвПред);
	КонецЕсли;
	
	УменьшитьОтступ(Контекст);
	
	ТекстФайла.ДобавитьСтроку(Контекст.Отступ + "</Подписант>");
	
	ТекстФайла.ДобавитьСтроку(Контекст.Отступ + "<УвКонтрСд>");
	
	ТекстФайла = ТекстФайла.ПолучитьТекст();
	
	Контекст.Вставить("ТекущийРазмер", РазмерВБайтахWin1251(ТекстФайла));
	
	ФайлВыгрузки = Новый ЗаписьТекста(Контекст.ВременныйКаталог + "TMP_" + ИдентификаторФайла + ".xml", "windows-1251");
	ФайлВыгрузки.Записать(ТекстФайла);
	
	Возврат ФайлВыгрузки;
	
КонецФункции

// Увеличивает отступ в структуре контекста.
// 
// Параметры:
//  КонтекстВыполнения - Структура - структура измерений:
//  * ВременныйКаталог         - Строка -путь временного каталога
//  * ЗавершающиеТэги          - Строка - описание завершающих тегов.
//  * ИдФайлИсх                - Строка
//  * ИдФайлПерв               - Строка
//  * ОсновныеСведенияВыгрузки - Структура - описание структуры выгрузки.
//  * Отступ                   - Строка - описание табуляции.
//  * РазмерЗавершающихТэгов   - Число - размер завершающих тегов.
//  * РазмерРазделения         - Число
//  * ТекущийРазмер            - Число
//
Процедура УвеличитьОтступ(КонтекстВыполнения)
	
	КонтекстВыполнения.Вставить("Отступ", КонтекстВыполнения.Отступ + Символы.Таб);
	
КонецПроцедуры

// Уменьшаает отступ в структуре контекста.
// 
// Параметры:
//  КонтекстВыполнения - Структура - структура измерений:
//  * ВременныйКаталог         - Строка -путь временного каталога
//  * ЗавершающиеТэги          - Строка - описание завершающих тегов.
//  * ИдФайлИсх                - Строка
//  * ИдФайлПерв               - Строка
//  * ОсновныеСведенияВыгрузки - Структура - описание структуры выгрузки.
//  * Отступ                   - Строка - описание табуляции.
//  * РазмерЗавершающихТэгов   - Число - размер завершающих тегов.
//  * РазмерРазделения         - Число
//  * ТекущийРазмер            - Число
//
Процедура УменьшитьОтступ(КонтекстВыполнения)
	
	КонтекстВыполнения.Вставить("Отступ", Сред(КонтекстВыполнения.Отступ, 2));
	
КонецПроцедуры

// Устанавливает значение атрибута и шаблона элемента для xml файла.
//
// Параметры:
//  ШаблонЭлемента   - Строка - шаблон элемента xml файла.
//  ИмяАтрибута      - Строка - имя атрибута xml строки.
//  ЗначениеАтрибута - Строка - значение атрибута xml строки.
//
Процедура УстановитьАтрибут(ШаблонЭлемента, ИмяАтрибута, Знач ЗначениеАтрибута)
	
	Если ТипЗнч(ЗначениеАтрибута) = Тип("Строка") Тогда
		ЗначениеАтрибута = СтрЗаменить(ЗначениеАтрибута, "&", "&amp;");
		ЗначениеАтрибута = СтрЗаменить(ЗначениеАтрибута, "<", "&lt;");
		ЗначениеАтрибута = СтрЗаменить(ЗначениеАтрибута, ">", "&gt;");
		ЗначениеАтрибута = СтрЗаменить(ЗначениеАтрибута, """", "&quot;");
		ЗначениеАтрибута = СтрЗаменить(ЗначениеАтрибута, "'", "&apos;");
	ИначеЕсли ТипЗнч(ЗначениеАтрибута) = Тип("Дата") Тогда
		ЗначениеАтрибута = Формат(ЗначениеАтрибута, "ДЛФ=D");
	Иначе
		ЗначениеАтрибута = XMLСтрока(ЗначениеАтрибута);
	КонецЕсли;
	
	ШаблонЭлемента = СтрЗаменить(ШаблонЭлемента, "%" + ИмяАтрибута + "%", ИмяАтрибута + "=""" + ЗначениеАтрибута + """");
	
КонецПроцедуры

// Устанавливает значение необязательного атрибута и шаблона элемента для xml файла.
//
// Параметры:
//  ШаблонЭлемента   - Строка - шаблон элемента xml файла.
//  ИмяАтрибута      - Строка - имя атрибута xml строки.
//  ЗначениеАтрибута - Строка - значение атрибута xml строки.
//
Процедура УстановитьНеобязательныйАтрибут(ШаблонЭлемента, ИмяАтрибута, ЗначениеАтрибута)
	
	Если ЗначениеЗаполнено(ЗначениеАтрибута) Тогда
		УстановитьАтрибут(ШаблонЭлемента, ИмяАтрибута, ЗначениеАтрибута)
	Иначе
		ШаблонЭлемента = СтрЗаменить(ШаблонЭлемента, " %" + ИмяАтрибута + "%", "");
	КонецЕсли;
	
КонецПроцедуры

// Заполняет сведения о физическом лице в файле.
// Поддерживаемая версия уведомления: 5.02.
// Параметры:
//  Контекст - Структура - структура измерений:
//  * ВременныйКаталог         - Строка -путь временного каталога
//  * ЗавершающиеТэги          - Строка - описание завершающих тегов.
//  * ИдФайлИсх                - Строка
//  * ИдФайлПерв               - Строка
//  * ОсновныеСведенияВыгрузки - Структура - описание структуры выгрузки.
//  * Отступ                   - Строка - описание табуляции.
//  * РазмерЗавершающихТэгов   - Число - размер завершающих тегов.
//  * РазмерРазделения         - Число
//  * ТекущийРазмер            - Число
//  ТекстФайла                - ТекстовыйДокумент - текстовый документ для вывода.
//
Процедура ЗаполнитьСведенияОФизлице_502(Контекст, ТекстФайла)
	
	ТекстФайла.ДобавитьСтроку(Контекст.Отступ + "<НПФЛ>");
	
	УвеличитьОтступ(Контекст);
	
	Шаблон_ФИО = "<ФИО %Фамилия% %Имя% %Отчество%/>";
	УстановитьАтрибут(Шаблон_ФИО, "Фамилия", Контекст.ОсновныеСведенияВыгрузки.НПФЛФамилия);
	УстановитьАтрибут(Шаблон_ФИО, "Имя", Контекст.ОсновныеСведенияВыгрузки.НПФЛИмя);
	УстановитьНеобязательныйАтрибут(Шаблон_ФИО, "Отчество", Контекст.ОсновныеСведенияВыгрузки.НПФЛОтчество);
	
	ТекстФайла.ДобавитьСтроку(Контекст.Отступ + Шаблон_ФИО);
	
	Если Контекст.ОсновныеСведенияВыгрузки.ЕстьИННФЛ Тогда
		Шаблон_ИННФЛ = СтрЗаменить("<ИННФЛ>%ИННФЛ%</ИННФЛ>", "%ИННФЛ%", Контекст.ОсновныеСведенияВыгрузки.ИННФЛ);
		
		ТекстФайла.ДобавитьСтроку(Контекст.Отступ + Шаблон_ИННФЛ);
	Иначе
		Шаблон_СвФЛ = "<СвФЛ %ДатаРожд% %МестоРожд% %НалГражд% %ОКСМ% %СтатусНП%>";
		УстановитьАтрибут(Шаблон_СвФЛ, "ДатаРожд", Контекст.ОсновныеСведенияВыгрузки.НПФЛДатаРожд);
		УстановитьАтрибут(Шаблон_СвФЛ, "МестоРожд", Контекст.ОсновныеСведенияВыгрузки.НПФЛМестоРожд);
		УстановитьАтрибут(Шаблон_СвФЛ, "НалГражд", Контекст.ОсновныеСведенияВыгрузки.НПФЛНалГражд);
		УстановитьАтрибут(Шаблон_СвФЛ, "ОКСМ", Контекст.ОсновныеСведенияВыгрузки.НПФЛОКСМ);
		УстановитьАтрибут(Шаблон_СвФЛ, "СтатусНП", Контекст.ОсновныеСведенияВыгрузки.НПФЛСтатусНП);
		
		ТекстФайла.ДобавитьСтроку(Контекст.Отступ + Шаблон_СвФЛ);
		
		УвеличитьОтступ(Контекст);
		
		Шаблон_УдЛичнФЛ = "<УдЛичнФЛ %КодВидДок% %СерНомДок% %ДатаДок% %ВыдДок%/>";
		УстановитьАтрибут(Шаблон_УдЛичнФЛ, "КодВидДок", Контекст.ОсновныеСведенияВыгрузки.НПФЛКодВидДок);
		УстановитьАтрибут(Шаблон_УдЛичнФЛ, "СерНомДок", Контекст.ОсновныеСведенияВыгрузки.НПФЛСерНомДок);
		УстановитьАтрибут(Шаблон_УдЛичнФЛ, "ДатаДок", Контекст.ОсновныеСведенияВыгрузки.НПФЛДатаДок);
		УстановитьАтрибут(Шаблон_УдЛичнФЛ, "ВыдДок", Контекст.ОсновныеСведенияВыгрузки.НПФЛВыдДок);
		
		ТекстФайла.ДобавитьСтроку(Контекст.Отступ + Шаблон_УдЛичнФЛ);
		
		Шаблон_СвАдрРФ = "<СвАдрРФ %ПрАдр%>";
		УстановитьАтрибут(Шаблон_СвАдрРФ, "ПрАдр", Контекст.ОсновныеСведенияВыгрузки.НПФЛПрАдр);
		
		ТекстФайла.ДобавитьСтроку(Контекст.Отступ + Шаблон_СвАдрРФ);
		
		УвеличитьОтступ(Контекст);
		
		Шаблон_АдрРФ = "<АдрРФ %Индекс% %КодРегион% %Район% %Город% %НаселПункт% %Улица% %Дом% %Корпус% %Кварт%/>";
		УстановитьНеобязательныйАтрибут(Шаблон_АдрРФ, "Индекс", Контекст.ОсновныеСведенияВыгрузки.НПФЛИндекс);
		УстановитьАтрибут(Шаблон_АдрРФ, "КодРегион", Контекст.ОсновныеСведенияВыгрузки.НПФЛКодРегион);
		УстановитьНеобязательныйАтрибут(Шаблон_АдрРФ, "Район", Контекст.ОсновныеСведенияВыгрузки.НПФЛРайон);
		УстановитьНеобязательныйАтрибут(Шаблон_АдрРФ, "Город", Контекст.ОсновныеСведенияВыгрузки.НПФЛГород);
		УстановитьНеобязательныйАтрибут(Шаблон_АдрРФ, "НаселПункт", Контекст.ОсновныеСведенияВыгрузки.НПФЛНаселПункт);
		УстановитьНеобязательныйАтрибут(Шаблон_АдрРФ, "Улица", Контекст.ОсновныеСведенияВыгрузки.НПФЛУлица);
		УстановитьНеобязательныйАтрибут(Шаблон_АдрРФ, "Дом", Контекст.ОсновныеСведенияВыгрузки.НПФЛДом);
		УстановитьНеобязательныйАтрибут(Шаблон_АдрРФ, "Корпус", Контекст.ОсновныеСведенияВыгрузки.НПФЛКорпус);
		УстановитьНеобязательныйАтрибут(Шаблон_АдрРФ, "Кварт", Контекст.ОсновныеСведенияВыгрузки.НПФЛКварт);
		
		ТекстФайла.ДобавитьСтроку(Контекст.Отступ + Шаблон_АдрРФ);
		
		УменьшитьОтступ(Контекст);
		
		ТекстФайла.ДобавитьСтроку(Контекст.Отступ + "</СвАдрРФ>");
		
		Если ЕстьЗаполненныеРеквизиты(Контекст.ОсновныеСведенияВыгрузки, "АдрИнКодСтраны, АдрИнТекст") Тогда
			Шаблон_АдрИн = "<АдрИн %ОКСМ% %АдрИнТекст%/>";
			УстановитьАтрибут(Шаблон_АдрИн, "ОКСМ", Контекст.ОсновныеСведенияВыгрузки.АдрИнКодСтраны);
			УстановитьАтрибут(Шаблон_АдрИн, "АдрИнТекст", Контекст.ОсновныеСведенияВыгрузки.АдрИнТекст);
			
			ТекстФайла.ДобавитьСтроку(Контекст.Отступ + Шаблон_АдрИн);
		КонецЕсли;
		
		УменьшитьОтступ(Контекст);
		
		ТекстФайла.ДобавитьСтроку(Контекст.Отступ + "</СвФЛ>");
	КонецЕсли;
	
	УменьшитьОтступ(Контекст);
	
	ТекстФайла.ДобавитьСтроку(Контекст.Отступ + "</НПФЛ>");
	
КонецПроцедуры

// Заполняет сведения о физическом лице в файле.
// Поддерживаемые версии уведомления: 5.03, 5.04, 5.05.
// Параметры:
//  Контекст - Структура - структура измерений:
//  * ВременныйКаталог         - Строка -путь временного каталога
//  * ЗавершающиеТэги          - Строка - описание завершающих тегов.
//  * ИдФайлИсх                - Строка
//  * ИдФайлПерв               - Строка
//  * ОсновныеСведенияВыгрузки - Структура - описание структуры выгрузки.
//  * Отступ                   - Строка - описание табуляции.
//  * РазмерЗавершающихТэгов   - Число - размер завершающих тегов.
//  * РазмерРазделения         - Число
//  * ТекущийРазмер            - Число
//  ТекстФайла                - ТекстовыйДокумент - текстовый документ для вывода.
//
Процедура ЗаполнитьСведенияОФизлице_503_504_505(Контекст, ТекстФайла)
	
	ТекстФайла.ДобавитьСтроку(Контекст.Отступ + "<НПФЛ>");
	
	УвеличитьОтступ(Контекст);
	
	Шаблон_ФИО = "<ФИО %Фамилия% %Имя% %Отчество%/>";
	УстановитьАтрибут(Шаблон_ФИО, "Фамилия", Контекст.ОсновныеСведенияВыгрузки.НПФЛФамилия);
	УстановитьАтрибут(Шаблон_ФИО, "Имя", Контекст.ОсновныеСведенияВыгрузки.НПФЛИмя);
	УстановитьНеобязательныйАтрибут(Шаблон_ФИО, "Отчество", Контекст.ОсновныеСведенияВыгрузки.НПФЛОтчество);
	
	ТекстФайла.ДобавитьСтроку(Контекст.Отступ + Шаблон_ФИО);
	
	Если Контекст.ОсновныеСведенияВыгрузки.ЕстьИННФЛ Тогда
		Шаблон_ИННФЛ = СтрЗаменить("<ИННФЛ>%ИННФЛ%</ИННФЛ>", "%ИННФЛ%", Контекст.ОсновныеСведенияВыгрузки.ИННФЛ);
		
		ТекстФайла.ДобавитьСтроку(Контекст.Отступ + Шаблон_ИННФЛ);
	Иначе
		Шаблон_СвФЛ = "<СвФЛ %ДатаРожд% %МестоРожд% %Гражд% %ОКСМ% %СтатусНП%>";
		УстановитьАтрибут(Шаблон_СвФЛ, "ДатаРожд", Контекст.ОсновныеСведенияВыгрузки.НПФЛДатаРожд);
		УстановитьАтрибут(Шаблон_СвФЛ, "МестоРожд", Контекст.ОсновныеСведенияВыгрузки.НПФЛМестоРожд);
		УстановитьАтрибут(Шаблон_СвФЛ, "Гражд", Контекст.ОсновныеСведенияВыгрузки.НПФЛНалГражд);
		УстановитьНеобязательныйАтрибут(Шаблон_СвФЛ, "ОКСМ", Контекст.ОсновныеСведенияВыгрузки.НПФЛОКСМ);
		
		УстановитьАтрибут(Шаблон_СвФЛ, "СтатусНП", Контекст.ОсновныеСведенияВыгрузки.НПФЛСтатусНП);
		
		ТекстФайла.ДобавитьСтроку(Контекст.Отступ + Шаблон_СвФЛ);
		
		УвеличитьОтступ(Контекст);
		
		Шаблон_УдЛичнФЛ = "<УдЛичнФЛ %КодВидДок% %СерНомДок% %ДатаДок% %ВыдДок%/>";
		УстановитьАтрибут(Шаблон_УдЛичнФЛ, "КодВидДок", Контекст.ОсновныеСведенияВыгрузки.НПФЛКодВидДок);
		УстановитьАтрибут(Шаблон_УдЛичнФЛ, "СерНомДок", Контекст.ОсновныеСведенияВыгрузки.НПФЛСерНомДок);
		УстановитьАтрибут(Шаблон_УдЛичнФЛ, "ДатаДок", Контекст.ОсновныеСведенияВыгрузки.НПФЛДатаДок);
		УстановитьАтрибут(Шаблон_УдЛичнФЛ, "ВыдДок", Контекст.ОсновныеСведенияВыгрузки.НПФЛВыдДок);
		
		ТекстФайла.ДобавитьСтроку(Контекст.Отступ + Шаблон_УдЛичнФЛ);
		
		Если ЕстьЗаполненныеРеквизиты(Контекст.ОсновныеСведенияВыгрузки, "АдрИнКодСтраны, АдрИнТекст") Тогда
			Шаблон_АдрИн = "<АдрИн %ОКСМ% %АдрИнТекст%/>";
			УстановитьАтрибут(Шаблон_АдрИн, "ОКСМ", Контекст.ОсновныеСведенияВыгрузки.АдрИнКодСтраны);
			УстановитьАтрибут(Шаблон_АдрИн, "АдрИнТекст", Контекст.ОсновныеСведенияВыгрузки.АдрИнТекст);
			
			ТекстФайла.ДобавитьСтроку(Контекст.Отступ + Шаблон_АдрИн);
		КонецЕсли;
		
		УменьшитьОтступ(Контекст);
		
		ТекстФайла.ДобавитьСтроку(Контекст.Отступ + "</СвФЛ>");
	КонецЕсли;
	
	УменьшитьОтступ(Контекст);
	
	ТекстФайла.ДобавитьСтроку(Контекст.Отступ + "</НПФЛ>");
	
КонецПроцедуры

// Возвращает текст по сделке.
// Поддерживаемая версия уведомления: 5.02.
// Параметры:
//  Контекст - Структура - структура измерений:
//  * ВременныйКаталог         - Строка -путь временного каталога
//  * ЗавершающиеТэги          - Строка - описание завершающих тегов.
//  * ИдФайлИсх                - Строка
//  * ИдФайлПерв               - Строка
//  * ОсновныеСведенияВыгрузки - Структура - описание структуры выгрузки.
//  * Отступ                   - Строка - описание табуляции.
//  * РазмерЗавершающихТэгов   - Число - размер завершающих тегов.
//  * РазмерРазделения         - Число
//  * ТекущийРазмер            - Число
//  ЛистыУведомления - Структура - структура данных уведомления, разделенная по разделам в виде таблиц значений.
//  Лист1А           - СтрокаТаблицыЗначений - структура листа 1А.
// // Возвращаемое значение:
//  Строка                     - текст по сделке.
//
Функция ТекстПоСделке502(Контекст, ЛистыУведомления, Лист1А)
	
	Листы1А = ЛистыУведомления.ЛистыРаздела1А;
	Листы1Б = ЛистыУведомления.ЛистыРаздела1Б;
	ЛистыРаздела2 = ЛистыУведомления.ЛистыРаздела2;
	ЛистыРаздела3 = ЛистыУведомления.ЛистыРаздела3;
	ДанныеРаздела2 = ЛистыУведомления.ДанныеРаздела2;
	ДанныеРаздела3 = ЛистыУведомления.ДанныеРаздела3;
	
	ТекстСделки = Новый ТекстовыйДокумент;
	
	Шаблон_СвКонтрСд = "<СвКонтрСд %НомПорСд%>";
	УстановитьАтрибут(Шаблон_СвКонтрСд, "НомПорСд", XMLСтрока(Лист1А.НомерЛиста1А));
	
	ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_СвКонтрСд);
	
	УвеличитьОтступ(Контекст);
	
	ЗаполнитьСведенияОснованияКонтроляСделки502(Контекст, ТекстСделки, Лист1А);
	
	ЗаполнитьСведенияОСделке502_503_504(Контекст, ТекстСделки, Лист1А);
	
	ЗаполнитьСведенияОДоходахРасходах502_503_504_505(Контекст, ТекстСделки, Лист1А);
	
	ЗаполнитьСведенияОПредметеСделки502(Контекст, ТекстСделки, Листы1Б, Лист1А.Сделка);
	
	ЗаполнитьСведенияОбУчастникахОрганизациях502(Контекст, ТекстСделки, ЛистыРаздела2, ДанныеРаздела2, Лист1А.Сделка);
	
	ЗаполнитьСведенияОбУчастникахФизЛицах502(Контекст, ТекстСделки, ЛистыРаздела3, ДанныеРаздела3, Лист1А.Сделка);
	
	УменьшитьОтступ(Контекст);
	
	ТекстСделки.ДобавитьСтроку(Контекст.Отступ + "</СвКонтрСд>");
	
	Возврат ТекстСделки.ПолучитьТекст();
	
КонецФункции

// Возвращает текст по сделке.
// Поддерживаемые версии уведомления: 5.03, 5.04.
// Параметры:
//  Контекст - Структура - структура измерений:
//  * ВременныйКаталог         - Строка -путь временного каталога
//  * ЗавершающиеТэги          - Строка - описание завершающих тегов.
//  * ИдФайлИсх                - Строка
//  * ИдФайлПерв               - Строка
//  * ОсновныеСведенияВыгрузки - Структура - описание структуры выгрузки.
//  * Отступ                   - Строка - описание табуляции.
//  * РазмерЗавершающихТэгов   - Число - размер завершающих тегов.
//  * РазмерРазделения         - Число
//  * ТекущийРазмер            - Число
//  ЛистыУведомления - Структура - структура данных уведомления, разделенная по разделам в виде таблиц значений.
//  Лист1А           - СтрокаТаблицыЗначений - структура листа 1А.
// // Возвращаемое значение:
//  Строка                     - текст по сделке.
//
Функция ТекстПоСделке503_504(Контекст, ЛистыУведомления, Лист1А)
	
	Листы1А = ЛистыУведомления.ЛистыРаздела1А;
	Листы1Б = ЛистыУведомления.ЛистыРаздела1Б;
	ЛистыРаздела2 = ЛистыУведомления.ЛистыРаздела2;
	ЛистыРаздела3 = ЛистыУведомления.ЛистыРаздела3;
	ДанныеРаздела2 = ЛистыУведомления.ДанныеРаздела2;
	ДанныеРаздела3 = ЛистыУведомления.ДанныеРаздела3;
	
	ТекстСделки = Новый ТекстовыйДокумент;
	
	Шаблон_СвКонтрСд = "<СвКонтрСд %НомПорСд% %СделкаСовАгент%>";
	УстановитьАтрибут(Шаблон_СвКонтрСд, "НомПорСд", XMLСтрока(Лист1А.НомерЛиста1А));
	УстановитьАтрибут(Шаблон_СвКонтрСд, "СделкаСовАгент", Лист1А.СделкаСовАгент);
	
	ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_СвКонтрСд);
	
	УвеличитьОтступ(Контекст);
	
	ЗаполнитьСведенияОснованияКонтроляСделки503_504_505(Контекст, ТекстСделки, Лист1А);
	
	ЗаполнитьСведенияОСделке502_503_504(Контекст, ТекстСделки, Лист1А);
	
	ЗаполнитьСведенияОДоходахРасходах502_503_504_505(Контекст, ТекстСделки, Лист1А);
	
	ЗаполнитьСведенияОПредметеСделки503_504(Контекст, ТекстСделки, Листы1Б, Лист1А.Сделка);
	
	ЗаполнитьСведенияОбУчастникахОрганизациях503_504(Контекст, ТекстСделки, ЛистыРаздела2, ДанныеРаздела2, Лист1А.Сделка);
	
	ЗаполнитьСведенияОбУчастникахФизЛицах503_504_505(Контекст, ТекстСделки, ЛистыРаздела3, ДанныеРаздела3, Лист1А.Сделка);
	
	УменьшитьОтступ(Контекст);
	
	ТекстСделки.ДобавитьСтроку(Контекст.Отступ + "</СвКонтрСд>");
	
	Возврат ТекстСделки.ПолучитьТекст();
	
КонецФункции

// Возвращает текст по сделке.
// Поддерживаемая версия уведомления: 5.05.
// Параметры:
//  Контекст - Структура - структура измерений:
//  * ВременныйКаталог         - Строка -путь временного каталога
//  * ЗавершающиеТэги          - Строка - описание завершающих тегов.
//  * ИдФайлИсх                - Строка
//  * ИдФайлПерв               - Строка
//  * ОсновныеСведенияВыгрузки - Структура - описание структуры выгрузки.
//  * Отступ                   - Строка - описание табуляции.
//  * РазмерЗавершающихТэгов   - Число - размер завершающих тегов.
//  * РазмерРазделения         - Число
//  * ТекущийРазмер            - Число
//  ЛистыУведомления - Структура - структура данных уведомления, разделенная по разделам в виде таблиц значений.
//  Лист1А           - СтрокаТаблицыЗначений - структура листа 1А.
// // Возвращаемое значение:
//  Строка                     - текст по сделке.
//
Функция ТекстПоСделке505(Контекст, ЛистыУведомления, Лист1А)
	
	Листы1А = ЛистыУведомления.ЛистыРаздела1А;
	Листы1Б = ЛистыУведомления.ЛистыРаздела1Б;
	Листы1В = ЛистыУведомления.ЛистыРаздела1В;
	Листы1Г = ЛистыУведомления.ЛистыРаздела1Г;
	ЛистыРаздела2 = ЛистыУведомления.ЛистыРаздела2;
	ЛистыРаздела3 = ЛистыУведомления.ЛистыРаздела3;
	ЛистыРаздела4 = ЛистыУведомления.ЛистыРаздела4;
	ДанныеРаздела2 = ЛистыУведомления.ДанныеРаздела2;
	ДанныеРаздела3 = ЛистыУведомления.ДанныеРаздела3;
	ДанныеРаздела4 = ЛистыУведомления.ДанныеРаздела4;
	
	ТекстСделки = Новый ТекстовыйДокумент;
	
	Шаблон_СвКонтрСд = "<СвКонтрСд %НомПорСд% %СделкаСовАгент%>";
	УстановитьАтрибут(Шаблон_СвКонтрСд, "НомПорСд", XMLСтрока(Лист1А.НомерЛиста1А));
	УстановитьАтрибут(Шаблон_СвКонтрСд, "СделкаСовАгент", Лист1А.СделкаСовАгент);
	
	ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_СвКонтрСд);
	
	УвеличитьОтступ(Контекст);
	
	ЗаполнитьСведенияОснованияКонтроляСделки503_504_505(Контекст, ТекстСделки, Лист1А);
	
	ЗаполнитьСведенияОСделке505(Контекст, ТекстСделки, Лист1А);
	
	ЗаполнитьСведенияОДоходахРасходах502_503_504_505(Контекст, ТекстСделки, Лист1А);
	
	ЗаполнитьСведенияОПредметеСделки505(Контекст, ТекстСделки, Листы1Б, Лист1А.Сделка);
	
	ЗаполнитьСведенияОПоследующейРеализации505(Контекст, ТекстСделки, Листы1В, Лист1А.Сделка);
	
	ЗаполнитьСведенияОСопутствующихУслугах505(Контекст, ТекстСделки, Листы1Г, Лист1А.Сделка);
	
	ЗаполнитьСведенияОбУчастникахОрганизациях505(Контекст, ТекстСделки, ЛистыРаздела2, ДанныеРаздела2, Лист1А.Сделка);
	
	ЗаполнитьСведенияОбУчастникахФизЛицах503_504_505(Контекст, ТекстСделки, ЛистыРаздела3, ДанныеРаздела3, Лист1А.Сделка);
	
	ЗаполнитьСведенияОбУчастникахПоследующейРеализации505(Контекст, ТекстСделки, ЛистыРаздела4, ДанныеРаздела4, Лист1А.Сделка);
	
	УменьшитьОтступ(Контекст);
	
	ТекстСделки.ДобавитьСтроку(Контекст.Отступ + "</СвКонтрСд>");
	
	Возврат ТекстСделки.ПолучитьТекст();
	
КонецФункции

// Заполняет сведения по организации в файле.
// 
// Параметры:
//  Контекст - Структура - структура измерений:
//  * ВременныйКаталог         - Строка -путь временного каталога
//  * ЗавершающиеТэги          - Строка - описание завершающих тегов.
//  * ИдФайлИсх                - Строка
//  * ИдФайлПерв               - Строка
//  * ОсновныеСведенияВыгрузки - Структура - описание структуры выгрузки.
//  * Отступ                   - Строка - описание табуляции.
//  * РазмерЗавершающихТэгов   - Число - размер завершающих тегов.
//  * РазмерРазделения         - Число
//  * ТекущийРазмер            - Число
//  ТекстФайла                - ТекстовыйДокумент - текстовый документ для вывода.
//
Процедура ЗаполнитьСведенияОрганизации(Контекст, ТекстФайла)
	
	Шаблон_НПЮЛ = "<НПЮЛ %НаимОрг% %ИННЮЛ% %КПП%>";
	УстановитьАтрибут(Шаблон_НПЮЛ, "НаимОрг", Контекст.ОсновныеСведенияВыгрузки.НаимОрг);
	УстановитьАтрибут(Шаблон_НПЮЛ, "ИННЮЛ", Контекст.ОсновныеСведенияВыгрузки.ИННЮЛ);
	УстановитьАтрибут(Шаблон_НПЮЛ, "КПП", Контекст.ОсновныеСведенияВыгрузки.КППЮЛ);
	
	ТекстФайла.ДобавитьСтроку(Контекст.Отступ + Шаблон_НПЮЛ);
	
	Если ЕстьЗаполненныеРеквизиты(Контекст.ОсновныеСведенияВыгрузки, "ФормРеорг, ИННЮЛРеорг, КППЮЛРеорг") Тогда
		Шаблон_СвРеоргЮЛ = "<СвРеоргЮЛ %ФормРеорг% %ИННЮЛ% %КПП%/>";
		УстановитьАтрибут(Шаблон_СвРеоргЮЛ, "ФормРеорг", Контекст.ОсновныеСведенияВыгрузки.ФормРеорг);
		УстановитьНеобязательныйАтрибут(Шаблон_СвРеоргЮЛ, "ИННЮЛ", Контекст.ОсновныеСведенияВыгрузки.ИННЮЛРеорг);
		УстановитьНеобязательныйАтрибут(Шаблон_СвРеоргЮЛ, "КПП", Контекст.ОсновныеСведенияВыгрузки.КППЮЛРеорг);
		
		УвеличитьОтступ(Контекст);
		ТекстФайла.ДобавитьСтроку(Контекст.Отступ + Шаблон_СвРеоргЮЛ);
		УменьшитьОтступ(Контекст);
	КонецЕсли;
	
	ТекстФайла.ДобавитьСтроку(Контекст.Отступ + "</НПЮЛ>");
	
КонецПроцедуры

// Заполняет сведения основания контроля сделки.
// Поддерживаемая версия уведомления: 5.02.
// Параметры:
//  Контекст - Структура - структура измерений:
//  * ВременныйКаталог         - Строка -путь временного каталога
//  * ЗавершающиеТэги          - Строка - описание завершающих тегов.
//  * ИдФайлИсх                - Строка
//  * ИдФайлПерв               - Строка
//  * ОсновныеСведенияВыгрузки - Структура - описание структуры выгрузки.
//  * Отступ                   - Строка - описание табуляции.
//  * РазмерЗавершающихТэгов   - Число - размер завершающих тегов.
//  * РазмерРазделения         - Число
//  * ТекущийРазмер            - Число
//  ТекстСделки                - ТекстовыйДокумент - текстовый документ для вывода.
//  Сведения - Структура - структура сведений.
Процедура ЗаполнитьСведенияОснованияКонтроляСделки502(Контекст, ТекстСделки, Сведения)
	
	Шаблон_ОснКонтрСд = "<ОснКонтрСд %ВзЗавис%>";
	УстановитьНеобязательныйАтрибут(Шаблон_ОснКонтрСд, "ВзЗавис", Сведения.Строка100Взаимозависимость);
	
	ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_ОснКонтрСд);
	
	УвеличитьОтступ(Контекст);
	
	Шаблон_Осн105_14 = "<Осн105.14 %Осн121% %Осн122% %Осн123% %Осн124%/>";
	УстановитьАтрибут(Шаблон_Осн105_14, "Осн121", Сведения.Строка121СтороныВзаимозависимыПоКодексу);
	УстановитьАтрибут(Шаблон_Осн105_14, "Осн122", Сведения.Строка122СделкаВОбластиВнешнейТорговли);
	УстановитьАтрибут(Шаблон_Осн105_14, "Осн123", Сведения.Строка123СделкаСКонтрагентомСЛьготнымНалогообложением);
	УстановитьАтрибут(Шаблон_Осн105_14, "Осн124", Сведения.Строка124СделкаСНезависимымПосредником);
	
	ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_Осн105_14);
	
	Шаблон_ОснРФ105_14 = "<ОснРФ105.14 %Осн131% %Осн132% %Осн133% %Осн134% %Осн135%/>";
	УстановитьАтрибут(Шаблон_ОснРФ105_14, "Осн131", Сведения.ОснованиеКонтролируемости131);
	УстановитьАтрибут(Шаблон_ОснРФ105_14, "Осн132", Сведения.ОснованиеКонтролируемости132);
	УстановитьАтрибут(Шаблон_ОснРФ105_14, "Осн133", Сведения.ОснованиеКонтролируемости133);
	УстановитьАтрибут(Шаблон_ОснРФ105_14, "Осн134", Сведения.ОснованиеКонтролируемости134);
	УстановитьАтрибут(Шаблон_ОснРФ105_14, "Осн135", Сведения.ОснованиеКонтролируемости135);
	
	ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_ОснРФ105_14);
	
	УменьшитьОтступ(Контекст);
	
	ТекстСделки.ДобавитьСтроку(Контекст.Отступ + "</ОснКонтрСд>");
	
КонецПроцедуры

// Заполняет сведения основания контроля сделки.
// Поддерживаемые версии уведомления: 5.03, 5.04, 5.05.
// Параметры:
//  Контекст - Структура - структура измерений:
//  * ВременныйКаталог         - Строка -путь временного каталога
//  * ЗавершающиеТэги          - Строка - описание завершающих тегов.
//  * ИдФайлИсх                - Строка
//  * ИдФайлПерв               - Строка
//  * ОсновныеСведенияВыгрузки - Структура - описание структуры выгрузки.
//  * Отступ                   - Строка - описание табуляции.
//  * РазмерЗавершающихТэгов   - Число - размер завершающих тегов.
//  * РазмерРазделения         - Число
//  * ТекущийРазмер            - Число
//  ТекстСделки                - ТекстовыйДокумент - текстовый документ для вывода.
//  Сведения - Структура - структура сведений.
Процедура ЗаполнитьСведенияОснованияКонтроляСделки503_504_505(Контекст, ТекстСделки, Сведения)
	
	ОсновныеСведенияВыгрузки = Контекст.ОсновныеСведенияВыгрузки;
	
	Шаблон_ОснКонтрСд = "<ОснКонтрСд %ВзЗавис%>";
	УстановитьНеобязательныйАтрибут(Шаблон_ОснКонтрСд, "ВзЗавис", Сведения.Строка100Взаимозависимость);
	
	ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_ОснКонтрСд);
	
	УвеличитьОтступ(Контекст);
	
	Шаблон_Осн105_14 = "<Осн105.14 %Осн121% %Осн122% %Осн123% %Осн124%/>";
	УстановитьАтрибут(Шаблон_Осн105_14, "Осн121", Сведения.Строка121СтороныВзаимозависимыПоКодексу);
	УстановитьАтрибут(Шаблон_Осн105_14, "Осн122", Сведения.Строка122СделкаВОбластиВнешнейТорговли);
	УстановитьАтрибут(Шаблон_Осн105_14, "Осн123", Сведения.Строка123СделкаСКонтрагентомСЛьготнымНалогообложением);
	УстановитьАтрибут(Шаблон_Осн105_14, "Осн124", Сведения.Строка124СделкаСНезависимымПосредником);
	
	ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_Осн105_14);
	
	Если ОсновныеСведенияВыгрузки.ВерсияУведомления 
		< КонтролируемыеСделкиКлиентСервер.ВерсияУведомления_2019() Тогда
		Шаблон_ОснРФ105_14 = "<ОснРФ105.14 %Осн131% %Осн132% %Осн133% %Осн134% %Осн135% %Осн136% %Осн137% %Осн138% %Осн139% %Осн140%/>";
	Иначе
		Шаблон_ОснРФ105_14 = "<ОснРФ105.14 %Осн131% %Осн132% %Осн133% %Осн134% %Осн135% %Осн136% %Осн137% %Осн138%/>";
	КонецЕсли;
		
	УстановитьАтрибут(Шаблон_ОснРФ105_14, "Осн131", Сведения.ОснованиеКонтролируемости131);
	УстановитьАтрибут(Шаблон_ОснРФ105_14, "Осн132", Сведения.ОснованиеКонтролируемости132);
	УстановитьАтрибут(Шаблон_ОснРФ105_14, "Осн133", Сведения.ОснованиеКонтролируемости133);
	УстановитьАтрибут(Шаблон_ОснРФ105_14, "Осн134", Сведения.ОснованиеКонтролируемости134);
	УстановитьАтрибут(Шаблон_ОснРФ105_14, "Осн135", Сведения.ОснованиеКонтролируемости135);
	УстановитьАтрибут(Шаблон_ОснРФ105_14, "Осн136", Сведения.ОснованиеКонтролируемости136);
	УстановитьАтрибут(Шаблон_ОснРФ105_14, "Осн137", Сведения.ОснованиеКонтролируемости137);
	УстановитьАтрибут(Шаблон_ОснРФ105_14, "Осн138", Сведения.ОснованиеКонтролируемости138);
	Если ОсновныеСведенияВыгрузки.ВерсияУведомления 
		< КонтролируемыеСделкиКлиентСервер.ВерсияУведомления_2019() Тогда
		УстановитьАтрибут(Шаблон_ОснРФ105_14, "Осн139", Сведения.ОснованиеКонтролируемости139);
		УстановитьАтрибут(Шаблон_ОснРФ105_14, "Осн140", Сведения.ОснованиеКонтролируемости140);
	КонецЕсли;
	
	ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_ОснРФ105_14);
	
	УменьшитьОтступ(Контекст);
	
	ТекстСделки.ДобавитьСтроку(Контекст.Отступ + "</ОснКонтрСд>");
	
КонецПроцедуры

// Заполняет сведения о сделке.
// Поддерживаемые версии уведомления: 5.02, 5.03, 5.04.
// Параметры:
//  Контекст - Структура - структура измерений:
//  * ВременныйКаталог         - Строка -путь временного каталога
//  * ЗавершающиеТэги          - Строка - описание завершающих тегов.
//  * ИдФайлИсх                - Строка
//  * ИдФайлПерв               - Строка
//  * ОсновныеСведенияВыгрузки - Структура - описание структуры выгрузки.
//  * Отступ                   - Строка - описание табуляции.
//  * РазмерЗавершающихТэгов   - Число - размер завершающих тегов.
//  * РазмерРазделения         - Число
//  * ТекущийРазмер            - Число
//  ТекстСделки                - ТекстовыйДокумент - текстовый документ для вывода.
//  Сведения - Структура - структура сведений.
Процедура ЗаполнитьСведенияОСделке502_503_504(Контекст, ТекстСделки, Сведения)
	
	ЭтоГруппаОднородныхСделок = Ложь;
	
	Шаблон_КонтрСд = "<КонтрСд %ГрупОС% %КодНаимСд% %КодСторСд% %ПрОпрЦен% %КомПрОпрЦен% %КодОпрЦен% %КомКодОпрЦен% %КодМетЦен% %КомКодМетЦен% %КолУчСд% %КомКолУчСд%>";
	
	УстановитьАтрибут(Шаблон_КонтрСд, "ГрупОС", ?(ЭтоГруппаОднородныхСделок, "1", "0"));
	УстановитьАтрибут(Шаблон_КонтрСд, "КодНаимСд", Сведения.Строка210КодНаименованияСделки);
	УстановитьАтрибут(Шаблон_КонтрСд, "КодСторСд", Сведения.Строка211КодСтороныСделки);
	УстановитьАтрибут(Шаблон_КонтрСд, "ПрОпрЦен", Сведения.Строка220ПризнакОпределенияЦеныСделки);
	УстановитьНеобязательныйАтрибут(Шаблон_КонтрСд, "КомПрОпрЦен", Сведения.Строка220_1Комментарий);
	УстановитьАтрибут(Шаблон_КонтрСд, "КодОпрЦен", Сведения.Строка230КодОпределенияЦены);
	УстановитьНеобязательныйАтрибут(Шаблон_КонтрСд, "КомКодОпрЦен", Сведения.Строка230_1Комментарий);
	УстановитьНеобязательныйАтрибут(Шаблон_КонтрСд, "КодМетЦен", Сведения.Строка240КодМетодовЦенообразования);
	УстановитьНеобязательныйАтрибут(Шаблон_КонтрСд, "КомКодМетЦен", Сведения.Строка240_1Комментарий);
	УстановитьАтрибут(Шаблон_КонтрСд, "КолУчСд", Формат(Сведения.Строка260КоличествоУчастниковСделки, "ЧЦ=3; ЧДЦ=0; ЧН=; ЧГ=0"));
	УстановитьНеобязательныйАтрибут(Шаблон_КонтрСд, "КомКолУчСд", Сведения.Строка260_1Комментарий);
	
	ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_КонтрСд);
	
	Если ЕстьЗаполненныеРеквизиты(Сведения, "Строка251, Строка252, Строка253, Строка254, Строка255, Строка256, Строка257, Строка258, Строка259") Тогда
		УвеличитьОтступ(Контекст);
		
		Шаблон_КодИстИнф = "<КодИстИнф %Ист251% %Ист252% %Ист253% %Ист254% %Ист255% %Ист256% %Ист257% %Ист258% %Ист259%/>";
		УстановитьАтрибут(Шаблон_КодИстИнф, "Ист251", Сведения.Строка251);
		УстановитьАтрибут(Шаблон_КодИстИнф, "Ист252", Сведения.Строка252);
		УстановитьАтрибут(Шаблон_КодИстИнф, "Ист253", Сведения.Строка253);
		УстановитьАтрибут(Шаблон_КодИстИнф, "Ист254", Сведения.Строка254);
		УстановитьАтрибут(Шаблон_КодИстИнф, "Ист255", Сведения.Строка255);
		УстановитьАтрибут(Шаблон_КодИстИнф, "Ист256", Сведения.Строка256);
		УстановитьАтрибут(Шаблон_КодИстИнф, "Ист257", Сведения.Строка257);
		УстановитьАтрибут(Шаблон_КодИстИнф, "Ист258", Сведения.Строка258);
		УстановитьАтрибут(Шаблон_КодИстИнф, "Ист259", Сведения.Строка259);
		
		ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_КодИстИнф);
		
		УменьшитьОтступ(Контекст);
	КонецЕсли;
	
	ТекстСделки.ДобавитьСтроку(Контекст.Отступ + "</КонтрСд>");
	
КонецПроцедуры

// Заполняет сведения о сделке.
// Поддерживаемая версия уведомления: 5.05.
// Параметры:
//  Контекст - Структура - структура измерений:
//  * ВременныйКаталог         - Строка -путь временного каталога
//  * ЗавершающиеТэги          - Строка - описание завершающих тегов.
//  * ИдФайлИсх                - Строка
//  * ИдФайлПерв               - Строка
//  * ОсновныеСведенияВыгрузки - Структура - описание структуры выгрузки.
//  * Отступ                   - Строка - описание табуляции.
//  * РазмерЗавершающихТэгов   - Число - размер завершающих тегов.
//  * РазмерРазделения         - Число
//  * ТекущийРазмер            - Число
//  ТекстСделки                - ТекстовыйДокумент - текстовый документ для вывода.
//  Сведения - Структура - структура сведений.
Процедура ЗаполнитьСведенияОСделке505(Контекст, ТекстСделки, Сведения)
	
	ЭтоГруппаОднородныхСделок = Ложь;
	
	Шаблон_КонтрСд = "<КонтрСд %ГрупОС% %КодНаимСд% %КодСторСд% %ПрОпрЦен% %КодОпрЦен% %КодМетЦен% %КолУчСд%>";
	
	УстановитьАтрибут(Шаблон_КонтрСд, "ГрупОС", ?(ЭтоГруппаОднородныхСделок, "1", "0"));
	УстановитьАтрибут(Шаблон_КонтрСд, "КодНаимСд", Сведения.Строка210КодНаименованияСделки);
	УстановитьАтрибут(Шаблон_КонтрСд, "КодСторСд", Сведения.Строка211КодСтороныСделки);
	УстановитьАтрибут(Шаблон_КонтрСд, "ПрОпрЦен", Сведения.Строка220ПризнакОпределенияЦеныСделки);
	УстановитьАтрибут(Шаблон_КонтрСд, "КодОпрЦен", Сведения.Строка230КодОпределенияЦены);
	УстановитьНеобязательныйАтрибут(Шаблон_КонтрСд, "КодМетЦен", Сведения.Строка240КодМетодовЦенообразования);
	УстановитьАтрибут(Шаблон_КонтрСд, "КолУчСд", Формат(Сведения.Строка260КоличествоУчастниковСделки, "ЧЦ=3; ЧДЦ=0; ЧН=; ЧГ=0"));
	
	ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_КонтрСд);
	
	УвеличитьОтступ(Контекст);
	
	Шаблон_КодТипСд = "<КодТипСд %Код1% %Код2% %Код3%/>";
	УстановитьАтрибут(Шаблон_КодТипСд, "Код1", Сведения.КодТипаСделки1);
	УстановитьАтрибут(Шаблон_КодТипСд, "Код2", Сведения.КодТипаСделки2);
	УстановитьАтрибут(Шаблон_КодТипСд, "Код3", Сведения.КодТипаСделки3);
	
	ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_КодТипСд);
	
	УменьшитьОтступ(Контекст);
	
	Если ЕстьЗаполненныеРеквизиты(Сведения, "Строка251, Строка252, Строка253, Строка254, Строка255, Строка256, Строка257, Строка258, Строка259") Тогда
		УвеличитьОтступ(Контекст);
		
		Шаблон_КодИстИнф = "<КодИстИнф %Ист251% %Ист252% %Ист253% %Ист254% %Ист255% %Ист256% %Ист257% %Ист258% %Ист259%/>";
		УстановитьАтрибут(Шаблон_КодИстИнф, "Ист251", Сведения.Строка251);
		УстановитьАтрибут(Шаблон_КодИстИнф, "Ист252", Сведения.Строка252);
		УстановитьАтрибут(Шаблон_КодИстИнф, "Ист253", Сведения.Строка253);
		УстановитьАтрибут(Шаблон_КодИстИнф, "Ист254", Сведения.Строка254);
		УстановитьАтрибут(Шаблон_КодИстИнф, "Ист255", Сведения.Строка255);
		УстановитьАтрибут(Шаблон_КодИстИнф, "Ист256", Сведения.Строка256);
		УстановитьАтрибут(Шаблон_КодИстИнф, "Ист257", Сведения.Строка257);
		УстановитьАтрибут(Шаблон_КодИстИнф, "Ист258", Сведения.Строка258);
		УстановитьАтрибут(Шаблон_КодИстИнф, "Ист259", Сведения.Строка259);
		
		ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_КодИстИнф);
		
		УменьшитьОтступ(Контекст);
	КонецЕсли;
	
	ТекстСделки.ДобавитьСтроку(Контекст.Отступ + "</КонтрСд>");
	
КонецПроцедуры

// Заполняет сведения о доходах и расходах сделки.
// Поддерживаемые версии уведомления: 5.02, 5.03, 5.04, 5.05.
// Параметры:
//  Контекст - Структура - структура измерений:
//  * ВременныйКаталог         - Строка -путь временного каталога
//  * ЗавершающиеТэги          - Строка - описание завершающих тегов.
//  * ИдФайлИсх                - Строка
//  * ИдФайлПерв               - Строка
//  * ОсновныеСведенияВыгрузки - Структура - описание структуры выгрузки.
//  * Отступ                   - Строка - описание табуляции.
//  * РазмерЗавершающихТэгов   - Число - размер завершающих тегов.
//  * РазмерРазделения         - Число
//  * ТекущийРазмер            - Число
//  ТекстСделки                - ТекстовыйДокумент - текстовый документ для вывода.
//  Сведения - Структура - структура сведений:
//  * Строка300СуммаДоходов              - Число
//  * Строка301СуммаРегулируемыхДоходов  - Число
//  * Строка310СуммаРасходов             - Число
//  * Строка311СуммаРегулируемыхРасходов - Число
Процедура ЗаполнитьСведенияОДоходахРасходах502_503_504_505(Контекст, ТекстСделки, Сведения)
	
	Шаблон_ДохРасхСд = "<ДохРасхСд %СумДохСд% %СумДохСдРег% %СумРасхСд% %СумРасхСдРег%/>";
	УстановитьАтрибут(Шаблон_ДохРасхСд, "СумДохСд", Формат(Сведения.Строка300СуммаДоходов, "ЧЦ=15; ЧДЦ=0; ЧН=; ЧГ=0"));
	УстановитьНеобязательныйАтрибут(Шаблон_ДохРасхСд, "СумДохСдРег", Формат(Сведения.Строка301СуммаРегулируемыхДоходов, "ЧЦ=15; ЧДЦ=0; ЧГ=0"));
	УстановитьАтрибут(Шаблон_ДохРасхСд, "СумРасхСд", Формат(Сведения.Строка310СуммаРасходов, "ЧЦ=15; ЧДЦ=0; ЧН=; ЧГ=0"));
	УстановитьНеобязательныйАтрибут(Шаблон_ДохРасхСд, "СумРасхСдРег", Формат(Сведения.Строка311СуммаРегулируемыхРасходов, "ЧЦ=15; ЧДЦ=0; ЧГ=0"));
	
	ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_ДохРасхСд);
	
КонецПроцедуры

// Заполняет сведения о предмете сделки уведомления версии 5.02.
// Параметры:
//  Контекст - Структура - структура измерений:
//  * ВременныйКаталог         - Строка -путь временного каталога
//  * ЗавершающиеТэги          - Строка - описание завершающих тегов.
//  * ИдФайлИсх                - Строка
//  * ИдФайлПерв               - Строка
//  * ОсновныеСведенияВыгрузки - Структура - описание структуры выгрузки.
//  * Отступ                   - Строка - описание табуляции.
//  * РазмерЗавершающихТэгов   - Число - размер завершающих тегов.
//  * РазмерРазделения         - Число
//  * ТекущийРазмер            - Число
//  ТекстСделки               - ТекстовыйДокумент - текстовый документ для вывода.
//  Листы1Б                   - ТаблицаЗначений - описание информации листов 1Б.
//  Сделка                    - ДокументСсылка.КонтролируемаяСделка - лист 1А.
Процедура ЗаполнитьСведенияОПредметеСделки502(Контекст, ТекстСделки, Листы1Б, Сделка)
	
	ТипыПредметовСделок = Новый Соответствие;
	
	СписокЛистов1Б = Листы1Б.НайтиСтроки(Новый Структура("Сделка", Сделка));
	Для Каждого Лист1Б Из СписокЛистов1Б Цикл
		ЛистыТипаПредмета = ТипыПредметовСделок.Получить(Лист1Б.Строка020ТипПредмета);
		Если ЛистыТипаПредмета = Неопределено Тогда
			ТипыПредметовСделок.Вставить(Лист1Б.Строка020ТипПредмета, Новый Массив());
			ЛистыТипаПредмета = ТипыПредметовСделок.Получить(Лист1Б.Строка020ТипПредмета);
		КонецЕсли;
		ЛистыТипаПредмета.Добавить(Лист1Б);
	КонецЦикла;
	
	Для Каждого ТипПредмета Из ТипыПредметовСделок Цикл
		Шаблон_СвПредмСд = "<СвПредмСд %ТипПредСд%>";
		
		ТипПредСд = ТипПредмета.Ключ;
		УстановитьАтрибут(Шаблон_СвПредмСд, "ТипПредСд", ТипПредСд);
		
		ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_СвПредмСд);
		
		УвеличитьОтступ(Контекст);
		
		Для Каждого Лист1Б Из ТипПредмета.Значение Цикл
			Шаблон_ПерПредСд = "<ПерПредСд %НаимПредСд% %ТНВЭД% %ОКП% %ОКВЭД% %НомУчСд% %НомДог% %ДатаДог% %ОКСМ% %КодУсловПост% %ОКЕИ% %Количество% %ЦенаЕдин% %СтоимИтог% %ДатаСовСд%>";
			
			ЗаполненОКП = ЗначениеЗаполнено(Лист1Б.Строка043КодПоОКП) ИЛИ ЗначениеЗаполнено(Лист1Б.Строка043КодПоОКПД2);
			ЗаполненТНВЭД = ЗначениеЗаполнено(Лист1Б.Строка040КодПоТНВЭД);
			
			Если ТипПредСд = 1 Тогда
				Если ЗаполненОКП Тогда
					УстановитьНеобязательныйАтрибут(Шаблон_ПерПредСд, "ТНВЭД", Лист1Б.Строка040КодПоТНВЭД); // Атрибут может присутствовать
				Иначе
					УстановитьАтрибут(Шаблон_ПерПредСд, "ТНВЭД", Лист1Б.Строка040КодПоТНВЭД); // Атрибут должен присутствовать
				КонецЕсли;
				Если Контекст.ОсновныеСведенияВыгрузки.ВерсияУведомления = КонтролируемыеСделкиКлиентСервер.ВерсияУведомления_2012() Тогда
					КодПоОКП = Лист1Б.Строка043КодПоОКП;
				Иначе
					КодПоОКП = КонтролируемыеСделки.КодПоОКПИзКодаПоОКПД2(Лист1Б.Строка043КодПоОКПД2);
				КонецЕсли;
				Если ЗаполненТНВЭД Тогда
					УстановитьНеобязательныйАтрибут(Шаблон_ПерПредСд, "ОКП", КодПоОКП); // Атрибут может присутствовать
				Иначе
					УстановитьАтрибут(Шаблон_ПерПредСд, "ОКП", КодПоОКП); // Атрибут должен присутствовать
				КонецЕсли;
				УстановитьНеобязательныйАтрибут(Шаблон_ПерПредСд, "ОКВЭД", ""); // Атрибут должен отсутствовать
				УстановитьНеобязательныйАтрибут(Шаблон_ПерПредСд, "КодУсловПост", Лист1Б.Строка100КодУсловийПоставки); // Атрибут может присутствовать
			Иначе // значения 2 и 3
				УстановитьНеобязательныйАтрибут(Шаблон_ПерПредСд, "ТНВЭД", ""); // Атрибут должен отсутствовать
				УстановитьНеобязательныйАтрибут(Шаблон_ПерПредСд, "ОКП", ""); // Атрибут должен отсутствовать
				Если Контекст.ОсновныеСведенияВыгрузки.ВерсияУведомления = КонтролируемыеСделкиКлиентСервер.ВерсияУведомления_2012() Тогда
					УстановитьАтрибут(Шаблон_ПерПредСд, "ОКВЭД", Лист1Б.Строка045КодОКВЭД); // Атрибут должен присутствовать
				Иначе
					УстановитьАтрибут(Шаблон_ПерПредСд, "ОКВЭД", Лист1Б.Строка045КодОКВЭД2); // Атрибут должен присутствовать
				КонецЕсли;
				УстановитьНеобязательныйАтрибут(Шаблон_ПерПредСд, "КодУсловПост", ""); // Атрибут должен отсутствовать
			КонецЕсли;
			
			УстановитьАтрибут(Шаблон_ПерПредСд, "НаимПредСд", Лист1Б.Строка030НаименованиеПредмета);
			УстановитьАтрибут(Шаблон_ПерПредСд, "НомУчСд", Формат(Лист1Б.Строка050НомерУчастникаСделки, "ЧЦ=4; ЧДЦ=0; ЧН=; ЧГ=0"));
			УстановитьАтрибут(Шаблон_ПерПредСд, "НомДог", Лист1Б.Строка060НомерДоговора);
			УстановитьАтрибут(Шаблон_ПерПредСд, "ДатаДог", Формат(Лист1Б.Строка065ДатаДоговора, "ДЛФ=D"));
			УстановитьНеобязательныйАтрибут(Шаблон_ПерПредСд, "ОКСМ", Лист1Б.Строка070КодСтраныПроисхождения);
			УстановитьАтрибут(Шаблон_ПерПредСд, "ОКЕИ", СокрЛП(Лист1Б.Строка110КодЕдиницыИзмерения));
			УстановитьАтрибут(Шаблон_ПерПредСд, "Количество", Формат(Лист1Б.Строка120Количество, "ЧЦ=15; ЧДЦ=0; ЧН=; ЧГ=0"));
			УстановитьАтрибут(Шаблон_ПерПредСд, "ЦенаЕдин", Формат(Лист1Б.Строка130Цена, "ЧЦ=15; ЧДЦ=0; ЧН=; ЧГ=0"));
			УстановитьАтрибут(Шаблон_ПерПредСд, "СтоимИтог", Формат(Лист1Б.Строка140Стоимость, "ЧЦ=15; ЧДЦ=0; ЧН=; ЧГ=0"));
			УстановитьАтрибут(Шаблон_ПерПредСд, "ДатаСовСд", Формат(Лист1Б.Строка150ДатаСовершения, "ДЛФ=D"));
			
			ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_ПерПредСд);
			
			УвеличитьОтступ(Контекст);
			
			Если ЕстьЗаполненныеРеквизиты(Лист1Б, "Строка080КодСтраныОтправки, Строка080КодРегионаОтправки, Строка080ГородОтправки, Строка080НаселенныйПунктОтправки") Тогда
				Шаблон_МестОтпрТов = "<МестОтпрТов %ОКСМ% %КодРегион% %Город% %НаселПункт%/>";
				УстановитьАтрибут(Шаблон_МестОтпрТов, "ОКСМ", Лист1Б.Строка080КодСтраныОтправки);
				УстановитьНеобязательныйАтрибут(Шаблон_МестОтпрТов, "КодРегион", Лист1Б.Строка080КодРегионаОтправки);
				УстановитьНеобязательныйАтрибут(Шаблон_МестОтпрТов, "Город", Лист1Б.Строка080ГородОтправки);
				УстановитьНеобязательныйАтрибут(Шаблон_МестОтпрТов, "НаселПункт", Лев(Лист1Б.Строка080НаселенныйПунктОтправки, 50));
				
				ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_МестОтпрТов);
			КонецЕсли;
			
			Если ЕстьЗаполненныеРеквизиты(Лист1Б, "Строка090КодСтраныСовершенияСделки, Строка090КодРегионаСовершенияСделки, Строка090ГородСовершенияСделки, Строка090НаселенныйПунктСовершенияСделки") Тогда
				Шаблон_МестСовСд = "<МестСовСд %ОКСМ% %КодРегион% %Город% %НаселПункт%/>";
				УстановитьАтрибут(Шаблон_МестСовСд, "ОКСМ", Лист1Б.Строка090КодСтраныСовершенияСделки);
				УстановитьНеобязательныйАтрибут(Шаблон_МестСовСд, "КодРегион", Лист1Б.Строка090КодРегионаСовершенияСделки);
				УстановитьНеобязательныйАтрибут(Шаблон_МестСовСд, "Город", Лист1Б.Строка090ГородСовершенияСделки);
				УстановитьНеобязательныйАтрибут(Шаблон_МестСовСд, "НаселПункт", Лев(Лист1Б.Строка090НаселенныйПунктСовершенияСделки, 50));
				
				ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_МестСовСд);
			КонецЕсли;
			
			УменьшитьОтступ(Контекст);
			
			ТекстСделки.ДобавитьСтроку(Контекст.Отступ + "</ПерПредСд>");
		КонецЦикла;
		
		УменьшитьОтступ(Контекст);
		
		ТекстСделки.ДобавитьСтроку(Контекст.Отступ + "</СвПредмСд>");
		
	КонецЦикла;
	
КонецПроцедуры

// Заполняет сведения о предмете сделки уведомления версий 5.03 или 5.04.
// Параметры:
//  Контекст - Структура - структура измерений:
//  * ВременныйКаталог         - Строка -путь временного каталога
//  * ЗавершающиеТэги          - Строка - описание завершающих тегов.
//  * ИдФайлИсх                - Строка
//  * ИдФайлПерв               - Строка
//  * ОсновныеСведенияВыгрузки - Структура - описание структуры выгрузки.
//  * Отступ                   - Строка - описание табуляции.
//  * РазмерЗавершающихТэгов   - Число - размер завершающих тегов.
//  * РазмерРазделения         - Число
//  * ТекущийРазмер            - Число
//  ТекстСделки               - ТекстовыйДокумент - текстовый документ для вывода.
//  Листы1Б                   - ТаблицаЗначений - описание информации листов 1Б.
//  Сделка                    - ДокументСсылка.КонтролируемаяСделка - лист 1А.
Процедура ЗаполнитьСведенияОПредметеСделки503_504(Контекст, ТекстСделки, Листы1Б, Сделка)
	
	ТипыПредметовСделок = Новый Соответствие;
	
	СписокЛистов1Б = Листы1Б.НайтиСтроки(Новый Структура("Сделка", Сделка));
	Для Каждого Лист1Б Из СписокЛистов1Б Цикл
		ЛистыТипаПредмета = ТипыПредметовСделок.Получить(Лист1Б.Строка020ТипПредмета);
		Если ЛистыТипаПредмета = Неопределено Тогда
			ТипыПредметовСделок.Вставить(Лист1Б.Строка020ТипПредмета, Новый Массив());
			ЛистыТипаПредмета = ТипыПредметовСделок.Получить(Лист1Б.Строка020ТипПредмета);
		КонецЕсли;
		ЛистыТипаПредмета.Добавить(Лист1Б);
	КонецЦикла;
	
	Для Каждого ТипПредмета ИЗ ТипыПредметовСделок Цикл
		Шаблон_СвПредмСдН = "<СвПредмСдН %ТипПредСд%>";
		
		ТипПредСд = ТипПредмета.Ключ;
		УстановитьАтрибут(Шаблон_СвПредмСдН, "ТипПредСд", ТипПредСд);
		
		ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_СвПредмСдН);
		
		УвеличитьОтступ(Контекст);
		
		Для Каждого Лист1Б ИЗ ТипПредмета.Значение Цикл
			Шаблон_ПерПредСд = "<ПерПредСд %НаимПредСд% %ТНВЭД% %ОКПД2% %ОКВЭД2% %НомУчСд% %НомДог% %ДатаДог% %ОКСМ% %КодУсловПост% %ОКЕИ% %Количество% %ЦенаЕдин% %КодВалют% %ПроцСтав% %СтоимИтог% %ДатаСовСд%>";
			
			ЗаполненОКПД2 = ЗначениеЗаполнено(Лист1Б.Строка043КодПоОКПД2);
			ЗаполненТНВЭД = ЗначениеЗаполнено(Лист1Б.Строка040КодПоТНВЭД);
			
			Если ТипПредСд = 1 Тогда
				Если ЗаполненОКПД2 Тогда
					УстановитьНеобязательныйАтрибут(Шаблон_ПерПредСд, "ТНВЭД", Лист1Б.Строка040КодПоТНВЭД); // Атрибут может присутствовать
				Иначе
					УстановитьАтрибут(Шаблон_ПерПредСд, "ТНВЭД", Лист1Б.Строка040КодПоТНВЭД); // Атрибут должен присутствовать
				КонецЕсли;
				Если ЗаполненТНВЭД Тогда
					УстановитьНеобязательныйАтрибут(Шаблон_ПерПредСд, "ОКПД2", Лист1Б.Строка043КодПоОКПД2); // Атрибут может присутствовать
				Иначе
					УстановитьАтрибут(Шаблон_ПерПредСд, "ОКПД2", Лист1Б.Строка043КодПоОКПД2); // Атрибут должен присутствовать
				КонецЕсли;
				УстановитьНеобязательныйАтрибут(Шаблон_ПерПредСд, "ОКВЭД2", ""); // Атрибут должен отсутствовать
				УстановитьНеобязательныйАтрибут(Шаблон_ПерПредСд, "КодУсловПост", Лист1Б.Строка100КодУсловийПоставки); // Атрибут может присутствовать
			Иначе // значения 2 и 3
				УстановитьНеобязательныйАтрибут(Шаблон_ПерПредСд, "ТНВЭД", ""); // Атрибут должен отсутствовать
				УстановитьНеобязательныйАтрибут(Шаблон_ПерПредСд, "ОКПД2", ""); // Атрибут должен отсутствовать
				УстановитьАтрибут(Шаблон_ПерПредСд, "ОКВЭД2", Лист1Б.Строка045КодОКВЭД2); // Атрибут должен присутствовать
				УстановитьНеобязательныйАтрибут(Шаблон_ПерПредСд, "КодУсловПост", ""); // Атрибут должен отсутствовать
			КонецЕсли;
			
			УстановитьАтрибут(Шаблон_ПерПредСд, "НаимПредСд", Лист1Б.Строка030НаименованиеПредмета);
			УстановитьАтрибут(Шаблон_ПерПредСд, "НомУчСд", Формат(Лист1Б.Строка050НомерУчастникаСделки, "ЧЦ=4; ЧДЦ=0; ЧН=; ЧГ=0"));
			УстановитьАтрибут(Шаблон_ПерПредСд, "НомДог", Лист1Б.Строка060НомерДоговора);
			УстановитьАтрибут(Шаблон_ПерПредСд, "ДатаДог", Формат(Лист1Б.Строка065ДатаДоговора, "ДЛФ=D"));
			УстановитьНеобязательныйАтрибут(Шаблон_ПерПредСд, "ОКСМ", Лист1Б.Строка070КодСтраныПроисхождения);
			УстановитьНеобязательныйАтрибут(Шаблон_ПерПредСд, "ОКЕИ", СокрЛП(Лист1Б.Строка110КодЕдиницыИзмерения));
			
			УстановитьАтрибут(Шаблон_ПерПредСд, "Количество", Формат(Лист1Б.Строка120Количество, "ЧЦ=14; ЧДЦ=5; ЧРД=.; ЧН=' '; ЧГ=0"));
			УстановитьНеобязательныйАтрибут(Шаблон_ПерПредСд, "ЦенаЕдин", Формат(Лист1Б.Строка130Цена, "ЧЦ=18; ЧДЦ=4; ЧРД=.; ЧН=' '; ЧГ=0"));
			УстановитьНеобязательныйАтрибут(Шаблон_ПерПредСд, "КодВалют", Лист1Б.Строка140КодВалюты);
			УстановитьНеобязательныйАтрибут(Шаблон_ПерПредСд, "ПроцСтав", Формат(Лист1Б.Строка150ПроцентнаяСтавка, "ЧЦ=7; ЧДЦ=4; ЧРД=.; ЧН=' '; ЧГ=0"));
			УстановитьНеобязательныйАтрибут(Шаблон_ПерПредСд, "СтоимИтог", Формат(Лист1Б.Строка160Стоимость, "ЧЦ=15; ЧДЦ=0; ЧН=' '; ЧГ=0"));
			УстановитьАтрибут(Шаблон_ПерПредСд, "ДатаСовСд", Формат(Лист1Б.Строка150ДатаСовершения, "ДЛФ=D"));
			
			ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_ПерПредСд);
			
			УвеличитьОтступ(Контекст);
			
			Если ЕстьЗаполненныеРеквизиты(Лист1Б, "Строка080КодСтраныОтправки, Строка080КодРегионаОтправки, Строка080НаселенныйПунктОтправки") Тогда
				Шаблон_МестОтпрТов = "<МестОтпрТов %ОКСМ% %КодРегион% %НаселПункт%/>";
				УстановитьАтрибут(Шаблон_МестОтпрТов, "ОКСМ", Лист1Б.Строка080КодСтраныОтправки);
				УстановитьНеобязательныйАтрибут(Шаблон_МестОтпрТов, "КодРегион", Лист1Б.Строка080КодРегионаОтправки);
				УстановитьНеобязательныйАтрибут(Шаблон_МестОтпрТов, "НаселПункт", Лист1Б.Строка080НаселенныйПунктОтправки);
				
				ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_МестОтпрТов);
			КонецЕсли;
			
			Если ЕстьЗаполненныеРеквизиты(Лист1Б, "Строка090КодСтраныСовершенияСделки, Строка090КодРегионаСовершенияСделки, Строка090НаселенныйПунктСовершенияСделки") Тогда
				Шаблон_МестСовСд = "<МестСовСд %ОКСМ% %КодРегион% %НаселПункт%/>";
				УстановитьАтрибут(Шаблон_МестСовСд, "ОКСМ", Лист1Б.Строка090КодСтраныСовершенияСделки);
				УстановитьНеобязательныйАтрибут(Шаблон_МестСовСд, "КодРегион", Лист1Б.Строка090КодРегионаСовершенияСделки);
				УстановитьНеобязательныйАтрибут(Шаблон_МестСовСд, "НаселПункт", Лист1Б.Строка090НаселенныйПунктСовершенияСделки);
				
				ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_МестСовСд);
			КонецЕсли;
			
			УменьшитьОтступ(Контекст);
			
			ТекстСделки.ДобавитьСтроку(Контекст.Отступ + "</ПерПредСд>");
		КонецЦикла;
		
		УменьшитьОтступ(Контекст);
		
		ТекстСделки.ДобавитьСтроку(Контекст.Отступ + "</СвПредмСдН>");
		
	КонецЦикла;
	
КонецПроцедуры

// Заполняет сведения о предмете сделки уведомления версии 5.05.
// Параметры:
//  Контекст - Структура - структура измерений:
//  * ВременныйКаталог         - Строка -путь временного каталога
//  * ЗавершающиеТэги          - Строка - описание завершающих тегов.
//  * ИдФайлИсх                - Строка
//  * ИдФайлПерв               - Строка
//  * ОсновныеСведенияВыгрузки - Структура - описание структуры выгрузки.
//  * Отступ                   - Строка - описание табуляции.
//  * РазмерЗавершающихТэгов   - Число - размер завершающих тегов.
//  * РазмерРазделения         - Число
//  * ТекущийРазмер            - Число
//  ТекстСделки               - ТекстовыйДокумент - текстовый документ для вывода.
//  Листы1Б                   - ТаблицаЗначений - описание информации листов 1Б.
//  Сделка                    - ДокументСсылка.КонтролируемаяСделка - лист 1А.
Процедура ЗаполнитьСведенияОПредметеСделки505(Контекст, ТекстСделки, Листы1Б, Сделка)
	
	ГруппировкаЛистов1Б = Новый ТаблицаЗначений;
	ГруппировкаЛистов1Б.Колонки.Добавить("ТипПредметаСделки", Листы1Б.Колонки.Строка020ТипПредмета.ТипЗначения);
	ГруппировкаЛистов1Б.Колонки.Добавить("НомерЛиста1Б", Листы1Б.Колонки.НомерЛиста1Б.ТипЗначения);
	ГруппировкаЛистов1Б.Колонки.Добавить("УникальныйНомерВалютногоКонтроля", Листы1Б.Колонки.УникальныйНомерВалютногоКонтроля.ТипЗначения);
	ГруппировкаЛистов1Б.Колонки.Добавить("Листы1Б", Новый ОписаниеТипов("Массив"));
	
	СписокЛистов1Б = Листы1Б.НайтиСтроки(Новый Структура("Сделка", Сделка));
	Для Каждого Лист1Б Из СписокЛистов1Б Цикл
		СтрокиГруппировки = ГруппировкаЛистов1Б.НайтиСтроки(
			Новый Структура("ТипПредметаСделки,НомерЛиста1Б,УникальныйНомерВалютногоКонтроля",
				Лист1Б.Строка020ТипПредмета, Лист1Б.НомерЛиста1Б, Лист1Б.УникальныйНомерВалютногоКонтроля));
		
		Если СтрокиГруппировки.Количество() > 0 Тогда
			СтрокаДляДобавления = СтрокиГруппировки[0];
		Иначе
			СтрокаДляДобавления = ГруппировкаЛистов1Б.Добавить();
			СтрокаДляДобавления.ТипПредметаСделки = Лист1Б.Строка020ТипПредмета;
			СтрокаДляДобавления.НомерЛиста1Б = Лист1Б.НомерЛиста1Б;
			СтрокаДляДобавления.УникальныйНомерВалютногоКонтроля = Лист1Б.УникальныйНомерВалютногоКонтроля;
		КонецЕсли;
		
		СтрокаДляДобавления.Листы1Б.Добавить(Лист1Б);
		
	КонецЦикла;
	
	ГруппировкаЛистов1Б.Сортировать("НомерЛиста1Б,ТипПредметаСделки");
	Для Каждого СтрокаГруппировки Из ГруппировкаЛистов1Б Цикл
		
		Шаблон_СвПредмСдН = "<СвПредмСдН %ТипПредСд% %НомПорЛиста%>";
		
		ТипПредСд = СтрокаГруппировки.ТипПредметаСделки;
		УстановитьАтрибут(Шаблон_СвПредмСдН, "ТипПредСд", ТипПредСд);
		НомерЛистаСтрокой = "";
		Если ЗначениеЗаполнено(Лист1Б.НомерЛиста1Б) Тогда
			НомерЛистаСтрокой = XMLСтрока(Лист1Б.НомерЛиста1Б);
		КонецЕсли;
		УстановитьНеобязательныйАтрибут(Шаблон_СвПредмСдН, "НомПорЛиста", НомерЛистаСтрокой);
		
		ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_СвПредмСдН);
		
		УвеличитьОтступ(Контекст);
		
		Для Каждого Лист1Б ИЗ СтрокаГруппировки.Листы1Б Цикл
			
			Шаблон_СведСделк = "<СведСделк %УникНомерКонтр%>";
			УстановитьНеобязательныйАтрибут(Шаблон_СведСделк, "УникНомерКонтр", Лист1Б.УникальныйНомерВалютногоКонтроля);
			
			ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_СведСделк);
			
			УвеличитьОтступ(Контекст);
		
			Шаблон_ПерПредСд = "<ПерПредСд %НаимПредСд% %ТНВЭД% %ОКПД2% %ОКВЭД2% %НомУчСд% %НомДог% %ДатаДог% %КодУсловПост% %КодПунктПост% %ОКСМ% %МестСовСд% %ОКЕИ% %Количество% %ЦенаЕдин% %КодВалют% %ПроцСтав% %СумРасчРоял% %СтоимИтог% %ДатаСовСд%/>";
			ЗаполненОКПД2 = ЗначениеЗаполнено(Лист1Б.Строка043КодПоОКПД2);
			ЗаполненТНВЭД = ЗначениеЗаполнено(Лист1Б.Строка040КодПоТНВЭД);
			
			Если ТипПредСд = 1 Тогда
				Если ЗаполненОКПД2 Тогда
					УстановитьНеобязательныйАтрибут(Шаблон_ПерПредСд, "ТНВЭД", Лист1Б.Строка040КодПоТНВЭД); // Атрибут может присутствовать
				Иначе
					УстановитьАтрибут(Шаблон_ПерПредСд, "ТНВЭД", Лист1Б.Строка040КодПоТНВЭД); // Атрибут должен присутствовать
				КонецЕсли;
				Если ЗаполненТНВЭД Тогда
					УстановитьНеобязательныйАтрибут(Шаблон_ПерПредСд, "ОКПД2", Лист1Б.Строка043КодПоОКПД2); // Атрибут может присутствовать
				Иначе
					УстановитьАтрибут(Шаблон_ПерПредСд, "ОКПД2", Лист1Б.Строка043КодПоОКПД2); // Атрибут должен присутствовать
				КонецЕсли;
				УстановитьНеобязательныйАтрибут(Шаблон_ПерПредСд, "ОКВЭД2", ""); // Атрибут должен отсутствовать
				УстановитьНеобязательныйАтрибут(Шаблон_ПерПредСд, "КодУсловПост", Лист1Б.Строка100КодУсловийПоставки); // Атрибут может присутствовать
			Иначе // значения 2 и 3
				УстановитьНеобязательныйАтрибут(Шаблон_ПерПредСд, "ТНВЭД", ""); // Атрибут должен отсутствовать
				УстановитьНеобязательныйАтрибут(Шаблон_ПерПредСд, "ОКПД2", ""); // Атрибут должен отсутствовать
				УстановитьАтрибут(Шаблон_ПерПредСд, "ОКВЭД2", Лист1Б.Строка045КодОКВЭД2.Код); // Атрибут должен присутствовать
				УстановитьНеобязательныйАтрибут(Шаблон_ПерПредСд, "КодУсловПост", ""); // Атрибут должен отсутствовать
			КонецЕсли;
			
			УстановитьАтрибут(Шаблон_ПерПредСд, "НаимПредСд", Лист1Б.Строка030НаименованиеПредмета);
			УстановитьАтрибут(Шаблон_ПерПредСд, "НомУчСд", Формат(Лист1Б.Строка050НомерУчастникаСделки, "ЧЦ=4; ЧДЦ=0; ЧН=; ЧГ=0"));
			УстановитьАтрибут(Шаблон_ПерПредСд, "НомДог", Лист1Б.Строка060НомерДоговора);
			УстановитьАтрибут(Шаблон_ПерПредСд, "ДатаДог", Формат(Лист1Б.Строка065ДатаДоговора, "ДЛФ=D"));
			УстановитьНеобязательныйАтрибут(Шаблон_ПерПредСд, "ОКЕИ", СокрЛП(Лист1Б.Строка110КодЕдиницыИзмерения));
			
			// Код страны происхождения заполняется только в случае приобретения налогоплательщиком товаров
			// и иных гражданских прав в области внешней торговли.
			// Поэтому проверим, что сделка внешнеторговая и это приобретение товаров или иных гражданских прав.
			КодСтраныПроисхождения = "";
			Если Лист1Б.ВнешнеторговаяСделка
				И Лист1Б.ТипСделки = Перечисления.ТипыКонтролируемыхСделок.ОсуществленРасход
				И ТипПредСд <> 2 Тогда
				КодСтраныПроисхождения = Лист1Б.Строка070КодСтраныПроисхождения;
			КонецЕсли;
			
			УстановитьНеобязательныйАтрибут(Шаблон_ПерПредСд, "ОКСМ", КодСтраныПроисхождения);
			
			КодПунктПост = "";
			МестСовСд = "";
			Если ТипПредСд = 1
				И КодСтраныПроисхождения = "643" Тогда
				// Из комментария к заполнению:
				// ОКМО, где осуществляется переход права собственности на товар.
				// В случае если переход права собственности на товар осуществлен за пределами Российской Федерации, поле 3.6 не заполняется.
				// В схеме есть проверка на ОКСМ = 643. Однако, ОКСМ - это страна происхождения предмета сделки, а не страна совершения сделки.
				// Возможно, это ошибка, но сейчас выгрузка соответствует схеме.
				КодПунктПост = Лист1Б.ОКТМОСовершенияСделки;
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(КодПунктПост) Тогда
				МестСовСд = Лист1Б.Строка090НаселенныйПунктСовершенияСделки;
			КонецЕсли;
			
			УстановитьНеобязательныйАтрибут(Шаблон_ПерПредСд, "КодПунктПост", КодПунктПост);
			УстановитьНеобязательныйАтрибут(Шаблон_ПерПредСд, "МестСовСд", МестСовСд);
			
			// Для обязательных атрибутов представление нуля должно быть 0, т.к. оно выгружается в любом случае.
			// Для необязательных атрибутов - должна быть пустая строка, т.к. в УстановитьНеобязательныйАтрибут()
			// проверяется заполненность текстового значения, поэтому пустое значение должно быть пробелом или пустой строкой.
			УстановитьАтрибут(Шаблон_ПерПредСд, "Количество", Формат(Лист1Б.Строка120Количество, "ЧЦ=14; ЧДЦ=5; ЧРД=.; ЧН=0.00000; ЧГ=0"));
			УстановитьНеобязательныйАтрибут(Шаблон_ПерПредСд, "ЦенаЕдин", Формат(Лист1Б.Строка130Цена, "ЧЦ=18; ЧДЦ=4; ЧРД=.; ЧН=' '; ЧГ=0"));
			УстановитьНеобязательныйАтрибут(Шаблон_ПерПредСд, "КодВалют", Лист1Б.Строка140КодВалюты);
			УстановитьНеобязательныйАтрибут(Шаблон_ПерПредСд, "ПроцСтав", Формат(Лист1Б.Строка150ПроцентнаяСтавка, "ЧЦ=7; ЧДЦ=4; ЧРД=.; ЧН=' '; ЧГ=0"));
			УстановитьНеобязательныйАтрибут(Шаблон_ПерПредСд, "СумРасчРоял", Формат(Лист1Б.СуммаБазыДляРасчетаРоялти, "ЧЦ=15; ЧДЦ=0; ЧН=' '; ЧГ=0"));
			УстановитьНеобязательныйАтрибут(Шаблон_ПерПредСд, "СтоимИтог", Формат(Лист1Б.Строка160Стоимость, "ЧЦ=15; ЧДЦ=0; ЧН=' '; ЧГ=0"));
			УстановитьАтрибут(Шаблон_ПерПредСд, "ДатаСовСд", Формат(Лист1Б.Строка150ДатаСовершения, "ДЛФ=D"));
			
			ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_ПерПредСд);
			
			УменьшитьОтступ(Контекст);
			
			ТекстСделки.ДобавитьСтроку(Контекст.Отступ + "</СведСделк>");
			
		КонецЦикла;
		
		УменьшитьОтступ(Контекст);
		
		ТекстСделки.ДобавитьСтроку(Контекст.Отступ + "</СвПредмСдН>");
		
	КонецЦикла;
	
КонецПроцедуры

// Заполняет сведения о предмете сделки уведомления версии 5.05.
// Параметры:
//  Контекст - Структура - структура измерений:
//  * ВременныйКаталог         - Строка -путь временного каталога
//  * ЗавершающиеТэги          - Строка - описание завершающих тегов.
//  * ИдФайлИсх                - Строка
//  * ИдФайлПерв               - Строка
//  * ОсновныеСведенияВыгрузки - Структура - описание структуры выгрузки.
//  * Отступ                   - Строка - описание табуляции.
//  * РазмерЗавершающихТэгов   - Число - размер завершающих тегов.
//  * РазмерРазделения         - Число
//  * ТекущийРазмер            - Число
//  ТекстСделки               - ТекстовыйДокумент - текстовый документ для вывода.
//  Листы1В                   - ТаблицаЗначений - описание информации листов 1B.
//  Сделка                    - ДокументСсылка.КонтролируемаяСделка - лист 1А.
Процедура ЗаполнитьСведенияОПоследующейРеализации505(Контекст, ТекстСделки, Листы1В, Сделка)
	
	СписокЛистов1В = Листы1В.НайтиСтроки(Новый Структура("Сделка", Сделка));
	Для Каждого Лист1В Из СписокЛистов1В Цикл
		
		НужноВыгружатьСведенияОСделке = Перечисления.ТипыИспользованияПроисхожденияТовараКонтролируемыхСделок.НужноВыгружатьСведенияОСделке(Лист1В.ИспользованиеПроисхождениеТовара);
		
		Шаблон_СвСдПослРеал = "<СвСдПослРеал %НомПорЛистаБ% %СпрНомерЛист% %КомНомерЛист% %НомПорЛистаВ.1% %НомПорЛистаВ.2% %НомПорЛистаВ.3% %НомПорЛистаВ.4% %ВзСвязУч% %КодТипСд% %КодИспТовар% %НаимИспТовар%";
		Если Не НужноВыгружатьСведенияОСделке Тогда
			Шаблон_СвСдПослРеал = Шаблон_СвСдПослРеал + "/";
		КонецЕсли;
		Шаблон_СвСдПослРеал = Шаблон_СвСдПослРеал + ">";
		
		УстановитьАтрибут(Шаблон_СвСдПослРеал, "НомПорЛистаБ", XMLСтрока(Лист1В.НомерЛиста1Б));
		
		УстановитьНеобязательныйАтрибут(Шаблон_СвСдПослРеал, "СпрНомерЛист", Лист1В.СправочноНомераЛистов1Б);
		
		УстановитьНеобязательныйАтрибут(Шаблон_СвСдПослРеал, "КомНомерЛист", Лист1В.КомментарийКЛисту1Б);
		
		УстановитьНеобязательныйАтрибут(Шаблон_СвСдПослРеал, "НомПорЛистаВ.1", Лист1В.НомерЛиста1В_0);
		УстановитьНеобязательныйАтрибут(Шаблон_СвСдПослРеал, "НомПорЛистаВ.2", Лист1В.НомерЛиста1В_1);
		УстановитьНеобязательныйАтрибут(Шаблон_СвСдПослРеал, "НомПорЛистаВ.3", Лист1В.НомерЛиста1В_2);
		УстановитьНеобязательныйАтрибут(Шаблон_СвСдПослРеал, "НомПорЛистаВ.4", Лист1В.НомерЛиста1В_3);
		
		УстановитьАтрибут(Шаблон_СвСдПослРеал, "ВзСвязУч", Лист1В.УчастникиВзаимозависимы);
		
		УстановитьАтрибут(Шаблон_СвСдПослРеал, "КодТипСд", Лист1В.КодТипаСделки);
		
		КодИспТовар = Перечисления.ТипыИспользованияПроисхожденияТовараКонтролируемыхСделок.КодДляВыгрузки(Лист1В.ИспользованиеПроисхождениеТовара);
		УстановитьАтрибут(Шаблон_СвСдПослРеал, "КодИспТовар", КодИспТовар);
		
		НаимИспТовар = "";
		Если КодИспТовар = "5" Тогда
			НаимИспТовар = Лист1В.НаименованиеИспользованияПроисхожденияТовара;
		КонецЕсли;
		
		УстановитьНеобязательныйАтрибут(Шаблон_СвСдПослРеал, "НаимИспТовар", НаимИспТовар);
		
		ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_СвСдПослРеал);
		
		Если НужноВыгружатьСведенияОСделке Тогда
			
			УвеличитьОтступ(Контекст);
			
			Шаблон_СвСделка = "<СвСделка %ПрУчСд% %НомДрУчСд% %НомерДог% %ДатаДог% %КодУслПост% %КодПунктПост% %ОКСМ% %ОКЕИ% %Количество% %ЦенаЕдин% %КодВалют% %СтоимИтог% %ДатаСовСд%/>";
			
			ПрУчСд = Перечисления.ТипыУчастниковЦепочкиКонтролируемыхСделок.КодДляВыгрузки(Лист1В.ПризнакУчастникаСделки);
			УстановитьАтрибут(Шаблон_СвСделка, "ПрУчСд", ПрУчСд);
			
			НомДрУчСд = "";
			НомерДог = "";
			ДатаДог = "";
			КодУслПост = "";
			КодПунктПост = "";
			ОКСМ = "";
			Если ПрУчСд <> "3" Тогда
				НомДрУчСд = Формат(Лист1В.НомерУчастникаСделки, "ЧЦ=4; ЧДЦ=0; ЧН=; ЧГ=0");
				НомерДог = Лист1В.НомерДоговора;
				ДатаДог = Формат(Лист1В.ДатаДоговора, "ДФ=dd.MM.yyyy");
				КодУслПост = Лист1В.КодУсловийПоставки;
				ОКСМ = Лист1В.КодСтраныСовершенияСделки;
				Если ОКСМ = "643" Тогда
					КодПунктПост = Лист1В.ОКТМОСовершенияСделки;
				КонецЕсли;
			КонецЕсли;
			УстановитьНеобязательныйАтрибут(Шаблон_СвСделка, "НомДрУчСд", НомДрУчСд);
			УстановитьНеобязательныйАтрибут(Шаблон_СвСделка, "НомерДог", НомерДог);
			УстановитьНеобязательныйАтрибут(Шаблон_СвСделка, "ДатаДог", ДатаДог);
			УстановитьНеобязательныйАтрибут(Шаблон_СвСделка, "КодУслПост", КодУслПост);
			УстановитьНеобязательныйАтрибут(Шаблон_СвСделка, "КодПунктПост", КодПунктПост);
			УстановитьНеобязательныйАтрибут(Шаблон_СвСделка, "ОКСМ", ОКСМ);
			
			УстановитьНеобязательныйАтрибут(Шаблон_СвСделка, "ОКЕИ", СокрЛП(Лист1В.КодЕдиницыИзмерения));
			
			// Для обязательных атрибутов представление нуля должно быть 0, т.к. оно выгружается в любом случае.
			// Для необязательных атрибутов - должна быть пустая строка, т.к. в УстановитьНеобязательныйАтрибут()
			// проверяется заполненность текстового значения, поэтому пустое значение должно быть пробелом или пустой строкой.
			УстановитьАтрибут(Шаблон_СвСделка, "Количество", Формат(Лист1В.Количество, "ЧЦ=14; ЧДЦ=5; ЧРД=.; ЧН=0.00000; ЧГ=0"));
			
			ЦенаЕдин = "";
			ДатаСовСд = "";
			Если ПрУчСд <> "3" Тогда
				ЦенаЕдин = Формат(Лист1В.Цена, "ЧЦ=18; ЧДЦ=4; ЧРД=.; ЧН=' '; ЧГ=0");
				ДатаСовСд = Формат(Лист1В.ДатаСовершенияСделки, "ДФ=dd.MM.yyyy");
			КонецЕсли;
			УстановитьНеобязательныйАтрибут(Шаблон_СвСделка, "ЦенаЕдин", ЦенаЕдин);
			УстановитьНеобязательныйАтрибут(Шаблон_СвСделка, "ДатаСовСд", ДатаСовСд);
			
			УстановитьНеобязательныйАтрибут(Шаблон_СвСделка, "КодВалют", Лист1В.КодВалюты);
			
			СтоимИтог = "";
			Если ЗначениеЗаполнено(Лист1В.Стоимость) Тогда
				СтоимИтог = Формат(Лист1В.Стоимость, "ЧЦ=15; ЧДЦ=0; ЧН=' '; ЧГ=0");
			КонецЕсли;
			УстановитьНеобязательныйАтрибут(Шаблон_СвСделка, "СтоимИтог", СтоимИтог);
			
			ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_СвСделка);
			
			УменьшитьОтступ(Контекст);
			
			ТекстСделки.ДобавитьСтроку(Контекст.Отступ + "</СвСдПослРеал>");
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Заполняет сведения о организациях, являющихся участниками контролируемой сделоки.
// Поддерживаемый формат уведомелния о контролируемых сделках 5.05.
//
// Параметры:
//  Контекст       - Структура - параметры контекста xml файла.
//  ТекстСделки    - ТекстовыйДокумент - описание сделки.
//  Листы1Г        - ТаблицаЗначений - описание параметров листов.
//  Сделка         - ДокументСсылка.КонтролируемаяСделка - документ, описывающий структуру Листа 1А.
//
Процедура ЗаполнитьСведенияОСопутствующихУслугах505(Контекст, ТекстСделки, Листы1Г, Сделка)
	
	СписокЛистов1Г = Листы1Г.НайтиСтроки(Новый Структура("Сделка", Сделка));
	Если Не ЗначениеЗаполнено(СписокЛистов1Г) Тогда
		Возврат;
	КонецЕсли;
	
	ГруппировкаЛистов1Г = Новый ТаблицаЗначений;
	ГруппировкаЛистов1Г.Колонки.Добавить("НомерЛиста1Б", Листы1Г.Колонки.НомерЛиста1Б.ТипЗначения);
	ГруппировкаЛистов1Г.Колонки.Добавить("Листы1Г", Новый ОписаниеТипов("Массив"));
	
	Для Каждого Лист1Г Из СписокЛистов1Г Цикл
		СтрокиГруппировки = ГруппировкаЛистов1Г.НайтиСтроки(Новый Структура("НомерЛиста1Б", Лист1Г.НомерЛиста1Б));
		
		Если СтрокиГруппировки.Количество() > 0 Тогда
			СтрокаДляДобавления = СтрокиГруппировки[0];
		Иначе
			СтрокаДляДобавления = ГруппировкаЛистов1Г.Добавить();
			СтрокаДляДобавления.НомерЛиста1Б = Лист1Г.НомерЛиста1Б;
		КонецЕсли;
		
		СтрокаДляДобавления.Листы1Г.Добавить(Лист1Г);
		
	КонецЦикла;
	
	ГруппировкаЛистов1Г.Сортировать("НомерЛиста1Б");
	
	Для Каждого СтрокаЛиста1Б Из ГруппировкаЛистов1Г Цикл
		
		Шаблон_СвСопУсл = "<СвСопУсл %НомПорЛистаБ%>";
		
		УстановитьАтрибут(Шаблон_СвСопУсл, "НомПорЛистаБ", XMLСтрока(СтрокаЛиста1Б.НомерЛиста1Б));
		
		ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_СвСопУсл);
		
		УвеличитьОтступ(Контекст);
		
		Для Каждого Лист1Г Из СтрокаЛиста1Б.Листы1Г Цикл
			
			Шаблон_СвСопУслСд = "<СвСопУслСд %НаимУсл% %ВзСвязУч% %НаимОргИсп% %РегНомер% %ОКСМ% %СтоимИтог% %КодВалют%/>";
			
			УстановитьАтрибут(Шаблон_СвСопУслСд, "НаимУсл", Лист1Г.НаименованиеУслуги);
			УстановитьАтрибут(Шаблон_СвСопУслСд, "ВзСвязУч", Лист1Г.УчастникиВзаимозависимы);
			УстановитьАтрибут(Шаблон_СвСопУслСд, "НаимОргИсп", Лист1Г.НаименованиеКонтрагента);
			УстановитьНеобязательныйАтрибут(Шаблон_СвСопУслСд, "РегНомер", Лист1Г.РегистрационныйНомер);
			УстановитьНеобязательныйАтрибут(Шаблон_СвСопУслСд, "ОКСМ", Лист1Г.КодСтраныРегистрации);
			
			СтоимИтог = "";
			Если ЗначениеЗаполнено(Лист1Г.Стоимость) Тогда
				СтоимИтог = Формат(Лист1Г.Стоимость, "ЧЦ=15; ЧДЦ=0; ЧН=' '; ЧГ=0");
			КонецЕсли;
			УстановитьНеобязательныйАтрибут(Шаблон_СвСопУслСд, "СтоимИтог", СтоимИтог);
			УстановитьНеобязательныйАтрибут(Шаблон_СвСопУслСд, "КодВалют", Лист1Г.КодВалюты);
			
			ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_СвСопУслСд);
			
		КонецЦикла;
		
		УменьшитьОтступ(Контекст);
		
		ТекстСделки.ДобавитьСтроку(Контекст.Отступ + "</СвСопУсл>");
		
	КонецЦикла;
	
КонецПроцедуры

// Заполняет сведения о организациях, являющихся участниками контролируемой сделоки.
// Поддерживаемый формат уведомелния о контролируемых сделках 5.02.
//
// Параметры:
//  Контекст       - Структура - параметры контекста xml файла.
//  ТекстСделки    - ТекстовыйДокумент - описание сделки.
//  Листы2         - ТаблицаЗначений - описание параметров листов.
//  ДанныеРаздела2 - ТаблицаЗначений - описание параметров раздела 3 уведомления.
//  Сделка         - ДокументСсылка.КонтролируемаяСделка - документ, описывающий структуру Листа 1А.
//
Процедура ЗаполнитьСведенияОбУчастникахОрганизациях502(Контекст, ТекстСделки, Листы2, ДанныеРаздела2, Сделка)
	
	СписокЛистов2 = Листы2.НайтиСтроки(Новый Структура("Сделка", Сделка));
	
	Для Каждого Лист2 Из СписокЛистов2 Цикл
		
		ДанныеЛиста2 = ДанныеРаздела2.Найти(Лист2.Контрагент, "Контрагент");
		
		Шаблон_СвОргУчаст = "<СвОргУчаст %НомПорСд% %ПрОрг% %ОКСМ% %НаимОрг% %ИННЮЛ% %КПП% %РегНомИн% %КодНПРег% %АдрИнТекст%/>";
		УстановитьАтрибут(Шаблон_СвОргУчаст, "НомПорСд", XMLСтрока(ДанныеЛиста2.НомерКонтрагента));
		УстановитьАтрибут(Шаблон_СвОргУчаст, "ПрОрг", ДанныеЛиста2.Строка020ТипОрганизации);
		УстановитьАтрибут(Шаблон_СвОргУчаст, "ОКСМ", ДанныеЛиста2.Строка030КакКодСтраныРегистрации);
		УстановитьАтрибут(Шаблон_СвОргУчаст, "НаимОрг", ДанныеЛиста2.Строка040Наименование);
		УстановитьНеобязательныйАтрибут(Шаблон_СвОргУчаст, "ИННЮЛ", ДанныеЛиста2.Строка050ИНН);
		УстановитьНеобязательныйАтрибут(Шаблон_СвОргУчаст, "КПП", ДанныеЛиста2.Строка060КПП);
		УстановитьНеобязательныйАтрибут(Шаблон_СвОргУчаст, "РегНомИн", ДанныеЛиста2.Строка070РегНомерВСтрокеРегистрации);
		УстановитьНеобязательныйАтрибут(Шаблон_СвОргУчаст, "КодНПРег", ДанныеЛиста2.Строка080КодНалогВСтранеРегистрации);
		УстановитьНеобязательныйАтрибут(Шаблон_СвОргУчаст, "АдрИнТекст", ?(ДанныеЛиста2.Строка020ТипОрганизации = 1, "", ДанныеЛиста2.Строка090АдресИностраннойОрганизации));
		
		ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_СвОргУчаст);
		
	КонецЦикла;
	
КонецПроцедуры

// Заполняет сведения о организациях, являющихся участниками контролируемой сделоки.
// Поддерживаемые форматы уведомелния о контролируемых сделках 5.03, 5.04.
//
// Параметры:
//  Контекст       - Структура - параметры контекста xml файла.
//  ТекстСделки    - ТекстовыйДокумент - описание сделки.
//  Листы2         - ТаблицаЗначений - описание параметров листов.
//  ДанныеРаздела2 - ТаблицаЗначений - описание параметров раздела 3 уведомления.
//  Сделка         - ДокументСсылка.КонтролируемаяСделка - документ, описывающий структуру Листа 1А.
//
Процедура ЗаполнитьСведенияОбУчастникахОрганизациях503_504(Контекст, ТекстСделки, Листы2, ДанныеРаздела2, Сделка)
	
	СписокЛистов2 = Листы2.НайтиСтроки(Новый Структура("Сделка", Сделка));
	
	Для Каждого Лист2 Из СписокЛистов2 Цикл
		
		ДанныеЛиста2 = ДанныеРаздела2.Найти(Лист2.Контрагент, "Контрагент");
		
		Шаблон_СвОргУчаст = "<СвОргУчаст %НомПорСд% %СведОтнАгент% %ПрОрг% %ОКСМ% %НаимОргРус% %НаимОргЛат% %ИННЮЛ% %КПП% %РегНомИн% %КодНПРег% %АдрИнТекст%/>";
		Если Не Лист2.СведенияОКомиссионере Тогда
			УстановитьАтрибут(Шаблон_СвОргУчаст, "НомПорСд", XMLСтрока(ДанныеЛиста2.НомерКонтрагента));
		Иначе
			УстановитьАтрибут(Шаблон_СвОргУчаст, "НомПорСд", XMLСтрока(0));
		КонецЕсли;
		УстановитьАтрибут(Шаблон_СвОргУчаст, "СведОтнАгент", Лист2.СведОтносительноАгента);
		УстановитьАтрибут(Шаблон_СвОргУчаст, "ПрОрг", ДанныеЛиста2.Строка020ТипОрганизации);
		УстановитьАтрибут(Шаблон_СвОргУчаст, "ОКСМ", ДанныеЛиста2.Строка030КакКодСтраныРегистрации);
		УстановитьАтрибут(Шаблон_СвОргУчаст, "НаимОргРус", ДанныеЛиста2.Строка040Наименование);
		УстановитьНеобязательныйАтрибут(Шаблон_СвОргУчаст, "НаимОргЛат", ДанныеЛиста2.Строка040НаименованиеЛат);
		УстановитьНеобязательныйАтрибут(Шаблон_СвОргУчаст, "ИННЮЛ", ДанныеЛиста2.Строка050ИНН);
		УстановитьНеобязательныйАтрибут(Шаблон_СвОргУчаст, "КПП", ДанныеЛиста2.Строка060КПП);
		УстановитьНеобязательныйАтрибут(Шаблон_СвОргУчаст, "РегНомИн", ДанныеЛиста2.Строка070РегНомерВСтрокеРегистрации);
		УстановитьНеобязательныйАтрибут(Шаблон_СвОргУчаст, "КодНПРег", ДанныеЛиста2.Строка080КодНалогВСтранеРегистрации);
		УстановитьНеобязательныйАтрибут(Шаблон_СвОргУчаст, "АдрИнТекст", ?(ДанныеЛиста2.Строка020ТипОрганизации = 1, "", ДанныеЛиста2.Строка090АдресИностраннойОрганизации));
		
		ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_СвОргУчаст);
		
	КонецЦикла;
	
КонецПроцедуры

// Заполняет сведения о организациях, являющихся участниками контролируемой сделоки.
// Поддерживаемый формат уведомелния о контролируемых сделках 5.05.
//
// Параметры:
//  Контекст       - Структура - параметры контекста xml файла.
//  ТекстСделки    - ТекстовыйДокумент - описание сделки.
//  Листы2         - ТаблицаЗначений - описание параметров листов.
//  ДанныеРаздела2 - ТаблицаЗначений - описание параметров раздела 3 уведомления.
//  Сделка         - ДокументСсылка.КонтролируемаяСделка - документ, описывающий структуру Листа 1А.
//
Процедура ЗаполнитьСведенияОбУчастникахОрганизациях505(Контекст, ТекстСделки, Листы2, ДанныеРаздела2, Сделка)
	
	СписокЛистов2 = Листы2.НайтиСтроки(Новый Структура("Сделка", Сделка));
	
	Для Каждого Лист2 Из СписокЛистов2 Цикл
		
		ДанныеЛиста2 = ДанныеРаздела2.Найти(Лист2.Контрагент, "Контрагент");
		
		Шаблон_СвОргУчаст = "<СвОргУчаст %НомПорСд% %СведОтнАгент% %ПрОрг% %ОКСМ% %НаимОргРус% %НаимОргЛат% %ИННЮЛ% %КПП% %РегНомИн% %НаимИдРегНом%>";
		Если Не Лист2.СведенияОКомиссионере Тогда
			УстановитьАтрибут(Шаблон_СвОргУчаст, "НомПорСд", XMLСтрока(ДанныеЛиста2.НомерКонтрагента));
		Иначе
			УстановитьАтрибут(Шаблон_СвОргУчаст, "НомПорСд", XMLСтрока(0));
		КонецЕсли;
		УстановитьАтрибут(Шаблон_СвОргУчаст, "СведОтнАгент", Лист2.СведОтносительноАгента);
		УстановитьАтрибут(Шаблон_СвОргУчаст, "ПрОрг", ДанныеЛиста2.Строка020ТипОрганизации);
		УстановитьАтрибут(Шаблон_СвОргУчаст, "ОКСМ", ДанныеЛиста2.Строка030КакКодСтраныРегистрации);
		УстановитьАтрибут(Шаблон_СвОргУчаст, "НаимОргРус", ДанныеЛиста2.Строка040Наименование);
		УстановитьНеобязательныйАтрибут(Шаблон_СвОргУчаст, "НаимОргЛат", ДанныеЛиста2.Строка040НаименованиеЛат);
		УстановитьНеобязательныйАтрибут(Шаблон_СвОргУчаст, "ИННЮЛ", ДанныеЛиста2.Строка050ИНН);
		УстановитьНеобязательныйАтрибут(Шаблон_СвОргУчаст, "КПП", ДанныеЛиста2.Строка060КПП);
		
		РегНомИн = "";
		НаимИдРегНом = "";
		Если ДанныеЛиста2.Строка020ТипОрганизации <> 1 Тогда
			РегНомИн = ДанныеЛиста2.НалоговыйРегистрационныйНомер;
			НаимИдРегНом = ДанныеЛиста2.НаименованиеИдентификатораРегистрационногоНомера;
		КонецЕсли;
		УстановитьНеобязательныйАтрибут(Шаблон_СвОргУчаст, "РегНомИн", РегНомИн);
		УстановитьНеобязательныйАтрибут(Шаблон_СвОргУчаст, "НаимИдРегНом", НаимИдРегНом);
		
		ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_СвОргУчаст);
		
		Если ДанныеЛиста2.Строка020ТипОрганизации <> 1
			И ЕстьЗаполненныеРеквизиты(ДанныеЛиста2, "АдресИностранный_УлицаДом, АдресИностранный_Район, АдресИностранный_НаселенныйПункт, АдресИностранный_Регион, АдресИностранный_Страна") Тогда
			
			УвеличитьОтступ(Контекст);
			
			Шаблон_АдрИнОрг = "<АдрИнОрг %ДомУлица% %Район% %ГородИндекс% %Регион% %Страна%/>";
			
			УстановитьНеобязательныйАтрибут(Шаблон_АдрИнОрг, "ДомУлица", ДанныеЛиста2.АдресИностранный_УлицаДом);
			УстановитьНеобязательныйАтрибут(Шаблон_АдрИнОрг, "Район", ДанныеЛиста2.АдресИностранный_Район);
			УстановитьНеобязательныйАтрибут(Шаблон_АдрИнОрг, "ГородИндекс", ДанныеЛиста2.АдресИностранный_НаселенныйПункт);
			УстановитьНеобязательныйАтрибут(Шаблон_АдрИнОрг, "Регион", ДанныеЛиста2.АдресИностранный_Регион);
			УстановитьНеобязательныйАтрибут(Шаблон_АдрИнОрг, "Страна", ДанныеЛиста2.АдресИностранный_Страна);
			
			ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_АдрИнОрг);
			
			УменьшитьОтступ(Контекст);
			
		КонецЕсли;
		
		ТекстСделки.ДобавитьСтроку(Контекст.Отступ + "</СвОргУчаст>");
		
	КонецЦикла;
	
КонецПроцедуры

// Заполняет сведения о физических лицах, являющихся участниками контролируемой сделоки.
// Поддерживаемый формат уведомелния о контролируемых сделках 5.02.
//
// Параметры:
//  Контекст       - Структура - параметры контекста xml файла.
//  ТекстСделки    - ТекстовыйДокумент - описание сделки.
//  Листы3         - ТаблицаЗначений - описание параметров листов.
//  ДанныеРаздела3 - ТаблицаЗначений - описание параметров раздела 3 уведомления.
//  Сделка         - ДокументСсылка.КонтролируемаяСделка - документ, описывающий структуру Листа 1А.
//
Процедура ЗаполнитьСведенияОбУчастникахФизЛицах502(Контекст, ТекстСделки, Листы3, ДанныеРаздела3, Сделка)
	
	СписокЛистов3 = Листы3.НайтиСтроки(Новый Структура("Сделка", Сделка));
	
	Для Каждого Лист3 Из СписокЛистов3 Цикл
		
		Физлицо = ДанныеРаздела3.Найти(Лист3.Контрагент, "Контрагент");
		
		Шаблон_СвФЛУчаст = "<СвФЛУчаст %НомПорСд% %КодВД%>";
		УстановитьАтрибут(Шаблон_СвФЛУчаст, "НомПорСд", Физлицо.НомерКонтрагента);
		УстановитьАтрибут(Шаблон_СвФЛУчаст, "КодВД", ФизЛицо.Строка020КодВидаДеятельности);
		
		ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_СвФЛУчаст);
		
		УвеличитьОтступ(Контекст);
		
		Шаблон_ФИО = "<ФИО %Фамилия% %Имя% %Отчество%/>";
		УстановитьАтрибут(Шаблон_ФИО, "Фамилия", ФизЛицо.Фамилия);
		УстановитьАтрибут(Шаблон_ФИО, "Имя", ФизЛицо.Имя);
		УстановитьНеобязательныйАтрибут(Шаблон_ФИО, "Отчество", ФизЛицо.Отчество);
		
		ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_ФИО);
		
		Если ЗначениеЗаполнено(ФизЛицо.Строка030ИНН) Тогда
			Шаблон_ИННФЛ = СтрЗаменить("<ИННФЛ>%ИННФЛ%</ИННФЛ>", "%ИННФЛ%", ФизЛицо.Строка030ИНН);
			
			ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_ИННФЛ);
		Иначе
			Шаблон_СвФЛ = "<СвФЛ %ДатаРожд% %МестоРожд% %НалГражд% %ОКСМ%>";
			УстановитьАтрибут(Шаблон_СвФЛ, "ДатаРожд", ФизЛицо.ФизическоеЛицо.ДатаРождения);
			УстановитьАтрибут(Шаблон_СвФЛ, "МестоРожд", ФизЛицо.ФизическоеЛицо.МестоРождения);
			УстановитьАтрибут(Шаблон_СвФЛ, "НалГражд", ?(ФизЛицо.ГражданствоФизЛицСтрана = Справочники.СтраныМира.Россия, "1", "2"));
			УстановитьАтрибут(Шаблон_СвФЛ, "ОКСМ", ?(ЗначениеЗаполнено(ФизЛицо.ГражданствоФизЛицСтрана), ФизЛицо.ГражданствоФизЛицСтрана.Код, "643"));
			
			ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_СвФЛ);
			
			УвеличитьОтступ(Контекст);
			
			Шаблон_УдЛичнФЛ = "<УдЛичнФЛ %КодВидДок% %СерНомДок% %ДатаДок% %ВыдДок%/>";
			УстановитьАтрибут(Шаблон_УдЛичнФЛ, "КодВидДок", КонтролируемыеСделкиПовтИсп.ПолучитьКодВидаДокументаПоВидуДокумента(ФизЛицо.ВидДокумента));
			УстановитьАтрибут(Шаблон_УдЛичнФЛ, "СерНомДок", Строка(ФизЛицо.ДокументСерия) + " " + ФизЛицо.ДокументНомер);
			УстановитьАтрибут(Шаблон_УдЛичнФЛ, "ДатаДок", ФизЛицо.ДокументДатаВыдачи);
			УстановитьАтрибут(Шаблон_УдЛичнФЛ, "ВыдДок", ФизЛицо.ДокументКемВыдан);
			
			ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_УдЛичнФЛ);
			
			Шаблон_СвАдрРФ = "<СвАдрРФ %ПрАдр%>";
			УстановитьАтрибут(Шаблон_СвАдрРФ, "ПрАдр", ?(ФизЛицо.ГражданствоФизЛицСтрана = Справочники.СтраныМира.Россия, "1", "2"));
			
			ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_СвАдрРФ);
			
			УвеличитьОтступ(Контекст);
			
			Шаблон_АдрРФ = "<АдрРФ %Индекс% %КодРегион% %Район% %Город% %НаселПункт% %Улица% %Дом% %Корпус% %Кварт%/>";
			
			ЗначениеАдреса = ?(ЗначениеЗаполнено(Физлицо.КонтактнаяИнформацияЗначениеJSON),
				Физлицо.КонтактнаяИнформацияЗначениеJSON,
				Физлицо.КонтактнаяИнформацияЗначенияПолей);
			
			АдресСтруктурой = КонтролируемыеСделки.СтруктураАдреса(ЗначениеАдреса);
			
			УстановитьНеобязательныйАтрибут(Шаблон_АдрРФ, "Индекс", АдресСтруктурой.Индекс);
			УстановитьАтрибут(Шаблон_АдрРФ, "КодРегион", АдресСтруктурой.КодРегиона);
			УстановитьНеобязательныйАтрибут(Шаблон_АдрРФ, "Район", АдресСтруктурой.Район);
			УстановитьНеобязательныйАтрибут(Шаблон_АдрРФ, "Город", АдресСтруктурой.Город);
			УстановитьНеобязательныйАтрибут(Шаблон_АдрРФ, "НаселПункт", АдресСтруктурой.НаселенныйПункт);
			УстановитьНеобязательныйАтрибут(Шаблон_АдрРФ, "Улица", АдресСтруктурой.Улица);
			УстановитьНеобязательныйАтрибут(Шаблон_АдрРФ, "Дом", АдресСтруктурой.Дом);
			УстановитьНеобязательныйАтрибут(Шаблон_АдрРФ, "Корпус", АдресСтруктурой.Корпус);
			УстановитьНеобязательныйАтрибут(Шаблон_АдрРФ, "Кварт", АдресСтруктурой.Квартира);
			
			ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_АдрРФ);
			
			УменьшитьОтступ(Контекст);
			
			ТекстСделки.ДобавитьСтроку(Контекст.Отступ + "</СвАдрРФ>");
			
			Если ФизЛицо.ГражданствоФизЛицСтрана <> Справочники.СтраныМира.Россия Тогда
				НаименованиеСтраны = ФизЛицо.КонтактнаяИнформацияЗаРФСтрана;
				Если ЗначениеЗаполнено(НаименованиеСтраны) Тогда
					НайденнаяСтрана = Справочники.СтраныМира.НайтиПоНаименованию(НаименованиеСтраны);
					Если ЗначениеЗаполнено(НайденнаяСтрана) И НайденнаяСтрана <> Справочники.СтраныМира.Россия Тогда
						Шаблон_АдрИн = "<АдрИн %ОКСМ% %АдрИнТекст%/>";
						УстановитьАтрибут(Шаблон_АдрИн, "ОКСМ", НайденнаяСтрана.Код);
						УстановитьАтрибут(Шаблон_АдрИн, "АдрИнТекст", ФизЛицо.КонтактнаяИнформацияЗаРФПредставление);
						
						ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_АдрИн);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			УменьшитьОтступ(Контекст);
			
			ТекстСделки.ДобавитьСтроку(Контекст.Отступ + "</СвФЛ>");
		КонецЕсли;
		
		УменьшитьОтступ(Контекст);
		
		ТекстСделки.ДобавитьСтроку(Контекст.Отступ + "</СвФЛУчаст>");
		
	КонецЦикла;
	
КонецПроцедуры

// Заполняет сведения о физических лицах, являющихся участниками контролируемой сделоки.
// Поддерживаемые форматы уведомелния о контролируемых сделках: 5.03, 5.04, 5.05.
//
// Параметры:
//  Контекст       - Структура - параметры контекста xml файла.
//  ТекстСделки    - ТекстовыйДокумент - описание сделки.
//  Листы3         - ТаблицаЗначений - описание параметров листов.
//  ДанныеРаздела3 - ТаблицаЗначений - описание параметров раздела 3 уведомления.
//  Сделка         - ДокументСсылка.КонтролируемаяСделка - документ, описывающий структуру Листа 1А.
//
Процедура ЗаполнитьСведенияОбУчастникахФизЛицах503_504_505(Контекст, ТекстСделки, Листы3, ДанныеРаздела3, Сделка)
	
	СписокЛистов3 = Листы3.НайтиСтроки(Новый Структура("Сделка", Сделка));
	
	Для Каждого Лист3 Из СписокЛистов3 Цикл
		
		Физлицо = ДанныеРаздела3.Найти(Лист3.Контрагент, "Контрагент");
		
		Шаблон_СвФЛУчаст = "<СвФЛУчаст %НомПорСд% %СведОтнАгент% %КодВД%>";
		УстановитьАтрибут(Шаблон_СвФЛУчаст, "НомПорСд", Физлицо.НомерКонтрагента);
		УстановитьАтрибут(Шаблон_СвФЛУчаст, "СведОтнАгент", Лист3.СведОтносительноАгента);
		УстановитьАтрибут(Шаблон_СвФЛУчаст, "КодВД", ФизЛицо.Строка020КодВидаДеятельности);
		
		ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_СвФЛУчаст);
		
		УвеличитьОтступ(Контекст);
		
		Шаблон_ФИО = "<ФИО %Фамилия% %Имя% %Отчество%/>";
		УстановитьАтрибут(Шаблон_ФИО, "Фамилия", ФизЛицо.Фамилия);
		УстановитьАтрибут(Шаблон_ФИО, "Имя", ФизЛицо.Имя);
		УстановитьНеобязательныйАтрибут(Шаблон_ФИО, "Отчество", ФизЛицо.Отчество);
		
		ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_ФИО);
		
		Если ЗначениеЗаполнено(ФизЛицо.Строка030ИНН) Тогда
			Шаблон_ИННФЛ = СтрЗаменить("<ИННФЛ>%ИННФЛ%</ИННФЛ>", "%ИННФЛ%", ФизЛицо.Строка030ИНН);
			
			ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_ИННФЛ);
		Иначе
			Шаблон_СвФЛ = "<СвФЛ %ДатаРожд% %МестоРожд% %Гражд% %ОКСМ%>";
			УстановитьАтрибут(Шаблон_СвФЛ, "ДатаРожд", ФизЛицо.ФизическоеЛицо.ДатаРождения);
			УстановитьАтрибут(Шаблон_СвФЛ, "МестоРожд", ФизЛицо.ФизическоеЛицо.МестоРождения);
			СведенияОГражданстве = Документы.УведомлениеОКонтролируемыхСделках.СведенияОГражданстве2018(ФизЛицо.ГражданствоФизЛицСтрана);
			УстановитьАтрибут(Шаблон_СвФЛ, "Гражд", СведенияОГражданстве.Гражд);
			УстановитьНеобязательныйАтрибут(Шаблон_СвФЛ, "ОКСМ", СведенияОГражданстве.ОКСМ);
			
			ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_СвФЛ);
			
			УвеличитьОтступ(Контекст);
			
			Шаблон_УдЛичнФЛ = "<УдЛичнФЛ %КодВидДок% %СерНомДок% %ДатаДок% %ВыдДок%/>";
			УстановитьАтрибут(Шаблон_УдЛичнФЛ, "КодВидДок", КонтролируемыеСделки.ПолучитьКодВидаДокументаПоВидуДокумента(ФизЛицо.ВидДокумента));
			УстановитьАтрибут(Шаблон_УдЛичнФЛ, "СерНомДок", Строка(ФизЛицо.ДокументСерия) + " " + ФизЛицо.ДокументНомер);
			УстановитьАтрибут(Шаблон_УдЛичнФЛ, "ДатаДок", ФизЛицо.ДокументДатаВыдачи);
			УстановитьАтрибут(Шаблон_УдЛичнФЛ, "ВыдДок", ФизЛицо.ДокументКемВыдан);
			
			ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_УдЛичнФЛ);
			
			Если ФизЛицо.ГражданствоФизЛицСтрана <> Справочники.СтраныМира.Россия Тогда
				НаименованиеСтраны = ФизЛицо.КонтактнаяИнформацияЗаРФСтрана;
				Если ЗначениеЗаполнено(НаименованиеСтраны) Тогда
					НайденнаяСтрана = Справочники.СтраныМира.НайтиПоНаименованию(НаименованиеСтраны);
					Если ЗначениеЗаполнено(НайденнаяСтрана) И НайденнаяСтрана <> Справочники.СтраныМира.Россия Тогда
						Шаблон_АдрИн = "<АдрИн %ОКСМ% %АдрИнТекст%/>";
						УстановитьАтрибут(Шаблон_АдрИн, "ОКСМ", НайденнаяСтрана.Код);
						УстановитьАтрибут(Шаблон_АдрИн, "АдрИнТекст", ФизЛицо.КонтактнаяИнформацияЗаРФПредставление);
						
						ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_АдрИн);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			УменьшитьОтступ(Контекст);
			
			ТекстСделки.ДобавитьСтроку(Контекст.Отступ + "</СвФЛ>");
		КонецЕсли;
		
		УменьшитьОтступ(Контекст);
		
		ТекстСделки.ДобавитьСтроку(Контекст.Отступ + "</СвФЛУчаст>");
		
	КонецЦикла;
	
КонецПроцедуры

// Заполняет сведения об участниках последующей реализации в цепочке сделок.
//
// Параметры:
//  Контекст       - Структура - параметры контекста xml файла.
//  ТекстСделки    - ТекстовыйДокумент - описание сделки.
//  Листы4         - ТаблицаЗначений - описание параметров листов.
//  ДанныеРаздела4 - ТаблицаЗначений - описание параметров раздела 4 уведомления.
//  Сделка         - ДокументСсылка.КонтролируемаяСделка - документ, описывающий структуру Листа 1А.
//
Процедура ЗаполнитьСведенияОбУчастникахПоследующейРеализации505(Контекст, ТекстСделки, Листы4, ДанныеРаздела4, Сделка)
	
	СписокЛистов4 = Листы4.НайтиСтроки(Новый Структура("Сделка", Сделка));
	
	Для Каждого Лист4 Из СписокЛистов4 Цикл
		
		ДанныеЛиста4 = ДанныеРаздела4.Найти(Лист4.Контрагент, "Контрагент");
		
		Шаблон_СвУчПослРеал = "<СвУчПослРеал %НомУчСдЛиста%>";
		УстановитьАтрибут(Шаблон_СвУчПослРеал, "НомУчСдЛиста", XMLСтрока(ДанныеЛиста4.НомерКонтрагента));
		
		ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_СвУчПослРеал);
		
		УвеличитьОтступ(Контекст);
		
		Шаблон_СвУчЦеп = "<СвУчЦеп %СтатусУч% %ДанУч% %ОКСМ% %РегИдНомер%/>";
		
		УстановитьАтрибут(Шаблон_СвУчЦеп, "СтатусУч", ДанныеЛиста4.ТипОрганизации);
		УстановитьАтрибут(Шаблон_СвУчЦеп, "ДанУч", ДанныеЛиста4.НаименованиеКонтрагента);
		УстановитьАтрибут(Шаблон_СвУчЦеп, "ОКСМ", ДанныеЛиста4.КодСтраныРегистрации);
		УстановитьАтрибут(Шаблон_СвУчЦеп, "РегИдНомер", ДанныеЛиста4.РегистрационныйНомер);
		
		ТекстСделки.ДобавитьСтроку(Контекст.Отступ + Шаблон_СвУчЦеп);
		
		УменьшитьОтступ(Контекст);
		
		ТекстСделки.ДобавитьСтроку(Контекст.Отступ + "</СвУчПослРеал>");
		
	КонецЦикла;
	
КонецПроцедуры

// Проверяет заполнены ли реквизиты в структуре данных.
//
// Параметры:
//  Данные - Структура - описание структуры данных выгрузки в xml.
//  СписокИменРеквизитов - Строка - список реквизитов через запятую.
//
// Возвращаемое значение:
//  Булево - Истина, если реквизиты в структуре данных заполнены.
//
Функция ЕстьЗаполненныеРеквизиты(Данные, СписокИменРеквизитов)
	
	Результат = Ложь;
	
	ИменаРеквизитов = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СписокИменРеквизитов);
	
	Для Каждого ИмяРеквизита Из ИменаРеквизитов Цикл
		ИмяРеквизита = СокрЛП(ИмяРеквизита);
		
		ПроверяемоеЗначение = Неопределено;
		Если ТипЗнч(Данные) = Тип("Структура") Тогда
			Данные.Свойство(ИмяРеквизита, ПроверяемоеЗначение);
		ИначеЕсли ТипЗнч(Данные) = Тип("СтрокаТаблицыЗначений") Тогда
			ПроверяемоеЗначение = Данные[ИмяРеквизита];
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПроверяемоеЗначение) Тогда
			Результат = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Возвращает дерево значений структуры XML.
//
// Возвращаемое значение:
//  ДеревоЗначений - дерево значений структуры XML.
//
Функция ИзвлечьСтруктуруXML()
	
	ДеревоСтруктуры = Новый ДеревоЗначений;
	ДеревоСтруктуры.Колонки.Добавить("Код");
	ДеревоСтруктуры.Колонки.Добавить("ЗначениеПоУмолчанию");
	ДеревоСтруктуры.Колонки.Добавить("Значение");
	ДеревоСтруктуры.Колонки.Добавить("Представление");
	ДеревоСтруктуры.Колонки.Добавить("Тип");
	ДеревоСтруктуры.Колонки.Добавить("Формат");
	ДеревоСтруктуры.Колонки.Добавить("МинРазмерность");
	ДеревоСтруктуры.Колонки.Добавить("МаксРазмерность");
	ДеревоСтруктуры.Колонки.Добавить("Обязательность");
	ДеревоСтруктуры.Колонки.Добавить("Многостраничность");
	ДеревоСтруктуры.Колонки.Добавить("Многострочность");
	ДеревоСтруктуры.Колонки.Добавить("Раздел");
	ДеревоСтруктуры.Колонки.Добавить("Ключ");
	ДеревоСтруктуры.Колонки.Добавить("Условие");
	ДеревоСтруктуры.Колонки.Добавить("Показатели");
	
	Макет = ПолучитьМакет("СтруктураXML501");
	ВысотаТаблицы = Макет.ВысотаТаблицы;
	
	УчтенныеГруппы = Новый Соответствие;
	
	Для Уровень = 0 По Макет.КоличествоУровнейГруппировокСтрок() - 1 Цикл
		Макет.ПоказатьУровеньГруппировокСтрок(Уровень);
		Для НомерСтроки = 2 По ВысотаТаблицы Цикл
			НомСтр = ВысотаТаблицы - НомерСтроки + 2;
			Если Макет.Область(НомСтр, 0, НомСтр, 0).Видимость И УчтенныеГруппы.Получить(НомСтр) = Неопределено Тогда
				
				РодительскийУзел = ДеревоСтруктуры;
				Если Уровень <> 0 Тогда
					Для Инд = 1 По НомСтр - 2 Цикл
						Узел = УчтенныеГруппы.Получить(НомСтр - Инд);
						Если Узел <> Неопределено Тогда
							РодительскийУзел = Узел;
							Прервать;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
				
				НовСтр = РодительскийУзел.Строки.Вставить(0);
				НовСтр.Код = СокрЛП(Макет.Область(НомСтр, 1, НомСтр, 1).Текст);
				НовСтр.Раздел = СокрЛП(Макет.Область(НомСтр, 2, НомСтр, 2).Текст);
				НовСтр.Ключ = СокрЛП(Макет.Область(НомСтр, 3, НомСтр, 3).Текст);
				НовСтр.Тип = СокрЛП(Макет.Область(НомСтр, 4, НомСтр, 4).Текст);
				НовСтр.Формат = СокрЛП(Макет.Область(НомСтр, 5, НомСтр, 5).Текст);
				МинРазмерность = СокрЛП(Макет.Область(НомСтр, 6, НомСтр, 6).Текст);
				НовСтр.МинРазмерность = ?(ПустаяСтрока(МинРазмерность), ?(НовСтр.Формат = "N", 99999, 0), Число(МинРазмерность));
				МаксРазмерность = СокрЛП(Макет.Область(НомСтр, 7, НомСтр, 7).Текст);
				НовСтр.МаксРазмерность = ?(ПустаяСтрока(МаксРазмерность), 99999, Число(МаксРазмерность));
				НовСтр.Обязательность = СокрЛП(Макет.Область(НомСтр, 8, НомСтр, 8).Текст);
				НовСтр.Многостраничность = НЕ ПустаяСтрока(Макет.Область(НомСтр, 9, НомСтр, 9).Текст);
				НовСтр.Многострочность = НЕ ПустаяСтрока(Макет.Область(НомСтр, 10, НомСтр, 10).Текст);
				НовСтр.Условие = СокрЛП(Макет.Область(НомСтр, 11, НомСтр, 11).Текст);
				НовСтр.ЗначениеПоУмолчанию = СокрЛП(Макет.Область(НомСтр, 12, НомСтр, 12).Текст);
				НовСтр.Представление = СокрЛП(Макет.Область(НомСтр, 13, НомСтр, 13).Текст);
				
				УчтенныеГруппы.Вставить(НомСтр, НовСтр);
				
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат ДеревоСтруктуры;
	
КонецФункции

// Заполняет дерево данными параметров структуры.
//
// Параметры:
//  ДеревоВыгрузки - ДеревоЗначений - описание структуры данных выгрузки в xml.
//  Параметры      - Структура - параметры данных файла.
//
Процедура ЗаполнитьДанными(ДеревоВыгрузки, Параметры)
	
	ОбработатьУсловныеЭлементы(Параметры, ДеревоВыгрузки);
	
	ЗаполнитьПараметры(Параметры, ДеревоВыгрузки);
	
	ЗаполнитьДаннымиПоКонтролируемымСделкам(Параметры, ДеревоВыгрузки);
	
	ОтсечьНезаполненныеНеобязательныеУзлы(ДеревоВыгрузки);
	
КонецПроцедуры

// Обрабатывает условные элементы дерева данными параметрами структуры.
//
// Параметры:
//  Параметры - Структура - параметры данных файла.
//  Узел      - ДеревоЗначений - описание структуры данных выгрузки в xml.
//
Процедура ОбработатьУсловныеЭлементы(Знач Параметры, Узел)
	
	КоличествоСтрок = Узел.Строки.Количество();
	Для Инд = 1 По КоличествоСтрок Цикл
		ТекСтр = Узел.Строки.Получить(КоличествоСтрок - Инд);
		Если НЕ ПустаяСтрока(ТекСтр.Условие) Тогда
			Если НЕ УсловиеВыполнено(Параметры, ТекСтр.Условие) Тогда
				Узел.Строки.Удалить(ТекСтр);
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		ОбработатьУсловныеЭлементы(Параметры, ТекСтр);
	КонецЦикла;
	
КонецПроцедуры

// Проверяет выполнено ли условие для параметра.
//
// Параметры:
//  Параметры - Структура - параметры данных файла.
//  Условие   - ДеревоЗначений - описание структуры данных выгрузки в xml.
//
// Возвращаемое значение:
//  Булево     - Истина, если условие для параметра выполнено.
//
Функция УсловиеВыполнено(Параметры, Условие)
	
	Попытка
		РезультатВычисленияВыражения = ОбщегоНазначения.ВычислитьВБезопасномРежиме(СтрЗаменить(Условие, "&", "Параметры."), Параметры);
		Возврат НЕ (РезультатВычисленияВыражения = Ложь);
	Исключение
		Возврат Истина;
	КонецПопытки;
	
КонецФункции

// Заполняет строки дерева значений параметрами структуры.
//
// Параметры:
//  Параметры - Структура - параметры данных файла.
//  Узел      - ДеревоЗначений - описание структуры данных выгрузки в xml.
//
Процедура ЗаполнитьПараметры(Параметры, Узел)
	
	Для Каждого Стр Из Узел.Строки Цикл
		Если Стр.Тип = "С" ИЛИ Стр.Тип = "C" Тогда // учтем оба варианта: кириллицу и латиницу
			ЗаполнитьПараметры(Параметры, Стр);
		Иначе
			Если ПустаяСтрока(Стр.ЗначениеПоУмолчанию) Тогда
				Если НЕ ПустаяСтрока(Стр.Ключ) Тогда
					ВывестиПоказательВXML(Стр, Параметры[Стр.Ключ]);
				Иначе
					Стр.Значение = "";
				КонецЕсли;
			ИначеЕсли Лев(Стр.ЗначениеПоУмолчанию, 1) = "&" Тогда
				ИмяПараметра = Сред(Стр.ЗначениеПоУмолчанию, 2);
				Если Параметры.Свойство(ИмяПараметра) Тогда
					ВывестиПоказательВXML(Стр, Параметры[ИмяПараметра]);
				КонецЕсли;
			Иначе
				Стр.Значение = Стр.ЗначениеПоУмолчанию;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Заполняет строки дерева значений параметрами структуры.
//
// Параметры:
//  Параметры - Структура - параметры данных файла.
//  Узел      - ДеревоЗначений - описание структуры данных выгрузки в XML.
//  ЗначениеПоказателя - Строка - значение показателя для вывода в XML.
//
Процедура ВывестиПоказательВXML(Узел, ЗначениеПоказателя)
	
	МинШирина = Узел.МинРазмерность;
	МаксШирина = Узел.МаксРазмерность;
	
	Если Узел.Формат = "T" ИЛИ Узел.Формат = "Т" Тогда // учтем оба варианта: кириллицу и латиницу
		Узел.Значение = ?(МаксШирина < СтрДлина(СокрЛП(ЗначениеПоказателя)), СокрЛП(Лев(СокрЛП(ЗначениеПоказателя), МаксШирина)), СокрЛП(ЗначениеПоказателя));
	ИначеЕсли Узел.Формат = "N" Тогда
		СтрокаФормата = "ЧРД=.;ЧН=0;ЧГ=;";
		Если Узел.МаксРазмерность <> 0 И Узел.МаксРазмерность <> 99999 Тогда
			СтрокаФормата = СтрокаФормата + "ЧЦ=" + Формат(Узел.МаксРазмерность, "ЧГ=") + ";";
		КонецЕсли;
		Если Узел.МинРазмерность <> 99999 Тогда
			СтрокаФормата = СтрокаФормата + "ЧДЦ=" + Формат(Узел.МинРазмерность, "ЧГ=") + ";";
		КонецЕсли;
		Узел.Значение = СокрЛП(Формат(ЗначениеПоказателя, СтрокаФормата));
	ИначеЕсли Узел.Формат = "gYear" Тогда
		Если ТипЗнч(ЗначениеПоказателя) = Тип("Дата") Тогда
			Узел.Значение = СокрЛП(Формат(ЗначениеПоказателя, "ДФ=гггг"));
		Иначе
			Узел.Значение = Прав(СокрЛП(ЗначениеПоказателя), 4);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Заполняет данными по контролируемым сделкам строки дерева значений.
//
// Параметры:
//  Параметры      - Структура - параметры данных файла.
//  ДеревоВыгрузки - ДеревоЗначений - описание структуры данных выгрузки в XML.
//
Процедура ЗаполнитьДаннымиПоКонтролируемымСделкам(Параметры, ДеревоВыгрузки)
	
	ДатаАктуальностиСведений = КонецГода(ОтчетныйГод);
	
	ЛистыУведомления = КонтролируемыеСделки.ПолучитьЛистыУведомления(Ссылка);
	
	Листы1А = ЛистыУведомления.ЛистыРаздела1А;
	Листы1Б = ЛистыУведомления.ЛистыРаздела1Б;
	ЛистыРаздела2 = ЛистыУведомления.ЛистыРаздела2;
	ЛистыРаздела3 = ЛистыУведомления.ЛистыРаздела3;
	ДанныеРаздела2 = ЛистыУведомления.ДанныеРаздела2;
	ДанныеРаздела3 = ЛистыУведомления.ДанныеРаздела3;
	
	Узел_Документ = ПолучитьПодчиненныйЭлемент(ДеревоВыгрузки, "Документ");
	Узел_УвКонтрСд = ПолучитьПодчиненныйЭлемент(Узел_Документ, "УвКонтрСд");
	
	ПрототипУзла_СвКонтрСд = ПолучитьПодчиненныйЭлемент(Узел_УвКонтрСд, "СвКонтрСд");
	Для Каждого Лист1А Из Листы1А Цикл
		Узел_СвКонтрСд = НовыйУзелИзПрототипа(ПрототипУзла_СвКонтрСд);
		
		УстановитьЗначениеЭлемента(Узел_СвКонтрСд, "НомПорСд", Лист1А.НомерЛиста1А);
		
		Узел_ОснКонтрСд = ПолучитьПодчиненныйЭлемент(Узел_СвКонтрСд, "ОснКонтрСд");
		ЗаполнитьСведенияОснованияКонтроляСделки(Узел_ОснКонтрСд, Лист1А);
		
		Узел_КонтрСд = ПолучитьПодчиненныйЭлемент(Узел_СвКонтрСд, "КонтрСд");
		ЗаполнитьСведенияОСделке(Узел_КонтрСд, Лист1А);
		
		Узел_ДохРасхСд = ПолучитьПодчиненныйЭлемент(Узел_СвКонтрСд, "ДохРасхСд");
		ЗаполнитьСведенияОДоходахРасходах(Узел_ДохРасхСд, Лист1А);
		
		ПрототипУзла_СвПредмСд = ПолучитьПодчиненныйЭлемент(Узел_СвКонтрСд, "СвПредмСд");
		ЗаполнитьСведенияОПредметеСделки(Параметры, ПрототипУзла_СвПредмСд, Листы1Б, Лист1А.Сделка);
		УдалитьУзел(ПрототипУзла_СвПредмСд);
		
		ПрототипУзла_СвОргУчаст = ПолучитьПодчиненныйЭлемент(Узел_СвКонтрСд, "СвОргУчаст");
		ЗаполнитьСведенияОбУчастникахОрганизациях(ПрототипУзла_СвОргУчаст, ЛистыРаздела2, ДанныеРаздела2, Лист1А.Сделка);
		УдалитьУзел(ПрототипУзла_СвОргУчаст);
		
		ПрототипУзла_СвФЛУчаст = ПолучитьПодчиненныйЭлемент(Узел_СвКонтрСд, "СвФЛУчаст");
		ЗаполнитьСведенияОбУчастникахФизЛицах(ПрототипУзла_СвФЛУчаст, ЛистыРаздела3, ДанныеРаздела3, Лист1А.Сделка);
		УдалитьУзел(ПрототипУзла_СвФЛУчаст);
		
	КонецЦикла;
	УдалитьУзел(ПрототипУзла_СвКонтрСд);
	
КонецПроцедуры

// Заполняяет сведения основания контроля сделки для узла выгрузки уведомления в XML.
//
// Параметры:
//  УзелВыгрузки - СтрокаДереваЗначений - узел дерева значений для выгрузки в XML.
//  Сведения - СтрокаТаблицыЗначений - сведения о сделке для выгрузки уведомления в XML.
//
Процедура ЗаполнитьСведенияОснованияКонтроляСделки(УзелВыгрузки, Сведения)
	
	УстановитьЗначениеЭлемента(УзелВыгрузки, "ВзЗавис", Сведения.Строка100Взаимозависимость);
	
	Узел_Осн105_14 = ПолучитьПодчиненныйЭлемент(УзелВыгрузки, "Осн105.14");
	УстановитьЗначениеЭлемента(Узел_Осн105_14, "Осн121", Сведения.Строка121СтороныВзаимозависимыПоКодексу);
	УстановитьЗначениеЭлемента(Узел_Осн105_14, "Осн122", Сведения.Строка122СделкаВОбластиВнешнейТорговли);
	УстановитьЗначениеЭлемента(Узел_Осн105_14, "Осн123", Сведения.Строка123СделкаСКонтрагентомСЛьготнымНалогообложением);
	УстановитьЗначениеЭлемента(Узел_Осн105_14, "Осн124", Сведения.Строка124СделкаСНезависимымПосредником);
	
	Узел_ОснРФ105_14 = ПолучитьПодчиненныйЭлемент(УзелВыгрузки, "ОснРФ105.14");
	УстановитьЗначениеЭлемента(Узел_ОснРФ105_14, "Осн131", Сведения.ОснованиеКонтролируемости131);
	УстановитьЗначениеЭлемента(Узел_ОснРФ105_14, "Осн132", Сведения.ОснованиеКонтролируемости132);
	УстановитьЗначениеЭлемента(Узел_ОснРФ105_14, "Осн133", Сведения.ОснованиеКонтролируемости133);
	УстановитьЗначениеЭлемента(Узел_ОснРФ105_14, "Осн134", Сведения.ОснованиеКонтролируемости134);
	УстановитьЗначениеЭлемента(Узел_ОснРФ105_14, "Осн135", Сведения.ОснованиеКонтролируемости135);
	
КонецПроцедуры

// Заполняяет сведения о сделке для узла выгрузки уведомления в XML.
//
// Параметры:
//  УзелВыгрузки - СтрокаДереваЗначений - узел дерева значений для выгрузки в XML.
//  Сведения - СтрокаТаблицыЗначений - сведения о сделке для выгрузки уведомления в XML.
//
Процедура ЗаполнитьСведенияОСделке(УзелВыгрузки, Сведения)
	
	ЭтоГруппаОднородныхСделок = Ложь;
	
	УстановитьЗначениеЭлемента(УзелВыгрузки, "ГрупОС",       ?(ЭтоГруппаОднородныхСделок, "1", "0"));
	УстановитьЗначениеЭлемента(УзелВыгрузки, "КодНаимСд",    Сведения.Строка210КодНаименованияСделки);
	УстановитьЗначениеЭлемента(УзелВыгрузки, "КодСторСд",    Сведения.Строка211КодСтороныСделки);
	УстановитьЗначениеЭлемента(УзелВыгрузки, "ПрОпрЦен",     Сведения.Строка220ПризнакОпределенияЦеныСделки);
	УстановитьЗначениеЭлемента(УзелВыгрузки, "КомПрОпрЦен",  Сведения.Строка220_1Комментарий);
	УстановитьЗначениеЭлемента(УзелВыгрузки, "КодОпрЦен",    Сведения.Строка230КодОпределенияЦены);
	УстановитьЗначениеЭлемента(УзелВыгрузки, "КомКодОпрЦен", Сведения.Строка230_1Комментарий);
	УстановитьЗначениеЭлемента(УзелВыгрузки, "КодМетЦен",    Сведения.Строка240КодМетодовЦенообразования);
	УстановитьЗначениеЭлемента(УзелВыгрузки, "КомКодМетЦен", Сведения.Строка240_1Комментарий);
	УстановитьЗначениеЭлемента(УзелВыгрузки, "КолУчСд",      Сведения.Строка260КоличествоУчастниковСделки);
	УстановитьЗначениеЭлемента(УзелВыгрузки, "КомКолУчСд",   Сведения.Строка260_1Комментарий);
	
	Узел_КодИстИнф = ПолучитьПодчиненныйЭлемент(УзелВыгрузки, "КодИстИнф");
	УстановитьЗначениеЭлемента(Узел_КодИстИнф, "Ист251", Сведения.Строка251);
	УстановитьЗначениеЭлемента(Узел_КодИстИнф, "Ист252", Сведения.Строка252);
	УстановитьЗначениеЭлемента(Узел_КодИстИнф, "Ист253", Сведения.Строка253);
	УстановитьЗначениеЭлемента(Узел_КодИстИнф, "Ист254", Сведения.Строка254);
	УстановитьЗначениеЭлемента(Узел_КодИстИнф, "Ист255", Сведения.Строка255);
	УстановитьЗначениеЭлемента(Узел_КодИстИнф, "Ист256", Сведения.Строка256);
	УстановитьЗначениеЭлемента(Узел_КодИстИнф, "Ист257", Сведения.Строка257);
	УстановитьЗначениеЭлемента(Узел_КодИстИнф, "Ист258", Сведения.Строка258);
	УстановитьЗначениеЭлемента(Узел_КодИстИнф, "Ист259", Сведения.Строка259);
	
КонецПроцедуры

// Заполняяет сведения о доходах и расходах для узла выгрузки уведомления в XML.
//
// Параметры:
//  УзелВыгрузки - СтрокаДереваЗначений - узел дерева значений для выгрузки в XML.
//  Сведения - СтрокаТаблицыЗначений - сведения о доходах и расходах для выгрузки уведомления в XML.
//
Процедура ЗаполнитьСведенияОДоходахРасходах(УзелВыгрузки, Сведения)
	
	УстановитьЗначениеЭлемента(УзелВыгрузки, "СумДохСд",     Сведения.Строка300СуммаДоходов);
	УстановитьЗначениеЭлемента(УзелВыгрузки, "СумДохСдРег",  Сведения.Строка301СуммаРегулируемыхДоходов);
	УстановитьЗначениеЭлемента(УзелВыгрузки, "СумРасхСд",    Сведения.Строка310СуммаРасходов);
	УстановитьЗначениеЭлемента(УзелВыгрузки, "СумРасхСдРег", Сведения.Строка311СуммаРегулируемыхРасходов);
	
КонецПроцедуры

// Заполняяет сведения о предмете сделки для узла выгрузки уведомления в XML.
//
// Параметры:
//  Параметры            - Структура - параметры данных файла.
//  ПрототипУзлаВыгрузки - СтрокаДереваЗначений - прототип узла для выгрузки уведомления в XML.
//  Листы1Б              - ТаблицаЗначений - описание листов 1Б уведомления.
//  Сделка               - ДокументСсылка.КонтролируемаяСделка - контролируемая сделка.
//
Процедура ЗаполнитьСведенияОПредметеСделки(Параметры, ПрототипУзлаВыгрузки, Листы1Б, Сделка)
	
	ТипыПредметовСделок = Новый Соответствие;
	
	СписокЛистов1Б = Листы1Б.НайтиСтроки(Новый Структура("Сделка", Сделка));
	Для Каждого Лист1Б Из СписокЛистов1Б Цикл
		ЛистыТипаПредмета = ТипыПредметовСделок.Получить(Лист1Б.Строка020ТипПредмета);
		Если ЛистыТипаПредмета = Неопределено Тогда
			ТипыПредметовСделок.Вставить(Лист1Б.Строка020ТипПредмета, Новый Массив());
			ЛистыТипаПредмета = ТипыПредметовСделок.Получить(Лист1Б.Строка020ТипПредмета);
		КонецЕсли;
		ЛистыТипаПредмета.Добавить(Лист1Б);
	КонецЦикла;
	
	Для Каждого ТипПредмета Из ТипыПредметовСделок Цикл
		УзелВыгрузки = НовыйУзелИзПрототипа(ПрототипУзлаВыгрузки);
		
		ТипПредСд = ТипПредмета.Ключ;
		УстановитьЗначениеЭлемента(УзелВыгрузки, "ТипПредСд", ТипПредСд);
		
		ПрототипУзла_ПерПредСд = ПолучитьПодчиненныйЭлемент(УзелВыгрузки, "ПерПредСд");
		Для Каждого Лист1Б Из ТипПредмета.Значение Цикл
			
			Узел_ПерПредСд = НовыйУзелИзПрототипа(ПрототипУзла_ПерПредСд);
			
			Если Параметры.ВерсияУведомления = КонтролируемыеСделкиКлиентСервер.ВерсияУведомления_2012() Тогда
				КодПоОКП = Лист1Б.Строка043КодПоОКП;
				КодПоОКВЭД = Лист1Б.Строка045КодОКВЭД;
			Иначе
				КодПоОКП = КонтролируемыеСделки.КодПоОКПИзКодаПоОКПД2(Лист1Б.Строка043КодПоОКПД2);
				КодПоОКВЭД = Лист1Б.Строка045КодОКВЭД2;
			КонецЕсли;
			
			ЗаполненОКП = ЗначениеЗаполнено(КодПоОКП);
			ЗаполненТНВЭД = ЗначениеЗаполнено(Лист1Б.Строка040КодПоТНВЭД);
			
			Если ТипПредСд = 1 Тогда
				УстановитьЗначениеЭлемента(Узел_ПерПредСд, "ТНВЭД",        Лист1Б.Строка040КодПоТНВЭД);
				УстановитьЗначениеЭлемента(Узел_ПерПредСд, "ОКП",          КодПоОКП);
				УстановитьЗначениеЭлемента(Узел_ПерПредСд, "КодУсловПост", Лист1Б.Строка100КодУсловийПоставки);
			Иначе // значения 2 и 3
				УстановитьЗначениеЭлемента(Узел_ПерПредСд, "ОКВЭД",        КодПоОКВЭД);
			КонецЕсли;
			
			УстановитьЗначениеЭлемента(Узел_ПерПредСд, "НаимПредСд",   Лист1Б.Строка030НаименованиеПредмета);
			УстановитьЗначениеЭлемента(Узел_ПерПредСд, "НомУчСд",      Лист1Б.Строка050НомерУчастникаСделки);
			УстановитьЗначениеЭлемента(Узел_ПерПредСд, "НомДог",       Лист1Б.Строка060НомерДоговора);
			УстановитьЗначениеЭлемента(Узел_ПерПредСд, "ДатаДог",      Лист1Б.Строка065ДатаДоговора);
			УстановитьЗначениеЭлемента(Узел_ПерПредСд, "ОКСМ",         Лист1Б.Строка070КодСтраныПроисхождения);
			УстановитьЗначениеЭлемента(Узел_ПерПредСд, "ОКЕИ",         СокрЛП(Лист1Б.Строка110КодЕдиницыИзмерения));
			УстановитьЗначениеЭлемента(Узел_ПерПредСд, "Количество",   Лист1Б.Строка120Количество);
			УстановитьЗначениеЭлемента(Узел_ПерПредСд, "ЦенаЕдин",     Лист1Б.Строка130Цена);
			УстановитьЗначениеЭлемента(Узел_ПерПредСд, "СтоимИтог",    Лист1Б.Строка140Стоимость);
			УстановитьЗначениеЭлемента(Узел_ПерПредСд, "ДатаСовСд",    Лист1Б.Строка150ДатаСовершения);
			
			Узел_МестОтпрТов = ПолучитьПодчиненныйЭлемент(Узел_ПерПредСд, "МестОтпрТов");
			
			УстановитьЗначениеЭлемента(Узел_МестОтпрТов, "ОКСМ",       Лист1Б.Строка080КодСтраныОтправки);
			УстановитьЗначениеЭлемента(Узел_МестОтпрТов, "КодРегион",  Лист1Б.Строка080КодРегионаОтправки);
			УстановитьЗначениеЭлемента(Узел_МестОтпрТов, "Город",      Лист1Б.Строка080ГородОтправки);
			УстановитьЗначениеЭлемента(Узел_МестОтпрТов, "НаселПункт", Лист1Б.Строка080НаселенныйПунктОтправки);
			
			Узел_МестСовСд = ПолучитьПодчиненныйЭлемент(Узел_ПерПредСд, "МестСовСд");
			
			УстановитьЗначениеЭлемента(Узел_МестСовСд, "ОКСМ",       Лист1Б.Строка090КодСтраныСовершенияСделки);
			УстановитьЗначениеЭлемента(Узел_МестСовСд, "КодРегион",  Лист1Б.Строка090КодРегионаСовершенияСделки);
			УстановитьЗначениеЭлемента(Узел_МестСовСд, "Город",      Лист1Б.Строка090ГородСовершенияСделки);
			УстановитьЗначениеЭлемента(Узел_МестСовСд, "НаселПункт", Лист1Б.Строка090НаселенныйПунктСовершенияСделки);
			
		КонецЦикла;
		УдалитьУзел(ПрототипУзла_ПерПредСд);
		
	КонецЦикла;
	
КонецПроцедуры

// Заполняяет сведения о юридических лицах, являющихся участниками контролируемых сделок,
// для узла выгрузки уведомления в XML.
//
// Параметры:
//  ПрототипУзлаВыгрузки - СтрокаДереваЗначений - прототип узла для выгрузки уведомления в XML.
//  Листы2              - ТаблицаЗначений - описание листов раздела 2 уведомления.
//  ДанныеРаздела2      - ТаблицаЗначений - описание данных раздела 2 уведомления.
//  Сделка               - ДокументСсылка.КонтролируемаяСделка - контролируемая сделка.
//
Процедура ЗаполнитьСведенияОбУчастникахОрганизациях(ПрототипУзлаВыгрузки, Листы2, ДанныеРаздела2, Сделка)
	
	СписокЛистов2 = Листы2.НайтиСтроки(Новый Структура("Сделка", Сделка));
	
	Для Каждого Лист2 Из СписокЛистов2 Цикл
		
		ДанныеЛиста2 = ДанныеРаздела2.Найти(Лист2.Контрагент, "Контрагент");
		
		УзелВыгрузки = НовыйУзелИзПрототипа(ПрототипУзлаВыгрузки);
		
		УстановитьЗначениеЭлемента(УзелВыгрузки, "НомПорСд",   ДанныеЛиста2.НомерКонтрагента);
		УстановитьЗначениеЭлемента(УзелВыгрузки, "ПрОрг",      ДанныеЛиста2.Строка020ТипОрганизации);
		УстановитьЗначениеЭлемента(УзелВыгрузки, "ОКСМ",       ДанныеЛиста2.Строка030КакКодСтраныРегистрации);
		УстановитьЗначениеЭлемента(УзелВыгрузки, "НаимОрг",    ДанныеЛиста2.Строка040Наименование);
		УстановитьЗначениеЭлемента(УзелВыгрузки, "ИННЮЛ",      ДанныеЛиста2.Строка050ИНН);
		УстановитьЗначениеЭлемента(УзелВыгрузки, "КПП",        ДанныеЛиста2.Строка060КПП);
		УстановитьЗначениеЭлемента(УзелВыгрузки, "РегНомИн",   ДанныеЛиста2.Строка070РегНомерВСтрокеРегистрации);
		УстановитьЗначениеЭлемента(УзелВыгрузки, "КодНПРег",   ДанныеЛиста2.Строка080КодНалогВСтранеРегистрации);
		УстановитьЗначениеЭлемента(УзелВыгрузки, "АдрИнТекст", ?(ДанныеЛиста2.Строка020ТипОрганизации = 1, "", ДанныеЛиста2.Строка090АдресИностраннойОрганизации));
		
	КонецЦикла;
	
КонецПроцедуры

// Заполняяет сведения о физических лицах, являющихся участниками контролируемых сделок,
// для узла выгрузки уведомления в XML.
//
// Параметры:
//  ПрототипУзлаВыгрузки - СтрокаДереваЗначений - прототип узла для выгрузки уведомления в XML.
//  Листы3              - ТаблицаЗначений - описание листов раздела 3 уведомления.
//  ДанныеРаздела3      - ТаблицаЗначений - описание данных раздела 3 уведомления.
//  Сделка               - ДокументСсылка.КонтролируемаяСделка - контролируемая сделка.
//
Процедура ЗаполнитьСведенияОбУчастникахФизЛицах(ПрототипУзлаВыгрузки, Листы3, ДанныеРаздела3, Сделка)
	
	СписокЛистов3 = Листы3.НайтиСтроки(Новый Структура("Сделка", Сделка));
	
	Для Каждого Лист3 Из СписокЛистов3 Цикл
		
		Физлицо = ДанныеРаздела3.Найти(Лист3.Контрагент, "Контрагент");
		
		УзелВыгрузки = НовыйУзелИзПрототипа(ПрототипУзлаВыгрузки);
		
		УстановитьЗначениеЭлемента(УзелВыгрузки, "НомПорСд", Физлицо.НомерКонтрагента);
		УстановитьЗначениеЭлемента(УзелВыгрузки, "КодВД", ФизЛицо.Строка020КодВидаДеятельности);
		
		Узел_ФИО = ПолучитьПодчиненныйЭлемент(УзелВыгрузки, "ФИО");
		
		УстановитьЗначениеЭлемента(Узел_ФИО, "Фамилия", ФизЛицо.Фамилия);
		УстановитьЗначениеЭлемента(Узел_ФИО, "Имя", ФизЛицо.Имя);
		УстановитьЗначениеЭлемента(Узел_ФИО, "Отчество", ФизЛицо.Отчество);
		
		Узел_ИННФЛ = ПолучитьПодчиненныйЭлемент(УзелВыгрузки, "ИННФЛ");
		Узел_СвФЛ = ПолучитьПодчиненныйЭлемент(УзелВыгрузки, "СвФЛ");
		
		Если ПустаяСтрока(ФизЛицо.Строка030ИНН) Тогда
			УдалитьУзел(Узел_ИННФЛ);
			
			УстановитьЗначениеЭлемента(Узел_СвФЛ, "ДатаРожд", ФизЛицо.ФизическоеЛицо.ДатаРождения);
			УстановитьЗначениеЭлемента(Узел_СвФЛ, "МестоРожд", ФизЛицо.ФизическоеЛицо.МестоРождения);
			УстановитьЗначениеЭлемента(Узел_СвФЛ, "НалГражд", ?(ФизЛицо.ГражданствоФизЛицСтрана = Справочники.СтраныМира.Россия, "1", "2"));
			УстановитьЗначениеЭлемента(Узел_СвФЛ, "ОКСМ", ?(ЗначениеЗаполнено(ФизЛицо.ГражданствоФизЛицСтрана), ФизЛицо.ГражданствоФизЛицСтрана.Код, ""));
			
			Узел_УдЛичнФЛ = ПолучитьПодчиненныйЭлемент(Узел_СвФЛ, "УдЛичнФЛ");
			
			УстановитьЗначениеЭлемента(Узел_УдЛичнФЛ, "КодВидДок", КонтролируемыеСделкиПовтИсп.ПолучитьКодВидаДокументаПоВидуДокумента(ФизЛицо.ВидДокумента));
			УстановитьЗначениеЭлемента(Узел_УдЛичнФЛ, "СерНомДок", Строка(ФизЛицо.ДокументСерия) + " " + ФизЛицо.ДокументНомер);
			УстановитьЗначениеЭлемента(Узел_УдЛичнФЛ, "ДатаДок", ФизЛицо.ДокументДатаВыдачи);
			УстановитьЗначениеЭлемента(Узел_УдЛичнФЛ, "ВыдДок", ФизЛицо.ДокументКемВыдан);
			
			Узел_СвАдрРФ = ПолучитьПодчиненныйЭлемент(Узел_СвФЛ, "СвАдрРФ");
			
			Если ФизЛицо.ГражданствоФизЛицСтрана = Справочники.СтраныМира.ПустаяСсылка() Тогда
				УстановитьЗначениеЭлемента(Узел_СвАдрРФ, "ПрАдр", "1");
			Иначе
				УстановитьЗначениеЭлемента(Узел_СвАдрРФ, "ПрАдр", "2");
			КонецЕсли;
			
			ЗначениеАдреса = ?(ЗначениеЗаполнено(Физлицо.КонтактнаяИнформацияЗначениеJSON),
				Физлицо.КонтактнаяИнформацияЗначениеJSON,
				Физлицо.КонтактнаяИнформацияЗначенияПолей);
			
			АдресСтруктурой = КонтролируемыеСделки.СтруктураАдреса(ЗначениеАдреса);
			
			Узел_АдрРФ = ПолучитьПодчиненныйЭлемент(Узел_СвАдрРФ, "АдрРФ");
			
			ПоляАдреса = Новый Структура;
			ПоляАдреса.Вставить("Индекс", "Индекс");
			ПоляАдреса.Вставить("КодРегиона", "КодРегион");
			ПоляАдреса.Вставить("Район", "Район");
			ПоляАдреса.Вставить("Город", "Город");
			ПоляАдреса.Вставить("НаселенныйПункт", "НаселПункт");
			ПоляАдреса.Вставить("Улица", "Улица");
			ПоляАдреса.Вставить("Дом", "Дом");
			ПоляАдреса.Вставить("Корпус", "Корпус");
			ПоляАдреса.Вставить("Квартира", "Кварт");

			Для Каждого ПолеАдреса Из ПоляАдреса Цикл
				Если АдресСтруктурой.Свойство(ПолеАдреса.Ключ) Тогда
					УстановитьЗначениеЭлемента(Узел_АдрРФ, ПолеАдреса.Значение, АдресСтруктурой[ПолеАдреса.Ключ]);
				КонецЕсли;
			КонецЦикла;
			
			Узел_АдрИн = ПолучитьПодчиненныйЭлемент(Узел_СвФЛ, "АдрИн");
			
			Если ФизЛицо.ГражданствоФизЛицСтрана <> Справочники.СтраныМира.Россия Тогда
				НаименованиеСтраны = ФизЛицо.КонтактнаяИнформацияЗаРФСтрана;
				Если ЗначениеЗаполнено(НаименованиеСтраны) Тогда
					НайденнаяСтрана = Справочники.СтраныМира.НайтиПоНаименованию(НаименованиеСтраны);
					Если ЗначениеЗаполнено(НайденнаяСтрана) И НайденнаяСтрана <> Справочники.СтраныМира.Россия Тогда
						УстановитьЗначениеЭлемента(Узел_АдрИн, "ОКСМ", НайденнаяСтрана.Код);
						УстановитьЗначениеЭлемента(Узел_АдрИн, "АдрИнТекст", ФизЛицо.КонтактнаяИнформацияЗаРФПредставление);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
		Иначе
			УстановитьЗначениеЭлемента(УзелВыгрузки, "ИННФЛ", ФизЛицо.Строка030ИНН);
			УдалитьУзел(Узел_СвФЛ);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Возвращает подчиненный элемент узла дерева значений по коду элемента.
//
// Параметры:
//  Узел        - ДеревоЗначений - описание структуры данных выгрузки в xml.
//  КодЭлемента - Строка - название кода элемента строки дерева значений.
// Возвращаемое значение:
//  СтрокаДереваЗначений - подчиненный элемент узла.
//
Функция ПолучитьПодчиненныйЭлемент(Узел, КодЭлемента)
	
	Для Каждого Стр Из Узел.Строки Цикл
		Если Стр.Код = КодЭлемента Тогда
			Возврат Стр;
		КонецЕсли;
	КонецЦикла;
	
КонецФункции

// Возвращает новый узел дерева значений на основе переданного узла.
//
// Параметры:
//  ПрототипУзла - СтрокаДереваЗначений - узел, на основании которого будет создан новый узел.
// Возвращаемое значение:
//  СтрокаДереваЗначений - новый узел, созданный на основании прототипа.
//
Функция НовыйУзелИзПрототипа(ПрототипУзла);
	
	РодительУзла = ПрототипУзла.Родитель;
	
	ПозицияИсходногоУзла = РодительУзла.Строки.Индекс(ПрототипУзла);
	НовыйУзел = РодительУзла.Строки.Вставить(ПозицияИсходногоУзла);
	ЗаполнитьЗначенияСвойств(НовыйУзел, ПрототипУзла, , "Родитель, Строки");
	Для Каждого Стр Из ПрототипУзла.Строки Цикл
		СкопироватьУзел(НовыйУзел, Стр);
	КонецЦикла;
	
	Возврат НовыйУзел;
	
КонецФункции

// Копирует узел дерева значений.
//
// Параметры:
//  Родитель - ДеревоЗначений, СтрокаДереваЗначений - родитель копируемого узла.
//  Узел     - СтрокаДереваЗначений - название кода элемента строки дерева значений.
// Возвращаемое значение:
//  СтрокаДереваЗначений - скопированный узел дерева значений.
//
Функция СкопироватьУзел(Родитель, Узел)
	
	НовыйУзел = Родитель.Строки.Добавить();
	ЗаполнитьЗначенияСвойств(НовыйУзел, Узел, , "Родитель, Строки");
	Для Каждого Стр Из Узел.Строки Цикл
		СкопироватьУзел(НовыйУзел, Стр);
	КонецЦикла;
	Возврат НовыйУзел;
	
КонецФункции

// Возвращает подчиненный элемент узла дерева значений по коду элемента.
//
// Параметры:
//  УзелРодитель     - СтрокаДереваЗначений - описание структуры данных выгрузки в xml.
//  ИмяЭлемента      - Строка - название кода элемента строки дерева значений.
//  ЗначениеЭлемента - СтрокаТаблицыЗначений, Неопределено - значение элемента.
//
Процедура УстановитьЗначениеЭлемента(УзелРодитель, ИмяЭлемента, ЗначениеЭлемента)
	
	ПодчиненныйЭлемент = ПолучитьПодчиненныйЭлемент(УзелРодитель, ИмяЭлемента);
	ВывестиПоказательВXML(ПодчиненныйЭлемент, ЗначениеЭлемента);
	
КонецПроцедуры

// Удаляет узел дереза значений.
//
// Параметры:
//  Узел - СтрокаДереваЗначений - узел дерева значений. 
//
Процедура УдалитьУзел(Узел)
	
	РодительУзла = ?(Узел.Родитель = Неопределено, Узел.Владелец(), Узел.Родитель);
	РодительУзла.Строки.Удалить(Узел);
	
КонецПроцедуры

// Отсекает необязательные узлы дерева значений.
//
// Параметры:
//  Узел      - ДеревоЗначений - описание структуры данных выгрузки в xml.
//
Процедура ОтсечьНезаполненныеНеобязательныеУзлы(Узел)
	
	КоличествоСтрок = Узел.Строки.Количество();
	Для Инд = 1 По КоличествоСтрок Цикл
		Стр = Узел.Строки.Получить(КоличествоСтрок - Инд);
		ОтсечьНезаполненныеНеобязательныеУзлы(Стр);
	КонецЦикла;
	
	Если ТипЗнч(Узел) <> Тип("ДеревоЗначений") Тогда
		Если (СтрНайти(Узел.Обязательность, "Н") <> 0 ИЛИ СтрНайти(Узел.Обязательность, "H") <> 0) И УзелПуст(Узел) Тогда // учтем оба варианта: кириллицу и латиницу
			УдалитьУзел(Узел);
		ИначеЕсли (СтрНайти(Узел.Обязательность, "М") <> 0 ИЛИ СтрНайти(Узел.Обязательность, "M") <> 0) // учтем оба варианта: кириллицу и латиницу
		И УзелПуст(Узел)
		И ?(СтрНайти(Узел.Обязательность, "О") <> 0 ИЛИ СтрНайти(Узел.Обязательность, "O") <> 0, ИмеютсяАналогичныеСоседниеУзлы(Узел), Истина) Тогда
			УдалитьУзел(Узел);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Проверяет пустой ли узел дереза значений.
//
// Параметры:
//  Узел - СтрокаДереваЗначений - узел дерева значений. 
// Возвращаемое значение:
//  Булево - Истана, если узел пустой.
//
Функция УзелПуст(Узел)
	
	ПустойУзел = ?(Узел.Формат = "N", Узел.Значение = "0" ИЛИ (НЕ ЗначениеЗаполнено(Узел.Значение)), НЕ ЗначениеЗаполнено(Узел.Значение));
	Для Каждого Стр Из Узел.Строки Цикл
		Если НЕ УзелПуст(Стр) Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	Возврат ПустойУзел;
	
КонецФункции

// Проверяет имеются аналогичные соседние узелы дереза значений.
//
// Параметры:
//  Стр - СтрокаДереваЗначений - узел дерева значений. 
// Возвращаемое значение:
//  Булево - Истана, если узел пустой.
//
Функция ИмеютсяАналогичныеСоседниеУзлы(Стр)
	
	Возврат (Стр.Родитель.Строки.НайтиСтроки(Новый Структура("Ключ", Стр.Ключ), Ложь).Количество() > 1);
	
КонецФункции

// Параметры:
//  ДеревоВыгрузки - ДеревоЗначений - дерево для выгрузки в XML.
//  ПотокXML       - ЗаписьXML - поток для записи XML.
//  Параметры      - Структура - параметры файла для записи в XML.
// Возвращаемое значение:
//  Строка - текст XML.
//
Функция ВыгрузитьДеревоВXML(ДеревоВыгрузки, Параметры)
	
	ПотокXML = СоздатьНовыйПотокXML(); // создаем новый поток для записи
	ЗаписатьУзелДереваВXML(ДеревоВыгрузки, ПотокXML, Параметры); // пишем дерево в поток
	ТекстДляЗаписи = ПотокXML.Закрыть(); // получаем текст XML
	ТекстДляЗаписи = "<?xml version=""1.0"" encoding=""windows-1251""?>" + Сред(ТекстДляЗаписи, СтрНайти(ТекстДляЗаписи, Символы.ПС));
	Возврат ТекстДляЗаписи;
	
КонецФункции

// Возвращает значение ЗаписьXML для организации последовательной записи документов и фрагментов XML.
//
// Возвращаемое значение:
//  ЗаписьXML - поток для записи XML.
//
Функция СоздатьНовыйПотокXML() Экспорт
	
	ПотокXML = Новый ЗаписьXML();
	ПотокXML.УстановитьСтроку("UTF-8");
	ПотокXML.ЗаписатьОбъявлениеXML();
	ПотокXML.Отступ = Истина;
	Возврат ПотокXML;
	
КонецФункции

// Параметры:
//  СтрокаДерева - СтрокаДереваЗначений, ДеревоЗначений - узел дерева.
//  ПотокXML     - ЗаписьXML - поток для записи XML.
//  Параметры    - Структура - параметры файла для записи в XML.
Процедура ЗаписатьУзелДереваВXML(СтрокаДерева, ПотокXML, Параметры) Экспорт
	
	Если ТипЗнч(СтрокаДерева) = Тип("ДеревоЗначений") Тогда
		ПотокXML.ЗаписатьНачалоЭлемента("Файл");
		ПотокXML.ЗаписатьАтрибут("xmlns:xsi", "http://www.w3.org/2001/XMLSchema-instance");
		Для Каждого Стр Из СтрокаДерева.Строки Цикл
			ЗаписатьУзелДереваВXML(Стр, ПотокXML, Параметры);
		КонецЦикла;
		ПотокXML.ЗаписатьКонецЭлемента();
	Иначе
		Если СтрокаДерева.Тип = "А" ИЛИ СтрокаДерева.Тип = "A" Тогда
			ПотокXML.ЗаписатьАтрибут(СтрокаДерева.Код, Строка(СтрокаДерева.Значение));
		Иначе
			ПотокXML.ЗаписатьНачалоЭлемента(СтрокаДерева.Код);
			Для Каждого Лист Из СтрокаДерева.Строки Цикл
				ЗаписатьУзелДереваВXML(Лист, ПотокXML, Параметры);
			КонецЦикла;
			ПотокXML.ЗаписатьТекст(Строка(СтрокаДерева.Значение));
			ПотокXML.ЗаписатьКонецЭлемента();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли