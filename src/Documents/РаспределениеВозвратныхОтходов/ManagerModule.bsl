#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	МеханизмыДокумента.Добавить("Обеспечение");
	МеханизмыДокумента.Добавить("ОперативныйУчетТоваровОрганизаций");
	МеханизмыДокумента.Добавить("РеестрДокументов");
	МеханизмыДокумента.Добавить("СебестоимостьИПартионныйУчет");
	МеханизмыДокумента.Добавить("СерийныйУчет");
	МеханизмыДокумента.Добавить("СуммыДокументовВВалютахУчета");
	МеханизмыДокумента.Добавить("АдресныйСклад");
	МеханизмыДокумента.Добавить("УчетДоходовРасходов");
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	МеханизмыДокумента.Добавить("УчетРабот");
	МеханизмыДокумента.Добавить("УчетНДС");
	МеханизмыДокумента.Добавить("ИсправлениеДокументов");
	
	РаспределениеВозвратныхОтходовЛокализация.ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента);
	
КонецПроцедуры

#КонецОбласти

// Формирует описание реквизитов объекта, заполняемых по статистике их использования.
//
// Параметры:
//  ОписаниеРеквизитов - Структура - описание реквизитов, для которых необходимо получить значения по статистике
//
//
Процедура ЗадатьОписаниеЗаполняемыхРеквизитовПоСтатистике(ОписаниеРеквизитов) Экспорт
	
	ЗаполнениеОбъектовПоСтатистике.ДобавитьОписаниеЗаполняемыхРеквизитов(ОписаниеРеквизитов,
		"Организация");

	Параметры = ЗаполнениеОбъектовПоСтатистике.ПараметрыЗаполняемыхРеквизитов();
	Параметры.РазрезыСбораСтатистики.ИспользоватьТолькоЗаполненные = "Организация";
	ЗаполнениеОбъектовПоСтатистике.ДобавитьОписаниеЗаполняемыхРеквизитов(
		ОписаниеРеквизитов, "Ответственный", Параметры);
	
КонецПроцедуры

#Область СтандартныеПодсистемыКоманды

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	ПроизводствоСервер.ДобавитьКомандуСоздатьВыпускПродукцииБезЗаказаНаОсновании(КомандыСозданияНаОсновании);
	
	Обработки.СправочноеРазмещениеНоменклатуры.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	ИсправлениеДокументов.ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, ПустаяСсылка().Метаданные());
	
	РаспределениеВозвратныхОтходовЛокализация.ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры);
	
КонецПроцедуры

// Добавляет команду создания документа "Распределение возвратных отходов".
//
// Параметры:
//  КомандыСозданияНаОсновании - ТаблицаЗначений - описание в СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании
// Возвращаемое значение:
// 	СтрокаТаблицыЗначений - описание в СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании 
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	Возврат Неопределено;
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	Отчеты.БазаРаспределенияМатериаловИРабот.ДобавитьКомандуРаспределениеВозвратногоОтхода(КомандыОтчетов);
	РаспределениеВозвратныхОтходовЛокализация.ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры);

КонецПроцедуры

// СтандартныеПодсистемы.Печать

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
#Область ПечатьВыпускаПродукции

	Если ПравоДоступа("Чтение", Метаданные.Документы.РаспределениеВозвратныхОтходов) Тогда
		
		// Выпуск продукции
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "Накладная";
		КомандаПечати.Представление = НСтр("ru = 'Выпуск продукции';
											|en = 'Product release'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		
	КонецЕсли;
	
#КонецОбласти

	Обработки.ПечатьЗаданияНаОтборРазмещениеТоваров.ДобавитьКомандуПечати(КомандыПечати, "ЗаданиеНаРазмещение");
	
	РаспределениеВозвратныхОтходовЛокализация.ДобавитьКомандыПечати(КомандыПечати);

КонецПроцедуры

// Конец СтандартныеПодсистемы.Печать

#КонецОбласти

#Область Серии

// Имена реквизитов, от значений которых зависят параметры указания серий.
//
//	Возвращаемое значение:
//		Строка - имена реквизитов, перечисленные через запятую.
//
Функция ИменаРеквизитовДляЗаполненияПараметровУказанияСерий() Экспорт
	
	Возврат "Получатель, Дата";
	
КонецФункции

// Возвращает параметры указания серий для товаров, указанных в документе.
//
//	Параметры:
//			Объект - Структура - структура значений реквизитов объекта, необходимых для заполнения параметров указания серий.
//	Возвращаемое значение:
//			Структура - Структура параметров указания серий для объекта.
//
Функция ПараметрыУказанияСерий(Объект) Экспорт
	
	ПараметрыСерийСклада = СкладыСервер.ИспользованиеСерийНаСкладе(Объект.Получатель, Ложь);
	
	ПараметрыУказанияСерий = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	ПараметрыУказанияСерий.ПолноеИмяОбъекта = "Документ.РаспределениеВозвратныхОтходов";
	
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры = ПараметрыСерийСклада.ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.УчитыватьСебестоимостьПоСериям = ПараметрыСерийСклада.УчитыватьСебестоимостьПоСериям;
	
	ПараметрыУказанияСерий.СкладскиеОперации.Добавить(Перечисления.СкладскиеОперации.ПриемкаПродукцииИзПроизводства);
	
	ПараметрыУказанияСерий.СерииПриПланированииОтгрузкиУказываютсяВТЧСерии = Истина;
	
	ПараметрыУказанияСерий.ПоляСвязи.Добавить("Назначение");
	
	ПараметрыУказанияСерий.ЭтоНакладная = Истина;
	ПараметрыУказанияСерий.ПланированиеОтгрузки = Ложь;
	ПараметрыУказанияСерий.ПланированиеОтбора   = Ложь;
	ПараметрыУказанияСерий.ФактОтбора           = Истина;
	
	ПараметрыУказанияСерий.РегистрироватьСерии = Истина;
	
	ПараметрыУказанияСерий.ТоварВШапке = Истина;
	ПараметрыУказанияСерий.ИмяПоляСклад = "Получатель";
	ПараметрыУказанияСерий.Дата = Объект.Дата;
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

// Возвращает текст запроса для расчета статусов указания серий.
//	Параметры:
//		ПараметрыУказанияСерий - Структура - состав полей задается в функции НоменклатураКлиентСервер.ПараметрыУказанияСерий.
//	Возвращаемое значение:
//		Строка - текст запроса.
//
Функция ТекстЗапросаЗаполненияСтатусовУказанияСерий(ПараметрыУказанияСерий) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Товары.Серия,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Назначение,
	|	Товары.Количество,
	|	Товары.СтатусУказанияСерий,
	|	Товары.НомерСтроки
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Серия,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Назначение,
	|	СУММА(Товары.Количество) КАК Количество,
	|	ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура).ВидНоменклатуры КАК ВидНоменклатуры
	|ПОМЕСТИТЬ ТоварыДляЗапроса
	|ИЗ
	|	Товары КАК Товары
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.Серия,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Назначение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Серии.Номенклатура,
	|	Серии.Характеристика,
	|	Серии.Назначение,
	|	Серии.Количество
	|ПОМЕСТИТЬ Серии
	|ИЗ
	|	&Серии КАК Серии
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСерий.Номенклатура,
	|	ТаблицаСерий.Характеристика,
	|	ТаблицаСерий.Назначение,
	|	СУММА(ТаблицаСерий.Количество) КАК Количество
	|ПОМЕСТИТЬ СерииДляЗапроса
	|ИЗ
	|	Серии КАК ТаблицаСерий
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаСерий.Номенклатура,
	|	ТаблицаСерий.Характеристика,
	|	ТаблицаСерий.Назначение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.СтатусУказанияСерий КАК СтарыйСтатусУказанияСерий,
	|	ВЫБОР
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий ЕСТЬ NULL 
	|			ТОГДА 0
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчитыватьСебестоимостьПоСериям
	|			ТОГДА ВЫБОР
	|					КОГДА Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|						ТОГДА 14
	|					ИНАЧЕ 13
	|				КОНЕЦ
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПриПланированииОтгрузки
	|			ТОГДА ВЫБОР
	|					КОГДА ТоварыДляЗапроса.Количество = ЕСТЬNULL(СерииДляЗапроса.Количество, 0)
	|							И ТоварыДляЗапроса.Количество > 0
	|						ТОГДА 10
	|					ИНАЧЕ 9
	|				КОНЕЦ
	|		КОГДА Склады.ИспользоватьОрдернуюСхемуПриОтгрузке
	|				И &Дата >= Склады.ДатаНачалаОрдернойСхемыПриОтгрузке
	|			ТОГДА 0
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПриПланированииОтбора
	|			ТОГДА ВЫБОР
	|					КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчетСерийПоFEFO
	|						ТОГДА ВЫБОР
	|								КОГДА ТоварыДляЗапроса.Количество = ЕСТЬNULL(СерииДляЗапроса.Количество, 0)
	|										И ТоварыДляЗапроса.Количество > 0
	|									ТОГДА 6
	|								ИНАЧЕ 5
	|							КОНЕЦ
	|					ИНАЧЕ ВЫБОР
	|							КОГДА ТоварыДляЗапроса.Количество = ЕСТЬNULL(СерииДляЗапроса.Количество, 0)
	|									И ТоварыДляЗапроса.Количество > 0
	|								ТОГДА 8
	|							ИНАЧЕ 7
	|						КОНЕЦ
	|				КОНЕЦ
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПоФактуОтбора
	|				И &ФактОтбора
	|				И ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПриОтгрузкеКомплектовДляРазборки
	|			ТОГДА ВЫБОР
	|					КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчитыватьОстаткиСерий
	|						ТОГДА ВЫБОР
	|								КОГДА ТоварыДляЗапроса.Количество = ЕСТЬNULL(СерииДляЗапроса.Количество, 0)
	|										И ТоварыДляЗапроса.Количество > 0
	|									ТОГДА 4
	|								ИНАЧЕ 3
	|							КОНЕЦ
	|					ИНАЧЕ ВЫБОР
	|							КОГДА ТоварыДляЗапроса.Количество = ЕСТЬNULL(СерииДляЗапроса.Количество, 0)
	|									И ТоварыДляЗапроса.Количество > 0
	|								ТОГДА 2
	|							ИНАЧЕ 1
	|						КОНЕЦ
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтатусУказанияСерий
	|ПОМЕСТИТЬ Статусы
	|ИЗ
	|	Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыДляЗапроса КАК ТоварыДляЗапроса
	|			ЛЕВОЕ СОЕДИНЕНИЕ СерииДляЗапроса КАК СерииДляЗапроса
	|			ПО ТоварыДляЗапроса.Номенклатура = СерииДляЗапроса.Номенклатура
	|				И ТоварыДляЗапроса.Характеристика = СерииДляЗапроса.Характеристика
	|				И ТоварыДляЗапроса.Назначение = СерииДляЗапроса.Назначение
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры.ПолитикиУчетаСерий КАК ПолитикиУчетаСерий
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|				ПО ПолитикиУчетаСерий.Склад = Склады.Ссылка
	|			ПО (ПолитикиУчетаСерий.Склад = &Склад)
	|				И ТоварыДляЗапроса.ВидНоменклатуры = ПолитикиУчетаСерий.Ссылка
	|		ПО Товары.Номенклатура = ТоварыДляЗапроса.Номенклатура
	|			И Товары.Характеристика = ТоварыДляЗапроса.Характеристика
	|			И Товары.Назначение = ТоварыДляЗапроса.Назначение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Статусы.НомерСтроки КАК НомерСтроки,
	|	Статусы.СтатусУказанияСерий КАК СтатусУказанияСерий
	|ИЗ
	|	Статусы КАК Статусы
	|ГДЕ
	|	Статусы.СтатусУказанияСерий <> Статусы.СтарыйСтатусУказанияСерий
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат ТекстЗапроса;

КонецФункции

#КонецОбласти

#Область Заполнение

// Формирует список документов, на основании переданных параметров.
//	Параметры:
//		Параметры - Структура - параметры для формирования документов.
//			Имеет структуру {ИзделияПоПравилу, ДанныеШапки, ГруппировкаЗатрат}, все ключи являются обязательными.
//			ИзделияПоПравилу - таблица значений, содержащая перечень выпущенной продукции, распределяемой по правилам.
//			ДанныеШапки - Структура - данные заполнения шапки документов.
//		СписокОбъектов - СписокЗначений - список сформированных документов.
//
Процедура ДокументыПоПараметрам(Параметры, СписокОбъектов) Экспорт
	
	ТаблицаПродукции = ПолучитьИзВременногоХранилища(Параметры.ИзделияПоПравилу);

	ИспользуетсяЦенообразование25 = ЦенообразованиеВызовСервера.ИспользуетсяЦенообразование25(Параметры.ДанныеШапки.Дата);
	
	ТекстыЗапросов = Новый Массив();
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаПродукции.Организация,
		|	ТаблицаПродукции.ПравилоРаспределения,
		|	ТаблицаПродукции.Подразделение,
		|	ТаблицаПродукции.Получатель,
		|	ТаблицаПродукции.Номенклатура,
		|	ТаблицаПродукции.Характеристика,
		|	ТаблицаПродукции.Серия,
		|	ТаблицаПродукции.Назначение,
		|	ТаблицаПродукции.Количество
		|ПОМЕСТИТЬ #ТаблицаПродукции
		|ИЗ
		|	&ТаблицаПродукции КАК ТаблицаПродукции
		|ГДЕ
		|	НЕ ТаблицаПродукции.Количество = 0
		|	И НЕ ТаблицаПродукции.ОшибкаВНастройкахМодели
		|;
		|";
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
	ИмяТаблицыТоваров = "ТаблицаПродукции";
	
	Если ИспользуетсяЦенообразование25 Тогда

		ИмяТаблицыТоваров = "ТаблицаПродукцииПредварительная";
		
		Настройки = ЦенообразованиеКлиентСервер.НастройкиДляВременнойТаблицыСДополнениемДляЦенообразования();
		
		Настройки.ИсточникТоваров = ИмяТаблицыТоваров;
		Настройки.ПриемникТоваров = "ТаблицаПродукции";
		Настройки.СписокПрочихПолей = "Организация, ПравилоРаспределения, Подразделение, Получатель, Назначение, Количество";
		Настройки.ПолеУпаковка = "";
		
		ТекстЗапроса = ЦенообразованиеКлиентСервер.ТекстЗапросаВременнойТаблицыСДополнениемДляЦенообразования(
												Настройки,
												ИспользуетсяЦенообразование25);
		ТекстыЗапросов.Добавить(ТекстЗапроса);
		ТекстыЗапросов.Добавить(";");
	КонецЕсли;
	
	ТекстЗапроса = "
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаПродукции.Организация,
		|	ВЫБОР
		|		КОГДА &ЗаполнятьАвтоматически
		|			ТОГДА ТаблицаПродукции.ПравилоРаспределения
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК ПравилоРаспределения,
		|	ПравилаРаспределения.БазаРаспределения КАК БазаРаспределения,
		|	ПравилаРаспределения.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	ПлановыйВидЦены.Значение КАК ВидЦены,
		|	ТаблицаПродукции.Подразделение,
		|	ТаблицаПродукции.Получатель,
		|	ТаблицаПродукции.Номенклатура,
		|	ТаблицаПродукции.Характеристика,
		|	ТаблицаПродукции.Серия,
		|	ТаблицаПродукции.Назначение,
		|	СУММА(ТаблицаПродукции.Количество) КАК Количество,
		|	СУММА(ТаблицаПродукции.Количество) КАК КоличествоПоПравилу,
		|	ЦеныНоменклатуры.Цена КАК Цена,
		|	СУММА(ТаблицаПродукции.Количество * ЦеныНоменклатуры.Цена) КАК Сумма
		|ИЗ
		|	ТаблицаПродукции КАК ТаблицаПродукции
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПравилаРаспределенияРасходов КАК ПравилаРаспределения
		|		ПО ТаблицаПродукции.ПравилоРаспределения = ПравилаРаспределения.Ссылка
		|			И (&ЗаполнятьАвтоматически)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Константа.ВидЦеныПлановойСтоимостиМатериаловРабот КАК ПлановыйВидЦены
		|		ПО (ИСТИНА)
		|		ЛЕВОЕ СОЕДИНЕНИЕ
		|			&ЦеныНоменклатуры КАК ЦеныНоменклатуры
		|		ПО 
		|			&УсловиеСоединенияЦеныНоменклатуры
		|
		|СГРУППИРОВАТЬ ПО
		|	ПлановыйВидЦены.Значение,
		|	ТаблицаПродукции.Организация,
		|	ПравилаРаспределения.СтатьяКалькуляции,
		|	ТаблицаПродукции.Серия,
		|	ТаблицаПродукции.Назначение,
		|	ТаблицаПродукции.Подразделение,
		|	ТаблицаПродукции.Характеристика,
		|	ТаблицаПродукции.Номенклатура,
		|	ТаблицаПродукции.Получатель,
		|	ТаблицаПродукции.ПравилоРаспределения,
		|	ПравилаРаспределения.БазаРаспределения,
		|	ЦеныНоменклатуры.Цена";
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = СтрСоединить(ТекстыЗапросов);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТаблицаПродукции", ИмяТаблицыТоваров);
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ЦеныНоменклатуры",
		ЦенообразованиеКлиентСервер.ТекстЗапросаРегистрСведенийЦеныНоменклатуры("ТаблицаПродукции",
																				,
																				,
																				ИспользуетсяЦенообразование25));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&УсловиеСоединенияЦеныНоменклатуры",
		ЦенообразованиеКлиентСервер.ТекстЗапросаРегистрСведенийЦеныНоменклатурыУсловиеСоединения(
		"ТаблицаПродукции",
		"ЦеныНоменклатуры",
		"ПлановыйВидЦены.Значение",
		ИспользуетсяЦенообразование25));
		
	Запрос = Новый Запрос(ТекстЗапроса);
		
	Запрос.УстановитьПараметр("ТаблицаПродукции",		ТаблицаПродукции);
	Запрос.УстановитьПараметр("ЗаполнятьАвтоматически",	Параметры.ЗаполнятьАвтоматически);
	Запрос.УстановитьПараметр("Дата",					Параметры.ДанныеШапки.Дата);
	
	ТаблицаПродукции = Запрос.Выполнить().Выгрузить();
	
	КоличествоНовыхДокументов	= 0;
	
	Для Каждого ТекПродукция Из ТаблицаПродукции Цикл
		
		ТекДокумент = Документы.РаспределениеВозвратныхОтходов.СоздатьДокумент();
		ТекДокумент.ПолучитьСсылкуНового();
		
		СписокОбъектов.Добавить(ТекДокумент);
		КоличествоНовыхДокументов = КоличествоНовыхДокументов + 1;
		
		ТекДокумент.Заполнить(Новый Структура("Организация", ТекПродукция.Организация));
		
		ЗаполнитьЗначенияСвойств(ТекДокумент, Параметры.ДанныеШапки);
		ЗаполнитьЗначенияСвойств(ТекДокумент, ТекПродукция);
		
		ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ТекДокумент, Документы.РаспределениеВозвратныхОтходов);
		НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ТекДокумент, ПараметрыУказанияСерий);
		
		Если (ТекДокумент.СтатусУказанияСерий = 3
				ИЛИ ТекДокумент.СтатусУказанияСерий = 5
				ИЛИ ТекДокумент.СтатусУказанияСерий = 7
				ИЛИ ТекДокумент.СтатусУказанияСерий = 9)
			И ЗначениеЗаполнено(ТекДокумент.Серия) Тогда
			ЗаполнитьЗначенияСвойств(ТекДокумент.Серии.Добавить(), ТекПродукция);
			ТекДокумент.СтатусУказанияСерий = ТекДокумент.СтатусУказанияСерий + 1;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область УчетНДС

// Инициализирует параметры заполнения вида деятельности НДС
//
// Параметры:
//  Объект		- ДокументОбъект.РаспределениеВозвратныхОтходов, ДанныеФормыСтруктура	- документ, для которого необходимо получить параметры.
//
// Возвращаемое значение:
//  См. УчетНДСУПКлиентСервер.ПараметрыЗаполненияВидаДеятельностиНДС
//
Функция ПараметрыЗаполненияВидаДеятельностиНДС(Объект) Экспорт
	
	ПараметрыЗаполнения = УчетНДСУПКлиентСервер.ПараметрыЗаполненияВидаДеятельностиНДС();
	ПараметрыЗаполнения.Организация							= Объект.Организация;
	ПараметрыЗаполнения.Дата								= Объект.Дата;
	ПараметрыЗаполнения.НаправлениеДеятельности				= Объект.НаправлениеДеятельности;
	ПараметрыЗаполнения.ВыпускПродукцииИРабот				= Истина;
	
	Возврат ПараметрыЗаполнения;
	
КонецФункции

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(Подразделение)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

// Возвращает параметры настройки счетов учета в документе.
//  
// Возвращаемое значение:
//	см. НастройкаСчетовУчетаСервер.ПараметрыНастройки
//
Функция ПараметрыНастройкиСчетовУчета() Экспорт
	
	ПараметрыНастройки = НастройкаСчетовУчетаСервер.ПараметрыНастройки();
	
	ПараметрыНастройки.ДоступностьПоОперации 	= Истина;
	ПараметрыНастройки.ПутьКДанным   			= "Объект.ПрочиеРасходы";
	ПараметрыНастройки.ТипСтатьи     			= "ТипСтатьи";
	ПараметрыНастройки.Организация 				= "Объект.Организация";
	
	ПараметрыНастройки.ЭлементыФормы.Добавить("ПрочиеРасходыПредставлениеОтраженияОперации");
	
	Возврат ПараметрыНастройки;
	
КонецФункции

// Возвращает параметры выбора статей и аналитик.
// 
// Возвращаемое значение:
//	см. ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики
//
Функция ПараметрыВыбораСтатейИАналитик() Экспорт 
	
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект.ПрочиеРасходы";
	ПараметрыВыбора.Статья      = "СтатьяРасходов";
	ПараметрыВыбора.ТипСтатьи   = "ТипСтатьи";
	
	ПараметрыВыбора.АналитикаРасходов 		 = "АналитикаРасходов";
	ПараметрыВыбора.АналитикаАктивовПассивов = "АналитикаАктивовПассивов";
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	
	МассивВариантов = Новый Массив;
	МассивВариантов.Добавить(Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности);
	МассивВариантов.Добавить(Перечисления.ВариантыРаспределенияРасходов.НаРасходыБудущихПериодов);
	МассивВариантов.Добавить(Перечисления.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты);
	МассивВариантов.Добавить(Перечисления.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы);
	МассивВариантов.Добавить(Перечисления.ВариантыРаспределенияРасходов.НеРаспределять);
	МассивВариантов.Добавить(Перечисления.ВариантыРаспределенияРасходов.НаОбъектыВозникновенияЗатрат);
	
	ПараметрыВыбора.ОтборСтатейРасходов.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СписаниеТоваровПоТребованию;
	ПараметрыВыбора.ОтборСтатейРасходов.ВариантРаспределенияРасходов = МассивВариантов;
	
	ПараметрыВыбора.ВыборСтатьиАктивовПассивов = Истина;
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("ПрочиеРасходыСтатьяРасходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("ПрочиеРасходыАналитикаРасходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаАктивовПассивов.Добавить("ПрочиеРасходыАналитикаАктивовПассивов");
	
	Возврат ПараметрыВыбора;
	
КонецФункции

// Возвращает правила печати печатной формы Задания на отбор (размещение) товаров.
//
// Возвращаемое значение:
//	Массив - элементами, которого является структура (См. Обработки.ПечатьЗаданияНаОтборРазмещениеТоваров.ДобавитьКомандуПечати)
//
Функция ПравилаПечатиЗаданияНаОтборРазмещение() Экспорт
	
	ПравилаПечатиЗадания = Обработки.ПечатьЗаданияНаОтборРазмещениеТоваров.ПравилаПечатиЗаданияНаОтборРазмещение();
	ПравилаПечатиЗадания.ОперацияПоступления	= Истина;
	ПравилаПечатиЗадания.ИмяПоляСклад			= "Получатель";
	
	Возврат ПравилаПечатиЗадания;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

#Область Инициализация

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка, ДокументОбъект - ссылка на документ или объект, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * ТаблицаИмяРегистра - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Если ТипЗнч(Документ) = Тип("ДокументОбъект.РаспределениеВозвратныхОтходов") Тогда
		ДокументСсылка = Документ.Ссылка;
	Иначе
		ДокументСсылка = Документ;
	КонецЕсли;
	
	Запрос			= Новый Запрос;
	ТекстыЗапроса	= Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
		
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса
		СформироватьСуммыДокументаВВалютахУчета(Запрос, ТекстыЗапроса, Регистры);
		
		ТекстЗапросаТаблицаДвиженияСерийТоваров(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДвиженияПоПрочимАктивамПассивам(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДатыПоступленияТоваровОрганизаций(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаМатериалыИРаботыВПроизводстве(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПартииПрочихРасходов(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаТоварыОрганизаций(Запрос, ТекстыЗапроса, Регистры);
		
		РасчетСебестоимостиПроведениеДокументов.ОтразитьВМеханизмеУчетаЗатратИСебестоимости(ДокументСсылка, Запрос, ТекстыЗапроса, Регистры);
		
		РаспределениеВозвратныхОтходовЛокализация.ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры);
	КонецЕсли;
	
	ОтразитьРаспределениеЗапасовДвижения(Запрос, ТекстыЗапроса, Регистры);
	ЗапланироватьПоступлениеТоваров(Запрос, ТекстыЗапроса, Регистры);
	ОформитьПоступлениеТоваровПоОдноходовке(Запрос, ТекстыЗапроса, Регистры);
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Реквизиты.Дата,
		|	Реквизиты.Дата КАК Период,
		|	Реквизиты.Ссылка,
		|	Реквизиты.Номер,
		|	Реквизиты.Проведен,
		|	Реквизиты.ПометкаУдаления,
		|	Реквизиты.Организация,
		|	Реквизиты.ВыпускПодДеятельность,
		|	Реквизиты.НаправлениеДеятельности,
		|	Реквизиты.ПравилоРаспределения,
		|	Реквизиты.КоличествоПоПравилу,
		|	Реквизиты.СтатьяКалькуляции,
		|	Реквизиты.ВидЦены,
		|	Реквизиты.Подразделение,
		|	Реквизиты.Получатель,
		|	Реквизиты.Номенклатура,
		|	СпрНоменклатура.ТипНоменклатуры КАК ТипНоменклатуры,
		|	СпрНоменклатура.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
		|	СпрНоменклатура.ГруппаАналитическогоУчета КАК ГруппаАналитическогоУчета,
		|	Реквизиты.Характеристика,
		|	Реквизиты.Серия,
		|	Реквизиты.СтатусУказанияСерий,
		|	Реквизиты.Количество,
		|	Реквизиты.Назначение,
		|	СпрНазначения.ДвиженияПоСкладскимРегистрам КАК ДвиженияПоСкладскимРегистрам,
		|	Реквизиты.Цена,
		|	Реквизиты.Сумма,
		|	Реквизиты.АналитикаУчетаНоменклатуры,
		|	Реквизиты.Валюта,
		|	Реквизиты.Ответственный,
		|	Реквизиты.Автор,
		|	Реквизиты.Комментарий,
		|	Реквизиты.ИдентификаторДокумента
		|ИЗ
		|	Документ.РаспределениеВозвратныхОтходов КАК Реквизиты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|		ПО (СпрНоменклатура.Ссылка = Реквизиты.Номенклатура)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Назначения КАК СпрНазначения
		|		ПО (СпрНазначения.Ссылка = Реквизиты.Назначение)
		|ГДЕ
		|	Реквизиты.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("АналитическийУчетПоГруппамПродукции",
								ПолучитьФункциональнуюОпцию("АналитическийУчетПоГруппамПродукции"));
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
	Запрос.УстановитьПараметр("ИдентификаторМетаданных",
								ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Тип("ДокументСсылка.РаспределениеВозвратныхОтходов")));
								
	Запрос.УстановитьПараметр("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	Запрос.УстановитьПараметр("ИдентификаторНеиспользуемойФинЗаписи", ПроведениеДокументов.ИдентификаторНеиспользуемойФинЗаписи());
	
	ПараметрыУчетаПоОрганизации = УчетНДСУП.ПараметрыУчетаПоОрганизации(Реквизиты.Организация, Реквизиты.Дата);
	Запрос.УстановитьПараметр("НалогообложениеОрганизации", ПараметрыУчетаПоОрганизации.ОсновноеНалогообложениеНДСПродажи);
	
КонецПроцедуры

Процедура УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос)
	
	Если Запрос.Параметры.Свойство("КоэффициентПересчетаВВалютуУпр")
		И Запрос.Параметры.Свойство("КоэффициентПересчетаВВалютуРегл") Тогда
		Возврат;
	КонецЕсли;
	
	Коэффициенты = РаботаСКурсамивалютУТ.ПолучитьКоэффициентыПересчетаВалюты(Запрос.Параметры.Валюта,
		                                                                         Запрос.Параметры.Валюта,
		                                                                         Запрос.Параметры.Дата,
		                                                                         Запрос.Параметры.Организация);
	
	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуУпр",  Коэффициенты.КоэффициентПересчетаВВалютуУпр);
	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуРегл", Коэффициенты.КоэффициентПересчетаВВалютуРегл);
	
КонецПроцедуры

Процедура ИнициализироватьКлючиАналитикиНоменклатуры(Запрос)

	Если Запрос = Неопределено ИЛИ Запрос.Параметры.Свойство("КлючиАналитикиНоменклатурыИнициализированы") Тогда
		Возврат;
	КонецЕсли;
	
	ЗапросАналитики = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаТовары.Склад КАК Склад,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	ТаблицаТовары.Назначение КАК Назначение,
	|	ТаблицаТовары.Серия КАК Серия,
	|	ТаблицаТовары.СтатьяКалькуляции
	|ИЗ
	|	(
	// аналитика получателя возвратных отходов с пустым назначением
	|	ВЫБРАТЬ
	|		&Получатель											КАК Склад,
	|		&Номенклатура										КАК Номенклатура,
	|		&Характеристика										КАК Характеристика,
	|		ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)		КАК Назначение,
	|		ВЫБОР
	|			КОГДА &СтатусУказанияСерий = 14
	|				ТОГДА &Серия
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		КОНЕЦ												КАК Серия,
	|		ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)	КАК СтатьяКалькуляции
	|	ГДЕ
	|		НЕ &УчитыватьСебестоимостьТоваровПоНазначениям
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// аналитика подразделения при выпуске на склад
	|	ВЫБРАТЬ
	|		&Подразделение										КАК Склад,
	|		&Номенклатура										КАК Номенклатура,
	|		&Характеристика										КАК Характеристика,
	|		&Назначение											КАК Назначение,
	|		ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)	КАК Серия,
	|		ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)	КАК СтатьяКалькуляции
	|	ГДЕ
	|		&УчитыватьСебестоимостьТоваровПоНазначениям
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// аналитика подразделения при выпуске на склад без назначения
	|	ВЫБРАТЬ
	|		&Подразделение										КАК Склад,
	|		&Номенклатура										КАК Номенклатура,
	|		&Характеристика										КАК Характеристика,
	|		ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)		КАК Назначение,
	|		ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)	КАК Серия,
	|		ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)	КАК СтатьяКалькуляции
	|	ГДЕ
	|		НЕ &УчитыватьСебестоимостьТоваровПоНазначениям
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// аналитика партии производства
	|	ВЫБРАТЬ
	|		&Подразделение										КАК Склад,
	|		&Номенклатура										КАК Номенклатура,
	|		&Характеристика										КАК Характеристика,
	| 		ЕСТЬNULL(СпрПартииПроизводства.Назначение, 
	| 			ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка))	КАК Назначение,
	|		ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)	КАК Серия,
	|		ТаблицаТовары.СтатьяКалькуляции						КАК СтатьяКалькуляции
	|	ИЗ
	|		Документ.РаспределениеВозвратныхОтходов.ПартииПроизводства КАК ТаблицаТовары
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК СпрПартииПроизводства
	|			ПО СпрПартииПроизводства.Ссылка = ТаблицаТовары.ПартияПроизводства
	|
	|	ГДЕ
	|		&УчитыватьСебестоимостьТоваровПоНазначениям
	|		И ТаблицаТовары.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// аналитика партии производства с пустым назначением
	|	ВЫБРАТЬ
	|		&Подразделение										КАК Склад,
	|		&Номенклатура										КАК Номенклатура,
	|		&Характеристика										КАК Характеристика,
	|		ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)		КАК Назначение,
	|		ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)	КАК Серия,
	|		ТаблицаТовары.СтатьяКалькуляции						КАК СтатьяКалькуляции
	|	ИЗ
	|		Документ.РаспределениеВозвратныхОтходов.ПартииПроизводства КАК ТаблицаТовары
	|	ГДЕ
	|		НЕ &УчитыватьСебестоимостьТоваровПоНазначениям
	|		И ТаблицаТовары.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// аналитика по правилам
	|	ВЫБРАТЬ
	|		&Подразделение										КАК Склад,
	|		&Номенклатура										КАК Номенклатура,
	|		&Характеристика										КАК Характеристика,
	|		&Назначение											КАК Назначение,
	|		ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)	КАК Серия,
	|		ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)	КАК СтатьяКалькуляции
	|	ГДЕ
	|		&УчитыватьСебестоимостьТоваровПоНазначениям
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// аналитика по правилам с пустым назначением
	|	ВЫБРАТЬ
	|		&Подразделение										КАК Склад,
	|		&Номенклатура										КАК Номенклатура,
	|		&Характеристика										КАК Характеристика,
	|		ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)		КАК Назначение,
	|		ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)	КАК Серия,
	|		ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)	КАК СтатьяКалькуляции
	|	ГДЕ
	|		НЕ &УчитыватьСебестоимостьТоваровПоНазначениям) КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|		ПО ТаблицаТовары.Номенклатура = Аналитика.Номенклатура
	|			И ТаблицаТовары.Характеристика = Аналитика.Характеристика
	|			И ТаблицаТовары.Серия = Аналитика.Серия
	|			И ТаблицаТовары.Склад = Аналитика.МестоХранения
	|			И ТаблицаТовары.Назначение = Аналитика.Назначение
	|			И ТаблицаТовары.СтатьяКалькуляции = Аналитика.СтатьяКалькуляции
	|ГДЕ
	|	Аналитика.Номенклатура ЕСТЬ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Склад,
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Назначение,
	|	ТаблицаТовары.Серия,
	|	ТаблицаТовары.СтатьяКалькуляции");
	
	ЗапросАналитики.УстановитьПараметр("Ссылка",					Запрос.Параметры.Ссылка);
	ЗапросАналитики.УстановитьПараметр("Получатель",				Запрос.Параметры.Получатель);
	ЗапросАналитики.УстановитьПараметр("Номенклатура",				Запрос.Параметры.Номенклатура);
	ЗапросАналитики.УстановитьПараметр("Характеристика",			Запрос.Параметры.Характеристика);
	ЗапросАналитики.УстановитьПараметр("Назначение",				Запрос.Параметры.Назначение);
	ЗапросАналитики.УстановитьПараметр("СтатусУказанияСерий",		Запрос.Параметры.СтатусУказанияСерий);
	ЗапросАналитики.УстановитьПараметр("Серия",						Запрос.Параметры.Серия);
	ЗапросАналитики.УстановитьПараметр("Подразделение",				Запрос.Параметры.Подразделение);
	ЗапросАналитики.УстановитьПараметр("УчитыватьСебестоимостьТоваровПоНазначениям", Запрос.Параметры.УчитыватьСебестоимостьТоваровПоНазначениям);
	
	Выборка = ЗапросАналитики.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		РегистрыСведений.АналитикаУчетаНоменклатуры.СоздатьКлючАналитики(Выборка)
	КонецЦикла;

	Запрос.УстановитьПараметр("КлючиАналитикиНоменклатурыИнициализированы", Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ПроведениеПоРеглУчету

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса.
//
Функция ТекстОтраженияВРеглУчете() Экспорт

	Возврат РаспределениеВозвратныхОтходовЛокализация.ТекстОтраженияВРеглУчете();

КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламетированном учете.
// Возвращаемое значение:
//	см. РаспределениеВозвратныхОтходовЛокализация.ТекстЗапросаВТОтраженияВРеглУчете
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт

	Возврат РаспределениеВозвратныхОтходовЛокализация.ТекстЗапросаВТОтраженияВРеглУчете();

КонецФункции

#КонецОбласти

#Область ТекстыЗапросовПроведения

#Область ТекстыЗапросовВременныеТаблицы

Функция ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтИсходныеПрочиеРасходы";
	
	ТекстЗапросаНастройкиПризнанияРасходовНДД = РасчетСебестоимостиЛокализация.ТекстЗапросаНастроекНДД("", "РаспределениеВозвратныхОтходов_ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы");
	ТекстыЗапроса.Добавить(ТекстЗапросаНастройкиПризнанияРасходовНДД, ИмяРегистра);
	
	УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)	КАК ВидДвижения,
	|	&Дата									КАК Период,
	|	&Организация							КАК Организация,
	|	ВидыЗапасов.Подразделение				КАК Подразделение,
	|	&НаправлениеДеятельности				КАК НаправлениеДеятельности,
	|	СпрНазначения.НаправлениеДеятельности	КАК КорНаправлениеДеятельности,
	|	ВидыЗапасов.СтатьяРасходов				КАК СтатьяРасходов,
	|	ВидыЗапасов.АналитикаРасходов			КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО							КАК ВидДеятельностиНДС,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость) КАК ХозяйственнаяОперация,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	|
	|	-ВЫРАЗИТЬ(ВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))	КАК СуммаСНДС,
	|	-ВЫРАЗИТЬ(ВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))	КАК СуммаБезНДС,
	|	-ВЫРАЗИТЬ(ВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))	КАК СуммаБезНДСУпр,
	|	-ВЫБОР
	|		КОГДА НЕ СтатьиРасходов.ПринятиеКНалоговомуУчету
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(ВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|	КОНЕЦ									КАК СуммаСНДСРегл,
	|	-ВЫБОР
	|		КОГДА СтатьиРасходов.ПринятиеКНалоговомуУчету
	|			ТОГДА ВЫРАЗИТЬ(ВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0
	|	КОНЕЦ									КАК СуммаБезНДСРегл,
	|	-ВЫБОР
	|		КОГДА СтатьиРасходов.ПринятиеКНалоговомуУчету
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(ВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|	КОНЕЦ									КАК ПостояннаяРазница,
	|	0										КАК ВременнаяРазница,
	|	-ВЫБОР
	|		КОГДА НастройкиПризнанияРасходовНДДПоПодразделению.ОбъектРаздельногоУчетаНДД ЕСТЬ NULL
	|			 И НастройкиПризнанияРасходовНДДПоНаправлениюДеятельности.ОбъектРаздельногоУчетаНДД ЕСТЬ NULL
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА НЕ СтатьиРасходов.ПринятиеКНалоговомуУчету
	|					ТОГДА 0
	|				ИНАЧЕ ВЫРАЗИТЬ(ВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|			КОНЕЦ
	|	КОНЕЦ КАК СуммаНДД,
	|
	|	ВидыЗапасов.ИдентификаторФинЗаписи	КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ВыпускПродукцииФиксированнаяСтоимость) КАК НастройкаХозяйственнойОперации
	|
	|ПОМЕСТИТЬ ВтИсходныеПрочиеРасходы
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов.ВидыЗапасов КАК ВидыЗапасов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|		ПО СтатьиРасходов.Ссылка = ВидыЗапасов.СтатьяРасходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Назначения КАК СпрНазначения
	|		ПО СпрНазначения.Ссылка = &Назначение
	|		ЛЕВОЕ СОЕДИНЕНИЕ НастройкиПризнанияРасходовНДД КАК НастройкиПризнанияРасходовНДДПоПодразделению
	|		ПО НастройкиПризнанияРасходовНДДПоПодразделению.ОбъектРаздельногоУчетаНДД = ВидыЗапасов.Подразделение
	|			И (НастройкиПризнанияРасходовНДДПоПодразделению.Организация = &Организация)
	|			И НастройкиПризнанияРасходовНДДПоПодразделению.РасходыВключаютсяВСтоимость
	|		ЛЕВОЕ СОЕДИНЕНИЕ НастройкиПризнанияРасходовНДД КАК НастройкиПризнанияРасходовНДДПоНаправлениюДеятельности
	|		ПО (НастройкиПризнанияРасходовНДДПоНаправлениюДеятельности.ОбъектРаздельногоУчетаНДД = &НаправлениеДеятельности)
	|			И (НастройкиПризнанияРасходовНДДПоНаправлениюДеятельности.Организация = &Организация)
	|			И НастройкиПризнанияРасходовНДДПоНаправлениюДеятельности.РасходыВключаютсяВСтоимость
	|
	|ГДЕ
	|	ВидыЗапасов.Ссылка = &Ссылка
	|	И ТИПЗНАЧЕНИЯ(ВидыЗапасов.СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиРасходов)
	|	И НЕ ВидыЗапасов.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)	КАК ВидДвижения,
	|	&Дата									КАК Период,
	|	&Организация							КАК Организация,
	|	ТаблицаРасходы.Подразделение			КАК Подразделение,
	|	&НаправлениеДеятельности				КАК НаправлениеДеятельности,
	|	СпрНазначения.НаправлениеДеятельности	КАК КорНаправлениеДеятельности,
	|	ТаблицаРасходы.СтатьяРасходов			КАК СтатьяРасходов,
	|	ТаблицаРасходы.АналитикаРасходов		КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО							КАК ВидДеятельностиНДС,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость) КАК ХозяйственнаяОперация,
	|	&АналитикаУчетаНоменклатуры				КАК АналитикаУчетаНоменклатуры,
	|
	|	-ВЫРАЗИТЬ(&Цена * ТаблицаРасходы.Количество * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))	КАК СуммаСНДС,
	|	-ВЫРАЗИТЬ(&Цена * ТаблицаРасходы.Количество * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))	КАК СуммаБезНДС,
	|	-ВЫРАЗИТЬ(&Цена * ТаблицаРасходы.Количество * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))	КАК СуммаБезНДСУпр,
	|	-ВЫБОР
	|		КОГДА НЕ СтатьиРасходов.ПринятиеКНалоговомуУчету
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(&Цена * ТаблицаРасходы.Количество * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|	КОНЕЦ									КАК СуммаСНДСРегл,
	|	-ВЫБОР
	|		КОГДА СтатьиРасходов.ПринятиеКНалоговомуУчету
	|			ТОГДА ВЫРАЗИТЬ(&Цена * ТаблицаРасходы.Количество * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0
	|	КОНЕЦ									КАК СуммаБезНДСРегл,
	|	-ВЫБОР
	|		КОГДА СтатьиРасходов.ПринятиеКНалоговомуУчету
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(&Цена * ТаблицаРасходы.Количество * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|	КОНЕЦ									КАК ПостояннаяРазница,
	|	0										КАК ВременнаяРазница,
	|	-ВЫБОР
	|		КОГДА НастройкиПризнанияРасходовНДДПоПодразделению.ОбъектРаздельногоУчетаНДД ЕСТЬ NULL
	|			 И НастройкиПризнанияРасходовНДДПоНаправлениюДеятельности.ОбъектРаздельногоУчетаНДД ЕСТЬ NULL
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА НЕ СтатьиРасходов.ПринятиеКНалоговомуУчету
	|					ТОГДА 0
	|				ИНАЧЕ ВЫРАЗИТЬ(&Цена * ТаблицаРасходы.Количество * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|			КОНЕЦ
	|	КОНЕЦ КАК СуммаНДД,
	|
	|	ТаблицаРасходы.ИдентификаторСтроки	КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ВыпускПродукцииФиксированнаяСтоимость) КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов.ПрочиеРасходы КАК ТаблицаРасходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|		ПО СтатьиРасходов.Ссылка = ТаблицаРасходы.СтатьяРасходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Назначения КАК СпрНазначения
	|		ПО СпрНазначения.Ссылка = &Назначение
	|		ЛЕВОЕ СОЕДИНЕНИЕ НастройкиПризнанияРасходовНДД КАК НастройкиПризнанияРасходовНДДПоПодразделению
	|		ПО НастройкиПризнанияРасходовНДДПоПодразделению.ОбъектРаздельногоУчетаНДД = ТаблицаРасходы.Подразделение
	|			И (НастройкиПризнанияРасходовНДДПоПодразделению.Организация = &Организация)
	|			И НастройкиПризнанияРасходовНДДПоПодразделению.РасходыВключаютсяВСтоимость
	|		ЛЕВОЕ СОЕДИНЕНИЕ НастройкиПризнанияРасходовНДД КАК НастройкиПризнанияРасходовНДДПоНаправлениюДеятельности
	|		ПО (НастройкиПризнанияРасходовНДДПоНаправлениюДеятельности.ОбъектРаздельногоУчетаНДД = &НаправлениеДеятельности)
	|			И (НастройкиПризнанияРасходовНДДПоНаправлениюДеятельности.Организация = &Организация)
	|			И НастройкиПризнанияРасходовНДДПоНаправлениюДеятельности.РасходыВключаютсяВСтоимость
	|ГДЕ
	|	ТаблицаРасходы.Ссылка = &Ссылка
	|	И &ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|;
	|УНИЧТОЖИТЬ НастройкиПризнанияРасходовНДД
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса) Экспорт
	
	ИмяРегистра = "ВтПрочиеРасходы";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтИсходныеПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ДополнительныеПоля = "";
	РасчетСебестоимостиЛокализация.ДобавитьДополнительныеПоляПоНДД(ДополнительныеПоля);
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаВтПрочиеРасходы(ДополнительныеПоля);
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтИсходныеПартииПрочихРасходов";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтИсходныеПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	ПрочиеРасходы.Подразделение,
	|	&Ссылка КАК ДокументПоступленияРасходов,
	|	ПрочиеРасходы.СтатьяРасходов КАК СтатьяРасходов,
	|	ПрочиеРасходы.АналитикаРасходов КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПартий,
	|	ПрочиеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|	ПрочиеРасходы.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
	|
	|	ПрочиеРасходы.СуммаСНДС КАК Стоимость,
	|	ПрочиеРасходы.СуммаБезНДС КАК СтоимостьБезНДС,
	|	ПрочиеРасходы.СуммаСНДСРегл КАК СтоимостьРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|	0 КАК НДСРегл,
	|	0 КАК НДСУпр,
	|
	|	ПрочиеРасходы.ХозяйственнаяОперация,
	|	ПрочиеРасходы.ИдентификаторФинЗаписи КАК ИдентификаторСтроки,
	|	ПрочиеРасходы.ИдентификаторФинЗаписи,
	|	ПрочиеРасходы.НастройкаХозяйственнойОперации
	|
	|ПОМЕСТИТЬ ВтИсходныеПартииПрочихРасходов
	|ИЗ
	|	ВтИсходныеПрочиеРасходы КАК ПрочиеРасходы
	|";

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтПартииПрочихРасходов(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтПартииПрочихРасходов";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтИсходныеПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаВтПартииПрочихРасходов();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

Функция ТекстЗапросаТаблицаТоварыОрганизаций(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ТоварыОрганизаций";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса =
	// приход возвратных отходов в кладовую
	"ВЫБРАТЬ
	|	&Дата																				КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)												КАК ВидДвижения,
	|	ТаблицаТовары.АналитикаУчетаНоменклатуры											КАК АналитикаУчетаНоменклатуры,
	|	&Организация																		КАК Организация,
	|	ТаблицаТовары.ВидЗапасов															КАК ВидЗапасов,
	|	ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)											КАК НомерГТД,
	|	ТаблицаТовары.Количество															КАК Количество,
	|	0																					КАК КоличествоПоРНПТ,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)	КАК ХозяйственнаяОперация,
	|	&ВыпускПодДеятельность																КАК НалогообложениеНДС,
	|	&Номенклатура																		КАК Номенклатура,
	|	&Характеристика																		КАК Характеристика,
	|	ИСТИНА																				КАК Первичное
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов.ВидыЗапасов КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ОформитьПоступлениеТоваровПоОдноходовке(Запрос, ТекстыЗапроса, Регистры)

	ТекстЗапросаДокумента = 
	"ВЫБРАТЬ
	|	ИсточникДанных.Ссылка				КАК Ссылка,
	|	ИсточникДанных.Ссылка				КАК Накладная,
	|	ЛОЖЬ								КАК Исправление,
	|	НЕОПРЕДЕЛЕНО						КАК ИсправляемыйДокумент,
	|	НЕОПРЕДЕЛЕНО						КАК Заказ,
	|	НЕОПРЕДЕЛЕНО						КАК Договор,
	|	НЕОПРЕДЕЛЕНО						КАК Соглашение,
	|	ЗНАЧЕНИЕ(Перечисление.ВариантыПриемкиТоваров.РазделенаТолькоПоНакладным) КАК ВариантПриемкиТоваров,
	|	ИсточникДанных.Дата					КАК Дата,
	|	ИсточникДанных.Номенклатура			КАК Номенклатура,
	|	ИсточникДанных.Характеристика		КАК Характеристика,
	|	ИсточникДанных.Назначение			КАК Назначение,
	|	ИсточникДанных.Серия				КАК Серия,
	|	ИсточникДанных.СтатусУказанияСерий	КАК СтатусУказанияСерий,
	|	ЛОЖЬ								КАК СверхЗаказа,
	|	ИсточникДанных.Получатель			КАК Склад,
	|	ИсточникДанных.Подразделение		КАК Отправитель,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость) КАК ХозяйственнаяОперация,
	|	ИсточникДанных.Количество           КАК Количество,
	|	ИСТИНА                              КАК ЭтоНакладная,
	|	ЛОЖЬ								КАК ПоступлениеПоЗаказам,
	|	ЛОЖЬ								КАК ЭтоКорректировкаВнутриНакладной
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов КАК ИсточникДанных
	|ГДЕ
	|	ИсточникДанных.Ссылка В (&Ссылка)";
	
	СкладыСервер.ОформитьПоступлениеТоваровПоОдноходовке(Запрос, 
														ТекстыЗапроса, 
														Регистры, 
														ТекстЗапросаДокумента, 
														Метаданные.Документы.РаспределениеВозвратныхОтходов, 
														"Серии");

КонецПроцедуры

Функция ТекстЗапросаТаблицаМатериалыИРаботыВПроизводстве(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "МатериалыИРаботыВПроизводстве";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	// приход работ по фиксированной стоимости по получателю
	"ВЫБРАТЬ
	|	&Дата									КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)	КАК ВидДвижения,
	|	&Организация							КАК Организация,
	|	&Получатель								КАК Подразделение,
	|	&Номенклатура							КАК Номенклатура,
	|	&Характеристика							КАК Характеристика,
	|	&Назначение								КАК Назначение,
	|	&Количество								КАК Количество
	|ГДЕ
	|	&ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДвиженияСерийТоваров(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "ДвиженияСерийТоваров";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	&Дата																	КАК Период,
		|	&Номенклатура															КАК Номенклатура,
		|	&Характеристика															КАК Характеристика,
		|	ВЫБОР
		|		КОГДА &ДвиженияПоСкладскимРегистрам
		|			ТОГДА &Назначение
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|	КОНЕЦ																	КАК Назначение,
		|	&Серия																	КАК Серия,
		|	&Количество																КАК Количество,
		|	&Получатель																КАК Отправитель,
		|	&Получатель																КАК Получатель,
		|	ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ПриемкаПродукцииИзПроизводства)	КАК СкладскаяОперация,
		|	&Ссылка																	КАК Документ,
		|	НЕ СпрСклады.ИспользоватьОрдернуюСхемуПриПоступлении
		|		ИЛИ &Дата < СпрСклады.ДатаНачалаОрдернойСхемыПриПоступлении			КАК ЭтоСкладскоеДвижение
		|ИЗ
		|	Справочник.Склады КАК СпрСклады
		|ГДЕ
		|	НЕ &Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
		|	И СпрСклады.Ссылка = &Получатель
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&Дата																	КАК Период,
		|	&Номенклатура															КАК Номенклатура,
		|	&Характеристика															КАК Характеристика,
		|	ВЫБОР
		|		КОГДА &ДвиженияПоСкладскимРегистрам
		|			ТОГДА &Назначение
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|	КОНЕЦ																	КАК Назначение,
		|	ТаблицаТовары.Серия														КАК Серия,
		|	ТаблицаТовары.Количество												КАК Количество,
		|	&Получатель																КАК Отправитель,
		|	&Получатель																КАК Получатель,
		|	ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ПриемкаПродукцииИзПроизводства)	КАК СкладскаяОперация,
		|	&Ссылка																	КАК Документ,
		|	НЕ СпрСклады.ИспользоватьОрдернуюСхемуПриПоступлении
		|		ИЛИ &Дата < СпрСклады.ДатаНачалаОрдернойСхемыПриПоступлении			КАК ЭтоСкладскоеДвижение
		|ИЗ
		|	Документ.РаспределениеВозвратныхОтходов.Серии КАК ТаблицаТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеВозвратныхОтходов КАК Шапка
		|		ПО ТаблицаТовары.Ссылка = Шапка.Ссылка
		|			И ТаблицаТовары.Номенклатура = Шапка.Номенклатура
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК СпрСклады
		|		ПО (СпрСклады.Ссылка = &Получатель)
		|ГДЕ
		|	ТаблицаТовары.Ссылка = &Ссылка
		|	И НЕ ТаблицаТовары.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)";

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаДатыПоступленияТоваровОрганизаций(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДатыПоступленияТоваровОрганизаций";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса =
	// приход возвратных отходов в кладовую
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Дата										КАК ДатаПоступления,
	|	&Номенклатура								КАК Номенклатура,
	|	&Характеристика								КАК Характеристика,
	|	&Серия										КАК Серия,
	|	&Назначение									КАК Назначение,
	|	ВидыЗапасов.ВидЗапасов						КАК ВидЗапасов,
	|	ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)	КАК НомерГТД
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов.ВидыЗапасов КАК ВидыЗапасов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДатыПоступленияТоваровОрганизаций КАК ПоступленияТоваров
	|		ПО ВидыЗапасов.ВидЗапасов = ПоступленияТоваров.ВидЗапасов
	|			И &Номенклатура = ПоступленияТоваров.Номенклатура
	|			И &Характеристика = ПоступленияТоваров.Характеристика
	|			И &Серия = ПоступленияТоваров.Серия
	|			И &Назначение = ПоступленияТоваров.Назначение
	|			И (ПоступленияТоваров.НомерГТД = ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка))
	|ГДЕ
	|	ВидыЗапасов.Ссылка = &Ссылка
	|	И (ПоступленияТоваров.ВидЗапасов ЕСТЬ NULL 
	|			ИЛИ ПоступленияТоваров.ДатаПоступления < &Дата)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка						КАК Ссылка,
	|	НЕОПРЕДЕЛЕНО				КАК РазделительЗаписи,
	|	&Дата						КАК ДатаДокументаИБ,
	|	&Номер						КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных	КАК ТипСсылки,
	|	&Организация				КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции) КАК ХозяйственнаяОперация,
	|	&НаправлениеДеятельности	КАК НаправлениеДеятельности,
	|	&Подразделение				КАК Подразделение,
	|	&Ответственный				КАК Ответственный,
	|	&Автор						КАК Автор,
	|	&Комментарий				КАК Комментарий,
	|	0							КАК Сумма,
	|	&Проведен					КАК Проведен,
	|	&ПометкаУдаления			КАК ПометкаУдаления,
	|	ЛОЖЬ						КАК ДополнительнаяЗапись,
	|	""""						КАК Дополнительно,
	|	&Дата						КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать				КАК НомерПервичногоДокумента,
	|	&Подразделение				КАК МестоХранения,
	|	&Дата						КАК ДатаОтраженияВУчете,
	|	НЕОПРЕДЕЛЕНО                КАК Приоритет
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура СформироватьСуммыДокументаВВалютахУчета(Запрос, ТекстыЗапроса, Регистры = Неопределено) Экспорт
	
	ТекстЗапросаДанных = 
	"ВЫБРАТЬ
	|	""ПрочиеРасходы"" КАК ИсточникДанных,
	|	ЛОЖЬ КАК РаспределятьОбщуюСумму,
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.Ссылка.Дата КАК Дата,
	|	ТаблицаДокумента.Ссылка.Организация КАК Организация,
	|	ТаблицаДокумента.Ссылка.Валюта КАК ВалютаДокумента,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаВзаиморасчетов,
	|	НЕОПРЕДЕЛЕНО КАК ПериодБазыНДС,
	|	ТаблицаДокумента.Ссылка.Дата КАК ДатаКурса,
	|
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ВЫРАЗИТЬ(ТаблицаДокумента.Ссылка.Цена * ТаблицаДокумента.Количество КАК ЧИСЛО(31, 2)) КАК СуммаБезНДС,
	|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС,
	|	0 КАК СуммаНДС,
	|	0 КАК СуммаВзаиморасчетов,
	|	0 КАК СуммаНДСВзаиморасчетов,
	|	0 КАК СуммаБезНДСРегл,
	|	0 КАК СуммаБезНДСУпр,
	|
	|	ЛОЖЬ КАК ОтражаетсяВРасчетах,
	|	НЕОПРЕДЕЛЕНО КАК ОбъектРасчетов,
	|	ЛОЖЬ КАК ПересчитыватьПоДаннымРасчетов
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов.ПрочиеРасходы КАК ТаблицаДокумента
	|
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В (&Ссылка)
	|";
	
	РегистрыСведений.СуммыДокументовВВалютахУчета.СформироватьПоДаннымДокумента(
		Запрос, ТекстыЗапроса, Регистры, ТекстЗапросаДанных);

КонецПроцедуры

Функция ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеРасходы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ДополнительныеПоля = "";
	РасчетСебестоимостиЛокализация.ДобавитьДополнительныеПоляПоНДД(ДополнительныеПоля);
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаПрочиеРасходы(ДополнительныеПоля);
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПартииПрочихРасходов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПартииПрочихРасходов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПартииПрочихРасходов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаПартииПрочихРасходов();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеАктивыПассивы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Дата																			КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)											КАК ВидДвижения,
	|	&Организация																	КАК Организация,
	|	ВидыЗапасов.Подразделение														КАК Подразделение,
	|	&НаправлениеДеятельности 														КАК НаправлениеДеятельности,
	|	ВидыЗапасов.СтатьяРасходов														КАК Статья,
	|	ВЫБОР
	|		КОГДА ВидыЗапасов.АналитикаАктивовПассивов = ЗНАЧЕНИЕ(Перечисление.СтатьиБезАналитики.ПустаяСсылка)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ВидыЗапасов.АналитикаАктивовПассивов
	|	КОНЕЦ 																			КАК Аналитика,
	|	ВЫРАЗИТЬ(ВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))	КАК Сумма
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов.ВидыЗапасов КАК ВидыЗапасов
	|ГДЕ
	|	ВидыЗапасов.Ссылка = &Ссылка
	|	И ТИПЗНАЧЕНИЯ(ВидыЗапасов.СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиАктивовПассивов)
	|	И НЕ ВидыЗапасов.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПустаяСсылка)
	|";
	ТекстЗапроса = ТекстЗапроса + "ОБЪЕДИНИТЬ ВСЕ"
		+ РегистрыНакопления.ПрочиеАктивыПассивы.ТекстЗапросаТаблицаПрочиеАктивыПассивы(Ложь);
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДвиженияПоПрочимАктивамПассивам(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДвиженияПоПрочимАктивамПассивам";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Дата КАК Период,
	|	&Организация КАК Организация,
	|	ВидыЗапасов.Подразделение КАК Подразделение,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВидыЗапасов.СтатьяРасходов КАК Статья,
	|	ВидыЗапасов.АналитикаАктивовПассивов КАК Аналитика,
	|	ВидыЗапасов.НастройкаСчетовУчета КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Дебет) КАК ДебетКредит,
	|
	|	ВЫРАЗИТЬ(ВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2)) КАК СуммаРегл,
	|	ВЫРАЗИТЬ(ВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)) КАК СуммаУпр,
	|	ВЫРАЗИТЬ(ВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)) КАК СуммаСНДС,
	|	ВЫРАЗИТЬ(ВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)) КАК СуммаБезНДС,
	|
	|	ВидыЗапасов.ИдентификаторФинЗаписи	КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ВыпускПродукцииФиксированнаяСтоимость) КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов.ВидыЗапасов КАК ВидыЗапасов
	|ГДЕ
	|	ВидыЗапасов.Ссылка = &Ссылка
	|	И ТИПЗНАЧЕНИЯ(ВидыЗапасов.СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиАктивовПассивов)
	|	И НЕ ВидыЗапасов.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПустаяСсылка)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ЗапланироватьПоступлениеТоваров(Запрос, ТекстыЗапроса, Регистры)
	
	ТекстЗапросаДокумента = 
	"ВЫБРАТЬ
	|	ИсточникДанных.Ссылка 				КАК Ссылка,
	|	ИсточникДанных.Ссылка               КАК Накладная,
	|	ЛОЖЬ                                КАК Исправление,
	|	НЕОПРЕДЕЛЕНО                        КАК ИсправляемыйДокумент,
	|	НЕОПРЕДЕЛЕНО	      				КАК Заказ,
	|	НЕОПРЕДЕЛЕНО               			КАК Договор,
	|	НЕОПРЕДЕЛЕНО             			КАК Соглашение,
	|	ЗНАЧЕНИЕ(Перечисление.ВариантыПриемкиТоваров.РазделенаТолькоПоНакладным) КАК ВариантПриемкиТоваров,
	|	
	|	ВЫБОР КОГДА ИсточникДанных.ДатаПоступления <> ДАТАВРЕМЯ(1, 1, 1) ТОГДА
	|				ИсточникДанных.ДатаПоступления
	|			ИНАЧЕ
	|				ИсточникДанных.Дата
	|		КОНЕЦ КАК Дата,
	|	
	|	ИсточникДанных.Номенклатура         КАК Номенклатура,
	|	ИсточникДанных.Характеристика       КАК Характеристика,
	|	ИсточникДанных.Назначение           КАК Назначение,
	|	ИсточникДанных.Серия                КАК Серия,
	|	ИсточникДанных.СтатусУказанияСерий  КАК СтатусУказанияСерий,
	|	ЛОЖЬ   								КАК СверхЗаказа,
	|	ИсточникДанных.Получатель           КАК Склад,
	|	ИсточникДанных.Подразделение        КАК Отправитель,
	|	ИсточникДанных.Количество           КАК Количество,
	|	ИСТИНА                              КАК ЭтоНакладная,
	|	ЛОЖЬ   								КАК ПоступлениеПоЗаказам,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)  КАК ХозяйственнаяОперация
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов КАК ИсточникДанных
	|ГДЕ
	|	ИсточникДанных.Ссылка В (&Ссылка)";
	
	СкладыСервер.ЗапланироватьПоступлениеТоваров(Запрос, ТекстыЗапроса, Регистры, ТекстЗапросаДокумента);
	
КонецПроцедуры

Процедура ОтразитьРаспределениеЗапасовДвижения(Запрос, ТекстыЗапроса, Регистры)
	
	ТекстЗапросаТабЧасть =
		// Приход на неордерном складе или по старым назначениям.
		"ВЫБРАТЬ
		|	ТабЧасть.Ссылка         КАК Ссылка,
		|	ТабЧасть.Дата           КАК Период,
		|	ТабЧасть.Номенклатура   КАК Номенклатура,
		|	ТабЧасть.Характеристика КАК Характеристика,
		|	ТабЧасть.Получатель     КАК Склад,
		|	ТабЧасть.Назначение     КАК Назначение,
		|	ТабЧасть.Количество     КАК Количество,
		|	ЛОЖЬ                    КАК ПоГрафику,
		|	НЕОПРЕДЕЛЕНО            КАК РаспоряжениеВГрафике
		|ИЗ
		|	Документ.РаспределениеВозвратныхОтходов КАК ТабЧасть
		|ГДЕ
		|	НЕ ТабЧасть.Получатель ССЫЛКА Справочник.Склады
		|		ИЛИ НЕ ТабЧасть.Получатель.ИспользоватьОрдернуюСхемуПриПоступлении
		|		ИЛИ ТабЧасть.Получатель.ДатаНачалаОрдернойСхемыПриПоступлении > ТабЧасть.Ссылка.Дата
		|		ИЛИ ТабЧасть.Назначение.ДвиженияПоСкладскимРегистрам = ЛОЖЬ";
	
	РаспределениеЗапасовДвижения.ПриходЗапаса(Запрос, ТекстыЗапроса, Регистры, ТекстЗапросаТабЧасть);
	
	ТекстЗапросаТабЧасть =
		// Сторно приходного ордера по старым назначениям.
		"ВЫБРАТЬ
		|	ТабЧасть.Ссылка КАК Ссылка,
		|	ТабЧасть.Дата КАК Период,
		|	ТабЧасть.Номенклатура КАК Номенклатура,
		|	ТабЧасть.Характеристика КАК Характеристика,
		|	ТабЧасть.Получатель КАК Склад,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
		|	-ТабЧасть.Количество КАК Количество,
		|	ЛОЖЬ КАК ПоГрафику,
		|	НЕОПРЕДЕЛЕНО КАК РаспоряжениеВГрафике
		|ИЗ
		|	Документ.РаспределениеВозвратныхОтходов КАК ТабЧасть
		|ГДЕ
		|	ТабЧасть.Назначение.ДвиженияПоСкладскимРегистрам = ЛОЖЬ
		|		И ТабЧасть.Получатель.ИспользоватьОрдернуюСхемуПриПоступлении
		|		И ТабЧасть.Получатель.ДатаНачалаОрдернойСхемыПриПоступлении <= ТабЧасть.Ссылка.Дата";
	
	РаспределениеЗапасовДвижения.ПриходЗапаса(Запрос, ТекстыЗапроса, Регистры, ТекстЗапросаТабЧасть);
	
	ТекстЗапросаТабЧасть =
		"ВЫБРАТЬ
		|	ТабЧасть.Ссылка         КАК Ссылка,
		|	ТабЧасть.Дата           КАК Период,
		|	ТабЧасть.Номенклатура   КАК Номенклатура,
		|	ТабЧасть.Характеристика КАК Характеристика,
		|	ТабЧасть.Получатель     КАК Склад,
		|	
		|	ТабЧасть.Назначение     КАК Назначение,
		|	
		|	ТабЧасть.Количество     КАК Количество,
		|	ТабЧасть.Ссылка         КАК Заказ,
		|	ТабЧасть.Дата           КАК ДатаПоступления,
		|	ИСТИНА                  КАК ДоступенДляРасхода,
		|	ЛОЖЬ                    КАК ПоГрафику,
		|	НЕОПРЕДЕЛЕНО            КАК РаспоряжениеВГрафике,
		|	0                       КАК КоличествоВГрафике
		|ИЗ
		|	Документ.РаспределениеВозвратныхОтходов КАК ТабЧасть
		|ГДЕ
		|	ТабЧасть.Получатель.ИспользоватьОрдернуюСхемуПриПоступлении
		|		И ТабЧасть.Получатель.ДатаНачалаОрдернойСхемыПриПоступлении <= ТабЧасть.Ссылка.Дата";
	
	РаспределениеЗапасовДвижения.ЗапланироватьПриходЗапаса(Запрос, ТекстыЗапроса, Регистры, ТекстЗапросаТабЧасть);
	
КонецПроцедуры

#Область ПартионныйУчет

Функция ОписаниеРегистровУчетаЗатратИСебестоимости(Документ) Экспорт
	
	ОписаниеРегистров = Новый Массив;
	ОписаниеРегистров.Добавить(Метаданные.РегистрыНакопления.СебестоимостьТоваров);
	
	Возврат ОписаниеРегистров;
	
КонецФункции

Функция УстановитьДополнительныеПараметрыОперацийУчетаЗатратИСебестоимости(Документ, Запрос = Неопределено) Экспорт
	
	ДополнительныеПараметры = Новый Массив;
	ДополнительныеПараметры.Добавить("КоэффициентПересчетаВВалютуУпр");
	ДополнительныеПараметры.Добавить("КоэффициентПересчетаВВалютуРегл");
	
	Если Запрос <> Неопределено Тогда
		УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос);
		ИнициализироватьКлючиАналитикиНоменклатуры(Запрос);
	КонецЕсли;
	
	Возврат ДополнительныеПараметры;
	
КонецФункции

Функция СформироватьДополнительныеТаблицыОперацийУчетаЗатратИСебестоимости(Документ, Запрос = Неопределено, ТекстыЗапроса = Неопределено) Экспорт
	
	ДополнительныеТаблицы = Новый Массив;
	
	Если Запрос <> Неопределено Тогда
		// Нет дополнительных таблиц.
	КонецЕсли;
	
	Возврат ДополнительныеТаблицы;
	
КонецФункции

Функция ОписаниеОперацийУчетаСебестоимости(Документ) Экспорт
	
	ОписаниеОпераций = Новый Массив;
	
	// Формируются движения по отражению возвратного отхода на складе.
	#Область ВнутреннееПоступление_ТоварПриходНаСклад
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	// Описание документа
	|	ТаблицаДокумента.Дата 	КАК Период,
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	НЕОПРЕДЕЛЕНО			КАК ДокументИсточник,
	|	НЕОПРЕДЕЛЕНО			КАК ПериодДокументаИсточника,
	|	НЕОПРЕДЕЛЕНО 			КАК КорректируемыйДокумент,
	|
	// Поля учета номенклатуры
	|	ТаблицаДокумента.Организация 					КАК Организация,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.ВидЗапасов 					КАК ВидЗапасов,
	|	ТаблицаДокумента.Ссылка							КАК Партия,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(АналитикаПоПолучателю.Назначение.ВидДеятельностиНДС,ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)) <>
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|			ТОГДА АналитикаПоПолучателю.Назначение.ВидДеятельностиНДС
	|		КОГДА ТаблицаДокумента.ВыпускПодДеятельность = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|			ТОГДА &НалогообложениеОрганизации
	|		ИНАЧЕ ТаблицаДокумента.ВыпускПодДеятельность
	|	КОНЕЦ											КАК ВидДеятельностиНДС,
	|	(ВЫБОР
	|		КОГДА ТаблицаДокумента.ВыпускПодДеятельность = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|			ТОГДА &НалогообложениеОрганизации
	|		ИНАЧЕ ТаблицаДокумента.ВыпускПодДеятельность КОНЕЦ) КАК ВидДеятельностиНДСДокумента,
	|
	// Поля аналитики учета партий
	|	НЕОПРЕДЕЛЕНО 	КАК АналитикаУчетаПартий,
	|	НЕОПРЕДЕЛЕНО	КАК КорАналитикаУчетаПартий,
	|	0				КАК КодСтроки,
	|
	// Корреспондирующие поля
	|	НЕОПРЕДЕЛЕНО						КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО						КАК КорВидЗапасов,
	|	НЕОПРЕДЕЛЕНО						КАК КорГруппаПродукции,
	|
	// Поля аналитики финансового учета
	|	НЕОПРЕДЕЛЕНО											КАК Сделка,
	|	ТаблицаДокумента.Подразделение							КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО											КАК Менеджер,
	|	ТаблицаДокумента.Номенклатура.ГруппаАналитическогоУчета	КАК ГруппаПродукции,
	|
	// Количественные и суммовые показатели
	|	ТаблицаВидыЗапасов.Количество 						 								  КАК Количество,
	|	НЕОПРЕДЕЛЕНО 																		  КАК ИдентификаторСтроки,
	|	(ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПолуфабрикатДавальца)
	|		 И СпрСклады.ЦеховаяКладовая
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0 КОНЕЦ)																	  КАК Стоимость,
	|	(ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПолуфабрикатДавальца)
	|		 И СпрСклады.ЦеховаяКладовая
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0 КОНЕЦ)																	  КАК СтоимостьБезНДС,
	|	(ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПолуфабрикатДавальца)
	|		 И СпрСклады.ЦеховаяКладовая
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0 КОНЕЦ)																	  КАК СтоимостьРегл,
	|	0                                   				 								  КАК НДСРегл,
	|	(ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПолуфабрикатДавальца)
	|		 И СпрСклады.ЦеховаяКладовая
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0 КОНЕЦ)																	  КАК СтоимостьУпр,
	|	0 													 								  КАК НДСУпр,
	|	(ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПолуфабрикатДавальца)
	|		 И СпрСклады.ЦеховаяКладовая
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0 КОНЕЦ)																	  КАК СтоимостьЗабалансовая,
	|	(ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПолуфабрикатДавальца)
	|		 И СпрСклады.ЦеховаяКладовая
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0 КОНЕЦ)																	  КАК СтоимостьЗабалансоваяРегл,
	|	0													 								  КАК ДопРасходы,
	|	0													 								  КАК ДопРасходыБезНДС,
	|	0																					  КАК ДопРасходыРегл,
	|	0																					  КАК ДопРасходыУпр,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаВидыЗапасов.СтатьяРасходов.ПринятиеКНалоговомуУчету, ИСТИНА)
	|			ТОГДА 0
	|		КОГДА ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПолуфабрикатДавальца)
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(ТаблицаВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|	КОНЕЦ																				  КАК ПостояннаяРазница,
	|
	|	0 													 								  КАК ВременнаяРазница,
	|	0													 								  КАК КорСтоимость,
	|
	// Прочие поля
	|	(ВЫБОР
	|		КОГДА НЕ СпрСклады.ЦеховаяКладовая
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаПродукцииИзПроизводства)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость) КОНЕЦ) КАК ХозяйственнаяОперация,
	|	ТаблицаВидыЗапасов.ИдентификаторФинЗаписи  	 					  						  КАК ИдентификаторФинЗаписи,
	|	(ВЫБОР
	|		КОГДА НЕ СпрСклады.ЦеховаяКладовая
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПередачаПродукцииИзПроизводства)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ВыпускПродукцииФиксированнаяСтоимость) КОНЕЦ) КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов КАК ТаблицаДокумента
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеВозвратныхОтходов.ВидыЗапасов КАК ТаблицаВидыЗапасов
	|		ПО ТаблицаДокумента.Ссылка = ТаблицаВидыЗапасов.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикаПоПолучателю
	|		ПО АналитикаПоПолучателю.Ссылка = ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК СпрСклады
	|		ПО СпрСклады.Ссылка = АналитикаПоПолучателю.МестоХранения
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В (&Ссылка)";
	
	РасчетСебестоимостиПроведениеДокументов.ДобавитьОписаниеОперацииДляОтраженияВУчетеСебестоимости(
		ОписаниеОпераций,
		Перечисления.ОперацииУчетаСебестоимости.ВнутреннееПоступление,
		ТекстЗапроса);
	
	#КонецОбласти

	// Формируются движения по отражению возвратного отхода в подразделении (при выпуске на оптовый склад).
	#Область ВнутреннееПоступление_ТоварПриходВПодразделение
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	// Описание документа
	|	ТаблицаДокумента.Дата 	КАК Период,
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	НЕОПРЕДЕЛЕНО			КАК ДокументИсточник,
	|	НЕОПРЕДЕЛЕНО			КАК ПериодДокументаИсточника,
	|	НЕОПРЕДЕЛЕНО 			КАК КорректируемыйДокумент,
	|
	// Поля учета номенклатуры
	|	ТаблицаДокумента.Организация 					КАК Организация,
	|	АналитикаПоПодразделению.КлючАналитики			КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.ВидЗапасов 					КАК ВидЗапасов,
	|	ТаблицаДокумента.Ссылка							КАК Партия,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(АналитикаПоПолучателю.Назначение.ВидДеятельностиНДС,ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)) <>
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|			ТОГДА АналитикаПоПолучателю.Назначение.ВидДеятельностиНДС
	|		КОГДА ТаблицаДокумента.ВыпускПодДеятельность = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|			ТОГДА &НалогообложениеОрганизации
	|		ИНАЧЕ ТаблицаДокумента.ВыпускПодДеятельность
	|	КОНЕЦ											КАК ВидДеятельностиНДС,
	|	(ВЫБОР
	|		КОГДА ТаблицаДокумента.ВыпускПодДеятельность = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|			ТОГДА &НалогообложениеОрганизации
	|		ИНАЧЕ ТаблицаДокумента.ВыпускПодДеятельность КОНЕЦ) КАК ВидДеятельностиНДСДокумента,
	|
	// Поля аналитики учета партий
	|	НЕОПРЕДЕЛЕНО 	КАК АналитикаУчетаПартий,
	|	НЕОПРЕДЕЛЕНО	КАК КорАналитикаУчетаПартий,
	|	0				КАК КодСтроки,
	|
	// Корреспондирующие поля
	|	НЕОПРЕДЕЛЕНО						КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО						КАК КорВидЗапасов,
	|	НЕОПРЕДЕЛЕНО						КАК КорГруппаПродукции,
	|
	// Поля аналитики финансового учета
	|	НЕОПРЕДЕЛЕНО											КАК Сделка,
	|	ТаблицаДокумента.Подразделение							КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО											КАК Менеджер,
	|	ТаблицаДокумента.Номенклатура.ГруппаАналитическогоУчета	КАК ГруппаПродукции,
	|
	// Количественные и суммовые показатели
	|	ТаблицаВидыЗапасов.Количество 						 								  КАК Количество,
	|	НЕОПРЕДЕЛЕНО 																		  КАК ИдентификаторСтроки,
	|	(ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПолуфабрикатДавальца)
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0 КОНЕЦ)																	  КАК Стоимость,
	|	(ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПолуфабрикатДавальца)
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0 КОНЕЦ)																	  КАК СтоимостьБезНДС,
	|	(ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПолуфабрикатДавальца)
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0 КОНЕЦ)																	  КАК СтоимостьРегл,
	|	0                                   				 								  КАК НДСРегл,
	|	(ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПолуфабрикатДавальца)
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0 КОНЕЦ)																	  КАК СтоимостьУпр,
	|	0 													 								  КАК НДСУпр,
	|	(ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПолуфабрикатДавальца)
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0 КОНЕЦ)																	  КАК СтоимостьЗабалансовая,
	|	(ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПолуфабрикатДавальца)
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0 КОНЕЦ)																	  КАК СтоимостьЗабалансоваяРегл,
	|	0													 								  КАК ДопРасходы,
	|	0													 								  КАК ДопРасходыБезНДС,
	|	0																					  КАК ДопРасходыРегл,
	|	0																					  КАК ДопРасходыУпр,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаВидыЗапасов.СтатьяРасходов.ПринятиеКНалоговомуУчету, ИСТИНА)
	|			ТОГДА 0
	|		КОГДА ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПолуфабрикатДавальца)
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(ТаблицаВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|	КОНЕЦ																				  КАК ПостояннаяРазница,
	|
	|	0 													 								  КАК ВременнаяРазница,
	|	0													 								  КАК КорСтоимость,
	|
	// Прочие поля
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость) 		  КАК ХозяйственнаяОперация,
	|	ТаблицаВидыЗапасов.ИдентификаторФинЗаписи  	 					  						  КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ВыпускПродукцииФиксированнаяСтоимость) КАК НастройкаХозяйственнойОперации,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Дополнение)										  КАК ТипЗаписи
	|
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов КАК ТаблицаДокумента
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеВозвратныхОтходов.ВидыЗапасов КАК ТаблицаВидыЗапасов
	|		ПО ТаблицаДокумента.Ссылка = ТаблицаВидыЗапасов.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикаПоПолучателю
	|		ПО АналитикаПоПолучателю.Ссылка = ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК СпрСклады
	|			ПО СпрСклады.Ссылка = АналитикаПоПолучателю.МестоХранения
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаПоПодразделению
	|			ПО АналитикаПоПолучателю.Номенклатура = АналитикаПоПодразделению.Номенклатура
	|			И АналитикаПоПолучателю.Характеристика = АналитикаПоПодразделению.Характеристика
	|			И ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) = АналитикаПоПодразделению.Серия
	|			И &Подразделение = АналитикаПоПодразделению.МестоХранения
	|			И АналитикаПоПолучателю.Назначение = АналитикаПоПодразделению.Назначение
	|			И АналитикаПоПолучателю.СтатьяКалькуляции = АналитикаПоПодразделению.СтатьяКалькуляции
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В (&Ссылка)
	|	И НЕ СпрСклады.ЦеховаяКладовая
	|";
	
	РасчетСебестоимостиПроведениеДокументов.ДобавитьОписаниеОперацииДляОтраженияВУчетеСебестоимости(
		ОписаниеОпераций,
		Перечисления.ОперацииУчетаСебестоимости.ВнутреннееПоступление,
		ТекстЗапроса);
	
	#КонецОбласти

	// Формируются движения по списанию из подразделения (при выпуске на оптовый склад).
	#Область СписаниеНаДругиеПартии_ТоварПередачаНаСклад
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	// Описание документа
	|	ТаблицаДокумента.Дата	КАК Период,
	|	ТаблицаДокумента.Ссылка	КАК Ссылка,
	|	НЕОПРЕДЕЛЕНО 			КАК КорректируемыйДокумент,
	|
	// Поля учета номенклатуры
	|	ТаблицаДокумента.Организация 					КАК Организация,
	|	АналитикаПоПодразделению.КлючАналитики			КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.ВидЗапасов 					КАК ВидЗапасов,
	|	ТаблицаДокумента.Ссылка							КАК Партия,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(АналитикаПоПолучателю.Назначение.ВидДеятельностиНДС,ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)) <>
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|			ТОГДА АналитикаПоПолучателю.Назначение.ВидДеятельностиНДС
	|		КОГДА ТаблицаДокумента.ВыпускПодДеятельность = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|			ТОГДА &НалогообложениеОрганизации
	|		ИНАЧЕ ТаблицаДокумента.ВыпускПодДеятельность
	|	КОНЕЦ											КАК ВидДеятельностиНДС,
	|	(ВЫБОР
	|		КОГДА ТаблицаДокумента.ВыпускПодДеятельность = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|			ТОГДА &НалогообложениеОрганизации
	|		ИНАЧЕ ТаблицаДокумента.ВыпускПодДеятельность КОНЕЦ) КАК ВидДеятельностиНДСДокумента,
	|
	// Поля аналитики учета партий
	|	НЕОПРЕДЕЛЕНО 									КАК АналитикаУчетаПартий,
	|
	// Корреспондирующие поля
	|	НЕОПРЕДЕЛЕНО									КАК КорАналитикаУчетаПартий,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры	КАК КорАналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.ВидЗапасов					КАК КорВидЗапасов,
	|	НЕОПРЕДЕЛЕНО									КАК КорОрганизация,
	|	ТаблицаДокумента.Ссылка							КАК КорПартия,
	|
	// Поля аналитики финансового учета
	|	НЕОПРЕДЕЛЕНО											КАК Сделка,
	|	ТаблицаДокумента.Подразделение							КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО											КАК Менеджер,
	|	ТаблицаДокумента.Номенклатура.ГруппаАналитическогоУчета	КАК ГруппаПродукции,
	|
	// Количественные и суммовые показатели
	|	ТаблицаВидыЗапасов.Количество КАК Количество,
	|
	// Прочие поля
	|	НЕОПРЕДЕЛЕНО 																		КАК ИдентификаторСтроки,
	|	ТаблицаВидыЗапасов.ИдентификаторФинЗаписи  	 					  					КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаПродукцииИзПроизводства)		КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПередачаПродукцииИзПроизводства)	КАК НастройкаХозяйственнойОперации,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Дополнение)									КАК ТипЗаписи,
	|	ТаблицаДокумента.Ссылка	КАК ДокументДвижения
	|
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов КАК ТаблицаДокумента
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеВозвратныхОтходов.ВидыЗапасов КАК ТаблицаВидыЗапасов
	|		ПО ТаблицаДокумента.Ссылка = ТаблицаВидыЗапасов.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикаПоПолучателю
	|		ПО АналитикаПоПолучателю.Ссылка = ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК СпрСклады
	|		ПО СпрСклады.Ссылка = АналитикаПоПолучателю.МестоХранения
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаПоПодразделению
	|		ПО АналитикаПоПолучателю.Номенклатура = АналитикаПоПодразделению.Номенклатура
	|		И АналитикаПоПолучателю.Характеристика = АналитикаПоПодразделению.Характеристика
	|		И ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) = АналитикаПоПодразделению.Серия
	|		И &Подразделение = АналитикаПоПодразделению.МестоХранения
	|		И АналитикаПоПолучателю.Назначение = АналитикаПоПодразделению.Назначение
	|		И АналитикаПоПолучателю.СтатьяКалькуляции = АналитикаПоПодразделению.СтатьяКалькуляции
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В (&Ссылка)
	|	И НЕ СпрСклады.ЦеховаяКладовая
	|";

	РасчетСебестоимостиПроведениеДокументов.ДобавитьОписаниеОперацииДляОтраженияВУчетеСебестоимости(
		ОписаниеОпераций,
		Перечисления.ОперацииУчетаСебестоимости.СписаниеНаДругиеПартии,
		ТекстЗапроса);

	#КонецОбласти

	#Область ВнутреннееПоступление_Товар_ПоПартииПроизводства
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	// Описание документа
	|	ТаблицаДокумента.Дата 	КАК Период,
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	НЕОПРЕДЕЛЕНО			КАК ДокументИсточник,
	|	НЕОПРЕДЕЛЕНО			КАК ПериодДокументаИсточника,
	|	НЕОПРЕДЕЛЕНО 			КАК КорректируемыйДокумент,
	|
	// Поля учета номенклатуры
	|	ТаблицаДокумента.Организация 					КАК Организация,
	|	АналитикаНоменклатурыПоПартии.КлючАналитики		КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.ВидЗапасов 					КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.ПартияПроизводства			КАК Партия,
	|	СпрПартииПроизводства.ВидДеятельностиНДС		КАК ВидДеятельностиНДС,
	|	(ВЫБОР
	|		КОГДА ТаблицаДокумента.ВыпускПодДеятельность = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|			ТОГДА &НалогообложениеОрганизации
	|		ИНАЧЕ ТаблицаДокумента.ВыпускПодДеятельность КОНЕЦ) КАК ВидДеятельностиНДСДокумента,
	|
	// Поля аналитики учета партий
	|	НЕОПРЕДЕЛЕНО 	КАК АналитикаУчетаПартий,
	|	НЕОПРЕДЕЛЕНО	КАК КорАналитикаУчетаПартий,
	|	0				КАК КодСтроки,
	|
	// Корреспондирующие поля
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры			КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО											КАК КорВидЗапасов,
	|	ТаблицаДокумента.Номенклатура.ГруппаАналитическогоУчета	КАК КорГруппаПродукции,
	|
	// Поля аналитики финансового учета
	|	НЕОПРЕДЕЛЕНО											КАК Сделка,
	|	ТаблицаДокумента.Подразделение							КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО											КАК Менеджер,
	|	СпрПартииПроизводства.ГруппаПродукции					КАК ГруппаПродукции,
	|
	// Количественные и суммовые показатели
	|	-ТаблицаВидыЗапасов.Количество 						 								   КАК Количество,
	|	НЕОПРЕДЕЛЕНО 																		   КАК ИдентификаторСтроки,
	|	-(ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПолуфабрикатДавальца)
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0 КОНЕЦ)																	  КАК Стоимость,
	|	-(ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПолуфабрикатДавальца)
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0 КОНЕЦ)																	  КАК СтоимостьБезНДС,
	|	-(ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПолуфабрикатДавальца)
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0 КОНЕЦ)																	  КАК СтоимостьРегл,
	|	0                                   				 								  КАК НДСРегл,
	|	-(ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПолуфабрикатДавальца)
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0 КОНЕЦ)																	  КАК СтоимостьУпр,
	|	0 													 								  КАК НДСУпр,
	|	-(ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПолуфабрикатДавальца)
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0 КОНЕЦ)																	  КАК СтоимостьЗабалансовая,
	|	-(ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПолуфабрикатДавальца)
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0 КОНЕЦ)																	  КАК СтоимостьЗабалансоваяРегл,
	|	0													 								   КАК ДопРасходы,
	|	0													 								   КАК ДопРасходыБезНДС,
	|	0																					   КАК ДопРасходыРегл,
	|	0																					   КАК ДопРасходыУпр,
	|	0 												 								  	   КАК ПостояннаяРазница,
	|	0 													 								   КАК ВременнаяРазница,
	|	0													 								   КАК КорСтоимость,
	|
	// Прочие поля
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость) 		  КАК ХозяйственнаяОперация,
	|	ТаблицаВидыЗапасов.ИдентификаторФинЗаписи  	 					  						  КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ВыпускПродукцииФиксированнаяСтоимость) КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов КАК ТаблицаДокумента
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеВозвратныхОтходов.ВидыЗапасов КАК ТаблицаВидыЗапасов
	|		ПО ТаблицаДокумента.Ссылка = ТаблицаВидыЗапасов.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК СпрПартииПроизводства
	|		ПО СпрПартииПроизводства.Ссылка = ТаблицаВидыЗапасов.ПартияПроизводства
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикаПоПолучателю
	|		ПО АналитикаПоПолучателю.Ссылка = ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаНоменклатурыПоПартии
	|		ПО АналитикаНоменклатурыПоПартии.Номенклатура = &Номенклатура
	|			И АналитикаНоменклатурыПоПартии.Характеристика = &Характеристика
	|			И АналитикаНоменклатурыПоПартии.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|			И АналитикаНоменклатурыПоПартии.МестоХранения = &Подразделение
	|			И АналитикаНоменклатурыПоПартии.Назначение = СпрПартииПроизводства.Назначение
	|			И АналитикаНоменклатурыПоПартии.СтатьяКалькуляции = ТаблицаВидыЗапасов.СтатьяКалькуляции
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В (&Ссылка)";
	
	РасчетСебестоимостиПроведениеДокументов.ДобавитьОписаниеОперацииДляОтраженияВУчетеСебестоимости(
		ОписаниеОпераций,
		Перечисления.ОперацииУчетаСебестоимости.ВнутреннееПоступление,
		ТекстЗапроса);
	
	#КонецОбласти
	
	#Область ВнутреннееПоступление_Работа
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	// Описание документа
	|	ТаблицаДокумента.Дата 	КАК Период,
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	НЕОПРЕДЕЛЕНО			КАК ДокументИсточник,
	|	НЕОПРЕДЕЛЕНО			КАК ПериодДокументаИсточника,
	|	НЕОПРЕДЕЛЕНО 			КАК КорректируемыйДокумент,
	|
	// Поля учета номенклатуры
	|	ТаблицаДокумента.Организация 					КАК Организация,
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры		КАК АналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО 									КАК ВидЗапасов,
	|	ТаблицаДокумента.Ссылка							КАК Партия,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаДокумента.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС, ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)) <>
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|			ТОГДА ТаблицаДокумента.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС
	|		КОГДА ТаблицаДокумента.ВыпускПодДеятельность = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|			ТОГДА &НалогообложениеОрганизации
	|		ИНАЧЕ ТаблицаДокумента.ВыпускПодДеятельность
	|	КОНЕЦ											КАК ВидДеятельностиНДС,
	|	(ВЫБОР
	|		КОГДА ТаблицаДокумента.ВыпускПодДеятельность = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|			ТОГДА &НалогообложениеОрганизации
	|		ИНАЧЕ ТаблицаДокумента.ВыпускПодДеятельность КОНЕЦ) КАК ВидДеятельностиНДСДокумента,
	|
	// Поля аналитики учета партий
	|	НЕОПРЕДЕЛЕНО 	КАК АналитикаУчетаПартий,
	|	НЕОПРЕДЕЛЕНО	КАК КорАналитикаУчетаПартий,
	|	0				КАК КодСтроки,
	|
	// Корреспондирующие поля
	|	НЕОПРЕДЕЛЕНО						КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО						КАК КорВидЗапасов,
	|	НЕОПРЕДЕЛЕНО						КАК КорГруппаПродукции,
	|
	// Поля аналитики финансового учета
	|	НЕОПРЕДЕЛЕНО											КАК Сделка,
	|	ТаблицаДокумента.Подразделение							КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО											КАК Менеджер,
	|	ТаблицаДокумента.Номенклатура.ГруппаАналитическогоУчета	КАК ГруппаПродукции,
	|
	// Количественные и суммовые показатели
	|	&КоличествоПоПравилу 						 								  		  	  КАК Количество,
	|	НЕОПРЕДЕЛЕНО 																			  КАК ИдентификаторСтроки,
	|	ВЫРАЗИТЬ(&Цена * &КоличествоПоПравилу * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))  КАК Стоимость,
	|	ВЫРАЗИТЬ(&Цена * &КоличествоПоПравилу * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))  КАК СтоимостьБезНДС,
	|	ВЫРАЗИТЬ(&Цена * &КоличествоПоПравилу * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2)) КАК СтоимостьРегл,
	|	0                                   				 								  	  КАК НДСРегл,
	|	ВЫРАЗИТЬ(&Цена * &КоличествоПоПравилу * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))  КАК СтоимостьУпр,
	|	0 													 								  	  КАК НДСУпр,
	|	0 													 								  	  КАК СтоимостьЗабалансовая,
	|	0 													 								  	  КАК СтоимостьЗабалансоваяРегл,
	|	0 													 								  	  КАК ДопРасходы,
	|	0 													 								  	  КАК ДопРасходыБезНДС,
	|	0 													 								  	  КАК ДопРасходыРегл,
	|	0 													 								  	  КАК ДопРасходыУпр,
	|	0 													 								  	  КАК ПостояннаяРазница,
	|	0 													 								  	  КАК ВременнаяРазница,
	|	0 													 								  	  КАК КорСтоимость,
	|
	// Прочие поля
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость) 		  	КАК ХозяйственнаяОперация,
	|	&ИдентификаторДокумента																		КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ВыпускПродукцииФиксированнаяСтоимость) 	КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В (&Ссылка)
	|	И ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|	И &КоличествоПоПравилу <> 0";
	
	РасчетСебестоимостиПроведениеДокументов.ДобавитьОписаниеОперацииДляОтраженияВУчетеСебестоимости(
		ОписаниеОпераций,
		Перечисления.ОперацииУчетаСебестоимости.ВнутреннееПоступление,
		ТекстЗапроса);
	
	#КонецОбласти
	
	#Область ВнутреннееПоступление_Работа_ПоРасходам
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	// Описание документа
	|	ТаблицаДокумента.Дата 	КАК Период,
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	НЕОПРЕДЕЛЕНО			КАК ДокументИсточник,
	|	НЕОПРЕДЕЛЕНО			КАК ПериодДокументаИсточника,
	|	НЕОПРЕДЕЛЕНО 			КАК КорректируемыйДокумент,
	|
	// Поля учета номенклатуры
	|	ТаблицаДокумента.Организация 					КАК Организация,
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры		КАК АналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО 									КАК ВидЗапасов,
	|	ТаблицаДокумента.Ссылка							КАК Партия,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаДокумента.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС, ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)) <>
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|			ТОГДА ТаблицаДокумента.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС
	|		КОГДА ТаблицаДокумента.ВыпускПодДеятельность = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|			ТОГДА &НалогообложениеОрганизации
	|		ИНАЧЕ ТаблицаДокумента.ВыпускПодДеятельность
	|	КОНЕЦ											КАК ВидДеятельностиНДС,
	|	(ВЫБОР
	|		КОГДА ТаблицаДокумента.ВыпускПодДеятельность = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|			ТОГДА &НалогообложениеОрганизации
	|		ИНАЧЕ ТаблицаДокумента.ВыпускПодДеятельность КОНЕЦ) КАК ВидДеятельностиНДСДокумента,
	|
	// Поля аналитики учета партий
	|	НЕОПРЕДЕЛЕНО 	КАК АналитикаУчетаПартий,
	|	НЕОПРЕДЕЛЕНО	КАК КорАналитикаУчетаПартий,
	|	0				КАК КодСтроки,
	|
	// Корреспондирующие поля
	|	НЕОПРЕДЕЛЕНО						КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО						КАК КорВидЗапасов,
	|	НЕОПРЕДЕЛЕНО						КАК КорГруппаПродукции,
	|
	// Поля аналитики финансового учета
	|	НЕОПРЕДЕЛЕНО											КАК Сделка,
	|	ТаблицаДокумента.Подразделение							КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО											КАК Менеджер,
	|	ТаблицаДокумента.Номенклатура.ГруппаАналитическогоУчета	КАК ГруппаПродукции,
	|
	// Количественные и суммовые показатели
	|	ТаблицаВидыЗапасов.Количество 						 								  	  			КАК Количество,
	|	НЕОПРЕДЕЛЕНО 																						КАК ИдентификаторСтроки,
	|	ВЫРАЗИТЬ(&Цена * ТаблицаВидыЗапасов.Количество * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))  	КАК Стоимость,
	|	ВЫРАЗИТЬ(&Цена * ТаблицаВидыЗапасов.Количество * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))  	КАК СтоимостьБезНДС,
	|	ВЫБОР КОГДА ЕСТЬNULL(ТаблицаВидыЗапасов.СтатьяРасходов.ПринятиеКНалоговомуУчету, ИСТИНА)
	|		ТОГДА ВЫРАЗИТЬ(&Цена * ТаблицаВидыЗапасов.Количество * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0
	|	КОНЕЦ																					  			КАК СтоимостьРегл,
	|	0                                   				 								  	  			КАК НДСРегл,
	|	ВЫРАЗИТЬ(&Цена * ТаблицаВидыЗапасов.Количество * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))  	КАК СтоимостьУпр,
	|	0 													 								  	  КАК НДСУпр,
	|	0 													 								  	  КАК СтоимостьЗабалансовая,
	|	0 													 								  	  КАК СтоимостьЗабалансоваяРегл,
	|	0 													 								  	  КАК ДопРасходы,
	|	0 													 								  	  КАК ДопРасходыБезНДС,
	|	0 													 								  	  КАК ДопРасходыРегл,
	|	0 													 								  	  КАК ДопРасходыУпр,
	|	ВЫБОР КОГДА ЕСТЬNULL(ТаблицаВидыЗапасов.СтатьяРасходов.ПринятиеКНалоговомуУчету, ИСТИНА)
	|		ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(&Цена * ТаблицаВидыЗапасов.Количество * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|	КОНЕЦ																					  КАК ПостояннаяРазница,
	|	0 													 								  	  КАК ВременнаяРазница,
	|	0 													 								  	  КАК КорСтоимость,
	|
	// Прочие поля
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость) 		  КАК ХозяйственнаяОперация,
	|	ТаблицаВидыЗапасов.ИдентификаторСтроки   												  КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ВыпускПродукцииФиксированнаяСтоимость) КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов КАК ТаблицаДокумента
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеВозвратныхОтходов.ПрочиеРасходы КАК ТаблицаВидыЗапасов
	|		ПО ТаблицаДокумента.Ссылка = ТаблицаВидыЗапасов.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В (&Ссылка)
	|	И ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)";
	
	РасчетСебестоимостиПроведениеДокументов.ДобавитьОписаниеОперацииДляОтраженияВУчетеСебестоимости(
		ОписаниеОпераций,
		Перечисления.ОперацииУчетаСебестоимости.ВнутреннееПоступление,
		ТекстЗапроса);
	
	#КонецОбласти
	
	#Область ВнутреннееПоступление_Работа_ПоПартиям
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	// Описание документа
	|	ТаблицаДокумента.Дата 	КАК Период,
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	НЕОПРЕДЕЛЕНО			КАК ДокументИсточник,
	|	НЕОПРЕДЕЛЕНО			КАК ПериодДокументаИсточника,
	|	НЕОПРЕДЕЛЕНО 			КАК КорректируемыйДокумент,
	|
	// Поля учета номенклатуры
	|	ТаблицаДокумента.Организация 					КАК Организация,
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры		КАК АналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО 									КАК ВидЗапасов,
	|	ТаблицаДокумента.Ссылка							КАК Партия,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаДокумента.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС, ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)) <>
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|			ТОГДА ТаблицаДокумента.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС
	|		КОГДА ТаблицаДокумента.ВыпускПодДеятельность = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|			ТОГДА &НалогообложениеОрганизации
	|		ИНАЧЕ ТаблицаДокумента.ВыпускПодДеятельность
	|	КОНЕЦ											КАК ВидДеятельностиНДС,
	|	(ВЫБОР
	|		КОГДА ТаблицаДокумента.ВыпускПодДеятельность = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|			ТОГДА &НалогообложениеОрганизации
	|		ИНАЧЕ ТаблицаДокумента.ВыпускПодДеятельность КОНЕЦ) КАК ВидДеятельностиНДСДокумента,
	|
	// Поля аналитики учета партий
	|	НЕОПРЕДЕЛЕНО 	КАК АналитикаУчетаПартий,
	|	НЕОПРЕДЕЛЕНО	КАК КорАналитикаУчетаПартий,
	|	0				КАК КодСтроки,
	|
	// Корреспондирующие поля
	|	НЕОПРЕДЕЛЕНО						КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО						КАК КорВидЗапасов,
	|	НЕОПРЕДЕЛЕНО						КАК КорГруппаПродукции,
	|
	// Поля аналитики финансового учета
	|	НЕОПРЕДЕЛЕНО											КАК Сделка,
	|	ТаблицаДокумента.Подразделение							КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО											КАК Менеджер,
	|	ТаблицаДокумента.Номенклатура.ГруппаАналитическогоУчета	КАК ГруппаПродукции,
	|
	// Количественные и суммовые показатели
	|	ТаблицаВидыЗапасов.Количество 						 								  	  			КАК Количество,
	|	НЕОПРЕДЕЛЕНО 																						КАК ИдентификаторСтроки,
	|	ВЫРАЗИТЬ(&Цена * ТаблицаВидыЗапасов.Количество * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))   КАК Стоимость,
	|	ВЫРАЗИТЬ(&Цена * ТаблицаВидыЗапасов.Количество * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))   КАК СтоимостьБезНДС,
	|	ВЫРАЗИТЬ(&Цена * ТаблицаВидыЗапасов.Количество * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2)) 	КАК СтоимостьРегл,
	|	0                                   				 								  	  			КАК НДСРегл,
	|	ВЫРАЗИТЬ(&Цена * ТаблицаВидыЗапасов.Количество * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))   КАК СтоимостьУпр,
	|	0 													 								  	  КАК НДСУпр,
	|	0 													 								  	  КАК СтоимостьЗабалансовая,
	|	0 													 								  	  КАК СтоимостьЗабалансоваяРегл,
	|	0 													 								  	  КАК ДопРасходы,
	|	0 													 								  	  КАК ДопРасходыБезНДС,
	|	0 													 								  	  КАК ДопРасходыРегл,
	|	0 													 								  	  КАК ДопРасходыУпр,
	|	0																					  	  КАК ПостояннаяРазница,
	|	0 													 								  	  КАК ВременнаяРазница,
	|	0 													 								  	  КАК КорСтоимость,
	|
	// Прочие поля
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость) 		  КАК ХозяйственнаяОперация,
	|	ТаблицаВидыЗапасов.ИдентификаторСтроки   												  КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ВыпускПродукцииФиксированнаяСтоимость) КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов КАК ТаблицаДокумента
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеВозвратныхОтходов.ПартииПроизводства КАК ТаблицаВидыЗапасов
	|		ПО ТаблицаДокумента.Ссылка = ТаблицаВидыЗапасов.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В (&Ссылка)
	|	И ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)";
	
	РасчетСебестоимостиПроведениеДокументов.ДобавитьОписаниеОперацииДляОтраженияВУчетеСебестоимости(
		ОписаниеОпераций,
		Перечисления.ОперацииУчетаСебестоимости.ВнутреннееПоступление,
		ТекстЗапроса);
	
	#КонецОбласти
	
	#Область ВнутреннееПоступление_Работа_ПоПартииПроизводства
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	// Описание документа
	|	ТаблицаДокумента.Дата 	КАК Период,
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	НЕОПРЕДЕЛЕНО			КАК ДокументИсточник,
	|	НЕОПРЕДЕЛЕНО			КАК ПериодДокументаИсточника,
	|	НЕОПРЕДЕЛЕНО 			КАК КорректируемыйДокумент,
	|
	// Поля учета номенклатуры
	|	ТаблицаДокумента.Организация 					КАК Организация,
	|	АналитикаНоменклатурыПоПартии.КлючАналитики		КАК АналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО 									КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.ПартияПроизводства			КАК Партия,
	|	СпрПартииПроизводства.ВидДеятельностиНДС		КАК ВидДеятельностиНДС,
	|	(ВЫБОР
	|		КОГДА ТаблицаДокумента.ВыпускПодДеятельность = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|			ТОГДА &НалогообложениеОрганизации
	|		ИНАЧЕ ТаблицаДокумента.ВыпускПодДеятельность КОНЕЦ) КАК ВидДеятельностиНДСДокумента,
	|
	// Поля аналитики учета партий
	|	НЕОПРЕДЕЛЕНО 	КАК АналитикаУчетаПартий,
	|	НЕОПРЕДЕЛЕНО	КАК КорАналитикаУчетаПартий,
	|	0				КАК КодСтроки,
	|
	// Корреспондирующие поля
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры				КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО											КАК КорВидЗапасов,
	|	ТаблицаДокумента.Номенклатура.ГруппаАналитическогоУчета	КАК КорГруппаПродукции,
	|
	// Поля аналитики финансового учета
	|	НЕОПРЕДЕЛЕНО											КАК Сделка,
	|	ТаблицаДокумента.Подразделение							КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО											КАК Менеджер,
	|	СпрПартииПроизводства.ГруппаПродукции					КАК ГруппаПродукции,
	|
	// Количественные и суммовые показатели
	|	-ТаблицаВидыЗапасов.Количество 						 								  	  			 КАК Количество,
	|	НЕОПРЕДЕЛЕНО 																						 КАК ИдентификаторСтроки,
	|	-ВЫРАЗИТЬ(&Цена * ТаблицаВидыЗапасов.Количество * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))   КАК Стоимость,
	|	-ВЫРАЗИТЬ(&Цена * ТаблицаВидыЗапасов.Количество * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))   КАК СтоимостьБезНДС,
	|	-ВЫРАЗИТЬ(&Цена * ТаблицаВидыЗапасов.Количество * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))  КАК СтоимостьРегл,
	|	0                                   				 								  	  			 КАК НДСРегл,
	|	-ВЫРАЗИТЬ(&Цена * ТаблицаВидыЗапасов.Количество * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))   КАК СтоимостьУпр,
	|	0 													 								  	  КАК НДСУпр,
	|	0 													 								  	  КАК СтоимостьЗабалансовая,
	|	0 													 								  	  КАК СтоимостьЗабалансоваяРегл,
	|	0 													 								  	  КАК ДопРасходы,
	|	0 													 								  	  КАК ДопРасходыБезНДС,
	|	0 													 								  	  КАК ДопРасходыРегл,
	|	0 													 								  	  КАК ДопРасходыУпр,
	|	0																					  	  КАК ПостояннаяРазница,
	|	0 													 								  	  КАК ВременнаяРазница,
	|	0 													 								  	  КАК КорСтоимость,
	|
	// Прочие поля
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость) 		  КАК ХозяйственнаяОперация,
	|	ТаблицаВидыЗапасов.ИдентификаторСтроки   												  КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ВыпускПродукцииФиксированнаяСтоимость) КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов КАК ТаблицаДокумента
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеВозвратныхОтходов.ПартииПроизводства КАК ТаблицаВидыЗапасов
	|		ПО ТаблицаДокумента.Ссылка = ТаблицаВидыЗапасов.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК СпрПартииПроизводства
	|			ПО СпрПартииПроизводства.Ссылка = ТаблицаВидыЗапасов.ПартияПроизводства
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаНоменклатурыПоПартии
	|			ПО &Номенклатура = АналитикаНоменклатурыПоПартии.Номенклатура
	|			И &Характеристика = АналитикаНоменклатурыПоПартии.Характеристика
	|			И АналитикаНоменклатурыПоПартии.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|			И &Подразделение = АналитикаНоменклатурыПоПартии.МестоХранения
	|			И АналитикаНоменклатурыПоПартии.Назначение = СпрПартииПроизводства.Назначение
	|			И ТаблицаВидыЗапасов.СтатьяКалькуляции = АналитикаНоменклатурыПоПартии.СтатьяКалькуляции
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В (&Ссылка)
	|	И ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)";
	
	РасчетСебестоимостиПроведениеДокументов.ДобавитьОписаниеОперацииДляОтраженияВУчетеСебестоимости(
		ОписаниеОпераций,
		Перечисления.ОперацииУчетаСебестоимости.ВнутреннееПоступление,
		ТекстЗапроса);
	
	#КонецОбласти
	
	Возврат ОписаниеОпераций;
	
КонецФункции

#КонецОбласти

#КонецОбласти

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента      = "Документ.РаспределениеВозвратныхОтходов";
	СинонимТаблицыДокумента = "";
	ВЗапросеЕстьИсточник    = Истина;
	
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("НомерНаПечать", """""");
	
	ЗначенияПараметров = Новый Структура;
	ЗначенияПараметров.Вставить("ИдентификаторМетаданных",
		ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ПолноеИмяДокумента));
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, ИмяРегистра);
		ВЗапросеЕстьИсточник = Ложь;
		
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';
								|en = 'In document %ПолноеИмяДокумента%, adaptation of request for generating records of register %ИмяРегистра% is not implemented.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
		ТекстЗапроса,
		ПолноеИмяДокумента,
		СинонимТаблицыДокумента,
		ВЗапросеЕстьИсточник,
		ПереопределениеРасчетаПараметров);
	
	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов - см. УправлениеПечатьюПереопределяемый.ПриПечати.МассивОбъектов
//  ПараметрыПечати - см. УправлениеПечатьюПереопределяемый.ПриПечати.ПараметрыПечати
//  КоллекцияПечатныхФорм - см. УправлениеПечатьюПереопределяемый.ПриПечати.КоллекцияПечатныхФорм
//  ОбъектыПечати - см. УправлениеПечатьюПереопределяемый.ПриПечати.ОбъектыПечати
//  ПараметрыВывода - см. УправлениеПечатьюПереопределяемый.ПриПечати.ПараметрыВывода
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Накладная") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"Накладная",
			НСтр("ru = 'Выпуск продукции';
				|en = 'Product release'"),
			СформироватьПечатнуюФормуНакладной(МассивОбъектов, ОбъектыПечати));
		
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьПечатнуюФормуНакладной(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ДвижениеПродукцииИМатериалов.ПФ_MXL_Накладная");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка						КАК Ссылка,
	|	ДанныеДокумента.Номер						КАК Номер,
	|	ДанныеДокумента.Дата						КАК Дата,
	|	ДанныеДокумента.Организация					КАК Организация,
	|	ДанныеДокумента.Организация.Префикс			КАК Префикс,
	|	1											КАК НомерСтроки,
	|	ДанныеДокумента.Номенклатура				КАК Номенклатура,
	|	ДанныеДокумента.Характеристика				КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ДанныеДокумента.Назначение.ДвиженияПоСкладскимРегистрам, ЛОЖЬ)
	|			ТОГДА ДанныеДокумента.Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ										КАК Назначение,
	|	ДанныеДокумента.Получатель					КАК Склад,
	|	ДанныеДокумента.Подразделение				КАК Подразделение,
	|	ДанныеДокумента.Количество					КАК Количество,
	|	ДанныеДокумента.Серия						КАК Серия
	|ПОМЕСТИТЬ ВтТовары
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов КАК ДанныеДокумента
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка В(&МассивДокументов)
	|	И НЕ ДанныеДокумента.СтатусУказанияСерий В (4, 6, 8, 10)
	|	И ДанныеДокумента.Номенклатура.ТипНоменклатуры В (
	|					ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|					ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И ТИПЗНАЧЕНИЯ(ДанныеДокумента.Получатель) = ТИП(Справочник.Склады)
	|	И НЕ ДанныеДокумента.Получатель.ЦеховаяКладовая
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка						КАК Ссылка,
	|	ДанныеДокумента.Номер						КАК Номер,
	|	ДанныеДокумента.Дата						КАК Дата,
	|	ДанныеДокумента.Организация					КАК Организация,
	|	ДанныеДокумента.Организация.Префикс			КАК Префикс,
	|	1											КАК НомерСтроки,
	|	ДанныеДокумента.Номенклатура				КАК Номенклатура,
	|	ДанныеДокумента.Характеристика				КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ДанныеДокумента.Назначение.ДвиженияПоСкладскимРегистрам, ЛОЖЬ)
	|			ТОГДА ДанныеДокумента.Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ										КАК Назначение,
	|	ДанныеДокумента.Получатель					КАК Склад,
	|	ДанныеДокумента.Подразделение				КАК Подразделение,
	|	ДанныеДокумента.Количество					КАК Количество,
	|	ТаблицаСерии.Серия							КАК Серия
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов.Серии КАК ТаблицаСерии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеВозвратныхОтходов КАК ДанныеДокумента
	|		ПО ТаблицаСерии.Ссылка = ДанныеДокумента.Ссылка
	|ГДЕ
	|	ТаблицаСерии.Ссылка В(&МассивДокументов)
	|	И ДанныеДокумента.СтатусУказанияСерий В (4, 6, 8, 10)
	|	И ТаблицаСерии.Количество <> 0
	|	И ДанныеДокумента.Номенклатура.ТипНоменклатуры В (
	|					ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|					ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И ТИПЗНАЧЕНИЯ(ДанныеДокумента.Получатель) = ТИП(Справочник.Склады)
	|	И НЕ ДанныеДокумента.Получатель.ЦеховаяКладовая
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Ссылка				КАК Ссылка,
	|	ТаблицаТовары.Номер					КАК Номер,
	|	ТаблицаТовары.Дата					КАК Дата,
	|	ТаблицаТовары.Организация			КАК Организация,
	|	ТаблицаТовары.Организация.Префикс	КАК Префикс,
	|	ТаблицаТовары.Номенклатура,
	//|	ТаблицаТовары.Номенклатура." + ?(КолонкаКодов = "", "Код", КолонкаКодов) + " КАК КодАртикул,
	|	ТаблицаТовары.Номенклатура.НаименованиеПолное КАК Номенклатура,
	|	ТаблицаТовары.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Характеристика.Наименование КАК ХарактеристикаНаименование,
	|	ТаблицаТовары.Серия.Наименование КАК СерияНаименование,
	|	ТаблицаТовары.Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмерения,
	|	ТаблицаТовары.Количество КАК Количество,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаПродукцииИзПроизводства) КАК НаправлениеВыпуска,
	|	ТаблицаТовары.Склад.Наименование КАК ПредставлениеПолучателя,
	|	ТаблицаТовары.Подразделение.Наименование КАК ПредставлениеПодразделения
	|ИЗ
	|	ВтТовары КАК ТаблицаТовары
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаТовары.Ссылка,
	|	ТаблицаТовары.НомерСтроки
	|ИТОГИ
	|	МАКСИМУМ(Номер),
	|	МАКСИМУМ(Дата),
	|	МАКСИМУМ(Организация),
	|	МАКСИМУМ(ПредставлениеПолучателя),
	|	МАКСИМУМ(ПредставлениеПодразделения),
	|	МАКСИМУМ(Префикс)
	|ПО
	|	Ссылка";

	Запрос.УстановитьПараметр("МассивДокументов",           МассивОбъектов);
	Запрос.УстановитьПараметр("ТипПолучателяСклад",         НСтр("ru = 'Склад';
																|en = 'Warehouse'"));
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ПолеСлева = 5;
	ТабличныйДокумент.ПолеСправа = 5;
	ТабличныйДокумент.РазмерКолонтитулаСверху = 0;
	ТабличныйДокумент.РазмерКолонтитулаСнизу = 0;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ВыпускПродукции_Накладная";
	
	МассивРезультатов		= Запрос.ВыполнитьПакет();
	ВыборкаПоДокументам 	= МассивРезультатов[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ПервыйДокумент = Истина;
	
	Пока ВыборкаПоДокументам.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтраницы = 1;
		
		ПервыйДокумент    = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		// Выводим общие реквизиты шапки
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
		ОбластьМакета.Параметры.Заполнить(ВыборкаПоДокументам);
		ОбластьМакета.Параметры.ТипПолучателя = НСтр("ru = 'Склад';
													|en = 'Warehouse'", ОбщегоНазначения.КодОсновногоЯзыка());
		
		ТекстЗаголовка = ОбщегоНазначенияУТКлиентСервер.СформироватьЗаголовокДокумента(ВыборкаПоДокументам, НСтр("ru = 'Выпуск продукции';
																												|en = 'Product release'", ОбщегоНазначения.КодОсновногоЯзыка()));
		ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
		
		СведенияОбОрганизации = ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ВыборкаПоДокументам.Организация, ВыборкаПоДокументам.Дата);
		ОбластьМакета.Параметры.ПредставлениеОрганизации = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОбОрганизации);
		
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		// Создаем массив для проверки вывода
		МассивВыводимыхОбластей = Новый Массив;
		
		// Выводим многострочную часть докмента
		ОбластьЗаголовокТаблицы = Макет.ПолучитьОбласть(?(КолонкаКодов = "", "ЗаголовокТаб", "ЗаголовокТабСКодом"));
		ОбластьМакета           = Макет.ПолучитьОбласть(?(КолонкаКодов = "", "Строка",       "СтрокаСКодом"));
		ОбластьПодвала          = Макет.ПолучитьОбласть("Подвал");
		
		СтрокаТовары = ВыборкаПоДокументам.Выбрать();
		КоличествоСтрок = СтрокаТовары.Количество();
		НомерСтроки = 0;
		ПараметрыСтрокиТовары = Новый Структура("НомерСтроки,ТоварНаименование");
		Пока СтрокаТовары.Следующий() Цикл
			
			ДопПараметрыПредставлениеНоменклатуры = НоменклатураКлиентСервер.ДополнительныеПараметрыПредставлениеНоменклатурыДляПечати();
			ДопПараметрыПредставлениеНоменклатуры.КодОсновногоЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();
			
			НомерСтроки = НомерСтроки + 1;
			
			ПараметрыСтрокиТовары.НомерСтроки = НомерСтроки;
			ПараметрыСтрокиТовары.ТоварНаименование = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
															СтрокаТовары.ТоварНаименование, 
															СтрокаТовары.ХарактеристикаНаименование,,
															СтрокаТовары.СерияНаименование,
															ДопПараметрыПредставлениеНоменклатуры);
															
			ОбластьМакета.Параметры.Заполнить(СтрокаТовары);
			ОбластьМакета.Параметры.Заполнить(ПараметрыСтрокиТовары);
			
			Если НомерСтроки = 1 Тогда
				
				ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Страница %1';
						|en = 'Page %1'", ОбщегоНазначения.КодОсновногоЯзыка()), НомерСтраницы); 
				ТабличныйДокумент.Вывести(ОбластьЗаголовокТаблицы);
				
			Иначе
				
				МассивВыводимыхОбластей.Очистить();
				МассивВыводимыхОбластей.Добавить(ОбластьМакета);
				
				Если НомерСтроки = КоличествоСтрок Тогда
					
					МассивВыводимыхОбластей.Добавить(ОбластьПодвала);
					
				КонецЕсли;
				
				Если НомерСтроки <> 1 И Не ТабличныйДокумент.ПроверитьВывод(МассивВыводимыхОбластей) Тогда
					
					НомерСтраницы = НомерСтраницы + 1;
					ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
					ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Страница %1';
							|en = 'Page %1'", ОбщегоНазначения.КодОсновногоЯзыка()), НомерСтраницы);
					ТабличныйДокумент.Вывести(ОбластьЗаголовокТаблицы);
					
				КонецЕсли;
				
			КонецЕсли;
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
		КонецЦикла;
		
		// Выводим подвал документа
		ОбластьПодвала.Параметры.ИтоговаяСтрока = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Всего наименований %1';
				|en = 'Total items %1'", ОбщегоНазначения.КодОсновногоЯзыка()), СтрокаТовары.Количество());
		
		ТабличныйДокумент.Вывести(ОбластьПодвала);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ВыборкаПоДокументам.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

// Возвращает текст запроса получения сведений о содержании табличных частей документа для печати Задания на отбор
// (размещение) товаров.
//
// Возвращаемое значение:
//	Строка - текст запроса получения сведений о содержании табличных частей документа для печати Задания на отбор (размещение) товаров.
//
Функция ТекстЗапросаТоварыДокументаДляПечатиЗаданияНаОтборРазмещениеТоваров() Экспорт
	
	ТекстаЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка			КАК Ссылка,
	|	1								КАК НомерСтроки,
	|	ДанныеДокумента.Получатель		КАК Склад,
	|	&Помещение						КАК Помещение,
	|	ДанныеДокумента.Номенклатура	КАК Номенклатура,
	|	ДанныеДокумента.Характеристика	КАК Характеристика,
	|	ДанныеДокумента.Серия			КАК Серия
	|ПОМЕСТИТЬ ТоварыДокумента
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Товары
	|		ПО ДанныеДокумента.Номенклатура = Товары.Ссылка
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|		ПО ДанныеДокумента.Получатель = Склады.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка В(&МассивОбъектов)
	|	И НЕ Товары.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|	И НЕ ДанныеДокумента.СтатусУказанияСерий В(4, 6, 8, 10)
	|	И (НЕ Склады.ИспользоватьОрдернуюСхемуПриПоступлении
	|		ИЛИ ДанныеДокумента.Дата < Склады.ДатаНачалаОрдернойСхемыПриПоступлении)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СерииТоваров.Ссылка				КАК Ссылка,
	|	1								КАК НомерСтроки,
	|	ДанныеДокумента.Получатель		КАК Склад,
	|	&Помещение						КАК Помещение,
	|	ДанныеДокумента.Номенклатура	КАК Номенклатура,
	|	ДанныеДокумента.Характеристика	КАК Характеристика,
	|	СерииТоваров.Серия				КАК Серия
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов.Серии КАК СерииТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеВозвратныхОтходов КАК ДанныеДокумента
	|		ПО СерииТоваров.Ссылка = ДанныеДокумента.Ссылка
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Товары
	|		ПО ДанныеДокумента.Номенклатура = Товары.Ссылка
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|		ПО ДанныеДокумента.Получатель = Склады.Ссылка
	|
	|ГДЕ
	|	СерииТоваров.Ссылка В(&МассивОбъектов)
	|	И НЕ Товары.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|	И ДанныеДокумента.СтатусУказанияСерий В(6, 8, 10)
	|	И (НЕ Склады.ИспользоватьОрдернуюСхемуПриПоступлении
	|		ИЛИ ДанныеДокумента.Дата < Склады.ДатаНачалаОрдернойСхемыПриПоступлении)";
	
	ТекстаЗапроса = СтрЗаменить(ТекстаЗапроса, "&Помещение", "ЗНАЧЕНИЕ(Справочник.СкладскиеПомещения.ПустаяСсылка)");
	
	Возврат ТекстаЗапроса;
	
КонецФункции

#КонецОбласти

#Область Назначения

// Инициализирует параметры, обслуживающие выбор назначений в формах документа.
//
//  Возвращаемое значение:
//  См. Справочники.Назначения.МакетФормыВыбораНазначений
//
Функция МакетФормыВыбораНазначений() Экспорт
	
	МакетФормы = Справочники.Назначения.МакетФормыВыбораНазначений();
	
	ШаблонНазначения = Справочники.Назначения.ДобавитьШаблонНазначений(МакетФормы);
	//++ НЕ УТКА
	ШаблонНазначения.ТипыНазначений.Удалить(ШаблонНазначения.ТипыНазначений.Найти(Перечисления.ТипыНазначений.Давальческое21));
	//-- НЕ УТКА
	
	Справочники.Назначения.ДобавитьОписаниеКолонок(МакетФормы, "ВсеНазначения", Ложь, "Объект.Назначение");
	Возврат МакетФормы;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
