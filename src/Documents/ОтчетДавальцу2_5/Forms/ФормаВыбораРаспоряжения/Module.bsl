
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МассивОтборов = Новый Массив;
	МассивОтборов.Добавить("Регистратор");
	МассивОтборов.Добавить("Организация");
	МассивОтборов.Добавить("Партнер");
	МассивОтборов.Добавить("Контрагент");
	МассивОтборов.Добавить("Договор");
	МассивОтборов.Добавить("Склад");
	МассивОтборов.Добавить("Валюта");
	МассивОтборов.Добавить("Сделка");
	МассивОтборов.Добавить("СтавкаНДС");
	МассивОтборов.Добавить("НалогообложениеНДС");
	МассивОтборов.Добавить("НаправлениеДеятельности");
	МассивОтборов.Добавить("ИспользоватьНаправленияДеятельности");
	
	Для каждого ИмяОтбора Из МассивОтборов Цикл
		
		ЗначениеОтбора = Неопределено;
		Параметры.Отбор.Свойство(ИмяОтбора, ЗначениеОтбора);
		
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, ИмяОтбора, ЗначениеОтбора);
		
	КонецЦикла;
	
	УстановитьУсловноеОформлениеПоляСсылка();
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Данные = Новый Структура;
	Данные.Вставить("ЗаказДавальца",   ТекущиеДанные.Ссылка);
	Данные.Вставить("Назначение",      ТекущиеДанные.Назначение);
	Данные.Вставить("ПорядокРасчетов", ТекущиеДанные.ПорядокРасчетов);
	
	Закрыть(Данные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	Если Не ОбщегоНазначенияУТКлиент.ПроверитьНаличиеВыделенныхВСпискеСтрок(Элементы.Список) Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Данные = Новый Структура;
	Данные.Вставить("ЗаказДавальца",   ТекущиеДанные.Ссылка);
	Данные.Вставить("Назначение",      ТекущиеДанные.Назначение);
	Данные.Вставить("ПорядокРасчетов", ТекущиеДанные.ПорядокРасчетов);
	
	Закрыть(Данные);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформлениеПоляСсылка()
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Ссылка.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.Ссылка");
	ОтборЭлемента.ВидСравнения  = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<без заказа>';
																|en = '<without order>'"));
	
КонецПроцедуры

#КонецОбласти