#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Сотрудники") Тогда
		ЗарплатаКадры.ЗаполнитьПоОснованиюСотрудником(ЭтотОбъект, ДанныеЗаполнения, , Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	ДанныеДляПроведения = ДанныеДляПроведения();
	
	УчетНДФЛ.СформироватьДоходыНДФЛПоКодамДоходовИзТаблицыЗначений(Движения, Отказ, Организация, ДатаОперации, ДанныеДляПроведения.Доходы, , , Ссылка);
	УчетНДФЛ.СформироватьПредоставленныеИмущественныеВычетыПоВременнойТаблице(Движения, Отказ, Организация, ДатаОперации, ДанныеДляПроведения.МенеджерВременныхТаблиц);
	УчетНДФЛ.СформироватьСтандартныеВычетыПоВременнойТаблице(Движения, Отказ, Организация, ДатаОперации, ДанныеДляПроведения.МенеджерВременныхТаблиц);
	УчетНДФЛ.СформироватьИсчисленныйНалогПоВременнойТаблице(Движения, Отказ, Организация, ДатаОперации, ДанныеДляПроведения.МенеджерВременныхТаблиц);
	Если Движения.РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Количество() > 0 Тогда
		УчетНДФЛ.СформироватьДокументыУчтенныеПриРасчетеДляМежрасчетногоДокумента(Движения, Отказ, Организация, Сотрудник, Ссылка); 	
	КонецЕсли;
	УчетНДФЛ.СформироватьИспользованныеАвансовыеПлатежиПоВременнойТаблице(Движения, Отказ, Организация, ДатаОперации, ДанныеДляПроведения.МенеджерВременныхТаблиц);
	УчетНДФЛ.СформироватьУдержанныйНалогПоВременнойТаблице(Движения, Отказ, Организация, ДатаОперации, ДанныеДляПроведения.МенеджерВременныхТаблиц);
	УчетНДФЛ.СформироватьПеречисленныйНалогПоВременнойТаблице(Движения, Отказ, Организация, ДатаОперации, ДанныеДляПроведения.МенеджерВременныхТаблиц);
	УчетНДФЛ.СформироватьПравоНаИмущественныеВычеты(Движения, Отказ, Организация, ДатаОперации, ДанныеДляПроведения.ДанныеОбУдержанныхВзносах);
	
	УчетНДФЛ.СформироватьНДФЛКПеречислению(Движения, Отказ);
	УчетНДФЛ.СформироватьНДФЛПеречисленный(Движения, Отказ);
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.РасчетыСБюджетомПоНДФЛ") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("РасчетыСБюджетомПоНДФЛ");
		Модуль.РасчетыСБюджетомПоНДФЛЗарегистрироватьНДФЛКПеречислению(Движения, Отказ);
		Модуль.РасчетыСБюджетомПоНДФЛЗарегистрироватьНДФЛПеречисленный(Движения, Отказ);
	КонецЕсли;
	
	Если ДанныеДляПроведения.НалогиПереданныеВНалоговыйОрган.Количество() > 0 Тогда
		УчетНДФЛ.СформироватьНалогиПереданныеВНалоговыйОрган(Движения, Отказ, Организация, ДатаОперации, ДанныеДляПроведения.НалогиПереданныеВНалоговыйОрган);
		
		УчетНачисленнойЗарплаты.ЗарегистрироватьНалогиПереданныеВНалоговыйОрган(Движения, Отказ, Организация, НачалоМесяца(ДатаОперации), 
			Дата(НалоговыйПериод, 12, 1), ДанныеДляПроведения.НалогиСотрудников);
		
		ДанныеДляПроведения = ОтражениеЗарплатыВУчете.НоваяСтруктураРезультатыРасчетаЗарплаты();
		ДанныеДляПроведения.НачисленияУдержания = Движения.НачисленияУдержанияПоСотрудникам.Выгрузить();
		СтрокаСписокТаблиц = "НачисленныйНДФЛ";
		ОтражениеЗарплатыВБухучете.СформироватьДвиженияПоДокументу(Движения, Отказ, Организация, НачалоМесяца(ДатаОперации), ДанныеДляПроведения, СтрокаСписокТаблиц);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если СведенияОДоходах.Количество() = 0
		И НДФЛИсчисленныйПоСтавке13.Количество() = 0
		И НДФЛУдержанный.Количество() = 0
		И НДФЛПеречисленный.Количество() = 0
		И НДФЛПереданныйВНалоговую.Количество() = 0
		И ВзносыЗасчитываемыеКакВычеты.Количество() = 0
		И ПредоставленныеВычеты.Количество() = 0 Тогда
		
		СообщениеОбОшибке = НСтр("ru = 'Не заполнены табличные части, должна быть заполнена хотя бы одна табличная часть.';
								|en = 'No tabular sections are filled. Fill at least one tabular section.'");
		ОбщегоНазначения.СообщитьПользователю(СообщениеОбОшибке,,,,Отказ);
		
	КонецЕсли;
	
	ИменаРесурсовНалога = УчетНДФЛ.РесурсыИсчисленногоНалогаВМассиве("Сумма");
	ИменаРесурсовЗачета = УчетНДФЛ.РесурсыИсчисленногоНалогаВМассиве("ЗачтеноАвансовыхПлатежей");
	Для Каждого СтрокаНалога Из НДФЛИсчисленныйПоСтавке13 Цикл
		ПроверяемаяСумма = 0;
		Для каждого ИмяРесурса Из ИменаРесурсовНалога Цикл
			ПроверяемаяСумма = ПроверяемаяСумма + СтрокаНалога[ИмяРесурса] * СтрокаНалога[ИмяРесурса]
		КонецЦикла;
		Для каждого ИмяРесурса Из ИменаРесурсовЗачета Цикл
			ПроверяемаяСумма = ПроверяемаяСумма + СтрокаНалога[ИмяРесурса] * СтрокаНалога[ИмяРесурса]
		КонецЦикла;
		Если ПроверяемаяСумма = 0 Тогда
			СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'В строке №%1 таблицы исчисленных налогов не указано ни суммы налога ни суммы зачтенных авансовых платежей.';
																							|en = 'In row #%1 of the calculated tax table, no tax amount or offset advance amount is specified.'"), СтрокаНалога.НомерСтроки);
			ОбщегоНазначения.СообщитьПользователю(СообщениеОбОшибке,,,,Отказ);
		КонецЕсли;
	КонецЦикла;
	
	Если НДФЛПереданныйВНалоговую.Количество() > 0 Тогда
		Если Не ЗначениеЗаполнено(НалоговыйПериод) Тогда
			СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'В сведениях о переданном в налоговый орган неудержанном налоге не указан налоговый период.';
																							|en = 'Fiscal period is not specified in the information about not deducted tax submitted to the tax authority.'"), СтрокаНалога.НомерСтроки);
			ОбщегоНазначения.СообщитьПользователю(СообщениеОбОшибке,,,,Отказ);
		КонецЕсли;
		Для Каждого СтрокаНалога Из НДФЛПереданныйВНалоговую Цикл
			ПроверяемаяСумма = 0;
			Для каждого ИмяРесурса Из ИменаРесурсовНалога Цикл
				ПроверяемаяСумма = ПроверяемаяСумма + СтрокаНалога[ИмяРесурса] * СтрокаНалога[ИмяРесурса]
			КонецЦикла;
			Если ПроверяемаяСумма = 0 Тогда
				СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'В строке №%1 сведений о переданном в налоговый орган неудержанном налоге не указана сумма налога.';
																								|en = 'In line #%1 of the information about not deducted tax submitted to the tax authority, tax amount is not specified.'"), СтрокаНалога.НомерСтроки);
				ОбщегоНазначения.СообщитьПользователю(СообщениеОбОшибке,,,,Отказ);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДанныеДляПроведения()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("КатегорииДоходовОсновнойСтавки", Перечисления.КатегорииДоходовНДФЛ.ПоОсновнойСтавке());
	Запрос.УстановитьПараметр("ДатаИзмененияПорядкаИсчисленияНалогаДляИностранцев", УчетНДФЛ.ДатаИзмененияПорядкаИсчисленияНалогаДляИностранцев());
	Запрос.УстановитьПараметр("НачалоРасчетаНДФЛПоПрогрессивнойШкале", УчетНДФЛ.ПараметрыРасчетаНДФЛПоПрогрессивнойШкале().НачалоРасчетаНДФЛПоПрогрессивнойШкале);
	Запрос.УстановитьПараметр("ДатаПереводаСоциальныхВычетовВОстаточныйРегистр", УчетНДФЛ.ДатаПереводаСоциальныхВычетовВОстаточныйРегистр());
	Запрос.УстановитьПараметр("ДатаРасширенияПрогрессивнойШкалыНалогообложения", УчетНДФЛ.ДатаЗакона176ФЗ());
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.Ссылка.Сотрудник КАК ФизическоеЛицо,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.СтавкаНалогообложения КАК СтавкаНалогообложенияРезидента,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.МесяцНалоговогоПериода КАК Период,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.КатегорияДохода КАК КатегорияДохода
	|ПОМЕСТИТЬ ВТПериодыНалогаУдержанного
	|ИЗ
	|	Документ.ОперацияНалоговогоУчетаПоНДФЛ.НДФЛУдержанный КАК ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный
	|ГДЕ
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.Ссылка = &Ссылка
	|	И ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.СтавкаНалогообложения = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
	|	И ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.МесяцНалоговогоПериода >= &НачалоРасчетаНДФЛПоПрогрессивнойШкале
	|	И ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.КатегорияДохода = ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.Дивиденды)
	|	И ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка15)";
	Если Запрос.Выполнить().Выгрузить()[0].Количество > 0 Тогда	
		УчетНДФЛ.СоздатьВТСтавкаНДФЛПоСтавкеРезидента(Запрос.МенеджерВременныхТаблиц, "ВТПериодыНалогаУдержанного");
	Иначе
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 0
		|	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка) КАК ФизическоеЛицо,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК Период,
		|	ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПустаяСсылка) КАК КатегорияДохода,
		|	ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.ПустаяСсылка) КАК СтавкаНалогообложенияРезидента,
		|	ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.ПустаяСсылка) КАК СтавкаНДФЛ
		|ПОМЕСТИТЬ ВТСтавкаНДФЛПоСтавкеРезидента";
		Запрос.Выполнить();
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Вычеты.Ссылка.Сотрудник КАК ФизическоеЛицо,
	|	Вычеты.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	Вычеты.ОбособленноеПодразделение КАК Подразделение,
	|	Вычеты.КодВычета КАК КодВычета,
	|	Вычеты.Сумма КАК Сумма,
	|	Вычеты.НалоговаяБаза КАК НалоговаяБаза
	|ПОМЕСТИТЬ ВТДанныеОбИмущественныхВычетах
	|ИЗ
	|	Документ.ОперацияНалоговогоУчетаПоНДФЛ.ПредоставленныеВычеты КАК Вычеты
	|ГДЕ
	|	Вычеты.Ссылка = &Ссылка
	|	И Вычеты.КодВычета.ГруппаВычета В (ЗНАЧЕНИЕ(Перечисление.ГруппыВычетовПоНДФЛ.СоциальныеПоУведомлениюНО), ЗНАЧЕНИЕ(Перечисление.ГруппыВычетовПоНДФЛ.Имущественные))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Вычеты.Ссылка.Сотрудник,
	|	Вычеты.МесяцНалоговогоПериода,
	|	Вычеты.ОбособленноеПодразделение,
	|	Вычеты.КодВычета,
	|	Вычеты.Сумма,
	|	Вычеты.НалоговаяБаза
	|ИЗ
	|	Документ.ОперацияНалоговогоУчетаПоНДФЛ.ПредоставленныеВычеты КАК Вычеты
	|ГДЕ
	|	Вычеты.Ссылка = &Ссылка
	|	И Вычеты.КодВычета.ГруппаВычета = ЗНАЧЕНИЕ(Перечисление.ГруппыВычетовПоНДФЛ.Социальные)
	|	И Вычеты.МесяцНалоговогоПериода >= &ДатаПереводаСоциальныхВычетовВОстаточныйРегистр
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Вычеты.Ссылка.Сотрудник КАК ФизическоеЛицо,
	|	Вычеты.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	Вычеты.ОбособленноеПодразделение КАК Подразделение,
	|	Вычеты.КодВычета КАК КодВычета,
	|	Вычеты.Сумма КАК Сумма,
	|	Вычеты.ВключатьВДекларациюПоНалогуНаПрибыль КАК ВключатьВДекларациюПоНалогуНаПрибыль,
	|	Вычеты.МесяцПериодаПредоставленияВычета КАК МесяцПериодаПредоставленияВычета,
	|	Вычеты.НалоговаяБаза КАК НалоговаяБаза
	|ПОМЕСТИТЬ ВТДанныеОСтандартныхВычетах
	|ИЗ
	|	Документ.ОперацияНалоговогоУчетаПоНДФЛ.ПредоставленныеВычеты КАК Вычеты
	|ГДЕ
	|	Вычеты.Ссылка = &Ссылка
	|	И НЕ Вычеты.КодВычета.ГруппаВычета В (ЗНАЧЕНИЕ(Перечисление.ГруппыВычетовПоНДФЛ.СоциальныеПоУведомлениюНО), ЗНАЧЕНИЕ(Перечисление.ГруппыВычетовПоНДФЛ.Имущественные))
	|	И НЕ(Вычеты.КодВычета.ГруппаВычета = ЗНАЧЕНИЕ(Перечисление.ГруппыВычетовПоНДФЛ.Социальные)
	|				И Вычеты.МесяцНалоговогоПериода >= &ДатаПереводаСоциальныхВычетовВОстаточныйРегистр)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.Ссылка.Сотрудник КАК ФизическоеЛицо,
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.ДатаПолученияДохода КАК ДатаПолученияДохода,
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.ОбособленноеПодразделение КАК Подразделение,
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.КодДохода КАК КодДохода,
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.КатегорияДохода КАК КатегорияДохода,
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.СуммаДохода КАК СуммаДохода,
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.КодВычета КАК КодВычета,
	|	ВЫБОР
	|		КОГДА ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.ДатаПолученияДохода >= &НачалоРасчетаНДФЛПоПрогрессивнойШкале
	|				И ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.КатегорияДохода = ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.Дивиденды)
	|			ТОГДА 0
	|		ИНАЧЕ ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.СуммаВычета
	|	КОНЕЦ КАК СуммаВычета,
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.СуммаНалогаИсчисленная КАК СуммаНалогаИсчисленная,
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.СуммаСПревышения КАК СуммаСПревышения,
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.ВключатьВДекларациюПоНалогуНаПрибыль КАК ВключатьВДекларациюПоНалогуНаПрибыль,
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.ИсточникДоходаЗаПределамиРФ КАК ИсточникДоходаЗаПределамиРФ,
	|	ВЫБОР
	|		КОГДА ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.ДатаПолученияДохода >= &НачалоРасчетаНДФЛПоПрогрессивнойШкале
	|				И ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.КатегорияДохода = ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.Дивиденды)
	|			ТОГДА ВЫБОР
	|					КОГДА ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.ДатаПолученияДохода >= ДАТАВРЕМЯ(2024, 1, 1, 0, 0, 0)
	|							И ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.ИсточникДоходаЗаПределамиРФ
	|						ТОГДА 0
	|					ИНАЧЕ ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.НалогНаПрибыльДляДивидендов
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НалогНаПрибыльДляДивидендов,
	|	ВЫБОР
	|		КОГДА ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.ДатаПолученияДохода >= ДАТАВРЕМЯ(2024, 1, 1, 0, 0, 0)
	|				И ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.КатегорияДохода = ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.Дивиденды)
	|				И ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.ИсточникДоходаЗаПределамиРФ
	|			ТОГДА ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.НалогНаПрибыльДляДивидендов
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НалогСДивидендовУплаченныйЗаРубежом,
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.СтрокаРаздела2Расчета6НДФЛ КАК СтрокаРаздела2Расчета6НДФЛ
	|ПОМЕСТИТЬ ВТСведенияОДоходахПоНДФЛ
	|ИЗ
	|	Документ.ОперацияНалоговогоУчетаПоНДФЛ.СведенияОДоходах КАК ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах
	|ГДЕ
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.Ссылка.Сотрудник КАК ФизическоеЛицо,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.ОбособленноеПодразделение КАК Подразделение,
	|	0 КАК СуммаНалога,
	|	ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13) КАК СтавкаНалогообложенияРезидента,
	|	ЗНАЧЕНИЕ(Справочник.ВидыДоходовНДФЛ.ПустаяСсылка) КАК КодДохода,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.ВключатьВДекларациюПоНалогуНаПрибыль КАК ВключатьВДекларациюПоНалогуНаПрибыль,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.КатегорияДохода КАК КатегорияДохода,
	|	0 КАК НалогНаПрибыльДляДивидендов,
	|	0 КАК НалогСДивидендовУплаченныйЗаРубежом
	|ПОМЕСТИТЬ ВТНДФЛИсчисленный
	|ИЗ
	|	Документ.ОперацияНалоговогоУчетаПоНДФЛ.НДФЛИсчисленныйПоСтавке13 КАК ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13
	|ГДЕ
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.ФизическоеЛицо,
	|	ВЫБОР
	|		КОГДА ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.ДатаПолученияДохода < &ДатаИзмененияПорядкаИсчисленияНалогаДляИностранцев
	|			ТОГДА НАЧАЛОПЕРИОДА(ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.ДатаПолученияДохода, МЕСЯЦ)
	|		ИНАЧЕ ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.ДатаПолученияДохода
	|	КОНЕЦ,
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.Подразделение,
	|	&СуммаНалогаИсчисленная,
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента,
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.КодДохода,
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.ВключатьВДекларациюПоНалогуНаПрибыль,
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.КатегорияДохода,
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.НалогНаПрибыльДляДивидендов,
	|	ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.НалогСДивидендовУплаченныйЗаРубежом
	|ИЗ
	|	ВТСведенияОДоходахПоНДФЛ КАК ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах
	|ГДЕ
	|	(ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента <> ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|			ИЛИ НЕ ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.КатегорияДохода В (&КатегорииДоходовОсновнойСтавки))
	|	И (ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.СуммаНалогаИсчисленная <> 0
	|			ИЛИ ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.СуммаСПревышения <> 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.Ссылка.Сотрудник КАК ФизическоеЛицо,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.ОбособленноеПодразделение КАК Подразделение,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.СтавкаНалогообложения КАК СтавкаНалогообложенияРезидента,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.Ставка КАК Ставка,
	|	ВЫБОР
	|		КОГДА ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.КатегорияДохода = ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.АвторскиеРоялтиПроцентыПоСтавке15)
	|			ТОГДА ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.Сумма
	|		КОГДА ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.СтавкаНалогообложения = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|				И ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.Ставка В (ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка15), ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка18), ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка20), ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка22))
	|			ТОГДА 0
	|		КОГДА ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.СтавкаНалогообложения = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
	|				И ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.МесяцНалоговогоПериода >= &НачалоРасчетаНДФЛПоПрогрессивнойШкале
	|				И ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.КатегорияДохода = ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.Дивиденды)
	|				И ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка15)
	|				И СтавкаНДФЛПоСтавкеРезидента.СтавкаНДФЛ = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|			ТОГДА 0
	|		ИНАЧЕ ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.Сумма
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.КатегорияДохода = ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.АвторскиеРоялтиПроцентыПоСтавке15)
	|			ТОГДА 0
	|		КОГДА ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.СтавкаНалогообложения = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|				И ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка15)
	|			ТОГДА ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.Сумма
	|		КОГДА ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.СтавкаНалогообложения = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
	|				И ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.МесяцНалоговогоПериода >= &НачалоРасчетаНДФЛПоПрогрессивнойШкале
	|				И ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.КатегорияДохода = ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.Дивиденды)
	|				И ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка15)
	|				И СтавкаНДФЛПоСтавкеРезидента.СтавкаНДФЛ = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|			ТОГДА ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.Сумма
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаСПревышения,
	|	ВЫБОР
	|		КОГДА ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.СтавкаНалогообложения = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|				И ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка18)
	|				И ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.МесяцНалоговогоПериода >= &ДатаРасширенияПрогрессивнойШкалыНалогообложения
	|			ТОГДА ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.Сумма
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаСПревышенияПоСтавке18,
	|	ВЫБОР
	|		КОГДА ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.СтавкаНалогообложения = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|				И ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка20)
	|				И ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.МесяцНалоговогоПериода >= &ДатаРасширенияПрогрессивнойШкалыНалогообложения
	|			ТОГДА ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.Сумма
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаСПревышенияПоСтавке20,
	|	ВЫБОР
	|		КОГДА ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.СтавкаНалогообложения = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|				И ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка22)
	|				И ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.МесяцНалоговогоПериода >= &ДатаРасширенияПрогрессивнойШкалыНалогообложения
	|			ТОГДА ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.Сумма
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаСПревышенияПоСтавке22,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.КодДохода КАК КодДохода,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.КатегорияДохода КАК КатегорияДохода,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.ВключатьВДекларациюПоНалогуНаПрибыль КАК ВключатьВДекларациюПоНалогуНаПрибыль,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.СрокПеречисления КАК СрокПеречисления,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.СуммаВыплаченногоДохода КАК СуммаВыплаченногоДохода,
	|	ВЫБОР
	|		КОГДА ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.ДокументОснование = НЕОПРЕДЕЛЕНО
	|			ТОГДА ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.Ссылка
	|		ИНАЧЕ ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.ДокументОснование
	|	КОНЕЦ КАК ДокументОснование,
	|	ИСТИНА КАК УчитыватьВыплаченныйДоходВ6НДФЛ
	|ПОМЕСТИТЬ ВТНалогУдержанный
	|ИЗ
	|	Документ.ОперацияНалоговогоУчетаПоНДФЛ.НДФЛУдержанный КАК ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтавкаНДФЛПоСтавкеРезидента КАК СтавкаНДФЛПоСтавкеРезидента
	|		ПО ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.Ссылка.Сотрудник = СтавкаНДФЛПоСтавкеРезидента.ФизическоеЛицо
	|			И ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.КатегорияДохода = СтавкаНДФЛПоСтавкеРезидента.КатегорияДохода
	|			И ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.СтавкаНалогообложения = СтавкаНДФЛПоСтавкеРезидента.СтавкаНалогообложенияРезидента
	|			И ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.МесяцНалоговогоПериода = СтавкаНДФЛПоСтавкеРезидента.Период
	|ГДЕ
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛУдержанный.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СведенияОДоходахПоНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СведенияОДоходахПоНДФЛ.ДатаПолученияДохода КАК ДатаПолученияДохода,
	|	НАЧАЛОПЕРИОДА(СведенияОДоходахПоНДФЛ.ДатаПолученияДохода, МЕСЯЦ) КАК МесяцНалоговогоПериода,
	|	СведенияОДоходахПоНДФЛ.Подразделение КАК Подразделение,
	|	СведенияОДоходахПоНДФЛ.КодДохода КАК КодДохода,
	|	СведенияОДоходахПоНДФЛ.СуммаДохода КАК СуммаДохода,
	|	СведенияОДоходахПоНДФЛ.КодВычета КАК КодВычета,
	|	СведенияОДоходахПоНДФЛ.КатегорияДохода КАК КатегорияДохода,
	|	СведенияОДоходахПоНДФЛ.СуммаВычета КАК СуммаВычета,
	|	СведенияОДоходахПоНДФЛ.ВключатьВДекларациюПоНалогуНаПрибыль КАК ВключатьВДекларациюПоНалогуНаПрибыль,
	|	СведенияОДоходахПоНДФЛ.ИсточникДоходаЗаПределамиРФ КАК ИсточникДоходаЗаПределамиРФ,
	|	СведенияОДоходахПоНДФЛ.СтрокаРаздела2Расчета6НДФЛ КАК СтрокаРаздела2Расчета6НДФЛ
	|ИЗ
	|	ВТСведенияОДоходахПоНДФЛ КАК СведенияОДоходахПоНДФЛ
	|ГДЕ
	|	(СведенияОДоходахПоНДФЛ.СуммаДохода <> 0
	|			ИЛИ СведенияОДоходахПоНДФЛ.СуммаВычета <> 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛПеречисленный.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛПеречисленный.Ссылка.Сотрудник КАК ФизическоеЛицо,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛПеречисленный.Ставка КАК Ставка,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛПеречисленный.Сумма КАК Сумма,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛПеречисленный.РеквизитыПлатежногоПоручения КАК РеквизитыПлатежногоПоручения,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛПеречисленный.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛПеречисленный.ВключатьВДекларациюПоНалогуНаПрибыль КАК ВключатьВДекларациюПоНалогуНаПрибыль,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛПеречисленный.ИсчисленоПоДивидендам КАК ИсчисленоПоДивидендам
	|ПОМЕСТИТЬ ВТНалогПеречисленный
	|ИЗ
	|	Документ.ОперацияНалоговогоУчетаПоНДФЛ.НДФЛПеречисленный КАК ОперацияНалоговогоУчетаПоНДФЛНДФЛПеречисленный
	|ГДЕ
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛПеречисленный.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.Ссылка.Сотрудник КАК ФизическоеЛицо,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.ОбособленноеПодразделение КАК Подразделение,
	|	0 КАК Сумма,
	|	ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13) КАК СтавкаНалогообложенияРезидента,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.ВключатьВДекларациюПоНалогуНаПрибыль КАК ВключатьВДекларациюПоНалогуНаПрибыль,
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.КатегорияДохода КАК КатегорияДохода
	|ПОМЕСТИТЬ ВТДанныеОЗачтенныхПлатежах
	|ИЗ
	|	Документ.ОперацияНалоговогоУчетаПоНДФЛ.НДФЛИсчисленныйПоСтавке13 КАК ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13
	|ГДЕ
	|	ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТСтавкаНДФЛПоСтавкеРезидента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТПериодыНалогаУдержанного";
	ИменаРесурсовНалога = УчетНДФЛ.РесурсыИсчисленногоНалогаВМассиве();
	ИменаРесурсовЗачета = УчетНДФЛ.РесурсыИсчисленногоНалогаВМассиве("ЗачтеноАвансовыхПлатежей");
	ТекстНалогов = "";
	Для каждого ИмяРесурса Из ИменаРесурсовНалога Цикл
		ТекстНалогов = ТекстНалогов + "ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13." + ИмяРесурса + " КАК " + ИмяРесурса + "," + Символы.ПС;
	КонецЦикла;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "0 КАК СуммаНалога,", ТекстНалогов);
	ТекстНалогов = "";
	Для каждого ИмяРесурса Из ИменаРесурсовНалога Цикл
		Если ИмяРесурса = "Сумма" Тогда
			ТекстНалогов = ТекстНалогов + "ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.СуммаНалогаИсчисленная," + Символы.ПС;
		ИначеЕсли ИмяРесурса = "СуммаСПревышения" Тогда
			ТекстНалогов = ТекстНалогов + "ОперацияНалоговогоУчетаПоНДФЛСведенияОДоходах.СуммаСПревышения," + Символы.ПС;
		Иначе
			ТекстНалогов = ТекстНалогов + "0," + Символы.ПС;
		КонецЕсли;
	КонецЦикла;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаНалогаИсчисленная,", ТекстНалогов);
	ТекстНалогов = "";
	Для каждого ИмяРесурса Из ИменаРесурсовЗачета Цикл
		ТекстНалогов = ТекстНалогов + "ОперацияНалоговогоУчетаПоНДФЛНДФЛИсчисленныйПоСтавке13." + ИмяРесурса + " КАК " + СтрЗаменить(ИмяРесурса, "ЗачтеноАвансовыхПлатежей", "Сумма") + "," + Символы.ПС;
	КонецЦикла;
	Запрос.Текст = СтрЗаменить(ТекстЗапроса, "0 КАК Сумма,", ТекстНалогов);
	Результаты = Запрос.ВыполнитьПакет();
	
	Доходы = Результаты[5].Выгрузить();
	
	ДанныеДляПроведения = Новый Структура("Доходы, МенеджерВременныхТаблиц", Доходы, Запрос.МенеджерВременныхТаблиц);
	
	ДанныеОЗадолженности = НДФЛПереданныйВНалоговую.Выгрузить();
	ДанныеДляПроведения.Вставить("НалогиПереданныеВНалоговыйОрган", ДанныеОЗадолженности);
	Если ДанныеОЗадолженности.Количество() > 0 Тогда
		
		МесяцНалоговогоПериода = Мин(Дата(НалоговыйПериод, 12, 1), НачалоМесяца(ДатаОперации));
		РегистрацияВНалоговомОргане = ЗарплатаКадры.РегистрацияВНалоговомОргане(Организация, ДатаОперации);
		
		ДанныеОЗадолженности.Колонки.Добавить("ФизическоеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
		ДанныеОЗадолженности.Колонки.Добавить("МесяцНалоговогоПериода", Новый ОписаниеТипов("Дата"));
		ДанныеОЗадолженности.Колонки.Добавить("Задолженность", Новый ОписаниеТипов("Число"));
		ДанныеОЗадолженности.Колонки.СтавкаНалогообложения.Имя = "СтавкаНалогообложенияРезидента";
		Для Каждого СтрокаЗадолженности Из ДанныеОЗадолженности Цикл 
			Если Не ЗначениеЗаполнено(СтрокаЗадолженности.РегистрацияВНалоговомОргане) Тогда
				СтрокаЗадолженности.РегистрацияВНалоговомОргане = РегистрацияВНалоговомОргане
			КонецЕсли;
			СтрокаЗадолженности.МесяцНалоговогоПериода = МесяцНалоговогоПериода;
			СтрокаЗадолженности.ФизическоеЛицо = Сотрудник;
			СтрокаЗадолженности.Задолженность = УчетНДФЛ.ИтоговаяСуммаНДФЛпоСтроке(СтрокаЗадолженности);
		КонецЦикла;
		
		ЗадолженностьФизлиц = ДанныеОЗадолженности.Скопировать(,"РегистрацияВНалоговомОргане,ФизическоеЛицо,Задолженность");
		ЗадолженностьФизлиц.Колонки.Задолженность.Имя = "Сумма";
		ЗадолженностьФизлиц.Свернуть("РегистрацияВНалоговомОргане,ФизическоеЛицо","Сумма");
		
		ДанныеДляПроведения.Вставить("НалогиСотрудников", ЗадолженностьФизлиц);
		
	КонецЕсли;
	
	ДанныеОбУдержанныхВзносах = ВзносыЗасчитываемыеКакВычеты.Выгрузить();
	ДанныеОбУдержанныхВзносах.Колонки["МесяцУдержания"].Имя = "МесяцРегистрации";
	ДанныеОбУдержанныхВзносах.Колонки.Добавить("Год", Новый ОписаниеТипов("Число"));
	ДанныеОбУдержанныхВзносах.Колонки.Добавить("ФизическоеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	ДанныеОбУдержанныхВзносах.Колонки.Добавить("МесяцНалоговогоПериода", Новый ОписаниеТипов("Дата"));
	Для каждого СтрокаВзносов Из ДанныеОбУдержанныхВзносах Цикл
		СтрокаВзносов.Год = Год(СтрокаВзносов.МесяцРегистрации);
		СтрокаВзносов.МесяцНалоговогоПериода = СтрокаВзносов.МесяцРегистрации;
		СтрокаВзносов.ФизическоеЛицо = Сотрудник;
	КонецЦикла;
	ДанныеДляПроведения.Вставить("ДанныеОбУдержанныхВзносах", ДанныеОбУдержанныхВзносах);
	
	Возврат ДанныеДляПроведения;

КонецФункции

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли