#Область ОписаниеПеременных

&НаКлиенте
Перем КонтекстЭДОКлиент Экспорт;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПараметрыФормы = Параметры.ПараметрыФормы;
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПараметрыФормы);
	ТекстПриглашения = Новый ФорматированнаяСтрока(ПараметрыФормы.ТекстПриглашения);
	
	ОбновитьТекстПриглашения();
	Если НЕ ЗначениеЗаполнено(ТекстПриглашения) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииЗавершение", ЭтотОбъект);
	ДокументооборотСКОКлиент.ПолучитьКонтекстЭДО(ОписаниеОповещения);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
// Код процедур и функций
#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы
// Код процедур и функций
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодготовитьЗаявление(Команда)
	Закрыть(Истина);
КонецПроцедуры

&НаКлиенте
Процедура НапомнитьПозже(Команда)
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура БольшеНеНапоминать(Команда)
	БольшеНеНапоминатьНаСервере();
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПриОткрытииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	КонтекстЭДОКлиент = Результат.КонтекстЭДО;
	
КонецПроцедуры

&НаСервере
Процедура БольшеНеНапоминатьНаСервере()
	
	Мультирежим.БольшеНеНапоминатьПроПриглашение(Организация, УчетнаяЗапись, Пользователь);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьТекстПриглашения()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ЗначениеЗаполнено(Заявление) Тогда
		
		Таблица = Мультирежим.ТаблицаПользователейСДопПолями(Заявление);
		Строка = МультирежимКлиентСервер.СтрокаТаблицыПользователей(Таблица, Пользователь);
		
		Если Строка = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Приглашение = Мультирежим.ДанныеПриглашенияПользователя(Заявление, Строка);
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Приглашение);
		
		Если НЕ Приглашение.ЕстьИзменение Тогда
			Возврат;
		КонецЕсли;
		
		ЭтоПодключение = Приглашение.ЭтоПодключение;
		
		// Текст получается форматированным, т.к. не из регистра
		ТекстПриглашения = Приглашение.ТекстПриглашения;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеФормой()
	
	Элементы.ТекстПриглашения.Заголовок = ТекстПриглашения;
	Элементы.ФормаПодготовитьЗаявление.КнопкаПоУмолчанию = ЭтоПодключение;
	Элементы.ФормаПодготовитьЗаявление.Видимость = ЭтоПодключение;
	Элементы.ФормаНапомнитьПозже1.Видимость = ЭтоПодключение;
	Элементы.ФормаНапомнитьПозже2.Видимость = НЕ ЭтоПодключение;
	Элементы.ФормаБольшеНеНапоминать.КнопкаПоУмолчанию = НЕ ЭтоПодключение;
	
	Если ЭтоПодключение Тогда
		Заголовок = НСтр("ru = 'Приглашение на подключение';
						|en = 'Приглашение на подключение'");
	Иначе
		Заголовок = НСтр("ru = 'Уведомление об изменении прав';
						|en = 'Уведомление об изменении прав'");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

