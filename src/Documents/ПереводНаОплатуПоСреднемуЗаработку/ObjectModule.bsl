#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") 
		И ДанныеЗаполнения.Свойство("Действие") И ДанныеЗаполнения.Действие = "Исправить" Тогда
			
		ИсправлениеДокументовЗарплатаКадры.СкопироватьДокумент(ЭтотОбъект, ДанныеЗаполнения.Ссылка);
			
		ИсправленныйДокумент = ДанныеЗаполнения.Ссылка;
			
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Проведение документа
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	ЗарплатаКадрыРасширенный.ИнициализироватьОтложеннуюРегистрациюВторичныхДанныхПоДвижениямДокумента(Движения);
	
	ДанныеДляПроведения = ДанныеДляПроведения();
	
	ЗарплатаКадрыРасширенный.УстановитьВремяРегистрацииДокумента(Движения, ДанныеДляПроведения.СотрудникиДаты, Ссылка);
	
	ИсправлениеПериодическихСведений.ИсправлениеПериодическихСведений(ЭтотОбъект, Отказ, РежимПроведения, Ссылка, ИсправленныйДокумент);
	
	Если РазмерОплатыУтвержден Тогда 
	
		СтруктураПлановыхНачислений = Новый Структура;
		СтруктураПлановыхНачислений.Вставить("ДанныеОПлановыхНачислениях", ДанныеДляПроведения.ПлановыеНачисления);
		СтруктураПлановыхНачислений.Вставить("ЗначенияПоказателей", ДанныеДляПроведения.ЗначенияПоказателей);
		
		РасчетЗарплаты.СформироватьДвиженияПлановыхНачислений(ЭтотОбъект, Движения, СтруктураПлановыхНачислений);
		
		ОтражениеЗарплатыВБухучетеРасширенный.СформироватьДвиженияКоэффициентыРаспределенияСреднегоЗаработка(Движения, ДанныеДляПроведения.КоэффициентыРаспределенияСреднегоЗаработка);
		
		// Корректировки данных для среднего заработка.
		ДополнительныеПараметрыЗаписиКорректировокОбщегоСреднегоЗаработка = УчетСреднегоЗаработка.ДополнительныеПараметрыЗаписиКорректировокОбщегоСреднегоЗаработка();
		ДополнительныеПараметрыЗаписиКорректировокОбщегоСреднегоЗаработка.Организация = Организация;
		ДополнительныеПараметрыЗаписиКорректировокОбщегоСреднегоЗаработка.Сотрудник = Сотрудник;
		ДополнительныеПараметрыЗаписиКорректировокОбщегоСреднегоЗаработка.ФизическоеЛицо = ФизическоеЛицо;
			
		УчетСреднегоЗаработка.ЗаписатьКорректировкиОбщегоСреднегоЗаработка(
			УчетСреднегоЗаработка.КорректировкиОбщегоСреднегоЗаработкаДокумента(Ссылка), 
			ПериодРасчетаСреднегоЗаработкаНачало, 
			ПериодРасчетаСреднегоЗаработкаОкончание, 
			ДополнительныеПараметрыЗаписиКорректировокОбщегоСреднегоЗаработка);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ЗарплатаКадры.ПроверитьКорректностьДаты(Ссылка, ДатаНачала, "Объект.ДатаНачала", Отказ, НСтр("ru = 'Дата начала';
																								|en = 'Start date'"), '19800101', , Ложь);
	
	КонтейнерОшибок = Неопределено;
	
	ПроверитьЗаполнениеРеквизитовШапки(КонтейнерОшибок);	
	
	Если РазмерОплатыУтвержден Тогда 
		
		ИсправлениеДокументовЗарплатаКадры.ПроверитьЗаполнение(ЭтотОбъект, ПроверяемыеРеквизиты, Отказ, "ПериодическиеСведения");
		
		Если Не ЗначениеЗаполнено(ВидРасчета) Тогда
			ТекстСообщения = Документы.ПереводНаОплатуПоСреднемуЗаработку.ТекстСообщенияНеЗаполненВидРасчета();
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(КонтейнерОшибок, "Объект.ВидРасчета", ТекстСообщения, "");
		КонецЕсли;
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(КонтейнерОшибок, Отказ);
	
	УдалитьПроверенныеРеквизиты(ПроверяемыеРеквизиты);
	
	КадровыйЭДО.ПроверитьВозможностьСохраненияИзмененийДокументаСПечатнымиФормами(
		ЭтотОбъект, Метаданные.Роли.ДобавлениеИзменениеДанныхДляНачисленияЗарплатыРасширенная, Отказ);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	ПредставлениеПериода = ЗарплатаКадрыРасширенный.ПредставлениеПериодаРасчетногоДокумента(ДатаНачала, ДатаОкончания);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	УчетСреднегоЗаработка.ЗаписатьДатуНачалаСобытия(Ссылка, Сотрудник, ДатаНачалаСобытия);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьЗаполнениеРеквизитовШапки(КонтейнерОшибок)
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		ТекстСообщения = НСтр("ru = 'Поле ""Организация"" обязательно к заполнению.';
								|en = 'Field ""Company"" is required.'");
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(КонтейнерОшибок, "Объект.Организация", ТекстСообщения, "");
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Сотрудник) Тогда
		ТекстСообщения = НСтр("ru = 'Поле ""Сотрудник"" обязательно к заполнению.';
								|en = 'Field ""Employee"" is required.'");
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(КонтейнерОшибок, "Объект.Сотрудник", ТекстСообщения, "");
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДатаНачала) И Не ЗначениеЗаполнено(ДатаОкончания) Тогда
		ТекстСообщения = НСтр("ru = 'Не указаны даты оплаты по среднему заработку.';
								|en = 'Average earnings payment dates are not specified.'");
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(КонтейнерОшибок, "Объект.ДатаОкончания", ТекстСообщения, "");
	Иначе
		Если Не ЗначениеЗаполнено(ДатаНачала) И ЗначениеЗаполнено(ДатаОкончания) Тогда
			ТекстСообщения = НСтр("ru = 'Не заполнена дата начала оплаты по среднему заработку.';
									|en = 'Start date of average earnings payment is not entered.'");
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(КонтейнерОшибок, "Объект.ДатаНачала", ТекстСообщения, "");
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ДатаОкончания) И ЗначениеЗаполнено(ДатаНачала) Тогда
			ТекстСообщения = НСтр("ru = 'Не заполнена дата окончания оплаты по среднему заработку.';
									|en = 'End date of average earnings payment is not entered.'");
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(КонтейнерОшибок, "Объект.ДатаОкончания", ТекстСообщения, "");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДатаНачала) И ЗначениеЗаполнено(ДатаОкончания) И ДатаНачала > ДатаОкончания Тогда
			ТекстСообщения = НСтр("ru = 'Дата окончания оплаты по среднему заработку не может быть меньше даты начала.';
									|en = 'End date of average earnings payment cannot be less than the start date.'");
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(КонтейнерОшибок, "Объект.ДатаОкончания", ТекстСообщения, "");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура УдалитьПроверенныеРеквизиты(ПроверяемыеРеквизиты)
	
	Если ПроверяемыеРеквизиты = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Организация");
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Сотрудник");
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ДатаНачала");
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ДатаОкончания");
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ВидРасчета");
	
	Если Не РазмерОплатыУтвержден Тогда 
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ПроцентОплаты");
	КонецЕсли;
	
КонецПроцедуры

Функция ДанныеДляПроведения()
	
	ДанныеДляПроведения = Новый Структура;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Если РазмерОплатыУтвержден Тогда
		
		Запрос.Текст = "ВЫБРАТЬ
		               |	ПереводНаОплатуПоСреднемуЗаработку.Сотрудник КАК Сотрудник,
		               |	ПереводНаОплатуПоСреднемуЗаработку.ВидРасчета КАК Начисление,
					   |	ПереводНаОплатуПоСреднемуЗаработку.ВидРасчетаДолиРайонногоКоэффициента КАК НачислениеДолиРайонногоКоэффициента,
					   |	ПереводНаОплатуПоСреднемуЗаработку.ВидРасчетаДолиСевернойНадбавки КАК НачислениеДолиСевернойНадбавки,
		               |	ПереводНаОплатуПоСреднемуЗаработку.ДатаНачалаСобытия КАК ДатаСобытия,
		               |	ПереводНаОплатуПоСреднемуЗаработку.Ссылка КАК ДокументОснование,
		               |	ИСТИНА КАК Используется,
		               |	ПереводНаОплатуПоСреднемуЗаработку.ФизическоеЛицо КАК ФизическоеЛицо,
		               |	ПереводНаОплатуПоСреднемуЗаработку.Сотрудник.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
		               |	ДОБАВИТЬКДАТЕ(ПереводНаОплатуПоСреднемуЗаработку.ДатаОкончания, ДЕНЬ, 1) КАК ДействуетДо
		               |ИЗ
		               |	Документ.ПереводНаОплатуПоСреднемуЗаработку КАК ПереводНаОплатуПоСреднемуЗаработку
		               |ГДЕ
		               |	ПереводНаОплатуПоСреднемуЗаработку.Ссылка = &Ссылка";
					   
		Выборка = Запрос.Выполнить().Выбрать();
		Выборка.Следующий();
		
		ДанныеОПлановыхНачислениях = РасчетЗарплатыРасширенный.ПустаяТаблицаРегистрацииПлановыхНачислений(Истина);
		ДанныеОПлановыхНачислениях.Колонки.Добавить("ИспользуетсяПоОкончании", Новый ОписаниеТипов("Булево"));
		
		НоваяСтрока = ДанныеОПлановыхНачислениях.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
		ДополнительныеПараметры = УчетСреднегоЗаработкаКлиентСервер.ДополнительныеПараметрыРасчетаСреднегоЗаработка();
		ДополнительныеПараметры.Индексации = ДанныеОбИндексации;
		ДополнительныеПараметры.ДатаНачалаСобытия = ДатаНачалаСобытия;
		ДополнительныеПараметры.НачалоПериода = ПериодРасчетаСреднегоЗаработкаНачало;
		ДополнительныеПараметры.ОкончаниеПериода = ПериодРасчетаСреднегоЗаработкаОкончание;
		ДополнительныеПараметры.ПоЧасам = ИспользуетсяСреднечасовойЗаработок();
		ЗначенияПоказателейСреднегоЗаработка = УчетСреднегоЗаработкаКлиентСервер.ЗначенияПоказателейСреднегоЗаработкаОбщего(СреднийЗаработокОбщий, ОтработанноеВремяДляСреднегоОбщий, ДополнительныеПараметры);
		
		//Дополнительные начисления долей РК, СН
		ЗаполнятьНачислениеДолиРК = РасчетЗарплатыРасширенный.ЗаполнятьНачислениеДолиРКСН(ЗначенияПоказателейСреднегоЗаработка, Выборка.НачислениеДолиРайонногоКоэффициента);
		ЗаполнятьНачислениеДолиСН = РасчетЗарплатыРасширенный.ЗаполнятьНачислениеДолиРКСН(ЗначенияПоказателейСреднегоЗаработка, Выборка.НачислениеДолиСевернойНадбавки);
		Если ЗаполнятьНачислениеДолиРК Тогда
			НоваяСтрока = ДанныеОПлановыхНачислениях.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.Начисление = Выборка.НачислениеДолиРайонногоКоэффициента;
		КонецЕсли;
		
		Если ЗаполнятьНачислениеДолиСН Тогда
			НоваяСтрока = ДанныеОПлановыхНачислениях.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.Начисление = Выборка.НачислениеДолиСевернойНадбавки;
		КонецЕсли;
			
		ДанныеДляПроведения.Вставить("ПлановыеНачисления", ДанныеОПлановыхНачислениях);
		
		Запрос.Текст = "ВЫБРАТЬ
		               |	ПереводНаОплатуПоСреднемуЗаработку.Организация КАК Организация,
		               |	ПереводНаОплатуПоСреднемуЗаработку.Сотрудник КАК Сотрудник,
		               |	ПереводНаОплатуПоСреднемуЗаработку.ДатаНачалаСобытия КАК ДатаСобытия,
		               |	ПереводНаОплатуПоСреднемуЗаработку.Ссылка КАК ДокументОснование,
		               |	ПереводНаОплатуПоСреднемуЗаработку.ФизическоеЛицо КАК ФизическоеЛицо,
		               |	ДОБАВИТЬКДАТЕ(ПереводНаОплатуПоСреднемуЗаработку.ДатаОкончания, ДЕНЬ, 1) КАК ДействуетДо,
		               |	ПереводНаОплатуПоСреднемуЗаработку.СреднийЗаработок КАК Значение
		               |ИЗ
		               |	Документ.ПереводНаОплатуПоСреднемуЗаработку КАК ПереводНаОплатуПоСреднемуЗаработку
		               |ГДЕ
		               |	ПереводНаОплатуПоСреднемуЗаработку.Ссылка = &Ссылка";
					   
		Выборка = Запрос.Выполнить().Выбрать();
		Выборка.Следующий();
		
		ЗначенияПоказателейПлановыхНачислений = Новый ТаблицаЗначений;
		ЗначенияПоказателейПлановыхНачислений.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
		ЗначенияПоказателейПлановыхНачислений.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
		ЗначенияПоказателейПлановыхНачислений.Колонки.Добавить("ДатаСобытия", Новый ОписаниеТипов("Дата"));
		ЗначенияПоказателейПлановыхНачислений.Колонки.Добавить("ДействуетДо", Новый ОписаниеТипов("Дата"));
		ЗначенияПоказателейПлановыхНачислений.Колонки.Добавить("ДокументОснование", Метаданные.ОпределяемыеТипы.ОснованиеНачисленияУдержания.Тип);
		ЗначенияПоказателейПлановыхНачислений.Колонки.Добавить("Показатель", Новый ОписаниеТипов("СправочникСсылка.ПоказателиРасчетаЗарплаты"));
		ЗначенияПоказателейПлановыхНачислений.Колонки.Добавить("Значение", Новый ОписаниеТипов("Число"));
		ЗначенияПоказателейПлановыхНачислений.Колонки.Добавить("ФизическоеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
		
		НоваяСтрока = ЗначенияПоказателейПлановыхНачислений.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		НоваяСтрока.Показатель = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ПоказателиРасчетаЗарплаты.ПроцентОплатыПоСреднему");
		НоваяСтрока.Значение = ПроцентОплаты;
		
		Для Каждого КлючИЗначение Из ЗначенияПоказателейСреднегоЗаработка Цикл
			Если КлючИЗначение.Значение = 0 Тогда
				Продолжить;
			КонецЕсли;
			НоваяСтрока = ЗначенияПоказателейПлановыхНачислений.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.Значение = КлючИЗначение.Значение;
			НоваяСтрока.Показатель = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ПоказателиРасчетаЗарплаты." + КлючИЗначение.Ключ);
		КонецЦикла;
				
		ДанныеДляПроведения.Вставить("ЗначенияПоказателей", ЗначенияПоказателейПлановыхНачислений);
		
		Запрос.Текст = "ВЫБРАТЬ
		               |	Коэффициенты.Ссылка КАК ДокументОснование,
		               |	Коэффициенты.СтатьяФинансирования КАК СтатьяФинансирования,
		               |	Коэффициенты.СтатьяРасходов КАК СтатьяРасходов,
		               |	Коэффициенты.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		               |	Коэффициенты.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		               |	СУММА(Коэффициенты.Коэффициент) КАК Коэффициент
		               |ИЗ
		               |	Документ.ПереводНаОплатуПоСреднемуЗаработку.КоэффициентыРаспределенияСреднегоЗаработка КАК Коэффициенты
		               |ГДЕ
		               |	Коэффициенты.Ссылка = &Ссылка
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	Коэффициенты.Ссылка,
		               |	Коэффициенты.СтатьяФинансирования,
		               |	Коэффициенты.СтатьяРасходов,
		               |	Коэффициенты.СпособОтраженияЗарплатыВБухучете,
		               |	Коэффициенты.ОблагаетсяЕНВД";
		
		КоэффициентыРаспределения = Запрос.Выполнить().Выгрузить();
		ДанныеДляПроведения.Вставить("КоэффициентыРаспределенияСреднегоЗаработка", КоэффициентыРаспределения);
		
	КонецЕсли;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПереводНаОплатуПоСреднемуЗаработку.Сотрудник КАК Сотрудник,
	               |	ПереводНаОплатуПоСреднемуЗаработку.ДатаНачалаСобытия КАК ДатаСобытия
	               |ИЗ
	               |	Документ.ПереводНаОплатуПоСреднемуЗаработку КАК ПереводНаОплатуПоСреднемуЗаработку
	               |ГДЕ
	               |	ПереводНаОплатуПоСреднемуЗаработку.Ссылка = &Ссылка";
				   
	СотрудникиДаты = Запрос.Выполнить().Выгрузить();
	ДанныеДляПроведения.Вставить("СотрудникиДаты", СотрудникиДаты);
	
	Возврат ДанныеДляПроведения;
	
КонецФункции

Функция ИспользуетсяСреднечасовойЗаработок()
	
	ИспользуетсяСреднечасовойЗаработок = Ложь;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если УчетРабочегоВремениРасширенный.СотрудникуПрименяетсяСуммированныйУчетРабочегоВремени(Сотрудник, ДатаНачалаСобытия) Тогда
		ИспользуетсяСреднечасовойЗаработок = Истина;
		Возврат ИспользуетсяСреднечасовойЗаработок;
	КонецЕсли;
	
	Если УчетСреднегоЗаработка.НачислениеИспользуетСреднечасовойЗаработок(ВидРасчета) Тогда
		ИспользуетсяСреднечасовойЗаработок = Истина;
	КонецЕсли;
	
	Возврат ИспользуетсяСреднечасовойЗаработок;
	
КонецФункции
#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли