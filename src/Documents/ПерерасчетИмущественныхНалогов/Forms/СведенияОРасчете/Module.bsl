#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ВидНалога = Параметры.ВидНалога;
	
	Если ВидНалога = Перечисления.ВидыИмущественныхНалогов.НалогНаИмущество Тогда

		СведенияОРасчете = ПолучитьИзВременногоХранилища(Параметры.АдресВременногоХранилища); // ДокументТабличнаяЧасть.ПерерасчетИмущественныхНалогов.РасчетНалогаНаИмущество
	
		Элементы.СтраницыНалоги.ТекущаяСтраница = Элементы.СтраницаРасчетНалогаНаИмущество;

		Для Каждого СтрокаРасчет Из СведенияОРасчете Цикл
			
			НоваяСтрока = РасчетНалогаНаИмущество.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРасчет);
			
			СуммаНалогаКУплатеДоИсправленийИтого = СуммаНалогаКУплатеДоИсправленийИтого + НоваяСтрока.СуммаНалогаКУплатеДоИсправлений;
			СуммаНалогаКУплатеИтого = СуммаНалогаКУплатеИтого + НоваяСтрока.СуммаНалогаКУплате;
			
			Если Год(СтрокаРасчет.ПериодРасчета) < 2016
				ИЛИ Месяц(СтрокаРасчет.ПериодРасчета) = 12 Тогда
				
				КоличествоМесяцевВПериоде = Месяц(СтрокаРасчет.ПериодРасчета);
			Иначе
				КоличествоМесяцевВПериоде = 3;
			КонецЕсли;
			
			Если СтрокаРасчет.ДоляВПравеОбщейСобственностиЧислитель <> 0 
				И СтрокаРасчет.ДоляВПравеОбщейСобственностиЗнаменатель <> 0 Тогда
					
				НоваяСтрока.ДоляВПравеОбщейСобственности = 
					Строка(СтрокаРасчет.ДоляВПравеОбщейСобственностиЧислитель)
					+ " / "
					+ Строка(СтрокаРасчет.ДоляВПравеОбщейСобственностиЗнаменатель);
					
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтрокаРасчет.КоличествоМесяцевВладения) Тогда

				НоваяСтрока.КоэффициентК =
					Строка(СтрокаРасчет.КоличествоМесяцевВладения)
					+ " / "
					+ Строка(КоличествоМесяцевВПериоде);
					
			КонецЕсли;
			
			Если СтрокаРасчет.КоличествоМесяцевИспользования = 0
				ИЛИ СтрокаРасчет.КоличествоМесяцевВладения = 0
				ИЛИ СтрокаРасчет.КоличествоМесяцевИспользования = СтрокаРасчет.КоличествоМесяцевВладения Тогда
					
				НоваяСтрока.КоэффициентКи = 0;
			Иначе
				НоваяСтрока.КоэффициентКи = СтрокаРасчет.КоличествоМесяцевИспользования / СтрокаРасчет.КоличествоМесяцевВладения;
			КонецЕсли;
			
			Если СтрокаРасчет.КоличествоМесяцевВладения = 0 
				ИЛИ СтрокаРасчет.КоличествоМесяцевВладения = КоличествоМесяцевВПериоде Тогда
				
				НоваяСтрока.КоэффициентКв = 0;
			Иначе
				НоваяСтрока.КоэффициентКв = СтрокаРасчет.КоличествоМесяцевВладения / КоличествоМесяцевВПериоде;
			КонецЕсли;

		КонецЦикла;

	ИначеЕсли ВидНалога = Перечисления.ВидыИмущественныхНалогов.ТранспортныйНалог Тогда
			
		СведенияОРасчете = ПолучитьИзВременногоХранилища(Параметры.АдресВременногоХранилища); // ДокументТабличнаяЧасть.ПерерасчетИмущественныхНалогов.РасчетТранспортногоНалога

		Элементы.СтраницыНалоги.ТекущаяСтраница = Элементы.СтраницаРасчетТранспортногоНалога;

		Для Каждого СтрокаРасчет Из СведенияОРасчете Цикл
			
			НоваяСтрока = РасчетТранспортногоНалога.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРасчет);
			
			СуммаНалогаКУплатеДоИсправленийИтого = СуммаНалогаКУплатеДоИсправленийИтого + НоваяСтрока.СуммаНалогаКУплатеДоИсправлений;
			СуммаНалогаКУплатеИтого = СуммаНалогаКУплатеИтого + НоваяСтрока.СуммаНалогаКУплате;

			НоваяСтрока.СуммаАвансовыхПлатежей = 
				СтрокаРасчет.СуммаАвансовыхПлатежей1Кв
				+ СтрокаРасчет.СуммаАвансовыхПлатежей2Кв
				+ СтрокаРасчет.СуммаАвансовыхПлатежей3Кв;
				
			Если СтрокаРасчет.ДоляВПравеОбщейСобственностиЧислитель <> 0 
				И СтрокаРасчет.ДоляВПравеОбщейСобственностиЗнаменатель <> 0 Тогда
					
				НоваяСтрока.ДоляВПравеОбщейСобственности = 
					Строка(СтрокаРасчет.ДоляВПравеОбщейСобственностиЧислитель)
					+ " / "
					+ Строка(СтрокаРасчет.ДоляВПравеОбщейСобственностиЗнаменатель);
					
			КонецЕсли;
			
			НоваяСтрока.Коэффициент = 
				СтрокаРасчет.КоличествоМесяцевВладения 
				/ ?(Месяц(СтрокаРасчет.ПериодРасчета) = 12, 12, 3);
				
		КонецЦикла;
		
	ИначеЕсли ВидНалога = Перечисления.ВидыИмущественныхНалогов.ЗемельныйНалог Тогда
		
		СведенияОРасчете = ПолучитьИзВременногоХранилища(Параметры.АдресВременногоХранилища); // ДокументТабличнаяЧасть.ПерерасчетИмущественныхНалогов.РасчетЗемельногоНалога

		Элементы.СтраницыНалоги.ТекущаяСтраница = Элементы.СтраницаРасчетЗемельногоНалога;

		Для Каждого СтрокаРасчет Из СведенияОРасчете Цикл
			
			НоваяСтрока = РасчетЗемельногоНалога.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРасчет);
				
			СуммаНалогаКУплатеДоИсправленийИтого = СуммаНалогаКУплатеДоИсправленийИтого + НоваяСтрока.СуммаНалогаКУплатеДоИсправлений;
			СуммаНалогаКУплатеИтого = СуммаНалогаКУплатеИтого + НоваяСтрока.СуммаНалогаКУплате;

			НоваяСтрока.СуммаАвансовыхПлатежей = 
				СтрокаРасчет.СуммаАвансовыхПлатежей1Кв
				+ СтрокаРасчет.СуммаАвансовыхПлатежей2Кв
				+ СтрокаРасчет.СуммаАвансовыхПлатежей3Кв;
				
			Если СтрокаРасчет.ДоляВПравеОбщейСобственностиЧислитель <> 0 
				И СтрокаРасчет.ДоляВПравеОбщейСобственностиЗнаменатель <> 0 Тогда
					
				НоваяСтрока.ДоляВПравеОбщейСобственности = 
					Строка(СтрокаРасчет.ДоляВПравеОбщейСобственностиЧислитель)
					+ " / "
					+ Строка(СтрокаРасчет.ДоляВПравеОбщейСобственностиЗнаменатель);
					
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
