
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("РазныеДатыДляСотрудников", РазныеДатыДляСотрудников);
	Параметры.Свойство("ДатаНачалаПараметр", ДатаНачалаПараметр);
	Параметры.Свойство("ДатаПараметр", ДатаПараметр);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ГруппаПериод",
		"Видимость",
		РазныеДатыДляСотрудников);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ГруппаСтатьяФинансирования",
		"Видимость",
		ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный"));
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ГруппаСтатьяРасходов", 
		"Видимость",
		ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении"));
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ГруппаЕНВД", 
		"Видимость",
		ОтражениеЗарплатыВБухучете.ИспользуетсяЕНВД(?(РазныеДатыДляСотрудников, ДатаПараметр, ДатаНачалаПараметр)));
		
	ДоступныеЭлементы = Новый Массив;
	ДоступныеЭлементы.Добавить("ДатаНачала");
	ДоступныеЭлементы.Добавить("ДатаОкончания");
	ДоступныеЭлементы.Добавить("СтатьяФинансирования");
	ДоступныеЭлементы.Добавить("СтатьяРасходов");
	ДоступныеЭлементы.Добавить("СпособыОтраженияЗарплатыВБухУчете");
	ДоступныеЭлементы.Добавить("ОтношениеКЕНВД");
	Для каждого ИмяЭлемента Из ДоступныеЭлементы Цикл
	
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			ИмяЭлемента,
			"Доступность",
			Ложь);
	
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИзменитьДатаНачалаПриИзменении(Элемент)
	
	Если ИзменитьДатаНачала Тогда
		ДатаНачала = ДатаНачалаПараметр;
	Иначе
		ДатаНачала = "";
	КонецЕсли;
	
	ДатаНачалаПриИзмененииНаСервере();
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ДатаНачала",
		"Доступность",
		ИзменитьДатаНачала);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ДатаОкончания",
		"Доступность",
		ИзменитьДатаНачала);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаПриИзменении(Элемент)
	
	ДатаНачалаПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьСтатьюФинансированияПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"СтатьяФинансирования",
		"Доступность",
		ИзменитьСтатьюФинансирования);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьСпособОтраженияПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"СпособыОтраженияЗарплатыВБухУчете",
		"Доступность",
		ИзменитьСпособОтражения);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьЕНВДПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ОтношениеКЕНВД",
		"Доступность",
		ИзменитьЕНВД);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Закрыть(РезультатРедактирования());
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция РезультатРедактирования()
	
	РезультатРедактирования = Новый Структура;
	
	Если ИзменитьДатаНачала Тогда
		РезультатРедактирования.Вставить("ДатаНачала", ДатаНачала);
		РезультатРедактирования.Вставить("ДатаОкончания", ДатаОкончания);
	КонецЕсли;
	Если ИзменитьСтатьюФинансирования Тогда
		РезультатРедактирования.Вставить("СтатьяФинансирования", СтатьяФинансирования);
	КонецЕсли;
	Если ИзменитьСпособОтражения Тогда
		РезультатРедактирования.Вставить("СпособОтраженияЗарплатыВБухучете", СпособОтраженияЗарплатыВБухучете);
	КонецЕсли;
	Если ИзменитьЕНВД Тогда
		РезультатРедактирования.Вставить("ОтношениеКЕНВД", ОтношениеКЕНВД);
	КонецЕсли;
	
	Возврат РезультатРедактирования;
	
КонецФункции

&НаСервере
Процедура ДатаНачалаПриИзмененииНаСервере()

	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ГруппаЕНВД", 
		"Видимость",
		ОтражениеЗарплатыВБухучете.ИспользуетсяЕНВД(?(ИзменитьДатаНачала, ДатаНачала, ДатаПараметр)));

КонецПроцедуры

#КонецОбласти


