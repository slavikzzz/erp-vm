
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Организация = Параметры.Организация;
	
	ИмяОтчета =
		УведомлениеОСпецрежимахНалогообложенияПовтИсп.ПолучитьСоответствиеВидовУведомленийИменамОтчетов()[Параметры.ВидУведомления];
	
	ТаблицаФорм = Отчеты[ИмяОтчета].ПолучитьТаблицуФорм();
	СписокВыбора = Элементы.ПолеРедакцияФормы.СписокВыбора;
	СписокВыбора.Очистить();
	Для Каждого СтрокаТаблицы Из ТаблицаФорм Цикл
		СписокВыбора.Добавить(СтрокаТаблицы.ИмяФормы, СтрокаТаблицы.ОписаниеФормы);
		ПолеРедакцияФормы = СтрокаТаблицы.ИмяФормы;
	КонецЦикла;
	
	Элементы.Создать.Доступность = ЗначениеЗаполнено(Организация);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	Элементы.Создать.Доступность = ЗначениеЗаполнено(Организация);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Создать(Команда)
	
	СоздатьУведомление();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьБезПомощника(Команда)
	
	СоздатьУведомление(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СоздатьУведомление(ИспользоватьПомощник = Истина)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Если ИспользоватьПомощник Тогда
		ОткрытьПомощникПодготовкиУведомления();
	Иначе
		ОткрытьСтандартнуюФормуУведомления();
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПомощникПодготовкиУведомления()
	
	Если ИмяОтчета = "РегламентированноеУведомлениеИсчисленныеСуммыНалогов" Тогда
		
		Если ВыбранаАктуальнаяФормаУведомления(ПолеРедакцияФормы) Тогда
			
			ЗначенияЗаполнения = Новый Структура("Организация",         Организация);
			ПараметрыУведомления= Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
			
			ОткрытьФорму("Документ.УведомлениеОбИсчисленныхСуммахНалогов.Форма.ФормаДокумента", ПараметрыУведомления);
			
		Иначе
			ОткрытьСтандартнуюФормуУведомления();
		КонецЕсли;
		
	Иначе
		
		ОткрытьСтандартнуюФормуУведомления();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСтандартнуюФормуУведомления()
	
	ПараметрыОткрытия = Новый Структура("Организация", Организация);
	ОткрытьФорму(СтрШаблон("Отчет.%1.Форма.%2", ИмяОтчета, ПолеРедакцияФормы), ПараметрыОткрытия, ВладелецФормы);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВыбранаАктуальнаяФормаУведомления(ВыбраннаяФорма)
	
	ИмяАктуальнойФормы = РегламентированнаяОтчетность.ПолучитьАктуальнуюФормуУведомления(
		Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ИсчисленныеСуммыНалогов);
	
	Возврат ВыбраннаяФорма = ИмяАктуальнойФормы;
	
КонецФункции

#КонецОбласти
