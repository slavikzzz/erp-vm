
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("Подразделение", Подразделение)
		И НЕ Подразделение.Пустая() Тогда
		
		УстановитьСвязьПараметраВыбора("ВидРабочегоЦентра", "Отбор.Подразделение", "Подразделение");
		УстановитьСвязьПараметраВыбора("Исполнитель", "Отбор.Подразделение", "Подразделение");
		
	КонецЕсли;
	
	СтруктураОтбораПоДаннымЭтапа = Неопределено;
	Если Параметры.Свойство("СтруктураОтбораПоДаннымЭтапа", СтруктураОтбораПоДаннымЭтапа)
		И СтруктураОтбораПоДаннымЭтапа <> Неопределено Тогда
		
		СтруктураОтбораПоДаннымЭтапа.Свойство("НоменклатураИзделия", НоменклатураИзделия);
		СтруктураОтбораПоДаннымЭтапа.Свойство("ХарактеристикаИзделия", ХарактеристикаИзделия);
		СтруктураОтбораПоДаннымЭтапа.Свойство("НоменклатураМатериала", НоменклатураМатериала);
		СтруктураОтбораПоДаннымЭтапа.Свойство("ХарактеристикаМатериала", ХарактеристикаМатериала);
		СтруктураОтбораПоДаннымЭтапа.Свойство("ВидРабочегоЦентра", ВидРабочегоЦентра);
		СтруктураОтбораПоДаннымЭтапа.Свойство("Исполнитель", Исполнитель);
		
	КонецЕсли;
	
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма);
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Результат = УстановленныеОтборы();
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиЭлементовФормы

&НаКлиенте
Процедура НоменклатураИзделияПриИзменении(Элемент)
	
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураМатериалаПриИзменении(Элемент)
	
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбслуживаниеФормы

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЗависимыеЭлементыФормы(Форма, СписокРеквизитов = "")
	
	Элементы = Форма.Элементы;
	
	Инициализация = ПустаяСтрока(СписокРеквизитов);
	СтруктураРеквизитов = Новый Структура(СписокРеквизитов);
	
	Если Инициализация ИЛИ СтруктураРеквизитов.Свойство("НоменклатураИзделия") Тогда
		
		Элементы.ХарактеристикаИзделия.Доступность = ХарактеристикиИспользуются(Форма.НоменклатураИзделия);
		
	КонецЕсли;
	
	Если Инициализация ИЛИ СтруктураРеквизитов.Свойство("НоменклатураМатериала") Тогда
		
		Элементы.ХарактеристикаМатериала.Доступность = ХарактеристикиИспользуются(Форма.НоменклатураМатериала);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаСервереБезКонтекста
Функция ХарактеристикиИспользуются(Номенклатура)
	
	Возврат Справочники.Номенклатура.ХарактеристикиИспользуются(Номенклатура)

КонецФункции

&НаСервере
Функция УстановленныеОтборы()
	
	Результат = Новый Структура;
	
	Результат.Вставить("ИспользуетсяОтборПоИзделию", НЕ НоменклатураИзделия.Пустая());
	Результат.Вставить("НоменклатураИзделия", НоменклатураИзделия);
	Результат.Вставить("ХарактеристикаИзделия", ХарактеристикаИзделия);
	
	Результат.Вставить("ИспользуетсяОтборПоМатериалу", НЕ НоменклатураМатериала.Пустая());
	Результат.Вставить("НоменклатураМатериала", НоменклатураМатериала);
	Результат.Вставить("ХарактеристикаМатериала", ХарактеристикаМатериала);
	
	Результат.Вставить("ИспользуетсяОтборПоВРЦ", НЕ ВидРабочегоЦентра.Пустая());
	Результат.Вставить("ВидРабочегоЦентра", ВидРабочегоЦентра);
	
	Результат.Вставить("ИспользуетсяОтборПоИсполнителю", ЗначениеЗаполнено(Исполнитель));
	Результат.Вставить("Исполнитель", Исполнитель);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура УстановитьСвязьПараметраВыбора(Реквизит, ИмяПараметра, ПутьКДанным)
	
	НоваяСвязь = Новый СвязьПараметраВыбора(ИмяПараметра, ПутьКДанным);
	
	НовыйМассив = Новый Массив();
	НовыйМассив.Добавить(НоваяСвязь);
	
	НовыеСвязи = Новый ФиксированныйМассив(НовыйМассив);
	
	Элементы[Реквизит].СвязиПараметровВыбора = НовыеСвязи;

КонецПроцедуры

#КонецОбласти

#КонецОбласти
