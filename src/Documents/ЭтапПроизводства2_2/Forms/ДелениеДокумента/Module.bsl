
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ЭтапПроизводства.Номер                                          КАК Номер,
		|	ЭтапПроизводства.НаименованиеЭтапа                              КАК НаименованиеЭтапа,
		|	ЭтапПроизводства.КоличествоУпаковокПлан                         КАК КоличествоУпаковок,
		|	ЭтапПроизводства.УпаковкаПлан                                   КАК Упаковка,
		|	ЭтапПроизводства.ПартияПроизводства.ОсновноеИзделиеНоменклатура КАК Номенклатура,
		|	ВЫБОР
		|		КОГДА ЭтапПроизводства.УпаковкаПлан = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
		|			ТОГДА ПРЕДСТАВЛЕНИЕ(ЭтапПроизводства.ПартияПроизводства.ОсновноеИзделиеНоменклатура.ЕдиницаИзмерения)
		|		ИНАЧЕ ПРЕДСТАВЛЕНИЕ(ЭтапПроизводства.УпаковкаПлан)
		|	КОНЕЦ                                                           КАК ПредставлениеУпаковки
		|ИЗ
		|	Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства
		|ГДЕ
		|	ЭтапПроизводства.Ссылка = &Ссылка");
	Запрос.УстановитьПараметр("Ссылка", Параметры.Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	ЭтаФорма.Заголовок = СтрШаблон(
		НСтр("ru = 'Деление этапа %1';
			|en = 'Stage division %1'"),
		Документы.ЭтапПроизводства2_2.ПредставлениеЭтапа(Выборка));
	
	Элементы.Декорация1.Заголовок = СтрШаблон(
		НСтр("ru = 'Текущий документ: %1 %2';
			|en = 'Current document: %1 %2'"),
		Выборка.КоличествоУпаковок,
		Выборка.ПредставлениеУпаковки);
	
	Упаковка = Выборка.Упаковка;
	Справочники.УпаковкиЕдиницыИзмерения.ОтобразитьИнформациюОЕдиницеХранения(Выборка.Номенклатура, Элементы.Упаковка);
	Элементы.Упаковка.ПараметрыВыбора = Новый ФиксированныйМассив(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(
			Новый ПараметрВыбора("Номенклатура", Выборка.Номенклатура)));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Разделить(Команда)
	
	ОчиститьСообщения();
	
	Если КоличествоУпаковок = 0 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Не заполнено количество в новом документе';
				|en = 'Quantity in the new document is not filled in'"),,
			"КоличествоУпаковок");
		Возврат;
	КонецЕсли;
	
	Отказ = Ложь;
	РазделитьНаСервере(Отказ);
	
	Если Не Отказ Тогда
		ОповеститьОбИзменении(Параметры.Ссылка);
		Оповестить("ДелениеЭтапа", Параметры.Ссылка, УникальныйИдентификатор);
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура РазделитьНаСервере(Отказ)
	
	Попытка
		
		Документы.ЭтапПроизводства2_2.РазделитьДокумент(Параметры.Ссылка, КоличествоУпаковок, Упаковка, Отказ);
		
	Исключение
		
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Не удалось выполнить действие по причине: %1';
				|en = 'Cannot perform the action. Reason: %1'"),
			КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти
