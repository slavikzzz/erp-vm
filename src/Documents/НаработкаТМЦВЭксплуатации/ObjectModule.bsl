#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКопировании(ОбъектКопирования)
	
	ИнициализироватьДокумент();
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ИнициализироватьДокумент();
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	Если Не Отказ И РежимЗаписи <> РежимЗаписиДокумента.Запись Тогда
		ПодготовитьДанныеДляФормированияЗаданияКЗакрытиюМесяцаПередЗаписью();
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ПриЗаписиДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	ОбщегоНазначенияУТ.ПроверитьНаличиеДублейСтрокТЧ(ЭтотОбъект, "Наработки", "ИнвентарныйНомер", Отказ);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеДокументов.ОбработкаПроведенияДокумента(ЭтотОбъект, Отказ);
	
	СформироватьЗаданиеПогашениеСтоимостиТМЦ();
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеДокументов.ОбработкаУдаленияПроведенияДокумента(ЭтотОбъект, Отказ);
	
	СформироватьЗаданиеПогашениеСтоимостиТМЦ(Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	
	Ответственный = Пользователи.ТекущийПользователь();
	Организация = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	Подразделение = ЗначениеНастроекПовтИсп.ПодразделениеПользователя(Ответственный, Подразделение);
	
КонецПроцедуры

#Область ЗаданияКЗакрытиюМесяца

Процедура ПодготовитьДанныеДляФормированияЗаданияКЗакрытиюМесяцаПередЗаписью()
	
	ЭтотОбъект.ДополнительныеСвойства.Вставить("МенеджерВременныхТаблицДокумента", Новый МенеджерВременныхТаблиц());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Ссылка.Дата, МЕСЯЦ) КАК Период,
	|	ТаблицаПередЗаписью.Ссылка.Организация КАК Организация,
	|	ТаблицаПередЗаписью.ИнвентарныйНомер,
	|	ТаблицаПередЗаписью.ТекущееЗначение,
	|	ТаблицаПередЗаписью.ПредельныйОбъем
	|ПОМЕСТИТЬ НаработкаТМЦВЭксплуатации_Наработки_ПередЗаписью
	|ИЗ
	|	Документ.НаработкаТМЦВЭксплуатации.Наработки КАК ТаблицаПередЗаписью
	|ГДЕ
	|	ТаблицаПередЗаписью.Ссылка = &Ссылка
	|	И ТаблицаПередЗаписью.Ссылка.Проведен";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ЭтотОбъект.ДополнительныеСвойства.МенеджерВременныхТаблицДокумента;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура СформироватьЗаданиеПогашениеСтоимостиТМЦ(ОтменаПроведения = Ложь)
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ) КАК Дата,
	|	&Организация КАК Организация,
	|	&Проведен КАК Проведен,
	|	ТаблицаПослеЗаписи.ИнвентарныйНомер,
	|	ТаблицаПослеЗаписи.ТекущееЗначение,
	|	ТаблицаПослеЗаписи.ПредельныйОбъем
	|ПОМЕСТИТЬ НаработкаТМЦВЭксплуатации_Наработки_ПослеЗаписи
	|ИЗ
	|	&Наработки КАК ТаблицаПослеЗаписи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПередЗаписью.Период КАК Период,
	|	ТаблицаПередЗаписью.Организация КАК Организация,
	|	ТаблицаПередЗаписью.ИнвентарныйНомер КАК ИнвентарныйНомер
	|ПОМЕСТИТЬ НаработкаТМЦВЭксплуатации_Изменения
	|ИЗ
	|	НаработкаТМЦВЭксплуатации_Наработки_ПередЗаписью КАК ТаблицаПередЗаписью
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ НаработкаТМЦВЭксплуатации_Наработки_ПослеЗаписи КАК ТаблицаПослеЗаписи
	|		ПО (ТаблицаПередЗаписью.ИнвентарныйНомер = ТаблицаПослеЗаписи.ИнвентарныйНомер)
	|			И (ТаблицаПередЗаписью.ТекущееЗначение = ТаблицаПослеЗаписи.ТекущееЗначение)
	|			И (ТаблицаПередЗаписью.ПредельныйОбъем = ТаблицаПослеЗаписи.ПредельныйОбъем)
	|			И (ТаблицаПередЗаписью.Организация = ТаблицаПослеЗаписи.Организация)
	|			И (НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, МЕСЯЦ) = НАЧАЛОПЕРИОДА(ТаблицаПослеЗаписи.Дата, МЕСЯЦ))
	|			И (ТаблицаПослеЗаписи.Проведен = ИСТИНА)
	|ГДЕ
	|	ТаблицаПослеЗаписи.ИнвентарныйНомер ЕСТЬ NULL
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(ТаблицаПослеЗаписи.Дата, МЕСЯЦ),
	|	ТаблицаПослеЗаписи.Организация,
	|	ТаблицаПослеЗаписи.ИнвентарныйНомер
	|ИЗ
	|	НаработкаТМЦВЭксплуатации_Наработки_ПослеЗаписи КАК ТаблицаПослеЗаписи
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ НаработкаТМЦВЭксплуатации_Наработки_ПередЗаписью КАК ТаблицаПередЗаписью
	|		ПО (ТаблицаПередЗаписью.ИнвентарныйНомер = ТаблицаПослеЗаписи.ИнвентарныйНомер)
	|			И (ТаблицаПередЗаписью.ТекущееЗначение = ТаблицаПослеЗаписи.ТекущееЗначение)
	|			И (ТаблицаПередЗаписью.ПредельныйОбъем = ТаблицаПослеЗаписи.ПредельныйОбъем)
	|			И (ТаблицаПередЗаписью.Организация = ТаблицаПослеЗаписи.Организация)
	|			И (НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, МЕСЯЦ) = НАЧАЛОПЕРИОДА(ТаблицаПослеЗаписи.Дата, МЕСЯЦ))
	|ГДЕ
	|	ТаблицаПередЗаписью.Организация ЕСТЬ NULL
	|	И ТаблицаПослеЗаписи.Проведен = ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НаработкаТМЦВЭксплуатации_Изменения.Период КАК Период,
	|	НаработкаТМЦВЭксплуатации_Изменения.Организация КАК Организация,
	|	ТМЦВЭксплуатации.ИнвентарныйНомер КАК Партия
	|ПОМЕСТИТЬ НаработкаТМЦВЭксплуатации_ИзмененияПартии
	|ИЗ
	|	НаработкаТМЦВЭксплуатации_Изменения КАК НаработкаТМЦВЭксплуатации_Изменения
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТМЦВЭксплуатации КАК ТМЦВЭксплуатации
	|		ПО ТМЦВЭксплуатации.ИнвентарныйНомер = НаработкаТМЦВЭксплуатации_Изменения.ИнвентарныйНомер
	|			И ТМЦВЭксплуатации.Активность
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(Таблица.Период) КАК Месяц,
	|	Таблица.Организация КАК Организация,
	|	ЕСТЬNULL(ПакетыПогашенияСтоимостиТМЦ.НомерПакета, 0) КАК НомерПакета,
	|	&Ссылка КАК Документ
	|ИЗ
	|	НаработкаТМЦВЭксплуатации_ИзмененияПартии КАК Таблица
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПакетыПогашенияСтоимостиТМЦ КАК ПакетыПогашенияСтоимостиТМЦ
	|		ПО ПакетыПогашенияСтоимостиТМЦ.Организация = Таблица.Организация
	|			И ПакетыПогашенияСтоимостиТМЦ.Партия = Таблица.Партия
	|
	|СГРУППИРОВАТЬ ПО
	|	Таблица.Организация,
	|	ЕСТЬNULL(ПакетыПогашенияСтоимостиТМЦ.НомерПакета, 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ НаработкаТМЦВЭксплуатации_Изменения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ НаработкаТМЦВЭксплуатации_ИзмененияПартии
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ НаработкаТМЦВЭксплуатации_Наработки_ПередЗаписью
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ НаработкаТМЦВЭксплуатации_Наработки_ПослеЗаписи";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ЭтотОбъект.ДополнительныеСвойства.МенеджерВременныхТаблицДокумента;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Проведен", ?(ОтменаПроведения, Ложь, Проведен));
	Запрос.УстановитьПараметр("Наработки", Наработки.Выгрузить());
	РезультатЗапроса = Запрос.Выполнить();
	
	РегистрыСведений.ЗаданияКПогашениюСтоимостиТМЦВЭксплуатации.СоздатьЗаписиРегистраПоДаннымВыборки(РезультатЗапроса.Выбрать());
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли