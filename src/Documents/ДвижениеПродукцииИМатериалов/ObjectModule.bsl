#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Устанавливает статус для объекта документа
//
// Параметры:
//	НовыйСтатус - Строка - Имя статуса, который будет установлен у заказов
//	ДополнительныеПараметры - Структура - Структура дополнительных параметров установки статуса.
//
// Возвращаемое значение:
//	Булево - Истина, в случае успешной установки нового статуса.
//
Функция УстановитьСтатус(НовыйСтатус, ДополнительныеПараметры) Экспорт
	
	Статус = Перечисления.СтатусыДвиженияПродукцииИМатериалов[НовыйСтатус];
	
	ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, Документы.ДвижениеПродукцииИМатериалов);
	НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ЭтотОбъект, ПараметрыУказанияСерий);
	Возврат ПроверитьЗаполнение();
	
КонецФункции

// Инициализирует параметры заполнения видов запасов дополнительных свойств документа, используемых при записи документа
// в режиме 'Проведения' или 'Отмены проведения'.
//
// Параметры:
//	ДокументОбъект - ДокументОбъект.ДвижениеПродукцииИМатериалов - документ, для которого выполняется инициализация параметров.
//	РежимЗаписи - РежимЗаписиДокумента - режим записи документа.
//
Процедура ИнициализироватьПараметрыЗаполненияВидовЗапасовДляПроведения(ДокументОбъект, РежимЗаписи) Экспорт
	
	ПараметрыЗаполнения = ПараметрыЗаполненияВидовЗапасов(Ложь, РежимЗаписи);
	
	Если РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		ПараметрыЗаполнения.ДокументДелаетИПриходИРасход = Ложь;
	КонецЕсли;
	
	ДокументОбъект.ДополнительныеСвойства.Вставить("ПараметрыЗаполненияВидовЗапасов", ПараметрыЗаполнения);
	
	Если ПоРаспоряжениям
		И РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения
		И ТипЗнч(Получатель) = Тип("СправочникСсылка.СтруктураПредприятия") Тогда
		
		ДокументОбъект.ДополнительныеСвойства.Вставить("НужноКонтролироватьОстаткиТоваровОрганизаций", Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

// Заполняет реквизиты, хранящие информацию о видах запасов и аналитиках учета номенклатуры в табличной части 'Товары'
// документа, а также заполняет табличную часть 'ВидыЗапасов'.
//
// Параметры:
//	Отказ - Булево - признак того, что не удалось заполнить данные.
//	ТаблицыДокумента - см. Документы.ДвижениеПродукцииИМатериалов.КоллекцияТабличныхЧастейТоваров.
//
Процедура ЗаполнитьВидыЗапасовПриОбмене(Отказ, ТаблицыДокумента) Экспорт
	
	ЗаполнитьАналитикиУчетаНоменклатурыВТабличныхЧастяхТоваров();
	
	Если ТаблицыДокумента <> Неопределено Тогда
		ЗаполнитьАналитикиУчетаНоменклатурыВТабличныхЧастяхТоваров(ТаблицыДокумента);
		ДополнительныеСвойства.Вставить("ТаблицыЗаполненияВидовЗапасовПриОбмене", ТаблицыДокумента);
	Иначе
		ИмяПараметра = "ТаблицыДокумента";
		
		ТекстИсключения = НСтр("ru = 'Для заполнения видов запасов не передан параметр ""%1"".';
								|en = 'The ""%1"" parameter is not transferred to fill in the inventory owner attributes.'");
		ТекстИсключения = СтрШаблон(ТекстИсключения, ИмяПараметра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	ЗаполнитьВидыЗапасов(Отказ);
	ДополнительныеСвойства.Удалить("ТаблицыЗаполненияВидовЗапасовПриОбмене");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ИсправлениеДокументов.ПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
	ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	// Очистим реквизиты документа не используемые для хозяйственной операции.
	МассивВсехРеквизитов = Новый Массив;
	МассивРеквизитовОперации = Новый Массив;
	Документы.ДвижениеПродукцииИМатериалов.ЗаполнитьИменаРеквизитовПоХозяйственнойОперации(
		ХозяйственнаяОперация,
		МассивВсехРеквизитов,
		МассивРеквизитовОперации);
		
	ДенежныеСредстваСервер.ОчиститьНеиспользуемыеРеквизиты(
		ЭтотОбъект,
		МассивВсехРеквизитов,
		МассивРеквизитовОперации);
	
	Для Каждого ДанныеСтроки Из Товары Цикл
		Если ПоРаспоряжениям И ЗначениеЗаполнено(Распоряжение)
			И ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаМатериаловВКладовую
			И Не ЗначениеЗаполнено(ДанныеСтроки.Распоряжение) Тогда
			
			ДанныеСтроки.Распоряжение = Распоряжение;
			
		ИначеЕсли Не ЗначениеЗаполнено(ДанныеСтроки.Распоряжение)
			И ДанныеСтроки.Распоряжение <> Неопределено Тогда
			
			ДанныеСтроки.Распоряжение = Неопределено;
			
		КонецЕсли;
	КонецЦикла;
	
	// Округление количества если движение не через кладовые
	Если (ТипЗнч(Отправитель) <> Тип("СправочникСсылка.Склады")	
			Или Не ЗначениеЗаполнено(Отправитель)
			Или Не ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Отправитель, "ЦеховаяКладовая"))
		И (ТипЗнч(Получатель) <> Тип("СправочникСсылка.Склады")	
			Или Не ЗначениеЗаполнено(Получатель)
			Или Не ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Получатель, "ЦеховаяКладовая")) Тогда
		
		НоменклатураСервер.ОкруглитьКоличествоШтучныхТоваров(ЭтотОбъект, РежимЗаписи);
		
	КонецЕсли;
	
	ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, Документы.ДвижениеПродукцииИМатериалов);
	НоменклатураСервер.ОчиститьНеиспользуемыеСерии(ЭтотОбъект, ПараметрыУказанияСерий);
	
	// Заполнение ТЧ ВидыЗапасов
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		ЗаполнитьАналитикиУчетаНоменклатурыВТабличныхЧастяхТоваров();
		ЗаполнитьВидыЗапасов(Отказ);
		
	ИначеЕсли РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		Если Не ВидыЗапасовУказаныВручную Тогда
			ВидыЗапасов.Очистить();
		КонецЕсли;
	КонецЕсли;
	
	ОбщегоНазначенияУТ.ЗаполнитьИдентификаторыДокумента(ЭтотОбъект, "Товары,ВидыЗапасов");
	
	ДвижениеПродукцииИМатериаловЛокализация.ПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);

КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ПриЗаписиДокумента(ЭтотОбъект, Отказ);
	
	ДвижениеПродукцииИМатериаловЛокализация.ПриЗаписи(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ИнициализироватьДокументДоЗаполнения();
	
	Распоряжение = ДокументОснованиеПриЗаполнении(ДанныеЗаполнения);
	ВариантПриемкиТоваров = ЗакупкиСервер.ПолучитьВариантПриемкиТоваров(Распоряжение);
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("Основание") Тогда
		
		Если ТипЗнч(ДанныеЗаполнения.Основание) = Тип("ДокументСсылка.ЗаказКлиента") Тогда
			ЗаполнитьВыпускПродукцииНаОснованииЗаказаКлиента(ДанныеЗаполнения);
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура")
		И ДанныеЗаполнения.Свойство("ОбъектыОснований") Тогда
		
		ОбъектОснование = ДанныеЗаполнения.ОбъектыОснований[0];
		
		Если ДанныеЗаполнения.ОбъектыОснований.Количество() = 1 Тогда
			ДокументОснование = ОбъектОснование;
		КонецЕсли;
		
		Если ТипЗнч(ОбъектОснование) = Тип("ДокументСсылка.ПриобретениеТоваровУслуг") Тогда
			
			ЗаполнитьПередачуМатериаловНаОснованииПриобретения(ДанныеЗаполнения.ОбъектыОснований);
			
		//++ НЕ УТКА

		//++ Устарело_Переработка24
		ИначеЕсли ТипЗнч(ОбъектОснование) = Тип("ДокументСсылка.ЗаказДавальца") Тогда
			
			Если ДанныеЗаполнения.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаМатериаловВКладовую Тогда
				ЗаполнитьПередачуМатериаловНаОснованииЗаказаДавальца(ДанныеЗаполнения.ОбъектыОснований);
			ИначеЕсли ДанныеЗаполнения.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзКладовой Тогда
				ЗаполнитьВыпускПродукцииНаОснованииЗаказаДавальца(ДанныеЗаполнения.ОбъектыОснований);
			Иначе
				ВызватьИсключение СтрШаблон(НСтр("ru = 'Заполнение на основании документа %1 с хозяйственной операцией
					|""%2"" не предусмотрено. Обратитесь к администратору.';
					|en = 'Population on the %1 document basis with the 
					|""%2"" business transaction is not provided. Please contact the administrator.'"), ОбъектОснование, ДанныеЗаполнения.ХозяйственнаяОперация);
			КонецЕсли;
		//-- Устарело_Переработка24
		ИначеЕсли ТипЗнч(ОбъектОснование) = Тип("ДокументСсылка.ЗаказДавальца2_5") Тогда
			
			Если ДанныеЗаполнения.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаМатериаловВКладовую Тогда
				ЗаполнитьПередачуМатериаловНаОснованииЗаказаДавальца(ДанныеЗаполнения.ОбъектыОснований);
			ИначеЕсли ДанныеЗаполнения.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзКладовой Тогда
				ЗаполнитьВыпускПродукцииНаОснованииЗаказаДавальца(ДанныеЗаполнения.ОбъектыОснований);
			Иначе
				ВызватьИсключение СтрШаблон(НСтр("ru = 'Заполнение на основании документа %1 с хозяйственной операцией
					|""%2"" не предусмотрено. Обратитесь к администратору.';
					|en = 'Population on the %1 document basis with the 
					|""%2"" business transaction is not provided. Please contact the administrator.'"), ОбъектОснование, ДанныеЗаполнения.ХозяйственнаяОперация);
			КонецЕсли;
			
		//++ Устарело_Переработка24
		ИначеЕсли ТипЗнч(ОбъектОснование) = Тип("ДокументСсылка.ПоступлениеСырьяОтДавальца") Тогда
			
			ЗаполнитьПередачуМатериаловНаОснованииПоступленияСырьяОтДавальца(ДанныеЗаполнения.ОбъектыОснований);
		//-- Устарело_Переработка24

		//-- НЕ УТКА
		Иначе
			ВызватьИсключение СтрШаблон(НСтр("ru = 'Заполнение на основании документа %1 не предусмотрено. Обратитесь к администратору.';
											|en = 'Population on the %1 document basis is not provided. Please contact the administrator.'"), ОбъектОснование);
		КонецЕсли; 
		
		ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, Документы.ДвижениеПродукцииИМатериалов);
		НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ЭтотОбъект, ПараметрыУказанияСерий);
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("Товары") Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения.РеквизитыШапки);
		
		Для Каждого Строка Из ДанныеЗаполнения.Товары Цикл
			НоваяСтрока = Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			НоваяСтрока.НазначениеОтправителя = НоваяСтрока.Назначение;
		КонецЦикла;
		
		ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, Документы.ДвижениеПродукцииИМатериалов);
		НоменклатураСервер.ЗаполнитьСерииПоFEFO(ЭтотОбъект,ПараметрыУказанияСерий, Ложь);
	
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ДвижениеПродукцииИМатериалов") Тогда
		
		ИсправлениеДокументов.ЗаполнитьИсправление(ЭтотОбъект, ДанныеЗаполнения);
		
	Иначе
		
		// Заполнение по распоряжениям.
		
		СтруктураЗаполнения = Неопределено;
		Если ТипЗнч(ДанныеЗаполнения) = Тип("Массив") Тогда // команда ввода на основании
			
			СтруктураЗаполнения = Новый Структура("МассивЗаказов", ДанныеЗаполнения);
			
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
			
			Если ДанныеЗаполнения.Свойство("ОпределитьРаспоряжения") Тогда
				
				СтруктураЗаполнения = ДанныеЗаполнения;
				СтруктураЗаполнения.Вставить("МассивЗаказов", ОпределитьРаспоряжения(ДанныеЗаполнения.РеквизитыШапки));
				
			ИначеЕсли ДанныеЗаполнения.Свойство("МассивЗаказов") Тогда // из спец. формы оформления накладных
				
				СтруктураЗаполнения = ДанныеЗаполнения;
				
			ИначеЕсли ДанныеЗаполнения.Свойство("РеквизитыШапки") Тогда
				
				ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения.РеквизитыШапки);
				
			Иначе
				
				ЗаполнитьПоОтбору(ДанныеЗаполнения);
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если СтруктураЗаполнения <> Неопределено Тогда
			
			СтруктураЗаполнения.РеквизитыШапки.Вставить("ВариантПриемкиТоваров", ВариантПриемкиТоваров);
			ЗаполнитьПоРаспоряжениям(СтруктураЗаполнения);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ДвижениеПродукцииИМатериаловЛокализация.ОбработкаЗаполнения(ЭтотОбъект, ДанныеЗаполнения, СтандартнаяОбработка);
	
	ИнициализироватьДокумент(ДанныеЗаполнения);

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ЗатратыСервер.ПроверитьИспользованиеПартионногоУчета22(ЭтотОбъект, Дата, Отказ);
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	МассивНепроверяемыхРеквизитов.Добавить("Отправитель");
	МассивНепроверяемыхРеквизитов.Добавить("Получатель");
	МассивНепроверяемыхРеквизитов.Добавить("Товары");
	//++ НЕ УТКА
	Если Не ВнутренняяПереработка Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ОрганизацияДавалец");
	КонецЕсли;
	//-- НЕ УТКА
	
	ОтключитьПроверкуЗаполненияПоХозяйственнойОперации(МассивНепроверяемыхРеквизитов);
	
	ПредставлениеПолей = Документы.ДвижениеПродукцииИМатериалов.ПредставлениеПолей(ЭтотОбъект);
	
	ПроверитьРеквизитыШапки(ПредставлениеПолей, Отказ);
	ПроверитьТовары(ПредставлениеПолей, Отказ);
	//++ НЕ УТКА
	ПроверитьСоответствиеНакладнойИЭтапов(ПредставлениеПолей.Товары, Отказ);
	//-- НЕ УТКА
	
	ПараметрыПроверки = НоменклатураСервер.ПараметрыПроверкиЗаполненияКоличества();
	ПараметрыПроверки.ПроверитьВозможностьОкругления = 
		(ТипЗнч(Отправитель) <> Тип("СправочникСсылка.Склады")	
			Или Не ЗначениеЗаполнено(Отправитель)
			Или Не ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Отправитель, "ЦеховаяКладовая"))
		И (ТипЗнч(Получатель) <> Тип("СправочникСсылка.Склады")	
	   		Или Не ЗначениеЗаполнено(Получатель)
			Или Не ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Получатель, "ЦеховаяКладовая"));
	НоменклатураСервер.ПроверитьЗаполнениеКоличества(ЭтотОбъект, ПроверяемыеРеквизиты, Отказ, ПараметрыПроверки);
	
	НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект, МассивНепроверяемыхРеквизитов, Отказ);
	
	// Проверка заполнения серий в т.ч. серии.
	ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, Документы.ДвижениеПродукцииИМатериалов);
	НоменклатураСервер.ПроверитьЗаполнениеСерий(ЭтотОбъект, ПараметрыУказанияСерий, Отказ, МассивНепроверяемыхРеквизитов);
		
	ИсправлениеДокументов.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты,МассивНепроверяемыхРеквизитов);

	ДвижениеПродукцииИМатериаловЛокализация.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ИнициализироватьПараметрыЗаполненияВидовЗапасовДляПроведения(ЭтотОбъект, РежимЗаписиДокумента.Проведение);
	
	ПроведениеДокументов.ОбработкаПроведенияДокумента(ЭтотОбъект, Отказ);
	
	ДвижениеПродукцииИМатериаловЛокализация.ОбработкаПроведения(ЭтотОбъект, Отказ, РежимПроведения);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ИнициализироватьПараметрыЗаполненияВидовЗапасовДляПроведения(ЭтотОбъект, РежимЗаписиДокумента.ОтменаПроведения);
	
	ПроведениеДокументов.ОбработкаУдаленияПроведенияДокумента(ЭтотОбъект, Отказ);
	
	ДвижениеПродукцииИМатериаловЛокализация.ОбработкаУдаленияПроведения(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ИнициализироватьДокументДоЗаполнения();
	
	СостояниеЗаполненияМногооборотнойТары = Перечисления.СостоянияЗаполненияМногооборотнойТары.ПустаяСсылка();
	
	ВидыЗапасовУказаныВручную = Ложь;
	ВидыЗапасов.Очистить();
	Серии.Очистить();
	
	Документы.ДвижениеПродукцииИМатериалов.ОтвязатьОтРаспоряжений(ЭтотОбъект);
	
	Для Каждого СтрокаТЧ Из Товары Цикл
		СтрокаТЧ.ИдентификаторСтроки = "";
	КонецЦикла;
	
	ВариантПриемкиТоваров = ЗакупкиСервер.ПолучитьВариантПриемкиТоваров();
	
	ИнициализироватьДокумент();
	
	ИсправлениеДокументов.ПриКопировании(ЭтотОбъект, ОбъектКопирования);
	
	ОбщегоНазначенияУТ.ОчиститьИдентификаторыДокумента(ЭтотОбъект, "Товары,ВидыЗапасов");
	
	ДвижениеПродукцииИМатериаловЛокализация.ПриКопировании(ЭтотОбъект, ОбъектКопирования);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ИнициализацияИЗаполнение

Процедура ИнициализироватьДокументДоЗаполнения()
	
	Автор = Пользователи.ТекущийПользователь();
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСтатусыДвиженийПродукцииИМатериалов") Тогда
		Статус = Перечисления.СтатусыДвиженияПродукцииИМатериалов.Отгружено;
	Иначе
		Статус = Перечисления.СтатусыДвиженияПродукцииИМатериалов.Принято;
	КонецЕсли;
	
КонецПроцедуры

Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	
	СписокОпераций = Документы.ДвижениеПродукцииИМатериалов.СписокОпераций();
	Если СписокОпераций.Количество() = 1 Тогда
		ХозяйственнаяОперация = СписокОпераций[0];
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Ответственный) Тогда
		Ответственный = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	Организация = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзПроизводства Тогда
		ВидЦены        = Справочники.ВидыЦен.ВидЦеныПлановойСтоимостиТМЦ(ВидЦены);
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаМатериаловВПроизводство
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаМатериаловВКладовую Тогда
		ВидЦены = Справочники.Склады.УчетныйВидЦены(Отправитель);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Валюта) Тогда
		Валюта = ДоходыИРасходыСервер.ПолучитьВалютуУправленческогоУчета(Справочники.ВидыЦен.ПолучитьРеквизитыВидаЦены(ВидЦены).ВалютаЦены);
	КонецЕсли;
	
	ПредставлениеПолей = Документы.ДвижениеПродукцииИМатериалов.ПредставлениеПолей(ЭтотОбъект);
	Если ПредставлениеПолей.ТипОтправителя = "Подразделение" Тогда
		Отправитель = ЗначениеНастроекПовтИсп.ПодразделениеПользователя(Ответственный, Отправитель);
	ИначеЕсли ПредставлениеПолей.ТипОтправителя = "Кладовая" Тогда
		Подразделение = ЗначениеНастроекПовтИсп.ПодразделениеПользователя(Ответственный);
		Отправитель = ЗначениеНастроекПовтИсп.ПолучитьЦеховуюКладовуюПоУмолчанию(Подразделение, Отправитель);
	Иначе
		Отправитель = ЗначениеНастроекПовтИсп.ПолучитьСкладПоУмолчанию(Отправитель);
	КонецЕсли;
	
	Если ПредставлениеПолей.ТипПолучателя = "Подразделение" Тогда
		Получатель = ЗначениеНастроекПовтИсп.ПодразделениеПользователя(Ответственный, Получатель);
	ИначеЕсли ПредставлениеПолей.ТипПолучателя = "Кладовая" Тогда
		Подразделение = ЗначениеНастроекПовтИсп.ПодразделениеПользователя(Ответственный);
		Получатель = ЗначениеНастроекПовтИсп.ПолучитьЦеховуюКладовуюПоУмолчанию(Подразделение, Получатель);
	Иначе
		Получатель = ЗначениеНастроекПовтИсп.ПолучитьСкладПоУмолчанию(Получатель);
	КонецЕсли;
	
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзПроизводства
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратМатериаловИзПроизводства
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаМатериаловВПроизводство Тогда
		ПоРаспоряжениям = Истина;
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзКладовой
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратМатериаловИзКладовой
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеремещениеПолуфабрикатов
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеремещениеПолуфабрикатовМеждуФилиалами Тогда
		ПоРаспоряжениям = Ложь;
	КонецЕсли;
	
	ПараметрыЗаполнения = Документы.ДвижениеПродукцииИМатериалов.ПараметрыЗаполненияВидаДеятельностиНДС(ЭтотОбъект);
	УчетНДСУП.ЗаполнитьВидДеятельностиНДС(НалогообложениеНДС, ПараметрыЗаполнения);
	
КонецПроцедуры

Функция ОпределитьРаспоряжения(РеквизитыШапки)

	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаОстатки.Распоряжение
	|ИЗ
	|	РегистрНакопления.ЗаказыМатериаловВПроизводство.Остатки(
	|			,
	|			Подразделение = &Подразделение
	|				И Склад = &Склад) КАК ТаблицаОстатки
	|ГДЕ
	|	ТаблицаОстатки.Распоряжение.Организация = &Организация
	|	И ТаблицаОстатки.КОформлениюОстаток <> 0";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Организация", РеквизитыШапки.Организация);
	Запрос.УстановитьПараметр("Подразделение", РеквизитыШапки.Получатель);
	Запрос.УстановитьПараметр("Склад", РеквизитыШапки.Отправитель);
	
	МассивЗаказов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Распоряжение");
	
	Возврат МассивЗаказов;

КонецФункции

Процедура ЗаполнитьПоОтбору(СтруктураОтбора)
	
	СтруктураОтбора.Свойство("Организация", Организация);
	
	Если СтруктураОтбора.Свойство("ХозяйственнаяОперация", ХозяйственнаяОперация) Тогда
		
		ПредставлениеПолей = Документы.ДвижениеПродукцииИМатериалов.ПредставлениеПолей(ЭтотОбъект);
		
		Если ПредставлениеПолей.ТипОтправителя = "Склад" Тогда
			СтруктураОтбора.Свойство("Склад", Отправитель);
		ИначеЕсли ПредставлениеПолей.ТипПолучателя = "Склад" Тогда
			СтруктураОтбора.Свойство("Склад", Получатель);
		КонецЕсли;
		
		Если ПредставлениеПолей.ТипОтправителя = "Подразделение" Тогда
			СтруктураОтбора.Свойство("Подразделение", Отправитель);
		ИначеЕсли ПредставлениеПолей.ТипПолучателя = "Подразделение" Тогда
			СтруктураОтбора.Свойство("Подразделение", Получатель);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПоРаспоряжениям(СтруктураЗаполнения)
	
	Если СтруктураЗаполнения.РеквизитыШапки.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПередачаМатериаловВПроизводство
		И СтруктураЗаполнения.РеквизитыШапки.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПередачаМатериаловВКладовую
		И СтруктураЗаполнения.РеквизитыШапки.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзПроизводства
		И СтруктураЗаполнения.РеквизитыШапки.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзКладовой
		И СтруктураЗаполнения.РеквизитыШапки.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ВозвратМатериаловИзПроизводства
		И СтруктураЗаполнения.РеквизитыШапки.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ВозвратМатериаловИзКладовой Тогда
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Заполнение по распоряжениям для операции ""%1"" не предусмотрено.';
										|en = 'Population by references for the ""%1"" operation is not provided.'"), СтруктураЗаполнения.РеквизитыШапки.ХозяйственнаяОперация);
	КонецЕсли;
	
	МассивЗаказов = СтруктураЗаполнения.МассивЗаказов;
	
	ПараметрыЗаполнения = Документы.ДвижениеПродукцииИМатериалов.ПараметрыЗаполненияДокумента();
	ЗаполнитьЗначенияСвойств(ПараметрыЗаполнения, СтруктураЗаполнения);
	Дата = ТекущаяДатаСеанса();
	
	Документы.ДвижениеПродукцииИМатериалов.ИнициализироватьПараметрыЗаполнения(ПараметрыЗаполнения,
		СтруктураЗаполнения.РеквизитыШапки, МассивЗаказов);
	
	ТаблицаНакладная = Товары.Выгрузить();
	
	Документы.ДвижениеПродукцииИМатериалов.ЗаполнитьПоЗаказамОрдерам(ТаблицаНакладная, Ссылка, ПараметрыЗаполнения);
	
	Если ПараметрыЗаполнения.ЗаполнятьПоОрдеру Тогда
		ТаблицаНакладная.Колонки.Количество.Имя        = "КоличествоДоИзменения";
		КолонкаКоличествоВОрдере = ТаблицаНакладная.Колонки.КоличествоВОрдере; // КолонкаТаблицыЗначений
		КолонкаКоличествоВОрдере.Имя = "Количество";
	Иначе
		ТаблицаНакладная.Колонки.Количество.Имя        = "КоличествоДоИзменения";
		КолонкаКоличествоВЗаказе = ТаблицаНакладная.Колонки.КоличествоВЗаказе; // КолонкаТаблицыЗначений
		КолонкаКоличествоВЗаказе.Имя = "Количество";
	КонецЕсли;
	
	НакладныеСервер.УдалитьПустыеСтроки(ТаблицаНакладная, "Количество");
	
	Товары.Загрузить(ТаблицаНакладная);
	
	Документы.ДвижениеПродукцииИМатериалов.ЗаполнитьШапкуДокументаПоЗаказу(ЭтотОбъект, ПараметрыЗаполнения, МассивЗаказов);
	
	Если ПараметрыЗаполнения.ЗаполнятьПоОрдеру Тогда
		Если ПараметрыЗаполнения.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаМатериаловВПроизводство
			Или ПараметрыЗаполнения.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаМатериаловВКладовую Тогда
			// Перенос складских серий из ордеров для получателя
			НастройкиОтборов = Документы.ДвижениеПродукцииИМатериалов.ПолучитьНастройкиОтборов(ПараметрыЗаполнения.РеквизитыШапки);
			Документы.ДвижениеПродукцииИМатериалов.ЗаполнитьСерииПоОтгрузке(
				ЭтотОбъект, ?(НастройкиОтборов <> Неопределено, НастройкиОтборов.ТаблицаОтборов, Неопределено));
		КонецЕсли;
	//++ НЕ УТКА
	ИначеЕсли ПараметрыЗаполнения.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзПроизводства Тогда
		Документы.ДвижениеПродукцииИМатериалов.ЗаполнитьСерииПоЭтапамПроизводства(ЭтотОбъект);
	//-- НЕ УТКА
	КонецЕсли;
	
	Документы.ДвижениеПродукцииИМатериалов.ОбновитьЗависимыеРеквизитыТабличнойЧасти(Товары, ПараметрыЗаполнения);
	
КонецПроцедуры

Процедура ЗаполнитьПередачуМатериаловНаОснованииПриобретения(МассивЗаказов)
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаМатериаловВКладовую) КАК ХозяйственнаяОперация,
		|	МАКСИМУМ(ТаблицаДокумента.Организация),
		|	МАКСИМУМ(ТаблицаТовары.Склад) КАК Отправитель,
		|	МАКСИМУМ(ТаблицаДокумента.НаправлениеДеятельности) КАК НаправлениеДеятельности,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТаблицаДокумента.Организация) КАК КоличествоОрганизаций,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТаблицаТовары.Склад) КАК КоличествоСкладов
		|ИЗ
		|	Документ.ПриобретениеТоваровУслуг КАК ТаблицаДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг.Товары КАК ТаблицаТовары
		|		ПО ТаблицаТовары.Ссылка = ТаблицаДокумента.Ссылка
		|			И ТаблицаТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)
		|ГДЕ
		|	ТаблицаДокумента.Ссылка В (&МассивЗаказов)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПриобретениеТоваровУслугТовары.Склад,
		|	ПриобретениеТоваровУслугТовары.Номенклатура,
		|	ПриобретениеТоваровУслугТовары.Характеристика,
		|	ПриобретениеТоваровУслугТовары.Назначение КАК Назначение,
		|	ПриобретениеТоваровУслугТовары.Назначение КАК НазначениеОтправителя,
		|	ПриобретениеТоваровУслугТовары.Упаковка,
		|	ПриобретениеТоваровУслугТовары.КоличествоУпаковок КАК КоличествоУпаковок,
		|	ПриобретениеТоваровУслугТовары.Количество КАК Количество,
		|	ПриобретениеТоваровУслугТовары.СтатусУказанияСерий КАК СтатусУказанияСерий,
		|	ПриобретениеТоваровУслугТовары.СтатусУказанияСерий КАК СтатусУказанияСерийОтправитель,
		|	ПриобретениеТоваровУслугТовары.Серия КАК Серия
		|ИЗ
		|	Документ.ПриобретениеТоваровУслуг.Товары КАК ПриобретениеТоваровУслугТовары
		|ГДЕ
		|	ПриобретениеТоваровУслугТовары.Ссылка В (&МассивЗаказов)
		|	И ПриобретениеТоваровУслугТовары.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПриобретениеТоваровУслугСерии.Склад,
		|	ПриобретениеТоваровУслугСерии.Серия,
		|	ПриобретениеТоваровУслугСерии.Количество,
		|	ПриобретениеТоваровУслугСерии.Номенклатура,
		|	ПриобретениеТоваровУслугСерии.Характеристика,
		|	ПриобретениеТоваровУслугСерии.Назначение КАК Назначение,
		|	ПриобретениеТоваровУслугСерии.Назначение КАК НазначениеОтправителя
		|ИЗ
		|	Документ.ПриобретениеТоваровУслуг.Серии КАК ПриобретениеТоваровУслугСерии
		|ГДЕ
		|	ПриобретениеТоваровУслугСерии.Ссылка В (&МассивЗаказов)
		|	И ПриобретениеТоваровУслугСерии.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар))");
	
	Запрос.УстановитьПараметр("МассивЗаказов", МассивЗаказов);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаРеквизиты = РезультатЗапроса[0].Выбрать();
	Если Не ВыборкаРеквизиты.Следующий() Тогда
		Возврат;
	КонецЕсли;
	
	// Проверка шапки и табличной части
	Если ВыборкаРеквизиты.КоличествоОрганизаций > 1 Тогда
		ТекстОшибки = НСтр("ru = 'Невозможно оформить передачу материалов по разным организациям.';
							|en = 'Cannot register material transfer for different companies.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	Если ВыборкаРеквизиты.КоличествоСкладов > 1 Тогда
		ТекстОшибки = НСтр("ru = 'Невозможно оформить передачу материалов, т.к материалы поступили на разные склады.';
							|en = 'Cannot register material transfer as materials were delivered to different warehouses.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	ТоварыОснования = РезультатЗапроса[1].Выгрузить();
	Если ТоварыОснования.Количество() = 0 Тогда
		ТекстОшибки = НСтр("ru = 'Поступление не содержит товаров. Ввод на основании невозможен.';
							|en = 'Receipt does not contain any goods. Cannot generate any documents from it.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаРеквизиты);
	
	// Разбиение строк, заполнение серий со статусом 10.
	ИндексыСтрок = Новый Массив();
	
	Для Каждого СтрокаТовары Из ТоварыОснования Цикл
		Если СтрокаТовары.СтатусУказанияСерий = 10 Тогда
			ИндексыСтрок.Вставить(0, ТоварыОснования.Индекс(СтрокаТовары));
		КонецЕсли;
	КонецЦикла;
	
	СерииОснования = РезультатЗапроса[2].Выгрузить();
	
	Если ИндексыСтрок.Количество() > 0 Тогда
		КлючСерии = "Номенклатура, Характеристика, Склад, Назначение";
		НакладныеСервер.ПеренестиСерииИзТаблицыВСтроки(ТоварыОснования, ИндексыСтрок, СерииОснования, КлючСерии);
	КонецЕсли;
	
	Товары.Загрузить(ТоварыОснования);
	
	Для Каждого СтрокаСерии Из СерииОснования Цикл
		Если СтрокаСерии.Количество > 0 Тогда
			НоваяСтрока = Серии.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаСерии);
			НоваяСтрока.НазначениеОтправителя = НоваяСтрока.Назначение;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

//++ НЕ УТКА

Процедура ЗаполнитьПередачуМатериаловНаОснованииЗаказаДавальца(МассивЗаказов)
	
	Запрос = Новый Запрос();
	Тексты = Новый Массив();
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаМатериаловВКладовую) КАК ХозяйственнаяОперация,
		|	МАКСИМУМ(Реквизиты.Организация)                                          КАК Организация,
		|	МАКСИМУМ(Реквизиты.СкладПоступления)                                     КАК Отправитель,
		|	МАКСИМУМ(Реквизиты.НалогообложениеНДС)                                   КАК ПотреблениеДляДеятельности,
		|	МАКСИМУМ(Реквизиты.НаправлениеДеятельности)                              КАК НаправлениеДеятельности,
		|	МАКСИМУМ(Реквизиты.ЕстьОшибкиПроведен)                                   КАК ЕстьОшибкиПроведен,
		|	МАКСИМУМ(Реквизиты.ЕстьОшибкиСтатус)                                     КАК ЕстьОшибкиСтатус,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Реквизиты.Организация)                              КАК КоличествоОрганизаций,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Реквизиты.СкладПоступления)                         КАК КоличествоСкладов,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Реквизиты.НалогообложениеНДС)                       КАК КоличествоВариантовНалогообложения
		|ИЗ
		|	(ВЫБРАТЬ
		|		ЗаказДавальца.Организация                             КАК Организация,
		|		ЗаказДавальца.СкладПоступления                        КАК СкладПоступления,
		|		ЗаказДавальца.НалогообложениеНДС                      КАК НалогообложениеНДС,
		|		ЗаказДавальца.НаправлениеДеятельности                 КАК НаправлениеДеятельности,
		|		НЕ ЗаказДавальца.Проведен                             КАК ЕстьОшибкиПроведен,
		|		НЕ ЗаказДавальца.Статус В (&МассивДопустимыхСтатусов) КАК ЕстьОшибкиСтатус 
		|	ИЗ
		|		Документ.ЗаказДавальца2_5 КАК ЗаказДавальца
		|	ГДЕ
		|		ЗаказДавальца.Ссылка В(&ЗаказыДавальца)
		|		И НЕ ЗаказДавальца.Склад.ЭтоГруппа
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ЗаказДавальца.Организация                             КАК Организация,
		|		ТабличнаяЧастьМатериалы.Склад                         КАК СкладПоступления,
		|		ЗаказДавальца.НалогообложениеНДС                      КАК НалогообложениеНДС,
		|		ЗаказДавальца.НаправлениеДеятельности                 КАК НаправлениеДеятельности,
		|		НЕ ЗаказДавальца.Проведен                             КАК ЕстьОшибкиПроведен,
		|		НЕ ЗаказДавальца.Статус В (&МассивДопустимыхСтатусов) КАК ЕстьОшибкиСтатус 
		|	ИЗ
		|		Документ.ЗаказДавальца2_5 КАК ЗаказДавальца
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказДавальца2_5.Материалы КАК ТабличнаяЧастьМатериалы
		|			ПО ЗаказДавальца.Ссылка = ТабличнаяЧастьМатериалы.Ссылка
		|	ГДЕ
		|		ЗаказДавальца.Ссылка В(&ЗаказыДавальца)
		|		И ЗаказДавальца.Склад.ЭтоГруппа
		//++ Устарело_Переработка24
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ЗаказДавальца.Организация                             КАК Организация,
		|		ЗаказДавальца.СкладПоступления                        КАК СкладПоступления,
		|		ЗаказДавальца.НалогообложениеНДС                      КАК НалогообложениеНДС,
		|		ЗаказДавальца.НаправлениеДеятельности                 КАК НаправлениеДеятельности,
		|		НЕ ЗаказДавальца.Проведен                             КАК ЕстьОшибкиПроведен,
		|		НЕ ЗаказДавальца.Статус В (&МассивДопустимыхСтатусов) КАК ЕстьОшибкиСтатус 
		|	ИЗ
		|		Документ.ЗаказДавальца КАК ЗаказДавальца
		|	ГДЕ
		|		ЗаказДавальца.Ссылка В(&ЗаказыДавальца)
		|		И НЕ ЗаказДавальца.Склад.ЭтоГруппа
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ЗаказДавальца.Организация                             КАК Организация,
		|		ТабличнаяЧастьМатериалы.Склад                         КАК СкладПоступления,
		|		ЗаказДавальца.НалогообложениеНДС                      КАК НалогообложениеНДС,
		|		ЗаказДавальца.НаправлениеДеятельности                 КАК НаправлениеДеятельности,
		|		НЕ ЗаказДавальца.Проведен                             КАК ЕстьОшибкиПроведен,
		|		НЕ ЗаказДавальца.Статус В (&МассивДопустимыхСтатусов) КАК ЕстьОшибкиСтатус 
		|	ИЗ
		|		Документ.ЗаказДавальца КАК ЗаказДавальца
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказДавальца.Материалы КАК ТабличнаяЧастьМатериалы
		|			ПО ЗаказДавальца.Ссылка = ТабличнаяЧастьМатериалы.Ссылка
		|	ГДЕ
		|		ЗаказДавальца.Ссылка В(&ЗаказыДавальца)
		|		И ЗаказДавальца.Склад.ЭтоГруппа
		//-- Устарело_Переработка24
		|	) КАК Реквизиты
		|;
		|
		|/////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Товары.Номенклатура         КАК Номенклатура,
		|	Товары.Характеристика       КАК Характеристика,
		|	Товары.Склад                КАК Склад,
		|	Товары.Назначение           КАК Назначение,
		|	СУММА(Товары.Количество)    КАК Количество,
		|	
		|	МИНИМУМ(Товары.Упаковка)    КАК Упаковка,
		|	МИНИМУМ(Товары.НомерСтроки) КАК НомерСтроки
		|ПОМЕСТИТЬ ВтТовары
		|ИЗ
		|	Документ.ЗаказДавальца2_5.Материалы КАК Товары
		|ГДЕ
		|	Товары.Ссылка В(&ЗаказыДавальца)
		|
		|СГРУППИРОВАТЬ ПО
		|	Товары.Номенклатура,
		|	Товары.Характеристика,
		|	Товары.Склад,
		|	Товары.Назначение
		//++ Устарело_Переработка24
		|	
		|ОБЪЕДИНИТЬ ВСЕ
		|	
		|ВЫБРАТЬ
		|	Товары.Номенклатура         КАК Номенклатура,
		|	Товары.Характеристика       КАК Характеристика,
		|	Товары.Склад                КАК Склад,
		|	Товары.Назначение           КАК Назначение,
		|	СУММА(Товары.Количество)    КАК Количество,
		|	
		|	МИНИМУМ(Товары.Упаковка)    КАК Упаковка,
		|	МИНИМУМ(Товары.НомерСтроки) КАК НомерСтроки
		|	
		|ИЗ
		|	Документ.ЗаказДавальца.Материалы КАК Товары
		|ГДЕ
		|	Товары.Ссылка В(&ЗаказыДавальца)
		|СГРУППИРОВАТЬ ПО
		|	Товары.Номенклатура, Товары.Характеристика, Товары.Склад, Товары.Назначение
		//-- Устарело_Переработка24
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	Характеристика,
		|	Склад,
		|	Назначение
		|";
	Тексты.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = ОбеспечениеВДокументахСервер.ВременнаяТаблицаСвободныеОстатки("ВтТовары", "ТаблицаОстатков");
	Тексты.Добавить(ТекстЗапроса);
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Товары.Номенклатура   КАК Номенклатура,
		|	Товары.Характеристика КАК Характеристика,
		|	Товары.Склад          КАК Склад,
		|	Товары.Назначение     КАК Назначение,
		|	Товары.Назначение     КАК НазначениеОтправителя,
		|	
		|	ВЫБОР КОГДА ТаблицаОстатки.Количество > Товары.Количество ТОГДА // передается не более чем количество, указанное в заказе.
		|				Товары.Количество
		|			ИНАЧЕ
		|				ТаблицаОстатки.Количество
		|		КОНЕЦ                     КАК Количество,
		|	
		|	Товары.Упаковка               КАК Упаковка,
		|	Товары.НомерСтроки            КАК НомерСтроки,
		|
		|	ВЫБОР КОГДА ТаблицаОстатки.Количество > Товары.Количество ТОГДА // передается не более чем количество, указанное в заказе.
		|				Товары.Количество
		|			ИНАЧЕ
		|				ТаблицаОстатки.Количество
		|		КОНЕЦ / ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) КАК КоличествоУпаковок
		|ИЗ
		|	ВтТовары КАК Товары
		|		
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаОстатков КАК ТаблицаОстатки
		|		ПО Товары.Номенклатура   = ТаблицаОстатки.Номенклатура
		|		 И Товары.Характеристика = ТаблицаОстатки.Характеристика
		|		 И Товары.Склад          = ТаблицаОстатки.Склад
		|		 И Товары.Назначение     = ТаблицаОстатки.Назначение
		|	
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	Тексты.Добавить(ТекстЗапроса);
	Запрос.Текст = СтрСоединить(Тексты, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	МассивДопустимыхСтатусов = Новый Массив();
	МассивДопустимыхСтатусов.Добавить(Перечисления.СтатусыЗаказовДавальцев.КПроизводству);
	МассивДопустимыхСтатусов.Добавить(Перечисления.СтатусыЗаказовДавальцев.КОтгрузке);
	МассивДопустимыхСтатусов.Добавить(Перечисления.СтатусыЗаказовДавальцев.Закрыт);
	
	Запрос.УстановитьПараметр("ЗаказыДавальца", МассивЗаказов);
	Запрос.УстановитьПараметр("МассивДопустимыхСтатусов", МассивДопустимыхСтатусов);
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"Товары.Упаковка",
		"Товары.Номенклатура"));
	
	Результат = Запрос.ВыполнитьПакет();
	
	Реквизиты = Результат[0].Выбрать();
	
	Если Не Реквизиты.Следующий() Тогда
		Возврат;
	КонецЕсли;
	
	Если Реквизиты.КоличествоОрганизаций > 1 Тогда
		ТекстОшибки = НСтр("ru = 'Невозможно оформить передачу материалов по разным организациям.';
							|en = 'Cannot register material transfer for different companies.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	Если Реквизиты.КоличествоСкладов > 1 Тогда
		ТекстОшибки = НСтр("ru = 'Невозможно оформить передачу материалов, т.к. материалы поступили на разные склады.';
							|en = 'Cannot register material transfer as materials were delivered to different warehouses.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	Если Реквизиты.ЕстьОшибкиСтатус Тогда
		ТекстОшибки = НСтр("ru = 'Для оформления передачи в производстово заказ должен быть в статусе ""К производству"", ""К отгрузке"" или ""Закрыт"".';
							|en = 'To register transfer to production, the order must be in the ""For production"", ""To ship"", or ""Closed"" status.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	Если Результат[Результат.ВГраница()].Пустой() Тогда
		ТекстОшибки = НСтр("ru = 'Поступление сырья от давальца не оформлено или все материалы уже переданы. Ввод на основании невозможен.';
							|en = 'No ""Goods receipt — Subcontracting services delivered"" is registered, or all materials are already transferred. Generation from base documents is not available.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	Если Реквизиты.КоличествоВариантовНалогообложения > 1 Тогда
		ТекстОшибки = НСтр("ru = 'Невозможно оформить один документ, т.к. выбранные заказы оформлены под разную деятельность по НДС.';
							|en = 'Cannot register one document as the selected orders are registered for different activity on VAT.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Реквизиты);
	
	Товары.Загрузить(Результат[Результат.ВГраница()].Выгрузить());
	
КонецПроцедуры

Процедура ЗаполнитьВыпускПродукцииНаОснованииЗаказаДавальца(МассивЗаказов)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДанныеДокумента.ХозяйственнаяОперация              КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация                        КАК Организация,
	|	ДанныеДокумента.Получатель                         КАК Получатель,
	|	ДанныеДокумента.НаправлениеДеятельности            КАК НаправлениеДеятельности,
	|	ДанныеДокумента.НалогообложениеНДС                 КАК НалогообложениеНДС,
	|	ДанныеДокумента.ЕстьОшибкиПроведен                 КАК ЕстьОшибкиПроведен,
	|	ДанныеДокумента.ЕстьОшибкиСтатус                   КАК ЕстьОшибкиСтатус,
	|	ДанныеДокумента.КоличествоОрганизаций              КАК КоличествоОрганизаций,
	|	ДанныеДокумента.КоличествоСкладов                  КАК КоличествоСкладов,
	|	ДанныеДокумента.КоличествоВариантовНалогообложения КАК КоличествоВариантовНалогообложения,
	|	ДанныеДокумента.КоличествоНаправленийДеятельности  КАК КоличествоНаправленийДеятельности
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаПродукцииИзКладовой) КАК ХозяйственнаяОперация,
	|		МАКСИМУМ(ЗаказДавальца2_5.Организация)                                   КАК Организация,
	|		МАКСИМУМ(ТабличнаяЧастьПродукция.Склад)                                  КАК Получатель,
	|	
	|		МАКСИМУМ(ВЫБОР
	|					КОГДА ЗаказДавальца2_5.НаправлениеДеятельности.УчетЗатрат
	|						ТОГДА ЗаказДавальца2_5.НаправлениеДеятельности
	|					ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|				КОНЕЦ)                                                           КАК НаправлениеДеятельности,
	|	
	|		МАКСИМУМ(ЗаказДавальца2_5.НалогообложениеНДС)                            КАК НалогообложениеНДС,
	|		МАКСИМУМ(НЕ ЗаказДавальца2_5.Проведен)                                   КАК ЕстьОшибкиПроведен,
	|		МАКСИМУМ(ВЫБОР
	|					КОГДА ЗаказДавальца2_5.Статус В (&МассивДопустимыхСтатусов)
	|						ТОГДА ЛОЖЬ
	|					ИНАЧЕ ИСТИНА
	|				КОНЕЦ)                                                           КАК ЕстьОшибкиСтатус,
	|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаказДавальца2_5.Организация)                       КАК КоличествоОрганизаций,
	|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТабличнаяЧастьПродукция.Склад)                      КАК КоличествоСкладов,
	|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаказДавальца2_5.НалогообложениеНДС)                КАК КоличествоВариантовНалогообложения,
	|	
	|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
	|								КОГДА ЗаказДавальца2_5.НаправлениеДеятельности.УчетЗатрат
	|									ТОГДА ЗаказДавальца2_5.НаправлениеДеятельности
	|								ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|							КОНЕЦ)                                               КАК КоличествоНаправленийДеятельности
	|	ИЗ
	|		Документ.ЗаказДавальца2_5 КАК ЗаказДавальца2_5
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказДавальца2_5.Продукция КАК ТабличнаяЧастьПродукция
	|			ПО ЗаказДавальца2_5.Ссылка = ТабличнаяЧастьПродукция.Ссылка
	|			 И НЕ ТабличнаяЧастьПродукция.Отменено
	|	ГДЕ
	|		ЗаказДавальца2_5.Ссылка В(&ЗаказыДавальца)
	//++ Устарело_Переработка24
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаПродукцииИзКладовой) КАК ХозяйственнаяОперация,
	|		МАКСИМУМ(ЗаказДавальца.Организация)        КАК Организация,
	|		МАКСИМУМ(ЗаказДавальца.Склад)              КАК Получатель,
	|	
	|		МАКСИМУМ(
	|			ВЫБОР КОГДА ЗаказДавальца.НаправлениеДеятельности.УчетЗатрат ТОГДА
	|						ЗаказДавальца.НаправлениеДеятельности
	|					ИНАЧЕ
	|						ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|				КОНЕЦ)                             КАК НаправлениеДеятельности,
	|	
	|		МАКСИМУМ(ЗаказДавальца.НалогообложениеНДС) КАК НалогообложениеНДС,
	|		МАКСИМУМ(НЕ ЗаказДавальца.Проведен)        КАК ЕстьОшибкиПроведен,
	|		МАКСИМУМ(ВЫБОР
	|				КОГДА ЗаказДавальца.Статус В (&МассивДопустимыхСтатусов)
	|					ТОГДА ЛОЖЬ
	|				ИНАЧЕ ИСТИНА
	|			КОНЕЦ) КАК ЕстьОшибкиСтатус,
	|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаказДавальца.Организация)   КАК КоличествоОрганизаций,
	|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаказДавальца.Склад)         КАК КоличествоСкладов,
	|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаказДавальца.НалогообложениеНДС) КАК КоличествоВариантовНалогообложения,
	|	
	|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ 
	|			ВЫБОР КОГДА ЗаказДавальца.НаправлениеДеятельности.УчетЗатрат ТОГДА
	|						ЗаказДавальца.НаправлениеДеятельности
	|					ИНАЧЕ
	|						ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|				КОНЕЦ)                                    КАК КоличествоНаправленийДеятельности
	|	ИЗ
	|		Документ.ЗаказДавальца КАК ЗаказДавальца
	|	ГДЕ
	|		ЗаказДавальца.Ссылка В(&ЗаказыДавальца)
	//-- Устарело_Переработка24
	|	) КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Организация <> НЕОПРЕДЕЛЕНО
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура         КАК Номенклатура,
	|	Товары.Характеристика       КАК Характеристика,
	|	Товары.Склад                КАК Склад,
	|	ТабДокументы.Назначение     КАК Назначение,
	|	СУММА(Товары.Количество)    КАК Количество,
	|	
	|	МИНИМУМ(Товары.Упаковка)    КАК Упаковка,
	|	МИНИМУМ(Товары.НомерСтроки) КАК НомерСтроки
	|ПОМЕСТИТЬ ВтТовары
	|ИЗ
	|	Документ.ЗаказДавальца2_5.Продукция КАК Товары
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказДавальца2_5 КАК ТабДокументы
	|		ПО ТабДокументы.Ссылка = Товары.Ссылка
	|ГДЕ
	|	Товары.Ссылка В(&ЗаказыДавальца)
	|	И НЕ Товары.Отменено
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Склад,
	|	ТабДокументы.Назначение
	//++ Устарело_Переработка24
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	Товары.Номенклатура         КАК Номенклатура,
	|	Товары.Характеристика       КАК Характеристика,
	|	Товары.Склад                КАК Склад,
	|	ТабДокументы.Назначение     КАК Назначение,
	|	СУММА(Товары.Количество)    КАК Количество,
	|	
	|	МИНИМУМ(Товары.Упаковка)     КАК Упаковка,
	|	МИНИМУМ(Товары.НомерСтроки)  КАК НомерСтроки
	|	
	|ИЗ
	|	Документ.ЗаказДавальца.Продукция КАК Товары
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказДавальца КАК ТабДокументы
	|		ПО ТабДокументы.Ссылка = Товары.Ссылка
	|ГДЕ
	|	Товары.Ссылка В(&ЗаказыДавальца)
	|	И НЕ Товары.Отменено
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура, Товары.Характеристика, Товары.Склад, ТабДокументы.Назначение
	//-- Устарело_Переработка24
	|	
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура, Характеристика, Склад, Назначение
	|;
	|
	|//////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РаспределениеЗапасов.Номенклатура        КАК Номенклатура,
	|	РаспределениеЗапасов.Характеристика      КАК Характеристика,
	|	РаспределениеЗапасов.Склад               КАК Склад,
	|	РаспределениеЗапасов.Назначение          КАК Назначение,
	|	СУММА(РаспределениеЗапасов.НеОбеспечено) КАК Количество
	|ПОМЕСТИТЬ ВтОстатки
	|ИЗ
	|	ВтТовары КАК НоменклатураЗаказа
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеЗапасов КАК РаспределениеЗапасов
	|		ПО РаспределениеЗапасов.Номенклатура   = НоменклатураЗаказа.Номенклатура
	|		 И РаспределениеЗапасов.Характеристика = НоменклатураЗаказа.Характеристика
	|		 И РаспределениеЗапасов.Склад          = НоменклатураЗаказа.Склад
	|		 И РаспределениеЗапасов.Назначение     = НоменклатураЗаказа.Назначение
	|		 И РаспределениеЗапасов.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.Обеспечить)
	|ГДЕ
	|	НЕ РаспределениеЗапасов.Номенклатура ЕСТЬ NULL
	|СГРУППИРОВАТЬ ПО
	|	РаспределениеЗапасов.Характеристика,
	|	РаспределениеЗапасов.Номенклатура,
	|	РаспределениеЗапасов.Склад,
	|	РаспределениеЗапасов.Назначение
	|ИМЕЮЩИЕ
	|	СУММА(РаспределениеЗапасов.НеОбеспечено) > 0
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура, Характеристика, Склад, Назначение
	|;
	|
	|//////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОстатки.Номенклатура   КАК Номенклатура,
	|	ТаблицаОстатки.Характеристика КАК Характеристика,
	|	ТаблицаОстатки.Склад          КАК Склад,
	|	ТаблицаОстатки.Назначение     КАК Назначение,
	|	ТаблицаОстатки.Назначение     КАК НазначениеОтправителя,
	|	
	|	ВЫБОР КОГДА ТаблицаОстатки.Количество > Товары.Количество ТОГДА // передается не более чем количество, указанное в заказе.
	|				Товары.Количество
	|			ИНАЧЕ
	|				ТаблицаОстатки.Количество
	|		КОНЕЦ                     КАК Количество,
	|	
	|	Товары.Упаковка               КАК Упаковка,
	|	Товары.НомерСтроки            КАК НомерСтроки,
	|
	|	ВЫБОР КОГДА ТаблицаОстатки.Количество > Товары.Количество ТОГДА // передается не более чем количество, указанное в заказе.
	|				Товары.Количество
	|			ИНАЧЕ
	|				ТаблицаОстатки.Количество
	|		КОНЕЦ / ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) КАК КоличествоУпаковок
	|ИЗ
	|	ВтОстатки КАК ТаблицаОстатки
	|		
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтТовары КАК Товары
	|	ПО Товары.Номенклатура   = ТаблицаОстатки.Номенклатура
	|	И Товары.Характеристика  = ТаблицаОстатки.Характеристика
	|	И Товары.Склад           = ТаблицаОстатки.Склад
	|	И Товары.Назначение      = ТаблицаОстатки.Назначение
	|	
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки");
	
	МассивДопустимыхСтатусов = Новый Массив();
	МассивДопустимыхСтатусов.Добавить(Перечисления.СтатусыЗаказовДавальцев.КПроизводству);
	МассивДопустимыхСтатусов.Добавить(Перечисления.СтатусыЗаказовДавальцев.КОтгрузке);
	МассивДопустимыхСтатусов.Добавить(Перечисления.СтатусыЗаказовДавальцев.Закрыт);
	
	Запрос.УстановитьПараметр("ЗаказыДавальца", МассивЗаказов);
	Запрос.УстановитьПараметр("МассивДопустимыхСтатусов", МассивДопустимыхСтатусов);
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"Товары.Упаковка",
		"Товары.Номенклатура"));
	
	Результат = Запрос.ВыполнитьПакет();
	
	Реквизиты = Результат[0].Выбрать();
	
	Если Не Реквизиты.Следующий() Тогда
		Возврат;
	КонецЕсли;
	
	Если Реквизиты.КоличествоОрганизаций > 1 Тогда
		ТекстОшибки = НСтр("ru = 'Невозможно оформить выпуск продукции по разным организациям.';
							|en = 'Cannot register product release by different companies.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	Если Реквизиты.КоличествоСкладов > 1 Тогда
		ТекстОшибки = НСтр("ru = 'Невозможно оформить один документ, т.к. продукция должна быть выпущена на разные склады.';
							|en = 'Cannot register one document as the products should be released to different warehouses.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	Если Реквизиты.ЕстьОшибкиСтатус Тогда
		ТекстОшибки = НСтр("ru = 'Для оформления выпуска продукции заказ должен быть в статусе ""К производству"", ""К отгрузке"" или ""Закрыт"".';
							|en = 'To register the product release, the order must be in the ""For production"", ""To ship"", or ""Closed"" status.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	Если Результат[Результат.ВГраница()].Пустой() Тогда
		ТекстОшибки = НСтр("ru = 'Вся продукция уже выпущена, ввод на основании не требуется.';
							|en = 'All products have already been released. Generation from base documents is not required.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	Если Реквизиты.КоличествоВариантовНалогообложения > 1 Тогда
		ТекстОшибки = НСтр("ru = 'Невозможно оформить один документ, т.к. выбранные заказы оформлены под разную деятельность по НДС.';
							|en = 'Cannot register one document as the selected orders are registered for different activity on VAT.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	Если Реквизиты.КоличествоНаправленийДеятельности > 1 Тогда
		ТекстОшибки = НСтр("ru = 'Невозможно оформить один документ, т.к. выбранные заказы оформлены под разные направления деятельности.';
							|en = 'Cannot register one document as the selected orders are registered for different lines of business.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Реквизиты);
	
	Товары.Загрузить(Результат[Результат.ВГраница()].Выгрузить());
	
КонецПроцедуры

//++ Устарело_Переработка24

Процедура ЗаполнитьПередачуМатериаловНаОснованииПоступленияСырьяОтДавальца(ПоступленияСырья)
	
	Запрос = Новый Запрос();
	Тексты = Новый Массив();
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаМатериаловВКладовую) КАК ХозяйственнаяОперация,
		|	МАКСИМУМ(ПоступлениеСырьяОтДавальца.Организация) КАК Организация,
		|	МАКСИМУМ(ПоступлениеСырьяОтДавальца.Склад) КАК Отправитель,
		|	МАКСИМУМ(ПоступлениеСырьяОтДавальцаТовары.ЗаказДавальца.НалогообложениеНДС) КАК ПотреблениеДляДеятельности,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПоступлениеСырьяОтДавальца.Организация) КАК КоличествоОрганизаций,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПоступлениеСырьяОтДавальца.Склад) КАК КоличествоСкладов,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПоступлениеСырьяОтДавальцаТовары.ЗаказДавальца.НалогообложениеНДС) КАК КоличествоВариантовНалогообложения
		|ИЗ
		|	Документ.ПоступлениеСырьяОтДавальца.Товары КАК ПоступлениеСырьяОтДавальцаТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеСырьяОтДавальца КАК ПоступлениеСырьяОтДавальца
		|		ПО ПоступлениеСырьяОтДавальцаТовары.Ссылка = ПоступлениеСырьяОтДавальца.Ссылка
		|ГДЕ
		|	ПоступлениеСырьяОтДавальца.Ссылка В(&ПоступленияСырья)
		|;
		|
		|//////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Товары.Номенклатура КАК Номенклатура,
		|	Товары.Характеристика КАК Характеристика,
		|	Товары.Склад КАК Склад,
		|	Товары.Назначение КАК Назначение,
		|	СУММА(Товары.Количество) КАК Количество,
		|	МАКСИМУМ(Товары.Упаковка) КАК Упаковка,
		|	МИНИМУМ(Товары.НомерСтроки) КАК НомерСтроки
		|ПОМЕСТИТЬ ВтТовары
		|ИЗ
		|	Документ.ПоступлениеСырьяОтДавальца.Товары КАК Товары
		|ГДЕ
		|	Товары.Ссылка В(&ПоступленияСырья)
		|СГРУППИРОВАТЬ ПО
		|	Товары.Номенклатура, Товары.Характеристика, Товары.Склад, Товары.Назначение
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение";
	Тексты.Добавить(ТекстЗапроса);
	ТекстЗапроса = ОбеспечениеВДокументахСервер.ВременнаяТаблицаСвободныеОстатки("ВтТовары", "ВтОстатки");
	Тексты.Добавить(ТекстЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаОстатки.Номенклатура   КАК Номенклатура,
	|	ТаблицаОстатки.Характеристика КАК Характеристика,
	|	ТаблицаОстатки.Склад          КАК Склад,
	|	ТаблицаОстатки.Назначение     КАК Назначение,
	|	ТаблицаОстатки.Назначение     КАК НазначениеОтправителя,
	|	ВЫБОР КОГДА ТаблицаОстатки.Количество < Товары.Количество ТОГДА
	|				ТаблицаОстатки.Количество
	|			ИНАЧЕ
	|				Товары.Количество
	|		КОНЕЦ                     КАК Количество,
	|	
	|	Товары.Упаковка       КАК Упаковка,
	|	Товары.НомерСтроки    КАК НомерСтроки,
	|
	|	ВЫБОР КОГДА ТаблицаОстатки.Количество < Товары.Количество ТОГДА
	|				ТаблицаОстатки.Количество
	|			ИНАЧЕ
	|				Товары.Количество
	|		КОНЕЦ / ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) КАК КоличествоУпаковок
	|ИЗ
	|	ВтОстатки КАК ТаблицаОстатки
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтТовары КАК Товары
	|		ПО Товары.Номенклатура   = ТаблицаОстатки.Номенклатура
	|		 И Товары.Характеристика = ТаблицаОстатки.Характеристика
	|		 И Товары.Склад          = ТаблицаОстатки.Склад
	|		 И Товары.Назначение     = ТаблицаОстатки.Назначение
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	Тексты.Добавить(ТекстЗапроса);
	
	Запрос.Текст = СтрСоединить(Тексты, ОбщегоНазначения.РазделительПакетаЗапросов());
	Запрос.УстановитьПараметр("ПоступленияСырья", ПоступленияСырья);
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"Товары.Упаковка",
		"Товары.Номенклатура"));
	
	Результат = Запрос.ВыполнитьПакет();
	
	Реквизиты = Результат[0].Выбрать();
	
	Если Не Реквизиты.Следующий() Тогда
		Возврат;
	КонецЕсли;
	
	Если Реквизиты.КоличествоОрганизаций > 1 Тогда
		ТекстОшибки = НСтр("ru = 'Невозможно оформить передачу материалов по разным организациям.';
							|en = 'Cannot register material transfer for different companies.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	Если Реквизиты.КоличествоСкладов > 1 Тогда
		ТекстОшибки = НСтр("ru = 'Невозможно оформить передачу материалов, т.к. материалы поступили на разные склады.';
							|en = 'Cannot register material transfer as materials were delivered to different warehouses.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	Если Реквизиты.КоличествоВариантовНалогообложения > 1 Тогда
		ТекстОшибки = НСтр("ru = 'Невозможно оформить один документ, т.к. материал передается под разную деятельность по НДС.';
							|en = 'Cannot register one document as the material is transferred for different activity subject to VAT.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	Если Результат[Результат.ВГраница()].Пустой() Тогда
		ТекстОшибки = НСтр("ru = 'На складе %1 отсутствуют остатки товаров к передаче в кладовую.';
							|en = 'No shelf stock to be transferred to the stockroom in the warehouse %1.'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, Реквизиты.Отправитель);
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Реквизиты);
	
	Товары.Загрузить(Результат[Результат.ВГраница()].Выгрузить());
	
КонецПроцедуры
//-- Устарело_Переработка24

//-- НЕ УТКА

Процедура ЗаполнитьВыпускПродукцииНаОснованииЗаказаКлиента(ДанныеЗаполнения)
	
	ЗаказКлиента = ДанныеЗаполнения.Основание;
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаПродукцииИзКладовой) КАК ХозяйственнаяОперация,
		|	&ЗаказКлиента КАК ДокументОснование,
		|	ЗаказКлиента.Организация КАК Организация,
		|	ЗаказКлиента.Склад КАК Получатель,
		|	ЗаказКлиента.НалогообложениеНДС КАК ВыпускПодДеятельность,
		|	ВЫБОР
		|		КОГДА ЗаказКлиента.НаправлениеДеятельности.УчетЗатрат
		|			ТОГДА ЗаказКлиента.НаправлениеДеятельности
		|	КОНЕЦ КАК НаправлениеДеятельности
		|ИЗ
		|	Документ.ЗаказКлиента КАК ЗаказКлиента
		|ГДЕ
		|	ЗаказКлиента.Ссылка = &ЗаказКлиента");
	
	Запрос.УстановитьПараметр("ЗаказКлиента", ЗаказКлиента);
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Реквизиты);
	
	Получатель = ДанныеЗаполнения.Склад;
	
	// Заполнение табличной части.
	Товары.Загрузить(ПолучитьИзВременногоХранилища(ДанныеЗаполнения.АдресТовары));
	УдалитьИзВременногоХранилища(ДанныеЗаполнения.АдресТовары);
	
	Для Каждого ДанныеСтроки Из Товары Цикл
		ДанныеСтроки.НазначениеОтправителя = ДанныеСтроки.Назначение;
	КонецЦикла;
	
КонецПроцедуры

Функция ДокументОснованиеПриЗаполнении(ДанныеЗаполнения)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура")
		И ДанныеЗаполнения.Свойство("ОбъектыОснований") Тогда
		
		Возврат ДанныеЗаполнения.ОбъектыОснований[0];
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура")
		И ДанныеЗаполнения.Свойство("Товары")
		И ТипЗнч(ДанныеЗаполнения.Товары) = Тип("ТаблицаЗначений")
		И ДанныеЗаполнения.Товары.Колонки.Найти("Распоряжение") <> Неопределено Тогда
		
		Возврат ДанныеЗаполнения.Товары[0].Распоряжение;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура")
		И ДанныеЗаполнения.Свойство("МассивЗаказов") Тогда
		
		Возврат ДанныеЗаполнения.МассивЗаказов[0];
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Массив") Тогда
		
		Возврат ДанныеЗаполнения[0];
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#Область ВидыЗапасов

Функция ВременныеТаблицыДанныхДокумента() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&Дата КАК Дата,
	|	&Организация КАК Организация,
	//++ НЕ УТКА
	|	&ВнутренняяПереработка КАК ВнутренняяПереработка,
	|	&ОрганизацияДавалец КАК ОрганизацияДавалец,
	//-- НЕ УТКА
	|	&Склад КАК Склад,
	|	НЕОПРЕДЕЛЕНО КАК Партнер,
	|	НЕОПРЕДЕЛЕНО КАК Контрагент,
	|	ЗНАЧЕНИЕ(Справочник.СоглашенияСПоставщиками.ПустаяСсылка) КАК Соглашение,
	|	ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) КАК Договор,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК Валюта,
	|	&НалогообложениеНДС КАК НалогообложениеНДС,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ЛОЖЬ КАК ЕстьСделкиВТабличнойЧасти,
	|	ВЫБОР
	|		КОГДА СтруктураПредприятия.ВариантОбособленногоУчетаТоваров = ЗНАЧЕНИЕ(Перечисление.ВариантыОбособленногоУчетаТоваров.ПоПодразделению)
	|				И &ФормироватьВидыЗапасовПоПодразделениямМенеджерам
	|			ТОГДА &Получатель
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|	КОНЕЦ КАК Подразделение,
	|	ВЫБОР
	|		КОГДА СтруктураПредприятия.ВариантОбособленногоУчетаТоваров = ЗНАЧЕНИЕ(Перечисление.ВариантыОбособленногоУчетаТоваров.ПоМенеджерамПодразделения)
	|				И &ФормироватьВидыЗапасовПоПодразделениямМенеджерам
	|			ТОГДА &Менеджер
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
	|	КОНЕЦ КАК Менеджер,
	|	ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка) КАК Сделка,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар) КАК ТипЗапасов
	|ПОМЕСТИТЬ ТаблицаДанныхДокумента
	|ИЗ
	|	Справочник.Организации КАК Организации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтруктураПредприятия КАК СтруктураПредприятия
	|		ПО СтруктураПредприятия.Ссылка = &Получатель
	|
	|ГДЕ
	|	Организации.Ссылка = &Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки,
	//++ НЕ УТКА
	|	&ВнутренняяПереработка КАК ВнутренняяПереработка,
	|	&ОрганизацияДавалец КАК ОрганизацияДавалец,
	//-- НЕ УТКА
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.Характеристика КАК Характеристика,
	|	ТаблицаТоваров.Назначение КАК НазначениеПолучателя,
	|	ТаблицаТоваров.НазначениеОтправителя КАК Назначение,
	|	ТаблицаТоваров.Серия КАК Серия,
	|	ТаблицаТоваров.СтатусУказанияСерийОтправитель КАК СтатусУказанияСерий,
	|	ТаблицаТоваров.СтатусУказанияСерийПолучатель КАК СтатусУказанияСерийПолучатель,
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВЫБОР
	|		КОГДА &УпаковкиВВидахЗапасов
	|			ТОГДА ТаблицаТоваров.Упаковка
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|	КОНЕЦ										КАК Упаковка,
	|	ВЫБОР
	|		КОГДА &УпаковкиВВидахЗапасов
	|			ТОГДА ТаблицаТоваров.КоличествоУпаковок
	|		ИНАЧЕ 0
	|	КОНЕЦ										КАК КоличествоУпаковок,
	|	ТаблицаТоваров.Количество КАК Количество,
	|	ВЫБОР
	|		КОГДА НЕ &ИспользоватьУчетПрослеживаемыхИмпортныхТоваров
	|				ИЛИ НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ) < &ДатаНачалаУчетаПрослеживаемыхИмпортныхТоваров
	|			ТОГДА 0
	|		ИНАЧЕ &ТекстПоляТаблицаТоваровКоличествоПоРНПТ_
	|	КОНЕЦ КАК КоличествоПоРНПТ,
	|	ТаблицаТоваров.Сумма КАК Сумма,
	|	ТаблицаТоваров.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаТоваров.Распоряжение КАК Распоряжение,
	|	ТаблицаТоваров.КодСтроки КАК КодСтроки,
	|	ТаблицаТоваров.ГруппаПродукции,
	|	&ТекстПоляТаблицаТоваровНомерГТД_ КАК НомерГТД
	|ПОМЕСТИТЬ ВтТаблицаТоваров
	|ИЗ
	|	&ТаблицаТоваров КАК ТаблицаТоваров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки,
	//++ НЕ УТКА
	|	ТаблицаТоваров.ВнутренняяПереработка КАК ВнутренняяПереработка,
	|	ТаблицаТоваров.ОрганизацияДавалец КАК ОрганизацияДавалец,
	//-- НЕ УТКА
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.Характеристика КАК Характеристика,
	|	ТаблицаТоваров.Серия КАК Серия,
	|	ТаблицаТоваров.СтатусУказанияСерий КАК СтатусУказанияСерий,
	|	ТаблицаТоваров.СтатусУказанияСерийПолучатель,
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаТоваров.Упаковка КАК Упаковка,
	|	ТаблицаТоваров.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ТаблицаТоваров.Количество КАК Количество,
	|	ТаблицаТоваров.КоличествоПоРНПТ КАК КоличествоПоРНПТ,
	|	ТаблицаТоваров.Сумма КАК Сумма,
	|	&Склад КАК Склад,
	|	ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка) КАК Сделка,
	|	ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка) КАК СтавкаНДС,
	|	0 КАК СуммаСНДС,
	|	0 КАК СуммаНДС,
	|	0 КАК СуммаВознаграждения,
	|	0 КАК СуммаНДСВознаграждения,
	|	ТаблицаТоваров.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаТоваров.Назначение КАК Назначение,
	|	ТаблицаТоваров.НазначениеПолучателя КАК НазначениеПолучателя,
	|	ТаблицаТоваров.ГруппаПродукции,
	|	ТаблицаТоваров.Распоряжение КАК Распоряжение,
	|	ТаблицаТоваров.КодСтроки КАК КодСтроки,
	|	ИСТИНА КАК ПодбиратьВидыЗапасов,
	|	ТаблицаТоваров.НомерГТД КАК НомерГТД
	|ПОМЕСТИТЬ ТаблицаТоваров
	|ИЗ
	|	ВтТаблицаТоваров КАК ТаблицаТоваров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки КАК НомерСтроки,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.НомерГТД КАК НомерГТД,
	|	ТаблицаВидыЗапасов.Упаковка КАК Упаковка,
	|	ТаблицаВидыЗапасов.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ТаблицаВидыЗапасов.Количество КАК Количество,
	|	ВЫБОР
	|		КОГДА НЕ &ИспользоватьУчетПрослеживаемыхИмпортныхТоваров
	|				ИЛИ НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ) < &ДатаНачалаУчетаПрослеживаемыхИмпортныхТоваров
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаВидыЗапасов.КоличествоПоРНПТ
	|	КОНЕЦ КАК КоличествоПоРНПТ,
	|	ТаблицаВидыЗапасов.Сумма КАК Сумма,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК СкладОтгрузки,
	|	&Склад КАК Склад,
	|	ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка) КАК Сделка,
	|	ТаблицаВидыЗапасов.Серия,
	|	ТаблицаВидыЗапасов.ГруппаПродукции,
	|	ТаблицаВидыЗапасов.НазначениеПолучателя,
	|	ТаблицаВидыЗапасов.Распоряжение КАК Распоряжение,
	|	ТаблицаВидыЗапасов.КодСтроки КАК КодСтроки,
	|	&ВидыЗапасовУказаныВручную КАК ВидыЗапасовУказаныВручную
	|ПОМЕСТИТЬ ВтВидыЗапасов
	|ИЗ
	|	&ТаблицаВидыЗапасов КАК ТаблицаВидыЗапасов
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки КАК НомерСтроки,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Аналитика.Номенклатура КАК Номенклатура,
	|	Аналитика.Характеристика КАК Характеристика,
	|	Аналитика.Серия КАК Серия,
	|	ТаблицаВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.НомерГТД КАК НомерГТД,
	|	ТаблицаВидыЗапасов.Упаковка КАК Упаковка,
	|	ТаблицаВидыЗапасов.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ТаблицаВидыЗапасов.Количество КАК Количество,
	|	ТаблицаВидыЗапасов.КоличествоПоРНПТ КАК КоличествоПоРНПТ,
	|	ТаблицаВидыЗапасов.Сумма КАК Сумма,
	|	ТаблицаВидыЗапасов.СкладОтгрузки КАК СкладОтгрузки,
	|	ТаблицаВидыЗапасов.Склад КАК Склад,
	|	ТаблицаВидыЗапасов.Сделка КАК Сделка,
	|	ТаблицаВидыЗапасов.Серия КАК СерияПолучатель,
	|	ТаблицаВидыЗапасов.ВидыЗапасовУказаныВручную КАК ВидыЗапасовУказаныВручную,
	|	ТаблицаВидыЗапасов.НазначениеПолучателя КАК НазначениеПолучателя,
	|	ТаблицаВидыЗапасов.Распоряжение КАК Распоряжение,
	|	ТаблицаВидыЗапасов.КодСтроки КАК КодСтроки,
	|	ТаблицаВидыЗапасов.ГруппаПродукции КАК ГруппаПродукции
	|ПОМЕСТИТЬ ТаблицаВидыЗапасов
	|ИЗ
	|	ВтВидыЗапасов КАК ТаблицаВидыЗапасов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|		ПО ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры = Аналитика.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаНоменклатуры";
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ТаблицаТоваров = ?(ДополнительныеСвойства.Свойство("ТаблицыЗаполненияВидовЗапасовПриОбмене")
							И ДополнительныеСвойства.ТаблицыЗаполненияВидовЗапасовПриОбмене <> Неопределено
							И ДополнительныеСвойства.ТаблицыЗаполненияВидовЗапасовПриОбмене.Свойство("Товары"),
						ДополнительныеСвойства.ТаблицыЗаполненияВидовЗапасовПриОбмене.Товары,
						Товары);
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Организация", Организация);
	//++ НЕ УТКА
	Запрос.УстановитьПараметр("ВнутренняяПереработка", ВнутренняяПереработка);
	Запрос.УстановитьПараметр("ОрганизацияДавалец",    ОрганизацияДавалец);
	//-- НЕ УТКА
	Запрос.УстановитьПараметр("Склад", Отправитель);
	Запрос.УстановитьПараметр("Менеджер", Ответственный);
	Запрос.УстановитьПараметр("Получатель", Получатель);
	Запрос.УстановитьПараметр("ПоРаспоряжениям", ПоРаспоряжениям);
	Запрос.УстановитьПараметр("ХозяйственнаяОперация", ХозяйственнаяОперация);
	Запрос.УстановитьПараметр("НалогообложениеНДС", НалогообложениеНДС);
	Запрос.УстановитьПараметр("ВидыЗапасовУказаныВручную", ВидыЗапасовУказаныВручную);
	Запрос.УстановитьПараметр("ФормироватьВидыЗапасовПоПодразделениямМенеджерам", ПолучитьФункциональнуюОпцию("ФормироватьВидыЗапасовПоПодразделениямМенеджерам"));
	Запрос.УстановитьПараметр("ФормироватьВидыЗапасовПоСделкам", ПолучитьФункциональнуюОпцию("ФормироватьВидыЗапасовПоСделкам"));
	Запрос.УстановитьПараметр("ТаблицаТоваров", ТаблицаТоваров);
	Запрос.УстановитьПараметр("ТаблицаВидыЗапасов", ВидыЗапасов.Выгрузить());
	Запрос.УстановитьПараметр("УпаковкиВВидахЗапасов", УпаковкиВВидахЗапасов);
	Запрос.УстановитьПараметр("ПустойКлючСвязи", Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000"));
	
	УчетПрослеживаемыхТоваровЛокализация.УстановитьПараметрыИспользованияУчетаПрослеживаемыхТоваров(Запрос);
	
	ЗапасыСервер.ДополнитьВременныеТаблицыОбязательнымиКолонками(Запрос);
	
	ОбщегоНазначенияУТ.ЗаменитьОтсутствующиеПоляТаблицыЗначенийВТекстеЗапроса(
		ТаблицаТоваров,
		Запрос.Текст,
		"&ТекстПоляТаблицаТоваровКоличествоПоРНПТ_",
		"ТаблицаТоваров",
		"КоличествоПоРНПТ",
		"ТаблицаТоваров.КоличествоПоРНПТ",
		"0");
	
	ОбщегоНазначенияУТ.ЗаменитьОтсутствующиеПоляТаблицыЗначенийВТекстеЗапроса(
		ТаблицаТоваров,
		Запрос.Текст,
		"&ТекстПоляТаблицаТоваровНомерГТД_",
		"ТаблицаТоваров",
		"НомерГТД",
		"ТаблицаТоваров.НомерГТД",
		"ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)");
	
	Запрос.Выполнить();
	
	Возврат МенеджерВременныхТаблиц;
	
КонецФункции

Функция ПроверитьИзменениеРеквизитовДокумента(МенеджерВременныхТаблиц)
	
	ИменаРеквизитов = "Организация, Дата";
	//++ НЕ УТКА
	ИменаРеквизитов = "Организация, Дата, ВнутренняяПереработка, ОрганизацияДавалец";
	//-- НЕ УТКА
	
	Возврат ЗапасыСервер.ПроверитьИзменениеРеквизитовДокумента(МенеджерВременныхТаблиц, Ссылка, ИменаРеквизитов);
	
КонецФункции

Процедура ЗаполнитьВидыЗапасов(Отказ)
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерВременныхТаблиц		= ВременныеТаблицыДанныхДокумента();
	ПерезаполнитьВидыЗапасов	= ЗапасыСервер.ПроверитьНеобходимостьПерезаполненияВидовЗапасовДокумента(ЭтотОбъект);
	
	Если Не Проведен
		Или ПерезаполнитьВидыЗапасов
		Или ПроверитьИзменениеРеквизитовДокумента(МенеджерВременныхТаблиц)
		Или ПроверитьИзменениеТоваров(МенеджерВременныхТаблиц) Тогда
		
		ПараметрыЗаполнения = ПараметрыЗаполненияВидовЗапасов(Истина);
		
		ЗапасыСервер.ЗаполнитьВидыЗапасовПоТоварамОрганизаций(ЭтотОбъект,
																МенеджерВременныхТаблиц,
																Отказ,
																ПараметрыЗаполнения);
		
		ВидыЗапасов.Свернуть("АналитикаУчетаНоменклатуры, ВидЗапасов, НомерГТД", "Количество, КоличествоПоРНПТ");
		
		ЗаполнитьАналитикуПолучателяВидовЗапасов(МенеджерВременныхТаблиц, Отказ);
		
		Если Не Отказ Тогда
			ЗаполнитьВидЗапасовПолучателя();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПараметрыЗаполненияВидовЗапасов(ЗаполнениеВидовЗапасов = Ложь, РежимЗаписи = Неопределено)
	
	ПараметрыЗаполнения = ЗапасыСервер.ПараметрыЗаполненияВидовЗапасов();
	
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзКладовой Тогда
		
		ПараметрыЗаполнения.ВладелецТовараВШапке = Не ЗаполнениеВидовЗапасов;
		ПараметрыЗаполнения.ПриНехваткеТоваровОрганизацииЗаполнятьВидамиЗапасовПоУмолчанию =
			ПраваПользователяПовтИсп.ОтключитьКонтрольПередачиПродукцииИзКладовой();
		ПараметрыЗаполнения.ТаблицаРеквизитовВидовЗапасовПоУмолчанию = ?(ЗаполнениеВидовЗапасов,
																		ТаблицаРеквизитовВидовЗапасовПоУмолчаниюЗаполнение(),
																		ТаблицаРеквизитовВидовЗапасовПоУмолчаниюПроведение());
		
	КонецЕсли;
	
	Если Не ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзКладовой
		И Не ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзПроизводства Тогда
		ПараметрыЗаполнения.ОтборыВидовЗапасов.ТипЗапасов.Добавить(Перечисления.ТипыЗапасов.МатериалДавальца);
	КонецЕсли;
	
	ПараметрыЗаполнения.ОтборыВидовЗапасов.ТипЗапасов.Добавить(Перечисления.ТипыЗапасов.ПолуфабрикатДавальца);
	ПараметрыЗаполнения.ОтборыВидовЗапасов.ТипЗапасов.Добавить(Перечисления.ТипыЗапасов.ПродукцияДавальца);
	
	// Отключаем подбор по интеркампани т.к. передача продукции\материалов по этапу должна быть оформлена только от
	// организации этапа.
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзПроизводства
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратМатериаловИзПроизводства Тогда
		
		Если РежимЗаписи = Неопределено
			Или (РежимЗаписи <> Неопределено
				И РежимЗаписи <> РежимЗаписиДокумента.ОтменаПроведения) Тогда
			ПараметрыЗаполнения.ПодбиратьВидыЗапасовПоИнтеркампани = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	//++ НЕ УТКА
	Если ВнутренняяПереработка
		И (ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаМатериаловВПроизводство
			Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратМатериаловИзПроизводства
			Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзПроизводства) Тогда
		ПараметрыЗаполнения.ОтборыВидовЗапасов.ОрганизацияДавалец = ОрганизацияДавалец;
	КонецЕсли;
	//-- НЕ УТКА
	
	ПараметрыЗаполнения.ДокументДелаетИПриходИРасход = Истина;
	ПараметрыЗаполнения.СторнируемыйДокумент = СторнируемыйДокумент;
	
	Возврат ПараметрыЗаполнения;
	
КонецФункции

Функция ТаблицаРеквизитовВидовЗапасовПоУмолчаниюЗаполнение()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаТоваров.НомерСтроки                КАК НомерСтроки,
		|	ТаблицаТоваров.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	ТаблицаТоваров.Назначение                 КАК Назначение
		|ПОМЕСТИТЬ ВтТаблицаТоваров
		|ИЗ
		|	&ТаблицаТоваров КАК ТаблицаТоваров;
		|
		|ВЫБРАТЬ
		|	ТаблицаТоваров.НомерСтроки                    КАК НомерСтроки,
		|	ТаблицаТоваров.АналитикаУчетаНоменклатуры     КАК АналитикаУчетаНоменклатуры,
		|	&Организация                                  КАК Организация,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)   КАК НомерГТД,
		|	ВЫБОР
		//++ НЕ УТКА
		|		КОГДА СпрНазначения.ТипНазначения В (
		//++ Устарело_Переработка24
		|			ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ДавальческоеПродукция22),
		|			ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ДавальческоеМатериалы22),
		//-- Устарело_Переработка24
		|			ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ДавальческоеМатериалыПодЭтап22),
		|			ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.Давальческое2_5))
		|			ТОГДА СпрНазначения.Партнер
		//-- НЕ УТКА
		|		КОГДА ИСТИНА
		|			ТОГДА &Организация
		|	КОНЕЦ                                         КАК ВладелецТовара,
		|	ВЫБОР
		//++ НЕ УТКА
		|		КОГДА СпрНазначения.Договор ЕСТЬ НЕ NULL
		|			ТОГДА СпрНазначения.Договор.Контрагент
		//-- НЕ УТКА
		|		КОГДА ИСТИНА
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|	КОНЕЦ                                         КАК Контрагент,
		|	СпрНазначения.Договор                         КАК Договор,
		|	ВЫБОР
		//++ НЕ УТКА
		|		КОГДА СпрНазначения.ТипНазначения В (
		//++ Устарело_Переработка24
		|			ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ДавальческоеПродукция22),
		|			ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ДавальческоеМатериалы22),
		//-- Устарело_Переработка24
		|			ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ДавальческоеМатериалыПодЭтап22),
		|			ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.Давальческое2_5))
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПолуфабрикатДавальца)
		//-- НЕ УТКА
		|		КОГДА ИСТИНА
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
		|	КОНЕЦ                                         КАК ТипЗапасов
		|ИЗ
		|	ВтТаблицаТоваров КАК ТаблицаТоваров
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Назначения КАК СпрНазначения
		|	ПО СпрНазначения.Ссылка = ТаблицаТоваров.Назначение
		|";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ТаблицаТоваров", Товары.Выгрузить(, "НомерСтроки, АналитикаУчетаНоменклатуры, Назначение"));
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция ТаблицаРеквизитовВидовЗапасовПоУмолчаниюПроведение()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки	КАК НомерСтроки,
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры		КАК АналитикаУчетаНоменклатуры,
	|	&Организация									КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)	КАК ВидЗапасов,
	|	ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)		КАК НомерГТД,
	|	ВЫБОР
	//++ НЕ УТКА
	|		КОГДА СпрНазначения.ТипНазначения В (
	//++ Устарело_Переработка24
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ДавальческоеПродукция22),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ДавальческоеМатериалы22),
	//-- Устарело_Переработка24
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ДавальческоеМатериалыПодЭтап22),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.Давальческое2_5))
	|			ТОГДА СпрНазначения.Партнер
	//-- НЕ УТКА
	|		КОГДА ИСТИНА
	|			ТОГДА &Организация
	|	КОНЕЦ											КАК ВладелецТовара,
	|	ВЫБОР
	//++ НЕ УТКА
	|		КОГДА СпрНазначения.Договор ЕСТЬ НЕ NULL
	|			ТОГДА СпрНазначения.Договор.Контрагент
	//-- НЕ УТКА
	|		КОГДА ИСТИНА
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|	КОНЕЦ											КАК Контрагент,
	|	СпрНазначения.Договор							КАК Договор,
	|	ВЫБОР
	//++ НЕ УТКА
	|		КОГДА СпрНазначения.ТипНазначения В (
	//++ Устарело_Переработка24
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ДавальческоеПродукция22),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ДавальческоеМатериалы22),
	//-- Устарело_Переработка24
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ДавальческоеМатериалыПодЭтап22),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.Давальческое2_5))
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПолуфабрикатДавальца)
	//-- НЕ УТКА
	|		КОГДА ИСТИНА
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|	КОНЕЦ											КАК ТипЗапасов
	|ИЗ
	|	Документ.ДвижениеПродукцииИМатериалов.Товары КАК ТаблицаТоваров
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Назначения КАК СпрНазначения
	|	ПО СпрНазначения.Ссылка = ТаблицаТоваров.Назначение
	|
	|ГДЕ
	|	ТаблицаТоваров.Ссылка = &Ссылка
	|";
	
	Запрос.УстановитьПараметр("Ссылка",			Ссылка);
	Запрос.УстановитьПараметр("Организация",	Организация);
	
	Возврат ЗапасыСервер.ТаблицаРеквизитовВидовЗапасовПоУмолчаниюЗапросом(Запрос);
	
КонецФункции

Процедура ЗаполнитьВидЗапасовПолучателя() Экспорт
			
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Запасы.НомерСтроки КАК НомерСтроки,
	|	Запасы.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Запасы.ВидЗапасов КАК ВидЗапасов,
	|	Запасы.ГруппаПродукции КАК ГруппаПродукции
	|ПОМЕСТИТЬ Запасы
	|ИЗ
	|	&ВидыЗапасов КАК Запасы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Запасы.НомерСтроки КАК НомерСтроки,
	|	СпрВидыЗапасов.Ссылка КАК ВидЗапасов,
	|	&ОрганизацияПолучатель КАК ОрганизацияПолучательЗапасов,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар) КАК ТипЗапасов,
	|	НЕОПРЕДЕЛЕНО КАК ВладелецТовара,
	|	НЕОПРЕДЕЛЕНО КАК Контрагент,
	|	НЕОПРЕДЕЛЕНО КАК Договор,
	|	НЕОПРЕДЕЛЕНО КАК Соглашение,
	|	НЕОПРЕДЕЛЕНО КАК Валюта,
	|	НЕОПРЕДЕЛЕНО КАК ВидЦены,
	|	&НалогообложениеНДС КАК НалогообложениеНДС,
	|	&НалогообложениеОрганизации КАК НалогообложениеОрганизации,
	|	Ключи.Номенклатура.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	Запасы.ГруппаПродукции КАК ГруппаПродукции,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика) КАК ХозОперацияВидаЗапаса
	|ИЗ
	|	Запасы КАК Запасы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК СпрВидыЗапасов
	|		ПО (СпрВидыЗапасов.Ссылка = Запасы.ВидЗапасов)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Ключи
	|		ПО (Ключи.Ссылка = Запасы.АналитикаУчетаНоменклатуры)
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеПолуфабрикатовМеждуФилиалами)
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Запрос.УстановитьПараметр("ВидыЗапасов", ВидыЗапасов.Выгрузить(, "НомерСтроки, АналитикаУчетаНоменклатуры, ВидЗапасов, ГруппаПродукции"));
	Запрос.УстановитьПараметр("НалогообложениеОрганизации", УчетНДСУП.ПараметрыУчетаПоОрганизации(Организация, Дата).ОсновноеНалогообложениеНДСПродажи);
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ОрганизацияПолучатель", ОрганизацияПолучатель);
	Запрос.УстановитьПараметр("НалогообложениеНДС", НалогообложениеНДС);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ХозяйственнаяОперация", ХозяйственнаяОперация);
	
	ВидыЗапасовПолучателя = Новый Соответствие;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ВидЗапасовПолучателя = Справочники.ВидыЗапасов.ВидЗапасовДокумента(
			Выборка.ОрганизацияПолучательЗапасов,
			Выборка.ХозОперацияВидаЗапаса,
			Выборка);
		ВидыЗапасовПолучателя.Вставить(Выборка.НомерСтроки, ВидЗапасовПолучателя);
	КонецЦикла;
	
	Для Каждого Запас Из ВидыЗапасов Цикл
		ВидЗапасовПолучателя = ВидыЗапасовПолучателя[Запас.НомерСтроки];
		Если Не ЗначениеЗаполнено(ВидЗапасовПолучателя) Тогда
			
			РеквизитыЗапаса = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Запас.ВидЗапасов, "РеализацияЗапасовДругойОрганизации, Организация");
			ВидЗапасовПолучателя = ?(
				РеквизитыЗапаса.РеализацияЗапасовДругойОрганизации,
				Справочники.ВидыЗапасов.ВидЗапасовДокумента(
					РеквизитыЗапаса.Организация,,
					Запас.ВидЗапасов),
				Запас.ВидЗапасов);
		КонецЕсли;
		Запас.ВидЗапасовПолучателя = ВидЗапасовПолучателя;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьАналитикуПолучателяВидовЗапасов(МенеджерВременныхТаблиц, Отказ)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры                КАК АналитикаУчетаНоменклатуры,
	//++ НЕ УТКА
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры.Номенклатура   КАК Номенклатура,
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	//-- НЕ УТКА
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.СтатусУказанияСерийПолучатель = 14
	|			ТОГДА ТаблицаТоваров.Серия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ                                                    КАК Серия,
	|	ТаблицаТоваров.НазначениеПолучателя                      КАК НазначениеПолучателя,
	//++ НЕ УТКА
	|	ЕСТЬNULL(ТаблицаТоваров.НазначениеПолучателя.ТипНазначения,
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ПустаяСсылка))  КАК ТипКорНазначения,
	//-- НЕ УТКА
	|	ТаблицаТоваров.ГруппаПродукции                           КАК ГруппаПродукции,
	|	ТаблицаТоваров.Распоряжение                              КАК Распоряжение,
	|	ТаблицаТоваров.КодСтроки                                 КАК КодСтроки,
	|	ТаблицаТоваров.Упаковка                                  КАК Упаковка,
	|	СУММА(ТаблицаТоваров.Количество)                         КАК Количество,
	|	СУММА(ТаблицаТоваров.КоличествоУпаковок)                 КАК КоличествоУпаковок,
	|	СУММА(ТаблицаТоваров.Сумма)                              КАК Сумма
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.СтатусУказанияСерийПолучатель = 14
	|			ТОГДА ТаблицаТоваров.Серия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаТоваров.НазначениеПолучателя,
	//++ НЕ УТКА
	|	ЕСТЬNULL(ТаблицаТоваров.НазначениеПолучателя.ТипНазначения, ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ПустаяСсылка)),
	//-- НЕ УТКА
	|	ТаблицаТоваров.Распоряжение,
	|	ТаблицаТоваров.КодСтроки,
	|	ТаблицаТоваров.ГруппаПродукции,
	|	ТаблицаТоваров.Упаковка
	|
	|УПОРЯДОЧИТЬ ПО
	|	АналитикаУчетаНоменклатуры,
	|	Количество УБЫВ
	|";
	
	ВыборкаТовары = Запрос.Выполнить().Выбрать();
	
	ОтборТоваров = Новый Структура("АналитикаУчетаНоменклатуры");
	
	ТипыЗапасов =
		ОбщегоНазначения.ЗначениеРеквизитаОбъектов(
			ВидыЗапасов.ВыгрузитьКолонку("ВидЗапасов"),
			"ТипЗапасов");
	
	Пока ВыборкаТовары.Следующий() Цикл
		
		КоличествоТоваров         = ВыборкаТовары.Количество;
		КоличествоУпаковокТоваров = ВыборкаТовары.КоличествоУпаковок;
		СуммаТоваров              = ВыборкаТовары.Сумма;
		
		ЗаполнитьЗначенияСвойств(ОтборТоваров, ВыборкаТовары);
		
		Для Каждого СтрокаЗапасов Из ВидыЗапасов.НайтиСтроки(ОтборТоваров) Цикл
			
			Если СтрокаЗапасов.Количество = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Количество = Мин(КоличествоТоваров, СтрокаЗапасов.Количество);
			
			НоваяСтрока = ВидыЗапасов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаЗапасов);
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаТовары, "Серия, ГруппаПродукции, НазначениеПолучателя, Распоряжение, КодСтроки, Упаковка");
			
			//++ НЕ УТКА
			Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратМатериаловИзПроизводства
				И (ТипыЗапасов[НоваяСтрока.ВидЗапасов] = Перечисления.ТипыЗапасов.МатериалДавальца
					Или ТипыЗапасов[НоваяСтрока.ВидЗапасов] = Перечисления.ТипыЗапасов.ПолуфабрикатДавальца
					Или ТипыЗапасов[НоваяСтрока.ВидЗапасов] = Перечисления.ТипыЗапасов.ПродукцияДавальца)
				//++ Устарело_Переработка24
				И Не ВыборкаТовары.ТипКорНазначения = Перечисления.ТипыНазначений.Давальческое21
				И Не ВыборкаТовары.ТипКорНазначения = Перечисления.ТипыНазначений.ДавальческоеМатериалы22
				И Не ВыборкаТовары.ТипКорНазначения = Перечисления.ТипыНазначений.ДавальческоеПродукция22
				//-- Устарело_Переработка24
				И Не ВыборкаТовары.ТипКорНазначения = Перечисления.ТипыНазначений.ДавальческоеМатериалыПодЭтап22
				И Не ВыборкаТовары.ТипКорНазначения = Перечисления.ТипыНазначений.Давальческое2_5 Тогда
				
				ТекстОшибки = НСтр("ru = 'Экономия давальческого материала ""%1"" на склад ""%2"" должна обосабливаться под давальческое назначение. Необходимо установить соответствующее назначение в строке табличной части распоряжения.';
									|en = 'The savings of the ""%1"" material provided to the ""%2"" warehouse must be assigned for the provider assignment. Set the corresponding assignment in the reference table row.'");
				ТекстОшибки =
					СтрШаблон(
						ТекстОшибки,
						НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
							ВыборкаТовары.Номенклатура,
							ВыборкаТовары.Характеристика,,
							ВыборкаТовары.Серия),
						Получатель);
				
				ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ВыборкаТовары.Распоряжение, "ЭкономияМатериалов",, Отказ);
				
				Продолжить;
				
			КонецЕсли;
			//-- НЕ УТКА
			
			НоваяСтрока.Количество			= Количество;
			НоваяСтрока.КоличествоПоРНПТ	= Количество * СтрокаЗапасов.КоличествоПоРНПТ / СтрокаЗапасов.Количество;
			НоваяСтрока.КоличествоУпаковок	= Количество * КоличествоУпаковокТоваров / КоличествоТоваров;
			НоваяСтрока.Сумма				= Количество * СуммаТоваров / КоличествоТоваров;
			
			СтрокаЗапасов.Количество		= СтрокаЗапасов.Количество - НоваяСтрока.Количество;
			СтрокаЗапасов.КоличествоПоРНПТ	= СтрокаЗапасов.КоличествоПоРНПТ - НоваяСтрока.КоличествоПоРНПТ;
			
			КоличествоТоваров = КоличествоТоваров - НоваяСтрока.Количество;
			КоличествоУпаковокТоваров = КоличествоУпаковокТоваров - НоваяСтрока.КоличествоУпаковок;
			СуммаТоваров = СуммаТоваров - НоваяСтрока.Сумма;
			
			Если КоличествоТоваров = 0 Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ПараметрыПоиска = Новый Структура("Количество", 0);
	МассивУдаляемыхСтрок = ВидыЗапасов.НайтиСтроки(ПараметрыПоиска);
	
	Для Каждого СтрокаТаблицы Из МассивУдаляемыхСтрок Цикл
		ВидыЗапасов.Удалить(СтрокаТаблицы);
	КонецЦикла;
	
КонецПроцедуры

// Заполняет аналитики учета номенклатуры в табличных частях документа, хранящих информацию о товарах.
// Если параметр не передан, тогда будет выполнено заполнение данных в табличных частях документа.
//
// Параметры:
//	ТаблицыДокумента - см. Документы.ДвижениеПродукцииИМатериалов.КоллекцияТабличныхЧастейТоваров.
//
Процедура ЗаполнитьАналитикиУчетаНоменклатурыВТабличныхЧастяхТоваров(ТаблицыДокумента = Неопределено)
	
	Если ТаблицыДокумента = Неопределено Тогда
		ТаблицыДокумента = Документы.ДвижениеПродукцииИМатериалов.КоллекцияТабличныхЧастейТоваров();
		
		ЗаполнитьЗначенияСвойств(ТаблицыДокумента, ЭтотОбъект);
	КонецЕсли;
	
	ТаблицаТовары = ТаблицыДокумента.Товары;
	
	МестаУчета = РегистрыСведений.АналитикаУчетаНоменклатуры.МестаУчета(ХозяйственнаяОперация,
																		Отправитель,
																		Получатель,
																		Неопределено);
	ИменаПолей = РегистрыСведений.АналитикаУчетаНоменклатуры.ИменаПолейКоллекцииПоУмолчанию();
	ИменаПолей.Вставить("СтатусУказанияСерий", "СтатусУказанияСерийОтправитель");
	ИменаПолей.Вставить("Назначение", "НазначениеОтправителя");
	
	РегистрыСведений.АналитикаУчетаНоменклатуры.ЗаполнитьВКоллекции(ТаблицаТовары, МестаУчета, ИменаПолей);
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкаЗаполнения

Процедура ПроверитьРеквизитыШапки(ПредставлениеПолей, Отказ)

	Если НЕ ЗначениеЗаполнено(Отправитель) Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Поле ""%1"" не заполнено';
										|en = '""%1"" is required'"), ПредставлениеПолей.Отправитель);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "Отправитель",, Отказ); 
	КонецЕсли; 
	Если НЕ ЗначениеЗаполнено(Получатель) Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Поле ""%1"" не заполнено';
										|en = '""%1"" is required'"), ПредставлениеПолей.Получатель);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "Получатель",, Отказ); 
	КонецЕсли; 
	Если Товары.Количество() = 0 Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Не введено ни одной строки в список ""%1""';
										|en = 'No line is entered into the ""%1"" list'"), ПредставлениеПолей.Товары);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "Товары",, Отказ); 
	КонецЕсли;
	
	Если Отправитель = Получатель И ЗначениеЗаполнено(Отправитель)
		И Не ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеремещениеПолуфабрикатовМеждуФилиалами Тогда
		Если ПредставлениеПолей.ТипОтправителя = "Склад" 
				И ПредставлениеПолей.ТипПолучателя = "Кладовая"
			ИЛИ ПредставлениеПолей.ТипОтправителя = "Кладовая" 
				И ПредставлениеПолей.ТипПолучателя = "Склад" Тогда
			ТекстСообщения = НСтр("ru = 'Склад и цеховая кладовая должны быть разными';
									|en = 'Warehouse and shop floor stockroom must be different'");
		Иначе
			ТекстСообщения = НСтр("ru = 'Отправитель и получатель должны быть разными';
									|en = 'Sender and recipient should be different'");
		КонецЕсли;
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "Отправитель",, Отказ); 
	КонецЕсли;
	
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзПроизводства
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзКладовой Тогда
		ЗакупкиСервер.ПроверитьКорректностьЗаполненияДокументаЗакупки(ЭтотОбъект,Отказ);
	КонецЕсли;
	
	//++ НЕ УТКА
	Если ВнутренняяПереработка Тогда
		
		Если ЗначениеЗаполнено(ОрганизацияДавалец)
			И ОрганизацияДавалец = Организация Тогда
			
			ТекстСообщения = НСтр("ru = 'Организация-давалец совпадает с организацией-переработчиком.';
									|en = 'Material provider company and the subcontractor company are the same.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ОрганизацияДавалец",, Отказ);
			
		КонецЕсли;
	
		Если ЗначениеЗаполнено(ОрганизацияДавалец)
			И Не ОрганизацияДавалец = Организация
			И Справочники.Организации.ОрганизацииВзаимосвязаныПоОрганизационнойСтруктуре(Организация, ОрганизацияДавалец) Тогда
			
			ТекстСообщения = НСтр("ru = 'Организация-давалец не должна быть взаимосвязана с организацией-переработчиком по организационной структуре.';
									|en = 'Material provider company must not be interrelated to the subcontractor company by the organizational structure.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ОрганизацияДавалец",, Отказ);
			
		КонецЕсли;
		
		СтруктураПроверки = Справочники.Организации.СтраныРегистрацииИВалютыРегламентированногоУчетаСовпадают(Организация, ОрганизацияДавалец);
		
		Если Не СтруктураПроверки.ВалютыСовпадают Тогда
			ТекстОшибки = НСтр("ru = 'Валюты регламентированного учета ""%1"" и ""%2"" должны совпадать.';
								|en = 'The %1 and %2 local accounting currencies must be the same.'");
			ТекстОшибки = СтрШаблон(ТекстОшибки, Организация, ОрганизацияДавалец);
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ЭтотОбъект, "ОрганизацияДавалец",, Отказ);
		КонецЕсли;
		
		Если Не СтруктураПроверки.СтраныСовпадают Тогда
			ТекстОшибки = НСтр("ru = 'Страны регистрации ""%1"" и ""%2"" должны совпадать.';
								|en = 'The %1 and %2 countries of residence must be the same.'");
			ТекстОшибки = СтрШаблон(ТекстОшибки, Организация, ОрганизацияДавалец);
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ЭтотОбъект, "ОрганизацияДавалец",, Отказ);
		КонецЕсли;
		
	КонецЕсли;
	//-- НЕ УТКА
	
КонецПроцедуры

Процедура ПроверитьТовары(ПредставлениеПолей, Отказ)
	
	ПроверятьКодСтроки = ПоРаспоряжениям
		И (ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзПроизводства
			Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратМатериаловИзПроизводства
			Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаМатериаловВПроизводство);
	
	ШаблонСообщения = НСтр("ru = 'Не допускается превышение запланированного количества товара по этапу в строке %1 списка ""%2"".';
							|en = 'Cannot exceed the planned goods quantity for the stage in line %1 of the ""%2"" list.'");
	ШаблонСообщенияПустоеРаспоряжение = НСтр("ru = 'В строке %1 списка ""%2"" не заполнено распоряжение.';
											|en = 'Reference in line %1 of list ""%2"" is not filled in.'");
	
	Для Каждого ДанныеСтроки Из Товары Цикл
		
		Если ПроверятьКодСтроки
			И ДанныеСтроки.КодСтроки = 0
			И ЗначениеЗаполнено(ДанныеСтроки.Распоряжение) Тогда
			
			ТекстСообщения = СтрШаблон(ШаблонСообщения, Формат(ДанныеСтроки.НомерСтроки, "ЧГ="), ПредставлениеПолей.Товары);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", ДанныеСтроки.НомерСтроки, "Номенклатура");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "", Отказ);
			
		КонецЕсли;
		
		Если ПоРаспоряжениям
			И Не ЗначениеЗаполнено(Распоряжение)
			И Не ЗначениеЗаполнено(ДанныеСтроки.Распоряжение) Тогда
			
			ТекстСообщения = СтрШаблон(ШаблонСообщенияПустоеРаспоряжение, Формат(ДанныеСтроки.НомерСтроки, "ЧГ="), ПредставлениеПолей.Товары);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", ДанныеСтроки.НомерСтроки, "Распоряжение");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "", Отказ);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

//++ НЕ УТКА

Процедура ПроверитьСоответствиеНакладнойИЭтапов(СинонимТабличнойЧасти, Отказ)
	
	Запрос = Новый Запрос();
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СтрокиТабЧасти.Распоряжение КАК Распоряжение,
		|	СтрокиТабЧасти.НомерСтроки  КАК НомерСтроки
		|ПОМЕСТИТЬ ВТСтрокиТабЧасти
		|ИЗ
		|	&СтрокиТабЧасти КАК СтрокиТабЧасти
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Распоряжение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СтрокиТабЧасти.НомерСтроки                                         КАК НомерСтроки,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(СтрокиТабЧасти.Распоряжение)                   КАК Распоряжение,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(МАКСИМУМ(ЭтапПроизводства.ОрганизацияДавалец)) КАК ОрганизацияДавалец,
		|	МАКСИМУМ(НЕ ЭтапПроизводства.ВнутренняяПереработка = &ВнутренняяПереработка) КАК ОшибкаВнутренняяПереработка,
		|	МАКСИМУМ(НЕ ЭтапПроизводства.ОрганизацияДавалец = &ОрганизацияДавалец)       КАК ОшибкаОрганизацияДавалец
		|ИЗ
		|	ВТСтрокиТабЧасти КАК СтрокиТабЧасти
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства
		|	ПО ЭтапПроизводства.Ссылка = СтрокиТабЧасти.Распоряжение
		|
		|ГДЕ
		|	&ВнутренняяПереработка
		|		И НЕ ЭтапПроизводства.ВнутренняяПереработка
		|	ИЛИ НЕ &ОрганизацияДавалец = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|		И НЕ ЭтапПроизводства.ОрганизацияДавалец = &ОрганизацияДавалец
		|
		|СГРУППИРОВАТЬ ПО
		|	СтрокиТабЧасти.НомерСтроки,
		|	СтрокиТабЧасти.Распоряжение
		|";
	
	Запрос.УстановитьПараметр("ВнутренняяПереработка", ВнутренняяПереработка);
	Запрос.УстановитьПараметр("ОрганизацияДавалец",    ОрганизацияДавалец);
	Запрос.УстановитьПараметр("СтрокиТабЧасти",        Товары.Выгрузить(, "НомерСтроки, Распоряжение"));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", Выборка.НомерСтроки, "Распоряжение");
		
		Если Выборка.ОшибкаВнутренняяПереработка Тогда
			
			ШаблонСообщения = НСтр("ru = 'Флаг внутренней переработки документа не соотвествует распоряжению ""%1"" в строке ""%2"" списка ""%3"".';
									|en = 'The internal document subcontracting flag does not correspond to the ""%1"" reference in line %2 of the ""%3"" list.'");
			
			ТекстСообщения = СтрШаблон(
				ШаблонСообщения,
				Выборка.Распоряжение,
				Формат(Выборка.НомерСтроки, "ЧГ="),
				СинонимТабличнойЧасти);
				
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "", Отказ);
			
		КонецЕсли;
		
		Если Выборка.ОшибкаОрганизацияДавалец Тогда
			
			ШаблонСообщения = НСтр("ru = 'Организация-давалец документа ""%1"" не соотвествует организации-давальцу ""%2"", указанной в распоряжении ""%3"" в строке ""%4"" списка ""%5"".';
									|en = 'The material provider company of the ""%1"" document does not correspond to the ""%2"" material provider company specified in the ""%3"" reference in line %4 of the ""%5"" list.'");
			
			ТекстСообщения = СтрШаблон(
				ШаблонСообщения,
				ОрганизацияДавалец,
				Выборка.ОрганизацияДавалец,
				Выборка.Распоряжение,
				Формат(Выборка.НомерСтроки, "ЧГ="),
				СинонимТабличнойЧасти);
				
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "", Отказ);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры
//-- НЕ УТКА

Процедура ОтключитьПроверкуЗаполненияПоХозяйственнойОперации(МассивНепроверяемыхРеквизитов)

	МассивВсехРеквизитов = Новый Массив;
	МассивРеквизитовОперации = Новый Массив;
	
	Документы.ДвижениеПродукцииИМатериалов.ЗаполнитьИменаРеквизитовПоХозяйственнойОперации(
		ХозяйственнаяОперация,
		МассивВсехРеквизитов,
		МассивРеквизитовОперации);
	
	Для Каждого ЭлементМассива Из МассивВсехРеквизитов Цикл
		
		// Если реквизит не используется для хозяйственной операции, исключаем данный реквизит из проверки.
		Если МассивРеквизитовОперации.Найти(ЭлементМассива) = Неопределено Тогда
			МассивНепроверяемыхРеквизитов.Добавить(ЭлементМассива);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

// Параметры:
// 	Таблица - см. УправлениеДоступом.ТаблицаНаборыЗначенийДоступа
//
Процедура ЗаполнитьНаборыЗначенийДоступа(Таблица) Экспорт
	
	СтрокаТаб = Таблица.Добавить();
	СтрокаТаб.ЗначениеДоступа = Организация;
	
	СтрокаТаб = Таблица.Добавить();
	СтрокаТаб.ЗначениеДоступа = Отправитель;
	
	СтрокаТаб = Таблица.Добавить();
	СтрокаТаб.ЗначениеДоступа = Получатель;
	
КонецПроцедуры

// Функция проверят изменение табличной части "Товары" относительно табличной части "Виды запасов" документа.
//
// Параметры:
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц
//
// Возвращаемое значение:
//	Булево - Истина - товары изменены
//           Ложь - товары не изменены.
//
Функция ПроверитьИзменениеТоваров(МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
	|ИЗ (
	|	ВЫБРАТЬ
	|		ТаблицаТоваров.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ТаблицаТоваров.Назначение КАК Назначение,
	|		ТаблицаТоваров.ГруппаПродукции КАК ГруппаПродукции,
	|		ТаблицаТоваров.Распоряжение КАК Распоряжение,
	|		ТаблицаТоваров.КодСтроки КАК КодСтроки,
	|		ТаблицаТоваров.Упаковка КАК Упаковка,
	|		(ВЫБОР ТаблицаТоваров.СтатусУказанияСерийПолучатель
	|			КОГДА 14 ТОГДА ТаблицаТоваров.Серия
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КОНЕЦ) КАК Серия,
	|		ТаблицаТоваров.Количество КАК Количество,
	|		ТаблицаТоваров.КоличествоУпаковок КАК КоличествоУпаковок,
	|		ТаблицаТоваров.Сумма КАК Сумма
	|	ИЗ
	|		ТаблицаТоваров КАК ТаблицаТоваров
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры.Назначение КАК Назначение,
	|		ТаблицаВидыЗапасов.ГруппаПродукции КАК ГруппаПродукции,
	|		ТаблицаВидыЗапасов.Распоряжение КАК Распоряжение,
	|		ТаблицаВидыЗапасов.КодСтроки КАК КодСтроки,
	|		ТаблицаВидыЗапасов.Упаковка КАК Упаковка,
	|		ТаблицаВидыЗапасов.СерияПолучатель КАК Серия,
	|		-ТаблицаВидыЗапасов.Количество КАК Количество,
	|		-ТаблицаВидыЗапасов.КоличествоУпаковок КАК КоличествоУпаковок,
	|		-ТаблицаВидыЗапасов.Сумма КАК Сумма
	|	ИЗ
	|		ТаблицаВидыЗапасов КАК ТаблицаВидыЗапасов
	|
	|	) КАК ТаблицаТоваров
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры,
	|	ТаблицаТоваров.Назначение,
	|	ТаблицаТоваров.ГруппаПродукции,
	|	ТаблицаТоваров.Распоряжение,
	|	ТаблицаТоваров.КодСтроки,
	|	ТаблицаТоваров.Упаковка,
	|	ТаблицаТоваров.Серия
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаТоваров.Количество) <> 0
	|	ИЛИ СУММА(ТаблицаТоваров.КоличествоУпаковок) <> 0
	|	ИЛИ СУММА(ТаблицаТоваров.Сумма) <> 0
	|");
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;

	РезультатЗапрос = Запрос.Выполнить();
	
	Возврат (Не РезультатЗапрос.Пустой());
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
