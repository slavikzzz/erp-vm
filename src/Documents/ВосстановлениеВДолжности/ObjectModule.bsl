#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ДатаВосстановления = ТекущаяДатаСеанса();
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Сотрудники")
		Или ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.Увольнение") Тогда
		
		Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Сотрудники") Тогда
			Сотрудник = ДанныеЗаполнения;
		Иначе
			
			ДокументУвольнения = ДанныеЗаполнения;
			ДанныеДокументаУвольнение = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, "Сотрудник");
			Сотрудник = ДанныеДокументаУвольнение.Сотрудник;
			
		КонецЕсли;
		
		УстановитьПривилегированныйРежим(Истина);
		Документы.ВосстановлениеВДолжности.ЗаполнитьОбъект(ЭтотОбъект, Сотрудник, ТекущаяДатаСеанса(), ДокументУвольнения);
		УстановитьПривилегированныйРежим(Ложь);
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		
		Если ДанныеЗаполнения.Свойство("Действие") И ДанныеЗаполнения.Действие = "Исправить" Тогда
			
			ИсправлениеДокументовЗарплатаКадры.СкопироватьДокумент(ЭтотОбъект, ДанныеЗаполнения.Ссылка);
			
			ИсправленныйДокумент = ДанныеЗаполнения.Ссылка;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.КадровыеРешения") Тогда
		МодульКадровыеРешения = ОбщегоНазначения.ОбщийМодуль("КадровыеРешения");
		МодульКадровыеРешения.ОбработкаЗаполненияКадровогоПриказа(ЭтотОбъект, ДанныеЗаполнения, СтандартнаяОбработка);
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) <> Тип("Структура") Или Не ДанныеЗаполнения.Свойство("ЕжегодныеОтпуска") Тогда
		
		Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
			
			Если ДанныеЗаполнения.Свойство("Организация") Тогда
				Организация = ДанныеЗаполнения.Организация;
			КонецЕсли;
			
			Если ДанныеЗаполнения.Свойство("Сотрудник") Тогда
				Сотрудник = ДанныеЗаполнения.Сотрудник;
			КонецЕсли;
			
			Если ДанныеЗаполнения.Свойство("ДатаВосстановления") Тогда
				ДатаВосстановления = ДанныеЗаполнения.ДатаВосстановления;
			КонецЕсли;
			
			Если ДанныеЗаполнения.Свойство("ДолжностьПоШтатномуРасписанию") Тогда
				ДолжностьПоШтатномуРасписанию = ДанныеЗаполнения.ДолжностьПоШтатномуРасписанию;
			КонецЕсли;
			
			Если ДанныеЗаполнения.Свойство("Подразделение") Тогда
				Подразделение = ДанныеЗаполнения.Подразделение;
			КонецЕсли;
			
			Если ДанныеЗаполнения.Свойство("Должность") Тогда
				Должность = ДанныеЗаполнения.Должность;
			КонецЕсли;
			
		КонецЕсли;
		
		ДанныеДокумента = ОстаткиОтпусков.ОписаниеПараметровДанныхКадровогоДокумента();
		ДанныеДокумента.Регистратор = Ссылка;
		ДанныеДокумента.Сотрудник = Сотрудник;
		ДанныеДокумента.ДатаСобытия = ДатаВосстановления;
		
		ДанныеНовойПозиции = ОстаткиОтпусков.ОписаниеПараметровДанныхПозиции();
		ДанныеНовойПозиции.ДолжностьПоШтатномуРасписанию = ДолжностьПоШтатномуРасписанию;
		ДанныеНовойПозиции.Подразделение = Подразделение;
		ДанныеНовойПозиции.Должность = Должность;
		ДанныеНовойПозиции.Территория = Территория;
		
		ОстаткиОтпусков.ЗаполнитьПоложеннымиПравамиСотрудника(ЕжегодныеОтпуска, ДанныеДокумента, ДанныеНовойПозиции);
		
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ГосударственнаяСлужба");
		Модуль.ОбработкаЗаполненияДокументаПриемНаРаботу(ЭтотОбъект, ДанныеЗаполнения, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
		
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект, , , ЗначениеЗаполнено(ИсправленныйДокумент));
	ЗарплатаКадрыРасширенный.ИнициализироватьОтложеннуюРегистрациюВторичныхДанныхПоДвижениямДокумента(Движения);
	
	ИсправлениеДокументовЗарплатаКадры.ПриПроведенииИсправления(Ссылка, Движения, РежимПроведения, Отказ,,, ЭтотОбъект, "ДатаВосстановления");
	
	// ЗарплатаКадрыПодсистемы.КадровыйУчет.ЭлектронныеТрудовыеКнижки
	ЭлектронныеТрудовыеКнижкиРасширенный.ПриИсправленииТрудовойДеятельности(ДокументУвольнения);
	// Конец ЗарплатаКадрыПодсистемы.КадровыйУчет.ЭлектронныеТрудовыеКнижки
	
	Если ДополнительныеСвойства.Свойство("ВосстановитьДвиженияУвольнения") Тогда
		
		ИсправлениеПериодическихСведений.ОтменитьИсправления(
			Ссылка, ДополнительныеСвойства.ВосстановитьДвиженияУвольнения, ИменаСторнируемыхРегистровДокументаУвольнения());
		
	КонецЕсли;
	
	// Сторнирование движений приказа об увольнении
	Если ДополнительныеСвойства.Свойство("ИсправлятьДвиженияУвольнения") Тогда
		
		ИсправлениеПериодическихСведений.ЗафиксироватьИсправления(
			Ссылка, ДокументУвольнения, ИменаСторнируемыхРегистровДокументаУвольнения());
		
	КонецЕсли;
	
	ДанныеДляПроведения = ДанныеДляПроведения();
	
	ЗарплатаКадрыРасширенный.УстановитьВремяРегистрацииДокумента(
		Движения, ДанныеДляПроведения.СотрудникиДаты, Ссылка);
	
	КадровыйУчет.СформироватьДвиженияВидовЗанятостиСотрудников(Движения, ДанныеДляПроведения.ДвиженияВидовЗанятости);
	КадровыйУчет.СформироватьКадровыеДвижения(ЭтотОбъект, Движения, ДанныеДляПроведения.КадровыеДвижения);
	
	КадровыйУчетРасширенный.СформироватьДвиженияЗанятостьПозицийШтатногоРасписания(
		Движения, ДанныеДляПроведения.ЗанятостьПозицийШтатногоРасписания);
	
	КадровыйУчетРасширенный.СформироватьИсториюИзмененияГрафиков(Движения, ДанныеДляПроведения.КадровыеДвижения);
	РазрядыКатегорииДолжностей.СформироватьДвиженияРазрядовКатегорийСотрудников(Движения, ДанныеДляПроведения.РазрядыКатегорииСотрудников);
	РазрядыКатегорииДолжностей.СформироватьДвиженияПКУСотрудников(Движения, ДанныеДляПроведения.ПКУСотрудников);
	
	Если НачисленияУтверждены Тогда
		
		СтруктураПлановыхНачислений = Новый Структура;
		СтруктураПлановыхНачислений.Вставить("ДанныеОПлановыхНачислениях", ДанныеДляПроведения.ПлановыеНачисления);
		СтруктураПлановыхНачислений.Вставить("ЗначенияПоказателей", ДанныеДляПроведения.ЗначенияПоказателейНачислений);
		СтруктураПлановыхНачислений.Вставить("ПрименениеДополнительныхПоказателей", ДанныеДляПроведения.ПрименениеДополнительныхПоказателей);
		
		РасчетЗарплаты.СформироватьДвиженияПлановыхНачислений(ЭтотОбъект, Движения, СтруктураПлановыхНачислений, Ложь);
		РасчетЗарплаты.СформироватьДвиженияПлановыхВыплат(Движения, ДанныеДляПроведения.КадровыеДвижения);
		РасчетЗарплатыРасширенный.СформироватьДвиженияПорядкаПересчетаТарифныхСтавок(Движения, ДанныеДляПроведения.ПорядокПересчетаТарифнойСтавки);
		
		РасчетЗарплатыРасширенный.СформироватьДвиженияПримененияПлановыхНачислений(Движения, ДанныеДляПроведения.ПрименениеНачислений);
		
		Движения.ПрименениеПлановыхНачислений.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
		
		ОтражениеЗарплатыВБухучетеРасширенный.СформироватьДвиженияБухучетНачисленийСотрудников(
			ЭтотОбъект, Движения, ДанныеДляПроведения.БухучетНачислений);
		
	КонецЕсли;
	
	ОстаткиОтпусков.СформироватьДвиженияПоложенныхЕжегодныхОтпусков(Ссылка, Движения, ДанныеДляПроведения.ЕжегодныеОтпуска);
	
	СостоянияСотрудников.ЗарегистрироватьСостоянияСотрудников(Движения, Ссылка, ДанныеДляПроведения.ДанныеСостояний);
	
	КонтрактыДоговорыСотрудников.СформироватьДвиженияОсновныхДанныхИСведенийОКонтрактахДоговорах(
		Движения, ДанныеДляПроведения.СведенияОКонтрактахДоговорах, Ложь);
	
	УчетСтажаПФР.ЗарегистрироватьПериодыВУчетеСтажаПФР(Движения, ДанныеДляПроведения.ДанныеДляРегистрацииВУчетаСтажаПФР);
	
	КадровыйУчетРасширенный.СформироватьДвиженияПоТерриториям(Движения, ДанныеДляПроведения.ТерриторииСотрудников);
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.Грейды") Тогда 
		Модуль = ОбщегоНазначения.ОбщийМодуль("Грейды");
		Модуль.СформироватьДвиженияГрейдовСотрудников(Движения, ДанныеДляПроведения.ДанныеГрейдовСотрудников);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба.КлассныеЧиныРанги") Тогда 
		Модуль = ОбщегоНазначения.ОбщийМодуль("КлассныеЧиныРанги");
		Модуль.СформироватьДвиженияКлассныхЧиновРанговФизическихЛиц(Движения, ДанныеДляПроведения.КлассныеЧиныРангиФизическихЛиц);
	КонецЕсли; 
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.РабочиеМестаОхраныТруда") Тогда
		МодульРабочиеМестаОхраныТруда = ОбщегоНазначения.ОбщийМодуль("РабочиеМестаОхраныТруда");
		МодульРабочиеМестаОхраныТруда.СформироватьДвиженияПоРабочимМестам(Движения, ДанныеДляПроведения);
	КонецЕсли;
	
	ОтражениеЗарплатыВБухучетеРасширенный.СформироватьДвиженияБухучетСотрудников(
		ЭтотОбъект, Движения, ДанныеДляПроведения.НастройкиБухучета);
		
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЗначениеЗаполнено(ДокументУвольнения) И Не ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументУвольнения, "Проведен") Тогда
		
		ТекстСообщения = НСтр("ru = 'Не проведен приказ об увольнении';
								|en = 'Termination of employment order is not posted'") + " - " + Строка(ДокументУвольнения);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "", ,Отказ);
		
	КонецЕсли;
	
	ЗарплатаКадры.ПроверитьКорректностьДаты(Ссылка, ДатаВосстановления, "Объект.ДатаВосстановления", Отказ, НСтр("ru = 'Дата восстановления в должности';
																												|en = 'Reinstatement date'"), ДатаУвольнения, НСтр("ru = 'даты увольнения';
																																												|en = 'termination of employment dates'") , Ложь);
	
	Сотрудники = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудник);
	ПроверяемыеСтруктуры = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ЭтотОбъект);
	
	ПроверитьСоответствиеПозицииШРПодразделению(Отказ, ПроверяемыеРеквизиты);
	
	ИсправлениеДокументовЗарплатаКадры.ПроверитьЗаполнение(ЭтотОбъект, ПроверяемыеРеквизиты, Отказ, "ПериодическиеСведения");
	
	ПроверяемыйРеквизитЕжегодныеОтпуска = ПроверяемыеРеквизиты.Найти("ЕжегодныеОтпуска");
	Если ПроверяемыйРеквизитЕжегодныеОтпуска <> Неопределено Тогда
		ПроверяемыеРеквизиты.Удалить(ПроверяемыйРеквизитЕжегодныеОтпуска);
	КонецЕсли;
	
	// проверка КоличествоДнейВГод
	МассивНепроверяемыхРеквизитов = Новый Массив;
	Для каждого Отпуск Из ЕжегодныеОтпуска Цикл
		Если НЕ ЗначениеЗаполнено(Отпуск.КоличествоДнейВГод) И НЕ ОстаткиОтпусков.ЭтоСтажевыйОтпуск(Отпуск.ВидЕжегодногоОтпуска) Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не заполнено количество дней в год для отпуска %1';
					|en = 'Number of days per year for leave %1 is not entered'"), Отпуск.ВидЕжегодногоОтпуска);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "", ,Отказ);
		КонецЕсли;
	КонецЦикла;
	МассивНепроверяемыхРеквизитов.Добавить("ЕжегодныеОтпуска.КоличествоДнейВГод");
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	СотрудникиДаты = Новый ТаблицаЗначений;
	СотрудникиДаты.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	СотрудникиДаты.Колонки.Добавить("ДатаСобытия", Новый ОписаниеТипов("Дата"));
	СотрудникиДаты.Колонки.Добавить("ВидСобытия", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыКадровыхСобытий"));
	
	Для Каждого ПроверяемаяСтруктура Из ПроверяемыеСтруктуры Цикл
		СтрокаСотрудникиДаты = СотрудникиДаты.Добавить();
		СтрокаСотрудникиДаты.Сотрудник = ПроверяемаяСтруктура.Сотрудник;
		СтрокаСотрудникиДаты.ВидСобытия = Перечисления.ВидыКадровыхСобытий.ВосстановлениеВДолжности;
		СтрокаСотрудникиДаты.ДатаСобытия = ПроверяемаяСтруктура.ДатаВосстановления;
	КонецЦикла;
	
	ВремяРегистрацииДокумента = ЗарплатаКадрыРасширенный.ЗначенияВремениРегистрацииДокумента(Ссылка, СотрудникиДаты);
	
	Для Каждого СтрокаСотрудника Из СотрудникиДаты Цикл
		ВремяРегистрацииСотрудников = ВремяРегистрацииДокумента.Получить(СтрокаСотрудника.ДатаСобытия);
		Если ВремяРегистрацииСотрудников <> Неопределено Тогда 
			СтрокаСотрудника.ДатаСобытия = ВремяРегистрацииСотрудников.Получить(СтрокаСотрудника.Сотрудник);
		КонецЕсли;
	КонецЦикла;
	
	ДокументыДляИсключения = Новый Массив;
	ДокументыДляИсключения.Добавить(Ссылка);
	ДокументыДляИсключения.Добавить(ДокументУвольнения);
	ДокументыДляИсключения.Добавить(ИсправленныйДокумент);
	
	КадровыйУчет.ПроверитьВозможностьПроведенияПоКадровомуУчетуТаблицыСотрудников(СотрудникиДаты, ДокументыДляИсключения, Отказ);
	
	СтруктурыПроверяемыхКонфликтовВидовЗанятости = Новый Массив;
	Для каждого ПроверяемаяСтруктура Из ПроверяемыеСтруктуры Цикл
		
		СтруктураПроверки = СотрудникиФормыРасширенный.ПустаяСтруктураДляПроверкиКонфликтовВидовЗанятостиСотрудников();
		ЗаполнитьЗначенияСвойств(СтруктураПроверки, ПроверяемаяСтруктура);
		СтруктураПроверки.ДатаСобытия = ПроверяемаяСтруктура.ДатаВосстановления;
		
		СтруктурыПроверяемыхКонфликтовВидовЗанятости.Добавить(СтруктураПроверки);
		
	КонецЦикла;
	
	ДокументыДляИсключения = Новый Массив;
	ДокументыДляИсключения.Добавить(Ссылка);
	Если ЗначениеЗаполнено(ИсправленныйДокумент) Тогда
		ДокументыДляИсключения.Добавить(ИсправленныйДокумент);
	КонецЕсли;
	
	ИсключаемыеИзПроверкиСотрудники = КадровыйУчетРасширенный.ВнутренниеСовместителиОсновныхСотрудников(
		СтруктурыПроверяемыхКонфликтовВидовЗанятости, Организация, ДокументыДляИсключения);
	
	СообщениеОКонфликтах = СотрудникиФормыРасширенный.СообщениеОКонфликтахВидовЗанятостиСотрудников(
		СтруктурыПроверяемыхКонфликтовВидовЗанятости, Организация, ДокументыДляИсключения, ИсключаемыеИзПроверкиСотрудники);
	
	Для каждого КонфликтыСотрудника Из СообщениеОКонфликтах Цикл
		ОбщегоНазначения.СообщитьПользователю(КонфликтыСотрудника.Значение, Ссылка, "ВидЗанятости", "Объект", Отказ);
	КонецЦикла;
	
	Если НЕ Отказ Тогда
		
		ДатаПроверки = ДатаВосстановления;
		
		КадровыйУчетРасширенный.ПроверкаСпискаНачисленийКадровогоДокумента(
			ЭтотОбъект, ДатаПроверки, "Начисления,Льготы", "Показатели", Отказ, , , "Начисление,Льгота");
		
		КадровыйУчет.ПроверитьСоответствиеСотрудниковОрганизации(Организация, Сотрудник, Отказ);
		
	КонецЕсли;
	
	Если НачисленияУтверждены Тогда 
		
		РасчетЗарплатыРасширенный.ПроверитьМножественностьОплатыВремениРаботникВШапке(
			ДатаВосстановления, Сотрудник, Начисления, Ссылка, Отказ, Перечисления.ДействияСНачислениямиИУдержаниями.Утвердить, ИсправленныйДокумент);		
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ГосударственнаяСлужба");
		Модуль.ПроверкаЗаполненияДокументаПриемНаРаботу(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	Иначе
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ВидДоговора");
	КонецЕсли;
	
	КадровыйЭДО.ПроверитьВозможностьСохраненияИзмененийДокументаСПечатнымиФормами(
		ЭтотОбъект, Метаданные.Роли.ДобавлениеИзменениеДанныхДляНачисленияЗарплатыРасширенная, Отказ);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ИсправлениеПериодическихСведений.ОтменитьИсправления(
		Ссылка, ДокументУвольнения, ИменаСторнируемыхРегистровДокументаУвольнения());
		
	ПроведениеСервер.ПодготовитьНаборыЗаписейКУдалениюПроведения(ЭтотОбъект, ЗначениеЗаполнено(ИсправленныйДокумент));
	
	// ЗарплатаКадрыПодсистемы.КадровыйУчет.ЭлектронныеТрудовыеКнижки
	ЭлектронныеТрудовыеКнижкиРасширенный.ОтменитьИсправлениеДокумента(ДокументУвольнения, ЭтотОбъект);
	// Конец ЗарплатаКадрыПодсистемы.КадровыйУчет.ЭлектронныеТрудовыеКнижки
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Документы.ВосстановлениеВДолжности.ЗаполнитьДатуЗапретаРедактирования(ЭтотОбъект);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьШтатноеРасписание") Тогда
		ДолжностьПозиции = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДолжностьПоШтатномуРасписанию, "Должность");
		Если Должность <> ДолжностьПозиции Тогда
			Должность = ДолжностьПозиции;
		КонецЕсли;
	Иначе
		Если ЗначениеЗаполнено(ДолжностьПоШтатномуРасписанию) Тогда
			ДолжностьПоШтатномуРасписанию = Справочники.ШтатноеРасписание.ПустаяСсылка();
		КонецЕсли;
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		Если Проведен Тогда
			
			ДокументУвольненияПредыдущий = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ДокументУвольнения");
			Если ДокументУвольненияПредыдущий <> ДокументУвольнения Тогда
				
				ДополнительныеСвойства.Вставить("ВосстановитьДвиженияУвольнения", ДокументУвольненияПредыдущий);
				ДополнительныеСвойства.Вставить("ИсправлятьДвиженияУвольнения");
				
			КонецЕсли;
			
		Иначе
			ДополнительныеСвойства.Вставить("ИсправлятьДвиженияУвольнения");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИменаСторнируемыхРегистровДокументаУвольнения()
	
	Возврат "КадроваяИсторияСотрудников,ДанныеСостоянийСотрудников,ПараметрыПериодовСтажаПФР,ЗанятостьПозицийШтатногоРасписания,МероприятияТрудовойДеятельности";
	
КонецФункции

Процедура ПроверитьСоответствиеПозицииШРПодразделению(Отказ, ПроверяемыеРеквизиты)
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьШтатноеРасписание") Тогда
		
		Если ПроверяемыеРеквизиты.Найти("ДолжностьПоШтатномуРасписанию") <> Неопределено
			ИЛИ ПроверяемыеРеквизиты.Найти("Сотрудники.ДолжностьПоШтатномуРасписанию") Тогда 
			
			ПроверяемыеСтруктуры = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ЭтотОбъект);
			ПутьКДанным = "Объект.Подразделение";
			
			Для каждого ПроверяемаяСтруктура Из ПроверяемыеСтруктуры Цикл
				
				Если ЗначениеЗаполнено(ПроверяемаяСтруктура.ДолжностьПоШтатномуРасписанию) Тогда
					
					Если ПроверяемыеСтруктуры.Количество() = 1 Тогда
						ПутьКДаннымРеквизита = ПутьКДанным;
					Иначе
						ПутьКДаннымРеквизита = ПутьКДанным + "[" + ПроверяемаяСтруктура.НомерСтроки + "].Подразделение";
					КонецЕсли;
					
					ПодразделениеПоШтатномуРасписанию = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПроверяемаяСтруктура.ДолжностьПоШтатномуРасписанию, "Подразделение");
					Если ПодразделениеПоШтатномуРасписанию <> ПроверяемаяСтруктура.Подразделение Тогда
						ТекстСообщения = НСтр("ru = 'Значение поля ""Подразделение"" не соответствует позиции штатного расписания указанной в документе.';
												|en = 'Value of the ""Business unit"" field does not correspond with the headcount position specified in the document.'");
						ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Ссылка, ПутьКДаннымРеквизита, , Отказ);
					КонецЕсли;
					
				КонецЕсли; 
				
			КонецЦикла;
				
		КонецЕсли;
		
	КонецЕсли; 
	
КонецПроцедуры	

#Область ПодготовкаДанныхДляПроведенияДокумента

Функция ДанныеДляПроведения()
	
	ДанныеДляПроведения = Новый Структура; 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВосстановлениеВДолжности.Ссылка КАК Ссылка,
		|	ВосстановлениеВДолжности.Ссылка.Организация КАК Организация,
		|	ВосстановлениеВДолжности.ДатаВосстановления КАК ДатаВосстановления,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДействуетДо,
		|	ВосстановлениеВДолжности.Сотрудник КАК Сотрудник,
		|	ВосстановлениеВДолжности.Сотрудник.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
		|	ВосстановлениеВДолжности.ДолжностьПоШтатномуРасписанию КАК ДолжностьПоШтатномуРасписанию,
		|	ВосстановлениеВДолжности.Подразделение КАК Подразделение,
		|	ВосстановлениеВДолжности.Территория КАК Территория,
		|	ВосстановлениеВДолжности.Должность КАК Должность,
		|	ВосстановлениеВДолжности.РабочееМесто КАК РабочееМесто,
		|	ВосстановлениеВДолжности.РазрядКатегория КАК РазрядКатегория,
		|	ВосстановлениеВДолжности.ПКУ КАК ПКУ,
		|	ВосстановлениеВДолжности.КоличествоСтавок КАК КоличествоСтавок,
		|	ВосстановлениеВДолжности.ГрафикРаботы КАК ГрафикРаботы,
		|	ВосстановлениеВДолжности.СпособРасчетаАванса КАК СпособРасчетаАванса,
		|	ВосстановлениеВДолжности.Аванс КАК Аванс,
		|	ВосстановлениеВДолжности.ВидЗанятости КАК ВидЗанятости,
		|	ВосстановлениеВДолжности.Ссылка.ВидДоговора КАК ВидДоговора,
		|	ВосстановлениеВДолжности.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ВосстановлениеВДолжности.ДатаЗавершенияТрудовогоДоговора КАК ДатаЗавершенияТрудовогоДоговора,
		|	ВосстановлениеВДолжности.ПорядокРасчетаСтоимостиЕдиницыВремени КАК ПорядокРасчетаСтоимостиЕдиницыВремени,
		|	ВосстановлениеВДолжности.СовокупнаяТарифнаяСтавка КАК СовокупнаяТарифнаяСтавка,
		|	ВосстановлениеВДолжности.ВидТарифнойСтавки КАК ВидТарифнойСтавки,
		|	ВосстановлениеВДолжности.ВидАктаГосоргана КАК ВидАктаГосоргана,
		|	ВосстановлениеВДолжности.ПоступлениеНаСлужбуВпервые КАК ПоступлениеНаСлужбуВпервые,
		|	ВосстановлениеВДолжности.СпособПоступленияНаСлужбу КАК СпособПоступленияНаСлужбу,
		|	ВосстановлениеВДолжности.СрочныйДоговор КАК СрочныйДоговор,
		|	ВосстановлениеВДолжности.СезонныйДоговор КАК СезонныйДоговор,
		|	ВосстановлениеВДолжности.СрокЗаключенияДоговора КАК СрокЗаключенияДоговора,
		|	ВосстановлениеВДолжности.ОснованиеСрочногоДоговора КАК ОснованиеСрочногоДоговора,
		|	ВосстановлениеВДолжности.ОборудованиеРабочегоМеста КАК ОборудованиеРабочегоМеста,
		|	ВосстановлениеВДолжности.ИныеУсловияДоговора КАК ИныеУсловияДоговора,
		|	ВосстановлениеВДолжности.Ссылка.Руководитель КАК Руководитель,
		|	ВосстановлениеВДолжности.Ссылка.ДолжностьРуководителя КАК ДолжностьРуководителя,
		|	ВосстановлениеВДолжности.Ссылка.ОснованиеПредставителяНанимателя КАК ОснованиеПредставителяНанимателя,
		|	ВосстановлениеВДолжности.ДатаУвольнения КАК ДатаУвольнения,
		|	ВосстановлениеВДолжности.СтатьяФинансирования КАК СтатьяФинансирования,
		|	ВосстановлениеВДолжности.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	ВосстановлениеВДолжности.ОтношениеКЕНВД КАК ОтношениеКЕНВД
		|ПОМЕСТИТЬ ВТСотрудники
		|ИЗ
		|	Документ.ВосстановлениеВДолжности КАК ВосстановлениеВДолжности
		|ГДЕ
		|	ВосстановлениеВДолжности.Ссылка = &Ссылка";
	
	Запрос.Выполнить();
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаСотрудников.ДатаВосстановления КАК ДатаСобытия,
		|	ТаблицаСотрудников.ДействуетДо КАК ДействуетДо,
		|	ТаблицаСотрудников.Сотрудник КАК Сотрудник,
		|	ТаблицаСотрудников.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
		|	ТаблицаСотрудников.ДолжностьПоШтатномуРасписанию КАК Позиция,
		|	ТаблицаСотрудников.Подразделение КАК Подразделение,
		|	ТаблицаСотрудников.Должность КАК Должность,
		|	ТаблицаСотрудников.КоличествоСтавок КАК КоличествоСтавок,
		|	ТаблицаСотрудников.ГрафикРаботы КАК ГрафикРаботы,
		|	ТаблицаСотрудников.СпособРасчетаАванса КАК СпособРасчетаАванса,
		|	ТаблицаСотрудников.Аванс КАК Аванс,
		|	ТаблицаСотрудников.Ссылка.ВидДоговора КАК ВидДоговора,
		|	ТаблицаСотрудников.ФизическоеЛицо,
		|	ТаблицаСотрудников.Ссылка.Руководитель КАК Руководитель,
		|	ТаблицаСотрудников.Ссылка.ДолжностьРуководителя КАК ДолжностьРуководителя
		|ИЗ
		|	ВТСотрудники КАК ТаблицаСотрудников";
	
	// Первый набор данных для проведения - таблица для формирования кадровых движений, истории графиков, авансов.
	КадровыеДвижения = Запрос.Выполнить().Выгрузить();
	КадровыеДвижения.Колонки.Добавить("ВидСобытия", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыКадровыхСобытий"));
	КадровыеДвижения.ЗаполнитьЗначения(Перечисления.ВидыКадровыхСобытий.ВосстановлениеВДолжности, "ВидСобытия");
	ДанныеДляПроведения.Вставить("КадровыеДвижения", КадровыеДвижения);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаСотрудников.ДатаВосстановления КАК Период,
		|	ТаблицаСотрудников.Сотрудник КАК Сотрудник,
		|	ТаблицаСотрудников.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
		|	ТаблицаСотрудников.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ТаблицаСотрудников.ДолжностьПоШтатномуРасписанию КАК ПозицияШтатногоРасписания,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыЗанятостиПозицийШтатногоРасписания.Занята) КАК ВидЗанятостиПозиции,
		|	ТаблицаСотрудников.КоличествоСтавок КАК КоличествоСтавок,
		|	ТаблицаСотрудников.ДатаЗавершенияТрудовогоДоговора КАК ПланируемаяДатаЗавершения
		|ИЗ
		|	ВТСотрудники КАК ТаблицаСотрудников";
	
	ДанныеДляПроведения.Вставить("ЗанятостьПозицийШтатногоРасписания", Запрос.Выполнить().Выгрузить());
	
	ДвиженияВидовЗанятости = Документы.ВосстановлениеВДолжности.РезультатЗапросаПоДвижениямВидовЗанятостиСотрудников(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Ссылка)).Выгрузить();
	
	// Данные о сотрудниках, переводимых на внутреннее совмещение
	ТаблицаДвиженийВидовЗанятостиВнутреннихСовместителей =
		КадровыйУчетРасширенный.ТаблицаДвиженийВидовЗанятостиВнутреннихСовместителей(ДвиженияВидовЗанятости, Организация);
	
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаДвиженийВидовЗанятостиВнутреннихСовместителей, ДвиженияВидовЗанятости);
	
	ДанныеДляПроведения.Вставить("ДвиженияВидовЗанятости", ДвиженияВидовЗанятости);
	
	Если НачисленияУтверждены Тогда
		
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Сотрудники.ДатаВосстановления КАК ДатаСобытия,
			|	Сотрудники.Сотрудник КАК Сотрудник,
			|	Начисления.Начисление,
			|	Начисления.Размер,
			|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо,
			|	Сотрудники.ГоловнаяОрганизация
			|ИЗ
			|	Документ.ВосстановлениеВДолжности.Начисления КАК Начисления
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСотрудники КАК Сотрудники
			|		ПО Начисления.Ссылка = Сотрудники.Ссылка
			|ГДЕ
			|	Начисления.Ссылка = &Ссылка
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	Сотрудники.ДатаВосстановления,
			|	Сотрудники.Сотрудник,
			|	Льготы.Льгота,
			|	Льготы.Размер,
			|	Сотрудники.ФизическоеЛицо,
			|	Сотрудники.ГоловнаяОрганизация
			|ИЗ
			|	Документ.ВосстановлениеВДолжности.Льготы КАК Льготы
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСотрудники КАК Сотрудники
			|		ПО Льготы.Ссылка = Сотрудники.Ссылка
			|ГДЕ
			|	Льготы.Ссылка = &Ссылка";
		
		// Второй набор данных для проведения - таблица для формирования плановых начислений.
		ПлановыеНачисления = Запрос.Выполнить().Выгрузить();
		ПлановыеНачисления.Колонки.Добавить("Используется", Новый ОписаниеТипов("Булево"));
		ПлановыеНачисления.ЗаполнитьЗначения(Истина, "Используется");
		ПлановыеНачисления.Колонки.Добавить("ДокументОснование", Новый ОписаниеТипов(Метаданные.ОпределяемыеТипы.ОснованиеНачисления.Тип));
		ДанныеДляПроведения.Вставить("ПлановыеНачисления", ПлановыеНачисления);
		
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ДОБАВИТЬКДАТЕ(Сотрудники.ДатаУвольнения, ДЕНЬ, 1) КАК ДатаСобытия,
			|	Сотрудники.Сотрудник КАК Сотрудник,
			|	ЛОЖЬ КАК Применение
			|ИЗ
			|	ВТСотрудники КАК Сотрудники
			|ГДЕ
			|	ДОБАВИТЬКДАТЕ(Сотрудники.ДатаУвольнения, ДЕНЬ, 1) < Сотрудники.ДатаВосстановления
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	Сотрудники.ДатаВосстановления,
			|	Сотрудники.Сотрудник,
			|	ИСТИНА
			|ИЗ
			|	ВТСотрудники КАК Сотрудники";
		
		ПрименениеНачислений = РасчетЗарплатыРасширенный.ПустаяТаблицаРегистрацииПримененияПлановыхНачислений();
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(Запрос.Выполнить().Выгрузить(), ПрименениеНачислений);
		ДанныеДляПроведения.Вставить("ПрименениеНачислений", ПрименениеНачислений);
		
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Сотрудники.ДатаВосстановления КАК ДатаСобытия,
			|	Сотрудники.Сотрудник КАК Сотрудник,
			|	Сотрудники.Организация КАК Организация,
			|	Сотрудники.ФизическоеЛицо,
			|	Показатели.Показатель,
			|	МАКСИМУМ(Показатели.Значение) КАК Значение,
			|	Показатели.Ссылка КАК Ссылка
			|ПОМЕСТИТЬ ВТЗначенияПоказателей
			|ИЗ
			|	Документ.ВосстановлениеВДолжности.Показатели КАК Показатели
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВосстановлениеВДолжности.Начисления КАК Начисления
			|		ПО Показатели.Ссылка = Начисления.Ссылка
			|			И Показатели.ИдентификаторСтрокиВидаРасчета = Начисления.ИдентификаторСтрокиВидаРасчета
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудники КАК Сотрудники
			|		ПО Показатели.Ссылка = Сотрудники.Ссылка
			|ГДЕ
			|	Показатели.Ссылка = &Ссылка
			|	И Показатели.Показатель <> ЗНАЧЕНИЕ(Справочник.ПоказателиРасчетаЗарплаты.ПустаяСсылка)
			|
			|СГРУППИРОВАТЬ ПО
			|	Сотрудники.ДатаВосстановления,
			|	Сотрудники.Сотрудник,
			|	Сотрудники.Организация,
			|	Сотрудники.ФизическоеЛицо,
			|	Показатели.Показатель,
			|	Показатели.Ссылка
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	Сотрудники.ДатаВосстановления,
			|	Сотрудники.Сотрудник,
			|	Сотрудники.Организация,
			|	Сотрудники.ФизическоеЛицо,
			|	Показатели.Показатель,
			|	Показатели.Значение,
			|	Показатели.Ссылка
			|ИЗ
			|	Документ.ВосстановлениеВДолжности.Показатели КАК Показатели
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудники КАК Сотрудники
			|		ПО Показатели.Ссылка = Сотрудники.Ссылка
			|ГДЕ
			|	Показатели.Ссылка = &Ссылка
			|	И Показатели.ИдентификаторСтрокиВидаРасчета = 0
			|	И Показатели.Показатель <> ЗНАЧЕНИЕ(Справочник.ПоказателиРасчетаЗарплаты.ПустаяСсылка)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ЗначенияПоказателей.ДатаСобытия,
			|	ЗначенияПоказателей.Сотрудник,
			|	ЗначенияПоказателей.Организация,
			|	ЗначенияПоказателей.ФизическоеЛицо,
			|	ЗначенияПоказателей.Показатель,
			|	МАКСИМУМ(ЗначенияПоказателей.Значение) КАК Значение,
			|	ЗначенияПоказателей.Ссылка
			|ИЗ
			|	ВТЗначенияПоказателей КАК ЗначенияПоказателей
			|
			|СГРУППИРОВАТЬ ПО
			|	ЗначенияПоказателей.ДатаСобытия,
			|	ЗначенияПоказателей.Сотрудник,
			|	ЗначенияПоказателей.Организация,
			|	ЗначенияПоказателей.ФизическоеЛицо,
			|	ЗначенияПоказателей.Показатель,
			|	ЗначенияПоказателей.Ссылка";
		
		// Третий набор данных для проведения - таблица для формирования значений показателей начислений.
		ЗначенияПоказателей = Запрос.Выполнить().Выгрузить();
		ЗначенияПоказателей.Колонки.Добавить("ДокументОснование", Новый ОписаниеТипов(Метаданные.ОпределяемыеТипы.ОснованиеНачисления.Тип));
		ДанныеДляПроведения.Вставить("ЗначенияПоказателейНачислений", ЗначенияПоказателей);
		
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Сотрудники.ДатаВосстановления КАК ДатаСобытия,
			|	Сотрудники.Организация КАК Организация,
			|	Сотрудники.Сотрудник КАК Сотрудник,
			|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо,
			|	Показатели.Показатель КАК Показатель,
			|	ИСТИНА КАК Применение
			|ИЗ
			|	Документ.ВосстановлениеВДолжности.Показатели КАК Показатели
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСотрудники КАК Сотрудники
			|		ПО Показатели.Ссылка = Сотрудники.Ссылка
			|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВосстановлениеВДолжности.Показатели КАК ПоказателиНачислений
			|		ПО Показатели.Ссылка = ПоказателиНачислений.Ссылка
			|			И Показатели.Показатель = ПоказателиНачислений.Показатель
			|			И (ПоказателиНачислений.ИдентификаторСтрокиВидаРасчета <> 0)
			|ГДЕ
			|	Показатели.Ссылка = &Ссылка
			|	И Показатели.ИдентификаторСтрокиВидаРасчета = 0
			|	И Показатели.Показатель <> ЗНАЧЕНИЕ(Справочник.ПоказателиРасчетаЗарплаты.ПустаяСсылка)
			|	И ПоказателиНачислений.Показатель ЕСТЬ NULL ";
		
		// Пятый набор данных для проведения - таблица для формирования применения дополнительных показателей.
		ПрименениеДополнительныхПоказателей = Запрос.Выполнить().Выгрузить();
		ДанныеДляПроведения.Вставить("ПрименениеДополнительныхПоказателей", ПрименениеДополнительныхПоказателей);
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	Сотрудники.ДатаВосстановления КАК ДатаСобытия,
			|	Сотрудники.Сотрудник КАК Сотрудник,
			|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо,
			|	Сотрудники.ПорядокРасчетаСтоимостиЕдиницыВремени КАК ПорядокРасчета,
			|	ДАТАВРЕМЯ(1, 1, 1) КАК ДействуетДо
			|ИЗ
			|	ВТСотрудники КАК Сотрудники";
		
		// Шестой набор данных для проведения - таблица для формирования значений порядка пересчета тарифной ставки.
		ПорядокПересчетаТарифнойСтавки = Запрос.Выполнить().Выгрузить();
		ДанныеДляПроведения.Вставить("ПорядокПересчетаТарифнойСтавки", ПорядокПересчетаТарифнойСтавки);
		
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Сотрудники.ДатаВосстановления КАК ДатаСобытия,
			|	Сотрудники.Сотрудник КАК Сотрудник,
			|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо,
			|	Сотрудники.СовокупнаяТарифнаяСтавка КАК Значение,
			|	ВЫБОР
			|		КОГДА Сотрудники.СовокупнаяТарифнаяСтавка = 0
			|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыТарифныхСтавок.ПустаяСсылка)
			|		ИНАЧЕ Сотрудники.ВидТарифнойСтавки
			|	КОНЕЦ КАК ВидТарифнойСтавки,
			|	ДАТАВРЕМЯ(1, 1, 1) КАК ДействуетДо
			|ИЗ
			|	ВТСотрудники КАК Сотрудники";
		
		// Седьмой набор данных для проведения - таблица для формирования значений совокупной тарифной ставки.
		ДанныеСовокупныхТарифныхСтавок = Запрос.Выполнить().Выгрузить();
		ДанныеДляПроведения.Вставить("ДанныеСовокупныхТарифныхСтавок", ДанныеСовокупныхТарифныхСтавок);
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Сотрудники.ДатаВосстановления КАК Период,
		|	Сотрудники.Сотрудник КАК Сотрудник,
		|	Сотрудники.Организация КАК Организация,
		|	Начисления.Начисление КАК Начисление,
		|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
		|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
		|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	БухучетНачислений.ДоляРаспределения КАК ДоляРаспределения,
		|	ИСТИНА КАК Используется,
		|	ИСТИНА КАК БухучетЗадан
		|ИЗ
		|	Документ.ВосстановлениеВДолжности.Начисления КАК Начисления
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВосстановлениеВДолжности.БухучетНачислений КАК БухучетНачислений
		|		ПО Начисления.ИдентификаторСтрокиВидаРасчета = БухучетНачислений.ИдентификаторСтрокиВидаРасчета
		|			И Начисления.Ссылка = БухучетНачислений.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудники КАК Сотрудники
		|		ПО Начисления.Ссылка = Сотрудники.Ссылка
		|ГДЕ
		|	Начисления.Ссылка = &Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Сотрудники.ДатаВосстановления,
		|	Сотрудники.Сотрудник,
		|	Сотрудники.Организация,
		|	Начисления.Льгота,
		|	БухучетНачислений.СтатьяФинансирования,
		|	БухучетНачислений.СтатьяРасходов,
		|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете,
		|	БухучетНачислений.ДоляРаспределения,
		|	ИСТИНА,
		|	ИСТИНА
		|ИЗ
		|	Документ.ВосстановлениеВДолжности.Льготы КАК Начисления
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВосстановлениеВДолжности.БухучетНачислений КАК БухучетНачислений
		|		ПО Начисления.ИдентификаторСтрокиВидаРасчета = БухучетНачислений.ИдентификаторСтрокиВидаРасчета
		|			И Начисления.Ссылка = БухучетНачислений.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудники КАК Сотрудники
		|		ПО Начисления.Ссылка = Сотрудники.Ссылка
		|ГДЕ
		|	Начисления.Ссылка = &Ссылка";
		Выборка = Запрос.Выполнить().Выбрать();
		Бухучет = ОтражениеЗарплатыВБухучетеРасширенный.НоваяТаблицаБухучетНачисленийСотрудников();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(Бухучет.Добавить(), Выборка);
		КонецЦикла;
		ДанныеДляПроведения.Вставить("БухучетНачислений", Бухучет);
		
	КонецЕсли;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ЕжегодныеОтпуска.ВидЕжегодногоОтпуска.ХарактерЗависимостиДнейОтпуска = ЗНАЧЕНИЕ(Перечисление.ХарактерЗависимостиКоличестваДнейОтпуска.ЗависитОтСтажа)
		|			ТОГДА ДОБАВИТЬКДАТЕ(Сотрудники.ДатаВосстановления, ДЕНЬ, 1)
		|		ИНАЧЕ Сотрудники.ДатаВосстановления
		|	КОНЕЦ КАК ДатаСобытия,
		|	Сотрудники.Сотрудник,
		|	ЕжегодныеОтпуска.ВидЕжегодногоОтпуска,
		|	ЕжегодныеОтпуска.КоличествоДнейВГод
		|ИЗ
		|	Документ.ВосстановлениеВДолжности.ЕжегодныеОтпуска КАК ЕжегодныеОтпуска
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСотрудники КАК Сотрудники
		|		ПО ЕжегодныеОтпуска.Ссылка = Сотрудники.Ссылка
		|ГДЕ
		|	ЕжегодныеОтпуска.Ссылка = &Ссылка";
	
	// Четвертый набор данных для проведения - таблица для формирования положенных видов ежегодных отпусков.
	ПоложенныеЕжегодныеОтпуска = Запрос.Выполнить().Выгрузить();
	ДанныеДляПроведения.Вставить("ЕжегодныеОтпуска", ПоложенныеЕжегодныеОтпуска);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Сотрудники.ДатаВосстановления КАК ДатаСобытия,
		|	Сотрудники.Сотрудник КАК Сотрудник,
		|	Сотрудники.РазрядКатегория КАК РазрядКатегория,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДействуетДо
		|ИЗ
		|	ВТСотрудники КАК Сотрудники";
	
	// Восьмой набор данных для проведения - таблица для формирования значений разряда сотрудника.
	РазрядыКатегорииСотрудников = Запрос.Выполнить().Выгрузить();
	ДанныеДляПроведения.Вставить("РазрядыКатегорииСотрудников", РазрядыКатегорииСотрудников);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Сотрудники.ДатаВосстановления КАК ДатаСобытия,
		|	Сотрудники.Сотрудник КАК Сотрудник
		|ИЗ
		|	ВТСотрудники КАК Сотрудники";
	
	// Девятый набор данных для проведения - таблица для формирования времени регистрации документа.
	СотрудникиДаты = Запрос.Выполнить().Выгрузить();
	ДанныеДляПроведения.Вставить("СотрудникиДаты", СотрудникиДаты);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Сотрудники.ДатаВосстановления КАК Период,
		|	Сотрудники.ДатаВосстановления КАК ДатаНачала,
		|	Сотрудники.Сотрудник КАК Сотрудник,
		|	Сотрудники.ФизическоеЛицо,
		|	Сотрудники.ГоловнаяОрганизация,
		|	Сотрудники.Организация,
		|	Сотрудники.ДатаЗавершенияТрудовогоДоговора КАК ДатаОкончания,
		|	Сотрудники.Руководитель КАК ПредставительНанимателя,
		|	Сотрудники.ДолжностьРуководителя КАК ДолжностьПредставителяНанимателя,
		|	Сотрудники.ОснованиеПредставителяНанимателя КАК ОснованиеПредставителяНанимателя,
		|	Сотрудники.ВидАктаГосоргана,
		|	Сотрудники.ПоступлениеНаСлужбуВпервые,
		|	Сотрудники.СпособПоступленияНаСлужбу,
		|	Сотрудники.СрочныйДоговор,
		|	Сотрудники.СезонныйДоговор,
		|	Сотрудники.СрокЗаключенияДоговора,
		|	Сотрудники.ОснованиеСрочногоДоговора,
		|	Сотрудники.ОборудованиеРабочегоМеста,
		|	Сотрудники.ИныеУсловияДоговора
		|ИЗ
		|	ВТСотрудники КАК Сотрудники";
	
	// Десятый набор данных для проведения - таблица для формирования описаний договоров сотрудников.
	СведенияОКонтрактахДоговорах = Запрос.Выполнить().Выгрузить();
	ДанныеДляПроведения.Вставить("СведенияОКонтрактахДоговорах", СведенияОКонтрактахДоговорах);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Сотрудники.Сотрудник КАК Сотрудник,
		|	ЗНАЧЕНИЕ(Перечисление.СостоянияСотрудника.Работа) КАК Состояние,
		|	Сотрудники.ДатаВосстановления КАК Начало,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК Окончание,
		|	НЕОПРЕДЕЛЕНО КАК ВидВремени
		|ИЗ
		|	ВТСотрудники КАК Сотрудники";
	
	// Данные состояний
	ДанныеСостояний = Запрос.Выполнить().Выгрузить();
	ДанныеДляПроведения.Вставить("ДанныеСостояний", ДанныеСостояний);
	
	Запрос.УстановитьПараметр("ИспользоватьОбособленныеТерритории",
		ПолучитьФункциональнуюОпцию("ИспользоватьОбособленныеТерритории", Новый Структура("Организация", Организация)));
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Сотрудники.ДатаВосстановления КАК Период,
		|	Сотрудники.Сотрудник КАК Сотрудник,
		|	ВЫРАЗИТЬ(Сотрудники.Сотрудник КАК Справочник.Сотрудники).ГоловнаяОрганизация КАК ГоловнаяОрганизация,
		|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо,
		|	Сотрудники.Территория КАК Территория
		|ИЗ
		|	ВТСотрудники КАК Сотрудники
		|ГДЕ
		|	&ИспользоватьОбособленныеТерритории";
	
	// Набор сведений для проведения по территориям сотрудников
	СведенияОТерриторияхСотрудников = Запрос.Выполнить().Выгрузить();
	ДанныеДляПроведения.Вставить("ТерриторииСотрудников", СведенияОТерриторияхСотрудников);
	
	// Данные для регистрации в учета стажа ПФР
	ДанныеДляРегистрацииВУчете = Документы.ВосстановлениеВДолжности.ДанныеДляРегистрацииВУчетаСтажаПФР(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Ссылка), ТаблицаДвиженийВидовЗанятостиВнутреннихСовместителей);
	
	ДанныеДляПроведения.Вставить("ДанныеДляРегистрацииВУчетаСтажаПФР", ДанныеДляРегистрацииВУчете[Ссылка]);
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.Грейды") Тогда 
		
		Модуль = ОбщегоНазначения.ОбщийМодуль("Грейды");
		ДанныеГрейдовСотрудников = Модуль.ДанныеДляПроведенияГрейдыСотрудников(Ссылка, "ДатаВосстановления");
		
		ДанныеДляПроведения.Вставить("ДанныеГрейдовСотрудников", ДанныеГрейдовСотрудников);
		
	КонецЕсли;
	
	// Данные для формирования значений ПКУ сотрудника.
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Сотрудники.ДатаВосстановления КАК ДатаСобытия,
		|	Сотрудники.Сотрудник КАК Сотрудник,
		|	Сотрудники.ПКУ КАК ПКУ,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДействуетДо
		|ИЗ
		|	ВТСотрудники КАК Сотрудники";
	
	ПКУСотрудников = Запрос.Выполнить().Выгрузить();
	ДанныеДляПроведения.Вставить("ПКУСотрудников", ПКУСотрудников);
	
	// Данные для регистрации бухучета сотрудника.
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Сотрудники.Сотрудник КАК Сотрудник,
	|	Сотрудники.ДатаВосстановления КАК Период,
	|	Сотрудники.СтатьяФинансирования КАК СтатьяФинансирования,
	|	Сотрудники.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	Сотрудники.ОтношениеКЕНВД КАК ОтношениеКЕНВД
	|ИЗ
	|	ВТСотрудники КАК Сотрудники";
	Выборка = Запрос.Выполнить().Выбрать();
	НастройкиБухучета = ОтражениеЗарплатыВБухучетеРасширенный.НоваяТаблицаНастройкиБухучетаЗаплатыСотрудников();
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.СтатьяФинансирования) Или ЗначениеЗаполнено(Выборка.СпособОтраженияЗарплатыВБухучете) Или ЗначениеЗаполнено(Выборка.ОтношениеКЕНВД) Тогда
			ЗаполнитьЗначенияСвойств(НастройкиБухучета.Добавить(), Выборка);
		КонецЕсли;
	КонецЦикла;
	ДанныеДляПроведения.Вставить("НастройкиБухучета", НастройкиБухучета);
	
	Если НачисленияУтверждены И ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.УправленческаяЗарплата") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("УправленческаяЗарплата");
		Модуль.ПриемНаРаботуДополнитьДанныеДляПроведение(ДанныеДляПроведения, Запрос.МенеджерВременныхТаблиц, Ссылка);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба.КлассныеЧиныРанги") Тогда 
		Модуль = ОбщегоНазначения.ОбщийМодуль("КлассныеЧиныРанги");
		КлассныеЧиныРангиФизическихЛиц = Модуль.ВосстановлениеВДолжностиСведенияОКлассныхЧинахРангах(Ссылка);
		ДанныеДляПроведения.Вставить("КлассныеЧиныРангиФизическихЛиц", КлассныеЧиныРангиФизическихЛиц);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.РабочиеМестаОхраныТруда") Тогда
		МодульРабочиеМестаОхраныТруда = ОбщегоНазначения.ОбщийМодуль("РабочиеМестаОхраныТруда");
		МодульРабочиеМестаОхраныТруда.ПриемНаРаботуСведенияОРабочихМестахСотрудников(ДанныеДляПроведения, Запрос, "ДатаВосстановления");
	КонецЕсли;
	
	Возврат ДанныеДляПроведения;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли