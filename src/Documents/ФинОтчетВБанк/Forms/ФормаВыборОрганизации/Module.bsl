#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("Организация") Тогда
		ОрганизацияПоУмолчанию = Параметры.Организация;
	КонецЕсли;
	
	// Ограничим список выбираемых организаций только теми, данных которых пользователь может просматривать.
	ДоступныеОрганизации = ОбщегоНазначенияБПВызовСервераПовтИсп.ВсеОрганизацииДанныеКоторыхДоступныПоRLS(Ложь);
	
	Если НЕ ЗначениеЗаполнено(ОрганизацияПоУмолчанию) Тогда
		ОрганизацияПоУмолчанию = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	КонецЕсли;
	Если ДоступныеОрганизации.Найти(ОрганизацияПоУмолчанию) = Неопределено Тогда
		ОрганизацияПоУмолчанию = Неопределено;
	КонецЕсли;
	

	// Сформируем список доступных организаций, включая обособленные подразделения.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДоступныеОрганизации", ДоступныеОрганизации);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НаборОрганизаций.Организация КАК Организация,
	|	НаборОрганизаций.Наименование КАК Наименование,
	|	НаборОрганизаций.ВключатьОбособленныеПодразделения
	|ИЗ
	|	(ВЫБРАТЬ
	|		Организации.Ссылка КАК Организация,
	|		Организации.Наименование КАК Наименование,
	|		ЛОЖЬ КАК ВключатьОбособленныеПодразделения
	|	ИЗ
	|		Справочник.Организации КАК Организации
	|	ГДЕ
	|		Организации.ПометкаУдаления = ЛОЖЬ
	|		И Организации.Ссылка В (&ДоступныеОрганизации)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		Организации.ГоловнаяОрганизация,
	|		Организации.ГоловнаяОрганизация.Наименование,
	|		ИСТИНА
	|	ИЗ
	|		Справочник.Организации КАК Организации
	|	ГДЕ
	|		Организации.ОбособленноеПодразделение
	|		И Организации.ПометкаУдаления = ЛОЖЬ
	|		И Организации.ГоловнаяОрганизация В (&ДоступныеОрганизации)) КАК НаборОрганизаций
	|
	|УПОРЯДОЧИТЬ ПО
	|	Наименование";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл

		НоваяСтрока = Организации.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
		Если Выборка.ВключатьОбособленныеПодразделения Тогда
			// Назовем элемент "с обособленными подразделениями" как в отчетах.
			НоваяСтрока.Наименование = СтрШаблон(НСтр("ru = '%1 с обособленными подразделениями';
														|en = '%1 with branch offices'"), Выборка.Наименование);
		КонецЕсли;
		
	КонецЦикла;

	АвтозаполнениеОтчетов = ЗаполнениеФинОтчетностиВБанки.ПолучитьПараметрАвтозаполнение();
	
	ПоказатьСкрытьБаннер();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// Встанем на организацию по умолчанию.
	Если ЗначениеЗаполнено(ОрганизацияПоУмолчанию) Тогда
		Отбор = Новый Структура();
		Отбор.Вставить("Организация", ОрганизацияПоУмолчанию);
		НайденныеСтроки = Организации.НайтиСтроки(Отбор);
		Если НайденныеСтроки.Количество() > 0 Тогда
			Элементы.Организации.ТекущаяСтрока = НайденныеСтроки[0].ПолучитьИдентификатор();
		КонецЕсли;
	Иначе
		Если Организации.Количество() > 0 Тогда
			Элементы.Организации.ТекущаяСтрока = Организации[0].ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыОрганизации

&НаКлиенте
Процедура ОрганизацииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ВыбратьИЗакрыть();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	ВыбратьИЗакрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыбратьИЗакрыть()

	ТекущиеДанные = Элементы.Организации.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Выберите организацию';
										|en = 'Select company'"));
		Возврат;
	КонецЕсли;
	
	СтруктураВозврата = Новый Структура();
	СтруктураВозврата.Вставить("Организация",                       ТекущиеДанные.Организация);
	СтруктураВозврата.Вставить("ВключатьОбособленныеПодразделения", ТекущиеДанные.ВключатьОбособленныеПодразделения);
	СтруктураВозврата.Вставить("АвтозаполнениеОтчетов", АвтозаполнениеОтчетов);
	
	СохранитьНастройкиАвтозаполненияНаСервере(АвтозаполнениеОтчетов);
	
	Закрыть(СтруктураВозврата);

КонецПроцедуры

#КонецОбласти

#Область РаботаСБаннеромИСохранениемНастроек

&НаСервере
Процедура ПоказатьСкрытьБаннер()
	
	ПоказыватьБаннер = ЗаполнениеФинОтчетностиВБанки.ПолучитьПараметрПоказыватьБаннерАвтозаполнение();
	
	Элементы.БаннерСФоном.Видимость = ПоказыватьБаннер;
	
	Элементы.КартинкаБаннера.Картинка = ЗаполнениеФинОтчетностиВБанки.КартинкаБаннерАвтозаполнение();
	
	Если ПоказыватьБаннер Тогда
		
		РазмерШрифтаБаннера = 10;
		
		ШрифтТекстаБаннера = Новый Шрифт(ШрифтыСтиля.ШрифтТекстаБаннера,,РазмерШрифтаБаннера);
		
		ТекстБаннера = Новый ФорматированнаяСтрока(
			Новый ФорматированнаяСтрока(НСтр("ru = 'Включение флажка ';
											|en = 'Select check box'"), ШрифтТекстаБаннера),
			Новый ФорматированнаяСтрока(НСтр("ru = '""Автоматически заполнить"" ';
											|en = '""Fill in automatically""'"), Новый Шрифт(ШрифтТекстаБаннера,,,Истина)),
			Новый ФорматированнаяСтрока(НСтр("ru = 'может замедлить создание пакета, но сократит работу в дальнейшем.
				|Состав отчетов к отправке можно будет изменить';
				|en = 'might slow down package creation but will ease the execution later.
				|You can change reports to be sent'"), ШрифтТекстаБаннера)
		);
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КартинкаЗакрытьБаннерНажатие(Элемент)
	
	Элементы.БаннерСФоном.Видимость = Ложь;
	СохранитьНастройкиБаннераНаСервере(Ложь);
	
КонецПроцедуры
	
&НаСервере
Процедура СохранитьНастройкиБаннераНаСервере(ЗначениеПараметра)
	
	ЗаполнениеФинОтчетностиВБанки.СохранитьПараметрПоказыватьБаннерАвтозаполнение(ЗначениеПараметра);

КонецПроцедуры
	
&НаСервере
Процедура СохранитьНастройкиАвтозаполненияНаСервере(ЗначениеПараметра)
	
	ЗаполнениеФинОтчетностиВБанки.СохранитьПараметрАвтозаполнение(ЗначениеПараметра);

КонецПроцедуры

#КонецОбласти

