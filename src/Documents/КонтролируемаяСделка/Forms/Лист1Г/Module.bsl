#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры.ДанныеЗаполнения);
	
	ВерсияУведомления = Параметры.ВерсияУведомления;
	
	Элементы.ИдентификаторЛиста1Б.СписокВыбора.Очистить();
	Для Каждого СтрокаСписка Из Параметры.Листы1Б Цикл
		ОписаниеЛиста1Б = СтрШаблон(НСтр("ru = '%1 (%2 на сумму %3)';
										|en = '%1 (%2 на сумму %3)'"),
			Формат(СтрокаСписка.ДатаСовершенияСделки, "ДЛФ=D"),
			СтрокаСписка.Количество, СтрокаСписка.Стоимость);
		
		Элементы.ИдентификаторЛиста1Б.СписокВыбора.Добавить(Строка(СтрокаСписка.ИдентификаторЛиста1Б), ОписаниеЛиста1Б);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	Если ПроверитьЗаполнение() Тогда
		Модифицированность = Ложь;
		СтруктураЗакрытия = ВернутьСтруктуруЗакрытия();
		Закрыть(СтруктураЗакрытия);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ВернутьСтруктуруЗакрытия()
	
	Структура = Новый Структура();
	
	Для Каждого ИмяРеквизита Из ВозвращаемыеРеквизиты() Цикл
		Структура.Вставить(ИмяРеквизита, ЭтотОбъект[ИмяРеквизита]);
	КонецЦикла;
	
	Структура.Вставить("ИдентификаторЛиста1Б", Новый УникальныйИдентификатор(ИдентификаторЛиста1Б));
	
	Возврат Структура;
	
КонецФункции

&НаСервере
Функция ВозвращаемыеРеквизиты()
	
	ВозвращаемыеРеквизиты = Новый Массив;
	ВозвращаемыеРеквизиты.Добавить("НаименованиеУслуги");
	ВозвращаемыеРеквизиты.Добавить("УчастникиВзаимозависимы");
	ВозвращаемыеРеквизиты.Добавить("Контрагент");
	ВозвращаемыеРеквизиты.Добавить("Валюта");
	ВозвращаемыеРеквизиты.Добавить("Стоимость");
	
	Возврат ВозвращаемыеРеквизиты;
	
КонецФункции

#КонецОбласти

