#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры.ДанныеЗаполнения);
	
	ВерсияУведомления = Параметры.ВерсияУведомления;
	ТипПредметаСделки = Параметры.ТипПредметаСделки;
	
	Если ВерсияУведомления < КонтролируемыеСделкиКлиентСервер.ВерсияУведомления_2018() Тогда
		Валюта = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	Иначе
		Валюта = Параметры.Валюта;
	КонецЕсли;
	
	НастроитьЭлементыПоВерсииФормата();
	
	СписокКодовПоставки = КонтролируемыеСделки.ПолучитьКодыУсловийПоставки();
	Элементы.КодУсловийПоставки.СписокВыбора.Очистить();
	Для Каждого Код Из СписокКодовПоставки Цикл
		НовыйКод = Элементы.КодУсловийПоставки.СписокВыбора.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйКод, Код);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	Если ПроверитьЗаполнение() Тогда
		Модифицированность = Ложь;
		СтруктураЗакрытия = ВернутьСтруктуруЗакрытия();
		Закрыть(СтруктураЗакрытия);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастроитьЭлементыПоВерсииФормата()
	
	Элементы.ПроцентнаяСтавка.Видимость = 
		ВерсияУведомления >= КонтролируемыеСделкиКлиентСервер.ВерсияУведомления_2018()
		И ТипПредметаСделки = Перечисления.ТипыПредметовКонтролируемыхСделок.ДолговоеОбязательство;
	
	Элементы.ГруппаЦена.Видимость = 
		ВерсияУведомления < КонтролируемыеСделкиКлиентСервер.ВерсияУведомления_2018()
		Или ТипПредметаСделки <> Перечисления.ТипыПредметовКонтролируемыхСделок.ДолговоеОбязательство;
	
	Версия2018ИВыше = ВерсияУведомления >= КонтролируемыеСделкиКлиентСервер.ВерсияУведомления_2018();
	Элементы.ГородОтправкиТовара.Видимость = Не Версия2018ИВыше;
	Элементы.ГородСовершенияСделки.Видимость = Не Версия2018ИВыше;
	
	Элементы.Количество.ОграничениеТипа = ТипПоказателяКоличество(ВерсияУведомления);
	
	Элементы.Цена.ОграничениеТипа = ТипПоказателяЦена(ВерсияУведомления);
	
	ВерсияУведомленияНиже2024 = ВерсияУведомления < КонтролируемыеСделкиКлиентСервер.ВерсияУведомления_2024();
	Элементы.ГруппаОтправка.Видимость = ВерсияУведомленияНиже2024;
	Элементы.ГруппаСделкаСтрана.Видимость = ВерсияУведомленияНиже2024;
	Элементы.ГородСовершенияСделки.Видимость = ВерсияУведомленияНиже2024;
	
	Элементы.ГруппаСуммаРоялти.Видимость = ВерсияУведомления >= КонтролируемыеСделкиКлиентСервер.ВерсияУведомления_2024()
		И ТипПредметаСделки = Перечисления.ТипыПредметовКонтролируемыхСделок.ИнойОбъектГражданскихПрав;
	
КонецПроцедуры

&НаСервере
Функция ВернутьСтруктуруЗакрытия()
	
	Структура = Новый Структура();
	
	Для Каждого Реквизит Из ПолучитьРеквизиты() Цикл
		Структура.Вставить(Реквизит.Имя, ЭтотОбъект[Реквизит.Имя]);
	КонецЦикла;
	
	Возврат Структура;
	
КонецФункции

#Область ПоказателиУведомления

Функция ТипПоказателяКоличество(ВерсияУведомления)
	
	Если ВерсияУведомления < КонтролируемыеСделкиКлиентСервер.ВерсияУведомления_2018() Тогда
		Возврат Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 0));
	Иначе
		Возврат Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(14, 5));
	КонецЕсли;
	
КонецФункции

Функция ТипПоказателяЦена(ВерсияУведомления)
	
	Если ВерсияУведомления < КонтролируемыеСделкиКлиентСервер.ВерсияУведомления_2018() Тогда
		Возврат Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 0));
	Иначе
		Возврат Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(18, 4));
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецОбласти


