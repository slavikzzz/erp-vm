#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов


#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ФизическоеЛицо)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Поля.Добавить("Дата");
	Поля.Добавить("Номер");
	Поля.Добавить("ВидСправки");
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Данные.ВидСправки) Тогда
		Представление = СтрШаблон("%1 %2 %3 %4", Строка(Данные.ВидСправки), Данные.Номер, НСтр("ru = 'от';
																								|en = 'от'"), Данные.Дата);
	Иначе
		Шаблон = НСтр("ru = 'Справка с места работы %1 от %2';
						|en = 'Справка с места работы %1 от %2'");
		Представление = СтрШаблон(Шаблон, Данные.Номер, Данные.Дата);
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

// Возвращает описание состава документа
//
// Возвращаемое значение:
//  Структура - см. ЗарплатаКадрыСоставДокументов.НовоеОписаниеСоставаОбъекта.
Функция ОписаниеСоставаОбъекта() Экспорт
	
	ОписаниеСостава = ЗарплатаКадрыСоставДокументов.НовоеОписаниеСоставаОбъекта();
	ОписаниеСостава.ИспользоватьКраткийСостав = Ложь;
	ОписаниеСостава.ЗаполнятьТабличнуюЧастьФизическиеЛицаДокумента = Ложь;
	ОписаниеСостава.ЗаполнятьФизическиеЛицаПоСотрудникам = Ложь;
	
	ОписаниеХранения = ЗарплатаКадрыСоставДокументов.ОписаниеХраненияСотрудниковФизическихЛиц();
	ОписаниеХранения.ПолеСотрудник      = "Сотрудник";
	ОписаниеХранения.ПолеФизическоеЛицо = "ФизическоеЛицо";
	
	ОписаниеСостава.ОписаниеХраненияСотрудниковФизическихЛиц.Добавить(ОписаниеХранения);
	
	Возврат ОписаниеСостава;
	
	
КонецФункции

Функция ОписаниеДокументаДляЗаявки(СписокОбщихПолей) Экспорт
	
	СтрокаСвойств = "НазначениеСправки,ВидСправки";
	СписокПолей = СтрШаблон("%1,%2", СписокОбщихПолей, СтрокаСвойств);
	
	Возврат Новый Структура(СписокПолей);

КонецФункции

Функция БизнесПроцессМенеджер() Экспорт

	Возврат БизнесПроцессы.ИсполнениеЗаявкиСотрудника;

КонецФункции

Функция НаименованиеЗадачи(ДокументОбъект) Экспорт
	
	Если ЗначениеЗаполнено(ДокументОбъект.ВидСправки) Тогда
		ШаблонНаименования = Строка(ДокументОбъект.ВидСправки);
	Иначе
		ШаблонНаименования = НСтр("ru = 'Справка с места работы';
									|en = 'Справка с места работы'");
	КонецЕсли;
	
	Возврат СтрШаблон("%1: %2", ШаблонНаименования, Строка(ДокументОбъект.ФизическоеЛицо));

КонецФункции

Функция СодержаниеЗадачи(ДокументОбъект) Экспорт
	
	ШаблонНаименования = НСтр("ru = 'Обработать заявление справку';
								|en = 'Обработать заявление справку'");
	
	Шаблон = НСтр("ru = '%1
	|Организация: %2
	|Сотрудник: %3';
	|en = '%1
	|Организация: %2
	|Сотрудник: %3'");
	
	Возврат СтрШаблон(Шаблон, ШаблонНаименования, Строка(ДокументОбъект.Организация), Строка(ДокументОбъект.Сотрудник));
	
КонецФункции

Функция РольИсполнителяЗаявки() Экспорт

	Возврат Справочники.РолиИсполнителей.ОтветственныйЗаСправкиСМестаРаботы;

КонецФункции

Функция ИдентификаторПечатнойФормы() Экспорт
	
	Возврат "ПФ_MXL_СправкаСМестаРаботы";
	
КонецФункции

Функция ЗаявкаГотоваДляАвтоматическогоВыполненияПослеПодписанияФайла(Заявка) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВладелецФайла", Заявка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ЗаявкаСправкаСМестаРаботыКабинетСотрудникаПрисоединенныеФайлы КАК Таблица
	|ГДЕ
	|	Таблица.ВладелецФайла = &ВладелецФайла
	|	И Таблица.ФайлОтвета
	|	И Таблица.ПодписанЭП
	|	И НЕ Таблица.ПометкаУдаления";
	
	УстановитьПривилегированныйРежим(Истина);
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

#КонецОбласти

#КонецЕсли