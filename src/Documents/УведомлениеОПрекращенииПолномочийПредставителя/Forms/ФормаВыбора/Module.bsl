
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Если РегламентированнаяОтчетностьВызовСервера.ИспользуетсяОднаОрганизация() Тогда
		Элементы.Список.ПодчиненныеЭлементы.Организация.Видимость = Ложь;
	КонецЕсли;
	
	Если РегламентированнаяОтчетностьВызовСервера.ИспользуетсяОднаОрганизация() Тогда
		Элементы.ОрганизацияОтбора.Видимость = Ложь;
		
	ИначеЕсли Параметры.Свойство("Отбор") И ТипЗнч(Параметры.Отбор) = Тип("Структура")
		И Параметры.Отбор.Свойство("Владелец") И ЗначениеЗаполнено(Параметры.Отбор.Владелец) Тогда
		
		ОрганизацияОтбора = Параметры.Отбор.Владелец;
		Параметры.Отбор.Удалить("Владелец");
		
		ОтборПоОрганизации = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборПоОрганизации.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Организация");
		ОтборПоОрганизации.ПравоеЗначение = ОрганизацияОтбора;
		ОтборПоОрганизации.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборПоОрганизации.Использование = Истина;
	КонецЕсли;
	
	ТекстЗаголовка = НСтр("ru = 'Уведомление о прекращении полномочий представителя ПФР';
							|en = 'Уведомление о прекращении полномочий представителя ПФР'");
	ЭтаФорма.Заголовок = ДокументооборотСКОКлиентСервер.ЗаменитьПФРиФССнаСФР(ТекстЗаголовка, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Завершение обновления"
		ИЛИ ИмяСобытия = "Завершение расшифровки"
		ИЛИ ИмяСобытия = "Завершение групповой отправки"
		ИЛИ ТипЗнч(Источник) <> Тип("ФормаКлиентскогоПриложения")
		И (СтрНайти(ИмяСобытия, "Запись_") > 0
		ИЛИ ИмяСобытия = "Завершение отправки в контролирующий орган"
		ИЛИ ИмяСобытия = "Завершение отправки"
		ИЛИ ИмяСобытия = "Актуализация состояния отправки") Тогда
		
		Элементы.Список.Обновить();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияОтбораПриИзменении(Элемент)
	
	Если Список.Отбор.Элементы.Количество() = 0 Тогда
		ОтборОрганизация = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Иначе
		ОтборОрганизация = Список.Отбор.Элементы[0]
	КонецЕсли;
	ОтборОрганизация.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Организация");
	ОтборОрганизация.ПравоеЗначение = ОрганизацияОтбора;
	ОтборОрганизация.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборОрганизация.Использование = ЗначениеЗаполнено(ОрганизацияОтбора);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Загрузить(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузитьПослеЗагрузки", ЭтотОбъект);
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("ДопустимыеТипыФайлов", 	"XML-файл доверенности СФР (*.xml)|*.xml");
	ДополнительныеПараметры.Вставить("ВозвращатьРазмер", 		Истина);
	ДополнительныеПараметры.Вставить("МножественныйВыбор", 		Ложь);
	
	ОперацииСФайламиЭДКОКлиент.ДобавитьФайлы(
		ОписаниеОповещения, 
		Новый УникальныйИдентификатор,
		НСтр("ru = 'Выберите xml-файл';
			|en = 'Выберите xml-файл'"),
		ДополнительныеПараметры);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗагрузитьПослеЗагрузки(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ Результат.Выполнено ИЛИ Результат.ОписанияФайлов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеФайла = Результат.ОписанияФайлов[0];
	
	Если ЗначениеЗаполнено(ОписаниеФайла) Тогда
		
		Попытка
			УПРУП = ДокументооборотСКОВызовСервера.ЗначениеXMLФайлаПоАдресу(ОписаниеФайла.Адрес, "xmlns", "utf-8");
			Если УПРУП <> "http://пф.рф/УПРУП/2020-02-03" Тогда
				ТекстСообщенияПользователю = ДокументооборотСКОКлиентСервер.ЗаменитьПФРиФССнаСФР(
					НСтр("ru = 'Выбран файл не уведомления о прекращении полномочий представителя ПФР';
						|en = 'Выбран файл не уведомления о прекращении полномочий представителя ПФР'"), Истина);
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщенияПользователю);
				Возврат;
			КонецЕсли;
		Исключение
			ТекстСообщенияПользователю = ДокументооборотСКОКлиентСервер.ЗаменитьПФРиФССнаСФР(
				НСтр("ru = 'Выбран файл не уведомления о прекращении полномочий представителя ПФР';
					|en = 'Выбран файл не уведомления о прекращении полномочий представителя ПФР'"), Истина);
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщенияПользователю,,,, Истина);
			Возврат;
		КонецПопытки;
		
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗагрузитьДокументИзФайлаОбмена", Истина);
	ПараметрыФормы.Вставить("АдресФайла", ОписаниеФайла.Адрес);
	ОткрытьФорму(
		"Документ.УведомлениеОПрекращенииПолномочийПредставителя.Форма.ФормаДокумента", 
		ПараметрыФормы,
		ЭтаФорма,,,,,);
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Элементы.Список);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти
