#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры, "Месяц, Организация, ФизическоеЛицо");
	УстановитьОтборСписка();
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	ДанныеПриказа = Новый Структура("Приказ, ДатаСобытия, ВидСобытия", ТекущиеДанные.Приказ, ТекущиеДанные.Период, ТекущиеДанные.ВидСобытия);
	Оповестить("ПодборКадровыхПриказовДляВоенкомата", ДанныеПриказа, ЭтаФорма);
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьОтборСписка()
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Список, "ДатаНачала", Месяц, Истина);
		
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Список, "ДатаОкончания", КонецМесяца(Месяц), Истина);
	
	Список.КомпоновщикНастроек.ФиксированныеНастройки.Отбор.Элементы.Очистить();
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список, "Организация", Организация, , , Истина, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
		
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список, "ФизическоеЛицо", ФизическоеЛицо, , , Истина, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
		
	ВидыСобытий = Документы.СведенияОПриемеПереводеИУвольненииДляВоенкомата.РегистрируемыеВидыСобытий();
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список, "ВидСобытия", ВидыСобытий, , , Истина, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
		
КонецПроцедуры

#КонецОбласти
