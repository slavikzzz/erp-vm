#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	ДанныеДляПроведения = ДанныеДляПроведения();
	СформироватьДвиженияСведений(Движения, ДанныеДляПроведения.ДанныеКадровыхИзменений);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДанныеДляПроведения()
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СведенияДляВоенкоматаСотрудники.Ссылка.Дата КАК Период,
		|	СведенияДляВоенкоматаСотрудники.ФизическоеЛицо КАК ФизическоеЛицо,
		|	СведенияДляВоенкоматаСотрудники.Приказ КАК Приказ,
		|	СведенияДляВоенкоматаСотрудники.ДатаСобытия КАК ДатаСобытия,
		|	СведенияДляВоенкоматаСотрудники.ВидСобытия КАК ВидСобытия,
		|	СведенияДляВоенкоматаСотрудники.Ссылка.Организация КАК Организация,
		|	СведенияДляВоенкоматаСотрудники.ДатаИИсходящийНомер КАК ДатаИИсходящийНомер
		|ИЗ
		|	Документ.СведенияОПриемеПереводеИУвольненииДляВоенкомата.Сотрудники КАК СведенияДляВоенкоматаСотрудники
		|ГДЕ
		|	СведенияДляВоенкоматаСотрудники.Ссылка = &Ссылка";
	
	ДанныеКадровыхИзменений = Запрос.Выполнить().Выгрузить();
	
	ДанныеДляПроведения = Новый Структура;
	ДанныеДляПроведения.Вставить("ДанныеКадровыхИзменений", ДанныеКадровыхИзменений);
	
	Возврат ДанныеДляПроведения;
	
КонецФункции

Процедура СформироватьДвиженияСведений(Движения, ДанныеКадровыхИзменений) Экспорт 
	
	Движения.ДанныеКадровыхИзмененийДляВоенкомата.Записывать = Истина;
	Движения.ДанныеКадровыхИзмененийДляВоенкомата.Загрузить(ДанныеКадровыхИзменений);
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли