#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	МеханизмыДокумента.Добавить("РезервыПредстоящихРасходов");
	МеханизмыДокумента.Добавить("УчетДоходовРасходов");
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	МеханизмыДокумента.Добавить("ОсновныеСредства");
	МеханизмыДокумента.Добавить("РеестрДокументов");
	
	НачислениеСписаниеРезервовПредстоящихРасходовЛокализация.ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента);
	
КонецПроцедуры

#КонецОбласти

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
//
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	КомандаОтчет = Отчеты.СправкаРасчетДисконтированияРезервовПредстоящихРасходов.ДобавитьКомандуОтчета(КомандыОтчетов);
	Если КомандаОтчет <> Неопределено Тогда
		КомандаОтчет.ВидимостьВФормах = "ФормаДокумента,ФормаСписка";
	КонецЕсли;	
	
	НачислениеСписаниеРезервовПредстоящихРасходовЛокализация.ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры);

КонецПроцедуры

// Возвращает данные по объектам учета резервов предстоящих расходов: направление деятельности, остатки на регистре,
//	данные по начислению и списанию. Данные заполняются на основании одного из следующих условий:
//		- дата, переданная в параметрах заполнения входит в период действия объекта учета резервов;
//		- есть остатки на регистре на дату, переданную в параметрах.
//	Параметры:
//		ПараметрыЗаполнения - Структура - структура, содержащая следующие ключи:
//			Дата - дата и время - дата, на которую необходимо получить данные регистра "РезервыПредстоящихРасходов";
//			Ссылка - ДокументСсылка - ссылка на документ, остатки по которому не надо учитывать при получении данных из регистра;
//			Организация - СправочникСсылка.Организации - Организация, по которой необходимо получить данные;
//			ВидРезервов - СправочникСсылка.Резервы - Резерв, по которому необходимо получить данные;
//		ОбъектыУчетаРезервов - СправочникСсылка.ОбъектыУчетаРезервовПредстоящихРасходов - если задан, то возвращаются данные только по этому объекту учета;
//
// Возвращаемое значение:
//		РезультатЗапроса - Данные результата запроса.
//
Функция ДанныеДляЗаполненияРезервов(ПараметрыЗаполнения, ОбъектыУчетаРезервов = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РезервыПредстоящихРасходовОстатки.Организация КАК Организация,
	|	РезервыПредстоящихРасходовОстатки.ВидРезервов КАК ВидРезервов,
	|	РезервыПредстоящихРасходовОстатки.ОбъектУчетаРезервов КАК ОбъектУчетаРезервов,
	|	-РезервыПредстоящихРасходовОстатки.СуммаРеглОстаток КАК СуммаРеглОстаток,
	|	-РезервыПредстоящихРасходовОстатки.СуммаНУОстаток КАК СуммаНУОстаток,
	|	-РезервыПредстоящихРасходовОстатки.СуммаУпрОстаток КАК СуммаУпрОстаток
	|ПОМЕСТИТЬ ОстаткиРезервовПредстоящихРасходов
	|ИЗ
	|	РегистрНакопления.РезервыПредстоящихРасходов.Остатки(
	|			&Граница,
	|			Организация = &Организация
	|				И ВидРезервов = &ВидРезервов
	|				И (НЕ &ОтборПоОбъектуУчетаРезервов ИЛИ ОбъектУчетаРезервов В (&ОбъектыУчетаРезервов))) КАК РезервыПредстоящихРасходовОстатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ВидРезервов,
	|	ОбъектУчетаРезервов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СрокиДисконтированияРезервовПредстоящихРасходов.ОбъектУчетаРезервов КАК ОбъектУчетаРезервов,
	|	СрокиДисконтированияРезервовПредстоящихРасходов.ДатаИсполнения КАК ДатаИсполнения
	|ПОМЕСТИТЬ СрокиДисконтированияРезервов
	|ИЗ
	|	Справочник.ОбъектыУчетаРезервовПредстоящихРасходов КАК ОбъектыУчетаРезервовПредстоящихРасходов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СрокиДисконтированияРезервовПредстоящихРасходов.СрезПоследних(
	|			&ТекДата,
	|			НЕ &ОтборПоОбъектуУчетаРезервов ИЛИ ОбъектУчетаРезервов В (&ОбъектыУчетаРезервов)) КАК СрокиДисконтированияРезервовПредстоящихРасходов
	|		ПО (ОбъектыУчетаРезервовПредстоящихРасходов.Ссылка = СрокиДисконтированияРезервовПредстоящихРасходов.ОбъектУчетаРезервов)
	|ГДЕ
	|	ОбъектыУчетаРезервовПредстоящихРасходов.ВидРезервов = &ВидРезервов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектУчетаРезервов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОбъектыУчетаРезервовПредстоящихРасходов.Ссылка КАК ОбъектУчетаРезервов,
	|	ОбъектыУчетаРезервовПредстоящихРасходов.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ОбъектыУчетаРезервовПредстоящихРасходов.ПодразделениеНачисления КАК ПодразделениеНачисления,
	|	ОбъектыУчетаРезервовПредстоящихРасходов.НаправлениеДеятельностиНачисления КАК НаправлениеДеятельностиНачисления,
	|	ОбъектыУчетаРезервовПредстоящихРасходов.СтатьяРасходовНачисления КАК СтатьяРасходовНачисления,
	|	ОбъектыУчетаРезервовПредстоящихРасходов.АналитикаРасходовНачисления КАК АналитикаРасходовНачисления,
	|	ОбъектыУчетаРезервовПредстоящихРасходов.ПодразделениеСписания КАК ПодразделениеСписания,
	|	ОбъектыУчетаРезервовПредстоящихРасходов.НаправлениеДеятельностиСписания КАК НаправлениеДеятельностиСписания,
	|	ОбъектыУчетаРезервовПредстоящихРасходов.СтатьяДоходовСписания КАК СтатьяДоходовСписания,
	|	ОбъектыУчетаРезервовПредстоящихРасходов.АналитикаДоходовСписания КАК АналитикаДоходовСписания,
	|	ОбъектыУчетаРезервовПредстоящихРасходов.ДопускаетсяРазличноеФормированиеРезервовБУиНУ КАК ДопускаетсяРазличноеФормированиеРезервовБУиНУ,
	|	ЕСТЬNULL(РезервыПредстоящихРасходовОстатки.СуммаРеглОстаток, 0) КАК ТекущаяСуммаРегл,
	|	ЕСТЬNULL(РезервыПредстоящихРасходовОстатки.СуммаНУОстаток, 0) КАК ТекущаяСуммаНУ,
	|	ЕСТЬNULL(РезервыПредстоящихРасходовОстатки.СуммаУпрОстаток, 0) КАК ТекущаяСуммаУпр,
	|	ЕСТЬNULL(СрокиДисконтированияРезервов.ДатаИсполнения, ОбъектыУчетаРезервовПредстоящихРасходов.КонецПериода) КАК ДатаИсполнения
	|ИЗ
	|	Справочник.ОбъектыУчетаРезервовПредстоящихРасходов КАК ОбъектыУчетаРезервовПредстоящихРасходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиРезервовПредстоящихРасходов КАК РезервыПредстоящихРасходовОстатки
	|		ПО (РезервыПредстоящихРасходовОстатки.ОбъектУчетаРезервов = ОбъектыУчетаРезервовПредстоящихРасходов.Ссылка)
	|			И (РезервыПредстоящихРасходовОстатки.Организация = ОбъектыУчетаРезервовПредстоящихРасходов.Организация)
	|			И (РезервыПредстоящихРасходовОстатки.ВидРезервов = ОбъектыУчетаРезервовПредстоящихРасходов.ВидРезервов)
	|		ЛЕВОЕ СОЕДИНЕНИЕ СрокиДисконтированияРезервов КАК СрокиДисконтированияРезервов
	|		ПО (СрокиДисконтированияРезервов.ОбъектУчетаРезервов = ОбъектыУчетаРезервовПредстоящихРасходов.Ссылка)
	|ГДЕ
	|	ОбъектыУчетаРезервовПредстоящихРасходов.Организация = &Организация
	|	И ОбъектыУчетаРезервовПредстоящихРасходов.ВидРезервов = &ВидРезервов
	|	И ВЫБОР
	|			КОГДА &ОтборПоОбъектуУчетаРезервов
	|				ТОГДА ОбъектыУчетаРезервовПредстоящихРасходов.Ссылка В (&ОбъектыУчетаРезервов)
	|			ИНАЧЕ ЕСТЬNULL(РезервыПредстоящихРасходовОстатки.СуммаРеглОстаток, 0) > 0
	|					ИЛИ ЕСТЬNULL(РезервыПредстоящихРасходовОстатки.СуммаНУОстаток, 0) > 0
	|					ИЛИ ЕСТЬNULL(РезервыПредстоящихРасходовОстатки.СуммаУпрОстаток, 0) > 0
	|					ИЛИ ОбъектыУчетаРезервовПредстоящихРасходов.НачалоПериода <= &ТекДата
	|						И ОбъектыУчетаРезервовПредстоящихРасходов.ПометкаУдаления = ЛОЖЬ
	|						И (ОбъектыУчетаРезервовПредстоящихРасходов.КонецПериода >= &ТекДата
	|							ИЛИ ОбъектыУчетаРезервовПредстоящихРасходов.КонецПериода = ДАТАВРЕМЯ(1, 1, 1))
	|		КОНЕЦ;
	|
	|УНИЧТОЖИТЬ ОстаткиРезервовПредстоящихРасходов";
	
	ДисконтированиеОценочногоОбязательства = Ложь;
	Если ЗначениеЗаполнено(ПараметрыЗаполнения.ВидРезервов) Тогда
		ДисконтированиеОценочногоОбязательства = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			ПараметрыЗаполнения.ВидРезервов, "ДисконтированиеРезервов");
	КонецЕсли;
	
	Если ДисконтированиеОценочногоОбязательства Тогда
		ДатаОстатков = НачалоМесяца(ПараметрыЗаполнения.Дата);
	Иначе
		ДатаОстатков = Новый Граница(Новый МоментВремени(ПараметрыЗаполнения.Дата, ПараметрыЗаполнения.Ссылка), ВидГраницы.Исключая);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Граница", ДатаОстатков);
	Запрос.УстановитьПараметр("ТекДата", ПараметрыЗаполнения.Дата);
	Запрос.УстановитьПараметр("Организация", ПараметрыЗаполнения.Организация);
	Запрос.УстановитьПараметр("ВидРезервов", ПараметрыЗаполнения.ВидРезервов);
	Запрос.УстановитьПараметр("ОбъектыУчетаРезервов", ОбъектыУчетаРезервов);
	Запрос.УстановитьПараметр("ОтборПоОбъектуУчетаРезервов", ОбъектыУчетаРезервов <> Неопределено И ОбъектыУчетаРезервов.Количество() > 0);
	
	Результат = Запрос.Выполнить();
	
	Возврат Результат;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

// Возвращает параметры выбора статей в документе.
// 
// Параметры:
// 	ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации -См. ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики
//
// Возвращаемое значение:
//  Массив Из см. ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики -
//
Функция ПараметрыВыбораСтатейИАналитик(ХозяйственнаяОперация) Экспорт
	
	МассивПараметровВыбора = Новый Массив;
	
	#Область РезервыСтатьяРасходовНачисления
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект.Резервы";
	ПараметрыВыбора.Статья      = "СтатьяРасходовНачисления";
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходовНачисления";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("РезервыСтатьяРасходовНачисления");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("РезервыАналитикаРасходовНачисления");
	
	МассивПараметровВыбора.Добавить(ПараметрыВыбора);
	#КонецОбласти
	
	#Область ПрочиеДоходыСтатьяДоходов
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным =    "Объект.Резервы";
	ПараметрыВыбора.Статья = "СтатьяДоходовСписания";
	
	ПараметрыВыбора.ВыборСтатьиДоходов = Истина;
	ПараметрыВыбора.АналитикаДоходов = "АналитикаДоходовСписания";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("РезервыСтатьяДоходовСписания");	
	ПараметрыВыбора.ЭлементыФормы.АналитикаДоходов.Добавить("РезервыАналитикаДоходовСписания");
	
	МассивПараметровВыбора.Добавить(ПараметрыВыбора);
	#КонецОбласти
	
	Возврат МассивПараметровВыбора;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка, ДокументОбъект - ссылка на документ или объект, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  см. ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Если ТипЗнч(Документ) = Тип("ДокументОбъект.НачислениеСписаниеРезервовПредстоящихРасходов") Тогда
		ДокументСсылка = Документ.Ссылка;
	Иначе
		ДокументСсылка = Документ;
	КонецЕсли;
	
	Запрос			= Новый Запрос;
	ТекстыЗапроса	= Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
		
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса
		
		ТекстЗапросаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаПрочиеДоходы(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаСтоимостьОС(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаРезервыПредстоящихРасходов(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаСтавкиДисконтированияРезервов(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаСрокиДисконтированияРезервовПредстоящихРасходов(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДокументыПоОС(Запрос, ТекстыЗапроса, Регистры);
		
		//++ НЕ УТКА
		ТекстЗапросаТаблицаОтражениеДокументовВМеждународномУчете(ТекстыЗапроса, Регистры);
		//-- НЕ УТКА
		
		НачислениеСписаниеРезервовПредстоящихРасходовЛокализация.ДополнитьТекстыЗапросовПроведения(Запрос,
																									ТекстыЗапроса,
																									Регистры);
	КонецЕсли;
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

// Формирует таблицы движений при отложенном проведении.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка - ссылка на документ, для которого формируются движения
//  МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Содержит вспомогательные временные таблицы, которые могут использоваться для формирования движений.
//
// Возвращаемое значение:
//  см. ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения
//
Функция ТаблицыОтложенногоФормированияДвижений(ДокументСсылка, МенеджерВременныхТаблиц) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	ТекстЗапросаПрочиеРасходы(Запрос, ТекстыЗапроса, Неопределено);
	ТекстЗапросаПрочиеДоходы(Запрос, ТекстыЗапроса, Неопределено);
	ТекстЗапросаСтоимостьОС(Запрос, ТекстыЗапроса, Неопределено);
	
	//++ НЕ УТКА
	ТекстЗапросаТаблицаОтражениеДокументовВМеждународномУчете(ТекстыЗапроса);
	//-- НЕ УТКА
	
	НачислениеСписаниеРезервовПредстоящихРасходовЛокализация.ТаблицыОтложенногоФормированияДвижений(ТекстыЗапроса);
	
	ТаблицыДляДвижений = ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(
							Запрос, ТекстыЗапроса, Неопределено);
	
	Возврат ТаблицыДляДвижений;
	
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)

	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Дата КАК Период,
	|	НАЧАЛОПЕРИОДА(ДанныеДокумента.Дата, МЕСЯЦ) КАК НачалоПериода,
	|	КОНЕЦПЕРИОДА(ДанныеДокумента.Дата, МЕСЯЦ) КАК ОкончаниеПериода,
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.ВидРезервов КАК ВидРезервов,
	|	ЕСТЬNULL(ДанныеДокумента.ВидРезервов.ТипРезерва = ЗНАЧЕНИЕ(Перечисление.ТипыРезервовПредстоящихРасходов.ЛиквидационныеОбязательства), ЛОЖЬ) КАК ЭтоРезервыЛиквидационныхОбязательств,
	|	ЕСТЬNULL(ДанныеДокумента.ВидРезервов.ДисконтированиеРезервов, ЛОЖЬ) КАК ДисконтированиеОценочногоОбязательства,
	|	ДанныеДокумента.СтавкаДисконтирования КАК СтавкаДисконтирования,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.Комментарий КАК Комментарий,
	|	ДанныеДокумента.Проведен КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления КАК ПометкаУдаления,
	|	ДанныеДокумента.Ответственный КАК Ответственный
	|ИЗ
	|	Документ.НачислениеСписаниеРезервовПредстоящихРасходов КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("КонецМесяцаГраница",
		Новый Граница(Запрос.Параметры.ОкончаниеПериода, ВидГраницы.Включая));
	
	Если НЕ РезервыПредстоящихРасходов.ИспользуетсяУчетДисконтированияРезервов(Запрос.Параметры.ОкончаниеПериода, Запрос.Параметры.Организация) Тогда
		Запрос.УстановитьПараметр("ДисконтированиеОценочногоОбязательства", Ложь);	
	КонецЕсли;
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
	Запрос.УстановитьПараметр("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.НачислениеСписаниеРезервовПредстоящихРасходов"));
	Запрос.УстановитьПараметр("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	Запрос.УстановитьПараметр("ХозОперацияДляРеестра", Перечисления.ХозяйственныеОперации.НачислениеСписаниеРезервовПредстоящихРасходов);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаВтНачисления(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтНачисления";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтДвиженияДисконтирования", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтДвиженияДисконтирования(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Строки.ПодразделениеНачисления КАК Подразделение,
	|	Строки.НаправлениеДеятельностиНачисления КАК НаправлениеДеятельностиНачисления,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	Строки.ОбъектУчетаРезервов КАК ОбъектУчетаРезервов,
	|	Строки.СтатьяРасходовНачисления КАК СтатьяРасходов,
	|	Строки.АналитикаРасходовНачисления КАК АналитикаРасходов,
	|	ВЫБОР
	|		КОГДА Строки.УстановитьНовуюСуммуРегл
	|				И Строки.ТекущаяСуммаРегл < Строки.НоваяСуммаРегл
	|			ТОГДА Строки.НоваяСуммаРегл - Строки.ТекущаяСуммаРегл - ЕСТЬNULL(Дисконтирование.СуммаДисконтированияРегл, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаРегл,
	|	ВЫБОР
	|		КОГДА &ЭтоРезервыЛиквидационныхОбязательств
	|			ТОГДА 0
	|		КОГДА НЕ Строки.ОбъектУчетаРезервов.ДопускаетсяРазличноеФормированиеРезервовБУиНУ
	|				И Строки.УстановитьНовуюСуммуРегл
	|				И Строки.ТекущаяСуммаРегл < Строки.НоваяСуммаРегл
	|			ТОГДА Строки.НоваяСуммаРегл - Строки.ТекущаяСуммаРегл
	|		КОГДА Строки.УстановитьНовуюСуммуНУ
	|				И Строки.ТекущаяСуммаНУ < Строки.НоваяСуммаНУ
	|			ТОГДА Строки.НоваяСуммаНУ - Строки.ТекущаяСуммаНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаНУ,
	|	ВЫБОР
	|		КОГДА НЕ &УправленческийУчетОрганизаций
	|			ТОГДА 0
	|		КОГДА Строки.УстановитьНовуюСуммаУпр
	|				И Строки.ТекущаяСуммаУпр < Строки.НоваяСуммаУпр
	|			ТОГДА Строки.НоваяСуммаУпр - Строки.ТекущаяСуммаУпр - ЕСТЬNULL(Дисконтирование.СуммаДисконтированияУпр, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаУпр,
	|	Строки.ТекущаяСуммаРегл КАК ТекущаяСуммаРегл,
	|	Строки.ТекущаяСуммаУпр КАК ТекущаяСуммаУпр,
	|	ЛОЖЬ КАК ЭтоИзменениеТолькоПроцентовДисконтирования,
	|	Строки.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ВтНачисления
	|ИЗ
	|	Документ.НачислениеСписаниеРезервовПредстоящихРасходов.Резервы КАК Строки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтДвиженияДисконтирования КАК Дисконтирование
	|		ПО Строки.ОбъектУчетаРезервов = Дисконтирование.ОбъектУчетаРезервов
	|			И Строки.ИдентификаторСтроки = Дисконтирование.ИдентификаторСтроки
	|ГДЕ
	|	Строки.Ссылка = &Ссылка
	|	И (Строки.УстановитьНовуюСуммуРегл
	|				И Строки.ТекущаяСуммаРегл < Строки.НоваяСуммаРегл
	|			ИЛИ Строки.УстановитьНовуюСуммаУпр
	|				И Строки.ТекущаяСуммаУпр < Строки.НоваяСуммаУпр
	|			ИЛИ Строки.УстановитьНовуюСуммуНУ
	|				И Строки.ТекущаяСуммаНУ < Строки.НоваяСуммаНУ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Строки.ПодразделениеНачисления,
	|	Строки.НаправлениеДеятельностиНачисления,
	|	Строки.НаправлениеДеятельности,
	|	Строки.ОбъектУчетаРезервов,
	|	Строки.СтатьяРасходовНачисления,
	|	Строки.АналитикаРасходовНачисления,
	|	ВЫБОР
	|		КОГДА Дисконтирование.ВидДвиженияРегл = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА Дисконтирование.СуммаДисконтированияРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	0,
	|	ВЫБОР
	|		КОГДА НЕ &УправленческийУчетОрганизаций
	|			ТОГДА 0
	|		КОГДА Дисконтирование.ВидДвиженияУпр = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА Дисконтирование.СуммаДисконтированияУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	Строки.ТекущаяСуммаРегл,
	|	Строки.ТекущаяСуммаУпр,
	|	ИСТИНА,
	|	Строки.ИдентификаторСтроки
	|ИЗ
	|	Документ.НачислениеСписаниеРезервовПредстоящихРасходов.Резервы КАК Строки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДвиженияДисконтирования КАК Дисконтирование
	|		ПО Строки.ОбъектУчетаРезервов = Дисконтирование.ОбъектУчетаРезервов
	|			И Строки.ИдентификаторСтроки = Дисконтирование.ИдентификаторСтроки
	|ГДЕ
	|	Строки.Ссылка = &Ссылка
	|	И (НЕ Строки.УстановитьНовуюСуммуРегл ИЛИ Строки.ТекущаяСуммаРегл = Строки.НоваяСуммаРегл)
	|	И (НЕ Строки.УстановитьНовуюСуммаУпр ИЛИ Строки.ТекущаяСуммаУпр = Строки.НоваяСуммаУпр)
	|	И (НЕ Строки.УстановитьНовуюСуммуНУ ИЛИ Строки.ТекущаяСуммаНУ = Строки.НоваяСуммаНУ)
	|	И (Дисконтирование.ВидДвиженияРегл = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ИЛИ Дисконтирование.ВидДвиженияУпр = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаРасходов,
	|	ИдентификаторСтроки";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтСписания(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтСписания";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтДвиженияДисконтирования", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтДвиженияДисконтирования(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Строки.ПодразделениеСписания КАК Подразделение,
	|	Строки.НаправлениеДеятельностиСписания КАК НаправлениеДеятельностиСписания,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	Строки.ОбъектУчетаРезервов КАК ОбъектУчетаРезервов,
	|	Строки.СтатьяДоходовСписания КАК СтатьяДоходов,
	|	Строки.СтатьяРасходовНачисления КАК СтатьяРасходов,
	|	Строки.АналитикаДоходовСписания КАК АналитикаДоходов,
	|	Строки.АналитикаРасходовНачисления КАК АналитикаРасходов,
	|	ВЫБОР
	|		КОГДА Строки.УстановитьНовуюСуммуРегл
	|				И Строки.ТекущаяСуммаРегл > Строки.НоваяСуммаРегл
	|			ТОГДА Строки.ТекущаяСуммаРегл - Строки.НоваяСуммаРегл - ЕСТЬNULL(Дисконтирование.СуммаДисконтированияРегл, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаРегл,
	|	ВЫБОР
	|		КОГДА НЕ Строки.ОбъектУчетаРезервов.ДопускаетсяРазличноеФормированиеРезервовБУиНУ
	|				И Строки.УстановитьНовуюСуммуРегл
	|				И Строки.ТекущаяСуммаРегл > Строки.НоваяСуммаРегл
	|			ТОГДА Строки.ТекущаяСуммаРегл - Строки.НоваяСуммаРегл
	|		КОГДА Строки.УстановитьНовуюСуммуНУ
	|				И Строки.ТекущаяСуммаНУ > Строки.НоваяСуммаНУ
	|			ТОГДА Строки.ТекущаяСуммаНУ - Строки.НоваяСуммаНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаНУ,
	|	ВЫБОР
	|		КОГДА НЕ &УправленческийУчетОрганизаций
	|			ТОГДА 0
	|		КОГДА Строки.УстановитьНовуюСуммаУпр
	|				И Строки.ТекущаяСуммаУпр > Строки.НоваяСуммаУпр
	|			ТОГДА Строки.ТекущаяСуммаУпр - Строки.НоваяСуммаУпр - ЕСТЬNULL(Дисконтирование.СуммаДисконтированияУпр, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаУпр,
	|	Строки.ТекущаяСуммаРегл КАК ТекущаяСуммаРегл,
	|	Строки.ТекущаяСуммаУпр КАК ТекущаяСуммаУпр,
	|	ЛОЖЬ КАК ЭтоИзменениеТолькоПроцентовДисконтирования,
	|	Строки.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ВтСписания
	|ИЗ
	|	Документ.НачислениеСписаниеРезервовПредстоящихРасходов.Резервы КАК Строки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтДвиженияДисконтирования КАК Дисконтирование
	|		ПО Строки.ОбъектУчетаРезервов = Дисконтирование.ОбъектУчетаРезервов
	|			И Строки.ИдентификаторСтроки = Дисконтирование.ИдентификаторСтроки
	|ГДЕ
	|	Строки.Ссылка = &Ссылка
	|	И (Строки.УстановитьНовуюСуммуРегл
	|				И Строки.ТекущаяСуммаРегл > Строки.НоваяСуммаРегл
	|			ИЛИ Строки.УстановитьНовуюСуммаУпр
	|				И Строки.ТекущаяСуммаУпр > Строки.НоваяСуммаУпр
	|			ИЛИ Строки.УстановитьНовуюСуммуНУ
	|				И Строки.ТекущаяСуммаНУ > Строки.НоваяСуммаНУ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Строки.ПодразделениеСписания,
	|	Строки.НаправлениеДеятельностиСписания,
	|	Строки.НаправлениеДеятельности,
	|	Строки.ОбъектУчетаРезервов,
	|	Строки.СтатьяДоходовСписания,
	|	Строки.СтатьяРасходовНачисления,
	|	Строки.АналитикаДоходовСписания,
	|	Строки.АналитикаРасходовНачисления,
	|	ВЫБОР
	|		КОГДА Дисконтирование.ВидДвиженияРегл = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА Дисконтирование.СуммаДисконтированияРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	0,
	|	ВЫБОР
	|		КОГДА НЕ &УправленческийУчетОрганизаций
	|			ТОГДА 0
	|		КОГДА Дисконтирование.ВидДвиженияУпр = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА Дисконтирование.СуммаДисконтированияУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	Строки.ТекущаяСуммаРегл,
	|	Строки.ТекущаяСуммаУпр,
	|	ИСТИНА,
	|	Строки.ИдентификаторСтроки
	|ИЗ
	|	Документ.НачислениеСписаниеРезервовПредстоящихРасходов.Резервы КАК Строки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДвиженияДисконтирования КАК Дисконтирование
	|		ПО Строки.ОбъектУчетаРезервов = Дисконтирование.ОбъектУчетаРезервов
	|			И Строки.ИдентификаторСтроки = Дисконтирование.ИдентификаторСтроки
	|ГДЕ
	|	Строки.Ссылка = &Ссылка
	|	И (НЕ Строки.УстановитьНовуюСуммуРегл ИЛИ Строки.ТекущаяСуммаРегл = Строки.НоваяСуммаРегл)
	|	И (НЕ Строки.УстановитьНовуюСуммаУпр ИЛИ Строки.ТекущаяСуммаУпр = Строки.НоваяСуммаУпр)
	|	И (НЕ Строки.УстановитьНовуюСуммуНУ ИЛИ Строки.ТекущаяСуммаНУ = Строки.НоваяСуммаНУ)
	|	И (Дисконтирование.ВидДвиженияРегл = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ИЛИ Дисконтирование.ВидДвиженияУпр = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаРасходов,
	|	ИдентификаторСтроки";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтДисконтирование(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтДисконтирование";
	
	// Сумма дисконтирования определяется по формуле:
	// S% = Sn * (1 – 1/(1 + Ставка)^(tn/365)), где
	// Sn – номинальная стоимость ликвидационного обязательства,
	// tn – это время в днях, оставшееся до даты исполнения обязательства.
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаДисконтирования.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаДисконтирования.ОбъектУчетаРезервов КАК ОбъектУчетаРезервов,
	|	ТаблицаДисконтирования.СуммаРегл * (1 - 1 / POW(1 + &СтавкаДисконтирования / 100, РАЗНОСТЬДАТ(ВЫБОР
	|					КОГДА НАЧАЛОПЕРИОДА(&НачалоПериода, МЕСЯЦ) > НАЧАЛОПЕРИОДА(ТаблицаДисконтирования.ДатаИсполнения, МЕСЯЦ)
	|						ТОГДА ТаблицаДисконтирования.ДатаИсполнения
	|					ИНАЧЕ &НачалоПериода
	|				КОНЕЦ,
	|		ТаблицаДисконтирования.ДатаИсполнения, ДЕНЬ) / 365)) КАК СуммаДисконтированияРегл,
	|	ТаблицаДисконтирования.СуммаУпр * (1 - 1 / POW(1 + &СтавкаДисконтирования / 100, РАЗНОСТЬДАТ(ВЫБОР
	|					КОГДА НАЧАЛОПЕРИОДА(&НачалоПериода, МЕСЯЦ) > НАЧАЛОПЕРИОДА(ТаблицаДисконтирования.ДатаИсполнения, МЕСЯЦ)
	|						ТОГДА ТаблицаДисконтирования.ДатаИсполнения
	|					ИНАЧЕ &НачалоПериода
	|				КОНЕЦ,
	|		ТаблицаДисконтирования.ДатаИсполнения, ДЕНЬ) / 365)) КАК СуммаДисконтированияУпр,
	|	ТаблицаДисконтирования.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ВтДисконтирование
	|ИЗ
	|	(ВЫБРАТЬ
	|		Строки.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		Строки.ОбъектУчетаРезервов КАК ОбъектУчетаРезервов,
	|		ВЫБОР
	|			КОГДА Строки.УстановитьНовуюСуммуРегл
	|				ТОГДА Строки.НоваяСуммаРегл
	|			ИНАЧЕ Строки.ТекущаяСуммаРегл
	|		КОНЕЦ КАК СуммаРегл,
	|		ВЫБОР
	|			КОГДА Строки.УстановитьНовуюСуммаУпр
	|				ТОГДА Строки.НоваяСуммаУпр
	|			ИНАЧЕ Строки.ТекущаяСуммаУпр
	|		КОНЕЦ КАК СуммаУпр,
	|		ВЫБОР
	|			КОГДА Строки.УстановитьНовуюДатуИсполнения
	|				ТОГДА Строки.НоваяДатаИсполнения
	|			ИНАЧЕ Строки.ДатаИсполнения
	|		КОНЕЦ КАК ДатаИсполнения,
	|		Строки.ИдентификаторСтроки КАК ИдентификаторСтроки
	|	ИЗ
	|		Документ.НачислениеСписаниеРезервовПредстоящихРасходов.Резервы КАК Строки
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиДисконтированияРезервов.СрезПоследних(&НачалоПериода,
	|				Организация = &Организация
	|			И ВидРезервов = &ВидРезервов
	|			И Регистратор <> &Ссылка) КАК СтавкиДисконтированияРезервов
	|			ПО ИСТИНА
	|	ГДЕ
	|		&ДисконтированиеОценочногоОбязательства
	|		И Строки.Ссылка = &Ссылка
	|		И (Строки.НоваяСуммаРегл <> 0 ИЛИ Строки.ТекущаяСуммаРегл <> 0 ИЛИ Строки.НоваяСуммаУпр <> 0 ИЛИ Строки.ТекущаяСуммаУпр <> 0)
	|		И (Строки.УстановитьНовуюСуммуРегл
	|					И Строки.НоваяСуммаРегл <> Строки.ТекущаяСуммаРегл
	|				ИЛИ Строки.УстановитьНовуюСуммаУпр
	|					И Строки.НоваяСуммаУпр <> Строки.ТекущаяСуммаУпр
	|				ИЛИ Строки.УстановитьНовуюДатуИсполнения
	|					И Строки.НоваяДатаИсполнения <> Строки.ДатаИсполнения
	|		ИЛИ ЕСТЬNULL(СтавкиДисконтированияРезервов.СтавкаДисконтирования,
	|			0) <> &СтавкаДисконтирования)) ТаблицаДисконтирования
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НаправлениеДеятельности,
	|	ОбъектУчетаРезервов";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтОстаткиРезервов(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтОстаткиРезервов";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтДисконтирование", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтДисконтирование(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ОстаткиРезервов.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ОстаткиРезервов.ОбъектУчетаРезервов КАК ОбъектУчетаРезервов,
	|	ОстаткиРезервов.СуммаДисконтированияУпрОстаток КАК СуммаДисконтированияУпр,
	|	ОстаткиРезервов.СуммаДисконтированияРеглОстаток КАК СуммаДисконтированияРегл
	|ПОМЕСТИТЬ ВтОстаткиРезервов
	|ИЗ
	|	РегистрНакопления.РезервыПредстоящихРасходов.Остатки(&НачалоПериода, &ДисконтированиеОценочногоОбязательства
	|	И (Организация, ВидРезервов, НаправлениеДеятельности, ОбъектУчетаРезервов) В
	|		(ВЫБРАТЬ
	|			&Организация,
	|			&ВидРезервов,
	|			Т.НаправлениеДеятельности,
	|			Т.ОбъектУчетаРезервов
	|		ИЗ
	|			ВтДисконтирование КАК Т)) КАК ОстаткиРезервов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НаправлениеДеятельности,
	|	ОбъектУчетаРезервов";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтДвиженияДисконтирования(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтДвиженияДисконтирования";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтДисконтирование", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтДисконтирование(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтОстаткиРезервов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтОстаткиРезервов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаДисконтирования.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаДисконтирования.ОбъектУчетаРезервов КАК ОбъектУчетаРезервов,
	|	ТаблицаДисконтирования.ВидДвиженияРегл КАК ВидДвиженияРегл,
	|	ТаблицаДисконтирования.СуммаДисконтированияРегл КАК СуммаДисконтированияРегл,
	|	ТаблицаДисконтирования.ВидДвиженияУпр КАК ВидДвиженияУпр,
	|	ТаблицаДисконтирования.СуммаДисконтированияУпр КАК СуммаДисконтированияУпр,
	|	ТаблицаДисконтирования.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ВтДвиженияДисконтирования
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаДисконтирования.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ТаблицаДисконтирования.ОбъектУчетаРезервов КАК ОбъектУчетаРезервов,
	|		ВЫБОР
	|			КОГДА ТаблицаДисконтирования.СуммаДисконтированияРегл > ЕСТЬNULL(ВтОстаткиРезервов.СуммаДисконтированияРегл, 0)
	|				ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		КОНЕЦ КАК ВидДвиженияРегл,
	|		ВЫБОР
	|			КОГДА ТаблицаДисконтирования.СуммаДисконтированияРегл > ЕСТЬNULL(ВтОстаткиРезервов.СуммаДисконтированияРегл, 0)
	|				ТОГДА ТаблицаДисконтирования.СуммаДисконтированияРегл - ЕСТЬNULL(ВтОстаткиРезервов.СуммаДисконтированияРегл, 0)
	|			ИНАЧЕ ЕСТЬNULL(ВтОстаткиРезервов.СуммаДисконтированияРегл, 0) - ТаблицаДисконтирования.СуммаДисконтированияРегл
	|		КОНЕЦ КАК СуммаДисконтированияРегл,
	|		ВЫБОР
	|			КОГДА ТаблицаДисконтирования.СуммаДисконтированияУпр > ЕСТЬNULL(ВтОстаткиРезервов.СуммаДисконтированияУпр, 0)
	|				ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		КОНЕЦ КАК ВидДвиженияУпр,
	|		ВЫБОР
	|			КОГДА ТаблицаДисконтирования.СуммаДисконтированияУпр > ЕСТЬNULL(ВтОстаткиРезервов.СуммаДисконтированияУпр, 0)
	|				ТОГДА ТаблицаДисконтирования.СуммаДисконтированияУпр - ЕСТЬNULL(ВтОстаткиРезервов.СуммаДисконтированияУпр, 0)
	|			ИНАЧЕ ЕСТЬNULL(ВтОстаткиРезервов.СуммаДисконтированияУпр, 0) - ТаблицаДисконтирования.СуммаДисконтированияУпр
	|		КОНЕЦ КАК СуммаДисконтированияУпр,
	|		ТаблицаДисконтирования.ИдентификаторСтроки КАК ИдентификаторСтроки
	|	ИЗ
	|		ВтДисконтирование КАК ТаблицаДисконтирования
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВтОстаткиРезервов КАК ВтОстаткиРезервов
	|			ПО ТаблицаДисконтирования.НаправлениеДеятельности = ВтОстаткиРезервов.НаправлениеДеятельности
	|				И ТаблицаДисконтирования.ОбъектУчетаРезервов = ВтОстаткиРезервов.ОбъектУчетаРезервов) КАК ТаблицаДисконтирования
	|ГДЕ
	|	(ТаблицаДисконтирования.СуммаДисконтированияРегл <> 0
	|			ИЛИ ТаблицаДисконтирования.СуммаДисконтированияУпр <> 0)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектУчетаРезервов,
	|	ИдентификаторСтроки";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтДанныеОсновныхСредств(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтДанныеОсновныхСредств";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПорядокУчетаОСУУ", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПорядокУчетаОСУУ(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПорядокУчетаОСБУ", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПорядокУчетаОСБУ(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПорядокУчетаОС", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПорядокУчетаОС(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтМестонахождениеОС", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтМестонахождениеОС(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Строки.АналитикаРасходовНачисления КАК ОсновноеСредство,
	|	Строки.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ЕСТЬNULL(ПорядокУчетаОСУУ.Состояние, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету) КАК
	|		ПринятоКУчетуУпр,
	|	ЕСТЬNULL(ПорядокУчетаОСБУ.СостояниеБУ, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету) КАК
	|		ПринятоКУчетуРегл,
	|	ПорядокУчетаОСБУ.НалогообложениеНДС КАК НалогообложениеНДС,
	|	ПорядокУчетаОС.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	ПорядокУчетаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	МестонахождениеОС.Подразделение КАК Подразделение
	|ПОМЕСТИТЬ ВтДанныеОсновныхСредств
	|ИЗ
	|	Документ.НачислениеСписаниеРезервовПредстоящихРасходов.Резервы КАК Строки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПорядокУчетаОСУУ КАК ПорядокУчетаОСУУ
	|		ПО Строки.АналитикаРасходовНачисления = ПорядокУчетаОСУУ.ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПорядокУчетаОСБУ КАК ПорядокУчетаОСБУ
	|		ПО Строки.АналитикаРасходовНачисления = ПорядокУчетаОСБУ.ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПорядокУчетаОС КАК ПорядокУчетаОС
	|		ПО Строки.АналитикаРасходовНачисления = ПорядокУчетаОС.ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтМестонахождениеОС КАК МестонахождениеОС
	|		ПО Строки.АналитикаРасходовНачисления = МестонахождениеОС.ОсновноеСредство
	|ГДЕ
	|	&ЭтоРезервыЛиквидационныхОбязательств
	|	И Строки.Ссылка = &Ссылка
	|	И ТИПЗНАЧЕНИЯ(Строки.АналитикаРасходовНачисления) = ТИП(Справочник.ОбъектыЭксплуатации)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ИдентификаторСтроки";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтПорядокУчетаОСУУ(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтПорядокУчетаОСУУ";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтНачисления", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтНачисления(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтСписания", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтСписания(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ПорядокУчетаОСУУ.ОсновноеСредство КАК ОсновноеСредство,
	|	ПорядокУчетаОСУУ.Состояние КАК Состояние
	|ПОМЕСТИТЬ ВтПорядокУчетаОСУУ
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОСУУ.СрезПоследних(&НачалоПериода, &ЭтоРезервыЛиквидационныхОбязательств И (ОсновноеСредство, Организация) В
	|			(ВЫБРАТЬ
	|				Т.АналитикаРасходов,
	|				&Организация
	|			ИЗ
	|				ВтНачисления КАК Т
	|
	|			ОБЪЕДИНИТЬ ВСЕ
	|
	|			ВЫБРАТЬ
	|				Т.АналитикаРасходов,
	|				&Организация
	|			ИЗ
	|				ВтСписания КАК Т)) КАК ПорядокУчетаОСУУ	
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтПорядокУчетаОСБУ(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтПорядокУчетаОСБУ";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтНачисления", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтНачисления(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтСписания", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтСписания(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	NULL КАК ОсновноеСредство,
	|	NULL КАК СостояниеБУ,
	|	NULL КАК НалогообложениеНДС
	|ПОМЕСТИТЬ ВтПорядокУчетаОСБУ";
	
	НачислениеСписаниеРезервовПредстоящихРасходовЛокализация.ТекстЗапросаТаблицаВтДанныеОсновныхСредств(ТекстЗапроса);
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтПорядокУчетаОС(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтПорядокУчетаОС";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтНачисления", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтНачисления(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтСписания", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтСписания(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ПорядокУчетаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ПорядокУчетаОС.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	ПорядокУчетаОС.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ПОМЕСТИТЬ ВтПорядокУчетаОС
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОС.СрезПоследних(&НачалоПериода, &ЭтоРезервыЛиквидационныхОбязательств И (ОсновноеСредство, Организация) В
	|			(ВЫБРАТЬ
	|				Т.АналитикаРасходов,
	|				&Организация
	|			ИЗ
	|				ВтНачисления КАК Т
	|
	|			ОБЪЕДИНИТЬ ВСЕ
	|
	|			ВЫБРАТЬ
	|				Т.АналитикаРасходов,
	|				&Организация
	|			ИЗ
	|				ВтСписания КАК Т)) КАК ПорядокУчетаОС	
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтМестонахождениеОС(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтМестонахождениеОС";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтНачисления", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтНачисления(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтСписания", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтСписания(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	МестонахождениеОС.ОсновноеСредство КАК ОсновноеСредство,
	|	МестонахождениеОС.Местонахождение КАК Подразделение
	|ПОМЕСТИТЬ ВтМестонахождениеОС
	|ИЗ
	|	РегистрСведений.МестонахождениеОС.СрезПоследних(&НачалоПериода, &ЭтоРезервыЛиквидационныхОбязательств
	|	И (ОсновноеСредство) В
	|		(ВЫБРАТЬ
	|			Т.АналитикаРасходов
	|		ИЗ
	|			ВтНачисления КАК Т
	|
	|		ОБЪЕДИНИТЬ ВСЕ
	|
	|		ВЫБРАТЬ
	|			Т.АналитикаРасходов
	|		ИЗ
	|			ВтСписания КАК Т)) КАК МестонахождениеОС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтСтоимостьОсновныхСредств(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтСтоимостьОсновныхСредств";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтСтоимостьОС", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтСтоимостьОС(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтАмортизацияОС", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтАмортизацияОС(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтОбесценениеВНА", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтОбесценениеВНА(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Строки.АналитикаРасходов КАК ОсновноеСредство,
	|	Строки.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ЕСТЬNULL(СтоимостьОС.ОстатокСтоимостиУпр, 0) - ЕСТЬNULL(АмортизацияОС.ОстатокАмортизацииУпр, 0) -
	|		ЕСТЬNULL(ОбесценениеВНА.ОстатокОбесцененияУпр, 0) КАК ОстатокСтоимостиУпр,
	|	ЕСТЬNULL(СтоимостьОС.ОстатокСтоимостиРегл, 0) - ЕСТЬNULL(АмортизацияОС.ОстатокАмортизацииРегл, 0) -
	|		ЕСТЬNULL(ОбесценениеВНА.ОстатокОбесцененияРегл, 0) КАК ОстатокСтоимостиРегл
	|ПОМЕСТИТЬ ВтСтоимостьОсновныхСредств
	|ИЗ
	|	ВтСписания КАК Строки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСтоимостьОС КАК СтоимостьОС
	|		ПО Строки.АналитикаРасходов = СтоимостьОС.ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтАмортизацияОС КАК АмортизацияОС
	|		ПО Строки.АналитикаРасходов = АмортизацияОС.ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтОбесценениеВНА КАК ОбесценениеВНА
	|		ПО Строки.АналитикаРасходов = ОбесценениеВНА.ОсновноеСредство
	|ГДЕ
	|	&ЭтоРезервыЛиквидационныхОбязательств
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ИдентификаторСтроки";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтСтоимостьОС(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтСтоимостьОС";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтСписания", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтСписания(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СтоимостьОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СтоимостьОС.СтоимостьОстаток КАК ОстатокСтоимостиУпр,
	|	СтоимостьОС.СтоимостьРеглОстаток КАК ОстатокСтоимостиРегл
	|ПОМЕСТИТЬ ВтСтоимостьОС
	|ИЗ
	|	РегистрНакопления.СтоимостьОС.Остатки(&КонецМесяцаГраница, &ЭтоРезервыЛиквидационныхОбязательств
	|	И (Организация, ОсновноеСредство) В
	|		(ВЫБРАТЬ
	|			&Организация,
	|			Т.АналитикаРасходов
	|		ИЗ
	|			ВтСписания КАК Т)) КАК СтоимостьОС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтАмортизацияОС(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтАмортизацияОС";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтСписания", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтСписания(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	АмортизацияОС.ОсновноеСредство КАК ОсновноеСредство,
	|	-АмортизацияОС.АмортизацияОстаток КАК ОстатокАмортизацииУпр,
	|	-АмортизацияОС.АмортизацияРеглОстаток КАК ОстатокАмортизацииРегл
	|ПОМЕСТИТЬ ВтАмортизацияОС
	|ИЗ
	|	РегистрНакопления.АмортизацияОС.Остатки(&КонецМесяцаГраница, &ЭтоРезервыЛиквидационныхОбязательств
	|	И (Организация, ОсновноеСредство) В
	|		(ВЫБРАТЬ
	|			&Организация,
	|			Т.АналитикаРасходов
	|		ИЗ
	|			ВтСписания КАК Т)) КАК АмортизацияОС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтСоставЕГДС(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтСоставЕГДС";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтСписания", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтСписания(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СоставЕГДС.ВнеоборотныйАктив КАК ВнеоборотныйАктив,
	|	СоставЕГДС.Организация КАК Организация,
	|	СоставЕГДС.ЕГДС КАК ЕГДС
	|ПОМЕСТИТЬ ВтСоставЕГДС
	|ИЗ
	|	РегистрСведений.СоставЕГДС.СрезПоследних(&КонецМесяцаГраница, &ЭтоРезервыЛиквидационныхОбязательств
	|	И (ВнеоборотныйАктив, Организация) В
	|		(ВЫБРАТЬ
	|			Т.АналитикаРасходов,
	|			&Организация
	|		ИЗ
	|			ВтСписания КАК Т)) КАК СоставЕГДС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВнеоборотныйАктив,
	|	Организация,
	|	ЕГДС";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтОбесценениеВНА(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтОбесценениеВНА";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтСоставЕГДС", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтСоставЕГДС(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ОбесценениеВНА.ВнеоборотныйАктив КАК ОсновноеСредство,
	|	-ОбесценениеВНА.ОбесценениеУпрОстаток КАК ОстатокОбесцененияУпр,
	|	-ОбесценениеВНА.ОбесценениеРеглОстаток КАК ОстатокОбесцененияРегл
	|ПОМЕСТИТЬ ВтОбесценениеВНА
	|ИЗ
	|	РегистрНакопления.ОбесценениеВНА.Остатки(&КонецМесяцаГраница, &ЭтоРезервыЛиквидационныхОбязательств
	|	И (ВнеоборотныйАктив, ЕГДС, Организация) В
	|		(ВЫБРАТЬ
	|			Т.ВнеоборотныйАктив,
	|			Т.ЕГДС,
	|			Т.Организация
	|		ИЗ
	|			ВтСоставЕГДС КАК Т)) КАК ОбесценениеВНА
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтИсходныеПрочиеРасходы";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтНачисления", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтНачисления(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтСписания", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтСписания(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтДанныеОсновныхСредств", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтДанныеОсновныхСредств(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА &ДисконтированиеОценочногоОбязательства
	|			ТОГДА &НачалоПериода
	|		ИНАЧЕ &Период
	|	КОНЕЦ КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	ТаблицаНачислений.Подразделение КАК Подразделение,
	|	ТаблицаНачислений.НаправлениеДеятельностиНачисления КАК НаправлениеДеятельности,
	|	ТаблицаНачислений.СтатьяРасходов КАК СтатьяРасходов,
	|	ТаблицаНачислений.АналитикаРасходов КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
	|	0 КАК СуммаСНДС,
	|	0 КАК СуммаБезНДС,
	|	СУММА(ТаблицаНачислений.СуммаУпр) КАК СуммаБезНДСУпр,
	|	СУММА(ТаблицаНачислений.СуммаРегл) КАК СуммаСНДСРегл,
	|	СУММА(ТаблицаНачислений.СуммаРегл) КАК СуммаБезНДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	СУММА(ТаблицаНачислений.СуммаРегл) - СУММА(ТаблицаНачислений.СуммаНУ) КАК ВременнаяРазница,
	|	ВЫБОР
	|		КОГДА ТаблицаНачислений.ЭтоИзменениеТолькоПроцентовДисконтирования
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПроцентовПоДисконтированию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачислениеРезервовПредстоящихРасходов)
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаНачислений.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ВЫБОР
	|		КОГДА ТаблицаНачислений.ЭтоИзменениеТолькоПроцентовДисконтирования
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СписаниеПроцентовПоДисконтированию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.НачислениеРезервовПредстоящихРасходов)
	|	КОНЕЦ КАК НастройкаХозяйственнойОперации
	|ПОМЕСТИТЬ ВтИсходныеПрочиеРасходы
	|ИЗ
	|	ВтНачисления КАК ТаблицаНачислений
	|ГДЕ
	|	НЕ &ЭтоРезервыЛиквидационныхОбязательств
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаНачислений.СтатьяРасходов,
	|	ТаблицаНачислений.АналитикаРасходов,
	|	ТаблицаНачислений.Подразделение,
	|	ТаблицаНачислений.НаправлениеДеятельностиНачисления,
	|	ВЫБОР
	|		КОГДА ТаблицаНачислений.ЭтоИзменениеТолькоПроцентовДисконтирования
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПроцентовПоДисконтированию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачислениеРезервовПредстоящихРасходов)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаНачислений.ЭтоИзменениеТолькоПроцентовДисконтирования
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СписаниеПроцентовПоДисконтированию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.НачислениеРезервовПредстоящихРасходов)
	|	КОНЕЦ,
	|	ТаблицаНачислений.ИдентификаторСтроки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&НачалоПериода КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	ТаблицаНачислений.Подразделение КАК Подразделение,
	|	ТаблицаНачислений.НаправлениеДеятельностиНачисления КАК НаправлениеДеятельности,
	|	ТаблицаНачислений.СтатьяРасходов КАК СтатьяРасходов,
	|	ТаблицаНачислений.АналитикаРасходов КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
	|	0 КАК СуммаСНДС,
	|	0 КАК СуммаБезНДС,
	|	СУММА(ВЫБОР
	|		КОГДА НЕ ВтДанныеОсновныхСредств.ПринятоКУчетуУпр
	|			ТОГДА ТаблицаНачислений.СуммаУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК СуммаБезНДСУпр,
	|	СУММА(ВЫБОР
	|		КОГДА НЕ ВтДанныеОсновныхСредств.ПринятоКУчетуРегл
	|			ТОГДА ТаблицаНачислений.СуммаРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК СуммаСНДСРегл,
	|	СУММА(ВЫБОР
	|		КОГДА НЕ ВтДанныеОсновныхСредств.ПринятоКУчетуРегл
	|			ТОГДА ТаблицаНачислений.СуммаРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК СуммаБезНДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	СУММА(ВЫБОР
	|		КОГДА НЕ ВтДанныеОсновныхСредств.ПринятоКУчетуРегл
	|			ТОГДА ТаблицаНачислений.СуммаРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК ВременнаяРазница,
	|	ВЫБОР
	|		КОГДА ТаблицаНачислений.ЭтоИзменениеТолькоПроцентовДисконтирования
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПроцентовПоДисконтированию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачислениеРезервовПредстоящихРасходов)
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаНачислений.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ВЫБОР
	|		КОГДА ТаблицаНачислений.ЭтоИзменениеТолькоПроцентовДисконтирования
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СписаниеПроцентовПоДисконтированию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.НачислениеРезервовПредстоящихРасходов)
	|	КОНЕЦ КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ВтНачисления КАК ТаблицаНачислений
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтДанныеОсновныхСредств КАК ВтДанныеОсновныхСредств
	|		ПО ТаблицаНачислений.АналитикаРасходов = ВтДанныеОсновныхСредств.ОсновноеСредство
	|		И ТаблицаНачислений.ИдентификаторСтроки = ВтДанныеОсновныхСредств.ИдентификаторСтроки
	|ГДЕ
	|	&ЭтоРезервыЛиквидационныхОбязательств
	|	И (НЕ ВтДанныеОсновныхСредств.ПринятоКУчетуУпр
	|	ИЛИ НЕ ВтДанныеОсновныхСредств.ПринятоКУчетуРегл)
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаНачислений.СтатьяРасходов,
	|	ТаблицаНачислений.АналитикаРасходов,
	|	ТаблицаНачислений.Подразделение,
	|	ТаблицаНачислений.НаправлениеДеятельностиНачисления,
	|	ВЫБОР
	|		КОГДА ТаблицаНачислений.ЭтоИзменениеТолькоПроцентовДисконтирования
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПроцентовПоДисконтированию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачислениеРезервовПредстоящихРасходов)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаНачислений.ЭтоИзменениеТолькоПроцентовДисконтирования
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СписаниеПроцентовПоДисконтированию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.НачислениеРезервовПредстоящихРасходов)
	|	КОНЕЦ,
	|	ТаблицаНачислений.ИдентификаторСтроки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&НачалоПериода КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	ТаблицаНачислений.Подразделение КАК Подразделение,
	|	ТаблицаНачислений.НаправлениеДеятельностиСписания КАК НаправлениеДеятельности,
	|	ТаблицаНачислений.СтатьяРасходов КАК СтатьяРасходов,
	|	ТаблицаНачислений.АналитикаРасходов КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
	|	0 КАК СуммаСНДС,
	|	0 КАК СуммаБезНДС,
	|	СУММА(ВЫБОР
	|		КОГДА ТаблицаНачислений.ЭтоИзменениеТолькоПроцентовДисконтирования
	|			ТОГДА ТаблицаНачислений.СуммаУпр
	|		КОГДА НЕ ВтДанныеОсновныхСредств.ПринятоКУчетуУпр
	|			ТОГДА -ТаблицаНачислений.СуммаУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК СуммаБезНДСУпр,
	|	СУММА(ВЫБОР
	|		КОГДА ТаблицаНачислений.ЭтоИзменениеТолькоПроцентовДисконтирования
	|			ТОГДА ТаблицаНачислений.СуммаРегл
	|		КОГДА НЕ ВтДанныеОсновныхСредств.ПринятоКУчетуРегл
	|			ТОГДА -ТаблицаНачислений.СуммаРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК СуммаСНДСРегл,
	|	СУММА(ВЫБОР
	|		КОГДА ТаблицаНачислений.ЭтоИзменениеТолькоПроцентовДисконтирования
	|			ТОГДА ТаблицаНачислений.СуммаРегл
	|		КОГДА НЕ ВтДанныеОсновныхСредств.ПринятоКУчетуРегл
	|			ТОГДА -ТаблицаНачислений.СуммаРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК СуммаБезНДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	СУММА(ВЫБОР
	|		КОГДА ТаблицаНачислений.ЭтоИзменениеТолькоПроцентовДисконтирования
	|			ТОГДА ТаблицаНачислений.СуммаРегл
	|		КОГДА НЕ ВтДанныеОсновныхСредств.ПринятоКУчетуРегл
	|			ТОГДА -ТаблицаНачислений.СуммаРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК ВременнаяРазница,
	|	ВЫБОР
	|		КОГДА ТаблицаНачислений.ЭтоИзменениеТолькоПроцентовДисконтирования
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачислениеПроцентовПоДисконтированию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРезервовПредстоящихРасходов)
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаНачислений.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ВЫБОР
	|		КОГДА ТаблицаНачислений.ЭтоИзменениеТолькоПроцентовДисконтирования
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.НачислениеПроцентовПоДисконтированию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СписаниеРезервовПредстоящихРасходов)
	|	КОНЕЦ КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ВтСписания КАК ТаблицаНачислений
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтДанныеОсновныхСредств КАК ВтДанныеОсновныхСредств
	|		ПО ТаблицаНачислений.АналитикаРасходов = ВтДанныеОсновныхСредств.ОсновноеСредство
	|		И ТаблицаНачислений.ИдентификаторСтроки = ВтДанныеОсновныхСредств.ИдентификаторСтроки
	|ГДЕ
	|	&ЭтоРезервыЛиквидационныхОбязательств
	|	И (НЕ ВтДанныеОсновныхСредств.ПринятоКУчетуУпр
	|	ИЛИ НЕ ВтДанныеОсновныхСредств.ПринятоКУчетуРегл)
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаНачислений.СтатьяРасходов,
	|	ТаблицаНачислений.АналитикаРасходов,
	|	ТаблицаНачислений.Подразделение,
	|	ТаблицаНачислений.НаправлениеДеятельностиСписания,
	|	ВЫБОР
	|		КОГДА ТаблицаНачислений.ЭтоИзменениеТолькоПроцентовДисконтирования
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачислениеПроцентовПоДисконтированию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРезервовПредстоящихРасходов)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаНачислений.ЭтоИзменениеТолькоПроцентовДисконтирования
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.НачислениеПроцентовПоДисконтированию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СписаниеРезервовПредстоящихРасходов)
	|	КОНЕЦ,
	|	ТаблицаНачислений.ИдентификаторСтроки";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтПрочиеРасходы";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтИсходныеПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаВтПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеРасходы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ТекстЗапросаПрочиеДоходы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеДоходы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтСписания", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтСписания(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтДанныеОсновныхСредств", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтДанныеОсновныхСредств(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтСтоимостьОсновныхСредств", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтСтоимостьОсновныхСредств(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА &ДисконтированиеОценочногоОбязательства
	|			ТОГДА &НачалоПериода
	|		ИНАЧЕ &Период
	|	КОНЕЦ КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	ТаблицаСписаний.Подразделение КАК Подразделение,
	|	ТаблицаСписаний.НаправлениеДеятельностиСписания КАК НаправлениеДеятельности,
	|	ТаблицаСписаний.СтатьяДоходов КАК СтатьяДоходов,
	|	ТаблицаСписаний.АналитикаДоходов КАК АналитикаДоходов,
	|	СУММА(0) КАК Сумма,
	|	СУММА(ТаблицаСписаний.СуммаРегл) КАК СуммаРегл,
	|	СУММА(ТаблицаСписаний.СуммаУпр) КАК СуммаУпр,
	|	ВЫБОР
	|		КОГДА ТаблицаСписаний.ЭтоИзменениеТолькоПроцентовДисконтирования
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачислениеПроцентовПоДисконтированию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРезервовПредстоящихРасходов)
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|	
	|	ТаблицаСписаний.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ВЫБОР
	|		КОГДА ТаблицаСписаний.ЭтоИзменениеТолькоПроцентовДисконтирования
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.НачислениеПроцентовПоДисконтированию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СписаниеРезервовПредстоящихРасходов)
	|	КОНЕЦ КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ВтСписания КАК ТаблицаСписаний
	|ГДЕ
	|	НЕ &ЭтоРезервыЛиквидационныхОбязательств
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаСписаний.СтатьяДоходов,
	|	ТаблицаСписаний.АналитикаДоходов,
	|	ТаблицаСписаний.Подразделение,
	|	ТаблицаСписаний.НаправлениеДеятельностиСписания,
	|	ВЫБОР
	|		КОГДА ТаблицаСписаний.ЭтоИзменениеТолькоПроцентовДисконтирования
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачислениеПроцентовПоДисконтированию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРезервовПредстоящихРасходов)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаСписаний.ЭтоИзменениеТолькоПроцентовДисконтирования
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.НачислениеПроцентовПоДисконтированию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СписаниеРезервовПредстоящихРасходов)
	|	КОНЕЦ,
	|	ТаблицаСписаний.ИдентификаторСтроки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&НачалоПериода КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	ТаблицаСписаний.Подразделение КАК Подразделение,
	|	ТаблицаСписаний.НаправлениеДеятельностиСписания КАК НаправлениеДеятельности,
	|	ТаблицаСписаний.СтатьяДоходов КАК СтатьяДоходов,
	|	ТаблицаСписаний.АналитикаДоходов КАК АналитикаДоходов,
	|	СУММА(0) КАК Сумма,
	|	СУММА(ВЫБОР
	|		КОГДА ВтДанныеОсновныхСредств.ПринятоКУчетуРегл
	|				И ВтСтоимостьОсновныхСредств.ОстатокСтоимостиРегл < ТаблицаСписаний.СуммаРегл
	|			ТОГДА ТаблицаСписаний.СуммаРегл - ВтСтоимостьОсновныхСредств.ОстатокСтоимостиРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК СуммаРегл,
	|	СУММА(ВЫБОР
	|		КОГДА ВтДанныеОсновныхСредств.ПринятоКУчетуУпр
	|				И ВтСтоимостьОсновныхСредств.ОстатокСтоимостиУпр < ТаблицаСписаний.СуммаУпр
	|			ТОГДА ТаблицаСписаний.СуммаУпр - ВтСтоимостьОсновныхСредств.ОстатокСтоимостиУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК СуммаУпр,
	|	ВЫБОР
	|		КОГДА ТаблицаСписаний.ЭтоИзменениеТолькоПроцентовДисконтирования
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачислениеПроцентовПоДисконтированию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРезервовПредстоящихРасходов)
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|	
	|	ТаблицаСписаний.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ВЫБОР
	|		КОГДА ТаблицаСписаний.ЭтоИзменениеТолькоПроцентовДисконтирования
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.НачислениеПроцентовПоДисконтированию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СписаниеРезервовПредстоящихРасходов)
	|	КОНЕЦ КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ВтСписания КАК ТаблицаСписаний
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтДанныеОсновныхСредств КАК ВтДанныеОсновныхСредств
	|		ПО ТаблицаСписаний.АналитикаРасходов = ВтДанныеОсновныхСредств.ОсновноеСредство
	|		И ТаблицаСписаний.ИдентификаторСтроки = ВтДанныеОсновныхСредств.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСтоимостьОсновныхСредств КАК ВтСтоимостьОсновныхСредств
	|		ПО ТаблицаСписаний.АналитикаРасходов = ВтСтоимостьОсновныхСредств.ОсновноеСредство
	|		И ТаблицаСписаний.ИдентификаторСтроки = ВтСтоимостьОсновныхСредств.ИдентификаторСтроки
	|ГДЕ
	|	&ЭтоРезервыЛиквидационныхОбязательств
	|	И (ВтДанныеОсновныхСредств.ПринятоКУчетуУпр И ВтСтоимостьОсновныхСредств.ОстатокСтоимостиУпр < ТаблицаСписаний.СуммаУпр
	|	ИЛИ ВтДанныеОсновныхСредств.ПринятоКУчетуРегл И ВтСтоимостьОсновныхСредств.ОстатокСтоимостиРегл < ТаблицаСписаний.СуммаРегл)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаСписаний.СтатьяДоходов,
	|	ТаблицаСписаний.АналитикаДоходов,
	|	ТаблицаСписаний.Подразделение,
	|	ТаблицаСписаний.НаправлениеДеятельностиСписания,
	|	ВЫБОР
	|		КОГДА ТаблицаСписаний.ЭтоИзменениеТолькоПроцентовДисконтирования
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачислениеПроцентовПоДисконтированию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРезервовПредстоящихРасходов)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаСписаний.ЭтоИзменениеТолькоПроцентовДисконтирования
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.НачислениеПроцентовПоДисконтированию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СписаниеРезервовПредстоящихРасходов)
	|	КОНЕЦ,
	|	ТаблицаСписаний.ИдентификаторСтроки
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаСтоимостьОС(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СтоимостьОС";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтНачисления", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтНачисления(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтСписания", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтСписания(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтДанныеОсновныхСредств", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтДанныеОсновныхСредств(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтСтоимостьОсновныхСредств", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтСтоимостьОсновныхСредств(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&НачалоПериода КАК Период,
	|	&Организация КАК Организация,
	|	ТаблицаНачислений.АналитикаРасходов КАК ОсновноеСредство,
	|	ВтДанныеОсновныхСредств.Подразделение КАК Подразделение,
	|	ВтДанныеОсновныхСредств.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	ВтДанныеОсновныхСредств.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК Арендатор,
	|	ВЫБОР
	|		КОГДА ТаблицаНачислений.ЭтоИзменениеТолькоПроцентовДисконтирования
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПроцентовПоДисконтированию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачислениеРезервовПредстоящихРасходов)
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|	ВтДанныеОсновныхСредств.ПринятоКУчетуРегл КАК ОтражатьВРеглУчете,
	|	ВтДанныеОсновныхСредств.ПринятоКУчетуУпр КАК ОтражатьВУпрУчете,
	|	ВтДанныеОсновныхСредств.НалогообложениеНДС КАК НалогообложениеНДС,
	|	ТаблицаНачислений.Подразделение КАК КорПодразделение,
	|	ВтДанныеОсновныхСредств.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	ВтДанныеОсновныхСредств.ГруппаФинансовогоУчета КАК КорГруппаФинансовогоУчета,
	|	ЛОЖЬ КАК РасчетСтоимости,
	|	0 КАК ПредварительнаяСтоимость,
	|	0 КАК ПредварительнаяСтоимостьРегл,
	|	ВЫБОР
	|		КОГДА ВтДанныеОсновныхСредств.ПринятоКУчетуУпр
	|			ТОГДА ТаблицаНачислений.СуммаУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Стоимость,
	|	ВЫБОР
	|		КОГДА ВтДанныеОсновныхСредств.ПринятоКУчетуРегл
	|			ТОГДА ТаблицаНачислений.СуммаРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьРегл,
	|	0 КАК СтоимостьНУ,
	|	0 КАК СтоимостьПР,
	|	ВЫБОР
	|		КОГДА ВтДанныеОсновныхСредств.ПринятоКУчетуРегл
	|			ТОГДА ТаблицаНачислений.СуммаРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьВР,
	|	0 КАК СтоимостьЦФ,
	|	0 КАК СтоимостьНУЦФ,
	|	0 КАК СтоимостьПРЦФ,
	|	0 КАК СтоимостьВРЦФ,
	|	0 КАК РезервПереоценкиСтоимости,
	|	0 КАК РезервПереоценкиСтоимостиРегл,
	|	0 КАК ПриростСтоимости,
	|	0 КАК ПриростСтоимостиРегл,
	|	ТаблицаНачислений.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ВЫБОР
	|		КОГДА ТаблицаНачислений.ЭтоИзменениеТолькоПроцентовДисконтирования
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СписаниеПроцентовПоДисконтированию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.НачислениеРезервовПредстоящихРасходов)
	|	КОНЕЦ КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ВтНачисления КАК ТаблицаНачислений
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДанныеОсновныхСредств КАК ВтДанныеОсновныхСредств
	|		ПО ТаблицаНачислений.АналитикаРасходов = ВтДанныеОсновныхСредств.ОсновноеСредство
	|		И ТаблицаНачислений.ИдентификаторСтроки = ВтДанныеОсновныхСредств.ИдентификаторСтроки
	|ГДЕ
	|	&ЭтоРезервыЛиквидационныхОбязательств
	|	И (ВтДанныеОсновныхСредств.ПринятоКУчетуУпр
	|	ИЛИ ВтДанныеОсновныхСредств.ПринятоКУчетуРегл)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&НачалоПериода КАК Период,
	|	&Организация КАК Организация,
	|	ТаблицаСписаний.АналитикаРасходов КАК ОсновноеСредство,
	|	ВтДанныеОсновныхСредств.Подразделение КАК Подразделение,
	|	ВтДанныеОсновныхСредств.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	ВтДанныеОсновныхСредств.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК Арендатор,
	|	ВЫБОР
	|		КОГДА ТаблицаСписаний.ЭтоИзменениеТолькоПроцентовДисконтирования
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачислениеПроцентовПоДисконтированию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРезервовПредстоящихРасходов)
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|	ВтДанныеОсновныхСредств.ПринятоКУчетуРегл КАК ОтражатьВРеглУчете,
	|	ВтДанныеОсновныхСредств.ПринятоКУчетуУпр КАК ОтражатьВУпрУчете,
	|	ВтДанныеОсновныхСредств.НалогообложениеНДС КАК НалогообложениеНДС,
	|	ТаблицаСписаний.Подразделение КАК КорПодразделение,
	|	ВтДанныеОсновныхСредств.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	ВтДанныеОсновныхСредств.ГруппаФинансовогоУчета КАК КорГруппаФинансовогоУчета,
	|	ЛОЖЬ КАК РасчетСтоимости,
	|	0 КАК ПредварительнаяСтоимость,
	|	0 КАК ПредварительнаяСтоимостьРегл,
	|	ВЫБОР
	|		КОГДА ВтДанныеОсновныхСредств.ПринятоКУчетуУпр 
	|				И ВтСтоимостьОсновныхСредств.ОстатокСтоимостиУпр >= ТаблицаСписаний.СуммаУпр
	|			ТОГДА -ТаблицаСписаний.СуммаУпр
	|		КОГДА ВтДанныеОсновныхСредств.ПринятоКУчетуУпр 
	|				И ВтСтоимостьОсновныхСредств.ОстатокСтоимостиУпр < ТаблицаСписаний.СуммаУпр
	|			ТОГДА -ВтСтоимостьОсновныхСредств.ОстатокСтоимостиУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Стоимость,
	|	ВЫБОР
	|		КОГДА ВтДанныеОсновныхСредств.ПринятоКУчетуРегл
	|				И ВтСтоимостьОсновныхСредств.ОстатокСтоимостиРегл >= ТаблицаСписаний.СуммаРегл
	|			ТОГДА -ТаблицаСписаний.СуммаРегл
	|		КОГДА ВтДанныеОсновныхСредств.ПринятоКУчетуРегл
	|				И ВтСтоимостьОсновныхСредств.ОстатокСтоимостиРегл < ТаблицаСписаний.СуммаРегл
	|			ТОГДА -ВтСтоимостьОсновныхСредств.ОстатокСтоимостиРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьРегл,
	|	0 КАК СтоимостьНУ,
	|	0 КАК СтоимостьПР,
	|	ВЫБОР
	|		КОГДА ВтДанныеОсновныхСредств.ПринятоКУчетуРегл
	|				И ВтСтоимостьОсновныхСредств.ОстатокСтоимостиРегл >= ТаблицаСписаний.СуммаРегл
	|			ТОГДА -ТаблицаСписаний.СуммаРегл
	|		КОГДА ВтДанныеОсновныхСредств.ПринятоКУчетуРегл
	|				И ВтСтоимостьОсновныхСредств.ОстатокСтоимостиРегл < ТаблицаСписаний.СуммаРегл
	|			ТОГДА -ВтСтоимостьОсновныхСредств.ОстатокСтоимостиРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьВР,
	|	0 КАК СтоимостьЦФ,
	|	0 КАК СтоимостьНУЦФ,
	|	0 КАК СтоимостьПРЦФ,
	|	0 КАК СтоимостьВРЦФ,
	|	0 КАК РезервПереоценкиСтоимости,
	|	0 КАК РезервПереоценкиСтоимостиРегл,
	|	0 КАК ПриростСтоимости,
	|	0 КАК ПриростСтоимостиРегл,
	|	ТаблицаСписаний.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ВЫБОР
	|		КОГДА ТаблицаСписаний.ЭтоИзменениеТолькоПроцентовДисконтирования
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.НачислениеПроцентовПоДисконтированию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СписаниеРезервовПредстоящихРасходов)
	|	КОНЕЦ КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ВтСписания КАК ТаблицаСписаний
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДанныеОсновныхСредств КАК ВтДанныеОсновныхСредств
	|		ПО ТаблицаСписаний.АналитикаРасходов = ВтДанныеОсновныхСредств.ОсновноеСредство
	|		И ТаблицаСписаний.ИдентификаторСтроки = ВтДанныеОсновныхСредств.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСтоимостьОсновныхСредств КАК ВтСтоимостьОсновныхСредств
	|		ПО ТаблицаСписаний.АналитикаРасходов = ВтСтоимостьОсновныхСредств.ОсновноеСредство
	|		И ТаблицаСписаний.ИдентификаторСтроки = ВтСтоимостьОсновныхСредств.ИдентификаторСтроки
	|ГДЕ
	|	&ЭтоРезервыЛиквидационныхОбязательств
	|	И (ВтДанныеОсновныхСредств.ПринятоКУчетуУпр
	|	ИЛИ ВтДанныеОсновныхСредств.ПринятоКУчетуРегл)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаРезервыПредстоящихРасходов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РезервыПредстоящихРасходов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтНачисления", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтНачисления(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтСписания", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтСписания(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтДвиженияДисконтирования", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтДвиженияДисконтирования(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА &ДисконтированиеОценочногоОбязательства ИЛИ &ЭтоРезервыЛиквидационныхОбязательств
	|			ТОГДА &НачалоПериода
	|		ИНАЧЕ &Период
	|	КОНЕЦ КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	&ВидРезервов КАК ВидРезервов,
	|	ТаблицаНачислений.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаНачислений.ОбъектУчетаРезервов КАК ОбъектУчетаРезервов,
	|	СУММА(ТаблицаНачислений.СуммаРегл) КАК СуммаРегл,
	|	СУММА(ТаблицаНачислений.СуммаНУ) КАК СуммаНУ,
	|	СУММА(ТаблицаНачислений.СуммаУпр) КАК СуммаУпр,
	|	0 КАК СуммаДисконтированияРегл,
	|	0 КАК СуммаДисконтированияУпр,
	|	ВЫБОР
	|		КОГДА ТаблицаНачислений.ЭтоИзменениеТолькоПроцентовДисконтирования
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПроцентовПоДисконтированию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачислениеРезервовПредстоящихРасходов)
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|	
	|	ТаблицаНачислений.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ВЫБОР
	|		КОГДА ТаблицаНачислений.ЭтоИзменениеТолькоПроцентовДисконтирования
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СписаниеПроцентовПоДисконтированию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.НачислениеРезервовПредстоящихРасходов)
	|	КОНЕЦ КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ВтНачисления КАК ТаблицаНачислений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаНачислений.ОбъектУчетаРезервов,
	|	ТаблицаНачислений.НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА ТаблицаНачислений.ЭтоИзменениеТолькоПроцентовДисконтирования
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПроцентовПоДисконтированию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачислениеРезервовПредстоящихРасходов)
	|	КОНЕЦ,
	|	ТаблицаНачислений.ИдентификаторСтроки,
	|	ВЫБОР
	|		КОГДА ТаблицаНачислений.ЭтоИзменениеТолькоПроцентовДисконтирования
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СписаниеПроцентовПоДисконтированию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.НачислениеРезервовПредстоящихРасходов)
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА &ДисконтированиеОценочногоОбязательства ИЛИ &ЭтоРезервыЛиквидационныхОбязательств
	|			ТОГДА &НачалоПериода
	|		ИНАЧЕ &Период
	|	КОНЕЦ,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	&Организация,
	|	&ВидРезервов,
	|	ТаблицаСписаний.НаправлениеДеятельности,
	|	ТаблицаСписаний.ОбъектУчетаРезервов,
	|	СУММА(ТаблицаСписаний.СуммаРегл),
	|	СУММА(ТаблицаСписаний.СуммаНУ),
	|	СУММА(ТаблицаСписаний.СуммаУпр),
	|	0 КАК СуммаДисконтированияРегл,
	|	0 КАК СуммаДисконтированияУпр,
	|	ВЫБОР
	|		КОГДА ТаблицаСписаний.ЭтоИзменениеТолькоПроцентовДисконтирования
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачислениеПроцентовПоДисконтированию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРезервовПредстоящихРасходов)
	|	КОНЕЦ,
	|	
	|	ТаблицаСписаний.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ВЫБОР
	|		КОГДА ТаблицаСписаний.ЭтоИзменениеТолькоПроцентовДисконтирования
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.НачислениеПроцентовПоДисконтированию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СписаниеРезервовПредстоящихРасходов)
	|	КОНЕЦ КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ВтСписания КАК ТаблицаСписаний
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаСписаний.ОбъектУчетаРезервов,
	|	ТаблицаСписаний.НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА ТаблицаСписаний.ЭтоИзменениеТолькоПроцентовДисконтирования
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачислениеПроцентовПоДисконтированию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРезервовПредстоящихРасходов)
	|	КОНЕЦ,
	|	ТаблицаСписаний.ИдентификаторСтроки,
	|	ВЫБОР
	|		КОГДА ТаблицаСписаний.ЭтоИзменениеТолькоПроцентовДисконтирования
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.НачислениеПроцентовПоДисконтированию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СписаниеРезервовПредстоящихРасходов)
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА &ДисконтированиеОценочногоОбязательства ИЛИ &ЭтоРезервыЛиквидационныхОбязательств
	|			ТОГДА &НачалоПериода
	|		ИНАЧЕ &Период
	|	КОНЕЦ КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	&ВидРезервов КАК ВидРезервов,
	|	ТаблицаДанных.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаДанных.ОбъектУчетаРезервов КАК ОбъектУчетаРезервов,
	|	0 КАК СуммаРегл,
	|	0 КАК СуммаНУ,
	|	0 КАК СуммаУпр,
	|	ВЫБОР
	|		КОГДА ТаблицаДанных.ВидДвиженияРегл = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ТаблицаДанных.СуммаДисконтированияРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаДисконтированияРегл,
	|	ВЫБОР
	|		КОГДА ТаблицаДанных.ВидДвиженияУпр = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ТаблицаДанных.СуммаДисконтированияУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаДисконтированияУпр,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачислениеПроцентовПоДисконтированию) КАК ХозяйственнаяОперация,
	|	
	|	ТаблицаДанных.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.НачислениеПроцентовПоДисконтированию) КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ВтДвиженияДисконтирования КАК ТаблицаДанных
	|ГДЕ
	|	&ДисконтированиеОценочногоОбязательства
	|	И (ТаблицаДанных.ВидДвиженияРегл = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ИЛИ ТаблицаДанных.ВидДвиженияУпр = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА &ДисконтированиеОценочногоОбязательства ИЛИ &ЭтоРезервыЛиквидационныхОбязательств
	|			ТОГДА &НачалоПериода
	|		ИНАЧЕ &Период
	|	КОНЕЦ КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	&ВидРезервов КАК ВидРезервов,
	|	ТаблицаДанных.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаДанных.ОбъектУчетаРезервов КАК ОбъектУчетаРезервов,
	|	ВЫБОР
	|		КОГДА ТаблицаДанных.ВидДвиженияРегл = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ТаблицаДанных.СуммаДисконтированияРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаРегл,
	|	0 КАК СуммаНУ,
	|	ВЫБОР
	|		КОГДА ТаблицаДанных.ВидДвиженияУпр = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ТаблицаДанных.СуммаДисконтированияУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаУпр,
	|	0 КАК СуммаДисконтированияРегл,
	|	0 КАК СуммаДисконтированияУпр,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачислениеПроцентовПоДисконтированию) КАК ХозяйственнаяОперация,
	|	
	|	ТаблицаДанных.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.НачислениеПроцентовПоДисконтированию) КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ВтДвиженияДисконтирования КАК ТаблицаДанных
	|ГДЕ
	|	&ДисконтированиеОценочногоОбязательства
	|	И (ТаблицаДанных.ВидДвиженияРегл = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ИЛИ ТаблицаДанных.ВидДвиженияУпр = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА &ДисконтированиеОценочногоОбязательства ИЛИ &ЭтоРезервыЛиквидационныхОбязательств
	|			ТОГДА &НачалоПериода
	|		ИНАЧЕ &Период
	|	КОНЕЦ КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	&ВидРезервов КАК ВидРезервов,
	|	ТаблицаДанных.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаДанных.ОбъектУчетаРезервов КАК ОбъектУчетаРезервов,
	|	0 КАК СуммаРегл,
	|	0 КАК СуммаНУ,
	|	0 КАК СуммаУпр,
	|	ВЫБОР
	|		КОГДА ТаблицаДанных.ВидДвиженияРегл = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА ТаблицаДанных.СуммаДисконтированияРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаДисконтированияРегл,
	|	ВЫБОР
	|		КОГДА ТаблицаДанных.ВидДвиженияУпр = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА ТаблицаДанных.СуммаДисконтированияУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаДисконтированияУпр,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПроцентовПоДисконтированию) КАК ХозяйственнаяОперация,
	|	
	|	ТаблицаДанных.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СписаниеПроцентовПоДисконтированию) КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ВтДвиженияДисконтирования КАК ТаблицаДанных
	|ГДЕ
	|	&ДисконтированиеОценочногоОбязательства
	|	И (ТаблицаДанных.ВидДвиженияРегл = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ИЛИ ТаблицаДанных.ВидДвиженияУпр = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА &ДисконтированиеОценочногоОбязательства ИЛИ &ЭтоРезервыЛиквидационныхОбязательств
	|			ТОГДА &НачалоПериода
	|		ИНАЧЕ &Период
	|	КОНЕЦ КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	&ВидРезервов КАК ВидРезервов,
	|	ТаблицаДанных.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаДанных.ОбъектУчетаРезервов КАК ОбъектУчетаРезервов,
	|	ВЫБОР
	|		КОГДА ТаблицаДанных.ВидДвиженияРегл = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА ТаблицаДанных.СуммаДисконтированияРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаРегл,
	|	0 КАК СуммаНУ,
	|	ВЫБОР
	|		КОГДА ТаблицаДанных.ВидДвиженияУпр = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА ТаблицаДанных.СуммаДисконтированияУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаУпр,
	|	0 КАК СуммаДисконтированияРегл,
	|	0 КАК СуммаДисконтированияУпр,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПроцентовПоДисконтированию) КАК ХозяйственнаяОперация,
	|	
	|	ТаблицаДанных.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СписаниеПроцентовПоДисконтированию) КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ВтДвиженияДисконтирования КАК ТаблицаДанных
	|ГДЕ
	|	&ДисконтированиеОценочногоОбязательства
	|	И (ТаблицаДанных.ВидДвиженияРегл = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ИЛИ ТаблицаДанных.ВидДвиженияУпр = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход))
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаСтавкиДисконтированияРезервов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СтавкиДисконтированияРезервов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.ВидРезервов КАК ВидРезервов,
	|	ДанныеДокумента.СтавкаДисконтирования КАК СтавкаДисконтирования
	|ИЗ
	|	Документ.НачислениеСписаниеРезервовПредстоящихРасходов КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиДисконтированияРезервов.СрезПоследних(&НачалоПериода,
	|			Организация = &Организация
	|		И ВидРезервов = &ВидРезервов
	|		И Регистратор <> &Ссылка) КАК СтавкиДисконтированияРезервов
	|		ПО ДанныеДокумента.Организация = СтавкиДисконтированияРезервов.Организация
	|		И ДанныеДокумента.ВидРезервов = СтавкиДисконтированияРезервов.ВидРезервов
	|ГДЕ
	|	&ДисконтированиеОценочногоОбязательства
	|	И ДанныеДокумента.Ссылка = &Ссылка
	|	И &СтавкаДисконтирования <> 0
	|	И ЕСТЬNULL(СтавкиДисконтированияРезервов.СтавкаДисконтирования, 0) <> &СтавкаДисконтирования";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаСрокиДисконтированияРезервовПредстоящихРасходов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СрокиДисконтированияРезервовПредстоящихРасходов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	Строки.ОбъектУчетаРезервов КАК ОбъектУчетаРезервов,
	|	ВЫБОР
	|		КОГДА Строки.УстановитьНовуюДатуИсполнения
	|			ТОГДА Строки.НоваяДатаИсполнения
	|		ИНАЧЕ Строки.ДатаИсполнения
	|	КОНЕЦ КАК ДатаИсполнения
	|ИЗ
	|	Документ.НачислениеСписаниеРезервовПредстоящихРасходов.Резервы КАК Строки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СрокиДисконтированияРезервовПредстоящихРасходов.СрезПоследних(&Период,
	|			Регистратор <> &Ссылка) КАК СрокиДисконтирования
	|		ПО Строки.ОбъектУчетаРезервов = СрокиДисконтирования.ОбъектУчетаРезервов
	|ГДЕ
	|	&ДисконтированиеОценочногоОбязательства
	|	И Строки.Ссылка = &Ссылка
	|	И (СрокиДисконтирования.ДатаИсполнения ЕСТЬ NULL
	|	ИЛИ Строки.УстановитьНовуюДатуИсполнения)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&ИдентификаторМетаданных КАК ТипСсылки,
	|	&ХозОперацияДляРеестра КАК ХозяйственнаяОперация,
	|	&Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК Партнер,
	|	НЕОПРЕДЕЛЕНО КАК МестоХранения,
	|	НЕОПРЕДЕЛЕНО КАК Контрагент,
	|	НЕОПРЕДЕЛЕНО КАК Подразделение,
	|	&Период КАК ДатаДокументаИБ,
	|	&Ссылка КАК Ссылка,
	|	&Номер КАК НомерДокументаИБ,
	|	НЕОПРЕДЕЛЕНО КАК Статус,
	|	&Ответственный КАК Ответственный,
	|	НЕОПРЕДЕЛЕНО КАК Автор,
	|	ЛОЖЬ КАК ДополнительнаяЗапись,
	|	НЕОПРЕДЕЛЕНО КАК Дополнительно,
	|	&Комментарий КАК Комментарий,
	|	&Проведен КАК Проведен,
	|	&ПометкаУдаления КАК ПометкаУдаления,
	|	&Период КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать КАК НомерПервичногоДокумента,
	|	0 КАК Сумма,
	|	НЕОПРЕДЕЛЕНО КАК Валюта,
	|	НЕОПРЕДЕЛЕНО КАК Договор,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
	|	ЛОЖЬ КАК СторноИсправление,
	|	НЕОПРЕДЕЛЕНО КАК СторнируемыйДокумент,
	|	НЕОПРЕДЕЛЕНО КАК ИсправляемыйДокумент,
	|	&Период КАК ДатаОтраженияВУчете,
	|	НЕОПРЕДЕЛЕНО КАК Приоритет";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтДокументыПоОС(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтДокументыПоОС";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтНачисления", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтНачисления(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтСписания", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтСписания(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтДанныеОсновныхСредств", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтДанныеОсновныхСредств(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Резервы.НомерСтроки - 1                 КАК НомерЗаписи,
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК Дата,
	|	&Организация                            КАК Организация,
	|	ДанныеОсновныхСредств.Подразделение     КАК Подразделение,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	ВЫБОР
	|		КОГДА ТаблицаНачислений.ЭтоИзменениеТолькоПроцентовДисконтирования
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПроцентовПоДисконтированию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачислениеРезервовПредстоящихРасходов)
	|	КОНЕЦ                                   КАК ХозяйственнаяОперация,
	|	&Проведен                               КАК Проведен,
	|	ЛОЖЬ                                    КАК ДополнительнаяЗапись,
	|	ДанныеОсновныхСредств.ПринятоКУчетуРегл КАК ОтражатьВРеглУчете,
	|	ДанныеОсновныхСредств.ПринятоКУчетуУпр  КАК ОтражатьВУпрУчете,
	|	НЕОПРЕДЕЛЕНО                            КАК СобытиеОС,
	|	ТаблицаНачислений.АналитикаРасходов     КАК ОсновноеСредство
	|ПОМЕСТИТЬ ВтДокументыПоОС
	|ИЗ
	|	ВтНачисления КАК ТаблицаНачислений
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НачислениеСписаниеРезервовПредстоящихРасходов.Резервы КАК Резервы
	|		ПО ТаблицаНачислений.ИдентификаторСтроки = Резервы.ИдентификаторСтроки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДанныеОсновныхСредств КАК ДанныеОсновныхСредств
	|		ПО ТаблицаНачислений.АналитикаРасходов = ДанныеОсновныхСредств.ОсновноеСредство
	|		И ТаблицаНачислений.ИдентификаторСтроки = ДанныеОсновныхСредств.ИдентификаторСтроки
	|ГДЕ
	|	&ЭтоРезервыЛиквидационныхОбязательств
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Резервы.НомерСтроки - 1                 КАК НомерЗаписи,
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК Дата,
	|	&Организация                            КАК Организация,
	|	ДанныеОсновныхСредств.Подразделение     КАК Подразделение,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	ВЫБОР
	|		КОГДА ТаблицаСписаний.ЭтоИзменениеТолькоПроцентовДисконтирования
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПроцентовПоДисконтированию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачислениеРезервовПредстоящихРасходов)
	|	КОНЕЦ                                   КАК ХозяйственнаяОперация,
	|	&Проведен                               КАК Проведен,
	|	ЛОЖЬ                                    КАК ДополнительнаяЗапись,
	|	ДанныеОсновныхСредств.ПринятоКУчетуРегл КАК ОтражатьВРеглУчете,
	|	ДанныеОсновныхСредств.ПринятоКУчетуУпр  КАК ОтражатьВУпрУчете,
	|	НЕОПРЕДЕЛЕНО                            КАК СобытиеОС,
	|	ТаблицаСписаний.АналитикаРасходов       КАК ОсновноеСредство
	|ИЗ
	|	ВтСписания КАК ТаблицаСписаний
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НачислениеСписаниеРезервовПредстоящихРасходов.Резервы КАК Резервы
	|		ПО ТаблицаСписаний.ИдентификаторСтроки = Резервы.ИдентификаторСтроки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДанныеОсновныхСредств КАК ДанныеОсновныхСредств
	|		ПО ТаблицаСписаний.АналитикаРасходов = ДанныеОсновныхСредств.ОсновноеСредство
	|		И ТаблицаСписаний.ИдентификаторСтроки = ДанныеОсновныхСредств.ИдентификаторСтроки
	|ГДЕ
	|	&ЭтоРезервыЛиквидационныхОбязательств
	|";
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДокументыПоОС(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДокументыПоОС";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтДокументыПоОС", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтДокументыПоОС(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаОС.НомерЗаписи           КАК НомерЗаписи,
	|	ТаблицаОС.Ссылка                КАК Ссылка,
	|	ТаблицаОС.Дата                  КАК Дата,
	|	ТаблицаОС.Организация           КАК Организация,
	|	ТаблицаОС.Подразделение         КАК Подразделение,
	|	ТаблицаОС.ТипСсылки             КАК ТипСсылки,
	|	ТаблицаОС.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ТаблицаОС.Проведен              КАК Проведен,
	|	ТаблицаОС.ДополнительнаяЗапись  КАК ДополнительнаяЗапись,
	|	ТаблицаОС.ОтражатьВРеглУчете    КАК ОтражатьВРеглУчете,
	|	ТаблицаОС.ОтражатьВУпрУчете     КАК ОтражатьВУпрУчете,
	|	ТаблицаОС.СобытиеОС             КАК СобытиеОС,
	|	ТаблицаОС.ОсновноеСредство      КАК ОсновноеСредство
	|ИЗ
	|	ВтДокументыПоОС КАК ТаблицаОС";
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.НачислениеСписаниеРезервовПредстоящихРасходов";
	ЗначенияПараметров = Новый Структура;
	ЗначенияПараметров.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.НачислениеСписаниеРезервовПредстоящихРасходов"));
	ЗначенияПараметров.Вставить("ХозОперацияДляРеестра", Перечисления.ХозяйственныеОперации.НачислениеСписаниеРезервовПредстоящихРасходов);
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "";
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';
								|en = 'In document %ПолноеИмяДокумента%, adaptation of request for generating records of register %ИмяРегистра% is not implemented.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
			ТекстЗапроса, ПолноеИмяДокумента, СинонимТаблицыДокумента, Ложь);
	Иначе
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
			ТекстЗапроса, ПолноеИмяДокумента, СинонимТаблицыДокумента);
	КонецЕсли;
	
	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

//++ НЕ УТКА

Функция ТекстЗапросаТаблицаОтражениеДокументовВМеждународномУчете(ТекстыЗапроса, Регистры = Неопределено)
	
	ИмяРегистра = "ОтражениеДокументовВМеждународномУчете";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Ссылка КАК Документ,
	|	ПланыСчетовМеждународногоУчетаОрганизаций.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА &ДисконтированиеОценочногоОбязательства
	|				ИЛИ &ЭтоРезервыЛиквидационныхОбязательств
	|			ТОГДА НАЧАЛОПЕРИОДА(&НачалоПериода, ДЕНЬ)
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(&Период, ДЕНЬ)
	|	КОНЕЦ КАК ДатаОтражения,
	|	ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчете) КАК Статус,
	|	ПланыСчетовМеждународногоУчетаОрганизаций.ПланСчетов КАК ПланСчетов,
	|	ПланыСчетовМеждународногоУчетаОрганизаций.НастройкаФормированияПроводок КАК НастройкаФормированияПроводок
	|ИЗ
	|	РегистрСведений.ПланыСчетовМеждународногоУчетаОрганизаций.СрезПоследних(&Период, Организация = &Организация) КАК ПланыСчетовМеждународногоУчетаОрганизаций";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

//-- НЕ УТКА

#КонецОбласти

#Область ПроводкиРеглУчета

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт

	Возврат НачислениеСписаниеРезервовПредстоящихРасходовЛокализация.ТекстОтраженияВРеглУчете();

КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете.
//
// Возвращаемое значение:
//   Строка - сформированный текст запроса.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт

	Возврат НачислениеСписаниеРезервовПредстоящихРасходовЛокализация.ТекстЗапросаВТОтраженияВРеглУчете();

КонецФункции

#КонецОбласти

#Область Прочее

#Область ЗагрузкаИзФайла

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой Загрузка из файла.

// Устанавливает параметры загрузки.
//
Процедура УстановитьПараметрыЗагрузкиИзФайлаВТЧ(Параметры) Экспорт
	Возврат;
КонецПроцедуры

// Производит сопоставление данных, загружаемых в табличную часть ПолноеИмяТабличнойЧасти,
// с данными в ИБ, и заполняет параметры АдресТаблицыСопоставления и СписокНеоднозначностей.
// 
// Параметры:
// 	АдресЗагружаемыхДанных    - Строка - адрес временного хранилища с таблицей значений, в которой
//                                        находятся загруженные данные из файла. Состав колонок:
//     Идентификатор - Число - порядковый номер строки.
//     остальные колонки соответствуют колонкам макета ЗагрузкаИзФайла.
// 	АдресТаблицыСопоставления - Строка - адрес временного хранилища с пустой таблицей значений,
//                                        являющейся копией табличной части документа,
//                                        которую необходимо заполнить из таблицы АдресЗагружаемыхДанных.
// 	СписокНеоднозначностей    - ТаблицаЗначений - список неоднозначных значений, для которых в ИБ имеется несколько:
// 		* Идентификатор - Число - порядковый номер строки.
// 		Остальные колонки.
// 	ПолноеИмяТабличнойЧасти   - Строка -
// 	ДополнительныеПараметры   - Структура - 
Процедура СопоставитьЗагружаемыеДанные(АдресЗагружаемыхДанных, АдресТаблицыСопоставления, СписокНеоднозначностей, ПолноеИмяТабличнойЧасти, ДополнительныеПараметры) Экспорт
	
	Резервы =  ПрочитатьТаблицуКЗагрузке(АдресТаблицыСопоставления);
	ЗагружаемыеДанные = ПрочитатьТаблицуКЗагрузке(АдресЗагружаемыхДанных);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ДанныеДляСопоставления.ОбъектУчетаРезервов КАК СТРОКА(&НаименованиеДлина)) КАК Наименование,
	|	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ДанныеДляСопоставления
	|ИЗ
	|	&ДанныеДляСопоставления КАК ДанныеДляСопоставления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(СправочникДляСопоставления.Ссылка) КАК ОбъектУчетаРезервов,
	|	ДанныеДляСопоставленияПоНаименованию.Идентификатор,
	|	КОЛИЧЕСТВО(ДанныеДляСопоставленияПоНаименованию.Идентификатор) КАК Количество
	|ИЗ
	|	ДанныеДляСопоставления КАК ДанныеДляСопоставленияПоНаименованию
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыУчетаРезервовПредстоящихРасходов КАК СправочникДляСопоставления
	|		ПО (СправочникДляСопоставления.Наименование = ДанныеДляСопоставленияПоНаименованию.Наименование)
	|			И НЕ СправочникДляСопоставления.ЭтоГруппа
	|ГДЕ
	|	НЕ СправочникДляСопоставления.Ссылка ЕСТЬ NULL
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДляСопоставленияПоНаименованию.Идентификатор";
	
	НаименованиеДлина = Метаданные.Справочники.ОбъектыУчетаРезервовПредстоящихРасходов.ДлинаНаименования;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&НаименованиеДлина", НаименованиеДлина); 
	
	Запрос.УстановитьПараметр("ДанныеДляСопоставления", ЗагружаемыеДанные);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТаблицаРезультата = РезультатЗапроса.Выгрузить();
	
	Для каждого СтрокаТаблицы Из ЗагружаемыеДанные Цикл 
		
		НоваяСтрока = Резервы.Добавить();
		НоваяСтрока.Идентификатор = СтрокаТаблицы.Идентификатор;
		
		СтрокаРезультата = ТаблицаРезультата.Найти(СтрокаТаблицы.Идентификатор, "Идентификатор");
		Если СтрокаРезультата <> Неопределено Тогда 
			Если СтрокаРезультата.Количество = 1 Тогда 
				НоваяСтрока.ОбъектУчетаРезервов  = СтрокаРезультата.ОбъектУчетаРезервов;
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы,, "Идентификатор,ОбъектУчетаРезервов");
			ИначеЕсли СтрокаРезультата.Количество > 1 Тогда 
				ЗаписьОНеоднозначности = СписокНеоднозначностей.Добавить();
				ЗаписьОНеоднозначности.Идентификатор = СтрокаТаблицы.Идентификатор; 
				ЗаписьОНеоднозначности.Колонка = "ОбъектУчетаРезервов";
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	ПоместитьВоВременноеХранилище(Резервы, АдресТаблицыСопоставления);
	
КонецПроцедуры

// Описание
// 
// Параметры:
// 	ПолноеИмяТабличнойЧасти - Строка - полное имя табличной части, в которую загружаются данные.
// 	СписокНеоднозначностей - Массив, ТаблицаЗначений - Список для заполнения с неоднозначными данными. Колонки:
//     * Идентификатор        - Число  - Уникальный идентификатор строки.
//     * Колонка              - Строка -  Имя колонки с возникшей неоднозначностью.
// 	ИмяКолонки - Строка - имя колонки, в который возникла неоднозначность.
// 	ЗагружаемыеЗначенияСтрока - Строка - Загружаемые данные на основании которых возникла неоднозначность.
// 	ДополнительныеПараметры - Структура -
Процедура ЗаполнитьСписокНеоднозначностей(ПолноеИмяТабличнойЧасти, СписокНеоднозначностей, ИмяКолонки, ЗагружаемыеЗначенияСтрока, ДополнительныеПараметры) Экспорт
	
	Если ИмяКолонки = "ОбъектУчетаРезервов" Тогда

		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОбъектыУчетаРезервов.Ссылка
		|ИЗ
		|	Справочник.ОбъектыУчетаРезервовПредстоящихРасходов КАК ОбъектыУчетаРезервов
		|ГДЕ
		|	ОбъектыУчетаРезервов.Наименование = &Наименование";
		Запрос.УстановитьПараметр("Наименование", ЗагружаемыеЗначенияСтрока.ОбъектУчетаРезервов);
		РезультатЗапроса = Запрос.Выполнить();
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			СписокНеоднозначностей.Добавить(ВыборкаДетальныеЗаписи.Ссылка);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Возвращает таблицу, переданную через временное хранилище
// 
// Параметры:
// 	АдресВременногоХранилища - Строка - адрес хранилища, в котором лежит таблица
// Возвращаемое значение:
// 	ТаблицаЗначений - таблица с колонками:
// 		* Идентификатор - Число - порядковый номер.
// 		Остальные колонки.
Функция ПрочитатьТаблицуКЗагрузке(АдресВременногоХранилища)
	Возврат ПолучитьИзВременногоХранилища(АдресВременногоХранилища); // ТаблицаЗначений
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецОбласти

#КонецЕсли
