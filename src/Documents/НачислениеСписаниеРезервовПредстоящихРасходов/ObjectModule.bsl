#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеДокументов.ОбработкаПроведенияДокумента(ЭтотОбъект, Отказ);
	
	НачислениеСписаниеРезервовПредстоящихРасходовЛокализация.ОбработкаПроведения(ЭтотОбъект, Отказ, РежимПроведения);
	
	// Если документом не меняется сумма резервов, то создадим задания к расчету себестоимости.
	Если Резервы.Итог("НоваяСуммаРегл") = 0
	 И Резервы.Итог("НоваяСуммаУпр") = 0
	 И Резервы.Итог("НоваяСуммаНУ") = 0 Тогда
		РегистрыСведений.ЗаданияКРасчетуСебестоимости.СоздатьЗаписьРегистра(Дата, Ссылка, Организация);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеДокументов.ОбработкаУдаленияПроведенияДокумента(ЭтотОбъект, Отказ);
	
	НачислениеСписаниеРезервовПредстоящихРасходовЛокализация.ОбработкаУдаленияПроведения(ЭтотОбъект, Отказ);
	
	// Если документом не меняется сумма резервов, то создадим задания к расчету себестоимости.
	Если Резервы.Итог("НоваяСуммаРегл") = 0
	 И Резервы.Итог("НоваяСуммаУпр") = 0
	 И Резервы.Итог("НоваяСуммаНУ") = 0 Тогда
		РегистрыСведений.ЗаданияКРасчетуСебестоимости.СоздатьЗаписьРегистра(Дата, Ссылка, Организация);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	ДисконтированиеОценочногоОбязательства = Ложь;
	Если ЗначениеЗаполнено(ВидРезервов) Тогда
		ДисконтированиеОценочногоОбязательства = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			ВидРезервов, "ДисконтированиеРезервов");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Организация) И ЗначениеЗаполнено(Дата) 
		И ДисконтированиеОценочногоОбязательства Тогда
		
		Если Не РезервыПредстоящихРасходов.ИспользуетсяУчетДисконтированияРезервов(КонецМесяца(Дата), Организация) Тогда
			ДисконтированиеОценочногоОбязательства = Ложь;
		КонецЕсли;
	Иначе
		ДисконтированиеОценочногоОбязательства = Ложь;		
	КонецЕсли;
	
	Если Не ДисконтированиеОценочногоОбязательства Тогда
		МассивНепроверяемыхРеквизитов.Добавить("СтавкаДисконтирования");
	КонецЕсли;
	
	Если ДисконтированиеОценочногоОбязательства Тогда
		ШаблонСообщения = НСтр("ru = 'Не заполнена колонка ""Новая дата исполнения"" в строке %1';
								|en = 'The ""New execution date"" column is not filled in line %1'");
		Для Каждого Строка Из Резервы Цикл
			Если Строка.УстановитьНовуюДатуИсполнения И Не ЗначениеЗаполнено(Строка.НоваяДатаИсполнения) Тогда
				ТекстСообщения = СтрШаблон(ШаблонСообщения, Строка.НомерСтроки);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "НоваяДатаИсполнения", "", Отказ);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	НачислениеСписаниеРезервовПредстоящихРасходовЛокализация.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	
	ПараметрыВыбораСтатейИАналитик = Документы.НачислениеСписаниеРезервовПредстоящихРасходов.ПараметрыВыбораСтатейИАналитик(Перечисления.ХозяйственныеОперации.ПустаяСсылка());
	ДоходыИРасходыСервер.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, ПараметрыВыбораСтатейИАналитик);

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Дата = КонецМесяца(Дата);

	// Проверим, что в этом месяце больше нет аналогичных документов:
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	НачислениеСписаниеРезервовПредстоящихРасходов.Ссылка КАК Ссылка
		               |ИЗ
		               |	Документ.НачислениеСписаниеРезервовПредстоящихРасходов КАК НачислениеСписаниеРезервовПредстоящихРасходов
		               |ГДЕ
		               |	НачислениеСписаниеРезервовПредстоящихРасходов.Ссылка <> &Ссылка
		               |	И НАЧАЛОПЕРИОДА(НачислениеСписаниеРезервовПредстоящихРасходов.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ)
		               |	И НачислениеСписаниеРезервовПредстоящихРасходов.Организация = &Организация
		               |	И НачислениеСписаниеРезервовПредстоящихРасходов.ВидРезервов = &ВидРезервов
		               |	И НачислениеСписаниеРезервовПредстоящихРасходов.Проведен";
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.УстановитьПараметр("Дата", Дата);
		Запрос.УстановитьПараметр("Организация", Организация);
		Запрос.УстановитьПараметр("ВидРезервов", ВидРезервов);
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		Если Выборка.Следующий() Тогда
			ТекстСообщения = НСтр("ru = 'По организации ""%1"" с видом резервов ""%2"" за ""%3"" месяц уже оформлен документ ""%4"".';
									|en = 'The %4 document is already registered for the %1 company with the %2 reserve kind for %3 month.'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, Организация, ВидРезервов, Формат(Дата, НСтр("ru = 'ДФ=''ММММ''';
																									|en = 'DF=''MMMM'''")), Выборка.Ссылка);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка, , , Отказ);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВидРезервов) Тогда
		ТипРезерва = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидРезервов, "ТипРезерва");
		ЭтоРезервыЛиквидационныхОбязательств = ТипРезерва
			= ПредопределенноеЗначение("Перечисление.ТипыРезервовПредстоящихРасходов.ЛиквидационныеОбязательства");
		
		Если ЭтоРезервыЛиквидационныхОбязательств Тогда
			Для Каждого СтрокаРезерва Из Резервы Цикл
				Если ЗначениеЗаполнено(СтрокаРезерва.ТекущаяСуммаНУ) Тогда
					СтрокаРезерва.ТекущаяСуммаНУ = 0;
				КонецЕсли;
				Если ЗначениеЗаполнено(СтрокаРезерва.НоваяСуммаНУ) Тогда
					СтрокаРезерва.НоваяСуммаНУ = 0;
				КонецЕсли;
				Если СтрокаРезерва.УстановитьНовуюСуммуНУ Тогда
					СтрокаРезерва.УстановитьНовуюСуммуНУ = Ложь;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
	ОбщегоНазначенияУТ.ЗаполнитьИдентификаторыДокумента(ЭтотОбъект, "Резервы");
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	НачислениеСписаниеРезервовПредстоящихРасходовЛокализация.ПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
	ПараметрыВыбораСтатейИАналитик = Документы.НачислениеСписаниеРезервовПредстоящихРасходов.ПараметрыВыбораСтатейИАналитик(Перечисления.ХозяйственныеОперации.ПустаяСсылка());
	ДоходыИРасходыСервер.ПередЗаписью(ЭтотОбъект, ПараметрыВыбораСтатейИАналитик);

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	НачислениеСписаниеРезервовПредстоящихРасходовЛокализация.ОбработкаЗаполнения(ЭтотОбъект, ДанныеЗаполнения, СтандартнаяОбработка);
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Если ДанныеЗаполнения.Свойство("Дата") Тогда
			ЭтотОбъект.Дата = ДанныеЗаполнения.Дата;
		КонецЕсли;
		СтруктураПроверки = Новый Структура("Организация, ВидРезервов");
		ЗаполнитьЗначенияСвойств(СтруктураПроверки, ДанныеЗаполнения);
		Если ЗначениеЗаполнено(СтруктураПроверки.Организация) И ЗначениеЗаполнено(СтруктураПроверки.ВидРезервов) Тогда
			Резервы.Загрузить(Документы.НачислениеСписаниеРезервовПредстоящихРасходов.ДанныеДляЗаполненияРезервов(ЭтотОбъект).Выгрузить());
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыВыбораСтатейИАналитик = Документы.НачислениеСписаниеРезервовПредстоящихРасходов.ПараметрыВыбораСтатейИАналитик(Перечисления.ХозяйственныеОперации.ПустаяСсылка());
	ДоходыИРасходыСервер.ОбработкаЗаполнения(ЭтотОбъект, ПараметрыВыбораСтатейИАналитик);

КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ПриЗаписиДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ОбщегоНазначенияУТ.ОчиститьИдентификаторыДокумента(ЭтотОбъект, "Резервы");
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
