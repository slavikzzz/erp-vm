#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Сотрудники") Тогда
		
		ЗарплатаКадры.ЗаполнитьПоОснованиюСотрудником(ЭтотОбъект, ДанныеЗаполнения, , Истина, Истина);
		Документы.СведенияОТрудовойДеятельностиРаботникаСТД_Р.ЗаполнитьДокумент(ЭтотОбъект);
		
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.Увольнение") Тогда
		
		ДанныеУвольнения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, "Организация, ФизическоеЛицо");
		Организация = ЗарплатаКадры.ГоловнаяОрганизация(ДанныеУвольнения["Организация"]);
		ФизическоеЛицо = ДанныеУвольнения["ФизическоеЛицо"];
		
		Документы.СведенияОТрудовойДеятельностиРаботникаСТД_Р.ЗаполнитьДокумент(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ОрганизацииДокумента = Новый Массив;
	Для Каждого СтрокаМероприятия Из Мероприятия Цикл
		Если ОрганизацииДокумента.Найти(СтрокаМероприятия.Организация) = Неопределено Тогда
			
			ОшибкиОрганизации = Новый Массив;
			ПерсонифицированныйУчет.ПроверитьДанныеОрганизации(
				ЭтотОбъект, СтрокаМероприятия.Организация, Отказ, КонецМесяца(Дата), ОшибкиОрганизации);
			Если ОшибкиОрганизации.Количество() > 0 Тогда
				
				ПутьКПолю = "Меропрития[" + Формат(СтрокаМероприятия.НомерСтроки - 1, "ЧГ=") + "].Организация";
				ОбщегоНазначения.СообщитьПользователю(
					СтрШаблон(
						НСтр("ru = 'Организация - %1';
							|en = 'Company - %1'"),
						СтрокаМероприятия.Организация),
					Ссылка, ПутьКПолю, "Объект", Отказ);
				Для Каждого Ошибка Из ОшибкиОрганизации Цикл
					ОбщегоНазначения.СообщитьПользователю(Ошибка, Ссылка, ПутьКПолю, "Объект", Отказ);
				КонецЦикла;
			КонецЕсли;
			ОрганизацииДокумента.Добавить(СтрокаМероприятия.Организация);
		КонецЕсли;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(СтраховойНомерПФР) Тогда 
		ТекстСообщения = "";
		Если Не РегламентированныеДанныеКлиентСервер.СтраховойНомерПФРСоответствуетТребованиям(СтраховойНомерПФР, ТекстСообщения) Тогда 
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "Объект.СтраховойНомерПФР" , , Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ФизическоеЛицо", ФизическоеЛицо);
	Запрос.УстановитьПараметр("ТаблицаМероприятий", Мероприятия.Выгрузить());
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаМероприятий.НомерСтроки КАК НомерСтроки,
		|	ТаблицаМероприятий.ИдМероприятия КАК ИдМероприятия,
		|	ТаблицаМероприятий.Отменено КАК Отменено,
		|	ТаблицаМероприятий.Организация КАК Организация
		|ПОМЕСТИТЬ ВТТаблицаМероприятий
		|ИЗ
		|	&ТаблицаМероприятий КАК ТаблицаМероприятий
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаМероприятий.НомерСтроки КАК НомерСтроки,
		|	ТаблицаМероприятий.ИдМероприятия КАК ИдМероприятия,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(Мероприятия.ИдМероприятия, МероприятияПрочие.ИдМероприятия) ЕСТЬ NULL
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК НеСуществующееМероприятие
		|ИЗ
		|	ВТТаблицаМероприятий КАК ТаблицаМероприятий
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МероприятияТрудовойДеятельностиПрочие КАК МероприятияПрочие
		|		ПО ТаблицаМероприятий.ИдМероприятия = МероприятияПрочие.ИдМероприятия
		|			И ТаблицаМероприятий.Организация = МероприятияПрочие.Организация
		|			И (МероприятияПрочие.ФизическоеЛицо = &ФизическоеЛицо)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МероприятияТрудовойДеятельности КАК Мероприятия
		|		ПО ТаблицаМероприятий.ИдМероприятия = Мероприятия.ИдМероприятия
		|			И ТаблицаМероприятий.Отменено = Мероприятия.Отменено
		|			И ТаблицаМероприятий.Организация = Мероприятия.Организация
		|			И (Мероприятия.ФизическоеЛицо = &ФизическоеЛицо)
		|ГДЕ
		|	ЕСТЬNULL(Мероприятия.ИдМероприятия, МероприятияПрочие.ИдМероприятия) ЕСТЬ NULL
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Среди мероприятий есть не зарегистрированные (%1).';
				|en = 'There are unregistered events (%1) among the events.'"), Выборка.ИдМероприятия);
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Ссылка,
			"Объект.Сотрудники[" + Формат(Выборка.НомерСтроки - 1, "ЧН=0; ЧГ=0") + "].Сотрудник" , , Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьОбязательныеПоля(Отказ) Экспорт
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не указана организация.';
													|en = 'Company is not specified.'"), ЭтотОбъект, "Организация", "Объект", Отказ);	
		Возврат;
	КонецЕсли;
	
	ДанныеОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация, "РегистрационныйНомерПФР, КодОрганаПФР");

	Если Не ЗначениеЗаполнено(ДанныеОрганизации.РегистрационныйНомерПФР) Тогда
		ТекстСообщения = НСтр("ru = 'У организации не заполнен регистрационный номер ПФР.';
								|en = 'PF registration number is not specified for the company.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Организация, "РегистрационныйНомерПФР", , Отказ);
	ИначеЕсли СтрДлина(ДанныеОрганизации.РегистрационныйНомерПФР) <> 14 Тогда
		ТекстСообщения = НСтр("ru = 'У организации не верно заполнен регистрационный номер ПФР.';
								|en = 'PF registration number is specified incorrectly for the company.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Организация, "РегистрационныйНомерПФР", , Отказ);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДанныеОрганизации.КодОрганаПФР) Тогда
		ТекстСообщения = НСтр("ru = 'У организации не заполнен код органа ПФР ';
								|en = 'PF authority code is not specified for the company.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Организация, "КодОрганаПФР", , Отказ);
	КонецЕсли;
	
КонецПроцедуры	

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли