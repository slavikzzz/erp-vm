#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	МеханизмыДокумента.Добавить("РеестрДокументов");
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка, ДокументОбъект - ссылка на документ или объект, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * Таблица_ИмяРегистра - ТаблицаЗначений - таблица данных для отражения в регистр (вместо _ИмяРегистра подставляется имя самого регистра).
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Если ТипЗнч(Документ) = Тип("ДокументОбъект.РегламентнаяОперацияМеждународныйУчет") Тогда
		ДокументСсылка = Документ.Ссылка;
	Иначе
		ДокументСсылка = Документ;
	КонецЕсли;
	
	Запрос			= Новый Запрос;
	ТекстыЗапроса	= Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
		
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса
		
		ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры);
		
	КонецЕсли;
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

#КонецОбласти

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.РегламентнаяОперацияМеждународныйУчет) Тогда
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.РегламентнаяОперацияМеждународныйУчет.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.РегламентнаяОперацияМеждународныйУчет);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьМеждународныйФинансовыйУчет";
	КонецЕсли;
	
КонецПроцедуры

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	Возврат; //В дальнейшем будет добавлен код команд
	
КонецПроцедуры

// Определяет количество всех валютных остатков для которых не расчитаны курсовые разницы.
// (ОстатокВВалюте*КурсФункциональнойВалюты <> ОстатокВфункциональнойВалюте).
//
// Параметры:
//  ПланСчетов - СправочникСсылка.ПланыСчетовМеждународногоУчета - План счетов, по которому отбираются валютные остатки.
//  Организации - Массив Из СправочникСсылка.Организации -
//              - СправочникСсылка.Организации - Отбор организаций, по которым определяются валютные остатки.
//  НаДату - Дата - Дата, на которую определяются остатки и курс функциональной валюты. Если не задана, то текущая.
//  ТолькоПервую - Булево - Истина, если нужно проверить только факт отсутствия необходимости расчета курсовых разниц.
//
// Возвращаемое значение:
//  Число - количество остатков, у которых есть расхождения между расчетными и реальными остатками в функциональной валюте.
//
Функция НезакрытыеКурсовыеРазницы(ПланСчетов, Организации, Знач НаДату = Неопределено, ТолькоПервую = Ложь) Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ОтносительныеКурсыВалют.БазоваяВалюта КАК БазоваяВалюта,
	|	ОтносительныеКурсыВалют.Валюта КАК Валюта,
	|	ОтносительныеКурсыВалют.КурсЧислитель КАК КурсЧислитель,
	|	ОтносительныеКурсыВалют.КурсЗнаменатель КАК КурсЗнаменатель
	|ПОМЕСТИТЬ втКурсыВалюты
	|ИЗ
	|	РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних(
	|			&НаДату,
	|			БазоваяВалюта В (
	|				ВЫБРАТЬ
	|					Организации.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета
	|				ИЗ
	|					Справочник.Организации КАК Организации
	|				ГДЕ
	|					Организации.Ссылка В (&Организации))) КАК ОтносительныеКурсыВалют
	|ИНДЕКСИРОВАТЬ ПО
	|	БазоваяВалюта,
	|	Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МеждународныйОстатки.Счет,
	|	МеждународныйОстатки.ПланСчетов,
	|	МеждународныйОстатки.Организация,
	|	МеждународныйОстатки.Подразделение,
	|	МеждународныйОстатки.НаправлениеДеятельности,
	|	МеждународныйОстатки.Валюта,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто1.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто1
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто1,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто2.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто2
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто2,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто3.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто3
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто3,
	|	СУММА(МеждународныйОстатки.СуммаОстаток) КАК СуммаОстаток,
	|	СУММА(МеждународныйОстатки.СуммаРазвернутыйОстатокДт) КАК СуммаОстатокДт,
	|	СУММА(МеждународныйОстатки.СуммаРазвернутыйОстатокКт) КАК СуммаОстатокКт,
	|	СУММА(МеждународныйОстатки.ВалютнаяСуммаОстаток) КАК ВалютнаяСуммаОстаток
	|ПОМЕСТИТЬ втОстаткиСчетов
	|ИЗ
	|	РегистрБухгалтерии.Международный.Остатки(
	|			&Граница,
	|			Счет.Валютный И НЕ Счет.ИсключитьИзРасчетаКурсовыхРазниц,
	|			,
	|			ПланСчетов = &ПланСчетов И Организация В (&Организации)) КАК МеждународныйОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто1
	|		ПО МеждународныйОстатки.Счет = МеждународныйВидыСубконто1.Ссылка
	|			И (МеждународныйВидыСубконто1.НомерСтроки = 1)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто2
	|		ПО МеждународныйОстатки.Счет = МеждународныйВидыСубконто2.Ссылка
	|			И (МеждународныйВидыСубконто2.НомерСтроки = 2)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто3
	|		ПО МеждународныйОстатки.Счет = МеждународныйВидыСубконто3.Ссылка
	|			И (МеждународныйВидыСубконто3.НомерСтроки = 3)
	|
	|СГРУППИРОВАТЬ ПО
	|	МеждународныйОстатки.Счет,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто1.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто1
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто2.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто2
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто3.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто3
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	МеждународныйОстатки.ПланСчетов,
	|	МеждународныйОстатки.Организация,
	|	МеждународныйОстатки.Подразделение,
	|	МеждународныйОстатки.НаправлениеДеятельности,
	|	МеждународныйОстатки.Валюта
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОстаткиСчетов.Счет,
	|	ОстаткиСчетов.ПланСчетов,
	|	ОстаткиСчетов.Организация,
	|	ОстаткиСчетов.Подразделение,
	|	ОстаткиСчетов.НаправлениеДеятельности,
	|	ОстаткиСчетов.Валюта,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто1.Валютный
	|			ТОГДА ОстаткиСчетов.Субконто1
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто1,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто2.Валютный
	|			ТОГДА ОстаткиСчетов.Субконто2
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто2,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто3.Валютный
	|			ТОГДА ОстаткиСчетов.Субконто3
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто3,
	|	СУММА(ОстаткиСчетов.СуммаОстаток) КАК СуммаОстаток,
	|	СУММА(ОстаткиСчетов.СуммаРазвернутыйОстатокДт) КАК СуммаОстатокДт,
	|	СУММА(ОстаткиСчетов.СуммаРазвернутыйОстатокКт) КАК СуммаОстатокКт,
	|	СУММА(ОстаткиСчетов.ВалютнаяСуммаОстаток) КАК ВалютнаяСуммаОстаток
	|ИЗ
	|	РегистрБухгалтерии.МеждународныйБезКорреспонденции.Остатки(
	|			&Граница,
	|			Счет.Валютный И НЕ Счет.ИсключитьИзРасчетаКурсовыхРазниц,
	|			,
	|			ПланСчетов = &ПланСчетов И Организация В (&Организации)) КАК ОстаткиСчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто1
	|		ПО ОстаткиСчетов.Счет = МеждународныйВидыСубконто1.Ссылка
	|			И (МеждународныйВидыСубконто1.НомерСтроки = 1)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто2
	|		ПО ОстаткиСчетов.Счет = МеждународныйВидыСубконто2.Ссылка
	|			И (МеждународныйВидыСубконто2.НомерСтроки = 2)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто3
	|		ПО ОстаткиСчетов.Счет = МеждународныйВидыСубконто3.Ссылка
	|			И (МеждународныйВидыСубконто3.НомерСтроки = 3)
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиСчетов.Счет,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто1.Валютный
	|			ТОГДА ОстаткиСчетов.Субконто1
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто2.Валютный
	|			ТОГДА ОстаткиСчетов.Субконто2
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто3.Валютный
	|			ТОГДА ОстаткиСчетов.Субконто3
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ОстаткиСчетов.ПланСчетов,
	|	ОстаткиСчетов.Организация,
	|	ОстаткиСчетов.Подразделение,
	|	ОстаткиСчетов.НаправлениеДеятельности,
	|	ОстаткиСчетов.Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КурсыВалютРасчетовПоДоговорамСрезПоследних.Договор КАК Договор,
	|	КурсыВалютРасчетовПоДоговорамСрезПоследних.КурсЧислитель КАК КурсЧислитель,
	|	КурсыВалютРасчетовПоДоговорамСрезПоследних.КурсЗнаменатель КАК КурсЗнаменатель
	|ПОМЕСТИТЬ ВтКурсыВалютРасчетовПоДоговорамСрезПоследних
	|
	|ИЗ
	|	РегистрСведений.КурсыВалютРасчетовПоДоговорам.СрезПоследних(&НаДату, ) КАК КурсыВалютРасчетовПоДоговорамСрезПоследних
	|ИНДЕКСИРОВАТЬ ПО
	|	Договор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОстаткиСчетов.Субконто1 КАК Субконто
	|ПОМЕСТИТЬ втОстаткиСчетовВсеСубконто
	|ИЗ
	|	втОстаткиСчетов
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|		
	|ВЫБРАТЬ
	|	втОстаткиСчетов.Субконто2
	|ИЗ
	|	втОстаткиСчетов
	|		
	|ОБЪЕДИНИТЬ ВСЕ
	|		
	|ВЫБРАТЬ
	|	втОстаткиСчетов.Субконто3
	|ИЗ
	|	втОстаткиСчетов	
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Субконто
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка                                             КАК Договор,
	|	ДоговорыКонтрагентов.ВариантКурсаДоговора                               КАК ВариантКурсаДоговора,
	|	ДоговорыКонтрагентов.ВалютаВзаиморасчетов                               КАК ВалютаВзаиморасчетов,
	|	ЕСТЬNULL(КурсыВалютРасчетовПоДоговорамСрезПоследних.КурсЧислитель, 1)   КАК КурсЧислитель,
	|	ЕСТЬNULL(КурсыВалютРасчетовПоДоговорамСрезПоследних.КурсЗнаменатель, 1) КАК КурсЗнаменатель
	|ПОМЕСТИТЬ ВтДоговоры
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтКурсыВалютРасчетовПоДоговорамСрезПоследних КАК КурсыВалютРасчетовПоДоговорамСрезПоследних
	|		ПО ДоговорыКонтрагентов.Ссылка = КурсыВалютРасчетовПоДоговорамСрезПоследних.Договор
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втОстаткиСчетовВсеСубконто КАК втОстаткиСчетовВсеСубконто
	|		ПО втОстаткиСчетовВсеСубконто.Субконто = ДоговорыКонтрагентов.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДоговорыМеждуОрганизациями.Ссылка                                       КАК Договор,
	|	ДоговорыМеждуОрганизациями.ВариантКурсаДоговора                         КАК ВариантКурсаДоговора,
	|	ДоговорыМеждуОрганизациями.ВалютаВзаиморасчетов                         КАК ВалютаВзаиморасчетов,
	|	ЕСТЬNULL(КурсыВалютРасчетовПоДоговорамСрезПоследних.КурсЧислитель, 1)   КАК КурсЧислитель,
	|	ЕСТЬNULL(КурсыВалютРасчетовПоДоговорамСрезПоследних.КурсЗнаменатель, 1) КАК КурсЗнаменатель
	|ИЗ
	|	Справочник.ДоговорыМеждуОрганизациями КАК ДоговорыМеждуОрганизациями
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтКурсыВалютРасчетовПоДоговорамСрезПоследних КАК КурсыВалютРасчетовПоДоговорамСрезПоследних
	|		ПО ДоговорыМеждуОрганизациями.Ссылка = КурсыВалютРасчетовПоДоговорамСрезПоследних.Договор
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втОстаткиСчетовВсеСубконто КАК втОстаткиСчетовВсеСубконто
	|		ПО втОстаткиСчетовВсеСубконто.Субконто = ДоговорыМеждуОрганизациями.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиСчетов.Счет                                                КАК Счет,
	|	ОстаткиСчетов.ПланСчетов                                          КАК ПланСчетов,
	|	ОстаткиСчетов.Организация                                         КАК Организация,
	|	ОстаткиСчетов.Подразделение                                       КАК Подразделение,
	|	ОстаткиСчетов.НаправлениеДеятельности                             КАК НаправлениеДеятельности,
	|	ОстаткиСчетов.Субконто1                                           КАК Субконто1,
	|	ОстаткиСчетов.Субконто2                                           КАК Субконто2,
	|	ОстаткиСчетов.Субконто3                                           КАК Субконто3,
	|	ОстаткиСчетов.Валюта                                              КАК Валюта,
	|	ОстаткиСчетов.ВалютнаяСуммаОстаток                                КАК ВалютнаяСуммаОстаток,
	|	ОстаткиСчетов.СуммаОстаток                                        КАК СуммаОстаток,
	|	ЕСТЬNULL(
	|		ЕСТЬNULL(
	|			втДоговоры1.ВариантКурсаДоговора,
	|			втДоговоры2.ВариантКурсаДоговора),
	|		ЕСТЬNULL(
	|			втДоговоры3.ВариантКурсаДоговора,
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.Переменный))) КАК ВариантКурсаДоговора,
	|	ЕСТЬNULL(
	|		ЕСТЬNULL(
	|			втДоговоры1.КурсЧислитель,
	|			втДоговоры2.КурсЧислитель),
	|		ЕСТЬNULL(
	|			втДоговоры3.КурсЧислитель,
	|			1))                                                       КАК КурсЧислитель,
	|	ЕСТЬNULL(
	|		ЕСТЬNULL(
	|			втДоговоры1.КурсЗнаменатель,
	|			втДоговоры2.КурсЗнаменатель),
	|		ЕСТЬNULL(
	|			втДоговоры3.КурсЗнаменатель,
	|			1))                                                       КАК КурсЗнаменатель,
	|	ЕСТЬNULL(
	|		ЕСТЬNULL(
	|			втДоговоры1.ВалютаВзаиморасчетов,
	|			втДоговоры2.ВалютаВзаиморасчетов),
	|		ЕСТЬNULL(
	|			втДоговоры3.ВалютаВзаиморасчетов,
	|			НЕОПРЕДЕЛЕНО))                                            КАК ВалютаВзаиморасчетов
	|ПОМЕСТИТЬ ВтОстаткиСчетовСКурсомДоговора
	|ИЗ
	|	втОстаткиСчетов КАК ОстаткиСчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтДоговоры КАК втДоговоры1
	|		ПО ОстаткиСчетов.Субконто1 = втДоговоры1.Договор
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтДоговоры КАК втДоговоры2
	|		ПО ОстаткиСчетов.Субконто2 = втДоговоры2.Договор
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтДоговоры КАК втДоговоры3
	|		ПО ОстаткиСчетов.Субконто3 = втДоговоры3.Договор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВтОстаткиСКурсомДоговора.Счет                    КАК Счет,
	|	ВтОстаткиСКурсомДоговора.ПланСчетов              КАК ПланСчетов,
	|	ВтОстаткиСКурсомДоговора.Организация             КАК Организация,
	|	ВтОстаткиСКурсомДоговора.Подразделение           КАК Подразделение,
	|	ВтОстаткиСКурсомДоговора.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВтОстаткиСКурсомДоговора.Субконто1               КАК Субконто1,
	|	ВтОстаткиСКурсомДоговора.Субконто2               КАК Субконто2,
	|	ВтОстаткиСКурсомДоговора.Субконто3               КАК Субконто3,
	|	ВтОстаткиСКурсомДоговора.Валюта                  КАК Валюта,
	|	ВтОстаткиСКурсомДоговора.ВалютнаяСуммаОстаток    КАК ВалютнаяСуммаОстаток,
	|	ВтОстаткиСКурсомДоговора.СуммаОстаток            КАК СуммаОстаток,
	|	ВтОстаткиСКурсомДоговора.ВариантКурсаДоговора    КАК ВариантКурсаДоговора,
	|	ВЫБОР
	|		КОГДА ВтОстаткиСКурсомДоговора.ВариантКурсаДоговора = ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.УстановленныйВДоговоре)
	|				И ВтОстаткиСКурсомДоговора.Валюта = ВтОстаткиСКурсомДоговора.ВалютаВзаиморасчетов
	|			ТОГДА ВтОстаткиСКурсомДоговора.КурсЧислитель
	|		ИНАЧЕ втКурсыВалюты.КурсЧислитель
	|	КОНЕЦ                                            КАК КурсЧислитель,
	|	ВЫБОР
	|		КОГДА ВтОстаткиСКурсомДоговора.ВариантКурсаДоговора = ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.УстановленныйВДоговоре)
	|				И ВтОстаткиСКурсомДоговора.Валюта = ВтОстаткиСКурсомДоговора.ВалютаВзаиморасчетов
	|			ТОГДА ВтОстаткиСКурсомДоговора.КурсЗнаменатель
	|		ИНАЧЕ втКурсыВалюты.КурсЗнаменатель
	|	КОНЕЦ                                            КАК КурсЗнаменатель,
	|	ВЫБОР
	|		КОГДА ВтОстаткиСКурсомДоговора.ВариантКурсаДоговора = ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.УстановленныйВДоговоре)
	|				И ВЫБОР
	|					КОГДА ВтОстаткиСКурсомДоговора.ПланСчетов.УчетВФункциональнойВалюте = ЗНАЧЕНИЕ(Перечисление.ВидыУчетаВФункциональнойВалюте.ВВалютеРегл)
	|						ТОГДА ВтОстаткиСКурсомДоговора.Организация.ВалютаРегламентированногоУчета
	|					ИНАЧЕ &ВалютаУправленческогоУчета
	|				КОНЕЦ = ВтОстаткиСКурсомДоговора.ВалютаВзаиморасчетов
	|			ТОГДА ВтОстаткиСКурсомДоговора.КурсЧислитель
	|		ИНАЧЕ втКурсыВалютыФункциональной.КурсЧислитель
	|	КОНЕЦ                                            КАК КурсЧислительФункциональнойВалюты,
	|	ВЫБОР
	|		КОГДА ВтОстаткиСКурсомДоговора.ВариантКурсаДоговора = ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.УстановленныйВДоговоре)
	|				И ВЫБОР
	|					КОГДА ВтОстаткиСКурсомДоговора.ПланСчетов.УчетВФункциональнойВалюте = ЗНАЧЕНИЕ(Перечисление.ВидыУчетаВФункциональнойВалюте.ВВалютеРегл)
	|						ТОГДА ВтОстаткиСКурсомДоговора.Организация.ВалютаРегламентированногоУчета
	|					ИНАЧЕ &ВалютаУправленческогоУчета
	|				КОНЕЦ = ВтОстаткиСКурсомДоговора.ВалютаВзаиморасчетов
	|			ТОГДА ВтОстаткиСКурсомДоговора.КурсЗнаменатель
	|		ИНАЧЕ втКурсыВалютыФункциональной.КурсЗнаменатель
	|	КОНЕЦ                                            КАК КурсЗнаменательФункциональнойВалюты
	|ПОМЕСТИТЬ ВтОстаткиСчетовСКурсами
	|ИЗ
	|	ВтОстаткиСчетовСКурсомДоговора КАК ВтОстаткиСКурсомДоговора
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКурсыВалюты КАК втКурсыВалюты
	|		ПО ВтОстаткиСКурсомДоговора.Организация.ВалютаРегламентированногоУчета = втКурсыВалюты.БазоваяВалюта
	|			И ВтОстаткиСКурсомДоговора.Валюта = втКурсыВалюты.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКурсыВалюты КАК втКурсыВалютыФункциональной
	|		ПО ВтОстаткиСКурсомДоговора.Организация.ВалютаРегламентированногоУчета = втКурсыВалютыФункциональной.БазоваяВалюта
	|			И (ВЫБОР
	|				КОГДА ВтОстаткиСКурсомДоговора.ПланСчетов.УчетВФункциональнойВалюте = ЗНАЧЕНИЕ(Перечисление.ВидыУчетаВФункциональнойВалюте.ВВалютеРегл)
	|					ТОГДА ВтОстаткиСКурсомДоговора.Организация.ВалютаРегламентированногоУчета = втКурсыВалютыФункциональной.Валюта
	|				ИНАЧЕ &ВалютаУправленческогоУчета = втКурсыВалютыФункциональной.Валюта
	|			КОНЕЦ)
	|ГДЕ
	|	ВтОстаткиСКурсомДоговора.ВариантКурсаДоговора <> ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.ФиксированныйНаДатуОтгрузки)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОстаткиСчетов.Счет,
	|	ОстаткиСчетов.Валюта КАК Валюта,
	|	(ВЫРАЗИТЬ(ОстаткиСчетов.ВалютнаяСуммаОстаток 
	|		* ОстаткиСчетов.КурсЧислитель 
	|		/ ОстаткиСчетов.КурсЗнаменатель 
	|		/ ОстаткиСчетов.КурсЧислительФункциональнойВалюты 
	|		* ОстаткиСчетов.КурсЗнаменательФункциональнойВалюты КАК ЧИСЛО(31,2))) - ОстаткиСчетов.СуммаОстаток КАК КурсоваяРазница,
	|	ОстаткиСчетов.ПланСчетов,
	|	ОстаткиСчетов.Организация,
	|	ОстаткиСчетов.Подразделение,
	|	ОстаткиСчетов.НаправлениеДеятельности,
	|	ОстаткиСчетов.Субконто1,
	|	ОстаткиСчетов.Субконто2,
	|	ОстаткиСчетов.Субконто3
	|ИЗ
	|	ВтОстаткиСчетовСКурсами КАК ОстаткиСчетов
	|
	|ГДЕ
	|	(ВЫРАЗИТЬ(ОстаткиСчетов.ВалютнаяСуммаОстаток 
	|		* ОстаткиСчетов.КурсЧислитель 
	|		/ ОстаткиСчетов.КурсЗнаменатель 
	|		/ ОстаткиСчетов.КурсЧислительФункциональнойВалюты 
	|		* ОстаткиСчетов.КурсЗнаменательФункциональнойВалюты КАК ЧИСЛО(31,2))) - ОстаткиСчетов.СуммаОстаток <> 0";
	
	Возврат ВсегоОсталосьКурсовыхРазниц(ТекстЗапроса, ПланСчетов, Организации, НаДату, ТолькоПервую);
	
КонецФункции

// Определяет количество всех остатков в функциональной валюте которые не пересчитаны в валюту представления.
// (ОстатокВФункциональнойВалюте*КоэффициентПересчетаВВалютуПредставления <> ОстатокВВалютеПредставления).
//
// Параметры:
//  ПланСчетов - СправочникСсылка.ПланыСчетовМеждународногоУчета - План счетов, по которому отбираются валютные остатки.
//  Организации - Массив Из СправочникСсылка.Организации -
//              - СправочникСсылка.Организации - Отбор организаций, по которым определяются валютные остатки.
//  НаДату - Дата - Дата, на которую определяются остатки и курс функциональной валюты. Если не задана, то текущая.
//  ТолькоПервую - Булево - Истина, если нужно проверить только факт отсутствия необходимости расчета курсовых разниц.
//
// Возвращаемое значение:
//  Число - количество остатков в функциональной валюте у которых есть расхождения между расчетными и реальными
//          остатками в валюте представления.
//
Функция НеПересчитаноВВалютуПредставления(ПланСчетов, Организации, НаДату = Неопределено, ТолькоПервую = Ложь) Экспорт

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	НастройкиНаДату.ПланСчетов КАК ПланСчетов,
	|	НастройкиНаДату.Организация КАК Организация,
	|	НастройкиНаДату.НастройкаФормированияПроводок КАК НастройкаФормированияПроводок,
	|	УчетныеПолитики.ДоходыОтРазницПриПересчетеВП КАК ДоходыКР,
	|	УчетныеПолитики.РасходыОтРазницПриПересчетеВП КАК РасходыКР
	|ПОМЕСТИТЬ втСчетаУчетаКР
	|ИЗ
	|	РегистрСведений.ПланыСчетовМеждународногоУчетаОрганизаций.СрезПоследних(
	|			&НаДату,
	|			ПланСчетов = &ПланСчетов И Организация В (&Организации)) КАК НастройкиНаДату
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НастройкиФормированияПроводокМеждународногоУчета КАК УчетныеПолитики
	|		ПО НастройкиНаДату.НастройкаФормированияПроводок = УчетныеПолитики.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ИсключитьИзПересчетаВП.Счет КАК Счет
	|ПОМЕСТИТЬ втСчетаИсключения
	|ИЗ
	|	Справочник.НастройкиФормированияПроводокМеждународногоУчета.ИсключитьИзПересчетаВП КАК ИсключитьИзПересчетаВП
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втСчетаУчетаКР.ДоходыКР
	|ИЗ
	|	втСчетаУчетаКР КАК втСчетаУчетаКР
	|ГДЕ
	|	втСчетаУчетаКР.ДоходыКР <> ЗНАЧЕНИЕ(ПланСчетов.Международный.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втСчетаУчетаКР.РасходыКР
	|ИЗ
	|	втСчетаУчетаКР КАК втСчетаУчетаКР
	|ГДЕ
	|	втСчетаУчетаКР.РасходыКР <> ЗНАЧЕНИЕ(ПланСчетов.Международный.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтносительныеКурсыВалют.БазоваяВалюта КАК БазоваяВалюта,
	|	ОтносительныеКурсыВалют.Валюта КАК Валюта,
	|	ОтносительныеКурсыВалют.КурсЧислитель КАК КурсЧислитель,
	|	ОтносительныеКурсыВалют.КурсЗнаменатель КАК КурсЗнаменатель
	|ПОМЕСТИТЬ втКурсыВалюты
	|ИЗ
	|	РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних(
	|			&НаДату,
	|			БазоваяВалюта В (
	|				ВЫБРАТЬ
	|					Организации.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета
	|				ИЗ
	|					Справочник.Организации КАК Организации
	|				ГДЕ
	|					Организации.Ссылка В (&Организации))) КАК ОтносительныеКурсыВалют
	|ИНДЕКСИРОВАТЬ ПО
	|	БазоваяВалюта,
	|	Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МеждународныйОстатки.Счет КАК Счет,
	|	МеждународныйОстатки.ПланСчетов КАК ПланСчетов,
	|	МеждународныйОстатки.Организация КАК Организация,
	|	МеждународныйОстатки.Подразделение КАК Подразделение,
	|	МеждународныйОстатки.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	МеждународныйОстатки.Валюта КАК Валюта,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто1.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто1
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто1,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто2.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто2
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто2,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто3.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто3
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто3,
	|	СУММА(МеждународныйОстатки.СуммаОстаток) КАК СуммаОстаток,
	|	СУММА(МеждународныйОстатки.СуммаПредставленияОстаток) КАК СуммаПредставленияОстаток
	|ПОМЕСТИТЬ втОстаткиСчетов
	|ИЗ
	|	РегистрБухгалтерии.Международный.Остатки(
	|			&Граница,
	|			НЕ Счет В (ВЫБРАТЬ Счет ИЗ втСчетаИсключения),
	|			,
	|			ПланСчетов = &ПланСчетов И Организация В (&Организации)) КАК МеждународныйОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСчетаУчетаКР КАК СчетаУчетаКР
	|		ПО МеждународныйОстатки.ПланСчетов = СчетаУчетаКР.ПланСчетов
	|			И МеждународныйОстатки.Организация = СчетаУчетаКР.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто1
	|		ПО (МеждународныйВидыСубконто1.НомерСтроки = 1)
	|			И МеждународныйОстатки.Счет = МеждународныйВидыСубконто1.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто2
	|		ПО (МеждународныйВидыСубконто2.НомерСтроки = 2)
	|			И МеждународныйОстатки.Счет = МеждународныйВидыСубконто2.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто3
	|		ПО (МеждународныйВидыСубконто3.НомерСтроки = 3)
	|			И МеждународныйОстатки.Счет = МеждународныйВидыСубконто3.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	МеждународныйОстатки.Счет,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто1.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто1
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто2.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто2
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто3.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто3
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	МеждународныйОстатки.ПланСчетов,
	|	МеждународныйОстатки.Организация,
	|	МеждународныйОстатки.Подразделение,
	|	МеждународныйОстатки.НаправлениеДеятельности,
	|	МеждународныйОстатки.Валюта
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОстаткиСчетов.Счет КАК Счет,
	|	ОстаткиСчетов.ПланСчетов КАК ПланСчетов,
	|	ОстаткиСчетов.Организация КАК Организация,
	|	ОстаткиСчетов.Подразделение КАК Подразделение,
	|	ОстаткиСчетов.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ОстаткиСчетов.Валюта КАК Валюта,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто1.Валютный
	|			ТОГДА ОстаткиСчетов.Субконто1
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто1,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто2.Валютный
	|			ТОГДА ОстаткиСчетов.Субконто2
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто2,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто3.Валютный
	|			ТОГДА ОстаткиСчетов.Субконто3
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто3,
	|	СУММА(ОстаткиСчетов.СуммаОстаток) КАК СуммаОстаток,
	|	СУММА(ОстаткиСчетов.СуммаПредставленияОстаток) КАК СуммаПредставленияОстаток
	|ИЗ
	|	РегистрБухгалтерии.МеждународныйБезКорреспонденции.Остатки(
	|			&Граница,
	|			НЕ Счет В (ВЫБРАТЬ Счет ИЗ втСчетаИсключения),
	|			,
	|			ПланСчетов = &ПланСчетов И Организация В (&Организации)) КАК ОстаткиСчетов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСчетаУчетаКР КАК СчетаУчетаКР
	|		ПО ОстаткиСчетов.ПланСчетов = СчетаУчетаКР.ПланСчетов
	|			И ОстаткиСчетов.Организация = СчетаУчетаКР.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто1
	|		ПО (МеждународныйВидыСубконто1.НомерСтроки = 1)
	|			И ОстаткиСчетов.Счет = МеждународныйВидыСубконто1.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто2
	|		ПО (МеждународныйВидыСубконто2.НомерСтроки = 2)
	|			И ОстаткиСчетов.Счет = МеждународныйВидыСубконто2.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто3
	|		ПО (МеждународныйВидыСубконто3.НомерСтроки = 3)
	|			И ОстаткиСчетов.Счет = МеждународныйВидыСубконто3.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиСчетов.Счет,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто1.Валютный
	|			ТОГДА ОстаткиСчетов.Субконто1
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто2.Валютный
	|			ТОГДА ОстаткиСчетов.Субконто2
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто3.Валютный
	|			ТОГДА ОстаткиСчетов.Субконто3
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ОстаткиСчетов.ПланСчетов,
	|	ОстаткиСчетов.Организация,
	|	ОстаткиСчетов.Подразделение,
	|	ОстаткиСчетов.НаправлениеДеятельности,
	|	ОстаткиСчетов.Валюта
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КурсДоговора.Договор КАК Договор,
	|	КурсДоговора.КурсЧислитель КАК КурсЧислитель,
	|	КурсДоговора.КурсЗнаменатель КАК КурсЗнаменатель
	|ПОМЕСТИТЬ втКурсыДоговоров
	|ИЗ
	|	РегистрСведений.КурсыВалютРасчетовПоДоговорам.СрезПоследних(&НаДату) КАК КурсДоговора
	|ИНДЕКСИРОВАТЬ ПО
	|	Договор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОстаткиСчетов.Субконто1 КАК Субконто
	|ПОМЕСТИТЬ втОстаткиСчетовВсеСубконто
	|ИЗ
	|	втОстаткиСчетов
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|		
	|ВЫБРАТЬ
	|	втОстаткиСчетов.Субконто2
	|ИЗ
	|	втОстаткиСчетов
	|		
	|ОБЪЕДИНИТЬ ВСЕ
	|		
	|ВЫБРАТЬ
	|	втОстаткиСчетов.Субконто3
	|ИЗ
	|	втОстаткиСчетов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Субконто
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка                                             КАК Договор,
	|	ДоговорыКонтрагентов.ВариантКурсаДоговора                               КАК ВариантКурсаДоговора,
	|	ДоговорыКонтрагентов.ВалютаВзаиморасчетов                               КАК ВалютаВзаиморасчетов,
	|	ЕСТЬNULL(КурсыВалютРасчетовПоДоговорамСрезПоследних.КурсЧислитель, 1)   КАК КурсЧислитель,
	|	ЕСТЬNULL(КурсыВалютРасчетовПоДоговорамСрезПоследних.КурсЗнаменатель, 1) КАК КурсЗнаменатель
	|ПОМЕСТИТЬ ВтДоговоры
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКурсыДоговоров КАК КурсыВалютРасчетовПоДоговорамСрезПоследних
	|		ПО ДоговорыКонтрагентов.Ссылка = КурсыВалютРасчетовПоДоговорамСрезПоследних.Договор
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втОстаткиСчетовВсеСубконто КАК втОстаткиСчетовВсеСубконто
	|		ПО втОстаткиСчетовВсеСубконто.Субконто = ДоговорыКонтрагентов.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДоговорыМеждуОрганизациями.Ссылка                                       КАК Договор,
	|	ДоговорыМеждуОрганизациями.ВариантКурсаДоговора                         КАК ВариантКурсаДоговора,
	|	ДоговорыМеждуОрганизациями.ВалютаВзаиморасчетов                         КАК ВалютаВзаиморасчетов,
	|	ЕСТЬNULL(КурсыВалютРасчетовПоДоговорамСрезПоследних.КурсЧислитель, 1)   КАК КурсЧислитель,
	|	ЕСТЬNULL(КурсыВалютРасчетовПоДоговорамСрезПоследних.КурсЗнаменатель, 1) КАК КурсЗнаменатель
	|ИЗ
	|	Справочник.ДоговорыМеждуОрганизациями КАК ДоговорыМеждуОрганизациями
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКурсыДоговоров КАК КурсыВалютРасчетовПоДоговорамСрезПоследних
	|		ПО ДоговорыМеждуОрганизациями.Ссылка = КурсыВалютРасчетовПоДоговорамСрезПоследних.Договор
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втОстаткиСчетовВсеСубконто КАК втОстаткиСчетовВсеСубконто
	|		ПО втОстаткиСчетовВсеСубконто.Субконто = ДоговорыМеждуОрганизациями.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиСчетов.Счет                                                КАК Счет,
	|	ОстаткиСчетов.ПланСчетов                                          КАК ПланСчетов,
	|	ОстаткиСчетов.Организация                                         КАК Организация,
	|	ОстаткиСчетов.Подразделение                                       КАК Подразделение,
	|	ОстаткиСчетов.НаправлениеДеятельности                             КАК НаправлениеДеятельности,
	|	ОстаткиСчетов.Субконто1                                           КАК Субконто1,
	|	ОстаткиСчетов.Субконто2                                           КАК Субконто2,
	|	ОстаткиСчетов.Субконто3                                           КАК Субконто3,
	|	ОстаткиСчетов.Валюта                                              КАК Валюта,
	|	ОстаткиСчетов.СуммаПредставленияОстаток                           КАК СуммаПредставленияОстаток,
	|	ОстаткиСчетов.СуммаОстаток                                        КАК СуммаОстаток,
	|	ЕСТЬNULL(
	|		ЕСТЬNULL(
	|			втДоговоры1.ВариантКурсаДоговора,
	|			втДоговоры2.ВариантКурсаДоговора),
	|		ЕСТЬNULL(
	|			втДоговоры3.ВариантКурсаДоговора,
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.Переменный))) КАК ВариантКурсаДоговора,
	|	ЕСТЬNULL(
	|		ЕСТЬNULL(
	|			втДоговоры1.КурсЧислитель,
	|			втДоговоры2.КурсЧислитель),
	|		ЕСТЬNULL(
	|			втДоговоры3.КурсЧислитель,
	|			1))                                                       КАК КурсЧислитель,
	|	ЕСТЬNULL(
	|		ЕСТЬNULL(
	|			втДоговоры1.КурсЗнаменатель,
	|			втДоговоры2.КурсЗнаменатель),
	|		ЕСТЬNULL(
	|			втДоговоры3.КурсЗнаменатель,
	|			1))                                                       КАК КурсЗнаменатель,
	|	ЕСТЬNULL(
	|		ЕСТЬNULL(
	|			втДоговоры1.ВалютаВзаиморасчетов,
	|			втДоговоры2.ВалютаВзаиморасчетов),
	|		ЕСТЬNULL(
	|			втДоговоры3.ВалютаВзаиморасчетов,
	|			НЕОПРЕДЕЛЕНО))                                            КАК ВалютаВзаиморасчетов
	|ПОМЕСТИТЬ ВтОстаткиСчетовСКурсомДоговора
	|ИЗ
	|	втОстаткиСчетов КАК ОстаткиСчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтДоговоры КАК втДоговоры1
	|		ПО ОстаткиСчетов.Субконто1 = втДоговоры1.Договор
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтДоговоры КАК втДоговоры2
	|		ПО ОстаткиСчетов.Субконто2 = втДоговоры2.Договор
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтДоговоры КАК втДоговоры3
	|		ПО ОстаткиСчетов.Субконто3 = втДоговоры3.Договор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВтОстаткиСКурсомДоговора.Счет                      КАК Счет,
	|	ВтОстаткиСКурсомДоговора.ПланСчетов                КАК ПланСчетов,
	|	ВтОстаткиСКурсомДоговора.Организация               КАК Организация,
	|	ВтОстаткиСКурсомДоговора.Подразделение             КАК Подразделение,
	|	ВтОстаткиСКурсомДоговора.НаправлениеДеятельности   КАК НаправлениеДеятельности,
	|	ВтОстаткиСКурсомДоговора.Субконто1                 КАК Субконто1,
	|	ВтОстаткиСКурсомДоговора.Субконто2                 КАК Субконто2,
	|	ВтОстаткиСКурсомДоговора.Субконто3                 КАК Субконто3,
	|	ВтОстаткиСКурсомДоговора.Валюта                    КАК Валюта,
	|	ВтОстаткиСКурсомДоговора.СуммаПредставленияОстаток КАК СуммаПредставленияОстаток,
	|	ВтОстаткиСКурсомДоговора.СуммаОстаток              КАК СуммаОстаток,
	|	ВтОстаткиСКурсомДоговора.ВариантКурсаДоговора      КАК ВариантКурсаДоговора,
	|	ВЫБОР
	|		КОГДА ВтОстаткиСКурсомДоговора.ВариантКурсаДоговора = ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.УстановленныйВДоговоре)
	|				И ВтОстаткиСКурсомДоговора.ПланСчетов.ВалютаПредставления = ВтОстаткиСКурсомДоговора.ВалютаВзаиморасчетов
	|			ТОГДА ВтОстаткиСКурсомДоговора.КурсЧислитель
	|		ИНАЧЕ КурсыВалютыПредставления.КурсЧислитель
	|	КОНЕЦ                                              КАК КурсЧислительВалютыПредставления,
	|	ВЫБОР
	|		КОГДА ВтОстаткиСКурсомДоговора.ВариантКурсаДоговора = ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.УстановленныйВДоговоре)
	|				И ВтОстаткиСКурсомДоговора.ПланСчетов.ВалютаПредставления = ВтОстаткиСКурсомДоговора.ВалютаВзаиморасчетов
	|			ТОГДА ВтОстаткиСКурсомДоговора.КурсЗнаменатель
	|		ИНАЧЕ КурсыВалютыПредставления.КурсЗнаменатель
	|	КОНЕЦ                                              КАК КурсЗнаменательВалютыПредставления,
	|	ВЫБОР
	|		КОГДА ВтОстаткиСКурсомДоговора.ВариантКурсаДоговора = ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.УстановленныйВДоговоре)
	|				И ВЫБОР
	|					КОГДА ВтОстаткиСКурсомДоговора.ПланСчетов.УчетВФункциональнойВалюте = ЗНАЧЕНИЕ(Перечисление.ВидыУчетаВФункциональнойВалюте.ВВалютеРегл)
	|						ТОГДА ВтОстаткиСКурсомДоговора.Организация.ВалютаРегламентированногоУчета
	|					ИНАЧЕ &ВалютаУправленческогоУчета
	|				КОНЕЦ = ВтОстаткиСКурсомДоговора.ВалютаВзаиморасчетов
	|			ТОГДА ВтОстаткиСКурсомДоговора.КурсЧислитель
	|		ИНАЧЕ КурсыВалютыФункциональной.КурсЧислитель
	|	КОНЕЦ                                              КАК КурсЧислительФункциональнойВалюты,
	|	ВЫБОР
	|		КОГДА ВтОстаткиСКурсомДоговора.ВариантКурсаДоговора = ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.УстановленныйВДоговоре)
	|				И ВЫБОР
	|					КОГДА ВтОстаткиСКурсомДоговора.ПланСчетов.УчетВФункциональнойВалюте = ЗНАЧЕНИЕ(Перечисление.ВидыУчетаВФункциональнойВалюте.ВВалютеРегл)
	|						ТОГДА ВтОстаткиСКурсомДоговора.Организация.ВалютаРегламентированногоУчета
	|					ИНАЧЕ &ВалютаУправленческогоУчета
	|				КОНЕЦ = ВтОстаткиСКурсомДоговора.ВалютаВзаиморасчетов
	|			ТОГДА ВтОстаткиСКурсомДоговора.КурсЗнаменатель
	|		ИНАЧЕ КурсыВалютыФункциональной.КурсЗнаменатель
	|	КОНЕЦ                                              КАК КурсЗнаменательФункциональнойВалюты
	|ПОМЕСТИТЬ ВтОстаткиСчетовСКурсами
	|ИЗ
	|	ВтОстаткиСчетовСКурсомДоговора КАК ВтОстаткиСКурсомДоговора
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		втКурсыВалюты КАК КурсыВалютыПредставления
	|	ПО
	|		ВтОстаткиСКурсомДоговора.Организация.ВалютаРегламентированногоУчета = КурсыВалютыПредставления.БазоваяВалюта
	|		И ВтОстаткиСКурсомДоговора.ПланСчетов.ВалютаПредставления = КурсыВалютыПредставления.Валюта
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		втКурсыВалюты КАК КурсыВалютыФункциональной
	|	ПО
	|		ВтОстаткиСКурсомДоговора.Организация.ВалютаРегламентированногоУчета = КурсыВалютыФункциональной.БазоваяВалюта
	|		И ВЫБОР 
	|			КОГДА ВтОстаткиСКурсомДоговора.ПланСчетов.УчетВФункциональнойВалюте = ЗНАЧЕНИЕ(Перечисление.ВидыУчетаВФункциональнойВалюте.ВВалютеРегл)
	|				ТОГДА ВтОстаткиСКурсомДоговора.Организация.ВалютаРегламентированногоУчета = КурсыВалютыФункциональной.Валюта
	|			ИНАЧЕ &ВалютаУправленческогоУчета = КурсыВалютыФункциональной.Валюта
	|		КОНЕЦ
	|ГДЕ
	|	ВтОстаткиСКурсомДоговора.ВариантКурсаДоговора <> ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.ФиксированныйНаДатуОтгрузки)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОстаткиСчетов.Счет КАК Счет,
	|	ОстаткиСчетов.Валюта КАК Валюта,
	|	ВЫРАЗИТЬ(ОстаткиСчетов.СуммаОстаток 
	|		* ОстаткиСчетов.КурсЧислительФункциональнойВалюты
	|		/ ОстаткиСчетов.КурсЗнаменательФункциональнойВалюты 
	|		/ ОстаткиСчетов.КурсЧислительВалютыПредставления 
	|		* ОстаткиСчетов.КурсЗнаменательВалютыПредставления КАК ЧИСЛО(31,2)) КАК СуммаВП,
	|	ОстаткиСчетов.СуммаПредставленияОстаток КАК СуммаВПОстаток,
	|	(ВЫРАЗИТЬ(ОстаткиСчетов.СуммаОстаток 
	|		* ОстаткиСчетов.КурсЧислительФункциональнойВалюты 
	|		/ ОстаткиСчетов.КурсЗнаменательФункциональнойВалюты
	|		/ ОстаткиСчетов.КурсЧислительВалютыПредставления
	|		* ОстаткиСчетов.КурсЗнаменательВалютыПредставления КАК ЧИСЛО(31,2))) - ОстаткиСчетов.СуммаПредставленияОстаток КАК КурсоваяРазница,
	|	ОстаткиСчетов.ПланСчетов КАК ПланСчетов,
	|	ОстаткиСчетов.Организация КАК Организация,
	|	ОстаткиСчетов.Подразделение КАК Подразделение,
	|	ОстаткиСчетов.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ОстаткиСчетов.Субконто1 КАК Субконто1,
	|	ОстаткиСчетов.Субконто2 КАК Субконто2,
	|	ОстаткиСчетов.Субконто3 КАК Субконто3
	|ИЗ
	|	ВтОстаткиСчетовСКурсами КАК ОстаткиСчетов
	|ГДЕ
	|	ЕСТЬNULL(ОстаткиСчетов.Валюта, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)) <> ОстаткиСчетов.ПланСчетов.ВалютаПредставления
	|	И (ВЫРАЗИТЬ(ОстаткиСчетов.СуммаОстаток 
	|		* ОстаткиСчетов.КурсЧислительФункциональнойВалюты 
	|		/ ОстаткиСчетов.КурсЗнаменательФункциональнойВалюты
	|		/ ОстаткиСчетов.КурсЧислительВалютыПредставления
	|		* ОстаткиСчетов.КурсЗнаменательВалютыПредставления КАК ЧИСЛО(31,2))) - ОстаткиСчетов.СуммаПредставленияОстаток <> 0";

	Возврат ВсегоОсталосьКурсовыхРазниц(ТекстЗапроса, ПланСчетов, Организации, НаДату, ТолькоПервую);

КонецФункции

// Определяет количество незакрытых счетов учета доходов и расходов на дату.
// 
// Параметры:
//  ПланСчетов - СправочникСсылка.ПланыСчетовМеждународногоУчета - План счетов, по которому выполняется проверка.
//  Организации - Массив Из СправочникСсылка.Организации -
//              - СправочникСсылка.Организации - Отбор организаций, по которым выполняется проверка.
//  НаДату - Дата - Дата, на которую выполняется проверка. Если не задана, то текущая.
// Возвращаемое значение:
// 	Число - количество незакрытых счетов
Функция ЕстьНезакрытыеСчетаУчетаДоходовРасходов(ПланСчетов, Организации, НаДату = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(НаДату) Тогда
		НаДату = КонецДня(НаДату);
	Иначе
		НаДату = КонецДня(ТекущаяДатаСеанса());
	КонецЕсли;
	
	ПериодичностьЗакрытияСчетов = Новый Массив();
	ПериодичностьЗакрытияСчетов.Добавить(Перечисления.ЗакрытиеСчетовДоходовИРасходов.Ежемесячно);
	
	Если КонецМесяца(НаДату) = КонецГода(НаДату) Тогда
		ПериодичностьЗакрытияСчетов.Добавить(Перечисления.ЗакрытиеСчетовДоходовИРасходов.ВКонцеГода);
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	НастройкиНаДату.ПланСчетов КАК ПланСчетов,
	|	НастройкиНаДату.НастройкаФормированияПроводок КАК НастройкаФормированияПроводок
	|ПОМЕСТИТЬ втНастройкиФормированияПроводок
	|ИЗ
	|	РегистрСведений.ПланыСчетовМеждународногоУчетаОрганизаций.СрезПоследних(
	|			&НаДату,
	|			ПланСчетов = &ПланСчетов И Организация В (&Организации)) КАК НастройкиНаДату
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НастройкиФормированияПроводокМеждународногоУчета КАК НастройкиФормированияПроводок
	|		ПО НастройкиНаДату.НастройкаФормированияПроводок = НастройкиФормированияПроводок.Ссылка
	|ГДЕ
	|	НастройкиФормированияПроводок.ЗакрытиеСчетовДоходовИРасходов В (&ПериодичностьЗакрытияСчетов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.ПланСчетов КАК ПланСчетов,
	|	Т.Счет КАК Счет
	|ПОМЕСТИТЬ втСчетаЗакрытия
	|ИЗ
	|	(ВЫБРАТЬ
	|		НастройкиФормированияПроводок.ПланСчетов КАК ПланСчетов,
	|		СчетаДоходов.Счет КАК Счет
	|	ИЗ
	|		втНастройкиФормированияПроводок КАК НастройкиФормированияПроводок
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НастройкиФормированияПроводокМеждународногоУчета.СчетаДоходов КАК СчетаДоходов
	|			ПО НастройкиФормированияПроводок.НастройкаФормированияПроводок = СчетаДоходов.Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НастройкиФормированияПроводок.ПланСчетов,
	|		СчетаРасходов.Счет
	|	ИЗ
	|		втНастройкиФормированияПроводок КАК НастройкиФормированияПроводок
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НастройкиФормированияПроводокМеждународногоУчета.СчетаРасходов КАК СчетаРасходов
	|			ПО НастройкиФормированияПроводок.НастройкаФормированияПроводок = СчетаРасходов.Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НастройкиФормированияПроводок.ПланСчетов,
	|		СчетаПрибылейУбытков.Счет
	|	ИЗ
	|		втНастройкиФормированияПроводок КАК НастройкиФормированияПроводок
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НастройкиФормированияПроводокМеждународногоУчета.СчетаПрибылейУбытков КАК СчетаПрибылейУбытков
	|			ПО НастройкиФормированияПроводок.НастройкаФормированияПроводок = СчетаПрибылейУбытков.Ссылка) КАК Т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МеждународныйОстатки.Счет КАК Счет,
	|	МеждународныйОстатки.ПланСчетов КАК ПланСчетов,
	|	МеждународныйОстатки.Организация КАК Организация,
	|	МеждународныйОстатки.Подразделение КАК Подразделение,
	|	МеждународныйОстатки.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	МеждународныйОстатки.Валюта КАК Валюта,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто1.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто1
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто1,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто2.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто2
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто2,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто3.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто3
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто3,
	|	СУММА(МеждународныйОстатки.СуммаОстаток) КАК СуммаОстаток,
	|	СУММА(МеждународныйОстатки.СуммаРазвернутыйОстатокДт) КАК СуммаОстатокДт,
	|	СУММА(МеждународныйОстатки.СуммаРазвернутыйОстатокКт) КАК СуммаОстатокКт,
	|	СУММА(МеждународныйОстатки.ВалютнаяСуммаОстаток) КАК ВалютнаяСуммаОстаток
	|
	|ИЗ
	|	РегистрБухгалтерии.Международный.Остатки(
	|			&Граница,
	|			Счет В
	|				(ВЫБРАТЬ Т.Счет ИЗ втСчетаЗакрытия КАК Т),
	|			,
	|			ПланСчетов = &ПланСчетов И Организация В (&Организации)) КАК МеждународныйОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто1
	|		ПО МеждународныйОстатки.Счет = МеждународныйВидыСубконто1.Ссылка
	|			И (МеждународныйВидыСубконто1.НомерСтроки = 1)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто2
	|		ПО МеждународныйОстатки.Счет = МеждународныйВидыСубконто2.Ссылка
	|			И (МеждународныйВидыСубконто2.НомерСтроки = 2)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто3
	|		ПО МеждународныйОстатки.Счет = МеждународныйВидыСубконто3.Ссылка
	|			И (МеждународныйВидыСубконто3.НомерСтроки = 3)
	|
	|СГРУППИРОВАТЬ ПО
	|	МеждународныйОстатки.Счет,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто1.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто1
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто2.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто2
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто3.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто3
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	МеждународныйОстатки.ПланСчетов,
	|	МеждународныйОстатки.Организация,
	|	МеждународныйОстатки.Подразделение,
	|	МеждународныйОстатки.НаправлениеДеятельности,
	|	МеждународныйОстатки.Валюта
	|	
	|ИМЕЮЩИЕ
	|	СУММА(МеждународныйОстатки.СуммаОстаток) <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОстаткиСчетов.Счет КАК Счет,
	|	ОстаткиСчетов.ПланСчетов КАК ПланСчетов,
	|	ОстаткиСчетов.Организация КАК Организация,
	|	ОстаткиСчетов.Подразделение КАК Подразделение,
	|	ОстаткиСчетов.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ОстаткиСчетов.Валюта КАК Валюта,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто1.Валютный
	|			ТОГДА ОстаткиСчетов.Субконто1
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто1,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто2.Валютный
	|			ТОГДА ОстаткиСчетов.Субконто2
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто2,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто3.Валютный
	|			ТОГДА ОстаткиСчетов.Субконто3
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто3,
	|	СУММА(ОстаткиСчетов.СуммаОстаток) КАК СуммаОстаток,
	|	СУММА(ОстаткиСчетов.СуммаРазвернутыйОстатокДт) КАК СуммаОстатокДт,
	|	СУММА(ОстаткиСчетов.СуммаРазвернутыйОстатокКт) КАК СуммаОстатокКт,
	|	СУММА(ОстаткиСчетов.ВалютнаяСуммаОстаток) КАК ВалютнаяСуммаОстаток
	|
	|ИЗ
	|	РегистрБухгалтерии.МеждународныйБезКорреспонденции.Остатки(
	|			&Граница,
	|			Счет В
	|				(ВЫБРАТЬ Т.Счет ИЗ втСчетаЗакрытия КАК Т),
	|			,
	|			ПланСчетов = &ПланСчетов И Организация В (&Организации)) КАК ОстаткиСчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто1
	|		ПО ОстаткиСчетов.Счет = МеждународныйВидыСубконто1.Ссылка
	|			И (МеждународныйВидыСубконто1.НомерСтроки = 1)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто2
	|		ПО ОстаткиСчетов.Счет = МеждународныйВидыСубконто2.Ссылка
	|			И (МеждународныйВидыСубконто2.НомерСтроки = 2)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто3
	|		ПО ОстаткиСчетов.Счет = МеждународныйВидыСубконто3.Ссылка
	|			И (МеждународныйВидыСубконто3.НомерСтроки = 3)
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиСчетов.Счет,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто1.Валютный
	|			ТОГДА ОстаткиСчетов.Субконто1
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто2.Валютный
	|			ТОГДА ОстаткиСчетов.Субконто2
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто3.Валютный
	|			ТОГДА ОстаткиСчетов.Субконто3
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ОстаткиСчетов.ПланСчетов,
	|	ОстаткиСчетов.Организация,
	|	ОстаткиСчетов.Подразделение,
	|	ОстаткиСчетов.НаправлениеДеятельности,
	|	ОстаткиСчетов.Валюта
	|	
	|ИМЕЮЩИЕ
	|	СУММА(ОстаткиСчетов.СуммаОстаток) <> 0";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("НаДату", НаДату);
	Запрос.УстановитьПараметр("Граница", Новый Граница(НаДату, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ПланСчетов", ПланСчетов);
	Запрос.УстановитьПараметр("Организации", Организации);
	Запрос.УстановитьПараметр("ПериодичностьЗакрытияСчетов", ПериодичностьЗакрытияСчетов);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат Выборка.Количество();
	
КонецФункции

// Определяет наличие долгосрочных активов и обязательств, подлежащих реклассификации в краткосрочные
// по переданным организациям в конце переданного периода.
// 
// Параметры:
//  Организации - Массив Из СправочникСсылка.Организации -
//              - СправочникСсылка.Организации - Организации, по которым выполняется проверка.
//  Период - Дата - Дата, определяющая месячный интервал, в котором выполняется проверка.
// 
// Возвращаемое значение:
// 	Булево - наличие реклассифицируемых долгосрочных активов и обязательств
//
Функция ЕстьРеклассифицируемыеДолгосрочныеАктивыОбязательства(Организации, Период) Экспорт
	
	ДанныеРеклассификации = МеждународныйУчетПоДаннымФинансовыхРегистров.ДанныеРеклассификацииДолгосрочныхАктивовОбязательств(
		Организации, Период);
	
	Возврат ДанныеРеклассификации.Таблицы.Количество() > 0;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	Поля.Добавить("Ссылка");
	Поля.Добавить("Номер");
	Поля.Добавить("Дата");
	Поля.Добавить("ТипОперации");
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	ТипыОпераций = Перечисления.ТипыРегламентныхОперацийМеждународныйУчет;
	Если Данные.ТипОперации = ТипыОпераций.ЗакрытиеСчетовДоходовРасходов Тогда
		СтандартнаяОбработка = Ложь;
		Представление = СтрШаблон(
							МеждународнаяОтчетностьВызовСервера.ПредставлениеЗакрытияДоходовРасходов(),
							Данные.Номер,
							Данные.Дата);
							
	ИначеЕсли Данные.ТипОперации = ТипыОпераций.РасчетКурсовыхРазницФункциональнаяВалюта Тогда
		СтандартнаяОбработка = Ложь;
		Представление = СтрШаблон(
							МеждународнаяОтчетностьВызовСервера.ПредставлениеРасчетКурсовыхРазницФункциональнойВалюте(),
							Данные.Номер,
							Данные.Дата);
							
	ИначеЕсли Данные.ТипОперации = ТипыОпераций.РасчетКурсовыхРазницФункциональнаяВалюта Тогда
		СтандартнаяОбработка = Ложь;
		Представление = СтрШаблон(
							МеждународнаяОтчетностьВызовСервера.ПредставлениеРасчетКурсовыхРазницВалютеПредставления(),
							Данные.Номер,
							Данные.Дата);
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Комментарий КАК Комментарий,
	|	ДанныеДокумента.Проведен КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления КАК ПометкаУдаления,
	|	ДанныеДокумента.Ответственный КАК Ответственный
	|ИЗ
	|	Документ.РегламентнаяОперацияМеждународныйУчет КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|";
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Период", Реквизиты.Период);
	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	Запрос.УстановитьПараметр("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.РегламентнаяОперацияМеждународныйУчет"));
	Запрос.УстановитьПараметр("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.РегламентнаяОперацияМеждународныйУчет);
	Запрос.УстановитьПараметр("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	Запрос.УстановитьПараметр("Номер", Реквизиты.Номер);
	Запрос.УстановитьПараметр("Комментарий", Реквизиты.Комментарий);
	Запрос.УстановитьПараметр("Проведен", Реквизиты.Проведен);
	Запрос.УстановитьПараметр("ПометкаУдаления", Реквизиты.ПометкаУдаления);
	Запрос.УстановитьПараметр("Ответственный", Реквизиты.Ответственный);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&ИдентификаторМетаданных КАК ТипСсылки,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	&Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК Партнер,
	|	НЕОПРЕДЕЛЕНО КАК МестоХранения,
	|	НЕОПРЕДЕЛЕНО КАК Контрагент,
	|	НЕОПРЕДЕЛЕНО КАК Подразделение,
	|	&Период КАК ДатаДокументаИБ,
	|	&Ссылка КАК Ссылка,
	|	&Номер КАК НомерДокументаИБ,
	|	НЕОПРЕДЕЛЕНО КАК Статус,
	|	&Ответственный КАК Ответственный,
	|	НЕОПРЕДЕЛЕНО КАК Автор,
	|	ЛОЖЬ КАК ДополнительнаяЗапись,
	|	НЕОПРЕДЕЛЕНО КАК Дополнительно,
	|	&Комментарий КАК Комментарий,
	|	&Проведен КАК Проведен,
	|	&ПометкаУдаления КАК ПометкаУдаления,
	|	&Период КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать КАК НомерПервичногоДокумента,
	|	0 КАК Сумма,
	|	НЕОПРЕДЕЛЕНО КАК Валюта,
	|	НЕОПРЕДЕЛЕНО КАК Договор,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
	|	ЛОЖЬ КАК СторноИсправление,
	|	НЕОПРЕДЕЛЕНО КАК СторнируемыйДокумент,
	|	НЕОПРЕДЕЛЕНО КАК ИсправляемыйДокумент,
	|	&Период КАК ДатаОтраженияВУчете,
	|	НЕОПРЕДЕЛЕНО КАК Приоритет";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.РегламентнаяОперацияМеждународныйУчет";
	ЗначенияПараметров = Новый Структура;
	ЗначенияПараметров.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.РегламентнаяОперацияМеждународныйУчет"));
	ЗначенияПараметров.Вставить("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.РегламентнаяОперацияМеждународныйУчет);
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "";
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';
								|en = 'In document %ПолноеИмяДокумента%, adaptation of request for generating records of register %ИмяРегистра% is not implemented.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
			ТекстЗапроса, ПолноеИмяДокумента, СинонимТаблицыДокумента, Ложь);
	Иначе
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
			ТекстЗапроса, ПолноеИмяДокумента, СинонимТаблицыДокумента);
	КонецЕсли;
	
	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

Функция ВсегоОсталосьКурсовыхРазниц(ТекстЗапроса, ПланСчетов, Организации, Знач НаДату, ТолькоПервую)
	
	Если ЗначениеЗаполнено(НаДату) Тогда
		НаДату = КонецДня(НаДату);
	Иначе
		НаДату = КонецДня(ТекущаяДатаСеанса());
	КонецЕсли;
	
	Если НЕ ТолькоПервую Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, " ПЕРВЫЕ 1", "");
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("НаДату", НаДату);
	Запрос.УстановитьПараметр("ПланСчетов", ПланСчетов);
	Запрос.УстановитьПараметр("Организации", Организации);
	Запрос.УстановитьПараметр("Граница", Новый Граница(НаДату, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ВалютаУправленческогоУчета", Константы.ВалютаУправленческогоУчета.Получить());
	
	Возврат Запрос.Выполнить().Выбрать().Количество();
	
КонецФункции

#КонецОбласти

#КонецЕсли
