#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Ответственный = Пользователи.ТекущийПользователь();
	
	Организация = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ТекущаяОрганизация", "");
	Организация = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	ПланСчетов = Справочники.ПланыСчетовМеждународногоУчета.ПланСчетовПоУмолчанию();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ТипыОднократныхОпераций = Новый Массив();
	ТипыОднократныхОпераций.Добавить(Перечисления.ТипыРегламентныхОперацийМеждународныйУчет.ЗакрытиеСчетовДоходовРасходов);
	ТипыОднократныхОпераций.Добавить(Перечисления.ТипыРегламентныхОперацийМеждународныйУчет.РеклассификацияДолгосрочныхАктивовОбязательств);
	
	Если ТипыОднократныхОпераций.Найти(ТипОперации) <> Неопределено Тогда
		ПроверитьДублиДокументовТекущегоПериода(Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ПриЗаписиДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	НастройкаФормированияПроводокЗаполнена();
	
	ПроведениеДокументов.ОбработкаПроведенияДокумента(ЭтотОбъект, Отказ);
	
	ТипыОпераций = Перечисления.ТипыРегламентныхОперацийМеждународныйУчет;
	
	Если ТипОперации = ТипыОпераций.РеклассификацияДолгосрочныхАктивовОбязательств Тогда
		РеклассификацияДолгосрочныхАктивовОбязательств(Отказ);
		
	ИначеЕсли ТипОперации = ТипыОпераций.ЗакрытиеСчетовДоходовРасходов Тогда
		ЗакрытиеСчетовУчетаДоходовРасходов(Отказ);
		
	Иначе
		РасчетКурсовыхРазниц();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеДокументов.ОбработкаУдаленияПроведенияДокумента(ЭтотОбъект, Отказ);
	
	Если НЕ Отказ
	   И НЕ ДополнительныеСвойства.Свойство("ВыполнениеЭтапаЗакрытияМесяца") Тогда
		СформироватьЗаданияКЗакрытиюМесяцаПриОтменеПроведения();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура НастройкаФормированияПроводокЗаполнена()
	
	Отбор = Новый Структура("ПланСчетов, Организация", ПланСчетов, Организация);
	НастройкиФормированияПроводок = РегистрыСведений.ПланыСчетовМеждународногоУчетаОрганизаций.СрезПоследних(КонецДня(Дата),Отбор);
	Если НастройкиФормированияПроводок.Количество() Тогда
		НастройкаФормированияПроводокОрганизации = НастройкиФормированияПроводок[0].НастройкаФормированияПроводок;
	Иначе
		Текст = СтрШаблон(
					НСтр("ru = 'Для организации ""%1"" не задана настройка формирования проводок по плану счетов ""%2""';
						|en = 'The posting schema according to chart of accounts ""%2"" is not specified for company ""%1""'"),
					Организация,
					ПланСчетов);
		ВызватьИсключение Текст;
	КонецЕсли;
	
	Если ТипОперации = Перечисления.ТипыРегламентныхОперацийМеждународныйУчет.ЗакрытиеСчетовДоходовРасходов Тогда
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НастройкаФормированияПроводокОрганизации,
			"ЗакрытиеСчетовДоходовИРасходов,СчетУчетаДоходов,СчетУчетаРасходов,СчетУчетаНераспределеннойПрибылиУбытка,СчетаДоходов,СчетаРасходов,СчетаПрибылейУбытков");
		
		Если Реквизиты.ЗакрытиеСчетовДоходовИРасходов = Перечисления.ЗакрытиеСчетовДоходовИРасходов.ПустаяСсылка() Тогда
			Текст = СтрШаблон(
				НСтр("ru = 'Для организации ""%1"" в настройке формирования проводок ""%2"" по плану счетов ""%3"" не задана периодичность закрытия счетов доходов и расходов';
					|en = 'The frequency of closing income and expense accounts is not set for the ""%1"" company in the ""%2"" posting schema for the ""%3"" chart of accounts'"),
				Организация,
				НастройкаФормированияПроводокОрганизации,
				ПланСчетов);
			ВызватьИсключение Текст;
		КонецЕсли;
		
		Если НЕ (ЗначениеЗаполнено(Реквизиты.СчетУчетаДоходов) И ЗначениеЗаполнено(Реквизиты.СчетаДоходов)
			ИЛИ ЗначениеЗаполнено(Реквизиты.СчетУчетаРасходов) И ЗначениеЗаполнено(Реквизиты.СчетаРасходов)
			ИЛИ ЗначениеЗаполнено(Реквизиты.СчетУчетаНераспределеннойПрибылиУбытка) И ЗначениеЗаполнено(Реквизиты.СчетаПрибылейУбытков)) Тогда
			
			Текст = СтрШаблон(
						НСтр("ru = 'Для организации ""%1"" в настройке формирования проводок ""%2"" по плану счетов ""%3"" не заданы счета закрытия периода';
							|en = 'Period-end closing accounts are not set for the company ""%1"" in posting schema ""%2"" for the chart of accounts ""%3""'"),
						Организация,
						НастройкаФормированияПроводокОрганизации,
						ПланСчетов);
						
			ДополнительныеСвойства.Вставить("НастройкаФормированияПроводокСОшибкой", НастройкаФормированияПроводокОрганизации);
			
			ВызватьИсключение Текст;
			
		КонецЕсли;
		
	ИначеЕсли ТипОперации = Перечисления.ТипыРегламентныхОперацийМеждународныйУчет.РасчетКурсовыхРазницВалютаПредставления
		ИЛИ ТипОперации = Перечисления.ТипыРегламентныхОперацийМеждународныйУчет.РасчетКурсовыхРазницФункциональнаяВалюта Тогда
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НастройкаФормированияПроводокОрганизации,
			"ДоходыКР,РасходыКР,ДоходыОтРазницПриПересчетеВП,РасходыОтРазницПриПересчетеВП");
		
		Если НЕ ЗначениеЗаполнено(Реквизиты.ДоходыКР) 
				ИЛИ НЕ ЗначениеЗаполнено(Реквизиты.РасходыКР)
				ИЛИ НЕ ЗначениеЗаполнено(Реквизиты.ДоходыОтРазницПриПересчетеВП) 
				ИЛИ НЕ ЗначениеЗаполнено(Реквизиты.РасходыОтРазницПриПересчетеВП) Тогда
			
			Текст = СтрШаблон(
						НСтр("ru = 'Для организации ""%1"" в настройке формирования проводок ""%2"" по плану счетов ""%3"" не заданы счета учета курсовых разниц';
							|en = 'Ledger accounts of exchange rate differences are not set for the company ""%1"" in posting schema ""%2"" for the chart of accounts ""%3""'"),
						Организация,
						НастройкаФормированияПроводокОрганизации,
						ПланСчетов);
			
			ДополнительныеСвойства.Вставить("НастройкаФормированияПроводокСОшибкой", НастройкаФормированияПроводокОрганизации);
			ВызватьИсключение Текст;
			
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьДублиДокументовТекущегоПериода(Отказ)
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Т.Ссылка
		|ИЗ
		|	Документ.РегламентнаяОперацияМеждународныйУчет КАК Т
		|ГДЕ
		|	Т.Проведен
		|	И Т.ПланСчетов = &ПланСчетов
		|	И Т.Организация = &Организация
		|	И Т.Дата МЕЖДУ &ПериодНачало И &ПериодОкончание
		|	И НЕ Т.Ссылка = &ТекущийДокумент
		|	И Т.ТипОперации = &ТипОперации");
	
	Запрос.УстановитьПараметр("ПланСчетов", ПланСчетов);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ТекущийДокумент", Ссылка);
	Запрос.УстановитьПараметр("ПериодНачало", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("ПериодОкончание", КонецМесяца(Дата));
	Запрос.УстановитьПараметр("ТипОперации", ТипОперации);
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'В указанном периоде уже существует аналогичный документ. Операция не выполнена.';
				|en = 'There is a similar document in the specified period. Operation is not executed.'"),,,,
			Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗакрытиеСчетовУчетаДоходовРасходов(Отказ)
	
	НаДату = КонецМесяца(Дата);
	
	ПериодичностьЗакрытияСчетов = Новый Массив();
	ПериодичностьЗакрытияСчетов.Добавить(Перечисления.ЗакрытиеСчетовДоходовИРасходов.Ежемесячно);
	
	Если НаДату = КонецГода(НаДату) Тогда
		ПериодичностьЗакрытияСчетов.Добавить(Перечисления.ЗакрытиеСчетовДоходовИРасходов.ВКонцеГода);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ПланСчетов", ПланСчетов);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Дата", НаДату);
	Запрос.УстановитьПараметр("Граница", Новый Граница(НаДату, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ПериодичностьЗакрытияСчетов", ПериодичностьЗакрытияСчетов);
	
	Замещать = Истина;
	
	Запрос.Текст = ТекстЗапросаЗакрытияСчетовУчетаДоходовРасходов("СчетУчетаДоходов", "СчетаДоходов", НСтр("ru = 'Закрытие доходов';
																											|en = 'Close income'"));
	ЗаписатьПроводкиПоЗакрытиюСчетовУчетаДоходовРасходов(Запрос, Отказ, Замещать);
	
	Запрос.Текст = ТекстЗапросаЗакрытияСчетовУчетаДоходовРасходов("СчетУчетаРасходов", "СчетаРасходов", НСтр("ru = 'Закрытие расходов';
																											|en = 'Close expenses'"));
	ЗаписатьПроводкиПоЗакрытиюСчетовУчетаДоходовРасходов(Запрос, Отказ, Замещать);
	
	Запрос.Текст = ТекстЗапросаЗакрытияСчетовУчетаДоходовРасходов("СчетУчетаНераспределеннойПрибылиУбытка", "СчетаПрибылейУбытков", НСтр("ru = 'Нераспредленная прибыль\убыток';
																																		|en = 'Retained profit\loss'"));
	ЗаписатьПроводкиПоЗакрытиюСчетовУчетаДоходовРасходов(Запрос, Отказ, Замещать);
	
	Движения.Международный.Записывать = Ложь;
	Движения.МеждународныйБезКорреспонденции.Записывать = Ложь;
	
КонецПроцедуры

Процедура РасчетКурсовыхРазниц()
	
	ВалютаМеждународногоУчета = МеждународныйУчетОбщегоНазначения.УчетныеВалюты(ПланСчетов, Организация);
	ВалютаРегламентированногоУчета = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Организация);
	ФункциональнаяВалюта = РаботаСКурсамиВалютУТ.ПолучитьКурсВалюты(ВалютаМеждународногоУчета.Функциональная, Дата, ВалютаРегламентированногоУчета);
	ВалютаПредставления = РаботаСКурсамиВалютУТ.ПолучитьКурсВалюты(ВалютаМеждународногоУчета.Представления, Дата, ВалютаРегламентированногоУчета);
	
	НаДату = КонецДня(Дата);
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПланСчетов", ПланСчетов);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", ВалютаРегламентированногоУчета);
	Запрос.УстановитьПараметр("Дата", НаДату);
	Запрос.УстановитьПараметр("Граница", Новый Граница(НаДату, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("КурсЧислительФВ", ФункциональнаяВалюта.КурсЧислитель);
	Запрос.УстановитьПараметр("КурсЗнаменательФВ", ФункциональнаяВалюта.КурсЗнаменатель);
	Запрос.УстановитьПараметр("КурсЧислительВП", ВалютаПредставления.КурсЧислитель);
	Запрос.УстановитьПараметр("КурсЗнаменательВП", ВалютаПредставления.КурсЗнаменатель);
	Запрос.УстановитьПараметр("ВалютаПредставления", ВалютаМеждународногоУчета.Представления);
	Запрос.УстановитьПараметр("ФункциональнаяВалюта", ВалютаМеждународногоУчета.Функциональная);
	
	ТипыОпераций = Перечисления.ТипыРегламентныхОперацийМеждународныйУчет;
	КоличествоВПорции = 1000;
	Если ТипОперации = ТипыОпераций.РасчетКурсовыхРазницВалютаПредставления Тогда
		Запрос.Текст = ТекстЗапросаКурсовыхРазницВалютаПредставления();
		ВсегоПорций = 
			Документы.РегламентнаяОперацияМеждународныйУчет.НеПересчитаноВВалютуПредставления(ПланСчетов, Организация, Дата)
			/ КоличествоВПорции;
	Иначе
		Запрос.Текст = ТекстЗапросаКурсовыхРазницФункциональнаяВалюта();
		ВсегоПорций = 
			Документы.РегламентнаяОперацияМеждународныйУчет.НезакрытыеКурсовыеРазницы(ПланСчетов, Организация, Дата)
			/ КоличествоВПорции;
	КонецЕсли;
	
	Замещать = Истина;
	Для НомерПорции = 0 По ВсегоПорций Цикл
		
		РезультатЗапроса = Запрос.Выполнить();
		Если РезультатЗапроса.Пустой() Тогда
			Прервать;
		КонецЕсли;

		// Обход порции результата запроса
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.ВариантФормированияПроводок = Перечисления.ВариантыФормированияПроводок.СКорреспонденцией Тогда
				Проводки = СформироватьПроводкиСКорреспонденциейПоКурсовойРазнице(Выборка);
			Иначе
				Проводки = СформироватьПроводкиБезКорреспонденцииПоКурсовойРазнице(Выборка);
			КонецЕсли;
			
			Для каждого Проводка Из Проводки Цикл
				Если ТипОперации = ТипыОпераций.РасчетКурсовыхРазницВалютаПредставления Тогда
					Проводка.Содержание = НСтр("ru = 'Корректировка курса валюты представления';
												|en = 'Adjustment of presentation currency exchange rate'");
					Проводка.Сумма = 0;
				Иначе
					Проводка.Содержание = НСтр("ru = 'Курсовая разница';
												|en = 'Exchange difference'");
					Если Выборка.Валюта = ВалютаМеждународногоУчета.Представления Тогда
						Проводка.СуммаПредставления = 0;
					Иначе
						Проводка.СуммаПредставления = РаботаСКурсамиВалютУТКлиентСервер.ПересчитатьПоКурсу(Проводка.Сумма,ФункциональнаяВалюта,ВалютаПредставления);
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
		КонецЦикла;// обработки порции
		
		Движения.Международный.Записать(Замещать);
		Если Замещать Тогда
			Движения.Международный.Очистить();
		КонецЕсли;
		
		Движения.МеждународныйБезКорреспонденции.Записать(Замещать);
		Если Замещать Тогда
			Движения.МеждународныйБезКорреспонденции.Очистить();
		КонецЕсли;
		Замещать = Ложь;
		
	КонецЦикла;// по всем порциям
	
	Движения.Международный.Записывать = Ложь;
	Движения.МеждународныйБезКорреспонденции.Записывать = Ложь;
	
КонецПроцедуры

Процедура РеклассификацияДолгосрочныхАктивовОбязательств(Отказ)
	
	ДанныеОперации = Новый Структура("Дата,Ссылка,Организация,ПланСчетов");
	ЗаполнитьЗначенияСвойств(ДанныеОперации, ЭтотОбъект);
	
	ТаблицаПроводок = МеждународныйУчетПроведениеСервер.ПроводкиРегламентнойОперации(ДанныеОперации);
	
	НаборЗаписей = Движения.Международный;
	НаборЗаписей.Очистить();
	
	СообщенияОбОшибках = Новый Массив();
	
	Для Каждого ДанныеПроводки Из ТаблицаПроводок Цикл
		Если ДанныеПроводки.Статус = Перечисления.СтатусыОтраженияВМеждународномУчете.ОтраженоВУчете
		 ИЛИ ДанныеПроводки.Статус = Перечисления.СтатусыОтраженияВМеждународномУчете.ОтраженоВУчетеВручную Тогда
			НоваяПроводка = НаборЗаписей.Добавить();
			НоваяПроводка.Активность = Истина;
			ЗаполнитьЗначенияСвойств(НоваяПроводка, ДанныеПроводки);
			НаборЗаписей.УстановитьСубконто(НоваяПроводка, ДанныеПроводки, "Дт");
			НаборЗаписей.УстановитьСубконто(НоваяПроводка, ДанныеПроводки, "Кт");
		Иначе
			СообщенияОбОшибках.Добавить(ДанныеПроводки.Комментарий);
		КонецЕсли;
	КонецЦикла;
	
	Если СообщенияОбОшибках.Количество() > 0 Тогда
		ВызватьИсключение СтрСоединить(СообщенияОбОшибках, Символы.ПС);
	КонецЕсли;
	
	НаборЗаписей.Записывать = Истина;
	
КонецПроцедуры

// Параметры:
// 	Выборка - ВыборкаИзРезультатаЗапроса - Описание:
// 	 *ВидыСубконтоСчета - ТаблицаЗначений - :
// 	  **НомерСтроки - Число - 
// Возвращаемое значение:
// 	Массив Из РегистрБухгалтерииЗапись.Международный - 
Функция СформироватьПроводкиСКорреспонденциейПоКурсовойРазнице(Выборка)
	
	МассивПроводок = Новый Массив;
	
	Проводка = Движения.Международный.Добавить();
	Проводка.Активность = Истина;
	Проводка.Период = Выборка.Период;
	Проводка.Организация = Выборка.Организация;
	Проводка.ПланСчетов = Выборка.ПланСчетов;

	Если Выборка.КурсоваяРазница > 0 Тогда// отразим прибыль
		Счет = "Дт";
		Кор = "Кт";
		КорСчет = Выборка.Доходы;
		КорСубконто = Выборка.СтатьяДоходов;
		КорВидСубконто = Выборка.ВидСубконтоСтатьиДоходов;
		КорВалютный = Выборка.ДоходыВалютный;
		Если Выборка.ДоходыПоПодразделениям Тогда
			Подразделение = Выборка.Подразделение;
		КонецЕсли;
		Если Выборка.ДоходыПоНаправлениям Тогда
			НаправлениеДеятельности = Выборка.НаправлениеДеятельности;
		КонецЕсли;
	Иначе// отразим убытки
		Счет = "Кт";
		Кор = "Дт";
		КорСчет = Выборка.Расходы;
		КорСубконто = Выборка.СтатьяРасходов;
		КорВидСубконто = Выборка.ВидСубконтоСтатьиРасходов;
		КорВалютный = Выборка.РасходыВалютный;
		Если Выборка.РасходыПоПодразделениям Тогда
			Подразделение = Выборка.Подразделение;
		КонецЕсли;
		Если Выборка.РасходыПоНаправлениям Тогда
			НаправлениеДеятельности = Выборка.НаправлениеДеятельности;
		КонецЕсли;
		
	КонецЕсли;
	
	// заполним счет и аналитику возникновения курсовой разницы
	Проводка["Счет"+Счет] = Выборка.Счет;
	Проводка["Валюта"+Счет] = Выборка.Валюта;
	Проводка["Подразделение"+Счет] = Выборка.Подразделение;
	Проводка["НаправлениеДеятельности"+Счет] = Выборка.НаправлениеДеятельности;
	ВидыСубконтоСчета = Выборка.ВидыСубконтоСчета.Выгрузить(); // ТабличнаяЧасть - 
	Для Каждого СтрокаТаблицы Из ВидыСубконтоСчета Цикл
		Если Счет = "Дт" Тогда
			Проводка.СубконтоДт[СтрокаТаблицы.ВидСубконто] = ЗначениеСубконтоСчетаКурсовойРазницы(СтрокаТаблицы, Выборка);
		Иначе
			Проводка.СубконтоКт[СтрокаТаблицы.ВидСубконто] = ЗначениеСубконтоСчетаКурсовойРазницы(СтрокаТаблицы, Выборка);
		КонецЕсли;
	КонецЦикла;
	
	// заполним счет учета прибыли\убытка от курсовой разницы
	Если НЕ Выборка.Забалансовый Тогда
		Проводка["Счет" + Кор] = КорСчет;
		Если КорВалютный Тогда
			Проводка["Валюта"+Кор] = Выборка.Валюта;
		КонецЕсли;
		Проводка["Подразделение"+Кор] = Подразделение;
		Проводка["НаправлениеДеятельности"+Кор] = НаправлениеДеятельности;
		Если Кор = "Дт" Тогда
			Проводка.СубконтоДт[КорВидСубконто] = КорСубконто;
		Иначе
			Проводка.СубконтоКт[КорВидСубконто] = КорСубконто;
		КонецЕсли;
	КонецЕсли;
	
	Проводка.Сумма = Макс(Выборка.КурсоваяРазница, -Выборка.КурсоваяРазница);
	Проводка.СуммаПредставления = Проводка.Сумма;
	
	МассивПроводок.Добавить(Проводка);
	
	Возврат МассивПроводок;
	
КонецФункции

// Параметры:
// 	Выборка - ВыборкаИзРезультатаЗапроса - Описание:
// 	 *ВидыСубконтоСчета - ТаблицаЗначений - :
// 	  **НомерСтроки - Число - 
// Возвращаемое значение:
// 	Массив Из РегистрБухгалтерииЗапись.МеждународныйБезКорреспонденции - 
Функция СформироватьПроводкиБезКорреспонденцииПоКурсовойРазнице(Выборка)
	
	МассивПроводок = Новый Массив;
	
	// Добавим проводку по счету курсовой разницы
	Проводка = Движения.МеждународныйБезКорреспонденции.Добавить();
	ЗаполнитьЗначенияСвойств(Проводка, Выборка);
	Проводка.Активность = Истина;
	Если Выборка.КурсоваяРазница > 0 Тогда
		Проводка.ВидДвижения = ВидДвиженияБухгалтерии.Дебет;
	Иначе
		Проводка.ВидДвижения = ВидДвиженияБухгалтерии.Кредит;
	КонецЕсли;
	ВидыСубконтоСчета = Выборка.ВидыСубконтоСчета.Выгрузить(); // ТабличнаяЧасть - 
	Для Каждого СтрокаТаблицы Из ВидыСубконтоСчета Цикл
		Проводка.Субконто[СтрокаТаблицы.ВидСубконто] = ЗначениеСубконтоСчетаКурсовойРазницы(СтрокаТаблицы, Выборка);
	КонецЦикла;
	Проводка.Сумма = Макс(Выборка.КурсоваяРазница, -Выборка.КурсоваяРазница);
	Проводка.СуммаПредставления = Проводка.Сумма;
	МассивПроводок.Добавить(Проводка);	
	
	
	// Добавим проводку по доходам/расходам
	Если НЕ Выборка.Забалансовый Тогда
		Проводка = Движения.МеждународныйБезКорреспонденции.Добавить();
		ЗаполнитьЗначенияСвойств(Проводка, Выборка, , "Счет");
		Если Выборка.КурсоваяРазница > 0 Тогда 
			Проводка.ВидДвижения = ВидДвиженияБухгалтерии.Кредит;
			Проводка.Счет = Выборка.Доходы;
			Проводка.Субконто[Выборка.ВидСубконтоСтатьиДоходов] = Выборка.СтатьяДоходов;
			Если Не Выборка.ДоходыВалютный Тогда
				Проводка.Валюта = Неопределено;
				Проводка.ВалютнаяСумма = 0;
			КонецЕсли;
		Иначе
			Проводка.ВидДвижения = ВидДвиженияБухгалтерии.Дебет;
			Проводка.Счет = Выборка.Расходы;
			Проводка.Субконто[Выборка.ВидСубконтоСтатьиРасходов] = Выборка.СтатьяРасходов;
			Если Не Выборка.РасходыВалютный Тогда
				Проводка.Валюта = Неопределено;
				Проводка.ВалютнаяСумма = 0;
			КонецЕсли;
		КонецЕсли;
		Проводка.Сумма = Макс(Выборка.КурсоваяРазница, -Выборка.КурсоваяРазница);
		Проводка.СуммаПредставления = Проводка.Сумма;
		МассивПроводок.Добавить(Проводка);
	КонецЕсли;
	
	Возврат МассивПроводок;
	
КонецФункции

Функция ЗначениеСубконтоСчетаКурсовойРазницы(СтрокаВидыСубконто, Выборка)
	
	ЗначениеСубконто = Неопределено;
	ЭтоПрибыль = Выборка.КурсоваяРазница > 0;
	
	Если НЕ СтрокаВидыСубконто.ТолькоОбороты Тогда
		ЗначениеСубконто = Выборка["Субконто" + СтрокаВидыСубконто.НомерСтроки];
	ИначеЕсли СтрокаВидыСубконто.ВидСубконто = ПланыВидовХарактеристик.ВидыСубконтоМеждународные.СтатьиДвиженияДенежныхСредств Тогда
		Если ЭтоПрибыль Тогда
			ЗначениеСубконто = Справочники.СтатьиДвиженияДенежныхСредств.КурсовыеРазницыПрибыль;
		Иначе
			ЗначениеСубконто = Справочники.СтатьиДвиженияДенежныхСредств.КурсовыеРазницыУбыток;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЗначениеСубконто;
	
КонецФункции

Процедура ЗаписатьПроводкиПоЗакрытиюСчетовУчетаДоходовРасходов(Запрос, Отказ, Замещать)
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Если РезультатЗапроса[4].Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ВидыСубконто = РезультатЗапроса[3].Выгрузить(); // ТаблицаЗначений - 
	ВидыСубконто.Индексы.Добавить("Счет");
	
	НаборЗаписейБезКорреспонденции = Движения.МеждународныйБезКорреспонденции;
	НаборЗаписейБезКорреспонденции.Очистить();
	
	НаборЗаписейСКорреспонденцией = Движения.Международный;
	НаборЗаписейСКорреспонденцией.Очистить();
	
	Выборка = РезультатЗапроса[4].Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.ВариантФормированияПроводок = Перечисления.ВариантыФормированияПроводок.БезКорреспонденции Тогда
			СформироватьПроводкиБезКорреспонденцииПоЗакрытиюСчетовУчетаДоходовРасходов(
				Выборка, ВидыСубконто, НаборЗаписейБезКорреспонденции);
		Иначе
			СформироватьПроводкуСКорреспонденциейПоЗакрытиюСчетовУчетаДоходовРасходов(
				Выборка, ВидыСубконто, НаборЗаписейСКорреспонденцией);
		КонецЕсли;
	КонецЦикла;
	
	НаборЗаписейБезКорреспонденции.Записать(Замещать);
	НаборЗаписейСКорреспонденцией.Записать(Замещать);
	
	Замещать = Ложь;
	
КонецПроцедуры

Процедура СформироватьПроводкуСКорреспонденциейПоЗакрытиюСчетовУчетаДоходовРасходов(Выборка, ВидыСубконто, НаборЗаписей)
	
	НоваяПроводка = НаборЗаписей.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяПроводка, Выборка);
	УстановитьСубконтоВПроводкеСКорреспонденцией(НоваяПроводка, Выборка, "Дт", ВидыСубконто);
	УстановитьСубконтоВПроводкеСКорреспонденцией(НоваяПроводка, Выборка, "Кт", ВидыСубконто);
	
КонецПроцедуры

Процедура СформироватьПроводкиБезКорреспонденцииПоЗакрытиюСчетовУчетаДоходовРасходов(Выборка, ВидыСубконто, НаборЗаписей)
	
	НоваяПроводка = НаборЗаписей.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяПроводка, Выборка, "Период, ПланСчетов, Организация, Сумма, СуммаПредставления, Содержание");
	НоваяПроводка.ВидДвижения = ВидДвиженияБухгалтерии.Дебет;
	НоваяПроводка.Подразделение = Выборка.ПодразделениеДт;
	НоваяПроводка.НаправлениеДеятельности = Выборка.НаправлениеДеятельностиДт;
	НоваяПроводка.Счет = Выборка.СчетДт;
	УстановитьСубконтоВПроводкеБезКорреспонденции(НоваяПроводка, Выборка, "Дт", ВидыСубконто);
	НоваяПроводка.Валюта = Выборка.ВалютаДт;
	НоваяПроводка.ВалютнаяСумма = Выборка.ВалютнаяСуммаДт;
	
	НоваяПроводка = НаборЗаписей.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяПроводка, Выборка, "Период, ПланСчетов, Организация, Сумма, СуммаПредставления, Содержание");
	НоваяПроводка.ВидДвижения = ВидДвиженияБухгалтерии.Кредит;
	НоваяПроводка.Подразделение = Выборка.ПодразделениеКт;
	НоваяПроводка.НаправлениеДеятельности = Выборка.НаправлениеДеятельностиКт;
	НоваяПроводка.Счет = Выборка.СчетКт;
	УстановитьСубконтоВПроводкеБезКорреспонденции(НоваяПроводка, Выборка, "Кт", ВидыСубконто);
	НоваяПроводка.Валюта = Выборка.ВалютаКт;
	НоваяПроводка.ВалютнаяСумма = Выборка.ВалютнаяСуммаКт;
	
КонецПроцедуры

// Заполняет субконто не по совпадению номеров субконто, а по совпадению типов
Процедура УстановитьСубконтоВПроводкеСКорреспонденцией(НоваяПроводка, Данные, ВидДвижения, ВидыСубконто)
	
	Отбор = Новый Структура("Счет", Данные["Счет" + ВидДвижения]);
	ВидыСубконтоСчета = ВидыСубконто.НайтиСтроки(Отбор);
	МаксКоличествоСубконто = МеждународныйУчетСерверПовтИсп.МаксКоличествоСубконто();
	ИмяСубконто = "Субконто" + ВидДвижения;
	Для Каждого ВидСубконто Из ВидыСубконтоСчета Цикл
		Для НомерСубконто = 1 По МаксКоличествоСубконто Цикл
			НовоеЗначениеСубконто = Данные[ИмяСубконто + НомерСубконто];
			ТипыСовпадают = Ложь;
			Для Каждого ТипВидаСубконто Из ВидСубконто.ТипЗначения.Типы() Цикл
				Если ТипВидаСубконто = ТипЗнч(НовоеЗначениеСубконто) Тогда
					ТипыСовпадают = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если ТипыСовпадают Тогда
				НоваяПроводка[ИмяСубконто][ВидСубконто.ВидСубконто] = НовоеЗначениеСубконто;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

// Заполняет субконто не по совпадению номеров субконто, а по совпадению типов
Процедура УстановитьСубконтоВПроводкеБезКорреспонденции(НоваяПроводка, Данные, ВидДвижения, ВидыСубконто)
	
	Отбор = Новый Структура("Счет", Данные["Счет" + ВидДвижения]);
	ВидыСубконтоСчета = ВидыСубконто.НайтиСтроки(Отбор);
	МаксКоличествоСубконто = МеждународныйУчетСерверПовтИсп.МаксКоличествоСубконто();
	ИмяСубконтоВДанных = "Субконто" + ВидДвижения;
	Для Каждого ВидСубконто Из ВидыСубконтоСчета Цикл
		Для НомерСубконто = 1 По МаксКоличествоСубконто Цикл
			НовоеЗначениеСубконто = Данные[ИмяСубконтоВДанных + НомерСубконто];
			ТипыСовпадают = Ложь;
			Для Каждого ТипВидаСубконто Из ВидСубконто.ТипЗначения.Типы() Цикл
				Если ТипВидаСубконто = ТипЗнч(НовоеЗначениеСубконто) Тогда
					ТипыСовпадают = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если ТипыСовпадают Тогда
				НоваяПроводка["Субконто"][ВидСубконто.ВидСубконто] = НовоеЗначениеСубконто;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Функция ТекстЗапросаЗакрытияСчетовУчетаДоходовРасходов(ИмяПоляСчета, ИмяТабЧастиСчетов, СодержаниеПроводки)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	НастройкаФормированияПроводокОрганизацийМУ.ПланСчетов,
	|	НастройкаФормированияПроводокОрганизацийМУ.Организация,
	|	НастройкаФормированияПроводокОрганизацийМУ.НастройкаФормированияПроводок,
	|	НастройкиФормированияПроводок.СчетУчетаДоходов КАК СчетУчета,
	|	НастройкиФормированияПроводок.СчетУчетаДоходов.Вид КАК ВидСчетаУчета,
	|	НастройкиФормированияПроводок.СчетУчетаДоходов.УчетПоПодразделениям КАК УчетПоПодразделениям,
	|	НастройкиФормированияПроводок.СчетУчетаДоходов.УчетПоНаправлениямДеятельности КАК УчетПоНаправлениям
	|ПОМЕСТИТЬ втСчетЗакрытия
	|ИЗ
	|	РегистрСведений.ПланыСчетовМеждународногоУчетаОрганизаций.СрезПоследних(&Дата, 
	|			ПланСчетов = &ПланСчетов И Организация = &Организация) КАК НастройкаФормированияПроводокОрганизацийМУ
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Справочник.НастройкиФормированияПроводокМеждународногоУчета КАК НастройкиФормированияПроводок
	|	ПО
	|		НастройкаФормированияПроводокОрганизацийМУ.НастройкаФормированияПроводок = НастройкиФормированияПроводок.Ссылка
	|ГДЕ
	|	НастройкиФормированияПроводок.ЗакрытиеСчетовДоходовИРасходов В (&ПериодичностьЗакрытияСчетов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втСчетЗакрытия.НастройкаФормированияПроводок,
	|	ЗакрываемыеСчета.Счет,
	|	ЗакрываемыеСчета.Счет.УчетПоПодразделениям КАК УчетПоПодразделениям,
	|	ЗакрываемыеСчета.Счет.УчетПоНаправлениямДеятельности КАК УчетПоНаправлениям
	|ПОМЕСТИТЬ втЗакрываемыеСчета
	|ИЗ
	|	втСчетЗакрытия КАК втСчетЗакрытия
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НастройкиФормированияПроводокМеждународногоУчета.СчетаДоходов КАК ЗакрываемыеСчета
	|		ПО втСчетЗакрытия.НастройкаФормированияПроводок = ЗакрываемыеСчета.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВариантыФормированияПроводок.СКорреспонденцией) КАК ВариантФормированияПроводок,
	|	МеждународныйОстатки.Счет,
	|	МеждународныйОстатки.ПланСчетов,
	|	МеждународныйОстатки.Организация,
	|	ВЫБОР КОГДА МеждународныйОстатки.Счет.УчетПоПодразделениям
	|		ТОГДА МеждународныйОстатки.Подразделение
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Подразделение,
	|	ВЫБОР КОГДА МеждународныйОстатки.Счет.УчетПоНаправлениямДеятельности
	|		ТОГДА МеждународныйОстатки.НаправлениеДеятельности
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА МеждународныйОстатки.Счет.Валютный
	|			ТОГДА МеждународныйОстатки.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Валюта,
	|	МеждународныйОстатки.Субконто1 КАК Субконто1,
	|	МеждународныйОстатки.Субконто2 КАК Субконто2,
	|	МеждународныйОстатки.Субконто3 КАК Субконто3,
	|	МеждународныйОстатки.СуммаОстатокДт - МеждународныйОстатки.СуммаОстатокКт КАК Сумма,
	|	МеждународныйОстатки.СуммаОстатокДт КАК СуммаДт,
	|	МеждународныйОстатки.СуммаОстатокКт КАК СуммаКт,
	|	МеждународныйОстатки.СуммаПредставленияОстатокДт - МеждународныйОстатки.СуммаПредставленияОстатокКт КАК СуммаПредставления,
	|	МеждународныйОстатки.СуммаПредставленияОстатокДт КАК СуммаПредставленияДт,
	|	МеждународныйОстатки.СуммаПредставленияОстатокКт КАК СуммаПредставленияКт,
	|	МеждународныйОстатки.ВалютнаяСуммаОстатокДт - МеждународныйОстатки.ВалютнаяСуммаОстатокКт КАК ВалютнаяСумма,
	|	МеждународныйОстатки.ВалютнаяСуммаОстатокДт КАК ВалютнаяСуммаДт,
	|	МеждународныйОстатки.ВалютнаяСуммаОстатокКт КАК ВалютнаяСуммаКт,
	|	МеждународныйОстатки.Счет.Вид КАК ВидСчета
	|ПОМЕСТИТЬ втОстаткиСчетов
	|ИЗ
	|	РегистрБухгалтерии.Международный.Остатки(
	|			&Граница,
	|			Счет В (ВЫБРАТЬ Т.Счет ИЗ втЗакрываемыеСчета КАК Т),
	|			,
	|			ПланСчетов = &ПланСчетов И Организация = &Организация) КАК МеждународныйОстатки
	|			
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВариантыФормированияПроводок.БезКорреспонденции) КАК ВариантФормированияПроводок,
	|	Остатки.Счет,
	|	Остатки.ПланСчетов,
	|	Остатки.Организация,
	|	ВЫБОР КОГДА Остатки.Счет.УчетПоПодразделениям
	|		ТОГДА Остатки.Подразделение
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Подразделение,
	|	ВЫБОР КОГДА Остатки.Счет.УчетПоНаправлениямДеятельности
	|		ТОГДА Остатки.НаправлениеДеятельности
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА Остатки.Счет.Валютный
	|			ТОГДА Остатки.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Валюта,
	|	Остатки.Субконто1 КАК Субконто1,
	|	Остатки.Субконто2 КАК Субконто2,
	|	Остатки.Субконто3 КАК Субконто3,
	|	Остатки.СуммаОстатокДт - Остатки.СуммаОстатокКт КАК Сумма,
	|	Остатки.СуммаОстатокДт КАК СуммаДт,
	|	Остатки.СуммаОстатокКт КАК СуммаКт,
	|	Остатки.СуммаПредставленияОстатокДт - Остатки.СуммаПредставленияОстатокКт КАК СуммаПредставления,
	|	Остатки.СуммаПредставленияОстатокДт КАК СуммаПредставленияДт,
	|	Остатки.СуммаПредставленияОстатокКт КАК СуммаПредставленияКт,
	|	Остатки.ВалютнаяСуммаОстатокДт - Остатки.ВалютнаяСуммаОстатокКт КАК ВалютнаяСумма,
	|	Остатки.ВалютнаяСуммаОстатокДт КАК ВалютнаяСуммаДт,
	|	Остатки.ВалютнаяСуммаОстатокКт КАК ВалютнаяСуммаКт,
	|	Остатки.Счет.Вид КАК ВидСчета
	|ИЗ
	|	РегистрБухгалтерии.МеждународныйБезКорреспонденции.Остатки(
	|			&Граница,
	|			Счет В (ВЫБРАТЬ Т.Счет ИЗ втЗакрываемыеСчета КАК Т),
	|			,
	|			ПланСчетов = &ПланСчетов И Организация = &Организация) КАК Остатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Счета.СчетУчета КАК Счет,
	|	ВидыСубконто.НомерСтроки КАК НомерСтроки,
	|	ВидыСубконто.ВидСубконто КАК ВидСубконто,
	|	ВидыСубконто.ВидСубконто.Наименование КАК Наименование,
	|	ВидыСубконто.ВидСубконто.ТипЗначения КАК ТипЗначения,
	|	ВидыСубконто.ТолькоОбороты КАК ТолькоОбороты
	|ИЗ
	|	втСчетЗакрытия КАК Счета
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК ВидыСубконто
	|	ПО Счета.СчетУчета = ВидыСубконто.Ссылка
	|ГДЕ
	|	НЕ ВидыСубконто.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Счета.Счет КАК Счет,
	|	ВидыСубконто.НомерСтроки КАК НомерСтроки,
	|	ВидыСубконто.ВидСубконто КАК ВидСубконто,
	|	ВидыСубконто.ВидСубконто.Наименование КАК Наименование,
	|	ВидыСубконто.ВидСубконто.ТипЗначения КАК ТипЗначения,
	|	ВидыСубконто.ТолькоОбороты КАК ТолькоОбороты
	|ИЗ
	|	втЗакрываемыеСчета КАК Счета
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК ВидыСубконто
	|	ПО Счета.Счет = ВидыСубконто.Ссылка
	|ГДЕ
	|	НЕ ВидыСубконто.Ссылка ЕСТЬ NULL
	|
	|УПОРЯДОЧИТЬ ПО
	|	Счет,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// Закроем счета
	|ВЫБРАТЬ
	|	&Дата КАК Период,
	|	&Ссылка КАК Регистратор,
	|	ОстаткиСчетов.ВариантФормированияПроводок КАК ВариантФормированияПроводок,
	|	ОстаткиСчетов.ПланСчетов КАК ПланСчетов,
	|	ОстаткиСчетов.Организация КАК Организация,
	|	
	|	ВЫБОР
	|		КОГДА ОстаткиСчетов.Сумма > 0
	|		 ИЛИ (ОстаткиСчетов.Сумма = 0 И ОстаткиСчетов.СуммаПредставления > 0)
	|		 ИЛИ (ОстаткиСчетов.Сумма = 0 И ОстаткиСчетов.СуммаПредставления = 0 И ОстаткиСчетов.ВалютнаяСумма > 0)
	|			ТОГДА ВЫБОР
	|				КОГДА СчетЗакрытия.УчетПоПодразделениям
	|					ТОГДА ОстаткиСчетов.Подразделение
	|				ИНАЧЕ НЕОПРЕДЕЛЕНО
	|			КОНЕЦ
	|		ИНАЧЕ ОстаткиСчетов.Подразделение
	|	КОНЕЦ КАК ПодразделениеДт,
	|	
	|	ВЫБОР
	|		КОГДА ОстаткиСчетов.Сумма > 0
	|		 ИЛИ (ОстаткиСчетов.Сумма = 0 И ОстаткиСчетов.СуммаПредставления > 0)
	|		 ИЛИ (ОстаткиСчетов.Сумма = 0 И ОстаткиСчетов.СуммаПредставления = 0 И ОстаткиСчетов.ВалютнаяСумма > 0)
	|			ТОГДА ВЫБОР
	|				КОГДА СчетЗакрытия.УчетПоНаправлениям
	|					ТОГДА ОстаткиСчетов.НаправлениеДеятельности
	|				ИНАЧЕ НЕОПРЕДЕЛЕНО
	|			КОНЕЦ
	|		ИНАЧЕ ОстаткиСчетов.НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельностиДт,
	|	
	|	ВЫБОР
	|		КОГДА ОстаткиСчетов.Сумма > 0
	|		 ИЛИ (ОстаткиСчетов.Сумма = 0 И ОстаткиСчетов.СуммаПредставления > 0)
	|		 ИЛИ (ОстаткиСчетов.Сумма = 0 И ОстаткиСчетов.СуммаПредставления = 0 И ОстаткиСчетов.ВалютнаяСумма > 0)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ОстаткиСчетов.Валюта
	|	КОНЕЦ КАК ВалютаДт,
	|	
	|	ВЫБОР
	|		КОГДА ОстаткиСчетов.Сумма > 0
	|		 ИЛИ (ОстаткиСчетов.Сумма = 0 И ОстаткиСчетов.СуммаПредставления > 0)
	|		 ИЛИ (ОстаткиСчетов.Сумма = 0 И ОстаткиСчетов.СуммаПредставления = 0 И ОстаткиСчетов.ВалютнаяСумма > 0)
	|			ТОГДА 0
	|		ИНАЧЕ -ОстаткиСчетов.ВалютнаяСумма
	|	КОНЕЦ КАК ВалютнаяСуммаДт,
	|	
	|	ВЫБОР
	|		КОГДА ОстаткиСчетов.Сумма > 0
	|		 ИЛИ (ОстаткиСчетов.Сумма = 0 И ОстаткиСчетов.СуммаПредставления > 0)
	|		 ИЛИ (ОстаткиСчетов.Сумма = 0 И ОстаткиСчетов.СуммаПредставления = 0 И ОстаткиСчетов.ВалютнаяСумма > 0)
	|			ТОГДА СчетЗакрытия.СчетУчета
	|		ИНАЧЕ ОстаткиСчетов.Счет
	|	КОНЕЦ КАК СчетДт,
	|	
	|	ОстаткиСчетов.Субконто1 КАК СубконтоДт1,
	|	ОстаткиСчетов.Субконто2 КАК СубконтоДт2,
	|	ОстаткиСчетов.Субконто3 КАК СубконтоДт3,
	|	
	|	ВЫБОР
	|		КОГДА ОстаткиСчетов.Сумма > 0
	|		 ИЛИ (ОстаткиСчетов.Сумма = 0 И ОстаткиСчетов.СуммаПредставления > 0)
	|		 ИЛИ (ОстаткиСчетов.Сумма = 0 И ОстаткиСчетов.СуммаПредставления = 0 И ОстаткиСчетов.ВалютнаяСумма > 0)
	|			ТОГДА ОстаткиСчетов.Подразделение
	|		ИНАЧЕ ВЫБОР
	|			КОГДА СчетЗакрытия.УчетПоПодразделениям
	|				ТОГДА ОстаткиСчетов.Подразделение
	|			ИНАЧЕ НЕОПРЕДЕЛЕНО
	|		КОНЕЦ
	|	КОНЕЦ КАК ПодразделениеКт,
	|	
	|	ВЫБОР
	|		КОГДА ОстаткиСчетов.Сумма > 0
	|		 ИЛИ (ОстаткиСчетов.Сумма = 0 И ОстаткиСчетов.СуммаПредставления > 0)
	|		 ИЛИ (ОстаткиСчетов.Сумма = 0 И ОстаткиСчетов.СуммаПредставления = 0 И ОстаткиСчетов.ВалютнаяСумма > 0)
	|			ТОГДА ОстаткиСчетов.НаправлениеДеятельности
	|		ИНАЧЕ ВЫБОР
	|			КОГДА СчетЗакрытия.УчетПоНаправлениям
	|				ТОГДА ОстаткиСчетов.НаправлениеДеятельности
	|			ИНАЧЕ НЕОПРЕДЕЛЕНО
	|		КОНЕЦ
	|	КОНЕЦ КАК НаправлениеДеятельностиКт,
	|	
	|	ВЫБОР
	|		КОГДА ОстаткиСчетов.Сумма > 0
	|		 ИЛИ (ОстаткиСчетов.Сумма = 0 И ОстаткиСчетов.СуммаПредставления > 0)
	|		 ИЛИ (ОстаткиСчетов.Сумма = 0 И ОстаткиСчетов.СуммаПредставления = 0 И ОстаткиСчетов.ВалютнаяСумма > 0)
	|			ТОГДА ОстаткиСчетов.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВалютаКт,
	|	
	|	ВЫБОР
	|		КОГДА ОстаткиСчетов.Сумма > 0
	|		 ИЛИ (ОстаткиСчетов.Сумма = 0 И ОстаткиСчетов.СуммаПредставления > 0)
	|		 ИЛИ (ОстаткиСчетов.Сумма = 0 И ОстаткиСчетов.СуммаПредставления = 0 И ОстаткиСчетов.ВалютнаяСумма > 0)
	|			ТОГДА ОстаткиСчетов.ВалютнаяСумма
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ВалютнаяСуммаКт,
	|	
	|	ВЫБОР
	|		КОГДА ОстаткиСчетов.Сумма > 0
	|		 ИЛИ (ОстаткиСчетов.Сумма = 0 И ОстаткиСчетов.СуммаПредставления > 0)
	|		 ИЛИ (ОстаткиСчетов.Сумма = 0 И ОстаткиСчетов.СуммаПредставления = 0 И ОстаткиСчетов.ВалютнаяСумма > 0)
	|			ТОГДА ОстаткиСчетов.Счет
	|		ИНАЧЕ СчетЗакрытия.СчетУчета
	|	КОНЕЦ КАК СчетКт,
	|	
	|	ОстаткиСчетов.Субконто1 КАК СубконтоКт1,
	|	ОстаткиСчетов.Субконто2 КАК СубконтоКт2,
	|	ОстаткиСчетов.Субконто3 КАК СубконтоКт3,
	|	
	|	ВЫБОР
	|		КОГДА ОстаткиСчетов.Сумма < 0
	|			ТОГДА -ОстаткиСчетов.Сумма
	|		ИНАЧЕ ОстаткиСчетов.Сумма
	|	КОНЕЦ КАК Сумма,
	|	
	|	ВЫБОР
	|		КОГДА ОстаткиСчетов.Сумма < 0
	|		 ИЛИ (ОстаткиСчетов.Сумма = 0 И ОстаткиСчетов.СуммаПредставления < 0)
	|			ТОГДА -ОстаткиСчетов.СуммаПредставления
	|		ИНАЧЕ ОстаткиСчетов.СуммаПредставления
	|	КОНЕЦ КАК СуммаПредставления,
	|	
	|	0 КАК ВалютнаяСумма,
	|	
	|	&Содержание КАК Содержание,
	|	НЕОПРЕДЕЛЕНО КАК ШаблонПроводки,
	|	НЕОПРЕДЕЛЕНО КАК ТипПроводки,
	|	ОстаткиСчетов.ВидСчета
	|ИЗ
	|	втОстаткиСчетов КАК ОстаткиСчетов
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСчетЗакрытия КАК СчетЗакрытия
	|	ПО ИСТИНА";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СчетУчетаДоходов" , ИмяПоляСчета);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СчетаДоходов"		, ИмяТабЧастиСчетов);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Содержание", """"+СодержаниеПроводки+"""");
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаКурсовыхРазницФункциональнаяВалюта()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	НастройкаФормированияПроводокОрганизацийМУ.ПланСчетов КАК ПланСчетов,
	|	НастройкаФормированияПроводокОрганизацийМУ.Организация КАК Организация,
	|	НастройкаФормированияПроводокОрганизацийМУ.НастройкаФормированияПроводок,
	|	НастройкиФормированияПроводок.ДоходыКР КАК ДоходыКР,
	|	НастройкиФормированияПроводок.СтатьяДоходовКР КАК СтатьяДоходовКР,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.СтатьиДоходов) КАК ВидСубконтоСтатьиДоходов,
	|	НастройкиФормированияПроводок.РасходыКР КАК РасходыКР,
	|	НастройкиФормированияПроводок.СтатьяРасходовКР КАК СтатьяРасходовКР,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.СтатьиРасходов) КАК ВидСубконтоСтатьиРасходов,
	|	НастройкиФормированияПроводок.ДоходыКР.Валютный КАК ДоходыВалютный,
	|	НастройкиФормированияПроводок.РасходыКР.Валютный КАК РасходыВалютный
	|ПОМЕСТИТЬ втСчетаУчетаКР
	|ИЗ
	|	РегистрСведений.ПланыСчетовМеждународногоУчетаОрганизаций.СрезПоследних(&Дата, 
	|			ПланСчетов = &ПланСчетов И Организация = &Организация) КАК НастройкаФормированияПроводокОрганизацийМУ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НастройкиФормированияПроводокМеждународногоУчета КАК НастройкиФормированияПроводок
	|		ПО НастройкаФормированияПроводокОрганизацийМУ.НастройкаФормированияПроводок = НастройкиФормированияПроводок.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Период,
	|	КурсыВалютСрезПоследних.Валюта КАК Валюта,
	|	КурсыВалютСрезПоследних.КурсЧислитель,
	|	КурсыВалютСрезПоследних.КурсЗнаменатель
	|ПОМЕСТИТЬ втКурсВалют
	|ИЗ
	|	РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних(&Дата, БазоваяВалюта = &ВалютаРегламентированногоУчета) КАК КурсыВалютСрезПоследних
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КурсДоговора.Договор КАК Договор,
	|	КурсДоговора.КурсЧислитель КАК КурсЧислитель,
	|	КурсДоговора.КурсЗнаменатель КАК КурсЗнаменатель
	|ПОМЕСТИТЬ втКурсыДоговоров
	|ИЗ
	|	РегистрСведений.КурсыВалютРасчетовПоДоговорам.СрезПоследних(&Дата) КАК КурсДоговора
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВариантыФормированияПроводок.СКорреспонденцией) КАК ВариантФормированияПроводок,
	|	МеждународныйОстатки.Счет,
	|	МеждународныйОстатки.ПланСчетов,
	|	МеждународныйОстатки.Организация,
	|	МеждународныйОстатки.Подразделение,
	|	МеждународныйОстатки.НаправлениеДеятельности,
	|	МеждународныйОстатки.Валюта,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто1.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто1
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто1,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто2.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто2
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто2,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто3.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто3
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто3,
	|	СУММА(МеждународныйОстатки.СуммаОстаток) КАК СуммаОстаток,
	|	СУММА(МеждународныйОстатки.ВалютнаяСуммаОстаток) КАК ВалютнаяСуммаОстаток
	|ПОМЕСТИТЬ втОстаткиСчетов
	|ИЗ
	|	РегистрБухгалтерии.Международный.Остатки(
	|			&Граница,
	|			Счет.Валютный
	|				И НЕ Счет.ИсключитьИзРасчетаКурсовыхРазниц,
	|			,
	|			ПланСчетов = &ПланСчетов И Организация = &Организация) КАК МеждународныйОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто1
	|		ПО МеждународныйОстатки.Счет = МеждународныйВидыСубконто1.Ссылка
	|			И (МеждународныйВидыСубконто1.НомерСтроки = 1)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто2
	|		ПО МеждународныйОстатки.Счет = МеждународныйВидыСубконто2.Ссылка
	|			И (МеждународныйВидыСубконто2.НомерСтроки = 2)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто3
	|		ПО МеждународныйОстатки.Счет = МеждународныйВидыСубконто3.Ссылка
	|			И (МеждународныйВидыСубконто3.НомерСтроки = 3)
	|
	|СГРУППИРОВАТЬ ПО
	|	МеждународныйОстатки.Счет,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто1.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто1
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто2.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто2
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто3.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто3
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	МеждународныйОстатки.ПланСчетов,
	|	МеждународныйОстатки.Организация,
	|	МеждународныйОстатки.Подразделение,
	|	МеждународныйОстатки.НаправлениеДеятельности,
	|	МеждународныйОстатки.Валюта
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВариантыФормированияПроводок.БезКорреспонденции) КАК ВариантФормированияПроводок,
	|	Остатки.Счет,
	|	Остатки.ПланСчетов,
	|	Остатки.Организация,
	|	Остатки.Подразделение,
	|	Остатки.НаправлениеДеятельности,
	|	Остатки.Валюта,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто1.Валютный
	|			ТОГДА Остатки.Субконто1
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто1,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто2.Валютный
	|			ТОГДА Остатки.Субконто2
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто2,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто3.Валютный
	|			ТОГДА Остатки.Субконто3
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто3,
	|	СУММА(Остатки.СуммаОстаток) КАК СуммаОстаток,
	|	СУММА(Остатки.ВалютнаяСуммаОстаток) КАК ВалютнаяСуммаОстаток
	|ИЗ
	|	РегистрБухгалтерии.МеждународныйБезКорреспонденции.Остатки(
	|			&Граница,
	|			Счет.Валютный
	|				И НЕ Счет.ИсключитьИзРасчетаКурсовыхРазниц,
	|			,
	|			ПланСчетов = &ПланСчетов И Организация = &Организация) КАК Остатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто1
	|		ПО Остатки.Счет = МеждународныйВидыСубконто1.Ссылка
	|			И (МеждународныйВидыСубконто1.НомерСтроки = 1)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто2
	|		ПО Остатки.Счет = МеждународныйВидыСубконто2.Ссылка
	|			И (МеждународныйВидыСубконто2.НомерСтроки = 2)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто3
	|		ПО Остатки.Счет = МеждународныйВидыСубконто3.Ссылка
	|			И (МеждународныйВидыСубконто3.НомерСтроки = 3)
	|
	|СГРУППИРОВАТЬ ПО
	|	Остатки.Счет,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто1.Валютный
	|			ТОГДА Остатки.Субконто1
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто2.Валютный
	|			ТОГДА Остатки.Субконто2
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто3.Валютный
	|			ТОГДА Остатки.Субконто3
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	Остатки.ПланСчетов,
	|	Остатки.Организация,
	|	Остатки.Подразделение,
	|	Остатки.НаправлениеДеятельности,
	|	Остатки.Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка                                             КАК Договор,
	|	ДоговорыКонтрагентов.ВариантКурсаДоговора                               КАК ВариантКурсаДоговора,
	|	ДоговорыКонтрагентов.ВалютаВзаиморасчетов                               КАК ВалютаВзаиморасчетов,
	|	ЕСТЬNULL(КурсыВалютРасчетовПоДоговорамСрезПоследних.КурсЧислитель, 1)   КАК КурсЧислитель,
	|	ЕСТЬNULL(КурсыВалютРасчетовПоДоговорамСрезПоследних.КурсЗнаменатель, 1) КАК КурсЗнаменатель
	|ПОМЕСТИТЬ ВтДоговоры
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКурсыДоговоров КАК КурсыВалютРасчетовПоДоговорамСрезПоследних
	|		ПО ДоговорыКонтрагентов.Ссылка = КурсыВалютРасчетовПоДоговорамСрезПоследних.Договор
	|ГДЕ
	|	ДоговорыКонтрагентов.Ссылка В
	|			(ВЫБРАТЬ
	|				втОстаткиСчетов.Субконто1
	|			ИЗ
	|				втОстаткиСчетов
	|		
	|			ОБЪЕДИНИТЬ ВСЕ
	|		
	|			ВЫБРАТЬ
	|				втОстаткиСчетов.Субконто2
	|			ИЗ
	|				втОстаткиСчетов
	|		
	|			ОБЪЕДИНИТЬ ВСЕ
	|		
	|			ВЫБРАТЬ
	|				втОстаткиСчетов.Субконто3
	|			ИЗ
	|				втОстаткиСчетов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДоговорыМеждуОрганизациями.Ссылка                                       КАК Договор,
	|	ДоговорыМеждуОрганизациями.ВариантКурсаДоговора                         КАК ВариантКурсаДоговора,
	|	ДоговорыМеждуОрганизациями.ВалютаВзаиморасчетов                         КАК ВалютаВзаиморасчетов,
	|	ЕСТЬNULL(КурсыВалютРасчетовПоДоговорамСрезПоследних.КурсЧислитель, 1)   КАК КурсЧислитель,
	|	ЕСТЬNULL(КурсыВалютРасчетовПоДоговорамСрезПоследних.КурсЗнаменатель, 1) КАК КурсЗнаменатель
	|ИЗ
	|	Справочник.ДоговорыМеждуОрганизациями КАК ДоговорыМеждуОрганизациями
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКурсыДоговоров КАК КурсыВалютРасчетовПоДоговорамСрезПоследних
	|		ПО ДоговорыМеждуОрганизациями.Ссылка = КурсыВалютРасчетовПоДоговорамСрезПоследних.Договор
	|ГДЕ
	|	ДоговорыМеждуОрганизациями.Ссылка В
	|			(ВЫБРАТЬ
	|				втОстаткиСчетов.Субконто1
	|			ИЗ
	|				втОстаткиСчетов
	|		
	|			ОБЪЕДИНИТЬ ВСЕ
	|		
	|			ВЫБРАТЬ
	|				втОстаткиСчетов.Субконто2
	|			ИЗ
	|				втОстаткиСчетов
	|		
	|			ОБЪЕДИНИТЬ ВСЕ
	|		
	|			ВЫБРАТЬ
	|				втОстаткиСчетов.Субконто3
	|			ИЗ
	|				втОстаткиСчетов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиСчетов.ВариантФормированияПроводок                         КАК ВариантФормированияПроводок,
	|	ОстаткиСчетов.Счет                                                КАК Счет,
	|	ОстаткиСчетов.ПланСчетов                                          КАК ПланСчетов,
	|	ОстаткиСчетов.Организация                                         КАК Организация,
	|	ОстаткиСчетов.Подразделение                                       КАК Подразделение,
	|	ОстаткиСчетов.НаправлениеДеятельности                             КАК НаправлениеДеятельности,
	|	ОстаткиСчетов.Субконто1                                           КАК Субконто1,
	|	ОстаткиСчетов.Субконто2                                           КАК Субконто2,
	|	ОстаткиСчетов.Субконто3                                           КАК Субконто3,
	|	ОстаткиСчетов.Валюта                                              КАК Валюта,
	|	ОстаткиСчетов.ВалютнаяСуммаОстаток                                КАК ВалютнаяСуммаОстаток,
	|	ОстаткиСчетов.СуммаОстаток                                        КАК СуммаОстаток,
	|	ЕСТЬNULL(
	|		ЕСТЬNULL(
	|			втДоговоры1.ВариантКурсаДоговора,
	|			втДоговоры2.ВариантКурсаДоговора),
	|		ЕСТЬNULL(
	|			втДоговоры3.ВариантКурсаДоговора,
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.Переменный))) КАК ВариантКурсаДоговора,
	|	ЕСТЬNULL(
	|		ЕСТЬNULL(
	|			втДоговоры1.КурсЧислитель,
	|			втДоговоры2.КурсЧислитель),
	|		ЕСТЬNULL(
	|			втДоговоры3.КурсЧислитель,
	|			1))                                                       КАК КурсЧислитель,
	|	ЕСТЬNULL(
	|		ЕСТЬNULL(
	|			втДоговоры1.КурсЗнаменатель,
	|			втДоговоры2.КурсЗнаменатель),
	|		ЕСТЬNULL(
	|			втДоговоры3.КурсЗнаменатель,
	|			1))                                                       КАК КурсЗнаменатель,
	|	ЕСТЬNULL(
	|		ЕСТЬNULL(
	|			втДоговоры1.ВалютаВзаиморасчетов,
	|			втДоговоры2.ВалютаВзаиморасчетов),
	|		ЕСТЬNULL(
	|			втДоговоры3.ВалютаВзаиморасчетов,
	|			НЕОПРЕДЕЛЕНО))                                            КАК ВалютаВзаиморасчетов
	|ПОМЕСТИТЬ ВтОстаткиСчетовСКурсомДоговора
	|ИЗ
	|	втОстаткиСчетов КАК ОстаткиСчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтДоговоры КАК втДоговоры1
	|		ПО ОстаткиСчетов.Субконто1 = втДоговоры1.Договор
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтДоговоры КАК втДоговоры2
	|		ПО ОстаткиСчетов.Субконто2 = втДоговоры2.Договор
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтДоговоры КАК втДоговоры3
	|		ПО ОстаткиСчетов.Субконто3 = втДоговоры3.Договор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВтОстаткиСКурсомДоговора.ВариантФормированияПроводок КАК ВариантФормированияПроводок,
	|	ВтОстаткиСКурсомДоговора.Счет                        КАК Счет,
	|	ВтОстаткиСКурсомДоговора.ПланСчетов                  КАК ПланСчетов,
	|	ВтОстаткиСКурсомДоговора.Организация                 КАК Организация,
	|	ВтОстаткиСКурсомДоговора.Подразделение               КАК Подразделение,
	|	ВтОстаткиСКурсомДоговора.НаправлениеДеятельности     КАК НаправлениеДеятельности,
	|	ВтОстаткиСКурсомДоговора.Субконто1                   КАК Субконто1,
	|	ВтОстаткиСКурсомДоговора.Субконто2                   КАК Субконто2,
	|	ВтОстаткиСКурсомДоговора.Субконто3                   КАК Субконто3,
	|	ВтОстаткиСКурсомДоговора.Валюта                      КАК Валюта,
	|	ВтОстаткиСКурсомДоговора.ВалютнаяСуммаОстаток        КАК ВалютнаяСуммаОстаток,
	|	ВтОстаткиСКурсомДоговора.СуммаОстаток                КАК СуммаОстаток,
	|	ВтОстаткиСКурсомДоговора.ВариантКурсаДоговора        КАК ВариантКурсаДоговора,
	|	ВЫБОР
	|		КОГДА ВтОстаткиСКурсомДоговора.ВариантКурсаДоговора = ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.УстановленныйВДоговоре)
	|				И ВтОстаткиСКурсомДоговора.Валюта = ВтОстаткиСКурсомДоговора.ВалютаВзаиморасчетов
	|			ТОГДА ВтОстаткиСКурсомДоговора.КурсЧислитель
	|		ИНАЧЕ втКурсВалют.КурсЧислитель
	|	КОНЕЦ                                                КАК КурсЧислитель,
	|	ВЫБОР
	|		КОГДА ВтОстаткиСКурсомДоговора.ВариантКурсаДоговора = ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.УстановленныйВДоговоре)
	|				И ВтОстаткиСКурсомДоговора.Валюта = ВтОстаткиСКурсомДоговора.ВалютаВзаиморасчетов
	|			ТОГДА ВтОстаткиСКурсомДоговора.КурсЗнаменатель
	|		ИНАЧЕ втКурсВалют.КурсЗнаменатель
	|	КОНЕЦ                                                КАК КурсЗнаменатель,
	|	ВЫБОР
	|		КОГДА ВтОстаткиСКурсомДоговора.ВариантКурсаДоговора = ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.УстановленныйВДоговоре)
	|				И &ФункциональнаяВалюта = ВтОстаткиСКурсомДоговора.ВалютаВзаиморасчетов
	|			ТОГДА ВтОстаткиСКурсомДоговора.КурсЧислитель
	|		ИНАЧЕ &КурсЧислительФВ
	|	КОНЕЦ                                            КАК КурсЧислительФункциональнойВалюты,
	|	ВЫБОР
	|		КОГДА ВтОстаткиСКурсомДоговора.ВариантКурсаДоговора = ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.УстановленныйВДоговоре)
	|				И &ФункциональнаяВалюта = ВтОстаткиСКурсомДоговора.ВалютаВзаиморасчетов
	|			ТОГДА ВтОстаткиСКурсомДоговора.КурсЗнаменатель
	|		ИНАЧЕ &КурсЗнаменательФВ
	|	КОНЕЦ                                            КАК КурсЗнаменательФункциональнойВалюты
	|ПОМЕСТИТЬ ВтОстаткиСчетовСКурсами
	|ИЗ
	|	ВтОстаткиСчетовСКурсомДоговора КАК ВтОстаткиСКурсомДоговора
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКурсВалют КАК втКурсВалют
	|		ПО ВтОстаткиСКурсомДоговора.Валюта = втКурсВалют.Валюта
	|ГДЕ
	|	ВтОстаткиСКурсомДоговора.ВариантКурсаДоговора <> ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.ФиксированныйНаДатуОтгрузки)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1000
	|	&Дата КАК Период,
	|	ОстаткиСчетов.ВариантФормированияПроводок КАК ВариантФормированияПроводок,
	|	ОстаткиСчетов.Счет,
	|	ОстаткиСчетов.Счет.Вид КАК ВидСчета,
	|	ОстаткиСчетов.Счет.Забалансовый КАК Забалансовый,
	|	ОстаткиСчетов.ПланСчетов,
	|	ОстаткиСчетов.Организация,
	|	ВЫБОР КОГДА ОстаткиСчетов.Счет.УчетПоПодразделениям
	|		ТОГДА ОстаткиСчетов.Подразделение
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Подразделение,
	|	ВЫБОР КОГДА ОстаткиСчетов.Счет.УчетПоНаправлениямДеятельности
	|		ТОГДА ОстаткиСчетов.НаправлениеДеятельности
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	ОстаткиСчетов.Счет.ВидыСубконто.(
	|		НомерСтроки КАК НомерСтроки,
	|		ВидСубконто КАК ВидСубконто,
	|		ТолькоОбороты КАК ТолькоОбороты
	|	) КАК ВидыСубконтоСчета,
	|	ОстаткиСчетов.Субконто1,
	|	ОстаткиСчетов.Субконто2,
	|	ОстаткиСчетов.Субконто3,
	|	ОстаткиСчетов.КурсЧислитель,
	|	ОстаткиСчетов.КурсЗнаменатель,
	|	ОстаткиСчетов.Валюта КАК Валюта,
	|	ОстаткиСчетов.ВалютнаяСуммаОстаток,
	|	ВЫРАЗИТЬ(ОстаткиСчетов.ВалютнаяСуммаОстаток * ОстаткиСчетов.КурсЧислитель / ОстаткиСчетов.КурсЗнаменатель / ОстаткиСчетов.КурсЧислительФункциональнойВалюты * ОстаткиСчетов.КурсЗнаменательФункциональнойВалюты КАК ЧИСЛО(31,2)) КАК ВалСуммаФВ,
	|	ОстаткиСчетов.СуммаОстаток,
	|	(ВЫРАЗИТЬ(ОстаткиСчетов.ВалютнаяСуммаОстаток * ОстаткиСчетов.КурсЧислитель / ОстаткиСчетов.КурсЗнаменатель / ОстаткиСчетов.КурсЧислительФункциональнойВалюты * ОстаткиСчетов.КурсЗнаменательФункциональнойВалюты КАК ЧИСЛО(31,2))) - ОстаткиСчетов.СуммаОстаток КАК КурсоваяРазница,
	|	ОстаткиСчетов.Счет.Порядок КАК СчетПорядок,
	|	втСчетаУчетаКР.ДоходыКР КАК Доходы,
	|	втСчетаУчетаКР.СтатьяДоходовКР КАК СтатьяДоходов,
	|	втСчетаУчетаКР.ВидСубконтоСтатьиДоходов,
	|	ЕСТЬNULL(втСчетаУчетаКР.ДоходыВалютный, ЛОЖЬ) КАК ДоходыВалютный,
	|	ЕСТЬNULL(втСчетаУчетаКР.ДоходыКР.УчетПоПодразделениям, ЛОЖЬ) КАК ДоходыПоПодразделениям,
	|	ЕСТЬNULL(втСчетаУчетаКР.ДоходыКР.УчетПоНаправлениямДеятельности, ЛОЖЬ) КАК ДоходыПоНаправлениям,
	|	втСчетаУчетаКР.РасходыКР КАК Расходы,
	|	втСчетаУчетаКР.СтатьяРасходовКР КАК СтатьяРасходов,
	|	втСчетаУчетаКР.ВидСубконтоСтатьиРасходов,
	|	ЕСТЬNULL(втСчетаУчетаКР.РасходыВалютный, ЛОЖЬ) КАК РасходыВалютный,
	|	ЕСТЬNULL(втСчетаУчетаКР.РасходыКР.УчетПоПодразделениям, ЛОЖЬ) КАК РасходыПоПодразделениям,
	|	ЕСТЬNULL(втСчетаУчетаКР.РасходыКР.УчетПоНаправлениямДеятельности, ЛОЖЬ) КАК РасходыПоНаправлениям
	|ИЗ
	|	ВтОстаткиСчетовСКурсами КАК ОстаткиСчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСчетаУчетаКР КАК втСчетаУчетаКР
	|		ПО ОстаткиСчетов.ПланСчетов = втСчетаУчетаКР.ПланСчетов
	|			И ОстаткиСчетов.Организация = втСчетаУчетаКР.Организация
	|ГДЕ
	|	(ВЫРАЗИТЬ(ОстаткиСчетов.ВалютнаяСуммаОстаток * ОстаткиСчетов.КурсЧислитель / ОстаткиСчетов.КурсЗнаменатель / ОстаткиСчетов.КурсЧислительФункциональнойВалюты * ОстаткиСчетов.КурсЗнаменательФункциональнойВалюты КАК ЧИСЛО(31,2))) - ОстаткиСчетов.СуммаОстаток <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	СчетПорядок";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаКурсовыхРазницВалютаПредставления()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	НастройкаФормированияПроводокОрганизацийМУ.ПланСчетов,
	|	НастройкаФормированияПроводокОрганизацийМУ.Организация,
	|	НастройкаФормированияПроводокОрганизацийМУ.НастройкаФормированияПроводок,
	|	НастройкиФормированияПроводок.ДоходыОтРазницПриПересчетеВП КАК Доходы,
	|	НастройкиФормированияПроводок.СтатьяДоходовОтРазницПересчета КАК СтатьяДоходов,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.СтатьиДоходов) КАК ВидСубконтоСтатьиДоходов,
	|	НастройкиФормированияПроводок.РасходыОтРазницПриПересчетеВП КАК Расходы,
	|	НастройкиФормированияПроводок.СтатьяРасходовОтРазницПересчета КАК СтатьяРасходов,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.СтатьиРасходов) КАК ВидСубконтоСтатьиРасходов,
	|	НастройкиФормированияПроводок.ДоходыОтРазницПриПересчетеВП.Валютный КАК ДоходыВалютный,
	|	НастройкиФормированияПроводок.РасходыОтРазницПриПересчетеВП.Валютный КАК РасходыВалютный
	|ПОМЕСТИТЬ втСчетаУчета
	|ИЗ
	|	РегистрСведений.ПланыСчетовМеждународногоУчетаОрганизаций.СрезПоследних(&Дата, ПланСчетов = &ПланСчетов И Организация = &Организация) КАК НастройкаФормированияПроводокОрганизацийМУ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НастройкиФормированияПроводокМеждународногоУчета КАК НастройкиФормированияПроводок
	|		ПО НастройкаФормированияПроводокОрганизацийМУ.НастройкаФормированияПроводок = НастройкиФормированияПроводок.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НастройкаФормированияПроводокОрганизацийМУ.ПланСчетов,
	|	НастройкаФормированияПроводокОрганизацийМУ.Организация,
	|	НастройкаФормированияПроводокОрганизацийМУ.НастройкаФормированияПроводок,
	|	НастройкиФормированияПроводокИсключитьИзПересчетаВП.Счет
	|ПОМЕСТИТЬ втСчетаИсключения
	|ИЗ
	|	РегистрСведений.ПланыСчетовМеждународногоУчетаОрганизаций.СрезПоследних(&Дата, ПланСчетов = &ПланСчетов И Организация = &Организация) КАК НастройкаФормированияПроводокОрганизацийМУ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НастройкиФормированияПроводокМеждународногоУчета.ИсключитьИзПересчетаВП КАК НастройкиФормированияПроводокИсключитьИзПересчетаВП
	|		ПО НастройкаФормированияПроводокОрганизацийМУ.НастройкаФормированияПроводок = НастройкиФормированияПроводокИсключитьИзПересчетаВП.Ссылка
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	втСчетаУчета.ПланСчетов,
	|	втСчетаУчета.Организация,
	|	втСчетаУчета.НастройкаФормированияПроводок,
	|	втСчетаУчета.Доходы
	|ИЗ
	|	втСчетаУчета КАК втСчетаУчета
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	втСчетаУчета.ПланСчетов,
	|	втСчетаУчета.Организация,
	|	втСчетаУчета.НастройкаФормированияПроводок,
	|	втСчетаУчета.Расходы
	|ИЗ
	|	втСчетаУчета КАК втСчетаУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Период,
	|	КурсыВалютСрезПоследних.Валюта,
	|	КурсыВалютСрезПоследних.КурсЧислитель,
	|	КурсыВалютСрезПоследних.КурсЗнаменатель
	|ПОМЕСТИТЬ втКурсВалют
	|ИЗ
	|	РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних(&Дата, БазоваяВалюта = &ВалютаРегламентированногоУчета) КАК КурсыВалютСрезПоследних
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КурсДоговора.Договор КАК Договор,
	|	КурсДоговора.КурсЧислитель КАК КурсЧислитель,
	|	КурсДоговора.КурсЗнаменатель КАК КурсЗнаменатель
	|ПОМЕСТИТЬ втКурсыДоговоров
	|ИЗ
	|	РегистрСведений.КурсыВалютРасчетовПоДоговорам.СрезПоследних(&Дата) КАК КурсДоговора
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВариантыФормированияПроводок.СКорреспонденцией) КАК ВариантФормированияПроводок,
	|	МеждународныйОстатки.Счет,
	|	МеждународныйОстатки.ПланСчетов,
	|	МеждународныйОстатки.Организация,
	|	МеждународныйОстатки.Подразделение,
	|	МеждународныйОстатки.НаправлениеДеятельности,
	|	МеждународныйОстатки.Валюта,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто1.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто1
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто1,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто2.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто2
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто2,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто3.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто3
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто3,
	|	СУММА(МеждународныйОстатки.СуммаОстаток) КАК СуммаОстаток,
	|	СУММА(МеждународныйОстатки.ВалютнаяСуммаОстаток) КАК ВалютнаяСуммаОстаток,
	|	СУММА(МеждународныйОстатки.СуммаПредставленияОстаток) КАК СуммаПредставленияОстаток
	|ПОМЕСТИТЬ втОстаткиСчетов
	|ИЗ
	|	РегистрБухгалтерии.Международный.Остатки(
	|			&Граница,
	|			НЕ Счет В (ВЫБРАТЬ втСчетаИсключения.Счет ИЗ втСчетаИсключения),
	|			,
	|			ПланСчетов = &ПланСчетов И Организация = &Организация) КАК МеждународныйОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто1
	|		ПО МеждународныйОстатки.Счет = МеждународныйВидыСубконто1.Ссылка
	|			И (МеждународныйВидыСубконто1.НомерСтроки = 1)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто2
	|		ПО МеждународныйОстатки.Счет = МеждународныйВидыСубконто2.Ссылка
	|			И (МеждународныйВидыСубконто2.НомерСтроки = 2)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто3
	|		ПО МеждународныйОстатки.Счет = МеждународныйВидыСубконто3.Ссылка
	|			И (МеждународныйВидыСубконто3.НомерСтроки = 3)
	|
	|СГРУППИРОВАТЬ ПО
	|	МеждународныйОстатки.Счет,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто1.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто1
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто2.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто2
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто3.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто3
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	МеждународныйОстатки.ПланСчетов,
	|	МеждународныйОстатки.Организация,
	|	МеждународныйОстатки.Подразделение,
	|	МеждународныйОстатки.НаправлениеДеятельности,
	|	МеждународныйОстатки.Валюта
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВариантыФормированияПроводок.БезКорреспонденции) КАК ВариантФормированияПроводок,
	|	Остатки.Счет,
	|	Остатки.ПланСчетов,
	|	Остатки.Организация,
	|	Остатки.Подразделение,
	|	Остатки.НаправлениеДеятельности,
	|	Остатки.Валюта,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто1.Валютный
	|			ТОГДА Остатки.Субконто1
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто1,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто2.Валютный
	|			ТОГДА Остатки.Субконто2
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто2,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто3.Валютный
	|			ТОГДА Остатки.Субконто3
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто3,
	|	СУММА(Остатки.СуммаОстаток) КАК СуммаОстаток,
	|	СУММА(Остатки.ВалютнаяСуммаОстаток) КАК ВалютнаяСуммаОстаток,
	|	СУММА(Остатки.СуммаПредставленияОстаток) КАК СуммаПредставленияОстаток
	|ИЗ
	|	РегистрБухгалтерии.МеждународныйБезКорреспонденции.Остатки(
	|			&Граница,
	|			НЕ Счет В (ВЫБРАТЬ втСчетаИсключения.Счет ИЗ втСчетаИсключения),
	|			,
	|			ПланСчетов = &ПланСчетов И Организация = &Организация) КАК Остатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто1
	|		ПО Остатки.Счет = МеждународныйВидыСубконто1.Ссылка
	|			И (МеждународныйВидыСубконто1.НомерСтроки = 1)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто2
	|		ПО Остатки.Счет = МеждународныйВидыСубконто2.Ссылка
	|			И (МеждународныйВидыСубконто2.НомерСтроки = 2)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто3
	|		ПО Остатки.Счет = МеждународныйВидыСубконто3.Ссылка
	|			И (МеждународныйВидыСубконто3.НомерСтроки = 3)
	|
	|СГРУППИРОВАТЬ ПО
	|	Остатки.Счет,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто1.Валютный
	|			ТОГДА Остатки.Субконто1
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто2.Валютный
	|			ТОГДА Остатки.Субконто2
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто3.Валютный
	|			ТОГДА Остатки.Субконто3
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	Остатки.ПланСчетов,
	|	Остатки.Организация,
	|	Остатки.Подразделение,
	|	Остатки.НаправлениеДеятельности,
	|	Остатки.Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка                                             КАК Договор,
	|	ДоговорыКонтрагентов.ВариантКурсаДоговора                               КАК ВариантКурсаДоговора,
	|	ДоговорыКонтрагентов.ВалютаВзаиморасчетов                               КАК ВалютаВзаиморасчетов,
	|	ЕСТЬNULL(КурсыВалютРасчетовПоДоговорамСрезПоследних.КурсЧислитель, 1)   КАК КурсЧислитель,
	|	ЕСТЬNULL(КурсыВалютРасчетовПоДоговорамСрезПоследних.КурсЗнаменатель, 1) КАК КурсЗнаменатель
	|ПОМЕСТИТЬ ВтДоговоры
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКурсыДоговоров КАК КурсыВалютРасчетовПоДоговорамСрезПоследних
	|		ПО ДоговорыКонтрагентов.Ссылка = КурсыВалютРасчетовПоДоговорамСрезПоследних.Договор
	|ГДЕ
	|	ДоговорыКонтрагентов.Ссылка В
	|			(ВЫБРАТЬ
	|				втОстаткиСчетов.Субконто1
	|			ИЗ
	|				втОстаткиСчетов
	|		
	|			ОБЪЕДИНИТЬ ВСЕ
	|		
	|			ВЫБРАТЬ
	|				втОстаткиСчетов.Субконто2
	|			ИЗ
	|				втОстаткиСчетов
	|		
	|			ОБЪЕДИНИТЬ ВСЕ
	|		
	|			ВЫБРАТЬ
	|				втОстаткиСчетов.Субконто3
	|			ИЗ
	|				втОстаткиСчетов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДоговорыМеждуОрганизациями.Ссылка                                       КАК Договор,
	|	ДоговорыМеждуОрганизациями.ВариантКурсаДоговора                         КАК ВариантКурсаДоговора,
	|	ДоговорыМеждуОрганизациями.ВалютаВзаиморасчетов                         КАК ВалютаВзаиморасчетов,
	|	ЕСТЬNULL(КурсыВалютРасчетовПоДоговорамСрезПоследних.КурсЧислитель, 1)   КАК КурсЧислитель,
	|	ЕСТЬNULL(КурсыВалютРасчетовПоДоговорамСрезПоследних.КурсЗнаменатель, 1) КАК КурсЗнаменатель
	|ИЗ
	|	Справочник.ДоговорыМеждуОрганизациями КАК ДоговорыМеждуОрганизациями
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКурсыДоговоров КАК КурсыВалютРасчетовПоДоговорамСрезПоследних
	|		ПО ДоговорыМеждуОрганизациями.Ссылка = КурсыВалютРасчетовПоДоговорамСрезПоследних.Договор
	|ГДЕ
	|	ДоговорыМеждуОрганизациями.Ссылка В
	|			(ВЫБРАТЬ
	|				втОстаткиСчетов.Субконто1
	|			ИЗ
	|				втОстаткиСчетов
	|		
	|			ОБЪЕДИНИТЬ ВСЕ
	|		
	|			ВЫБРАТЬ
	|				втОстаткиСчетов.Субконто2
	|			ИЗ
	|				втОстаткиСчетов
	|		
	|			ОБЪЕДИНИТЬ ВСЕ
	|		
	|			ВЫБРАТЬ
	|				втОстаткиСчетов.Субконто3
	|			ИЗ
	|				втОстаткиСчетов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиСчетов.ВариантФормированияПроводок                         КАК ВариантФормированияПроводок,
	|	ОстаткиСчетов.Счет                                                КАК Счет,
	|	ОстаткиСчетов.ПланСчетов                                          КАК ПланСчетов,
	|	ОстаткиСчетов.Организация                                         КАК Организация,
	|	ОстаткиСчетов.Подразделение                                       КАК Подразделение,
	|	ОстаткиСчетов.НаправлениеДеятельности                             КАК НаправлениеДеятельности,
	|	ОстаткиСчетов.Субконто1                                           КАК Субконто1,
	|	ОстаткиСчетов.Субконто2                                           КАК Субконто2,
	|	ОстаткиСчетов.Субконто3                                           КАК Субконто3,
	|	ОстаткиСчетов.Валюта                                              КАК Валюта,
	|	ОстаткиСчетов.ВалютнаяСуммаОстаток                                КАК ВалютнаяСуммаОстаток,
	|	ОстаткиСчетов.СуммаОстаток                                        КАК СуммаОстаток,
	|	ОстаткиСчетов.СуммаПредставленияОстаток                           КАК СуммаПредставленияОстаток,
	|	ЕСТЬNULL(
	|		ЕСТЬNULL(
	|			втДоговоры1.ВариантКурсаДоговора,
	|			втДоговоры2.ВариантКурсаДоговора),
	|		ЕСТЬNULL(
	|			втДоговоры3.ВариантКурсаДоговора,
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.Переменный))) КАК ВариантКурсаДоговора,
	|	ЕСТЬNULL(
	|		ЕСТЬNULL(
	|			втДоговоры1.КурсЧислитель,
	|			втДоговоры2.КурсЧислитель),
	|		ЕСТЬNULL(
	|			втДоговоры3.КурсЧислитель,
	|			1))                                                       КАК КурсЧислитель,
	|	ЕСТЬNULL(
	|		ЕСТЬNULL(
	|			втДоговоры1.КурсЗнаменатель,
	|			втДоговоры2.КурсЗнаменатель),
	|		ЕСТЬNULL(
	|			втДоговоры3.КурсЗнаменатель,
	|			1))                                                       КАК КурсЗнаменатель,
	|	ЕСТЬNULL(
	|		ЕСТЬNULL(
	|			втДоговоры1.ВалютаВзаиморасчетов,
	|			втДоговоры2.ВалютаВзаиморасчетов),
	|		ЕСТЬNULL(
	|			втДоговоры3.ВалютаВзаиморасчетов,
	|			НЕОПРЕДЕЛЕНО))                                            КАК ВалютаВзаиморасчетов
	|ПОМЕСТИТЬ ВтОстаткиСчетовСКурсомДоговора
	|ИЗ
	|	втОстаткиСчетов КАК ОстаткиСчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтДоговоры КАК втДоговоры1
	|		ПО ОстаткиСчетов.Субконто1 = втДоговоры1.Договор
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтДоговоры КАК втДоговоры2
	|		ПО ОстаткиСчетов.Субконто2 = втДоговоры2.Договор
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтДоговоры КАК втДоговоры3
	|		ПО ОстаткиСчетов.Субконто3 = втДоговоры3.Договор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВтОстаткиСКурсомДоговора.ВариантФормированияПроводок КАК ВариантФормированияПроводок,
	|	ВтОстаткиСКурсомДоговора.Счет                        КАК Счет,
	|	ВтОстаткиСКурсомДоговора.ПланСчетов                  КАК ПланСчетов,
	|	ВтОстаткиСКурсомДоговора.Организация                 КАК Организация,
	|	ВтОстаткиСКурсомДоговора.Подразделение               КАК Подразделение,
	|	ВтОстаткиСКурсомДоговора.НаправлениеДеятельности     КАК НаправлениеДеятельности,
	|	ВтОстаткиСКурсомДоговора.Субконто1                   КАК Субконто1,
	|	ВтОстаткиСКурсомДоговора.Субконто2                   КАК Субконто2,
	|	ВтОстаткиСКурсомДоговора.Субконто3                   КАК Субконто3,
	|	ВтОстаткиСКурсомДоговора.Валюта                      КАК Валюта,
	|	ВтОстаткиСКурсомДоговора.ВалютнаяСуммаОстаток        КАК ВалютнаяСуммаОстаток,
	|	ВтОстаткиСКурсомДоговора.СуммаОстаток                КАК СуммаОстаток,
	|	ВтОстаткиСКурсомДоговора.СуммаПредставленияОстаток   КАК СуммаПредставленияОстаток,
	|	ВтОстаткиСКурсомДоговора.ВариантКурсаДоговора        КАК ВариантКурсаДоговора,
	|	ВЫБОР
	|		КОГДА ВтОстаткиСКурсомДоговора.ВариантКурсаДоговора = ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.УстановленныйВДоговоре)
	|				И &ВалютаПредставления = ВтОстаткиСКурсомДоговора.ВалютаВзаиморасчетов
	|			ТОГДА ВтОстаткиСКурсомДоговора.КурсЧислитель
	|		ИНАЧЕ &КурсЧислительВП
	|	КОНЕЦ                                                КАК КурсЧислительВалютыПредставления,
	|	ВЫБОР
	|		КОГДА ВтОстаткиСКурсомДоговора.ВариантКурсаДоговора = ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.УстановленныйВДоговоре)
	|				И &ВалютаПредставления = ВтОстаткиСКурсомДоговора.ВалютаВзаиморасчетов
	|			ТОГДА ВтОстаткиСКурсомДоговора.КурсЗнаменатель
	|		ИНАЧЕ &КурсЗнаменательВП
	|	КОНЕЦ                                                КАК КурсЗнаменательВалютыПредставления,
	|	ВЫБОР
	|		КОГДА ВтОстаткиСКурсомДоговора.ВариантКурсаДоговора = ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.УстановленныйВДоговоре)
	|				И &ФункциональнаяВалюта = ВтОстаткиСКурсомДоговора.ВалютаВзаиморасчетов
	|			ТОГДА ВтОстаткиСКурсомДоговора.КурсЧислитель
	|		ИНАЧЕ &КурсЧислительФВ
	|	КОНЕЦ                                            КАК КурсЧислительФункциональнойВалюты,
	|	ВЫБОР
	|		КОГДА ВтОстаткиСКурсомДоговора.ВариантКурсаДоговора = ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.УстановленныйВДоговоре)
	|				И &ФункциональнаяВалюта = ВтОстаткиСКурсомДоговора.ВалютаВзаиморасчетов
	|			ТОГДА ВтОстаткиСКурсомДоговора.КурсЗнаменатель
	|		ИНАЧЕ &КурсЗнаменательФВ
	|	КОНЕЦ                                            КАК КурсЗнаменательФункциональнойВалюты
	|ПОМЕСТИТЬ ВтОстаткиСчетовСКурсами
	|ИЗ
	|	ВтОстаткиСчетовСКурсомДоговора КАК ВтОстаткиСКурсомДоговора
	|ГДЕ
	|	ВтОстаткиСКурсомДоговора.ВариантКурсаДоговора <> ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.ФиксированныйНаДатуОтгрузки)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1000
	|	&Дата КАК Период,
	|	ОстаткиСчетов.ВариантФормированияПроводок,
	|	ОстаткиСчетов.Счет,
	|	ОстаткиСчетов.Счет.Вид КАК ВидСчета,
	|	ОстаткиСчетов.Счет.Валютный КАК Валютный,
	|	ОстаткиСчетов.Счет.Забалансовый КАК Забалансовый,
	|	ОстаткиСчетов.ПланСчетов,
	|	ОстаткиСчетов.Организация,
	|	ВЫБОР КОГДА ОстаткиСчетов.Счет.УчетПоПодразделениям
	|		ТОГДА ОстаткиСчетов.Подразделение
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Подразделение,
	|	ВЫБОР КОГДА ОстаткиСчетов.Счет.УчетПоНаправлениямДеятельности
	|		ТОГДА ОстаткиСчетов.НаправлениеДеятельности
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	ОстаткиСчетов.Счет.ВидыСубконто.(
	|		НомерСтроки КАК НомерСтроки,
	|		ВидСубконто КАК ВидСубконто,
	|		ТолькоОбороты КАК ТолькоОбороты
	|	) КАК ВидыСубконтоСчета,
	|	ОстаткиСчетов.Субконто1,
	|	ОстаткиСчетов.Субконто2,
	|	ОстаткиСчетов.Субконто3,
	|	ОстаткиСчетов.Валюта,
	|	ОстаткиСчетов.СуммаОстаток,
	|	ВЫРАЗИТЬ(ОстаткиСчетов.СуммаОстаток * ОстаткиСчетов.КурсЧислительФункциональнойВалюты / ОстаткиСчетов.КурсЗнаменательФункциональнойВалюты / ОстаткиСчетов.КурсЧислительВалютыПредставления * ОстаткиСчетов.КурсЗнаменательВалютыПредставления КАК ЧИСЛО(31,2)) КАК СуммаВП,
	|	ОстаткиСчетов.СуммаПредставленияОстаток КАК СуммаВПОстаток,
	|	(ВЫРАЗИТЬ(ОстаткиСчетов.СуммаОстаток * ОстаткиСчетов.КурсЧислительФункциональнойВалюты / ОстаткиСчетов.КурсЗнаменательФункциональнойВалюты / ОстаткиСчетов.КурсЧислительВалютыПредставления * ОстаткиСчетов.КурсЗнаменательВалютыПредставления КАК ЧИСЛО(31,2))) - ОстаткиСчетов.СуммаПредставленияОстаток КАК КурсоваяРазница,
	|	ОстаткиСчетов.Счет.Порядок КАК СчетПорядок,
	|	втСчетаУчета.Доходы,
	|	ЕСТЬNULL(втСчетаУчета.ДоходыВалютный,ЛОЖЬ) КАК ДоходыВалютный,
	|	ЕСТЬNULL(втСчетаУчета.Доходы.УчетПоПодразделениям, ЛОЖЬ) КАК ДоходыПоПодразделениям,
	|	ЕСТЬNULL(втСчетаУчета.Доходы.УчетПоНаправлениямДеятельности, ЛОЖЬ) КАК ДоходыПоНаправлениям,
	|	втСчетаУчета.СтатьяДоходов,
	|	втСчетаУчета.ВидСубконтоСтатьиДоходов,
	|	втСчетаУчета.Расходы,
	|	ЕСТЬNULL(втСчетаУчета.РасходыВалютный,ЛОЖЬ) КАК РасходыВалютный,
	|	ЕСТЬNULL(втСчетаУчета.Расходы.УчетПоПодразделениям, ЛОЖЬ) КАК РасходыПоПодразделениям,
	|	ЕСТЬNULL(втСчетаУчета.Расходы.УчетПоНаправлениямДеятельности, ЛОЖЬ) КАК РасходыПоНаправлениям,
	|	втСчетаУчета.СтатьяРасходов,
	|	втСчетаУчета.ВидСубконтоСтатьиРасходов
	|ИЗ
	|	ВтОстаткиСчетовСКурсами КАК ОстаткиСчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСчетаУчета КАК втСчетаУчета
	|		ПО ОстаткиСчетов.ПланСчетов = втСчетаУчета.ПланСчетов
	|			И ОстаткиСчетов.Организация = втСчетаУчета.Организация
	|ГДЕ
	|	ЕСТЬNULL(ОстаткиСчетов.Валюта, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)) <> &ВалютаПредставления
	|	И (ВЫРАЗИТЬ(ОстаткиСчетов.СуммаОстаток * ОстаткиСчетов.КурсЧислительФункциональнойВалюты / ОстаткиСчетов.КурсЗнаменательФункциональнойВалюты / ОстаткиСчетов.КурсЧислительВалютыПредставления * ОстаткиСчетов.КурсЗнаменательВалютыПредставления КАК ЧИСЛО(31,2))) - ОстаткиСчетов.СуммаПредставленияОстаток <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	СчетПорядок";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура СформироватьЗаданияКЗакрытиюМесяцаПриОтменеПроведения()
	
	Если ТипОперации = Перечисления.ТипыРегламентныхОперацийМеждународныйУчет.РеклассификацияДолгосрочныхАктивовОбязательств
		И Документы.РегламентнаяОперацияМеждународныйУчет.ЕстьРеклассифицируемыеДолгосрочныеАктивыОбязательства(Организация, Дата) Тогда
		
		РегистрыСведений.ЗаданияКЗакрытиюМесяца.СоздатьЗаписьРегистра(
			НачалоМесяца(Дата),
			Ссылка,
			Организация,
			Перечисления.ОперацииЗакрытияМесяца.РеклассификацияДолгосрочныхАктивовОбязательствМУ);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
