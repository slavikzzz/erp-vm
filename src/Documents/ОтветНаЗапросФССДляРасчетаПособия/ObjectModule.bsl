#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКопировании(ОбъектКопирования)
	ИсправленныйДокумент = ОбъектКопирования.Ссылка;
	ОтключитьПроверкиПроведения = Ложь;
	ХранилищеXML                = Неопределено;
	ДатаОтправки                = '00010101';
	ИдентификаторСообщения      = "";
КонецПроцедуры

Процедура ОбработкаЗаполнения(Основание, ТекстЗаполнения, СтандартнаяОбработка)
	
	АвтозаполнениеПериодовОплаты = Истина;
	Если Не ЗначениеЗаполнено(Дата) Тогда
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	СсылкаОснования = Основание;
	ПричинаНетрудоспособности = Неопределено;
	Если ТипЗнч(Основание) = Тип("Структура") Тогда
		Если Основание.Свойство("Действие")
			И Основание.Действие = "Исправить"
			И ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыРасширеннаяПодсистемы.ИсправленияДокументов") Тогда
			МодульИсправлениеДокументовЗарплатаКадры = ОбщегоНазначения.ОбщийМодуль("ИсправлениеДокументовЗарплатаКадры");
			МодульИсправлениеДокументовЗарплатаКадры.СкопироватьДокумент(
				ЭтотОбъект,
				Основание.Ссылка,
				,
				,
				Основание);
		Иначе
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, Основание);
			СсылкаОснования = ОбщегоНазначенияБЗК.ЗначениеСвойства(Основание, "Основание");
			Основание.Свойство("ПричинаНетрудоспособности", ПричинаНетрудоспособности);
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(СсылкаОснования) = Тип("СправочникСсылка.Сотрудники") Тогда
		Сотрудник = СсылкаОснования;
	ИначеЕсли ТипЗнч(СсылкаОснования) = Тип("ДокументСсылка.ВходящийЗапросФССДляРасчетаПособия") Тогда
		ВходящийЗапрос = СсылкаОснования;
	ИначеЕсли Метаданные().Реквизиты.ПервичныйДокумент.Тип.СодержитТип(ТипЗнч(СсылкаОснования))
		И ЗначениеЗаполнено(СсылкаОснования) Тогда
		ПервичныйДокумент = СсылкаОснования;
	КонецЕсли;
	
	ЗначенияДляЗаполнения = Новый Структура("Ответственный");
	ЗарплатаКадры.ПолучитьЗначенияПоУмолчанию(ЗначенияДляЗаполнения);
	
	Если ЗначениеЗаполнено(Сотрудник) Тогда
		КадровыеДанные = КадровыеДанныеСотрудника();
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, КадровыеДанные);
	КонецЕсли;
	Если ЗначениеЗаполнено(ВходящийЗапрос) Тогда
		ЗаполнитьПервичныеДанныеИзВходящегоЗапроса(Ложь);
		ПараметрыФиксации = Документы.ОтветНаЗапросФССДляРасчетаПособия.ПараметрыФиксацииВторичныхДанных();
		ДополнитьДаннымиВходящегоЗапроса(ПараметрыФиксации, Истина);
		ЗаполнитьДанныеОрганизации(ПараметрыФиксации);
		ЗаполнитьКадровыеДанныеСотрудника(ПараметрыФиксации);
	КонецЕсли;
	Если ЗначениеЗаполнено(ПервичныйДокумент) Тогда
		ПараметрыФиксации = Документы.ОтветНаЗапросФССДляРасчетаПособия.ПараметрыФиксацииВторичныхДанных();
		ЗаполнитьДанныеИзПервичногоДокумента(ПараметрыФиксации, Ложь);
	КонецЕсли;
	ЗаполнитьДатуНачалаСобытия(Ложь);
	Если Не ЗначениеЗаполнено(ВидПособия) И ЗначениеЗаполнено(ПричинаНетрудоспособности) Тогда
		ВидПособия = Перечисления.ПособияНазначаемыеФСС.НайтиПоПричинеНетрудоспособности(ПричинаНетрудоспособности);
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ВходящийЗапрос) Тогда
		Если Не ЗначениеЗаполнено(ФизическоеЛицо) И ЗначениеЗаполнено(Сотрудник) Тогда
			ЗаполнитьКадровыеДанныеСотрудника(Ложь);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ГоловнаяОрганизация) И ЗначениеЗаполнено(Организация) Тогда
			ЗаполнитьДанныеОрганизации(Ложь);
		КонецЕсли;
		ЗаполнитьВходящийЗапросПоПервичнымДанным();
	КонецЕсли;
	
	ОбновитьВторичныеДанные();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	ПроверяемыеРеквизиты.Очистить();
	ПроверяемыеРеквизиты.Добавить("Дата");
	ПроверяемыеРеквизиты.Добавить("Организация");
	ПроверяемыеРеквизиты.Добавить("НомерПроцесса");
	ПроверяемыеРеквизиты.Добавить("ВходящийЗапросИдентификатор");
	
	Если ОтказВНазначенииПособия Тогда
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "ОтказВНазначенииПособияПричина");
	Иначе
		ПроверяемыеРеквизиты.Добавить("ВидПособия");
		СведенияОВидеПособия = СЭДОФСС.СведенияОВидеПособия(ВидПособия);
		Особенности = Документы.ОтветНаЗапросФССДляРасчетаПособия.ОсобенностиВыгрузки(ЭтотОбъект);
		Если СведенияОВидеПособия.ЭтоОбычныйБольничный
			Или СведенияОВидеПособия.ЭтоНСПЗ Тогда
			ПроверитьСведенияОбЭЛН(Отказ);
			ПроверитьСведенияОСтрахователе(Отказ, Особенности);
			ПроверитьСведенияОЗастрахованномЛице(Отказ, СведенияОВидеПособия, ПроверяемыеРеквизиты);
			ПроверитьСпособВыплаты(Отказ, ПроверяемыеРеквизиты);
			ПроверитьДанныеДляРасчета(Отказ, СведенияОВидеПособия, ПроверяемыеРеквизиты);
			ПроверитьПараметрыНазначения(Отказ, СведенияОВидеПособия, Особенности);
			ПроверитьДанныеНСПЗ(Отказ, СведенияОВидеПособия);
			ПроверитьИзвещение(Отказ);
		ИначеЕсли СведенияОВидеПособия.ЭтоБольничныйПоБиР Тогда
			ПроверитьСведенияОбЭЛН(Отказ);
			ПроверитьСведенияОСтрахователе(Отказ, Особенности);
			ПроверитьСведенияОЗастрахованномЛице(Отказ, СведенияОВидеПособия, ПроверяемыеРеквизиты);
			ПроверитьСпособВыплаты(Отказ, ПроверяемыеРеквизиты);
			ПроверитьДанныеДляРасчета(Отказ, СведенияОВидеПособия, ПроверяемыеРеквизиты);
			ПроверитьПараметрыНазначения(Отказ, СведенияОВидеПособия, Особенности);
			ПроверитьСведенияОбОтпуске(Отказ);
			ПроверитьИзвещение(Отказ);
		ИначеЕсли СведенияОВидеПособия.ЭтоПособиеПриРождении Тогда
			ПроверитьСведенияОСтрахователе(Отказ, Особенности);
			ПроверитьСведенияОЗастрахованномЛице(Отказ, СведенияОВидеПособия, ПроверяемыеРеквизиты);
			ПроверитьСпособВыплаты(Отказ, ПроверяемыеРеквизиты);
			ПроверитьДанныеДляРасчета(Отказ, СведенияОВидеПособия, ПроверяемыеРеквизиты);
			ПроверитьПараметрыНазначения(Отказ, СведенияОВидеПособия, Особенности);
			ПроверитьИнформациюОбУсыновленииОпеке(Отказ);
			ПроверитьИнформациюОПередачеНаВоспитание(Отказ);
			ПроверитьИнформациюОРебенкеПособияПриРождении(Отказ);
			ПроверитьИнформациюОДругомРодителе(Отказ);
			ПроверитьИзвещение(Отказ);
		ИначеЕсли СведенияОВидеПособия.ЭтоОтпускПоУходу Тогда
			ПроверитьСведенияОСтрахователе(Отказ, Особенности);
			ПроверитьСведенияОЗастрахованномЛице(Отказ, СведенияОВидеПособия, ПроверяемыеРеквизиты);
			ПроверитьСпособВыплаты(Отказ, ПроверяемыеРеквизиты);
			ПроверитьДанныеДляРасчета(Отказ, СведенияОВидеПособия, ПроверяемыеРеквизиты);
			ПроверитьПараметрыНазначения(Отказ, СведенияОВидеПособия, Особенности);
			ПроверитьИнформациюОбУсыновленииОпеке(Отказ);
			ПроверитьИнформациюОРебенкеОтпускаПоУходу(Отказ);
			ПроверитьОснованияДляОтпускаПоУходу(Отказ);
			ПроверитьСправкуОНеполученииПособия(Отказ);
			ПроверитьИзвещение(Отказ);
			ПроверитьПрочиеСведенияОтпускаПоУходу(Отказ);
		КонецЕсли;
		Если Исправление Тогда
			ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "ИсправлениеКодПричины");
		КонецЕсли;
	КонецЕсли;
	Если Перерасчет Тогда
		СЭДОФСС.ПроверитьИдентификаторРеестраПВСО(Отказ, ЭтотОбъект, "ПерерасчетИдентификаторСтрокиРеестра");
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	// Заполнение недостающих полей.
	Если Не ЗначениеЗаполнено(Дата) Тогда
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ДатаСоздания) Тогда
		ДатаСоздания = ТекущаяДата(); // АПК:143 Для фильтрации событий в журнале регистрации требуется дата сервера.
	КонецЕсли;
	
	// Заполнение текста XML.
	ЭтоПроведение = (РежимЗаписи = РежимЗаписиДокумента.Проведение
		Или (Проведен И РежимЗаписи = РежимЗаписиДокумента.Запись));
	Если ЭтоПроведение Тогда
		ТекстXML = Документы.ОтветНаЗапросФССДляРасчетаПособия.ТекстXML(ЭтотОбъект);
	Иначе
		ТекстXML = "";
	КонецЕсли;
	ХранилищеXML = Новый ХранилищеЗначения(ТекстXML, Новый СжатиеДанных(9));
	
	// Чтение реквизитов "До записи" для проверки и подготовки сведений к процедуре ПриЗаписи.
	ЗначенияРеквизитовДоЗаписи = ЗначенияРеквизитовДоЗаписи();
	ДополнительныеСвойства.Вставить("ЗначенияРеквизитовДоЗаписи", ЗначенияРеквизитовДоЗаписи);
	
	// Проверка отсутствия критичных изменений в отправленном документе.
	Если ЗначениеЗаполнено(ИдентификаторСообщения)
		И ЗначениеЗаполнено(ЗначенияРеквизитовДоЗаписи.ФизическоеЛицо)
		И ЗначенияРеквизитовДоЗаписи.ФизическоеЛицо <> ФизическоеЛицо Тогда
		ВызватьИсключение НСтр("ru = 'Недопустимо изменять физическое лицо в отправленном документе.';
								|en = 'You cannot change an individual in the sent document.'");
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	РегистрыСведений.РегистрацииОтветовНаЗапросыФССДляРасчетаПособий.ЗаполнитьПоДокументу(ЭтотОбъект);
	Документы.ВходящийЗапросФССДляРасчетаПособия.ПриЗаписиОтветаНаЗапрос(ЭтотОбъект);
	РегистрыСведений.СведенияОбЭЛН.ПриЗаписиОтветаНаЗапрос(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПередЗаписью

Функция ЗначенияРеквизитовДоЗаписи()
	ИменаРеквизитов = "Дата, НомерЛН, ГоловнаяОрганизация, ФизическоеЛицо, ПометкаУдаления, ВходящийЗапрос, Проведен";
	ЭтоНовый = ЭтоНовый();
	Если ЭтоНовый Тогда
		Значения = ОбщегоНазначенияБЗК.ЗначенияСвойств(ЭтотОбъект, ИменаРеквизитов);
	Иначе
		Значения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, ИменаРеквизитов);
	КонецЕсли;
	Значения.Вставить("ЭтоНовый", ЭтоНовый);
	Возврат Значения;
КонецФункции

#КонецОбласти

#Область ФиксацияВторичныхДанныхВДокументах

Функция ОбновитьВторичныеДанные(ПараметрыФиксации = Неопределено) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	Модифицирован = Ложь;
	
	Если ОбъектЗафиксирован() Тогда
		Возврат Модифицирован;
	КонецЕсли;
	
	Особенности = Документы.ОтветНаЗапросФССДляРасчетаПособия.ОсобенностиВыгрузки(ЭтотОбъект);
	ДополнительныеСвойства.Вставить("Особенности", Особенности);
	
	Если ПараметрыФиксации = Неопределено Тогда
		ПараметрыФиксации = Документы.ОтветНаЗапросФССДляРасчетаПособия.ПараметрыФиксацииВторичныхДанных(ЭтотОбъект);
	КонецЕсли;
	
	ОрганизацияДо = Организация;
	
	Если Не ЗначениеЗаполнено(Дата) Тогда
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Если ЗаполнитьВерсиюСпецификации(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьПервичныеДанныеИзВходящегоЗапроса(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьПервичныйДокумент(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьДатуНачалаСобытия(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьПриоритеты(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьДанныеИзПервичногоДокумента(ПараметрыФиксации, Истина) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьДанныеОрганизации(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если (Не ЗначениеЗаполнено(Составитель) Или ОрганизацияДо <> Организация)
		И Особенности.Версия_2_34 И СЭДОФСС.ЗаполнитьПодписантов(ЭтотОбъект, ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если Особенности.Версия_2_34 И ЗаполнитьКадровыеДанныеСоставителя(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьКадровыеДанныеСотрудника(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьВидДоговора(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьКодСтраныГражданства(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьРазрешениеНаПроживание(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьСпособВыплатыПособия(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьКонтактыФизическогоЛица(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьКодыАдреса(ПараметрыФиксации, "АдресРегистрации", Истина) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьКодыАдреса(ПараметрыФиксации, "АдресПроживания", Ложь) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьКадровыеДанныеРебенка(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьДанныеГПХ(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьПериодДоговора(ПараметрыФиксации, Особенности) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ДополнитьДаннымиВходящегоЗапроса(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьСтатусНалогоплательщика(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьКодСтатусаНалогоплательщика(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьПредставления(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Возврат Модифицирован;
КонецФункции

Функция ОбъектЗафиксирован() Экспорт
	Возврат Документы.ОтветНаЗапросФССДляРасчетаПособия.ОбъектЗафиксирован(ЭтотОбъект);
КонецФункции

Функция ЗаполнитьПервичныеДанныеИзВходящегоЗапроса(ПараметрыФиксации) Экспорт
	Если Не ЗначениеЗаполнено(ВходящийЗапрос) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	РеквизитыЗапроса = ДанныеВходящегоЗапроса();
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("ВходящийЗапросИдентификатор", РеквизитыЗапроса.ИдентификаторСообщения);
	Реквизиты.Вставить("НомерПроцесса",               РеквизитыЗапроса.НомерПроцесса);
	Реквизиты.Вставить("Организация",                 РеквизитыЗапроса.Организация);
	Реквизиты.Вставить("Сотрудник",                   РеквизитыЗапроса.Сотрудник);
	Реквизиты.Вставить("Ребенок",                     РеквизитыЗапроса.Ребенок);
	Реквизиты.Вставить("ИзвещениеФССНомер",           РеквизитыЗапроса.ИзвещениеФССНомер);
	Реквизиты.Вставить("ИзвещениеФССДата",            РеквизитыЗапроса.ИзвещениеФССДата);
	Реквизиты.Вставить("ВидПособия",                  РеквизитыЗапроса.ВидПособия);
	Реквизиты.Вставить("НомерЛН",                     РеквизитыЗапроса.НомерЛН);
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации);
КонецФункции

Функция ЗаполнитьПервичныйДокумент(ПараметрыФиксации)
	ЕстьИзменения = СЭДОФСС.ЗаполнитьПервичныйДокументДляРасчетаПособияФСС(ЭтотОбъект, ПараметрыФиксации);
	Если ЕстьИзменения Тогда
		ДополнительныеСвойства.Удалить("РеквизитыПервичногоДокумента");
		ДополнительныеСвойства.Удалить("ДанныеЗаполняемыеИзПервичногоДокумента");
	КонецЕсли;
	Возврат ЕстьИзменения;
КонецФункции

Функция ЗаполнитьДанныеИзПервичногоДокумента(ПараметрыФиксации, УчитыватьФиксацию)
	Реквизиты = ДанныеЗаполняемыеИзПервичногоДокумента();
	Если Реквизиты = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	Реквизиты = КоллекцииБЗК.СкопироватьСтруктуру(Реквизиты);
	
	ЕстьИзменения = Ложь;
	
	Если АвтозаполнениеПериодовОплаты И Реквизиты.Свойство("ИсключаемыеПериоды") Тогда
		ИсключаемыеПериодыДоИзменения = ИсключаемыеПериоды.Выгрузить();
		ИсключаемыеПериоды.Загрузить(Реквизиты.ИсключаемыеПериоды);
		ИсключаемыеПериоды.Сортировать("ДатаНачала, ДатаОкончания");
		// Исключаемые периоды должны заполняться только по тем дням, которые приходятся на период оплаты за счет ФСС.
		// При взведенном флаге "Сдвигать оплату больничного" отключаем механику исключения периодов, приходящиеся на оплату за счет работодателя.
		Если Не Реквизиты.Свойство("СдвигатьПериодОплаты") Тогда
			Если ЗначениеЗаполнено(ОплатаФССДатаНачала) И ЗначениеЗаполнено(ОплатаФССДатаОкончания) Тогда
				ОбратныйИндекс = ИсключаемыеПериоды.Количество();
				Пока ОбратныйИндекс > 0 Цикл
					ОбратныйИндекс = ОбратныйИндекс - 1;
					ИсключаемыйПериод = ИсключаемыеПериоды[ОбратныйИндекс];
					Если ИсключаемыйПериод.ДатаНачала < ОплатаФССДатаНачала Тогда
						ИсключаемыйПериод.ДатаНачала = ОплатаФССДатаНачала;
					КонецЕсли;
					Если ИсключаемыйПериод.ДатаОкончания > ОплатаФССДатаОкончания Тогда
						ИсключаемыйПериод.ДатаОкончания = ОплатаФССДатаОкончания;
					КонецЕсли;
					Если ИсключаемыйПериод.ДатаНачала > ИсключаемыйПериод.ДатаОкончания Тогда
						ИсключаемыеПериоды.Удалить(ИсключаемыйПериод);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		// Определение наличия изменений в исключаемых периодах.
		Если Не ОбщегоНазначенияБЗК.ЗначенияСовпадают(ИсключаемыеПериодыДоИзменения, ИсключаемыеПериоды.Выгрузить()) Тогда
			ЕстьИзменения = Истина;
		КонецЕсли;
	КонецЕсли;
	Реквизиты.Удалить("ИсключаемыеПериоды");
	
	Если АвтозаполнениеПериодовОплаты И Реквизиты.Свойство("ПериодыПростоя") Тогда
		ПериодыПростояДоИзменения = ПериодыПростоя.Выгрузить();
		ПериодыПростоя.Загрузить(Реквизиты.ПериодыПростоя);
		ПериодыПростоя.Сортировать("ДатаНачала, ДатаОкончания");
		Если Не ОбщегоНазначенияБЗК.ЗначенияСовпадают(ПериодыПростояДоИзменения, ПериодыПростоя.Выгрузить()) Тогда
			ЕстьИзменения = Истина;
		КонецЕсли;
	КонецЕсли;
	Реквизиты.Удалить("ПериодыПростоя");
	
	Если АвтозаполнениеПериодовОплаты И Реквизиты.Свойство("ПриостановленияТрудовыхДоговоров") Тогда
		ДоИзменения = ПриостановленияТрудовыхДоговоров.Выгрузить();
		ПриостановленияТрудовыхДоговоров.Загрузить(Реквизиты.ПриостановленияТрудовыхДоговоров);
		ПриостановленияТрудовыхДоговоров.Сортировать("ДатаНачала, ДатаОкончания");
		Если Не ОбщегоНазначенияБЗК.ЗначенияСовпадают(ДоИзменения, ПриостановленияТрудовыхДоговоров.Выгрузить()) Тогда
			ЕстьИзменения = Истина;
		КонецЕсли;
	КонецЕсли;
	Реквизиты.Удалить("ПриостановленияТрудовыхДоговоров");
	
	Если УчитыватьФиксацию Тогда
		Если ФиксацияВторичныхДанныхВДокументах.РеквизитШапкиЗафиксирован(ЭтотОбъект, "Ребенок")
			Или (Реквизиты.Свойство("Ребенок") И ЗначениеЗаполнено(Реквизиты.Ребенок)) Тогда
			КоллекцииБЗК.УдалитьКлючиСтруктуры(Реквизиты, ИменаРеквизитовЗаполняемыхИзРебенка(ПараметрыФиксации));
		КонецЕсли;
		Если ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации) Тогда
			ЕстьИзменения = Истина;
		КонецЕсли;
	Иначе
		Если ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапкиБезУчетаФиксации(ЭтотОбъект, Реквизиты) Тогда
			ЕстьИзменения = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЕстьИзменения;
КонецФункции

Функция РеквизитыПервичногоДокумента()
	Если Не ЗначениеЗаполнено(ПервичныйДокумент) Тогда
		Возврат Неопределено;
	КонецЕсли;
	Реквизиты = ОбщегоНазначенияБЗК.ЗначениеСвойства(ДополнительныеСвойства, "РеквизитыПервичногоДокумента");
	Если Реквизиты <> Неопределено Тогда
		Возврат Реквизиты;
	КонецЕсли;
	Реквизиты = Новый Структура;
	
	ДанныеДляРасчетаСреднегоЗаработка = Неопределено;
	
	// Получение данных документа.
	МенеджерОснования = ОбщегоНазначения.МенеджерОбъектаПоСсылке(ПервичныйДокумент);
	МенеджерОснования.ЗаполнитьОтветНаЗапросФССДляРасчетаПособия(ПервичныйДокумент, ЭтотОбъект, Реквизиты, ДанныеДляРасчетаСреднегоЗаработка);
	
	// Получение данных среднего заработка.
	Если ДанныеДляРасчетаСреднегоЗаработка <> Неопределено Тогда
		ЗаполнитьРеквизитыРасчетаСреднегоЗаработка(Реквизиты, ДанныеДляРасчетаСреднегоЗаработка);
	КонецЕсли;
	
	// Удаление неподходящих данных.
	Если Реквизиты.Свойство("ДоляВремени")
		И Не (ТипЗнч(Реквизиты.ДоляВремени) = Тип("Число")
			И Реквизиты.ДоляВремени > 0
			И Реквизиты.ДоляВремени <= 1) Тогда
		Реквизиты.Удалить("ДоляВремени");
	КонецЕсли;
	Если Реквизиты.Свойство("РайонныйКоэффициент")
		И Не (ТипЗнч(Реквизиты.РайонныйКоэффициент) = Тип("Число")
			И Реквизиты.РайонныйКоэффициент >= 1) Тогда
		Реквизиты.Удалить("РайонныйКоэффициент");
	КонецЕсли;
	Если Реквизиты.Свойство("ПричинаПредоставленияЛьготы")
		И Не (ТипЗнч(Реквизиты.ПричинаПредоставленияЛьготы) = Тип("ПеречислениеСсылка.ОснованияФинансированияПособияФедеральнымБюджетом")
			И ЗначениеЗаполнено(Реквизиты.ПричинаПредоставленияЛьготы)) Тогда
		Реквизиты.Удалить("ПричинаПредоставленияЛьготы");
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("РеквизитыПервичногоДокумента", Реквизиты);
	Возврат Реквизиты;
КонецФункции

Функция ДанныеЗаполняемыеИзПервичногоДокумента()
	Реквизиты = ОбщегоНазначенияБЗК.ЗначениеСвойства(ДополнительныеСвойства, "ДанныеЗаполняемыеИзПервичногоДокумента");
	Если Реквизиты <> Неопределено Тогда
		Возврат Реквизиты;
	КонецЕсли;
	РеквизитыПервичногоДокумента = РеквизитыПервичногоДокумента();
	Если РеквизитыПервичногоДокумента = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	Реквизиты = КоллекцииБЗК.СкопироватьСтруктуру(РеквизитыПервичногоДокумента);
	
	ДанныеВходящегоЗапроса = ДанныеВходящегоЗапроса();
	Если ДанныеВходящегоЗапроса <> Неопределено Тогда
		Если ЗначениеЗаполнено(ДанныеВходящегоЗапроса.ВидПособия) Тогда
			Реквизиты.Удалить("ВидПособия");
		КонецЕсли;
		ИзмененРасчетныйПериод = СтраховательИзменилРасчетныйПериод(ДанныеВходящегоЗапроса, РеквизитыПервичногоДокумента);
		Особенности = ?(ДополнительныеСвойства.Свойство("Особенности"),
			ДополнительныеСвойства.Особенности, Документы.ОтветНаЗапросФССДляРасчетаПособия.ОсобенностиВыгрузки(ЭтотОбъект));
		
		// При изменении расчетного периода страхователь должен отправить исходный вид заработка и новые годы.
		// В ответ Фонд пришлет новый входящий запрос с новыми годами и новым видом заработка.
		Если ПриоритетУДанныхВходящегоЗапроса Или ИзмененРасчетныйПериод Или Особенности.Версия_2_41 Тогда
			Реквизиты.Удалить("ПервыйРасчетныйГодВидЗаработка");
			Реквизиты.Удалить("ВторойРасчетныйГодВидЗаработка");
		КонецЕсли;
		Если ПриоритетУДанныхВходящегоЗапроса
			И ЗначениеЗаполнено(ДанныеВходящегоЗапроса.ПервыйРасчетныйГод)
			И ЗначениеЗаполнено(ДанныеВходящегоЗапроса.ВторойРасчетныйГод) Тогда
			Реквизиты.Удалить("ПервыйРасчетныйГод");
			Реквизиты.Удалить("ВторойРасчетныйГод");
			Реквизиты.Удалить("ПервыйРасчетныйГодЗаработок");
			Реквизиты.Удалить("ВторойРасчетныйГодЗаработок");
			Реквизиты.Удалить("ЗаменаЛетРасчетногоПериода");
		КонецЕсли;
		Если ПриоритетУДанныхВходящегоЗапроса
			И ЗначениеЗаполнено(ДанныеВходящегоЗапроса.КалендарныхДнейРасчетногоПериода) Тогда
			Реквизиты.Удалить("КалендарныхДнейРасчетногоПериода");
		КонецЕсли;
		Если ИспользоватьСтажИзВходящегоЗапроса(ДанныеВходящегоЗапроса) Тогда
			Реквизиты.Удалить("СтраховойСтажЛет");
			Реквизиты.Удалить("СтраховойСтажМесяцев");
			Реквизиты.Удалить("НестраховойСтажЛет");
			Реквизиты.Удалить("НестраховойСтажМесяцев");
		КонецЕсли;
		Если ИспользоватьУходЗаНесколькимиДетьмиИзВходящегоЗапроса(ДанныеВходящегоЗапроса) Тогда
			Реквизиты.Удалить("УходЗаНесколькимиДетьми");
			Реквизиты.Удалить("СреднемесячныйЗаработок");
		КонецЕсли;
		Если ИспользоватьПериодОплатыСФРИзВходящегоЗапроса(ДанныеВходящегоЗапроса) Тогда
			Реквизиты.Удалить("ОплатаФССДатаНачала");
			Реквизиты.Удалить("ОплатаФССДатаОкончания");
		КонецЕсли;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ДанныеЗаполняемыеИзПервичногоДокумента", Реквизиты);
	Возврат Реквизиты;
КонецФункции

Процедура ЗаполнитьРеквизитыРасчетаСреднегоЗаработка(Реквизиты, ДанныеДляРасчетаСреднегоЗаработка)
	
	ПараметрыРасчета      = ДанныеДляРасчетаСреднегоЗаработка.ПараметрыРасчета;
	ДанныеРасчетаСреднего = ДанныеДляРасчетаСреднегоЗаработка.ДанныеРасчетаСреднего;
	
	Реквизиты.Вставить("ДатаНачалаРасчетногоПериода",    ПараметрыРасчета.ПериодРасчетаСреднегоЗаработкаНачало);
	Реквизиты.Вставить("ДатаОкончанияРасчетногоПериода", ПараметрыРасчета.ПериодРасчетаСреднегоЗаработкаОкончание);
	Реквизиты.Вставить("ПервыйРасчетныйГод",             ПараметрыРасчета.РасчетныеГоды[0]);
	Реквизиты.Вставить("ВторойРасчетныйГод",             ПараметрыРасчета.РасчетныеГоды[1]);
	Реквизиты.Вставить("ЗаменаЛетРасчетногоПериода",     Ложь);
	Реквизиты.Вставить("ПервыйРасчетныйГодЗаработок",    0);
	Реквизиты.Вставить("ВторойРасчетныйГодЗаработок",    0);
	Реквизиты.Вставить("ДоляВремени", УчетПособийСоциальногоСтрахованияКлиентСервер.ДоляНеполногоВремени(ПараметрыРасчета));
	
	Если Реквизиты.ВидПособия = Перечисления.ПособияНазначаемыеФСС.ЕжемесячноеПособиеПоУходуЗаРебенком Тогда
		// Фонд может передать как "ВсеСтрахователи" так и "ВсеСтрахователиЗаТекущийПериодРаботы".
		// На расчет это не влияет, но если изменить, то вернется ошибка.
		// Поэтому для пособий по уходу вид заработка заполняется из входящего запроса.
	ИначеЕсли Не ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыРасширеннаяПодсистемы.ПособияСоциальногоСтрахования") Тогда
		// В базовой реализации нет учета заработка других страхователей.
	Иначе
		Если ПараметрыРасчета.УчитыватьЗаработокПредыдущихСтрахователей Тогда
			ВидЗаработка = Перечисления.СпособыУчетаГодовогоЗаработкаСФР.ВсеСтрахователи;
		Иначе
			ВидЗаработка = Перечисления.СпособыУчетаГодовогоЗаработкаСФР.ТекущийСтрахователь;
		КонецЕсли;
		Реквизиты.Вставить("ПервыйРасчетныйГодВидЗаработка", ВидЗаработка);
		Реквизиты.Вставить("ВторойРасчетныйГодВидЗаработка", ВидЗаработка);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыРасчета.ДатаНачалаСобытия) Тогда
		Реквизиты.Вставить("ДатаНачалаСобытия", ПараметрыРасчета.ДатаНачалаСобытия);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыРасчета.ДатаНачалаСобытия) И ЗначениеЗаполнено(ПараметрыРасчета.ПорядокРасчета) Тогда
		ПериодРасчетаСреднего = УчетПособийСоциальногоСтрахованияКлиентСервер.ПериодРасчетаСреднегоЗаработкаФСС(
			ПараметрыРасчета.ДатаНачалаСобытия,
			ПараметрыРасчета.ПорядокРасчета);
		
		ПервыйГодПоДатеСобытия = Год(ПериодРасчетаСреднего.ДатаНачала);
		ВторойГодПоДатеСобытия = Год(ПериодРасчетаСреднего.ДатаОкончания);
		
		Если ПервыйГодПоДатеСобытия <> Реквизиты.ПервыйРасчетныйГод
			Или ВторойГодПоДатеСобытия <> Реквизиты.ВторойРасчетныйГод Тогда
			Реквизиты.ЗаменаЛетРасчетногоПериода = Истина;
		КонецЕсли;
	КонецЕсли;
	
	// Фонд сам учитыват дни приостановлеения ТД.
	ПараметрыРасчета.УчитыватьДниПриостановленияТД = Ложь;
	УчитываемыхДнейВКалендарныхГодах = УчетПособийСоциальногоСтрахованияКлиентСервер.УчитываемыхДнейВКалендарныхГодахФСС(
		ПараметрыРасчета,
		ДанныеРасчетаСреднего);
	Реквизиты.Вставить("КалендарныхДнейРасчетногоПериода", УчитываемыхДнейВКалендарныхГодах);
	
	СтрокаПоГоду = УчетПособийСоциальногоСтрахованияКлиентСервер.ЭлементКоллекцииПоОтбору(
		ДанныеРасчетаСреднего,
		Новый Структура("РасчетныйГод", Реквизиты.ПервыйРасчетныйГод));
	Если СтрокаПоГоду <> Неопределено Тогда
		Реквизиты.ПервыйРасчетныйГодЗаработок = УчетПособийСоциальногоСтрахованияКлиентСервер.УчитываемыйЗаработокФССПоГоду(
			ПараметрыРасчета,
			СтрокаПоГоду);
	КонецЕсли;
	
	СтрокаПоГоду = УчетПособийСоциальногоСтрахованияКлиентСервер.ЭлементКоллекцииПоОтбору(
		ДанныеРасчетаСреднего,
		Новый Структура("РасчетныйГод", Реквизиты.ВторойРасчетныйГод));
	Если СтрокаПоГоду <> Неопределено Тогда
		Реквизиты.ВторойРасчетныйГодЗаработок = УчетПособийСоциальногоСтрахованияКлиентСервер.УчитываемыйЗаработокФССПоГоду(
			ПараметрыРасчета,
			СтрокаПоГоду);
	КонецЕсли;
	
КонецПроцедуры

Функция СтраховательИзменилРасчетныйПериод(ДанныеВходящегоЗапроса, РеквизитыПервичногоДокумента) Экспорт
	Если РеквизитыПервичногоДокумента.Свойство("ПервыйРасчетныйГод")
		И РеквизитыПервичногоДокумента.Свойство("ВторойРасчетныйГод")
		И ЗначениеЗаполнено(РеквизитыПервичногоДокумента.ПервыйРасчетныйГод)
		И ЗначениеЗаполнено(РеквизитыПервичногоДокумента.ВторойРасчетныйГод) Тогда
		Возврат ДанныеВходящегоЗапроса.ПервыйРасчетныйГод <> РеквизитыПервичногоДокумента.ПервыйРасчетныйГод
			Или ДанныеВходящегоЗапроса.ВторойРасчетныйГод <> РеквизитыПервичногоДокумента.ВторойРасчетныйГод;
	Иначе
		Возврат Ложь;
	КонецЕсли;
КонецФункции

Функция ИспользоватьСтажИзВходящегоЗапроса(ДанныеВходящегоЗапроса)
	Возврат ПриоритетУДанныхВходящегоЗапроса
		И (ЗначениеЗаполнено(ДанныеВходящегоЗапроса.СтраховойСтажЛет)
			Или ЗначениеЗаполнено(ДанныеВходящегоЗапроса.СтраховойСтажМесяцев)
			Или ЗначениеЗаполнено(ДанныеВходящегоЗапроса.НестраховойСтажЛет)
			Или ЗначениеЗаполнено(ДанныеВходящегоЗапроса.НестраховойСтажМесяцев));
КонецФункции

Функция ИспользоватьУходЗаНесколькимиДетьмиИзВходящегоЗапроса(ДанныеВходящегоЗапроса)
	Возврат ПриоритетУДанныхВходящегоЗапроса
		И (ДанныеВходящегоЗапроса.УходЗаНесколькимиДетьми
			Или ЗначениеЗаполнено(ДанныеВходящегоЗапроса.СреднемесячныйЗаработок));
КонецФункции

Функция ИспользоватьПериодОплатыСФРИзВходящегоЗапроса(ДанныеВходящегоЗапроса)
	Возврат ПриоритетУДанныхВходящегоЗапроса
		И ЗначениеЗаполнено(ДанныеВходящегоЗапроса.ОплатаФССДатаНачала)
		И ЗначениеЗаполнено(ДанныеВходящегоЗапроса.ОплатаФССДатаОкончания);
КонецФункции

Функция ЗаполнитьДатуНачалаСобытия(ПараметрыФиксации)
	Реквизиты = Новый Структура("ДатаНачалаСобытия", ДатаНачалаСобытия());
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации);
КонецФункции

Функция ДатаНачалаСобытия()
	Если ЗначениеЗаполнено(ГоловнаяОрганизация) И ЗначениеЗаполнено(НомерЛН) Тогда
		НайденнаяДата = РегистрыСведений.СведенияОбЭЛН.ДатаНачалаСобытия(НомерЛН, ГоловнаяОрганизация);
		Если ЗначениеЗаполнено(НайденнаяДата) Тогда
			Возврат НайденнаяДата;
		КонецЕсли;
	КонецЕсли;
	Если ЗначениеЗаполнено(ПервичныйДокумент) Тогда
		НайденнаяДата = ОбщегоНазначенияБЗК.ЗначениеСвойства(РеквизитыПервичногоДокумента(), "ДатаНачалаСобытия");
		Если ЗначениеЗаполнено(НайденнаяДата) Тогда
			Возврат НайденнаяДата;
		КонецЕсли;
	КонецЕсли;
	Если ЗначениеЗаполнено(ВходящийЗапрос) Тогда
		ДанныеВходящегоЗапроса = ДанныеВходящегоЗапроса();
		Если ДанныеВходящегоЗапроса <> Неопределено Тогда
			Если ЗначениеЗаполнено(ДанныеВходящегоЗапроса.ОтпускПоУходуДатаНачала) Тогда
				Возврат ДанныеВходящегоЗапроса.ОтпускПоУходуДатаНачала;
			ИначеЕсли ЗначениеЗаполнено(ДанныеВходящегоЗапроса.ОтпускПоУходуДата) Тогда
				Возврат ДанныеВходящегоЗапроса.ОтпускПоУходуДата;
			ИначеЕсли ЗначениеЗаполнено(ДанныеВходящегоЗапроса.ОплатаФССДатаНачала) Тогда
				Возврат ДанныеВходящегоЗапроса.ОплатаФССДатаНачала;
			ИначеЕсли ЗначениеЗаполнено(ДанныеВходящегоЗапроса.РебенокДатаРождения) Тогда
				Возврат ДанныеВходящегоЗапроса.РебенокДатаРождения;
			ИначеЕсли ЗначениеЗаполнено(ДанныеВходящегоЗапроса.ДатаСообщения) Тогда
				Возврат ДанныеВходящегоЗапроса.ДатаСообщения;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Если ЗначениеЗаполнено(ОтпускПоУходуДатаНачала) Тогда
		Возврат ОтпускПоУходуДатаНачала;
	ИначеЕсли ЗначениеЗаполнено(ОтпускПоУходуДата) Тогда
		Возврат ОтпускПоУходуДата;
	ИначеЕсли ЗначениеЗаполнено(ОплатаФССДатаНачала) Тогда
		Возврат ОплатаФССДатаНачала;
	КонецЕсли;
	Возврат НачалоДня(Дата);
КонецФункции

Функция ЗаполнитьПриоритеты(ПараметрыФиксации)
	Реквизиты = Новый Структура("ПриоритетУДанныхВходящегоЗапроса");
	
	// Если есть только одна ссылка из двух - то приоритет у тех данных, по которым есть ссылка.
	Если Не ЗначениеЗаполнено(ВходящийЗапрос) Тогда
		ФиксацияВторичныхДанныхВДокументах.ОтменитьФиксациюРеквизитаШапки(ЭтотОбъект, "ПриоритетУДанныхВходящегоЗапроса");
		Реквизиты.ПриоритетУДанныхВходящегоЗапроса = Ложь;
		Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации);
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ПервичныйДокумент) Тогда
		ФиксацияВторичныхДанныхВДокументах.ОтменитьФиксациюРеквизитаШапки(ЭтотОбъект, "ПриоритетУДанныхВходящегоЗапроса");
		Реквизиты.ПриоритетУДанныхВходящегоЗапроса = Истина;
		Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации);
	КонецЕсли;
	
	ДанныеВходящегоЗапроса = ДанныеВходящегоЗапроса();
	
	ДатаВходящегоЗапроса = ДанныеВходящегоЗапроса.ДатаСообщения;
	
	Если ЗначениеЗаполнено(ДатаНачалаСобытия) Тогда
		ДатаСобытия = ДатаНачалаСобытия;
	ИначеЕсли ЗначениеЗаполнено(ВходящийЗапрос) Тогда
		ДатаСобытия = ДатаВходящегоЗапроса;
	ИначеЕсли ЗначениеЗаполнено(Дата) Тогда
		ДатаСобытия = Дата;
	Иначе
		ДатаСобытия = ТекущаяДатаСеанса();
	КонецЕсли;
	
	// Дата когда законодательно появилась возможность запрашивать данные СФР для расчета пособий.
	ДатаНачалаИспользованияДанныхСФР = '20230101';
	// Ориентировочная дата когда во входящем запросе появился достаточный объем сведений для расчета пособий.
	ДатаНачалаИспользованияДанныхСФРДляРасчетаПособий = '20230601';
	
	Если ДатаСобытия < ДатаНачалаИспользованияДанныхСФР
		Или ДатаВходящегоЗапроса < ДатаНачалаИспользованияДанныхСФРДляРасчетаПособий Тогда
		// До 2023 года приоритет был у первичного документа.
		Реквизиты.ПриоритетУДанныхВходящегоЗапроса = Ложь;
	ИначеЕсли Не ЗначениеЗаполнено(ДанныеВходящегоЗапроса.ПервыйРасчетныйГодЗаработок)
		И Не ЗначениеЗаполнено(ДанныеВходящегоЗапроса.ВторойРасчетныйГодЗаработок) Тогда
		// Во входящем запросе недостаточно данных.
		Реквизиты.ПриоритетУДанныхВходящегоЗапроса = Ложь;
	ИначеЕсли СтраховательИзменилРасчетныйПериод(ДанныеВходящегоЗапроса, РеквизитыПервичногоДокумента()) Тогда
		// Расчетный период должен совпадать у Фонда и страхователя.
		Реквизиты.ПриоритетУДанныхВходящегоЗапроса = Ложь;
	Иначе
		// Можно использовать данные входящего запроса.
		Реквизиты.ПриоритетУДанныхВходящегоЗапроса = Истина;
	КонецЕсли;
	
	ЕстьИзменения = ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации);
	Если ЕстьИзменения Тогда
		ДополнительныеСвойства.Удалить("ДанныеЗаполняемыеИзПервичногоДокумента");
	КонецЕсли;
	Возврат ЕстьИзменения;
КонецФункции

Функция ЗаполнитьДанныеОрганизации(ПараметрыФиксации)
	// Головная организация заполняется безусловно, т.к. определяет права.
	ГоловнаяОрганизация = ЗарплатаКадры.ГоловнаяОрганизация(Организация);
	
	Реквизиты = Новый Структура("Страхователь, РегистрационныйНомерСФР, НомерСтрахователяФСС, КодПодчиненностиФСС");
	Если ФиксацияВторичныхДанныхВДокументах.РеквизитыШапкиЗафиксированы(ЭтотОбъект, Реквизиты) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ИменаПолей = "РегНомерСФР, НомерСтрахователяФСС, КодПодчиненностиФСС";
		Сведения = СЭДОФСС.СведенияОСтрахователе(Организация, ИменаПолей, Дата);
		Реквизиты.Страхователь            = Сведения.Страхователь;
		Реквизиты.РегистрационныйНомерСФР = Сведения.РегНомерСФР;
		Реквизиты.НомерСтрахователяФСС    = Сведения.НомерСтрахователяФСС;
		Реквизиты.КодПодчиненностиФСС     = Сведения.КодПодчиненностиФСС;
	КонецЕсли;
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации);
КонецФункции

Функция ЗаполнитьКадровыеДанныеСоставителя(ПараметрыФиксации)
	Реквизиты = Новый Структура("СоставительФИО, СоставительТелефон, СоставительЭлектроннаяПочта");
	Если ФиксацияВторичныхДанныхВДокументах.РеквизитыШапкиЗафиксированы(ЭтотОбъект, Реквизиты) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Составитель) Тогда
		ИменаПолей = "ФИО, ТелефонРабочийПредставление, EMailПредставление";
		КадровыеДанные = КадровыйУчет.КадровыеДанныеФизическогоЛица(Ложь, Составитель, ИменаПолей, ТекущаяДатаСеанса());
		Если КадровыеДанные <> Неопределено Тогда
			Реквизиты.СоставительФИО              = КадровыеДанные.ФИО;
			Реквизиты.СоставительТелефон          = КадровыеДанные.ТелефонРабочийПредставление;
			Реквизиты.СоставительЭлектроннаяПочта = КадровыеДанные.EMailПредставление;
		КонецЕсли;
	КонецЕсли;
	
	Если (Не ЗначениеЗаполнено(Реквизиты.СоставительТелефон)
			Или Не ЗначениеЗаполнено(Реквизиты.СоставительЭлектроннаяПочта))
		И ЗначениеЗаполнено(Организация) Тогда
		ИменаПолей = "ТелОрганизации, АдресЭлектроннойПочтыОрганизации";
		СведенияОбОрганизации = СЭДОФСС.СведенияОСтрахователе(Организация, ИменаПолей, ТекущаяДатаСеанса());
		Если Не ЗначениеЗаполнено(Реквизиты.СоставительТелефон) Тогда
			Реквизиты.СоставительТелефон = СведенияОбОрганизации.ТелОрганизации;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Реквизиты.СоставительЭлектроннаяПочта) Тогда
			Реквизиты.СоставительЭлектроннаяПочта = СведенияОбОрганизации.АдресЭлектроннойПочтыОрганизации;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации);
КонецФункции

Функция ЗаполнитьКадровыеДанныеСотрудника(ПараметрыФиксации)
	Реквизиты = Новый Структура(
	"СотрудникФамилия,
	|СотрудникИмя,
	|СотрудникОтчество,
	|СотрудникДатаРождения,
	|СотрудникГражданство,
	|СотрудникПол,
	|СотрудникСНИЛС,
	|СотрудникИНН,
	|ДоляВремени,
	|РайонныйКоэффициент,
	|УдостоверениеЛичностиВид,
	|УдостоверениеЛичностиСерия,
	|УдостоверениеЛичностиНомер,
	|УдостоверениеЛичностиДатаВыдачи,
	|УдостоверениеЛичностиКемВыдан,
	|УдостоверениеЛичностиСрокДействия,
	|ВнешнееСовместительство,
	|ПричинаПредоставленияЛьготы,
	|ОтношениеКОсобойЗоне,
	|ВидОсобойЗоны");
	ФизическоеЛицо = Неопределено; // Заполняется безусловно, т.к. определяет права.
	
	КадровыеДанные = КадровыеДанныеСотрудника();
	Если КадровыеДанные <> Неопределено Тогда
		ФизическоеЛицо = КадровыеДанные.ФизическоеЛицо; // Заполняется безусловно, т.к. определяет права.
		
		// ФИО, ИНН, СНИЛС, Дата рождения, Гражданство.
		Реквизиты.СотрудникФамилия      = КадровыеДанные.Фамилия;
		Реквизиты.СотрудникИмя          = КадровыеДанные.Имя;
		Реквизиты.СотрудникОтчество     = КадровыеДанные.Отчество;
		Реквизиты.СотрудникГражданство  = КадровыеДанные.Страна;
		Реквизиты.СотрудникДатаРождения = КадровыеДанные.ДатаРождения;
		Реквизиты.СотрудникПол          = КадровыеДанные.Пол;
		Реквизиты.СотрудникСНИЛС        = КадровыеДанные.СтраховойНомерПФР;
		Реквизиты.СотрудникИНН          = КадровыеДанные.ИНН;
		
		// Удостоверение личности.
		Реквизиты.УдостоверениеЛичностиВид          = КадровыеДанные.ДокументВид;
		Реквизиты.УдостоверениеЛичностиСерия        = СокрЛП(КадровыеДанные.ДокументСерия);
		Реквизиты.УдостоверениеЛичностиНомер        = СокрЛП(КадровыеДанные.ДокументНомер);
		Реквизиты.УдостоверениеЛичностиДатаВыдачи   = КадровыеДанные.ДокументДатаВыдачи;
		Реквизиты.УдостоверениеЛичностиКемВыдан     = КадровыеДанные.ДокументКемВыдан;
		Реквизиты.УдостоверениеЛичностиСрокДействия = КадровыеДанные.ДокументСрокДействия;
		
		// Место работы.
		Реквизиты.ВнешнееСовместительство = ЭтоВнешнееСовместительство(КадровыеДанные.ВидЗанятости);
		
		Если КадровыеДанные.Свойство("ДоляНеполногоРабочегоВремени") Тогда
			Реквизиты.ДоляВремени = УчетПособийСоциальногоСтрахования.ДоляНеполногоРабочегоВремени(
				КадровыеДанные.ДоляНеполногоРабочегоВремени);
		КонецЕсли;
		Если ЗначениеЗаполнено(КадровыеДанные.РайонныйКоэффициентРФ) Тогда
			Реквизиты.РайонныйКоэффициент = КадровыеДанные.РайонныйКоэффициентРФ;
		КонецЕсли;
		
		// Льготы.
		Если Реквизиты.Свойство("ПричинаПредоставленияЛьготы") Тогда
			Реквизиты.ПричинаПредоставленияЛьготы = КадровыеДанные.ЛьготаПриНачисленииПособий;
		КонецЕсли;
		Если Реквизиты.Свойство("ОтношениеКОсобойЗоне") Тогда
			Реквизиты.ОтношениеКОсобойЗоне = КадровыеДанные.ОтношениеКОсобойЗоне;
		КонецЕсли;
		Если Реквизиты.Свойство("ВидОсобойЗоны") Тогда
			Реквизиты.ВидОсобойЗоны = КадровыеДанные.ВидОсобойЗоны;
		КонецЕсли;
	КонецЕсли;
	
	Если Реквизиты.РайонныйКоэффициент = Неопределено И ЗначениеЗаполнено(Организация) Тогда
		Реквизиты.РайонныйКоэффициент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "РайонныйКоэффициентРФ");
	КонецЕсли;
	
	Если Реквизиты.ДоляВремени = Неопределено Тогда
		Реквизиты.ДоляВремени = 1;
	Конецесли;
	
	// Удаление полей кадровых данных которые могут заполниться из учетного документа.
	РеквизитыПервичногоДокумента = РеквизитыПервичногоДокумента();
	Если РеквизитыПервичногоДокумента <> Неопределено Тогда
		Для Каждого КлючИЗначение Из Реквизиты Цикл
			Если РеквизитыПервичногоДокумента.Свойство(КлючИЗначение.Ключ) Тогда
				Реквизиты.Удалить(КлючИЗначение.Ключ);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации);
КонецФункции

Функция КадровыеДанныеСотрудника()
	КадровыеДанныеСотрудника = ОбщегоНазначенияБЗК.ЗначениеСвойства(ДополнительныеСвойства, "КадровыеДанные");
	Если КадровыеДанныеСотрудника = Неопределено Тогда
		Если Не ЗначениеЗаполнено(Сотрудник) Тогда
			Возврат Неопределено;
		КонецЕсли;
		ДатаЗапроса = ?(ДополнительныеСвойства.Свойство("РеквизитыЗапроса"),
			ДополнительныеСвойства.РеквизитыЗапроса.Дата, ТекущаяДатаСеанса());
		ИменаПолей = Документы.ОтветНаЗапросФССДляРасчетаПособия.ИменаПолейТребуемыхКадровыхДанных();
		КадровыеДанныеСотрудника = КадровыйУчет.КадровыеДанныеСотрудника(Истина, Сотрудник, ИменаПолей, ДатаЗапроса);
		Если КадровыеДанныеСотрудника = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли;
	Если ТипЗнч(КадровыеДанныеСотрудника) = Тип("Структура") Тогда
		Возврат КадровыеДанныеСотрудника;
	КонецЕсли;
	КадровыеДанные = КадровыеДанныеВСтруктуру(КадровыеДанныеСотрудника);
	ДополнительныеСвойства.Вставить("КадровыеДанные", КадровыеДанные);
	Возврат КадровыеДанные;
КонецФункции

Функция КадровыеДанныеВСтруктуру(КадровыеДанныеСотрудника)
	ИменаПолей = Документы.ОтветНаЗапросФССДляРасчетаПособия.ИменаПолейТребуемыхКадровыхДанных();
	КадровыеДанные = Новый Структура(ИменаПолей);
	ЗаполнитьЗначенияСвойств(КадровыеДанные, КадровыеДанныеСотрудника);
	Возврат КадровыеДанные;
КонецФункции

Функция ЗаполнитьКодСтраныГражданства(ПараметрыФиксации)
	Реквизиты = Новый Структура("КодСтраныГражданства");
	
	Если ЗначениеЗаполнено(СотрудникГражданство) Тогда
		Реквизиты.КодСтраныГражданства = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СотрудникГражданство, "Код");
	КонецЕсли;
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации);
КонецФункции

Функция ЗаполнитьРазрешениеНаПроживание(ПараметрыФиксации)
	Реквизиты = Новый Структура("РазрешениеНаПроживаниеВид, РазрешениеНаПроживаниеСерия, РазрешениеНаПроживаниеНомер,
		|РазрешениеНаПроживаниеДатаВыдачи");
	
	Если ЗначениеЗаполнено(ФизическоеЛицо) И СЭДОФСС.ПоказыватьРазрешениеНаПроживание(КодСтраныГражданства) Тогда
		РазрешениеНаПроживание = Документы.СведенияОЗастрахованномЛицеФСС.НайтиРазрешениеНаПроживание(
			ФизическоеЛицо,
			Дата,
			Документы.ОтветНаЗапросФССДляРасчетаПособия.КодыМВДРазрешенияНаПроживание());
		Реквизиты.РазрешениеНаПроживаниеВид          = РазрешениеНаПроживание.Вид;
		Реквизиты.РазрешениеНаПроживаниеСерия        = СокрЛП(РазрешениеНаПроживание.Серия);
		Реквизиты.РазрешениеНаПроживаниеНомер        = СокрЛП(РазрешениеНаПроживание.Номер);
		Реквизиты.РазрешениеНаПроживаниеДатаВыдачи   = РазрешениеНаПроживание.ДатаВыдачи;
	КонецЕсли;
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации);
КонецФункции

Функция ЭтоВнешнееСовместительство(ВидЗанятости)
	Возврат ВидЗанятости = Перечисления.ВидыЗанятости.Совместительство;
КонецФункции

Функция ЗаполнитьВидДоговора(ПараметрыФиксации)
	Реквизиты = Новый Структура("ВидДоговораТрудовой, ВидДоговораГПХ", Ложь, Ложь);
	
	ТекущийДоговорФизлица = ТекущийДоговорФизлица();
	Если ТекущийДоговорФизлица <> Неопределено Тогда
		Реквизиты.ВидДоговораТрудовой = ТекущийДоговорФизлица.Трудовой;
		Реквизиты.ВидДоговораГПХ      = ТекущийДоговорФизлица.ГПХ;
	КонецЕсли;
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации);
КонецФункции

Функция ТекущийДоговорФизлица()
	Если ДополнительныеСвойства.Свойство("ТекущийДоговорФизлица") Тогда // Служебный кэш, соответствующий страхователю.
		Возврат ДополнительныеСвойства.ТекущийДоговорФизлица;
	КонецЕсли;
	Результат = СЭДОФСС.ТекущийДоговорФизлица(ГоловнаяОрганизация, ФизическоеЛицо, ТекущаяДатаСеанса());
	ДополнительныеСвойства.Вставить("ТекущийДоговорФизлица", Результат);
	Возврат Результат;
КонецФункции

Функция ЗаполнитьСпособВыплатыПособия(ПараметрыФиксации)
	// Получение сведений о способах выплаты.
	Если ЗначениеЗаполнено(Организация) И ЗначениеЗаполнено(ФизическоеЛицо) Тогда
		НастройкиПрямыхВыплат = СпособыПрямыхВыплатФСС.НастройкиСотрудников(Организация, ФизическоеЛицо, Дата);
		СпособПрямыхвыплат    = НастройкиПрямыхВыплат.ТаблицаРезультат[0];
		СпособЗаполнен        = Не (СпособПрямыхвыплат.ОпределеноАвтоматически
			И СпособПрямыхвыплат.Значение = КадровыйУчет.ВидКонтактнойИнформацииАдресМестаПроживанияФизическогоЛица());
	Иначе
		НастройкиПрямыхВыплат = Неопределено;
		СпособПрямыхвыплат    = Неопределено;
		СпособЗаполнен        = Ложь;
	КонецЕсли;
	// Заполнение первичных сведений о способе выплаты.
	ЕстьИзменения = Ложь;
	РеквизитыСпособа = РеквизитыСпособаПрямыхвыплат(СпособПрямыхвыплат);
	Если РеквизитыСпособа.СпособВыплатыПособия = Неопределено Тогда
		СпособЗаполнен = Ложь;
	КонецЕсли;
	Если Не СпособЗаполнен И ЗначениеЗаполнено(ВходящийЗапрос) Тогда
		ДанныеВходящегоЗапроса = ДанныеВходящегоЗапроса();
		Если ЗначениеЗаполнено(ДанныеВходящегоЗапроса.СпособВыплатыПособия) Тогда
			Если ДанныеВходящегоЗапроса.СпособВыплатыПособия = Перечисления.СпособыВыплатыПособия.ИнаяОрганизация Тогда
				РеквизитыСпособа.Вставить("ИнаяОрганизацияБИК");
				РеквизитыСпособа.Вставить("ИнаяОрганизацияИНН");
				РеквизитыСпособа.Вставить("ИнаяОрганизацияКБК");
				РеквизитыСпособа.Вставить("ИнаяОрганизацияКПП");
				РеквизитыСпособа.Вставить("ИнаяОрганизацияЛицевойСчетОрганизации");
				РеквизитыСпособа.Вставить("ИнаяОрганизацияЛицевойСчетСотрудника");
				РеквизитыСпособа.Вставить("ИнаяОрганизацияНаименование");
				РеквизитыСпособа.Вставить("ИнаяОрганизацияНомерСчетаСтрахователя");
				РеквизитыСпособа.Вставить("ИнаяОрганизацияРегистрационныйНомер");
				РеквизитыСпособа.Вставить("ИнаяОрганизацияРегистрационныйНомерСФР");
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(РеквизитыСпособа, ДанныеВходящегоЗапроса);
		КонецЕсли;
	КонецЕсли;
	Если ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(РеквизитыСпособа, ЭтотОбъект, ПараметрыФиксации) Тогда
		ЕстьИзменения = Истина;
	КонецЕсли;
	// Заполнение банковских реквизитов.
	РеквизитыБанка = РеквизитыБанка(НастройкиПрямыхВыплат, СпособПрямыхвыплат);
	Если ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(РеквизитыБанка, ЭтотОбъект, ПараметрыФиксации) Тогда
		ЕстьИзменения = Истина;
	КонецЕсли;
	Возврат ЕстьИзменения;
КонецФункции

Функция РеквизитыСпособаПрямыхвыплат(СпособПрямыхвыплат)
	РеквизитыСпособа = Новый Структура("СпособВыплатыПособия, КартаМИР, Банк, НомерСчета, АдресПроживанияВид");
	Если СпособПрямыхвыплат = Неопределено Тогда
		Возврат РеквизитыСпособа;
	КонецЕсли;
	
	ТипыСпособовПрямыхВыплат = СпособыПрямыхВыплатФСС.ТипыСпособовПрямыхВыплат();
	ТипСпособа = ТипыСпособовПрямыхВыплат.Найти(ТипЗнч(СпособПрямыхвыплат.Значение), "Тип");
	Если ТипСпособа = Неопределено Тогда
		Возврат РеквизитыСпособа;
	КонецЕсли;
	
	РеквизитыСпособа.СпособВыплатыПособия = ТипСпособа.СпособВыплатыПособия;
	Если РеквизитыСпособа.СпособВыплатыПособия = Перечисления.СпособыВыплатыПособия.НаКартуМИР Тогда
		РеквизитыСпособа.КартаМИР           = СпособПрямыхвыплат.Значение;
	ИначеЕсли РеквизитыСпособа.СпособВыплатыПособия = Перечисления.СпособыВыплатыПособия.ЧерезБанк Тогда
		РеквизитыСпособа.Банк               = СпособПрямыхвыплат.Банк;
		РеквизитыСпособа.НомерСчета         = СпособПрямыхвыплат.НомерСчета;
	Иначе
		РеквизитыСпособа.АдресПроживанияВид = СпособПрямыхвыплат.Значение;
	КонецЕсли;
	
	Возврат РеквизитыСпособа;
КонецФункции

Функция РеквизитыБанка(НастройкиПрямыхВыплат, СпособПрямыхвыплат)
	РеквизитыБанка = Новый Структура("БанкНаименование, БанкБИК, БанкКоррСчет");
	Если Не ЗначениеЗаполнено(Банк) Тогда
		Возврат РеквизитыБанка;
	КонецЕсли;
	
	// Определение способа прямых выплат по выбранному банку.
	Если НастройкиПрямыхВыплат <> Неопределено
		И (СпособПрямыхвыплат = Неопределено Или Банк <> СпособПрямыхвыплат.Банк) Тогда
		Найденные = НастройкиПрямыхВыплат.БанковскиеРеквизиты.НайтиСтроки(Новый Структура("Банк, ПометкаУдаления", Банк, Ложь));
		Если Найденные.Количество() = 0 Тогда
			Найденные = НастройкиПрямыхВыплат.БанковскиеРеквизиты.НайтиСтроки(Новый Структура("Банк", Банк));
		КонецЕсли;
		Если Найденные.Количество() = 0 Тогда
			СпособПрямыхвыплат = Неопределено;
		Иначе
			СпособПрямыхвыплат = Найденные[0];
		КонецЕсли;
	КонецЕсли;
	
	// Определение реквизитов банка.
	Если СпособПрямыхвыплат = Неопределено Тогда
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Банк, "Наименование, Код, КоррСчет");
		РеквизитыБанка.БанкНаименование = Реквизиты.Наименование;
		РеквизитыБанка.БанкБИК          = Реквизиты.Код;
		РеквизитыБанка.БанкКоррСчет     = Реквизиты.КоррСчет;
	Иначе
		РеквизитыБанка.БанкНаименование = СпособПрямыхвыплат.НаименованиеБанка;
		РеквизитыБанка.БанкБИК          = СпособПрямыхвыплат.БИК;
		РеквизитыБанка.БанкКоррСчет     = СпособПрямыхвыплат.КоррСчет;
	КонецЕсли;
	
	Возврат РеквизитыБанка;
КонецФункции

Функция ЗаполнитьКонтактыФизическогоЛица(ПараметрыФиксации)
	Реквизиты = Новый Структура("АдресРегистрации, АдресПроживания, СотрудникТелефон");
	
	Если ЗначениеЗаполнено(ФизическоеЛицо) Тогда
		АдресРегистрацииВид = КадровыйУчет.ВидКонтактнойИнформацииАдресПоПропискеФизическиеЛица();
		ТелефонДомашнийВид  = КадровыйУчет.ВидКонтактнойИнформацииТелефонДомашнийФизическиеЛица();
		ТелефонМобильныйВид = КадровыйУчет.ВидКонтактнойИнформацииТелефонМобильныйФизическиеЛица();
		
		ВидыКИ = Новый Массив;
		ВидыКИ.Добавить(АдресРегистрацииВид);
		Если ЗначениеЗаполнено(АдресПроживанияВид) Тогда
			ВидыКИ.Добавить(АдресПроживанияВид);
		КонецЕсли;
		ВидыКИ.Добавить(ТелефонДомашнийВид);
		ВидыКИ.Добавить(ТелефонМобильныйВид);
		
		ТаблицаКИ = КонтактнаяИнформацияБЗК.КонтактнаяИнформацияОбъектов(ФизическоеЛицо, , ВидыКИ);
		
		СтрокаКИ = ТаблицаКИ.Найти(АдресРегистрацииВид, "Вид");
		Если СтрокаКИ <> Неопределено Тогда
			Реквизиты.АдресРегистрации = СтрокаКИ.Значение;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(АдресПроживанияВид) Тогда
			СтрокаКИ = ТаблицаКИ.Найти(АдресПроживанияВид, "Вид");
			Если СтрокаКИ <> Неопределено Тогда
				Реквизиты.АдресПроживания = СтрокаКИ.Значение;
			КонецЕсли;
		КонецЕсли;
		
		СтрокаКИ = ТаблицаКИ.Найти(ТелефонМобильныйВид, "Вид");
		Если СтрокаКИ <> Неопределено Тогда
			Реквизиты.СотрудникТелефон = СтрокаКИ.Значение;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Реквизиты.СотрудникТелефон) Тогда
			СтрокаКИ = ТаблицаКИ.Найти(ТелефонДомашнийВид, "Вид");
			Если СтрокаКИ <> Неопределено Тогда
				Реквизиты.СотрудникТелефон = СтрокаКИ.Значение;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации, Истина);
КонецФункции

Функция ЗаполнитьКодыАдреса(ПараметрыФиксации, ИмяПоляАдрес, ЕстьПолеЗаПределамиРФ)
	Суффиксы = "Индекс, Регион, Район, НаселенныйПункт, Улица, Дом, Корпус, Квартира, ФИАСДом, ФИАСРО";
	Если ЕстьПолеЗаПределамиРФ Тогда
		Суффиксы = Суффиксы + ", ЗаПределамиРФ";
	КонецЕсли;
	Массив = СтрРазделить(Суффиксы, ", ", Ложь);
	ИменаРеквизитов = ИмяПоляАдрес + СтрСоединить(Массив, ", " + ИмяПоляАдрес);
	Реквизиты = Новый Структура(ИменаРеквизитов);
	
	Адрес = ЭтотОбъект[ИмяПоляАдрес];
	Если Не ЗначениеЗаполнено(Адрес) Тогда
		Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации);
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("БезПредставлений",               Не ЕстьПолеЗаПределамиРФ);
	ДополнительныеПараметры.Вставить("КодыАдреса",                     Истина);
	ДополнительныеПараметры.Вставить("КодыКЛАДР",                      Ложь);
	ДополнительныеПараметры.Вставить("НаименованиеВключаетСокращение", Истина);
	ДополнительныеПараметры.Вставить("ПроверитьАдрес",                 Ложь);
	СведенияОбАдресе = РаботаСАдресами.СведенияОбАдресе(Адрес, ДополнительныеПараметры);
	
	ИменаПолей = "Индекс, Регион, Район, НаселенныйПункт, Улица, Дом, Корпус, Строение, Сооружение, Участок, Квартира";
	СтруктураАдреса = КонтактнаяИнформацияБЗК.СвернутьСведенияОбАдресе(СведенияОбАдресе, ИменаПолей);
	СтруктураАдреса.Корпус = КонтактнаяИнформацияБЗК.ПредставлениеКорпусов(СтруктураАдреса, 30);
	СтруктураАдреса.Удалить("Строение");
	СтруктураАдреса.Удалить("Сооружение");
	СтруктураАдреса.Удалить("Участок");
	
	СтруктураАдреса.Вставить("ФИАСДом", Строка(СведенияОбАдресе.ИдентификаторДома));
	СтруктураАдреса.Вставить("ФИАСРО",  Строка(СведенияОбАдресе.ИдентификаторАдресногоОбъекта));
	Если Не ЗначениеЗаполнено(СтруктураАдреса.ФИАСДом)
		И СтруктураАдреса.ФИАСРО <> СведенияОбАдресе.ИдентификаторЗемельногоУчастка Тогда
		СтруктураАдреса.ФИАСДом = Строка(СведенияОбАдресе.ИдентификаторЗемельногоУчастка);
	КонецЕсли;
	
	Если ЕстьПолеЗаПределамиРФ Тогда
		Если СведенияОбАдресе.КодСтраны = "643" Тогда
			ЗаПределамиРФ = "";
		Иначе
			ЗаПределамиРФ = СведенияОбАдресе.Представление;
		КонецЕсли;
		СтруктураАдреса.Вставить("ЗаПределамиРФ", ЗаПределамиРФ);
	КонецЕсли;
	
	Для Каждого КлючИЗначение Из СтруктураАдреса Цикл
		Реквизиты[ИмяПоляАдрес + КлючИЗначение.Ключ] = КлючИЗначение.Значение;
	КонецЦикла;
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации);
КонецФункции

Функция ЗаполнитьКадровыеДанныеРебенка(ПараметрыФиксации)
	Реквизиты = Новый Структура(ИменаРеквизитовЗаполняемыхИзРебенка(ПараметрыФиксации));
	
	Если ЗначениеЗаполнено(Ребенок) Тогда
		КадровыеДанныеРебенка = СЭДОФСС.КадровыеДанныеРодственника(Ребенок, Истина);
		Реквизиты.РебенокФамилия      = КадровыеДанныеРебенка.Фамилия;
		Реквизиты.РебенокИмя          = КадровыеДанныеРебенка.Имя;
		Реквизиты.РебенокОтчество     = КадровыеДанныеРебенка.Отчество;
		Реквизиты.РебенокДатаРождения = КадровыеДанныеРебенка.ДатаРождения;
		Реквизиты.РебенокСНИЛС        = КадровыеДанныеРебенка.СНИЛС;
		Реквизиты.РебенокКодСвязи     = КадровыеДанныеРебенка.КодСвязи;
		Реквизиты.РебенокПол          = КадровыеДанныеРебенка.Пол;
	ИначеЕсли ЗначениеЗаполнено(ПервичныйДокумент) Или ЗначениеЗаполнено(ВходящийЗапрос) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации);
КонецФункции

Функция ЗаполнитьКодСтатусаНалогоплательщика(ПараметрыФиксации)
	Если ФиксацияВторичныхДанныхВДокументах.РеквизитШапкиЗафиксирован(ЭтотОбъект, "КодСтатусаНалогоплательщика") Тогда
		Возврат Ложь;
	КонецЕсли;
	Реквизиты = Новый Структура("КодСтатусаНалогоплательщика");
	Если ЗначениеЗаполнено(СтатусНалогоплательщика) Тогда
		Реквизиты.КодСтатусаНалогоплательщика = Формат(СЭДОФСС.КодСтатусаНалогоплательщика(СтатусНалогоплательщика), "ЧН=");
	КонецЕсли;
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации);
КонецФункции

Функция ИменаРеквизитовЗаполняемыхИзРебенка(ПараметрыФиксации)
	ИменаПолей = ФиксацияВторичныхДанныхВДокументахКлиентСервер.ИменаПолейПоОснованиямЗаполнения(ПараметрыФиксации);
	Возврат СтрСоединить(ИменаПолей.Ребенок, ", ");
КонецФункции

Процедура ЗаполнитьВходящийЗапросПоПервичнымДанным()
	Если Не ЗначениеЗаполнено(ГоловнаяОрганизация) Или Не ЗначениеЗаполнено(ФизическоеЛицо) Тогда
		Возврат;
	КонецЕсли;
	ЕстьПервичныйДокумент = ЗначениеЗаполнено(ПервичныйДокумент);
	ЕстьНомерЛН           = ЗначениеЗаполнено(НомерЛН);
	Если Не ЕстьПервичныйДокумент И Не ЕстьНомерЛН Тогда
		Возврат;
	КонецЕсли;
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВходящийЗапросФСС.Ссылка КАК Ссылка,
	|	ВходящийЗапросФСС.ПометкаУдаления КАК ПометкаУдаления
	|ИЗ
	|	Документ.ВходящийЗапросФССДляРасчетаПособия КАК ВходящийЗапросФСС
	|ГДЕ
	|	ВходящийЗапросФСС.ГоловнаяОрганизация = &ГоловнаяОрганизация
	|	И ВходящийЗапросФСС.ФизическоеЛицо = &ФизическоеЛицо
	|	И ВходящийЗапросФСС.ПервичныйДокумент = &ПервичныйДокумент
	|	И ВходящийЗапросФСС.НомерЛН = &НомерЛН
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПометкаУдаления";
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", ГоловнаяОрганизация);
	Запрос.УстановитьПараметр("ФизическоеЛицо", ФизическоеЛицо);
	Если ЕстьПервичныйДокумент Тогда
		Запрос.УстановитьПараметр("ПервичныйДокумент", ПервичныйДокумент);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И ВходящийЗапросФСС.НомерЛН = &НомерЛН", "");
	ИначеЕсли ЕстьНомерЛН Тогда
		Запрос.УстановитьПараметр("НомерЛН", НомерЛН);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И ВходящийЗапросФСС.ПервичныйДокумент = &ПервичныйДокумент", "");
	Иначе
		Возврат;
	КонецЕсли;
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ВходящийЗапрос = Выборка.Ссылка;
	КонецЕсли;
КонецПроцедуры

Функция ЗаполнитьВерсиюСпецификации(ПараметрыФиксации)
	
	Реквизиты = Новый Структура("ВерсияСпецификации");
	
	ПоддерживаемыеВерсии = Документы.ОтветНаЗапросФССДляРасчетаПособия.ПоддерживаемыеВерсииСпецификации();
	ПоследняяВерсия = ПоддерживаемыеВерсии[ПоддерживаемыеВерсии.Количество() - 1].Значение;
	
	Если ЗначениеЗаполнено(ВходящийЗапрос) Тогда
		РеквизитыЗапроса = ДанныеВходящегоЗапроса();
		Если РеквизитыЗапроса.ВерсияСпецификации = "" Тогда // Версия запроса = 2.20.
			Реквизиты.ВерсияСпецификации = ""; // Версия ответа = 2.20.
		ИначеЕсли (РеквизитыЗапроса.ВерсияСпецификации = "2.34" Или Реквизиты.ВерсияСпецификации = "2.36")
			И Не СЭДОФСС.НачатПереходНаВерсиюПроактива_2_41() Тогда
			Реквизиты.ВерсияСпецификации = "2.36";
		Иначе
			Реквизиты.ВерсияСпецификации = ПоследняяВерсия;
		КонецЕсли;
	Иначе
		Если Не СЭДОФСС.НачатПереходНаВерсиюПроактива_2_41() Тогда
			Реквизиты.ВерсияСпецификации = "2.36";
		Иначе
			Реквизиты.ВерсияСпецификации = ПоследняяВерсия;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации);
КонецФункции

Функция ЗаполнитьДанныеГПХ(ПараметрыФиксации)
	ДанныеГПХ = ДанныеГПХ();
	Если ДанныеГПХ = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(ДанныеГПХ, ЭтотОбъект, ПараметрыФиксации);
КонецФункции

Функция ДанныеГПХ()
	РасширенныеДанные = РасширенныеДанные();
	Если Не РасширенныеДанные.Свойство("ДоговорГПХОблагаетсяВзносамиНС") Тогда
		Возврат Неопределено;
	КонецЕсли;
	Реквизиты = Новый Структура("ДоговорГПХОблагаетсяВзносамиНС", РасширенныеДанные.ДоговорГПХОблагаетсяВзносамиНС);
	Возврат Реквизиты;
КонецФункции

Функция РасширенныеДанные()
	РасширенныеДанные = Неопределено;
	Если ДополнительныеСвойства.Свойство("РасширенныеДанные", РасширенныеДанные) Тогда
		Возврат РасширенныеДанные;
	КонецЕсли;
	РасширенныеДанные = СЭДОФСС.ОтветНаЗапросРасширенныеДанные(ЭтотОбъект);
	ДополнительныеСвойства.Вставить("РасширенныеДанные", РасширенныеДанные);
	Возврат РасширенныеДанные;
КонецФункции

Функция ДополнитьДаннымиВходящегоЗапроса(ПараметрыФиксации, Принудительно = Ложь)
	Если Не ЗначениеЗаполнено(ВходящийЗапрос) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Реквизиты = Новый Структура;
	РеквизитыЗапроса = ДанныеВходящегоЗапроса();
	
	ДанныеПервичногоДокумента = ?(Принудительно, Неопределено, ДанныеЗаполняемыеИзПервичногоДокумента());
	КадровыеДанные            = ?(Принудительно, Неопределено, КадровыеДанныеСотрудника());
	ДанныеГПХ                 = ?(Принудительно, Неопределено, ДанныеГПХ());
	
	УжеЗаполненныеДанные = ДанныеПервичногоДокумента;
	Если КадровыеДанные <> Неопределено Тогда
		Если УжеЗаполненныеДанные = Неопределено Тогда
			УжеЗаполненныеДанные = КадровыеДанные;
		Иначе
			Если УжеЗаполненныеДанные = ДанныеПервичногоДокумента Тогда
				УжеЗаполненныеДанные = КоллекцииБЗК.СкопироватьСтруктуру(УжеЗаполненныеДанные);
			КонецЕсли;
			ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(УжеЗаполненныеДанные, КадровыеДанные, Ложь);
		КонецЕсли;
	КонецЕсли;
	Если ДанныеГПХ <> Неопределено Тогда
		Если УжеЗаполненныеДанные = Неопределено Тогда
			УжеЗаполненныеДанные = ДанныеГПХ;
		Иначе
			Если УжеЗаполненныеДанные = ДанныеПервичногоДокумента Тогда
				УжеЗаполненныеДанные = КоллекцииБЗК.СкопироватьСтруктуру(УжеЗаполненныеДанные);
			КонецЕсли;
			ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(УжеЗаполненныеДанные, ДанныеГПХ, Ложь);
		КонецЕсли;
	КонецЕсли;
	
	ИменаПолей = "АктВПроизвольнойФормеДатаВыдачи, АктВПроизвольнойФормеНомер";
	ДополнитьСтруктуруДаннымиВходящегоЗапроса(Реквизиты, РеквизитыЗапроса, ИменаПолей, УжеЗаполненныеДанные);
	
	ИменаПолей = "АктН1ДатаВыдачи, АктН1Номер";
	ДополнитьСтруктуруДаннымиВходящегоЗапроса(Реквизиты, РеквизитыЗапроса, ИменаПолей, УжеЗаполненныеДанные);
	
	ИменаПолей = "АктоваяЗаписьОРожденииДата, АктоваяЗаписьОРожденииНомер";
	ДополнитьСтруктуруДаннымиВходящегоЗапроса(Реквизиты, РеквизитыЗапроса, ИменаПолей, УжеЗаполненныеДанные);
	
	ИменаПолей = "АктПЗДатаВыдачи";
	ДополнитьСтруктуруДаннымиВходящегоЗапроса(Реквизиты, РеквизитыЗапроса, ИменаПолей, УжеЗаполненныеДанные);
	
	ИменаПолей = "ВидПособия";
	ДополнитьСтруктуруДаннымиВходящегоЗапроса(Реквизиты, РеквизитыЗапроса, ИменаПолей, УжеЗаполненныеДанные);
	
	ИменаПолей = "ДоговорГПХОблагаетсяВзносамиНС";
	ДополнитьСтруктуруДаннымиВходящегоЗапроса(Реквизиты, РеквизитыЗапроса, ИменаПолей, УжеЗаполненныеДанные);
	
	ИменаПолей = "ДругойРодительОтсутствует, СправкаОтДругогоРодителяНомер, СправкаОтДругогоРодителяДата";
	ДополнитьСтруктуруДаннымиВходящегоЗапроса(Реквизиты, РеквизитыЗапроса, ИменаПолей, УжеЗаполненныеДанные);
	
	ИменаПолей = "ЗаменаПослеродовогоОтпускаДата, ЗаменаПослеродовогоОтпускаНомер";
	ДополнитьСтруктуруДаннымиВходящегоЗапроса(Реквизиты, РеквизитыЗапроса, ИменаПолей, УжеЗаполненныеДанные);
	
	ИменаПолей = "ИнойДокументОРожденииДатаВыдачи, ИнойДокументОРожденииСерияНомер, ИнойДокументОРожденииСерия, ИнойДокументОРожденииНомер";
	Заполнено = ДополнитьСтруктуруДаннымиВходящегоЗапроса(Реквизиты, РеквизитыЗапроса, ИменаПолей, УжеЗаполненныеДанные);
	Если Заполнено
		И ЗначениеЗаполнено(Реквизиты.ИнойДокументОРожденииСерияНомер)
		И Не ЗначениеЗаполнено(Реквизиты.ИнойДокументОРожденииСерия)
		И Не ЗначениеЗаполнено(Реквизиты.ИнойДокументОРожденииНомер) Тогда
		Реквизиты.ИнойДокументОРожденииНомер      = Реквизиты.ИнойДокументОРожденииСерияНомер;
		Реквизиты.ИнойДокументОРожденииСерияНомер = "";
	КонецЕсли;
	
	ИменаПолей = "НаличиеДокументовОРожденииПредыдущихДетей";
	ДополнитьСтруктуруДаннымиВходящегоЗапроса(Реквизиты, РеквизитыЗапроса, ИменаПолей, УжеЗаполненныеДанные);
	
	ИменаПолей = "НаличиеРешенияСудаОЛишенииПрав";
	ДополнитьСтруктуруДаннымиВходящегоЗапроса(Реквизиты, РеквизитыЗапроса, ИменаПолей, УжеЗаполненныеДанные);
	
	ИменаПолей = "НаличиеСвидетельстваОСмертиПредыдущихДетей";
	ДополнитьСтруктуруДаннымиВходящегоЗапроса(Реквизиты, РеквизитыЗапроса, ИменаПолей, УжеЗаполненныеДанные);
	
	ИменаПолей = "ОплатаФССДатаНачала, ОплатаФССДатаОкончания";
	ДополнитьСтруктуруДаннымиВходящегоЗапроса(Реквизиты, РеквизитыЗапроса, ИменаПолей, УжеЗаполненныеДанные);
	
	ИменаПолей = "ОтпускПоУходуДата, ОтпускПоУходуДатаНачала, ОтпускПоУходуДатаОкончания, ОтпускПоУходуНомер";
	ДополнитьСтруктуруДаннымиВходящегоЗапроса(Реквизиты, РеквизитыЗапроса, ИменаПолей, УжеЗаполненныеДанные);
	
	ИменаПолей = "ПередачаНаВоспитаниеДата, ПередачаНаВоспитаниеНомер";
	ДополнитьСтруктуруДаннымиВходящегоЗапроса(Реквизиты, РеквизитыЗапроса, ИменаПолей, УжеЗаполненныеДанные);
	
	ИменаПолей = "ПризнаниеСлучаяСтраховым";
	ДополнитьСтруктуруДаннымиВходящегоЗапроса(Реквизиты, РеквизитыЗапроса, ИменаПолей, УжеЗаполненныеДанные);
	
	Если Не ЗначениеЗаполнено(Ребенок) Тогда
		ИменаПолей = "РебенокФамилия, РебенокИмя, РебенокОтчество";
		ДополнитьСтруктуруДаннымиВходящегоЗапроса(Реквизиты, РеквизитыЗапроса, ИменаПолей, УжеЗаполненныеДанные);
		ИменаПолей = "РебенокДатаРождения, РебенокСНИЛС, РебенокКодСвязи, РебенокОчередность, РебенокПол";
		ДополнитьСтруктуруДаннымиВходящегоЗапроса(Реквизиты, РеквизитыЗапроса, ИменаПолей, УжеЗаполненныеДанные);
	КонецЕсли;
	
	ИменаПолей = "РешениеСудаНомер, РешениеСудаДатаВыдачи";
	ДополнитьСтруктуруДаннымиВходящегоЗапроса(Реквизиты, РеквизитыЗапроса, ИменаПолей, УжеЗаполненныеДанные);
	
	ИменаПолей = "УтратаТрудоспобности";
	ДополнитьСтруктуруДаннымиВходящегоЗапроса(Реквизиты, РеквизитыЗапроса, ИменаПолей, УжеЗаполненныеДанные);
	
	ИменаПолей = "СвидетельствоОРожденииДатаВыдачи, СвидетельствоОРожденииСерияНомер, СвидетельствоОРожденииСерия, СвидетельствоОРожденииНомер";
	ДополнитьСтруктуруДаннымиВходящегоЗапроса(Реквизиты, РеквизитыЗапроса, ИменаПолей, УжеЗаполненныеДанные);
	
	ИменаПолей = "СотрудникКатегория";
	ДополнитьСтруктуруДаннымиВходящегоЗапроса(Реквизиты, РеквизитыЗапроса, ИменаПолей, УжеЗаполненныеДанные);
	
	ИменаПолей = "СправкаОНеполученииПособияОтМатериДатаВыдачи, СправкаОНеполученииПособияОтМатериНомер";
	ДополнитьСтруктуруДаннымиВходящегоЗапроса(Реквизиты, РеквизитыЗапроса, ИменаПолей, УжеЗаполненныеДанные);
	
	ИменаПолей = "СправкаОНеполученииПособияОтОтцаДатаВыдачи, СправкаОНеполученииПособияОтОтцаНомер";
	ДополнитьСтруктуруДаннымиВходящегоЗапроса(Реквизиты, РеквизитыЗапроса, ИменаПолей, УжеЗаполненныеДанные);
	
	ИменаПолей = "СправкаОНеполученииПособияПоДругомуМестуРаботыДатаВыдачи, СправкаОНеполученииПособияПоДругомуМестуРаботыНомер";
	ДополнитьСтруктуруДаннымиВходящегоЗапроса(Реквизиты, РеквизитыЗапроса, ИменаПолей, УжеЗаполненныеДанные);
	
	ИменаПолей = "СправкаОРожденииДатаВыдачи, СправкаОРожденииНомер, СправкаОРожденииФорма";
	ДополнитьСтруктуруДаннымиВходящегоЗапроса(Реквизиты, РеквизитыЗапроса, ИменаПолей, УжеЗаполненныеДанные);
	
	Если ЗначениеЗаполнено(РеквизитыЗапроса.ДоляВремени)
		И (КадровыеДанные = Неопределено Или Не КадровыеДанные.Свойство("ДоляНеполногоРабочегоВремени")) Тогда
		ИменаПолей = "ДоляВремени";
		ДополнитьСтруктуруДаннымиВходящегоЗапроса(Реквизиты, РеквизитыЗапроса, ИменаПолей, УжеЗаполненныеДанные);
	КонецЕсли;
	
	ВидЗаработкаСовпадает = Ложь;
	ИменаПолей = "ПервыйРасчетныйГодВидЗаработка, ВторойРасчетныйГодВидЗаработка";
	Если ДополнитьСтруктуруДаннымиВходящегоЗапроса(Реквизиты, РеквизитыЗапроса, ИменаПолей, УжеЗаполненныеДанные) Тогда
		ВидЗаработкаСовпадает = Истина;
	Иначе
		ВидЗаработкаСовпадает = 
			ЗначенияСвойстваСовпадают(РеквизитыЗапроса, УжеЗаполненныеДанные, "ПервыйРасчетныйГодВидЗаработка")
			И ЗначенияСвойстваСовпадают(РеквизитыЗапроса, УжеЗаполненныеДанные, "ВторойРасчетныйГодВидЗаработка");
	КонецЕсли;
	
	РасчетныеГодыСовпадают = Ложь;
	Если ЗначениеЗаполнено(РеквизитыЗапроса.ПервыйРасчетныйГод)
		И ЗначениеЗаполнено(РеквизитыЗапроса.ВторойРасчетныйГод) Тогда
		ИменаПолей = "ПервыйРасчетныйГод, ВторойРасчетныйГод, ЗаменаЛетРасчетногоПериода";
		Если ДополнитьСтруктуруДаннымиВходящегоЗапроса(Реквизиты, РеквизитыЗапроса, ИменаПолей, УжеЗаполненныеДанные) Тогда
			РасчетныеГодыСовпадают = Истина;
		Иначе
			РасчетныеГодыСовпадают = 
				ЗначенияСвойстваСовпадают(РеквизитыЗапроса, УжеЗаполненныеДанные, "ПервыйРасчетныйГод")
				И ЗначенияСвойстваСовпадают(РеквизитыЗапроса, УжеЗаполненныеДанные, "ВторойРасчетныйГод");
		КонецЕсли;
	КонецЕсли;
	
	Если ВидЗаработкаСовпадает И РасчетныеГодыСовпадают Тогда
		ИменаПолей = "ПервыйРасчетныйГодЗаработок, ВторойРасчетныйГодЗаработок";
		ДополнитьСтруктуруДаннымиВходящегоЗапроса(Реквизиты, РеквизитыЗапроса, ИменаПолей, УжеЗаполненныеДанные);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РеквизитыЗапроса.СтраховойСтажЛет)
		Или ЗначениеЗаполнено(РеквизитыЗапроса.СтраховойСтажМесяцев)
		Или ЗначениеЗаполнено(РеквизитыЗапроса.НестраховойСтажЛет)
		Или ЗначениеЗаполнено(РеквизитыЗапроса.НестраховойСтажМесяцев) Тогда
		ИменаПолей = "СтраховойСтажЛет, СтраховойСтажМесяцев, НестраховойСтажЛет, НестраховойСтажМесяцев";
		ДополнитьСтруктуруДаннымиВходящегоЗапроса(Реквизиты, РеквизитыЗапроса, ИменаПолей, УжеЗаполненныеДанные);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РеквизитыЗапроса.КалендарныхДнейРасчетногоПериода) Тогда
		ИменаПолей = "КалендарныхДнейРасчетногоПериода";
		ДополнитьСтруктуруДаннымиВходящегоЗапроса(Реквизиты, РеквизитыЗапроса, ИменаПолей, УжеЗаполненныеДанные);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РеквизитыЗапроса.СреднемесячныйЗаработок) Тогда
		ИменаПолей = "СреднемесячныйЗаработок";
		ДополнитьСтруктуруДаннымиВходящегоЗапроса(Реквизиты, РеквизитыЗапроса, ИменаПолей, УжеЗаполненныеДанные);
	КонецЕсли;
	
	ИменаПолей = "УсыновлениеОпекаТип, УсыновлениеОпекаНомер, УсыновлениеОпекаДата";
	ДополнитьСтруктуруДаннымиВходящегоЗапроса(Реквизиты, РеквизитыЗапроса, ИменаПолей, УжеЗаполненныеДанные);
	
	ИменаПолей = "УходЗаНесколькимиДетьми";
	ДополнитьСтруктуруДаннымиВходящегоЗапроса(Реквизиты, РеквизитыЗапроса, ИменаПолей, УжеЗаполненныеДанные);
	
	ИменаПолей = "Перерасчет, ПерерасчетИдентификаторСтрокиРеестра";
	ДополнитьСтруктуруДаннымиВходящегоЗапроса(Реквизиты, РеквизитыЗапроса, ИменаПолей, УжеЗаполненныеДанные);
	
	ИменаПолей = "ЧислитсяУНесколькихСтрахователей, СоставСтрахователейЗаДваГода, МетодНазначенияИВыплатыПособия";
	ДополнитьСтруктуруДаннымиВходящегоЗапроса(Реквизиты, РеквизитыЗапроса, ИменаПолей, УжеЗаполненныеДанные);
	
	ИменаПолей = "ВходящийСрокОтвета";
	ДополнитьСтруктуруДаннымиВходящегоЗапроса(Реквизиты, РеквизитыЗапроса, ИменаПолей, УжеЗаполненныеДанные);

	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации);
КонецФункции

Функция ДополнитьСтруктуруДаннымиВходящегоЗапроса(Реквизиты, РеквизитыЗапроса, ИменаПолей, УжеЗаполненныеДанные = Неопределено)
	СтруктураСвойств = Новый Структура(ИменаПолей);
	// Проверка отсутствия любого из реквизитов в первичном документе.
	Если УжеЗаполненныеДанные <> Неопределено Тогда
		Для Каждого КлючИЗначение Из СтруктураСвойств Цикл
			Если УжеЗаполненныеДанные.Свойство(КлючИЗначение.Ключ) Тогда
				Возврат Ложь;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	// Вставка с контролем наличия реквизита во входящем запросе.
	Для Каждого КлючИЗначение Из СтруктураСвойств Цикл
		Реквизиты.Вставить(КлючИЗначение.Ключ, РеквизитыЗапроса[КлючИЗначение.Ключ]);
	КонецЦикла;
	Возврат Истина;
КонецФункции

Функция ДанныеВходящегоЗапроса()
	РеквизитыЗапроса = ОбщегоНазначенияБЗК.ЗначениеСвойства(ДополнительныеСвойства, "РеквизитыЗапроса");
	Если РеквизитыЗапроса <> Неопределено Тогда
		Возврат РеквизитыЗапроса;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ВходящийЗапрос) Тогда
		Возврат Неопределено;
	КонецЕсли;
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ * ИЗ Документ.ВходящийЗапросФССДляРасчетаПособия ГДЕ Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", ВходящийЗапрос);
	РеквизитыЗапроса = Запрос.Выполнить().Выгрузить()[0];
	ДополнительныеСвойства.Вставить("РеквизитыЗапроса", РеквизитыЗапроса);
	Возврат РеквизитыЗапроса;
КонецФункции

Функция ЗаполнитьПериодДоговора(ПараметрыФиксации, Особенности)
	Реквизиты = Новый Структура(
	"ТрудовойДоговорДатаНачала,
	|ТрудовойДоговорДатаОкончания");
	
	КадровыеДанные = КадровыеДанныеСотрудника();
	РеквизитыЗапроса = ДанныеВходящегоЗапроса();
	
	Если Не Особенности.Версия_2_41 Тогда // Версия спецификации < 2.41.
		Если ЕстьКадровыеДанныеИЕстьРасширеннаяРеализация(КадровыеДанные) Тогда
			Если КадровыеДанные.СрочныйДоговор = Истина // Может быть Null для непринятых сотрудников.
				И ПериодТрудовогоДоговораСоответствуетКоду46(КадровыеДанные.ДатаНачала, КадровыеДанные.ДатаОкончания) Тогда
				Реквизиты.ТрудовойДоговорДатаНачала    = КадровыеДанные.ДатаНачала;
				Реквизиты.ТрудовойДоговорДатаОкончания = КадровыеДанные.ДатаОкончания;
			КонецЕсли;
		Иначе
			Если РеквизитыЗапроса <> Неопределено И ПериодТрудовогоДоговораСоответствуетКоду46(
				РеквизитыЗапроса.ТрудовойДоговорДатаНачала,
				РеквизитыЗапроса.ТрудовойДоговорДатаОкончания) Тогда
				Реквизиты.ТрудовойДоговорДатаНачала    = РеквизитыЗапроса.ТрудовойДоговорДатаНачала;
				Реквизиты.ТрудовойДоговорДатаОкончания = РеквизитыЗапроса.ТрудовойДоговорДатаОкончания;
			КонецЕсли;
		КонецЕсли;
	Иначе // Версия спецификации >= 2.41.
		// Период договора определяется по имеющимся в учете данным.
		СрочныйДоговор = Неопределено;
		Если ЕстьКадровыеДанныеИЕстьРасширеннаяРеализация(КадровыеДанные)
			И ЗначениеЗаполнено(КадровыеДанные.ДатаНачала)
			И КадровыеДанные.ДатаНачала <= ДатаНачалаСобытия Тогда
			СрочныйДоговор = ОбщегоНазначенияБЗК.ЗначениеСвойства(КадровыеДанные, "СрочныйДоговор");
			Реквизиты.ТрудовойДоговорДатаНачала    = КадровыеДанные.ДатаНачала;
			Реквизиты.ТрудовойДоговорДатаОкончания = КадровыеДанные.ДатаОкончания;
		Иначе
			Договор = СЭДОФСС.ДоговорФизлицаНаДатуСобытия(ГоловнаяОрганизация, ФизическоеЛицо, ДатаНачалаСобытия);
			Если Договор <> Неопределено
				И ЗначениеЗаполнено(Договор.Начало)
				И Договор.Начало <= ДатаНачалаСобытия Тогда
				Реквизиты.ТрудовойДоговорДатаНачала    = Договор.Начало;
				Реквизиты.ТрудовойДоговорДатаОкончания = Договор.Окончание;
			ИначеЕсли РеквизитыЗапроса <> Неопределено
				И ЗначениеЗаполнено(РеквизитыЗапроса.ТрудовойДоговорДатаНачала)
				И РеквизитыЗапроса.ТрудовойДоговорДатаНачала <= ДатаНачалаСобытия Тогда
				Реквизиты.ТрудовойДоговорДатаНачала    = РеквизитыЗапроса.ТрудовойДоговорДатаНачала;
				Реквизиты.ТрудовойДоговорДатаОкончания = РеквизитыЗапроса.ТрудовойДоговорДатаОкончания;
			КонецЕсли;
		КонецЕсли;
		// Период договора проверяется и очищается в некоторых случаях.
		// Для ТД период договора должен заполняться только если это срочный ТД
		// на срок менее шести месяцев (условие исчисления "46").
		// Для ГПХ период договора должен заполняться всегда.
		Если ВидДоговораТрудовой Тогда
			Если ТипЗнч(СрочныйДоговор) <> Тип("Булево") Тогда
				СрочныйДоговор = ЗначениеЗаполнено(Реквизиты.ТрудовойДоговорДатаОкончания);
			КонецЕсли;
			Если Не СрочныйДоговор Или Не ПериодТрудовогоДоговораСоответствуетКоду46(
					Реквизиты.ТрудовойДоговорДатаНачала,
					Реквизиты.ТрудовойДоговорДатаОкончания) Тогда
				Реквизиты.ТрудовойДоговорДатаНачала    = '00010101';
				Реквизиты.ТрудовойДоговорДатаОкончания = '00010101';
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации);
КонецФункции

Функция ПериодТрудовогоДоговораСоответствуетКоду46(ДатаНачалаДоговора, ДатаОкончанияДоговора)
	// ДатаНачалаСобытия может быть пустой.
	Возврат ЗначениеЗаполнено(ДатаНачалаДоговора)
		И ЗначениеЗаполнено(ДатаОкончанияДоговора)
		И ДатаОкончанияДоговора >= ДатаНачалаСобытия
		И ДобавитьМесяц(ДатаНачалаДоговора, 6) >= ДатаОкончанияДоговора;
КонецФункции

Функция ЕстьКадровыеДанныеИЕстьРасширеннаяРеализация(КадровыеДанные)
	Возврат КадровыеДанные <> Неопределено И КадровыеДанные.Свойство("СрочныйДоговор");
КонецФункции

Функция ЗаполнитьПредставления(ПараметрыФиксации)
	Если ФиксацияВторичныхДанныхВДокументах.РеквизитШапкиЗафиксирован(ЭтотОбъект, "СправкаОНеполученииПособияПредставление") Тогда
		Возврат Ложь;
	КонецЕсли;
	Реквизиты = Новый Структура("СправкаОНеполученииПособияПредставление");
	Реквизиты.СправкаОНеполученииПособияПредставление = ПредставлениеСправокОНеполученииПособия();
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации);
КонецФункции

Функция ПредставлениеСправокОНеполученииПособия()
	Представления = Новый Массив;
	
	НомерДата = СЭДОФСС.ПредставлениеНомераИДаты(
		СправкаОНеполученииПособияОтМатериНомер,
		СправкаОНеполученииПособияОтМатериДатаВыдачи);
	Если НомерДата <> "" Тогда
		Представления.Добавить(НСтр("ru = 'Справка о неполучении пособия от матери:';
									|en = 'Certificate of allowance non-receipt from mother:'") +" "+ НомерДата);
	КонецЕсли;
	
	НомерДата = СЭДОФСС.ПредставлениеНомераИДаты(
		СправкаОНеполученииПособияОтОтцаНомер,
		СправкаОНеполученииПособияОтОтцаДатаВыдачи);
	Если НомерДата <> "" Тогда
		Представления.Добавить(НСтр("ru = 'Справка о неполучении пособия от отца:';
									|en = 'Certificate of allowance non-receipt from father:'") +" "+ НомерДата);
	КонецЕсли;
	
	НомерДата = СЭДОФСС.ПредставлениеНомераИДаты(
		СправкаОНеполученииПособияПоДругомуМестуРаботыНомер,
		СправкаОНеполученииПособияПоДругомуМестуРаботыДатаВыдачи);
	Если НомерДата <> "" Тогда
		Представления.Добавить(НСтр("ru = 'Справка о неполучении пособия по другому месту работы:';
									|en = 'Certificate of allowance non-receipt in other workplaces:'") +" "+ НомерДата);
	КонецЕсли;
	
	Если Представления.Количество() > 1 Тогда
		Возврат СтрСоединить(Представления, ";" + Символы.ПС) + ".";
	Иначе
		Возврат ""; // Не требуется выгружать в примечание, т.к. справка всего одна - она попадет в тег refNpay.
	КонецЕсли;
КонецФункции

Функция ЗначенияСвойстваСовпадают(ЛеваяКоллекция, ПраваяКоллекция, ИмяСвойства)
	Возврат ОбщегоНазначенияБЗК.ЗначениеСвойства(ЛеваяКоллекция, ИмяСвойства)
		= ОбщегоНазначенияБЗК.ЗначениеСвойства(ПраваяКоллекция, ИмяСвойства);
КонецФункции

Функция ЗаполнитьСтатусНалогоплательщика(ПараметрыФиксации)
	Если ФиксацияВторичныхДанныхВДокументах.РеквизитШапкиЗафиксирован(ЭтотОбъект, "СтатусНалогоплательщика") Тогда
		Возврат Ложь;
	КонецЕсли;
	Реквизиты = Новый Структура("СтатусНалогоплательщика");
	КадровыеДанные = КадровыеДанныеСотрудника();
	Если КадровыеДанные <> Неопределено И ЗначениеЗаполнено(КадровыеДанные.СтатусНалогоплательщика) Тогда
		Реквизиты.СтатусНалогоплательщика = КадровыеДанные.СтатусНалогоплательщика;
	Иначе
		РеквизитыЗапроса = ДанныеВходящегоЗапроса();
		Если РеквизитыЗапроса <> Неопределено И ЗначениеЗаполнено(РеквизитыЗапроса.КодСтатусаНалогоплательщика) Тогда
			Реквизиты.СтатусНалогоплательщика = СЭДОФСС.СтатусНалогоплательщика(РеквизитыЗапроса.КодСтатусаНалогоплательщика);
		КонецЕсли;
	КонецЕсли;
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации);
КонецФункции

#КонецОбласти

#Область ОбработкаПроверкиЗаполнения

Процедура ПроверитьСведенияОбЭЛН(Отказ)
	ПроверкиБЗК.ПроверитьНомерЛН(Отказ, ЭтотОбъект, "НомерЛН");
КонецПроцедуры

Процедура ПроверитьСведенияОСтрахователе(Отказ, Особенности)
	Если Особенности.Версия_2_41 Тогда
		ПроверитьНомерСтрахователяФСС    = ЗначениеЗаполнено(НомерСтрахователяФСС);
		ПроверитьРегистрационныйНомерСФР = ЗначениеЗаполнено(РегистрационныйНомерСФР) Или Не ПроверитьНомерСтрахователяФСС;
	Иначе
		ПроверитьНомерСтрахователяФСС    = Истина;
		ПроверитьРегистрационныйНомерСФР = Ложь;
	КонецЕсли;
	Если ПроверитьНомерСтрахователяФСС Тогда
		ПроверкиБЗК.ПроверитьРегистрационныйНомерФСС(Отказ, ЭтотОбъект, "НомерСтрахователяФСС");
	КонецЕсли;
	Если ПроверитьРегистрационныйНомерСФР Тогда
		ПроверкиБЗК.ПроверитьРегистрационныйНомерСФР(Отказ, ЭтотОбъект, "РегистрационныйНомерСФР");
	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьСведенияОЗастрахованномЛице(Отказ, СведенияОВидеПособия, ПроверяемыеРеквизиты)
	ЭтоРезидент = (КодСтраныГражданства = "643");
	
	ПроверяемыеРеквизиты.Добавить("СотрудникФамилия");
	ПроверяемыеРеквизиты.Добавить("СотрудникИмя");
	ПроверяемыеРеквизиты.Добавить("СотрудникКатегория");
	
	// Категория застрахованного лица.
	ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "СотрудникКатегория");
	
	// СНИЛС.
	Если Не ЗначениеЗаполнено(СотрудникСНИЛС) Тогда
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "СотрудникСНИЛС");
	Иначе
		ТекстОшибки = "";
		Если Не РегламентированныеДанныеКлиентСервер.СтраховойНомерПФРСоответствуетТребованиям(СотрудникСНИЛС, ТекстОшибки) Тогда
			Текст = НСтр("ru = 'Ошибка в СНИЛС %1: %2';
						|en = 'Error in SNILS %1: %2'");
			Текст = СтрШаблон(Текст, СотрудникСНИЛС, ТекстОшибки);
			СообщенияБЗК.СообщитьОбОшибкеВОбъекте(Отказ, ЭтотОбъект, Текст, "СотрудникСНИЛС");
		КонецЕсли;
	КонецЕсли;
	
	// ИНН.
	Если ЗначениеЗаполнено(СотрудникИНН) Тогда
		ТекстОшибки = "";
		Если Не РегламентированныеДанныеКлиентСервер.ИННСоответствуетТребованиям(СотрудникИНН, Ложь, ТекстОшибки) Тогда
			Текст = НСтр("ru = 'Ошибка в ИНН %1: %2';
						|en = 'Error in TIN %1: %2'");
			Текст = СтрШаблон(Текст, СотрудникИНН, ТекстОшибки);
			СообщенияБЗК.СообщитьОбОшибкеВОбъекте(Отказ, ЭтотОбъект, Текст, "СотрудникИНН");
		КонецЕсли;
	КонецЕсли;
	
	// ФИО.
	ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "СотрудникФамилия");
	ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "СотрудникИмя");
	
	// Дата рождения, Пол.
	ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "СотрудникДатаРождения");
	ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "СотрудникПол");
	
	// Код статуса налогоплательщика, код страны гражданства.
	ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "КодСтатусаНалогоплательщика");
	ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "КодСтраныГражданства");
	
	// Адрес регистрации.
	Если ЗначениеЗаполнено(АдресРегистрацииФИАСДом) Или ЗначениеЗаполнено(АдресРегистрацииФИАСРО) Тогда
		ПроверитьАдрес(Отказ, "АдресРегистрации");
	КонецЕсли;
	Если Не ЭтоРезидент И Не ЗначениеЗаполнено(АдресРегистрации) Тогда
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "АдресРегистрацииЗаПределамиРФ");
	КонецЕсли;
	
	// Удостоверение личности.
	ИменаРеквизитов = Новый Структура;
	ИменаРеквизитов.Вставить("Сотрудник",           "Сотрудник");
	ИменаРеквизитов.Вставить("ФизическоеЛицо",      "ФизическоеЛицо");
	ИменаРеквизитов.Вставить("ВидДокумента",        "УдостоверениеЛичностиВид");
	ИменаРеквизитов.Вставить("СерияДокумента",      "УдостоверениеЛичностиСерия");
	ИменаРеквизитов.Вставить("НомерДокумента",      "УдостоверениеЛичностиНомер");
	ИменаРеквизитов.Вставить("ДатаВыдачиДокумента", "УдостоверениеЛичностиДатаВыдачи");
	ИменаРеквизитов.Вставить("КемВыданДокумент",    "УдостоверениеЛичностиКемВыдан");
	ПрямыеВыплатыПособийСоциальногоСтрахования.ПроверитьДокументУдостоверяющийЛичность(Отказ, ЭтотОбъект, ИменаРеквизитов);
	
	СведенияОВидеДокумента = СЭДОФССПовтИсп.СведенияОВидеДокумента(УдостоверениеЛичностиВид);
	Если ЗначениеЗаполнено(УдостоверениеЛичностиВид) И Не ЗначениеЗаполнено(СведенияОВидеДокумента.КодМВД) Тогда
		Текст = НСтр("ru = 'Не заполнен код вида документа, удостоверяющего личность';
					|en = 'Identity document code is not filled'");
		СообщенияБЗК.СообщитьОбОшибкеВОбъекте(Отказ, УдостоверениеЛичностиВид, Текст, "КодМВД");
	КонецЕсли;
	Если ЗначениеЗаполнено(УдостоверениеЛичностиВид)
		И Не ЗначениеЗаполнено(УдостоверениеЛичностиСрокДействия)
		И Документы.СведенияОЗастрахованномЛицеФСС.ЭтоВременноеУдостоверениеЛичности(СведенияОВидеДокумента.КодМВД) Тогда
		Текст = НСтр("ru = 'Не заполнен срок действия документа, удостоверяющего личность';
					|en = 'Identity document validity period is not filled'");
		СообщенияБЗК.СообщитьОбОшибкеВОбъекте(Отказ, ЭтотОбъект, Текст, "УдостоверениеЛичностиСрокДействия");
	ИначеЕсли ЗначениеЗаполнено(УдостоверениеЛичностиСрокДействия)
		И УдостоверениеЛичностиСрокДействия < ?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса()) Тогда
		Текст = НСтр("ru = 'Истек срок действия документа, удостоверяющего личность (%1)';
					|en = 'Identity document is expired (%1)'");
		Текст = СтрШаблон(Текст, Формат(УдостоверениеЛичностиСрокДействия, "ДЛФ=D"));
		СообщенияБЗК.СообщитьОбОшибкеВОбъекте(Отказ, ЭтотОбъект, Текст, "УдостоверениеЛичностиСрокДействия");
	КонецЕсли;
	
	// Сведения о документах, подтверждающих постоянное или временное проживание (пребывание) на территории РФ
	// (для иностранных граждан и лиц без гражданства).
	ПроверитьРазрешениеНаПроживание(Отказ);
	
	// Срочный трудовой договор.
	Если (СведенияОВидеПособия.ЭтоОбычныйБольничный
		Или СведенияОВидеПособия.ЭтоНСПЗ
		Или СведенияОВидеПособия.ЭтоПособиеПриРождении) Тогда
		Если ЗначениеЗаполнено(ТрудовойДоговорДатаОкончания) Тогда
			ТекстОшибки = ПроверкиБЗККлиентСервер.ТекстОшибкиЗаполненияПериода(ТрудовойДоговорДатаНачала, ТрудовойДоговорДатаОкончания);
			Если ЗначениеЗаполнено(ТекстОшибки) Тогда
				ТекстОшибки = СтрШаблон(ТекстОшибки, НСтр("ru = 'срочного трудового договора';
															|en = 'fixed-term employment contract'"));
				СообщенияБЗК.СообщитьОбОшибкеВОбъекте(Отказ, ЭтотОбъект, ТекстОшибки, "ТрудовойДоговорДатаНачала");
			КонецЕсли;
		КонецЕсли;
		Если ЗначениеЗаполнено(ТрудовойДоговорДатаНачала) И ЗначениеЗаполнено(ТрудовойДоговорДатаОкончания) Тогда
			Если ДатаНачалаСобытия > ТрудовойДоговорДатаОкончания Тогда
				Текст = НСтр("ru = 'Срочный трудовой договор закончился (%1) до начала страхового случая (%2)';
							|en = 'Fixed-term employment contract ended (%1) before the insured event happened (%2)'");
				Текст = СтрШаблон(Текст, Формат(ТрудовойДоговорДатаОкончания, "ДЛФ=D"), Формат(ДатаНачалаСобытия, "ДЛФ=D"));
				СообщенияБЗК.СообщитьОбОшибкеВОбъекте(Отказ, ЭтотОбъект, Текст, "ТрудовойДоговорДатаОкончания");
			КонецЕсли;
			Если Не УчетПособийСоциальногоСтрахования.ИспользуетсяУсловиеИсчисления(ЭтотОбъект, "46")
				И ТрудовойДоговорДатаОкончания <= ДобавитьМесяц(ТрудовойДоговорДатаНачала, 6) Тогда
				Текст = НСтр("ru = 'ERR_PR_1007: При указании срочного трудового договора менее 6 месяцев необходимо заполнить условия исчисления: код 46';
							|en = 'ERR_PR_1007: При указании срочного трудового договора менее 6 месяцев необходимо заполнить условия исчисления: код 46'");
				ИмяРеквизита = ?(Не ЗначениеЗаполнено(УсловияИсчисленияКод1), "УсловияИсчисленияКод1",
							   ?(Не ЗначениеЗаполнено(УсловияИсчисленияКод2), "УсловияИсчисленияКод2", "УсловияИсчисленияКод3"));
				// Выводим предупреждение без отказа
				СообщенияБЗК.СообщитьОбОшибкеВОбъекте(Неопределено, ЭтотОбъект, Текст, ИмяРеквизита);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если СведенияОВидеПособия.ЭтоПособиеПриРождении Или СведенияОВидеПособия.ЭтоОтпускПоУходу Тогда
		ПроверяемыеРеквизиты.Добавить("РебенокКодСвязи");
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьСпособВыплаты(Отказ, ПроверяемыеРеквизиты)
	
	Если СпособВыплатыПособия = Перечисления.СпособыВыплатыПособия.ПочтовымПереводом Тогда
		ПроверитьАдрес(Отказ, "АдресПроживания");
	ИначеЕсли СпособВыплатыПособия = Перечисления.СпособыВыплатыПособия.НаКартуМИР Тогда
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "КартаМИР");
	ИначеЕсли СпособВыплатыПособия = Перечисления.СпособыВыплатыПособия.ЧерезБанк Тогда
		Если ЗначениеЗаполнено(БанкБИК) И ЗначениеЗаполнено(НомерСчета) Тогда
			РезультатПроверки = ПроверкиБЗК.РезультатПроверкиНомераСчета(НомерСчета, БанкБИК, БанкКоррСчет);
			Если Не РезультатПроверки.Успех Тогда
				СообщенияБЗК.СообщитьОбОшибкеВОбъекте(Отказ, ЭтотОбъект, РезультатПроверки.Пояснение, "НомерСчета");
			КонецЕсли;
		Иначе
			ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "Банк");
			ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "БанкБИК");
			ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "НомерСчета");
		КонецЕсли;
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "БанкНаименование");
	ИначеЕсли СпособВыплатыПособия = Перечисления.СпособыВыплатыПособия.ИнаяОрганизация Тогда
		ПроверяемыеРеквизиты.Добавить("ИнаяОрганизацияНаименование");
		ПроверяемыеРеквизиты.Добавить("ИнаяОрганизацияНомерСчетаСтрахователя");
		Если СотрудникКатегория <> "PRISONER" Тогда
			Текст = СтрШаблон(
				НСтр("ru = 'Способ выплаты пособия ""%1"" доступен только для застрахованных лиц осужденных к лишению свободы.';
					|en = 'The ""%1"" allowance payment method is only available for insured persons sentenced to imprisonment.'"),
				СпособВыплатыПособия);
			// По какой-то причине платформа не позиционирует сообщение на поле переключателя "СпособВыплатыПособия", 
			// поэтому сообщение привязывается к следующему за ним полю ввода "ИнаяОрганизацияНаименование".
			СообщенияБЗК.СообщитьОбОшибкеВОбъекте(Отказ, ЭтотОбъект, Текст, "ИнаяОрганизацияНаименование");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьДанныеДляРасчета(Отказ, СведенияОВидеПособия, ПроверяемыеРеквизиты)
	
	Если СведенияОВидеПособия.ЭтоОбычныйБольничный
		Или СведенияОВидеПособия.ЭтоНСПЗ
		Или СведенияОВидеПособия.ЭтоБольничныйПоБиР Тогда
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "КалендарныхДнейРасчетногоПериода");
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "ПервыйРасчетныйГод");
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "ВторойРасчетныйГод");
	КонецЕсли;
	
	// Районный коэффициент.
	СЭДОФСС.ПроверитьРайонныйКоэффициент(Отказ, ЭтотОбъект, "РайонныйКоэффициент", Ложь);
	
	// ERR_PR_1029: Значение поля sumType недопустимо для редактирования.
	ДанныеВходящегоЗапроса = ДанныеВходящегоЗапроса();
	Если ДанныеВходящегоЗапроса <> Неопределено Тогда
		Если ЗначениеЗаполнено(ДанныеВходящегоЗапроса.ПервыйРасчетныйГодВидЗаработка)
			И ДанныеВходящегоЗапроса.ПервыйРасчетныйГодВидЗаработка <> ПервыйРасчетныйГодВидЗаработка Тогда
			Текст = НСтр("ru = 'Изменен вид заработка первого года по сравнению со входящим запросом.';
						|en = 'The first year earnings kind is changed comparing to the incoming request.'");
			СообщенияБЗК.СообщитьОбОшибкеВОбъекте(Отказ, ЭтотОбъект, Текст, "ПервыйРасчетныйГодВидЗаработка");
		КонецЕсли;
		Если ЗначениеЗаполнено(ДанныеВходящегоЗапроса.ВторойРасчетныйГодВидЗаработка)
			И ДанныеВходящегоЗапроса.ВторойРасчетныйГодВидЗаработка <> ВторойРасчетныйГодВидЗаработка Тогда
			Текст = НСтр("ru = 'Изменен вид заработка второго года по сравнению со входящим запросом.';
						|en = 'The second year earnings kind is changed comparing to the incoming request.'");
			СообщенияБЗК.СообщитьОбОшибкеВОбъекте(Отказ, ЭтотОбъект, Текст, "ВторойРасчетныйГодВидЗаработка");
		КонецЕсли;
	КонецЕсли;
	
	Если СведенияОВидеПособия.ЭтоОбычныйБольничный
		Или СведенияОВидеПособия.ЭтоНСПЗ Тогда
		
		// Период за который начисляется пособие ФСС.
		ПроверкиБЗК.ПроверитьПериод(
			Отказ,
			ЭтотОбъект,
			"ОплатаФССДатаНачала",
			"ОплатаФССДатаОкончания",
			НСтр("ru = 'оплаты за счет ФСС';
				|en = 'payments out of SSF funds'"),
			Ложь);
		
		// Периоды за которые не начисляется пособие за счет ФСС.
		ПроверитьИсключаемыеПериоды(Отказ, ПроверяемыеРеквизиты);
		
		// Периоды простоя.
		ПроверитьПериодыПростоя(Отказ, ПроверяемыеРеквизиты);
		
		// Приостановления трудовых договоров.
		ПроверитьПриостановленияТД(Отказ, ПроверяемыеРеквизиты);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьПараметрыНазначения(Отказ, СведенияОВидеПособия, Особенности)
	Если Не Особенности.Версия_2_34 Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Перерасчет Тогда
		
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "МетодНазначенияИВыплатыПособия");
		
		// Из спецификации: Поле ВыборПодтвержденЗастрахованнымЛицом обязательно для заполнения
		// если застрахованное лицо трудоустроено по трудовому договору, занято у нескольких страхователей,
		// а также метод назначения пособия "По одному из страхователей по трудовому договору".
		Если СведенияОВидеПособия.ЭтоОбычныйБольничный
			Или СведенияОВидеПособия.ЭтоБольничныйПоБиР
			Или СведенияОВидеПособия.ЭтоОтпускПоУходу Тогда
			Если Не ВыборПодтвержденЗастрахованнымЛицом
				И ВидДоговораТрудовой
				И ЧислитсяУНесколькихСтрахователей
				И (МетодНазначенияИВыплатыПособия = Перечисления.МетодыНазначенияИВыплатыПособийСФР.ПоОдномуСтрахователю
					Или МетодНазначенияИВыплатыПособия = Перечисления.МетодыНазначенияИВыплатыПособийСФР.ПоОдномуСтрахователюПоТД) Тогда
				Текст = НСтр("ru = 'Поскольку сотрудник числится у нескольких страхователей, то необходимо получить потверждение сотрудника на использование метода назначения пособия ""%1"" и включить соответствующий флажок';
							|en = 'As the employee is registered with multiple insurants, obtain the employee''s confirmation to use the %1 method to grant the allowance and select the respective check box'");
				Текст = СтрШаблон(Текст, МетодНазначенияИВыплатыПособия);
				СообщенияБЗК.СообщитьОбОшибкеВОбъекте(Отказ, ЭтотОбъект, Текст, "ВыборПодтвержденЗастрахованнымЛицом");
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если СведенияОВидеПособия.ЭтоОбычныйБольничный
		Или СведенияОВидеПособия.ЭтоБольничныйПоБиР Тогда
		Если Не ВидДоговораТрудовой И Не ВидДоговораГПХ Тогда
			Текст = НСтр("ru = 'Не заполнен вид договора';
						|en = 'Contract kind is required'");
			СообщенияБЗК.СообщитьОбОшибкеВОбъекте(Отказ, ЭтотОбъект, Текст, "ВидДоговораТрудовой");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьДанныеНСПЗ(Отказ, СведенияОВидеПособия)
	Если СведенияОВидеПособия.ЭтоНСПЗ Тогда
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "ПризнаниеСлучаяСтраховым");
	КонецЕсли;
	ЕстьАктН1 = ЗначениеЗаполнено(АктН1Номер) Или ЗначениеЗаполнено(АктН1ДатаВыдачи);
	Если ЕстьАктН1 Тогда
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "АктН1Номер");
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "АктН1ДатаВыдачи");
	КонецЕсли;
	ЕстьАктПФ = ЗначениеЗаполнено(АктВПроизвольнойФормеНомер) Или ЗначениеЗаполнено(АктВПроизвольнойФормеДатаВыдачи);
	Если ЕстьАктПФ Тогда
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "АктВПроизвольнойФормеНомер");
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "АктВПроизвольнойФормеДатаВыдачи");
	КонецЕсли;
	ЕстьРешениеСуда = ЗначениеЗаполнено(РешениеСудаНомер) Или ЗначениеЗаполнено(РешениеСудаДатаВыдачи);
	Если ЕстьРешениеСуда Тогда
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "РешениеСудаНомер");
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "РешениеСудаДатаВыдачи");
	КонецЕсли;
	Если СведенияОВидеПособия.ЭтоНСПЗ
		И Не ЕстьАктН1
		И Не ЕстьАктПФ
		И Не ЕстьРешениеСуда
		И Не ЗначениеЗаполнено(АктПЗДатаВыдачи) Тогда
		Текст = НСтр("ru = 'Требуется заполнить документы НС/ПЗ';
					|en = 'Fill the documents on industrial accidents and occupational diseases'");
		СообщенияБЗК.СообщитьОбОшибкеВОбъекте(Отказ, ЭтотОбъект, Текст, "АктН1Номер");
	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьИсключаемыеПериоды(Отказ, ПроверяемыеРеквизиты)
	ПроверяемыеРеквизиты.Добавить("ИсключаемыеПериоды.ДатаНачала");
	ПроверяемыеРеквизиты.Добавить("ИсключаемыеПериоды.ДатаОкончания");
	Для Каждого ИсключаемыйПериод Из ИсключаемыеПериоды Цикл
		ПроверкиБЗК.ПроверитьПериодВСтрокеТаблицы(
			Отказ,
			ЭтотОбъект,
			"ИсключаемыеПериоды",
			ИсключаемыйПериод,
			"ДатаНачала",
			"ДатаОкончания",
			НСтр("ru = 'исключаемого периода';
				|en = 'excluded period'"));
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаСтрокиТаблицы(
			Отказ,
			ЭтотОбъект,
			"ИсключаемыеПериоды",
			ИсключаемыйПериод,
			"Причина");
	КонецЦикла;
КонецПроцедуры

Процедура ПроверитьПериодыПростоя(Отказ, ПроверяемыеРеквизиты)
	ПроверяемыеРеквизиты.Добавить("ПериодыПростоя.ДатаНачала");
	ПроверяемыеРеквизиты.Добавить("ПериодыПростоя.ДатаОкончания");
	Для Каждого ПериодПростоя Из ПериодыПростоя Цикл
		ПроверкиБЗК.ПроверитьПериодВСтрокеТаблицы(
			Отказ,
			ЭтотОбъект,
			"ПериодыПростоя",
			ПериодПростоя,
			"ДатаНачала",
			"ДатаОкончания",
			НСтр("ru = 'периода простоя';
				|en = 'downtime period'"));
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаСтрокиТаблицы(
			Отказ,
			ЭтотОбъект,
			"ПериодыПростоя",
			ПериодПростоя,
			"СреднийЗаработок");
	КонецЦикла;
КонецПроцедуры

Процедура ПроверитьПриостановленияТД(Отказ, ПроверяемыеРеквизиты)
	Для Каждого Период Из ПриостановленияТрудовыхДоговоров Цикл
		ПроверкиБЗК.ПроверитьПериодВСтрокеТаблицы(
			Отказ,
			ЭтотОбъект,
			"ПриостановленияТрудовыхДоговоров",
			Период,
			"ДатаНачала",
			"ДатаОкончания",
			НСтр("ru = 'приостановления трудового договора';
				|en = 'employment contract suspensions'"));
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаСтрокиТаблицы(
			Отказ,
			ЭтотОбъект,
			"ПриостановленияТрудовыхДоговоров",
			Период,
			"ОснованиеНПА");
	КонецЦикла;
КонецПроцедуры

Процедура ПроверитьИзвещение(Отказ)
	Если ЗначениеЗаполнено(ИзвещениеФССДата) Или ЗначениеЗаполнено(ИзвещениеФССНомер) Тогда
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "ИзвещениеФССДата");
		СЭДОФСС.ПроверитьНомерИзвещенияФСС(Отказ, ЭтотОбъект, "ИзвещениеФССНомер");
	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьСведенияОбОтпуске(Отказ)
	Если Не ЗначениеЗаполнено(ОтпускПоУходуНомер)
		И Не ЗначениеЗаполнено(ОтпускПоУходуДата)
		И Не ЗначениеЗаполнено(ОтпускПоУходуДатаНачала)
		И Не ЗначениеЗаполнено(ОтпускПоУходуДатаОкончания) Тогда
		Возврат;
	КонецЕсли;
	ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "ОтпускПоУходуНомер");
	ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "ОтпускПоУходуДата");
	ПроверкиБЗК.ПроверитьПериод(
		Отказ,
		ЭтотОбъект,
		"ОтпускПоУходуДатаНачала",
		"ОтпускПоУходуДатаОкончания",
		НСтр("ru = 'отпуска по уходу';
			|en = 'care leave'"),
		Ложь);
КонецПроцедуры

Процедура ПроверитьИнформациюОбУсыновленииОпеке(Отказ)
	// Данные об усыновлении/опеке.
	Если Не ЗначениеЗаполнено(УсыновлениеОпекаТип)
		И Не ЗначениеЗаполнено(УсыновлениеОпекаНомер)
		И Не ЗначениеЗаполнено(УсыновлениеОпекаДата) Тогда
		Возврат;
	КонецЕсли;
	// Тип, Номер, Дата выдачи.
	ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "УсыновлениеОпекаТип");
	ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "УсыновлениеОпекаНомер");
	ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "УсыновлениеОпекаДата");
КонецПроцедуры

Процедура ПроверитьИнформациюОПередачеНаВоспитание(Отказ)
	// Документ о передаче в приемную семью.
	Если Не ЗначениеЗаполнено(ПередачаНаВоспитаниеДата)
		И Не ЗначениеЗаполнено(ПередачаНаВоспитаниеНомер) Тогда
		Возврат;
	КонецЕсли;
	// Номер, Дата выдачи документа о передаче в приемную семью.
	ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "ПередачаНаВоспитаниеДата");
	ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "ПередачаНаВоспитаниеНомер");
КонецПроцедуры

Процедура ПроверитьИнформациюОРебенкеПособияПриРождении(Отказ)
	// Данные о актовой записи ЗАГС.
	ЕстьАктоваяЗаписьЗАГС = (ЗначениеЗаполнено(АктоваяЗаписьОРожденииНомер)
		Или ЗначениеЗаполнено(АктоваяЗаписьОРожденииДата));
	Если ЕстьАктоваяЗаписьЗАГС Тогда
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "АктоваяЗаписьОРожденииНомер");
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "АктоваяЗаписьОРожденииДата");
	КонецЕсли;
	
	// Свидетельство о рождении.
	ЕстьСвидетельствоОРождении = ЗначениеЗаполнено(СвидетельствоОРожденииСерияНомер)
		Или ЗначениеЗаполнено(СвидетельствоОРожденииСерия)
		Или ЗначениеЗаполнено(СвидетельствоОРожденииНомер)
		Или ЗначениеЗаполнено(СвидетельствоОРожденииДатаВыдачи);
	Если ЕстьСвидетельствоОРождении Тогда
		Если Не ЗначениеЗаполнено(СвидетельствоОРожденииСерияНомер) Тогда
			ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "СвидетельствоОРожденииСерия");
			ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "СвидетельствоОРожденииНомер");
		КонецЕсли;
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "СвидетельствоОРожденииДатаВыдачи");
	КонецЕсли;
	
	// Справка о рождении.
	ЕстьСправкаОРождении = (ЗначениеЗаполнено(СправкаОРожденииФорма)
		Или ЗначениеЗаполнено(СправкаОРожденииНомер)
		Или ЗначениеЗаполнено(СправкаОРожденииДатаВыдачи));
	Если ЕстьСправкаОРождении Тогда
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "СправкаОРожденииФорма");
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "СправкаОРожденииНомер");
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "СправкаОРожденииДатаВыдачи");
	КонецЕсли;
	
	// Иной документ подтверждающий рождение ребенка.
	ЕстьИнойДокумент = (ЗначениеЗаполнено(ИнойДокументОРожденииСерия)
		Или ЗначениеЗаполнено(ИнойДокументОРожденииНомер)
		Или ЗначениеЗаполнено(ИнойДокументОРожденииДатаВыдачи));
	Если ЕстьИнойДокумент Тогда
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "ИнойДокументОРожденииНомер");
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "ИнойДокументОРожденииДатаВыдачи");
	КонецЕсли;
	
	// Должен быть хотя бы один документ о рождении.
	Если Не ЕстьАктоваяЗаписьЗАГС
		И Не ЕстьСвидетельствоОРождении
		И Не ЕстьСправкаОРождении
		И Не ЕстьИнойДокумент Тогда
		Текст = НСтр("ru = 'Требуется заполнить документ о рождении';
					|en = 'Fill the birth document'");
		СообщенияБЗК.СообщитьОбОшибкеВОбъекте(Отказ, ЭтотОбъект, Текст, "СвидетельствоОРожденииСерия");
	КонецЕсли;
	
	// ФИО, СНИЛС, Дата рождения ребенка.
	ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "РебенокФамилия");
	ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "РебенокИмя");
	ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "РебенокДатаРождения");
	Если ЗначениеЗаполнено(РебенокСНИЛС) Или Не ЗначениеЗаполнено(РебенокПол) Тогда
		ПроверкиБЗК.ПроверитьСНИЛС(Отказ, ЭтотОбъект, "РебенокСНИЛС");
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьИнформациюОДругомРодителе(Отказ)
	
	// Справка от другого родителя о неполучении пособия.
	Если ЗначениеЗаполнено(СправкаОтДругогоРодителяНомер) Или ЗначениеЗаполнено(СправкаОтДругогоРодителяДата) Тогда
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "СправкаОтДругогоРодителяНомер");
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "СправкаОтДругогоРодителяДата");
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьИнформациюОРебенкеОтпускаПоУходу(Отказ)
	// ФИО, СНИЛС, Дата рождения ребенка.
	ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "РебенокИмя");
	ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "РебенокФамилия");
	ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "РебенокДатаРождения");
	// Обязательность СНИЛС определяется документом ИсходящееСообщениеОСтраховомСлучаеФСС.
	Если ЗначениеЗаполнено(РебенокСНИЛС) Тогда
		ПроверкиБЗК.ПроверитьСНИЛС(Отказ, ЭтотОбъект, "РебенокСНИЛС");
	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьОснованияДляОтпускаПоУходу(Отказ)
	ПроверкиБЗК.ПроверитьПериод(
		Отказ,
		ЭтотОбъект,
		"ОтпускПоУходуДатаНачала",
		"ОтпускПоУходуДатаОкончания",
		НСтр("ru = 'отпуска по уходу';
			|en = 'care leave'"));
	
	Если ЗначениеЗаполнено(ОтпускПоУходуНомер) <> ЗначениеЗаполнено(ОтпускПоУходуДата) Тогда
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "ОтпускПоУходуНомер");
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "ОтпускПоУходуДата");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗаменаПослеродовогоОтпускаНомер) <> ЗначениеЗаполнено(ЗаменаПослеродовогоОтпускаДата) Тогда
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "ЗаменаПослеродовогоОтпускаНомер");
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "ЗаменаПослеродовогоОтпускаДата");
	КонецЕсли;
	
	// Свидетельство о рождении.
	ЕстьСвидетельствоОРождении = ЗначениеЗаполнено(СвидетельствоОРожденииСерия)
		Или ЗначениеЗаполнено(СвидетельствоОРожденииНомер)
		Или ЗначениеЗаполнено(СвидетельствоОРожденииДатаВыдачи);
	Если ЕстьСвидетельствоОРождении Тогда
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "СвидетельствоОРожденииСерия");
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "СвидетельствоОРожденииНомер");
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "СвидетельствоОРожденииДатаВыдачи");
	КонецЕсли;
	
	// Иной документ подтверждающий рождение ребенка.
	ЕстьИнойДокумент = (ЗначениеЗаполнено(ИнойДокументОРожденииСерия)
		Или ЗначениеЗаполнено(ИнойДокументОРожденииНомер)
		Или ЗначениеЗаполнено(ИнойДокументОРожденииДатаВыдачи));
	Если ЕстьИнойДокумент Тогда
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "ИнойДокументОРожденииНомер");
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "ИнойДокументОРожденииДатаВыдачи");
	КонецЕсли;
	
	// Данные о актовой записи ЗАГС.
	ЕстьАктоваяЗаписьЗАГС = (ЗначениеЗаполнено(АктоваяЗаписьОРожденииНомер)
		Или ЗначениеЗаполнено(АктоваяЗаписьОРожденииДата));
	Если ЕстьАктоваяЗаписьЗАГС Тогда
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "АктоваяЗаписьОРожденииНомер");
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "АктоваяЗаписьОРожденииДата");
	КонецЕсли;
	
	// Должен быть хотя бы один документ о рождении.
	Если Не ЕстьАктоваяЗаписьЗАГС
		И Не ЕстьСвидетельствоОРождении
		И Не ЕстьИнойДокумент Тогда
		Текст = НСтр("ru = 'Требуется заполнить документ о рождении';
					|en = 'Fill the birth document'");
		СообщенияБЗК.СообщитьОбОшибкеВОбъекте(Отказ, ЭтотОбъект, Текст, "СвидетельствоОРожденииСерия");
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьСправкуОНеполученииПособия(Отказ)
	Если ЗначениеЗаполнено(СправкаОНеполученииПособияОтМатериНомер)
		<> ЗначениеЗаполнено(СправкаОНеполученииПособияОтМатериДатаВыдачи) Тогда
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "СправкаОНеполученииПособияОтМатериНомер");
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "СправкаОНеполученииПособияОтМатериДатаВыдачи");
	КонецЕсли;
	Если ЗначениеЗаполнено(СправкаОНеполученииПособияОтОтцаНомер)
		<> ЗначениеЗаполнено(СправкаОНеполученииПособияОтОтцаДатаВыдачи) Тогда
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "СправкаОНеполученииПособияОтОтцаНомер");
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "СправкаОНеполученииПособияОтОтцаДатаВыдачи");
	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьПрочиеСведенияОтпускаПоУходу(Отказ)
	ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "РебенокОчередность");
	Если РебенокОчередность > 1
		И Не НаличиеДокументовОРожденииПредыдущихДетей
		И Не НаличиеСвидетельстваОСмертиПредыдущихДетей Тогда
		Текст = НСтр("ru = 'Для второго и последующих детей должен быть включен один из флажков наличия документов на предыдущих детей.';
					|en = 'For the second child and the following children, select one of the check boxes of the documents for the previous children.'");
		СообщенияБЗК.СообщитьОбОшибкеВОбъекте(Отказ, ЭтотОбъект, Текст, "НаличиеДокументовОРожденииПредыдущихДетей");
	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьАдрес(Отказ, ИмяПоляАдрес)
	ТекстОшибки = ТекстОшибкиЗаполненияАдреса(ИмяПоляАдрес);
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		Текст = Метаданные().Реквизиты[ИмяПоляАдрес].Представление() + ": " + ТекстОшибки;
		СообщенияБЗК.СообщитьОбОшибкеВОбъекте(Отказ, ЭтотОбъект, Текст, ИмяПоляАдрес);
	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьРазрешениеНаПроживание(Отказ)
	Если Документы.СведенияОЗастрахованномЛицеФСС.ТребуетсяВыгрузитьРазрешениеНаПроживание(ЭтотОбъект) Тогда
		Если Не ЗначениеЗаполнено(РазрешениеНаПроживаниеВид) Тогда
			ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "РазрешениеНаПроживаниеВид");
		Иначе
			ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "РазрешениеНаПроживаниеНомер");
			ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "РазрешениеНаПроживаниеДатаВыдачи");
			СведенияОВиде = СЭДОФСС.СведенияОВидеРазрешенияНаПроживание(РазрешениеНаПроживаниеВид);
			Если СведенияОВиде.ЭтоВНЖ Тогда
				ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "РазрешениеНаПроживаниеСерия");
			КонецЕсли;
			Массив = Документы.ОтветНаЗапросФССДляРасчетаПособия.ДопустимыеКодыВидаРазрешенияНаПроживание();
			Если Массив.Найти(СведенияОВиде.КодМВД) = Неопределено Тогда
				Текст = НСтр("ru = 'Недопустимый вид разрешения на проживания: %1, Фонд допускает только следующие виды: %2';
							|en = 'Incorrect residence permit kind: %1. The Social Security Fund allows only the following kinds: %2'");
				Текст = СтрШаблон(Текст, СведенияОВиде.КодМВД, СтрСоединить(Массив, ", "));
				СообщенияБЗК.СообщитьОбОшибкеВОбъекте(Отказ, ЭтотОбъект, Текст, "РазрешениеНаПроживаниеВид");
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Функция ТекстОшибкиЗаполненияАдреса(ИмяПоляАдрес)
	ЗначениеАдреса = ЭтотОбъект[ИмяПоляАдрес];
	Если Не ЗначениеЗаполнено(ЗначениеАдреса) Тогда
		Возврат НСтр("ru = 'Адрес не заполнен';
					|en = 'Address is not filled in'");
	КонецЕсли;
	
	ТекстыОшибок = Новый Массив;
	
	Индекс  = ЭтотОбъект[ИмяПоляАдрес + "Индекс"];
	ФИАСДом = ЭтотОбъект[ИмяПоляАдрес + "ФИАСДом"];
	ФИАСРО  = ЭтотОбъект[ИмяПоляАдрес + "ФИАСРО"];
	
	Если Не ЗначениеЗаполнено(Индекс) Тогда
		ТекстыОшибок.Добавить(НСтр("ru = 'Не заполнен индекс.';
									|en = 'Postal code is not filled in.'"));
	ИначеЕсли СтрДлина(Индекс) <> 6 Тогда
		ТекстыОшибок.Добавить(СтрШаблон(НСтр("ru = 'Индекс занимает %1 символов, ожидалось 6.';
											|en = 'Postal code contains %1 characters, expected 6.'"), СтрДлина(Индекс)));
	Иначе
		ЛишниеСимволы = СтроковыеФункцииБЗККлиентСервер.УдалитьЦифрыИзСтроки(Индекс);
		Если Не ПустаяСтрока(ЛишниеСимволы) Тогда
			Если СтрДлина(ЛишниеСимволы) = 1 Тогда
				Текст = НСтр("ru = 'В индексе должны быть только цифры (%1 - не цифра).';
							|en = 'Postal code must contain digits only (%1 is not a digit).'");
			Иначе
				Текст = НСтр("ru = 'В индексе должны быть только цифры (%1 - не цифры).';
							|en = 'Postal code must contain digits only (%1 are not digits).'");
			КонецЕсли;
			ТекстыОшибок.Добавить(СтрШаблон(Текст, ЛишниеСимволы));
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ФИАСДом) И Не ЗначениеЗаполнено(ФИАСРО) Тогда
		ТекстыОшибок.Добавить(НСтр("ru = 'Не заполнен идентификатор ФИАС.';
									|en = 'FIAS ID is not filled in.'"));
	КонецЕсли;
	
	Возврат СтрСоединить(ТекстыОшибок, Символы.ПС);
КонецФункции

#КонецОбласти

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли
