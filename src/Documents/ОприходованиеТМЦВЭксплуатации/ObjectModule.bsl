#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ИнициализироватьДокументПередЗаполнением(ДанныеЗаполнения);
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);

	Если ТипДанныхЗаполнения = Тип("Структура") Тогда
		
		ЗаполнитьДокументПоОтбору(ДанныеЗаполнения);
	
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ИнвентаризацияТМЦВЭксплуатации") Тогда
		
		ЗаполнитьНаОснованииИнвентаризации(ДанныеЗаполнения);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	НепроверяемыеРеквизиты.Добавить("Товары.СтатьяРасходов");
	НепроверяемыеРеквизиты.Добавить("Товары.АналитикаРасходов");
	
	НоменклатураСервер.ПроверитьЗаполнениеКоличества(ЭтотОбъект, ПроверяемыеРеквизиты, Отказ);
	НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект, НепроверяемыеРеквизиты, Отказ);
	
	ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, Документы.ОприходованиеТМЦВЭксплуатации);
	НоменклатураСервер.ПроверитьЗаполнениеСерий(ЭтотОбъект, ПараметрыУказанияСерий, Отказ, НепроверяемыеРеквизиты);
		
	ПараметрыПроверки = ТМЦВЭксплуатацииСервер.ПараметрыПроверкиУчетаПоФизЛицам();
	ПараметрыПроверки.ПредставлениеТЧ = НСтр("ru = 'Товары';
											|en = 'Goods'");
	ТМЦВЭксплуатацииСервер.ПроверитьУчетПоФизЛицам(ЭтотОбъект, НепроверяемыеРеквизиты, Отказ, ПараметрыПроверки);
	
	ТМЦВЭксплуатацииСервер.ПроверитьПередачуВЭксплуатацию(ЭтотОбъект, НепроверяемыеРеквизиты, Отказ);

	ВыполнитьПроверкуЗаполнения(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты, Отказ);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	ВыполнитьДействияПередЗаписью(Отказ, РежимЗаписи);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ПриЗаписиДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеДокументов.ОбработкаПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеДокументов.ОбработкаУдаленияПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Ответственный = Пользователи.ТекущийПользователь();
	
	ОбщегоНазначенияУТ.ОчиститьИдентификаторыДокумента(ЭтотОбъект, "Товары");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Заполнение

Процедура ИнициализироватьДокументПередЗаполнением(ДанныеЗаполнения)
	
	Организация = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	Ответственный = Пользователи.ТекущийПользователь();
	
	ЗаполнениеОбъектовПоСтатистике.ЗаполнитьРеквизитыОбъекта(ЭтотОбъект, ДанныеЗаполнения);
	
	Если Не ЗначениеЗаполнено(Валюта) Тогда
		Валюта = ДоходыИРасходыСервер.ПолучитьВалютуУправленческогоУчета(
			Справочники.ВидыЦен.ПолучитьРеквизитыВидаЦены(ВидЦены).ВалютаЦены);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДокументПоОтбору(Основание)

	Если Основание.Свойство("ДокументОснование")
		И ТипЗнч(Основание.ДокументОснование) = Тип("ДокументСсылка.ИнвентаризацияТМЦВЭксплуатации") Тогда
			
		ЗаполнитьНаОснованииИнвентаризации(Основание);
	КонецЕсли; 
	
КонецПроцедуры

Процедура ЗаполнитьНаОснованииИнвентаризации(Знач ДанныеЗаполнения)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ДокументОснование = ДанныеЗаполнения.ДокументОснование;
		МассивНомеровСтрок = ДанныеЗаполнения.МассивНомеровСтрок;
		СообщатьОбОшибках = ДанныеЗаполнения.СообщатьОбОшибках;
	Иначе
		ДокументОснование = ДанныеЗаполнения;
		МассивНомеровСтрок = Неопределено;
		СообщатьОбОшибках = Истина;
	КонецЕсли;
	
	ОперацияВыполнена = Документы.ОприходованиеТМЦВЭксплуатации.ЗаполнитьНаОснованииИнвентаризации(ЭтотОбъект, МассивНомеровСтрок);
	
	Если НЕ ОперацияВыполнена И СообщатьОбОшибках Тогда
		
		ТекстОшибки = НСтр("ru = 'В документе %1 отсутствуют строки требующие оприходования';
							|en = 'There are no lines that require receipt in the %1 document'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, ДокументОснование);
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,, "Объект.ДокументОснование");
		
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкаЗаполнения

Процедура ВыполнитьПроверкуЗаполнения(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты, Отказ)
	
	НачалоПримененияФСБУ5 = РеглУчетКлиентСервер.НачалоПримененияФСБУ5_2019();
	
	Если Дата < НачалоПримененияФСБУ5 Тогда
		ТекстСообщения = НСтр("ru = 'Дата документа должна быть не раньше %1 (начало применения ФСБУ 5/2019)';
								|en = 'The document date must not be earlier than %1 (the start of the Russian GAAP (FSBU 5/2019) application)'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, Формат(НачалоПримененияФСБУ5, "ДЛФ=D"));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Ссылка, "Дата", "Объект", Отказ);
	КонецЕсли;
	
	ПроверитьТабличнуюЧасть(Отказ);
	
КонецПроцедуры

Процедура ПроверитьТабличнуюЧасть(Отказ)
	
	РеквизитыКатегорий = ТМЦВЭксплуатацииСервер.РеквизитыКатегорийЭксплуатации(Товары);
	
	ШаблонСообщения = НСтр("ru = 'В строке %1 выбрана категория эксплуатации ""%2"", в которой включен учет в виде группового ОС. Оприходование с такой категорией эксплуатации не поддерживается.""';
							|en = 'In line %1, the ""%2"" operation category is selected. In this category, accounting as a group fixed asset is enabled. Receipt with such operation category is not supported.'");
	
	Для Каждого ДанныеСтроки Из Товары Цикл
		
		Если НЕ ЗначениеЗаполнено(ДанныеСтроки.КатегорияЭксплуатации) Тогда
			Продолжить;
		КонецЕсли;

		СвойстваКатегории = РеквизитыКатегорий.Получить(ДанныеСтроки.КатегорияЭксплуатации);
		Если СвойстваКатегории <> Неопределено 
			И СвойстваКатегории.УчитыватьВВидеГрупповогоОС Тогда
			
			ТекстСообщения = 
				СтрШаблон(ШаблонСообщения, 
					Формат(ДанныеСтроки.НомерСтроки, "ЧГ=0;"),
					Строка(ДанныеСтроки.КатегорияЭксплуатации));
			
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", ДанныеСтроки.НомерСтроки, "КатегорияЭксплуатации");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Ссылка, Поле, "Объект", Отказ);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Процедура ВыполнитьДействияПередЗаписью(Отказ, РежимЗаписи)
	
	НоменклатураСервер.ОчиститьНеиспользуемыеСерии(
		ЭтотОбъект,
		НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, Документы.ОприходованиеТМЦВЭксплуатации));
	
	НоменклатураСервер.ОкруглитьКоличествоШтучныхТоваров(ЭтотОбъект, РежимЗаписи);
	
	ОбщегоНазначенияУТ.ЗаполнитьИдентификаторыДокумента(ЭтотОбъект, "Товары");
	
	ТМЦВЭксплуатацииСервер.ЗаполнитьРеквизитыПоКатегорииЭксплуатации(Товары);
	
	Справочники.ПартииТМЦВЭксплуатации.ЗаполнитьПартии(ЭтотОбъект, РежимЗаписи);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
