#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ИнициализироватьДокумент(ДанныеЗаполнения);
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("Структура") Тогда
		ЗаполнитьДокументПоОтбору(ДанныеЗаполнения);
	ИначеЕсли ТипДанныхЗаполнения = Тип("СправочникСсылка.ОбъектыЭксплуатации") Тогда
		ЗаполнитьНаОснованииОбъектаЭксплуатации(ДанныеЗаполнения);
	КонецЕсли;
	
	ПараметрыВыбораСтатейИАналитик = Документы.ПараметрыНачисленияЗемельногоНалога.ПараметрыВыбораСтатейИАналитик();
	ДоходыИРасходыСервер.ОбработкаЗаполнения(ЭтотОбъект, ПараметрыВыбораСтатейИАналитик);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ИнициализироватьДокумент();
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	ЗаполнитьРеквизитыПередЗаписью();
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ПроверитьНаличиеПараметровНаДату(Отказ);
	КонецЕсли;

	ПараметрыВыбораСтатейИАналитик = Документы.ВводПервоначальныхСведенийДляРасчетаНалогаНаИмущество.ПараметрыВыбораСтатейИАналитик();
	ДоходыИРасходыСервер.ПередЗаписью(ЭтотОбъект, ПараметрыВыбораСтатейИАналитик);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ПриЗаписиДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	ПроверитьРеквизиты(НепроверяемыеРеквизиты, Отказ);
	
	ПараметрыВыбораСтатейИАналитик = Документы.ВводПервоначальныхСведенийДляРасчетаНалогаНаИмущество.ПараметрыВыбораСтатейИАналитик();
	ДоходыИРасходыСервер.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, ПараметрыВыбораСтатейИАналитик);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Документы.ВводПервоначальныхСведенийДляРасчетаНалогаНаИмущество.ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(
		НСтр("ru = 'Ввод первоначальных сведений для расчета налога на имущество';
			|en = 'Enter initial information to calculate a property tax'"));
	
	ПроведениеДокументов.ОбработкаПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеДокументов.ОбработкаУдаленияПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	
	Организация = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	Ответственный = Пользователи.ТекущийПользователь();
	
	ЗаполнениеОбъектовПоСтатистике.ЗаполнитьРеквизитыОбъекта(ЭтотОбъект, ДанныеЗаполнения);
	
КонецПроцедуры

Процедура ЗаполнитьРеквизитыПередЗаписью()
	
	УчетСНачалаГода = (КонецМесяца(Дата) = КонецГода(Дата));

	ПараметрыРеквизитовОбъекта = ИмущественныеНалогиКлиентСервер.НастройкаРеквизитов_ВводПервоначальныхСведенийДляРасчетаНалогаНаИмущество(ЭтотОбъект);
	ОбщегоНазначенияУТКлиентСервер.ОчиститьНеиспользуемыеРеквизиты(
		ЭтотОбъект, ПараметрыРеквизитовОбъекта, "ПараметрыРегистрации,ПараметрыНалога,НалоговыйОрган,СтоимостьИАмортизация");

	УказаныСпособыОтражениеРасходов = (ОтражениеРасходов.Количество() <> 0);
	
	Для Каждого ДанныеСтроки Из ПараметрыРегистрации Цикл

		Если УчетСНачалаГода Тогда
			ДанныеСтроки.ДатаПараметров = КонецМесяца(Дата) + 1;
		КонецЕсли;
		
		Если Не ДанныеСтроки.ОбщаяСобственность Тогда
			ДанныеСтроки.ДоляВПравеОбщейСобственностиЧислитель = 0;
			ДанныеСтроки.ДоляВПравеОбщейСобственностиЗнаменатель = 0;
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого ДанныеСтроки Из ПараметрыНалога Цикл
		
		Если УчетСНачалаГода Тогда
			ДанныеСтроки.ДатаПараметров = КонецМесяца(Дата) + 1;
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого ДанныеСтроки Из НалоговыйОрган Цикл
		
		Если УчетСНачалаГода Тогда
			ДанныеСтроки.ДатаПараметров = КонецМесяца(Дата) + 1;
		КонецЕсли;

		Если ДанныеСтроки.ПостановкаНаУчетВНалоговомОргане = Перечисления.ПостановкаНаУчетВНалоговомОргане.СДругимКодомПоОКАТО
			И ДанныеСтроки.ПостановкаНаУчетВНалоговомОргане = Перечисления.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане Тогда
			
			ДанныеСтроки.КодПоОКТМО = "";
		КонецЕсли;
		
		Если ДанныеСтроки.ПостановкаНаУчетВНалоговомОргане <> Перечисления.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане Тогда
			ДанныеСтроки.НалоговыйОрган = Справочники.РегистрацииВНалоговомОргане.ПустаяСсылка();
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьДокументПоОтбору(Основание)

	Если Основание.Свойство("Дата") Тогда
		Дата = Основание.Дата;
	КонецЕсли;
	
	Если Основание.Свойство("Организация")
		И ЗначениеЗаполнено(Основание.Организация) Тогда
		Организация = Основание.Организация;
	КонецЕсли;

	Если Основание.Свойство("ОсновноеСредство") Тогда
		ЗаполнитьНаОснованииОбъектаЭксплуатации(Основание.ОсновноеСредство);
	КонецЕсли; 
	
КонецПроцедуры

Процедура ЗаполнитьНаОснованииОбъектаЭксплуатации(ДанныеЗаполнения)
	
	РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, "ЭтоГруппа");
	
	Если РеквизитыОснования.ЭтоГруппа Тогда
		
		ТекстСообщения = НСтр("ru = 'Оформление документа на основании группы ОС невозможен.
			|Выберите ОС. Для раскрытия группы используйте клавиши Ctrl и стрелку вниз.';
			|en = 'Cannot create documents based on the fixed assets group.
			|Select fixed assets. To expand the group, press Ctrl+Down.'");
			
		ВызватьИсключение(ТекстСообщения);
		
	КонецЕсли;
	
	ОсновноеСредство = ДанныеЗаполнения;
	
КонецПроцедуры

Процедура ПроверитьРеквизиты(НепроверяемыеРеквизиты, Отказ)
	
	ПараметрыРеквизитовОбъекта = ИмущественныеНалогиКлиентСервер.НастройкаРеквизитов_ВводПервоначальныхСведенийДляРасчетаНалогаНаИмущество(
									ЭтотОбъект);
									
	ОбщегоНазначенияУТ.ОтключитьПроверкуЗаполненияРеквизитовОбъекта(ПараметрыРеквизитовОбъекта, НепроверяемыеРеквизиты);

	ПроверитьДатыПараметров(НепроверяемыеРеквизиты, Отказ);
	
	ПроверитьПараметрыРегистрации(НепроверяемыеРеквизиты, Отказ);
	ПроверитьПараметрыНалога(НепроверяемыеРеквизиты, Отказ);
	ПроверитьНалоговыйОрган(НепроверяемыеРеквизиты, Отказ);
	ПроверитьСтоимостьИАмортизацию(НепроверяемыеРеквизиты, Отказ);	
	
КонецПроцедуры

Процедура ПроверитьДатыПараметров(НепроверяемыеРеквизиты, Отказ)
	
	УчетСНачалаГода = (КонецМесяца(Дата) = КонецГода(Дата));

	Если УчетСНачалаГода Тогда
		
		НепроверяемыеРеквизиты.Добавить("ПараметрыРегистрации.ДатаПараметров");
		НепроверяемыеРеквизиты.Добавить("ПараметрыНалога.ДатаПараметров");
		НепроверяемыеРеквизиты.Добавить("НалоговыйОрган.ДатаПараметров");
		
		Возврат;
		
	КонецЕсли;

	МинимальнаяДата = '000101010000';
	НачалоГода = НачалоГода(Дата);
	
	Для Каждого ДанныеСтроки Из ПараметрыРегистрации Цикл
		
		Если НЕ ЗначениеЗаполнено(ДанныеСтроки.ДатаПараметров) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ДанныеСтроки.ДатаПараметров < НачалоГода Тогда
			
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Дата сведений о регистрации должна быть не раньше начала года %1';
					|en = 'The registration information date must not be earlier than the beginning of the year %1'"),
				Формат(НачалоГода, "ДЛФ=D;"));
			
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
				"ПараметрыРегистрации", ДанныеСтроки.НомерСтроки, "ДатаПараметров");
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле,, Отказ);
			
		ИначеЕсли ДанныеСтроки.ДатаПараметров > КонецДня(Дата) Тогда
			
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Дата сведений о регистрации должна быть не позже даты документа %1';
					|en = 'The registration information date must not be later than the document date %1'"),
				Формат(Дата, "ДЛФ=D;"));
			
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
				"ПараметрыРегистрации", ДанныеСтроки.НомерСтроки, "ДатаПараметров");
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле,, Отказ);
			
		КонецЕсли;
		
		МинимальнаяДата = ?(МинимальнаяДата <> '000101010000', 
							Мин(ДанныеСтроки.ДатаПараметров, МинимальнаяДата), 
							ДанныеСтроки.ДатаПараметров);
		
	КонецЦикла;
		
	Для Каждого ДанныеСтроки Из ПараметрыНалога Цикл
		
		Если НЕ ЗначениеЗаполнено(ДанныеСтроки.ДатаПараметров) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ДанныеСтроки.ДатаПараметров < НачалоГода Тогда
			
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Дата параметров начисления налога должна быть не раньше начала года %1';
					|en = 'The date of financial details of tax charge must not be earlier than the beginning of the year %1'"),
				Формат(НачалоГода, "ДЛФ=D;"));
			
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
				"ПараметрыНалога", ДанныеСтроки.НомерСтроки, "ДатаПараметров");
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле,, Отказ);
			
		ИначеЕсли ДанныеСтроки.ДатаПараметров > КонецДня(Дата) Тогда
			
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Дата параметров начисления налога должна быть не позже даты документа %1';
					|en = 'The date of financial details of tax charge must not be later than the document date %1'"),
				Формат(Дата, "ДЛФ=D;"));
			
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
				"ПараметрыНалога", ДанныеСтроки.НомерСтроки, "ДатаПараметров");
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле,, Отказ);
			
		КонецЕсли;
		
		МинимальнаяДата = ?(МинимальнаяДата <> '000101010000', 
							Мин(ДанныеСтроки.ДатаПараметров, МинимальнаяДата), 
							ДанныеСтроки.ДатаПараметров);
		
	КонецЦикла;
	
	Для Каждого ДанныеСтроки Из НалоговыйОрган Цикл

		Если НЕ ЗначениеЗаполнено(ДанныеСтроки.ДатаПараметров) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ДанныеСтроки.ДатаПараметров < НачалоГода Тогда
			
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Дата регистрации в налоговом органе должна быть не раньше начала года %1';
					|en = 'The date of registration with tax authority must not be earlier than the beginning of the year %1'"),
				Формат(НачалоГода, "ДЛФ=D;"));
			
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
				"НалоговыйОрган", ДанныеСтроки.НомерСтроки, "ДатаПараметров");
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле,, Отказ);
			
		ИначеЕсли ДанныеСтроки.ДатаПараметров > КонецДня(Дата) Тогда
			
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Дата регистрации в налоговом органе должна быть не позже даты документа %1';
					|en = 'The date of registration with tax authority must not be later than the document date %1'"),
				Формат(Дата, "ДЛФ=D;"));
			
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
				"НалоговыйОрган", ДанныеСтроки.НомерСтроки, "ДатаПараметров");
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле,, Отказ);
			
		КонецЕсли;
		
		МинимальнаяДата = ?(МинимальнаяДата <> '000101010000', 
							Мин(ДанныеСтроки.ДатаПараметров, МинимальнаяДата), 
							ДанныеСтроки.ДатаПараметров);

	КонецЦикла;
	
	Если МинимальнаяДата <> '000101010000' Тогда
		
		МинимальнаяДата = Макс(МинимальнаяДата, НачалоГода);
		
		Если ПараметрыРегистрации.Найти(МинимальнаяДата, "ДатаПараметров") = Неопределено Тогда
			
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'В списке ""Сведения о регистрации"" необходимо указать параметры на дату %1';
					|en = 'In the ""Registration information"" list, specify financial details for the date %1'"), 
				Формат(МинимальнаяДата, "ДЛФ=D;"));
				
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ПараметрыРегистрации",, Отказ);
			
		КонецЕсли;
		
		Если ПараметрыНалога.Найти(МинимальнаяДата, "ДатаПараметров") = Неопределено Тогда
			
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'В списке ""Налог на имущество"" необходимо указать параметры на дату %1';
					|en = 'In the ""Property tax"" list, specify financial details for the date %1'"), 
				Формат(МинимальнаяДата, "ДЛФ=D;"));
				
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ПараметрыНалога",, Отказ);
			
		КонецЕсли;
		
		Если НалоговыйОрган.Найти(МинимальнаяДата, "ДатаПараметров") = Неопределено Тогда
			
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'В списке ""Налоговый орган"" необходимо указать параметры на дату %1';
					|en = 'In the ""Tax authority"" list, specify financial details for the date %1'"), 
				Формат(МинимальнаяДата, "ДЛФ=D;"));
				
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "НалоговыйОрган",, Отказ);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьПараметрыРегистрации(НепроверяемыеРеквизиты, Отказ)
	
	НепроверяемыеРеквизиты.Добавить("ПараметрыРегистрации.ДоляВПравеОбщейСобственностиЧислитель");
	НепроверяемыеРеквизиты.Добавить("ПараметрыРегистрации.ДоляВПравеОбщейСобственностиЗнаменатель");
	НепроверяемыеРеквизиты.Добавить("ПараметрыРегистрации.ДоляСтоимостиЧислитель");
	НепроверяемыеРеквизиты.Добавить("ПараметрыРегистрации.ДоляСтоимостиЗнаменатель");
	НепроверяемыеРеквизиты.Добавить("ПараметрыРегистрации.ДоляПлощадиЧислитель");
	НепроверяемыеРеквизиты.Добавить("ПараметрыРегистрации.ДоляПлощадиЗнаменатель");
	НепроверяемыеРеквизиты.Добавить("ПараметрыРегистрации.ДатаРегистрацииПравНаОбъектНедвижимости");
	
	УчетСНачалаГода = (КонецМесяца(Дата) = КонецГода(Дата));

	ШаблонСообщения = НСтр("ru = 'Не заполнена колонка ""%1"" в строке %2 списка ""Сведения об имуществе""';
							|en = 'Column %1 in line %2 of the ""Property information"" list is not filled'");
	
	Если УчетСНачалаГода И ПараметрыРегистрации.Количество() > 1 Тогда
		
		ТекстСообщения = НСтр("ru = 'При учете с начала года вводится только одна строка сведений об имуществе';
								|en = 'When accounting from the beginning of the year, enter only one line of property information'");
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ПараметрыРегистрации",, Отказ);
		
	Иначе
		
		ВнеоборотныеАктивы.ПроверитьОтсутствиеДублейВТабличнойЧасти(ЭтотОбъект, "ПараметрыРегистрации", "ДатаПараметров", Отказ);
	
	КонецЕсли;
	
	Для Каждого ДанныеСтроки Из ПараметрыРегистрации Цикл
		
		Если ДанныеСтроки.ОбщаяСобственность Тогда
			
			Если Не ЗначениеЗаполнено(ДанныеСтроки.ДоляВПравеОбщейСобственностиЧислитель) Тогда
				
				ТекстСообщения = СтрШаблон(ШаблонСообщения, НСтр("ru = 'Числитель доли в праве общей собственности';
																|en = 'Numerator of share in ownership rights'"), ДанныеСтроки.НомерСтроки);
				
				Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
					"ПараметрыРегистрации", ДанныеСтроки.НомерСтроки, "ДоляВПравеОбщейСобственностиЧислитель");
				
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле,, Отказ);
				
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(ДанныеСтроки.ДоляВПравеОбщейСобственностиЗнаменатель) Тогда
				
				ТекстСообщения = СтрШаблон(ШаблонСообщения, НСтр("ru = 'Знаменатель доли в праве общей собственности';
																|en = 'Denominator of share in ownership rights'"), ДанныеСтроки.НомерСтроки);
				
				Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
					"ПараметрыРегистрации", ДанныеСтроки.НомерСтроки, "ДоляВПравеОбщейСобственностиЗнаменатель");
				
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле,, Отказ);
				
			КонецЕсли;
			
		КонецЕсли;
				
		Если ДанныеСтроки.КадастроваяСтоимостьОпределенаПоДолеПлощади Тогда
			
			Если Не ЗначениеЗаполнено(ДанныеСтроки.ДоляПлощадиЧислитель) Тогда
				
				ТекстСообщения = СтрШаблон(ШаблонСообщения, НСтр("ru = 'Доля площади (числитель)';
																|en = 'Area share (numerator)'"), ДанныеСтроки.НомерСтроки);
				
				Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
					"ПараметрыРегистрации", ДанныеСтроки.НомерСтроки, "ДоляПлощадиЧислитель");
				
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле,, Отказ);
				
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(ДанныеСтроки.ДоляПлощадиЗнаменатель) Тогда
				
				ТекстСообщения = СтрШаблон(ШаблонСообщения, НСтр("ru = 'Доля площади (знаменатель)';
																|en = 'Area share (denominator)'"), ДанныеСтроки.НомерСтроки);
				
				Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
					"ПараметрыРегистрации", ДанныеСтроки.НомерСтроки, "ДоляПлощадиЗнаменатель");
				
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле,, Отказ);
				
			КонецЕсли;
			
		КонецЕсли;

		Если ДанныеСтроки.ОтноситсяКТерриторииСубъектаРФВДоле Тогда
			
			Если Не ЗначениеЗаполнено(ДанныеСтроки.ДоляСтоимостиЧислитель) Тогда
				
				ТекстСообщения = СтрШаблон(ШаблонСообщения, НСтр("ru = 'Доля стоимости (числитель)';
																|en = 'Cost percentage (numerator)'"), ДанныеСтроки.НомерСтроки);
				
				Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
					"ПараметрыРегистрации", ДанныеСтроки.НомерСтроки, "ДоляСтоимостиЧислитель");
				
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле,, Отказ);
				
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(ДанныеСтроки.ДоляСтоимостиЗнаменатель) Тогда
				
				ТекстСообщения = СтрШаблон(ШаблонСообщения, НСтр("ru = 'Доля стоимости (знаменатель)';
																|en = 'Cost percentage (denominator)'"), ДанныеСтроки.НомерСтроки);
				
				Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
					"ПараметрыРегистрации", ДанныеСтроки.НомерСтроки, "ДоляСтоимостиЗнаменатель");
				
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле,, Отказ);
				
			КонецЕсли;
			
		КонецЕсли;
			
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьПараметрыНалога(НепроверяемыеРеквизиты, Отказ)
	
	НепроверяемыеРеквизиты.Добавить("ПараметрыНалога.НалоговаяСтавка");
	
	УчетСНачалаГода = (КонецМесяца(Дата) = КонецГода(Дата));

	ШаблонСообщения = НСтр("ru = 'Не заполнена колонка ""%1"" в строке %2 списка ""Налог на имущество""';
							|en = 'Column %1 in line %2 of the ""Property tax"" list is not filled'");
	
	Если УчетСНачалаГода И ПараметрыНалога.Количество() > 1 Тогда
		
		ТекстСообщения = НСтр("ru = 'При учете с начала года вводится только одна строка параметров налога';
								|en = 'When accounting from the beginning of the year, enter only one line of tax details'");
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ПараметрыНалога",, Отказ);
		
	Иначе
		
		ВнеоборотныеАктивы.ПроверитьОтсутствиеДублейВТабличнойЧасти(ЭтотОбъект, "ПараметрыНалога", "ДатаПараметров", Отказ);
	
	КонецЕсли;
	
	Для Каждого ДанныеСтроки Из ПараметрыНалога Цикл
		
		Если Не ЗначениеЗаполнено(ДанныеСтроки.НалоговаяСтавка)
			И ДанныеСтроки.НалоговаяБаза = Перечисления.НалоговаяБазаПоНалогуНаИмущество.КадастроваяСтоимость Тогда
			
			ТекстСообщения = СтрШаблон(ШаблонСообщения, НСтр("ru = 'Налоговая ставка';
															|en = 'Tax rate'"), ДанныеСтроки.НомерСтроки);
			
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
				"ПараметрыНалога", ДанныеСтроки.НомерСтроки, "НалоговаяСтавка");
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле,, Отказ);
			
		КонецЕсли;
			
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьНалоговыйОрган(НепроверяемыеРеквизиты, Отказ)
	
	НепроверяемыеРеквизиты.Добавить("НалоговыйОрган.НалоговаяСтавка");
	
	УчетСНачалаГода = (КонецМесяца(Дата) = КонецГода(Дата));
	
	Если УчетСНачалаГода И НалоговыйОрган.Количество() > 1 Тогда
		
		ТекстСообщения = НСтр("ru = 'При учете с начала года вводится только одна строка параметров налогового органа';
								|en = 'When accounting from the beginning of the year, enter only one line of tax authority details'");
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "НалоговыйОрган",, Отказ);
		
	Иначе
		
		ВнеоборотныеАктивы.ПроверитьОтсутствиеДублейВТабличнойЧасти(ЭтотОбъект, "НалоговыйОрган", "ДатаПараметров", Отказ);
		
	КонецЕсли;
	
	ИмущественныеНалоги.ПроверитьНалоговыйОрган(ЭтотОбъект, НепроверяемыеРеквизиты, Отказ);
	
	ШаблонСообщения = НСтр("ru = 'Не заполнена колонка ""%1"" в строке %2 списка ""Налоговый орган""';
							|en = 'Column %1 in line %2 of the Tax authority list is not filled'");
	
	ОпределениеНалоговойБазыПоКадастровойСтоимости = Ложь;
	
	Для Каждого ДанныеСтроки Из ПараметрыНалога Цикл
		Если ДанныеСтроки.НалоговаяБаза = ПредопределенноеЗначение("Перечисление.НалоговаяБазаПоНалогуНаИмущество.КадастроваяСтоимость") Тогда
			ОпределениеНалоговойБазыПоКадастровойСтоимости = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ДанныеСтроки Из НалоговыйОрган Цикл
		
		Если Не ЗначениеЗаполнено(ДанныеСтроки.НалоговаяСтавка)
			И ДанныеСтроки.ПостановкаНаУчетВНалоговомОргане = Перечисления.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане
			И НЕ ОпределениеНалоговойБазыПоКадастровойСтоимости Тогда
			
			ТекстСообщения = СтрШаблон(ШаблонСообщения, НСтр("ru = 'Налоговая ставка';
															|en = 'Tax rate'"), ДанныеСтроки.НомерСтроки);
			
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
				"НалоговыйОрган", ДанныеСтроки.НомерСтроки, "НалоговаяСтавка");
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле,, Отказ);
			
		КонецЕсли;
			
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьСтоимостьИАмортизацию(НепроверяемыеРеквизиты, Отказ)
	
	НепроверяемыеРеквизиты.Добавить("СтоимостьИАмортизация.Дата");

	ВнеоборотныеАктивы.ПроверитьОтсутствиеДублейВТабличнойЧасти(ЭтотОбъект, "СтоимостьИАмортизация", "Дата", Отказ,, "МЕСЯЦ");
	
	ШаблонСообщения = НСтр("ru = 'Не заполнена колонка ""%1"" в строке %2 списка ""Стоимость и амортизация""';
							|en = 'Column %1 in line %2 of the ""Cost and depreciation"" list is not filled'");
	
	Для Каждого ДанныеСтроки Из СтоимостьИАмортизация Цикл
		
		Если Не ЗначениеЗаполнено(ДанныеСтроки.Дата) Тогда
			
			ТекстСообщения = СтрШаблон(ШаблонСообщения, НСтр("ru = 'Месяц';
															|en = 'Month'"), ДанныеСтроки.НомерСтроки);
			
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
				"СтоимостьИАмортизация", ДанныеСтроки.НомерСтроки, "МесяцТекст");
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле,, Отказ);
			
		КонецЕсли;
			
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьНаличиеПараметровНаДату(Отказ)

	СписокЗапросов = Новый СписокЗначений();
	
	Документы.ВводПервоначальныхСведенийДляРасчетаНалогаНаИмущество.ТекстЗапросаТаблицаВтДатыПараметровПоследние(СписокЗапросов);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДанныеРегистра.Регистратор КАК Регистратор,
	|	ДанныеРегистра.Период КАК Период,
	|	ДанныеРегистра.ОсновноеСредство КАК ОсновноеСредство,
	|	ДанныеРегистра.ОсновноеСредство.Представление КАК ОсновноеСредствоПредставление
	|ИЗ
	|	втДатыПараметровПоследние КАК втДатыПараметровПоследние
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыНачисленияНалогаНаИмущество КАК ДанныеРегистра
	|		ПО ДанныеРегистра.Период = НАЧАЛОПЕРИОДА(втДатыПараметровПоследние.Дата, ДЕНЬ)
	|			И ДанныеРегистра.Регистратор <> &Ссылка
	|			И ДанныеРегистра.Организация = &Организация
	|			И ДанныеРегистра.ОсновноеСредство = &ОсновноеСредство
	|			И ДанныеРегистра.Активность
	|";
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ОсновноеСредство", ОсновноеСредство);
	Запрос.УстановитьПараметр("НачалоГода", НачалоГода(КонецДня(Дата) + 1));
	
	ШаблонСообщения = НСтр("ru = 'Для основного средства ""%1"" уже введены параметры на %2 документом %3.
						   |Не поддерживается ввод параметров на одну дату разными документами.';
						   |en = 'For the ""%1"" fixed asset, the parameters for %2 are already entered based on the %3 document.
						   |You cannot enter parameters for the same date by different documents.'");
	
	Результаты = ОбщегоНазначенияУТ.ВыполнитьПакетЗапросов(Запрос, СписокЗапросов);
	Выборка = Результаты[Результаты.ВГраница()].Выбрать();

	Если Выборка.Следующий() Тогда
	
		ТекстСообщения = СтрШаблон(ШаблонСообщения, Выборка.ОсновноеСредствоПредставление, Формат(Выборка.Период, "ДЛФ=D;"), Выборка.Регистратор);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ОсновноеСредство",, Отказ);
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
