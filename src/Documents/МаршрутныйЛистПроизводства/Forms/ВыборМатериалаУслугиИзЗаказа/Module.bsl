//++ Устарело_Производство21
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МатериалыИУслуги.Номенклатура КАК Номенклатура,
	|	МатериалыИУслуги.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ МатериалыИУслуги
	|ИЗ
	|	&МатериалыИУслуги КАК МатериалыИУслуги
	|ГДЕ
	|	МатериалыИУслуги.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ТаблицаМатериалыИУслуги.Номенклатура КАК Номенклатура,
	|	ТаблицаМатериалыИУслуги.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ТаблицаМатериалыИУслуги.Номенклатура.ИспользованиеХарактеристик В (ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ИндивидуальныеДляНоменклатуры), ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеДляВидаНоменклатуры), ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеСДругимВидомНоменклатуры))
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ХарактеристикиИспользуются,
	|	ТаблицаМатериалыИУслуги.ПрименениеМатериала КАК ПрименениеМатериала
	|ИЗ
	|	Документ.ЗаказНаПроизводство.ЭтапыГрафик КАК ТаблицаЭтапыГрафик
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.МатериалыИУслуги КАК ТаблицаМатериалыИУслуги
	|		ПО (ТаблицаМатериалыИУслуги.Ссылка = ТаблицаЭтапыГрафик.Ссылка)
	|			И (ТаблицаМатериалыИУслуги.КлючСвязиЭтапы = ТаблицаЭтапыГрафик.КлючСвязиЭтапы)
	|		ЛЕВОЕ СОЕДИНЕНИЕ МатериалыИУслуги КАК МатериалыИУслуги
	|		ПО (МатериалыИУслуги.Номенклатура = ТаблицаМатериалыИУслуги.Номенклатура)
	|			И (МатериалыИУслуги.Характеристика = ТаблицаМатериалыИУслуги.Характеристика)
	|ГДЕ
	|	ТаблицаЭтапыГрафик.Ссылка = &Ссылка
	|	И ТаблицаЭтапыГрафик.КодСтроки = &КодСтрокиЭтапыГрафик
	|	И МатериалыИУслуги.Номенклатура ЕСТЬ NULL 
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура,
	|	Характеристика";
	
	Запрос.УстановитьПараметр("Ссылка", Параметры.Распоряжение);
	Запрос.УстановитьПараметр("КодСтрокиЭтапыГрафик", Параметры.КодСтрокиЭтапыГрафик);
	Запрос.УстановитьПараметр("МатериалыИУслуги", Параметры.МатериалыИУслуги.Выгрузить(,"Номенклатура,Характеристика"));
	
	ДанныеЗаказа.Загрузить(Запрос.Выполнить().Выгрузить());

	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДанныеЗаказа

&НаКлиенте
Процедура ДанныеЗаказаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыбратьСтроку();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаВыбрать(Команда)
	
	ВыбратьСтроку();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	//

	НоменклатураСервер.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(ЭтаФорма, 
																			 "ДанныеЗаказаХарактеристика",
																		     "ДанныеЗаказа.ХарактеристикиИспользуются");

КонецПроцедуры

#Область Прочее

&НаКлиенте
Процедура ВыбратьСтроку()

	ТекущиеДанные = Элементы.ДанныеЗаказа.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Закрыть(Новый Структура("Номенклатура,Характеристика", ТекущиеДанные.Номенклатура, ТекущиеДанные.Характеристика));
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
//-- Устарело_Производство21