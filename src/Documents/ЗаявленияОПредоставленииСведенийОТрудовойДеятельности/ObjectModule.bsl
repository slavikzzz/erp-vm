#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	УникальныеСотрудники = Новый Соответствие;
	ЗаявленияСотрудников = ЭлектронныеТрудовыеКнижки.ЗаявленияСотрудниковОВеденииТрудовыхКнижек(
		Истина, Организация, Дата, Сотрудники.Выгрузить(, "Сотрудник").ВыгрузитьКолонку("Сотрудник"), Ссылка);
	
	Для Каждого ДанныеСотрудника Из Сотрудники Цикл 
		
		Если ЗначениеЗаполнено(ДанныеСотрудника.Сотрудник) Тогда
			
			Если УникальныеСотрудники[ДанныеСотрудника.Сотрудник] <> Неопределено Тогда
				
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Строка %1: Сотрудник %2 уже был введен ранее в строке %3.';
						|en = 'Line %1: Employee %2 is already filled in line %3.'"),
					ДанныеСотрудника.НомерСтроки, ДанныеСотрудника.Сотрудник, УникальныеСотрудники[ДанныеСотрудника.Сотрудник]);
				
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Ссылка,
					"Объект.Сотрудники[" + Формат(ДанныеСотрудника.НомерСтроки - 1, "ЧН=0; ЧГ=0") + "].Сотрудник" , , Отказ);
				
			Иначе
				УникальныеСотрудники.Вставить(ДанныеСотрудника.Сотрудник, ДанныеСотрудника.НомерСтроки);
			КонецЕсли;
			
		КонецЕсли;
		
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("ФизическоеЛицо", ДанныеСотрудника.Сотрудник);
		ЗаявленияСотрудника = ЗаявленияСотрудников.Скопировать(СтруктураПоиска);
		
		Для Каждого СтрокаЗаявления Из ЗаявленияСотрудника Цикл
			Если ЗначениеЗаполнено(СтрокаЗаявления.Дата) И НачалоДня(СтрокаЗаявления.Дата) = НачалоДня(Дата) Тогда
				
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Строка %1: Сотруднику %2 на %3 уже оформлено заявление на предоставление сведений о трудовой деятельности.';
						|en = 'Line %1: Employee %2 has already filled in an application for providing information about labor activity as of %3 .'"),
					ДанныеСотрудника.НомерСтроки, ДанныеСотрудника.Сотрудник, Формат(Дата, "ДФ=dd.MM.yyyy"));
				
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Ссылка,
					"Объект.Сотрудники[" + Формат(ДанныеСотрудника.НомерСтроки - 1, "ЧН=0; ЧГ=0") + "].Сотрудник" , , Отказ);
				
			КонецЕсли;
		КонецЦикла;
		
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("ФизическоеЛицо", ДанныеСотрудника.Сотрудник);
		СтруктураПоиска.Вставить("ВидЗаявления", Перечисления.ВидыЗаявленийОПредоставленииСведенийОТрудовойДеятельности.СведенияОТрудовойДеятельностиВЭлектроннойФорме);
		
		ЗаявленияНаВедениеВЭлектроннойФорме = ЗаявленияСотрудника.НайтиСтроки(СтруктураПоиска);
		Если ЗаявленияНаВедениеВЭлектроннойФорме.Количество() > 0 Тогда
			
			ЗаявлениеНаВедениеВЭлектроннойФорме = ЗаявленияНаВедениеВЭлектроннойФорме[0];
			ДатаЗаявленияНаВедениеВЭлектроннойФорме = ЗаявлениеНаВедениеВЭлектроннойФорме.ПериодЗаписи;
			ЗаявленияНаВедениеВЭлектроннойФормеДействует = Не ЗаявлениеНаВедениеВЭлектроннойФорме.Отозвано;
			РегистраторЗаявленияНаВедениеВЭлектроннойФорме = ЗаявлениеНаВедениеВЭлектроннойФорме.Регистратор;
			
		Иначе
			
			ДатаЗаявленияНаВедениеВЭлектроннойФорме = '00010101';
			ЗаявленияНаВедениеВЭлектроннойФормеДействует = Ложь;
			РегистраторЗаявленияНаВедениеВЭлектроннойФорме = Неопределено;
			
		КонецЕсли;
		
		Если ДанныеСотрудника.ВидЗаявления = Перечисления.ВидыЗаявленийОПредоставленииСведенийОТрудовойДеятельности.СведенияОТрудовойДеятельностиВЭлектроннойФорме Тогда
			
			Если ЗаявленияНаВедениеВЭлектроннойФормеДействует Тогда
				
				Если ДанныеСотрудника.ВидЗаявления = Перечисления.ВидыЗаявленийОПредоставленииСведенийОТрудовойДеятельности.СведенияОТрудовойДеятельностиВЭлектроннойФорме Тогда
					
					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Строка %1: Сотруднику %2 уже оформлено заявление на предоставление сведений о трудовой деятельности в электронной форме в документе %3.';
							|en = 'Line %1: Employee %2 has already received an application for the provision of information on labor activity in electronic form in document %3.'"),
						ДанныеСотрудника.НомерСтроки, ДанныеСотрудника.Сотрудник, РегистраторЗаявленияНаВедениеВЭлектроннойФорме);
					
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, РегистраторЗаявленияНаВедениеВЭлектроннойФорме, , , Отказ);
					
				КонецЕсли;
				
			КонецЕсли;
			
		Иначе
			
			СтруктураПоиска.Вставить("ВидЗаявления", Перечисления.ВидыЗаявленийОПредоставленииСведенийОТрудовойДеятельности.ВедениеБумажнойТрудовойКнижки);
			ЗаявленияНаВедениеВБумажнойФорме = ЗаявленияСотрудников.НайтиСтроки(СтруктураПоиска);
			
			Если ЗаявленияНаВедениеВБумажнойФорме.Количество() > 0 Тогда
				
				ЗаявлениеНаВедениеВБумажнойФорме = ЗаявленияНаВедениеВБумажнойФорме[0];
				ДатаЗаявленияНаВедениеВБумажнойФорме = ЗаявлениеНаВедениеВБумажнойФорме.ПериодЗаписи;
				ЗаявлениеНаВедениеВБумажнойФормеДействует = Не ЗаявлениеНаВедениеВБумажнойФорме.Отозвано;
				РегистраторЗаявленияНаВедениеВБумажнойФорме = ЗаявлениеНаВедениеВБумажнойФорме.Регистратор;
				
			Иначе
				
				ДатаЗаявленияНаВедениеВБумажнойФорме = '00010101';
				ЗаявлениеНаВедениеВБумажнойФормеДействует = Ложь;
				РегистраторЗаявленияНаВедениеВБумажнойФорме = Неопределено;
				
			КонецЕсли;
			
			Если ЗаявленияНаВедениеВЭлектроннойФормеДействует Тогда
				
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Строка %1: Сотруднику %2 перед оформлением заявления на ведение бумажной трудовой книжки необходимо отозвать заявление на предоставление сведений о трудовой деятельности в электронной форме %3.';
						|en = 'Line %1: Employee %2, before filling out an application for maintaining a paper employment record book, must revoke the application for the provision of information on labor activity in electronic form %3.'"),
					ДанныеСотрудника.НомерСтроки, ДанныеСотрудника.Сотрудник, РегистраторЗаявленияНаВедениеВЭлектроннойФорме);
				
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, РегистраторЗаявленияНаВедениеВЭлектроннойФорме, , , Отказ);
				
			ИначеЕсли ЗаявлениеНаВедениеВБумажнойФормеДействует Тогда
				
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Строка %1: Сотруднику %2 уже оформлено заявление на ведение бумажной трудовой книжки в документе %3.';
						|en = 'Line %1: Employee %2 has already received an application for maintaining a hard-copy employment record book in a document %3.'"),
					ДанныеСотрудника.НомерСтроки, ДанныеСотрудника.Сотрудник, РегистраторЗаявленияНаВедениеВБумажнойФорме);
				
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, РегистраторЗаявленияНаВедениеВБумажнойФорме, , , Отказ);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	ДанныеДляПроведения = Документы.ЗаявленияОПредоставленииСведенийОТрудовойДеятельности.ДанныеДляПроведенияДокумента(Ссылка);
	ЭлектронныеТрудовыеКнижки.СформироватьДвиженияЗаявленийОВеденииТрудовыхКнижек(
		Движения.ЗаявленияОВеденииТрудовыхКнижек, ДанныеДляПроведения.ЗаявленияОВеденииТрудовыхКнижек);
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли