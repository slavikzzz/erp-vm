#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
 	ИнициализироватьДокумент(ДанныеЗаполнения);

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроверитьВведенныеДокументыМесяца(Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	
	Дата = ?(ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("Период") И ЗначениеЗаполнено(ДанныеЗаполнения.Период),
		КонецМесяца(ДанныеЗаполнения.Период), КонецМесяца(ТекущаяДатаСеанса()));
	
	Если ТипЗнч(ДанныеЗаполнения) <> Тип("Структура")
		Или Не ДанныеЗаполнения.Свойство("Организация")
		Или Не ЗначениеЗаполнено(ДанныеЗаполнения.Организация) Тогда
		Организация = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ТекущаяОрганизация", "");
		Организация = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	КонецЕсли;
	
	Ответственный = Пользователи.ТекущийПользователь();
	
КонецПроцедуры

Процедура ПроверитьВведенныеДокументыМесяца(Отказ)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СведенияОСреднесписочнойЧисленности.Ссылка КАК Документ,
		|	СведенияОСреднесписочнойЧисленности.Организация
		|ИЗ
		|	Документ.СтатистикаПерсонала КАК СведенияОСреднесписочнойЧисленности
		|ГДЕ
		|	СведенияОСреднесписочнойЧисленности.ПериодРегистрации = &ПериодРегистрации
		|	И СведенияОСреднесписочнойЧисленности.Организация = &Организация
		|	И СведенияОСреднесписочнойЧисленности.Проведен
		|	И СведенияОСреднесписочнойЧисленности.Ссылка <> &ТекущийДокумент";
	
	Запрос.УстановитьПараметр("ТекущийДокумент", Ссылка);
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Отказ = Истина;
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			ТекстСообщения = НСтр("ru = 'Для %1 в данном периоде уже введен документ %2. Необходимо отменить проведение предыдущего документа';
									|en = 'The document %2 has already been entered for %1 in this period. Cancel posting of the previous document'");
			ОбщегоНазначения.СообщитьПользователю(
				СтрШаблон(ТекстСообщения, ВыборкаДетальныеЗаписи.Организация, ВыборкаДетальныеЗаписи.Документ)
				,,,,Отказ);
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли