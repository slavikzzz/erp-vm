#Область ОписаниеПеременных

&НаКлиенте
Перем НомерТекущейСтроиЗаписиОСтаже;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаписиОСтажеТекст = НСтр("ru = 'Записи о стаже';
							|en = 'Records of service'");
	Если Параметры.Ключ.Пустая() Тогда
		Объект.КорректируемыйПериод = НачалоГода(ДобавитьМесяц(ТекущаяДатаСеанса(), -12));
		Объект.ОкончаниеКорректируемогоПериода = КонецГода(Объект.КорректируемыйПериод);
		Объект.ОтчетныйПериод = Макс(НачалоГода(ТекущаяДатаСеанса()), '20170101');
		Объект.ОкончаниеОтчетногоПериода = КонецГода(Объект.ОтчетныйПериод);
		Объект.ТипСведений = Перечисления.ТипыСведенийСЗВ_КОРР.Корректирующая;
		ЗарплатаКадры.ЗаполнитьПервоначальныеЗначенияВФорме(ЭтотОбъект, ЗапрашиваемыеЗначенияПервоначальногоЗаполнения());
		ЗаполнитьСведенияОбОрганизацииВКорректируемомПериоде();
		ПриПолученииДанныхНаСервере();
	КонецЕсли;
	
	Если Не ЧтениеБезОграничений И ЗначениеЗаполнено(Объект.КорректируемыйПериод) И Объект.КорректируемыйПериод < Дата(2017, 1, 1) Тогда 
		ТекстСообщения = НСтр("ru = 'Недоступен просмотр документов, корректирующих периоды до 2017 года.';
								|en = 'Cannot view documents adjusting periods before 2017.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
		Возврат;
	КонецЕсли;
	
	Если Не ИзменениеБезОграничений И ЗначениеЗаполнено(Объект.КорректируемыйПериод) И Объект.КорректируемыйПериод < Дата(2017, 1, 1) Тогда 
		ТолькоПросмотр = Истина;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	// ПанельОтправкиВКонтролирующиеОрганы
	РегламентированнаяОтчетностьКлиентСервер.ПриИнициализацииФормыРегламентированногоОтчета(ЭтаФорма, "ПФР");
	// Конец ПанельОтправкиВКонтролирующиеОрганы
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриПолученииДанныхНаСервере();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененыДанныеСотрудникаСЗВ_КОРР" И Источник.ВладелецФормы = ЭтаФорма Тогда
		ОбработатьИзменениеДанныхСотрудника(Параметр.АдресВоВременномХранилище);
	КонецЕсли;
	
	Если ИмяСобытия = "Завершение отправки в контролирующий орган" 
		И ТипЗнч(Параметр) = Тип("Структура")
		И Параметр.Ссылка = Объект.Ссылка Тогда
	    ПослеОтправкиВПФР();
	КонецЕсли;
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	ТекущийОбъект = РеквизитФормыВЗначение("Объект", Тип("ДокументОбъект.ДанныеОКорректировкеСведенийЗастрахованныхЛицСЗВ_КОРР"));
	Если Не ТекущийОбъект.ПроверитьЗаполнение() Тогда 
		Отказ = Истина;
	КонецЕсли;	
	
	ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(ПроверяемыеРеквизиты, "Объект");
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// ПанельОтправкиВКонтролирующиеОрганы
	СохранитьСтатусОтправки(ЭтотОбъект, Объект.Ссылка);
	// Конец ПанельОтправкиВКонтролирующиеОрганы
	
	УстановитьСвойстваЭлементовФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтчетныйПериодРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	ПерсонифицированныйУчетКлиент.ПериодОтчетаРегулирование(Объект.ОтчетныйПериод, Объект.ОкончаниеОтчетногоПериода, ПериодСтрока, Направление, '20170101', '20171231');
	
КонецПроцедуры

&НаКлиенте
Процедура КорректируемыйПериодРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.КлючевыеРеквизитыЗаполненияФормыОчиститьТаблицы(ЭтаФорма);
	МинимальныйПериод = ?(ИзменениеБезОграничений, '00010101', '20170101');
	МинимальныйПериодОкончание = ?(ИзменениеБезОграничений, '00010101', '20171231');
	ПерсонифицированныйУчетКлиент.ПериодОтчетаРегулирование(Объект.КорректируемыйПериод, Объект.ОкончаниеКорректируемогоПериода, КорректируемыйПериодСтрока, Направление, МинимальныйПериод, МинимальныйПериодОкончание);
	КорректируемыйПериодПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ТипСведенийПриИзменении(Элемент)
	
	ТипСведенийПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
    ЗарплатаКадрыКлиент.КлючевыеРеквизитыЗаполненияФормыОчиститьТаблицы(ЭтаФорма);
	
	ОрганизацияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура РегистрационныйНомерПФРвКорректируемыйПериодПриИзменении(Элемент)
	
	Если Не ИспользованиеРегистрационногоНомераСФР(Объект.ОкончаниеКорректируемогоПериода, Объект.Организация) Тогда  
		ПерсонифицированныйУчетКлиентСервер.УстановитьФорматРегистрационногоНомераПФР(Объект.РегистрационныйНомерПФРвКорректируемыйПериод);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФлагБлокировкиДокументаПриИзменении(Элемент)
	
	ФлагБлокировкиДокументаПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПрежнийСтраховательНаименованиеПолноеПриИзменении(Элемент)
	
	УстановитьЗаголовокГруппыПрежнегоСтрахователя(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрежнийСтраховательРегистрационныйНомерПФРПриИзменении(Элемент)
	
	УстановитьЗаголовокГруппыПрежнегоСтрахователя(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрежнийСтраховательИННПриИзменении(Элемент)
	
	УстановитьЗаголовокГруппыПрежнегоСтрахователя(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрежнийСтраховательКПППриИзменении(Элемент)
	
	УстановитьЗаголовокГруппыПрежнегоСтрахователя(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСотрудники

&НаКлиенте
Процедура СотрудникиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Сотрудники.ТекущиеДанные;
	Если Не ЗначениеЗаполнено(ТекущиеДанные.Сотрудник) Тогда
		Возврат;
	КонецЕсли;	
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФормуРедактированияКарточкиДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если Копирование Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПередУдалением(Элемент, Отказ)
	
	СотрудникиПередУдалениемНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ТекущиеДанные = Элементы.Сотрудники.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекущиеДанные.Сотрудник) Тогда 
		Возврат;
	КонецЕсли;
	
	СотрудникиПриОкончанииРедактированияНаСервере(ТекущиеДанные.Сотрудник);
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) <> Тип("Массив") Тогда
		Возврат;
	КонецЕсли;	
	
	СтандартнаяОбработка = Ложь;
	ЗаполнитьДанныеСотрудников(ВыбранноеЗначение);
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПослеУдаления(Элемент)
	
	ЗарплатаКадрыКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(
		ЭтаФорма, , 
		КлючевыеРеквизитыОписаниеКлючевыхРеквизитов(), 
		КлючевыеРеквизитыТаблицыОчищаемыеПриИзменении());
		
	ПриИзмененииРеквизитовОпределяющихОграниченияНаУровнеЗаписей();
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиСотрудникПриИзменении(Элемент)
	
	СотрудникиСотрудникПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНачисленныеУплаченныеВзносы

&НаКлиенте
Процедура НачисленныеУплаченныеВзносыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ЗарплатаКадрыКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленныеУплаченныеВзносыПослеУдаления(Элемент)
	
	ЗарплатаКадрыКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(
		ЭтаФорма, , 
		КлючевыеРеквизитыОписаниеКлючевыхРеквизитов(), 
		КлючевыеРеквизитыТаблицыОчищаемыеПриИзменении());
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУплаченныеВзносыРасшифровка

&НаКлиенте
Процедура УплаченныеВзносыРасшифровкаПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ЗарплатаКадрыКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура УплаченныеВзносыРасшифровкаПослеУдаления(Элемент)
	
	ЗарплатаКадрыКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(
		ЭтаФорма, , 
		КлючевыеРеквизитыОписаниеКлючевыхРеквизитов(), 
		КлючевыеРеквизитыТаблицыОчищаемыеПриИзменении());
	
КонецПроцедуры

&НаКлиенте
Процедура УплаченныеВзносыРасшифровкаГодПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.УплаченныеВзносыРасшифровка.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.Год <= 2013 Тогда 
		ТекущиеДанные.ПоТарифуСтраховыхВзносов = 0;
	КонецЕсли;
	
	Если ТекущиеДанные.Год <= 2001 Или ТекущиеДанные.Год >= 2014 Тогда 
		ТекущиеДанные.НаСтраховуюПенсию = 0;
		ТекущиеДанные.НаНакопительнуюПенсию = 0;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	Оповещение = Новый ОписаниеОповещения("ВыполнитьПодключаемуюКомандуЗавершение", ЭтотОбъект, Команда);
	ПроверитьСЗапросомДальнейшегоДействия(Оповещение);
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПодключаемуюКомандуЗавершение(Результат, Команда) Экспорт
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура Проверить(Команда)
	
	ОчиститьСообщения();

	Отказ = Ложь;
	ПроверкаЗаполненияДокумента(Отказ);
	ПроверкаСтороннимиПрограммами(Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура Подбор(Команда)
	
	ПерсонифицированныйУчетКлиент.ПодобратьФизическихЛиц(Элементы.Сотрудники, ОрганизацияДляПодбора, АдресСпискаПодобранныхСотрудников());
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьНаДиск(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ЗаписатьНаДискЗавершение", ЭтотОбъект);	
	ПроверитьСЗапросомДальнейшегоДействия(Оповещение);			
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьНаДискЗавершение(Результат, Параметры) Экспорт
	
	Если Модифицированность Тогда
		Записать();
	КонецЕсли;

	ДанныеФайла = ПолучитьДанныеФайлаНаСервере(Объект.Ссылка, УникальныйИдентификатор);
	РаботаСФайламиКлиент.СохранитьФайлКак(ДанныеФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФайл(Команда)
	
	Если Модифицированность Тогда
		Записать();
	КонецЕсли;

	ДанныеФайла = ПолучитьДанныеФайлаНаСервере(Объект.Ссылка, УникальныйИдентификатор);
	Если ДанныеФайла <> Неопределено Тогда
		РаботаСФайламиКлиент.ОткрытьФайл(ДанныеФайла, Ложь);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВИнтернете(Команда)
	
	Если Модифицированность Тогда
		Записать();
	КонецЕсли;

	РегламентированнаяОтчетностьКлиент.ПроверитьВИнтернете(ЭтаФорма, "ПФР");	
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьВПФР(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ОтправитьВКонтролирующийОрганЗавершение", ЭтотОбъект);	
	ПроверитьСЗапросомДальнейшегоДействия(Оповещение);	
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьВКонтролирующийОрганЗавершение(Результат, Параметры) Экспорт
	
	Если Модифицированность Тогда
		Записать();
	КонецЕсли;

	РегламентированнаяОтчетностьКлиент.ПриНажатииНаКнопкуОтправкиВКонтролирующийОрган(ЭтаФорма, "ПФР");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#Область ПанельОтправкиВКонтролирующиеОрганы

&НаКлиенте
Процедура ОбновитьОтправку(Команда)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОбновитьОтправкуИзПанелиОтправки(ЭтаФорма, "ПФР");
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаПротоколНажатие(Элемент)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьПротоколИзПанелиОтправки(ЭтаФорма, "ПФР");
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьНеотправленноеИзвещение(Команда)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОтправитьНеотправленноеИзвещениеИзПанелиОтправки(ЭтаФорма, "ПФР");
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыОтправкиНажатие(Элемент)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьСостояниеОтправкиИзПанелиОтправки(ЭтаФорма, "ПФР");
КонецПроцедуры

&НаКлиенте
Процедура КритическиеОшибкиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылка, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьКритическиеОшибкиИзПанелиОтправки(ЭтаФорма, "ПФР");
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаНаименованиеЭтапаНажатие(Элемент)
	
	ПараметрыИзменения = Новый Структура;
	ПараметрыИзменения.Вставить("Форма", ЭтаФорма);
	ПараметрыИзменения.Вставить("Организация", Объект.Организация);
	ПараметрыИзменения.Вставить("КонтролирующийОрган",
		ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ПФР"));
	
	РегламентированнаяОтчетностьКлиент.ИзменитьСтатусОтправки(ПараметрыИзменения);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьСтатусОтправки(Форма, Ссылка)
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Форма", Форма);
	СтруктураПараметров.Вставить("СсылкаНаОбъект", Ссылка);
	
	ИнтерфейсыВзаимодействияБРО.СохранитьСтатусОтправки(СтруктураПараметров);
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура ПриПолученииДанныхНаСервере()
	
	УстановитьПраваДоступаНаДокумент();
	
	ФлагБлокировкиДокумента = Объект.ДокументПринятВПФР;	
	
	ДанныеПериода = ПерсонифицированныйУчетКлиентСервер.ДанныеОтчетногоПериода(Объект.ОтчетныйПериод, Объект.ОкончаниеОтчетногоПериода);
	ПериодСтрока = ДанныеПериода.Представление;
	
	ДанныеКорректируемогоПериода = ПерсонифицированныйУчетКлиентСервер.ДанныеОтчетногоПериода(Объект.КорректируемыйПериод, Объект.ОкончаниеКорректируемогоПериода);
	КорректируемыйПериодСтрока = ДанныеКорректируемогоПериода.Представление;
	
	ОтправленВПФР = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ОбъектОтправлялсяВКонтролирующиеОрганы(Объект.Ссылка);
	
	УстановитьСвойстваЭлементовФормы();
	УстановитьДоступностьДанныхФормы();
	УстановитьСписокВыбораВидаВзносов(ЭтаФорма);
	
	ЗарплатаКадры.КлючевыеРеквизитыЗаполненияФормыЗаполнитьПредупреждения(ЭтаФорма, КлючевыеРеквизитыОписаниеКлючевыхРеквизитов());
	ЗарплатаКадрыКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтаФорма, , КлючевыеРеквизитыОписаниеКлючевыхРеквизитов(), КлючевыеРеквизитыТаблицыОчищаемыеПриИзменении());
	
	ПерсонифицированныйУчет.УстановитьСвязиПараметровВыбораСотрудников(Элементы.СотрудникиСотрудник);
	
	ЗаполнитьОрганизациюДляПодбораСотрудников(Объект);
	
КонецПроцедуры

&НаСервере
Процедура ПослеОтправкиВПФР()
	
	ОтправленВПФР = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ОбъектОтправлялсяВКонтролирующиеОрганы(Объект.Ссылка);
	УстановитьСвойстваЭлементовФормы();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьДанныхФормы()
	
	Если Объект.ДокументПринятВПФР Тогда  
		ТолькоПросмотр = Истина;	
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗапрашиваемыеЗначенияПервоначальногоЗаполнения()
	
	ЗапрашиваемыеЗначения = ЗапрашиваемыеЗначенияЗаполненияПоОрганизации();
	ЗапрашиваемыеЗначения.Вставить("Организация", "Объект.Организация");
	ЗапрашиваемыеЗначения.Вставить("Ответственный", "Объект.Ответственный");
	
	Возврат ЗапрашиваемыеЗначения;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗапрашиваемыеЗначенияЗаполненияПоОрганизации()
	
	ЗапрашиваемыеЗначения = Новый Структура;
	ЗапрашиваемыеЗначения.Вставить("Руководитель", "Объект.Руководитель");
	ЗапрашиваемыеЗначения.Вставить("ДолжностьРуководителя", "Объект.ДолжностьРуководителя");
	
	Возврат ЗапрашиваемыеЗначения;
	
КонецФункции 	

&НаСервере
Процедура СотрудникиСотрудникПриИзмененииНаСервере()
	
	ДанныеСотрудника = Объект.Сотрудники.НайтиПоИдентификатору(Элементы.Сотрудники.ТекущаяСтрока);
	СписокСотрудников = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДанныеСотрудника.Сотрудник);
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ОкончаниеПериода = КонецДня(Объект.ОкончаниеКорректируемогоПериода);
	Документы.ДанныеОКорректировкеСведенийЗастрахованныхЛицСЗВ_КОРР.СоздатьВТПерсональныеДанныеСотрудников(
		МенеджерВременныхТаблиц, Объект.Организация, Объект.КорректируемыйПериод, ОкончаниеПериода, СписокСотрудников);
		
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("СписокСотрудников", СписокСотрудников);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ФизическиеЛица.Ссылка КАК Сотрудник,
	               |	ФизическиеЛица.Фамилия,
	               |	ФизическиеЛица.Имя,
	               |	ФизическиеЛица.Отчество
	               |ПОМЕСТИТЬ ВТФизическиеЛица
	               |ИЗ
	               |	Справочник.ФизическиеЛица КАК ФизическиеЛица
	               |ГДЕ
	               |	ФизическиеЛица.Ссылка В(&СписокСотрудников)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ФизическиеЛица.Сотрудник,
	               |	ЕСТЬNULL(ПерсональныеДанныеСотрудников.Фамилия, ФизическиеЛица.Фамилия) КАК Фамилия,
	               |	ЕСТЬNULL(ПерсональныеДанныеСотрудников.Имя, ФизическиеЛица.Имя) КАК Имя,
	               |	ЕСТЬNULL(ПерсональныеДанныеСотрудников.Отчество, ФизическиеЛица.Отчество) КАК Отчество,
	               |	ПерсональныеДанныеСотрудников.*
	               |ИЗ
	               |	ВТФизическиеЛица КАК ФизическиеЛица
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТПерсональныеДанныеСотрудников КАК ПерсональныеДанныеСотрудников
	               |		ПО ФизическиеЛица.Сотрудник = ПерсональныеДанныеСотрудников.Сотрудник";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда  
		ЗаполнитьЗначенияСвойств(ДанныеСотрудника, Выборка);
	КонецЕсли;
	
	ПриИзмененииРеквизитовОпределяющихОграниченияНаУровнеЗаписей();
	
КонецПроцедуры	

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	ЗапрашиваемыеЗначения = ЗапрашиваемыеЗначенияЗаполненияПоОрганизации();
	Если Не ЗапрашиваемыеЗначения.Свойство("Организация") Тогда
		ЗапрашиваемыеЗначения.Вставить("Организация", "Объект.Организация");
	КонецЕсли; 
	
	ЗарплатаКадры.ЗаполнитьЗначенияВФорме(ЭтаФорма, ЗапрашиваемыеЗначения, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве("Организация"));	
	ЗаполнитьСведенияОбОрганизацииВКорректируемомПериоде();
	
	ПриИзмененииРеквизитовОпределяющихОграниченияНаУровнеЗаписей();
	ЗаполнитьОрганизациюДляПодбораСотрудников(Объект);
	
КонецПроцедуры

&НаСервере
Процедура ТипСведенийПриИзмененииНаСервере()
	
	Если Объект.ТипСведений = Перечисления.ТипыСведенийСЗВ_КОРР.Отменяющая Тогда
		Объект.ЗаписиОСтаже.Очистить();
		Объект.СведенияОЗаработке.Очистить();
		Объект.СведенияОЗаработкеНаВредныхИТяжелыхРаботах.Очистить();
		Объект.КорректирующиеСведения.Очистить();
		Объект.СведенияОбУплаченныхВзносах.Очистить();
		Для Каждого СтрокаСотрудника Из Объект.Сотрудники Цикл 
			СтрокаСотрудника.Заработок = 0;
			СтрокаСотрудника.Доначислено = 0;
			СтрокаСотрудника.Уплачено = 0;
		КонецЦикла;
	КонецЕсли;	
	
	Если Объект.ТипСведений <> Перечисления.ТипыСведенийСЗВ_КОРР.Особая Тогда 
		Объект.НачисленныеУплаченныеВзносы.Очистить();
		Объект.УплаченныеВзносыРасшифровка.Очистить();
	КонецЕсли;
	
	УстановитьСвойстваЭлементовФормы();
	
КонецПроцедуры

&НаСервере
Процедура ФлагБлокировкиДокументаПриИзмененииНаСервере()
	
	Модифицированность = Истина;
	Объект.ДокументПринятВПФР = ФлагБлокировкиДокумента;
	Если Не ФлагБлокировкиДокумента Тогда
		ТолькоПросмотр = Ложь;
	КонецЕсли;	
	
	УстановитьСвойстваЭлементовФормы();
	
КонецПроцедуры

&НаСервере
Процедура КорректируемыйПериодПриИзмененииНаСервере()
	
	УстановитьСписокВыбораВидаВзносов(ЭтаФорма);
	УстановитьСвойстваЭлементовФормы();
	ЗаполнитьСведенияОбОрганизацииВКорректируемомПериоде();
	
КонецПроцедуры

&НаСервере
Процедура СотрудникиПередУдалениемНаСервере()
	
	УдаляемыеСтроки = Элементы.Сотрудники.ВыделенныеСтроки;
	Для Каждого Идентификатор Из УдаляемыеСтроки Цикл
		СтрокаСотрудника = Объект.Сотрудники.НайтиПоИдентификатору(Идентификатор);
		ОчиститьСведенияОСотруднике(СтрокаСотрудника.Сотрудник);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СотрудникиПриОкончанииРедактированияНаСервере(Сотрудник)
	
	ДанныеОСтаже = Объект.ЗаписиОСтаже.НайтиСтроки(Новый Структура("Сотрудник", Сотрудник));
	Если ДанныеОСтаже.Количество() > 0 Тогда 
		Возврат;
	КонецЕсли;
	
	ЗарплатаКадрыКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтаФорма);
	ЗаполнитьДанныеОСтажеСотрудников(Сотрудник);
	ЗаполнитьДанныеОЗаработкеСотрудников(Сотрудник);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуРедактированияКарточкиДокумента()
	
	ДанныеТекущейСтроки = Элементы.Сотрудники.ТекущиеДанные;
	
	Если ДанныеТекущейСтроки <> Неопределено Тогда	
		
		ДанныеТекущегоДокументаПоСотрудникуВоВременноеХранилище();
		
		ПараметрыОткрытияФормы = Новый Структура;
		ПараметрыОткрытияФормы.Вставить("АдресВоВременномХранилище", АдресДанныхТекущегоДокументаВХранилище);
		ПараметрыОткрытияФормы.Вставить("РедактируемыйДокументСсылка", Объект.Ссылка);
		ПараметрыОткрытияФормы.Вставить("Сотрудник", ДанныеТекущейСтроки.Сотрудник);
		ПараметрыОткрытияФормы.Вставить("ТипСведений", Объект.ТипСведений);
		ПараметрыОткрытияФормы.Вставить("Организация", Объект.Организация);
		ПараметрыОткрытияФормы.Вставить("Период", Объект.КорректируемыйПериод);
		ПараметрыОткрытияФормы.Вставить("ОкончаниеПериода", КонецДня(Объект.ОкончаниеКорректируемогоПериода));
		ПараметрыОткрытияФормы.Вставить("ИсходныйНомерСтроки", 0);
		ПараметрыОткрытияФормы.Вставить("ТолькоПросмотр", ТолькоПросмотр);
		ПараметрыОткрытияФормы.Вставить("ИзменениеБезОграничений", ИзменениеБезОграничений);
		ПараметрыОткрытияФормы.Вставить("ЧтениеБезОграничений", ЧтениеБезОграничений);
		ПараметрыОткрытияФормы.Вставить("НеОтображатьОшибки", Истина);
		
		ОткрытьФорму("Документ.ДанныеОКорректировкеСведенийЗастрахованныхЛицСЗВ_КОРР.Форма.ФормаРедактированияДанныхСотрудника", ПараметрыОткрытияФормы, ЭтаФорма);	
		
	КонецЕсли;
	
КонецПроцедуры	

&НаСервере
Процедура ДанныеТекущегоДокументаПоСотрудникуВоВременноеХранилище()
	
	Если Элементы.Сотрудники.ТекущаяСтрока = Неопределено Тогда
		АдресДанныхТекущегоДокументаВХранилище = "";
		Возврат;
	КонецЕсли;	
	
	ДанныеТекущейСтрокиПоСотруднику = Объект.Сотрудники.НайтиПоИдентификатору(Элементы.Сотрудники.ТекущаяСтрока);
	
	Если ДанныеТекущейСтрокиПоСотруднику = Неопределено Тогда
		АдресДанныхТекущегоДокументаВХранилище = "";
		Возврат;
	КонецЕсли;
	
	ДанныеСотрудника = Новый Структура;
	ДанныеСотрудника.Вставить("Сотрудник", ДанныеТекущейСтрокиПоСотруднику.Сотрудник);
	ДанныеСотрудника.Вставить("СтраховойНомерПФР", ДанныеТекущейСтрокиПоСотруднику.СтраховойНомерПФР);
	ДанныеСотрудника.Вставить("Фамилия", ДанныеТекущейСтрокиПоСотруднику.Фамилия);
	ДанныеСотрудника.Вставить("Имя", ДанныеТекущейСтрокиПоСотруднику.Имя);
	ДанныеСотрудника.Вставить("Отчество", ДанныеТекущейСтрокиПоСотруднику.Отчество);
	ДанныеСотрудника.Вставить("СотрудникУволен", ДанныеТекущейСтрокиПоСотруднику.СотрудникУволен);
	ДанныеСотрудника.Вставить("Заработок", ДанныеТекущейСтрокиПоСотруднику.Заработок);
	ДанныеСотрудника.Вставить("Доначислено", ДанныеТекущейСтрокиПоСотруднику.Доначислено);
	ДанныеСотрудника.Вставить("Уплачено", ДанныеТекущейСтрокиПоСотруднику.Уплачено);
	ДанныеСотрудника.Вставить("ТипДоговора", ДанныеТекущейСтрокиПоСотруднику.ТипДоговора);
	ДанныеСотрудника.Вставить("КатегорияЗастрахованныхЛиц", ДанныеТекущейСтрокиПоСотруднику.КатегорияЗастрахованныхЛиц);
	ДанныеСотрудника.Вставить("НомерДоговора", ДанныеТекущейСтрокиПоСотруднику.НомерДоговора);
	ДанныеСотрудника.Вставить("ДатаДоговора", ДанныеТекущейСтрокиПоСотруднику.ДатаДоговора);
	ДанныеСотрудника.Вставить("ДополнительныйТариф", ДанныеТекущейСтрокиПоСотруднику.ДополнительныйТариф);
	ДанныеСотрудника.Вставить("ДатаРождения", ДанныеТекущейСтрокиПоСотруднику.ДатаРождения);
	ДанныеСотрудника.Вставить("ИНН", ДанныеТекущейСтрокиПоСотруднику.ИНН);
	ДанныеСотрудника.Вставить("КодКатегории", ДанныеТекущейСтрокиПоСотруднику.КодКатегории);
	ДанныеСотрудника.Вставить("Гражданство", ДанныеТекущейСтрокиПоСотруднику.Гражданство);
	ДанныеСотрудника.Вставить("ФиксНачисленныеВзносы", ДанныеТекущейСтрокиПоСотруднику.ФиксНачисленныеВзносы);
	ДанныеСотрудника.Вставить("ФиксУплаченныеВзносы", Ложь);
	ДанныеСотрудника.Вставить("ФиксСтаж", ДанныеТекущейСтрокиПоСотруднику.ФиксСтаж);
	ДанныеСотрудника.Вставить("ФиксЗаработок", ДанныеТекущейСтрокиПоСотруднику.ФиксЗаработок);
	ДанныеСотрудника.Вставить("СведенияОЗаработке", Новый Массив);
	ДанныеСотрудника.Вставить("СведенияОЗаработкеНаВредныхИТяжелыхРаботах", Новый Массив);
    ДанныеСотрудника.Вставить("ЗаписиОСтаже", Новый Массив);
	ДанныеСотрудника.Вставить("ИсходныйНомерСтроки", ДанныеТекущейСтрокиПоСотруднику.ИсходныйНомерСтроки);
	ДанныеСотрудника.Вставить("КорректирующиеСведения", Новый Массив);
	ДанныеСотрудника.Вставить("СведенияОбУплаченныхВзносах", Новый Массив);
	
	ЗначенияРеквизитовХраненияОшибок = ПерсонифицированныйУчетКлиентСервер.ЗначенияРеквизитовХраненияОшибокВСтруктуру(
											ЭтаФорма, 
											ДанныеТекущейСтрокиПоСотруднику,
											"Объект.Сотрудники");
											
	ДанныеСотрудника.Вставить("ЗначенияРеквизитовХраненияОшибок", ЗначенияРеквизитовХраненияОшибок);	
	
	СтруктураПоиска = Новый Структура("Сотрудник", ДанныеТекущейСтрокиПоСотруднику.Сотрудник);
	
	СтрокиСведенияОЗаработке = Объект.СведенияОЗаработке.НайтиСтроки(СтруктураПоиска);
	Для Каждого СтрокаЗаработок Из СтрокиСведенияОЗаработке Цикл
		СтруктураПолейСведенияОЗаработке = СтруктураПолейСведенияОЗаработке();
		ЗаполнитьЗначенияСвойств(СтруктураПолейСведенияОЗаработке, СтрокаЗаработок);
		ЗначенияРеквизитовХраненияОшибок = ПерсонифицированныйУчетКлиентСервер.ЗначенияРеквизитовХраненияОшибокВСтруктуру(
												ЭтаФорма, 
												СтрокаЗаработок,
												"Объект.СведенияОЗаработке");	
		ДанныеСотрудника.СведенияОЗаработке.Добавить(СтруктураПолейСведенияОЗаработке);
	КонецЦикла;	
	
	СтрокиСведенияОЗаработкеПоКлассамУсловий = Объект.СведенияОЗаработкеНаВредныхИТяжелыхРаботах.НайтиСтроки(СтруктураПоиска);
	Для Каждого СтрокаЗаработок Из СтрокиСведенияОЗаработкеПоКлассамУсловий Цикл
		СтруктураПолейСведенияОЗаработке = СтруктураПолейСведенияОЗаработкеНаВредныхИТяжелыхРаботах();
		ЗаполнитьЗначенияСвойств(СтруктураПолейСведенияОЗаработке, СтрокаЗаработок);
		ЗначенияРеквизитовХраненияОшибок = ПерсонифицированныйУчетКлиентСервер.ЗначенияРеквизитовХраненияОшибокВСтруктуру(
												ЭтаФорма, 
												СтрокаЗаработок,
												"Объект.СведенияОЗаработкеНаВредныхИТяжелыхРаботах");	
		ДанныеСотрудника.СведенияОЗаработкеНаВредныхИТяжелыхРаботах.Добавить(СтруктураПолейСведенияОЗаработке);
	КонецЦикла;	
	
	СтрокиЗаписиОСтаже = Объект.ЗаписиОСтаже.НайтиСтроки(СтруктураПоиска);
	Для Каждого СтрокаСтаж Из СтрокиЗаписиОСтаже Цикл
		СтруктураПолейЗаписиОСтаже = СтруктураПолейЗаписиОСтаже();
		ЗаполнитьЗначенияСвойств(СтруктураПолейЗаписиОСтаже, СтрокаСтаж);
		ЗначенияРеквизитовХраненияОшибок = ПерсонифицированныйУчетКлиентСервер.ЗначенияРеквизитовХраненияОшибокВСтруктуру(
												ЭтаФорма, 
												СтрокаСтаж,
												"Объект.ЗаписиОСтаже");	
		ДанныеСотрудника.ЗаписиОСтаже.Добавить(СтруктураПолейЗаписиОСтаже);
	КонецЦикла;	
	
	СтрокиДоначисленныхВзносов = Объект.КорректирующиеСведения.НайтиСтроки(СтруктураПоиска);
	Для Каждого СтрокаДоначисленныхВзносов Из СтрокиДоначисленныхВзносов Цикл
		СтруктураПолейДоначисленныхВзносов = СтруктураПолейДоначисленныхВзносов();
		ЗаполнитьЗначенияСвойств(СтруктураПолейДоначисленныхВзносов, СтрокаДоначисленныхВзносов);
		ДанныеСотрудника.КорректирующиеСведения.Добавить(СтруктураПолейДоначисленныхВзносов);
	КонецЦикла;	
	
	СтрокиСведенияОбУплаченныхВзносах = Объект.СведенияОбУплаченныхВзносах.НайтиСтроки(СтруктураПоиска);
	Для Каждого СтрокаУплаченныеВзносы Из СтрокиСведенияОбУплаченныхВзносах Цикл 
		СтруктураПолейСведенияОбУплаченныхВзносах = СтруктураПолейСведенияОбУплаченныхВзносах();
		ЗаполнитьЗначенияСвойств(СтруктураПолейСведенияОбУплаченныхВзносах, СтрокаУплаченныеВзносы);
		ДанныеСотрудника.СведенияОбУплаченныхВзносах.Добавить(СтруктураПолейСведенияОбУплаченныхВзносах);
	КонецЦикла;
	
	Если ЗначениеЗаполнено(АдресДанныхТекущегоДокументаВХранилище) Тогда
		ПоместитьВоВременноеХранилище(ДанныеСотрудника, АдресДанныхТекущегоДокументаВХранилище);	
	Иначе	
		АдресДанныхТекущегоДокументаВХранилище = ПоместитьВоВременноеХранилище(ДанныеСотрудника, УникальныйИдентификатор);
	КонецЕсли;	
	
КонецПроцедуры	

&НаСервере
Функция СтруктураПолейСведенияОЗаработке()
	
	СтруктураПолей = Новый Структура;
	СтруктураПолей.Вставить("КатегорияЗастрахованныхЛиц");
	СтруктураПолей.Вставить("КодМесяца");
	СтруктураПолей.Вставить("Заработок");
	СтруктураПолей.Вставить("ОблагаетсяВзносами");
	СтруктураПолей.Вставить("ОблагаетсяВзносамиДоПредельнойВеличины");
	СтруктураПолей.Вставить("ОблагаетсяВзносамиСвышеПредельнойВеличины");
	СтруктураПолей.Вставить("ПоДоговорамГПХДоПредельнойВеличины");
	СтруктураПолей.Вставить("ПоДоговорамГПХСвышеПредельнойВеличины");
	
	Возврат СтруктураПолей;
	
КонецФункции

&НаСервере
Функция СтруктураПолейСведенияОЗаработкеНаВредныхИТяжелыхРаботах()
	
	СтруктураПолей = Новый Структура;
	СтруктураПолей.Вставить("КодМесяца");
	СтруктураПолей.Вставить("КлассУсловийТруда");
	СтруктураПолей.Вставить("ОблагаетсяВзносамиЗаЗанятыхНаПодземныхИВредныхРаботах");
	СтруктураПолей.Вставить("ОблагаетсяВзносамиЗаЗанятыхНаТяжелыхИПрочихРаботах");
	
	Возврат СтруктураПолей;
	
КонецФункции

&НаСервере
Функция СтруктураПолейЗаписиОСтаже()
	
	СтруктураПолей = Новый Структура;
	СтруктураПолей.Вставить("НомерОсновнойЗаписи");
	СтруктураПолей.Вставить("НомерДополнительнойЗаписи");
	СтруктураПолей.Вставить("ДатаНачалаПериода");
	СтруктураПолей.Вставить("ДатаОкончанияПериода");
	СтруктураПолей.Вставить("ОсобыеУсловияТруда");
	СтруктураПолей.Вставить("КодПозицииСписка");
	СтруктураПолей.Вставить("ОснованиеИсчисляемогоСтажа");
	СтруктураПолей.Вставить("ПервыйПараметрИсчисляемогоСтажа");
	СтруктураПолей.Вставить("ВторойПараметрИсчисляемогоСтажа");
	СтруктураПолей.Вставить("ТретийПараметрИсчисляемогоСтажа");
	СтруктураПолей.Вставить("ОснованиеВыслугиЛет");
	СтруктураПолей.Вставить("ПервыйПараметрВыслугиЛет");
	СтруктураПолей.Вставить("ВторойПараметрВыслугиЛет");
	СтруктураПолей.Вставить("ТретийПараметрВыслугиЛет");
	СтруктураПолей.Вставить("ТерриториальныеУсловия");
	СтруктураПолей.Вставить("ПараметрТерриториальныхУсловий");
	СтруктураПолей.Вставить("ЗамещениеГосударственныхМуниципальныхДолжностей");
	СтруктураПолей.Вставить("НомерРабочегоМеста");
	СтруктураПолей.Вставить("КлассПодклассУсловийТруда");
	СтруктураПолей.Вставить("РайонныйКоэффициент");
	
	Возврат СтруктураПолей;
	
КонецФункции

&НаСервере
Функция СтруктураПолейДоначисленныхВзносов()
	
	СтруктураПолей = Новый Структура;
	СтруктураПолей.Вставить("КодМесяца");
	СтруктураПолей.Вставить("ДоначисленоНаОПС");
	СтруктураПолей.Вставить("ДоначисленоСтраховая");
	СтруктураПолей.Вставить("ДоначисленоНакопительная");
	СтруктураПолей.Вставить("УплачиваемыеСтрахователем");
	СтруктураПолей.Вставить("УплачиваемыеИзЗаработкаЗЛ");
	
	Возврат СтруктураПолей;
	
КонецФункции

&НаСервере
Функция СтруктураПолейСведенияОбУплаченныхВзносах()
	
	СтруктураПолей = Новый Структура;
	СтруктураПолей.Вставить("КодМесяца");
	СтруктураПолей.Вставить("УплаченоСтраховая");
	СтруктураПолей.Вставить("УплаченоНакопительная");
	
	Возврат СтруктураПолей;
	
КонецФункции

&НаСервере
Процедура УстановитьСвойстваЭлементовФормы()
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СотрудникиУплачено", "Видимость",
		Объект.КорректируемыйПериод < '20140101' И Объект.КорректируемыйПериод >= '20100101' И ЧтениеБезОграничений);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДанныеПоСтрахователюСтраница", "Видимость", ЧтениеБезОграничений);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДанныеПоСтрахователюСтраница", "Доступность",
		Объект.ТипСведений = Перечисления.ТипыСведенийСЗВ_КОРР.Особая);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СотрудникиТипДоговора", "Видимость", Год(Объект.КорректируемыйПериод) = 2010 Или Год(Объект.КорректируемыйПериод) = 2013);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СотрудникиКатегорияЗастрахованныхЛиц", "Видимость", Объект.КорректируемыйПериод < '20170101');
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СотрудникиЗаработок", "Видимость", Объект.КорректируемыйПериод < '20170101' И ЧтениеБезОграничений);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СотрудникиДоначислено", "Видимость", Объект.КорректируемыйПериод < '20170101' И ЧтениеБезОграничений);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаТребуетсяПерезаполнение", "Видимость", Не ФлагБлокировкиДокумента И Не Объект.АктуальныйФорматФайла);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Организация", "ТолькоПросмотр", ОтправленВПФР);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ОтчетныйПериод", "ТолькоПросмотр", ОтправленВПФР);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "КорректируемыйПериод", "ТолькоПросмотр", ОтправленВПФР);
	
	ЭтоЕФС1 = ПерсонифицированныйУчетКлиентСервер.ЭтоФормаЕФС1(Год(Объект.КорректируемыйПериод));
	ЭтаФорма.Заголовок = ?(ЭтоЕФС1, "", НСтр("ru = 'Корректировка сведений о страховом стаже, СЗВ-КОРР';
											|en = 'Adjustment of information on pensionable service, SZV-CORR'") + ?(ЗначениеЗаполнено(Объект.Ссылка), 
		" " + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 от %2';
																			|en = '%1 dated %2'"), Объект.Номер, Формат(Объект.Дата, "ДЛФ=Д")), " (" + НСтр("ru = 'создание';
																																						|en = 'create'")+ ")"));
	ЭтаФорма.АвтоЗаголовок = ЭтоЕФС1;
	
	ВыборТипаСведений = Новый Массив;
	Если ЭтоЕФС1 Тогда 
		ТипыСведений = Новый Массив;
		ТипыСведений.Добавить(Перечисления.ТипыСведенийСЗВ_КОРР.Корректирующая);
		ТипыСведений.Добавить(Перечисления.ТипыСведенийСЗВ_КОРР.Отменяющая);
		ВыборТипаСведений.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", Новый ФиксированныйМассив(ТипыСведений)));
	КонецЕсли;
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТипСведений", "ПараметрыВыбора", Новый ФиксированныйМассив(ВыборТипаСведений));
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСписокВыбораВидаВзносов(Форма)
	
	СписокВыбора = Форма.Элементы.НачисленныеУплаченныеВзносыВидСтраховыхВзносов.СписокВыбора;
	СписокВыбора.Очистить();
	
	ОтчетныйГод = Год(Форма.Объект.КорректируемыйПериод);
	Если ОтчетныйГод >=2014 Или ОтчетныйГод < 2002 Тогда 
		СписокВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВидыСтраховыхВзносовПФР.ПоТарифуСтраховыхВзносов"));
	КонецЕсли;
	
	СписокВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВидыСтраховыхВзносовПФР.НаСтраховуюПенсию"));
	СписокВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВидыСтраховыхВзносовПФР.НаНакопительнуюПенсию"));
		
КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеДанныхСотрудника(АдресВоВременномХранилище)
	
	ДанныеШапкиДокумента = Объект;
	
	ДанныеТекущегоДокумента = ПолучитьИзВременногоХранилища(АдресВоВременномХранилище);
	
	Если ДанныеТекущегоДокумента = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеТекущейСтрокиПоСотруднику = Неопределено;
	
	НайденныеСтроки = Объект.Сотрудники.НайтиСтроки(Новый Структура("Сотрудник", ДанныеТекущегоДокумента.Сотрудник));
	Если НайденныеСтроки.Количество() > 0 Тогда
		ДанныеТекущейСтрокиПоСотруднику = НайденныеСтроки[0];
	КонецЕсли;
	
	Если ДанныеТекущейСтрокиПоСотруднику = Неопределено  Тогда
		ВызватьИсключение НСтр("ru = 'В текущем документе не найдены данные по редактируемому сотруднику.';
								|en = 'Data on the edited employee is not found in the current document.'");
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ДанныеТекущейСтрокиПоСотруднику, ДанныеТекущегоДокумента);
	
	Заработок = 0;
	Доначислено = 0;
	Уплачено = 0;
	
	ОчиститьСведенияОСотруднике(ДанныеТекущейСтрокиПоСотруднику.Сотрудник);
	
	Для Каждого СтрокаЗаработок Из ДанныеТекущегоДокумента.СведенияОЗаработке Цикл
		СтрокаЗаработокОбъекта = Объект.СведенияОЗаработке.Добавить();
		СтрокаЗаработокОбъекта.Сотрудник = ДанныеТекущейСтрокиПоСотруднику.Сотрудник;
		ЗаполнитьЗначенияСвойств(СтрокаЗаработокОбъекта, СтрокаЗаработок);
		Заработок = Заработок + СтрокаЗаработок.Заработок;
	КонецЦикла;
		
	СтрокиСтажаПоСотруднику = Новый Массив;
	Для Каждого СтрокаСтаж Из ДанныеТекущегоДокумента.ЗаписиОСтаже Цикл
		СтрокаСтажОбъекта = Объект.ЗаписиОСтаже.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаСтажОбъекта, СтрокаСтаж);
		СтрокаСтажОбъекта.Сотрудник = ДанныеТекущейСтрокиПоСотруднику.Сотрудник;
		СтрокиСтажаПоСотруднику.Добавить(СтрокаСтажОбъекта);
	КонецЦикла;
	
	ПерсонифицированныйУчетКлиентСервер.ВыполнитьНумерациюЗаписейОСтаже(СтрокиСтажаПоСотруднику);
		
	Для Каждого СтрокаВредныйЗаработок Из ДанныеТекущегоДокумента.СведенияОЗаработкеНаВредныхИТяжелыхРаботах Цикл
		СтрокаВредныйЗаработокОбъекта = Объект.СведенияОЗаработкеНаВредныхИТяжелыхРаботах.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаВредныйЗаработокОбъекта, СтрокаВредныйЗаработок);
		СтрокаВредныйЗаработокОбъекта.Сотрудник = ДанныеТекущейСтрокиПоСотруднику.Сотрудник;
	КонецЦикла;
	
	Для Каждого СтрокаКорректирующихСведений Из ДанныеТекущегоДокумента.КорректирующиеСведения Цикл
		СтрокаКорректирующихСведенийОбъекта = Объект.КорректирующиеСведения.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаКорректирующихСведенийОбъекта, СтрокаКорректирующихСведений);
		СтрокаКорректирующихСведенийОбъекта.Сотрудник = ДанныеТекущейСтрокиПоСотруднику.Сотрудник;
		Доначислено = Доначислено + СтрокаКорректирующихСведений.ДоначисленоСтраховая + СтрокаКорректирующихСведений.ДоначисленоНакопительная
			+ СтрокаКорректирующихСведений.ДоначисленоНаОПС + СтрокаКорректирующихСведений.УплачиваемыеСтрахователем + СтрокаКорректирующихСведений.УплачиваемыеИзЗаработкаЗЛ;
	КонецЦикла;
	
	Для Каждого СтрокаУплаченныхВзносов Из ДанныеТекущегоДокумента.СведенияОбУплаченныхВзносах Цикл
		СтрокаУплаченныхВзносовОбъекта = Объект.СведенияОбУплаченныхВзносах.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаУплаченныхВзносовОбъекта, СтрокаУплаченныхВзносов);
		СтрокаУплаченныхВзносовОбъекта.Сотрудник = ДанныеТекущейСтрокиПоСотруднику.Сотрудник;
		Уплачено = Уплачено + СтрокаУплаченныхВзносов.УплаченоСтраховая + СтрокаУплаченныхВзносов.УплаченоНакопительная;
	КонецЦикла;
	
	ДанныеТекущейСтрокиПоСотруднику.Заработок = Заработок;
	ДанныеТекущейСтрокиПоСотруднику.Доначислено = Доначислено;
	ДанныеТекущейСтрокиПоСотруднику.Уплачено = Уплачено;
	
	Если ДанныеТекущегоДокумента.Модифицированность Тогда
		Модифицированность = Истина;
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ОчиститьСведенияОСотруднике(Сотрудник)
	
	СтруктураПоиска = Новый Структура("Сотрудник", Сотрудник);
	
	СтрокиЗаработка = Объект.СведенияОЗаработке.НайтиСтроки(СтруктураПоиска);
	Для Каждого СтрокаЗаработокСотрудника Из СтрокиЗаработка Цикл
		Объект.СведенияОЗаработке.Удалить(СтрокаЗаработокСотрудника);
	КонецЦикла;
	
	СтрокиСтажа = Объект.ЗаписиОСтаже.НайтиСтроки(СтруктураПоиска);
	Для Каждого СтрокаСтажСотрудника Из СтрокиСтажа Цикл
		Объект.ЗаписиОСтаже.Удалить(СтрокаСтажСотрудника);
	КонецЦикла;
	
	СтрокиВредногоЗаработка = Объект.СведенияОЗаработкеНаВредныхИТяжелыхРаботах.НайтиСтроки(СтруктураПоиска);
	Для Каждого СтрокаВредныйЗаработокСотрудника Из СтрокиВредногоЗаработка Цикл
		Объект.СведенияОЗаработкеНаВредныхИТяжелыхРаботах.Удалить(СтрокаВредныйЗаработокСотрудника);
	КонецЦикла;
	
	СтрокиКорректирующихСведений = Объект.КорректирующиеСведения.НайтиСтроки(СтруктураПоиска);
	Для Каждого СтрокаКорректирующихСведений Из СтрокиКорректирующихСведений Цикл
		Объект.КорректирующиеСведения.Удалить(СтрокаКорректирующихСведений);
	КонецЦикла;
	
	СтрокиУплаченныхВзносов = Объект.СведенияОбУплаченныхВзносах.НайтиСтроки(СтруктураПоиска);
	Для Каждого СтрокаУплаченныхВзносов Из СтрокиУплаченныхВзносов Цикл
		Объект.СведенияОбУплаченныхВзносах.Удалить(СтрокаУплаченныхВзносов);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПроверкаЗаполненияДокумента(Отказ = Ложь)
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ДокументОбъект.ПроверитьДанныеДокумента(Отказ);
	
	Если ПерсонифицированныйУчетКлиентСервер.ЭтоФормаЕФС1(Год(Объект.КорректируемыйПериод)) Тогда 
		ПерсонифицированныйУчет.ПроверитьВзносыПоДополнительнымТарифамСотрудников(ДокументОбъект.Организация, ДокументОбъект.Сотрудники, ДокументОбъект.КорректируемыйПериод, КонецГода(ДокументОбъект.КорректируемыйПериод), ДокументОбъект.ЗаписиОСтаже, Отказ);
	КонецЕсли;
	
КонецПроцедуры	

&НаКлиенте
Процедура ПроверитьСЗапросомДальнейшегоДействия(ОповещениеЗавершения = Неопределено)
	
	ОчиститьСообщения();
	
	ДополнительныеПараметры = Новый Структура("ОповещениеЗавершения", ОповещениеЗавершения);
	
	Если Не Объект.Проведен Или Модифицированность Тогда 
		ТекстВопроса = НСтр("ru = 'Для выполнения команды необходимо предварительно провести документ. Выполнить проведение документа и продолжить?';
							|en = 'Cannot run the command for unposted documents. Do you want to post the document and continue?'");
		Оповещение = Новый ОписаниеОповещения("ПроверитьСЗапросомДальнейшегоДействияПродолжение", ЭтотОбъект, ДополнительныеПараметры);
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Продолжить';
													|en = 'Continue'"));
		Кнопки.Добавить(КодВозвратаДиалога.Отмена);
		ПоказатьВопрос(Оповещение, ТекстВопроса, Кнопки);
	Иначе 
		ПроверитьСЗапросомДальнейшегоДействияПродолжение(КодВозвратаДиалога.Да, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры	

&НаКлиенте
Процедура ПроверитьСЗапросомДальнейшегоДействияПродолжение(Ответ, ДополнительныеПараметры) Экспорт 
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;			
	
	Если Не Объект.Проведен Или Модифицированность Тогда
		Если Не Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение)) Тогда 
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Отказ = Ложь;
	ПроверкаЗаполненияДокумента(Отказ);	
	
	Если Отказ Тогда 
		ТекстВопроса = НСтр("ru = 'В комплекте обнаружены ошибки.
							|Продолжить (не рекомендуется)?';
							|en = 'Errors were detected in the set.
							|Continue (not recommended)?'");
		Оповещение = Новый ОписаниеОповещения("ПроверитьСЗапросомДальнейшегоДействияЗавершение", ЭтотОбъект, ДополнительныеПараметры);					
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет, НСтр("ru = 'Предупреждение.';
																											|en = 'Warning.'"));
	Иначе 
		ПроверитьСЗапросомДальнейшегоДействияЗавершение(КодВозвратаДиалога.Да, ДополнительныеПараметры);				
	КонецЕсли;	
	
КонецПроцедуры	

&НаКлиенте
Процедура ПроверитьСЗапросомДальнейшегоДействияЗавершение(Ответ, ДополнительныеПараметры) Экспорт 
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;			
	
	Если ДополнительныеПараметры.ОповещениеЗавершения <> Неопределено Тогда 
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеЗавершения);
	КонецЕсли;
	
КонецПроцедуры	

&НаКлиенте
Процедура ПроверкаСтороннимиПрограммами(Отказ)
	
	Если Отказ Тогда
		ТекстВопроса = НСтр("ru = 'При проверке встроенной проверкой обнаружены ошибки.
		|Выполнить проверку сторонними программами?';
		|en = 'Errors occurred while checking with the integrated check.
		|Check with third-party applications?'")
	Иначе	
		ТекстВопроса = НСтр("ru = 'При проверке встроенной проверкой ошибок не обнаружено.
		|Выполнить проверку сторонними программами?';
		|en = 'Errors are not detected while checking with the integrated check.
		|Check with third-party applications?'");
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПроверкаСтороннимиПрограммамиЗавершение", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаСтороннимиПрограммамиЗавершение(Ответ, ДополнительныеПараметры) Экспорт 
	
	Если Ответ = КодВозвратаДиалога.Да Тогда 
		ПроверитьСтороннимиПрограммами();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьСтороннимиПрограммами()
	
	Если Модифицированность Тогда
		Записать();
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	
	ПроверяемыеОбъекты = Новый Массив;
	ПроверяемыеОбъекты.Добавить(Объект.Ссылка);
	
	ПараметрыОткрытия.Вставить("СсылкиНаПроверяемыеОбъекты", ПроверяемыеОбъекты);
	
	ОткрытьФорму("ОбщаяФорма.ПроверкаФайловОтчетностиПерсУчетаПФР", ПараметрыОткрытия, ЭтаФорма);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьДанныеФайлаНаСервере(Ссылка, УникальныйИдентификатор)
	
	Возврат ЗарплатаКадры.ПолучитьДанныеФайла(Ссылка, УникальныйИдентификатор);
	
КонецФункции

&НаСервере
Функция АдресСпискаПодобранныхСотрудников()
	
	Возврат ПоместитьВоВременноеХранилище(Объект.Сотрудники.Выгрузить(,"Сотрудник").ВыгрузитьКолонку("Сотрудник"), УникальныйИдентификатор);
	
КонецФункции

&НаСервере
Процедура ЗаполнитьДанныеСотрудников(СписокСотрудников)
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ОкончаниеПериода = КонецДня(Объект.ОкончаниеКорректируемогоПериода);
	Документы.ДанныеОКорректировкеСведенийЗастрахованныхЛицСЗВ_КОРР.СоздатьВТПерсональныеДанныеСотрудников(
		МенеджерВременныхТаблиц, Объект.Организация, Объект.КорректируемыйПериод, ОкончаниеПериода, СписокСотрудников);
		
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("СписокСотрудников", СписокСотрудников);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ФизическиеЛица.Ссылка КАК Сотрудник,
	               |	ФизическиеЛица.Фамилия,
	               |	ФизическиеЛица.Имя,
	               |	ФизическиеЛица.Отчество
	               |ПОМЕСТИТЬ ВТФизическиеЛица
	               |ИЗ
	               |	Справочник.ФизическиеЛица КАК ФизическиеЛица
	               |ГДЕ
	               |	ФизическиеЛица.Ссылка В(&СписокСотрудников)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ФизическиеЛица.Сотрудник,
	               |	ЕСТЬNULL(ПерсональныеДанныеСотрудников.Фамилия, ФизическиеЛица.Фамилия) КАК Фамилия,
	               |	ЕСТЬNULL(ПерсональныеДанныеСотрудников.Имя, ФизическиеЛица.Имя) КАК Имя,
	               |	ЕСТЬNULL(ПерсональныеДанныеСотрудников.Отчество, ФизическиеЛица.Отчество) КАК Отчество,
	               |	ПерсональныеДанныеСотрудников.*
	               |ИЗ
	               |	ВТФизическиеЛица КАК ФизическиеЛица
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТПерсональныеДанныеСотрудников КАК ПерсональныеДанныеСотрудников
	               |		ПО ФизическиеЛица.Сотрудник = ПерсональныеДанныеСотрудников.Сотрудник";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		НоваяСтрока = Объект.Сотрудники.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	КонецЦикла;
	
	ЗаполнитьДанныеОСтажеСотрудников(СписокСотрудников);
	ЗаполнитьДанныеОЗаработкеСотрудников(СписокСотрудников);
	
	ПриИзмененииРеквизитовОпределяющихОграниченияНаУровнеЗаписей();
	
	ЗарплатаКадрыКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеОСтажеСотрудников(Сотрудники)
	
	ЭтоЕФС1 = ПерсонифицированныйУчетКлиентСервер.ЭтоФормаЕФС1(Год(Объект.КорректируемыйПериод));
	Если Объект.ТипСведений = Перечисления.ТипыСведенийСЗВ_КОРР.Отменяющая И Не ЭтоЕФС1 Тогда
		Возврат;
	КонецЕсли;
	
	СписокСотрудников = ?(ТипЗнч(Сотрудники) = Тип("Массив"), Сотрудники, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудники));
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Документы.ДанныеОКорректировкеСведенийЗастрахованныхЛицСЗВ_КОРР.СоздатьВТДанныеСтажаПФР(МенеджерВременныхТаблиц,
		Объект.Организация,  Объект.КорректируемыйПериод, Объект.ОкончаниеКорректируемогоПериода, СписокСотрудников, Объект.Ссылка);
		
	ДоговорыГПХ = Новый Массив;
	ДоговорыГПХ.Добавить(Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.ДОГОВОР);
	ДоговорыГПХ.Добавить(Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.НЕОПЛДОГ);
	ДоговорыГПХ.Добавить(Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.НЕОПЛАВТ);
	
	ДатаПостановления322 = РегистрыСведений.ДатыВступленияВСилуНА.ДатаВступленияВСилуНА("ПостановлениеПравительстваРФот4марта2021Номер322");
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ДоговорыГПХ", ДоговорыГПХ);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ДанныеСтажаСотрудников.ФизическоеЛицо КАК Сотрудник,
	               |	ВЫБОР
	               |		КОГДА ДанныеСтажаСотрудников.ТретийПараметрИсчисляемогоСтажа В (&ДоговорыГПХ)
	               |			ТОГДА ИСТИНА
	               |		ИНАЧЕ ЛОЖЬ
	               |	КОНЕЦ КАК ДоговорГПХ,
	               |	ВЫБОР
	               |		КОГДА ТИПЗНАЧЕНИЯ(ДанныеСтажаСотрудников.ОснованиеВыслугиЛет) = ТИП(Справочник.ОснованияДосрочногоНазначенияПенсии)
	               |			ТОГДА ДанныеСтажаСотрудников.ОснованиеВыслугиЛет
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ОснованияДосрочногоНазначенияПенсии.ПустаяСсылка)
	               |	КОНЕЦ КАК ОснованиеВыслугиЛет,
	               |	ВЫБОР
	               |		КОГДА ТИПЗНАЧЕНИЯ(ДанныеСтажаСотрудников.ОсобыеУсловияТруда) = ТИП(Справочник.ОсобыеУсловияТрудаПФР)
	               |			ТОГДА ДанныеСтажаСотрудников.ОсобыеУсловияТруда
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ОсобыеУсловияТрудаПФР.ПустаяСсылка)
	               |	КОНЕЦ КАК ОсобыеУсловияТруда,
	               |	ДанныеСтажаСотрудников.*
	               |ИЗ
	               |	ВТДанныеСтажаСотрудников КАК ДанныеСтажаСотрудников
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Сотрудник,
	               |	ДоговорГПХ УБЫВ,
	               |	ДанныеСтажаСотрудников.ДатаНачалаПериода";
	
	Если Объект.КорректируемыйПериод < '20020101' Тогда 
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Справочник.ОснованияДосрочногоНазначенияПенсии", "Справочник.ОснованияДосрочногоНазначенияПенсииДляСЗВК");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Справочник.ОсобыеУсловияТрудаПФР", "Справочник.ОсобыеУсловияТрудаДляСЗВКПФР");
	КонецЕсли;
				   
	Выборка = Запрос.Выполнить().Выбрать();
	ПроверяемыеРеквизиты = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(
		"Сотрудник,КодПозицииСписка,ОснованиеИсчисляемогоСтажа,ПервыйПараметрИсчисляемогоСтажа,ВторойПараметрИсчисляемогоСтажа,
		|ОснованиеВыслугиЛет,ПервыйПараметрВыслугиЛет,ВторойПараметрВыслугиЛет,ТретийПараметрВыслугиЛет,ОсобыеУсловияТруда, РайонныйКоэффициент,
		|ТерриториальныеУсловия,ПараметрТерриториальныхУсловий,ТерриториальныеУсловияДополнительные,ЗамещениеГосударственныхМуниципальныхДолжностей,
		|КлассПодклассУсловийТруда,НомерРабочегоМеста", , Истина, Истина);
	РеквизитыСтроки = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(ПроверяемыеРеквизиты);
	РеквизитыСтроки.Добавить("ТретийПараметрИсчисляемогоСтажа");
	ТаблицаОтклонений = Объект.ЗаписиОСтаже.Выгрузить(Новый Массив);
	Пока Выборка.СледующийПоЗначениюПоля("Сотрудник") Цикл 
		ПредыдущаяСтрока = Неопределено;
		ТаблицаОтклонений.Очистить();
		ИндексПервойСтрокиСотрудника = Объект.ЗаписиОСтаже.Количество();
		Пока Выборка.Следующий() Цикл
			Если ЗначениеЗаполнено(Выборка.ТретийПараметрИсчисляемогоСтажа) И ЗначениеЗаполнено(Выборка.ЗамещениеГосударственныхМуниципальныхДолжностей) Тогда 
				ПредыдущаяСтрока = ПредыдущаяОсновнаяЗапись();
				Если ПредыдущаяСтрока = Неопределено Тогда 
					ПредыдущаяСтрока = Объект.ЗаписиОСтаже.Добавить();
					ЗаполнитьЗначенияСвойств(ПредыдущаяСтрока, Выборка, , "ТретийПараметрИсчисляемогоСтажа");
				Иначе 
					Если ПерсонифицированныйУчет.ДанныеСтрокСовпадают(ПредыдущаяСтрока, Выборка, ПроверяемыеРеквизиты) Тогда 
						ПредыдущаяСтрока.ДатаОкончанияПериода = Выборка.ДатаОкончанияПериода;
					Иначе
						ВывестиСтрокиОтклонений(ТаблицаОтклонений, ПредыдущаяСтрока);
						ПредыдущаяСтрока = Объект.ЗаписиОСтаже.Добавить();
						ЗаполнитьЗначенияСвойств(ПредыдущаяСтрока, Выборка, , "ТретийПараметрИсчисляемогоСтажа");
					КонецЕсли;
				КонецЕсли;
				ДобавитьСтрокуЗаписейОСтаже(ТаблицаОтклонений, Выборка, ДатаПостановления322);
				Продолжить;
			КонецЕсли;
			Если ПредыдущаяСтрока <> Неопределено 
				И КонецДня(ПредыдущаяСтрока.ДатаОкончанияПериода) + 1 = Выборка.ДатаНачалаПериода
				И ПерсонифицированныйУчет.ДанныеСтрокСовпадают(ПредыдущаяСтрока, Выборка, ПроверяемыеРеквизиты) Тогда 
				ПредыдущаяСтрока.ДатаОкончанияПериода = Выборка.ДатаОкончанияПериода;
				Продолжить;
			КонецЕсли;
			ПредыдущаяЗапись = ПредыдущаяОсновнаяЗапись();
			Если ПредыдущаяЗапись <> Неопределено 
				И КонецДня(ПредыдущаяЗапись.ДатаОкончанияПериода) + 1 = Выборка.ДатаНачалаПериода
				И ПерсонифицированныйУчет.ДанныеСтрокСовпадают(ПредыдущаяЗапись, Выборка, РеквизитыСтроки) Тогда 
				ПредыдущаяЗапись.ДатаОкончанияПериода = Выборка.ДатаОкончанияПериода;
				Продолжить;
			КонецЕсли;
			ВывестиСтрокиОтклонений(ТаблицаОтклонений, ПредыдущаяСтрока);
			ДобавитьСтрокуЗаписейОСтаже(Объект.ЗаписиОСтаже, Выборка, ДатаПостановления322);
		КонецЦикла;
		ВывестиСтрокиОтклонений(ТаблицаОтклонений, ПредыдущаяСтрока);
		ИндексПоследнейСтрокиСотрудника = Объект.ЗаписиОСтаже.Количество() - 1;
		СтрокиСтажаПоСотруднику = Новый Массив;
		Для Индекс = ИндексПервойСтрокиСотрудника По ИндексПоследнейСтрокиСотрудника Цикл 
			СтрокаСтажа = Объект.ЗаписиОСтаже[Индекс];
			Если Объект.КорректируемыйПериод >= '20170101'
				И ЗначениеЗаполнено(СтрокаСтажа.ТерриториальныеУсловияДополнительные) 
				И Не ЗначениеЗаполнено(СтрокаСтажа.ТерриториальныеУсловия) Тогда 
				СтрокаСтажа.ТерриториальныеУсловия = СтрокаСтажа.ТерриториальныеУсловияДополнительные;
			КонецЕсли;
		    СтрокиСтажаПоСотруднику.Добавить(СтрокаСтажа);
		КонецЦикла;
		ПерсонифицированныйУчетКлиентСервер.ВыполнитьНумерациюЗаписейОСтаже(СтрокиСтажаПоСотруднику);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ВывестиСтрокиОтклонений(ТаблицаОтклонений, ПредыдущаяСтрока)
	
	Для Каждого СтрокаОтклонения Из ТаблицаОтклонений Цикл 
		НоваяСтрока = Объект.ЗаписиОСтаже.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаОтклонения);
	КонецЦикла;
	
	ТаблицаОтклонений.Очистить();
	ПредыдущаяСтрока = Неопределено;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьСтрокуЗаписейОСтаже(ДанныеОСтаже, Выборка, ДатаПостановления322)
	
	НоваяСтрока = ДанныеОСтаже.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	
	Если Объект.КорректируемыйПериод < '20170101' Тогда
		ПерсонифицированныйУчет.ОбработатьПериодПовышенияКвалификации(ДанныеОСтаже, НоваяСтрока, ДатаПостановления322);
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Выборка.ТерриториальныеУсловияДополнительные) И ЗначениеЗаполнено(Выборка.ТерриториальныеУсловия) Тогда 
		НоваяСтрока = ДанныеОСтаже.Добавить();
		НоваяСтрока.Сотрудник = Выборка.Сотрудник;
		НоваяСтрока.ТерриториальныеУсловия = Выборка.ТерриториальныеУсловияДополнительные;
	КонецЕсли;
	
	ПерсонифицированныйУчет.ОбработатьПериодПовышенияКвалификации(ДанныеОСтаже, НоваяСтрока, ДатаПостановления322);
	
КонецПроцедуры

&НаСервере
Функция ПредыдущаяОсновнаяЗапись()
	
	КоличествоСтрок = Объект.ЗаписиОСтаже.Количество();
	Если КоличествоСтрок = 0 Тогда 
		Возврат Неопределено;
	КонецЕсли;
	
	Для Сч = 1 По КоличествоСтрок Цикл 
		Если ЗначениеЗаполнено(Объект.ЗаписиОСтаже[КоличествоСтрок-Сч].ДатаНачалаПериода) Тогда 
			Возврат Объект.ЗаписиОСтаже[КоличествоСтрок-Сч];
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьДанныеОЗаработкеСотрудников(СписокСотрудников)
	
	Если Год(Объект.КорректируемыйПериод) < 2014 Или Год(Объект.КорректируемыйПериод) >= 2017 Тогда 
		Возврат;
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ПараметрыФормированияДокументов = ПерсонифицированныйУчет.ПараметрыФормированияДокументовКвартальнойОтчетности();
	ПараметрыФормированияДокументов.ТипСведенийСЗВ = Перечисления.ТипыСведенийСЗВ.КОРРЕКТИРУЮЩАЯ;
	
	ПараметрыПолученияДанных = ПерсонифицированныйУчет.ПараметрыДляСоздатьВТСведенияОВзносахЗаработкеСтажеЗЛ();
	ЗаполнитьЗначенияСвойств(ПараметрыПолученияДанных, ПараметрыФормированияДокументов);
	
	ПараметрыОтбора = ПерсонифицированныйУчет.СтруктураОтбораДанныхДляКвартальнойОтчетности();
	ПараметрыОтбора.СписокФизическихЛиц = СписокСотрудников;
	
	ПараметрыПолученияЗаработка = ПерсонифицированныйУчет.ПараметрыДляСоздатьВТСведенияОЗаработке();
	ЗаполнитьЗначенияСвойств(ПараметрыПолученияЗаработка, ПараметрыПолученияДанных);
	ПерсонифицированныйУчет.СоздатьВТСведенияОЗаработке2014(МенеджерВременныхТаблиц, Объект.Организация, Объект.КорректируемыйПериод, ПараметрыПолученияЗаработка, ПараметрыОтбора);
		
	КодыМесяцев = ПерсонифицированныйУчетКлиентСервер.КодыМесяцевСЗВ_КОРР();	
	ДанныеСотрудников = Новый Соответствие;
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	СведенияОЗаработке.ФизическоеЛицо КАК Сотрудник,
	               |	СведенияОЗаработке.Месяц КАК Месяц,
	               |	СУММА(СведенияОЗаработке.Заработок) КАК Заработок,
	               |	СУММА(СведенияОЗаработке.ОблагаетсяВзносамиДоПредельнойВеличины) КАК ОблагаетсяВзносамиДоПредельнойВеличины,
	               |	СУММА(СведенияОЗаработке.ПоДоговорамГПХДоПредельнойВеличины) КАК ПоДоговорамГПХДоПредельнойВеличины,
	               |	СУММА(СведенияОЗаработке.ОблагаетсяВзносамиСвышеПредельнойВеличины) КАК ОблагаетсяВзносамиСвышеПредельнойВеличины
	               |ИЗ
	               |	ВТСведенияОЗаработке КАК СведенияОЗаработке
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СведенияОЗаработке.ФизическоеЛицо,
	               |	СведенияОЗаработке.Месяц
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Сотрудник,
	               |	Месяц";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Сотрудник") Цикл
		Сотрудник = Выборка.Сотрудник;
		ДанныеСотрудника = ДанныеСотрудников[Сотрудник];
		Если ДанныеСотрудника = Неопределено Тогда 
			ДанныеСотрудника = СведенияОЗаработкеВзносах();
			ДанныеСотрудников.Вставить(Сотрудник, ДанныеСотрудника);
		КонецЕсли;
		Пока Выборка.Следующий() Цикл 
			Если Не ЗначениеЗаполнено(Выборка.Месяц) Тогда 
				Продолжить;
			КонецЕсли;
			НоваяСтрока = Объект.СведенияОЗаработке.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.КодМесяца = КодыМесяцев[Выборка.Месяц];
			ДанныеСотрудника.Заработок = ДанныеСотрудника.Заработок + Выборка.Заработок;
		КонецЦикла;
	КонецЦикла;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	СведенияОЗаработкеПоКлассамУсловийТруда.ФизическоеЛицо КАК Сотрудник,
	               |	СведенияОЗаработкеПоКлассамУсловийТруда.Месяц КАК Месяц,
	               |	СведенияОЗаработкеПоКлассамУсловийТруда.КлассУсловийТруда КАК КлассУсловийТруда,
	               |	СУММА(СведенияОЗаработкеПоКлассамУсловийТруда.ОблагаетсяВзносамиЗаЗанятыхНаТяжелыхИПрочихРаботах) КАК ОблагаетсяВзносамиЗаЗанятыхНаТяжелыхИПрочихРаботах,
	               |	СУММА(СведенияОЗаработкеПоКлассамУсловийТруда.ОблагаетсяВзносамиЗаЗанятыхНаПодземныхИВредныхРаботах) КАК ОблагаетсяВзносамиЗаЗанятыхНаПодземныхИВредныхРаботах
	               |ИЗ
	               |	ВТСведенияОЗаработкеПоКлассамУсловийТруда КАК СведенияОЗаработкеПоКлассамУсловийТруда
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СведенияОЗаработкеПоКлассамУсловийТруда.ФизическоеЛицо,
	               |	СведенияОЗаработкеПоКлассамУсловийТруда.КлассУсловийТруда,
	               |	СведенияОЗаработкеПоКлассамУсловийТруда.Месяц
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Сотрудник,
	               |	Месяц,
	               |	КлассУсловийТруда";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл 
		НоваяСтрока = Объект.СведенияОЗаработкеНаВредныхИТяжелыхРаботах.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		НоваяСтрока.КодМесяца = КодыМесяцев[Выборка.Месяц];
	КонецЦикла;
	
	Для Каждого СтрокаСотрудника Из Объект.Сотрудники Цикл 
		ДанныеСотрудника = ДанныеСотрудников[СтрокаСотрудника.Сотрудник];
		Если ДанныеСотрудника <> Неопределено Тогда 
			ЗаполнитьЗначенияСвойств(СтрокаСотрудника, ДанныеСотрудника);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция СведенияОЗаработкеВзносах()
	
	Возврат Новый Структура("Заработок, Доначислено, Уплачено", 0, 0, 0);
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСведенияОбОрганизацииВКорректируемомПериоде()
	
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда 
		Возврат;
	КонецЕсли;
	
	Объект.КППвКорректируемыйПериод = Неопределено;
	Объект.ИННвКорректируемыйПериод = Неопределено;
	Объект.РегистрационныйНомерПФРвКорректируемыйПериод = Неопределено;
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("Период", Объект.ОкончаниеКорректируемогоПериода);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	&Организация КАК Организация,
	               |	&Период КАК Период
	               |ПОМЕСТИТЬ ВТОрганизации";
	
	Запрос.Выполнить();
	
	ПерсонифицированныйУчет.СоздатьВТСведенияОбОрганизацияхПоВременнойТаблице(Запрос.МенеджерВременныхТаблиц);	
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	СведенияОбОрганизациях.*
	               |ИЗ
	               |	ВТСведенияОбОрганизациях КАК СведенияОбОрганизациях";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Объект.КППвКорректируемыйПериод = Выборка.КПП;
		Объект.ИННвКорректируемыйПериод = Выборка.ИНН;
		Объект.РегистрационныйНомерПФРвКорректируемыйПериод = Выборка.РегистрационныйНомерПФР;
		Если ЗарплатаКадры.ИспользованиеРегистрационногоНомераСФР(Объект.ОкончаниеКорректируемогоПериода, Объект.Организация) 
			И ЗначениеЗаполнено(Выборка.РегистрационныйНомерСФР) Тогда
			Объект.РегистрационныйНомерПФРвКорректируемыйПериод = Выборка.РегистрационныйНомерСФР;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОписаниеЭлементовСИндикациейОшибок() Экспорт
	
	ОписаниеЭлементовИндикацииОшибок = Новый Соответствие;	
	Возврат ОписаниеЭлементовИндикацииОшибок;
	
КонецФункции	

&НаСервере
Процедура УстановитьПраваДоступаНаДокумент()
	
	ДанныеДляПроверки = Новый Структура("Организация,ФизическиеЛица");
	ДанныеДляПроверки.Организация = Объект.Организация;
	ДанныеДляПроверки.ФизическиеЛица = ОбщегоНазначения.ВыгрузитьКолонку(Объект.Сотрудники, "Сотрудник", Истина);
	
	ИзменениеБезОграничений = Не Документы.ДанныеОКорректировкеСведенийЗастрахованныхЛицСЗВ_КОРР.ЕстьОграничениеДоступа("Изменение", ДанныеДляПроверки);
	ЧтениеБезОграничений = ?(ИзменениеБезОграничений, Истина, 
		Не Документы.ДанныеОКорректировкеСведенийЗастрахованныхЛицСЗВ_КОРР.ЕстьОграничениеДоступа("Чтение", ДанныеДляПроверки));
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииРеквизитовОпределяющихОграниченияНаУровнеЗаписей()
	
	БылоИзменениеБезОграничений = ИзменениеБезОграничений;
	БылоЧтениеБезОграничений = ЧтениеБезОграничений;
	
	УстановитьПраваДоступаНаДокумент();
	
	Если БылоЧтениеБезОграничений <> ЧтениеБезОграничений Или БылоИзменениеБезОграничений <> ИзменениеБезОграничений Тогда 
		УстановитьСвойстваЭлементовФормы();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗаголовокГруппыПрежнегоСтрахователя(УправляемаяФорма)
	
	Объект = УправляемаяФорма.Объект;
	Элементы = УправляемаяФорма.Элементы;
	
	Если Не ЗначениеЗаполнено(Объект.ПрежнийСтраховательНаименованиеПолное) Тогда
		ПредставлениеСтрахователя = НСтр("ru = 'не задан';
										|en = 'not specified'");
	Иначе
		ПредставленияСтрахователя = Новый Массив;
		ПредставленияСтрахователя.Добавить(СокрЛП(Объект.ПрежнийСтраховательНаименованиеПолное));
		Если ЗначениеЗаполнено(Объект.ПрежнийСтраховательРегистрационныйНомерПФР) Тогда
			ПредставленияСтрахователя.Добавить(СтрШаблон(
				НСтр("ru = 'рег. номер: %1';
					|en = 'reg. number: %1'"), СокрЛП(Объект.ПрежнийСтраховательРегистрационныйНомерПФР)));
		КонецЕсли; 
		Если ЗначениеЗаполнено(Объект.ПрежнийСтраховательИНН) Тогда
			ПредставленияСтрахователя.Добавить(СтрШаблон(
				НСтр("ru = 'ИНН: %1';
					|en = 'TIN: %1'"), СокрЛП(Объект.ПрежнийСтраховательИНН)));
		КонецЕсли; 
		Если ЗначениеЗаполнено(Объект.ПрежнийСтраховательКПП) Тогда
			ПредставленияСтрахователя.Добавить(СтрШаблон(
				НСтр("ru = 'КПП: %1';
					|en = 'CRTR: %1'"), СокрЛП(Объект.ПрежнийСтраховательКПП)));
		КонецЕсли;
		ПредставлениеСтрахователя = СтрСоединить(ПредставленияСтрахователя, ", ");
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ГруппаПрежнийСтрахователь",
		"ЗаголовокСвернутогоОтображения",
		СтрШаблон(НСтр("ru = 'Сведения о работодателе, правопреемником которого является страхователь (%1)';
						|en = 'Information record on the employer whose successor is the insurant (%1)'"), ПредставлениеСтрахователя));
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИспользованиеРегистрационногоНомераСФР(ДатаАктуальности, Организация)
	
	Возврат ЗарплатаКадры.ИспользованиеРегистрационногоНомераСФР(ДатаАктуальности, Организация)
	
КонецФункции

&НаСервере
Функция КлючевыеРеквизитыЗаполненияФормыТаблицыОчищаемыеПриИзменении() Экспорт
	
	Массив = КлючевыеРеквизитыТаблицыОчищаемыеПриИзменении();
	Возврат Массив;
	
КонецФункции 

// Функция возвращает массив реквизитов формы подключенных к механизму ключевых реквизитов формы.
&НаСервере
Функция КлючевыеРеквизитыЗаполненияФормыОписаниеКлючевыхРеквизитов() Экспорт
	
	Массив = КлючевыеРеквизитыОписаниеКлючевыхРеквизитов();
	Возврат Массив;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьОрганизациюДляПодбораСотрудников(ТекущийОбъект)
	ОрганизацияДляПодбора.Очистить();
	Если УчетСтраховыхВзносов.ОрганизацияОтчитываетсяПоВзносамСамостоятельно(ТекущийОбъект.Организация, ТекущийОбъект.ОтчетныйПериод) Тогда
		Для каждого Организация Из УчетСтраховыхВзносов.ОрганизацииДляКонсолидацииОтчетовПоВзносам(ТекущийОбъект.Организация, ТекущийОбъект.ОтчетныйПериод) Цикл
			ОрганизацияДляПодбора.Добавить(Организация)
		КонецЦикла;
	Иначе 	
		ОрганизацияДляПодбора.Добавить(ТекущийОбъект.Организация)
	КонецЕсли;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция КлючевыеРеквизитыТаблицыОчищаемыеПриИзменении() 
	
	Массив = Новый Массив;
	Массив.Добавить("Объект.Сотрудники");
	Массив.Добавить("Объект.СведенияОЗаработке");
	Массив.Добавить("Объект.СведенияОЗаработкеНаВредныхИТяжелыхРаботах");
	Массив.Добавить("Объект.ЗаписиОСтаже");
	Массив.Добавить("Объект.КорректирующиеСведения");
	Массив.Добавить("Объект.СведенияОбУплаченныхВзносах");
	Массив.Добавить("Объект.НачисленныеУплаченныеВзносы");
	Массив.Добавить("Объект.УплаченныеВзносыРасшифровка");
	
	Возврат Массив;
	
КонецФункции 

&НаКлиентеНаСервереБезКонтекста
Функция КлючевыеРеквизитыОписаниеКлючевыхРеквизитов() 
	
	Массив = Новый Массив;
	Массив.Добавить(Новый Структура("ЭлементФормы, Представление", "Организация",			НСтр("ru = 'организации';
																									|en = 'companies'")));
	Массив.Добавить(Новый Структура("ЭлементФормы, Представление", "КорректируемыйПериод", 	НСтр("ru = 'периода';
																									|en = 'period'")));
	
	Возврат Массив;
	
КонецФункции

#КонецОбласти
