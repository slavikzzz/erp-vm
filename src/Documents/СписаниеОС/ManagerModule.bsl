
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	МеханизмыДокумента.Добавить("ОсновныеСредства");
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	МеханизмыДокумента.Добавить("РеестрДокументов");
	МеханизмыДокумента.Добавить("СебестоимостьИПартионныйУчет");
	МеханизмыДокумента.Добавить("УчетДоходовРасходов");
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка, ДокументОбъект - ссылка на документ или объект, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * Таблица<ИмяРегистра> - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Если ТипЗнч(Документ) = Тип("ДокументОбъект.СписаниеОС") Тогда
		ДокументСсылка = Документ.Ссылка;
	Иначе
		ДокументСсылка = Документ;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДопПараметры);
		
		УчетОСВызовСервера.ПрочиеРасходы(ТекстыЗапроса, Регистры, ПрочиеРасходы(ТекстыЗапроса, Регистры));
		УчетОСВызовСервера.ПартииПрочихРасходов(ТекстыЗапроса, Регистры, ПартииПрочихРасходов(ТекстыЗапроса, Регистры));
		УчетОСВызовСервера.ПрочиеДоходы(ТекстыЗапроса, Регистры, ПрочиеДоходы(ТекстыЗапроса, Регистры));
		УчетОСВызовСервера.ПрочиеАктивыПассивы(ТекстыЗапроса, Регистры, ПрочиеАктивыПассивы(ТекстыЗапроса, Регистры));
		УчетОСВызовСервера.ПорядокОтраженияПрочихОпераций(ТекстыЗапроса, Регистры);
		УчетОСВызовСервера.ОтражениеДокументовВРеглУчете(ТекстыЗапроса, Регистры, ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете());
		
		ТекстЗапросаТаблицаСобытияОСОрганизаций(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПорядокУчетаОСБУ(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПараметрыАмортизацииОСБУ(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПереданныеВАрендуОС(ТекстыЗапроса, Регистры);
	
		ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, Регистры);
	КонецЕсли;
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

#КонецОбласти

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Документы.ПрочееОприходованиеТоваров.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	//++ НЕ УТКА
	Документы.СписаниеОСМеждународныйУчет.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	//-- НЕ УТКА
	
КонецПроцедуры

// Добавляет команду создания документа "Списание ОС".
//
// Параметры:
//   КомандыСозданияНаОсновании - См. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Порядок						 - Число			 - Порядок команды.
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании, Порядок = 0) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.СписаниеОС) Тогда
		
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.СписаниеОС.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.СписаниеОС);
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ОтображатьВнеоборотныеАктивы2_2";
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		КомандаСоздатьНаОсновании.Порядок = Порядок;
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт
	
	ИсточникиДанных = Новый Соответствие;
	Возврат ИсточникиДанных;
	
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.СписаниеОС";
	
	ЗначенияПараметров = ЗначенияПараметровПроведения();
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("НомерНаПечать", """""");
	
	ВЗапросеЕстьИсточник = Истина;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "";
		ВЗапросеЕстьИсточник = Ложь;
		
	ИначеЕсли ИмяРегистра = "ДокументыПоОС" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';
								|en = 'In document %ПолноеИмяДокумента%, adaptation of request for generating records of register %ИмяРегистра% is not implemented.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов"
		ИЛИ ИмяРегистра = "ДокументыПоОС" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ВЗапросеЕстьИсточник,
										ПереопределениеРасчетаПараметров);
	Иначе	
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ПереопределениеРасчетаПараметров);
	КонецЕсли; 

	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДопПараметры)
	
	ПроведениеДокументов.ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДопПараметры);

	ПолучитьДанныеДокумента(Запрос);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.Дата КАК Дата,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.ПометкаУдаления,
	|	ДанныеДокумента.Проведен,
	|	ДанныеДокумента.Комментарий,
	|	ДанныеДокумента.Ответственный,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ДанныеДокумента.СобытиеОС КАК СобытиеОС,
	|	НЕ ДанныеДокумента.ЧастичнаяЛиквидация КАК СписаниеОстаточнойСтоимости,
	|	ДанныеДокумента.ЧастичнаяЛиквидация КАК ЧастичнаяЛиквидация,
	|	ДанныеДокумента.СтатьяРасходов КАК СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходов КАК АналитикаРасходов
	|ИЗ
	|	Документ.СписаниеОС КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	УчетОСВызовСервера.ИнициализироватьПараметрыЗапросаПриОтраженииАмортизации(Запрос, ДопПараметры);
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("Граница", Новый Граница(НачалоМесяца(Реквизиты.Период), ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("КонецМесяца", Новый Граница(КонецМесяца(Реквизиты.Период), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ДатаСписанияОстаточнойСтоимости", Реквизиты.Период);
	Запрос.УстановитьПараметр("ТекущееСостояниеОбъектов", Истина);
	
	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения(Реквизиты);
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла; 
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
КонецПроцедуры

Процедура ПолучитьДанныеДокумента(Запрос)
	
	СписокЗапросов = Новый Массив;
	
	#Область ДанныеДокументаРеквизиты
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Организация.ГоловнаяОрганизация КАК ГоловнаяОрганизация
	|
	|ПОМЕСТИТЬ ДанныеДокументаРеквизиты
	|
	|ИЗ
	|	Документ.СписаниеОС КАК ДанныеДокумента
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ДанныеДокументаТаблицаОС
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаДокумента.ОсновноеСредство КАК ОбъектУчета
	|
	|ПОМЕСТИТЬ ДанныеДокументаТаблицаОС
	|
	|ИЗ
	|	Документ.СписаниеОС.ОС КАК ТаблицаДокумента
	|
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	ОсновноеСредство
	|";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	Запрос.Текст = СтрСоединить(СписокЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)

	ЗначенияПараметровПроведения = Новый Структура;
	ЗначенияПараметровПроведения.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.СписаниеОС"));
	ЗначенияПараметровПроведения.Вставить("НазваниеДокумента", НСтр("ru = 'Списание ОС';
																	|en = 'Dispose fixed assets'"));
	ЗначенияПараметровПроведения.Вставить("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.СписаниеОС);

	Если Реквизиты <> Неопределено Тогда
		ЗначенияПараметровПроведения.Вставить("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	КонецЕсли; 
	
	Возврат ЗначенияПараметровПроведения;
	
КонецФункции

Процедура ВременнаяТаблицаОсновныхСредств(ТекстыЗапроса)
	
	ИмяТаблицы = "ТаблицаОС";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	УчетОСВызовСервера.ДобавитьВременныеТаблицы(ТекстыЗапроса, "ТекущееСостояниеОбъектов");
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПорядокУчетаОС(ТекстыЗапроса);
	
	Текст =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.ОсновноеСредство,
	|	втПорядокУчетаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЕСТЬNULL(втПорядокУчетаОС.ПоказательНаработки, ЗНАЧЕНИЕ(Справочник.ПоказателиНаработки.ПустаяСсылка)) КАК ПоказательНаработки,
	|	
	|	ТаблицаДокумента.СуммаСписанияБУ КАК СуммаСписанияБУ,
	|	ТаблицаДокумента.СуммаСписанияНУ КАК СуммаСписанияНУ,
	|	
	|	ВЫБОР КОГДА ЕСТЬNULL(втТекущееСостояниеОбъектов.ДоляБУЦФ, 0) = 0 ТОГДА 0
	|		ИНАЧЕ (ВЫРАЗИТЬ(ТаблицаДокумента.СуммаСписанияБУ 
	|				* ЕСТЬNULL(втТекущееСостояниеОбъектов.ДоляБУЦФ, 0) КАК ЧИСЛО(31,2))) 
	|				- (ВЫРАЗИТЬ(втТекущееСостояниеОбъектов.НакопленнаяАмортизацияБУЦФ 
	|							* (ВЫРАЗИТЬ(ТаблицаДокумента.СуммаСписанияБУ 
	|										* ЕСТЬNULL(втТекущееСостояниеОбъектов.ДоляБУЦФ, 0) КАК ЧИСЛО(31,2))) 
	|					/ втТекущееСостояниеОбъектов.ВосстановительнаяСтоимостьБУЦФ КАК ЧИСЛО(31,2)))
	|	КОНЕЦ КАК СуммаСписанияБУЦФ,
	|
	|	ВЫБОР КОГДА ЕСТЬNULL(втТекущееСостояниеОбъектов.ДоляНУЦФ, 0) = 0 ТОГДА 0
	|		ИНАЧЕ (ВЫРАЗИТЬ(ТаблицаДокумента.СуммаСписанияНУ
	|				* ЕСТЬNULL(втТекущееСостояниеОбъектов.ДоляНУЦФ, 0) КАК ЧИСЛО(31,2))) 
	|				- (ВЫРАЗИТЬ(втТекущееСостояниеОбъектов.НакопленнаяАмортизацияНУЦФ 
	|							* (ВЫРАЗИТЬ(ТаблицаДокумента.СуммаСписанияНУ 
	|										* ЕСТЬNULL(втТекущееСостояниеОбъектов.ДоляНУЦФ, 0) КАК ЧИСЛО(31,2))) 
	|					/ втТекущееСостояниеОбъектов.ВосстановительнаяСтоимостьНУЦФ КАК ЧИСЛО(31,2)))
	|	КОНЕЦ КАК СуммаСписанияНУЦФ,
	|
	|	ВЫБОР КОГДА ЕСТЬNULL(втТекущееСостояниеОбъектов.ДоляПРЦФ, 0) = 0 ТОГДА 0
	|		ИНАЧЕ (ВЫРАЗИТЬ(ТаблицаДокумента.СуммаСписанияПР 
	|				* ЕСТЬNULL(втТекущееСостояниеОбъектов.ДоляПРЦФ, 0) КАК ЧИСЛО(31,2))) 
	|				- (ВЫРАЗИТЬ(втТекущееСостояниеОбъектов.НакопленнаяАмортизацияПРЦФ 
	|							* (ВЫРАЗИТЬ(ТаблицаДокумента.СуммаСписанияПР 
	|										* ЕСТЬNULL(втТекущееСостояниеОбъектов.ДоляПРЦФ, 0) КАК ЧИСЛО(31,2))) 
	|					/ втТекущееСостояниеОбъектов.ВосстановительнаяСтоимостьПРЦФ КАК ЧИСЛО(31,2)))
	|	КОНЕЦ КАК СуммаСписанияПРЦФ,
	|	
	|	ВЫБОР КОГДА ЕСТЬNULL(втТекущееСостояниеОбъектов.ПолнаяВосстановительнаяСтоимостьБУ, 0) = 0 ТОГДА 0
	|		ИНАЧЕ ТаблицаДокумента.СуммаСписанияБУ 
	|				- (ВЫРАЗИТЬ(втТекущееСостояниеОбъектов.ПолнаяНакопленнаяАмортизацияБУ 
	|					* ТаблицаДокумента.СуммаСписанияБУ 
	|					/ втТекущееСостояниеОбъектов.ПолнаяВосстановительнаяСтоимостьБУ КАК ЧИСЛО(31,2)))
	|	КОНЕЦ КАК РасходБУ,
	|
	|	ВЫБОР КОГДА ЕСТЬNULL(втТекущееСостояниеОбъектов.ПолнаяВосстановительнаяСтоимостьБУ, 0) = 0 ТОГДА 0
	|		ИНАЧЕ ТаблицаДокумента.СуммаСписанияНУ 
	|				- (ВЫРАЗИТЬ(втТекущееСостояниеОбъектов.ПолнаяНакопленнаяАмортизацияНУ 
	|					* ТаблицаДокумента.СуммаСписанияНУ 
	|					/ втТекущееСостояниеОбъектов.ПолнаяВосстановительнаяСтоимостьБУ КАК ЧИСЛО(31,2)))
	|				+ (ВЫРАЗИТЬ(втТекущееСостояниеОбъектов.НеУчитываемаяСтоимостьНУ 
	|					* ТаблицаДокумента.СуммаСписанияБУ 
	|					/ втТекущееСостояниеОбъектов.ПолнаяВосстановительнаяСтоимостьБУ КАК ЧИСЛО(31,2)))
	|	КОНЕЦ КАК РасходНУ,
	|
	|	ВЫБОР КОГДА ЕСТЬNULL(втТекущееСостояниеОбъектов.ПолнаяВосстановительнаяСтоимостьБУ, 0) = 0 ТОГДА 0
	|		ИНАЧЕ ТаблицаДокумента.СуммаСписанияПР 
	|				- (ВЫРАЗИТЬ(ТаблицаДокумента.СуммаСписанияПР 
	|					* ТаблицаДокумента.СуммаСписанияБУ 
	|					/ втТекущееСостояниеОбъектов.ПолнаяВосстановительнаяСтоимостьБУ КАК ЧИСЛО(31,2)))
	|				+ (ВЫРАЗИТЬ(втТекущееСостояниеОбъектов.КорректировкаСтоимостиПР 
	|					* ТаблицаДокумента.СуммаСписанияБУ 
	|					/ втТекущееСостояниеОбъектов.ПолнаяВосстановительнаяСтоимостьБУ КАК ЧИСЛО(31,2)))
	|	КОНЕЦ КАК РасходПР,
	|	
	|	ЕСТЬNULL(втТекущееСостояниеОбъектов.ПолнаяОстаточнаяСтоимостьБУ, 0) КАК ОстаточнаяСтоимостьБУ,
	|
	|	ЕСТЬNULL(втТекущееСостояниеОбъектов.ПолнаяОстаточнаяСтоимостьНУ, 0)
	|		+ ЕСТЬNULL(втТекущееСостояниеОбъектов.НеУчитываемаяСтоимостьНУ, 0) КАК ОстаточнаяСтоимостьНУ,
	|
	|	ЕСТЬNULL(втТекущееСостояниеОбъектов.ПолнаяОстаточнаяСтоимостьПР, 0)
	|		+ ЕСТЬNULL(втТекущееСостояниеОбъектов.КорректировкаСтоимостиПР, 0) КАК ОстаточнаяСтоимостьПР,
	|
	|	ЕСТЬNULL(втТекущееСостояниеОбъектов.ПолнаяОстаточнаяСтоимостьВР, 0)
	|		+ ЕСТЬNULL(втТекущееСостояниеОбъектов.КорректировкаСтоимостиВР, 0) КАК ОстаточнаяСтоимостьВР
	|
	|ПОМЕСТИТЬ ТаблицаОС
	|ИЗ
	|	Документ.СписаниеОС.ОС КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ втТекущееСостояниеОбъектов КАК втТекущееСостояниеОбъектов
	|		ПО ТаблицаДокумента.ОсновноеСредство = втТекущееСостояниеОбъектов.ОбъектУчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаОС КАК втПорядокУчетаОС
	|		ПО ТаблицаДокумента.ОсновноеСредство = втПорядокУчетаОС.ОсновноеСредство
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПорядокУчетаОСБУ(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПорядокУчетаОСБУ";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтСписокОС(ТекстыЗапроса);
	
	Текст = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.СнятоСУчета) КАК СостояниеБУ,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.СнятоСУчета) КАК СостояниеНУ,
	|	ЛОЖЬ КАК НачислятьАмортизациюБУ,
	|	ЛОЖЬ КАК НачислятьАмортизациюНУ,
	|	ПорядокУчета.АмортизационнаяГруппа КАК АмортизационнаяГруппа,
	|	ПорядокУчета.НедвижимоеИмущество КАК НедвижимоеИмущество
	|ИЗ
	|	Документ.СписаниеОС.ОС КАК ТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСБУ.СрезПоследних(&Период, Регистратор <> &Ссылка
	|		И Организация = &Организация
	|		И ОсновноеСредство В
	|			(ВЫБРАТЬ
	|				СписокОС.ОсновноеСредство
	|			ИЗ
	|				втСписокОС КАК СписокОС)) КАК ПорядокУчета
	|		ПО ТаблицаОС.ОсновноеСредство = ПорядокУчета.ОсновноеСредство
	|		И ПорядокУчета.Организация = &Организация
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И НЕ &ЧастичнаяЛиквидация";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПараметрыАмортизацииОСБУ(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПараметрыАмортизацииОСБУ";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныхСредств(ТекстыЗапроса);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыАмортизацииОСБУ(ТекстыЗапроса);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтНаработкиОбъектовЭксплуатации(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                      КАК Период,
	|	&Организация                 КАК Организация,
	|	&Период                      КАК ДатаПоследнегоИзменения,
	|	ТаблицаОС.ОсновноеСредство   КАК ОсновноеСредство,
	|
	// СтоимостьДляВычисленияАмортизации
	|	ТаблицаОС.ОстаточнаяСтоимостьБУ - ТаблицаОС.РасходБУ КАК СтоимостьДляВычисленияАмортизации,
	|
	|	ПараметрыАмортизацииОСБУ.СтоимостьДляВычисленияАмортизацииЦФ  КАК СтоимостьДляВычисленияАмортизацииЦФ,
	|	ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияБУ         КАК СрокПолезногоИспользованияБУ,
	|	ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияНУ         КАК СрокПолезногоИспользованияНУ,
	|	ПараметрыАмортизацииОСБУ.КоэффициентАмортизацииБУ             КАК КоэффициентАмортизацииБУ,
	|	ПараметрыАмортизацииОСБУ.КоэффициентУскорения                 КАК КоэффициентУскорения,
	|	ПараметрыАмортизацииОСБУ.ПРДляВычисленияАмортизации           КАК ПРДляВычисленияАмортизации,
	|	ПараметрыАмортизацииОСБУ.ГрафикАмортизации                    КАК ГрафикАмортизации,
	|	ПараметрыАмортизацииОСБУ.СпециальныйКоэффициент               КАК СпециальныйКоэффициент,
	|
	|	ПараметрыАмортизацииОСБУ.ОбъемПродукцииРаботДляВычисленияАмортизации
	|		- ЕСТЬNULL(НаработкиОбъектовЭксплуатации.Значение, 0) КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|
	|	ПараметрыАмортизацииОСБУ.СрокИспользованияДляВычисленияАмортизации
	|		- РАЗНОСТЬДАТ(ЕСТЬNULL(ПараметрыАмортизацииОСБУ.ДатаПоследнегоИзменения, &Период), &Период, МЕСЯЦ) КАК СрокИспользованияДляВычисленияАмортизации
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПараметрыАмортизацииОСБУ КАК ПараметрыАмортизацииОСБУ
	|		ПО (ПараметрыАмортизацииОСБУ.Организация = &Организация)
	|			И (ПараметрыАмортизацииОСБУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтНаработкиОбъектовЭксплуатации КАК НаработкиОбъектовЭксплуатации
	|		ПО ТаблицаОС.ОсновноеСредство = НаработкиОбъектовЭксплуатации.ОбъектЭксплуатации
	|ГДЕ
	|	&ЧастичнаяЛиквидация";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПереданныеВАрендуОС(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПереданныеВАрендуОС";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПереданныеВАрендуОС(ТекстыЗапроса);

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                               КАК Период,
	|	&Организация                          КАК Организация,
	|	ПереданныеВАрендуОС.ОсновноеСредство  КАК ОсновноеСредство
	|ИЗ
	|	ВтПереданныеВАрендуОС КАК ПереданныеВАрендуОС
	|ГДЕ
	|	НЕ &ЧастичнаяЛиквидация";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры	

Процедура ТекстЗапросаТаблицаСобытияОСОрганизаций(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СобытияОСОрганизаций";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныхСредств(ТекстыЗапроса);
	
	Текст =
	"ВЫБРАТЬ
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|
	|	&СобытиеОС КАК Событие,
	|
	|	&НазваниеДокумента КАК НазваниеДокумента,
	|	&Номер КАК НомерДокумента,
	|	ВЫБОР КОГДА &ЧастичнаяЛиквидация ТОГДА ТаблицаОС.СуммаСписанияБУ ИНАЧЕ 0 КОНЕЦ КАК СуммаЗатратБУ,
	|	ВЫБОР КОГДА &ЧастичнаяЛиквидация ТОГДА ТаблицаОС.СуммаСписанияНУ ИНАЧЕ 0 КОНЕЦ КАК СуммаЗатратНУ,
	|	0 КАК СуммаЗатратУСН
	|
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра);
	
КонецПроцедуры

Функция ПрочиеРасходы(ТекстыЗапроса, Регистры)
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений("ПрочиеРасходы", Регистры) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныхСредств(ТекстыЗапроса);
	
	Возврат
	"ВЫБРАТЬ
	|	&Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	ТаблицаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
	|	
	|	0 КАК СуммаСНДС,
	|	0 КАК СуммаБезНДС,
	|	0 КАК СуммаБезНДСУпр,
	|	ТаблицаОС.ОстаточнаяСтоимостьБУ КАК СуммаСНДСРегл,
	|	ТаблицаОС.ОстаточнаяСтоимостьБУ КАК СуммаБезНДСРегл,
	|	ТаблицаОС.ОстаточнаяСтоимостьПР КАК ПостояннаяРазница,
	|	ТаблицаОС.ОстаточнаяСтоимостьВР КАК ВременнаяРазница,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК АналитикаУчетаНоменклатуры,
	|
	|	""""         КАК ИдентификаторФинЗаписи,
	|	НЕОПРЕДЕЛЕНО КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|		
	|ГДЕ
	|	НЕ &ЧастичнаяЛиквидация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	ТаблицаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
	|	
	|	0 КАК СуммаСНДС,
	|	0 КАК СуммаБезНДС,
	|	0 КАК СуммаБезНДСУпр,
	|	ТаблицаОС.РасходБУ КАК СуммаСНДСРегл,
	|	ТаблицаОС.РасходБУ КАК СуммаБезНДСРегл,
	|	ТаблицаОС.РасходПР КАК ПостояннаяРазница,
	|	ТаблицаОС.РасходБУ - ТаблицаОС.РасходНУ - ТаблицаОС.РасходПР КАК ВременнаяРазница,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК АналитикаУчетаНоменклатуры,
	|
	|	""""         КАК ИдентификаторФинЗаписи,
	|	НЕОПРЕДЕЛЕНО КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|		
	|ГДЕ
	|	&ЧастичнаяЛиквидация
	|";
	
КонецФункции

Функция ПартииПрочихРасходов(ТекстыЗапроса, Регистры)
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений("ПартииПрочихРасходов", Регистры) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныхСредств(ТекстыЗапроса);
	
	Возврат
	"ВЫБРАТЬ
	|	&Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
	|	&Ссылка КАК ДокументПоступленияРасходов,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
	|	ТаблицаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК АналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
	|	
	|	0 КАК Сумма,
	|	0 КАК СуммаБезНДС,
	|	0 КАК НДСУпр,
	|	ТаблицаОС.ОстаточнаяСтоимостьБУ КАК СтоимостьРегл,
	|	0 КАК НДСРегл,
	|	ТаблицаОС.ОстаточнаяСтоимостьПР КАК ПостояннаяРазница,
	|	ТаблицаОС.ОстаточнаяСтоимостьВР КАК ВременнаяРазница,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
	|
	|	""""         КАК ИдентификаторФинЗаписи,
	|	НЕОПРЕДЕЛЕНО КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|		
	|ГДЕ
	|	НЕ &ЧастичнаяЛиквидация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
	|	&Ссылка КАК ДокументПоступленияРасходов,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
	|	ТаблицаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК АналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
	|	
	|	0 КАК Сумма,
	|	0 КАК СуммаБезНДС,
	|	0 КАК НДСУпр,
	|	ТаблицаОС.РасходБУ КАК СтоимостьРегл,
	|	0 КАК НДСРегл,
	|	ТаблицаОС.РасходПР КАК ПостояннаяРазница,
	|	ТаблицаОС.РасходБУ - ТаблицаОС.РасходНУ - ТаблицаОС.РасходПР КАК ВременнаяРазница,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
	|
	|	""""         КАК ИдентификаторФинЗаписи,
	|	НЕОПРЕДЕЛЕНО КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|		
	|ГДЕ
	|	&ЧастичнаяЛиквидация
	|";
	
КонецФункции

Функция ПрочиеДоходы(ТекстыЗапроса, Регистры)
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений("ПрочиеДоходы", Регистры) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныхСредств(ТекстыЗапроса);
	УчетОСВызовСервера.ДобавитьВременныеТаблицы(
		ТекстыЗапроса,
		"ПараметрыОбъектовУчета, ЦелевыеСредстваРеглСгруппированныеПоНаправлениям, ЦелевыеСредстваУпр");
	
	// Движения признания упр. стоимости доходов от целевого финансирования при частичной ликвидаци.
	Возврат
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Ссылка КАК Регистратор,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	ТаблицаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	втПараметрыОбъектовУчета.СтатьяДоходовЦФ КАК СтатьяДоходов,
	|	втПараметрыОбъектовУчета.АналитикаДоходовЦФ КАК АналитикаДоходов,
	|	
	|	ВЫРАЗИТЬ((втЦелевыеСредстваУпр.Сумма * (ТаблицаОС.СуммаСписанияБУЦФ/втЦелевыеСредстваРеглСгруппированныеПоНаправлениям.Сумма)) КАК ЧИСЛО(31,2)) КАК Сумма,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
	|	ТаблицаОС.СуммаСписанияБУЦФ КАК СуммаРегл,
	|	ЛОЖЬ КАК РасчетСебестоимости
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПараметрыОбъектовУчета КАК втПараметрыОбъектовУчета
	|		ПО ТаблицаОС.ОсновноеСредство = втПараметрыОбъектовУчета.ОбъектУчета
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЦелевыеСредстваРеглСгруппированныеПоНаправлениям КАК втЦелевыеСредстваРеглСгруппированныеПоНаправлениям
	|		ПО &Подразделение = втЦелевыеСредстваРеглСгруппированныеПоНаправлениям.Подразделение
	|			И ТаблицаОС.НаправлениеДеятельности = втЦелевыеСредстваРеглСгруппированныеПоНаправлениям.НаправлениеДеятельности
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЦелевыеСредстваУпр КАК втЦелевыеСредстваУпр
	|		ПО &Подразделение = втЦелевыеСредстваУпр.Подразделение
	|			И ТаблицаОС.НаправлениеДеятельности = втЦелевыеСредстваУпр.НаправлениеДеятельности
	|ГДЕ
	|	&ИспользоватьУчетПрочихДоходовРасходов
	|	И &ПогашениеУпрЗатрат
	|	И &ЧастичнаяЛиквидация";
	
КонецФункции

Функция ПрочиеАктивыПассивы(ТекстыЗапроса, Регистры)
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений("ПрочиеАктивыПассивы", Регистры) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныхСредств(ТекстыЗапроса);
	УчетОСВызовСервера.ДобавитьВременныеТаблицы(ТекстыЗапроса, "ЦелевыеСредстваРеглСгруппированныеПоНаправлениям, ЦелевыеСредстваУпр");
	
	// Движения признания упр. стоимости доходов от целевого финансирования при частичной ликвидаци.
	Возврат
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	ТаблицаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ЦелевоеФинансирование) КАК Статья,
	|	НЕОПРЕДЕЛЕНО КАК Аналитика,
	|	
	|	ВЫРАЗИТЬ((втЦелевыеСредстваУпр.Сумма * (ТаблицаОС.СуммаСписанияБУЦФ/втЦелевыеСредстваРеглСгруппированныеПоНаправлениям.Сумма)) КАК ЧИСЛО(31,2)) КАК Сумма
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЦелевыеСредстваРеглСгруппированныеПоНаправлениям КАК втЦелевыеСредстваРеглСгруппированныеПоНаправлениям
	|		ПО &Подразделение = втЦелевыеСредстваРеглСгруппированныеПоНаправлениям.Подразделение
	|			И ТаблицаОС.НаправлениеДеятельности = втЦелевыеСредстваРеглСгруппированныеПоНаправлениям.НаправлениеДеятельности
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЦелевыеСредстваУпр КАК втЦелевыеСредстваУпр
	|		ПО &Подразделение = втЦелевыеСредстваУпр.Подразделение
	|			И ТаблицаОС.НаправлениеДеятельности = втЦелевыеСредстваУпр.НаправлениеДеятельности
	|ГДЕ
	|	&ИспользоватьУчетПрочихАктивовПассивов
	|	И &ПогашениеУпрЗатрат
	|	И &ЧастичнаяЛиквидация";
	
КонецФункции

Функция ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете()
	
	Возврат
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	
	|	ПорядокУчетаОСБУ.Организация КАК Организация,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ) КАК ДатаОтражения
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОСБУ.СрезПоследних(
	|		&Дата,
	|		Регистратор<>&Ссылка
	|		И ОсновноеСредство В (ВЫБРАТЬ Т.ОсновноеСредство ИЗ ТаблицаОС КАК Т)) КАК ПорядокУчетаОСБУ
	|ГДЕ
	|	ПорядокУчетаОСБУ.СостояниеБУ = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКЗабалансовомуУчету)";
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК ДатаДокументаИБ,
	|	&Номер                                  КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&Организация                            КАК Организация,
	|	&Подразделение                          КАК Подразделение,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	&Ответственный                          КАК Ответственный,
	|	&Комментарий                            КАК Комментарий,
	|	&Проведен                               КАК Проведен,
	|	&ПометкаУдаления                        КАК ПометкаУдаления,
	|	ЛОЖЬ                                    КАК ДополнительнаяЗапись,
	|	&Период                                 КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать                          КАК НомерПервичногоДокумента,
	|	ЛОЖЬ                                    КАК СторноИсправление,
	|	НЕОПРЕДЕЛЕНО                            КАК СторнируемыйДокумент,
	|	НЕОПРЕДЕЛЕНО                            КАК ИсправляемыйДокумент,
	|	&Период                                 КАК ДатаОтраженияВУчете,
	|	НЕОПРЕДЕЛЕНО                            КАК Приоритет";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДокументыПоОС";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(ТаблицаОС.НомерСтроки-1, 0)    КАК НомерЗаписи,
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК Дата,
	|	&Организация                            КАК Организация,
	|	&Подразделение                          КАК Подразделение,
	|	&Проведен                               КАК Проведен,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&СобытиеОС                              КАК СобытиеОС,
	|	ИСТИНА                                  КАК ОтражатьВРеглУчете,
	|	ЛОЖЬ                                    КАК ОтражатьВУпрУчете,
	|	ТаблицаОС.ОсновноеСредство              КАК ОсновноеСредство
	|ИЗ
	|	Документ.СписаниеОС КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СписаниеОС.ОС КАК ТаблицаОС
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область ПроведениеПоРеглУчету

Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	ТекстыВременныхТаблиц = Новый Массив;
	ТекстыВременныхТаблиц.Добавить(УчетОСВызовСервера.ТекстЗапросаВТОтраженияВРеглУчетеНачисленнойАмортизации("СписаниеОС"));
	ТекстыВременныхТаблиц.Добавить(УчетОСВызовСервера.ТекстЗапросаВТОтраженияВРеглУчетеОстаткиПоСчетам());
	ТекстыВременныхТаблиц.Добавить(УчетОСВызовСервера.ТекстЗапросаВТОтраженияВРеглУчетеОстаткиРезерваПереоценки());
	ТекстыВременныхТаблиц.Добавить(ТекстЗапросаВТСуммыЧастичнойЛиквидации());
	ТекстыВременныхТаблиц.Добавить(Символы.ПС);
	
	Возврат СтрСоединить(ТекстыВременныхТаблиц, ОбщегоНазначения.РазделительПакетаЗапросов());
	
КонецФункции

Функция ТекстЗапросаВТСуммыЧастичнойЛиквидации()
	
	Возврат
	"ВЫБРАТЬ
	|	Строки.ОсновноеСредство КАК ОбъектУчета,
	|	
	|	Строки.СуммаСписанияБУ - ВЫРАЗИТЬ(Строки.СуммаСписанияБУ * втОстаткиПоСчетам.ДоляЦФБУ КАК ЧИСЛО(31,2)) КАК СуммаБУ,
	|	Строки.СуммаСписанияНУ - ВЫРАЗИТЬ(Строки.СуммаСписанияНУ * втОстаткиПоСчетам.ДоляЦФНУ КАК ЧИСЛО(31,2)) КАК СуммаНУ,
	|	Строки.СуммаСписанияПР - ВЫРАЗИТЬ(Строки.СуммаСписанияПР * втОстаткиПоСчетам.ДоляЦФБУ КАК ЧИСЛО(31,2)) КАК СуммаПР,
	|	
	|	ВЫРАЗИТЬ(Строки.СуммаСписанияБУ * втОстаткиПоСчетам.ДоляЦФБУ КАК ЧИСЛО(31,2)) КАК СуммаБУЦФ,
	|	ВЫРАЗИТЬ(Строки.СуммаСписанияНУ * втОстаткиПоСчетам.ДоляЦФНУ КАК ЧИСЛО(31,2)) КАК СуммаНУЦФ,
	|	ВЫРАЗИТЬ(Строки.СуммаСписанияПР * втОстаткиПоСчетам.ДоляЦФБУ КАК ЧИСЛО(31,2)) КАК СуммаПРЦФ
	|	
	|ПОМЕСТИТЬ втВостановительныеСтоимостиЧастичнойЛиквидации
	|ИЗ
	|	Документ.СписаниеОС КАК Операция
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеОС.ОС КАК Строки
	|		ПО Строки.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиПоСчетам КАК втОстаткиПоСчетам
	|		ПО Строки.ОсновноеСредство = втОстаткиПоСчетам.ОбъектУчета
	|
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ЧастичнаяЛиквидация
	|;
	|
	|ВЫБРАТЬ
	|	втВостановительныеСтоимостиЧастичнойЛиквидации.ОбъектУчета КАК ОбъектУчета,
	|	
	|	втВостановительныеСтоимостиЧастичнойЛиквидации.СуммаБУ КАК ВосстановительнаяСтоимостьБУ,
	|	втВостановительныеСтоимостиЧастичнойЛиквидации.СуммаНУ КАК ВосстановительнаяСтоимостьНУ,
	|	втВостановительныеСтоимостиЧастичнойЛиквидации.СуммаПР КАК ВосстановительнаяСтоимостьПР,
	|	
	|	втВостановительныеСтоимостиЧастичнойЛиквидации.СуммаБУЦФ КАК ВосстановительнаяСтоимостьБУЦФ,
	|	втВостановительныеСтоимостиЧастичнойЛиквидации.СуммаНУЦФ КАК ВосстановительнаяСтоимостьНУЦФ,
	|	втВостановительныеСтоимостиЧастичнойЛиквидации.СуммаПРЦФ КАК ВосстановительнаяСтоимостьПРЦФ,
	|	
	// НакопленнаяАмортизацияБУ
	|	ВЫБОР КОГДА ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ, 0) = 0 ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ((ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияБУ, 0))
	|				* втВостановительныеСтоимостиЧастичнойЛиквидации.СуммаБУ
	|				/ ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ, 0) КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК НакопленнаяАмортизацияБУ,
	|	
	// НакопленнаяАмортизацияНУ
	|	ВЫБОР КОГДА ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьНУ, 0) = 0 ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ((ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияНУ, 0))
	|				* втВостановительныеСтоимостиЧастичнойЛиквидации.СуммаНУ 
	|				/ ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьНУ, 0) КАК ЧИСЛО(31,2)) 
	|	КОНЕЦ КАК НакопленнаяАмортизацияНУ,
	|	
	// НакопленнаяАмортизацияПР
	|	ВЫБОР КОГДА ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ, 0) = 0 ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ((ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияПР, 0)) 
	|				* втВостановительныеСтоимостиЧастичнойЛиквидации.СуммаПР 
	|				/ ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ, 0) КАК ЧИСЛО(31,2)) 
	|	КОНЕЦ КАК НакопленнаяАмортизацияПР,
	|	
	// НакопленнаяАмортизацияБУЦФ
	|	ВЫБОР КОГДА ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУЦФ, 0) = 0 ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ((ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияБУЦФ, 0)) 
	|				* втВостановительныеСтоимостиЧастичнойЛиквидации.СуммаБУЦФ 
	|				/ ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУЦФ, 0) КАК ЧИСЛО(31,2)) 
	|	КОНЕЦ КАК НакопленнаяАмортизацияБУЦФ,
	|	
	// НакопленнаяАмортизацияНУЦФ
	|	ВЫБОР КОГДА ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьНУЦФ, 0) = 0 ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ((ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияНУЦФ, 0)) 
	|				* втВостановительныеСтоимостиЧастичнойЛиквидации.СуммаНУЦФ 
	|				/ ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьНУЦФ, 0) КАК ЧИСЛО(31,2)) 
	|	КОНЕЦ КАК НакопленнаяАмортизацияНУЦФ,
	|	
	// НакопленнаяАмортизацияПРЦФ
	|	ВЫБОР КОГДА ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУЦФ, 0) = 0 ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ((ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияПРЦФ, 0)) 
	|				* втВостановительныеСтоимостиЧастичнойЛиквидации.СуммаПРЦФ 
	|				/ ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУЦФ, 0) КАК ЧИСЛО(31,2)) 
	|	КОНЕЦ КАК НакопленнаяАмортизацияПРЦФ,
	|	
	// ОстаточнаяСтоимостьБУ
	|	втВостановительныеСтоимостиЧастичнойЛиквидации.СуммаБУ
	|		- ВЫБОР КОГДА ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ, 0) = 0 ТОГДА 0
	|				ИНАЧЕ ВЫРАЗИТЬ((ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияБУ, 0)) 
	|						* втВостановительныеСтоимостиЧастичнойЛиквидации.СуммаБУ 
	|						/ ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ, 0) КАК ЧИСЛО(31,2)) 
	|	КОНЕЦ КАК ОстаточнаяСтоимостьБУ,
	|	
	// ОстаточнаяСтоимостьНУ
	|	втВостановительныеСтоимостиЧастичнойЛиквидации.СуммаНУ
	|		- ВЫБОР КОГДА ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьНУ, 0) = 0 ТОГДА 0
	|				ИНАЧЕ ВЫРАЗИТЬ((ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияНУ, 0)) 
	|						* втВостановительныеСтоимостиЧастичнойЛиквидации.СуммаНУ 
	|						/ ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьНУ, 0) КАК ЧИСЛО(31,2)) 
	|	КОНЕЦ КАК ОстаточнаяСтоимостьНУ,
	|	
	// ОстаточнаяСтоимостьПР
	|	втВостановительныеСтоимостиЧастичнойЛиквидации.СуммаПР
	|		- ВЫБОР КОГДА ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ, 0) = 0 ТОГДА 0
	|				ИНАЧЕ ВЫРАЗИТЬ((ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияПР, 0)) 
	|						* втВостановительныеСтоимостиЧастичнойЛиквидации.СуммаПР 
	|						/ ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ, 0) КАК ЧИСЛО(31,2)) 
	|	КОНЕЦ КАК ОстаточнаяСтоимостьПР,
	|	
	// ОстаточнаяСтоимостьБУЦФ
	|	втВостановительныеСтоимостиЧастичнойЛиквидации.СуммаБУЦФ
	|		- ВЫБОР КОГДА ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУЦФ, 0) = 0 ТОГДА 0
	|				ИНАЧЕ ВЫРАЗИТЬ((ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияБУЦФ, 0)) 
	|						* втВостановительныеСтоимостиЧастичнойЛиквидации.СуммаБУЦФ 
	|						/ ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУЦФ, 0) КАК ЧИСЛО(31,2)) 
	|	КОНЕЦ КАК ОстаточнаяСтоимостьБУЦФ,
	|	
	// ОстаточнаяСтоимостьНУЦФ
	|	втВостановительныеСтоимостиЧастичнойЛиквидации.СуммаНУЦФ
	|		- ВЫБОР КОГДА ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьНУЦФ, 0) = 0 ТОГДА 0
	|				ИНАЧЕ ВЫРАЗИТЬ((ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияНУЦФ, 0)) 
	|						* втВостановительныеСтоимостиЧастичнойЛиквидации.СуммаНУЦФ 
	|						/ ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьНУЦФ, 0) КАК ЧИСЛО(31,2)) 
	|	КОНЕЦ КАК ОстаточнаяСтоимостьНУЦФ,
	|	
	// ОстаточнаяСтоимостьПРЦФ
	|	втВостановительныеСтоимостиЧастичнойЛиквидации.СуммаПРЦФ
	|		- ВЫБОР КОГДА ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУЦФ, 0) = 0 ТОГДА 0
	|				ИНАЧЕ ВЫРАЗИТЬ((ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияПРЦФ, 0)) 
	|						* втВостановительныеСтоимостиЧастичнойЛиквидации.СуммаПРЦФ 
	|						/ ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУЦФ, 0) КАК ЧИСЛО(31,2)) 
	|	КОНЕЦ КАК ОстаточнаяСтоимостьПРЦФ,
	|	
	// НеУчитываемаяСтоимостьНУ
	|	ВЫБОР КОГДА ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ, 0) = 0 ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ((ЕСТЬNULL(втОстаткиПоСчетам.НеУчитываемаяСтоимостьНУ, 0))
	|				* втВостановительныеСтоимостиЧастичнойЛиквидации.СуммаБУ
	|				/ ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ, 0) КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК НеУчитываемаяСтоимостьНУ,
	|	
	// КорректировкаСтоимостиПР
	|	ВЫБОР КОГДА ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ, 0) = 0 ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ((ЕСТЬNULL(втОстаткиПоСчетам.КорректировкаСтоимостиПР, 0))
	|				* втВостановительныеСтоимостиЧастичнойЛиквидации.СуммаБУ
	|				/ ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ, 0) КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК КорректировкаСтоимостиПР,
	|	
	// КорректировкаСтоимостиВР
	|	ВЫБОР КОГДА ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ, 0) = 0 ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ((ЕСТЬNULL(втОстаткиПоСчетам.КорректировкаСтоимостиВР, 0))
	|				* втВостановительныеСтоимостиЧастичнойЛиквидации.СуммаБУ
	|				/ ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ, 0) КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК КорректировкаСтоимостиВР
	|
	|ПОМЕСТИТЬ втСуммыЧастичнойЛиквидации
	|ИЗ
	|	втВостановительныеСтоимостиЧастичнойЛиквидации КАК втВостановительныеСтоимостиЧастичнойЛиквидации
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиПоСчетам КАК втОстаткиПоСчетам
	|		ПО втВостановительныеСтоимостиЧастичнойЛиквидации.ОбъектУчета = втОстаткиПоСчетам.ОбъектУчета
	|;
	|
	|ВЫБРАТЬ
	|	втОстаткиРезерваДооценки.ОбъектУчета КАК ОбъектУчета,
	|	втОстаткиРезерваДооценки.Счет КАК Счет,
	|	ВЫБОР КОГДА ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ, 0) = 0 ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ((ЕСТЬNULL(втОстаткиРезерваДооценки.СуммаБУ, 0))
	|				* втВостановительныеСтоимостиЧастичнойЛиквидации.СуммаБУ
	|				/ ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ, 0) КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаБУ,
	|	ВЫБОР КОГДА ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ, 0) = 0 ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ((ЕСТЬNULL(втОстаткиРезерваДооценки.СуммаНУ, 0))
	|				* втВостановительныеСтоимостиЧастичнойЛиквидации.СуммаБУ
	|				/ ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ, 0) КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаНУ,
	|	ВЫБОР КОГДА ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ, 0) = 0 ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ((ЕСТЬNULL(втОстаткиРезерваДооценки.СуммаПР, 0))
	|				* втВостановительныеСтоимостиЧастичнойЛиквидации.СуммаБУ
	|				/ ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ, 0) КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаПР,
	|	ВЫБОР КОГДА ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ, 0) = 0 ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ((ЕСТЬNULL(втОстаткиРезерваДооценки.СуммаВР, 0))
	|				* втВостановительныеСтоимостиЧастичнойЛиквидации.СуммаБУ
	|				/ ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ, 0) КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаВР
	|ПОМЕСТИТЬ втРезервДооценкиЧастичнойЛиквидации
	|ИЗ
	|	втОстаткиРезерваДооценки КАК втОстаткиРезерваДооценки
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втВостановительныеСтоимостиЧастичнойЛиквидации КАК втВостановительныеСтоимостиЧастичнойЛиквидации
	|		ПО втВостановительныеСтоимостиЧастичнойЛиквидации.ОбъектУчета = втОстаткиРезерваДооценки.ОбъектУчета
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиПоСчетам КАК втОстаткиПоСчетам
	|		ПО втОстаткиПоСчетам.ОбъектУчета = втОстаткиРезерваДооценки.ОбъектУчета";
	
КонецФункции

Функция ТекстОтраженияВРеглУчете() Экспорт
	
	ТекстыОтражения = Новый Массив;
	ТекстыОтражения.Добавить(УчетОСВызовСервера.ТекстОтраженияВРеглУчетеНачисленнойАмортизации());
	ТекстыОтражения.Добавить(СписаниеПервоначальнойСтоимостиОС());
	ТекстыОтражения.Добавить(СписаниеПервоначальнойСтоимостиОСЦФ());
	ТекстыОтражения.Добавить(СписаниеНачисленнойАмортизации());
	ТекстыОтражения.Добавить(СписаниеНачисленнойАмортизацииЦФ());
	ТекстыОтражения.Добавить(СписаниеНачисленногоИзноса());
	ТекстыОтражения.Добавить(СписаниеОстаточнойСтоимостиОС());
	ТекстыОтражения.Добавить(СписаниеДоходыОтЦелевогоФинансирования());
	ТекстыОтражения.Добавить(ЧастичнаяЛиквидацияПервоначальнойСтоимостиОС());
	ТекстыОтражения.Добавить(ЧастичнаяЛиквидацияПервоначальнойСтоимостиОСЦФ());
	ТекстыОтражения.Добавить(ЧастичнаяЛиквидацияНачисленнойАмортизации());
	ТекстыОтражения.Добавить(ЧастичнаяЛиквидацияНачисленнойАмортизацииЦФ());
	ТекстыОтражения.Добавить(ЧастичнаяЛиквидацияНачисленногоИзноса());
	ТекстыОтражения.Добавить(ЧастичнаяЛиквидацияСоСчетаВыбытияОС());
	ТекстыОтражения.Добавить(ЧастичнаяЛиквидацияДоходыОтЦелевогоФинансирования());
	ТекстыОтражения.Добавить(СписаниеРезерваДооценки());
	ТекстыОтражения.Добавить(ЧастичноеСписаниеРезерваДооценки());
	ТекстыОтражения.Добавить(ВыбытиеАрендованногоОС());
	ТекстыОтражения.Добавить(СписаниеКорректировкиСтоимостиАрендованногоИмущества());
	ТекстыОтражения.Добавить(ЧастичноеСписаниеКорректировкиСтоимостиАрендованногоИмущества());
	
	Возврат СтрСоединить(ТекстыОтражения, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
КонецФункции

Функция СписаниеПервоначальнойСтоимостиОС()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Списание первоначальной стоимости ОС (Дт 01.09:: Кт Счет учета ОС)
	|ВЫБРАТЬ
	|	
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ, 0) КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	втПервоначальныеСведения.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	Операция.СчетВыбытияОС КАК СчетДт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьНУ, 0) КАК СуммаНУДт,
	|	ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьПР, 0) КАК СуммаПРДт,
	|	ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьВР, 0) КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	втПервоначальныеСведения.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	СчетаОтражения.СчетУчета КАК СчетКт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьНУ, 0) КАК СуммаНУКт,
	|	ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьПР, 0) КАК СуммаПРКт,
	|	ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьВР, 0) КАК СуммаВРКт,
	|	
	|	""Списание первоначальной стоимости ОС"" КАК Содержание
	|ИЗ
	|	Документ.СписаниеОС КАК Операция
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеОС.ОС КАК Строки
	|		ПО Строки.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСчетаОтражения КАК СчетаОтражения
	|		ПО Строки.ОсновноеСредство = СчетаОтражения.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиПоСчетам КАК втОстаткиПоСчетам
	|		ПО Строки.ОсновноеСредство = втОстаткиПоСчетам.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПервоначальныеСведения КАК втПервоначальныеСведения
	|	ПО Строки.ОсновноеСредство = втПервоначальныеСведения.ОбъектУчета
	|	
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И НЕ Операция.ЧастичнаяЛиквидация
	|";
	
КонецФункции

Функция СписаниеПервоначальнойСтоимостиОСЦФ()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Списание первоначальной стоимости ОС (Дт 01.09:: Кт Счет учета ОС)
	|ВЫБРАТЬ
	|	
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУЦФ, 0) КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	втПервоначальныеСведения.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	Операция.СчетВыбытияОС КАК СчетДт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьНУЦФ, 0) КАК СуммаНУДт,
	|	ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьПРЦФ, 0) КАК СуммаПРДт,
	|	ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьВРЦФ, 0) КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	втПервоначальныеСведения.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	СчетаОтражения.СчетУчетаЦФ КАК СчетКт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьНУЦФ, 0) КАК СуммаНУКт,
	|	ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьПРЦФ, 0) КАК СуммаПРКт,
	|	ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьВРЦФ, 0) КАК СуммаВРКт,
	|	
	|	""Списание первоначальной стоимости ОС"" КАК Содержание
	|ИЗ
	|	Документ.СписаниеОС КАК Операция
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеОС.ОС КАК Строки
	|		ПО Строки.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСчетаОтражения КАК СчетаОтражения
	|		ПО Строки.ОсновноеСредство = СчетаОтражения.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиПоСчетам КАК втОстаткиПоСчетам
	|		ПО Строки.ОсновноеСредство = втОстаткиПоСчетам.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПервоначальныеСведения КАК втПервоначальныеСведения
	|	ПО Строки.ОсновноеСредство = втПервоначальныеСведения.ОбъектУчета
	|	
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И НЕ Операция.ЧастичнаяЛиквидация
	|	И СчетаОтражения.ПрименениеЦелевогоФинансирования
	|";
	
КонецФункции

Функция СписаниеНачисленнойАмортизации()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Списание начисленной амортизации (Дт Счет начисления амортизации:: Кт 01.09)
	|ВЫБРАТЬ
	|	
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияБУ, 0) КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	втПервоначальныеСведения.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	СчетаОтражения.СчетНачисленияАмортизации КАК СчетДт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияНУ, 0) КАК СуммаНУДт,
	|	ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияПР, 0) КАК СуммаПРДт,
	|	ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияВР, 0) КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	втПервоначальныеСведения.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	Операция.СчетВыбытияОС КАК СчетКт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияНУ, 0) КАК СуммаНУКт,
	|	ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияПР, 0) КАК СуммаПРКт,
	|	ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияВР, 0) КАК СуммаВРКт,
	|	
	|	""Списание начисленной амортизации"" КАК Содержание
	|ИЗ
	|	Документ.СписаниеОС КАК Операция
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеОС.ОС КАК Строки
	|		ПО Строки.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПервоначальныеСведения КАК втПервоначальныеСведения
	|	ПО Строки.ОсновноеСредство = втПервоначальныеСведения.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСчетаОтражения КАК СчетаОтражения
	|		ПО Строки.ОсновноеСредство = СчетаОтражения.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиПоСчетам КАК втОстаткиПоСчетам
	|		ПО Строки.ОсновноеСредство = втОстаткиПоСчетам.ОбъектУчета
	|	
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И НЕ Операция.ЧастичнаяЛиквидация
	|	И НЕ втПервоначальныеСведения.НачислятьИзнос
	|	И втПервоначальныеСведения.ЕстьСчетАмортизации
	|";
	
КонецФункции

Функция СписаниеНачисленнойАмортизацииЦФ()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Списание начисленной амортизации (Дт Счет начисления амортизации:: Кт 01.09)
	|ВЫБРАТЬ
	|	
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияБУЦФ,0) КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	втПервоначальныеСведения.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	СчетаОтражения.СчетНачисленияАмортизацииЦФ КАК СчетДт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияНУЦФ,0) КАК СуммаНУДт,
	|	ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияПРЦФ,0) КАК СуммаПРДт,
	|	ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияВРЦФ,0) КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	втПервоначальныеСведения.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	Операция.СчетВыбытияОС КАК СчетКт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияНУЦФ,0) КАК СуммаНУКт,
	|	ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияПРЦФ,0) КАК СуммаПРКт,
	|	ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияВРЦФ,0) КАК СуммаВРКт,
	|	
	|	""Списание начисленной амортизации"" КАК Содержание
	|ИЗ
	|	Документ.СписаниеОС КАК Операция
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеОС.ОС КАК Строки
	|		ПО Строки.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПервоначальныеСведения КАК втПервоначальныеСведения
	|	ПО Строки.ОсновноеСредство = втПервоначальныеСведения.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСчетаОтражения КАК СчетаОтражения
	|		ПО Строки.ОсновноеСредство = СчетаОтражения.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиПоСчетам КАК втОстаткиПоСчетам
	|		ПО Строки.ОсновноеСредство = втОстаткиПоСчетам.ОбъектУчета
	|	
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И НЕ Операция.ЧастичнаяЛиквидация
	|	И НЕ втПервоначальныеСведения.НачислятьИзнос
	|	И СчетаОтражения.ПрименениеЦелевогоФинансирования
	|	И втПервоначальныеСведения.ЕстьСчетАмортизации
	|";
	
КонецФункции

Функция СписаниеНачисленногоИзноса()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Списание начисленной амортизации (Дт --- :: Кт СчетИзноса)
	|ВЫБРАТЬ
	|	
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияБУ,0) КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	
	|	СчетаОтражения.СчетНачисленияАмортизации КАК СчетКт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Списание начисленного износа"" КАК Содержание
	|ИЗ
	|	Документ.СписаниеОС КАК Операция
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеОС.ОС КАК Строки
	|		ПО Строки.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПервоначальныеСведения КАК втПервоначальныеСведения
	|	ПО Строки.ОсновноеСредство = втПервоначальныеСведения.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСчетаОтражения КАК СчетаОтражения
	|		ПО Строки.ОсновноеСредство = СчетаОтражения.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиПоСчетам КАК втОстаткиПоСчетам
	|		ПО Строки.ОсновноеСредство = втОстаткиПоСчетам.ОбъектУчета
	|	
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И НЕ Операция.ЧастичнаяЛиквидация
	|	И втПервоначальныеСведения.НачислятьИзнос
	|";
	
КонецФункции

Функция СписаниеОстаточнойСтоимостиОС()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Списание остаточной стоимости ОС (Дт ...:: Кт 01.09)
	|ВЫБРАТЬ
	|	
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	ЕСТЬNULL(втОстаткиПоСчетам.ОстаточнаяСтоимостьБУ + втОстаткиПоСчетам.ОстаточнаяСтоимостьБУЦФ, 0) КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ВЫБОР КОГДА СтатьиСтроительства.ВидЦенностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеОперации)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы)
	|	КОНЕЦ КАК ВидСчетаДт,
	|	Операция.СтатьяРасходов КАК АналитикаУчетаДт,
	|	Операция.Подразделение КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	втПервоначальныеСведения.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	|	ВЫБОР КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL ТОГДА
	|		Операция.АналитикаРасходов // Объект строительства
	|	ИНАЧЕ
	|		Операция.СтатьяРасходов
	|	КОНЕЦ КАК СубконтоДт1,
	|	ВЫБОР КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL ТОГДА
	|		Операция.СтатьяРасходов
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее)
	|	КОНЕЦ КАК СубконтоДт2,
	|	ВЫБОР КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Подрядный)
	|	ИНАЧЕ
	|		Строки.ОсновноеСредство
	|	КОНЕЦ КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	ЕСТЬNULL(втОстаткиПоСчетам.ОстаточнаяСтоимостьНУ 
	|		+ втОстаткиПоСчетам.ОстаточнаяСтоимостьНУЦФ 
	|		+ втОстаткиПоСчетам.НеУчитываемаяСтоимостьНУ, 0) КАК СуммаНУДт,
	|	ЕСТЬNULL(втОстаткиПоСчетам.ОстаточнаяСтоимостьПР 
	|		+ втОстаткиПоСчетам.ОстаточнаяСтоимостьПРЦФ 
	|		+ втОстаткиПоСчетам.КорректировкаСтоимостиПР, 0) КАК СуммаПРДт,
	|	ЕСТЬNULL(втОстаткиПоСчетам.ОстаточнаяСтоимостьВР 
	|		+ втОстаткиПоСчетам.ОстаточнаяСтоимостьВРЦФ 
	|		+ втОстаткиПоСчетам.КорректировкаСтоимостиВР, 0) КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	втПервоначальныеСведения.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	Операция.СчетВыбытияОС КАК СчетКт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	ЕСТЬNULL(втОстаткиПоСчетам.ОстаточнаяСтоимостьНУ 
	|		+ втОстаткиПоСчетам.ОстаточнаяСтоимостьНУЦФ 
	|		+ втОстаткиПоСчетам.НеУчитываемаяСтоимостьНУ, 0) КАК СуммаНУКт,
	|	ЕСТЬNULL(втОстаткиПоСчетам.ОстаточнаяСтоимостьПР 
	|		+ втОстаткиПоСчетам.ОстаточнаяСтоимостьПРЦФ 
	|		+ втОстаткиПоСчетам.КорректировкаСтоимостиПР, 0) КАК СуммаПРКт,
	|	ЕСТЬNULL(втОстаткиПоСчетам.ОстаточнаяСтоимостьВР 
	|		+ втОстаткиПоСчетам.ОстаточнаяСтоимостьВРЦФ 
	|		+ втОстаткиПоСчетам.КорректировкаСтоимостиВР, 0) КАК СуммаВРКт,
	|	
	|	""Списание остаточной стоимости ОС"" КАК Содержание
	|ИЗ
	|	Документ.СписаниеОС КАК Операция
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеОС.ОС КАК Строки
	|		ПО Строки.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПервоначальныеСведения КАК втПервоначальныеСведения
	|	ПО Строки.ОсновноеСредство = втПервоначальныеСведения.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСчетаОтражения КАК СчетаОтражения
	|		ПО Строки.ОсновноеСредство = СчетаОтражения.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиПоСчетам КАК втОстаткиПоСчетам
	|		ПО Строки.ОсновноеСредство = втОстаткиПоСчетам.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
	|	ПО
	|		Операция.СтатьяРасходов = СтатьиСтроительства.Ссылка
	|		И СтатьиСтроительства.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|	
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И НЕ Операция.ЧастичнаяЛиквидация
	|";
КонецФункции

Функция СписаниеДоходыОтЦелевогоФинансирования()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Признание доходов от целевого финансирования (Дт 98 :: Кт 91)
	|ВЫБРАТЬ
	|	
	|	&Ссылка КАК Ссылка,
	|	&Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	"""" КАК ИдентификаторСтроки,
	|	
	|	ЕСТЬNULL(втОстаткиПоСчетам.ОстаточнаяСтоимостьБУЦФ, 0) КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Местонахождение.Подразделение КАК ПодразделениеДт,
	|	втПервоначальныеСведения.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ДоходыБудущихПериодовОтЦелевогоФинансированияОС) КАК СчетДт,
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	ЕСТЬNULL(втОстаткиПоСчетам.ОстаточнаяСтоимостьНУЦФ, 0)
	|	+ ЕСТЬNULL(втОстаткиПоСчетам.ОстаточнаяСтоимостьПРЦФ, 0)
	|	+ ЕСТЬNULL(втОстаткиПоСчетам.ОстаточнаяСтоимостьВРЦФ, 0) КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Доходы) КАК ВидСчетаКт,
	|	СчетаОтражения.СтатьяДоходов КАК АналитикаУчетаКт,
	|	Местонахождение.Подразделение КАК МестоУчетаКт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Местонахождение.Подразделение КАК ПодразделениеКт,
	|	втПервоначальныеСведения.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	СчетаОтражения.СтатьяДоходов КАК СубконтоКт1,
	|	СчетаОтражения.АналитикаДоходов КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	ЕСТЬNULL(втОстаткиПоСчетам.ОстаточнаяСтоимостьНУЦФ, 0)
	|	+ ЕСТЬNULL(втОстаткиПоСчетам.ОстаточнаяСтоимостьПРЦФ, 0)
	|	+ ЕСТЬNULL(втОстаткиПоСчетам.ОстаточнаяСтоимостьВРЦФ, 0) КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	/////////////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	""Признание доходов от целевого финансирования при списании остаточной стоимости"" КАК Содержание
	|ИЗ
	|	Документ.СписаниеОС КАК Операция
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеОС.ОС КАК Строки
	|		ПО Строки.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втМестонахожденияОС КАК Местонахождение
	|	ПО Строки.ОсновноеСредство = Местонахождение.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПервоначальныеСведения КАК втПервоначальныеСведения
	|	ПО Строки.ОсновноеСредство = втПервоначальныеСведения.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСчетаОтражения КАК СчетаОтражения
	|		ПО Строки.ОсновноеСредство = СчетаОтражения.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиПоСчетам КАК втОстаткиПоСчетам
	|		ПО Строки.ОсновноеСредство = втОстаткиПоСчетам.ОбъектУчета
	|	
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И НЕ Операция.ЧастичнаяЛиквидация
	|	И СчетаОтражения.ПрименениеЦелевогоФинансирования
	|";
	
КонецФункции

Функция ЧастичнаяЛиквидацияПервоначальнойСтоимостиОС()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Частичная ликвидация восстановительной стоимости ОС (Дт 01.09:: Кт Счет учета ОС)
	|ВЫБРАТЬ
	|	
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	втСуммыЧастичнойЛиквидации.ВосстановительнаяСтоимостьБУ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	втПервоначальныеСведения.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	Операция.СчетВыбытияОС КАК СчетДт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	втСуммыЧастичнойЛиквидации.ВосстановительнаяСтоимостьНУ КАК СуммаНУДт,
	|	втСуммыЧастичнойЛиквидации.ВосстановительнаяСтоимостьПР КАК СуммаПРДт,
	|	втСуммыЧастичнойЛиквидации.ВосстановительнаяСтоимостьБУ - втСуммыЧастичнойЛиквидации.ВосстановительнаяСтоимостьНУ - втСуммыЧастичнойЛиквидации.ВосстановительнаяСтоимостьПР КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	втПервоначальныеСведения.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	СчетаОтражения.СчетУчета КАК СчетКт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	втСуммыЧастичнойЛиквидации.ВосстановительнаяСтоимостьНУ КАК СуммаНУКт,
	|	втСуммыЧастичнойЛиквидации.ВосстановительнаяСтоимостьПР КАК СуммаПРКт,
	|	втСуммыЧастичнойЛиквидации.ВосстановительнаяСтоимостьБУ - втСуммыЧастичнойЛиквидации.ВосстановительнаяСтоимостьНУ - втСуммыЧастичнойЛиквидации.ВосстановительнаяСтоимостьПР КАК СуммаВРКт,
	|	
	|	""Частичная ликвидация восстановительной стоимости"" КАК Содержание
	|ИЗ
	|	Документ.СписаниеОС КАК Операция
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеОС.ОС КАК Строки
	|		ПО Строки.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСчетаОтражения КАК СчетаОтражения
	|		ПО Строки.ОсновноеСредство = СчетаОтражения.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСуммыЧастичнойЛиквидации КАК втСуммыЧастичнойЛиквидации
	|	ПО Строки.ОсновноеСредство = втСуммыЧастичнойЛиквидации.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПервоначальныеСведения КАК втПервоначальныеСведения
	|	ПО Строки.ОсновноеСредство = втПервоначальныеСведения.ОбъектУчета
	|	
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ЧастичнаяЛиквидация
	|";
	
КонецФункции

Функция ЧастичнаяЛиквидацияПервоначальнойСтоимостиОСЦФ()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Частичная ликвидация целевых средств в восстановительной стоимости ОС (Дт 01.09:: Кт Счет учета ОС)
	|ВЫБРАТЬ
	|	
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	втСуммыЧастичнойЛиквидации.ВосстановительнаяСтоимостьБУЦФ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	втПервоначальныеСведения.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	Операция.СчетВыбытияОС КАК СчетДт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	втСуммыЧастичнойЛиквидации.ВосстановительнаяСтоимостьНУЦФ КАК СуммаНУДт,
	|	втСуммыЧастичнойЛиквидации.ВосстановительнаяСтоимостьПРЦФ КАК СуммаПРДт,
	|	втСуммыЧастичнойЛиквидации.ВосстановительнаяСтоимостьБУЦФ - втСуммыЧастичнойЛиквидации.ВосстановительнаяСтоимостьНУЦФ - втСуммыЧастичнойЛиквидации.ВосстановительнаяСтоимостьПРЦФ КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	втПервоначальныеСведения.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	СчетаОтражения.СчетУчетаЦФ КАК СчетКт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	втСуммыЧастичнойЛиквидации.ВосстановительнаяСтоимостьНУЦФ КАК СуммаНУКт,
	|	втСуммыЧастичнойЛиквидации.ВосстановительнаяСтоимостьПРЦФ КАК СуммаПРКт,
	|	втСуммыЧастичнойЛиквидации.ВосстановительнаяСтоимостьБУЦФ - втСуммыЧастичнойЛиквидации.ВосстановительнаяСтоимостьНУЦФ - втСуммыЧастичнойЛиквидации.ВосстановительнаяСтоимостьПРЦФ КАК СуммаВРКт,
	|	
	|	""Частичная ликвидация целевых средств в восстановительной стоимости"" КАК Содержание
	|ИЗ
	|	Документ.СписаниеОС КАК Операция
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеОС.ОС КАК Строки
	|		ПО Строки.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСчетаОтражения КАК СчетаОтражения
	|		ПО Строки.ОсновноеСредство = СчетаОтражения.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСуммыЧастичнойЛиквидации КАК втСуммыЧастичнойЛиквидации
	|	ПО Строки.ОсновноеСредство = втСуммыЧастичнойЛиквидации.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПервоначальныеСведения КАК втПервоначальныеСведения
	|	ПО Строки.ОсновноеСредство = втПервоначальныеСведения.ОбъектУчета
	|	
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ЧастичнаяЛиквидация
	|	И СчетаОтражения.ПрименениеЦелевогоФинансирования
	|";
	
КонецФункции

Функция ЧастичнаяЛиквидацияНачисленнойАмортизации()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Частичная ликвидация начисленной амортизации (Дт Счет начисления амортизации:: Кт 01.09)
	|ВЫБРАТЬ
	|	
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	втСуммыЧастичнойЛиквидации.НакопленнаяАмортизацияБУ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	втПервоначальныеСведения.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	СчетаОтражения.СчетНачисленияАмортизации КАК СчетДт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	втСуммыЧастичнойЛиквидации.НакопленнаяАмортизацияНУ КАК СуммаНУДт,
	|	втСуммыЧастичнойЛиквидации.НакопленнаяАмортизацияПР КАК СуммаПРДт,
	|	втСуммыЧастичнойЛиквидации.НакопленнаяАмортизацияБУ - втСуммыЧастичнойЛиквидации.НакопленнаяАмортизацияНУ - втСуммыЧастичнойЛиквидации.НакопленнаяАмортизацияПР КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	втПервоначальныеСведения.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	Операция.СчетВыбытияОС КАК СчетКт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	втСуммыЧастичнойЛиквидации.НакопленнаяАмортизацияНУ КАК СуммаНУКт,
	|	втСуммыЧастичнойЛиквидации.НакопленнаяАмортизацияПР КАК СуммаПРКт,
	|	втСуммыЧастичнойЛиквидации.НакопленнаяАмортизацияБУ - втСуммыЧастичнойЛиквидации.НакопленнаяАмортизацияНУ - втСуммыЧастичнойЛиквидации.НакопленнаяАмортизацияПР КАК СуммаВРКт,
	|	
	|	""Частичная ликвидация накопленной амортизации"" КАК Содержание
	|ИЗ
	|	Документ.СписаниеОС КАК Операция
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеОС.ОС КАК Строки
	|		ПО Строки.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПервоначальныеСведения КАК втПервоначальныеСведения
	|	ПО Строки.ОсновноеСредство = втПервоначальныеСведения.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСчетаОтражения КАК СчетаОтражения
	|		ПО Строки.ОсновноеСредство = СчетаОтражения.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСуммыЧастичнойЛиквидации КАК втСуммыЧастичнойЛиквидации
	|	ПО Строки.ОсновноеСредство = втСуммыЧастичнойЛиквидации.ОбъектУчета
	|	
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ЧастичнаяЛиквидация
	|	И НЕ втПервоначальныеСведения.НачислятьИзнос
	|	И НЕ (
	|		втСуммыЧастичнойЛиквидации.НакопленнаяАмортизацияБУ = 0
	|		И втСуммыЧастичнойЛиквидации.НакопленнаяАмортизацияНУ = 0
	|		И втСуммыЧастичнойЛиквидации.НакопленнаяАмортизацияПР = 0
	|	)
	|";
	
КонецФункции

Функция ЧастичнаяЛиквидацияНачисленнойАмортизацииЦФ()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Частичная ликвидация целевых средств в начисленной амортизации (Дт Счет начисления амортизации:: Кт 01.09)
	|ВЫБРАТЬ
	|	
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	втСуммыЧастичнойЛиквидации.НакопленнаяАмортизацияБУЦФ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	втПервоначальныеСведения.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	СчетаОтражения.СчетНачисленияАмортизацииЦФ КАК СчетДт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	втСуммыЧастичнойЛиквидации.НакопленнаяАмортизацияНУЦФ КАК СуммаНУДт,
	|	втСуммыЧастичнойЛиквидации.НакопленнаяАмортизацияПРЦФ КАК СуммаПРДт,
	|	втСуммыЧастичнойЛиквидации.НакопленнаяАмортизацияБУЦФ - втСуммыЧастичнойЛиквидации.НакопленнаяАмортизацияНУЦФ - втСуммыЧастичнойЛиквидации.НакопленнаяАмортизацияПРЦФ КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	втПервоначальныеСведения.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	Операция.СчетВыбытияОС КАК СчетКт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	втСуммыЧастичнойЛиквидации.НакопленнаяАмортизацияНУЦФ КАК СуммаНУКт,
	|	втСуммыЧастичнойЛиквидации.НакопленнаяАмортизацияПРЦФ КАК СуммаПРКт,
	|	втСуммыЧастичнойЛиквидации.НакопленнаяАмортизацияБУЦФ - втСуммыЧастичнойЛиквидации.НакопленнаяАмортизацияНУЦФ - втСуммыЧастичнойЛиквидации.НакопленнаяАмортизацияПРЦФ КАК СуммаВРКт,
	|	
	|	""Частичная ликвидация целевых средств в накопленной амортизации"" КАК Содержание
	|ИЗ
	|	Документ.СписаниеОС КАК Операция
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеОС.ОС КАК Строки
	|		ПО Строки.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПервоначальныеСведения КАК втПервоначальныеСведения
	|	ПО Строки.ОсновноеСредство = втПервоначальныеСведения.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСчетаОтражения КАК СчетаОтражения
	|		ПО Строки.ОсновноеСредство = СчетаОтражения.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСуммыЧастичнойЛиквидации КАК втСуммыЧастичнойЛиквидации
	|	ПО Строки.ОсновноеСредство = втСуммыЧастичнойЛиквидации.ОбъектУчета
	|	
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ЧастичнаяЛиквидация
	|	И НЕ втПервоначальныеСведения.НачислятьИзнос
	|	И СчетаОтражения.ПрименениеЦелевогоФинансирования
	|	И НЕ (
	|		втСуммыЧастичнойЛиквидации.НакопленнаяАмортизацияБУЦФ = 0
	|		И втСуммыЧастичнойЛиквидации.НакопленнаяАмортизацияНУЦФ = 0
	|		И втСуммыЧастичнойЛиквидации.НакопленнаяАмортизацияПРЦФ = 0
	|	)
	|";
	
КонецФункции

Функция ЧастичнаяЛиквидацияНачисленногоИзноса()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Частичная ликвидация начисленной амортизации (Дт --- :: Кт СчетИзноса)
	|ВЫБРАТЬ
	|	
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	ВЫБОР КОГДА ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ, 0) = 0 ТОГДА 0
	|	ИНАЧЕ ВЫРАЗИТЬ(ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияБУ, 0) * Строки.СуммаСписанияБУ / ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ, 0) КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	
	|	СчетаОтражения.СчетНачисленияАмортизации КАК СчетКт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Частичная ликвидация начисленного износа"" КАК Содержание
	|ИЗ
	|	Документ.СписаниеОС КАК Операция
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеОС.ОС КАК Строки
	|		ПО Строки.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПервоначальныеСведения КАК втПервоначальныеСведения
	|	ПО Строки.ОсновноеСредство = втПервоначальныеСведения.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСчетаОтражения КАК СчетаОтражения
	|		ПО Строки.ОсновноеСредство = СчетаОтражения.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиПоСчетам КАК втОстаткиПоСчетам
	|		ПО Строки.ОсновноеСредство = втОстаткиПоСчетам.ОбъектУчета
	|	
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И НЕ Операция.ЧастичнаяЛиквидация
	|	И втПервоначальныеСведения.НачислятьИзнос
	|	И ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияБУ, 0) <> 0
	|";
	
КонецФункции

Функция ЧастичнаяЛиквидацияСоСчетаВыбытияОС()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Частичная ликвидация остаточной стоимости ОС (Дт ...:: Кт 01.09)
	|ВЫБРАТЬ
	|	
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	втСуммыЧастичнойЛиквидации.ОстаточнаяСтоимостьБУ + втСуммыЧастичнойЛиквидации.ОстаточнаяСтоимостьБУЦФ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	Операция.СтатьяРасходов КАК АналитикаУчетаДт,
	|	Операция.Подразделение КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	втПервоначальныеСведения.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ВЫБОР КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL ТОГДА
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СтроительствоОбъектовОсновныхСредств)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|	КОНЕЦ КАК СчетДт,
	|	
	|	ВЫБОР КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL ТОГДА
	|		Операция.АналитикаРасходов // Объект строительства
	|	ИНАЧЕ
	|		Операция.СтатьяРасходов
	|	КОНЕЦ КАК СубконтоДт1,
	|	ВЫБОР КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL ТОГДА
	|		Операция.СтатьяРасходов
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее)
	|	КОНЕЦ КАК СубконтоДт2,
	|	ВЫБОР КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Подрядный)
	|	ИНАЧЕ
	|		Строки.ОсновноеСредство
	|	КОНЕЦ КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	втСуммыЧастичнойЛиквидации.ОстаточнаяСтоимостьНУ 
	|		+ втСуммыЧастичнойЛиквидации.ОстаточнаяСтоимостьНУЦФ 
	|		+ втСуммыЧастичнойЛиквидации.НеУчитываемаяСтоимостьНУ КАК СуммаНУДт,
	|	втСуммыЧастичнойЛиквидации.ОстаточнаяСтоимостьПР
	|		+ втСуммыЧастичнойЛиквидации.ОстаточнаяСтоимостьПРЦФ 
	|		+ втСуммыЧастичнойЛиквидации.КорректировкаСтоимостиПР КАК СуммаПРДт,
	|	(втСуммыЧастичнойЛиквидации.ОстаточнаяСтоимостьБУ + втСуммыЧастичнойЛиквидации.ОстаточнаяСтоимостьБУЦФ)
	|		- (втСуммыЧастичнойЛиквидации.ОстаточнаяСтоимостьНУ + втСуммыЧастичнойЛиквидации.ОстаточнаяСтоимостьНУЦФ)
	|		- (втСуммыЧастичнойЛиквидации.ОстаточнаяСтоимостьПР + втСуммыЧастичнойЛиквидации.ОстаточнаяСтоимостьПРЦФ)
	|		+ втСуммыЧастичнойЛиквидации.КорректировкаСтоимостиВР КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	втПервоначальныеСведения.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	Операция.СчетВыбытияОС КАК СчетКт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	втСуммыЧастичнойЛиквидации.ОстаточнаяСтоимостьНУ 
	|		+ втСуммыЧастичнойЛиквидации.ОстаточнаяСтоимостьНУЦФ 
	|		+ втСуммыЧастичнойЛиквидации.НеУчитываемаяСтоимостьНУ КАК СуммаНУКт,
	|	втСуммыЧастичнойЛиквидации.ОстаточнаяСтоимостьПР
	|		+ втСуммыЧастичнойЛиквидации.ОстаточнаяСтоимостьПРЦФ 
	|		+ втСуммыЧастичнойЛиквидации.КорректировкаСтоимостиПР КАК СуммаПРКт,
	|	(втСуммыЧастичнойЛиквидации.ОстаточнаяСтоимостьБУ + втСуммыЧастичнойЛиквидации.ОстаточнаяСтоимостьБУЦФ)
	|		- (втСуммыЧастичнойЛиквидации.ОстаточнаяСтоимостьНУ + втСуммыЧастичнойЛиквидации.ОстаточнаяСтоимостьНУЦФ)
	|		- (втСуммыЧастичнойЛиквидации.ОстаточнаяСтоимостьПР + втСуммыЧастичнойЛиквидации.ОстаточнаяСтоимостьПРЦФ)
	|		+ втСуммыЧастичнойЛиквидации.КорректировкаСтоимостиВР КАК СуммаВРКт,
	|	
	|	""Частичная ликвидация остаточной стоимости ОС"" КАК Содержание
	|ИЗ
	|	Документ.СписаниеОС КАК Операция
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеОС.ОС КАК Строки
	|		ПО Строки.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПервоначальныеСведения КАК втПервоначальныеСведения
	|	ПО Строки.ОсновноеСредство = втПервоначальныеСведения.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСуммыЧастичнойЛиквидации КАК втСуммыЧастичнойЛиквидации
	|	ПО Строки.ОсновноеСредство = втСуммыЧастичнойЛиквидации.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
	|	ПО
	|		Операция.СтатьяРасходов = СтатьиСтроительства.Ссылка
	|		И СтатьиСтроительства.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ЧастичнаяЛиквидация
	|";
КонецФункции

Функция ЧастичнаяЛиквидацияДоходыОтЦелевогоФинансирования()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Признание доходов от целевого финансирования (Дт 98 :: Кт 91)
	|ВЫБРАТЬ
	|	
	|	&Ссылка КАК Ссылка,
	|	&Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	"""" КАК ИдентификаторСтроки,
	|	
	|	втСуммыЧастичнойЛиквидации.ОстаточнаяСтоимостьБУЦФ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Местонахождение.Подразделение КАК ПодразделениеДт,
	|	втПервоначальныеСведения.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ДоходыБудущихПериодовОтЦелевогоФинансированияОС) КАК СчетДт,
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	втСуммыЧастичнойЛиквидации.ОстаточнаяСтоимостьБУЦФ КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Доходы) КАК ВидСчетаКт,
	|	СчетаОтражения.СтатьяДоходов КАК АналитикаУчетаКт,
	|	Местонахождение.Подразделение КАК МестоУчетаКт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Местонахождение.Подразделение КАК ПодразделениеКт,
	|	втПервоначальныеСведения.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	СчетаОтражения.СтатьяДоходов КАК СубконтоКт1,
	|	СчетаОтражения.АналитикаДоходов КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	втСуммыЧастичнойЛиквидации.ОстаточнаяСтоимостьБУЦФ КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	/////////////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	""Признание доходов от целевого финансирования при списании остаточной стоимости"" КАК Содержание
	|ИЗ
	|	Документ.СписаниеОС КАК Операция
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеОС.ОС КАК Строки
	|	ПО Строки.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втМестонахожденияОС КАК Местонахождение
	|	ПО Строки.ОсновноеСредство = Местонахождение.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПервоначальныеСведения КАК втПервоначальныеСведения
	|	ПО Строки.ОсновноеСредство = втПервоначальныеСведения.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСчетаОтражения КАК СчетаОтражения
	|	ПО Строки.ОсновноеСредство = СчетаОтражения.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСуммыЧастичнойЛиквидации КАК втСуммыЧастичнойЛиквидации
	|	ПО Строки.ОсновноеСредство = втСуммыЧастичнойЛиквидации.ОбъектУчета
	|	
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ЧастичнаяЛиквидация
	|	И СчетаОтражения.ПрименениеЦелевогоФинансирования
	|";
	
КонецФункции

Функция ВыбытиеАрендованногоОС()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Выбытие с забалансового учета арендуемых ОС (Дт ---:: Кт СчетЗабалансовогоУчета)
	|ВЫБРАТЬ
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	ОстаткиПоСчетуУчета.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	ОстаткиПоСчетуУчета.СуммаОстатокДт КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СчетДт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	ОстаткиПоСчетуУчета.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	СчетаОтражения.СчетУчета КАК СчетКт,
	|	ОстаткиПоСчетуУчета.Субконто1 КАК СубконтоКт1,
	|	ОстаткиПоСчетуУчета.Субконто2 КАК СубконтоКт2,
	|	ОстаткиПоСчетуУчета.Субконто3 КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Выбытие с забалансового учета арендуемых ОС"" КАК Содержание
	|ИЗ
	|	Документ.СписаниеОС КАК Операция
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеОС.ОС КАК Строки
	|		ПО Строки.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСБУ.СрезПоследних(
	|			&Дата,
	|			Регистратор <> &Ссылка
	|			И (ОсновноеСредство) В
	|				(ВЫБРАТЬ
	|					Т.ОсновноеСредство
	|				ИЗ
	|					Документ.СписаниеОС.ОС КАК Т
	|				ГДЕ Т.Ссылка = &Ссылка)) КАК СостоянияОС
	|		ПО Строки.ОсновноеСредство = СостоянияОС.ОсновноеСредство
	|			И СостоянияОС.СостояниеБУ = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКЗабалансовомуУчету)
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОС.СрезПоследних(
	|			&Дата,
	|			(ОсновноеСредство) В
	|				(ВЫБРАТЬ
	|					Т.ОсновноеСредство
	|				ИЗ
	|					Документ.СписаниеОС.ОС КАК Т
	|				ГДЕ Т.Ссылка = &Ссылка)) КАК СчетаОтражения
	|		ПО Строки.ОсновноеСредство = СчетаОтражения.ОсновноеСредство
	|			И СостоянияОС.Организация = СчетаОтражения.Организация
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&Дата,
	|			Счет В 
	|				(ВЫБРАТЬ СчетаОтражения.СчетУчета
	|				ИЗ РегистрСведений.ПорядокУчетаОС.СрезПоследних(
	|					&Дата,
	|					(ОсновноеСредство) В
	|						(ВЫБРАТЬ
	|							Т.ОсновноеСредство
	|						ИЗ
	|							Документ.СписаниеОС.ОС КАК Т
	|						ГДЕ Т.Ссылка = &Ссылка)) КАК СчетаОтражения),,
	|			(Субконто1, Субконто2) В
	|				(ВЫБРАТЬ
	|					ТабличнаяЧастьОС.Ссылка.Организация, ТабличнаяЧастьОС.ОсновноеСредство
	|				ИЗ
	|					Документ.СписаниеОС.ОС КАК ТабличнаяЧастьОС
	|				ГДЕ
	|					ТабличнаяЧастьОС.Ссылка = &Ссылка)
	|			) КАК ОстаткиПоСчетуУчета
	|		ПО Строки.ОсновноеСредство = ОстаткиПоСчетуУчета.Субконто2
	|			И СчетаОтражения.СчетУчета = ОстаткиПоСчетуУчета.Счет
	|			И СостоянияОС.Организация = ОстаткиПоСчетуУчета.Организация
	|
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И НЕ Операция.ЧастичнаяЛиквидация
	|	И ОстаткиПоСчетуУчета.СуммаОстатокДт <> 0
	|";
	
КонецФункции

Функция СписаниеРезерваДооценки()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Списание резерва доценки (Дт 83.01 :: Кт 84.01)
	|ВЫБРАТЬ
	|	
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	втОстаткиРезерваДооценки.СуммаБУ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	втПервоначальныеСведения.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	втОстаткиРезерваДооценки.Счет КАК СчетДт,
	|	втОстаткиРезерваДооценки.ОбъектУчета КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	втОстаткиРезерваДооценки.СуммаНУ КАК СуммаНУДт,
	|	втОстаткиРезерваДооценки.СуммаПР КАК СуммаПРДт,
	|	втОстаткиРезерваДооценки.СуммаВР КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	втПервоначальныеСведения.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрибыльПодлежащаяРаспределению) КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	втОстаткиРезерваДооценки.СуммаНУ КАК СуммаНУКт,
	|	втОстаткиРезерваДооценки.СуммаПР КАК СуммаПРКт,
	|	втОстаткиРезерваДооценки.СуммаВР КАК СуммаВРКт,
	|	
	|	""Списание резерва дооценки"" КАК Содержание
	|ИЗ
	|	Документ.СписаниеОС КАК Операция
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеОС.ОС КАК Строки
	|		ПО Строки.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиРезерваДооценки КАК втОстаткиРезерваДооценки
	|		ПО Строки.ОсновноеСредство = втОстаткиРезерваДооценки.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыЭксплуатации КАК ОбъектыЭксплуатации
	|		ПО Строки.ОсновноеСредство = ОбъектыЭксплуатации.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПервоначальныеСведения КАК втПервоначальныеСведения
	|	ПО Строки.ОсновноеСредство = втПервоначальныеСведения.ОбъектУчета
	|	
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И НЕ Операция.ЧастичнаяЛиквидация
	|	И НЕ втОстаткиРезерваДооценки.Счет ЕСТЬ NULL
	|	И НЕ(втОстаткиРезерваДооценки.СуммаБУ=0 И втОстаткиРезерваДооценки.СуммаНУ=0 И втОстаткиРезерваДооценки.СуммаПР=0 И втОстаткиРезерваДооценки.СуммаВР=0)
	|";
	
КонецФункции

Функция ЧастичноеСписаниеРезерваДооценки()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Списание резерва доценки (Дт 83.01 :: Кт 84.01)
	|ВЫБРАТЬ
	|	
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	втОстаткиРезерваДооценки.СуммаБУ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	втПервоначальныеСведения.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	втОстаткиРезерваДооценки.Счет КАК СчетДт,
	|	втОстаткиРезерваДооценки.ОбъектУчета КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	втОстаткиРезерваДооценки.СуммаНУ КАК СуммаНУДт,
	|	втОстаткиРезерваДооценки.СуммаПР КАК СуммаПРДт,
	|	втОстаткиРезерваДооценки.СуммаВР КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	втПервоначальныеСведения.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрибыльПодлежащаяРаспределению) КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	втОстаткиРезерваДооценки.СуммаНУ КАК СуммаНУКт,
	|	втОстаткиРезерваДооценки.СуммаПР КАК СуммаПРКт,
	|	втОстаткиРезерваДооценки.СуммаВР КАК СуммаВРКт,
	|	
	|	""Списание резерва дооценки"" КАК Содержание
	|ИЗ
	|	Документ.СписаниеОС КАК Операция
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеОС.ОС КАК Строки
	|		ПО Строки.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втРезервДооценкиЧастичнойЛиквидации КАК втОстаткиРезерваДооценки
	|		ПО Строки.ОсновноеСредство = втОстаткиРезерваДооценки.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыЭксплуатации КАК ОбъектыЭксплуатации
	|		ПО Строки.ОсновноеСредство = ОбъектыЭксплуатации.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПервоначальныеСведения КАК втПервоначальныеСведения
	|	ПО Строки.ОсновноеСредство = втПервоначальныеСведения.ОбъектУчета
	|	
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ЧастичнаяЛиквидация
	|	И НЕ втОстаткиРезерваДооценки.Счет ЕСТЬ NULL
	|	И НЕ(втОстаткиРезерваДооценки.СуммаБУ=0 И втОстаткиРезерваДооценки.СуммаНУ=0 И втОстаткиРезерваДооценки.СуммаПР=0 И втОстаткиРезерваДооценки.СуммаВР=0)
	|";
	
КонецФункции

Функция СписаниеКорректировкиСтоимостиАрендованногоИмущества()

	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Списание резерва доценки (Дт 01.09 :: Кт 01.К)
	|ВЫБРАТЬ
	|	
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	0 КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	втПервоначальныеСведения.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	Операция.СчетВыбытияОС КАК СчетДт,
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	втОстаткиПоСчетам.НеУчитываемаяСтоимостьНУ КАК СуммаНУДт,
	|	втОстаткиПоСчетам.КорректировкаСтоимостиПР КАК СуммаПРДт,
	|	втОстаткиПоСчетам.КорректировкаСтоимостиВР КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	втПервоначальныеСведения.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.КорректировкаСтоимостиАрендованногоИмущества) КАК СчетКт,
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	втОстаткиПоСчетам.НеУчитываемаяСтоимостьНУ КАК СуммаНУКт,
	|	втОстаткиПоСчетам.КорректировкаСтоимостиПР КАК СуммаПРКт,
	|	втОстаткиПоСчетам.КорректировкаСтоимостиВР КАК СуммаВРКт,
	|	
	|	""Списание корректировки стоимости арендованного имущества"" КАК Содержание
	|ИЗ
	|	Документ.СписаниеОС КАК Операция
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеОС.ОС КАК Строки
	|		ПО Строки.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиПоСчетам КАК втОстаткиПоСчетам
	|		ПО Строки.ОсновноеСредство = втОстаткиПоСчетам.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПервоначальныеСведения КАК втПервоначальныеСведения
	|	ПО Строки.ОсновноеСредство = втПервоначальныеСведения.ОбъектУчета
	|	
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И НЕ Операция.ЧастичнаяЛиквидация
	|	И (втОстаткиПоСчетам.НеУчитываемаяСтоимостьНУ <> 0
	|		ИЛИ втОстаткиПоСчетам.КорректировкаСтоимостиПР <> 0
	|		ИЛИ втОстаткиПоСчетам.КорректировкаСтоимостиВР <> 0)
	|";

КонецФункции

Функция ЧастичноеСписаниеКорректировкиСтоимостиАрендованногоИмущества()

	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Списание резерва доценки (Дт 01.09 :: Кт 01.К)
	|ВЫБРАТЬ
	|	
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	0 КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	втПервоначальныеСведения.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	Операция.СчетВыбытияОС КАК СчетДт,
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	втСуммыЧастичнойЛиквидации.НеУчитываемаяСтоимостьНУ КАК СуммаНУДт,
	|	втСуммыЧастичнойЛиквидации.КорректировкаСтоимостиПР КАК СуммаПРДт,
	|	втСуммыЧастичнойЛиквидации.КорректировкаСтоимостиВР КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	втПервоначальныеСведения.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.КорректировкаСтоимостиАрендованногоИмущества) КАК СчетКт,
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	втСуммыЧастичнойЛиквидации.НеУчитываемаяСтоимостьНУ КАК СуммаНУКт,
	|	втСуммыЧастичнойЛиквидации.КорректировкаСтоимостиПР КАК СуммаПРКт,
	|	втСуммыЧастичнойЛиквидации.КорректировкаСтоимостиВР КАК СуммаВРКт,
	|	
	|	""Списание корректировки стоимости арендованного имущества"" КАК Содержание
	|ИЗ
	|	Документ.СписаниеОС КАК Операция
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеОС.ОС КАК Строки
	|		ПО Строки.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСуммыЧастичнойЛиквидации КАК втСуммыЧастичнойЛиквидации
	|		ПО Строки.ОсновноеСредство = втСуммыЧастичнойЛиквидации.ОбъектУчета
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПервоначальныеСведения КАК втПервоначальныеСведения
	|	ПО Строки.ОсновноеСредство = втПервоначальныеСведения.ОбъектУчета
	|	
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ЧастичнаяЛиквидация
	|	И (втСуммыЧастичнойЛиквидации.НеУчитываемаяСтоимостьНУ <> 0
	|		ИЛИ втСуммыЧастичнойЛиквидации.КорректировкаСтоимостиПР <> 0
	|		ИЛИ втСуммыЧастичнойЛиквидации.КорректировкаСтоимостиВР <> 0)
	|";

КонецФункции

#КонецОбласти

#Область Печать

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	// ОС-4 (Акт о списании ОС)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ОС4";
	КомандаПечати.Представление = НСтр("ru = 'ОС-4 (Акт о списании ОС)';
										|en = 'FA-4 (FA retirement certificate)'");

КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ОС4") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм, "ОС4",
			НСтр("ru = 'ОС-4 (Акт о списании ОС)';
				|en = 'FA-4 (FA retirement certificate)'"),
			ПечатьОС4(МассивОбъектов, ОбъектыПечати, ПараметрыПечати));
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьВременныеТаблицыРегистров(МВТ, МассивОбъектов)
	
	УстановитьПривилегированныйРежим(Истина);
	
	// 1. Определим "границы" регистров сведений - список организаций, ОС, период, ...
	//    чтобы ограничить выборку из регистров сведений.
	Запрос = Новый Запрос(
		"
		|// 1. Период дат
		|ВЫБРАТЬ
		|	МИНИМУМ(Док.Дата)  КАК ДатаОТ,
		|	МАКСИМУМ(Док.Дата) КАК ДатаДО
		|ИЗ
		|	Документ.СписаниеОС КАК Док
		|ГДЕ
		|	Док.Ссылка В (&МассивОбъектов)
		|;
		|
		|// 2. Список организаций
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Док.Организация КАК Организация
		|ИЗ
		|	Документ.СписаниеОС КАК Док
		|ГДЕ
		|	Док.Ссылка В (&МассивОбъектов)
		|;
		|
		|// 3. Список ОС
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Док.ОсновноеСредство КАК ОсновноеСредство
		|ИЗ
		|	Документ.СписаниеОС.ОС КАК Док
		|ГДЕ
		|	Док.Ссылка В (&МассивОбъектов)
		|
		|");
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Результат = Запрос.ВыполнитьПакет();
	// 1.1. Интервал дат
	Выборка = Результат[0].Выбрать(ОбходРезультатаЗапроса.Прямой);
	Выборка.Следующий();
	ДатаОТ = Выборка.ДатаОТ;
	ДатаДО = Выборка.ДатаДО;
	
	// 1.2. Список организаций
	МассивОрганизаций = Результат[1].Выгрузить(ОбходРезультатаЗапроса.Прямой).ВыгрузитьКолонку("Организация");
	
	// 1.3. Список ОС
	МассивОС = Результат[2].Выгрузить(ОбходРезультатаЗапроса.Прямой).ВыгрузитьКолонку("ОсновноеСредство");
	
	// 2. Создание временных таблиц
	ИДКонфигурации = РегламентированнаяОтчетностьПереопределяемый.ИДКонфигурации();
	
	СписокЗапросов = Новый Массив;
	
	#Область СписокОС
	ТекстЗапроса =	
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаОС.Ссылка КАК Ссылка,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаОС.Ссылка.СчетВыбытияОС КАК СчетВыбытияОС
	|ПОМЕСТИТЬ
	|	ВТ_ВсеОС
	|ИЗ
	|	Документ.СписаниеОС.ОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка В (&МассивОбъектов)
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	ОсновноеСредство";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область ВТ_ПервоначальныеСведенияОС
	ТекстЗапроса =	
	"ВЫБРАТЬ
	|	Рег1.Период           КАК Период,
	|	МИНИМУМ(Рег2.Период)  КАК ПервыйПериод,
	|	Рег1.ОсновноеСредство КАК ОсновноеСредство,
	|	Рег1.Организация КАК Организация
	|ПОМЕСТИТЬ ВТ_ПервоначальныеСведенияОС_Рег2
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияОС КАК Рег1
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОС КАК Рег2
	|	ПО Рег1.Организация = Рег2.Организация
	|		И Рег1.ОсновноеСредство = Рег2.ОсновноеСредство
	|		И Рег1.Период < Рег2.Период
	|ГДЕ
	|	Рег1.Период <= &ДатаДО
	|	И Рег1.ОсновноеСредство В (&МассивОС)
	|	И Рег2.Период <= &ДатаДО
	|	И Рег2.ОсновноеСредство В (&МассивОС)
	|
	|СГРУППИРОВАТЬ ПО
	|	Рег1.Период,
	|	Рег1.Организация,
	|	Рег1.ОсновноеСредство
	|ИНДЕКСИРОВАТЬ ПО
	|	Рег1.Период,
	|	Рег1.ОсновноеСредство
	|;
	|
	|// 2.2. Запрос к первоначальным сведениям ОС
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА Рег2.ПервыйПериод ЕСТЬ NULL ТОГДА
	|			ДАТАВРЕМЯ(3999,12,31)
	|		ИНАЧЕ
	|			ДОБАВИТЬКДАТЕ(Рег2.ПервыйПериод, СЕКУНДА, -1)
	|	КОНЕЦ                             КАК ДатаОкончания,
	|	Рег1.Период                       КАК ДатаНачала,
	|	Рег1.ОсновноеСредство             КАК ОсновноеСредство,
	|	Рег1.Организация                  КАК Организация,
	|	Рег1.СпособПоступления            КАК СпособПоступления,
	|	Рег1.ПервоначальнаяСтоимостьБУ    КАК ПервоначальнаяСтоимость,
	|	Рег1.МетодНачисленияАмортизацииБУ КАК СпособНачисленияАмортизации,
	|	Рег1.ПорядокУчетаБУ               КАК ПорядокУчетаБУ
	|ПОМЕСТИТЬ ВТ_ПервоначальныеСведенияОС
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияОС КАК Рег1
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПервоначальныеСведенияОС_Рег2 КАК Рег2
	|	ПО Рег1.Организация = Рег2.Организация
	|		И Рег1.ОсновноеСредство = Рег2.ОсновноеСредство
	|		И Рег1.Период = Рег2.Период
	|ГДЕ
	|	Рег1.Период <= &ДатаДО
	|	И Рег1.ОсновноеСредство В (&МассивОС)
	|ИНДЕКСИРОВАТЬ ПО
	|	ДатаНачала,
	|	ДатаОкончания,
	|	ОсновноеСредство";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область ВТ_ПорядокУчетаОС
	ТекстЗапроса =	
	"ВЫБРАТЬ
	|	Рег1.Период           КАК Период,
	|	МИНИМУМ(Рег2.Период)  КАК ПервыйПериод,
	|	Рег1.Организация      КАК Организация,
	|	Рег1.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ
	|	ВТ_ПорядокУчетаОС_Рег2
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОС КАК Рег1
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрСведений.ПорядокУчетаОС КАК Рег2
	|	ПО
	|		Рег1.ОсновноеСредство = Рег2.ОсновноеСредство
	|		И Рег1.Организация    = Рег2.Организация
	|		И Рег1.Период         < Рег2.Период
	|ГДЕ
	|	Рег1.Период <= &ДатаДО
	|	И Рег1.Организация В (&МассивОрганизаций)
	|	И Рег1.ОсновноеСредство В (&МассивОС)
	|	И Рег2.Период <= &ДатаДО
	|	И Рег2.Организация В (&МассивОрганизаций)
	|	И Рег2.ОсновноеСредство В (&МассивОС)
	|СГРУППИРОВАТЬ ПО
	|	Рег1.Период,
	|	Рег1.Организация,
	|	Рег1.ОсновноеСредство
	|ИНДЕКСИРОВАТЬ ПО
	|	Рег1.Период,
	|	Рег1.Организация,
	|	Рег1.ОсновноеСредство
	|;
	|
	|// 3.2. Запрос к счетам бухгалтерского учета ОС
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА Рег2.ПервыйПериод ЕСТЬ NULL ТОГДА
	|			ДАТАВРЕМЯ(3999,12,31)
	|		ИНАЧЕ
	|			ДОБАВИТЬКДАТЕ(Рег2.ПервыйПериод, СЕКУНДА, -1)
	|	КОНЕЦ                          КАК ДатаОкончания,
	|	Рег1.Период                    КАК ДатаНачала,
	|	Рег1.Организация               КАК Организация,
	|	Рег1.ОсновноеСредство          КАК ОсновноеСредство,
	|	Рег1.СчетУчета                 КАК СчетУчета,
	|	ВЫБОР
	|		КОГДА &ЭтоБАУилиБАУКОРП ТОГДА
	|			Рег1.СчетНачисленияАмортизации
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|	КОНЕЦ                          КАК СчетНачисленияИзноса,
	|	ВЫБОР
	|		КОГДА &ЭтоБАУилиБАУКОРП ТОГДА
	|			ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		ИНАЧЕ
	|			Рег1.СчетНачисленияАмортизации
	|	КОНЕЦ                          КАК СчетНачисленияАмортизации,
	|	Рег1.СчетНачисленияАмортизации КАК СчетНачисленияИзносаАмортизации
	|ПОМЕСТИТЬ ВТ_ПорядокУчетаОС
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОС КАК Рег1
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|	ВТ_ПорядокУчетаОС_Рег2 КАК Рег2
	|	ПО
	|		Рег1.ОсновноеСредство = Рег2.ОсновноеСредство
	|		И Рег1.Организация    = Рег2.Организация
	|		И Рег1.Период         = Рег2.Период
	|ГДЕ
	|	Рег1.Период <= &ДатаДО
	|	И Рег1.Организация В (&МассивОрганизаций)
	|	И Рег1.ОсновноеСредство В (&МассивОС)
	|ИНДЕКСИРОВАТЬ ПО
	|	ДатаНачала,
	|	ДатаОкончания,
	|	Организация,
	|	ОсновноеСредство";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область ВТ_МестонахождениеОС
	ТекстЗапроса =	
	"ВЫБРАТЬ
	|	Рег1.Период           КАК Период,
	|	МИНИМУМ(Рег2.Период)  КАК ПервыйПериод,
	|	Рег1.Организация      КАК Организация,
	|	Рег1.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ
	|	ВТ_МестонахождениеОС_Рег2
	|ИЗ
	|	РегистрСведений.МестонахождениеОС КАК Рег1
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрСведений.МестонахождениеОС КАК Рег2
	|	ПО
	|		Рег1.ОсновноеСредство = Рег2.ОсновноеСредство
	|		И Рег1.Организация    = Рег2.Организация
	|		И Рег1.Период         < Рег2.Период
	|ГДЕ
	|	Рег1.Период <= &ДатаДО
	|	И Рег1.Организация В (&МассивОрганизаций)
	|	И Рег1.ОсновноеСредство В (&МассивОС)
	|	И Рег2.Период <= &ДатаДО
	|	И Рег2.Организация В (&МассивОрганизаций)
	|	И Рег2.ОсновноеСредство В (&МассивОС)
	|СГРУППИРОВАТЬ ПО
	|	Рег1.Период,
	|	Рег1.Организация,
	|	Рег1.ОсновноеСредство
	|ИНДЕКСИРОВАТЬ ПО
	|	Рег1.Период,
	|	Рег1.Организация,
	|	Рег1.ОсновноеСредство
	|;
	|
	|// 4.2. Запрос к Местонахождение ОС
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА Рег2.ПервыйПериод ЕСТЬ NULL ТОГДА
	|			ДАТАВРЕМЯ(3999,12,31)
	|		ИНАЧЕ
	|			ДОБАВИТЬКДАТЕ(Рег2.ПервыйПериод, СЕКУНДА, -1)
	|	КОНЕЦ                          КАК ДатаОкончания,
	|	Рег1.Период                    КАК ДатаНачала,
	|	Рег1.Организация               КАК Организация,
	|	Рег1.ОсновноеСредство          КАК ОсновноеСредство,
	|	Рег1.МОЛ                       КАК МОЛ,
	|	Рег1.Местонахождение           КАК Местонахождение
	|ПОМЕСТИТЬ ВТ_МестонахождениеОС
	|ИЗ
	|	РегистрСведений.МестонахождениеОС КАК Рег1
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|	ВТ_МестонахождениеОС_Рег2 КАК Рег2
	|	ПО
	|		Рег1.ОсновноеСредство = Рег2.ОсновноеСредство
	|		И Рег1.Организация    = Рег2.Организация
	|		И Рег1.Период         = Рег2.Период
	|ГДЕ
	|	Рег1.Период <= &ДатаДО
	|	И Рег1.Организация В (&МассивОрганизаций)
	|	И Рег1.ОсновноеСредство В (&МассивОС)
	|ИНДЕКСИРОВАТЬ ПО
	|	ДатаНачала,
	|	ДатаОкончания,
	|	Организация,
	|	ОсновноеСредство";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область ВТ_ПорядокУчетаОСБУ
	ТекстЗапроса =	
	"ВЫБРАТЬ
	|	Рег1.Период           КАК Период,
	|	МИНИМУМ(Рег2.Период)  КАК ПервыйПериод,
	|	Рег1.Организация      КАК Организация,
	|	Рег1.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ
	|	ВТ_ПорядокУчетаОСБУ_Рег2
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОСБУ КАК Рег1
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСБУ КАК Рег2
	|	ПО Рег1.ОсновноеСредство = Рег2.ОсновноеСредство
	|		И Рег1.Организация = Рег2.Организация
	|		И Рег1.Период < Рег2.Период
	|ГДЕ
	|	Рег1.Период <= &ДатаДО
	|	И Рег1.Организация В (&МассивОрганизаций)
	|	И Рег1.ОсновноеСредство В (&МассивОС)
	|	И Рег2.Период <= &ДатаДО
	|	И Рег2.Организация В (&МассивОрганизаций)
	|	И Рег2.ОсновноеСредство В (&МассивОС)
	|СГРУППИРОВАТЬ ПО
	|	Рег1.Период,
	|	Рег1.Организация,
	|	Рег1.ОсновноеСредство
	|ИНДЕКСИРОВАТЬ ПО
	|	Рег1.Период,
	|	Рег1.Организация,
	|	Рег1.ОсновноеСредство
	|;
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА Рег2.ПервыйПериод ЕСТЬ NULL ТОГДА
	|			ДАТАВРЕМЯ(3999,12,31)
	|		ИНАЧЕ
	|			ДОБАВИТЬКДАТЕ(Рег2.ПервыйПериод, СЕКУНДА, -1)
	|	КОНЕЦ                          КАК ДатаОкончания,
	|	Рег1.Период                    КАК ДатаНачала,
	|	Рег1.Организация               КАК Организация,
	|	Рег1.ОсновноеСредство          КАК ОсновноеСредство,
	|	Рег1.АмортизационнаяГруппа     КАК АмортизационнаяГруппа
	|ПОМЕСТИТЬ ВТ_ПорядокУчетаОСБУ
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОСБУ КАК Рег1
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПорядокУчетаОСБУ_Рег2 КАК Рег2
	|	ПО Рег1.ОсновноеСредство = Рег2.ОсновноеСредство
	|		И Рег1.Организация    = Рег2.Организация
	|		И Рег1.Период         = Рег2.Период
	|ГДЕ
	|	Рег1.Период <= &ДатаДО
	|	И Рег1.Организация В (&МассивОрганизаций)
	|	И Рег1.ОсновноеСредство В (&МассивОС)
	|ИНДЕКСИРОВАТЬ ПО
	|	ДатаНачала,
	|	ДатаОкончания,
	|	Организация,
	|	ОсновноеСредство";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область ВТ_СтоимостьОС
	ТекстЗапроса =	
	"ВЫБРАТЬ
	|	ХозрасчетныйОборотыДтКт.Регистратор КАК Регистратор,
	|	ХозрасчетныйОборотыДтКт.СчетКт      КАК СчетКт,
	|	ХозрасчетныйОборотыДтКт.СубконтоКт1 КАК СубконтоКт1,
	|	ХозрасчетныйОборотыДтКт.СуммаОборот КАК СуммаОборот
	|ПОМЕСТИТЬ
	|	ВТ_СтоимостьОС
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(
	|		ДОБАВИТЬКДАТЕ(&ДатаОТ, СЕКУНДА, -1),
	|		ДОБАВИТЬКДАТЕ(&ДатаДО, СЕКУНДА, 1),
	|		Регистратор,
	|		СчетДт В (ВЫБРАТЬ Т.СчетВыбытияОС ИЗ ВТ_ВсеОС КАК Т),
	|		,
	|		,
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства),
	|		Организация В (&МассивОрганизаций)
	|	) КАК ХозрасчетныйОборотыДтКт
	|ГДЕ
	|	ХозрасчетныйОборотыДтКт.Регистратор В (&МассивОбъектов)
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	СчетКт,
	|	СубконтоКт1";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ВТ_АмортизацияОС
	ТекстЗапроса =	
	"ВЫБРАТЬ
	|	ХозрасчетныйОборотыДтКт.Регистратор КАК Регистратор,
	|	ХозрасчетныйОборотыДтКт.СчетДт      КАК СчетДт,
	|	ХозрасчетныйОборотыДтКт.СубконтоДт1 КАК СубконтоДт1,
	|	ХозрасчетныйОборотыДтКт.СуммаОборот КАК СуммаОборот
	|ПОМЕСТИТЬ
	|	ВТ_АмортизацияОС
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(
	|		ДОБАВИТЬКДАТЕ(&ДатаОТ, СЕКУНДА, -1),
	|		ДОБАВИТЬКДАТЕ(&ДатаДО, СЕКУНДА, 1),
	|		Регистратор,
	|		,
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства),
	|		СчетКт В (ВЫБРАТЬ Т.СчетВыбытияОС ИЗ ВТ_ВсеОС КАК Т),
	|		,
	|		Организация В (&МассивОрганизаций)
	|	) КАК ХозрасчетныйОборотыДтКт
	|ГДЕ
	|	ХозрасчетныйОборотыДтКт.Регистратор В (&МассивОбъектов)
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	СчетДт,
	|	СубконтоДт1";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ВТ_ИзносОС
	ТекстЗапроса =	
	"ВЫБРАТЬ
	|	ХозрасчетныйОборотыДтКт.Регистратор КАК Регистратор,
	|	ХозрасчетныйОборотыДтКт.СчетКт      КАК СчетКт,
	|	ХозрасчетныйОборотыДтКт.СубконтоКт1 КАК СубконтоКт1,
	|	ХозрасчетныйОборотыДтКт.Сумма       КАК СуммаОборот
	|ПОМЕСТИТЬ
	|	ВТ_ИзносОС
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	|		ДОБАВИТЬКДАТЕ(&ДатаОТ, СЕКУНДА, -1),
	|		ДОБАВИТЬКДАТЕ(&ДатаДО, СЕКУНДА, 1),
	|		Организация В (&МассивОрганизаций)
	|			И СубконтоКт1 В (&МассивОС)
	|			И СчетКт В (ВЫБРАТЬ ВТ_ПорядокУчетаОС.СчетНачисленияИзносаАмортизации ИЗ ВТ_ПорядокУчетаОС)
	|	) КАК ХозрасчетныйОборотыДтКт
	|ГДЕ
	|	ХозрасчетныйОборотыДтКт.Регистратор В (&МассивОбъектов)
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	СчетКт,
	|	СубконтоКт1";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(СписокЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МВТ;
	Запрос.УстановитьПараметр("ДатаОТ", ДатаОТ);
	Запрос.УстановитьПараметр("ДатаДО", ДатаДО);
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);
	Запрос.УстановитьПараметр("МассивОС", МассивОС);
	Запрос.УстановитьПараметр("ЭтоБАУилиБАУКОРП", (ИДКонфигурации = "БАУ" Или ИДКонфигурации = "БАУКОРП"));
	
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ДанныеПечатнойФормыОС4(МассивОбъектов)
	
	ИДКонфигурации = РегламентированнаяОтчетностьПереопределяемый.ИДКонфигурации();
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	СоздатьВременныеТаблицыРегистров(МенеджерВременныхТаблиц, МассивОбъектов);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр("ЭтоБАУилиБАУКОРП", (ИДКонфигурации = "БАУ" ИЛИ ИДКонфигурации = "БАУКОРП"));
	Запрос.УстановитьПараметр("ВалютаУправленческогоУчета", Константы.ВалютаУправленческогоУчета.Получить());
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаОС.Ссылка КАК Ссылка,
	|	ТаблицаОС.Ссылка.Номер КАК Номер,
	|	ТаблицаОС.Ссылка.Дата КАК Дата,
	|	ТаблицаОС.Ссылка.СобытиеОС КАК Состояние,
	|	ТаблицаОС.Ссылка.ПричинаСписания КАК ПричинаСписания,
	|	ТаблицаОС.Ссылка.Организация КАК Организация,
	|	ТаблицаОС.Ссылка.Организация.КодПоОКПО КАК КодОКПО,
	|	ТаблицаОС.Ссылка.Подразделение КАК ПодразделениеОрганизации,
	|	ТаблицаОС.Ссылка.МоментВремени КАК МоментВремени,
	|	ТаблицаОС.НомерСтроки КАК НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство КАК ОС,
	|	ТаблицаОС.ОсновноеСредство.ЗаводскойНомер КАК ЗаводскойНомер,
	|	ВЫБОР
	|		КОГДА ТаблицаОС.ОсновноеСредство.ГруппаОС = ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ТранспортныеСредства)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Автотранспорт,
	|	ТаблицаОС.ОсновноеСредство.ДатаВыпуска КАК ГодВыпуска,
	|	ВЫБОР
	|		КОГДА ТаблицаОС.ОсновноеСредство.НаименованиеПолное = """"
	|			ТОГДА ТаблицаОС.ОсновноеСредство.Наименование
	|		ИНАЧЕ ТаблицаОС.ОсновноеСредство.НаименованиеПолное
	|	КОНЕЦ КАК НаимОС,
	|	ТаблицаОС.ОсновноеСредство.ГруппаОС КАК ГруппаОС,
	|	ВТ_ПорядокУчетаОСБУ.АмортизационнаяГруппа.Порядок КАК АмортизационнаяГруппа,
	|	ВТ_ПервоначальныеСведенияОС.ПервоначальнаяСтоимость КАК НачСтоимость,
	|	ТаблицаОС.ОсновноеСредство.ИнвентарныйНомер КАК ИнвНомер,
	|	ВТ_ПервоначальныеСведенияОС.ПорядокУчетаБУ КАК ПорядокУчетаБУ,
	|	ВТ_МестонахождениеОС.Местонахождение КАК Подразделение,
	|	ВТ_МестонахождениеОС.МОЛ КАК МОЛ,
	|	ЕСТЬNULL(ВТ_СтоимостьОС.СуммаОборот, 0) КАК Стоимость,
	|	ВЫБОР
	|		КОГДА ВТ_ПервоначальныеСведенияОС.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеИзносаПоЕНАОФ)
	|				ИЛИ ВТ_ПервоначальныеСведенияОС.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеИзноса)
	|			ТОГДА ЕСТЬNULL(ВТ_ИзносОС.СуммаОборот, 0)
	|		ИНАЧЕ ЕСТЬNULL(ВТ_АмортизацияОС.СуммаОборот, 0)
	|	КОНЕЦ КАК НачАмортизация,
	|	ОбъектыЭксплуатации.ДрагоценныеМатериалы.(
	|		ДрагоценныйМатериал КАК ДМНаименование,
	|		Количество КАК ДММасса,
	|		ЕдиницаИзмерения КАК ДМЕдиницаИзмерения
	|	) КАК ДрагоценныеМатериалы,
	|	ТаблицаОС.Ссылка.ЧастичнаяЛиквидация КАК ЧастичнаяЛиквидация
	|ИЗ
	|	Документ.СписаниеОС.ОС КАК ТаблицаОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПервоначальныеСведенияОС КАК ВТ_ПервоначальныеСведенияОС
	|		ПО (ТаблицаОС.Ссылка.Дата МЕЖДУ ВТ_ПервоначальныеСведенияОС.ДатаНачала И ВТ_ПервоначальныеСведенияОС.ДатаОкончания)
	|			И ТаблицаОС.Ссылка.Организация = ВТ_ПервоначальныеСведенияОС.Организация
	|			И ТаблицаОС.ОсновноеСредство = ВТ_ПервоначальныеСведенияОС.ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПорядокУчетаОС КАК ВТ_ПорядокУчетаОС
	|		ПО (ТаблицаОС.Ссылка.Дата МЕЖДУ ВТ_ПорядокУчетаОС.ДатаНачала И ВТ_ПорядокУчетаОС.ДатаОкончания)
	|			И ТаблицаОС.ОсновноеСредство = ВТ_ПорядокУчетаОС.ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_МестонахождениеОС КАК ВТ_МестонахождениеОС
	|		ПО (ТаблицаОС.Ссылка.Дата МЕЖДУ ВТ_МестонахождениеОС.ДатаНачала И ВТ_МестонахождениеОС.ДатаОкончания)
	|			И ТаблицаОС.Ссылка.Организация = ВТ_МестонахождениеОС.Организация
	|			И ТаблицаОС.ОсновноеСредство = ВТ_МестонахождениеОС.ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПорядокУчетаОСБУ КАК ВТ_ПорядокУчетаОСБУ
	|		ПО (ТаблицаОС.Ссылка.Дата МЕЖДУ ВТ_ПорядокУчетаОСБУ.ДатаНачала И ВТ_МестонахождениеОС.ДатаОкончания)
	|			И ТаблицаОС.Ссылка.Организация = ВТ_ПорядокУчетаОСБУ.Организация
	|			И ТаблицаОС.ОсновноеСредство = ВТ_ПорядокУчетаОСБУ.ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьОС КАК ВТ_СтоимостьОС
	|		ПО ТаблицаОС.Ссылка = ВТ_СтоимостьОС.Регистратор
	|			И (ВТ_ПорядокУчетаОС.СчетУчета = ВТ_СтоимостьОС.СчетКт)
	|			И ТаблицаОС.ОсновноеСредство = ВТ_СтоимостьОС.СубконтоКт1
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияОС КАК ВТ_АмортизацияОС
	|		ПО ТаблицаОС.Ссылка = ВТ_АмортизацияОС.Регистратор
	|			И (ВТ_ПорядокУчетаОС.СчетНачисленияАмортизации = ВТ_АмортизацияОС.СчетДт)
	|			И ТаблицаОС.ОсновноеСредство = ВТ_АмортизацияОС.СубконтоДт1
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИзносОС КАК ВТ_ИзносОС
	|		ПО ТаблицаОС.Ссылка = ВТ_ИзносОС.Регистратор
	|			И (ВТ_ПорядокУчетаОС.СчетНачисленияИзносаАмортизации = ВТ_ИзносОС.СчетКт)
	|			И ТаблицаОС.ОсновноеСредство = ВТ_ИзносОС.СубконтоКт1
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыЭксплуатации КАК ОбъектыЭксплуатации
	|		ПО ТаблицаОС.ОсновноеСредство = ОбъектыЭксплуатации.Ссылка
	|ГДЕ
	|	ТаблицаОС.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОС.Ссылка.Дата,
	|	ТаблицаОС.Ссылка,
	|	НомерСтроки
	|ИТОГИ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДемонтируемыеУзлы.Ссылка.ДокументОснование КАК Ссылка,
	|	ДемонтируемыеУзлы.Ссылка КАК УзелДокумент,
	|	ДемонтируемыеУзлы.Ссылка.Номер КАК УзелДокументНомер,
	|	ДемонтируемыеУзлы.Ссылка.Дата КАК УзелДокументДата,
	|	ТИПЗНАЧЕНИЯ(ДемонтируемыеУзлы.Ссылка) КАК УзелДокументНаименование,
	|	ДемонтируемыеУзлы.Номенклатура КАК УзелНоменклатура,
	|	ДемонтируемыеУзлы.Номенклатура.НаименованиеПолное КАК УзелНоменклатураНаименованиеПолное,
	|	ДемонтируемыеУзлы.Характеристика КАК УзелХарактеристика,
	|	ДемонтируемыеУзлы.Характеристика.НаименованиеПолное КАК УзелХарактеристикаНаименованиеПолное,
	|	ДемонтируемыеУзлы.Количество КАК УзелКоличество,
	|	ДемонтируемыеУзлы.Номенклатура.ЕдиницаИзмерения КАК УзелЕдиницаИзмерения,
	|	ЕСТЬNULL(СуммыДокументовВВалютахУчета.СуммаБезНДСРегл / ДемонтируемыеУзлы.Количество, ДемонтируемыеУзлы.Цена) КАК УзелЦена,
	|	ЕСТЬNULL(СуммыДокументовВВалютахУчета.СуммаБезНДСРегл, ДемонтируемыеУзлы.Сумма) КАК УзелСумма
	|ИЗ
	|	Документ.ПрочееОприходованиеТоваров.Товары КАК ДемонтируемыеУзлы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
	|		ПО ДемонтируемыеУзлы.ИдентификаторСтроки = СуммыДокументовВВалютахУчета.ИдентификаторСтроки
	|			И (ДемонтируемыеУзлы.Ссылка.Организация.ВалютаРегламентированногоУчета <> &ВалютаУправленческогоУчета)
	|ГДЕ
	|	ДемонтируемыеУзлы.Ссылка.Проведен
	|	И НЕ ДемонтируемыеУзлы.Ссылка.ПометкаУдаления
	|	И ДемонтируемыеУзлы.Ссылка.ДокументОснование В(&МассивОбъектов)
	|	И ДемонтируемыеУзлы.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОприходованиеЗаСчетДоходов)
	|	И (ДемонтируемыеУзлы.Ссылка.Организация.ВалютаРегламентированногоУчета = &ВалютаУправленческогоУчета
	|			ИЛИ НЕ СуммыДокументовВВалютахУчета.Регистратор ЕСТЬ NULL )
	|ИТОГИ ПО
	|	Ссылка";
	
	Пакет = Запрос.ВыполнитьПакет();
	
	Возврат Новый Структура(
		"ПоДокументам, ПоПрочимОприходованиям",
		Пакет[0],
		Пакет[1]);
	
КонецФункции

Функция ПечатьОС4(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	Перем ПодразделениеОтветственныхЛиц;
	
	Если МассивОбъектов.Количество() = 0 Тогда
		Возврат Новый ТабличныйДокумент;
	КонецЕсли;
	
	ИспользоватьУчетДМ = ПолучитьФункциональнуюОпцию("ИспользоватьУчетДрагоценныхМатериалов");
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СписаниеОС_ОС4";
	
	МакетОС4  = УправлениеПечатью.МакетПечатнойФормы("Документ.СписаниеОС.ПФ_MXL_ОС4");
	МакетОС4А = УправлениеПечатью.МакетПечатнойФормы("Документ.СписаниеОС.ПФ_MXL_ОС4а");
	МакетОС4Б = УправлениеПечатью.МакетПечатнойФормы("Документ.СписаниеОС.ПФ_MXL_ОС4б");
	
	ДанныеПечатнойФормы = ДанныеПечатнойФормыОС4(МассивОбъектов);
	
	Результат = ДанныеПечатнойФормы.ПоДокументам;
	ВыборкаПрочихОприходований = ДанныеПечатнойФормы.ПоПрочимОприходованиям.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Если Результат.Пустой() Тогда
		Возврат Новый ТабличныйДокумент;
	КонецЕсли;
	
	ВыборкаДокументов = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ШаблонСообщенияОбОшибке = НСтр("ru = 'Для документа %1 не требуется акт о списании (ОС-4)';
									|en = 'Retirement certificate (FA-4) is not required for the %1 document'");
	ПервыйДокумент = Истина;
	
	Пока ВыборкаДокументов.Следующий() Цикл
		
		Если ВыборкаДокументов.ЧастичнаяЛиквидация Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтрШаблон(ШаблонСообщенияОбОшибке, ВыборкаДокументов.Ссылка),
				ВыборкаДокументов.Ссылка);
			
			Продолжить;
		КонецЕсли;
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		ЭтоАвто = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		СведенияОбОрганизации= БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(ВыборкаДокументов.Организация, ВыборкаДокументов.Дата);
		ПредставлениеОрганизации = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбОрганизации, "НаименованиеДляПечатныхФорм");
		
		ПодразделениеОтветственныхЛиц = ВыборкаДокументов.ПодразделениеОрганизации;
		
		ОтветственныеЛица = ОтветственныеЛицаБП.ОтветственныеЛица(ВыборкаДокументов.Организация, ВыборкаДокументов.Дата, ПодразделениеОтветственныхЛиц);
		
		ВыборкаСтрок = ВыборкаДокументов.Выбрать(ОбходРезультатаЗапроса.Прямой);
		
		Если ВыборкаСтрок.Количество() = 1 Тогда
			ВыборкаСтрок.Следующий();
			
			ЭтоАвто = (ВыборкаСтрок.Автотранспорт);
			Макет = ?(ЭтоАвто, МакетОС4А, МакетОС4);
			
			ДанныеФизЛица = УправлениеВнеоборотнымиАктивамиПереопределяемый.ДанныеФизЛица(ВыборкаДокументов.Организация, ВыборкаДокументов.МОЛ, ВыборкаДокументов.Дата, Истина);
			
			Страница1 = Макет.ПолучитьОбласть("Страница1");
			Страница1.Параметры.Заполнить(ВыборкаСтрок);
			Страница1.Параметры.Организация = ПредставлениеОрганизации;
			Страница1.Параметры.ТабНомерМОЛ = ДанныеФизЛица.ТабельныйНомер;
			
			ДокументВводаВЭксплуатацию = "";
			ВведеноВЭксплуатацию       = "";
			
			УчетОСВызовСервера.СведенияОПринятииКУчетуОС(
				ВыборкаСтрок.ОС,
				ВыборкаСтрок.Организация,
				ДокументВводаВЭксплуатацию,
				ВведеноВЭксплуатацию);
			
			Если ЭтоАвто Тогда
				Страница1.Параметры.ВведеноВЭксплуатацию = ВведеноВЭксплуатацию;
				Если ДокументВводаВЭксплуатацию = Неопределено Тогда
					Страница1.Параметры.Пробег = ПолучитьПробегАвто(ВыборкаСтрок.ОС, ВыборкаСтрок.Дата, ВыборкаСтрок.Дата, ВыборкаСтрок.Организация);
				Иначе
					Страница1.Параметры.Пробег = ПолучитьПробегАвто(ВыборкаСтрок.ОС, ДокументВводаВЭксплуатацию.Дата, ВыборкаСтрок.Дата, ВыборкаСтрок.Организация);
				КонецЕсли;
			Иначе
				Страница1.Параметры.СрокЭкспл = ?(
					ЗначениеЗаполнено(ВведеноВЭксплуатацию),
					ВнеоборотныеАктивыКлиентСервер.ПредставлениеКоличестваМесяцевСтрокой(
						УправлениеВнеоборотнымиАктивами.ОпределитьФактическийСрокИспользования(ВведеноВЭксплуатацию, ВыборкаСтрок.Дата), Ложь),
					0);
			КонецЕсли;
			
			СтоимостьОС = ?(ВыборкаСтрок.ПорядокУчетаБУ = Перечисления.ПорядокПогашенияСтоимостиОС.СписаниеПриПринятииКУчету,
				ВыборкаСтрок.НачСтоимость,
				ВыборкаСтрок.Стоимость);
			
			АмортизацияОС = ?(ВыборкаСтрок.ПорядокУчетаБУ = Перечисления.ПорядокПогашенияСтоимостиОС.СписаниеПриПринятииКУчету,
				0,
				ВыборкаСтрок.НачАмортизация);
			
			Страница1.Параметры.ГодВыпуска     = ?(ЗначениеЗаполнено(ВыборкаСтрок.ГодВыпуска), Год(ВыборкаСтрок.ГодВыпуска), 0);
			Страница1.Параметры.ПринятоКУчету  = ВведеноВЭксплуатацию;
			Страница1.Параметры.НачСтоимость   = СтоимостьОС;
			Страница1.Параметры.НачАмортизация = АмортизацияОС;
			
			Страница1.Параметры.ОстСтоимость = ?(
				ВыборкаСтрок.ПорядокУчетаБУ = Перечисления.ПорядокПогашенияСтоимостиОС.СписаниеПриПринятииКУчету,
				0,
				СтоимостьОС - АмортизацияОС);
			
			Страница1.Параметры.Руководитель          = ОтветственныеЛица.РуководительПредставление;
			Страница1.Параметры.ДолжностьРуководителя = ОтветственныеЛица.РуководительДолжностьПредставление;
			
			Страница1.Параметры.Номер = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ВыборкаСтрок.Номер);
			
			ТабличныйДокумент.Вывести(Страница1);
			
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			
			Страница2_Шапка = Макет.ПолучитьОбласть("Страница2_Шапка");
			ТабличныйДокумент.Вывести(Страница2_Шапка);
			
			Если ЭтоАвто Тогда
				
				Страница2_Таблица3_Шапка = Макет.ПолучитьОбласть("Страница2_Таблица3_Шапка");
				ТабличныйДокумент.Вывести(Страница2_Таблица3_Шапка);
				
				Страница2_Таблица3_Строка = Макет.ПолучитьОбласть("Страница2_Таблица3_Строка");
				ТаблицаДМ = ВыборкаСтрок.ДрагоценныеМатериалы.Выгрузить();
				Если ИспользоватьУчетДМ И ТаблицаДМ.Количество()<>0 Тогда
					Для Каждого СтрокаДМ Из ТаблицаДМ Цикл
						Страница2_Таблица3_Строка.Параметры.Заполнить(СтрокаДМ);
						ТабличныйДокумент.Вывести(Страница2_Таблица3_Строка);
					КонецЦикла;
				Иначе
					Для к=0 По 6 Цикл // Пустые строки для заполнения вручную
						ТабличныйДокумент.Вывести(Страница2_Таблица3_Строка);
					КонецЦикла;
				КонецЕсли;
				Страница2_Таблица3_Подвал = Макет.ПолучитьОбласть("Страница2_Таблица3_Подвал");
				ТабличныйДокумент.Вывести(Страница2_Таблица3_Подвал);
				
				Страница2_Подвал = Макет.ПолучитьОбласть("Страница2_Подвал");
				ТабличныйДокумент.Вывести(Страница2_Подвал);
				
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				
				Страница3_Шапка = Макет.ПолучитьОбласть("Страница3_Шапка");
				ТабличныйДокумент.Вывести(Страница3_Шапка);
				Страница3_Таблица4_Шапка = Макет.ПолучитьОбласть("Страница3_Таблица4_Шапка");
				ТабличныйДокумент.Вывести(Страница3_Таблица4_Шапка);
				Страница3_Таблица4_Строка = Макет.ПолучитьОбласть("Страница3_Таблица4_Строка");
				Для к=0 По 6 Цикл // Пустые строки для заполнения вручную
					ТабличныйДокумент.Вывести(Страница3_Таблица4_Строка);
				КонецЦикла;
				Страница3_Таблица4_Подвал = Макет.ПолучитьОбласть("Страница3_Таблица4_Подвал");
				ТабличныйДокумент.Вывести(Страница3_Таблица4_Подвал);
				Страница3_Таблица5_Шапка = Макет.ПолучитьОбласть("Страница3_Таблица5_Шапка");
				ТабличныйДокумент.Вывести(Страница3_Таблица5_Шапка);
				Страница3_Таблица5_Строка = Макет.ПолучитьОбласть("Страница3_Таблица5_Строка");
				ВыборкаПрочихОприходований.Сбросить();
				ИтогСумма = 0;
				Если ВыборкаПрочихОприходований.НайтиСледующий(Новый Структура("Ссылка", ВыборкаСтрок.Ссылка)) Тогда
					ВыборкаТоваров = ВыборкаПрочихОприходований.Выбрать();
					Пока ВыборкаТоваров.Следующий() Цикл
						ИтогСумма = ИтогСумма + ВыборкаТоваров.УзелСумма;
						Страница3_Таблица5_Строка.Параметры.Заполнить(ВыборкаТоваров);
						ДанныеДокумента = Новый Структура(
							"Номер,Дата",
							ВыборкаТоваров.УзелДокументНомер, ВыборкаТоваров.УзелДокументДата);
						Страница3_Таблица5_Строка.Параметры.УзелДокумент = ОбщегоНазначенияУТКлиентСервер.СформироватьЗаголовокДокумента(
							ДанныеДокумента,
							НСтр("ru = 'Прочее оприходование';
								|en = 'Other recording as received'"));
						Страница3_Таблица5_Строка.Параметры.УзелНаименование = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
							ВыборкаТоваров.УзелНоменклатураНаименованиеПолное,
							ВыборкаТоваров.УзелХарактеристикаНаименованиеПолное);
						ТабличныйДокумент.Вывести(Страница3_Таблица5_Строка);
					КонецЦикла;
				Иначе
					Для к=0 По 6 Цикл // Пустые строки для заполнения вручную
						ТабличныйДокумент.Вывести(Страница3_Таблица5_Строка);
					КонецЦикла;
				КонецЕсли;
				Страница3_Таблица5_Подвал = Макет.ПолучитьОбласть("Страница3_Таблица5_Подвал");
				Страница3_Таблица5_Подвал.Параметры.ИтогСумма = ИтогСумма;
				ТабличныйДокумент.Вывести(Страница3_Таблица5_Подвал);
				
			Иначе
				Страница2_Таблица2_Шапка = Макет.ПолучитьОбласть("Страница2_Таблица2_Шапка");
				ТабличныйДокумент.Вывести(Страница2_Таблица2_Шапка);
				
				Страница2_Таблица2_Строка = Макет.ПолучитьОбласть("Страница2_Таблица2_Строка");
				ТаблицаДМ = ВыборкаСтрок.ДрагоценныеМатериалы.Выгрузить();
				Если ИспользоватьУчетДМ И ТаблицаДМ.Количество()<>0 Тогда
					Для Каждого СтрокаДМ Из ТаблицаДМ Цикл
						Страница2_Таблица2_Строка.Параметры.Заполнить(СтрокаДМ);
						ТабличныйДокумент.Вывести(Страница2_Таблица2_Строка);
					КонецЦикла;
				Иначе
					Для к=0 По 6 Цикл // Пустые строки для заполнения вручную
						ТабличныйДокумент.Вывести(Страница2_Таблица2_Строка);
					КонецЦикла;
				КонецЕсли;
				Страница2_Таблица2_Подвал = Макет.ПолучитьОбласть("Страница2_Таблица2_Подвал");
				ТабличныйДокумент.Вывести(Страница2_Таблица2_Подвал);
				
				Страница2_Таблица3_Шапка = Макет.ПолучитьОбласть("Страница2_Таблица3_Шапка");
				ТабличныйДокумент.Вывести(Страница2_Таблица3_Шапка);
				Страница2_Таблица3_Строка = Макет.ПолучитьОбласть("Страница2_Таблица3_Строка");
				ВыборкаПрочихОприходований.Сбросить();
				ИтогСумма = 0;
				Если ВыборкаПрочихОприходований.НайтиСледующий(Новый Структура("Ссылка", ВыборкаСтрок.Ссылка)) Тогда
					ВыборкаТоваров = ВыборкаПрочихОприходований.Выбрать();
					Пока ВыборкаТоваров.Следующий() Цикл
						ИтогСумма = ИтогСумма + ВыборкаТоваров.УзелСумма;
						Страница2_Таблица3_Строка.Параметры.Заполнить(ВыборкаТоваров);
						ДанныеДокумента = Новый Структура(
							"Номер,Дата",
							ВыборкаТоваров.УзелДокументНомер, ВыборкаТоваров.УзелДокументДата);
						Страница2_Таблица3_Строка.Параметры.УзелДокумент = ОбщегоНазначенияУТКлиентСервер.СформироватьЗаголовокДокумента(
							ДанныеДокумента,
							НСтр("ru = 'Прочее оприходование';
								|en = 'Other recording as received'"));
						Страница2_Таблица3_Строка.Параметры.УзелНаименование = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
							ВыборкаТоваров.УзелНоменклатураНаименованиеПолное,
							ВыборкаТоваров.УзелХарактеристикаНаименованиеПолное);
						ТабличныйДокумент.Вывести(Страница2_Таблица3_Строка);
					КонецЦикла;
				Иначе
					Для к=0 По 6 Цикл // Пустые строки для заполнения вручную
						ТабличныйДокумент.Вывести(Страница2_Таблица3_Строка);
					КонецЦикла;
				КонецЕсли;
				Страница2_Таблица3_Подвал = Макет.ПолучитьОбласть("Страница2_Таблица3_Подвал");
				Страница2_Таблица3_Подвал.Параметры.ИтогСумма = ИтогСумма;
				ТабличныйДокумент.Вывести(Страница2_Таблица3_Подвал);
				
			КонецЕсли;
			
		Иначе // Табличная часть состоит из нескольких строк
			
			Макет = МакетОС4Б;
			
			ДанныеФизЛица = УправлениеВнеоборотнымиАктивамиПереопределяемый.ДанныеФизЛица(ВыборкаДокументов.Организация, ВыборкаДокументов.МОЛ, ВыборкаДокументов.Дата, Истина);
			
			ПерваяИтерация = Истина;
			СсылкаДокумента = Неопределено;
			Пока ВыборкаСтрок.Следующий() Цикл
				
				Если ПерваяИтерация Тогда
					
					СсылкаДокумента = ВыборкаСтрок.Ссылка;
					
					Страница1_Шапка = Макет.ПолучитьОбласть("Страница1_Шапка");
					
					Страница1_Шапка.Параметры.Заполнить(ВыборкаСтрок);
					Страница1_Шапка.Параметры.Организация           = ПредставлениеОрганизации;
					Страница1_Шапка.Параметры.Руководитель          = ОтветственныеЛица.РуководительПредставление;
					Страница1_Шапка.Параметры.ДолжностьРуководителя = ОтветственныеЛица.РуководительДолжностьПредставление;
					Страница1_Шапка.Параметры.ТабНомерМОЛ           = ДанныеФизЛица.ТабельныйНомер;
					
					Страница1_Шапка.Параметры.Номер = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ВыборкаСтрок.Номер);
					
					ТабличныйДокумент.Вывести(Страница1_Шапка);
					
					Страница1_Таблица1_Шапка = Макет.ПолучитьОбласть("Страница1_Таблица1_Шапка");
					Страница1_Таблица1_Шапка.Параметры.Заполнить(ВыборкаСтрок);
					ТабличныйДокумент.Вывести(Страница1_Таблица1_Шапка);
					
					ПерваяИтерация = Ложь;
					
				КонецЕсли;
				
				Страница1_Таблица1_Строка = Макет.ПолучитьОбласть("Страница1_Таблица1_Строка");
				Страница1_Таблица1_Строка.Параметры.Заполнить(ВыборкаСтрок);
				СтоимостьОС = ?(
					ВыборкаСтрок.ПорядокУчетаБУ = Перечисления.ПорядокПогашенияСтоимостиОС.СписаниеПриПринятииКУчету,
					ВыборкаСтрок.НачСтоимость,
					ВыборкаСтрок.Стоимость);
				АмортизацияОС = ?(
					ВыборкаСтрок.ПорядокУчетаБУ = Перечисления.ПорядокПогашенияСтоимостиОС.СписаниеПриПринятииКУчету,
					0,
					ВыборкаСтрок.НачАмортизация);
				
				Страница1_Таблица1_Строка.Параметры.НачСтоимость = СтоимостьОС;
				Страница1_Таблица1_Строка.Параметры.НачАмортизация = АмортизацияОС;
				Страница1_Таблица1_Строка.Параметры.ОстСтоимость = ?(
					ВыборкаСтрок.ПорядокУчетаБУ = Перечисления.ПорядокПогашенияСтоимостиОС.СписаниеПриПринятииКУчету,
					0,
					СтоимостьОС - АмортизацияОС);
				
				Страница1_Таблица1_Строка.Параметры.Причина = ВыборкаСтрок.ПричинаСписания;
				
				ДокументВводаВЭксплуатацию = "";
				ВведеноВЭксплуатацию       = "";
				
				УчетОСВызовСервера.СведенияОПринятииКУчетуОС(
					ВыборкаСтрок.ОС,
					ВыборкаСтрок.Организация,
					ДокументВводаВЭксплуатацию,
					ВведеноВЭксплуатацию);
				
				Страница1_Таблица1_Строка.Параметры.СрокЭкспл = ?(
					ЗначениеЗаполнено(ВведеноВЭксплуатацию),
					ВнеоборотныеАктивыКлиентСервер.ПредставлениеКоличестваМесяцевСтрокой(
						УправлениеВнеоборотнымиАктивами.ОпределитьФактическийСрокИспользования(ВведеноВЭксплуатацию, ВыборкаСтрок.Дата), Ложь),
					0);
				
				ПерваяИтерацияДМ = Истина;
				ТаблицаДМ = ВыборкаСтрок.ДрагоценныеМатериалы.Выгрузить();
				Если ИспользоватьУчетДМ И ТаблицаДМ.Количество()<>0 Тогда
					Для Каждого СтрокаДМ Из ТаблицаДМ Цикл
						Если Не ПерваяИтерацияДМ Тогда
							Страница1_Таблица1_Строка = Макет.ПолучитьОбласть("Страница1_Таблица1_Строка");
						КонецЕсли;
						ПерваяИтерацияДМ = Ложь;
						Страница1_Таблица1_Строка.Параметры.Заполнить(СтрокаДМ);
						ТабличныйДокумент.Вывести(Страница1_Таблица1_Строка);
					КонецЦикла;
				Иначе
					ТабличныйДокумент.Вывести(Страница1_Таблица1_Строка);
				КонецЕсли;
				
			КонецЦикла;
			Страница1_Подвал = Макет.ПолучитьОбласть("Страница1_Подвал");
			ТабличныйДокумент.Вывести(Страница1_Подвал);
			
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			
			Страница2_Шапка = Макет.ПолучитьОбласть("Страница2_Шапка");
			ТабличныйДокумент.Вывести(Страница2_Шапка);
			Страница2_Таблица2_Шапка = Макет.ПолучитьОбласть("Страница2_Таблица2_Шапка");
			ТабличныйДокумент.Вывести(Страница2_Таблица2_Шапка);
			Страница2_Таблица2_Строка = Макет.ПолучитьОбласть("Страница2_Таблица2_Строка");
			ВыборкаПрочихОприходований.Сбросить();
			ИтогСумма = 0;
			Если ВыборкаПрочихОприходований.НайтиСледующий(Новый Структура("Ссылка", СсылкаДокумента)) Тогда
				ВыборкаТоваров = ВыборкаПрочихОприходований.Выбрать();
				Пока ВыборкаТоваров.Следующий() Цикл
					ИтогСумма = ИтогСумма + ВыборкаТоваров.УзелСумма;
					Страница2_Таблица2_Строка.Параметры.Заполнить(ВыборкаТоваров);
					Страница2_Таблица2_Строка.Параметры.УзелДокументНаименование = НСтр("ru = 'Прочее оприходование';
																						|en = 'Other recording as received'");
					Страница2_Таблица2_Строка.Параметры.УзелДокументНомер = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(
						ВыборкаТоваров.УзелДокументНомер);
					Страница2_Таблица2_Строка.Параметры.УзелНаименование = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
						ВыборкаТоваров.УзелНоменклатураНаименованиеПолное,
						ВыборкаТоваров.УзелХарактеристикаНаименованиеПолное);
					ТабличныйДокумент.Вывести(Страница2_Таблица2_Строка);
				КонецЦикла;
			Иначе
				Для к=0 По 6 Цикл // Пустые строки для заполнения вручную
					ТабличныйДокумент.Вывести(Страница2_Таблица2_Строка);
				КонецЦикла;
			КонецЕсли;
			Страница2_Таблица2_Подвал = Макет.ПолучитьОбласть("Страница2_Таблица2_Подвал");
			Страница2_Таблица2_Подвал.Параметры.ИтогСумма = ИтогСумма;
			ТабличныйДокумент.Вывести(Страница2_Таблица2_Подвал);
			
		КонецЕсли;
		
		Подвал = Макет.ПолучитьОбласть(?(ЭтоАвто, "Страница3_Подвал", "Страница2_Подвал"));
		Подвал.Параметры.ГлавБух = ОтветственныеЛица.ГлавныйБухгалтерПредставление;
		ТабличныйДокумент.Вывести(Подвал);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументов.Ссылка);
		
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ПолучитьПробегАвто(ОбъектОС, НачДата, КонДата, Организация)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ЕСТЬNULL(НаработкиПериодОкончание.Значение, 0) - ЕСТЬNULL(НаработкиПериодНачало.Значение, 0) КАК Пробег
		|ИЗ
		|	РегистрСведений.НаработкиОбъектовЭксплуатации.СрезПоследних(&НачГраница, ОбъектЭксплуатации = &ОС) КАК НаработкиПериодНачало
		|		ПОЛНОЕ СОЕДИНЕНИЕ РегистрСведений.НаработкиОбъектовЭксплуатации.СрезПоследних(&КонГраница, ОбъектЭксплуатации = &ОС) КАК НаработкиПериодОкончание
		|		ПО НаработкиПериодНачало.ОбъектЭксплуатации = НаработкиПериодОкончание.ОбъектЭксплуатации
		|			И НаработкиПериодНачало.ПоказательНаработки = НаработкиПериодОкончание.ПоказательНаработки");
	
	Запрос.УстановитьПараметр("НачГраница", Новый Граница(НачДата, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("КонГраница", Новый Граница(КонДата, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ОС", ОбъектОС);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.Пробег;
	
КонецФункции

#КонецОбласти

#Область БлокировкаПриОбновленииИБ

Процедура ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(ПредставлениеОперации) Экспорт
	
	ВходящиеДанные = Новый Соответствие;
	
	УчетОСВызовСервера.ЗаполнитьВходящиеДанныеАмортизации(ВходящиеДанные);
	
	ЗакрытиеМесяцаСервер.ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(ВходящиеДанные, ПредставлениеОперации);
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Функция ПараметрыВыбораСтатейИАналитик() Экспорт
	
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья = "СтатьяРасходов";
	ПараметрыВыбора.ДоступностьПоОперации = Неопределено;
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходов";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходов");
	
	Возврат ПараметрыВыбора;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
