
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Параметры.Свойство("Действие", Действие);
	Параметры.Свойство("Документ", Документ);
	Параметры.Свойство("НоваяСтрока", НоваяСтрока);
	ПодборТоваровКлиентСервер.СформироватьЗаголовокФормыПодбора(Заголовок, Документ);

	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СтатусПриИзменении(Элемент)
	УстановитьОтборПоСтатусу();
КонецПроцедуры

&НаКлиенте
Процедура ЗаказНаПроизводствоПриИзменении(Элемент)
	УстановитьОтборПоЗаказуНаПроизводство();
КонецПроцедуры

&НаКлиенте
Процедура ИзделиеПриИзменении(Элемент)
	УстановитьОтборПоИзделию();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	ЗначениеВыбора = Новый Структура("Объект, КодСтрокиЗаказаНаПроизводство, Номенклатура, Характеристика");
	ЗаполнитьЗначенияСвойств(ЗначениеВыбора, ТекущиеДанные);
	
	СтруктураВыбора = Новый Структура;
	СтруктураВыбора.Вставить("ВыполняемаяОперация", "ВыборПозицииЗаказаНаПроизводство");
	СтруктураВыбора.Вставить("ЗначениеВыбора", ЗначениеВыбора);
	СтруктураВыбора.Вставить("Действие", Действие);
	СтруктураВыбора.Вставить("НоваяСтрока", НоваяСтрока);
	
	ОповеститьОВыборе(СтруктураВыбора);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	//
	
	НоменклатураСервер.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(ЭтаФорма, 
																			 "СписокХарактеристика",
																		     "Список.ХарактеристикиИспользуются");
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоСтатусу()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список,
		"Статус",
		Статус,
		ВидСравненияКомпоновкиДанных.Равно,
		, // Представление - автоматически
		ЗначениеЗаполнено(Статус));
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоЗаказуНаПроизводство()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список,
		"Объект",
		ЗаказНаПроизводство,
		ВидСравненияКомпоновкиДанных.Равно,
		, // Представление - автоматически
		ЗначениеЗаполнено(ЗаказНаПроизводство));
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоИзделию()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список,
		"Номенклатура",
		Изделие,
		ВидСравненияКомпоновкиДанных.Равно,
		, // Представление - автоматически
		ЗначениеЗаполнено(Изделие));
	
КонецПроцедуры

#КонецОбласти

