#Область ОписаниеПеременных

&НаКлиенте
Перем КэшированныеЗначения;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);

	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПриЧтенииСозданииНаСервере();
	КонецЕсли;
	
	ИнициализироватьРасчетПоФормуле();
	
	// Обработчик механизма "Свойства"
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Объект", Объект);
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтаФорма, ДополнительныеПараметры);
	
	Элементы.ВозвратныеОтходыВидЦены.БыстрыйВыбор = Не ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоВидовЦен");
	Элементы.МатериалыИУслугиВидЦены.БыстрыйВыбор = Не ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоВидовЦен");

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// ИнтеграцияС1СДокументооборотом
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПриСозданииНаСервере(ЭтаФорма);
	// Конец ИнтеграцияС1СДокументооборотом

	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);

	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриЧтенииСозданииНаСервере();

	МодификацияКонфигурацииПереопределяемый.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Перем Действие;
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
		ВыбранноеЗначение.Свойство("Действие", Действие);
	КонецЕсли;
	
	Если ИсточникВыбора.ИмяФормы = "Документ.ПлановаяКалькуляция.Форма.ФормаВводаОбластиДействия" И Действие = "ВводОбластиДействия" Тогда
		
		Модифицированность = Истина;
		ЗначениеВыбора = ВыбранноеЗначение.ЗначениеВыбора;
		
		ВыборОбластиДействияНаСервере(ЗначениеВыбора);
		
	ИначеЕсли ИсточникВыбора.ИмяФормы = "Документ.ПлановаяКалькуляция.Форма.ВыборПозицииЗаказаНаПроизводство" И Действие = "ВыборПозиции" Тогда
		
		Модифицированность = Истина;
		ЗначениеВыбора = ВыбранноеЗначение.ЗначениеВыбора;
		
		Объект.ДействиеКалькуляции.Очистить();
		НоваяСтрока = Объект.ДействиеКалькуляции.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ЗначениеВыбора);
		ЗаказНаПроизводство = ЗначениеВыбора.Объект;
		КодСтрокиЗаказаНаПроизводство = ЗначениеВыбора.КодСтрокиЗаказаНаПроизводство;
		
		ЗаполнитьРеквизитыПредставленияОбластиДействия();
		
	ИначеЕсли ИсточникВыбора.ИмяФормы = "Документ.ПлановаяКалькуляция.Форма.ФормаВводаОбластиДействия" И Действие = "ЗаполнениеПоПозиции" Тогда
		
		Модифицированность = Истина;
		ЗначениеВыбора = ВыбранноеЗначение.ЗначениеВыбора;
		
		ЗаполнитьПоСпецификацииЗаказаНаСервере(ЗначениеВыбора.ЗаказНаПроизводство, ЗначениеВыбора.КодСтроки);
		
		ОбновитьИтогиТекущейТаблицы();
		
	ИначеЕсли ИсточникВыбора.ИмяФормы = "Справочник.РесурсныеСпецификации.Форма.ФормаВыбора" Тогда
		
		Модифицированность = Истина;
		ЗаполнитьДокументПоСпецификации(ВыбранноеЗначение, КэшированныеЗначения);
		
		ОбновитьИтогиТекущейТаблицы();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	СообщениеКалькуляцииОшибка = НСтр("ru = 'Формула в строке %1 списка ""Статьи калькуляции"" содержит ошибки.';
										|en = 'Formula in line %1 of list ""Costing items"" contains errors.'");
	СообщениеРасходыОшибка = НСтр("ru = 'Формула в строке %1 списка ""Статьи расходов"" содержит ошибки.';
									|en = 'Formula in line %1 of list ""Expense items"" contains errors.'");
	
	СообщениеКалькуляцииНеЗаполнено = НСтр("ru = 'Не заполнена формула в строке %1 списка ""Статьи калькуляции"".';
											|en = 'Formula in line %1 of the ""Costing items"" list is not filled in.'");
	СообщениеРасходыНеЗаполнено = НСтр("ru = 'Не заполнена формула в строке %1 списка ""Статьи расходов"".';
										|en = 'Formula in line %1 of the ""Expense items"" list is not filled in.'");
	
	СтруктураОтбора = Новый Структура("СпособРасчета", Перечисления.СпособыРасчетаЗатратПлановойКалькуляции.ПоФормуле);
	НайденныеСтроки = Объект.СтатьиКалькуляции.НайтиСтроки(СтруктураОтбора);
	Для Каждого Строка Из НайденныеСтроки Цикл
		
		Если Не ЗначениеЗаполнено(Строка.Формула) Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(СообщениеКалькуляцииНеЗаполнено, Строка.НомерСтроки);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстОшибки,
				Объект.Ссылка,
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Объект.СтатьиКалькуляции", Строка.НомерСтроки, "Формула"),
				,
				Отказ);
		КонецЕсли;
		
		Если Строка.ФормулаСодержитОшибки Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(СообщениеКалькуляцииОшибка, Строка.НомерСтроки);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстОшибки,
				Объект.Ссылка,
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Объект.СтатьиКалькуляции", Строка.НомерСтроки, "Формула"),
				,
				Отказ);
		КонецЕсли;
		
	КонецЦикла;
	
	СтруктураОтбора = Новый Структура("РасчетПоФормуле", Истина);
	НайденныеСтроки = Объект.СтатьиРасходов.НайтиСтроки(СтруктураОтбора);
	Для Каждого Строка Из НайденныеСтроки Цикл
		
		Если Не ЗначениеЗаполнено(Строка.Формула) Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(СообщениеРасходыНеЗаполнено, Строка.НомерСтроки);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстОшибки,
				Объект.Ссылка,
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Объект.СтатьиРасходов", Строка.НомерСтроки, "Формула"),
				,
				Отказ);
		КонецЕсли;
		
		Если Строка.ФормулаСодержитОшибки Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(СообщениеРасходыОшибка, Строка.НомерСтроки);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстОшибки,
				Объект.Ссылка,
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Объект.СтатьиРасходов", Строка.НомерСтроки, "Формула"),
				,
				Отказ);
		КонецЕсли;
		
	КонецЦикла;
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ЗаполнитьСлужебныеРеквизитыФормы();
	ЗаполнитьРеквизитыПредставленияОбластиДействия();
	ЗаполнитьСлужебныеРеквизитыТабличныхЧастей();
	

	МодификацияКонфигурацииПереопределяемый.ПослеЗаписиНаСервере(ЭтаФорма, ТекущийОбъект, ПараметрыЗаписи);

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)

	МодификацияКонфигурацииКлиентПереопределяемый.ПослеЗаписи(ЭтаФорма, ПараметрыЗаписи);

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ПодключаемыеКоманды") Тогда
		МодульПодключаемыеКомандыКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ПодключаемыеКомандыКлиент");
		МодульПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.КраткийСоставДокумента = "";
	
	Для Каждого Строка Из Объект.ДействиеКалькуляции Цикл
		
		Если Объект.ОбъектКалькуляции = Перечисления.ОбъектыКалькуляции.ЗаказНаПроизводство Тогда
			Представление = Строка.ПредставлениеПозицииЗаказа;
		ИначеЕсли Объект.ОбъектКалькуляции = Перечисления.ОбъектыКалькуляции.Изделие И Строка.ИспользоватьХарактеристику Тогда
			Представление = НоменклатураКлиентСервер.ПредставлениеНоменклатуры(Строка.Объект, Строка.Характеристика);
		Иначе
			Представление = Строка(Строка.Объект);
		КонецЕсли;
		
		Если СтрДлина(ТекущийОбъект.КраткийСоставДокумента + Представление) < 100 Тогда
			
			Если ЗначениеЗаполнено(ТекущийОбъект.КраткийСоставДокумента) Тогда
				ТекущийОбъект.КраткийСоставДокумента = ТекущийОбъект.КраткийСоставДокумента + "; " + Представление;
			Иначе
				ТекущийОбъект.КраткийСоставДокумента = Представление;
			КонецЕсли;
			
		Иначе
			
			Если ЗначениеЗаполнено(ТекущийОбъект.КраткийСоставДокумента) Тогда
				ТекущийОбъект.КраткийСоставДокумента = ТекущийОбъект.КраткийСоставДокумента + "; ...";
			Иначе
				ТекущийОбъект.КраткийСоставДокумента = Лев(Представление, 100);
			КонецЕсли;
			
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	МодификацияКонфигурацииПереопределяемый.ПередЗаписьюНаСервере(ЭтаФорма, Отказ, ТекущийОбъект, ПараметрыЗаписи);
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// ИнтеграцияС1СДокументооборотом
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПередЗаписьюНаСервере(ЭтаФорма, ТекущийОбъект, ПараметрыЗаписи);
	// Конец ИнтеграцияС1СДокументооборотом
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОбъектКалькуляцииПриИзменении(Элемент)
	
	ОбъектКалькуляцииПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура НесколькоОбъектовПриИзменении(Элемент)
	
	НесколькоОбъектовПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура УказатьХарактеристикуПриИзменении(Элемент)
	
	Если Не УказатьХарактеристику Тогда
		Характеристика = ПредопределенноеЗначение("Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка");
		Объект.ДействиеКалькуляции[0].Характеристика = ПредопределенноеЗначение("Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка");
		Объект.ДействиеКалькуляции[0].ИспользоватьХарактеристику = Ложь;
		Элементы.Характеристика.ПодсказкаВвода = НСтр("ru = 'Любая';
														|en = 'Any'");
	Иначе
		Объект.ДействиеКалькуляции[0].ИспользоватьХарактеристику = Истина;
		Элементы.Характеристика.ПодсказкаВвода = "";
	КонецЕсли;
	
	Элементы.Характеристика.Доступность = УказатьХарактеристику;
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураПриИзменении(Элемент)
	
	НоменклатураПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ХарактеристикаПриИзменении(Элемент)
	
	Объект.ДействиеКалькуляции.Очистить();
	НоваяСтрока = Объект.ДействиеКалькуляции.Добавить();
	НоваяСтрока.Объект = Номенклатура;
	НоваяСтрока.Характеристика = Характеристика;
	НоваяСтрока.ИспользоватьХарактеристику = Истина;
	
	ЗаполнитьРеквизитыПредставленияОбластиДействия();
	
КонецПроцедуры

&НаКлиенте
Процедура СпецификацияПриИзменении(Элемент)
	
	Объект.ДействиеКалькуляции.Очистить();
	
	Если ЗначениеЗаполнено(Спецификация) Тогда
		НоваяСтрока = Объект.ДействиеКалькуляции.Добавить();
		НоваяСтрока.Объект = Спецификация;
	КонецЕсли;
	
	ЗаполнитьРеквизитыПредставленияОбластиДействия(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаказНаПроизводствоПриИзменении(Элемент)
	
	Объект.ДействиеКалькуляции.Очистить();
	
	Если ЗначениеЗаполнено(ПредставлениеПозицииЗаказа) Тогда
		ПредставлениеПозицииЗаказа = ПредставлениеПозицииЗаказаИсходное;
		НоваяСтрока = Объект.ДействиеКалькуляции.Добавить();
		НоваяСтрока.Объект = ЗаказНаПроизводство;
		НоваяСтрока.КодСтрокиЗаказаНаПроизводство = КодСтрокиЗаказаНаПроизводство;
	КонецЕсли;
	
	ЗаполнитьРеквизитыПредставленияОбластиДействия(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаказНаПроизводствоНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПараметрыФормы = Новый Структура("Документ, Действие", Объект.Ссылка, "ВыборПозиции");
	ОткрытьФорму("Документ.ПлановаяКалькуляция.Форма.ВыборПозицииЗаказаНаПроизводство", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаказНаПроизводствоОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если Объект.ДействиеКалькуляции.Количество() > 0 Тогда
		ПоказатьЗначение(Неопределено, Объект.ДействиеКалькуляции[0].Объект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДетализироватьСтатьиКалькуляцииПриИзменении(Элемент)
	
	Если Не Объект.ДетализироватьСтатьиКалькуляции Тогда
		
		ДетальныеЗаписи = ДетальныеЗаписиПоСтатье();
		
		Если ДетальныеЗаписи <> Неопределено Тогда
			ТекстВопроса = НСтр("ru = 'Детальные записи по статьям калькуляции будут удалены. Продолжить?';
								|en = 'Detailed records on costing items will be deleted. Continue?'");
			ОписаниеОповещения = Новый ОписаниеОповещения("ДетализироватьСтатьиКалькуляцииПриИзмененииЗавершение", ЭтотОбъект);
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
            Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	ДетализироватьСтатьиКалькуляцииПриИзмененииФрагмент();
КонецПроцедуры

&НаКлиенте
Процедура ДетализироватьСтатьиКалькуляцииПриИзмененииЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    Ответ = РезультатВопроса;
    
    Если Ответ = КодВозвратаДиалога.Нет Тогда
        Объект.ДетализироватьСтатьиКалькуляции = Истина;
        Возврат;
    КонецЕсли;
    
    ДетализироватьСтатьиКалькуляцииПриИзмененииФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура ДетализироватьСтатьиКалькуляцииПриИзмененииФрагмент()
    
    Перем НайденныеСтроки, ОтборСпособРасчета, Строка;
    
    Объект.ВозвратныеОтходы.Очистить();
    Объект.МатериалыИУслуги.Очистить();
    Объект.Трудозатраты.Очистить();
    Объект.СтатьиРасходов.Очистить();
    
    ОтборСпособРасчета = Новый Структура("СпособРасчета", ПредопределенноеЗначение("Перечисление.СпособыРасчетаЗатратПлановойКалькуляции.ПоДетальнымЗаписям"));
    НайденныеСтроки = Объект.СтатьиКалькуляции.НайтиСтроки(ОтборСпособРасчета);
    
    Для Каждого Строка Из НайденныеСтроки Цикл
        Строка.СпособРасчета = ПредопределенноеЗначение("Перечисление.СпособыРасчетаЗатратПлановойКалькуляции.ФиксированнымЗначением");
    КонецЦикла;
    
    УстановитьВидимостьПоПризнакуДетализации();

КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеОбластиДействияНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	
	ПараметрыФормы.Вставить("Действие", "ВводОбластиДействия");
	ПараметрыФормы.Вставить("АдресВХранилище", ДействиеКалькуляцииВХранилище());
	ПараметрыФормы.Вставить("КалькуляционнаяЕдиница", Объект.КалькуляционнаяЕдиница);
	ПараметрыФормы.Вставить("ОбъектКалькуляции", Объект.ОбъектКалькуляции);
	ПараметрыФормы.Вставить("Документ", Объект.Ссылка);
	
	ОткрытьФорму("Документ.ПлановаяКалькуляция.Форма.ФормаВводаОбластиДействия", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьОрганизациюПриИзменении(Элемент)
	
	Если Объект.ИспользоватьОрганизацию Тогда
		
		ЗаполнитьОбъектПоСтатистике();
		
		Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
			
			Объект.Организация = ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка");
			Элементы.Организация.ПодсказкаВвода = НСтр("ru = 'Любая';
														|en = 'Any'");
			
		КонецЕсли;
			
	Иначе
		
		Объект.Организация = Неопределено;
		Элементы.Организация.ПодсказкаВвода = "";
		
	КонецЕсли;
	
	Элементы.Организация.Доступность = Объект.ИспользоватьОрганизацию;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСтатьиКалькуляции

&НаКлиенте
Процедура СтатьиКалькуляцииСпособРасчетаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.СтатьиКалькуляции.ТекущиеДанные;
	
	ТекущиеДанные.ФормулаСодержитОшибки = Ложь;
	ТекущиеДанные.Формула = "";
	
	Если Не Объект.ДетализироватьСтатьиКалькуляции
		Или ТекущиеДанные.СпособРасчета = ПредопределенноеЗначение("Перечисление.СпособыРасчетаЗатратПлановойКалькуляции.ПоДетальнымЗаписям")
		Или Не ЗначениеЗаполнено(ТекущиеДанные.СтатьяКалькуляции) Тогда
		Возврат;
	КонецЕсли;
	
	ДетальныеЗаписи = ДетальныеЗаписиПоСтатье(ТекущиеДанные.СтатьяКалькуляции);
	
	Если ДетальныеЗаписи <> Неопределено Тогда
		
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Документ содержит детальные записи по статье калькуляции ""%1"".
										|При установке способа расчета ""%2"" детальные записи будут удалены. 
										|Продолжить?';
										|en = 'Document contains detailed records on the ""%1"" costing item.
										|Detailed records will be deleted when setting the ""%2"" calculation method. 
										|Continue?'"),
							ТекущиеДанные.СтатьяКалькуляции,
							ТекущиеДанные.СпособРасчета);
							
		ОписаниеОповещения = Новый ОписаниеОповещения("СтатьиКалькуляцииСпособРасчетаПриИзмененииЗавершение", ЭтотОбъект, Новый Структура("ДетальныеЗаписи, ТекущиеДанные", ДетальныеЗаписи, ТекущиеДанные));
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьиКалькуляцииСпособРасчетаПриИзмененииЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    ДетальныеЗаписи = ДополнительныеПараметры.ДетальныеЗаписи;
    ТекущиеДанные = ДополнительныеПараметры.ТекущиеДанные;
    
    Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
        ТекущиеДанные.СпособРасчета = ПредопределенноеЗначение("Перечисление.СпособыРасчетаЗатратПлановойКалькуляции.ПоДетальнымЗаписям");
		Возврат;
    КонецЕсли;
        
    Для Каждого Строка Из ДетальныеЗаписи.Отходы Цикл
        Объект.ВозвратныеОтходы.Удалить(Объект.ВозвратныеОтходы.НайтиПоИдентификатору(Строка));
    КонецЦикла;
    
    Для Каждого Строка Из ДетальныеЗаписи.Материалы Цикл
        Объект.МатериалыИУслуги.Удалить(Объект.МатериалыИУслуги.НайтиПоИдентификатору(Строка));
    КонецЦикла;
    
    Для Каждого Строка Из ДетальныеЗаписи.Трудозатраты Цикл
        Объект.Трудозатраты.Удалить(Объект.Трудозатраты.НайтиПоИдентификатору(Строка));
    КонецЦикла;
    
    Для Каждого Строка Из ДетальныеЗаписи.Статьи Цикл
        Объект.СтатьиРасходов.Удалить(Объект.СтатьиРасходов.НайтиПоИдентификатору(Строка));
    КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура СтатьиКалькуляцииФормулаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.СтатьиКалькуляции.ТекущиеДанные;
	
	АдресВХранилище = БазовыеСтатьиКалькуляцииВХранилище(ТекущиеДанные.СтатьяКалькуляции);
	
	ПараметрыРедактирования = Новый Структура;
	ПараметрыРедактирования.Вставить("Формула", ТекущиеДанные.Формула);
	ПараметрыРедактирования.Вставить("ДеревоОперандов", АдресВХранилище);
	ПараметрыРедактирования.Вставить("ОперандыЗаголовок", НСтр("ru = 'Статьи калькуляции';
																|en = 'Product cost elements'"));
	ПараметрыРедактирования.Вставить("Операторы", АдресХранилищаДереваОператоров);
	ПараметрыРедактирования.Вставить("ТипРезультата", Новый ОписаниеТипов("Число"));
	
	РезультатРедактирования = Неопределено;

	
	ОткрытьФорму("ОбщаяФорма.КонструкторФормул", ПараметрыРедактирования, Элементы.СтатьиКалькуляцииФормула,,,, Новый ОписаниеОповещения("СтатьиКалькуляцииФормулаНачалоВыбораЗавершение", ЭтотОбъект, Новый Структура("ТекущиеДанные", ТекущиеДанные)), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьиКалькуляцииФормулаНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    ТекущиеДанные = ДополнительныеПараметры.ТекущиеДанные;
    
    
    РезультатРедактирования = Результат;
    
    Если РезультатРедактирования <> Неопределено Тогда
        ТекущиеДанные.Формула = РезультатРедактирования;
        ТекущиеДанные.ФормулаСодержитОшибки = Ложь;
    КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СтатьиКалькуляцииПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если Копирование Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьиКалькуляцииПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		
		ТекущиеДанные = Элементы.СтатьиКалькуляции.ТекущиеДанные;
		
		Если Объект.ДетализироватьСтатьиКалькуляции Тогда
			ТекущиеДанные.СпособРасчета = ПредопределенноеЗначение("Перечисление.СпособыРасчетаЗатратПлановойКалькуляции.ПоДетальнымЗаписям");
		Иначе
			ТекущиеДанные.СпособРасчета = ПредопределенноеЗначение("Перечисление.СпособыРасчетаЗатратПлановойКалькуляции.ФиксированнымЗначением");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьиКалькуляцииПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	ТекущиеДанные = Элементы.СтатьиКалькуляции.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		СтруктураОтбора = Новый Структура("СтатьяКалькуляции", ТекущиеДанные.СтатьяКалькуляции);
		НайденныеСтроки = Объект.СтатьиКалькуляции.НайтиСтроки(СтруктураОтбора);
		
		Если НайденныеСтроки.Количество() > 1 Тогда
			
			Если Не ОтменаРедактирования Тогда
				ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Статья калькуляции ""%1"" уже содержится в списке!';
					|en = 'Costing item ""%1"" is already in the list.'"),
				ТекущиеДанные.СтатьяКалькуляции);
				
				ПоказатьПредупреждение(, ТекстПредупреждения);
				
				Отказ = Истина;
			Иначе
				ТекущиеДанные.СтатьяКалькуляции = ПредопределенноеЗначение("Справочник.СтатьиКалькуляции.ПустаяСсылка");
				ТекущиеДанные.Идентификатор = "";
				ТекущиеДанные.СтрокаИзменена = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьиКалькуляцииПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ТекущиеДанные = Элементы.СтатьиКалькуляции.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		ТекущиеДанные.СтрокаИзменена = Истина;
		СтатьиКалькуляцииСтатьяКалькуляцииПриИзмененииНаСервере(ТекущиеДанные.ПолучитьИдентификатор());
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьиКалькуляцииПередУдалением(Элемент, Отказ)
	
	ТекущиеДанные = Элементы.СтатьиКалькуляции.ТекущиеДанные;
	
	Если Не Объект.ДетализироватьСтатьиКалькуляции
		Или ТекущиеДанные.СпособРасчета <> ПредопределенноеЗначение("Перечисление.СпособыРасчетаЗатратПлановойКалькуляции.ПоДетальнымЗаписям") Тогда
		Возврат;
	КонецЕсли;
	
	ДетальныеЗаписи = ДетальныеЗаписиПоСтатье(ТекущиеДанные.СтатьяКалькуляции);
	
	Если ДетальныеЗаписи <> Неопределено Тогда
		
		Отказ = Истина;
		
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'По статье калькуляции ""%1"" введены детальные записи. 
									|Удаление строки приведет к удалению детальных записей. 
									|Удалить строку?';
									|en = 'Detailed entries are entered for costing item ""%1"".
									|The line removal will lead to the detailed entry removal.
									|Remove the line?'"),
						ТекущиеДанные.СтатьяКалькуляции);
			
		ДопПараметры = Новый Структура("ДетальныеЗаписи, ИдентификаторСтроки", ДетальныеЗаписи, ТекущиеДанные.ПолучитьИдентификатор());
		ОписаниеОповещения = Новый ОписаниеОповещения("СтатьиКалькуляцииПередУдалениемЗавершение", ЭтотОбъект, ДопПараметры);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьиКалькуляцииПослеУдаления(Элемент)
	
	РассчитатьТаблицуСтатейКалькуляции();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыВозвратныеОтходы

&НаКлиенте
Процедура ВозвратныеОтходыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле = Элементы.ВозвратныеОтходыЦена Тогда
		
		ТекущиеДанные = Элементы.ВозвратныеОтходы.ТекущиеДанные;
		Если ЗначениеЗаполнено(ТекущиеДанные.ВидЦены) Тогда
			ОчиститьСообщения();
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Для редактирования цены очистите вид цены';
				|en = 'To edit the price, clear the price type'"),
			Объект.Ссылка,
			ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Объект.ВозвратныеОтходы", ТекущиеДанные.НомерСтроки, "ВидЦены"),);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратныеОтходыПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.ВозвратныеОтходы.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(ТекущиеДанные.СтатьяКалькуляции) Тогда
		ОбновитьИтогиПоСтатьеКалькуляции("Отходы", ТекущиеДанные.СтатьяКалькуляции, ТекущиеДанные.Сумма);
	Иначе
		ОбновитьИтогиПоСтатьеКалькуляции("Отходы");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратныеОтходыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока И Не Копирование Тогда
		
		ТекущиеДанные = Элементы.ВозвратныеОтходы.ТекущиеДанные;
		ТекущиеДанные.ВидЦены = Объект.ВидЦены;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратныеОтходыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	РассчитатьЗависимыеИтогиПоТабличнымЧастям();
	
	ТекущиеДанные = Элементы.ВозвратныеОтходы.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(ТекущиеДанные.СтатьяКалькуляции) Тогда
		ОбновитьИтогиПоСтатьеКалькуляции("Отходы", ТекущиеДанные.СтатьяКалькуляции, ТекущиеДанные.Сумма);
	Иначе
		ОбновитьИтогиПоСтатьеКалькуляции("Отходы");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыходныеИзделияНоменклатураПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ВозвратныеОтходы.ТекущиеДанные;
	
	СтруктураЗаполненияЦены = Новый Структура;
	СтруктураЗаполненияЦены.Вставить("Дата",   Объект.Дата);
	СтруктураЗаполненияЦены.Вставить("Валюта", Объект.Валюта);
	
	СтруктураЗаполненияВидаЦены = Новый Структура;
	СтруктураЗаполненияВидаЦены.Вставить("ВидЦены", Объект.ВидЦены);
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", ТекущиеДанные.Характеристика);
	СтруктураДействий.Вставить("ПроверитьЗаполнитьУпаковкуПоВладельцу", ТекущиеДанные.Упаковка);
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	СтруктураДействий.Вставить("ЗаполнитьВидЦены", СтруктураЗаполненияВидаЦены);
	
	СтруктураДействий.Вставить("ЗаполнитьЦенуПродажи", СтруктураЗаполненияЦены);
	СтруктураДействий.Вставить("ПересчитатьСумму");

	СтруктураДействий.Вставить("НоменклатураПриИзмененииПереопределяемый", Новый Структура("ИмяФормы, ИмяТабличнойЧасти",
		ЭтаФорма.ИмяФормы, "ВыходныеИзделия"));

	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущиеДанные, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыходныеИзделияКоличествоУпаковокПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ВозвратныеОтходы.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущиеДанные, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыходныеИзделияУпаковкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ВозвратныеОтходы.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	СтруктураДействий.Вставить("ПересчитатьЦенуЗаУпаковку", ТекущиеДанные.Количество);
	СтруктураДействий.Вставить("ПересчитатьСумму");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущиеДанные, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыходныеИзделияЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ВозвратныеОтходы.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьСумму");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущиеДанные, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыходныеИзделияСуммаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ВозвратныеОтходы.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьЦенуПоСумме", "КоличествоУпаковок");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущиеДанные, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыходныеИзделияСтатьяКалькуляцииПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ВозвратныеОтходы.ТекущиеДанные;
	ЗапросИзмененияСпособаРасчета(Неопределено, ТекущиеДанные, НСтр("ru = 'возвратных отходов';
																	|en = 'recyclable waste '"));
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратныеОтходыВидЦеныПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ВозвратныеОтходы.ТекущиеДанные;
	
	СтруктураЗаполненияЦены = Новый Структура;
	СтруктураЗаполненияЦены.Вставить("Дата",   Объект.Дата);
	СтруктураЗаполненияЦены.Вставить("Валюта", Валюта);
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ЗаполнитьЦенуПродажи", СтруктураЗаполненияЦены);
	СтруктураДействий.Вставить("ПересчитатьСумму");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратныеОтходыПослеУдаления(Элемент)
	
	РассчитатьЗависимыеИтогиПоТабличнымЧастям();
	
	ТекущиеДанные = Элементы.ВозвратныеОтходы.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(ТекущиеДанные.СтатьяКалькуляции) Тогда
		ОбновитьИтогиПоСтатьеКалькуляции("Отходы", ТекущиеДанные.СтатьяКалькуляции, ТекущиеДанные.Сумма);
	Иначе
		ОбновитьИтогиПоСтатьеКалькуляции("Отходы");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыМатериалыИУслуги

&НаКлиенте
Процедура МатериалыИУслугиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле = Элементы.МатериалыИУслугиЦена Тогда
		
		ТекущиеДанные = Элементы.МатериалыИУслуги.ТекущиеДанные;
		Если ЗначениеЗаполнено(ТекущиеДанные.ВидЦены) Тогда
			ОчиститьСообщения();
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					НСтр("ru = 'Для редактирования цены очистите вид цены';
						|en = 'To edit the price, clear the price type'"),
					Объект.Ссылка,
					ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Объект.МатериалыИУслуги", ТекущиеДанные.НомерСтроки, "ВидЦены"),);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыИУслугиПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.МатериалыИУслуги.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(ТекущиеДанные.СтатьяКалькуляции) Тогда
		ОбновитьИтогиПоСтатьеКалькуляции("Материалы", ТекущиеДанные.СтатьяКалькуляции, ТекущиеДанные.Сумма);
	Иначе
		ОбновитьИтогиПоСтатьеКалькуляции("Материалы");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыИУслугиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока И Не Копирование Тогда
		
		ТекущиеДанные = Элементы.МатериалыИУслуги.ТекущиеДанные;
		ТекущиеДанные.ВидЦены = Объект.ВидЦены;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыИУслугиНоменклатураПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.МатериалыИУслуги.ТекущиеДанные;
	
	СтруктураЗаполненияЦены = Новый Структура;
	СтруктураЗаполненияЦены.Вставить("Дата",   Объект.Дата);
	СтруктураЗаполненияЦены.Вставить("Валюта", Объект.Валюта);
	
	СтруктураЗаполненияВидаЦены = Новый Структура;
	СтруктураЗаполненияВидаЦены.Вставить("ВидЦены", Объект.ВидЦены);
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", ТекущиеДанные.Характеристика);
	СтруктураДействий.Вставить("ПроверитьЗаполнитьУпаковкуПоВладельцу", ТекущиеДанные.Упаковка);
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	СтруктураДействий.Вставить("ЗаполнитьВидЦены", СтруктураЗаполненияВидаЦены);
	
	СтруктураДействий.Вставить("ЗаполнитьЦенуПродажи", СтруктураЗаполненияЦены);
	СтруктураДействий.Вставить("ПересчитатьСумму");

	СтруктураДействий.Вставить("НоменклатураПриИзмененииПереопределяемый", Новый Структура("ИмяФормы, ИмяТабличнойЧасти",
		ЭтаФорма.ИмяФормы, "МатериалыИУслуги"));

	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущиеДанные, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыИУслугиКоличествоУпаковокПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.МатериалыИУслуги.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущиеДанные, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыИУслугиУпаковкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.МатериалыИУслуги.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	СтруктураДействий.Вставить("ПересчитатьЦенуЗаУпаковку", ТекущиеДанные.Количество);
	СтруктураДействий.Вставить("ПересчитатьСумму");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущиеДанные, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыИУслугиВидЦеныПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.МатериалыИУслуги.ТекущиеДанные;
	
	СтруктураЗаполненияЦены = Новый Структура;
	СтруктураЗаполненияЦены.Вставить("Дата",   Объект.Дата);
	СтруктураЗаполненияЦены.Вставить("Валюта", Валюта);
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ЗаполнитьЦенуПродажи", СтруктураЗаполненияЦены);
	СтруктураДействий.Вставить("ПересчитатьСумму");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыИУслугиЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.МатериалыИУслуги.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьСумму");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущиеДанные, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыИУслугиСуммаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.МатериалыИУслуги.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьЦенуПоСумме", "КоличествоУпаковок");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущиеДанные, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыИУслугиСтатьяКалькуляцииПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.МатериалыИУслуги.ТекущиеДанные;
	ЗапросИзмененияСпособаРасчета(Неопределено, ТекущиеДанные, НСтр("ru = 'материалов';
																	|en = 'materials '"));
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыИУслугиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	РассчитатьЗависимыеИтогиПоТабличнымЧастям();
	
	ТекущиеДанные = Элементы.МатериалыИУслуги.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(ТекущиеДанные.СтатьяКалькуляции) Тогда
		ОбновитьИтогиПоСтатьеКалькуляции("Материалы", ТекущиеДанные.СтатьяКалькуляции, ТекущиеДанные.Сумма);
	Иначе
		ОбновитьИтогиПоСтатьеКалькуляции("Материалы");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыИУслугиКалькуляцияПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.МатериалыИУслуги.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЦеныПоКалькуляцииНаСервере(ТекущиеДанные.ПолучитьИдентификатор());
	
	Если ЗначениеЗаполнено(ТекущиеДанные.Калькуляция) И ЗначениеЗаполнено(ТекущиеДанные.ВидЦены) Тогда
		ТекущиеДанные.ВидЦены = ПредопределенноеЗначение("Справочник.ВидыЦен.ПустаяСсылка");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыИУслугиПослеУдаления(Элемент)
	
	РассчитатьЗависимыеИтогиПоТабличнымЧастям();
	
	ТекущиеДанные = Элементы.МатериалыИУслуги.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(ТекущиеДанные.СтатьяКалькуляции) Тогда
		ОбновитьИтогиПоСтатьеКалькуляции("Материалы", ТекущиеДанные.СтатьяКалькуляции, ТекущиеДанные.Сумма);
	Иначе
		ОбновитьИтогиПоСтатьеКалькуляции("Материалы");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТрудозатраты

&НаКлиенте
Процедура ТрудозатратыПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.Трудозатраты.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(ТекущиеДанные.СтатьяКалькуляции) Тогда
		ОбновитьИтогиПоСтатьеКалькуляции("Трудозатраты", ТекущиеДанные.СтатьяКалькуляции, ТекущиеДанные.Сумма);
	Иначе
		ОбновитьИтогиПоСтатьеКалькуляции("Трудозатраты");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТрудозатратыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	РассчитатьЗависимыеИтогиПоТабличнымЧастям();
	
	ТекущиеДанные = Элементы.Трудозатраты.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(ТекущиеДанные.СтатьяКалькуляции) Тогда
		ОбновитьИтогиПоСтатьеКалькуляции("Трудозатраты", ТекущиеДанные.СтатьяКалькуляции, ТекущиеДанные.Сумма);
	Иначе
		ОбновитьИтогиПоСтатьеКалькуляции("Трудозатраты");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТрудозатратыВидРаботПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Трудозатраты.ТекущиеДанные;
	Идентификаторы = Новый Массив;
	Идентификаторы.Добавить(ТекущиеДанные.ПолучитьИдентификатор());
	
	РассчитатьТрудозатратыПоРасценкам(Идентификаторы);
	
КонецПроцедуры

&НаКлиенте
Процедура ТрудозатратыКоличествоПриИзменении(Элемент)
	ТекущиеДанные = Элементы.Трудозатраты.ТекущиеДанные;
	ТекущиеДанные.Сумма = ТекущиеДанные.Количество * ТекущиеДанные.Расценка;
КонецПроцедуры

&НаКлиенте
Процедура ТрудозатратыРасценкаПриИзменении(Элемент)
	ТекущиеДанные = Элементы.Трудозатраты.ТекущиеДанные;
	ТекущиеДанные.Сумма = ТекущиеДанные.Количество * ТекущиеДанные.Расценка;
КонецПроцедуры

&НаКлиенте
Процедура ТрудозатратыСуммаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Трудозатраты.ТекущиеДанные;
	
	Если ТекущиеДанные.Расценка > 0 Тогда
		ТекущиеДанные.Количество = ТекущиеДанные.Сумма / ТекущиеДанные.Расценка;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТрудозатратыСтатьяКалькуляцииПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Трудозатраты.ТекущиеДанные;
	ЗапросИзмененияСпособаРасчета(Неопределено, ТекущиеДанные, НСтр("ru = 'трудозатрат';
																	|en = 'labor costs'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ТрудозатратыПослеУдаления(Элемент)
	
	РассчитатьЗависимыеИтогиПоТабличнымЧастям();
	
	ТекущиеДанные = Элементы.Трудозатраты.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(ТекущиеДанные.СтатьяКалькуляции) Тогда
		ОбновитьИтогиПоСтатьеКалькуляции("Трудозатраты", ТекущиеДанные.СтатьяКалькуляции, ТекущиеДанные.Сумма);
	Иначе
		ОбновитьИтогиПоСтатьеКалькуляции("Трудозатраты");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСтатьиРасходов

&НаКлиенте
Процедура СтатьиРасходовСтатьяРасходовПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.СтатьиРасходов.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.СтатьяРасходов) Тогда
		СтатьиРасходовСтатьяРасходовПриИзмененииНаСервере(ТекущиеДанные.ПолучитьИдентификатор());
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьиРасходовПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.СтатьиРасходов.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(ТекущиеДанные.СтатьяКалькуляции) Тогда
		ОбновитьИтогиПоСтатьеКалькуляции("Статьи", ТекущиеДанные.СтатьяКалькуляции, ТекущиеДанные.Сумма);
	Иначе
		ОбновитьИтогиПоСтатьеКалькуляции("Статьи");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьиРасходовПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	РассчитатьЗависимыеИтогиПоТабличнымЧастям();
	ТекущиеДанные = Элементы.СтатьиРасходов.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(ТекущиеДанные.СтатьяКалькуляции) Тогда
		ОбновитьИтогиПоСтатьеКалькуляции("Статьи", ТекущиеДанные.СтатьяКалькуляции, ТекущиеДанные.Сумма);
	Иначе
		ОбновитьИтогиПоСтатьеКалькуляции("Статьи");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьиРасходовФормулаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.СтатьиРасходов.ТекущиеДанные;
	
	АдресВХранилище = БазовыеСтатьиКалькуляцииВХранилище(ТекущиеДанные.СтатьяКалькуляции);
	
	ПараметрыРедактирования = Новый Структура;
	ПараметрыРедактирования.Вставить("Формула", ТекущиеДанные.Формула);
	ПараметрыРедактирования.Вставить("ДеревоОперандов", АдресВХранилище);
	ПараметрыРедактирования.Вставить("ОперандыЗаголовок", НСтр("ru = 'Статьи калькуляции';
																|en = 'Product cost elements'"));
	ПараметрыРедактирования.Вставить("Операторы", АдресХранилищаДереваОператоров);
	ПараметрыРедактирования.Вставить("ТипРезультата", Новый ОписаниеТипов("Число"));
	
	РезультатРедактирования = Неопределено;

	
	ОткрытьФорму("ОбщаяФорма.КонструкторФормул", ПараметрыРедактирования, Элементы.СтатьиКалькуляцииФормула,,,, Новый ОписаниеОповещения("СтатьиРасходовФормулаНачалоВыбораЗавершение", ЭтотОбъект, Новый Структура("ТекущиеДанные", ТекущиеДанные)), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьиРасходовФормулаНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    ТекущиеДанные = ДополнительныеПараметры.ТекущиеДанные;
    
    
    РезультатРедактирования = Результат;
    
    Если РезультатРедактирования <> Неопределено Тогда
        
        ТекущиеДанные.Формула = РезультатРедактирования;
        РассчитатьСуммуСтатьиРасходов(ТекущиеДанные.ПолучитьИдентификатор());
        
    КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СтатьиРасходовСтатьяКалькуляцииПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.СтатьиРасходов.ТекущиеДанные;
	ЗапросИзмененияСпособаРасчета(Новый ОписаниеОповещения("СтатьиРасходовСтатьяКалькуляцииПриИзмененииЗавершение", ЭтотОбъект, Новый Структура("ТекущиеДанные", ТекущиеДанные)), ТекущиеДанные, НСтр("ru = 'статей расходов';
																																																		|en = 'expense items'"));
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьиРасходовСтатьяКалькуляцииПриИзмененииЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    ТекущиеДанные = ДополнительныеПараметры.ТекущиеДанные;
    
    
    Если ЗначениеЗаполнено(ТекущиеДанные.СтатьяКалькуляции) Тогда
        СтатьиРасходовСтатьяКалькуляцииПриИзмененииНаСервере(ТекущиеДанные.ПолучитьИдентификатор());
    КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СтатьиРасходовФормулаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.СтатьиРасходов.ТекущиеДанные;
	
	РассчитатьСуммуСтатьиРасходов(ТекущиеДанные.ПолучитьИдентификатор());
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьиРасходовРасчетПоФормулеПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.СтатьиРасходов.ТекущиеДанные;
	Если Не ТекущиеДанные.РасчетПоФормуле Тогда
		ТекущиеДанные.Формула = "";
		ТекущиеДанные.ФормулаСодержитОшибки = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьиРасходовПослеУдаления(Элемент)
	
	РассчитатьЗависимыеИтогиПоТабличнымЧастям();
	ТекущиеДанные = Элементы.СтатьиРасходов.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(ТекущиеДанные.СтатьяКалькуляции) Тогда
		ОбновитьИтогиПоСтатьеКалькуляции("Статьи", ТекущиеДанные.СтатьяКалькуляции, ТекущиеДанные.Сумма);
	Иначе
		ОбновитьИтогиПоСтатьеКалькуляции("Статьи");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры


&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры


&НаКлиенте
Процедура УстановитьИнтервал(Команда)
	
	Диалог = Новый ДиалогРедактированияСтандартногоПериода();
	
	Диалог.Период.ДатаНачала    = Объект.ДатаНачалаДействия;
	Диалог.Период.ДатаОкончания = Объект.ДатаОкончанияДействия;
	
	Диалог.Показать(Новый ОписаниеОповещения("УстановитьИнтервалЗавершение", ЭтотОбъект, Новый Структура("Диалог", Диалог)));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьИнтервалЗавершение(Период, ДополнительныеПараметры) Экспорт
	
	Диалог = ДополнительныеПараметры.Диалог; // ДиалогРедактированияСтандартногоПериода
	
	Если Период <> Неопределено Тогда
		
		Объект.ДатаНачалаДействия    = Диалог.Период.ДатаНачала;
		Объект.ДатаОкончанияДействия = Диалог.Период.ДатаОкончания;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоРесурснойСпецификации(Команда)
	
	Если Объект.ВозвратныеОтходы.Количество() > 0
		Или Объект.МатериалыИУслуги.Количество() > 0 
		Или Объект.Трудозатраты.Количество() > 0 Тогда



		
		ТекстВопроса = НСтр("ru = 'Таблицы ""Материалы и работы"", ""Возвратные отходы"", ""Трудозатраты"" 
			|документа будут перезаполнены. Продолжить?';
			|en = 'The ""Materials and works"", ""Recyclable waste"", ""Labor costs"" tables will be repopulated.
			|Continue?'");
		
		ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьПоРесурснойСпецификацииЗавершение", ЭтотОбъект), ТекстВопроса, РежимДиалогаВопрос.ДаНет);
        Возврат;
		
	КонецЕсли;
	
	ЗаполнитьПоРесурснойСпецификацииФрагмент();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоРесурснойСпецификацииЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    Ответ = РезультатВопроса;
    
    Если Ответ = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;
    
    
    ЗаполнитьПоРесурснойСпецификацииФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоРесурснойСпецификацииФрагмент()
    
    Перем МассивСпецификаций, ПараметрыФормы, Строка;
    
    ПараметрыФормы = Новый Структура;
    
    Если Объект.ОбъектКалькуляции = ПредопределенноеЗначение("Перечисление.ОбъектыКалькуляции.Изделие")
        И Объект.ДействиеКалькуляции.Количество() > 0 Тогда
        ПараметрыФормы.Вставить("ОтборИзделие", Объект.ДействиеКалькуляции[0].Объект);
        
    ИначеЕсли Объект.ОбъектКалькуляции = ПредопределенноеЗначение("Перечисление.ОбъектыКалькуляции.РесурснаяСпецификация")
        И Объект.ДействиеКалькуляции.Количество() > 0 Тогда
        
        МассивСпецификаций = Новый Массив;
        
        Для Каждого Строка Из Объект.ДействиеКалькуляции Цикл
            МассивСпецификаций.Добавить(Строка.Объект);
        КонецЦикла;
        
        ПараметрыФормы.Вставить("ОтборСписокСпецификаций", Истина);
        ПараметрыФормы.Вставить("МассивСпецификаций", МассивСпецификаций);
        
    ИначеЕсли Объект.ОбъектКалькуляции = ПредопределенноеЗначение("Перечисление.ОбъектыКалькуляции.ЗаказНаПроизводство")
        И Объект.ДействиеКалькуляции.Количество() > 0 Тогда
        
        МассивСпецификаций = СпецификацииДокумента();
        
        ПараметрыФормы.Вставить("ОтборСписокСпецификаций", Истина);
        ПараметрыФормы.Вставить("МассивСпецификаций", МассивСпецификаций);
        
    КонецЕсли;
    
    ОткрытьФорму("Справочник.РесурсныеСпецификации.ФормаВыбора", ПараметрыФормы, ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура МатериалыЗаполнитьЦеныПоВидуЦен(Команда)
	
	Если Объект.МатериалыИУслуги.Количество() = 0 Тогда
		ТекстПредупреждения = НСтр("ru = 'В документе не заполнена таблица %ПредставлениеТабличнойЧасти%. Цены не могут быть заполнены';
									|en = 'Table %ПредставлениеТабличнойЧасти% in the document is not filled in. Cannot fill in the prices'");
		ТекстПредупреждения = СтрЗаменить(ТекстПредупреждения, "%ПредставлениеТабличнойЧасти%", НСтр("ru = 'Материалы и работы';
																									|en = 'Materials and works'"));
		ПоказатьПредупреждение(Неопределено, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	СтруктураОтбора = Новый Структура("ЦенаВключаетНДС", Истина);
	СтруктураОтбора.Вставить("Валюта", Объект.Валюта);
	
	ВидЦен = Неопределено;

	
	ОткрытьФорму("Справочник.ВидыЦен.ФормаВыбора", СтруктураОтбора,,,,, Новый ОписаниеОповещения("МатериалыЗаполнитьЦеныПоВидуЦенЗавершение", ЭтотОбъект), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыЗаполнитьЦеныПоВидуЦенЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    ВидЦен = Результат;
    
    Если ЗначениеЗаполнено(ВидЦен) Тогда
        
        ЦеныРассчитаны = ЗаполнитьЦеныВыделенныхСтрокПоВидуЦенСервер("МатериалыИУслуги", ВидЦен);
        ПродажиКлиент.ОповеститьОбОкончанииЗаполненияЦенПоВидуЦен(ЦеныРассчитаны, ВидЦен);
        
    КонецЕсли;
    
	РассчитатьЗависимыеИтогиПоТабличнымЧастям();
    ОбновитьИтогиТекущейТаблицы();

КонецПроцедуры

&НаКлиенте
Процедура ОтходыЗаполнитьЦеныПоВидуЦен(Команда)
	
	Если Объект.ВозвратныеОтходы.Количество() = 0 Тогда
		ТекстПредупреждения = НСтр("ru = 'В документе не заполнена таблица %ПредставлениеТабличнойЧасти%. Цены не могут быть заполнены';
									|en = 'Table %ПредставлениеТабличнойЧасти% in the document is not filled in. Cannot fill in the prices'");
		ТекстПредупреждения = СтрЗаменить(ТекстПредупреждения, "%ПредставлениеТабличнойЧасти%", НСтр("ru = 'Возвратные отходы';
																									|en = 'Recyclable waste'"));
		ПоказатьПредупреждение(Неопределено, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	СтруктураОтбора = Новый Структура("ЦенаВключаетНДС", Истина);
	СтруктураОтбора.Вставить("Валюта", Объект.Валюта);
	
	ВидЦен = Неопределено;

	
	ОткрытьФорму("Справочник.ВидыЦен.ФормаВыбора", СтруктураОтбора,,,,, Новый ОписаниеОповещения("ОтходыЗаполнитьЦеныПоВидуЦенЗавершение", ЭтотОбъект), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтходыЗаполнитьЦеныПоВидуЦенЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ВидЦен = Результат;
	
	Если ЗначениеЗаполнено(ВидЦен) Тогда
		ЦеныРассчитаны = ЗаполнитьЦеныВыделенныхСтрокПоВидуЦенСервер("ВозвратныеОтходы", ВидЦен);
		ПродажиКлиент.ОповеститьОбОкончанииЗаполненияЦенПоВидуЦен(ЦеныРассчитаны, ВидЦен);
	КонецЕсли;
	
	РассчитатьЗависимыеИтогиПоТабличнымЧастям();
	ОбновитьИтогиТекущейТаблицы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиДокумент(Команда)
	
	ОбщегоНазначенияУТКлиент.Провести(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиИЗакрыть(Команда)
	
	ОбщегоНазначенияУТКлиент.ПровестиИЗакрыть(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ТрудозатратыЗаполнитьРасценки(Команда)
	
	Если Объект.Трудозатраты.Количество() = 0 Тогда
		ТекстПредупреждения = НСтр("ru = 'В документе не заполнена таблица %ПредставлениеТабличнойЧасти%. Расценки не могут быть заполнены';
									|en = 'Table %ПредставлениеТабличнойЧасти% is not filled in the document. Cannot fill in the rates'");
		ТекстПредупреждения = СтрЗаменить(ТекстПредупреждения, "%ПредставлениеТабличнойЧасти%", "Трудозатраты");
		ПоказатьПредупреждение(Неопределено, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	Идентификаторы = Новый Массив;
	Для Каждого Строка Из Элементы.Трудозатраты.ВыделенныеСтроки Цикл
		Идентификаторы.Добавить(Строка);
	КонецЦикла;
	
	РассчитатьТрудозатратыПоРасценкам(Идентификаторы);
	
	ПоказатьОповещениеПользователя(
	НСтр("ru = 'Расценки заполнены';
		|en = 'Rates are filled in'"),
	,
	НСтр("ru = 'Текущие расценки на виды работ заполнены';
		|en = 'Current rates of activity kinds are filled in'"),
	БиблиотекаКартинок.Информация32);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоСпецификацииЗаказа(Команда)
	
	Если Объект.ДействиеКалькуляции.Количество() = 0 Тогда
		ПоказатьПредупреждение(Неопределено, НСтр("ru = 'Не выбрана позиция заказа';
													|en = 'Order item is not selected'"));
		Возврат;
	КонецЕсли;
	
	Если Объект.ВозвратныеОтходы.Количество() > 0
		Или Объект.МатериалыИУслуги.Количество() > 0 
		Или Объект.Трудозатраты.Количество() > 0 Тогда
		
		ТекстВопроса = НСтр("ru = 'Таблицы ""Материалы и работы"", ""Возвратные отходы"", ""Трудозатраты"" 
			|документа будут перезаполнены. Продолжить?';
			|en = 'The ""Materials and works"", ""Recyclable waste"", ""Labor costs"" tables will be repopulated.
			|Continue?'");
		
		ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьПоСпецификацииЗаказаЗавершение", ЭтотОбъект), ТекстВопроса, РежимДиалогаВопрос.ДаНет);
        Возврат;
		
	КонецЕсли;
	
	ЗаполнитьПоСпецификацииЗаказаФрагмент();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоСпецификацииЗаказаЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    Ответ = РезультатВопроса;
    
    Если Ответ = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;
    
    
    ЗаполнитьПоСпецификацииЗаказаФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоСпецификацииЗаказаФрагмент()
    
    Перем ДанныеПозиции, ПараметрыФормы;
    
    Если Объект.ДействиеКалькуляции.Количество() = 1 Тогда
        ДанныеПозиции = Объект.ДействиеКалькуляции[0];
        ЗаполнитьПоСпецификацииЗаказаНаСервере(ДанныеПозиции.Объект, ДанныеПозиции.КодСтрокиЗаказаНаПроизводство);
    ИначеЕсли Объект.ДействиеКалькуляции.Количество() > 1 Тогда
        
        ПараметрыФормы = Новый Структура;
        
        ПараметрыФормы.Вставить("Действие", "ЗаполнениеПоПозиции");
        ПараметрыФормы.Вставить("РежимВыбора", Истина);
        ПараметрыФормы.Вставить("АдресВХранилище", ДействиеКалькуляцииВХранилище());
        ПараметрыФормы.Вставить("КалькуляционнаяЕдиница", Объект.КалькуляционнаяЕдиница);
        ПараметрыФормы.Вставить("ОбъектКалькуляции", Объект.ОбъектКалькуляции);
        ПараметрыФормы.Вставить("Документ", Объект.Ссылка);
        
        ОткрытьФорму("Документ.ПлановаяКалькуляция.Форма.ФормаВводаОбластиДействия", ПараметрыФормы, ЭтаФорма);
        
    КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура ЗаписатьДокумент(Команда)
	
	ОбщегоНазначенияУТКлиент.Записать(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПлановыеКалькуляции(Команда)
	
	ОчиститьСообщения();
	
	ВыделенныеСтроки = Элементы.МатериалыИУслуги.ВыделенныеСтроки;
	
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		
		ТекстСообщения = НСтр("ru = 'В таблице ""Материалы и работы"" не выбрано ни одной строки. Калькуляции и цены не могут быть заполнены';
								|en = 'No row is selected in the ""Materials and works"" table. Costings and prices cannot be filled in'");
		ПоказатьПредупреждение( , ТекстСообщения);
		Возврат;
		
	КонецЕсли;
	
	ЗаполнитьПлановыеКалькуляцииИЦеныНаСервере(ВыделенныеСтроки);
	
	ОбновитьИтогиТекущейТаблицы();
	
КонецПроцедуры

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

// ИнтеграцияС1СДокументооборотом
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(Команда, ЭтаФорма, Объект);
	
КонецПроцедуры
// Конец ИнтеграцияС1СДокументооборотом

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

// СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства

#Область Инициализация

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	// ввод формулы доступен только для способа расчета По формуле
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СтатьиКалькуляцииФормула.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.СтатьиКалькуляции.СпособРасчета");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.СпособыРасчетаЗатратПлановойКалькуляции.ПоФормуле;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаНеактуальногоСписка);
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<не требуется>';
																|en = '<not required>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	// характеристика для материалов
	
	НоменклатураСервер.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(ЭтаФорма, 
																			 "МатериалыИУслугиХарактеристика",
																		     "Объект.МатериалыИУслуги.ХарактеристикиИспользуются");
																			 
	НоменклатураСервер.УстановитьУсловноеОформлениеЕдиницИзмерения(ЭтаФорма, 
																	"МатериалыИУслугиНоменклатураЕдиницаИзмерения",
																	"Объект.МатериалыИУслуги.Упаковка");
	
	// любая характеристика для материалов, если материал указан
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.МатериалыИУслугиХарактеристика.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.МатериалыИУслуги.ХарактеристикиИспользуются");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.МатериалыИУслуги.Номенклатура");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.МатериалыИУслуги.Характеристика");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийТекст);
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<любая характеристика>';
																|en = '<any variant>'"));
	
	// любая характеристика для материалов, если материал не указан
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.МатериалыИУслугиХарактеристика.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.МатериалыИУслуги.Номенклатура");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийТекст);
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<любая характеристика>';
																|en = '<any variant>'"));
	
	// любой материал
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.МатериалыИУслугиНоменклатура.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.МатериалыИУслуги.Номенклатура");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийТекст);
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<любая номенклатура>';
																|en = '<any items>'"));
	
	// характеристика для отходов
	
	НоменклатураСервер.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(ЭтаФорма, 
																			 "ВозвратныеОтходыХарактеристика",
																		     "Объект.ВозвратныеОтходы.ХарактеристикиИспользуются");
																			 
	НоменклатураСервер.УстановитьУсловноеОформлениеЕдиницИзмерения(ЭтаФорма, 
																		 	"ВозвратныеОтходыНоменклатураЕдиницаИзмерения",
																	     	"Объект.ВозвратныеОтходы.Упаковка");

	// любая характеристика для отхода, если отход указан
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ВозвратныеОтходыХарактеристика.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ВозвратныеОтходы.ХарактеристикиИспользуются");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ВозвратныеОтходы.Номенклатура");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ВозвратныеОтходы.Характеристика");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийТекст);
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<любая характеристика>';
																|en = '<any variant>'"));
	
	// любая характеристика для отхода, если отход не указан
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ВозвратныеОтходыХарактеристика.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ВозвратныеОтходы.Номенклатура");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийТекст);
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<любая характеристика>';
																|en = '<any variant>'"));
	
	// любой материал
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ВозвратныеОтходыНоменклатура.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ВозвратныеОтходы.Номенклатура");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийТекст);
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<любая номенклатура>';
																|en = '<any items>'"));
	
	// формула доступна, если установлен признак расчета по формуле
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СтатьиРасходовФормула.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.СтатьиРасходов.РасчетПоФормуле");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	// сумма доступна, если не установлен признак расчета по формуле
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СтатьиРасходовСумма.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.СтатьиРасходов.РасчетПоФормуле");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	// любой вид работ
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТрудозатратыВидРабот.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Трудозатраты.ВидРабот");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийТекст);
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<любой вид работ>';
																|en = '<any activity kind>'"));
	
	// ошибки в формулах выделяются красным (СтатьиКалькуляции)
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СтатьиКалькуляцииФормула.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.СтатьиКалькуляции.ФормулаСодержитОшибки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.SpecialTextColor);
	
	// ошибки в формулах выделяются красным (СтатьиРасходов)
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СтатьиРасходовФормула.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.СтатьиРасходов.ФормулаСодержитОшибки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.SpecialTextColor);

	// Сумма по статьям не требует заполнения, если собирается по детальным записям или формуле.
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СтатьиКалькуляцииСумма.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.СтатьиКалькуляции.СпособРасчета");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
	СписокЗначений = Новый СписокЗначений;
	СписокЗначений.Добавить(Перечисления.СпособыРасчетаЗатратПлановойКалькуляции.ПоДетальнымЗаписям);
	СписокЗначений.Добавить(Перечисления.СпособыРасчетаЗатратПлановойКалькуляции.ПоФормуле);
	ОтборЭлемента.ПравоеЗначение = СписокЗначений;

	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	// произвольный вид цены для возвратных отходов
	
	ЦеныПредприятияЗаполнениеСервер.УстановитьУсловноеОформлениеВидовЦен(ЭтаФорма, "ВозвратныеОтходыВидЦены", "Объект.ВозвратныеОтходы.ВидЦены");
	
	// произвольный вид цены для материалов
	
	ЦеныПредприятияЗаполнениеСервер.УстановитьУсловноеОформлениеВидовЦен(ЭтаФорма, "МатериалыИУслугиВидЦены", "Объект.МатериалыИУслуги.ВидЦены");
	
	// цена и сумма только для просмотра, если указан вид цены (МатериалыИУслуги)
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.МатериалыИУслугиЦена.Имя);
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.МатериалыИУслугиСумма.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.МатериалыИУслуги.ВидЦены");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	// цена и сумма только для просмотра, если указан вид цены (ВозвратныеОтходы)
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ВозвратныеОтходыЦена.Имя);
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ВозвратныеОтходыСумма.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ВозвратныеОтходы.ВидЦены");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	// вид цены только для просмотра, если выбрана плановая калькуляция
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.МатериалыИУслугиВидЦены.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.МатериалыИУслуги.Калькуляция");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	
	НастроитьПараметрыВыбораВидаЦен();
	
	ЗаполнитьСлужебныеРеквизитыФормы();
	
	ЗаполнитьРеквизитыПредставленияОбластиДействия();
	
	ЗаполнитьСлужебныеРеквизитыТабличныхЧастей();
	
	УстановитьВидимостьПоПризнакуДетализации();
	
	УстановитьВидимостьПоОбъектуКалькуляции();
	
	Элементы.ГруппаОрганизация.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций");
	
	Элементы.Организация.Доступность = Объект.ИспользоватьОрганизацию;
	
	Если Не Объект.ИспользоватьОрганизацию Тогда
		Элементы.Организация.ПодсказкаВвода = НСтр("ru = 'Любая';
													|en = 'Any'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСлужебныеРеквизитыФормы()
	
	Валюта = Объект.Валюта;
	
	ЗаполнитьБазовыеСтатьиКалькуляции();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыПредставленияОбластиДействия(ОчиститьСлужебныеРеквизиты = Ложь)
	
	Если ОчиститьСлужебныеРеквизиты Тогда
		ОчиститьСлужебныеРеквизиты();
	КонецЕсли;
	
	Если Объект.ДействиеКалькуляции.Количество() > 1 Тогда
		НесколькоОбъектов = Истина;
	КонецЕсли;
	
	Документы.ПлановаяКалькуляция.ЗаполнитьПредставленияОбъектовКалькуляции(
		Объект.ДействиеКалькуляции,
		Объект.ОбъектКалькуляции,
		НесколькоОбъектов,
		ПредставлениеОбщее,
		Элементы.НесколькоОбъектов.Заголовок);
	
	Если Объект.ДействиеКалькуляции.Количество() = 1 Тогда
		
		Строка = Объект.ДействиеКалькуляции[0];
		Если Объект.ОбъектКалькуляции = Перечисления.ОбъектыКалькуляции.Изделие Тогда
			Номенклатура = Строка.Объект;
			Характеристика = Строка.Характеристика;
			
			СтруктураДействий = Новый Структура;
			СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", Характеристика);
			
			СтруктураСтроки = Новый Структура;
			СтруктураСтроки.Вставить("Номенклатура",	Номенклатура);
			СтруктураСтроки.Вставить("Характеристика",	Характеристика);
			СтруктураСтроки.Вставить("ХарактеристикиИспользуются", ХарактеристикиИспользуются);
			
			ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(СтруктураСтроки, СтруктураДействий, Неопределено);
			
			ХарактеристикиИспользуются = СтруктураСтроки.ХарактеристикиИспользуются;
			Характеристика = СтруктураСтроки.Характеристика;
			УказатьХарактеристику = ЗначениеЗаполнено(Характеристика);
			
		ИначеЕсли Объект.ОбъектКалькуляции = Перечисления.ОбъектыКалькуляции.РесурснаяСпецификация Тогда
			Спецификация = Строка.Объект;
			СведенияОбИзделиях = Строка.СведенияОбИзделиях;
		Иначе
			ПредставлениеПозицииЗаказа = Строка.ПредставлениеПозицииЗаказа;
			ПредставлениеПозицииЗаказаИсходное = Строка.ПредставлениеПозицииЗаказа;
			ЗаказНаПроизводство = Строка.Объект;
			КодСтрокиЗаказаНаПроизводство = Строка.КодСтрокиЗаказаНаПроизводство;
			СведенияОбИзделиях = Строка.СведенияОбИзделиях;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Объект.ОбъектКалькуляции = Перечисления.ОбъектыКалькуляции.Изделие И Объект.ДействиеКалькуляции.Количество() > 0 Тогда
		ЕдиницаИзмерения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ДействиеКалькуляции[0].Объект, "ЕдиницаИзмерения");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСлужебныеРеквизитыТабличныхЧастей(ИмяТабличнойЧасти = Неопределено, Идентификатор = Неопределено)
	
	Если ИмяТабличнойЧасти = Неопределено Или ИмяТабличнойЧасти = "СтатьиКалькуляции" Тогда
		
		ШаблонПредставленияЗаголовок = НСтр("ru = 'По статье калькуляции ""%1"" указано позиций';
											|en = 'Positions specified for costing item ""%1""'");
		ШаблонПредставленияОтходы = НСтр("ru = 'отходов: %1';
										|en = 'waste: %1'");
		ШаблонПредставленияМатериалы = НСтр("ru = 'материалов: %1';
											|en = 'materials: %1 '");
		ШаблонПредставленияТрудозатраты = НСтр("ru = 'трудозатрат: %1';
												|en = 'labor costs: %1'");
		ШаблонПредставленияСтатьи = НСтр("ru = 'постатейных расходов: %1';
										|en = 'itemized expenses: %1'");
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СтатьиКалькуляции.СтатьяКалькуляции КАК СтатьяКалькуляции
		|ПОМЕСТИТЬ ВТСтатьиКалькуляции
		|ИЗ
		|	&СтатьиКалькуляции КАК СтатьиКалькуляции
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Отходы.СтатьяКалькуляции,
		|	Отходы.НомерСтроки
		|ПОМЕСТИТЬ ВТОтходы
		|ИЗ
		|	&Отходы КАК Отходы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Материалы.СтатьяКалькуляции,
		|	Материалы.НомерСтроки
		|ПОМЕСТИТЬ ВТМатериалы
		|ИЗ
		|	&Материалы КАК Материалы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Трудозатраты.СтатьяКалькуляции,
		|	Трудозатраты.НомерСтроки
		|ПОМЕСТИТЬ ВТТрудозатраты
		|ИЗ
		|	&Трудозатраты КАК Трудозатраты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Статьи.СтатьяКалькуляции,
		|	Статьи.НомерСтроки
		|ПОМЕСТИТЬ ВТСтатьи
		|ИЗ
		|	&Статьи КАК Статьи
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТСтатьиКалькуляции.СтатьяКалькуляции,
		|	КОЛИЧЕСТВО(Различные ВТМатериалы.НомерСтроки) КАК Материалы,
		|	КОЛИЧЕСТВО(Различные ВТОтходы.НомерСтроки) КАК Отходы,
		|	КОЛИЧЕСТВО(Различные ВТСтатьи.НомерСтроки) КАК Статьи,
		|	КОЛИЧЕСТВО(Различные ВТТрудозатраты.НомерСтроки) КАК Трудозатраты,
		|	ВТСтатьиКалькуляции.СтатьяКалькуляции.ИдентификаторДляФормул КАК Идентификатор
		|ИЗ
		|	ВТСтатьиКалькуляции КАК ВТСтатьиКалькуляции
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОтходы КАК ВТОтходы
		|		ПО ВТСтатьиКалькуляции.СтатьяКалькуляции = ВТОтходы.СтатьяКалькуляции
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТМатериалы КАК ВТМатериалы
		|		ПО ВТСтатьиКалькуляции.СтатьяКалькуляции = ВТМатериалы.СтатьяКалькуляции
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТрудозатраты КАК ВТТрудозатраты
		|		ПО ВТСтатьиКалькуляции.СтатьяКалькуляции = ВТТрудозатраты.СтатьяКалькуляции
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатьи КАК ВТСтатьи
		|		ПО ВТСтатьиКалькуляции.СтатьяКалькуляции = ВТСтатьи.СтатьяКалькуляции
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТСтатьиКалькуляции.СтатьяКалькуляции,
		|	ВТСтатьиКалькуляции.СтатьяКалькуляции.ИдентификаторДляФормул");
		
		Запрос.УстановитьПараметр("СтатьиКалькуляции", Объект.СтатьиКалькуляции.Выгрузить());
		Запрос.УстановитьПараметр("Отходы", Объект.ВозвратныеОтходы.Выгрузить());
		Запрос.УстановитьПараметр("Материалы", Объект.МатериалыИУслуги.Выгрузить());
		Запрос.УстановитьПараметр("Трудозатраты", Объект.Трудозатраты.Выгрузить());
		Запрос.УстановитьПараметр("Статьи", Объект.СтатьиРасходов.Выгрузить());
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Для Каждого Строка Из Объект.СтатьиКалькуляции Цикл
			
			Если Идентификатор <> Неопределено И Строка.ПолучитьИдентификатор() <> Идентификатор Тогда
				Продолжить;
			КонецЕсли;
			
			Выборка.НайтиСледующий(Строка.СтатьяКалькуляции);
			Строка.Идентификатор = Выборка.Идентификатор;
			
			Если Строка.СпособРасчета <> Перечисления.СпособыРасчетаЗатратПлановойКалькуляции.ПоДетальнымЗаписям Тогда
				Строка.ПредставлениеДетальныхЗаписей = "";
			Иначе
				
				Если Выборка.Отходы + Выборка.Материалы + Выборка.Трудозатраты + Выборка.Статьи = 0 Тогда
					Строка.ПредставлениеДетальныхЗаписей = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Укажите отходы, материалы, виды работ или статьи расходов для статьи калькуляции ""%1"".';
							|en = 'Specify waste, materials, activity kinds or expense items for the ""%1"" costing item.'"),
						СокрЛП(Выборка.СтатьяКалькуляции));
				Иначе
					
					Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						ШаблонПредставленияЗаголовок,
						СокрЛП(Выборка.СтатьяКалькуляции));
					
					Разделитель = " ";
					
					Если Выборка.Отходы > 0 Тогда
						Представление = Представление + Разделитель + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							ШаблонПредставленияОтходы,
							Выборка.Отходы);
						Разделитель = ", ";
					КонецЕсли;
					
					Если Выборка.Материалы > 0 Тогда
						Представление = Представление + Разделитель + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							ШаблонПредставленияМатериалы,
							Выборка.Материалы);
						Разделитель = ", ";
					КонецЕсли;
					
					Если Выборка.Трудозатраты > 0 Тогда
						Представление = Представление + Разделитель + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							ШаблонПредставленияТрудозатраты,
							Выборка.Трудозатраты);
						Разделитель = ", ";
					КонецЕсли;
					
					Если Выборка.Статьи > 0 Тогда
						Представление = Представление + Разделитель + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							ШаблонПредставленияСтатьи,
							Выборка.Статьи);
						Разделитель = ", ";
					КонецЕсли;
					
					Строка.ПредставлениеДетальныхЗаписей = Представление;
					
				КонецЕсли;
				
			КонецЕсли;
			
			Выборка.Сбросить();
			
		КонецЦикла;
		
	КонецЕсли;
	
	СтруктураХарактеристики  = Новый Структура("Номенклатура", "ХарактеристикиИспользуются");
	
	ВыполняемыеДействия = Новый Структура();
	ВыполняемыеДействия.Вставить("ЗаполнитьПризнакХарактеристикиИспользуются", СтруктураХарактеристики);
	
	НоменклатураСервер.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВКоллекции(
		Объект.ВозвратныеОтходы, ВыполняемыеДействия);
	НоменклатураСервер.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВКоллекции(
		Объект.МатериалыИУслуги, ВыполняемыеДействия);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьПоПризнакуДетализации()
	
	Перем МассивВсехРеквизитов;
	Перем МассивРеквизитовДокумента;
	
	Документы.ПлановаяКалькуляция.ИменаРеквизитовПоПризнакуДетализации(
		Объект.ДетализироватьСтатьиКалькуляции,
		МассивВсехРеквизитов,
		МассивРеквизитовДокумента);
	
	ДенежныеСредстваСервер.УстановитьВидимостьЭлементовПоМассиву(
		Элементы,
		МассивВсехРеквизитов,
		МассивРеквизитовДокумента);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьПоОбъектуКалькуляции()
	
	Если Объект.ОбъектКалькуляции = Перечисления.ОбъектыКалькуляции.Изделие Тогда
		Элементы.СтраницыОбластьДействия.ТекущаяСтраница = Элементы.СтраницыОбластьДействия.ПодчиненныеЭлементы.СтраницаИзделие;
		Элементы.УказатьХарактеристику.Доступность = ХарактеристикиИспользуются;
		Элементы.Характеристика.Доступность = ХарактеристикиИспользуются И УказатьХарактеристику;
		
		Если ХарактеристикиИспользуются И Не УказатьХарактеристику Тогда
			Элементы.Характеристика.ПодсказкаВвода = НСтр("ru = 'Любая';
															|en = 'Any'");
		Иначе
			Элементы.Характеристика.ПодсказкаВвода = "";
		КонецЕсли;
		
	ИначеЕсли Объект.ОбъектКалькуляции = Перечисления.ОбъектыКалькуляции.РесурснаяСпецификация Тогда
		Элементы.СтраницыОбластьДействия.ТекущаяСтраница = Элементы.СтраницыОбластьДействия.ПодчиненныеЭлементы.СтраницаСпецификация;
	ИначеЕсли Объект.ОбъектКалькуляции = Перечисления.ОбъектыКалькуляции.ЗаказНаПроизводство Тогда
		Элементы.СтраницыОбластьДействия.ТекущаяСтраница = Элементы.СтраницыОбластьДействия.ПодчиненныеЭлементы.СтраницаЗаказНаПроизводство;
	КонецЕсли;
	
	Элементы.СтраницыОбластьДействия.Видимость = Не НесколькоОбъектов;
	Элементы.ГруппаКалькуляционнаяЕдиница.Видимость = Объект.ОбъектКалькуляции = Перечисления.ОбъектыКалькуляции.Изделие;
	
	НастроитьКомандуЗаполненияПоСпецификацииЗаказа();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьКомандуЗаполненияПоСпецификацииЗаказа();
	
	КнопкаФормы = Элементы.Найти("ЗаполнитьПоСпецификацииЗаказа");
	Элемент = Элементы.Найти("ФормаЗаполнитьПоРесурснойСпецификации");
	
	Если Объект.ОбъектКалькуляции = Перечисления.ОбъектыКалькуляции.ЗаказНаПроизводство Тогда
		
		Если КнопкаФормы = Неопределено Тогда
			КнопкаФормы = Элементы.Вставить("ЗаполнитьПоСпецификацииЗаказа", Тип("КнопкаФормы"), Элементы.ФормаКоманднаяПанель, КнопкаФормы);
			КнопкаФормы.ИмяКоманды = "ЗаполнитьПоСпецификацииЗаказа";
		Иначе
			КнопкаФормы.Видимость = Истина;
		КонецЕсли;
		
	Иначе
		Если КнопкаФормы <> Неопределено Тогда
			КнопкаФормы.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ЗаполнениеПоСпецификации

&НаСервере
Процедура ЗаполнитьДокументПоСпецификации(ВыбранноеЗначение, КэшированныеЗначения)
	
	Документы.ПлановаяКалькуляция.ЗаполнитьПоСпецификации(Объект, ВыбранноеЗначение);
	
	РассчитатьТаблицуСтатейКалькуляции();
	ЗаполнитьРеквизитыПредставленияОбластиДействия();
	ЗаполнитьСлужебныеРеквизитыТабличныхЧастей("СтатьиКалькуляции");
	
КонецПроцедуры

&НаСервере
Функция СпецификацииДокумента()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДействиеКалькуляции.Объект КАК Объект,
	|	ДействиеКалькуляции.КодСтрокиЗаказаНаПроизводство КАК КодСтроки
	|ПОМЕСТИТЬ ВТЗаказы
	|ИЗ
	|	&ДействиеКалькуляции КАК ДействиеКалькуляции
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Продукция.Спецификация КАК Спецификация
	|ИЗ
	|	ВТЗаказы КАК ВТЗаказы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.Продукция КАК Продукция
	|		ПО ВТЗаказы.Объект = Продукция.Ссылка
	|			И ВТЗаказы.КодСтроки = Продукция.КодСтроки");
	
	Запрос.УстановитьПараметр("ДействиеКалькуляции", Объект.ДействиеКалькуляции.Выгрузить());
	Выборка = Запрос.Выполнить().Выбрать();
	Результат = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		Результат.Добавить(Выборка.Спецификация);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьПоСпецификацииЗаказаНаСервере(ЗаказНаПроизводство, КодСтроки);
	
	Документы.ПлановаяКалькуляция.ЗаполнитьПоСпецификацииЗаказа(Объект, ЗаказНаПроизводство, КодСтроки);
	
	РассчитатьТаблицуСтатейКалькуляции();
	ЗаполнитьРеквизитыПредставленияОбластиДействия();
	ЗаполнитьСлужебныеРеквизитыТабличныхЧастей("СтатьиКалькуляции");
	
КонецПроцедуры

#КонецОбласти

#Область ЗаполнениеКалькуляцийЦенРасценок

&НаСервере
Функция ЗаполнитьЦеныВыделенныхСтрокПоВидуЦенСервер(ИмяТабличнойЧасти, ВидЦен)
	
	МассивСтрок = Новый Массив;
	Для Каждого Строка Из Элементы[ИмяТабличнойЧасти].ВыделенныеСтроки Цикл
		
		ДанныеСтроки = Объект[ИмяТабличнойЧасти].НайтиПоИдентификатору(Строка);
		
		Если ИмяТабличнойЧасти = "МатериалыИУслуги" И ЗначениеЗаполнено(ДанныеСтроки.Калькуляция) Тогда
			Продолжить;
		КонецЕсли;
		
		МассивСтрок.Добавить(ДанныеСтроки);
		
	КонецЦикла;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьСумму");
	
	СтруктураЗаполненияЦен = Новый Структура();
	СтруктураЗаполненияЦен.Вставить("Дата",           Объект.Дата);
	СтруктураЗаполненияЦен.Вставить("Организация",    Объект.Организация);
	СтруктураЗаполненияЦен.Вставить("Валюта",         Валюта);
	СтруктураЗаполненияЦен.Вставить("ВидЦены",        ВидЦен);
	СтруктураЗаполненияЦен.Вставить("ПоляЗаполнения", "Цена, ВидЦены");
	
	ЦеныРассчитаны = ЦеныПредприятияЗаполнениеСервер.ЗаполнитьЦены(
		Объект[ИмяТабличнойЧасти],
		МассивСтрок, // Массив строк или структура отбора
		СтруктураЗаполненияЦен,
		СтруктураДействий);
	
	Возврат ЦеныРассчитаны;
	
КонецФункции

&НаСервере
Процедура РассчитатьТрудозатратыПоРасценкам(Идентификаторы);
	Документы.ПлановаяКалькуляция.РассчитатьТрудозатратыПоРасценкам(Объект, Идентификаторы);
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПлановыеКалькуляцииИЦеныНаСервере(Знач Строки = Неопределено)
	
	Если Строки <> Неопределено Тогда
		МассивСтрок = Новый Массив;
		Для Каждого Строка Из Строки Цикл
			МассивСтрок.Добавить(Объект.МатериалыИУслуги.НайтиПоИдентификатору(Строка));
		КонецЦикла;
		Строки = МассивСтрок;
	КонецЕсли;
	
	ПараметрыКоллекции = Новый Структура();
	ПараметрыКоллекции.Вставить("ОчищатьВидЦены");
	
	Документы.ПлановаяКалькуляция.ЗаполнитьКалькуляцииВКоллекции(
		Объект.МатериалыИУслуги,
		Строки,
		ПараметрыКоллекции,
		Объект.Организация,
		Объект.Дата);
		
	ЗаполнитьЦеныПоКалькуляцииНаСервере(Строки);
	
	РассчитатьЗависимыеИтогиПоТабличнымЧастям();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЦеныПоКалькуляцииНаСервере(Знач Строки = Неопределено)
	
	Если Строки = Неопределено Тогда
		Строки = Объект.МатериалыИУслуги;
	ИначеЕсли ТипЗнч(Строки) = Тип("Число") Тогда
		МассивСтрок = Новый Массив;
		МассивСтрок.Добавить(Объект.МатериалыИУслуги.НайтиПоИдентификатору(Строки));
		Строки = МассивСтрок;
	Иначе
		Возврат;
	КонецЕсли;
	
	ПараметрыКоллекции = Новый Структура();
	ПараметрыКоллекции.Вставить("ОчищатьВидЦены");
	
	МассивКалькуляций = Новый Массив;
	Для Каждого Строка Из Строки Цикл
		МассивКалькуляций.Добавить(Строка.Калькуляция);
	КонецЦикла;
	
	СуммыКалькуляций = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(МассивКалькуляций, "СуммаПоДокументу");
	
	Для Каждого Строка Из Строки Цикл
		
		Если Не ЗначениеЗаполнено(Строка.Калькуляция) И ЗначениеЗаполнено(Строка.ВидЦены) Тогда
			Продолжить;
		КонецЕсли;
		
		Строка.Цена     = СуммыКалькуляций[Строка.Калькуляция];
		Строка.Сумма    = Строка.Цена * Строка.Количество;
		Строка.ВидЦены  = Справочники.ВидыЦен.ПустаяСсылка();
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиРеквизитовШапки

&НаСервере
Процедура ОбъектКалькуляцииПриИзмененииНаСервере()
	
	Объект.ДействиеКалькуляции.Очистить();
	
	ОчиститьСлужебныеРеквизиты();
	
	Документы.ПлановаяКалькуляция.ЗаполнитьПредставленияОбъектовКалькуляции(
		Объект.ДействиеКалькуляции,
		Объект.ОбъектКалькуляции,
		НесколькоОбъектов,
		,
		Элементы.НесколькоОбъектов.Заголовок);
	
	УстановитьВидимостьПоОбъектуКалькуляции();
	
КонецПроцедуры

&НаСервере
Процедура НесколькоОбъектовПриИзмененииНаСервере()
	
	Если Не НесколькоОбъектов Тогда
		
		МассивУдаляемыхСтрок = Новый Массив;
		Для Каждого Строка Из Объект.ДействиеКалькуляции Цикл
			
			Если Строка.НомерСтроки = 1 Тогда
				Продолжить;
			КонецЕсли;
			МассивУдаляемыхСтрок.Добавить(Строка);
			
		КонецЦикла;
		
		Для Каждого Строка Из МассивУдаляемыхСтрок Цикл
			Объект.ДействиеКалькуляции.Удалить(Строка);
		КонецЦикла;
		
	КонецЕсли;
	
	ЗаполнитьРеквизитыПредставленияОбластиДействия();
	УстановитьВидимостьПоОбъектуКалькуляции();
	
КонецПроцедуры

&НаСервере
Процедура НоменклатураПриИзмененииНаСервере()
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", Характеристика);
	
	СтруктураСтроки = Новый Структура;
	СтруктураСтроки.Вставить("Номенклатура",	Номенклатура);
	СтруктураСтроки.Вставить("Характеристика",	Характеристика);
	СтруктураСтроки.Вставить("ХарактеристикиИспользуются", ХарактеристикиИспользуются);

	СтруктураДействий.Вставить("НоменклатураПриИзмененииПереопределяемый", Новый Структура("ИмяФормы, ИмяТабличнойЧасти",
		ЭтаФорма.ИмяФормы, "Объект"));
		
	ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(СтруктураСтроки, СтруктураДействий, Неопределено);
	
	ЗаполнитьЗначенияСвойств(Объект, СтруктураСтроки);
	ХарактеристикиИспользуются = СтруктураСтроки.ХарактеристикиИспользуются;
	ЕдиницаИзмерения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ЕдиницаИзмерения");
	
	Элементы.Характеристика.Доступность = ХарактеристикиИспользуются И УказатьХарактеристику;
	Элементы.УказатьХарактеристику.Доступность = ХарактеристикиИспользуются;
	
	Объект.ДействиеКалькуляции.Очистить();
	
	Если ЗначениеЗаполнено(Номенклатура) Тогда
		НоваяСтрока = Объект.ДействиеКалькуляции.Добавить();
		НоваяСтрока.Объект = Номенклатура;
		НоваяСтрока.Характеристика = Характеристика;
	КонецЕсли;
	
	Если Не ХарактеристикиИспользуются Тогда
		Характеристика = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
		УказатьХарактеристику = Ложь;
		Элементы.Характеристика.ПодсказкаВвода = "";
	ИначеЕсли ХарактеристикиИспользуются И Не УказатьХарактеристику Тогда
		Элементы.Характеристика.ПодсказкаВвода = НСтр("ru = 'Любая';
														|en = 'Any'");
	КонецЕсли;
	
	ЗаполнитьРеквизитыПредставленияОбластиДействия();
	
КонецПроцедуры

&НаСервере
Процедура ВыборОбластиДействияНаСервере(АдресВХранилище)
	
	РезультатРедактирования = ПолучитьИзВременногоХранилища(АдресВХранилище);
	
	Объект.ДействиеКалькуляции.Загрузить(РезультатРедактирования.ДействиеКалькуляции);
	ЕдиницаИзмерения = РезультатРедактирования.ЕдиницаИзмерения;
	
	Если Объект.ДействиеКалькуляции.Количество() < 2 Тогда
		НесколькоОбъектов = Ложь;
	КонецЕсли;
	
	ЗаполнитьРеквизитыПредставленияОбластиДействия(Истина);
	УстановитьВидимостьПоОбъектуКалькуляции();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиРеквизитов_СтатьиРасходов

&НаСервере
Процедура РассчитатьСуммуСтатьиРасходов(Идентификатор)
	
	ДанныеСтроки = Объект.СтатьиРасходов.НайтиПоИдентификатору(Идентификатор);
	
	ТипРезультата = Новый ОписаниеТипов("Число");
	ПараметрыПроверки = РаботаСФормулами.ПараметрыПроверкиФормулы();
	ПараметрыПроверки.Поле = "Объект.СтатьиРасходов[" + (ДанныеСтроки.НомерСтроки-1) + "].Формула";
	ПараметрыПроверки.НеВыводитьСообщения = Истина;
	ДанныеСтроки.ФормулаСодержитОшибки = Не РаботаСФормулами.ПроверитьФормулу(ДанныеСтроки.Формула,
		МассивБазовыхСтатейКалькуляции(ДанныеСтроки.СтатьяКалькуляции),
		ТипРезультата,
		ПараметрыПроверки);
	
	Если ДанныеСтроки.ФормулаСодержитОшибки Тогда
		ДанныеСтроки.Сумма = 0;
	Иначе
		МассивВлияющихСтатей = ИзвлечьБазовыеСтатьиИзФормулы(ДанныеСтроки.Формула);
		РассчитатьСтроку(ДанныеСтроки, МассивВлияющихСтатей);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиРеквизитов_СтатьиКалькуляции

&НаСервере
Процедура СтатьиКалькуляцииСтатьяКалькуляцииПриИзмененииНаСервере(Идентификатор)
	
	ЗаполнитьСлужебныеРеквизитыТабличныхЧастей("СтатьиКалькуляции", Идентификатор);
	РассчитатьЗависимыеИтогиПоТабличнымЧастям();
	
КонецПроцедуры

&НаСервере
Процедура СтатьиРасходовСтатьяКалькуляцииПриИзмененииНаСервере(Идентификатор)
	
	ДанныеСтроки = Объект.СтатьиРасходов.НайтиПоИдентификатору(Идентификатор);
	
	ОтборСтатья = Новый Структура("СтатьяКалькуляции", ДанныеСтроки.СтатьяКалькуляции);
	НайденныеСтроки = Объект.СтатьиКалькуляции.НайтиСтроки(ОтборСтатья);
	
	Если НайденныеСтроки.Количество() = 0 Тогда
		НоваяСтрока = Объект.СтатьиКалькуляции.Добавить();
		НоваяСтрока.СтатьяКалькуляции = ДанныеСтроки.СтатьяКалькуляции;
		НоваяСтрока.СпособРасчета = Перечисления.СпособыРасчетаЗатратПлановойКалькуляции.ПоДетальнымЗаписям;
		ЗаполнитьБазовыеСтатьиКалькуляции();
	КонецЕсли;
	
	ПрочиеРасходыПроверитьФормулы();
	РассчитатьСуммуСтатьиРасходов(Идентификатор);
	
КонецПроцедуры

&НаСервере
Процедура СтатьиРасходовСтатьяРасходовПриИзмененииНаСервере(Идентификатор)
	
	ДанныеСтроки = Объект.СтатьиРасходов.НайтиПоИдентификатору(Идентификатор);
	СтатьяКалькуляции = ОбщегоНазначенияУТВызовСервера.ЗначениеРеквизитаОбъекта(ДанныеСтроки.СтатьяРасходов, "СтатьяКалькуляции");
	
	Если ЗначениеЗаполнено(СтатьяКалькуляции) Тогда
		ОтборСтатья = Новый Структура("СтатьяКалькуляции", СтатьяКалькуляции);
		НайденныеСтроки = Объект.СтатьиКалькуляции.НайтиСтроки(ОтборСтатья);
		
		Если НайденныеСтроки.Количество() > 0 Тогда
			Строка = НайденныеСтроки[0];
			Если Строка.СпособРасчета = Перечисления.СпособыРасчетаЗатратПлановойКалькуляции.ПоФормуле И ЗначениеЗаполнено(Строка.Формула) Тогда
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ДанныеСтроки.СтатьяКалькуляции = СтатьяКалькуляции;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСФормулами

&НаСервере
Процедура ИнициализироватьРасчетПоФормуле()
	
	ПостроитьДеревоОператоров();
	
КонецПроцедуры

&НаСервере
Процедура ПостроитьДеревоОператоров()
	
	Дерево = РаботаСФормулами.ПолучитьПустоеДеревоОператоров();
	
	ПредставлениеГруппы = НСтр("ru = 'Операторы';
								|en = 'Operators'");
	ГруппаОператоров = РаботаСФормулами.ДобавитьГруппуОператоров(Дерево, "Операторы", ПредставлениеГруппы);
	РаботаСФормулами.ДобавитьОператор(ГруппаОператоров, "+", " + ", "+");
	РаботаСФормулами.ДобавитьОператор(ГруппаОператоров, "-", " - ", "-");
	РаботаСФормулами.ДобавитьОператор(ГруппаОператоров, "*", " * ", "*");
	РаботаСФормулами.ДобавитьОператор(ГруппаОператоров, "/", " / ", "/");
	
	ПредставлениеГруппы = НСтр("ru = 'Логические операторы и константы';
								|en = 'Logical operators and constants'");
	ГруппаОператоров = РаботаСФормулами.ДобавитьГруппуОператоров(Дерево, "ЛогическиеОператорыИКонстанты", ПредставлениеГруппы);
	РаботаСФормулами.ДобавитьОператор(ГруппаОператоров, "<",  " < ",  "<");
	РаботаСФормулами.ДобавитьОператор(ГруппаОператоров, ">",  " > ",  ">");
	РаботаСФормулами.ДобавитьОператор(ГруппаОператоров, "<=", " <= ", "<=");
	РаботаСФормулами.ДобавитьОператор(ГруппаОператоров, ">=", " >= ", ">=");
	РаботаСФормулами.ДобавитьОператор(ГруппаОператоров, "=",  " = ",  "=");
	РаботаСФормулами.ДобавитьОператор(ГруппаОператоров, "<>", " <> ", "<>");
	
	Идентификатор = "И";
	КонструкцияДляВставки = " " + "И" + " ";
	Представление = НСтр("ru = 'И';
						|en = 'AND'");
	РаботаСФормулами.ДобавитьОператор(ГруппаОператоров, Идентификатор, КонструкцияДляВставки, Представление);
	
	Идентификатор = "ИЛИ";
	КонструкцияДляВставки = " " + "ИЛИ" + " ";
	Представление = НСтр("ru = 'ИЛИ';
						|en = 'OR'");
	РаботаСФормулами.ДобавитьОператор(ГруппаОператоров, Идентификатор, КонструкцияДляВставки, Представление);
	
	Идентификатор = "НЕ";
	КонструкцияДляВставки = " " + "НЕ" + " ";
	Представление = НСтр("ru = 'НЕ';
						|en = 'NOT'");
	РаботаСФормулами.ДобавитьОператор(ГруппаОператоров, Идентификатор, КонструкцияДляВставки, Представление);
	
	Идентификатор = "ИСТИНА";
	КонструкцияДляВставки = " " + "ИСТИНА" + " ";
	Представление = НСтр("ru = 'ИСТИНА';
						|en = 'TRUE'");
	РаботаСФормулами.ДобавитьОператор(ГруппаОператоров, Идентификатор, КонструкцияДляВставки, Представление);
	
	Идентификатор = "ЛОЖЬ";
	КонструкцияДляВставки = " " + "ЛОЖЬ" + " ";
	Представление = НСтр("ru = 'ЛОЖЬ';
						|en = 'FALSE'");
	РаботаСФормулами.ДобавитьОператор(ГруппаОператоров, Идентификатор, КонструкцияДляВставки, Представление);
	
	ПредставлениеГруппы = НСтр("ru = 'Функции';
								|en = 'Functions'");
	ГруппаОператоров = РаботаСФормулами.ДобавитьГруппуОператоров(Дерево, "Функции", ПредставлениеГруппы);
	
	Идентификатор = "Макс";
	ПредставлениеПараметра1 = НСтр("ru = '<Значение 1>';
									|en = '<Value 1>'");
	ПредставлениеПараметра2 = НСтр("ru = '<Значение 2>';
									|en = '<Value 2>'");
	КонструкцияДляВставки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		"Макс(%1, %2)",
		ПредставлениеПараметра1,
		ПредставлениеПараметра2);
	Представление = НСтр("ru = 'Максимум';
						|en = 'Maximum'");
	РаботаСФормулами.ДобавитьОператор(ГруппаОператоров, Идентификатор, КонструкцияДляВставки, Представление,, 2);
	
	Идентификатор = "Мин";
	ПредставлениеПараметра1 = НСтр("ru = '<Значение 1>';
									|en = '<Value 1>'");
	ПредставлениеПараметра2 = НСтр("ru = '<Значение 2>';
									|en = '<Value 2>'");
	КонструкцияДляВставки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		"Мин(%1, %2)",
		ПредставлениеПараметра1,
		ПредставлениеПараметра2);
	Представление = НСтр("ru = 'Минимум';
						|en = 'Minimum'");
	РаботаСФормулами.ДобавитьОператор(ГруппаОператоров, Идентификатор, КонструкцияДляВставки, Представление,, 2);
	
	Идентификатор = "Окр";
	ПредставлениеПараметра1 = НСтр("ru = '<Округляемое значение>';
									|en = '<Round value>'");
	ПредставлениеПараметра2 = НСтр("ru = '<Количество знаков после запятой>';
									|en = '<Decimal places>'");
	КонструкцияДляВставки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		"Окр(%1, %2)",
		ПредставлениеПараметра1,
		ПредставлениеПараметра2);
	Представление = НСтр("ru = 'Округление';
						|en = 'Rounding'");
	РаботаСФормулами.ДобавитьОператор(ГруппаОператоров, Идентификатор, КонструкцияДляВставки, Представление,, 2);
	
	Идентификатор = "Цел";
	КонструкцияДляВставки = "Цел()";
	Представление = НСтр("ru = 'Целая часть';
						|en = 'Integral part'");
	РаботаСФормулами.ДобавитьОператор(ГруппаОператоров, Идентификатор, КонструкцияДляВставки, Представление,, 1);
	
	Идентификатор = "?";
	ПредставлениеПараметра1 = НСтр("ru = '<Логическое выражение>';
									|en = '<Logical expression>'");
	ПредставлениеПараметра2 = НСтр("ru = '<Значение если результат условия истина>';
									|en = '<Value if the condition result is true>'");
	ПредставлениеПараметра3 = НСтр("ru = '<Значение если результат условия ложь>';
									|en = '<Value if the condition result is false>'");
	КонструкцияДляВставки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		"?(%1, %2, %3)",
		ПредставлениеПараметра1,
		ПредставлениеПараметра2,
		ПредставлениеПараметра3);
	Представление = НСтр("ru = 'Условие';
						|en = 'Condition'");
	РаботаСФормулами.ДобавитьОператор(ГруппаОператоров, Идентификатор, КонструкцияДляВставки, Представление,, 3);
	
	АдресХранилищаДереваОператоров = ПоместитьВоВременноеХранилище(Дерево, УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьБазовыеСтатьиКалькуляции()
	
	БазовыеСтатьиКалькуляции.Очистить();
	
	ОтборПоВлияющимСтатьям = Новый Структура("СтатьяКалькуляции, ВлияющаяСтатьяКалькуляции");
	ОтборПоСтатьеТЧ = Новый Структура("СтатьяКалькуляцииТЧ");
	МассивПроверяемыхСтатей = Новый Массив;
	МассивУдаляемых = Новый Массив;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтатьиКалькуляции.СтатьяКалькуляции КАК СтатьяКалькуляции
	|ПОМЕСТИТЬ ВТСтатьи
	|ИЗ
	|	&СтатьиКалькуляции КАК СтатьиКалькуляции
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТСтатьи.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	ВТСтатьи.СтатьяКалькуляции.ИдентификаторДляФормул КАК Идентификатор
	|ИЗ
	|	ВТСтатьи КАК ВТСтатьи");
	
	Запрос.УстановитьПараметр("СтатьиКалькуляции", Объект.СтатьиКалькуляции.Выгрузить());
	ИдентификаторыСтатейКалькуляции = Запрос.Выполнить().Выгрузить();
	
	Для Каждого Строка Из Объект.СтатьиКалькуляции Цикл
		
		Для Каждого Идентификатор Из ИдентификаторыСтатейКалькуляции Цикл
			
			Если Идентификатор.СтатьяКалькуляции = Строка.СтатьяКалькуляции Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = БазовыеСтатьиКалькуляции.Добавить();
			НоваяСтрока.СтатьяКалькуляцииТЧ = Строка.СтатьяКалькуляции;
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Идентификатор);
			
		КонецЦикла;
		
		ОтборПоСтатьеТЧ.СтатьяКалькуляцииТЧ = Строка.СтатьяКалькуляции;
		МассивПроверяемыхСтатей.Очистить();
		МассивПроверяемыхСтатей.Добавить(Строка.СтатьяКалькуляции);
		
		Пока МассивПроверяемыхСтатей.Количество() > 0 Цикл
			
			МассивУдаляемых.Очистить();
			ТекСтатья = МассивПроверяемыхСтатей[0];
			
			ТекущиеБазовыеСтатьи = БазовыеСтатьиКалькуляции.НайтиСтроки(ОтборПоСтатьеТЧ);
			
			Для Каждого ТекущаяБазоваяСтатья Из ТекущиеБазовыеСтатьи Цикл
				
				ОтборПоВлияющимСтатьям.ВлияющаяСтатьяКалькуляции = ТекСтатья;
				ОтборПоВлияющимСтатьям.СтатьяКалькуляции = ТекущаяБазоваяСтатья.СтатьяКалькуляции;
				
				НайденныеСтроки = Объект.ВлияющиеСтатьиКалькуляции.НайтиСтроки(ОтборПоВлияющимСтатьям);
				
				Если НайденныеСтроки.Количество() > 0 Тогда
					МассивУдаляемых.Добавить(ТекущаяБазоваяСтатья);
					МассивПроверяемыхСтатей.Добавить(НайденныеСтроки[0].СтатьяКалькуляции);
				КонецЕсли;
				
			КонецЦикла;
			
			Для Каждого Строка Из МассивУдаляемых Цикл
				БазовыеСтатьиКалькуляции.Удалить(Строка);
			КонецЦикла;
			
			МассивПроверяемыхСтатей.Удалить(0);
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция БазовыеСтатьиКалькуляцииВХранилище(СтатьяКалькуляции)
	
	ОтборПоСтатьеТЧ = Новый Структура("СтатьяКалькуляцииТЧ", СтатьяКалькуляции);
	
	Результат = БазовыеСтатьиКалькуляции.Выгрузить(ОтборПоСтатьеТЧ, "СтатьяКалькуляции, Идентификатор");
	Дерево = РаботаСФормулами.ПолучитьПустоеДеревоОперандов();
	Для Каждого СтрокаРезультат Из Результат Цикл
		СтрокаДерева = РаботаСФормулами.НоваяСтрокаДереваОперанда(Дерево);
		СтрокаДерева.Идентификатор = СтрокаРезультат.Идентификатор;
		СтрокаДерева.Представление = СтрокаРезультат.Идентификатор;
		СтрокаДерева.ТипЗначения = Новый ОписаниеТипов("Число");
	КонецЦикла;
	АдресВХранилище = ПоместитьВоВременноеХранилище(Дерево, УникальныйИдентификатор);
	
	Возврат АдресВХранилище;
	
КонецФункции

&НаСервере
Функция МассивБазовыхСтатейКалькуляции(СтатьяКалькуляции)
	
	ОтборПоСтатьеТЧ = Новый Структура("СтатьяКалькуляцииТЧ", СтатьяКалькуляции);
	
	НайденныеСтроки = БазовыеСтатьиКалькуляции.НайтиСтроки(ОтборПоСтатьеТЧ);
	
	Результат = Новый Массив;
	
	Для Каждого Строка Из НайденныеСтроки Цикл
		Результат.Добавить(Строка.Идентификатор);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура СтатьиКалькуляцииПроверитьФормулы(Идентификатор = Неопределено, ИзменятьСтроки = Истина)
	
	ТипРезультата = Новый ОписаниеТипов("Число");
	ПараметрыПроверки = РаботаСФормулами.ПараметрыПроверкиФормулы();
	ПараметрыПроверки.НеВыводитьСообщения = Истина;
	Для Каждого Строка Из Объект.СтатьиКалькуляции Цикл
		
		Если Идентификатор <> Неопределено И Строка.ПолучитьИдентификатор() <> Идентификатор Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыПроверки.Поле = "Объект.СтатьиКалькуляции[" + (Строка.НомерСтроки-1) + "].Формула";
		Строка.ФормулаСодержитОшибки = Не РаботаСФормулами.ПроверитьФормулу(Строка.Формула,
			МассивБазовыхСтатейКалькуляции(Строка.СтатьяКалькуляции),
			ТипРезультата,
			ПараметрыПроверки);
		
		Если Строка.ФормулаСодержитОшибки И ИзменятьСтроки Тогда
			Строка.Сумма = 0;
			Строка.СтрокаИзменена = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого Строка Из Объект.СтатьиРасходов Цикл
		
		Если Идентификатор <> Неопределено И Строка.ПолучитьИдентификатор() <> Идентификатор Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыПроверки.Поле = "Объект.СтатьиКалькуляции[" + (Строка.НомерСтроки-1) + "].Формула";
		Строка.ФормулаСодержитОшибки = Не РаботаСФормулами.ПроверитьФормулу(Строка.Формула,
			МассивБазовыхСтатейКалькуляции(Строка.СтатьяКалькуляции),
			ТипРезультата,
			ПараметрыПроверки);
		
		Если Строка.ФормулаСодержитОшибки И ИзменятьСтроки Тогда
			Строка.Сумма = 0;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПрочиеРасходыПроверитьФормулы(Идентификатор = Неопределено, ИзменятьСтроки = Истина)
	
	ТипРезультата = Новый ОписаниеТипов("Число");
	ПараметрыПроверки = РаботаСФормулами.ПараметрыПроверкиФормулы();
	ПараметрыПроверки.НеВыводитьСообщения = Истина;
	
	Для Каждого Строка Из Объект.СтатьиРасходов Цикл
		
		Если Идентификатор <> Неопределено И Строка.ПолучитьИдентификатор() <> Идентификатор Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыПроверки.Поле = "Объект.СтатьиКалькуляции[" + (Строка.НомерСтроки-1) + "].Формула";
		Строка.ФормулаСодержитОшибки = Не РаботаСФормулами.ПроверитьФормулу(Строка.Формула,
			МассивБазовыхСтатейКалькуляции(Строка.СтатьяКалькуляции),
			ТипРезультата,
			ПараметрыПроверки);
		
		Если Строка.ФормулаСодержитОшибки И ИзменятьСтроки Тогда
			Строка.Сумма = 0;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьСуммыПоСтатьям()
	
	ОтборВТЧСтатьиРасходов = Новый Структура();
	ОтборВТЧСтатьиРасходов.Вставить("СтатьяКалькуляции");
	ОтборВТЧСтатьиРасходов.Вставить("РасчетПоФормуле", Истина);
	
	ОтборПоСтатьеКалькуляции = Новый Структура("СтатьяКалькуляции");
	
	// Поиск измененных статей калькуляции.
	ОтборИзмененныхСтрок = Новый Структура("СтрокаИзменена", Истина);
	ИзмененныеСтроки = Объект.СтатьиКалькуляции.НайтиСтроки(ОтборИзмененныхСтрок);
	
	СтатьиКРасчету = Новый Массив;
	
	Для Каждого СтрокаТЧСтатьиКалькуляции Из ИзмененныеСтроки Цикл
		
		Если Не СтрокаТЧСтатьиКалькуляции.СпособРасчета = Перечисления.СпособыРасчетаЗатратПлановойКалькуляции.ПоФормуле Тогда
			
			// Проверка на расчет по формуле в ТЧ Статьи расходов.
			ОтборВТЧСтатьиРасходов.СтатьяКалькуляции = СтрокаТЧСтатьиКалькуляции.СтатьяКалькуляции;
			СтрокиТЧСтатьиРасходов = Объект.СтатьиРасходов.НайтиСтроки(ОтборВТЧСтатьиРасходов);
			Если СтрокиТЧСтатьиРасходов.Количество() > 0 Тогда
				// Добавление к расчету.
				СтатьиКРасчету.Добавить(СтрокаТЧСтатьиКалькуляции.СтатьяКалькуляции);
			Иначе
				// Статья калькуляции уже рассчитана.
				СтрокаТЧСтатьиКалькуляции.СтрокаИзменена = Ложь;
			КонецЕсли;
			
		Иначе
			// Добавление к расчету.
			СтатьиКРасчету.Добавить(СтрокаТЧСтатьиКалькуляции.СтатьяКалькуляции);
		КонецЕсли;
		
	КонецЦикла;
	
	Если СтатьиКРасчету.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Добавление связанных статей к расчету.
	ОтборВлияющейСтатьиКалькуляции = Новый Структура("ВлияющаяСтатьяКалькуляции");
	Для Каждого СтатьяКРасчету Из СтатьиКРасчету Цикл
		
		ОтборПоСтатьеКалькуляции.СтатьяКалькуляции = СтатьяКРасчету;
		СтрокиВлияющиеСтатьи = Объект.ВлияющиеСтатьиКалькуляции.НайтиСтроки(ОтборПоСтатьеКалькуляции);
		
		Для Каждого СтрокаВлияющаяСтатья Из СтрокиВлияющиеСтатьи Цикл
			Если СтатьиКРасчету.Найти(СтрокаВлияющаяСтатья.ВлияющаяСтатьяКалькуляции) = Неопределено Тогда
				СтатьиКРасчету.Добавить(СтрокаВлияющаяСтатья.ВлияющаяСтатьяКалькуляции);
			КонецЕсли;
		КонецЦикла;
		
		ОтборВлияющейСтатьиКалькуляции.ВлияющаяСтатьяКалькуляции = СтатьяКРасчету;
		СтрокиЗависимыеСтатьи = Объект.ВлияющиеСтатьиКалькуляции.НайтиСтроки(ОтборВлияющейСтатьиКалькуляции);
		
		Для Каждого СтрокаЗависимаяСтатья Из СтрокиЗависимыеСтатьи Цикл
			Если СтатьиКРасчету.Найти(СтрокаЗависимаяСтатья.СтатьяКалькуляции) = Неопределено Тогда
				СтатьиКРасчету.Добавить(СтрокаЗависимаяСтатья.СтатьяКалькуляции);
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
	Индекс = 0;
	Пока СтатьиКРасчету.Количество() > 0 И Индекс <= СтатьиКРасчету.Количество() - 1 Цикл
		
		СтатьяКРасчету = СтатьиКРасчету[Индекс];
		ОтборПоСтатьеКалькуляции.СтатьяКалькуляции = СтатьяКРасчету;
		
		// Определение возможности расчета статьи.
		ПоказателиРассчитаны = Истина;
		СтрокиВлияющиеСтатьи = Объект.ВлияющиеСтатьиКалькуляции.НайтиСтроки(ОтборПоСтатьеКалькуляции);
		Если СтрокиВлияющиеСтатьи.Количество() > 0 Тогда
			
			// Расчет зависит от др. статей.
			Для Каждого СтрокаВлияющаяСтатья Из СтрокиВлияющиеСтатьи Цикл
				Если Не СтатьиКРасчету.Найти(СтрокаВлияющаяСтатья.ВлияющаяСтатьяКалькуляции) = Неопределено Тогда
					// Статья присутствует в списке статей к расчету. Расчет невозможен пока все статьи влияющие не будут рассчитаны.
					ПоказателиРассчитаны = Ложь;
					Прервать;
					
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
		Если ПоказателиРассчитаны Тогда
			
			// Расчет ТЧ Статьи калькуляции.
			СтрокиКРасчету = Объект.СтатьиКалькуляции.НайтиСтроки(ОтборПоСтатьеКалькуляции);
			Для Каждого СтрокаКРасчету Из СтрокиКРасчету Цикл
				Если СтрокаКРасчету.СпособРасчета = Перечисления.СпособыРасчетаЗатратПлановойКалькуляции.ПоФормуле Тогда
					// Расчет строки.
					МассивВлияющихСтатей = ИзвлечьБазовыеСтатьиИзФормулы(СтрокаКРасчету.Формула);
					РассчитатьСтроку(СтрокаКРасчету, МассивВлияющихСтатей);
					СтрокаКРасчету.СтрокаИзменена = Ложь;
					
				КонецЕсли;
			КонецЦикла;
			
			// Расчет ТЧ Статьи расходов.
			СтрокиКРасчетуСтатьиРасходов = Объект.СтатьиРасходов.НайтиСтроки(ОтборПоСтатьеКалькуляции);
			Для Каждого СтрокаКРасчету Из СтрокиКРасчетуСтатьиРасходов Цикл
				Если СтрокаКРасчету.РасчетПоФормуле Тогда
					// Расчет строки.
					МассивВлияющихСтатей = ИзвлечьБазовыеСтатьиИзФормулы(СтрокаКРасчету.Формула);
					РассчитатьСтроку(СтрокаКРасчету, МассивВлияющихСтатей);
					
				КонецЕсли;
			КонецЦикла;
			
			Если СтрокиКРасчетуСтатьиРасходов.Количество() > 0 Тогда
				
				// Обновление показателя у статьи калькуляции.
				СуммаПоказателя = 0;
				СтрокиКРасчетуСтатьиРасходов = Объект.СтатьиРасходов.НайтиСтроки(ОтборПоСтатьеКалькуляции);
				Для Каждого СтрокаКРасчету Из СтрокиКРасчетуСтатьиРасходов Цикл
					СуммаПоказателя = СуммаПоказателя + СтрокаКРасчету.Сумма;
				КонецЦикла;
				
				СтрокиКРасчету = Объект.СтатьиКалькуляции.НайтиСтроки(ОтборПоСтатьеКалькуляции);
				Для Каждого СтрокаКРасчету Из СтрокиКРасчету Цикл
					СтрокаКРасчету.Сумма = СуммаПоказателя;
				КонецЦикла;
				
			КонецЕсли;
			
			// Статья рассчитана. Удаление из списка к расчету.
			СтатьиКРасчету.Удалить(Индекс);
			// Обнуление счетчика для прохода списка заново.
			Индекс = 0;			
			
		Иначе
			Индекс = Индекс + 1;
		КонецЕсли;
		
	КонецЦикла;
		
КонецПроцедуры

&НаСервере
Процедура РассчитатьСтроку(РассчитываемаяСтрока, МассивВлияющихСтатей)
	
	Если РассчитываемаяСтрока.ФормулаСодержитОшибки Тогда
		РассчитываемаяСтрока.Сумма = 0;
		Возврат;
	КонецЕсли;
	
	ОтборПоСтатьеКалькуляции = Новый Структура("СтатьяКалькуляции");
	
	Формула = РассчитываемаяСтрока.Формула;
	
	Если Не ЗначениеЗаполнено(Формула) Тогда
		РассчитываемаяСтрока.Сумма = 0;
		Возврат;
	КонецЕсли;
	
	Для Каждого Строка Из МассивВлияющихСтатей Цикл
		
		ОтборПоСтатьеКалькуляции.СтатьяКалькуляции = Строка;
		НайденныеСтроки = Объект.СтатьиКалькуляции.НайтиСтроки(ОтборПоСтатьеКалькуляции);
		
		Если НайденныеСтроки.Количество() > 0 Тогда
			
			ДанныеСтроки = НайденныеСтроки[0];
			Формула = СтрЗаменить(Формула, "["+ ДанныеСтроки.Идентификатор + "]", Формат(ДанныеСтроки.Сумма, "ЧРД=.; ЧН=0; ЧГ="));
			
		КонецЕсли;
		
	КонецЦикла;
	
	РассчитываемаяСтрока.Сумма = ОбщегоНазначения.ВычислитьВБезопасномРежиме(Формула);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗависимостиСтатейКалькуляции()
	
	Объект.ВлияющиеСтатьиКалькуляции.Очистить();
	
	ОтборСтатьиКалькуляции = Новый Структура("СтатьяКалькуляции, ВлияющаяСтатьяКалькуляции");
	
	ОтборСпособРасчета = Новый Структура("СпособРасчета", Перечисления.СпособыРасчетаЗатратПлановойКалькуляции.ПоФормуле);
	РассчитываемыеСтроки = Объект.СтатьиКалькуляции.НайтиСтроки(ОтборСпособРасчета);
	
	Для Каждого Строка Из РассчитываемыеСтроки Цикл
		
		МассивВлияющихСтатей = ИзвлечьБазовыеСтатьиИзФормулы(Строка.Формула);
		
		Для Каждого ВлияющаяСтатья Из МассивВлияющихСтатей Цикл
			
			ОтборСтатьиКалькуляции.СтатьяКалькуляции = Строка.СтатьяКалькуляции;
			ОтборСтатьиКалькуляции.ВлияющаяСтатьяКалькуляции = ВлияющаяСтатья;
			
			НайденныеСтроки = Объект.ВлияющиеСтатьиКалькуляции.НайтиСтроки(ОтборСтатьиКалькуляции);
			
			Если НайденныеСтроки.Количество() > 0 Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = Объект.ВлияющиеСтатьиКалькуляции.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ОтборСтатьиКалькуляции);
			
		КонецЦикла;
		
	КонецЦикла;
	
	ОтборСпособРасчета = Новый Структура("РасчетПоФормуле", Истина);
	РассчитываемыеСтроки = Объект.СтатьиРасходов.НайтиСтроки(ОтборСпособРасчета);
	
	Для Каждого Строка Из РассчитываемыеСтроки Цикл
		
		МассивВлияющихСтатей = ИзвлечьБазовыеСтатьиИзФормулы(Строка.Формула);
		
		Для Каждого ВлияющаяСтатья Из МассивВлияющихСтатей Цикл
			
			ОтборСтатьиКалькуляции.СтатьяКалькуляции = Строка.СтатьяКалькуляции;
			ОтборСтатьиКалькуляции.ВлияющаяСтатьяКалькуляции = ВлияющаяСтатья;
			
			НайденныеСтроки = Объект.ВлияющиеСтатьиКалькуляции.НайтиСтроки(ОтборСтатьиКалькуляции);
			
			Если НайденныеСтроки.Количество() > 0 Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = Объект.ВлияющиеСтатьиКалькуляции.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ОтборСтатьиКалькуляции);
			
		КонецЦикла;
		
	КонецЦикла;

КонецПроцедуры

&НаСервере
Функция ИзвлечьБазовыеСтатьиИзФормулы(Формула)
	
	Результат = Новый Массив;
	ОтборПоИдентификатору = Новый Структура("Идентификатор");
	
	МассивЭлементов = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Формула, "[");
	
	Для Индекс = 1 По МассивЭлементов.Количество() - 1 Цикл
		
		Если ЗначениеЗаполнено(МассивЭлементов[Индекс]) Тогда
			
			ОкончаниеИдентификатора = СтрНайти(МассивЭлементов[Индекс], "]");
			
			Если ОкончаниеИдентификатора > 0 Тогда
				
				БазоваяСтатьяИдентификатор = Лев(МассивЭлементов[Индекс], ОкончаниеИдентификатора - 1);
				ОтборПоИдентификатору.Идентификатор = БазоваяСтатьяИдентификатор;
				
				БазовыеСтатьи = Объект.СтатьиКалькуляции.НайтиСтроки(ОтборПоИдентификатору);
				Если БазовыеСтатьи.Количество() > 0 Тогда
					Результат.Добавить(БазовыеСтатьи[0].СтатьяКалькуляции);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Прочее

&НаСервере
Процедура ЗаполнитьОбъектПоСтатистике()
	
	ЗаполнениеОбъектовПоСтатистике.ЗаполнитьПодчиненныеРеквизитыОбъекта(Объект, "ИспользоватьОрганизацию");
	
КонецПроцедуры

&НаСервере
Процедура НастроитьПараметрыВыбораВидаЦен()
	
	ВидЦеныПлановойСтоимости = Справочники.ВидыЦен.ВидЦеныПлановойСтоимостиТМЦ();
	ЦенаВключаетНДС = Не ЗначениеЗаполнено(ВидЦеныПлановойСтоимости) Или ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидЦеныПлановойСтоимости, "ЦенаВключаетНДС");
	
	МассивПараметров = Новый Массив;
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.ЦенаВключаетНДС",       ЦенаВключаетНДС));
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Статус", Перечисления.СтатусыДействияВидовЦен.Действует));
	
	Элементы.ВидЦены.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	
	МассивПараметров.Добавить(Новый ПараметрВыбора("ВыводитьПроизвольныйВидЦен",  Истина));
	
	Элементы.МатериалыИУслугиВидЦены.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.ВозвратныеОтходыВидЦены.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьЗависимыеИтогиПоТабличнымЧастям()
	
	Документы.ПлановаяКалькуляция.РассчитатьЗависимыеИтогиПоТабличнымЧастям(Объект);
	
	РассчитатьТаблицуСтатейКалькуляции();
	
	ЗаполнитьСлужебныеРеквизитыТабличныхЧастей("СтатьиКалькуляции");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИтогиТекущейТаблицы()
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаВозвратныеОтходы Тогда
		ТекущиеДанные = Элементы.ВозвратныеОтходы.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(ТекущиеДанные.СтатьяКалькуляции) Тогда
			ОбновитьИтогиПоСтатьеКалькуляции("Отходы", ТекущиеДанные.СтатьяКалькуляции, ТекущиеДанные.Сумма);
		Иначе
			ОбновитьИтогиПоСтатьеКалькуляции("Отходы");
		КонецЕсли;
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаМатериалыИУслуги Тогда
		ТекущиеДанные = Элементы.МатериалыИУслуги.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(ТекущиеДанные.СтатьяКалькуляции) Тогда
			ОбновитьИтогиПоСтатьеКалькуляции("Материалы", ТекущиеДанные.СтатьяКалькуляции, ТекущиеДанные.Сумма);
		Иначе
			ОбновитьИтогиПоСтатьеКалькуляции("Материалы");
		КонецЕсли;
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаТрудозатраты Тогда
		ТекущиеДанные = Элементы.Трудозатраты.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(ТекущиеДанные.СтатьяКалькуляции) Тогда
			ОбновитьИтогиПоСтатьеКалькуляции("Трудозатраты", ТекущиеДанные.СтатьяКалькуляции, ТекущиеДанные.Сумма);
		Иначе
			ОбновитьИтогиПоСтатьеКалькуляции("Трудозатраты");
		КонецЕсли;
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаСтатьиРасходов Тогда
		ТекущиеДанные = Элементы.СтатьиРасходов.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(ТекущиеДанные.СтатьяКалькуляции) Тогда
			ОбновитьИтогиПоСтатьеКалькуляции("Статьи", ТекущиеДанные.СтатьяКалькуляции, ТекущиеДанные.Сумма);
		Иначе
			ОбновитьИтогиПоСтатьеКалькуляции("Статьи");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИтогиПоСтатьеКалькуляции(ИмяПоказателя, СтатьяКалькуляции = Неопределено, Сумма = 0)
	
	ИмяЭлементаФормы = "Итоги" + ИмяПоказателя;
	
	Если СтатьяКалькуляции = Неопределено Тогда
		Элементы[ИмяЭлементаФормы].ТекущаяСтраница = Элементы[ИмяЭлементаФормы + "Пояснение"];
		Возврат;
	Иначе
		Элементы[ИмяЭлементаФормы].ТекущаяСтраница = Элементы[ИмяЭлементаФормы + "Расчет"];
	КонецЕсли;
	
	ОтборСтатьяКалькуляции = Новый Структура("СтатьяКалькуляции", СтатьяКалькуляции);
	
	НайденныеСтроки = Объект.СтатьиКалькуляции.НайтиСтроки(ОтборСтатьяКалькуляции);
	
	Если НайденныеСтроки.Количество() > 0 Тогда
		
		ЭтаФорма["ЗаголовокИтогаПоСтатье" + ИмяПоказателя] = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Всего (%1):';
				|en = 'Total (%1): '"),
			СокрЛП(СтатьяКалькуляции));
			
		Если НайденныеСтроки[0].Сумма > 0 Тогда
			Итог = НайденныеСтроки[0].Сумма;
		Иначе
			Итог = - НайденныеСтроки[0].Сумма;
		КонецЕсли;
		
		ЭтаФорма["ИтогПоСтатье" + ИмяПоказателя] = Итог;
		
		Если Итог = 0 Тогда
			ЭтаФорма["УдельныйВес" + ИмяПоказателя] = 0;
		Иначе
			ЭтаФорма["УдельныйВес" + ИмяПоказателя] = 100 * Сумма / Итог;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ДетальныеЗаписиПоСтатье(СтатьяКалькуляции = Неопределено)
	
	Если СтатьяКалькуляции = Неопределено Тогда
		СтруктураОтбора = Новый Структура;
	Иначе
		СтруктураОтбора = Новый Структура("СтатьяКалькуляции", СтатьяКалькуляции);
	КонецЕсли;
	
	ТЗОтходы = Объект.ВозвратныеОтходы.НайтиСтроки(СтруктураОтбора);
	ТЗМатериалы = Объект.МатериалыИУслуги.НайтиСтроки(СтруктураОтбора);
	ТЗТрудозатраты = Объект.Трудозатраты.НайтиСтроки(СтруктураОтбора);
	ТЗСтатьи = Объект.СтатьиРасходов.НайтиСтроки(СтруктураОтбора);
	
	Если ТЗОтходы.Количество() = 0 
		И ТЗМатериалы.Количество() = 0 
		И ТЗТрудозатраты.Количество() = 0 
		И ТЗСтатьи.Количество() = 0 Тогда
		
		Возврат Неопределено;
	КонецЕсли;
	
	Отходы = Новый Массив;
	Для Каждого Строка Из ТЗОтходы Цикл
		Отходы.Добавить(Строка.ПолучитьИдентификатор());
	КонецЦикла;
	Материалы = Новый Массив;
	Для Каждого Строка Из ТЗМатериалы Цикл
		Материалы.Добавить(Строка.ПолучитьИдентификатор());
	КонецЦикла;
	Трудозатраты = Новый Массив;
	Для Каждого Строка Из ТЗТрудозатраты Цикл
		Трудозатраты.Добавить(Строка.ПолучитьИдентификатор());
	КонецЦикла;
	Статьи = Новый Массив;
	Для Каждого Строка Из ТЗСтатьи Цикл
		Статьи.Добавить(Строка.ПолучитьИдентификатор());
	КонецЦикла;
	
	Результат = Новый Структура();
	Результат.Вставить("Отходы",       Отходы);
	Результат.Вставить("Материалы",    Материалы);
	Результат.Вставить("Трудозатраты", Трудозатраты);
	Результат.Вставить("Статьи",       Статьи);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура РассчитатьТаблицуСтатейКалькуляции()
	
	ЗаполнитьЗависимостиСтатейКалькуляции();
	ЗаполнитьБазовыеСтатьиКалькуляции();
	СтатьиКалькуляцииПроверитьФормулы();
	РассчитатьСуммыПоСтатьям();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапросИзмененияСпособаРасчета(Знач Оповещение, ДанныеСтроки, ПредставлениеТЧ)
	
	ОтборСтатьяКалькуляции = Новый Структура("СтатьяКалькуляции", ДанныеСтроки.СтатьяКалькуляции);
	
	НайденныеСтроки = Объект.СтатьиКалькуляции.НайтиСтроки(ОтборСтатьяКалькуляции);
	
	Если НайденныеСтроки.Количество() > 0 Тогда
		
		ТекущийСпособ = НайденныеСтроки[0].СпособРасчета;
		
		Если ТекущийСпособ <> ПредопределенноеЗначение("Перечисление.СпособыРасчетаЗатратПлановойКалькуляции.ПоДетальнымЗаписям") Тогда
			
			Если ТекущийСпособ = ПредопределенноеЗначение("Перечисление.СпособыРасчетаЗатратПлановойКалькуляции.ФиксированнымЗначением") Тогда
				ТекстВопроса = НСтр("ru = 'Сумма по выбранной статье калькуляции задается фиксированным значением. 
											|Для детализации статьи до %1 требуется установить способ расчета ""По детальным записям"". 
											|Установить способ расчета автоматически?';
											|en = 'Amount of the selected costing item is specified with a fixed value. 
											|To detail the item to %1, set the ""By detailed entries"" calculation method. 
											|Set the calculation method automatically?'");
			Иначе
				ТекстВопроса = НСтр("ru = 'Сумма по выбранной статье калькуляции рассчитывается по формуле.
											|Для детализации статьи до %1 требуется установить способ расчета ""По детальным записям"". 
											|Установить способ расчета автоматически?';
											|en = 'Amount of the selected costing item is calculated using the formula.
											|To detail the item to %1, set the ""By detailed entries"" calculation method. 
											|Set the calculation method automatically?'");
			КонецЕсли;
			
			ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстВопроса, ПредставлениеТЧ);
			ОписаниеОповещения = Новый ОписаниеОповещения("ЗапросИзмененияСпособаРасчетаЗавершение", ЭтотОбъект, Новый Структура("ДанныеСтроки, Оповещение", ДанныеСтроки, Оповещение));
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
			
			Возврат;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ЗапросИзмененияСпособаРасчетаФрагмент(Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапросИзмененияСпособаРасчетаЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    ДанныеСтроки = ДополнительныеПараметры.ДанныеСтроки;
    Оповещение = ДополнительныеПараметры.Оповещение;
    
    Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
        ДанныеСтроки.СтатьяКалькуляции = ПредопределенноеЗначение("Справочник.СтатьиКалькуляции.ПустаяСсылка");
    КонецЕсли;
    
    ЗапросИзмененияСпособаРасчетаФрагмент(Оповещение);

КонецПроцедуры

&НаКлиенте
Процедура ЗапросИзмененияСпособаРасчетаФрагмент(Знач Оповещение)
    
    ВыполнитьОбработкуОповещения(Оповещение);

КонецПроцедуры

&НаСервере
Процедура ОчиститьСлужебныеРеквизиты()
	
	Номенклатура = ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка");
	Характеристика = ПредопределенноеЗначение("Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка");
	Спецификация = ПредопределенноеЗначение("Справочник.РесурсныеСпецификации.ПустаяСсылка");
	ЗаказНаПроизводство = ПредопределенноеЗначение("Документ.ЗаказНаПроизводство.ПустаяСсылка");
	ПредставлениеПозицииЗаказа = "";
	ПредставлениеПозицииЗаказаИсходное = "";
	СведенияОбИзделиях = "";
	ПредставлениеОбщее = ?(НесколькоОбъектов, НСтр("ru = 'Выбрать';
													|en = 'Select'"), "");
	
	ХарактеристикиИспользуются = Ложь;
	УказатьХарактеристику = Ложь;
	
КонецПроцедуры

&НаСервере
Функция ДействиеКалькуляцииВХранилище()
	
	АдресВХранилище = ПоместитьВоВременноеХранилище(Объект.ДействиеКалькуляции.Выгрузить());
	
	Возврат АдресВХранилище;
	
КонецФункции

&НаКлиенте
Процедура СтатьиКалькуляцииПередУдалениемЗавершение(РезультатВопроса, ДопПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока = Объект.СтатьиКалькуляции.НайтиПоИдентификатору(ДопПараметры.ИдентификаторСтроки);
	Объект.СтатьиКалькуляции.Удалить(ТекущаяСтрока);
	
	Для Каждого Строка Из ДопПараметры.ДетальныеЗаписи.Отходы Цикл
		Объект.ВозвратныеОтходы.Удалить(Объект.ВозвратныеОтходы.НайтиПоИдентификатору(Строка));
	КонецЦикла;
	
	Для Каждого Строка Из ДопПараметры.ДетальныеЗаписи.Материалы Цикл
		Объект.МатериалыИУслуги.Удалить(Объект.МатериалыИУслуги.НайтиПоИдентификатору(Строка));
	КонецЦикла;
	
	Для Каждого Строка Из ДопПараметры.ДетальныеЗаписи.Трудозатраты Цикл
		Объект.Трудозатраты.Удалить(Объект.Трудозатраты.НайтиПоИдентификатору(Строка));
	КонецЦикла;
	
	Для Каждого Строка Из ДопПараметры.ДетальныеЗаписи.Статьи Цикл
		Объект.СтатьиРасходов.Удалить(Объект.СтатьиРасходов.НайтиПоИдентификатору(Строка));
	КонецЦикла;
	
	РассчитатьТаблицуСтатейКалькуляции();
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

