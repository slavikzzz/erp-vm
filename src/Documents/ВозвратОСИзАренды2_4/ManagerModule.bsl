#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив из Строка - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	МеханизмыДокумента.Добавить("ОсновныеСредства");
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	МеханизмыДокумента.Добавить("РеестрДокументов");
	
	ВозвратОСИзАрендыЛокализация.ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента);
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка, ДокументОбъект - ссылка на документ или объект, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  см. ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Если ТипЗнч(Документ) = Тип("ДокументОбъект.ВозвратОСИзАренды2_4") Тогда
		ДокументСсылка = Документ.Ссылка;
	Иначе
		ДокументСсылка = Документ;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		
		ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДопПараметры);
		
		ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры);
		
		ТекстЗапросаТаблицаМестонахождениеОС(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПорядокУчетаОС(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПорядокУчетаОСУУ(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПорядокУчетаОСБУ(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПервоначальныеСведенияОС(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПереданныеВАрендуОС(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаГрафикОплатУслугПоДоходнойАренде(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаГрафикНачисленияУслугПоДоходнойАренде(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаГрафикНачисленияПроцентовПоДоходнойАренде(ТекстыЗапроса, Регистры);
		
		//++ НЕ УТКА
		ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаОтражениеДокументовВМеждународномУчете(ТекстыЗапроса, Регистры);
		//-- НЕ УТКА
		
		ВозвратОСИзАрендыЛокализация.ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры);
		
	КонецЕсли;
	
	ТаблицыДвижений = ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
	Возврат ТаблицыДвижений;
	
КонецФункции

// Формирует таблицы движений при отложенном проведении.
//
// Параметры:
//  МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Содержит временные таблицы, которые используются при формировании движений.
//
// Возвращаемое значение:
//  см. ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения
//
Функция ТаблицыОтложенногоФормированияДвижений(МенеджерВременныхТаблиц) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ПолучитьДанныеДокумента(Запрос);
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализацииПоПериодам(Запрос, Запрос.Параметры,, "ВтСписокДокументов");
	
	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения();
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла;
	
	ТекстыЗапроса = Новый СписокЗначений;
		
	ТекстЗапросаТаблицаСтоимостьОС(ТекстыЗапроса);
	ТекстЗапросаТаблицаАмортизацияОС(ТекстыЗапроса);
	ТекстЗапросаДвиженияДоходыРасходыПрочиеАктивыПассивы(ТекстыЗапроса);
	
	ТекстЗапросаТаблицаПорядокУчетаОС(ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПорядокУчетаОСУУ(ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПорядокУчетаОСБУ(ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПервоначальныеСведенияОС(ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПараметрыАмортизацииОСБУ(ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПараметрыАмортизацииОСУУ(ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПереданныеВАрендуОС(ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаИнвестицииВАренду(Запрос, ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаДвиженияПоПрочимАктивамПассивам(Запрос, ТекстыЗапроса, Неопределено);
	
	//++ НЕ УТКА
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаОтражениеДокументовВМеждународномУчете(ТекстыЗапроса, Неопределено);
	//-- НЕ УТКА
	
	ВозвратОСИзАрендыЛокализация.ТаблицыОтложенногоФормированияДвижений(ТекстыЗапроса);
	
	ТаблицыДвижений = ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса);
	
	Возврат ТаблицыДвижений;
	
КонецФункции

#КонецОбласти

#Область Команды

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	ВозвратОСИзАрендыЛокализация.ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры);

КонецПроцедуры

// Добавляет команду создания документа "Возврат ОС из аренды".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  
// Возвращаемое значение:
//	СтрокаТаблицыЗначений - добавленная команда.
//	
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.ВозвратОСИзАренды2_4) Тогда
		
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.ВозвратОСИзАренды2_4.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.ВозвратОСИзАренды2_4);
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьВнеоборотныеАктивы2_4";
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		
		Возврат КомандаСоздатьНаОсновании;
	
	КонецЕсли;
	
	Возврат Неопределено;

КонецФункции

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	ВозвратОСИзАрендыЛокализация.ДобавитьКомандыПечати(КомандыПечати);

КонецПроцедуры

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	ВозвратОСИзАрендыЛокализация.ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры);

КонецПроцедуры

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт
	
	ИсточникиДанных = Новый Соответствие;
	Возврат ИсточникиДанных;
	
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.ВозвратОСИзАренды2_4";
	
	ЗначенияПараметров = ЗначенияПараметровПроведения();
	ПереопределениеРасчетаПараметров = Новый Структура;
	
	ВЗапросеЕстьИсточник = Истина;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	ИначеЕсли ИмяРегистра = "ДокументыПоОС" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';
								|en = 'In document %ПолноеИмяДокумента%, adaptation of request for generating records of register %ИмяРегистра% is not implemented.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов"
		ИЛИ ИмяРегистра = "ДокументыПоОС" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ВЗапросеЕстьИсточник,
										ПереопределениеРасчетаПараметров);
	Иначе	
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ПереопределениеРасчетаПараметров);
	КонецЕсли; 

	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДопПараметры)
	
	ПроведениеДокументов.ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДопПараметры);
	
	СформироватьТаблицуВтСписокДокументов(Запрос);
	ПолучитьДанныеДокумента(Запрос);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Период  КАК Период,
	|	ДанныеДокумента.Номер   КАК Номер
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения(Реквизиты);
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла; 
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
КонецПроцедуры

Процедура СформироватьТаблицуВтСписокДокументов(Запрос)
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СписокДокументов.Ссылка КАК Ссылка,
	|	СписокДокументов.Организация КАК Организация,
	|	СписокДокументов.Дата КАК Период
	|
	|ПОМЕСТИТЬ ВтСписокДокументов
	|
	|ИЗ
	|	Документ.ВозвратОСИзАренды2_4 КАК СписокДокументов
	|
	|ГДЕ
	|	СписокДокументов.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|";
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПолучитьДанныеДокумента(Запрос)
	
	СписокЗапросов = Новый Массив;
	
	#Область ДанныеДокументаРеквизиты
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                  КАК Ссылка,
	|	ДанныеДокумента.Дата                    КАК Период,
	|	ДанныеДокумента.Номер                   КАК Номер,
	|	ДанныеДокумента.Организация             КАК Организация,
	|	ДанныеДокумента.Организация.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ДанныеДокумента.Организация.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета,
	|	ДанныеДокумента.Арендатор               КАК Арендатор,
	|	ДанныеДокумента.Подразделение           КАК Подразделение,
	|	ДанныеДокумента.МОЛ                     КАК МОЛ,
	|	ДанныеДокумента.АдресМестонахождения    КАК АдресМестонахождения,
	|	ДанныеДокумента.АдресМестонахожденияЗначение КАК АдресМестонахожденияЗначение,
	|	ДанныеДокумента.ПодразделениеПолучатель КАК ПодразделениеПолучатель,
	|	ДанныеДокумента.Ответственный           КАК Ответственный,
	|	ДанныеДокумента.Комментарий             КАК Комментарий,
	|	ДанныеДокумента.Проведен                КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления         КАК ПометкаУдаления,
	|	ДанныеДокумента.ОтражатьВРеглУчете      КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете       КАК ОтражатьВУпрУчете,
	|	ДанныеДокумента.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|	ДанныеДокумента.СтатьяРасходовБУ        КАК СтатьяРасходовБУ,
	|	ДанныеДокумента.АналитикаРасходовБУ     КАК АналитикаРасходовБУ,
	|	ДанныеДокумента.СтатьяРасходовУУ        КАК СтатьяРасходовУУ,
	|	ДанныеДокумента.АналитикаРасходовУУ     КАК АналитикаРасходовУУ,
	|	ДанныеДокумента.НачислениеАмортизацииБУ КАК НачислениеАмортизацииБУ,
	|	ДанныеДокумента.НачислениеАмортизацииУУ КАК НачислениеАмортизацииУУ,
	|	ДанныеДокумента.СобытиеОС               КАК СобытиеОС,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОСИзАренды) КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Договор                 КАК Договор,
	|	ДанныеДокумента.Договор <> ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) КАК ВозвратИзФинаренды,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.РасчетыМеждуОрганизациямиАрендатор
	|			ТОГДА ДанныеДокумента.Арендатор
	|		ИНАЧЕ ДанныеДокумента.Организация
	|	КОНЕЦ КАК ОрганизацияОтправитель,
	|
	|	ДанныеДокумента.РасчетыМеждуОрганизациямиАрендатор КАК РасчетыМеждуОрганизациямиАрендатор
	|
	|ПОМЕСТИТЬ ДанныеДокументаРеквизиты
	|
	|ИЗ
	|	Документ.ВозвратОСИзАренды2_4 КАК ДанныеДокумента
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка В (
	|		ВЫБРАТЬ
	|			ВтСписокДокументов.Ссылка
	|		ИЗ
	|			ВтСписокДокументов КАК ВтСписокДокументов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ДанныеДокументаТаблицаОС
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаДокумента.ОсновноеСредство КАК ОбъектУчета,
	|	ТаблицаДокумента.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаДокумента.ПорядокУчетаОС КАК ПорядокУчетаОС,
	|	ТаблицаДокумента.СтатьяРасходов КАК СтатьяРасходов,
	|	ТаблицаДокумента.АналитикаРасходов КАК АналитикаРасходов,
	|	ТаблицаДокумента.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
	|	ТаблицаДокумента.НастройкаСчетовУчета КАК НастройкаСчетовУчета
	|
	|ПОМЕСТИТЬ ДанныеДокументаТаблицаОС
	|
	|ИЗ
	|	Документ.ВозвратОСИзАренды2_4.ОС КАК ТаблицаДокумента
	|
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В (
	|		ВЫБРАТЬ
	|			ВтСписокДокументов.Ссылка
	|		ИЗ
	|			ВтСписокДокументов КАК ВтСписокДокументов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	ОсновноеСредство
	|";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	Запрос.Текст = СтрСоединить(СписокЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)

	ЗначенияПараметровПроведения = Новый Структура;
	ЗначенияПараметровПроведения.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ВозвратОСИзАренды2_4"));
	ЗначенияПараметровПроведения.Вставить("НазваниеДокумента", НСтр("ru = 'Возврат ОС из аренды';
																	|en = 'Return assets from lessee'"));
	
	ЗначенияПараметровПроведения.Вставить("ХО_Стоимость", Перечисления.ХозяйственныеОперации.ИзменениеПараметровСтоимостиОС);
	ЗначенияПараметровПроведения.Вставить("ХО_Амортизация", Перечисления.ХозяйственныеОперации.ИзменениеПараметровАмортизацииОС);
	ЗначенияПараметровПроведения.Вставить("НастройкаХО_Стоимость", Справочники.НастройкиХозяйственныхОпераций.ИзменениеПараметровСтоимостиОС);
	ЗначенияПараметровПроведения.Вставить("НастройкаХО_Амортизация", Справочники.НастройкиХозяйственныхОпераций.ИзменениеПараметровАмортизацииОС);
	ЗначенияПараметровПроведения.Вставить("СтатьяАП_ОС", ПланыВидовХарактеристик.СтатьиАктивовПассивов.ОсновныеСредства);
	
	ЗначенияПараметровПроведения.Вставить("ХО_ВозвратОСИзАренды", Перечисления.ХозяйственныеОперации.ВозвратОСИзАренды);
	ЗначенияПараметровПроведения.Вставить("НастройкаХО_ВозвратОСИзАренды", Справочники.НастройкиХозяйственныхОпераций.ВозвратОСИзАренды);
	ЗначенияПараметровПроведения.Вставить("НастройкаХО_ДосрочноеПрекращениеДоговораАренды", Справочники.НастройкиХозяйственныхОпераций.ДосрочноеПрекращениеДоговораАренды);
	ЗначенияПараметровПроведения.Вставить("НастройкаХО_ВключениеНГЛСВСтоимостьЧИА", Справочники.НастройкиХозяйственныхОпераций.ВключениеНГЛСВСтоимостьЧИА);
	
	ЗначенияПараметровПроведения.Вставить("ВалютаУпр", ЗначениеНастроекПовтИсп.ВалютаУправленческогоУчета());
	ЗначенияПараметровПроведения.Вставить("ТипыДоговоровПоФСБУ25", УчетАрендованныхОСКлиентСервер.ТипыДоговоровДоходнойАренды());
	
	Если Реквизиты <> Неопределено Тогда
		ЗначенияПараметровПроведения.Вставить("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	КонецЕсли;
	
	Возврат ЗначенияПараметровПроведения;
	
КонецФункции

Функция ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДокументыПоОС";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(ТаблицаОС.НомерСтроки-1, 0)     КАК НомерЗаписи,
	|	ДанныеДокумента.Дата                     КАК Дата,
	|	ДанныеДокумента.Ссылка                   КАК Ссылка,
	|	ДанныеДокумента.Организация              КАК Организация,
	|	ДанныеДокумента.ПодразделениеПолучатель  КАК Подразделение,
	|	ДанныеДокумента.Проведен                 КАК Проведен,
	|	&ХО_ВозвратОСИзАренды                    КАК ХозяйственнаяОперация,
	|	&ИдентификаторМетаданных                 КАК ТипСсылки,
	|	ДанныеДокумента.ОтражатьВРеглУчете       КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете        КАК ОтражатьВУпрУчете,
	|	ДанныеДокумента.СобытиеОС                КАК СобытиеОС,
	|	ТаблицаОС.ОсновноеСредство               КАК ОсновноеСредство,
	|	ЛОЖЬ                                     КАК ДополнительнаяЗапись
	|
	|ИЗ
	|	Документ.ВозвратОСИзАренды2_4 КАК ДанныеДокумента
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратОСИзАренды2_4.ОС КАК ТаблицаОС
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";	
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка          КАК Ссылка,
	|	ДанныеДокумента.Дата            КАК ДатаДокументаИБ,
	|	ДанныеДокумента.Номер           КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных        КАК ТипСсылки,
	|	ДанныеДокумента.Организация     КАК Организация,
	|	ДанныеДокумента.Арендатор       КАК Контрагент,
	|	ДанныеДокумента.Договор         КАК Договор,
	|	&ХО_ВозвратОСИзАренды           КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                    КАК НаправлениеДеятельности,
	|	ДанныеДокумента.ПодразделениеПолучатель КАК Подразделение,
	|	ДанныеДокумента.Ответственный   КАК Ответственный,
	|	ДанныеДокумента.Комментарий     КАК Комментарий,
	|	ДанныеДокумента.Проведен        КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления КАК ПометкаУдаления,
	|	ЛОЖЬ                            КАК ДополнительнаяЗапись,
	|	ДанныеДокумента.Дата            КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать                  КАК НомерПервичногоДокумента,
	|	ЛОЖЬ                            КАК СторноИсправление,
	|	НЕОПРЕДЕЛЕНО                    КАК СторнируемыйДокумент,
	|	НЕОПРЕДЕЛЕНО                    КАК ИсправляемыйДокумент,
	|	ДанныеДокумента.Дата            КАК ДатаОтраженияВУчете,
	|	НЕОПРЕДЕЛЕНО                    КАК Приоритет
	|
	|ИЗ
	|	Документ.ВозвратОСИзАренды2_4 КАК ДанныеДокумента
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ТекстЗапросаТаблицаМестонахождениеОС(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "МестонахождениеОС";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтРасширеннаяТаблицаОС(ТекстыЗапроса);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтМестонахождениеОС(ТекстыЗапроса, "ВтРасширеннаяТаблицаОС");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка               КАК Регистратор,
	|	ДанныеДокумента.Период               КАК Период,
	|	ДанныеДокумента.Организация          КАК Организация,
	|	МестонахождениеОС.ОсновноеСредство   КАК ОсновноеСредство,
	|	ДанныеДокумента.МОЛ                  КАК МОЛ,
	|	ДанныеДокумента.ПодразделениеПолучатель КАК Местонахождение,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.АдресМестонахождения <> """"
	|			ТОГДА ДанныеДокумента.АдресМестонахождения
	|		ИНАЧЕ МестонахождениеОС.АдресМестонахождения
	|	КОНЕЦ КАК АдресМестонахождения,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.АдресМестонахождения <> """"
	|			ТОГДА ДанныеДокумента.АдресМестонахожденияЗначение
	|		ИНАЧЕ МестонахождениеОС.АдресМестонахожденияЗначение
	|	КОНЕЦ КАК АдресМестонахожденияЗначение
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втМестонахождениеОС КАК МестонахождениеОС
	|		ПО МестонахождениеОС.Ссылка = ДанныеДокумента.Ссылка
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПорядокУчетаОС(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПорядокУчетаОС";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтРасширеннаяТаблицаОС(ТекстыЗапроса);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПорядокУчетаОС(ТекстыЗапроса, "ВтРасширеннаяТаблицаОС");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                    КАК Регистратор,
	|	ДанныеДокумента.Период                    КАК Период,
	|	ДанныеДокумента.Организация               КАК Организация,
	|	втПорядокУчетаОС.ОсновноеСредство         КАК ОсновноеСредство,
	|	ДанныеДокумента.ГруппаФинансовогоУчета    КАК ГруппаФинансовогоУчета,
	|	втПорядокУчетаОС.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	втПорядокУчетаОС.ПоказательНаработки      КАК ПоказательНаработки,
	|	втПорядокУчетаОС.ОбъемНаработки           КАК ОбъемНаработки,
	|	ВЫБОР КОГДА ДанныеДокумента.ВозвратИзФинаренды 
	|		И ТаблицаОС.ПорядокУчетаОС = ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаОСПриВозвратеИзАренды.УчитыватьВСоставеОС)
	|		ТОГДА ИСТИНА
	|	ИНАЧЕ
	|		ДанныеДокумента.ОтражатьВРеглУчете
	|	КОНЕЦ КАК ОтражатьВРеглУчете,
	|	ВЫБОР КОГДА ДанныеДокумента.ВозвратИзФинаренды 
	|		И ТаблицаОС.ПорядокУчетаОС = ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаОСПриВозвратеИзАренды.УчитыватьВСоставеОС)
	|		ТОГДА ИСТИНА
	|	ИНАЧЕ
	|		ДанныеДокумента.ОтражатьВУпрУчете
	|	КОНЕЦ КАК ОтражатьВУпрУчете
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПорядокУчетаОС КАК втПорядокУчетаОС
	|		ПО втПорядокУчетаОС.Ссылка = ДанныеДокумента.Ссылка
	|		И втПорядокУчетаОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|ГДЕ ДанныеДокумента.ВозвратИзФинаренды
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                  КАК Регистратор,
	|	ДанныеДокумента.Период                  КАК Период,
	|	ДанныеДокумента.Организация             КАК Организация,
	|	ПорядокУчетаОС.ОсновноеСредство         КАК ОсновноеСредство,
	|	ДанныеДокумента.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|	ПорядокУчетаОС.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	ПорядокУчетаОС.ПоказательНаработки      КАК ПоказательНаработки,
	|	ПорядокУчетаОС.ОбъемНаработки           КАК ОбъемНаработки,
	|	ДанныеДокумента.ОтражатьВРеглУчете      КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете       КАК ОтражатьВУпрУчете
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаОС КАК ПорядокУчетаОС
	|		ПО ПорядокУчетаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ НЕ ДанныеДокумента.ВозвратИзФинаренды
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПорядокУчетаОСУУ(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПорядокУчетаОСУУ";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтРасширеннаяТаблицаОС(ТекстыЗапроса);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтУзлыКомпонентыАмортизации(ТекстыЗапроса, "ВтРасширеннаяТаблицаОС");
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПорядокУчетаОСУУ(ТекстыЗапроса, "ВтРасширеннаяТаблицаОС");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                  КАК Регистратор,
	|	ДанныеДокумента.Период                  КАК Период,
	|	ДанныеДокумента.Организация             КАК Организация,
	|	ТаблицаОС.ОсновноеСредство              КАК ОсновноеСредство,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету) КАК Состояние,
	|	ДанныеДокумента.НачислениеАмортизацииУУ = ЗНАЧЕНИЕ(Перечисление.ВариантыИзмененияНачисленияАмортизации.Действует) КАК НачислятьАмортизациюУУ,
	|	ДанныеДокумента.СтатьяРасходовУУ        КАК СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходовУУ     КАК АналитикаРасходов
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасширеннаяТаблицаОС КАК ТаблицаОС
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ОтражатьВУпрУчете
	|	И НЕ ДанныеДокумента.ВозвратИзФинаренды
	|	И ДанныеДокумента.НачислениеАмортизацииУУ В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыИзмененияНачисленияАмортизации.Действует),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыИзмененияНачисленияАмортизации.Остановлено))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                  КАК Регистратор,
	|	ДанныеДокумента.Период                  КАК Период,
	|	ДанныеДокумента.Организация             КАК Организация,
	|	ТаблицаОС.ОсновноеСредство              КАК ОсновноеСредство,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету) КАК Состояние,
	|	ИСТИНА                                  КАК НачислятьАмортизациюУУ,
	|	втПорядокУчетаОСУУ.СтатьяРасходов       КАК СтатьяРасходов,
	|	втПорядокУчетаОСУУ.АналитикаРасходов    КАК АналитикаРасходов
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаОСУУ КАК втПорядокУчетаОСУУ
	|		ПО втПорядокУчетаОСУУ.Ссылка = ТаблицаОС.Ссылка
	|			И втПорядокУчетаОСУУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|ГДЕ
	|	ДанныеДокумента.ВозвратИзФинаренды
	|	И ТаблицаОС.ПорядокУчетаОС = ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаОСПриВозвратеИзАренды.УчитыватьВСоставеОС)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                  КАК Регистратор,
	|	ДанныеДокумента.Период                  КАК Период,
	|	ДанныеДокумента.Организация             КАК Организация,
	|	ТаблицаОС.ОсновноеСредство              КАК ОсновноеСредство,
	|	ВЫБОР КОГДА втПорядокУчетаОСУУ.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияОС.СнятоСУчета)
	|		ИНАЧЕ втПорядокУчетаОСУУ.Состояние
	|	КОНЕЦ                                   КАК Состояние,
	|	ЛОЖЬ                                    КАК НачислятьАмортизациюУУ,
	|	втПорядокУчетаОСУУ.СтатьяРасходов       КАК СтатьяРасходов,
	|	втПорядокУчетаОСУУ.АналитикаРасходов    КАК АналитикаРасходов
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаОСУУ КАК втПорядокУчетаОСУУ
	|		ПО втПорядокУчетаОСУУ.Ссылка = ТаблицаОС.Ссылка
	|			И втПорядокУчетаОСУУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|ГДЕ
	|	ДанныеДокумента.ВозвратИзФинаренды
	|	И ТаблицаОС.ПорядокУчетаОС В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаОСПриВозвратеИзАренды.УчитыватьВСоставеИнвестицииВАренду),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаОСПриВозвратеИзАренды.СписатьНаРасходы),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаОСПриВозвратеИзАренды.СписатьНаСтатьюАктивов))
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПорядокУчетаОСБУ(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПорядокУчетаОСБУ";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтРасширеннаяТаблицаОС(ТекстыЗапроса);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПорядокУчетаОСБУ(ТекстыЗапроса, "ВтРасширеннаяТаблицаОС");
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПервоначальныеСведенияОС(ТекстыЗапроса, "ВтРасширеннаяТаблицаОС");
	
	ТекстЗапроса = ВозвратОСИзАрендыЛокализация.ТекстЗапросаТаблицаПорядокУчетаОСБУ();
	
	Если ТекстЗапроса = Неопределено Тогда
		
		ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтУчетнаяПолитикаФинансовогоУчета(ТекстыЗапроса);
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка                  КАК Регистратор,
		|	ДанныеДокумента.Период                  КАК Период,
		|	ДанныеДокумента.Организация             КАК Организация,
		|	ТаблицаОС.ОсновноеСредство              КАК ОсновноеСредство,
		|
		|	ПорядокУчетаОСБУ.СостояниеБУ            КАК СостояниеБУ,
		|
		// НачислятьАмортизациюБУ
		|	ВЫБОР 
		|		КОГДА ДанныеДокумента.НачислениеАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.ВариантыИзмененияНачисленияАмортизации.Действует)
		|			И (ПорядокУчетаОСБУ.НачислятьАмортизациюБУ
		|				ИЛИ ПервоначальныеСведенияОС.ПорядокУчетаБУ В (
		|					ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеАмортизации),
		|					ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.АмортизацияНачислена)))
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК НачислятьАмортизациюБУ,
		|
		|	ДанныеДокумента.СтатьяРасходовБУ                       КАК СтатьяРасходовБУ,
		|	ДанныеДокумента.АналитикаРасходовБУ                    КАК АналитикаРасходовБУ
		|
		|ИЗ
		|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасширеннаяТаблицаОС КАК ТаблицаОС
		|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаОСБУ КАК ПорядокУчетаОСБУ
		|		ПО ПорядокУчетаОСБУ.Ссылка = ТаблицаОС.Ссылка
		|			И ПорядокУчетаОСБУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
		|		ПО ПервоначальныеСведенияОС.Ссылка = ТаблицаОС.Ссылка
		|			И ПервоначальныеСведенияОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтУчетнаяПолитикаФинансовогоУчета КАК ВтУчетнаяПолитикаФинансовогоУчета
		|		ПО ВтУчетнаяПолитикаФинансовогоУчета.Ссылка = ДанныеДокумента.Ссылка
		|
		|ГДЕ
		|	ДанныеДокумента.ОтражатьВРеглУчете
		|	И НЕ ДанныеДокумента.ВозвратИзФинаренды
		|	И ЕСТЬNULL(ВтУчетнаяПолитикаФинансовогоУчета.ПорядокУчетаВНАРегл, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАРегл.Независимый)
		|	И ДанныеДокумента.НачислениеАмортизацииБУ В (
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыИзмененияНачисленияАмортизации.Действует),
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыИзмененияНачисленияАмортизации.Остановлено))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка                  КАК Регистратор,
		|	ДанныеДокумента.Период                  КАК Период,
		|	ДанныеДокумента.Организация             КАК Организация,
		|	ТаблицаОС.ОсновноеСредство              КАК ОсновноеСредство,
		|
		|	ПорядокУчетаОСБУ.СостояниеБУ          КАК СостояниеБУ,
		|
		// НачислятьАмортизациюБУ
		|	ВЫБОР 
		|		КОГДА ДанныеДокумента.НачислениеАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.ВариантыИзмененияНачисленияАмортизации.Действует)
		|			И (ПорядокУчетаОСБУ.НачислятьАмортизациюБУ
		|				ИЛИ ПервоначальныеСведенияОС.ПорядокУчетаБУ В (
		|					ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеАмортизации),
		|					ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.АмортизацияНачислена)))
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК НачислятьАмортизациюБУ,
		|
		|	ДанныеДокумента.СтатьяРасходовУУ         КАК СтатьяРасходовБУ,
		|	ДанныеДокумента.АналитикаРасходовУУ      КАК АналитикаРасходовБУ
		|
		|ИЗ
		|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасширеннаяТаблицаОС КАК ТаблицаОС
		|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаОСБУ КАК ПорядокУчетаОСБУ
		|		ПО ПорядокУчетаОСБУ.Ссылка = ТаблицаОС.Ссылка
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
		|		ПО ПервоначальныеСведенияОС.Ссылка = ТаблицаОС.Ссылка
		|			И ПервоначальныеСведенияОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтУчетнаяПолитикаФинансовогоУчета КАК ВтУчетнаяПолитикаФинансовогоУчета
		|		ПО ВтУчетнаяПолитикаФинансовогоУчета.Ссылка = ДанныеДокумента.Ссылка
		|
		|ГДЕ
		|	ЕСТЬNULL(ВтУчетнаяПолитикаФинансовогоУчета.ПорядокУчетаВНАРегл, НЕОПРЕДЕЛЕНО) <> ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАРегл.Независимый)
		|	И НЕ ДанныеДокумента.ВозвратИзФинаренды
		|	И ДанныеДокумента.НачислениеАмортизацииУУ В (
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыИзмененияНачисленияАмортизации.Действует),
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыИзмененияНачисленияАмортизации.Остановлено))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка                  КАК Регистратор,
		|	ДанныеДокумента.Период                  КАК Период,
		|	ДанныеДокумента.Организация             КАК Организация,
		|	втПорядокУчетаОСБУ.ОсновноеСредство     КАК ОсновноеСредство,
		|
		|	втПорядокУчетаОСБУ.СостояниеБУ          КАК СостояниеБУ,
		|
		|	ИСТИНА КАК НачислятьАмортизациюБУ,
		|
		|	втПорядокУчетаОСБУ.СтатьяРасходовБУ       КАК СтатьяРасходовБУ,
		|	втПорядокУчетаОСБУ.АналитикаРасходовБУ    КАК АналитикаРасходовБУ
		|
		|ИЗ
		|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
		|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаОСБУ КАК втПорядокУчетаОСБУ
		|		ПО втПорядокУчетаОСБУ.Ссылка = ТаблицаОС.Ссылка
		|			И втПорядокУчетаОСБУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ втПервоначальныеСведенияОС КАК втПервоначальныеСведенияОС
		|		ПО втПервоначальныеСведенияОС.Ссылка = ТаблицаОС.Ссылка
		|			И втПервоначальныеСведенияОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
		|ГДЕ
		|	ДанныеДокумента.ВозвратИзФинаренды
		|	И ТаблицаОС.ПорядокУчетаОС = ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаОСПриВозвратеИзАренды.УчитыватьВСоставеОС)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка                  КАК Регистратор,
		|	ДанныеДокумента.Период                  КАК Период,
		|	ДанныеДокумента.Организация             КАК Организация,
		|	втПорядокУчетаОСБУ.ОсновноеСредство     КАК ОсновноеСредство,
		|
		|	ВЫБОР КОГДА втПорядокУчетаОСБУ.СостояниеБУ = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
		|		ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияОС.СнятоСУчета)
		|		ИНАЧЕ втПорядокУчетаОСБУ.СостояниеБУ
		|	КОНЕЦ                                   КАК СостояниеБУ,
		|
		|	ЛОЖЬ КАК НачислятьАмортизациюБУ,
		|
		|	втПорядокУчетаОСБУ.СтатьяРасходовБУ       КАК СтатьяРасходовБУ,
		|	втПорядокУчетаОСБУ.АналитикаРасходовБУ    КАК АналитикаРасходовБУ
		|
		|ИЗ
		|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
		|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаОСБУ КАК втПорядокУчетаОСБУ
		|		ПО втПорядокУчетаОСБУ.Ссылка = ТаблицаОС.Ссылка
		|			И втПорядокУчетаОСБУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ втПервоначальныеСведенияОС КАК втПервоначальныеСведенияОС
		|		ПО втПервоначальныеСведенияОС.Ссылка = ТаблицаОС.Ссылка
		|			И втПервоначальныеСведенияОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
		|ГДЕ
		|	ДанныеДокумента.ВозвратИзФинаренды
		|	И ТаблицаОС.ПорядокУчетаОС В (
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаОСПриВозвратеИзАренды.УчитыватьВСоставеИнвестицииВАренду),
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаОСПриВозвратеИзАренды.СписатьНаРасходы),
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаОСПриВозвратеИзАренды.СписатьНаСтатьюАктивов))
		|
		|";

	КонецЕсли;
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПараметрыАмортизацииОСБУ(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПараметрыАмортизацииОСБУ";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;

	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтРасширеннаяТаблицаОС(ТекстыЗапроса);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыАмортизацииОСБУ(ТекстыЗапроса, "ВтРасширеннаяТаблицаОС");
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтНаработкиОбъектовЭксплуатации(ТекстыЗапроса, "ВтРасширеннаяТаблицаОС");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка       КАК Регистратор,
	|	ДанныеДокумента.Период       КАК Период,
	|	ДанныеДокумента.Организация  КАК Организация,
	|	ДанныеДокумента.Период       КАК ДатаПоследнегоИзменения,
	|	ТаблицаОС.ОсновноеСредство   КАК ОсновноеСредство,
	|
	|	ИнвестицииВАренду.АрендныеПлатежиРегл КАК СтоимостьДляВычисленияАмортизации,
	|	0 КАК СтоимостьДляВычисленияАмортизацииЦФ,
	|	ЕСТЬNULL(СтоимостьОС.СтоимостьПР, 0) КАК ПРДляВычисленияАмортизации,
	|
	|	ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияБУ         КАК СрокПолезногоИспользованияБУ,
	|	ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияНУ         КАК СрокПолезногоИспользованияНУ,
	|	ПараметрыАмортизацииОСБУ.КоэффициентАмортизацииБУ             КАК КоэффициентАмортизацииБУ,
	|	ПараметрыАмортизацииОСБУ.КоэффициентУскорения                 КАК КоэффициентУскорения,
	|	ПараметрыАмортизацииОСБУ.ГрафикАмортизации                    КАК ГрафикАмортизации,
	|	ПараметрыАмортизацииОСБУ.СпециальныйКоэффициент               КАК СпециальныйКоэффициент,
	|	0                                                             КАК ЛиквидационнаяСтоимость,
	|
	// ОбъемПродукцииРаботДляВычисленияАмортизации
	|	ПараметрыАмортизацииОСБУ.ОбъемПродукцииРаботДляВычисленияАмортизации
	|		- ЕСТЬNULL(НаработкиОбъектовЭксплуатации.Значение, 0) КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|
	// СрокИспользованияДляВычисленияАмортизации
	|	ПараметрыАмортизацииОСБУ.СрокИспользованияДляВычисленияАмортизации
	|		- РАЗНОСТЬДАТ(ЕСТЬNULL(ПараметрыАмортизацииОСБУ.ДатаПоследнегоИзменения, ДанныеДокумента.Период), ДанныеДокумента.Период, МЕСЯЦ) КАК СрокИспользованияДляВычисленияАмортизации
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьОС
	|		ПО СтоимостьОС.Ссылка = ТаблицаОС.Ссылка
	|			И СтоимостьОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПараметрыАмортизацииОСБУ КАК ПараметрыАмортизацииОСБУ
	|		ПО (ПараметрыАмортизацииОСБУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтНаработкиОбъектовЭксплуатации КАК НаработкиОбъектовЭксплуатации
	|		ПО ТаблицаОС.ОсновноеСредство = НаработкиОбъектовЭксплуатации.ОбъектЭксплуатации
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПереданныеВАрендуОС КАК ВтПереданныеВАрендуОС
	|		ПО ВтПереданныеВАрендуОС.Ссылка = ТаблицаОС.Ссылка
	|			И ВтПереданныеВАрендуОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ИнвестицииВАренду КАК ИнвестицииВАренду
	|		ПО ИнвестицииВАренду.Ссылка = ТаблицаОС.Ссылка
	|			И ИнвестицииВАренду.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|
	|ГДЕ
	|	ВЫРАЗИТЬ(ВтПереданныеВАрендуОС.Договор КАК Справочник.ДоговорыКонтрагентов).ТипДоговора В (&ТипыДоговоровПоФСБУ25)
	|	И ВтПереданныеВАрендуОС.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПереданоВАренду)
	|	И ТаблицаОС.ПорядокУчетаОС = ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаОСПриВозвратеИзАренды.УчитыватьВСоставеОС)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПараметрыАмортизацииОСУУ(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПараметрыАмортизацииОСУУ";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;

	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтРасширеннаяТаблицаОС(ТекстыЗапроса);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтНаработкиОбъектовЭксплуатации(ТекстыЗапроса, "ВтРасширеннаяТаблицаОС");
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыАмортизацииОСУУ(ТекстыЗапроса, "ВтРасширеннаяТаблицаОС");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка       КАК Регистратор,
	|	ДанныеДокумента.Период       КАК Период,
	|	ДанныеДокумента.Организация  КАК Организация,
	|	ДанныеДокумента.Период       КАК ДатаПоследнегоИзменения,
	|	ТаблицаОС.ОсновноеСредство   КАК ОсновноеСредство,
	|
	|	ПараметрыАмортизацииОСУУ.СрокИспользования           КАК СрокИспользования,
	|	ПараметрыАмортизацииОСУУ.КоэффициентУскорения        КАК КоэффициентУскорения,
	|	ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации  КАК МетодНачисленияАмортизации,
	|	0 КАК ЛиквидационнаяСтоимость,
	|	0 КАК ЛиквидационнаяСтоимостьРегл,
	|
	|	ПараметрыАмортизацииОСУУ.ОбъемПродукцииРаботДляВычисленияАмортизации
	|		- ЕСТЬNULL(НаработкиОбъектовЭксплуатации.Значение, 0) КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|
	|	ПараметрыАмортизацииОСУУ.СрокИспользованияДляВычисленияАмортизации
	|		- РАЗНОСТЬДАТ(ЕСТЬNULL(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения, ДанныеДокумента.Период), ДанныеДокумента.Период, МЕСЯЦ) КАК СрокИспользованияДляВычисленияАмортизации,
	|
	|	ИнвестицииВАренду.АрендныеПлатежиУпр КАК СтоимостьДляВычисленияАмортизации,
	|	ИнвестицииВАренду.АрендныеПлатежиРегл КАК СтоимостьДляВычисленияАмортизацииРегл
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьОС
	|		ПО СтоимостьОС.Ссылка = ТаблицаОС.Ссылка
	|			И СтоимостьОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПараметрыАмортизацииОСУУ КАК ПараметрыАмортизацииОСУУ
	|		ПО ПараметрыАмортизацииОСУУ.Ссылка = ТаблицаОС.Ссылка
	|			И ПараметрыАмортизацииОСУУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтНаработкиОбъектовЭксплуатации КАК НаработкиОбъектовЭксплуатации
	|		ПО НаработкиОбъектовЭксплуатации.Ссылка = ТаблицаОС.Ссылка
	|			И НаработкиОбъектовЭксплуатации.ОбъектЭксплуатации = ТаблицаОС.ОсновноеСредство
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПереданныеВАрендуОС КАК ВтПереданныеВАрендуОС
	|		ПО ВтПереданныеВАрендуОС.Ссылка = ТаблицаОС.Ссылка
	|			И ВтПереданныеВАрендуОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ИнвестицииВАренду КАК ИнвестицииВАренду
	|		ПО ИнвестицииВАренду.Ссылка = ТаблицаОС.Ссылка
	|			И ИнвестицииВАренду.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|
	|ГДЕ
	|	ВЫРАЗИТЬ(ВтПереданныеВАрендуОС.Договор КАК Справочник.ДоговорыКонтрагентов).ТипДоговора В (&ТипыДоговоровПоФСБУ25)
	|	И ВтПереданныеВАрендуОС.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПереданоВАренду)
	|	И ТаблицаОС.ПорядокУчетаОС = ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаОСПриВозвратеИзАренды.УчитыватьВСоставеОС)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПереданныеВАрендуОС(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПереданныеВАрендуОС";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтРасширеннаяТаблицаОС(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка      КАК Регистратор,
	|	ДанныеДокумента.Период      КАК Период,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство  КАК ОсновноеСредство
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасширеннаяТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры	

Процедура ТекстЗапросаТаблицаСтоимостьОС(ТекстыЗапроса)

	ИмяРегистра = "СтоимостьОС";

	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтРасширеннаяТаблицаОС(ТекстыЗапроса);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыУчетаОС(ТекстыЗапроса, "ВтРасширеннаяТаблицаОС");
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПереданныеВАрендуОС(ТекстыЗапроса, "ВтРасширеннаяТаблицаОС");
	
	СписокЗапросов = Новый Массив;
	
	#Область ВозвратИзФинАренды
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)             КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                             КАК Регистратор,
	|	ДанныеДокумента.Период                             КАК Период,
	|	&ХО_ВозвратОСИзАренды                              КАК ХозяйственнаяОперация,
	|	ТаблицаОС.ОсновноеСредство                         КАК ОсновноеСредство,
	|	ДанныеДокумента.Организация                        КАК Организация,
	|	ДанныеДокумента.ПодразделениеПолучатель            КАК Подразделение,
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности         КАК НаправлениеДеятельности,
	|	ДанныеДокумента.ГруппаФинансовогоУчета             КАК ГруппаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО                                       КАК Арендатор,
	|
	|	ИнвестицииВАренду.АрендныеПлатежиУпр
	|		+ ИнвестицииВАренду.ПроцентныйДисконтУпр
	|		+ ИнвестицииВАренду.НГЛСУпр                    КАК Стоимость,
	|
	|	ИнвестицииВАренду.АрендныеПлатежиРегл
	|		+ ИнвестицииВАренду.ПроцентныйДисконтРегл
	|		+ ИнвестицииВАренду.НГЛСРегл                   КАК СтоимостьРегл,
	|
	|	0                                                  КАК СтоимостьНУ,
	|	0                                                  КАК СтоимостьПР,
	|
	|	ИнвестицииВАренду.АрендныеПлатежиРегл
	|		+ ИнвестицииВАренду.ПроцентныйДисконтРегл
	|		+ ИнвестицииВАренду.НГЛСРегл                   КАК СтоимостьВР,
	|
	|	0                                                  КАК СтоимостьЦФ,
	|	0                                                  КАК СтоимостьНУЦФ,
	|	0                                                  КАК СтоимостьПРЦФ,
	|	0                                                  КАК СтоимостьВРЦФ,
	|	0                                                  КАК АмортизационнаяПремия,
	|	0                                                  КАК ПредварительнаяСтоимость,
	|	0                                                  КАК РезервПереоценкиСтоимости,
	|	0                                                  КАК ЗалоговаяСтоимость,
	|
	|	ДанныеДокумента.Арендатор                          КАК КорОрганизация,
	|	ДанныеДокумента.Подразделение                      КАК КорПодразделение,
	|	ДанныеДокумента.Арендатор                          КАК КорКонтрагент,
	|	НЕОПРЕДЕЛЕНО                                       КАК КорНаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                       КАК КорГруппаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО                                       КАК КорСтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                                       КАК КорАналитикаРасходов,
	|	НЕ ИнвестицииВАренду.ОбъектУчета ЕСТЬ NULL         КАК РасчетСтоимости,
	|	ДанныеДокумента.ОтражатьВРеглУчете                 КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете                  КАК ОтражатьВУпрУчете,
	|	
	|	ТаблицаОС.ИдентификаторСтроки                      КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_ВозвратОСИзАренды                     КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПараметрыУчетаОС КАК ВтПараметрыУчетаОС
	|		ПО ВтПараметрыУчетаОС.Ссылка = ТаблицаОС.Ссылка
	|			И ВтПараметрыУчетаОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПереданныеВАрендуОС КАК ВтПереданныеВАрендуОС
	|		ПО ВтПереданныеВАрендуОС.Ссылка = ТаблицаОС.Ссылка
	|			И ВтПереданныеВАрендуОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ИнвестицииВАренду КАК ИнвестицииВАренду
	|		ПО ИнвестицииВАренду.Ссылка = ТаблицаОС.Ссылка
	|			И ИнвестицииВАренду.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|
	|ГДЕ
	|	ВЫРАЗИТЬ(ВтПереданныеВАрендуОС.Договор КАК Справочник.ДоговорыКонтрагентов).ТипДоговора В (&ТипыДоговоровПоФСБУ25)
	|	И ВтПереданныеВАрендуОС.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПереданоВАренду)
	|	И ТаблицаОС.ПорядокУчетаОС = ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаОСПриВозвратеИзАренды.УчитыватьВСоставеОС)
	|";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область ВозвратИзАренды
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)             КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                             КАК Регистратор,
	|	ДанныеДокумента.Период                             КАК Период,
	|	&ХО_Стоимость                                      КАК ХозяйственнаяОперация,
	|	ТаблицаОС.ОсновноеСредство                         КАК ОсновноеСредство,
	|	ДанныеДокумента.Организация                        КАК Организация,
	|	ДанныеДокумента.Подразделение                      КАК Подразделение,
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности         КАК НаправлениеДеятельности,
	|	ВтПараметрыУчетаОС.ГруппаФинансовогоУчета          КАК ГруппаФинансовогоУчета,
	|	ДанныеДокумента.Арендатор                          КАК Арендатор,
	|
	|	СтоимостьОС.Стоимость                              КАК Стоимость,
	|	СтоимостьОС.СтоимостьРегл                          КАК СтоимостьРегл,
	|	СтоимостьОС.СтоимостьНУ                            КАК СтоимостьНУ,
	|	СтоимостьОС.СтоимостьПР                            КАК СтоимостьПР,
	|	СтоимостьОС.СтоимостьВР                            КАК СтоимостьВР,
	|	СтоимостьОС.СтоимостьЦФ                            КАК СтоимостьЦФ,
	|	СтоимостьОС.СтоимостьНУЦФ                          КАК СтоимостьНУЦФ,
	|	СтоимостьОС.СтоимостьПРЦФ                          КАК СтоимостьПРЦФ,
	|	СтоимостьОС.СтоимостьВРЦФ                          КАК СтоимостьВРЦФ,
	|
	|	СтоимостьОС.АмортизационнаяПремия                  КАК АмортизационнаяПремия,
	|	СтоимостьОС.ПредварительнаяСтоимость               КАК ПредварительнаяСтоимость,
	|	СтоимостьОС.РезервПереоценкиСтоимости              КАК РезервПереоценкиСтоимости,
	|	0                                                  КАК ЗалоговаяСтоимость,
	|
	|	ДанныеДокумента.Организация                        КАК КорОрганизация,
	|	ДанныеДокумента.ПодразделениеПолучатель            КАК КорПодразделение,
	|	НЕОПРЕДЕЛЕНО                                       КАК КорКонтрагент,
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности         КАК КорНаправлениеДеятельности,
	|	ВтПараметрыУчетаОС.ГруппаФинансовогоУчета          КАК КорГруппаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО                                       КАК КорСтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                                       КАК КорАналитикаРасходов,
	|	НЕ СтоимостьОС.ОбъектУчета ЕСТЬ NULL               КАК РасчетСтоимости,
	|	ДанныеДокумента.ОтражатьВРеглУчете                 КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете                  КАК ОтражатьВУпрУчете,
	|	
	|	ТаблицаОС.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_Стоимость КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасширеннаяТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПараметрыУчетаОС КАК ВтПараметрыУчетаОС
	|		ПО ВтПараметрыУчетаОС.Ссылка = ТаблицаОС.Ссылка
	|			И ВтПараметрыУчетаОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьОС
	|		ПО СтоимостьОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И СтоимостьОС.Ссылка = ТаблицаОС.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПереданныеВАрендуОС КАК ВтПереданныеВАрендуОС
	|		ПО ВтПереданныеВАрендуОС.Ссылка = ТаблицаОС.Ссылка
	|			И ВтПереданныеВАрендуОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|ГДЕ
	|	НЕ ЕСТЬNULL(ВЫРАЗИТЬ(ВтПереданныеВАрендуОС.Договор КАК Справочник.ДоговорыКонтрагентов).ТипДоговора, НЕОПРЕДЕЛЕНО) В (&ТипыДоговоровПоФСБУ25)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)             КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                             КАК Регистратор,
	|	ДанныеДокумента.Период                             КАК Период,
	|	&ХО_Стоимость                                      КАК ХозяйственнаяОперация,
	|	ТаблицаОС.ОсновноеСредство                         КАК ОсновноеСредство,
	|	ДанныеДокумента.Организация                        КАК Организация,
	|	ДанныеДокумента.ПодразделениеПолучатель            КАК Подразделение,
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности         КАК НаправлениеДеятельности,
	|	ДанныеДокумента.ГруппаФинансовогоУчета             КАК ГруппаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО                                       КАК Арендатор,
	|
	|	СтоимостьОС.Стоимость                              КАК Стоимость,
	|	СтоимостьОС.СтоимостьРегл                          КАК СтоимостьРегл,
	|	СтоимостьОС.СтоимостьНУ                            КАК СтоимостьНУ,
	|	СтоимостьОС.СтоимостьПР                            КАК СтоимостьПР,
	|	СтоимостьОС.СтоимостьВР                            КАК СтоимостьВР,
	|	СтоимостьОС.СтоимостьЦФ                            КАК СтоимостьЦФ,
	|	СтоимостьОС.СтоимостьНУЦФ                          КАК СтоимостьНУЦФ,
	|	СтоимостьОС.СтоимостьПРЦФ                          КАК СтоимостьПРЦФ,
	|	СтоимостьОС.СтоимостьВРЦФ                          КАК СтоимостьВРЦФ,
	|	СтоимостьОС.АмортизационнаяПремия                  КАК АмортизационнаяПремия,
	|	СтоимостьОС.ПредварительнаяСтоимость               КАК ПредварительнаяСтоимость,
	|	СтоимостьОС.РезервПереоценкиСтоимости              КАК РезервПереоценкиСтоимости,
	|	0                                                  КАК ЗалоговаяСтоимость,
	|
	|	ДанныеДокумента.Арендатор                          КАК КорОрганизация,
	|	ДанныеДокумента.Подразделение                      КАК КорПодразделение,
	|	ДанныеДокумента.Арендатор                          КАК КорКонтрагент,
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности         КАК КорНаправлениеДеятельности,
	|	ВтПараметрыУчетаОС.ГруппаФинансовогоУчета          КАК КорГруппаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО                                       КАК КорСтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                                       КАК КорАналитикаРасходов,
	|	НЕ СтоимостьОС.ОбъектУчета ЕСТЬ NULL               КАК РасчетСтоимости,
	|	ДанныеДокумента.ОтражатьВРеглУчете                 КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете                  КАК ОтражатьВУпрУчете,
	|	
	|	ТаблицаОС.ИдентификаторСтроки                      КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_Стоимость                             КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасширеннаяТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПараметрыУчетаОС КАК ВтПараметрыУчетаОС
	|		ПО ВтПараметрыУчетаОС.Ссылка = ТаблицаОС.Ссылка
	|			И ВтПараметрыУчетаОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьОС
	|		ПО СтоимостьОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И СтоимостьОС.Ссылка = ТаблицаОС.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПереданныеВАрендуОС КАК ВтПереданныеВАрендуОС
	|		ПО ВтПереданныеВАрендуОС.Ссылка = ТаблицаОС.Ссылка
	|			И ВтПереданныеВАрендуОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|ГДЕ
	|	НЕ ЕСТЬNULL(ВЫРАЗИТЬ(ВтПереданныеВАрендуОС.Договор КАК Справочник.ДоговорыКонтрагентов).ТипДоговора, НЕОПРЕДЕЛЕНО) В (&ТипыДоговоровПоФСБУ25)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)             КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                             КАК Регистратор,
	|	ДанныеДокумента.Период                             КАК Период,
	|	&ХО_Стоимость                                      КАК ХозяйственнаяОперация,
	|	ТаблицаОС.ОсновноеСредство                         КАК ОсновноеСредство,
	|	ДанныеДокумента.Организация                        КАК Организация,
	|	ДанныеДокумента.Подразделение                      КАК Подразделение,
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности         КАК НаправлениеДеятельности,
	|	ВтПараметрыУчетаОС.ГруппаФинансовогоУчета          КАК ГруппаФинансовогоУчета,
	|	ДанныеДокумента.Арендатор                          КАК Арендатор,
	|
	|	0 КАК Стоимость,
	|	0 КАК СтоимостьРегл,
	|	СтоимостьОС.СтоимостьНУ                            КАК СтоимостьНУ,
	|	СтоимостьОС.СтоимостьПР                            КАК СтоимостьПР,
	|	СтоимостьОС.СтоимостьВР                            КАК СтоимостьВР,
	|
	|	0 КАК СтоимостьЦФ,
	|	0 КАК СтоимостьНУЦФ,
	|	0 КАК СтоимостьПРЦФ,
	|	0 КАК СтоимостьВРЦФ,
	|
	|	0 КАК АмортизационнаяПремия,
	|	0 КАК ПредварительнаяСтоимость,
	|	0 КАК РезервПереоценкиСтоимости,
	|	0 КАК ЗалоговаяСтоимость,
	|
	|	ДанныеДокумента.Организация                        КАК КорОрганизация,
	|	ДанныеДокумента.ПодразделениеПолучатель            КАК КорПодразделение,
	|	НЕОПРЕДЕЛЕНО                                       КАК КорКонтрагент,
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности         КАК КорНаправлениеДеятельности,
	|	ВтПараметрыУчетаОС.ГруппаФинансовогоУчета          КАК КорГруппаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО                                       КАК КорСтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                                       КАК КорАналитикаРасходов,
	|	НЕ СтоимостьОС.ОбъектУчета ЕСТЬ NULL               КАК РасчетСтоимости,
	|	ДанныеДокумента.ОтражатьВРеглУчете                 КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете                  КАК ОтражатьВУпрУчете,
	|	
	|	ТаблицаОС.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_Стоимость КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПараметрыУчетаОС КАК ВтПараметрыУчетаОС
	|		ПО ВтПараметрыУчетаОС.Ссылка = ТаблицаОС.Ссылка
	|			И ВтПараметрыУчетаОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьОС
	|		ПО СтоимостьОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И СтоимостьОС.Ссылка = ТаблицаОС.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПереданныеВАрендуОС КАК ВтПереданныеВАрендуОС
	|		ПО ВтПереданныеВАрендуОС.Ссылка = ТаблицаОС.Ссылка
	|			И ВтПереданныеВАрендуОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|ГДЕ
	|	ЕСТЬNULL(ВЫРАЗИТЬ(ВтПереданныеВАрендуОС.Договор КАК Справочник.ДоговорыКонтрагентов).ТипДоговора, НЕОПРЕДЕЛЕНО) В (&ТипыДоговоровПоФСБУ25)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)             КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                             КАК Регистратор,
	|	ДанныеДокумента.Период                             КАК Период,
	|	&ХО_Стоимость                                      КАК ХозяйственнаяОперация,
	|	ТаблицаОС.ОсновноеСредство                         КАК ОсновноеСредство,
	|	ДанныеДокумента.Организация                        КАК Организация,
	|	ДанныеДокумента.ПодразделениеПолучатель            КАК Подразделение,
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности         КАК НаправлениеДеятельности,
	|	ДанныеДокумента.ГруппаФинансовогоУчета             КАК ГруппаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО                                       КАК Арендатор,
	|
	|	0 КАК Стоимость,
	|	0 КАК СтоимостьРегл,
	|	СтоимостьОС.СтоимостьНУ                            КАК СтоимостьНУ,
	|	СтоимостьОС.СтоимостьПР                            КАК СтоимостьПР,
	|	СтоимостьОС.СтоимостьВР                            КАК СтоимостьВР,
	|
	|	0 КАК СтоимостьЦФ,
	|	0 КАК СтоимостьНУЦФ,
	|	0 КАК СтоимостьПРЦФ,
	|	0 КАК СтоимостьВРЦФ,
	|
	|	0 КАК АмортизационнаяПремия,
	|	0 КАК ПредварительнаяСтоимость,
	|	0 КАК РезервПереоценкиСтоимости,
	|	0 КАК ЗалоговаяСтоимость,
	|
	|	ДанныеДокумента.Арендатор                          КАК КорОрганизация,
	|	ДанныеДокумента.Подразделение                      КАК КорПодразделение,
	|	ДанныеДокумента.Арендатор                          КАК КорКонтрагент,
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности         КАК КорНаправлениеДеятельности,
	|	ВтПараметрыУчетаОС.ГруппаФинансовогоУчета          КАК КорГруппаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО                                       КАК КорСтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                                       КАК КорАналитикаРасходов,
	|	НЕ СтоимостьОС.ОбъектУчета ЕСТЬ NULL               КАК РасчетСтоимости,
	|	ДанныеДокумента.ОтражатьВРеглУчете                 КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете                  КАК ОтражатьВУпрУчете,
	|	
	|	ТаблицаОС.ИдентификаторСтроки                      КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_Стоимость                             КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПараметрыУчетаОС КАК ВтПараметрыУчетаОС
	|		ПО ВтПараметрыУчетаОС.Ссылка = ТаблицаОС.Ссылка
	|			И ВтПараметрыУчетаОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьОС
	|		ПО СтоимостьОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И СтоимостьОС.Ссылка = ТаблицаОС.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПереданныеВАрендуОС КАК ВтПереданныеВАрендуОС
	|		ПО ВтПереданныеВАрендуОС.Ссылка = ТаблицаОС.Ссылка
	|			И ВтПереданныеВАрендуОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|ГДЕ
	|	ЕСТЬNULL(ВЫРАЗИТЬ(ВтПереданныеВАрендуОС.Договор КАК Справочник.ДоговорыКонтрагентов).ТипДоговора, НЕОПРЕДЕЛЕНО) В (&ТипыДоговоровПоФСБУ25)
	|";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область СписаниеЗалоговойСтоимости
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)             КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                             КАК Регистратор,
	|	ДанныеДокумента.Период                             КАК Период,
	|	&ХО_Стоимость                                      КАК ХозяйственнаяОперация,
	|	ТаблицаОС.ОсновноеСредство                         КАК ОсновноеСредство,
	|	ДанныеДокумента.Организация                        КАК Организация,
	|	ДанныеДокумента.Подразделение                      КАК Подразделение,
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности         КАК НаправлениеДеятельности,
	|	ВтПараметрыУчетаОС.ГруппаФинансовогоУчета          КАК ГруппаФинансовогоУчета,
	|	ДанныеДокумента.Арендатор                          КАК Арендатор,
	|
	|	0                                                  КАК Стоимость,
	|	0                                                  КАК СтоимостьРегл,
	|	0                                                  КАК СтоимостьНУ,
	|	0                                                  КАК СтоимостьПР,
	|	0                                                  КАК СтоимостьВР,
	|	0                                                  КАК СтоимостьЦФ,
	|	0                                                  КАК СтоимостьНУЦФ,
	|	0                                                  КАК СтоимостьПРЦФ,
	|	0                                                  КАК СтоимостьВРЦФ,
	|	0                                                  КАК АмортизационнаяПремия,
	|	0                                                  КАК ПредварительнаяСтоимость,
	|	0                                                  КАК РезервПереоценкиСтоимости,
	|	СтоимостьОС.ЗалоговаяСтоимость                     КАК ЗалоговаяСтоимость,
	|
	|	ДанныеДокумента.Организация                        КАК КорОрганизация,
	|	ВтПараметрыУчетаОС.ПодразделениеАрендатора         КАК КорПодразделение,
	|	НЕОПРЕДЕЛЕНО                                       КАК КорКонтрагент,
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности         КАК КорНаправлениеДеятельности,
	|	ВтПараметрыУчетаОС.ГруппаФинансовогоУчета          КАК КорГруппаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО                                       КАК КорСтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                                       КАК КорАналитикаРасходов,
	|	ИСТИНА                                             КАК РасчетСтоимости,
	|	ДанныеДокумента.ОтражатьВРеглУчете                 КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете                  КАК ОтражатьВУпрУчете,
	|	
	|	ТаблицаОС.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_Стоимость КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасширеннаяТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПараметрыУчетаОС КАК ВтПараметрыУчетаОС
	|		ПО ВтПараметрыУчетаОС.Ссылка = ТаблицаОС.Ссылка
	|			И ВтПараметрыУчетаОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьОС
	|		ПО СтоимостьОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И СтоимостьОС.Ссылка = ТаблицаОС.Ссылка
	|ГДЕ
	|	СтоимостьОС.ЗалоговаяСтоимость <> 0
	|";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(СписокЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);

КонецПроцедуры

Процедура ТекстЗапросаТаблицаАмортизацияОС(ТекстыЗапроса)

	ИмяРегистра = "АмортизацияОС";
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтРасширеннаяТаблицаОС(ТекстыЗапроса);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыУчетаОС(ТекстыЗапроса, "ВтРасширеннаяТаблицаОС");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)    КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                    КАК Регистратор,
	|	ДанныеДокумента.Период                    КАК Период,
	|	&ХО_Амортизация                           КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация               КАК Организация,
	|	ТаблицаОС.ОсновноеСредство                КАК ОсновноеСредство,
	|	ДанныеДокумента.Подразделение             КАК Подразделение,
	|	ВтПараметрыУчетаОС.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	АмортизацияОС.Амортизация                 КАК Амортизация,
	|	АмортизацияОС.АмортизацияРегл             КАК АмортизацияРегл,
	|	АмортизацияОС.АмортизацияНУ               КАК АмортизацияНУ,
	|	АмортизацияОС.АмортизацияПР               КАК АмортизацияПР,
	|	АмортизацияОС.АмортизацияВР               КАК АмортизацияВР,
	|	АмортизацияОС.АмортизацияЦФ               КАК АмортизацияЦФ,
	|	АмортизацияОС.АмортизацияНУЦФ             КАК АмортизацияНУЦФ,
	|	АмортизацияОС.АмортизацияПРЦФ             КАК АмортизацияПРЦФ,
	|	АмортизацияОС.АмортизацияВРЦФ             КАК АмортизацияВРЦФ,
	|	АмортизацияОС.РезервПереоценкиАмортизации КАК РезервПереоценкиАмортизации,
	|	ДанныеДокумента.Организация               КАК КорОрганизация,
	|	ДанныеДокумента.ПодразделениеПолучатель   КАК КорПодразделение,
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	ДанныеДокумента.ГруппаФинансовогоУчета    КАК КорГруппаФинансовогоУчета,
	|	АмортизацияОС.НачислятьИзнос              КАК НачислятьИзнос,
	|	
	|	ТаблицаОС.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_Амортизация КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасширеннаяТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПараметрыУчетаОС КАК ВтПараметрыУчетаОС
	|		ПО ВтПараметрыУчетаОС.Ссылка = ТаблицаОС.Ссылка
	|			И ВтПараметрыУчетаОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияОС
	|		ПО АмортизацияОС.Ссылка = ТаблицаОС.Ссылка
	|			И АмортизацияОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)    КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                    КАК Регистратор,
	|	ДанныеДокумента.Период                    КАК Период,
	|	&ХО_Амортизация                           КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация               КАК Организация,
	|	ТаблицаОС.ОсновноеСредство                КАК ОсновноеСредство,
	|	ДанныеДокумента.ПодразделениеПолучатель   КАК Подразделение,
	|	ДанныеДокумента.ГруппаФинансовогоУчета    КАК ГруппаФинансовогоУчета,
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	АмортизацияОС.Амортизация                 КАК Амортизация,
	|	АмортизацияОС.АмортизацияРегл             КАК АмортизацияРегл,
	|	АмортизацияОС.АмортизацияНУ               КАК АмортизацияНУ,
	|	АмортизацияОС.АмортизацияПР               КАК АмортизацияПР,
	|	АмортизацияОС.АмортизацияВР               КАК АмортизацияВР,
	|	АмортизацияОС.АмортизацияЦФ               КАК АмортизацияЦФ,
	|	АмортизацияОС.АмортизацияНУЦФ             КАК АмортизацияНУЦФ,
	|	АмортизацияОС.АмортизацияПРЦФ             КАК АмортизацияПРЦФ,
	|	АмортизацияОС.АмортизацияВРЦФ             КАК АмортизацияВРЦФ,
	|	АмортизацияОС.РезервПереоценкиАмортизации КАК РезервПереоценкиАмортизации,
	|	ДанныеДокумента.Организация               КАК КорОрганизация,
	|	ДанныеДокумента.Подразделение             КАК КорПодразделение,
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	ВтПараметрыУчетаОС.ГруппаФинансовогоУчета КАК КорГруппаФинансовогоУчета,
	|	АмортизацияОС.НачислятьИзнос              КАК НачислятьИзнос,
	|	
	|	ТаблицаОС.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_Амортизация КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасширеннаяТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПараметрыУчетаОС КАК ВтПараметрыУчетаОС
	|		ПО ВтПараметрыУчетаОС.Ссылка = ТаблицаОС.Ссылка
	|			И ВтПараметрыУчетаОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияОС
	|		ПО АмортизацияОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И АмортизацияОС.Ссылка = ТаблицаОС.Ссылка
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаДвиженияДоходыРасходыПрочиеАктивыПассивы(ТекстыЗапроса)

	ИмяРегистра = "ДвиженияДоходыРасходыПрочиеАктивыПассивы";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтРасширеннаяТаблицаОС(ТекстыЗапроса);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыУчетаОС(ТекстыЗапроса, "ВтРасширеннаяТаблицаОС");
	
	ТекстЗапроса =
	"// Перемещение амортизации
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка            КАК Регистратор,
	|	ДанныеДокумента.Период            КАК Период,
	|	&ХО_Амортизация                   КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.ОрганизацияОтправитель КАК Организация,
	|
	|	ДанныеДокумента.Подразделение     КАК Подразделение,
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК Статья,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаРасходов,
	|	ТаблицаОС.ОсновноеСредство        КАК АналитикаАктивовПассивов,
	|	ВтПараметрыУчетаОС.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|
	|	ДанныеДокумента.ПодразделениеПолучатель КАК КорПодразделение,
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаРасходов,
	|	ТаблицаОС.ОсновноеСредство        КАК КорАналитикаАктивовПассивов,
	|	ВтПараметрыУчетаОС.ГруппаФинансовогоУчета КАК КорГруппаФинансовогоУчета,
	|
	|	ДанныеДокумента.Организация       КАК КорОрганизация,
	|
	|	АмортизацияОС.Амортизация         КАК Сумма,
	|	АмортизацияОС.Амортизация         КАК СуммаУпр,
	|	АмортизацияОС.АмортизацияРегл + АмортизацияОС.АмортизацияЦФ КАК СуммаРегл
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасширеннаяТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПараметрыУчетаОС КАК ВтПараметрыУчетаОС
	|		ПО ВтПараметрыУчетаОС.Ссылка = ТаблицаОС.Ссылка
	|			И ВтПараметрыУчетаОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияОС
	|		ПО АмортизацияОС.Ссылка = ТаблицаОС.Ссылка
	|			И АмортизацияОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Перемещение стоимости
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка            КАК Регистратор,
	|	ДанныеДокумента.Период            КАК Период,
	|	&ХО_Стоимость                     КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.ОрганизацияОтправитель КАК Организация,
	|
	|	ДанныеДокумента.Подразделение     КАК Подразделение,
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК Статья,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаРасходов,
	|	ТаблицаОС.ОсновноеСредство        КАК АналитикаАктивовПассивов,
	|	ВтПараметрыУчетаОС.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|
	|	ДанныеДокумента.ПодразделениеПолучатель КАК КорПодразделение,
	|	ВтПараметрыУчетаОС.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаРасходов,
	|	ТаблицаОС.ОсновноеСредство        КАК КорАналитикаАктивовПассивов,
	|	ВтПараметрыУчетаОС.ГруппаФинансовогоУчета КАК КорГруппаФинансовогоУчета,
	|
	|	ДанныеДокумента.Организация       КАК КорОрганизация,
	|
	|	СтоимостьОС.Стоимость             КАК Сумма,
	|	СтоимостьОС.Стоимость             КАК СуммаУпр,
	|	СтоимостьОС.СтоимостьРегл + СтоимостьОС.СтоимостьЦФ КАК СуммаРегл
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасширеннаяТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПараметрыУчетаОС КАК ВтПараметрыУчетаОС
	|		ПО ВтПараметрыУчетаОС.Ссылка = ТаблицаОС.Ссылка
	|			И ВтПараметрыУчетаОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьОС
	|		ПО СтоимостьОС.Ссылка = ТаблицаОС.Ссылка
	|			И СтоимостьОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаГрафикОплатУслугПоДоходнойАренде(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ГрафикОплатУслугПоДоходнойАренде";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтТаблицаОСПоФСБУ25(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаОС.Ссылка КАК АктуальныеУсловияДоговора,
	|	ТаблицаОС.Период КАК Период,
	|	ТаблицаОС.Договор КАК Договор,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаОС.Период КАК Дата,
	|	ТаблицаОС.Период КАК ДатаИзменения,
	|	0 КАК УслугаПоАренде,
	|	0 КАК УслугаПоАрендеНДС
	|ИЗ
	|	втТаблицаОСПоФСБУ25 КАК ТаблицаОС";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаГрафикНачисленияУслугПоДоходнойАренде(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ГрафикНачисленияУслугПоДоходнойАренде";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтТаблицаОСПоФСБУ25(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаОС.Ссылка КАК АктуальныеУсловияДоговора,
	|	ТаблицаОС.Период КАК Период,
	|	ТаблицаОС.Договор КАК Договор,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаОС.Период КАК Дата,
	|	ТаблицаОС.Период КАК ДатаИзменения,
	|	0 КАК УслугаПоАренде,
	|	0 КАК УслугаПоАрендеНДС
	|ИЗ
	|	втТаблицаОСПоФСБУ25 КАК ТаблицаОС";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаГрафикНачисленияПроцентовПоДоходнойАренде(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ГрафикНачисленияПроцентовПоДоходнойАренде";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтТаблицаОСПоФСБУ25(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ГрафикНачисленияПроцентовПоДоходнойАренде.ДатаИзменения КАК ДатаИзменения,
	|	ГрафикНачисленияПроцентовПоДоходнойАренде.Проценты КАК Проценты,
	|	ГрафикНачисленияПроцентовПоДоходнойАренде.Дата КАК Дата,
	|	ГрафикНачисленияПроцентовПоДоходнойАренде.ОсновноеСредство КАК ОсновноеСредство,
	|	ГрафикНачисленияПроцентовПоДоходнойАренде.Договор КАК Договор
	|ПОМЕСТИТЬ втГрафикНачисленияПроцентовОбщий
	|ИЗ
	|	РегистрСведений.ГрафикНачисленияПроцентовПоДоходнойАренде КАК ГрафикНачисленияПроцентовПоДоходнойАренде
	|ГДЕ
	|	(ГрафикНачисленияПроцентовПоДоходнойАренде.Договор, ГрафикНачисленияПроцентовПоДоходнойАренде.ОсновноеСредство) В
	|			(ВЫБРАТЬ
	|				ТаблицаОС.Договор,
	|				ТаблицаОС.ОсновноеСредство
	|			ИЗ
	|				втТаблицаОСПоФСБУ25 КАК ТаблицаОС)
	|	И ГрафикНачисленияПроцентовПоДоходнойАренде.Активность
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Договор,
	|	ОсновноеСредство,
	|	ДатаИзменения,
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОС.Ссылка КАК Ссылка,
	|	ТаблицаОС.Период КАК Период,
	|	ТаблицаОС.Договор КАК Договор,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ГрафикНачисленияПроцентов.ДатаИзменения КАК ДатаИзменения,
	|	ГрафикНачисленияПроцентов.Проценты КАК Проценты,
	|	ГрафикНачисленияПроцентов.Дата КАК Дата
	|ПОМЕСТИТЬ втГрафикНачисленияПроцентов
	|ИЗ
	|	втТаблицаОСПоФСБУ25 КАК ТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрафикНачисленияПроцентовОбщий КАК ГрафикНачисленияПроцентов
	|		ПО (ГрафикНачисленияПроцентов.Договор = ТаблицаОС.Договор)
	|			И (ГрафикНачисленияПроцентов.ОсновноеСредство = ТаблицаОС.ОсновноеСредство)
	|			И (ГрафикНачисленияПроцентов.ДатаИзменения < ТаблицаОС.Период)
	|			И (ГрафикНачисленияПроцентов.Дата <= КОНЕЦПЕРИОДА(ТаблицаОС.Период, МЕСЯЦ))
	|			И (ГрафикНачисленияПроцентов.Дата >= НАЧАЛОПЕРИОДА(ТаблицаОС.Период, МЕСЯЦ))
	|			И (ГрафикНачисленияПроцентов.Проценты > 0)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Договор,
	|	ОсновноеСредство,
	|	ДатаИзменения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикНачисленияПроцентов.Ссылка КАК Ссылка,
	|	ГрафикНачисленияПроцентов.ОсновноеСредство КАК ОсновноеСредство,
	|	МАКСИМУМ(ГрафикНачисленияПроцентов.ДатаИзменения) КАК ДатаИзменения
	|ПОМЕСТИТЬ втГрафикНачисленияПроцентовПериоды
	|ИЗ
	|	втГрафикНачисленияПроцентов КАК ГрафикНачисленияПроцентов
	|
	|СГРУППИРОВАТЬ ПО
	|	ГрафикНачисленияПроцентов.Ссылка,
	|	ГрафикНачисленияПроцентов.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикНачисленияПроцентов.Ссылка КАК АктуальныеУсловияДоговора,
	|	ГрафикНачисленияПроцентов.Период КАК Период,
	|	ГрафикНачисленияПроцентов.Договор КАК Договор,
	|	ГрафикНачисленияПроцентов.ОсновноеСредство КАК ОсновноеСредство,
	|	ГрафикНачисленияПроцентов.Дата КАК Дата,
	|	ГрафикНачисленияПроцентов.Проценты КАК Проценты,
	|	ГрафикНачисленияПроцентов.Период КАК ДатаИзменения
	|ИЗ
	|	втГрафикНачисленияПроцентов КАК ГрафикНачисленияПроцентов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрафикНачисленияПроцентовПериоды КАК ГрафикНачисленияПроцентовПериоды
	|		ПО ГрафикНачисленияПроцентов.Ссылка = ГрафикНачисленияПроцентовПериоды.Ссылка
	|			И (ГрафикНачисленияПроцентовПериоды.ОсновноеСредство = ГрафикНачисленияПроцентов.ОсновноеСредство)
	|			И (ГрафикНачисленияПроцентовПериоды.ДатаИзменения = ГрафикНачисленияПроцентов.ДатаИзменения)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаОС.Ссылка,
	|	ТаблицаОС.Период,
	|	ТаблицаОС.Договор,
	|	ТаблицаОС.ОсновноеСредство,
	|	ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ТаблицаОС.Период, МЕСЯЦ), МЕСЯЦ, 1),
	|	0,
	|	ТаблицаОС.Период
	|ИЗ
	|	втТаблицаОСПоФСБУ25 КАК ТаблицаОС";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаИнвестицииВАренду(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ИнвестицииВАренду";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтТаблицаОСПоФСБУ25(ТекстыЗапроса);
	ОтложенноеФормированиеДвиженийВНА.ТекстЗапросаПустыеТаблицыОтложенныхДвижений(Запрос, ТекстыЗапроса);
	
	СписокЗапросовОбъединение = Новый Массив;
	
	#Область ВосстановлениеПроцентовПоАренде // Дт <58.%> - Кт <58.АП>
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)          КАК ВидДвижения,
	|	ТаблицаОС.Ссылка                                КАК Регистратор,
	|	ТаблицаОС.Период                                КАК Период,
	|	ТаблицаОС.Организация                           КАК Организация,
	|	ИнвестицииОстатки.Договор                       КАК Договор,
	|	ИнвестицииОстатки.ОбъектУчета                   КАК ОсновноеСредство,
	|	ИнвестицииОстатки.Валюта                        КАК Валюта,
	|	ИнвестицииОстатки.ГруппаФинансовогоУчета        КАК ГруппаФинансовогоУчета,
	|	0                                               КАК АрендныеПлатежи,
	|	0                                               КАК АрендныеПлатежиРегл,
	|	0                                               КАК АрендныеПлатежиУпр,
	|	0                                               КАК НГЛСРегл,
	|	0                                               КАК НГЛСУпр,
	|	-ИнвестицииОстатки.ПроцентныйДисконт            КАК ПроцентныйДисконт,
	|	-ИнвестицииОстатки.ПроцентныйДисконтРегл        КАК ПроцентныйДисконтРегл,
	|	-ИнвестицииОстатки.ПроцентныйДисконтУпр         КАК ПроцентныйДисконтУпр,
	|	&НастройкаХО_ДосрочноеПрекращениеДоговораАренды КАК НастройкаХозяйственнойОперации,
	|	ТаблицаОС.ИдентификаторСтроки                   КАК ИдентификаторФинЗаписи
	|ИЗ
	|	втТаблицаОСПОФСБУ25 КАК ТаблицаОС
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ИнвестицииВАренду КАК ИнвестицииОстатки
	|		ПО ТаблицаОС.ОсновноеСредство = ИнвестицииОстатки.ОбъектУчета
	|		И ТаблицаОС.Договор = ИнвестицииОстатки.Договор
	|		И ИнвестицииОстатки.Ссылка = ТаблицаОС.Ссылка
	|		И (ИнвестицииОстатки.ПроцентныйДисконт <> 0
	|		ИЛИ ИнвестицииОстатки.ПроцентныйДисконтРегл <> 0
	|		ИЛИ ИнвестицииОстатки.ПроцентныйДисконтУпр <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)          КАК ВидДвижения,
	|	ТаблицаОС.Ссылка                                КАК Регистратор,
	|	ТаблицаОС.Период                                КАК Период,
	|	ТаблицаОС.Организация                           КАК Организация,
	|	ИнвестицииОстатки.Договор                       КАК Договор,
	|	ИнвестицииОстатки.ОбъектУчета                   КАК ОсновноеСредство,
	|	ИнвестицииОстатки.Валюта                        КАК Валюта,
	|	ИнвестицииОстатки.ГруппаФинансовогоУчета        КАК ГруппаФинансовогоУчета,
	|	-ИнвестицииОстатки.ПроцентныйДисконт            КАК АрендныеПлатежи,
	|	-ИнвестицииОстатки.ПроцентныйДисконтРегл        КАК АрендныеПлатежиРегл,
	|	-ИнвестицииОстатки.ПроцентныйДисконтУпр         КАК АрендныеПлатежиУпр,
	|	0                                               КАК НГЛСРегл,
	|	0                                               КАК НГЛСУпр,
	|	0                                               КАК ПроцентныйДисконт,
	|	0                                               КАК ПроцентныйДисконтРегл,
	|	0                                               КАК ПроцентныйДисконтУпр,
	|	&НастройкаХО_ДосрочноеПрекращениеДоговораАренды КАК НастройкаХозяйственнойОперации,
	|	ТаблицаОС.ИдентификаторСтроки                   КАК ИдентификаторФинЗаписи
	|ИЗ
	|	втТаблицаОСПОФСБУ25 КАК ТаблицаОС
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ИнвестицииВАренду КАК ИнвестицииОстатки
	|		ПО ТаблицаОС.ОсновноеСредство = ИнвестицииОстатки.ОбъектУчета
	|		И ТаблицаОС.Договор = ИнвестицииОстатки.Договор
	|		И ИнвестицииОстатки.Ссылка = ТаблицаОС.Ссылка
	|		И (ИнвестицииОстатки.ПроцентныйДисконт <> 0
	|		ИЛИ ИнвестицииОстатки.ПроцентныйДисконтРегл <> 0
	|		ИЛИ ИнвестицииОстатки.ПроцентныйДисконтУпр <> 0)";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область ВключениеНГЛСВСтоимостьИнвестицииВАренду // Дт <58.АП> - Кт <58.НГЛС>
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)          КАК ВидДвижения,
	|	ТаблицаОС.Ссылка                                КАК Регистратор,
	|	ТаблицаОС.Период                                КАК Период,
	|	ТаблицаОС.Организация                           КАК Организация,
	|	ИнвестицииОстатки.Договор                       КАК Договор,
	|	ИнвестицииОстатки.ОбъектУчета                   КАК ОсновноеСредство,
	|	ИнвестицииОстатки.Валюта                        КАК Валюта,
	|	ИнвестицииОстатки.ГруппаФинансовогоУчета        КАК ГруппаФинансовогоУчета,
	|	0                                               КАК АрендныеПлатежи,
	|	ИнвестицииОстатки.НГЛСРегл                      КАК АрендныеПлатежиРегл,
	|	ИнвестицииОстатки.НГЛСУпр                       КАК АрендныеПлатежиУпр,
	|	0                                               КАК НГЛСРегл,
	|	0                                               КАК НГЛСУпр,
	|	0                                               КАК ПроцентныйДисконт,
	|	0                                               КАК ПроцентныйДисконтУпр,
	|	0                                               КАК ПроцентныйДисконтРегл,
	|	&НастройкаХО_ВключениеНГЛСВСтоимостьЧИА         КАК НастройкаХозяйственнойОперации,
	|	ТаблицаОС.ИдентификаторСтроки                   КАК ИдентификаторФинЗаписи
	|ИЗ
	|	втТаблицаОСПОФСБУ25 КАК ТаблицаОС
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ИнвестицииВАренду КАК ИнвестицииОстатки
	|		ПО ТаблицаОС.ОсновноеСредство = ИнвестицииОстатки.ОбъектУчета
	|		И ТаблицаОС.Договор = ИнвестицииОстатки.Договор
	|		И ИнвестицииОстатки.Ссылка = ТаблицаОС.Ссылка
	|		И (ИнвестицииОстатки.НГЛСРегл <> 0
	|		ИЛИ ИнвестицииОстатки.НГЛСУпр <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)          КАК ВидДвижения,
	|	ТаблицаОС.Ссылка                                КАК Регистратор,
	|	ТаблицаОС.Период                                КАК Период,
	|	ТаблицаОС.Организация                           КАК Организация,
	|	ИнвестицииОстатки.Договор                       КАК Договор,
	|	ИнвестицииОстатки.ОбъектУчета                   КАК ОсновноеСредство,
	|	ИнвестицииОстатки.Валюта                        КАК Валюта,
	|	ИнвестицииОстатки.ГруппаФинансовогоУчета        КАК ГруппаФинансовогоУчета,
	|	0                                               КАК АрендныеПлатежи,
	|	0                                               КАК АрендныеПлатежиРегл,
	|	0                                               КАК АрендныеПлатежиУпр,
	|	ИнвестицииОстатки.НГЛСРегл                      КАК НГЛСРегл,
	|	ИнвестицииОстатки.НГЛСУпр                       КАК НГЛСУпр,
	|	0                                               КАК ПроцентныйДисконт,
	|	0                                               КАК ПроцентныйДисконтРегл,
	|	0                                               КАК ПроцентныйДисконтУпр,
	|	&НастройкаХО_ВключениеНГЛСВСтоимостьЧИА         КАК НастройкаХозяйственнойОперации,
	|	ТаблицаОС.ИдентификаторСтроки                   КАК ИдентификаторФинЗаписи
	|ИЗ
	|	втТаблицаОСПОФСБУ25 КАК ТаблицаОС
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ИнвестицииВАренду КАК ИнвестицииОстатки
	|		ПО ТаблицаОС.ОсновноеСредство = ИнвестицииОстатки.ОбъектУчета
	|		И ТаблицаОС.Договор = ИнвестицииОстатки.Договор
	|		И ИнвестицииОстатки.Ссылка = ТаблицаОС.Ссылка
	|		И (ИнвестицииОстатки.НГЛСРегл <> 0
	|		ИЛИ ИнвестицииОстатки.НГЛСУпр <> 0)";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область ПринятКУчетуВозвращенныйИзАрендыАктив  // Дт <01.01> - Кт <58.АП>
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)          КАК ВидДвижения,
	|	ТаблицаОС.Ссылка                                КАК Регистратор,
	|	ТаблицаОС.Период                                КАК Период,
	|	ТаблицаОС.Организация                           КАК Организация,
	|	ИнвестицииОстатки.Договор                       КАК Договор,
	|	ИнвестицииОстатки.ОбъектУчета                   КАК ОсновноеСредство,
	|	ИнвестицииОстатки.Валюта                        КАК Валюта,
	|	ИнвестицииОстатки.ГруппаФинансовогоУчета        КАК ГруппаФинансовогоУчета,
	|	ИнвестицииОстатки.АрендныеПлатежи
	|		+ ИнвестицииОстатки.ПроцентныйДисконт       КАК АрендныеПлатежи,
	|	ИнвестицииОстатки.АрендныеПлатежиРегл
	|		+ ИнвестицииОстатки.ПроцентныйДисконтРегл
	|		+ ИнвестицииОстатки.НГЛСРегл                КАК АрендныеПлатежиРегл,
	|	ИнвестицииОстатки.АрендныеПлатежиУпр
	|		+ ИнвестицииОстатки.ПроцентныйДисконтУпр
	|		+ ИнвестицииОстатки.НГЛСУпр                 КАК АрендныеПлатежиУпр,
	|	0                                               КАК НГЛСРегл,
	|	0                                               КАК НГЛСУпр,
	|	0                                               КАК ПроцентныйДисконт,
	|	0                                               КАК ПроцентныйДисконтРегл,
	|	0                                               КАК ПроцентныйДисконтУпр,
	|	&НастройкаХО_ВозвратОСИзАренды                  КАК НастройкаХозяйственнойОперации,
	|	ТаблицаОС.ИдентификаторСтроки                   КАК ИдентификаторФинЗаписи
	|ИЗ
	|	втТаблицаОСПОФСБУ25 КАК ТаблицаОС
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ИнвестицииВАренду КАК ИнвестицииОстатки
	|		ПО ТаблицаОС.ОсновноеСредство = ИнвестицииОстатки.ОбъектУчета
	|		И ТаблицаОС.Договор = ИнвестицииОстатки.Договор
	|		И ТаблицаОС.ПорядокУчетаОС = ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаОСПриВозвратеИзАренды.УчитыватьВСоставеОС)
	|		И ИнвестицииОстатки.Ссылка = ТаблицаОС.Ссылка
	|		И (ИнвестицииОстатки.АрендныеПлатежиРегл + ИнвестицииОстатки.ПроцентныйДисконтРегл + ИнвестицииОстатки.НГЛСРегл <> 0
	|		ИЛИ ИнвестицииОстатки.АрендныеПлатежиУпр + ИнвестицииОстатки.ПроцентныйДисконтУпр + ИнвестицииОстатки.НГЛСУпр <> 0)";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область ВозвратВСоставВложенийВАренду // Дт <58.ВА> - Кт <58.АП>
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)          КАК ВидДвижения,
	|	ТаблицаОС.Ссылка                                КАК Регистратор,
	|	ТаблицаОС.Период                                КАК Период,
	|	ТаблицаОС.Организация                           КАК Организация,
	|	ИнвестицииОстатки.Договор                       КАК Договор,
	|	ИнвестицииОстатки.ОбъектУчета                   КАК ОсновноеСредство,
	|	ИнвестицииОстатки.Валюта                        КАК Валюта,
	|	ИнвестицииОстатки.ГруппаФинансовогоУчета        КАК ГруппаФинансовогоУчета,
	|	ИнвестицииОстатки.АрендныеПлатежи
	|		+ ИнвестицииОстатки.ПроцентныйДисконт       КАК АрендныеПлатежи,
	|	ИнвестицииОстатки.АрендныеПлатежиРегл
	|		+ ИнвестицииОстатки.ПроцентныйДисконтРегл
	|		+ ИнвестицииОстатки.НГЛСРегл                КАК АрендныеПлатежиРегл,
	|	ИнвестицииОстатки.АрендныеПлатежиУпр
	|		+ ИнвестицииОстатки.ПроцентныйДисконтУпр
	|		+ ИнвестицииОстатки.НГЛСУпр                 КАК АрендныеПлатежиУпр,
	|	0                                               КАК НГЛСРегл,
	|	0                                               КАК НГЛСУпр,
	|	0                                               КАК ПроцентныйДисконт,
	|	0                                               КАК ПроцентныйДисконтРегл,
	|	0                                               КАК ПроцентныйДисконтУпр,
	|	&НастройкаХО_ВозвратОСИзАренды                  КАК НастройкаХозяйственнойОперации,
	|	ТаблицаОС.ИдентификаторСтроки                   КАК ИдентификаторФинЗаписи
	|ИЗ
	|	втТаблицаОСПОФСБУ25 КАК ТаблицаОС
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ИнвестицииВАренду КАК ИнвестицииОстатки
	|		ПО ТаблицаОС.ОсновноеСредство = ИнвестицииОстатки.ОбъектУчета
	|		И ТаблицаОС.Договор = ИнвестицииОстатки.Договор
	|		И ТаблицаОС.ПорядокУчетаОС = ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаОСПриВозвратеИзАренды.УчитыватьВСоставеИнвестицииВАренду)
	|		И (ИнвестицииОстатки.АрендныеПлатежиРегл + ИнвестицииОстатки.ПроцентныйДисконтРегл + ИнвестицииОстатки.НГЛСРегл <> 0
	|		ИЛИ ИнвестицииОстатки.АрендныеПлатежиУпр + ИнвестицииОстатки.ПроцентныйДисконтУпр + ИнвестицииОстатки.НГЛСУпр <> 0)";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область СписаниеОбъектаВРасходы // Дт <91.02> - Кт <58.АП>
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)          КАК ВидДвижения,
	|	ТаблицаОС.Ссылка                                КАК Регистратор,
	|	ТаблицаОС.Период                                КАК Период,
	|	ТаблицаОС.Организация                           КАК Организация,
	|	ИнвестицииОстатки.Договор                       КАК Договор,
	|	ИнвестицииОстатки.ОбъектУчета                   КАК ОсновноеСредство,
	|	ИнвестицииОстатки.Валюта                        КАК Валюта,
	|	ИнвестицииОстатки.ГруппаФинансовогоУчета        КАК ГруппаФинансовогоУчета,
	|	ИнвестицииОстатки.АрендныеПлатежи
	|		+ ИнвестицииОстатки.ПроцентныйДисконт       КАК АрендныеПлатежи,
	|	ИнвестицииОстатки.АрендныеПлатежиРегл
	|		+ ИнвестицииОстатки.ПроцентныйДисконтРегл
	|		+ ИнвестицииОстатки.НГЛСРегл                КАК АрендныеПлатежиРегл,
	|	ИнвестицииОстатки.АрендныеПлатежиУпр
	|		+ ИнвестицииОстатки.ПроцентныйДисконтУпр
	|		+ ИнвестицииОстатки.НГЛСУпр                 КАК АрендныеПлатежиУпр,
	|	0                                               КАК НГЛСРегл,
	|	0                                               КАК НГЛСУпр,
	|	0                                               КАК ПроцентныйДисконт,
	|	0                                               КАК ПроцентныйДисконтРегл,
	|	0                                               КАК ПроцентныйДисконтУпр,
	|	&НастройкаХО_ВозвратОСИзАренды                  КАК НастройкаХозяйственнойОперации,
	|	ТаблицаОС.ИдентификаторСтроки                   КАК ИдентификаторФинЗаписи
	|ИЗ
	|	втТаблицаОСПОФСБУ25 КАК ТаблицаОС
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ИнвестицииВАренду КАК ИнвестицииОстатки
	|		ПО ТаблицаОС.ОсновноеСредство = ИнвестицииОстатки.ОбъектУчета
	|		И ТаблицаОС.Договор = ИнвестицииОстатки.Договор
	|		И ТаблицаОС.ПорядокУчетаОС = ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаОСПриВозвратеИзАренды.СписатьНаРасходы)
	|		И ИнвестицииОстатки.Ссылка = ТаблицаОС.Ссылка
	|		И (ИнвестицииОстатки.АрендныеПлатежиРегл + ИнвестицииОстатки.ПроцентныйДисконтРегл + ИнвестицииОстатки.НГЛСРегл <> 0
	|		ИЛИ ИнвестицииОстатки.АрендныеПлатежиУпр + ИнвестицииОстатки.ПроцентныйДисконтУпр + ИнвестицииОстатки.НГЛСУпр <> 0)";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область СписаниеНаСтатьюАП // Дт <XXX> - Кт <58.АП>
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)          КАК ВидДвижения,
	|	ТаблицаОС.Ссылка                                КАК Регистратор,
	|	ТаблицаОС.Период                                КАК Период,
	|	ТаблицаОС.Организация                           КАК Организация,
	|	ИнвестицииОстатки.Договор                       КАК Договор,
	|	ИнвестицииОстатки.ОбъектУчета                   КАК ОсновноеСредство,
	|	ИнвестицииОстатки.Валюта                        КАК Валюта,
	|	ИнвестицииОстатки.ГруппаФинансовогоУчета        КАК ГруппаФинансовогоУчета,
	|	ИнвестицииОстатки.АрендныеПлатежи
	|		+ ИнвестицииОстатки.ПроцентныйДисконт       КАК АрендныеПлатежи,
	|	ИнвестицииОстатки.АрендныеПлатежиРегл
	|		+ ИнвестицииОстатки.ПроцентныйДисконтРегл
	|		+ ИнвестицииОстатки.НГЛСРегл                КАК АрендныеПлатежиРегл,
	|	ИнвестицииОстатки.АрендныеПлатежиУпр
	|		+ ИнвестицииОстатки.ПроцентныйДисконтУпр
	|		+ ИнвестицииОстатки.НГЛСУпр                 КАК АрендныеПлатежиУпр,
	|	0                                               КАК НГЛСРегл,
	|	0                                               КАК НГЛСУпр,
	|	0                                               КАК ПроцентныйДисконт,
	|	0                                               КАК ПроцентныйДисконтРегл,
	|	0                                               КАК ПроцентныйДисконтУпр,
	|	&НастройкаХО_ВозвратОСИзАренды                  КАК НастройкаХозяйственнойОперации,
	|	ТаблицаОС.ИдентификаторСтроки                   КАК ИдентификаторФинЗаписи
	|ИЗ
	|	втТаблицаОСПОФСБУ25 КАК ТаблицаОС
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ИнвестицииВАренду КАК ИнвестицииОстатки
	|		ПО ТаблицаОС.ОсновноеСредство = ИнвестицииОстатки.ОбъектУчета
	|		И ТаблицаОС.Договор = ИнвестицииОстатки.Договор
	|		И ТаблицаОС.ПорядокУчетаОС = ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаОСПриВозвратеИзАренды.СписатьНаСтатьюАктивов)
	|		И ИнвестицииОстатки.Ссылка = ТаблицаОС.Ссылка
	|		И (ИнвестицииОстатки.АрендныеПлатежиРегл + ИнвестицииОстатки.ПроцентныйДисконтРегл + ИнвестицииОстатки.НГЛСРегл <> 0
	|		ИЛИ ИнвестицииОстатки.АрендныеПлатежиУпр + ИнвестицииОстатки.ПроцентныйДисконтУпр + ИнвестицииОстатки.НГЛСУпр <> 0)";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(СписокЗапросовОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаДвиженияПоПрочимАктивамПассивам(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДвиженияПоПрочимАктивамПассивам";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	// Списание на статью активов/пассивов Дт <XXX> - Кт <58.АП>
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаОС.Ссылка                       КАК Регистратор,
	|	ТаблицаОС.Период                       КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаОС.Организация                  КАК Организация,
	|	ТаблицаОС.Подразделение                КАК Подразделение,
	|	ТаблицаОС.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|	ТаблицаОС.СтатьяРасходов               КАК Статья,
	|	ТаблицаОС.АналитикаАктивовПассивов     КАК Аналитика,
	|	ТаблицаОС.НастройкаСчетовУчета         КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Дебет) КАК ДебетКредит,
	|
	|	ИнвестицииОстатки.АрендныеПлатежиРегл
	|		+ ИнвестицииОстатки.ПроцентныйДисконтРегл
	|		+ ИнвестицииОстатки.НГЛСРегл       КАК СуммаРегл,
	|	0                                      КАК ПостояннаяРазница,
	|	ИнвестицииОстатки.АрендныеПлатежиРегл
	|		+ ИнвестицииОстатки.ПроцентныйДисконтРегл
	|		+ ИнвестицииОстатки.НГЛСРегл       КАК ВременнаяРазница,
	|	ИнвестицииОстатки.АрендныеПлатежиУпр
	|		+ ИнвестицииОстатки.ПроцентныйДисконтУпр
	|		+ ИнвестицииОстатки.НГЛСУпр        КАК СуммаУпр,
	|	ИнвестицииОстатки.АрендныеПлатежиУпр
	|		+ ИнвестицииОстатки.ПроцентныйДисконтУпр
	|		+ ИнвестицииОстатки.НГЛСУпр        КАК СуммаСНДС,
	|	ИнвестицииОстатки.АрендныеПлатежиУпр
	|		+ ИнвестицииОстатки.ПроцентныйДисконтУпр
	|		+ ИнвестицииОстатки.НГЛСУпр        КАК СуммаБезНДС,
	|
	|	ТаблицаОС.ИдентификаторСтроки          КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_ВозвратОСИзАренды         КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втТаблицаОСПОФСБУ25 КАК ТаблицаОС
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ИнвестицииВАренду КАК ИнвестицииОстатки
	|		ПО ТаблицаОС.ОсновноеСредство = ИнвестицииОстатки.ОбъектУчета
	|		И ТаблицаОС.Договор = ИнвестицииОстатки.Договор
	|		И ТаблицаОС.ПорядокУчетаОС = ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаОСПриВозвратеИзАренды.СписатьНаСтатьюАктивов)
	|		И ИнвестицииОстатки.Ссылка = ТаблицаОС.Ссылка
	|		И (ИнвестицииОстатки.АрендныеПлатежиРегл + ИнвестицииОстатки.ПроцентныйДисконтРегл + ИнвестицииОстатки.НГЛСРегл <> 0
	|		ИЛИ ИнвестицииОстатки.АрендныеПлатежиУпр + ИнвестицииОстатки.ПроцентныйДисконтУпр + ИнвестицииОстатки.НГЛСУпр <> 0)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеРасходы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ДопПоля = "Регистратор";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса, ДопПоля);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаПрочиеРасходы(ДопПоля, Истина);
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПервоначальныеСведенияОС(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПервоначальныеСведенияОС";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтРасширеннаяТаблицаОС(ТекстыЗапроса);
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПервоначальныеСведенияОС(
		ТекстыЗапроса, "ВтРасширеннаяТаблицаОС", "ВтПервоначальныеСведенияОС_СУчетомИсправлений", Истина);
		
	ТекстЗапроса = ВозвратОСИзАрендыЛокализация.ТекстЗапросаТаблицаПервоначальныеСведенияОС();
	
	Если ТекстЗапроса = Неопределено Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка КАК Регистратор,
		|	ДанныеДокумента.Период КАК Период,
		|	ДАТАВРЕМЯ(1,1,1) КАК ДатаИсправления,
		|
		|	ДанныеДокумента.Организация КАК Организация,
		|	ПервоначальныеСведенияОС.ОсновноеСредство КАК ОсновноеСредство,
		|
		|	ПервоначальныеСведенияОС.ПорядокУчетаБУ КАК ПорядокУчетаБУ,
		|	ПервоначальныеСведенияОС.ПорядокУчетаУУ КАК ПорядокУчетаУУ,
		|	ПервоначальныеСведенияОС.МетодНачисленияАмортизацииБУ КАК МетодНачисленияАмортизацииБУ,
		|	ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьБУ КАК ПервоначальнаяСтоимостьБУ,
		|	ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьУУ КАК ПервоначальнаяСтоимостьУУ,
		|	ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюБУ КАК ДатаВводаВЭксплуатациюБУ,
		|	ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюУУ КАК ДатаВводаВЭксплуатациюУУ,
		|	ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюБУ КАК ДокументВводаВЭксплуатациюБУ,
		|	ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюУУ КАК ДокументВводаВЭксплуатациюУУ,
		|
		|	ВЫБОР КОГДА ПервоначальныеСведенияОС.ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.ПустаяСсылка) ТОГДА
		|		ДанныеДокумента.Ссылка
		|	ИНАЧЕ ПервоначальныеСведенияОС.ДокументСнятияСУчетаБУ КОНЕЦ КАК ДокументСнятияСУчетаБУ,
		|	ВЫБОР КОГДА ПервоначальныеСведенияОС.ПорядокУчетаУУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.ПустаяСсылка) ТОГДА
		|		ДанныеДокумента.Ссылка
		|	ИНАЧЕ ПервоначальныеСведенияОС.ДокументСнятияСУчетаУУ КОНЕЦ КАК ДокументСнятияСУчетаУУ
		|
		|ИЗ
		|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
		|			ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
		|		ПО ПервоначальныеСведенияОС.Ссылка = ДанныеДокумента.Ссылка
		|			И ТаблицаОС.ОсновноеСредство = ПервоначальныеСведенияОС.ОсновноеСредство
		|
		|ГДЕ
		|	ДанныеДокумента.ВозвратИзФинаренды
		|	И ТаблицаОС.ПорядокУчетаОС В (
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаОСПриВозвратеИзАренды.УчитыватьВСоставеИнвестицииВАренду),
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаОСПриВозвратеИзАренды.СписатьНаРасходы),
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаОСПриВозвратеИзАренды.СписатьНаСтатьюАктивов))
		|";
		
	КонецЕсли;
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтТаблицаОСПоФСБУ25(ТекстыЗапроса)
	
	ИмяТаблицы = "втТаблицаОСПоФСБУ25";

	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Период КАК Период,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.ПодразделениеПолучатель КАК Подразделение,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаОС.ПорядокУчетаОС КАК ПорядокУчетаОС,
	|	ТаблицаОС.СтатьяРасходов КАК СтатьяРасходов,
	|	ТаблицаОС.АналитикаРасходов КАК АналитикаРасходов,
	|	ТаблицаОС.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
	|	ТаблицаОС.НастройкаСчетовУчета КАК НастройкаСчетовУчета,
	|	ТаблицаОС.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ДанныеДокумента.Договор КАК Договор,
	|	ДанныеДокумента.Договор.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ПОМЕСТИТЬ втТаблицаОСПоФСБУ25
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|ГДЕ
	|	ДанныеДокумента.ВозвратИзФинаренды
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	Договор,
	|	ОсновноеСредство,
	|	Период";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);

КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса, ДопПоля)
	
	ИмяРегистра = "ВтПрочиеРасходы";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтИсходныеПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(ТекстыЗапроса, ДопПоля);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаВтПрочиеРасходы(ДопПоля, Истина);
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(ТекстыЗапроса, ДопПоля)
	
	ИмяРегистра = "ВтИсходныеПрочиеРасходы";
	
	ТекстЗапросаТаблицаВтТаблицаОСПоФСБУ25(ТекстыЗапроса);
	
	СписокЗапросовОбъединение = Новый Массив;
	СписокЗапросовОбъединение.Добавить(РегистрыНакопления.ПрочиеРасходы.ТекстОписаниеВтИсходныеПрочиеРасходы(ДопПоля));
	
	#Область ВозвратВСоставВложенийВАренду // Дт <58.ВА> - Кт <58.АП>
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаОС.Период                        КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)  КАК ВидДвижения,
	|	
	|	ТаблицаОС.Организация                   КАК Организация,
	|	ТаблицаОС.Подразделение                 КАК Подразделение,
	|	ТаблицаОС.СтатьяРасходов                КАК СтатьяРасходов,
	|	ТаблицаОС.ОсновноеСредство              КАК АналитикаРасходов,
	|	ТаблицаОС.НаправлениеДеятельности       КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                            КАК ВидДеятельностиНДС,
	|	ИнвестицииОстатки.АрендныеПлатежиУпр
	|		+ ИнвестицииОстатки.ПроцентныйДисконтУпр
	|		+ ИнвестицииОстатки.НГЛСУпр         КАК СуммаСНДС,
	|	ИнвестицииОстатки.АрендныеПлатежиУпр
	|		+ ИнвестицииОстатки.ПроцентныйДисконтУпр
	|		+ ИнвестицииОстатки.НГЛСУпр         КАК СуммаБезНДС,
	|	ИнвестицииОстатки.АрендныеПлатежиУпр
	|		+ ИнвестицииОстатки.ПроцентныйДисконтУпр
	|		+ ИнвестицииОстатки.НГЛСУпр         КАК СуммаБезНДСУпр,
	|	ИнвестицииОстатки.АрендныеПлатежиРегл
	|		+ ИнвестицииОстатки.ПроцентныйДисконтРегл
	|		+ ИнвестицииОстатки.НГЛСРегл        КАК СуммаСНДСРегл,
	|	ИнвестицииОстатки.АрендныеПлатежиРегл
	|		+ ИнвестицииОстатки.ПроцентныйДисконтРегл
	|		+ ИнвестицииОстатки.НГЛСРегл        КАК СуммаБезНДСРегл,
	|
	|	0                                       КАК ПостояннаяРазница,
	|	ИнвестицииОстатки.АрендныеПлатежиРегл
	|		+ ИнвестицииОстатки.ПроцентныйДисконтРегл
	|		+ ИнвестицииОстатки.НГЛСРегл        КАК ВременнаяРазница,
	|	
	|	&ХО_ВозвратОСИзАренды                   КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                            КАК АналитикаУчетаНоменклатуры,
	|
	|	ТаблицаОС.ИдентификаторСтроки           КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_ВозвратОСИзАренды          КАК НастройкаХозяйственнойОперации,
	|	ТаблицаОС.Ссылка                        КАК Регистратор
	|ИЗ
	|	втТаблицаОСПОФСБУ25 КАК ТаблицаОС
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ИнвестицииВАренду КАК ИнвестицииОстатки
	|		ПО ТаблицаОС.ОсновноеСредство = ИнвестицииОстатки.ОбъектУчета
	|		И ТаблицаОС.Договор = ИнвестицииОстатки.Договор
	|		И ТаблицаОС.ПорядокУчетаОС = ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаОСПриВозвратеИзАренды.УчитыватьВСоставеИнвестицииВАренду)
	|		И ИнвестицииОстатки.Ссылка = ТаблицаОС.Ссылка
	|		И (ИнвестицииОстатки.АрендныеПлатежиРегл + ИнвестицииОстатки.ПроцентныйДисконтРегл + ИнвестицииОстатки.НГЛСРегл <> 0
	|		ИЛИ ИнвестицииОстатки.АрендныеПлатежиУпр + ИнвестицииОстатки.ПроцентныйДисконтУпр + ИнвестицииОстатки.НГЛСУпр <> 0)";

	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область СписаниеОбъектаВРасходы // Дт <91.02> - Кт <58.АП>
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаОС.Период                       КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	ТаблицаОС.Организация                   КАК Организация,
	|	ТаблицаОС.Подразделение                 КАК Подразделение,
	|	ТаблицаОС.СтатьяРасходов                КАК СтатьяРасходов,
	|	ТаблицаОС.ОсновноеСредство              КАК АналитикаРасходов,
	|	ТаблицаОС.НаправлениеДеятельности       КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                            КАК ВидДеятельностиНДС,
	|	ИнвестицииОстатки.АрендныеПлатежиУпр
	|		+ ИнвестицииОстатки.ПроцентныйДисконтУпр
	|		+ ИнвестицииОстатки.НГЛСУпр         КАК СуммаСНДС,
	|	ИнвестицииОстатки.АрендныеПлатежиУпр
	|		+ ИнвестицииОстатки.ПроцентныйДисконтУпр
	|		+ ИнвестицииОстатки.НГЛСУпр         КАК СуммаБезНДС,
	|	ИнвестицииОстатки.АрендныеПлатежиУпр
	|		+ ИнвестицииОстатки.ПроцентныйДисконтУпр
	|		+ ИнвестицииОстатки.НГЛСУпр         КАК СуммаБезНДСУпр,
	|	ИнвестицииОстатки.АрендныеПлатежиРегл
	|		+ ИнвестицииОстатки.ПроцентныйДисконтРегл
	|		+ ИнвестицииОстатки.НГЛСРегл        КАК СуммаСНДСРегл,
	|	ИнвестицииОстатки.АрендныеПлатежиРегл
	|		+ ИнвестицииОстатки.ПроцентныйДисконтРегл
	|		+ ИнвестицииОстатки.НГЛСРегл        КАК СуммаБезНДСРегл,
	|
	|	0                                       КАК ПостояннаяРазница,
	|	ИнвестицииОстатки.АрендныеПлатежиРегл
	|		+ ИнвестицииОстатки.ПроцентныйДисконтРегл
	|		+ ИнвестицииОстатки.НГЛСРегл        КАК ВременнаяРазница,
	|	
	|	&ХО_ВозвратОСИзАренды                   КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                            КАК АналитикаУчетаНоменклатуры,
	|
	|	ТаблицаОС.ИдентификаторСтроки           КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_ВозвратОСИзАренды          КАК НастройкаХозяйственнойОперации,
	|	ТаблицаОС.Ссылка                        КАК Регистратор
	|ИЗ
	|	втТаблицаОСПоФСБУ25 КАК ТаблицаОС
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ИнвестицииВАренду КАК ИнвестицииОстатки
	|		ПО ТаблицаОС.ОсновноеСредство = ИнвестицииОстатки.ОбъектУчета
	|		И ТаблицаОС.Договор = ИнвестицииОстатки.Договор
	|		И ТаблицаОС.ПорядокУчетаОС = ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаОСПриВозвратеИзАренды.СписатьНаРасходы)
	|		И ИнвестицииОстатки.Ссылка = ТаблицаОС.Ссылка
	|		И (ИнвестицииОстатки.АрендныеПлатежиРегл + ИнвестицииОстатки.ПроцентныйДисконтРегл + ИнвестицииОстатки.НГЛСРегл <> 0
	|		ИЛИ ИнвестицииОстатки.АрендныеПлатежиУпр + ИнвестицииОстатки.ПроцентныйДисконтУпр + ИнвестицииОстатки.НГЛСУпр <> 0)";
	
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(СписокЗапросовОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

#КонецОбласти

#Область ПроведениеРеглУчет

Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт

	Возврат ВозвратОСИзАрендыЛокализация.ТекстЗапросаВТОтраженияВРеглУчете();

КонецФункции

Функция ТекстОтраженияВРеглУчете() Экспорт

	Возврат ВозвратОСИзАрендыЛокализация.ТекстОтраженияВРеглУчете();

КонецФункции

#КонецОбласти

#Область Печать

Функция ПолучитьДанныеДляПечатнойФормыОС1(ПараметрыПечати, МассивОбъектов) Экспорт
	
	Возврат ВозвратОСИзАрендыЛокализация.ПолучитьДанныеДляПечатнойФормыОС1(ПараметрыПечати, МассивОбъектов);
		
КонецФункции

#КонецОбласти

#Область Прочее

Функция ПараметрыВыбораСтатейИАналитик() Экспорт
	
	ПараметрыВыбораСтатьиИАналитики = Новый Массив;
	
	// СтатьяРасходовБУ
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья = "СтатьяРасходовБУ";
	ПараметрыВыбора.ДоступностьПоОперации = Неопределено;
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходовБУ";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходовБУ");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходовБУ");
	
	ПараметрыВыбораСтатьиИАналитики.Добавить(ПараметрыВыбора);
	
	// СтатьяРасходовУУ
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья = "СтатьяРасходовУУ";
	ПараметрыВыбора.ДоступностьПоОперации = Неопределено;
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходовУУ";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходовУУ");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходовУУ");
	
	ПараметрыВыбораСтатьиИАналитики.Добавить(ПараметрыВыбора);
	
	//СтатьяРасходовОС
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();

	ПараметрыВыбора.ПутьКДанным = "Объект.ОС";
	ПараметрыВыбора.Статья = "СтатьяРасходов";
	ПараметрыВыбора.ТипСтатьи = "ТипСтатьиРасходы";
	//ПараметрыВыбора.ДоступностьПоОперации = ВозвратИзФинаренды;

	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходов";
	
	ПараметрыВыбора.ВыборСтатьиАктивовПассивов = Истина;
	ПараметрыВыбора.АналитикаАктивовПассивов = "АналитикаАктивовПассивов";

	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("ОССтатьяРасходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("ОСАналитикаРасходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаАктивовПассивов.Добавить("ОСАналитикаАктивовПассивов");
	
	МассивОтборПорядокУчетаОС = Новый Массив;
	МассивОтборПорядокУчетаОС.Добавить(Перечисления.ВариантыУчетаОСПриВозвратеИзАренды.УчитыватьВСоставеИнвестицииВАренду);
	МассивОтборПорядокУчетаОС.Добавить(Перечисления.ВариантыУчетаОСПриВозвратеИзАренды.СписатьНаРасходы);
	МассивОтборПорядокУчетаОС.Добавить(Перечисления.ВариантыУчетаОСПриВозвратеИзАренды.СписатьНаСтатьюАктивов);
	
	ПараметрыВыбора.УсловияДоступностиСтатьиВСтроках.Вставить("ПорядокУчетаОС", МассивОтборПорядокУчетаОС);

	ПараметрыВыбораСтатьиИАналитики.Добавить(ПараметрыВыбора);
	
	Возврат ПараметрыВыбораСтатьиИАналитики;
	
КонецФункции

// Возвращает параметры настройки счетов учета в документе.
// Возвращаемое значение:
//  см. НастройкаСчетовУчетаСервер.ПараметрыНастройки.
//
Функция ПараметрыНастройкиСчетовУчета() Экспорт
	
	ПараметрыНастройки = НастройкаСчетовУчетаСервер.ПараметрыНастройки();
	
	ПараметрыНастройки.ДоступностьПоОперации = Истина;
	ПараметрыНастройки.ПутьКДанным = "Объект.ОС";
	ПараметрыНастройки.ТипСтатьи = "ТипСтатьиРасходы";
	
	// Элементы формы, которые используются для настройки.
	ПараметрыНастройки.ЭлементыФормы.Добавить("ОСПредставлениеОтраженияОперации");
	
	Возврат ПараметрыНастройки;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
