
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ПрефиксИмениФайлаПечатнойФормы = ЭлектронныйДокументооборотСФССКлиентСервер.ПрефиксИмениФайлаПечатнойФормыДокументаДоходнойЧастиСЭДО();
	Если Параметры.Свойство("Основание") Тогда
		ДокументСсылка = Параметры.Основание;
		Если ЗначениеЗаполнено(ДокументСсылка) Тогда
			Для каждого Файл из ДокументСсылка.ФайлыПриложений Цикл
				Если НЕ СтрНачинаетсяС(Файл.ИмяФайла, ПрефиксИмениФайлаПечатнойФормы)
					И НЕ Файл.ВидФайла = ДокументСсылка.ВидДокументаФСС Тогда
					РеквизитыФайла = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Файл.ФайлПриложения, "Размер, Расширение");
					НовыйФайл						= Файлы.Добавить();
					НовыйФайл.ИмяФайлаПриложения	= Файл.ИмяФайла + "." + РеквизитыФайла.Расширение;
					ПредставлениеРазмера = ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.ТекстовоеПредставлениеРазмераФайла(
						РеквизитыФайла.Размер);
					НовыйФайл.РазмерФайлаПриложения	= ПредставлениеРазмера;
					НовыйФайл.ФайлПриложенияСсылка	= Файл.ФайлПриложения;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ФайлыИмяФайлаПриложения" Тогда
		ТекущиеДанные = Элементы.Файлы.ТекущиеДанные;
		
		Если ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Если НЕ ТекущиеДанные.ФайлПриложенияСсылка.Пустая() Тогда
			
			ДанныеФайла = РаботаСФайламиКлиент.ДанныеФайла(ТекущиеДанные.ФайлПриложенияСсылка,
				ЭтотОбъект.УникальныйИдентификатор, Истина);
				
			РасширениеPDF = "pdf";
			Если ВРег(ДанныеФайла.Расширение) = ВРег(РасширениеPDF)
				И НЕ ЭлектронныйДокументооборотСФССКлиент.ЭтоФайлPDF(ДанныеФайла) Тогда
				ТекстСообщения = НСтр("ru = 'Открыть файл с вложением невозможно. С сервера СФР получен некорректный файл pdf.';
										|en = 'Открыть файл с вложением невозможно. С сервера СФР получен некорректный файл pdf.'");
				ПоказатьПредупреждение(, ТекстСообщения);
				Возврат;
			КонецЕсли;
			
			РаботаСФайламиКлиент.ОткрытьФайл(ДанныеФайла, Ложь);
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти