#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(МассивИзвещенийФСС, ПараметрыВыполненияКоманды)
	ИдентификаторыСообщений = ИдентификаторыСообщенийСтрахователей(МассивИзвещенийФСС);
	СЭДОФССКлиент.ПовторноПолучитьСообщенияИзФСС(ИдентификаторыСообщений);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ИдентификаторыСообщенийСтрахователей(Знач МассивИзвещенийФСС)
	ЗначенияРеквизитовИзвещений = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(
		МассивИзвещенийФСС,
		"Страхователь, ИдентификаторСообщения",
		Истина);
	
	Результат = Новый Соответствие;
	Для Каждого КлючИЗначение Из ЗначенияРеквизитовИзвещений Цикл
		ЗначенияРеквизитовИзвещения = КлючИЗначение.Значение;
		Если Не ЗначениеЗаполнено(ЗначенияРеквизитовИзвещения.Страхователь) Тогда
			Текст = НСтр("ru = 'Не заполнено поле ""Страхователь""';
						|en = '""Insurant"" is not filled in'");
			СообщенияБЗК.СообщитьОПроблеме(Текст, КлючИЗначение.Ключ, "Страхователь");
			Продолжить;
		КонецЕсли;
		ИдентификаторыСообщений = Результат[ЗначенияРеквизитовИзвещения.Страхователь];
		Если ИдентификаторыСообщений = Неопределено Тогда
			ИдентификаторыСообщений = Новый Массив;
			Результат.Вставить(ЗначенияРеквизитовИзвещения.Страхователь, ИдентификаторыСообщений);
		КонецЕсли;
		Если ИдентификаторыСообщений.Найти(ЗначенияРеквизитовИзвещения.ИдентификаторСообщения) = Неопределено Тогда
			ИдентификаторыСообщений.Добавить(ЗначенияРеквизитовИзвещения.ИдентификаторСообщения);
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;
КонецФункции

#КонецОбласти
