#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// БлокировкаИзмененияОбъектов
	БлокировкаИзмененияОбъектов.ПриСозданииНаСервереФормыСписка(ЭтотОбъект, Элементы.ФормаКоманднаяПанель);
	// Конец БлокировкаИзмененияОбъектов
	
	// КадровыйЭДО
	КадровыйЭДО.ПриСозданииНаСервереФормыСписка(ЭтотОбъект, Список);
	// Конец КадровыйЭДО
	
	ЗарплатаКадры.ПриСозданииНаСервереФормыСДинамическимСписком(ЭтотОбъект, "Список");
	
	Список.Параметры.УстановитьЗначениеПараметра("ОтображатьПодписанные", Не ОтображатьТолькоНеподписанные);

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	// КадровыйЭДО
	КадровыйЭДОКлиент.ОбработкаОповещенияВФормеСписка(
		ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец КадровыйЭДО
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтображатьТолькоНеподписанныеПриИзменении(Элемент)
	Список.Параметры.УстановитьЗначениеПараметра("ОтображатьПодписанные", Не ОтображатьТолькоНеподписанные);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	// СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	// КадровыйЭДО
	КадровыйЭДО.СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки);
	// Конец КадровыйЭДО
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подписать(Команда)
	МассивСогласий = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивСогласий, Элементы.Список.ВыделенныеСтроки); 
	ПрисоединенныеФайлыСогласий = ФайлыНаПодписание(МассивСогласий);
	КадровыйЭДОКлиент.ОткрытьФормуПодписанияПечатныхФорм(ПрисоединенныеФайлыСогласий);
КонецПроцедуры

&НаКлиенте
Процедура ПодтвердитьНаличие(Команда)
	МассивСогласий = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивСогласий, Элементы.Список.ВыделенныеСтроки);
	ПодтвердитьНаличиеНаСервере(МассивСогласий);
	Элементы.Список.Обновить();
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Элементы.Список);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// БлокировкаИзмененияОбъектов

&НаКлиенте
Процедура Подключаемый_РазблокироватьОбъекты(Команда)
	БлокировкаИзмененияОбъектовКлиент.РазблокироватьОбъектыСписка(ЭтотОбъект, Список, Элементы.Список.ВыделенныеСтроки);
КонецПроцедуры

// Конец БлокировкаИзмененияОбъектов

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СерверныеОбработчикиКомандФормы

&НаСервереБезКонтекста
Процедура ПодтвердитьНаличиеНаСервере(МассивСогласий)
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	СостояниеСогласияНаПрисоединениеККЭДО.Ссылка КАК Ссылка,
	               |	СостояниеСогласияНаПрисоединениеККЭДО.Состояние КАК Состояние
	               |ИЗ
	               |	РегистрСведений.СостояниеСогласияНаПрисоединениеККЭДО КАК СостояниеСогласияНаПрисоединениеККЭДО
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СогласиеНаПрисоединениеККЭДО КАК СогласиеНаПрисоединениеККЭДО
	               |		ПО СостояниеСогласияНаПрисоединениеККЭДО.Ссылка = СогласиеНаПрисоединениеККЭДО.Ссылка
	               |ГДЕ
	               |	СостояниеСогласияНаПрисоединениеККЭДО.Ссылка В(&Согласия)
	               |	И СостояниеСогласияНаПрисоединениеККЭДО.Подтвержден = ЛОЖЬ
	               |	И СостояниеСогласияНаПрисоединениеККЭДО.Состояние <> ЗНАЧЕНИЕ(Перечисление.СостоянияСогласийНаПрисоединениеККЭДО.ПустаяСсылка)";
	
	Запрос.УстановитьПараметр("Согласия", МассивСогласий);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		РегистрыСведений.СостояниеСогласияНаПрисоединениеККЭДО.УстановитьСостояние(Выборка.Ссылка, Выборка.Состояние, Истина);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

&НаСервереБезКонтекста
Функция ФайлыНаПодписание(МассивСогласий)
	
	Запрос = Новый Запрос();
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ДокументКадровогоЭДО.ЭлектронныйДокумент КАК ЭлектронныйДокумент
	               |ИЗ
	               |	Документ.ДокументКадровогоЭДО КАК ДокументКадровогоЭДО
	               |ГДЕ
	               |	ДокументКадровогоЭДО.ПометкаУдаления = ЛОЖЬ
	               |	И ДокументКадровогоЭДО.ОснованиеДокумента В(&МассивСогласий)";
	
	Запрос.УстановитьПараметр("МассивСогласий", МассивСогласий);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ЭлектронныйДокумент");
	
КонецФункции

#КонецОбласти
