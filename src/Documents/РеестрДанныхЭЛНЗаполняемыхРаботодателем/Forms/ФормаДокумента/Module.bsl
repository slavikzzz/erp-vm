#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая() Тогда
		ЗначенияДляЗаполнения = Новый Структура("Организация, Ответственный", "Объект.Организация", "Объект.Ответственный");
		ЗарплатаКадры.ЗаполнитьПервоначальныеЗначенияВФорме(ЭтотОбъект, ЗначенияДляЗаполнения);
		ОбновитьГоловнуюОрганизацию();
	Иначе
		НомерЛН = ОбщегоНазначенияБЗК.ЗначениеСвойства(Параметры, "НомерЛН");
		Если ЗначениеЗаполнено(НомерЛН) Тогда
			Найденные = Объект.ДанныеЭЛН.НайтиСтроки(Новый Структура("НомерЛисткаНетрудоспособности", НомерЛН));
			Для Каждого СтрокаТаблицы Из Найденные Цикл
				Элементы.ДанныеЭЛН.ТекущаяСтрока = СтрокаТаблицы.ПолучитьИдентификатор();
				ОткрытьФормуСтрокиПриОткрытии = Истина;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	ЭлементУО = ОбщегоНазначенияБЗК.ДобавитьУсловноеОформление(ЭтотОбъект, "ДанныеЭЛНСостояние, ДанныеЭЛНКомментарийКСостоянию");
	ОбщегоНазначенияБЗК.УстановитьПараметрУсловногоОформления(ЭлементУО, "ЦветТекста", ЦветаСтиля.РезультатУспехЦвет);
	ОбщегоНазначенияБЗК.ДобавитьОтборУсловногоОформления(ЭлементУО, "Объект.ДанныеЭЛН.Состояние", "=", Перечисления.СостоянияЭЛНВРеестрахЭЛН.ПринятФСС);
	
	ЭлементУО = ОбщегоНазначенияБЗК.ДобавитьУсловноеОформление(ЭтотОбъект, "ДанныеЭЛНСостояние, ДанныеЭЛНКомментарийКСостоянию");
	ОбщегоНазначенияБЗК.УстановитьПараметрУсловногоОформления(ЭлементУО, "ЦветТекста", ЦветаСтиля.РезультатПроблемаЦвет);
	ОбщегоНазначенияБЗК.ДобавитьОтборУсловногоОформления(ЭлементУО, "Объект.ДанныеЭЛН.Состояние", "=", Перечисления.СостоянияЭЛНВРеестрахЭЛН.НеПринятФСС);
	
	Элементы.ГруппаСоздатьСледующийРеестр.Видимость = Ложь;
	
	Если Объект.Ссылка.Пустая() Тогда
		ПриПолученииДанныхНаСервере("Объект");
	КонецЕсли;
	
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначения.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплата");
		МодульПроцессыОбработкиДокументовЗарплата.ПриСозданииНаСервере(ЭтотОбъект, Объект);
	КонецЕсли;	
	// Конец ПроцессыОбработкиДокументов
	
	ПраваНаФактическиеНачисления = СЭДОФСС.ПраваНаФактическиеНачисленияВДокументе(Объект);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	Если ИмяСобытия = "ПревышениеОграниченияКоличестваОтправляемыхЭЛН"
		И Параметр.Организация = Объект.Организация
		И Не Параметр.ВидимостьГруппыПревышениеОграниченияКоличестваОтправляемыхЭЛН Тогда
		Элементы.ГруппаСоздатьСледующийРеестр.Видимость = Ложь;
	КонецЕсли;
	
	Если ИмяСобытия = "Запись_БольничныйЛист" Тогда
		ОтключитьОбработчикОжидания("ОбновитьВторичныеДанныеНаКлиенте");
		ПодключитьОбработчикОжидания("ОбновитьВторичныеДанныеНаКлиенте", 5, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Если Не ТолькоПросмотр
		И ТекущийОбъект.Проведен
		И Не ТекущийОбъект.ПометкаУдаления
		И (ТекущийОбъект.СостояниеРеестра = Перечисления.СостоянияРеестровФСС.НеПринятФСС
			Или ТекущийОбъект.СостояниеРеестра = Перечисления.СостоянияРеестровФСС.ЧастичноПринятФСС) Тогда
		// Группа "ГруппаСоздатьЕщеОдинРеестр" рассчитывается на основании последнего состояния ЭЛН.
		ОсталисьНеПринятыеЭЛН = ТекущийОбъект.ВсеЭЛНПринятыФСС();
	Иначе
		// Группа "ГруппаСоздатьЕщеОдинРеестр" скрывается.
		ОсталисьНеПринятыеЭЛН = Ложь;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	ПриПолученииДанныхНаСервере(ТекущийОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначения.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплата");
		МодульПроцессыОбработкиДокументовЗарплата.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;	
	// Конец ПроцессыОбработкиДокументов
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	Если ОткрытьФормуСтрокиПриОткрытии Тогда
		ПодключитьОбработчикОжидания("ОткрытьФормуСтроки", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("Объект"));
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	Если Не ДокументОбъект.ПроверитьЗаполнение() Тогда
		Отказ = Истина;
	КонецЕсли;
	
	ОбработатьСообщенияПользователю(ДокументОбъект);
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	ФиксацияЗаполнитьИдентификаторыФиксТЧ(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ОбновитьГоловнуюОрганизацию();
	
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.СохранитьРеквизитыФормыФикс(ЭтотОбъект, ТекущийОбъект);
	
	// Перед записью переадресуем сообщения с полей объекта на поля формы
	Сообщения = ПолучитьСообщенияПользователю(Ложь);
	Для Каждого Сообщение Из Сообщения Цикл
		Если Найти(Сообщение.Поле, "].Больничный") > 0 Тогда
			Продолжить;
		КонецЕсли;
		
		НачалоНомераСтроки = Найти(Сообщение.Поле, "[");
		ОкончаниеНомераСтроки = Найти(Сообщение.Поле, "]");
		Если НачалоНомераСтроки > 0
			И ОкончаниеНомераСтроки > 0 Тогда
			НачалоНомераСтроки = НачалоНомераСтроки + 1;
			НомерСтроки = Сред(Сообщение.Поле, НачалоНомераСтроки, ОкончаниеНомераСтроки - НачалоНомераСтроки);
			Больничный = Объект.ДанныеЭЛН[Число(НомерСтроки)].Больничный;
			Если ЗначениеЗаполнено(Больничный) Тогда
				Сообщение.КлючДанных = Больничный;
				Сообщение.Поле = Прав(Сообщение.Поле, СтрДлина(Сообщение.Поле) - (ОкончаниеНомераСтроки + 1));
				Сообщение.ПутьКДанным = "Объект";
			Иначе
				Сообщение.Поле = Лев(Сообщение.Поле, Найти(Сообщение.Поле, "]")) + ".Больничный";
			КонецЕсли;
		КонецЕсли;
		Отказ = Истина;
	КонецЦикла;
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначения.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплата");
		МодульПроцессыОбработкиДокументовЗарплата.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи, Отказ);
	КонецЕсли;		
	// Конец ПроцессыОбработкиДокументов
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ФиксацияЗаполнитьРеквизитыФормыФикс(ТекущийОбъект);
	ФиксацияВторичныхДанныхВДокументахФормы.УстановитьОбъектЗафиксирован(ЭтотОбъект);
	ФиксацияОбновитьФиксациюВФорме();
	
	ОбновитьВидимостьДоступность();
	
	// ЗарплатаКадрыПодсистемы.ПодписиДокументов
	ПодписиДокументов.ПослеЗаписиНаСервере(ЭтотОбъект);
	// Конец ЗарплатаКадрыПодсистемы.ПодписиДокументов
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначения.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплата");
		МодульПроцессыОбработкиДокументовЗарплата.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;	
	// Конец ПроцессыОбработкиДокументов
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначенияКлиент.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплатаКлиент");
		МодульПроцессыОбработкиДокументовЗарплата.ПередЗакрытием(ЭтотОбъект, Объект, Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	КонецЕсли;	
	// Конец ПроцессыОбработкиДокументов
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("Запись_РеестрДанныхЭЛНЗаполняемыхРаботодателем", ПараметрыЗаписи, Объект.Ссылка);
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	ЗарплатаКадрыКлиент.КлючевыеРеквизитыЗаполненияФормыОчиститьТаблицы(ЭтотОбъект);
	ОрганизацияПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СостояниеРеестраОчистка(Элемент, СтандартнаяОбработка)
	Если Объект.Проведен И Не Объект.ПометкаУдаления Тогда
		Объект.СостояниеРеестра = ПредопределенноеЗначение("Перечисление.СостоянияРеестровФСС.Подготовлен");
	Иначе
		Объект.СостояниеРеестра = ПредопределенноеЗначение("Перечисление.СостоянияРеестровФСС.ВРаботе");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СостояниеРеестраПриИзменении(Элемент)
	ОчиститьСообщения();
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Объект.Проведен Тогда
		ПараметрыЗаписи = Новый Структура;
		ПараметрыЗаписи.Вставить("РежимЗаписи", РежимЗаписиДокумента.Проведение);
		Если Не Записать(ПараметрыЗаписи) Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	СостояниеРеестраПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.КомментарийНачалоВыбора(ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка)
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыДанныеЭЛН

&НаКлиенте
Процедура ВывестиОповещениеПользователя(Структура)
	Если Структура.Свойство("ОповещениеПользователя") Тогда
		ПоказатьОповещениеПользователя(
			Структура.ОповещениеПользователя.Текст,
			Структура.ОповещениеПользователя.ДействиеПриНажатии,
			Структура.ОповещениеПользователя.Пояснение,
			Структура.ОповещениеПользователя.Картинка);
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПодготовитьОповещениеПользователя(Структура, Текст, ДействиеПриНажатии = Неопределено, Пояснение = "", Картинка = Неопределено)
	ОповещениеПользователя = Новый Структура("Текст, ДействиеПриНажатии, Пояснение, Картинка", Текст, ДействиеПриНажатии, Пояснение, Картинка);
	Структура.Вставить("ОповещениеПользователя", ОповещениеПользователя);
КонецПроцедуры

&НаСервере
Функция ДанныеЭЛНОбработкаВыбораНаСервере(Знач РезультатВыбора) Экспорт
	Если ОбъектЗафиксирован() Тогда
		Текст = НСтр("ru = 'Документ отправлен';
					|en = 'Document sent'");
		Пояснение = НСтр("ru = 'Добавление новых ЭЛН невозможно';
						|en = 'Adding new ESLRs is not available'");
		ПодготовитьОповещениеПользователя(РезультатВыбора, Текст, , Пояснение, БиблиотекаКартинок.Предупреждение32);
		Возврат Ложь;
	КонецЕсли;
	Найденные = Объект.ДанныеЭЛН.НайтиСтроки(Новый Структура("НомерЛисткаНетрудоспособности", РезультатВыбора.НомерЛисткаНетрудоспособности));
	Если Найденные.Количество() > 0 Тогда
		Элементы.ДанныеЭЛН.ТекущаяСтрока = Найденные[0].ПолучитьИдентификатор();
		Текст = НСтр("ru = 'ЭЛН уже выбран в реестре';
					|en = 'ESLR is already selected in registry'");
		ПодготовитьОповещениеПользователя(РезультатВыбора, Текст, , , БиблиотекаКартинок.Информация32);
		Возврат Истина;
	КонецЕсли;
	Количество = Объект.ДанныеЭЛН.Количество();
	Если Количество >= 30 Тогда
		РезультатВыбора.Отказ = Истина;
		Текст = НСтр("ru = 'Достигнуто ограничение в 30 ЭЛН';
					|en = 'Limit of 30 ESLRs reached'");
		Пояснение = НСтр("ru = 'Добавление новых ЭЛН невозможно';
						|en = 'Adding new ESLRs is not available'");
		ПодготовитьОповещениеПользователя(РезультатВыбора, Текст, , Пояснение, БиблиотекаКартинок.Предупреждение32);
		Элементы.ГруппаСоздатьСледующийРеестр.Видимость = Истина;
		Возврат Ложь;
	Иначе
		СтрокаТаблицы = Объект.ДанныеЭЛН.Добавить();
		СтрокаТаблицы.НомерЛисткаНетрудоспособности = РезультатВыбора.НомерЛисткаНетрудоспособности;
		СтрокаТаблицы.Больничный = РезультатВыбора.Больничный;
		Элементы.ДанныеЭЛН.ТекущаяСтрока = СтрокаТаблицы.ПолучитьИдентификатор();
		ОбновитьВторичныеДанныеНаСервере(Ложь);
		Текст = СтрШаблон(НСтр("ru = 'ЭЛН добавлен в реестр под номером %1';
								|en = 'ESLR is added to registry under number %1'"), Количество + 1);
		ПодготовитьОповещениеПользователя(РезультатВыбора, Текст, , , БиблиотекаКартинок.Успешно32);
		Элементы.ГруппаСоздатьСледующийРеестр.Видимость = Ложь;
		Возврат Истина;
	КонецЕсли;
КонецФункции

&НаКлиенте
Процедура ДанныеЭЛНОбработкаВыбора(Элемент, РезультатВыбора, СтандартнаяОбработка)
	Если ТипЗнч(РезультатВыбора) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	СтандартнаяОбработка = Ложь;
	Если Не ДанныеЭЛНОбработкаВыбораНаСервере(РезультатВыбора) Тогда
		РезультатВыбора.Отказ = Истина;
	КонецЕсли;
	ВывестиОповещениеПользователя(РезультатВыбора);
КонецПроцедуры

&НаКлиенте
Процедура ДанныеЭЛНПослеУдаления(Элемент)
	ЗарплатаКадрыКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтотОбъект);
	Если Объект.ДанныеЭЛН.Количество() <= 30 Тогда
		Элементы.ГруппаСоздатьСледующийРеестр.Видимость = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДанныеЭЛНПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	ДанныеЭЛНПриОкончанииРедактированияНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ДанныеЭЛНВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Поле = Элементы.ДанныеЭЛНСостояние
		Или Поле = Элементы.ДанныеЭЛНКомментарийКСостоянию
		Или Поле = Элементы.ДанныеЭЛНБольничный Тогда
		Возврат;
	КонецЕсли;
	СтандартнаяОбработка = Ложь;
	ОткрытьФормуСтроки();
КонецПроцедуры

&НаКлиенте
Процедура ДанныеЭЛНБольничныйПриИзменении(Элемент)
	ИдентификаторСтроки = Элементы.ДанныеЭЛН.ТекущаяСтрока;
	Если ИдентификаторСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	БольничныйПриИзмененииНаСервере(ИдентификаторСтроки);
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#Область ОбработчикиСобытийПроцессыОбработкиДокументов

&НаКлиенте
Процедура Подключаемый_ВыполнитьЗадачуПоОбработкеДокумента(Команда)
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначенияКлиент.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплатаКлиент");
		МодульПроцессыОбработкиДокументовЗарплата.ВыполнитьЗадачу(ЭтотОбъект, Команда, Объект)
	КонецЕсли;		
	// Конец ПроцессыОбработкиДокументов
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьЗадачуПоОбработкеДокументаОповещение(Контекст, ДополнительныеПараметры) Экспорт
	ВыполнитьЗадачуПоОбработкеДокументаНаСервере(Контекст);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры, Контекст);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьЗадачуПоОбработкеДокументаНаСервере(Контекст)
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда	
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначения.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплата");
		МодульПроцессыОбработкиДокументовЗарплата.ВыполнитьЗадачу(ЭтотОбъект, Контекст, Объект);
	КонецЕсли;		
	// Конец ПроцессыОбработкиДокументов
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КомментарийНаправившегоОткрытие(Элемент, СтандартнаяОбработка)
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда	
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначенияКлиент.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплатаКлиент");
		МодульПроцессыОбработкиДокументовЗарплата.КомментарийНаправившегоОткрытие(ЭтотОбъект, Элемент, СтандартнаяОбработка);
	КонецЕсли;		
	// Конец ПроцессыОбработкиДокументов
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КомментарийСледующемуНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда	
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначенияКлиент.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплатаКлиент");
		МодульПроцессыОбработкиДокументовЗарплата.КомментарийСледующемуНачалоВыбора(ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка);
	КонецЕсли;		
	// Конец ПроцессыОбработкиДокументов
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура Заполнить(Команда)
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не выбрана организация.';
														|en = 'Company is not selected.'"), , "Объект.Организация");
		Возврат;
	КонецЕсли;
	ЗаполнитьТаблицуЭЛН();
	Оповестить_ВидимостьГруппыПревышениеОграниченияКоличестваОтправляемыхЭЛН();
КонецПроцедуры

&НаКлиенте
Процедура Оповестить_ВидимостьГруппыПревышениеОграниченияКоличестваОтправляемыхЭЛН()
	Параметр = Новый Структура;
	Параметр.Вставить("Организация", Объект.Организация);
	Параметр.Вставить("ВидимостьГруппыПревышениеОграниченияКоличестваОтправляемыхЭЛН", Элементы.ГруппаСоздатьСледующийРеестр.Видимость);
	Оповестить("ПревышениеОграниченияКоличестваОтправляемыхЭЛН", Параметр, ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьЭЛН(Команда)
	Если ОбъектЗафиксирован() Тогда
		Текст = НСтр("ru = 'Реестр отправлен.
			|Добавление новых ЭЛН невозможно.';
			|en = 'The registry has been sent.
			|No new ESLRs can be added.'");
		ПоказатьПредупреждение(, Текст);
		Возврат;
	ИначеЕсли Объект.ДанныеЭЛН.Количество() >= 30 Тогда
		Текст = НСтр("ru = 'Достигнуто ограничение в 30 ЭЛН.
			|Добавление новых ЭЛН невозможно.';
			|en = 'The limit of 30 ESLRs reached.
			|No new ESLRs can be added.'");
		ПоказатьПредупреждение(, Текст);
		Возврат;
	КонецЕсли;
	ВыбранныеНомераЭЛН = Новый Массив;
	Для Каждого СтрокаТаблицы Из Объект.ДанныеЭЛН Цикл
		ВыбранныеНомераЭЛН.Добавить(СтрокаТаблицы.НомерЛисткаНетрудоспособности);
	КонецЦикла;
	
	ОтборФормы = Новый Структура;
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		ОтборФормы.Вставить("Организация", Объект.Организация);
	КонецЕсли;
	
	ПараметрыФормыВыбора = Новый Структура;
	ПараметрыФормыВыбора.Вставить("РежимВыбора", Истина);
	ПараметрыФормыВыбора.Вставить("ЗакрыватьПриВыборе", Ложь);
	ПараметрыФормыВыбора.Вставить("ЗакрыватьПриЗакрытииВладельца", Истина);
	ПараметрыФормыВыбора.Вставить("МножественныйВыбор", Ложь);
	ПараметрыФормыВыбора.Вставить("ВыбранныеНомераЭЛН", ВыбранныеНомераЭЛН);
	ПараметрыФормыВыбора.Вставить("Отбор", ОтборФормы);
	
	ОткрытьФорму("РегистрСведений.СведенияОбЭЛН.ФормаСписка", ПараметрыФормыВыбора, Элементы.ДанныеЭЛН, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВторичныеДанные(Команда)
	ОбновитьВторичныеДанныеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьВсеИсправления(Команда)
	ОтменитьВсеИсправленияНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьРеестрВФСС(Команда)
	Обработчик = Новый ОписаниеОповещения("ОтправитьВКонтролирующийОрганПослеПроверки", ЭтотОбъект);
	ЗаписатьИПроверитьПоРегламентуФСС(Обработчик, РежимЗаписиДокумента.Проведение);
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьФайлРеестра(Команда)
	Обработчик = Новый ОписаниеОповещения("ЗаписатьФайлРеестраПослеПроверки", ЭтотОбъект);
	ЗаписатьИПроверитьПоРегламентуФСС(Обработчик, РежимЗаписиДокумента.Проведение);
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьФайлРеестра(Команда)
	Обработчик = Новый ОписаниеОповещения("ПоказатьФайлРеестраПослеПроверки", ЭтотОбъект);
	ЗаписатьИПроверитьПоРегламентуФСС(Обработчик, РежимЗаписиДокумента.Проведение);
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// Работа с формой.

&НаСервере
Процедура ПриПолученииДанныхНаСервере(ТекущийОбъект)
	// ЗарплатаКадрыПодсистемы.ПодписиДокументов
	ПодписиДокументов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец ЗарплатаКадрыПодсистемы.ПодписиДокументов
	
	ФиксацияВторичныхДанныхВДокументахФормы.ИнициализироватьМеханизмФиксацииРеквизитов(ЭтотОбъект, ТекущийОбъект);
	ФиксацияВторичныхДанныхВДокументахФормы.ПодключитьОбработчикиФиксацииИзмененийРеквизитов(ЭтотОбъект, ФиксацияБыстрыйПоискРеквизитов());
	
	ЗарплатаКадры.КлючевыеРеквизитыЗаполненияФормыЗаполнитьПредупреждения(ЭтотОбъект);
	ЗарплатаКадрыКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтотОбъект);
	
	ОбновитьВторичныеДанныеНаСервере();
	ОбновитьИнформационнуюПанельБРО();
	ФиксацияОбновитьФиксациюВФорме();
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обслуживание элементов шапки.

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	ОбновитьГоловнуюОрганизацию();
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.СброситьФиксациюИзмененийРеквизитовПоОснованиюЗаполнения(ЭтотОбъект, "Организация");
	ОбновитьВторичныеДанныеНаСервере();
	ОбновитьИнформационнуюПанельБРО();
	ФиксацияОбновитьФиксациюВФорме();
КонецПроцедуры

&НаСервере
Процедура ОбновитьГоловнуюОрганизацию()
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		Объект.ГоловнаяОрганизация = ЗарплатаКадры.ГоловнаяОрганизация(Объект.Организация);
	Иначе
		Объект.ГоловнаяОрганизация = Справочники.Организации.ПустаяСсылка();
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обслуживание команд.

&НаКлиенте
Процедура ОтправитьВКонтролирующийОрганПослеПроверки(ЕстьОшибки, ПустойПараметр) Экспорт
	// Формирование параметров запроса к сервису отправки Реестров ЭЛН.
	РезультатВыгрузки = ВыгрузитьРеестрЭЛН(Ложь, Истина, Истина);
	Если РезультатВыгрузки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Подписание и отправка запроса, расшифровка ответа.
	Оповещение = Новый ОписаниеОповещения("ОтправитьВКонтролирующийОрганЗавершение", ЭтотОбъект);
	ЭлектронныйДокументооборотСФССКлиент.ОтправитьВыгрузкуРеестраДанныхЭЛНВСервисФСС(Оповещение, РезультатВыгрузки);
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьВКонтролирующийОрганЗавершение(Результат, ПустойПараметр) Экспорт
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Загрузка ответа ФСС.
	ЗагрузитьОтветСервисаФССВДанныеФормы(Результат.АдресРасшифрованногоОтветаSOAP);
	АктуализироватьХешиЭЛНИПровестиДокумент();
КонецПроцедуры

#Область АктуализацияХешейБольничных

&НаКлиенте
Процедура АктуализироватьХешиЭЛНИПровестиДокумент()
	Найденные = Объект.ДанныеЭЛН.НайтиСтроки(Новый Структура("ТребуетсяАктуализацияХеша", Истина));
	Если Найденные.Количество() = 0 Тогда
		Если ТребуетсяПовторнаяОтправка Тогда
			// Произведена актуализации хешей, требуется повторный обмен.
			ТребуетсяПовторнаяОтправка = Ложь;
			ОтправитьРеестрВФСС(Неопределено);
		Иначе
			// Обмен завершен, требуется проведение документа.
			ПараметрыЗаписи = Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение);
			Записать(ПараметрыЗаписи);
			ПоказатьОповещениеПользователя(, , НСтр("ru = 'Реестр ЭЛН отправлен в ФСС.';
													|en = 'ESLR registry is sent to SSF.'"), БиблиотекаКартинок.Информация32);
		КонецЕсли;
	Иначе
		// Формирование параметров запроса к сервису получения ЭЛН.
		ИдентификаторСтроки = Найденные[0].ПолучитьИдентификатор();
		ЗапросДляПолученияЭЛН = ВыгрузитьЗапросДляПолученияЭЛН(ИдентификаторСтроки);
		Если ЗапросДляПолученияЭЛН = Неопределено Тогда
			// Не удалось сформировать запрос, переход к следующему больничному.
			СтрокаТаблицы = Объект.ДанныеЭЛН.НайтиПоИдентификатору(ИдентификаторСтроки);
			СтрокаТаблицы.ТребуетсяАктуализацияХеша = Ложь;
			АктуализироватьХешиЭЛНИПровестиДокумент();
		Иначе
			// Подписание и отправка запроса, расшифровка ответа.
			Оповещение = Новый ОписаниеОповещения("ПродолжитьАктуализациюХешейЭЛН", ЭтотОбъект, ИдентификаторСтроки);
			ЭлектронныйДокументооборотСФССКлиент.ПолучитьДанныеЭЛНИзФСС(Оповещение, ЗапросДляПолученияЭЛН);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьАктуализациюХешейЭЛН(Результат, ИдентификаторСтроки) Экспорт
	// Загрузка ответа.
	СтрокаТаблицы = Объект.ДанныеЭЛН.НайтиПоИдентификатору(ИдентификаторСтроки);
	СтрокаТаблицы.ТребуетсяАктуализацияХеша = Ложь;
	Если Результат <> Неопределено
		И ЗагрузитьХешЭЛН(Результат.АдресРасшифрованногоОтветаSOAP, ИдентификаторСтроки) Тогда
		ТребуетсяПовторнаяОтправка = Истина;
	КонецЕсли;
	// Переход к следующему больничному.
	АктуализироватьХешиЭЛНИПровестиДокумент();
КонецПроцедуры

&НаСервере
Функция ВыгрузитьЗапросДляПолученияЭЛН(ИдентификаторСтроки)
	БольничныйВВидеСтруктуры = БольничныйВВидеСтруктуры(Объект.ДанныеЭЛН.НайтиПоИдентификатору(ИдентификаторСтроки));
	Возврат ОбменЛисткамиНетрудоспособностиФСС.ВыгрузитьЗапросДляПолученияЭЛН(БольничныйВВидеСтруктуры);
КонецФункции

&НаСервере
Функция БольничныйВВидеСтруктуры(СтрокаТаблицы)
	БольничныйВВидеСтруктуры = Новый Структура("Организация, Дата, Сотрудник, НомерЛисткаНетрудоспособности");
	ЗаполнитьЗначенияСвойств(БольничныйВВидеСтруктуры, Объект, "Организация, Дата");
	ЗаполнитьЗначенияСвойств(БольничныйВВидеСтруктуры, СтрокаТаблицы, "Сотрудник, НомерЛисткаНетрудоспособности");
	Возврат БольничныйВВидеСтруктуры;
КонецФункции

&НаСервере
Функция ЗагрузитьХешЭЛН(Знач АдресРасшифрованногоОтветаSOAP, ИдентификаторСтроки)
	// У строки документа снимается признак необходимости актуализации хеша.
	СтрокаТаблицы = Объект.ДанныеЭЛН.НайтиПоИдентификатору(ИдентификаторСтроки);
	СтрокаТаблицы.ТребуетсяАктуализацияХеша = Ложь;
	
	// Хеш записывается в регистр сведений СведенияОбЭЛН.
	БольничныйВВидеСтруктуры = БольничныйВВидеСтруктуры(СтрокаТаблицы);
	РезультатОбмена = ОбменЛисткамиНетрудоспособностиФСС.ЗагрузитьОтветСервисаФСС(
		БольничныйВВидеСтруктуры,
		"getPrivateLNData",
		АдресРасшифрованногоОтветаSOAP,
		Ложь);
	
	Возврат Не РезультатОбмена.Отказ;
КонецФункции

#КонецОбласти

&НаСервере
Функция ВыгрузитьРеестрЭЛН(ПомещатьВФайл, ВнутреннийФормат, ИсключатьПринятые)
	// Регистрационный номер ФСС организации (страхователя).
	Если Не ЗначениеЗаполнено(Объект.РегистрационныйНомерФСС) Тогда
		Текст = НСтр("ru = 'У организации не заполнен регистрационный номер ФСС';
					|en = 'Company SSF registration number is required'");
		ОбщегоНазначения.СообщитьПользователю(Текст, , "РегистрационныйНомерФСС", "Объект");
		Возврат Неопределено;
	КонецЕсли;
	
	РезультатВыгрузки = ОбменЛисткамиНетрудоспособностиФСС.ВыгрузитьЗапросДляОтправкиРеестраЭЛН(
		Объект, ПомещатьВФайл, УникальныйИдентификатор, ВнутреннийФормат, ИсключатьПринятые);
	
	ИмяБезРасширения = СтрШаблон("%1_%2_%3_%4",
		"prParseReestrFile",
		Объект.РегистрационныйНомерФСС,
		Формат(Объект.Дата, "ДФ=yyyy-MM-dd"),
		СокрЛП(Объект.Номер));
	РезультатВыгрузки.Вставить("ИмяБезРасширения", ИмяБезРасширения);
	
	Возврат РезультатВыгрузки;
КонецФункции

&НаСервере
Процедура ЗагрузитьОтветСервисаФССВДанныеФормы(Знач АдресРасшифрованногоОтветаSOAP)
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ДокументОбъект.ДатаПолученияОтветаФСС = ТекущаяДатаСеанса();
	
	ОбменЛисткамиНетрудоспособностиФСС.ЗагрузитьОтветСервисаФСС(
		ДокументОбъект,
		"prParseReestrFile",
		АдресРасшифрованногоОтветаSOAP,
		Ложь);
	
	ДокументОбъект.СостояниеРеестра = ДокументОбъект.СостояниеДокументаИзСостоянийЭЛН();
	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
	
	// Группа "ГруппаСоздатьЕщеОдинРеестр" рассчитывается на основании данных шапки.
	ОсталисьНеПринятыеЭЛН = Истина;
	
	ОбновитьВидимостьДоступность();
КонецПроцедуры

&НаСервере
Процедура СостояниеРеестраПриИзмененииНаСервере()
	СостояниеЭЛН = УчетПособийСоциальногоСтрахования.СостояниеЭЛНИзСостоянияРеестраЭЛН(Объект.СостояниеРеестра);
	Если СостояниеЭЛН <> Неопределено Тогда
		Для Каждого СтрокаТаблицы Из Объект.ДанныеЭЛН Цикл
			СтрокаТаблицы.Состояние = СостояниеЭЛН;
		КонецЦикла;
	КонецЕсли;
	ОбновитьВидимостьДоступность();
	ФиксацияВторичныхДанныхВДокументахФормы.УстановитьОбъектЗафиксирован(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ОбновитьВидимостьДоступность()
	Если ЗначениеЗаполнено(Объект.ДатаПолученияОтветаФСС) Тогда
		ТолькоПросмотр = Истина;
		Элементы.ДатаПолученияОтветаФСС.Видимость = Истина;
		РеестрПроведен = Истина;
	Иначе
		Элементы.ДатаПолученияОтветаФСС.Видимость = Ложь;
		РеестрПроведен = (Объект.Проведен И Не Объект.ПометкаУдаления);
	КонецЕсли; // Флажок ТолькоПросмотр не отключается, т.к. отправленный реестр не подлежит отзыву.
	
	РеестрПринят         = (Объект.СостояниеРеестра = Перечисления.СостоянияРеестровФСС.ПринятФСС);
	РеестрНеПринят       = (Объект.СостояниеРеестра = Перечисления.СостоянияРеестровФСС.НеПринятФСС);
	РеестрЧастичноПринят = (Объект.СостояниеРеестра = Перечисления.СостоянияРеестровФСС.ЧастичноПринятФСС);
	РеестрОтправлен          = РеестрПринят Или РеестрНеПринят Или РеестрЧастичноПринят;
	РеестрПроведенИОтправлен = РеестрПроведен И РеестрОтправлен;
	
	Элементы.Организация.ТолькоПросмотр             = РеестрПроведенИОтправлен;
	Элементы.РегистрационныйНомерФСС.ТолькоПросмотр = РеестрПроведенИОтправлен;
	Элементы.ШапкаКолонка2.ТолькоПросмотр           = РеестрПроведенИОтправлен;
	Элементы.ГруппаОтветственныеЛица.ТолькоПросмотр = РеестрПроведенИОтправлен;
	Если РеестрПроведен И РеестрПринят Тогда
		Элементы.ДанныеЭЛН.ТолькоПросмотр = Истина;
	Иначе
		Элементы.ДанныеЭЛН.ТолькоПросмотр = Ложь;
		Элементы.ДанныеЭЛН.КоманднаяПанель.ТолькоПросмотр              = РеестрПроведенИОтправлен;
		Элементы.ДанныеЭЛНСотрудник.ТолькоПросмотр                     = РеестрПроведенИОтправлен;
		Элементы.ДанныеЭЛННомерЛисткаНетрудоспособности.ТолькоПросмотр = РеестрПроведенИОтправлен;
		Элементы.ДанныеЭЛНБольничный.ТолькоПросмотр                    = РеестрПроведенИОтправлен;
		Элементы.ДанныеЭЛНСостояние.ТолькоПросмотр                     = РеестрПроведен И (РеестрПринят Или РеестрНеПринят);
		Элементы.ДанныеЭЛНКомментарийКСостоянию.ТолькоПросмотр         = РеестрПроведен И РеестрПринят;
	КонецЕсли;
	Элементы.ДанныеЭЛНЗаполнить.Доступность            = Не РеестрПроведенИОтправлен;
	Элементы.ДанныеЭЛНПодобратьЭЛН.Доступность         = Не РеестрПроведенИОтправлен;
	Элементы.ДанныеЭЛНСтандартныеКоманды.Доступность   = Не РеестрПроведенИОтправлен;
	Элементы.ДанныеЭЛНСостояние.Видимость              = РеестрПроведенИОтправлен;
	Элементы.ДанныеЭЛНКомментарийКСостоянию.Видимость  = РеестрПроведенИОтправлен;
	Элементы.ГруппаСостояниеРеестра.Видимость          = РеестрПроведен;
	Элементы.ГруппаПомеченНаУдаление.Видимость         = Не РеестрПроведен И Объект.ПометкаУдаления;
	Элементы.ГруппаНеПроведен.Видимость                = Не РеестрПроведен И Не Объект.ПометкаУдаления;
	Элементы.ОтправитьВКонтролирующийОрган.Доступность = РеестрПроведен И Не РеестрОтправлен;
	Элементы.ПодменюВыгрузить.Доступность              = РеестрПроведен;
	Элементы.ФормаОбновитьВторичныеДанные.Видимость    = Не РеестрПроведенИОтправлен;
	Элементы.ФормаОтменитьВсеИсправления.Видимость     = Не РеестрПроведенИОтправлен;
	Элементы.ГруппаСоздатьЕщеОдинРеестр.Видимость      = ОсталисьНеПринятыеЭЛН И РеестрПроведен И (РеестрЧастичноПринят Или РеестрНеПринят);
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьФайлРеестраПослеПроверки(ЕстьОшибки, ПустойПараметр) Экспорт
	ОписаниеФайла = ВыгрузитьРеестрЭЛН(Истина, Ложь, Ложь);
	УчетПособийСоциальногоСтрахованияКлиент.СохранитьФайлXML(ОписаниеФайла);
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьФайлРеестраПослеПроверки(ЕстьОшибки, ПустойПараметр) Экспорт
	РезультатВыгрузки = ВыгрузитьРеестрЭЛН(Ложь, Ложь, Ложь);
	Если РезультатВыгрузки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.УстановитьТекст(РезультатВыгрузки.ТекстXML);
	ТекстовыйДокумент.Показать(, РезультатВыгрузки.ИмяБезРасширения + ".xml");
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обслуживание табличной части ДанныеЭЛН.

&НаКлиенте
Процедура ОткрытьФормуСтроки()
	ТекущиеДанные = Элементы.ДанныеЭЛН.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СписокРеквизитов = ИменаРеквизитовСтрокиНаФорме();
	СписокРеквизитов.Добавить("НомерСтроки");
	СписокРеквизитов.Добавить("Больничный");
	СтруктураДанныеЭЛН = Новый Структура;
	Для Каждого ЭлементСписка Из СписокРеквизитов Цикл
		СтруктураДанныеЭЛН.Вставить(ЭлементСписка.Значение, ТекущиеДанные[ЭлементСписка.Значение]);
	КонецЦикла;
	
	ИнформацияОДокументе = Новый Структура;
	ИнформацияОДокументе.Вставить("Ссылка", Объект.Ссылка);
	ИнформацияОДокументе.Вставить("ЭтоНовый", Объект.Ссылка.Пустая());
	ИнформацияОДокументе.Вставить("Номер", Объект.Номер);
	ИнформацияОДокументе.Вставить("Дата", Объект.Дата);
	ИнформацияОДокументе.Вставить("Организация", Объект.Организация);
	ИнформацияОДокументе.Вставить("ГоловнаяОрганизация", Объект.ГоловнаяОрганизация);
	ИнформацияОДокументе.Вставить("ОбъектЗафиксирован", ОбъектЗафиксирован());
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ДанныеЭЛН", СтруктураДанныеЭЛН);
	ПараметрыФормы.Вставить("ИнформацияОДокументе", ИнформацияОДокументе);
	ПараметрыФормы.Вставить("ПраваНаФактическиеНачисления", ПраваНаФактическиеНачисления);
	ПараметрыФормы.Вставить("ТолькоПросмотр", ТолькоПросмотр);
	
	Оповещение = Новый ОписаниеОповещения("ДанныеЭЛНВыборЗавершение", ЭтотОбъект);
	
	ОткрытьФорму(
		"Документ.РеестрДанныхЭЛНЗаполняемыхРаботодателем.Форма.СведенияОЛисткеНетрудоспособности",
		ПараметрыФормы,
		ЭтотОбъект,
		,
		,
		,
		Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаСервере
Процедура ДанныеЭЛНПриОкончанииРедактированияНаСервере()
	ЗарплатаКадрыКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ДанныеЭЛНВыборЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОтобранныеСтроки = Объект.ДанныеЭЛН.НайтиСтроки(Новый Структура("НомерСтроки", Результат.НомерСтроки));
	Результат.Удалить("НомерСтроки");
	Если ОтобранныеСтроки.Количество() = 1 Тогда
		ЗаполнитьЗначенияСвойств(ОтобранныеСтроки[0], Результат);
		ОбновитьВторичныеДанныеНаСервере(Истина);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура БольничныйПриИзмененииНаСервере(ИдентификаторСтроки) Экспорт
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.СброситьФиксациюСтрокиТаблицы(ЭтотОбъект, "Больничный", ИдентификаторСтроки);
	ОбновитьВторичныеДанныеНаСервере();
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Заполнение документа.

&НаСервере
Процедура ЗаполнитьТаблицуЭЛН()
	Если ОбъектЗафиксирован() Тогда
		Возврат;
	КонецЕсли;
	УжеДобавленныеЭЛН = Объект.ДанныеЭЛН.Выгрузить(, "НомерЛисткаНетрудоспособности").ВыгрузитьКолонку("НомерЛисткаНетрудоспособности");
	ЗапрашиваемоеКоличество = Макс(30 - УжеДобавленныеЭЛН.Количество(), 0) + 1;
	Если Объект.Проведен Тогда
		ПодготовленныйКОтправкеРеестр = Объект.Ссылка;
	Иначе
		ПодготовленныйКОтправкеРеестр = Неопределено;
	КонецЕсли;
	ТаблицаОснований = РегистрыСведений.СведенияОбЭЛН.ТаблицаОснованийДляРеестраЭЛН(Объект.Организация, ПодготовленныйКОтправкеРеестр, Неопределено, УжеДобавленныеЭЛН, ЗапрашиваемоеКоличество);
	Если ТаблицаОснований.Количество() = ЗапрашиваемоеКоличество Тогда
		ТаблицаОснований.Удалить(ЗапрашиваемоеКоличество-1);
		Элементы.ГруппаСоздатьСледующийРеестр.Видимость = Истина;
	Иначе
		Элементы.ГруппаСоздатьСледующийРеестр.Видимость = Ложь;
	КонецЕсли;
	Для Каждого СтрокаТаблицы Из ТаблицаОснований Цикл
		ЗаполнитьЗначенияСвойств(Объект.ДанныеЭЛН.Добавить(), СтрокаТаблицы);
	КонецЦикла;
	ОбновитьВторичныеДанныеНаСервере();
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ИменаРеквизитовСтрокиНаФорме()
	СписокРеквизитов = Новый СписокЗначений;
	
	СписокРеквизитов.Добавить("НомерЛисткаНетрудоспособности");
	СписокРеквизитов.Добавить("Сотрудник");
	СписокРеквизитов.Добавить("ВидЗанятости");
	СписокРеквизитов.Добавить("СНИЛС");
	СписокРеквизитов.Добавить("ИНН");
	СписокРеквизитов.Добавить("СтажЛет");
	СписокРеквизитов.Добавить("СтажМесяцев");
	СписокРеквизитов.Добавить("СтажДней");
	СписокРеквизитов.Добавить("СтажРасширенныйЛет");
	СписокРеквизитов.Добавить("СтажРасширенныйМесяцев");
	СписокРеквизитов.Добавить("СтажРасширенныйДней");
	СписокРеквизитов.Добавить("УсловияИсчисленияКод1");
	СписокРеквизитов.Добавить("УсловияИсчисленияКод2");
	СписокРеквизитов.Добавить("УсловияИсчисленияКод3");
	СписокРеквизитов.Добавить("ДатаАктаН1");
	СписокРеквизитов.Добавить("ДатаНачалаРаботы");
	СписокРеквизитов.Добавить("ПриступитьКРаботеС");
	СписокРеквизитов.Добавить("ДатаНачалаОплаты");
	СписокРеквизитов.Добавить("ДатаОкончанияОплаты");
	СписокРеквизитов.Добавить("ДатаНачалаОплатыФСС");
	СписокРеквизитов.Добавить("ДатаОкончанияОплатыФСС");
	СписокРеквизитов.Добавить("БазаДляРасчетаСреднегоЗаработка");
	СписокРеквизитов.Добавить("СреднийДневнойЗаработок");
	СписокРеквизитов.Добавить("СуммаОплатыЗаСчетРаботодателя");
	СписокРеквизитов.Добавить("СуммаОплатыЗаСчетФСС");
	СписокРеквизитов.Добавить("ФинансированиеФедеральнымБюджетом");
	СписокРеквизитов.Добавить("Исправление");
	СписокРеквизитов.Добавить("КодПричиныИсправления");
	СписокРеквизитов.Добавить("ОписаниеПричиныИсправления");
	СписокРеквизитов.Добавить("ИдентификаторСтрокиФикс");
	
	Возврат СписокРеквизитов;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Проверка заполнения

&НаСервере
Процедура ОбработатьСообщенияПользователю(ДокументОбъект, Сообщения = Неопределено)
	Если Сообщения = Неопределено Тогда
		Сообщения = ПолучитьСообщенияПользователю(Ложь);
	КонецЕсли;
	// В сообщениях есть следующие поля:
	//   ИдентификаторНазначения - Идентификатор формы, в которой необходимо вывести сообщение.
	//   КлючДанных - Ссылка объекта, который необходимо открыть для обработки сообщения.
	//   Поле - Путь от объекта к ошибочному полю.
	//   ПутьКДанным - Путь от формы к объекту.
	//   Текст - Текст сообщения.
	// Как правило, на сервере заполняются только "Поле" и "Текст".
	// Задача процедуры в том, чтобы заполнить "ПутьКДанным" и адаптировать "Поле" к специфике формы.
	
	Для Каждого Сообщение Из Сообщения Цикл
		// Привязка сообщений к объекту.
		Если Не ЗначениеЗаполнено(Сообщение.КлючДанных)
			И Не ЗначениеЗаполнено(Сообщение.ПутьКДанным)
			И Не СтрНачинаетсяС(Сообщение.Поле, "Объект.") Тогда
			Сообщение.ПутьКДанным = "Объект";
		КонецЕсли;
		// Адаптация к специфике редактирования данных в форме.
		НачалоСкобки = СтрНайти(Сообщение.Поле, "[");
		Если НачалоСкобки = 0 Тогда
			Продолжить;
		КонецЕсли;
		ОкончаниеСкобки = СтрНайти(Сообщение.Поле, "].");
		Если НачалоСкобки > ОкончаниеСкобки Тогда
			Продолжить;
		КонецЕсли;
		ИмяТаблицы   = Лев(Сообщение.Поле, НачалоСкобки - 1);
		ИндексСтроки = Сред(Сообщение.Поле, НачалоСкобки + 1, ОкончаниеСкобки - НачалоСкобки - 1);
		ИмяРеквизита = Сред(Сообщение.Поле, ОкончаниеСкобки + 2);
		Если ИмяТаблицы = "ДанныеЭЛН"
			И ИмяРеквизита <> "Больничный"
			И ИмяРеквизита <> "НомерЛисткаНетрудоспособности"
			И ИмяРеквизита <> "Сотрудник" Тогда
			Сообщение.Поле = ИмяТаблицы + "[" + ИндексСтроки + "].НомерЛисткаНетрудоспособности";
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИПроверитьПоРегламентуФСС(ОбработчикЗавершения, РежимЗаписи)
	ОчиститьСообщения();
	
	ПараметрыЗаписи = Новый Структура("РежимЗаписи", РежимЗаписи);
	Если Не Записать(ПараметрыЗаписи) Тогда
		Возврат;
	КонецЕсли;
	
	РезультатПроверки = ПроверитьЗаполнениеПоРегламентуФСС();
	
	Если РезультатПроверки.Успех Тогда
		ВыполнитьОбработкуОповещения(ОбработчикЗавершения, Ложь);
	Иначе
		Обработчик = Новый ОписаниеОповещения("ПроверитьПослеОтветаНаВопрос", ЭтотОбъект, ОбработчикЗавершения);
		ТекстВопроса = СтрШаблон(
			НСтр("ru = 'В документе обнаружены ошибки (%1).
				|Продолжить (не рекомендуется)?';
				|en = 'Errors were found in the document (%1).
				|Continue (not recommended)?'"),
			РезультатПроверки.КоличествоОшибок);
		ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да, НСтр("ru = 'Предупреждение';
																										|en = 'Warning'"));
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ПроверитьЗаполнениеПоРегламентуФСС()
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	Успех = ДокументОбъект.ПроверитьЗаполнениеПоРегламентуФСС();
	Сообщения = ПолучитьСообщенияПользователю(Ложь);
	ОбработатьСообщенияПользователю(ДокументОбъект, Сообщения);
	Возврат Новый Структура("Успех, КоличествоОшибок", Успех, Сообщения.Количество());
КонецФункции

&НаКлиенте
Процедура ПроверитьПослеОтветаНаВопрос(Ответ, ОбработчикЗавершения) Экспорт
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ВыполнитьОбработкуОповещения(ОбработчикЗавершения, Истина);
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Интеграция с другими подсистемами.

#Область КлючевыеРеквизитыЗаполненияФормы

// ЗарплатаКадрыПодсистемы
&НаСервере
Функция КлючевыеРеквизитыЗаполненияФормыТаблицыОчищаемыеПриИзменении() Экспорт
	// Возвращает описание таблиц формы подключенных к механизму ключевых реквизитов формы.
	Массив = Новый Массив;
	Массив.Добавить("Объект.ДанныеЭЛН");
	Возврат Массив
КонецФункции

&НаСервере
Функция КлючевыеРеквизитыЗаполненияФормыОписаниеКлючевыхРеквизитов() Экспорт
	// Возвращает массив реквизитов формы подключенных к механизму ключевых реквизитов формы.
	Массив = Новый Массив;
	Массив.Добавить(Новый Структура("ЭлементФормы, Представление", "Организация", НСтр("ru = 'организации';
																						|en = 'companies'")));
	Возврат Массив
КонецФункции
// Конец ЗарплатаКадрыПодсистемы

#КонецОбласти

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область Свойства

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти

#Область ПодписиДокументов

// ЗарплатаКадрыПодсистемы.ПодписиДокументов
&НаКлиенте
Процедура Подключаемый_ПодписиДокументовЭлементПриИзменении(Элемент)
	ПодписиДокументовКлиент.ПриИзмененииПодписывающегоЛица(ЭтотОбъект, Элемент.Имя);
	Если Элемент.Имя = ПодписиДокументовКлиентСервер.ИмяЭлементаФормыПоРолиПодписанта("Руководитель") Тогда
		ФиксацияВторичныхДанныхВДокументахКлиентСервер.ЗафиксироватьИзменениеРеквизита(ЭтотОбъект, "Руководитель");
	ИначеЕсли Элемент.Имя = ПодписиДокументовКлиентСервер.ИмяЭлементаФормыПоРолиПодписанта("ГлавныйБухгалтер") Тогда
		ФиксацияВторичныхДанныхВДокументахКлиентСервер.ЗафиксироватьИзменениеРеквизита(ЭтотОбъект, "ГлавныйБухгалтер");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПодписиДокументовЭлементНажатие(Элемент)
	ПодписиДокументовКлиент.РасширеннаяПодсказкаНажатие(ЭтотОбъект, Элемент.Имя);
КонецПроцедуры
// Конец ЗарплатаКадрыПодсистемы.ПодписиДокументов

#КонецОбласти

#Область ФиксацияВторичныхДанныхВДокументах

&НаСервере
Функция ОбъектЗафиксирован() Экспорт
	Возврат РеквизитФормыВЗначение("Объект").ОбъектЗафиксирован();
КонецФункции

&НаКлиенте
Процедура ОбновитьВторичныеДанныеНаКлиенте()
	ОбновитьВторичныеДанныеНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОбновитьВторичныеДанныеНаСервере(ФиксироватьОтличия = Ложь)
	Если ФиксацияВторичныхДанныхВДокументахКлиентСервер.ОбъектФормыЗафиксирован(ЭтотОбъект) Тогда
		ОбновитьВидимостьДоступность();
		Возврат;
	КонецЕсли;
	
	ФиксацияЗаполнитьИдентификаторыФиксТЧ(ЭтотОбъект);
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.СохранитьРеквизитыФормыФикс(ЭтотОбъект, ДокументОбъект);
	
	ПараметрыФиксации = ЭтотОбъект["ПараметрыФиксацииВторичныхДанных"];
	ПараметрыФиксации.ФиксироватьОтличия = ФиксироватьОтличия;
	ДокументИзменен = ДокументОбъект.ОбновитьВторичныеДанные(ПараметрыФиксации);
	ПараметрыФиксации.ФиксироватьОтличия = Ложь;
	
	Если ДокументИзменен Тогда
		Если Не ДокументОбъект.ЭтоНовый() Тогда
			ФиксацияВторичныхДанныхВДокументахФормы.УстановитьМодифицированность(ЭтотОбъект, Истина);
		КонецЕсли;
		ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
		ФиксацияЗаполнитьРеквизитыФормыФикс(Объект);
	КонецЕсли;
	
	ОбновитьВидимостьДоступность();
	
	ПодписиДокументовКлиентСервер.УстановитьПредставлениеПодписей(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Функция ФиксацияОписаниеФормы(ПараметрыФиксацииВторичныхДанных) Экспорт
	ОписаниеФормы = ФиксацияВторичныхДанныхВДокументахФормы.ОписаниеФормы();
	
	ОписаниеЭлементовФормы = Новый Соответствие();
	ОписаниеЭлементаФормы = ФиксацияВторичныхДанныхВДокументахФормы.ОписаниеЭлементаФормы();
	ОписаниеЭлементаФормы.ПрефиксПути = "Объект";
	ОписаниеЭлементаФормы.ПрефиксПутиТекущиеДанные = "Элементы.ДанныеЭЛН.ТекущиеДанные";
	Для Каждого ОписаниеФиксацииРеквизита Из ПараметрыФиксацииВторичныхДанных.ОписаниеФиксацииРеквизитов Цикл
		ОписаниеЭлементовФормы.Вставить(ОписаниеФиксацииРеквизита.Ключ, ОписаниеЭлементаФормы);
	КонецЦикла;
	ОписаниеФормы.Вставить("ОписаниеЭлементовФормы", ОписаниеЭлементовФормы);
	
	ОписаниеФормы.Вставить("ФормаРедактируетсяПослеФиксации", Ложь);
	Возврат ОписаниеФормы;
КонецФункции

&НаСервере
Функция ФиксацияБыстрыйПоискРеквизитов()
	БыстрыйПоискРеквизитов = Новый Соответствие; // Ключ - имя элемента, значение - имя реквизита.
	
	ПараметрыФиксации = ЭтотОбъект["ПараметрыФиксацииВторичныхДанных"];
	Для Каждого КлючИЗначение Из ПараметрыФиксации.ОписаниеФиксацииРеквизитов Цикл
		ИмяРеквизита = КлючИЗначение.Значение.ИмяРеквизита;
		Если Элементы.Найти(ИмяРеквизита) <> Неопределено Тогда
			БыстрыйПоискРеквизитов.Вставить(ИмяРеквизита, ИмяРеквизита);
		ИначеЕсли Элементы.Найти(КлючИЗначение.Ключ) <> Неопределено Тогда
			БыстрыйПоискРеквизитов.Вставить(КлючИЗначение.Ключ, ИмяРеквизита);
		КонецЕсли;
	КонецЦикла;
	
	Возврат БыстрыйПоискРеквизитов;
КонецФункции

&НаСервере
Процедура ФиксацияОбновитьФиксациюВФорме()
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.ОбновитьФорму(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ФиксацияЗаполнитьРеквизитыФормыФикс(ТекущийОбъект)
	ФиксацияВторичныхДанныхВДокументахФормы.ЗаполнитьРеквизитыФормыФикс(ЭтотОбъект, ТекущийОбъект);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ФиксацияЗаполнитьИдентификаторыФиксТЧ(Форма)
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.ЗаполнитьИдентификаторыСтрок(Форма);
КонецПроцедуры

&НаСервере
Процедура ОтменитьВсеИсправленияНаСервере()
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.ОчиститьФиксациюИзменений(ЭтотОбъект, Объект);
	ФиксацияЗаполнитьРеквизитыФормыФикс(Объект);
	ОбновитьВторичныеДанныеНаСервере();
	ФиксацияОбновитьФиксациюВФорме();
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗафиксироватьИзменениеРеквизитаВФорме(Элемент, СтандартнаяОбработка = Ложь) Экспорт
	ИдентификаторСтроки = Элементы.ДанныеЭЛН.ТекущаяСтрока;
	ОписаниеЭлементов = ФиксацияБыстрыйПоискРеквизитов();
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.Подключаемый_ЗафиксироватьИзменениеРеквизитаВФорме(ЭтотОбъект, Элемент, ОписаниеЭлементов, ИдентификаторСтроки);
КонецПроцедуры

#КонецОбласти

#Область ИнформационнаяПанельБРО

&НаСервере
Процедура ОбновитьИнформационнуюПанельБРО()
	ПараметрыОтображения = ЭлектронныйДокументооборотСФСС.ПараметрыИнформационнойПанелиБРО();
	ПараметрыОтображения.Организация = СЭДОФСС.СтраховательОрганизации(Объект.Организация);
	ПараметрыОтображения.Группа      = Элементы.ИнформационнаяПанельБРО;
	ПараметрыОтображения.Форма       = ЭтотОбъект;
	ПараметрыОтображения.ЭтоСЭДО     = Ложь;
	ЭлектронныйДокументооборотСФСС.ОбновитьИнформационнуюПанельБРО(ПараметрыОтображения);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ИнформационнаяПанельБРООбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	ЭлектронныйДокументооборотСФССКлиент.ИнформационнаяПанельБРОНажатие(
		ЭтотОбъект,
		НавигационнаяСсылкаФорматированнойСтроки, 
		СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти

#КонецОбласти
