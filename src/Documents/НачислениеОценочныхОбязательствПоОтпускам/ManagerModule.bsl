#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// ERP Резервы отпусков 3286

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	МеханизмыДокумента.Добавить("ОборотныеРегистрыУправленческогоУчета");
	МеханизмыДокумента.Добавить("ОтражениеЗарплатыВФинансовомУчете");
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	МеханизмыДокумента.Добавить("СебестоимостьИПартионныйУчет");
	МеханизмыДокумента.Добавить("УчетДоходовРасходов");
	МеханизмыДокумента.Добавить("УчетНЗП");
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	МеханизмыДокумента.Добавить("РеестрДокументов");
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка, ДокументОбъект - ссылка на документ или объект, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * Таблица<ИмяРегистра> - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Если ТипЗнч(Документ) = Тип("ДокументОбъект.НачислениеОценочныхОбязательствПоОтпускам") Тогда
		ДокументСсылка = Документ.Ссылка;
	Иначе
		ДокументСсылка = Документ;
	КонецЕсли;
	
	Запрос			= Новый Запрос;
	ТекстыЗапроса	= Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
		
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса
		
		ТекстЗапросаТаблицаПрочиеРасходы(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПартииПрочихРасходов(ТекстыЗапроса, Регистры);
		ТекстЗапросаПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаДвиженияДоходыРасходыПрочиеАктивыПассивы(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДвиженияПоПрочимАктивамПассивам(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПорядокОтраженияПрочихОпераций(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаТрудозатратыНезавершенногоПроизводства(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаОтражениеЗарплатыВФинансовомУчете(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры);
		
	КонецЕсли;
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

#КонецОбласти

#Область ПроводкиРегУчета

// Возвращает текст запроса для отражения в регл. учете.
//
// Возвращаемое значение:
//   Строка - текст отражения в регл. учете
//
Функция ТекстОтраженияВРеглУчете() Экспорт
	
	#Область ТекстНачислениеОценочныхОбязательств
	ТекстНачислениеОценочныхОбязательств = "
	// на расходы (Дт <счет затрат> :: Кт <96.01.1, 96.01.2>)
	|ВЫБРАТЬ 
	|
	|	Операция.Ссылка КАК Ссылка,
	|	КОНЕЦПЕРИОДА(Операция.ПериодРегистрации, МЕСЯЦ) КАК Период,
	|	Операция.Организация КАК Организация,
	|	"""" КАК ИдентификаторСтроки,
	|
	|	Строки.Сумма КАК Сумма,
	|	Строки.СуммаУУ КАК СуммаУУ,

	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	Строки.СтатьяРасходов КАК АналитикаУчетаДт,
	|	Строки.Подразделение КАК МестоУчетаДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.СтатьяРасходов КАК СубконтоДт1,
	|	Строки.АналитикаРасходов КАК СубконтоДт2,
	|	ВЫБОР КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL ТОГДА
	|			ВЫБОР 
	|				КОГДА ОбъектыСтроительства.СпособСтроительства <> ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.ПустаяСсылка) 
	|					ТОГДА ОбъектыСтроительства.СпособСтроительства
	|				ИНАЧЕ
	|					ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Хозспособ) 
	|			КОНЕЦ
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее)
	|	КОНЕЦ КАК СубконтоДт3,

	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	
	|	Строки.СуммаНУ КАК СуммаНУДт,
	|	ВЫБОР
	|		КОГДА Строки.ПринятиеКНалоговомуУчету ТОГДА 0
	|		ИНАЧЕ Строки.Сумма - Строки.СуммаНУ
	|	КОНЕЦ КАК СуммаПРДт,
	|	ВЫБОР
	|		КОГДА НЕ Строки.ПринятиеКНалоговомуУчету ТОГДА 0
	|		ИНАЧЕ Строки.Сумма - Строки.СуммаНУ
	|	КОНЕЦ КАК СуммаВРДт,
	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	&ВалютаРеглУчета ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	Строки.СчетКт КАК СчетКт,
	|	ЗНАЧЕНИЕ(Справочник.Резервы.РезервЕжегодныхОтпусков) КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	Строки.Сумма КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.СуммаНУ КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	Строки.Сумма - Строки.СуммаНУ КАК СуммаВРКт,
	|	""Начисление оценочных обязательств по отпускам на расходы"" КАК Содержание
	|
	|ИЗ 
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.НачислениеОценочныхОбязательствПоОтпускам КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РасходыДокумента КАК Строки
	|	ПО
	|		ДокументыКОтражению.Ссылка = Строки.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|		ПО Строки.СтатьяРасходов = СтатьиРасходов.Ссылка
	|			И (СтатьиРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПрочиеАктивы))
	|			И (СтатьиРасходов.ВидЦенностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги))
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
	|	ПО
	|		Строки.СтатьяРасходов = СтатьиСтроительства.Ссылка
	|		И СтатьиСтроительства.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Справочник.ОбъектыСтроительства КАК ОбъектыСтроительства
	|	ПО 
	|		ОбъектыСтроительства.Ссылка = Строки.АналитикаРасходов
	|ГДЕ 
	|	СтатьиРасходов.ВидЦенностиНДС ЕСТЬ NULL
	|	И Строки.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиРасходов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// на прочие операции (устаревшее) (Дт <прочие> :: Кт <96.01.1, 96.01.2>)
	|ВЫБРАТЬ 
	|
	|	Операция.Ссылка КАК Ссылка,
	|	КОНЕЦПЕРИОДА(Операция.ПериодРегистрации, МЕСЯЦ) КАК Период,
	|	Операция.Организация КАК Организация,
	|	"""" КАК ИдентификаторСтроки,
	|
	|	Строки.Сумма КАК Сумма,
	|	Строки.СуммаУУ КАК СуммаУУ,

	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеОперации) КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	
	|	Строки.СуммаНУ КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	Строки.Сумма - Строки.СуммаНУ КАК СуммаВРДт,
	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	&ВалютаРеглУчета ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	Строки.СчетКт КАК СчетКт,
	|	ЗНАЧЕНИЕ(Справочник.Резервы.РезервЕжегодныхОтпусков) КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	Строки.Сумма КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.СуммаНУ КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	Строки.Сумма - Строки.СуммаНУ КАК СуммаВРКт,
	|	""Начисление оценочных обязательств по отпускам на прочие операции"" КАК Содержание
	|
	|ИЗ 
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.НачислениеОценочныхОбязательствПоОтпускам КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РасходыДокумента КАК Строки
	|	ПО
	|		ДокументыКОтражению.Ссылка = Строки.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|		ПО Строки.СтатьяРасходов = СтатьиРасходов.Ссылка
	|			И (СтатьиРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПрочиеАктивы))
	|			И (СтатьиРасходов.ВидЦенностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги))
	|
	|ГДЕ 
	|	НЕ СтатьиРасходов.ВидЦенностиНДС ЕСТЬ NULL
	|	И Строки.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиРасходов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// на прочие пассивы (Дт <прочие> :: Кт <96.01.1, 96.01.2>)
	|ВЫБРАТЬ 
	|
	|	Операция.Ссылка КАК Ссылка,
	|	КОНЕЦПЕРИОДА(Операция.ПериодРегистрации, МЕСЯЦ) КАК Период,
	|	Операция.Организация КАК Организация,
	|	"""" КАК ИдентификаторСтроки,
	|
	|	Строки.Сумма КАК Сумма,
	|	Строки.СуммаУУ КАК СуммаУУ,

	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеАктивыПассивы) КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	Строки.СчетУчета КАК СчетДт,
	|	Строки.Субконто1 КАК СубконтоДт1,
	|	Строки.Субконто2 КАК СубконтоДт2,
	|	Строки.Субконто3 КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	
	|	Строки.СуммаНУ КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	Строки.Сумма - Строки.СуммаНУ КАК СуммаВРДт,
	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	&ВалютаРеглУчета ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	Строки.СчетКт КАК СчетКт,
	|	ЗНАЧЕНИЕ(Справочник.Резервы.РезервЕжегодныхОтпусков) КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	Строки.Сумма КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.СуммаНУ КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	Строки.Сумма - Строки.СуммаНУ КАК СуммаВРКт,
	|	""Начисление оценочных обязательств по отпускам на пассивы"" КАК Содержание
	|
	|ИЗ 
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.НачислениеОценочныхОбязательствПоОтпускам КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РасходыДокумента КАК Строки
	|	ПО
	|		ДокументыКОтражению.Ссылка = Строки.Ссылка
	|
	|ГДЕ
	|	Строки.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|";
	
	#КонецОбласти
	
	#Область ТекстЗапросаНачисленоСдельно
	ТекстЗапросаНачисленоСдельно = "
	// прямые производственные
	|ВЫБРАТЬ
	|	Строки.Регистратор КАК Ссылка,
	|	КОНЕЦПЕРИОДА(Строки.Период, МЕСЯЦ) КАК Период,
	|	Строки.Организация КАК Организация,
	|	"""" КАК ИдентификаторСтроки,
	|
	|	Сумма(Строки.СуммаРегл) КАК Сумма,
	|	Сумма(Строки.СуммаРегл / КурсВалютыУпрУчета.Курс) КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство) КАК ВидСчетаДт,
	|	Строки.ГруппаПродукции КАК АналитикаУчетаДт,
	|	Строки.Подразделение КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.ВидРабот КАК СубконтоДт1,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.СдельнаяОплатаТруда) КАК СубконтоДт2,
	|	Строки.ГруппаПродукции КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Сумма(Строки.СуммаРегл - Строки.ПостояннаяРазница - Строки.ВременнаяРазница) КАК СуммаНУДт,
	|	Сумма(Строки.ПостояннаяРазница) КАК СуммаПРДт,
	|	Сумма(Строки.ВременнаяРазница) КАК СуммаВРДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	&ВалютаРеглУчета КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	ВтСчета.СчетКт КАК СчетКт,
	|	ЗНАЧЕНИЕ(Справочник.Резервы.РезервЕжегодныхОтпусков) КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	Сумма(Строки.СуммаРегл) КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Сумма(Строки.СуммаРегл - Строки.ПостояннаяРазница - Строки.ВременнаяРазница) КАК СуммаНУКт,
	|	Сумма(Строки.ПостояннаяРазница) КАК СуммаПРКт,
	|	Сумма(Строки.ВременнаяРазница) КАК СуммаВРКт,
	|	""Распределение оценочных обязательств по выработке"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ОтражениеЗарплатыВФинансовомУчете КАК Строки
	|	ПО
	|		ДокументыКОтражению.Ссылка = Строки.Регистратор
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		(
	|			ВЫБРАТЬ ЗНАЧЕНИЕ(Перечисление.ТипыОценочныхОбязательств.ОценочныеОбязательстваПоВознаграждениям) КАК ТипНалога,ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОценочныеОбязательстваПоВознаграждениям) КАК СчетКт
	|			ОБЪЕДИНИТЬ ВСЕ ВЫБРАТЬ ЗНАЧЕНИЕ(Перечисление.ТипыОценочныхОбязательств.ОценочныеОбязательстваПоСтраховымВзносам),ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОценочныеОбязательстваПостраховымВзносам)
	|			ОБЪЕДИНИТЬ ВСЕ ВЫБРАТЬ ЗНАЧЕНИЕ(Перечисление.ТипыОценочныхОбязательств.ОценочныеОбязательстваПоВзносамФССНСиПЗ),ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОценочныеОбязательстваПостраховымВзносам)
	|		) КАК ВтСчета
	|	ПО
	|		Строки.ТипНалога = ВтСчета.ТипНалога
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсВалютыУпрУчета
	|	ПО
	|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
	|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(Строки.Период, МЕСЯЦ), День)
	|
	|СГРУППИРОВАТЬ ПО
	|	Строки.Регистратор,
	|	КОНЕЦПЕРИОДА(Строки.Период, МЕСЯЦ),
	|	Строки.Организация,
	|	Строки.Подразделение,
	|	Строки.НаправлениеДеятельности,
	|	Строки.ВидРабот,
	|	Строки.ГруппаПродукции,
	|	ВтСчета.СчетКт
	|";
	#КонецОбласти

	ТекстыОтражения = Новый Массив;
	ТекстыОтражения.Добавить(ТекстНачислениеОценочныхОбязательств);
	ТекстыОтражения.Добавить(ТекстЗапросаНачисленоСдельно);
	
	Возврат СтрСоединить(ТекстыОтражения, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламетированном учете
//
// Возвращаемое значение:
//   Строка - текст построения временной таблицы для отражения в регл. учете
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	ТекстЗапросаТаблицаРасходыДокумента = 
	"ВЫБРАТЬ
	|	ВТРасходыДокумента.СтатьяРасходов,
	|	ВТРасходыДокумента.НаправлениеДеятельности,
	|	ВТРасходыДокумента.ВариантРаспределения,
	|	ВТРасходыДокумента.ПринятиеКналоговомуУчету,
	|	ВТРасходыДокумента.АналитикаРасходов,
	|	ВТРасходыДокумента.АналитикаАктивовПассивов,
	|	ВТРасходыДокумента.СчетУчета,
	|	ВТРасходыДокумента.Субконто1,
	|	ВТРасходыДокумента.Субконто2,
	|	ВТРасходыДокумента.Субконто3,
	|	ВТРасходыДокумента.СчетКт,
	|	СУММА(ВТРасходыДокумента.СуммаРегл) КАК Сумма,
	|	СУММА(ВЫРАЗИТЬ(ВТРасходыДокумента.СуммаРегл / КурсВалютыУпрУчета.Курс КАК ЧИСЛО(15, 2))) КАК СуммаУУ,
	|	СУММА(ВТРасходыДокумента.СуммаНУ) КАК СуммаНУ,
	|	МАКСИМУМ(ВТРасходыДокумента.НомерСтроки) КАК НомерСтроки,
	|	ВТРасходыДокумента.Подразделение,
	|	ВТРасходыДокумента.СтатьяАктивовПассивов,
	|	ВТРасходыДокумента.Ссылка
	|ПОМЕСТИТЬ РасходыДокумента
	|ИЗ
	|	ВТРасходыДокументаРегл КАК ВТРасходыДокумента
	|	ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсВалютыУпрУчета
	|	ПО КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
	|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(ВТРасходыДокумента.ПериодРегистрации, День)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТРасходыДокумента.Ссылка,
	|	ВТРасходыДокумента.СчетКт,
	|	ВТРасходыДокумента.Подразделение,
	|	ВТРасходыДокумента.СтатьяАктивовПассивов,
	|	ВТРасходыДокумента.СтатьяРасходов,
	|	ВТРасходыДокумента.НаправлениеДеятельности,
	|	ВТРасходыДокумента.ВариантРаспределения,
	|	ВТРасходыДокумента.ПринятиеКналоговомуУчету,
	|	ВТРасходыДокумента.АналитикаРасходов,
	|	ВТРасходыДокумента.АналитикаАктивовПассивов,
	|	ВТРасходыДокумента.СчетУчета,
	|	ВТРасходыДокумента.Субконто1,
	|	ВТРасходыДокумента.Субконто2,
	|	ВТРасходыДокумента.Субконто3
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	ТекстыСозданияВременныхТаблиц = Новый Массив;
	ТекстыСозданияВременныхТаблиц.Добавить(ТекстЗапросаВтСписокСпособовОтражения());
	ТекстыСозданияВременныхТаблиц.Добавить(Справочники.СпособыОтраженияЗарплатыВБухУчете.ТекстЗапросаДанныхСпособов("ВТСпособыОтражения"));
	ТекстыСозданияВременныхТаблиц.Добавить(ТекстЗапросаТаблицаВТРасходыДокументаРегл());
	ТекстыСозданияВременныхТаблиц.Добавить(ТекстЗапросаТаблицаРасходыДокумента);
	
	ТекстЗапросаСозданияВременныхТаблиц = СтрСоединить(ТекстыСозданияВременныхТаблиц, ОбщегоНазначения.РазделительПакетаЗапросов());
	ТекстЗапросаСозданияВременныхТаблиц = СтрЗаменить(ТекстЗапросаСозданияВременныхТаблиц, "&КоэффициентПересчетаВВалютуУпр", "1");
	
	Возврат ТекстЗапросаСозданияВременныхТаблиц;
	
КонецФункции

#КонецОбласти

// Конец ERP

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ДляВсехСтрок( ЗначениеРазрешено(ФизическиеЛица.ФизическоеЛицо, NULL КАК ИСТИНА)
	|	) И ЗначениеРазрешено(Организация)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает описание состава документа
//
// Возвращаемое значение:
//  Структура - см. ЗарплатаКадрыСоставДокументов.НовоеОписаниеСоставаОбъекта.
Функция ОписаниеСоставаОбъекта() Экспорт
	
	МетаданныеДокумента = Метаданные.Документы.НачислениеОценочныхОбязательствПоОтпускам;
	Возврат ЗарплатаКадрыСоставДокументов.ОписаниеСоставаОбъектаПоМетаданнымФизическиеЛицаВТабличныхЧастях(МетаданныеДокумента);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// ERP Резервы отпусков 3286

#Область Проведение

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеШапки.ПериодРегистрации КАК Период,
	|	ДанныеШапки.Организация КАК Организация,
	|	ДанныеШапки.КорректировкаОбязательств КАК КорректировкаОбязательств,
	|	ДанныеШапки.Проведен КАК Проведен,
	|	ДанныеШапки.Ссылка КАК Ссылка,
	|	МАКСИМУМ(ЕСТЬNULL(Сотрудники.НомерСтроки,0)) КАК КоличествоСотрудников,
	|	ДанныеШапки.Номер КАК Номер,
	|	ВЫРАЗИТЬ(ДанныеШапки.Комментарий КАК СТРОКА(100)) КАК Комментарий,
	|	ДанныеШапки.ПометкаУдаления КАК ПометкаУдаления,
	|	ДанныеШапки.Ответственный КАК Ответственный
	|ИЗ
	|	Документ.НачислениеОценочныхОбязательствПоОтпускам КАК ДанныеШапки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.НачислениеОценочныхОбязательствПоОтпускам.ОценочныеОбязательстваПоСотрудникам КАК Сотрудники
	|	    ПО ДанныеШапки.Ссылка = Сотрудники.Ссылка
	|ГДЕ
	|	ДанныеШапки.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеШапки.ПериодРегистрации,
	|	ДанныеШапки.Организация,
	|	ДанныеШапки.КорректировкаОбязательств,
	|	ДанныеШапки.Ссылка,
	|	ДанныеШапки.Номер,
	|	ВЫРАЗИТЬ(ДанныеШапки.Комментарий КАК СТРОКА(100)),
	|	ДанныеШапки.Проведен,
	|	ДанныеШапки.ПометкаУдаления,
	|	ДанныеШапки.Ответственный
	|
	|";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	ВалютаРегл = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Реквизиты.Организация);
	Коэффициенты = РаботаСКурсамивалютУТ.ПолучитьКоэффициентыПересчетаВалюты(
		ВалютаРегл, ВалютаРегл, КонецМесяца(Реквизиты.Период), Реквизиты.Организация);
	
	Запрос.УстановитьПараметр("Период",                         КонецМесяца(Реквизиты.Период));
	Запрос.УстановитьПараметр("ИспользоватьНачислениеЗарплаты", ПолучитьФункциональнуюОпцию("ИспользоватьНачислениеЗарплатыУТ"));
	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуУпр", Коэффициенты.КоэффициентПересчетаВВалютуУПР);
	Запрос.УстановитьПараметр("ДатаНачала",                     НачалоМесяца(Реквизиты.Период));
	Запрос.УстановитьПараметр("ДатаОкончания",                  КонецМесяца(Реквизиты.Период));
	Запрос.УстановитьПараметр("Сводно",                         Реквизиты.КоличествоСотрудников = 0);
	Запрос.УстановитьПараметр("ПроводкиПоРаботникам",           Ложь);
	Запрос.УстановитьПараметр("ВалютаРегл",                     ВалютаРегл);
	Запрос.УстановитьПараметр("НастройкаХозяйственнойОперации", Справочники.НастройкиХозяйственныхОпераций.НачислениеРезервовПоВознаграждениям);
	Запрос.УстановитьПараметр("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.НачислениеОценочныхОбязательствПоОтпускам"));
	Запрос.УстановитьПараметр("Номер", Реквизиты.Номер);
	Запрос.УстановитьПараметр("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	Запрос.УстановитьПараметр("Комментарий", Реквизиты.Комментарий);
	Запрос.УстановитьПараметр("Проведен", Реквизиты.Проведен);
	Запрос.УстановитьПараметр("ПометкаУдаления", Реквизиты.ПометкаУдаления);
	Запрос.УстановитьПараметр("ХозОперацияДляРеестра", Перечисления.ХозяйственныеОперации.НачислениеОценочныхОбязательствПоОтпускам);
	Запрос.УстановитьПараметр("Ответственный", Реквизиты.Ответственный);
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаДокументовКОтражению(ТекстыЗапроса)
	
	ИмяРегистра = "ДокументыКОтражению";
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Документ.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ДокументыКОтражению
	|ИЗ
	|	Документ.НачислениеОценочныхОбязательствПоОтпускам КАК Документ
	|ГДЕ
	|	Документ.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтСписокСпособовОтражения(ТекстыЗапроса = Неопределено)
	
	ИмяРегистра = "ВтСписокСпособовОтражения";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ДокументыКОтражению", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаДокументовКОтражению(ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОценочныеОбязательства.СпособОтраженияЗарплатыВБухучете КАК СпособОтражения
	|ПОМЕСТИТЬ ВтСписокСпособовОтражения
	|ИЗ
	|	Документ.НачислениеОценочныхОбязательствПоОтпускам.ОценочныеОбязательства КАК ОценочныеОбязательства
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыКОтражению КАК ДокументыКОтражению
	|		ПО ОценочныеОбязательства.Ссылка = ДокументыКОтражению.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.НачислениеОценочныхОбязательствПоОтпускам.ОценочныеОбязательстваПоСотрудникам КАК ПоСотрудникам
	|		ПО ОценочныеОбязательства.Ссылка = ПоСотрудникам.Ссылка
	|ГДЕ
	|	ПоСотрудникам.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОценочныеОбязательства.СпособОтраженияЗарплатыВБухучете КАК СпособОтражения
	|ИЗ
	|	Документ.НачислениеОценочныхОбязательствПоОтпускам.ОценочныеОбязательстваПоСотрудникам КАК ОценочныеОбязательства
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыКОтражению КАК ДокументыКОтражению
	|		ПО ОценочныеОбязательства.Ссылка = ДокументыКОтражению.Ссылка";
	
	Если Не ТекстыЗапроса = Неопределено Тогда
		ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВТСпособыОтражения(ТекстыЗапроса = Неопределено)
	
	ИмяРегистра = "ВТСпособыОтражения";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтСписокСпособовОтражения", ТекстыЗапроса) Тогда
		ТекстЗапросаВтСписокСпособовОтражения(ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = Справочники.СпособыОтраженияЗарплатыВБухУчете.ТекстЗапросаДанныхСпособов("ВТСпособыОтражения");
	
	Если Не ТекстыЗапроса = Неопределено Тогда
		ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтВыработкаСотрудников(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаВыработкаСотрудников";
	
	УстановитьПараметрЗапросаТаблицаВыработкаСотрудников(Запрос);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВыработкаСотрудников.Период,
	|	ВыработкаСотрудников.Организация,
	|	ВыработкаСотрудников.Подразделение,
	|	ВыработкаСотрудников.НаправлениеДеятельности,
	
	|	ВыработкаСотрудников.ПартияПроизводства,
	|	ВыработкаСотрудников.ЗаказНаПроизводство,
	|	ВыработкаСотрудников.КодСтрокиПродукция,
	|	ВыработкаСотрудников.ВидРабот,
	|	ВыработкаСотрудников.ГруппаПродукции,
	|	ВыработкаСотрудников.Этап,
	
	|	ВыработкаСотрудников.ТипНалога,
	|	ВыработкаСотрудников.ВидПлатежаВГосБюджет,
	|	ВыработкаСотрудников.ВидНачисленияОплатыТруда,
	|	ВыработкаСотрудников.ВидОперацииПоЗарплате,
	|	ВыработкаСотрудников.ВидФондаВзносов,
	
	|	ВыработкаСотрудников.СтатьяКалькуляции,
	|	ВыработкаСотрудников.СтатьяРасходов,
	|	ВыработкаСотрудников.АналитикаРасходов,
	|	ВыработкаСотрудников.АналитикаАктивовПассивов,
	|	ВыработкаСотрудников.СчетУчета,
	|	ВыработкаСотрудников.Субконто1,
	|	ВыработкаСотрудников.Субконто2,
	|	ВыработкаСотрудников.Субконто3,
	|	ВыработкаСотрудников.Сотрудник,
	
	|	ВыработкаСотрудников.Сумма,
	|	ВыработкаСотрудников.СуммаРегл,
	|	ВыработкаСотрудников.ВременнаяРазница,
	|	ВыработкаСотрудников.ПостояннаяРазница,
	
	|	ВыработкаСотрудников.ИдентификаторСтроки
	
	|ПОМЕСТИТЬ ВтТаблицаВыработкаСотрудников
	|ИЗ
	|	&ТаблицаВыработкаСотрудников КАК ВыработкаСотрудников";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура УстановитьПараметрЗапросаТаблицаВыработкаСотрудников(Запрос)
	
	Если Запрос.Параметры.Свойство("ТаблицаВыработкаСотрудников") Тогда
		Возврат;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ТаблицаВыработкаСотрудников", 
		ОтражениеЗарплатыВФинансовомУчетеУП.РаспределитьНачисленияПоЭтапамПроизводства(Запрос.Параметры));
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаТрудозатратыНезавершенногоПроизводства(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ТрудозатратыНезавершенногоПроизводства";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	УстановитьПараметрЗапросаТаблицаВыработкаСотрудников(Запрос);
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаВыработкаСотрудников", ТекстыЗапроса) Тогда
		ТекстЗапросаВтВыработкаСотрудников(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период                                  КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)   КАК ВидДвижения,
	|	&Организация                             КАК Организация,
	|	ВыработкаСотрудников.Подразделение       КАК Подразделение,
	|	ВыработкаСотрудников.ПартияПроизводства  КАК ПартияПроизводства,
	|	ВыработкаСотрудников.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|	ВыработкаСотрудников.КодСтрокиПродукция  КАК КодСтрокиПродукция,
	|	ВыработкаСотрудников.Этап                КАК Этап,
	|	ВыработкаСотрудников.СтатьяКалькуляции   КАК СтатьяКалькуляции,
	|	ВыработкаСотрудников.ВидРабот            КАК ВидРабот,
	|	ВыработкаСотрудников.ГруппаПродукции     КАК ГруппаПродукции,
	|	ВыработкаСотрудников.ВидФондаВзносов     КАК ВидФондаВзносов,
	|	СУММА(ВыработкаСотрудников.Сумма)        КАК Стоимость,
	|	СУММА(ВыработкаСотрудников.СуммаРегл)    КАК СтоимостьРегл,
	|	СУММА(ВыработкаСотрудников.ВременнаяРазница)  КАК ВременнаяРазница,
	|	СУММА(ВыработкаСотрудников.ПостояннаяРазница) КАК ПостояннаяРазница,
	|	
	|	ВыработкаСотрудников.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	&НастройкаХозяйственнойОперации          КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ВтТаблицаВыработкаСотрудников КАК ВыработкаСотрудников
	|ГДЕ
	|	ВыработкаСотрудников.СтатьяРасходов = НЕОПРЕДЕЛЕНО
	|
	|СГРУППИРОВАТЬ ПО
	|	ВыработкаСотрудников.Подразделение,
	|	ВыработкаСотрудников.ПартияПроизводства,
	|	ВыработкаСотрудников.ЗаказНаПроизводство,
	|	ВыработкаСотрудников.КодСтрокиПродукция,
	|	ВыработкаСотрудников.Этап,
	|	ВыработкаСотрудников.СтатьяКалькуляции,
	|	ВыработкаСотрудников.ВидРабот,
	|	ВыработкаСотрудников.ГруппаПродукции,
	|	ВыработкаСотрудников.ВидФондаВзносов,
	|	ВыработкаСотрудников.ИдентификаторСтроки";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаОтражениеЗарплатыВФинансовомУчете(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОтражениеЗарплатыВФинансовомУчете";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	УстановитьПараметрЗапросаТаблицаВыработкаСотрудников(Запрос);
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаВыработкаСотрудников", ТекстыЗапроса) Тогда
		ТекстЗапросаВтВыработкаСотрудников(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ВыработкаСотрудников.Подразделение КАК Подразделение,
	|	ВыработкаСотрудников.НаправлениеДеятельности,
	|	ВыработкаСотрудников.ВидНачисленияОплатыТруда,
	|	ВыработкаСотрудников.ВидОперацииПоЗарплате,
	|	ВыработкаСотрудников.СтатьяРасходов,
	|	ВыработкаСотрудников.АналитикаРасходов,
	|	ВыработкаСотрудников.ВидРабот,
	|	ВЫБОР
	|		КОГДА НЕ &Сводно
	|				И &ПроводкиПоРаботникам
	|			ТОГДА ВыработкаСотрудников.Сотрудник
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|	КОНЕЦ КАК ФизическоеЛицо,
	|	ВыработкаСотрудников.ГруппаПродукции,
	|	ВыработкаСотрудников.ТипНалога,
	|	ВыработкаСотрудников.ВидПлатежаВГосБюджет,
	|	ВыработкаСотрудников.АналитикаАктивовПассивов,
	|	ВыработкаСотрудников.СчетУчета,
	|	ВыработкаСотрудников.Субконто1,
	|	ВыработкаСотрудников.Субконто2,
	|	ВыработкаСотрудников.Субконто3,
	|	СУММА(ВыработкаСотрудников.СуммаРегл) КАК СуммаРегл,
	|	СУММА(ВыработкаСотрудников.ПостояннаяРазница) КАК ПостояннаяРазница,
	|	СУММА(ВыработкаСотрудников.ВременнаяРазница) КАК ВременнаяРазница
	|ИЗ
	|	ВтТаблицаВыработкаСотрудников КАК ВыработкаСотрудников
	|
	|СГРУППИРОВАТЬ ПО
	|	ВыработкаСотрудников.Подразделение,
	|	ВыработкаСотрудников.НаправлениеДеятельности,
	|	ВыработкаСотрудников.ВидНачисленияОплатыТруда,
	|	ВыработкаСотрудников.ВидОперацииПоЗарплате,
	|	ВыработкаСотрудников.СтатьяРасходов,
	|	ВыработкаСотрудников.АналитикаРасходов,
	|	ВыработкаСотрудников.ВидРабот,
	|	ВЫБОР
	|		КОГДА НЕ &Сводно
	|				И &ПроводкиПоРаботникам
	|			ТОГДА ВыработкаСотрудников.Сотрудник
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВыработкаСотрудников.ГруппаПродукции,
	|	ВыработкаСотрудников.ТипНалога,
	|	ВыработкаСотрудников.ВидПлатежаВГосБюджет,
	|	ВыработкаСотрудников.АналитикаАктивовПассивов,
	|	ВыработкаСотрудников.СчетУчета,
	|	ВыработкаСотрудников.Субконто1,
	|	ВыработкаСотрудников.Субконто2,
	|	ВыработкаСотрудников.Субконто3";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВТРасходыДокумента(ТекстыЗапроса = Неопределено)
	
	ИмяРегистра = "ВТРасходыДокумента";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВТСпособыОтражения", ТекстыЗапроса) Тогда
		ТекстЗапросаВТСпособыОтражения(ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СтатьиРасходов.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	СтатьиРасходов.СтатьяРасходов КАК СтатьяРасходов,
	|	СтатьиРасходов.ВариантРаспределения КАК ВариантРаспределения,
	|	СтатьиРасходов.ПринятиеКналоговомуУчету КАК ПринятиеКналоговомуУчету,
	|
	|	ВЫБОР
	|		КОГДА СтатьиРасходов.АналитикаРасходов = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.МестоВСтруктуреПредприятия
	|		КОГДА СтатьиРасходов.АналитикаРасходов = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.Ссылка.Организация
	|		ИНАЧЕ СтатьиРасходов.АналитикаРасходов
	|	КОНЕЦ КАК АналитикаРасходов,
	|
	|	ВЫБОР
	|		КОГДА СтатьиРасходов.АналитикаАктивовПассивов = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.МестоВСтруктуреПредприятия
	|		КОГДА СтатьиРасходов.АналитикаАктивовПассивов = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.Ссылка.Организация
	|		ИНАЧЕ СтатьиРасходов.АналитикаАктивовПассивов
	|	КОНЕЦ КАК АналитикаАктивовПассивов,
	|
	|	СтатьиРасходов.СчетУчета КАК СчетУчета,
	|	СтатьиРасходов.Субконто1 КАК Субконто1,
	|	СтатьиРасходов.Субконто2 КАК Субконто2,
	|	СтатьиРасходов.Субконто3 КАК Субконто3,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОценочныеОбязательстваПоВознаграждениям) КАК СчетКт,
	|	ВЫРАЗИТЬ(ОценочныеОбязательства.СуммаРезерва * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2)) КАК Сумма,
	|	ВЫРАЗИТЬ(ОценочныеОбязательства.СуммаРезерваНУ * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2)) КАК СуммаНУ,
	|	ОценочныеОбязательства.СуммаРезерва КАК СуммаРегл,
	|	ОценочныеОбязательства.СуммаРезерваНУ КАК СуммаРеглНУ,
	|	ОценочныеОбязательства.НомерСтроки КАК НомерСтроки,
	|	ОценочныеОбязательства.МестоВСтруктуреПредприятия КАК Подразделение,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачислениеРезервовПоВознаграждениям) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ОплатаТруда) КАК СтатьяАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаСтатьиАктивовПассивов,
	|	ОценочныеОбязательства.Ссылка,
	|	КОНЕЦПЕРИОДА(ОценочныеОбязательства.Ссылка.ПериодРегистрации, МЕСЯЦ) КАК ПериодРегистрации,
	|	
	|	ОценочныеОбязательства.ИдентификаторСтроки КАК ИдентификаторСтроки
	|	
	|ПОМЕСТИТЬ ВТРасходыДокумента
	|ИЗ
	|	Документ.НачислениеОценочныхОбязательствПоОтпускам.ОценочныеОбязательства КАК ОценочныеОбязательства
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыКОтражению КАК ДокументыКОтражению
	|		ПО ОценочныеОбязательства.Ссылка = ДокументыКОтражению.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСпособыОтражения КАК СтатьиРасходов
	|		ПО ОценочныеОбязательства.СпособОтраженияЗарплатыВБухучете = СтатьиРасходов.СпособОтражения
	|			И ОценочныеОбязательства.ОблагаетсяЕНВД = СтатьиРасходов.ОблагаетсяЕНВД
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.НачислениеОценочныхОбязательствПоОтпускам.ОценочныеОбязательстваПоСотрудникам КАК ПоСотрудникам
	|		ПО ОценочныеОбязательства.Ссылка = ПоСотрудникам.Ссылка
	|ГДЕ
	|	ОценочныеОбязательства.СуммаРезерва + 
	|		ОценочныеОбязательства.СуммаРезерваНУ <> 0
	|	И НЕ СтатьиРасходов.ОплатаСдельныхРабот
	|	И ПоСотрудникам.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СтатьиРасходов.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	СтатьиРасходов.СтатьяРасходовВзносов,
	|	СтатьиРасходов.ВариантРаспределенияВзносов,
	|	СтатьиРасходов.ПринятиеКналоговомуУчетуВзносов,
	|
	|	ВЫБОР
	|		КОГДА СтатьиРасходов.АналитикаРасходовВзносов = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.МестоВСтруктуреПредприятия
	|		КОГДА СтатьиРасходов.АналитикаРасходовВзносов = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.Ссылка.Организация
	|		ИНАЧЕ СтатьиРасходов.АналитикаРасходовВзносов
	|	КОНЕЦ,
	|
	|	ВЫБОР
	|		КОГДА СтатьиРасходов.АналитикаАктивовПассивовВзносов = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.МестоВСтруктуреПредприятия
	|		КОГДА СтатьиРасходов.АналитикаАктивовПассивовВзносов = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.Ссылка.Организация
	|		ИНАЧЕ СтатьиРасходов.АналитикаАктивовПассивовВзносов
	|	КОНЕЦ,
	|
	|	СтатьиРасходов.СчетУчетаВзносов,
	|	СтатьиРасходов.Субконто1Взносов,
	|	СтатьиРасходов.Субконто2Взносов,
	|	СтатьиРасходов.Субконто3Взносов,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОценочныеОбязательстваПостраховымВзносам),
	|	ВЫРАЗИТЬ((ОценочныеОбязательства.СуммаРезерваСтраховыхВзносов) * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2)),
	|	ВЫРАЗИТЬ((ОценочныеОбязательства.СуммаРезерваСтраховыхВзносовНУ) * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2)),
	|	ОценочныеОбязательства.СуммаРезерваСтраховыхВзносов,
	|	ОценочныеОбязательства.СуммаРезерваСтраховыхВзносовНУ,
	|	ОценочныеОбязательства.НомерСтроки,
	|	ОценочныеОбязательства.МестоВСтруктуреПредприятия,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачислениеРезервовПоСтраховымВзносам),
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.Налоги),
	|	НЕОПРЕДЕЛЕНО,
	|	ОценочныеОбязательства.Ссылка,
	|	КОНЕЦПЕРИОДА(ОценочныеОбязательства.Ссылка.ПериодРегистрации, МЕСЯЦ) КАК ПериодРегистрации,
	|	
	|	ОценочныеОбязательства.ИдентификаторСтроки КАК ИдентификаторСтроки
	|	
	|ИЗ
	|	Документ.НачислениеОценочныхОбязательствПоОтпускам.ОценочныеОбязательства КАК ОценочныеОбязательства
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыКОтражению КАК ДокументыКОтражению
	|		ПО ОценочныеОбязательства.Ссылка = ДокументыКОтражению.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСпособыОтражения КАК СтатьиРасходов
	|		ПО ОценочныеОбязательства.СпособОтраженияЗарплатыВБухучете = СтатьиРасходов.СпособОтражения
	|			И ОценочныеОбязательства.ОблагаетсяЕНВД = СтатьиРасходов.ОблагаетсяЕНВД
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.НачислениеОценочныхОбязательствПоОтпускам.ОценочныеОбязательстваПоСотрудникам КАК ПоСотрудникам
	|		ПО ОценочныеОбязательства.Ссылка = ПоСотрудникам.Ссылка
	|ГДЕ
	|	ОценочныеОбязательства.СуммаРезерваСтраховыхВзносов + 
	|		ОценочныеОбязательства.СуммаРезерваСтраховыхВзносовНУ <> 0
	|	И НЕ СтатьиРасходов.ОплатаСдельныхРабот
	|	И ПоСотрудникам.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СтатьиРасходов.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	СтатьиРасходов.СтатьяРасходовТравматизм,
	|	СтатьиРасходов.ВариантРаспределенияТравматизм,
	|	СтатьиРасходов.ПринятиеКналоговомуУчетуТравматизм,
	|
	|	ВЫБОР
	|		КОГДА СтатьиРасходов.АналитикаРасходовТравматизм = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.МестоВСтруктуреПредприятия
	|		КОГДА СтатьиРасходов.АналитикаРасходовТравматизм = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.Ссылка.Организация
	|		ИНАЧЕ СтатьиРасходов.АналитикаРасходовТравматизм
	|	КОНЕЦ,
	|
	|	ВЫБОР
	|		КОГДА СтатьиРасходов.АналитикаАктивовПассивовТравматизм = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.МестоВСтруктуреПредприятия
	|		КОГДА СтатьиРасходов.АналитикаАктивовПассивовТравматизм = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.Ссылка.Организация
	|		ИНАЧЕ СтатьиРасходов.АналитикаАктивовПассивовТравматизм
	|	КОНЕЦ,
	|
	|	СтатьиРасходов.СчетУчетаТравматизм,
	|	СтатьиРасходов.Субконто1Травматизм,
	|	СтатьиРасходов.Субконто2Травматизм,
	|	СтатьиРасходов.Субконто3Травматизм,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОценочныеОбязательстваПостраховымВзносам),
	|	ВЫРАЗИТЬ((ОценочныеОбязательства.СуммаРезерваФССНесчастныеСлучаи) * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2)),
	|	ВЫРАЗИТЬ((ОценочныеОбязательства.СуммаРезерваФССНесчастныеСлучаиНУ) * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2)),
	|	ОценочныеОбязательства.СуммаРезерваФССНесчастныеСлучаи,
	|	ОценочныеОбязательства.СуммаРезерваФССНесчастныеСлучаиНУ,
	|	ОценочныеОбязательства.НомерСтроки,
	|	ОценочныеОбязательства.МестоВСтруктуреПредприятия,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачислениеРезервовПоСтраховымВзносам),
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.Налоги),
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ФССНС),
	|	ОценочныеОбязательства.Ссылка,
	|	КОНЕЦПЕРИОДА(ОценочныеОбязательства.Ссылка.ПериодРегистрации, МЕСЯЦ) КАК ПериодРегистрации,
	|	
	|	ОценочныеОбязательства.ИдентификаторСтроки КАК ИдентификаторСтроки
	|	
	|ИЗ
	|	Документ.НачислениеОценочныхОбязательствПоОтпускам.ОценочныеОбязательства КАК ОценочныеОбязательства
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыКОтражению КАК ДокументыКОтражению
	|		ПО ОценочныеОбязательства.Ссылка = ДокументыКОтражению.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСпособыОтражения КАК СтатьиРасходов
	|		ПО ОценочныеОбязательства.СпособОтраженияЗарплатыВБухучете = СтатьиРасходов.СпособОтражения
	|			И ОценочныеОбязательства.ОблагаетсяЕНВД = СтатьиРасходов.ОблагаетсяЕНВД
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.НачислениеОценочныхОбязательствПоОтпускам.ОценочныеОбязательстваПоСотрудникам КАК ПоСотрудникам
	|		ПО ОценочныеОбязательства.Ссылка = ПоСотрудникам.Ссылка
	|ГДЕ
	|	ОценочныеОбязательства.СуммаРезерваФССНесчастныеСлучаи + 
	|		ОценочныеОбязательства.СуммаРезерваФССНесчастныеСлучаиНУ <> 0
	|	И (НЕ СтатьиРасходов.ОплатаСдельныхРабот ИЛИ СтатьиРасходов.РежимУчетаВзносовНаТравматизм = 1)
	|	И ПоСотрудникам.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СтатьиРасходов.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	СтатьиРасходов.СтатьяРасходов КАК СтатьяРасходов,
	|	СтатьиРасходов.ВариантРаспределения КАК ВариантРаспределения,
	|	СтатьиРасходов.ПринятиеКналоговомуУчету КАК ПринятиеКналоговомуУчету,
	|
	|	ВЫБОР
	|		КОГДА СтатьиРасходов.АналитикаРасходов = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.МестоВСтруктуреПредприятия
	|		КОГДА СтатьиРасходов.АналитикаРасходов = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.Ссылка.Организация
	|		КОГДА СтатьиРасходов.АналитикаРасходов = ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.Сотрудник.ФизическоеЛицо
	|		ИНАЧЕ СтатьиРасходов.АналитикаРасходов
	|	КОНЕЦ КАК АналитикаРасходов,
	|
	|	ВЫБОР
	|		КОГДА СтатьиРасходов.АналитикаАктивовПассивов = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.МестоВСтруктуреПредприятия
	|		КОГДА СтатьиРасходов.АналитикаАктивовПассивов = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.Ссылка.Организация
	|		КОГДА СтатьиРасходов.АналитикаАктивовПассивов = ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.Сотрудник.ФизическоеЛицо
	|		ИНАЧЕ СтатьиРасходов.АналитикаАктивовПассивов
	|	КОНЕЦ КАК АналитикаАктивовПассивов,
	|
	|	СтатьиРасходов.СчетУчета КАК СчетУчета,
	|	СтатьиРасходов.Субконто1 КАК Субконто1,
	|	СтатьиРасходов.Субконто2 КАК Субконто2,
	|	СтатьиРасходов.Субконто3 КАК Субконто3,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОценочныеОбязательстваПоВознаграждениям) КАК СчетКт,
	|	ВЫРАЗИТЬ(ОценочныеОбязательства.СуммаРезерва * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2)) КАК Сумма,
	|	ВЫРАЗИТЬ(ОценочныеОбязательства.СуммаРезерваНУ * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2)) КАК СуммаНУ,
	|	ОценочныеОбязательства.СуммаРезерва КАК СуммаРегл,
	|	ОценочныеОбязательства.СуммаРезерваНУ КАК СуммаРеглНУ,
	|	ОценочныеОбязательства.НомерСтроки КАК НомерСтроки,
	|	ОценочныеОбязательства.МестоВСтруктуреПредприятия КАК Подразделение,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачислениеРезервовПоВознаграждениям) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ОплатаТруда) КАК СтатьяАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаСтатьиАктивовПассивов,
	|	ОценочныеОбязательства.Ссылка,
	|	КОНЕЦПЕРИОДА(ОценочныеОбязательства.Ссылка.ПериодРегистрации, МЕСЯЦ) КАК ПериодРегистрации,
	|	
	|	ОценочныеОбязательства.ИдентификаторСтроки КАК ИдентификаторСтроки
	|	
	|ИЗ
	|	Документ.НачислениеОценочныхОбязательствПоОтпускам.ОценочныеОбязательстваПоСотрудникам КАК ОценочныеОбязательства
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыКОтражению КАК ДокументыКОтражению
	|		ПО ОценочныеОбязательства.Ссылка = ДокументыКОтражению.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСпособыОтражения КАК СтатьиРасходов
	|		ПО ОценочныеОбязательства.СпособОтраженияЗарплатыВБухучете = СтатьиРасходов.СпособОтражения
	|			И ОценочныеОбязательства.ОблагаетсяЕНВД = СтатьиРасходов.ОблагаетсяЕНВД
	|ГДЕ
	|	ОценочныеОбязательства.СуммаРезерва + 
	|		ОценочныеОбязательства.СуммаРезерваНУ <> 0
	|	И НЕ СтатьиРасходов.ОплатаСдельныхРабот
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СтатьиРасходов.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	СтатьиРасходов.СтатьяРасходовВзносов,
	|	СтатьиРасходов.ВариантРаспределенияВзносов,
	|	СтатьиРасходов.ПринятиеКналоговомуУчетуВзносов,
	|
	|	ВЫБОР
	|		КОГДА СтатьиРасходов.АналитикаРасходовВзносов = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.МестоВСтруктуреПредприятия
	|		КОГДА СтатьиРасходов.АналитикаРасходовВзносов = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.Ссылка.Организация
	|		КОГДА СтатьиРасходов.АналитикаРасходов = ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.Сотрудник.ФизическоеЛицо
	|		ИНАЧЕ СтатьиРасходов.АналитикаРасходовВзносов
	|	КОНЕЦ,
	|
	|	ВЫБОР
	|		КОГДА СтатьиРасходов.АналитикаАктивовПассивовВзносов = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.МестоВСтруктуреПредприятия
	|		КОГДА СтатьиРасходов.АналитикаАктивовПассивовВзносов = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.Ссылка.Организация
	|		КОГДА СтатьиРасходов.АналитикаАктивовПассивов = ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.Сотрудник.ФизическоеЛицо
	|		ИНАЧЕ СтатьиРасходов.АналитикаАктивовПассивовВзносов
	|	КОНЕЦ,
	|
	|	СтатьиРасходов.СчетУчетаВзносов,
	|	СтатьиРасходов.Субконто1Взносов,
	|	СтатьиРасходов.Субконто2Взносов,
	|	СтатьиРасходов.Субконто3Взносов,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОценочныеОбязательстваПостраховымВзносам),
	|	ВЫРАЗИТЬ((ОценочныеОбязательства.СуммаРезерваСтраховыхВзносов) * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2)),
	|	ВЫРАЗИТЬ((ОценочныеОбязательства.СуммаРезерваСтраховыхВзносовНУ) * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2)),
	|	ОценочныеОбязательства.СуммаРезерваСтраховыхВзносов,
	|	ОценочныеОбязательства.СуммаРезерваСтраховыхВзносовНУ,
	|	ОценочныеОбязательства.НомерСтроки,
	|	ОценочныеОбязательства.МестоВСтруктуреПредприятия,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачислениеРезервовПоСтраховымВзносам),
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.Налоги),
	|	НЕОПРЕДЕЛЕНО,
	|	ОценочныеОбязательства.Ссылка,
	|	КОНЕЦПЕРИОДА(ОценочныеОбязательства.Ссылка.ПериодРегистрации, МЕСЯЦ) КАК ПериодРегистрации,
	|	
	|	ОценочныеОбязательства.ИдентификаторСтроки КАК ИдентификаторСтроки
	|	
	|ИЗ
	|	Документ.НачислениеОценочныхОбязательствПоОтпускам.ОценочныеОбязательстваПоСотрудникам КАК ОценочныеОбязательства
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыКОтражению КАК ДокументыКОтражению
	|		ПО ОценочныеОбязательства.Ссылка = ДокументыКОтражению.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСпособыОтражения КАК СтатьиРасходов
	|		ПО ОценочныеОбязательства.СпособОтраженияЗарплатыВБухучете = СтатьиРасходов.СпособОтражения
	|			И ОценочныеОбязательства.ОблагаетсяЕНВД = СтатьиРасходов.ОблагаетсяЕНВД
	|ГДЕ
	|	ОценочныеОбязательства.СуммаРезерваСтраховыхВзносов + 
	|		ОценочныеОбязательства.СуммаРезерваСтраховыхВзносовНУ <> 0
	|	И НЕ СтатьиРасходов.ОплатаСдельныхРабот
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СтатьиРасходов.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	СтатьиРасходов.СтатьяРасходовТравматизм,
	|	СтатьиРасходов.ВариантРаспределенияТравматизм,
	|	СтатьиРасходов.ПринятиеКналоговомуУчетуТравматизм,
	|
	|	ВЫБОР
	|		КОГДА СтатьиРасходов.АналитикаРасходовТравматизм = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.МестоВСтруктуреПредприятия
	|		КОГДА СтатьиРасходов.АналитикаРасходовТравматизм = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.Ссылка.Организация
	|		КОГДА СтатьиРасходов.АналитикаРасходов = ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.Сотрудник.ФизическоеЛицо
	|		ИНАЧЕ СтатьиРасходов.АналитикаРасходовТравматизм
	|	КОНЕЦ,
	|
	|	ВЫБОР
	|		КОГДА СтатьиРасходов.АналитикаАктивовПассивовТравматизм = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.МестоВСтруктуреПредприятия
	|		КОГДА СтатьиРасходов.АналитикаАктивовПассивовТравматизм = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.Ссылка.Организация
	|		КОГДА СтатьиРасходов.АналитикаАктивовПассивов = ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.Сотрудник.ФизическоеЛицо
	|		ИНАЧЕ СтатьиРасходов.АналитикаАктивовПассивовТравматизм
	|	КОНЕЦ,
	|
	|	СтатьиРасходов.СчетУчетаТравматизм,
	|	СтатьиРасходов.Субконто1Травматизм,
	|	СтатьиРасходов.Субконто2Травматизм,
	|	СтатьиРасходов.Субконто3Травматизм,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОценочныеОбязательстваПостраховымВзносам),
	|	ВЫРАЗИТЬ((ОценочныеОбязательства.СуммаРезерваФССНесчастныеСлучаи) * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2)),
	|	ВЫРАЗИТЬ((ОценочныеОбязательства.СуммаРезерваФССНесчастныеСлучаиНУ) * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2)),
	|	ОценочныеОбязательства.СуммаРезерваФССНесчастныеСлучаи,
	|	ОценочныеОбязательства.СуммаРезерваФССНесчастныеСлучаиНУ,
	|	ОценочныеОбязательства.НомерСтроки,
	|	ОценочныеОбязательства.МестоВСтруктуреПредприятия,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачислениеРезервовПоСтраховымВзносам),
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.Налоги),
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ФССНС),
	|	ОценочныеОбязательства.Ссылка,
	|	КОНЕЦПЕРИОДА(ОценочныеОбязательства.Ссылка.ПериодРегистрации, МЕСЯЦ) КАК ПериодРегистрации,
	|	
	|	ОценочныеОбязательства.ИдентификаторСтроки КАК ИдентификаторСтроки
	|	
	|ИЗ
	|	Документ.НачислениеОценочныхОбязательствПоОтпускам.ОценочныеОбязательстваПоСотрудникам КАК ОценочныеОбязательства
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыКОтражению КАК ДокументыКОтражению
	|		ПО ОценочныеОбязательства.Ссылка = ДокументыКОтражению.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСпособыОтражения КАК СтатьиРасходов
	|		ПО ОценочныеОбязательства.СпособОтраженияЗарплатыВБухучете = СтатьиРасходов.СпособОтражения
	|			И ОценочныеОбязательства.ОблагаетсяЕНВД = СтатьиРасходов.ОблагаетсяЕНВД
	|ГДЕ
	|	ОценочныеОбязательства.СуммаРезерваФССНесчастныеСлучаи + 
	|		ОценочныеОбязательства.СуммаРезерваФССНесчастныеСлучаиНУ <> 0
	|	И (НЕ СтатьиРасходов.ОплатаСдельныхРабот ИЛИ СтатьиРасходов.РежимУчетаВзносовНаТравматизм = 1)
	|";
	
	Если Не ТекстыЗапроса = Неопределено Тогда	
		ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(ТекстыЗапроса)
	
	ИмяРегистра = "ВтИсходныеПрочиеРасходы";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВТРасходыДокумента", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВТРасходыДокумента(ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	&Организация                           КАК Организация,
	|	ВТДанныеРасходы.Подразделение          КАК Подразделение,
	|	ВТДанныеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВТДанныеРасходы.СтатьяРасходов         КАК СтатьяРасходов,
	|	ВТДанныеРасходы.АналитикаРасходов      КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
	|
	|	СУММА(ВТДанныеРасходы.Сумма) КАК СуммаСНДС,
	|	СУММА(ВТДанныеРасходы.Сумма) КАК СуммаБезНДС,
	|	СУММА(ВТДанныеРасходы.Сумма) КАК СуммаБезНДСУпр,
	|	СУММА(ВТДанныеРасходы.СуммаРегл) КАК СуммаСНДСРегл,
	|	СУММА(ВТДанныеРасходы.СуммаРегл) КАК СуммаБезНДСРегл,
	|	СУММА(ВЫБОР
	|		КОГДА НЕ ВТДанныеРасходы.СтатьяРасходов.ПринятиеКналоговомуУчету
	|			ТОГДА ВТДанныеРасходы.СуммаРегл - ВТДанныеРасходы.СуммаРеглНУ
	|		ИНАЧЕ 0 КОНЕЦ) КАК ПостояннаяРазница,
	|	СУММА(ВЫБОР
	|		КОГДА ВТДанныеРасходы.СтатьяРасходов.ПринятиеКналоговомуУчету
	|			ТОГДА ВТДанныеРасходы.СуммаРегл - ВТДанныеРасходы.СуммаРеглНУ
	|		ИНАЧЕ 0 КОНЕЦ) КАК ВременнаяРазница,
	|	НЕОПРЕДЕЛЕНО КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|	
	|	ВТДанныеРасходы.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ВЫБОР
	|		КОГДА ВТДанныеРасходы.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачислениеРезервовПоВознаграждениям)
	|			ТОГДА &НастройкаХозяйственнойОперации
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.НачислениеРезервовПоСтраховымВзносам)
	|	КОНЕЦ КАК НастройкаХозяйственнойОперации
	|
	|ПОМЕСТИТЬ ВтИсходныеПрочиеРасходы
	|ИЗ
	|	ВТРасходыДокумента КАК ВТДанныеРасходы
	|
	|ГДЕ
	|	ВТДанныеРасходы.СтатьяРасходов Ссылка ПланВидовХарактеристик.СтатьиРасходов
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТДанныеРасходы.ВариантРаспределения,
	|	ВТДанныеРасходы.НомерСтроки,
	|	ВТДанныеРасходы.СтатьяРасходов,
	|	ВТДанныеРасходы.НаправлениеДеятельности,
	|	ВТДанныеРасходы.АналитикаРасходов,
	|	ВТДанныеРасходы.Подразделение,
	|	ВТДанныеРасходы.ИдентификаторСтроки,
	|	ВЫБОР
	|		КОГДА ВТДанныеРасходы.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачислениеРезервовПоВознаграждениям)
	|			ТОГДА &НастройкаХозяйственнойОперации
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.НачислениеРезервовПоСтраховымВзносам)
	|	КОНЕЦ
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтПрочиеРасходы(ТекстыЗапроса)
	
	ИмяРегистра = "ВтПрочиеРасходы";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтИсходныеПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаВтПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеРасходы(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеРасходы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(ТекстыЗапроса)
	
	ИмяРегистра = "ВтИсходныеПартииПрочихРасходов";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВТРасходыДокумента", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВТРасходыДокумента(ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)      КАК ВидДвижения,
	|	&Период                                     КАК Период,
	|	&Организация                                КАК Организация,
	|	ВТДанныеРасходы.Подразделение               КАК Подразделение,
	|	ВТДанныеРасходы.СтатьяРасходов              КАК СтатьяРасходов,
	|	ВТДанныеРасходы.АналитикаРасходов           КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                                КАК АналитикаАктивовПассивов,
	|	ВТДанныеРасходы.Ссылка                      КАК ДокументПоступленияРасходов,
	|	НЕОПРЕДЕЛЕНО                                КАК АналитикаУчетаПартий,
	|	ВТДанныеРасходы.НаправлениеДеятельности 	КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                КАК АналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО                                КАК ВидДеятельностиНДС,
	|
	|	СУММА(ВТДанныеРасходы.Сумма)                КАК Стоимость,
	|	СУММА(ВТДанныеРасходы.Сумма)                КАК СтоимостьБезНДС,
	|	0                                           КАК НДСУпр,
	|	СУММА(ВТДанныеРасходы.СуммаРегл)            КАК СтоимостьРегл,
	|	СУММА(ВЫБОР
	|		КОГДА НЕ ВТДанныеРасходы.СтатьяРасходов.ПринятиеКналоговомуУчету
	|			ТОГДА ВТДанныеРасходы.СуммаРегл - ВТДанныеРасходы.СуммаРеглНУ
	|		ИНАЧЕ 0 КОНЕЦ)                          КАК ПостояннаяРазница,
	|	СУММА(ВЫБОР
	|		КОГДА ВТДанныеРасходы.СтатьяРасходов.ПринятиеКналоговомуУчету
	|			ТОГДА ВТДанныеРасходы.СуммаРегл - ВТДанныеРасходы.СуммаРеглНУ
	|		ИНАЧЕ 0 КОНЕЦ)                          КАК ВременнаяРазница,
	|	0                                           КАК НДСРегл,
	|	НЕОПРЕДЕЛЕНО                                КАК ХозяйственнаяОперация,
	|	
	|	ВТДанныеРасходы.ИдентификаторСтроки         КАК ИдентификаторФинЗаписи,
	|	&НастройкаХозяйственнойОперации             КАК НастройкаХозяйственнойОперации
	|	
	|
	|ПОМЕСТИТЬ ВтИсходныеПартииПрочихРасходов
	|ИЗ
	|	ВТРасходыДокумента КАК ВТДанныеРасходы
	|
	|ГДЕ
	|	ВТДанныеРасходы.СтатьяРасходов Ссылка ПланВидовХарактеристик.СтатьиРасходов
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТДанныеРасходы.Подразделение,
	|	ВТДанныеРасходы.СтатьяРасходов,
	|	ВТДанныеРасходы.АналитикаРасходов,
	|	ВТДанныеРасходы.НаправлениеДеятельности,
	|	ВТДанныеРасходы.Ссылка,
	|	ВТДанныеРасходы.ИдентификаторСтроки
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтПартииПрочихРасходов(ТекстыЗапроса)
	
	ИмяРегистра = "ВтПартииПрочихРасходов";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтИсходныеПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаВтПартииПрочихРасходов();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПартииПрочихРасходов(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПартииПрочихРасходов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПартииПрочихРасходов(ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаПартииПрочихРасходов();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПрочиеАктивыПассивы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВТРасходыДокумента", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВТРасходыДокумента(ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПартииПрочихРасходов(ТекстыЗапроса);
	КонецЕсли;
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаВыработкаСотрудников", ТекстыЗапроса) Тогда
		ТекстЗапросаВтВыработкаСотрудников(Запрос, ТекстыЗапроса);
	КонецЕсли;

	ТекстЗапроса = 
	// расходное движение по предопределенным статьям Оплата труда и Взносы с оплаты труда
	"ВЫБРАТЬ
	|	&Период                                  КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)   КАК ВидДвижения,
	|	&Организация                             КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО                             КАК НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА ВТДанныеРасходы.СтатьяАктивовПассивов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ОплатаТруда)
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РезервыПоВознаграждениям)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РезервыПоСтраховымВзносам)
	|	КОНЕЦ                                    КАК Статья,
	|	ЗНАЧЕНИЕ(Справочник.Резервы.РезервЕжегодныхОтпусков) КАК Аналитика,
	|	СУММА(ВТДанныеРасходы.Сумма)             КАК Сумма
	|ИЗ
	|	ВТРасходыДокумента КАК ВТДанныеРасходы
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА ВТДанныеРасходы.СтатьяАктивовПассивов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ОплатаТруда)
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РезервыПоВознаграждениям)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РезервыПоСтраховымВзносам)
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// сдельная зарплата к выплате на пассивы
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РезервыПоВознаграждениям) КАК Статья,
	|	ЗНАЧЕНИЕ(Справочник.Резервы.РезервЕжегодныхОтпусков) КАК Аналитика,
	|	СУММА(Строки.Сумма) КАК Сумма
	|ИЗ
	|	ВтТаблицаВыработкаСотрудников КАК Строки
	|ГДЕ
	|	Строки.ТипНалога = ЗНАЧЕНИЕ(Перечисление.ТипыОценочныхОбязательств.ОценочныеОбязательстваПоВознаграждениям)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// взносы со сдельной зарплаты к перечислению на пассивы
	|ВЫБРАТЬ
	|	&Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	&Организация,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РезервыПоСтраховымВзносам) КАК Статья,
	|	ЗНАЧЕНИЕ(Справочник.Резервы.РезервЕжегодныхОтпусков),
	|	СУММА(Строки.Сумма) КАК Сумма
	|ИЗ
	|	ВтТаблицаВыработкаСотрудников КАК Строки
	|ГДЕ
	|	Строки.ТипНалога <> ЗНАЧЕНИЕ(Перечисление.ТипыОценочныхОбязательств.ОценочныеОбязательстваПоВознаграждениям)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// приходное движение по статьям пассивов, указанных в способах отражения
	|ВЫБРАТЬ
	|	&Период                                  КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)   КАК ВидДвижения,
	|	&Организация                             КАК Организация,
	|	ВТДанныеРасходы.Подразделение            КАК Подразделение,
	|	ВТДанныеРасходы.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	ВТДанныеРасходы.СтатьяРасходов           КАК Статья,
	|	ВТДанныеРасходы.АналитикаАктивовПассивов КАК Аналитика,
	|	СУММА(ВТДанныеРасходы.Сумма)             КАК Сумма
	|ИЗ
	|	ВТРасходыДокумента КАК ВТДанныеРасходы
	|
	|ГДЕ
	|	ВТДанныеРасходы.СтатьяРасходов Ссылка ПланВидовХарактеристик.СтатьиАктивовПассивов
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТДанныеРасходы.Подразделение,
	|	ВТДанныеРасходы.СтатьяРасходов,
	|	ВТДанныеРасходы.НаправлениеДеятельности,
	|	ВТДанныеРасходы.АналитикаАктивовПассивов
	|";
	ТекстЗапроса = ТекстЗапроса + "ОБЪЕДИНИТЬ ВСЕ"
		+ РегистрыНакопления.ПрочиеАктивыПассивы.ТекстЗапросаТаблицаПрочиеАктивыПассивы();
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДвиженияПоПрочимАктивамПассивам(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "ДвиженияПоПрочимАктивамПассивам";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВТРасходыДокумента", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВТРасходыДокумента(ТекстыЗапроса);
	КонецЕсли;
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаВыработкаСотрудников", ТекстыЗапроса) Тогда
		ТекстЗапросаВтВыработкаСотрудников(Запрос, ТекстыЗапроса);
	КонецЕсли;

	ТекстЗапроса = 
	// расходное движение по предопределенным статьям Оплата труда и Взносы с оплаты труда
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ВТДанныеРасходы.Подразделение КАК Подразделение,
	|	ВТДанныеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА ВТДанныеРасходы.СтатьяАктивовПассивов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ОплатаТруда)
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РезервыПоВознаграждениям)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РезервыПоСтраховымВзносам)
	|	КОНЕЦ КАК Статья,
	|	ЗНАЧЕНИЕ(Справочник.Резервы.РезервЕжегодныхОтпусков) КАК Аналитика,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиСчетовУчетаПрочихОпераций.ПустаяСсылка) КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Кредит) КАК ДебетКредит,
	|
	|	СУММА(ВТДанныеРасходы.СуммаРегл) КАК СуммаРегл,
	|	0 КАК ПостояннаяРазница,
	|	СУММА(ВТДанныеРасходы.СуммаРегл - ВТДанныеРасходы.СуммаРеглНУ) КАК ВременнаяРазница,
	|	СУММА(ВТДанныеРасходы.Сумма) КАК СуммаУпр,
	|	СУММА(ВТДанныеРасходы.Сумма) КАК СуммаСНДС,
	|	СУММА(ВТДанныеРасходы.Сумма) КАК СуммаБезНДС,
	|	
	|	ВТДанныеРасходы.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ВЫБОР
	|		КОГДА ВТДанныеРасходы.СтатьяАктивовПассивов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ОплатаТруда)
	|			ТОГДА &НастройкаХозяйственнойОперации
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.НачислениеРезервовПоСтраховымВзносам)
	|	КОНЕЦ КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ВТРасходыДокумента КАК ВТДанныеРасходы
	|СГРУППИРОВАТЬ ПО
	|	ВТДанныеРасходы.Подразделение,
	|	ВТДанныеРасходы.НаправлениеДеятельности,
	|	ВТДанныеРасходы.СтатьяАктивовПассивов,
	|	ВТДанныеРасходы.АналитикаСтатьиАктивовПассивов,
	|	ВТДанныеРасходы.ИдентификаторСтроки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// сдельная зарплата к выплате на пассивы
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РезервыПоВознаграждениям) КАК Статья,
	|	ЗНАЧЕНИЕ(Справочник.Резервы.РезервЕжегодныхОтпусков) КАК Аналитика,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиСчетовУчетаПрочихОпераций.ПустаяСсылка) КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Кредит) КАК ДебетКредит,
	|
	|	СУММА(Строки.СуммаРегл) КАК СуммаРегл,
	|	СУММА(Строки.ВременнаяРазница) КАК ПостояннаяРазница,
	|	СУММА(Строки.ПостояннаяРазница) КАК ВременнаяРазница,
	|	СУММА(Строки.Сумма) КАК СуммаУпр,
	|	СУММА(Строки.Сумма) КАК СуммаСНДС,
	|	СУММА(Строки.Сумма) КАК СуммаБезНДС,
	|
	|	Строки.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	&НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ВтТаблицаВыработкаСотрудников КАК Строки
	|ГДЕ
	|	Строки.ТипНалога = ЗНАЧЕНИЕ(Перечисление.ТипыОценочныхОбязательств.ОценочныеОбязательстваПоВознаграждениям)
	|СГРУППИРОВАТЬ ПО
	|	Строки.ИдентификаторСтроки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// взносы со сдельной зарплаты к перечислению на пассивы
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РезервыПоСтраховымВзносам) КАК Статья,
	|	ЗНАЧЕНИЕ(Справочник.Резервы.РезервЕжегодныхОтпусков) КАК Аналитика,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиСчетовУчетаПрочихОпераций.ПустаяСсылка) КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Кредит) КАК ДебетКредит,
	|
	|	СУММА(Строки.СуммаРегл) КАК СуммаРегл,
	|	СУММА(Строки.ВременнаяРазница) КАК ПостояннаяРазница,
	|	СУММА(Строки.ПостояннаяРазница) КАК ВременнаяРазница,
	|	СУММА(Строки.Сумма) КАК СуммаУпр,
	|	СУММА(Строки.Сумма) КАК СуммаСНДС,
	|	СУММА(Строки.Сумма) КАК СуммаБезНДС,
	|
	|	Строки.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	&НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ВтТаблицаВыработкаСотрудников КАК Строки
	|ГДЕ
	|	Строки.ТипНалога <> ЗНАЧЕНИЕ(Перечисление.ТипыОценочныхОбязательств.ОценочныеОбязательстваПоВознаграждениям)
	|СГРУППИРОВАТЬ ПО
	|	Строки.ИдентификаторСтроки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// приходное движение по статьям пассивов, указанных в способах отражения
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ВТДанныеРасходы.Подразделение КАК Подразделение,
	|	ВТДанныеРасходы.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	ВТДанныеРасходы.СтатьяРасходов КАК Статья,
	|	ВТДанныеРасходы.АналитикаАктивовПассивов КАК Аналитика,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиСчетовУчетаПрочихОпераций.ПустаяСсылка) КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Дебет) КАК ДебетКредит,
	|
	|	СУММА(ВТДанныеРасходы.СуммаРегл) КАК СуммаРегл,
	|	0 КАК ПостояннаяРазница,
	|	СУММА(ВТДанныеРасходы.СуммаРегл - ВТДанныеРасходы.СуммаРеглНУ) КАК ВременнаяРазница,
	|	СУММА(ВТДанныеРасходы.Сумма) КАК СуммаУпр,
	|	СУММА(ВТДанныеРасходы.Сумма) КАК СуммаСНДС,
	|	СУММА(ВТДанныеРасходы.Сумма) КАК СуммаБезНДС,
	|	
	|	ВТДанныеРасходы.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	&НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ВТРасходыДокумента КАК ВТДанныеРасходы
	|ГДЕ
	|	ВТДанныеРасходы.СтатьяРасходов Ссылка ПланВидовХарактеристик.СтатьиАктивовПассивов
	|СГРУППИРОВАТЬ ПО
	|	ВТДанныеРасходы.Подразделение,
	|	ВТДанныеРасходы.НаправлениеДеятельности,
	|	ВТДанныеРасходы.СтатьяРасходов,
	|	ВТДанныеРасходы.АналитикаАктивовПассивов,
	|	ВТДанныеРасходы.ИдентификаторСтроки
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДвиженияДоходыРасходыПрочиеАктивыПассивы(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ДвиженияДоходыРасходыПрочиеАктивыПассивы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВТРасходыДокумента", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВТРасходыДокумента(ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса =
	// расходы - пассивы
	"ВЫБРАТЬ
	|	&Период                                  КАК Период,
	|	МАКСИМУМ(ВТДанныеРасходы.НомерСтроки)    КАК НомерСтроки,
	|	ВТДанныеРасходы.ХозяйственнаяОперация    КАК ХозяйственнаяОперация,
	|
	|	&Организация                             КАК Организация,
	
	|	ВТДанныеРасходы.Подразделение            КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО                             КАК НаправлениеДеятельности,
	|	ВТДанныеРасходы.СтатьяАктивовПассивов    КАК Статья,
	|	НЕОПРЕДЕЛЕНО                             КАК АналитикаРасходов,
	|	ВЫБОР КОГДА ВТДанныеРасходы.СтатьяАктивовПассивов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.Налоги) ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ПустаяСсылка)
	|	ИНАЧЕ
	|		НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК АналитикаАктивовПассивов,
	
	|	ВТДанныеРасходы.Подразделение            КАК КорПодразделение,
	|	ВТДанныеРасходы.НаправлениеДеятельности  КАК КорНаправлениеДеятельности,
	|	ВТДанныеРасходы.СтатьяРасходов           КАК КорСтатья,
	|	ВТДанныеРасходы.АналитикаРасходов        КАК КорАналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                             КАК КорАналитикаАктивовПассивов,
	|
	|	СУММА(ВТДанныеРасходы.Сумма)             КАК Сумма,
	|	СУММА(ВТДанныеРасходы.Сумма)             КАК СуммаУпр,
	|	СУММА(ВТДанныеРасходы.СуммаРегл)         КАК СуммаРегл
	|
	|ИЗ
	|	ВТРасходыДокумента КАК ВТДанныеРасходы
	|
	|ГДЕ
	|	ВТДанныеРасходы.Сумма <> 0
	|	И ВТДанныеРасходы.СтатьяРасходов Ссылка ПланВидовХарактеристик.СтатьиРасходов
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТДанныеРасходы.СтатьяРасходов,
	|	ВТДанныеРасходы.АналитикаРасходов,
	|	ВТДанныеРасходы.СтатьяАктивовПассивов,
	|	ВТДанныеРасходы.ХозяйственнаяОперация,
	|	ВТДанныеРасходы.Подразделение,
	|	ВТДанныеРасходы.НаправлениеДеятельности
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// пассивы - пассивы
	|ВЫБРАТЬ
	|	&Период                                  КАК Период,
	|	МАКСИМУМ(ВТДанныеРасходы.НомерСтроки)    КАК НомерСтроки,
	|
	|	ВТДанныеРасходы.ХозяйственнаяОперация    КАК ХозяйственнаяОперация,
	|
	|	&Организация                             КАК Организация,
	
	|	ВТДанныеРасходы.Подразделение            КАК Подразделение,
	|	ВТДанныеРасходы.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	ВТДанныеРасходы.СтатьяАктивовПассивов    КАК Статья,
	|	НЕОПРЕДЕЛЕНО                             КАК АналитикаРасходов,
	|	ВЫБОР КОГДА ВТДанныеРасходы.СтатьяАктивовПассивов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.Налоги) ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ПустаяСсылка)
	|	ИНАЧЕ
	|		НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК АналитикаАктивовПассивов,
	|
	|	ВТДанныеРасходы.Подразделение            КАК КорПодразделение,
	|	ВТДанныеРасходы.НаправлениеДеятельности  КАК КорНаправлениеДеятельности,
	|	ВТДанныеРасходы.СтатьяРасходов           КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                             КАК КорАналитикаРасходов,
	|	ВТДанныеРасходы.АналитикаАктивовПассивов КАК КорАналитикаАктивовПассивов,
	
	|	СУММА(ВТДанныеРасходы.Сумма)             КАК Сумма,
	|	СУММА(ВТДанныеРасходы.Сумма)             КАК СуммаУпр,
	|	СУММА(ВТДанныеРасходы.СуммаРегл)         КАК СуммаРегл
	|
	|ИЗ
	|	ВТРасходыДокумента КАК ВТДанныеРасходы
	|
	|ГДЕ
	|	ВТДанныеРасходы.Сумма > 0
	|	И ВТДанныеРасходы.СтатьяРасходов Ссылка ПланВидовХарактеристик.СтатьиАктивовПассивов
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТДанныеРасходы.СтатьяРасходов,
	|	ВТДанныеРасходы.АналитикаАктивовПассивов,
	|	ВТДанныеРасходы.СтатьяАктивовПассивов,
	|	ВТДанныеРасходы.ХозяйственнаяОперация,
	|	ВТДанныеРасходы.Подразделение,
	|	ВТДанныеРасходы.НаправлениеДеятельности
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПорядокОтраженияПрочихОпераций(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПорядокОтраженияПрочихОпераций";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	&Период	КАК Дата,
	|	&Организация КАК Организация,
	|	&Ссылка КАК Документ,
	|	"""" КАК ИдентификаторСтроки
	|ИЗ
	|	Документ.НачислениеОценочныхОбязательствПоОтпускам.ОценочныеОбязательства КАК Строки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СпособыОтраженияЗарплатыВБухУчете КАК СпособыОтражения
	|		ПО СпособыОтражения.Ссылка = Строки.СпособОтраженияЗарплатыВБухучете
	|ГДЕ
	|	Строки.Ссылка = &Ссылка
	|	И СпособыОтражения.ЕстьПрочиеОперации
	|	И НЕ &КорректировкаОбязательств";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОтражениеДокументовВРеглУчете";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период           КАК Период,
	|	&Организация      КАК Организация,
	|	&Период           КАК ДатаОтражения";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВТРасходыДокументаРегл(ТекстыЗапроса = Неопределено)
	
	ИмяРегистра = "ВТРасходыДокументаРегл";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВТСпособыОтражения", ТекстыЗапроса) Тогда
		ТекстЗапросаВТСпособыОтражения(ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СтатьиРасходов.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	СтатьиРасходов.СтатьяРасходов КАК СтатьяРасходов,
	|	СтатьиРасходов.ВариантРаспределения КАК ВариантРаспределения,
	|	СтатьиРасходов.ПринятиеКналоговомуУчету КАК ПринятиеКналоговомуУчету,
	|
	|	ВЫБОР
	|		КОГДА СтатьиРасходов.АналитикаРасходов = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.МестоВСтруктуреПредприятия
	|		КОГДА СтатьиРасходов.АналитикаРасходов = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.Ссылка.Организация
	|		ИНАЧЕ СтатьиРасходов.АналитикаРасходов
	|	КОНЕЦ КАК АналитикаРасходов,
	|
	|	ВЫБОР
	|		КОГДА СтатьиРасходов.АналитикаАктивовПассивов = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.МестоВСтруктуреПредприятия
	|		КОГДА СтатьиРасходов.АналитикаАктивовПассивов = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.Ссылка.Организация
	|		ИНАЧЕ СтатьиРасходов.АналитикаАктивовПассивов
	|	КОНЕЦ КАК АналитикаАктивовПассивов,
	|
	|	СтатьиРасходов.СчетУчета КАК СчетУчета,
	|	СтатьиРасходов.Субконто1 КАК Субконто1,
	|	СтатьиРасходов.Субконто2 КАК Субконто2,
	|	СтатьиРасходов.Субконто3 КАК Субконто3,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОценочныеОбязательстваПоВознаграждениям) КАК СчетКт,
	|	ВЫРАЗИТЬ(ОценочныеОбязательства.СуммаРезерва * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2)) КАК Сумма,
	|	ВЫРАЗИТЬ(ОценочныеОбязательства.СуммаРезерваНУ * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2)) КАК СуммаНУ,
	|	ОценочныеОбязательства.СуммаРезерва КАК СуммаРегл,
	|	ОценочныеОбязательства.СуммаРезерваНУ КАК СуммаРеглНУ,
	|	ОценочныеОбязательства.НомерСтроки КАК НомерСтроки,
	|	ОценочныеОбязательства.МестоВСтруктуреПредприятия КАК Подразделение,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачислениеРезервовПоВознаграждениям) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ОплатаТруда) КАК СтатьяАктивовПассивов,
	|	ОценочныеОбязательства.Ссылка,
	|	КОНЕЦПЕРИОДА(ОценочныеОбязательства.Ссылка.ПериодРегистрации, МЕСЯЦ) КАК ПериодРегистрации
	|ПОМЕСТИТЬ ВТРасходыДокументаРегл
	|ИЗ
	|	Документ.НачислениеОценочныхОбязательствПоОтпускам.ОценочныеОбязательства КАК ОценочныеОбязательства
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыКОтражению КАК ДокументыКОтражению
	|		ПО ОценочныеОбязательства.Ссылка = ДокументыКОтражению.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСпособыОтражения КАК СтатьиРасходов
	|		ПО ОценочныеОбязательства.СпособОтраженияЗарплатыВБухучете = СтатьиРасходов.СпособОтражения
	|			И ОценочныеОбязательства.ОблагаетсяЕНВД = СтатьиРасходов.ОблагаетсяЕНВД
	|ГДЕ
	|	ОценочныеОбязательства.СуммаРезерва + 
	|		ОценочныеОбязательства.СуммаРезерваНУ <> 0
	|	И НЕ СтатьиРасходов.ОплатаСдельныхРабот
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СтатьиРасходов.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	СтатьиРасходов.СтатьяРасходовВзносов,
	|	СтатьиРасходов.ВариантРаспределенияВзносов,
	|	СтатьиРасходов.ПринятиеКналоговомуУчетуВзносов,
	|
	|	ВЫБОР
	|		КОГДА СтатьиРасходов.АналитикаРасходовВзносов = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.МестоВСтруктуреПредприятия
	|		КОГДА СтатьиРасходов.АналитикаРасходовВзносов = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.Ссылка.Организация
	|		ИНАЧЕ СтатьиРасходов.АналитикаРасходовВзносов
	|	КОНЕЦ,
	|
	|	ВЫБОР
	|		КОГДА СтатьиРасходов.АналитикаАктивовПассивовВзносов = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.МестоВСтруктуреПредприятия
	|		КОГДА СтатьиРасходов.АналитикаАктивовПассивовВзносов = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.Ссылка.Организация
	|		ИНАЧЕ СтатьиРасходов.АналитикаАктивовПассивовВзносов
	|	КОНЕЦ,
	|
	|	СтатьиРасходов.СчетУчетаВзносов,
	|	СтатьиРасходов.Субконто1Взносов,
	|	СтатьиРасходов.Субконто2Взносов,
	|	СтатьиРасходов.Субконто3Взносов,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОценочныеОбязательстваПостраховымВзносам),
	|	ВЫРАЗИТЬ((ОценочныеОбязательства.СуммаРезерваСтраховыхВзносов) * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2)),
	|	ВЫРАЗИТЬ((ОценочныеОбязательства.СуммаРезерваСтраховыхВзносовНУ) * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2)),
	|	ОценочныеОбязательства.СуммаРезерваСтраховыхВзносов,
	|	ОценочныеОбязательства.СуммаРезерваСтраховыхВзносовНУ,
	|	ОценочныеОбязательства.НомерСтроки,
	|	ОценочныеОбязательства.МестоВСтруктуреПредприятия,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачислениеРезервовПоСтраховымВзносам),
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.Налоги),
	|	ОценочныеОбязательства.Ссылка,
	|	КОНЕЦПЕРИОДА(ОценочныеОбязательства.Ссылка.ПериодРегистрации, МЕСЯЦ) КАК ПериодРегистрации
	|ИЗ
	|	Документ.НачислениеОценочныхОбязательствПоОтпускам.ОценочныеОбязательства КАК ОценочныеОбязательства
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыКОтражению КАК ДокументыКОтражению
	|		ПО ОценочныеОбязательства.Ссылка = ДокументыКОтражению.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСпособыОтражения КАК СтатьиРасходов
	|		ПО ОценочныеОбязательства.СпособОтраженияЗарплатыВБухучете = СтатьиРасходов.СпособОтражения
	|			И ОценочныеОбязательства.ОблагаетсяЕНВД = СтатьиРасходов.ОблагаетсяЕНВД
	|ГДЕ
	|	ОценочныеОбязательства.СуммаРезерваСтраховыхВзносов + 
	|		ОценочныеОбязательства.СуммаРезерваСтраховыхВзносовНУ <> 0
	|	И НЕ СтатьиРасходов.ОплатаСдельныхРабот
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СтатьиРасходов.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	СтатьиРасходов.СтатьяРасходовТравматизм,
	|	СтатьиРасходов.ВариантРаспределенияТравматизм,
	|	СтатьиРасходов.ПринятиеКналоговомуУчетуТравматизм,
	|
	|	ВЫБОР
	|		КОГДА СтатьиРасходов.АналитикаРасходовТравматизм = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.МестоВСтруктуреПредприятия
	|		КОГДА СтатьиРасходов.АналитикаРасходовТравматизм = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.Ссылка.Организация
	|		ИНАЧЕ СтатьиРасходов.АналитикаРасходовТравматизм
	|	КОНЕЦ,
	|
	|	ВЫБОР
	|		КОГДА СтатьиРасходов.АналитикаАктивовПассивовТравматизм = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.МестоВСтруктуреПредприятия
	|		КОГДА СтатьиРасходов.АналитикаАктивовПассивовТравматизм = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ОценочныеОбязательства.Ссылка.Организация
	|		ИНАЧЕ СтатьиРасходов.АналитикаАктивовПассивовТравматизм
	|	КОНЕЦ,
	|
	|	СтатьиРасходов.СчетУчетаТравматизм,
	|	СтатьиРасходов.Субконто1Травматизм,
	|	СтатьиРасходов.Субконто2Травматизм,
	|	СтатьиРасходов.Субконто3Травматизм,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОценочныеОбязательстваПостраховымВзносам),
	|	ВЫРАЗИТЬ((ОценочныеОбязательства.СуммаРезерваФССНесчастныеСлучаи) * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2)),
	|	ВЫРАЗИТЬ((ОценочныеОбязательства.СуммаРезерваФССНесчастныеСлучаиНУ) * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2)),
	|	ОценочныеОбязательства.СуммаРезерваФССНесчастныеСлучаи,
	|	ОценочныеОбязательства.СуммаРезерваФССНесчастныеСлучаиНУ,
	|	ОценочныеОбязательства.НомерСтроки,
	|	ОценочныеОбязательства.МестоВСтруктуреПредприятия,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачислениеРезервовПоСтраховымВзносам),
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.Налоги),
	|	ОценочныеОбязательства.Ссылка,
	|	КОНЕЦПЕРИОДА(ОценочныеОбязательства.Ссылка.ПериодРегистрации, МЕСЯЦ) КАК ПериодРегистрации
	|ИЗ
	|	Документ.НачислениеОценочныхОбязательствПоОтпускам.ОценочныеОбязательства КАК ОценочныеОбязательства
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыКОтражению КАК ДокументыКОтражению
	|		ПО ОценочныеОбязательства.Ссылка = ДокументыКОтражению.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСпособыОтражения КАК СтатьиРасходов
	|		ПО ОценочныеОбязательства.СпособОтраженияЗарплатыВБухучете = СтатьиРасходов.СпособОтражения
	|			И ОценочныеОбязательства.ОблагаетсяЕНВД = СтатьиРасходов.ОблагаетсяЕНВД
	|ГДЕ
	|	ОценочныеОбязательства.СуммаРезерваФССНесчастныеСлучаи + 
	|		ОценочныеОбязательства.СуммаРезерваФССНесчастныеСлучаиНУ <> 0
	|	И (НЕ СтатьиРасходов.ОплатаСдельныхРабот ИЛИ СтатьиРасходов.РежимУчетаВзносовНаТравматизм = 1)
	|";
	
	Если Не ТекстыЗапроса = Неопределено Тогда	
		ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&ИдентификаторМетаданных КАК ТипСсылки,
	|	&ХозОперацияДляРеестра КАК ХозяйственнаяОперация,
	|	&Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК Партнер,
	|	НЕОПРЕДЕЛЕНО КАК МестоХранения,
	|	НЕОПРЕДЕЛЕНО КАК Контрагент,
	|	НЕОПРЕДЕЛЕНО КАК Подразделение,
	|	&Период КАК ДатаДокументаИБ,
	|	&Ссылка КАК Ссылка,
	|	&Номер КАК НомерДокументаИБ,
	|	НЕОПРЕДЕЛЕНО КАК Статус,
	|	&Ответственный КАК Ответственный,
	|	НЕОПРЕДЕЛЕНО КАК Автор,
	|	ЛОЖЬ КАК ДополнительнаяЗапись,
	|	НЕОПРЕДЕЛЕНО КАК Дополнительно,
	|	&Комментарий КАК Комментарий,
	|	&Проведен КАК Проведен,
	|	&ПометкаУдаления КАК ПометкаУдаления,
	|	&Период КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать КАК НомерПервичногоДокумента,
	|	0 КАК Сумма,
	|	НЕОПРЕДЕЛЕНО КАК Валюта,
	|	НЕОПРЕДЕЛЕНО КАК Договор,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
	|	ЛОЖЬ КАК СторноИсправление,
	|	НЕОПРЕДЕЛЕНО КАК СторнируемыйДокумент,
	|	НЕОПРЕДЕЛЕНО КАК ИсправляемыйДокумент,
	|	&Период КАК ДатаОтраженияВУчете,
	|	НЕОПРЕДЕЛЕНО КАК Приоритет";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.НачислениеОценочныхОбязательствПоОтпускам";
	ЗначенияПараметров = Новый Структура;
	ЗначенияПараметров.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.НачислениеОценочныхОбязательствПоОтпускам"));
	ЗначенияПараметров.Вставить("ХозОперацияДляРеестра", Перечисления.ХозяйственныеОперации.НачислениеОценочныхОбязательствПоОтпускам);
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ПереопределениеРасчетаПараметров = Новый Структура;
		ПереопределениеРасчетаПараметров.Вставить("Период", "КОНЕЦПЕРИОДА(ТаблицаДокументаОбновлениеИБ.Ссылка.Дата, МЕСЯЦ)");
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "";
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';
								|en = 'In document %ПолноеИмяДокумента%, adaptation of request for generating records of register %ИмяРегистра% is not implemented.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
			ТекстЗапроса, ПолноеИмяДокумента, СинонимТаблицыДокумента, Ложь, ПереопределениеРасчетаПараметров);
	Иначе
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
			ТекстЗапроса, ПолноеИмяДокумента, СинонимТаблицыДокумента);
	КонецЕсли;
	
	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

// Конец ERP

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

// Сформировать печатные формы
//
// ВХОДЯЩИЕ:
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать.
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы.
//   ОшибкиПечати          - Список значений  - Ошибки печати  (значение - ссылка на объект, представление - текст
//                           ошибки).
//   ОбъектыПечати         - Список значений  - Объекты печати (значение - ссылка на объект, представление - имя
//                           области в которой был выведен объект).
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_СправкаПоОтпускамСотрудника") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
						КоллекцияПечатныхФорм,
						"ПФ_MXL_СправкаПоОтпускамСотрудника", НСтр("ru = 'Справка по отпускам';
																	|en = 'Leave statement'"),
						ПечатьСправкаПоОтпускамСотрудника(МассивОбъектов, ОбъектыПечати, ПараметрыПечати), ,);
	КонецЕсли;

КонецПроцедуры

Функция ПечатьСправкаПоОтпускамСотрудника(МассивОбъектов, ОбъектыПечати, ПараметрыПечати) Экспорт
	
	ДокументРезультат = Новый ТабличныйДокумент;
	ДокументРезультат.АвтоМасштаб = Истина;
	НомерСтрокиНачало = ДокументРезультат.ВысотаТаблицы + 1;
	
	Если ТипЗнч(ПараметрыПечати) = Тип("Структура") 
		И ПараметрыПечати.Свойство("ПериодРегистрации") Тогда
		ДатаОтчета = КонецМесяца(ПараметрыПечати.ПериодРегистрации);
	Иначе
		ДатаОтчета = ТекущаяДатаСеанса();
	КонецЕсли;

	ОтчетСправкаПоОтпускамСотрудника = Отчеты["СправкаПоОтпускам"].Создать();
	ОтчетСправкаПоОтпускамСотрудника.ИнициализироватьОтчет();
	КомпоновщикНастроек = ОтчетСправкаПоОтпускамСотрудника.КомпоновщикНастроек;
	
	Отбор = КомпоновщикНастроек.Настройки.Отбор;
	Отбор.Элементы.Очистить();
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Отбор, "Сотрудник", ВидСравненияКомпоновкиДанных.ВСписке, МассивОбъектов);
	
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	ДатаОстатков = НастройкиОтчета.ПараметрыДанных.Элементы.Найти("ДатаОстатков");
	ДатаОстатков.Значение = Новый СтандартнаяДатаНачала(ДатаОтчета);
	ДатаОстатков.Использование = Истина;
	КомпоновщикНастроек.ЗагрузитьНастройки(НастройкиОтчета);
	
	ОтчетСправкаПоОтпускамСотрудника.СкомпоноватьРезультат(ДокументРезультат);
	
	Возврат ДокументРезультат;
	
КонецФункции

#КонецОбласти

Процедура ПодготовитьДанныеДляОтраженияВУчете(ДокументСсылка, ДанныеДляОтражения, КорректировкаОбязательств) Экспорт
	
	Резерв = Справочники.Резервы.РезервЕжегодныхОтпусков();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("Резерв", Резерв);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РасчетРезерваОтпусков.*,
	|	&Резерв КАК Резерв
	|ИЗ
	|	Документ.НачислениеОценочныхОбязательствПоОтпускам.РасчетРезерваОтпусков КАК РасчетРезерваОтпусков
	|ГДЕ
	|	РасчетРезерваОтпусков.Ссылка = &Ссылка
	|;
	|
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОценочныеОбязательстваПоСотрудникам.*,
	|	&Резерв КАК Резерв
	|ИЗ
	|	Документ.НачислениеОценочныхОбязательствПоОтпускам.ОценочныеОбязательстваПоСотрудникам КАК ОценочныеОбязательстваПоСотрудникам
	|ГДЕ
	|	ОценочныеОбязательстваПоСотрудникам.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОценочныеОбязательства.*,
	|	&Резерв КАК Резерв
	|ИЗ
	|	Документ.НачислениеОценочныхОбязательствПоОтпускам.ОценочныеОбязательства КАК ОценочныеОбязательства
	|ГДЕ
	|	ОценочныеОбязательства.Ссылка = &Ссылка";
	
	Результат = Запрос.ВыполнитьПакет();
	
	РасчетРезерваОтпусков               = Результат[0].Выгрузить();
	ОценочныеОбязательстваПоСотрудникам = Результат[1].Выгрузить();
	ОценочныеОбязательства              = Результат[2].Выгрузить();
	
	Если НЕ КорректировкаОбязательств Тогда
		ДанныеДляОтражения.Вставить("РасчетРезерваОтпусков", РасчетРезерваОтпусков);
	КонецЕсли;
	ДанныеДляОтражения.ОценочныеОбязательстваПоСотрудникам = ОценочныеОбязательстваПоСотрудникам;
	ДанныеДляОтражения.ОценочныеОбязательства              = ОценочныеОбязательства;
	
КонецПроцедуры

Функция ТребуетсяУтверждениеДокументаБухгалтером(Организация = Неопределено) Экспорт
	
	// Подтверждение требуется, если используется обмен с бухгалтерией.
	ТребуетсяУтверждение = Ложь;
	
	// ЗарплатаКадрыПриложения.ОбменЗарплата3Бухгалтерия3
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОбменЗарплата3Бухгалтерия3")
		И Не ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.КонфигурацииЗарплатаКадры") Тогда
		
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОбменДаннымиЗарплата3Бухгалтерия3");
		ТребуетсяУтверждение = Модуль.ОбменИспользуется(Организация);
		
	КонецЕсли;
	// Конец ЗарплатаКадрыПриложения.ОбменЗарплата3Бухгалтерия3
	
	ОтражениеЗарплатыВБухучетеПереопределяемый.ТребуетсяУтверждениеДокументаБухгалтером(Организация, ТребуетсяУтверждение);
	
	Возврат ТребуетсяУтверждение;
	
КонецФункции

Процедура ЗаполнитьНачислениеОценочныхОбязательствПоОтпускам(ПараметрыЗаполнения, АдресХранилища) Экспорт
	
	ЭтотОбъект = ПараметрыЗаполнения.Объект;
	РезервОтпусков.ЗаполнитьДокументНачислениеОценочныхОбязательствПоОтпускам(ЭтотОбъект);
	
	Результат = Новый Структура("ЗаданиеВыполнено, Объект", Истина, ЭтотОбъект);
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);

КонецПроцедуры

Процедура ПеречитатьОценочныеОбязательства(ПараметрыЗаполнения, АдресХранилища) Экспорт
	
	ЭтотОбъект = ПараметрыЗаполнения.Объект;
	РезервОтпусков.ПеречитатьОценочныеОбязательства(ЭтотОбъект, Истина);
	
	Результат = Новый Структура("ЗаданиеВыполнено, Объект", Истина, ЭтотОбъект);
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);

КонецПроцедуры

Процедура ПеречитатьОценочныеОбязательстваПоСотрудникам(ПараметрыЗаполнения, АдресХранилища) Экспорт
	
	ЭтотОбъект = ПараметрыЗаполнения.Объект;
	РезервОтпусков.ПеречитатьОценочныеОбязательства(ЭтотОбъект, Ложь);
	
	Результат = Новый Структура("ЗаданиеВыполнено, Объект, ИмяТаблицы", Истина, ЭтотОбъект, "ОценочныеОбязательстваПоСотрудникам");
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);

КонецПроцедуры

Процедура ОбновитьОтражениеВУчете(ПараметрыЗаполнения, АдресХранилища) Экспорт
	
	ЭтотОбъект = ПараметрыЗаполнения.Объект;
	РезервОтпусков.ОбновитьОтражениеВУчете(ЭтотОбъект);
	
	Результат = Новый Структура("ЗаданиеВыполнено, Объект, ИмяТаблицы", Истина, ЭтотОбъект, "ОценочныеОбязательства");
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);

КонецПроцедуры

#КонецОбласти

#КонецЕсли