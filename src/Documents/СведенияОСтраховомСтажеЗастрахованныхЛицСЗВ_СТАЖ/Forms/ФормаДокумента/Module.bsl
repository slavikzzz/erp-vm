
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.ПриИнициализацииФормыРегламентированногоОтчета(ЭтаФорма, "ПФР");	
	ЭлектронныйДокументооборотСКонтролирующимиОрганами.ОтметитьКакПрочтенное(Объект.Ссылка);
	
	Если Параметры.Ключ.Пустая() Тогда
		
		Объект.Год = Макс(2017, Год(ТекущаяДатаСеанса()) - 1);
		ЗаполнитьОтчетныйПериод(ЭтаФорма);
		Объект.ТипСведений = Перечисления.ТипыСведенийСЗВ_СТАЖ.Исходная;
		ЗарплатаКадры.ЗаполнитьПервоначальныеЗначенияВФорме(ЭтотОбъект, ЗапрашиваемыеЗначенияПервоначальногоЗаполнения());
		ПерсонифицированныйУчет.ЗаполнитьДанныеРуководителя(Объект);
		
		ПриПолученииДанныхНаСервере();
		Если ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда 
			ЗаполнитьЗаписиОСтажеПриКопировании(Параметры.ЗначениеКопирования);
		КонецЕсли;
		
	КонецЕсли;
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначения.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплата");
		МодульПроцессыОбработкиДокументовЗарплата.ПриСозданииНаСервере(ЭтотОбъект, Объект);
	КонецЕсли;	
	// Конец ПроцессыОбработкиДокументов
	
	// ПанельОтправкиВКонтролирующиеОрганы
	РегламентированнаяОтчетностьКлиентСервер.ПриИнициализацииФормыРегламентированногоОтчета(ЭтаФорма, "ПФР");
	// Конец ПанельОтправкиВКонтролирующиеОрганы
	
	// БлокировкаИзмененияОбъектов
	БлокировкаИзмененияОбъектов.ПриСозданииНаСервереФормыОбъекта(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	// Конец БлокировкаИзмененияОбъектов
	
	// КадровыйЭДО
	КадровыйЭДО.ПриСозданииНаСервереФормыОбъекта(ЭтотОбъект, Отказ, СтандартнаяОбработка, Объект);
	// Конец КадровыйЭДО
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриПолученииДанныхНаСервере();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначения.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплата");
		МодульПроцессыОбработкиДокументовЗарплата.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;	
	// Конец ПроцессыОбработкиДокументов
	
	// БлокировкаИзмененияОбъектов
	БлокировкаИзмененияОбъектов.ПриЧтенииНаСервереФормыОбъекта(ЭтотОбъект, ТекущийОбъект,
		ПравоДоступа("Изменение", Метаданные.Документы.СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ));
	// Конец БлокировкаИзмененияОбъектов
	
	// КадровыйЭДО
	КадровыйЭДО.ПриЧтенииНаСервереФормыОбъекта(ЭтотОбъект, ТекущийОбъект, Объект);
	// Конец КадровыйЭДО
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЗначениеЗаполнено(ТекущийОбъект.Ссылка) Тогда 
		ДокументОснование = ТекущийОбъект.Ссылка;
	Иначе 
		ДокументОснование = Документы.СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ.ПолучитьСсылку();
		ТекущийОбъект.УстановитьСсылкуНового(ДокументОснование);
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.ЗаписиОСтажеСведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ДокументОснование.Установить(ДокументОснование);
	
	НомерСтроки = 1;
	Для Каждого ДанныеОСтаже Из ЗаписиОСтаже Цикл 
		НоваяЗапись = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, ДанныеОСтаже);
		НоваяЗапись.ДокументОснование = ДокументОснование;
		НоваяЗапись.Организация = ТекущийОбъект.Организация;
		НоваяЗапись.НомерСтрокиДокумента = НомерСтроки;
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
	
	ТекущийОбъект.ДополнительныеСвойства.Вставить("ЗаписиОСтаже", НаборЗаписей);
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначения.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплата");
		МодульПроцессыОбработкиДокументовЗарплата.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи, Отказ);
	КонецЕсли;		
	// Конец ПроцессыОбработкиДокументов
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// ПанельОтправкиВКонтролирующиеОрганы
	СохранитьСтатусОтправки(ЭтотОбъект, Объект.Ссылка);
	// Конец ПанельОтправкиВКонтролирующиеОрганы
	
	УстановитьДоступностьДанныхФормы();
	УстановитьСвойстваЭлементовФормы();
	
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначения.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплата");
		МодульПроцессыОбработкиДокументовЗарплата.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;	
	// Конец ПроцессыОбработкиДокументов
	
	// БлокировкаИзмененияОбъектов
	БлокировкаИзмененияОбъектов.ПослеЗаписиНаСервереФормыОбъекта(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец БлокировкаИзмененияОбъектов
	
	// КадровыйЭДО
	КадровыйЭДО.ПослеЗаписиНаСервереФормыОбъекта(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи, Объект);
	// Конец КадровыйЭДО
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначенияКлиент.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплатаКлиент");
		МодульПроцессыОбработкиДокументовЗарплата.ПередЗакрытием(ЭтотОбъект, Объект, Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	КонецЕсли;	
	// Конец ПроцессыОбработкиДокументов
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("Запись_СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ", ПараметрыЗаписи, Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "РедактированиеДанныхСтажаПоСотруднику" И Источник.ВладелецФормы = ЭтаФорма Тогда
		ПриИзмененииДанныхДокументаПоСотруднику(Параметр.АдресВоВременномХранилище);
	КонецЕсли;
	
	Если ИмяСобытия = "Завершение отправки в контролирующий орган" 
		И ТипЗнч(Параметр) = Тип("Структура")
		И Параметр.Ссылка = Объект.Ссылка Тогда
	    ПослеОтправкиВПФР();
	КонецЕсли;
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	// КадровыйЭДО
	КадровыйЭДОКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец КадровыйЭДО
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	ТекущийОбъект = РеквизитФормыВЗначение("Объект", Тип("ДокументОбъект.СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ"));
	ТекущийОбъект.ДополнительныеСвойства.Вставить("НеПроверятьДанныеСотрудников");
	
	Если Не ТекущийОбъект.ПроверитьЗаполнение() Тогда 
		Отказ = Истина;
	КонецЕсли;	
	
	ПерсонифицированныйУчет.ПроверитьДанныеСотрудников(ТекущийОбъект.Сотрудники, ТекущийОбъект.ОтчетныйПериод, ТекущийОбъект.ОкончаниеОтчетногоПериода, ЗаписиОСтаже, Отказ);
	
	ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(ПроверяемыеРеквизиты, "Объект");
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОрганизацияПриИзмененииНаСервере();	
	
КонецПроцедуры

&НаКлиенте
Процедура ГодПриИзменении(Элемент)
	
	ЗаполнитьОтчетныйПериод(ЭтаФорма);
	УстановитьСвойстваЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ТипСведенийПриИзменении(Элемент)
	
	ТипСведенийПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ФлагБлокировкиДокументаПриИзменении(Элемент)
	
	ФлагБлокировкиДокументаПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПрежнийСтраховательНаименованиеПолноеПриИзменении(Элемент)
	
	УстановитьЗаголовокГруппыПрежнегоСтрахователя(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрежнийСтраховательРегистрационныйНомерПФРПриИзменении(Элемент)
	
	УстановитьЗаголовокГруппыПрежнегоСтрахователя(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрежнийСтраховательИННПриИзменении(Элемент)
	
	УстановитьЗаголовокГруппыПрежнегоСтрахователя(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрежнийСтраховательКПППриИзменении(Элемент)
	
	УстановитьЗаголовокГруппыПрежнегоСтрахователя(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСотрудники

&НаКлиенте
Процедура СотрудникиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если Копирование Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока И Объект.ТипСведений = ПредопределенноеЗначение("Перечисление.ТипыСведенийСЗВ_СТАЖ.НазначениеПенсии") Тогда 
		Элемент.ТекущиеДанные.ДатаВыходаНаПенсию = Объект.Дата;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПередУдалением(Элемент, Отказ)
	
	СотрудникиПередУдалениемНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "СотрудникиДатаВыходаНаПенсию" Тогда 
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Сотрудники.ТекущиеДанные;
	Если Не ЗначениеЗаполнено(ТекущиеДанные.Сотрудник) Тогда
		Возврат;
	КонецЕсли;	
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФормуРедактированияКарточкиДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) <> Тип("Массив") Тогда
		Возврат;
	КонецЕсли;	
	
	СтандартнаяОбработка = Ложь;
	
	СписокСотрудников = Новый Массив;
	Для Каждого Сотрудник Из ВыбранноеЗначение Цикл
		СписокСотрудников.Добавить(Сотрудник);
	КонецЦикла;
	
	ЗаполнитьДанныеСотрудников(СписокСотрудников);
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ТекущиеДанные = Элементы.Сотрудники.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекущиеДанные.Сотрудник) Тогда 
		Возврат;
	КонецЕсли;
	
	СотрудникиПриОкончанииРедактированияНаСервере(ТекущиеДанные.Сотрудник);
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиСотрудникПриИзменении(Элемент)
	
	СотрудникиСотрудникПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиДатаВыходаНаПенсиюПриИзменении(Элемент)
	
	СотрудникиДатаВыходаНаПенсиюПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДосрочноеНазначениеПенсии

&НаКлиенте
Процедура ДосрочноеНазначениеПенсииПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущиеДанные = Элементы.ДосрочноеНазначениеПенсии.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДосрочноеНазначениеПенсииОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") И ВыбранноеЗначение.Свойство("ОснованиеВыслугиЛет") Тогда 
		СтандартнаяОбработка = Ложь;
		ТекущиеДанные = Элементы.ДосрочноеНазначениеПенсии.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(ТекущиеДанные, ВыбранноеЗначение);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	Оповещение = Новый ОписаниеОповещения("ВыполнитьПодключаемуюКомандуЗавершение", ЭтотОбъект, Команда);
	ПроверитьСЗапросомДальнейшегоДействия(Оповещение);
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПодключаемуюКомандуЗавершение(Результат, Команда) Экспорт
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ОбновитьПодключаемыеКоманды(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#Область ОбработчикиСобытийПроцессыОбработкиДокументов

&НаКлиенте
Процедура Подключаемый_ВыполнитьЗадачуПоОбработкеДокумента(Команда)
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначенияКлиент.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплатаКлиент");
		МодульПроцессыОбработкиДокументовЗарплата.ВыполнитьЗадачу(ЭтотОбъект, Команда, Объект)
	КонецЕсли;		
	// Конец ПроцессыОбработкиДокументов
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьЗадачуПоОбработкеДокументаОповещение(Контекст, ДополнительныеПараметры) Экспорт
	ВыполнитьЗадачуПоОбработкеДокументаНаСервере(Контекст);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры, Контекст);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьЗадачуПоОбработкеДокументаНаСервере(Контекст)
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда	
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначения.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплата");
		МодульПроцессыОбработкиДокументовЗарплата.ВыполнитьЗадачу(ЭтотОбъект, Контекст, Объект);
	КонецЕсли;		
	// Конец ПроцессыОбработкиДокументов
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КомментарийНаправившегоОткрытие(Элемент, СтандартнаяОбработка)
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда	
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначенияКлиент.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплатаКлиент");
		МодульПроцессыОбработкиДокументовЗарплата.КомментарийНаправившегоОткрытие(ЭтотОбъект, Элемент, СтандартнаяОбработка);
	КонецЕсли;		
	// Конец ПроцессыОбработкиДокументов
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КомментарийСледующемуНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда	
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначенияКлиент.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплатаКлиент");
		МодульПроцессыОбработкиДокументовЗарплата.КомментарийСледующемуНачалоВыбора(ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка);
	КонецЕсли;		
	// Конец ПроцессыОбработкиДокументов
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура Заполнить(Команда)
	
	ЗаполнитьДокументНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьВсемиСотрудниками(Команда)
	
	ЗаполнитьДокументНаСервере(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСотрудникамиСЛьготнымСтажем(Команда)
	
	ЗаполнитьДокументНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура Подбор(Команда)
	
	ПерсонифицированныйУчетКлиент.ПодобратьФизическихЛиц(Элементы.Сотрудники, ОрганизацияДляПодбора, АдресСпискаПодобранныхСотрудников());
	
КонецПроцедуры

&НаКлиенте
Процедура Проверить(Команда)
	
	ОчиститьСообщения();

	Отказ = Ложь;
	ПроверкаЗаполненияДокумента(Отказ);
	ПроверкаСтороннимиПрограммами(Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВИнтернете(Команда)
	
	Если Модифицированность Тогда
		Записать();
	КонецЕсли;

	РегламентированнаяОтчетностьКлиент.ПроверитьВИнтернете(ЭтаФорма, "ПФР");	
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьВПФР(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ОтправитьВКонтролирующийОрганЗавершение", ЭтотОбъект);	
	ПроверитьСЗапросомДальнейшегоДействия(Оповещение);	
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьВКонтролирующийОрганЗавершение(Результат, Параметры) Экспорт
	
	Если Модифицированность Тогда
		Записать();
	КонецЕсли;

	РегламентированнаяОтчетностьКлиент.ПриНажатииНаКнопкуОтправкиВКонтролирующийОрган(ЭтаФорма, "ПФР");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьНаДиск(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ЗаписатьНаДискЗавершение", ЭтотОбъект);	
	ПроверитьСЗапросомДальнейшегоДействия(Оповещение);			
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьНаДискЗавершение(Результат, Параметры) Экспорт
	
	Если Модифицированность Тогда
		Записать();
	КонецЕсли;

	ДанныеФайла = ПолучитьДанныеФайлаНаСервере(Объект.Ссылка, УникальныйИдентификатор);
	РаботаСФайламиКлиент.СохранитьФайлКак(ДанныеФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФайл(Команда)
	
	Если Модифицированность Тогда
		Записать();
	КонецЕсли;

	ДанныеФайла = ПолучитьДанныеФайлаНаСервере(Объект.Ссылка, УникальныйИдентификатор);
	Если ДанныеФайла <> Неопределено Тогда
		РаботаСФайламиКлиент.ОткрытьФайл(ДанныеФайла, Ложь);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьСЗВ_СТАЖ(Команда)
	
	ВыделенныеСтроки = Элементы.Сотрудники.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПечатьСЗВ_СТАЖЗавершение", ЭтотОбъект, ВыделенныеСтроки);	
	ПроверитьСЗапросомДальнейшегоДействия(Оповещение);			
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьСЗВ_СТАЖЗавершение(Результат, ВыделенныеСтроки) Экспорт
	
	Если Модифицированность Тогда
		Записать();
	КонецЕсли;

	СписокСотрудников = Новый Массив;
	Для Каждого ИдентификаторСтроки Из ВыделенныеСтроки Цикл
		Сотрудник = Объект.Сотрудники.НайтиПоИдентификатору(ИдентификаторСтроки).Сотрудник;
		СписокСотрудников.Добавить(Сотрудник);
	КонецЦикла;
	
	ПараметрыПечати = Новый Структура("СписокСотрудников", СписокСотрудников);
	МассивОбъектов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.Ссылка);
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати("Документ.СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ", 
		"ПФ_MXL_ФормаСЗВ_СТАЖ", МассивОбъектов, ЭтаФорма, ПараметрыПечати);
	
КонецПроцедуры

// БлокировкаИзмененияОбъектов
&НаКлиенте
Процедура Подключаемый_РазблокироватьФормуОбъекта(Команда)
	
	БлокировкаИзмененияОбъектовКлиент.РазблокироватьФормуОбъекта(ЭтотОбъект, Объект.Ссылка);
	
КонецПроцедуры
// Конец БлокировкаИзмененияОбъектов

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#Область ПанельОтправкиВКонтролирующиеОрганы

&НаКлиенте
Процедура ОбновитьОтправку(Команда)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОбновитьОтправкуИзПанелиОтправки(ЭтаФорма, "ПФР");
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаПротоколНажатие(Элемент)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьПротоколИзПанелиОтправки(ЭтаФорма, "ПФР");
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьНеотправленноеИзвещение(Команда)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОтправитьНеотправленноеИзвещениеИзПанелиОтправки(ЭтаФорма, "ПФР");
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыОтправкиНажатие(Элемент)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьСостояниеОтправкиИзПанелиОтправки(ЭтаФорма, "ПФР");
КонецПроцедуры

&НаКлиенте
Процедура КритическиеОшибкиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылка, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьКритическиеОшибкиИзПанелиОтправки(ЭтаФорма, "ПФР");
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаНаименованиеЭтапаНажатие(Элемент)
	
	ПараметрыИзменения = Новый Структура;
	ПараметрыИзменения.Вставить("Форма", ЭтаФорма);
	ПараметрыИзменения.Вставить("Организация", Объект.Организация);
	ПараметрыИзменения.Вставить("КонтролирующийОрган",
		ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ПФР"));
	
	РегламентированнаяОтчетностьКлиент.ИзменитьСтатусОтправки(ПараметрыИзменения);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьСтатусОтправки(Форма, Ссылка)
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Форма", Форма);
	СтруктураПараметров.Вставить("СсылкаНаОбъект", Ссылка);
	
	ИнтерфейсыВзаимодействияБРО.СохранитьСтатусОтправки(СтруктураПараметров);
	
КонецПроцедуры

#КонецОбласти

#Область ИнформационнаяПанельБРО

&НаСервере
Процедура ОбновитьИнформационнуюПанельБРО()
	ПараметрыОтображения = ЭлектронныйДокументооборотСФСС.ПараметрыИнформационнойПанелиБРО();
	ПараметрыОтображения.Организация = СЭДОФСС.СтраховательОрганизации(Объект.Организация);
	ПараметрыОтображения.Группа      = Элементы.ИнформационнаяПанельБРО;
	ПараметрыОтображения.Форма       = ЭтотОбъект;
	ПараметрыОтображения.ЭтоСЭДО     = Ложь;
	ЭлектронныйДокументооборотСФСС.ОбновитьИнформационнуюПанельБРО(ПараметрыОтображения);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ИнформационнаяПанельБРООбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	ЭлектронныйДокументооборотСФССКлиент.ИнформационнаяПанельБРОНажатие(
		ЭтотОбъект,
		НавигационнаяСсылкаФорматированнойСтроки, 
		СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура ПриПолученииДанныхНаСервере()
	
	ФлагБлокировкиДокумента = Объект.ДокументПринятВПФР;	
	ОтправленВПФР = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ОбъектОтправлялсяВКонтролирующиеОрганы(Объект.Ссылка);
	
	ПрочитатьЗаписиОСтаже();
	УстановитьДоступностьДанныхФормы();
	УстановитьСвойстваЭлементовФормы();
	
	ПерсонифицированныйУчет.УстановитьСвязиПараметровВыбораСотрудников(Элементы.СотрудникиСотрудник);
	
	ЗаполнитьОрганизациюДляПодбораСотрудников(Объект);
	
	ОбновитьИнформационнуюПанельБРО();
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьЗаписиОСтаже()
	
	ЗаписиОСтаже.Очистить();
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда 
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ДокументОснование", Объект.Ссылка);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗаписиОСтаже.Сотрудник КАК Сотрудник,
	               |	ЗаписиОСтаже.НомерОсновнойЗаписи КАК НомерОсновнойЗаписи,
	               |	ЗаписиОСтаже.НомерДополнительнойЗаписи КАК НомерДополнительнойЗаписи,
	               |	ЗаписиОСтаже.ДатаНачалаПериода КАК ДатаНачалаПериода,
	               |	ЗаписиОСтаже.ДатаОкончанияПериода КАК ДатаОкончанияПериода,
	               |	ЗаписиОСтаже.ОсобыеУсловияТруда КАК ОсобыеУсловияТруда,
	               |	ЗаписиОСтаже.КодПозицииСписка КАК КодПозицииСписка,
	               |	ЗаписиОСтаже.ОснованиеИсчисляемогоСтажа КАК ОснованиеИсчисляемогоСтажа,
	               |	ЗаписиОСтаже.ПервыйПараметрИсчисляемогоСтажа КАК ПервыйПараметрИсчисляемогоСтажа,
	               |	ЗаписиОСтаже.ВторойПараметрИсчисляемогоСтажа КАК ВторойПараметрИсчисляемогоСтажа,
	               |	ЗаписиОСтаже.ТретийПараметрИсчисляемогоСтажа КАК ТретийПараметрИсчисляемогоСтажа,
	               |	ЗаписиОСтаже.ОснованиеВыслугиЛет КАК ОснованиеВыслугиЛет,
	               |	ЗаписиОСтаже.ПервыйПараметрВыслугиЛет КАК ПервыйПараметрВыслугиЛет,
	               |	ЗаписиОСтаже.ВторойПараметрВыслугиЛет КАК ВторойПараметрВыслугиЛет,
	               |	ЗаписиОСтаже.ТретийПараметрВыслугиЛет КАК ТретийПараметрВыслугиЛет,
	               |	ЗаписиОСтаже.ТерриториальныеУсловия КАК ТерриториальныеУсловия,
	               |	ЗаписиОСтаже.ПараметрТерриториальныхУсловий КАК ПараметрТерриториальныхУсловий,
	               |	ЗаписиОСтаже.ЗамещениеГосударственныхМуниципальныхДолжностей КАК ЗамещениеГосударственныхМуниципальныхДолжностей,
	               |	ЗаписиОСтаже.НомерРабочегоМеста КАК НомерРабочегоМеста,
	               |	ЗаписиОСтаже.КлассПодклассУсловийТруда КАК КлассПодклассУсловийТруда,
	               |	ЗаписиОСтаже.РайонныйКоэффициент КАК РайонныйКоэффициент
	               |ИЗ
	               |	РегистрСведений.ЗаписиОСтажеСведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ КАК ЗаписиОСтаже
	               |ГДЕ
	               |	ЗаписиОСтаже.ДокументОснование = &ДокументОснование";
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	Пока Выборка.Следующий() Цикл 
		ЗаполнитьЗначенияСвойств(ЗаписиОСтаже.Добавить(), Выборка);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПослеОтправкиВПФР()
	
	ОтправленВПФР = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ОбъектОтправлялсяВКонтролирующиеОрганы(Объект.Ссылка);
	УстановитьСвойстваЭлементовФормы();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьДанныхФормы()
	
	Если Объект.ДокументПринятВПФР Тогда  
		ТолькоПросмотр = Истина;	
	КонецЕсли;
	
	УстановитьЗаголовокГруппыПрежнегоСтрахователя(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваЭлементовФормы()
	
	ЭтоЕФС1 = ПерсонифицированныйУчетКлиентСервер.ЭтоФормаЕФС1(Объект.Год);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, 
		"СтраницаДосрочноеНазначениеПенсии", 
		"Доступность", 
		(Объект.ТипСведений = Перечисления.ТипыСведенийСЗВ_СТАЖ.Исходная Или Объект.ДосрочноеНазначениеПенсии.Количество() <> 0) 
			И ПолучитьФункциональнуюОпцию("ИспользуютсяРаботыСДосрочнойПенсией", Новый Структура("Организация", Объект.Организация)));
			
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, 
		"Страницы", 
		"ОтображениеСтраниц", 
		?(ПолучитьФункциональнуюОпцию("ИспользуютсяРаботыСДосрочнойПенсией"), ОтображениеСтраницФормы.ЗакладкиСверху, ОтображениеСтраницФормы.Нет));
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, 
		"НачисленыВзносыНаОПС", 
		"Доступность", 
		Объект.ТипСведений = Перечисления.ТипыСведенийСЗВ_СТАЖ.НазначениеПенсии); 
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, 
		"НачисленыВзносыПоДТ", 
		"Доступность", 
		Объект.ТипСведений = Перечисления.ТипыСведенийСЗВ_СТАЖ.НазначениеПенсии);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, 
		"СотрудникиДатаВыходаНаПенсию", 
		"Видимость", 
		Объект.ТипСведений = Перечисления.ТипыСведенийСЗВ_СТАЖ.НазначениеПенсии);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, 
		"ГруппаТребуетсяПерезаполнение", 
		"Видимость", 
		Не ФлагБлокировкиДокумента И Не Объект.АктуальныйФорматФайла);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Организация", "ТолькоПросмотр", ОтправленВПФР);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Год", "ТолькоПросмотр", ОтправленВПФР);
	
	ЭтаФорма.Заголовок = ?(ЭтоЕФС1, "", НСтр("ru = 'Сведения о страховом стаже, СЗВ-СТАЖ';
											|en = 'Information on length of service, SZV-STAZH'") + ?(ЗначениеЗаполнено(Объект.Ссылка), 
		" " + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 от %2';
																			|en = '%1 dated %2'"), Объект.Номер, Формат(Объект.Дата, "ДЛФ=Д")), " (" + НСтр("ru = 'создание';
																																						|en = 'create'")+ ")"));
	ЭтаФорма.АвтоЗаголовок = ЭтоЕФС1;
	
	ПодменюЗаполнитьВидимость = ЭтоЕФС1 И Объект.ТипСведений = Перечисления.ТипыСведенийСЗВ_СТАЖ.Исходная;
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПодменюЗаполнить", "Видимость", ПодменюЗаполнитьВидимость);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СотрудникиЗаполнить", "Видимость", Не ПодменюЗаполнитьВидимость);
	
	ВыборТипаСведений = Новый Массив;
	Если ЭтоЕФС1 Тогда 
		ТипыСведений = Новый Массив;
		ТипыСведений.Добавить(Перечисления.ТипыСведенийСЗВ_СТАЖ.Исходная);
		ТипыСведений.Добавить(Перечисления.ТипыСведенийСЗВ_СТАЖ.НазначениеПенсии);
		ТипыСведений.Добавить(Перечисления.ТипыСведенийСЗВ_СТАЖ.НазначениеВыплатПоОСС);
		ВыборТипаСведений.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", Новый ФиксированныйМассив(ТипыСведений)));
	КонецЕсли;
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТипСведений", "ПараметрыВыбора", Новый ФиксированныйМассив(ВыборТипаСведений));
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗапрашиваемыеЗначенияПервоначальногоЗаполнения()
	
	ЗапрашиваемыеЗначения = Новый Структура;
	ЗапрашиваемыеЗначения.Вставить("Организация", "Объект.Организация");
	ЗапрашиваемыеЗначения.Вставить("Ответственный", "Объект.Ответственный");
	
	Возврат ЗапрашиваемыеЗначения;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьДокументНаСервере(ВсеСотрудники = Ложь)
	
	Объект.Сотрудники.Очистить();
	Объект.ДосрочноеНазначениеПенсии.Очистить();
	ЗаписиОСтаже.Очистить();
	
	Если НЕ УчетСтраховыхВзносов.ОрганизацияОтчитываетсяПоВзносамСамостоятельно(Объект.Организация, Объект.ОтчетныйПериод) Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Документы.СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ.СоздатьВТПерсональныеДанныеСотрудников(МенеджерВременныхТаблиц, Объект.Организация, Объект.Год, Объект.Дата);
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПерсональныеДанные.Сотрудник КАК Сотрудник,
	               |	ПерсональныеДанные.Фамилия КАК Фамилия,
	               |	ПерсональныеДанные.Имя КАК Имя,
	               |	ПерсональныеДанные.Отчество КАК Отчество,
	               |	ПерсональныеДанные.СтраховойНомерПФР КАК СтраховойНомерПФР,
	               |	ПерсональныеДанные.Гражданство КАК Гражданство,
	               |	ПерсональныеДанные.ИНН КАК ИНН,
	               |	ПерсональныеДанные.ДатаРождения КАК ДатаРождения,
	               |	ПерсональныеДанные.КодКатегории КАК КодКатегории,
	               |	ПерсональныеДанные.СотрудникУволен КАК СотрудникУволен
	               |ИЗ
	               |	ВТПерсональныеДанныеСотрудников КАК ПерсональныеДанные";
								
	Выборка = Запрос.Выполнить().Выбрать();							
	
	СписокСотрудников = Новый Массив;
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Объект.Сотрудники.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		Если Объект.ТипСведений = Перечисления.ТипыСведенийСЗВ_СТАЖ.НазначениеПенсии Тогда 
			НоваяСтрока.ДатаВыходаНаПенсию = Объект.Дата;
		КонецЕсли;
		СписокСотрудников.Добавить(Выборка.Сотрудник);
	КонецЦикла;
	
	ЗаполнитьДанныеОСтажеСотрудников(СписокСотрудников);
	
	Если Не ВсеСотрудники Тогда 
		УдалитьСотрудниковБезЛьготногоСтажа(СписокСотрудников);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеСотрудников(СписокСотрудников)
	
	Год = ?(ЗначениеЗаполнено(Объект.Год), Объект.Год, Год(ТекущаяДатаСеанса()));
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Документы.СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ.СоздатьВТПерсональныеДанныеСотрудников(МенеджерВременныхТаблиц, Объект.Организация, Год, Объект.Дата, СписокСотрудников);
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПерсональныеДанные.Сотрудник КАК Сотрудник,
	               |	ПерсональныеДанные.Фамилия КАК Фамилия,
	               |	ПерсональныеДанные.Имя КАК Имя,
	               |	ПерсональныеДанные.Отчество КАК Отчество,
	               |	ПерсональныеДанные.СтраховойНомерПФР КАК СтраховойНомерПФР,
	               |	ПерсональныеДанные.Гражданство КАК Гражданство,
	               |	ПерсональныеДанные.ИНН КАК ИНН,
	               |	ПерсональныеДанные.ДатаРождения КАК ДатаРождения,
	               |	ПерсональныеДанные.КодКатегории КАК КодКатегории,
	               |	ПерсональныеДанные.СотрудникУволен КАК СотрудникУволен
	               |ИЗ
	               |	ВТПерсональныеДанныеСотрудников КАК ПерсональныеДанные";
								
	Выборка = Запрос.Выполнить().Выбрать();							
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Объект.Сотрудники.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		Если Объект.ТипСведений = Перечисления.ТипыСведенийСЗВ_СТАЖ.НазначениеПенсии Тогда 
			НоваяСтрока.ДатаВыходаНаПенсию = Объект.Дата;
		КонецЕсли;
	КонецЦикла;
	
	ЗаполнитьДанныеОСтажеСотрудников(СписокСотрудников);
	ЗаполнитьДанныеОФактеНачисленияВзносов();
	
КонецПроцедуры

&НаСервере
Процедура СотрудникиСотрудникПриИзмененииНаСервере()
	
	СтрокаСотрудника = Объект.Сотрудники.НайтиПоИдентификатору(Элементы.Сотрудники.ТекущаяСтрока);
	
	Год = ?(ЗначениеЗаполнено(Объект.Год), Объект.Год, Год(ТекущаяДатаСеанса()));
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Документы.СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ.СоздатьВТПерсональныеДанныеСотрудников(МенеджерВременныхТаблиц, Объект.Организация, Год, Объект.Дата, СтрокаСотрудника.Сотрудник);
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПерсональныеДанные.Сотрудник КАК Сотрудник,
	               |	ПерсональныеДанные.Фамилия КАК Фамилия,
	               |	ПерсональныеДанные.Имя КАК Имя,
	               |	ПерсональныеДанные.Отчество КАК Отчество,
	               |	ПерсональныеДанные.СтраховойНомерПФР КАК СтраховойНомерПФР,
	               |	ПерсональныеДанные.Гражданство КАК Гражданство,
	               |	ПерсональныеДанные.ИНН КАК ИНН,
	               |	ПерсональныеДанные.ДатаРождения КАК ДатаРождения,
	               |	ПерсональныеДанные.КодКатегории КАК КодКатегории,
	               |	ПерсональныеДанные.СотрудникУволен КАК СотрудникУволен
	               |ИЗ
	               |	ВТПерсональныеДанныеСотрудников КАК ПерсональныеДанные";
								
	Выборка = Запрос.Выполнить().Выбрать();							
	
	СтруктураПоиска = Новый Структура("Сотрудник");
	
	Пока Выборка.Следующий() Цикл
		СтруктураПоиска.Сотрудник = Выборка.Сотрудник;
		НайденныеСтроки = Объект.Сотрудники.НайтиСтроки(СтруктураПоиска);
		Если НайденныеСтроки.Количество() <> 0 Тогда 
			СтрокаСотрудника = НайденныеСтроки[0];
			ЗаполнитьЗначенияСвойств(СтрокаСотрудника, Выборка);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры	

&НаСервере
Процедура СотрудникиДатаВыходаНаПенсиюПриИзмененииНаСервере()
	
	ПерсонифицированныйУчетКлиентСервер.ДокументыРедактированияСтажаСотрудникиПередУдалением(Элементы.Сотрудники.ВыделенныеСтроки, Объект.Сотрудники, ЗаписиОСтаже);
	
КонецПроцедуры

&НаСервере
Процедура СотрудникиПриОкончанииРедактированияНаСервере(Сотрудник)
	
	ДанныеОСтаже = ЗаписиОСтаже.НайтиСтроки(Новый Структура("Сотрудник", Сотрудник));
	Если ДанныеОСтаже.Количество() > 0 Тогда 
		Возврат;
	КонецЕсли;
	
	ЗаполнитьДанныеОСтажеСотрудников(Сотрудник);
	ЗаполнитьДанныеОФактеНачисленияВзносов();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеОСтажеСотрудников(Сотрудники)
	
	СписокСотрудников = ?(ТипЗнч(Сотрудники) = Тип("Массив"), Сотрудники, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудники));
	Год = ?(ЗначениеЗаполнено(Объект.Год), Объект.Год, Год(ТекущаяДатаСеанса()));
	
	ДатыВыходаНаПенсию = Неопределено;
	Если Объект.ТипСведений = Перечисления.ТипыСведенийСЗВ_СТАЖ.НазначениеПенсии Тогда 
		ДатыВыходаНаПенсию = Новый Соответствие;
		Для Каждого СтрокаСотрудника Из Объект.Сотрудники Цикл 
			Если СписокСотрудников.Найти(СтрокаСотрудника.Сотрудник) <> Неопределено Тогда 
				ДатыВыходаНаПенсию.Вставить(СтрокаСотрудника.Сотрудник, СтрокаСотрудника.ДатаВыходаНаПенсию);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ДатаВыплатПоОСС = Неопределено;
	Если Объект.ТипСведений = Перечисления.ТипыСведенийСЗВ_СТАЖ.НазначениеВыплатПоОСС Тогда 
		ДатаВыплатПоОСС = НачалоДня(Объект.Дата);
	КонецЕсли;
	
	ПерсонифицированныйУчет.ЗаполнитьДанныеОСтажеСотрудников(
		ЗаписиОСтаже, Объект.Организация, СписокСотрудников, Год, Объект.Ссылка, ДатыВыходаНаПенсию, ДатаВыплатПоОСС);
	
	ЗаполнитьДанныеДосрочногоНазначенияПенсии();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеДосрочногоНазначенияПенсии()
	
	Объект.ДосрочноеНазначениеПенсии.Очистить();
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользуютсяРаботыСДосрочнойПенсией", Новый Структура("Организация", Объект.Организация)) Тогда 
		Возврат;
	КонецЕсли;
	
	Если Не Объект.ТипСведений = Перечисления.ТипыСведенийСЗВ_СТАЖ.Исходная Тогда 
		Возврат;
	КонецЕсли;
	
	Год = ?(ЗначениеЗаполнено(Объект.Год), Объект.Год, Год(ТекущаяДатаСеанса()));
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ПерсонифицированныйУчет.СоздатьВТДосрочноеНазначениеПенсииДляСЗВ_СТАЖ(МенеджерВременныхТаблиц, Объект.Организация, Год, ЗаписиОСтаже);
		
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДосрочноеНазначениеПенсии.*
		|ИЗ
		|	ВТДосрочноеНазначениеПенсии КАК ДосрочноеНазначениеПенсии
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДосрочноеНазначениеПенсии.Подразделение,
		|	ДосрочноеНазначениеПенсии.Должность";
	
	ВсегоРаботающихДНП = 0;
	ВсегоРабочихМестДНП = 0;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл 
		НоваяСтрока = Объект.ДосрочноеНазначениеПенсии.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		ВсегоРаботающихДНП = ВсегоРаботающихДНП + Выборка.КоличествоРаботающих;
		ВсегоРабочихМестДНП = ВсегоРабочихМестДНП + Выборка.КоличествоРабочихМест;
	КонецЦикла;
	
	Объект.ВсегоРаботающихДНП = ВсегоРаботающихДНП;
	Объект.ВсегоРабочихМестДНП = ВсегоРабочихМестДНП;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеОФактеНачисленияВзносов(УдаляемыеСтроки = Неопределено)
	
	Если Объект.ТипСведений <> Перечисления.ТипыСведенийСЗВ_СТАЖ.НазначениеПенсии Тогда 
		Объект.НачисленыВзносыНаОПС = Ложь;
		Объект.НачисленыВзносыПоДТ = Ложь;
		Возврат;
	КонецЕсли;
	
	УдаляемыеСотрудники = Новый Соответствие;
	Если УдаляемыеСтроки <> Неопределено Тогда 
		Для Каждого Идентификатор Из УдаляемыеСтроки Цикл
			СтрокаСотрудника = Объект.Сотрудники.НайтиПоИдентификатору(Идентификатор);
			Если СтрокаСотрудника <> Неопределено Тогда
				УдаляемыеСотрудники.Вставить(СтрокаСотрудника.Сотрудник, Истина);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ТаблицаСотрудников = Новый ТаблицаЗначений;
	ТаблицаСотрудников.Колонки.Добавить("ФизическоеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	ТаблицаСотрудников.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	
	ДатаАктуальности = Объект.Дата;
	Если Не ЗначениеЗаполнено(ДатаАктуальности) Тогда
		ДатаАктуальности = ТекущаяДатаСеанса();
	КонецЕсли;	
	
	Для Каждого СтрокаКоллекции Из Объект.Сотрудники Цикл
		Если УдаляемыеСтроки <> Неопределено И УдаляемыеСотрудники[СтрокаКоллекции.Сотрудник] <> Неопределено Тогда 
			Продолжить;
		КонецЕсли;
		СтрокаТаблицы = ТаблицаСотрудников.Добавить();
		СтрокаТаблицы.ФизическоеЛицо = СтрокаКоллекции.Сотрудник;
		СтрокаТаблицы.Период = Макс(ДатаАктуальности, СтрокаКоллекции.ДатаВыходаНаПенсию);
	КонецЦикла;
	
	Если ТаблицаСотрудников.Количество() = 0 Тогда 
		Объект.НачисленыВзносыНаОПС = Ложь;
		Объект.НачисленыВзносыПоДТ = Ложь;
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ТаблицаСотрудников", ТаблицаСотрудников);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТаблицаСотрудников.ФизическоеЛицо,
	               |	ТаблицаСотрудников.Период
	               |ПОМЕСТИТЬ ВТФизическиеЛица
	               |ИЗ
	               |	&ТаблицаСотрудников КАК ТаблицаСотрудников";
	
	Запрос.Выполнить();
	
	ОтчетныйПериод = ?(ЗначениеЗаполнено(Объект.Год), Дата(Объект.Год, 1, 1), НачалоГода(ТекущаяДатаСеанса()));
	
	УстановитьПривилегированныйРежим(Истина);
	УчетСтраховыхВзносов.СформироватьВТДанныеОФактеНачисленияВзносовВПФР(
		Запрос.МенеджерВременныхТаблиц,
		Объект.Организация,
		ОтчетныйПериод,
		КонецГода(ОтчетныйПериод));
	УстановитьПривилегированныйРежим(Ложь);
		
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	               |	ИСТИНА КАК ЗначениеИстина
	               |ИЗ
	               |	ВТДанныеОФактеНачисленияВзносов КАК ДанныеОФактеНачисленияВзносов
	               |ГДЕ
	               |	ДанныеОФактеНачисленияВзносов.НачисленоНаОПС
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ ПЕРВЫЕ 1
	               |	ИСТИНА КАК ЗначениеИстина
	               |ИЗ
	               |	ВТДанныеОФактеНачисленияВзносов КАК ДанныеОФактеНачисленияВзносов
	               |ГДЕ
	               |	ДанныеОФактеНачисленияВзносов.НачисленоПоДополнительнымТарифам";
				   
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	Объект.НачисленыВзносыНаОПС = Не РезультатыЗапроса[0].Пустой();	
	Объект.НачисленыВзносыПоДТ = Не РезультатыЗапроса[1].Пустой();	
	
КонецПроцедуры

&НаСервере
Процедура УдалитьСотрудниковБезЛьготногоСтажа(СписокСотрудников)
	
	Если Не ПерсонифицированныйУчетКлиентСервер.ЭтоФормаЕФС1(Объект.Год) Тогда 
		Возврат;
	КонецЕсли;
	
	Если Объект.ТипСведений = Перечисления.ТипыСведенийСЗВ_СТАЖ.НазначениеПенсии
		Или Объект.ТипСведений = Перечисления.ТипыСведенийСЗВ_СТАЖ.НазначениеВыплатПоОСС Тогда 
		Возврат;
	КонецЕсли;
	
	ПараметрыСтажа = Новый Массив; 
	ПараметрыСтажа.Добавить(Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.ВРНЕТРУД);
	ПараметрыСтажа.Добавить(Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.ДЕКРЕТ);
	ПараметрыСтажа.Добавить(Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.ДЕТИ);
	ПараметрыСтажа.Добавить(Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.ДИСПР);
	ПараметрыСтажа.Добавить(Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.ДЛОТПУСК);
	ПараметрыСтажа.Добавить(Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.ДОГОВОР);
	ПараметрыСтажа.Добавить(Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.ДОПВЫХ);
	ПараметрыСтажа.Добавить(Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.КВАЛИФОБ);
	ПараметрыСтажа.Добавить(Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.НЕОПЛАВТ);
	ПараметрыСтажа.Добавить(Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.НЕОПЛДОГ);
	ПараметрыСтажа.Добавить(Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.СДКРОВ);
	ПараметрыСтажа.Добавить(Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.УЧОТПУСК);
	ПараметрыСтажа.Добавить(Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.ЧАЭС);
	
	СтруктураПолей = СтруктураПолейЗаписиОСтаже();
	СтруктураПолей.Удалить("НомерОсновнойЗаписи");
	СтруктураПолей.Удалить("НомерДополнительнойЗаписи");
	СтруктураПолей.Удалить("ДатаНачалаПериода");
	СтруктураПолей.Удалить("ДатаОкончанияПериода");
	СтруктураПолей.Удалить("ИдентификаторИсходнойСтроки");
	СтруктураПолей.Удалить("НомерРабочегоМеста");
	СтруктураПолей.Удалить("КлассПодклассУсловийТруда");
	СотрудникиСЛьготнымСтажем = Новый Массив;
	
	Для Каждого ДанныеОСтаже Из ЗаписиОСтаже Цикл
		Для Каждого КлючИЗначение Из СтруктураПолей Цикл 
			Если ЗначениеЗаполнено(ДанныеОСтаже[КлючИЗначение.Ключ]) Тогда 
				Если КлючИЗначение.Ключ <> "ТретийПараметрИсчисляемогоСтажа"
					Или ПараметрыСтажа.Найти(ДанныеОСтаже[КлючИЗначение.Ключ]) = Неопределено Тогда 
					СотрудникиСЛьготнымСтажем.Добавить(ДанныеОСтаже.Сотрудник);
					Прервать;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Если Объект.ТипСведений = Перечисления.ТипыСведенийСЗВ_СТАЖ.Исходная Тогда 
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Год", Объект.Год);
		Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖСотрудники.Сотрудник КАК Сотрудник
			|ИЗ
			|	Документ.СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ.Сотрудники КАК СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖСотрудники
			|ГДЕ
			|	СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖСотрудники.Ссылка.ТипСведений В (ЗНАЧЕНИЕ(Перечисление.ТипыСведенийСЗВ_СТАЖ.НазначениеПенсии), ЗНАЧЕНИЕ(Перечисление.ТипыСведенийСЗВ_СТАЖ.НазначениеВыплатПоОСС))
			|	И СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖСотрудники.Ссылка.Проведен
			|	И СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖСотрудники.Ссылка.Год = &Год";
		Пенсионеры = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Сотрудник");
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СотрудникиСЛьготнымСтажем, Пенсионеры, Истина);	
	КонецЕсли;
	
	СотрудникиКУдалению = ОбщегоНазначенияКлиентСервер.СократитьМассив(СписокСотрудников, СотрудникиСЛьготнымСтажем);
	Для Каждого Сотрудник Из СотрудникиКУдалению Цикл 
		Отбор = Новый Структура("Сотрудник", Сотрудник);
		НайденныеСтроки = Объект.Сотрудники.НайтиСтроки(Отбор);
		Для Каждого СтрокаСотрудника Из НайденныеСтроки Цикл 
			Объект.Сотрудники.Удалить(СтрокаСотрудника);
		КонецЦикла;
		УдаляемыеСтрокиТаблицы = ЗаписиОСтаже.НайтиСтроки(Отбор);
		Для Каждого УдаляемаяСтрока Из УдаляемыеСтрокиТаблицы Цикл
			ЗаписиОСтаже.Удалить(ЗаписиОСтаже.Индекс(УдаляемаяСтрока));
		КонецЦикла;	
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	Объект.Сотрудники.Очистить();
	Объект.ДосрочноеНазначениеПенсии.Очистить();
	ЗаписиОСтаже.Очистить();
	
	ПерсонифицированныйУчет.ЗаполнитьДанныеРуководителя(Объект);
	УстановитьСвойстваЭлементовФормы();
	ЗаполнитьОрганизациюДляПодбораСотрудников(Объект);
	
	ОбновитьИнформационнуюПанельБРО();
	
КонецПроцедуры

&НаСервере
Процедура ТипСведенийПриИзмененииНаСервере()
	
	ЗаполнитьДатуНазначенияПенсии();
	ЗаполнитьДанныеОФактеНачисленияВзносов();
	
	ЗаписиОСтаже.Очистить();
	ЗаполнитьДанныеОСтажеСотрудников(ОбщегоНазначения.ВыгрузитьКолонку(Объект.Сотрудники, "Сотрудник"));
	
	УстановитьСвойстваЭлементовФормы();
	
КонецПроцедуры

&НаСервере
Процедура ФлагБлокировкиДокументаПриИзмененииНаСервере()
	
	Модифицированность = Истина;
	Объект.ДокументПринятВПФР = ФлагБлокировкиДокумента;
	Если Не ФлагБлокировкиДокумента Тогда
		ТолькоПросмотр = Ложь;
	КонецЕсли;	
	
	УстановитьСвойстваЭлементовФормы();
	
КонецПроцедуры

&НаСервере
Процедура СотрудникиПередУдалениемНаСервере()
	
	ПерсонифицированныйУчетКлиентСервер.ДокументыРедактированияСтажаСотрудникиПередУдалением(Элементы.Сотрудники.ВыделенныеСтроки, Объект.Сотрудники, ЗаписиОСтаже);
	ЗаполнитьДанныеОФактеНачисленияВзносов(Элементы.Сотрудники.ВыделенныеСтроки);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьОтчетныйПериод(Форма)
	
	Объект = Форма.Объект;
	Если Не ЗначениеЗаполнено(Объект.Год) Тогда 
		Возврат;
	КонецЕсли;
	
	Объект.ОтчетныйПериод = Дата(Объект.Год, 1, 1);
	Объект.ОкончаниеОтчетногоПериода = КонецГода(Объект.ОтчетныйПериод);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДатуНазначенияПенсии()
	
	Если Объект.ТипСведений <> Перечисления.ТипыСведенийСЗВ_СТАЖ.НазначениеПенсии Тогда 
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаСотрудника Из Объект.Сотрудники Цикл 
		СтрокаСотрудника.ДатаВыходаНаПенсию = Объект.Дата;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуРедактированияКарточкиДокумента()
	
	ДанныеТекущейСтроки = Элементы.Сотрудники.ТекущиеДанные;
	ДанныеШапкиТекущегоДокумента = Объект;
	
	Если ДанныеТекущейСтроки <> Неопределено Тогда	
		
		ДанныеТекущегоДокументаПоСотрудникуВоВременноеХранилище();
		
		ПараметрыОткрытияФормы = Новый Структура;
		ПараметрыОткрытияФормы.Вставить("АдресВоВременномХранилище", АдресДанныхТекущегоДокументаВХранилище);
		ПараметрыОткрытияФормы.Вставить("РедактируемыйДокументСсылка", ДанныеШапкиТекущегоДокумента.Ссылка);
		ПараметрыОткрытияФормы.Вставить("Сотрудник", ДанныеТекущейСтроки.Сотрудник);
		ПараметрыОткрытияФормы.Вставить("ДатаВыходаНаПенсию", ДанныеТекущейСтроки.ДатаВыходаНаПенсию);
		ПараметрыОткрытияФормы.Вставить("Организация", ДанныеШапкиТекущегоДокумента.Организация);
		ПараметрыОткрытияФормы.Вставить("Год", Объект.Год);
		ПараметрыОткрытияФормы.Вставить("ИсходныйНомерСтроки", 0);
		ПараметрыОткрытияФормы.Вставить("ТолькоПросмотр", ТолькоПросмотр);
		ПараметрыОткрытияФормы.Вставить("НеОтображатьОшибки", Истина);
		
		ОткрытьФорму("Документ.СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ.Форма.ФормаРедактированияСтажа", ПараметрыОткрытияФормы, ЭтаФорма);	
		
	КонецЕсли;
	
КонецПроцедуры	

&НаСервере
Процедура ДанныеТекущегоДокументаПоСотрудникуВоВременноеХранилище()
	
	Если Элементы.Сотрудники.ТекущаяСтрока = Неопределено Тогда
		АдресДанныхТекущегоДокументаВХранилище = "";
		Возврат;
	КонецЕсли;	
	
	ДанныеТекущейСтрокиПоСотруднику = Объект.Сотрудники.НайтиПоИдентификатору(Элементы.Сотрудники.ТекущаяСтрока);
	
	Если ДанныеТекущейСтрокиПоСотруднику = Неопределено Тогда
		АдресДанныхТекущегоДокументаВХранилище = "";
		Возврат;
	КонецЕсли;
	
	ДанныеСотрудника = Новый Структура;
	ДанныеСотрудника.Вставить("Сотрудник", ДанныеТекущейСтрокиПоСотруднику.Сотрудник);
	ДанныеСотрудника.Вставить("СтраховойНомерПФР", ДанныеТекущейСтрокиПоСотруднику.СтраховойНомерПФР);
	ДанныеСотрудника.Вставить("Фамилия", ДанныеТекущейСтрокиПоСотруднику.Фамилия);
	ДанныеСотрудника.Вставить("Имя", ДанныеТекущейСтрокиПоСотруднику.Имя);
	ДанныеСотрудника.Вставить("Отчество", ДанныеТекущейСтрокиПоСотруднику.Отчество);
	ДанныеСотрудника.Вставить("СотрудникУволен", ДанныеТекущейСтрокиПоСотруднику.СотрудникУволен);
	ДанныеСотрудника.Вставить("ФиксСтаж", ДанныеТекущейСтрокиПоСотруднику.ФиксСтаж);
	ДанныеСотрудника.Вставить("ДатаРождения", ДанныеТекущейСтрокиПоСотруднику.ДатаРождения);
	ДанныеСотрудника.Вставить("ИНН", ДанныеТекущейСтрокиПоСотруднику.ИНН);
	ДанныеСотрудника.Вставить("КодКатегории", ДанныеТекущейСтрокиПоСотруднику.КодКатегории);
	ДанныеСотрудника.Вставить("Гражданство", ДанныеТекущейСтрокиПоСотруднику.Гражданство);
	ДанныеСотрудника.Вставить("ЗаписиОСтаже", Новый Массив);
	ДанныеСотрудника.Вставить("ИсходныйНомерСтроки", ДанныеТекущейСтрокиПоСотруднику.ИсходныйНомерСтроки);
	
	ЗначенияРеквизитовХраненияОшибок = ПерсонифицированныйУчетКлиентСервер.ЗначенияРеквизитовХраненияОшибокВСтруктуру(
											ЭтаФорма, 
											ДанныеТекущейСтрокиПоСотруднику,
											"Объект.Сотрудники");
											
	ДанныеСотрудника.Вставить("ЗначенияРеквизитовХраненияОшибок", ЗначенияРеквизитовХраненияОшибок);	
	
	СтруктураПоиска = Новый Структура("Сотрудник", ДанныеТекущейСтрокиПоСотруднику.Сотрудник);
	
	СтрокиЗаписиОСтаже = ЗаписиОСтаже.НайтиСтроки(СтруктураПоиска);
	
	Для Каждого СтрокаСтаж Из СтрокиЗаписиОСтаже Цикл
		СтруктураПолейЗаписиОСтаже = СтруктураПолейЗаписиОСтаже();
		ЗаполнитьЗначенияСвойств(СтруктураПолейЗаписиОСтаже, СтрокаСтаж);
		СтруктураПолейЗаписиОСтаже.ИдентификаторИсходнойСтроки = СтрокаСтаж.ПолучитьИдентификатор(); 
		
		ЗначенияРеквизитовХраненияОшибок = ПерсонифицированныйУчетКлиентСервер.ЗначенияРеквизитовХраненияОшибокВСтруктуру(
												ЭтаФорма, 
												СтрокаСтаж,
												"ЗаписиОСтаже");	
		
		ДанныеСотрудника.ЗаписиОСтаже.Добавить(СтруктураПолейЗаписиОСтаже);
	КонецЦикла;	
	
	Если ЗначениеЗаполнено(АдресДанныхТекущегоДокументаВХранилище) Тогда
		ПоместитьВоВременноеХранилище(ДанныеСотрудника, АдресДанныхТекущегоДокументаВХранилище);	
	Иначе	
		АдресДанныхТекущегоДокументаВХранилище = ПоместитьВоВременноеХранилище(ДанныеСотрудника, УникальныйИдентификатор);
	КонецЕсли;	
	
КонецПроцедуры	

&НаСервере
Функция АдресСпискаПодобранныхСотрудников()
	
	Возврат ПоместитьВоВременноеХранилище(Объект.Сотрудники.Выгрузить(,"Сотрудник").ВыгрузитьКолонку("Сотрудник"), УникальныйИдентификатор);
	
КонецФункции

&НаСервере
Функция СтруктураПолейЗаписиОСтаже()
	
	СтруктураПолей = Новый Структура;
	СтруктураПолей.Вставить("НомерОсновнойЗаписи");
	СтруктураПолей.Вставить("НомерДополнительнойЗаписи");
	СтруктураПолей.Вставить("ДатаНачалаПериода");
	СтруктураПолей.Вставить("ДатаОкончанияПериода");
	СтруктураПолей.Вставить("ОсобыеУсловияТруда");
	СтруктураПолей.Вставить("КодПозицииСписка");
	СтруктураПолей.Вставить("ОснованиеИсчисляемогоСтажа");
	СтруктураПолей.Вставить("ПервыйПараметрИсчисляемогоСтажа");
	СтруктураПолей.Вставить("ВторойПараметрИсчисляемогоСтажа");
	СтруктураПолей.Вставить("ТретийПараметрИсчисляемогоСтажа");
	СтруктураПолей.Вставить("ОснованиеВыслугиЛет");
	СтруктураПолей.Вставить("ПервыйПараметрВыслугиЛет");
	СтруктураПолей.Вставить("ВторойПараметрВыслугиЛет");
	СтруктураПолей.Вставить("ТретийПараметрВыслугиЛет");
	СтруктураПолей.Вставить("ТерриториальныеУсловия");
	СтруктураПолей.Вставить("ПараметрТерриториальныхУсловий");
	СтруктураПолей.Вставить("ЗамещениеГосударственныхМуниципальныхДолжностей");
	СтруктураПолей.Вставить("НомерРабочегоМеста");
	СтруктураПолей.Вставить("КлассПодклассУсловийТруда");
	СтруктураПолей.Вставить("РайонныйКоэффициент");
	СтруктураПолей.Вставить("ИдентификаторИсходнойСтроки");

	Возврат СтруктураПолей;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьЗаписиОСтажеПриКопировании(ЗначениеКопирования)
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ДокументОснование", ЗначениеКопирования);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗаписиОСтаже.*
	               |ИЗ
	               |	РегистрСведений.ЗаписиОСтажеСведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ КАК ЗаписиОСтаже
	               |ГДЕ
	               |	ЗаписиОСтаже.ДокументОснование = &ДокументОснование";
	
	УстановитьПривилегированныйРежим(Истина);
	ЗаписиОСтаже.Загрузить(Запрос.Выполнить().Выгрузить());
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаСервере
Функция ОписаниеЭлементовСИндикациейОшибок() Экспорт
	ОписаниеЭлементовИндикацииОшибок = Новый Соответствие;	
	Возврат ОписаниеЭлементовИндикацииОшибок;
КонецФункции	

&НаСервере
Процедура ЗаполнитьОрганизациюДляПодбораСотрудников(ТекущийОбъект)
	ОрганизацияДляПодбора.Очистить();
	Если УчетСтраховыхВзносов.ОрганизацияОтчитываетсяПоВзносамСамостоятельно(ТекущийОбъект.Организация, ТекущийОбъект.ОтчетныйПериод) Тогда
		Для каждого Организация Из УчетСтраховыхВзносов.ОрганизацииДляКонсолидацииОтчетовПоВзносам(ТекущийОбъект.Организация, ТекущийОбъект.ОтчетныйПериод) Цикл
			ОрганизацияДляПодбора.Добавить(Организация)
		КонецЦикла;
	Иначе 	
		ОрганизацияДляПодбора.Добавить(ТекущийОбъект.Организация)
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииДанныхДокументаПоСотруднику(АдресВоВременномХранилище)
	
	ДанныеТекущегоДокументаПоСотрудникуВДанныеФормы(АдресВоВременномХранилище);
	
КонецПроцедуры	

&НаСервере
Процедура ДанныеТекущегоДокументаПоСотрудникуВДанныеФормы(АдресВоВременномХранилище)
	
	ДанныеШапкиДокумента = Объект;
	
	ДанныеТекущегоДокумента = ПолучитьИзВременногоХранилища(АдресВоВременномХранилище);
	
	Если ДанныеТекущегоДокумента = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеТекущейСтрокиПоСотруднику = Неопределено;
	НайденныеСтроки = Объект.Сотрудники.НайтиСтроки(Новый Структура("Сотрудник", ДанныеТекущегоДокумента.Сотрудник));
		
	Если НайденныеСтроки.Количество() > 0 Тогда
		ДанныеТекущейСтрокиПоСотруднику = НайденныеСтроки[0];
		Если ДанныеТекущейСтрокиПоСотруднику.Сотрудник <> ДанныеТекущегоДокумента.Сотрудник Тогда
			ДанныеТекущейСтрокиПоСотруднику = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	Если ДанныеТекущейСтрокиПоСотруднику = Неопределено  Тогда
		ВызватьИсключение НСтр("ru = 'В текущем документе не найдены данные по редактируемому сотруднику.';
								|en = 'Data on the edited employee is not found in the current document.'");
	КонецЕсли;
	
	ДанныеТекущейСтрокиПоСотруднику = Объект.Сотрудники.НайтиПоИдентификатору(Элементы.Сотрудники.ТекущаяСтрока);
	ЗаполнитьЗначенияСвойств(ДанныеТекущейСтрокиПоСотруднику, ДанныеТекущегоДокумента);
		
	СтруктураПоиска = Новый Структура("Сотрудник", ДанныеТекущейСтрокиПоСотруднику.Сотрудник);
	
	СтрокиСтажа = ЗаписиОСтаже.НайтиСтроки(СтруктураПоиска);
	Для Каждого СтрокаСтажСотрудника Из СтрокиСтажа Цикл
		ЗаписиОСтаже.Удалить(ЗаписиОСтаже.Индекс(СтрокаСтажСотрудника));
	КонецЦикла;
	
	СтрокиСтажаПоСотруднику = Новый Массив;
	Для Каждого СтрокаСтаж Из ДанныеТекущегоДокумента.ЗаписиОСтаже Цикл
		СтрокаСтажОбъекта = ЗаписиОСтаже.Добавить();
		СтрокаСтажОбъекта.Сотрудник = ДанныеТекущейСтрокиПоСотруднику.Сотрудник;
		ЗаполнитьЗначенияСвойств(СтрокаСтажОбъекта, СтрокаСтаж);
		СтрокиСтажаПоСотруднику.Добавить(СтрокаСтажОбъекта);
	КонецЦикла;
	
	ПерсонифицированныйУчетКлиентСервер.ВыполнитьНумерациюЗаписейОСтаже(СтрокиСтажаПоСотруднику);
		
	Если ДанныеТекущегоДокумента.Модифицированность Тогда
		Модифицированность = Истина;
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ПроверкаЗаполненияДокумента(Отказ = Ложь)
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ДокументОбъект.ДополнительныеСвойства.Вставить("НеПроверятьДанныеСотрудников");
	ДокументОбъект.ПроверитьДанныеДокумента(Отказ);
	
	ПерсонифицированныйУчет.ПроверитьДанныеСотрудников(ДокументОбъект.Сотрудники, ДокументОбъект.ОтчетныйПериод, ДокументОбъект.ОкончаниеОтчетногоПериода, ЗаписиОСтаже, Отказ);
	
	Если ПерсонифицированныйУчетКлиентСервер.ЭтоФормаЕФС1(Объект.Год) Тогда 
		ПерсонифицированныйУчет.ПроверитьВзносыПоДополнительнымТарифамСотрудников(ДокументОбъект.Организация, ДокументОбъект.Сотрудники, ДокументОбъект.ОтчетныйПериод, ДокументОбъект.ОкончаниеОтчетногоПериода, ЗаписиОСтаже, Отказ);
	КонецЕсли;
	
КонецПроцедуры	

&НаКлиенте
Процедура ПроверитьСЗапросомДальнейшегоДействия(ОповещениеЗавершения = Неопределено)
	
	ОчиститьСообщения();
	
	ДополнительныеПараметры = Новый Структура("ОповещениеЗавершения", ОповещениеЗавершения);
	
	Если Не Объект.Проведен Или Модифицированность Тогда 
		ТекстВопроса = НСтр("ru = 'Для выполнения команды необходимо предварительно провести документ. Выполнить проведение документа и продолжить?';
							|en = 'Cannot run the command for unposted documents. Do you want to post the document and continue?'");
		Оповещение = Новый ОписаниеОповещения("ПроверитьСЗапросомДальнейшегоДействияПродолжение", ЭтотОбъект, ДополнительныеПараметры);
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Продолжить';
													|en = 'Continue'"));
		Кнопки.Добавить(КодВозвратаДиалога.Отмена);
		ПоказатьВопрос(Оповещение, ТекстВопроса, Кнопки);
	Иначе 
		ПроверитьСЗапросомДальнейшегоДействияПродолжение(КодВозвратаДиалога.Да, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры	

&НаКлиенте
Процедура ПроверитьСЗапросомДальнейшегоДействияПродолжение(Ответ, ДополнительныеПараметры) Экспорт 
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;			
	
	Если Не Объект.Проведен Или Модифицированность Тогда
		Если Не Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение)) Тогда 
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Отказ = Ложь;
	ПроверкаЗаполненияДокумента(Отказ);	
	
	Если Отказ Тогда 
		ТекстВопроса = НСтр("ru = 'В комплекте обнаружены ошибки.
							|Продолжить (не рекомендуется)?';
							|en = 'Errors were detected in the set.
							|Continue (not recommended)?'");
							
		Оповещение = Новый ОписаниеОповещения("ПроверитьСЗапросомДальнейшегоДействияЗавершение", ЭтотОбъект, ДополнительныеПараметры);					
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет, НСтр("ru = 'Предупреждение.';
																											|en = 'Warning.'"));
	Иначе 
		ПроверитьСЗапросомДальнейшегоДействияЗавершение(КодВозвратаДиалога.Да, ДополнительныеПараметры);				
	КонецЕсли;	
	
КонецПроцедуры	

&НаКлиенте
Процедура ПроверитьСЗапросомДальнейшегоДействияЗавершение(Ответ, ДополнительныеПараметры) Экспорт 
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;			
	
	Если ДополнительныеПараметры.ОповещениеЗавершения <> Неопределено Тогда 
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеЗавершения);
	КонецЕсли;
	
КонецПроцедуры	

&НаКлиенте
Процедура ПроверкаСтороннимиПрограммами(Отказ)
	
	Если Отказ Тогда
		ТекстВопроса = НСтр("ru = 'При проверке встроенной проверкой обнаружены ошибки.
		|Выполнить проверку сторонними программами?';
		|en = 'Errors occurred while checking with the integrated check.
		|Check with third-party applications?'")
	Иначе	
		ТекстВопроса = НСтр("ru = 'При проверке встроенной проверкой ошибок не обнаружено.
		|Выполнить проверку сторонними программами?';
		|en = 'Errors are not detected while checking with the integrated check.
		|Check with third-party applications?'");
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПроверкаСтороннимиПрограммамиЗавершение", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаСтороннимиПрограммамиЗавершение(Ответ, ДополнительныеПараметры) Экспорт 
	
	Если Ответ = КодВозвратаДиалога.Да Тогда 
		ПроверитьСтороннимиПрограммами();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьСтороннимиПрограммами()
	
	Если Модифицированность Тогда
		Записать();
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	
	ПроверяемыеОбъекты = Новый Массив;
	ПроверяемыеОбъекты.Добавить(Объект.Ссылка);
	
	ПараметрыОткрытия.Вставить("СсылкиНаПроверяемыеОбъекты", ПроверяемыеОбъекты);
	
	ОткрытьФорму("ОбщаяФорма.ПроверкаФайловОтчетностиПерсУчетаПФР", ПараметрыОткрытия, ЭтаФорма);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьДанныеФайлаНаСервере(Ссылка, УникальныйИдентификатор)
	
	Возврат ЗарплатаКадры.ПолучитьДанныеФайла(Ссылка, УникальныйИдентификатор);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗаголовокГруппыПрежнегоСтрахователя(УправляемаяФорма)
	
	Объект = УправляемаяФорма.Объект;
	Элементы = УправляемаяФорма.Элементы;
	
	Если Не ЗначениеЗаполнено(Объект.ПрежнийСтраховательНаименованиеПолное) Тогда
		ПредставлениеСтрахователя = НСтр("ru = 'не задан';
										|en = 'not specified'");
	Иначе
		ПредставленияСтрахователя = Новый Массив;
		ПредставленияСтрахователя.Добавить(СокрЛП(Объект.ПрежнийСтраховательНаименованиеПолное));
		Если ЗначениеЗаполнено(Объект.ПрежнийСтраховательРегистрационныйНомерПФР) Тогда
			ПредставленияСтрахователя.Добавить(СтрШаблон(
				НСтр("ru = 'рег. номер: %1';
					|en = 'reg. number: %1'"), СокрЛП(Объект.ПрежнийСтраховательРегистрационныйНомерПФР)));
		КонецЕсли; 
		Если ЗначениеЗаполнено(Объект.ПрежнийСтраховательИНН) Тогда
			ПредставленияСтрахователя.Добавить(СтрШаблон(
				НСтр("ru = 'ИНН: %1';
					|en = 'TIN: %1'"), СокрЛП(Объект.ПрежнийСтраховательИНН)));
		КонецЕсли; 
		Если ЗначениеЗаполнено(Объект.ПрежнийСтраховательКПП) Тогда
			ПредставленияСтрахователя.Добавить(СтрШаблон(
				НСтр("ru = 'КПП: %1';
					|en = 'CRTR: %1'"), СокрЛП(Объект.ПрежнийСтраховательКПП)));
		КонецЕсли;
		ПредставлениеСтрахователя = СтрСоединить(ПредставленияСтрахователя, ", ");
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ГруппаПрежнийСтрахователь",
		"ЗаголовокСвернутогоОтображения",
		СтрШаблон(НСтр("ru = 'Сведения о работодателе, правопреемником которого является страхователь (%1)';
						|en = 'Information record on the employer whose successor is the insurant (%1)'"), ПредставлениеСтрахователя));
	
КонецПроцедуры

// КадровыйЭДО
&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьПодключаемыеКоманды(УправляемаяФорма)
	КадровыйЭДОКлиентСервер.ОбновитьКоманды(УправляемаяФорма, УправляемаяФорма.Объект, Истина);
КонецПроцедуры
// Конец КадровыйЭДО

#КонецОбласти
