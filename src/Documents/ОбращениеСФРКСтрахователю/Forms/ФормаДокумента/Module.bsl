&НаКлиенте
Перем КонтекстЭДОКлиент;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Заголовок = НСтр("ru = 'Обращение СФР:';
					|en = 'Обращение СФР:'") + " " + Объект.Заголовок;
	
	СписокИменФайлов = Новый Массив;
	ИндексФайла = 0;
	Для каждого Вложение Из Объект.ФайлыОбращения Цикл
		ПометкаУдаления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Вложение.Содержимое, "ПометкаУдаления");
		Если ЗначениеЗаполнено(Вложение.Содержимое)
			И НЕ ПометкаУдаления Тогда
			ДанныеФайла = Новый Структура("ИмяФайла,Размер,Ссылка",
				Вложение.ИмяФайла, Вложение.Размер, Строка(ИндексФайла));
			СписокИменФайлов.Добавить(ДанныеФайла);
			ИндексФайла = ИндексФайла + 1;
		КонецЕсли;
	КонецЦикла;
	Элементы.Вложения.Заголовок = ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.ФорматированноеПредставлениеСпискаВложений(
		СписокИменФайлов);
	ОтправительТекстом = НСтр("ru = 'СФР';
								|en = 'СФР'");
	ПолучательТекстом = Объект.Организация;
	
	ЕстьВложения = Объект.ФайлыОбращения.Количество() > 0;
	Элементы.Вложения.Видимость = ЕстьВложения;
	Элементы.ЗаголовокВложения.Видимость = ЕстьВложения;
	Элементы.КомандаСохранитьВложения.Видимость = Ложь;
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганами.ОтметитьКакПрочтенное(Объект.Ссылка);
	
	УправлениеЭлементамиФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Объект.Ссылка.Пустая() Тогда
		ТестСообщения = НСтр("ru = 'Создание обращений к СФР не предусмотрено, они загружаются из сервиса СЭДО.';
							|en = 'Создание обращений к СФР не предусмотрено, они загружаются из сервиса СЭДО.'");
		Отказ = Истина;
		СтандартнаяОбработка = Ложь;
		ПоказатьПредупреждение(, ТестСообщения, 60);
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВложенияОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ИндексФайла = Число(НавигационнаяСсылкаФорматированнойСтроки);
	ОбработатьВложениеНавигационнойСсылки(ИндексФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаОтветыНажатие(Элемент)
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПоказатьОтветыПослеПолученияКонтекста", 
		ЭтотОбъект);
		
	ДокументооборотСКОКлиент.ПолучитьКонтекстЭДО(ОписаниеОповещения);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ДатыЗапретаИзменения") Тогда
		ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	ИмяСобытияПолучения = ЭлектронныйДокументооборотСФССКлиент.ИмяСобытияОповещенияЗавершенияПолученияСообщенийИзСЭДО();
	ИмяСобытияОтправки = ЭлектронныйДокументооборотСФССКлиент.ИмяСобытияОповещенияЗавершенияОтправкиСообщенийВСЭДО();
	
	Если ИмяСобытия = ИмяСобытияПолучения
		ИЛИ ИмяСобытия = ИмяСобытияОтправки Тогда
		
		УправлениеЭлементамиФормы();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Асинх Процедура Ответить(Команда)
	
	Ответы = ОтветыНаОбращение(Объект.Ссылка);
	
	// ищем существующий ответ
	Если Ответы.Количество() > 0 Тогда
		ТекстВопроса = НСтр("ru = 'На данное обращение уже создан ответ. Вы уверены, что хотите создать новый?';
							|en = 'На данное обращение уже создан ответ. Вы уверены, что хотите создать новый?'");
		Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Если НЕ Ответ = КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	СформированныйОтвет = СфомироватьОтветНаСервере(Объект.Ссылка);
	
	Если СформированныйОтвет =
			ПредопределенноеЗначение("Документ.ОтветСтрахователяНаОбращениеСФР.ПустаяСсылка") Тогда
			
			Возврат;
	КонецЕсли;
		
	ПараметрыФормы = Новый Структура("Ключ", СформированныйОтвет);
	ОткрытьФорму(
		"Документ.ОтветСтрахователяНаОбращениеСФР.Форма.ФормаДокумента",
		ПараметрыФормы);
		
	УправлениеЭлементамиФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьВложения(Команда)
	
	Вложения = Новый Массив;
	Для каждого Вложение Из Объект.ФайлыОбращения Цикл
		Вложения.Добавить(Вложение.ИмяФайла);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УправлениеЭлементамиФормы()
	
	Ответы = ОтветыНаОбращение(Объект.Ссылка);
	ОтветыКоличество = Ответы.Количество();
	ЗаголовокОтветы = "Ответы (" + ОтветыКоличество + ")";
	Элементы.ГиперссылкаОтветы.Заголовок = ЗаголовокОтветы;
	Элементы.ГиперссылкаОтветы.Видимость = ОтветыКоличество > 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	               |	ОтправкиФСС.Ссылка КАК Ссылка,
	               |	ОтправкиФСС.СтатусОтправки КАК СтатусОтправки
	               |ИЗ
	               |	Справочник.ОтправкиФСС КАК ОтправкиФСС
	               |ГДЕ
	               |	ОтправкиФСС.ОтчетСсылка = &Обращение
	               |	И ОтправкиФСС.ПометкаУдаления = ЛОЖЬ
	               |	И ОтправкиФСС.Организация = &Организация
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ОтправкиФСС.ДатаОтправки УБЫВ";

	Запрос.УстановитьПараметр("Обращение", Объект.Ссылка);
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	
	ОтправкиОбращения = Запрос.Выполнить().Выгрузить();
	Если ОтправкиОбращения.Количество() = 0 Тогда
		Статус = Объект.Статус;
	Иначе
		Статус = ОтправкиОбращения[0].СтатусОтправки;
	КонецЕсли;
	
	ПараметрыПрорисовкиПанелиОтправки = ДокументооборотСКОВызовСервера.ПараметрыПрорисовкиПанелиОтправки(
		Объект.Ссылка, Объект.Организация, Перечисления.ТипыКонтролирующихОрганов.ФСС);
	НаименованиеЭтапа = ПараметрыПрорисовкиПанелиОтправки.ТекущийЭтапОтправки.ТекстСтатуса;
	Элементы.СостояниеОтправки.Видимость = НЕ ПустаяСтрока(НаименованиеЭтапа);
	Если Статус = Перечисления.СтатусыОтправки.НеПринят Тогда
		Элементы.БлокСостоянияОтправки.ЦветФона = ЦветаСтиля.ЦветФонаОшибкиОтправки;
	ИначеЕсли Статус = Перечисления.СтатусыОтправки.Сдан Тогда
		Элементы.БлокСостоянияОтправки.ЦветФона = ЦветаСтиля.ЦветФонаУдачнойОтправки;
	ИначеЕсли Статус = Перечисления.СтатусыОтправки.Отправлен Тогда
		Элементы.БлокСостоянияОтправки.ЦветФона = ЦветаСтиля.ЦветФонаТекущейОтправки;
	Иначе
		Элементы.БлокСостоянияОтправки.ЦветФона = ЦветаСтиля.БазовыйЦветФонаЭтапаОтправки;
	КонецЕсли;
	Элементы.НаименованиеЭтапа.Заголовок = НаименованиеЭтапа;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ОтветыНаОбращение(Ссылка)
	
	Возврат ЭлектронныйДокументооборотСФСС.ОтветыНаОбращениеСФРКСтрахователю(Ссылка);
	
КонецФункции

&НаСервере
Функция СфомироватьОтветНаСервере(Ссылка)
	
	Результат = ЭлектронныйДокументооборотСФСС.СфомироватьОтветНаОбращениеСФРКСтрахователю(Ссылка);
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ДанныеФайла(СсылкаНаФайл, Идентификатор)
	
	ПараметрыФайла = РаботаСФайламиКлиентСервер.ПараметрыДанныхФайла();
	ПараметрыФайла.ИдентификаторФормы = Идентификатор;
	Возврат РаботаСФайлами.ДанныеФайла(СсылкаНаФайл, ПараметрыФайла);
	
КонецФункции

&НаКлиенте
Процедура ПоказатьОтветыПослеПолученияКонтекста(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.КонтекстЭДО = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КонтекстЭДОКлиент = Результат.КонтекстЭДО;
	КонтекстЭДОКлиент.ПоказатьОтветыНаОбращениеСФРКСтрахователю(Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВложениеНавигационнойСсылки(ИндексФайла)
	
	Файл = Объект.ФайлыОбращения[ИндексФайла];
	ДанныеФайла = ДанныеФайла(Файл.Содержимое, ЭтотОбъект.УникальныйИдентификатор);
	РаботаСФайламиКлиент.ОткрытьФайл(ДанныеФайла);
	
КонецПроцедуры

#КонецОбласти
