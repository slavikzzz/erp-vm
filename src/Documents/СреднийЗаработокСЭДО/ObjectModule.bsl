#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ДатаСоздания) Тогда
		ДатаСоздания = ТекущаяДата(); // АПК:143 Для фильтрации событий в журнале регистрации требуется дата сервера.
	КонецЕсли;
	Если ОбменДанными.Загрузка Тогда
		// Режим записи "БЗК".
		// При этом не срабатыват штатное проведение документа, поэтому движения необходимо записать "вручную".
		Если ДокументБудетПроведен(РежимЗаписи, РежимПроведения) Тогда
			// Наполнение наборов записей.
			ОбработкаПроведения(Отказ, РежимПроведения);
		Иначе
			// Включение флажка записи пустых наборов (при записи наборы будут очищены).
			Для Каждого НаборЗаписей Из Движения Цикл
				НаборЗаписей.Записывать = Истина;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	Если ОбменДанными.Загрузка Тогда
		// Режим записи "БЗК".
		// При этом не срабатыват штатное проведение документа, поэтому движения необходимо записать "вручную".
		Для Каждого НаборЗаписей Из Движения Цикл
			НаборЗаписей.Записать(Истина);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	Для Каждого НаборЗаписей Из Движения Цикл
		НаборЗаписей.Записывать = Истина;
		НаборЗаписей.ОбработкаПроведенияРегистратора(ЭтотОбъект, Отказ, РежимПроведения);
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПередЗаписью

Функция ДокументБудетПроведен(РежимЗаписи, РежимПроведения)
	Если ПометкаУдаления Тогда
		Возврат Ложь;
	КонецЕсли;
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		Возврат Истина;
	ИначеЕсли РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		Возврат Ложь;
	Иначе
		Возврат Проведен;
	КонецЕсли;
КонецФункции

#КонецОбласти

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли