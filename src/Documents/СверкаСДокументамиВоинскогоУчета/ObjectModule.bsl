#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	ДанныеДляПроведения = ДанныеДляПроведения();
	СформироватьДвиженияСверки(Движения, ДанныеДляПроведения.ДанныеСверки);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДанныеДляПроведения()
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СверкаСДокументамиВоинскогоУчетаСотрудники.Ссылка.Дата КАК Период,
		|	СверкаСДокументамиВоинскогоУчетаСотрудники.ФизическоеЛицо КАК ФизическоеЛицо,
		|	СверкаСДокументамиВоинскогоУчетаСотрудники.Ссылка.Организация КАК Организация,
		|	СверкаСДокументамиВоинскогоУчетаСотрудники.Ссылка.СверкуПровел КАК СверкуПровел
		|ИЗ
		|	Документ.СверкаСДокументамиВоинскогоУчета.Сотрудники КАК СверкаСДокументамиВоинскогоУчетаСотрудники
		|ГДЕ
		|	СверкаСДокументамиВоинскогоУчетаСотрудники.Ссылка = &Ссылка";
	
	ДанныеСверки = Запрос.Выполнить().Выгрузить();
	
	ДанныеДляПроведения = Новый Структура;
	ДанныеДляПроведения.Вставить("ДанныеСверки", ДанныеСверки);
	
	Возврат ДанныеДляПроведения;
	
КонецФункции

Процедура СформироватьДвиженияСверки(Движения, ДанныеСверки) Экспорт 
	
	Движения.ДанныеСверкиСДокументамиВоинскогоУчета.Записывать = Истина;
	Движения.ДанныеСверкиСДокументамиВоинскогоУчета.Загрузить(ДанныеСверки);
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли