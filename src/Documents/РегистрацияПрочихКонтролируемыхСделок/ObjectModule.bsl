
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	Если ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(УведомлениеОКонтролируемойСделке) Тогда
		Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УведомлениеОКонтролируемойСделке, "Организация");
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ВалютаДокумента) Тогда
		Если Не ЗначениеЗаполнено(ВалютаДокумента) Тогда
			ВалютаДокумента = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Организация);
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Организация = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если Не ЗначениеЗаполнено(УведомлениеОКонтролируемойСделке) Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыУведомления = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(УведомлениеОКонтролируемойСделке, "ОтчетныйГод");
	НачалоГода = НачалоГода(РеквизитыУведомления.ОтчетныйГод);
	ОкончаниеГода = КонецГода(РеквизитыУведомления.ОтчетныйГод);
	
	Для Каждого Сделка Из Сделки Цикл
		
		Префикс = "Сделки[" + Формат(Сделка.НомерСтроки - 1, "ЧН=0; ЧГ=") + "].";
		
		Если ЗначениеЗаполнено(Сделка.ДатаСовершенияСделки)
			И (Сделка.ДатаСовершенияСделки > ОкончаниеГода Или Сделка.ДатаСовершенияСделки < НачалоГода) Тогда
			ТекстСообщения = НСтр("ru = 'Дата совершения сделки в строке %1 не соответствует отчетному году уведомления. Сделка должна попадать в %2 год.';
									|en = 'Transaction date in line %1 does not conform to the notification''s reporting year. The transaction must fall within the year of %2.  '");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Сделка.НомерСтроки, Формат(НачалоГода, "ДФ=yyyy"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Префикс+"ДатаСовершенияСделки",, Отказ);
		КонецЕсли;
		
		Если Сделка.ТипПредметаСделки = Перечисления.ТипыПредметовКонтролируемыхСделок.ДолговоеОбязательство Тогда
			Если НЕ ЗначениеЗаполнено(Сделка.ДатаПроцентнойСтавки) Тогда
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", , НСтр("ru = 'Дата изменения процентной ставки';
																										|en = 'Interest rate change date'"),
					Сделка.НомерСтроки, "Сделки");
				Поле = Префикс + "ДатаПроцентнойСтавки";
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
			КонецЕсли;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Сделка.СуммаБезНДСВРублях) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", , НСтр("ru = 'Сумма (руб)';
																									|en = 'Amount (rub)'"),
				Сделка.НомерСтроки, "Сделки");
			Поле = Префикс + "СуммаБезНДСВРублях";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Сделка.СтавкаНДС) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", , НСтр("ru = 'Ставка НДС';
																									|en = 'VAT rate'"),
				Сделка.НомерСтроки, "Сделки");
			Поле = Префикс + "СтавкаНДС";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
		КонецЕсли;
		
	КонецЦикла;
	
	МассивНепроверяемыхРеквизитов = Новый Массив();
	МассивНепроверяемыхРеквизитов.Добавить("Сделки.ДатаПроцентнойСтавки");
	
	Если Не СделкаСовершенаЧерезКомиссионера Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Комиссионер");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	СуммаДокумента = ЭтотОбъект.Сделки.Итог("СуммаБезНДСВРублях") + ЭтотОбъект.Сделки.Итог("СуммаНДСВРублях");
	
	ПоддерживаетсяЕНВД = ПоддерживаетсяЕНВД();
	
	Для Каждого Сделка Из Сделки Цикл
		
		Если Сделка.ТипПредметаСделки <> Перечисления.ТипыПредметовКонтролируемыхСделок.Товар Тогда
			Сделка.Грузоотправитель = Неопределено;
		КонецЕсли;
		
		Если Сделка.ТипПредметаСделки = Перечисления.ТипыПредметовКонтролируемыхСделок.РаботаУслуга Тогда
			Сделка.СтранаПроисхожденияПредметаСделки = Справочники.СтраныМира.ПустаяСсылка();
		КонецЕсли;
		
		Если Не ПоддерживаетсяЕНВД И Сделка.ОперацияОблагаетсяЕНВД Тогда
			Сделка.ОперацияОблагаетсяЕНВД = Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(УведомлениеОКонтролируемойСделке) Тогда
		ВерсияУведомления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УведомлениеОКонтролируемойСделке, "ВерсияУведомления");
		Если ВерсияУведомления < КонтролируемыеСделкиКлиентСервер.ВерсияУведомления_2018() Тогда
			СделкаСовершенаЧерезКомиссионера = Ложь;
			Комиссионер = Справочники.Контрагенты.ПустаяСсылка();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ПриЗаписиДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеДокументов.ОбработкаПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеДокументов.ОбработкаУдаленияПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПоддерживаетсяЕНВД()
	
	Если ЗначениеЗаполнено(УведомлениеОКонтролируемойСделке) Тогда
		СвойстваУведомления = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(УведомлениеОКонтролируемойСделке, "ОтчетныйГод");
		Если СвойстваУведомления.ОтчетныйГод >= КонтролируемыеСделкиКлиентСервер.ДатаОтказаОтЕНВД() Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#КонецЕсли