#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УстановитьУсловноеОформление();

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПриЧтенииСозданииНаСервере();
	КонецЕсли;
	
	МеждународныйУчетОбщегоНазначения.УстановитьЗаголовкиПодразделения(Элементы.ПодразделениеДт, НСтр("ru = 'Дт';
																										|en = 'Dr'"));
	МеждународныйУчетОбщегоНазначения.УстановитьЗаголовкиПодразделения(Элементы.ПодразделениеКт, НСтр("ru = 'Кт';
																										|en = 'Cr'"));
	МеждународныйУчетОбщегоНазначения.УстановитьЗаголовкиПодразделения(Элементы.Подразделение, "");
	
	ФлажокСписок = Объект.ЗаполнениеДвижений.Количество() > 1;
	МассивТиповДокумента = Новый Массив;
	Для каждого Тип Из Метаданные.РегистрыБухгалтерии.Международный.СтандартныеРеквизиты.Регистратор.Тип.Типы() Цикл
		Если ОбщегоНазначения.ОбъектМетаданныхДоступенПоФункциональнымОпциям(Метаданные.НайтиПоТипу(Тип)) Тогда
			МассивТиповДокумента.Добавить(Тип);
		КонецЕсли;
	КонецЦикла;
	МассивТиповДокумента.Добавить(Тип("Строка"));
	НовыйТип = Новый ОписаниеТипов(МассивТиповДокумента);
	Элементы.СторнируемыйДокумент.ОграничениеТипа = НовыйТип;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);

	// ИнтеграцияС1СДокументооборотом
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПриСозданииНаСервере(ЭтаФорма);
	// Конец ИнтеграцияС1СДокументооборотом
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ПриЧтенииСозданииНаСервере();
	
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
	ТипыСвязанныеСОрганизацией = НастройкаСчетовУчетаСервер.ТипыСвязанныеСОрганизацией();
	
	МодификацияКонфигурацииПереопределяемый.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
		Если ВыбранноеЗначение.Свойство("АдресПроводокВХранилище") Тогда
			ЗагрузитьПроводкиИзХранилища(ВыбранноеЗначение.АдресПроводокВХранилище);
			Модифицированность = Истина;
		ИначеЕсли ВыбранноеЗначение.Свойство("ШаблонОперации") Тогда
			ИменаШаблонов = ОбоработкаВыбораШаблонаОперацииСервер(ВыбранноеЗначение.ШаблонОперации);
			ПоказатьОповещениеПользователя(НСтр("ru = 'Добавлены типовые проводки:';
												|en = 'Standard accounting entries are added:'"),,ИменаШаблонов,БиблиотекаКартинок.Информация32);
		КонецЕсли;
	ИначеЕсли Элементы.СторнируемыйДокумент.ОграничениеТипа.СодержитТип(ТипЗнч(ВыбранноеЗначение)) Тогда
		СторнируемыйДокумент = ВыбранноеЗначение;
		Модифицированность = Истина;
		ЗагрузитьПроводкиДляСторнирования(ВыбранноеЗначение);
	КонецЕсли;
	
	Если Окно <> Неопределено Тогда
		Окно.Активизировать();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		ЭтаФорма.Прочитать();
	КонецЕсли;
	
	МеждународныйУчетОбщегоНазначения.ЗаполнитьПредставлениеВидовСубконто(Объект.Движения.Международный, "Дт");
	МеждународныйУчетОбщегоНазначения.ЗаполнитьПредставлениеВидовСубконто(Объект.Движения.Международный, "Кт");
	МеждународныйУчетОбщегоНазначения.ЗаполнитьПредставлениеВидовСубконто(Объект.Движения.МеждународныйБезКорреспонденции, "");
	
	ЗаполнитьВспомогательныеРеквизитыПроводокБезКорреспонденции();
	МодификацияКонфигурацииПереопределяемый.ПослеЗаписиНаСервере(ЭтаФорма, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)

	МодификацияКонфигурацииКлиентПереопределяемый.ПослеЗаписи(ЭтаФорма, ПараметрыЗаписи);

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ПодключаемыеКоманды") Тогда
		МодульПодключаемыеКомандыКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ПодключаемыеКомандыКлиент");
		МодульПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	Если НЕ ФлажокСписок И ЗначениеЗаполнено(СторнируемыйДокумент) И ТипЗнч(СторнируемыйДокумент) <> Тип("Строка") Тогда
		ТекущийОбъект.ЗаполнениеДвижений.Очистить();
		НоваяСтрока = ТекущийОбъект.ЗаполнениеДвижений.Добавить();
		НоваяСтрока.Документ = СторнируемыйДокумент;
	КонецЕсли;
	МодификацияКонфигурацииПереопределяемый.ПередЗаписьюНаСервере(ЭтаФорма, Отказ, ТекущийОбъект, ПараметрыЗаписи);

	// ИнтеграцияС1СДокументооборотом
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПередЗаписьюНаСервере(ЭтаФорма, ТекущийОбъект, ПараметрыЗаписи);
	// Конец ИнтеграцияС1СДокументооборотом
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	ДатаПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ДатаПриИзмененииНаСервере()
	
	СформироватьЗаголовкиАннулирующихКоманд();
	
КонецПроцедуры

&НаКлиенте
Процедура ВводитьПериодПоСтрокамПриИзменении(Элемент)
	
	Элементы.Период.Видимость = Объект.ВводитьПериодПострокам;
	Элементы.МеждународныйБезКорреспонденцииПериод.Видимость = Объект.ВводитьПериодПострокам;
		
КонецПроцедуры

&НаКлиенте
Процедура СпособЗаполненияПриИзменении(Элемент)
	
	ОбновитьОтображениеСторнируемогоДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ЕстьДвижения = 
		Объект.Движения.Международный.Количество() > 0
		Или Объект.Движения.МеждународныйБезКорреспонденции.Количество() > 0;

	Если ЕстьДвижения Тогда
		ТекстВопроса = НСтр("ru = 'Указанные в проводках расчетные счета, договоры, подразделения, документы будут очищены. Продолжить?';
							|en = 'Current accounts, contracts, business units, documents specified in the entries will be cleared. Continue?'");
		Оповещение = Новый ОписаниеОповещения("ВопросПриИзмененииОрганизацииЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	Иначе
		ОрганизацияПриИзмененииНаСервере();
	КонецЕсли;
	
	ОбновитьОтображениеПоПлануСчетов();
	
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	СформироватьЗаголовкиАннулирующихКоманд();
	
КонецПроцедуры

&НаКлиенте
Процедура ПланСчетовПриИзменении(Элемент)
	
	ЕстьДвижения = 
		Объект.Движения.Международный.Количество() > 0
		Или Объект.Движения.МеждународныйБезКорреспонденции.Количество() > 0;
		
	Если ЕстьДвижения И ТекущийПланСчетов <> Объект.ПланСчетов Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ПриИзмененииПланаСчетовВопрос", ЭтотОбъект);
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.ОК, НСтр("ru = 'Продолжить';
													|en = 'Continue'"));
		Кнопки.Добавить(КодВозвратаДиалога.Отмена, НСтр("ru = 'Отмена';
														|en = 'Cancel'"));
		ПоказатьВопрос(ОписаниеОповещения, НСтр("ru = 'Таблица проводок будет очищена. Продолжить?';
												|en = 'Postings table will be cleared. Continue?'"), Кнопки);
	Иначе
		ТекущийПланСчетов = Объект.ПланСчетов;
		ПланСчетовПриИзмененииНаСервере();
	КонецЕсли;

	
КонецПроцедуры

&НаСервере
Процедура ПланСчетовПриИзмененииНаСервере()
	
	СформироватьЗаголовкиАннулирующихКоманд();
	ОбновитьОтображениеПоПлануСчетов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовЗаполнениеДвижений

&НаКлиенте
Процедура ЗаполнениеДвиженийДокументНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ЗаполнениеДвижений.ТекущиеДанные;
	Элемент.ВыбиратьТип = НЕ ЗначениеЗаполнено(ТекущиеДанные.Документ);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДвиженияМеждународный

&НаКлиенте
Процедура ДвиженияМеждународныйПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущаяСтрока = Элементы.ДвиженияМеждународный.ТекущиеДанные;
	Если НоваяСтрока И НЕ Копирование Тогда
		ТекущаяСтрока.Активность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДвиженияМеждународныйПередНачаломИзменения(Элемент, Отказ)
	СтрокаТаблицы = Элементы.ДвиженияМеждународный.ТекущиеДанные;
	НастройкаСчетовУчетаКлиент.ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект, СтрокаТаблицы, Объект.Организация, "Дт");
	НастройкаСчетовУчетаКлиент.ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект, СтрокаТаблицы, Объект.Организация, "Кт");
КонецПроцедуры

&НаКлиенте
Процедура СчетДтПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДвиженияМеждународный.ТекущиеДанные;
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, ПредставлениеВидовСубконтоСчета(ТекущиеДанные.СчетДт, "Дт"));
	
КонецПроцедуры

&НаКлиенте
Процедура СчетКтПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДвиженияМеждународный.ТекущиеДанные;
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, ПредставлениеВидовСубконтоСчета(ТекущиеДанные.СчетКт, "Кт"));
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ДвиженияМеждународный.ТекущиеДанные;
	ТекущаяСтрока.СуммаПредставления = РассчитатьСуммуПредставления(Объект.ПланСчетов, Объект.Организация, ТекущаяСтрока.Сумма, Объект.Дата);
	
КонецПроцедуры

&НаКлиенте
Процедура ВалютаДтПриИзменении(Элемент)
	
	ВалютаПриИзмененииКлиент(Элементы.ДвиженияМеждународный, "Дт");
	
КонецПроцедуры

&НаКлиенте
Процедура ВалютаКтПриИзменении(Элемент)
	
	ВалютаПриИзмененииКлиент(Элементы.ДвиженияМеждународный, "Кт");
	
КонецПроцедуры

&НаКлиенте
Процедура ВалютнаяСуммаДтПриИзменении(Элемент)
	
	ВалютаПриИзмененииКлиент(Элементы.ДвиженияМеждународный, "Дт");
	
КонецПроцедуры

&НаКлиенте
Процедура ВалютнаяСуммаКтПриИзменении(Элемент)
	
	ВалютаПриИзмененииКлиент(Элементы.ДвиженияМеждународный, "Кт");
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоДт1ПриИзменении(Элемент)
	СтрокаТаблицы = Элементы.ДвиженияМеждународный.ТекущиеДанные;
	НастройкаСчетовУчетаКлиент.ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект, СтрокаТаблицы, Объект.Организация, "Дт");
КонецПроцедуры

&НаКлиенте
Процедура СубконтоДт1ОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ОчиститьПараметрыВыбора(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура СубконтоДт2ПриИзменении(Элемент)
	СтрокаТаблицы = Элементы.ДвиженияМеждународный.ТекущиеДанные;
	НастройкаСчетовУчетаКлиент.ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект, СтрокаТаблицы, Объект.Организация, "Дт");
КонецПроцедуры

&НаКлиенте
Процедура СубконтоДт2ОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ОчиститьПараметрыВыбора(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура СубконтоДт3ПриИзменении(Элемент)
	СтрокаТаблицы = Элементы.ДвиженияМеждународный.ТекущиеДанные;
	НастройкаСчетовУчетаКлиент.ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект, СтрокаТаблицы, Объект.Организация, "Дт");
КонецПроцедуры

&НаКлиенте
Процедура СубконтоДт3ОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ОчиститьПараметрыВыбора(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКт1ПриИзменении(Элемент)
	СтрокаТаблицы = Элементы.ДвиженияМеждународный.ТекущиеДанные;
	НастройкаСчетовУчетаКлиент.ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект, СтрокаТаблицы, Объект.Организация, "Кт");
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКт1ОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ОчиститьПараметрыВыбора(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКт2ПриИзменении(Элемент)
	СтрокаТаблицы = Элементы.ДвиженияМеждународный.ТекущиеДанные;
	НастройкаСчетовУчетаКлиент.ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект, СтрокаТаблицы, Объект.Организация, "Кт");
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКт2ОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ОчиститьПараметрыВыбора(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКт3ПриИзменении(Элемент)
	СтрокаТаблицы = Элементы.ДвиженияМеждународный.ТекущиеДанные;
	НастройкаСчетовУчетаКлиент.ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект, СтрокаТаблицы, Объект.Организация, "Кт");
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКт3ОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ОчиститьПараметрыВыбора(Элемент);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДвиженияМеждународныйБезКорреспонденции

&НаКлиенте
Процедура МеждународныйБезКорреспонденцииПередНачаломИзменения(Элемент, Отказ)
	СтрокаТаблицы = Элементы.МеждународныйБезКорреспонденции.ТекущиеДанные;
	НастройкаСчетовУчетаКлиент.ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект, СтрокаТаблицы, Объект.Организация, "");
КонецПроцедуры

&НаКлиенте
Процедура МеждународныйБезКорреспонденцииПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	Если Не ОтменаРедактирования Тогда
		РассчитатьИтогиПоПроводкамБезКорреспонденции(ЭтотОбъект);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура МеждународныйБезКорреспонденцииПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущаяСтрока = Элементы.МеждународныйБезКорреспонденции.ТекущиеДанные;
	Если НоваяСтрока И НЕ Копирование Тогда
		ТекущаяСтрока.Активность = Ложь;
		Если ИтогСуммаДт >= ИтогСуммаКт Тогда
			ТекущаяСтрока.ВидДвижения = ВидДвиженияКредит;
			ТекущаяСтрока.СуммаКт = ИтогСуммаДт - ИтогСуммаКт;
			ТекущаяСтрока.Сумма = ТекущаяСтрока.СуммаКт;
			
			ТекущаяСтрока.СуммаПредставленияКт = РассчитатьСуммуПредставления(Объект.ПланСчетов, Объект.Организация, ТекущаяСтрока.Сумма, Объект.Дата);
			ТекущаяСтрока.СуммаПредставления = ТекущаяСтрока.СуммаПредставленияКт;
		Иначе
			ТекущаяСтрока.ВидДвижения = ВидДвиженияДебет;
			ТекущаяСтрока.СуммаДт = ИтогСуммаКт - ИтогСуммаДт;
			ТекущаяСтрока.Сумма = ТекущаяСтрока.СуммаДт;
			
			ТекущаяСтрока.СуммаПредставленияДт = РассчитатьСуммуПредставления(Объект.ПланСчетов, Объект.Организация, ТекущаяСтрока.Сумма, Объект.Дата);
			ТекущаяСтрока.СуммаПредставления = ТекущаяСтрока.СуммаПредставленияДт;
		КонецЕсли;
		РассчитатьИтогиПоПроводкамБезКорреспонденции(ЭтотОбъект);
		ТекущаяСтрока.ВидДвиженияИндекс = 
			ВидДвиженияИндексКартинки(ТекущаяСтрока.ВидДвижения, ТекущаяСтрока.Активность, ТекущаяСтрока.Сумма, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура МеждународныйБезКорреспонденцииПослеУдаления(Элемент)
	РассчитатьИтогиПоПроводкамБезКорреспонденции(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура СчетПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.МеждународныйБезКорреспонденции.ТекущиеДанные;
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, ПредставлениеВидовСубконтоСчета(ТекущиеДанные.Счет, ""));
	
	Если ЗначениеЗаполнено(ТекущиеДанные.Счет) Тогда
		СвойстваСчета = МеждународныйУчетВызовСервера.СвойстваСчета(ТекущиеДанные.Счет);
		Если СвойстваСчета.Валютный
		   И ЗначениеЗаполнено(ТекущиеДанные.Сумма)
		   И Не ЗначениеЗаполнено(ТекущиеДанные.Валюта)
		   И Не ЗначениеЗаполнено(ТекущиеДанные.ВалютнаяСумма) Тогда
			ТекущиеДанные.Валюта = ВалютаФункциональная;
			ТекущиеДанные.ВалютнаяСумма = ТекущиеДанные.Сумма;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаДтПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.МеждународныйБезКорреспонденции.ТекущиеДанные;
	ТекущиеДанные.ВидДвижения = ВидДвиженияДебет;
	ТекущиеДанные.Сумма = ТекущиеДанные.СуммаДт;
	ТекущиеДанные.СуммаКт = 0;
	
	ТекущиеДанные.СуммаПредставленияДт = РассчитатьСуммуПредставления(Объект.ПланСчетов, Объект.Организация, ТекущиеДанные.Сумма, Объект.Дата);
	ТекущиеДанные.СуммаПредставления = ТекущиеДанные.СуммаПредставленияДт;
	ТекущиеДанные.СуммаПредставленияКт = 0;
	
	ТекущиеДанные.ВидДвиженияИндекс = 
		ВидДвиженияИндексКартинки(ТекущиеДанные.ВидДвижения, ТекущиеДанные.Активность, ТекущиеДанные.Сумма, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаКтПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.МеждународныйБезКорреспонденции.ТекущиеДанные;
	ТекущиеДанные.ВидДвижения = ВидДвиженияКредит;
	ТекущиеДанные.Сумма = ТекущиеДанные.СуммаКт;
	ТекущиеДанные.СуммаДт = 0;
	
	ТекущиеДанные.СуммаПредставленияКт = РассчитатьСуммуПредставления(Объект.ПланСчетов, Объект.Организация, ТекущиеДанные.Сумма, Объект.Дата);
	ТекущиеДанные.СуммаПредставления = ТекущиеДанные.СуммаПредставленияКт;
	ТекущиеДанные.СуммаПредставленияДт = 0;
	
	ТекущиеДанные.ВидДвиженияИндекс = 
		ВидДвиженияИндексКартинки(ТекущиеДанные.ВидДвижения, ТекущиеДанные.Активность, ТекущиеДанные.Сумма, ЭтотОбъект);
		
КонецПроцедуры

&НаКлиенте
Процедура СуммаПредставленияДтПриИзменении(Элемент)
	ТекущиеДанные = Элементы.МеждународныйБезКорреспонденции.ТекущиеДанные;
	ТекущиеДанные.СуммаПредставления = ТекущиеДанные.СуммаПредставленияДт;
	ТекущиеДанные.ВидДвижения = ВидДвиженияДебет;
	Если ТекущиеДанные.СуммаКт <> 0 Тогда
		ТекущиеДанные.СуммаКт = 0;
		ТекущиеДанные.Сумма = 0;
	КонецЕсли;
	Если ТекущиеДанные.СуммаПредставленияКт <> 0 Тогда
		ТекущиеДанные.СуммаПредставленияКт = 0;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СуммаПредставленияКтПриИзменении(Элемент)
	ТекущиеДанные = Элементы.МеждународныйБезКорреспонденции.ТекущиеДанные;
	ТекущиеДанные.СуммаПредставления = ТекущиеДанные.СуммаПредставленияКт;
	ТекущиеДанные.ВидДвижения = ВидДвиженияКредит;
	Если ТекущиеДанные.СуммаДт <> 0 Тогда
		ТекущиеДанные.СуммаДт = 0;
		ТекущиеДанные.Сумма = 0;
	КонецЕсли;
	Если ТекущиеДанные.СуммаПредставленияДт <> 0 Тогда
		ТекущиеДанные.СуммаПредставленияДт = 0;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВалютнаяСуммаПриИзменении(Элемент)
	ВалютаПриИзмененииКлиент(Элементы.МеждународныйБезКорреспонденции, "");
КонецПроцедуры

&НаКлиенте
Процедура ВалютаПриИзменении(Элемент)
	ВалютаПриИзмененииКлиент(Элементы.МеждународныйБезКорреспонденции, "");
КонецПроцедуры

&НаКлиенте
Процедура Субконто1ПриИзменении(Элемент)
	СтрокаТаблицы = Элементы.МеждународныйБезКорреспонденции.ТекущиеДанные;
	НастройкаСчетовУчетаКлиент.ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект, СтрокаТаблицы, Объект.Организация, "");
КонецПроцедуры

&НаКлиенте
Процедура Субконто1ОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ОчиститьПараметрыВыбора(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура Субконто2ПриИзменении(Элемент)
	СтрокаТаблицы = Элементы.МеждународныйБезКорреспонденции.ТекущиеДанные;
	НастройкаСчетовУчетаКлиент.ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект, СтрокаТаблицы, Объект.Организация, "");
КонецПроцедуры

&НаКлиенте
Процедура Субконто2ОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ОчиститьПараметрыВыбора(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура Субконто3ПриИзменении(Элемент)
	СтрокаТаблицы = Элементы.МеждународныйБезКорреспонденции.ТекущиеДанные;
	НастройкаСчетовУчетаКлиент.ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект, СтрокаТаблицы, Объект.Организация, "");
КонецПроцедуры

&НаКлиенте
Процедура Субконто3ОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ОчиститьПараметрыВыбора(Элемент);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьПроводки(Команда)
	
	НачатьЗагрузкуПроводок(Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьПроводкиСторно(Команда)
	
	НачатьЗагрузкуПроводок(Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПоШаблону(Команда)
	
	ПараметрыФормы = Новый Структура("Объект",Объект);
	Если ЗначениеЗаполнено(Объект.ПланСчетов) Тогда
		ОтборПоПлануСчетов = Новый Структура("ПланСчетов", Объект.ПланСчетов);
		ПараметрыФормы.Вставить("Отбор", ОтборПоПлануСчетов);
	КонецЕсли;
	
	ОткрытьФорму("Справочник.ТиповыеОперацииМеждународныйУчет.ФормаВыбора", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьДокумент(Команда)
	
	ОбщегоНазначенияУТКлиент.Записать(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиДокумент(Команда)
	
	ОбщегоНазначенияУТКлиент.Провести(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиИЗакрыть(Команда)
	
	ОбщегоНазначенияУТКлиент.ПровестиИЗакрыть(ЭтаФорма);
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

// ИнтеграцияС1СДокументооборотом
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(Команда, ЭтаФорма, Объект);
	
КонецПроцедуры
// Конец ИнтеграцияС1СДокументооборотом

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	// оформление отрицательных сумм красным
	ОформлениеОтрицательныхСумм(Элементы.Сумма);
	ОформлениеОтрицательныхСумм(Элементы.СуммаПредставления);
	ОформлениеОтрицательныхСумм(Элементы.ВалютнаяСуммаДт);
	ОформлениеОтрицательныхСумм(Элементы.ВалютнаяСуммаКт);
	
	ОформлениеОтрицательныхСумм(Элементы.СуммаДт);
	ОформлениеОтрицательныхСумм(Элементы.СуммаКт);
	ОформлениеОтрицательныхСумм(Элементы.СуммаПредставленияДт);
	ОформлениеОтрицательныхСумм(Элементы.СуммаПредставленияКт);
	ОформлениеОтрицательныхСумм(Элементы.ВалютнаяСумма);
	
	// оформление полей "Субконто1", ... и т.д.
	МеждународныйУчетОбщегоНазначения.УстановитьОформлениеПроводок(
		УсловноеОформление, 
		"Объект.Движения.Международный",
		Истина);
	МеждународныйУчетОбщегоНазначения.УстановитьОформлениеПроводок(
		УсловноеОформление, 
		"Объект.Движения.МеждународныйБезКорреспонденции",
		Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	
	ВидДвиженияДебет = ВидДвиженияБухгалтерии.Дебет;
	ВидДвиженияКредит = ВидДвиженияБухгалтерии.Кредит;
	ПросроченныеДанныеЦвет = ЦветаСтиля.ПросроченныеДанныеЦвет;
	
	Элементы.Период.Видимость = Объект.ВводитьПериодПострокам;
	Элементы.МеждународныйБезКорреспонденцииПериод.Видимость = Объект.ВводитьПериодПострокам;
	ТекущийПланСчетов = Объект.ПланСчетов;
	ОбновитьОтображениеПоПлануСчетов();
	ОбновитьОтображениеСторнируемогоДокумента();
	СформироватьЗаголовкиАннулирующихКоманд();
	ЗаполнитьВспомогательныеРеквизитыПроводокБезКорреспонденции();
	
	МеждународныйУчетОбщегоНазначения.ЗаполнитьПредставлениеВидовСубконто(Объект.Движения.Международный, "Дт");
	МеждународныйУчетОбщегоНазначения.ЗаполнитьПредставлениеВидовСубконто(Объект.Движения.Международный, "Кт");
	МеждународныйУчетОбщегоНазначения.ЗаполнитьПредставлениеВидовСубконто(Объект.Движения.МеждународныйБезКорреспонденции, "");
	
КонецПроцедуры

&НаСервере
Процедура ОформлениеОтрицательныхСумм(ПолеФормы)

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ПолеФормы.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПолеФормы.ПутьКДанным);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Меньше;
	ОтборЭлемента.ПравоеЗначение = 0;
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветОтрицательногоЧисла);

КонецПроцедуры

&НаСервере
Процедура ОбновитьОтображениеСторнируемогоДокумента()
	
	Элементы.ГруппаСторнироватьСписок.Видимость = Ложь;
	Если Объект.ЗаполнениеДвижений.Количество() > 1 Тогда
		Элементы.ГруппаСторнироватьСписок.Видимость = Истина;
		Элементы.СтраницыСторнирования.ТекущаяСтраница = Элементы.ГруппаСторнироватьСписок;
		СторнируемыйДокумент = Неопределено;
		СторнируемыйДокумент = ТекстВвестиДокумент();
		
	ИначеЕсли Объект.ЗаполнениеДвижений.Количество() = 1 Тогда
		Элементы.СтраницыСторнирования.ТекущаяСтраница = Элементы.ГруппаСторнироватьДокумент;
		СторнируемыйДокумент = Объект.ЗаполнениеДвижений[0].Документ;
		
	Иначе
		Элементы.СтраницыСторнирования.ТекущаяСтраница = Элементы.ГруппаСторнироватьДокумент;
		СторнируемыйДокумент = ТекстВвестиДокумент();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОтображениеПоПлануСчетов()
	
	ВалютаМУ = МеждународныйУчетОбщегоНазначения.УчетныеВалюты(Объект.ПланСчетов, Объект.Организация);
	ВалютаФункциональная = ВалютаМУ.Функциональная;
	
	Если Не ЗначениеЗаполнено(ВалютаМУ.Функциональная) Тогда
		ТекстСумма = НСтр("ru = 'Функц.';
							|en = 'Functional'");
		ТекстСуммаДт = НСтр("ru = 'Сумма функц. Дт';
							|en = 'Dr funct. currency amount'");
		ТекстСуммаКт = НСтр("ru = 'Сумма функц. Кт';
							|en = 'Cr funct. currency amount'");
	Иначе
		ТекстСумма = СтрШаблон(НСтр("ru = 'Функц. (%1)';
									|en = 'Funct. (%1)'"), Строка(ВалютаМУ.Функциональная));
		ТекстСуммаДт = СтрШаблон(НСтр("ru = 'Сумма функц. Дт (%1)';
										|en = 'Dr funct. currency amount (%1)'"), Строка(ВалютаМУ.Функциональная));
		ТекстСуммаКт = СтрШаблон(НСтр("ru = 'Сумма функц. Кт (%1)';
										|en = 'Cr funct. currency amount (%1)'"), Строка(ВалютаМУ.Функциональная));
	КонецЕсли;
	Элементы.Сумма.Заголовок = ТекстСумма;
	Элементы.СуммаДт.Заголовок = ТекстСуммаДт;
	Элементы.СуммаКт.Заголовок = ТекстСуммаКт;
	
	Элементы.СуммаПредставления.Видимость = Истина;
	Элементы.СуммаПредставленияДт.Видимость = Истина;
	Элементы.СуммаПредставленияКт.Видимость = Истина;
	Если Не ЗначениеЗаполнено(ВалютаМУ.Представления) Тогда
		ТекстСуммаПредставления = НСтр("ru = 'Пред.';
										|en = 'Presentation'");
		ТекстСуммаПредставленияДт = НСтр("ru = 'Сумма предст. Дт';
										|en = 'Dr present. amount'");
		ТекстСуммаПредставленияКт = НСтр("ru = 'Сумма предст. Кт';
										|en = 'Cr present. amount'");
	Иначе
		ТекстСуммаПредставления = СтрШаблон(НСтр("ru = 'Пред. (%1)';
												|en = 'Present. (%1)'"), Строка(ВалютаМУ.Представления));
		ТекстСуммаПредставленияДт = СтрШаблон(НСтр("ru = 'Сумма предст. Дт (%1)';
													|en = 'Dr present. amount (%1)'"), Строка(ВалютаМУ.Представления));
		ТекстСуммаПредставленияКт = СтрШаблон(НСтр("ru = 'Сумма предст. Кт (%1)';
													|en = 'Cr present. amount (%1)'"), Строка(ВалютаМУ.Представления));
	КонецЕсли;
	Элементы.СуммаПредставления.Заголовок = ТекстСуммаПредставления;
	Элементы.СуммаПредставленияДт.Заголовок = ТекстСуммаПредставленияДт;
	Элементы.СуммаПредставленияКт.Заголовок = ТекстСуммаПредставленияКт;
	
	Если ЗначениеЗаполнено(ВалютаМУ.Функциональная) И ВалютаМУ.Функциональная = ВалютаМУ.Представления Тогда
		Элементы.СуммаПредставления.Видимость = Ложь;
		Элементы.СуммаПредставленияДт.Видимость = Ложь;
		Элементы.СуммаПредставленияКт.Видимость = Ложь;
		ТекстСумма = СтрШаблон(НСтр("ru = 'Сумма (%1)';
									|en = 'Amount (%1)'"), Строка(ВалютаМУ.Функциональная));
		ТекстСуммаДт = СтрШаблон(НСтр("ru = 'Сумма Дт (%1)';
										|en = 'Dr amount (%1)'"), Строка(ВалютаМУ.Функциональная));
		ТекстСуммаКт = СтрШаблон(НСтр("ru = 'Сумма Кт (%1)';
										|en = 'Cr amount (%1)'"), Строка(ВалютаМУ.Функциональная));
		Элементы.Сумма.Заголовок = ТекстСумма;
		Элементы.СуммаДт.Заголовок = ТекстСуммаДт;
		Элементы.СуммаКт.Заголовок = ТекстСуммаКт;
	КонецЕсли;
	
	ЗаголовокГруппы = "";
	ВариантФормированияПроводок = Неопределено;
	Если ЗначениеЗаполнено(Объект.ПланСчетов) Тогда
		РеквизитыПланаСчетов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.ПланСчетов, "Наименование, ВариантФормированияПроводок");
		ЗаголовокГруппы = РеквизитыПланаСчетов.Наименование;
		ВариантФормированияПроводок = РеквизитыПланаСчетов.ВариантФормированияПроводок;
		
		Если Не ЗначениеЗаполнено(ВариантФормированияПроводок) Тогда
			ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Неопределено,
				"Справочник.ПланыСчетовМеждународногоУчета", Объект.ПланСчетов);
			Если Не ОбработкаЗавершена Тогда
				ВариантФормированияПроводок = Перечисления.ВариантыФормированияПроводок.СКорреспонденцией;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Элементы.ГруппаПроводкиБезКорреспонденции.Заголовок = ЗаголовокГруппы;
	Элементы.ГруппаПроводкиСКорреспонденцией.Заголовок = ЗаголовокГруппы;
	
	Элементы.ГруппаПроводкиБезКорреспонденции.Видимость = (ВариантФормированияПроводок = Перечисления.ВариантыФормированияПроводок.БезКорреспонденции);
	Элементы.ГруппаПроводкиСКорреспонденцией.Видимость = (ВариантФормированияПроводок = Перечисления.ВариантыФормированияПроводок.СКорреспонденцией);
	Элементы.ГруппаПланСчетовНеВыбран.Видимость = Не ЗначениеЗаполнено(ВариантФормированияПроводок);
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьЗагрузкуПроводок(ИмяКоманды)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьДвижения = 
		Объект.Движения.Международный.Количество() > 0
		Или Объект.Движения.МеждународныйБезКорреспонденции.Количество() > 0;
	
	Если ЕстьДвижения Тогда
		
		ПоказатьВопрос(Новый ОписаниеОповещения("ОбработкаОтветаНаВопросЕстьСтроки", ЭтотОбъект, ИмяКоманды),
						НСтр("ru = 'Текущие проводки документа при загрузке будут удалены. Продолжить?';
							|en = 'Current document entries will be deleted upon import. Continue?'"),
							РежимДиалогаВопрос.ДаНет);
	Иначе
		ВыполнитьЗагрузкуПроводок(ИмяКоманды);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОтветаНаВопросЕстьСтроки(Ответ, ИмяКоманды) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ВыполнитьЗагрузкуПроводок(ИмяКоманды);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗагрузкуПроводок(ИмяКоманды)
	
	Если ИмяКоманды = "ЗагрузитьПроводки" Тогда
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ДатаДокумента", Объект.Дата);
		ПараметрыФормы.Вставить("Организация", Объект.Организация);
		ПараметрыФормы.Вставить("ПланСчетов", Объект.ПланСчетов);
		ПараметрыФормы.Вставить("ВводитьПериодПоСтрокам", Объект.ВводитьПериодПоСтрокам);
		ОткрытьФорму("Документ.ОперацияМеждународный.Форма.ФормаЗагрузки", ПараметрыФормы, ЭтаФорма);
	ИначеЕсли ИмяКоманды = "ЗагрузитьПроводкиСторно" Тогда
		ЗагрузитьПроводкиСторноНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция РассчитатьСуммуПредставления(ПланСчетов, Организация, Сумма, Дата)
	
	Возврат МеждународныйУчетОбщегоНазначения.РассчитатьСуммуПредставления(ПланСчетов, Организация, Сумма, Дата);
	
КонецФункции

&НаКлиенте
Процедура ВалютаПриИзмененииКлиент(ТаблицаФормы, ДтКт)
	
	ТекущаяСтрока = ТаблицаФормы.ТекущиеДанные;
	НаДату = ?(Объект.ВводитьПериодПоСтрокам, ТекущаяСтрока.Период, Объект.Дата);
	СуммыПроводки = РассчитатьСуммы(Объект.ПланСчетов, Объект.Организация, ТекущаяСтрока["ВалютнаяСумма"+ДтКт], ТекущаяСтрока["Валюта"+ДтКт], НаДату);
	ЗаполнитьЗначенияСвойств(ТекущаяСтрока, СуммыПроводки);
	Если ДтКт = "" Тогда
		ЗаполнитьВспомогательныеРеквизитыПроводки(ТекущаяСтрока, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция РассчитатьСуммы(ПланСчетов, Организация, ВалютнаяСумма, Валюта, НаДату)
	
	СуммыПроводки = Новый Структура("Сумма,СуммаПредставления",0,0);
	КоэффициентыПересчета = МеждународныйУчетОбщегоНазначения.ПолучитьКоэффициентыПересчетаВалюты(ПланСчетов, Организация, Валюта, НаДату);
	СуммыПроводки.Сумма = ВалютнаяСумма * КоэффициентыПересчета.ВФункциональнуюВалюту;
	СуммыПроводки.СуммаПредставления = ВалютнаяСумма * КоэффициентыПересчета.ВВалютуПредставления;
	
	Возврат СуммыПроводки;
	
КонецФункции

// Описание
// 
// Параметры:
// 	ШаблоныОперации - СправочникСсылка.ТиповыеОперацииМеждународныйУчет, Массив из СправочникСсылка.ТиповыеОперацииМеждународныйУчет - 
// Возвращаемое значение:
// 	Строка - Описание
&НаСервере
Функция ОбоработкаВыбораШаблонаОперацииСервер(ШаблоныОперации)
	
	ТекстыОписания = Новый Массив();
	
	Если ТипЗнч(ШаблоныОперации) = Тип("СправочникСсылка.ТиповыеОперацииМеждународныйУчет") Тогда
		ТекстОписания = ЗагрузитьШаблонОперации(ШаблоныОперации);
		ТекстыОписания.Добавить(ТекстОписания);
	ИначеЕсли ТипЗнч(ШаблоныОперации) = Тип("Массив") Тогда
		Для Каждого Шаблон Из ШаблоныОперации Цикл
			ТекстОписания = ЗагрузитьШаблонОперации(Шаблон);
			ТекстыОписания.Добавить(ТекстОписания);
		КонецЦикла;
	КонецЕсли;
	
	Возврат СтрСоединить(ТекстыОписания, "," + Символы.ПС);
	
КонецФункции

&НаСервере
Функция ЗагрузитьШаблонОперации(ШаблонОперации)
	
	Если Объект.Организация.Пустая() Тогда
		Объект.Организация = ШаблонОперации.Организация;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.СодержаниеОперации) Тогда
		Объект.СодержаниеОперации = ШаблонОперации.СодержаниеОперации;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.ПланСчетов) Тогда
		Объект.ПланСчетов = ШаблонОперации.ПланСчетов;
	КонецЕсли;
	
	Для Каждого Проводка Из ШаблонОперации.ПроводкиСКорреспонденцией Цикл
		
		НоваяПроводка = Объект.Движения.Международный.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяПроводка, Проводка);
		НоваяПроводка.Активность = Ложь;
		НоваяПроводка.Период = Объект.Дата;
		
	КонецЦикла;
	
	Для Каждого Проводка Из ШаблонОперации.ПроводкиБезКорреспонденции Цикл
		
		НоваяПроводка = Объект.Движения.МеждународныйБезКорреспонденции.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяПроводка, Проводка);
		НоваяПроводка.Активность = Ложь;
		НоваяПроводка.Период = Объект.Дата;
		
		Если Проводка.ВидДвижения = Перечисления.ВидыДвиженийБухгалтерии.Дебет Тогда
			НоваяПроводка.ВидДвижения = ВидДвиженияДебет;
		ИначеЕсли Проводка.ВидДвижения = Перечисления.ВидыДвиженийБухгалтерии.Кредит Тогда
			НоваяПроводка.ВидДвижения = ВидДвиженияКредит;
		Иначе
			НоваяПроводка.ВидДвижения = Неопределено;
		КонецЕсли;
		
		НоваяПроводка.СуммаПредставления = РассчитатьСуммуПредставления(Объект.ПланСчетов, Объект.Организация, НоваяПроводка.Сумма, Объект.Дата);
		
	КонецЦикла;
	
	МеждународныйУчетОбщегоНазначения.ЗаполнитьПредставлениеВидовСубконто(Объект.Движения.Международный, "Дт");
	МеждународныйУчетОбщегоНазначения.ЗаполнитьПредставлениеВидовСубконто(Объект.Движения.Международный, "Кт");
	МеждународныйУчетОбщегоНазначения.ЗаполнитьПредставлениеВидовСубконто(Объект.Движения.МеждународныйБезКорреспонденции, "");
	
	ЗаполнитьВспомогательныеРеквизитыПроводокБезКорреспонденции();
	
	Возврат ШаблонОперации.Наименование;
	
КонецФункции

&НаСервере
Процедура ЗагрузитьПроводкиИзХранилища(АдресПроводокВХранилище)
	
	Если ВариантФормированияПроводок = Перечисления.ВариантыФормированияПроводок.СКорреспонденцией Тогда
		Объект.Движения.Международный.Загрузить(ПолучитьИзВременногоХранилища(АдресПроводокВХранилище));
	Иначе
		Объект.Движения.МеждународныйБезКорреспонденции.Загрузить(ПолучитьИзВременногоХранилища(АдресПроводокВХранилище));
		ЗаполнитьВспомогательныеРеквизитыПроводокБезКорреспонденции();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПредставлениеВидовСубконтоСчета(Счет, ДтКт)
	
	Возврат МеждународныйУчетОбщегоНазначения.ПредставлениеВидовСубконто(Счет, ДтКт);
	
КонецФункции

&НаСервере
Процедура ЗагрузитьПроводкиСторноНаСервере()
	
	ДокументыСторно = Объект.ЗаполнениеДвижений.Выгрузить(,"Документ");
	Если ЗначениеЗаполнено(СторнируемыйДокумент) Тогда
		НовыйДокумент = ДокументыСторно.Добавить();
		НовыйДокумент.Документ = СторнируемыйДокумент;
		ДокументыСторно.Свернуть("Документ");
	КонецЕсли;
	ЗагрузитьПроводкиДляСторнирования(ДокументыСторно);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьПроводкиДляСторнирования(Регистраторы)
	
	Если ВариантФормированияПроводок = Перечисления.ВариантыФормированияПроводок.СКорреспонденцией Тогда
		ПроводкиОперации = РегистрыБухгалтерии.Международный.ПроводкиСторно(Регистраторы,
			Объект.ПланСчетов, Объект.Дата, Объект.ВводитьПериодПоСтрокам);
		Объект.Движения.Международный.Загрузить(ПроводкиОперации);
	Иначе
		ПроводкиОперации = РегистрыБухгалтерии.МеждународныйБезКорреспонденции.ПроводкиСторно(Регистраторы,
			Объект.ПланСчетов, Объект.Дата, Объект.ВводитьПериодПоСтрокам);
		Объект.Движения.МеждународныйБезКорреспонденции.Загрузить(ПроводкиОперации);
		ЗаполнитьВспомогательныеРеквизитыПроводокБезКорреспонденции();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СторнируемыйДокументНажатие(Элемент, СтандартнаяОбработка)
	
	Если ТипЗнч(СторнируемыйДокумент) = Тип("Строка") Тогда
		
		СтандартнаяОбработка = Ложь;
		СписокТипов = Новый СписокЗначений;
		СписокТипов.ЗагрузитьЗначения(Элемент.ОграничениеТипа.Типы());
		СписокТипов.СортироватьПоЗначению();
		СписокТипов.Удалить(СписокТипов.НайтиПоЗначению(Тип("Строка")));
		СписокТипов.ПоказатьВыборЭлемента(Новый ОписаниеОповещения("ОбработчикВыбораТипаДокумента", ЭтотОбъект),
											НСтр("ru = 'Выберите тип:';
												|en = 'Select type:'"));
		
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикВыбораТипаДокумента(ВыбранныйТип, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйТип <> Неопределено Тогда
		ИмяДокумента = ИмяТипа(ВыбранныйТип.Значение);
		ОткрытьФорму("Документ." + ИмяДокумента + ".ФормаВыбора",,ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИмяТипа(ЗаданныйТип)
	
	Возврат Метаданные.НайтиПоТипу(ЗаданныйТип).Имя;
	
КонецФункции

&НаКлиенте
Процедура ПерейтиВСписок(Команда)
	
	ФлажокСписок = Истина;
	Элементы.СтраницыСторнирования.ТекущаяСтраница = Элементы.ГруппаСторнироватьСписок;
	Элементы.ГруппаСторнироватьСписок.Видимость = Истина;
	Если ЗначениеЗаполнено(СторнируемыйДокумент) И ТипЗнч(СторнируемыйДокумент) <> Тип("Строка") Тогда
		Объект.ЗаполнениеДвижений.Очистить();
		НоваяСтрока = Объект.ЗаполнениеДвижений.Добавить();
		НоваяСтрока.Документ = СторнируемыйДокумент;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыйтиИзСписка(Команда)
	
	ФлажокСписок = Ложь;
	Элементы.СтраницыСторнирования.ТекущаяСтраница = Элементы.ГруппаСторнироватьДокумент;
	Элементы.ГруппаСторнироватьСписок.Видимость = Ложь;
	Если Объект.ЗаполнениеДвижений.Количество() > 0 Тогда
		СторнируемыйДокумент = Объект.ЗаполнениеДвижений[0].Документ;
		Объект.ЗаполнениеДвижений.Очистить();
	ИначеЕсли Объект.ЗаполнениеДвижений.Количество() = 0 Тогда
		СторнируемыйДокумент = Неопределено;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СторнируемыйДокумент) Тогда
		СторнируемыйДокумент = ТекстВвестиДокумент();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьПараметрыВыбора(Элемент)
	Элемент.ПараметрыВыбора = Новый ФиксированныйМассив(Новый Массив);
КонецПроцедуры

&НаКлиенте
Процедура ВопросПриИзмененииОрганизацииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Для каждого Проводка Из Объект.Движения.Международный Цикл
			НастройкаСчетовУчетаКлиент.ОбработкаПроводокПриИзмененииОрганизации(Проводка, ТипыСвязанныеСОрганизацией, "Дт");
			НастройкаСчетовУчетаКлиент.ОбработкаПроводокПриИзмененииОрганизации(Проводка, ТипыСвязанныеСОрганизацией, "Кт"); 
			НастройкаСчетовУчетаКлиент.ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект, Проводка, Объект.Организация, "Дт");
			НастройкаСчетовУчетаКлиент.ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект, Проводка, Объект.Организация, "Кт");
		КонецЦикла;
		Для каждого Проводка Из Объект.Движения.МеждународныйБезКорреспонденции Цикл
			НастройкаСчетовУчетаКлиент.ОбработкаПроводокПриИзмененииОрганизации(Проводка, ТипыСвязанныеСОрганизацией, ""); 
			НастройкаСчетовУчетаКлиент.ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект, Проводка, Объект.Организация, "");
		КонецЦикла;
		ТекущаяОрганизация = Объект.Организация;
		ОрганизацияПриИзмененииНаСервере();
	Иначе
		Объект.Организация = ТекущаяОрганизация;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПланаСчетовВопрос(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = КодВозвратаДиалога.ОК Тогда
		Объект.Движения.Международный.Очистить();
		Объект.Движения.МеждународныйБезКорреспонденции.Очистить();
		ТекущийПланСчетов = Объект.ПланСчетов;
		ПланСчетовПриИзмененииНаСервере();
	Иначе
		Объект.ПланСчетов = ТекущийПланСчетов;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьЗаголовкиАннулирующихКоманд()
	
	НастройкаФормированияПроводок = МеждународныйУчетСерверПовтИсп.ИспользуемаяНастройкаФормированияПроводок(
		НачалоМесяца(Объект.Дата), Объект.ПланСчетов, Объект.Организация);
	
	СпособАннулированияПроводок = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НастройкаФормированияПроводок, "СпособАннулированияПроводок");
	
	Если СпособАннулированияПроводок = Перечисления.СпособыАннулированияПроводокМеждународногоУчета.РеверсивнымиПроводками Тогда
		Элементы.ВыйтиИзСписка.Заголовок = НСтр("ru = 'Реверс одного документа';
												|en = 'Reverse of a document'");
		Элементы.ПерейтиВСписок.Заголовок = НСтр("ru = 'Реверс списка документов';
												|en = 'Reverse of the document list'");
		ЗаголовокЗагрузкаСторноПроводок = НСтр("ru = 'Реверсивными проводками по документам';
												|en = 'Reversing entries by documents'");
		Элементы.ДвиженияМеждународныйЗагрузитьПроводкиСторно.Заголовок = ЗаголовокЗагрузкаСторноПроводок;
		Элементы.МеждународныйБезКорреспонденцииЗагрузитьПроводкиСторно.Заголовок = ЗаголовокЗагрузкаСторноПроводок;
	Иначе
		Элементы.ВыйтиИзСписка.Заголовок = НСтр("ru = 'Сторно одного документа';
												|en = 'One document storno'");
		Элементы.ПерейтиВСписок.Заголовок = НСтр("ru = 'Сторно списка документов';
												|en = 'Document list storno'");
		ЗаголовокЗагрузкаСторноПроводок = НСтр("ru = 'Сторно проводками по документам';
												|en = 'Storno entries by documents'");
		Элементы.ДвиженияМеждународныйЗагрузитьПроводкиСторно.Заголовок = ЗаголовокЗагрузкаСторноПроводок;
		Элементы.МеждународныйБезКорреспонденцииЗагрузитьПроводкиСторно.Заголовок = ЗаголовокЗагрузкаСторноПроводок;
	КонецЕсли;
	
	МеждународныйУчетОбщегоНазначения.ДобавитьТипыАннулирующихПроводок(СпособАннулированияПроводок,
		Элементы.ТипПроводки.СписокВыбора);
	МеждународныйУчетОбщегоНазначения.ДобавитьТипыАннулирующихПроводок(СпособАннулированияПроводок,
		Элементы.МеждународныйБезКорреспонденцииТипПроводки.СписокВыбора);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура РассчитатьИтогиПоПроводкамБезКорреспонденции(ЭтаФорма)
	
	Элементы = ЭтаФорма.Элементы;
	
	ЭтаФорма.ИтогСуммаДт = 0;
	ЭтаФорма.ИтогСуммаКт = 0;
	ЭтаФорма.ИтогСуммаПредставленияДт = 0;
	ЭтаФорма.ИтогСуммаПредставленияКт = 0;
	Для каждого Запись Из ЭтаФорма.Объект.Движения.МеждународныйБезКорреспонденции Цикл
		ЭтаФорма.ИтогСуммаДт = ЭтаФорма.ИтогСуммаДт + ?(Запись.ВидДвижения = ЭтаФорма.ВидДвиженияДебет, Запись.Сумма, 0);
		ЭтаФорма.ИтогСуммаКт = ЭтаФорма.ИтогСуммаКт + ?(Запись.ВидДвижения = ЭтаФорма.ВидДвиженияКредит, Запись.Сумма, 0);
		ЭтаФорма.ИтогСуммаПредставленияДт = ЭтаФорма.ИтогСуммаПредставленияДт + ?(Запись.ВидДвижения = ЭтаФорма.ВидДвиженияДебет, Запись.СуммаПредставления, 0);
		ЭтаФорма.ИтогСуммаПредставленияКт = ЭтаФорма.ИтогСуммаПредставленияКт + ?(Запись.ВидДвижения = ЭтаФорма.ВидДвиженияКредит, Запись.СуммаПредставления, 0);
	КонецЦикла;
	
	Если ЭтаФорма.ИтогСуммаДт <> ЭтаФорма.ИтогСуммаКт Тогда
		Элементы.СуммаДт.ЦветТекстаПодвала = ЭтаФорма.ПросроченныеДанныеЦвет;
		Элементы.СуммаКт.ЦветТекстаПодвала = ЭтаФорма.ПросроченныеДанныеЦвет;
	Иначе
		Элементы.СуммаДт.ЦветТекстаПодвала = Новый Цвет();
		Элементы.СуммаКт.ЦветТекстаПодвала = Новый Цвет();
	КонецЕсли;
	
	Если ЭтаФорма.ИтогСуммаПредставленияДт <> ЭтаФорма.ИтогСуммаПредставленияКт Тогда
		Элементы.СуммаПредставленияДт.ЦветТекстаПодвала = ЭтаФорма.ПросроченныеДанныеЦвет;
		Элементы.СуммаПредставленияКт.ЦветТекстаПодвала = ЭтаФорма.ПросроченныеДанныеЦвет;
	Иначе
		Элементы.СуммаПредставленияДт.ЦветТекстаПодвала = Новый Цвет();
		Элементы.СуммаПредставленияКт.ЦветТекстаПодвала = Новый Цвет();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВспомогательныеРеквизитыПроводокБезКорреспонденции()
	
	Для каждого Запись Из Объект.Движения.МеждународныйБезКорреспонденции Цикл
		ЗаполнитьВспомогательныеРеквизитыПроводки(Запись, ЭтотОбъект);
	КонецЦикла;
	РассчитатьИтогиПоПроводкамБезКорреспонденции(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьВспомогательныеРеквизитыПроводки(Запись, ЭтаФорма)
	
	Запись.СуммаДт = ?(Запись.ВидДвижения = ЭтаФорма.ВидДвиженияДебет, Запись.Сумма, 0);
	Запись.СуммаКт = ?(Запись.ВидДвижения = ЭтаФорма.ВидДвиженияКредит, Запись.Сумма, 0);
	Запись.СуммаПредставленияДт = ?(Запись.ВидДвижения = ЭтаФорма.ВидДвиженияДебет, Запись.СуммаПредставления, 0);
	Запись.СуммаПредставленияКт = ?(Запись.ВидДвижения = ЭтаФорма.ВидДвиженияКредит, Запись.СуммаПредставления, 0);
	Запись.ВидДвиженияИндекс = ВидДвиженияИндексКартинки(Запись.ВидДвижения, Запись.Активность, Запись.Сумма, ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ВидДвиженияИндексКартинки(ВидДвижения, Активность, Сумма, ЭтаФормы)
	
	Если ВидДвижения = ЭтаФормы.ВидДвиженияДебет И Сумма <> 0 Тогда
		Возврат ?(Активность, 1, 3);
	ИначеЕсли ВидДвижения = ЭтаФормы.ВидДвиженияКредит И Сумма <> 0 Тогда
		Возврат ?(Активность, 2, 4);
	Иначе
		Возврат 0;
	КонецЕсли;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстВвестиДокумент()
	Возврат НСтр("ru = 'Ввести документ';
				|en = 'Enter document'");
КонецФункции;

#КонецОбласти
