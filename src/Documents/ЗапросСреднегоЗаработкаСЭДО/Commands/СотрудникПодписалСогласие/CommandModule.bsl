#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(МассивСсылок, ПараметрыВыполненияКоманды)
	Если МассивСсылок.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Результат = ВыполнитьКоманду(МассивСсылок);
	
	Оповещение     = Результат.Оповещение;
	Предупреждение = Результат.Предупреждение;
	Если Оповещение.Выводить Тогда
		Если ЗначениеЗаполнено(Предупреждение.Подробно) Тогда
			Обработчик = Новый ОписаниеОповещения("ПоказатьПодробности", ЭтотОбъект, Результат);
		Иначе
			Обработчик = Неопределено;
		КонецЕсли;
		ПоказатьОповещениеПользователя(Оповещение.Заголовок, Обработчик, Оповещение.Текст, Оповещение.Картинка);
	ИначеЕсли ЗначениеЗаполнено(Предупреждение.Кратко) Тогда
		Кнопки = Новый СписокЗначений;
		Если ЗначениеЗаполнено(Предупреждение.Подробно) Тогда
			Кнопки.Добавить("Подробнее", НСтр("ru = 'Подробнее';
												|en = 'Details'"));
		КонецЕсли;
		Кнопки.Добавить(0, НСтр("ru = 'Закрыть';
								|en = 'Close'"));
		Обработчик = Новый ОписаниеОповещения("ПоказатьТекстЗавершение", ЭтотОбъект, Результат);
		ПоказатьВопрос(Обработчик, Предупреждение.Кратко, Кнопки, , 0, Предупреждение.Заголовок);
	ИначеЕсли ЗначениеЗаполнено(Предупреждение.Подробно) Тогда
		ПоказатьПодробности(Результат);
	КонецЕсли;
	
	СтандартныеПодсистемыКлиент.ОповеститьФормыОбИзменении(Результат.ОповещенияФорм);
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьПодробности(Результат) Экспорт
	Предупреждение = Результат.Предупреждение;
	ИнформированиеПользователяКлиент.ПоказатьПодробности(Предупреждение.Подробно, Предупреждение.Заголовок, Предупреждение.Картинка);
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьТекстЗавершение(Ответ, Результат) Экспорт
	Если Ответ = "Подробнее" Тогда
		ПоказатьПодробности(Результат);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ВыполнитьКоманду(Знач МассивСсылок)
	Результат = ПустойРезультат();
	
	ОбщееКоличество         = МассивСсылок.Количество();
	КоличествоПодписано     = 0;
	КоличествоУжеПодписано  = 0;
	КоличествоУжеОтправлено = 0;
	ИзмененныеОбъекты       = Новый Массив;
	Ошибки                  = Новый Массив;
	КоличествоОшибок        = 0;
	
	ИменаПолей = "СотрудникПодписалСогласие, ДатаОтправки";
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивСсылок, ИменаПолей, Истина);
	Для Каждого Ссылка Из МассивСсылок Цикл
		Реквизиты = ЗначенияРеквизитов[Ссылка];
		Если Реквизиты.СотрудникПодписалСогласие Тогда
			КоличествоУжеПодписано = КоличествоУжеПодписано + 1;
			Продолжить;
		КонецЕсли;
		Если ЗначениеЗаполнено(Реквизиты.ДатаОтправки) Тогда
			КоличествоУжеОтправлено = КоличествоУжеОтправлено + 1;
			Продолжить;
		КонецЕсли;
		Объект = Ссылка.ПолучитьОбъект();
		Объект.СотрудникПодписалСогласие = Истина;
		Попытка
			СЭДОФСС.ЗаписатьДокумент(Объект, Истина, Истина);
			КоличествоПодписано = КоличествоПодписано + 1;
			ИзмененныеОбъекты.Добавить(Ссылка);
		Исключение
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			Подробно = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
			Текст = НСтр("ru = '%1: При включении флажка ""Сотрудник подписал согласие"" возникла ошибка: %2';
						|en = '%1: An error occurred when selecting the ""The employee has signed the consent"" check box: %2'");
			Текст = СтрШаблон(Текст, Ссылка, Подробно);
			Ошибки.Добавить(Текст);
			КоличествоОшибок = КоличествоОшибок + 1;
		КонецПопытки;
	КонецЦикла;
	
	Результат.ОповещенияФорм = СтандартныеПодсистемыСервер.ПодготовитьОповещениеФормОбИзменении(ИзмененныеОбъекты);
	
	Оповещение     = Результат.Оповещение;
	Предупреждение = Результат.Предупреждение;
	
	Если ОбщееКоличество = 1 Тогда
		Если КоличествоПодписано = 1 Тогда
			Оповещение.Выводить = Истина;
			Оповещение.Текст = НСтр("ru = 'Установлена отметка согласия';
									|en = 'The consent mark is set'");
			Оповещение.Картинка = БиблиотекаКартинок.Успешно32;
		ИначеЕсли КоличествоУжеПодписано = 1 Тогда
			Оповещение.Выводить = Истина;
			Оповещение.Текст = НСтр("ru = 'Установка отметки согласия не требуется (уже установлена)';
									|en = 'Setting the consent mark is not required (it is already set)'");
			Оповещение.Картинка = БиблиотекаКартинок.Успешно32;
		ИначеЕсли КоличествоУжеОтправлено = 1 Тогда
			Оповещение.Выводить = Истина;
			Оповещение.Текст = НСтр("ru = 'Установка отметки согласия не требуется (документ уже отправлен в Фонд)';
									|en = 'Setting the consent mark is not required (the document is already sent to the Social Insurance Fund)'");
			Оповещение.Картинка = БиблиотекаКартинок.Информация32;
		Иначе
			Предупреждение.Выводить = Истина;
			Предупреждение.Заголовок = НСтр("ru = 'Не удалось установить отметку согласия';
											|en = 'Cannot set the consent mark'");
		КонецЕсли;
	Иначе
		ПредставлениеСогласий = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
			НСтр("ru = ';%1 согласия;;%1 согласий;%1 согласий;';
				|en = ';%1 consent;;;;%1 consents;'"),
			ОбщееКоличество);
		Текст = СтрШаблон(НСтр("ru = 'Установлена отметка %1';
								|en = 'The %1 mark is set'"), ПредставлениеСогласий);
		Если ОбщееКоличество = КоличествоПодписано Тогда
			Оповещение.Выводить = Истина;
			Оповещение.Текст = СтрШаблон(НСтр("ru = 'Установлена отметка %1';
												|en = 'The %1 mark is set'"), ПредставлениеСогласий);
			Оповещение.Картинка = БиблиотекаКартинок.Успешно32;
		Иначе
			Предупреждение.Выводить = Истина;
			Если КоличествоПодписано = 0 Тогда
				Если КоличествоОшибок = 0 Тогда
					Предупреждение.Заголовок = НСтр("ru = 'Установка отметки согласий не требуется';
													|en = 'Setting the consent mark is not required'");
				Иначе
					Предупреждение.Заголовок = СтрШаблон(НСтр("ru = 'Не удалось установить отметку %1';
																|en = 'Cannot set the %1 mark'"), ПредставлениеСогласий);
				КонецЕсли;
			Иначе
				Предупреждение.Заголовок = СтрШаблон(НСтр("ru = 'Установлена отметка %1 из %2';
															|en = 'Mark is set: %1 out of %2'"), КоличествоПодписано, ПредставлениеСогласий);
			КонецЕсли;
			ПредставлениеДокументов = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
				НСтр("ru = ';%1 документа;;%1 документов;%1 документов;';
					|en = ';%1 document;;;;%1 documents;'"),
				ОбщееКоличество);
			Предупреждение.Кратко = СтрШаблон(
				НСтр("ru = 'Из %1:
					|Установлена отметка согласия: %2
					|Установка отметки не требуется: %3
					|Ошибок: %4';
					|en = 'Out of %1:
					|Consent mark is set: %2
					|Setting the consent mark is not required: %3
					|Errors: %4'"),
				ПредставлениеДокументов,
				КоличествоПодписано,
				КоличествоУжеПодписано + КоличествоУжеОтправлено,
				КоличествоОшибок);
			Если КоличествоОшибок = 0 Тогда
				Оповещение.Выводить     = Истина;
				Оповещение.Текст        = Предупреждение.Заголовок;
				Оповещение.Картинка     = БиблиотекаКартинок.Успешно32;
				Предупреждение.Подробно = Предупреждение.Кратко;
				Предупреждение.Картинка = Оповещение.Картинка;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если КоличествоОшибок > 0 Тогда
		ТекстОшибок = СтрСоединить(Ошибки, Символы.ПС + Символы.ПС);
		Предупреждение.Выводить  = Истина;
		Предупреждение.Подробно  = СокрЛП(Предупреждение.Кратко + Символы.ПС + Символы.ПС + ТекстОшибок);
		Предупреждение.Картинка  = БиблиотекаКартинок.Предупреждение32;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

&НаСервере
Функция ПустойРезультат()
	Результат = Новый Структура("Оповещение, Предупреждение, ОповещенияФорм");
	Результат.Оповещение     = Новый Структура("Выводить, Заголовок, Текст, Картинка");
	Результат.Предупреждение = Новый Структура("Выводить, Заголовок, Кратко, Подробно, Картинка");
	Результат.ОповещенияФорм = Новый Соответствие;
	Результат.Оповещение.Выводить     = Ложь;
	Результат.Предупреждение.Выводить = Ложь;
	Возврат Результат;
КонецФункции

#КонецОбласти


