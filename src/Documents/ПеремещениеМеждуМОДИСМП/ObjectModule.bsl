#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если Не ДополнительныеСвойства.Свойство("НеЗаполнятьТабличнуюЧасть") Тогда
		Товары.Очистить();
		ШтрихкодыУпаковок.Очистить();
	КонецЕсли;
	
	ИнтеграцияИСМППереопределяемый.ОбработкаЗаполненияДокумента(ЭтотОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	
	ЗаполнитьОбъектПоСтатистике();
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	ИнтеграцияИСПереопределяемый.ПередЗаписьюОбъекта(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	ЭтоИндивидуальныйПредприниматель = ОбщегоНазначенияИСВызовСервера.ПартнерОрганизацияЭтоИндивидуальныйПредприниматель(Организация);
	
	Если ЭтоИндивидуальныйПредприниматель Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("Получатель");
		
	Иначе
		
		МассивНепроверяемыхРеквизитов.Добавить("ОтправительАдрес");
		МассивНепроверяемыхРеквизитов.Добавить("ОтправительКодФИАС");
		
		МассивНепроверяемыхРеквизитов.Добавить("ПолучательАдрес");
		МассивНепроверяемыхРеквизитов.Добавить("ПолучательКодФИАС");
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ИдентификаторЭлектронногоПеревозочногоДокумента)
		И Не (ЗначениеЗаполнено(НомерПеревозочногоДокумента) И ЗначениеЗаполнено(ДатаПеревозочногоДокумента)) Тогда
		
		ТекстОшибки = НСтр("ru = 'Укажите данные ТТН или электронного перевозочного документа.';
							|en = 'Укажите данные ТТН или электронного перевозочного документа.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,
			ЭтотОбъект,
			"ИдентификаторЭлектронногоПеревозочногоДокумента",,
			Отказ);
		
	КонецЕсли;
	
	ИнтеграцияИСМППереопределяемый.ПриОпределенииОбработкиПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбменДаннымиИСМП.ЗаписатьСтатусДокументаПоУмолчанию(ЭтотОбъект);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ДокументОснование           = Неопределено;
	НомерПеревозочногоДокумента = "";
	ДатаПеревозочногоДокумента  = Дата(1, 1, 1);
	НомерТранспортногоСредства  = "";
	ИдентификаторЗаявки         = "";
	ИдентификаторРазрешения     = "";
	
	ШтрихкодыУпаковок.Очистить();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработкаЗаполнения

Процедура ЗаполнитьОбъектПоСтатистике()
	
	ДанныеСтатистики = ЗаполнениеОбъектовПоСтатистикеИСМП.ДанныеЗаполненияПеремещениеМеждуМОД_ИСМП(Организация);
	
	Для Каждого КлючИЗначение Из ДанныеСтатистики Цикл
		ЗаполнениеОбъектовПоСтатистикеИСМП.ЗаполнитьПустойРеквизит(ЭтотОбъект, ДанныеСтатистики, КлючИЗначение.Ключ);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли