#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = Элементы.ГруппаКоманднаяПанель;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	СобытияФорм.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);

	СтандартныеПодсистемыСервер.УстановитьУсловноеОформлениеПоляДата(ЭтотОбъект);

	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	
	Если НЕ ЗначениеЗаполнено(УведомлениеОКонтролируемыхСделках)
		И Параметры.Свойство("УведомлениеОКонтролируемыхСделках") Тогда
			
		УведомлениеОКонтролируемыхСделках = Параметры.УведомлениеОКонтролируемыхСделках;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(УведомлениеОКонтролируемыхСделках) Тогда
		ПараметрыУведомления = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(УведомлениеОКонтролируемыхСделках,
			"Организация, ОтчетныйГод");
			
		ОтборОрганизация = ПараметрыУведомления.Организация;
		ОтборОтчетныйГод = Год(ПараметрыУведомления.ОтчетныйГод);
		
		Элементы.УведомлениеОКонтролируемойСделке.Видимость = Ложь;
		Элементы.Организация.Видимость = Ложь;
		Элементы.ГруппаОтборОрганизация.Видимость = Ложь;
		Элементы.ГруппаОтчетныйГод.Видимость = Ложь;
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ОтборОтчетныйГод) Тогда
		ОтборОтчетныйГод = Год(ТекущаяДатаСеанса());
	КонецЕсли;
	
	УстановитьЗаголовокФормы();
	
	МожноРедактировать = ПравоДоступа("Редактирование", Метаданные.Документы.ЦепочкаСозданияСтоимостиКонтролируемойСделки);
	Элементы.СписокКонтекстноеМенюИзменитьВыделенные.Видимость = МожноРедактировать;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	УстановитьВосстановленныеОтборы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборОрганизацияИспользованиеПриИзменении(Элемент)
	
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Организация");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияПриИзменении(Элемент)
	
	ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Организация");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОтчетныйГодИспользованиеПриИзменении(Элемент)
	
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "ОтчетныйГод");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОтчетныйГодПриИзменении(Элемент)
	
	ОтборОтчетныйГодИспользование = ЗначениеЗаполнено(ОтборОтчетныйГод);
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "ОтчетныйГод");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборТипСделкиИспользованиеПриИзменении(Элемент)
	
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "ТипСделки");
	
КонецПроцедуры

&НаКлиенте
Процедура ТипСделкиОтборПриИзменении(Элемент)
	
	ОтборТипСделкиИспользование = ЗначениеЗаполнено(ОтборТипСделки);
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "ТипСделки");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокСделок(Команда)
	
	ПараметрыОткрытия = Новый Структура;
	
	ПараметрыОткрытия.Вставить("Организация", ОтборОрганизация);
	ПараметрыОткрытия.Вставить("ОтчетныйГод", ОтборОтчетныйГод);
	
	ОткрытьФорму("Отчет.СписокЦепочекСозданияСтоимостиКонтролируемыхСделок.Форма.ФормаОтчета",
		ПараметрыОткрытия, ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайла(Команда)
	
	ОткрытьФорму("Документ.ЦепочкаСозданияСтоимостиКонтролируемойСделки.Форма.ФормаЗагрузки",, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьЗаголовокФормы()
	
	ТекстЗаголовка = НСтр("ru = 'Цепочки создания стоимости';
							|en = 'Цепочки создания стоимости'");
	
	Если ЗначениеЗаполнено(УведомлениеОКонтролируемыхСделках) Тогда
		ТекстЗаголовка = КонтролируемыеСделки.ПредставлениеУведомления(
			УведомлениеОКонтролируемыхСделках, ТекстЗаголовка);
	КонецЕсли;
	
	Заголовок = ТекстЗаголовка;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВосстановленныеОтборы()
	
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "ТипСделки");
	
КонецПроцедуры

#Область СлужебныеПроцедурыИФункцииБСП

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти

