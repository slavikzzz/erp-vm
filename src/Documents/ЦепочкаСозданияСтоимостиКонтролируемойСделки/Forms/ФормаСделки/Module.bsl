#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры.ДанныеЗаполнения);
	
	СтранаРоссия = Справочники.СтраныМира.Россия;
	
	НастроитьФормуПоИспользованиюТовара(ЭтотОбъект);
	
	НастроитьФормуПоПризнакуУчастника(ЭтотОбъект);
	
	НастроитьДоступностьОКТМО(ЭтотОбъект);
	
	СписокКодовПоставки = КонтролируемыеСделки.ПолучитьКодыУсловийПоставки();
	Элементы.КодУсловийПоставки.СписокВыбора.Очистить();
	Для Каждого Код Из СписокКодовПоставки Цикл
		НовыйКод = Элементы.КодУсловийПоставки.СписокВыбора.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйКод, Код);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив();
	
	НезаполняемыеПоля = НезаполняемыеПоля(ЭтотОбъект);
	Для Каждого ИмяПоля Из НезаполняемыеПоля Цикл
		МассивНепроверяемыхРеквизитов.Добавить(ИмяПоля);
	КонецЦикла;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИспользованиеПроисхождениеТовараПриИзменении(Элемент)
	
	НастроитьФормуПоИспользованиюТовара(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПризнакУчастникаСделкиПриИзменении(Элемент)
	
	НастроитьФормуПоПризнакуУчастника(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СтранаСовершенияСделкиПриИзменении(Элемент)
	
	НастроитьДоступностьОКТМО(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	Если ПроверитьЗаполнение() Тогда
		Модифицированность = Ложь;
		СтруктураЗакрытия = ВернутьСтруктуруЗакрытия();
		Закрыть(СтруктураЗакрытия);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ВернутьСтруктуруЗакрытия()
	
	Структура = Новый Структура();
	
	Для Каждого ИмяРеквизита Из ВозвращаемыеРеквизиты() Цикл
		Структура.Вставить(ИмяРеквизита, ЭтотОбъект[ИмяРеквизита]);
	КонецЦикла;
	
	ОчиститьЛишниеПоля(Структура);
	
	Возврат Структура;
	
КонецФункции

&НаСервере
Функция ВозвращаемыеРеквизиты()
	
	ВозвращаемыеРеквизиты = Новый Массив;
	ВозвращаемыеРеквизиты.Добавить("ИспользованиеПроисхождениеТовара");
	ВозвращаемыеРеквизиты.Добавить("НаименованиеИспользованияПроисхожденияТовара");
	ВозвращаемыеРеквизиты.Добавить("ДатаСовершенияСделки");
	ВозвращаемыеРеквизиты.Добавить("ПризнакУчастникаСделки");
	ВозвращаемыеРеквизиты.Добавить("Контрагент");
	ВозвращаемыеРеквизиты.Добавить("УчастникиВзаимозависимы");
	ВозвращаемыеРеквизиты.Добавить("НомерДоговора");
	ВозвращаемыеРеквизиты.Добавить("ДатаДоговора");
	ВозвращаемыеРеквизиты.Добавить("КодУсловийПоставки");
	ВозвращаемыеРеквизиты.Добавить("Количество");
	ВозвращаемыеРеквизиты.Добавить("ЕдиницаИзмерения");
	ВозвращаемыеРеквизиты.Добавить("Цена");
	ВозвращаемыеРеквизиты.Добавить("Валюта");
	ВозвращаемыеРеквизиты.Добавить("Стоимость");
	ВозвращаемыеРеквизиты.Добавить("СтранаСовершенияСделки");
	ВозвращаемыеРеквизиты.Добавить("ОКТМОСовершенияСделки");
	
	Возврат ВозвращаемыеРеквизиты;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НезаполняемыеПоля(Форма)
	
	НезаполняемыеПоля = Новый Массив;
	
	Если Форма.ИспользованиеПроисхождениеТовара <> ПредопределенноеЗначение("Перечисление.ТипыИспользованияПроисхожденияТовараКонтролируемыхСделок.Иное") Тогда
		НезаполняемыеПоля.Добавить("НаименованиеИспользованияПроисхожденияТовара");
	КонецЕсли;
	
	Если Не КонтролируемыеСделкиКлиентСервер.НужноЗаполнятьСведенияОСделкеЛиста1В(Форма.ИспользованиеПроисхождениеТовара) Тогда
		НезаполняемыеПоля.Добавить("ДатаСовершенияСделки");
		НезаполняемыеПоля.Добавить("ПризнакУчастникаСделки");
		НезаполняемыеПоля.Добавить("Контрагент");
		НезаполняемыеПоля.Добавить("НомерДоговора");
		НезаполняемыеПоля.Добавить("ДатаДоговора");
		НезаполняемыеПоля.Добавить("КодУсловийПоставки");
		НезаполняемыеПоля.Добавить("Количество");
		НезаполняемыеПоля.Добавить("ЕдиницаИзмерения");
		НезаполняемыеПоля.Добавить("Цена");
		НезаполняемыеПоля.Добавить("Валюта");
		НезаполняемыеПоля.Добавить("Стоимость");
		НезаполняемыеПоля.Добавить("СтранаСовершенияСделки");
		НезаполняемыеПоля.Добавить("ОКТМОСовершенияСделки");
	ИначеЕсли КонтролируемыеСделкиКлиентСервер.ЭтоРозничнаяПродажаЦепочкиСделок(Форма.ПризнакУчастникаСделки) Тогда
		НезаполняемыеПоля.Добавить("ДатаСовершенияСделки");
		НезаполняемыеПоля.Добавить("Контрагент");
		НезаполняемыеПоля.Добавить("НомерДоговора");
		НезаполняемыеПоля.Добавить("ДатаДоговора");
		НезаполняемыеПоля.Добавить("КодУсловийПоставки");
		НезаполняемыеПоля.Добавить("Цена");
		НезаполняемыеПоля.Добавить("СтранаСовершенияСделки");
		НезаполняемыеПоля.Добавить("ОКТМОСовершенияСделки");
	КонецЕсли;
	
	Возврат НезаполняемыеПоля;
	
КонецФункции

&НаКлиенте
Процедура ОчиститьЛишниеПоля(ВозвращаемыйЛист1Б)
	
	НезаполняемыеПоля = НезаполняемыеПоля(ЭтотОбъект);
	Для Каждого ИмяПоля Из НезаполняемыеПоля Цикл
		ВозвращаемыйЛист1Б[ИмяПоля] = Неопределено;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьФормуПоИспользованиюТовара(Форма)
	
	Элементы = Форма.Элементы;
	ИспользованиеПроисхождениеТовара = Форма.ИспользованиеПроисхождениеТовара;
	
	Элементы.НаименованиеИспользованияПроисхожденияТовара.Видимость =
		ИспользованиеПроисхождениеТовара = ПредопределенноеЗначение("Перечисление.ТипыИспользованияПроисхожденияТовараКонтролируемыхСделок.Иное");
	Элементы.ГруппаСведенияОСделке.Видимость = КонтролируемыеСделкиКлиентСервер.НужноЗаполнятьСведенияОСделкеЛиста1В(ИспользованиеПроисхождениеТовара);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьДоступностьОКТМО(Форма)
	
	Форма.Элементы.ОКТМОСовершенияСделки.Доступность = Форма.СтранаСовершенияСделки = Форма.СтранаРоссия;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьФормуПоПризнакуУчастника(Форма)
	
	Элементы = Форма.Элементы;
	
	ПризнакУчастникаСделки = Форма.ПризнакУчастникаСделки;
	
	ЭтоРозничнаяПродажа = КонтролируемыеСделкиКлиентСервер.ЭтоРозничнаяПродажаЦепочкиСделок(ПризнакУчастникаСделки);
	Элементы.Контрагент.Видимость = Не ЭтоРозничнаяПродажа;
	Элементы.ГруппаНомерДатаДоговора.Видимость = Не ЭтоРозничнаяПродажа;
	Элементы.ДатаСовершенияСделки.Видимость = Не ЭтоРозничнаяПродажа;
	Элементы.КодУсловийПоставки.Видимость = Не ЭтоРозничнаяПродажа;
	Элементы.Цена.Видимость = Не ЭтоРозничнаяПродажа;
	Элементы.Валюта.ПоложениеЗаголовка = ?(ЭтоРозничнаяПродажа,
		ПоложениеЗаголовкаЭлементаФормы.Авто, ПоложениеЗаголовкаЭлементаФормы.Нет);
	Элементы.СтранаСовершенияСделки.Видимость = Не ЭтоРозничнаяПродажа;
	Элементы.ОКТМОСовершенияСделки.Видимость = Не ЭтоРозничнаяПродажа;
	
КонецПроцедуры

#КонецОбласти

