#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если Не ДополнительныеСвойства.Свойство("СинхронизацияКлючаСоЗначением")
		И Не ПометкаУдаления Тогда

		// Документ самостоятельно не используется.

		Отказ = Истина;
		ТекстОшибки = НСтр("ru = 'Запись этого объекта возможна только из служебных механизмов';
							|en = 'Recording of this object is possible only from the internal application algorithms'");
		ОбщегоНазначения.СообщитьПользователю(
			ТекстОшибки,
			ЭтотОбъект,
			,
			,
			Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ГрафикПоступленияТоваров.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.ЗапасыИПотребности КАК ГрафикПоступленияТоваров
	|ГДЕ
	|	ГрафикПоступленияТоваров.Регистратор = &Регистратор";
	
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	Если Не Запрос.Выполнить().Пустой() Тогда
		
		ТекстИсключения = НСтр("ru = 'По документу %Документ% есть движения в регистре ""Запасы и потребности"". Удаление документа невозможно.';
								|en = 'There are records in the ""Inventory and demands"" register for the %Документ% document. Cannot delete the document.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%Документ%", Ссылка);
		
		ВызватьИсключение ТекстИсключения;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли