
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Свойства = "ТипСверки, ВидУслуги, Организация, КоличествоКПП, УИДРодителя";
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, Свойства);
	КоличествоКПП = Строка(Параметры.КоличествоКПП);
	ТаблицаИзАдресаВРеквизит(Параметры.Адрес);
	
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура ТаблицаИзАдресаВРеквизит(Адрес)

	Таблица = ПолучитьИзВременногоХранилища(Адрес);
	ЗначениеВРеквизитФормы(Таблица, "ПереченьКПП");

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КоличествоВсеПриИзменении(Элемент)
	
	ПереченьКПП.Очистить();
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоОдинПриИзменении(Элемент)
	
	ПереченьКПП.Очистить();
	ПереченьКПП.Добавить();
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоНесколькоПриИзменении(Элемент)
	
	ПереченьКПП.Очистить();
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура КППНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ВыбратьИзРегистрации("КПП", "КПП");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)

	Результат = Новый Структура();
	Результат.Вставить("НовоеКоличествоКПП", ПредопределенноеЗначение("Перечисление.КоличествоЭлементовДляСверкиИОН." + КоличествоКПП));
	Результат.Вставить("Адрес", АдресТаблицы());
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура Подобрать(Команда)
	
	ВыбратьИзРегистрации("КПП", "КПП");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УправлениеФормой()
	
	ЭтоВсе  = КоличествоКПП = НСтр("ru = 'Все';
									|en = 'Все'");
	ЭтоОдин = КоличествоКПП = НСтр("ru = 'Один';
									|en = 'Один'");
	ЭтоНесколько = КоличествоКПП = НСтр("ru = 'Несколько';
										|en = 'Несколько'");
	
	Элементы.КПП.Видимость = ЭтоОдин;
	
	Если ЭтоНесколько Тогда
		Элементы.СтраницыНесколько.ТекущаяСтраница = Элементы.СтраницаТаблица;
	Иначе
		Элементы.СтраницыНесколько.ТекущаяСтраница = Элементы.СтраницаПустая;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПеренумероватьСтроки(Таблица)
	
	НомерСтроки = 1;
	Для каждого СтрокаТаблицы Из Таблица Цикл
		СтрокаТаблицы.НомерСтроки = НомерСтроки;
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция АдресТаблицы()

	Таблица = ПереченьКПП.Выгрузить();
	Адрес = ПоместитьВоВременноеХранилище(Таблица, УИДРодителя);
	
	Возврат Адрес;

КонецФункции

&НаКлиенте
Процедура ВыбратьИзРегистрации(Имя, ПолеРегистрации)
	
	ЗначениеОтбора = Новый Структура("Владелец", Организация);
	ВходящийКонтекст = Новый Структура("Отбор", ЗначениеОтбора);
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("Имя", Имя);
	ДополнительныеПараметры.Вставить("ПолеРегистрации", ПолеРегистрации);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ВыбратьИзРегистрации_Завершение", 
		ЭтотОбъект, 
		ДополнительныеПараметры);

	ОткрытьФорму("Справочник.РегистрацииВНалоговомОргане.ФормаВыбора", ВходящийКонтекст,,,,,ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьИзРегистрации_Завершение(Результат, ВходящийКонтекст) Экспорт
	
	ДобавитьВТаблицуИзРегистраций(Результат, ВходящийКонтекст.Имя, ВходящийКонтекст.ПолеРегистрации);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьВТаблицуИзРегистраций(Регистрации, Имя, ПолеРегистрации) Экспорт
	
	Если ТипЗнч(Регистрации) = Тип("Массив") Тогда
		
		Для каждого Регистрация Из Регистрации Цикл
		
			ДобавитьВТаблицуИзРегистрации(Регистрация, Имя, ПолеРегистрации);
		
		КонецЦикла;
		
	ИначеЕсли ТипЗнч(Регистрации) = Тип("СправочникСсылка.РегистрацииВНалоговомОргане") Тогда
		
		ДобавитьВТаблицуИзРегистрации(Регистрации, Имя, ПолеРегистрации);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьВТаблицуИзРегистрации(Регистрация, Имя, ПолеРегистрации) Экспорт
	
	НовоеЗначение = Регистрация[ПолеРегистрации];
	
	Если ЗначениеЗаполнено(НовоеЗначение) Тогда
		
		Если КоличествоКПП = "Один" Тогда
			
			// Чтобы 2,3 и т.д. строки в таблицу не добавлялись
			ПереченьКПП.Очистить();
			ДобавитьСтрокуТаблицыКПП(НовоеЗначение);
			
		Иначе
			Отбор = Новый Структура();
			Отбор.Вставить(Имя, НовоеЗначение);
			
			НайденныеСтроки = ПереченьКПП.НайтиСтроки(Отбор);
				
			Если НайденныеСтроки.Количество() = 0 Тогда
				
				ДобавитьСтрокуТаблицыКПП(НовоеЗначение);
				
			Иначе
				
				Текст = НСтр("ru = '%1 %2 не добавлено, поскольку уже есть в таблице';
							|en = '%1 %2 не добавлено, поскольку уже есть в таблице'");
				Текст = СтрШаблон(Текст, Имя, НовоеЗначение);
				ОбщегоНазначения.СообщитьПользователю(Текст);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ПеренумероватьСтроки(ПереченьКПП);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьСтрокуТаблицыКПП(НовоеЗначение)
	
	НоваяСтрока = ПереченьКПП.Добавить();
	НоваяСтрока.КПП = НовоеЗначение;
	
КонецПроцедуры

&НаКлиенте
Процедура ПереченьКПППриИзменении(Элемент)
	ПеренумероватьСтроки(ПереченьКПП);
КонецПроцедуры


#КонецОбласти
