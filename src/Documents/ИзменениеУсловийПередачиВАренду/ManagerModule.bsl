#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	МеханизмыДокумента.Добавить("ОсновныеСредства");
	МеханизмыДокумента.Добавить("РеестрДокументов");
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	МеханизмыДокумента.Добавить("УчетДоходовРасходов");

	ИзменениеУсловийПередачиВАрендуЛокализация.ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента);
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка, ДокументОбъект - ссылка на документ или объект, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура:
//     * Ключ - Строка - Имя таблицы.
//     * Значение - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Если ТипЗнч(Документ) = Тип("ДокументОбъект.ИзменениеУсловийПередачиВАренду") Тогда
		ДокументСсылка = Документ.Ссылка;
	Иначе
		ДокументСсылка = Документ;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда

		ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДопПараметры);
		
		ТекстЗапросаТаблицаГрафикОплатУслугПоДоходнойАренде(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаГрафикНачисленияУслугПоДоходнойАренде(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаГрафикНачисленияПроцентовПоДоходнойАренде(ТекстыЗапроса, Регистры);
		
		ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры);
	
		//++ НЕ УТКА
		ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаОтражениеДокументовВМеждународномУчете(ТекстыЗапроса, Регистры);
		//-- НЕ УТКА
	
		ИзменениеУсловийПередачиВАрендуЛокализация.ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры);
	
	КонецЕсли;
	
	ТаблицыДвижений = ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
	Возврат ТаблицыДвижений;
	
КонецФункции

// Формирует таблицы движений при выполнении операции "Расчет стоимости инвестиции в аренду".
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ПередачаОСВАренду2_4 - Документ, для которого формируются движения
//  МенеджерВременныхТаблиц	- МенеджерВременныхТаблиц - Содержит вспомогательные временные таблицы, которые могут использоваться для формирования движений.
//
// Возвращаемое значение:
//  см. ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения
//
Функция ТаблицыДвиженийПриРасчетеСтоимостиИнвестицииВАренду(ДокументСсылка, МенеджерВременныхТаблиц) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, Неопределено);
	
	РасчетСтоимостиИнвестицииВАренду.УстановитьПараметрыЗапроса(Запрос);
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализацииПоПериодам(Запрос, Запрос.Параметры,, "ВтСписокДокументов");
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	РасчетСтоимостиИнвестицииВАренду.ТекстЗапросаТаблицаПрочиеРасходы(ТекстыЗапроса);
	РасчетСтоимостиИнвестицииВАренду.ТекстЗапросаТаблицаГрафикНачисленияПроцентовПоДоходнойАренде(ТекстыЗапроса);
	ТекстЗапросаТаблицаИнвестицииВАренду(Запрос, ТекстыЗапроса, Неопределено);
	
	ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	ДопПараметры.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ДопПараметры.ТолькоПомеченные = Истина;
	
	ТаблицыДляДвижений = ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
	Возврат ТаблицыДляДвижений;
	
КонецФункции

#КонецОбласти

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
КонецПроцедуры

// Добавляет команду создания документа "Передача ОС в аренду".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
// Возвращаемое значение:
//	СтрокаТаблицыЗначений - добавленная команда
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании, Порядок = 0) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.ИзменениеУсловийПередачиВАренду) Тогда
		
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.ИзменениеУсловийПередачиВАренду.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.ИзменениеУсловийПередачиВАренду);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользуетсяПередачаВАрендуСубарендуПоФСБУ25";
		КомандаСоздатьНаОсновании.Порядок = Порядок;
	
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;

	Возврат Неопределено;
	
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт
	
	ИсточникиДанных = Новый Соответствие;
	Возврат ИсточникиДанных;
	
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.ИзменениеУсловийПередачиВАренду";
	
	ЗначенияПараметров = ЗначенияПараметровПроведения();
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("НомерНаПечать", """""");
	
	ВЗапросеЕстьИсточник = Истина;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	ИначеЕсли ИмяРегистра = "ДокументыПоОС" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';
								|en = 'In document %ПолноеИмяДокумента%, adaptation of request for generating records of register %ИмяРегистра% is not implemented.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов"
		ИЛИ ИмяРегистра = "ДокументыПоОС" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ВЗапросеЕстьИсточник,
										ПереопределениеРасчетаПараметров);
	Иначе	
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ПереопределениеРасчетаПараметров);
	КонецЕсли; 

	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДопПараметры)
	
	ПроведениеДокументов.ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДопПараметры);

	СформироватьТаблицуВтСписокДокументов(Запрос);
	ПолучитьДанныеДокумента(Запрос);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Период  КАК Период,
	|	ДанныеДокумента.Номер   КАК Номер
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения(Реквизиты);
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла; 

	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
КонецПроцедуры

Процедура СформироватьТаблицуВтСписокДокументов(Запрос)
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СписокДокументов.Ссылка КАК Ссылка,
	|	СписокДокументов.Организация КАК Организация,
	|	СписокДокументов.Дата КАК Период
	|ПОМЕСТИТЬ ВтСписокДокументов
	|ИЗ
	|	Документ.ИзменениеУсловийПередачиВАренду КАК СписокДокументов
	|ГДЕ
	|	СписокДокументов.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка";
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПолучитьДанныеДокумента(Запрос)
	
	СписокЗапросов = Новый Массив;
	
	#Область ДанныеДокументаРеквизиты
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Арендатор КАК Арендатор,
	|	ДанныеДокумента.Договор КАК Договор,
	|	ДанныеДокумента.Договор.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ДанныеДокумента.Ответственный КАК Ответственный,
	|	ДанныеДокумента.Комментарий КАК Комментарий,
	|	ДанныеДокумента.Проведен КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления КАК ПометкаУдаления,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИзменениеУсловийПередачиВАренду) КАК ХозяйственнаяОперация
	|ПОМЕСТИТЬ ДанныеДокументаРеквизиты
	|ИЗ
	|	Документ.ИзменениеУсловийПередачиВАренду КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка В
	|			(ВЫБРАТЬ
	|				ВтСписокДокументов.Ссылка
	|			ИЗ
	|				ВтСписокДокументов КАК ВтСписокДокументов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ДанныеДокументаТаблицаОС
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаДокумента.ОсновноеСредство КАК ОбъектУчета,
	|	ТаблицаДокумента.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаДокумента.АрендныеПлатежи КАК АрендныеПлатежи,
	|	ТаблицаДокумента.НГЛСРегл КАК НГЛСРегл,
	|	ТаблицаДокумента.НГЛСУпр КАК НГЛСУпр
	|ПОМЕСТИТЬ ДанныеДокументаТаблицаОС
	|ИЗ
	|	Документ.ИзменениеУсловийПередачиВАренду.ОС КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В
	|			(ВЫБРАТЬ
	|				ВтСписокДокументов.Ссылка
	|			ИЗ
	|				ВтСписокДокументов КАК ВтСписокДокументов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	ОсновноеСредство";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ДанныеДокументаТаблицаГрафикОплатУслуг
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка					КАК Ссылка,
	|	ТаблицаДокумента.Дата					КАК Дата,
	|	ТаблицаДокумента.ОсновноеСредство		КАК ОсновноеСредство,
	|	ТаблицаДокумента.УслугаПоАренде			КАК УслугаПоАренде,
	|	ТаблицаДокумента.УслугаПоАрендеНДС		КАК УслугаПоАрендеНДС
	|
	|ПОМЕСТИТЬ ДанныеДокументаТаблицаГрафикОплатУслуг
	|
	|ИЗ
	|	Документ.ИзменениеУсловийПередачиВАренду.ГрафикОплатУслуг КАК ТаблицаДокумента
	|
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В (
	|		ВЫБРАТЬ
	|			ВтСписокДокументов.Ссылка
	|		ИЗ
	|			ВтСписокДокументов КАК ВтСписокДокументов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	ОсновноеСредство
	|";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ДанныеДокументаТаблицаГрафикНачисленияУслуг
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка					КАК Ссылка,
	|	ТаблицаДокумента.Дата					КАК Дата,
	|	ТаблицаДокумента.ОсновноеСредство		КАК ОсновноеСредство,
	|	ТаблицаДокумента.УслугаПоАренде			КАК УслугаПоАренде,
	|	ТаблицаДокумента.УслугаПоАрендеНДС		КАК УслугаПоАрендеНДС
	|
	|ПОМЕСТИТЬ ДанныеДокументаТаблицаГрафикНачисленияУслуг
	|
	|ИЗ
	|	Документ.ИзменениеУсловийПередачиВАренду.ГрафикНачисленияУслуг КАК ТаблицаДокумента
	|
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В (
	|		ВЫБРАТЬ
	|			ВтСписокДокументов.Ссылка
	|		ИЗ
	|			ВтСписокДокументов КАК ВтСписокДокументов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	ОсновноеСредство
	|";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ДанныеДокументаТаблицаГрафикНачисленияПроцентов
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка					КАК Ссылка,
	|	ТаблицаДокумента.Дата					КАК Дата,
	|	ТаблицаДокумента.ОсновноеСредство		КАК ОсновноеСредство,
	|	ТаблицаДокумента.Проценты				КАК Проценты
	|
	|ПОМЕСТИТЬ ДанныеДокументаТаблицаГрафикНачисленияПроцентов
	|
	|ИЗ
	|	Документ.ИзменениеУсловийПередачиВАренду.ГрафикНачисленияПроцентов КАК ТаблицаДокумента
	|
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В (
	|		ВЫБРАТЬ
	|			ВтСписокДокументов.Ссылка
	|		ИЗ
	|			ВтСписокДокументов КАК ВтСписокДокументов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	ОсновноеСредство
	|";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	Запрос.Текст = СтрСоединить(СписокЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)

	ЗначенияПараметровПроведения = Новый Структура;
	ЗначенияПараметровПроведения.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ИзменениеУсловийПередачиВАренду"));
	ЗначенияПараметровПроведения.Вставить("НазваниеДокумента", НСтр("ru = 'Изменение условий передачи в аренду';
																	|en = 'Change rental terms'"));
	
	ЗначенияПараметровПроведения.Вставить("ИспользоватьУчетПрочихДоходовРасходов",ПолучитьФункциональнуюОпцию("ИспользоватьУчетПрочихДоходовРасходов"));
	ЗначенияПараметровПроведения.Вставить("ИспользоватьУчетПрочихАктивовПассивов",ПолучитьФункциональнуюОпцию("ИспользоватьУчетПрочихАктивовПассивов"));
	ЗначенияПараметровПроведения.Вставить("ИспользоватьУчетПрослеживаемыхИмпортныхТоваров",ПолучитьФункциональнуюОпцию("ИспользоватьУчетПрослеживаемыхИмпортныхТоваров"));
	ЗначенияПараметровПроведения.Вставить("ВедетсяРегламентированныйУчетВНА", ВнеоборотныеАктивыСлужебный.ВедетсяРегламентированныйУчетВНА());
	ЗначенияПараметровПроведения.Вставить("ИдентификаторНеиспользуемойФинЗаписи", ПроведениеДокументов.ИдентификаторНеиспользуемойФинЗаписи());
	ЗначенияПараметровПроведения.Вставить("СтатьяАП_ОС", ПланыВидовХарактеристик.СтатьиАктивовПассивов.ОсновныеСредства);
	ЗначенияПараметровПроведения.Вставить("ИдентификаторНеиспользуемойФинЗаписи", ПроведениеДокументов.ИдентификаторНеиспользуемойФинЗаписи());

	ЗначенияПараметровПроведения.Вставить("ВалютаУпр", ЗначениеНастроекПовтИсп.ВалютаУправленческогоУчета());

	ЗначенияПараметровПроведения.Вставить("ХО_ИзменениеУсловийПередачиВАренду",
		Перечисления.ХозяйственныеОперации.ИзменениеУсловийПередачиВАренду);
	ЗначенияПараметровПроведения.Вставить("НастройкаХО_ИзменениеУсловийПередачиВАренду",
		Справочники.НастройкиХозяйственныхОпераций.ИзменениеУсловийПередачиВАренду);
		
	ЗначенияПараметровПроведения.Вставить("НастройкаХО_ВводОстатковИнвестицииВАренду",
		Справочники.НастройкиХозяйственныхОпераций.ВводОстатковИнвестицииВАренду);
	ЗначенияПараметровПроведения.Вставить("ХО_ВводОстатковИнвестицииВАренду",
		Перечисления.ХозяйственныеОперации.ВводОстатковИнвестицииВАренду);
		
	ЗначенияПараметровПроведения.Вставить("НастройкаХО_ВыделениеНГЛС", Справочники.НастройкиХозяйственныхОпераций.ВыделениеНГЛС);
	ЗначенияПараметровПроведения.Вставить("НастройкаХО_НачислениеПроцентовПоДоходнойАренде",
		Справочники.НастройкиХозяйственныхОпераций.НачислениеПроцентовПоДоходнойАренде);

	ЗначенияПараметровПроведения.Вставить("ХО_ПереоценкаФИ", Перечисления.ХозяйственныеОперации.ПереоценкаФинансовыхИнструментов);
	ЗначенияПараметровПроведения.Вставить("НастройкаХО_ПереоценкаФИ", Справочники.НастройкиХозяйственныхОпераций.ПереоценкаФинансовыхИнструментов);
	
	Если Реквизиты <> Неопределено Тогда
		
		ЗначенияПараметровПроведения.Вставить("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
		
	КонецЕсли; 
	
	Возврат ЗначенияПараметровПроведения;
	
КонецФункции

#Область ТекстыЗапросаДляПроведения

Функция ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДокументыПоОС";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(ТаблицаОС.НомерСтроки-1, 0) КАК НомерЗаписи,
	|	ДанныеДокумента.Дата             КАК Дата,
	|	ДанныеДокумента.Ссылка           КАК Ссылка,
	|	ДанныеДокумента.Организация      КАК Организация,
	|	ДанныеДокумента.Проведен         КАК Проведен,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИзменениеУсловийПередачиВАренду) КАК ХозяйственнаяОперация,
	|	&ИдентификаторМетаданных         КАК ТипСсылки,
	|	ТаблицаОС.ОсновноеСредство       КАК ОсновноеСредство,
	|	ЛОЖЬ                             КАК ДополнительнаяЗапись
	|
	|ИЗ
	|	Документ.ИзменениеУсловийПередачиВАренду КАК ДанныеДокумента
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзменениеУсловийПередачиВАренду.ОС КАК ТаблицаОС
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Дата КАК ДатаДокументаИБ,
	|	ДанныеДокумента.Номер КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных КАК ТипСсылки,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Арендатор КАК Контрагент,
	|	ДанныеДокумента.Договор КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИзменениеУсловийПередачиВАренду) КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ДанныеДокумента.Ответственный КАК Ответственный,
	|	ДанныеДокумента.Комментарий КАК Комментарий,
	|	ДанныеДокумента.Проведен КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления КАК ПометкаУдаления,
	|	ЛОЖЬ КАК ДополнительнаяЗапись,
	|	ДанныеДокумента.Дата КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать КАК НомерПервичногоДокумента,
	|	ЛОЖЬ КАК СторноИсправление,
	|	НЕОПРЕДЕЛЕНО КАК СторнируемыйДокумент,
	|	НЕОПРЕДЕЛЕНО КАК ИсправляемыйДокумент,
	|	ДанныеДокумента.Дата   КАК ДатаОтраженияВУчете,
	|	НЕОПРЕДЕЛЕНО КАК Приоритет
	|
	|ИЗ
	|	Документ.ИзменениеУсловийПередачиВАренду КАК ДанныеДокумента
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ТекстЗапросаТаблицаГрафикОплатУслугПоДоходнойАренде(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ГрафикОплатУслугПоДоходнойАренде";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Период              КАК ДатаИзменения,
	|	ДанныеДокумента.Договор             КАК Договор,
	|	ДанныеДокумента.Ссылка              КАК АктуальныеУсловияДоговора,
	|	ТаблицаГрафика.Дата                 КАК Дата,
	|	ТаблицаГрафика.ОсновноеСредство     КАК ОсновноеСредство,
	|	ТаблицаГрафика.УслугаПоАренде       КАК УслугаПоАренде,
	|	ТаблицаГрафика.УслугаПоАрендеНДС    КАК УслугаПоАрендеНДС
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаГрафикОплатУслуг КАК ТаблицаГрафика
	|		ПО ДанныеДокумента.Ссылка = ТаблицаГрафика.Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаГрафикНачисленияУслугПоДоходнойАренде(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ГрафикНачисленияУслугПоДоходнойАренде";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Период              КАК ДатаИзменения,
	|	ДанныеДокумента.Договор             КАК Договор,
	|	ДанныеДокумента.Ссылка              КАК АктуальныеУсловияДоговора,
	|	ТаблицаГрафика.Дата                 КАК Дата,
	|	ТаблицаГрафика.ОсновноеСредство     КАК ОсновноеСредство,
	|	ТаблицаГрафика.УслугаПоАренде       КАК УслугаПоАренде,
	|	ТаблицаГрафика.УслугаПоАрендеНДС    КАК УслугаПоАрендеНДС
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаГрафикНачисленияУслуг КАК ТаблицаГрафика
	|		ПО ДанныеДокумента.Ссылка = ТаблицаГрафика.Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаГрафикНачисленияПроцентовПоДоходнойАренде(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ГрафикНачисленияПроцентовПоДоходнойАренде";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Период              КАК ДатаИзменения,
	|	ДанныеДокумента.Договор             КАК Договор,
	|	ДанныеДокумента.Ссылка              КАК АктуальныеУсловияДоговора,
	|	ТаблицаГрафика.Дата                 КАК Дата,
	|	ТаблицаГрафика.ОсновноеСредство     КАК ОсновноеСредство,
	|	ТаблицаГрафика.Проценты             КАК Проценты
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаГрафикНачисленияПроцентов КАК ТаблицаГрафика
	|		ПО ДанныеДокумента.Ссылка = ТаблицаГрафика.Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаИнвестицииВАренду(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ИнвестицииВАренду";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтТаблицаОС(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтИзменениеПроцентов(ТекстыЗапроса);
	
	ОтложенноеФормированиеДвиженийВНА.ТекстЗапросаПустыеТаблицыОтложенныхДвижений(Запрос, ТекстыЗапроса);
	
	СписокЗапросовОбъединение = Новый Массив;
	
	#Область ОтнесениеВложенийВАрендуНаЧИА // Дт <58.АП> - Кт <58.ВА>
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                 КАК Регистратор,
	|	ДанныеДокумента.Период                 КАК Период,
	|	ДанныеДокумента.Организация            КАК Организация,
	|	ДанныеДокумента.Договор                КАК Договор,
	|	ДанныеДокумента.ВалютаВзаиморасчетов   КАК Валюта,
	|	ТаблицаОС.ОсновноеСредство             КАК ОсновноеСредство,
	|	ТаблицаОС.ГруппаФинансовогоУчета       КАК ГруппаФинансовогоУчета,
	|	0                                      КАК АрендныеПлатежи,
	|	СуммаРасходов.РасходыРегл              КАК АрендныеПлатежиРегл,
	|	СуммаРасходов.РасходыУпр               КАК АрендныеПлатежиУпр,
	|	0                                      КАК НГЛСРегл,
	|	0                                      КАК НГЛСУпр,
	|	0                                      КАК ПроцентныйДисконт,
	|	0                                      КАК ПроцентныйДисконтРегл,
	|	0                                      КАК ПроцентныйДисконтУпр,
	|	&НастройкаХО_ИзменениеУсловийПередачиВАренду КАК НастройкаХозяйственнойОперации,
	|	ТаблицаОС.ИдентификаторСтроки                КАК ИдентификаторФинЗаписи
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СуммаРасходов КАК СуммаРасходов
	|		ПО ТаблицаОС.ОсновноеСредство = СуммаРасходов.ОсновноеСредство
	|";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ПересчетПроцентовПоНовымУсловиям // Дт <58.АП> - Кт <58.%>
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                 КАК Регистратор,
	|	ДанныеДокумента.Период                 КАК Период,
	|	ДанныеДокумента.Организация            КАК Организация,
	|	ДанныеДокумента.Договор                КАК Договор,
	|	ДанныеДокумента.ВалютаВзаиморасчетов   КАК Валюта,
	|	ТаблицаОС.ОсновноеСредство             КАК ОсновноеСредство,
	|	ТаблицаОС.ГруппаФинансовогоУчета       КАК ГруппаФинансовогоУчета,
	|	0                                      КАК АрендныеПлатежи,
	|	0                                      КАК АрендныеПлатежиРегл,
	|	0                                      КАК АрендныеПлатежиУпр,
	|	0                                      КАК НГЛСРегл,
	|	0                                      КАК НГЛСУпр,
	|	ИзменениеПроцентов.Сумма               КАК ПроцентныйДисконт,
	|	ИзменениеПроцентов.СуммаРегл           КАК ПроцентныйДисконтРегл,
	|	ИзменениеПроцентов.СуммаУпр            КАК ПроцентныйДисконтУпр,
	|	&НастройкаХО_НачислениеПроцентовПоДоходнойАренде КАК НастройкаХозяйственнойОперации,
	|	ТаблицаОС.ИдентификаторСтроки          КАК ИдентификаторФинЗаписи
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втИзменениеПроцентов КАК ИзменениеПроцентов
	|		ПО ТаблицаОС.ОсновноеСредство = ИзменениеПроцентов.ОсновноеСредство
	|		И ДанныеДокумента.Ссылка = ИзменениеПроцентов.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ХозяйственнаяОперация = &ХО_ИзменениеУсловийПередачиВАренду
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                 КАК Регистратор,
	|	ДанныеДокумента.Период                 КАК Период,
	|	ДанныеДокумента.Организация            КАК Организация,
	|	ДанныеДокумента.Договор                КАК Договор,
	|	ДанныеДокумента.ВалютаВзаиморасчетов   КАК Валюта,
	|	ТаблицаОС.ОсновноеСредство             КАК ОсновноеСредство,
	|	ТаблицаОС.ГруппаФинансовогоУчета       КАК ГруппаФинансовогоУчета,
	|	ДанныеОС.ИзменениеАрендныхПлатежей     КАК АрендныеПлатежи,
	|	ИзменениеПроцентов.СуммаРегл           КАК АрендныеПлатежиРегл,
	|	ИзменениеПроцентов.СуммаУпр            КАК АрендныеПлатежиУпр,
	|	0                                      КАК НГЛСРегл,
	|	0                                      КАК НГЛСУпр,
	|	0                                      КАК ПроцентныйДисконт,
	|	0                                      КАК ПроцентныйДисконтРегл,
	|	0                                      КАК ПроцентныйДисконтУпр,
	|	&НастройкаХО_НачислениеПроцентовПоДоходнойАренде КАК НастройкаХозяйственнойОперации,
	|	ТаблицаОС.ИдентификаторСтроки          КАК ИдентификаторФинЗаписи
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втИзменениеПроцентов КАК ИзменениеПроцентов
	|		ПО ТаблицаОС.ОсновноеСредство = ИзменениеПроцентов.ОсновноеСредство
	|		И ДанныеДокумента.Ссылка = ИзменениеПроцентов.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеОС КАК ДанныеОС
	|		ПО ТаблицаОС.ОсновноеСредство = ДанныеОС.ОсновноеСредство
	|		И ДанныеДокумента.Ссылка = ДанныеОС.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ХозяйственнаяОперация = &ХО_ИзменениеУсловийПередачиВАренду
	|";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ВыделениеНГЛС // Дт <58.НГЛС> - Кт <58.АП>
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)              КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                              КАК Регистратор,
	|	ДанныеДокумента.Период                              КАК Период,
	|	ДанныеДокумента.Организация                         КАК Организация,
	|	ДанныеДокумента.Договор                             КАК Договор,
	|	ДанныеДокумента.ВалютаВзаиморасчетов                КАК Валюта,
	|	ТаблицаОС.ОсновноеСредство                          КАК ОсновноеСредство,
	|	ТаблицаОС.ГруппаФинансовогоУчета                    КАК ГруппаФинансовогоУчета,
	|	0                                                   КАК АрендныеПлатежи,
	|	ЕСТЬNULL(ДанныеОС.НГЛСРегл, 0)                      КАК АрендныеПлатежиРегл,
	|	ЕСТЬNULL(ДанныеОС.НГЛСУпр, 0)                       КАК АрендныеПлатежиУпр,
	|	0                                                   КАК НГЛСРегл,
	|	0                                                   КАК НГЛСУпр,
	|	0                                                   КАК ПроцентныйДисконт,
	|	0                                                   КАК ПроцентныйДисконтУпр,
	|	0                                                   КАК ПроцентныйДисконтРегл,
	|	&НастройкаХО_ВыделениеНГЛС                          КАК НастройкаХозяйственнойОперации,
	|	ТаблицаОС.ИдентификаторСтроки                       КАК ИдентификаторФинЗаписи
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТаблицаОС КАК ТаблицаОС
	|	 ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ДанныеОС КАК ДанныеОС
	|	 ПО ТаблицаОС.ОсновноеСредство = ДанныеОС.ОсновноеСредство
	|	 И ДанныеДокумента.Ссылка = ДанныеОС.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ХозяйственнаяОперация = &ХО_ИзменениеУсловийПередачиВАренду
	|	И (ЕСТЬNULL(ДанныеОС.НГЛСРегл, 0) <> 0
	|		ИЛИ ЕСТЬNULL(ДанныеОС.НГЛСУпр, 0) <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)              КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                              КАК Регистратор,
	|	ДанныеДокумента.Период                              КАК Период,
	|	ДанныеДокумента.Организация                         КАК Организация,
	|	ДанныеДокумента.Договор                             КАК Договор,
	|	ДанныеДокумента.ВалютаВзаиморасчетов                КАК Валюта,
	|	ТаблицаОС.ОсновноеСредство                          КАК ОсновноеСредство,
	|	ТаблицаОС.ГруппаФинансовогоУчета                    КАК ГруппаФинансовогоУчета,
	|	0                                                   КАК АрендныеПлатежи,
	|	0                                                   КАК АрендныеПлатежиРегл,
	|	0                                                   КАК АрендныеПлатежиУпр,
	|	ЕСТЬNULL(ДанныеОС.НГЛСРегл, 0)                      КАК НГЛСРегл,
	|	ЕСТЬNULL(ДанныеОС.НГЛСУпр, 0)                       КАК НГЛСУпр,
	|	0                                                   КАК ПроцентныйДисконт,
	|	0                                                   КАК ПроцентныйДисконтУпр,
	|	0                                                   КАК ПроцентныйДисконтРегл,
	|	&НастройкаХО_ВыделениеНГЛС                          КАК НастройкаХозяйственнойОперации,
	|	ТаблицаОС.ИдентификаторСтроки                       КАК ИдентификаторФинЗаписи
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТаблицаОС КАК ТаблицаОС
	|	 ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ДанныеОС КАК ДанныеОС
	|	 ПО ТаблицаОС.ОсновноеСредство = ДанныеОС.ОсновноеСредство
	|	 И ДанныеДокумента.Ссылка = ДанныеОС.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ХозяйственнаяОперация = &ХО_ИзменениеУсловийПередачиВАренду
	|	И (ЕСТЬNULL(ДанныеОС.НГЛСРегл, 0) <> 0
	|		ИЛИ ЕСТЬNULL(ДанныеОС.НГЛСУпр, 0) <> 0)
	|";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(СписокЗапросовОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ФормированиеВременныхТаблицДляПроведения

Процедура ТекстЗапросаТаблицаВтТаблицаОС(ТекстыЗапроса)
	
	ИмяТаблицы = "втТаблицаОС";

	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтРасширеннаяТаблицаОС(ТекстыЗапроса);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыУчетаОС(ТекстыЗапроса, "ВтРасширеннаяТаблицаОС");
	ТекстЗапросаТаблицаВтКоэффициентыПересчетаВалют(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ПараметрыУчетаОС.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаОС.НГЛСРегл КАК НГЛСРегл,
	|	ТаблицаОС.НГЛСУпр КАК НГЛСУпр,
	|	ТаблицаОС.АрендныеПлатежи КАК АрендныеПлатежи,
	|	ВЫРАЗИТЬ(ТаблицаОС.АрендныеПлатежи * ЕСТЬNULL(Коэффициенты.КоэффициентПересчетаРегл, 0) КАК ЧИСЛО(31, 2)) КАК АрендныеПлатежиРегл,
	|	ВЫРАЗИТЬ(ТаблицаОС.АрендныеПлатежи * ЕСТЬNULL(Коэффициенты.КоэффициентПересчетаУпр, 0) КАК ЧИСЛО(31, 2)) КАК АрендныеПлатежиУпр,
	|	ТаблицаОС.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ втТаблицаОС
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПараметрыУчетаОС КАК ПараметрыУчетаОС
	|		ПО ДанныеДокумента.Ссылка = ПараметрыУчетаОС.Ссылка
	|		И ТаблицаОС.ОсновноеСредство = ПараметрыУчетаОС.ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКоэффициентыПересчетаВалют КАК Коэффициенты
	|		ПО (Коэффициенты.Дата = ДанныеДокумента.Период)
	|			И (Коэффициенты.Организация = ДанныеДокумента.Организация)
	|			И (Коэффициенты.ВалютаВзаиморасчетов = ДанныеДокумента.ВалютаВзаиморасчетов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	ИдентификаторСтроки,
	|	ОсновноеСредство";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);

КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтПроцентыПоГрафику(ТекстыЗапроса)
	
	ИмяТаблицы = "втПроцентыПоГрафику";

	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Период КАК Период,
	|	ГрафикНачисленияПроцентов.ОсновноеСредство КАК ОсновноеСредство,
	|	СУММА(ГрафикНачисленияПроцентов.Проценты) КАК Сумма
	|ПОМЕСТИТЬ втПроцентыПоГрафику
	|ИЗ
	|	ТаблицаГрафикНачисленияПроцентов КАК ГрафикНачисленияПроцентов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|		ПО (ДанныеДокумента.Ссылка = ГрафикНачисленияПроцентов.Ссылка)
	|			И (ДанныеДокумента.Период <= ГрафикНачисленияПроцентов.Дата)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокумента.Ссылка,
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.Период,
	|	ГрафикНачисленияПроцентов.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	ОсновноеСредство";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтИзменениеПроцентов(ТекстыЗапроса)
	
	ИмяТаблицы = "втИзменениеПроцентов";

	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтКоэффициентыПересчетаВалют(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтПроцентыПоГрафику(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	втПроцентыПоГрафику.Ссылка КАК Ссылка,
	|	втПроцентыПоГрафику.ОсновноеСредство КАК ОсновноеСредство,
	|	втПроцентыПоГрафику.Сумма - ЕСТЬNULL(втОстаткиПроцентов.ПроцентныйДисконт, 0) КАК Сумма,
	|	(втПроцентыПоГрафику.Сумма - ЕСТЬNULL(втОстаткиПроцентов.ПроцентныйДисконт, 0))
	|		* ЕСТЬNULL(Коэффициенты.КоэффициентПересчетаРегл, 0) КАК СуммаРегл,
	|	(втПроцентыПоГрафику.Сумма - ЕСТЬNULL(втОстаткиПроцентов.ПроцентныйДисконт, 0))
	|		* ЕСТЬNULL(Коэффициенты.КоэффициентПересчетаУпр, 0) КАК СуммаУпр
	|ПОМЕСТИТЬ втИзменениеПроцентов
	|ИЗ
	|	втПроцентыПоГрафику КАК втПроцентыПоГрафику
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПроцентыДоИзменения КАК втОстаткиПроцентов
	|		ПО втПроцентыПоГрафику.ОсновноеСредство = втОстаткиПроцентов.ОсновноеСредство
	|			И втПроцентыПоГрафику.Ссылка = втОстаткиПроцентов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКоэффициентыПересчетаВалют КАК Коэффициенты
	|		ПО Коэффициенты.Организация = втПроцентыПоГрафику.Организация
	|			И Коэффициенты.Дата = втПроцентыПоГрафику.Период
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	ОсновноеСредство";

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтКоэффициентыПересчетаВалют(ТекстыЗапроса)

	ИмяТаблицы = "втКоэффициентыПересчетаВалют";

	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ДанныеДокумента.Период КАК Дата
	|ПОМЕСТИТЬ ТаблицаПериодов
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ВалютаВзаиморасчетов,
	|	Дата";
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ТаблицаПериодов");
	
	ФинансовыеИнструменты.ТекстЗапросаВтКоэффициентыПересчетаВалют(ТекстыЗапроса, ИмяТаблицы, Истина);

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ПроведениеПоРеглУчету

Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	Возврат ИзменениеУсловийПередачиВАрендуЛокализация.ТекстЗапросаВТОтраженияВРеглУчете();
	
КонецФункции

Функция ТекстОтраженияВРеглУчете() Экспорт
	
	Возврат ИзменениеУсловийПередачиВАрендуЛокализация.ТекстОтраженияВРеглУчете();
	
КонецФункции

#КонецОбласти

#Область Печать

// Добавляет команды печати.
// 
// Параметры:
// 	КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

// Заполнение на основании договора контрагента
//
// Параметры:
//  Объект - ДокументОбъект.ИзменениеУсловийПередачиВАренду
//  Договор - СправочникСсылка.ДоговорыКонтрагентов
//
Процедура ЗаполнитьНаОснованииДоговора(Объект, Договор) Экспорт
	
	ТекстыПакетаЗапросов = Новый Массив;

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ПереданныеВАрендуОС.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ втПереданныеВАренду
	|ИЗ
	|	РегистрСведений.ПереданныеВАрендуОС.СрезПоследних(
	|			&ПериодГраница,
	|			Организация = &Организация) КАК ПереданныеВАрендуОС
	|ГДЕ
	|	ПереданныеВАрендуОС.Договор = &Договор
	|	И ПереданныеВАрендуОС.Состояние В (ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПереданоВАренду))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИнвестицииВАрендуОстатки.ОсновноеСредство КАК ОсновноеСредство,
	|	ИнвестицииВАрендуОстатки.НГЛСРеглОстаток КАК НГЛСРеглОстаток,
	|	ИнвестицииВАрендуОстатки.НГЛСУпрОстаток КАК НГЛСУпрОстаток
	|ПОМЕСТИТЬ втИнвестицииОстатки
	|ИЗ
	|	РегистрНакопления.ИнвестицииВАренду.Остатки(
	|			&ПериодГраница,
	|			Организация = &Организация
	|				И Договор = &Договор
	|				И ОсновноеСредство В
	|					(ВЫБРАТЬ
	|						втПереданныеВАренду.ОсновноеСредство КАК ОсновноеСредство
	|					ИЗ
	|						втПереданныеВАренду КАК втПереданныеВАренду)) КАК ИнвестицииВАрендуОстатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикОплат.АктуальныеУсловияДоговора КАК АктуальныеУсловияДоговора,
	|	ГрафикОплат.ОсновноеСредство КАК ОсновноеСредство,
	|	ГрафикОплат.Дата КАК Дата,
	|	ГрафикОплат.УслугаПоАренде КАК УслугаПоАренде,
	|	ГрафикОплат.УслугаПоАрендеНДС КАК УслугаПоАрендеНДС,
	|	ГрафикОплат.ДатаИзменения КАК ДатаИзменения
	|ПОМЕСТИТЬ втГрафикОплат
	|ИЗ
	|	РегистрСведений.ГрафикОплатУслугПоДоходнойАренде КАК ГрафикОплат
	|ГДЕ
	|	ГрафикОплат.Договор = &Договор
	|	И ГрафикОплат.ОсновноеСредство В
	|			(ВЫБРАТЬ
	|				втПереданныеВАренду.ОсновноеСредство КАК ОсновноеСредство
	|			ИЗ
	|				втПереданныеВАренду КАК втПереданныеВАренду)
	|	И ГрафикОплат.ДатаИзменения < &Дата
	|	И ГрафикОплат.Активность
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ДатаИзменения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрафикОплат.ОсновноеСредство КАК ОсновноеСредство,
	|	МАКСИМУМ(втГрафикОплат.ДатаИзменения) КАК ДатаИзменения
	|ПОМЕСТИТЬ втГрафикОплатПериод
	|ИЗ
	|	втГрафикОплат КАК втГрафикОплат
	|
	|СГРУППИРОВАТЬ ПО
	|	втГрафикОплат.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ДатаИзменения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрафикОплат.АктуальныеУсловияДоговора КАК АктуальныеУсловияДоговора,
	|	втГрафикОплат.ОсновноеСредство КАК ОсновноеСредство,
	|	втГрафикОплат.Дата КАК Дата,
	|	втГрафикОплат.УслугаПоАренде КАК УслугаПоАренде,
	|	втГрафикОплат.УслугаПоАрендеНДС КАК УслугаПоАрендеНДС
	|ПОМЕСТИТЬ втАктуальныйГрафикОплат
	|
	|ИЗ
	|	втГрафикОплат КАК втГрафикОплат
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрафикОплатПериод КАК втГрафикОплатПериод
	|		ПО втГрафикОплат.ОсновноеСредство = втГрафикОплатПериод.ОсновноеСредство
	|			И втГрафикОплат.ДатаИзменения = втГрафикОплатПериод.ДатаИзменения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрафикОплат.АктуальныеУсловияДоговора КАК АктуальныеУсловияДоговора,
	|	втГрафикОплат.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ втАктуальныеУсловияДоговора
	|
	|ИЗ
	|	втАктуальныйГрафикОплат КАК втГрафикОплат
	|
	|СГРУППИРОВАТЬ ПО
	|	втГрафикОплат.АктуальныеУсловияДоговора,
	|	втГрафикОплат.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДатыАренды.ОсновноеСредство КАК ОсновноеСредство,
	|	МАКСИМУМ(ДатыАренды.ДатаОкончанияАренды) КАК ДатаОкончанияАренды
	|ПОМЕСТИТЬ втДатыАренды
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПередачаОСВАренду2_4ОС.ОсновноеСредство КАК ОсновноеСредство,
	|		ПередачаОСВАренду2_4ОС.ДатаОкончанияАренды КАК ДатаОкончанияАренды
	|	ИЗ
	|		Документ.ПередачаОСВАренду2_4.ОС КАК ПередачаОСВАренду2_4ОС
	|	ГДЕ
	|		(ПередачаОСВАренду2_4ОС.Ссылка, ПередачаОСВАренду2_4ОС.ОсновноеСредство) В
	|				(ВЫБРАТЬ
	|					втАктуальныеУсловияДоговора.АктуальныеУсловияДоговора КАК АктуальныеУсловияДоговора,
	|					втАктуальныеУсловияДоговора.ОсновноеСредство КАК ОсновноеСредство
	|				ИЗ
	|					втАктуальныеУсловияДоговора КАК втАктуальныеУсловияДоговора)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ИзменениеУсловийПередачиВАрендуОС.ОсновноеСредство,
	|		ИзменениеУсловийПередачиВАрендуОС.ДатаОкончанияАренды
	|	ИЗ
	|		Документ.ИзменениеУсловийПередачиВАренду.ОС КАК ИзменениеУсловийПередачиВАрендуОС
	|	ГДЕ
	|		(ИзменениеУсловийПередачиВАрендуОС.Ссылка, ИзменениеУсловийПередачиВАрендуОС.ОсновноеСредство) В
	|				(ВЫБРАТЬ
	|					втАктуальныеУсловияДоговора.АктуальныеУсловияДоговора КАК АктуальныеУсловияДоговора,
	|					втАктуальныеУсловияДоговора.ОсновноеСредство КАК ОсновноеСредство
	|				ИЗ
	|					втАктуальныеУсловияДоговора КАК втАктуальныеУсловияДоговора)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВводОстатковИнвестицииВАрендуОС.ОсновноеСредство,
	|		ВводОстатковИнвестицииВАрендуОС.ДатаОкончанияАренды
	|	ИЗ
	|		Документ.ВводОстатковИнвестицииВАренду.ОС КАК ВводОстатковИнвестицииВАрендуОС
	|	ГДЕ
	|		(ВводОстатковИнвестицииВАрендуОС.Ссылка, ВводОстатковИнвестицииВАрендуОС.ОсновноеСредство) В
	|				(ВЫБРАТЬ
	|					втАктуальныеУсловияДоговора.АктуальныеУсловияДоговора КАК АктуальныеУсловияДоговора,
	|					втАктуальныеУсловияДоговора.ОсновноеСредство КАК ОсновноеСредство
	|				ИЗ
	|					втАктуальныеУсловияДоговора КАК втАктуальныеУсловияДоговора)) КАК ДатыАренды
	|
	|СГРУППИРОВАТЬ ПО
	|	ДатыАренды.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикНачисления.ОсновноеСредство КАК ОсновноеСредство,
	|	ГрафикНачисления.Дата КАК Дата,
	|	ГрафикНачисления.УслугаПоАренде КАК УслугаПоАренде,
	|	ГрафикНачисления.УслугаПоАрендеНДС КАК УслугаПоАрендеНДС,
	|	ГрафикНачисления.ДатаИзменения КАК ДатаИзменения
	|ПОМЕСТИТЬ втГрафикНачисления
	|ИЗ
	|	РегистрСведений.ГрафикНачисленияУслугПоДоходнойАренде КАК ГрафикНачисления
	|ГДЕ
	|	ГрафикНачисления.Договор = &Договор
	|	И ГрафикНачисления.ОсновноеСредство В
	|			(ВЫБРАТЬ
	|				втПереданныеВАренду.ОсновноеСредство КАК ОсновноеСредство
	|			ИЗ
	|				втПереданныеВАренду КАК втПереданныеВАренду)
	|	И ГрафикНачисления.ДатаИзменения < &Дата
	|	И ГрафикНачисления.Активность
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ДатаИзменения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрафикНачисления.ОсновноеСредство КАК ОсновноеСредство,
	|	МАКСИМУМ(втГрафикНачисления.ДатаИзменения) КАК ДатаИзменения
	|ПОМЕСТИТЬ втГрафикНачисленияПериод
	|ИЗ
	|	втГрафикНачисления КАК втГрафикНачисления
	|
	|СГРУППИРОВАТЬ ПО
	|	втГрафикНачисления.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ДатаИзменения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикПроцентов.ОсновноеСредство КАК ОсновноеСредство,
	|	ГрафикПроцентов.Дата КАК Дата,
	|	ГрафикПроцентов.Проценты КАК Проценты,
	|	ГрафикПроцентов.ДатаИзменения КАК ДатаИзменения
	|ПОМЕСТИТЬ втГрафикПроцентов
	|ИЗ
	|	РегистрСведений.ГрафикНачисленияПроцентовПоДоходнойАренде КАК ГрафикПроцентов
	|ГДЕ
	|	ГрафикПроцентов.Договор = &Договор
	|	И ГрафикПроцентов.ОсновноеСредство В
	|			(ВЫБРАТЬ
	|				втПереданныеВАренду.ОсновноеСредство КАК ОсновноеСредство
	|			ИЗ
	|				втПереданныеВАренду КАК втПереданныеВАренду)
	|	И ГрафикПроцентов.ДатаИзменения < &Дата
	|	И ГрафикПроцентов.Активность
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ДатаИзменения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрафикПроцентов.ОсновноеСредство КАК ОсновноеСредство,
	|	МАКСИМУМ(втГрафикПроцентов.ДатаИзменения) КАК ДатаИзменения
	|ПОМЕСТИТЬ втГрафикПроцентовПериод
	|ИЗ
	|	втГрафикПроцентов КАК втГрафикПроцентов
	|
	|СГРУППИРОВАТЬ ПО
	|	втГрафикПроцентов.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ДатаИзменения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПереданныеВАренду.ОсновноеСредство КАК ОсновноеСредство,
	|	ЕСТЬNULL(втДатыАренды.ДатаОкончанияАренды, ДАТАВРЕМЯ(1,1,1)) КАК ДатаОкончанияАренды,
	|	ЕСТЬNULL(втИнвестицииОстатки.НГЛСРеглОстаток, 0) КАК НГЛСРегл,
	|	ЕСТЬNULL(втИнвестицииОстатки.НГЛСУпрОстаток, 0) КАК НГЛСУпр
	|ИЗ
	|	втПереданныеВАренду КАК втПереданныеВАренду
	|		ЛЕВОЕ СОЕДИНЕНИЕ втИнвестицииОстатки КАК втИнвестицииОстатки
	|		ПО втПереданныеВАренду.ОсновноеСредство = втИнвестицииОстатки.ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ втДатыАренды КАК втДатыАренды
	|		ПО втПереданныеВАренду.ОсновноеСредство = втДатыАренды.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрафикОплат.ОсновноеСредство КАК ОсновноеСредство,
	|	втГрафикОплат.Дата КАК Дата,
	|	втГрафикОплат.УслугаПоАренде КАК УслугаПоАренде,
	|	втГрафикОплат.УслугаПоАрендеНДС КАК УслугаПоАрендеНДС
	|ИЗ
	|	втАктуальныйГрафикОплат КАК втГрафикОплат
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрафикНачисления.ОсновноеСредство КАК ОсновноеСредство,
	|	втГрафикНачисления.Дата КАК Дата,
	|	втГрафикНачисления.УслугаПоАренде КАК УслугаПоАренде,
	|	втГрафикНачисления.УслугаПоАрендеНДС КАК УслугаПоАрендеНДС
	|ИЗ
	|	втГрафикНачисления КАК втГрафикНачисления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрафикНачисленияПериод КАК втГрафикНачисленияПериод
	|		ПО втГрафикНачисления.ОсновноеСредство = втГрафикНачисленияПериод.ОсновноеСредство
	|			И втГрафикНачисления.ДатаИзменения = втГрафикНачисленияПериод.ДатаИзменения
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрафикПроцентов.ОсновноеСредство КАК ОсновноеСредство,
	|	втГрафикПроцентов.Дата КАК Дата,
	|	втГрафикПроцентов.Проценты КАК Проценты
	|ИЗ
	|	втГрафикПроцентов КАК втГрафикПроцентов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрафикПроцентовПериод КАК втГрафикПроцентовПериод
	|		ПО втГрафикПроцентов.ОсновноеСредство = втГрафикПроцентовПериод.ОсновноеСредство
	|			И втГрафикПроцентов.ДатаИзменения = втГрафикПроцентовПериод.ДатаИзменения
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата";
	ТекстыПакетаЗапросов.Добавить(ТекстЗапроса);
	
	ДатаДок = ?(Объект.Дата <> '000101010000', Объект.Дата, ТекущаяДатаСеанса());
	ПериодГраница = Новый Граница(Новый МоментВремени(ДатаДок, Объект.Ссылка), ВидГраницы.Исключая);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("Договор", Договор);
	Запрос.УстановитьПараметр("Дата", НачалоДня(ДатаДок));
	Запрос.УстановитьПараметр("ПериодГраница", ПериодГраница);
	
	Результат = Запрос.ВыполнитьПакет();
	Если Результат[0].Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ОС.Загрузить(Результат[Результат.ВГраница()-3].Выгрузить());
	Объект.ГрафикОплатУслуг.Загрузить(Результат[Результат.ВГраница()-2].Выгрузить());
	Объект.ГрафикНачисленияУслуг.Загрузить(Результат[Результат.ВГраница()-1].Выгрузить());
	Объект.ГрафикНачисленияПроцентов.Загрузить(Результат[Результат.ВГраница()].Выгрузить());
	
КонецПроцедуры

// Заполнение на основании документа ИзменениеУсловийДоговораАренды
//
// Параметры:
//  Объект - ДокументОбъект.ИзменениеУсловийПередачиВАренду
//  Основание - ДокументСсылка.ИзменениеУсловийДоговораАренды
//
Процедура ЗаполнитьНаОснованииИзмененияУсловийДоговораАренды(Объект, Основание) Экспорт
	
	Объект.ДокументОснование = Основание;
	Объект.ОС.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1 РАЗРЕШЕННЫЕ
		|	ДоговорыКонтрагентов.Ссылка КАК Договор,
		|	ДоговорыКонтрагентов.Организация КАК Организация,
		|	ДоговорыКонтрагентов.Подразделение КАК Подразделение,
		|	ДоговорыКонтрагентов.Контрагент КАК Арендатор
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|ГДЕ
		|	ДоговорыКонтрагентов.ДоговорАренды = &ДоговорАренды
		|	И ДоговорыКонтрагентов.Организация = &Организация
		|	И ДоговорыКонтрагентов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.Действует)
		|	И НЕ ДоговорыКонтрагентов.ПометкаУдаления";
	
	РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Основание, "Договор, Организация");
	Запрос.УстановитьПараметр("ДоговорАренды", РеквизитыОснования.Договор);
	Запрос.УстановитьПараметр("Организация", РеквизитыОснования.Организация);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Объект, Выборка);
		Документы.ИзменениеУсловийПередачиВАренду.ЗаполнитьНаОснованииДоговора(Объект, Выборка.Договор);
	КонецЕсли;
	
КонецПроцедуры

// Удаляет строки до даты изменения аренды
//
// Параметры:
//  ТаблицаОС - ДанныеФормыКоллекция
//  ГрафикОплатУслуг - ДанныеФормыКоллекция
//  ГрафикНачисленияУслуг - ДанныеФормыКоллекция
//  ГрафикНачисленияПроцентов - ДанныеФормыКоллекция
//  Дата - Дата
//
Процедура УдалитьНеактуальныеСтрокиГрафиков(ТаблицаОС, ГрафикОплатУслуг, ГрафикНачисленияУслуг, ГрафикНачисленияПроцентов, Дата) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОС", ТаблицаОС.Выгрузить().ВыгрузитьКолонку("ОсновноеСредство"));
	Запрос.УстановитьПараметр("ГрафикОплатУслуг", ГрафикОплатУслуг.Выгрузить());
	Запрос.УстановитьПараметр("ГрафикНачисленияУслуг", ГрафикНачисленияУслуг.Выгрузить());
	Запрос.УстановитьПараметр("ГрафикНачисленияПроцентов", ГрафикНачисленияПроцентов.Выгрузить());
	Запрос.УстановитьПараметр("Период", НачалоДня(Дата));

	Запрос.Текст =
	"ВЫБРАТЬ
	|	ГрафикОплатУслуг.Дата КАК Дата,
	|	ГрафикОплатУслуг.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ втСтрокиГрафикаОплат
	|ИЗ
	|	&ГрафикОплатУслуг КАК ГрафикОплатУслуг
	|ГДЕ
	|	(НЕ ГрафикОплатУслуг.ОсновноеСредство В (&МассивОС)
	|			ИЛИ ГрафикОплатУслуг.Дата < &Период)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикНачисленияУслуг.Дата КАК Дата,
	|	ГрафикНачисленияУслуг.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ втСтрокиГрафикаНачислений
	|ИЗ
	|	&ГрафикНачисленияУслуг КАК ГрафикНачисленияУслуг
	|ГДЕ
	|	(НЕ ГрафикНачисленияУслуг.ОсновноеСредство В (&МассивОС)
	|			ИЛИ ГрафикНачисленияУслуг.Дата < &Период)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикНачисленияПроцентов.Дата КАК Дата,
	|	ГрафикНачисленияПроцентов.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ втСтрокиГрафикаПроцентов
	|ИЗ
	|	&ГрафикНачисленияПроцентов КАК ГрафикНачисленияПроцентов
	|ГДЕ
	|	(НЕ ГрафикНачисленияПроцентов.ОсновноеСредство В (&МассивОС)
	|			ИЛИ ГрафикНачисленияПроцентов.Дата <= КОНЕЦПЕРИОДА(&Период, МЕСЯЦ))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтрокиГрафикаОплат.Дата КАК Дата,
	|	СтрокиГрафикаОплат.ОсновноеСредство КАК ОсновноеСредство
	|ИЗ
	|	втСтрокиГрафикаОплат КАК СтрокиГрафикаОплат
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтрокиГрафикаНачислений.Дата КАК Дата,
	|	СтрокиГрафикаНачислений.ОсновноеСредство КАК ОсновноеСредство
	|ИЗ
	|	втСтрокиГрафикаНачислений КАК СтрокиГрафикаНачислений
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтрокиГрафикаПроцентов.Дата КАК Дата,
	|	СтрокиГрафикаПроцентов.ОсновноеСредство КАК ОсновноеСредство
	|ИЗ
	|	втСтрокиГрафикаПроцентов КАК СтрокиГрафикаПроцентов";

	РезультатыЗапроса = Запрос.ВыполнитьПакет();

	СтруктураОтбора = Новый Структура("ОсновноеСредство, Дата");

	РезультатОплаты = РезультатыЗапроса[РезультатыЗапроса.ВГраница()-2];
	Если НЕ РезультатОплаты.Пустой() Тогда
		Выборка = РезультатОплаты.Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(СтруктураОтбора, Выборка);
			НайденныеСтрокиГрафика = ГрафикОплатУслуг.НайтиСтроки(СтруктураОтбора);
			Для Каждого СтрокаГрафика Из НайденныеСтрокиГрафика Цикл
				ГрафикОплатУслуг.Удалить(СтрокаГрафика);
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;

	РезультатНачисления = РезультатыЗапроса[РезультатыЗапроса.ВГраница()-1];
	Если НЕ РезультатНачисления.Пустой() Тогда
		Выборка = РезультатНачисления.Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(СтруктураОтбора, Выборка);
			НайденныеСтрокиГрафика = ГрафикНачисленияУслуг.НайтиСтроки(СтруктураОтбора);
			Для Каждого СтрокаГрафика Из НайденныеСтрокиГрафика Цикл
				ГрафикНачисленияУслуг.Удалить(СтрокаГрафика);
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;

	РезультатПроценты = РезультатыЗапроса[РезультатыЗапроса.ВГраница()];
	Если НЕ РезультатПроценты.Пустой() Тогда
		Выборка = РезультатПроценты.Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(СтруктураОтбора, Выборка);
			НайденныеСтрокиГрафика = ГрафикНачисленияПроцентов.НайтиСтроки(СтруктураОтбора);
			Для Каждого СтрокаГрафика Из НайденныеСтрокиГрафика Цикл
				ГрафикНачисленияПроцентов.Удалить(СтрокаГрафика);
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Заполняет строки графиков до даты изменения аренды по предыдущим условиям
//
// Параметры:
//  ТаблицаОС - ДанныеФормыКоллекция
//  ГрафикОплатУслуг - ДанныеФормыКоллекция
//  ГрафикНачисленияУслуг - ДанныеФормыКоллекция
//  ГрафикНачисленияПроцентов - ДанныеФормыКоллекция
//  Договор - СправочникСсылка.ДоговорыКонтрагентов
//  Дата - Дата
//
Процедура ДополнитьГрафикиПоПредыдущимУсловиям(ТаблицаОС, ГрафикОплатУслуг, ГрафикНачисленияУслуг, ГрафикНачисленияПроцентов, Договор, Дата) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Договор", Договор);
	Запрос.УстановитьПараметр("МассивОС", ТаблицаОС.Выгрузить().ВыгрузитьКолонку("ОсновноеСредство"));
	Запрос.УстановитьПараметр("Дата", НачалоДня(Дата));
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ГрафикОплат.ОсновноеСредство КАК ОсновноеСредство,
	|	ГрафикОплат.Дата КАК Дата,
	|	ГрафикОплат.УслугаПоАренде КАК УслугаПоАренде,
	|	ГрафикОплат.УслугаПоАрендеНДС КАК УслугаПоАрендеНДС,
	|	ГрафикОплат.ДатаИзменения КАК ДатаИзменения
	|ПОМЕСТИТЬ втГрафикОплат
	|ИЗ
	|	РегистрСведений.ГрафикОплатУслугПоДоходнойАренде КАК ГрафикОплат
	|ГДЕ
	|	ГрафикОплат.Договор = &Договор
	|	И ГрафикОплат.ОсновноеСредство В (&МассивОС)
	|	И ГрафикОплат.Дата < &Дата
	|	И ГрафикОплат.ДатаИзменения < &Дата
	|	И ГрафикОплат.Активность
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ДатаИзменения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрафикОплат.ОсновноеСредство КАК ОсновноеСредство,
	|	МАКСИМУМ(втГрафикОплат.ДатаИзменения) КАК ДатаИзменения
	|ПОМЕСТИТЬ втГрафикОплатПериод
	|ИЗ
	|	втГрафикОплат КАК втГрафикОплат
	|
	|СГРУППИРОВАТЬ ПО
	|	втГрафикОплат.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ДатаИзменения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикНачисления.ОсновноеСредство КАК ОсновноеСредство,
	|	ГрафикНачисления.Дата КАК Дата,
	|	ГрафикНачисления.УслугаПоАренде КАК УслугаПоАренде,
	|	ГрафикНачисления.УслугаПоАрендеНДС КАК УслугаПоАрендеНДС,
	|	ГрафикНачисления.ДатаИзменения КАК ДатаИзменения
	|ПОМЕСТИТЬ втГрафикНачисления
	|ИЗ
	|	РегистрСведений.ГрафикНачисленияУслугПоДоходнойАренде КАК ГрафикНачисления
	|ГДЕ
	|	ГрафикНачисления.Договор = &Договор
	|	И ГрафикНачисления.ОсновноеСредство В (&МассивОС)
	|	И ГрафикНачисления.Дата < &Дата
	|	И ГрафикНачисления.ДатаИзменения < &Дата
	|	И ГрафикНачисления.Активность
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ДатаИзменения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрафикНачисления.ОсновноеСредство КАК ОсновноеСредство,
	|	МАКСИМУМ(втГрафикНачисления.ДатаИзменения) КАК ДатаИзменения
	|ПОМЕСТИТЬ втГрафикНачисленияПериод
	|ИЗ
	|	втГрафикНачисления КАК втГрафикНачисления
	|
	|СГРУППИРОВАТЬ ПО
	|	втГрафикНачисления.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ДатаИзменения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикПроцентов.ОсновноеСредство КАК ОсновноеСредство,
	|	ГрафикПроцентов.Дата КАК Дата,
	|	ГрафикПроцентов.Проценты КАК Проценты,
	|	ГрафикПроцентов.ДатаИзменения КАК ДатаИзменения
	|ПОМЕСТИТЬ втГрафикПроцентов
	|ИЗ
	|	РегистрСведений.ГрафикНачисленияПроцентовПоДоходнойАренде КАК ГрафикПроцентов
	|ГДЕ
	|	ГрафикПроцентов.Договор = &Договор
	|	И ГрафикПроцентов.ОсновноеСредство В (&МассивОС)
	|	И ГрафикПроцентов.Дата <= КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ)
	|	И ГрафикПроцентов.ДатаИзменения < &Дата
	|	И ГрафикПроцентов.Активность
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ДатаИзменения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрафикПроцентов.ОсновноеСредство КАК ОсновноеСредство,
	|	МАКСИМУМ(втГрафикПроцентов.ДатаИзменения) КАК ДатаИзменения
	|ПОМЕСТИТЬ втГрафикПроцентовПериод
	|ИЗ
	|	втГрафикПроцентов КАК втГрафикПроцентов
	|
	|СГРУППИРОВАТЬ ПО
	|	втГрафикПроцентов.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ДатаИзменения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрафикОплат.ОсновноеСредство КАК ОсновноеСредство,
	|	втГрафикОплат.Дата КАК Дата,
	|	втГрафикОплат.УслугаПоАренде КАК УслугаПоАренде,
	|	втГрафикОплат.УслугаПоАрендеНДС КАК УслугаПоАрендеНДС
	|ИЗ
	|	втГрафикОплат КАК втГрафикОплат
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрафикОплатПериод КАК втГрафикОплатПериод
	|		ПО втГрафикОплат.ОсновноеСредство = втГрафикОплатПериод.ОсновноеСредство
	|			И втГрафикОплат.ДатаИзменения = втГрафикОплатПериод.ДатаИзменения
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрафикНачисления.ОсновноеСредство КАК ОсновноеСредство,
	|	втГрафикНачисления.Дата КАК Дата,
	|	втГрафикНачисления.УслугаПоАренде КАК УслугаПоАренде,
	|	втГрафикНачисления.УслугаПоАрендеНДС КАК УслугаПоАрендеНДС
	|ИЗ
	|	втГрафикНачисления КАК втГрафикНачисления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрафикНачисленияПериод КАК втГрафикНачисленияПериод
	|		ПО втГрафикНачисления.ОсновноеСредство = втГрафикНачисленияПериод.ОсновноеСредство
	|			И втГрафикНачисления.ДатаИзменения = втГрафикНачисленияПериод.ДатаИзменения
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрафикПроцентов.ОсновноеСредство КАК ОсновноеСредство,
	|	втГрафикПроцентов.Дата КАК Дата,
	|	втГрафикПроцентов.Проценты КАК Проценты
	|ИЗ
	|	втГрафикПроцентов КАК втГрафикПроцентов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрафикПроцентовПериод КАК втГрафикПроцентовПериод
	|		ПО втГрафикПроцентов.ОсновноеСредство = втГрафикПроцентовПериод.ОсновноеСредство
	|			И втГрафикПроцентов.ДатаИзменения = втГрафикПроцентовПериод.ДатаИзменения
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата";
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	РезультатОплаты = РезультатыЗапроса[РезультатыЗапроса.ВГраница()-2];
	Если НЕ РезультатОплаты.Пустой() Тогда
		Выборка = РезультатОплаты.Выбрать();
		Пока Выборка.Следующий() Цикл
			СтрокаГрафика = ГрафикОплатУслуг.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаГрафика, Выборка);
		КонецЦикла;
		ГрафикОплатУслуг.Сортировать("ОсновноеСредство, Дата");
	КонецЕсли;
	
	РезультатНачисления = РезультатыЗапроса[РезультатыЗапроса.ВГраница()-1];
	Если НЕ РезультатНачисления.Пустой() Тогда
		Выборка = РезультатНачисления.Выбрать();
		Пока Выборка.Следующий() Цикл
			СтрокаГрафика = ГрафикНачисленияУслуг.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаГрафика, Выборка);
		КонецЦикла;
		ГрафикНачисленияУслуг.Сортировать("ОсновноеСредство, Дата");
	КонецЕсли;
	
	РезультатПроценты = РезультатыЗапроса[РезультатыЗапроса.ВГраница()];
	Если НЕ РезультатПроценты.Пустой() Тогда
		Выборка = РезультатПроценты.Выбрать();
		Пока Выборка.Следующий() Цикл
			СтрокаГрафика = ГрафикНачисленияПроцентов.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаГрафика, Выборка);
		КонецЦикла;
		ГрафикНачисленияПроцентов.Сортировать("ОсновноеСредство, Дата");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
