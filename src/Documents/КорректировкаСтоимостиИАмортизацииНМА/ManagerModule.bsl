#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	
	МеханизмыДокумента.Добавить("НематериальныеАктивы");
	МеханизмыДокумента.Добавить("РеестрДокументов");
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	МеханизмыДокумента.Добавить("ОборотныеРегистрыУправленческогоУчета");
	МеханизмыДокумента.Добавить("УчетДоходовРасходов");
	
	КорректировкаСтоимостиИАмортизацииНМАЛокализация.ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента);
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка, ДокументОбъект - ссылка на документ или объект, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура:
//     * Ключ - Строка - Имя таблицы.
//     * Значение - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Если ТипЗнч(Документ) = Тип("ДокументОбъект.КорректировкаСтоимостиИАмортизацииНМА") Тогда
		ДокументСсылка = Документ.Ссылка;
	Иначе
		ДокументСсылка = Документ;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		
		ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДопПараметры);
		
		ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДокументыПоНМА(Запрос, ТекстыЗапроса, Регистры);
		
		ТекстЗапросаТаблицаПервоначальныеСведенияНМА(Запрос, ТекстыЗапроса, Регистры);
		
		//++ НЕ УТКА
		ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаОтражениеДокументовВМеждународномУчете(ТекстыЗапроса, Регистры);
		//-- НЕ УТКА
		
		КорректировкаСтоимостиИАмортизацииНМАЛокализация.ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры);
		
	КонецЕсли;
	
	ТаблицыДвижений = ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
	Возврат ТаблицыДвижений;
	
КонецФункции

// Формирует таблицы движений при отложенном проведении.
//
// Параметры:
//  МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Содержит временные таблицы, которые используются при формировании движений.
//
// Возвращаемое значение:
//  см. ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения
//
Функция ТаблицыОтложенногоФормированияДвижений(МенеджерВременныхТаблиц) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ПолучитьДанныеДокумента(Запрос);
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализацииПоПериодам(Запрос, Запрос.Параметры,, "ВтСписокДокументов");

	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения();
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла; 
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	ТекстЗапросаТаблицаСтоимостьНМА(ТекстыЗапроса);
	ТекстЗапросаТаблицаАмортизацияНМА(ТекстыЗапроса);

	ТекстЗапросаТаблицаПервоначальныеСведенияНМА(Запрос, ТекстыЗапроса, Неопределено);
	
	ТекстЗапросаТаблицаПрочиеРасходы(ТекстыЗапроса);
	ТекстЗапросаТаблицаПартииПрочихРасходов(ТекстыЗапроса);
	ТекстЗапросаТаблицаПрочиеАктивыПассивы(ТекстыЗапроса);
	ТекстЗапросаТаблицаДвиженияДоходыРасходыПрочиеАктивыПассивы(ТекстыЗапроса);
	ТекстЗапросаТаблицаДвиженияПоПрочимАктивамПассивам(ТекстыЗапроса);
	ТекстЗапросаТаблицаПрочиеДоходы(ТекстыЗапроса);
	
	//++ НЕ УТКА
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаОтражениеДокументовВМеждународномУчете(ТекстыЗапроса, Неопределено);
	//-- НЕ УТКА

	КорректировкаСтоимостиИАмортизацииНМАЛокализация.ТаблицыОтложенногоФормированияДвижений(ТекстыЗапроса);
	
	ТаблицыДвижений = ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса);
	
	Возврат ТаблицыДвижений;
	
КонецФункции

#КонецОбласти

#Область Команды

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Документы.ИзменениеПараметровНМА2_4.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);

КонецПроцедуры

// Добавляет команду создания документа "Корректировка стоимости и амортизации НМА".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
// Возвращаемое значение:
//	СтрокаТаблицыЗначений - добавленная команда
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.КорректировкаСтоимостиИАмортизацииНМА) Тогда
		
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.КорректировкаСтоимостиИАмортизацииНМА.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.КорректировкаСтоимостиИАмортизацииНМА);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьВнеоборотныеАктивы2_4";
	
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;

	Возврат Неопределено;
	
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки	 - Структура - настройки подсистемы.
//
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.КорректировкаСтоимостиИАмортизацииНМА";
	ТекстыЗапросаВременныхТаблиц = Новый Соответствие();
	
	ЗначенияПараметров = ЗначенияПараметровПроведения();
	
	ПереопределениеРасчетаПараметров = Новый Структура;
	
	ВЗапросеЕстьИсточник = Истина;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	ИначеЕсли ИмяРегистра = "ДокументыПоНМА" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаДокументыПоНМА(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	КонецЕсли;
		
	Если ТекстЗапроса = Неопределено Тогда
		
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';
								|en = 'In document %ПолноеИмяДокумента%, adaptation of request for generating records of register %ИмяРегистра% is not implemented.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов"
		ИЛИ ИмяРегистра = "ДокументыПоНМА" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ВЗапросеЕстьИсточник,
										ПереопределениеРасчетаПараметров);
	Иначе	
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ПереопределениеРасчетаПараметров,
										ТекстыЗапросаВременныхТаблиц);
	КонецЕсли; 

	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДопПараметры)
	
	ПроведениеДокументов.ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДопПараметры);
	
	СформироватьТаблицуВтСписокДокументов(Запрос);
	ПолучитьДанныеДокумента(Запрос);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокументаРеквизиты.Период КАК Период,
	|	ДанныеДокументаРеквизиты.Номер КАК Номер,
	|	ДанныеДокументаРеквизиты.Организация КАК Организация
	|	
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокументаРеквизиты";
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения(Реквизиты);
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла; 
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализацииПоПериодам(Запрос, Запрос.Параметры,, "ВтСписокДокументов");
	
КонецПроцедуры

Процедура СформироватьТаблицуВтСписокДокументов(Запрос)
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СписокДокументов.Ссылка КАК Ссылка,
	|	СписокДокументов.Организация КАК Организация,
	|	СписокДокументов.Дата КАК Период
	|
	|ПОМЕСТИТЬ ВтСписокДокументов
	|
	|ИЗ
	|	Документ.КорректировкаСтоимостиИАмортизацииНМА КАК СписокДокументов
	|
	|ГДЕ
	|	СписокДокументов.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|";
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПолучитьДанныеДокумента(Запрос)
	
	СписокЗапросов = Новый Массив;
	
	#Область ДанныеДокументаРеквизиты
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.ПометкаУдаления КАК ПометкаУдаления,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.Проведен КАК Проведен,
	|	ДанныеДокумента.АналитикаДоходов КАК АналитикаДоходов,
	|	ДанныеДокумента.АналитикаРасходов КАК АналитикаРасходов,
	|	ДанныеДокумента.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
	|	ДанныеДокумента.АналитикаАктивовПассивовДоходов КАК АналитикаАктивовПассивовДоходов,
	|	ДанныеДокумента.НастройкаСчетовУчета КАК НастройкаСчетовУчета,
	|	ДанныеДокумента.НастройкаСчетовУчетаДоходов КАК НастройкаСчетовУчетаДоходов,
	|	ДанныеДокумента.Комментарий КАК Комментарий,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Организация.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ДанныеДокумента.Ответственный КАК Ответственный,
	|	ДанныеДокумента.ОтражатьВРеглУчете КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете КАК ОтражатьВУпрУчете,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ДанныеДокумента.СтатьяДоходов КАК СтатьяДоходов,
	|	ДанныеДокумента.СтатьяРасходов КАК СтатьяРасходов,
	|	ДанныеДокумента.КорректировкаСтоимости КАК КорректировкаСтоимости,
	|	ДанныеДокумента.КорректировкаПервоначальнойСтоимости КАК КорректировкаПервоначальнойСтоимости,
	|	ДанныеДокумента.КорректировкаАмортизации КАК КорректировкаАмортизации,
	|	ДанныеДокумента.ВключаетАмортизациюТекущегоМесяца КАК ВключаетАмортизациюТекущегоМесяца,
	|	ДанныеДокумента.КорректировкаВСвязиСИзменениемУчетнойПолитики КАК КорректировкаВСвязиСИзменениемУчетнойПолитики,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаСтоимостиИАмортизацииНМА) КАК ХозяйственнаяОперация
	|
	|ПОМЕСТИТЬ ДанныеДокументаРеквизиты
	|
	|ИЗ
	|	Документ.КорректировкаСтоимостиИАмортизацииНМА КАК ДанныеДокумента
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка В (
	|		ВЫБРАТЬ
	|			ВтСписокДокументов.Ссылка
	|		ИЗ
	|			ВтСписокДокументов КАК ВтСписокДокументов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ДанныеДокументаТаблицаНМА
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаДокумента.НематериальныйАктив КАК НематериальныйАктив,
	|	ТаблицаДокумента.НематериальныйАктив КАК ОбъектУчета,
	|
	|	ТаблицаДокумента.СтоимостьУУ КАК СтоимостьУУ,
	|	ТаблицаДокумента.СтоимостьБУ КАК СтоимостьБУ,
	|	ТаблицаДокумента.СтоимостьНУ КАК СтоимостьНУ,
	|	ТаблицаДокумента.СтоимостьПР КАК СтоимостьПР,
	|	ТаблицаДокумента.СтоимостьЦФ КАК СтоимостьЦФ,
	|
	|	ТаблицаДокумента.АмортизацияБУ КАК АмортизацияБУ,
	|	ТаблицаДокумента.АмортизацияНУ КАК АмортизацияНУ,
	|	ТаблицаДокумента.АмортизацияПР КАК АмортизацияПР,
	|	ТаблицаДокумента.АмортизацияУУ КАК АмортизацияУУ,
	|	ТаблицаДокумента.АмортизацияЦФ КАК АмортизацияЦФ,
	|
	|	ТаблицаДокумента.ПервоначальнаяСтоимостьБУ КАК ПервоначальнаяСтоимостьБУ,
	|	ТаблицаДокумента.ПервоначальнаяСтоимостьНУ КАК ПервоначальнаяСтоимостьНУ,
	|	ТаблицаДокумента.ПервоначальнаяСтоимостьУУ КАК ПервоначальнаяСтоимостьУУ
	|
	|ПОМЕСТИТЬ ДанныеДокументаТаблицаНМА
	|
	|ИЗ
	|	Документ.КорректировкаСтоимостиИАмортизацииНМА.НМА КАК ТаблицаДокумента
	|
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В (
	|		ВЫБРАТЬ
	|			ВтСписокДокументов.Ссылка
	|		ИЗ
	|			ВтСписокДокументов КАК ВтСписокДокументов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	НематериальныйАктив
	|";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	Запрос.Текст = СтрСоединить(СписокЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)

	ЗначенияПараметровПроведения = Новый Структура;
	ЗначенияПараметровПроведения.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.КорректировкаСтоимостиИАмортизацииНМА"));
	ЗначенияПараметровПроведения.Вставить("НазваниеДокумента", НСтр("ru = 'Корректировка стоимости и амортизации НМА';
																	|en = 'Adjust intangible assets cost and amortization'"));
	ЗначенияПараметровПроведения.Вставить("ВедетсяРегламентированныйУчетВНА", ВнеоборотныеАктивыСлужебный.ВедетсяРегламентированныйУчетВНА());
	
	ЗначенияПараметровПроведения.Вставить("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.КорректировкаСтоимостиИАмортизацииНМА);

	ЗначенияПараметровПроведения.Вставить("НастройкаХозяйственнойОперации", Справочники.НастройкиХозяйственныхОпераций.КорректировкаСтоимостиИАмортизацииНМА);
	ЗначенияПараметровПроведения.Вставить("СтатьяАП_НМА", ПланыВидовХарактеристик.СтатьиАктивовПассивов.НематериальныеАктивы);
	ЗначенияПараметровПроведения.Вставить("СтатьяАП_ПрибыльУбыток", ПланыВидовХарактеристик.СтатьиАктивовПассивов.ПрибылиИУбытки);
	
	ЗначенияПараметровПроведения.Вставить("ХО_УвеличениеСтоимости", Перечисления.ХозяйственныеОперации.УвеличениеСтоимостиНМА);
	ЗначенияПараметровПроведения.Вставить("ХО_УменьшениеСтоимости", Перечисления.ХозяйственныеОперации.УменьшениеСтоимостиНМА);
	ЗначенияПараметровПроведения.Вставить("ХО_УменьшениеСтоимостиЦФ", Перечисления.ХозяйственныеОперации.УменьшениеСтоимостиЦФНМА);
	ЗначенияПараметровПроведения.Вставить("ХО_УвеличениеАмортизации", Перечисления.ХозяйственныеОперации.УвеличениеНакопленнойАмортизацииНМА);
	ЗначенияПараметровПроведения.Вставить("ХО_УвеличениеАмортизацииЦФ", Перечисления.ХозяйственныеОперации.УвеличениеНакопленнойАмортизацииЦФНМА);
	ЗначенияПараметровПроведения.Вставить("ХО_УменьшениеАмортизации", Перечисления.ХозяйственныеОперации.УменьшениеНакопленнойАмортизацииНМА);
	ЗначенияПараметровПроведения.Вставить("ХО_ИзменениеДоходовЦФ", Перечисления.ХозяйственныеОперации.ИзменениеДоходовБудущихПериодовОтЦелевогоФинансированияНМА);
	ЗначенияПараметровПроведения.Вставить("НастройкаХО_УвеличениеСтоимости", Справочники.НастройкиХозяйственныхОпераций.УвеличениеСтоимостиНМА);
	ЗначенияПараметровПроведения.Вставить("НастройкаХО_УменьшениеСтоимости", Справочники.НастройкиХозяйственныхОпераций.УменьшениеСтоимостиНМА);
	ЗначенияПараметровПроведения.Вставить("НастройкаХО_УменьшениеСтоимостиЦФ", Справочники.НастройкиХозяйственныхОпераций.УменьшениеСтоимостиЦФНМА);
	ЗначенияПараметровПроведения.Вставить("НастройкаХО_УменьшениеНакопленнойАмортизации", Справочники.НастройкиХозяйственныхОпераций.УменьшениеНакопленнойАмортизацииНМА);
	ЗначенияПараметровПроведения.Вставить("НастройкаХО_УвеличениеНакопленнойАмортизации", Справочники.НастройкиХозяйственныхОпераций.УвеличениеНакопленнойАмортизацииНМА);
	ЗначенияПараметровПроведения.Вставить("НастройкаХО_УвеличениеНакопленнойАмортизацииЦФ", Справочники.НастройкиХозяйственныхОпераций.УвеличениеНакопленнойАмортизацииЦФНМА);
	ЗначенияПараметровПроведения.Вставить("ИдентификаторНеиспользуемойФинЗаписи", ПроведениеДокументов.ИдентификаторНеиспользуемойФинЗаписи());
	
	Если Реквизиты <> Неопределено Тогда
		
		ЗначенияПараметровПроведения.Вставить("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
		
	КонецЕсли; 
	
	Возврат ЗначенияПараметровПроведения;
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                  КАК Ссылка,
	|	ДанныеДокумента.Дата                    КАК ДатаДокументаИБ,
	|	ДанныеДокумента.Номер                   КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	ДанныеДокумента.Организация             КАК Организация,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Подразделение           КАК Подразделение,
	|	ДанныеДокумента.Ответственный           КАК Ответственный,
	|	ДанныеДокумента.Комментарий             КАК Комментарий,
	|	ДанныеДокумента.Проведен                КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления         КАК ПометкаУдаления,
	|	ЛОЖЬ                                    КАК ДополнительнаяЗапись,
	|	ДанныеДокумента.Дата                    КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать                          КАК НомерПервичногоДокумента,
	|	ЛОЖЬ                                    КАК СторноИсправление,
	|	НЕОПРЕДЕЛЕНО                            КАК СторнируемыйДокумент,
	|	НЕОПРЕДЕЛЕНО                            КАК ИсправляемыйДокумент,
	|	ДанныеДокумента.Дата                    КАК ДатаОтраженияВУчете,
	|	НЕОПРЕДЕЛЕНО                            КАК Приоритет
	|
	|ИЗ
	|	Документ.КорректировкаСтоимостиИАмортизацииНМА КАК ДанныеДокумента
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДокументыПоНМА(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДокументыПоНМА";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(ТаблицаНМА.НомерСтроки-1, 0)   КАК НомерЗаписи,
	|	ДанныеДокумента.Ссылка                  КАК Ссылка,
	|	ДанныеДокумента.Дата                    КАК Дата,
	|	ДанныеДокумента.Организация             КАК Организация,
	|	ДанныеДокумента.Подразделение           КАК Подразделение,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	ДанныеДокумента.Проведен                КАК Проведен,
	|	ДанныеДокумента.ОтражатьВРеглУчете      КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете       КАК ОтражатьВУпрУчете,
	|	ТаблицаНМА.НематериальныйАктив          КАК НематериальныйАктив
	|ИЗ
	|	Документ.КорректировкаСтоимостиИАмортизацииНМА КАК ДанныеДокумента
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаСтоимостиИАмортизацииНМА.НМА КАК ТаблицаНМА
	|		ПО ДанныеДокумента.Ссылка = ТаблицаНМА.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ТекстЗапросаТаблицаСтоимостьНМА(ТекстыЗапроса)

	ИмяРегистра = "СтоимостьНМА";
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыУчетаНМА(ТекстыЗапроса,, "втСведенияОбУчете");

	ТекстЗапросаВтУвеличениеСтоимости(ТекстыЗапроса);
	ТекстЗапросаВтУменьшениеСтоимости(ТекстыЗапроса);
	
	СписокЗапросовОбъединение = Новый Массив;
	
	#Область УвеличениеСтоимости
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)      КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                      КАК Регистратор,
	|	ДанныеДокумента.Период                      КАК Период,
	|	&ХО_УвеличениеСтоимости                     КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация                 КАК Организация,
	|	ВтУвеличениеСтоимости.НематериальныйАктив   КАК НематериальныйАктив,
	|	ДанныеДокумента.Подразделение               КАК Подразделение,
	|	СведенияОбУчете.ГруппаФинансовогоУчета      КАК ГруппаФинансовогоУчета,
	|	СведенияОбУчете.НаправлениеДеятельности     КАК НаправлениеДеятельности,
	|
	|	ВтУвеличениеСтоимости.Стоимость             КАК Стоимость,
	|	ВтУвеличениеСтоимости.СтоимостьРегл         КАК СтоимостьРегл,
	|	ВтУвеличениеСтоимости.СтоимостьНУ           КАК СтоимостьНУ,
	|	ВтУвеличениеСтоимости.СтоимостьПР           КАК СтоимостьПР,
	|
	|	ВтУвеличениеСтоимости.СтоимостьРегл
	|		- ВтУвеличениеСтоимости.СтоимостьНУ
	|		- ВтУвеличениеСтоимости.СтоимостьПР     КАК СтоимостьВР,
	|
	|	ВтУвеличениеСтоимости.СтоимостьЦФ           КАК СтоимостьЦФ,
	|	ВтУвеличениеСтоимости.СтоимостьПРЦФ         КАК СтоимостьПРЦФ,
	|
	|	ДанныеДокумента.Организация                 КАК КорОрганизация,
	|	ДанныеДокумента.Подразделение               КАК КорПодразделение,
	|	СведенияОбУчете.НаправлениеДеятельности     КАК КорНаправлениеДеятельности,
	|	ДанныеДокумента.СтатьяРасходов              КАК КорСтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходов           КАК КорАналитикаРасходов,
	|	СведенияОбУчете.ГруппаФинансовогоУчета      КАК КорГруппаФинансовогоУчета,
	|	ДанныеДокумента.ОтражатьВРеглУчете          КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете           КАК ОтражатьВУпрУчете,
	|
	|	ВтУвеличениеСтоимости.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|	ВтУвеличениеСтоимости.НастройкаХозяйственнойОперации  КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтУвеличениеСтоимости КАК ВтУвеличениеСтоимости
	|		ПО ВтУвеличениеСтоимости.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.Ссылка = ВтУвеличениеСтоимости.Ссылка
	|			И СведенияОбУчете.НематериальныйАктив = ВтУвеличениеСтоимости.НематериальныйАктив
	|
	|ГДЕ
	|	ВтУвеличениеСтоимости.Стоимость <> 0
	|		ИЛИ ВтУвеличениеСтоимости.СтоимостьРегл <> 0
	|		ИЛИ ВтУвеличениеСтоимости.СтоимостьНУ <> 0
	|		ИЛИ ВтУвеличениеСтоимости.СтоимостьПР <> 0
	|		ИЛИ ВтУвеличениеСтоимости.СтоимостьЦФ <> 0
	|		ИЛИ ВтУвеличениеСтоимости.СтоимостьПРЦФ <> 0
	|";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область УменьшениеСтоимости
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)      КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                      КАК Регистратор,
	|	ДанныеДокумента.Период                      КАК Период,
	|	&ХО_УменьшениеСтоимости                     КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация                 КАК Организация,
	|	ВтУменьшениеСтоимости.НематериальныйАктив   КАК НематериальныйАктив,
	|	ДанныеДокумента.Подразделение               КАК Подразделение,
	|	СведенияОбУчете.ГруппаФинансовогоУчета      КАК ГруппаФинансовогоУчета,
	|	СведенияОбУчете.НаправлениеДеятельности     КАК НаправлениеДеятельности,
	|
	|	ВтУменьшениеСтоимости.Стоимость             КАК Стоимость,
	|	ВтУменьшениеСтоимости.СтоимостьРегл         КАК СтоимостьРегл,
	|	ВтУменьшениеСтоимости.СтоимостьНУ           КАК СтоимостьНУ,
	|	ВтУменьшениеСтоимости.СтоимостьПР           КАК СтоимостьПР,
	|
	|	ВтУменьшениеСтоимости.СтоимостьРегл
	|		- ВтУменьшениеСтоимости.СтоимостьНУ
	|		- ВтУменьшениеСтоимости.СтоимостьПР     КАК СтоимостьВР,
	|
	|	0 КАК СтоимостьЦФ,
	|	0 КАК СтоимостьПРЦФ,
	|
	|	ДанныеДокумента.Организация                 КАК КорОрганизация,
	|	ДанныеДокумента.Подразделение               КАК КорПодразделение,
	|	СведенияОбУчете.НаправлениеДеятельности     КАК КорНаправлениеДеятельности,
	|	ДанныеДокумента.СтатьяРасходов              КАК КорСтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходов           КАК КорАналитикаРасходов,
	|	СведенияОбУчете.ГруппаФинансовогоУчета      КАК КорГруппаФинансовогоУчета,
	|	ДанныеДокумента.ОтражатьВРеглУчете          КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете           КАК ОтражатьВУпрУчете,
	|
	|	ВтУменьшениеСтоимости.ИдентификаторФинЗаписи          КАК ИдентификаторФинЗаписи,
	|	ВтУменьшениеСтоимости.НастройкаХозяйственнойОперации  КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтУменьшениеСтоимости КАК ВтУменьшениеСтоимости
	|		ПО ВтУменьшениеСтоимости.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.Ссылка = ВтУменьшениеСтоимости.Ссылка
	|			И СведенияОбУчете.НематериальныйАктив = ВтУменьшениеСтоимости.НематериальныйАктив
	|
	|ГДЕ
	|	ВтУменьшениеСтоимости.Стоимость <> 0
	|		ИЛИ ВтУменьшениеСтоимости.СтоимостьРегл <> 0
	|		ИЛИ ВтУменьшениеСтоимости.СтоимостьНУ <> 0
	|		ИЛИ ВтУменьшениеСтоимости.СтоимостьПР <> 0
	|";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область УменьшениеСтоимостиЦФ
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)      КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                      КАК Регистратор,
	|	ДанныеДокумента.Период                      КАК Период,
	|	&ХО_УменьшениеСтоимостиЦФ                   КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация                 КАК Организация,
	|	ВтУменьшениеСтоимости.НематериальныйАктив   КАК НематериальныйАктив,
	|	ДанныеДокумента.Подразделение               КАК Подразделение,
	|	СведенияОбУчете.ГруппаФинансовогоУчета      КАК ГруппаФинансовогоУчета,
	|	СведенияОбУчете.НаправлениеДеятельности     КАК НаправлениеДеятельности,
	|
	|	0 КАК Стоимость,
	|	0 КАК СтоимостьРегл,
	|	0 КАК СтоимостьНУ,
	|	0 КАК СтоимостьПР,
	|	0 КАК СтоимостьВР,
	|
	|	ВтУменьшениеСтоимости.СтоимостьЦФ           КАК СтоимостьЦФ,
	|	ВтУменьшениеСтоимости.СтоимостьПРЦФ         КАК СтоимостьПРЦФ,
	|
	|	ДанныеДокумента.Организация                 КАК КорОрганизация,
	|	ДанныеДокумента.Подразделение               КАК КорПодразделение,
	|	СведенияОбУчете.НаправлениеДеятельности     КАК КорНаправлениеДеятельности,
	|	ДанныеДокумента.СтатьяРасходов              КАК КорСтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходов           КАК КорАналитикаРасходов,
	|	СведенияОбУчете.ГруппаФинансовогоУчета      КАК КорГруппаФинансовогоУчета,
	|	ДанныеДокумента.ОтражатьВРеглУчете          КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете           КАК ОтражатьВУпрУчете,
	|
	|	ВтУменьшениеСтоимости.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_УменьшениеСтоимостиЦФ  КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтУменьшениеСтоимости КАК ВтУменьшениеСтоимости
	|		ПО ВтУменьшениеСтоимости.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.Ссылка = ВтУменьшениеСтоимости.Ссылка
	|			И СведенияОбУчете.НематериальныйАктив = ВтУменьшениеСтоимости.НематериальныйАктив
	|
	|ГДЕ
	|	ВтУменьшениеСтоимости.СтоимостьЦФ <> 0
	|		ИЛИ ВтУменьшениеСтоимости.СтоимостьПРЦФ <> 0
	|";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(СписокЗапросовОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаАмортизацияНМА(ТекстыЗапроса)

	ИмяРегистра = "АмортизацияНМА";
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыУчетаНМА(ТекстыЗапроса,, "втСведенияОбУчете");
	
	ТекстЗапросаВтУвеличениеАмортизации(ТекстыЗапроса);
	ТекстЗапросаВтУменьшениеАмортизации(ТекстыЗапроса);
	
	СписокЗапросовОбъединение = Новый Массив;

	#Область УвеличениеАмортизации
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)      КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                      КАК Регистратор,
	|	ДанныеДокумента.Период                      КАК Период,
	|	&ХО_УвеличениеАмортизации                   КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация                 КАК Организация,
	|	ВтУвеличениеАмортизации.НематериальныйАктив КАК НематериальныйАктив,
	|	ДанныеДокумента.Подразделение               КАК Подразделение,
	|	СведенияОбУчете.ГруппаФинансовогоУчета      КАК ГруппаФинансовогоУчета,
	|	СведенияОбУчете.НаправлениеДеятельности     КАК НаправлениеДеятельности,
	|
	|	ВтУвеличениеАмортизации.Амортизация         КАК Амортизация,
	|	ВтУвеличениеАмортизации.АмортизацияРегл     КАК АмортизацияРегл,
	|	ВтУвеличениеАмортизации.АмортизацияНУ       КАК АмортизацияНУ,
	|	ВтУвеличениеАмортизации.АмортизацияПР       КАК АмортизацияПР,
	|
	|	ВтУвеличениеАмортизации.АмортизацияРегл
	|		- ВтУвеличениеАмортизации.АмортизацияНУ
	|		- ВтУвеличениеАмортизации.АмортизацияПР КАК АмортизацияВР,
	|
	|	0 КАК АмортизацияЦФ,
	|	0 КАК АмортизацияПРЦФ,
	|
	|	ДанныеДокумента.Организация                 КАК КорОрганизация,
	|	ДанныеДокумента.Подразделение               КАК КорПодразделение,
	|	СведенияОбУчете.НаправлениеДеятельности     КАК КорНаправлениеДеятельности,
	|	ДанныеДокумента.СтатьяРасходов              КАК КорСтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходов           КАК КорАналитикаРасходов,
	|	СведенияОбУчете.ГруппаФинансовогоУчета      КАК КорГруппаФинансовогоУчета,
	|	ДанныеДокумента.ОтражатьВРеглУчете          КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете           КАК ОтражатьВУпрУчете,
	|
	|	ВтУвеличениеАмортизации.ИдентификаторФинЗаписи          КАК ИдентификаторФинЗаписи,
	|	ВтУвеличениеАмортизации.НастройкаХозяйственнойОперации  КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтУвеличениеАмортизации КАК ВтУвеличениеАмортизации
	|		ПО ВтУвеличениеАмортизации.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.Ссылка = ВтУвеличениеАмортизации.Ссылка
	|			И СведенияОбУчете.НематериальныйАктив = ВтУвеличениеАмортизации.НематериальныйАктив
	|
	|ГДЕ
	|	ВтУвеличениеАмортизации.Амортизация <> 0
	|		ИЛИ ВтУвеличениеАмортизации.АмортизацияРегл <> 0
	|		ИЛИ ВтУвеличениеАмортизации.АмортизацияНУ <> 0
	|		ИЛИ ВтУвеличениеАмортизации.АмортизацияПР <> 0
	|";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область УвеличениеАмортизацииЦФ
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)      КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                      КАК Регистратор,
	|	ДанныеДокумента.Период                      КАК Период,
	|	&ХО_УвеличениеАмортизацииЦФ                 КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация                 КАК Организация,
	|	ВтУвеличениеАмортизации.НематериальныйАктив КАК НематериальныйАктив,
	|	ДанныеДокумента.Подразделение               КАК Подразделение,
	|	СведенияОбУчете.ГруппаФинансовогоУчета      КАК ГруппаФинансовогоУчета,
	|	СведенияОбУчете.НаправлениеДеятельности     КАК НаправлениеДеятельности,
	|
	|	0 КАК Амортизация,
	|	0 КАК АмортизацияРегл,
	|	0 КАК АмортизацияНУ,
	|	0 КАК АмортизацияПР,
	|	0 КАК АмортизацияВР,
	|
	|	ВтУвеличениеАмортизации.АмортизацияЦФ       КАК АмортизацияЦФ,
	|	ВтУвеличениеАмортизации.АмортизацияПРЦФ     КАК АмортизацияПРЦФ,
	|
	|	ДанныеДокумента.Организация                 КАК КорОрганизация,
	|	ДанныеДокумента.Подразделение               КАК КорПодразделение,
	|	СведенияОбУчете.НаправлениеДеятельности     КАК КорНаправлениеДеятельности,
	|	ДанныеДокумента.СтатьяРасходов              КАК КорСтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходов           КАК КорАналитикаРасходов,
	|	СведенияОбУчете.ГруппаФинансовогоУчета      КАК КорГруппаФинансовогоУчета,
	|	ДанныеДокумента.ОтражатьВРеглУчете          КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете           КАК ОтражатьВУпрУчете,
	|
	|	ВтУвеличениеАмортизации.ИдентификаторФинЗаписи   КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_УвеличениеНакопленнойАмортизацииЦФ  КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтУвеличениеАмортизации КАК ВтУвеличениеАмортизации
	|		ПО ВтУвеличениеАмортизации.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.Ссылка = ВтУвеличениеАмортизации.Ссылка
	|			И СведенияОбУчете.НематериальныйАктив = ВтУвеличениеАмортизации.НематериальныйАктив
	|
	|ГДЕ
	|	ВтУвеличениеАмортизации.АмортизацияЦФ <> 0
	|		ИЛИ ВтУвеличениеАмортизации.АмортизацияПРЦФ <> 0
	|";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область УменьшениеАмортизации
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)      КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                      КАК Регистратор,
	|	ДанныеДокумента.Период                      КАК Период,
	|	&ХО_УменьшениеАмортизации                   КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация                 КАК Организация,
	|	ВтУменьшениеАмортизации.НематериальныйАктив КАК НематериальныйАктив,
	|	ДанныеДокумента.Подразделение               КАК Подразделение,
	|	СведенияОбУчете.ГруппаФинансовогоУчета      КАК ГруппаФинансовогоУчета,
	|	СведенияОбУчете.НаправлениеДеятельности     КАК НаправлениеДеятельности,
	|
	|	ВтУменьшениеАмортизации.Амортизация         КАК Амортизация,
	|	ВтУменьшениеАмортизации.АмортизацияРегл     КАК АмортизацияРегл,
	|	ВтУменьшениеАмортизации.АмортизацияНУ       КАК АмортизацияНУ,
	|	ВтУменьшениеАмортизации.АмортизацияПР       КАК АмортизацияПР,
	|
	|	ВтУменьшениеАмортизации.АмортизацияРегл
	|		- ВтУменьшениеАмортизации.АмортизацияНУ
	|		- ВтУменьшениеАмортизации.АмортизацияПР КАК АмортизацияВР,
	|
	|	ВтУменьшениеАмортизации.АмортизацияЦФ       КАК АмортизацияЦФ,
	|	ВтУменьшениеАмортизации.АмортизацияПРЦФ     КАК АмортизацияПРЦФ,
	|
	|	ДанныеДокумента.Организация                 КАК КорОрганизация,
	|	ДанныеДокумента.Подразделение               КАК КорПодразделение,
	|	СведенияОбУчете.НаправлениеДеятельности     КАК КорНаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                КАК КорСтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                                КАК КорАналитикаРасходов,
	|	СведенияОбУчете.ГруппаФинансовогоУчета      КАК КорГруппаФинансовогоУчета,
	|	ДанныеДокумента.ОтражатьВРеглУчете          КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете           КАК ОтражатьВУпрУчете,
	|
	|	ВтУменьшениеАмортизации.ИдентификаторФинЗаписи          КАК ИдентификаторФинЗаписи,
	|	ВтУменьшениеАмортизации.НастройкаХозяйственнойОперации  КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтУменьшениеАмортизации КАК ВтУменьшениеАмортизации
	|		ПО ВтУменьшениеАмортизации.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.Ссылка = ВтУменьшениеАмортизации.Ссылка
	|			И СведенияОбУчете.НематериальныйАктив = ВтУменьшениеАмортизации.НематериальныйАктив
	|
	|ГДЕ
	|	ВтУменьшениеАмортизации.Амортизация <> 0
	|		ИЛИ ВтУменьшениеАмортизации.АмортизацияРегл <> 0
	|		ИЛИ ВтУменьшениеАмортизации.АмортизацияНУ <> 0
	|		ИЛИ ВтУменьшениеАмортизации.АмортизацияПР <> 0
	|		ИЛИ ВтУменьшениеАмортизации.АмортизацияЦФ <> 0
	|		ИЛИ ВтУменьшениеАмортизации.АмортизацияПРЦФ <> 0
	|";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	ТекстЗапроса = СтрСоединить(СписокЗапросовОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаПрочиеРасходы(ТекстыЗапроса, Регистры = Неопределено)
	
	ИмяРегистра = "ПрочиеРасходы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ДопПоля = "Регистратор";

	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(ТекстыЗапроса, ДопПоля);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаПрочиеРасходы(ДопПоля, Истина);
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПартииПрочихРасходов(ТекстыЗапроса, Регистры = Неопределено)
	
	ИмяРегистра = "ПартииПрочихРасходов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ДопПоля = "Регистратор";

	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПартииПрочихРасходов(ТекстыЗапроса, ДопПоля);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаПартииПрочихРасходов(ДопПоля);
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ТекстЗапросаТаблицаПрочиеАктивыПассивы(ТекстыЗапроса)

	ИмяРегистра = "ПрочиеАктивыПассивы";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыУчетаНМА(ТекстыЗапроса,, "втСведенияОбУчете");
	
	ТекстЗапросаВтУменьшениеСтоимости(ТекстыЗапроса);
	ТекстЗапросаВтУвеличениеАмортизации(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)    КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                    КАК Регистратор,
	|	ДанныеДокумента.Период                    КАК Период,
	|	&ХО_УменьшениеСтоимости                   КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация               КАК Организация,
	|	ДанныеДокумента.Подразделение             КАК Подразделение,
	|	СведенияОбУчете.НаправлениеДеятельности   КАК НаправлениеДеятельности,
	|	ДанныеДокумента.СтатьяРасходов            КАК Статья,
	|	ДанныеДокумента.АналитикаАктивовПассивов  КАК Аналитика,
	|	ВтУменьшениеСтоимости.Стоимость           КАК Сумма
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтУменьшениеСтоимости КАК ВтУменьшениеСтоимости
	|		ПО ВтУменьшениеСтоимости.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.Ссылка = ВтУменьшениеСтоимости.Ссылка
	|			И СведенияОбУчете.НематериальныйАктив = ВтУменьшениеСтоимости.НематериальныйАктив
	|
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ДанныеДокумента.СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиАктивовПассивов)
	|	И ДанныеДокумента.ОтражатьВУпрУчете
	|	И ВтУменьшениеСтоимости.Стоимость <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)    КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                    КАК Регистратор,
	|	ДанныеДокумента.Период                    КАК Период,
	|	&ХО_УвеличениеАмортизации                 КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация               КАК Организация,
	|	ДанныеДокумента.Подразделение             КАК Подразделение,
	|	СведенияОбУчете.НаправлениеДеятельности   КАК НаправлениеДеятельности,
	|	ДанныеДокумента.СтатьяРасходов            КАК Статья,
	|	ДанныеДокумента.АналитикаАктивовПассивов  КАК Аналитика,
	|	ВтУвеличениеАмортизации.Амортизация       КАК Сумма
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтУвеличениеАмортизации КАК ВтУвеличениеАмортизации
	|		ПО ВтУвеличениеАмортизации.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.Ссылка = ВтУвеличениеАмортизации.Ссылка
	|			И СведенияОбУчете.НематериальныйАктив = ВтУвеличениеАмортизации.НематериальныйАктив
	|
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ДанныеДокумента.СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиАктивовПассивов)
	|	И ДанныеДокумента.ОтражатьВУпрУчете
	|	И ВтУвеличениеАмортизации.Амортизация <> 0
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаДвиженияПоПрочимАктивамПассивам(ТекстыЗапроса)

	ИмяРегистра = "ДвиженияПоПрочимАктивамПассивам";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыУчетаНМА(ТекстыЗапроса,, "втСведенияОбУчете");
	
	ТекстЗапросаВтУвеличениеСтоимости(ТекстыЗапроса);
	ТекстЗапросаВтУменьшениеСтоимости(ТекстыЗапроса);
	ТекстЗапросаВтУвеличениеАмортизации(ТекстыЗапроса);
	ТекстЗапросаВтУменьшениеАмортизации(ТекстыЗапроса);
	
	ТекстыЗапросаОбъединение = Новый Массив;

	#Область УвеличениеСтоимости
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                  КАК Регистратор,
	|	ДанныеДокумента.Период                  КАК Период,
	|	ДанныеДокумента.Организация             КАК Организация,
	|	ДанныеДокумента.Подразделение           КАК Подразделение,
	|	СведенияОбУчете.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ДанныеДокумента.СтатьяДоходов           КАК Статья,
	|	ДанныеДокумента.АналитикаАктивовПассивовДоходов КАК Аналитика,
	|	ДанныеДокумента.НастройкаСчетовУчетаДоходов КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Кредит) КАК ДебетКредит,
	|
	|	ВтУвеличениеСтоимости.СтоимостьРегл
	|		+ ВтУвеличениеСтоимости.СтоимостьЦФ КАК СуммаРегл,
	|
	|	ВтУвеличениеСтоимости.СтоимостьПР
	|		+ ВтУвеличениеСтоимости.СтоимостьПРЦФ КАК ПостояннаяРазница,
	|
	|	ВтУвеличениеСтоимости.СтоимостьРегл
	|		+ ВтУвеличениеСтоимости.СтоимостьЦФ
	|		- ВтУвеличениеСтоимости.СтоимостьНУ
	|		- ВтУвеличениеСтоимости.СтоимостьПР
	|		- ВтУвеличениеСтоимости.СтоимостьПРЦФ КАК ВременнаяРазница,
	|
	|	ВтУвеличениеСтоимости.Стоимость КАК СуммаУпр,
	|	ВтУвеличениеСтоимости.Стоимость КАК СуммаСНДС,
	|	ВтУвеличениеСтоимости.Стоимость КАК СуммаБезНДС,
	|
	|	ВтУвеличениеСтоимости.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|	ВтУвеличениеСтоимости.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтУвеличениеСтоимости КАК ВтУвеличениеСтоимости
	|		ПО ВтУвеличениеСтоимости.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.Ссылка = ВтУвеличениеСтоимости.Ссылка
	|			И СведенияОбУчете.НематериальныйАктив = ВтУвеличениеСтоимости.НематериальныйАктив
	|
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ДанныеДокумента.СтатьяДоходов) = ТИП(ПланВидовХарактеристик.СтатьиАктивовПассивов)
	|	И (ВтУвеличениеСтоимости.Стоимость <> 0
	|		ИЛИ ВтУвеличениеСтоимости.СтоимостьРегл <> 0
	|		ИЛИ ВтУвеличениеСтоимости.СтоимостьЦФ <> 0
	|		ИЛИ ВтУвеличениеСтоимости.СтоимостьНУ <> 0
	|		ИЛИ ВтУвеличениеСтоимости.СтоимостьПР <> 0
	|		ИЛИ ВтУвеличениеСтоимости.СтоимостьПРЦФ <> 0)
	|";
	ТекстыЗапросаОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область УменьшениеСтоимости
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                  КАК Регистратор,
	|	ДанныеДокумента.Период                  КАК Период,
	|	ДанныеДокумента.Организация             КАК Организация,
	|	ДанныеДокумента.Подразделение           КАК Подразделение,
	|	СведенияОбУчете.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ДанныеДокумента.СтатьяРасходов          КАК Статья,
	|	ДанныеДокумента.АналитикаАктивовПассивов КАК Аналитика,
	|	ДанныеДокумента.НастройкаСчетовУчета    КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Дебет) КАК ДебетКредит,
	|
	|	ВтУменьшениеСтоимости.СтоимостьРегл
	|		+ ВтУменьшениеСтоимости.СтоимостьЦФ   КАК СуммаРегл,
	|
	|	ВтУменьшениеСтоимости.СтоимостьПР
	|		+ ВтУменьшениеСтоимости.СтоимостьПРЦФ КАК ПостояннаяРазница,
	|
	|	ВтУменьшениеСтоимости.СтоимостьРегл
	|		+ ВтУменьшениеСтоимости.СтоимостьЦФ
	|		- ВтУменьшениеСтоимости.СтоимостьНУ
	|		- ВтУменьшениеСтоимости.СтоимостьПР
	|		- ВтУменьшениеСтоимости.СтоимостьПРЦФ КАК ВременнаяРазница,
	|
	|	ВтУменьшениеСтоимости.Стоимость КАК СуммаУпр,
	|	ВтУменьшениеСтоимости.Стоимость КАК СуммаСНДС,
	|	ВтУменьшениеСтоимости.Стоимость КАК СуммаБезНДС,
	|
	|	ВтУменьшениеСтоимости.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|	ВтУменьшениеСтоимости.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтУменьшениеСтоимости КАК ВтУменьшениеСтоимости
	|		ПО ВтУменьшениеСтоимости.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.Ссылка = ВтУменьшениеСтоимости.Ссылка
	|			И СведенияОбУчете.НематериальныйАктив = ВтУменьшениеСтоимости.НематериальныйАктив
	|
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ДанныеДокумента.СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиАктивовПассивов)
	|	И (ВтУменьшениеСтоимости.Стоимость <> 0
	|		ИЛИ ВтУменьшениеСтоимости.СтоимостьРегл <> 0
	|		ИЛИ ВтУменьшениеСтоимости.СтоимостьЦФ <> 0
	|		ИЛИ ВтУменьшениеСтоимости.СтоимостьНУ <> 0
	|		ИЛИ ВтУменьшениеСтоимости.СтоимостьПР <> 0
	|		ИЛИ ВтУменьшениеСтоимости.СтоимостьПРЦФ <> 0)
	|";
	ТекстыЗапросаОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область УвеличениеАмортизации
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                  КАК Регистратор,
	|	ДанныеДокумента.Период                  КАК Период,
	|	ДанныеДокумента.Организация             КАК Организация,
	|	ДанныеДокумента.Подразделение           КАК Подразделение,
	|	СведенияОбУчете.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ДанныеДокумента.СтатьяРасходов          КАК Статья,
	|	ДанныеДокумента.АналитикаАктивовПассивов КАК Аналитика,
	|	ДанныеДокумента.НастройкаСчетовУчета    КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Дебет) КАК ДебетКредит,
	|
	|	ВтУвеличениеАмортизации.АмортизацияРегл
	|		+ ВтУвеличениеАмортизации.АмортизацияЦФ   КАК СуммаРегл,
	|
	|	ВтУвеличениеАмортизации.АмортизацияПР
	|		+ ВтУвеличениеАмортизации.АмортизацияПРЦФ КАК ПостояннаяРазница,
	|
	|	ВтУвеличениеАмортизации.АмортизацияРегл
	|		+ ВтУвеличениеАмортизации.АмортизацияЦФ
	|		- ВтУвеличениеАмортизации.АмортизацияНУ
	|		- ВтУвеличениеАмортизации.АмортизацияПР
	|		- ВтУвеличениеАмортизации.АмортизацияПРЦФ КАК ВременнаяРазница,
	|
	|	ВтУвеличениеАмортизации.Амортизация КАК СуммаУпр,
	|	ВтУвеличениеАмортизации.Амортизация КАК СуммаСНДС,
	|	ВтУвеличениеАмортизации.Амортизация КАК СуммаБезНДС,
	|
	|	ВтУвеличениеАмортизации.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|	ВтУвеличениеАмортизации.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтУвеличениеАмортизации КАК ВтУвеличениеАмортизации
	|		ПО ВтУвеличениеАмортизации.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.Ссылка = ВтУвеличениеАмортизации.Ссылка
	|			И СведенияОбУчете.НематериальныйАктив = ВтУвеличениеАмортизации.НематериальныйАктив
	|
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ДанныеДокумента.СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиАктивовПассивов)
	|	И (ВтУвеличениеАмортизации.Амортизация <> 0
	|		ИЛИ ВтУвеличениеАмортизации.АмортизацияРегл <> 0
	|		ИЛИ ВтУвеличениеАмортизации.АмортизацияЦФ <> 0
	|		ИЛИ ВтУвеличениеАмортизации.АмортизацияНУ <> 0
	|		ИЛИ ВтУвеличениеАмортизации.АмортизацияПР <> 0
	|		ИЛИ ВтУвеличениеАмортизации.АмортизацияПРЦФ <> 0)
	|";
	ТекстыЗапросаОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область УменьшениеАмортизации
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                  КАК Регистратор,
	|	ДанныеДокумента.Период                  КАК Период,
	|	ДанныеДокумента.Организация             КАК Организация,
	|	ДанныеДокумента.Подразделение           КАК Подразделение,
	|	СведенияОбУчете.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ДанныеДокумента.СтатьяДоходов           КАК Статья,
	|	ДанныеДокумента.АналитикаАктивовПассивовДоходов КАК Аналитика,
	|	ДанныеДокумента.НастройкаСчетовУчетаДоходов КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Кредит) КАК ДебетКредит,
	|
	|	ВтУменьшениеАмортизации.АмортизацияРегл
	|		+ ВтУменьшениеАмортизации.АмортизацияЦФ   КАК СуммаРегл,
	|
	|	ВтУменьшениеАмортизации.АмортизацияПР
	|		+ ВтУменьшениеАмортизации.АмортизацияПРЦФ КАК ПостояннаяРазница,
	|
	|	ВтУменьшениеАмортизации.АмортизацияРегл
	|		+ ВтУменьшениеАмортизации.АмортизацияЦФ
	|		- ВтУменьшениеАмортизации.АмортизацияНУ
	|		- ВтУменьшениеАмортизации.АмортизацияПР
	|		- ВтУменьшениеАмортизации.АмортизацияПРЦФ КАК ВременнаяРазница,
	|
	|	ВтУменьшениеАмортизации.Амортизация КАК СуммаУпр,
	|	ВтУменьшениеАмортизации.Амортизация КАК СуммаСНДС,
	|	ВтУменьшениеАмортизации.Амортизация КАК СуммаБезНДС,
	|
	|	ВтУменьшениеАмортизации.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|	ВтУменьшениеАмортизации.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтУменьшениеАмортизации КАК ВтУменьшениеАмортизации
	|		ПО ВтУменьшениеАмортизации.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.Ссылка = ВтУменьшениеАмортизации.Ссылка
	|			И СведенияОбУчете.НематериальныйАктив = ВтУменьшениеАмортизации.НематериальныйАктив
	|
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ДанныеДокумента.СтатьяДоходов) = ТИП(ПланВидовХарактеристик.СтатьиАктивовПассивов)
	|	И (ВтУменьшениеАмортизации.Амортизация <> 0
	|		ИЛИ ВтУменьшениеАмортизации.АмортизацияРегл <> 0
	|		ИЛИ ВтУменьшениеАмортизации.АмортизацияЦФ <> 0
	|		ИЛИ ВтУменьшениеАмортизации.АмортизацияНУ <> 0
	|		ИЛИ ВтУменьшениеАмортизации.АмортизацияПР <> 0
	|		ИЛИ ВтУменьшениеАмортизации.АмортизацияПРЦФ <> 0)
	|";
	ТекстыЗапросаОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	ТекстЗапроса = СтрСоединить(ТекстыЗапросаОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаДвиженияДоходыРасходыПрочиеАктивыПассивы(ТекстыЗапроса)

	ИмяРегистра = "ДвиженияДоходыРасходыПрочиеАктивыПассивы";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыУчетаНМА(ТекстыЗапроса,, "втСведенияОбУчете");

	ТекстЗапросаВтУвеличениеСтоимости(ТекстыЗапроса);
	ТекстЗапросаВтУменьшениеСтоимости(ТекстыЗапроса);
	ТекстЗапросаВтУвеличениеАмортизации(ТекстыЗапроса);
	ТекстЗапросаВтУменьшениеАмортизации(ТекстыЗапроса);
	
	ТекстыЗапросаОбъединение = Новый Массив;
	
	#Область УвеличениеСтоимости
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                     КАК Регистратор,
	|	ДанныеДокумента.Период                     КАК Период,
	|	&ХО_УвеличениеСтоимости                    КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация                КАК Организация,
	|
	|	ДанныеДокумента.Подразделение              КАК Подразделение,
	|	СведенияОбУчете.НаправлениеДеятельности    КАК НаправлениеДеятельности,
	|	&СтатьяАП_ПрибыльУбыток                    КАК Статья,
	|	НЕОПРЕДЕЛЕНО                               КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                               КАК АналитикаРасходов,
	|	ВтУвеличениеСтоимости.НематериальныйАктив  КАК АналитикаАктивовПассивов,
	|	СведенияОбУчете.ГруппаФинансовогоУчета     КАК ГруппаФинансовогоУчета,
	|
	|	ДанныеДокумента.Подразделение              КАК КорПодразделение,
	|	СведенияОбУчете.НаправлениеДеятельности    КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_НМА                              КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                               КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                               КАК КорАналитикаРасходов,
	|	ВтУвеличениеСтоимости.НематериальныйАктив  КАК КорАналитикаАктивовПассивов,
	|	СведенияОбУчете.ГруппаФинансовогоУчета     КАК КорГруппаФинансовогоУчета,
	|
	|	ВтУвеличениеСтоимости.Стоимость            КАК Сумма,
	|	ВтУвеличениеСтоимости.Стоимость            КАК СуммаУпр,
	|	ВтУвеличениеСтоимости.СтоимостьРегл        КАК СуммаРегл
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтУвеличениеСтоимости КАК ВтУвеличениеСтоимости
	|		ПО ВтУвеличениеСтоимости.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.Ссылка = ВтУвеличениеСтоимости.Ссылка
	|			И СведенияОбУчете.НематериальныйАктив = ВтУвеличениеСтоимости.НематериальныйАктив
	|
	|ГДЕ
	|	(ВтУвеличениеСтоимости.Стоимость <> 0
	|		ИЛИ ВтУвеличениеСтоимости.СтоимостьРегл <> 0)
	|";
	ТекстыЗапросаОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область УменьшениеСтоимости
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                     КАК Регистратор,
	|	ДанныеДокумента.Период                     КАК Период,
	|	&ХО_УменьшениеСтоимости                    КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация                КАК Организация,
	|
	|	ДанныеДокумента.Подразделение              КАК Подразделение,
	|	СведенияОбУчете.НаправлениеДеятельности    КАК НаправлениеДеятельности,
	|	&СтатьяАП_НМА                              КАК Статья,
	|	НЕОПРЕДЕЛЕНО                               КАК АналитикаДоходов,
	|	Неопределено                               КАК АналитикаРасходов,
	|	ВтУменьшениеСтоимости.НематериальныйАктив  КАК АналитикаАктивовПассивов,
	|	СведенияОбУчете.ГруппаФинансовогоУчета     КАК ГруппаФинансовогоУчета,
	|
	|	ДанныеДокумента.Подразделение              КАК КорПодразделение,
	|	СведенияОбУчете.НаправлениеДеятельности    КАК КорНаправлениеДеятельности,
	|	ДанныеДокумента.СтатьяРасходов             КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                               КАК КорАналитикаДоходов,
	|	ДанныеДокумента.АналитикаРасходов          КАК КорАналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                               КАК КорАналитикаАктивовПассивов,
	|	СведенияОбУчете.ГруппаФинансовогоУчета     КАК КорГруппаФинансовогоУчета,
	|
	|	ВтУменьшениеСтоимости.Стоимость            КАК Сумма,
	|	ВтУменьшениеСтоимости.Стоимость            КАК СуммаУпр,
	|	ВтУменьшениеСтоимости.СтоимостьРегл        КАК СуммаРегл
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтУменьшениеСтоимости КАК ВтУменьшениеСтоимости
	|		ПО ВтУменьшениеСтоимости.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.Ссылка = ВтУменьшениеСтоимости.Ссылка
	|			И СведенияОбУчете.НематериальныйАктив = ВтУменьшениеСтоимости.НематериальныйАктив
	|ГДЕ
	|	(ВтУменьшениеСтоимости.Стоимость <> 0
	|		ИЛИ ВтУменьшениеСтоимости.СтоимостьРегл <> 0)
	|";
	ТекстыЗапросаОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область УвеличениеСтоимостиЦФ
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                     КАК Регистратор,
	|	ДанныеДокумента.Период                     КАК Период,
	|	&ХО_УвеличениеСтоимости                    КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация                КАК Организация,
	|
	|	ДанныеДокумента.Подразделение              КАК Подразделение,
	|	СведенияОбУчете.НаправлениеДеятельности    КАК НаправлениеДеятельности,
	|	&СтатьяАП_НМА                              КАК Статья,
	|	НЕОПРЕДЕЛЕНО                               КАК АналитикаДоходов,
	|	Неопределено                               КАК АналитикаРасходов,
	|	ВтУвеличениеСтоимости.НематериальныйАктив  КАК АналитикаАктивовПассивов,
	|	СведенияОбУчете.ГруппаФинансовогоУчета     КАК ГруппаФинансовогоУчета,
	|
	|	ДанныеДокумента.Подразделение              КАК КорПодразделение,
	|	СведенияОбУчете.НаправлениеДеятельности    КАК КорНаправлениеДеятельности,
	|	ДанныеДокумента.СтатьяРасходов             КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                               КАК КорАналитикаДоходов,
	|	ДанныеДокумента.АналитикаРасходов          КАК КорАналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                               КАК КорАналитикаАктивовПассивов,
	|	СведенияОбУчете.ГруппаФинансовогоУчета     КАК КорГруппаФинансовогоУчета,
	|
	|	0                                          КАК Сумма,
	|	0                                          КАК СуммаУпр,
	|	ВтУвеличениеСтоимости.СтоимостьЦФ          КАК СуммаРегл
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтУвеличениеСтоимости КАК ВтУвеличениеСтоимости
	|		ПО ВтУвеличениеСтоимости.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.Ссылка = ВтУвеличениеСтоимости.Ссылка
	|			И СведенияОбУчете.НематериальныйАктив = ВтУвеличениеСтоимости.НематериальныйАктив
	|ГДЕ
	|	ВтУвеличениеСтоимости.СтоимостьЦФ <> 0
	|";
	ТекстыЗапросаОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область УменьшениеСтоимостиЦФ
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                     КАК Регистратор,
	|	ДанныеДокумента.Период                     КАК Период,
	|	&ХО_УменьшениеСтоимости                    КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация                КАК Организация,
	|
	|	ДанныеДокумента.Подразделение              КАК Подразделение,
	|	СведенияОбУчете.НаправлениеДеятельности    КАК НаправлениеДеятельности,
	|	&СтатьяАП_ПрибыльУбыток                    КАК Статья,
	|	НЕОПРЕДЕЛЕНО                               КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                               КАК АналитикаРасходов,
	|	ВтУменьшениеСтоимости.НематериальныйАктив  КАК АналитикаАктивовПассивов,
	|	СведенияОбУчете.ГруппаФинансовогоУчета     КАК ГруппаФинансовогоУчета,
	|
	|	ДанныеДокумента.Подразделение              КАК КорПодразделение,
	|	СведенияОбУчете.НаправлениеДеятельности    КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_НМА                              КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                               КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                               КАК КорАналитикаРасходов,
	|	ВтУменьшениеСтоимости.НематериальныйАктив  КАК КорАналитикаАктивовПассивов,
	|	СведенияОбУчете.ГруппаФинансовогоУчета     КАК КорГруппаФинансовогоУчета,
	|
	|	0                                          КАК Сумма,
	|	0                                          КАК СуммаУпр,
	|	ВтУменьшениеСтоимости.СтоимостьЦФ          КАК СуммаРегл
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтУменьшениеСтоимости КАК ВтУменьшениеСтоимости
	|		ПО ВтУменьшениеСтоимости.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.Ссылка = ВтУменьшениеСтоимости.Ссылка
	|			И СведенияОбУчете.НематериальныйАктив = ВтУменьшениеСтоимости.НематериальныйАктив
	|ГДЕ
	|	ВтУменьшениеСтоимости.СтоимостьЦФ <> 0";
	ТекстыЗапросаОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область УменьшениеАмортизации
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                     КАК Регистратор,
	|	ДанныеДокумента.Период                     КАК Период,
	|	&ХО_УменьшениеАмортизации                  КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация                КАК Организация,
	|
	|	ДанныеДокумента.Подразделение              КАК Подразделение,
	|	СведенияОбУчете.НаправлениеДеятельности    КАК НаправлениеДеятельности,
	|	ДанныеДокумента.СтатьяДоходов              КАК Статья,
	|	ДанныеДокумента.АналитикаДоходов           КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                               КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                               КАК АналитикаАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО                               КАК ГруппаФинансовогоУчета,
	|
	|	ДанныеДокумента.Подразделение              КАК КорПодразделение,
	|	СведенияОбУчете.НаправлениеДеятельности    КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_НМА                              КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                               КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                               КАК КорАналитикаРасходов,
	|	ВтУменьшениеАмортизации.НематериальныйАктив КАК КорАналитикаАктивовПассивов,
	|	СведенияОбУчете.ГруппаФинансовогоУчета     КАК КорГруппаФинансовогоУчета,
	|
	|	ВтУменьшениеАмортизации.Амортизация        КАК Сумма,
	|	ВтУменьшениеАмортизации.Амортизация        КАК СуммаУпр,
	|
	|	ВтУменьшениеАмортизации.АмортизацияРегл
	|		+ ВтУменьшениеАмортизации.АмортизацияЦФ КАК СуммаРегл
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтУменьшениеАмортизации КАК ВтУменьшениеАмортизации
	|		ПО ВтУменьшениеАмортизации.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.Ссылка = ВтУменьшениеАмортизации.Ссылка
	|			И СведенияОбУчете.НематериальныйАктив = ВтУменьшениеАмортизации.НематериальныйАктив
	|
	|ГДЕ
	|	(ВтУменьшениеАмортизации.Амортизация <> 0
	|		ИЛИ ВтУменьшениеАмортизации.АмортизацияРегл <> 0
	|		ИЛИ ВтУменьшениеАмортизации.АмортизацияЦФ <> 0)
	|";
	ТекстыЗапросаОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область УвеличениеАмортизации
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                     КАК Регистратор,
	|	ДанныеДокумента.Период                     КАК Период,
	|	&ХО_УвеличениеАмортизации                  КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация                КАК Организация,
	|
	|	ДанныеДокумента.Подразделение              КАК Подразделение,
	|	СведенияОбУчете.НаправлениеДеятельности    КАК НаправлениеДеятельности,
	|	&СтатьяАП_НМА                              КАК Статья,
	|	НЕОПРЕДЕЛЕНО                               КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                               КАК АналитикаРасходов,
	|	ВтУвеличениеАмортизации.НематериальныйАктив КАК АналитикаАктивовПассивов,
	|	СведенияОбУчете.ГруппаФинансовогоУчета     КАК ГруппаФинансовогоУчета,
	|
	|	ДанныеДокумента.Подразделение              КАК КорПодразделение,
	|	СведенияОбУчете.НаправлениеДеятельности    КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_ПрибыльУбыток                    КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                               КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                               КАК КорАналитикаРасходов,
	|	ВтУвеличениеАмортизации.НематериальныйАктив КАК КорАналитикаАктивовПассивов,
	|	СведенияОбУчете.ГруппаФинансовогоУчета     КАК КорГруппаФинансовогоУчета,
	|
	|	ВтУвеличениеАмортизации.Амортизация        КАК Сумма,
	|	ВтУвеличениеАмортизации.Амортизация        КАК СуммаУпр,
	|
	|	ВтУвеличениеАмортизации.АмортизацияРегл
	|		+ ВтУвеличениеАмортизации.АмортизацияЦФ КАК СуммаРегл
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтУвеличениеАмортизации КАК ВтУвеличениеАмортизации
	|		ПО ВтУвеличениеАмортизации.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.Ссылка = ВтУвеличениеАмортизации.Ссылка
	|			И СведенияОбУчете.НематериальныйАктив = ВтУвеличениеАмортизации.НематериальныйАктив
	|ГДЕ
	|	(ВтУвеличениеАмортизации.Амортизация <> 0
	|		ИЛИ ВтУвеличениеАмортизации.АмортизацияРегл <> 0
	|		ИЛИ ВтУвеличениеАмортизации.АмортизацияЦФ <> 0)
	|";
	ТекстыЗапросаОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(ТекстыЗапросаОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаПрочиеДоходы(ТекстыЗапроса)

	ИмяРегистра = "ПрочиеДоходы";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат "";
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыУчетаНМА(ТекстыЗапроса,, "втСведенияОбУчете");
	
	ТекстЗапросаВтУвеличениеСтоимости(ТекстыЗапроса);
	ТекстЗапросаВтУменьшениеСтоимости(ТекстыЗапроса);
	ТекстЗапросаВтУвеличениеАмортизации(ТекстыЗапроса);
	ТекстЗапросаВтУменьшениеАмортизации(ТекстыЗапроса);
	
	ТекстыЗапросаОбъединение = Новый Массив;
	
	#Область УвеличениеСтоимости
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)         КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                         КАК Регистратор,
	|	ДанныеДокумента.Период                         КАК Период,
	|	ДанныеДокумента.Организация                    КАК Организация,
	|	ДанныеДокумента.Подразделение                  КАК Подразделение,
	|	СведенияОбУчете.НаправлениеДеятельности        КАК НаправлениеДеятельности,
	|	&ХО_УвеличениеСтоимости                        КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.СтатьяДоходов                  КАК СтатьяДоходов,
	|	ДанныеДокумента.АналитикаДоходов               КАК АналитикаДоходов,
	|
	|	ВтУвеличениеСтоимости.Стоимость                КАК Сумма,
	|	ВтУвеличениеСтоимости.Стоимость                КАК СуммаУпр,
	|
	|	ВтУвеличениеСтоимости.СтоимостьРегл
	|		+ ВтУвеличениеСтоимости.СтоимостьЦФ        КАК СуммаРегл,
	|
	|	ВтУвеличениеСтоимости.ИдентификаторФинЗаписи          КАК ИдентификаторФинЗаписи,
	|	ВтУвеличениеСтоимости.НастройкаХозяйственнойОперации  КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтУвеличениеСтоимости КАК ВтУвеличениеСтоимости
	|		ПО ВтУвеличениеСтоимости.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.Ссылка = ВтУвеличениеСтоимости.Ссылка
	|			И СведенияОбУчете.НематериальныйАктив = ВтУвеличениеСтоимости.НематериальныйАктив
	|
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ДанныеДокумента.СтатьяДоходов) = ТИП(ПланВидовХарактеристик.СтатьиДоходов)
	|	И (ВтУвеличениеСтоимости.Стоимость <> 0
	|		ИЛИ ВтУвеличениеСтоимости.СтоимостьРегл <> 0
	|		ИЛИ ВтУвеличениеСтоимости.СтоимостьЦФ <> 0)
	|";
	ТекстыЗапросаОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область УменьшениеСтоимостиЦФ
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)         КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                         КАК Регистратор,
	|	ДанныеДокумента.Период                         КАК Период,
	|	ДанныеДокумента.Организация                    КАК Организация,
	|	ДанныеДокумента.Подразделение                  КАК Подразделение,
	|	СведенияОбУчете.НаправлениеДеятельности        КАК НаправлениеДеятельности,
	|	&ХО_ИзменениеДоходовЦФ                         КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.СтатьяДоходов                  КАК СтатьяДоходов,
	|	ДанныеДокумента.АналитикаДоходов               КАК АналитикаДоходов,
	|
	|	0                                              КАК Сумма,
	|	0                                              КАК СуммаУпр,
	|	ВтУменьшениеСтоимости.СтоимостьЦФ              КАК СуммаРегл,
	|
	|	&ИдентификаторНеиспользуемойФинЗаписи          КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПустаяСсылка) КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтУменьшениеСтоимости КАК ВтУменьшениеСтоимости
	|		ПО ВтУменьшениеСтоимости.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.Ссылка = ВтУменьшениеСтоимости.Ссылка
	|			И СведенияОбУчете.НематериальныйАктив = ВтУменьшениеСтоимости.НематериальныйАктив
	|
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ДанныеДокумента.СтатьяДоходов) = ТИП(ПланВидовХарактеристик.СтатьиДоходов)
	|	И ВтУменьшениеСтоимости.СтоимостьЦФ <> 0
	|";
	ТекстыЗапросаОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область УменьшениеАмортизации
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)         КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                         КАК Регистратор,
	|	ДанныеДокумента.Период                         КАК Период,
	|	ДанныеДокумента.Организация                    КАК Организация,
	|	ДанныеДокумента.Подразделение                  КАК Подразделение,
	|	СведенияОбУчете.НаправлениеДеятельности        КАК НаправлениеДеятельности,
	|	&ХО_УменьшениеАмортизации                      КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.СтатьяДоходов                  КАК СтатьяДоходов,
	|	ДанныеДокумента.АналитикаДоходов               КАК АналитикаДоходов,
	|
	|	ВтУменьшениеАмортизации.Амортизация            КАК Сумма,
	|
	|	ВЫБОР 
	|		КОГДА НЕ ЕСТЬNULL(ПараметрыПартионногоУчета.УправленческийУчетОрганизаций, ЛОЖЬ)
	|			ТОГДА 0
	|		ИНАЧЕ ВтУменьшениеАмортизации.Амортизация
	|	КОНЕЦ КАК СуммаУпр,
	|
	|	ВЫБОР 
	|		КОГДА НЕ ЕСТЬNULL(ПараметрыПартионногоУчета.ИспользоватьУчетПрочихДоходовРасходовРегл, ЛОЖЬ)
	|			ТОГДА 0
	|		ИНАЧЕ ВтУменьшениеАмортизации.АмортизацияРегл + ВтУменьшениеАмортизации.АмортизацияЦФ
	|	КОНЕЦ КАК СуммаРегл,
	|
	|	ВтУменьшениеАмортизации.ИдентификаторФинЗаписи          КАК ИдентификаторФинЗаписи,
	|	ВтУменьшениеАмортизации.НастройкаХозяйственнойОперации  КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтУменьшениеАмортизации КАК ВтУменьшениеАмортизации
	|		ПО ВтУменьшениеАмортизации.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.Ссылка = ВтУменьшениеАмортизации.Ссылка
	|			И СведенияОбУчете.НематериальныйАктив = ВтУменьшениеАмортизации.НематериальныйАктив
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПараметрыПартионногоУчетаДляПроведения КАК ПараметрыПартионногоУчета
	|		ПО ПараметрыПартионногоУчета.Период = НАЧАЛОПЕРИОДА(ДанныеДокумента.Период, МЕСЯЦ)
	|			И ПараметрыПартионногоУчета.Организация = ДанныеДокумента.Организация
	|
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ДанныеДокумента.СтатьяДоходов) = ТИП(ПланВидовХарактеристик.СтатьиДоходов)
	|	И (ВтУменьшениеАмортизации.Амортизация <> 0
	|		ИЛИ ВтУменьшениеАмортизации.АмортизацияРегл <> 0 
	|		ИЛИ ВтУменьшениеАмортизации.АмортизацияЦФ <> 0)
	|";
	ТекстыЗапросаОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(ТекстыЗапросаОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;

КонецФункции

Процедура ТекстЗапросаТаблицаПервоначальныеСведенияНМА(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПервоначальныеСведенияНМА";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = КорректировкаСтоимостиИАмортизацииНМАЛокализация.ТекстЗапросаТаблицаПервоначальныеСведенияНМА(ТекстыЗапроса);
	
	Если ТекстЗапроса = Неопределено Тогда
		
		ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПервоначальныеСведенияНМА(ТекстыЗапроса);
		ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтУчетнаяПолитикаФинансовогоУчета(ТекстыЗапроса);
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка КАК Регистратор,
		|	ДанныеДокумента.Период КАК Период,
		|	ДанныеДокумента.Организация КАК Организация,
		|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
		|
		|	втПервоначальныеСведенияНМА.ДатаПриобретения КАК ДатаПриобретения,
		|	втПервоначальныеСведенияНМА.ДатаПринятияКУчетуБУ КАК ДатаПринятияКУчетуБУ,
		|	втПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ КАК ДатаПринятияКУчетуУУ,
		|	втПервоначальныеСведенияНМА.ДокументПринятияКУчетуБУ КАК ДокументПринятияКУчетуБУ,
		|	втПервоначальныеСведенияНМА.ДокументПринятияКУчетуУУ КАК ДокументПринятияКУчетуУУ,
		|	втПервоначальныеСведенияНМА.ДокументСписания КАК ДокументСписания,
		|
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете
		|			ТОГДА ТаблицаНМА.ПервоначальнаяСтоимостьБУ
		|		ИНАЧЕ втПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьБУ
		|	КОНЕЦ КАК ПервоначальнаяСтоимостьБУ,
		|
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете
		|			ТОГДА ТаблицаНМА.ПервоначальнаяСтоимостьУУ
		|		ИНАЧЕ втПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьУУ
		|	КОНЕЦ КАК ПервоначальнаяСтоимостьУУ
		|
		|ИЗ
		|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаНМА КАК ТаблицаНМА
		|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПервоначальныеСведенияНМА КАК втПервоначальныеСведенияНМА
		|		ПО втПервоначальныеСведенияНМА.Ссылка = ТаблицаНМА.Ссылка
		|			И втПервоначальныеСведенияНМА.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтУчетнаяПолитикаФинансовогоУчета КАК ВтУчетнаяПолитикаФинансовогоУчета
		|		ПО ВтУчетнаяПолитикаФинансовогоУчета.Ссылка = ДанныеДокумента.Ссылка
		|
		|ГДЕ
		|	ЕСТЬNULL(ВтУчетнаяПолитикаФинансовогоУчета.ПорядокУчетаВНАРегл, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАРегл.Независимый)
		|	И ДанныеДокумента.КорректировкаПервоначальнойСтоимости
		|	И (ДанныеДокумента.ОтражатьВРеглУчете
		|				И втПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьБУ <> ТаблицаНМА.ПервоначальнаяСтоимостьБУ
		|			ИЛИ ДанныеДокумента.ОтражатьВУпрУчете
		|				И втПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьУУ <> ТаблицаНМА.ПервоначальнаяСтоимостьУУ)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка КАК Регистратор,
		|	ДанныеДокумента.Период КАК Период,
		|	ДанныеДокумента.Организация КАК Организация,
		|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
		|
		|	втПервоначальныеСведенияНМА.ДатаПриобретения КАК ДатаПриобретения,
		|	втПервоначальныеСведенияНМА.ДатаПринятияКУчетуБУ КАК ДатаПринятияКУчетуБУ,
		|	втПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ КАК ДатаПринятияКУчетуУУ,
		|	втПервоначальныеСведенияНМА.ДокументПринятияКУчетуБУ КАК ДокументПринятияКУчетуБУ,
		|	втПервоначальныеСведенияНМА.ДокументПринятияКУчетуУУ КАК ДокументПринятияКУчетуУУ,
		|	втПервоначальныеСведенияНМА.ДокументСписания КАК ДокументСписания,
		|
		|	ТаблицаНМА.ПервоначальнаяСтоимостьБУ КАК ПервоначальнаяСтоимостьБУ,
		|	ТаблицаНМА.ПервоначальнаяСтоимостьУУ КАК ПервоначальнаяСтоимостьУУ
		|
		|ИЗ
		|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаНМА КАК ТаблицаНМА
		|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПервоначальныеСведенияНМА КАК втПервоначальныеСведенияНМА
		|		ПО втПервоначальныеСведенияНМА.Ссылка = ТаблицаНМА.Ссылка
		|			И втПервоначальныеСведенияНМА.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтУчетнаяПолитикаФинансовогоУчета КАК ВтУчетнаяПолитикаФинансовогоУчета
		|		ПО ВтУчетнаяПолитикаФинансовогоУчета.Ссылка = ДанныеДокумента.Ссылка
		|
		|ГДЕ
		|	ЕСТЬNULL(ВтУчетнаяПолитикаФинансовогоУчета.ПорядокУчетаВНАРегл, НЕОПРЕДЕЛЕНО) <> ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАРегл.Независимый)
		|	И ДанныеДокумента.КорректировкаПервоначальнойСтоимости
		|	И (втПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьБУ <> ТаблицаНМА.ПервоначальнаяСтоимостьБУ
		|			ИЛИ втПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьУУ <> ТаблицаНМА.ПервоначальнаяСтоимостьУУ)
		|";
		
	КонецЕсли; 
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры
 
Процедура ТекстЗапросаВтУвеличениеСтоимости(ТекстыЗапроса)

	ИмяТаблицы = "ВтУвеличениеСтоимости";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаВтКорректировкаСтоимости(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	КорректировкаСтоимости.Ссылка КАК Ссылка,
	|	КорректировкаСтоимости.НематериальныйАктив КАК НематериальныйАктив,
	|
	// Стоимость
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.Стоимость > 0
	|			ТОГДА КорректировкаСтоимости.Стоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК Стоимость,
	|
	// СтоимостьРегл
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.СтоимостьРегл > 0
	|			ТОГДА КорректировкаСтоимости.СтоимостьРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК СтоимостьРегл,
	|
	// СтоимостьНУ
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.СтоимостьНУ > 0
	|			ТОГДА КорректировкаСтоимости.СтоимостьНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК СтоимостьНУ,
	|
	// СтоимостьПР
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.СтоимостьРегл = 0
	|				И КорректировкаСтоимости.СтоимостьНУ = 0
	|			ТОГДА 0
	|		КОГДА КорректировкаСтоимости.СтоимостьРегл > 0
	|			ТОГДА КорректировкаСтоимости.СтоимостьПР
	|		КОГДА КорректировкаСтоимости.СтоимостьРегл = 0
	|				И КорректировкаСтоимости.СтоимостьНУ > 0
	|			ТОГДА КорректировкаСтоимости.СтоимостьПР
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК СтоимостьПР,
	|
	// СтоимостьЦФ
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.СтоимостьЦФ > 0
	|			ТОГДА КорректировкаСтоимости.СтоимостьЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК СтоимостьЦФ,
	|
	// СтоимостьПРЦФ
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.СтоимостьПРЦФ > 0
	|			ТОГДА КорректировкаСтоимости.СтоимостьПРЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК СтоимостьПРЦФ,
	|
	|	КорректировкаСтоимости.ИдентификаторСтроки  КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_УвеличениеСтоимости             КАК НастройкаХозяйственнойОперации
	|
	|ПОМЕСТИТЬ ВтУвеличениеСтоимости
	|
	|ИЗ
	|	ВтКорректировкаСтоимости КАК КорректировкаСтоимости
	|
	|ГДЕ
	|	КорректировкаСтоимости.Стоимость > 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьРегл > 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьНУ > 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьЦФ > 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьПРЦФ > 0
	|		ИЛИ ВЫБОР 
	|				КОГДА КорректировкаСтоимости.СтоимостьРегл = 0
	|						И КорректировкаСтоимости.СтоимостьНУ = 0
	|					ТОГДА 0
	|				КОГДА КорректировкаСтоимости.СтоимостьРегл > 0
	|					ТОГДА КорректировкаСтоимости.СтоимостьПР
	|				КОГДА КорректировкаСтоимости.СтоимостьРегл = 0
	|						И КорректировкаСтоимости.СтоимостьНУ > 0
	|					ТОГДА КорректировкаСтоимости.СтоимостьПР
	|				ИНАЧЕ 0
	|			КОНЕЦ <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	НематериальныйАктив
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ТекстЗапросаВтУменьшениеСтоимости(ТекстыЗапроса)

	ИмяТаблицы = "ВтУменьшениеСтоимости";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаВтКорректировкаСтоимости(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	КорректировкаСтоимости.Ссылка КАК Ссылка,
	|	КорректировкаСтоимости.НематериальныйАктив КАК НематериальныйАктив,
	|
	// Стоимость
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.Стоимость < 0
	|			ТОГДА -КорректировкаСтоимости.Стоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК Стоимость,
	|
	// СтоимостьРегл
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.СтоимостьРегл < 0
	|			ТОГДА -КорректировкаСтоимости.СтоимостьРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК СтоимостьРегл,
	|
	// СтоимостьНУ
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.СтоимостьНУ < 0
	|			ТОГДА -КорректировкаСтоимости.СтоимостьНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК СтоимостьНУ,
	|
	// СтоимостьПР
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.СтоимостьРегл < 0
	|			ТОГДА -КорректировкаСтоимости.СтоимостьПР
	|		КОГДА КорректировкаСтоимости.СтоимостьРегл = 0
	|				И КорректировкаСтоимости.СтоимостьНУ < 0
	|			ТОГДА -КорректировкаСтоимости.СтоимостьПР
	|		КОГДА КорректировкаСтоимости.СтоимостьРегл = 0
	|				И КорректировкаСтоимости.СтоимостьНУ = 0
	|			ТОГДА -КорректировкаСтоимости.СтоимостьПР
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК СтоимостьПР,
	|
	// СтоимостьЦФ
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.СтоимостьЦФ < 0
	|			ТОГДА -КорректировкаСтоимости.СтоимостьЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК СтоимостьЦФ,
	|
	// СтоимостьПРЦФ
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.СтоимостьПРЦФ < 0
	|			ТОГДА -КорректировкаСтоимости.СтоимостьПРЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК СтоимостьПРЦФ,
	|
	|	КорректировкаСтоимости.ИдентификаторСтроки  КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_УменьшениеСтоимости             КАК НастройкаХозяйственнойОперации
	|
	|ПОМЕСТИТЬ ВтУменьшениеСтоимости
	|
	|ИЗ
	|	ВтКорректировкаСтоимости КАК КорректировкаСтоимости
	|
	|ГДЕ
	|	КорректировкаСтоимости.Стоимость < 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьРегл < 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьНУ < 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьЦФ < 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьПРЦФ < 0
	|		ИЛИ ВЫБОР 
	|				КОГДА КорректировкаСтоимости.СтоимостьРегл < 0
	|					ТОГДА -КорректировкаСтоимости.СтоимостьПР
	|				КОГДА КорректировкаСтоимости.СтоимостьРегл = 0
	|						И КорректировкаСтоимости.СтоимостьНУ < 0
	|					ТОГДА -КорректировкаСтоимости.СтоимостьПР
	|				КОГДА КорректировкаСтоимости.СтоимостьРегл = 0
	|						И КорректировкаСтоимости.СтоимостьНУ = 0
	|					ТОГДА -КорректировкаСтоимости.СтоимостьПР
	|				ИНАЧЕ 0
	|			КОНЕЦ <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	НематериальныйАктив
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ТекстЗапросаВтУвеличениеАмортизации(ТекстыЗапроса)

	ИмяТаблицы = "втУвеличениеАмортизации";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаВтКорректировкаАмортизации(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	КорректировкаАмортизации.Ссылка КАК Ссылка,
	|	КорректировкаАмортизации.НематериальныйАктив КАК НематериальныйАктив,
	|
	// Амортизация
	|	ВЫБОР 
	|		КОГДА КорректировкаАмортизации.Амортизация > 0
	|			ТОГДА КорректировкаАмортизации.Амортизация
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК Амортизация,
	|
	// АмортизацияРегл
	|	ВЫБОР 
	|		КОГДА КорректировкаАмортизации.АмортизацияРегл > 0
	|			ТОГДА КорректировкаАмортизации.АмортизацияРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК АмортизацияРегл,
	|
	// АмортизацияНУ
	|	ВЫБОР 
	|		КОГДА КорректировкаАмортизации.АмортизацияНУ > 0
	|			ТОГДА КорректировкаАмортизации.АмортизацияНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК АмортизацияНУ,
	|
	// АмортизацияПР
	|	ВЫБОР 
	|		КОГДА КорректировкаАмортизации.АмортизацияРегл > 0
	|			ТОГДА КорректировкаАмортизации.АмортизацияПР
	|		КОГДА КорректировкаАмортизации.АмортизацияРегл = 0
	|				И КорректировкаАмортизации.АмортизацияНУ > 0
	|			ТОГДА КорректировкаАмортизации.АмортизацияПР
	|		КОГДА КорректировкаАмортизации.АмортизацияРегл = 0
	|				И КорректировкаАмортизации.АмортизацияНУ = 0
	|			ТОГДА КорректировкаАмортизации.АмортизацияПР
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК АмортизацияПР,
	|
	// АмортизацияЦФ
	|	ВЫБОР 
	|		КОГДА КорректировкаАмортизации.АмортизацияЦФ > 0
	|			ТОГДА КорректировкаАмортизации.АмортизацияЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК АмортизацияЦФ,
	|
	// АмортизацияПРЦФ
	|	ВЫБОР 
	|		КОГДА КорректировкаАмортизации.АмортизацияПРЦФ > 0
	|			ТОГДА КорректировкаАмортизации.АмортизацияПРЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК АмортизацияПРЦФ,
	|
	|	КорректировкаАмортизации.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_УвеличениеНакопленнойАмортизации КАК НастройкаХозяйственнойОперации
	|
	|ПОМЕСТИТЬ втУвеличениеАмортизации
	|
	|ИЗ
	|	ВтКорректировкаАмортизации КАК КорректировкаАмортизации
	|
	|ГДЕ
	|	КорректировкаАмортизации.Амортизация > 0
	|		ИЛИ КорректировкаАмортизации.АмортизацияРегл > 0
	|		ИЛИ КорректировкаАмортизации.АмортизацияНУ > 0
	|		ИЛИ КорректировкаАмортизации.АмортизацияЦФ > 0
	|		ИЛИ КорректировкаАмортизации.АмортизацияПРЦФ > 0
	|		ИЛИ ВЫБОР 
	|				КОГДА КорректировкаАмортизации.АмортизацияРегл > 0
	|					ТОГДА КорректировкаАмортизации.АмортизацияПР
	|				КОГДА КорректировкаАмортизации.АмортизацияРегл = 0
	|						И КорректировкаАмортизации.АмортизацияНУ > 0
	|					ТОГДА КорректировкаАмортизации.АмортизацияПР
	|				КОГДА КорректировкаАмортизации.АмортизацияРегл = 0
	|						И КорректировкаАмортизации.АмортизацияНУ = 0
	|					ТОГДА КорректировкаАмортизации.АмортизацияПР
	|				ИНАЧЕ 0
	|			КОНЕЦ <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	НематериальныйАктив
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ТекстЗапросаВтУменьшениеАмортизации(ТекстыЗапроса)

	ИмяТаблицы = "втУменьшениеАмортизации";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаВтКорректировкаАмортизации(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	КорректировкаАмортизации.Ссылка КАК Ссылка,
	|	КорректировкаАмортизации.НематериальныйАктив КАК НематериальныйАктив,
	|
	// Амортизация
	|	ВЫБОР 
	|		КОГДА КорректировкаАмортизации.Амортизация < 0
	|			ТОГДА -КорректировкаАмортизации.Амортизация
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК Амортизация,
	|
	// АмортизацияРегл
	|	ВЫБОР 
	|		КОГДА КорректировкаАмортизации.АмортизацияРегл < 0
	|			ТОГДА -КорректировкаАмортизации.АмортизацияРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК АмортизацияРегл,
	|
	// АмортизацияНУ
	|	ВЫБОР 
	|		КОГДА КорректировкаАмортизации.АмортизацияНУ < 0
	|			ТОГДА -КорректировкаАмортизации.АмортизацияНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК АмортизацияНУ,
	|
	// АмортизацияПР
	|	ВЫБОР 
	|		КОГДА КорректировкаАмортизации.АмортизацияРегл = 0
	|				И КорректировкаАмортизации.АмортизацияНУ = 0
	|			ТОГДА 0
	|		КОГДА КорректировкаАмортизации.АмортизацияРегл < 0
	|			ТОГДА -КорректировкаАмортизации.АмортизацияПР
	|		КОГДА КорректировкаАмортизации.АмортизацияРегл = 0
	|				И КорректировкаАмортизации.АмортизацияНУ < 0
	|			ТОГДА -КорректировкаАмортизации.АмортизацияПР
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК АмортизацияПР,
	|
	// АмортизацияЦФ
	|	ВЫБОР 
	|		КОГДА КорректировкаАмортизации.АмортизацияЦФ < 0
	|			ТОГДА -КорректировкаАмортизации.АмортизацияЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК АмортизацияЦФ,
	|
	// АмортизацияПРЦФ
	|	ВЫБОР 
	|		КОГДА КорректировкаАмортизации.АмортизацияПРЦФ < 0
	|			ТОГДА -КорректировкаАмортизации.АмортизацияПРЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК АмортизацияПРЦФ,
	|
	|	КорректировкаАмортизации.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_УменьшениеНакопленнойАмортизации КАК НастройкаХозяйственнойОперации
	|
	|ПОМЕСТИТЬ втУменьшениеАмортизации
	|
	|ИЗ
	|	ВтКорректировкаАмортизации КАК КорректировкаАмортизации
	|
	|ГДЕ
	|	КорректировкаАмортизации.Амортизация < 0
	|		ИЛИ КорректировкаАмортизации.АмортизацияРегл < 0
	|		ИЛИ КорректировкаАмортизации.АмортизацияНУ < 0
	|		ИЛИ КорректировкаАмортизации.АмортизацияЦФ < 0
	|		ИЛИ КорректировкаАмортизации.АмортизацияПРЦФ < 0
	|		ИЛИ ВЫБОР 
	|				КОГДА КорректировкаАмортизации.АмортизацияРегл = 0
	|						И КорректировкаАмортизации.АмортизацияНУ = 0
	|					ТОГДА 0
	|				КОГДА КорректировкаАмортизации.АмортизацияРегл < 0
	|					ТОГДА -КорректировкаАмортизации.АмортизацияПР
	|				КОГДА КорректировкаАмортизации.АмортизацияРегл = 0
	|						И КорректировкаАмортизации.АмортизацияНУ < 0
	|					ТОГДА -КорректировкаАмортизации.АмортизацияПР
	|				ИНАЧЕ 0
	|			КОНЕЦ <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	НематериальныйАктив
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ТекстЗапросаВтКорректировкаСтоимости(ТекстыЗапроса)

	ИмяТаблицы = "втКорректировкаСтоимости";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаНМА.Ссылка               КАК Ссылка,
	|	ТаблицаНМА.НематериальныйАктив  КАК НематериальныйАктив,
	|
	// Стоимость
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(СтоимостьНМА.Стоимость, 0) <> ТаблицаНМА.СтоимостьУУ 
	|				И ДанныеДокумента.ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаНМА.СтоимостьУУ - ЕСТЬNULL(СтоимостьНМА.Стоимость, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Стоимость,
	|
	// СтоимостьРегл
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(СтоимостьНМА.СтоимостьРегл, 0) <> ТаблицаНМА.СтоимостьБУ 
	|				И ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.СтоимостьБУ - ЕСТЬNULL(СтоимостьНМА.СтоимостьРегл, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьРегл,
	|
	// СтоимостьНУ
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(СтоимостьНМА.СтоимостьНУ, 0) <> ТаблицаНМА.СтоимостьНУ 
	|				И ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.СтоимостьНУ - ЕСТЬNULL(СтоимостьНМА.СтоимостьНУ, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьНУ,
	|
	// СтоимостьПР
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(СтоимостьНМА.СтоимостьПР, 0) <> ТаблицаНМА.СтоимостьПР 
	|				И ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.СтоимостьПР - ЕСТЬNULL(СтоимостьНМА.СтоимостьПР, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьПР,
	|
	// СтоимостьЦФ
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(СтоимостьНМА.СтоимостьЦФ, 0) <> ТаблицаНМА.СтоимостьЦФ 
	|				И ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.СтоимостьЦФ - ЕСТЬNULL(СтоимостьНМА.СтоимостьЦФ, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьЦФ,
	|
	// СтоимостьПРЦФ
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(СтоимостьНМА.СтоимостьПРЦФ, 0) <> ТаблицаНМА.СтоимостьЦФ 
	|				И ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.СтоимостьЦФ - ЕСТЬNULL(СтоимостьНМА.СтоимостьПРЦФ, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьПРЦФ,
	|
	|	ТаблицаНМА.ИдентификаторСтроки              КАК ИдентификаторСтроки
	|
	|ПОМЕСТИТЬ втКорректировкаСтоимости
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаНМА КАК ТаблицаНМА
	|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьНМА
	|		ПО СтоимостьНМА.Ссылка = ТаблицаНМА.Ссылка
	|			И СтоимостьНМА.ОбъектУчета = ТаблицаНМА.НематериальныйАктив
	|
	|ГДЕ
	|	ДанныеДокумента.КорректировкаСтоимости
	|	И (ЕСТЬNULL(СтоимостьНМА.Стоимость, 0) <> ТаблицаНМА.СтоимостьУУ 
	|				И ДанныеДокумента.ОтражатьВУпрУчете
	|
	|		ИЛИ ЕСТЬNULL(СтоимостьНМА.СтоимостьРегл, 0) <> ТаблицаНМА.СтоимостьБУ 
	|				И ДанныеДокумента.ОтражатьВРеглУчете
	|
	|		ИЛИ ЕСТЬNULL(СтоимостьНМА.СтоимостьНУ, 0) <> ТаблицаНМА.СтоимостьНУ 
	|				И ДанныеДокумента.ОтражатьВРеглУчете
	|
	|		ИЛИ ЕСТЬNULL(СтоимостьНМА.СтоимостьПР, 0) <> ТаблицаНМА.СтоимостьПР 
	|				И ДанныеДокумента.ОтражатьВРеглУчете
	|
	|		ИЛИ ЕСТЬNULL(СтоимостьНМА.СтоимостьЦФ, 0) <> ТаблицаНМА.СтоимостьЦФ 
	|				И ДанныеДокумента.ОтражатьВРеглУчете)
	|
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ТекстЗапросаВтКорректировкаАмортизации(ТекстыЗапроса)

	ИмяТаблицы = "втКорректировкаАмортизации";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаНМА.Ссылка               КАК Ссылка,
	|	ТаблицаНМА.НематериальныйАктив  КАК НематериальныйАктив,
	|
	// Амортизация
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(АмортизацияНМА.Амортизация, 0) <> ТаблицаНМА.АмортизацияУУ 
	|				И ДанныеДокумента.ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаНМА.АмортизацияУУ - ЕСТЬNULL(АмортизацияНМА.Амортизация, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Амортизация,
	|
	// АмортизацияРегл
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(АмортизацияНМА.АмортизацияРегл, 0) <> ТаблицаНМА.АмортизацияБУ 
	|				И ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.АмортизацияБУ - ЕСТЬNULL(АмортизацияНМА.АмортизацияРегл, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК АмортизацияРегл,
	|
	// АмортизацияНУ
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(АмортизацияНМА.АмортизацияНУ, 0) <> ТаблицаНМА.АмортизацияНУ 
	|				И ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.АмортизацияНУ - ЕСТЬNULL(АмортизацияНМА.АмортизацияНУ, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК АмортизацияНУ,
	|
	// АмортизацияПР
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(АмортизацияНМА.АмортизацияПР, 0) <> ТаблицаНМА.АмортизацияПР 
	|				И ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.АмортизацияПР - ЕСТЬNULL(АмортизацияНМА.АмортизацияПР, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК АмортизацияПР,
	|
	// АмортизацияЦФ
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(АмортизацияНМА.АмортизацияЦФ, 0) <> ТаблицаНМА.АмортизацияЦФ 
	|				И ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.АмортизацияЦФ - ЕСТЬNULL(АмортизацияНМА.АмортизацияЦФ, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК АмортизацияЦФ,
	|
	// АмортизацияПРЦФ
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(АмортизацияНМА.АмортизацияПРЦФ, 0) <> ТаблицаНМА.АмортизацияЦФ 
	|				И ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.АмортизацияЦФ - ЕСТЬNULL(АмортизацияНМА.АмортизацияПРЦФ, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК АмортизацияПРЦФ,
	|
	|	ТаблицаНМА.ИдентификаторСтроки КАК ИдентификаторСтроки
	|
	|ПОМЕСТИТЬ втКорректировкаАмортизации
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаНМА КАК ТаблицаНМА
	|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияНМА
	|		ПО АмортизацияНМА.Ссылка = ТаблицаНМА.Ссылка
	|			И АмортизацияНМА.ОбъектУчета = ТаблицаНМА.НематериальныйАктив
	|
	|ГДЕ
	|	ДанныеДокумента.КорректировкаАмортизации
	|	И (ЕСТЬNULL(АмортизацияНМА.Амортизация, 0) <> ТаблицаНМА.АмортизацияУУ 
	|				И ДанныеДокумента.ОтражатьВУпрУчете
	|
	|
	|		ИЛИ ЕСТЬNULL(АмортизацияНМА.АмортизацияРегл, 0) <> ТаблицаНМА.АмортизацияБУ 
	|				И ДанныеДокумента.ОтражатьВРеглУчете 
	|
	|		ИЛИ ЕСТЬNULL(АмортизацияНМА.АмортизацияНУ, 0) <> ТаблицаНМА.АмортизацияНУ 
	|				И ДанныеДокумента.ОтражатьВРеглУчете 
	|
	|		ИЛИ ЕСТЬNULL(АмортизацияНМА.АмортизацияПР, 0) <> ТаблицаНМА.АмортизацияПР 
	|				И ДанныеДокумента.ОтражатьВРеглУчете 
	|
	|		ИЛИ ЕСТЬNULL(АмортизацияНМА.АмортизацияЦФ, 0) <> ТаблицаНМА.АмортизацияЦФ 
	|				И ДанныеДокумента.ОтражатьВРеглУчете)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(ТекстыЗапроса, ДопПоля)
	
	ИмяРегистра = "ВтИсходныеПрочиеРасходы";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат "";
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыУчетаНМА(ТекстыЗапроса,, "втСведенияОбУчете");
	
	ТекстЗапросаВтУменьшениеСтоимости(ТекстыЗапроса);
	ТекстЗапросаВтУвеличениеАмортизации(ТекстыЗапроса);
	
	СписокЗапросовОбъединение = Новый Массив;
	СписокЗапросовОбъединение.Добавить(РегистрыНакопления.ПрочиеРасходы.ТекстОписаниеВтИсходныеПрочиеРасходы(ДопПоля));
	
	#Область УменьшениеСтоимости
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Период                          КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)          КАК ВидДвижения,
	|	ДанныеДокумента.Организация                     КАК Организация,
	|	ДанныеДокумента.Подразделение                   КАК Подразделение,
	|	ДанныеДокумента.СтатьяРасходов                  КАК СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходов               КАК АналитикаРасходов,
	|	СведенияОбУчете.НаправлениеДеятельности         КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                    КАК ВидДеятельностиНДС,
	|	
	|	ВтУменьшениеСтоимости.Стоимость СуммаСНДС,
	|	ВтУменьшениеСтоимости.Стоимость КАК СуммаБезНДС,
	|	ВтУменьшениеСтоимости.Стоимость КАК СуммаБезНДСУпр,
	|
	|	ВтУменьшениеСтоимости.СтоимостьРегл
	|		+ ВтУменьшениеСтоимости.СтоимостьЦФ КАК СуммаСНДСРегл,
	|
	|	ВтУменьшениеСтоимости.СтоимостьРегл
	|		+ ВтУменьшениеСтоимости.СтоимостьЦФ КАК СуммаБезНДСРегл,
	|
	|	ВтУменьшениеСтоимости.СтоимостьПР
	|		+ ВтУменьшениеСтоимости.СтоимостьПРЦФ КАК ПостояннаяРазница,
	|
	|	ВтУменьшениеСтоимости.СтоимостьРегл
	|		+ ВтУменьшениеСтоимости.СтоимостьЦФ
	|		- ВтУменьшениеСтоимости.СтоимостьНУ
	|		- ВтУменьшениеСтоимости.СтоимостьПР
	|		- ВтУменьшениеСтоимости.СтоимостьПРЦФ КАК ВременнаяРазница,
	|
	|	&ХО_УменьшениеСтоимости КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|
	|	ВтУменьшениеСтоимости.ИдентификаторФинЗаписи          КАК ИдентификаторФинЗаписи,
	|	ВтУменьшениеСтоимости.НастройкаХозяйственнойОперации  КАК НастройкаХозяйственнойОперации,
	|	ДанныеДокумента.Ссылка КАК Регистратор
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтУменьшениеСтоимости КАК ВтУменьшениеСтоимости
	|		ПО ВтУменьшениеСтоимости.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.Ссылка = ВтУменьшениеСтоимости.Ссылка
	|			И СведенияОбУчете.НематериальныйАктив = ВтУменьшениеСтоимости.НематериальныйАктив
	|
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ДанныеДокумента.СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиРасходов)
	|	И (ВтУменьшениеСтоимости.Стоимость <> 0
	|		ИЛИ ВтУменьшениеСтоимости.СтоимостьРегл <> 0
	|		ИЛИ ВтУменьшениеСтоимости.СтоимостьНУ <> 0
	|		ИЛИ ВтУменьшениеСтоимости.СтоимостьПР <> 0)
	|";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область УвеличениеСтоимостиЦФ
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Период                          КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)          КАК ВидДвижения,
	|	ДанныеДокумента.Организация                     КАК Организация,
	|	ДанныеДокумента.Подразделение                   КАК Подразделение,
	|	ДанныеДокумента.СтатьяРасходов                  КАК СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходов               КАК АналитикаРасходов,
	|	СведенияОбУчете.НаправлениеДеятельности         КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                    КАК ВидДеятельностиНДС,
	|	
	|	0 СуммаСНДС,
	|	0 КАК СуммаБезНДС,
	|	0 КАК СуммаБезНДСУпр,
	|
	|	ВтУвеличениеСтоимости.СтоимостьЦФ КАК СуммаСНДСРегл,
	|
	|	ВтУвеличениеСтоимости.СтоимостьЦФ КАК СуммаБезНДСРегл,
	|
	|	ВтУвеличениеСтоимости.СтоимостьПРЦФ КАК ПостояннаяРазница,
	|
	|	ВтУвеличениеСтоимости.СтоимостьЦФ
	|		- ВтУвеличениеСтоимости.СтоимостьПРЦФ КАК ВременнаяРазница,
	|
	|	&ХО_ИзменениеДоходовЦФ КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|
	|	&ИдентификаторНеиспользуемойФинЗаписи КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПустаяСсылка) КАК НастройкаХозяйственнойОперации,
	|	ДанныеДокумента.Ссылка КАК Регистратор
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтУвеличениеСтоимости КАК ВтУвеличениеСтоимости
	|		ПО ВтУвеличениеСтоимости.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.Ссылка = ВтУвеличениеСтоимости.Ссылка
	|			И СведенияОбУчете.НематериальныйАктив = ВтУвеличениеСтоимости.НематериальныйАктив
	|
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ДанныеДокумента.СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиРасходов)
	|	И (ВтУвеличениеСтоимости.СтоимостьЦФ <> 0
	|		ИЛИ ВтУвеличениеСтоимости.СтоимостьПРЦФ <> 0)
	|";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область УвеличениеАмортизации
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Период                          КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)          КАК ВидДвижения,
	|	ДанныеДокумента.Организация                     КАК Организация,
	|	ДанныеДокумента.Подразделение                   КАК Подразделение,
	|	ДанныеДокумента.СтатьяРасходов                  КАК СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходов               КАК АналитикаРасходов,
	|	СведенияОбУчете.НаправлениеДеятельности         КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                    КАК ВидДеятельностиНДС,
	|	
	|	ВтУвеличениеАмортизации.Амортизация             КАК СуммаСНДС,
	|	ВтУвеличениеАмортизации.Амортизация             КАК СуммаБезНДС,
	|	ВтУвеличениеАмортизации.Амортизация             КАК СуммаБезНДСУпр,
	|
	|	ВтУвеличениеАмортизации.АмортизацияРегл         КАК СуммаСНДСРегл,
	|
	|	ВтУвеличениеАмортизации.АмортизацияРегл         КАК СуммаБезНДСРегл,
	|
	|	ВтУвеличениеАмортизации.АмортизацияПР           КАК ПостояннаяРазница,
	|
	|	ВтУвеличениеАмортизации.АмортизацияРегл
	|		- ВтУвеличениеАмортизации.АмортизацияНУ
	|		- ВтУвеличениеАмортизации.АмортизацияПР     КАК ВременнаяРазница,
	|
	|	&ХО_УвеличениеАмортизации КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|
	|	ВтУвеличениеАмортизации.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_УвеличениеНакопленнойАмортизации  КАК НастройкаХозяйственнойОперации,
	|	ДанныеДокумента.Ссылка КАК Регистратор
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтУвеличениеАмортизации КАК ВтУвеличениеАмортизации
	|		ПО ВтУвеличениеАмортизации.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.Ссылка = ВтУвеличениеАмортизации.Ссылка
	|			И СведенияОбУчете.НематериальныйАктив = ВтУвеличениеАмортизации.НематериальныйАктив
	|
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ДанныеДокумента.СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиРасходов)
	|	И (ВтУвеличениеАмортизации.Амортизация <> 0
	|		ИЛИ ВтУвеличениеАмортизации.АмортизацияРегл <> 0
	|		ИЛИ ВтУвеличениеАмортизации.АмортизацияНУ <> 0
	|		ИЛИ ВтУвеличениеАмортизации.АмортизацияПР <> 0)
	|";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область УвеличениеАмортизацииЦФ
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Период                          КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)          КАК ВидДвижения,
	|	ДанныеДокумента.Организация                     КАК Организация,
	|	ДанныеДокумента.Подразделение                   КАК Подразделение,
	|	ДанныеДокумента.СтатьяРасходов                  КАК СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходов               КАК АналитикаРасходов,
	|	СведенияОбУчете.НаправлениеДеятельности         КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                    КАК ВидДеятельностиНДС,
	|	
	|	0             КАК СуммаСНДС,
	|	0             КАК СуммаБезНДС,
	|	0             КАК СуммаБезНДСУпр,
	|
	|	ВтУвеличениеАмортизации.АмортизацияЦФ     КАК СуммаСНДСРегл,
	|	ВтУвеличениеАмортизации.АмортизацияЦФ     КАК СуммаБезНДСРегл,
	|
	|	ВтУвеличениеАмортизации.АмортизацияПРЦФ   КАК ПостояннаяРазница,
	|
	|	ВтУвеличениеАмортизации.АмортизацияЦФ
	|		- ВтУвеличениеАмортизации.АмортизацияПРЦФ   КАК ВременнаяРазница,
	|
	|	&ХО_УвеличениеАмортизацииЦФ КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|
	|	ВтУвеличениеАмортизации.ИдентификаторФинЗаписи   КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_УвеличениеНакопленнойАмортизацииЦФ  КАК НастройкаХозяйственнойОперации,
	|	ДанныеДокумента.Ссылка КАК Регистратор
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтУвеличениеАмортизации КАК ВтУвеличениеАмортизации
	|		ПО ВтУвеличениеАмортизации.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.Ссылка = ВтУвеличениеАмортизации.Ссылка
	|			И СведенияОбУчете.НематериальныйАктив = ВтУвеличениеАмортизации.НематериальныйАктив
	|
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ДанныеДокумента.СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиРасходов)
	|	И (ВтУвеличениеАмортизации.АмортизацияЦФ <> 0
	|		ИЛИ ВтУвеличениеАмортизации.АмортизацияПРЦФ <> 0)
	|";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	ТекстЗапроса = СтрСоединить(СписокЗапросовОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(ТекстыЗапроса, ДопПоля)
	
	ИмяРегистра = "ВтИсходныеПартииПрочихРасходов";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат "";
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыУчетаНМА(ТекстыЗапроса,, "втСведенияОбУчете");
	
	ТекстЗапросаВтУменьшениеСтоимости(ТекстыЗапроса);
	ТекстЗапросаВтУвеличениеАмортизации(ТекстыЗапроса);
	
	СписокЗапросовОбъединение = Новый Массив;
	СписокЗапросовОбъединение.Добавить(РегистрыНакопления.ПартииПрочихРасходов.ТекстОписаниеВтИсходныеПартииПрочихРасходов(ДопПоля));
	
	#Область УменьшениеСтоимости
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ДанныеДокумента.Ссылка КАК ДокументПоступленияРасходов,
	|	ДанныеДокумента.СтатьяРасходов КАК СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходов КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
	|	СведенияОбУчете.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
	|
	|	ВтУменьшениеСтоимости.Стоимость КАК Стоимость,
	|	ВтУменьшениеСтоимости.Стоимость КАК СтоимостьБезНДС,
	|	0 КАК НДСУпр,
	|
	|	ВтУменьшениеСтоимости.СтоимостьРегл КАК СтоимостьРегл,
	|
	|	0 КАК НДСРегл,
	|
	|	ВтУменьшениеСтоимости.СтоимостьПР КАК ПостояннаяРазница,
	|
	|	ВтУменьшениеСтоимости.СтоимостьРегл
	|		- ВтУменьшениеСтоимости.СтоимостьНУ
	|		- ВтУменьшениеСтоимости.СтоимостьПР КАК ВременнаяРазница,
	|
	|	&ХО_УменьшениеСтоимости КАК ХозяйственнаяОперация,
	|
	|	ВтУменьшениеСтоимости.ИдентификаторФинЗаписи          КАК ИдентификаторФинЗаписи,
	|	ВтУменьшениеСтоимости.НастройкаХозяйственнойОперации  КАК НастройкаХозяйственнойОперации,
	|	ДанныеДокумента.Ссылка КАК Регистратор
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтУменьшениеСтоимости КАК ВтУменьшениеСтоимости
	|		ПО ВтУменьшениеСтоимости.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.Ссылка = ВтУменьшениеСтоимости.Ссылка
	|			И СведенияОбУчете.НематериальныйАктив = ВтУменьшениеСтоимости.НематериальныйАктив
	|
	|ГДЕ
	|	(ВтУменьшениеСтоимости.Стоимость <> 0
	|		ИЛИ ВтУменьшениеСтоимости.СтоимостьРегл <> 0
	|		ИЛИ ВтУменьшениеСтоимости.СтоимостьНУ <> 0
	|		ИЛИ ВтУменьшениеСтоимости.СтоимостьПР <> 0)
	|";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область УвеличениеСтоимостиЦФ
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ДанныеДокумента.Ссылка КАК ДокументПоступленияРасходов,
	|	ДанныеДокумента.СтатьяРасходов КАК СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходов КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
	|	СведенияОбУчете.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
	|
	|	0 КАК Стоимость,
	|	0 КАК СтоимостьБезНДС,
	|	0 КАК НДСУпр,
	|
	|	ВтУвеличениеСтоимости.СтоимостьЦФ КАК СтоимостьРегл,
	|
	|	0 КАК НДСРегл,
	|
	|	ВтУвеличениеСтоимости.СтоимостьПРЦФ КАК ПостояннаяРазница,
	|
	|	ВтУвеличениеСтоимости.СтоимостьЦФ
	|		- ВтУвеличениеСтоимости.СтоимостьПРЦФ КАК ВременнаяРазница,
	|
	|	&ХО_УвеличениеСтоимости КАК ХозяйственнаяОперация,
	|
	|	ВтУвеличениеСтоимости.ИдентификаторФинЗаписи          КАК ИдентификаторФинЗаписи,
	|	ВтУвеличениеСтоимости.НастройкаХозяйственнойОперации  КАК НастройкаХозяйственнойОперации,
	|	ДанныеДокумента.Ссылка КАК Регистратор
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтУвеличениеСтоимости КАК ВтУвеличениеСтоимости
	|		ПО ВтУвеличениеСтоимости.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.Ссылка = ВтУвеличениеСтоимости.Ссылка
	|			И СведенияОбУчете.НематериальныйАктив = ВтУвеличениеСтоимости.НематериальныйАктив
	|
	|ГДЕ
	|	(ВтУвеличениеСтоимости.СтоимостьЦФ <> 0
	|		ИЛИ ВтУвеличениеСтоимости.СтоимостьПРЦФ <> 0)
	|";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область УвеличениеАмортизации
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ДанныеДокумента.Ссылка КАК ДокументПоступленияРасходов,
	|	ДанныеДокумента.СтатьяРасходов КАК СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходов КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
	|	СведенияОбУчете.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
	|
	|	ВтУвеличениеАмортизации.Амортизация КАК Стоимость,
	|	ВтУвеличениеАмортизации.Амортизация КАК СтоимостьБезНДС,
	|	0 КАК НДСУпр,
	|
	|	ВтУвеличениеАмортизации.АмортизацияРегл
	|		+ ВтУвеличениеАмортизации.АмортизацияЦФ КАК СтоимостьРегл,
	|
	|	0 КАК НДСРегл,
	|
	|	ВтУвеличениеАмортизации.АмортизацияПР
	|		+ ВтУвеличениеАмортизации.АмортизацияПРЦФ КАК ПостояннаяРазница,
	|
	|	ВтУвеличениеАмортизации.АмортизацияРегл
	|		+ ВтУвеличениеАмортизации.АмортизацияЦФ
	|		- ВтУвеличениеАмортизации.АмортизацияНУ
	|		- ВтУвеличениеАмортизации.АмортизацияПР
	|		- ВтУвеличениеАмортизации.АмортизацияПРЦФ КАК ВременнаяРазница,
	|
	|	&ХО_УвеличениеАмортизации КАК ХозяйственнаяОперация,
	|
	|	ВтУвеличениеАмортизации.ИдентификаторФинЗаписи          КАК ИдентификаторФинЗаписи,
	|	ВтУвеличениеАмортизации.НастройкаХозяйственнойОперации  КАК НастройкаХозяйственнойОперации,
	|	ДанныеДокумента.Ссылка КАК Регистратор
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтУвеличениеАмортизации КАК ВтУвеличениеАмортизации
	|		ПО ВтУвеличениеАмортизации.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.Ссылка = ВтУвеличениеАмортизации.Ссылка
	|			И СведенияОбУчете.НематериальныйАктив = ВтУвеличениеАмортизации.НематериальныйАктив
	|
	|ГДЕ
	|	(ВтУвеличениеАмортизации.Амортизация <> 0
	|		ИЛИ ВтУвеличениеАмортизации.АмортизацияРегл <> 0
	|		ИЛИ ВтУвеличениеАмортизации.АмортизацияНУ <> 0
	|		ИЛИ ВтУвеличениеАмортизации.АмортизацияПР <> 0
	|		ИЛИ ВтУвеличениеАмортизации.АмортизацияЦФ <> 0
	|		ИЛИ ВтУвеличениеАмортизации.АмортизацияПРЦФ <> 0)
	|";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(СписокЗапросовОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтПрочиеРасходы(ТекстыЗапроса, ДопПоля)
	
	ИмяРегистра = "ВтПрочиеРасходы";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтИсходныеПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(ТекстыЗапроса, ДопПоля);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаВтПрочиеРасходы(ДопПоля, Истина);
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтПартииПрочихРасходов(ТекстыЗапроса, ДопПоля)
	
	ИмяРегистра = "ВтПартииПрочихРасходов";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтИсходныеПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(ТекстыЗапроса, ДопПоля);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаВтПартииПрочихРасходов(ДопПоля, Истина);
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область ПроведениеПоРеглУчету

Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт

	Возврат "";

КонецФункции

Функция ТекстОтраженияВРеглУчете() Экспорт

	Возврат КорректировкаСтоимостиИАмортизацииНМАЛокализация.ТекстОтраженияВРеглУчете();

КонецФункции

#КонецОбласти

#Область Прочее

Функция ПараметрыВыбораСтатейИАналитик() Экспорт
	
	ПараметрыВыбораСтатьиИАналитики = Новый Массив;
	
	// СтатьяРасходов
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья = "СтатьяРасходов";
	ПараметрыВыбора.ТипСтатьи = "ТипСтатьи";
	ПараметрыВыбора.ДоступностьПоОперации = Неопределено;
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходов";
	
	ПараметрыВыбора.ВыборСтатьиАктивовПассивов = Истина;
	ПараметрыВыбора.АналитикаАктивовПассивов = "АналитикаАктивовПассивов";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаАктивовПассивов.Добавить("АналитикаАктивовПассивов");
	
	ПараметрыВыбораСтатьиИАналитики.Добавить(ПараметрыВыбора);
	
	// СтатьяДоходов
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья = "СтатьяДоходов";
	ПараметрыВыбора.ТипСтатьи = "ТипСтатьиДоходов";
	ПараметрыВыбора.ДоступностьПоОперации = Неопределено;
	
	ПараметрыВыбора.ВыборСтатьиДоходов = Истина;
	ПараметрыВыбора.АналитикаДоходов = "АналитикаДоходов";
	
	ПараметрыВыбора.ВыборСтатьиАктивовПассивов = Истина;
	ПараметрыВыбора.АналитикаАктивовПассивов = "АналитикаАктивовПассивовДоходов";

	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяДоходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаДоходов.Добавить("АналитикаДоходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаАктивовПассивов.Добавить("АналитикаАктивовПассивовДоходов");
	
	ПараметрыВыбораСтатьиИАналитики.Добавить(ПараметрыВыбора);
	
	Возврат ПараметрыВыбораСтатьиИАналитики;
	
КонецФункции

Функция ПараметрыНастройкиСчетовУчета() Экспорт
	
	МассивПараметров = Новый Массив();

	#Область Расходы
	ПараметрыНастройки = НастройкаСчетовУчетаСервер.ПараметрыНастройки();
	ПараметрыНастройки.ПутьКДанным = "Объект";
	ПараметрыНастройки.НастройкаСчетовУчета = "НастройкаСчетовУчета";
	ПараметрыНастройки.СтатьяАктивовПассивов = "СтатьяРасходов";
	ПараметрыНастройки.АналитикаАктивовПассивов = "АналитикаАктивовПассивов";
	ПараметрыНастройки.Представление = "ПредставлениеОтраженияВРеглУчете";
	ПараметрыНастройки.ТипСтатьи = "ТипСтатьи";
	
	ПараметрыНастройки.Организация = "Объект.Организация";

	ПараметрыНастройки.ЭлементыФормы.Добавить("ПредставлениеОтраженияВРеглУчете");
	МассивПараметров.Добавить(ПараметрыНастройки);
	#КонецОбласти
		
	#Область Доходы
	ПараметрыНастройки = НастройкаСчетовУчетаСервер.ПараметрыНастройки();
	ПараметрыНастройки.ПутьКДанным = "Объект";
	ПараметрыНастройки.НастройкаСчетовУчета = "НастройкаСчетовУчетаДоходов";
	ПараметрыНастройки.СтатьяАктивовПассивов = "СтатьяДоходов";
	ПараметрыНастройки.АналитикаАктивовПассивов = "АналитикаАктивовПассивовДоходов";
	ПараметрыНастройки.Представление = "ПредставлениеОтраженияДоходовВРеглУчете";
	ПараметрыНастройки.ТипСтатьи = "ТипСтатьиДоходов";

	ПараметрыНастройки.Организация = "Объект.Организация";

	ПараметрыНастройки.ЭлементыФормы.Добавить("ПредставлениеОтраженияДоходовВРеглУчете");
	МассивПараметров.Добавить(ПараметрыНастройки);
	#КонецОбласти
	
	Возврат МассивПараметров;
	
КонецФункции

Процедура ЗаполнитьПоДаннымУчета(Объект, ВыбранныеСтроки = Неопределено) Экспорт

	Если ВыбранныеСтроки <> Неопределено
		И ВыбранныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СтоимостьИАмортизация.НематериальныйАктив КАК НематериальныйАктив,
	|	СУММА(СтоимостьИАмортизация.СтоимостьУУ) КАК СтоимостьУУ,
	|	СУММА(СтоимостьИАмортизация.СтоимостьБУ) КАК СтоимостьБУ,
	|	СУММА(СтоимостьИАмортизация.СтоимостьНУ) КАК СтоимостьНУ,
	|	СУММА(СтоимостьИАмортизация.СтоимостьПР) КАК СтоимостьПР,
	|	СУММА(СтоимостьИАмортизация.СтоимостьЦФ) КАК СтоимостьЦФ,
	|	СУММА(СтоимостьИАмортизация.АмортизацияБУ) КАК АмортизацияБУ,
	|	СУММА(СтоимостьИАмортизация.АмортизацияНУ) КАК АмортизацияНУ,
	|	СУММА(СтоимостьИАмортизация.АмортизацияПР) КАК АмортизацияПР,
	|	СУММА(СтоимостьИАмортизация.АмортизацияУУ) КАК АмортизацияУУ,
	|	СУММА(СтоимостьИАмортизация.АмортизацияЦФ) КАК АмортизацияЦФ,
	|	МАКСИМУМ(ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьБУ) КАК ПервоначальнаяСтоимостьБУ,
	|	МАКСИМУМ(ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьНУ) КАК ПервоначальнаяСтоимостьНУ,
	|	МАКСИМУМ(ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьУУ) КАК ПервоначальнаяСтоимостьУУ
	|ИЗ
	|	(ВЫБРАТЬ
	|		СтоимостьНМАОстатки.НематериальныйАктив КАК НематериальныйАктив,
	|		СтоимостьНМАОстатки.СтоимостьОстаток КАК СтоимостьУУ,
	|		СтоимостьНМАОстатки.СтоимостьРеглОстаток КАК СтоимостьБУ,
	|		СтоимостьНМАОстатки.СтоимостьНУОстаток КАК СтоимостьНУ,
	|		СтоимостьНМАОстатки.СтоимостьПРОстаток КАК СтоимостьПР,
	|		СтоимостьНМАОстатки.СтоимостьЦФОстаток КАК СтоимостьЦФ,
	|		0 КАК АмортизацияБУ,
	|		0 КАК АмортизацияНУ,
	|		0 КАК АмортизацияПР,
	|		0 КАК АмортизацияУУ,
	|		0 КАК АмортизацияЦФ
	|	ИЗ
	|		РегистрНакопления.СтоимостьНМА.Остатки(
	|				&ПериодГраница,
	|				Организация = &Организация
	|					И НематериальныйАктив В (&СписокНМА)) КАК СтоимостьНМАОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СтоимостьНМА.НематериальныйАктив,
	|		СтоимостьНМА.Стоимость,
	|		ВЫБОР
	|			КОГДА СтоимостьНМА.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -СтоимостьНМА.СтоимостьРегл
	|			ИНАЧЕ СтоимостьНМА.СтоимостьРегл
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА СтоимостьНМА.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -СтоимостьНМА.СтоимостьНУ
	|			ИНАЧЕ СтоимостьНМА.СтоимостьНУ
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА СтоимостьНМА.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -СтоимостьНМА.СтоимостьПР
	|			ИНАЧЕ СтоимостьНМА.СтоимостьПР
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА СтоимостьНМА.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -СтоимостьНМА.СтоимостьЦФ
	|			ИНАЧЕ СтоимостьНМА.СтоимостьЦФ
	|		КОНЕЦ,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0
	|	ИЗ
	|		РегистрНакопления.СтоимостьНМА КАК СтоимостьНМА
	|	ГДЕ
	|		СтоимостьНМА.Регистратор = &Ссылка
	|		И СтоимостьНМА.Организация = &Организация
	|		И СтоимостьНМА.Период < &Период
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		АмортизацияНМАОстатки.НематериальныйАктив,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		-АмортизацияНМАОстатки.АмортизацияРеглОстаток,
	|		-АмортизацияНМАОстатки.АмортизацияНУОстаток,
	|		-АмортизацияНМАОстатки.АмортизацияПРОстаток,
	|		-АмортизацияНМАОстатки.АмортизацияОстаток,
	|		-АмортизацияНМАОстатки.АмортизацияЦФОстаток
	|	ИЗ
	|		РегистрНакопления.АмортизацияНМА.Остатки(
	|				&ПериодГраница,
	|				Организация = &Организация
	|					И НематериальныйАктив В (&СписокНМА)) КАК АмортизацияНМАОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		АмортизацияНМА.НематериальныйАктив,
	|		АмортизацияНМА.Амортизация,
	|		0,
	|		0,
	|		0,
	|		0,
	|		ВЫБОР
	|			КОГДА АмортизацияНМА.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА -АмортизацияНМА.АмортизацияРегл
	|			ИНАЧЕ АмортизацияНМА.АмортизацияРегл
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА АмортизацияНМА.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА -АмортизацияНМА.АмортизацияНУ
	|			ИНАЧЕ АмортизацияНМА.АмортизацияНУ
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА АмортизацияНМА.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА -АмортизацияНМА.АмортизацияПР
	|			ИНАЧЕ АмортизацияНМА.АмортизацияПР
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА АмортизацияНМА.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА -АмортизацияНМА.Амортизация
	|			ИНАЧЕ АмортизацияНМА.Амортизация
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА АмортизацияНМА.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА -АмортизацияНМА.АмортизацияЦФ
	|			ИНАЧЕ АмортизацияНМА.АмортизацияЦФ
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.АмортизацияНМА КАК АмортизацияНМА
	|	ГДЕ
	|		АмортизацияНМА.Регистратор = &Ссылка
	|		И АмортизацияНМА.Организация = &Организация
	|		И АмортизацияНМА.Период < &Период
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		АмортизацияНМА.НематериальныйАктив,
	|		АмортизацияНМА.Амортизация,
	|		0,
	|		0,
	|		0,
	|		0,
	|		ВЫБОР
	|			КОГДА АмортизацияНМА.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА -АмортизацияНМА.АмортизацияРегл
	|			ИНАЧЕ АмортизацияНМА.АмортизацияРегл
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА АмортизацияНМА.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА -АмортизацияНМА.АмортизацияНУ
	|			ИНАЧЕ АмортизацияНМА.АмортизацияНУ
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА АмортизацияНМА.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА -АмортизацияНМА.АмортизацияПР
	|			ИНАЧЕ АмортизацияНМА.АмортизацияПР
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА АмортизацияНМА.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА -АмортизацияНМА.Амортизация
	|			ИНАЧЕ АмортизацияНМА.Амортизация
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА АмортизацияНМА.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА -АмортизацияНМА.АмортизацияЦФ
	|			ИНАЧЕ АмортизацияНМА.АмортизацияЦФ
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.АмортизацияНМА КАК АмортизацияНМА
	|	ГДЕ
	|		АмортизацияНМА.Регистратор ССЫЛКА Документ.АмортизацияНМА2_4
	|		И АмортизацияНМА.Организация = &Организация
	|		И АмортизацияНМА.Период < &Период
	|		И АмортизацияНМА.Период МЕЖДУ НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ) И КОНЕЦПЕРИОДА(&Период, МЕСЯЦ)
	|
	|	) КАК СтоимостьИАмортизация
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияНМА.СрезПоследних(
	|			&Период,
	|			Регистратор <> &Ссылка
	|				И Организация = &Организация
	|				И НематериальныйАктив В (&СписокНМА)) КАК ПервоначальныеСведенияНМА
	|		ПО (ПервоначальныеСведенияНМА.НематериальныйАктив = СтоимостьИАмортизация.НематериальныйАктив)
	|			И (ПервоначальныеСведенияНМА.Организация = &Организация)
	|
	|СГРУППИРОВАТЬ ПО
	|	СтоимостьИАмортизация.НематериальныйАктив";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	
	Период = ?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("ПериодГраница", Новый Граница(Период, ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("Период", Период);
	
	СписокНМА = Объект.НМА.Выгрузить(ВыбранныеСтроки).ВыгрузитьКолонку("НематериальныйАктив");
	Запрос.УстановитьПараметр("СписокНМА", СписокНМА);
	
	СтоимостьИАмортизация = Запрос.Выполнить().Выгрузить();
	СтоимостьИАмортизация.Индексы.Добавить("НематериальныйАктив");
	
	Если ВыбранныеСтроки <> Неопределено Тогда
		СписокСтрок = ВыбранныеСтроки;
	Иначе
		СписокСтрок = Объект.НМА;
	КонецЕсли; 
	
	Для каждого ДанныеСтроки Из СписокСтрок Цикл
		
		ДанныеУчета = СтоимостьИАмортизация.Найти(ДанныеСтроки.НематериальныйАктив, "НематериальныйАктив");
		Если ДанныеУчета <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(ДанныеСтроки, ДанныеУчета);
		Иначе
			ДанныеСтроки.СтоимостьБУ = 0;
			ДанныеСтроки.СтоимостьНУ = 0;
			ДанныеСтроки.СтоимостьПР = 0;
			ДанныеСтроки.СтоимостьУУ = 0;
			ДанныеСтроки.СтоимостьЦФ = 0;
			ДанныеСтроки.АмортизацияБУ = 0;
			ДанныеСтроки.АмортизацияНУ = 0;
			ДанныеСтроки.АмортизацияПР = 0;
			ДанныеСтроки.АмортизацияУУ = 0;
			ДанныеСтроки.АмортизацияЦФ = 0;
			ДанныеСтроки.ПервоначальнаяСтоимостьБУ = 0;
			ДанныеСтроки.ПервоначальнаяСтоимостьНУ = 0;
			ДанныеСтроки.ПервоначальнаяСтоимостьУУ = 0;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Рассчитывает амортизацию при переходе на ФСБУ14 по формуле:
// Акорр = (ПС - ЛС) / СПИобщ * СПИист - Ан
//	где
//		Акорр - сумма корректировки накопленной амортизации объекта НМА;
//		ПС - первоначальная стоимость объекта НМА;
//		ЛС - ликвидационная стоимость объекта НМА;
//		СПИобщ - общий срок полезного использования объекта НМА в месяцах;
//		СПИист - истекший срок полезного использования объекта НМА на дату перехода на ФСБУ 14;
//		Ан - сумма накопленной по объекту НМА амортизации до даты перехода на ФСБУ 14.
//		
// Параметры:
//  Объект - ДокументОбъект.КорректировкаСтоимостиИАмортизацииНМА - Объект
//  ВыбранныеСтроки - Массив - Выбранные строки
Процедура РассчитатьАмортизациюПриПереходеНаФСБУ14(Объект, ВыбранныеСтроки = Неопределено) Экспорт

	Если ВыбранныеСтроки <> Неопределено
		И ВыбранныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПервоначальныеСведенияНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуБУ, МЕСЯЦ), &НачалоМесяца, МЕСЯЦ) КАК ИстекшийСрокБУ,
	|	РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, МЕСЯЦ), &НачалоМесяца, МЕСЯЦ) КАК ИстекшийСрокУУ,
	|	ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьБУ КАК ПервоначальнаяСтоимостьБУ,
	|	ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьУУ КАК ПервоначальнаяСтоимостьУУ
	|
	|ПОМЕСТИТЬ ПервоначальныеСведенияНМА
	|
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияНМА.СрезПоследних(
	|		&КонецМесяца, 
	|		Организация = &Организация
	|			И НематериальныйАктив В (&СписокНМА)) КАК ПервоначальныеСведенияНМА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПараметрыАмортизацииНМАБУ.НематериальныйАктив КАК НематериальныйАктив,
	|	ПараметрыАмортизацииНМАБУ.СрокПолезногоИспользованияБУ КАК СрокПолезногоИспользованияБУ,
	|	ПараметрыАмортизацииНМАБУ.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость
	|
	|ПОМЕСТИТЬ ПараметрыАмортизацииНМАБУ
	|
	|ИЗ
	|	РегистрСведений.ПараметрыАмортизацииНМАБУ.СрезПоследних(
	|		&КонецМесяца, 
	|		Организация = &Организация
	|			И НематериальныйАктив В (&СписокНМА)) КАК ПараметрыАмортизацииНМАБУ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПараметрыАмортизацииНМАУУ.НематериальныйАктив КАК НематериальныйАктив,
	|	ПараметрыАмортизацииНМАУУ.СрокИспользования КАК СрокИспользования,
	|	ПараметрыАмортизацииНМАУУ.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость
	|
	|ПОМЕСТИТЬ ПараметрыАмортизацииНМАУУ
	|
	|ИЗ
	|	РегистрСведений.ПараметрыАмортизацииНМАУУ.СрезПоследних(
	|		&КонецМесяца, 
	|		Организация = &Организация
	|			И НематериальныйАктив В (&СписокНМА)) КАК ПараметрыАмортизацииНМАУУ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПараметрыАмортизацииНМАБУ.НематериальныйАктив КАК НематериальныйАктив,
	|	ПараметрыАмортизацииНМАБУ.СрокПолезногоИспользованияБУ КАК СрокПолезногоИспользованияБУ
	|
	|ПОМЕСТИТЬ ПараметрыАмортизацииНМАБУ_НачалоМесяца
	|
	|ИЗ
	|	РегистрСведений.ПараметрыАмортизацииНМАБУ.СрезПоследних(
	|		&НачалоМесяца, 
	|		Организация = &Организация
	|			И НематериальныйАктив В (&СписокНМА)) КАК ПараметрыАмортизацииНМАБУ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПараметрыАмортизацииНМАУУ.НематериальныйАктив КАК НематериальныйАктив,
	|	ПараметрыАмортизацииНМАУУ.СрокИспользования КАК СрокИспользования
	|
	|ПОМЕСТИТЬ ПараметрыАмортизацииНМАУУ_НачалоМесяца
	|
	|ИЗ
	|	РегистрСведений.ПараметрыАмортизацииНМАУУ.СрезПоследних(
	|		&НачалоМесяца, 
	|		Организация = &Организация
	|			И НематериальныйАктив В (&СписокНМА)) КАК ПараметрыАмортизацииНМАУУ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокНМА.Ссылка КАК НематериальныйАктив,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПараметрыАмортизацииНМАУУ.СрокИспользования, 0) = 0
	|				ИЛИ ЕСТЬNULL(ПараметрыАмортизацииНМАУУ_НачалоМесяца.СрокИспользования, 0) = 0
	|
	|			ТОГДА 0
	|
	|		ИНАЧЕ (ЕСТЬNULL(ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьУУ, 0) 
	|					- ЕСТЬNULL(ПараметрыАмортизацииНМАУУ.ЛиквидационнаяСтоимость, 0))
	|	 			/ ЕСТЬNULL(ПараметрыАмортизацииНМАУУ.СрокИспользования, 0)
	|				* ЕСТЬNULL(ПервоначальныеСведенияНМА.ИстекшийСрокУУ, 0)
	|
	|			- ВЫБОР
	|				КОГДА ЕСТЬNULL(ПервоначальныеСведенияНМА.ИстекшийСрокУУ, 0) <= ЕСТЬNULL(ПараметрыАмортизацииНМАУУ_НачалоМесяца.СрокИспользования, 0)
	|					ТОГДА ЕСТЬNULL(ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьУУ, 0)
	|							/ ЕСТЬNULL(ПараметрыАмортизацииНМАУУ_НачалоМесяца.СрокИспользования, 0) 
	|					ИНАЧЕ 0 
	|			 КОНЕЦ
	|
	|	КОНЕЦ КАК АмортизацияУУ,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПараметрыАмортизацииНМАБУ.СрокПолезногоИспользованияБУ, 0) = 0
	|				ИЛИ ЕСТЬNULL(ПараметрыАмортизацииНМАБУ_НачалоМесяца.СрокПолезногоИспользованияБУ, 0) = 0
	|
	|			ТОГДА 0
	|
	|		ИНАЧЕ (ЕСТЬNULL(ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьБУ, 0) 
	|					- ЕСТЬNULL(ПараметрыАмортизацииНМАБУ.ЛиквидационнаяСтоимость, 0))
	|	 			/ ЕСТЬNULL(ПараметрыАмортизацииНМАБУ.СрокПолезногоИспользованияБУ, 0)
	|				* ЕСТЬNULL(ПервоначальныеСведенияНМА.ИстекшийСрокБУ, 0)
	|
	|			- ВЫБОР
	|				КОГДА ЕСТЬNULL(ПервоначальныеСведенияНМА.ИстекшийСрокБУ, 0) <= ЕСТЬNULL(ПараметрыАмортизацииНМАБУ_НачалоМесяца.СрокПолезногоИспользованияБУ, 0)
	|					ТОГДА ЕСТЬNULL(ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьБУ, 0)
	|							/ ЕСТЬNULL(ПараметрыАмортизацииНМАБУ_НачалоМесяца.СрокПолезногоИспользованияБУ, 0) 
	|					ИНАЧЕ 0 
	|			 КОНЕЦ
	|
	|	КОНЕЦ КАК АмортизацияБУ
	|
	|ИЗ
	|	Справочник.НематериальныеАктивы КАК СписокНМА
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПервоначальныеСведенияНМА КАК ПервоначальныеСведенияНМА
	|		ПО ПервоначальныеСведенияНМА.НематериальныйАктив = СписокНМА.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииНМАБУ КАК ПараметрыАмортизацииНМАБУ
	|		ПО ПараметрыАмортизацииНМАБУ.НематериальныйАктив = СписокНМА.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииНМАБУ_НачалоМесяца КАК ПараметрыАмортизацииНМАБУ_НачалоМесяца
	|		ПО ПараметрыАмортизацииНМАБУ_НачалоМесяца.НематериальныйАктив = СписокНМА.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииНМАУУ КАК ПараметрыАмортизацииНМАУУ
	|		ПО ПараметрыАмортизацииНМАУУ.НематериальныйАктив = СписокНМА.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииНМАУУ_НачалоМесяца КАК ПараметрыАмортизацииНМАУУ_НачалоМесяца
	|		ПО ПараметрыАмортизацииНМАУУ_НачалоМесяца.НематериальныйАктив = СписокНМА.Ссылка
	|
	|ГДЕ
	|	СписокНМА.Ссылка В (&СписокНМА)
	|";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	
	Период = ?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(Период));
	Запрос.УстановитьПараметр("КонецМесяца", КонецМесяца(Период));
	
	СписокНМА = Объект.НМА.Выгрузить(ВыбранныеСтроки).ВыгрузитьКолонку("НематериальныйАктив");
	Запрос.УстановитьПараметр("СписокНМА", СписокНМА);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл

		СтруктураПоиска = Новый Структура("НематериальныйАктив", Выборка.НематериальныйАктив);
		СписокСтрок = Объект.НМА.НайтиСтроки(СтруктураПоиска);
		
		Если СписокСтрок.Количество() <> 0 Тогда
			Если Объект.ОтражатьВУпрУчете Тогда
				СписокСтрок[0].АмортизацияУУ = Выборка.АмортизацияУУ;
			КонецЕсли;
			Если Объект.ОтражатьВРеглУчете Тогда
				СписокСтрок[0].АмортизацияБУ = Выборка.АмортизацияБУ;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "Документы.КорректировкаСтоимостиИАмортизацииНМА.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.5.17.3";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Многопоточный = Истина;
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("b39d4edc-7927-4a30-8119-3e71176bf097");
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Документы.КорректировкаСтоимостиИАмортизацииНМА.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru = 'Обновляет документы ""Корректировка стоимости и амортизации НМА"":
								  |- заполняет новые реквизиты ""Корректировка стоимости"", ""Корректировка первоначальной стоимости"", ""Корректировка амортизации"".';
								  |en = 'Updates the ""Adjust intangible asset cost and amortization"" documents:
								  |- Fills the new ""Cost adjustment"", ""Initial cost adjustment"", and ""Amortization adjustment"" attributes.'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Документы.КорректировкаСтоимостиИАмортизацииНМА.ПолноеИмя());

	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.Документы.КорректировкаСтоимостиИАмортизацииНМА.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.Документы.КорректировкаСтоимостиИАмортизацииНМА.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");

КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаОбъектов = "Документ.КорректировкаСтоимостиИАмортизацииНМА";
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Ссылка.Дата УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Ссылка");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиСсылки();

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.КорректировкаСтоимостиИАмортизацииНМА КАК ДанныеДокумента
	|
	|ГДЕ
	|	ДанныеДокумента.ВидОбъектовУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.НематериальныйАктив)
	|	И НЕ ДанныеДокумента.КорректировкаСтоимости
	|	И НЕ ДанныеДокумента.КорректировкаПервоначальнойСтоимости
	|	И НЕ ДанныеДокумента.КорректировкаАмортизации
	|";
	
	Запрос = Новый Запрос(ТекстЗапроса);

	СписокСсылок = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, СписокСсылок);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
		"Документ.КорректировкаСтоимостиИАмортизацииНМА.ОбработатьДанныеДляПереходаНаНовуюВерсию");
		
	ПолноеИмяОбъекта = "Документ.КорректировкаСтоимостиИАмортизацииНМА";
	
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	ПроблемныхОбъектов = 0;
	ОбъектовОбработано = 0;

	Для Каждого Документ Из ОбновляемыеДанные Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Документ.Ссылка);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			
			Блокировка.Заблокировать();
			
			ДокументОбъект = Документ.Ссылка.ПолучитьОбъект(); // ДокументОбъект.КорректировкаСтоимостиИАмортизацииНМА
			Если ДокументОбъект = Неопределено Тогда
				ЗафиксироватьТранзакцию();
				Продолжить;
			КонецЕсли;
			
			Если ДокументОбъект.ВидОбъектовУчета = Перечисления.ВидыОбъектовУчетаНМА.НематериальныйАктив
				И НЕ ДокументОбъект.КорректировкаСтоимости
				И НЕ ДокументОбъект.КорректировкаПервоначальнойСтоимости
				И НЕ ДокументОбъект.КорректировкаАмортизации Тогда
				
				ДокументОбъект.КорректировкаСтоимости = Истина;
				ДокументОбъект.КорректировкаПервоначальнойСтоимости = Истина;
				ДокументОбъект.КорректировкаАмортизации = Истина;
				
			КонецЕсли;

			Если ДокументОбъект.Модифицированность() Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(ДокументОбъект.Ссылка);
			КонецЕсли;
			
			ОбъектовОбработано = ОбъектовОбработано + 1;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;

			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), Документ.Ссылка);
			
		КонецПопытки;
		
	КонецЦикла;
	
 	ВнеоборотныеАктивыСлужебный.ПроверитьВыполнениеОбработчика(
 		ПроблемныхОбъектов, 
 		ОбъектовОбработано, 
 		ПолноеИмяОбъекта);
 		
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, ОбъектовОбработано);	
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
