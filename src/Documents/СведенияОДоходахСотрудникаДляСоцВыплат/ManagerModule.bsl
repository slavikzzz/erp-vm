#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(Сотрудник)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает описание состава документа
//
// Возвращаемое значение:
//  Структура - см. ЗарплатаКадрыСоставДокументов.НовоеОписаниеСоставаОбъекта.
Функция ОписаниеСоставаОбъекта() Экспорт
	Возврат ЗарплатаКадрыСоставДокументов.ОписаниеСоставаОбъектаФизическоеЛицоВШапке("Сотрудник", "");
КонецФункции

// Создает структуру для проверки данных физических лиц.
// 
// Возвращаемое значение:
//   Структура -параметры для проверки данных физических лиц:
//    * Ссылка 							- Неопределено - ссылка на документ, из которого происходит проверка.
//    * ПроверяемыеДанные 				- Неопределено - данные, требующие проверки.
//    * ДатаДокумента 					- Неопределено.
//    * ПутьКДаннымФизическогоЛица 		- Неопределено - строка с элементом хранения данных о физическом лице.
//    * НомерСтроки 					- Неопределено.
//    * ПроверятьАдрес 					- Булево - выполнять ли блок проверки адресов.
//    * ОшибкиДокумента 				- Неопределено - будет передаваться массив, содержащий накопленные ошибки.
Функция ПараметрыПроверкиДанныхФизическихЛиц() Экспорт
	
	ПараметрыПроверкиДанныхФизическихЛиц = Новый Структура;
	ПараметрыПроверкиДанныхФизическихЛиц.Вставить("Ссылка");
	ПараметрыПроверкиДанныхФизическихЛиц.Вставить("ПроверяемыеДанные");
	ПараметрыПроверкиДанныхФизическихЛиц.Вставить("ДатаДокумента");
	ПараметрыПроверкиДанныхФизическихЛиц.Вставить("ПутьКДаннымФизическогоЛица");
	ПараметрыПроверкиДанныхФизическихЛиц.Вставить("НомерСтроки", Неопределено);
	ПараметрыПроверкиДанныхФизическихЛиц.Вставить("ПроверятьАдрес", Истина);
	ПараметрыПроверкиДанныхФизическихЛиц.Вставить("ОшибкиДокумента", Неопределено);
	
	Возврат ПараметрыПроверкиДанныхФизическихЛиц;
	
КонецФункции

// Проверяет переданные данные по физическому лицу
//
// Параметры:
//  ПараметрыПроверкиДанныхФизическихЛиц - см. СведенияОДоходахСотрудникаДляСоцВыплат.ПараметрыПроверкиДанныхФизическихЛиц.
//  Отказ                                - Булево - флаг наличия ошибок.
Процедура ПроверитьДанныеФизическогоЛица(ПараметрыПроверкиДанныхФизическихЛиц, Отказ) Экспорт
	
	Если ПараметрыПроверкиДанныхФизическихЛиц.ОшибкиДокумента = Неопределено Тогда
		ПараметрыПроверкиДанныхФизическихЛиц.ОшибкиДокумента = Новый Массив;
	КонецЕсли;
	
	Ошибки = Новый Соответствие;
	
	ПравилаПроверки = ПравилаПроверкиДанныхФизическогоЛица(
	ПараметрыПроверкиДанныхФизическихЛиц.ПроверяемыеДанные.Гражданство, 
	ПараметрыПроверкиДанныхФизическихЛиц.ДатаДокумента);
	
	ДанныеФизическогоЛицаДляПроверки = ДанныеФизическогоЛицаДляПроверки(
	ПараметрыПроверкиДанныхФизическихЛиц.ПроверяемыеДанные);
	
	ФизическиеЛицаЗарплатаКадры.ПроверитьДанныеФизическогоЛица(
		ДанныеФизическогоЛицаДляПроверки,
		ПравилаПроверки,
		Ошибки);
	
	Если ПараметрыПроверкиДанныхФизическихЛиц.НомерСтроки <> Неопределено Тогда
		ПутьКДанным = ПараметрыПроверкиДанныхФизическихЛиц.ПутьКДаннымФизическогоЛица 
		+ "[" + Формат(ПараметрыПроверкиДанныхФизическихЛиц.НомерСтроки - 1, "ЧН=0; ЧГ=") + "].";
	ИначеЕсли Не ПустаяСтрока(ПараметрыПроверкиДанныхФизическихЛиц.ПутьКДаннымФизическогоЛица) Тогда
		ПутьКДанным = ПараметрыПроверкиДанныхФизическихЛиц.ПутьКДаннымФизическогоЛица + ".";
	Иначе
		ПутьКДанным = ПараметрыПроверкиДанныхФизическихЛиц.ПутьКДаннымФизическогоЛица;
	КонецЕсли;
	
	Для Каждого ОшибкиПоФизЛицу Из Ошибки Цикл
		Для Каждого ОшибкаДанныхФизическогоЛица Из ОшибкиПоФизЛицу.Значение Цикл
			
				ОбщегоНазначения.СообщитьПользователю(
					ОшибкаДанныхФизическогоЛица.ТекстОшибки,
					ПараметрыПроверкиДанныхФизическихЛиц.Ссылка,
					ПутьКДанным + ОшибкаДанныхФизическогоЛица.ПолеФормы,,
					Отказ);
					ПараметрыПроверкиДанныхФизическихЛиц.ОшибкиДокумента.Добавить(
					ОшибкаДанныхФизическогоЛица.ТекстОшибки);
					
		КонецЦикла;
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(ПараметрыПроверкиДанныхФизическихЛиц.ПроверяемыеДанные.СтатусНалогоплательщика) Тогда
		ТекстОшибки = СтрШаблон(
						НСтр("ru = 'Сотрудник %1: не заполнено поле ""Статус налогоплательщика""';
							|en = 'Employee %1: the ""Taxpayer status"" field is not filled in'"),
						ПараметрыПроверкиДанныхФизическихЛиц.ПроверяемыеДанные.СотрудникНаименование);
		
		ОбщегоНазначения.СообщитьПользователю(
			ТекстОшибки,
			ПараметрыПроверкиДанныхФизическихЛиц.Ссылка,
			ПутьКДанным + "СтатусНалогоплательщика",,
			Отказ);
			ПараметрыПроверкиДанныхФизическихЛиц.ОшибкиДокумента.Добавить(ТекстОшибки);
	КонецЕсли;
	
	// Адреса
	Если ПараметрыПроверкиДанныхФизическихЛиц.ПроверятьАдрес Тогда
		Если Не ЗначениеЗаполнено(ПараметрыПроверкиДанныхФизическихЛиц.ПроверяемыеДанные.Адрес) 
			И Не ЗначениеЗаполнено(ПараметрыПроверкиДанныхФизическихЛиц.ПроверяемыеДанные.АдресЗарубежом) Тогда
			Если ПараметрыПроверкиДанныхФизическихЛиц.ПроверяемыеДанные.Гражданство = Справочники.СтраныМира.Россия Тогда 
				ШаблонОшибки = НСтр("ru = 'Сотрудник %1: не указан адрес в РФ.';
									|en = 'Employee %1: address in the Russian Federation is not specified.'");
			Иначе
				ШаблонОшибки = НСтр("ru = 'Сотрудник %1: для физического лица нерезидента или иностранца должен быть заполнен ""Адрес зарубежом"".';
									|en = 'Employee %1: the ""Address abroad"" field must be filled in for a non-resident individual or foreigner.'");
			КонецЕсли;
			ТекстОшибки = СтрШаблон(ШаблонОшибки, ПараметрыПроверкиДанныхФизическихЛиц.ПроверяемыеДанные.СотрудникНаименование); 
			
			ОбщегоНазначения.СообщитьПользователю(
				ТекстОшибки,
				ПараметрыПроверкиДанныхФизическихЛиц.Ссылка,
				ПутьКДанным + "АдресЗаРубежом",,
				Отказ);
			ПараметрыПроверкиДанныхФизическихЛиц.ОшибкиДокумента.Добавить(ТекстОшибки);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Формирует печатную форму "Сведения о доходах физического лица, направляемые в целях назначения социальных выплат".
//
// Параметры:
// 		МассивОбъектов - Массив - содержит ДокументСсылка.СведенияОДоходахСотрудникаДляСоцВыплат
// 		ОбъектыПечати - СписокЗначений - может быть пустой
//
// Возвращаемое значение:
// 		ТабличныйДокумент
//
Функция СформироватьПечатнуюФормуСведенияОДоходахСотрудникаДляСоцВыплат(МассивОбъектов, ОбъектыПечати) Экспорт
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	ДанныеНА = УчетНДФЛ.СправкиНДФЛДанныеДляПечати(МассивОбъектов);
	ТаблицыДокумента = ТаблицыДанныхДокументаДляПечати(МассивОбъектов);
	
	Возврат УчетНДФЛ.СформироватьПечатнуюФормуСведенияОДоходахСотрудникаДляСоцВыплат(ОбъектыПечати, МассивОбъектов, ДанныеНА, ТаблицыДокумента.Сотрудники, ТаблицыДокумента.СведенияОДоходах, ТаблицыДокумента.СведенияОВычетах, ТаблицыДокумента.СведенияОбУведомлениях);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПравилаПроверкиДанныхФизическогоЛица(Гражданство, ДатаДокумента)
	
	Правила = Новый Массив;
	
	ФизическиеЛицаЗарплатаКадры.ДобавитьПравилоПроверки(
		Правила,
		"ИНН",
		"ИНН",
		НСтр("ru = 'ИНН';
			|en = 'TIN'"),
		Ложь);
	
	ФизическиеЛицаЗарплатаКадры.ДобавитьПравилоПроверкиФИО(
		Правила,
		"Фамилия",
		"Имя",
		"Отчество",
		"Гражданство",
		НСтр("ru = 'ФИО';
			|en = 'Full name'"));
	
	ФизическиеЛицаЗарплатаКадры.ДобавитьПравилоПроверкиДатыРождения(
		Правила,
		"ДатаРождения",
		НСтр("ru = 'Дата рождения';
			|en = 'Date of birth'"),
		ДатаДокумента,
		Истина);
	
	ФизическиеЛицаЗарплатаКадры.ДобавитьПравилоПроверкиУдостоверенияЛичности(
		Правила,
		"ВидДокумента",
		"СерияДокумента",
		"НомерДокумента",
		"ДатаВыдачи",
		"КемВыдан",
		НСтр("ru = 'Документ удостоверяющий личность';
			|en = 'Identity document'"),
		Истина,
		Истина,
		Ложь);
	
	Возврат Правила;
	
КонецФункции

Функция ДанныеФизическогоЛицаДляПроверки(ПроверяемыеДанные)
	
	ДанныеФизическогоЛицаДляПроверки = Новый Структура;
	ДанныеФизическогоЛицаДляПроверки.Вставить("ФизическоеЛицо", ПроверяемыеДанные.Сотрудник);
	ДанныеФизическогоЛицаДляПроверки.Вставить("Наименование", ПроверяемыеДанные.СотрудникНаименование);
	ДанныеФизическогоЛицаДляПроверки.Вставить("Фамилия");
	ДанныеФизическогоЛицаДляПроверки.Вставить("Имя");
	ДанныеФизическогоЛицаДляПроверки.Вставить("Отчество");
	ДанныеФизическогоЛицаДляПроверки.Вставить("ИНН");
	ДанныеФизическогоЛицаДляПроверки.Вставить("Адрес");
	ДанныеФизическогоЛицаДляПроверки.Вставить("ВидДокумента");
	ДанныеФизическогоЛицаДляПроверки.Вставить("СерияДокумента");
	ДанныеФизическогоЛицаДляПроверки.Вставить("НомерДокумента");
	ДанныеФизическогоЛицаДляПроверки.Вставить("Гражданство");
	ДанныеФизическогоЛицаДляПроверки.Вставить("ДатаРождения");
	ДанныеФизическогоЛицаДляПроверки.Вставить("АдресЗарубежом");
	ДанныеФизическогоЛицаДляПроверки.Вставить("ДатаВыдачи");
	ДанныеФизическогоЛицаДляПроверки.Вставить("КемВыдан");
	
	ЗаполнитьЗначенияСвойств(ДанныеФизическогоЛицаДляПроверки, ПроверяемыеДанные);
	
	Возврат ДанныеФизическогоЛицаДляПроверки;
	
КонецФункции

Функция  ОписаниеПодписейДокумента() Экспорт 

	ОписаниеПодписей = ПодписиДокументов.ОписаниеТаблицыПодписей();

	ОписаниеПодписиРуководитель = ПодписиДокументов.ОписаниеРеквизитовПодписанта();
	ОписаниеПодписиРуководитель.ФизическоеЛицо = "СправкуПодписал";
	ОписаниеПодписиРуководитель.Должность = "ДолжностьПодписавшегоЛица";
	ОписаниеПодписиРуководитель.ОснованиеПодписи = "ОснованиеПодписи";

	ПереопределяемыеИмена = Новый Соответствие;
	ПереопределяемыеИмена.Вставить("Руководитель", ОписаниеПодписиРуководитель);

	ПодписиДокументов.ДобавитьОписаниеПодписейОрганизации(
		ОписаниеПодписей,
		"Руководитель",
		ПереопределяемыеИмена);

Возврат ОписаниеПодписей;

КонецФункции

#Область ПроцедурыИФункцииПечати

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Сведения о доходах физического лица, направляемые в целях назначения социальных выплат
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "УправлениеПечатьюБЗККлиент.ВыполнитьКомандуПечати";
	КомандаПечати.Идентификатор = "ФормаСведенияОДоходахСотрудникаДляСоцВыплат";
	КомандаПечати.Представление = НСтр("ru = 'Сведения для соц. выплат';
										|en = 'Information for social payments'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Отказ = Ложь;
		
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ФормаСведенияОДоходахСотрудникаДляСоцВыплат") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ФормаСведенияОДоходахСотрудникаДляСоцВыплат",
			НСтр("ru = 'Сведения о доходах физического лица, направляемые в целях назначения социальных выплат';
				|en = 'Information on personal income to submit for social payment assignment'"),
			СформироватьПечатнуюФормуСведенияОДоходахСотрудникаДляСоцВыплат(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
	
КонецПроцедуры

Функция ТаблицыДанныхДокументаДляПечати(Ссылка)
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СведенияДляСоцВыплат.Ссылка КАК Ссылка,
	|	СведенияДляСоцВыплат.ВерсияДанных КАК ВерсияДанных,
	|	СведенияДляСоцВыплат.ПометкаУдаления КАК ПометкаУдаления,
	|	СведенияДляСоцВыплат.Номер КАК Номер,
	|	СведенияДляСоцВыплат.Дата КАК Дата,
	|	СведенияДляСоцВыплат.Проведен КАК Проведен,
	|	СведенияДляСоцВыплат.НалоговыйПериод КАК НалоговыйПериод,
	|	СведенияДляСоцВыплат.Организация КАК Организация,
	|	СведенияДляСоцВыплат.СправкуПодписал КАК СправкуПодписал,
	|	СведенияДляСоцВыплат.ДолжностьПодписавшегоЛица КАК ДолжностьПодписавшегоЛица,
	|	СведенияДляСоцВыплат.СпособФормирования КАК СпособФормирования,
	|	СведенияДляСоцВыплат.Сотрудник КАК Сотрудник,
	|	СведенияДляСоцВыплат.Сотрудник.Наименование КАК СотрудникНаименование,
	|	СведенияДляСоцВыплат.ИНН КАК ИНН,
	|	СведенияДляСоцВыплат.Номер КАК НомерСправки,
	|	СведенияДляСоцВыплат.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	СведенияДляСоцВыплат.Фамилия КАК Фамилия,
	|	СведенияДляСоцВыплат.Имя КАК Имя,
	|	СведенияДляСоцВыплат.Отчество КАК Отчество,
	|	СведенияДляСоцВыплат.Адрес КАК Адрес,
	|	СведенияДляСоцВыплат.ВидДокумента КАК ВидДокумента,
	|	СведенияДляСоцВыплат.СерияДокумента КАК СерияДокумента,
	|	СведенияДляСоцВыплат.НомерДокумента КАК НомерДокумента,
	|	СведенияДляСоцВыплат.СтранаВыдачиДокумента КАК СтранаВыдачиДокумента,
	|	СведенияДляСоцВыплат.Гражданство КАК Гражданство,
	|	СведенияДляСоцВыплат.ИННвСтранеГражданства КАК ИННвСтранеГражданства,
	|	СведенияДляСоцВыплат.ДатаРождения КАК ДатаРождения,
	|	СведенияДляСоцВыплат.СтатусНалогоплательщика КАК СтатусНалогоплательщика,
	|	СведенияДляСоцВыплат.Ответственный КАК Ответственный,
	|	СведенияДляСоцВыплат.Комментарий КАК Комментарий,
	|	СведенияДляСоцВыплат.АдресЗаРубежом КАК АдресЗаРубежом,
	|	СведенияДляСоцВыплат.ОбщаяСуммаДоходаПоСтавке13 КАК ОбщаяСуммаДоходаПоСтавке13,
	|	СведенияДляСоцВыплат.ОблагаемаяСуммаДоходаПоСтавке13 КАК ОблагаемаяСуммаДоходаПоСтавке13,
	|	СведенияДляСоцВыплат.ИсчисленоПоСтавке13 КАК ИсчисленоПоСтавке13,
	|	СведенияДляСоцВыплат.УдержаноПоСтавке13 КАК УдержаноПоСтавке13,
	|	СведенияДляСоцВыплат.ЗадолженностьПоСтавке13 КАК ЗадолженностьПоСтавке13,
	|	СведенияДляСоцВыплат.ИзлишнеУдержаноПоСтавке13 КАК ИзлишнеУдержаноПоСтавке13,
	|	СведенияДляСоцВыплат.ПеречисленоПоСтавке13 КАК ПеречисленоПоСтавке13,
	|	СведенияДляСоцВыплат.ОбщаяСуммаДоходаПоСтавке30 КАК ОбщаяСуммаДоходаПоСтавке30,
	|	СведенияДляСоцВыплат.ОблагаемаяСуммаДоходаПоСтавке30 КАК ОблагаемаяСуммаДоходаПоСтавке30,
	|	СведенияДляСоцВыплат.ИсчисленоПоСтавке30 КАК ИсчисленоПоСтавке30,
	|	СведенияДляСоцВыплат.УдержаноПоСтавке30 КАК УдержаноПоСтавке30,
	|	СведенияДляСоцВыплат.ЗадолженностьПоСтавке30 КАК ЗадолженностьПоСтавке30,
	|	СведенияДляСоцВыплат.ИзлишнеУдержаноПоСтавке30 КАК ИзлишнеУдержаноПоСтавке30,
	|	СведенияДляСоцВыплат.ПеречисленоПоСтавке30 КАК ПеречисленоПоСтавке30,
	|	СведенияДляСоцВыплат.ОбщаяСуммаДоходаПоСтавке9 КАК ОбщаяСуммаДоходаПоСтавке9,
	|	СведенияДляСоцВыплат.ОблагаемаяСуммаДоходаПоСтавке9 КАК ОблагаемаяСуммаДоходаПоСтавке9,
	|	СведенияДляСоцВыплат.ИсчисленоПоСтавке9 КАК ИсчисленоПоСтавке9,
	|	СведенияДляСоцВыплат.УдержаноПоСтавке9 КАК УдержаноПоСтавке9,
	|	СведенияДляСоцВыплат.ЗадолженностьПоСтавке9 КАК ЗадолженностьПоСтавке9,
	|	СведенияДляСоцВыплат.ИзлишнеУдержаноПоСтавке9 КАК ИзлишнеУдержаноПоСтавке9,
	|	СведенияДляСоцВыплат.ПеречисленоПоСтавке9 КАК ПеречисленоПоСтавке9,
	|	СведенияДляСоцВыплат.ОбщаяСуммаДоходаПоСтавке15 КАК ОбщаяСуммаДоходаПоСтавке15,
	|	СведенияДляСоцВыплат.ОблагаемаяСуммаДоходаПоСтавке15 КАК ОблагаемаяСуммаДоходаПоСтавке15,
	|	СведенияДляСоцВыплат.ИсчисленоПоСтавке15 КАК ИсчисленоПоСтавке15,
	|	СведенияДляСоцВыплат.УдержаноПоСтавке15 КАК УдержаноПоСтавке15,
	|	СведенияДляСоцВыплат.ЗадолженностьПоСтавке15 КАК ЗадолженностьПоСтавке15,
	|	СведенияДляСоцВыплат.ИзлишнеУдержаноПоСтавке15 КАК ИзлишнеУдержаноПоСтавке15,
	|	СведенияДляСоцВыплат.ПеречисленоПоСтавке15 КАК ПеречисленоПоСтавке15,
	|	СведенияДляСоцВыплат.ОбщаяСуммаДоходаПоСтавке18 КАК ОбщаяСуммаДоходаПоСтавке18,
	|	СведенияДляСоцВыплат.ОблагаемаяСуммаДоходаПоСтавке18 КАК ОблагаемаяСуммаДоходаПоСтавке18,
	|	СведенияДляСоцВыплат.ИсчисленоПоСтавке18 КАК ИсчисленоПоСтавке18,
	|	СведенияДляСоцВыплат.УдержаноПоСтавке18 КАК УдержаноПоСтавке18,
	|	СведенияДляСоцВыплат.ЗадолженностьПоСтавке18 КАК ЗадолженностьПоСтавке18,
	|	СведенияДляСоцВыплат.ИзлишнеУдержаноПоСтавке18 КАК ИзлишнеУдержаноПоСтавке18,
	|	СведенияДляСоцВыплат.ПеречисленоПоСтавке18 КАК ПеречисленоПоСтавке18,
	|	СведенияДляСоцВыплат.ОбщаяСуммаДоходаПоСтавке20 КАК ОбщаяСуммаДоходаПоСтавке20,
	|	СведенияДляСоцВыплат.ОблагаемаяСуммаДоходаПоСтавке20 КАК ОблагаемаяСуммаДоходаПоСтавке20,
	|	СведенияДляСоцВыплат.ИсчисленоПоСтавке20 КАК ИсчисленоПоСтавке20,
	|	СведенияДляСоцВыплат.УдержаноПоСтавке20 КАК УдержаноПоСтавке20,
	|	СведенияДляСоцВыплат.ЗадолженностьПоСтавке20 КАК ЗадолженностьПоСтавке20,
	|	СведенияДляСоцВыплат.ИзлишнеУдержаноПоСтавке20 КАК ИзлишнеУдержаноПоСтавке20,
	|	СведенияДляСоцВыплат.ПеречисленоПоСтавке20 КАК ПеречисленоПоСтавке20,
	|	СведенияДляСоцВыплат.ОбщаяСуммаДоходаПоСтавке22 КАК ОбщаяСуммаДоходаПоСтавке22,
	|	СведенияДляСоцВыплат.ОблагаемаяСуммаДоходаПоСтавке22 КАК ОблагаемаяСуммаДоходаПоСтавке22,
	|	СведенияДляСоцВыплат.ИсчисленоПоСтавке22 КАК ИсчисленоПоСтавке22,
	|	СведенияДляСоцВыплат.УдержаноПоСтавке22 КАК УдержаноПоСтавке22,
	|	СведенияДляСоцВыплат.ЗадолженностьПоСтавке22 КАК ЗадолженностьПоСтавке22,
	|	СведенияДляСоцВыплат.ИзлишнеУдержаноПоСтавке22 КАК ИзлишнеУдержаноПоСтавке22,
	|	СведенияДляСоцВыплат.ПеречисленоПоСтавке22 КАК ПеречисленоПоСтавке22,
	|	СведенияДляСоцВыплат.ОбщаяСуммаДоходаПоСтавке35 КАК ОбщаяСуммаДоходаПоСтавке35,
	|	СведенияДляСоцВыплат.ОблагаемаяСуммаДоходаПоСтавке35 КАК ОблагаемаяСуммаДоходаПоСтавке35,
	|	СведенияДляСоцВыплат.ИсчисленоПоСтавке35 КАК ИсчисленоПоСтавке35,
	|	СведенияДляСоцВыплат.УдержаноПоСтавке35 КАК УдержаноПоСтавке35,
	|	СведенияДляСоцВыплат.ЗадолженностьПоСтавке35 КАК ЗадолженностьПоСтавке35,
	|	СведенияДляСоцВыплат.ИзлишнеУдержаноПоСтавке35 КАК ИзлишнеУдержаноПоСтавке35,
	|	СведенияДляСоцВыплат.ПеречисленоПоСтавке35 КАК ПеречисленоПоСтавке35,
	|	СведенияДляСоцВыплат.ОбщаяСуммаДоходаПоСтавке5 КАК ОбщаяСуммаДоходаПоСтавке5,
	|	СведенияДляСоцВыплат.ОблагаемаяСуммаДоходаПоСтавке5 КАК ОблагаемаяСуммаДоходаПоСтавке5,
	|	СведенияДляСоцВыплат.ИсчисленоПоСтавке5 КАК ИсчисленоПоСтавке5,
	|	СведенияДляСоцВыплат.УдержаноПоСтавке5 КАК УдержаноПоСтавке5,
	|	СведенияДляСоцВыплат.ЗадолженностьПоСтавке5 КАК ЗадолженностьПоСтавке5,
	|	СведенияДляСоцВыплат.ИзлишнеУдержаноПоСтавке5 КАК ИзлишнеУдержаноПоСтавке5,
	|	СведенияДляСоцВыплат.ПеречисленоПоСтавке5 КАК ПеречисленоПоСтавке5,
	|	СведенияДляСоцВыплат.ОбщаяСуммаДоходаПоСтавке10 КАК ОбщаяСуммаДоходаПоСтавке10,
	|	СведенияДляСоцВыплат.ОблагаемаяСуммаДоходаПоСтавке10 КАК ОблагаемаяСуммаДоходаПоСтавке10,
	|	СведенияДляСоцВыплат.ИсчисленоПоСтавке10 КАК ИсчисленоПоСтавке10,
	|	СведенияДляСоцВыплат.УдержаноПоСтавке10 КАК УдержаноПоСтавке10,
	|	СведенияДляСоцВыплат.ЗадолженностьПоСтавке10 КАК ЗадолженностьПоСтавке10,
	|	СведенияДляСоцВыплат.ИзлишнеУдержаноПоСтавке10 КАК ИзлишнеУдержаноПоСтавке10,
	|	СведенияДляСоцВыплат.ПеречисленоПоСтавке10 КАК ПеречисленоПоСтавке10,
	|	СведенияДляСоцВыплат.ЗачтеноАвансовыхПлатежейПоСтавке5 КАК ЗачтеноАвансовыхПлатежейПоСтавке5,
	|	СведенияДляСоцВыплат.ЗачтеноАвансовыхПлатежейПоСтавке9 КАК ЗачтеноАвансовыхПлатежейПоСтавке9,
	|	СведенияДляСоцВыплат.ЗачтеноАвансовыхПлатежейПоСтавке10 КАК ЗачтеноАвансовыхПлатежейПоСтавке10,
	|	СведенияДляСоцВыплат.ЗачтеноАвансовыхПлатежейПоСтавке13 КАК ЗачтеноАвансовыхПлатежейПоСтавке13,
	|	СведенияДляСоцВыплат.ЗачтеноАвансовыхПлатежейПоСтавке35 КАК ЗачтеноАвансовыхПлатежейПоСтавке35,
	|	СведенияДляСоцВыплат.ЗачтеноАвансовыхПлатежейПоСтавке30 КАК ЗачтеноАвансовыхПлатежейПоСтавке30,
	|	СведенияДляСоцВыплат.ЗачтеноАвансовыхПлатежейПоСтавке15 КАК ЗачтеноАвансовыхПлатежейПоСтавке15,
	|	СведенияДляСоцВыплат.ЗачтеноАвансовыхПлатежейПоСтавке18 КАК ЗачтеноАвансовыхПлатежейПоСтавке18,
	|	СведенияДляСоцВыплат.ЗачтеноАвансовыхПлатежейПоСтавке20 КАК ЗачтеноАвансовыхПлатежейПоСтавке20,
	|	СведенияДляСоцВыплат.ЗачтеноАвансовыхПлатежейПоСтавке22 КАК ЗачтеноАвансовыхПлатежейПоСтавке22,
	|	СведенияДляСоцВыплат.НомерУведомленияАвансовыеПлатежи КАК НомерУведомленияАвансовыеПлатежи,
	|	СведенияДляСоцВыплат.ДатаУведомленияАвансовыеПлатежи КАК ДатаУведомленияАвансовыеПлатежи,
	|	СведенияДляСоцВыплат.КодНалоговогоОрганаУведомленияАвансовыеПлатежи КАК КодНалоговогоОрганаУведомленияАвансовыеПлатежи,
	|	СведенияДляСоцВыплат.ЗачтеноАвансовыхПлатежейПоСтавке3 КАК ЗачтеноАвансовыхПлатежейПоСтавке3,
	|	СведенияДляСоцВыплат.ОбщаяСуммаДоходаПоСтавке3 КАК ОбщаяСуммаДоходаПоСтавке3,
	|	СведенияДляСоцВыплат.ОблагаемаяСуммаДоходаПоСтавке3 КАК ОблагаемаяСуммаДоходаПоСтавке3,
	|	СведенияДляСоцВыплат.ИсчисленоПоСтавке3 КАК ИсчисленоПоСтавке3,
	|	СведенияДляСоцВыплат.УдержаноПоСтавке3 КАК УдержаноПоСтавке3,
	|	СведенияДляСоцВыплат.ЗадолженностьПоСтавке3 КАК ЗадолженностьПоСтавке3,
	|	СведенияДляСоцВыплат.ИзлишнеУдержаноПоСтавке3 КАК ИзлишнеУдержаноПоСтавке3,
	|	СведенияДляСоцВыплат.ПеречисленоПоСтавке3 КАК ПеречисленоПоСтавке3,
	|	СведенияДляСоцВыплат.ЗачтеноАвансовыхПлатежейПоСтавке6 КАК ЗачтеноАвансовыхПлатежейПоСтавке6,
	|	СведенияДляСоцВыплат.ОбщаяСуммаДоходаПоСтавке6 КАК ОбщаяСуммаДоходаПоСтавке6,
	|	СведенияДляСоцВыплат.ОблагаемаяСуммаДоходаПоСтавке6 КАК ОблагаемаяСуммаДоходаПоСтавке6,
	|	СведенияДляСоцВыплат.ИсчисленоПоСтавке6 КАК ИсчисленоПоСтавке6,
	|	СведенияДляСоцВыплат.УдержаноПоСтавке6 КАК УдержаноПоСтавке6,
	|	СведенияДляСоцВыплат.ЗадолженностьПоСтавке6 КАК ЗадолженностьПоСтавке6,
	|	СведенияДляСоцВыплат.ИзлишнеУдержаноПоСтавке6 КАК ИзлишнеУдержаноПоСтавке6,
	|	СведенияДляСоцВыплат.ПеречисленоПоСтавке6 КАК ПеречисленоПоСтавке6,
	|	СведенияДляСоцВыплат.ЗачтеноАвансовыхПлатежейПоСтавке7 КАК ЗачтеноАвансовыхПлатежейПоСтавке7,
	|	СведенияДляСоцВыплат.ОбщаяСуммаДоходаПоСтавке7 КАК ОбщаяСуммаДоходаПоСтавке7,
	|	СведенияДляСоцВыплат.ОблагаемаяСуммаДоходаПоСтавке7 КАК ОблагаемаяСуммаДоходаПоСтавке7,
	|	СведенияДляСоцВыплат.ИсчисленоПоСтавке7 КАК ИсчисленоПоСтавке7,
	|	СведенияДляСоцВыплат.УдержаноПоСтавке7 КАК УдержаноПоСтавке7,
	|	СведенияДляСоцВыплат.ЗадолженностьПоСтавке7 КАК ЗадолженностьПоСтавке7,
	|	СведенияДляСоцВыплат.ИзлишнеУдержаноПоСтавке7 КАК ИзлишнеУдержаноПоСтавке7,
	|	СведенияДляСоцВыплат.ПеречисленоПоСтавке7 КАК ПеречисленоПоСтавке7,
	|	СведенияДляСоцВыплат.ЗачтеноАвансовыхПлатежейПоСтавке12 КАК ЗачтеноАвансовыхПлатежейПоСтавке12,
	|	СведенияДляСоцВыплат.ОбщаяСуммаДоходаПоСтавке12 КАК ОбщаяСуммаДоходаПоСтавке12,
	|	СведенияДляСоцВыплат.ОблагаемаяСуммаДоходаПоСтавке12 КАК ОблагаемаяСуммаДоходаПоСтавке12,
	|	СведенияДляСоцВыплат.ИсчисленоПоСтавке12 КАК ИсчисленоПоСтавке12,
	|	СведенияДляСоцВыплат.УдержаноПоСтавке12 КАК УдержаноПоСтавке12,
	|	СведенияДляСоцВыплат.ЗадолженностьПоСтавке12 КАК ЗадолженностьПоСтавке12,
	|	СведенияДляСоцВыплат.ИзлишнеУдержаноПоСтавке12 КАК ИзлишнеУдержаноПоСтавке12,
	|	СведенияДляСоцВыплат.ПеречисленоПоСтавке12 КАК ПеречисленоПоСтавке12,
	|	СведенияДляСоцВыплат.СуммаДоходаНеудержанногоПоСтавке13 КАК СуммаДоходаНеудержанногоПоСтавке13,
	|	СведенияДляСоцВыплат.СуммаДоходаНеудержанногоПоСтавке30 КАК СуммаДоходаНеудержанногоПоСтавке30,
	|	СведенияДляСоцВыплат.СуммаДоходаНеудержанногоПоСтавке9 КАК СуммаДоходаНеудержанногоПоСтавке9,
	|	СведенияДляСоцВыплат.СуммаДоходаНеудержанногоПоСтавке15 КАК СуммаДоходаНеудержанногоПоСтавке15,
	|	СведенияДляСоцВыплат.СуммаДоходаНеудержанногоПоСтавке18 КАК СуммаДоходаНеудержанногоПоСтавке18,
	|	СведенияДляСоцВыплат.СуммаДоходаНеудержанногоПоСтавке20 КАК СуммаДоходаНеудержанногоПоСтавке20,
	|	СведенияДляСоцВыплат.СуммаДоходаНеудержанногоПоСтавке22 КАК СуммаДоходаНеудержанногоПоСтавке22,
	|	СведенияДляСоцВыплат.СуммаДоходаНеудержанногоПоСтавке35 КАК СуммаДоходаНеудержанногоПоСтавке35,
	|	СведенияДляСоцВыплат.СуммаДоходаНеудержанногоПоСтавке5 КАК СуммаДоходаНеудержанногоПоСтавке5,
	|	СведенияДляСоцВыплат.СуммаДоходаНеудержанногоПоСтавке10 КАК СуммаДоходаНеудержанногоПоСтавке10,
	|	СведенияДляСоцВыплат.СуммаДоходаНеудержанногоПоСтавке3 КАК СуммаДоходаНеудержанногоПоСтавке3,
	|	СведенияДляСоцВыплат.СуммаДоходаНеудержанногоПоСтавке6 КАК СуммаДоходаНеудержанногоПоСтавке6,
	|	СведенияДляСоцВыплат.СуммаДоходаНеудержанногоПоСтавке7 КАК СуммаДоходаНеудержанногоПоСтавке7,
	|	СведенияДляСоцВыплат.СуммаДоходаНеудержанногоПоСтавке12 КАК СуммаДоходаНеудержанногоПоСтавке12,
	|	СведенияДляСоцВыплат.НалогНаПрибыльДляДивидендовПоСтавке13 КАК НалогНаПрибыльДляДивидендовПоСтавке13,
	|	СведенияДляСоцВыплат.НалогНаПрибыльДляДивидендовПоСтавке30 КАК НалогНаПрибыльДляДивидендовПоСтавке30,
	|	СведенияДляСоцВыплат.НалогНаПрибыльДляДивидендовПоСтавке9 КАК НалогНаПрибыльДляДивидендовПоСтавке9,
	|	СведенияДляСоцВыплат.НалогНаПрибыльДляДивидендовПоСтавке15 КАК НалогНаПрибыльДляДивидендовПоСтавке15,
	|	СведенияДляСоцВыплат.НалогНаПрибыльДляДивидендовПоСтавке18 КАК НалогНаПрибыльДляДивидендовПоСтавке18,
	|	СведенияДляСоцВыплат.НалогНаПрибыльДляДивидендовПоСтавке20 КАК НалогНаПрибыльДляДивидендовПоСтавке20,
	|	СведенияДляСоцВыплат.НалогНаПрибыльДляДивидендовПоСтавке22 КАК НалогНаПрибыльДляДивидендовПоСтавке22,
	|	СведенияДляСоцВыплат.НалогНаПрибыльДляДивидендовПоСтавке35 КАК НалогНаПрибыльДляДивидендовПоСтавке35,
	|	СведенияДляСоцВыплат.НалогНаПрибыльДляДивидендовПоСтавке5 КАК НалогНаПрибыльДляДивидендовПоСтавке5,
	|	СведенияДляСоцВыплат.НалогНаПрибыльДляДивидендовПоСтавке10 КАК НалогНаПрибыльДляДивидендовПоСтавке10,
	|	СведенияДляСоцВыплат.НалогНаПрибыльДляДивидендовПоСтавке3 КАК НалогНаПрибыльДляДивидендовПоСтавке3,
	|	СведенияДляСоцВыплат.НалогНаПрибыльДляДивидендовПоСтавке6 КАК НалогНаПрибыльДляДивидендовПоСтавке6,
	|	СведенияДляСоцВыплат.НалогНаПрибыльДляДивидендовПоСтавке7 КАК НалогНаПрибыльДляДивидендовПоСтавке7,
	|	СведенияДляСоцВыплат.НалогНаПрибыльДляДивидендовПоСтавке12 КАК НалогНаПрибыльДляДивидендовПоСтавке12,
	|	СведенияДляСоцВыплат.НалогСДивидендовУплаченныйЗаРубежомПоСтавке13 КАК НалогСДивидендовУплаченныйЗаРубежомПоСтавке13,
	|	СведенияДляСоцВыплат.НалогСДивидендовУплаченныйЗаРубежомПоСтавке30 КАК НалогСДивидендовУплаченныйЗаРубежомПоСтавке30,
	|	СведенияДляСоцВыплат.НалогСДивидендовУплаченныйЗаРубежомПоСтавке9 КАК НалогСДивидендовУплаченныйЗаРубежомПоСтавке9,
	|	СведенияДляСоцВыплат.НалогСДивидендовУплаченныйЗаРубежомПоСтавке15 КАК НалогСДивидендовУплаченныйЗаРубежомПоСтавке15,
	|	СведенияДляСоцВыплат.НалогСДивидендовУплаченныйЗаРубежомПоСтавке18 КАК НалогСДивидендовУплаченныйЗаРубежомПоСтавке18,
	|	СведенияДляСоцВыплат.НалогСДивидендовУплаченныйЗаРубежомПоСтавке20 КАК НалогСДивидендовУплаченныйЗаРубежомПоСтавке20,
	|	СведенияДляСоцВыплат.НалогСДивидендовУплаченныйЗаРубежомПоСтавке22 КАК НалогСДивидендовУплаченныйЗаРубежомПоСтавке22,
	|	СведенияДляСоцВыплат.НалогСДивидендовУплаченныйЗаРубежомПоСтавке35 КАК НалогСДивидендовУплаченныйЗаРубежомПоСтавке35,
	|	СведенияДляСоцВыплат.НалогСДивидендовУплаченныйЗаРубежомПоСтавке5 КАК НалогСДивидендовУплаченныйЗаРубежомПоСтавке5,
	|	СведенияДляСоцВыплат.НалогСДивидендовУплаченныйЗаРубежомПоСтавке10 КАК НалогСДивидендовУплаченныйЗаРубежомПоСтавке10,
	|	СведенияДляСоцВыплат.НалогСДивидендовУплаченныйЗаРубежомПоСтавке3 КАК НалогСДивидендовУплаченныйЗаРубежомПоСтавке3,
	|	СведенияДляСоцВыплат.НалогСДивидендовУплаченныйЗаРубежомПоСтавке6 КАК НалогСДивидендовУплаченныйЗаРубежомПоСтавке6,
	|	СведенияДляСоцВыплат.НалогСДивидендовУплаченныйЗаРубежомПоСтавке7 КАК НалогСДивидендовУплаченныйЗаРубежомПоСтавке7,
	|	СведенияДляСоцВыплат.НалогСДивидендовУплаченныйЗаРубежомПоСтавке12 КАК НалогСДивидендовУплаченныйЗаРубежомПоСтавке12
	|ИЗ
	|	Документ.СведенияОДоходахСотрудникаДляСоцВыплат КАК СведенияДляСоцВыплат
	|ГДЕ
	|	СведенияДляСоцВыплат.Ссылка В(&Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СведенияДляСоцВыплатСведенияОДоходах.Ссылка КАК Ссылка,
	|	СведенияДляСоцВыплатСведенияОДоходах.НомерСтроки КАК НомерСтроки,
	|	СведенияДляСоцВыплатСведенияОДоходах.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	СведенияДляСоцВыплатСведенияОДоходах.КодДохода КАК КодДохода,
	|	СведенияДляСоцВыплатСведенияОДоходах.СуммаДохода КАК СуммаДохода,
	|	СведенияДляСоцВыплатСведенияОДоходах.КодВычета КАК КодВычета,
	|	СведенияДляСоцВыплатСведенияОДоходах.СуммаВычета КАК СуммаВычета,
	|	СведенияДляСоцВыплатСведенияОДоходах.Ссылка.Номер КАК НомерСправки,
	|	СведенияДляСоцВыплатСведенияОДоходах.Ссылка.Сотрудник КАК Сотрудник,
	|	СведенияДляСоцВыплатСведенияОДоходах.Ставка КАК Ставка
	|ИЗ
	|	Документ.СведенияОДоходахСотрудникаДляСоцВыплат.СведенияОДоходах КАК СведенияДляСоцВыплатСведенияОДоходах
	|ГДЕ
	|	СведенияДляСоцВыплатСведенияОДоходах.Ссылка В(&Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СведенияДляСоцВыплатСведенияОВычетах.Ссылка КАК Ссылка,
	|	СведенияДляСоцВыплатСведенияОВычетах.НомерСтроки КАК НомерСтроки,
	|	СведенияДляСоцВыплатСведенияОВычетах.КодВычета КАК КодВычета,
	|	СведенияДляСоцВыплатСведенияОВычетах.СуммаВычета КАК СуммаВычета,
	|	СведенияДляСоцВыплатСведенияОВычетах.Ссылка.Номер КАК НомерСправки,
	|	СведенияДляСоцВыплатСведенияОВычетах.Ссылка.Сотрудник КАК Сотрудник,
	|	СведенияДляСоцВыплатСведенияОВычетах.Ставка КАК Ставка
	|ИЗ
	|	Документ.СведенияОДоходахСотрудникаДляСоцВыплат.СведенияОВычетах КАК СведенияДляСоцВыплатСведенияОВычетах
	|ГДЕ
	|	СведенияДляСоцВыплатСведенияОВычетах.Ссылка В(&Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СведенияДляСоцВыплатУведомленияНОоПравеНаВычеты.Ссылка КАК Ссылка,
	|	СведенияДляСоцВыплатУведомленияНОоПравеНаВычеты.НомерСтроки КАК НомерСтроки,
	|	СведенияДляСоцВыплатУведомленияНОоПравеНаВычеты.Ссылка.Номер КАК НомерСправки,
	|	СведенияДляСоцВыплатУведомленияНОоПравеНаВычеты.Ссылка.Сотрудник КАК Сотрудник,
	|	СведенияДляСоцВыплатУведомленияНОоПравеНаВычеты.ДатаУведомления КАК ДатаУведомления,
	|	СведенияДляСоцВыплатУведомленияНОоПравеНаВычеты.НомерУведомления КАК НомерУведомления,
	|	СведенияДляСоцВыплатУведомленияНОоПравеНаВычеты.КодНалоговогоОрганаУведомления КАК КодНалоговогоОрганаУведомления,
	|	СведенияДляСоцВыплатУведомленияНОоПравеНаВычеты.ГруппаВычета КАК ГруппаВычета
	|ИЗ
	|	Документ.СведенияОДоходахСотрудникаДляСоцВыплат.УведомленияНОоПравеНаВычеты КАК СведенияДляСоцВыплатУведомленияНОоПравеНаВычеты
	|ГДЕ
	|	СведенияДляСоцВыплатУведомленияНОоПравеНаВычеты.Ссылка В(&Ссылка)";
	
	Результаты = Запрос.ВыполнитьПакет();
	
	Сотрудники = Результаты[0].Выгрузить();
	
	Для Каждого СтрокаТаблицы Из Сотрудники Цикл
		СтрокаТаблицы.НомерСправки = НомерСправкиБезПрефиксов(СтрокаТаблицы.НомерСправки);
	КонецЦикла;
	
	СведенияОДоходах = Результаты[1].Выгрузить();
	
	Для Каждого СтрокаТаблицы Из СведенияОДоходах Цикл
		СтрокаТаблицы.НомерСправки =  НомерСправкиБезПрефиксов(СтрокаТаблицы.НомерСправки);
	КонецЦикла;
	
	СведенияОВычетах = Результаты[2].Выгрузить();
	
	Для Каждого СтрокаТаблицы Из СведенияОВычетах Цикл
		СтрокаТаблицы.НомерСправки =  НомерСправкиБезПрефиксов(СтрокаТаблицы.НомерСправки);
	КонецЦикла;
	
	СведенияОбУведомлениях = Результаты[3].Выгрузить();
	Для Каждого СтрокаТаблицы Из СведенияОбУведомлениях Цикл
		СтрокаТаблицы.НомерСправки =  НомерСправкиБезПрефиксов(СтрокаТаблицы.НомерСправки);
	КонецЦикла;
	
	Возврат Новый Структура("Сотрудники, СведенияОДоходах, СведенияОВычетах, СведенияОбУведомлениях", Сотрудники, СведенияОДоходах, СведенияОВычетах, СведенияОбУведомлениях);
	
КонецФункции

Функция НомерСправкиБезПрефиксов(НомерДокумента)
	Возврат ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(НомерДокумента, Истина, Истина);
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли