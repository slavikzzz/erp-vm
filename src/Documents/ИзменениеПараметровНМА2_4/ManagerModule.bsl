#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	МеханизмыДокумента.Добавить("НематериальныеАктивы");
	МеханизмыДокумента.Добавить("РеестрДокументов");
	МеханизмыДокумента.Добавить("ОбесценениеВНА");
	МеханизмыДокумента.Добавить("ОборотныеРегистрыУправленческогоУчета");
	
	ИзменениеПараметровНМАЛокализация.ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента);
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка, ДокументОбъект - ссылка на документ или объект, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура:
//     * Ключ - Строка - Имя таблицы.
//     * Значение - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Если ТипЗнч(Документ) = Тип("ДокументОбъект.ИзменениеПараметровНМА2_4") Тогда
		ДокументСсылка = Документ.Ссылка;
	Иначе
		ДокументСсылка = Документ;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		
		ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДопПараметры);
		
		ТекстЗапросаТаблицаДокументыПоНМА(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры);
		
		ТекстЗапросаТаблицаПервоначальныеСведенияНМА(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПорядокУчетаНМА(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПорядокУчетаНМАБУ(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПорядокУчетаНМАУУ(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПараметрыАмортизацииНМАБУ(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПараметрыАмортизацииНМАУУ(ТекстыЗапроса, Регистры);
		
		//++ НЕ УТКА
		ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаОтражениеДокументовВМеждународномУчете(ТекстыЗапроса, Регистры);
		//-- НЕ УТКА
		
		ТекстЗапросаТаблицаСоставЕГДС(Запрос, ТекстыЗапроса, Регистры);
	
		ИзменениеПараметровНМАЛокализация.ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры);
	
	КонецЕсли;
	
	ТаблицыДвижений = ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
	Возврат ТаблицыДвижений;
	
КонецФункции

// Формирует таблицы движений при отложенном проведении.
//
// Параметры:
//  МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Содержит временные таблицы, которые используются при формировании движений.
//
// Возвращаемое значение:
//  см. ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения
//
Функция ТаблицыОтложенногоФормированияДвижений(МенеджерВременныхТаблиц) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;

	ПолучитьДанныеДокумента(Запрос);
	
	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения();
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла;
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	ТекстЗапросаТаблицаПервоначальныеСведенияНМА(ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПорядокУчетаНМА(ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПорядокУчетаНМАБУ(ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПорядокУчетаНМАУУ(ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПараметрыАмортизацииНМАБУ(ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПараметрыАмортизацииНМАУУ(ТекстыЗапроса, Неопределено);
	
	ТекстЗапросаТаблицаСтоимостьНМА(ТекстыЗапроса);
	ТекстЗапросаТаблицаАмортизацияНМА(ТекстыЗапроса);
	ТекстЗапросаДвиженияДоходыРасходыПрочиеАктивыПассивы(ТекстыЗапроса);
	
	//++ НЕ УТКА
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаОтражениеДокументовВМеждународномУчете(ТекстыЗапроса, Неопределено);
	//-- НЕ УТКА
	
	ИзменениеПараметровНМАЛокализация.ТаблицыОтложенногоФормированияДвижений(ТекстыЗапроса);

	ТаблицыДвижений = ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса);
	
	Возврат ТаблицыДвижений;
	
КонецФункции

#КонецОбласти

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Документы.КорректировкаСтоимостиИАмортизацииНМА.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	ИзменениеПараметровНМАЛокализация.ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры);

КонецПроцедуры

// Добавляет команду создания документа "Изменение параметров НМА".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
// Возвращаемое значение:
//	СтрокаТаблицыЗначений - добавленная команда
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт

	Если ПравоДоступа("Добавление", Метаданные.Документы.ИзменениеПараметровНМА2_4) Тогда
		
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.ИзменениеПараметровНМА2_4.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.ИзменениеПараметровНМА2_4);
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьВнеоборотныеАктивы2_4";
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	ИзменениеПараметровНМАЛокализация.ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры);

КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	ИзменениеПараметровНМАЛокализация.ДобавитьКомандыПечати(КомандыПечати);

КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.ИзменениеПараметровНМА2_4";
	
	ЗначенияПараметров = ЗначенияПараметровПроведения();
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("НомерНаПечать", """""");
	
	ВЗапросеЕстьИсточник = Истина;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';
								|en = 'In document %ПолноеИмяДокумента%, adaptation of request for generating records of register %ИмяРегистра% is not implemented.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ВЗапросеЕстьИсточник,
										ПереопределениеРасчетаПараметров);
	Иначе
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ПереопределениеРасчетаПараметров);
	КонецЕсли;
	
	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДопПараметры)
	
	ПроведениеДокументов.ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДопПараметры);
	
	СформироватьТаблицуВтСписокДокументов(Запрос);
	ПолучитьДанныеДокумента(Запрос);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Период  КАК Период,
	|	ДанныеДокумента.Номер   КАК Номер
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения(Реквизиты);
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла; 
	
КонецПроцедуры

Процедура СформироватьТаблицуВтСписокДокументов(Запрос)
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СписокДокументов.Ссылка КАК Ссылка,
	|	СписокДокументов.Организация КАК Организация,
	|	СписокДокументов.Дата КАК Период
	|
	|ПОМЕСТИТЬ ВтСписокДокументов
	|
	|ИЗ
	|	Документ.ИзменениеПараметровНМА2_4 КАК СписокДокументов
	|
	|ГДЕ
	|	СписокДокументов.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|";
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПолучитьДанныеДокумента(Запрос)
	
	СписокЗапросов = Новый Массив;
	
	#Область ДанныеДокументаРеквизиты
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.ПометкаУдаления КАК ПометкаУдаления,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.Проведен КАК Проведен,
	|	ДанныеДокумента.ОтражатьВУпрУчете КАК ОтражатьВУпрУчете,
	|	ДанныеДокумента.ОтражатьВРеглУчете КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВБУ КАК ОтражатьВБУ,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Организация.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ДанныеДокумента.Организация.ВалютаРегламентированногоУчета КАК ВалютаРегл,
	|	ДанныеДокумента.ПорядокУчетаБУ КАК ПорядокУчетаБУ,
	|	ДанныеДокумента.ПорядокУчетаБУФлаг КАК ПорядокУчетаБУФлаг,
	|	ДанныеДокумента.ПорядокУчетаУУ КАК ПорядокУчетаУУ,
	|	ДанныеДокумента.ПорядокУчетаУУФлаг КАК ПорядокУчетаУУФлаг,
	|	ДанныеДокумента.МетодНачисленияАмортизацииБУ КАК МетодНачисленияАмортизацииБУ,
	|	ДанныеДокумента.МетодНачисленияАмортизацииБУФлаг КАК МетодНачисленияАмортизацииБУФлаг,
	|	ДанныеДокумента.МетодНачисленияАмортизацииУУ КАК МетодНачисленияАмортизацииУУ,
	|	ДанныеДокумента.МетодНачисленияАмортизацииУУФлаг КАК МетодНачисленияАмортизацииУУФлаг,
	|	ДанныеДокумента.СрокИспользованияБУ КАК СрокИспользованияБУ,
	|	ДанныеДокумента.СрокИспользованияБУФлаг КАК СрокИспользованияБУФлаг,
	|	ДанныеДокумента.СрокИспользованияУУ КАК СрокИспользованияУУ,
	|	ДанныеДокумента.СрокИспользованияУУФлаг КАК СрокИспользованияУУФлаг,
	|	ДанныеДокумента.ОбъемНаработки КАК ОбъемНаработки,
	|	ДанныеДокумента.ОбъемНаработкиФлаг КАК ОбъемНаработкиФлаг,
	|	ДанныеДокумента.КоэффициентБУ КАК КоэффициентБУ,
	|	ДанныеДокумента.КоэффициентБУФлаг КАК КоэффициентБУФлаг,
	|	ДанныеДокумента.КоэффициентУскоренияУУ КАК КоэффициентУскоренияУУ,
	|	ДанныеДокумента.КоэффициентУскоренияУУФлаг КАК КоэффициентУскоренияУУФлаг,
	|	ДанныеДокумента.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость,
	|	ДанныеДокумента.ЛиквидационнаяСтоимостьФлаг КАК ЛиквидационнаяСтоимостьФлаг,
	|	ДанныеДокумента.ЛиквидационнаяСтоимостьРегл КАК ЛиквидационнаяСтоимостьРегл,
	|	ДанныеДокумента.ЛиквидационнаяСтоимостьРеглФлаг КАК ЛиквидационнаяСтоимостьРеглФлаг,
	|	ДанныеДокумента.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	ДанныеДокумента.ГруппаФинансовогоУчетаФлаг КАК ГруппаФинансовогоУчетаФлаг,
	|	ДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ДанныеДокумента.НаправлениеДеятельностиФлаг КАК НаправлениеДеятельностиФлаг,
	|	ДанныеДокумента.АмортизационныеРасходыФлаг КАК АмортизационныеРасходыФлаг,
	|	ДанныеДокумента.Ответственный КАК Ответственный,
	|	ДанныеДокумента.Комментарий КАК Комментарий,
	|	ДанныеДокумента.ЕГДСФлаг КАК ЕГДСФлаг,
	|	ДанныеДокумента.ЕГДС КАК ЕГДС,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИзменениеПараметровНМА) КАК ХозяйственнаяОперация,
	|
	|	&ИзменениеПараметровНМА_РеквизитыДокумента
	|
	|ПОМЕСТИТЬ ДанныеДокументаРеквизиты
	|
	|ИЗ
	|	Документ.ИзменениеПараметровНМА2_4 КАК ДанныеДокумента
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка В (
	|		ВЫБРАТЬ
	|			ВтСписокДокументов.Ссылка
	|		ИЗ
	|			ВтСписокДокументов КАК ВтСписокДокументов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|";
	ИзменениеПараметровНМАЛокализация.ДобавитьВТекстЗапросаРеквизитыДокумента(ТекстЗапроса, "ДанныеДокумента");
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ДанныеДокументаТаблицаНМА
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаДокумента.НематериальныйАктив КАК ОбъектУчета,
	|	ТаблицаДокумента.НематериальныйАктив КАК НематериальныйАктив
	|
	|ПОМЕСТИТЬ ДанныеДокументаТаблицаНМА
	|
	|ИЗ
	|	Документ.ИзменениеПараметровНМА2_4.НМА КАК ТаблицаДокумента
	|
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В (
	|		ВЫБРАТЬ
	|			ВтСписокДокументов.Ссылка
	|		ИЗ
	|			ВтСписокДокументов КАК ВтСписокДокументов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	НематериальныйАктив
	|";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	Запрос.Текст = СтрСоединить(СписокЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)
	
	ЗначенияПараметровПроведения = Новый Структура;
	ЗначенияПараметровПроведения.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ИзменениеПараметровНМА2_4"));
	ЗначенияПараметровПроведения.Вставить("СтатьяАП_НМА", ПланыВидовХарактеристик.СтатьиАктивовПассивов.НематериальныеАктивы);
	ЗначенияПараметровПроведения.Вставить("НазваниеДокумента", НСтр("ru = 'Изменение параметров НМА';
																	|en = 'Adjust intangible asset financial details'"));
	ЗначенияПараметровПроведения.Вставить("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ИзменениеПараметровНМА);
	ЗначенияПараметровПроведения.Вставить("ДатаНачалаУчета", ВнеоборотныеАктивыЛокализация.ДатаНачалаУчетаВнеоборотныхАктивов2_4());
	ЗначенияПараметровПроведения.Вставить("ВедетсяРегламентированныйУчетВНА", ВнеоборотныеАктивыСлужебный.ВедетсяРегламентированныйУчетВНА());
	ЗначенияПараметровПроведения.Вставить("ВалютаУпр", Константы.ВалютаУправленческогоУчета.Получить());
	
	Если Реквизиты <> Неопределено Тогда
		
		ЗначенияПараметровПроведения.Вставить("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
		
	КонецЕсли;
	
	Возврат ЗначенияПараметровПроведения;
	
КонецФункции

Функция ТекстЗапросаТаблицаДокументыПоНМА(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДокументыПоНМА";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(ТаблицаНМА.НомерСтроки-1, 0) КАК НомерЗаписи,
	|	ДанныеДокумента.Ссылка                КАК Ссылка,
	|	ТаблицаНМА.НематериальныйАктив        КАК НематериальныйАктив,
	|	&ХозяйственнаяОперация                КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация           КАК Организация,
	|	НЕОПРЕДЕЛЕНО                          КАК Подразделение,
	|	ДанныеДокумента.Дата                  КАК Дата,
	|	&ИдентификаторМетаданных              КАК ТипСсылки,
	|	ДанныеДокумента.Проведен              КАК Проведен,
	|	ДанныеДокумента.ОтражатьВУпрУчете     КАК ОтражатьВУпрУчете,
	|	ДанныеДокумента.ОтражатьВРеглУчете    КАК ОтражатьВРеглУчете
	|
	|ИЗ
	|	Документ.ИзменениеПараметровНМА2_4 КАК ДанныеДокумента
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзменениеПараметровНМА2_4.НМА КАК ТаблицаНМА
	|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка         КАК Ссылка,
	|	ДанныеДокумента.Дата           КАК ДатаДокументаИБ,
	|	ДанныеДокумента.Номер          КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных       КАК ТипСсылки,
	|	ДанныеДокумента.Организация    КАК Организация,
	|	&ХозяйственнаяОперация         КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                   КАК НаправлениеДеятельности,
	|	ДанныеДокумента.Ответственный  КАК Ответственный,
	|	ДанныеДокумента.Комментарий    КАК Комментарий,
	|	ДанныеДокумента.Проведен       КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления КАК ПометкаУдаления,
	|	ЛОЖЬ                           КАК ДополнительнаяЗапись,
	|	ДанныеДокумента.Дата           КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать                 КАК НомерПервичногоДокумента,
	|	ЛОЖЬ                           КАК СторноИсправление,
	|	НЕОПРЕДЕЛЕНО                   КАК СторнируемыйДокумент,
	|	НЕОПРЕДЕЛЕНО                   КАК ИсправляемыйДокумент,
	|	ДанныеДокумента.Дата           КАК ДатаОтраженияВУчете,
	|	НЕОПРЕДЕЛЕНО                   КАК Приоритет
	|
	|ИЗ
	|	Документ.ИзменениеПараметровНМА2_4 КАК ДанныеДокумента
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ТекстЗапросаТаблицаПервоначальныеСведенияНМА(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПервоначальныеСведенияНМА";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПервоначальныеСведенияНМА(ТекстыЗапроса);
	
	ТекстЗапроса = ИзменениеПараметровНМАЛокализация.ТекстЗапросаТаблицаПервоначальныеСведенияНМА();
	
	Если ТекстЗапроса = Неопределено Тогда
		
		ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтУчетнаяПолитикаФинансовогоУчета(ТекстыЗапроса);

		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка КАК Регистратор,
		|	ДанныеДокумента.Период КАК Период,
		|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
		|	ДанныеДокумента.Организация КАК Организация,
		|	втПервоначальныеСведенияНМА.СпособПоступления КАК СпособПоступления,
		|	втПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьУУ КАК ПервоначальнаяСтоимостьУУ,
		|	втПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьБУ КАК ПервоначальнаяСтоимостьБУ,
		|	ДанныеДокумента.МетодНачисленияАмортизацииБУ КАК МетодНачисленияАмортизацииБУ,
		|	втПервоначальныеСведенияНМА.ДатаПриобретения КАК ДатаПриобретения,
		|	втПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ КАК ДатаПринятияКУчетуУУ,
		|	втПервоначальныеСведенияНМА.ДатаПринятияКУчетуБУ КАК ДатаПринятияКУчетуБУ,
		|	втПервоначальныеСведенияНМА.ДокументПринятияКУчетуУУ КАК ДокументПринятияКУчетуУУ,
		|	втПервоначальныеСведенияНМА.ДокументПринятияКУчетуБУ КАК ДокументПринятияКУчетуБУ,
		|	втПервоначальныеСведенияНМА.ДокументСписания КАК ДокументСписания,
		|	втПервоначальныеСведенияНМА.РезервПереоценкиСтоимости КАК РезервПереоценкиСтоимости,
		|	втПервоначальныеСведенияНМА.РезервПереоценкиАмортизации КАК РезервПереоценкиАмортизации
		|ИЗ
		|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаНМА КАК ТаблицаНМА
		|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ втПервоначальныеСведенияНМА КАК втПервоначальныеСведенияНМА
		|		ПО втПервоначальныеСведенияНМА.Ссылка = ТаблицаНМА.Ссылка
		|			И втПервоначальныеСведенияНМА.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтУчетнаяПолитикаФинансовогоУчета КАК ВтУчетнаяПолитикаФинансовогоУчета
		|		ПО ВтУчетнаяПолитикаФинансовогоУчета.Ссылка = ДанныеДокумента.Ссылка
		|
		|ГДЕ
		|	ЕСТЬNULL(ВтУчетнаяПолитикаФинансовогоУчета.ПорядокУчетаВНАРегл, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАРегл.Независимый)
		|	И ДанныеДокумента.ОтражатьВБУ
		|	И ДанныеДокумента.МетодНачисленияАмортизацииБУФлаг
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка КАК Регистратор,
		|	ДанныеДокумента.Период КАК Период,
		|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
		|	ДанныеДокумента.Организация КАК Организация,
		|	втПервоначальныеСведенияНМА.СпособПоступления КАК СпособПоступления,
		|	втПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьУУ КАК ПервоначальнаяСтоимостьУУ,
		|	втПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьБУ КАК ПервоначальнаяСтоимостьБУ,
		|	ДанныеДокумента.МетодНачисленияАмортизацииУУ КАК МетодНачисленияАмортизацииБУ,
		|	втПервоначальныеСведенияНМА.ДатаПриобретения КАК ДатаПриобретения,
		|	втПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ КАК ДатаПринятияКУчетуУУ,
		|	втПервоначальныеСведенияНМА.ДатаПринятияКУчетуБУ КАК ДатаПринятияКУчетуБУ,
		|	втПервоначальныеСведенияНМА.ДокументПринятияКУчетуУУ КАК ДокументПринятияКУчетуУУ,
		|	втПервоначальныеСведенияНМА.ДокументПринятияКУчетуБУ КАК ДокументПринятияКУчетуБУ,
		|	втПервоначальныеСведенияНМА.ДокументСписания КАК ДокументСписания,
		|	втПервоначальныеСведенияНМА.РезервПереоценкиСтоимости КАК РезервПереоценкиСтоимости,
		|	втПервоначальныеСведенияНМА.РезервПереоценкиАмортизации КАК РезервПереоценкиАмортизации
		|
		|ИЗ
		|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаНМА КАК ТаблицаНМА
		|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ втПервоначальныеСведенияНМА КАК втПервоначальныеСведенияНМА
		|		ПО втПервоначальныеСведенияНМА.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтУчетнаяПолитикаФинансовогоУчета КАК ВтУчетнаяПолитикаФинансовогоУчета
		|		ПО ВтУчетнаяПолитикаФинансовогоУчета.Ссылка = ДанныеДокумента.Ссылка
		|
		|ГДЕ
		|	ЕСТЬNULL(ВтУчетнаяПолитикаФинансовогоУчета.ПорядокУчетаВНАРегл, НЕОПРЕДЕЛЕНО) <> ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАРегл.Независимый)
		|	И ДанныеДокумента.МетодНачисленияАмортизацииУУФлаг
		|";
		
	КонецЕсли;
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПорядокУчетаНМА(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПорядокУчетаНМА";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПорядокУчетаНМА(ТекстыЗапроса);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Регистратор,
	|	ДанныеДокумента.Период КАК Период,
	|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	ДанныеДокумента.Организация КАК Организация,
	|	
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ГруппаФинансовогоУчетаФлаг
	|			ТОГДА ДанныеДокумента.ГруппаФинансовогоУчета
	|		ИНАЧЕ ПорядокУчетаНМА.ГруппаФинансовогоУчета
	|	КОНЕЦ КАК ГруппаФинансовогоУчета,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.НаправлениеДеятельностиФлаг
	|			ТОГДА ДанныеДокумента.НаправлениеДеятельности
	|		ИНАЧЕ ПорядокУчетаНМА.НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОбъемНаработкиФлаг
	|			ТОГДА ДанныеДокумента.ОбъемНаработки
	|		ИНАЧЕ ПорядокУчетаНМА.ОбъемНаработки
	|	КОНЕЦ КАК ОбъемНаработки,
	|	
	|	ДанныеДокумента.ОтражатьВУпрУчете КАК ОтражатьВУпрУчете,
	|	ДанныеДокумента.ОтражатьВРеглУчете КАК ОтражатьВРеглУчете
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаНМА КАК ТаблицаНМА
	|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаНМА КАК ПорядокУчетаНМА
	|		ПО ПорядокУчетаНМА.Ссылка = ТаблицаНМА.Ссылка
	|			И ПорядокУчетаНМА.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
	|
	|ГДЕ
	|	(ДанныеДокумента.ГруппаФинансовогоУчетаФлаг
	|		ИЛИ ДанныеДокумента.НаправлениеДеятельностиФлаг
	|		ИЛИ ДанныеДокумента.ОбъемНаработкиФлаг)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПорядокУчетаНМАБУ(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПорядокУчетаНМАБУ";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПорядокУчетаНМАБУ(ТекстыЗапроса);
	
	ТекстЗапроса = ИзменениеПараметровНМАЛокализация.ТекстЗапросаТаблицаПорядокУчетаНМАБУ();
	
	Если ТекстЗапроса = Неопределено Тогда
		
		ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтУчетнаяПолитикаФинансовогоУчета(ТекстыЗапроса);
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка КАК Регистратор,
		|	ДанныеДокумента.Период КАК Период,
		|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
		|	ДанныеДокумента.Организация КАК Организация,
		|	
		|	втПорядокУчетаНМАБУ.СостояниеБУ КАК СостояниеБУ,
		|
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.ПорядокУчетаБУФлаг
		|			ТОГДА ДанныеДокумента.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НачислятьАмортизацию)
		|		ИНАЧЕ втПорядокУчетаНМАБУ.НачислятьАмортизациюБУ
		|	КОНЕЦ КАК НачислятьАмортизациюБУ,
		|
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.АмортизационныеРасходыФлаг
		|			ТОГДА ДанныеДокумента.Ссылка
		|		ИНАЧЕ втПорядокУчетаНМАБУ.СпособОтраженияРасходовБУ
		|	КОНЕЦ КАК СпособОтраженияРасходовБУ,
		|
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.АмортизационныеРасходыФлаг
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ втПорядокУчетаНМАБУ.СтатьяРасходовБУ
		|	КОНЕЦ КАК СтатьяРасходовБУ,
		|
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.АмортизационныеРасходыФлаг
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ втПорядокУчетаНМАБУ.АналитикаРасходовБУ
		|	КОНЕЦ КАК АналитикаРасходовБУ
		|
		|ИЗ
		|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаНМА КАК ТаблицаНМА
		|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаНМАБУ КАК втПорядокУчетаНМАБУ
		|		ПО втПорядокУчетаНМАБУ.Ссылка = ТаблицаНМА.Ссылка
		|			И втПорядокУчетаНМАБУ.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтУчетнаяПолитикаФинансовогоУчета КАК ВтУчетнаяПолитикаФинансовогоУчета
		|		ПО ВтУчетнаяПолитикаФинансовогоУчета.Ссылка = ДанныеДокумента.Ссылка
		|
		|ГДЕ
		|	ЕСТЬNULL(ВтУчетнаяПолитикаФинансовогоУчета.ПорядокУчетаВНАРегл, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАРегл.Независимый)
		|	И ДанныеДокумента.ОтражатьВБУ
		|	И (ДанныеДокумента.ПорядокУчетаБУФлаг
		|		ИЛИ ДанныеДокумента.АмортизационныеРасходыФлаг)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка КАК Регистратор,
		|	ДанныеДокумента.Период КАК Период,
		|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
		|	ДанныеДокумента.Организация КАК Организация,
		|	
		|	втПорядокУчетаНМАБУ.СостояниеБУ КАК СостояниеБУ,
		|
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.ПорядокУчетаУУФлаг
		|			ТОГДА ДанныеДокумента.ПорядокУчетаУУ = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НачислятьАмортизацию)
		|		ИНАЧЕ втПорядокУчетаНМАБУ.НачислятьАмортизациюБУ
		|	КОНЕЦ КАК НачислятьАмортизациюБУ,
		|
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.АмортизационныеРасходыФлаг
		|			ТОГДА ДанныеДокумента.Ссылка
		|		ИНАЧЕ втПорядокУчетаНМАБУ.СпособОтраженияРасходовБУ
		|	КОНЕЦ КАК СпособОтраженияРасходовБУ,
		|
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.АмортизационныеРасходыФлаг
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ втПорядокУчетаНМАБУ.СтатьяРасходовБУ
		|	КОНЕЦ КАК СтатьяРасходовБУ,
		|
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.АмортизационныеРасходыФлаг
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ втПорядокУчетаНМАБУ.АналитикаРасходовБУ
		|	КОНЕЦ КАК АналитикаРасходовБУ
		|
		|ИЗ
		|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаНМА КАК ТаблицаНМА
		|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаНМАБУ КАК втПорядокУчетаНМАБУ
		|		ПО втПорядокУчетаНМАБУ.Ссылка = ТаблицаНМА.Ссылка
		|			И втПорядокУчетаНМАБУ.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтУчетнаяПолитикаФинансовогоУчета КАК ВтУчетнаяПолитикаФинансовогоУчета
		|		ПО ВтУчетнаяПолитикаФинансовогоУчета.Ссылка = ДанныеДокумента.Ссылка
		|
		|ГДЕ
		|	ЕСТЬNULL(ВтУчетнаяПолитикаФинансовогоУчета.ПорядокУчетаВНАРегл, НЕОПРЕДЕЛЕНО) <> ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАРегл.Независимый)
		|	И (ДанныеДокумента.ПорядокУчетаУУФлаг
		|		ИЛИ ДанныеДокумента.АмортизационныеРасходыФлаг)
		|";
		
	КонецЕсли;
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПорядокУчетаНМАУУ(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПорядокУчетаНМАУУ";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПорядокУчетаНМАУУ(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Регистратор,
	|	ДанныеДокумента.Период КАК Период,
	|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	ДанныеДокумента.Организация КАК Организация,
	|	
	|	втПорядокУчетаНМАУУ.Состояние КАК Состояние,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ПорядокУчетаУУФлаг
	|			ТОГДА ДанныеДокумента.ПорядокУчетаУУ = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НачислятьАмортизацию)
	|		ИНАЧЕ втПорядокУчетаНМАУУ.НачислятьАмортизациюУУ
	|	КОНЕЦ КАК НачислятьАмортизациюУУ,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.АмортизационныеРасходыФлаг
	|			ТОГДА ДанныеДокумента.Ссылка
	|		ИНАЧЕ втПорядокУчетаНМАУУ.СпособОтраженияРасходов
	|	КОНЕЦ КАК СпособОтраженияРасходов,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.АмортизационныеРасходыФлаг
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ втПорядокУчетаНМАУУ.СтатьяРасходов
	|	КОНЕЦ КАК СтатьяРасходов,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.АмортизационныеРасходыФлаг
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ втПорядокУчетаНМАУУ.АналитикаРасходов
	|	КОНЕЦ КАК АналитикаРасходов,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.АмортизационныеРасходыФлаг
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ втПорядокУчетаНМАУУ.ПередаватьРасходыВДругуюОрганизацию
	|	КОНЕЦ КАК ПередаватьРасходыВДругуюОрганизацию,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.АмортизационныеРасходыФлаг
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|		ИНАЧЕ втПорядокУчетаНМАУУ.ОрганизацияПолучательРасходов
	|	КОНЕЦ КАК ОрганизацияПолучательРасходов
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаНМА КАК ТаблицаНМА
	|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаНМАУУ КАК втПорядокУчетаНМАУУ
	|		ПО втПорядокУчетаНМАУУ.Ссылка = ТаблицаНМА.Ссылка
	|			И втПорядокУчетаНМАУУ.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
	|
	|ГДЕ
	|	ДанныеДокумента.ОтражатьВУпрУчете
	|	И (ДанныеДокумента.ПорядокУчетаУУФлаг
	|		ИЛИ ДанныеДокумента.АмортизационныеРасходыФлаг)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПараметрыАмортизацииНМАБУ(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПараметрыАмортизацииНМАБУ";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыАмортизацииНМАБУ(ТекстыЗапроса);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПорядокУчетаНМА(ТекстыЗапроса);
	
	ТекстЗапроса = ИзменениеПараметровНМАЛокализация.ТекстЗапросаТаблицаПараметрыАмортизацииНМАБУ();
	
	Если ТекстЗапроса = Неопределено Тогда
		
		ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтУчетнаяПолитикаФинансовогоУчета(ТекстыЗапроса);

		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка КАК Регистратор,
		|	ДанныеДокумента.Период КАК Период,
		|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
		|	ДанныеДокумента.Организация КАК Организация,
		|
		|	втПараметрыАмортизацииНМАБУ.ДатаПоследнегоИзменения КАК ДатаПоследнегоИзменения,
		|
		// СрокИспользованияДляВычисленияАмортизации 
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.СрокИспользованияБУФлаг И втПараметрыАмортизацииНМАБУ.НематериальныйАктив ЕСТЬ NULL
		|			ТОГДА ДанныеДокумента.СрокИспользованияБУ
		|		КОГДА ДанныеДокумента.СрокИспользованияБУФлаг
		|				И втПараметрыАмортизацииНМАБУ.СрокПолезногоИспользованияБУ = втПараметрыАмортизацииНМАБУ.СрокИспользованияДляВычисленияАмортизации
		|			ТОГДА ДанныеДокумента.СрокИспользованияБУ
		|		КОГДА ДанныеДокумента.СрокИспользованияБУФлаг
		|			ТОГДА втПараметрыАмортизацииНМАБУ.СрокИспользованияДляВычисленияАмортизации + (ДанныеДокумента.СрокИспользованияБУ - втПараметрыАмортизацииНМАБУ.СрокПолезногоИспользованияБУ)
		|		ИНАЧЕ втПараметрыАмортизацииНМАБУ.СрокИспользованияДляВычисленияАмортизации
		|	КОНЕЦ КАК СрокИспользованияДляВычисленияАмортизации,
		|
		// ОбъемПродукцииРаботДляВычисленияАмортизации
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.ОбъемНаработкиФлаг И втПараметрыАмортизацииНМАБУ.НематериальныйАктив ЕСТЬ NULL
		|			ТОГДА ДанныеДокумента.ОбъемНаработки
		|		КОГДА ДанныеДокумента.ОбъемНаработкиФлаг
		|				И втПорядокУчетаНМА.ОбъемНаработки = втПараметрыАмортизацииНМАБУ.ОбъемПродукцииРаботДляВычисленияАмортизации
		|			ТОГДА ДанныеДокумента.ОбъемНаработки
		|		КОГДА ДанныеДокумента.ОбъемНаработкиФлаг
		|			ТОГДА втПараметрыАмортизацииНМАБУ.ОбъемПродукцииРаботДляВычисленияАмортизации + (ДанныеДокумента.ОбъемНаработки - втПорядокУчетаНМА.ОбъемНаработки)
		|		ИНАЧЕ втПараметрыАмортизацииНМАБУ.ОбъемПродукцииРаботДляВычисленияАмортизации
		|	КОНЕЦ КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
		|
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.СрокИспользованияБУФлаг
		|			ТОГДА ДанныеДокумента.СрокИспользованияБУ
		|		ИНАЧЕ втПараметрыАмортизацииНМАБУ.СрокПолезногоИспользованияБУ
		|	КОНЕЦ КАК СрокПолезногоИспользованияБУ,
		|
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.КоэффициентБУФлаг
		|			ТОГДА ДанныеДокумента.КоэффициентБУ
		|		ИНАЧЕ втПараметрыАмортизацииНМАБУ.КоэффициентБУ
		|	КОНЕЦ КАК КоэффициентБУ,
		|
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.ЛиквидационнаяСтоимостьРеглФлаг
		|			ТОГДА ДанныеДокумента.ЛиквидационнаяСтоимостьРегл
		|		ИНАЧЕ втПараметрыАмортизацииНМАБУ.ЛиквидационнаяСтоимость
		|	КОНЕЦ КАК ЛиквидационнаяСтоимость
		|
		|ИЗ
		|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаНМА КАК ТаблицаНМА
		|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПараметрыАмортизацииНМАБУ КАК втПараметрыАмортизацииНМАБУ
		|		ПО втПараметрыАмортизацииНМАБУ.Ссылка = ТаблицаНМА.Ссылка
		|			И втПараметрыАмортизацииНМАБУ.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаНМА КАК втПорядокУчетаНМА
		|		ПО втПорядокУчетаНМА.Ссылка = ТаблицаНМА.Ссылка
		|			И втПорядокУчетаНМА.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтУчетнаяПолитикаФинансовогоУчета КАК ВтУчетнаяПолитикаФинансовогоУчета
		|		ПО ВтУчетнаяПолитикаФинансовогоУчета.Ссылка = ДанныеДокумента.Ссылка
		|
		|ГДЕ
		|	ЕСТЬNULL(ВтУчетнаяПолитикаФинансовогоУчета.ПорядокУчетаВНАРегл, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАРегл.Независимый)
		|	И ДанныеДокумента.ОтражатьВРеглУчете
		|	И (ДанныеДокумента.КоэффициентБУФлаг
		|		ИЛИ ДанныеДокумента.СрокИспользованияБУФлаг
		|		ИЛИ ДанныеДокумента.ЛиквидационнаяСтоимостьРеглФлаг
		|		ИЛИ ДанныеДокумента.ОбъемНаработкиФлаг)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка КАК Регистратор,
		|	ДанныеДокумента.Период КАК Период,
		|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
		|	ДанныеДокумента.Организация КАК Организация,
		|
		|	втПараметрыАмортизацииНМАБУ.ДатаПоследнегоИзменения КАК ДатаПоследнегоИзменения,
		|
		// СрокИспользованияДляВычисленияАмортизации 
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.СрокИспользованияУУФлаг И втПараметрыАмортизацииНМАБУ.НематериальныйАктив ЕСТЬ NULL
		|			ТОГДА ДанныеДокумента.СрокИспользованияУУ
		|		КОГДА ДанныеДокумента.СрокИспользованияУУФлаг
		|				И втПараметрыАмортизацииНМАБУ.СрокПолезногоИспользованияБУ = втПараметрыАмортизацииНМАБУ.СрокИспользованияДляВычисленияАмортизации
		|			ТОГДА ДанныеДокумента.СрокИспользованияУУ
		|		КОГДА ДанныеДокумента.СрокИспользованияУУФлаг
		|			ТОГДА втПараметрыАмортизацииНМАБУ.СрокИспользованияДляВычисленияАмортизации + (ДанныеДокумента.СрокИспользованияУУ - втПараметрыАмортизацииНМАБУ.СрокПолезногоИспользованияБУ)
		|		ИНАЧЕ втПараметрыАмортизацииНМАБУ.СрокИспользованияДляВычисленияАмортизации
		|	КОНЕЦ КАК СрокИспользованияДляВычисленияАмортизации,
		|
		// ОбъемПродукцииРаботДляВычисленияАмортизации
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.ОбъемНаработкиФлаг И втПараметрыАмортизацииНМАБУ.НематериальныйАктив ЕСТЬ NULL
		|			ТОГДА ДанныеДокумента.ОбъемНаработки
		|		КОГДА ДанныеДокумента.ОбъемНаработкиФлаг
		|				И втПорядокУчетаНМА.ОбъемНаработки = втПараметрыАмортизацииНМАБУ.ОбъемПродукцииРаботДляВычисленияАмортизации
		|			ТОГДА ДанныеДокумента.ОбъемНаработки
		|		КОГДА ДанныеДокумента.ОбъемНаработкиФлаг
		|			ТОГДА втПараметрыАмортизацииНМАБУ.ОбъемПродукцииРаботДляВычисленияАмортизации + (ДанныеДокумента.ОбъемНаработки - втПорядокУчетаНМА.ОбъемНаработки)
		|		ИНАЧЕ втПараметрыАмортизацииНМАБУ.ОбъемПродукцииРаботДляВычисленияАмортизации
		|	КОНЕЦ КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
		|
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.СрокИспользованияУУФлаг
		|			ТОГДА ДанныеДокумента.СрокИспользованияУУ
		|		ИНАЧЕ втПараметрыАмортизацииНМАБУ.СрокПолезногоИспользованияБУ
		|	КОНЕЦ КАК СрокПолезногоИспользованияБУ,
		|
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.КоэффициентУскоренияУУФлаг
		|			ТОГДА ДанныеДокумента.КоэффициентУскоренияУУ
		|		ИНАЧЕ втПараметрыАмортизацииНМАБУ.КоэффициентБУ
		|	КОНЕЦ КАК КоэффициентБУ,
		|
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.ЛиквидационнаяСтоимостьРеглФлаг
		|			ТОГДА ДанныеДокумента.ЛиквидационнаяСтоимостьРегл
		|		ИНАЧЕ втПараметрыАмортизацииНМАБУ.ЛиквидационнаяСтоимость
		|	КОНЕЦ КАК ЛиквидационнаяСтоимость
		|
		|ИЗ
		|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаНМА КАК ТаблицаНМА
		|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПараметрыАмортизацииНМАБУ КАК втПараметрыАмортизацииНМАБУ
		|		ПО втПараметрыАмортизацииНМАБУ.Ссылка = ТаблицаНМА.Ссылка
		|			И втПараметрыАмортизацииНМАБУ.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаНМА КАК втПорядокУчетаНМА
		|		ПО втПорядокУчетаНМА.Ссылка = ТаблицаНМА.Ссылка
		|			И втПорядокУчетаНМА.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтУчетнаяПолитикаФинансовогоУчета КАК ВтУчетнаяПолитикаФинансовогоУчета
		|		ПО ВтУчетнаяПолитикаФинансовогоУчета.Ссылка = ДанныеДокумента.Ссылка
		|
		|ГДЕ
		|	ЕСТЬNULL(ВтУчетнаяПолитикаФинансовогоУчета.ПорядокУчетаВНАРегл, НЕОПРЕДЕЛЕНО) <> ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАРегл.Независимый)
		|	И (ДанныеДокумента.КоэффициентУскоренияУУФлаг
		|		ИЛИ ДанныеДокумента.СрокИспользованияУУФлаг
		|		ИЛИ ДанныеДокумента.ЛиквидационнаяСтоимостьРеглФлаг
		|		ИЛИ ДанныеДокумента.ОбъемНаработкиФлаг)
		|";
		
	КонецЕсли;
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПараметрыАмортизацииНМАУУ(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПараметрыАмортизацииНМАУУ";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыАмортизацииНМАУУ(ТекстыЗапроса);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПорядокУчетаНМА(ТекстыЗапроса);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтУчетнаяПолитикаФинансовогоУчета(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Регистратор,
	|	ДанныеДокумента.Период КАК Период,
	|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	ДанныеДокумента.Организация КАК Организация,
	|
	|	ЕСТЬNULL(ПараметрыАмортизацииНМАУУ.ДатаПоследнегоИзменения, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаПоследнегоИзменения,
	|	
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.СрокИспользованияУУФлаг
	|			ТОГДА ДанныеДокумента.СрокИспользованияУУ
	|		ИНАЧЕ ЕСТЬNULL(ПараметрыАмортизацииНМАУУ.СрокИспользования, 0)
	|	КОНЕЦ КАК СрокИспользования,
	|
	// СрокИспользованияДляВычисленияАмортизации 
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.СрокИспользованияУУФлаг И ПараметрыАмортизацииНМАУУ.НематериальныйАктив ЕСТЬ NULL
	|			ТОГДА ДанныеДокумента.СрокИспользованияУУ
	|		КОГДА ДанныеДокумента.СрокИспользованияУУФлаг
	|				И ПараметрыАмортизацииНМАУУ.СрокИспользования = ПараметрыАмортизацииНМАУУ.СрокИспользованияДляВычисленияАмортизации
	|			ТОГДА ДанныеДокумента.СрокИспользованияУУ
	|		КОГДА ДанныеДокумента.СрокИспользованияУУФлаг
	|			ТОГДА ПараметрыАмортизацииНМАУУ.СрокИспользованияДляВычисленияАмортизации + (ДанныеДокумента.СрокИспользованияУУ - ПараметрыАмортизацииНМАУУ.СрокИспользования)
	|		ИНАЧЕ ПараметрыАмортизацииНМАУУ.СрокИспользованияДляВычисленияАмортизации
	|	КОНЕЦ КАК СрокИспользованияДляВычисленияАмортизации,
	|
	// ОбъемПродукцииРаботДляВычисленияАмортизации
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОбъемНаработкиФлаг И ПараметрыАмортизацииНМАУУ.НематериальныйАктив ЕСТЬ NULL
	|			ТОГДА ДанныеДокумента.ОбъемНаработки
	|		КОГДА ДанныеДокумента.ОбъемНаработкиФлаг
	|				И втПорядокУчетаНМА.ОбъемНаработки = ПараметрыАмортизацииНМАУУ.ОбъемПродукцииРаботДляВычисленияАмортизации
	|			ТОГДА ДанныеДокумента.ОбъемНаработки
	|		КОГДА ДанныеДокумента.ОбъемНаработкиФлаг
	|			ТОГДА ПараметрыАмортизацииНМАУУ.ОбъемПродукцииРаботДляВычисленияАмортизации + (ДанныеДокумента.ОбъемНаработки - втПорядокУчетаНМА.ОбъемНаработки)
	|		ИНАЧЕ ПараметрыАмортизацииНМАУУ.ОбъемПродукцииРаботДляВычисленияАмортизации
	|	КОНЕЦ КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.КоэффициентУскоренияУУФлаг
	|			ТОГДА ДанныеДокумента.КоэффициентУскоренияУУ
	|		ИНАЧЕ ЕСТЬNULL(ПараметрыАмортизацииНМАУУ.КоэффициентУскорения, 0)
	|	КОНЕЦ КАК КоэффициентУскорения,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.МетодНачисленияАмортизацииУУФлаг
	|			ТОГДА ДанныеДокумента.МетодНачисленияАмортизацииУУ
	|		ИНАЧЕ ПараметрыАмортизацииНМАУУ.МетодНачисленияАмортизации
	|	КОНЕЦ КАК МетодНачисленияАмортизации,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ЛиквидационнаяСтоимостьФлаг
	|			ТОГДА ДанныеДокумента.ЛиквидационнаяСтоимость
	|		КОГДА ДанныеДокумента.ЛиквидационнаяСтоимостьРеглФлаг
	|				И ЕСТЬNULL(ВтУчетнаяПолитикаФинансовогоУчета.ПорядокУчетаВНАРегл, НЕОПРЕДЕЛЕНО) <> ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАРегл.Независимый)
	|				И &ВалютаУпр = ДанныеДокумента.ВалютаРегл
	|			ТОГДА ДанныеДокумента.ЛиквидационнаяСтоимостьРегл
	|		ИНАЧЕ ЕСТЬNULL(ПараметрыАмортизацииНМАУУ.ЛиквидационнаяСтоимость, 0)
	|	КОНЕЦ КАК ЛиквидационнаяСтоимость
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаНМА КАК ТаблицаНМА
	|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПараметрыАмортизацииНМАУУ КАК ПараметрыАмортизацииНМАУУ
	|		ПО ПараметрыАмортизацииНМАУУ.Ссылка = ТаблицаНМА.Ссылка
	|			И ПараметрыАмортизацииНМАУУ.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаНМА КАК втПорядокУчетаНМА
	|		ПО втПорядокУчетаНМА.Ссылка = ТаблицаНМА.Ссылка
	|			И втПорядокУчетаНМА.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтУчетнаяПолитикаФинансовогоУчета КАК ВтУчетнаяПолитикаФинансовогоУчета
	|		ПО ВтУчетнаяПолитикаФинансовогоУчета.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ОтражатьВУпрУчете
	|	И (ДанныеДокумента.СрокИспользованияУУФлаг
	|		ИЛИ ДанныеДокумента.КоэффициентУскоренияУУФлаг
	|		ИЛИ ДанныеДокумента.МетодНачисленияАмортизацииУУФлаг
	|		ИЛИ ДанныеДокумента.ЛиквидационнаяСтоимостьФлаг
	|		ИЛИ ДанныеДокумента.ЛиквидационнаяСтоимостьРеглФлаг
	|			И ЕСТЬNULL(ВтУчетнаяПолитикаФинансовогоУчета.ПорядокУчетаВНАРегл, НЕОПРЕДЕЛЕНО) <> ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАРегл.Независимый)
	|			И &ВалютаУпр = ДанныеДокумента.ВалютаРегл
	|		ИЛИ ДанныеДокумента.ОбъемНаработкиФлаг)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаСтоимостьНМА(ТекстыЗапроса)

	ИмяРегистра = "СтоимостьНМА";
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыУчетаНМА(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)  КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                  КАК Регистратор,
	|	ДанныеДокумента.Период                  КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИзменениеПараметровСтоимостиНМА) КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация             КАК Организация,
	|	ТаблицаНМА.НематериальныйАктив          КАК НематериальныйАктив,
	|	ВтПараметрыУчетаНМА.Подразделение           КАК Подразделение,
	|	ВтПараметрыУчетаНМА.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|	ВтПараметрыУчетаНМА.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|
	|	СтоимостьНМА.Стоимость                  КАК Стоимость,
	|	СтоимостьНМА.СтоимостьРегл              КАК СтоимостьРегл,
	|	СтоимостьНМА.СтоимостьНУ                КАК СтоимостьНУ,
	|	СтоимостьНМА.СтоимостьПР                КАК СтоимостьПР,
	|	СтоимостьНМА.СтоимостьВР                КАК СтоимостьВР,
	|	СтоимостьНМА.СтоимостьЦФ                КАК СтоимостьЦФ,
	|	СтоимостьНМА.СтоимостьНУЦФ              КАК СтоимостьНУЦФ,
	|	СтоимостьНМА.СтоимостьПРЦФ              КАК СтоимостьПРЦФ,
	|	СтоимостьНМА.СтоимостьВРЦФ              КАК СтоимостьВРЦФ,
	|	СтоимостьНМА.ПредварительнаяСтоимость       КАК ПредварительнаяСтоимость,
	|	СтоимостьНМА.ПредварительнаяСтоимостьРегл   КАК ПредварительнаяСтоимостьРегл,
	|	СтоимостьНМА.РезервПереоценкиСтоимости      КАК РезервПереоценкиСтоимости,
	|	СтоимостьНМА.РезервПереоценкиСтоимостиРегл  КАК РезервПереоценкиСтоимостиРегл,
	|
	|	0                                       КАК ПриростСтоимости,
	|	0                                       КАК ПриростСтоимостиРегл,
	|
	|	ДанныеДокумента.Организация             КАК КорОрганизация,
	|	ВтПараметрыУчетаНМА.Подразделение       КАК КорПодразделение,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.НаправлениеДеятельностиФлаг
	|			ТОГДА ДанныеДокумента.НаправлениеДеятельности
	|		ИНАЧЕ ВтПараметрыУчетаНМА.НаправлениеДеятельности
	|	КОНЕЦ КАК КорНаправлениеДеятельности,
	|
	|	НЕОПРЕДЕЛЕНО                            КАК КорСтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                            КАК КорАналитикаРасходов,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ГруппаФинансовогоУчетаФлаг
	|			ТОГДА ДанныеДокумента.ГруппаФинансовогоУчета
	|		ИНАЧЕ ВтПараметрыУчетаНМА.ГруппаФинансовогоУчета
	|	КОНЕЦ КАК КорГруппаФинансовогоУчета,
	|	
	|	ТаблицаНМА.ИдентификаторСтроки          КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ИзменениеПараметровСтоимостиНМА) КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаНМА КАК ТаблицаНМА
	|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПараметрыУчетаНМА КАК ВтПараметрыУчетаНМА
	|		ПО ВтПараметрыУчетаНМА.Ссылка = ТаблицаНМА.Ссылка
	|			И ВтПараметрыУчетаНМА.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьНМА
	|		ПО СтоимостьНМА.Ссылка = ТаблицаНМА.Ссылка
	|			И СтоимостьНМА.ОбъектУчета = ТаблицаНМА.НематериальныйАктив
	|
	|ГДЕ
	|	ДанныеДокумента.ГруппаФинансовогоУчетаФлаг
	|		ИЛИ ДанныеДокумента.НаправлениеДеятельностиФлаг
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)  КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                  КАК Регистратор,
	|	ДанныеДокумента.Период                  КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИзменениеПараметровСтоимостиНМА) КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация             КАК Организация,
	|	ТаблицаНМА.НематериальныйАктив          КАК НематериальныйАктив,
	|	ВтПараметрыУчетаНМА.Подразделение       КАК Подразделение,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ГруппаФинансовогоУчетаФлаг
	|			ТОГДА ДанныеДокумента.ГруппаФинансовогоУчета
	|		ИНАЧЕ ВтПараметрыУчетаНМА.ГруппаФинансовогоУчета
	|	КОНЕЦ КАК ГруппаФинансовогоУчета,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.НаправлениеДеятельностиФлаг
	|			ТОГДА ДанныеДокумента.НаправлениеДеятельности
	|		ИНАЧЕ ВтПараметрыУчетаНМА.НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|
	|	СтоимостьНМА.Стоимость                  КАК Стоимость,
	|	СтоимостьНМА.СтоимостьРегл              КАК СтоимостьРегл,
	|	СтоимостьНМА.СтоимостьНУ                КАК СтоимостьНУ,
	|	СтоимостьНМА.СтоимостьПР                КАК СтоимостьПР,
	|	СтоимостьНМА.СтоимостьВР                КАК СтоимостьВР,
	|	СтоимостьНМА.СтоимостьЦФ                КАК СтоимостьЦФ,
	|	СтоимостьНМА.СтоимостьНУЦФ              КАК СтоимостьНУЦФ,
	|	СтоимостьНМА.СтоимостьПРЦФ              КАК СтоимостьПРЦФ,
	|	СтоимостьНМА.СтоимостьВРЦФ              КАК СтоимостьВРЦФ,
	|	СтоимостьНМА.ПредварительнаяСтоимость       КАК ПредварительнаяСтоимость,
	|	СтоимостьНМА.ПредварительнаяСтоимостьРегл   КАК ПредварительнаяСтоимостьРегл,
	|	СтоимостьНМА.РезервПереоценкиСтоимости      КАК РезервПереоценкиСтоимости,
	|	СтоимостьНМА.РезервПереоценкиСтоимостиРегл  КАК РезервПереоценкиСтоимостиРегл,
	|
	|	0                                       КАК ПриростСтоимости,
	|	0                                       КАК ПриростСтоимостиРегл,
	|
	|	ДанныеДокумента.Организация             КАК КорОрганизация,
	|	ВтПараметрыУчетаНМА.Подразделение       КАК КорПодразделение,
	|	ВтПараметрыУчетаНМА.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                            КАК КорСтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                            КАК КорАналитикаРасходов,
	|	ВтПараметрыУчетаНМА.ГруппаФинансовогоУчета КАК КорГруппаФинансовогоУчета,
	|	
	|	ТаблицаНМА.ИдентификаторСтроки          КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ИзменениеПараметровСтоимостиНМА) КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаНМА КАК ТаблицаНМА
	|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПараметрыУчетаНМА КАК ВтПараметрыУчетаНМА
	|		ПО ВтПараметрыУчетаНМА.Ссылка = ТаблицаНМА.Ссылка
	|			И ВтПараметрыУчетаНМА.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьНМА
	|		ПО СтоимостьНМА.Ссылка = ДанныеДокумента.Ссылка
	|			И СтоимостьНМА.ОбъектУчета = ТаблицаНМА.НематериальныйАктив
	|
	|ГДЕ
	|	ДанныеДокумента.ГруппаФинансовогоУчетаФлаг
	|		ИЛИ ДанныеДокумента.НаправлениеДеятельностиФлаг
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)  КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                  КАК Регистратор,
	|	ДанныеДокумента.Период                  КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИзменениеПараметровСтоимостиНМА) КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация             КАК Организация,
	|	ТаблицаНМА.НематериальныйАктив          КАК НематериальныйАктив,
	|	НЕОПРЕДЕЛЕНО                            КАК Подразделение,
	|	ВтПараметрыУчетаНМА.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	ВтПараметрыУчетаНМА.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|
	|	0                                       КАК Стоимость,
	|	0                                       КАК СтоимостьРегл,
	|	0                                       КАК СтоимостьНУ,
	|	0                                       КАК СтоимостьПР,
	|	0                                       КАК СтоимостьВР,
	|	0                                       КАК СтоимостьЦФ,
	|	0                                       КАК СтоимостьНУЦФ,
	|	0                                       КАК СтоимостьПРЦФ,
	|	0                                       КАК СтоимостьВРЦФ,
	|	0                                       КАК ПредварительнаяСтоимость,
	|	0                                       КАК ПредварительнаяСтоимостьРегл,
	|	0                                       КАК РезервПереоценкиСтоимости,
	|	0                                       КАК РезервПереоценкиСтоимостиРегл,
	|
	|	СтоимостьНМА.ПриростСтоимости           КАК ПриростСтоимости,
	|	СтоимостьНМА.ПриростСтоимостиРегл       КАК ПриростСтоимостиРегл,
	|
	|	ДанныеДокумента.Организация             КАК КорОрганизация,
	|	НЕОПРЕДЕЛЕНО                            КАК КорПодразделение,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.НаправлениеДеятельностиФлаг
	|			ТОГДА ДанныеДокумента.НаправлениеДеятельности
	|		ИНАЧЕ ВтПараметрыУчетаНМА.НаправлениеДеятельности
	|	КОНЕЦ КАК КорНаправлениеДеятельности,
	|
	|	НЕОПРЕДЕЛЕНО                            КАК КорСтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                            КАК КорАналитикаРасходов,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ГруппаФинансовогоУчетаФлаг
	|			ТОГДА ДанныеДокумента.ГруппаФинансовогоУчета
	|		ИНАЧЕ ВтПараметрыУчетаНМА.ГруппаФинансовогоУчета
	|	КОНЕЦ КАК КорГруппаФинансовогоУчета,
	|	
	|	ТаблицаНМА.ИдентификаторСтроки          КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ИзменениеПараметровСтоимостиНМА) КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаНМА КАК ТаблицаНМА
	|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПараметрыУчетаНМА КАК ВтПараметрыУчетаНМА
	|		ПО ВтПараметрыУчетаНМА.Ссылка = ТаблицаНМА.Ссылка
	|			И ВтПараметрыУчетаНМА.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьНМА
	|		ПО СтоимостьНМА.Ссылка = ТаблицаНМА.Ссылка
	|			И СтоимостьНМА.ОбъектУчета = ТаблицаНМА.НематериальныйАктив
	|
	|ГДЕ
	|	(ДанныеДокумента.ГруппаФинансовогоУчетаФлаг ИЛИ ДанныеДокумента.НаправлениеДеятельностиФлаг)
	|	И (СтоимостьНМА.ПриростСтоимости <> 0 ИЛИ СтоимостьНМА.ПриростСтоимостиРегл <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)  КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                  КАК Регистратор,
	|	ДанныеДокумента.Период                  КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИзменениеПараметровСтоимостиНМА) КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация             КАК Организация,
	|	ТаблицаНМА.НематериальныйАктив          КАК НематериальныйАктив,
	|	НЕОПРЕДЕЛЕНО                            КАК Подразделение,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ГруппаФинансовогоУчетаФлаг
	|			ТОГДА ДанныеДокумента.ГруппаФинансовогоУчета
	|		ИНАЧЕ ВтПараметрыУчетаНМА.ГруппаФинансовогоУчета
	|	КОНЕЦ КАК ГруппаФинансовогоУчета,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.НаправлениеДеятельностиФлаг
	|			ТОГДА ДанныеДокумента.НаправлениеДеятельности
	|		ИНАЧЕ ВтПараметрыУчетаНМА.НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|
	|	0                                       КАК Стоимость,
	|	0                                       КАК СтоимостьРегл,
	|	0                                       КАК СтоимостьНУ,
	|	0                                       КАК СтоимостьПР,
	|	0                                       КАК СтоимостьВР,
	|	0                                       КАК СтоимостьЦФ,
	|	0                                       КАК СтоимостьНУЦФ,
	|	0                                       КАК СтоимостьПРЦФ,
	|	0                                       КАК СтоимостьВРЦФ,
	|	0                                       КАК ПредварительнаяСтоимость,
	|	0                                       КАК ПредварительнаяСтоимостьРегл,
	|	0                                       КАК РезервПереоценкиСтоимости,
	|	0                                       КАК РезервПереоценкиСтоимостиРегл,
	|
	|	СтоимостьНМА.ПриростСтоимости           КАК ПриростСтоимости,
	|	СтоимостьНМА.ПриростСтоимостиРегл       КАК ПриростСтоимостиРегл,
	|
	|	ДанныеДокумента.Организация             КАК КорОрганизация,
	|	НЕОПРЕДЕЛЕНО                            КАК КорПодразделение,
	|	ВтПараметрыУчетаНМА.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                            КАК КорСтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                            КАК КорАналитикаРасходов,
	|	ВтПараметрыУчетаНМА.ГруппаФинансовогоУчета КАК КорГруппаФинансовогоУчета,
	|	
	|	ТаблицаНМА.ИдентификаторСтроки          КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ИзменениеПараметровСтоимостиНМА) КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаНМА КАК ТаблицаНМА
	|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПараметрыУчетаНМА КАК ВтПараметрыУчетаНМА
	|		ПО ВтПараметрыУчетаНМА.Ссылка = ТаблицаНМА.Ссылка
	|			И ВтПараметрыУчетаНМА.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьНМА
	|		ПО СтоимостьНМА.Ссылка = ТаблицаНМА.Ссылка
	|			И СтоимостьНМА.ОбъектУчета = ТаблицаНМА.НематериальныйАктив
	|
	|ГДЕ
	|	(ДанныеДокумента.ГруппаФинансовогоУчетаФлаг 
	|		ИЛИ ДанныеДокумента.НаправлениеДеятельностиФлаг)
	|
	|	И (СтоимостьНМА.ПриростСтоимости <> 0 
	|		ИЛИ СтоимостьНМА.ПриростСтоимостиРегл <> 0)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаАмортизацияНМА(ТекстыЗапроса)

	ИмяРегистра = "АмортизацияНМА";
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыУчетаНМА(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)     КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                     КАК Регистратор,
	|	ДанныеДокумента.Период                     КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИзменениеПараметровАмортизацииНМА) КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация                КАК Организация,
	|	ТаблицаНМА.НематериальныйАктив             КАК НематериальныйАктив,
	|	ВтПараметрыУчетаНМА.Подразделение          КАК Подразделение,
	|	ВтПараметрыУчетаНМА.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	ВтПараметрыУчетаНМА.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                               КАК Контрагент,
	|	АмортизацияНМА.Амортизация                 КАК Амортизация,
	|	АмортизацияНМА.АмортизацияРегл             КАК АмортизацияРегл,
	|	АмортизацияНМА.АмортизацияНУ               КАК АмортизацияНУ,
	|	АмортизацияНМА.АмортизацияПР               КАК АмортизацияПР,
	|	АмортизацияНМА.АмортизацияВР               КАК АмортизацияВР,
	|	АмортизацияНМА.АмортизацияЦФ               КАК АмортизацияЦФ,
	|	АмортизацияНМА.АмортизацияНУЦФ             КАК АмортизацияНУЦФ,
	|	АмортизацияНМА.АмортизацияПРЦФ             КАК АмортизацияПРЦФ,
	|	АмортизацияНМА.АмортизацияВРЦФ             КАК АмортизацияВРЦФ,
	|	АмортизацияНМА.РезервПереоценкиАмортизации КАК РезервПереоценкиАмортизации,
	|	ДанныеДокумента.Организация                КАК КорОрганизация,
	|	ВтПараметрыУчетаНМА.Подразделение          КАК КорПодразделение,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.НаправлениеДеятельностиФлаг
	|			ТОГДА ДанныеДокумента.НаправлениеДеятельности
	|		ИНАЧЕ ВтПараметрыУчетаНМА.НаправлениеДеятельности
	|	КОНЕЦ КАК КорНаправлениеДеятельности,
	|
	|	НЕОПРЕДЕЛЕНО                               КАК КорСтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                               КАК КорАналитикаРасходов,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ГруппаФинансовогоУчетаФлаг
	|			ТОГДА ДанныеДокумента.ГруппаФинансовогоУчета
	|		ИНАЧЕ ВтПараметрыУчетаНМА.ГруппаФинансовогоУчета
	|	КОНЕЦ КАК КорГруппаФинансовогоУчета,
	|	
	|	ТаблицаНМА.ИдентификаторСтроки             КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ИзменениеПараметровАмортизацииНМА) КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаНМА КАК ТаблицаНМА
	|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПараметрыУчетаНМА КАК ВтПараметрыУчетаНМА
	|		ПО ВтПараметрыУчетаНМА.Ссылка = ТаблицаНМА.Ссылка
	|			И ВтПараметрыУчетаНМА.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияНМА
	|		ПО АмортизацияНМА.ОбъектУчета = ТаблицаНМА.НематериальныйАктив
	|			И АмортизацияНМА.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ГруппаФинансовогоУчетаФлаг
	|		ИЛИ ДанныеДокумента.НаправлениеДеятельностиФлаг
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)     КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                     КАК Регистратор,
	|	ДанныеДокумента.Период                     КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИзменениеПараметровАмортизацииНМА) КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация                КАК Организация,
	|	ТаблицаНМА.НематериальныйАктив             КАК НематериальныйАктив,
	|	ВтПараметрыУчетаНМА.Подразделение          КАК Подразделение,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ГруппаФинансовогоУчетаФлаг
	|			ТОГДА ДанныеДокумента.ГруппаФинансовогоУчета
	|		ИНАЧЕ ВтПараметрыУчетаНМА.ГруппаФинансовогоУчета
	|	КОНЕЦ КАК ГруппаФинансовогоУчета,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.НаправлениеДеятельностиФлаг
	|			ТОГДА ДанныеДокумента.НаправлениеДеятельности
	|		ИНАЧЕ ВтПараметрыУчетаНМА.НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|
	|	НЕОПРЕДЕЛЕНО                               КАК Контрагент,
	|	АмортизацияНМА.Амортизация                 КАК Амортизация,
	|	АмортизацияНМА.АмортизацияРегл             КАК АмортизацияРегл,
	|	АмортизацияНМА.АмортизацияНУ               КАК АмортизацияНУ,
	|	АмортизацияНМА.АмортизацияПР               КАК АмортизацияПР,
	|	АмортизацияНМА.АмортизацияВР               КАК АмортизацияВР,
	|	АмортизацияНМА.АмортизацияЦФ               КАК АмортизацияЦФ,
	|	АмортизацияНМА.АмортизацияНУЦФ             КАК АмортизацияНУЦФ,
	|	АмортизацияНМА.АмортизацияПРЦФ             КАК АмортизацияПРЦФ,
	|	АмортизацияНМА.АмортизацияВРЦФ             КАК АмортизацияВРЦФ,
	|	АмортизацияНМА.РезервПереоценкиАмортизации КАК РезервПереоценкиАмортизации,
	|	ДанныеДокумента.Организация                КАК КорОрганизация,
	|	ВтПараметрыУчетаНМА.Подразделение          КАК КорПодразделение,
	|	ВтПараметрыУчетаНМА.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                               КАК КорСтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                               КАК КорАналитикаРасходов,
	|	ВтПараметрыУчетаНМА.ГруппаФинансовогоУчета КАК КорГруппаФинансовогоУчета,
	|	
	|	ТаблицаНМА.ИдентификаторСтроки             КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ИзменениеПараметровАмортизацииНМА) КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаНМА КАК ТаблицаНМА
	|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПараметрыУчетаНМА КАК ВтПараметрыУчетаНМА
	|		ПО ВтПараметрыУчетаНМА.Ссылка = ТаблицаНМА.Ссылка
	|			И ВтПараметрыУчетаНМА.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияНМА
	|		ПО АмортизацияНМА.ОбъектУчета = ТаблицаНМА.НематериальныйАктив
	|			И АмортизацияНМА.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ГруппаФинансовогоУчетаФлаг
	|		ИЛИ ДанныеДокумента.НаправлениеДеятельностиФлаг";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаДвиженияДоходыРасходыПрочиеАктивыПассивы(ТекстыЗапроса)

	ИмяРегистра = "ДвиженияДоходыРасходыПрочиеАктивыПассивы";
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыУчетаНМА(ТекстыЗапроса);
	
	ТекстЗапроса =
	// Перемещение амортизации
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                       КАК Регистратор,
	|	ДанныеДокумента.Период                       КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИзменениеПараметровАмортизацииНМА) КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация                  КАК Организация,
	|
	|	ВтПараметрыУчетаНМА.Подразделение            КАК Подразделение,
	|	ВтПараметрыУчетаНМА.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	&СтатьяАП_НМА                                КАК Статья,
	|	НЕОПРЕДЕЛЕНО                                 КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                                 КАК АналитикаРасходов,
	|	ТаблицаНМА.НематериальныйАктив               КАК АналитикаАктивовПассивов,
	|	ВтПараметрыУчетаНМА.ГруппаФинансовогоУчета   КАК ГруппаФинансовогоУчета,
	|
	|	ВтПараметрыУчетаНМА.Подразделение            КАК КорПодразделение,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.НаправлениеДеятельностиФлаг
	|			ТОГДА ДанныеДокумента.НаправлениеДеятельности
	|		ИНАЧЕ ВтПараметрыУчетаНМА.НаправлениеДеятельности
	|	КОНЕЦ КАК КорНаправлениеДеятельности,
	|
	|	&СтатьяАП_НМА                                КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                                 КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                                 КАК КорАналитикаРасходов,
	|	ТаблицаНМА.НематериальныйАктив               КАК КорАналитикаАктивовПассивов,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ГруппаФинансовогоУчетаФлаг
	|			ТОГДА ДанныеДокумента.ГруппаФинансовогоУчета
	|		ИНАЧЕ ВтПараметрыУчетаНМА.ГруппаФинансовогоУчета
	|	КОНЕЦ КАК КорГруппаФинансовогоУчета,
	|
	|	ДанныеДокумента.Организация                  КАК КорОрганизация,
	|
	|	АмортизацияНМА.Амортизация                   КАК Сумма,
	|	АмортизацияНМА.Амортизация                   КАК СуммаУпр,
	|	АмортизацияНМА.АмортизацияРегл + АмортизацияНМА.АмортизацияЦФ КАК СуммаРегл
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаНМА КАК ТаблицаНМА
	|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПараметрыУчетаНМА КАК ВтПараметрыУчетаНМА
	|		ПО ВтПараметрыУчетаНМА.Ссылка = ТаблицаНМА.Ссылка
	|			И ВтПараметрыУчетаНМА.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияНМА
	|		ПО АмортизацияНМА.ОбъектУчета = ТаблицаНМА.НематериальныйАктив
	|			И АмортизацияНМА.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ГруппаФинансовогоУчетаФлаг
	|		ИЛИ ДанныеДокумента.НаправлениеДеятельностиФлаг
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Перемещение стоимости
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                       КАК Регистратор,
	|	ДанныеДокумента.Период                       КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИзменениеПараметровСтоимостиНМА) КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация                  КАК Организация,
	|
	|	ВтПараметрыУчетаНМА.Подразделение            КАК Подразделение,
	|	ВтПараметрыУчетаНМА.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	&СтатьяАП_НМА                                КАК Статья,
	|	НЕОПРЕДЕЛЕНО                                 КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                                 КАК АналитикаРасходов,
	|	ТаблицаНМА.НематериальныйАктив               КАК АналитикаАктивовПассивов,
	|	ВтПараметрыУчетаНМА.ГруппаФинансовогоУчета   КАК ГруппаФинансовогоУчета,
	|
	|	ВтПараметрыУчетаНМА.Подразделение            КАК КорПодразделение,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.НаправлениеДеятельностиФлаг
	|			ТОГДА ДанныеДокумента.НаправлениеДеятельности
	|		ИНАЧЕ ВтПараметрыУчетаНМА.НаправлениеДеятельности
	|	КОНЕЦ КАК КорНаправлениеДеятельности,
	|
	|	&СтатьяАП_НМА                                КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                                 КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                                 КАК КорАналитикаРасходов,
	|	ТаблицаНМА.НематериальныйАктив               КАК КорАналитикаАктивовПассивов,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ГруппаФинансовогоУчетаФлаг
	|			ТОГДА ДанныеДокумента.ГруппаФинансовогоУчета
	|		ИНАЧЕ ВтПараметрыУчетаНМА.ГруппаФинансовогоУчета
	|	КОНЕЦ КАК КорГруппаФинансовогоУчета,
	|
	|	ДанныеДокумента.Организация                  КАК КорОрганизация,
	|
	|	СтоимостьНМА.Стоимость                       КАК Сумма,
	|	СтоимостьНМА.Стоимость                       КАК СуммаУпр,
	|	СтоимостьНМА.СтоимостьРегл + СтоимостьНМА.СтоимостьЦФ КАК СуммаРегл
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаНМА КАК ТаблицаНМА
	|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПараметрыУчетаНМА КАК ВтПараметрыУчетаНМА
	|		ПО ВтПараметрыУчетаНМА.Ссылка = ТаблицаНМА.Ссылка
	|			И ВтПараметрыУчетаНМА.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьНМА
	|		ПО СтоимостьНМА.ОбъектУчета = ТаблицаНМА.НематериальныйАктив
	|			И СтоимостьНМА.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ГруппаФинансовогоУчетаФлаг
	|		ИЛИ ДанныеДокумента.НаправлениеДеятельностиФлаг
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаСоставЕГДС(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СоставЕГДС";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыУчетаНМА(ТекстыЗапроса);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка         КАК Регистратор,
	|	ДанныеДокумента.Период         КАК Период,
	|	ТаблицаНМА.НематериальныйАктив КАК ВнеоборотныйАктив,
	|	ДанныеДокумента.Организация    КАК Организация,
	|	ДанныеДокумента.ЕГДС           КАК ЕГДС
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаНМА КАК ТаблицаНМА
	|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ЕГДСФлаг";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область ПроведениеПоРеглУчету

Функция ТекстОтраженияВРеглУчете() Экспорт

	Возврат ИзменениеПараметровНМАЛокализация.ТекстОтраженияВРеглУчете();

КонецФункции

Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт

	Возврат ИзменениеПараметровНМАЛокализация.ТекстЗапросаВТОтраженияВРеглУчете();

КонецФункции

#КонецОбласти

#Область Прочее

Функция ПараметрыВыбораСтатейИАналитик() Экспорт
	
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект.АмортизационныеРасходы";
	ПараметрыВыбора.Статья = "СтатьяРасходов";
	ПараметрыВыбора.ДоступностьПоОперации = Неопределено;
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходов";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("АмортизационныеРасходыСтатьяРасходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АмортизационныеРасходыАналитикаРасходов");
	
	Возврат ПараметрыВыбора;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
