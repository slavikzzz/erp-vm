#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов


#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ФизическоеЛицо)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Поля.Добавить("Дата");
	Поля.Добавить("Номер");
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	Шаблон = НСтр("ru = 'Заявка на налоговые вычеты %1 от %2';
					|en = 'Заявка на налоговые вычеты %1 от %2'");
	Представление = СтрШаблон(Шаблон, Данные.Номер, Данные.Дата);
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

// Возвращает описание состава документа
//
// Возвращаемое значение:
//  Структура - см. ЗарплатаКадрыСоставДокументов.НовоеОписаниеСоставаОбъекта.
Функция ОписаниеСоставаОбъекта() Экспорт
	
	ОписаниеСостава = ЗарплатаКадрыСоставДокументов.НовоеОписаниеСоставаОбъекта();
	ОписаниеСостава.ИспользоватьКраткийСостав = Ложь;
	ОписаниеСостава.ЗаполнятьТабличнуюЧастьФизическиеЛицаДокумента = Ложь;
	ОписаниеСостава.ЗаполнятьФизическиеЛицаПоСотрудникам = Ложь;
	
	ОписаниеХранения = ЗарплатаКадрыСоставДокументов.ОписаниеХраненияСотрудниковФизическихЛиц();
	ОписаниеХранения.ПолеСотрудник      = "Сотрудник";
	ОписаниеХранения.ПолеФизическоеЛицо = "ФизическоеЛицо";
	
	ОписаниеСостава.ОписаниеХраненияСотрудниковФизическихЛиц.Добавить(ОписаниеХранения);
	
	Возврат ОписаниеСостава;
	
	
КонецФункции

Функция ОписаниеДокументаДляЗаявки(СписокОбщихПолей) Экспорт
	
	СтрокаСвойств = "ВидВычета,ДанныеВычетовНаДетей";
	СписокПолей = СтрШаблон("%1,%2", СписокОбщихПолей, СтрокаСвойств);
	
	Описание = Новый Структура(СписокПолей);
	Описание.ДанныеВычетовНаДетей = ОписаниеВычетыНаДетей();
	
	Возврат Описание;

КонецФункции

Функция БизнесПроцессМенеджер() Экспорт

	Возврат БизнесПроцессы.ИсполнениеЗаявкиСотрудника;

КонецФункции

Функция НаименованиеЗадачи(ДокументОбъект) Экспорт
	
	ШаблонНаименования = НСтр("ru = 'Налоговые вычеты';
								|en = 'Налоговые вычеты'");
	Если ДокументОбъект.ВидВычета = Перечисления.ВидыНалоговыхВычетовКабинетСотрудника.Личный Тогда
		ШаблонНаименования = НСтр("ru = 'Личный налоговый вычет';
									|en = 'Личный налоговый вычет'");
	ИначеЕсли ДокументОбъект.ВидВычета = Перечисления.ВидыНалоговыхВычетовКабинетСотрудника.НаДетей Тогда
		ШаблонНаименования = НСтр("ru = 'Налоговые вычеты на детей';
									|en = 'Налоговые вычеты на детей'");
	ИначеЕсли ДокументОбъект.ВидВычета = Перечисления.ВидыНалоговыхВычетовКабинетСотрудника.Недвижимость Тогда
		ШаблонНаименования = НСтр("ru = 'Налоговый вычеты имущественный';
									|en = 'Налоговый вычеты имущественный'");
	ИначеЕсли ДокументОбъект.ВидВычета = Перечисления.ВидыНалоговыхВычетовКабинетСотрудника.НаЛечение Тогда
		ШаблонНаименования = НСтр("ru = 'Налоговый вычеты лечение';
									|en = 'Налоговый вычеты лечение'");
	ИначеЕсли ДокументОбъект.ВидВычета = Перечисления.ВидыНалоговыхВычетовКабинетСотрудника.НаОбучение Тогда
		ШаблонНаименования = НСтр("ru = 'Налоговый вычеты обучение';
									|en = 'Налоговый вычеты обучение'");
	КонецЕсли;
	
	Возврат СтрШаблон("%1: %2", ШаблонНаименования, Строка(ДокументОбъект.ФизическоеЛицо));

КонецФункции

Функция СодержаниеЗадачи(ДокументОбъект) Экспорт
	
	ШаблонНаименования = НСтр("ru = 'Обработать заявление на налоговые вычеты';
								|en = 'Обработать заявление на налоговые вычеты'");
	
	Шаблон = НСтр("ru = '%1
	|Организация: %2
	|Сотрудник: %3';
	|en = '%1
	|Организация: %2
	|Сотрудник: %3'");
	
	Возврат СтрШаблон(Шаблон, ШаблонНаименования, Строка(ДокументОбъект.Организация), Строка(ДокументОбъект.Сотрудник));
	
КонецФункции

Функция РольИсполнителяЗаявки() Экспорт

	Возврат Справочники.РолиИсполнителей.ОтветственныйЗаНалоговыеВычеты;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОписаниеВычетыНаДетей()

	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("ОписаниеВычета", 	Новый ОписаниеТипов("Строка"));
	Таблица.Колонки.Добавить("ДействуетДо", 	Новый ОписаниеТипов("Дата"));
	
	Возврат Таблица;

КонецФункции

#КонецОбласти


#КонецЕсли