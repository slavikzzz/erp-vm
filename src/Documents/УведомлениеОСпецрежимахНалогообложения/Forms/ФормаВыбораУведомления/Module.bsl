#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура СоздатьУведомление(Команда)
	Если ЗначениеЗаполнено(ИмяФормыУведомления) Тогда 
		ПараметрыОткрытия = Новый Структура("Организация, Данные", Организация, ПолучитьИзВременногоХранилища(АдресДанные));
		ОткрытьФорму(ИмяФормыУведомления, ПараметрыОткрытия, ВладелецФормы, ИмяФормыУведомления);
		Закрыть();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	Элементы.ФормаСоздатьУведомление.Доступность = (ЗначениеЗаполнено(Организация));
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Данные = Неопределено;
	Параметры.Свойство("Данные", Данные);
	АдресДанные = ПоместитьВоВременноеХранилище(Данные, УникальныйИдентификатор);

	Если РегламентированнаяОтчетностьВызовСервера.ИспользуетсяОднаОрганизация() Тогда
		ОргПоУмолчанию = ОбщегоНазначения.ОбщийМодуль("Справочники.Организации").ОрганизацияПоУмолчанию();
		Организация = ОргПоУмолчанию;
	КонецЕсли;
	
	Если Параметры.Свойство("Тип") Тогда
		ВидУведомления = Параметры.Тип;
		ИмяФормыУведомления = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПолноеИмяФормыПоВиду(ВидУведомления);
		УведомленияДляОрганизаций = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ВидыУведомленийДляОрганизации();
		УведомленияДляИП = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ВидыУведомленийДляИП();
		
		Если УведомленияДляИП.Найти(ВидУведомления) <> Неопределено И УведомленияДляОрганизаций.Найти(ВидУведомления) = Неопределено Тогда 
			МассивОтбора = РегламентированнаяОтчетность.ВсеЮрФизЛица(Ложь);
			МассивПараметровВыбора = Новый Массив;
			МассивПараметровВыбора.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", Новый ФиксированныйМассив(МассивОтбора)));
			Элементы.Организация.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметровВыбора);
		ИначеЕсли УведомленияДляИП.Найти(ВидУведомления) = Неопределено И УведомленияДляОрганизаций.Найти(ВидУведомления) <> Неопределено Тогда 
			МассивОтбора = РегламентированнаяОтчетность.ВсеЮрФизЛица(Истина);
			МассивПараметровВыбора = Новый Массив;
			МассивПараметровВыбора.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", Новый ФиксированныйМассив(МассивОтбора)));
			Элементы.Организация.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметровВыбора);
		КонецЕсли;
		
		Элементы.Декорация1.Заголовок = Элементы.Декорация1.Заголовок + ВидУведомления;
		Элементы.ФормаСоздатьУведомление.Доступность = (ЗначениеЗаполнено(Организация));
	Иначе 
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти