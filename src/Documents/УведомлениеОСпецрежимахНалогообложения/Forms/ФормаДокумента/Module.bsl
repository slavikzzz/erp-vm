#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.Ключ) Тогда 
		РеквизитыКлюча = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.Ключ, "ИмяФормы,ИмяОтчета");
	ИначеЕсли ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда 
		РеквизитыКлюча = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.ЗначениеКопирования, "ИмяФормы,ИмяОтчета");
	Иначе
		РеквизитыКлюча = Новый Структура("ИмяФормы,ИмяОтчета");
	КонецЕсли;
	
	Если ТипЗнч(Параметры.Ключ) = Тип("ДокументСсылка.УведомлениеОСпецрежимахНалогообложения") И ЗначениеЗаполнено(Параметры.Ключ) Тогда 
		МенеджерУстаревшихУведомлений = ОбщегоНазначения.ОбщийМодуль("Отчеты.ВизуализацияУстаревшихУведомлений");
		Если МенеджерУстаревшихУведомлений.РедакцияУдалена(РеквизитыКлюча.ИмяОтчета, РеквизитыКлюча.ИмяФормы) Тогда 
			ИнформацияДляПечати = МенеджерУстаревшихУведомлений.ИнформацияДляПечатиУведомления(Параметры.Ключ);
			АдресДанныхПечати = ПоместитьВоВременноеХранилище(ИнформацияДляПечати, УникальныйИдентификатор);
		КонецЕсли;
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "ЗапретКопирования", Ложь) Тогда 
		СтандартнаяОбработка = Ложь;
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Копирование данной формы не поддерживается';
													|en = 'Копирование данной формы не поддерживается'"));
		Отказ = Истина;
		Возврат;
	ИначеЕсли Не ЭтоАдресВременногоХранилища(АдресДанныхПечати) 
		И ЗначениеЗаполнено(Объект.ИмяОтчета) 
		И Неопределено = Метаданные.Отчеты.Найти(Объект.ИмяОтчета) Тогда 
		
		СтандартнаяОбработка = Ложь;
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Данная форма не поддерживается';
													|en = 'Данная форма не поддерживается'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Данные = Неопределено;
	КопированиеНеПоддерживается = Ложь;
	Параметры.Свойство("ВыбраннаяФорма", ВыбраннаяФорма);
	Параметры.Свойство("ЗначениеКопирования", ЗначениеКопирования);
	Параметры.Свойство("UIDФорма1СОтчетность", UIDФорма1СОтчетность);
	Параметры.Свойство("ИмяОтчета", ИмяОтчета);
	Параметры.Свойство("Данные", Данные);
	Параметры.Свойство("ЗаполнитьПриОткрытии", ЗаполнитьПриОткрытии);
	Если Параметры.Свойство("СсылкаНового") Тогда 
		ПереданаСсылкаНового = Истина;
		Параметры.Свойство("СсылкаНового", СсылкаНового);
	КонецЕсли;
	
	Если ТипЗнч(ЗначениеКопирования) = Тип("ДокументСсылка.УведомлениеОСпецрежимахНалогообложения") И ЗначениеЗаполнено(ЗначениеКопирования) Тогда 
		ВыбраннаяФорма = "Отчет." + РеквизитыКлюча.ИмяОтчета + ".Форма." + РеквизитыКлюча.ИмяФормы;
		
		Попытка
			ТаблицаФорм = Отчеты[РеквизитыКлюча.ИмяОтчета].ПолучитьТаблицуФорм();
			Если ТаблицаФорм.Количество() > 0
				И ТаблицаФорм.НайтиСтроки(Новый Структура("ИмяФормы", РеквизитыКлюча.ИмяФормы)).Количество() = 0 Тогда 
				
				СтандартнаяОбработка = Ложь;
				ВыбраннаяФорма = "";
				ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Копирование данной редакции формы не поддерживается';
															|en = 'Копирование данной редакции формы не поддерживается'"));
				КопированиеНеПоддерживается = Истина;
				Возврат;
			КонецЕсли;
		Исключение
		КонецПопытки;
	КонецЕсли;
	АдресДанные = ПоместитьВоВременноеХранилище(Данные, УникальныйИдентификатор);
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда 
		ВидУведомления = Объект.ВидУведомления;
		Организация = Объект.Организация;
	Иначе
		Если Параметры.Свойство("Организация") Тогда
			Организация = Параметры.Организация;
		КонецЕсли;
		Если Параметры.Свойство("ВидУведомления") Тогда
			ВидУведомления = Параметры.ВидУведомления;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьИмяФормыДляОбъекта(ВидУведомления, ИмяОтчета, ИмяФормы)
	УведомлениеОСпецрежимахНалогообложения.ЗаменитьИмяОтчетаПередОткрытием(ВидУведомления, ИмяФормы, ИмяОтчета);
	Если ЗначениеЗаполнено(ИмяОтчета) И ЗначениеЗаполнено(ИмяФормы) 
		И Метаданные.Отчеты.Найти(ИмяОтчета) <> Неопределено Тогда 
		
		Имя = "Отчет."+ИмяОтчета+".Форма."+ИмяФормы;
	Иначе 
		Имя = ПолучитьИмяФормы(ВидУведомления);
	КонецЕсли;
	Возврат Имя;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьИмяФормы(Вид)
	Возврат Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПолноеИмяФормыПоВиду(Вид);
КонецФункции

&НаКлиенте
Процедура ПолучитьОрганизациюИОткрыть(Вид, ИмяФормыСообщения)
	Данные = ПолучитьИзВременногоХранилища(АдресДанные);
	Если ЗначениеЗаполнено(ИмяФормыСообщения) Тогда
		ПараметрыОткрытия = Новый Структура("ЗаполнитьПриОткрытии", ЗаполнитьПриОткрытии);
		Если ЗначениеЗаполнено(Организация) Тогда
			ПараметрыОткрытия.Вставить("Организация", Организация);
			ПараметрыОткрытия.Вставить("ЗначениеКопирования", ЗначениеКопирования);
			ПараметрыОткрытия.Вставить("UIDФорма1СОтчетность", UIDФорма1СОтчетность);
			ПараметрыОткрытия.Вставить("Данные", Данные);
			ПараметрыОткрытия.Вставить("ВидУведомления", ВидУведомления);
			Форма = ПолучитьФорму(ИмяФормыСообщения, ПараметрыОткрытия, ВладелецФормы);
			Если Форма <> Неопределено Тогда 
				Форма.Открыть();
			КонецЕсли;
		Иначе
			ПараметрыОткрытия.Вставить("Тип", Вид);
			ПараметрыОткрытия.Вставить("ЗначениеКопирования", ЗначениеКопирования);
			ПараметрыОткрытия.Вставить("UIDФорма1СОтчетность", UIDФорма1СОтчетность);
			ПараметрыОткрытия.Вставить("Данные", Данные);
			ПараметрыОткрытия.Вставить("ВидУведомления", ВидУведомления);
			
			ФормаОрганизации = ПолучитьФорму("Документ.УведомлениеОСпецрежимахНалогообложения.Форма.ФормаВыбораУведомления", ПараметрыОткрытия, ВладелецФормы, Данные);
			ФормаОрганизации.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
			ФормаОрганизации.Открыть();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Отказ = Истина;
	
	Если ЭтоАдресВременногоХранилища(АдресДанныхПечати) Тогда 
		ИнформацияДляПечати = ПолучитьИзВременногоХранилища(АдресДанныхПечати);
		Если ИнформацияДляПечати.ПечатьБСП Тогда 
			МассивПечати = Новый Массив;
			МассивПечати.Добавить(Объект.Ссылка);
			УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(ИнформацияДляПечати.МенеджерПечати, "Уведомление", МассивПечати, Неопределено);
		Иначе
			РегламентированнаяОтчетностьКлиент.ОткрытьФормуПредварительногоПросмотра(ЭтотОбъект, , Ложь, 
				ИнформацияДляПечати.СписокПечатаемыхЛистов, ИнформацияДляПечати);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Если ВидУведомления = ПредопределенноеЗначение("Перечисление.ВидыУведомленийОСпецрежимахНалогообложения.ФормаР11001")
		Или ВидУведомления = ПредопределенноеЗначение("Перечисление.ВидыУведомленийОСпецрежимахНалогообложения.ФормаР21001") Тогда
		// Для регистрации юридического лица или ИП не нужно осуществлять проверку на заполнение организации
		ПроверятьЗаполнениеОрганизации = Ложь;
	Иначе
		ПроверятьЗаполнениеОрганизации = Истина;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПараметрыОткрытияФормы = Новый Структура;
		ПараметрыОткрытияФормы.Вставить("Ключ", Объект.Ссылка);
		ПараметрыОткрытияФормы.Вставить("UIDФорма1СОтчетность", UIDФорма1СОтчетность);
		ПараметрыОткрытияФормы.Вставить("ВидУведомления", ВидУведомления);
		ПараметрыОткрытияФормы.Вставить("ЗаполнитьПриОткрытии", ЗаполнитьПриОткрытии);
		ПараметрыОткрытияФормы.Вставить("ЗакрыватьПриЗакрытииВладельца", Ложь);
		
		Имя = ПолучитьИмяФормыДляОбъекта(ВидУведомления, Объект.ИмяОтчета, Объект.ИмяФормы);
		Если Имя = Неопределено Тогда 
			Возврат;
		КонецЕсли;
		ОткрытьФорму(Имя, ПараметрыОткрытияФормы, , Объект.Ссылка);
	ИначеЕсли ЗначениеЗаполнено(ВыбраннаяФорма) И (ЗначениеЗаполнено(Организация) Или Не ПроверятьЗаполнениеОрганизации) Тогда 
		Данные = ПолучитьИзВременногоХранилища(АдресДанные);
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("Организация", Организация);
		ПараметрыОткрытия.Вставить("ЗначениеКопирования", ЗначениеКопирования);
		ПараметрыОткрытия.Вставить("UIDФорма1СОтчетность", UIDФорма1СОтчетность);
		ПараметрыОткрытия.Вставить("ИмяОтчета", ИмяОтчета);
		ПараметрыОткрытия.Вставить("Данные", Данные);
		ПараметрыОткрытия.Вставить("ВидУведомления", ВидУведомления);
		ПараметрыОткрытия.Вставить("ЗаполнитьПриОткрытии", ЗаполнитьПриОткрытии);
		Если ПереданаСсылкаНового Тогда 
			ПараметрыОткрытия.Вставить("СсылкаНового", СсылкаНового);
		КонецЕсли;
		
		НоваяФорма = ПолучитьФорму(ВыбраннаяФорма, ПараметрыОткрытия, ВладелецФормы);
		Если НоваяФорма = Неопределено Тогда
			Возврат;
		Иначе
			НоваяФорма.Открыть();
		КонецЕсли;
	ИначеЕсли ЗначениеЗаполнено(ВидУведомления) Тогда
		ИмяФормыСообщения = ПолучитьИмяФормы(ВидУведомления);
		ПолучитьОрганизациюИОткрыть(ВидУведомления, ИмяФормыСообщения);
	ИначеЕсли КопированиеНеПоддерживается Тогда 
		Отказ = Истина;
	Иначе
		Данные = ПолучитьИзВременногоХранилища(АдресДанные);
		ФормаВыбор = ПолучитьФорму("Документ.УведомлениеОСпецрежимахНалогообложения.Форма.ФормаВыбораВидаСообщения", Новый Структура("Организация, Данные", Организация, Данные));
		ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииЗавершение", ЭтотОбъект);
		ФормаВыбор.ОписаниеОповещенияОЗакрытии = ОписаниеОповещения;
		ФормаВыбор.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		ФормаВыбор.Открыть();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатВыбора) = Тип("Структура")
		И РезультатВыбора.Свойство("РезультатВыбора")
		И ЗначениеЗаполнено(РезультатВыбора.РезультатВыбора) Тогда
		
		ИмяФормыСообщения = ПолучитьИмяФормы(РезультатВыбора.РезультатВыбора);
		ПолучитьОрганизациюИОткрыть(РезультатВыбора.РезультатВыбора, ИмяФормыСообщения);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

#КонецОбласти