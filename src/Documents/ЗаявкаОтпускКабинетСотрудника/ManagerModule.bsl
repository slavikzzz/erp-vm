#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов


#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ФизическоеЛицо)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Поля.Добавить("Дата");
	Поля.Добавить("Номер");
	Поля.Добавить("ВариантИсправления");
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	Если Данные.ВариантИсправления = Перечисления.ВариантыИсправленияЗаявокКабинетСотрудника.Отмена Тогда
		Шаблон = НСтр("ru = 'Отмена отпуска %1 от %2';
						|en = 'Отмена отпуска %1 от %2'");
	ИначеЕсли Данные.ВариантИсправления = Перечисления.ВариантыИсправленияЗаявокКабинетСотрудника.Перенос Тогда
		Шаблон = НСтр("ru = 'Перенос отпуска %1 от %2';
						|en = 'Перенос отпуска %1 от %2'");
	Иначе
		Шаблон = НСтр("ru = 'Заявка отпуск %1 от %2';
						|en = 'Заявка отпуск %1 от %2'");
	КонецЕсли;
	Представление = СтрШаблон(Шаблон, Данные.Номер, Данные.Дата);
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

// Возвращает описание состава документа
//
// Возвращаемое значение:
//  Структура - см. ЗарплатаКадрыСоставДокументов.НовоеОписаниеСоставаОбъекта.
Функция ОписаниеСоставаОбъекта() Экспорт
	
	ОписаниеСостава = ЗарплатаКадрыСоставДокументов.НовоеОписаниеСоставаОбъекта();
	ОписаниеСостава.ИспользоватьКраткийСостав = Ложь;
	ОписаниеСостава.ЗаполнятьТабличнуюЧастьФизическиеЛицаДокумента = Ложь;
	ОписаниеСостава.ЗаполнятьФизическиеЛицаПоСотрудникам = Ложь;
	
	ОписаниеХранения = ЗарплатаКадрыСоставДокументов.ОписаниеХраненияСотрудниковФизическихЛиц();
	ОписаниеХранения.ПолеСотрудник      = "Сотрудник";
	ОписаниеХранения.ПолеФизическоеЛицо = "ФизическоеЛицо";
	
	ОписаниеСостава.ОписаниеХраненияСотрудниковФизическихЛиц.Добавить(ОписаниеХранения);
	
	Возврат ОписаниеСостава;
	
	
КонецФункции

Функция ОписаниеДокументаДляЗаявки(СписокОбщихПолей) Экспорт
	
	СтрокаСвойств = "ДатаНачала,ДатаОкончания,ДнейОтпуска,
	|ПредоставитьКомпенсациюОтпуска,КоличествоДнейКомпенсацииОтпуска,
	|ПредоставитьМатериальнуюПомощьПриОтпуске,ПредоставитьЕдиновременнуюВыплатуКОтпуску,
	|ДанныеПоВидамОтпусков";
	
	СписокПолей = СтрШаблон("%1,%2", СписокОбщихПолей, СтрокаСвойств);
	
	Описание = Новый Структура(СписокПолей);
	Описание.ДанныеПоВидамОтпусков = ОписаниеДанныеПоВидамОтпусков();
	
	Возврат Описание;

КонецФункции

Функция БизнесПроцессМенеджер() Экспорт

	Возврат БизнесПроцессы.ИсполнениеЗаявкиСотрудника;

КонецФункции

Функция НаименованиеЗадачи(ДокументОбъект) Экспорт
	
	Если ДокументОбъект.ВариантИсправления = Перечисления.ВариантыИсправленияЗаявокКабинетСотрудника.Отмена Тогда
		ШаблонНаименования = НСтр("ru = 'Отмена отпуска';
									|en = 'Отмена отпуска'");
	ИначеЕсли ДокументОбъект.ВариантИсправления = Перечисления.ВариантыИсправленияЗаявокКабинетСотрудника.Перенос Тогда
		ШаблонНаименования = НСтр("ru = 'Перенос отпуска';
									|en = 'Перенос отпуска'");
	Иначе
		ШаблонНаименования = НСтр("ru = 'Отпуск';
									|en = 'Отпуск'");
	КонецЕсли;
	
	Возврат СтрШаблон("%1: %2", ШаблонНаименования, Строка(ДокументОбъект.ФизическоеЛицо));

КонецФункции

Функция СодержаниеЗадачи(ДокументОбъект) Экспорт
	
	Если ДокументОбъект.ВариантИсправления = Перечисления.ВариантыИсправленияЗаявокКабинетСотрудника.Отмена Тогда
		ШаблонНаименования = НСтр("ru = 'Обработать заявление на отмену отпуска';
									|en = 'Обработать заявление на отмену отпуска'");
	ИначеЕсли ДокументОбъект.ВариантИсправления = Перечисления.ВариантыИсправленияЗаявокКабинетСотрудника.Перенос Тогда
		ШаблонНаименования = НСтр("ru = 'Обработать заявление на перенос отпуска';
									|en = 'Обработать заявление на перенос отпуска'");
	Иначе
		ШаблонНаименования = НСтр("ru = 'Обработать заявление на отпуск';
									|en = 'Обработать заявление на отпуск'");
	КонецЕсли;
	
	Шаблон = НСтр("ru = '%1
	|Организация: %2
	|Сотрудник: %3';
	|en = '%1
	|Организация: %2
	|Сотрудник: %3'");
	
	Возврат СтрШаблон(Шаблон, ШаблонНаименования, Строка(ДокументОбъект.Организация), Строка(ДокументОбъект.Сотрудник));
	
КонецФункции

Функция РольИсполнителяЗаявки() Экспорт

	Возврат Справочники.РолиИсполнителей.ОтветственныйЗаЗаявленияНаОтпуск;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОписаниеДанныеПоВидамОтпусков()

	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("ВидОтпуска", 							Новый ОписаниеТипов(Метаданные.ОпределяемыеТипы.ВидОтпускаКабинетСотрудника.Тип));
	Таблица.Колонки.Добавить("ДатаНачала", 							Новый ОписаниеТипов("Дата"));
	Таблица.Колонки.Добавить("ДатаОкончания", 						Новый ОписаниеТипов("Дата"));
	Таблица.Колонки.Добавить("ДнейОтпуска", 						Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(3, 0)));
	Таблица.Колонки.Добавить("ОтпускБезОплаты", 					Новый ОписаниеТипов("Булево"));
	Таблица.Колонки.Добавить("ПредоставитьКомпенсациюОтпуска", 		Новый ОписаниеТипов("Булево"));
	Таблица.Колонки.Добавить("КоличествоДнейКомпенсацииОтпуска", 	Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(3, 0)));
	
	Возврат Таблица;

КонецФункции

#КонецОбласти

#КонецЕсли