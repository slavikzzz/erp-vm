
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("Структура") Тогда
		ЗаполнитьДокументПоОтбору(ДанныеЗаполнения, СтандартнаяОбработка);
	ИначеЕсли ТипДанныхЗаполнения = Тип("СправочникСсылка.ДоговорыАренды") Тогда
		ЗаполнитьНаОснованииДоговораАренды(ДанныеЗаполнения);
	КонецЕсли;
	
	ИнициализироватьДокумент();
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Комментарий = "";
	
	ОбщегоНазначенияУТ.ОчиститьИдентификаторыДокумента(ЭтотОбъект, "ОС");

	ИнициализироватьДокумент();

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	ЗаполнитьРеквизитыПередЗаписью(РежимЗаписи);
	
	Если Не Отказ И РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ВнеоборотныеАктивыСлужебный.ПроверитьЧтоОСПринятыКУчету(ЭтотОбъект, Отказ);
		Документы.ИзменениеУсловийДоговораАренды.ПроверитьАктуальностьДоговораАренды(Ссылка, Договор, Дата, Отказ);
	КонецЕсли;

	СозданДляПерехода = УчетАрендованныхОС.СозданДляПереходаНаФСБУ25_2018(Дата, Организация);

	ПараметрыВыбораСтатейИАналитик = Документы.ИзменениеУсловийДоговораАренды.ПараметрыВыбораСтатейИАналитик(СозданДляПерехода);
	ДоходыИРасходыСервер.ПередЗаписью(ЭтотОбъект, ПараметрыВыбораСтатейИАналитик);
	
	НастройкаСчетовУчетаСервер.ПередЗаписью(ЭтотОбъект, Документы.ИзменениеУсловийДоговораАренды.ПараметрыНастройкиСчетовУчета());
		
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ПриЗаписиДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	МассивНепроверяемыхРеквизитов.Добавить("ОС.СтоимостьБУ");
	МассивНепроверяемыхРеквизитов.Добавить("ОС.СтоимостьУУ");
	МассивНепроверяемыхРеквизитов.Добавить("ОС.СрокИспользованияУУ");
	МассивНепроверяемыхРеквизитов.Добавить("ОС.Подразделение");
	МассивНепроверяемыхРеквизитов.Добавить("ОС.СтатьяРасходов");
	МассивНепроверяемыхРеквизитов.Добавить("ОС.СправедливаяСтоимость");
	МассивНепроверяемыхРеквизитов.Добавить("ОС.ВыкупнаяСтоимость");
	
	ВспомогательныеРеквизиты = ВспомогательныеРеквизиты();
	
	ВнеоборотныеАктивы.ПроверитьСоответствиеДатыВерсииУчета(ЭтотОбъект, Истина, Отказ);
	ВнеоборотныеАктивы.ПроверитьОтсутствиеДублейВТабличнойЧасти(ЭтотОбъект, "ОС", "ОсновноеСредство", Отказ);
	УчетАрендованныхОС.ПроверитьВозвратИзСубаренды(ЭтотОбъект, Отказ, Истина);
	
	ПараметрыРеквизитовОбъекта = 
		УчетАрендованныхОСКлиентСервер.ЗначенияСвойствЗависимыхРеквизитов_ИзменениеУсловийДоговораАренды(ЭтотОбъект, ВспомогательныеРеквизиты);
	ОбщегоНазначенияУТ.ОтключитьПроверкуЗаполненияРеквизитовОбъекта(ПараметрыРеквизитовОбъекта, МассивНепроверяемыхРеквизитов);
	
	ПроверитьОсновныеСредства(ВспомогательныеРеквизиты, Отказ);
	
	СозданДляПерехода = УчетАрендованныхОС.СозданДляПереходаНаФСБУ25_2018(Дата, Организация);
	
	ПараметрыВыбораСтатейИАналитик = Документы.ИзменениеУсловийДоговораАренды.ПараметрыВыбораСтатейИАналитик(СозданДляПерехода);
	
	ДоходыИРасходыСервер.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, ПараметрыВыбораСтатейИАналитик);
	
	УчетАрендованныхОС.ПроверитьДокументАренды(ЭтотОбъект, ВспомогательныеРеквизиты, МассивНепроверяемыхРеквизитов, Отказ);
	
	ИзменениеУсловийДоговораАрендыЛокализация.ОбработкаПроверкиЗаполнения(ЭтотОбъект, ВспомогательныеРеквизиты, МассивНепроверяемыхРеквизитов, Отказ);
	
	ПроверитьИзменениеДоговора(МассивНепроверяемыхРеквизитов);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеДокументов.ОбработкаПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеДокументов.ОбработкаУдаленияПроведенияДокумента(ЭтотОбъект, Отказ);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Заполнение

Процедура ИнициализироватьДокумент()
	
	Ответственный = Пользователи.ТекущийПользователь();
	Организация = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	
	СтруктураДействий = Новый Структура;
	КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	СтруктураЗаполненияСтавкиНДС = ОбработкаТабличнойЧастиКлиентСервер.ПараметрыЗаполненияСтавкиНДС(ЭтотОбъект);
	СтруктураЗаполненияСтавкиНДС.ИнициализацияВходящегоДокумента = Истина;
	СтруктураДействий.Вставить("СкорректироватьСтавкуНДС", СтруктураЗаполненияСтавкиНДС);
	ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(ЭтотОбъект, СтруктураДействий, КэшированныеЗначения);
	
	Если КэшированныеЗначения.ОбработанныеСтроки.Количество() Тогда
		УчетАрендованныхОСКлиентСервер.ПересчитатьСуммуДокументаАренды(ЭтотОбъект);
		УчетАрендованныхОСКлиентСервер.ПересчитатьСуммуНДСВДокументеАренды(ЭтотОбъект);
	КонецЕсли;
	
	СозданДляПерехода = УчетАрендованныхОС.СозданДляПереходаНаФСБУ25_2018(Дата, Организация);
	
	ПараметрыВыбораСтатейИАналитик = Документы.ИзменениеУсловийДоговораАренды.ПараметрыВыбораСтатейИАналитик(СозданДляПерехода);
	ДоходыИРасходыСервер.ОбработкаЗаполнения(ЭтотОбъект, ПараметрыВыбораСтатейИАналитик);
	
КонецПроцедуры

Процедура ЗаполнитьДокументПоОтбору(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ДанныеЗаполнения.Свойство("Договор")
	 	И ТипЗнч(ДанныеЗаполнения.Договор) = Тип("СправочникСсылка.ДоговорыАренды") Тогда
		
		ЗаполнитьНаОснованииДоговораАренды(ДанныеЗаполнения.Договор);
	КонецЕсли; 
	
КонецПроцедуры

Процедура ЗаполнитьНаОснованииДоговораАренды(ДанныеЗаполнения)
	
	Если НЕ ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыДоговора = ВнеоборотныеАктивыСлужебный.ПроверитьРеквизитыДоговораАренды(ДанныеЗаполнения);

	Если РеквизитыДоговора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Организация = РеквизитыДоговора.Организация;
	Партнер = РеквизитыДоговора.Партнер;
	Контрагент = РеквизитыДоговора.Контрагент;
	Договор = ДанныеЗаполнения;
	
	Документы.ИзменениеУсловийДоговораАренды.ЗаполнитьНаОснованииДоговораАренды(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Процедура ЗаполнитьРеквизитыПередЗаписью(РежимЗаписи)

	ВспомогательныеРеквизиты = ВспомогательныеРеквизиты();

	ОчиститьНеиспользуемыеРеквизиты(ВспомогательныеРеквизиты);

	СуммаДокумента = СуммаСНДС;
	СтоимостьПредметовАренды
		= УчетАрендованныхОСКлиентСервер.СтоимостьПредметовАренды(ЭтотОбъект, ВспомогательныеРеквизиты.РеквизитыДоговора, "ОС");
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		Документы.ИзменениеУсловийДоговораАренды.РассчитатьСтавкуСтоимостьПроценты(
			ЭтотОбъект, ВспомогательныеРеквизиты.РеквизитыДоговора);
		РассчитатьСтоимостьОС(ВспомогательныеРеквизиты.РеквизитыДоговора);
		
	КонецЕсли;
	
	ОбщегоНазначенияУТ.ЗаполнитьИдентификаторыДокумента(ЭтотОбъект, "ОС");
	
КонецПроцедуры

Процедура ОчиститьНеиспользуемыеРеквизиты(ВспомогательныеРеквизиты)
	
	ПараметрыРеквизитовОбъекта = УчетАрендованныхОСКлиентСервер.ЗначенияСвойствЗависимыхРеквизитов_ИзменениеУсловийДоговораАренды(
		ЭтотОбъект, ВспомогательныеРеквизиты);
	
	ОбщегоНазначенияУТКлиентСервер.ОчиститьНеиспользуемыеРеквизиты(
		ЭтотОбъект, 
		ПараметрыРеквизитовОбъекта, 
		"ОС,ГрафикОплатУслуг,ГрафикНачисленияУслуг,ГрафикНачисленияПроцентов");

КонецПроцедуры

Функция ВспомогательныеРеквизиты()

	ВспомогательныеРеквизиты = Новый Структура;
	ВспомогательныеРеквизиты.Вставить("ИспользуетсяУправлениеВНА_2_4", ВнеоборотныеАктивы.ИспользуетсяУправлениеВНА_2_4(Дата));
	ВспомогательныеРеквизиты.Вставить("ИспользуетсяУчетАрендыПоФСБУ25_2018", УчетАрендованныхОС.ИспользуетсяУчетАрендыПоФСБУ25_2018(Организация, Дата, Истина));
	ВспомогательныеРеквизиты.Вставить("НачалоУчетаАрендыПоФСБУ25_2018", УчетАрендованныхОС.НачалоУчетаАрендыПоФСБУ25_2018(Организация));
	ВспомогательныеРеквизиты.Вставить("НезначащаяСтавкаНДС", УчетНДСУП.НезначащаяСтавка(СтавкаНДС));
	ВспомогательныеРеквизиты.Вставить("ОтражатьВОперативномУчете", Истина);
	ВспомогательныеРеквизиты.Вставить("ВерсияДокумента24", Ложь);
	ВспомогательныеРеквизиты.Вставить("ИмяТабличнойЧастиОС", "ОС");
	
	ВалютаУпр = Константы.ВалютаУправленческогоУчета.Получить();
	ВалютаРегл = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Организация);
	ВспомогательныеРеквизиты.Вставить("ВалютаУпр", ВалютаУпр);
	ВспомогательныеРеквизиты.Вставить("ВалютаРегл", ВалютаРегл);
	ВспомогательныеРеквизиты.Вставить("ВалютыСовпадают", ВалютаУпр = ВалютаРегл);

	ВспомогательныеРеквизиты.Вставить("РеквизитыДоговора", УчетАрендованныхОС.РеквизитыДоговораАренды(Договор));

	Возврат ВспомогательныеРеквизиты;
	
КонецФункции

Процедура ПроверитьОсновныеСредства(ВспомогательныеРеквизиты, Отказ)
	
	ТолькоПрекращениеАрендыОС = Истина;
	ТолькоВыкупАрендованныхОС = Истина;

	СписокОС = Новый Массив;
	Для Каждого ДанныеСтроки Из ОС Цикл
		
		Если ЗначениеЗаполнено(ДанныеСтроки.ОсновноеСредство) Тогда
			СписокОС.Добавить(ДанныеСтроки.ОсновноеСредство);
			
			Если ДанныеСтроки.ДействиеСПредметомАренды <> Перечисления.ДействияСПредметамиАренды.ПрекращениеАренды
				И ДанныеСтроки.ДействиеСПредметомАренды <> Перечисления.ДействияСПредметамиАренды.ДосрочныйВыкуп Тогда
				ТолькоПрекращениеАрендыОС = Ложь;
			КонецЕсли;
			Если ДанныеСтроки.ДействиеСПредметомАренды <> Перечисления.ДействияСПредметамиАренды.ДосрочныйВыкуп Тогда
				ТолькоВыкупАрендованныхОС = Ложь;
			КонецЕсли;
			
		КонецЕсли;

	КонецЦикла;
	
	Если СписокОС.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = ИзменениеУсловийДоговораАрендыЛокализация.ТекстЗапросаДляПроверкиОсновныхСредств();
	
	Если ТекстЗапроса = Неопределено Тогда
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ТаблицаОС.НомерСтроки КАК НомерСтроки,
		|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
		|	ТаблицаОС.ДействиеСПредметомАренды КАК ДействиеСПредметомАренды,
		|	ТаблицаОС.Подразделение КАК Подразделение,
		|	ТаблицаОС.СтатьяРасходов КАК СтатьяРасходов,
		|	ТаблицаОС.СправедливаяСтоимость КАК СправедливаяСтоимость,
		|	ТаблицаОС.ВыкупнаяСтоимость КАК ВыкупнаяСтоимость,
		|	ТаблицаОС.СрокИспользованияБУ КАК СрокИспользованияБУ,
		|	ТаблицаОС.СрокИспользованияУУ КАК СрокИспользованияУУ,
		|	ТаблицаОС.Субаренда КАК Субаренда
		|ПОМЕСТИТЬ ТаблицаОС
		|ИЗ
		|	&ТаблицаОС КАК ТаблицаОС
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РезультатПроверки.НомерСтроки КАК НомерСтроки,
		|	РезультатПроверки.ОсновноеСредство КАК ОсновноеСредство,
		|	РезультатПроверки.НеЗаполненоПодразделение КАК НеЗаполненоПодразделение,
		|	РезультатПроверки.НеЗаполненаСтатьяРасходов КАК НеЗаполненаСтатьяРасходов,
		|	РезультатПроверки.НеЗаполненаСправедливаяСтоимость КАК НеЗаполненаСправедливаяСтоимость,
		|	РезультатПроверки.НеЗаполненаВыкупнаяСтоимость КАК НеЗаполненаВыкупнаяСтоимость,
		|	РезультатПроверки.НеЗаполненСрокИспользованияБУ КАК НеЗаполненСрокИспользованияБУ,
		|	РезультатПроверки.НеЗаполненСрокИспользованияУУ КАК НеЗаполненСрокИспользованияУУ
		|ИЗ
		|	(ВЫБРАТЬ
		|		ТаблицаОС.НомерСтроки КАК НомерСтроки,
		|		ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
		|		(ТаблицаОС.ДействиеСПредметомАренды = ЗНАЧЕНИЕ(Перечисление.ДействияСПредметамиАренды.ВзятиеВАренду) ИЛИ ТаблицаОС.Субаренда)
		|			И ТаблицаОС.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК НеЗаполненоПодразделение,
		|		(ТаблицаОС.ДействиеСПредметомАренды = ЗНАЧЕНИЕ(Перечисление.ДействияСПредметамиАренды.ВзятиеВАренду) ИЛИ ТаблицаОС.Субаренда)
		|			И ТаблицаОС.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК НеЗаполненаСтатьяРасходов,
		|		ТаблицаОС.ДействиеСПредметомАренды В (
		|			ЗНАЧЕНИЕ(Перечисление.ДействияСПредметамиАренды.ИзменениеУсловийАренды),
		|			ЗНАЧЕНИЕ(Перечисление.ДействияСПредметамиАренды.ПрекращениеАренды))
		|			И &ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговоровАренды.Аренда)
		|			И ТаблицаОС.СправедливаяСтоимость = 0 КАК НеЗаполненаСправедливаяСтоимость,
		|		ТаблицаОС.ДействиеСПредметомАренды =
		|			ЗНАЧЕНИЕ(Перечисление.ДействияСПредметамиАренды.ДосрочныйВыкуп)
		|			И ТаблицаОС.ВыкупнаяСтоимость = 0 КАК НеЗаполненаВыкупнаяСтоимость,
		|
		|		ЕСТЬNULL(ПорядокУчетаОСБУ.СостояниеБУ, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
		|			И ТаблицаОС.СрокИспользованияБУ = 0 КАК НеЗаполненСрокИспользованияБУ,
		|
		|		ЕСТЬNULL(ПорядокУчетаОСУУ.Состояние, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
		|			И ТаблицаОС.СрокИспользованияУУ = 0 КАК НеЗаполненСрокИспользованияУУ
		|	ИЗ
		|		ТаблицаОС КАК ТаблицаОС
		|
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСБУ.СрезПоследних(
		|				&Дата, 
		|				ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|					И Организация = &Организация
		|					И ОсновноеСредство В
		|						(ВЫБРАТЬ
		|							ОсновноеСредство
		|						ИЗ
		|							ТаблицаОС)) КАК ПорядокУчетаОСБУ
		|				ПО ПорядокУчетаОСБУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
		|
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСУУ.СрезПоследних(
		|				&Дата,
		|				ДатаИсправления = ДАТАВРЕМЯ(1,1,1) 
		|					И Организация = &Организация
		|					И ОсновноеСредство В
		|						(ВЫБРАТЬ
		|							ОсновноеСредство
		|						ИЗ
		|							ТаблицаОС)) КАК ПорядокУчетаОСУУ
		|			ПО ПорядокУчетаОСУУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
		|
		|	) КАК РезультатПроверки
		|
		|ГДЕ
		|	(РезультатПроверки.НеЗаполненСрокИспользованияБУ
		|		ИЛИ РезультатПроверки.НеЗаполненСрокИспользованияУУ
		|		ИЛИ РезультатПроверки.НеЗаполненоПодразделение
		|		ИЛИ РезультатПроверки.НеЗаполненаСтатьяРасходов
		|		ИЛИ РезультатПроверки.НеЗаполненаСправедливаяСтоимость
		|		ИЛИ РезультатПроверки.НеЗаполненаВыкупнаяСтоимость)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Договор", Договор);
	Запрос.УстановитьПараметр("ТаблицаОС", ОС.Выгрузить());
	Запрос.УстановитьПараметр("ИспользуетсяУчетАрендыПоФСБУ25_2018", ВспомогательныеРеквизиты.ИспользуетсяУчетАрендыПоФСБУ25_2018);
	Запрос.УстановитьПараметр("ВалютыСовпадают", ВспомогательныеРеквизиты.ВалютыСовпадают);
	Запрос.УстановитьПараметр("ТипДоговора", ВспомогательныеРеквизиты.РеквизитыДоговора.ТипДоговора);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	ШаблонСообщенияСрокИспользованияБУ = НСтр("ru = 'Не заполнена колонка ""Срок использования (бухгалтерский учет)"" в строке %1 списка ""Предметы аренды""';
												|en = '""Useful life (accounting)"" column in line %1 of the ""Rental objects"" list is not filled'");
	ШаблонСообщенияСрокИспользованияУУ = НСтр("ru = 'Не заполнена колонка ""Срок использования (управленческий учет)"" в строке %1 списка ""Предметы аренды""';
												|en = 'The ""Term of use (management accounting)"" column in line %1 of the list ""Rental objects"" is not filled in'");
	ШаблонСообщенияПодразделение = НСтр("ru = 'Не заполнена колонка ""Подразделение"" в строке %1 списка ""Предметы аренды""';
										|en = 'The Business unit column is not filled in line %1 of the Rental objects list'");
	ШаблонСообщенияСтатьяРасходов = НСтр("ru = 'Не заполнена колонка ""Статья расходов"" в строке %1 списка ""Предметы аренды""';
										|en = 'The Expense item column is not filled in line %1 of the Rental objects list'");
	ШаблонСообщенияСправедливаяСтоимость = НСтр("ru = 'Не заполнена колонка ""Справедливая стоимость"" в строке %1 списка ""Предметы аренды""';
												|en = 'The Fair value column is not filled in line %1 of the Rental objects list'");
	ШаблонСообщенияВыкупнаяСтоимость = НСтр("ru = 'Не заполнена колонка ""Выкупная стоимость"" в строке %1 списка ""Предметы аренды""';
											|en = 'The Buyout value column is not filled in line %1 of the Rental objects list'");
	
	Если ТолькоПрекращениеАрендыОС И НЕ ТолькоВыкупАрендованныхОС Тогда
		ТекстСообщения = НСтр("ru = 'Не может быть договора аренды без предметов аренды. Такая операция оформляется документом ""Прекращение договора аренды""';
								|en = 'A rental contract must contain at least one rental object. Such operation is registered with the Rental termination document'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ОС", "", Отказ);
	КонецЕсли;
	
	Если ТолькоВыкупАрендованныхОС Тогда
		ТекстСообщения = НСтр("ru = 'Для полного выкупа преметов аренды необходимо использовать документ ""Выкуп арендованных ОС""';
								|en = 'For the full redemption of rental objects, use the Vendor invoice — Leased assets document'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ОС", "", Отказ);
	КонецЕсли;
	
	Пока Выборка.Следующий() Цикл
	
		Если Выборка.НеЗаполненСрокИспользованияБУ Тогда
			ТекстСообщения = СтрШаблон(ШаблонСообщенияСрокИспользованияБУ, Выборка.НомерСтроки);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ОС", Выборка.НомерСтроки, "СрокИспользованияБУ");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "", Отказ);
		КонецЕсли;
	
		Если Выборка.НеЗаполненСрокИспользованияУУ Тогда
			ТекстСообщения = СтрШаблон(ШаблонСообщенияСрокИспользованияУУ, Выборка.НомерСтроки);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ОС", Выборка.НомерСтроки, "СрокИспользованияУУ");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "", Отказ);
		КонецЕсли;
		
		Если Выборка.НеЗаполненоПодразделение Тогда
			ТекстСообщения = СтрШаблон(ШаблонСообщенияПодразделение, Выборка.НомерСтроки);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ОС", Выборка.НомерСтроки, "Подразделение");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "", Отказ);
		КонецЕсли;
		
		Если Выборка.НеЗаполненаСтатьяРасходов Тогда
			ТекстСообщения = СтрШаблон(ШаблонСообщенияСтатьяРасходов, Выборка.НомерСтроки);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ОС", Выборка.НомерСтроки, "СтатьяРасходов");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "", Отказ);
		КонецЕсли;
		
		Если Выборка.НеЗаполненаСправедливаяСтоимость Тогда
			ТекстСообщения = СтрШаблон(ШаблонСообщенияСправедливаяСтоимость, Выборка.НомерСтроки);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ОС", Выборка.НомерСтроки, "СправедливаяСтоимость");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "", Отказ);
		КонецЕсли;
		
		Если Выборка.НеЗаполненаВыкупнаяСтоимость Тогда
			ТекстСообщения = СтрШаблон(ШаблонСообщенияВыкупнаяСтоимость, Выборка.НомерСтроки);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ОС", Выборка.НомерСтроки, "ВыкупнаяСтоимость");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "", Отказ);
		КонецЕсли;

		ИзменениеУсловийДоговораАрендыЛокализация.СообщитьОПроверкеОсновныхСредств(Выборка, ЭтотОбъект, Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьИзменениеДоговора(МассивНепроверяемыхРеквизитов)
	
	МассивКонтролируемыхРеквизитов = Новый Массив;
	МассивКонтролируемыхРеквизитов.Добавить("НовыйДоговор");
	МассивКонтролируемыхРеквизитов.Добавить("НовыйКонтрагент");
	МассивКонтролируемыхРеквизитов.Добавить("НовыйПартнер");
	
	НеобходимоПроверять = Ложь;
	Для Каждого КонтролируемыйРеквизит Из МассивКонтролируемыхРеквизитов Цикл
		Если ЗначениеЗаполнено(ЭтотОбъект[КонтролируемыйРеквизит]) Тогда
			НеобходимоПроверять = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ НеобходимоПроверять Тогда
		Для Каждого КонтролируемыйРеквизит Из МассивКонтролируемыхРеквизитов Цикл
			МассивНепроверяемыхРеквизитов.Добавить(КонтролируемыйРеквизит);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура РассчитатьСтоимостьОС(РеквизитыДоговора) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТаблицаОС", ОС.Выгрузить(
		, "ОсновноеСредство, ДействиеСПредметомАренды, Стоимость, СправедливаяСтоимость, НегарантированнаяСтоимость"));
	Запрос.УстановитьПараметр("ЭтоЛизинг", РеквизитыДоговора.ТипДоговора
		= Перечисления.ТипыДоговоровАренды.Лизинг);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("ПериодГраница", Новый Граница(МоментВремени(), ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("Организация", Организация);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаОС.ДействиеСПредметомАренды КАК ДействиеСПредметомАренды,
	|	ВЫБОР
	|		КОГДА &ЭтоЛизинг
	|			ТОГДА ТаблицаОС.Стоимость
	|		ИНАЧЕ ТаблицаОС.СправедливаяСтоимость
	|	КОНЕЦ КАК Стоимость
	|ПОМЕСТИТЬ ТаблицаОС
	|ИЗ
	|	&ТаблицаОС КАК ТаблицаОС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтоимостьОСОстатки.ОсновноеСредство КАК ОсновноеСредство,
	|	СтоимостьОСОстатки.СтоимостьОстаток КАК СтоимостьУУ,
	|	СтоимостьОСОстатки.СтоимостьРеглОстаток + СтоимостьОСОстатки.СтоимостьЦФОстаток КАК СтоимостьБУ,
	|	СтоимостьОСОстатки.СтоимостьНУОстаток КАК СтоимостьНУ
	|ПОМЕСТИТЬ СтоимостьОС
	|ИЗ
	|	РегистрНакопления.СтоимостьОС.Остатки(
	|			&ПериодГраница,
	|			Организация = &Организация
	|				И ОсновноеСредство В
	|					(ВЫБРАТЬ
	|						ТАблицаОС.ОсновноеСредство
	|					ИЗ
	|						ТаблицаОС КАК ТАблицаОС)) КАК СтоимостьОСОстатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство";
	Запрос.Выполнить();
	
	Если РеквизитыДоговора.СпособОпределенияСтоимостиАктивов =
		Перечисления.СпособыОпределенияСтоимостиПредметовАренды.ПоФактическойСтоимости Тогда
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
		|	ЕСТЬNULL(СтоимостьОС.СтоимостьУУ, 0) КАК СтоимостьУУ,
		|	ЕСТЬNULL(СтоимостьОС.СтоимостьБУ, 0) КАК СтоимостьБУ,
		|	ЕСТЬNULL(СтоимостьОС.СтоимостьНУ, 0) КАК СтоимостьНУ
		|ИЗ
		|	ТаблицаОС КАК ТаблицаОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ СтоимостьОС КАК СтоимостьОС
		|		ПО ТаблицаОС.ОсновноеСредство = СтоимостьОС.ОсновноеСредство";
		
	Иначе
		
		МассивДоговоров = УчетАрендованныхОС.СвязанныеДоговорыАренды(Договор);
		Запрос.УстановитьПараметр("МассивДоговоров", МассивДоговоров);
		
		ТаблицаОС = ТаблицаИзмененияСтоимости(Запрос);
		
		Запрос.УстановитьПараметр("ТаблицаОС", ТаблицаОС);
		Коэффициенты = РаботаСКурсамиВалютУТ.ПолучитьКоэффициентыПересчетаВалюты(
		Валюта, ВалютаВзаиморасчетов, Дата, Организация);
		Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуУпр", Коэффициенты.КоэффициентПересчетаВВалютуУпр);
		Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуРегл", Коэффициенты.КоэффициентПересчетаВВалютуРегл);
		
		ИспользуетсяУчетАрендыПоФСБУ25_2018 =
		УчетАрендованныхОС.ИспользуетсяУчетАрендыПоФСБУ25_2018(Организация, Дата, Истина);
		ПараметрыУчетаПредметовАренды = УчетАрендованныхОСКлиентСервер.ПараметрыУчетаПредметовАренды(
			ИспользуетсяУчетАрендыПоФСБУ25_2018, РеквизитыДоговора);
		
		Запрос.УстановитьПараметр("ЕстьУчетУУ", ПараметрыУчетаПредметовАренды.ЕстьУчетУУ);
		Запрос.УстановитьПараметр("ЕстьУчетБУ", ПараметрыУчетаПредметовАренды.ЕстьУчетБУ);
		Запрос.УстановитьПараметр("ЕстьУчетНУ", ПараметрыУчетаПредметовАренды.ЕстьУчетНУ);
		
		Запрос.УстановитьПараметр(
			"ПорогОкругления", Документы.ИзменениеУсловийДоговораАренды.ПорогОкругленияИзмененияПриведеннойСтоимости());
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	втИзменениеСтоимостиОС.ОсновноеСредство КАК ОсновноеСредство,
		|	ВЫБОР
		|		КОГДА втИзменениеСтоимостиОС.Сумма > 0
		|					И втИзменениеСтоимостиОС.Сумма >= &ПорогОкругления
		|				ИЛИ втИзменениеСтоимостиОС.Сумма < 0
		|					И втИзменениеСтоимостиОС.Сумма <= -&ПорогОкругления
		|			ТОГДА втИзменениеСтоимостиОС.Сумма
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Сумма
		|ПОМЕСТИТЬ втИзменениеСтоимостиОСПредварительная
		|ИЗ
		|	&ТаблицаОС КАК втИзменениеСтоимостиОС
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИзменениеСтоимостиОС.ОсновноеСредство КАК ОсновноеСредство,
		|	ВЫБОР
		|		КОГДА &ЕстьУчетУУ
		|			ТОГДА ИзменениеСтоимостиОС.Сумма * &КоэффициентПересчетаВВалютуУпр
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СуммаУУ,
		|	ВЫБОР
		|		КОГДА &ЕстьУчетБУ
		|			ТОГДА ИзменениеСтоимостиОС.Сумма * &КоэффициентПересчетаВВалютуРегл
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СуммаБУ,
		|	ВЫБОР
		|		КОГДА &ЕстьУчетНУ
		|			ТОГДА ИзменениеСтоимостиОС.Сумма * &КоэффициентПересчетаВВалютуРегл
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СуммаНУ
		|ПОМЕСТИТЬ втИзменениеСтоимостиОС
		|ИЗ
		|	втИзменениеСтоимостиОСПредварительная КАК ИзменениеСтоимостиОС
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	УсловияДоговоровАренды.Регистратор КАК Ссылка,
		|	УсловияДоговоровАренды.Период КАК Период
		|ПОМЕСТИТЬ втПредыдущийДокументИзменения
		|ИЗ
		|	РегистрСведений.УсловияДоговоровАренды КАК УсловияДоговоровАренды
		|ГДЕ
		|	УсловияДоговоровАренды.Период МЕЖДУ НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ) И &Период
		|	И УсловияДоговоровАренды.Регистратор ССЫЛКА Документ.ИзменениеУсловийДоговораАренды
		|	И УсловияДоговоровАренды.Регистратор <> &Ссылка
		|	И УсловияДоговоровАренды.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияДоговоровКонтрагентов.Действует)
		|	И УсловияДоговоровАренды.Активность
		|	И УсловияДоговоровАренды.Договор В(&МассивДоговоров)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период УБЫВ
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПредыдущаяТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
		|	ПредыдущаяТаблицаОС.СтоимостьБУ КАК СтоимостьБУ,
		|	ПредыдущаяТаблицаОС.СтоимостьНУ КАК СтоимостьНУ,
		|	ПредыдущаяТаблицаОС.СтоимостьУУ КАК СтоимостьУУ
		|ПОМЕСТИТЬ ПредыдущаяТаблицаОС
		|ИЗ
		|	Документ.ИзменениеУсловийДоговораАренды.ОС КАК ПредыдущаяТаблицаОС
		|ГДЕ
		|	ПредыдущаяТаблицаОС.Ссылка В
		|			(ВЫБРАТЬ
		|				втПредыдущийДокументИзменения.Ссылка КАК Ссылка
		|			ИЗ
		|				втПредыдущийДокументИзменения КАК втПредыдущийДокументИзменения)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ПредыдущаяТаблицаОС.СтоимостьУУ, ЕСТЬNULL(СтоимостьОС.СтоимостьУУ, 0)) + ИзменениеСтоимостиОС.СуммаУУ > 0
		|			ТОГДА ЕСТЬNULL(ПредыдущаяТаблицаОС.СтоимостьУУ, ЕСТЬNULL(СтоимостьОС.СтоимостьУУ, 0)) + ИзменениеСтоимостиОС.СуммаУУ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СтоимостьУУ,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ПредыдущаяТаблицаОС.СтоимостьБУ, ЕСТЬNULL(СтоимостьОС.СтоимостьБУ, 0)) + ИзменениеСтоимостиОС.СуммаБУ > 0
		|			ТОГДА ЕСТЬNULL(ПредыдущаяТаблицаОС.СтоимостьБУ, ЕСТЬNULL(СтоимостьОС.СтоимостьБУ, 0)) + ИзменениеСтоимостиОС.СуммаБУ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СтоимостьБУ,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ПредыдущаяТаблицаОС.СтоимостьНУ, ЕСТЬNULL(СтоимостьОС.СтоимостьНУ, 0)) + ИзменениеСтоимостиОС.СуммаНУ > 0
		|			ТОГДА ЕСТЬNULL(ПредыдущаяТаблицаОС.СтоимостьНУ, ЕСТЬNULL(СтоимостьОС.СтоимостьНУ, 0)) + ИзменениеСтоимостиОС.СуммаНУ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СтоимостьНУ
		|ИЗ
		|	ТаблицаОС КАК ТаблицаОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ втИзменениеСтоимостиОС КАК ИзменениеСтоимостиОС
		|		ПО ТаблицаОС.ОсновноеСредство = ИзменениеСтоимостиОС.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПредыдущаяТаблицаОС КАК ПредыдущаяТаблицаОС
		|		ПО ТаблицаОС.ОсновноеСредство = ПредыдущаяТаблицаОС.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ СтоимостьОС КАК СтоимостьОС
		|		ПО ТаблицаОС.ОсновноеСредство = СтоимостьОС.ОсновноеСредство";
		
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();

	Пока Выборка.Следующий() Цикл
		СтрокаОС = ОС.Найти(Выборка.ОсновноеСредство, "ОсновноеСредство");
		ЗаполнитьЗначенияСвойств(СтрокаОС, Выборка, "СтоимостьУУ, СтоимостьБУ, СтоимостьНУ");
	КонецЦикла;
	
КонецПроцедуры

Функция ТаблицаИзмененияСтоимости(Запрос)
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	УсловияДоговоровАренды.АктуальныеУсловияДоговора КАК АктуальныеУсловияДоговора,
	|	УсловияДоговоровАренды.ДатаНачалаАренды КАК ДатаНачалаАренды,
	|	УсловияДоговоровАренды.ПроцентнаяСтавка КАК ПроцентнаяСтавка,
	|	УсловияДоговоровАренды.Период КАК Период
	|ПОМЕСТИТЬ втПредыдущиеУсловия
	|ИЗ
	|	РегистрСведений.УсловияДоговоровАренды.СрезПоследних(
	|			&Период,
	|			Регистратор <> &Ссылка
	|				И Договор В (&МассивДоговоров)
	|				И Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияДоговоровКонтрагентов.Действует)) КАК УсловияДоговоровАренды
	|УПОРЯДОЧИТЬ ПО
	|	Период УБЫВ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ГрафикОплатУслугПоАренде.УслугаПоАренде
	|		- ГрафикОплатУслугПоАренде.УслугаПоАрендеНДС
	|		+ ГрафикОплатУслугПоАренде.ОбеспечительныйПлатеж
	|		- ГрафикОплатУслугПоАренде.ОбеспечительныйПлатежНДС
	|		+ ГрафикОплатУслугПоАренде.ВыкупнаяСтоимость
	|		+ ГрафикОплатУслугПоАренде.ВыкупнаяСтоимостьАванс
	|		- ГрафикОплатУслугПоАренде.ВыкупнаяСтоимостьНДС) КАК Сумма
	|ПОМЕСТИТЬ втОстаткиОплат
	|ИЗ
	|	втПредыдущиеУсловия КАК втПредыдущиеУсловия
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикОплатУслугПоАренде КАК ГрафикОплатУслугПоАренде
	|		ПО втПредыдущиеУсловия.АктуальныеУсловияДоговора = ГрафикОплатУслугПоАренде.АктуальныеУсловияДоговора
	|			И (ГрафикОплатУслугПоАренде.Дата >= НАЧАЛОПЕРИОДА(&Период, ДЕНЬ))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА( ГрафикНачисленияУслугПоАренде.ОбеспечительныйПлатеж
	|		- ГрафикНачисленияУслугПоАренде.ОбеспечительныйПлатежНДС) КАК ОбеспечительныйПлатеж
	|ПОМЕСТИТЬ втОстаткиНачислений
	|ИЗ
	|	втПредыдущиеУсловия КАК втПредыдущиеУсловия
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикНачисленияУслугПоАренде КАК ГрафикНачисленияУслугПоАренде
	|		ПО втПредыдущиеУсловия.АктуальныеУсловияДоговора = ГрафикНачисленияУслугПоАренде.АктуальныеУсловияДоговора
	|			И (ГрафикНачисленияУслугПоАренде.Дата >= НАЧАЛОПЕРИОДА(&Период, ДЕНЬ))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикПроцентов.Проценты КАК Проценты
	|ПОМЕСТИТЬ ГрафикПроцентов
	|ИЗ
	|	&ГрафикПроцентов КАК ГрафикПроцентов
	|ГДЕ
	|	ГрафикПроцентов.Дата >= ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&Период, ДЕНЬ), ДЕНЬ, -1)
	|		И ГрафикПроцентов.Дата < НАЧАЛОПЕРИОДА(&Период, ДЕНЬ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ГрафикПроцентов.СуммаПроцентов) КАК Сумма
	|ПОМЕСТИТЬ втОстаткиПроцентов
	|ИЗ
	|	(ВЫБРАТЬ
	|		-ЕСТЬNULL(ГрафикПроцентов.Проценты, 0) КАК СуммаПроцентов
	|	ИЗ
	|		ГрафикПроцентов КАК ГрафикПроцентов
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЕСТЬNULL(ГрафикНачисленияПроцентовПоАренде.Проценты, 0)
	|	ИЗ
	|		втПредыдущиеУсловия КАК втПредыдущиеУсловия
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикНачисленияПроцентовПоАренде КАК ГрафикНачисленияПроцентовПоАренде
	|			ПО втПредыдущиеУсловия.АктуальныеУсловияДоговора = ГрафикНачисленияПроцентовПоАренде.АктуальныеУсловияДоговора
	|				И (ГрафикНачисленияПроцентовПоАренде.Дата >= ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&Период, ДЕНЬ), ДЕНЬ, -1))) КАК ГрафикПроцентов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(втОстаткиОплат.Сумма, 0)
	|		+ ЕСТЬNULL(втОстаткиНачислений.ОбеспечительныйПлатеж, 0)
	|		- ЕСТЬNULL(втОстаткиПроцентов.Сумма, 0) КАК СтоимостьОстаток,
	|	&ПриведеннаяСтоимость
	|		+ ЕСТЬNULL(втОстаткиНачислений.ОбеспечительныйПлатеж, 0) КАК Стоимость
	|ИЗ
	|	втПредыдущиеУсловия КАК втПредыдущиеУсловия
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиОплат КАК втОстаткиОплат
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиНачислений КАК втОстаткиНачислений
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиПроцентов КАК втОстаткиПроцентов
	|		ПО (ИСТИНА)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	0 КАК Сумма,
	|	ВЫБОР
	|		КОГДА ТаблицаОС.ДействиеСПредметомАренды <> ЗНАЧЕНИЕ(Перечисление.ДействияСПредметамиАренды.ВзятиеВАренду)
	|			ТОГДА ТаблицаОС.Стоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоэффициентРаспределенияОстатков,
	|	ВЫБОР
	|		КОГДА ТаблицаОС.ДействиеСПредметомАренды <> ЗНАЧЕНИЕ(Перечисление.ДействияСПредметамиАренды.ПрекращениеАренды)
	|				И ТаблицаОС.ДействиеСПредметомАренды <> ЗНАЧЕНИЕ(Перечисление.ДействияСПредметамиАренды.ДосрочныйВыкуп)
	|			ТОГДА ТаблицаОС.Стоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоэффициентРаспределенияСтоимости
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС";
	
	Запрос.УстановитьПараметр("ГрафикПроцентов", ГрафикНачисленияПроцентов.Выгрузить());
	Запрос.УстановитьПараметр("ПриведеннаяСтоимость", ПриведеннаяСтоимость);
	
	Результаты = Запрос.ВыполнитьПакет();
	
	ТаблицаОС = Результаты[Результаты.ВГраница()].Выгрузить();
	
	Выборка = Результаты[Результаты.ВГраница()-1].Выбрать();
	Выборка.Следующий();
	
	Коэффициенты = ТаблицаОС.ВыгрузитьКолонку("КоэффициентРаспределенияОстатков");

	РаспределениеОстатковСтоимости = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(
		Выборка.СтоимостьОстаток, Коэффициенты);

	Коэффициенты = ТаблицаОС.ВыгрузитьКолонку("КоэффициентРаспределенияСтоимости");

	РаспределениеСтоимости = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(
		Выборка.Стоимость, Коэффициенты);

	Для ИндексСтроки = 0 По ТаблицаОС.Количество()-1 Цикл
		
		ДанныеСтроки = ТаблицаОС.Получить(ИндексСтроки);
		
		Если РаспределениеСтоимости <> Неопределено ИЛИ РаспределениеОстатковСтоимости <> Неопределено Тогда
			ОстатокСтоимостиОС = ?(РаспределениеОстатковСтоимости <> Неопределено, РаспределениеОстатковСтоимости[ИндексСтроки], 0);
			СтоимостьОС = ?(РаспределениеСтоимости <> Неопределено, РаспределениеСтоимости[ИндексСтроки], 0);
			ДанныеСтроки.Сумма = СтоимостьОС - ОстатокСтоимостиОС;
		КонецЕсли;
		
	КонецЦикла;

	Возврат ТаблицаОС;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
