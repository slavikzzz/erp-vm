
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	
	МеханизмыДокумента.Добавить("ОсновныеСредства");
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	МеханизмыДокумента.Добавить("РеестрДокументов");
	МеханизмыДокумента.Добавить("УчетИмпорта");
	
	ИзменениеУсловийДоговораАрендыЛокализация.ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента);
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка, ДокументОбъект - ссылка на документ или объект, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура:
//     * Ключ - Строка - Имя таблицы.
//     * Значение - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Если ТипЗнч(Документ) = Тип("ДокументОбъект.ИзменениеУсловийДоговораАренды") Тогда
		ДокументСсылка = Документ.Ссылка;
	Иначе
		ДокументСсылка = Документ;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		
		ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДопПараметры);
		
		ТекстЗапросаТаблицаПараметрыАмортизацииОСУУ(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаСтоимостьОС(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаАмортизацияОС(Запрос, ТекстыЗапроса, Регистры);

		ТекстЗапросаТаблицаАрендованныеОС(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаУсловияДоговоровАренды(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаГрафикОплатУслугПоАренде(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаГрафикНачисленияУслугПоАренде(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаГрафикНачисленияПроцентовПоАренде(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаРасчетыПоФинансовымИнструментам(Запрос, ТекстыЗапроса, Регистры);
		
		ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры);
		
		ТекстЗапросаТаблицаДвиженияПоПрочимАктивамПассивам(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПартииПрочихРасходов(Запрос, ТекстыЗапроса, Регистры);
	
		ТекстЗапросаТаблицаПрочиеДоходы(Запрос, ТекстыЗапроса, Регистры);
		
		ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, Регистры);
		
		ТекстЗапросаТаблицаТоварыКОформлениюДокументовИмпорта(Запрос, ТекстыЗапроса, Регистры);
		
		//++ НЕ УТКА
		ТекстЗапросаТаблицаОтражениеДокументовВМеждународномУчете(ТекстыЗапроса, Регистры);
		//-- НЕ УТКА
		
		ИзменениеУсловийДоговораАрендыЛокализация.ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры);
	
	КонецЕсли;

	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

// Формирует таблицы движений при отложенном проведении.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ИзменениеУсловийДоговораАренды - Документ, для которого формируются движения.
//  МенеджерВременныхТаблиц	- МенеджерВременныхТаблиц - Содержит вспомогательные временные таблицы, которые могут использоваться для формирования движений.
//
// Возвращаемое значение:
//  см. ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения
//
Функция ТаблицыОтложенногоФормированияДвижений(ДокументСсылка, МенеджерВременныхТаблиц) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, Неопределено);
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	ТекстЗапросаТаблицаПараметрыАмортизацииОСУУ(Запрос, ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПараметрыАмортизацииОСБУ(Запрос, ТекстыЗапроса);
	ТекстЗапросаТаблицаСтоимостьОС(Запрос, ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаАмортизацияОС(Запрос, ТекстыЗапроса, Неопределено);

	ТекстЗапросаТаблицаУсловияДоговоровАренды(ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаРасчетыПоФинансовымИнструментам(Запрос, ТекстыЗапроса, Неопределено);
	
	ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Неопределено);
	
	ТекстЗапросаТаблицаДвиженияПоПрочимАктивамПассивам(Запрос, ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПартииПрочихРасходов(Запрос, ТекстыЗапроса, Неопределено);
	
	ТекстЗапросаТаблицаПрочиеДоходы(Запрос, ТекстыЗапроса, Неопределено);

	//++ НЕ УТКА
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаОтражениеДокументовВМеждународномУчете(ТекстыЗапроса, Неопределено);
	//-- НЕ УТКА
	
	ИзменениеУсловийДоговораАрендыЛокализация.ТаблицыОтложенногоФормированияДвижений(ТекстыЗапроса);

	ТаблицыДвижений = ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса);
	
	Возврат ТаблицыДвижений;
	
КонецФункции

#КонецОбласти

#Область Команды

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Документы.ПринятиеКУчетуОС2_4.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Документы.СписаниеОС2_4.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Документы.ВыкупАрендованныхОС.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Документы.ИзменениеУсловийПередачиВАренду.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);

КонецПроцедуры

// Добавляет команду создания документа "Изменение условий договора аренды".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Порядок - Число -
//
// Возвращаемое значение:
//	СтрокаТаблицыЗначений - добавленная команда
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании, Порядок = 0) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.ИзменениеУсловийДоговораАренды) Тогда
		
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.ИзменениеУсловийДоговораАренды.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.ИзменениеУсловийДоговораАренды);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьВнеоборотныеАктивы2_4";
		КомандаСоздатьНаОсновании.Порядок = Порядок;
	
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;

	Возврат Неопределено;
	
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки	 - Структура - настройки подсистемы.
//
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#Область Прочее

// Возвращает параметры механизма взаиморасчетов.
//
// Параметры:
// 	ДанныеЗаполнения - ДокументОбъект, СправочникОбъект, ДокументСсылка, СправочникСсылка, Структура, ДанныеФормыСтруктура - Объект или коллекция для
//              расчета параметров взаиморасчетов.
//
// Возвращаемое значение:
// 	См. ВзаиморасчетыСервер.ПараметрыМеханизма
//
Функция ПараметрыВзаиморасчеты(ДанныеЗаполнения = Неопределено) Экспорт
	
	СтруктураПараметров = ВзаиморасчетыСервер.ПараметрыМеханизма();
	
	СтруктураПараметров.ЭтоПродажаЗакупка                = Истина;
	СтруктураПараметров.ИзменяетПланОплаты               = Ложь;
	СтруктураПараметров.ИзменяетПланОтгрузкиПоставки     = Ложь;

	#Область НеобязательныеПараметры
	
	СтруктураПараметров.ВалютаВзаиморасчетов             = "Объект.ВалютаВзаиморасчетов";
	СтруктураПараметров.СуммаВзаиморасчетов              = "Объект.СуммаВзаиморасчетов";
	СтруктураПараметров.НаправлениеДеятельности          = "";
	СтруктураПараметров.ПорядокРасчетов                  = "";
	СтруктураПараметров.ПутьКДаннымТЧ                    = "";
	СтруктураПараметров.Соглашение                       = "";
	СтруктураПараметров.БанковскийСчетОрганизации        = "";
	СтруктураПараметров.БанковскийСчетКонтрагента        = "";
	СтруктураПараметров.Касса                            = "";
	СтруктураПараметров.ФормаОплаты                      = "";
	СтруктураПараметров.ОплатаВВалюте                    = "";
	СтруктураПараметров.ИдентификаторПлатежа             = "";
	СтруктураПараметров.Договор                          = "";
	
	// Реквизиты для объекта расчетов, используются в проведении.
	СтруктураПараметров.ГруппаФинансовогоУчета           = "";
	СтруктураПараметров.Подразделение                    = "";
	СтруктураПараметров.КурсЧислитель                    = "Объект.КурсЧислитель";
	СтруктураПараметров.КурсЗнаменатель                  = "Объект.КурсЗнаменатель";
	
	СтруктураПараметров.ЭлементыФормы.НадписьВалюты = "ДекорацияВалюты";
	СтруктураПараметров.ВалютыИКурсДокументаТолькоПросмотр = Ложь;
	СтруктураПараметров.ВалютаДокументаТолькоПросмотр = Ложь;
	
	СтруктураПараметров.ВалютаВзаиморасчетовТолькоПросмотр = Истина;
	
	#КонецОбласти
	
	Возврат СтруктураПараметров;

КонецФункции


// Возвращает список основных средств, для которых необходима проверка на принятие к учету
//
// Параметры:
//  Объект	 - ДокументОбъект.ИзменениеУсловийДоговораАренды - 
// 
// Возвращаемое значение:
//  Массив из СправочникСсылка.ОбъектыЭксплуатации
//
Функция СписокПроверяемыхОС(Объект) Экспорт
	
	СписокОС = Новый Массив;
	
	Для Каждого СтрокаОС Из Объект.ОС Цикл
		Если СтрокаОС.ДействиеСПредметомАренды <> Перечисления.ДействияСПредметамиАренды.ВзятиеВАренду Тогда
			СписокОС.Добавить(СтрокаОС.ОсновноеСредство);
		КонецЕсли;
	КонецЦикла;
	
	Возврат СписокОС;

КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "Документы.ИзменениеУсловийДоговораАренды.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.5.15.15";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("5e24d87f-b3dc-4000-a88d-a84d154e9050");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Документы.ИзменениеУсловийДоговораАренды.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Порядок = Перечисления.ПорядокОбработчиковОбновления.Обычный;
	Обработчик.Комментарий = НСтр("ru = 'Обновляет документы ""Изменение условий договора аренды"":
	|- Заполняет реквизиты ""Действие"" в табличной части ""Предметы аренды""';
	|en = 'Updates the Rental term adjustment documents:
	|- Fills the Action attributes in the Rental objects table'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Документы.ИзменениеУсловийДоговораАренды.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.Документы.ИзменениеУсловийДоговораАренды.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.Документы.ИзменениеУсловийДоговораАренды.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");

КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаОбъектов = "Документ.ИзменениеУсловийДоговораАренды";
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Ссылка.Дата УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Ссылка");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиСсылки();
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды.ОС КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.ДействиеСПредметомАренды = ЗНАЧЕНИЕ(Перечисление.ДействияСПредметамиАренды.ПустаяСсылка)
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокумента.Ссылка";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
		"Документ.ИзменениеУсловийДоговораАренды.ОбработатьДанныеДляПереходаНаНовуюВерсию");

	ПолноеИмяОбъекта = "Документ.ИзменениеУсловийДоговораАренды";
	
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	ПроблемныхОбъектов = 0;
	ОбъектовОбработано = 0;
	
	СтруктураОтбора = Новый Структура("ДействиеСПредметомАренды", 
						Перечисления.ДействияСПредметамиАренды.ПустаяСсылка());
	Для Каждого Документ Из ОбновляемыеДанные Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Документ.Ссылка);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			
			Блокировка.Заблокировать();
			
			ДокументОбъект = Документ.Ссылка.ПолучитьОбъект(); // ДокументОбъект.ИзменениеУсловийДоговораАренды
			Если ДокументОбъект = Неопределено Тогда
				ЗафиксироватьТранзакцию();
				Продолжить;
			КонецЕсли;
			
			СтрокиОС = ДокументОбъект.ОС.НайтиСтроки(СтруктураОтбора);
			Для Каждого СтрокаОС Из СтрокиОС Цикл
				СтрокаОС.ДействиеСПредметомАренды = Перечисления.ДействияСПредметамиАренды.ИзменениеУсловийАренды;
			КонецЦикла;

			Если ДокументОбъект.Модифицированность() Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(ДокументОбъект.Ссылка);
			КонецЕсли;
			
			ОбъектовОбработано = ОбъектовОбработано + 1;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;

			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), Документ.Ссылка);
			
		КонецПопытки;
		
	КонецЦикла;
	
 	ВнеоборотныеАктивыСлужебный.ПроверитьВыполнениеОбработчика(
 		ПроблемныхОбъектов, 
 		ОбъектовОбработано, 
 		ПолноеИмяОбъекта);
 		
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, ОбъектовОбработано);	
	
КонецПроцедуры

#КонецОбласти

#Область Проведение

Процедура ИнициализироватьКлючиАналитикиУчетаПоПартнерам(Запрос)
	
	Если Запрос.Параметры.Свойство("КлючиАналитикиУчетаПоПартнерамИнициализированы") Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Организация     КАК Организация,
	|	ДанныеДокумента.НовыйПартнер    КАК Партнер,
	|	ДанныеДокумента.НовыйКонтрагент КАК Контрагент,
	|	ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) КАК Договор,
	|	ДанныеДокумента.НовыйДоговор.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК РегистрАналитикаУчетаПоПартнерам
	|		ПО ДанныеДокумента.Организация = РегистрАналитикаУчетаПоПартнерам.Организация
	|			И &Партнер = РегистрАналитикаУчетаПоПартнерам.Партнер
	|			И &Контрагент = РегистрАналитикаУчетаПоПартнерам.Контрагент
	|			И ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) = РегистрАналитикаУчетаПоПартнерам.Договор
	|			И ВЫРАЗИТЬ(&Договор КАК Справочник.ДоговорыАренды).НаправлениеДеятельности = РегистрАналитикаУчетаПоПартнерам.НаправлениеДеятельности
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И ДанныеДокумента.НовыйДоговор <> ЗНАЧЕНИЕ(Справочник.ДоговорыАренды.ПустаяСсылка)
	|	И РегистрАналитикаУчетаПоПартнерам.КлючАналитики ЕСТЬ NULL";
	
	ЗапросАналитик = Новый Запрос(ТекстЗапроса);
	Для Каждого ПараметрЗапроса Из Запрос.Параметры Цикл
		ЗапросАналитик.УстановитьПараметр(ПараметрЗапроса.Ключ, ПараметрЗапроса.Значение);
	КонецЦикла;
	РезультатЗапроса = ЗапросАналитик.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		РегистрыСведений.АналитикаУчетаПоПартнерам.СоздатьКлючАналитики(Выборка);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("КлючиАналитикиУчетаПоПартнерамИнициализированы", Истина);
	
КонецПроцедуры

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.ИзменениеУсловийДоговораАренды";
	ТекстыЗапросаВременныхТаблиц = Новый Соответствие();
	
	ЗначенияПараметров = ЗначенияПараметровПроведения();
	
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("Подразделение", "ДанныеДокумента.Договор.Подразделение");
	
	ВЗапросеЕстьИсточник = Истина;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	ИначеЕсли ИмяРегистра = "ДокументыПоОС" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	КонецЕсли;
		
	Если ТекстЗапроса = Неопределено Тогда
		
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';
								|en = 'In document %ПолноеИмяДокумента%, adaptation of request for generating records of register %ИмяРегистра% is not implemented.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов"
		ИЛИ ИмяРегистра = "ДокументыПоОС" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ВЗапросеЕстьИсточник,
										ПереопределениеРасчетаПараметров);
	Иначе	
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ПереопределениеРасчетаПараметров,
										ТекстыЗапросаВременныхТаблиц);
	КонецЕсли; 

	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДопПараметры)
	
	ПроведениеДокументов.ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДопПараметры);
	
	ПолучитьДанныеДокумента(Запрос);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                                     КАК Ссылка,
	|	ДанныеДокумента.Дата                                       КАК Период,
	|	ДанныеДокумента.Номер                                      КАК Номер,
	|	ДанныеДокумента.Организация                                КАК Организация,
	|	ВЫБОР КОГДА ДанныеДокумента.НовыйПартнер = ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка) ТОГДА
	|		ДанныеДокумента.Партнер
	|	ИНАЧЕ 
	|		ДанныеДокумента.НовыйПартнер
	|	КОНЕЦ КАК Партнер,
	|	ВЫБОР КОГДА ДанныеДокумента.НовыйКонтрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) ТОГДА
	|		ДанныеДокумента.Контрагент
	|	ИНАЧЕ
	|		ДанныеДокумента.НовыйКонтрагент 
	|	КОНЕЦ КАК Контрагент,
	|	ВЫБОР КОГДА ДанныеДокумента.НовыйДоговор = ЗНАЧЕНИЕ(Справочник.ДоговорыАренды.ПустаяСсылка) ТОГДА
	|		ДанныеДокумента.Договор
	|	ИНАЧЕ
	|		ДанныеДокумента.НовыйДоговор
	|	КОНЕЦ КАК Договор,
	|	ВЫБОР КОГДА ДанныеДокумента.НовыйДоговор = ЗНАЧЕНИЕ(Справочник.ДоговорыАренды.ПустаяСсылка) ТОГДА
	|		ДанныеДокумента.Договор.ТипДоговора
	|	ИНАЧЕ
	|		ДанныеДокумента.НовыйДоговор.ТипДоговора
	|	КОНЕЦ КАК ТипДоговора,
	|	ВЫБОР КОГДА ДанныеДокумента.НовыйДоговор = ЗНАЧЕНИЕ(Справочник.ДоговорыАренды.ПустаяСсылка) ТОГДА
	|		ДанныеДокумента.Договор.Балансодержатель
	|	ИНАЧЕ
	|		ДанныеДокумента.НовыйДоговор.Балансодержатель
	|	КОНЕЦ КАК Балансодержатель,
	|	ВЫБОР КОГДА ДанныеДокумента.НовыйДоговор = ЗНАЧЕНИЕ(Справочник.ДоговорыАренды.ПустаяСсылка) ТОГДА
	|		ДанныеДокумента.Договор.НаправлениеДеятельности
	|	ИНАЧЕ
	|		ДанныеДокумента.НовыйДоговор.НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	ВЫБОР КОГДА ДанныеДокумента.НовыйДоговор = ЗНАЧЕНИЕ(Справочник.ДоговорыАренды.ПустаяСсылка) ТОГДА
	|		ДанныеДокумента.Договор.УчитыватьПредметыАрендыВРеглУчете
	|	ИНАЧЕ
	|		ДанныеДокумента.НовыйДоговор.УчитыватьПредметыАрендыВРеглУчете
	|	КОНЕЦ КАК УчитыватьПредметыАрендыВРеглУчете,
	|	ВЫБОР КОГДА ДанныеДокумента.НовыйДоговор = ЗНАЧЕНИЕ(Справочник.ДоговорыАренды.ПустаяСсылка) ТОГДА
	|		ДанныеДокумента.Договор.УчитыватьПредметыАрендыВУпрУчете
	|	ИНАЧЕ
	|		ДанныеДокумента.НовыйДоговор.УчитыватьПредметыАрендыВУпрУчете
	|	КОНЕЦ КАК УчитыватьПредметыАрендыВУпрУчете,
	|	ВЫБОР КОГДА ДанныеДокумента.НовыйДоговор = ЗНАЧЕНИЕ(Справочник.ДоговорыАренды.ПустаяСсылка) ТОГДА
	|		ДанныеДокумента.Договор.Подразделение
	|	ИНАЧЕ
	|		ДанныеДокумента.НовыйДоговор.Подразделение
	|	КОНЕЦ КАК Подразделение,
	|	ВЫБОР КОГДА ДанныеДокумента.НовыйДоговор = ЗНАЧЕНИЕ(Справочник.ДоговорыАренды.ПустаяСсылка) ТОГДА
	|		ДанныеДокумента.Договор.СтатьяРасходовПроценты
	|	ИНАЧЕ
	|		ДанныеДокумента.НовыйДоговор.СтатьяРасходовПроценты
	|	КОНЕЦ КАК СтатьяРасходовПроценты,
	|	ВЫБОР КОГДА ДанныеДокумента.НовыйДоговор = ЗНАЧЕНИЕ(Справочник.ДоговорыАренды.ПустаяСсылка) ТОГДА
	|		ДанныеДокумента.Договор.АналитикаРасходовПроценты
	|	ИНАЧЕ
	|		ДанныеДокумента.НовыйДоговор.АналитикаРасходовПроценты
	|	КОНЕЦ КАК АналитикаРасходовПроценты,
	|	ВЫБОР КОГДА ДанныеДокумента.НовыйДоговор = ЗНАЧЕНИЕ(Справочник.ДоговорыАренды.ПустаяСсылка) ТОГДА
	|		ДанныеДокумента.Договор.ПодразделениеПроценты
	|	ИНАЧЕ
	|		ДанныеДокумента.НовыйДоговор.ПодразделениеПроценты
	|	КОНЕЦ КАК ПодразделениеПроценты,
	|	ВЫБОР КОГДА ДанныеДокумента.НовыйДоговор = ЗНАЧЕНИЕ(Справочник.ДоговорыАренды.ПустаяСсылка) ТОГДА
	|		ДанныеДокумента.Договор.СпособОпределенияСтоимостиАктивов
	|	ИНАЧЕ
	|		ДанныеДокумента.НовыйДоговор.СпособОпределенияСтоимостиАктивов
	|	КОНЕЦ КАК СпособОпределенияСтоимостиАктивов,
	|	ДанныеДокумента.Партнер КАК СтарыйПартнер,
	|	ДанныеДокумента.Контрагент КАК СтарыйКонтрагент,
	|	ДанныеДокумента.Договор КАК СтарыйДоговор,
	|	ЕСТЬNULL(СпрДоговорАренды.ТребуетсяОформлениеЗаявленийОВвозе, ЛОЖЬ) КАК ТребуетсяОформлениеЗаявленийОВвозе,
	|	ДанныеДокумента.ВалютаВзаиморасчетов                       КАК ВалютаВзаиморасчетов,
	|	ДанныеДокумента.СуммаВзаиморасчетов                        КАК СуммаВзаиморасчетов,
	|	ДанныеДокумента.СуммаДокумента                             КАК СуммаДокумента,
	|	ДанныеДокумента.Валюта                                     КАК Валюта,
	|	ДанныеДокумента.Ответственный                              КАК Ответственный,
	|	ДанныеДокумента.Комментарий                                КАК Комментарий,
	|	ДанныеДокумента.Проведен                                   КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления                            КАК ПометкаУдаления,
	|	ДанныеДокумента.Сумма                                      КАК Сумма,
	|	ДанныеДокумента.СуммаОбеспечительногоПлатежа               КАК СуммаОбеспечительногоПлатежа,
	|	ДанныеДокумента.СуммаУслугПоАренде                         КАК СуммаУслугПоАренде,
	|	ДанныеДокумента.СуммаВыкупаПредметовАренды                 КАК СуммаВыкупаПредметовАренды,
	|	ДанныеДокумента.СуммаНДСВыкупаПредметовАренды              КАК СуммаНДСВыкупаПредметовАренды,
	|	ДанныеДокумента.СуммаПроцентов                             КАК СуммаПроцентов,
	|	ДанныеДокумента.СуммаСНДС                                  КАК СуммаСНДС,
	|	ДанныеДокумента.СуммаНДС                                   КАК СуммаНДС,
	|	ДанныеДокумента.СтавкаНДС                                  КАК СтавкаНДС,
	|	ДанныеДокумента.СрокЛет                                    КАК СрокЛет,
	|	ДанныеДокумента.СрокМес                                    КАК СрокМес,
	|	ДанныеДокумента.СрокДней                                   КАК СрокДней,
	|	ДанныеДокумента.ДатаНачалаАренды                           КАК ДатаНачалаАренды,
	|	ДанныеДокумента.ДатаПервогоПлатежа                         КАК ДатаПервогоПлатежа,
	|	ДанныеДокумента.ДатаПервогоОбеспечительногоПлатежа         КАК ДатаПервогоОбеспечительногоПлатежа,
	|	ДанныеДокумента.ДатаВыкупа                                 КАК ДатаВыкупа,
	|	ДанныеДокумента.ПроцентнаяСтавка                           КАК ПроцентнаяСтавка,
	|	ДанныеДокумента.ПериодичностьОплат                         КАК ПериодичностьОплат,
	|	ДанныеДокумента.ПериодичностьНачислений                    КАК ПериодичностьНачислений,
	|	ДанныеДокумента.СтоимостьПредметовАренды                   КАК СтоимостьПредметовАренды,
	|	ДанныеДокумента.ОплатаУслугиПоАренде                       КАК ОплатаУслугиПоАренде,
	|	ДанныеДокумента.НачислениеУслугиПоАренде                   КАК НачислениеУслугиПоАренде,
	|	ДанныеДокумента.ИдентификаторДокумента                     КАК ИдентификаторДокумента,
	|	ДанныеДокумента.СтатьяРасходов                             КАК СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходов                          КАК АналитикаРасходов,
	|	ДанныеДокумента.АналитикаАктивовПассивовРасходы            КАК АналитикаАктивовПассивовРасходы,
	|	ДанныеДокумента.НастройкаСчетовУчетаРасходы                КАК НастройкаСчетовУчетаРасходы,
	|	ДанныеДокумента.СтатьяДоходов                              КАК СтатьяДоходов,
	|	ДанныеДокумента.АналитикаДоходов                           КАК АналитикаДоходов,
	|	ДанныеДокумента.ГрафикНачисленияПроцентовВведенВручную     КАК ГрафикНачисленияПроцентовВведенВручную,
	|	ДанныеДокумента.Организация.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыАренды КАК СпрДоговорАренды
	|		ПО ДанныеДокумента.Договор = СпрДоговорАренды.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения(Реквизиты);
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла; 
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
КонецПроцедуры

Процедура ПолучитьДанныеДокумента(Запрос)
	
	СписокЗапросов = Новый Массив;
	
	#Область ДанныеДокументаРеквизиты
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИзменениеУсловийДоговораАренды) КАК ХозяйственнаяОперация,
	|	ИСТИНА КАК ОтражатьВРеглУчете,
	|	ИСТИНА КАК ОтражатьВУпрУчете
	|
	|ПОМЕСТИТЬ ДанныеДокументаРеквизиты
	|
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды КАК ДанныеДокумента
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ДанныеДокументаТаблицаОС
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаДокумента.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаДокумента.ОсновноеСредство КАК ОбъектУчета,
	|	ТаблицаДокумента.Стоимость КАК Стоимость,
	|	ТаблицаДокумента.СтоимостьБУ КАК СтоимостьБУ,
	|	ТаблицаДокумента.СтоимостьНУ КАК СтоимостьНУ,
	|	ТаблицаДокумента.СтоимостьУУ КАК СтоимостьУУ
	|
	|ПОМЕСТИТЬ ДанныеДокументаТаблицаОС
	|
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды.ОС КАК ТаблицаДокумента
	|
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	ОсновноеСредство
	|";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	Запрос.Текст = СтрСоединить(СписокЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)

	ЗначенияПараметровПроведения = Новый Структура;
	УчетАрендованныхОС.ДополнитьЗначенияПараметровПроведенияДокументаАренды(ЗначенияПараметровПроведения);

	ЗначенияПараметровПроведения.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ИзменениеУсловийДоговораАренды"));
	ЗначенияПараметровПроведения.Вставить("ВедетсяРегламентированныйУчетВНА", ВнеоборотныеАктивыСлужебный.ВедетсяРегламентированныйУчетВНА());
	ЗначенияПараметровПроведения.Вставить("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ИзменениеУсловийДоговораАренды);
	ЗначенияПараметровПроведения.Вставить("ВалютаУправленческогоУчета", Константы.ВалютаУправленческогоУчета.Получить());
	ЗначенияПараметровПроведения.Вставить("ОтражатьВРеглУчете", Истина);
	ЗначенияПараметровПроведения.Вставить("ОтражатьВУпрУчете", Истина);

	ЗначенияПараметровПроведения.Вставить("ХО_УвеличениеНакопленнойАмортизации", Перечисления.ХозяйственныеОперации.УвеличениеНакопленнойАмортизацииОС);
	ЗначенияПараметровПроведения.Вставить("НастройкаХО_УвеличениеНакопленнойАмортизации", Справочники.НастройкиХозяйственныхОпераций.УвеличениеНакопленнойАмортизацииОС);

	ЗначенияПараметровПроведения.Вставить("ХО_УменьшениеНакопленнойАмортизации", Перечисления.ХозяйственныеОперации.УменьшениеНакопленнойАмортизацииОС);
	ЗначенияПараметровПроведения.Вставить("НастройкаХО_УменьшениеНакопленнойАмортизации", Справочники.НастройкиХозяйственныхОпераций.УменьшениеНакопленнойАмортизацииОС);
	ЗначенияПараметровПроведения.Вставить("ИдентификаторЗаявлениеОВвозеТоваров", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ЗаявлениеОВвозеТоваров"));

	ЗначенияПараметровПроведения.Вставить("ХО_ФормированиеСтоимостиАрендованныхОС", Перечисления.ХозяйственныеОперации.ФормированиеСтоимостиАрендованныхОС);
	ЗначенияПараметровПроведения.Вставить("НастройкаХО_ФормированиеСтоимостиАрендованныхОС", Справочники.НастройкиХозяйственныхОпераций.ФормированиеСтоимостиАрендованныхОС);

	ЗначенияПараметровПроведения.Вставить("ХО_ИзменениеУсловийДоговораАренды", Перечисления.ХозяйственныеОперации.ИзменениеУсловийДоговораАренды);
	ЗначенияПараметровПроведения.Вставить("НастройкаХО_ИзменениеУсловийДоговораАренды", Справочники.НастройкиХозяйственныхОпераций.ИзменениеУсловийДоговораАренды);

	ЗначенияПараметровПроведения.Вставить("ХО_ОтражениеАрендныхОбязательствВДоходах", Перечисления.ХозяйственныеОперации.ОтражениеАрендныхОбязательствВДоходах);
	ЗначенияПараметровПроведения.Вставить("НастройкаХО_ОтражениеАрендныхОбязательствВДоходах", Справочники.НастройкиХозяйственныхОпераций.ОтражениеАрендныхОбязательствВДоходах);

	Если Реквизиты <> Неопределено Тогда
		
		ЗначенияПараметровПроведения.Вставить("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
		ЗначенияПараметровПроведения.Вставить("НачалоМесяца", НачалоМесяца(Реквизиты.Период));
		ЗначенияПараметровПроведения.Вставить("КонецМесяца", КонецМесяца(Реквизиты.Период));
		ЗначенияПараметровПроведения.Вставить("ГраницаОстатков", Новый Граница(НачалоДня(Реквизиты.Период), ВидГраницы.Исключая));
		
		ЗначенияПараметровПроведения.Вставить("ИспользуетсяУчетАрендыПоФСБУ25_2018",
			УчетАрендованныхОС.ИспользуетсяУчетАрендыПоФСБУ25_2018(Реквизиты.Организация, Реквизиты.Период, Истина));
		
		Коэффициенты = РаботаСКурсамиВалютУТ.ПолучитьКоэффициентыПересчетаВалюты(
			Реквизиты.Валюта, Реквизиты.ВалютаВзаиморасчетов, Реквизиты.Период, Реквизиты.Организация);
		ЗначенияПараметровПроведения.Вставить("КоэффициентПересчетаВВалютуУпр",  Коэффициенты.КоэффициентПересчетаВВалютуУпр);
		ЗначенияПараметровПроведения.Вставить("КоэффициентПересчетаВВалютуРегл", Коэффициенты.КоэффициентПересчетаВВалютуРегл);
		
		КоэффициентПересчетаИзВалютыУпр = РаботаСКурсамиВалютУТ.ПолучитьКоэффициентПересчетаИзВалютыВВалюту(
			ЗначенияПараметровПроведения.ВалютаУправленческогоУчета, 
			Реквизиты.ВалютаВзаиморасчетов, 
			Реквизиты.Период);
		ЗначенияПараметровПроведения.Вставить("КоэффициентПересчетаИзВалютыУпр", КоэффициентПересчетаИзВалютыУпр);
		
		КоэффициентПересчетаИзВалютыРегл = РаботаСКурсамиВалютУТ.ПолучитьКоэффициентПересчетаИзВалютыВВалюту(
			Реквизиты.ВалютаРегламентированногоУчета, 
			Реквизиты.ВалютаВзаиморасчетов, 
			Реквизиты.Период);
		ЗначенияПараметровПроведения.Вставить("КоэффициентПересчетаИзВалютыРегл", КоэффициентПересчетаИзВалютыРегл);
		
		ПараметрыУчетаПредметовАренды = УчетАрендованныхОСКлиентСервер.ПараметрыУчетаПредметовАренды(
			ЗначенияПараметровПроведения.ИспользуетсяУчетАрендыПоФСБУ25_2018, Реквизиты);
		
		ЗначенияПараметровПроведения.Вставить("ЕстьУчетУУ", ПараметрыУчетаПредметовАренды.ЕстьУчетУУ);
		ЗначенияПараметровПроведения.Вставить("ЕстьУчетБУ", ПараметрыУчетаПредметовАренды.ЕстьУчетБУ);
		ЗначенияПараметровПроведения.Вставить("ЕстьУчетНУ", ПараметрыУчетаПредметовАренды.ЕстьУчетНУ);
		
		ЗначенияПараметровПроведения.Вставить("СозданДляПерехода", 
						УчетАрендованныхОС.СозданДляПереходаНаФСБУ25_2018(Реквизиты.Период, Реквизиты.Организация));
			
		ЗначенияПараметровПроведения.Вставить("ДокументЗаключенияДоговораАренды", Справочники.ДоговорыАренды.ДокументЗаключенияДоговораАренды(Реквизиты.Договор));
		
		ЗначенияПараметровПроведения.Вставить("ИзменениеДоговора", ?(Реквизиты.СтарыйДоговор <> Реквизиты.Договор, Истина, Ложь));
		
		// Небольшие отклонения при расчете изменения стоимости ОС списываем на погрешность округления
		ЗначенияПараметровПроведения.Вставить("ПорогОкругления", ПорогОкругленияИзмененияПриведеннойСтоимости());
		
	КонецЕсли;
	
	ИзменениеУсловийДоговораАрендыЛокализация.ДополнитьЗначенияПараметровПроведения(ЗначенияПараметровПроведения, Реквизиты);
	
	Возврат ЗначенияПараметровПроведения;
	
КонецФункции

#Область ТекстыЗапросаДляПроведения

Процедура ТекстЗапросаТаблицаПараметрыАмортизацииОСУУ(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПараметрыАмортизацииОСУУ";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ОтложенноеФормированиеДвиженийВНА.ТекстЗапросаПустыеТаблицыОтложенныхДвижений(Запрос, ТекстыЗапроса);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыАмортизацииОСУУ(ТекстыЗапроса);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаВтТаблицаПереоценки(Запрос, ТекстыЗапроса, "ОС", "Документ.ИзменениеУсловийДоговораАренды");
	ТекстЗапросаТаблицаВтТаблицаОС(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Ссылка                                                        КАК Регистратор,
	|	&Период                                                        КАК Период,
	|	&Организация                                                   КАК Организация,
	|	ТаблицаОС.ОсновноеСредство                                     КАК ОсновноеСредство,
	|
	|	ДанныеРегистра.КоэффициентУскорения                            КАК КоэффициентУскорения,
	|	ДанныеРегистра.МетодНачисленияАмортизации                      КАК МетодНачисленияАмортизации,
	|	ДанныеРегистра.ЛиквидационнаяСтоимость                         КАК ЛиквидационнаяСтоимость,
	|	ДанныеРегистра.ЛиквидационнаяСтоимостьРегл                     КАК ЛиквидационнаяСтоимостьРегл,
	|	ДанныеРегистра.ДатаПоследнегоИзменения                         КАК ДатаПоследнегоИзменения,
	|	ДанныеРегистра.ОбъемПродукцииРаботДляВычисленияАмортизации     КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|
	|	ТаблицаОС.СрокИспользованияУУ                                  КАК СрокИспользования,
	|
	|	ВЫБОР
	|		КОГДА ДанныеРегистра.СрокИспользования <> ДанныеРегистра.СрокИспользованияДляВычисленияАмортизации
	|			ТОГДА ДанныеРегистра.СрокИспользованияДляВычисленияАмортизации + (ТаблицаОС.СрокИспользованияУУ - ДанныеРегистра.СрокИспользования)
	|		ИНАЧЕ ТаблицаОС.СрокИспользованияУУ
	|	КОНЕЦ КАК СрокИспользованияДляВычисленияАмортизации,
	|
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(ТаблицаПереоценки.СуммаДооценкиСтоимостиУУ, 0) = 0
	|				И ЕСТЬNULL(ТаблицаПереоценки.СуммаУценкиСтоимостиУУ, 0) = 0
	|			ТОГДА ДанныеРегистра.СтоимостьДляВычисленияАмортизации
	|		КОГДА ДанныеРегистра.СрокИспользования <> ДанныеРегистра.СрокИспользованияДляВычисленияАмортизации
	|			ТОГДА ТаблицаОС.СтоимостьУУ 
	|					- (ЕСТЬNULL(ТаблицаПереоценки.НакопленнаяАмортизацияУУ, 0)
	|						+ ЕСТЬNULL(ТаблицаПереоценки.СуммаДооценкиАмортизацииУУ, 0)
	|						- ЕСТЬNULL(ТаблицаПереоценки.СуммаУценкиАмортизацииУУ, 0))
	|		ИНАЧЕ ДанныеРегистра.СтоимостьДляВычисленияАмортизации 
	|				+ ЕСТЬNULL(ТаблицаПереоценки.СуммаДооценкиСтоимостиУУ, 0)
	|				- ЕСТЬNULL(ТаблицаПереоценки.СуммаУценкиСтоимостиУУ, 0)
	|	КОНЕЦ КАК СтоимостьДляВычисленияАмортизации,
	|
	|	ВЫБОР 
	|		КОГДА &ВедетсяРегламентированныйУчетВНА
	|			ТОГДА 0
	|		КОГДА ЕСТЬNULL(ТаблицаПереоценки.СуммаДооценкиСтоимостиУУ, 0) = 0
	|				И ЕСТЬNULL(ТаблицаПереоценки.СуммаУценкиСтоимостиУУ, 0) = 0
	|			ТОГДА ДанныеРегистра.СтоимостьДляВычисленияАмортизацииРегл
	|		КОГДА ДанныеРегистра.СрокИспользования <> ДанныеРегистра.СрокИспользованияДляВычисленияАмортизации
	|			ТОГДА ТаблицаОС.СтоимостьБУ 
	|					- (ЕСТЬNULL(ТаблицаПереоценки.НакопленнаяАмортизацияБУ, 0)
	|						+ ЕСТЬNULL(ТаблицаПереоценки.СуммаДооценкиАмортизацииБУ, 0)
	|						- ЕСТЬNULL(ТаблицаПереоценки.СуммаУценкиАмортизацииБУ, 0))
	|		ИНАЧЕ ДанныеРегистра.СтоимостьДляВычисленияАмортизациирегл 
	|				+ ЕСТЬNULL(ТаблицаПереоценки.СуммаДооценкиСтоимостиБУ, 0)
	|				- ЕСТЬNULL(ТаблицаПереоценки.СуммаУценкиСтоимостиБУ, 0)
	|	КОНЕЦ КАК СтоимостьДляВычисленияАмортизацииРегл
	|
	|ИЗ
	|	втТаблицаОС КАК ТаблицаОС
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПараметрыАмортизацииОСУУ КАК ДанныеРегистра
	|		ПО ДанныеРегистра.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаПереоценки КАК ТаблицаПереоценки
	|		ПО ТаблицаПереоценки.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|ГДЕ
	|	ТаблицаОС.ДействиеСПредметомАренды = ЗНАЧЕНИЕ(Перечисление.ДействияСПредметамиАренды.ИзменениеУсловийАренды)
	|	И (ТаблицаОС.СрокИспользованияУУ <> ДанныеРегистра.СрокИспользования
	|		ИЛИ ЕСТЬNULL(ТаблицаПереоценки.СуммаДооценкиСтоимостиУУ, 0) <> 0
	|		ИЛИ ЕСТЬNULL(ТаблицаПереоценки.СуммаУценкиСтоимостиУУ, 0) <> 0)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);

КонецПроцедуры

Процедура ТекстЗапросаТаблицаПараметрыАмортизацииОСБУ(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ПараметрыАмортизацииОСБУ";
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыАмортизацииОСБУ(ТекстыЗапроса);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаВтТаблицаПереоценки(Запрос, ТекстыЗапроса, "ОС", "Документ.ИзменениеУсловийДоговораАренды");
	ТекстЗапросаТаблицаВтТаблицаОС(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Ссылка                      КАК Регистратор,
	|	&Период                      КАК Период,
	|	&Организация                 КАК Организация,
	|	ТаблицаОС.ОсновноеСредство   КАК ОсновноеСредство,
	
	|	ПараметрыАмортизацииОСБУ.СтоимостьДляВычисленияАмортизацииЦФ  КАК СтоимостьДляВычисленияАмортизацииЦФ,
	|	ПараметрыАмортизацииОСБУ.ПРДляВычисленияАмортизации           КАК ПРДляВычисленияАмортизации,
	|	ПараметрыАмортизацииОСБУ.ДатаПоследнегоИзменения              КАК ДатаПоследнегоИзменения,
	|	ПараметрыАмортизацииОСБУ.КоэффициентАмортизацииБУ             КАК КоэффициентАмортизацииБУ,
	|	ПараметрыАмортизацииОСБУ.КоэффициентУскорения                 КАК КоэффициентУскорения,
	|	ПараметрыАмортизацииОСБУ.ГрафикАмортизации                    КАК ГрафикАмортизации,
	|	ПараметрыАмортизацииОСБУ.СпециальныйКоэффициент               КАК СпециальныйКоэффициент,
	|	ПараметрыАмортизацииОСБУ.ЛиквидационнаяСтоимость              КАК ЛиквидационнаяСтоимость,
	|
	|	ПараметрыАмортизацииОСБУ.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|
	// СрокПолезногоИспользованияБУ
	|	ВЫБОР
	|		КОГДА ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияБУ <> 0
	|			ТОГДА ТаблицаОС.СрокИспользованияБУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СрокПолезногоИспользованияБУ,
	|
	// СрокПолезногоИспользованияНУ
	|	ВЫБОР
	|		КОГДА ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияНУ <> 0
	|			ТОГДА ТаблицаОС.СрокИспользованияНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СрокПолезногоИспользованияНУ,
	|
	// СрокИспользованияДляВычисленияАмортизации
	|	ВЫБОР
	|		КОГДА ТаблицаОС.СрокИспользованияБУ = ПараметрыАмортизацииОСБУ.СрокИспользованияДляВычисленияАмортизации
	|			ТОГДА ПараметрыАмортизацииОСБУ.СрокИспользованияДляВычисленияАмортизации
	|		КОГДА ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияБУ <> ПараметрыАмортизацииОСБУ.СрокИспользованияДляВычисленияАмортизации
	|			ТОГДА ПараметрыАмортизацииОСБУ.СрокИспользованияДляВычисленияАмортизации + (ТаблицаОС.СрокИспользованияБУ - ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияБУ)
	|		ИНАЧЕ ТаблицаОС.СрокИспользованияБУ
	|	КОНЕЦ КАК СрокИспользованияДляВычисленияАмортизации,
	|
	// СтоимостьДляВычисленияАмортизации
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(ТаблицаПереоценки.СуммаДооценкиСтоимостиБУ, 0) = 0
	|				И ЕСТЬNULL(ТаблицаПереоценки.СуммаУценкиСтоимостиБУ, 0) = 0
	|			ТОГДА ПараметрыАмортизацииОСБУ.СтоимостьДляВычисленияАмортизации
	|
	|		КОГДА ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияБУ <> ПараметрыАмортизацииОСБУ.СрокИспользованияДляВычисленияАмортизации
	|			ТОГДА ТаблицаОС.СтоимостьБУ 
	|					- (ЕСТЬNULL(ТаблицаПереоценки.НакопленнаяАмортизацияБУ, 0)
	|						+ ЕСТЬNULL(ТаблицаПереоценки.СуммаДооценкиАмортизацииБУ, 0)
	|						- ЕСТЬNULL(ТаблицаПереоценки.СуммаУценкиАмортизацииБУ, 0))
	|		ИНАЧЕ ПараметрыАмортизацииОСБУ.СтоимостьДляВычисленияАмортизации 
	|				+ ЕСТЬNULL(ТаблицаПереоценки.СуммаДооценкиСтоимостиБУ, 0)
	|				- ЕСТЬNULL(ТаблицаПереоценки.СуммаУценкиСтоимостиБУ, 0)
	|	КОНЕЦ КАК СтоимостьДляВычисленияАмортизации
	|
	|ИЗ
	|	втТаблицаОС КАК ТаблицаОС
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПараметрыАмортизацииОСБУ КАК ПараметрыАмортизацииОСБУ
	|		ПО (ПараметрыАмортизацииОСБУ.Организация = &Организация)
	|			И (ПараметрыАмортизацииОСБУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаПереоценки КАК ТаблицаПереоценки
	|		ПО ТаблицаПереоценки.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|ГДЕ
	|	ТаблицаОС.ДействиеСПредметомАренды = ЗНАЧЕНИЕ(Перечисление.ДействияСПредметамиАренды.ИзменениеУсловийАренды)
	|	И (ТаблицаОС.СрокИспользованияБУ <> ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияБУ
	|		ИЛИ ТаблицаОС.СрокИспользованияНУ <> ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияНУ
	|		ИЛИ ЕСТЬNULL(ТаблицаПереоценки.СуммаДооценкиСтоимостиБУ, 0) <> 0
	|		ИЛИ ЕСТЬNULL(ТаблицаПереоценки.СуммаУценкиСтоимостиБУ, 0) <> 0)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаСтоимостьОС(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "СтоимостьОС";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;

	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыУчетаОС(ТекстыЗапроса,, "втСведенияОбУчете");
	
	ТекстЗапросаТаблицаВтУвеличениеСтоимости(Запрос, ТекстыЗапроса);
	ТекстЗапросаТаблицаВтУменьшениеСтоимости(Запрос, ТекстыЗапроса);
	
	ТекстЗапросаТаблицаВтБалансоваяСтоимостьОС(ТекстыЗапроса);

	СписокЗапросовОбъединение = Новый Массив;
	
	#Область УвеличениеСтоимостиОС // Дт <01.03> - Кт <76.07.1>
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                КАК ВидДвижения,
	|	&Ссылка                                               КАК Регистратор,
	|	&Период                                               КАК Период,
	|	&Организация                                          КАК Организация,
	|	УвеличениеСтоимости.ОсновноеСредство                  КАК ОсновноеСредство,
	|	СведенияОбУчете.Местонахождение                       КАК Подразделение,
	|	СведенияОбУчете.ГруппаФинансовогоУчета                КАК ГруппаФинансовогоУчета,
	|	СведенияОбУчете.НаправлениеДеятельности               КАК НаправлениеДеятельности,
	|	СведенияОбУчете.Арендатор                             КАК Арендатор,
	|                     
	|	УвеличениеСтоимости.Стоимость                         КАК Стоимость,
	|	УвеличениеСтоимости.СтоимостьРегл                     КАК СтоимостьРегл,
	|	УвеличениеСтоимости.СтоимостьНУ                       КАК СтоимостьНУ,
	|
	|	УвеличениеСтоимости.СтоимостьРегл
	|	 - УвеличениеСтоимости.СтоимостьНУ                    КАК СтоимостьВР,
	|
	|	УвеличениеСтоимости.ЗалоговаяСтоимость                КАК ЗалоговаяСтоимость,
	|	0                                                     КАК НеУчитываемаяСтоимостьНУ,
	|	
	|	УвеличениеСтоимости.ИдентификаторСтроки               КАК ИдентификаторФинЗаписи,
	|	&ХО_УвеличениеСтоимостиАрендованныхОС                 КАК ХозяйственнаяОперация,
	|	&НастройкаХО_УвеличениеСтоимостиАрендованныхОС        КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	втУвеличениеСтоимости КАК УвеличениеСтоимости
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.ОсновноеСредство = УвеличениеСтоимости.ОсновноеСредство
	|ГДЕ
	|	НЕ УвеличениеСтоимости.Субаренда
	|";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);

	#КонецОбласти
	
	#Область УменьшениеСтоимостиОС // Дт <76.07.1> - Кт <01.03>
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)                КАК ВидДвижения,
	|	&Ссылка                                               КАК Регистратор,
	|	&Период                                               КАК Период,
	|	&Организация                                          КАК Организация,
	|	УменьшениеСтоимости.ОсновноеСредство                  КАК ОсновноеСредство,
	|	СведенияОбУчете.Местонахождение                       КАК Подразделение,
	|	СведенияОбУчете.ГруппаФинансовогоУчета                КАК ГруппаФинансовогоУчета,
	|	СведенияОбУчете.НаправлениеДеятельности               КАК НаправлениеДеятельности,
	|	СведенияОбУчете.Арендатор                             КАК Арендатор,
	|	ВЫБОР
	|		КОГДА БалансоваяСтоимость.Ссылка ЕСТЬ NULL ТОГДА
	|			УменьшениеСтоимости.Стоимость
	|		КОГДА УменьшениеСтоимости.Стоимость < БалансоваяСтоимость.БалансоваяСтоимостьУпр ТОГДА
	|			УменьшениеСтоимости.Стоимость
	|		ИНАЧЕ
	|			ЕСТЬNULL(БалансоваяСтоимость.БалансоваяСтоимостьУпр, 0)
	|		КОНЕЦ                                            КАК Стоимость,
	|	
	|	ВЫБОР
	|		КОГДА БалансоваяСтоимость.Ссылка ЕСТЬ NULL ТОГДА
	|			УменьшениеСтоимости.СтоимостьРегл
	|		КОГДА УменьшениеСтоимости.СтоимостьРегл < БалансоваяСтоимость.БалансоваяСтоимостьРегл ТОГДА
	|			УменьшениеСтоимости.СтоимостьРегл
	|		ИНАЧЕ
	|			ЕСТЬNULL(БалансоваяСтоимость.БалансоваяСтоимостьРегл, 0)
	|		КОНЕЦ                                             КАК СтоимостьРегл,
	|	
	|	УменьшениеСтоимости.СтоимостьНУ                       КАК СтоимостьНУ,
	|
	|	ВЫБОР
	|		КОГДА БалансоваяСтоимость.Ссылка ЕСТЬ NULL ТОГДА
	|			УменьшениеСтоимости.СтоимостьРегл
	|			- УменьшениеСтоимости.СтоимостьНУ
	|		КОГДА УменьшениеСтоимости.СтоимостьРегл < БалансоваяСтоимость.БалансоваяСтоимостьРегл ТОГДА
	|			УменьшениеСтоимости.СтоимостьРегл
	|			- УменьшениеСтоимости.СтоимостьНУ
	|		ИНАЧЕ
	|			ЕСТЬNULL(БалансоваяСтоимость.БалансоваяСтоимостьРегл, 0)
	|			- УменьшениеСтоимости.СтоимостьНУ
	|		КОНЕЦ                                             КАК СтоимостьВР,
	|
	|	УменьшениеСтоимости.ЗалоговаяСтоимость                КАК ЗалоговаяСтоимость,
	|	0                                                     КАК НеУчитываемаяСтоимостьНУ,
	|	
	|	УменьшениеСтоимости.ИдентификаторСтроки               КАК ИдентификаторФинЗаписи,
	|	&ХО_УменьшениеСтоимостиАрендованныхОС                 КАК ХозяйственнаяОперация,
	|	&НастройкаХО_УменьшениеСтоимостиАрендованныхОС        КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	втУменьшениеСтоимости КАК УменьшениеСтоимости
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.ОсновноеСредство = УменьшениеСтоимости.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтБалансоваяСтоимостьОС КАК БалансоваяСтоимость
	|		ПО УменьшениеСтоимости.ОсновноеСредство = БалансоваяСтоимость.ОсновноеСредство
	|			И БалансоваяСтоимость.Ссылка = &Ссылка
	|ГДЕ
	|	НЕ УменьшениеСтоимости.Субаренда
	|";

	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	ТекстЗапроса = СтрСоединить(СписокЗапросовОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаАмортизацияОС(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "АмортизацияОС";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыУчетаОС(ТекстыЗапроса,, "втСведенияОбУчете");
	ОтложенноеФормированиеДвиженийВНА.ТекстЗапросаПустыеТаблицыОтложенныхДвижений(Запрос, ТекстыЗапроса);
	ИнициализироватьТаблицаВтКорректировкаСтоимостиОСПоДолям(Запрос, ТекстыЗапроса);
	
	#Область УменьшениеНакопленнойАмортизацииПриПереходеНаФСБУ25
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)       КАК ВидДвижения,
	|	&Период                                      КАК Период,
	|	&Организация                                 КАК Организация,
	|	втСведенияОбУчете.Местонахождение            КАК Подразделение,
	|	Проценты.ОсновноеСредство                    КАК ОсновноеСредство,
	|	втСведенияОбУчете.ГруппаФинансовогоУчета     КАК ГруппаФинансовогоУчета,
	|	втСведенияОбУчете.НаправлениеДеятельности    КАК НаправлениеДеятельности,
	|
	|	ВЫБОР
	|		КОГДА втСведенияОбУчете.ОтражатьВУпрУчете
	|			ТОГДА ВЫБОР
	|					КОГДА ЕСТЬNULL(ВТ_СтоимостьВНА.Стоимость, 0) > 0
	|						ТОГДА (1 - (ВТ_СтоимостьВНА.Стоимость - Проценты.СуммаПроцентовУпр) / ВТ_СтоимостьВНА.Стоимость) 
	|								* ВТ_АмортизацияВНА.Амортизация
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Амортизация,
	|
	|	ВЫБОР
	|		КОГДА втСведенияОбУчете.ОтражатьВРеглУчете
	|			ТОГДА ВЫБОР
	|					КОГДА ВТ_АмортизацияВНА.НачислятьИзнос
	|						ТОГДА 0
	|					ИНАЧЕ ВЫБОР
	|							КОГДА ЕСТЬNULL(ВТ_СтоимостьВНА.СтоимостьРегл, 0) > 0
	|								ТОГДА (1 - (ВТ_СтоимостьВНА.СтоимостьРегл - Проценты.СуммаПроцентовРегл) / ВТ_СтоимостьВНА.СтоимостьРегл) 
	|										* ВТ_АмортизацияВНА.АмортизацияРегл
	|							ИНАЧЕ 0
	|						КОНЕЦ
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК АмортизацияРегл,
	|
	|	0 КАК АмортизацияНУ,
	|
	|	ВЫБОР
	|		КОГДА втСведенияОбУчете.ОтражатьВРеглУчете
	|			ТОГДА ВЫБОР
	|					КОГДА ВТ_АмортизацияВНА.НачислятьИзнос
	|						ТОГДА 0
	|					ИНАЧЕ ВЫБОР
	|							КОГДА ВТ_СтоимостьВНА.СтоимостьРегл ЕСТЬ НЕ NULL 
	|								ТОГДА (1 - (ВТ_СтоимостьВНА.СтоимостьРегл - Проценты.СуммаПроцентовРегл) / ВТ_СтоимостьВНА.СтоимостьРегл) 
	|										* ВТ_АмортизацияВНА.АмортизацияРегл
	|							ИНАЧЕ 0
	|						КОНЕЦ
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК АмортизацияВР,
	|
	|	Проценты.ИдентификаторСтроки                  КАК ИдентификаторФинЗаписи,
	|	&ХО_УменьшениеНакопленнойАмортизации          КАК ХозяйственнаяОперация,
	|	&НастройкаХО_УменьшениеНакопленнойАмортизации КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втКорректировкаСтоимостиОСПоДолям КАК Проценты
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК ВТ_СтоимостьВНА
	|		ПО Проценты.ОсновноеСредство = ВТ_СтоимостьВНА.ОбъектУчета
	|			И (ВТ_СтоимостьВНА.Ссылка = &Ссылка)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК ВТ_АмортизацияВНА
	|		ПО Проценты.ОсновноеСредство = ВТ_АмортизацияВНА.ОбъектУчета
	|			И (ВТ_АмортизацияВНА.Ссылка = &Ссылка)
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК втСведенияОбУчете
	|		ПО Проценты.ОсновноеСредство = втСведенияОбУчете.ОсновноеСредство
	|ГДЕ
	|	&СозданДляПерехода";
	
	#КонецОбласти
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаАрендованныеОС(ТекстыЗапроса, Регистры)

	ИмяРегистра = "АрендованныеОС";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтАрендованныеОС(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтТаблицаОС(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаОС.Стоимость КАК Стоимость,
	|	ТаблицаОС.СправедливаяСтоимость КАК НоминальнаяСтоимость,
	|	&Организация КАК Организация,
	|	&Партнер КАК Партнер,
	|	&Контрагент КАК Контрагент,
	|	&Договор КАК Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаОС.ДействиеСПредметомАренды <> ЗНАЧЕНИЕ(Перечисление.ДействияСПредметамиАренды.ПрекращениеАренды)
	|			ТОГДА ВтАрендованныеОС.Состояние
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостоянияОС.СнятоСУчета)
	|	КОНЕЦ КАК Состояние
	|ИЗ
	|	втТаблицаОС КАК ТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтАрендованныеОС КАК ВтАрендованныеОС
	|		ПО ВтАрендованныеОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|			И ВтАрендованныеОС.Состояние <> ЗНАЧЕНИЕ(Перечисление.СостоянияОС.СнятоСУчета)
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И (ТаблицаОС.Стоимость <> ВтАрендованныеОС.Стоимость
	|	ИЛИ &ИзменениеДоговора
	|	ИЛИ ТаблицаОС.ДействиеСПредметомАренды = ЗНАЧЕНИЕ(Перечисление.ДействияСПредметамиАренды.ПрекращениеАренды))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаОС.Стоимость КАК Стоимость,
	|	ТаблицаОС.СправедливаяСтоимость КАК НоминальнаяСтоимость,
	|	&Организация КАК Организация,
	|	&Партнер КАК Партнер,
	|	&Контрагент КАК Контрагент,
	|	&Договор КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ЗаключенДоговорАренды) КАК Состояние
	|ИЗ
	|	втТаблицаОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.ДействиеСПредметомАренды = ЗНАЧЕНИЕ(Перечисление.ДействияСПредметамиАренды.ВзятиеВАренду)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
		
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ТекстЗапросаТаблицаУсловияДоговоровАренды(ТекстыЗапроса, Регистры)

	ИмяРегистра = "УсловияДоговоровАренды";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Ссылка                                КАК Регистратор,
	|	&Период                                КАК Период,
	|	&Договор                               КАК Договор,
	|	&СуммаОбеспечительногоПлатежа          КАК СуммаОбеспечительногоПлатежа,
	|	&СуммаУслугПоАренде                    КАК СуммаУслугПоАренде,
	|	&СуммаВыкупаПредметовАренды            КАК СуммаВыкупаПредметовАренды,
	|	&СуммаПроцентов                        КАК СуммаПроцентов,
	|	&СуммаНДС                              КАК СуммаНДС,
	|	&СтавкаНДС                             КАК СтавкаНДС,
	|	&СрокЛет                               КАК СрокЛет,
	|	&СрокМес                               КАК СрокМес,
	|	&СрокДней                              КАК СрокДней,
	|	&ДатаНачалаАренды                      КАК ДатаНачалаАренды,
	|	&ДатаПервогоПлатежа                    КАК ДатаПервогоПлатежа,
	|	&ДатаПервогоОбеспечительногоПлатежа    КАК ДатаПервогоОбеспечительногоПлатежа,
	|	&ДатаВыкупа                            КАК ДатаВыкупа,
	|	&ПроцентнаяСтавка                      КАК ПроцентнаяСтавка,
	|	&ПериодичностьОплат                    КАК ПериодичностьОплат,
	|	&ПериодичностьНачислений               КАК ПериодичностьНачислений,
	|	&СтоимостьПредметовАренды              КАК СтоимостьПредметовАренды,
	|	&ОплатаУслугиПоАренде                  КАК ОплатаУслугиПоАренде,
	|	&НачислениеУслугиПоАренде              КАК НачислениеУслугиПоАренде,
	|	&ГрафикНачисленияПроцентовВведенВручную КАК ГрафикНачисленияПроцентовВведенВручную,
	|	&Ссылка                                КАК АктуальныеУсловияДоговора,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияДоговоровКонтрагентов.Действует) КАК Состояние
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ 
	|
	|ВЫБРАТЬ
	|	&Ссылка КАК Ссылка,
	|	ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&Период, ДЕНЬ), СЕКУНДА, -1) КАК Период,
	|	УсловияДоговоровАренды.Договор КАК Договор,
	|	УсловияДоговоровАренды.СуммаОбеспечительногоПлатежа КАК СуммаОбеспечительногоПлатежа,
	|	УсловияДоговоровАренды.СуммаУслугПоАренде КАК СуммаУслугПоАренде,
	|	УсловияДоговоровАренды.СуммаВыкупаПредметовАренды КАК СуммаВыкупаПредметовАренды,
	|	УсловияДоговоровАренды.СуммаПроцентов КАК СуммаПроцентов,
	|	УсловияДоговоровАренды.СуммаНДС КАК СуммаНДС,
	|	УсловияДоговоровАренды.СтавкаНДС КАК СтавкаНДС,
	|	УсловияДоговоровАренды.СрокЛет КАК СрокЛет,
	|	УсловияДоговоровАренды.СрокМес КАК СрокМес,
	|	УсловияДоговоровАренды.СрокДней КАК СрокДней,
	|	УсловияДоговоровАренды.ДатаНачалаАренды КАК ДатаНачалаАренды,
	|	УсловияДоговоровАренды.ДатаПервогоПлатежа КАК ДатаПервогоПлатежа,
	|	УсловияДоговоровАренды.ДатаПервогоОбеспечительногоПлатежа КАК ДатаПервогоОбеспечительногоПлатежа,
	|	УсловияДоговоровАренды.ДатаВыкупа КАК ДатаВыкупа,
	|	УсловияДоговоровАренды.ПроцентнаяСтавка КАК ПроцентнаяСтавка,
	|	УсловияДоговоровАренды.ПериодичностьОплат КАК ПериодичностьОплат,
	|	УсловияДоговоровАренды.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	УсловияДоговоровАренды.СтоимостьПредметовАренды КАК СтоимостьПредметовАренды,
	|	УсловияДоговоровАренды.ОплатаУслугиПоАренде КАК ОплатаУслугиПоАренде,
	|	УсловияДоговоровАренды.НачислениеУслугиПоАренде КАК НачислениеУслугиПоАренде,
	|	УсловияДоговоровАренды.ГрафикНачисленияПроцентовВведенВручную КАК ГрафикНачисленияПроцентовВведенВручную,
	|	&Ссылка КАК АктуальныеУсловияДоговора,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияДоговоровКонтрагентов.Закрыт) КАК Состояние
	|ИЗ
	|	РегистрСведений.УсловияДоговоровАренды.СрезПоследних(
	|			&Период,
	|			Договор = &СтарыйДоговор
	|				И Регистратор <> &Ссылка) КАК УсловияДоговоровАренды
	|ГДЕ
	|	&ИзменениеДоговора";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
		
КонецПроцедуры

Функция ТекстЗапросаТаблицаГрафикОплатУслугПоАренде(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ГрафикОплатУслугПоАренде";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтПредыдущиеУсловияДоговора(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ГрафикОплатУслугПоАренде.ДатаИзменения КАК ДатаИзменения,
	|	ГрафикОплатУслугПоАренде.Договор КАК Договор,
	|	&Ссылка КАК АктуальныеУсловияДоговора,
	|	ГрафикОплатУслугПоАренде.Дата КАК Дата,
	|	ГрафикОплатУслугПоАренде.УслугаПоАренде КАК УслугаПоАренде,
	|	ГрафикОплатУслугПоАренде.УслугаПоАрендеНДС КАК УслугаПоАрендеНДС,
	|	ГрафикОплатУслугПоАренде.ОбеспечительныйПлатеж КАК ОбеспечительныйПлатеж,
	|	ГрафикОплатУслугПоАренде.ОбеспечительныйПлатежНДС КАК ОбеспечительныйПлатежНДС,
	|	ГрафикОплатУслугПоАренде.ВыкупнаяСтоимость КАК ВыкупнаяСтоимость,
	|	ГрафикОплатУслугПоАренде.ВыкупнаяСтоимостьАванс КАК ВыкупнаяСтоимостьАванс,
	|	ГрафикОплатУслугПоАренде.ВыкупнаяСтоимостьНДС КАК ВыкупнаяСтоимостьНДС
	|ИЗ
	|	РегистрСведений.ГрафикОплатУслугПоАренде КАК ГрафикОплатУслугПоАренде
	|ГДЕ
	|	ГрафикОплатУслугПоАренде.АктуальныеУсловияДоговора В
	|			(ВЫБРАТЬ
	|				Т.АктуальныеУсловияДоговора
	|			ИЗ
	|				втПредыдущиеУсловияДоговора КАК Т)
	|	И ГрафикОплатУслугПоАренде.Дата < НАЧАЛОПЕРИОДА(&Период, ДЕНЬ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	&Договор,
	|	&Ссылка,
	|	ТаблицаГрафика.Дата,
	|	ТаблицаГрафика.УслугаПоАренде,
	|	ТаблицаГрафика.УслугаПоАрендеНДС,
	|	ТаблицаГрафика.ОбеспечительныйПлатеж,
	|	ТаблицаГрафика.ОбеспечительныйПлатежНДС,
	|	ВЫБОР
	|		КОГДА ТаблицаГрафика.Дата >= &ДатаВыкупа
	|			ТОГДА ТаблицаГрафика.ВыкупнаяСтоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаГрафика.Дата < &ДатаВыкупа
	|			ТОГДА ТаблицаГрафика.ВыкупнаяСтоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаГрафика.ВыкупнаяСтоимостьНДС
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды.ГрафикОплатУслуг КАК ТаблицаГрафика
	|ГДЕ
	|	ТаблицаГрафика.Ссылка = &Ссылка
	|	И ТаблицаГрафика.Дата >= НАЧАЛОПЕРИОДА(&Период, ДЕНЬ)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаГрафикНачисленияУслугПоАренде(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ГрафикНачисленияУслугПоАренде";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтПредыдущиеУсловияДоговора(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ГрафикНачисленияУслугПоАренде.ДатаИзменения КАК ДатаИзменения,
	|	ГрафикНачисленияУслугПоАренде.Договор КАК Договор,
	|	&Ссылка КАК АктуальныеУсловияДоговора,
	|	ГрафикНачисленияУслугПоАренде.Дата КАК Дата,
	|	ГрафикНачисленияУслугПоАренде.УслугаПоАренде КАК УслугаПоАренде,
	|	ГрафикНачисленияУслугПоАренде.УслугаПоАрендеНДС КАК УслугаПоАрендеНДС,
	|	ГрафикНачисленияУслугПоАренде.ОбеспечительныйПлатеж КАК ОбеспечительныйПлатеж,
	|	ГрафикНачисленияУслугПоАренде.ОбеспечительныйПлатежНДС КАК ОбеспечительныйПлатежНДС,
	|	ГрафикНачисленияУслугПоАренде.ВыкупнаяСтоимость КАК ВыкупнаяСтоимость,
	|	ГрафикНачисленияУслугПоАренде.ВыкупнаяСтоимостьНДС КАК ВыкупнаяСтоимостьНДС
	|ИЗ
	|	РегистрСведений.ГрафикНачисленияУслугПоАренде КАК ГрафикНачисленияУслугПоАренде
	|ГДЕ
	|	ГрафикНачисленияУслугПоАренде.АктуальныеУсловияДоговора В
	|			(ВЫБРАТЬ
	|				Т.АктуальныеУсловияДоговора
	|			ИЗ
	|				втПредыдущиеУсловияДоговора КАК Т)
	|	И ГрафикНачисленияУслугПоАренде.Дата < НАЧАЛОПЕРИОДА(&Период, ДЕНЬ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	ВЫБОР
	|		КОГДА НЕ &ИзменениеДоговора
	|			ТОГДА &Договор
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ТаблицаГрафика.Дата < НАЧАЛОПЕРИОДА(&Период, ДЕНЬ)
	|					ТОГДА &СтарыйДоговор
	|				ИНАЧЕ &Договор
	|			КОНЕЦ
	|	КОНЕЦ,
	|	&Ссылка,
	|	ТаблицаГрафика.Дата,
	|	ТаблицаГрафика.УслугаПоАренде,
	|	ТаблицаГрафика.УслугаПоАрендеНДС,
	|	ТаблицаГрафика.ОбеспечительныйПлатеж,
	|	ТаблицаГрафика.ОбеспечительныйПлатежНДС,
	|	ВЫБОР
	|		КОГДА &ДатаВыкупа = ТаблицаГрафика.Дата
	|			ТОГДА &СуммаВыкупаПредметовАренды
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ДатаВыкупа = ТаблицаГрафика.Дата
	|			ТОГДА &СуммаНДСВыкупаПредметовАренды
	|		ИНАЧЕ 0
	|	КОНЕЦ
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды.ГрафикНачисленияУслуг КАК ТаблицаГрафика
	|ГДЕ
	|	ТаблицаГрафика.Ссылка = &Ссылка
	|	И ТаблицаГрафика.Дата >= НАЧАЛОПЕРИОДА(&Период, ДЕНЬ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	&Договор,
	|	&Ссылка,
	|	&ДатаВыкупа,
	|	0,
	|	0,
	|	0,
	|	0,
	|	&СуммаВыкупаПредметовАренды,
	|	&СуммаНДСВыкупаПредметовАренды
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзменениеУсловийДоговораАренды.ГрафикНачисленияУслуг КАК ТаблицаГрафика
	|		ПО (ТаблицаГрафика.Ссылка = ДанныеДокумента.Ссылка)
	|			И (ТаблицаГрафика.Дата = ДанныеДокумента.ДатаВыкупа)
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И ТаблицаГрафика.Ссылка ЕСТЬ NULL
	|	И &СуммаВыкупаПредметовАренды <> 0
	|	И &ДатаВыкупа >= НАЧАЛОПЕРИОДА(&Период, ДЕНЬ)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаГрафикНачисленияПроцентовПоАренде(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ГрафикНачисленияПроцентовПоАренде";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтПредыдущиеУсловияДоговора(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ГрафикНачисленияПроцентовПоАренде.ДатаИзменения КАК ДатаИзменения,
	|	ГрафикНачисленияПроцентовПоАренде.Договор КАК Договор,
	|	&Ссылка КАК АктуальныеУсловияДоговора,
	|	ГрафикНачисленияПроцентовПоАренде.Дата КАК Дата,
	|	ГрафикНачисленияПроцентовПоАренде.Проценты КАК Проценты
	|ИЗ
	|	РегистрСведений.ГрафикНачисленияПроцентовПоАренде КАК ГрафикНачисленияПроцентовПоАренде
	|ГДЕ
	|	ГрафикНачисленияПроцентовПоАренде.АктуальныеУсловияДоговора В
	|			(ВЫБРАТЬ
	|				Т.АктуальныеУсловияДоговора
	|			ИЗ
	|				втПредыдущиеУсловияДоговора КАК Т)
	|	И ГрафикНачисленияПроцентовПоАренде.Дата < ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&Период, ДЕНЬ), ДЕНЬ, -1)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	ВЫБОР
	|		КОГДА НЕ &ИзменениеДоговора
	|			ТОГДА &Договор
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ТаблицаГрафика.Дата < НАЧАЛОПЕРИОДА(&Период, ДЕНЬ)
	|					ТОГДА &СтарыйДоговор
	|				ИНАЧЕ &Договор
	|			КОНЕЦ
	|	КОНЕЦ,
	|	&Ссылка,
	|	ТаблицаГрафика.Дата,
	|	ТаблицаГрафика.Проценты
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды.ГрафикНачисленияПроцентов КАК ТаблицаГрафика
	|ГДЕ
	|	ТаблицаГрафика.Ссылка = &Ссылка
	|	И ТаблицаГрафика.Дата >= ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&Период, ДЕНЬ), ДЕНЬ, -1)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаРасчетыПоФинансовымИнструментам(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РасчетыПоФинансовымИнструментам";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ИнициализироватьКлючиАналитикиУчетаПоПартнерам(Запрос);

	ИнициализироватьТаблицаВтОстаткиРасчетыПоФинансовымИнструментам(Запрос, ТекстыЗапроса);
	ТекстЗапросаТаблицаВтТаблицаОС(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтДоходыПриЧастичномВыкупе(Запрос, ТекстыЗапроса);
	ТекстЗапросаТаблицаВтУвеличениеСтоимости(Запрос, ТекстыЗапроса);
	ТекстЗапросаТаблицаВтУменьшениеСтоимости(Запрос, ТекстыЗапроса);
	ТекстЗапросаТаблицаВтКорректировкаРасчетов(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтИзлишнеНачисленныеПроценты(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтНедоначисленныеПроценты(ТекстыЗапроса);
	
	СписокЗапросовОбъединение = Новый Массив;

	#Область ПереносАрендныхОбязательств // Дт 76.07.1 - Кт 76.07.1
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период																КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)								КАК ВидДвижения,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.АналитикаУчетаПоПартнерам 	КАК АналитикаУчетаПоПартнерам,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.Договор					КАК Договор,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.ТипСуммы					КАК ТипСуммы,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.РасчетныйДокумент			КАК РасчетныйДокумент,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.Валюта						КАК Валюта,
	|	-ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаОстаток				КАК Сумма,
	|	-ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаУпрОстаток			КАК СуммаУпр,
	|	-ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаРеглОстаток			КАК СуммаРегл,
	|	НЕОПРЕДЕЛЕНО														КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО														КАК СтатьяАналитики,
	|	НЕОПРЕДЕЛЕНО														КАК ИдентификаторСтроки,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.ИдентификаторФинЗаписи		КАК ИдентификаторФинЗаписи,
	|	&ХО_ИзменениеУсловийДоговораАренды									КАК ХозяйственнаяОперация,
	|	&НастройкаХО_ИзменениеУсловийДоговораАренды							КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ВтОстаткиРасчетыПоФинансовымИнструментам КАК ВтОстаткиРасчетыПоФинансовымИнструментам
	|ГДЕ
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.АрендныеОбязательства)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО),
	|	&Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.АрендныеОбязательства),
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов,
	|	-ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаОстаток,
	|	-ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаУпрОстаток,
	|	-ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаРеглОстаток,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.ИдентификаторФинЗаписи,
	|	&ХО_ИзменениеУсловийДоговораАренды,
	|	&НастройкаХО_ИзменениеУсловийДоговораАренды
	|ИЗ
	|	ВтОстаткиРасчетыПоФинансовымИнструментам КАК ВтОстаткиРасчетыПоФинансовымИнструментам
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО (АналитикаПоПартнерам.Организация = &Организация)
	|			И (АналитикаПоПартнерам.Партнер = &Партнер)
	|			И (АналитикаПоПартнерам.Контрагент = &Контрагент)
	|			И (АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка))
	|			И (АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности)
	|ГДЕ
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.АрендныеОбязательства)";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область ПереносТекущихПлатежейПоАренде // Дт 76.07.2 - Кт 76.07.2
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период																КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)								КАК ВидДвижения,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.АналитикаУчетаПоПартнерам	КАК АналитикаУчетаПоПартнерам,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.Договор					КАК Договор,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.ТипСуммы					КАК ТипСуммы,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.РасчетныйДокумент			КАК РасчетныйДокумент,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.Валюта						КАК Валюта,
	|	-ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаОстаток				КАК Сумма,
	|	-ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаУпрОстаток			КАК СуммаУпр,
	|	-ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаРеглОстаток			КАК СуммаРегл,
	|	НЕОПРЕДЕЛЕНО														КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО														КАК СтатьяАналитики,
	|	НЕОПРЕДЕЛЕНО														КАК ИдентификаторСтроки,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.ИдентификаторФинЗаписи		КАК ИдентификаторФинЗаписи,
	|	&ХО_ИзменениеУсловийДоговораАренды									КАК ХозяйственнаяОперация,
	|	&НастройкаХО_ИзменениеУсловийДоговораАренды							КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ВтОстаткиРасчетыПоФинансовымИнструментам КАК ВтОстаткиРасчетыПоФинансовымИнструментам
	|ГДЕ
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.ТипСуммы В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.УслугаПоАренде),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.УслугаПоАрендеАванс))
	|		И ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаОстаток < 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО),
	|	&Договор,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.ТипСуммы,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов,
	|	-ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаОстаток,
	|	-ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаУпрОстаток,
	|	-ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаРеглОстаток,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.ИдентификаторФинЗаписи,
	|	&ХО_ИзменениеУсловийДоговораАренды,
	|	&НастройкаХО_ИзменениеУсловийДоговораАренды
	|ИЗ
	|	ВтОстаткиРасчетыПоФинансовымИнструментам КАК ВтОстаткиРасчетыПоФинансовымИнструментам
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО (АналитикаПоПартнерам.Организация = &Организация)
	|			И (АналитикаПоПартнерам.Партнер = &Партнер)
	|			И (АналитикаПоПартнерам.Контрагент = &Контрагент)
	|			И (АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка))
	|			И (АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности)
	|ГДЕ
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.ТипСуммы В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.УслугаПоАренде),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.УслугаПоАрендеАванс))
	|		И ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаОстаток < 0";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область ПереносАвансовТекущихПлатежейПоАренде // Дт 76.07.2 - Кт 76.07.2
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период																КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)								КАК ВидДвижения,
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО)			КАК АналитикаУчетаПоПартнерам,
	|	&Договор															КАК Договор,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.ТипСуммы					КАК ТипСуммы,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.РасчетныйДокумент			КАК РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов												КАК Валюта,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаОстаток				КАК Сумма,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаУпрОстаток			КАК СуммаУпр,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаРеглОстаток			КАК СуммаРегл,
	|	НЕОПРЕДЕЛЕНО														КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО														КАК СтатьяАналитики,
	|	НЕОПРЕДЕЛЕНО														КАК ИдентификаторСтроки,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.ИдентификаторФинЗаписи		КАК ИдентификаторФинЗаписи,
	|	&ХО_ИзменениеУсловийДоговораАренды									КАК ХозяйственнаяОперация,
	|	&НастройкаХО_ИзменениеУсловийДоговораАренды							КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ВтОстаткиРасчетыПоФинансовымИнструментам КАК ВтОстаткиРасчетыПоФинансовымИнструментам
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО (АналитикаПоПартнерам.Организация = &Организация)
	|			И (АналитикаПоПартнерам.Партнер = &Партнер)
	|			И (АналитикаПоПартнерам.Контрагент = &Контрагент)
	|			И (АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка))
	|			И (АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности)
	|ГДЕ
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.ТипСуммы В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.УслугаПоАренде),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.УслугаПоАрендеАванс))
	|	И ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаОстаток > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.АналитикаУчетаПоПартнерам,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.Договор,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.ТипСуммы,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.РасчетныйДокумент,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.Валюта,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаОстаток,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаУпрОстаток,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаРеглОстаток,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.ИдентификаторФинЗаписи,
	|	&ХО_ИзменениеУсловийДоговораАренды,
	|	&НастройкаХО_ИзменениеУсловийДоговораАренды
	|ИЗ
	|	ВтОстаткиРасчетыПоФинансовымИнструментам КАК ВтОстаткиРасчетыПоФинансовымИнструментам
	|ГДЕ
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.ТипСуммы В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.УслугаПоАренде),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.УслугаПоАрендеАванс))
	|	И ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаОстаток > 0";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область ПереносПервоначальныхАвансовПоАренде // Дт 76.07.3 - Кт 76.07.3
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период																КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)								КАК ВидДвижения,
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО)			КАК АналитикаУчетаПоПартнерам,
	|	&Договор															КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.ОбеспечительныйПлатеж)	КАК ТипСуммы,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.РасчетныйДокумент			КАК РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов												КАК Валюта,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаОстаток				КАК Сумма,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаУпрОстаток			КАК СуммаУпр,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаРеглОстаток			КАК СуммаРегл,
	|	НЕОПРЕДЕЛЕНО														КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО														КАК СтатьяАналитики,
	|	НЕОПРЕДЕЛЕНО														КАК ИдентификаторСтроки,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.ИдентификаторФинЗаписи		КАК ИдентификаторФинЗаписи,
	|	&ХО_ИзменениеУсловийДоговораАренды									КАК ХозяйственнаяОперация,
	|	&НастройкаХО_ИзменениеУсловийДоговораАренды							КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ВтОстаткиРасчетыПоФинансовымИнструментам КАК ВтОстаткиРасчетыПоФинансовымИнструментам
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО (АналитикаПоПартнерам.Организация = &Организация)
	|			И (АналитикаПоПартнерам.Партнер = &Партнер)
	|			И (АналитикаПоПартнерам.Контрагент = &Контрагент)
	|			И (АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка))
	|			И (АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности)
	|ГДЕ
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.ОбеспечительныйПлатеж)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.АналитикаУчетаПоПартнерам,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.Договор,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.ТипСуммы,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.РасчетныйДокумент,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.Валюта,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаОстаток,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаУпрОстаток,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаРеглОстаток,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.ИдентификаторФинЗаписи,
	|	&ХО_ИзменениеУсловийДоговораАренды,
	|	&НастройкаХО_ИзменениеУсловийДоговораАренды
	|ИЗ
	|	ВтОстаткиРасчетыПоФинансовымИнструментам КАК ВтОстаткиРасчетыПоФинансовымИнструментам
	|ГДЕ
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.ОбеспечительныйПлатеж)";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти 

	#Область ПереносАвансовПоВыкупнымПлатежам // Дт 76.07.4 - Кт 76.07.4
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период																КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)								КАК ВидДвижения,
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО)			КАК АналитикаУчетаПоПартнерам,
	|	&Договор															КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.ВыкупнаяСтоимостьАванс)	КАК ТипСуммы,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.РасчетныйДокумент			КАК РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов												КАК Валюта,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаОстаток				КАК Сумма,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаУпрОстаток			КАК СуммаУпр,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаРеглОстаток			КАК СуммаРегл,
	|	НЕОПРЕДЕЛЕНО														КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО														КАК СтатьяАналитики,
	|	НЕОПРЕДЕЛЕНО														КАК ИдентификаторСтроки,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.ИдентификаторФинЗаписи		КАК ИдентификаторФинЗаписи,
	|	&ХО_ИзменениеУсловийДоговораАренды									КАК ХозяйственнаяОперация,
	|	&НастройкаХО_ИзменениеУсловийДоговораАренды							КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ВтОстаткиРасчетыПоФинансовымИнструментам КАК ВтОстаткиРасчетыПоФинансовымИнструментам
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО (АналитикаПоПартнерам.Организация = &Организация)
	|			И (АналитикаПоПартнерам.Партнер = &Партнер)
	|			И (АналитикаПоПартнерам.Контрагент = &Контрагент)
	|			И (АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка))
	|			И (АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности)
	|ГДЕ
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.ВыкупнаяСтоимостьАванс)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.АналитикаУчетаПоПартнерам,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.Договор,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.ТипСуммы,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.РасчетныйДокумент,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.Валюта,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаОстаток,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаУпрОстаток,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаРеглОстаток,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.ИдентификаторФинЗаписи,
	|	&ХО_ИзменениеУсловийДоговораАренды,
	|	&НастройкаХО_ИзменениеУсловийДоговораАренды
	|ИЗ
	|	ВтОстаткиРасчетыПоФинансовымИнструментам КАК ВтОстаткиРасчетыПоФинансовымИнструментам
	|ГДЕ
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.ВыкупнаяСтоимостьАванс)";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область ПереносПроцентовПоАренднымОбязательствам // Дт 76.07.5 - Кт 76.07.5
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период																КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)								КАК ВидДвижения,
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО)			КАК АналитикаУчетаПоПартнерам,
	|	&Договор															КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.Проценты)				КАК ТипСуммы,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.РасчетныйДокумент			КАК РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов												КАК Валюта,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаОстаток				КАК Сумма,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаУпрОстаток			КАК СуммаУпр,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаРеглОстаток			КАК СуммаРегл,
	|	НЕОПРЕДЕЛЕНО														КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО														КАК СтатьяАналитики,
	|	НЕОПРЕДЕЛЕНО														КАК ИдентификаторСтроки,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.ИдентификаторФинЗаписи		КАК ИдентификаторФинЗаписи,
	|	&ХО_ИзменениеУсловийДоговораАренды									КАК ХозяйственнаяОперация,
	|	&НастройкаХО_ИзменениеУсловийДоговораАренды							КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ВтОстаткиРасчетыПоФинансовымИнструментам КАК ВтОстаткиРасчетыПоФинансовымИнструментам
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО (АналитикаПоПартнерам.Организация = &Организация)
	|			И (АналитикаПоПартнерам.Партнер = &Партнер)
	|			И (АналитикаПоПартнерам.Контрагент = &Контрагент)
	|			И (АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка))
	|			И (АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности)
	|ГДЕ
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.Проценты)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.АналитикаУчетаПоПартнерам,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.Договор,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.ТипСуммы,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.РасчетныйДокумент,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.Валюта,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаОстаток,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаУпрОстаток,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаРеглОстаток,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.ИдентификаторФинЗаписи,
	|	&ХО_ИзменениеУсловийДоговораАренды,
	|	&НастройкаХО_ИзменениеУсловийДоговораАренды
	|ИЗ
	|	ВтОстаткиРасчетыПоФинансовымИнструментам КАК ВтОстаткиРасчетыПоФинансовымИнструментам
	|ГДЕ
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.Проценты)";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область ПереносНДСПоАренднымОбязательствам // Дт 76.07.5 - Кт 76.07.5
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период																КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)								КАК ВидДвижения,
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО)			КАК АналитикаУчетаПоПартнерам,
	|	&Договор															КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.НДС)						КАК ТипСуммы,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.РасчетныйДокумент			КАК РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов												КАК Валюта,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаОстаток				КАК Сумма,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаУпрОстаток			КАК СуммаУпр,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаРеглОстаток			КАК СуммаРегл,
	|	НЕОПРЕДЕЛЕНО														КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО														КАК СтатьяАналитики,
	|	НЕОПРЕДЕЛЕНО														КАК ИдентификаторСтроки,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.ИдентификаторФинЗаписи		КАК ИдентификаторФинЗаписи,
	|	&ХО_ИзменениеУсловийДоговораАренды									КАК ХозяйственнаяОперация,
	|	&НастройкаХО_ИзменениеУсловийДоговораАренды							КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ВтОстаткиРасчетыПоФинансовымИнструментам КАК ВтОстаткиРасчетыПоФинансовымИнструментам
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО (АналитикаПоПартнерам.Организация = &Организация)
	|			И (АналитикаПоПартнерам.Партнер = &Партнер)
	|			И (АналитикаПоПартнерам.Контрагент = &Контрагент)
	|			И (АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка))
	|			И (АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности)
	|ГДЕ
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.НДС)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.АналитикаУчетаПоПартнерам,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.Договор,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.ТипСуммы,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.РасчетныйДокумент,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.Валюта,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаОстаток,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаУпрОстаток,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.СуммаРеглОстаток,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.ИдентификаторФинЗаписи,
	|	&ХО_ИзменениеУсловийДоговораАренды,
	|	&НастройкаХО_ИзменениеУсловийДоговораАренды
	|ИЗ
	|	ВтОстаткиРасчетыПоФинансовымИнструментам КАК ВтОстаткиРасчетыПоФинансовымИнструментам
	|ГДЕ
	|	ВтОстаткиРасчетыПоФинансовымИнструментам.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.НДС)";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область ФормированиеСтоимостиАрендованныхОС // Дт 08.04 - Кт 76.07.1
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период                                                                      КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)                                       КАК ВидДвижения,
	|	
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО)                   КАК АналитикаУчетаПоПартнерам,
	|	&Договор                                                                     КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.АрендныеОбязательства)            КАК ТипСуммы,
	|	НЕОПРЕДЕЛЕНО                                                                 КАК РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов                                                        КАК Валюта,
	|	
	|	ТаблицаОС.Стоимость КАК Сумма,
	|
	|	ТаблицаОС.СтоимостьУУ КАК СуммаУпр,
	|	ТаблицаОС.СтоимостьБУ КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО                                                                 КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО                                                                 КАК СтатьяАналитики,
	|	ТаблицаОС.ИдентификаторСтроки                                                КАК ИдентификаторСтроки,
	|	ТаблицаОС.ИдентификаторСтроки                                                КАК ИдентификаторФинЗаписи,
	|	&ХО_ФормированиеСтоимостиАрендованныхОС                                      КАК ХозяйственнаяОперация,
	|	&НастройкаХО_ФормированиеСтоимостиАрендованныхОС                             КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втТаблицаОС КАК ТаблицаОС
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО АналитикаПоПартнерам.Организация = &Организация
	|			И АналитикаПоПартнерам.Партнер = &Партнер
	|			И АналитикаПоПартнерам.Контрагент = &Контрагент
	|			И АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			И АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности
	|ГДЕ
	|	ТаблицаОС.ДействиеСПредметомАренды = ЗНАЧЕНИЕ(Перечисление.ДействияСПредметамиАренды.ВзятиеВАренду)
	|	И (&ЕстьУчетУУ ИЛИ &ЕстьУчетБУ)";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область УвеличениеСтоимостиОС // Дт <01.03> - Кт <76.07.1>
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО) КАК АналитикаУчетаПоПартнерам,
	|	&Договор КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.АрендныеОбязательства) КАК ТипСуммы,
	|	НЕОПРЕДЕЛЕНО КАК РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов КАК Валюта,
	|
	|	ВЫБОР
	|		КОГДА УвеличениеСтоимости.СтоимостьРегл <> 0
	|			ТОГДА УвеличениеСтоимости.СтоимостьРегл * ВЫБОР
	|				КОГДА &ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета
	|					ТОГДА 1
	|				ИНАЧЕ &КоэффициентПересчетаИзВалютыРегл
	|			КОНЕЦ
	|		КОГДА УвеличениеСтоимости.Стоимость <> 0
	|			ТОГДА УвеличениеСтоимости.Стоимость * ВЫБОР
	|				КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета
	|					ТОГДА 1
	|				ИНАЧЕ &КоэффициентПересчетаИзВалютыУпр
	|			КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Сумма,
	|
	|	УвеличениеСтоимости.Стоимость КАК СуммаУпр,
	|	УвеличениеСтоимости.СтоимостьРегл КАК СуммаРегл,
	|	НЕОПРЕДЕЛЕНО КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО КАК СтатьяАналитики,
	|	УвеличениеСтоимости.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	УвеличениеСтоимости.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ВЫБОР
	|		КОГДА УвеличениеСтоимости.Субаренда ТОГДА
	|			&ХО_ФормированиеСтоимостиАрендованныхОС
	|		ИНАЧЕ
	|			&ХО_УвеличениеСтоимостиАрендованныхОС
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|	ВЫБОР
	|		КОГДА УвеличениеСтоимости.Субаренда ТОГДА
	|			&НастройкаХО_ФормированиеСтоимостиАрендованныхОС
	|		ИНАЧЕ
	|			&НастройкаХО_УвеличениеСтоимостиАрендованныхОС
	|	КОНЕЦ КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втУвеличениеСтоимости КАК УвеличениеСтоимости
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО АналитикаПоПартнерам.Организация = &Организация
	|		И АналитикаПоПартнерам.Партнер = &Партнер
	|		И АналитикаПоПартнерам.Контрагент = &Контрагент
	|		И АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|		И АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности
	|ГДЕ
	|	УвеличениеСтоимости.Стоимость <> 0
	|	ИЛИ УвеличениеСтоимости.СтоимостьРегл <> 0";

	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область УменьшениеСтоимостиОС // Дт <76.07.1> - Кт <01.03>
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                                                           КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                            КАК ВидДвижения,
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО)        КАК АналитикаУчетаПоПартнерам,
	|	&Договор                                                          КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.АрендныеОбязательства) КАК ТипСуммы,
	|	НЕОПРЕДЕЛЕНО                                                      КАК РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов                                             КАК Валюта,
	|	
	|	ВЫБОР
	|		КОГДА УменьшениеСтоимости.СтоимостьРегл <> 0
	|			ТОГДА УменьшениеСтоимости.СтоимостьРегл
	|					* ВЫБОР
	|						КОГДА &ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета
	|							ТОГДА 1
	|						ИНАЧЕ &КоэффициентПересчетаИзВалютыРегл
	|					КОНЕЦ
	|		КОГДА УменьшениеСтоимости.Стоимость <> 0
	|			ТОГДА УменьшениеСтоимости.Стоимость
	|					* ВЫБОР
	|						КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета
	|							ТОГДА 1
	|						ИНАЧЕ &КоэффициентПересчетаИзВалютыУпр
	|					КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Сумма,
	|
	|	УменьшениеСтоимости.Стоимость                                     КАК СуммаУпр,
	|	УменьшениеСтоимости.СтоимостьРегл                                 КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО                                                      КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО                                                      КАК СтатьяАналитики,
	|	УменьшениеСтоимости.ИдентификаторСтроки                           КАК ИдентификаторСтроки,
	|	УменьшениеСтоимости.ИдентификаторСтроки                           КАК ИдентификаторФинЗаписи,
	|	ВЫБОР
	|		КОГДА УменьшениеСтоимости.Субаренда ТОГДА
	|			&ХО_ФормированиеСтоимостиАрендованныхОС
	|		ИНАЧЕ
	|			&ХО_УменьшениеСтоимостиАрендованныхОС
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|	ВЫБОР
	|		КОГДА УменьшениеСтоимости.Субаренда ТОГДА
	|			&НастройкаХО_ФормированиеСтоимостиАрендованныхОС
	|		ИНАЧЕ
	|			&НастройкаХО_УменьшениеСтоимостиАрендованныхОС
	|	КОНЕЦ КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втУменьшениеСтоимости КАК УменьшениеСтоимости
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО АналитикаПоПартнерам.Организация = &Организация
	|			И АналитикаПоПартнерам.Партнер = &Партнер
	|			И АналитикаПоПартнерам.Контрагент = &Контрагент
	|			И АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			И АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности
	|ГДЕ
	|	(УменьшениеСтоимости.Стоимость <> 0
	|		ИЛИ УменьшениеСтоимости.СтоимостьРегл <> 0)";

	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область УвеличениеНДС // Дт <76.07.9> - Кт <76.07.1>
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период                                                                 КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                                  КАК ВидДвижения,
	|	
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО)              КАК АналитикаУчетаПоПартнерам,
	|	&Договор                                                                КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.НДС)                         КАК ТипСуммы,
	|	НЕОПРЕДЕЛЕНО                                                            КАК РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов                                                   КАК Валюта,
	|	
	|	Расчеты.СуммаНДС                                                        КАК Сумма,
	|	Расчеты.СуммаНДСУпр                                                     КАК СуммаУпр,
	|	Расчеты.СуммаНДСРегл                                                    КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО                                                            КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО                                                            КАК СтатьяАналитики,
	|	НЕОПРЕДЕЛЕНО                                                            КАК ИдентификаторСтроки,
	|	&ИдентификаторДокумента                                                 КАК ИдентификаторФинЗаписи,
	|	&ХО_УвеличениеНДСПоАренде                                               КАК ХозяйственнаяОперация,
	|	&НастройкаХО_УвеличениеНДСПоАренде                                      КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втКорректировкаРасчетов КАК Расчеты
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО АналитикаПоПартнерам.Организация = &Организация
	|			И АналитикаПоПартнерам.Партнер = &Партнер
	|			И АналитикаПоПартнерам.Контрагент = &Контрагент
	|			И АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			И АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности
	|ГДЕ
	|	Расчеты.СуммаНДС > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период                                                                 КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)                                  КАК ВидДвижения,
	|	
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО)              КАК АналитикаУчетаПоПартнерам,
	|	&Договор                                                                КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.АрендныеОбязательства)       КАК ТипСуммы,
	|	НЕОПРЕДЕЛЕНО                                                            КАК РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов                                                   КАК Валюта,
	|	
	|	Расчеты.СуммаНДС                                                        КАК Сумма,
	|	Расчеты.СуммаНДСУпр                                                     КАК СуммаУпр,
	|	Расчеты.СуммаНДСРегл                                                    КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО                                                            КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО                                                            КАК СтатьяАналитики,
	|	НЕОПРЕДЕЛЕНО                                                            КАК ИдентификаторСтроки,
	|	&ИдентификаторДокумента                                                 КАК ИдентификаторФинЗаписи,
	|	&ХО_УвеличениеНДСПоАренде                                               КАК ХозяйственнаяОперация,
	|	&НастройкаХО_УвеличениеНДСПоАренде                                      КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втКорректировкаРасчетов КАК Расчеты
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО АналитикаПоПартнерам.Организация = &Организация
	|			И АналитикаПоПартнерам.Партнер = &Партнер
	|			И АналитикаПоПартнерам.Контрагент = &Контрагент
	|			И АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			И АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности
	|ГДЕ
	|	Расчеты.СуммаНДС > 0";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область УменьшениеНДС // Дт <76.07.1> - Кт <76.07.9>
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период                                                                 КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                                  КАК ВидДвижения,
	|	
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО)              КАК АналитикаУчетаПоПартнерам,
	|	&Договор                                                                КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.АрендныеОбязательства)       КАК ТипСуммы,
	|	НЕОПРЕДЕЛЕНО                                                            КАК РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов                                                   КАК Валюта,
	|	
	|	-Расчеты.СуммаНДС                                                       КАК Сумма,
	|	-Расчеты.СуммаНДСУпр                                                    КАК СуммаУпр,
	|	-Расчеты.СуммаНДСРегл                                                   КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО                                                            КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО                                                            КАК СтатьяАналитики,
	|	НЕОПРЕДЕЛЕНО                                                            КАК ИдентификаторСтроки,
	|	&ИдентификаторДокумента                                                 КАК ИдентификаторФинЗаписи,
	|	&ХО_УменьшениеНДСПоАренде                                               КАК ХозяйственнаяОперация,
	|	&НастройкаХО_УменьшениеНДСПоАренде                                      КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втКорректировкаРасчетов КАК Расчеты
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО АналитикаПоПартнерам.Организация = &Организация
	|			И АналитикаПоПартнерам.Партнер = &Партнер
	|			И АналитикаПоПартнерам.Контрагент = &Контрагент
	|			И АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			И АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности
	|ГДЕ
	|	Расчеты.СуммаНДС < 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период                                                                 КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)                                  КАК ВидДвижения,
	|	
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО)              КАК АналитикаУчетаПоПартнерам,
	|	&Договор                                                                КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.НДС)                         КАК ТипСуммы,
	|	НЕОПРЕДЕЛЕНО                                                            КАК РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов                                                   КАК Валюта,
	|	
	|	-Расчеты.СуммаНДС                                                       КАК Сумма,
	|	-Расчеты.СуммаНДСУпр                                                    КАК СуммаУпр,
	|	-Расчеты.СуммаНДСРегл                                                   КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО                                                            КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО                                                            КАК СтатьяАналитики,
	|	НЕОПРЕДЕЛЕНО                                                            КАК ИдентификаторСтроки,
	|	&ИдентификаторДокумента                                                 КАК ИдентификаторФинЗаписи,
	|	&ХО_УменьшениеНДСПоАренде                                               КАК ХозяйственнаяОперация,
	|	&НастройкаХО_УменьшениеНДСПоАренде                                      КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втКорректировкаРасчетов КАК Расчеты
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО АналитикаПоПартнерам.Организация = &Организация
	|			И АналитикаПоПартнерам.Партнер = &Партнер
	|			И АналитикаПоПартнерам.Контрагент = &Контрагент
	|			И АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			И АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности
	|ГДЕ
	|	Расчеты.СуммаНДС < 0";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область УвеличениеПроцентов // Дт <76.07.5> - Кт <76.07.1>
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период                                                                 КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                                  КАК ВидДвижения,
	|	
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО)              КАК АналитикаУчетаПоПартнерам,
	|	&Договор                                                                КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.Проценты)                    КАК ТипСуммы,
	|	НЕОПРЕДЕЛЕНО                                                            КАК РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов                                                   КАК Валюта,
	|	
	|	Расчеты.СуммаПроцентов                                                  КАК Сумма,
	|	Расчеты.СуммаПроцентовУпр                                               КАК СуммаУпр,
	|	Расчеты.СуммаПроцентовРегл                                              КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО                                                            КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО                                                            КАК СтатьяАналитики,
	|	НЕОПРЕДЕЛЕНО                                                            КАК ИдентификаторСтроки,
	|	&ИдентификаторДокумента                                                 КАК ИдентификаторФинЗаписи,
	|	&ХО_УвеличениеПроцентовПоАренде                                         КАК ХозяйственнаяОперация,
	|	&НастройкаХО_УвеличениеПроцентовПоАренде                                КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втКорректировкаРасчетов КАК Расчеты
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО АналитикаПоПартнерам.Организация = &Организация
	|			И АналитикаПоПартнерам.Партнер = &Партнер
	|			И АналитикаПоПартнерам.Контрагент = &Контрагент
	|			И АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			И АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности
	|ГДЕ
	|	Расчеты.СуммаПроцентов > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период                                                                 КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)                                  КАК ВидДвижения,
	|	
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО)              КАК АналитикаУчетаПоПартнерам,
	|	&Договор                                                                КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.АрендныеОбязательства)       КАК ТипСуммы,
	|	НЕОПРЕДЕЛЕНО                                                            КАК РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов                                                   КАК Валюта,
	|	
	|	Расчеты.СуммаПроцентов                                                  КАК Сумма,
	|	Расчеты.СуммаПроцентовУпр                                               КАК СуммаУпр,
	|	Расчеты.СуммаПроцентовРегл                                              КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО                                                            КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО                                                            КАК СтатьяАналитики,
	|	НЕОПРЕДЕЛЕНО                                                            КАК ИдентификаторСтроки,
	|	&ИдентификаторДокумента                                                 КАК ИдентификаторФинЗаписи,
	|	&ХО_УвеличениеПроцентовПоАренде                                         КАК ХозяйственнаяОперация,
	|	&НастройкаХО_УвеличениеПроцентовПоАренде                                КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втКорректировкаРасчетов КАК Расчеты
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО АналитикаПоПартнерам.Организация = &Организация
	|			И АналитикаПоПартнерам.Партнер = &Партнер
	|			И АналитикаПоПартнерам.Контрагент = &Контрагент
	|			И АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			И АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности
	|ГДЕ
	|	Расчеты.СуммаПроцентов > 0";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область УменьшениеПроцентов // Дт <76.07.1> - Кт <76.07.5>
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период                                                                 КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                                  КАК ВидДвижения,
	|	
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО)              КАК АналитикаУчетаПоПартнерам,
	|	&Договор                                                                КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.АрендныеОбязательства)       КАК ТипСуммы,
	|	НЕОПРЕДЕЛЕНО                                                            КАК РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов                                                   КАК Валюта,
	|	
	|	-Расчеты.СуммаПроцентов                                                 КАК Сумма,
	|	-Расчеты.СуммаПроцентовУпр                                              КАК СуммаУпр,
	|	-Расчеты.СуммаПроцентовРегл                                             КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО                                                            КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО                                                            КАК СтатьяАналитики,
	|	НЕОПРЕДЕЛЕНО                                                            КАК ИдентификаторСтроки,
	|	&ИдентификаторДокумента                                                 КАК ИдентификаторФинЗаписи,
	|	&ХО_УменьшениеПроцентовПоАренде                                         КАК ХозяйственнаяОперация,
	|	&НастройкаХО_УменьшениеПроцентовПоАренде                                КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втКорректировкаРасчетов КАК Расчеты
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО АналитикаПоПартнерам.Организация = &Организация
	|			И АналитикаПоПартнерам.Партнер = &Партнер
	|			И АналитикаПоПартнерам.Контрагент = &Контрагент
	|			И АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			И АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности
	|ГДЕ
	|	Расчеты.СуммаПроцентов < 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период                                                                 КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)                                  КАК ВидДвижения,
	|	
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО)              КАК АналитикаУчетаПоПартнерам,
	|	&Договор                                                                КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.Проценты)                    КАК ТипСуммы,
	|	НЕОПРЕДЕЛЕНО                                                            КАК РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов                                                   КАК Валюта,
	|	
	|	-Расчеты.СуммаПроцентов                                                 КАК Сумма,
	|	-Расчеты.СуммаПроцентовУпр                                              КАК СуммаУпр,
	|	-Расчеты.СуммаПроцентовРегл                                             КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО                                                            КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО                                                            КАК СтатьяАналитики,
	|	НЕОПРЕДЕЛЕНО                                                            КАК ИдентификаторСтроки,
	|	&ИдентификаторДокумента                                                 КАК ИдентификаторФинЗаписи,
	|	&ХО_УменьшениеПроцентовПоАренде                                         КАК ХозяйственнаяОперация,
	|	&НастройкаХО_УменьшениеПроцентовПоАренде                                КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втКорректировкаРасчетов КАК Расчеты
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО АналитикаПоПартнерам.Организация = &Организация
	|			И АналитикаПоПартнерам.Партнер = &Партнер
	|			И АналитикаПоПартнерам.Контрагент = &Контрагент
	|			И АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			И АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности
	|ГДЕ
	|	Расчеты.СуммаПроцентов < 0";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область ИзлишнеНачисленныеПроценты // Дт <76.07.5> - Кт <91.01>
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период                                                                 КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                                  КАК ВидДвижения,
	|	
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО)              КАК АналитикаУчетаПоПартнерам,
	|	&Договор                                                                КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.Проценты)                    КАК ТипСуммы,
	|	НЕОПРЕДЕЛЕНО                                                            КАК РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов                                                   КАК Валюта,
	|	
	|	Расчеты.Сумма                                                           КАК Сумма,
	|	Расчеты.СуммаУпр                                                        КАК СуммаУпр,
	|	Расчеты.СуммаРегл                                                       КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО                                                            КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО                                                            КАК СтатьяАналитики,
	|	НЕОПРЕДЕЛЕНО                                                            КАК ИдентификаторСтроки,
	|	&ИдентификаторДокумента                                                 КАК ИдентификаторФинЗаписи,
	|	&ХО_ИзлишнеНачисленныеПроцентыПоАренде                                  КАК ХозяйственнаяОперация,
	|	&НастройкаХО_ИзлишнеНачисленныеПроцентыПоАренде                         КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втИзлишнеНачисленныеПроценты КАК Расчеты
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО АналитикаПоПартнерам.Организация = &Организация
	|			И АналитикаПоПартнерам.Партнер = &Партнер
	|			И АналитикаПоПартнерам.Контрагент = &Контрагент
	|			И АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			И АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности
	|ГДЕ
	|	&СозданДляПерехода";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область НедоначисленныеПроценты // Дт <91.02> - Кт <76.07.5>
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период                                                                 КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)                                  КАК ВидДвижения,
	|	
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО)              КАК АналитикаУчетаПоПартнерам,
	|	&Договор                                                                КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.Проценты)                    КАК ТипСуммы,
	|	НЕОПРЕДЕЛЕНО                                                            КАК РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов                                                   КАК Валюта,
	|	
	|	Расчеты.Сумма                                                           КАК Сумма,
	|	Расчеты.СуммаУпр                                                        КАК СуммаУпр,
	|	Расчеты.СуммаРегл                                                       КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО                                                            КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО                                                            КАК СтатьяАналитики,
	|	НЕОПРЕДЕЛЕНО                                                            КАК ИдентификаторСтроки,
	|	&ИдентификаторДокумента                                                 КАК ИдентификаторФинЗаписи,
	|	&ХО_НедоначисленныеПроцентыПоАренде                                     КАК ХозяйственнаяОперация,
	|	&НастройкаХО_НедоначисленныеПроцентыПоАренде                            КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втНедоначисленныеПроценты КАК Расчеты
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО АналитикаПоПартнерам.Организация = &Организация
	|			И АналитикаПоПартнерам.Партнер = &Партнер
	|			И АналитикаПоПартнерам.Контрагент = &Контрагент
	|			И АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			И АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности
	|ГДЕ
	|	&СозданДляПерехода";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область ОтражениеАрендныхОбязательствВДоходах // Дт <76.07.1> - Кт <Доходы>
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                                                                 КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                                  КАК ВидДвижения,
	|	
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО)              КАК АналитикаУчетаПоПартнерам,
	|	&Договор                                                                КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.АрендныеОбязательства)       КАК ТипСуммы,
	|	НЕОПРЕДЕЛЕНО                                                            КАК РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов                                                   КАК Валюта,
	|	
	|	ТаблицаДоходы.Сумма                                                     КАК Сумма,
	|	ТаблицаДоходы.СуммаУпр                                                  КАК СуммаУпр,
	|	ТаблицаДоходы.СуммаРегл                                                 КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО                                                            КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО                                                            КАК СтатьяАналитики,
	|	НЕОПРЕДЕЛЕНО                                                            КАК ИдентификаторСтроки,
	|	ТаблицаДоходы.ИдентификаторФинЗаписи                                    КАК ИдентификаторФинЗаписи,
	|	ТаблицаДоходы.ХозяйственнаяОперация                                     КАК ХозяйственнаяОперация,
	|	ТаблицаДоходы.НастройкаХозяйственнойОперации                            КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втДоходыПриЧастичномВыкупе КАК ТаблицаДоходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО (АналитикаПоПартнерам.Организация = &Организация)
	|			И (АналитикаПоПартнерам.Партнер = &Партнер)
	|			И (АналитикаПоПартнерам.Контрагент = &Контрагент)
	|			И (АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка))
	|			И (АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности)";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(СписокЗапросовОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеРасходы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаДвиженияПоПрочимАктивамПассивам(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "ДвиженияПоПрочимАктивамПассивам";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыУчетаОС(ТекстыЗапроса,, "втСведенияОбУчете");
	ОтложенноеФормированиеДвиженийВНА.ТекстЗапросаПустыеТаблицыОтложенныхДвижений(Запрос, ТекстыЗапроса);
	ИнициализироватьТаблицаВтКорректировкаСтоимостиОСПоДолям(Запрос, ТекстыЗапроса);
	ТекстЗапросаТаблицаВтНедоначисленныеПроценты(ТекстыЗапроса);

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                           КАК Период,
	|	&Организация                      КАК Организация,
	|	&ПодразделениеПроценты            КАК Подразделение,
	|	&НаправлениеДеятельности          КАК НаправлениеДеятельности,
	|	&СтатьяРасходов                   КАК Статья,
	|	&АналитикаАктивовПассивовРасходы  КАК Аналитика,
	|	&НастройкаСчетовУчетаРасходы      КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Кредит) КАК ДебетКредит,
	|
	|	ВЫБОР
	|		КОГДА втСведенияОбУчете.ОтражатьВРеглУчете
	|			ТОГДА ВЫБОР
	|					КОГДА ВТ_АмортизацияВНА.НачислятьИзнос
	|						ТОГДА 0
	|					ИНАЧЕ ВЫБОР
	|							КОГДА ЕСТЬNULL(ВТ_СтоимостьВНА.СтоимостьРегл, 0) > 0
	|								ТОГДА (1 - (ВТ_СтоимостьВНА.СтоимостьРегл - Проценты.СуммаПроцентовРегл) / ВТ_СтоимостьВНА.СтоимостьРегл) 
	|										* ВТ_АмортизацияВНА.АмортизацияРегл
	|							ИНАЧЕ 0
	|						КОНЕЦ
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаРегл,
	|
	|	0 КАК ПостояннаяРазница,
	|
	|	ВЫБОР
	|		КОГДА втСведенияОбУчете.ОтражатьВРеглУчете
	|			ТОГДА ВЫБОР
	|					КОГДА ВТ_АмортизацияВНА.НачислятьИзнос
	|						ТОГДА 0
	|					ИНАЧЕ ВЫБОР
	|							КОГДА ЕСТЬNULL(ВТ_СтоимостьВНА.СтоимостьРегл, 0) > 0
	|								ТОГДА (1 - (ВТ_СтоимостьВНА.СтоимостьРегл - Проценты.СуммаПроцентовРегл) / ВТ_СтоимостьВНА.СтоимостьРегл) 
	|										* ВТ_АмортизацияВНА.АмортизацияРегл
	|							ИНАЧЕ 0
	|						КОНЕЦ
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ВременнаяРазница,
	|
	|	ВЫБОР
	|		КОГДА втСведенияОбУчете.ОтражатьВУпрУчете
	|			ТОГДА ВЫБОР
	|					КОГДА ЕСТЬNULL(ВТ_СтоимостьВНА.Стоимость, 0) > 0
	|						ТОГДА (1 - (ВТ_СтоимостьВНА.Стоимость - Проценты.СуммаПроцентовУпр) / ВТ_СтоимостьВНА.Стоимость) 
	|								* ВТ_АмортизацияВНА.Амортизация
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаУпр,
	|
	|	ВЫБОР
	|		КОГДА втСведенияОбУчете.ОтражатьВУпрУчете
	|			ТОГДА ВЫБОР
	|					КОГДА ЕСТЬNULL(ВТ_СтоимостьВНА.Стоимость, 0) > 0
	|						ТОГДА (1 - (ВТ_СтоимостьВНА.Стоимость - Проценты.СуммаПроцентовУпр) / ВТ_СтоимостьВНА.Стоимость) 
	|								* ВТ_АмортизацияВНА.Амортизация
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаСНДС,
	|
	|	ВЫБОР
	|		КОГДА втСведенияОбУчете.ОтражатьВУпрУчете
	|			ТОГДА ВЫБОР
	|					КОГДА ЕСТЬNULL(ВТ_СтоимостьВНА.Стоимость, 0) > 0
	|						ТОГДА (1 - (ВТ_СтоимостьВНА.Стоимость - Проценты.СуммаПроцентовУпр) / ВТ_СтоимостьВНА.Стоимость) 
	|								* ВТ_АмортизацияВНА.Амортизация
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаБезНДС,
	|
	|	Проценты.ИдентификаторСтроки                      КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_УменьшениеНакопленнойАмортизации     КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втКорректировкаСтоимостиОСПоДолям КАК Проценты
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК ВТ_СтоимостьВНА
	|		ПО Проценты.ОсновноеСредство = ВТ_СтоимостьВНА.ОбъектУчета
	|			И (ВТ_СтоимостьВНА.Ссылка = &Ссылка)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК ВТ_АмортизацияВНА
	|		ПО Проценты.ОсновноеСредство = ВТ_АмортизацияВНА.ОбъектУчета
	|			И (ВТ_АмортизацияВНА.Ссылка = &Ссылка)
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК втСведенияОбУчете
	|		ПО втСведенияОбУчете.ОсновноеСредство = Проценты.ОсновноеСредство
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(&СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиАктивовПассивов)
	|	И &СозданДляПерехода
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период                           КАК Период,
	|	&Организация                      КАК Организация,
	|	&ПодразделениеПроценты            КАК Подразделение,
	|	&НаправлениеДеятельности          КАК НаправлениеДеятельности,
	|	&СтатьяРасходов                   КАК Статья,
	|	&АналитикаАктивовПассивовРасходы  КАК Аналитика,
	|	&НастройкаСчетовУчетаРасходы      КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Дебет) КАК ДебетКредит,
	|
	|	ТаблицаРасходы.СуммаРегл КАК СуммаРегл,
	|
	|	0 КАК ПостояннаяРазница,
	|
	|	ТаблицаРасходы.СуммаРегл КАК ВременнаяРазница,
	|
	|	ТаблицаРасходы.СуммаУпр КАК СуммаУпр,
	|	ТаблицаРасходы.СуммаУпр КАК СуммаСНДС,
	|	ТаблицаРасходы.СуммаУпр КАК СуммаБезНДС,
	|
	|	&ИдентификаторДокумента КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_НедоначисленныеПроцентыПоАренде КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втНедоначисленныеПроценты КАК ТаблицаРасходы
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(&СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиАктивовПассивов)
	|	И &СозданДляПерехода";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПартииПрочихРасходов(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	ИмяРегистра = "ПартииПрочихРасходов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтПартииПрочихРасходов(Запрос, ТекстыЗапроса);
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаПартииПрочихРасходов();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПрочиеДоходы(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПрочиеДоходы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;

	ТекстЗапросаТаблицаВтИзлишнеНачисленныеПроценты(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтДоходыПриЧастичномВыкупе(Запрос, ТекстыЗапроса);
	ИнициализироватьТаблицаВтКорректировкаСтоимостиОСПоДолям(Запрос, ТекстыЗапроса);
	
	СписокЗапросовОбъединение = Новый Массив;
	
	#Область ИзлишнеНачисленныеПроценты
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)            КАК ВидДвижения,
	|	&Период                                           КАК Период,
	|	&Организация                                      КАК Организация,
	|	&Подразделение                                    КАК Подразделение,
	|	&НаправлениеДеятельности                          КАК НаправлениеДеятельности,
	|	&СтатьяДоходов                                    КАК СтатьяДоходов,
	|	&АналитикаДоходов                                 КАК АналитикаДоходов,
	|
	|	ТаблицаДоходы.Сумма                               КАК Сумма,
	|	ТаблицаДоходы.СуммаУпр                            КАК СуммаУпр,
	|	ТаблицаДоходы.СуммаРегл                           КАК СуммаРегл,
	|
	|	&ИдентификаторДокумента                           КАК ИдентификаторФинЗаписи,
	|	&ХО_ИзлишнеНачисленныеПроцентыПоАренде            КАК ХозяйственнаяОперация,
	|	&НастройкаХО_ИзлишнеНачисленныеПроцентыПоАренде   КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втИзлишнеНачисленныеПроценты КАК ТаблицаДоходы
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(&СтатьяДоходов) = ТИП(ПланВидовХарактеристик.СтатьиДоходов)
	|	И &СозданДляПерехода";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область ПревышениеУменьшенияППАНадБалансовойСтоимостью
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                КАК ВидДвижения,
	|	&Период                                               КАК Период,
	|	&Организация                                          КАК Организация,
	|	&Подразделение                                        КАК Подразделение,
	|	&НаправлениеДеятельности                              КАК НаправлениеДеятельности,
	|	&СтатьяДоходов                                        КАК СтатьяДоходов,
	|	&АналитикаДоходов                                     КАК АналитикаДоходов,
	|
	|	ВЫБОР
	|		КОГДА УменьшениеСтоимости.Стоимость > БалансоваяСтоимость.БалансоваяСтоимостьУпр ТОГДА
	|			УменьшениеСтоимости.Стоимость - БалансоваяСтоимость.БалансоваяСтоимостьУпр
	|		ИНАЧЕ 0
	|		КОНЕЦ                                             КАК Сумма,
	|
	|	ВЫБОР
	|		КОГДА УменьшениеСтоимости.Стоимость > БалансоваяСтоимость.БалансоваяСтоимостьУпр ТОГДА
	|			УменьшениеСтоимости.Стоимость - БалансоваяСтоимость.БалансоваяСтоимостьУпр
	|		ИНАЧЕ 0
	|		КОНЕЦ                                             КАК СуммаУпр,
	|	
	|	ВЫБОР
	|		КОГДА УменьшениеСтоимости.СтоимостьРегл > БалансоваяСтоимость.БалансоваяСтоимостьРегл ТОГДА
	|			УменьшениеСтоимости.СтоимостьРегл - БалансоваяСтоимость.БалансоваяСтоимостьРегл
	|		ИНАЧЕ 0
	|		КОНЕЦ                                             КАК СуммаРегл,
	|	
	|	УменьшениеСтоимости.ИдентификаторСтроки               КАК ИдентификаторФинЗаписи,
	|	&ХО_УменьшениеСтоимостиАрендованныхОС                 КАК ХозяйственнаяОперация,
	|	&НастройкаХО_УменьшениеСтоимостиАрендованныхОС        КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	втУменьшениеСтоимости КАК УменьшениеСтоимости
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтБалансоваяСтоимостьОС КАК БалансоваяСтоимость
	|		ПО УменьшениеСтоимости.ОсновноеСредство = БалансоваяСтоимость.ОсновноеСредство
	|ГДЕ
	|	НЕ УменьшениеСтоимости.Субаренда
	|	И (ВЫБОР
	|		КОГДА УменьшениеСтоимости.Стоимость > БалансоваяСтоимость.БалансоваяСтоимостьУпр ТОГДА
	|			УменьшениеСтоимости.Стоимость - БалансоваяСтоимость.БалансоваяСтоимостьУпр
	|		ИНАЧЕ 0
	|		КОНЕЦ > 0
	|
	|	ИЛИ
	|
	|	ВЫБОР
	|		КОГДА УменьшениеСтоимости.СтоимостьРегл > БалансоваяСтоимость.БалансоваяСтоимостьРегл ТОГДА
	|			УменьшениеСтоимости.СтоимостьРегл - БалансоваяСтоимость.БалансоваяСтоимостьРегл
	|		ИНАЧЕ 0
	|		КОНЕЦ > 0)
	|";
	
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область УменьшениеНакопленнойАмортизацииПриПереходеНаФСБУ25
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)   КАК ВидДвижения,
	|	&Период                                  КАК Период,
	|	&Организация                             КАК Организация,
	|	втСведенияОбУчете.Местонахождение        КАК Подразделение,
	|	&НаправлениеДеятельности                 КАК НаправлениеДеятельности,
	|	&СтатьяДоходов                           КАК СтатьяДоходов,
	|	&АналитикаДоходов                        КАК АналитикаДоходов,
	|
	|	ВЫБОР
	|		КОГДА втСведенияОбУчете.ОтражатьВУпрУчете
	|			ТОГДА ВЫБОР
	|					КОГДА ЕСТЬNULL(ВТ_СтоимостьВНА.Стоимость, 0) <> 0 
	|						ТОГДА (1 - (ВТ_СтоимостьВНА.Стоимость - Проценты.СуммаПроцентовУпр) / ВТ_СтоимостьВНА.Стоимость) 
	|								* ВТ_АмортизацияВНА.Амортизация
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Сумма,
	|
	|	ВЫБОР
	|		КОГДА втСведенияОбУчете.ОтражатьВУпрУчете
	|			ТОГДА ВЫБОР
	|					КОГДА ЕСТЬNULL(ВТ_СтоимостьВНА.Стоимость, 0) <> 0
	|						ТОГДА (1 - (ВТ_СтоимостьВНА.Стоимость - Проценты.СуммаПроцентовУпр) / ВТ_СтоимостьВНА.Стоимость) 
	|								* ВТ_АмортизацияВНА.Амортизация
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаУпр,
	|
	|	ВЫБОР
	|		КОГДА втСведенияОбУчете.ОтражатьВРеглУчете
	|			ТОГДА ВЫБОР
	|					КОГДА ВТ_АмортизацияВНА.НачислятьИзнос
	|						ТОГДА 0
	|					ИНАЧЕ ВЫБОР
	|							КОГДА ЕСТЬNULL(ВТ_СтоимостьВНА.СтоимостьРегл, 0) <> 0
	|								ТОГДА (1 - (ВТ_СтоимостьВНА.СтоимостьРегл - Проценты.СуммаПроцентовРегл) / ВТ_СтоимостьВНА.СтоимостьРегл) 
	|										* ВТ_АмортизацияВНА.АмортизацияРегл
	|							ИНАЧЕ 0
	|						КОНЕЦ
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаРегл,
	|
	|	Проценты.ИдентификаторСтроки                  КАК ИдентификаторФинЗаписи,
	|	&ХО_УменьшениеНакопленнойАмортизации          КАК ХозяйственнаяОперация,
	|	&НастройкаХО_УменьшениеНакопленнойАмортизации КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втКорректировкаСтоимостиОСПоДолям КАК Проценты
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК ВТ_СтоимостьВНА
	|		ПО Проценты.ОсновноеСредство = ВТ_СтоимостьВНА.ОбъектУчета
	|			И (ВТ_СтоимостьВНА.Ссылка = &Ссылка)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК ВТ_АмортизацияВНА
	|		ПО Проценты.ОсновноеСредство = ВТ_АмортизацияВНА.ОбъектУчета
	|			И (ВТ_АмортизацияВНА.Ссылка = &Ссылка)
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК втСведенияОбУчете
	|		ПО Проценты.ОсновноеСредство = втСведенияОбУчете.ОсновноеСредство
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(&СтатьяДоходов) = ТИП(ПланВидовХарактеристик.СтатьиДоходов)
	|	И ТИПЗНАЧЕНИЯ(&СтатьяРасходов) <> ТИП(ПланВидовХарактеристик.СтатьиАктивовПассивов)
	|	И &СозданДляПерехода";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ОтражениеАрендныхОбязательствВДоходах // Дт <76.07.1> - Кт <Доходы>
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)				КАК ВидДвижения,
	|	&Период												КАК Период,
	|	&Организация										КАК Организация,
	|	втСведенияОбУчете.Местонахождение					КАК Подразделение,
	|	&НаправлениеДеятельности							КАК НаправлениеДеятельности,
	|	&СтатьяДоходов										КАК СтатьяДоходов,
	|	&АналитикаДоходов									КАК АналитикаДоходов,
	|
	|	Доходы.Сумма 										КАК Сумма,
	|	Доходы.СуммаУпр 									КАК СуммаУпр,
	|	Доходы.СуммаРегл 									КАК СуммаРегл,
	|
	|	Доходы.ИдентификаторФинЗаписи						КАК ИдентификаторФинЗаписи,
	|	&ХО_ОтражениеАрендныхОбязательствВДоходах			КАК ХозяйственнаяОперация,
	|	&НастройкаХО_ОтражениеАрендныхОбязательствВДоходах	КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ВтДоходыПриЧастичномВыкупе КАК Доходы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК втСведенияОбУчете
	|		ПО Доходы.ОсновноеСредство = втСведенияОбУчете.ОсновноеСредство";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(СписокЗапросовОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаТоварыКОформлениюДокументовИмпорта(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ТоварыКОформлениюДокументовИмпорта";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтПредыдущиеУсловияДоговора(ТекстыЗапроса);  
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	КОНЕЦПЕРИОДА(ТоварыКОформлениюДокументовИмпортаОстаткиИОбороты.Период, МЕСЯЦ) КАК Период,
	|	ТоварыКОформлениюДокументовИмпортаОстаткиИОбороты.СуммаКонечныйОстаток КАК СуммаКонечныйОстаток
	|ПОМЕСТИТЬ ВтТоварыКОформлениюДокументовИмпортаОстатки
	|ИЗ
	|	РегистрНакопления.ТоварыКОформлениюДокументовИмпорта.ОстаткиИОбороты(,, Месяц,,
	|		ДокументПоступления = &ДокументЗаключенияДоговораАренды
	|	И ТипДокументаИмпорта = &ИдентификаторЗаявлениеОВвозеТоваров) КАК ТоварыКОформлениюДокументовИмпортаОстаткиИОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОНЕЦПЕРИОДА(ПредыдущийГрафикОплатыУслугПоАренде.Дата, МЕСЯЦ) КАК Период,
	|	СУММА(ПредыдущийГрафикОплатыУслугПоАренде.УслугаПоАренде) КАК УслугаПоАренде,
	|	СУММА(ПредыдущийГрафикОплатыУслугПоАренде.ОбеспечительныйПлатеж) КАК ОбеспечительныйПлатеж,
	|	СУММА(ПредыдущийГрафикОплатыУслугПоАренде.ВыкупнаяСтоимость) КАК ВыкупнаяСтоимость,
	|	СУММА(ПредыдущийГрафикОплатыУслугПоАренде.ВыкупнаяСтоимостьАванс) КАК ВыкупнаяСтоимостьАванс
	|ПОМЕСТИТЬ ВтПредыдущийГрафикОплатыУслугПоАренде
	|ИЗ
	|	РегистрСведений.ГрафикОплатУслугПоАренде КАК ПредыдущийГрафикОплатыУслугПоАренде
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПредыдущиеУсловияДоговора КАК ВтПредыдущиеУсловияДоговора
	|		ПО ПредыдущийГрафикОплатыУслугПоАренде.АктуальныеУсловияДоговора = ВтПредыдущиеУсловияДоговора.АктуальныеУсловияДоговора
	|ГДЕ
	|	&ТребуетсяОформлениеЗаявленийОВвозе
	|СГРУППИРОВАТЬ ПО
	|	КОНЕЦПЕРИОДА(ПредыдущийГрафикОплатыУслугПоАренде.Дата, МЕСЯЦ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОНЕЦПЕРИОДА(ТекущийГрафикОплатыУслугПоАренде.Дата, МЕСЯЦ) КАК Период,
	|	СУММА(ТекущийГрафикОплатыУслугПоАренде.УслугаПоАренде) КАК УслугаПоАренде,
	|	СУММА(ТекущийГрафикОплатыУслугПоАренде.ОбеспечительныйПлатеж) КАК ОбеспечительныйПлатеж,
	|	СУММА(ТекущийГрафикОплатыУслугПоАренде.ВыкупнаяСтоимость) КАК ВыкупнаяСтоимость
	|ПОМЕСТИТЬ ВтТекущийГрафикОплатыУслугПоАренде
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды.ГрафикОплатУслуг КАК ТекущийГрафикОплатыУслугПоАренде
	|ГДЕ
	|	ТекущийГрафикОплатыУслугПоАренде.Ссылка = &Ссылка
	|	И &ТребуетсяОформлениеЗаявленийОВвозе
	|СГРУППИРОВАТЬ ПО
	|	КОНЕЦПЕРИОДА(ТекущийГрафикОплатыУслугПоАренде.Дата, МЕСЯЦ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ВтТекущийГрафикОплатыУслугПоАренде.Период, ВтПредыдущийГрафикОплатыУслугПоАренде.Период) КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Ссылка КАК Регистратор,
	|	&Организация КАК Организация,
	|	&Партнер КАК Поставщик,
	|	&ДокументЗаключенияДоговораАренды КАК ДокументПоступления,
	|	&ИдентификаторЗаявлениеОВвозеТоваров КАК ТипДокументаИмпорта,
	|	0 КАК Количество,
	|	0 КАК КоличествоПоРНПТ,
	|	-(ВтПредыдущийГрафикОплатыУслугПоАренде.УслугаПоАренде + ВтПредыдущийГрафикОплатыУслугПоАренде.ОбеспечительныйПлатеж +
	|		ВтПредыдущийГрафикОплатыУслугПоАренде.ВыкупнаяСтоимость +
	|		ВтПредыдущийГрафикОплатыУслугПоАренде.ВыкупнаяСтоимостьАванс) КАК Сумма,
	|	ИСТИНА КАК Первичное
	|ИЗ
	|	ВтПредыдущийГрафикОплатыУслугПоАренде КАК ВтПредыдущийГрафикОплатыУслугПоАренде
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтТекущийГрафикОплатыУслугПоАренде КАК ВтТекущийГрафикОплатыУслугПоАренде
	|		ПО ВтПредыдущийГрафикОплатыУслугПоАренде.Период = ВтТекущийГрафикОплатыУслугПоАренде.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтТоварыКОформлениюДокументовИмпортаОстатки КАК ВтТоварыКОформлениюДокументовИмпортаОстатки
	|		ПО ВтПредыдущийГрафикОплатыУслугПоАренде.Период = ВтТоварыКОформлениюДокументовИмпортаОстатки.Период
	|ГДЕ
	|	(ЕСТЬNULL(ВтТоварыКОформлениюДокументовИмпортаОстатки.СуммаКонечныйОстаток, 0) <> 0
	|	ИЛИ ВтТоварыКОформлениюДокументовИмпортаОстатки.СуммаКонечныйОстаток ЕСТЬ NULL)
	|	И ВтПредыдущийГрафикОплатыУслугПоАренде.УслугаПоАренде + ВтПредыдущийГрафикОплатыУслугПоАренде.ОбеспечительныйПлатеж
	|		+ ВтПредыдущийГрафикОплатыУслугПоАренде.ВыкупнаяСтоимость +
	|		ВтПредыдущийГрафикОплатыУслугПоАренде.ВыкупнаяСтоимостьАванс <> ЕСТЬNULL(ВтТекущийГрафикОплатыУслугПоАренде.УслугаПоАренде
	|		+ ВтТекущийГрафикОплатыУслугПоАренде.ОбеспечительныйПлатеж + ВтТекущийГрафикОплатыУслугПоАренде.ВыкупнаяСтоимость,
	|		0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВтТекущийГрафикОплатыУслугПоАренде.Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	&Ссылка,
	|	&Организация,
	|	&Партнер,
	|	&ДокументЗаключенияДоговораАренды,
	|	&ИдентификаторЗаявлениеОВвозеТоваров,
	|	0,
	|	0,
	|	ВтТекущийГрафикОплатыУслугПоАренде.УслугаПоАренде + ВтТекущийГрафикОплатыУслугПоАренде.ОбеспечительныйПлатеж +
	|		ВтТекущийГрафикОплатыУслугПоАренде.ВыкупнаяСтоимость,
	|	ИСТИНА
	|ИЗ
	|	ВтТекущийГрафикОплатыУслугПоАренде КАК ВтТекущийГрафикОплатыУслугПоАренде
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтТоварыКОформлениюДокументовИмпортаОстатки КАК ВтТоварыКОформлениюДокументовИмпортаОстатки
	|		ПО ВтТекущийГрафикОплатыУслугПоАренде.Период = ВтТоварыКОформлениюДокументовИмпортаОстатки.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПредыдущийГрафикОплатыУслугПоАренде КАК ВтПредыдущийГрафикОплатыУслугПоАренде
	|		ПО ВтТекущийГрафикОплатыУслугПоАренде.Период = ВтПредыдущийГрафикОплатыУслугПоАренде.Период
	|ГДЕ
	|	(ЕСТЬNULL(ВтТоварыКОформлениюДокументовИмпортаОстатки.СуммаКонечныйОстаток, 0) <> 0
	|	ИЛИ ВтТоварыКОформлениюДокументовИмпортаОстатки.СуммаКонечныйОстаток ЕСТЬ NULL)
	|	И ВтТекущийГрафикОплатыУслугПоАренде.УслугаПоАренде + ВтТекущийГрафикОплатыУслугПоАренде.ОбеспечительныйПлатеж +
	|		ВтТекущийГрафикОплатыУслугПоАренде.ВыкупнаяСтоимость <> ЕСТЬNULL(ВтПредыдущийГрафикОплатыУслугПоАренде.УслугаПоАренде
	|		+ ВтПредыдущийГрафикОплатыУслугПоАренде.ОбеспечительныйПлатеж +
	|		ВтПредыдущийГрафикОплатыУслугПоАренде.ВыкупнаяСтоимость +
	|		ВтПредыдущийГрафикОплатыУслугПоАренде.ВыкупнаяСтоимостьАванс, 0)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.ДатаДокументаИБ КАК ДатаДокументаИБ,
	|	ДанныеДокумента.НомерДокументаИБ КАК НомерДокументаИБ,
	|	ДанныеДокумента.ТипСсылки КАК ТипСсылки,
	|	ДанныеДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Партнер КАК Партнер,
	|	ДанныеДокумента.Контрагент КАК Контрагент,
	|	ДанныеДокумента.Договор КАК Договор,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ДанныеДокумента.Ответственный КАК Ответственный,
	|	ДанныеДокумента.Комментарий КАК Комментарий,
	|	ДанныеДокумента.Проведен КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления КАК ПометкаУдаления,
	|	ДанныеДокумента.ДополнительнаяЗапись КАК ДополнительнаяЗапись,
	|	ДанныеДокумента.ДатаПервичногоДокумента КАК ДатаПервичногоДокумента,
	|	ДанныеДокумента.НомерПервичногоДокумента КАК НомерПервичногоДокумента,
	|	ДанныеДокумента.Сумма КАК Сумма,
	|	ДанныеДокумента.Валюта КАК Валюта,
	|	ДанныеДокумента.ДатаОтраженияВУчете КАК ДатаОтраженияВУчете
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДанныеДокумента.Ссылка КАК Ссылка,
	|		&Период КАК ДатаДокументаИБ,
	|		&Номер КАК НомерДокументаИБ,
	|		&ИдентификаторМетаданных КАК ТипСсылки,
	|		&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|		&Организация КАК Организация,
	|		ДанныеДокумента.Партнер КАК Партнер,
	|		ДанныеДокумента.Контрагент КАК Контрагент,
	|		ДанныеДокумента.Договор КАК Договор,
	|		&Подразделение КАК Подразделение,
	|		&Ответственный КАК Ответственный,
	|		&Комментарий КАК Комментарий,
	|		&Проведен КАК Проведен,
	|		&ПометкаУдаления КАК ПометкаУдаления,
	|		ЛОЖЬ КАК ДополнительнаяЗапись,
	|		&Период КАК ДатаПервичногоДокумента,
	|		&НомерНаПечать КАК НомерПервичногоДокумента,
	|		&СуммаДокумента КАК Сумма,
	|		&Валюта КАК Валюта,
	|		&Период КАК ДатаОтраженияВУчете
	|	ИЗ
	|		Документ.ИзменениеУсловийДоговораАренды КАК ДанныеДокумента
	|	ГДЕ
	|		ДанныеДокумента.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДанныеДокумента.Ссылка,
	|		&Период,
	|		&Номер,
	|		&ИдентификаторМетаданных,
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗаключениеДоговораАренды),
	|		&Организация,
	|		ДанныеДокумента.НовыйПартнер,
	|		ДанныеДокумента.НовыйКонтрагент,
	|		ДанныеДокумента.НовыйДоговор,
	|		&Подразделение,
	|		&Ответственный,
	|		&Комментарий,
	|		&Проведен,
	|		&ПометкаУдаления,
	|		ИСТИНА,
	|		&Период,
	|		&НомерНаПечать,
	|		&СуммаДокумента,
	|		&Валюта,
	|		&Период
	|	ИЗ
	|		Документ.ИзменениеУсловийДоговораАренды КАК ДанныеДокумента
	|	ГДЕ
	|		ДанныеДокумента.НовыйДоговор <> ЗНАЧЕНИЕ(Справочник.ДоговорыАренды.ПустаяСсылка)
	|		И ДанныеДокумента.Ссылка = &Ссылка) КАК ДанныеДокумента";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДокументыПоОС";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтТаблицаОС(ТекстыЗапроса);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(ТаблицаОС.НомерСтроки-1, 0)    КАК НомерЗаписи,
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК Дата,
	|	&Организация                            КАК Организация,
	|	&Подразделение                          КАК Подразделение,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&Проведен                               КАК Проведен,
	|	ИСТИНА                                  КАК ОтражатьВРеглУчете,
	|	ИСТИНА                                  КАК ОтражатьВУпрУчете,
	|	ТаблицаОС.ОсновноеСредство              КАК ОсновноеСредство
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ втТаблицаОС КАК ТаблицаОС
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
	Возврат ТекстЗапроса;
	
КонецФункции

//++ НЕ УТКА

Функция ТекстЗапросаТаблицаОтражениеДокументовВМеждународномУчете(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОтражениеДокументовВМеждународномУчете";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                        КАК Период,
	|	&Организация                   КАК Организация,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ)   КАК ДатаОтражения,
	|	&ХозяйственнаяОперация         КАК ХозяйственнаяОперация";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

//-- НЕ УТКА

#КонецОбласти

#Область ФормированиеВременныхТаблицыДляПроведения

Процедура ТекстЗапросаТаблицаВтТаблицаОС(ТекстыЗапроса)
	
	ИмяТаблицы = "втТаблицаОС";

	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаОС.Ссылка КАК Ссылка,
	|	ТаблицаОС.НомерСтроки КАК НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаОС.ДействиеСПредметомАренды КАК ДействиеСПредметомАренды,
	|	ТаблицаОС.Субаренда КАК Субаренда,
	|	ТаблицаОС.Подразделение КАК Подразделение,
	|	ТаблицаОС.СтатьяРасходов КАК СтатьяРасходов,
	|	ТаблицаОС.Стоимость КАК Стоимость,
	|	ВЫБОР
	|		КОГДА &ЕстьУчетУУ
	|			ТОГДА ТаблицаОС.Стоимость * ВЫБОР
	|					КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета
	|						ТОГДА 1
	|					ИНАЧЕ &КоэффициентПересчетаВВалютуУпр
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьУУ,
	|	ВЫБОР
	|		КОГДА &ЕстьУчетБУ
	|			ТОГДА ТаблицаОС.Стоимость * ВЫБОР
	|					КОГДА &ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета
	|						ТОГДА 1
	|					ИНАЧЕ &КоэффициентПересчетаВВалютуРегл
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьБУ,
	|	ВЫБОР
	|		КОГДА &ЕстьУчетНУ
	|			ТОГДА ТаблицаОС.Стоимость * ВЫБОР
	|					КОГДА &ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета
	|						ТОГДА 1
	|					ИНАЧЕ &КоэффициентПересчетаВВалютуРегл
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьНУ,
	|	ТаблицаОС.СправедливаяСтоимость КАК СправедливаяСтоимость,
	|	ТаблицаОС.НегарантированнаяСтоимость КАК НегарантированнаяСтоимость,
	|	ТаблицаОС.ВыкупнаяСтоимость КАК ВыкупнаяСтоимость,
	|	ТаблицаОС.СрокИспользованияБУ КАК СрокИспользованияБУ,
	|	ТаблицаОС.СрокИспользованияНУ КАК СрокИспользованияНУ,
	|	ТаблицаОС.СрокИспользованияУУ КАК СрокИспользованияУУ,
	|	ТаблицаОС.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ втТаблицаОС
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды.ОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ИдентификаторСтроки,
	|	ОсновноеСредство";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);

КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса)
	
	ИмяТаблицы = "ВтПрочиеРасходы";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса);
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаВтПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса)
	
	ИмяТаблицы = "ВтИсходныеПрочиеРасходы";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтНедоначисленныеПроценты(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтТаблицаОС(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтКорректировкаСтоимостиОС(Запрос, ТекстыЗапроса);
	
	СписокЗапросовОбъединение = Новый Массив;
	СписокЗапросовОбъединение.Добавить(РегистрыНакопления.ПрочиеРасходы.ТекстОписаниеВтИсходныеПрочиеРасходы());
	
	#Область НедоначисленныеПроценты // Дт <91.02> - Кт <76.07.5>
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	&Организация КАК Организация,
	|	&ПодразделениеПроценты КАК Подразделение,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
	|	
	|	ТаблицаРасходы.СуммаУпр КАК СуммаСНДС,
	|	ТаблицаРасходы.СуммаУпр КАК СуммаБезНДС,
	|	ТаблицаРасходы.СуммаУпр КАК СуммаБезНДСУпр,
	|	ТаблицаРасходы.СуммаРегл КАК СуммаСНДСРегл,
	|	ТаблицаРасходы.СуммаРегл КАК СуммаБезНДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	ТаблицаРасходы.СуммаРегл КАК ВременнаяРазница,
	|	
	|	&ХО_НедоначисленныеПроцентыПоАренде КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|
	|	&ИдентификаторДокумента КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_НедоначисленныеПроцентыПоАренде КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втНедоначисленныеПроценты КАК ТаблицаРасходы
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(&СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиРасходов)
	|	И &СозданДляПерехода";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ФормированиеСтоимостиПредметовАренды // Дт <08.04> - Кт <76.07.1>
	ТекстЗапроса = ИзменениеУсловийДоговораАрендыЛокализация.ТекстЗапросаТаблицаВтИсходныеПрочиеРасходыФормированиеСтоимости();
	
	Если ТекстЗапроса = Неопределено Тогда
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	&Период КАК Период,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	&Организация КАК Организация,
		|	ТаблицаОС.Подразделение КАК Подразделение,
		|	ТаблицаОС.СтатьяРасходов КАК СтатьяРасходов,
		|	ТаблицаОС.ОсновноеСредство КАК АналитикаРасходов,
		|	&НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
		|	ТаблицаОС.СтоимостьУУ КАК СуммаСНДС,
		|	ТаблицаОС.СтоимостьУУ КАК СуммаБезНДС,
		|	ТаблицаОС.СтоимостьУУ КАК СуммаБезНДСУпр,
		|	ТаблицаОС.СтоимостьБУ КАК СуммаСНДСРегл,
		|	ТаблицаОС.СтоимостьБУ КАК СуммаБезНДСРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	&ХО_ФормированиеСтоимостиАрендованныхОС КАК ХозяйственнаяОперация,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
		|	ТаблицаОС.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
		|	&НастройкаХО_ФормированиеСтоимостиАрендованныхОС КАК НастройкаХозяйственнойОперации
		|ИЗ
		|	втТаблицаОС КАК ТаблицаОС
		|ГДЕ
		|	(&ЕстьУчетУУ
		|			ИЛИ &ЕстьУчетБУ)
		|	И ТаблицаОС.Ссылка = &Ссылка
		|	И ТаблицаОС.ДействиеСПредметомАренды = ЗНАЧЕНИЕ(Перечисление.ДействияСПредметамиАренды.ВзятиеВАренду)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&Период КАК Период,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	&Организация КАК Организация,
		|	ТаблицаОС.Подразделение КАК Подразделение,
		|	ТаблицаОС.СтатьяРасходов КАК СтатьяРасходов,
		|	ТаблицаОС.ОсновноеСредство КАК АналитикаРасходов,
		|	&НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
		|	ТаблицаОС.СтоимостьУУ КАК СуммаСНДС,
		|	ТаблицаОС.СтоимостьУУ КАК СуммаБезНДС,
		|	ТаблицаОС.СтоимостьУУ КАК СуммаБезНДСУпр,
		|	ТаблицаОС.СтоимостьБУ КАК СуммаСНДСРегл,
		|	ТаблицаОС.СтоимостьБУ КАК СуммаБезНДСРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	&ХО_ФормированиеСтоимостиАрендованныхОС КАК ХозяйственнаяОперация,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
		|	ТаблицаОС.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
		|	&НастройкаХО_ФормированиеСтоимостиАрендованныхОС КАК НастройкаХозяйственнойОперации
		|ИЗ
		|	втТаблицаОС КАК ТаблицаОС
		|ГДЕ
		|	(&ЕстьУчетУУ
		|			ИЛИ &ЕстьУчетБУ)
		|	И ТаблицаОС.Ссылка = &Ссылка
		|	И ТаблицаОС.Субаренда";
		
	КонецЕсли;
	
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(СписокЗапросовОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтПартииПрочихРасходов(Запрос, ТекстыЗапроса)
	
	ИмяТаблицы = "ВтПартииПрочихРасходов";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(Запрос, ТекстыЗапроса);
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаВтПартииПрочихРасходов();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(Запрос, ТекстыЗапроса)
	
	ИмяТаблицы = "ВтИсходныеПартииПрочихРасходов";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;

	ТекстЗапросаТаблицаВтНедоначисленныеПроценты(ТекстыЗапроса);
	
	СписокЗапросовОбъединение = Новый Массив;
	СписокЗапросовОбъединение.Добавить(РегистрыНакопления.ПартииПрочихРасходов.ТекстОписаниеВтИсходныеПартииПрочихРасходов());

	#Область НедоначисленныеПроценты
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                                         КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)          КАК ВидДвижения,
	|	&Организация                                    КАК Организация,
	|	&ПодразделениеПроценты                          КАК Подразделение,
	|	&СтатьяРасходовПроценты                         КАК СтатьяРасходов,
	|	&АналитикаРасходовПроценты                      КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                                    КАК АналитикаАктивовПассивов,
	|	&Ссылка                                         КАК ДокументПоступленияРасходов,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
	|	&НаправлениеДеятельности                        КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                    КАК АналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО                                    КАК ВидДеятельностиНДС,
	|
	|	ТаблицаРасходы.СуммаУпр                         КАК Стоимость,
	|	ТаблицаРасходы.СуммаУпр                         КАК СтоимостьБезНДС,
	|	0                                               КАК НДСУпр,
	|	ТаблицаРасходы.СуммаРегл                        КАК СтоимостьРегл,
	|	0                                               КАК НДСРегл,
	|	0                                               КАК ПостояннаяРазница,
	|	ТаблицаРасходы.СуммаРегл                        КАК ВременнаяРазница,
	|
	|	&ХО_НедоначисленныеПроцентыПоАренде             КАК ХозяйственнаяОперация,
	|	&ИдентификаторДокумента                         КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_НедоначисленныеПроцентыПоАренде    КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	втНедоначисленныеПроценты КАК ТаблицаРасходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК ПВХСтатьиРасходов
	|		ПО &СтатьяРасходовПроценты = ПВХСтатьиРасходов.Ссылка
	|ГДЕ
	|	ПВХСтатьиРасходов.ВариантРаспределенияРасходовРегл В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров))
	|	И &СозданДляПерехода";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(СписокЗапросовОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);

КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтУвеличениеСтоимости(Запрос, ТекстыЗапроса)

	ИмяТаблицы = "ВтУвеличениеСтоимости";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтКорректировкаСтоимостиОС(Запрос, ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	КорректировкаСтоимости.ОсновноеСредство     КАК ОсновноеСредство,
	|	КорректировкаСтоимости.Субаренда            КАК Субаренда,
	|	КорректировкаСтоимости.ИдентификаторСтроки  КАК ИдентификаторСтроки,
	|
	// Стоимость
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.Стоимость > 0
	|			ТОГДА КорректировкаСтоимости.Стоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Стоимость,
	|
	// СтоимостьРегл
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.СтоимостьРегл > 0
	|			ТОГДА КорректировкаСтоимости.СтоимостьРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьРегл,
	|
	// СтоимостьНУ
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.СтоимостьНУ > 0
	|			ТОГДА КорректировкаСтоимости.СтоимостьНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьНУ,
	|
	// ЗалоговаяСтоимость
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.ЗалоговаяСтоимость > 0
	|			ТОГДА КорректировкаСтоимости.ЗалоговаяСтоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ЗалоговаяСтоимость
	|
	|ПОМЕСТИТЬ ВтУвеличениеСтоимости
	|ИЗ
	|	ВтКорректировкаСтоимостиОС КАК КорректировкаСтоимости
	|ГДЕ
	|	КорректировкаСтоимости.Стоимость > 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьРегл > 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьНУ > 0
	|		ИЛИ КорректировкаСтоимости.ЗалоговаяСтоимость > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтУменьшениеСтоимости(Запрос, ТекстыЗапроса)

	ИмяТаблицы = "ВтУменьшениеСтоимости";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтКорректировкаСтоимостиОС(Запрос, ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	КорректировкаСтоимости.ОсновноеСредство     КАК ОсновноеСредство,
	|	КорректировкаСтоимости.Субаренда            КАК Субаренда,
	|	КорректировкаСтоимости.ИдентификаторСтроки  КАК ИдентификаторСтроки,
	|
	// Стоимость
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.Стоимость < 0
	|			ТОГДА -КорректировкаСтоимости.Стоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК Стоимость,
	|
	// СтоимостьРегл
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.СтоимостьРегл < 0
	|			ТОГДА -КорректировкаСтоимости.СтоимостьРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК СтоимостьРегл,
	|
	// СтоимостьНУ
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.СтоимостьНУ < 0
	|			ТОГДА -КорректировкаСтоимости.СтоимостьНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК СтоимостьНУ,
	|
	// ЗалоговаяСтоимость
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.ЗалоговаяСтоимость < 0
	|			ТОГДА -КорректировкаСтоимости.ЗалоговаяСтоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК ЗалоговаяСтоимость
	|
	|ПОМЕСТИТЬ ВтУменьшениеСтоимости
	|ИЗ
	|	ВтКорректировкаСтоимостиОС КАК КорректировкаСтоимости
	|ГДЕ
	|	КорректировкаСтоимости.Стоимость < 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьРегл < 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьНУ < 0
	|		ИЛИ КорректировкаСтоимости.ЗалоговаяСтоимость < 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтКорректировкаЗалоговойСтоимостиОС(Запрос, ТекстыЗапроса)
	
	ИмяТаблицы = "втКорректировкаЗалоговойСтоимостиОС";

	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ОтложенноеФормированиеДвиженийВНА.ТекстЗапросаПустыеТаблицыОтложенныхДвижений(Запрос, ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаОС.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	0 КАК СтоимостьУУ,
	|	0 КАК СтоимостьБУ,
	|	0 КАК СтоимостьНУ,
	|	ТаблицаОС.Стоимость - ЕСТЬNULL(СтоимостьОС.ЗалоговаяСтоимость, 0) КАК ЗалоговаяСтоимость
	|ПОМЕСТИТЬ втКорректировкаЗалоговойСтоимостиОС
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды.ОС КАК ТаблицаОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьОС
	|		ПО (СтоимостьОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство)
	|			И (СтоимостьОС.Ссылка = &Ссылка)
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И ТаблицаОС.Стоимость <> ЕСТЬNULL(СтоимостьОС.ЗалоговаяСтоимость, 0)
	|	И &Балансодержатель = ЗНАЧЕНИЕ(Перечисление.БалансодержательПредметовАренды.Арендодатель)
	|	И НЕ &ИспользуетсяУчетАрендыПоФСБУ25_2018";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);

КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтКорректировкаСтоимостиОС(Запрос, ТекстыЗапроса)
	
	ИмяТаблицы = "втКорректировкаСтоимостиОС";

	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ИнициализироватьТаблицаВтКорректировкаСтоимостиОСПоДолям(Запрос, ТекстыЗапроса);
	ТекстЗапросаТаблицаВтКорректировкаЗалоговойСтоимостиОС(Запрос, ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	КорректировкаСтоимостиОСПоДолям.ОсновноеСредство КАК ОсновноеСредство,
	|	КорректировкаСтоимостиОСПоДолям.Субаренда КАК Субаренда,
	|	КорректировкаСтоимостиОСПоДолям.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	КорректировкаСтоимостиОСПоДолям.СтоимостьУУ КАК Стоимость,
	|	КорректировкаСтоимостиОСПоДолям.СтоимостьБУ КАК СтоимостьРегл,
	|	КорректировкаСтоимостиОСПоДолям.СтоимостьНУ КАК СтоимостьНУ,
	|	0 КАК ЗалоговаяСтоимость
	|ПОМЕСТИТЬ втКорректировкаСтоимостиОС
	|ИЗ
	|	втКорректировкаСтоимостиОСПоДолям КАК КорректировкаСтоимостиОСПоДолям
	|ГДЕ
	|	&СпособОпределенияСтоимостиАктивов = ЗНАЧЕНИЕ(Перечисление.СпособыОпределенияСтоимостиПредметовАренды.Дисконтирование)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаСтоимостиОСПоДолям.ОсновноеСредство,
	|	ЛОЖЬ КАК Субаренда,
	|	КорректировкаСтоимостиОСПоДолям.ИдентификаторСтроки,
	|	-КорректировкаСтоимостиОСПоДолям.СуммаПроцентовУпр,
	|	-КорректировкаСтоимостиОСПоДолям.СуммаПроцентовРегл,
	|	0,
	|	0
	|ИЗ
	|	втКорректировкаСтоимостиОСПоДолям КАК КорректировкаСтоимостиОСПоДолям
	|ГДЕ
	|	&СозданДляПерехода
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втКорректировкаЗалоговойСтоимостиОС.ОсновноеСредство,
	|	ЛОЖЬ КАК Субаренда,
	|	втКорректировкаЗалоговойСтоимостиОС.ИдентификаторСтроки,
	|	втКорректировкаЗалоговойСтоимостиОС.СтоимостьУУ,
	|	втКорректировкаЗалоговойСтоимостиОС.СтоимостьБУ,
	|	втКорректировкаЗалоговойСтоимостиОС.СтоимостьНУ,
	|	втКорректировкаЗалоговойСтоимостиОС.ЗалоговаяСтоимость
	|ИЗ
	|	втКорректировкаЗалоговойСтоимостиОС КАК втКорректировкаЗалоговойСтоимостиОС";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);

КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтИзлишнеНачисленныеПроценты(ТекстыЗапроса)
	
	ИмяТаблицы = "втИзлишнеНачисленныеПроценты";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтКорректировкаНачисленныхПроцентов(ТекстыЗапроса);

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	-Т.Сумма КАК Сумма,
	|	-Т.СуммаУпр КАК СуммаУпр,
	|	-Т.СуммаРегл КАК СуммаРегл
	|
	|ПОМЕСТИТЬ втИзлишнеНачисленныеПроценты
	|ИЗ
	|	втКорректировкаНачисленныхПроцентов КАК Т
	|ГДЕ
	|	Т.Сумма < 0
	|	И &СозданДляПерехода";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтНедоначисленныеПроценты(ТекстыЗапроса)
	
	ИмяТаблицы = "втНедоначисленныеПроценты";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтКорректировкаНачисленныхПроцентов(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Т.Сумма КАК Сумма,
	|	Т.СуммаУпр КАК СуммаУпр,
	|	Т.СуммаРегл КАК СуммаРегл
	|
	|ПОМЕСТИТЬ втНедоначисленныеПроценты
	|ИЗ
	|	втКорректировкаНачисленныхПроцентов КАК Т
	|ГДЕ
	|	Т.Сумма > 0
	|	И &СозданДляПерехода";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтКорректировкаНачисленныхПроцентов(ТекстыЗапроса)
	
	ИмяТаблицы = "втКорректировкаНачисленныхПроцентов";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтПредыдущиеУсловияДоговора(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СУММА(Графики.Сумма) КАК Сумма,
	|
	|	ВЫБОР
	|		КОГДА НЕ &ЕстьУчетУУ
	|			ТОГДА 0
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета
	|			ТОГДА СУММА(Графики.Сумма)
	|		ИНАЧЕ СУММА(ВЫРАЗИТЬ(Графики.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)))
	|	КОНЕЦ КАК СуммаУпр,
	|
	|	ВЫБОР
	|		КОГДА НЕ &ЕстьУчетБУ
	|			ТОГДА 0
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета
	|			ТОГДА СУММА(Графики.Сумма)
	|		ИНАЧЕ СУММА(ВЫРАЗИТЬ(Графики.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2)))
	|	КОНЕЦ КАК СуммаРегл
	|
	|ПОМЕСТИТЬ втКорректировкаНачисленныхПроцентов
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТекущийГрафик.Проценты КАК Сумма
	|	ИЗ
	|		Документ.ИзменениеУсловийДоговораАренды.ГрафикНачисленияПроцентов КАК ТекущийГрафик
	|	ГДЕ
	|		ТекущийГрафик.Ссылка = &Ссылка
	|		И (ТекущийГрафик.Дата < &НачалоМесяца
	|				ИЛИ &СозданДляПерехода И ТекущийГрафик.Дата <= КОНЕЦПЕРИОДА(&Период, ДЕНЬ))
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		-ПредыдущийГрафик.Проценты
	|	ИЗ
	|		РегистрСведений.ГрафикНачисленияПроцентовПоАренде КАК ПредыдущийГрафик
	|	ГДЕ
	|		ПредыдущийГрафик.Дата < &НачалоМесяца
	|		И ПредыдущийГрафик.Активность
	|		И ПредыдущийГрафик.Договор = &Договор
	|		И ПредыдущийГрафик.АктуальныеУсловияДоговора В (
	|			ВЫБРАТЬ
	|				Т.АктуальныеУсловияДоговора 
	|			ИЗ 
	|				втПредыдущиеУсловияДоговора КАК Т)
	|
	|	) КАК Графики";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтКорректировкаРасчетов(ТекстыЗапроса)
	
	ИмяТаблицы = "втКорректировкаРасчетов";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтПредыдущиеУсловияДоговора(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтСуммаПроцентов(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтОстаткиПоПроцентам(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтОстаткиПоНДС(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	УсловияДоговоровАренды.АктуальныеУсловияДоговора КАК АктуальныеУсловияДоговора,
	|	&СуммаНДС - втОстаткиПоНДС.СуммаНДС КАК СуммаНДС,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(втСуммаПроцентов.Сумма, 0) - втОстаткиПоПроцентам.СуммаПроцентов > 0
	|					И ЕСТЬNULL(втСуммаПроцентов.Сумма, 0) - втОстаткиПоПроцентам.СуммаПроцентов >= &ПорогОкругления
	|				ИЛИ ЕСТЬNULL(втСуммаПроцентов.Сумма, 0) - втОстаткиПоПроцентам.СуммаПроцентов < 0
	|					И ЕСТЬNULL(втСуммаПроцентов.Сумма, 0) - втОстаткиПоПроцентам.СуммаПроцентов <= -&ПорогОкругления
	|			ТОГДА ЕСТЬNULL(втСуммаПроцентов.Сумма, 0) - втОстаткиПоПроцентам.СуммаПроцентов
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаПроцентов
	|ПОМЕСТИТЬ втКорректировкаРасчетовПредварительная
	|ИЗ
	|	втПредыдущиеУсловияДоговора КАК УсловияДоговоровАренды
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиПоПроцентам КАК втОстаткиПоПроцентам
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиПоНДС КАК втОстаткиПоНДС
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСуммаПроцентов КАК втСуммаПроцентов
	|		ПО (ИСТИНА)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КорректировкаРасчетов.АктуальныеУсловияДоговора КАК АктуальныеУсловияДоговора,
	|	КорректировкаРасчетов.СуммаНДС КАК СуммаНДС,
	|	КорректировкаРасчетов.СуммаПроцентов КАК СуммаПроцентов,
	|	ВЫБОР
	|		КОГДА НЕ &ЕстьУчетУУ
	|			ТОГДА 0
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета
	|			ТОГДА КорректировкаРасчетов.СуммаНДС
	|		ИНАЧЕ ВЫРАЗИТЬ(КорректировкаРасчетов.СуммаНДС * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31, 2))
	|	КОНЕЦ КАК СуммаНДСУпр,
	|	ВЫБОР
	|		КОГДА НЕ &ЕстьУчетБУ
	|			ТОГДА 0
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета
	|			ТОГДА КорректировкаРасчетов.СуммаНДС
	|		ИНАЧЕ ВЫРАЗИТЬ(КорректировкаРасчетов.СуммаНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31, 2))
	|	КОНЕЦ КАК СуммаНДСРегл,
	|	ВЫБОР
	|		КОГДА НЕ &ЕстьУчетУУ
	|			ТОГДА 0
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета
	|			ТОГДА КорректировкаРасчетов.СуммаПроцентов
	|		ИНАЧЕ ВЫРАЗИТЬ(КорректировкаРасчетов.СуммаПроцентов * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31, 2))
	|	КОНЕЦ КАК СуммаПроцентовУпр,
	|	ВЫБОР
	|		КОГДА НЕ &ЕстьУчетБУ
	|			ТОГДА 0
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета
	|			ТОГДА КорректировкаРасчетов.СуммаПроцентов
	|		ИНАЧЕ ВЫРАЗИТЬ(КорректировкаРасчетов.СуммаПроцентов * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31, 2))
	|	КОНЕЦ КАК СуммаПроцентовРегл
	|ПОМЕСТИТЬ втКорректировкаРасчетов
	|ИЗ
	|	втКорректировкаРасчетовПредварительная КАК КорректировкаРасчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втКорректировкаРасчетовПредварительная";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтПредыдущиеУсловияДоговора(ТекстыЗапроса)
	
	ИмяТаблицы = "втПредыдущиеУсловияДоговора";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	УсловияДоговоровАренды.АктуальныеУсловияДоговора,
	|	УсловияДоговоровАренды.СуммаНДС КАК СуммаНДС,
	|	УсловияДоговоровАренды.СуммаПроцентов КАК СуммаПроцентов
	|ПОМЕСТИТЬ втПредыдущиеУсловияДоговора
	|ИЗ
	|	РегистрСведений.УсловияДоговоровАренды.СрезПоследних(&Период, Договор = &СтарыйДоговор И Регистратор <> &Ссылка) КАК УсловияДоговоровАренды";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтСуммаПроцентов(ТекстыЗапроса)

	ИмяТаблицы = "втСуммаПроцентов";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&СуммаПроцентов КАК Сумма
	|ПОМЕСТИТЬ втСуммаПроцентов
	|ГДЕ
	|	НЕ &СозданДляПерехода
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СУММА(ТекущийГрафик.Проценты)
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды.ГрафикНачисленияПроцентов КАК ТекущийГрафик
	|ГДЕ
	|	ТекущийГрафик.Ссылка = &Ссылка
	|	И &СозданДляПерехода";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);

КонецПроцедуры

Процедура ИнициализироватьТаблицаВтКорректировкаСтоимостиОСПоДолям(Запрос, ТекстыЗапроса)

	ИмяТаблицы = "втКорректировкаСтоимостиОСПоДолям";

	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;

	// Запрет повторной инициализации.
	Если Запрос.Параметры.Свойство("втКорректировкаСтоимостиОСПоДолямИнициализирована") Тогда
		Возврат;
	КонецЕсли;

	ТекстыЗапросаИнициализации = Новый СписокЗначений;

	ТекстЗапросаТаблицаВтОстаткиПоАренднымОбязательствам(ТекстыЗапросаИнициализации);
	ТекстЗапросаТаблицаВтКорректировкаРасчетов(ТекстыЗапросаИнициализации);
	ТекстЗапросаТаблицаВтТаблицаОС(ТекстыЗапросаИнициализации);

	ЗапросИнициализации = Новый Запрос;

	Для Каждого ПараметрЗапроса Из Запрос.Параметры Цикл
		ЗапросИнициализации.Параметры.Вставить(ПараметрЗапроса.Ключ,ПараметрЗапроса.Значение);
	КонецЦикла;

	// Получим доли каждого ОС в общей справедливой стоимости.
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	втКорректировкаРасчетов.СуммаПроцентов КАК СуммаПроцентов,
	|	втКорректировкаРасчетов.СуммаПроцентовУпр КАК СуммаПроцентовУпр,
	|	втКорректировкаРасчетов.СуммаПроцентовРегл КАК СуммаПроцентовРегл
	|ИЗ
	|	втКорректировкаРасчетов КАК втКорректировкаРасчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИзменениеУсловийДоговораАренды.ПриведеннаяСтоимость
	|		+ ЕСТЬNULL(втОстаткиПоАренднымОбязательствам.ОбеспечительныйПлатеж, 0) КАК ПриведеннаяСтоимость
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды КАК ИзменениеУсловийДоговораАренды
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиПоАренднымОбязательствам КАК втОстаткиПоАренднымОбязательствам
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ИзменениеУсловийДоговораАренды.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОстаткиПоАренднымОбязательствам.СтоимостьПредметовАренды КАК ОбщаяСтоимостьОстаток
	|ИЗ
	|	втОстаткиПоАренднымОбязательствам КАК втОстаткиПоАренднымОбязательствам
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ОС.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ОС.ДействиеСПредметомАренды КАК ДействиеСПредметомАренды,
	|	ОС.Субаренда КАК Субаренда,
	|	0 КАК Стоимость,
	|	0 КАК СуммаПроцентов,
	|	0 КАК СуммаПроцентовРегл,
	|	0 КАК СуммаПроцентовУпр,
	|	ВЫБОР
	|		КОГДА ОС.ДействиеСПредметомАренды <> ЗНАЧЕНИЕ(Перечисление.ДействияСПредметамиАренды.ВзятиеВАренду)
	|			ТОГДА ВЫБОР
	|					КОГДА &ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговоровАренды.Лизинг)
	|						ТОГДА ОС.Стоимость
	|					КОГДА &ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговоровАренды.Аренда)
	|						ТОГДА ОС.СправедливаяСтоимость
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоэффициентРаспределенияОстатков,
	|	ВЫБОР
	|		КОГДА ОС.ДействиеСПредметомАренды <> ЗНАЧЕНИЕ(Перечисление.ДействияСПредметамиАренды.ПрекращениеАренды)
	|				И ОС.ДействиеСПредметомАренды <> ЗНАЧЕНИЕ(Перечисление.ДействияСПредметамиАренды.ДосрочныйВыкуп)
	|			ТОГДА ВЫБОР
	|					КОГДА &ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговоровАренды.Лизинг)
	|						ТОГДА ОС.Стоимость
	|					КОГДА &ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговоровАренды.Аренда)
	|						ТОГДА ОС.СправедливаяСтоимость
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоэффициентРаспределенияСтоимости
	|ИЗ
	|	втТаблицаОС КАК ОС
	|ГДЕ
	|	ОС.Ссылка = &Ссылка";

	ТекстыЗапросаИнициализации.Добавить(ТекстЗапроса, "ТекстЗаросаИнициализации");

	ЗапросИнициализации.Текст = СтрСоединить(ТекстыЗапросаИнициализации.ВыгрузитьЗначения(), 
	ОбщегоНазначения.РазделительПакетаЗапросов());

	Результаты = ЗапросИнициализации.ВыполнитьПакет(); 

	ТаблицаОС = Результаты[Результаты.ВГраница()].Выгрузить();
	
	Выборка = Результаты[Результаты.ВГраница()-1].Выбрать();
	Выборка.Следующий();
	ОбщаяСтоимостьОстаток = Выборка.ОбщаяСтоимостьОстаток;
	
	Выборка = Результаты[Результаты.ВГраница()-2].Выбрать();
	Выборка.Следующий();
	ПриведеннаяСтоимость = Выборка.ПриведеннаяСтоимость;
	
	Выборка = Результаты[Результаты.ВГраница()-3].Выбрать();
	Выборка.Следующий();
	СуммаПроцентов = Выборка.СуммаПроцентов;
	СуммаПроцентовУпр = Выборка.СуммаПроцентовУпр;
	СуммаПроцентовРегл = Выборка.СуммаПроцентовРегл;

	Коэффициенты = ТаблицаОС.ВыгрузитьКолонку("КоэффициентРаспределенияОстатков");

	РаспределениеОстатковСтоимости = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(
		ОбщаяСтоимостьОстаток, Коэффициенты);
	РаспределениеПроцентов = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(
		СуммаПроцентов, Коэффициенты);
	РаспределениеПроцентовУпр = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(
		СуммаПроцентовУпр, Коэффициенты);
	РаспределениеПроцентовРегл = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(
		СуммаПроцентовРегл, Коэффициенты);

	Коэффициенты = ТаблицаОС.ВыгрузитьКолонку("КоэффициентРаспределенияСтоимости");

	РаспределениеСтоимости = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(
		ПриведеннаяСтоимость, Коэффициенты);

	Для ИндексСтроки = 0 По ТаблицаОС.Количество()-1 Цикл
		
		ДанныеСтроки = ТаблицаОС.Получить(ИндексСтроки);
		
		Если РаспределениеСтоимости <> Неопределено ИЛИ РаспределениеОстатковСтоимости <> Неопределено Тогда
			ОстатокСтоимостиОС = ?(РаспределениеОстатковСтоимости <> Неопределено, РаспределениеОстатковСтоимости[ИндексСтроки], 0);
			СтоимостьОС = ?(РаспределениеСтоимости <> Неопределено, РаспределениеСтоимости[ИндексСтроки], 0);
			ДанныеСтроки.Стоимость = СтоимостьОС - ОстатокСтоимостиОС;
		КонецЕсли;
		
		Если РаспределениеПроцентов <> Неопределено Тогда
			ДанныеСтроки.СуммаПроцентов = РаспределениеПроцентов[ИндексСтроки];
		КонецЕсли;
		Если РаспределениеПроцентовУпр <> Неопределено Тогда
			ДанныеСтроки.СуммаПроцентовУпр = РаспределениеПроцентовУпр[ИндексСтроки];
		КонецЕсли;
		Если РаспределениеПроцентовРегл <> Неопределено Тогда
			ДанныеСтроки.СуммаПроцентовРегл = РаспределениеПроцентовРегл[ИндексСтроки];
		КонецЕсли;
		
	КонецЦикла;

	Запрос.УстановитьПараметр("ТаблицаОС", ТаблицаОС);

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	втИзменениеСтоимостиОС.ОсновноеСредство КАК ОсновноеСредство,
	|	втИзменениеСтоимостиОС.Субаренда КАК Субаренда,
	|	втИзменениеСтоимостиОС.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ВЫБОР
	|		КОГДА втИзменениеСтоимостиОС.Стоимость > 0
	|					И втИзменениеСтоимостиОС.Стоимость >= &ПорогОкругления
	|				ИЛИ втИзменениеСтоимостиОС.Стоимость < 0
	|					И втИзменениеСтоимостиОС.Стоимость <= -&ПорогОкругления
	|			ТОГДА втИзменениеСтоимостиОС.Стоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Стоимость,
	|	втИзменениеСтоимостиОС.СуммаПроцентов КАК СуммаПроцентов,
	|	втИзменениеСтоимостиОС.СуммаПроцентовУпр КАК СуммаПроцентовУпр,
	|	втИзменениеСтоимостиОС.СуммаПроцентовРегл КАК СуммаПроцентовРегл
	|ПОМЕСТИТЬ втКорректировкаСтоимостиОСПоДолямПредварительная
	|ИЗ
	|	&ТаблицаОС КАК втИзменениеСтоимостиОС
	|ГДЕ
	|	втИзменениеСтоимостиОС.ДействиеСПредметомАренды <> ЗНАЧЕНИЕ(Перечисление.ДействияСПредметамиАренды.ВзятиеВАренду)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втИзменениеСтоимостиОС.ОсновноеСредство КАК ОсновноеСредство,
	|	втИзменениеСтоимостиОС.Субаренда КАК Субаренда,
	|	втИзменениеСтоимостиОС.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	втИзменениеСтоимостиОС.Стоимость КАК Стоимость,
	|	ВЫБОР
	|		КОГДА &ЕстьУчетУУ
	|			ТОГДА втИзменениеСтоимостиОС.Стоимость * ВЫБОР
	|					КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета
	|						ТОГДА 1
	|					ИНАЧЕ &КоэффициентПересчетаВВалютуУпр
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьУУ,
	|	ВЫБОР
	|		КОГДА &ЕстьУчетБУ
	|			ТОГДА втИзменениеСтоимостиОС.Стоимость * ВЫБОР
	|					КОГДА &ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета
	|						ТОГДА 1
	|					ИНАЧЕ &КоэффициентПересчетаВВалютуРегл
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьБУ,
	|	ВЫБОР
	|		КОГДА &ЕстьУчетНУ
	|			ТОГДА втИзменениеСтоимостиОС.Стоимость * ВЫБОР
	|					КОГДА &ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета
	|						ТОГДА 1
	|					ИНАЧЕ &КоэффициентПересчетаВВалютуРегл
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьНУ,
	|	втИзменениеСтоимостиОС.СуммаПроцентов КАК СуммаПроцентов,
	|	втИзменениеСтоимостиОС.СуммаПроцентовУпр КАК СуммаПроцентовУпр,
	|	втИзменениеСтоимостиОС.СуммаПроцентовРегл КАК СуммаПроцентовРегл
	|ПОМЕСТИТЬ втКорректировкаСтоимостиОСПоДолям
	|ИЗ
	|	втКорректировкаСтоимостиОСПоДолямПредварительная КАК втИзменениеСтоимостиОС
	|ГДЕ
	|	втИзменениеСтоимостиОС.Стоимость <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ИдентификаторСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втКорректировкаСтоимостиОСПоДолямПредварительная";
	
	Запрос.УстановитьПараметр("втКорректировкаСтоимостиОСПоДолямИнициализирована", Истина);

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);

КонецПроцедуры

Процедура ИнициализироватьТаблицаВтОстаткиРасчетыПоФинансовымИнструментам(Запрос, ТекстыЗапроса)

	ИмяТаблицы = "ВтОстаткиРасчетыПоФинансовымИнструментам";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	// Запрет повторной инициализации.
	Если Запрос.Параметры.Свойство("втОстаткиРасчетыПоФинансовымИнструментамИнициализирована") Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	
	ЗапросПоОстаткам = Новый Запрос;
	ЗапросПоОстаткам.МенеджерВременныхТаблиц = МенеджерВТ;
	
	Для Каждого ПараметрЗапроса Из Запрос.Параметры Цикл
		ЗапросПоОстаткам.Параметры.Вставить(ПараметрЗапроса.Ключ,ПараметрЗапроса.Значение);
	КонецЦикла;
	
	ЗапросПоОстаткам.Текст =
	"ВЫБРАТЬ
	|	РасчетыПоФинансовымИнструментамОстатки.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	РасчетыПоФинансовымИнструментамОстатки.Договор КАК Договор,
	|	РасчетыПоФинансовымИнструментамОстатки.ТипСуммы КАК ТипСуммы,
	|	РасчетыПоФинансовымИнструментамОстатки.РасчетныйДокумент КАК РасчетныйДокумент,
	|	РасчетыПоФинансовымИнструментамОстатки.Валюта КАК Валюта,
	|	РасчетыПоФинансовымИнструментамОстатки.СуммаОстаток КАК СуммаОстаток,
	|	РасчетыПоФинансовымИнструментамОстатки.СуммаУпрОстаток КАК СуммаУпрОстаток,
	|	РасчетыПоФинансовымИнструментамОстатки.СуммаРеглОстаток КАК СуммаРеглОстаток
	|ИЗ
	|	РегистрНакопления.РасчетыПоФинансовымИнструментам.Остатки(&ГраницаОстатков, Договор = &СтарыйДоговор) КАК РасчетыПоФинансовымИнструментамОстатки
	|ГДЕ
	|	&ИзменениеДоговора";
	
	ТаблицаОстатковПоФинИнструментам = ЗапросПоОстаткам.Выполнить().Выгрузить();
	
	ОбщегоНазначенияУТ.ДобавитьИдентификаторыВТаблицуЗначений(ТаблицаОстатковПоФинИнструментам);
	
	Запрос.УстановитьПараметр("ТаблицаОстатковПоФинИнструментам", ТаблицаОстатковПоФинИнструментам);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РасчетыПоФинансовымИнструментамОстатки.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	РасчетыПоФинансовымИнструментамОстатки.Договор КАК Договор,
	|	РасчетыПоФинансовымИнструментамОстатки.ТипСуммы КАК ТипСуммы,
	|	РасчетыПоФинансовымИнструментамОстатки.РасчетныйДокумент КАК РасчетныйДокумент,
	|	РасчетыПоФинансовымИнструментамОстатки.Валюта КАК Валюта,
	|	РасчетыПоФинансовымИнструментамОстатки.СуммаОстаток КАК СуммаОстаток,
	|	РасчетыПоФинансовымИнструментамОстатки.СуммаУпрОстаток КАК СуммаУпрОстаток,
	|	РасчетыПоФинансовымИнструментамОстатки.СуммаРеглОстаток КАК СуммаРеглОстаток,
	|	РасчетыПоФинансовымИнструментамОстатки.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи
	|ПОМЕСТИТЬ ВтОстаткиРасчетыПоФинансовымИнструментам
	|ИЗ
	|	&ТаблицаОстатковПоФинИнструментам КАК РасчетыПоФинансовымИнструментамОстатки
	|ГДЕ
	|	&ИзменениеДоговора";
	
	Запрос.УстановитьПараметр("втОстаткиРасчетыПоФинансовымИнструментамИнициализирована", Истина);
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтБалансоваяСтоимостьОС(ТекстыЗапроса)
	ИмяТаблицы = "ВтБалансоваяСтоимостьОС";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втСтоимость.Ссылка                                                  КАК Ссылка,
	|	втСтоимость.ОбъектУчета                                             КАК ОсновноеСредство,
	|
	|	втСтоимость.Стоимость 
	|		- ЕСТЬNULL(втАмортизация.Амортизация, 0)
	|		- ЕСТЬNULL(втОбесценение.ОбесценениеУпр, 0)                     КАК БалансоваяСтоимостьУпр,
	|
	|	втСтоимость.СтоимостьРегл + втСтоимость.СтоимостьЦФ 
	|		- ЕСТЬNULL(втАмортизация.АмортизацияРегл, 0) - ЕСТЬNULL(втАмортизация.АмортизацияЦФ, 0) 
	|		- ЕСТЬNULL(втОбесценение.ОбесценениеРегл, 0)                    КАК БалансоваяСтоимостьРегл
	|
	|ПОМЕСТИТЬ ВтБалансоваяСтоимостьОС
	|
	|ИЗ
	|	ВТ_СтоимостьВНА КАК втСтоимость
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК втАмортизация
	|		ПО втСтоимость.ОбъектУчета = втАмортизация.ОбъектУчета
	|			И втАмортизация.Ссылка = &Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОбесценениеВНА КАК втОбесценение
	|		ПО втСтоимость.ОбъектУчета = втОбесценение.ОбъектУчета
	|ГДЕ
	|	втСтоимость.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтОстаткиПоПроцентам(ТекстыЗапроса) Экспорт
	
	ИмяТаблицы = "втОстаткиПоПроцентам";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтПредыдущиеУсловияДоговора(ТекстыЗапроса);
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	СУММА(ТаблицаГрафика.Проценты) КАК СуммаПроцентовПоПредыдущимУсловиям
	|ПОМЕСТИТЬ втТекущийГрафикПроцентов
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды.ГрафикНачисленияПроцентов КАК ТаблицаГрафика
	|ГДЕ
	|	ТаблицаГрафика.Ссылка = &Ссылка
	|	И ТаблицаГрафика.Дата >= ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&Период, ДЕНЬ), ДЕНЬ, -1)
	|	И ТаблицаГрафика.Дата < НАЧАЛОПЕРИОДА(&Период, ДЕНЬ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ЕСТЬNULL(ГрафикНачисленияПроцентовПоАренде.Проценты, 0)) КАК СуммаПроцентов
	|ПОМЕСТИТЬ втОстаткиПоПроцентамПредварительная
	|ИЗ
	|	втПредыдущиеУсловияДоговора КАК втПредыдущиеУсловияДоговора
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикНачисленияПроцентовПоАренде КАК ГрафикНачисленияПроцентовПоАренде
	|		ПО втПредыдущиеУсловияДоговора.АктуальныеУсловияДоговора = ГрафикНачисленияПроцентовПоАренде.АктуальныеУсловияДоговора
	|			И (ГрафикНачисленияПроцентовПоАренде.Дата >= ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&Период, ДЕНЬ), ДЕНЬ, -1))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОстаткиПоПроцентамПредварительная.СуммаПроцентов
	|		- ЕСТЬNULL(втТекущийГрафикПроцентов.СуммаПроцентовПоПредыдущимУсловиям, 0) КАК СуммаПроцентов
	|ПОМЕСТИТЬ втОстаткиПоПроцентам
	|ИЗ
	|	втОстаткиПоПроцентамПредварительная КАК втОстаткиПоПроцентамПредварительная
	|		ЛЕВОЕ СОЕДИНЕНИЕ втТекущийГрафикПроцентов КАК втТекущийГрафикПроцентов
	|		ПО (ИСТИНА)";

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);

КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтОстаткиПоНДС(ТекстыЗапроса) Экспорт
	
	ИмяТаблицы = "втОстаткиПоНДС";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтПредыдущиеУсловияДоговора(ТекстыЗапроса);
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	СУММА(ГрафикНачисленияУслугПоАренде.УслугаПоАрендеНДС) + СУММА(ГрафикНачисленияУслугПоАренде.ОбеспечительныйПлатежНДС) КАК Сумма,
	|	СУММА(ГрафикНачисленияУслугПоАренде.ОбеспечительныйПлатежНДС) КАК ОбеспечительныйПлатеж
	|ПОМЕСТИТЬ втНДСНачислений
	|ИЗ
	|	РегистрСведений.ГрафикНачисленияУслугПоАренде КАК ГрафикНачисленияУслугПоАренде
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПредыдущиеУсловияДоговора КАК втПредыдущиеУсловияДоговора
	|		ПО ГрафикНачисленияУслугПоАренде.АктуальныеУсловияДоговора = втПредыдущиеУсловияДоговора.АктуальныеУсловияДоговора
	|			И (ГрафикНачисленияУслугПоАренде.Дата >= НАЧАЛОПЕРИОДА(&Период, ДЕНЬ))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ГрафикОплатУслугПоАренде.ВыкупнаяСтоимостьНДС) КАК Сумма
	|ПОМЕСТИТЬ втНДСВыкупнойСтоимости
	|ИЗ
	|	РегистрСведений.ГрафикОплатУслугПоАренде КАК ГрафикОплатУслугПоАренде
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПредыдущиеУсловияДоговора КАК втПредыдущиеУсловияДоговора
	|		ПО ГрафикОплатУслугПоАренде.АктуальныеУсловияДоговора = втПредыдущиеУсловияДоговора.АктуальныеУсловияДоговора
	|			И (ГрафикОплатУслугПоАренде.Дата >= НАЧАЛОПЕРИОДА(&Период, ДЕНЬ))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(втНДСНачислений.Сумма, 0) + ЕСТЬNULL(втНДСВыкупнойСтоимости.Сумма, 0) КАК СуммаНДС,
	|	ЕСТЬNULL(втНДСНачислений.ОбеспечительныйПлатеж, 0) КАК ОбеспечительныйПлатеж
	|ПОМЕСТИТЬ втОстаткиПоНДС
	|ИЗ
	|	втПредыдущиеУсловияДоговора КАК втПредыдущиеУсловияДоговора
	|		ЛЕВОЕ СОЕДИНЕНИЕ втНДСНачислений КАК втНДСНачислений
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втНДСВыкупнойСтоимости КАК втНДСВыкупнойСтоимости
	|		ПО (ИСТИНА)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);

КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтОстаткиПоАренднымОбязательствам(ТекстыЗапроса) Экспорт
	
	ИмяТаблицы = "втОстаткиПоАренднымОбязательствам";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтПредыдущиеУсловияДоговора(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтОстаткиПоПроцентам(ТекстыЗапроса);
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	СУММА(ГрафикОплатУслугПоАренде.УслугаПоАренде
	|		- ГрафикОплатУслугПоАренде.УслугаПоАрендеНДС
	|		+ ГрафикОплатУслугПоАренде.ОбеспечительныйПлатеж
	|		- ГрафикОплатУслугПоАренде.ОбеспечительныйПлатежНДС
	|		+ ГрафикОплатУслугПоАренде.ВыкупнаяСтоимость
	|		+ ГрафикОплатУслугПоАренде.ВыкупнаяСтоимостьАванс
	|		- ГрафикОплатУслугПоАренде.ВыкупнаяСтоимостьНДС) КАК Сумма
	|ПОМЕСТИТЬ втОплаты
	|ИЗ
	|	РегистрСведений.ГрафикОплатУслугПоАренде КАК ГрафикОплатУслугПоАренде
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПредыдущиеУсловияДоговора КАК втПредыдущиеУсловияДоговора
	|		ПО ГрафикОплатУслугПоАренде.АктуальныеУсловияДоговора = втПредыдущиеУсловияДоговора.АктуальныеУсловияДоговора
	|			И (ГрафикОплатУслугПоАренде.Дата >= НАЧАЛОПЕРИОДА(&Период, ДЕНЬ))
	|;
	|
	|ВЫБРАТЬ
	|	СУММА(ГрафикНачисленияУслугПоАренде.ОбеспечительныйПлатеж
	|	- ГрафикНачисленияУслугПоАренде.ОбеспечительныйПлатежНДС) КАК ОбеспечительныйПлатеж
	|ПОМЕСТИТЬ втНачисления
	|ИЗ
	|	РегистрСведений.ГрафикНачисленияУслугПоАренде КАК ГрафикНачисленияУслугПоАренде
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПредыдущиеУсловияДоговора КАК втПредыдущиеУсловияДоговора
	|		ПО ГрафикНачисленияУслугПоАренде.АктуальныеУсловияДоговора = втПредыдущиеУсловияДоговора.АктуальныеУсловияДоговора
	|			И (ГрафикНачисленияУслугПоАренде.Дата >= НАЧАЛОПЕРИОДА(&Период, ДЕНЬ))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(втОплаты.Сумма, 0)
	|	+ ЕСТЬNULL(втНачисления.ОбеспечительныйПлатеж, 0)
	|	- ЕСТЬNULL(втОстаткиПоПроцентам.СуммаПроцентов, 0) КАК СтоимостьПредметовАренды,
	|	ЕСТЬNULL(втНачисления.ОбеспечительныйПлатеж, 0) КАК ОбеспечительныйПлатеж
	|ПОМЕСТИТЬ втОстаткиПоАренднымОбязательствам
	|ИЗ
	|	втПредыдущиеУсловияДоговора КАК втПредыдущиеУсловияДоговора
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОплаты КАК втОплаты
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втНачисления КАК втНачисления
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиПоПроцентам КАК втОстаткиПоПроцентам
	|		ПО (ИСТИНА)";

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтДоходыПриЧастичномВыкупе(Запрос, ТекстыЗапроса)

	ИмяТаблицы = "ВтДоходыПриЧастичномВыкупе";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ИнициализироватьТаблицаВтКорректировкаСтоимостиОСПоДолям(Запрос, ТекстыЗапроса);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ОС.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	-ЕСТЬNULL(СтоимостьОС.Стоимость,0) - ОС.ВыкупнаяСтоимость КАК Сумма,
	|	ВЫБОР
	|		КОГДА &ЕстьУчетУУ ТОГДА
	|			-ЕСТЬNULL(СтоимостьОС.СтоимостьУУ,0) - ОС.ВыкупнаяСтоимость * 
	|				ВЫБОР КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета
	|					ТОГДА 1
	|				ИНАЧЕ &КоэффициентПересчетаВВалютуУпр
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаУпр,
	|	ВЫБОР
	|		КОГДА &ЕстьУчетБУ ТОГДА
	|			-ЕСТЬNULL(СтоимостьОС.СтоимостьБУ,0) - ОС.ВыкупнаяСтоимость *
	|				ВЫБОР КОГДА &ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета
	|					ТОГДА 1
	|				ИНАЧЕ &КоэффициентПересчетаВВалютуРегл
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаРегл,
	|	&ХО_ОтражениеАрендныхОбязательствВДоходах КАК ХозяйственнаяОперация,
	|	&НастройкаХО_ОтражениеАрендныхОбязательствВДоходах КАК НастройкаХозяйственнойОперации
	|ПОМЕСТИТЬ ВтДоходыПриЧастичномВыкупе
	|ИЗ
	|	втТаблицаОС КАК ОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКорректировкаСтоимостиОСПоДолям КАК СтоимостьОС
	|		ПО ОС.ИдентификаторСтроки = СтоимостьОС.ИдентификаторСтроки
	|ГДЕ
	|	ОС.ДействиеСПредметомАренды = ЗНАЧЕНИЕ(Перечисление.ДействияСПредметамиАренды.ДосрочныйВыкуп)
	|	И -ЕСТЬNULL(СтоимостьОС.Стоимость,0) - ОС.ВыкупнаяСтоимость > 0";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ПроведениеПоРеглУчету

Функция ТекстОтраженияВРеглУчете() Экспорт

	Возврат ИзменениеУсловийДоговораАрендыЛокализация.ТекстОтраженияВРеглУчете();

КонецФункции

Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт

	Возврат "";

КонецФункции

#КонецОбласти

#Область Заполнение

// Заполняет документ на основании договора аренды.
// 
// Параметры:
// 	Объект - ДокументОбъект.ИзменениеУсловийДоговораАренды - 
Процедура ЗаполнитьНаОснованииДоговораАренды(Объект, Обновить = Ложь) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Объект.Договор) Тогда
		Возврат;
	КонецЕсли;
	
	Если Обновить Тогда
		
		ОС = Объект.ОС.Выгрузить();
		ГрафикОплатУслуг = Объект.ГрафикОплатУслуг.Выгрузить();
		ГрафикНачисленияУслуг = Объект.ГрафикНачисленияУслуг.Выгрузить();
		
	Иначе
		
		ОС = Объект.ОС.Выгрузить().СкопироватьКолонки();
		ГрафикОплатУслуг = Объект.ГрафикОплатУслуг.Выгрузить().СкопироватьКолонки();
		ГрафикНачисленияУслуг = Объект.ГрафикНачисленияУслуг.Выгрузить().СкопироватьКолонки();
		
	КонецЕсли;
	
	Объект.ОС.Очистить();
	Объект.ГрафикОплатУслуг.Очистить();
	Объект.ГрафикНачисленияУслуг.Очистить();
	Объект.ГрафикНачисленияПроцентов.Очистить();
	
	ТекстыПакетаЗапросов = Новый Массив;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	АрендованныеОС.ОсновноеСредство КАК ОсновноеСредство,
	|	АрендованныеОС.Стоимость КАК Стоимость,
	|	АрендованныеОС.СправедливаяСтоимость КАК НоминальнаяСтоимость,
	|	АрендованныеОС.ДействиеСПредметомАренды КАК Действие,
	|	АрендованныеОС.Подразделение КАК Подразделение,
	|	АрендованныеОС.СтатьяРасходов КАК СтатьяРасходов
	|ПОМЕСТИТЬ втАрендованныеОСИзДокумента
	|ИЗ
	|	&ОС КАК АрендованныеОС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|ВЫБРАТЬ
	|	АрендованныеОС.ОсновноеСредство КАК ОсновноеСредство,
	|	АрендованныеОС.Стоимость КАК Стоимость,
	|	ЕСТЬNULL(втАрендованныеОСИзДокумента.НоминальнаяСтоимость, АрендованныеОС.НоминальнаяСтоимость) КАК НоминальнаяСтоимость,
	|	ЕСТЬNULL(втАрендованныеОСИзДокумента.Действие, ЗНАЧЕНИЕ(Перечисление.ДействияСПредметамиАренды.ИзменениеУсловийАренды)) КАК Действие,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов
	|ПОМЕСТИТЬ втАрендованныеОС_Предварительная
	|ИЗ
	|	РегистрСведений.АрендованныеОС.СрезПоследних(&ПериодГраница, Регистратор <> &Ссылка И Договор = &Договор) КАК АрендованныеОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ втАрендованныеОСИзДокумента КАК втАрендованныеОСИзДокумента
	|		ПО АрендованныеОС.ОсновноеСредство = втАрендованныеОСИзДокумента.ОсновноеСредство
	|ГДЕ
	|	АрендованныеОС.Состояние В (
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ЗаключенДоговорАренды),
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ВАренде))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	АрендованныеОС.ОсновноеСредство,
	|	АрендованныеОС.Стоимость,
	|	АрендованныеОС.НоминальнаяСтоимость,
	|	АрендованныеОС.Действие,
	|	АрендованныеОС.Подразделение,
	|	АрендованныеОС.СтатьяРасходов
	|ИЗ
	|	втАрендованныеОСИзДокумента  КАК АрендованныеОС
	|ГДЕ
	|	АрендованныеОС.Действие = ЗНАЧЕНИЕ(Перечисление.ДействияСПредметамиАренды.ВзятиеВАренду)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПереданныеВАренду.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ втПереданныеВАренду
	|ИЗ
	|	РегистрСведений.ПереданныеВАрендуОС.СрезПоследних(
	|			&ПериодГраница,
	|			ОсновноеСредство В
	|				(ВЫБРАТЬ
	|					Т.ОсновноеСредство
	|				ИЗ
	|					втАрендованныеОС_Предварительная КАК Т)) КАК ПереданныеВАренду
	|ГДЕ
	|	ПереданныеВАренду.Договор.ТипДоговора В(&ТипыДоговоровПоФСБУ25)
	|	И ПереданныеВАренду.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПереданоВАренду)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АрендованныеОС.ОсновноеСредство КАК ОсновноеСредство,
	|	АрендованныеОС.Стоимость КАК Стоимость,
	|	АрендованныеОС.НоминальнаяСтоимость КАК НоминальнаяСтоимость,
	|	АрендованныеОС.Действие КАК Действие,
	|	АрендованныеОС.Подразделение КАК Подразделение,
	|	АрендованныеОС.СтатьяРасходов КАК СтатьяРасходов,
	|	НЕ ПереданныеВАренду.ОсновноеСредство ЕСТЬ NULL КАК Субаренда
	|ПОМЕСТИТЬ втАрендованныеОС
	|ИЗ
	|	втАрендованныеОС_Предварительная КАК АрендованныеОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПереданныеВАренду КАК ПереданныеВАренду
	|		ПО ПереданныеВАренду.ОсновноеСредство = АрендованныеОС.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УсловияДоговоровАренды.Договор.Организация КАК Организация,
	|	УсловияДоговоровАренды.Договор.Партнер КАК Партнер,
	|	УсловияДоговоровАренды.Договор.Контрагент КАК Контрагент,
	|	УсловияДоговоровАренды.Договор.ВалютаВзаиморасчетов КАК Валюта,
	|	УсловияДоговоровАренды.Договор.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	УсловияДоговоровАренды.СтавкаНДС КАК СтавкаНДС,
	|	УсловияДоговоровАренды.СуммаОбеспечительногоПлатежа КАК СуммаОбеспечительногоПлатежа,
	|	УсловияДоговоровАренды.СуммаУслугПоАренде КАК СуммаУслугПоАренде,
	|	УсловияДоговоровАренды.СуммаВыкупаПредметовАренды КАК СуммаВыкупаПредметовАренды,
	|	УсловияДоговоровАренды.СуммаПроцентов КАК СуммаПроцентов,
	|	УсловияДоговоровАренды.СтоимостьПредметовАренды КАК СтоимостьПредметовАренды,
	|	УсловияДоговоровАренды.СуммаНДС КАК СуммаНДС,
	|	УсловияДоговоровАренды.СрокЛет КАК СрокЛет,
	|	УсловияДоговоровАренды.СрокМес КАК СрокМес,
	|	УсловияДоговоровАренды.СрокДней КАК СрокДней,
	|	УсловияДоговоровАренды.ДатаНачалаАренды КАК ДатаНачалаАренды,
	|	УсловияДоговоровАренды.ДатаПервогоПлатежа КАК ДатаПервогоПлатежа,
	|	УсловияДоговоровАренды.ДатаПервогоОбеспечительногоПлатежа КАК ДатаПервогоОбеспечительногоПлатежа,
	|	УсловияДоговоровАренды.ДатаВыкупа КАК ДатаВыкупа,
	|	УсловияДоговоровАренды.ПроцентнаяСтавка КАК ПроцентнаяСтавка,
	|	УсловияДоговоровАренды.ПериодичностьОплат КАК ПериодичностьОплат,
	|	УсловияДоговоровАренды.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	УсловияДоговоровАренды.ОплатаУслугиПоАренде КАК ОплатаУслугиПоАренде,
	|	УсловияДоговоровАренды.НачислениеУслугиПоАренде КАК НачислениеУслугиПоАренде,
	|	УсловияДоговоровАренды.АктуальныеУсловияДоговора КАК АктуальныеУсловияДоговора,
	|	ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(УсловияДоговоровАренды.АктуальныеУсловияДоговора)
	|		= ТИП(Документ.ИзменениеУсловийДоговораАренды) ТОГДА
	|		НАЧАЛОПЕРИОДА(ВЫРАЗИТЬ(УсловияДоговоровАренды.АктуальныеУсловияДоговора КАК Документ.ИзменениеУсловийДоговораАренды).Дата, ДЕНЬ)
	|	ИНАЧЕ
	|		УсловияДоговоровАренды.ДатаНачалаАренды
	|	КОНЕЦ КАК ДатаИзмененияДоговора
	|ПОМЕСТИТЬ втУсловияДоговоровАренды
	|ИЗ
	|	РегистрСведений.УсловияДоговоровАренды.СрезПоследних(
	|			&Дата,
	|			Регистратор <> &Ссылка
	|				И Договор = &Договор) КАК УсловияДоговоровАренды
	|;
	| 
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеГрафика.Дата КАК Дата,
	|	ДанныеГрафика.УслугаПоАренде КАК УслугаПоАренде,
	|	ДанныеГрафика.УслугаПоАрендеНДС КАК УслугаПоАрендеНДС,
	|	ДанныеГрафика.ОбеспечительныйПлатеж КАК ОбеспечительныйПлатеж,
	|	ДанныеГрафика.ОбеспечительныйПлатежНДС КАК ОбеспечительныйПлатежНДС,
	|	ДанныеГрафика.ВыкупнаяСтоимость КАК ВыкупнаяСтоимость,
	|	ДанныеГрафика.ВыкупнаяСтоимостьНДС КАК ВыкупнаяСтоимостьНДС
	|ПОМЕСТИТЬ втГрафикОплатУслугИзДокумента
	|ИЗ
	|	&ГрафикОплатУслуг КАК ДанныеГрафика
	|ГДЕ
	|	ДанныеГрафика.Дата >= НАЧАЛОПЕРИОДА(&Дата, ДЕНЬ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеГрафика.Дата КАК Дата,
	|	ДанныеГрафика.УслугаПоАренде КАК УслугаПоАренде,
	|	ДанныеГрафика.УслугаПоАрендеНДС КАК УслугаПоАрендеНДС,
	|	ДанныеГрафика.ОбеспечительныйПлатеж КАК ОбеспечительныйПлатеж,
	|	ДанныеГрафика.ОбеспечительныйПлатежНДС КАК ОбеспечительныйПлатежНДС
	|ПОМЕСТИТЬ втГрафикНачисленияУслугИзДокумента
	|ИЗ
	|	&ГрафикНачисленияУслуг КАК ДанныеГрафика
	|ГДЕ
	|	ДанныеГрафика.Дата >= НАЧАЛОПЕРИОДА(&Дата, ДЕНЬ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеГрафика.Дата КАК Дата,
	|	ДанныеГрафика.УслугаПоАренде КАК УслугаПоАренде,
	|	ДанныеГрафика.УслугаПоАрендеНДС КАК УслугаПоАрендеНДС,
	|	ДанныеГрафика.ОбеспечительныйПлатеж КАК ОбеспечительныйПлатеж,
	|	ДанныеГрафика.ОбеспечительныйПлатежНДС КАК ОбеспечительныйПлатежНДС,
	|	ДанныеГрафика.ВыкупнаяСтоимость + ДанныеГрафика.ВыкупнаяСтоимостьАванс КАК ВыкупнаяСтоимость,
	|	ДанныеГрафика.ВыкупнаяСтоимостьНДС КАК ВыкупнаяСтоимостьНДС
	|ПОМЕСТИТЬ втГрафикОплатУслуг
	|ИЗ
	|	РегистрСведений.ГрафикОплатУслугПоАренде КАК ДанныеГрафика
	|ГДЕ
	|	ДанныеГрафика.АктуальныеУсловияДоговора В
	|			(ВЫБРАТЬ
	|				Т.АктуальныеУсловияДоговора
	|			ИЗ
	|				втУсловияДоговоровАренды КАК Т)
	|	И ДанныеГрафика.Активность
	|	И (ДанныеГрафика.Дата < НАЧАЛОПЕРИОДА(&Дата, ДЕНЬ) ИЛИ &Перезаполнить)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеГрафика.Дата КАК Дата,
	|	ДанныеГрафика.УслугаПоАренде КАК УслугаПоАренде,
	|	ДанныеГрафика.УслугаПоАрендеНДС КАК УслугаПоАрендеНДС,
	|	ДанныеГрафика.ОбеспечительныйПлатеж КАК ОбеспечительныйПлатеж,
	|	ДанныеГрафика.ОбеспечительныйПлатежНДС КАК ОбеспечительныйПлатежНДС,
	|	ДанныеГрафика.ВыкупнаяСтоимость КАК ВыкупнаяСтоимость,
	|	ДанныеГрафика.ВыкупнаяСтоимостьНДС КАК ВыкупнаяСтоимостьНДС
	|ИЗ
	|	втГрафикОплатУслугИзДокумента КАК ДанныеГрафика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеГрафика.Дата КАК Дата,
	|	ДанныеГрафика.УслугаПоАренде КАК УслугаПоАренде,
	|	ДанныеГрафика.УслугаПоАрендеНДС КАК УслугаПоАрендеНДС,
	|	ДанныеГрафика.ОбеспечительныйПлатеж КАК ОбеспечительныйПлатеж,
	|	ДанныеГрафика.ОбеспечительныйПлатежНДС КАК ОбеспечительныйПлатежНДС
	|ПОМЕСТИТЬ втГрафикНачисленияУслуг
	|ИЗ
	|	РегистрСведений.ГрафикНачисленияУслугПоАренде КАК ДанныеГрафика
	|ГДЕ
	|	ДанныеГрафика.АктуальныеУсловияДоговора В
	|			(ВЫБРАТЬ
	|				Т.АктуальныеУсловияДоговора
	|			ИЗ
	|				втУсловияДоговоровАренды КАК Т)
	|	И ДанныеГрафика.Активность
	|	И (ДанныеГрафика.УслугаПоАренде <> 0
	|			ИЛИ ДанныеГрафика.ОбеспечительныйПлатеж <> 0)
	|	И (ДанныеГрафика.Дата < НАЧАЛОПЕРИОДА(&Дата, ДЕНЬ) ИЛИ &Перезаполнить)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеГрафика.Дата КАК Дата,
	|	ДанныеГрафика.УслугаПоАренде КАК УслугаПоАренде,
	|	ДанныеГрафика.УслугаПоАрендеНДС КАК УслугаПоАрендеНДС,
	|	ДанныеГрафика.ОбеспечительныйПлатеж КАК ОбеспечительныйПлатеж,
	|	ДанныеГрафика.ОбеспечительныйПлатежНДС КАК ОбеспечительныйПлатежНДС
	|ИЗ
	|	втГрафикНачисленияУслугИзДокумента КАК ДанныеГрафика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеГрафика.Дата КАК Дата,
	|	ДанныеГрафика.Проценты КАК Проценты
	|ПОМЕСТИТЬ втГрафикНачисленияПроцентов
	|ИЗ
	|	РегистрСведений.ГрафикНачисленияПроцентовПоАренде КАК ДанныеГрафика
	|ГДЕ
	|	ДанныеГрафика.АктуальныеУсловияДоговора В
	|			(ВЫБРАТЬ
	|				Т.АктуальныеУсловияДоговора
	|			ИЗ
	|				втУсловияДоговоровАренды КАК Т)
	|	И ДанныеГрафика.Активность
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(втГрафикОплатУслуг.УслугаПоАренде) КАК УслугаПоАренде,
	|	СУММА(втГрафикОплатУслуг.УслугаПоАрендеНДС) КАК УслугаПоАрендеНДС,
	|	СУММА(втГрафикОплатУслуг.ОбеспечительныйПлатеж) КАК ОбеспечительныйПлатеж,
	|	СУММА(втГрафикОплатУслуг.ОбеспечительныйПлатежНДС) КАК ОбеспечительныйПлатежНДС,
	|	СУММА(втГрафикОплатУслуг.ВыкупнаяСтоимость) КАК ВыкупнаяСтоимость,
	|	СУММА(втГрафикОплатУслуг.ВыкупнаяСтоимостьНДС) КАК ВыкупнаяСтоимостьНДС
	|ПОМЕСТИТЬ втГрафикОплатУслугОстатки
	|ИЗ
	|	втГрафикОплатУслуг КАК втГрафикОплатУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ втУсловияДоговоровАренды КАК втУсловияДоговоровАренды
	|		ПО (ИСТИНА)
	|ГДЕ
	|	втГрафикОплатУслуг.Дата < НАЧАЛОПЕРИОДА(&Дата, ДЕНЬ)
	|	И втГрафикОплатУслуг.Дата >= ЕСТЬNULL(втУсловияДоговоровАренды.ДатаИзмененияДоговора, ДАТАВРЕМЯ(1,1,1))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(втГрафикНачисленияУслуг.УслугаПоАренде) КАК УслугаПоАренде,
	|	СУММА(втГрафикНачисленияУслуг.УслугаПоАрендеНДС) КАК УслугаПоАрендеНДС,
	|	СУММА(втГрафикНачисленияУслуг.ОбеспечительныйПлатеж) КАК ОбеспечительныйПлатеж,
	|	СУММА(втГрафикНачисленияУслуг.ОбеспечительныйПлатежНДС) КАК ОбеспечительныйПлатежНДС
	|ПОМЕСТИТЬ втГрафикНачисленияУслугОстатки
	|ИЗ
	|	втГрафикНачисленияУслуг КАК втГрафикНачисленияУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ втУсловияДоговоровАренды КАК втУсловияДоговоровАренды
	|		ПО (ИСТИНА)
	|ГДЕ
	|	втГрафикНачисленияУслуг.Дата < НАЧАЛОПЕРИОДА(&Дата, ДЕНЬ)
	|	И втГрафикНачисленияУслуг.Дата >= ЕСТЬNULL(втУсловияДоговоровАренды.ДатаИзмененияДоговора, ДАТАВРЕМЯ(1,1,1))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(втГрафикНачисленияПроцентов.Проценты) КАК Проценты
	|ПОМЕСТИТЬ втГрафикНачисленияПроцентовОстатки
	|ИЗ
	|	втГрафикНачисленияПроцентов КАК втГрафикНачисленияПроцентов
	|		ЛЕВОЕ СОЕДИНЕНИЕ втУсловияДоговоровАренды КАК втУсловияДоговоровАренды
	|		ПО (ИСТИНА)
	|ГДЕ
	|	втГрафикНачисленияПроцентов.Дата < НАЧАЛОПЕРИОДА(&Дата, ДЕНЬ)
	|	И втГрафикНачисленияПроцентов.Дата >= ЕСТЬNULL(втУсловияДоговоровАренды.ДатаИзмененияДоговора, ДАТАВРЕМЯ(1,1,1))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(втГрафикНачисленияУслуг.УслугаПоАренде) КАК УслугаПоАренде,
	|	СУММА(втГрафикНачисленияУслуг.УслугаПоАрендеНДС) КАК УслугаПоАрендеНДС
	|ПОМЕСТИТЬ втГрафикНачисленияУслугОстаткиЗаВесьПериод
	|ИЗ
	|	втГрафикНачисленияУслуг КАК втГрафикНачисленияУслуг
	|ГДЕ
	|	втГрафикНачисленияУслуг.Дата < НАЧАЛОПЕРИОДА(&Дата, ДЕНЬ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(втГрафикОплатУслуг.УслугаПоАренде) КАК УслугаПоАренде,
	|	СУММА(втГрафикОплатУслуг.УслугаПоАрендеНДС) КАК УслугаПоАрендеНДС
	|ПОМЕСТИТЬ втГрафикОплатУслугОстаткиЗаВесьПериод
	|ИЗ
	|	втГрафикОплатУслуг КАК втГрафикОплатУслуг
	|ГДЕ
	|	втГрафикОплатУслуг.Дата < НАЧАЛОПЕРИОДА(&Дата, ДЕНЬ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(втГрафикОплатУслугОстаткиЗаВесьПериод.УслугаПоАренде, 0)
	|		- ЕСТЬNULL(втГрафикНачисленияУслугОстаткиЗаВесьПериод.УслугаПоАренде, 0) КАК ОплатыОстаток,
	|	ЕСТЬNULL(втГрафикОплатУслугОстаткиЗаВесьПериод.УслугаПоАрендеНДС, 0)
	|		- ЕСТЬNULL(втГрафикНачисленияУслугОстаткиЗаВесьПериод.УслугаПоАрендеНДС, 0) КАК ОплатыНДСОстаток
	|ПОМЕСТИТЬ втРазницаОстатковОплат
	|ИЗ
	|	втГрафикОплатУслугОстаткиЗаВесьПериод КАК втГрафикОплатУслугОстаткиЗаВесьПериод,
	|	втГрафикНачисленияУслугОстаткиЗаВесьПериод КАК втГрафикНачисленияУслугОстаткиЗаВесьПериод
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(втГрафикОплатУслуг.Дата) КАК Дата
	|ПОМЕСТИТЬ втГрафикОплатУслугПерваяОплата
	|ИЗ
	|	втГрафикОплатУслуг КАК втГрафикОплатУслуг
	|ГДЕ
	|	втГрафикОплатУслуг.Дата >= НАЧАЛОПЕРИОДА(&Дата, ДЕНЬ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(втГрафикОплатУслуг.ВыкупнаяСтоимостьНДС) КАК ВыкупнаяСтоимостьНДС
	|ПОМЕСТИТЬ втГрафикОплатУслугВыкуп
	|ИЗ
	|	втГрафикОплатУслуг КАК втГрафикОплатУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ втУсловияДоговоровАренды КАК втУсловияДоговоровАренды
	|		ПО (ИСТИНА)
	|ГДЕ
	|	втГрафикОплатУслуг.Дата >= НАЧАЛОПЕРИОДА(&Дата, ДЕНЬ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ 
	|	&Дата КАК Дата,
	|	УсловияДоговоровАренды.Организация КАК Организация,
	|	УсловияДоговоровАренды.Партнер КАК Партнер,
	|	УсловияДоговоровАренды.Контрагент КАК Контрагент,
	|	УсловияДоговоровАренды.Валюта КАК Валюта,
	|	УсловияДоговоровАренды.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	УсловияДоговоровАренды.СтавкаНДС КАК СтавкаНДС,
	|	УсловияДоговоровАренды.СуммаОбеспечительногоПлатежа
	|		- ЕСТЬNULL(втГрафикНачисленияУслугОстатки.ОбеспечительныйПлатеж, 0) КАК СуммаОбеспечительногоПлатежа,
	|	УсловияДоговоровАренды.СуммаУслугПоАренде
	|		- ЕСТЬNULL(втГрафикОплатУслугОстатки.УслугаПоАренде, 0)
	|		+ ЕСТЬNULL(втРазницаОстатковОплат.ОплатыОстаток, 0) КАК СуммаУслугПоАренде,
	|	УсловияДоговоровАренды.СуммаВыкупаПредметовАренды
	|		- ЕСТЬNULL(втГрафикОплатУслугОстатки.ВыкупнаяСтоимость, 0) КАК СуммаВыкупаПредметовАренды,
	|	УсловияДоговоровАренды.СуммаПроцентов
	|		- ЕСТЬNULL(втГрафикНачисленияПроцентовОстатки.Проценты, 0) КАК СуммаПроцентов,
	|	УсловияДоговоровАренды.СтоимостьПредметовАренды КАК СтоимостьПредметовАренды,
	|	УсловияДоговоровАренды.СуммаНДС - ЕСТЬNULL(втГрафикОплатУслугОстатки.УслугаПоАрендеНДС, 0) 
	|		- ЕСТЬNULL(втГрафикОплатУслугОстатки.ВыкупнаяСтоимостьНДС, 0)
	|		- ЕСТЬNULL(втГрафикНачисленияУслугОстатки.ОбеспечительныйПлатежНДС,0)
	|		+ ЕСТЬNULL(втРазницаОстатковОплат.ОплатыНДСОстаток, 0) КАК СуммаНДС,
	|	ЕСТЬNULL(втГрафикОплатУслугВыкуп.ВыкупнаяСтоимостьНДС, 0) КАК СуммаНДСВыкупаПредметовАренды,
	|	УсловияДоговоровАренды.СрокЛет КАК СрокЛет,
	|	УсловияДоговоровАренды.СрокМес КАК СрокМес,
	|	УсловияДоговоровАренды.СрокДней КАК СрокДней,
	|	УсловияДоговоровАренды.ДатаНачалаАренды КАК ДатаНачалаАренды,
	|	ЕСТЬNULL(втГрафикОплатУслугПерваяОплата.Дата, УсловияДоговоровАренды.ДатаПервогоПлатежа) КАК ДатаПервогоПлатежа,
	|	УсловияДоговоровАренды.ДатаПервогоОбеспечительногоПлатежа КАК ДатаПервогоОбеспечительногоПлатежа,
	|	УсловияДоговоровАренды.ДатаВыкупа КАК ДатаВыкупа,
	|	УсловияДоговоровАренды.ПроцентнаяСтавка КАК ПроцентнаяСтавка,
	|	УсловияДоговоровАренды.ПериодичностьОплат КАК ПериодичностьОплат,
	|	УсловияДоговоровАренды.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	УсловияДоговоровАренды.ОплатаУслугиПоАренде КАК ОплатаУслугиПоАренде,
	|	УсловияДоговоровАренды.НачислениеУслугиПоАренде КАК НачислениеУслугиПоАренде,
	|	УсловияДоговоровАренды.АктуальныеУсловияДоговора КАК ПредыдущиеУсловияАренды
	|ИЗ
	|	втУсловияДоговоровАренды КАК УсловияДоговоровАренды
	|		ЛЕВОЕ СОЕДИНЕНИЕ втГрафикОплатУслугОстатки КАК втГрафикОплатУслугОстатки
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втГрафикНачисленияПроцентовОстатки КАК втГрафикНачисленияПроцентовОстатки
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втГрафикНачисленияУслугОстатки КАК втГрафикНачисленияУслугОстатки
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втГрафикОплатУслугПерваяОплата КАК втГрафикОплатУслугПерваяОплата
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втГрафикОплатУслугВыкуп КАК втГрафикОплатУслугВыкуп
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втРазницаОстатковОплат КАК втРазницаОстатковОплат
	|		ПО (ИСТИНА)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрафикОплатУслуг.Дата КАК Дата,
	|	втГрафикОплатУслуг.УслугаПоАренде КАК УслугаПоАренде,
	|	втГрафикОплатУслуг.УслугаПоАрендеНДС КАК УслугаПоАрендеНДС,
	|	втГрафикОплатУслуг.ОбеспечительныйПлатеж КАК ОбеспечительныйПлатеж,
	|	втГрафикОплатУслуг.ОбеспечительныйПлатежНДС КАК ОбеспечительныйПлатежНДС,
	|	втГрафикОплатУслуг.ВыкупнаяСтоимость КАК ВыкупнаяСтоимость,
	|	втГрафикОплатУслуг.ВыкупнаяСтоимостьНДС КАК ВыкупнаяСтоимостьНДС
	|ИЗ
	|	втГрафикОплатУслуг КАК втГрафикОплатУслуг
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрафикНачисленияУслуг.Дата КАК Дата,
	|	втГрафикНачисленияУслуг.УслугаПоАренде КАК УслугаПоАренде,
	|	втГрафикНачисленияУслуг.УслугаПоАрендеНДС КАК УслугаПоАрендеНДС,
	|	втГрафикНачисленияУслуг.ОбеспечительныйПлатеж КАК ОбеспечительныйПлатеж,
	|	втГрафикНачисленияУслуг.ОбеспечительныйПлатежНДС КАК ОбеспечительныйПлатежНДС
	|ИЗ
	|	втГрафикНачисленияУслуг КАК втГрафикНачисленияУслуг
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрафикНачисленияПроцентов.Дата КАК Дата,
	|	втГрафикНачисленияПроцентов.Проценты КАК Проценты
	|ИЗ
	|	втГрафикНачисленияПроцентов КАК втГрафикНачисленияПроцентов
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата";
	ТекстыПакетаЗапросов.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = ИзменениеУсловийДоговораАрендыЛокализация.ТекстЗапросаЗаполнениеТЧПоДоговору();
	
	Если ТекстЗапроса = Неопределено Тогда
		ТекстЗапроса =	
		"ВЫБРАТЬ
		|	АрендованныеОС.ОсновноеСредство КАК ОсновноеСредство,
		|	ВЫБОР КОГДА &РасчетПоФактическойСтоимости ТОГДА
		|		ВЫБОР КОГДА АмортизацияОС.АмортизацияРеглОстаток <> 0 ТОГДА
		|			АрендованныеОС.Стоимость + ЕСТЬNULL(АмортизацияОС.АмортизацияРеглОстаток, 0) * &КоэффициентРегл
		|		КОГДА АмортизацияОС.АмортизацияОстаток <> 0 ТОГДА
		|			АрендованныеОС.Стоимость + ЕСТЬNULL(АмортизацияОС.АмортизацияОстаток, 0) * &КоэффициентУпр
		|		ИНАЧЕ
		|			АрендованныеОС.Стоимость
		|		КОНЕЦ
		|	ИНАЧЕ
		|		АрендованныеОС.НоминальнаяСтоимость
		|	КОНЕЦ КАК СправедливаяСтоимость,
		|	ВЫБОР КОГДА &РасчетПоФактическойСтоимости ТОГДА
		|		ВЫБОР КОГДА АмортизацияОС.АмортизацияРеглОстаток <> 0 ТОГДА
		|			АрендованныеОС.Стоимость + ЕСТЬNULL(АмортизацияОС.АмортизацияРеглОстаток, 0) * &КоэффициентРегл
		|		КОГДА АмортизацияОС.АмортизацияОстаток <> 0 ТОГДА
		|			АрендованныеОС.Стоимость + ЕСТЬNULL(АмортизацияОС.АмортизацияОстаток, 0) * &КоэффициентУпр
		|		ИНАЧЕ
		|			АрендованныеОС.Стоимость
		|		КОНЕЦ
		|	ИНАЧЕ
		|		0
		|	КОНЕЦ КАК Стоимость,
		|	АрендованныеОС.Действие КАК ДействиеСПредметомАренды,
		|	АрендованныеОС.Подразделение,
		|	АрендованныеОС.СтатьяРасходов,
		|	ЕСТЬNULL(СтоимостьОС.СтоимостьОстаток, 0) КАК СтоимостьУУ,
		|	ЕСТЬNULL(СтоимостьОС.СтоимостьРеглОстаток, 0) КАК СтоимостьБУ,
		|	ЕСТЬNULL(ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияБУ, 0) КАК СрокИспользованияБУ,
		|	ЕСТЬNULL(ПараметрыАмортизацииОСУУ.СрокИспользования, 0) КАК СрокИспользованияУУ
		|ИЗ
		|	втАрендованныеОС КАК АрендованныеОС
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СтоимостьОС.Остатки(
		|			&ПериодГраница, 
		|			Организация = &Организация
		|				И ОсновноеСредство В (
		|					ВЫБРАТЬ 
		|						втАрендованныеОС.ОсновноеСредство 
		|					ИЗ 
		|						втАрендованныеОС КАК втАрендованныеОС)) КАК СтоимостьОС
		|		ПО СтоимостьОС.ОсновноеСредство = АрендованныеОС.ОсновноеСредство
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСБУ.СрезПоследних(
		|			&Дата, 
		|			Регистратор <> &Ссылка
		|				И Организация = &Организация
		|				И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|				И ОсновноеСредство В (
		|					ВЫБРАТЬ 
		|						втАрендованныеОС.ОсновноеСредство 
		|					ИЗ 
		|						втАрендованныеОС КАК втАрендованныеОС)) КАК ПараметрыАмортизацииОСБУ
		|		ПО ПараметрыАмортизацииОСБУ.ОсновноеСредство = АрендованныеОС.ОсновноеСредство
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСУУ.СрезПоследних(
		|			&Дата, 
		|			Регистратор <> &Ссылка
		|				И Организация = &Организация
		|				И ДатаИсправления = ДАТАВРЕМЯ(1, 1, 1)
		|				И ОсновноеСредство В (
		|					ВЫБРАТЬ 
		|						втАрендованныеОС.ОсновноеСредство 
		|					ИЗ 
		|						втАрендованныеОС КАК втАрендованныеОС)) КАК ПараметрыАмортизацииОСУУ
		|		ПО ПараметрыАмортизацииОСУУ.ОсновноеСредство = АрендованныеОС.ОсновноеСредство
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.АмортизацияОС.Остатки(&НачалоМесяца, Организация = &Организация
		|		И ОсновноеСредство В
		|			(ВЫБРАТЬ
		|				втАрендованныеОС.ОсновноеСредство
		|			ИЗ
		|				втАрендованныеОС КАК втАрендованныеОС)) КАК АмортизацияОС
		|		ПО АмортизацияОС.ОсновноеСредство = АрендованныеОС.ОсновноеСредство
		|		И &РасчетПоФактическойСтоимости
		|
		|УПОРЯДОЧИТЬ ПО
		|	АрендованныеОС.ОсновноеСредство.Наименование,
		|	АрендованныеОС.ОсновноеСредство.ИнвентарныйНомер";
		
	КонецЕсли;
	
	ТекстыПакетаЗапросов.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = СтрСоединить(ТекстыПакетаЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	ДатаДок = ?(Объект.Дата <> '000101010000', Объект.Дата, ТекущаяДатаСеанса());

	РеквизитыДоговора = УчетАрендованныхОС.РеквизитыДоговораАренды(Объект.Договор);
	РасчетПоФактическойСтоимости = РеквизитыДоговора.СпособОпределенияСтоимостиАктивов
			= Перечисления.СпособыОпределенияСтоимостиПредметовАренды.ПоФактическойСтоимости;
	КоэффициентПересчетаИзВалютыРегл = РаботаСКурсамиВалютУТ.ПолучитьКоэффициентПересчетаИзВалютыВВалюту(
		ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Объект.Организация),
		Объект.Валюта,
		ДатаДок);
	КоэффициентПересчетаИзВалютыУпр = РаботаСКурсамиВалютУТ.ПолучитьКоэффициентПересчетаИзВалютыВВалюту(
		ЗначениеНастроекПовтИсп.ВалютаУправленческогоУчета(),
		Объект.Валюта,
		ДатаДок);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("Договор", Объект.Договор);
	Запрос.УстановитьПараметр("Дата", ДатаДок);
	Запрос.УстановитьПараметр("ПериодГраница", Новый Граница(Новый МоментВремени(ДатаДок, Объект.Ссылка), ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("Перезаполнить", НЕ Обновить);
	Запрос.УстановитьПараметр("ГрафикОплатУслуг", ГрафикОплатУслуг);
	Запрос.УстановитьПараметр("ГрафикНачисленияУслуг", ГрафикНачисленияУслуг);
	Запрос.УстановитьПараметр("ОС", ОС);
	Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(ДатаДок));
	Запрос.УстановитьПараметр("РасчетПоФактическойСтоимости", РасчетПоФактическойСтоимости);
	Запрос.УстановитьПараметр("КоэффициентРегл", КоэффициентПересчетаИзВалютыРегл);
	Запрос.УстановитьПараметр("КоэффициентУпр", КоэффициентПересчетаИзВалютыУпр);
	Запрос.УстановитьПараметр("ТипыДоговоровПоФСБУ25", УчетАрендованныхОСКлиентСервер.ТипыДоговоровДоходнойАренды());
	
	Результат = Запрос.ВыполнитьПакет();
	Если Результат[0].Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = Результат[Результат.ВГраница()-4].Выбрать();
	Выборка.Следующий();
	
	ЗаполнитьЗначенияСвойств(Объект, Выборка);
	
	Объект.ГрафикОплатУслуг.Загрузить(Результат[Результат.ВГраница()-3].Выгрузить());
	Объект.ГрафикНачисленияУслуг.Загрузить(Результат[Результат.ВГраница()-2].Выгрузить());
	Объект.ГрафикНачисленияПроцентов.Загрузить(Результат[Результат.ВГраница()-1].Выгрузить());
	Объект.ОС.Загрузить(Результат[Результат.ВГраница()].Выгрузить());
	
	Если Выборка.СуммаНДСВыкупаПредметовАренды = 0 Тогда
		ТекПроцентНДС = УчетНДСУПКлиентСервер.ЗначениеСтавкиНДС(Объект.СтавкаНДС);
		Объект.СуммаНДСВыкупаПредметовАренды = УчетНДСУПКлиентСервер.РассчитатьСуммуНДС(Объект.СуммаВыкупаПредметовАренды, ТекПроцентНДС, Истина);
	КонецЕсли;
	
	УчетАрендованныхОСКлиентСервер.ПересчитатьСуммуДокументаАренды(Объект);
	УчетАрендованныхОСКлиентСервер.ПересчитатьСуммуНДСВДокументеАренды(Объект);
	
КонецПроцедуры

#КонецОбласти

#Область Печать

Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = Справочники.ДоговорыАренды.ДобавитьКомандуРасшифровкиРасчетаПроцентов(КомандыПечати);
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	УправлениеПечатью.ДобавитьУсловиеВидимостиКоманды(КомандаПечати, "ГрафикНачисленияПроцентовВведенВручную", Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

// Параметры выбора статей и аналитик.
// 
// Параметры:
//  СозданДляПерехода - Булево - Создан для перехода.
// 
// Возвращаемое значение:
//  см. ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики.
Функция ПараметрыВыбораСтатейИАналитик(СозданДляПерехода) Экспорт

	СписокПараметров = Новый Массив; // Структура
	
	#Область Расходы
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья = "СтатьяРасходов";
	ПараметрыВыбора.ТипСтатьи = "ТипСтатьиРасходы";
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходов";
	
	
	ПараметрыВыбора.ВыборСтатьиАктивовПассивов = СозданДляПерехода;
	ПараметрыВыбора.АналитикаАктивовПассивов = "АналитикаАктивовПассивовРасходы";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаАктивовПассивов.Добавить("АналитикаАктивовПассивовРасходы");
	
	СписокПараметров.Добавить(ПараметрыВыбора);
	#КонецОбласти
	
	#Область Доходы
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья = "СтатьяДоходов";
	
	ПараметрыВыбора.ВыборСтатьиДоходов = Истина;
	ПараметрыВыбора.АналитикаДоходов = "АналитикаДоходов";

	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяДоходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаДоходов.Добавить("АналитикаДоходов");
	
	СписокПараметров.Добавить(ПараметрыВыбора);
	#КонецОбласти

	Возврат СписокПараметров;
	
КонецФункции

Функция ПараметрыНастройкиСчетовУчета() Экспорт
	
	СписокПараметров = Новый Массив;

	#Область Расходы
	ПараметрыНастройки = НастройкаСчетовУчетаСервер.ПараметрыНастройки();
	ПараметрыНастройки.ПутьКДанным = "Объект";
	ПараметрыНастройки.Представление = "ПредставлениеОтраженияВРеглУчетеРасходы";
	ПараметрыНастройки.ТипСтатьи = "ТипСтатьиРасходы";
	ПараметрыНастройки.НастройкаСчетовУчета = "НастройкаСчетовУчетаРасходы";
	
	ПараметрыНастройки.Организация = "Объект.Организация";
	ПараметрыНастройки.АналитикаАктивовПассивов = "АналитикаАктивовПассивовРасходы";

	ПараметрыНастройки.ЭлементыФормы.Добавить("ПредставлениеОтраженияВРеглУчетеРасходы");
	
	СписокПараметров.Добавить(ПараметрыНастройки);
	#КонецОбласти
	
	Возврат СписокПараметров;
	
КонецФункции

// Рассчитать ставку, стоимость, проценты.
// 
// Параметры:
//  Объект - ДокументОбъект.ИзменениеУсловийДоговораАренды
//  РеквизитыДоговора - Структура
Процедура РассчитатьСтавкуСтоимостьПроценты(Объект, РеквизитыДоговора) Экспорт

	ПараметрыРасчета = УчетАрендованныхОС.ПараметрыРасчетаСтавкиСтоимостиПроцентов(Объект, РеквизитыДоговора);
	ПараметрыРасчета.ТаблицаОС = Объект.ОС;
	ПараметрыРасчета.ДатаНачалаАренды = Объект.ДатаНачалаАренды;
	
	ПараметрыРасчета.Вставить("СозданДляПерехода", УчетАрендованныхОС.СозданДляПереходаНаФСБУ25_2018(Объект.Дата, Объект.Организация));
	
	ПредыдущиеУсловия = УчетАрендованныхОС.СтоимостьПоПредыдущимУсловиям(Объект, Истина);
	ПараметрыРасчета.Вставить("ПриведеннаяСтоимостьПоПредыдущимУсловиям", ПредыдущиеУсловия.ПриведеннаяСтоимость);
	ПараметрыРасчета.Вставить("ДатаПредыдущегоИзменения", ПредыдущиеУсловия.ДатаИзменения);
	Если ПредыдущиеУсловия.ГрафикПроцентов <> Неопределено Тогда
		ПараметрыРасчета.Вставить("ГрафикПроцентовПоПредыдущимУсловиям", ПредыдущиеУсловия.ГрафикПроцентов);
	КонецЕсли;
	
	ПараметрыРасчета.СтоимостьПредметовАренды
		= УчетАрендованныхОСКлиентСервер.СтоимостьПредметовАренды(Объект, РеквизитыДоговора, "ОС");

	// Расчет производится по данным из табличных частей документа.
	// Заполнение табличных частей зависит от режима перехода на ФСБУ25.
	УчетАрендованныхОС.РассчитатьСтавкуСтоимостьПроценты(ПараметрыРасчета);
	
	Объект.ПроцентнаяСтавка = ПараметрыРасчета.ПроцентнаяСтавка;
	Объект.СуммаПроцентов = ПараметрыРасчета.СуммаПроцентов;
	Объект.ПриведеннаяСтоимость = ПараметрыРасчета.ПриведеннаяСтоимость;
	
КонецПроцедуры

Процедура ПроверитьАктуальностьДоговораАренды(Ссылка, Договор, Дата, Отказ = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПРЕДСТАВЛЕНИЕ(УсловияДоговоровАрендыНаДату.АктуальныеУсловияДоговора) КАК ПредставлениеДокумента,
		|	УсловияДоговоровАрендыНаДату.Состояние КАК Состояние,
		|	УсловияДоговоровАрендыНаДату.ДатаНачалаАренды КАК ДатаНачалаАренды
		|ИЗ
		|	РегистрСведений.УсловияДоговоровАренды.СрезПоследних(
		|			&Дата,
		|			Регистратор <> &Ссылка
		|				И Договор = &Договор) КАК УсловияДоговоровАрендыНаДату
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 10
		|	ПРЕДСТАВЛЕНИЕ(УсловияДоговоровАренды.Регистратор) КАК ПредставлениеДокумента,
		|	УсловияДоговоровАренды.Регистратор КАК СсылкаНаДокумент
		|ИЗ
		|	РегистрСведений.УсловияДоговоровАренды КАК УсловияДоговоровАренды
		|ГДЕ
		|	(УсловияДоговоровАренды.Регистратор ССЫЛКА Документ.ИзменениеУсловийДоговораАренды
		|	ИЛИ УсловияДоговоровАренды.Регистратор ССЫЛКА Документ.ЗаключениеДоговораАренды)
		|	И НАЧАЛОПЕРИОДА(УсловияДоговоровАренды.Период, ДЕНЬ) = НАЧАЛОПЕРИОДА(&Дата, ДЕНЬ)
		|	И УсловияДоговоровАренды.Договор = &Договор
		|	И УсловияДоговоровАренды.Регистратор <> &Ссылка
		|УПОРЯДОЧИТЬ ПО
		|	УсловияДоговоровАренды.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 10
		|	ПРЕДСТАВЛЕНИЕ(УсловияДоговоровАренды.Регистратор) КАК ПредставлениеДокумента,
		|	УсловияДоговоровАренды.Регистратор КАК СсылкаНаДокумент
		|ИЗ
		|	РегистрСведений.УсловияДоговоровАренды КАК УсловияДоговоровАренды
		|ГДЕ
		|	УсловияДоговоровАренды.Период >= &Дата
		|	И УсловияДоговоровАренды.Договор = &Договор
		|	И УсловияДоговоровАренды.Регистратор <> &Ссылка
		|УПОРЯДОЧИТЬ ПО
		|	УсловияДоговоровАренды.Период";
	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Договор", Договор);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	СсылкаНаДокумент = Ссылка;
	Если РезультатыЗапроса[0].Пустой() Тогда
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Не оформлено заключение договора на дату %1';
									|en = 'Contract conclusion is not registered as of %1'"), Дата);
	КонецЕсли;
	
	Выборка = РезультатыЗапроса[0].Выбрать();
	Выборка.Следующий();
	
	Если НЕ ЗначениеЗаполнено(ТекстОшибки) И Выборка.Состояние <> Перечисления.СостоянияДоговоровКонтрагентов.Действует Тогда
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Договор аренды не действует на дату %1';
									|en = 'The rental contract is not valid as of %1'"), Дата);
		ТекстОшибки = ТекстОшибки + Символы.ПС + СтрШаблон(НСтр("ru = 'См. документ: %1';
																|en = 'See the document: %1'"), Выборка.ПредставлениеДокумента);
	КонецЕсли; 
	
	Если НЕ ЗначениеЗаполнено(ТекстОшибки) И Выборка.ДатаНачалаАренды >= Дата Тогда
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Дата документа должна быть больше даты начала аренды %1';
									|en = 'The document date must be later than the rental start date %1'"), Выборка.ДатаНачалаАренды);
		ТекстОшибки = ТекстОшибки + Символы.ПС + СтрШаблон(НСтр("ru = 'См. документ: %1';
																|en = 'See the document: %1'"), Выборка.ПредставлениеДокумента);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ТекстОшибки) И НЕ РезультатыЗапроса[1].Пустой() Тогда
		ТекстОшибки = НСтр("ru = 'В один день не может быть оформлено несколько документов изменений условий аренды по одному договору. Проведение документа будет недоступно.';
							|en = 'You cannot create several rental term adjustment documents under one contract within one day. You will not be able to post the document.'");
		Выборка = РезультатыЗапроса[1].Выбрать();
		ПервыйДокумент = Истина;
		Пока Выборка.Следующий() Цикл
			Если ПервыйДокумент Тогда
				СсылкаНаДокумент = Выборка.СсылкаНаДокумент;
				ПервыйДокумент = Ложь;
			КонецЕсли;
			ТекстОшибки = ТекстОшибки + Символы.ПС + СтрШаблон(НСтр("ru = 'См. документ: %1';
																	|en = 'See the document: %1'"), Выборка.ПредставлениеДокумента);
		КонецЦикла;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ТекстОшибки) И НЕ РезультатыЗапроса[2].Пустой() Тогда
		ТекстОшибки = НСтр("ru = 'Оформлены более поздние документы изменения условий договора. Проведение документа будет недоступно.';
							|en = 'More recent contract term adjustment documents were issued. You will not be able to post the document.'");
		Выборка = РезультатыЗапроса[2].Выбрать();
		ПервыйДокумент = Истина;
		Пока Выборка.Следующий() Цикл
			Если ПервыйДокумент Тогда
				СсылкаНаДокумент = Выборка.СсылкаНаДокумент;
				ПервыйДокумент = Ложь;
			КонецЕсли;
			ТекстОшибки = ТекстОшибки + Символы.ПС + СтрШаблон(НСтр("ru = 'См. документ: %1';
																	|en = 'See the document: %1'"), Выборка.ПредставлениеДокумента);
		КонецЦикла;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, СсылкаНаДокумент, "Объект.Дата",, Отказ);
	КонецЕсли;
	
КонецПроцедуры

Функция ПредыдущиеУсловияДоговораАренды(Ссылка, Договор, Дата) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УсловияДоговоровАрендыНаДату.АктуальныеУсловияДоговора КАК УсловияДоговора,
		|	УсловияДоговоровАрендыНаДату.Состояние КАК Состояние
		|ИЗ
		|	РегистрСведений.УсловияДоговоровАренды.СрезПоследних(&Дата, Регистратор <> &Ссылка И Договор = &Договор) КАК УсловияДоговоровАрендыНаДату";
	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Договор", Договор);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Если Выборка.Состояние = Перечисления.СостоянияДоговоровКонтрагентов.Действует Тогда
			Возврат Выборка.УсловияДоговора;
		Иначе
			Возврат Неопределено;
		КонецЕсли;
	Иначе
		Возврат Неопределено;
	КонецЕсли;

КонецФункции 

Процедура ЗаполнитьГрафикиПоПредыдущимУсловиямСУчетомИзменений(Объект) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Объект.Договор) Тогда
		Возврат;
	КонецЕсли;
	
	ГрафикОплатУслуг = Объект.ГрафикОплатУслуг.Выгрузить();
	ГрафикНачисленияУслуг = Объект.ГрафикНачисленияУслуг.Выгрузить();
	ГрафикНачисленияПроцентов = Объект.ГрафикНачисленияПроцентов.Выгрузить();

	Объект.ГрафикОплатУслуг.Очистить();
	Объект.ГрафикНачисленияУслуг.Очистить();
	Объект.ГрафикНачисленияПроцентов.Очистить();
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	УсловияДоговоровАренды.АктуальныеУсловияДоговора КАК АктуальныеУсловияДоговора
	|ПОМЕСТИТЬ втУсловияДоговоровАренды
	|ИЗ
	|	РегистрСведений.УсловияДоговоровАренды.СрезПоследних(
	|			&Дата,
	|			Регистратор <> &Ссылка
	|				И Договор = &Договор) КАК УсловияДоговоровАренды
	|;
	| 
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеГрафика.Дата КАК Дата,
	|	ДанныеГрафика.УслугаПоАренде КАК УслугаПоАренде,
	|	ДанныеГрафика.УслугаПоАрендеНДС КАК УслугаПоАрендеНДС,
	|	ДанныеГрафика.ОбеспечительныйПлатеж КАК ОбеспечительныйПлатеж,
	|	ДанныеГрафика.ОбеспечительныйПлатежНДС КАК ОбеспечительныйПлатежНДС,
	|	ДанныеГрафика.ВыкупнаяСтоимость КАК ВыкупнаяСтоимость,
	|	ДанныеГрафика.ВыкупнаяСтоимостьНДС КАК ВыкупнаяСтоимостьНДС
	|ПОМЕСТИТЬ втГрафикОплатУслугИзДокумента
	|ИЗ
	|	&ГрафикОплатУслуг КАК ДанныеГрафика
	|ГДЕ
	|	ДанныеГрафика.Дата >= НАЧАЛОПЕРИОДА(&Дата, ДЕНЬ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеГрафика.Дата КАК Дата,
	|	ДанныеГрафика.УслугаПоАренде КАК УслугаПоАренде,
	|	ДанныеГрафика.УслугаПоАрендеНДС КАК УслугаПоАрендеНДС,
	|	ДанныеГрафика.ОбеспечительныйПлатеж КАК ОбеспечительныйПлатеж,
	|	ДанныеГрафика.ОбеспечительныйПлатежНДС КАК ОбеспечительныйПлатежНДС
	|ПОМЕСТИТЬ втГрафикНачисленияУслугИзДокумента
	|ИЗ
	|	&ГрафикНачисленияУслуг КАК ДанныеГрафика
	|ГДЕ
	|	ДанныеГрафика.Дата >= НАЧАЛОПЕРИОДА(&Дата, ДЕНЬ)
	|; 
	|
	|ВЫБРАТЬ
	|	ДанныеГрафика.Дата КАК Дата,
	|	ДанныеГрафика.Проценты КАК Проценты
	|ПОМЕСТИТЬ втГрафикНачисленияПроцентовИзДокумента
	|ИЗ
	|	&ГрафикНачисленияПроцентов КАК ДанныеГрафика
	|ГДЕ
	|	ДанныеГрафика.Дата >= НАЧАЛОПЕРИОДА(&Дата, ДЕНЬ)
	|; 
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеГрафика.Дата КАК Дата,
	|	ДанныеГрафика.УслугаПоАренде КАК УслугаПоАренде,
	|	ДанныеГрафика.УслугаПоАрендеНДС КАК УслугаПоАрендеНДС,
	|	ДанныеГрафика.ОбеспечительныйПлатеж КАК ОбеспечительныйПлатеж,
	|	ДанныеГрафика.ОбеспечительныйПлатежНДС КАК ОбеспечительныйПлатежНДС,
	|	ДанныеГрафика.ВыкупнаяСтоимость + ДанныеГрафика.ВыкупнаяСтоимостьАванс КАК ВыкупнаяСтоимость,
	|	ДанныеГрафика.ВыкупнаяСтоимостьНДС КАК ВыкупнаяСтоимостьНДС
	|ИЗ
	|	РегистрСведений.ГрафикОплатУслугПоАренде КАК ДанныеГрафика
	|ГДЕ
	|	ДанныеГрафика.АктуальныеУсловияДоговора В
	|			(ВЫБРАТЬ
	|				Т.АктуальныеУсловияДоговора
	|			ИЗ
	|				втУсловияДоговоровАренды КАК Т)
	|	И ДанныеГрафика.Договор = &Договор
	|	И ДанныеГрафика.Активность
	|	И (ДанныеГрафика.Дата < НАЧАЛОПЕРИОДА(&Дата, ДЕНЬ))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеГрафика.Дата КАК Дата,
	|	ДанныеГрафика.УслугаПоАренде КАК УслугаПоАренде,
	|	ДанныеГрафика.УслугаПоАрендеНДС КАК УслугаПоАрендеНДС,
	|	ДанныеГрафика.ОбеспечительныйПлатеж КАК ОбеспечительныйПлатеж,
	|	ДанныеГрафика.ОбеспечительныйПлатежНДС КАК ОбеспечительныйПлатежНДС,
	|	ДанныеГрафика.ВыкупнаяСтоимость КАК ВыкупнаяСтоимость,
	|	ДанныеГрафика.ВыкупнаяСтоимостьНДС КАК ВыкупнаяСтоимостьНДС
	|ИЗ
	|	втГрафикОплатУслугИзДокумента КАК ДанныеГрафика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеГрафика.Дата КАК Дата,
	|	ДанныеГрафика.УслугаПоАренде КАК УслугаПоАренде,
	|	ДанныеГрафика.УслугаПоАрендеНДС КАК УслугаПоАрендеНДС,
	|	ДанныеГрафика.ОбеспечительныйПлатеж КАК ОбеспечительныйПлатеж,
	|	ДанныеГрафика.ОбеспечительныйПлатежНДС КАК ОбеспечительныйПлатежНДС
	|ИЗ
	|	РегистрСведений.ГрафикНачисленияУслугПоАренде КАК ДанныеГрафика
	|ГДЕ
	|	ДанныеГрафика.АктуальныеУсловияДоговора В
	|			(ВЫБРАТЬ
	|				Т.АктуальныеУсловияДоговора
	|			ИЗ
	|				втУсловияДоговоровАренды КАК Т)
	|	И ДанныеГрафика.Договор = &Договор
	|	И ДанныеГрафика.Активность
	|	И (ДанныеГрафика.УслугаПоАренде <> 0
	|			ИЛИ ДанныеГрафика.ОбеспечительныйПлатеж <> 0)
	|	И (ДанныеГрафика.Дата < НАЧАЛОПЕРИОДА(&Дата, ДЕНЬ))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеГрафика.Дата КАК Дата,
	|	ДанныеГрафика.УслугаПоАренде КАК УслугаПоАренде,
	|	ДанныеГрафика.УслугаПоАрендеНДС КАК УслугаПоАрендеНДС,
	|	ДанныеГрафика.ОбеспечительныйПлатеж КАК ОбеспечительныйПлатеж,
	|	ДанныеГрафика.ОбеспечительныйПлатежНДС КАК ОбеспечительныйПлатежНДС
	|ИЗ
	|	втГрафикНачисленияУслугИзДокумента КАК ДанныеГрафика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеГрафика.Дата КАК Дата,
	|	ДанныеГрафика.Проценты КАК Проценты
	|ИЗ
	|	РегистрСведений.ГрафикНачисленияПроцентовПоАренде КАК ДанныеГрафика
	|ГДЕ
	|	ДанныеГрафика.АктуальныеУсловияДоговора В
	|			(ВЫБРАТЬ
	|				Т.АктуальныеУсловияДоговора
	|			ИЗ
	|				втУсловияДоговоровАренды КАК Т)
	|	И ДанныеГрафика.Договор = &Договор
	|	И ДанныеГрафика.Активность
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеГрафика.Дата КАК Дата,
	|	ДанныеГрафика.Проценты КАК Проценты
	|ИЗ
	|	втГрафикНачисленияПроцентовИзДокумента КАК ДанныеГрафика";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("Договор", Объект.Договор);
	Запрос.УстановитьПараметр("Дата", Объект.Дата);
	Запрос.УстановитьПараметр("ГрафикОплатУслуг", ГрафикОплатУслуг);
	Запрос.УстановитьПараметр("ГрафикНачисленияУслуг", ГрафикНачисленияУслуг);
	Запрос.УстановитьПараметр("ГрафикНачисленияПроцентов", ГрафикНачисленияПроцентов);
	
	Результат = Запрос.ВыполнитьПакет();
	Если Результат[0].Пустой() Тогда
		Возврат;
	КонецЕсли;

	Объект.ГрафикОплатУслуг.Загрузить(Результат[Результат.ВГраница()-2].Выгрузить());
	Объект.ГрафикНачисленияУслуг.Загрузить(Результат[Результат.ВГраница()-1].Выгрузить());
	Объект.ГрафикНачисленияПроцентов.Загрузить(Результат[Результат.ВГраница()].Выгрузить());
	
КонецПроцедуры

// Значение изменения приведенной стоимости, при котором изменение не фиксируется в движениях
// и считается погрешностью округления.
// 
// Возвращаемое значение:
//  Число - Порог округления изменения приведенной стоимости
Функция ПорогОкругленияИзмененияПриведеннойСтоимости() Экспорт
	
	Возврат 0.015;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
