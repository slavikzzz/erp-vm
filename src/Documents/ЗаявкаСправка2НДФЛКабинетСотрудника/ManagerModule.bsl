#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ФизическоеЛицо)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Поля.Добавить("Дата");
	Поля.Добавить("Номер");
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	Шаблон = НСтр("ru = 'Запрос справки 2-НДФЛ %1 от %2';
					|en = 'Запрос справки 2-НДФЛ %1 от %2'");
	Представление = СтрШаблон(Шаблон, Данные.Номер, Данные.Дата);
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

// Возвращает описание состава документа
//
// Возвращаемое значение:
//  Структура - см. ЗарплатаКадрыСоставДокументов.НовоеОписаниеСоставаОбъекта.
Функция ОписаниеСоставаОбъекта() Экспорт
	
	ОписаниеСостава = ЗарплатаКадрыСоставДокументов.НовоеОписаниеСоставаОбъекта();
	ОписаниеСостава.ИспользоватьКраткийСостав = Ложь;
	ОписаниеСостава.ЗаполнятьТабличнуюЧастьФизическиеЛицаДокумента = Ложь;
	ОписаниеСостава.ЗаполнятьФизическиеЛицаПоСотрудникам = Ложь;
	
	ОписаниеХранения = ЗарплатаКадрыСоставДокументов.ОписаниеХраненияСотрудниковФизическихЛиц();
	ОписаниеХранения.ПолеСотрудник      = "Сотрудник";
	ОписаниеХранения.ПолеФизическоеЛицо = "ФизическоеЛицо";
	
	ОписаниеСостава.ОписаниеХраненияСотрудниковФизическихЛиц.Добавить(ОписаниеХранения);
	
	Возврат ОписаниеСостава;
	
	
КонецФункции

Функция ОписаниеДокументаДляЗаявки(СписокОбщихПолей) Экспорт
	
	СтрокаСвойств = "НалоговыйПериод,НачалоПериода,ОкончаниеПериода,СпособФормирования";
	СписокПолей = СтрШаблон("%1,%2", СписокОбщихПолей, СтрокаСвойств);
	
	Возврат Новый Структура(СписокПолей);

КонецФункции

Функция БизнесПроцессМенеджер() Экспорт

	Возврат БизнесПроцессы.ИсполнениеЗаявкиСотрудника;

КонецФункции

Функция НаименованиеЗадачи(ДокументОбъект) Экспорт
	
	ШаблонНаименования = НСтр("ru = 'Запрос справки 2-НДФЛ';
								|en = 'Запрос справки 2-НДФЛ'");
	
	Возврат СтрШаблон("%1: %2", ШаблонНаименования, Строка(ДокументОбъект.ФизическоеЛицо));

КонецФункции

Функция СодержаниеЗадачи(ДокументОбъект) Экспорт
	
	ШаблонНаименования = НСтр("ru = 'Обработать запрос на выдачу справки 2-НДФЛ';
								|en = 'Обработать запрос на выдачу справки 2-НДФЛ'");
	
	Шаблон = НСтр("ru = '%1
	|Организация: %2
	|Сотрудник: %3';
	|en = '%1
	|Организация: %2
	|Сотрудник: %3'");
	
	Возврат СтрШаблон(Шаблон, ШаблонНаименования, Строка(ДокументОбъект.Организация), Строка(ДокументОбъект.Сотрудник));
	
КонецФункции

Функция РольИсполнителяЗаявки() Экспорт

	Возврат Справочники.РолиИсполнителей.ОтветственныйЗаСправкиНДФЛ;

КонецФункции

Функция ЗаявкаГотоваДляАвтоматическогоВыполненияПослеПодписанияФайла(Заявка) Экспорт
	
	// Все документы должны иметь подписанный файл

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Заявка", Заявка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Заявки.Ссылка КАК Ссылка,
	|	ЕСТЬNULL(ПрисоединенныеФайлы.ПодписанЭП, ЛОЖЬ) КАК ПодписанЭП
	|ИЗ
	|	Документ.ЗаявкаСправка2НДФЛКабинетСотрудника КАК Заявки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявкаСправка2НДФЛКабинетСотрудника.ДокументыПоЗаявке КАК ЗаявкаСправки
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ДокументКадровогоЭДО КАК ДокументКадровогоЭДО
	|				ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СправкаНДФЛПрисоединенныеФайлы КАК ПрисоединенныеФайлы
	|				ПО ДокументКадровогоЭДО.ЭлектронныйДокумент = ПрисоединенныеФайлы.Ссылка
	|					И (НЕ ПрисоединенныеФайлы.ПометкаУдаления)
	|					И (ПрисоединенныеФайлы.ПодписанЭП)
	|			ПО ЗаявкаСправки.ДокументСсылка = ДокументКадровогоЭДО.ОснованиеДокумента
	|				И (НЕ ДокументКадровогоЭДО.ПометкаУдаления)
	|		ПО Заявки.Ссылка = ЗаявкаСправки.Ссылка
	|ГДЕ
	|	Заявки.Ссылка = &Заявка";
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Не Выборка.ПодписанЭП Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#КонецЕсли