#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Формирует временные таблицы данных документа.
// Используется для заполнения видов запасов.
//
// Возвращаемое значение:
//	МенеджерВременныхТаблиц - менеджер временных таблиц.
//
Функция ВременныеТаблицыДанныхДокумента() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	&Дата					КАК Дата,
	|	&Организация			КАК Организация,
	|	&Партнер				КАК Партнер,
	|	&Контрагент				КАК Контрагент,
	|	&Соглашение				КАК Соглашение,
	|	&Договор				КАК Договор,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК Валюта,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеНДС,
	|	&Склад					КАК Склад,
	|	&ХозяйственнаяОперация	КАК ХозяйственнаяОперация,
	|	ЛОЖЬ					КАК ЕстьСделкиВТабличнойЧасти,
	|	&Подразделение			КАК Подразделение,
	|	&Менеджер				КАК Менеджер,
	|	ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка) КАК Сделка,
	|	ВЫБОР
	//++ НЕ УТКА
	|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаДавальцу2_5)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПолуфабрикатДавальца)
	|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДавальцу2_5)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.МатериалДавальца)
	//-- НЕ УТКА
	|		КОГДА ИСТИНА
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ТоварНаХраненииСПравомПродажи)
	|	КОНЕЦ                                                      КАК ТипЗапасов
	|ПОМЕСТИТЬ ТаблицаДанныхДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки                КАК НомерСтроки,
	|	ТаблицаТоваров.Номенклатура               КАК Номенклатура,
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаТоваров.Характеристика             КАК Характеристика,
	|	ТаблицаТоваров.Назначение                 КАК Назначение,
	|	ТаблицаТоваров.Серия                      КАК Серия,
	|	ТаблицаТоваров.СтатусУказанияСерий        КАК СтатусУказанияСерий,
	|	ТаблицаТоваров.ДокументПоступления        КАК ДокументПоступления,
	|	ТаблицаТоваров.ЗаказКлиента               КАК ЗаказКлиента,
	|	ТаблицаТоваров.Количество                 КАК Количество,
	|	ВЫБОР
	|		КОГДА НЕ &ИспользоватьУчетПрослеживаемыхИмпортныхТоваров
	|				ИЛИ НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ) < &ДатаНачалаУчетаПрослеживаемыхИмпортныхТоваров
	|			ТОГДА 0
	|		ИНАЧЕ &ТекстПоляТаблицаТоваровКоличествоПоРНПТ_
	|	КОНЕЦ								      КАК КоличествоПоРНПТ,
	|	ТаблицаТоваров.Упаковка                   КАК Упаковка,
	|	ТаблицаТоваров.Сумма                      КАК Сумма,
	|	ТаблицаТоваров.Склад                      КАК Склад,
	|	&ТекстПоляТаблицаТоваровНомерГТД_	      КАК НомерГТД
	|ПОМЕСТИТЬ ВтТаблицаТоваров
	|ИЗ
	|	&ТаблицаТоваров КАК ТаблицаТоваров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки         КАК НомерСтроки,
	|	ТаблицаТоваров.Номенклатура        КАК Номенклатура,
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаТоваров.Характеристика      КАК Характеристика,
	|	ТаблицаТоваров.Серия			   КАК Серия,
	|	ТаблицаТоваров.СтатусУказанияСерий КАК СтатусУказанияСерий,
	|	ТаблицаТоваров.Назначение          КАК Назначение,
	|	ТаблицаТоваров.ДокументПоступления КАК ДокументПоступления,
	|	ТаблицаТоваров.ЗаказКлиента        КАК ЗаказКлиента,
	|	ТаблицаТоваров.Количество          КАК Количество,
	|	ТаблицаТоваров.КоличествоПоРНПТ    КАК КоличествоПоРНПТ,
	|	ТаблицаТоваров.Сумма               КАК Сумма,
	|	ТаблицаТоваров.Склад               КАК Склад,
	|	ИСТИНА                             КАК ПодбиратьВидыЗапасов,
	|	НЕОПРЕДЕЛЕНО                       КАК ДокументРеализации,
	|	ТаблицаТоваров.НомерГТД            КАК НомерГТД
	|ПОМЕСТИТЬ ТаблицаТоваров
	|ИЗ
	|	ВтТаблицаТоваров КАК ТаблицаТоваров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки                КАК НомерСтроки,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.ВидЗапасов                 КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.НомерГТД                   КАК НомерГТД,
	|	ТаблицаВидыЗапасов.ДокументПоступления        КАК ДокументПоступления,
	|	ТаблицаВидыЗапасов.ЗаказКлиента               КАК ЗаказКлиента,
	|	ТаблицаВидыЗапасов.Количество                 КАК Количество,
	|	ВЫБОР
	|		КОГДА НЕ &ИспользоватьУчетПрослеживаемыхИмпортныхТоваров
	|				ИЛИ НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ) < &ДатаНачалаУчетаПрослеживаемыхИмпортныхТоваров
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаВидыЗапасов.КоличествоПоРНПТ
	|	КОНЕЦ                                         КАК КоличествоПоРНПТ,
	|	ТаблицаВидыЗапасов.Сумма                      КАК Сумма
	|
	|ПОМЕСТИТЬ ИсходныеВидыЗапасов
	|ИЗ
	|	&ТаблицаВидыЗапасов КАК ТаблицаВидыЗапасов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки                КАК НомерСтроки,
	|	НЕОПРЕДЕЛЕНО                                  КАК ДокументРеализации,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Аналитика.Номенклатура                        КАК Номенклатура,
	|	Аналитика.Характеристика                      КАК Характеристика,
	|	Аналитика.Серия                               КАК Серия,
	|	ТаблицаВидыЗапасов.ВидЗапасов                 КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.НомерГТД                   КАК НомерГТД,
	|	ТаблицаВидыЗапасов.ДокументПоступления        КАК ДокументПоступления,
	|	ТаблицаВидыЗапасов.ЗаказКлиента               КАК ЗаказКлиента,
	|	ТаблицаВидыЗапасов.Количество                 КАК Количество,
	|	ТаблицаВидыЗапасов.КоличествоПоРНПТ           КАК КоличествоПоРНПТ,
	|	ТаблицаВидыЗапасов.Сумма                      КАК Сумма,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)      КАК СкладОтгрузки,
	|	Аналитика.МестоХранения                       КАК Склад,
	|	ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка) КАК Сделка,
	|	&ВидыЗапасовУказаныВручную                    КАК ВидыЗапасовУказаныВручную
	|
	|ПОМЕСТИТЬ ТаблицаВидыЗапасов
	|ИЗ
	|	ИсходныеВидыЗапасов КАК ТаблицаВидыЗапасов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|		ПО ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры = Аналитика.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаНоменклатуры
	|";
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ТаблицаТоваров = ?(ДополнительныеСвойства.Свойство("ТаблицыЗаполненияВидовЗапасовПриОбмене")
							И ДополнительныеСвойства.ТаблицыЗаполненияВидовЗапасовПриОбмене <> Неопределено
							И ДополнительныеСвойства.ТаблицыЗаполненияВидовЗапасовПриОбмене.Свойство("Товары"),
						ДополнительныеСвойства.ТаблицыЗаполненияВидовЗапасовПриОбмене.Товары,
						Товары);
	//++ НЕ УТКА
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаДавальцу2_5
	 Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДавальцу2_5 Тогда
		ТекущаяОперация = ХозяйственнаяОперация;
	Иначе
	//-- НЕ УТКА
		ТекущаяОперация = Перечисления.ХозяйственныеОперации.ОтгрузкаПринятыхСПравомПродажиТоваровСХранения;
	//++ НЕ УТКА
	КонецЕсли;
	//-- НЕ УТКА
	
	Запрос.УстановитьПараметр("Ссылка",						Ссылка);
	Запрос.УстановитьПараметр("Дата",						Дата);
	Запрос.УстановитьПараметр("Партнер",					Партнер);
	Запрос.УстановитьПараметр("Склад",						Склад);
	Запрос.УстановитьПараметр("Контрагент",					Контрагент);
	Запрос.УстановитьПараметр("Соглашение",					Соглашение);
	Запрос.УстановитьПараметр("Договор",					Договор);
	Запрос.УстановитьПараметр("Организация",				Организация);
	Запрос.УстановитьПараметр("Подразделение",				Подразделение);
	Запрос.УстановитьПараметр("ХозяйственнаяОперация",		ТекущаяОперация);
	Запрос.УстановитьПараметр("Менеджер",					Менеджер);
	Запрос.УстановитьПараметр("Валюта",						Валюта);
	Запрос.УстановитьПараметр("ТаблицаТоваров",				ТаблицаТоваров);
	Запрос.УстановитьПараметр("ТаблицаВидыЗапасов",			ВидыЗапасов);
	Запрос.УстановитьПараметр("ВидыЗапасовУказаныВручную",	ВидыЗапасовУказаныВручную);
	Запрос.УстановитьПараметр("ФормироватьВидыЗапасовПоПодразделениямМенеджерам",
								ПолучитьФункциональнуюОпцию("ФормироватьВидыЗапасовПоПодразделениямМенеджерам"));
	
	УчетПрослеживаемыхТоваровЛокализация.УстановитьПараметрыИспользованияУчетаПрослеживаемыхТоваров(Запрос);
	
	ЗапасыСервер.ДополнитьВременныеТаблицыОбязательнымиКолонками(Запрос);
	
	ОбщегоНазначенияУТ.ЗаменитьОтсутствующиеПоляТаблицыЗначенийВТекстеЗапроса(
		ТаблицаТоваров,
		Запрос.Текст,
		"&ТекстПоляТаблицаТоваровКоличествоПоРНПТ_",
		"ТаблицаТоваров",
		"КоличествоПоРНПТ",
		"ТаблицаТоваров.КоличествоПоРНПТ",
		"0");
	
	ОбщегоНазначенияУТ.ЗаменитьОтсутствующиеПоляТаблицыЗначенийВТекстеЗапроса(
		ТаблицаТоваров,
		Запрос.Текст,
		"&ТекстПоляТаблицаТоваровНомерГТД_",
		"ТаблицаТоваров",
		"НомерГТД",
		"ТаблицаТоваров.НомерГТД",
		"ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)");
	
	Запрос.Выполнить();
	
	Возврат Запрос.МенеджерВременныхТаблиц;
	
КонецФункции

// Формирует временную таблицу товаров с аналитикой обособленного учета.
//
// Параметры:
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - менеджер временных таблиц.
//
Процедура СформироватьВременнуюТаблицуТоваровИАналитики(МенеджерВременныхТаблиц) Экспорт
	
	Запрос                         = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры                  КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаТоваров.Номенклатура                                КАК Номенклатура,
	|	ТаблицаТоваров.Характеристика                              КАК Характеристика,
	|	ТаблицаТоваров.Назначение                                  КАК Назначение,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.СтатусУказанияСерий = 14
	|			ТОГДА ТаблицаТоваров.Серия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ                                                      КАК Серия,
	|	ТаблицаТоваров.Количество                                  КАК Количество,
	|	ТаблицаДанныхДокумента.Партнер                             КАК Партнер,
	|	ТаблицаДанныхДокумента.Соглашение                          КАК Соглашение,
	|	ТаблицаДанныхДокумента.Подразделение                       КАК Подразделение,
	|	ТаблицаТоваров.Склад                                       КАК Склад,
	|	ТаблицаДанныхДокумента.Менеджер                            КАК Менеджер,
	|	ТаблицаДанныхДокумента.Сделка                              КАК Сделка,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеНДС
	|
	|ПОМЕСТИТЬ ТаблицаТоваровИАналитики
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаДанныхДокумента КАК ТаблицаДанныхДокумента
	|		ПО (ИСТИНА)";
	
	Запрос.Выполнить();
	
КонецПроцедуры

// Заполняет условия закупок по умолчанию в документе отгрузки товаров с хранения.
//
// Параметры:
//	ПересчитатьЦены - Булево - Истина, если необходимо пересчитать цены в табличной части документа.
//	Операция        - Неопределено, ПеречислениеСсылка.ХозяйственныеОперации - 
//
Процедура ЗаполнитьУсловияЗакупокПоУмолчанию(ПересчитатьЦены = Истина, Операция = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(Соглашение) Тогда
		Соглашение = Справочники.СоглашенияСПоставщиками.ПустаяСсылка();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Партнер) Тогда
		
		//++ НЕ УТКА
		Если Операция = Перечисления.ХозяйственныеОперации.ПередачаДавальцу2_5
		 Или Операция = Перечисления.ХозяйственныеОперации.ВозвратДавальцу2_5 Тогда
			УсловияЗакупокПоУмолчанию = Неопределено;
		Иначе
		//-- НЕ УТКА
			ПараметрыОтбора = Новый Структура;
			ПараметрыОтбора.Вставить("ТолькоДействующее",                       Истина);
			ПараметрыОтбора.Вставить("УчитыватьГруппыСкладов",                  Истина);
			ПараметрыОтбора.Вставить("ИсключитьГруппыСкладовДоступныеВЗаказах", Истина);
			ПараметрыОтбора.Вставить("ВыбранноеСоглашение",                     Соглашение);
			ПараметрыОтбора.Вставить("ХозяйственныеОперации", Перечисления.ХозяйственныеОперации.ПриемНаХранениеСПравомПродажи);
		
			УсловияЗакупокПоУмолчанию = ЗакупкиСервер.ПолучитьУсловияЗакупокПоУмолчанию(Партнер, ПараметрыОтбора);
		//++ НЕ УТКА
		КонецЕсли;
		//-- НЕ УТКА
		
		Если УсловияЗакупокПоУмолчанию <> Неопределено Тогда
			
			Если Соглашение <> УсловияЗакупокПоУмолчанию.Соглашение
				И ЗначениеЗаполнено(УсловияЗакупокПоУмолчанию.Соглашение) Тогда
				
				Соглашение = УсловияЗакупокПоУмолчанию.Соглашение;
				ЗаполнитьУсловияЗакупок(УсловияЗакупокПоУмолчанию);
				
				Если ПересчитатьЦены
					И ЗначениеЗаполнено(Соглашение) Тогда
					
					ПараметрыЗаполнения = ЦеныПартнеровЗаполнениеСервер.НовыйПараметрыЗаполненияЗаполнитьЦены();
					ПараметрыЗаполнения.Вставить("ПоляЗаполнения", "Цена");
					ПараметрыЗаполнения.Вставить("Дата",           Дата);
					ПараметрыЗаполнения.Вставить("Валюта",         Валюта);
					ПараметрыЗаполнения.Вставить("Соглашение",     Соглашение);
					
					СтруктураДействий = Новый Структура;
					СтруктураДействий.Вставить("ПересчитатьСумму", "КоличествоУпаковок");
					
					ЦеныЗаполнены = ЦеныПартнеровЗаполнениеСервер.ЗаполнитьЦены(Товары, Неопределено, ПараметрыЗаполнения, СтруктураДействий);
					
				КонецЕсли;
				
			Иначе
				Соглашение = УсловияЗакупокПоУмолчанию.Соглашение;
				ПартнерыИКонтрагенты.ЗаполнитьКонтрагентаПартнераПоУмолчанию(Партнер, Контрагент);
			КонецЕсли;
			
		Иначе
			Соглашение = Неопределено;
			ПартнерыИКонтрагенты.ЗаполнитьКонтрагентаПартнераПоУмолчанию(Партнер, Контрагент);
		КонецЕсли;
		
		БанковскийСчетКонтрагента = ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетКонтрагентаПоУмолчанию(Контрагент,
			Неопределено, БанковскийСчетКонтрагента);
		
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеДоставкой") Тогда
		АдресДоставки = ФормированиеПечатныхФорм.ПолучитьАдресИзКонтактнойИнформации(Партнер);
	КонецЕсли;
	
	ПартнерыИКонтрагенты.ЗаполнитьКонтактноеЛицоПартнераПоУмолчанию(Партнер, КонтактноеЛицо);
	
КонецПроцедуры

// Заполняет условия закупок по соглашению в документе отгрузки товаров с хранения.
//
Процедура ЗаполнитьУсловияЗакупокПоСоглашению(ПересчитатьЦены = Истина) Экспорт
	
	УсловияЗакупок = ЗакупкиСервер.ПолучитьУсловияЗакупок(Соглашение);
	ЗаполнитьУсловияЗакупок(УсловияЗакупок);
	
	Если ПересчитатьЦены
		И ЗначениеЗаполнено(Соглашение) Тогда
		
		ПараметрыЗаполнения = ЦеныПартнеровЗаполнениеСервер.НовыйПараметрыЗаполненияЗаполнитьЦены();
		ПараметрыЗаполнения.Вставить("ПоляЗаполнения", "Цена");
		ПараметрыЗаполнения.Вставить("Дата",           Дата);
		ПараметрыЗаполнения.Вставить("Валюта",         Валюта);
		ПараметрыЗаполнения.Вставить("Соглашение",     Соглашение);
		
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить("ПересчитатьСумму", "КоличествоУпаковок");
		
		ЦеныПартнеровЗаполнениеСервер.ЗаполнитьЦены(Товары, Неопределено, ПараметрыЗаполнения, СтруктураДействий);
		
	КонецЕсли;
	
	БанковскийСчетКонтрагента = ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетКонтрагентаПоУмолчанию(Контрагент,
		Неопределено, БанковскийСчетКонтрагента);
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеДоставкой") Тогда
		АдресДоставки = ФормированиеПечатныхФорм.ПолучитьАдресИзКонтактнойИнформации(Партнер);
	КонецЕсли;
	
КонецПроцедуры

// Заполняет условия закупок в документе отгрузки товаров с хранения по заданным параметрам.
//
// Параметры:
//	УсловияЗакупок - Структура - структура для заполнения реквизитов документа.
//
Процедура ЗаполнитьУсловияЗакупок(Знач УсловияЗакупок) Экспорт
	
	Если УсловияЗакупок = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Валюта                  = УсловияЗакупок.Валюта;
	НаправлениеДеятельности = УсловияЗакупок.НаправлениеДеятельности;
	
	Если ЗначениеЗаполнено(УсловияЗакупок.Организация)
		И УсловияЗакупок.Организация <> Организация Тогда
		
		Организация         = УсловияЗакупок.Организация;
		
		СтруктураПараметров = ДенежныеСредстваСервер.ПараметрыЗаполненияБанковскогоСчетаОрганизацииПоУмолчанию();
		СтруктураПараметров.Организация             = Организация;
		СтруктураПараметров.НаправлениеДеятельности = НаправлениеДеятельности;
		
		БанковскийСчетОрганизации = ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетОрганизацииПоУмолчанию(
			СтруктураПараметров);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(УсловияЗакупок.Контрагент)
		И УсловияЗакупок.Контрагент <> Контрагент Тогда
		
		Контрагент = УсловияЗакупок.Контрагент;
		
	КонецЕсли;
	
	ПартнерыИКонтрагенты.ЗаполнитьКонтрагентаПартнераПоУмолчанию(Партнер, Контрагент);
	
	Если ЗначениеЗаполнено(УсловияЗакупок.Склад) Тогда
		Склад                   = УсловияЗакупок.Склад;
		СтруктураОтветственного = ЗакупкиСервер.ПолучитьОтветственногоПоСкладу(Склад, Менеджер);
		
		Если СтруктураОтветственного <> Неопределено Тогда
			Отпустил          = СтруктураОтветственного.Ответственный;
			ОтпустилДолжность = СтруктураОтветственного.ОтветственныйДолжность;
		КонецЕсли;
	КонецЕсли;
	
	ХозяйственнаяОперацияДоговора = УсловияЗакупок.ХозяйственнаяОперация;
	
	Если УсловияЗакупок.ИспользуютсяДоговорыКонтрагентов <> Неопределено
		И УсловияЗакупок.ИспользуютсяДоговорыКонтрагентов Тогда
		
		ДопПараметры = ЗакупкиСервер.ДополнительныеПараметрыОтбораДоговоров();
		ДопПараметры.ВалютаВзаиморасчетов = Валюта;
		Договор = ЗакупкиСервер.ПолучитьДоговорПоУмолчанию(ЭтотОбъект, ХозяйственнаяОперацияДоговора, ДопПараметры);
		
		ЗакупкиВызовСервера.ЗаполнитьБанковскиеСчетаПоДоговору(Договор, БанковскийСчетОрганизации,
			БанковскийСчетКонтрагента);
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетЗатратПоНаправлениямДеятельности") Тогда
			НаправленияДеятельностиСервер.ЗаполнитьНаправлениеПоУмолчанию(НаправлениеДеятельности, Соглашение, Договор);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает сумму документа.
//
// Возвращаемое значение:
//	Число - сумма документа.
//
Функция ПолучитьСуммуДокумента() Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Сумма        КАК Сумма
	|
	|ПОМЕСТИТЬ
	|	Товары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(СУММА(Товары.Сумма), 0) КАК Сумма
	|ИЗ
	|	Товары КАК Товары";
	
	Запрос.УстановитьПараметр("Товары", Товары.Выгрузить(, "Номенклатура, Сумма"));
	
	Выгрузка   = Запрос.Выполнить().Выгрузить();
	СуммаИтого = Выгрузка[0].Сумма;
	
	Возврат СуммаИтого;
	
КонецФункции

// Заполняет аналитики учета номенклатуры. Используется в отчете ОстаткиТоваровОрганизаций.
//
Процедура ЗаполнитьАналитикиУчетаНоменклатуры() Экспорт
	
	ЗаполнитьАналитикиУчетаНоменклатурыВТабличныхЧастяхТоваров();
	
КонецПроцедуры

// Инициализирует параметры заполнения видов запасов дополнительных свойств документа, используемых при записи документа
// в режиме 'Проведения' или 'Отмены проведения'.
//
// Параметры:
//	ДокументОбъект - ДокументОбъект.ОтгрузкаТоваровСХранения - документ, для которого выполняется инициализация параметров.
//	РежимЗаписи - РежимЗаписиДокумента - режим записи документа.
//
Процедура ИнициализироватьПараметрыЗаполненияВидовЗапасовДляПроведения(ДокументОбъект, РежимЗаписи = Неопределено) Экспорт
	
	ДокументОбъект.ДополнительныеСвойства.Вставить("ПараметрыЗаполненияВидовЗапасов", ПараметрыЗаполненияВидовЗапасов());
	
КонецПроцедуры

// Заполняет реквизиты, хранящие информацию о видах запасов и аналитиках учета номенклатуры в табличной части 'Товары'
// документа, а также заполняет табличную часть 'ВидыЗапасов'.
//
// Параметры:
//	Отказ - Булево - признак того, что не удалось заполнить данные.
//	ТаблицыДокумента - см. Документы.ОтгрузкаТоваровСХранения.КоллекцияТабличныхЧастейТоваров.
//
Процедура ЗаполнитьВидыЗапасовПриОбмене(Отказ, ТаблицыДокумента) Экспорт
	
	ЗаполнитьАналитикиУчетаНоменклатурыВТабличныхЧастяхТоваров();
	
	Если ТаблицыДокумента <> Неопределено Тогда
		ЗаполнитьАналитикиУчетаНоменклатурыВТабличныхЧастяхТоваров(ТаблицыДокумента);
		ДополнительныеСвойства.Вставить("ТаблицыЗаполненияВидовЗапасовПриОбмене", ТаблицыДокумента);
	Иначе
		ИмяПараметра = "ТаблицыДокумента";
		
		ТекстИсключения = НСтр("ru = 'Для заполнения видов запасов не передан параметр ""%1"".';
								|en = 'The ""%1"" parameter is not transferred to fill in the inventory owner attributes.'");
		ТекстИсключения = СтрШаблон(ТекстИсключения, ИмяПараметра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	ЗаполнитьВидыЗапасов(Отказ);
	ДополнительныеСвойства.Удалить("ТаблицыЗаполненияВидовЗапасовПриОбмене");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("Структура") Тогда
		
		Если ДанныеЗаполнения.Свойство("АктОРасхождениях")
			И ДанныеЗаполнения.Свойство("ОснованиеАкта") Тогда
			
			Если ТипЗнч(ДанныеЗаполнения.ОснованиеАкта) = Тип("ДокументСсылка.ОтгрузкаТоваровСХранения") Тогда
				ЗаполнитьНаОснованииАктОРасхожденияхПоОтгрузкеТоваровСХранения(ДанныеЗаполнения);
			ИначеЕсли ТипЗнч(ДанныеЗаполнения.ОснованиеАкта) = Тип("ДокументСсылка.ПриемкаТоваровНаХранение") Тогда
				ЗаполнитьНаОснованииАктОРасхожденияхПоПриемкеТоваровНаХранение(ДанныеЗаполнения);
			КонецЕсли;
			
		//++ НЕ УТКА
		ИначеЕсли ДанныеЗаполнения.Свойство("ДокументОснование") Тогда
			
			ТипОснование = ТипЗнч(ДанныеЗаполнения.ДокументОснование);
			
			Если ТипОснование = Тип("ДокументСсылка.ЗаказДавальца2_5") Тогда
				
				ЗаполнитьНаОснованииЗаказДавальца2_5(ДанныеЗаполнения);
				
			ИначеЕсли ТипОснование = Тип("Массив") Тогда
				
				Если ТипЗнч(ДанныеЗаполнения.ДокументОснование[0]) = Тип("ДокументСсылка.ЗаказДавальца2_5") Тогда
					
					РеквизитыШапки = Неопределено;
					ДанныеЗаполнения.Свойство("РеквизитыШапки", РеквизитыШапки);
					
					ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыШапки);
					Если Не РеквизитыШапки.Свойство("Валюта") Тогда
						РеквизитыШапки.Свойство("ВалютаВзаиморасчетов", Валюта);
					КонецЕсли;
					
					СкладОтгрузки = Неопределено;
					ДанныеЗаполнения.Свойство("СкладОтгрузки", СкладОтгрузки);
					
					ПараметрыОформления = Неопределено;
					ДанныеЗаполнения.Свойство("ПараметрыОформления", ПараметрыОформления);
					
					ПараметрыЗаполнения = ?(ЗначениеЗаполнено(ПараметрыОформления),
												Новый Структура("ПараметрыОформления", ПараметрыОформления),
												Неопределено);
					
					ДанныеОтбора = Новый Структура("Ссылка, Партнер, Контрагент, Договор, Организация, Валюта, Сделка,
												   |НаправлениеДеятельности");
					
					ЗаполнитьЗначенияСвойств(ДанныеОтбора, ЭтотОбъект);
					
					Если РеквизитыШапки.Свойство("ТоварыОтгрузки")
					   И ЗначениеЗаполнено(РеквизитыШапки.ТоварыОтгрузки) Тогда
						ДанныеОтбора.Вставить("ТоварыОтгрузки", РеквизитыШапки.ТоварыОтгрузки);
					КонецЕсли;
					
					Документы.ОтгрузкаТоваровСХранения.ЗаполнитьПоОстаткамЗаказов(
						ДанныеОтбора, Товары, СкладОтгрузки, ДанныеЗаполнения.ДокументОснование, ПараметрыЗаполнения);
					
					Если ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыДавальцев2_5") Тогда
					
						Заказы = Новый Массив;
						ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Заказы, Товары.ВыгрузитьКолонку("ЗаказКлиента"), Истина);
						
						КоличествоЗаказов = Заказы.Количество();
						
						ПоЗаказу = КоличествоЗаказов > 0;
						Если КоличествоЗаказов = 1 Тогда
							Основание = Заказы[0];
						КонецЕсли;
					
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
		//-- НЕ УТКА
			
		Иначе
			ЗаполнитьДокументПоОтбору(ДанныеЗаполнения);
		КонецЕсли;
		
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ПриемкаТоваровНаХранение") Тогда
		ЗаполнитьДокументНаОснованииПриемкиТоваровНаХранение(ДанныеЗаполнения);
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ЗаказКлиента") Тогда
		ЗаполнитьДокументПоЗаказуКлиента(ДанныеЗаполнения);
	//++ НЕ УТКА
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ОтчетДавальцу2_5") Тогда
		ЗаполнитьДокументНаОснованииОтчетДавальцу2_5(ДанныеЗаполнения);	
	//-- НЕ УТКА
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка." + Метаданные().Имя) Тогда
		ИсправлениеДокументов.ЗаполнитьИсправление(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли;
	
	ОтгрузкаТоваровСХраненияЛокализация.ОбработкаЗаполнения(ЭтотОбъект, ДанныеЗаполнения, СтандартнаяОбработка);
	
	Автор = Пользователи.ТекущийПользователь();
	ОтветственныеЛицаСервер.ЗаполнитьМенеджера(ЭтотОбъект, Ложь);
	ЗаполнениеОбъектовПоСтатистике.ЗаполнитьРеквизитыОбъекта(ЭтотОбъект, ДанныеЗаполнения);
	
	ИнициализироватьДокумент(ДанныеЗаполнения);
	
	ДополнительныеСвойства.Вставить("НеобходимостьЗаполненияСчетаПриФОИспользоватьНесколькоСчетовЛожь", Ложь);
	
	СкладГруппа = Справочники.Склады.ЭтоГруппаИСкладыИспользуютсяВТЧДокументовЗакупки(Склад);
	СкладыСервер.ЗаполнитьСкладыВТабличнойЧасти(Склад, СкладГруппа, Товары, Ложь);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	ЗатратыСервер.ПроверитьИспользованиеПартионногоУчета22(ЭтотОбъект, Дата, Отказ);
	
	ПараметрыУказанияСерий        = НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект,
		Документы.ОтгрузкаТоваровСХранения);
	
	НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект, МассивНепроверяемыхРеквизитов, Отказ);
	НоменклатураСервер.ПроверитьЗаполнениеСерий(ЭтотОбъект, ПараметрыУказанияСерий, Отказ,
		МассивНепроверяемыхРеквизитов);
	
	НоменклатураСервер.ПроверитьЗаполнениеКоличества(ЭтотОбъект, ПроверяемыеРеквизиты, Отказ);
	
	ДоставкаТоваров.ПроверитьЗаполнениеРеквизитовДоставки(ЭтотОбъект, МассивНепроверяемыхРеквизитов, Отказ);
	
	Если ПолучитьФункциональнуюОпцию("ФормироватьВидыЗапасовПоПодразделениямМенеджерам") Тогда
		ПроверяемыеРеквизиты.Добавить("Подразделение");
	КонецЕсли;
	
	//++ НЕ УТКА
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаДавальцу2_5
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДавальцу2_5 Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("Партнер");
		МассивНепроверяемыхРеквизитов.Добавить("Валюта");
		
		Если Не ЗначениеЗаполнено(Партнер) Тогда
			ТекстОшибки = НСтр("ru = 'Поле ""Давалец"" не заполнено';
								|en = 'The ""Material provider"" field is not filled'");
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ЭтотОбъект, "Партнер",, Отказ);
		КонецЕсли;
		
		НайденныеСтроки = Товары.НайтиСтроки(Новый Структура("Назначение", Справочники.Назначения.ПустаяСсылка()));
		Если НайденныеСтроки.Количество() > 0 Тогда
			
			ТипыНоменклатуры = Товары.ВыгрузитьКолонку("Номенклатура");
			ТипыНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(ТипыНоменклатуры, "ТипНоменклатуры");
			
			ТекстОшибки = НСтр("ru = 'В строке %1 списка ""Товары"" не заполнено поле ""Назначение""';
								|en = 'The ""Assignment"" field in line %1 of the ""Goods"" list is not filled'");
			
			Для каждого СтрокаТаблицы Из НайденныеСтроки Цикл
				
				Если ТипыНоменклатуры[СтрокаТаблицы.Номенклатура] = Перечисления.ТипыНоменклатуры.МногооборотнаяТара Тогда
					Продолжить;
				КонецЕсли;
				
				ОбщегоНазначения.СообщитьПользователю(
					СтрШаблон(ТекстОшибки, СтрокаТаблицы.НомерСтроки),
					ЭтотОбъект,
					ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", СтрокаТаблицы.НомерСтроки, "Назначение"),,
					Отказ);
					
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	//-- НЕ УТКА
	
	ИсправлениеДокументов.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	Если Не Отказ
		И ОбщегоНазначенияУТ.ПроверитьЗаполнениеРеквизитовОбъекта(ЭтотОбъект, ПроверяемыеРеквизиты) Тогда
		
		Отказ = Истина;
		
	КонецЕсли;
	
	ПроверитьСоответствиеДоговораТовара(Отказ);
	
	//++ НЕ УТКА
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаДавальцу2_5 Тогда
		ПродажиСервер.ПроверитьЗапретОтгрузки(Партнер, Отказ);
	Иначе
	//-- НЕ УТКА
		ЗакупкиСервер.ПроверитьКорректностьЗаполненияДокументаЗакупки(ЭтотОбъект, Отказ);
		ЗакупкиСервер.ПроверитьКорректностьВозвращаемыхТоваров(ЭтотОбъект, Отказ);
	//++ НЕ УТКА
	КонецЕсли;
	//-- НЕ УТКА
	
	ОтгрузкаТоваровСХраненияЛокализация.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ИсправлениеДокументов.ПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
	ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	СуммаДокумента         = ПолучитьСуммуДокумента();
	ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, Документы.ОтгрузкаТоваровСХранения);
	
	ПараметрыОкругления = Неопределено;
	//++ НЕ УТКА
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаДавальцу2_5
	 Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДавальцу2_5 Тогда
	 	
	 	ПараметрыОкругления = НоменклатураСервер.ПараметрыПроверкиЗаполненияКоличества();
		ПараметрыОкругления.ДополнительныеПоля.Вставить("Склад", "Товары.Склад");
		ПараметрыОкругления.УсловиеОтбораСтрокПоДополнительнымПолям =
			ПроизводствоСервер.УсловиеОтбораСтрокДляОкругления("Товары", "Склад");
		
	КонецЕсли;
	//-- НЕ УТКА
	
	НоменклатураСервер.ОкруглитьКоличествоШтучныхТоваров(ЭтотОбъект, РежимЗаписи, ПараметрыОкругления);
	
	НоменклатураСервер.ОчиститьНеиспользуемыеСерии(ЭтотОбъект, ПараметрыУказанияСерий);
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		ЗаполнитьАналитикиУчетаНоменклатурыВТабличныхЧастяхТоваров();
		ЗаполнитьВидыЗапасов(Отказ);
		
	КонецЕсли;
	
	//++ НЕ УТКА
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаДавальцу2_5
	   И ПоЗаказу И ЗначениеЗаполнено(Основание) Тогда
		
		Для Каждого ТекСтрока Из Товары Цикл
			
			Если Не ЗначениеЗаполнено(ТекСтрока.ЗаказКлиента) Тогда
				ТекСтрока.ЗаказКлиента = Основание;
			КонецЕсли;
			
		КонецЦикла;
	
	КонецЕсли;
	//-- НЕ УТКА
	
	ОбщегоНазначенияУТ.ЗаполнитьИдентификаторыДокумента(ЭтотОбъект, "Товары,ВидыЗапасов");
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		НоменклатураПартнеровСервер.ЗаполнитьПустоеСопоставлениеВНоменклатуреПартнераПоНоменклатуреИБ(Товары, Отказ);
	КонецЕсли;
	
	Если ЭтоНовый()
		И Не ЗначениеЗаполнено(Номер) Тогда
		
		УстановитьНовыйНомер();
		
	КонецЕсли;
	
	ОтгрузкаТоваровСХраненияЛокализация.ПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ИнициализироватьПараметрыЗаполненияВидовЗапасовДляПроведения(ЭтотОбъект);
	
	ПроведениеДокументов.ОбработкаПроведенияДокумента(ЭтотОбъект, Отказ);
	
	ДоставкаТоваров.ОтразитьСостояниеДоставки(Ссылка, Отказ);
	
	ОтгрузкаТоваровСХраненияЛокализация.ОбработкаПроведения(ЭтотОбъект, Отказ, РежимПроведения);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ИнициализироватьПараметрыЗаполненияВидовЗапасовДляПроведения(ЭтотОбъект);
	
	ПроведениеДокументов.ОбработкаУдаленияПроведенияДокумента(ЭтотОбъект, Отказ);
	
	ДоставкаТоваров.ОтразитьСостояниеДоставки(Ссылка, Отказ, Истина);
	
	ОтгрузкаТоваровСХраненияЛокализация.ОбработкаУдаленияПроведения(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ПриЗаписиДокумента(ЭтотОбъект, Отказ);
	
	ОтгрузкаТоваровСХраненияЛокализация.ПриЗаписи(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ИсправлениеДокументов.ПриКопировании(ЭтотОбъект, ОбъектКопирования);

	ВидыЗапасовУказаныВручную = Ложь;
	СостояниеЗаполненияМногооборотнойТары = Перечисления.СостоянияЗаполненияМногооборотнойТары.ПустаяСсылка();
	
	Для Каждого СтрокаТЧ Из Товары Цикл
		СтрокаТЧ.ИдентификаторСтроки = "";
	КонецЦикла;
	
	ИнициализироватьДокумент();
	
	Серии.Очистить();
	ВидыЗапасов.Очистить();
	
	ОбщегоНазначенияУТ.ОчиститьИдентификаторыДокумента(ЭтотОбъект, "Товары,ВидыЗапасов");
	
	ОтгрузкаТоваровСХраненияЛокализация.ПриКопировании(ЭтотОбъект, ОбъектКопирования);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьСоответствиеДоговораТовара(Отказ)
	
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоставкаПодПринципала Тогда
		
		Для Каждого СтрокаТовары Из Товары Цикл
			
			Если ЗначениеЗаполнено(СтрокаТовары.Назначение) Тогда
				ДоговорТовара = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаТовары.Назначение, "Договор");
				Если Договор <> ДоговорТовара Тогда
					Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Договор назначения в строке %1 списка ""Товары"" не совпадает с договором документа.';
							|en = 'The assignment contract in line %1 of the ""Goods"" list does not match the document contract.'"),
						Товары.Индекс(СтрокаТовары) + 1);
						
					ОбщегоНазначения.СообщитьПользователю(
						Текст,
						ЭтотОбъект,
						"Товары[" + Товары.Индекс(СтрокаТовары) + "].Назначение",
						,
						Отказ);
				КонецЕсли;
				
			КонецЕсли;
				
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#Область ИнициализацияИЗаполнение

Процедура ЗаполнитьНаОснованииАктОРасхожденияхПоОтгрузкеТоваровСХранения(Знач ДанныеЗаполнения)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	АктОРасхожденияхПослеОтгрузки.Ссылка                             КАК АктОРасхождениях,
	|	ОтгрузкаТоваровСХранения.Ссылка                                  КАК Ссылка,
	|	ОтгрузкаТоваровСХранения.Партнер                                 КАК Партнер,
	|	ОтгрузкаТоваровСХранения.Контрагент                              КАК Контрагент,
	|	ОтгрузкаТоваровСХранения.Договор                                 КАК Договор,
	|	ОтгрузкаТоваровСХранения.Соглашение                              КАК Соглашение,
	|	ОтгрузкаТоваровСХранения.Организация                             КАК Организация,
	|	ОтгрузкаТоваровСХранения.Подразделение                           КАК Подразделение,
	|	ОтгрузкаТоваровСХранения.Склад                                   КАК Склад,
	|	ОтгрузкаТоваровСХранения.Руководитель                            КАК Руководитель,
	|	ОтгрузкаТоваровСХранения.ГлавныйБухгалтер                        КАК ГлавныйБухгалтер,
	|	ОтгрузкаТоваровСХранения.КонтактноеЛицо                          КАК КонтактноеЛицо,
	|	ОтгрузкаТоваровСХранения.БанковскийСчетОрганизации               КАК БанковскийСчетОрганизации,
	|	ОтгрузкаТоваровСХранения.БанковскийСчетКонтрагента               КАК БанковскийСчетКонтрагента,
	|	ОтгрузкаТоваровСХранения.ХозяйственнаяОперация                   КАК ХозяйственнаяОперация,
	|	ОтгрузкаТоваровСХранения.Сделка                                  КАК Сделка,
	|	ОтгрузкаТоваровСХранения.Валюта                                  КАК Валюта,
	|	НЕ ОтгрузкаТоваровСХранения.Проведен                             КАК ЕстьОшибкиПроведен,
	|	НЕ АктОРасхожденияхПослеОтгрузки.Проведен                        КАК ЕстьОшибкиПроведенАктОРасхождениях,
	|	АктОРасхожденияхПослеОтгрузки.Статус                             КАК СтатусАктаОРасхождениях,
	|	НЕ АктОРасхожденияхПослеОтгрузки.Статус В (
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.Отработано),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.КВыполнению)) КАК ЕстьОшибкиСтатусАкт
	|ИЗ
	|	Документ.ОтгрузкаТоваровСХранения КАК ОтгрузкаТоваровСХранения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АктОРасхожденияхПослеОтгрузки КАК АктОРасхожденияхПослеОтгрузки
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ОтгрузкаТоваровСХранения.Ссылка        = &ДокументОснование
	|	И АктОРасхожденияхПослеОтгрузки.Ссылка = &АктОРасхождениях
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АктОРасхожденияхПослеОтгрузкиТовары.Номенклатура                          КАК Номенклатура,
	|	АктОРасхожденияхПослеОтгрузкиТовары.Характеристика                        КАК Характеристика,
	|	АктОРасхожденияхПослеОтгрузкиТовары.Назначение                            КАК Назначение,
	|	АктОРасхожденияхПослеОтгрузкиТовары.ЗаказКлиента                          КАК ЗаказКлиента,
	|	АктОРасхожденияхПослеОтгрузкиТовары.КодСтроки                             КАК КодСтроки,
	|	АктОРасхожденияхПослеОтгрузкиТовары.Упаковка                              КАК Упаковка,
	|	АктОРасхожденияхПослеОтгрузкиТовары.Серия                                 КАК Серия,
	|	СУММА(АктОРасхожденияхПослеОтгрузкиТовары.КоличествоУпаковокПоДокументу -
	|		АктОРасхожденияхПослеОтгрузкиТовары.КоличествоУпаковок)               КАК КоличествоУпаковок,
	|	СУММА(АктОРасхожденияхПослеОтгрузкиТовары.КоличествоПоДокументу -
	|		АктОРасхожденияхПослеОтгрузкиТовары.Количество)                       КАК Количество,
	|	АктОРасхожденияхПослеОтгрузкиТовары.Цена                                  КАК Цена,
	|	АктОРасхожденияхПослеОтгрузкиТовары.Склад                                 КАК Склад,
	|	СУММА(АктОРасхожденияхПослеОтгрузкиТовары.СуммаПоДокументу -
	|		АктОРасхожденияхПослеОтгрузкиТовары.Сумма)                            КАК Сумма,
	|	АктОРасхожденияхПослеОтгрузкиТовары.ДокументПоступления                   КАК ДокументПоступления
	|ИЗ
	|	Документ.АктОРасхожденияхПослеОтгрузки.Товары КАК АктОРасхожденияхПослеОтгрузкиТовары
	|ГДЕ
	|	АктОРасхожденияхПослеОтгрузкиТовары.Ссылка = &АктОРасхождениях
	|	И АктОРасхожденияхПослеОтгрузкиТовары.Действие = ЗНАЧЕНИЕ(
	|		Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ТребуетсяДопоставка)
	|	И АктОРасхожденияхПослеОтгрузкиТовары.Реализация = &ДокументОснование
	|	И (АктОРасхожденияхПослеОтгрузкиТовары.КоличествоУпаковокПоДокументу -
	|		АктОРасхожденияхПослеОтгрузкиТовары.КоличествоУпаковок) > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	АктОРасхожденияхПослеОтгрузкиТовары.Номенклатура,
	|	АктОРасхожденияхПослеОтгрузкиТовары.Характеристика,
	|	АктОРасхожденияхПослеОтгрузкиТовары.Назначение,
	|	АктОРасхожденияхПослеОтгрузкиТовары.ЗаказКлиента,
	|	АктОРасхожденияхПослеОтгрузкиТовары.КодСтроки,
	|	АктОРасхожденияхПослеОтгрузкиТовары.Упаковка,
	|	АктОРасхожденияхПослеОтгрузкиТовары.Серия,
	|	АктОРасхожденияхПослеОтгрузкиТовары.Цена,
	|	АктОРасхожденияхПослеОтгрузкиТовары.Склад,
	|	АктОРасхожденияхПослеОтгрузкиТовары.ДокументПоступления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АктОРасхожденияхПослеОтгрузкиСерии.Номенклатура                  КАК Номенклатура,
	|	АктОРасхожденияхПослеОтгрузкиСерии.Характеристика                КАК Характеристика,
	|	АктОРасхожденияхПослеОтгрузкиСерии.Назначение                    КАК Назначение,
	|	АктОРасхожденияхПослеОтгрузкиСерии.Серия                         КАК Серия,
	|	СУММА(АктОРасхожденияхПослеОтгрузкиСерии.КоличествоПоДокументу -
	|		АктОРасхожденияхПослеОтгрузкиСерии.Количество)               КАК Количество,
	|	АктОРасхожденияхПослеОтгрузкиСерии.Склад                         КАК Склад
	|ИЗ
	|	Документ.АктОРасхожденияхПослеОтгрузки.Серии КАК АктОРасхожденияхПослеОтгрузкиСерии
	|ГДЕ
	|	АктОРасхожденияхПослеОтгрузкиСерии.Ссылка = &АктОРасхождениях
	|	И АктОРасхожденияхПослеОтгрузкиСерии.Действие = ЗНАЧЕНИЕ(
	|		Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ТребуетсяДопоставка)
	|	И АктОРасхожденияхПослеОтгрузкиСерии.Реализация = &ДокументОснование
	|	И АктОРасхожденияхПослеОтгрузкиСерии.КоличествоПоДокументу - АктОРасхожденияхПослеОтгрузкиСерии.Количество > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	АктОРасхожденияхПослеОтгрузкиСерии.Номенклатура,
	|	АктОРасхожденияхПослеОтгрузкиСерии.Характеристика,
	|	АктОРасхожденияхПослеОтгрузкиСерии.Назначение,
	|	АктОРасхожденияхПослеОтгрузкиСерии.Серия,
	|	АктОРасхожденияхПослеОтгрузкиСерии.Склад";
	
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения.ОснованиеАкта);
	Запрос.УстановитьПараметр("АктОРасхождениях",  ДанныеЗаполнения.АктОРасхождениях);
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	ВыборкаШапка  = ПакетЗапросов[0].Выбрать();
	
	ВыборкаШапка.Следующий();
	
	// По документу отгрузки товаров с хранения.
	ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОсновании(ВыборкаШапка.Ссылка, Неопределено,
		ВыборкаШапка.ЕстьОшибкиПроведен);
	
	// По акту о расхождениях после отгрузки.
	ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОсновании(ВыборкаШапка.АктОРасхождениях,
		ВыборкаШапка.СтатусАктаОРасхождениях,
		ВыборкаШапка.ЕстьОшибкиПроведенАктОРасхождениях,
		ВыборкаШапка.ЕстьОшибкиСтатусАкт,
		РасхожденияСервер.МассивДопустимыхСтатусовАктовОРасхожденияхПриСозданииНаОсновании());
	
	ДовозвратПоВозврату = ДанныеЗаполнения.ОснованиеАкта;
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаШапка);
	
	ТаблицаТоваров = ПакетЗапросов[1].Выгрузить();
	
	МассивЗаказовКлиента = Новый Массив();
	
	Для Каждого ТекСтрока Из ТаблицаТоваров Цикл
		НоваяСтрока = Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,ТекСтрока);
		НоваяСтрока.Сумма = НоваяСтрока.Цена * НоваяСтрока.КоличествоУпаковок;
		Если МассивЗаказовКлиента.Найти(НоваяСтрока.ЗаказКлиента) = Неопределено Тогда
			МассивЗаказовКлиента.Добавить(НоваяСтрока.ЗаказКлиента);
		КонецЕсли;
	КонецЦикла;
	
	Серии.Загрузить(ПакетЗапросов[2].Выгрузить());
	
	Если МассивЗаказовКлиента.Количество() = 1 Тогда
		ПоЗаказу = Истина;
		Основание = МассивЗаказовКлиента[0];
	ИначеЕсли МассивЗаказовКлиента.Количество() > 1 Тогда
		ПоЗаказу = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьНаОснованииАктОРасхожденияхПоПриемкеТоваровНаХранение(Знач ДанныеЗаполнения)
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	АктОРасхожденияхПослеПриемки.Ссылка                              КАК АктОРасхождениях,
	|	АктОРасхожденияхПослеПриемки.Партнер                             КАК Партнер,
	|	АктОРасхожденияхПослеПриемки.Контрагент                          КАК Контрагент,
	|	АктОРасхожденияхПослеПриемки.Соглашение                          КАК Соглашение,
	|	АктОРасхожденияхПослеПриемки.Договор                             КАК Договор,
	|	АктОРасхожденияхПослеПриемки.Организация                         КАК Организация,
	|	АктОРасхожденияхПослеПриемки.Подразделение                       КАК Подразделение,
	|	&Склад                                                           КАК Склад,
	|	&АктОРасхождениях                                                КАК Основание,
	|	АктОРасхожденияхПослеПриемки.Руководитель                        КАК Руководитель,
	|	АктОРасхожденияхПослеПриемки.ГлавныйБухгалтер                    КАК ГлавныйБухгалтер,
	|	АктОРасхожденияхПослеПриемки.ХозяйственнаяОперация               КАК ХозяйственнаяОперация,
	|	АктОРасхожденияхПослеПриемки.Валюта                              КАК Валюта,
	|	АктОРасхожденияхПослеПриемки.Менеджер                            КАК Менеджер,
	|	НЕ АктОРасхожденияхПослеПриемки.Проведен                         КАК ЕстьОшибкиПроведенАктОРасхождениях,
	|	АктОРасхожденияхПослеПриемки.Статус                              КАК СтатусАктаОРасхождениях,
	|	НЕ АктОРасхожденияхПослеПриемки.Статус В (
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.Отработано),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.КВыполнению)) КАК ЕстьОшибкиСтатусАкт
	|ИЗ
	|	Документ.АктОРасхожденияхПослеПриемки КАК АктОРасхожденияхПослеПриемки
	|ГДЕ
	|	АктОРасхожденияхПослеПриемки.Ссылка = &АктОРасхождениях
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АктОРасхожденияхПослеПриемкиТовары.Номенклатура                       КАК Номенклатура,
	|	АктОРасхожденияхПослеПриемкиТовары.Характеристика                     КАК Характеристика,
	|	АктОРасхожденияхПослеПриемкиТовары.Упаковка                           КАК Упаковка,
	|	АктОРасхожденияхПослеПриемкиТовары.Назначение                         КАК Назначение,
	|	АктОРасхожденияхПослеПриемкиТовары.Серия                              КАК Серия,
	|	АктОРасхожденияхПослеПриемкиТовары.Цена                               КАК Цена,
	|	АктОРасхожденияхПослеПриемкиТовары.Склад                              КАК Склад,
	|	СУММА(АктОРасхожденияхПослеПриемкиТовары.КоличествоУпаковок -
	|		АктОРасхожденияхПослеПриемкиТовары.КоличествоУпаковокПоДокументу) КАК КоличествоУпаковок,
	|	СУММА(АктОРасхожденияхПослеПриемкиТовары.Количество -
	|		АктОРасхожденияхПослеПриемкиТовары.КоличествоПоДокументу)         КАК Количество,
	|	СУММА(АктОРасхожденияхПослеПриемкиТовары.Сумма -
	|		АктОРасхожденияхПослеПриемкиТовары.СуммаПоДокументу)              КАК Сумма,
	|	АктОРасхожденияхПослеПриемкиТовары.ДокументОснование                  КАК ДокументПоступления
	|ИЗ
	|	Документ.АктОРасхожденияхПослеПриемки.Товары КАК АктОРасхожденияхПослеПриемкиТовары
	|ГДЕ
	|	АктОРасхожденияхПослеПриемкиТовары.Ссылка = &АктОРасхождениях
	|	И АктОРасхожденияхПослеПриемкиТовары.Действие = ЗНАЧЕНИЕ(
	|		Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОформитьПерепоставленноеИВернуть)
	|	И АктОРасхожденияхПослеПриемкиТовары.Склад = &Склад
	|	И (АктОРасхожденияхПослеПриемкиТовары.КоличествоУпаковок -
	|		АктОРасхожденияхПослеПриемкиТовары.КоличествоУпаковокПоДокументу) > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	АктОРасхожденияхПослеПриемкиТовары.Номенклатура,
	|	АктОРасхожденияхПослеПриемкиТовары.Характеристика,
	|	АктОРасхожденияхПослеПриемкиТовары.Упаковка,
	|	АктОРасхожденияхПослеПриемкиТовары.Назначение,
	|	АктОРасхожденияхПослеПриемкиТовары.Серия,
	|	АктОРасхожденияхПослеПриемкиТовары.Цена,
	|	АктОРасхожденияхПослеПриемкиТовары.Склад,
	|	АктОРасхожденияхПослеПриемкиТовары.ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АктОРасхожденияхПослеПриемкиСерии.Номенклатура               КАК Номенклатура,
	|	АктОРасхожденияхПослеПриемкиСерии.Характеристика             КАК Характеристика,
	|	АктОРасхожденияхПослеПриемкиСерии.Назначение                 КАК Назначение,
	|	АктОРасхожденияхПослеПриемкиСерии.Серия                      КАК Серия,
	|	АктОРасхожденияхПослеПриемкиСерии.Склад                      КАК Склад,
	|	СУММА(АктОРасхожденияхПослеПриемкиСерии.Количество -
	|		АктОРасхожденияхПослеПриемкиСерии.КоличествоПоДокументу) КАК Количество
	|ИЗ
	|	Документ.АктОРасхожденияхПослеПриемки.Серии КАК АктОРасхожденияхПослеПриемкиСерии
	|ГДЕ
	|	АктОРасхожденияхПослеПриемкиСерии.Ссылка = &АктОРасхождениях
	|	И АктОРасхожденияхПослеПриемкиСерии.Действие = ЗНАЧЕНИЕ(
	|		Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОформитьПерепоставленноеИВернуть)
	|	И АктОРасхожденияхПослеПриемкиСерии.Склад = &Склад
	|	И АктОРасхожденияхПослеПриемкиСерии.Количество - АктОРасхожденияхПослеПриемкиСерии.КоличествоПоДокументу > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	АктОРасхожденияхПослеПриемкиСерии.Номенклатура,
	|	АктОРасхожденияхПослеПриемкиСерии.Характеристика,
	|	АктОРасхожденияхПослеПриемкиСерии.Назначение,
	|	АктОРасхожденияхПослеПриемкиСерии.Серия,
	|	АктОРасхожденияхПослеПриемкиСерии.Склад";
	
	Запрос.УстановитьПараметр("Склад",             ДанныеЗаполнения.Склад);
	Запрос.УстановитьПараметр("АктОРасхождениях",  ДанныеЗаполнения.АктОРасхождениях);
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	ВыборкаШапка  = ПакетЗапросов[0].Выбрать();
	
	ВыборкаШапка.Следующий();
	
	// По акту о расхождениях после приемки.
	ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОсновании(ВыборкаШапка.АктОРасхождениях,
		ВыборкаШапка.СтатусАктаОРасхождениях,
		ВыборкаШапка.ЕстьОшибкиПроведенАктОРасхождениях,
		ВыборкаШапка.ЕстьОшибкиСтатусАкт,
		РасхожденияСервер.МассивДопустимыхСтатусовАктовОРасхожденияхПриСозданииНаОсновании());
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаШапка);
	
	ХозяйственнаяОперация     = ЗакупкиСервер.ПолучитьХозяйственнуюОперациюВозвратаПоПоступлению(
		ВыборкаШапка.ХозяйственнаяОперация);
	БанковскийСчетКонтрагента = ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетКонтрагентаПоУмолчанию(Контрагент,
		Неопределено, БанковскийСчетКонтрагента);
	
	ТаблицаТоваров = ПакетЗапросов[1].Выгрузить();
	
	Для Каждого ТекСтрока Из ТаблицаТоваров Цикл
		НоваяСтрока = Товары.Добавить();
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока,ТекСтрока);
		
		НоваяСтрока.Сумма = НоваяСтрока.Цена * НоваяСтрока.КоличествоУпаковок;
		
	КонецЦикла;
	
	Серии.Загрузить(ПакетЗапросов[2].Выгрузить());
	
	ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, Документы.ОтгрузкаТоваровСХранения);
	НоменклатураСервер.ЗаполнитьСерииПоFEFO(ЭтотОбъект, ПараметрыУказанияСерий, Ложь);
	
КонецПроцедуры

Процедура ЗаполнитьДокументНаОснованииПриемкиТоваровНаХранение(Знач ДанныеЗаполнения)
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПриемкаТоваровНаХранение.Ссылка                    КАК ДокументОснование,
	|	ПриемкаТоваровНаХранение.Партнер                   КАК Партнер,
	|	ПриемкаТоваровНаХранение.Контрагент                КАК Контрагент,
	|	ПриемкаТоваровНаХранение.Соглашение                КАК Соглашение,
	|	ПриемкаТоваровНаХранение.Договор                   КАК Договор,
	|	ПриемкаТоваровНаХранение.Организация               КАК Организация,
	|	ПриемкаТоваровНаХранение.Подразделение             КАК Подразделение,
	|	ПриемкаТоваровНаХранение.Склад                     КАК Склад,
	|	ПриемкаТоваровНаХранение.Руководитель              КАК Руководитель,
	|	ПриемкаТоваровНаХранение.ГлавныйБухгалтер          КАК ГлавныйБухгалтер,
	|	ПриемкаТоваровНаХранение.ХозяйственнаяОперация     КАК ХозяйственнаяОперация,
	|	ПриемкаТоваровНаХранение.Валюта                    КАК Валюта,
	|	ПриемкаТоваровНаХранение.СуммаДокумента            КАК СуммаДокумента,
	|	ПриемкаТоваровНаХранение.Менеджер                  КАК Менеджер,
	|	ПриемкаТоваровНаХранение.Сделка                    КАК Сделка,
	|	ПриемкаТоваровНаХранение.НаправлениеДеятельности   КАК НаправлениеДеятельности,
	|	ПриемкаТоваровНаХранение.БанковскийСчетОрганизации КАК БанковскийСчетОрганизации,
	|	ПриемкаТоваровНаХранение.БанковскийСчетКонтрагента КАК БанковскийСчетКонтрагента,
	|	НЕ ПриемкаТоваровНаХранение.Проведен               КАК ЕстьОшибкиПроведен
	|ИЗ
	|	Документ.ПриемкаТоваровНаХранение КАК ПриемкаТоваровНаХранение
	|ГДЕ
	|	ПриемкаТоваровНаХранение.Ссылка = &ДокументОснование
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 1
	|ВЫБРАТЬ
	|	ТаблицаТовары.КодСтроки 					КАК КодСтроки,
	|	ТаблицаТовары.НоменклатураПартнера 		КАК НоменклатураПартнера,
	|	ТаблицаТовары.Номенклатура 					КАК Номенклатура,
	|	ТаблицаТовары.Номенклатура.ТипНоменклатуры 	КАК ТипНоменклатуры,
	|	ТаблицаТовары.Характеристика 				КАК Характеристика,
	|	ТаблицаТовары.Упаковка 						КАК Упаковка,
	|	ТаблицаТовары.Назначение 					КАК Назначение,
	|	ТаблицаТовары.Серия 						КАК Серия,
	|	ТаблицаТовары.СтатусУказанияСерий 			КАК СтатусУказанияСерий,
	|	ТаблицаТовары.КоличествоУпаковок 			КАК КоличествоУпаковок,
	|	ТаблицаТовары.Количество 					КАК Количество,
	|	ТаблицаТовары.Цена							КАК Цена,
	|	ТаблицаТовары.Сумма							КАК Сумма,
	|	ТаблицаТовары.Склад 						КАК Склад,
	|	ТаблицаТовары.НомерСтроки 					КАК НомерСтроки
	|ПОМЕСТИТЬ ТоварыДокументаПриемки
	|ИЗ
	|	Документ.ПриемкаТоваровНаХранение.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыДокументаПриемки.НоменклатураПартнера 	КАК НоменклатураПартнера,
	|	ТоварыДокументаПриемки.Номенклатура 			КАК Номенклатура,
	|	ТоварыДокументаПриемки.ТипНоменклатуры 			КАК ТипНоменклатуры,
	|	ТоварыДокументаПриемки.Характеристика 			КАК Характеристика,
	|	ТоварыДокументаПриемки.Упаковка 				КАК Упаковка,
	|	ТоварыДокументаПриемки.Назначение 				КАК Назначение,
	|	ТоварыДокументаПриемки.Серия 					КАК Серия,
	|	ТоварыДокументаПриемки.СтатусУказанияСерий 		КАК СтатусУказанияСерий,
	|	ТоварыДокументаПриемки.КоличествоУпаковок 		КАК КоличествоУпаковок,
	|	ТоварыДокументаПриемки.Количество 				КАК Количество,
	|	ТоварыДокументаПриемки.Цена 					КАК Цена,
	|	ТоварыДокументаПриемки.Сумма 					КАК Сумма,
	|	ТоварыДокументаПриемки.Склад 					КАК Склад,
	|	&ДокументОснование 								КАК ДокументПоступления
	|ИЗ
	|	ТоварыДокументаПриемки КАК ТоварыДокументаПриемки
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТоварыДокументаПриемки.НомерСтроки
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 3
	|ВЫБРАТЬ
	|	ТаблицаСерии.Номенклатура               КАК Номенклатура,
	|	ТаблицаСерии.Характеристика             КАК Характеристика,
	|	ТаблицаСерии.Назначение                 КАК Назначение,
	|	ТаблицаСерии.Серия                      КАК Серия,
	|	ТаблицаСерии.Склад                      КАК Склад,
	|	СУММА(ТаблицаСерии.Количество)          КАК Количество
	|ИЗ
	|	Документ.ПриемкаТоваровНаХранение.Серии КАК ТаблицаСерии
	|ГДЕ
	|	ТаблицаСерии.Ссылка = &ДокументОснование
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаСерии.Номенклатура,
	|	ТаблицаСерии.Характеристика,
	|	ТаблицаСерии.Назначение,
	|	ТаблицаСерии.Серия,
	|	ТаблицаСерии.Склад";
	
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	ВыборкаШапка  = ПакетЗапросов[0].Выбрать();
	
	ВыборкаШапка.Следующий();
	
	ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОсновании(ДанныеЗаполнения, Неопределено,
		ВыборкаШапка.ЕстьОшибкиПроведен);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаШапка);
	
	ХозяйственнаяОперация     = ЗакупкиСервер.ПолучитьХозяйственнуюОперациюВозвратаПоПоступлению(
		ВыборкаШапка.ХозяйственнаяОперация);
	БанковскийСчетКонтрагента = ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетКонтрагентаПоУмолчанию(Контрагент,
		Неопределено, БанковскийСчетКонтрагента);
	
	// Разбиение строк, заполнение серий со статусом 10.
	ИндексыСтрок   = Новый Массив();
	ТаблицаТоваров = ПакетЗапросов[2].Выгрузить(); // ТаблицаЗначений
	СерииОснования = ПакетЗапросов[3].Выгрузить();
	
	Для Каждого СтрокаТовары Из ТаблицаТоваров Цикл
		Если СтрокаТовары.СтатусУказанияСерий = 10 Тогда
			ИндексыСтрок.Вставить(0, ТаблицаТоваров.Индекс(СтрокаТовары));
		КонецЕсли;
	КонецЦикла;
	
	ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, Документы.ОтгрузкаТоваровСХранения);
	
	ПоляСвязиСерийМассив = Новый Массив;
	ПоляСвязиСерийМассив.Добавить("Номенклатура");
	ПоляСвязиСерийМассив.Добавить("Характеристика");
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПоляСвязиСерийМассив, ПараметрыУказанияСерий.ПоляСвязи);
	ПоляСвязиСерийСтрока = СтрСоединить(ПоляСвязиСерийМассив, ",");
	
	Если ИндексыСтрок.Количество() > 0 Тогда
		НакладныеСервер.ПеренестиСерииИзТаблицыВСтроки(ТаблицаТоваров, ИндексыСтрок, СерииОснования, ПоляСвязиСерийСтрока);
	КонецЕсли;
	
	ПоляСвязиСерийМассив.Добавить("СтатусУказанияСерий");
	ПоляВыгрузкиТоваровПоСериям = СтрСоединить(ПоляСвязиСерийМассив, ",");
	
	ТаблицаТоваровПоСериям = ТаблицаТоваров.Скопировать(,ПоляВыгрузкиТоваровПоСериям);
	ТаблицаТоваровПоСериям.Свернуть(ПоляВыгрузкиТоваровПоСериям);
	
	Для Каждого СтрокаТоваров Из ТаблицаТоваровПоСериям Цикл
		Если НоменклатураКлиентСервер.ВЭтомСтатусеСерииУказываютсяВТЧСерии(СтрокаТоваров.СтатусУказанияСерий, ПараметрыУказанияСерий) Тогда
			
			ОтборСтрок = Новый Структура(ПоляСвязиСерийСтрока);
			ЗаполнитьЗначенияСвойств(ОтборСтрок, СтрокаТоваров);
			
			СтрокиСерий = СерииОснования.НайтиСтроки(ОтборСтрок);
			
			Для Каждого СтрокаСерий Из СтрокиСерий Цикл				
				НоваяСтрока = Серии.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаСерий);
			КонецЦикла;
			
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ТекСтрока Из ТаблицаТоваров Цикл
		НоваяСтрока = Товары.Добавить();
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
		НоваяСтрока.Сумма = НоваяСтрока.Цена * НоваяСтрока.КоличествоУпаковок;
	КонецЦикла;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСкладыВТабличнойЧастиДокументовЗакупки")
		И Не ПолучитьФункциональнуюОпцию("ИспользоватьСкладыВТабличнойЧастиДокументовПродажи") Тогда
		
		МассивСкладов = ОбщегоНазначенияУТ.УдалитьПовторяющиесяЭлементыМассива(Товары.ВыгрузитьКолонку("Склад"));
		
		Если МассивСкладов.Количество() = 1 Тогда
			Склад = МассивСкладов[0];
		Иначе
			Склад = Справочники.Склады.ПустаяСсылка();
		КонецЕсли;
		
	КонецЕсли;
	
	НоменклатураСервер.ЗаполнитьСерииПоFEFO(ЭтотОбъект, ПараметрыУказанияСерий, Ложь);
	
КонецПроцедуры

//++ НЕ УТКА
Процедура ЗаполнитьНаОснованииЗаказДавальца2_5(ДанныеЗаполнения)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаДавальцу2_5)
	|			ТОГДА ЗаказДавальца2_5.Ссылка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                             КАК Основание,
	|	&ПоЗаказу                                         КАК ПоЗаказу,
	|	ЗаказДавальца2_5.Статус                           КАК СтатусДокумента,
	|	ЗаказДавальца2_5.Партнер                          КАК Партнер,
	|	ЗаказДавальца2_5.Контрагент                       КАК Контрагент,
	|	ЗаказДавальца2_5.Договор                          КАК Договор,
	|	ЗаказДавальца2_5.Организация                      КАК Организация,
	|	ЗаказДавальца2_5.Подразделение                    КАК Подразделение,
	|	ВЫБОР
	|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаДавальцу2_5)
	|			ТОГДА ЗаказДавальца2_5.Склад
	|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДавальцу2_5)
	|			ТОГДА ЗаказДавальца2_5.СкладПоступления
	|	КОНЕЦ                                             КАК Склад,
	|	&ХозяйственнаяОперация                            КАК ХозяйственнаяОперация,
	|	ЗаказДавальца2_5.Валюта                           КАК Валюта,
	|	ЗаказДавальца2_5.Менеджер                         КАК Менеджер,
	|	ЗаказДавальца2_5.Сделка                           КАК Сделка,
	|	ЗаказДавальца2_5.НаправлениеДеятельности          КАК НаправлениеДеятельности,
	|	ЗаказДавальца2_5.БанковскийСчет                   КАК БанковскийСчетОрганизации,
	|	ЗаказДавальца2_5.БанковскийСчетКонтрагента        КАК БанковскийСчетКонтрагента,
	|	ЗаказДавальца2_5.Руководитель                     КАК Руководитель,
	|	ЗаказДавальца2_5.ГлавныйБухгалтер                 КАК ГлавныйБухгалтер,
	|	
	// Доставка. Начало
	|	ЗаказДавальца2_5.АдресДоставки                    КАК АдресДоставки,
	|	ЗаказДавальца2_5.АдресДоставкиЗначенияПолей       КАК АдресДоставкиЗначенияПолей,
	|	ЗаказДавальца2_5.СпособДоставки                   КАК СпособДоставки,
	|	ЗаказДавальца2_5.ЗонаДоставки                     КАК ЗонаДоставки,
	|	ЗаказДавальца2_5.ПеревозчикПартнер                КАК ПеревозчикПартнер,
	|	ЗаказДавальца2_5.ВремяДоставкиС                   КАК ВремяДоставкиС,
	|	ЗаказДавальца2_5.ВремяДоставкиПо                  КАК ВремяДоставкиПо,
	|	ЗаказДавальца2_5.ДополнительнаяИнформацияПоДоставке КАК ДополнительнаяИнформацияПоДоставке,
	|	ЗаказДавальца2_5.ОсобыеУсловияПеревозки           КАК ОсобыеУсловияПеревозки,
	|	ЗаказДавальца2_5.ОсобыеУсловияПеревозкиОписание   КАК ОсобыеУсловияПеревозкиОписание,
	// Доставка. Окончание
	|	
	|	НЕ ЗаказДавальца2_5.Статус В (&ДопустимыеСтатусы) КАК ЕстьОшибкиСтатус,
	|	НЕ ЗаказДавальца2_5.Проведен                      КАК ЕстьОшибкиПроведен,
	|	&ДокументОснование                                КАК ДокументОснование
	|ИЗ
	|	Документ.ЗаказДавальца2_5 КАК ЗаказДавальца2_5
	|ГДЕ
	|	ЗаказДавальца2_5.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Справочник.НоменклатураКонтрагентов.ПустаяСсылка) КАК НоменклатураПартнера,
	|	ТаблицаТовары.Номенклатура                 КАК Номенклатура,
	|	ТаблицаТовары.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ТаблицаТовары.Характеристика               КАК Характеристика,
	|	Продукция.Ссылка.Назначение                КАК Назначение,
	|	ТаблицаТовары.Серия                        КАК Серия,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Продукция.Упаковка, ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)) =
	|				ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|			ТОГДА ТаблицаТовары.КПередачеОстаток
	|		ИНАЧЕ ТаблицаТовары.КПередачеОстаток / &ТекстЗапросаКоэффициентУпаковки
	|	КОНЕЦ                                      КАК КоличествоУпаковок,
	|	ТаблицаТовары.КПередачеОстаток             КАК Количество,
	|	ЕСТЬNULL(Продукция.Упаковка, ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)) КАК Упаковка,
	|	0                                          КАК Цена,
	|	0                                          КАК Сумма,
	|	ТаблицаТовары.Склад                        КАК Склад,
	|	ТаблицаТовары.ЗаказКлиента                 КАК ЗаказКлиента,
	|	ТаблицаТовары.КодСтроки                    КАК КодСтроки
	|ИЗ
	|	РегистрНакопления.ЗаказыКлиентов.Остатки(, ЗаказКлиента = &ДокументОснование) КАК ТаблицаТовары
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказДавальца2_5.Продукция КАК Продукция
	|	ПО ТаблицаТовары.ЗаказКлиента = Продукция.Ссылка
	|	И ТаблицаТовары.КодСтроки     = Продукция.КодСтроки
	|
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаДавальцу2_5)
	|	И ТаблицаТовары.Номенклатура.ТипНоменклатуры В(
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТовары.НоменклатураПартнера         КАК НоменклатураПартнера,
	|	ТаблицаТовары.Номенклатура                 КАК Номенклатура,
	|	ТаблицаТовары.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ТаблицаТовары.Характеристика               КАК Характеристика,
	|	ТаблицаТовары.Назначение                   КАК Назначение,
	|	ТаблицаТовары.Серия                        КАК Серия,
	|	ТаблицаТовары.КоличествоУпаковок           КАК КоличествоУпаковок,
	|	ТаблицаТовары.Количество                   КАК Количество,
	|	ТаблицаТовары.Упаковка                     КАК Упаковка,
	|	ТаблицаТовары.Цена                         КАК Цена,
	|	ТаблицаТовары.Сумма                        КАК Сумма,
	|	ТаблицаТовары.Склад                        КАК Склад,
	|	НЕОПРЕДЕЛЕНО                               КАК ЗаказКлиента,
	|	0                                          КАК КодСтроки
	|ИЗ
	|	Документ.ЗаказДавальца2_5.Материалы КАК ТаблицаТовары
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДавальцу2_5)
	|	И ТаблицаТовары.Ссылка = &ДокументОснование
	|	И ТаблицаТовары.Действие = ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоПерерабатываемымМатериалам.ПринятьНаХранение)
	|	И НЕ ТаблицаТовары.Отменено
	|";
	
	Запрос.Текст =
		СтрЗаменить(
			Запрос.Текст,
			"&ТекстЗапросаКоэффициентУпаковки",
			Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
				"Продукция.Упаковка", "Продукция.Номенклатура"));
	
	Запрос.УстановитьПараметр("ДокументОснование",     ДанныеЗаполнения.ДокументОснование);
	Запрос.УстановитьПараметр("ХозяйственнаяОперация", ДанныеЗаполнения.ХозяйственнаяОперация);
	
	ПризнакПоЗаказу =
		ДанныеЗаполнения.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаДавальцу2_5
		И ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыДавальцев2_5");
	Запрос.УстановитьПараметр("ПоЗаказу",              ПризнакПоЗаказу);
	
	ДопустимыеСтатусы =
		Документы.ЗаказДавальца2_5.ДопустимыеСтатусыВводаНаОсновании(
			Метаданные().Имя,
			ДанныеЗаполнения.ХозяйственнаяОперация);
	
	Запрос.УстановитьПараметр("ДопустимыеСтатусы", ДопустимыеСтатусы);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ВыборкаШапка = МассивРезультатов[0].Выбрать();
	ВыборкаШапка.Следующий();
	
	ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОсновании(
		ДанныеЗаполнения.ДокументОснование,
		ВыборкаШапка.СтатусДокумента,
		ВыборкаШапка.ЕстьОшибкиПроведен,
		ВыборкаШапка.ЕстьОшибкиСтатус,
		ДопустимыеСтатусы);
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаШапка);
	
	БанковскийСчетКонтрагента = ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетКонтрагентаПоУмолчанию(
		Контрагент, Неопределено, БанковскийСчетКонтрагента);
	
	Товары.Загрузить(МассивРезультатов[1].Выгрузить());
	
	Если ДанныеЗаполнения.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаДавальцу2_5
		И Товары.Количество() = 0 Тогда
		ВызватьИсключение НСтр("ru = 'Нет остатков к оформлению.';
								|en = 'There are no remaining goods to register.'");
	КонецЕсли;
	
	НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(
		ЭтотОбъект,
		Новый ФиксированнаяСтруктура(НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, Документы.ОтгрузкаТоваровСХранения)));
	
КонецПроцедуры

Процедура ЗаполнитьДокументНаОснованииОтчетДавальцу2_5(ДанныеЗаполнения)
	
	ДанныеОснование = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
						ДанныеЗаполнения,
						"Организация, Партнер, Контрагент, Подразделение, Склад, Договор, ОтчетПоЗаказам, ЗаказДавальца,
						|ВалютаВзаиморасчетов, Менеджер");
	
	Если ДанныеОснование.ОтчетПоЗаказам Тогда
		
		Если ЗначениеЗаполнено(ДанныеОснование.ЗаказДавальца) Тогда
			
			ОписаниеЗаказа = Новый Структура;
			ОписаниеЗаказа.Вставить("ДокументОснование",     ДанныеОснование.ЗаказДавальца);
			ОписаниеЗаказа.Вставить("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ПередачаДавальцу2_5);
			
			ЗаполнитьНаОснованииЗаказДавальца2_5(ОписаниеЗаказа);
			
		Иначе
			
			ТекстИсключение =
				НСтр("ru = 'Отчет давальцу оформлен по нескольким заказам. Ввод на основании невозможен.
						   |Воспользуйтесь рабочим местом ""Прием в переработку"" - ""Документы к оформлению"".';
						   |en = 'Report to the provider is registered by several orders. Cannot generate from base documents.
						   |Use the ""Subcontracting services delivered - Unregistered documents"" workplace.'");
			
			ВызватьИсключение ТекстИсключение;
			
		КонецЕсли;
		
		Возврат;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаТоваров.Номенклатура      КАК Номенклатура,
	|	ТаблицаТоваров.Характеристика    КАК Характеристика,
	|	ТаблицаТоваров.Назначение        КАК Назначение,
	|	ТаблицаТоваров.Серия             КАК Серия,
	|	ТаблицаТоваров.Склад             КАК Склад,
	|	СУММА(ТаблицаТоваров.Количество) КАК Количество
	|ПОМЕСТИТЬ ТаблицаТоваров
	|ИЗ
	|	Документ.ОтчетДавальцу2_5.Продукция КАК ТаблицаТоваров
	|ГДЕ
	|	ТаблицаТоваров.Ссылка = &ДокументОснование
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.Номенклатура,
	|	ТаблицаТоваров.Характеристика,
	|	ТаблицаТоваров.Назначение,
	|	ТаблицаТоваров.Серия,
	|	ТаблицаТоваров.Склад
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Назначение,
	|	Серия,
	|	Склад
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиБезЗаказа.Номенклатура     КАК Номенклатура,
	|	ОстаткиБезЗаказа.Характеристика   КАК Характеристика,
	|	ОстаткиБезЗаказа.Назначение       КАК Назначение,
	|	ОстаткиБезЗаказа.Склад            КАК Склад,
	|	ОстаткиБезЗаказа.Серия            КАК Серия,
	|	СУММА(ОстаткиБезЗаказа.КПередаче) КАК КПередаче
	|ПОМЕСТИТЬ ОстаткиБезЗаказа
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Номенклатура   КАК Номенклатура,
	|		ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	|		ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Назначение     КАК Назначение,
	|		ТоварыОрганизаций.АналитикаУчетаНоменклатуры.МестоХранения  КАК Склад,
	|		ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Серия          КАК Серия,
	|		ТоварыОрганизаций.КоличествоОстаток                         КАК КПередаче
	|	ИЗ
	|		РегистрНакопления.ТоварыОрганизаций.Остатки(,
	|				ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПродукцияДавальца)
	|				И ВЫРАЗИТЬ(ВидЗапасов.Договор КАК Справочник.ДоговорыКонтрагентов).ТипДоговора =
	|																ЗНАЧЕНИЕ(Перечисление.ТипыДоговоров.СДавальцем2_5)
	|				И АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Склад)
	|				И Организация = &Организация
	|				И ВидЗапасов.Договор = &Договор
	|				И (АналитикаУчетаНоменклатуры.Номенклатура,
	|				   АналитикаУчетаНоменклатуры.Характеристика,
	|				   АналитикаУчетаНоменклатуры.Назначение,
	|				   АналитикаУчетаНоменклатуры.Серия,
	|				   АналитикаУчетаНоменклатуры.МестоХранения) В
	|						(ВЫБРАТЬ
	|							ТаблицаТоваров.Номенклатура   КАК Номенклатура,
	|							ТаблицаТоваров.Характеристика КАК Характеристика,
	|							ТаблицаТоваров.Назначение     КАК Назначение,
	|							ТаблицаТоваров.Серия          КАК Серия,
	|							ТаблицаТоваров.Склад          КАК Склад
	|						ИЗ
	|							ТаблицаТоваров КАК ТаблицаТоваров)) КАК ТоварыОрганизаций
	|	ГДЕ
	|		ТоварыОрганизаций.КоличествоОстаток > 0
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		Обороты.Номенклатура   КАК Номенклатура,
	|		Обороты.Характеристика КАК Характеристика,
	|		Продукция.Назначение   КАК Назначение,
	|		Обороты.Склад          КАК Склад,
	|		Обороты.Серия          КАК Серия,
	|		-Продукция.Количество  КАК КПередаче
	|	ИЗ
	|		РегистрНакопления.ЗаказыКлиентов.Обороты(
	|				,,,
	|				ТИПЗНАЧЕНИЯ(ЗаказКлиента) = ТИП(Документ.ЗаказДавальца2_5)
	|				И ВЫРАЗИТЬ(ЗаказКлиента КАК Документ.ЗаказДавальца2_5).Договор = &Договор) КАК Обороты
	|
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетДавальцу2_5.Продукция КАК Продукция
	|			ПО Обороты.ЗаказКлиента = Продукция.ЗаказДавальца
	|			 И Обороты.КодСтроки    = Продукция.КодСтроки
	|			 И Продукция.Ссылка.Проведен
	|	ГДЕ
	|		НЕ ВЫРАЗИТЬ(Обороты.ЗаказКлиента КАК Документ.ЗаказДавальца2_5).Ссылка ЕСТЬ NULL // для отработки RLS
	|		И НЕ Обороты.Склад.Ссылка ЕСТЬ NULL
	|		И ИСТИНА В (
	|			ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				РегистрСведений.РеестрДокументов КАК РеестрДокументов
	|			ГДЕ
	|				РеестрДокументов.Ссылка = Обороты.ЗаказКлиента
	|				И РеестрДокументов.Организация = &Организация
	|				И НЕ РеестрДокументов.ДополнительнаяЗапись)
	|		И ИСТИНА В 
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				ТаблицаТоваров КАК ТаблицаТоваров
	|			ГДЕ
	|				ТаблицаТоваров.Номенклатура     = Обороты.Номенклатура
	|				И ТаблицаТоваров.Характеристика = Обороты.Характеристика
	|				И ТаблицаТоваров.Назначение     = Продукция.Назначение
	|				И ТаблицаТоваров.Серия          = Обороты.Серия
	|				И ТаблицаТоваров.Склад          = Обороты.Склад)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Обороты.Номенклатура   КАК Номенклатура,
	|		Обороты.Характеристика КАК Характеристика,
	|		ТаблицаТовары.Назначение   КАК Назначение,
	|		Обороты.Склад          КАК Склад,
	|		Обороты.Серия          КАК Серия,
	|		ТаблицаТовары.Количество  КАК КПередаче
	|	ИЗ
	|		РегистрНакопления.ЗаказыКлиентов.Обороты(
	|				,,,
	|				ТИПЗНАЧЕНИЯ(ЗаказКлиента) = ТИП(Документ.ЗаказДавальца2_5)
	|				И ВЫРАЗИТЬ(ЗаказКлиента КАК Документ.ЗаказДавальца2_5).Договор = &Договор) КАК Обороты
	|
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтгрузкаТоваровСХранения.Товары КАК ТаблицаТовары
	|			ПО Обороты.ЗаказКлиента = ТаблицаТовары.ЗаказКлиента
	|			И Обороты.КодСтроки    = ТаблицаТовары.КодСтроки
	|			И ТаблицаТовары.Ссылка.Проведен
	|	ГДЕ
	|		НЕ ВЫРАЗИТЬ(Обороты.ЗаказКлиента КАК Документ.ЗаказДавальца2_5).Ссылка ЕСТЬ NULL // для отработки RLS
	|		И НЕ Обороты.Склад.Ссылка ЕСТЬ NULL
	|		И ИСТИНА В (
	|			ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				РегистрСведений.РеестрДокументов КАК РеестрДокументов
	|			ГДЕ
	|				РеестрДокументов.Ссылка = Обороты.ЗаказКлиента
	|				И РеестрДокументов.Организация = &Организация
	|				И НЕ РеестрДокументов.ДополнительнаяЗапись)
	|		И ИСТИНА В 
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				ТаблицаТоваров КАК ТаблицаТоваров
	|			ГДЕ
	|				ТаблицаТоваров.Номенклатура     = Обороты.Номенклатура
	|				И ТаблицаТоваров.Характеристика = Обороты.Характеристика
	|				И ТаблицаТоваров.Назначение     = ТаблицаТовары.Назначение
	|				И ТаблицаТоваров.Серия          = Обороты.Серия
	|				И ТаблицаТоваров.Склад          = Обороты.Склад)
	|	) КАК ОстаткиБезЗаказа
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиБезЗаказа.Номенклатура,
	|	ОстаткиБезЗаказа.Характеристика,
	|	ОстаткиБезЗаказа.Назначение,
	|	ОстаткиБезЗаказа.Склад,
	|	ОстаткиБезЗаказа.Серия
	|
	|ИМЕЮЩИЕ
	|	СУММА(ОстаткиБезЗаказа.КПередаче) > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Назначение,
	|	Серия,
	|	Склад
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Номенклатура   КАК Номенклатура,
	|	ТаблицаТоваров.Характеристика КАК Характеристика,
	|	ТаблицаТоваров.Назначение     КАК Назначение,
	|	ТаблицаТоваров.Серия          КАК Серия,
	|	ТаблицаТоваров.Склад          КАК Склад,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.Количество > ОстаткиБезЗаказа.КПередаче
	|			ТОГДА ОстаткиБезЗаказа.КПередаче
	|		ИНАЧЕ ТаблицаТоваров.Количество
	|	КОНЕЦ КАК Количество
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиБезЗаказа КАК ОстаткиБезЗаказа
	|		ПО ТаблицаТоваров.Номенклатура   = ОстаткиБезЗаказа.Номенклатура
	|		 И ТаблицаТоваров.Характеристика = ОстаткиБезЗаказа.Характеристика
	|		 И ТаблицаТоваров.Назначение     = ОстаткиБезЗаказа.Назначение
	|		 И ТаблицаТоваров.Серия          = ОстаткиБезЗаказа.Серия
	|		 И ТаблицаТоваров.Склад          = ОстаткиБезЗаказа.Склад
	|ГДЕ
	|	НЕ ОстаткиБезЗаказа.Номенклатура ЕСТЬ NULL
	|";
	
	Запрос.УстановитьПараметр("ДокументОснование",       ДанныеЗаполнения);
	Запрос.УстановитьПараметр("Договор",                 ДанныеОснование.Договор);
	Запрос.УстановитьПараметр("Организация",             ДанныеОснование.Организация);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		ВызватьИсключение НСтр("ru = 'Нет остатков к оформлению.';
								|en = 'There are no remaining goods to register.'");
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеОснование);
	
	ДокументОснование     = ДанныеЗаполнения;
	ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаДавальцу2_5;
	Валюта                = ДанныеОснование.ВалютаВзаиморасчетов;
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СтрокаТЧПродукция = Товары.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТЧПродукция, Выборка);
		СтрокаТЧПродукция.КоличествоУпаковок = Выборка.Количество;
		
	КонецЦикла;
	
	НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(
		ЭтотОбъект,
		Новый ФиксированнаяСтруктура(НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, Документы.ОтгрузкаТоваровСХранения)));
	
КонецПроцедуры
//-- НЕ УТКА

Процедура ЗаполнитьДокументПоОтбору(Знач ДанныеЗаполнения)
	
	Если ДанныеЗаполнения.Свойство("Партнер") Тогда
		Партнер = ДанныеЗаполнения.Партнер;
		
		ТекущаяОперация = 
			?(ДанныеЗаполнения.Свойство("ХозяйственнаяОперация"), ДанныеЗаполнения.ХозяйственнаяОперация, Неопределено);
		
		ЗаполнитьУсловияЗакупокПоУмолчанию(, ТекущаяОперация);
		
	ИначеЕсли ДанныеЗаполнения.Свойство("ДокументОснование") Тогда
		Если ТипЗнч(ДанныеЗаполнения.ДокументОснование) = Тип("Массив") Тогда
			Для каждого ТекущиеДанные Из ДанныеЗаполнения.ДокументОснование Цикл
				ЗаполнитьДокументПоЗаказуКлиента(ТекущиеДанные, ДанныеЗаполнения.ПараметрыОформления.ПоОрдерам);
			КонецЦикла;
		Иначе
			ЗаполнитьДокументПоЗаказуКлиента(ДанныеЗаполнения.ДокументОснование, ДанныеЗаполнения.ПараметрыОформления.ПоОрдерам);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	
	Автор = Пользователи.ТекущийПользователь();
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Если ДанныеЗаполнения.Свойство("Организация") Тогда
			Организация = ДанныеЗаполнения.Организация;
		КонецЕсли;
	КонецЕсли;
	
	ИспользоватьСкладВТЧ = ПолучитьФункциональнуюОпцию("ИспользоватьСкладыВТабличнойЧастиДокументовПродажи");
	
	Если Не ЗначениеЗаполнено(Менеджер) Тогда
		Менеджер = Пользователи.ТекущийПользователь();
	КонецЕсли;
	Организация   = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	Если Не ЗначениеЗаполнено(Валюта) Тогда
		Валюта = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Организация);
	КонецЕсли;
	//++ НЕ УТКА
	Если ЗначениеЗаполнено(Договор)
	   И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Договор, "ХозяйственнаяОперация") =
											ДавальческаяСхемаКлиентСервер.ХозяйственнаяОперацияДоговора() Тогда
		Подразделение = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Договор, "Подразделение");
	Иначе
	//-- НЕ УТКА
		Подразделение = ЗначениеНастроекПовтИсп.ПодразделениеПользователя(Менеджер, Подразделение);
	//++ НЕ УТКА
	КонецЕсли;
	//-- НЕ УТКА
	Склад         = ЗначениеНастроекПовтИсп.ПолучитьСкладПоУмолчанию(Склад, ИспользоватьСкладВТЧ, Истина);
	
	СтруктураПараметров = ДенежныеСредстваСервер.ПараметрыЗаполненияБанковскогоСчетаОрганизацииПоУмолчанию();
	СтруктураПараметров.Организация    = Организация;
	СтруктураПараметров.БанковскийСчет = БанковскийСчетОрганизации;
	
	БанковскийСчетОрганизации = ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетОрганизацииПоУмолчанию(СтруктураПараметров);
	БанковскийСчетКонтрагента = ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетКонтрагентаПоУмолчанию(Контрагент,
		Неопределено, БанковскийСчетКонтрагента);
	
	СтруктураОтветственного = ЗакупкиСервер.ПолучитьОтветственногоПоСкладу(Склад, Менеджер);
	
	Если СтруктураОтветственного <> Неопределено Тогда
		Отпустил          = СтруктураОтветственного.Ответственный;
		ОтпустилДолжность = СтруктураОтветственного.ОтветственныйДолжность;
	КонецЕсли;
	
	СтруктураОснование = Документы.ОтгрузкаТоваровСХранения.СтруктураОснованияДляПечати(ЭтотОбъект);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураОснование);	
	
КонецПроцедуры

// Параметры:
// 	ДанныеЗаполнения - Структура - содержит:
// 		* Партнер - СправочникСсылка.Партнеры - 
// 		* Контрагент - СправочникСсылка.Контрагенты - 
// 		* Договор - СправочникСсылка.ДоговорыКонтрагентов - 
// 		* Соглашение - СправочникСсылка.СоглашенияСКлиентами, СправочникСсылка.СоглашенияСПоставщиками - 
// 		* Склад - СправочникСсылка.Склады - 
// 		* Ссылка - ДокументСсылка - 
// 		* Организация - СправочникСсылка.Организации - 
// 		* Валюта - СправочникСсылка.Валюты - 
// 	ПоОрдерам - Булево - 
Процедура ЗаполнитьДокументПоЗаказуКлиента(Знач ДанныеЗаполнения, ПоОрдерам = Ложь)
	
	ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоставкаПодПринципала;
	Партнер = ДанныеЗаполнения.Партнер;
	Контрагент = ДанныеЗаполнения.Контрагент;
	Договор = ДанныеЗаполнения.Договор;
	Соглашение = ДанныеЗаполнения.Соглашение;
	Склад = ДанныеЗаполнения.Склад;
	Основание = ДанныеЗаполнения.Ссылка;
	Организация = ДанныеЗаполнения.Организация;
	Валюта = ДанныеЗаполнения.Валюта;
	ПоЗаказу = Истина;
	
	МассивЗаказов = Новый Массив();
	МассивЗаказов.Добавить(ДанныеЗаполнения);
	
	ПараметрыОтбора = Новый Структура();
	ПараметрыОтбора.Вставить("Партнер", Партнер);
	ПараметрыОтбора.Вставить("Контрагент", Контрагент);
	ПараметрыОтбора.Вставить("Организация", Организация);
	ПараметрыОтбора.Вставить("Склад", Склад);
	ПараметрыОтбора.Вставить("Договор", Договор);
	ПараметрыОтбора.Вставить("ЗаказыКлиентов", МассивЗаказов);
	ПараметрыОтбора.Вставить("ТоварыОтгрузки", Товары.Выгрузить());
	ПараметрыОтбора.Вставить("ДокументОтгрузки", Ссылка);
	ПараметрыОтбора.Вставить("ПоЗаказу", Истина);
	ПараметрыОтбора.Вставить("ОрдернаяСхемаПриОтгрузке", СкладыСервер.ЕстьОрдерныйНаОтгрузкуСклад(, Дата, Склад));

	ДанныеПоОтгрузке = Документы.ОтгрузкаТоваровСХранения.ПолучитьТаблицуОстатковПоОтгрузке(ПараметрыОтбора);
	
	Если ДанныеПоОтгрузке.Количество() = 0 Тогда
		ТекстОшибки = ОбеспечениеВДокументахСервер.ТекстОшибкиНетТоваровДоступныхДляОтгрузки();
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьКоличествоУпаковок");
	
	КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	
	Для каждого СтрокаОстаток Из ДанныеПоОтгрузке Цикл
		
		СтрокаТоваров = ЭтотОбъект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТоваров, СтрокаОстаток);
		Если СтрокаОстаток.ОрдернаяСхемаПриОтгрузке
			И ПоОрдерам Тогда
			СтрокаТоваров.Количество = СтрокаОстаток.КоличествоСобрано;
			ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(СтрокаТоваров, СтруктураДействий, КэшированныеЗначения);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ВидыЗапасов

Процедура ЗаполнитьВидыЗапасов(Отказ)
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерВременныхТаблиц  = ВременныеТаблицыДанныхДокумента();
	ПерезаполнитьВидыЗапасов = ДополнительныеСвойства.Свойство("ПерезаполнитьВидыЗапасов");
	
	Если Не Проведен
		Или ПерезаполнитьВидыЗапасов
		Или ПроверитьИзменениеРеквизитовДокумента(МенеджерВременныхТаблиц)
		Или ПроверитьИзменениеТоваров(МенеджерВременныхТаблиц) Тогда
		
		ПараметрыЗаполнения = ПараметрыЗаполненияВидовЗапасов();
		
		ЗапасыСервер.ЗаполнитьВидыЗапасовПоТоварамОрганизаций(ЭтотОбъект,
																МенеджерВременныхТаблиц,
																Отказ,
																ПараметрыЗаполнения);
			
		ВидыЗапасов.Свернуть("АналитикаУчетаНоменклатуры, ВидЗапасов, НомерГТД", "Количество, КоличествоПоРНПТ, Сумма");
		
		ЗаполнитьДопКолонкиВидовЗапасов();
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПараметрыЗаполненияВидовЗапасов()
	
	ПараметрыЗаполнения = ЗапасыСервер.ПараметрыЗаполненияВидовЗапасов();
	ПараметрыЗаполнения.ПодбиратьВидыЗапасовПоИнтеркампани = Ложь;
	ПараметрыЗаполнения.СторнируемыйДокумент = СторнируемыйДокумент;
	
	//++ НЕ УТКА
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаДавальцу2_5 Тогда
		
		ПараметрыЗаполнения.ВладелецТовараВШапке = Ложь;
		
		ПараметрыЗаполнения.ПриНехваткеТоваровОрганизацииЗаполнятьВидамиЗапасовПоУмолчанию =
			ПраваПользователяПовтИсп.ОтключитьКонтрольПередачиПродукцииДавальцу();
		
		ПараметрыЗаполнения.ТаблицаРеквизитовВидовЗапасовПоУмолчанию = ТаблицаРеквизитовВидовЗапасовПоУмолчаниюЗаполнение();
		
		ПараметрыЗаполнения.ОтборыВидовЗапасов.ТипЗапасов = Перечисления.ТипыЗапасов.ПродукцияДавальца;
	
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДавальцу2_5 Тогда
		
		ОтборыВидовЗапасов = ПараметрыЗаполнения.ОтборыВидовЗапасов;
		ОтборыВидовЗапасов.Организация    = Организация;
		ОтборыВидовЗапасов.ВладелецТовара = Партнер;
		ОтборыВидовЗапасов.Договор        = Договор;
		ОтборыВидовЗапасов.ТипЗапасов     = Перечисления.ТипыЗапасов.МатериалДавальца;
		
	Иначе
	//-- НЕ УТКА
	
		ОтборыВидовЗапасов = ПараметрыЗаполнения.ОтборыВидовЗапасов;
		ОтборыВидовЗапасов.Организация = Организация;
		ОтборыВидовЗапасов.ВладелецТовара = Партнер;
		ОтборыВидовЗапасов.Договор = Договор;
		ОтборыВидовЗапасов.ТипЗапасов = Перечисления.ТипыЗапасов.ТоварНаХраненииСПравомПродажи;
	
	//++ НЕ УТКА
	КонецЕсли;
	//-- НЕ УТКА
	
	Возврат ПараметрыЗаполнения;
	
КонецФункции

Функция ПроверитьИзменениеРеквизитовДокумента(МенеджерВременныхТаблиц)
	
	ИменаРеквизитов = "Организация, Дата, Партнер, Склад";
	
	Возврат ЗапасыСервер.ПроверитьИзменениеРеквизитовДокумента(МенеджерВременныхТаблиц, Ссылка, ИменаРеквизитов);
	
КонецФункции

Функция ПроверитьИзменениеТоваров(МенеджерВременныхТаблиц)
	
	Запрос                         = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаТоваров.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ТаблицаТоваров.ДокументПоступления        КАК ДокументПоступления,
	|		ТаблицаТоваров.ЗаказКлиента               КАК ЗаказКлиента,
	|		ТаблицаТоваров.Количество                 КАК Количество,
	|		ТаблицаТоваров.Сумма                      КАК Сумма
	|	ИЗ
	|		ТаблицаТоваров КАК ТаблицаТоваров
	|	ГДЕ
	|		ТаблицаТоваров.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры,
	|		ТаблицаВидыЗапасов.ДокументПоступления,
	|		ТаблицаВидыЗапасов.ЗаказКлиента,
	|		-ТаблицаВидыЗапасов.Количество,
	|		-ТаблицаВидыЗапасов.Сумма
	|	ИЗ
	|		ТаблицаВидыЗапасов КАК ТаблицаВидыЗапасов) КАК ТаблицаТоваров
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаТоваров.Количество) <> 0
	|	ИЛИ СУММА(ТаблицаТоваров.Сумма) <> 0";
	
	РезультатЗапрос = Запрос.Выполнить();
	
	Возврат (Не РезультатЗапрос.Пустой());
	
КонецФункции

// Процедура заполняет дополнительные колонки табличной части 'ВидыЗапасов' документа.
//
Процедура ЗаполнитьДопКолонкиВидовЗапасов() Экспорт
	
	ТаблицаТовары = Товары.Выгрузить(, "АналитикаУчетаНоменклатуры, Упаковка, ДокументПоступления, ЗаказКлиента, КоличествоУпаковок, Количество, Цена, Сумма");
	ТаблицаТовары.Свернуть("АналитикаУчетаНоменклатуры, Упаковка, ДокументПоступления, ЗаказКлиента, Цена", "Количество, КоличествоУпаковок, Сумма");
	
	СтруктураПоиска = Новый Структура("АналитикаУчетаНоменклатуры");
	
	Для Каждого СтрокаТоваров Из ТаблицаТовары Цикл
		
		КоличествоТоваров  = СтрокаТоваров.Количество;
		КоличествоУпаковок = СтрокаТоваров.КоличествоУпаковок;
		
		Если КоличествоТоваров = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаТоваров);
		
		Для Каждого СтрокаЗапасов Из ВидыЗапасов.НайтиСтроки(СтруктураПоиска) Цикл
			
			Если СтрокаЗапасов.Количество = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Количество = Мин(КоличествоТоваров, СтрокаЗапасов.Количество);
			
			НоваяСтрока = ВидыЗапасов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаЗапасов);
			
			НоваяСтрока.Цена                = СтрокаТоваров.Цена;
			НоваяСтрока.Упаковка            = СтрокаТоваров.Упаковка;
			НоваяСтрока.ДокументПоступления = СтрокаТоваров.ДокументПоступления;
			НоваяСтрока.ЗаказКлиента        = СтрокаТоваров.ЗаказКлиента;
			НоваяСтрока.КоличествоУпаковок  = КоличествоУпаковок * Количество / КоличествоТоваров;
			НоваяСтрока.Количество          = Количество;
			НоваяСтрока.КоличествоПоРНПТ    = Количество * СтрокаЗапасов.КоличествоПоРНПТ / СтрокаЗапасов.Количество;
			НоваяСтрока.Сумма               = ?(Количество = КоличествоТоваров,
													СтрокаТоваров.Сумма,
													Количество * СтрокаТоваров.Сумма / КоличествоТоваров);
			
			СтрокаЗапасов.Количество		= СтрокаЗапасов.Количество - НоваяСтрока.Количество;
			СтрокаЗапасов.КоличествоПоРНПТ	= СтрокаЗапасов.КоличествоПоРНПТ - НоваяСтрока.КоличествоПоРНПТ;
			СтрокаЗапасов.Сумма				= СтрокаЗапасов.Сумма - НоваяСтрока.Сумма;
			
			КоличествоТоваров       = КоличествоТоваров - НоваяСтрока.Количество;
			КоличествоУпаковок      = КоличествоУпаковок - НоваяСтрока.КоличествоУпаковок;
			СтрокаТоваров.Сумма     = СтрокаТоваров.Сумма - НоваяСтрока.Сумма;
			
			Если КоличествоТоваров = 0 Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Отбор = Новый Структура("Количество", 0);
	МассивУдаляемыхСтрок = ВидыЗапасов.НайтиСтроки(Отбор);
	
	Для Каждого СтрокаТаблицы Из МассивУдаляемыхСтрок Цикл
		ВидыЗапасов.Удалить(СтрокаТаблицы);
	КонецЦикла;
	
КонецПроцедуры

// Заполняет аналитики учета номенклатуры в табличных частях документа, хранящих информацию о товарах.
// Если параметр не передан, тогда будет выполнено заполнение данных в табличных частях документа.
//
// Параметры:
//	ТаблицыДокумента - см. Документы.ОтгрузкаТоваровСХранения.КоллекцияТабличныхЧастейТоваров.
//
Процедура ЗаполнитьАналитикиУчетаНоменклатурыВТабличныхЧастяхТоваров(ТаблицыДокумента = Неопределено)
	
	Если ТаблицыДокумента = Неопределено Тогда
		ТаблицыДокумента = Документы.ОтгрузкаТоваровСХранения.КоллекцияТабличныхЧастейТоваров();
		ЗаполнитьЗначенияСвойств(ТаблицыДокумента, ЭтотОбъект);
	КонецЕсли;
	
	ТаблицаТовары = ТаблицыДокумента.Товары;
	
	//++ НЕ УТКА
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаДавальцу2_5
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДавальцу2_5 Тогда
		ХозяйственнаяОперацияДляПроведения = ХозяйственнаяОперация;
	Иначе
	//-- НЕ УТКА
		ХозяйственнаяОперацияДляПроведения =
			Перечисления.ХозяйственныеОперации.ОтгрузкаПринятыхСПравомПродажиТоваровСХранения;
	//++ НЕ УТКА
	КонецЕсли;
	//-- НЕ УТКА
	
	// Если Склад - группа, то для аналитики учета номенклатуры склад берем из ТЧ
	ИменаПолей = РегистрыСведений.АналитикаУчетаНоменклатуры.ИменаПолейКоллекцииПоУмолчанию();
	РеквизитыСклада = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Склад, "ЭтоГруппа, ВыборГруппы");
	
	Если РеквизитыСклада.ЭтоГруппа
		И РеквизитыСклада.ВыборГруппы = Перечисления.ВыборГруппыСкладов.РазрешитьВЗаказахИНакладных Тогда
		ИменаПолей.Вставить("Произвольный", "Склад");
	КонецЕсли;
	
	МестаУчета =
		РегистрыСведений.АналитикаУчетаНоменклатуры.МестаУчета(
			ХозяйственнаяОперацияДляПроведения,
			Склад,
			Подразделение,
			Партнер);
	
	РегистрыСведений.АналитикаУчетаНоменклатуры.ЗаполнитьВКоллекции(ТаблицаТовары, МестаУчета, ИменаПолей);
	
КонецПроцедуры

//++ НЕ УТКА
Функция ТаблицаРеквизитовВидовЗапасовПоУмолчаниюЗаполнение()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки                КАК НомерСтроки,
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаТоваров.Номенклатура               КАК Номенклатура,
	|	ТаблицаТоваров.Назначение                 КАК Назначение
	|ПОМЕСТИТЬ ВтТаблицаТоваров
	|ИЗ
	|	&ТаблицаТоваров КАК ТаблицаТоваров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки                           КАК НомерСтроки,
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры            КАК АналитикаУчетаНоменклатуры,
	|	&Организация                                         КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)        КАК ВидЗапасов,
	|	ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)          КАК НомерГТД,
	|	ВЫБОР
	|		КОГДА СпрНоменклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
	|		ИНАЧЕ СпрНазначения.Партнер
	|	КОНЕЦ                                                КАК ВладелецТовара,
	|	ВЫБОР
	|		КОГДА СпрНоменклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|		ИНАЧЕ СпрНазначения.Договор.Контрагент
	|	КОНЕЦ                                                КАК Контрагент,
	|	ВЫБОР
	|		КОГДА СпрНоменклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|		ИНАЧЕ СпрНазначения.Договор
	|	КОНЕЦ                                                КАК Договор,
	|	ВЫБОР
	|		КОГДА СпрНоменклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПродукцияДавальца)
	|	КОНЕЦ                                                КАК ТипЗапасов
	|ИЗ
	|	ВтТаблицаТоваров КАК ТаблицаТоваров
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|	ПО СпрНоменклатура.Ссылка = ТаблицаТоваров.Номенклатура
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Назначения КАК СпрНазначения
	|	ПО СпрНазначения.Ссылка = ТаблицаТоваров.Назначение
	|";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр(
		"ТаблицаТоваров", Товары.Выгрузить(, "НомерСтроки, АналитикаУчетаНоменклатуры, Номенклатура, Назначение"));
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции
//-- НЕ УТКА

#КонецОбласти

#КонецОбласти

#КонецЕсли
