#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли; 
	
	// Порядок выплаты заполняем значением по умолчанию 
	// (важно для документов, созданных до появления реквизита ПорядокВыплаты).
	Если ПорядокВыплаты = Перечисления.ХарактерВыплатыЗарплаты.ПустаяСсылка() Тогда
		ПорядокВыплаты = Перечисления.ХарактерВыплатыЗарплаты.Зарплата;
	КонецЕсли;
	
	ПериодыПерерасчета = "";
	
	ПериодыПерерасчетов = НачисленияПерерасчет.Выгрузить(, "ДатаНачала");
	ПериодыПерерасчетов.Свернуть("ДатаНачала");
	ПериодыПерерасчетов.Сортировать("ДатаНачала");
	
	НепрерывныеПериоды = Новый ТаблицаЗначений;
	НепрерывныеПериоды.Колонки.Добавить("Начало", Новый ОписаниеТипов("Дата"));
	НепрерывныеПериоды.Колонки.Добавить("Окончание", Новый ОписаниеТипов("Дата"));
	
	НепрерывныйПериод = Неопределено;
	Для каждого СтрокаПериода Из ПериодыПерерасчетов Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаПериода.ДатаНачала) Тогда
			Продолжить;
		КонецЕсли;
		
		ПериодПерерасчета = НачалоМесяца(СтрокаПериода.ДатаНачала);
		
		Если НепрерывныйПериод = Неопределено ИЛИ НепрерывныйПериод.Окончание + 1 < ПериодПерерасчета Тогда
			НепрерывныйПериод = НепрерывныеПериоды.Добавить();
			НепрерывныйПериод.Начало = ПериодПерерасчета;
		КонецЕсли;
		
		НепрерывныйПериод.Окончание = КонецМесяца(ПериодПерерасчета);
		
	КонецЦикла;
	
	ДобавитьЗапятую = Ложь;
	Для каждого НепрерывныйПериод Из НепрерывныеПериоды Цикл
		
		Если ДобавитьЗапятую Тогда
			ПериодыПерерасчета = ПериодыПерерасчета + ", ";
		Иначе
			ДобавитьЗапятую = Истина;
		КонецЕсли; 
		
		Если НепрерывныйПериод.Начало = НачалоМесяца(НепрерывныйПериод.Окончание) Тогда
			ПредставлениеПериода = Формат(НепрерывныйПериод.Начало, "ДФ='ММММ гггг'");
		Иначе
			ПредставлениеПериода = Формат(НепрерывныйПериод.Начало, "ДФ='ММММ гггг'") + " - " + Формат(НепрерывныйПериод.Окончание, "ДФ='ММММ гггг'");
		КонецЕсли;
		
		ПериодыПерерасчета = ПериодыПерерасчета + ПредставлениеПериода;
		
	КонецЦикла;
	
	ДокументПроведен =  Неопределено;
	ДополнительныеСвойства.Свойство("Проведен", ДокументПроведен);
	
	РасчетЗарплатыРасширенный.ЗаполнитьИсходныйДокумент(ЭтотОбъект);

	Документы.НачислениеЗарплаты.ЗаполнитьПредставлениеРаспределенияРезультатовРасчета(ЭтотОбъект);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	РасчетЗарплатыРасширенный.ЗаполнитьИсходныйДокументПриКопировании(ЭтотОбъект, ОбъектКопирования.Ссылка);

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ПроверятьДатуВыплатыДокумента = УчетНДФЛ.ДоходыУчитываютсяТолькоПоДатеВыплаты(МесяцНачисления, -1) Или РежимДоначисления;
	
	Если ПроверятьДатуВыплатыДокумента Тогда
		ЗарплатаКадры.ПроверитьДатуВыплаты(ЭтотОбъект, Отказ);
	КонецЕсли;
	
	Если Не РежимДоначисления Тогда
		Для каждого СтрокаНачислений Из НачисленияПоДоговорам Цикл
			
			ЗарплатаКадры.ПроверитьКорректностьДаты(Ссылка, СтрокаНачислений.ПланируемаяДатаВыплаты, "Объект.НачисленияПоДоговорам[" + Формат(СтрокаНачислений.НомерСтроки - 1, "ЧГ=") + "].ПланируемаяДатаВыплаты",
				Отказ, НСтр("ru = 'Дата выплаты';
							|en = 'Payment date'"), Дата, НСтр("ru = 'даты документа';
															|en = 'document dates'"));
			
		КонецЦикла;
	КонецЕсли;
	
	УчетНДФЛДокументы.ПроверитьЗаполненияКодовВычетаДокумента(ЭтотОбъект, Отказ);
	
	ПроверитьПериодДействияНачислений(Отказ);
	
	ПроверитьНачисленияПоДоговорам(Отказ);
	
	ПроверитьРаспределениеПоИсточникамФинансирования(Отказ);
	
	ПроверитьРаспределениеПоТерриториямУсловиямТруда(Отказ);
	
	Если Не ПроверятьДатуВыплатыДокумента Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ПорядокВыплаты");
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ПланируемаяДатаВыплаты");
	КонецЕсли;
	
	ПроверитьВыплатуПособийУчастникомПроектаПоПрямымВыплатамФСС(Отказ);
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ОтсутствияСотрудников") Тогда
		
		Сотрудники = ОбщегоНазначения.ВыгрузитьКолонку(Начисления, "Сотрудник", Истина);
		
		МодульОтсутствияСотрудников = ОбщегоНазначения.ОбщийМодуль("ОтсутствияСотрудников");
		МодульОтсутствияСотрудников.ПроверитьПериодыОтсутствияПоСпискуСотрудников(Сотрудники, МесяцНачисления, Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Документы.НачислениеЗарплаты.ПровестиПоУчетам(Ссылка, РежимПроведения, Отказ, Неопределено, Движения, ЭтотОбъект, ДополнительныеСвойства);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПерерасчетЗарплаты.ВосстановитьПерерасчеты(Ссылка, Организация);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// Вспомогательные процедуры и функции.

Процедура ПроверитьПериодДействияНачислений(Отказ)
	
	ПараметрыПроверки = РасчетЗарплатыРасширенный.ПараметрыПроверкиПериодаДействия();
	ПараметрыПроверки.Ссылка = Ссылка;
	
	ПроверяемыеКоллекции = Новый Массив;
	ПроверяемыеКоллекции.Добавить(РасчетЗарплатыРасширенный.ОписаниеКоллекцииДляПроверкиПериодаДействия("Начисления", НСтр("ru = 'Начисления';
																															|en = 'Accruals'")));
	ПроверяемыеКоллекции.Добавить(РасчетЗарплатыРасширенный.ОписаниеКоллекцииДляПроверкиПериодаДействия("НачисленияПерерасчет", НСтр("ru = 'Доначисления, перерасчеты';
																																	|en = 'Additional accruals, recalculations'")));
	ПроверяемыеКоллекции.Добавить(РасчетЗарплатыРасширенный.ОписаниеКоллекцииДляПроверкиПериодаДействия("Пособия", НСтр("ru = 'Пособия';
																														|en = 'Allowances'")));
	ПроверяемыеКоллекции.Добавить(РасчетЗарплатыРасширенный.ОписаниеКоллекцииДляПроверкиПериодаДействия("ПособияПерерасчет", НСтр("ru = 'Перерасчет пособий';
																																	|en = 'Allowance recalculation'")));
	ПроверяемыеКоллекции.Добавить(РасчетЗарплатыРасширенный.ОписаниеКоллекцииДляПроверкиПериодаДействия("Удержания", НСтр("ru = 'Удержания';
																															|en = 'Deductions'"), "Удержание"));
	
	РасчетЗарплатыРасширенный.ПроверитьПериодДействияВКоллекцияхНачислений(ЭтотОбъект, ПараметрыПроверки, ПроверяемыеКоллекции, Отказ);
	
КонецПроцедуры

Процедура ПроверитьВыплатуПособийУчастникомПроектаПоПрямымВыплатамФСС(Отказ)
	
	ЭтоУчастникПилотногоПроектаБезДатыВступленияВПроект = ПрямыеВыплатыПособийСоциальногоСтрахования.ЭтоУчастникПилотногоПроектаБезДатыВступленияВПроект(Организация, МесяцНачисления);
	Если ЭтоУчастникПилотногоПроектаБезДатыВступленияВПроект Тогда
		ТекстСообщения = ПрямыеВыплатыПособийСоциальногоСтрахования.ТекстСообщенияЭтоУчастникПилотногоПроектаБезДатыВступленияВПроект(Организация);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "Объект.Организация" , , Отказ);
	Иначе
		
		ВыплачиваемыеПособия = Начисления.Выгрузить(, "Начисление, Результат");
		ВыплачиваемыеПособия.Свернуть("Начисление", "Результат");
		
		ВыплачиваемыеПособияДляПроверки = ПрямыеВыплатыПособийСоциальногоСтрахования.ПустаяТаблицаДляПроверкиОплатыПособийУчастникомПилотногоПроектаФСС();
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ВыплачиваемыеПособия, ВыплачиваемыеПособияДляПроверки);
		   
		ТекстСообщения = ПрямыеВыплатыПособийСоциальногоСтрахования.ПроверитьОплатуПособийУчастникомПилотногоПроектаФСС(Организация, МесяцНачисления, ВыплачиваемыеПособияДляПроверки);
		Если ЗначениеЗаполнено(ТекстСообщения) Тогда
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "МесяцНачисленияСтрокой", , Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьНачисленияПоДоговорам(Отказ)
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНачисленияПоДоговорам") Тогда
		Возврат;
	КонецЕсли;
		
	Для Каждого СтрокаПоДоговору Из НачисленияПоДоговорам Цикл
		Если Не ЗначениеЗаполнено(СтрокаПоДоговору.ДокументОснование) Тогда
			Продолжить;
		КонецЕсли;
		СотрудникДоговора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаПоДоговору.ДокументОснование, "Сотрудник");
		Если СтрокаПоДоговору.Сотрудник <> СотрудникДоговора Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'В строке %1, табличной части ""Начисления по договорам"" для сотрудника %2 указан договор другого сотрудника (%3)';
					|en = 'In row %1 of the ""Accruals under contracts"" tabular section, a contract of another employee (%3) was specified for employee %2'"),
				СтрокаПоДоговору.НомерСтроки,
				СтрокаПоДоговору.Сотрудник,
				СотрудникДоговора);
			ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения, , "Объект.НачисленияПоДоговорам[" + Формат(СтрокаПоДоговору.НомерСтроки - 1, "ЧН=; ЧГ=") + "].ДокументОснование" , , Отказ);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьРаспределениеПоИсточникамФинансирования(Отказ)
	
	Если РежимДоначисления Тогда
		
		ИменаТаблицРаспределяемыхПоСтатьямФинансирования =
			"Начисления,
			|НачисленияПерерасчет,
			|НачисленияПоДоговорам,
			|Пособия,
			|ПособияПерерасчет,
			|Удержания,
			|НДФЛ,
			|ПогашениеЗаймов,
			|КорректировкиВыплаты";
		
	Иначе
		
		ИменаТаблицРаспределяемыхПоСтатьямФинансирования = "";
		
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОперацииРасчетаЗарплаты") Тогда
			
			Модуль = ОбщегоНазначения.ОбщийМодуль("ОперацииРасчетаЗарплаты");
			
			Если ДополнительныеСвойства.Свойство("ВидОперации") Тогда
				ВидОперации = ДополнительныеСвойства.ВидОперации;
			Иначе
				ВидОперации = Модуль.ВидОперацииДокумента(Ссылка);
			КонецЕсли;
				
			ДанныеВидаОперации = Модуль.ДанныеВидаОперации(ВидОперации);
			
		Иначе
			
			ДанныеВидаОперации = Новый Структура;
			ДанныеВидаОперации.Вставить("ИспользоватьНачисление", Истина);
			ДанныеВидаОперации.Вставить("ИспользоватьДоговоры", Истина);
			ДанныеВидаОперации.Вставить("ИспользоватьПособия", Истина);
			ДанныеВидаОперации.Вставить("ИспользоватьЛьготы", Истина);
			ДанныеВидаОперации.Вставить("ИспользоватьНДФЛ", Истина);
			ДанныеВидаОперации.Вставить("ИспользоватьУдержания", Истина);
			ДанныеВидаОперации.Вставить("ИспользоватьЗаймы", Истина);
			
		КонецЕсли;
		
		Если ДанныеВидаОперации.ИспользоватьНачисление Тогда
			
			ИменаТаблицРаспределяемыхПоСтатьямФинансирования = ?(ПустаяСтрока(ИменаТаблицРаспределяемыхПоСтатьямФинансирования), "", ИменаТаблицРаспределяемыхПоСтатьямФинансирования + ",")
				+ "Начисления,НачисленияПерерасчет";
			
		КонецЕсли;
		
		Если ДанныеВидаОперации.ИспользоватьДоговоры Тогда
			
			ИменаТаблицРаспределяемыхПоСтатьямФинансирования = ?(ПустаяСтрока(ИменаТаблицРаспределяемыхПоСтатьямФинансирования), "", ИменаТаблицРаспределяемыхПоСтатьямФинансирования + ",")
				+ "НачисленияПоДоговорам";
			
		КонецЕсли;
		
		Если ДанныеВидаОперации.ИспользоватьПособия Тогда
			
			ИменаТаблицРаспределяемыхПоСтатьямФинансирования = ?(ПустаяСтрока(ИменаТаблицРаспределяемыхПоСтатьямФинансирования), "", ИменаТаблицРаспределяемыхПоСтатьямФинансирования + ",")
				+ "Пособия,ПособияПерерасчет";
			
		КонецЕсли;
		
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ЛьготыСотрудников") Тогда
			МодульЛьготыСотрудников = ОбщегоНазначения.ОбщийМодуль("ЛьготыСотрудников");
			МодульЛьготыСотрудников.ДополнитьИменаТаблицРаспределяемыхПоСтатьямФинансирования(ИменаТаблицРаспределяемыхПоСтатьямФинансирования, ДанныеВидаОперации);
		КонецЕсли;
		
		Если ДанныеВидаОперации.ИспользоватьНДФЛ Тогда
			
			ИменаТаблицРаспределяемыхПоСтатьямФинансирования = ?(ПустаяСтрока(ИменаТаблицРаспределяемыхПоСтатьямФинансирования), "", ИменаТаблицРаспределяемыхПоСтатьямФинансирования + ",")
				+ "НДФЛ,КорректировкиВыплаты";
			
		КонецЕсли;
		
		Если ДанныеВидаОперации.ИспользоватьУдержания Тогда
			
			ИменаТаблицРаспределяемыхПоСтатьямФинансирования = ?(ПустаяСтрока(ИменаТаблицРаспределяемыхПоСтатьямФинансирования), "", ИменаТаблицРаспределяемыхПоСтатьямФинансирования + ",")
				+ "Удержания";
			
		КонецЕсли;
		
		Если ДанныеВидаОперации.ИспользоватьЗаймы Тогда
			
			ИменаТаблицРаспределяемыхПоСтатьямФинансирования = ?(ПустаяСтрока(ИменаТаблицРаспределяемыхПоСтатьямФинансирования), "", ИменаТаблицРаспределяемыхПоСтатьямФинансирования + ",")
				+ "ПогашениеЗаймов";
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ПустаяСтрока(ИменаТаблицРаспределяемыхПоСтатьямФинансирования) Тогда
		
		ОтражениеЗарплатыВБухучетеРасширенный.ПроверитьРезультатыРаспределенияНачисленийУдержанийОбъекта(
			ЭтотОбъект, ИменаТаблицРаспределяемыхПоСтатьямФинансирования, Отказ);
			
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьРаспределениеПоТерриториямУсловиямТруда(Отказ)
	
	ИменаТаблиц = "Начисления,НачисленияПерерасчет,Пособия,ПособияПерерасчет";
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ЛьготыСотрудников") Тогда
		МодульЛьготыСотрудников = ОбщегоНазначения.ОбщийМодуль("ЛьготыСотрудников");
		МодульЛьготыСотрудников.ДополнитьИменаТаблицРаспределенияПоТерриториямУсловиямТруда(ИменаТаблиц);
	КонецЕсли;
	
	РасчетЗарплатыРасширенный.ПроверитьРаспределениеПоТерриториямУсловиямТрудаДокумента(ЭтотОбъект, ИменаТаблиц, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли