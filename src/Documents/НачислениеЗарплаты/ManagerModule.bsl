#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры
// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ДляВсехСтрок( ЗначениеРазрешено(ФизическиеЛица.ФизическоеЛицо, NULL КАК ИСТИНА)
	|	) И ЗначениеРазрешено(Организация)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

// Проводит документ по учетам. Если в параметре ВидыУчетов передано Неопределено, то документ проводится по всем учетам.
// Процедура вызывается из обработки проведения и может вызываться из вне.
// 
// Параметры:
//  ДокументСсылка	- ДокументСсылка.НачислениеЗарплаты - Ссылка на документ
//  РежимПроведения - РежимПроведенияДокумента - Режим проведения документа (оперативный, неоперативный)
//  Отказ 			- Булево - Признак отказа от выполнения проведения
//  ВидыУчетов 		- Строка - Список видов учета, по которым необходимо провести документ. Если параметр пустой или Неопределено, то документ проведется по всем учетам
//  Движения 		- Коллекция движений документа - Передается только при вызове из обработки проведения документа
//  Объект			- ДокументОбъект.НачислениеЗарплаты - Передается только при вызове из обработки проведения документа
//  ДополнительныеПараметры - Структура - Дополнительные параметры, необходимые для проведения документа.
//
Процедура ПровестиПоУчетам(ДокументСсылка, РежимПроведения, Отказ, ВидыУчетов = Неопределено, Движения = Неопределено, Объект = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ДополнительныеПараметры <> Неопределено И ДополнительныеПараметры.Свойство("ВидыУчетов") Тогда 
		ВидыУчетов = ДополнительныеПараметры.ВидыУчетов;
	КонецЕсли;
	
	ВидОперации = Неопределено;
	Если ДополнительныеПараметры <> Неопределено И ДополнительныеПараметры.Свойство("ВидОперации") Тогда 
		ВидОперации = ДополнительныеПараметры.ВидОперации;
	КонецЕсли;
			
	ВременнаяРегистрацияДвижений = Ложь;
	Если ДополнительныеПараметры <> Неопределено И ДополнительныеПараметры.Свойство("ВременнаяРегистрацияДвижений") Тогда
		ВременнаяРегистрацияДвижений = Истина;
	КонецЕсли;
	
	СтруктураВидовУчета = ПроведениеРасширенныйСервер.СтруктураВидовУчета();
	ПроведениеРасширенныйСервер.ПодготовитьНаборыЗаписейКРегистрацииДвиженийПоВидамУчета(РежимПроведения, ДокументСсылка, СтруктураВидовУчета, ВидыУчетов, Движения, Объект, Отказ);
	
	// Предусмотрен режим проведения по отдельным физическим лицам.
	СписокФизическихЛиц = Неопределено;
	Если ДополнительныеПараметры <> Неопределено  
		И ДополнительныеПараметры.Свойство("ФизическиеЛица")
		И ДополнительныеПараметры.ФизическиеЛица.Количество() > 0 Тогда
		СписокФизическихЛиц = ДополнительныеПараметры.ФизическиеЛица
	КонецЕсли;
	
	РеквизитыДляПроведения = РеквизитыДляПроведения(ДокументСсылка);
	ДанныеДляПроведения = ДанныеДляПроведения(РеквизитыДляПроведения, РеквизитыДляПроведения.МесяцНачисления, СписокФизическихЛиц, СтруктураВидовУчета);
	
	Если СтруктураВидовУчета.Начисления Тогда
		РасчетЗарплатыРасширенный.СформироватьДвиженияНачислений(Движения, Отказ, РеквизитыДляПроведения.Организация, КонецМесяца(РеквизитыДляПроведения.МесяцНачисления), ДанныеДляПроведения.Начисления, ДанныеДляПроведения.ПоказателиНачислений, Истина);
		РасчетЗарплатыРасширенный.СформироватьДвиженияРаспределенияПоТерриториямУсловиямТруда(Движения, Отказ, РеквизитыДляПроведения.Ссылка, РеквизитыДляПроведения.РаспределениеПоТерриториямУсловиямТруда);
		РасчетЗарплатыРасширенный.СформироватьДвиженияРаспределенияРезультатовНачислений(Движения, Отказ, РеквизитыДляПроведения.Ссылка, РеквизитыДляПроведения.РаспределениеРезультатовНачислений);
		РасчетЗарплаты.СформироватьДвиженияНачисленийПоДоговорам(Движения, Отказ, РеквизитыДляПроведения.Организация, НачалоМесяца(РеквизитыДляПроведения.МесяцНачисления), ДанныеДляПроведения.НачисленияПоДоговорам);
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.УправленческаяЗарплата") Тогда
			Модуль = ОбщегоНазначения.ОбщийМодуль("УправленческаяЗарплата");
			Модуль.СформироватьДвиженияНачислений(Движения, Отказ, ДанныеДляПроведения, РеквизитыДляПроведения.МесяцНачисления);
		КонецЕсли;
		ПерерасчетЗарплаты.СформироватьДвиженияИсходныеДанныхПерерасчетов(Движения, РеквизитыДляПроведения.Организация, РеквизитыДляПроведения.МесяцНачисления, ДанныеДляПроведения.Начисления);
		ПроверитьПересечениеФактическогоПериодаДействия(ДокументСсылка, РеквизитыДляПроведения, Отказ);
	КонецЕсли;
	
	Если СтруктураВидовУчета.ОстальныеВидыУчета Тогда
		
		РасчетЗарплатыРасширенный.СформироватьДвиженияУдержаний(Движения, Отказ, РеквизитыДляПроведения.Организация, КонецМесяца(РеквизитыДляПроведения.МесяцНачисления), ДанныеДляПроведения.Удержания, ДанныеДляПроведения.ПоказателиУдержаний);
		ИсполнительныеЛисты.СформироватьУдержанияПоИсполнительнымДокументам(Движения, ДанныеДляПроведения.УдержанияПоИсполнительнымДокументам);
		РасчетЗарплатыРасширенный.СформироватьДвиженияУдержанийДоПределаПоСотрудникам(Движения, Отказ, РеквизитыДляПроведения.МесяцНачисления, ДанныеДляПроведения.УдержанияДоПределаПоСотрудникам, Ложь);
		
		ДанныеМежрасчетногоПериода = ?(РеквизитыДляПроведения.РежимДоначисления, Истина, Ложь);
		РасчетЗарплатыРасширенный.СформироватьЗадолженностьПоУдержаниямФизическихЛиц(Движения, ДанныеДляПроведения.ЗадолженностьПоУдержаниям, Ложь, ДанныеМежрасчетногоПериода);
		РасчетЗарплатыРасширенный.СформироватьДополнениеРасчетнойБазыУдержаний(Движения, ДанныеДляПроведения.ДополнениеРасчетнойБазыУдержаний);
		
		// Формирование движение в учетах: НДФЛ, учет начисленной зарплаты, бухучет.
		// Важна последовательность регистрации в учетах.

		РегистрироватьСуммыНалога = Истина;
		РегистрироватьСуммыНалогаНаМатериальнуюВыгоду = Истина;
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОперацииРасчетаЗарплаты") Тогда 
			Модуль = ОбщегоНазначения.ОбщийМодуль("ОперацииРасчетаЗарплаты");
			Если Не ЗначениеЗаполнено(ВидОперации) Тогда
				ВидОперации = Модуль.ВидОперацииДокумента(ДокументСсылка);
			КонецЕсли;
			ДанныеВидаОперации = Модуль.ДанныеВидаОперации(ВидОперации);
			РегистрироватьСуммыНалога = ДанныеВидаОперации.ИспользоватьНДФЛ;
			РегистрироватьСуммыНалогаНаМатериальнуюВыгоду = ДанныеВидаОперации.ИспользоватьЗаймы;
		КонецЕсли;
		ОкончательныйРасчет = Не РеквизитыДляПроведения.РежимДоначисления;
		
		ПланируемаяДатаВыплаты = ?(УчетНДФЛ.ДоходыУчитываютсяТолькоПоДатеВыплаты(РеквизитыДляПроведения.МесяцНачисления, -1), РеквизитыДляПроведения.ПланируемаяДатаВыплаты, РасчетЗарплатыРасширенныйКлиентСервер.ПланируемаяДатаВыплатыЗарплаты(РеквизитыДляПроведения.Организация, РеквизитыДляПроведения.МесяцНачисления));

		// Заполним описание данных для проведения в учете начисленной зарплаты.
		ДанныеДляПроведенияУчетЗарплаты = ОтражениеЗарплатыВУчете.ОписаниеДанныеДляПроведения();
		ДанныеДляПроведенияУчетЗарплаты.Движения 				= Движения;
		ДанныеДляПроведенияУчетЗарплаты.Организация 			= РеквизитыДляПроведения.Организация;
		ДанныеДляПроведенияУчетЗарплаты.ПериодРегистрации 		= РеквизитыДляПроведения.МесяцНачисления;
		ДанныеДляПроведенияУчетЗарплаты.ПланируемаяДатаВыплаты	= ПланируемаяДатаВыплаты;
		ДанныеДляПроведенияУчетЗарплаты.ПорядокВыплаты 			= РеквизитыДляПроведения.ПорядокВыплаты;
		ДанныеДляПроведенияУчетЗарплаты.ОкончательныйРасчет		= ОкончательныйРасчет;
		ДанныеДляПроведенияУчетЗарплаты.МенеджерВременныхТаблиц = ДанныеДляПроведения.МенеджерВременныхТаблиц;
		
		// - Регистрация договоров в учете начислений и удержаний.
		УчетНачисленнойЗарплаты.ЗарегистрироватьНачисления(ДанныеДляПроведенияУчетЗарплаты, Отказ, ДанныеДляПроведения.НачисленияПоДоговорамСРаспределением, Неопределено);
		
		// - Регистрация начислений в учете начислений и удержаний.
		УчетНачисленнойЗарплаты.ЗарегистрироватьНачисления(ДанныеДляПроведенияУчетЗарплаты, Отказ, ДанныеДляПроведения.НачисленияПоСотрудникам, Неопределено);
		УчетНачисленнойЗарплаты.ЗарегистрироватьОтработанноеВремя(ДанныеДляПроведенияУчетЗарплаты, Отказ, ДанныеДляПроведения.ОтработанноеВремяПоСотрудникам, Истина);
			
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.УправленческаяЗарплата") Тогда
			Модуль = ОбщегоНазначения.ОбщийМодуль("УправленческаяЗарплата");
			Модуль.ЗарегистрироватьНачисленияУдержания(Движения, Отказ, ДанныеДляПроведения, РеквизитыДляПроведения.МесяцНачисления, 
				РеквизитыДляПроведения.ПорядокВыплаты, РеквизитыДляПроведения.ПланируемаяДатаВыплаты);
			Модуль.ЗарегистрироватьОтработанноеВремя(Движения, Отказ, ДанныеДляПроведения, РеквизитыДляПроведения.МесяцНачисления, РеквизитыДляПроведения.ПорядокВыплаты);
		КонецЕсли;	
		
		// - Регистрация начислений в бухучете.
		ОтражениеЗарплатыВБухучетеРасширенный.ЗарегистрироватьНачисленияУдержания(ДанныеДляПроведенияУчетЗарплаты, Отказ,
				ДанныеДляПроведения.НачисленияПоСотрудникам, Неопределено, Неопределено);
				
		ОтражениеЗарплатыВБухучетеРасширенный.ЗарегистрироватьНачисленияУдержания(ДанныеДляПроведенияУчетЗарплаты, Отказ,
				ДанныеДляПроведения.НачисленияПоДоговорамСРаспределением, Неопределено, Неопределено);
				
		// НДФЛ
		ДатаОперации = КонецМесяца(РеквизитыДляПроведения.МесяцНачисления);
		Если РеквизитыДляПроведения.РежимДоначисления Тогда
			ДатаОперации = УчетНДФЛ.ДатаОперацииПоДокументу(РеквизитыДляПроведения.Дата, РеквизитыДляПроведения.МесяцНачисления);
		КонецЕсли;
		
		// - Регистрация начислений в учете НДФЛ.
		УчетНДФЛРасширенный.СформироватьДоходыНДФЛПоНачислениям(Движения, Отказ, РеквизитыДляПроведения.Организация, ДатаОперации,
			ПланируемаяДатаВыплаты, ДанныеДляПроведения.МенеджерВременныхТаблиц,
			РеквизитыДляПроведения.МесяцНачисления, , ОкончательныйРасчет, "ВТНачисленияДляУчетаДоходовНДФЛ", ДокументСсылка);

		// - Регистрация договоров в учете НДФЛ.
		Если РасчетЗарплатыРасширенный.ВыплатыПоДоговорамГПХМогутНеОблагатьсяНДФЛ() Тогда
			ОбратныйИндекс = ДанныеДляПроведения.НачисленияПоДоговорамДляУчетаНДФЛ.Количество();
			Пока ОбратныйИндекс > 0 Цикл
				ОбратныйИндекс = ОбратныйИндекс - 1;
				Если ДанныеДляПроведения.НачисленияПоДоговорамДляУчетаНДФЛ[ОбратныйИндекс].НеОблагаетсяНДФЛ Тогда
					ДанныеДляПроведения.НачисленияПоДоговорамДляУчетаНДФЛ.Удалить(ОбратныйИндекс);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ГрантыНеоблагаемыеНДФЛ") Тогда
			Модуль = ОбщегоНазначения.ОбщийМодуль("ГрантыНеоблагаемыеНДФЛ");
			Модуль.ОтобратьДоходыПоДоговорамПодрядаОблагаемыеНДФЛ(ДанныеДляПроведения.МенеджерВременныхТаблиц, ДанныеДляПроведения.НачисленияПоДоговорамДляУчетаНДФЛ);
		КонецЕсли;
		
		УчетНДФЛ.СформироватьДоходыНДФЛПоКодамДоходовИзТаблицыЗначений(Движения, Отказ, РеквизитыДляПроведения.Организация, КонецМесяца(РеквизитыДляПроведения.МесяцНачисления),
			ДанныеДляПроведения.НачисленияПоДоговорамДляУчетаНДФЛ, , ОкончательныйРасчет, ДокументСсылка);
		
		УчетНДФЛ.СформироватьДоходыНДФЛПоКодамДоходовИзТаблицыЗначений(Движения, Отказ, РеквизитыДляПроведения.Организация, КонецМесяца(РеквизитыДляПроведения.МесяцНачисления),
			ДанныеДляПроведения.МатериальнаяВыгода, , ОкончательныйРасчет, ДокументСсылка);
			
		Если РегистрироватьСуммыНалога Тогда
			ПланируемаяДатаВыплаты = НачалоДня(КонецМесяца(РеквизитыДляПроведения.МесяцНачисления));
			УчетНДФЛ.СформироватьНалогиВычеты(Движения, Отказ, РеквизитыДляПроведения.Организация, ДатаОперации,
				ДанныеДляПроведения.НДФЛ, , ОкончательныйРасчет, ПланируемаяДатаВыплаты);
			РазрешитьПересмотрДатыПолученияДоходаПоДоговорам(Движения.РасчетыНалогоплательщиковСБюджетомПоНДФЛ, ДанныеДляПроведения.НачисленияПоДоговорамДляУчетаНДФЛ, ПланируемаяДатаВыплаты);
			УчетНДФЛ.СформироватьДокументыУчтенныеПриРасчетеДляМежрасчетногоДокумента(Движения, Отказ, РеквизитыДляПроведения.Организация, ДанныеДляПроведения.НДФЛ.ВыгрузитьКолонку("ФизическоеЛицо"), РеквизитыДляПроведения.Ссылка); 	
			Если ОкончательныйРасчет Тогда
				УчетНДФЛ.СформироватьДокументыУчтенныеПриРасчетеПриОкончательномРасчете(Движения, Отказ, РеквизитыДляПроведения.Организация, ДанныеДляПроведения.УчтенныеПриРасчетеДокументы);
			КонецЕсли;
		КонецЕсли;
		
		Если РегистрироватьСуммыНалогаНаМатериальнуюВыгоду Тогда
			// - Регистрация материальной выгоды в учете НДФЛ.
			УчетНДФЛ.СформироватьНалогиВычеты(Движения, Отказ, РеквизитыДляПроведения.Организация,
				КонецМесяца(РеквизитыДляПроведения.МесяцНачисления), ДанныеДляПроведения.НалогНаМатериальнуюВыгоду, , ОкончательныйРасчет);
		КонецЕсли;
			
		МесяцСоцВычета = ?(УчетНДФЛ.ДоходыУчитываютсяТолькоПоДатеВыплаты(РеквизитыДляПроведения.МесяцНачисления, -1), РеквизитыДляПроведения.ПланируемаяДатаВыплаты, РеквизитыДляПроведения.МесяцНачисления);
		УчетНДФЛРасширенный.СформироватьСоциальныеВычетыПоУдержаниям(РеквизитыДляПроведения.Ссылка, Движения, Отказ, РеквизитыДляПроведения.Организация,
				КонецМесяца(РеквизитыДляПроведения.МесяцНачисления), МесяцСоцВычета, ДанныеДляПроведения.Удержания, , ОкончательныйРасчет);
		Если ОкончательныйРасчет Тогда // Возможно, ранее доходы вписал в учет аванс, тогда их надо зачесть.
			УчетНДФЛРасширенный.ДополнитьДвиженияСписаниемАванса(ДокументСсылка, Движения, РеквизитыДляПроведения.Организация, ДатаОперации);
		КонецЕсли;
			
		// Дополняем доходы НДФЛ сведениями о распределении по статьям финансирования и/или статьям расходов.
		ОтражениеЗарплатыВУчетеРасширенный.ДополнитьСведенияОДоходахНДФЛСведениямиОРаспределенииПоСтатьям(Движения);
		
		// - Регистрация НДФЛ и Корректировок выплаты в учете начисленной зарплаты.
		УчетНачисленнойЗарплаты.СоздатьВТРаспределениеНачисленийТекущегоДокумента(ДанныеДляПроведенияУчетЗарплаты);
		УчетНачисленнойЗарплаты.ПодготовитьДанныеНДФЛКРегистрации(ДанныеДляПроведения.НДФЛПоСотрудникам, РеквизитыДляПроведения.Организация, ДатаОперации);
		Если ОкончательныйРасчет Тогда
			УчетНачисленнойЗарплаты.СформироватьСтрокиНДФЛКакЗачетАванса(Движения, ДанныеДляПроведения.НДФЛПоСотрудникам, РеквизитыДляПроведения.МесяцНачисления);
		КонецЕсли;
		УчетНачисленнойЗарплаты.ЗарегистрироватьНДФЛПоСотрудникам(ДанныеДляПроведенияУчетЗарплаты, Отказ, ДанныеДляПроведения.НДФЛПоСотрудникам);
		УчетНачисленнойЗарплаты.ЗарегистрироватьНДФЛКЗачетуВозврату(ДанныеДляПроведенияУчетЗарплаты, Отказ,
			ДанныеДляПроведения.КорректировкиВыплатыПоСотрудникам, ДанныеДляПроведения.НДФЛПоСотрудникам);
			
		// - Регистрация удержаний и займов в учете начисленной зарплаты.
		УчетНачисленнойЗарплаты.ПодготовитьДанныеНДФЛКРегистрации(ДанныеДляПроведения.НалогНаМатериальнуюВыгоду, РеквизитыДляПроведения.Организация, КонецМесяца(РеквизитыДляПроведения.МесяцНачисления));
		УчетНачисленнойЗарплатыРасширенный.ЗарегистрироватьУдержания(ДанныеДляПроведенияУчетЗарплаты, Отказ, ДанныеДляПроведения.УдержанияПоСотрудникам);
		УчетНачисленнойЗарплатыРасширенный.ЗарегистрироватьЗаймы(ДанныеДляПроведенияУчетЗарплаты, Отказ,
			ДанныеДляПроведения.УдержанияЗаймов, ДанныеДляПроведения.НалогНаМатериальнуюВыгоду);
			
		ОтражениеЗарплатыВБухучетеРасширенный.ЗарегистрироватьНачисленияУдержания(ДанныеДляПроведенияУчетЗарплаты, Отказ,
			Неопределено, Неопределено, ДанныеДляПроведения.НалогНаМатериальнуюВыгоду);

		// - Регистрация бухучета удержаний и НДФЛ
		ОтражениеЗарплатыВБухучетеРасширенный.ЗарегистрироватьНачисленияУдержания(ДанныеДляПроведенияУчетЗарплаты, Отказ,
			Неопределено, ДанныеДляПроведения.УдержанияПоСотрудникам, ДанныеДляПроведения.НДФЛПоСотрудникам);
			
		// - Регистрация бухучета займов.
		ОтражениеЗарплатыВБухучетеРасширенный.ЗарегистрироватьНачисленияУдержания(ДанныеДляПроведенияУчетЗарплаты, Отказ,
			Неопределено, ДанныеДляПроведения.УдержанияЗаймов, Неопределено);
		
		// - Регистрация начислений в доходах для страховых взносов.
		УчетСтраховыхВзносов.СформироватьСведенияОДоходахСтраховыеВзносы(Движения, Отказ, РеквизитыДляПроведения.Организация, РеквизитыДляПроведения.МесяцНачисления,
			ДанныеДляПроведения.МенеджерВременныхТаблиц, , Истина, РеквизитыДляПроведения.Ссылка);
		
		// - Регистрация договоров в доходах для страховых взносов.
		СведенияОДоходахСтраховыеВзносы = УчетСтраховыхВзносов.СведенияОДоходахПоДоговорамСтраховыеВзносы(
			РеквизитыДляПроведения.Организация, РеквизитыДляПроведения.МесяцНачисления, ДанныеДляПроведения.МенеджерВременныхТаблиц, РеквизитыДляПроведения.Ссылка);
		
		УчетСтраховыхВзносов.СформироватьДоходыСтраховыеВзносы(Движения, Отказ, РеквизитыДляПроведения.Организация, РеквизитыДляПроведения.МесяцНачисления, СведенияОДоходахСтраховыеВзносы, Истина);
		
	КонецЕсли;
	
	Если СтруктураВидовУчета.ИсчисленныеСтраховыеВзносы Тогда
		// - страховые взносы
		УчетСтраховыхВзносов.СформироватьИсчисленныеВзносы(Движения, Отказ, РеквизитыДляПроведения.Организация, РеквизитыДляПроведения.МесяцНачисления, ДанныеДляПроведения.СтраховыеВзносы);
	КонецЕсли;
	
	Если СтруктураВидовУчета.ОстальныеВидыУчета Тогда
		
		СтандартнаяОбработка = Истина;
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОтложенноеПроведениеДокументов") Тогда 
			Модуль = ОбщегоНазначения.ОбщийМодуль("ОтражениеДокументовВУчетеСтраховыхВзносов");
			Модуль.ЗарегистрироватьДокументДляОтраженияВУчетеСтраховыхВзносов(Движения, РеквизитыДляПроведения.Организация, РеквизитыДляПроведения.МесяцНачисления, СтандартнаяОбработка);
		КонецЕсли;
		
		Если СтандартнаяОбработка Тогда 
			УчетСтраховыхВзносов.СформироватьСтраховыеВзносыПоФизическимЛицам(Движения, Отказ, РеквизитыДляПроведения.Организация, РеквизитыДляПроведения.МесяцНачисления, РеквизитыДляПроведения.Ссылка, ДанныеДляПроведения.СтраховыеВзносы);
		КонецЕсли;
		
		// Займы
		// - взаиморасчеты по займам
		ЗаймыСотрудникам.ЗарегистрироватьВзаиморасчетыПоЗаймам(Движения, ДанныеДляПроведения.ВзаиморасчетыПоЗаймам, Отказ);
		
		УчетСтраховыхВзносов.СформироватьПособия(Движения, Отказ, РеквизитыДляПроведения.Организация, РеквизитыДляПроведения.МесяцНачисления, ДанныеДляПроведения.Пособия, ДанныеДляПроведения.ПособияПоУходу);
		
	КонецЕсли;
	
	Если СтруктураВидовУчета.ДанныеДляРасчетаСреднего Тогда
		УчетСреднегоЗаработка.ЗарегистрироватьДанныеСреднегоЗаработка(Движения, Отказ, ДанныеДляПроведения.НачисленияДляСреднегоЗаработка);
	КонецЕсли;
	
	ПроведениеРасширенныйСервер.ЗаписьДвиженийПоУчетам(Движения, СтруктураВидовУчета);
	
	Если СтруктураВидовУчета.ОстальныеВидыУчета Или СтруктураВидовУчета.Начисления Тогда
		
		Если Не ВременнаяРегистрацияДвижений Тогда
			
			// Получение признака о том, что нужно удалить перерасчеты текущего периода
			УдалитьПерерасчетыТекущегоПериода = Неопределено;
			Если ДополнительныеПараметры <> Неопределено Тогда
				ДополнительныеПараметры.Свойство("УдалитьПерерасчетыТекущегоПериода", УдалитьПерерасчетыТекущегоПериода);
			КонецЕсли;
			УдалитьПерерасчетыТекущегоПериода = (УдалитьПерерасчетыТекущегоПериода = Истина);
			ПерерасчетЗарплаты.УдалитьПерерасчеты(РеквизитыДляПроведения.Ссылка, УдалитьПерерасчетыТекущегоПериода);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Сторнирует документ по учетам. Используется подсистемой исправления документов.
//
// Параметры:
//  Движения				 - КоллекцияДвижений, Структура	 - Коллекция движений исправляющего документа в которую будут добавлены сторно стоки.
//  Регистратор				 - ДокументСсылка				 - Документ регистратор исправления (документ исправление).
//  ИсправленныйДокумент	 - ДокументСсылка				 - Исправленный документ движения которого будут сторнированы.
//  СтруктураВидовУчета		 - Структура					 - Виды учета, по которым будет выполнено сторнирование исправленного документа.
//  					Состав полей см. в ПроведениеРасширенныйСервер.СтруктураВидовУчета().
//  ДополнительныеПараметры	 - Структура					 - Структура со свойствами:
//  					* ИсправлениеВТекущемПериоде - Булево - Истина когда исправление выполняется в периоде регистрации исправленного документа.
//						* ОтменаДокумента - Булево - Истина когда исправление вызвано документом СторнированиеНачислений.
//  					* ПериодРегистрации	- Дата - Период регистрации документа регистратора исправления.
// 
// Возвращаемое значение:
//  Булево - "Истина" если сторнирование выполнено этой функцией, "Ложь" если специальной процедуры не предусмотрено.
//
Функция СторнироватьПоУчетам(Движения, Регистратор, ИсправленныйДокумент, СтруктураВидовУчета, ДополнительныеПараметры) Экспорт
	
КонецФункции

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	РасчетЗарплатыРасширенныйВызовСервера.НачислениеЗарплатыОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка);	
КонецПроцедуры

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Поля.Добавить("РежимДоначисления");
	Поля.Добавить("Дата");
	Поля.Добавить("Номер");
	Поля.Добавить("Ссылка");
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область СлужебныйПрограммныйИнтерфейс

#Область ОбработчикиПравилРегистрации

// См. ЗарплатаКадрыРасширенныйСинхронизацияДанных.ШаблонОбработчика
Процедура ПриЗаполненииНастроекОбработчиковПравилРегистрации(Настройки) Экспорт
	ЗарплатаКадрыРасширенныйСинхронизацияДанных.ДляОтбораПоОрганизации(Настройки);
	ЗарплатаКадрыРасширенныйСинхронизацияДанных.ДляОбъектаСПрисоединеннымиФайлами(Настройки);
КонецПроцедуры

#КонецОбласти

// Возвращает описание состава документа
//
// Возвращаемое значение:
//  Структура - см. ЗарплатаКадрыСоставДокументов.НовоеОписаниеСоставаОбъекта.
Функция ОписаниеСоставаОбъекта() Экспорт
	
	МетаданныеДокумента = Метаданные.Документы.НачислениеЗарплаты;
	ОписаниеСостава = ЗарплатаКадрыСоставДокументов.ОписаниеСоставаОбъектаПоМетаданнымФизическиеЛицаВТабличныхЧастях(МетаданныеДокумента);
	Для Каждого ОписаниеЗаполнения Из ОписаниеСостава.ОписаниеХраненияСотрудниковФизическихЛиц Цикл
		ОписаниеЗаполнения.ВключатьВКраткийСостав = Истина;
	КонецЦикла;
	
	Возврат ОписаниеСостава;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

#КонецОбласти

Процедура ПодготовитьДанныеДляЗаполнения(СтруктураПараметров, АдресХранилища) Экспорт
	
	РезультатЗаполнения = Новый Структура;
	
	// Получение данных для заполнения табличных частей документа.
	ОписаниеДокумента = СтруктураПараметров.ОписаниеДокумента;
	Организация = СтруктураПараметров.Организация;
	МесяцНачисления = СтруктураПараметров.МесяцНачисления;
	
	ДополнительныеПараметры = РасчетЗарплатыРасширенный.ДополнительныеПараметрыЗаполненияТаблицДокумента();
	ДополнительныеПараметры.ДокументСсылка = СтруктураПараметров.ДокументСсылка;
	ДополнительныеПараметры.Подразделение = СтруктураПараметров.Подразделение;
	ДополнительныеПараметры.ОкончаниеПериода = СтруктураПараметров.ОкончаниеПериода;
	ДополнительныеПараметры.РежимНачисления = СтруктураПараметров.РежимНачисления;
	ДополнительныеПараметры.ПорядокВыплаты = СтруктураПараметров.ПорядокВыплаты;
	ДополнительныеПараметры.СотрудникиПериодДействияПерерасчет = СтруктураПараметров.СотрудникиПериодДействияПерерасчет;
	ДополнительныеПараметры.ИспользоватьВоеннуюСлужбу = СтруктураПараметров.ИспользоватьВоеннуюСлужбу;
	ДополнительныеПараметры.НачислениеЗарплатыВоеннослужащим = СтруктураПараметров.НачислениеЗарплатыВоеннослужащим;
	ДополнительныеПараметры.ОкончательныйРасчетНДФЛ = СтруктураПараметров.ОкончательныйРасчетНДФЛ;
	ДополнительныеПараметры.ПроверятьРегистрациюПроцентаЕНВД = СтруктураПараметров.ПроверятьРегистрациюПроцентаЕНВД;
	ДополнительныеПараметры.ДатаВыплаты = СтруктураПараметров.ДатаВыплаты;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.УправленческаяЗарплата") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("УправленческаяЗарплата");
		Модуль.ПриЗаполненииДополнительныхПараметровДанныхДляНачисленияЗарплаты(ДополнительныеПараметры);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.МногопотоковоеЗаполнениеДокументов") Тогда 
		Модуль = ОбщегоНазначения.ОбщийМодуль("МногопотоковоеЗаполнениеДокументов");
		Если Не РегистрыРасчета.Начисления.ЕстьДвиженияПоРегистратору(СтруктураПараметров.ДокументСсылка) И Модуль.ИспользоватьМногопотоковоеЗаполнениеДокументов() Тогда
			ДополнительныеПараметры.АдресХранилища = АдресХранилища;
			Модуль.ПодготовитьДанныеДляЗаполнения(ОписаниеДокумента, Организация, МесяцНачисления, ДополнительныеПараметры);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ДанныеЗаполнения = РасчетЗарплатыРасширенный.ДанныеДляЗаполненияТаблицДокумента(ОписаниеДокумента, Организация, МесяцНачисления, ДополнительныеПараметры);
	
	РезультатЗаполнения.Вставить("ДанныеДляЗаполненияТаблицДокумента", ДанныеЗаполнения);
	
	ПоместитьВоВременноеХранилище(РезультатЗаполнения, АдресХранилища);
	
КонецПроцедуры

Процедура ВыполнитьПроведение(СтруктураПараметров, АдресХранилища) Экспорт
	
	ДокументОбъект = ЗарплатаКадры.ДесериализоватьОбъектИзДвоичныхДанных(СтруктураПараметров.ДанныеДокумента);
	ДокументОбъект.ДополнительныеСвойства.Вставить("УдалитьПерерасчетыТекущегоПериода", СтруктураПараметров.УдалитьПерерасчетыТекущегоПериода);
	ДокументОбъект.ДополнительныеСвойства.Вставить("ВидОперации", СтруктураПараметров.ВидОперации);
	
	Если СтруктураПараметров.Свойство("ПериодыРасчетаСотрудников") Тогда
		ДокументОбъект.ДополнительныеСвойства.Вставить("ПериодыРасчетаСотрудников", СтруктураПараметров.ПериодыРасчетаСотрудников);
	КонецЕсли; 
	
	ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОперацииРасчетаЗарплаты") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОперацииРасчетаЗарплаты");
		Модуль.ЗаписатьВидОперацииДокумента(ДокументОбъект.Ссылка, СтруктураПараметров.ВидОперации);
	КонецЕсли;
	ПоместитьВоВременноеХранилище(ЗарплатаКадры.СериализоватьОбъектВДвоичныеДанные(ДокументОбъект), АдресХранилища);
	
КонецПроцедуры

Функция ПредставлениеТипаДоначислениеПерерасчет() Экспорт
	
	Возврат НСтр("ru = 'Доначисление, перерасчет';
				|en = 'Additional accrual, recalculation'");
	
КонецФункции

#Область ПервоначальноеЗаполнениеИОбновлениеИнформационнойБазы

Процедура ЗаполнитьИсходныеДанныеПерерасчетов(ПараметрыОбновления) Экспорт

	ПараметрыЗаполнения = ПерерасчетЗарплаты.ПараметрыЗаполненияИсходныхДанныхПерерасчетов();
	ПараметрыЗаполнения.ТаблицыНачислений = "Начисления,НачисленияПерерасчет,Пособия,Льготы,ПособияПерерасчет,ЛьготыПерерасчет";
	ПараметрыЗаполнения.ПолеПериодРегистрации = "Ссылка.МесяцНачисления";
	ПерерасчетЗарплаты.ЗаполнитьИсходныеДанныеПерерасчетов(ПараметрыОбновления, Метаданные.Документы.НачислениеЗарплаты, ПараметрыЗаполнения);

КонецПроцедуры

#КонецОбласти

Функция ДобавитьКомандыСозданияДокументов(КомандыСозданияДокументов, ДополнительныеПараметры) Экспорт
	
	ПредставлениеДокумента = Метаданные.Документы.НачислениеЗарплаты.Представление();
	
	ОписаниеКоманды = ЗарплатаКадрыРасширенный.ОписаниеКомандыСозданияДокумента(
		"Документ.НачислениеЗарплаты",
		ПредставлениеДокумента);
		
	ЗарплатаКадрыРасширенный.ДобавитьВКоллекциюКомандуСозданияДокумента(
		КомандыСозданияДокументов, ОписаниеКоманды);
		
	ВидыОперацийРасчетаЗарплаты = Новый Массив;
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОперацииРасчетаЗарплаты") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОперацииРасчетаЗарплаты");
		ВидыОперацийРасчетаЗарплаты = Модуль.ВидыОперацийРасчетаЗарплаты();
	КонецЕсли; 
	
	ПорядокВидовОпераций = 1;
	Для Каждого ВидОперации Из ВидыОперацийРасчетаЗарплаты Цикл
		
		ВидОперацииСтрокой = Строка(ВидОперации);
		ОписаниеКоманды = ЗарплатаКадрыРасширенный.ОписаниеКомандыСозданияДокумента(
			"Документ.НачислениеЗарплаты",
			ВидОперацииСтрокой,
			ПредставлениеДокумента + ПорядокВидовОпераций);
		
		Параметры = Новый Структура;
		Параметры.Вставить("ВидОперации", ВидОперации);

		ОписаниеКоманды.Параметры = Параметры; 
		
		ЗарплатаКадрыРасширенный.ДобавитьВКоллекциюКомандуСозданияДокумента(
			КомандыСозданияДокументов, ОписаниеКоманды);
			
		ПорядокВидовОпераций = ПорядокВидовОпераций + 1;
			
	КонецЦикла; 
	
КонецФункции

Процедура ЗаполнитьПредставлениеРаспределенияРезультатовРасчета(ДокументОбъект) Экспорт 
	
	ДокументОбъект.ПредставлениеРаспределенияРезультатовРасчета.Очистить();
	
	ПроверяемыеПоля = Новый Структура;
	ПроверяемыеПоля.Вставить("СтатьяФинансирования", Справочники.СтатьиФинансированияЗарплата.ПустаяСсылка());
	Если ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		ПроверяемыеПоля.Вставить("СтатьяРасходов", Справочники.СтатьиРасходовЗарплата.ПустаяСсылка());
	КонецЕсли;	
	ПроверяемыеПоля.Вставить("СпособОтраженияЗарплатыВБухучете", Справочники.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка());
		
	ЗаполнитьДанныеПредставленияРаспределенияРезультатовНачисленийУдержаний(ДокументОбъект, ДокументОбъект.РаспределениеРезультатовНачислений, ПроверяемыеПоля, Истина);
	
	ПроверяемыеПоля.Удалить("СпособОтраженияЗарплатыВБухучете");
	ПроверяемыеПоля.Вставить("Сотрудник", Справочники.Сотрудники.ПустаяСсылка());
	
	ЗаполнитьДанныеПредставленияРаспределенияРезультатовНачисленийУдержаний(ДокументОбъект, ДокументОбъект.РаспределениеРезультатовУдержаний, ПроверяемыеПоля, Ложь);
	
КонецПроцедуры

Процедура ЗаполнитьДанныеПредставленияРаспределенияРезультатовНачисленийУдержаний(ДокументОбъект, РаспределениеРезультатов, ПроверяемыеПоля, РаспределениеНачислений)
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("РаспределениеРезультатов", РаспределениеРезультатов);
					   
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РаспределениеРезультатов.НомерСтроки,
	|	РаспределениеРезультатов.ИдентификаторСтроки,
	|	&ПроверяемыеПоля КАК ПроверяемыеПоля,
	|	РаспределениеРезультатов.Результат
	|ПОМЕСТИТЬ ВТРаспределениеРезультатов
	|ИЗ
	|	&РаспределениеРезультатов КАК РаспределениеРезультатов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РаспределениеРезультатов.ИдентификаторСтроки,
	|	КОЛИЧЕСТВО(РаспределениеРезультатов.НомерСтроки) КАК КоличествоСтрок,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА &ПроверкаНаличияПустыхПолей
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ЕстьОшибкиЗаполнения
	|ПОМЕСТИТЬ ВТСгруппированныеДанныеПоИдентификаторам
	|ИЗ
	|	ВТРаспределениеРезультатов КАК РаспределениеРезультатов
	|
	|СГРУППИРОВАТЬ ПО
	|	РаспределениеРезультатов.ИдентификаторСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РаспределениеРезультатов.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	РаспределениеРезультатов.НомерСтроки КАК НомерСтрокиРаспределения,
	|	СгруппированныеДанныеПоИдентификаторам.КоличествоСтрок КАК КоличествоЭлементовПредставления,
	|	СгруппированныеДанныеПоИдентификаторам.ЕстьОшибкиЗаполнения КАК ЕстьОшибкиЗаполнения,
	|	РаспределениеРезультатов.Результат КАК Результат
	|ИЗ
	|	ВТРаспределениеРезультатов КАК РаспределениеРезультатов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСгруппированныеДанныеПоИдентификаторам КАК СгруппированныеДанныеПоИдентификаторам
	|		ПО РаспределениеРезультатов.ИдентификаторСтроки = СгруппированныеДанныеПоИдентификаторам.ИдентификаторСтроки
	|
	|УПОРЯДОЧИТЬ ПО
	|	ИдентификаторСтроки,
	|	НомерСтрокиРаспределения";

	
	ПроверяемыеПоляТаблицы = "";
	ПроверкаНаличияПустыхПолей = "";
	
	Для Каждого КлючЗначение Из ПроверяемыеПоля Цикл
		
		Запрос.УстановитьПараметр(КлючЗначение.Ключ + "ПустаяСсылка", КлючЗначение.Значение);
		
		ПроверяемыеПоляТаблицы = ПроверяемыеПоляТаблицы + "
								|	РаспределениеРезультатов." + КлючЗначение.Ключ + ",";
								
		ПроверкаНаличияПустыхПолей = ПроверкаНаличияПустыхПолей + " ИЛИ РаспределениеРезультатов." + КлючЗначение.Ключ + " = &" + КлючЗначение.Ключ + "ПустаяСсылка";
							
	КонецЦикла;	
						
	ПроверкаНаличияПустыхПолей = Сред(ПроверкаНаличияПустыхПолей, 6);
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПроверяемыеПоля КАК ПроверяемыеПоля,", ПроверяемыеПоляТаблицы);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПроверкаНаличияПустыхПолей", ПроверкаНаличияПустыхПолей); 
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.СледующийПоЗначениюПоля("ИдентификаторСтроки") Цикл
		
		НомерЭлементаПредставления = 0;
		Пока Выборка.Следующий() Цикл
			НомерЭлементаПредставления = НомерЭлементаПредставления + 1;
					
			СтрокаПредставленияРаспределения = ДокументОбъект.ПредставлениеРаспределенияРезультатовРасчета.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаПредставленияРаспределения, Выборка);
			
			СтрокаПредставленияРаспределения.ПредставлениеРезультата = Формат(Выборка.Результат, "ЧДЦ=2; ЧРГ=; ЧН=' '");
			СтрокаПредставленияРаспределения.РаспределениеНачислений = РаспределениеНачислений;
			СтрокаПредставленияРаспределения.НомерЭлементаПредставления = НомерЭлементаПредставления;
		КонецЦикла;	
		
	КонецЦикла;
	
КонецПроцедуры

Функция ДанныеДляПроведения(РеквизитыДляПроведения, МесяцНачисления, СписокФизическихЛиц, СтруктураВидовУчета)
	
	ДанныеДляПроведения = РасчетЗарплаты.СоздатьДанныеДляПроведенияНачисленияЗарплаты();
	
	ОстальныеВидыУчета = Ложь;
	Для Каждого КлючИЗначение Из СтруктураВидовУчета Цикл
		Если КлючИЗначение.Ключ = "ДанныеДляРасчетаСреднего" Тогда 
			Продолжить;
		КонецЕсли;
		Если ТипЗнч(КлючИЗначение.Значение) = Тип("Булево") 
			И КлючИЗначение.Значение Тогда 
			ОстальныеВидыУчета = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ОстальныеВидыУчета Тогда
		РасчетЗарплатыРасширенный.ЗаполнитьНачисления(ДанныеДляПроведения, РеквизитыДляПроведения.Ссылка, "Начисления,НачисленияПерерасчет,Пособия,ПособияПерерасчет,Льготы,ЛьготыПерерасчет", "Ссылка.МесяцНачисления", , СписокФизическихЛиц);
		РасчетЗарплатыРасширенный.ЗаполнитьУдержания(ДанныеДляПроведения, РеквизитыДляПроведения.Ссылка, "Удержания,УдержанияПерерасчет", СписокФизическихЛиц);
		РасчетЗарплаты.ЗаполнитьСписокФизическихЛиц(ДанныеДляПроведения, РеквизитыДляПроведения.Ссылка, , СписокФизическихЛиц);
		РасчетЗарплатыРасширенный.ЗаполнитьПогашениеЗадолженностиПоУдержаниям(ДанныеДляПроведения, РеквизитыДляПроведения.Ссылка, РеквизитыДляПроведения.МесяцНачисления, ?(РеквизитыДляПроведения.РежимДоначисления, "УдержанияПерерасчет", "Удержания"));
		
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.УправленческаяЗарплата") Тогда
			Модуль = ОбщегоНазначения.ОбщийМодуль("УправленческаяЗарплата");
			ПараметрыУправленческаяЗарплата = Модуль.ДополнительныеПараметрыПодготовкиДанныхДляПроведения();
			ПараметрыУправленческаяЗарплата.ПолеДатыДействия = "Ссылка.МесяцНачисления"; 
			ПараметрыУправленческаяЗарплата.ПолеВидаНачисления = "Начисление"; 
			ПараметрыУправленческаяЗарплата.СписокФизическихЛиц = СписокФизическихЛиц;
			Модуль.ПриПодготовкеДанныхДляПроведенияДокументаРасчетаЗарплаты(ДанныеДляПроведения, РеквизитыДляПроведения.Ссылка, ПараметрыУправленческаяЗарплата);
		КонецЕсли;
		
		ОтражениеЗарплатыВБухучете.СоздатьВТНачисленияСДаннымиЕНВД(РеквизитыДляПроведения.Организация, РеквизитыДляПроведения.МесяцНачисления, ДанныеДляПроведения.МенеджерВременныхТаблиц, ДанныеДляПроведения.НачисленияПоСотрудникам);
		РасчетЗарплатыРасширенный.ЗаполнитьДанныеПоДоговорамПодряда(ДанныеДляПроведения, РеквизитыДляПроведения, СписокФизическихЛиц);
		
		ПособиеПлатитУчастникПилотногоПроекта = ПрямыеВыплатыПособийСоциальногоСтрахования.ПособиеПлатитУчастникПилотногоПроекта(РеквизитыДляПроведения.Организация, РеквизитыДляПроведения.МесяцНачисления);
		
		УчетПособийСоциальногоСтрахованияРасширенный.ЗаполнитьСведенияОПособияхПоУходуЗаРебенком(РеквизитыДляПроведения.Ссылка, ПособиеПлатитУчастникПилотногоПроекта, ДанныеДляПроведения, , "ПособияПерерасчет", СписокФизическихЛиц);
		
		РасчетЗарплаты.ЗаполнитьДанныеНДФЛ(ДанныеДляПроведения, РеквизитыДляПроведения.Ссылка, СписокФизическихЛиц);
		РасчетЗарплатыРасширенный.ЗаполнитьДанныеКорректировкиВыплаты(ДанныеДляПроведения, РеквизитыДляПроведения.Ссылка, СписокФизическихЛиц);
		РасчетЗарплаты.ЗаполнитьДанныеСтраховыхВзносов(ДанныеДляПроведения, РеквизитыДляПроведения.Ссылка, СписокФизическихЛиц);
		
		ЗаймыСотрудникам.ЗаполнитьДанныеДляПроведенияПоЗаймам(ДанныеДляПроведения, РеквизитыДляПроведения.Ссылка, КонецМесяца(МесяцНачисления), , СписокФизическихЛиц);
		
	КонецЕсли;
	
	Если СтруктураВидовУчета.ДанныеДляРасчетаСреднего Тогда
		ДополнительныеПараметры = УчетСреднегоЗаработка.ДополнительныеПараметрыРегистрацииДанныхСреднегоЗаработка("Начисления,НачисленияПерерасчет,Пособия,ПособияПерерасчет");
		УчетСреднегоЗаработка.ЗаполнитьТаблицыДляРегистрацииДанныхСреднегоЗаработка(ДанныеДляПроведения, РеквизитыДляПроведения.Ссылка, ДополнительныеПараметры, СписокФизическихЛиц);
		УчетСреднегоЗаработка.ЗаполнитьДанныеДляРегистрацииСреднегоЗаработкаПоГПХ(ДанныеДляПроведения, РеквизитыДляПроведения, СписокФизическихЛиц);
	КонецЕсли;
	
	Возврат ДанныеДляПроведения;
	
КонецФункции

Функция РеквизитыДляПроведения(ДокументСсылка)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НачислениеЗарплаты.Ссылка КАК Ссылка,
	|	НачислениеЗарплаты.Дата КАК Дата,
	|	НачислениеЗарплаты.МесяцНачисления КАК МесяцНачисления,
	|	НачислениеЗарплаты.Организация КАК Организация,
	|	НачислениеЗарплаты.ПланируемаяДатаВыплаты КАК ПланируемаяДатаВыплаты,
	|	НачислениеЗарплаты.РежимДоначисления КАК РежимДоначисления,
	|	НачислениеЗарплаты.ПорядокВыплаты КАК ПорядокВыплаты
	|ИЗ
	|	Документ.НачислениеЗарплаты КАК НачислениеЗарплаты
	|ГДЕ
	|	НачислениеЗарплаты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачислениеЗарплатыРаспределениеПоТерриториямУсловиямТруда.НомерСтроки КАК НомерСтроки,
	|	НачислениеЗарплатыРаспределениеПоТерриториямУсловиямТруда.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	НачислениеЗарплатыРаспределениеПоТерриториямУсловиямТруда.Территория КАК Территория,
	|	НачислениеЗарплатыРаспределениеПоТерриториямУсловиямТруда.УсловияТруда КАК УсловияТруда,
	|	НачислениеЗарплатыРаспределениеПоТерриториямУсловиямТруда.ДоляРаспределения КАК ДоляРаспределения,
	|	НачислениеЗарплатыРаспределениеПоТерриториямУсловиямТруда.Результат КАК Результат,
	|	НачислениеЗарплатыРаспределениеПоТерриториямУсловиямТруда.ИдентификаторСтрокиПоказателей КАК ИдентификаторСтрокиПоказателей,
	|	НачислениеЗарплатыРаспределениеПоТерриториямУсловиямТруда.СуммаВычета КАК СуммаВычета,
	|	НачислениеЗарплатыРаспределениеПоТерриториямУсловиямТруда.СкидкаПоВзносам КАК СкидкаПоВзносам,
	|	НачислениеЗарплатыРаспределениеПоТерриториямУсловиямТруда.РанееНачислено КАК РанееНачислено
	|ИЗ
	|	Документ.НачислениеЗарплаты.РаспределениеПоТерриториямУсловиямТруда КАК НачислениеЗарплатыРаспределениеПоТерриториямУсловиямТруда
	|ГДЕ
	|	НачислениеЗарплатыРаспределениеПоТерриториямУсловиямТруда.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачислениеЗарплатыРаспределениеРезультатовНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	НачислениеЗарплатыРаспределениеРезультатовНачислений.Территория КАК Территория,
	|	НачислениеЗарплатыРаспределениеРезультатовНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
	|	НачислениеЗарплатыРаспределениеРезультатовНачислений.СтатьяРасходов КАК СтатьяРасходов,
	|	НачислениеЗарплатыРаспределениеРезультатовНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	НачислениеЗарплатыРаспределениеРезультатовНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	СУММА(НачислениеЗарплатыРаспределениеРезультатовНачислений.Результат) КАК Результат,
	|	НачислениеЗарплатыРаспределениеРезультатовНачислений.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат
	|ИЗ
	|	Документ.НачислениеЗарплаты.РаспределениеРезультатовНачислений КАК НачислениеЗарплатыРаспределениеРезультатовНачислений
	|ГДЕ
	|	НачислениеЗарплатыРаспределениеРезультатовНачислений.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	НачислениеЗарплатыРаспределениеРезультатовНачислений.ПодразделениеУчетаЗатрат,
	|	НачислениеЗарплатыРаспределениеРезультатовНачислений.СпособОтраженияЗарплатыВБухучете,
	|	НачислениеЗарплатыРаспределениеРезультатовНачислений.СтатьяФинансирования,
	|	НачислениеЗарплатыРаспределениеРезультатовНачислений.Территория,
	|	НачислениеЗарплатыРаспределениеРезультатовНачислений.СтатьяРасходов,
	|	НачислениеЗарплатыРаспределениеРезультатовНачислений.ОблагаетсяЕНВД,
	|	НачислениеЗарплатыРаспределениеРезультатовНачислений.ИдентификаторСтроки";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Результаты = Запрос.ВыполнитьПакет();
	
	РеквизитыДляПроведения = РеквизитыДляПроведенияПустаяСтруктура();
	
	ВыборкаРеквизиты = Результаты[0].Выбрать();
	
	Пока ВыборкаРеквизиты.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(РеквизитыДляПроведения, ВыборкаРеквизиты);
	КонецЦикла;
	
	РаспределениеПоТерриториямУсловиямТруда = Результаты[1].Выгрузить();
	РаспределениеРезультатовНачислений = Результаты[2].Выгрузить();
	РеквизитыДляПроведения.РаспределениеПоТерриториямУсловиямТруда = РаспределениеПоТерриториямУсловиямТруда;
	РеквизитыДляПроведения.РаспределениеРезультатовНачислений = РаспределениеРезультатовНачислений;
	
	Возврат РеквизитыДляПроведения;
	
КонецФункции

Функция РеквизитыДляПроведенияПустаяСтруктура()
	
	РеквизитыДляПроведенияПустаяСтруктура = Новый Структура("Ссылка, Дата, МесяцНачисления, Организация, ПланируемаяДатаВыплаты, РежимДоначисления,
		|ПорядокВыплаты, РаспределениеПоТерриториямУсловиямТруда, РаспределениеРезультатовНачислений");	
	
	Возврат РеквизитыДляПроведенияПустаяСтруктура;
	
КонецФункции

Процедура ПроверитьПересечениеФактическогоПериодаДействия(ДокументСсылка, РеквизитыДокумента, Отказ)
	
	Если Отказ Тогда
		Возврат;	
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	Начисления.*
	               |ИЗ
	               |	Документ.НачислениеЗарплаты.Начисления КАК Начисления
	               |ГДЕ
	               |	Начисления.Ссылка = &Ссылка";
				   
	Начисления = Запрос.Выполнить().Выгрузить();
	
	ПараметрыПроверки = РасчетЗарплатыРасширенный.ПараметрыПроверкиПересеченияФактическогоПериодаДействия();
	ПараметрыПроверки.Организация = РеквизитыДокумента.Организация;
	ПараметрыПроверки.ПериодРегистрации = РеквизитыДокумента.МесяцНачисления;
	ПараметрыПроверки.Документ = ДокументСсылка;
	ПараметрыПроверки.Начисления = Начисления;
	
	РасчетЗарплатыРасширенный.ПроверитьПересечениеФактическогоПериодаДействия(ПараметрыПроверки, Отказ);
	
КонецПроцедуры

Процедура РазрешитьПересмотрДатыПолученияДоходаПоДоговорам(Налоги, Знач НачисленияПоДоговорам, Знач ПредполагаемаяДатаВыплаты)
	
	Если НачисленияПоДоговорам.Количество() = 0 Тогда
		Возврат
	КонецЕсли;
	
	КолонкиДоходов = Новый Массив;
	КолонкиДоходов.Добавить("КатегорияДохода");
	КолонкиДоходов.Добавить("ДатаПолученияДохода");
	КолонкиДоходов.Добавить("Подразделение");
	КолонкиДоходов.Добавить("ФизическоеЛицо");
	
	КолонкиДоходовСтрокой = СтрСоединить(КолонкиДоходов, ",");
	
	НачисленияОснования = НачисленияПоДоговорам.СкопироватьКолонки(КолонкиДоходовСтрокой);
	Для Каждого СтрокаНачислений Из НачисленияПоДоговорам Цикл
		Если ЗначениеЗаполнено(СтрокаНачислений.ДатаПолученияДохода) И СтрокаНачислений.ДатаПолученияДохода < ПредполагаемаяДатаВыплаты Тогда
			НоваяСтрока = НачисленияОснования.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНачислений);
			Если Не ЗначениеЗаполнено(НоваяСтрока.КатегорияДохода) Тогда
				НоваяСтрока.КатегорияДохода = УчетНДФЛПовтИсп.КатегорияДоходаПоЕгоКоду(СтрокаНачислений.КодДохода);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если НачисленияОснования.Количество() = 0 Тогда
		Возврат
	КонецЕсли;
	
	НачисленияОснования.Колонки.ДатаПолученияДохода.Имя = "МесяцНалоговогоПериода";
	КолонкиДоходовСтрокой = СтрЗаменить(КолонкиДоходовСтрокой, "ДатаПолученияДохода", "МесяцНалоговогоПериода");
	
	НачисленияОснования.Индексы.Добавить(КолонкиДоходовСтрокой);
	Отбор = Новый Структура(КолонкиДоходовСтрокой);
	
	Для Каждого СтрокаНалога Из Налоги Цикл
		Если Не СтрокаНалога.ДатаПолученияДоходаФиксирована Тогда
			Продолжить;
		КонецЕсли;
		ФиксированныеКатегории = Перечисления.КатегорииДоходовНДФЛ.СФиксированнойДатойПолученияДохода(СтрокаНалога.МесяцНалоговогоПериода);
		Если ФиксированныеКатегории.Найти(СтрокаНалога.КатегорияДохода) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(Отбор, СтрокаНалога);
		Если НачисленияОснования.НайтиСтроки(Отбор).Количество() > 0 Тогда
			СтрокаНалога.ДатаПолученияДоходаФиксирована = Ложь;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли