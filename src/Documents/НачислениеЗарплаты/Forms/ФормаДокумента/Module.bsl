#Область ОписаниеПеременных

&НаКлиенте
Перем СтарыеЗначенияКонтролируемыхПолей;

&НаКлиенте
Перем СтароеЗначениеФизическогоЛица;

&НаКлиенте
Перем СотрудникиПередУдалением;

&НаКлиенте
Перем ПараметрыОбработчикаОжидания;

&НаКлиенте
Перем ФормаДлительнойОперации;

&НаКлиенте
Перем СотрудникиКРасчету Экспорт;

&НаКлиенте
Перем ПечатныеФормыСотрудников;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РасчетЗарплатыРасширенныйФормы.ДокументыПриСозданииНаСервере(ЭтаФорма);
	РасчетЗарплатыРасширенныйФормы.ИнициализироватьМеханизмПересчетаДокументаПриРедактировании(ЭтаФорма);
	
	Если Параметры.Ключ.Пустая() Тогда
		
		Если Объект.РежимДоначисления Тогда
			КлючиИдентификаторовЗаполнения = "Организация, Ответственный, Месяц";
		Иначе
			КлючиИдентификаторовЗаполнения = "Организация, Ответственный, МесяцРасчета";
		КонецЕсли;
		
		ЗначенияДляЗаполнения = Новый Структура(КлючиИдентификаторовЗаполнения, "Объект.Организация", "Объект.Ответственный", "Объект.МесяцНачисления");
		
		Если ПолучитьФункциональнуюОпцию("ВыполнятьРасчетЗарплатыПоПодразделениям") Тогда
			ЗначенияДляЗаполнения.Вставить("Подразделение", "Объект.Подразделение");
		КонецЕсли;
		
		ЗарплатаКадры.ЗаполнитьПервоначальныеЗначенияВФорме(ЭтаФорма, ЗначенияДляЗаполнения);
		
		Если Объект.РежимДоначисления Тогда
			Объект.ПорядокВыплаты = Перечисления.ХарактерВыплатыЗарплаты.Межрасчет;
		КонецЕсли; 
			
		ЗарплатаКадрыРасширенный.УстановитьНаименованиеПервичногоДокумента(Объект);
		ПриПолученииДанныхНаСервере(РеквизитФормыВЗначение("Объект"));
		Если ТребуетсяЗаполнитьДатуВыплаты(Объект.МесяцНачисления, Объект.РежимДоначисления) Тогда
			РасчетЗарплатыРасширенный.УстановитьПланируемуюДатуВыплаты(ЭтотОбъект, ОписаниеДокумента(ЭтотОбъект));
		КонецЕсли;
		УстановитьВидимостьКолонокПрогрессивногоНалога(Объект.ПланируемаяДатаВыплаты);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"НачислениеЗарплатыВоеннослужащим",
		"Видимость",
		ИспользоватьВоеннуюСлужбу);
		
		Если Параметры.ЗначенияЗаполнения.Свойство("НачислениеЗарплатыВоеннослужащим") Тогда
			Параметры.ЗначенияЗаполнения.Свойство("НачислениеЗарплатыВоеннослужащим", Объект.НачислениеЗарплатыВоеннослужащим);
		Иначе
			Объект.НачислениеЗарплатыВоеннослужащим = ИспользоватьВоеннуюСлужбу;
		КонецЕсли;
	КонецЕсли;
	
	Если Объект.РежимДоначисления Тогда
		РасчетЗарплатыРасширенныйФормы.УстановитьДоступныеХарактерыВыплаты(Элементы);
		ЗаполнитьПриОткрытии = Параметры.ЗначенияЗаполнения.Свойство("ЗаполнитьПриОткрытии");
	КонецЕсли; 
	
	// Обработчик подсистемы "ВерсионированиеОбъектов".
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = Элементы.КоманднаяПанельФормы;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ОкончательныйРасчетНДФЛ = Не Объект.РежимДоначисления;
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначения.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплата");
		МодульПроцессыОбработкиДокументовЗарплата.ПриСозданииНаСервере(ЭтотОбъект, Объект);
	КонецЕсли;	
	// Конец ПроцессыОбработкиДокументов
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗаполнитьПриОткрытии Тогда
		
		ПодключитьОбработчикОжидания("ЗаполнитьДанныеФормыНаКлиенте", 0.1, Истина);
		ЗаполнитьПриОткрытии = Ложь;
		
		Модифицированность = Истина;
		
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Массив")
		И ВыбранноеЗначение.Количество() > 0
		И ТипЗнч(ВыбранноеЗначение[0]) = Тип("СправочникСсылка.Сотрудники") Тогда
		
		ОбработкаПодбораНаКлиенте(ВыбранноеЗначение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_НачислениеЗарплаты" Тогда
		
		Если Параметр.Свойство("Организация")
			И Параметр.Организация = Объект.Организация
			И Параметр.Свойство("РежимДоначисления")
			И Параметр.РежимДоначисления Тогда
			
			УстановитьОтображениеСообщенияОНеобходимостиПерерасчета();
			
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "ИзмененыРезультатыРасчетаНДФЛ" И Источник.ВладелецФормы = ЭтаФорма Тогда
		ОбновитьДанныеНДФЛНаСервере(Параметр);
		РасчетЗарплатыКлиент.СтрокаРасчетаПриОкончанииРедактирования(ЭтаФорма, ОписаниеТаблицыНДФЛ(), Истина);
	КонецЕсли; 
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	ЗаполнениеВыполнено = Ложь;
	ПриПолученииДанныхНаСервере(ТекущийОбъект);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"НачислениеЗарплатыВоеннослужащим",
		"Видимость",
		ИспользоватьВоеннуюСлужбу);
		
	ЗарплатаКадрыРасширенныйКлиентСервер.ПланируемаяДатаВыплатыУстановитьОтображениеПредупреждения(ЭтаФорма,"ПланируемаяДатаВыплаты",
												СтрокиТаблицДокументаПоФизическимЛицам);
	УстановитьВидимостьКолонокПрогрессивногоНалога(Объект.ПланируемаяДатаВыплаты);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.КонтрольВеденияУчета
	КонтрольВеденияУчетаБЗК.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.КонтрольВеденияУчета
	
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначения.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплата");
		МодульПроцессыОбработкиДокументовЗарплата.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;	
	// Конец ПроцессыОбработкиДокументов
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	РасчетЗарплатыРасширенныйКлиент.ПередЗаписьюДокументаСоСпискомСотрудников(ЭтаФорма, "ПередЗаписьюОкончание", Отказ, ПараметрыЗаписи);
	
	Если Не Отказ Тогда
		
		// Если документ проведен, то имитируем режим ПриЗаписиПерепроводить.
		Если Объект.Проведен И ПараметрыЗаписи.РежимЗаписи <> РежимЗаписиДокумента.ОтменаПроведения Тогда
			ПровестиДокументНаКлиенте();
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	РеквизитыВДанные(ТекущийОбъект);
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначения.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплата");
		МодульПроцессыОбработкиДокументовЗарплата.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи, Отказ);
	КонецЕсли;		
	// Конец ПроцессыОбработкиДокументов
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОперацииРасчетаЗарплаты") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОперацииРасчетаЗарплаты");
		Модуль.ЗаписатьВидОперацииДокумента(ТекущийОбъект.Ссылка, ЭтотОбъект["ВидОперации"]);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ЗаполнениеВыполнено = Ложь;
	ПриПолученииДанныхНаСервере(ТекущийОбъект, Ложь);
	
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначения.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплата");
		МодульПроцессыОбработкиДокументовЗарплата.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;	
	// Конец ПроцессыОбработкиДокументов
	
	// СтандартныеПодсистемы.КонтрольВеденияУчета
	КонтрольВеденияУчетаБЗК.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.КонтрольВеденияУчета
		
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	ЗакрыватьПослеЗаписи = Истина;
	
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначенияКлиент.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплатаКлиент");
		МодульПроцессыОбработкиДокументовЗарплата.ПередЗакрытием(ЭтотОбъект, Объект, Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	КонецЕсли;	
	// Конец ПроцессыОбработкиДокументов
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("Запись_НачислениеЗарплаты", ПараметрыЗаписи, Объект.Ссылка);
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	ТекущийОбъект = РеквизитФормыВЗначение("Объект");
	РеквизитыВДанные(ТекущийОбъект);
	
	Если НЕ ТекущийОбъект.ПроверитьЗаполнение() Тогда
		Отказ = Истина;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Объект");
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#Область РеквизитыПервичногоДокумента

&НаКлиенте
Процедура НаименованиеПервичногоДокументаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ОткрытьФормуСпискаЗначенийСтроковогоРеквизита(Элемент, 
		СтандартнаяОбработка, "ПервичныйДокумент", НСтр("ru = 'Первичные документы';
														|en = 'Source documents'"), НСтр("ru = 'Первичный документ';
																							|en = 'Source document'"));
		
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПервичногоДокументаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Строка") Тогда
		Объект.НаименованиеПервичногоДокумента = ВыбранноеЗначение;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПервичногоДокументаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ПодборЗначенияСтроковогоРеквизита(Текст, ДанныеВыбора, СтандартнаяОбработка, "ПервичныйДокумент");
КонецПроцедуры

&НаКлиенте
Процедура ДатаПервичногоДокументаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Оповещение = Новый ОписаниеОповещения("ДатаПервичногоДокументаНачалоВыбораЗавершение", ЭтотОбъект);
	ПоказатьВводДаты(Оповещение, ?(ЗначениеЗаполнено(Объект.ДатаПервичногоДокумента), Объект.ДатаПервичногоДокумента, Объект.Дата), НСтр("ru = 'Дата первичного документа';
																																		|en = 'Source document date'"), ЧастиДаты.Дата);
КонецПроцедуры

&НаКлиенте
Процедура ДатаПервичногоДокументаНачалоВыбораЗавершение(Дата, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Дата) Тогда
		Объект.ДатаПервичногоДокумента = Дата;
		ДатаПервичногоДокументаПредставление = Формат(Дата,"ДФ=dd.MM.yyyy");
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПервичногоДокументаОчистка(Элемент, СтандартнаяОбработка)
	Объект.ДатаПервичногоДокумента = Дата(1,1,1);
	Модифицированность = Истина;
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	ЗарплатаКадрыРасширенныйКлиентСервер.УстановитьОтображениеРеквизитовПервичногоДокумента(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура НомерПриИзменении(Элемент)
	ЗарплатаКадрыРасширенныйКлиентСервер.УстановитьОтображениеРеквизитовПервичногоДокумента(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования,
		ЭтотОбъект,
		"Объект.Комментарий");
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
    ЗарплатаКадрыРасширенныйКлиент.КлючевыеРеквизитыЗаполненияФормыОчиститьТаблицы(ЭтаФорма);
	ОбработатьИзменениеОрганизацииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеОрганизацииПриИзменении(Элемент)
	
	ЗарплатаКадрыРасширенныйКлиент.КлючевыеРеквизитыЗаполненияФормыОчиститьТаблицы(ЭтаФорма);
	
	РассчитатьИтогиПоДокументу(ЭтаФорма);
	УстановитьОтображениеСообщенияОНеобходимостиПерерасчета();
	
КонецПроцедуры

&НаКлиенте
Процедура СообщениеОНеобходимостиПерерасчетаНадписьОбработкаНавигационнойСсылки(Элемент, 
	НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "Подробнее" Тогда
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("Организация", Объект.Организация);
		ПараметрыОткрытия.Вставить("ПериодДействия", Объект.МесяцНачисления);
		
		Если ЗначениеЗаполнено(Объект.Подразделение) Тогда
			ПараметрыОткрытия.Вставить("Подразделение", Объект.Подразделение);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Объект.Подразделение) Тогда
			ПараметрыОткрытия.Вставить("Сотрудники", АдресСпискаСотрудниковВХранилище());
		КонецЕсли;
		
		ОткрытьФорму("Обработка.УправлениеПерерасчетами.Форма.ПричиныПерерасчетов",  ПараметрыОткрытия, ЭтотОбъект,
			, , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// Редактирование месяца строкой.

&НаКлиенте
Процедура МесяцНачисленияСтрокойПриИзменении(Элемент)
	
	ЗарплатаКадрыКлиент.ВводМесяцаПриИзменении(ЭтаФорма, "Объект.МесяцНачисления", "МесяцНачисленияСтрокой", Модифицированность);
	ПриИзмененииМесяцаНачисления();
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцНачисленияСтрокойНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("МесяцНачисленияСтрокойНачалоВыбораЗавершение", ЭтотОбъект);
	ЗарплатаКадрыКлиент.ВводМесяцаНачалоВыбора(ЭтаФорма, ЭтаФорма, "Объект.МесяцНачисления", "МесяцНачисленияСтрокой", , Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцНачисленияСтрокойНачалоВыбораЗавершение(ЗначениеВыбрано, ДополнительныеПараметры) Экспорт
	
	ПриИзмененииМесяцаНачисления();
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцНачисленияСтрокойРегулирование(Элемент, Направление, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаРегулирование(ЭтаФорма, "Объект.МесяцНачисления", "МесяцНачисленияСтрокой", Направление, Модифицированность);
	ПодключитьОбработчикОжидания("ОбработчикОжиданияМесяцНачисленияПриИзменении", 0.3, Истина);
КонецПроцедуры

&НаКлиенте
Процедура МесяцНачисленияСтрокойАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура МесяцНачисленияСтрокойОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаОкончаниеВводаТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНачисления

&НаКлиенте
Процедура НачисленияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПриНажатииПолеРаспределениеПоТерриториямУсловиямТруда(ОписаниеТаблицыНачислений(ЭтаФорма), Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка);
	
	Если Не СтандартнаяОбработка Тогда
		Возврат;	
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Элемент", Элемент);
	ДополнительныеПараметры.Вставить("ВыбраннаяСтрока", ВыбраннаяСтрока);
	ДополнительныеПараметры.Вставить("Поле", Поле);
	
	Оповещение = Новый ОписаниеОповещения("НачисленияВыборЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	РасчетЗарплатыРасширенныйКлиент.ВыполнитьКомандуРасшифровкиЗначенияПоказателя(ЭтаФорма, ОписаниеТаблицыНачислений(ЭтаФорма), Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка, Оповещение);
	
	Если СтрНайти(Поле.Имя, "КомандаРедактированияРаспределения") <> 0 Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура НачисленияВыборЗавершение(Результат, ДополнительныеПараметры) Экспорт 
	
	Элемент = ДополнительныеПараметры.Элемент;
	ВыбраннаяСтрока = ДополнительныеПараметры.ВыбраннаяСтрока;
	Поле = ДополнительныеПараметры.Поле;
	
	ВыполнитьКомандуРедактированияРезультатовРаспределенияНачисленийИУдержаний(ОписаниеТаблицыНачислений(ЭтаФорма), Элемент, ВыбраннаяСтрока, Поле);
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПриАктивизацииСтроки(Элемент)
	РасчетЗарплатыРасширенныйКлиент.ДокументыВыполненияНачисленийПриАктивацииСтроки(ЭтаФорма, "Начисления", Истина);
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПередУдалением(Элемент, Отказ)
	
	СотрудникиПередУдалением = РасчетЗарплатыРасширенныйКлиент.ВыделенныеСотрудникиКоллекции(Объект.Начисления, Элементы.Начисления.ВыделенныеСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	РасчетЗарплатыКлиент.СтрокаРасчетаПриНачалеРедактирования(ЭтаФорма, "Начисления", Элементы.Начисления.ТекущиеДанные, НоваяСтрока, Копирование);
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	РасчетЗарплатыРасширенныйКлиент.ДобавитьИзмененныеДанные(
		ИзмененныеДанные,
		"Начисления",
		,
		ТекущиеДанные.Сотрудник,
		ТекущиеДанные.Начисление);
		
	ОписаниеТаблицы = ОписаниеТаблицыНачислений(ЭтаФорма);
	Если НоваяСтрока Тогда
		ДобавитьИдентификаторСтрокиФизическогоЛица(ТекущиеДанные.ФизическоеЛицо, ОписаниеТаблицы.ИмяТаблицы, ТекущиеДанные.ПолучитьИдентификатор());
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
		
	ОписаниеТаблицы = ОписаниеТаблицыНачислений(ЭтаФорма);
	РасчетЗарплатыКлиент.СтрокаРасчетаПриОкончанииРедактирования(ЭтаФорма, ОписаниеТаблицы);
	УстановитьДоступностьКомандыЗаполнитьССохранениемИсправленийПриОкончанииРедактированияСтроки(Элемент.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПослеУдаления(Элемент) 
	
	ПересчитатьНачисленияПослеУдаленияСтроки(СотрудникиПередУдалением);
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьНачисленияПослеУдаленияСтроки(СотрудникиПередУдалением)
	
	РассчитатьСотрудника(СотрудникиПередУдалением, ОписаниеТаблицыНачислений(ЭтаФорма));
	ЗарплатаКадрыРасширенныйКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(
		ЭтаФорма, , 
		КлючевыеРеквизитыОписаниеКлючевыхРеквизитов(ИспользоватьВоеннуюСлужбу), 
		КлючевыеРеквизитыТаблицыОчищаемыеПриИзменении());
	ЗарплатаКадрыРасширенныйКлиентСервер.ПланируемаяДатаВыплатыУстановитьОтображениеПредупреждения(ЭтаФорма,"ПланируемаяДатаВыплаты",
												СтрокиТаблицДокументаПоФизическимЛицам);
	УстановитьДоступностьКомандыЗаполнитьССохранениемИсправленийПослеУдаленияСтроки();
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияСотрудникПриИзменении(Элемент)
	ОписаниеТаблицы = ОписаниеТаблицыНачислений(ЭтаФорма);
	ПриИзмененииСотрудника(Элемент, ОписаниеТаблицы.ИмяТаблицы);
	РасчетЗарплатыРасширенныйКлиент.ДополнитьСтрокуРасчета(ЭтаФорма, ОписаниеТаблицы, Истина, Истина);
КонецПроцедуры

&НаКлиенте
Процедура НачисленияНачислениеПриИзменении(Элемент)
	
	ОписаниеТаблицыВидовРасчета = ОписаниеТаблицыНачислений(ЭтаФорма);
	ЗарплатаКадрыРасширенныйКлиент.ВводНачисленийНачислениеПриИзменении(ЭтаФорма, ОписаниеТаблицыВидовРасчета, 2);
	РасчетЗарплатыРасширенныйКлиент.ДополнитьСтрокуРасчета(ЭтаФорма, ОписаниеТаблицыВидовРасчета, Истина, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияДатаНачалаПриИзменении(Элемент)
	РасчетЗарплатыРасширенныйКлиент.ДополнитьСтрокуРасчета(ЭтаФорма, ОписаниеТаблицыНачислений(ЭтаФорма), Истина, Истина);
КонецПроцедуры

&НаКлиенте
Процедура НачисленияДатаОкончанияПриИзменении(Элемент)
	РасчетЗарплатыРасширенныйКлиент.ДополнитьСтрокуРасчета(ЭтаФорма, ОписаниеТаблицыНачислений(ЭтаФорма), Ложь, Истина);
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПериодДействияПриИзменении(Элемент)
	ЗарплатаКадрыКлиент.ВводМесяцаПриИзменении(Элементы.Начисления.ТекущиеДанные, "ПериодДействия", "ПериодДействияСтрокой", Модифицированность);
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПериодДействияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаНачалоВыбора(ЭтаФорма, Элементы.Начисления.ТекущиеДанные, "ПериодДействия", "ПериодДействияСтрокой");
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПериодДействияРегулирование(Элемент, Направление, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаРегулирование(Элементы.Начисления.ТекущиеДанные, "ПериодДействия", "ПериодДействияСтрокой", Направление, Модифицированность);
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПериодДействияАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПериодДействияОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаОкончаниеВводаТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНачисленияПерерасчет

&НаКлиенте
Процедура НачисленияПерерасчетВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПриНажатииПолеРаспределениеПоТерриториямУсловиямТруда(ОписаниеТаблицыПерерасчетов(ЭтаФорма), Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка);
	
	Если Не СтандартнаяОбработка Тогда
		Возврат;	
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Элемент", Элемент);
	ДополнительныеПараметры.Вставить("ВыбраннаяСтрока", ВыбраннаяСтрока);
	ДополнительныеПараметры.Вставить("Поле", Поле);
	
	Оповещение = Новый ОписаниеОповещения("НачисленияПерерасчетВыборЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	РасчетЗарплатыРасширенныйКлиент.ВыполнитьКомандуРасшифровкиЗначенияПоказателя(ЭтаФорма, ОписаниеТаблицыПерерасчетов(ЭтаФорма), Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка, Оповещение);
	
	Если СтрНайти(Поле.Имя, "КомандаРедактированияРаспределения") <> 0 Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПерерасчетВыборЗавершение(Результат, ДополнительныеПараметры) Экспорт 
	
	Элемент = ДополнительныеПараметры.Элемент;
	ВыбраннаяСтрока = ДополнительныеПараметры.ВыбраннаяСтрока;
	Поле = ДополнительныеПараметры.Поле;
	
	ВыполнитьКомандуРедактированияРезультатовРаспределенияНачисленийИУдержаний(ОписаниеТаблицыПерерасчетов(ЭтаФорма), Элемент, ВыбраннаяСтрока, Поле);
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПерерасчетПриАктивизацииСтроки(Элемент)
	
	РасчетЗарплатыРасширенныйКлиент.ДокументыВыполненияНачисленийПриАктивацииСтроки(ЭтаФорма, "НачисленияПерерасчет", Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПерерасчетПередУдалением(Элемент, Отказ)
	
	СотрудникиПередУдалением = РасчетЗарплатыРасширенныйКлиент.ВыделенныеСотрудникиКоллекции(Объект.НачисленияПерерасчет, Элементы.НачисленияПерерасчет.ВыделенныеСтроки);
	УдалитьВыделенныеПерерасчеты(Элементы.НачисленияПерерасчет.ВыделенныеСтроки, Объект.НачисленияПерерасчет, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПерерасчетПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	РасчетЗарплатыКлиент.СтрокаРасчетаПриНачалеРедактирования(
		ЭтаФорма, "НачисленияПерерасчет", Элементы.НачисленияПерерасчет.ТекущиеДанные, НоваяСтрока, Копирование);
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПерерасчетПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	РасчетЗарплатыРасширенныйКлиент.ДобавитьИзмененныеДанные(
		ИзмененныеДанные,
		"Начисления",
		,
		ТекущиеДанные.Сотрудник,
		ТекущиеДанные.Начисление);
		
	ОписаниеТаблицы = ОписаниеТаблицыПерерасчетов(ЭтаФорма);
	Если НоваяСтрока Тогда
		ДобавитьИдентификаторСтрокиФизическогоЛица(ТекущиеДанные.ФизическоеЛицо, ОписаниеТаблицы.ИмяТаблицы, ТекущиеДанные.ПолучитьИдентификатор());
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПерерасчетПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
		
	ОписаниеТаблицы = ОписаниеТаблицыПерерасчетов(ЭтаФорма);
	РасчетЗарплатыКлиент.СтрокаРасчетаПриОкончанииРедактирования(ЭтаФорма, ОписаниеТаблицы);
	УстановитьДоступностьКомандыЗаполнитьССохранениемИсправленийПриОкончанииРедактированияСтроки(Элемент.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПерерасчетОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ОбработкаПодбораНаКлиенте(ВыбранноеЗначение);
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПерерасчетПослеУдаления(Элемент)
	
	РассчитатьСотрудника(СотрудникиПередУдалением, ОписаниеТаблицыПерерасчетов(ЭтаФорма));
	ЗарплатаКадрыРасширенныйКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(
		ЭтаФорма, , 
		КлючевыеРеквизитыОписаниеКлючевыхРеквизитов(ИспользоватьВоеннуюСлужбу), 
		КлючевыеРеквизитыТаблицыОчищаемыеПриИзменении());
	ЗарплатаКадрыРасширенныйКлиентСервер.ПланируемаяДатаВыплатыУстановитьОтображениеПредупреждения(ЭтаФорма,"ПланируемаяДатаВыплаты",
												СтрокиТаблицДокументаПоФизическимЛицам);
	УстановитьДоступностьКомандыЗаполнитьССохранениемИсправленийПослеУдаленияСтроки();
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПерерасчетСотрудникПриИзменении(Элемент)
	
	ОписаниеТаблицы = ОписаниеТаблицыПерерасчетов(ЭтаФорма);
	ПриИзмененииСотрудника(Элемент, ОписаниеТаблицы.ИмяТаблицы);
	РасчетЗарплатыРасширенныйКлиент.ДополнитьСтрокуРасчета(ЭтаФорма, ОписаниеТаблицы, Истина, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПерерасчетНачислениеПриИзменении(Элемент)
	
	ОписаниеТаблицыВидовРасчета = ОписаниеТаблицыПерерасчетов(ЭтаФорма);
	ЗарплатаКадрыРасширенныйКлиент.ВводНачисленийНачислениеПриИзменении(ЭтаФорма, ОписаниеТаблицыВидовРасчета, 2);
	РасчетЗарплатыРасширенныйКлиент.ДополнитьСтрокуРасчета(ЭтаФорма, ОписаниеТаблицыВидовРасчета, Истина, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПерерасчетДатаНачалаПриИзменении(Элемент)
	
	РасчетЗарплатыРасширенныйКлиент.ДополнитьСтрокуРасчета(ЭтаФорма, ОписаниеТаблицыПерерасчетов(ЭтаФорма), Истина, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПерерасчетДатаОкончанияПриИзменении(Элемент)
	
	РасчетЗарплатыРасширенныйКлиент.ДополнитьСтрокуРасчета(ЭтаФорма, ОписаниеТаблицыПерерасчетов(ЭтаФорма), Ложь, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПерерасчетПериодДействияПриИзменении(Элемент)
	ЗарплатаКадрыКлиент.ВводМесяцаПриИзменении(Элементы.НачисленияПерерасчет.ТекущиеДанные, "ПериодДействия", "ПериодДействияСтрокой", Модифицированность);
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПерерасчетПериодДействияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаНачалоВыбора(ЭтаФорма, Элементы.НачисленияПерерасчет.ТекущиеДанные, "ПериодДействия", "ПериодДействияСтрокой");
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПерерасчетПериодДействияРегулирование(Элемент, Направление, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаРегулирование(Элементы.НачисленияПерерасчет.ТекущиеДанные, "ПериодДействия", "ПериодДействияСтрокой", Направление, Модифицированность);
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПерерасчетПериодДействияАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПерерасчетПериодДействияОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаОкончаниеВводаТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНачисленияПоДоговорам

&НаКлиенте
Процедура НачисленияПоДоговорамВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПриНажатииПолеРаспределениеПоТерриториямУсловиямТруда(ОписаниеТаблицыНачисленийПоДоговорам(ЭтаФорма), Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка);
	
	Если Не СтандартнаяОбработка Тогда
		Возврат;
	КонецЕсли;
	
	ВыполнитьКомандуРедактированияРезультатовРаспределенияНачисленийИУдержаний(ОписаниеТаблицыНачисленийПоДоговорам(ЭтаФорма), Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПоДоговорамСотрудникПриИзменении(Элемент)
	ПриИзмененииСотрудника(Элемент, "НачисленияПоДоговорам");
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПоДоговорамДокументОснованиеПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.НачисленияПоДоговорам.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(ТекущиеДанные.ДокументОснование) Тогда
		ЗаполнитьСотрудникаПриВыбореДоговораГПХ();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПоДоговорамПриАктивизацииСтроки(Элемент)
	РасчетЗарплатыРасширенныйКлиент.ДокументыВыполненияНачисленийПриАктивацииСтроки(ЭтаФорма, "НачисленияПоДоговорам", Истина);
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПоДоговорамПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	РасчетЗарплатыКлиент.СтрокаРасчетаПриНачалеРедактирования(ЭтаФорма, "НачисленияПоДоговорам", Элементы.НачисленияПоДоговорам.ТекущиеДанные, НоваяСтрока, Копирование);
	Если НоваяСтрока Тогда
		Элементы.НачисленияПоДоговорам.ТекущиеДанные.ПланируемаяДатаВыплаты = 
			РасчетЗарплатыРасширенныйКлиентСервер.ПланируемаяДатаВыплатыЗарплатыПоНастройкам(Объект.МесяцНачисления, НастройкиВыплатыЗарплатыВОрганизации);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПоДоговорамПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	РасчетЗарплатыРасширенныйКлиент.ДобавитьИзмененныеДанные(ИзмененныеДанные, "ДоговорыПодряда",, ТекущиеДанные.Сотрудник);
	
	ОписаниеТаблицы = ОписаниеТаблицыНачисленийПоДоговорам(ЭтаФорма);
	Если НоваяСтрока Тогда
		ДобавитьИдентификаторСтрокиФизическогоЛица(ТекущиеДанные.ФизическоеЛицо, ОписаниеТаблицы.ИмяТаблицы, ТекущиеДанные.ПолучитьИдентификатор());
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПоДоговорамПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеТаблицы = ОписаниеТаблицыНачисленийПоДоговорам(ЭтаФорма);
	РасчетЗарплатыКлиент.СтрокаРасчетаПриОкончанииРедактирования(ЭтаФорма, ОписаниеТаблицы);
	НачисленияПоДоговорамПриОкончанииРедактированияНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПоДоговорамПередУдалением(Элемент, Отказ)
	
	СотрудникиПередУдалением = РасчетЗарплатыРасширенныйКлиент.ВыделенныеСотрудникиКоллекции(Объект.НачисленияПоДоговорам, Элементы.НачисленияПоДоговорам.ВыделенныеСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПоДоговорамПослеУдаления(Элемент)
	РассчитатьСотрудника(СотрудникиПередУдалением, ОписаниеТаблицыНачисленийПоДоговорам(ЭтаФорма));
	УстановитьДоступностьКомандыЗаполнитьССохранениемИсправленийПослеУдаленияСтроки();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПособия

&НаКлиенте
Процедура ПособияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПриНажатииПолеРаспределениеПоТерриториямУсловиямТруда(ОписаниеТаблицыПособия(ЭтаФорма), Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка);
	
	Если Не СтандартнаяОбработка Тогда
		Возврат;	
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Элемент", Элемент);
	ДополнительныеПараметры.Вставить("ВыбраннаяСтрока", ВыбраннаяСтрока);
	ДополнительныеПараметры.Вставить("Поле", Поле);
	
	Оповещение = Новый ОписаниеОповещения("ПособияВыборЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	РасчетЗарплатыРасширенныйКлиент.ВыполнитьКомандуРасшифровкиЗначенияПоказателя(ЭтаФорма, ОписаниеТаблицыПособия(ЭтаФорма), Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка, Оповещение);
	
	Если СтрНайти(Поле.Имя, "КомандаРедактированияРаспределения") <> 0 Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПособияВыборЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Элемент = ДополнительныеПараметры.Элемент;
	ВыбраннаяСтрока = ДополнительныеПараметры.ВыбраннаяСтрока;
	Поле = ДополнительныеПараметры.Поле;
	
	ВыполнитьКомандуРедактированияРезультатовРаспределенияНачисленийИУдержаний(ОписаниеТаблицыПособия(ЭтаФорма), Элемент, ВыбраннаяСтрока, Поле);
	
КонецПроцедуры

&НаКлиенте
Процедура ПособияПриАктивизацииСтроки(Элемент)
	РасчетЗарплатыРасширенныйКлиент.ДокументыВыполненияНачисленийПриАктивацииСтроки(ЭтаФорма, "Пособия", Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПособияПередУдалением(Элемент, Отказ)
	
	СотрудникиПередУдалением = РасчетЗарплатыРасширенныйКлиент.ВыделенныеСотрудникиКоллекции(Объект.Пособия, Элементы.Пособия.ВыделенныеСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура ПособияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	РасчетЗарплатыКлиент.СтрокаРасчетаПриНачалеРедактирования(ЭтаФорма, "Пособия", Элементы.Пособия.ТекущиеДанные, НоваяСтрока, Копирование);
КонецПроцедуры

&НаКлиенте
Процедура ПособияПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	РасчетЗарплатыРасширенныйКлиент.ДобавитьИзмененныеДанные(
		ИзмененныеДанные,
		"Начисления",
		,
		ТекущиеДанные.Сотрудник,
		ТекущиеДанные.Начисление);
		
	ОписаниеТаблицы = ОписаниеТаблицыПособия(ЭтаФорма);
	Если НоваяСтрока Тогда
		ДобавитьИдентификаторСтрокиФизическогоЛица(ТекущиеДанные.ФизическоеЛицо, ОписаниеТаблицы.ИмяТаблицы, ТекущиеДанные.ПолучитьИдентификатор());
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПособияПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
		
	ОписаниеТаблицы = ОписаниеТаблицыПособия(ЭтаФорма);
	РасчетЗарплатыКлиент.СтрокаРасчетаПриОкончанииРедактирования(ЭтаФорма, ОписаниеТаблицы);
	УстановитьДоступностьКомандыЗаполнитьССохранениемИсправленийПриОкончанииРедактированияСтроки(Элемент.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ПособияПослеУдаления(Элемент) 
	
	ПересчитатьНачисленияПослеУдаленияСтроки(СотрудникиПередУдалением);
	
КонецПроцедуры

&НаКлиенте
Процедура ПособияСотрудникПриИзменении(Элемент)
	ОписаниеТаблицы = ОписаниеТаблицыПособия(ЭтаФорма);
	ПриИзмененииСотрудника(Элемент, ОписаниеТаблицы.ИмяТаблицы);
	РасчетЗарплатыРасширенныйКлиент.ДополнитьСтрокуРасчета(ЭтаФорма, ОписаниеТаблицы, Истина, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПособияНачислениеПриИзменении(Элемент)
	
	ОписаниеТаблицыВидовРасчета = ОписаниеТаблицыПособия(ЭтаФорма);
	ЗарплатаКадрыРасширенныйКлиент.ВводНачисленийНачислениеПриИзменении(ЭтаФорма, ОписаниеТаблицыВидовРасчета, 2);
	РасчетЗарплатыРасширенныйКлиент.ДополнитьСтрокуРасчета(ЭтаФорма, ОписаниеТаблицыВидовРасчета, Истина, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПособияДатаНачалаПриИзменении(Элемент)
	РасчетЗарплатыРасширенныйКлиент.ДополнитьСтрокуРасчета(ЭтаФорма, ОписаниеТаблицыПособия(ЭтаФорма), Истина, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПособияДатаОкончанияПриИзменении(Элемент)
	РасчетЗарплатыРасширенныйКлиент.ДополнитьСтрокуРасчета(ЭтаФорма, ОписаниеТаблицыПособия(ЭтаФорма), Ложь, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПособияПериодДействияПриИзменении(Элемент)
	ЗарплатаКадрыКлиент.ВводМесяцаПриИзменении(Элементы.Пособия.ТекущиеДанные, "ПериодДействия", "ПериодДействияСтрокой", Модифицированность);
КонецПроцедуры

&НаКлиенте
Процедура ПособияПериодДействияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаНачалоВыбора(ЭтаФорма, Элементы.Пособия.ТекущиеДанные, "ПериодДействия", "ПериодДействияСтрокой");
КонецПроцедуры

&НаКлиенте
Процедура ПособияПериодДействияРегулирование(Элемент, Направление, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаРегулирование(Элементы.Пособия.ТекущиеДанные, "ПериодДействия", "ПериодДействияСтрокой", Направление, Модифицированность);
КонецПроцедуры

&НаКлиенте
Процедура ПособияПериодДействияАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ПособияПериодДействияОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаОкончаниеВводаТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПособияПерерасчет

&НаКлиенте
Процедура ПособияПерерасчетВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПриНажатииПолеРаспределениеПоТерриториямУсловиямТруда(ОписаниеТаблицыПособияПерерасчет(ЭтаФорма), Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка);
	
	Если Не СтандартнаяОбработка Тогда
		Возврат;	
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Элемент", Элемент);
	ДополнительныеПараметры.Вставить("ВыбраннаяСтрока", ВыбраннаяСтрока);
	ДополнительныеПараметры.Вставить("Поле", Поле);
	
	Оповещение = Новый ОписаниеОповещения("ПособияПерерасчетВыборЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	РасчетЗарплатыРасширенныйКлиент.ВыполнитьКомандуРасшифровкиЗначенияПоказателя(ЭтаФорма, ОписаниеТаблицыПособияПерерасчет(ЭтаФорма), Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка, Оповещение);
	
	Если СтрНайти(Поле.Имя, "КомандаРедактированияРаспределения") <> 0 Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПособияПерерасчетВыборЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Элемент = ДополнительныеПараметры.Элемент;
	ВыбраннаяСтрока = ДополнительныеПараметры.ВыбраннаяСтрока;
	Поле = ДополнительныеПараметры.Поле;
	
	ВыполнитьКомандуРедактированияРезультатовРаспределенияНачисленийИУдержаний(ОписаниеТаблицыПособияПерерасчет(ЭтаФорма), Элемент, ВыбраннаяСтрока, Поле);
	
КонецПроцедуры

&НаКлиенте
Процедура ПособияПерерасчетПриАктивизацииСтроки(Элемент)
	РасчетЗарплатыРасширенныйКлиент.ДокументыВыполненияНачисленийПриАктивацииСтроки(ЭтаФорма, "ПособияПерерасчет", Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПособияПерерасчетПередУдалением(Элемент, Отказ)
	
	СотрудникиПередУдалением = РасчетЗарплатыРасширенныйКлиент.ВыделенныеСотрудникиКоллекции(Объект.ПособияПерерасчет, Элементы.ПособияПерерасчет.ВыделенныеСтроки);
	УдалитьВыделенныеПерерасчеты(Элементы.ПособияПерерасчет.ВыделенныеСтроки, Объект.ПособияПерерасчет,,Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПособияПерерасчетПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	РасчетЗарплатыКлиент.СтрокаРасчетаПриНачалеРедактирования(ЭтаФорма, "ПособияПерерасчет", Элементы.ПособияПерерасчет.ТекущиеДанные, НоваяСтрока, Копирование);
КонецПроцедуры

&НаКлиенте
Процедура ПособияПерерасчетПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	РасчетЗарплатыРасширенныйКлиент.ДобавитьИзмененныеДанные(
		ИзмененныеДанные,
		"Начисления",
		,
		ТекущиеДанные.Сотрудник,
		ТекущиеДанные.Начисление);
		
	ОписаниеТаблицы = ОписаниеТаблицыПособияПерерасчет(ЭтаФорма);
	Если НоваяСтрока Тогда
		ДобавитьИдентификаторСтрокиФизическогоЛица(ТекущиеДанные.ФизическоеЛицо, ОписаниеТаблицы.ИмяТаблицы, ТекущиеДанные.ПолучитьИдентификатор());
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПособияПерерасчетПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
		
	ОписаниеТаблицы = ОписаниеТаблицыПособияПерерасчет(ЭтаФорма);
	РасчетЗарплатыКлиент.СтрокаРасчетаПриОкончанииРедактирования(ЭтаФорма, ОписаниеТаблицы);
	УстановитьДоступностьКомандыЗаполнитьССохранениемИсправленийПриОкончанииРедактированияСтроки(Элемент.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ПособияПерерасчетПослеУдаления(Элемент) 
	
	ПересчитатьНачисленияПослеУдаленияСтроки(СотрудникиПередУдалением);
	
КонецПроцедуры

&НаКлиенте
Процедура ПособияПерерасчетСотрудникПриИзменении(Элемент)
	ОписаниеТаблицы = ОписаниеТаблицыПособияПерерасчет(ЭтаФорма);
	ПриИзмененииСотрудника(Элемент, ОписаниеТаблицы.ИмяТаблицы);
	РасчетЗарплатыРасширенныйКлиент.ДополнитьСтрокуРасчета(ЭтаФорма, ОписаниеТаблицы, Истина, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПособияПерерасчетНачислениеПриИзменении(Элемент)
	
	ОписаниеТаблицыВидовРасчета = ОписаниеТаблицыПособияПерерасчет(ЭтаФорма);
	ЗарплатаКадрыРасширенныйКлиент.ВводНачисленийНачислениеПриИзменении(ЭтаФорма, ОписаниеТаблицыВидовРасчета, 2);
	РасчетЗарплатыРасширенныйКлиент.ДополнитьСтрокуРасчета(ЭтаФорма, ОписаниеТаблицыВидовРасчета, Истина, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПособияПерерасчетДатаНачалаПриИзменении(Элемент)
	РасчетЗарплатыРасширенныйКлиент.ДополнитьСтрокуРасчета(ЭтаФорма, ОписаниеТаблицыПособияПерерасчет(ЭтаФорма), Истина, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПособияПерерасчетДатаОкончанияПриИзменении(Элемент)
	РасчетЗарплатыРасширенныйКлиент.ДополнитьСтрокуРасчета(ЭтаФорма, ОписаниеТаблицыПособияПерерасчет(ЭтаФорма), Ложь, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПособияПерерасчетПериодДействияПриИзменении(Элемент)
	ЗарплатаКадрыКлиент.ВводМесяцаПриИзменении(Элементы.ПособияПерерасчет.ТекущиеДанные, "ПериодДействия", "ПериодДействияСтрокой", Модифицированность);
КонецПроцедуры

&НаКлиенте
Процедура ПособияПерерасчетПериодДействияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаНачалоВыбора(ЭтаФорма, Элементы.ПособияПерерасчет.ТекущиеДанные, "ПериодДействия", "ПериодДействияСтрокой");
КонецПроцедуры

&НаКлиенте
Процедура ПособияПерерасчетПериодДействияРегулирование(Элемент, Направление, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаРегулирование(Элементы.ПособияПерерасчет.ТекущиеДанные, "ПериодДействия", "ПериодДействияСтрокой", Направление, Модифицированность);
КонецПроцедуры

&НаКлиенте
Процедура ПособияПерерасчетПериодДействияАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ПособияПерерасчетПериодДействияОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаОкончаниеВводаТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУдержания

&НаКлиенте
Процедура УдержанияПриАктивизацииСтроки(Элемент)
	РасчетЗарплатыРасширенныйКлиент.ДокументыВыполненияНачисленийПриАктивацииСтроки(ЭтаФорма, "Удержания", Истина);
КонецПроцедуры

&НаКлиенте
Процедура УдержанияУдержаниеПриИзменении(Элемент)
	ОписаниеТаблицыВидовРасчета = ОписаниеТаблицыУдержаний();
	ЗарплатаКадрыРасширенныйКлиент.ВводНачисленийНачислениеПриИзменении(ЭтаФорма, ОписаниеТаблицыВидовРасчета, 2);
КонецПроцедуры

&НаКлиенте
Процедура УдержанияДокументОснованиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ЗарплатаКадрыРасширенныйКлиент.УстановитьОграничениеТипаДокументаУдержанияПоКатегории(
		Элемент, Элементы.Удержания.ТекущиеДанные, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура УдержанияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	СтароеЗначениеФизическогоЛица = Элементы.Удержания.ТекущиеДанные.ФизическоеЛицо;
	РасчетЗарплатыКлиент.СтрокаРасчетаПриНачалеРедактирования(ЭтаФорма, "Удержания", Элементы.Удержания.ТекущиеДанные, НоваяСтрока, Копирование);
КонецПроцедуры

&НаКлиенте
Процедура УдержанияПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	РасчетЗарплатыРасширенныйКлиент.ДобавитьИзмененныеДанные(
		ИзмененныеДанные,
		"Удержания",
		ТекущиеДанные.ФизическоеЛицо,
		,
		ТекущиеДанные.Удержание);
		
	ОписаниеТаблицы = ОписаниеТаблицыУдержаний();
	Если НоваяСтрока Тогда
		ДобавитьИдентификаторСтрокиФизическогоЛица(ТекущиеДанные.ФизическоеЛицо, ОписаниеТаблицы.ИмяТаблицы, ТекущиеДанные.ПолучитьИдентификатор());
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УдержанияПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если ИспользоватьСтатьиФинансирования Тогда
		Если ТекущиеДанные <> Неопределено Тогда
			Если ТекущиеДанные.РезультатРаспределения = Неопределено И ЗначениеЗаполнено(АдресТаблицыРаспределенияУдержаний) Тогда 
				ТекущиеДанные.РезультатРаспределения = РезультатРаспределенияИзВременногоХранилища(ТекущиеДанные.ИдентификаторСтрокиВидаРасчета, АдресТаблицыРаспределенияУдержаний, ОписаниеТаблицыНДФЛ());
			КонецЕсли;
			ОтражениеЗарплатыВБухучетеКлиентСерверРасширенный.ПерераспределитьУдержания(ТекущиеДанные, РаботаВБюджетномУчреждении);
		КонецЕсли;
	КонецЕсли;	
	
	ОписаниеТаблицы = ОписаниеТаблицыУдержаний();	
	РасчетЗарплатыКлиент.СтрокаРасчетаПриОкончанииРедактирования(ЭтаФорма, ОписаниеТаблицы);
	УстановитьДоступностьКомандыЗаполнитьССохранениемИсправленийПриОкончанииРедактированияСтроки(Элемент.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УдержанияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Элемент", Элемент);
	ДополнительныеПараметры.Вставить("ВыбраннаяСтрока", ВыбраннаяСтрока);
	ДополнительныеПараметры.Вставить("Поле", Поле);
	
	Оповещение = Новый ОписаниеОповещения("УдержанияВыборЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	РасчетЗарплатыРасширенныйКлиент.ВыполнитьКомандуРасшифровкиЗначенияПоказателя(
		ЭтаФорма, ОписаниеТаблицыУдержаний(), Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка, Оповещение);
		
	Если СтрНайти(Поле.Имя, "КомандаРедактированияРаспределения") <> 0 Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура УдержанияВыборЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Элемент = ДополнительныеПараметры.Элемент;
	ВыбраннаяСтрока = ДополнительныеПараметры.ВыбраннаяСтрока;
	Поле = ДополнительныеПараметры.Поле;
	
	ВыполнитьКомандуРедактированияРезультатовРаспределенияНачисленийИУдержаний(ОписаниеТаблицыУдержаний(), Элемент, ВыбраннаяСтрока, Поле);
	
КонецПроцедуры

&НаКлиенте
Процедура УдержанияПередУдалением(Элемент, Отказ)
	
	СотрудникиПередУдалением = РасчетЗарплатыРасширенныйКлиент.ВыделенныеСотрудникиКоллекции(Объект.Удержания, Элементы.Удержания.ВыделенныеСтроки, "ФизическоеЛицо");
	
КонецПроцедуры

&НаКлиенте
Процедура УдержанияПослеУдаления(Элемент)
	
	РассчитатьСотрудника(СотрудникиПередУдалением, ОписаниеТаблицыУдержаний());
	ЗарплатаКадрыРасширенныйКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(
		ЭтаФорма, , 
		КлючевыеРеквизитыОписаниеКлючевыхРеквизитов(ИспользоватьВоеннуюСлужбу), 
		КлючевыеРеквизитыТаблицыОчищаемыеПриИзменении());
	ЗарплатаКадрыРасширенныйКлиентСервер.ПланируемаяДатаВыплатыУстановитьОтображениеПредупреждения(ЭтаФорма,"ПланируемаяДатаВыплаты",
												СтрокиТаблицДокументаПоФизическимЛицам);
	УстановитьДоступностьКомандыЗаполнитьССохранениемИсправленийПослеУдаленияСтроки();
	
КонецПроцедуры

&НаКлиенте
Процедура УдержанияФизическоеЛицоПриИзменении(Элемент)
	ОписаниеТаблицы = ОписаниеТаблицыУдержаний();
	Элементы.Удержания.ТекущиеДанные.Сотрудник = Неопределено;
	ПриИзмененииФизическогоЛица(Элемент, ОписаниеТаблицы.ИмяТаблицы);
	РасчетЗарплатыРасширенныйКлиент.ДополнитьСтрокуРасчета(ЭтаФорма, ОписаниеТаблицы, Истина, Истина);
КонецПроцедуры

&НаКлиенте
Процедура УдержанияДатаНачалаПриИзменении(Элемент)
	РасчетЗарплатыРасширенныйКлиент.ДополнитьСтрокуРасчета(ЭтаФорма, ОписаниеТаблицыУдержаний(), Истина, Истина);
КонецПроцедуры

&НаКлиенте
Процедура УдержанияДатаОкончанияПриИзменении(Элемент)
	РасчетЗарплатыРасширенныйКлиент.ДополнитьСтрокуРасчета(ЭтаФорма, ОписаниеТаблицыУдержаний(), Ложь, Истина);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУдержанияПерерасчет

&НаКлиенте
Процедура УдержанияПерерасчетПриАктивизацииСтроки(Элемент)
	РасчетЗарплатыРасширенныйКлиент.ДокументыВыполненияНачисленийПриАктивацииСтроки(ЭтаФорма, "УдержанияПерерасчет", Истина);
КонецПроцедуры

&НаКлиенте
Процедура УдержанияПерерасчетУдержаниеПриИзменении(Элемент)
	ОписаниеТаблицыВидовРасчета = ОписаниеТаблицыУдержанияПерерасчет();
	ЗарплатаКадрыРасширенныйКлиент.ВводНачисленийНачислениеПриИзменении(ЭтаФорма, ОписаниеТаблицыВидовРасчета, 2);
КонецПроцедуры

&НаКлиенте
Процедура УдержанияПерерасчетДокументОснованиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ЗарплатаКадрыРасширенныйКлиент.УстановитьОграничениеТипаДокументаУдержанияПоКатегории(
		Элемент, Элементы.УдержанияПерерасчет.ТекущиеДанные, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура УдержанияПерерасчетПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	СтароеЗначениеФизическогоЛица = Элементы.УдержанияПерерасчет.ТекущиеДанные.ФизическоеЛицо;
	РасчетЗарплатыКлиент.СтрокаРасчетаПриНачалеРедактирования(ЭтаФорма, "УдержанияПерерасчет", Элементы.УдержанияПерерасчет.ТекущиеДанные, НоваяСтрока, Копирование);
КонецПроцедуры

&НаКлиенте
Процедура УдержанияПерерасчетПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	РасчетЗарплатыРасширенныйКлиент.ДобавитьИзмененныеДанные(
		ИзмененныеДанные,
		"Удержания",
		ТекущиеДанные.ФизическоеЛицо,
		,
		ТекущиеДанные.Удержание);
	
	ОписаниеТаблицы = ОписаниеТаблицыУдержанияПерерасчет();
	Если НоваяСтрока Тогда
		ДобавитьИдентификаторСтрокиФизическогоЛица(ТекущиеДанные.ФизическоеЛицо, ОписаниеТаблицы.ИмяТаблицы, ТекущиеДанные.ПолучитьИдентификатор());
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УдержанияПерерасчетПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ИспользоватьСтатьиФинансирования Тогда
		Если ТекущиеДанные <> Неопределено Тогда
			Если ТекущиеДанные.РезультатРаспределения = Неопределено И ЗначениеЗаполнено(АдресТаблицыРаспределенияУдержаний) Тогда 
				ТекущиеДанные.РезультатРаспределения = РезультатРаспределенияИзВременногоХранилища(ТекущиеДанные.ИдентификаторСтрокиВидаРасчета, АдресТаблицыРаспределенияУдержаний, ОписаниеТаблицыНДФЛ());
			КонецЕсли;
			ОтражениеЗарплатыВБухучетеКлиентСерверРасширенный.ПерераспределитьУдержания(ТекущиеДанные, РаботаВБюджетномУчреждении);
		КонецЕсли;
	КонецЕсли;
	
	ОписаниеТаблицы = ОписаниеТаблицыУдержанияПерерасчет();
	
	РасчетЗарплатыКлиент.СтрокаРасчетаПриОкончанииРедактирования(ЭтаФорма, ОписаниеТаблицы);
	УстановитьДоступностьКомандыЗаполнитьССохранениемИсправленийПриОкончанииРедактированияСтроки(Элемент.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УдержанияПерерасчетВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Элемент", Элемент);
	ДополнительныеПараметры.Вставить("ВыбраннаяСтрока", ВыбраннаяСтрока);
	ДополнительныеПараметры.Вставить("Поле", Поле);
	
	Оповещение = Новый ОписаниеОповещения("УдержанияПерерасчетВыборЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	РасчетЗарплатыРасширенныйКлиент.ВыполнитьКомандуРасшифровкиЗначенияПоказателя(
		ЭтаФорма, ОписаниеТаблицыУдержанияПерерасчет(), Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка, Оповещение);
		
	Если СтрНайти(Поле.Имя, "КомандаРедактированияРаспределения") <> 0 Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура УдержанияПерерасчетВыборЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Элемент = ДополнительныеПараметры.Элемент;
	ВыбраннаяСтрока = ДополнительныеПараметры.ВыбраннаяСтрока;
	Поле = ДополнительныеПараметры.Поле;
	
	ВыполнитьКомандуРедактированияРезультатовРаспределенияНачисленийИУдержаний(ОписаниеТаблицыУдержанияПерерасчет(), Элемент, ВыбраннаяСтрока, Поле);
	
КонецПроцедуры

&НаКлиенте
Процедура УдержанияПерерасчетПередУдалением(Элемент, Отказ)
	
	СотрудникиПередУдалением = РасчетЗарплатыРасширенныйКлиент.ВыделенныеСотрудникиКоллекции(Объект.УдержанияПерерасчет, Элементы.УдержанияПерерасчет.ВыделенныеСтроки, "ФизическоеЛицо");
	УдалитьВыделенныеПерерасчеты(Элементы.УдержанияПерерасчет.ВыделенныеСтроки, Объект.УдержанияПерерасчет,,,,Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура УдержанияПерерасчетПослеУдаления(Элемент)
	
	РассчитатьСотрудника(СотрудникиПередУдалением, ОписаниеТаблицыУдержанияПерерасчет());
	ЗарплатаКадрыРасширенныйКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(
		ЭтаФорма, , 
		КлючевыеРеквизитыОписаниеКлючевыхРеквизитов(ИспользоватьВоеннуюСлужбу), 
		КлючевыеРеквизитыТаблицыОчищаемыеПриИзменении());
	ЗарплатаКадрыРасширенныйКлиентСервер.ПланируемаяДатаВыплатыУстановитьОтображениеПредупреждения(ЭтаФорма,"ПланируемаяДатаВыплаты",
												СтрокиТаблицДокументаПоФизическимЛицам);
	УстановитьДоступностьКомандыЗаполнитьССохранениемИсправленийПослеУдаленияСтроки();
	
КонецПроцедуры

&НаКлиенте
Процедура УдержанияПерерасчетФизическоеЛицоПриИзменении(Элемент)
	ОписаниеТаблицы = ОписаниеТаблицыУдержанияПерерасчет();
	Элементы.УдержанияПерерасчет.ТекущиеДанные.Сотрудник = Неопределено;
	ПриИзмененииФизическогоЛица(Элемент, ОписаниеТаблицы.ИмяТаблицы);
	РасчетЗарплатыРасширенныйКлиент.ДополнитьСтрокуРасчета(ЭтаФорма, ОписаниеТаблицы, Истина, Истина);
КонецПроцедуры

&НаКлиенте
Процедура УдержанияПерерасчетДатаНачалаПриИзменении(Элемент)
	РасчетЗарплатыРасширенныйКлиент.ДополнитьСтрокуРасчета(ЭтаФорма, ОписаниеТаблицыУдержанияПерерасчет(), Истина, Истина);
КонецПроцедуры

&НаКлиенте
Процедура УдержанияПерерасчетДатаОкончанияПриИзменении(Элемент)
	РасчетЗарплатыРасширенныйКлиент.ДополнитьСтрокуРасчета(ЭтаФорма, ОписаниеТаблицыУдержанияПерерасчет(), Ложь, Истина);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНдфл

&НаКлиенте
Процедура НДФЛВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ИменаРесурсовЗачета = УчетНДФЛКлиентСервер.РесурсыИсчисленногоНалогаВМассиве("НДФЛЗачтеноАвансовыхПлатежей", Ложь, Ложь);
	Если Поле.Имя = "НДФЛПодразделение" Или Поле.Имя = "НДФЛМесяцНалоговогоПериода" Или ИменаРесурсовЗачета.Найти(Поле.Имя) <> Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.НДФЛ.ТекущиеДанные;
	Если Не ЗначениеЗаполнено(ТекущиеДанные.ФизическоеЛицо) Тогда
		Возврат;
	КонецЕсли;	
	
	СтандартнаяОбработка = Ложь;
	УчетНДФЛКлиент.ОткрытьФормуПодробнееОРасчетеНДФЛ(
		Объект.Организация, ЭтаФорма, Объект.МесяцНачисления, Элемент.ТекущиеДанные.ФизическоеЛицо, Объект.ПланируемаяДатаВыплаты);
	
КонецПроцедуры

&НаСервере
Функция СведенияОбНДФЛ(ФизическоеЛицо = Неопределено) Экспорт
	
	ОписаниеТаблицы = ОписаниеТаблицыНДФЛ();
	
	Если ФизическоеЛицо = Неопределено Тогда 
		ТекущиеДанные = Объект.НДФЛ.НайтиПоИдентификатору(Элементы.НДФЛ.ТекущаяСтрока);
		ФизическоеЛицо = ТекущиеДанные.ФизическоеЛицо;
	КонецЕсли;
	
	ДополнительныеСведения = УчетНДФЛФормыРасширенный.ДополнительныеДанныеДляПолученияСведенийОДоходахНДФЛДокумента();
	ДополнительныеСведения.МесяцНачисления = Объект.МесяцНачисления;
	ДополнительныеСведения.ПланируемаяДатаВыплаты = ДатаВыплаты(Объект.Организация, Объект.МесяцНачисления, Объект.ПланируемаяДатаВыплаты);
	
	СведенияОДоходахНДФЛ = УчетНДФЛФормыРасширенный.СведенияОДоходахНДФЛДокумента(Объект, "Начисления,НачисленияПерерасчет,Пособия,ПособияПерерасчет", ДополнительныеСведения, ФизическоеЛицо,,, АдресТаблицыРаспределенияПоТерриториямУсловиямТруда);	
	АдресСведенийОбНДФЛ = УчетНДФЛФормыРасширенный.СведенияОбНДФЛ(ЭтаФорма, ФизическоеЛицо, ОписаниеТаблицы.ПутьКДаннымАдресРаспределенияРезультатовВХранилище);
	
	ДанныеОбНДФЛ = ПолучитьИзВременногоХранилища(АдресСведенийОбНДФЛ);
	ДанныеОбНДФЛ.Вставить("СведенияОДоходах", СведенияОДоходахНДФЛ.СведенияОДоходах);
	ДанныеОбНДФЛ.Вставить("ВычетыКДоходам", СведенияОДоходахНДФЛ.ВычетыКДоходам);
	
	МаксимальныйИдентификаторСтрокиНДФЛ = ЭтаФорма[ОписаниеПанелиВычеты().ИмяГруппыФормыПанелиВычеты + "МаксимальныйИдентификаторСтрокиНДФЛ"];
	ДанныеОбНДФЛ.Вставить("МаксимальныйИдентификаторСтрокиНДФЛ", МаксимальныйИдентификаторСтрокиНДФЛ);
	
	Возврат ПоместитьВоВременноеХранилище(ДанныеОбНДФЛ, ЭтаФорма.УникальныйИдентификатор);
	
КонецФункции

&НаСервере
Процедура ОбновитьДанныеНДФЛНаСервере(АдресВременногоХранилища)
	
	Параметр = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
	
	ТаблицыНДФЛ = Новый Массив;
	ТаблицыНДФЛ.Добавить("НДФЛ");
	ТаблицыНДФЛ.Добавить("ПримененныеВычетыНаДетейИИмущественные");
	ФизическиеЛица = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Параметр.ФизическоеЛицо);
	
	НазначитьНовыеИдентификаторыСтрокамНДФЛ(Параметр.НДФЛ, Параметр.ПримененныеВычетыНаДетейИИмущественные, ГруппаКарточкаМаксимальныйИдентификаторСтрокиНДФЛ);
	СодержимоеСтрокНДФЛ = СтрокиТаблицПоФизическимЛицам(Параметр,Ложь,ТаблицыНДФЛ);
	ЗаменитьСтрокиТаблицДокумента(Объект, ФизическиеЛица, СтрокиТаблицДокументаПоФизическимЛицам,СодержимоеСтрокНДФЛ,ТаблицыНДФЛ);
	
	Для Каждого СтрокаНачислений Из Параметр.Начисления Цикл
		СтруктураПоиска = Новый Структура("ФизическоеЛицо,Начисление,ИдентификаторСтрокиВидаРасчета");
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаНачислений);
		СтруктураПоиска.ИдентификаторСтрокиВидаРасчета = СтрокаНачислений.ИдентификаторСтроки;
		НайденныеСтроки = Объект.Начисления.НайтиСтроки(СтруктураПоиска);
		Если НайденныеСтроки.Количество() > 0 Тогда
			НайденныеСтроки[0].КодВычета = СтрокаНачислений.КодВычета;
			НайденныеСтроки[0].СуммаВычета = СтрокаНачислений.СуммаВычета;
		КонецЕсли; 
	КонецЦикла;
	
	СоответствиеНачисленийРеквизитам = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(Параметр.Начисления.ВыгрузитьКолонку("Начисление"),
																					"КатегорияДохода");
	
	ИзмененныеДанные.Загрузить(Параметр.ИзмененныеДанные);
	
	ОписаниеПанелиВычеты = ОписаниеПанелиВычеты();
	НайденныеСтроки = СтрокиТаблицДокументаПоФизическимЛицам[Параметр.ФизическоеЛицо].НДФЛ;
	Для Каждого Идентификатор Из НайденныеСтроки Цикл
		СтрокаНДФЛ = Объект.НДФЛ.НайтиПоИдентификатору(Идентификатор);
		Если СтрокаНДФЛ = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		УчетНДФЛФормы.ЗаполнитьПредставленияВычетовСтрокиНДФЛ(ЭтаФорма, СтрокаНДФЛ, ОписаниеПанелиВычеты, СоответствиеНачисленийРеквизитам);
	КонецЦикла;
	
	Модифицированность = Истина;
	НДФЛПриОкончанииРедактированияНаСервере();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура НазначитьНовыеИдентификаторыСтрокамНДФЛ(НДФЛ, ПримененныеВычетыНаДетейИИмущественные, МаксимальныйИдентификаторСтрокиНДФЛ)
	
	УчетНДФЛФормы.НазначитьИдентификаторыНовымСтрокамТаблицамНДФЛИПримененныеВычетыНаДетейИИмущественные(
		МаксимальныйИдентификаторСтрокиНДФЛ + 1,
		НДФЛ,
		ПримененныеВычетыНаДетейИИмущественные);
		
	МаксимальныйИдентификаторСтрокиНДФЛ = МаксимальныйИдентификаторСтрокиНДФЛ + НДФЛ.Количество();
	
КонецПроцедуры

&НаКлиенте
Процедура НДФЛПриАктивизацииСтроки(Элемент)
	
	РасчетЗарплатыРасширенныйКлиент.ДокументыВыполненияНачисленийПриАктивацииСтроки(ЭтаФорма, "НДФЛ", Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура НДФЛПередНачаломИзменения(Элемент, Отказ)
	
	УчетНДФЛКлиент.НДФЛПередНачаломИзменения(ЭтаФорма, Элементы.НДФЛ.ТекущиеДанные, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура НДФЛПередУдалением(Элемент, Отказ)
	// Механизм закладки примененные вычеты.
	СотрудникиПередУдалением = РасчетЗарплатыРасширенныйКлиент.ВыделенныеСотрудникиКоллекции(Объект.НДФЛ, 
																		Элементы.НДФЛ.ВыделенныеСтроки,"ФизическоеЛицо");
	
	НДФЛВыделенныеСтроки = Новый Массив;
	Для Каждого ИдентификаторСтрок ИЗ Элементы.НДФЛ.ВыделенныеСтроки Цикл
		
		СтрокаТаблицы = Объект.НДФЛ.НайтиПоИдентификатору(ИдентификаторСтрок);
		Если СтрокаТаблицы <> Неопределено Тогда
			РасчетЗарплатыРасширенныйКлиент.ДобавитьИзмененныеДанные(ИзмененныеДанные, "НДФЛ", СтрокаТаблицы.ФизическоеЛицо);
			НДФЛВыделенныеСтроки.Добавить(СтрокаТаблицы.ИдентификаторСтрокиНДФЛ);
		КонецЕсли;
		
	КонецЦикла;
	УчетНДФЛКлиент.НДФЛПередУдалением(ЭтаФорма, НДФЛВыделенныеСтроки, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура НДФЛПослеУдаления(Элемент)
	// Механизм закладки примененные вычеты.
	УчетНДФЛКлиентРасширенный.УдалитьПримененныеВычеты(ЭтаФорма);
	НДФЛПослеУдаленияНаСервере(СотрудникиПередУдалением);
КонецПроцедуры

&НаКлиенте
Процедура НДФЛПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	РасчетЗарплатыРасширенныйКлиент.ДобавитьИзмененныеДанные(ИзмененныеДанные, "НДФЛ", ТекущиеДанные.ФизическоеЛицо);
	
	ОписаниеТаблицы = ОписаниеТаблицыНДФЛ();
	Если НоваяСтрока Тогда
		ДобавитьИдентификаторСтрокиФизическогоЛица(ТекущиеДанные.ФизическоеЛицо, ОписаниеТаблицы.ИмяТаблицы, ТекущиеДанные.ПолучитьИдентификатор());
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НДФЛПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	Если ИспользоватьСтатьиФинансирования Тогда
		ТекущиеДанные = Элемент.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено Тогда
			Если ТекущиеДанные.РезультатРаспределения = Неопределено И ЗначениеЗаполнено(АдресТаблицыРаспределенияУдержаний) Тогда 
				ТекущиеДанные.РезультатРаспределения = РезультатРаспределенияИзВременногоХранилища(ТекущиеДанные.ИдентификаторСтрокиНДФЛ, АдресТаблицыРаспределенияУдержаний, ОписаниеТаблицыНДФЛ());
			КонецЕсли;
			ОтражениеЗарплатыВБухучетеКлиентСерверРасширенный.ПерераспределитьНДФЛ(ТекущиеДанные, РаботаВБюджетномУчреждении);
		КонецЕсли;
	КонецЕсли;
	
	РасчетЗарплатыКлиент.СтрокаРасчетаПриОкончанииРедактирования(ЭтаФорма, ОписаниеТаблицыНДФЛ());
	НДФЛПриОкончанииРедактированияНаСервере();	
КонецПроцедуры

&НаСервереБезКонтекста
Функция РезультатРаспределенияИзВременногоХранилища(ИдентификаторСтроки, АдресТаблицыРаспределения, ОписаниеТаблицы)
	
	ДанныеРаспределения = ПолучитьИзВременногоХранилища(АдресТаблицыРаспределения);
	
	Если ДанныеРаспределения = Неопределено Тогда 
		Возврат Неопределено;
	КонецЕсли;
	
	ИменаКолонокТаблицы = ЗарплатаКадрыРасширенныйКлиентСервер.ИменаКолонокТаблицыРаспределенияРезультатовРасчета(ОписаниеТаблицы);
	
	ОтборСтрок = Новый Структура("ИдентификаторСтроки", ИдентификаторСтроки);
	ТаблицаРаспределенияПредварительно = ДанныеРаспределения.Скопировать(ОтборСтрок);
	
	ОтражениеЗарплатыВБухучетеРасширенный.ДополнитьТаблицуРаспределенияКодомСтатьиФинансирования(ТаблицаРаспределенияПредварительно);
	
	ТаблицаРаспределения = ТаблицаРаспределенияПредварительно.Скопировать(, ИменаКолонокТаблицы);
	СтруктураРаспределения = ЗарплатаКадрыРасширенный.ТаблицаЗначенийВСтруктуру(ТаблицаРаспределения);
	
	Возврат СтруктураРаспределения;
	
КонецФункции

&НаКлиенте
Процедура НДФЛПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	РасчетЗарплатыКлиент.СтрокаРасчетаПриНачалеРедактирования(ЭтаФорма, "НДФЛ", Элементы.НДФЛ.ТекущиеДанные, НоваяСтрока, Копирование);
	// Механизм закладки примененные вычеты.
	УчетНДФЛКлиент.НДФЛПриНачалеРедактирования(ЭтаФорма, Элементы.НДФЛ.ТекущиеДанные, НоваяСтрока, Копирование);
КонецПроцедуры

&НаКлиенте
Процедура ПогашениеЗаймовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ВыполнитьКомандуРедактированияРезультатовРаспределенияНачисленийИУдержаний(ОписаниеТаблицыПогашениеЗаймов(), Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ПогашениеЗаймовПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	СтароеЗначениеФизическогоЛица = Элементы.ПогашениеЗаймов.ТекущиеДанные.ФизическоеЛицо;
	РасчетЗарплатыКлиент.СтрокаРасчетаПриНачалеРедактирования(ЭтаФорма, "ПогашениеЗаймов", Элементы.ПогашениеЗаймов.ТекущиеДанные, НоваяСтрока, Копирование);
КонецПроцедуры

&НаКлиенте
Процедура ПогашениеЗаймовФизическоеЛицоПриИзменении(Элемент)
	ПриИзмененииФизическогоЛица(Элемент, "ПогашениеЗаймов");
КонецПроцедуры

&НаКлиенте
Процедура ПогашенияЗаймовПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ИспользоватьСтатьиФинансирования Тогда
		Если ТекущиеДанные <> Неопределено Тогда
			Если ТекущиеДанные.РезультатРаспределения = Неопределено И ЗначениеЗаполнено(АдресТаблицыРаспределенияУдержаний) Тогда 
				ТекущиеДанные.РезультатРаспределения = РезультатРаспределенияИзВременногоХранилища(ТекущиеДанные.ИдентификаторСтроки, АдресТаблицыРаспределенияУдержаний, ОписаниеТаблицыПогашениеЗаймов());
			КонецЕсли;
			ОтражениеЗарплатыВБухучетеКлиентСерверРасширенный.ПерераспределитьПогашениеЗаймов(ТекущиеДанные, РаботаВБюджетномУчреждении);
		КонецЕсли;
	КонецЕсли;
	
	ОписаниеТаблицы = ОписаниеТаблицыПогашениеЗаймов();
	Если НоваяСтрока Тогда
		ДобавитьИдентификаторСтрокиФизическогоЛица(ТекущиеДанные.ФизическоеЛицо, ОписаниеТаблицы.ИмяТаблицы, ТекущиеДанные.ПолучитьИдентификатор());
	КонецЕсли;
	РасчетЗарплатыКлиент.СтрокаРасчетаПриОкончанииРедактирования(ЭтаФорма, ОписаниеТаблицы, Ложь, Ложь);
	
	ПогашенияЗаймовПриОкончанииРедактированияНаСервере();
	
	УстановитьДоступностьКомандыЗаполнитьССохранениемИсправленийПриОкончанииРедактированияСтроки(Элемент.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ПогашенияЗаймовПослеУдаления(Элемент)
	ПогашенияЗаймовПослеУдаленияНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыВзносы

&НаКлиенте
Процедура ВзносыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	ТекущиеДанные = Элементы.Взносы.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ТекущиеДанные.ДатаПолученияДохода = КонецМесяца(Объект.МесяцНачисления);
		ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДате(ТекущиеДанные, "ДатаПолученияДохода", "ДатаПолученияДоходаСтрокой");
		СтароеЗначениеФизическогоЛица = ТекущиеДанные.ФизическоеЛицо;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВзносыФизическоеЛицоПриИзменении(Элемент)
	ПриИзмененииФизическогоЛица(Элемент, "Взносы");
КонецПроцедуры

&НаКлиенте
Процедура ВзносыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	Если НоваяСтрока Тогда
		ТекущиеДанные = Элемент.ТекущиеДанные;
		ДобавитьИдентификаторСтрокиФизическогоЛица(ТекущиеДанные.ФизическоеЛицо, "Взносы", ТекущиеДанные.ПолучитьИдентификатор());
	КонецЕсли;
	
	ВзносыПриОкончанииРедактированияНаСервере()
КонецПроцедуры

&НаКлиенте
Процедура ВзносыПослеУдаления(Элемент)
	ВзносыПослеУдаленияНаСервере()
КонецПроцедуры

&НаКлиенте
Процедура ВзносыДатаПолученияДоходаПриИзменении(Элемент)
	ДанныеТекущейСтроки = Элементы.Взносы.ТекущиеДанные;
	УчетСтраховыхВзносовКлиент.ВзносыДатаПолученияДоходаСтрокойПриИзменении(ДанныеТекущейСтроки, "ДатаПолученияДохода", "ДатаПолученияДоходаСтрокой", Модифицированность);
КонецПроцедуры

&НаКлиенте
Процедура ВзносыДатаПолученияДоходаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ДанныеТекущейСтроки = Элементы.Взносы.ТекущиеДанные;
	УчетСтраховыхВзносовКлиент.ВзносыДатаПолученияДоходаСтрокойНачалоВыбора(ЭтотОбъект, ДанныеТекущейСтроки, "ДатаПолученияДохода", "ДатаПолученияДоходаСтрокой");
КонецПроцедуры

&НаКлиенте
Процедура ВзносыДатаПолученияДоходаРегулирование(Элемент, Направление, СтандартнаяОбработка)
	УчетСтраховыхВзносовКлиент.ВзносыДатаПолученияДоходаСтрокойРегулирование(
		Элементы.Взносы.ТекущиеДанные, 
		"ДатаПолученияДохода", 
		"ДатаПолученияДоходаСтрокой",
		Направление, 
		Модифицированность);
КонецПроцедуры

&НаКлиенте
Процедура ВзносыДатаПолученияДоходаАвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ВзносыДатаПолученияДоходаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, Параметры, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЛьготы

&НаКлиенте
Процедура ЛьготыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПриНажатииПолеРаспределениеПоТерриториямУсловиямТруда(ОписаниеТаблицыЛьгот(ЭтаФорма), Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Элемент", Элемент);
	ДополнительныеПараметры.Вставить("ВыбраннаяСтрока", ВыбраннаяСтрока);
	ДополнительныеПараметры.Вставить("Поле", Поле);
	
	Оповещение = Новый ОписаниеОповещения("ЛьготыВыборЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	РасчетЗарплатыРасширенныйКлиент.ВыполнитьКомандуРасшифровкиЗначенияПоказателя(ЭтаФорма, ОписаниеТаблицыЛьгот(ЭтаФорма), Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка, Оповещение);
	
	Если СтрНайти(Поле.Имя, "КомандаРедактированияРаспределения") <> 0 Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЛьготыВыборЗавершение(Результат, ДополнительныеПараметры) Экспорт 
	
	Элемент = ДополнительныеПараметры.Элемент;
	ВыбраннаяСтрока = ДополнительныеПараметры.ВыбраннаяСтрока;
	Поле = ДополнительныеПараметры.Поле;
	
	ВыполнитьКомандуРедактированияРезультатовРаспределенияНачисленийИУдержаний(ОписаниеТаблицыЛьгот(ЭтаФорма), Элемент, ВыбраннаяСтрока, Поле);
	
КонецПроцедуры

&НаКлиенте
Процедура ЛьготыПриАктивизацииСтроки(Элемент)
	
	РасчетЗарплатыРасширенныйКлиент.ДокументыВыполненияНачисленийПриАктивацииСтроки(ЭтаФорма, "Льготы", Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЛьготыПередУдалением(Элемент, Отказ)
	
	СотрудникиПередУдалением = РасчетЗарплатыРасширенныйКлиент.ВыделенныеСотрудникиКоллекции(Объект.Льготы, Элементы.Льготы.ВыделенныеСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура ЛьготыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	РасчетЗарплатыКлиент.СтрокаРасчетаПриНачалеРедактирования(ЭтаФорма, "Льготы", Элементы.Льготы.ТекущиеДанные, НоваяСтрока, Копирование);
	
КонецПроцедуры

&НаКлиенте
Процедура ЛьготыПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	РасчетЗарплатыРасширенныйКлиент.ДобавитьИзмененныеДанные(
		ИзмененныеДанные,
		"Начисления",
		,
		ТекущиеДанные.Сотрудник,
		ТекущиеДанные.Начисление);
		
	ОписаниеТаблицы = ОписаниеТаблицыЛьгот(ЭтаФорма);
	Если НоваяСтрока Тогда
		ДобавитьИдентификаторСтрокиФизическогоЛица(ТекущиеДанные.ФизическоеЛицо, ОписаниеТаблицы.ИмяТаблицы, ТекущиеДанные.ПолучитьИдентификатор());
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ЛьготыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	РасчетЗарплатыКлиент.СтрокаРасчетаПриОкончанииРедактирования(ЭтаФорма, ОписаниеТаблицыЛьгот(ЭтаФорма));
	УстановитьДоступностьКомандыЗаполнитьССохранениемИсправленийПриОкончанииРедактированияСтроки(Элемент.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ЛьготыПослеУдаления(Элемент)
	
	ПересчитатьНачисленияПослеУдаленияСтроки(СотрудникиПередУдалением);
	ЗарплатаКадрыРасширенныйКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(
		ЭтаФорма, , 
		КлючевыеРеквизитыОписаниеКлючевыхРеквизитов(ИспользоватьВоеннуюСлужбу), 
		КлючевыеРеквизитыТаблицыОчищаемыеПриИзменении());
	ЗарплатаКадрыРасширенныйКлиентСервер.ПланируемаяДатаВыплатыУстановитьОтображениеПредупреждения(ЭтаФорма,"ПланируемаяДатаВыплаты",
												СтрокиТаблицДокументаПоФизическимЛицам);
	УстановитьДоступностьКомандыЗаполнитьССохранениемИсправленийПослеУдаленияСтроки();
	
КонецПроцедуры

&НаКлиенте
Процедура ЛьготыСотрудникПриИзменении(Элемент)
	
	ОписаниеТаблицы = ОписаниеТаблицыЛьгот(ЭтаФорма);
	ПриИзмененииСотрудника(Элемент, ОписаниеТаблицы.ИмяТаблицы);
	РасчетЗарплатыРасширенныйКлиент.ДополнитьСтрокуРасчета(ЭтаФорма, ОписаниеТаблицы, Истина, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЛьготыНачислениеПриИзменении(Элемент)
	
	ОписаниеТаблицыВидовРасчета = ОписаниеТаблицыЛьгот(ЭтаФорма);
	ЗарплатаКадрыРасширенныйКлиент.ВводНачисленийНачислениеПриИзменении(ЭтаФорма, ОписаниеТаблицыВидовРасчета, 2);
	РасчетЗарплатыРасширенныйКлиент.ДополнитьСтрокуРасчета(ЭтаФорма, ОписаниеТаблицыВидовРасчета, Истина, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЛьготыДатаНачалаПриИзменении(Элемент)
	
	РасчетЗарплатыРасширенныйКлиент.ДополнитьСтрокуРасчета(ЭтаФорма, ОписаниеТаблицыНачислений(ЭтаФорма), Истина, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЛьготыДатаОкончанияПриИзменении(Элемент)
	
	РасчетЗарплатыРасширенныйКлиент.ДополнитьСтрокуРасчета(ЭтаФорма, ОписаниеТаблицыНачислений(ЭтаФорма), Ложь, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЛьготыПерерасчет

&НаКлиенте
Процедура ЛьготыПерерасчетВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПриНажатииПолеРаспределениеПоТерриториямУсловиямТруда(ОписаниеТаблицыЛьготыПерерасчет(ЭтаФорма), Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Элемент", Элемент);
	ДополнительныеПараметры.Вставить("ВыбраннаяСтрока", ВыбраннаяСтрока);
	ДополнительныеПараметры.Вставить("Поле", Поле);
	
	Оповещение = Новый ОписаниеОповещения("ЛьготыПерерасчетВыборЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	РасчетЗарплатыРасширенныйКлиент.ВыполнитьКомандуРасшифровкиЗначенияПоказателя(ЭтаФорма, ОписаниеТаблицыЛьготыПерерасчет(ЭтаФорма), Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка, Оповещение);
	
	Если СтрНайти(Поле.Имя, "КомандаРедактированияРаспределения") <> 0 Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЛьготыПерерасчетВыборЗавершение(Результат, ДополнительныеПараметры) Экспорт 
	
	Элемент = ДополнительныеПараметры.Элемент;
	ВыбраннаяСтрока = ДополнительныеПараметры.ВыбраннаяСтрока;
	Поле = ДополнительныеПараметры.Поле;
	
	ВыполнитьКомандуРедактированияРезультатовРаспределенияНачисленийИУдержаний(ОписаниеТаблицыЛьготыПерерасчет(ЭтаФорма), Элемент, ВыбраннаяСтрока, Поле);
	
КонецПроцедуры

&НаКлиенте
Процедура ЛьготыПерерасчетПриАктивизацииСтроки(Элемент)
	
	РасчетЗарплатыРасширенныйКлиент.ДокументыВыполненияНачисленийПриАктивацииСтроки(ЭтаФорма, "ЛьготыПерерасчет", Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЛьготыПерерасчетПередУдалением(Элемент, Отказ)
	
	СотрудникиПередУдалением = РасчетЗарплатыРасширенныйКлиент.ВыделенныеСотрудникиКоллекции(Объект.ЛьготыПерерасчет, Элементы.ЛьготыПерерасчет.ВыделенныеСтроки);
	УдалитьВыделенныеПерерасчеты(Элементы.ЛьготыПерерасчет.ВыделенныеСтроки, Объект.ЛьготыПерерасчет,,,Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЛьготыПерерасчетПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	РасчетЗарплатыКлиент.СтрокаРасчетаПриНачалеРедактирования(ЭтаФорма, "ЛьготыПерерасчет", Элементы.ЛьготыПерерасчет.ТекущиеДанные, НоваяСтрока, Копирование);
	
КонецПроцедуры

&НаКлиенте
Процедура ЛьготыПерерасчетПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	РасчетЗарплатыРасширенныйКлиент.ДобавитьИзмененныеДанные(
		ИзмененныеДанные,
		"Начисления",
		,
		ТекущиеДанные.Сотрудник,
		ТекущиеДанные.Начисление);
		
	ОписаниеТаблицы = ОписаниеТаблицыЛьготыПерерасчет(ЭтаФорма);
	Если НоваяСтрока Тогда
		ДобавитьИдентификаторСтрокиФизическогоЛица(ТекущиеДанные.ФизическоеЛицо, ОписаниеТаблицы.ИмяТаблицы, ТекущиеДанные.ПолучитьИдентификатор());
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЛьготыПерерасчетПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеТаблицы = ОписаниеТаблицыЛьготыПерерасчет(ЭтаФорма);
	РасчетЗарплатыКлиент.СтрокаРасчетаПриОкончанииРедактирования(ЭтаФорма, ОписаниеТаблицы);
	УстановитьДоступностьКомандыЗаполнитьССохранениемИсправленийПриОкончанииРедактированияСтроки(Элемент.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ЛьготыПерерасчетПослеУдаления(Элемент)
	
	ПересчитатьНачисленияПослеУдаленияСтроки(СотрудникиПередУдалением);
	ЗарплатаКадрыРасширенныйКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(
		ЭтаФорма, , 
		КлючевыеРеквизитыОписаниеКлючевыхРеквизитов(ИспользоватьВоеннуюСлужбу), 
		КлючевыеРеквизитыТаблицыОчищаемыеПриИзменении());
	ЗарплатаКадрыРасширенныйКлиентСервер.ПланируемаяДатаВыплатыУстановитьОтображениеПредупреждения(ЭтаФорма,"ПланируемаяДатаВыплаты",
												СтрокиТаблицДокументаПоФизическимЛицам);
	УстановитьДоступностьКомандыЗаполнитьССохранениемИсправленийПослеУдаленияСтроки();
	
КонецПроцедуры

&НаКлиенте
Процедура ЛьготыПерерасчетСотрудникПриИзменении(Элемент)
	
	ОписаниеТаблицы = ОписаниеТаблицыЛьготыПерерасчет(ЭтаФорма);
	ПриИзмененииСотрудника(Элемент, ОписаниеТаблицы.ИмяТаблицы);
	РасчетЗарплатыРасширенныйКлиент.ДополнитьСтрокуРасчета(ЭтаФорма, ОписаниеТаблицы, Истина, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЛьготыПерерасчетНачислениеПриИзменении(Элемент)
	
	ОписаниеТаблицыВидовРасчета = ОписаниеТаблицыЛьготыПерерасчет(ЭтаФорма);
	ЗарплатаКадрыРасширенныйКлиент.ВводНачисленийНачислениеПриИзменении(ЭтаФорма, ОписаниеТаблицыВидовРасчета, 2);
	РасчетЗарплатыРасширенныйКлиент.ДополнитьСтрокуРасчета(ЭтаФорма, ОписаниеТаблицыВидовРасчета, Истина, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЛьготыПерерасчетДатаНачалаПриИзменении(Элемент)
	
	РасчетЗарплатыРасширенныйКлиент.ДополнитьСтрокуРасчета(ЭтаФорма, ОписаниеТаблицыЛьготыПерерасчет(ЭтаФорма), Истина, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЛьготыПерерасчетДатаОкончанияПриИзменении(Элемент)
	
	РасчетЗарплатыРасширенныйКлиент.ДополнитьСтрокуРасчета(ЭтаФорма, ОписаниеТаблицыЛьготыПерерасчет(ЭтаФорма), Ложь, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыКорректировкиВыплаты

&НаКлиенте
Процедура КорректировкиВыплатыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ВыполнитьКомандуРедактированияРезультатовРаспределенияНачисленийИУдержаний(ОписаниеТаблицыКорректировкиВыплаты(), Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура КорректировкиВыплатыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	СтароеЗначениеФизическогоЛица = Элементы.КорректировкиВыплаты.ТекущиеДанные.ФизическоеЛицо;
КонецПроцедуры

&НаКлиенте
Процедура КорректировкиВыплатыФизическоеЛицоПриИзменении(Элемент)
	ПриИзмененииФизическогоЛица(Элемент, "КорректировкиВыплаты");
КонецПроцедуры

&НаКлиенте
Процедура КорректировкиВыплатыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеТаблицы = ОписаниеТаблицыКорректировкиВыплаты();
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ИспользоватьСтатьиФинансирования Тогда
		Если ТекущиеДанные <> Неопределено Тогда
			Если ТекущиеДанные.РезультатРаспределения = Неопределено И ЗначениеЗаполнено(АдресТаблицыРаспределенияУдержаний) Тогда 
				ТекущиеДанные.РезультатРаспределения = РезультатРаспределенияИзВременногоХранилища(ТекущиеДанные.ИдентификаторСтроки, АдресТаблицыРаспределенияУдержаний, ОписаниеТаблицы);
			КонецЕсли;
			ОтражениеЗарплатыВБухучетеКлиентСерверРасширенный.ПерераспределитьКорректировкиВыплаты(ТекущиеДанные, РаботаВБюджетномУчреждении);
		КонецЕсли;
	КонецЕсли;
	
	Если НоваяСтрока Тогда
		ДобавитьИдентификаторСтрокиФизическогоЛица(ТекущиеДанные.ФизическоеЛицо, ОписаниеТаблицы.ИмяТаблицы, ТекущиеДанные.ПолучитьИдентификатор());
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУправленческиеНачисления

&НаКлиенте
Процедура УправленческиеНачисленияПриАктивизацииСтроки(Элемент)
	РасчетЗарплатыРасширенныйКлиент.ДокументыВыполненияНачисленийПриАктивацииСтроки(ЭтаФорма, "УправленческиеНачисления", Истина);
КонецПроцедуры

&НаКлиенте
Процедура УправленческиеНачисленияПередУдалением(Элемент, Отказ)
	
	СотрудникиПередУдалением = РасчетЗарплатыРасширенныйКлиент.ВыделенныеСотрудникиКоллекции(Объект.УправленческиеНачисления, Элементы.УправленческиеНачисления.ВыделенныеСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура УправленческиеНачисленияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	РасчетЗарплатыКлиент.СтрокаРасчетаПриНачалеРедактирования(ЭтаФорма, "УправленческиеНачисления", Элементы.УправленческиеНачисления.ТекущиеДанные, НоваяСтрока, Копирование);
КонецПроцедуры

&НаКлиенте
Процедура УправленческиеНачисленияПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.УправленческаяЗарплата") Тогда
		
		ТекущиеДанные = Элемент.ТекущиеДанные;
		РасчетЗарплатыРасширенныйКлиент.ДобавитьИзмененныеДанные(
			ИзмененныеДанные,
			"Начисления",
			,
			ТекущиеДанные.Сотрудник,
			ТекущиеДанные.Начисление);
			
		Если НоваяСтрока Тогда
			ДобавитьИдентификаторСтрокиФизическогоЛица(ТекущиеДанные.ФизическоеЛицо, "УправленческиеНачисления", ТекущиеДанные.ПолучитьИдентификатор());
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УправленческиеНачисленияПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.УправленческаяЗарплата") Тогда
		Модуль = ОбщегоНазначенияКлиент.ОбщийМодуль("УправленческаяЗарплатаКлиент");
		Модуль.ПриОкончанииРедактированияСтрокиНачисленияЗарплаты(ЭтаФорма);
	КонецЕсли;
	
	УстановитьДоступностьКомандЗаполненияДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура УправленческиеНачисленияПослеУдаления(Элемент) 
	
	ПересчитатьНачисленияПослеУдаленияСтроки(СотрудникиПередУдалением);
	
КонецПроцедуры

&НаКлиенте
Процедура УправленческиеНачисленияСотрудникПриИзменении(Элемент)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.УправленческаяЗарплата") Тогда
		ПриИзмененииСотрудника(Элемент, "УправленческиеНачисления");
		Модуль = ОбщегоНазначенияКлиент.ОбщийМодуль("УправленческаяЗарплатаКлиент");
		Модуль.ПриИзмененииСотрудникаСтрокиНачисленияЗарплаты(Элемент, ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УправленческиеНачисленияНачислениеПриИзменении(Элемент)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.УправленческаяЗарплата") Тогда
		Модуль = ОбщегоНазначенияКлиент.ОбщийМодуль("УправленческаяЗарплатаКлиент");
		Модуль.ПриИзмененииНачисленияСтрокиНачисленияЗарплаты(ЭтаФорма, ОписаниеДокумента(ЭтаФорма));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УправленческиеНачисленияДатаНачалаПриИзменении(Элемент)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.УправленческаяЗарплата") Тогда
		Модуль = ОбщегоНазначенияКлиент.ОбщийМодуль("УправленческаяЗарплатаКлиент");
		Модуль.ПриИзмененииДатыНачалаСтрокиНачисленияЗарплаты(ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УправленческиеНачисленияДатаОкончанияПриИзменении(Элемент)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.УправленческаяЗарплата") Тогда
		Модуль = ОбщегоНазначенияКлиент.ОбщийМодуль("УправленческаяЗарплатаКлиент");
		Модуль.ПриИзмененииДатыОкончанияСтрокиНачисленияЗарплаты(ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УправленческиеНачисленияПериодДействияПриИзменении(Элемент)
	ЗарплатаКадрыКлиент.ВводМесяцаПриИзменении(Элементы.УправленческиеНачисления.ТекущиеДанные, "ПериодДействия", "ПериодДействияСтрокой", Модифицированность);
КонецПроцедуры

&НаКлиенте
Процедура УправленческиеНачисленияПериодДействияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаНачалоВыбора(ЭтаФорма, Элементы.УправленческиеНачисления.ТекущиеДанные, "ПериодДействия", "ПериодДействияСтрокой");
КонецПроцедуры

&НаКлиенте
Процедура УправленческиеНачисленияПериодДействияРегулирование(Элемент, Направление, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаРегулирование(Элементы.УправленческиеНачисления.ТекущиеДанные, "ПериодДействия", "ПериодДействияСтрокой", Направление, Модифицированность);
КонецПроцедуры

&НаКлиенте
Процедура УправленческиеНачисленияПериодДействияАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура УправленческиеНачисленияПериодДействияОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаОкончаниеВводаТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура УправленческиеНачисленияПодробно(Команда)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.УправленческаяЗарплата") Тогда
		Модуль = ОбщегоНазначенияКлиент.ОбщийМодуль("УправленческаяЗарплатаКлиент");
		Модуль.ПриНажатииКнопкиПоказатьПодробностиРасчета(ЭтаФорма, Элементы.УправленческиеНачисленияПодробно);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УправленческиеНачисленияОтменитьИсправление(Команда)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.УправленческаяЗарплата") Тогда
		Модуль = ОбщегоНазначенияКлиент.ОбщийМодуль("УправленческаяЗарплатаКлиент");
		Модуль.ПриОтменеИсправленияНачисленияЗарплаты(ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УправленческиеНачисленияОтменитьВсеИсправления(Команда)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.УправленческаяЗарплата") Тогда
		Модуль = ОбщегоНазначенияКлиент.ОбщийМодуль("УправленческаяЗарплатаКлиент");
		Модуль.ПриОтменеВсехИсправленийНачисленияЗарплаты(ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьКомандЗаполненияДокумента(ЕстьИсправленныеСтроки = Неопределено)
	
	Если ЕстьИсправленныеСтроки = Неопределено Тогда 
		ЕстьИсправленныеСтроки = ЕстьИсправленныеСтрокиНаСервере();
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, 
		"ЗаполнитьССохранениемИсправлений", "Доступность", ЕстьИсправленныеСтроки);
		
	ИменаТаблиц = Новый Массив;
	ИменаТаблиц.Добавить("Начисления");
	ИменаТаблиц.Добавить("НачисленияПерерасчет");
	ИменаТаблиц.Добавить("НачисленияПоДоговорам");
	ИменаТаблиц.Добавить("Пособия");
	ИменаТаблиц.Добавить("ПособияПерерасчет");
	ИменаТаблиц.Добавить("Льготы");
	ИменаТаблиц.Добавить("Взносы");
	ИменаТаблиц.Добавить("НДФЛ");
	ИменаТаблиц.Добавить("ПогашениеЗаймов");
	ИменаТаблиц.Добавить("Удержания");
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.УправленческаяЗарплата") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("УправленческаяЗарплата");
		Модуль.ПриЗаполненииСпискаТаблицНачисленияЗарплаты(ИменаТаблиц);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПустойДокумент = Истина;
	Для Каждого ИмяТаблицы Из ИменаТаблиц Цикл 
		Если Объект[ИмяТаблицы].Количество() > 0 Тогда 
			ПустойДокумент = Ложь;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Дозаполнить", "Доступность", Не ПустойДокумент);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#Область ОбработчикиСобытийПроцессыОбработкиДокументов

&НаКлиенте
Процедура Подключаемый_ВыполнитьЗадачуПоОбработкеДокумента(Команда)
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначенияКлиент.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплатаКлиент");
		МодульПроцессыОбработкиДокументовЗарплата.ВыполнитьЗадачу(ЭтотОбъект, Команда, Объект)
	КонецЕсли;		
	// Конец ПроцессыОбработкиДокументов
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьЗадачуПоОбработкеДокументаОповещение(Контекст, ДополнительныеПараметры) Экспорт
	ВыполнитьЗадачуПоОбработкеДокументаНаСервере(Контекст);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры, Контекст);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьЗадачуПоОбработкеДокументаНаСервере(Контекст)
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда	
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначения.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплата");
		МодульПроцессыОбработкиДокументовЗарплата.ВыполнитьЗадачу(ЭтотОбъект, Контекст, Объект);
	КонецЕсли;		
	// Конец ПроцессыОбработкиДокументов
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КомментарийНаправившегоОткрытие(Элемент, СтандартнаяОбработка)
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда	
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначенияКлиент.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплатаКлиент");
		МодульПроцессыОбработкиДокументовЗарплата.КомментарийНаправившегоОткрытие(ЭтотОбъект, Элемент, СтандартнаяОбработка);
	КонецЕсли;		
	// Конец ПроцессыОбработкиДокументов
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КомментарийСледующемуНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда	
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначенияКлиент.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплатаКлиент");
		МодульПроцессыОбработкиДокументовЗарплата.КомментарийСледующемуНачалоВыбора(ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка);
	КонецЕсли;		
	// Конец ПроцессыОбработкиДокументов
КонецПроцедуры

#КонецОбласти

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Заполнить(Команда) Экспорт
	
	ОчиститьСообщения();
	
	Если Не РасчетЗарплатыРасширенныйКлиентСервер.ФормаДокументаГотоваДляРасчетаЗарплаты(ЭтаФорма, ОписаниеДокумента(ЭтотОбъект)) Тогда
		Возврат;
	КонецЕсли;
	
	ПечатныеФормыСотрудников = Неопределено;
	
	РасчетЗарплатыРасширенныйКлиент.ЗаполнитьДокументНачисленияЗарплаты(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьССохранениемИсправлений(Команда)
	
	ОчиститьСообщения();
	
	Если Не РасчетЗарплатыРасширенныйКлиентСервер.ФормаДокументаГотоваДляРасчетаЗарплаты(ЭтаФорма, ОписаниеДокумента(ЭтотОбъект)) Тогда
		Возврат;
	КонецЕсли;
	
	ПечатныеФормыСотрудников = Неопределено;
	
	ПерезаполнитьДанныеФормыНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура Дозаполнить(Команда)
	
	ОчиститьСообщения();
	
	Если Не РасчетЗарплатыРасширенныйКлиентСервер.ФормаДокументаГотоваДляРасчетаЗарплаты(ЭтаФорма, ОписаниеДокумента(ЭтотОбъект)) Тогда
		Возврат;
	КонецЕсли;
	
	ПечатныеФормыСотрудников = Неопределено;
	
	МассивСотрудников = ОтсутствующиеВДокументеСотрудники();
	
	Если МассивСотрудников.Количество() = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	Результат = ПерезаполнитьДанныеФормыНаСервере(МассивСотрудников, Истина);
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.ЗаданиеВыполнено Тогда
		РасчетЗарплатыРасширенныйКлиент.ОчиститьСписокСотрудниковКРасчету(ЭтаФорма, МассивСотрудников);
 		ОповеститьОбИзмененииОбъекта();
		УстановитьЗначенияКонтролируемыхПолей();
	Иначе
		ИдентификаторЗадания = Результат.ИдентификаторЗадания;
		АдресХранилища		 = Результат.АдресХранилища;
		
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", 1, Истина);
		ФормаДлительнойОперации = ДлительныеОперацииКлиент.ОткрытьФормуДлительнойОперации(ЭтаФорма, ИдентификаторЗадания);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьНДФЛ(Команда)
	
	ОчиститьСообщения();
	
	Если Не РасчетЗарплатыРасширенныйКлиентСервер.ФормаДокументаГотоваДляРасчетаЗарплаты(ЭтаФорма, ОписаниеДокумента(ЭтотОбъект)) Тогда
		Возврат;
	КонецЕсли;
	
	РежимПересчетаНДФЛ = Истина;
	ПерезаполнитьДанныеФормыНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьВзносы(Команда)
	
	ОчиститьСообщения();
	
	Если Не РасчетЗарплатыРасширенныйКлиентСервер.ФормаДокументаГотоваДляРасчетаЗарплаты(ЭтаФорма, ОписаниеДокумента(ЭтотОбъект)) Тогда
		Возврат;
	КонецЕсли;
	
	РежимПересчетаВзносов = Истина;
	ПерезаполнитьДанныеФормыНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборСотрудников(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("ПодборСотрудникаВФормеДокументаНачислениеЗарплаты");
	
	Если Объект.РежимДоначисления Тогда
		ВладелецОтбора = Элементы.НачисленияПерерасчет;
	Иначе
		ВладелецОтбора = ЭтотОбъект;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	Отборы = Новый Структура;
	Отборы.Вставить("ПоказыватьПодработки", Истина);
	Отборы.Вставить("ПоказыватьДоговорниковГПХ", Истина);
	
	Если ИспользоватьВоеннуюСлужбу Тогда
		Отборы.Вставить("НачислениеЗарплатыВоеннослужащим", Объект.НачислениеЗарплатыВоеннослужащим);
	КонецЕсли;
	
	ПараметрыОткрытия.Вставить("Отбор", Отборы);
	КадровыйУчетРасширенныйКлиент.ДобавитьПараметрыОтбораПоФункциональнойОпцииВыполнятьРасчетЗарплатыПоПодразделениям(
		ЭтаФорма, ПараметрыОткрытия);
		
	КадровыйУчетКлиент.ВыбратьСотрудниковРаботающихВПериодеПоПараметрамОткрытияФормыСписка(
		ВладелецОтбора,
		Объект.Организация,
		Объект.Подразделение,
		Объект.МесяцНачисления, 
		КонецМесяца(Объект.МесяцНачисления),
		,
		АдресСпискаПодобранныхСотрудников(),
		ПараметрыОткрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура Очистить(Команда)
	
	Если ЕстьИсправленныеСтроки() Тогда
		
		Оповещение = Новый ОписаниеОповещения("ОчиститьЗавершение", ЭтотОбъект);
		
		ТекстВопроса = НСтр("ru = 'Документ содержит ручные исправления (выделены шрифтом). 
	                         |Очистить данные?';
	                         |en = 'Document contains manual changes (in bold).
	                         |Clear data?'");
							 
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
		
	Иначе 
		
		ОчиститьЗавершение(КодВозвратаДиалога.Да, Неопределено);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьЗавершение(Ответ, ДополнительныеПараметры) Экспорт 
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда 
		Возврат;
	КонецЕсли;
	
	ПечатныеФормыСотрудников = Неопределено;
	ОчиститьТаблицыДокументаНаСервере();
	
	ЗарплатаКадрыРасширенныйКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтаФорма);
	ЗарплатаКадрыРасширенныйКлиентСервер.ПланируемаяДатаВыплатыУстановитьОтображениеПредупреждения(ЭтаФорма,"ПланируемаяДатаВыплаты");
	РассчитатьИтогиПоДокументу(ЭтаФорма);
	УстановитьОтображениеСообщенияОНеобходимостиПерерасчета();
	УстановитьДоступностьКомандыЗаполнитьССохранениемИсправлений();
	УстановитьДоступностьКомандыДозаполнить();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьДанныеФормы(Команда)
	
	ЗакрыватьПослеЗаписи = Ложь;
	
	Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПровести(Команда)
	
	ЗакрыватьПослеЗаписи = Ложь;
	
	ПровестиДокумент();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПровестиИЗакрыть(Команда)
	
	ЗакрыватьПослеЗаписи = Истина;
	
	ПровестиДокумент();
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПодробно(Команда)
	РасчетЗарплатыРасширенныйКлиентСервер.ДокументыВыполненияНачисленийУстановитьРежимОтображенияПодробно(ЭтаФорма, Не Элементы.НачисленияПодробно.Пометка, ОписаниеТаблицыНачислений(ЭтаФорма));
	РасчетЗарплатыРасширенныйВызовСервера.СохранитьНастройкуРежимаОтображенияПодробно(Объект.Ссылка, Элементы.НачисленияПодробно.Пометка, ОписаниеТаблицыНачислений(ЭтаФорма));
КонецПроцедуры

&НаКлиенте
Процедура ПособияПодробно(Команда)
	РасчетЗарплатыРасширенныйКлиентСервер.ДокументыВыполненияНачисленийУстановитьРежимОтображенияПодробно(ЭтаФорма, Не Элементы.ПособияПодробно.Пометка, ОписаниеТаблицыПособия(ЭтаФорма));
	РасчетЗарплатыРасширенныйВызовСервера.СохранитьНастройкуРежимаОтображенияПодробно(Объект.Ссылка, Элементы.ПособияПодробно.Пометка, ОписаниеТаблицыПособия(ЭтаФорма));
КонецПроцедуры

&НаКлиенте
Процедура ПособияПерерасчетПодробно(Команда)
	РасчетЗарплатыРасширенныйКлиентСервер.ДокументыВыполненияНачисленийУстановитьРежимОтображенияПодробно(ЭтаФорма, Не Элементы.ПособияПерерасчетПодробно.Пометка, ОписаниеТаблицыПособияПерерасчет(ЭтаФорма));
	РасчетЗарплатыРасширенныйВызовСервера.СохранитьНастройкуРежимаОтображенияПодробно(Объект.Ссылка, Элементы.ПособияПерерасчетПодробно.Пометка, ОписаниеТаблицыПособияПерерасчет(ЭтаФорма));
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПерерасчетПодробно(Команда)
	РасчетЗарплатыРасширенныйКлиентСервер.ДокументыВыполненияНачисленийУстановитьРежимОтображенияПодробно(ЭтаФорма, Не Элементы.НачисленияПерерасчетПодробно.Пометка, ОписаниеТаблицыПерерасчетов(ЭтаФорма));
	РасчетЗарплатыРасширенныйВызовСервера.СохранитьНастройкуРежимаОтображенияПодробно(Объект.Ссылка, Элементы.НачисленияПерерасчетПодробно.Пометка, ОписаниеТаблицыПерерасчетов(ЭтаФорма));
КонецПроцедуры

&НаКлиенте
Процедура УдержанияПодробно(Команда)
	РасчетЗарплатыРасширенныйКлиентСервер.ДокументыВыполненияНачисленийУстановитьРежимОтображенияПодробно(ЭтаФорма, Не Элементы.УдержанияПодробно.Пометка, ОписаниеТаблицыУдержаний());
	РасчетЗарплатыРасширенныйВызовСервера.СохранитьНастройкуРежимаОтображенияПодробно(Объект.Ссылка, Элементы.УдержанияПодробно.Пометка, ОписаниеТаблицыУдержаний());
КонецПроцедуры

&НаКлиенте
Процедура УдержанияПерерасчетПодробно(Команда)
	РасчетЗарплатыРасширенныйКлиентСервер.ДокументыВыполненияНачисленийУстановитьРежимОтображенияПодробно(ЭтаФорма, Не Элементы.УдержанияПерерасчетПодробно.Пометка, ОписаниеТаблицыУдержанияПерерасчет());
	РасчетЗарплатыРасширенныйВызовСервера.СохранитьНастройкуРежимаОтображенияПодробно(Объект.Ссылка, Элементы.УдержанияПерерасчетПодробно.Пометка, ОписаниеТаблицыУдержанияПерерасчет());
КонецПроцедуры

&НаКлиенте
Процедура ЛьготыПодробно(Команда)
	
	РасчетЗарплатыРасширенныйКлиентСервер.ДокументыВыполненияНачисленийУстановитьРежимОтображенияПодробно(ЭтаФорма, Не Элементы.ЛьготыПодробно.Пометка, ОписаниеТаблицыЛьгот(ЭтаФорма));
	РасчетЗарплатыРасширенныйВызовСервера.СохранитьНастройкуРежимаОтображенияПодробно(Объект.Ссылка, Элементы.ЛьготыПодробно.Пометка, ОписаниеТаблицыЛьгот(ЭтаФорма));
	
КонецПроцедуры

&НаКлиенте
Процедура ЛьготыПерерасчетПодробно(Команда)
	
	РасчетЗарплатыРасширенныйКлиентСервер.ДокументыВыполненияНачисленийУстановитьРежимОтображенияПодробно(ЭтаФорма, Не Элементы.ЛьготыПерерасчетПодробно.Пометка, ОписаниеТаблицыЛьготыПерерасчет(ЭтаФорма));
	РасчетЗарплатыРасширенныйВызовСервера.СохранитьНастройкуРежимаОтображенияПодробно(Объект.Ссылка, Элементы.ЛьготыПерерасчетПодробно.Пометка, ОписаниеТаблицыЛьготыПерерасчет(ЭтаФорма));
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НачисленияОтменитьИсправление(Команда)
	РасчетЗарплатыКлиент.ОтменитьИсправление(ЭтаФорма, ОписаниеТаблицыНачислений(ЭтаФорма));	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НачисленияОтменитьВсеИсправления(Команда)
	РасчетЗарплатыКлиент.ОтменитьВсеИсправления(ЭтаФорма, ОписаниеТаблицыНачислений(ЭтаФорма));
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НачисленияПоДоговорамОтменитьИсправление(Команда)
	РасчетЗарплатыКлиент.ОтменитьИсправление(ЭтаФорма, ОписаниеТаблицыНачисленийПоДоговорам(ЭтаФорма));
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НачисленияПоДоговорамОтменитьВсеИсправления(Команда)
	РасчетЗарплатыКлиент.ОтменитьВсеИсправления(ЭтаФорма, ОписаниеТаблицыНачисленийПоДоговорам(ЭтаФорма));
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПособияОтменитьИсправление(Команда)
	РасчетЗарплатыКлиент.ОтменитьИсправление(ЭтаФорма, ОписаниеТаблицыПособия(ЭтаФорма));
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПособияПерерасчетОтменитьИсправление(Команда)
	РасчетЗарплатыКлиент.ОтменитьИсправление(ЭтаФорма, ОписаниеТаблицыПособияПерерасчет(ЭтаФорма));
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПособияОтменитьВсеИсправления(Команда)
	РасчетЗарплатыКлиент.ОтменитьВсеИсправления(ЭтаФорма, ОписаниеТаблицыПособия(ЭтаФорма));
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПособияПерерасчетОтменитьВсеИсправления(Команда)
	РасчетЗарплатыКлиент.ОтменитьВсеИсправления(ЭтаФорма, ОписаниеТаблицыПособияПерерасчет(ЭтаФорма));
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УдержанияОтменитьИсправление(Команда)
	РасчетЗарплатыКлиент.ОтменитьИсправление(ЭтаФорма, ОписаниеТаблицыУдержаний());
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УдержанияОтменитьВсеИсправления(Команда)
	РасчетЗарплатыКлиент.ОтменитьВсеИсправления(ЭтаФорма, ОписаниеТаблицыУдержаний());
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УдержанияПерерасчетОтменитьИсправление(Команда)
	РасчетЗарплатыКлиент.ОтменитьИсправление(ЭтаФорма, ОписаниеТаблицыУдержанияПерерасчет());
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УдержанияПерерасчетОтменитьВсеИсправления(Команда)
	РасчетЗарплатыКлиент.ОтменитьВсеИсправления(ЭтаФорма, ОписаниеТаблицыУдержанияПерерасчет());
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НДФЛОтменитьИсправление(Команда)
	РасчетЗарплатыКлиент.ОтменитьИсправление(ЭтаФорма, ОписаниеТаблицыНДФЛ());
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НДФЛОтменитьВсеИсправления(Команда)
	РасчетЗарплатыКлиент.ОтменитьВсеИсправления(ЭтаФорма, ОписаниеТаблицыНДФЛ());
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПогашениеЗаймовОтменитьИсправление(Команда)
	РасчетЗарплатыКлиент.ОтменитьИсправление(ЭтаФорма, ОписаниеТаблицыПогашениеЗаймов());
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПогашениеЗаймовОтменитьВсеИсправления(Команда)
	РасчетЗарплатыКлиент.ОтменитьВсеИсправления(ЭтаФорма, ОписаниеТаблицыПогашениеЗаймов());
КонецПроцедуры

&НаКлиенте
Процедура КарточкаУчетаПоСтраховымВзносам(Команда)
	
	ВзносыВыделенныеСтроки = Элементы.Взносы.ВыделенныеСтроки;
	Если ВзносыВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если СотрудникиКРасчету.Количество() > 0 Тогда
		ПересчитатьДокументНаКлиенте();
	КонецЕсли; 
	
	КарточкаУчетаПоСтраховымВзносам = КарточкаУчетаПоСтраховымВзносамНаСервере(ВзносыВыделенныеСтроки);
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
		"Отчет.КарточкаУчетаПоСтраховымВзносам",
		"КарточкаУчетаПоСтраховымВзносамПодробнее", 
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.Ссылка),
		ЭтотОбъект,
		Новый Структура("ПечатаемыйДокумент", КарточкаУчетаПоСтраховымВзносам));
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьДокумент(Команда)
	ПересчитатьДокументНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура РасчетныйЛисток(Команда) Экспорт
	
	ПодключитьОбработчикОжидания("РасчетныйЛистокНаКлиенте", 0.1, Истина);
	
КонецПроцедуры
	
&НаКлиенте
Процедура РасчетныйЛистокНаКлиенте() Экспорт

	ОценкаПроизводительностиКлиент.ЗамерВремени("ФормированиеРасчетногоЛисткаИзФормыДокументаНачислениеЗарплаты");
	
	НачисленияВыделенныеСтроки = Элементы.Начисления.ВыделенныеСтроки;
	Если НачисленияВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ВывестиРасчетныйЛисток(НачисленияВыделенныеСтроки, "Начисления");
	
КонецПроцедуры

&НаКлиенте
Процедура РасчетныйЛистокДоначисленияПерерасчеты(Команда)
	
	ПодключитьОбработчикОжидания("РасчетныйЛистокДоначисленияПерерасчетыНаКлиенте", 0.1, Истина);
	
КонецПроцедуры
	
&НаКлиенте
Процедура РасчетныйЛистокДоначисленияПерерасчетыНаКлиенте() Экспорт
	
	ВыделенныеСтроки = Элементы.НачисленияПерерасчет.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ВывестиРасчетныйЛисток(ВыделенныеСтроки, "НачисленияПерерасчет");
	
КонецПроцедуры

&НаКлиенте
Процедура РасчетныйЛистокУдержания(Команда)
	
	ПодключитьОбработчикОжидания("РасчетныйЛистокУдержанияНаКлиенте", 0.1, Истина);
	
КонецПроцедуры
	
&НаКлиенте
Процедура РасчетныйЛистокУдержанияНаКлиенте() Экспорт
	
	УдержанияВыделенныеСтроки = Элементы.Удержания.ВыделенныеСтроки;
	Если УдержанияВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ВывестиРасчетныйЛисток(УдержанияВыделенныеСтроки, "Удержания");
	
КонецПроцедуры

&НаКлиенте
Процедура РасчетныйЛистокУдержанияПерерасчет(Команда)
	
	ПодключитьОбработчикОжидания("РасчетныйЛистокУдержанияПерерасчетНаКлиенте", 0.1, Истина);
	
КонецПроцедуры
	
&НаКлиенте
Процедура РасчетныйЛистокУдержанияПерерасчетНаКлиенте() Экспорт
	
	УдержанияВыделенныеСтроки = Элементы.УдержанияПерерасчет.ВыделенныеСтроки;
	Если УдержанияВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ВывестиРасчетныйЛисток(УдержанияВыделенныеСтроки, "УдержанияПерерасчет");
	
КонецПроцедуры

&НаКлиенте
Процедура РасчетныйЛистокПособия(Команда)
	
	ПодключитьОбработчикОжидания("РасчетныйЛистокПособияНаКлиенте", 0.1, Истина);
	
КонецПроцедуры
	
&НаКлиенте
Процедура РасчетныйЛистокПособияНаКлиенте() Экспорт
	
	ВыделенныеСтроки = Элементы.Пособия.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ВывестиРасчетныйЛисток(ВыделенныеСтроки, "Пособия");
	
КонецПроцедуры

&НаКлиенте
Процедура РасчетныйЛистокДоговорникиГПХ(Команда)
	
	ПодключитьОбработчикОжидания("РасчетныйЛистокДоговорникиГПХНаКлиенте", 0.1, Истина);
	
КонецПроцедуры
	
&НаКлиенте
Процедура РасчетныйЛистокДоговорникиГПХНаКлиенте() Экспорт
	
	ВыделенныеСтроки = Элементы.НачисленияПоДоговорам.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ВывестиРасчетныйЛисток(ВыделенныеСтроки, "НачисленияПоДоговорам");
	
КонецПроцедуры

&НаКлиенте
Процедура ДоначислитьСейчас(Команда)
	
	Отбор = Новый Структура;
	Отбор.Вставить("НачислениеЗарплатыВоеннослужащим", Объект.НачислениеЗарплатыВоеннослужащим);
	РасчетЗарплатыРасширенныйКлиент.ДоначислитьЗарплатуСейчас(Объект.Организация, Объект.МесяцНачисления, Отбор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьСотрудников(Команда)
	
	ПересчитатьСотрудниковНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура УточнитьМатВыгоду(Команда)
	
	ОткрытьФорму("Обработка.УточнениеМатВыгодыПоЗаймам.Форма.Форма");
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НачисленияПересчитатьСотрудника(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("ПерерасчетСотрудникаВФормеДокументаНачислениеЗарплаты");
	
	РасчетЗарплатыРасширенныйКлиент.ПересчитатьСотрудника(ЭтаФорма, "Начисления", "Сотрудник", Тип("СправочникСсылка.Сотрудники"));
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НачисленияПерерасчетПересчитатьСотрудника(Команда)
	РасчетЗарплатыРасширенныйКлиент.ПересчитатьСотрудника(ЭтаФорма, "НачисленияПерерасчет", "Сотрудник", Тип("СправочникСсылка.Сотрудники"));
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НачисленияПерерасчетОтменитьИсправление(Команда)
	РасчетЗарплатыКлиент.ОтменитьИсправление(ЭтаФорма, ОписаниеТаблицыПерерасчетов(ЭтаФорма));	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НачисленияПоДоговорамПересчитатьСотрудника(Команда)
	РасчетЗарплатыРасширенныйКлиент.ПересчитатьСотрудника(ЭтаФорма, "НачисленияПоДоговорам", "Сотрудник", Тип("СправочникСсылка.Сотрудники"));
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПособияПересчитатьСотрудника(Команда)
	РасчетЗарплатыРасширенныйКлиент.ПересчитатьСотрудника(ЭтаФорма, "Пособия", "Сотрудник", Тип("СправочникСсылка.Сотрудники"));
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПособияПерерасчетПересчитатьСотрудника(Команда)
	РасчетЗарплатыРасширенныйКлиент.ПересчитатьСотрудника(ЭтаФорма, "ПособияПерерасчет", "Сотрудник", Тип("СправочникСсылка.Сотрудники"));
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УдержанияПересчитатьСотрудника(Команда)
	РасчетЗарплатыРасширенныйКлиент.ПересчитатьСотрудника(ЭтаФорма, "Удержания", "ФизическоеЛицо", Тип("СправочникСсылка.ФизическиеЛица"));
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УдержанияПерерасчетПересчитатьСотрудника(Команда)
	РасчетЗарплатыРасширенныйКлиент.ПересчитатьСотрудника(ЭтаФорма, "УдержанияПерерасчет", "ФизическоеЛицо", Тип("СправочникСсылка.ФизическиеЛица"));
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НДФЛПересчитатьСотрудника(Команда)
	РасчетЗарплатыРасширенныйКлиент.ПересчитатьСотрудника(ЭтаФорма, "НДФЛ", "ФизическоеЛицо", Тип("СправочникСсылка.ФизическиеЛица"));
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВзносыПересчитатьСотрудника(Команда)
	РасчетЗарплатыРасширенныйКлиент.ПересчитатьСотрудника(ЭтаФорма, "Взносы", "ФизическоеЛицо", Тип("СправочникСсылка.ФизическиеЛица"));
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПогашениеЗаймовПересчитатьСотрудника(Команда)
	РасчетЗарплатыРасширенныйКлиент.ПересчитатьСотрудника(ЭтаФорма, "ПогашениеЗаймов", "ФизическоеЛицо", Тип("СправочникСсылка.ФизическиеЛица"));
КонецПроцедуры

#Область ПодключаемыеКомандыУдалитьСотрудника

&НаКлиенте
Процедура Подключаемый_НачисленияУдалитьСотрудника(Команда)
	
	ТекущиеДанные = Элементы.Начисления.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда 
		УдалитьСотрудникаНаСервере(ТекущиеДанные.Сотрудник);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НачисленияПерерасчетУдалитьСотрудника(Команда)

	ТекущиеДанные = Элементы.НачисленияПерерасчет.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда 
		УдалитьСотрудникаНаСервере(ТекущиеДанные.Сотрудник);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НачисленияПоДоговорамУдалитьСотрудника(Команда)
	
	ТекущиеДанные = Элементы.НачисленияПоДоговорам.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда 
		УдалитьСотрудникаНаСервере(ТекущиеДанные.Сотрудник);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПособияУдалитьСотрудника(Команда)
	
	ТекущиеДанные = Элементы.Пособия.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда 
		УдалитьСотрудникаНаСервере(ТекущиеДанные.Сотрудник);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПособияПерерасчетУдалитьСотрудника(Команда)
	
	ТекущиеДанные = Элементы.ПособияПерерасчет.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда 
		УдалитьСотрудникаНаСервере(ТекущиеДанные.Сотрудник);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЛьготыУдалитьСотрудника(Команда)
	
	ТекущиеДанные = Элементы.Льготы.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда 
		УдалитьСотрудникаНаСервере(ТекущиеДанные.Сотрудник);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УдержанияУдалитьСотрудника(Команда)

	ТекущиеДанные = Элементы.Удержания.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда 
		УдалитьДанныеСотрудникаНаСервере(ТекущиеДанные.ФизическоеЛицо);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НДФЛУдалитьСотрудника(Команда)
	
	ТекущиеДанные = Элементы.НДФЛ.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда 
		УдалитьДанныеСотрудникаНаСервере(ТекущиеДанные.ФизическоеЛицо);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВзносыУдалитьСотрудника(Команда)
	
	ТекущиеДанные = Элементы.Взносы.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда 
		УдалитьДанныеСотрудникаНаСервере(ТекущиеДанные.ФизическоеЛицо);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПогашениеЗаймовУдалитьСотрудника(Команда)
	
	ТекущиеДанные = Элементы.ПогашениеЗаймов.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда 
		УдалитьДанныеСотрудникаНаСервере(ТекущиеДанные.ФизическоеЛицо);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура Подключаемый_НачисленияПерерасчетРазрешеноРедактироватьСторно(Команда)
	РасчетЗарплатыРасширенныйКлиент.НастроитьРедактированиеСторно(ЭтотОбъект, Команда.Имя);
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// Подключаемые команды «Пересчитать сотрудника».

&НаКлиенте
Процедура Подключаемый_ПланируемаяДатаВыплатыПриИзменении(Элемент)
	
	Если СтрокиТаблицДокументаПоФизическимЛицам <> Неопределено Тогда
		СписокФизическихЛиц = Новый Массив;
		Для Каждого СтрокаФизическогоЛица ИЗ СтрокиТаблицДокументаПоФизическимЛицам Цикл
			СписокФизическихЛиц.Добавить(СтрокаФизическогоЛица.Ключ);
		КонецЦикла;
		
		Если СписокФизическихЛиц.Количество() > 0 Тогда
			РежимПересчетаНДФЛ = Истина;
			ПерезаполнитьДанныеФормыНаКлиенте(СписокФизическихЛиц);
		КонецЕсли;
	КонецЕсли;
	УстановитьВидимостьКолонокПрогрессивногоНалога(Объект.ПланируемаяДатаВыплаты);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

// СтандартныеПодсистемы.КонтрольВеденияУчета

&НаКлиенте
Процедура Подключаемый_ОткрытьОтчетПоПроблемам(ЭлементИлиКоманда, НавигационнаяСсылка, СтандартнаяОбработка)
	
	КонтрольВеденияУчетаКлиентБЗК.ОткрытьОтчетПоПроблемамОбъекта(ЭтотОбъект, Объект.Ссылка, СтандартнаяОбработка);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.КонтрольВеденияУчета


&НаКлиенте
Процедура ВыполнитьКомандуРедактированияРезультатовРаспределенияНачисленийИУдержаний(ОписаниеТаблицы, Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка = Истина)

	Если СтрНайти(Поле.Имя, "КомандаРедактированияРаспределения") = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаСДанными = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(ЭтотОбъект, ОписаниеТаблицы.ПутьКДанным);
	ТекущиеДанные = ТаблицаСДанными.НайтиПоИдентификатору(ВыбраннаяСтрока);
	
	ЗарплатаКадрыРасширенныйКлиент.ОткрытьФормуРедактированияРезультатовРаспределенияПоИсточникамФинансирования(ЭтаФорма, ОписаниеТаблицы, ВыбраннаяСтрока, Объект.МесяцНачисления);
	СтандартнаяОбработка = Ложь;

КонецПроцедуры

#Область ИнициализацияФормы

&НаСервере
Процедура ПриПолученииДанныхНаСервере(ТекущийОбъект, ДополнятьФорму = Истина)
	
	ИспользоватьВоеннуюСлужбу = Ложь;
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба") Тогда
		НастройкиПриложения = Новый Структура;
		НастройкиПриложения.Вставить("ИспользоватьВоеннуюСлужбу", Ложь);
		НастройкиПриложения.Вставить("НачислятьОкладЗаКлассныйЧинМуниципальнымСлужащим", Ложь);
		НастройкиПриложения.Вставить("ИспользоватьРасчетСохраняемогоДенежногоСодержания", Ложь);  
		Модуль = ОбщегоНазначения.ОбщийМодуль("ГосударственнаяСлужба");
		Модуль.НастройкиПрограммыБюджетногоУчреждения(НастройкиПриложения);	
		ИспользоватьВоеннуюСлужбу = НастройкиПриложения.ИспользоватьВоеннуюСлужбу;
	КонецЕсли;

	НовыйДокумент = Параметры.Ключ.Пустая();
	НачатьИнициализациюФормы(ДополнятьФорму, НовыйДокумент);
	ЗарплатаКадрыРасширенный.ИзменитьРеквизитыФормы(ЭтаФорма);
	ЗавершитьИнициализациюФормы(ТекущийОбъект, ДополнятьФорму, НовыйДокумент);
	
	РеквизитыКДобавлению.Очистить();
	РеквизитыКУдалению.Очистить();
	
	ЗаполнитьСтрокиТаблицПоКлючевымПолям();
		
КонецПроцедуры

&НаСервере
Процедура НачатьИнициализациюФормы(ДополнятьФорму, НовыйДокумент = Истина)
	
	ЗаполнитьВидыОпераций(НовыйДокумент);
	
	// Устанавливаем значения реквизитов формы.
	РаботаВБюджетномУчреждении = ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении");
	ИспользоватьСтатьиФинансирования = ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный");
	УстановитьФункциональныеОпцииФормы();
	
	Если Не ФормаДополнена И ДополнятьФорму Тогда
		// Вызываем ДополнитьФорму() в режиме отложенных вызовов.
		ДополнитьФорму(Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗавершитьИнициализациюФормы(ТекущийОбъект, ДополнятьФорму, НовыйДокумент = Истина)

	Если Не ФормаДополнена И ДополнятьФорму Тогда 
		// Досоздаем форму.
		ДополнитьФорму(Ложь);
		ДополнитьФорму();
	КонецЕсли;
	
	// чтение данных
	ДанныеВРеквизиты(ТекущийОбъект);
	
	НастройкиВыплатыЗарплатыВОрганизации = Новый ФиксированнаяСтруктура(
		РасчетЗарплатыРасширенныйВызовСервера.СведенияОНастройкахЗарплатаКадрыРасширенная(Объект.Организация));
	
	Если НовыйДокумент И ТребуетсяЗаполнитьДатуВыплаты(Объект.МесяцНачисления, Объект.РежимДоначисления) Тогда
		РасчетЗарплатыРасширенный.УстановитьПланируемуюДатуВыплаты(ЭтотОбъект, ОписаниеДокумента(ЭтотОбъект));
	КонецЕсли;
		
	ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДате(ЭтаФорма, "Объект.МесяцНачисления", "МесяцНачисленияСтрокой");
	ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДатеВТабличнойЧасти(Объект.Взносы, "ДатаПолученияДохода", "ДатаПолученияДоходаСтрокой");
	ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДатеВТабличнойЧасти(Объект.Начисления, "ПериодДействия", "ПериодДействияСтрокой");
	ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДатеВТабличнойЧасти(Объект.НачисленияПерерасчет, "ПериодДействия", "ПериодДействияСтрокой");
	ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДатеВТабличнойЧасти(Объект.Пособия, "ПериодДействия", "ПериодДействияСтрокой");
	ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДатеВТабличнойЧасти(Объект.ПособияПерерасчет, "ПериодДействия", "ПериодДействияСтрокой");
	
	УчетСтраховыхВзносов.УстановитьВидимостьКолонокТаблицыСтраховыхВзносов(ЭтаФорма, Объект.МесяцНачисления);
	
	РасчетЗарплатыРасширенныйКлиентСервер.ДокументыВыполненияНачисленийУстановитьРежимОтображенияПодробно(ЭтаФорма, Элементы.НачисленияПодробно.Пометка, ОписаниеТаблицыНачислений(ЭтаФорма));
	РасчетЗарплатыРасширенныйКлиентСервер.ДокументыВыполненияНачисленийУстановитьРежимОтображенияПодробно(ЭтаФорма, Элементы.НачисленияПерерасчетПодробно.Пометка, ОписаниеТаблицыПерерасчетов(ЭтаФорма));
	РасчетЗарплатыРасширенныйКлиентСервер.ДокументыВыполненияНачисленийУстановитьРежимОтображенияПодробно(ЭтаФорма, Элементы.ПособияПодробно.Пометка, ОписаниеТаблицыПособия(ЭтаФорма));
	РасчетЗарплатыРасширенныйКлиентСервер.ДокументыВыполненияНачисленийУстановитьРежимОтображенияПодробно(ЭтаФорма, Элементы.ПособияПерерасчетПодробно.Пометка, ОписаниеТаблицыПособияПерерасчет(ЭтаФорма));
	РасчетЗарплатыРасширенныйКлиентСервер.ДокументыВыполненияНачисленийУстановитьРежимОтображенияПодробно(ЭтаФорма, Элементы.УдержанияПодробно.Пометка, ОписаниеТаблицыУдержаний());
	РасчетЗарплатыРасширенныйКлиентСервер.ДокументыВыполненияНачисленийУстановитьРежимОтображенияПодробно(ЭтаФорма, Элементы.УдержанияПерерасчетПодробно.Пометка, ОписаниеТаблицыУдержанияПерерасчет());
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ЛьготыСотрудников") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ЛьготыСотрудников");
		Модуль.ДокументыВыполненияНачисленийУстановитьРежимОтображенияПодробно(ЭтотОбъект, ОписанияТаблицЛьгот(ЭтаФорма));
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.УправленческаяЗарплата") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("УправленческаяЗарплата");
		Модуль.ПриЗавершенииИнициализацииФормыДокументаНачисленияЗарплаты(ЭтаФорма, Объект.РежимДоначисления, ИспользоватьНачисление);
	КонецЕсли;
	
	РассчитатьИтогиПоДокументу(ЭтаФорма);
	УстановитьОтображениеСообщенияОНеобходимостиПерерасчета();
	
	Если Объект.РежимДоначисления Тогда
		
		ГруппаСтраницы = Элементы.Найти("Страницы");
		Если ГруппаСтраницы <> Неопределено Тогда
			
			ГруппаНачисления = Элементы.Найти("ГруппаНачисления");
			
			ГруппаСтраница = Элементы.Найти("НачисленияПерерасчетСтраница");
			Если ГруппаСтраница <> Неопределено Тогда
				Элементы.Переместить(ГруппаСтраница, ГруппаСтраницы, ГруппаНачисления);
			КонецЕсли;
			
			ГруппаСтраница = Элементы.Найти("ПособияПерерасчетСтраницы");
			Если ГруппаСтраница <> Неопределено Тогда
				Элементы.Переместить(ГруппаСтраница, ГруппаСтраницы, ГруппаНачисления);
			КонецЕсли;
			
			ГруппаСтраница = Элементы.Найти("ЛьготыПерерасчетСтраница");
			Если ГруппаСтраница <> Неопределено Тогда
				Элементы.Переместить(ГруппаСтраница, ГруппаСтраницы, ГруппаНачисления);
			КонецЕсли;
			
			ГруппаСтраница = Элементы.Найти("УдержанияПерерасчетСтраница");
			Если ГруппаСтраница <> Неопределено Тогда
				Элементы.Переместить(ГруппаСтраница, ГруппаСтраницы, ГруппаНачисления);
			КонецЕсли;
			
		КонецЕсли;
	
		// Страницы с табличными частями
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ГруппаНачисления",
			"Видимость",
			Ложь);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ГруппаНачисленияПоДоговорам",
			"Видимость",
			Ложь);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ГруппаУправленческиеНачисления",
			"Видимость",
			Ложь);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ГруппаЛьготы",
			"Видимость",
			Ложь);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ГруппаУдержания",
			"Видимость",
			Ложь);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ГруппаПособия",
			"Видимость",
			Ложь);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ГруппаПогашениеЗаймов",
			"Видимость",
			Ложь);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ГруппаВзносы",
			"Видимость",
			Ложь);
		
		// Итоги по документу
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ИтогНачислено",
			"Видимость",
			Ложь);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ИтогВзносы",
			"Видимость",
			Ложь);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"РасчетныйЛистокУдержания",
			"Видимость",
			Ложь);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"РегистрНалоговогоУчетаПоНДФЛ",
			"Видимость",
			Ложь);
		
	Иначе
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ГруппаНачисления",
			"Видимость",
			ИспользоватьНачисление);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"НачисленияПерерасчетСтраница",
			"Видимость",
			ИспользоватьНачисление);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ГруппаПособия",
			"Видимость",
			ИспользоватьПособия);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ПособияПерерасчетСтраницы",
			"Видимость",
			ИспользоватьПособия);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ГруппаНачисленияПоДоговорам",
			"Видимость",
			ИспользоватьДоговоры);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ГруппаЛьготы",
			"Видимость",
			ИспользоватьЛьготы);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ГруппаУдержания",
			"Видимость",
			ИспользоватьУдержания);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"УдержанияПерерасчетСтраница",
			"Видимость",
			ИспользоватьУдержания);	
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ГруппаНДФЛ",
			"Видимость",
			ИспользоватьНДФЛ);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ГруппаКорректировкиВыплаты",
			"Видимость",
			ИспользоватьНДФЛ);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ГруппаПогашениеЗаймов",
			"Видимость",
			ИспользоватьЗаймы);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ГруппаВзносы",
			"Видимость",
			ИспользоватьВзносы);
		
		// Итоги по документу
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ИтогНачислено",
			"Видимость",
			ИспользоватьНачисление Или ИспользоватьПособия Или ИспользоватьДоговоры Или ИспользоватьЛьготы);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ИтогНачисленоПерерасчет",
			"Видимость",
			ИспользоватьНачисление);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ИтогУдержано",
			"Видимость",
			ИспользоватьУдержания Или ИспользоватьНДФЛ);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ИтогВзносы",
			"Видимость",
			ИспользоватьВзносы);
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ЛьготыПерерасчетСтраница",
		"Видимость",
		ИспользоватьЛьготы);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"НДФЛКомандаРедактированияРаспределения",
		"ГиперссылкаЯчейки",
		Ложь);
	
	ЗагрузитьНастройки();
	ЗаполнитьЗаголовокФормы();
	УстановитьДоступностьКомандыЗаполнитьССохранениемИсправлений(НайденыИсправленияПриОткрытии);
	УстановитьДоступностьКомандыДозаполнить();
	
КонецПроцедуры

// Подключение универсальных механизмов.
&НаСервере
Процедура ДополнитьФорму(ОтложенноеИзменение = Неопределено, КонтролируемыеПоля = Неопределено, ОписаниеПанелиВычеты = Неопределено, ОписаниеКлючевыхРеквизитов = Неопределено, ТаблицыОчищаемыеПриИзменении = Неопределено)
	
	Если ОтложенноеИзменение = Неопределено Тогда // Выполняем процедуры, не нуждающиеся в механизме отложенного создания.	
		
		// Создание реквизитов.
		РасчетЗарплатыРасширенныйФормы.ДокументыНачисленийДополнитьФормуРезультатыРаспределения(ЭтаФорма, ОписанияТаблицДляРаспределенияРезультата(ЭтаФорма));
		РасчетЗарплатыРасширенныйФормы.ДокументыНачисленийДополнитьФорму(ЭтаФорма, ОписаниеДокумента(ЭтотОбъект), Истина);
		УчетНДФЛФормы.ДополнитьФормуПанельюВычетов(ЭтаФорма, ОписаниеПанелиВычеты, Ложь);
		
		// Создание элементов.
		ЗарплатаКадрыРасширенный.КлючевыеРеквизитыЗаполненияФормыЗаполнитьПредупреждения(ЭтаФорма, ОписаниеКлючевыхРеквизитов);
		ЗарплатаКадрыРасширенныйКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтаФорма, , ОписаниеКлючевыхРеквизитов, ТаблицыОчищаемыеПриИзменении);
		// Установка свойств элементов формы.
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ВыплатаГруппа", "Видимость", ТребуетсяЗаполнитьДатуВыплаты(Объект.МесяцНачисления, Объект.РежимДоначисления));
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПланируемаяДатаВыплаты", "ПредупреждениеПриРедактировании", 
												НСтр("ru = 'Редактирование даты выплаты приведет к пересчету НДФЛ';
													|en = 'If you edit the payment date, it will recalculate the personal income tax'"));
		ДобавитьКомандуУдалитьСотрудника();
		
	Иначе
		
		Если ОтложенноеИзменение Тогда
			ДобавлятьЭлементыФормы = Ложь;
			ДобавлятьРеквизитыФормы = Истина;
		Иначе
			ДобавлятьЭлементыФормы = Истина;
			ДобавлятьРеквизитыФормы = Ложь;
		КонецЕсли;
		
		ДополнительныеПараметры = Новый Структура(
			"КонтролируемыеПоля, 
			|ДобавлятьЭлементыФормы, 
			|ДобавлятьРеквизитыФормы, 
			|ОтложенноеИзменение");
		ДополнительныеПараметры.КонтролируемыеПоля = КонтролируемыеПоля; 
		ДополнительныеПараметры.ДобавлятьЭлементыФормы = ДобавлятьЭлементыФормы;
		ДополнительныеПараметры.ДобавлятьРеквизитыФормы = ДобавлятьРеквизитыФормы;
		ДополнительныеПараметры.ОтложенноеИзменение = ОтложенноеИзменение;
		
		РасчетЗарплатыРасширенныйФормы.ДокументыВыполненияНачисленийДополнитьФорму(
			ЭтаФорма, ОписаниеТаблицыНачислений(ЭтаФорма), "Начисления", "НачисленияАвтоКоманды", , КонтролируемыеПоля, ДобавлятьЭлементыФормы, ДобавлятьРеквизитыФормы, ОтложенноеИзменение, Объект.Ссылка);
		РасчетЗарплатыРасширенныйФормы.ДокументыВыполненияНачисленийДополнитьФорму(
			ЭтаФорма, ОписаниеТаблицыПерерасчетов(ЭтаФорма), "Начисления", "НачисленияПерерасчетАвтоКоманды", , КонтролируемыеПоля, ДобавлятьЭлементыФормы, ДобавлятьРеквизитыФормы, ОтложенноеИзменение, Объект.Ссылка);
		РасчетЗарплатыРасширенныйФормы.ДокументыВыполненияНачисленийДополнитьФорму(
			ЭтаФорма, ОписаниеТаблицыНачисленийПоДоговорам(ЭтаФорма), "", "НачисленийПоДоговорамАвтоКоманды" , , КонтролируемыеПоля, ДобавлятьЭлементыФормы, ДобавлятьРеквизитыФормы, ОтложенноеИзменение, Объект.Ссылка);
		РасчетЗарплатыРасширенныйФормы.ДокументыВыполненияНачисленийДополнитьФорму(
			ЭтаФорма, ОписаниеТаблицыПособия(ЭтаФорма), "Пособия", "ПособияАвтоКоманды" , , КонтролируемыеПоля, ДобавлятьЭлементыФормы, ДобавлятьРеквизитыФормы, ОтложенноеИзменение, Объект.Ссылка);
		РасчетЗарплатыРасширенныйФормы.ДокументыВыполненияНачисленийДополнитьФорму(
			ЭтаФорма, ОписаниеТаблицыПособияПерерасчет(ЭтаФорма), "Пособия", "ПособияПерерасчетАвтоКоманды" , , КонтролируемыеПоля, ДобавлятьЭлементыФормы, ДобавлятьРеквизитыФормы, ОтложенноеИзменение, Объект.Ссылка);
		РасчетЗарплатыРасширенныйФормы.ДокументыВыполненияНачисленийДополнитьФорму(
			ЭтаФорма, ОписаниеТаблицыУдержаний(), "Удержания", "УдержанияАвтоКоманды" , , КонтролируемыеПоля, ДобавлятьЭлементыФормы, ДобавлятьРеквизитыФормы, ОтложенноеИзменение, Объект.Ссылка);
		РасчетЗарплатыРасширенныйФормы.ДокументыВыполненияНачисленийДополнитьФорму(
			ЭтаФорма, ОписаниеТаблицыУдержанияПерерасчет(), "Удержания", "УдержанияПерерасчетАвтоКоманды" , , КонтролируемыеПоля, ДобавлятьЭлементыФормы, ДобавлятьРеквизитыФормы, ОтложенноеИзменение, Объект.Ссылка);
		РасчетЗарплатыРасширенныйФормы.ДокументыВыполненияНачисленийДополнитьФорму(
			ЭтаФорма, ОписаниеТаблицыНДФЛ(), "", "НДФЛАвтоКоманды" , , КонтролируемыеПоля, ДобавлятьЭлементыФормы, ДобавлятьРеквизитыФормы, ОтложенноеИзменение, Объект.Ссылка);
		РасчетЗарплатыРасширенныйФормы.ДокументыВыполненияНачисленийДополнитьФорму(
			ЭтаФорма, ОписаниеТаблицыПогашениеЗаймов(), "", "ПогашениеЗаймовАвтоКоманды" , , КонтролируемыеПоля, ДобавлятьЭлементыФормы, ДобавлятьРеквизитыФормы, ОтложенноеИзменение, Объект.Ссылка);
			
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ЛьготыСотрудников") Тогда
			Модуль = ОбщегоНазначения.ОбщийМодуль("ЛьготыСотрудников");
			Модуль.ДокументыВыполненияНачисленийДополнитьФорму(ЭтотОбъект, ОписаниеТаблицыЛьгот(ЭтаФорма), "ЛьготыАвтоКоманды", КонтролируемыеПоля, , , ДобавлятьЭлементыФормы, ДобавлятьРеквизитыФормы, ОтложенноеИзменение, Объект.Ссылка);
			Модуль.ДокументыВыполненияНачисленийДополнитьФорму(ЭтотОбъект, ОписаниеТаблицыЛьготыПерерасчет(ЭтаФорма), "ЛьготыПерерасчетАвтоКоманды", КонтролируемыеПоля, , , ДобавлятьЭлементыФормы, ДобавлятьРеквизитыФормы, ОтложенноеИзменение, Объект.Ссылка);
		КонецЕсли;
			
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.УправленческаяЗарплата") Тогда
			Модуль = ОбщегоНазначения.ОбщийМодуль("УправленческаяЗарплата");
			Модуль.ПриДополненииФормыДокументаНачислениеЗарплаты(ЭтаФорма, ДополнительныеПараметры);
		КонецЕсли;
		
		РасчетЗарплатыРасширенныйФормы.ДополнитьДанныеФормыРаспределенияПоТерриториямУсловиямТруда(
			ЭтаФорма, Объект.Организация, ОписанияТаблицСРаспределениемПоТерриториямУсловиямТруда(ЭтаФорма), ДобавлятьЭлементыФормы, ДобавлятьРеквизитыФормы, ОтложенноеИзменение);
		РасчетЗарплатыРасширенныйФормы.ПорядокВыплатыЗарплатыДополнитьФорму(
			ЭтаФорма,, ДобавлятьЭлементыФормы, ДобавлятьРеквизитыФормы, ОтложенноеИзменение);
			
		Если Не Объект.РежимДоначисления Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПорядокВыплаты", "Видимость", Ложь);
		КонецЕсли;
		
		ОтражениеЗарплатыВБухучетеРасширенный.ОбъектПриЧтенииНаСервереПроцентЕНВД(
			ЭтаФорма, Объект.Организация, Объект.МесяцНачисления, ДобавлятьЭлементыФормы, ДобавлятьРеквизитыФормы, ОтложенноеИзменение);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

&НаСервере
Процедура ЗаполнитьВидыОпераций(НовыйДокумент)
	
	ИспользоватьНачисление 	= Истина;
	ИспользоватьПособия 	= Истина;
	ИспользоватьДоговоры 	= Истина;
	ИспользоватьЛьготы 		= Истина;
	ИспользоватьУдержания 	= Истина;
	ИспользоватьНДФЛ 		= Истина;
	ИспользоватьЗаймы 		= Истина;
	ИспользоватьВзносы 		= Истина;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОперацииРасчетаЗарплаты") Тогда 
		
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОперацииРасчетаЗарплаты");
		
		Если НовыйДокумент Тогда
			Параметры.Свойство("ВидОперации", ВидОперации);
			Если ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда 
				Модуль.УстановитьВидОперацииДокумента(ЭтотОбъект, Параметры.ЗначениеКопирования);
			КонецЕсли;
		Иначе
			Модуль.УстановитьВидОперацииДокумента(ЭтотОбъект, Объект.Ссылка);
		КонецЕсли;
		
		ДанныеВидаОперации = Модуль.ДанныеВидаОперации(ВидОперации);
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеВидаОперации);
		
	КонецЕсли;
	
	Если ИспользоватьЛьготы И ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ЛьготыСотрудников") Тогда 
		Модуль = ОбщегоНазначения.ОбщийМодуль("ЛьготыСотрудников");
		ИспользоватьЛьготы = Модуль.ИспользуетсяФункционалЛьгот();
	Иначе 
		ИспользоватьЛьготы = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗаголовокФормы()
	
	Если Объект.РежимДоначисления Тогда
		
		Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
			АвтоЗаголовок = Истина;
			Заголовок = "";
		Иначе 
			АвтоЗаголовок = Ложь;
			Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 (создание)';
																					|en = '%1 (creation)'"), НСтр("ru = 'Доначисление, перерасчет';
																													|en = 'Additional accrual, recalculation'"));
		КонецЕсли;
		
		Возврат;
		
	КонецЕсли;
	
	АвтоЗаголовок = Истина;
	НаименованиеВидаОперации = "";
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОперацииРасчетаЗарплаты") Тогда 
		АвтоЗаголовок = Не ЗначениеЗаполнено(ЭтотОбъект["ВидОперации"]);
	    НаименованиеВидаОперации = Строка(ЭтотОбъект["ВидОперации"]);
	КонецЕсли;
	
	Если Не АвтоЗаголовок Тогда
		Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
			Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = '%1 (создание)';
								|en = '%1 (creation)'"), НаименованиеВидаОперации);
		Иначе
			Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = '%1 %2 от %3';
								|en = '%1 %2 dated %3'"), НаименованиеВидаОперации, Объект.Номер, Формат(Объект.Дата, "ДЛФ=Д"));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НачисленияПоДоговорамПриОкончанииРедактированияНаСервере()
	
	ЗаполнитьВторичныеДанныеФормыПримененныеВычеты();
	ЗарплатаКадрыРасширенныйКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтаФорма);
	ЗарплатаКадрыРасширенныйКлиентСервер.ПланируемаяДатаВыплатыУстановитьОтображениеПредупреждения(ЭтаФорма,"ПланируемаяДатаВыплаты",
												СтрокиТаблицДокументаПоФизическимЛицам);
	УстановитьДоступностьКомандыЗаполнитьССохранениемИсправлений();
	
КонецПроцедуры

&НаСервере
Процедура НДФЛПриОкончанииРедактированияНаСервере()
	ЗарплатаКадрыРасширенныйКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтаФорма);
	ЗарплатаКадрыРасширенныйКлиентСервер.ПланируемаяДатаВыплатыУстановитьОтображениеПредупреждения(ЭтаФорма,"ПланируемаяДатаВыплаты",
												СтрокиТаблицДокументаПоФизическимЛицам);
	УстановитьДоступностьКомандыЗаполнитьССохранениемИсправлений();
	Если ИспользоватьСтатьиФинансирования Тогда
		// Если пользователь изменил физическое лицо, то в таблице распределения следует заменить сотрудника
		ИдентификаторСтроки = Элементы.НДФЛ.ТекущаяСтрока;
		Если ИдентификаторСтроки <> Неопределено Тогда
			ТекущиеДанные 			= Объект.НДФЛ.НайтиПоИдентификатору(ИдентификаторСтроки);
			РезультатРаспределения	= ТекущиеДанные.РезультатРаспределения;
			Если РезультатРаспределения <> Неопределено И РезультатРаспределения.Количество() > 0 Тогда
				ФизическоеЛицоНДФЛ			= ТекущиеДанные.ФизическоеЛицо;
				ФизическоеЛицоРаспределение	= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РезультатРаспределения[0].Сотрудник,"ФизическоеЛицо");
				Если ФизическоеЛицоНДФЛ <> ФизическоеЛицоРаспределение Тогда
					// Пользователь изменил физическое лицо. Очищаем в этом случае распределение
					НовоеРаспределение = Новый Массив;
					ТекущиеДанные.РезультатРаспределения = Новый ФиксированныйМассив(НовоеРаспределение);
					ОтражениеЗарплатыВБухучетеКлиентСерверРасширенный.ЗаполнитьПредставлениеРаспределенияВСтроке(ТекущиеДанные, Ложь, "НДФЛ", 
																												РаботаВБюджетномУчреждении);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура НДФЛПослеУдаленияНаСервере(СотрудникиПередУдалением)
	
	ЗарплатаКадрыРасширенныйКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтаФорма);
	РассчитатьИтогУдержано(ЭтаФорма);
	УстановитьДоступностьКомандыЗаполнитьССохранениемИсправлений();
	УчетНДФЛФормыКлиентСервер.УдалитьУчтенныеПриРасчетеНДФЛДокументы(Объект.НДФЛ, Объект.УчтенныеПриРасчетеНДФЛДокументы, СотрудникиПередУдалением);
	
КонецПроцедуры

&НаСервере
Процедура ПогашенияЗаймовПриОкончанииРедактированияНаСервере()
	
	ЗарплатаКадрыРасширенныйКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтаФорма);
	ЗарплатаКадрыРасширенныйКлиентСервер.ПланируемаяДатаВыплатыУстановитьОтображениеПредупреждения(ЭтаФорма,"ПланируемаяДатаВыплаты",
												СтрокиТаблицДокументаПоФизическимЛицам);
	РассчитатьИтогУдержано(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ПогашенияЗаймовПослеУдаленияНаСервере()
	
	ЗарплатаКадрыРасширенныйКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтаФорма);
	ЗарплатаКадрыРасширенныйКлиентСервер.ПланируемаяДатаВыплатыУстановитьОтображениеПредупреждения(ЭтаФорма,"ПланируемаяДатаВыплаты",
												СтрокиТаблицДокументаПоФизическимЛицам);
	РассчитатьИтогУдержано(ЭтаФорма);
	УстановитьДоступностьКомандыЗаполнитьССохранениемИсправлений();
	
КонецПроцедуры

&НаСервере
Процедура ВзносыПриОкончанииРедактированияНаСервере()
	
	ЗарплатаКадрыРасширенныйКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтаФорма);
	ЗарплатаКадрыРасширенныйКлиентСервер.ПланируемаяДатаВыплатыУстановитьОтображениеПредупреждения(ЭтаФорма,"ПланируемаяДатаВыплаты",
												СтрокиТаблицДокументаПоФизическимЛицам);
	РассчитатьИтогВзносы(ЭтаФорма);
	УстановитьДоступностьКомандыЗаполнитьССохранениемИсправлений();
	
КонецПроцедуры

&НаСервере
Процедура ВзносыПослеУдаленияНаСервере()
	
	ЗарплатаКадрыРасширенныйКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтаФорма);
	ЗарплатаКадрыРасширенныйКлиентСервер.ПланируемаяДатаВыплатыУстановитьОтображениеПредупреждения(ЭтаФорма,"ПланируемаяДатаВыплаты",
												СтрокиТаблицДокументаПоФизическимЛицам);
	РассчитатьИтогВзносы(ЭтаФорма);
	УстановитьДоступностьКомандыЗаполнитьССохранениемИсправлений();
	
КонецПроцедуры

// Месяц начисления
&НаКлиенте
Процедура ОбработчикОжиданияМесяцНачисленияПриИзменении()

	ПриИзмененииМесяцаНачисления();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииМесяцаНачисления()
	ЗарплатаКадрыРасширенныйКлиент.КлючевыеРеквизитыЗаполненияФормыОчиститьТаблицы(ЭтаФорма);
	ОбработатьИзменениеМесяцНачисленияНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииСотрудника(Элемент, ИмяТаблицы)
	
	ТекущиеДанные = Элемент.Родитель.ТекущиеДанные;
	Идентификатор = ТекущиеДанные.ПолучитьИдентификатор();
	Если ЗначениеЗаполнено(ТекущиеДанные.ФизическоеЛицо) Тогда
		УдалитьИдентификаторСтрокиФизическогоЛица(ТекущиеДанные.ФизическоеЛицо, ИмяТаблицы, Идентификатор);
	КонецЕсли;
	Если ЗначениеЗаполнено(ТекущиеДанные.Сотрудник) Тогда
		ТекущиеДанные.ФизическоеЛицо = СотрудникиКлиентСерверПовтИсп.ФизическиеЛицаСотрудников(ТекущиеДанные.Сотрудник)[0];
		ДобавитьИдентификаторСтрокиФизическогоЛица(ТекущиеДанные.ФизическоеЛицо, ИмяТаблицы, Идентификатор); 
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииФизическогоЛица(Элемент, ИмяТаблицы) Экспорт
	ТекущиеДанные = Элемент.Родитель.ТекущиеДанные;
	Идентификатор = ТекущиеДанные.ПолучитьИдентификатор();
	Если ЗначениеЗаполнено(СтароеЗначениеФизическогоЛица) Тогда
		УдалитьИдентификаторСтрокиФизическогоЛица(СтароеЗначениеФизическогоЛица, ИмяТаблицы, Идентификатор);
	КонецЕсли;
	ДобавитьИдентификаторСтрокиФизическогоЛица(ТекущиеДанные.ФизическоеЛицо, ИмяТаблицы, Идентификатор); 
КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеМесяцНачисленияНаСервере()
	
	УстановитьФункциональныеОпцииФормы();
	ОтражениеЗарплатыВБухучетеРасширенный.ОбработатьИзменениеМесяцНачисленияПроцентЕНВД(ЭтаФорма, Объект.Организация, Объект.МесяцНачисления);
	УчетСтраховыхВзносов.УстановитьВидимостьКолонокТаблицыСтраховыхВзносов(ЭтаФорма, Объект.МесяцНачисления);
	
	ЗаполнениеВыполнено = Ложь;
	РассчитатьИтогиПоДокументу(ЭтаФорма);
	УстановитьОтображениеСообщенияОНеобходимостиПерерасчета();
	
	НужнаДатаВыплаты = ТребуетсяЗаполнитьДатуВыплаты(Объект.МесяцНачисления, Объект.РежимДоначисления);
	Если НужнаДатаВыплаты Тогда
		РасчетЗарплатыРасширенный.УстановитьПланируемуюДатуВыплаты(ЭтотОбъект, ОписаниеДокумента(ЭтотОбъект));
		УстановитьВидимостьКолонокПрогрессивногоНалога(Объект.ПланируемаяДатаВыплаты);
	КонецЕсли;
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ВыплатаГруппа", "Видимость", НужнаДатаВыплаты);
	
КонецПроцедуры

// Заполнение и расчет начислений.

// Процедура используется для инициации расчета начислений из вне (на клиенте).
&НаКлиенте   
Процедура РассчитатьСотрудника(Сотрудник, ОписаниеТаблицы) Экспорт
	
	Если Не ЗначениеЗаполнено(Сотрудник) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Сотрудник) <> Тип("Массив") Тогда
		Сотрудники = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудник);
	Иначе
		Сотрудники = Сотрудник;
	КонецЕсли;
	
	МассивСотрудниковКРасчету = Новый Массив;
	Для Каждого СотрудникРасчета Из Сотрудники Цикл
		УдалитьПечатныеФормыСотрудника(СотрудникРасчета);
		Если НЕ РасчетЗарплатыРасширенныйКлиент.ДобавитьСотрудникаКРасчету(ЭтаФорма, СотрудникРасчета, ОписаниеТаблицы) Тогда
			МассивСотрудниковКРасчету.Добавить(СотрудникРасчета);
		КонецЕсли;
	КонецЦикла;
	
	Если МассивСотрудниковКРасчету.Количество() > 0 Тогда
		РассчитатьСотрудникаНаСервере(МассивСотрудниковКРасчету, ОписаниеТаблицы);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьСотрудникаНаСервере(Сотрудник, ОписаниеТаблицы = Неопределено, РассчитатьИтоги = Истина)
	
	Если Не РасчетЗарплатыРасширенныйКлиентСервер.ФормаДокументаГотоваДляРасчетаЗарплаты(ЭтаФорма, ОписаниеДокумента(ЭтотОбъект), Ложь) Тогда
		Возврат;
	КонецЕсли;
	
	Отбор = СотрудникиФизическиеЛицаОтбор(Сотрудник);
	Если Отбор = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерРасчета = РасчетЗарплатыРасширенный.СоздатьМенеджерРасчета(Объект.МесяцНачисления, Объект.Организация);
	ЗаполнитьНастройкиМенеджераРасчета(МенеджерРасчета, Отбор);
	РасчетЗарплатыРасширенныйФормы.ЗаполнитьИсточникиИзмененийМенеджераРасчета(МенеджерРасчета, ИзмененныеДанные);
	
	СотрудникиКРасчетуОтбор = Отбор.СотрудникиКРасчету;
	ПозицииВставки = Новый Структура;
	ДанныеФормыВДанныеМенеджераРасчета(МенеджерРасчета, Отбор, ПозицииВставки);
	МенеджерРасчета.РассчитатьЗарплату();
	
	ДанныеЗаполнения = РасчетЗарплатыРасширенный.ОписаниеДанныхДляЗаполненияТаблицДокумента();
	
	ДанныеЗаполнения.Начисления = МенеджерРасчета.Зарплата.Начисления;
	ДанныеЗаполнения.НачисленияПоДоговорам = МенеджерРасчета.Зарплата.ДоговорыПодряда;
	ДанныеЗаполнения.Удержания = МенеджерРасчета.Зарплата.Удержания;
	ДанныеЗаполнения.НДФЛ = МенеджерРасчета.Зарплата.НДФЛ;
	ДанныеЗаполнения.ПогашениеЗаймов = МенеджерРасчета.Зарплата.ПогашениеЗаймов;
	ДанныеЗаполнения.Взносы = МенеджерРасчета.Зарплата.Взносы;
	ДанныеЗаполнения.КорректировкиВыплаты = МенеджерРасчета.Зарплата.КорректировкиВыплаты;
	
	ДанныеДляЗаполненияВДанныеФормы(ДанныеЗаполнения, Объект, ПозицииВставки, СотрудникиКРасчетуОтбор, Ложь);
	ЗаполнитьСтрокиТаблицПоКлючевымПолям(ПозицииВставки, СотрудникиКРасчетуОтбор);
	
	УчетНДФЛФормы.ЗаполнитьВторичныеДанныеТабличныхЧастей(ЭтаФорма, , СотрудникиКРасчетуОтбор);
	ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДатеВТабличнойЧасти(Объект.Взносы, "ДатаПолученияДохода", "ДатаПолученияДоходаСтрокой");
	
	МассивОписанийТаблицФормы = МассивОписанийТаблицФормы(ЭтаФорма);
	ЗарплатаКадрыРасширенный.ПериодНачисленияЗаполнитьИспользованиеПериода(ЭтаФорма, МассивОписанийТаблицФормы, СотрудникиКРасчетуОтбор);
	ЗарплатаКадрыРасширенный.ЗаполнитьДоступностьМестаПолученияДохода(ЭтаФорма, МассивОписанийТаблицФормы, СотрудникиКРасчетуОтбор);
	ЗарплатаКадрыРасширенныйКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтаФорма);
	ЗарплатаКадрыРасширенныйКлиентСервер.ПланируемаяДатаВыплатыУстановитьОтображениеПредупреждения(ЭтаФорма,"ПланируемаяДатаВыплаты",
												СтрокиТаблицДокументаПоФизическимЛицам);
	ЗарплатаКадрыРасширенный.ВводНачисленийЗаполнитьВторичныеДанныеПоказателей(ЭтаФорма, МассивОписанийТаблицФормы, СотрудникиКРасчетуОтбор);
	РасчетЗарплатыРасширенныйФормы.ЗаполнитьПредставлениеРаспределенияПоТерриториямУсловиямТруда(ЭтаФорма, Объект.Организация, ОписанияТаблицСРаспределениемПоТерриториямУсловиямТруда(ЭтаФорма), СотрудникиКРасчетуОтбор);
	Если РассчитатьИтоги Тогда
		РассчитатьИтогиПоДокументу(ЭтаФорма);
		УстановитьОтображениеСообщенияОНеобходимостиПерерасчета();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДанныеФормыВДанныеМенеджераРасчета(МенеджерРасчета, Отбор, ПозицииВставки)
	
	ДанныеРезультатовРаспределенияНачислений = Неопределено;
	Если ЗначениеЗаполнено(АдресТаблицыРаспределенияНачислений) Тогда 
		ДанныеРезультатовРаспределенияНачислений = ПолучитьИзВременногоХранилища(АдресТаблицыРаспределенияНачислений);
	КонецЕсли;
	
	ДанныеРезультатовРаспределенияУдержаний = Неопределено;
	Если ЗначениеЗаполнено(АдресТаблицыРаспределенияУдержаний) Тогда 
		ДанныеРезультатовРаспределенияУдержаний = ПолучитьИзВременногоХранилища(АдресТаблицыРаспределенияУдержаний);
	КонецЕсли;
	
	ДанныеРаспределенияПоТерриториямУсловиямТруда = Неопределено;
	Если ЗначениеЗаполнено(АдресТаблицыРаспределенияПоТерриториямУсловиямТруда) Тогда 
		ДанныеРаспределенияПоТерриториямУсловиямТруда = ПолучитьИзВременногоХранилища(АдресТаблицыРаспределенияПоТерриториямУсловиямТруда);
	КонецЕсли;
	
	//Начисления
	Если Не Объект.РежимДоначисления Тогда
		РасчетЗарплатыРасширенныйФормы.НачисленияВДанныеМенеджераРасчета(
			Объект.Начисления,
			МенеджерРасчета,
			Объект.Организация,
			Отбор,
			ПозицииВставки,
			ДанныеРезультатовРаспределенияНачислений,
			ДанныеРаспределенияПоТерриториямУсловиямТруда);
			
		РасчетЗарплатыРасширенныйФормы.ПособияВДанныеМенеджераРасчета(Объект.Пособия, МенеджерРасчета, Объект.Организация, Отбор, ПозицииВставки, ДанныеРезультатовРаспределенияНачислений, ДанныеРаспределенияПоТерриториямУсловиямТруда);
	КонецЕсли;
	
	РасчетЗарплатыРасширенныйФормы.НачисленияПерерасчетВДанныеМенеджераРасчета(Объект.НачисленияПерерасчет, МенеджерРасчета, Объект.Организация, Отбор, ПозицииВставки, ДанныеРезультатовРаспределенияНачислений, ДанныеРаспределенияПоТерриториямУсловиямТруда);
	РасчетЗарплатыРасширенныйФормы.ПособияПерерасчетВДанныеМенеджераРасчета(Объект.ПособияПерерасчет, МенеджерРасчета, Объект.Организация, Отбор, ПозицииВставки, ДанныеРезультатовРаспределенияНачислений, ДанныеРаспределенияПоТерриториямУсловиямТруда);
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ЛьготыСотрудников") Тогда
		РасчетЗарплатыРасширенныйФормы.ЛьготыВДанныеМенеджераРасчета(Объект.Льготы, МенеджерРасчета, Объект.Организация, Отбор, ПозицииВставки, ДанныеРезультатовРаспределенияНачислений, ДанныеРаспределенияПоТерриториямУсловиямТруда);
		РасчетЗарплатыРасширенныйФормы.ЛьготыВДанныеМенеджераРасчета(Объект.ЛьготыПерерасчет, МенеджерРасчета, Объект.Организация, Отбор, ПозицииВставки, ДанныеРезультатовРаспределенияНачислений, ДанныеРаспределенияПоТерриториямУсловиямТруда, Истина);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.УправленческаяЗарплата") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("УправленческаяЗарплата");
		Модуль.УправленческиеНачисленияВДанныеМенеджераРасчета(Объект.УправленческиеНачисления, МенеджерРасчета, Отбор, ПозицииВставки, ДанныеРезультатовРаспределенияНачислений, ДанныеРаспределенияПоТерриториямУсловиямТруда);
	КонецЕсли;
		
	//НачисленияПоДоговорам
	РасчетЗарплатыРасширенныйФормы.НачисленияПоДоговорамВДанныеМенеджераРасчета(Объект.НачисленияПоДоговорам, МенеджерРасчета, Объект.Организация, Отбор, ПозицииВставки, ДанныеРезультатовРаспределенияНачислений, ДанныеРаспределенияПоТерриториямУсловиямТруда);
	
	//Удержания
	РасчетЗарплатыРасширенныйФормы.УдержанияВДанныеМенеджераРасчета(Объект.Удержания, МенеджерРасчета, Отбор, ПозицииВставки, ДанныеРезультатовРаспределенияУдержаний);
	РасчетЗарплатыРасширенныйФормы.УдержанияВДанныеМенеджераРасчета(Объект.УдержанияПерерасчет, МенеджерРасчета, Отбор, ПозицииВставки, ДанныеРезультатовРаспределенияУдержаний, Истина);
	
	//КорректировкиВыплаты
	РасчетЗарплатыРасширенныйФормы.КорректировкиВыплатыВДанныеМенеджераРасчета(Объект.КорректировкиВыплаты, МенеджерРасчета, Отбор, ПозицииВставки, ДанныеРезультатовРаспределенияУдержаний);
	
	//ПогашениеЗаймов
	РасчетЗарплатыРасширенныйФормы.ПогашениеЗаймовВДанныеМенеджераРасчета(Объект.ПогашениеЗаймов, МенеджерРасчета, Отбор, ПозицииВставки, ДанныеРезультатовРаспределенияУдержаний);
	
	//НДФЛ
	РасчетЗарплатыРасширенныйФормы.НДФЛВДанныеМенеджераРасчета(Объект.НДФЛ, Объект.ПримененныеВычетыНаДетейИИмущественные, МенеджерРасчета, Отбор, ПозицииВставки, ДанныеРезультатовРаспределенияУдержаний);
	
	//Взносы
	РасчетЗарплатыРасширенныйФормы.ВзносыВДанныеМенеджераРасчета(Объект.Взносы, МенеджерРасчета, Отбор, ПозицииВставки);
	
КонецПроцедуры
	
&НаСервере
Процедура ЗаполнитьНастройкиМенеджераРасчета(МенеджерРасчета, Отбор = Неопределено)
	
	Если Отбор <> Неопределено Тогда
		МенеджерРасчета.НастройкиРасчета.Сотрудники = Отбор.СотрудникиКРасчету;
		МенеджерРасчета.НастройкиНДФЛ.Сотрудники = Отбор.СотрудникиКРасчету;
		МенеджерРасчета.НастройкиРасчета.ФизическиеЛица = Отбор.ФизическиеЛица;
		МенеджерРасчета.НастройкиЗаймов.Сотрудники = Отбор.СотрудникиКРасчету;
	КонецЕсли;
	
	МенеджерРасчета.ИсключаемыйРегистратор = Объект.Ссылка;
	МенеджерРасчета.НастройкиРасчета.РассчитыватьНачисления = ИспользоватьНачисление;
	МенеджерРасчета.НастройкиРасчета.РассчитыватьДоговорыПодряда = ИспользоватьДоговоры;
	МенеджерРасчета.НастройкиРасчета.РассчитыватьУдержания = ИспользоватьУдержания;
	МенеджерРасчета.НастройкиРасчета.РассчитыватьНДФЛ = ИспользоватьНДФЛ;
	МенеджерРасчета.НастройкиРасчета.РассчитыватьВзносы = ИспользоватьВзносы И Не Объект.РежимДоначисления;
	МенеджерРасчета.НастройкиРасчета.РассчитыватьЗаймы = ИспользоватьЗаймы И Не Объект.РежимДоначисления;
	МенеджерРасчета.НастройкиРасчета.РассчитыватьКорректировкиВыплаты = ИспользоватьНДФЛ;
	МенеджерРасчета.НастройкиРасчета.ОкончательныйРасчет = Истина;
	
	МенеджерРасчета.НастройкиНДФЛ.ОкончательныйРасчет = ОкончательныйРасчетНДФЛ;
	МенеджерРасчета.НастройкиНДФЛ.ДатаВыплаты = ДатаВыплаты(Объект.Организация, Объект.МесяцНачисления, Объект.ПланируемаяДатаВыплаты);
	МенеджерРасчета.НастройкиНДФЛ.ЗасчитыватьДанныеАвансов = Не Объект.РежимДоначисления;
	
	МенеджерРасчета.НастройкиЗаймов.ДатаПогашения = КонецМесяца(Объект.МесяцНачисления);
	
	МенеджерРасчета.НастройкиБухучета.ПроверятьРегистрациюПроцентаЕНВД = Истина;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ДанныеДляЗаполненияВДанныеФормы(ДанныеЗаполнения, Объект, ПозицииВставки = Неопределено,
	Сотрудники = Неопределено, ЗаполнятьСотрудникиПерерасчет = Истина)
	
	//Начисления
	ТаблицыНачислений = РасчетЗарплатыРасширенныйФормы.ТаблицыНачисленийФормы();
	Если Не Объект.РежимДоначисления Тогда 
		ТаблицыНачислений.Начисления = Объект.Начисления;
		ТаблицыНачислений.Пособия = Объект.Пособия;
	КонецЕсли;
	ТаблицыНачислений.НачисленияПерерасчет = Объект.НачисленияПерерасчет;
	ТаблицыНачислений.ПособияПерерасчет = Объект.ПособияПерерасчет;
	ТаблицыНачислений.Льготы = Объект.Льготы;
	ТаблицыНачислений.ЛьготыПерерасчет = Объект.ЛьготыПерерасчет;
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.УправленческаяЗарплата") Тогда
		ТаблицыНачислений.УправленческиеНачисления = Объект.УправленческиеНачисления;
	КонецЕсли;
	
	Если Сотрудники <> Неопределено 
		И Сотрудники.Количество() = 1 Тогда
		СортироватьДанныеДляЗаполнения(ДанныеЗаполнения, Объект.Организация, Объект.МесяцНачисления, Ложь);
	Иначе
		СортироватьДанныеДляЗаполнения(ДанныеЗаполнения, Объект.Организация, Объект.МесяцНачисления, Истина);
	КонецЕсли;
	РасчетЗарплатыРасширенныйФормы.РасчетЗарплатыНачисленияВДанныеФормы(ТаблицыНачислений, ДанныеЗаполнения.Начисления, Объект.Организация, Объект.МесяцНачисления, ПозицииВставки, Объект.РежимДоначисления);
	
	//Удержания
	ТаблицыУдержаний = РасчетЗарплатыРасширенныйФормы.ТаблицыУдержанийФормы();
	ТаблицыУдержаний.Удержания = Объект.Удержания;
	ТаблицыУдержаний.УдержанияПерерасчет = Объект.УдержанияПерерасчет;
	РасчетЗарплатыРасширенныйФормы.РасчетЗарплатыУдержанияВДанныеФормы(ТаблицыУдержаний, ДанныеЗаполнения.Удержания, Объект.МесяцНачисления, ПозицииВставки, Объект.РежимДоначисления);
	
	Если ЗаполнятьСотрудникиПерерасчет Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ДанныеЗаполнения.СотрудникиПерерасчет, Объект.СотрудникиПерерасчет);
	КонецЕсли;
	ПерерасчетЗарплаты.СвернутьНачисленияПерерасчеты(Объект.НачисленияПерерасчет);
	ПерерасчетЗарплаты.СвернутьНачисленияПерерасчеты(Объект.ПособияПерерасчет);
	
	//НачисленияПоДоговорам
	РасчетЗарплатыРасширенныйФормы.РасчетЗарплатыНачисленияПоДоговорамВДанныеФормы(Объект.НачисленияПоДоговорам, ДанныеЗаполнения.НачисленияПоДоговорам, Объект.Организация, ПозицииВставки);
	
	//ПогашениеЗаймов
	РасчетЗарплатыРасширенныйФормы.РасчетЗарплатыПогашениеЗаймовВДанныеФормы(Объект.ПогашениеЗаймов, ДанныеЗаполнения.ПогашениеЗаймов, ПозицииВставки);
	
	//НДФЛ
	РасчетЗарплатыРасширенныйФормы.РасчетЗарплатыНДФЛВДанныеФормы(Объект.НДФЛ, Объект.ПримененныеВычетыНаДетейИИмущественные, ДанныеЗаполнения.НДФЛ, ПозицииВставки, Объект.УчтенныеПриРасчетеНДФЛДокументы, Объект.Ссылка);
	
	//КорректировкиВыплаты
	РасчетЗарплатыРасширенныйФормы.РасчетЗарплатыКорректировкиВыплатыВДанныеФормы(Объект.КорректировкиВыплаты, ДанныеЗаполнения.КорректировкиВыплаты, ПозицииВставки);
	
	//Взносы
	РасчетЗарплатыРасширенныйФормы.РасчетЗарплатыВзносыВДанныеФормы(Объект.Взносы, ДанныеЗаполнения.Взносы, ПозицииВставки);
	
КонецПроцедуры

&НаСервереБезКонтекста 
Процедура СортироватьДанныеДляЗаполнения(ДанныеЗаполнения, Организация, МесяцНачисления, ИспользоватьСортировкуПоУмолчанию)	
	СортироватьРезультатРасчетаНачислений(ДанныеЗаполнения, ИспользоватьСортировкуПоУмолчанию);
	СортироватьРезультатыРасчетаПоФизическимЛицам(ДанныеЗаполнения, Организация, МесяцНачисления);
КонецПроцедуры	

&НаСервереБезКонтекста 
Процедура СортироватьРезультатРасчетаНачислений(ДанныеЗаполнения, ИспользоватьСортировкуПоУмолчанию = Истина)
	
	СортируемыеКоллекции = Новый Массив;
	
	Коллекция = СотрудникиФормыРасширенный.СортируемаяКоллекцияСотрудников(ДанныеЗаполнения.Начисления);
	Коллекция.ОписаниеКоллекции.Псевдоним = "Начисления";
	Коллекция.ОписаниеКоллекции.ИмяПоляСотрудник = "Сотрудник";
	Коллекция.ОписаниеКоллекции.ИмяПоляПодразделение = "Подразделение";
	Коллекция.ОписаниеКоллекции.ИмяПоляПериод = "ДатаНачала";
	СортируемыеКоллекции.Добавить(Коллекция);
		
	МетаданныеРеквизитов = Метаданные.Документы.НачислениеЗарплаты.ТабличныеЧасти.Начисления.Реквизиты;
	Если ИспользоватьСортировкуПоУмолчанию Тогда
		НастройкиСортировки = СотрудникиФормыРасширенный.НастройкиСортировкиПоУмолчанию();
	Иначе
		НастройкиСортировки = СотрудникиФормыРасширенный.НастройкиСортировки();
	КонецЕсли;
	
	СотрудникиФормыРасширенный.ДобавитьДополнительноеПолеСортировки(Коллекция, "Начисление.ОчередностьРасчета", МетаданныеРеквизитов.Начисление.Тип);
	СотрудникиФормыРасширенный.ДобавитьДополнительноеПолеСортировки(Коллекция, "Начисление.РеквизитДопУпорядочивания", МетаданныеРеквизитов.Начисление.Тип);
	СотрудникиФормыРасширенный.ДобавитьДополнительноеПолеСортировки(Коллекция, "ДокументОснование", МетаданныеРеквизитов.ДокументОснование.Тип);
	СотрудникиФормыРасширенный.ДобавитьДополнительноеПолеСортировки(Коллекция, "ДатаНачала", МетаданныеРеквизитов.ДатаНачала.Тип);	
	
	Коллекция = СотрудникиФормыРасширенный.СортируемаяКоллекцияСотрудников(ДанныеЗаполнения.НачисленияПоДоговорам);
	Коллекция.ОписаниеКоллекции.Псевдоним = "НачисленияПоДоговорам";
	Коллекция.ОписаниеКоллекции.ИмяПоляСотрудник = "Сотрудник";
	Коллекция.ОписаниеКоллекции.ИмяПоляПодразделение = "Подразделение";
	Коллекция.ОписаниеКоллекции.ИмяПоляПериод = "ДатаНачала";
	СортируемыеКоллекции.Добавить(Коллекция);
	
	УстановитьПривилегированныйРежим(Истина);
	ПорядокСортировки = СотрудникиФормыРасширенный.ПорядокСтрокКоллекцийСотрудников(СортируемыеКоллекции, НастройкиСортировки);
	УстановитьПривилегированныйРежим(Ложь);
	
	Для Каждого КлючЗначение Из ПорядокСортировки Цикл
	    ТаблицаИсходныхДанных = ДанныеЗаполнения[КлючЗначение.Ключ];
		ТаблицаИсходныхДанных.Колонки.Добавить("Порядок", Новый ОписаниеТипов("Число"));
			
		Для Каждого СтрокаПорядка Из КлючЗначение.Значение Цикл
			СтрокаТаблицы = ТаблицаИсходныхДанных[СтрокаПорядка.ИндексСтрокиИсходнойТаблицы];
			СтрокаТаблицы.Порядок = СтрокаПорядка.Порядок;	
		КонецЦикла;	
		
		ТаблицаИсходныхДанных.Сортировать("Порядок");
		ТаблицаИсходныхДанных.Колонки.Удалить("Порядок");
	КонецЦикла;
		
КонецПроцедуры	

&НаСервереБезКонтекста 
Процедура СортироватьРезультатыРасчетаПоФизическимЛицам(ДанныеЗаполнения, Организация, МесяцНачисления)	
	СортируемыеКоллекции = Новый Массив;
	
	Коллекция = СотрудникиФормыРасширенный.СортируемаяКоллекцияФизическихЛиц(ДанныеЗаполнения.Удержания);
	Коллекция.ОписаниеКоллекции.Псевдоним = "Удержания";
	Коллекция.ОписаниеКоллекции.ИмяПоляФизическоеЛицо = "ФизическоеЛицо";
	
	МетаданныеРеквизитов = Метаданные.Документы.НачислениеЗарплаты.ТабличныеЧасти.Удержания.Реквизиты;
	СотрудникиФормыРасширенный.ДобавитьДополнительноеПолеСортировки(Коллекция, "Удержание.ОчередностьРасчета", МетаданныеРеквизитов.Удержание.Тип);
	СотрудникиФормыРасширенный.ДобавитьДополнительноеПолеСортировки(Коллекция, "Удержание", МетаданныеРеквизитов.Удержание.Тип);
	
	СортируемыеКоллекции.Добавить(Коллекция);
		
	Коллекция = СотрудникиФормыРасширенный.СортируемаяКоллекцияФизическихЛиц(ДанныеЗаполнения.НДФЛ);
	Коллекция.ОписаниеКоллекции.Псевдоним = "НДФЛ";
	Коллекция.ОписаниеКоллекции.ИмяПоляФизическоеЛицо = "ФизическоеЛицо";
	
	МетаданныеРеквизитов = Метаданные.Документы.НачислениеЗарплаты.ТабличныеЧасти.НДФЛ.Реквизиты;
	СотрудникиФормыРасширенный.ДобавитьДополнительноеПолеСортировки(Коллекция, "МесяцНалоговогоПериода", МетаданныеРеквизитов.МесяцНалоговогоПериода.Тип);
	СотрудникиФормыРасширенный.ДобавитьДополнительноеПолеСортировки(Коллекция, "Подразделение", МетаданныеРеквизитов.Подразделение.Тип);
	
	СортируемыеКоллекции.Добавить(Коллекция);
	
	Коллекция = СотрудникиФормыРасширенный.СортируемаяКоллекцияФизическихЛиц(ДанныеЗаполнения.Взносы);
	Коллекция.ОписаниеКоллекции.Псевдоним = "Взносы";
	Коллекция.ОписаниеКоллекции.ИмяПоляФизическоеЛицо = "ФизическоеЛицо";
	
	МетаданныеРеквизитов = Метаданные.Документы.НачислениеЗарплаты.ТабличныеЧасти.Взносы.Реквизиты;
	СотрудникиФормыРасширенный.ДобавитьДополнительноеПолеСортировки(Коллекция, "МесяцНалоговогоПериода", МетаданныеРеквизитов.ДатаПолученияДохода.Тип);
	
	СортируемыеКоллекции.Добавить(Коллекция);
	
	Коллекция = СотрудникиФормыРасширенный.СортируемаяКоллекцияФизическихЛиц(ДанныеЗаполнения.ПогашениеЗаймов);	
	Коллекция.ОписаниеКоллекции.Псевдоним = "ПогашениеЗаймов";
	Коллекция.ОписаниеКоллекции.ИмяПоляФизическоеЛицо = "ФизическоеЛицо";
	
	МетаданныеРеквизитов = Метаданные.Документы.НачислениеЗарплаты.ТабличныеЧасти.ПогашениеЗаймов.Реквизиты;
	СотрудникиФормыРасширенный.ДобавитьДополнительноеПолеСортировки(Коллекция, "ФизическоеЛицо", МетаданныеРеквизитов.ФизическоеЛицо.Тип);
	СотрудникиФормыРасширенный.ДобавитьДополнительноеПолеСортировки(Коллекция, "ДоговорЗайма", МетаданныеРеквизитов.ДоговорЗайма.Тип);
	СотрудникиФормыРасширенный.ДобавитьДополнительноеПолеСортировки(Коллекция, "ДатаНачала", Новый ОписаниеТипов("Дата"));
	
	СортируемыеКоллекции.Добавить(Коллекция);
	
	Коллекция = СотрудникиФормыРасширенный.СортируемаяКоллекцияФизическихЛиц(ДанныеЗаполнения.КорректировкиВыплаты);
	Коллекция.ОписаниеКоллекции.Псевдоним = "КорректировкиВыплаты";
	Коллекция.ОписаниеКоллекции.ИмяПоляФизическоеЛицо = "ФизическоеЛицо";
	СортируемыеКоллекции.Добавить(Коллекция);
	
	УстановитьПривилегированныйРежим(Истина);
	ПорядокСортировки = СотрудникиФормыРасширенный.ПорядокСортировкиКоллекцияФизическихЛиц(СортируемыеКоллекции, Организация, КонецМесяца(МесяцНачисления));
	УстановитьПривилегированныйРежим(Ложь);
	
	Для Каждого КлючЗначение Из ПорядокСортировки Цикл
	    ТаблицаИсходныхДанных = ДанныеЗаполнения[КлючЗначение.Ключ];
		ТаблицаИсходныхДанных.Колонки.Добавить("Порядок", Новый ОписаниеТипов("Число"));
			
		Для Каждого СтрокаПорядка Из КлючЗначение.Значение Цикл
			СтрокаТаблицы = ТаблицаИсходныхДанных[СтрокаПорядка.ИндексСтрокиИсходнойТаблицы];
			СтрокаТаблицы.Порядок = СтрокаПорядка.Порядок;	
		КонецЦикла;	
		
		ТаблицаИсходныхДанных.Сортировать("Порядок");
		ТаблицаИсходныхДанных.Колонки.Удалить("Порядок");
	КонецЦикла;	
		
КонецПроцедуры

&НаКлиенте
Процедура ПерезаполнитьНачисленияСотрудника(Сотрудники, СохранятьИсправления = Истина) Экспорт
	
	ПерезаполнитьДанныеФормыНаКлиенте(Сотрудники, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДанныеФормыНаКлиенте() Экспорт
	
	ИтогНачислено = 0;
	ИтогУдержано = 0;
	ИтогВзносы = 0;
	
	УИДЗамера = ОценкаПроизводительностиКлиент.ЗамерВремени("ЗаполнениеДокументаНачислениеЗарплаты",,Ложь);
	
	Результат = РезультатЗаполненияВДлительнойОперации();
	
	Если Результат.ЗаданиеВыполнено Тогда
		
		Если ЗначениеЗаполнено(УИДЗамера) Тогда
			ОценкаПроизводительностиКлиент.ЗавершитьЗамерВремени(УИДЗамера);
			УИДЗамера = Неопределено;
		КонецЕсли;
		ОповеститьОбИзмененииОбъекта();
		РасчетЗарплатыРасширенныйКлиент.ОчиститьСписокСотрудниковКРасчету(ЭтаФорма);
	
	Иначе
		ИдентификаторЗадания = Результат.ИдентификаторЗадания;
		АдресХранилища		 = Результат.АдресХранилища;
		
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", 1, Истина);
		ФормаДлительнойОперации = ДлительныеОперацииКлиент.ОткрытьФормуДлительнойОперации(ЭтаФорма, ИдентификаторЗадания);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПерезаполнитьДанныеФормыНаКлиенте(Знач Сотрудники = Неопределено, СохранятьИсправления = Истина) Экспорт
	
	ОчиститьСообщения();
	
	Если Не РасчетЗарплатыРасширенныйКлиентСервер.ФормаДокументаГотоваДляРасчетаЗарплаты(ЭтаФорма, ОписаниеДокумента(ЭтотОбъект)) Тогда
		Возврат;
	КонецЕсли;
	
	Если Сотрудники <> Неопределено И ТипЗнч(Сотрудники) <> Тип("Массив") Тогда
		Сотрудники = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудники);
	КонецЕсли;
	
	ОчищатьСотрудниковКРасчету = Не РежимПересчетаНДФЛ И Не РежимПересчетаВзносов;
	
	Результат = ПерезаполнитьДанныеФормыНаСервере(Сотрудники, СохранятьИсправления, СотрудникиКРасчету);
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.ЗаданиеВыполнено Тогда
		Если ОчищатьСотрудниковКРасчету Тогда
			РасчетЗарплатыРасширенныйКлиент.ОчиститьСписокСотрудниковКРасчету(ЭтаФорма, Сотрудники);
		КонецЕсли;
		УстановитьЗначенияКонтролируемыхПолей();
	Иначе
		ИдентификаторЗадания = Результат.ИдентификаторЗадания;
		АдресХранилища		 = Результат.АдресХранилища;
		
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", 1, Истина);
		ФормаДлительнойОперации = ДлительныеОперацииКлиент.ОткрытьФормуДлительнойОперации(ЭтаФорма, ИдентификаторЗадания);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПерезаполнитьДанныеФормыНаСервере(Сотрудники = Неопределено, СохранятьИсправления = Истина, СотрудникиКРасчету = Неопределено)
	
	ИспользоватьДлительнуюОперацию = Истина;
	Если Сотрудники <> Неопределено Тогда
		// Если заполнение выполняется по ограниченному составу сотрудников, то обходимся без длительной операции.
		Если ТипЗнч(Сотрудники) <> Тип("Массив") Или Сотрудники.Количество() < 10 Тогда 
			ИспользоватьДлительнуюОперацию = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	СписокСотрудников = Неопределено;
	Если Сотрудники <> Неопределено Тогда
		СписокСотрудников = СотрудникиДляРасчета(Сотрудники);
	КонецЕсли;

	Результат = РасчетЗарплатыРасширенный.ПерезаполнитьДанныеФормы(
		ЭтаФорма,
		ОписаниеДокумента(ЭтотОбъект),
		СписокСотрудников,
		СохранятьИсправления,
		ИспользоватьДлительнуюОперацию,,
		СотрудникиПериодДействияДляПерерасчета(СписокСотрудников));
		
	РежимПересчетаНДФЛ = Ложь;	
	РежимПересчетаВзносов = Ложь;	 
	
	Если Результат <> Неопределено Тогда
		АдресХранилища = Результат.АдресХранилища;
	КонецЕсли;
	
	Если Результат <> Неопределено И Результат.ЗаданиеВыполнено Тогда
		ЗаполнениеПослеВыполненияДлительнойОперации();
	КонецЕсли;
			
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПересчитатьСотрудника(ИмяТаблицы, ВыбранныеСтроки, ВедущееПоле, ТипВедущегоПоля, СохранятьИсправления = Истина) Экспорт
	
	ОчиститьСообщения();
	
	Если Не РасчетЗарплатыРасширенныйКлиентСервер.ФормаДокументаГотоваДляРасчетаЗарплаты(ЭтаФорма, ОписаниеДокумента(ЭтотОбъект)) Тогда
		Возврат;
	КонецЕсли;		

	ТабличнаяЧасть = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(ЭтотОбъект, "Объект." + ИмяТаблицы);
	
	ФизическиеЛицаСотрудников = Новый Массив;
	Для Каждого ИдентификаторСтроки Из ВыбранныеСтроки Цикл
		
		ПерерассчитываемаяСтрока = ТабличнаяЧасть.НайтиПоИдентификатору(ИдентификаторСтроки);
		Сотрудник = ПерерассчитываемаяСтрока[ВедущееПоле];
		Если ТипЗнч(Сотрудник) = Тип("СправочникСсылка.Сотрудники") Тогда
			ФизическиеЛицаСотрудников.Добавить(СотрудникиКлиентСерверПовтИсп.ФизическиеЛицаСотрудников(Сотрудник)[0]);
		Иначе
			ФизическиеЛицаСотрудников.Добавить(Сотрудник);
		КонецЕсли;
	КонецЦикла;
	
	ФизическиеЛицаСотрудников = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ФизическиеЛицаСотрудников);
	Сотрудники = Новый Массив;
	ОписаниеДокумента = ОписаниеДокумента(ЭтаФорма);
	
	СодержимоеДокумента = СодержимоеСотрудниковДокумента(ФизическиеЛицаСотрудников, Сотрудники, ТипВедущегоПоля);
	
	СтруктураПараметров = РасчетЗарплатыРасширенныйКлиентСервер.ПараметрыПолученияДанныхЗаполненияДокумента();
	СтруктураПараметров.Организация = Объект.Организация;
	СтруктураПараметров.ДокументСсылка = Объект.Ссылка;
	СтруктураПараметров.Подразделение = Объект.Подразделение;
	СтруктураПараметров.МесяцНачисления = Объект.МесяцНачисления;
	СтруктураПараметров.ДатаВыплаты = ДатаВыплаты(Объект.Организация, Объект.МесяцНачисления, Объект.ПланируемаяДатаВыплаты);
	СтруктураПараметров.Сотрудники = Сотрудники;
	СтруктураПараметров.ФизическиеЛица = ФизическиеЛицаСотрудников;
	СтруктураПараметров.ОкончаниеПериода = КонецМесяца(Объект.МесяцНачисления);
	СтруктураПараметров.РежимНачисления = ПредопределенноеЗначение("Перечисление.РежимНачисленияЗарплаты.ОкончательныйРасчет");
	СтруктураПараметров.ОписаниеДокумента = ОписаниеДокумента;
	СтруктураПараметров.СохранятьИсправления = СохранятьИсправления;
	СтруктураПараметров.СодержимоеДокумента = СодержимоеДокумента;
	СтруктураПараметров.РежимПересчетаНДФЛ = РежимПересчетаНДФЛ;
	СтруктураПараметров.РежимПересчетаВзносов = РежимПересчетаВзносов;
	СтруктураПараметров.ОкончательныйРасчетНДФЛ = ОкончательныйРасчетНДФЛ;
	СтруктураПараметров.ПозицииВставки = Неопределено;
	СтруктураПараметров.НачислениеЗарплатыВоеннослужащим = Объект.НачислениеЗарплатыВоеннослужащим;
	
	АдресаРаспределения = Новый Структура;
	АдресРаспределенияПоТерриториямУсловиямТруда = Неопределено;
	
	Если ОписаниеДокумента.ОписанияТаблицДляРаспределенияРезультата <> Неопределено Тогда 
		Для Каждого КлючИЗначение Из ОписаниеДокумента.ОписанияТаблицДляРаспределенияРезультата Цикл
			ПутьКДанным = КлючИЗначение.Значение.ПутьКДаннымАдресРаспределенияРезультатовВХранилище;
			Если ЗначениеЗаполнено(ПутьКДанным) И ЗначениеЗаполнено(ЭтаФорма[ПутьКДанным]) И Не АдресаРаспределения.Свойство(ПутьКДанным) Тогда
				АдресаРаспределения.Вставить(ПутьКДанным, ЭтаФорма[ПутьКДанным]);
			КонецЕсли;
			ПутьКДанным = КлючИЗначение.Значение.ПутьКДаннымАдресРаспределенияПоТерриториямУсловиямТруда;
			Если ЗначениеЗаполнено(ПутьКДанным) И ЗначениеЗаполнено(ЭтаФорма[ПутьКДанным]) И АдресРаспределенияПоТерриториямУсловиямТруда = Неопределено Тогда
				АдресРаспределенияПоТерриториямУсловиямТруда = ЭтаФорма[ПутьКДанным];
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Результат = ПересчитатьСотрудникаНаСервере(СтруктураПараметров, УникальныйИдентификатор, СохранятьИсправления, АдресаРаспределения, АдресРаспределенияПоТерриториямУсловиямТруда);
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат <> Неопределено Тогда
		АдресХранилища = Результат.АдресХранилища;
	КонецЕсли;
		
	Если Результат <> Неопределено И Результат.ЗаданиеВыполнено Тогда
		ЗаполнениеПослеВыполненияДлительнойОперацииНаКлиенте();	
	КонецЕсли;
	
	Если Результат.ЗаданиеВыполнено Тогда
		РасчетЗарплатыРасширенныйКлиент.ОчиститьСписокСотрудниковКРасчету(ЭтаФорма);
		УстановитьЗначенияКонтролируемыхПолей();
	Иначе
		ИдентификаторЗадания = Результат.ИдентификаторЗадания;
		АдресХранилища		 = Результат.АдресХранилища;
		ПересчетСотрудникаВДлительнойОперации = Истина;
		
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", 1, Истина);
		ФормаДлительнойОперации = ДлительныеОперацииКлиент.ОткрытьФормуДлительнойОперации(ЭтаФорма, ИдентификаторЗадания);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ДополнитьСотрудниковКРасчету(Сотрудники, ФизическиеЛица, Организация)
	
	СотрудникиФизическихЛиц = КадровыйУчетРасширенный.СотрудникиФизическихЛиц(ФизическиеЛица, Организация, , Истина);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Сотрудники, СотрудникиФизическихЛиц);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПересчитатьСотрудникаНаСервере(Знач СтруктураПараметров, УникальныйИдентификатор, СохранятьИсправления, АдресаРаспределения, АдресРаспределенияПоТерриториямУсловиямТруда)
	
	ИспользоватьДлительнуюОперацию = Истина;
	// Если заполнение выполняется по ограниченному составу сотрудников, то обходимся без длительной операции.
	Если ТипЗнч(СтруктураПараметров.Сотрудники) <> Тип("Массив") Или СтруктураПараметров.Сотрудники.Количество() < 10 Тогда 
		ИспользоватьДлительнуюОперацию = Ложь;
	КонецЕсли;
	
	ДанныеРаспределения = Новый Структура;
	ДанныеРаспределенияПоТерриториямУсловиямТруда = Неопределено;
	
	Для Каждого КлючИЗначение Из АдресаРаспределения Цикл
		ДанныеРаспределения.Вставить(КлючИЗначение.Ключ, ПолучитьИзВременногоХранилища(КлючИЗначение.Значение));
	КонецЦикла;
	
	Если АдресРаспределенияПоТерриториямУсловиямТруда <> Неопределено Тогда
		ДанныеРаспределенияПоТерриториямУсловиямТруда = ПолучитьИзВременногоХранилища(АдресРаспределенияПоТерриториямУсловиямТруда);
	КонецЕсли;

	СтруктураПараметров.РаспределениеРезультатов = ДанныеРаспределения;
	СтруктураПараметров.ДанныеРаспределенияПоТерриториямУсловиямТруда = ДанныеРаспределенияПоТерриториямУсловиямТруда;
	СтруктураПараметров.КонтролируемыеПоля = КонтролируемыеПоля();
	
	СотрудникиПерерасчет = ПерерасчетЗарплаты.ПустаяТаблицаСотрудникиПериодДействияПерерасчет();
	
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(СтруктураПараметров.СодержимоеДокумента.СотрудникиПерерасчет, СотрудникиПерерасчет);
	СтруктураПараметров.СотрудникиПериодДействияПерерасчет = СотрудникиПерерасчет;
	
	Если Не ИспользоватьДлительнуюОперацию Тогда
		АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
		РасчетЗарплатыРасширенный.ПодготовитьДанныеДляПерезаполнения(СтруктураПараметров, АдресХранилища);
		Возврат Новый Структура("ЗаданиеВыполнено, АдресХранилища", Истина, АдресХранилища);
	КонецЕсли;
	
	НаименованиеЗадания = НСтр("ru = 'Заполнение документа ""Начисление зарплаты""';
								|en = 'Fill in the ""Salary accounting"" document'");
	Результат = ДлительныеОперации.ЗапуститьВыполнениеВФоне(
		УникальныйИдентификатор,
		"РасчетЗарплатыРасширенный.ПодготовитьДанныеДляПерезаполнения",
		СтруктураПараметров,
		НаименованиеЗадания);
			
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НоваяСтрокаТаблицыСодержимогоДокумента(ТабличнаяЧасть, Идентификатор, СтруктураТаблицДокумента, ИмяТаблицы)
	
	СодержимоеДокумента = СтруктураТаблицДокумента.СодержимоеДокумента;
	ШаблонСтрокТаблиц = СтруктураТаблицДокумента.ШаблонСтрокТаблиц;
	
	СтрокаТаблицы = ТабличнаяЧасть.НайтиПоИдентификатору(Идентификатор);
	Если СтрокаТаблицы = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	НоваяСтрока = НоваяСтрокаДанныхПоСотрудникам(СодержимоеДокумента[ИмяТаблицы], ШаблонСтрокТаблиц[ИмяТаблицы]);
	ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
	
	Возврат НоваяСтрока;
	
КонецФункции

&НаКлиенте
Процедура ДополнитьСтроку(ИдентификаторСтроки, ОписаниеТаблицы, ЗаполнятьСведенияСотрудников, ЗаполнятьЗначенияПоказателей) Экспорт
	ДополнитьСтрокуНаСервере(ИдентификаторСтроки, ОписаниеТаблицы, ЗаполнятьСведенияСотрудников, ЗаполнятьЗначенияПоказателей);
КонецПроцедуры

&НаСервере
Процедура ДополнитьСтрокуНаСервере(ИдентификаторСтроки, ОписаниеТаблицы, ЗаполнятьСведенияСотрудников, ЗаполнятьЗначенияПоказателей)
	РасчетЗарплатыРасширенныйФормы.ДополнитьСтрокуРасчета(ЭтаФорма, ОписаниеДокумента(ЭтотОбъект), ИдентификаторСтроки, ОписаниеТаблицы, ЗаполнятьСведенияСотрудников, ЗаполнятьЗначенияПоказателей);
КонецПроцедуры

&НаСервере
Функция РезультатЗаполненияВДлительнойОперации()
	
	СотрудникиПерерасчет = СотрудникиПериодДействияДляПерерасчета();
	
	РасчетЗарплатыРасширенныйКлиентСервер.ОчиститьТаблицыДокумента(ЭтаФорма, ОписаниеДокумента(ЭтотОбъект));
	
	СтруктураПараметров = РасчетЗарплатыРасширенныйКлиентСервер.ПараметрыПолученияДанныхЗаполненияДокумента();
	СтруктураПараметров.ОписаниеДокумента = ОписаниеДокумента(ЭтотОбъект);
	СтруктураПараметров.Организация = Объект.Организация;
	СтруктураПараметров.ДокументСсылка = Объект.Ссылка;
	СтруктураПараметров.Подразделение = Объект.Подразделение;
	СтруктураПараметров.МесяцНачисления = Объект.МесяцНачисления;
	СтруктураПараметров.ОкончаниеПериода = КонецМесяца(Объект.МесяцНачисления);
	СтруктураПараметров.РежимНачисления = Перечисления.РежимНачисленияЗарплаты.ОкончательныйРасчет;
	СтруктураПараметров.СотрудникиПериодДействияПерерасчет = СотрудникиПерерасчет;
	СтруктураПараметров.ИспользоватьВоеннуюСлужбу = ИспользоватьВоеннуюСлужбу;
	СтруктураПараметров.НачислениеЗарплатыВоеннослужащим = Объект.НачислениеЗарплатыВоеннослужащим;
	СтруктураПараметров.ОкончательныйРасчетНДФЛ = ОкончательныйРасчетНДФЛ;
	СтруктураПараметров.ПроверятьРегистрациюПроцентаЕНВД = Не РаботаВБюджетномУчреждении;
	СтруктураПараметров.ДатаВыплаты = ДатаВыплаты(Объект.Организация, Объект.МесяцНачисления, Объект.ПланируемаяДатаВыплаты);
	
	НаименованиеЗадания = НСтр("ru = 'Заполнение документа «Начисление зарплаты»';
								|en = 'Fill in the ""Salary accounting"" document'");
	
	Результат = ДлительныеОперации.ЗапуститьВыполнениеВФоне(
		УникальныйИдентификатор,
		"Документы.НачислениеЗарплаты.ПодготовитьДанныеДляЗаполнения",
		СтруктураПараметров,
		НаименованиеЗадания);
	
	АдресХранилища = Результат.АдресХранилища;
	
	Если Результат.ЗаданиеВыполнено Тогда
		ЗаполнениеПослеВыполненияДлительнойОперации();
	КонецЕсли;
		
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция СотрудникиПериодДействияДляПерерасчета(Сотрудники = Неопределено)
	
	Если Сотрудники = Неопределено Тогда
		СотрудникиПериодДействияПерерасчет = Объект.СотрудникиПерерасчет.Выгрузить();
	Иначе
		
		СотрудникиПериодДействияПерерасчет = Неопределено;
		Если ТипЗнч(Сотрудники) = Тип("СправочникСсылка.Сотрудники") Тогда
			СписокСотрудников = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудники);
		ИначеЕсли ТипЗнч(Сотрудники) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
			СписокСотрудников = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудники);
		Иначе
			СписокСотрудников = Сотрудники;
		КонецЕсли;
		
		СписокФизическихЛиц = Новый Массив;
		Для каждого Сотрудник Из СписокСотрудников Цикл
			
			Если ТипЗнч(Сотрудники) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
				СписокФизическихЛиц.Добавить(Сотрудник);
			КонецЕсли;
			
		КонецЦикла;
		
		Если СписокФизическихЛиц.Количество() > 0 Тогда
			
			ПараметрыПолученияСотрудников = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
			ПараметрыПолученияСотрудников.Организация = Объект.Организация;
			ПараметрыПолученияСотрудников.ОкончаниеПериода = Объект.МесяцНачисления - 1;
			ПараметрыПолученияСотрудников.СписокФизическихЛиц = СписокФизическихЛиц;
			
			СотрудникиФизическихЛиц = КадровыйУчет.СотрудникиОрганизации(Истина, ПараметрыПолученияСотрудников);
			Если СотрудникиФизическихЛиц.Количество() > 0  Тогда
				
				Для каждого СведенияСотрудника Из СотрудникиФизическихЛиц Цикл
					СписокСотрудников.Добавить(СведенияСотрудника.Сотрудник);
				КонецЦикла;
				
			КонецЕсли;
			
			ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияТипаИзМассива(СписокСотрудников, Тип("СправочникСсылка.ФизическиеЛица"));
			СписокСотрудников = ОбщегоНазначенияКлиентСервер.СвернутьМассив(СписокСотрудников);
			
		КонецЕсли;
		
		НайденныеСтроки = РасчетЗарплатыРасширенный.НайтиСтрокиОтборПоМассиву(Объект.СотрудникиПерерасчет, "Сотрудник", СписокСотрудников);
		СотрудникиПериодДействияПерерасчет = Объект.СотрудникиПерерасчет.Выгрузить(НайденныеСтроки);
	КонецЕсли;
	
	Объект.СотрудникиПерерасчет.Очистить();
	
	Возврат СотрудникиПериодДействияПерерасчет;
	
КонецФункции

&НаСервере
Функция СотрудникиДляРасчета(Сотрудники)
	
	Если ТипЗнч(Сотрудники) = Тип("СправочникСсылка.ФизическиеЛица")
		Или ТипЗнч(Сотрудники) = Тип("Массив") И Сотрудники.Количество() > 0 И ТипЗнч(Сотрудники[0]) = Тип("СправочникСсылка.ФизическиеЛица") Тогда 
		Возврат Сотрудники;
	КонецЕсли;
	
	СотрудникиДокумента = СотрудникиДокумента();
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Сотрудники", Сотрудники);
	Запрос.УстановитьПараметр("СотрудникиДокумента", СотрудникиДокумента);
	
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	Сотрудники.ФизическоеЛицо
	               |ПОМЕСТИТЬ ВТФизическиеЛица
	               |ИЗ
	               |	Справочник.Сотрудники КАК Сотрудники
	               |ГДЕ
	               |	Сотрудники.Ссылка В(&Сотрудники)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Сотрудники.Ссылка
	               |ИЗ
	               |	Справочник.Сотрудники КАК Сотрудники
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТФизическиеЛица КАК ФизическиеЛица
	               |		ПО Сотрудники.ФизическоеЛицо = ФизическиеЛица.ФизическоеЛицо
	               |			И (Сотрудники.Ссылка В (&СотрудникиДокумента))
	               |
	               |ОБЪЕДИНИТЬ
	               |
	               |ВЫБРАТЬ
	               |	Сотрудники.Ссылка
	               |ИЗ
	               |	Справочник.Сотрудники КАК Сотрудники
	               |ГДЕ
	               |	Сотрудники.Ссылка В(&Сотрудники)";
				   
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");			   
	
КонецФункции

&НаСервере
Функция СотрудникиФизическиеЛицаОтбор(Сотрудники)
	
	СотрудникиДокумента = СотрудникиДокумента();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Сотрудники", Сотрудники);
	Запрос.УстановитьПараметр("СотрудникиДокумента", СотрудникиДокумента);
	
	Если ТипЗнч(Сотрудники) = Тип("СправочникСсылка.ФизическиеЛица")
		Или ТипЗнч(Сотрудники) = Тип("Массив") И Сотрудники.Количество() > 0 И ТипЗнч(Сотрудники[0]) = Тип("СправочникСсылка.ФизическиеЛица") Тогда 
		
		ФизическиеЛица = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудники);
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Сотрудники.Ссылка КАК Сотрудник,
			|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо
			|ИЗ
			|	Справочник.Сотрудники КАК Сотрудники
			|ГДЕ
			|	Сотрудники.Ссылка В(&СотрудникиДокумента)
			|	И Сотрудники.ФизическоеЛицо В(&Сотрудники)";
			
		СотрудникиФизическиеЛица = Запрос.Выполнить().Выбрать();
		Если ТипЗнч(Сотрудники) <> Тип("Массив") Тогда
			ФизическиеЛица = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудники);
		Иначе
			ФизическиеЛица = Сотрудники;
		КонецЕсли;
	Иначе
		
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Сотрудники.ФизическоеЛицо
			|ПОМЕСТИТЬ ВТФизическиеЛица
			|ИЗ
			|	Справочник.Сотрудники КАК Сотрудники
			|ГДЕ
			|	Сотрудники.Ссылка В(&Сотрудники)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Сотрудники.Ссылка КАК Сотрудник,
			|	ФизическиеЛица.ФизическоеЛицо КАК ФизическоеЛицо
			|ИЗ
			|	Справочник.Сотрудники КАК Сотрудники
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТФизическиеЛица КАК ФизическиеЛица
			|		ПО Сотрудники.ФизическоеЛицо = ФизическиеЛица.ФизическоеЛицо
			|			И (Сотрудники.Ссылка В (&СотрудникиДокумента))
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВТФизическиеЛица.ФизическоеЛицо
			|ИЗ
			|	ВТФизическиеЛица КАК ВТФизическиеЛица";
			
		Результат = Запрос.ВыполнитьПакет();
		ФизическиеЛица = Результат[2].Выгрузить().ВыгрузитьКолонку("ФизическоеЛицо");
		СотрудникиФизическиеЛица = Результат[1].Выбрать();
		
	КонецЕсли;
	
	Отбор = Новый Структура("СотрудникиКРасчету, ФизическиеЛицаСотрудников, ФизическиеЛицаОтбор, ФизическиеЛица");
	Отбор.ФизическиеЛица = ФизическиеЛица;
	Отбор.ФизическиеЛицаСотрудников = Новый Соответствие;
	Отбор.ФизическиеЛицаОтбор = Новый Соответствие;
	Отбор.СотрудникиКРасчету = Новый Массив;
	
	Пока СотрудникиФизическиеЛица.Следующий() Цикл
		Отбор.ФизическиеЛицаСотрудников.Вставить(СотрудникиФизическиеЛица.Сотрудник, СотрудникиФизическиеЛица.ФизическоеЛицо);
		Отбор.СотрудникиКРасчету.Добавить(СотрудникиФизическиеЛица.Сотрудник);
	КонецЦикла;
	
	Для каждого ФизическоеЛицо Из ФизическиеЛица Цикл
		Отбор.ФизическиеЛицаОтбор.Вставить(ФизическоеЛицо, Истина)
	КонецЦикла; 
	
	Возврат Отбор;
	
КонецФункции

&НаСервере
Функция СотрудникиДокумента()
	
	СотрудникиДокумента = Новый Массив;
	
	ОписанияТаблиц = ОписанияТаблицНачисленийДляРасчетногоЛистка();
	Для Каждого ОписаниеТаблицы Из ОписанияТаблиц Цикл 
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СотрудникиДокумента, ОбщегоНазначения.ВыгрузитьКолонку(Объект[ОписаниеТаблицы.ИмяТаблицы], ОписаниеТаблицы.ИмяРеквизитаСотрудник, Истина));
	КонецЦикла;
	
	Возврат СотрудникиДокумента;
	
КонецФункции

&НаКлиенте
Процедура ПровестиДокумент()
	
	ОчиститьСообщения();
	
	Если Объект.ПометкаУдаления Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Помеченный на удаление документ не может быть проведен.';
										|en = 'Document marked for deletion cannot be posted.'"));
		Возврат;
	КонецЕсли;
	
	Если СотрудникиКРасчету.Количество() > 0 Тогда
		РасчетЗарплатыРасширенныйКлиент.ПередЗаписьюДокументаСоСпискомСотрудников(ЭтаФорма, "ПровестиДокументОкончание");
	Иначе
		ПровестиДокументНаКлиенте();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПровестиДокументОкончание(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ПересчитатьДокументНаКлиенте();
		ПровестиДокументНаКлиенте();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиДокументНаКлиенте()
	
	УИДЗамера = ОценкаПроизводительностиКлиент.ЗамерВремени("ПроведениеДокументаНачислениеЗарплаты",,Ложь);
	
	Результат = РезультатПроведенияВДлительнойОперации();
	
	Если Результат.Свойство("ОшибкиПроверкиЗаполнения") Тогда
		Возврат;
	КонецЕсли;
	
	ПечатныеФормыСотрудников = Неопределено;
	
	Если Результат.ЗаданиеВыполнено Тогда
		Если ЗначениеЗаполнено(УИДЗамера) Тогда
			ОценкаПроизводительностиКлиент.ЗавершитьЗамерВремени(УИДЗамера);
			УИДЗамера = Неопределено;
		КонецЕсли;
		ОповеститьОбИзмененииОбъекта();
		ПроведениеВДлительнойОперации = Ложь;
		Если ЗакрыватьПослеЗаписи Тогда
			ПодключитьОбработчикОжидания("ЗакрытьФорму", 0.1, Истина);
		КонецЕсли;
	Иначе
		ИдентификаторЗадания = Результат.ИдентификаторЗадания;
		АдресХранилища		 = Результат.АдресХранилища;
		
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", 1, Истина);
		ФормаДлительнойОперации = ДлительныеОперацииКлиент.ОткрытьФормуДлительнойОперации(ЭтаФорма, ИдентификаторЗадания);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция РезультатПроведенияВДлительнойОперации()
	
	Если Не ПроверитьЗаполнение() Тогда 
		УчетНДФЛФормыРасширенный.ОбработатьСообщенияПроверкиЗаполнения(ЭтаФорма, ОписаниеТаблицыНДФЛ());
		Возврат Новый Структура("ОшибкиПроверкиЗаполнения", Истина);
	КонецЕсли;
	
	УдалитьПерерасчетыТекущегоПериода = ЗаполнениеВыполнено;
	
	ДокументОбъект = Неопределено;
	// Преобразовываем данные формы в объект, чтобы записать его.
	Если Модифицированность Или Объект.Проведен Тогда
		ДокументОбъект = РеквизитФормыВЗначение("Объект");
		РеквизитыВДанные(ДокументОбъект);
		ДокументОбъект.ДополнительныеСвойства.Вставить("Проведен", ДокументОбъект.Проведен);
		ДанныеДокумента = ЗарплатаКадры.СериализоватьОбъектВДвоичныеДанные(ДокументОбъект);
	КонецЕсли;
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ДокументСсылка", Объект.Ссылка);
	СтруктураПараметров.Вставить("ДанныеДокумента", ДанныеДокумента);
	СтруктураПараметров.Вставить("Отказ", Ложь);
	СтруктураПараметров.Вставить("УстановитьПроведениеДокумента", Истина);
	СтруктураПараметров.Вставить("УдалитьПерерасчетыТекущегоПериода", УдалитьПерерасчетыТекущегоПериода);
	СтруктураПараметров.Вставить("ВидОперации", ЭтотОбъект["ВидОперации"]);
	
	Если ДокументОбъект <> Неопределено Тогда
		
		Если ДокументОбъект.ДополнительныеСвойства.Свойство("ПериодыРасчетаСотрудников") Тогда
			СтруктураПараметров.Вставить("ПериодыРасчетаСотрудников", ДокументОбъект.ДополнительныеСвойства.ПериодыРасчетаСотрудников);
		КонецЕсли; 
		
	КонецЕсли; 
	
	НаименованиеЗадания = НСтр("ru = 'Проведение документа «Начисление зарплаты»';
								|en = 'Post document ""Salary accounting""'");
	
	Результат = ДлительныеОперации.ЗапуститьВыполнениеВФоне(
		УникальныйИдентификатор,
		"Документы.НачислениеЗарплаты.ВыполнитьПроведение",
		СтруктураПараметров,
		НаименованиеЗадания);
	
	АдресХранилища = Результат.АдресХранилища;
	
	ПроведениеВДлительнойОперации = Не Результат.ЗаданиеВыполнено;
	
	Если Результат.ЗаданиеВыполнено Тогда
		ПроведениеПослеВыполненияДлительнойОперации();
		ПроведениеВДлительнойОперации = Ложь;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ЗаполнениеПослеВыполненияДлительнойОперации()
	
	СтруктураДанных = ПолучитьИзВременногоХранилища(АдресХранилища);
	Если ТипЗнч(СтруктураДанных) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеДляЗаполнения = Неопределено;
	Сотрудники = Неопределено;
	ПозицииВставки = Неопределено;
	
	Если СтруктураДанных.Свойство("ДанныеДляЗаполненияТаблицДокумента", ДанныеДляЗаполнения) Тогда
		// Заполнение табличных частей и вторичных данных коллекций, которые с ней связаны.
		ОчиститьТаблицыДокументаНаСервере();
		
		ДанныеДляЗаполненияВДанныеФормы(ДанныеДляЗаполнения, Объект);
		УчетНДФЛФормы.ЗаполнитьВторичныеДанныеТабличныхЧастей(ЭтаФорма);
		ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДатеВТабличнойЧасти(Объект.Взносы, "ДатаПолученияДохода", "ДатаПолученияДоходаСтрокой");
		
		МассивОписанийТаблицФормы = МассивОписанийТаблицФормы(ЭтаФорма);
		ЗарплатаКадрыРасширенный.ПериодНачисленияЗаполнитьИспользованиеПериода(ЭтаФорма, МассивОписанийТаблицФормы);
		ЗарплатаКадрыРасширенный.ЗаполнитьДоступностьМестаПолученияДохода(ЭтаФорма, МассивОписанийТаблицФормы);
		ЗарплатаКадрыРасширенныйКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтаФорма);
		ЗарплатаКадрыРасширенныйКлиентСервер.ПланируемаяДатаВыплатыУстановитьОтображениеПредупреждения(ЭтаФорма,"ПланируемаяДатаВыплаты",
												СтрокиТаблицДокументаПоФизическимЛицам);
		ЗарплатаКадрыРасширенный.ВводНачисленийЗаполнитьВторичныеДанныеПоказателей(ЭтаФорма, МассивОписанийТаблицФормы);
		
		РасчетЗарплатыРасширенныйФормы.ЗаполнитьПредставлениеРаспределенияПоТерриториямУсловиямТруда(ЭтаФорма, Объект.Организация, ОписанияТаблицСРаспределениемПоТерриториямУсловиямТруда(ЭтаФорма));
		
	КонецЕсли;
	
	Если СтруктураДанных.Свойство("ДанныеДляПерезаполненияТаблицДокумента", ДанныеДляЗаполнения) Тогда
		Сотрудники = СтруктураДанных.СтруктураПараметров.Сотрудники;
		ПозицииВставки = СтруктураДанных.СтруктураПараметров.ПозицииВставки;
		ДанныеДляЗаполненияВДанныеФормы(ДанныеДляЗаполнения, Объект, ПозицииВставки, Сотрудники);
		УчетНДФЛФормы.ЗаполнитьВторичныеДанныеТабличныхЧастей(ЭтаФорма, , Сотрудники);
		ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДатеВТабличнойЧасти(Объект.Взносы, "ДатаПолученияДохода", "ДатаПолученияДоходаСтрокой");
		МассивОписанийТаблицФормы = МассивОписанийТаблицФормы(ЭтаФорма);
		ЗарплатаКадрыРасширенный.ПериодНачисленияЗаполнитьИспользованиеПериода(ЭтаФорма, МассивОписанийТаблицФормы, Сотрудники);
		ЗарплатаКадрыРасширенный.ЗаполнитьДоступностьМестаПолученияДохода(ЭтаФорма, МассивОписанийТаблицФормы, Сотрудники);
		ЗарплатаКадрыРасширенныйКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтаФорма);
		ЗарплатаКадрыРасширенныйКлиентСервер.ПланируемаяДатаВыплатыУстановитьОтображениеПредупреждения(ЭтаФорма,"ПланируемаяДатаВыплаты",
												СтрокиТаблицДокументаПоФизическимЛицам);
		ЗарплатаКадрыРасширенный.ВводНачисленийЗаполнитьВторичныеДанныеПоказателей(ЭтаФорма, МассивОписанийТаблицФормы, Сотрудники);
		РасчетЗарплатыРасширенныйФормы.ЗаполнитьПредставлениеРаспределенияПоТерриториямУсловиямТруда(ЭтаФорма, Объект.Организация, ОписанияТаблицСРаспределениемПоТерриториямУсловиямТруда(ЭтаФорма), Сотрудники);
	КонецЕсли;
	
	ЗаполнениеВыполнено = Истина;
	
	ЗаполнитьСтрокиТаблицПоКлючевымПолям(ПозицииВставки, Сотрудники);
	РассчитатьИтогиПоДокументу(ЭтаФорма);
	УстановитьОтображениеСообщенияОНеобходимостиПерерасчета();
	УстановитьДоступностьКомандыЗаполнитьССохранениемИсправлений();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнениеПослеВыполненияДлительнойОперацииНаКлиенте()
	
	СодержимоеДокумента = СодержимоеДокументаПослеРасчета(
		АдресХранилища,
		МассивОписанийТаблицФормы(ЭтаФорма),
		ОписанияТаблицСРаспределениемПоТерриториямУсловиямТруда(ЭтаФорма),
		ГруппаКарточкаМаксимальныйИдентификаторСтрокиНДФЛ);
		
	ФизическиеЛица = СодержимоеДокумента.ФизическиеЛица;
	СтрокиТаблицРасчетаПоФизическимЛицам = СодержимоеДокумента.СтрокиТаблицРасчетаПоФизическимЛицам;
	Если СодержимоеДокумента = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаменитьСтрокиТаблицДокумента(Объект, ФизическиеЛица, СтрокиТаблицДокументаПоФизическимЛицам, СтрокиТаблицРасчетаПоФизическимЛицам);
	
	ЗарплатаКадрыРасширенныйКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(
		ЭтаФорма,, 
		КлючевыеРеквизитыОписаниеКлючевыхРеквизитов(ИспользоватьВоеннуюСлужбу), 
		КлючевыеРеквизитыТаблицыОчищаемыеПриИзменении());
	ЗарплатаКадрыРасширенныйКлиентСервер.ПланируемаяДатаВыплатыУстановитьОтображениеПредупреждения(ЭтаФорма,"ПланируемаяДатаВыплаты",
												СтрокиТаблицДокументаПоФизическимЛицам);
	РассчитатьИтогиПоДокументу(ЭтаФорма);
	
	ЗаполнениеВыполнено = Истина;
	ПересчетСотрудникаВДлительнойОперации = Ложь;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаменитьСтрокиТаблицДокумента(Объект, ФизическиеЛица, СтрокиТаблицДокумента, СтрокиТаблицРасчета, ТаблицыДокумента = Неопределено)
	
	Если ТаблицыДокумента = Неопределено Тогда
		ТаблицыДокумента = ТаблицыДокументаСДаннымиФизическихЛиц();
	КонецЕсли;
	
	Для Каждого ФизическоеЛицо Из ФизическиеЛица Цикл
		
		ТаблицыДокументаФизическогоЛица = СтрокиТаблицДокумента[ФизическоеЛицо];
		ТаблицыРасчетаФизическогоЛица = СтрокиТаблицРасчета[ФизическоеЛицо];
		
		Для Каждого ИмяТаблицы Из ТаблицыДокумента Цикл
			СтрокиТаблицыДокумента = Неопределено;
			Если Не ТаблицыДокументаФизическогоЛица.Свойство(ИмяТаблицы, СтрокиТаблицыДокумента) Тогда
				СтрокиТаблицыДокумента = Новый Массив;
				ТаблицыДокументаФизическогоЛица.Вставить(ИмяТаблицы, СтрокиТаблицыДокумента);
			КонецЕсли;
			
			СтрокиТаблицыРасчета = Неопределено;
			ОбходТаблицыИдентификаторов = РасчетЗарплатыРасширенныйКлиентСервер.НовыйОбходИдентификаторовТаблицы(СтрокиТаблицыДокумента);
			
			Если ТаблицыРасчетаФизическогоЛица <> Неопределено
				И ТаблицыРасчетаФизическогоЛица.Свойство(ИмяТаблицы, СтрокиТаблицыРасчета) Тогда
				
				Для Каждого СтрокаИсточника Из СтрокиТаблицыРасчета Цикл
					СтрокаФормы = РасчетЗарплатыРасширенныйКлиентСервер.СтрокаТаблицыПоНачальнойПозиции(Объект[ИмяТаблицы], ОбходТаблицыИдентификаторов);
					ЗаполнитьЗначенияСвойств(СтрокаФормы, СтрокаИсточника);
				КонецЦикла;
			КонецЕсли;
			
			РасчетЗарплатыРасширенныйКлиентСервер.УдалитьНеактуальныеСтрокиФормы(Объект[ИмяТаблицы], ОбходТаблицыИдентификаторов);
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Функция СодержимоеСотрудниковДокумента(ФизическиеЛицаСотрудников, Сотрудники, ТипВедущегоПоля)
	
	СтруктураТаблицДокумента = НовыйСтруктураТаблицДокумента();
	СодержимоеДокумента = СтруктураТаблицДокумента.СодержимоеДокумента;
	ШаблонСтрокТаблиц = СтруктураТаблицДокумента.ШаблонСтрокТаблиц;
	
	Для Каждого ФизическоеЛицоСотрудника Из ФизическиеЛицаСотрудников Цикл
		УдалитьПечатныеФормыСотрудника(ФизическоеЛицоСотрудника);
		
		СтрокиТаблицПоФизическомуЛицу = СтрокиТаблицДокументаПоФизическимЛицам.Получить(ФизическоеЛицоСотрудника);
		Если СтрокиТаблицПоФизическомуЛицу = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого КлючИЗначение Из СтрокиТаблицПоФизическомуЛицу Цикл
			ИмяТаблицыФизическогоЛица = КлючИЗначение.Ключ;
			
			Идентификаторы = КлючИЗначение.Значение;
			ТабличнаяЧасть = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(ЭтотОбъект, "Объект." + ИмяТаблицыФизическогоЛица);
			
			Для Каждого Идентификатор Из Идентификаторы Цикл
				НоваяСтрока = НоваяСтрокаТаблицыСодержимогоДокумента(ТабличнаяЧасть, Идентификатор, СтруктураТаблицДокумента, ИмяТаблицыФизическогоЛица);
				Если НоваяСтрока = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				Если НоваяСтрока.Свойство("Сотрудник")
					И ЗначениеЗаполнено(НоваяСтрока.Сотрудник)
					И ТипЗнч(НоваяСтрока.Сотрудник) = Тип("СправочникСсылка.Сотрудники") Тогда
						
					Сотрудники.Добавить(НоваяСтрока.Сотрудник);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	Если ТипВедущегоПоля = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		ДополнитьСотрудниковКРасчету(Сотрудники, ФизическиеЛицаСотрудников, Объект.Организация);
	КонецЕсли;
	
	Сотрудники = ОбщегоНазначенияКлиентСервер.СвернутьМассив(Сотрудники);
	
	Возврат СодержимоеДокумента;
	
КонецФункции

&НаСервереБезКонтекста
Функция СодержимоеДокументаПослеРасчета(АдресХранилища, ОписаниеТаблиц, ОписанияТаблицСРаспределениемПоТерриториямУсловиямТруда, МаксимальныйИдентификаторСтрокиНДФЛ)
	
	СтруктураДанных = ПолучитьИзВременногоХранилища(АдресХранилища);
	Если ТипЗнч(СтруктураДанных) <> Тип("Структура") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДанныеДляЗаполнения = Неопределено;
	Если Не СтруктураДанных.Свойство("ДанныеДляПерезаполненияТаблицДокумента", ДанныеДляЗаполнения) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СтруктураПараметров = СтруктураДанных.СтруктураПараметров;
	
	СодержимоеДокумента = РасчетЗарплатыРасширенный.НовыйСодержимоеДокументаРасчетаЗарплаты();
	СодержимоеДокумента.Вставить("Ссылка", СтруктураПараметров.ДокументСсылка);
	СодержимоеДокумента.Вставить("Сотрудники", СтруктураПараметров.Сотрудники);
	СодержимоеДокумента.Вставить("Организация", СтруктураПараметров.Организация);
	СодержимоеДокумента.Вставить("МесяцНачисления", СтруктураПараметров.МесяцНачисления);
	СодержимоеДокумента.Вставить("РежимДоначисления", СтруктураПараметров.ОписаниеДокумента.РежимДоначисления);
	
	СодержимоеФормыДляЗаполнения = Новый Структура("Объект", СодержимоеДокумента);
	Сотрудники = СтруктураПараметров.Сотрудники;
	
	ДанныеДляЗаполненияВДанныеФормы(ДанныеДляЗаполнения, СодержимоеДокумента,, Сотрудники);
	ЗаполнитьВторичныеДанныеТабличныхЧастейНДФЛ(СодержимоеФормыДляЗаполнения, МаксимальныйИдентификаторСтрокиНДФЛ);
	ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДатеВТабличнойЧасти(СодержимоеДокумента.Взносы, "ДатаПолученияДохода", "ДатаПолученияДоходаСтрокой");
	ЗарплатаКадрыРасширенный.ПериодНачисленияЗаполнитьИспользованиеПериода(СодержимоеФормыДляЗаполнения, ОписаниеТаблиц);
	ЗарплатаКадрыРасширенный.ЗаполнитьДоступностьМестаПолученияДохода(СодержимоеФормыДляЗаполнения, ОписаниеТаблиц);
	ЗарплатаКадрыРасширенный.ВводНачисленийЗаполнитьВторичныеДанныеПоказателей(СодержимоеФормыДляЗаполнения, ОписаниеТаблиц);
	РасчетЗарплатыРасширенныйФормы.ЗаполнитьПредставлениеРаспределенияПоТерриториямУсловиямТруда(СодержимоеФормыДляЗаполнения, СтруктураПараметров.Организация, ОписанияТаблицСРаспределениемПоТерриториямУсловиямТруда);
	
	СтрокиТаблицПоФизическимЛицам = СтрокиТаблицПоФизическимЛицам(СодержимоеДокумента, Ложь);
	
	Если ЗначениеЗаполнено(СтруктураПараметров.ФизическиеЛица) Тогда
		ФизическиеЛица = СтруктураПараметров.ФизическиеЛица;
	Иначе
		ФизическиеЛица = ОбщегоНазначения.ВыгрузитьКолонку(СтрокиТаблицПоФизическимЛицам, "Ключ");	
	КонецЕсли;
		
	Результат = Новый Структура;
	Результат.Вставить("СтрокиТаблицРасчетаПоФизическимЛицам", СтрокиТаблицПоФизическимЛицам);
	Результат.Вставить("ФизическиеЛица", ФизическиеЛица);
		
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ЗаполнитьВторичныеДанныеТабличныхЧастейНДФЛ(СодержимоеФормыДляЗаполнения, МаксимальныйИдентификаторСтрокиНДФЛ)
	
	Объект = СодержимоеФормыДляЗаполнения.Объект;
	СоответствиеКодовВычетовКодамДоходов = Новый ФиксированноеСоответствие(УчетНДФЛ.ВычетыКДоходам(Год(Объект.МесяцНачисления)));
	Начисления = ОбщегоНазначения.ВыгрузитьКолонку(Объект.Начисления, "Начисление", Истина);
	
	УстановитьПривилегированныйРежим(Истина);
	КодыДоходов = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(Начисления, "КодДоходаНДФЛ");
	УстановитьПривилегированныйРежим(Ложь);
	
	Для каждого СтрокаНачисления Из Объект.Начисления Цикл
		Если ЗначениеЗаполнено(СтрокаНачисления.Начисление)
			И ТипЗнч(СтрокаНачисления.Начисление) = Тип("ПланВидовРасчетаСсылка.Начисления") Тогда
			
			СтрокаНачисления.ВычетПримененныйКДоходам = СоответствиеКодовВычетовКодамДоходов.Получить(КодыДоходов.Получить(СтрокаНачисления.Начисление)) <> Неопределено;
		КонецЕсли; 
	КонецЦикла;
	
	СоответствиеНачисленийРеквизитам = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(Объект.Начисления.ВыгрузитьКолонку("Начисление"),
																					"КатегорияДохода");
		
	ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДатеВТабличнойЧасти(Объект.ПримененныеВычетыНаДетейИИмущественные, "МесяцПериодаПредоставленияВычета", "МесяцПериодаПредоставленияВычетаСтрокой");
	
	Для каждого СтрокаНДФЛ Из Объект.НДФЛ Цикл
		УчетНДФЛФормы.ЗаполнитьПредставленияВычетовСтрокиНДФЛ(СодержимоеФормыДляЗаполнения, СтрокаНДФЛ, ОписаниеПанелиВычеты(),СоответствиеНачисленийРеквизитам);
	КонецЦикла; 
	
	НазначитьНовыеИдентификаторыСтрокамНДФЛ(Объект.НДФЛ, Объект.ПримененныеВычетыНаДетейИИмущественные, МаксимальныйИдентификаторСтрокиНДФЛ);
	
КонецПроцедуры

&НаСервере
Процедура ПроведениеПослеВыполненияДлительнойОперации()
	
	Модифицированность = Ложь;
	Если Не ЗакрыватьПослеЗаписи Тогда
		ДокументОбъект = ЗарплатаКадры.ДесериализоватьОбъектИзДвоичныхДанных(ПолучитьИзВременногоХранилища(АдресХранилища));
		ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
		ПриПолученииДанныхНаСервере(ДокументОбъект, Ложь);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОтложенноеПроведениеДокументов") Тогда 
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОтражениеДокументовВУчетеСтраховыхВзносов");
		Модуль.ОтразитьДокументыВУчетеСтраховыхВзносовВДлительнойОперации(Объект.Организация, Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаданиеВыполнено(ИдентификаторЗадания)
	
	Возврат ДлительныеОперации.ЗаданиеВыполнено(ИдентификаторЗадания);
	
КонецФункции

&НаСервереБезКонтекста
Функция СообщенияФоновогоЗадания(ИдентификаторЗадания)
	Возврат ДлительныеОперации.СообщенияПользователю(Истина, ИдентификаторЗадания);
КонецФункции

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеЗадания()
	
	ЗаданиеВыполненоВДлительнойОперации = Ложь;
	
	Попытка
		Если ФормаДлительнойОперации.Открыта() 
			И ФормаДлительнойОперации.ИдентификаторЗадания = ИдентификаторЗадания Тогда
			Если ЗаданиеВыполнено(ИдентификаторЗадания) Тогда
				Если ПроведениеВДлительнойОперации Тогда
					ПроведениеПослеВыполненияДлительнойОперации();
					Если ЗначениеЗаполнено(УИДЗамера) Тогда
						ОценкаПроизводительностиКлиент.ЗавершитьЗамерВремени(УИДЗамера);
						УИДЗамера = Неопределено;
					КонецЕсли;
				Иначе
					РасчетЗарплатыРасширенныйКлиент.ОчиститьСписокСотрудниковКРасчету(ЭтаФорма);
					Если ПересчетСотрудникаВДлительнойОперации Тогда
						ЗаполнениеПослеВыполненияДлительнойОперацииНаКлиенте();
					Иначе
						ЗаполнениеПослеВыполненияДлительнойОперации();
					КонецЕсли;
					Если ЗначениеЗаполнено(УИДЗамера) Тогда
						ОценкаПроизводительностиКлиент.ЗавершитьЗамерВремени(УИДЗамера);
						УИДЗамера = Неопределено;
					КонецЕсли;
				КонецЕсли;
				ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
				ЗаданиеВыполненоВДлительнойОперации = Истина;
				ОповеститьОбИзмененииОбъекта();
			Иначе
				ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
				ПодключитьОбработчикОжидания(
					"Подключаемый_ПроверитьВыполнениеЗадания",
					ПараметрыОбработчикаОжидания.ТекущийИнтервал,
					Истина);
			КонецЕсли;
		ИначеЕсли Не ФормаДлительнойОперации.Открыта() И ПроведениеВДлительнойОперации Тогда
			ПроведениеВДлительнойОперации = Ложь;
			ЗакрыватьПослеЗаписи = Ложь;
		КонецЕсли;
	Исключение
		ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
		
		СообщенияПользователю = СообщенияФоновогоЗадания(ИдентификаторЗадания);
		Если СообщенияПользователю <> Неопределено Тогда
			Для каждого СообщениеФоновогоЗадания Из СообщенияПользователю Цикл
				СообщениеФоновогоЗадания.Сообщить();
			КонецЦикла;
			Если СообщенияПользователю.Количество() = 0 Тогда
				ВызватьИсключение КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			КонецЕсли;
		КонецЕсли;
		
		Если ПроведениеВДлительнойОперации Тогда
			ВызватьИсключение НСтр("ru = 'Не удалось провести документ';
									|en = 'Cannot post the document'");
		КонецЕсли;
	КонецПопытки;
	
	Если ПроведениеВДлительнойОперации И ЗаданиеВыполненоВДлительнойОперации Тогда
		Если ЗакрыватьПослеЗаписи Тогда
			ПодключитьОбработчикОжидания("ЗакрытьФорму", 0.1, Истина);
		КонецЕсли;
		ПроведениеВДлительнойОперации = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РаспределитьЗависимыеРезультаты(ПараметрыДляРаспределения) Экспорт
	
	Если ПараметрыДляРаспределения.РаспределятьЗависимыеТаблицы Тогда
		ОбновитьБухучетРезультатовРасчетаНаСервере(ПараметрыДляРаспределения);
	КонецЕсли;    
	
КонецПроцедуры

&НаСервере
Функция АдресСпискаПодобранныхСотрудников()
	
	МассивСотрудников = ОбщегоНазначения.ВыгрузитьКолонку(Объект.Начисления, "Сотрудник", Истина);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивСотрудников, ОбщегоНазначения.ВыгрузитьКолонку(Объект.НачисленияПерерасчет, "Сотрудник", Истина));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивСотрудников, ОбщегоНазначения.ВыгрузитьКолонку(Объект.Пособия, "Сотрудник", Истина));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивСотрудников, ОбщегоНазначения.ВыгрузитьКолонку(Объект.НачисленияПоДоговорам, "Сотрудник", Истина));
	
	Возврат ПоместитьВоВременноеХранилище(МассивСотрудников, УникальныйИдентификатор);
	
КонецФункции

&НаКлиенте
Процедура ОбработкаПодбораНаКлиенте(Сотрудники)
	
	МассивСотрудников = ПодобранныеСотрудники(Сотрудники);
	
	Если МассивСотрудников.Количество() = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	Результат = ПерезаполнитьДанныеФормыНаСервере(МассивСотрудников, Ложь);
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.ЗаданиеВыполнено Тогда
		РасчетЗарплатыРасширенныйКлиент.ОчиститьСписокСотрудниковКРасчету(ЭтаФорма, Сотрудники);
 		ОповеститьОбИзмененииОбъекта();
		УстановитьЗначенияКонтролируемыхПолей();
	Иначе
		ИдентификаторЗадания = Результат.ИдентификаторЗадания;
		АдресХранилища		 = Результат.АдресХранилища;
		
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", 1, Истина);
		ФормаДлительнойОперации = ДлительныеОперацииКлиент.ОткрытьФормуДлительнойОперации(ЭтаФорма, ИдентификаторЗадания);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПодобранныеСотрудники(Сотрудники)
	
	// Оставляем только сотрудников, которых нет в документе.
	МассивСотрудников = Новый Массив;
	
	ИменаТаблиц = Новый Массив;
	ИменаТаблиц.Добавить("Начисления");
	ИменаТаблиц.Добавить("НачисленияПерерасчет");
	ИменаТаблиц.Добавить("НачисленияПоДоговорам");
	ИменаТаблиц.Добавить("Пособия");
	ИменаТаблиц.Добавить("ПособияПерерасчет");
	ИменаТаблиц.Добавить("Льготы");
	
	УникальныеСотрудники = Новый Соответствие;
	СписокСотрудниковДокумента = Новый Массив;
	
	Для Каждого ИмяТаблицы Из ИменаТаблиц Цикл
		Для Каждого СтрокаСотрудника Из Объект[ИмяТаблицы] Цикл 
			Если УникальныеСотрудники[СтрокаСотрудника.Сотрудник] = Неопределено Тогда 
				СписокСотрудниковДокумента.Добавить(СтрокаСотрудника.Сотрудник);
				УникальныеСотрудники.Вставить(СтрокаСотрудника.Сотрудник, Истина);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Для Каждого Сотрудник Из Сотрудники Цикл
		Если УникальныеСотрудники[Сотрудник] = Неопределено Тогда 
			МассивСотрудников.Добавить(Сотрудник);
		КонецЕсли;
	КонецЦикла;
	
	// Для корректного пересчета зависимых таблиц добавляем сотрудников из документа с совпадающими физическими лицами.
	ФизическиеЛицаПодобранныхСотрудников = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(МассивСотрудников, "ФизическоеЛицо");
	
	ФизическиеЛица = Новый Соответствие;
	Для Каждого КлючИЗначение Из ФизическиеЛицаПодобранныхСотрудников Цикл 
		ФизическиеЛица.Вставить(КлючИЗначение.Значение, Истина);
	КонецЦикла;
	
	ФизическиеЛицаСотрудниковДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(СписокСотрудниковДокумента, "ФизическоеЛицо");
	
	Для Каждого КлючИЗначение Из ФизическиеЛицаСотрудниковДокумента Цикл 
		Если ФизическиеЛица[КлючИЗначение.Значение] <> Неопределено Тогда 
			МассивСотрудников.Добавить(КлючИЗначение.Ключ);
		КонецЕсли;
	КонецЦикла;
	
	Возврат МассивСотрудников;
	
КонецФункции

&НаСервере
Функция ОтсутствующиеВДокументеСотрудники()
	
	ОписаниеДокумента = ОписаниеДокумента(ЭтотОбъект);
	
	ЗаполнятьНачисленияПоДоговорамПодряда = Не ОписаниеДокумента.РежимДоначисления 
		И (ОписаниеДокумента.НачисленияПоДоговорамИмя <> Неопределено
		Или ОписаниеДокумента.УдержанияИмя <> Неопределено Или ОписаниеДокумента.НДФЛИмя <> Неопределено
		Или ОписаниеДокумента.ПогашениеЗаймовИмя <> Неопределено Или ОписаниеДокумента.ВзносыИмя <> Неопределено);
	
	ДополнительныеПараметры = РасчетЗарплатыРасширенный.ДополнительныеПараметрыСотрудниковДляНачисленияЗарплаты();
	ДополнительныеПараметры.ОкончаниеПериода = КонецМесяца(Объект.МесяцНачисления);
	ДополнительныеПараметры.ЗаполнятьНачисленияПоДоговорамПодряда = ЗаполнятьНачисленияПоДоговорамПодряда;
	ДополнительныеПараметры.ИспользоватьВоеннуюСлужбу = ИспользоватьВоеннуюСлужбу;
	ДополнительныеПараметры.НачислениеЗарплатыВоеннослужащим = Объект.НачислениеЗарплатыВоеннослужащим;
	
	Сотрудники = РасчетЗарплатыРасширенный.СотрудникиДляНачисленияЗарплаты(Объект.Организация, Объект.Подразделение, Объект.МесяцНачисления, ДополнительныеПараметры);
		
	Возврат ПодобранныеСотрудники(Сотрудники);
	
КонецФункции

// Описания документа, таблиц документа, панелей документа.
&НаКлиентеНаСервереБезКонтекста
Функция ОписаниеДокумента(Форма)
	
	РежимДоначисления = Форма.Объект.РежимДоначисления;
	
	Описание = РасчетЗарплатыРасширенныйКлиентСервер.ОписаниеРасчетногоДокумента();
	Описание.РежимДоначисления = РежимДоначисления;
	Описание.РежимНачисления = ПредопределенноеЗначение("Перечисление.РежимНачисленияЗарплаты.ОкончательныйРасчет");
	Описание.ЭтоНачислениеЗарплаты = Истина;
	
	Если РежимДоначисления Тогда
		
		Описание.НачисленияПерерасчетИмя = "НачисленияПерерасчет";
		Описание.НачисленияПерерасчетКоманднаяПанельИмя = "НачисленияПерерасчетАвтоКоманды";
		Описание.ОписанияТаблиц.Вставить("НачисленияПерерасчет", ОписаниеТаблицыПерерасчетов(Форма));
		
		Описание.ПособияПерерасчетИмя = "ПособияПерерасчет";
		Описание.ПособияПерерасчетКоманднаяПанельИмя = "ПособияПерерасчетАвтоКоманды";
		Описание.ОписанияТаблиц.Вставить("ПособияПерерасчет", ОписаниеТаблицыПособияПерерасчет(Форма));
		
		Описание.ЛьготыПерерасчетИмя = "ЛьготыПерерасчет";
		Описание.ЛьготыПерерасчетКоманднаяПанельИмя = "ЛьготыПерерасчетАвтоКоманды";
		Описание.ОписанияТаблиц.Вставить("ЛьготыПерерасчет", ОписаниеТаблицыЛьготыПерерасчет(Форма));
		
		Описание.УдержанияПерерасчетИмя = "УдержанияПерерасчет";
		Описание.УдержанияПерерасчетКоманднаяПанельИмя = "УдержанияПерерасчетАвтоКоманды";
		Описание.ОписанияТаблиц.Вставить("УдержанияПерерасчет", ОписаниеТаблицыУдержанияПерерасчет());
		
		Описание.НДФЛИмя = "НДФЛ";
		Описание.НДФЛКоманднаяПанельИмя = "НДФЛАвтоКоманды";
		Описание.ПримененныеВычетыИмя = "ПримененныеВычетыНаДетейИИмущественные";
		Описание.УчтенныеПриРасчетеНДФЛДокументыИмя = "УчтенныеПриРасчетеНДФЛДокументы";
		Описание.ОписанияТаблиц.Вставить("НДФЛ", ОписаниеТаблицыНДФЛ());
		
		Описание.КорректировкиВыплатыИмя = "КорректировкиВыплаты";
		Описание.ОписанияТаблиц.Вставить("КорректировкиВыплаты", ОписаниеТаблицыКорректировкиВыплаты());
		
	Иначе
		
		Если Форма.ИспользоватьНачисление Тогда 
			Описание.НачисленияИмя = "Начисления";
			Описание.НачисленияКоманднаяПанельИмя = "НачисленияАвтоКоманды";
			Описание.НачисленияПерерасчетИмя = "НачисленияПерерасчет";
			Описание.НачисленияПерерасчетКоманднаяПанельИмя = "НачисленияПерерасчетАвтоКоманды";
			Описание.ОписанияТаблиц.Вставить("Начисления", ОписаниеТаблицыНачислений(Форма));
			Описание.ОписанияТаблиц.Вставить("НачисленияПерерасчет", ОписаниеТаблицыПерерасчетов(Форма));
		КонецЕсли;
		
		Если Форма.ИспользоватьДоговоры Тогда 
			Описание.НачисленияПоДоговорамИмя = "НачисленияПоДоговорам";
			Описание.НачисленияПоДоговорамКоманднаяПанельИмя = "НачисленияПоДоговорамАвтоКоманды";
			Описание.ОписанияТаблиц.Вставить("НачисленияПоДоговорам", ОписаниеТаблицыНачисленийПоДоговорам(Форма));
		КонецЕсли;
		
		Если Форма.ИспользоватьПособия Тогда 
			Описание.ПособияИмя = "Пособия";
			Описание.ПособияКоманднаяПанельИмя = "ПособияАвтоКоманды";
			Описание.ПособияПерерасчетИмя = "ПособияПерерасчет";
			Описание.ПособияПерерасчетКоманднаяПанельИмя = "ПособияПерерасчетАвтоКоманды";
			Описание.ОписанияТаблиц.Вставить("Пособия", ОписаниеТаблицыПособия(Форма));
			Описание.ОписанияТаблиц.Вставить("ПособияПерерасчет", ОписаниеТаблицыПособияПерерасчет(Форма));
		КонецЕсли;
		
		Если Форма.ИспользоватьЛьготы Тогда 
			Описание.ЛьготыИмя = "Льготы";
			Описание.ЛьготыКоманднаяПанельИмя = "ЛьготыАвтоКоманды";
			Описание.ЛьготыПерерасчетИмя = "ЛьготыПерерасчет";
			Описание.ЛьготыПерерасчетКоманднаяПанельИмя = "ЛьготыПерерасчетАвтоКоманды";
			Описание.ОписанияТаблиц.Вставить("Льготы", ОписаниеТаблицыЛьгот(Форма));
			Описание.ОписанияТаблиц.Вставить("ЛьготыПерерасчет", ОписаниеТаблицыЛьготыПерерасчет(Форма));
		КонецЕсли;
		
		Если Форма.ИспользоватьУдержания Тогда 
			Описание.УдержанияИмя = "Удержания";
			Описание.УдержанияКоманднаяПанельИмя = "УдержанияАвтоКоманды";
			Описание.УдержанияПерерасчетИмя = "УдержанияПерерасчет";
			Описание.УдержанияПерерасчетКоманднаяПанельИмя = "УдержанияПерерасчетАвтоКоманды";
			Описание.ОписанияТаблиц.Вставить("Удержания", ОписаниеТаблицыУдержаний());
			Описание.ОписанияТаблиц.Вставить("УдержанияПерерасчет", ОписаниеТаблицыУдержанияПерерасчет());
		КонецЕсли;
		
		Если Форма.ИспользоватьНДФЛ Тогда 
			Описание.НДФЛИмя = "НДФЛ";
			Описание.НДФЛКоманднаяПанельИмя = "НДФЛАвтоКоманды";
			Описание.ПримененныеВычетыИмя = "ПримененныеВычетыНаДетейИИмущественные";
			Описание.УчтенныеПриРасчетеНДФЛДокументыИмя = "УчтенныеПриРасчетеНДФЛДокументы";
			Описание.КорректировкиВыплатыИмя = "КорректировкиВыплаты";
			Описание.ОписанияТаблиц.Вставить("НДФЛ", ОписаниеТаблицыНДФЛ());
			Описание.ОписанияТаблиц.Вставить("КорректировкиВыплаты", ОписаниеТаблицыКорректировкиВыплаты());
		КонецЕсли;
		
		Если Форма.ИспользоватьЗаймы Тогда 
			Описание.ПогашениеЗаймовИмя = "ПогашениеЗаймов";
			Описание.ОписанияТаблиц.Вставить("ПогашениеЗаймов", ОписаниеТаблицыПогашениеЗаймов());
		КонецЕсли;
		
		Если Форма.ИспользоватьВзносы Тогда 
			Описание.ВзносыИмя = "Взносы";
		КонецЕсли;
		
		Если Не ТребуетсяЗаполнитьДатуВыплаты(Форма.Объект.МесяцНачисления, РежимДоначисления) Тогда
			Описание.ПланируемаяДатаВыплатыИмя = Неопределено;
			Описание.ПорядокВыплатыИмя = Неопределено;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ОбщегоНазначенияБЗККлиентСервер.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.УправленческаяЗарплата") Тогда
		Модуль = ОбщегоНазначенияБЗККлиентСервер.ОбщийМодуль("УправленческаяЗарплатаКлиентСервер");
		Модуль.ДополнитьОписаниеРасчетногоДокумента(Описание, РежимДоначисления, Форма.ИспользоватьНачисление);
	КонецЕсли;
	
	Описание.ОбязательныеПоля.Добавить(РасчетЗарплатыРасширенныйКлиентСервер.ОписаниеОбязательногоПоляДокумента(НСтр("ru = 'Месяц начисления';
																													|en = 'Accrual month'"), "МесяцНачисленияСтрокой"));
	
	Описание.ОписанияТаблицДляРаспределенияРезультата = СтруктураОписанияТаблицДляРаспределенияРезультата(Форма);
	
	Возврат Описание;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ОписаниеПанелиВычеты()
	
	ОписаниеПанелиВычеты = УчетНДФЛКлиентСервер.ОписаниеПанелиВычеты();
	
	ОписаниеТабличнойЧастиНДФЛ = УчетНДФЛКлиентСервер.ОписаниеТабличнойЧастиНДФЛ();
	ОписаниеТабличнойЧастиНДФЛ.ИспользуетсяФиксРасчет = Истина;
	ОписаниеТабличнойЧастиНДФЛ.ИмяПоляПериод = "";
	ОписаниеПанелиВычеты.ТабличнаяЧастьНДФЛ = ОписаниеТабличнойЧастиНДФЛ;
	
	РасчетЗарплатыКлиентСервер.ДополнитьОписаниеПанелиВычеты(ОписаниеПанелиВычеты);
	ОписаниеТабличнойЧастиКорректировкиВыплаты = РасчетЗарплатыКлиентСервер.ОписаниеТабличнойЧастиКорректировкиВыплаты();
	ОписаниеТабличнойЧастиКорректировкиВыплаты.ИспользуетсяФиксРасчет = Истина;
	ОписаниеПанелиВычеты.ТабличнаяЧастьКорректировкиВыплаты = ОписаниеТабличнойЧастиКорректировкиВыплаты;
	
	НастраиваемыеПанели = Новый Соответствие;
	НастраиваемыеПанели.Вставить("ВычетыЛичные", 				Истина);
	НастраиваемыеПанели.Вставить("ВычетыНаДетейИИмущественные", "Объект.ПримененныеВычетыНаДетейИИмущественные");
	НастраиваемыеПанели.Вставить("ВычетыКДоходам", 				"Объект.Начисления");
	
	ОписаниеПанелиВычеты.НастраиваемыеПанели = НастраиваемыеПанели;
	
	ОписаниеПанелиВычеты.ИмяКолонкиПримененныеВычеты = "ПримененныеВычеты";
	
	Возврат ОписаниеПанелиВычеты;
	
КонецФункции

&НаКлиенте
Функция ОписаниеПанелиВычетыНаКлиенте() Экспорт
	Возврат ОписаниеПанелиВычеты();
КонецФункции

&НаСервере
Функция ОписаниеПанелиВычетыНаСервере() Экспорт
	Возврат ОписаниеПанелиВычеты();
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ОписаниеТаблицыНачислений(Форма)
	
	Описание = РасчетЗарплатыРасширенныйКлиентСервер.ОписаниеТаблицыРасчета();
	Описание.СодержитПолеСотрудник = Истина;
	Описание.ИмяРеквизитаСотрудник = "Сотрудник";
	Описание.ИмяПоляДляВставкиПоказателей = "ДатыНачисления";
	Описание.СодержитПолеКодВычета = Истина;
	Описание.ИмяРеквизитаДокументОснование = "ДокументОснование";
	
	Описание.ИмяПоляДляВставкиРаспределенияРезультатов 	= "НачисленияРезультат";
	Описание.ВставлятьПослеПоля 						= Истина;
	
	Описание.ОтображатьПоляОписанияВремени = Истина;
	Описание.ОтображатьПоляНормыВремени = Истина;
	Описание.УправлятьОтображениемПолейОписанияВремени = Истина;
	
	Описание.СодержитПолеИсходныйДокумент = Истина;
	
	СтрокаЗависимыеТаблицы = "";
	Если Форма.ИспользоватьНачисление Тогда
		СтрокаЗависимыеТаблицы = "Начисления";
	КонецЕсли;
	Если Форма.ИспользоватьПособия Тогда
		СтрокаЗависимыеТаблицы = СтрокаЗависимыеТаблицы + ?(ПустаяСтрока(СтрокаЗависимыеТаблицы),"",",") + "Пособия";
	КонецЕсли;
	Если Форма.ИспользоватьНДФЛ Тогда
		СтрокаЗависимыеТаблицы = СтрокаЗависимыеТаблицы + ?(ПустаяСтрока(СтрокаЗависимыеТаблицы),"",",") + "НДФЛ,КорректировкиВыплаты";
	КонецЕсли;
	Если Форма.ИспользоватьУдержания Тогда
		СтрокаЗависимыеТаблицы = СтрокаЗависимыеТаблицы + ?(ПустаяСтрока(СтрокаЗависимыеТаблицы),"",",") + "Удержания";	
	КонецЕсли;
	Если Форма.ИспользоватьЗаймы Тогда
		СтрокаЗависимыеТаблицы = СтрокаЗависимыеТаблицы + ?(ПустаяСтрока(СтрокаЗависимыеТаблицы),"",",") + "ПогашениеЗаймов";	
	КонецЕсли;
	Описание.РаспределениеРезультатовЗависимыеТаблицы = СтрокаЗависимыеТаблицы;
	
	Описание.ПутьКДаннымАдресРаспределенияРезультатовВХранилище = "АдресТаблицыРаспределенияНачислений";
	Описание.ПутьКДаннымАдресРаспределенияПоТерриториямУсловиямТруда = "АдресТаблицыРаспределенияПоТерриториямУсловиямТруда";
	
	Описание.ОтменятьВсеИсправления	= Истина;
	
	Описание.СодержитПолеМестоПолученияДохода = Истина;
	
	Возврат Описание;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ОписаниеТаблицыПерерасчетов(Форма)
	
	ОписаниеТаблицы = РасчетЗарплатыРасширенныйКлиентСервер.ОписаниеТаблицыРасчета();
	
	ОписаниеТаблицы.СодержитПолеСотрудник = Истина;
	ОписаниеТаблицы.ИмяРеквизитаСотрудник = "Сотрудник";
	
	ОписаниеТаблицы.ИмяТаблицы									= "НачисленияПерерасчет";
	ОписаниеТаблицы.ПутьКДанным									= "Объект.НачисленияПерерасчет";
	
	ОписаниеТаблицы.ИмяПоляДляВставкиПоказателей				= "ДатыНачисленияПерерасчет";
	ОписаниеТаблицы.СодержитПолеКодВычета = Истина;
	ОписаниеТаблицы.ИмяРеквизитаДокументОснование 				= "ДокументОснование";
	
	ОписаниеТаблицы.НомерТаблицы 								= 6;
	
	ОписаниеТаблицы.ОтображатьПоляОписанияВремени				= Истина;
	ОписаниеТаблицы.ОтображатьПоляНормыВремени 					= Истина;
	ОписаниеТаблицы.УправлятьОтображениемПолейОписанияВремени	= Истина;
	
	ОписаниеТаблицы.ЭтоПерерасчеты								= Истина;
	ОписаниеТаблицы.ИмяРеквизитаФиксСторно 						= "ФиксСторно";
	
	ОписаниеТаблицы.ИмяПоляДляВставкиРаспределенияРезультатов 	= "НачисленияПерерасчетРезультат";
	ОписаниеТаблицы.ВставлятьПослеПоля 							= Истина;
	
	СтрокаЗависимыеТаблицы = "НачисленияПерерасчет";
	Если Форма.ИспользоватьНДФЛ Тогда
		СтрокаЗависимыеТаблицы = СтрокаЗависимыеТаблицы + ",НДФЛ,КорректировкиВыплаты";
	КонецЕсли;
	Если Форма.ИспользоватьУдержания Тогда
		СтрокаЗависимыеТаблицы = СтрокаЗависимыеТаблицы + ",Удержания";	
	КонецЕсли;
	ОписаниеТаблицы.РаспределениеРезультатовЗависимыеТаблицы = СтрокаЗависимыеТаблицы;
	
	ОписаниеТаблицы.ПутьКДаннымАдресРаспределенияРезультатовВХранилище = "АдресТаблицыРаспределенияНачислений";
	ОписаниеТаблицы.ПутьКДаннымАдресРаспределенияПоТерриториямУсловиямТруда = "АдресТаблицыРаспределенияПоТерриториямУсловиямТруда";
	
	ОписаниеТаблицы.СодержитПолеМестоПолученияДохода = Истина;
	ОписаниеТаблицы.СодержитРегистраторРазовогоНачисления = Истина;
	ОписаниеТаблицы.СодержитПолеИсходныйДокумент = Истина;
	
	Возврат ОписаниеТаблицы;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ОписаниеТаблицыНачисленийПоДоговорам(Форма)
	
	Описание = РасчетЗарплатыРасширенныйКлиентСервер.ОписаниеТаблицыРасчета();
	Описание.ИмяТаблицы = "НачисленияПоДоговорам";
	Описание.ИмяРеквизитаВидРасчета = "";
	Описание.СодержитПолеСотрудник = Истина;
	Описание.СодержитПолеВидРасчета = Ложь;
    Описание.ИмяРеквизитаСотрудник = "Сотрудник";
	Описание.НомерТаблицы = 1;
	Описание.ПутьКДанным = "Объект.НачисленияПоДоговорам";
	Описание.ПроверяемыеРеквизиты = "Сотрудник,ДокументОснование,КодДохода,ДатаНачала,ДатаОкончания";
	Описание.ИмяРеквизитаИдентификаторСтроки = "ИдентификаторСтроки";
	Описание.УстанавливатьИдентификаторСтрокиРаспределенияРезультата = Ложь;
	
	СтрокаЗависимыеТаблицы = "";
	Если Форма.ИспользоватьНДФЛ Тогда
		СтрокаЗависимыеТаблицы = "НДФЛ,КорректировкиВыплаты";
	КонецЕсли;
	Если Форма.ИспользоватьУдержания Тогда
		СтрокаЗависимыеТаблицы = СтрокаЗависимыеТаблицы + ?(ПустаяСтрока(СтрокаЗависимыеТаблицы),"",",") + "Удержания";	
	КонецЕсли;
	Если Форма.ИспользоватьЗаймы Тогда
		СтрокаЗависимыеТаблицы = СтрокаЗависимыеТаблицы + ?(ПустаяСтрока(СтрокаЗависимыеТаблицы),"",",") + "ПогашениеЗаймов";	
	КонецЕсли;
	Описание.РаспределениеРезультатовЗависимыеТаблицы = СтрокаЗависимыеТаблицы;
	
	Описание.ПутьКДаннымАдресРаспределенияРезультатовВХранилище = "АдресТаблицыРаспределенияНачислений";
	Описание.ПутьКДаннымАдресРаспределенияПоТерриториямУсловиямТруда = "АдресТаблицыРаспределенияПоТерриториямУсловиямТруда";
	Описание.ПоказыватьУсловияТруда = Ложь;
	
	Описание.ИмяПоляДляВставкиРаспределенияРезультатов 	= "НачисленияПоДоговорамРезультат";
	Описание.ВставлятьПослеПоля 						= Истина;
	
	Описание.ОтменятьВсеИсправления	= Истина;
	
	Описание.СодержитПолеМестоПолученияДохода = Истина;
	
	Возврат Описание;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ОписаниеТаблицыПособия(Форма)
	
	Описание = РасчетЗарплатыРасширенныйКлиентСервер.ОписаниеТаблицыРасчета();
	Описание.СодержитПолеСотрудник = Истина;
	Описание.ИмяРеквизитаСотрудник = "Сотрудник";
	Описание.ИмяПоляДляВставкиПоказателей = "ДатыПособия";
	Описание.ИмяТаблицы = "Пособия";
	Описание.ПутьКДанным = "Объект.Пособия";
	Описание.НомерТаблицы = 4;
	
	Описание.ИмяПоляДляВставкиРаспределенияРезультатов = "ПособияОплаченоДней";
	
	СтрокаЗависимыеТаблицы = "";
	Если Форма.ИспользоватьНДФЛ Тогда
		СтрокаЗависимыеТаблицы = "НДФЛ,КорректировкиВыплаты";
	КонецЕсли;
	Если Форма.ИспользоватьУдержания Тогда
		СтрокаЗависимыеТаблицы = СтрокаЗависимыеТаблицы + ?(ПустаяСтрока(СтрокаЗависимыеТаблицы),"",",") + "Удержания";	
	КонецЕсли;
	Если Форма.ИспользоватьЗаймы Тогда
		СтрокаЗависимыеТаблицы = СтрокаЗависимыеТаблицы + ?(ПустаяСтрока(СтрокаЗависимыеТаблицы),"",",") + "ПогашениеЗаймов";	
	КонецЕсли;
	Описание.РаспределениеРезультатовЗависимыеТаблицы = СтрокаЗависимыеТаблицы;
	
	Описание.ПутьКДаннымАдресРаспределенияРезультатовВХранилище = "АдресТаблицыРаспределенияНачислений";
	Описание.ПутьКДаннымАдресРаспределенияПоТерриториямУсловиямТруда = "АдресТаблицыРаспределенияПоТерриториямУсловиямТруда";
	
	Описание.ОтменятьВсеИсправления	= Истина;
	
	Возврат Описание;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ОписаниеТаблицыПособияПерерасчет(Форма)
	
	Описание = РасчетЗарплатыРасширенныйКлиентСервер.ОписаниеТаблицыРасчета();
	Описание.СодержитПолеСотрудник = Истина;
	Описание.ИмяРеквизитаСотрудник = "Сотрудник";
	Описание.ИмяПоляДляВставкиПоказателей = "ПерерасчетДатыПособия";
	Описание.ИмяТаблицы = "ПособияПерерасчет";
	Описание.ПутьКДанным = "Объект.ПособияПерерасчет";
	Описание.НомерТаблицы = 8;
	
	Описание.ИмяПоляДляВставкиРаспределенияРезультатов = "ПособияПерерасчетОплаченоДней";
	
	СтрокаЗависимыеТаблицы = "";
	Если Форма.ИспользоватьНДФЛ Тогда
		СтрокаЗависимыеТаблицы = "НДФЛ,КорректировкиВыплаты";
	КонецЕсли;
	Если Форма.ИспользоватьУдержания Тогда
		СтрокаЗависимыеТаблицы = СтрокаЗависимыеТаблицы + ?(ПустаяСтрока(СтрокаЗависимыеТаблицы),"",",") + "Удержания";	
	КонецЕсли;
	Если Форма.ИспользоватьЗаймы Тогда
		СтрокаЗависимыеТаблицы = СтрокаЗависимыеТаблицы + ?(ПустаяСтрока(СтрокаЗависимыеТаблицы),"",",") + "ПогашениеЗаймов";	
	КонецЕсли;
	Описание.РаспределениеРезультатовЗависимыеТаблицы = СтрокаЗависимыеТаблицы;
	
	Описание.ПутьКДаннымАдресРаспределенияРезультатовВХранилище = "АдресТаблицыРаспределенияНачислений";
	Описание.ПутьКДаннымАдресРаспределенияПоТерриториямУсловиямТруда = "АдресТаблицыРаспределенияПоТерриториямУсловиямТруда";
	
	Описание.ЭтоПерерасчеты			= Истина;
	Описание.ОтменятьВсеИсправления = Истина;
	
	Возврат Описание;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ОписаниеТаблицыУдержаний()
	
	Описание = РасчетЗарплатыРасширенныйКлиентСервер.ОписаниеТаблицыРасчета();
	Описание.ИмяРеквизитаВидРасчета = "Удержание";
	Описание.ИмяТаблицы = "Удержания";
	Описание.ПутьКДанным = "Объект.Удержания";
	Описание.ИмяПоляДляВставкиПоказателей = "УдержанияРезультат";
	Описание.НомерТаблицы = 2;
	Описание.СодержитПолеСотрудник = Истина;
	Описание.ИмяРеквизитаСотрудник = "ФизическоеЛицо";
	
	Описание.ПутьКДаннымРаспределениеРезультатов = "Объект.РаспределениеРезультатовУдержаний";
	Описание.ПутьКДаннымАдресРаспределенияРезультатовВХранилище = "АдресТаблицыРаспределенияУдержаний";
	Описание.ИмяПоляДляВставкиРаспределенияРезультатов = "УдержанияРезультат";
	Описание.ВставлятьПослеПоля 					   = Истина;
	
	Описание.ОтменятьВсеИсправления	= Истина;
	
	Возврат Описание;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ОписаниеТаблицыУдержанияПерерасчет()
	
	Описание = РасчетЗарплатыРасширенныйКлиентСервер.ОписаниеТаблицыРасчета();
	
	Описание.ИмяРеквизитаВидРасчета = "Удержание";
	Описание.ИмяТаблицы = "УдержанияПерерасчет";
	Описание.ПутьКДанным = "Объект.УдержанияПерерасчет";
	Описание.ИмяПоляДляВставкиПоказателей = "УдержанияПерерасчетРезультат";
	Описание.НомерТаблицы = 2.5;
	Описание.СодержитПолеСотрудник = Истина;
	Описание.ИмяРеквизитаСотрудник = "ФизическоеЛицо";
	
	Описание.ПутьКДаннымРаспределениеРезультатов = "Объект.РаспределениеРезультатовУдержаний";
	Описание.ПутьКДаннымАдресРаспределенияРезультатовВХранилище = "АдресТаблицыРаспределенияУдержаний";
	Описание.ИмяПоляДляВставкиРаспределенияРезультатов = "УдержанияПерерасчетРезультат";
	
	Описание.ВставлятьПослеПоля = Истина;
	
	Описание.ОтменятьВсеИсправления = Истина;
	
	Возврат Описание;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ОписаниеТаблицыНДФЛ()
	
	Описание = РасчетЗарплатыРасширенныйКлиентСервер.ОписаниеТаблицыРасчета();
	Описание.ИмяТаблицы = "НДФЛ";
	Описание.ПутьКДанным = "Объект.НДФЛ";
	Описание.ИмяПоляДляВставкиПоказателей = "Налог";
	Описание.ИмяПоляРезультат = "Налог";
	Описание.ИмяРеквизитаПериод = "МесяцНалоговогоПериода";
	Описание.НомерТаблицы = 3;
	Описание.СодержитПолеВидРасчета = Ложь;
	Описание.СодержитПолеСотрудник = Истина;
    Описание.ИмяРеквизитаСотрудник = "ФизическоеЛицо";
    Описание.ПроверяемыеРеквизиты = "ФизическоеЛицо,МесяцНалоговогоПериода";
	
	Описание.ПутьКДаннымРаспределениеРезультатов = "Объект.РаспределениеРезультатовУдержаний";
	Описание.ПутьКДаннымАдресРаспределенияРезультатовВХранилище = "АдресТаблицыРаспределенияУдержаний";
	Описание.ИмяРеквизитаИдентификаторСтроки = "ИдентификаторСтрокиНДФЛ";
	Описание.ИмяПоляДляВставкиРаспределенияРезультатов = "НДФЛПодразделение";
	
	Описание.ОтменятьВсеИсправления	= Истина;
	
	Возврат Описание;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ОписаниеТаблицыКорректировкиВыплаты()
	
	Описание = РасчетЗарплатыРасширенныйКлиентСервер.ОписаниеТаблицыКорректировкиВыплаты();
	Описание.ПроверяемыеРеквизиты = "ФизическоеЛицо";
	Описание.ПутьКДаннымАдресРаспределенияРезультатовВХранилище = "АдресТаблицыРаспределенияУдержаний";
	Описание.ОтображатьПоляРаспределенияРезультатов = Истина;
	
	Возврат Описание;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ОписаниеТаблицыПогашениеЗаймов()
	
	Описание = РасчетЗарплатыРасширенныйКлиентСервер.ОписаниеТаблицыРасчета();
	Описание.ИмяТаблицы = "ПогашениеЗаймов";
	Описание.ПутьКДанным = "Объект.ПогашениеЗаймов";
	Описание.СодержитПолеВидРасчета = Ложь;
	Описание.НомерТаблицы = 5;
	Описание.ПутьКДаннымРаспределениеРезультатов = "Объект.РаспределениеРезультатовУдержаний";
	Описание.ПутьКДаннымАдресРаспределенияРезультатовВХранилище = "АдресТаблицыРаспределенияУдержаний";
	Описание.ИмяРеквизитаИдентификаторСтроки = "ИдентификаторСтроки";
	Описание.УстанавливатьИдентификаторСтрокиРаспределенияРезультата = Истина;
	Описание.ИмяРеквизитаСотрудник = "ФизическоеЛицо";
	
	Описание.ОтменятьВсеИсправления	= Истина;
	
	Возврат Описание;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ОписанияТаблицЛьгот(Форма)
	
	ОписанияТаблиц = Новый Массив;
	ОписанияТаблиц.Добавить(ОписаниеТаблицыЛьгот(Форма));
	ОписанияТаблиц.Добавить(ОписаниеТаблицыЛьготыПерерасчет(Форма));
	
	Возврат ОписанияТаблиц;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ОписаниеТаблицыЛьгот(Форма)
	
	Описание = РасчетЗарплатыРасширенныйКлиентСервер.ОписаниеТаблицыРасчета();
	Описание.ИмяТаблицы = "Льготы";
	Описание.ПутьКДанным = "Объект.Льготы";
	Описание.СодержитПолеСотрудник = Истина;
	Описание.ИмяРеквизитаСотрудник = "Сотрудник";
	Описание.ИмяПоляДляВставкиПоказателей = "ДатыЛьготы";
	Описание.СодержитПолеКодВычета = Истина;
	Описание.НомерТаблицы = 7;
	
	Описание.ИмяПоляДляВставкиРаспределенияРезультатов 	= "ЛьготыРезультат";
	Описание.ВставлятьПослеПоля 						= Истина;
	
	Описание.ОтображатьПоляОписанияВремени = Истина;
	Описание.ОтображатьПоляНормыВремени = Истина;
	Описание.УправлятьОтображениемПолейОписанияВремени = Истина;
	
	СтрокаЗависимыеТаблицы = "";
	Если Форма.ИспользоватьНДФЛ Тогда
		СтрокаЗависимыеТаблицы = "НДФЛ,КорректировкиВыплаты";
	КонецЕсли;
	Если Форма.ИспользоватьУдержания Тогда
		СтрокаЗависимыеТаблицы = СтрокаЗависимыеТаблицы + ?(ПустаяСтрока(СтрокаЗависимыеТаблицы),"",",") + "Удержания";	
	КонецЕсли;
	Если Форма.ИспользоватьЗаймы Тогда
		СтрокаЗависимыеТаблицы = СтрокаЗависимыеТаблицы + ?(ПустаяСтрока(СтрокаЗависимыеТаблицы),"",",") + "ПогашениеЗаймов";	
	КонецЕсли;
	Описание.РаспределениеРезультатовЗависимыеТаблицы = СтрокаЗависимыеТаблицы;
	
	Описание.ПутьКДаннымАдресРаспределенияРезультатовВХранилище = "АдресТаблицыРаспределенияНачислений";
	Описание.ПутьКДаннымАдресРаспределенияПоТерриториямУсловиямТруда = "АдресТаблицыРаспределенияПоТерриториямУсловиямТруда";
	
	Описание.ОтменятьВсеИсправления	= Истина;
	Описание.СодержитПолеМестоПолученияДохода = Истина;
	
	Возврат Описание;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ОписаниеТаблицыЛьготыПерерасчет(Форма)
	
	Описание = РасчетЗарплатыРасширенныйКлиентСервер.ОписаниеТаблицыРасчета();
	Описание.ИмяТаблицы = "ЛьготыПерерасчет";
	Описание.ПутьКДанным = "Объект.ЛьготыПерерасчет";
	Описание.СодержитПолеСотрудник = Истина;
	Описание.ИмяРеквизитаСотрудник = "Сотрудник";
	Описание.ИмяПоляДляВставкиПоказателей = "ДатыЛьготыПерерасчет";
	Описание.СодержитПолеКодВычета = Истина;
	Описание.НомерТаблицы = 7.5;
	
	Описание.ИмяПоляДляВставкиРаспределенияРезультатов = "ЛьготыПерерасчетРезультат";
	Описание.ВставлятьПослеПоля = Истина;
	
	Описание.ОтображатьПоляОписанияВремени = Истина;
	Описание.ОтображатьПоляНормыВремени = Истина;
	Описание.УправлятьОтображениемПолейОписанияВремени = Истина;
	
	Описание.ЭтоПерерасчеты = Истина;
	Описание.ИмяРеквизитаФиксСторно = "ФиксСторно";
	
	СтрокаЗависимыеТаблицы = "";
	Если Форма.ИспользоватьНДФЛ Тогда
		СтрокаЗависимыеТаблицы = "НДФЛ,КорректировкиВыплаты";
	КонецЕсли;
	Если Форма.ИспользоватьУдержания Тогда
		СтрокаЗависимыеТаблицы = СтрокаЗависимыеТаблицы + ?(ПустаяСтрока(СтрокаЗависимыеТаблицы),"",",") + "Удержания";	
	КонецЕсли;
	Если Форма.ИспользоватьЗаймы Тогда
		СтрокаЗависимыеТаблицы = СтрокаЗависимыеТаблицы + ?(ПустаяСтрока(СтрокаЗависимыеТаблицы),"",",") + "ПогашениеЗаймов";	
	КонецЕсли;
	Описание.РаспределениеРезультатовЗависимыеТаблицы = СтрокаЗависимыеТаблицы;
	
	Описание.ПутьКДаннымАдресРаспределенияРезультатовВХранилище = "АдресТаблицыРаспределенияНачислений";
	Описание.ПутьКДаннымАдресРаспределенияПоТерриториямУсловиямТруда = "АдресТаблицыРаспределенияПоТерриториямУсловиямТруда";
	
	Описание.ОтменятьВсеИсправления = Истина;
	Описание.СодержитПолеМестоПолученияДохода = Истина;
	
	Возврат Описание;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция МассивОписанийТаблицФормы(Форма)
	
	МассивОписанийТаблицФормы = Новый Структура;
	
	Если Форма.Объект.РежимДоначисления Тогда
		МассивОписанийТаблицФормы.Вставить("Начисления", ОписаниеТаблицыНачислений(Форма));
		МассивОписанийТаблицФормы.Вставить("НачисленияПерерасчет", ОписаниеТаблицыПерерасчетов(Форма));
		МассивОписанийТаблицФормы.Вставить("Пособия", ОписаниеТаблицыПособия(Форма));
		МассивОписанийТаблицФормы.Вставить("ПособияПерерасчет", ОписаниеТаблицыПособияПерерасчет(Форма));
		МассивОписанийТаблицФормы.Вставить("Удержания", ОписаниеТаблицыУдержаний());
		МассивОписанийТаблицФормы.Вставить("УдержанияПерерасчет", ОписаниеТаблицыУдержанияПерерасчет());
	Иначе 
		Если Форма.ИспользоватьНачисление Тогда 
			МассивОписанийТаблицФормы.Вставить("Начисления", ОписаниеТаблицыНачислений(Форма));
			МассивОписанийТаблицФормы.Вставить("НачисленияПерерасчет", ОписаниеТаблицыПерерасчетов(Форма));
		КонецЕсли;
		Если Форма.ИспользоватьПособия Тогда 
			МассивОписанийТаблицФормы.Вставить("Пособия", ОписаниеТаблицыПособия(Форма));
			МассивОписанийТаблицФормы.Вставить("ПособияПерерасчет", ОписаниеТаблицыПособияПерерасчет(Форма));
		КонецЕсли;
		Если Форма.ИспользоватьЛьготы Тогда 
			МассивОписанийТаблицФормы.Вставить("Льготы", ОписаниеТаблицыЛьгот(Форма));
			МассивОписанийТаблицФормы.Вставить("ЛьготыПерерасчет", ОписаниеТаблицыЛьготыПерерасчет(Форма));
		КонецЕсли;
		Если Форма.ИспользоватьУдержания Тогда 
			МассивОписанийТаблицФормы.Вставить("Удержания", ОписаниеТаблицыУдержаний());
			МассивОписанийТаблицФормы.Вставить("УдержанияПерерасчет", ОписаниеТаблицыУдержанияПерерасчет());
		КонецЕсли;
	КонецЕсли;
	
	Если ОбщегоНазначенияБЗККлиентСервер.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.УправленческаяЗарплата") Тогда
		Модуль = ОбщегоНазначенияБЗККлиентСервер.ОбщийМодуль("УправленческаяЗарплатаКлиентСервер");
		Модуль.ПриЗаполненииСтруктурыОписанийТаблицНачисленияЗарплаты(МассивОписанийТаблицФормы, Форма.Объект.РежимДоначисления, Форма.ИспользоватьНачисление);
	КонецЕсли;
	
	Возврат МассивОписанийТаблицФормы;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ОписанияТаблицДляРаспределенияРезультата(Форма)

	ОписанияТаблиц = Новый Массив;
	
	Если Форма.Объект.РежимДоначисления Тогда 
		
		ОписанияТаблиц.Добавить(ОписаниеТаблицыНачислений(Форма));
		ОписанияТаблиц.Добавить(ОписаниеТаблицыПерерасчетов(Форма));
		ОписанияТаблиц.Добавить(ОписаниеТаблицыНачисленийПоДоговорам(Форма));
		ОписанияТаблиц.Добавить(ОписаниеТаблицыПособия(Форма));
		ОписанияТаблиц.Добавить(ОписаниеТаблицыПособияПерерасчет(Форма));
		ОписанияТаблиц.Добавить(ОписаниеТаблицыУдержаний());
		ОписанияТаблиц.Добавить(ОписаниеТаблицыУдержанияПерерасчет());
		ОписанияТаблиц.Добавить(ОписаниеТаблицыНДФЛ());
		ОписанияТаблиц.Добавить(ОписаниеТаблицыКорректировкиВыплаты());
		ОписанияТаблиц.Добавить(ОписаниеТаблицыПогашениеЗаймов());
		
	Иначе 
		
		Если Форма.ИспользоватьНачисление Тогда 
			ОписанияТаблиц.Добавить(ОписаниеТаблицыНачислений(Форма));
			ОписанияТаблиц.Добавить(ОписаниеТаблицыПерерасчетов(Форма));
		КонецЕсли;
		
		Если Форма.ИспользоватьДоговоры Тогда 
			ОписанияТаблиц.Добавить(ОписаниеТаблицыНачисленийПоДоговорам(Форма));
		КонецЕсли;
		
		Если Форма.ИспользоватьПособия Тогда 
			ОписанияТаблиц.Добавить(ОписаниеТаблицыПособия(Форма));
			ОписанияТаблиц.Добавить(ОписаниеТаблицыПособияПерерасчет(Форма));
		КонецЕсли;
		
		Если Форма.ИспользоватьЛьготы Тогда 
			ОписанияТаблиц.Добавить(ОписаниеТаблицыЛьгот(Форма));
			ОписанияТаблиц.Добавить(ОписаниеТаблицыЛьготыПерерасчет(Форма));
		КонецЕсли;
		
		Если Форма.ИспользоватьУдержания Тогда 
			ОписанияТаблиц.Добавить(ОписаниеТаблицыУдержаний());
			ОписанияТаблиц.Добавить(ОписаниеТаблицыУдержанияПерерасчет());
		КонецЕсли;
		
		Если Форма.ИспользоватьНДФЛ Тогда 
			ОписанияТаблиц.Добавить(ОписаниеТаблицыНДФЛ());
			ОписанияТаблиц.Добавить(ОписаниеТаблицыКорректировкиВыплаты());
		КонецЕсли;

		Если Форма.ИспользоватьЗаймы Тогда 
			ОписанияТаблиц.Добавить(ОписаниеТаблицыПогашениеЗаймов());
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ОписанияТаблиц;

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СтруктураОписанияТаблицДляРаспределенияРезультата(Форма)

	ОписанияТаблиц = Новый Структура;
	
	Если Форма.Объект.РежимДоначисления Тогда 
		ОписанияТаблиц.Вставить("Начисления", ОписаниеТаблицыНачислений(Форма));
		ОписанияТаблиц.Вставить("НачисленияПерерасчет", ОписаниеТаблицыПерерасчетов(Форма));
		ОписанияТаблиц.Вставить("НачисленияПоДоговорам", ОписаниеТаблицыНачисленийПоДоговорам(Форма));
		ОписанияТаблиц.Вставить("Пособия", ОписаниеТаблицыПособия(Форма));
		ОписанияТаблиц.Вставить("ПособияПерерасчет", ОписаниеТаблицыПособияПерерасчет(Форма));
		ОписанияТаблиц.Вставить("Удержания", ОписаниеТаблицыУдержаний());
		ОписанияТаблиц.Вставить("УдержанияПерерасчет", ОписаниеТаблицыУдержанияПерерасчет());
		ОписанияТаблиц.Вставить("НДФЛ", ОписаниеТаблицыНДФЛ());
		ОписанияТаблиц.Вставить("КорректировкиВыплаты", ОписаниеТаблицыКорректировкиВыплаты());
		ОписанияТаблиц.Вставить("ПогашениеЗаймов", ОписаниеТаблицыПогашениеЗаймов());
	Иначе 
		Если Форма.ИспользоватьНачисление Тогда 
			ОписанияТаблиц.Вставить("Начисления", ОписаниеТаблицыНачислений(Форма));
			ОписанияТаблиц.Вставить("НачисленияПерерасчет", ОписаниеТаблицыПерерасчетов(Форма));
		КонецЕсли;
		Если Форма.ИспользоватьДоговоры Тогда 
			ОписанияТаблиц.Вставить("НачисленияПоДоговорам", ОписаниеТаблицыНачисленийПоДоговорам(Форма));
		КонецЕсли;
		Если Форма.ИспользоватьПособия Тогда 
			ОписанияТаблиц.Вставить("Пособия", ОписаниеТаблицыПособия(Форма));
			ОписанияТаблиц.Вставить("ПособияПерерасчет", ОписаниеТаблицыПособияПерерасчет(Форма));
		КонецЕсли;
		Если Форма.ИспользоватьЛьготы Тогда 
			ОписанияТаблиц.Вставить("Льготы", ОписаниеТаблицыЛьгот(Форма));
			ОписанияТаблиц.Вставить("ЛьготыПерерасчет", ОписаниеТаблицыЛьготыПерерасчет(Форма));
		КонецЕсли;
		Если Форма.ИспользоватьУдержания Тогда 
			ОписанияТаблиц.Вставить("Удержания", ОписаниеТаблицыУдержаний());
			ОписанияТаблиц.Вставить("УдержанияПерерасчет", ОписаниеТаблицыУдержанияПерерасчет());
		КонецЕсли;
		Если Форма.ИспользоватьНДФЛ Тогда 
			ОписанияТаблиц.Вставить("НДФЛ", ОписаниеТаблицыНДФЛ());
			ОписанияТаблиц.Вставить("КорректировкиВыплаты", ОписаниеТаблицыКорректировкиВыплаты());
		КонецЕсли;
		Если Форма.ИспользоватьЗаймы Тогда 
			ОписанияТаблиц.Вставить("ПогашениеЗаймов", ОписаниеТаблицыПогашениеЗаймов());
		КонецЕсли;
	КонецЕсли;
	
	Возврат ОписанияТаблиц;

КонецФункции

&НаСервере
Процедура ДанныеВРеквизиты(ТекущийОбъект)
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ИнициализироватьИсточникиДанныхФормыПриПолученииДанных(ТекущийОбъект, МенеджерВременныхТаблиц);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяВТРаспределение", "ВТРаспределениеРезультатовНачислений");
	ДополнительныеПараметры.Вставить("ИмяВТПредставлениеРаспределения", "ВТПредставлениеРаспределенияНачислений");
	ДополнительныеПараметры.Вставить("ПодготовленаВТВычетыКДоходам", Ложь);
	ДополнительныеПараметры.Вставить("Период", '00010101');         
	ДополнительныеПараметры.Вставить("ОписаниеПанелиВычеты", ОписаниеПанелиВычеты());
	ДополнительныеПараметры.Вставить("ОписанияТаблицСРаспределениемПоТерриториямУсловиямТруда", ОписанияТаблицСРаспределениемПоТерриториямУсловиямТруда(ЭтаФорма));
	ДополнительныеПараметры.Вставить("КонтролируемыеПоля", ПолучитьКонтролируемыеПоля());
	
	ЗарплатаКадрыРасширенный.ЗаполнитьТаблицуВидовРасчетаПоДаннымОбъекта(ЭтотОбъект, ТекущийОбъект, ОписаниеТаблицыНачислений(ЭтотОбъект), МенеджерВременныхТаблиц, ДополнительныеПараметры);
	ЗарплатаКадрыРасширенный.ЗаполнитьТаблицуВидовРасчетаПоДаннымОбъекта(ЭтотОбъект, ТекущийОбъект, ОписаниеТаблицыПерерасчетов(ЭтотОбъект), МенеджерВременныхТаблиц, ДополнительныеПараметры);
	ЗарплатаКадрыРасширенный.ЗаполнитьТаблицуВидовРасчетаПоДаннымОбъекта(ЭтотОбъект, ТекущийОбъект, ОписаниеТаблицыПособия(ЭтотОбъект), МенеджерВременныхТаблиц, ДополнительныеПараметры);
	ЗарплатаКадрыРасширенный.ЗаполнитьТаблицуВидовРасчетаПоДаннымОбъекта(ЭтотОбъект, ТекущийОбъект, ОписаниеТаблицыПособияПерерасчет(ЭтотОбъект), МенеджерВременныхТаблиц, ДополнительныеПараметры);
	ЗарплатаКадрыРасширенный.ЗаполнитьТаблицуВидовРасчетаПоДаннымОбъекта(ЭтотОбъект, ТекущийОбъект, ОписаниеТаблицыНачисленийПоДоговорам(ЭтотОбъект), МенеджерВременныхТаблиц, ДополнительныеПараметры);
	
	ЗарплатаКадрыРасширенный.ВводНачисленийЗаполнитьВторичныеДанныеПоказателей(ЭтотОбъект, МассивОписанийТаблицФормы(ЭтотОбъект));
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ЛьготыСотрудников") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ЛьготыСотрудников");
		Модуль.ЗаполнитьТаблицуВидовРасчетаПоДаннымОбъекта(ЭтотОбъект, ТекущийОбъект, ОписанияТаблицЛьгот(ЭтаФорма), МенеджерВременныхТаблиц, ДополнительныеПараметры);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.УправленческаяЗарплата") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("УправленческаяЗарплата");
		Модуль.ПриПолученииДанныхДокументаНачисленияЗарплаты(ЭтотОбъект, ТекущийОбъект, МенеджерВременныхТаблиц, ДополнительныеПараметры);
	КонецЕсли;
	
	ДополнительныеПараметры.ИмяВТРаспределение = "ВТРаспределениеРезультатовУдержаний";
	ДополнительныеПараметры.ИмяВТПредставлениеРаспределения = "ВТПредставлениеРаспределенияУдержаний";
	
	ЗарплатаКадрыРасширенный.ЗаполнитьТаблицуВидовРасчетаПоДаннымОбъекта(ЭтотОбъект, ТекущийОбъект, ОписаниеТаблицыУдержаний(), МенеджерВременныхТаблиц, ДополнительныеПараметры);
	ЗарплатаКадрыРасширенный.ЗаполнитьТаблицуВидовРасчетаПоДаннымОбъекта(ЭтотОбъект, ТекущийОбъект, ОписаниеТаблицыУдержанияПерерасчет(), МенеджерВременныхТаблиц, ДополнительныеПараметры);

	ЗарплатаКадрыРасширенный.ЗаполнитьВторичныеДанныеНДФЛПоДаннымОбъекта(ЭтотОбъект, ТекущийОбъект, ОписаниеТаблицыНДФЛ(), МенеджерВременныхТаблиц, ДополнительныеПараметры);
	ЗарплатаКадрыРасширенный.ЗаполнитьТаблицуВидовРасчетаПоДаннымОбъекта(ЭтотОбъект, ТекущийОбъект, ОписаниеТаблицыКорректировкиВыплаты(), МенеджерВременныхТаблиц, ДополнительныеПараметры);
	ЗарплатаКадрыРасширенный.ЗаполнитьТаблицуВидовРасчетаПоДаннымОбъекта(ЭтотОбъект, ТекущийОбъект, ОписаниеТаблицыПогашениеЗаймов(), МенеджерВременныхТаблиц, ДополнительныеПараметры);
	
	ЗаполнитьФизическоеЛицоСотрудниковПерерасчета();
	
	ЗарплатаКадрыРасширенный.ЗаполнитьДоступностьМестаПолученияДохода(ЭтаФорма, МассивОписанийТаблицФормы(ЭтаФорма));
	ЗарплатаКадрыРасширенныйКлиентСервер.УстановитьОтображениеРеквизитовПервичногоДокумента(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьФизическоеЛицоСотрудниковПерерасчета()
	
	Сотрудники = ОбщегоНазначения.ВыгрузитьКолонку(Объект.СотрудникиПерерасчет, "Сотрудник");
	ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияТипаИзМассива(Сотрудники, Тип("СправочникСсылка.ФизическиеЛица"));
	ФизическиеЛицаСотрудников = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(Сотрудники, "ФизическоеЛицо", Истина);
	Для Каждого СтрокаСотрудника Из Объект.СотрудникиПерерасчет Цикл
		Если ТипЗнч(СтрокаСотрудника.Сотрудник) = Тип("СправочникСсылка.Сотрудники") Тогда
			СтрокаСотрудника.ФизическоеЛицо = ФизическиеЛицаСотрудников[СтрокаСотрудника.Сотрудник];
		Иначе
			СтрокаСотрудника.ФизическоеЛицо = СтрокаСотрудника.Сотрудник;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьИсточникиДанныхФормыПриПолученииДанных(ТекущийОбъект, МенеджерВременныхТаблиц)
	
	АдресТаблицыРаспределенияПоТерриториямУсловиямТруда = "";
	
	СоздатьВТПоказатели(МенеджерВременныхТаблиц, ТекущийОбъект);
	СоздатьВТРаспределениеПоТерриториямУсловиямТруда(МенеджерВременныхТаблиц, ТекущийОбъект);
	СоздатьВТРаспределениеРезультатовНачислений(МенеджерВременныхТаблиц, ТекущийОбъект);
	СоздатьВТРаспределениеРезультатовУдержаний(МенеджерВременныхТаблиц, ТекущийОбъект);
	СоздатьВТПредставлениеРаспределенияРезультатовРасчета(МенеджерВременныхТаблиц, ТекущийОбъект);
	
	ЗарплатаКадрыРасширенный.СоздатьВТПредставлениеРаспределенияНачисленийУдержаний(
		МенеджерВременныхТаблиц, 
		ТекущийОбъект.Ссылка, 
		"ВТРаспределениеРезультатовНачислений", 
		"ВТПредставлениеРаспределенияНачислений");
		
	ЗарплатаКадрыРасширенный.СоздатьВТПредставлениеРаспределенияНачисленийУдержаний(
		МенеджерВременныхТаблиц, 
		ТекущийОбъект.Ссылка, 
		"ВТРаспределениеРезультатовУдержаний", 
		"ВТПредставлениеРаспределенияУдержаний");
		
КонецПроцедуры

&НаСервере
Процедура СоздатьВТПоказатели(МенеджерВременныхТаблиц, ТекущийОбъект)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ТекстЗапроса = "ВЫБРАТЬ
	               |	Таблица.НомерСтроки,
	               |	Таблица.Показатель КАК Показатель,
	               |	Таблица.Значение,
	               |	Таблица.ИдентификаторСтрокиВидаРасчета КАК ИдентификаторСтрокиВидаРасчета
	               |ПОМЕСТИТЬ ВТПоказатели
	               |ИЗ
	               |	&ТаблицаПоказателей КАК Таблица
				   |ГДЕ
				   |	&Условие";
	Если Параметры.Ключ.Пустая()
		И ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицаПоказателей", "Документ.НачислениеЗарплаты.Показатели");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Условие", "Таблица.Ссылка = &Ссылка");
		Запрос.УстановитьПараметр("Ссылка", Параметры.ЗначениеКопирования);
	Иначе
		Запрос.УстановитьПараметр("ТаблицаПоказателей", ТекущийОбъект.Показатели);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Условие", "ИСТИНА");
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.Выполнить();
	
КонецПроцедуры

&НаСервере
Процедура СоздатьВТРаспределениеПоТерриториямУсловиямТруда(МенеджерВременныхТаблиц, ТекущийОбъект)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	Таблица.НомерСтроки КАК НомерСтроки,
	               |	Таблица.ИдентификаторСтроки КАК ИдентификаторСтроки,
	               |	Таблица.Территория КАК Территория,
	               |	Таблица.УсловияТруда КАК УсловияТруда,
	               |	Таблица.ДоляРаспределения КАК ДоляРаспределения,
	               |	Таблица.Результат КАК Результат,
	               |	Таблица.СуммаВычета КАК СуммаВычета,
	               |	Таблица.ИдентификаторСтрокиПоказателей КАК ИдентификаторСтрокиПоказателей,
	               |	Таблица.РанееНачислено КАК РанееНачислено
	               |ПОМЕСТИТЬ ВТРаспределениеПоТерриториямУсловиямТруда
	               |ИЗ
	               |	&РаспределениеПоТерриториямУсловиямТруда КАК Таблица
				   |ГДЕ
				   |	&Условие";
	
	Если Параметры.Ключ.Пустая()
		И ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&РаспределениеПоТерриториямУсловиямТруда", "Документ.НачислениеЗарплаты.РаспределениеПоТерриториямУсловиямТруда");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Условие", "Таблица.Ссылка = &Ссылка");
		Запрос.УстановитьПараметр("Ссылка", Параметры.ЗначениеКопирования);
	Иначе
		Запрос.УстановитьПараметр("РаспределениеПоТерриториямУсловиямТруда", ТекущийОбъект.РаспределениеПоТерриториямУсловиямТруда);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Условие", "ИСТИНА");
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.Выполнить();
	
КонецПроцедуры

&НаСервере
Процедура СоздатьВТРаспределениеРезультатовНачислений(МенеджерВременныхТаблиц, ТекущийОбъект)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Таблица.НомерСтроки КАК НомерСтроки,
	|	Таблица.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Таблица.Территория КАК Территория,
	|	Таблица.СтатьяФинансирования КАК СтатьяФинансирования,
	|	Таблица.СтатьяРасходов КАК СтатьяРасходов,
	|	Таблица.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	Таблица.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	Таблица.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|	Таблица.Результат КАК Результат
	|ПОМЕСТИТЬ ВТРаспределениеРезультатовНачислений
	|ИЗ
	|	&ТаблицаРаспределенияНачислений КАК Таблица
	|ГДЕ
	|	&Условие
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РаспределениеНачислений.НомерСтроки КАК НомерСтроки,
	|	РаспределениеНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	РаспределениеНачислений.Территория КАК Территория,
	|	РаспределениеНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
	|	РаспределениеНачислений.СтатьяРасходов КАК СтатьяРасходов,
	|	РаспределениеНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	РаспределениеНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	РаспределениеНачислений.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|	РаспределениеНачислений.Результат КАК Результат,
	|	ЕСТЬNULL(СтатьиФинансированияЗарплата.Код, """") КАК КодСтатьиФинансирования
	|ИЗ
	|	ВТРаспределениеРезультатовНачислений КАК РаспределениеНачислений
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтатьиФинансированияЗарплата КАК СтатьиФинансированияЗарплата
	|		ПО РаспределениеНачислений.СтатьяФинансирования = СтатьиФинансированияЗарплата.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки,
	|	ИдентификаторСтроки,
	|	Территория,
	|	СтатьяФинансирования,
	|	СтатьяРасходов,
	|	СпособОтраженияЗарплатыВБухучете,
	|	ОблагаетсяЕНВД";
	
	Если Параметры.Ключ.Пустая()
		И ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицаРаспределенияНачислений", "Документ.НачислениеЗарплаты.РаспределениеРезультатовНачислений");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Условие", "Таблица.Ссылка = &Ссылка");
		Запрос.УстановитьПараметр("Ссылка", Параметры.ЗначениеКопирования);
	Иначе
		Запрос.УстановитьПараметр("ТаблицаРаспределенияНачислений", ТекущийОбъект.РаспределениеРезультатовНачислений);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Условие", "ИСТИНА");
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	ТаблицаРаспределенияНачислений = Запрос.Выполнить().Выгрузить();
	ТаблицаРаспределенияНачислений.Индексы.Добавить("ИдентификаторСтроки");
	АдресТаблицыРаспределенияНачислений = ПоместитьВоВременноеХранилище(ТаблицаРаспределенияНачислений, УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервере
Процедура СоздатьВТРаспределениеРезультатовУдержаний(МенеджерВременныхТаблиц, ТекущийОбъект)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Таблица.НомерСтроки КАК НомерСтроки,
	|	Таблица.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Таблица.ВидУдержания КАК ВидУдержания,
	|	Таблица.Сотрудник КАК Сотрудник,
	|	Таблица.Подразделение КАК Подразделение,
	|	Таблица.СтатьяФинансирования КАК СтатьяФинансирования,
	|	Таблица.СтатьяРасходов КАК СтатьяРасходов,
	|	Таблица.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|	Таблица.ВидДоходаИсполнительногоПроизводства КАК ВидДоходаИсполнительногоПроизводства,
	|	Таблица.Результат КАК Результат
	|ПОМЕСТИТЬ ВТРаспределениеРезультатовУдержаний
	|ИЗ
	|	&ТаблицаРаспределенияУдержаний КАК Таблица
	|ГДЕ
	|	&Условие
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РаспределениеРезультатов.НомерСтроки КАК НомерСтроки,
	|	РаспределениеРезультатов.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	РаспределениеРезультатов.ВидУдержания КАК ВидУдержания,
	|	РаспределениеРезультатов.Сотрудник КАК Сотрудник,
	|	РаспределениеРезультатов.Подразделение КАК Подразделение,
	|	РаспределениеРезультатов.СтатьяФинансирования КАК СтатьяФинансирования,
	|	РаспределениеРезультатов.СтатьяРасходов КАК СтатьяРасходов,
	|	РаспределениеРезультатов.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|	РаспределениеРезультатов.ВидДоходаИсполнительногоПроизводства КАК ВидДоходаИсполнительногоПроизводства,
	|	РаспределениеРезультатов.Результат КАК Результат,
	|	ЕСТЬNULL(СтатьиФинансированияЗарплата.Код, """") КАК КодСтатьиФинансирования
	|ИЗ
	|	ВТРаспределениеРезультатовУдержаний КАК РаспределениеРезультатов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтатьиФинансированияЗарплата КАК СтатьиФинансированияЗарплата
	|		ПО РаспределениеРезультатов.СтатьяФинансирования = СтатьиФинансированияЗарплата.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки,
	|	ИдентификаторСтроки,
	|	ВидУдержания,
	|	Сотрудник,
	|	Подразделение,
	|	СтатьяФинансирования,
	|	СтатьяРасходов";
	
	Если Параметры.Ключ.Пустая()
		И ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицаРаспределенияУдержаний", "Документ.НачислениеЗарплаты.РаспределениеРезультатовУдержаний");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Условие", "Таблица.Ссылка = &Ссылка");
		Запрос.УстановитьПараметр("Ссылка", Параметры.ЗначениеКопирования);
	Иначе
		Запрос.УстановитьПараметр("ТаблицаРаспределенияУдержаний", ТекущийОбъект.РаспределениеРезультатовУдержаний);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Условие", "ИСТИНА");
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	ТаблицаРаспределенияУдержаний = Запрос.Выполнить().Выгрузить();
	ТаблицаРаспределенияУдержаний.Индексы.Добавить("ИдентификаторСтроки");
	АдресТаблицыРаспределенияУдержаний = ПоместитьВоВременноеХранилище(ТаблицаРаспределенияУдержаний, УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервере
Процедура СоздатьВТПредставлениеРаспределенияРезультатовРасчета(МенеджерВременныхТаблиц, ТекущийОбъект)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	Таблица.НомерСтроки,
	               |	Таблица.ИдентификаторСтроки,
	               |	Таблица.НомерСтрокиРаспределения,
	               |	Таблица.НомерЭлементаПредставления,
	               |	Таблица.КоличествоЭлементовПредставления,
	               |	Таблица.ПредставлениеРезультата,
	               |	Таблица.ЕстьОшибкиЗаполнения,
	               |	Таблица.РаспределениеНачислений
	               |ПОМЕСТИТЬ ВТПредставлениеРаспределенияРезультатовРасчета
	               |ИЗ
	               |	&ПредставлениеРаспределенияРезультатовРасчета КАК Таблица
				   |ГДЕ
				   |	&Условие";
	
	Если Параметры.Ключ.Пустая()
		И ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПредставлениеРаспределенияРезультатовРасчета", "Документ.НачислениеЗарплаты.ПредставлениеРаспределенияРезультатовРасчета");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Условие", "Таблица.Ссылка = &Ссылка");
		Запрос.УстановитьПараметр("Ссылка", Параметры.ЗначениеКопирования);
	Иначе
		Запрос.УстановитьПараметр("ПредставлениеРаспределенияРезультатовРасчета", ТекущийОбъект.ПредставлениеРаспределенияРезультатовРасчета);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Условие", "ИСТИНА");
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.Выполнить();
	
КонецПроцедуры	

&НаСервере
Процедура РеквизитыВДанные(ТекущийОбъект)
	
	ТекущийОбъект.Показатели.Очистить();
	
	ОписанияТаблиц = Новый Массив;
	
	Если Объект.РежимДоначисления Тогда 
		ОписанияТаблиц.Добавить(ОписаниеТаблицыНачислений(ЭтаФорма));
		ОписанияТаблиц.Добавить(ОписаниеТаблицыПерерасчетов(ЭтаФорма));
		ОписанияТаблиц.Добавить(ОписаниеТаблицыПособия(ЭтаФорма));
		ОписанияТаблиц.Добавить(ОписаниеТаблицыПособияПерерасчет(ЭтаФорма));
		ОписанияТаблиц.Добавить(ОписаниеТаблицыУдержаний());
		ОписанияТаблиц.Добавить(ОписаниеТаблицыУдержанияПерерасчет());
	Иначе 
		Если ИспользоватьНачисление Тогда 
			ОписанияТаблиц.Добавить(ОписаниеТаблицыНачислений(ЭтаФорма));
			ОписанияТаблиц.Добавить(ОписаниеТаблицыПерерасчетов(ЭтаФорма));
		КонецЕсли;
		Если ИспользоватьПособия Тогда 
			ОписанияТаблиц.Добавить(ОписаниеТаблицыПособия(ЭтаФорма));
			ОписанияТаблиц.Добавить(ОписаниеТаблицыПособияПерерасчет(ЭтаФорма));
		КонецЕсли;
		Если ИспользоватьЛьготы Тогда 
			ОписанияТаблиц.Добавить(ОписаниеТаблицыЛьгот(ЭтаФорма));
			ОписанияТаблиц.Добавить(ОписаниеТаблицыЛьготыПерерасчет(ЭтаФорма));
		КонецЕсли;
		Если ИспользоватьУдержания Тогда 
			ОписанияТаблиц.Добавить(ОписаниеТаблицыУдержаний());
			ОписанияТаблиц.Добавить(ОписаниеТаблицыУдержанияПерерасчет());
		КонецЕсли;
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.УправленческаяЗарплата") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("УправленческаяЗарплатаКлиентСервер");
		Модуль.ПриЗаполненииМассиваОписанийТаблицНачисленияЗарплаты(ОписанияТаблиц, Объект.РежимДоначисления, ИспользоватьНачисление);
	КонецЕсли;
	
	Если Объект.РежимДоначисления Или ИспользоватьДоговоры Тогда 
		УстановитьИдентификаторыСтрок(ТекущийОбъект, ОписаниеТаблицыНачисленийПоДоговорам(ЭтаФорма));
	КонецЕсли;
		
	ЗарплатаКадрыРасширенный.ВводНачисленийРеквизитВДанные(ЭтаФорма, ТекущийОбъект, ОписанияТаблиц, 2);
	ЗарплатаКадрыРасширенный.ВводРаспределенияРезультатовРасчетаРеквизитВДанные(ЭтаФорма, ТекущийОбъект, ОписанияТаблицДляРаспределенияРезультата(ЭтаФорма));
	РасчетЗарплатыРасширенныйФормы.РаспределениеПоТерриториямУсловиямТрудаРеквизитВДанные(ЭтаФорма, ТекущийОбъект, ОписанияТаблицСРаспределениемПоТерриториямУсловиямТруда(ЭтаФорма));
	
КонецПроцедуры	

&НаСервере
Процедура УстановитьФункциональныеОпцииФормы()
	ПараметрыФО = Новый Структура("Организация, Период", Объект.Организация, НачалоДня(Объект.МесяцНачисления));
	УстановитьПараметрыФункциональныхОпцийФормы(ПараметрыФО);
КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеОрганизацииНаСервере()
	
	ЗаполнитьДанныеФормыПоОрганизации();
	УстановитьФункциональныеОпцииФормы();
	ОтражениеЗарплатыВБухучетеРасширенный.ОбработатьИзменениеОрганизацииПроцентЕНВД(ЭтотОбъект, Объект.Организация, Объект.МесяцНачисления);
	РасчетЗарплатыРасширенныйФормы.ДополнитьДанныеФормыРаспределенияПоТерриториямУсловиямТруда(ЭтотОбъект, Объект.Организация, ОписанияТаблицСРаспределениемПоТерриториямУсловиямТруда(ЭтаФорма), Ложь, Ложь);
	
	РасчетЗарплатыРасширенныйФормы.ОбновитьПлановыеДатыВыплатыПоОрганизации(ЭтотОбъект);
	
	Если ТребуетсяЗаполнитьДатуВыплаты(Объект.МесяцНачисления, Объект.РежимДоначисления) Тогда
		РасчетЗарплатыРасширенный.УстановитьПланируемуюДатуВыплаты(ЭтотОбъект, ОписаниеДокумента(ЭтотОбъект));
		УстановитьВидимостьКолонокПрогрессивногоНалога(Объект.ПланируемаяДатаВыплаты);
	КонецЕсли;
	
	РассчитатьИтогиПоДокументу(ЭтотОбъект);
	УстановитьОтображениеСообщенияОНеобходимостиПерерасчета();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеФормыПоОрганизации()
	
	Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		Возврат;
	КонецЕсли; 
	
	ЗапрашиваемыеЗначения = Новый Структура("Организация, МесяцРасчета", "Объект.Организация", "Объект.МесяцНачисления");
	ЗарплатаКадры.ЗаполнитьЗначенияВФорме(ЭтаФорма, ЗапрашиваемыеЗначения, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве("Организация"));
	ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДате(ЭтотОбъект, "Объект.МесяцНачисления", "МесяцНачисленияСтрокой");
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗначенияКонтролируемыхПолей()
	
	// Выполняет заполнение сведений о значениях контролируемых полей, 
	// которые могли измениться в результате не интерактивного обновления значений.
	РасчетЗарплатыРасширенныйКлиент.ДокументыВыполненияНачисленийПриАктивацииСтроки(ЭтаФорма, "Начисления", Истина);
	РасчетЗарплатыРасширенныйКлиент.ДокументыВыполненияНачисленийПриАктивацииСтроки(ЭтаФорма, "НачисленияПоДоговорам", Истина);
	РасчетЗарплатыРасширенныйКлиент.ДокументыВыполненияНачисленийПриАктивацииСтроки(ЭтаФорма, "Пособия", Истина);
	РасчетЗарплатыРасширенныйКлиент.ДокументыВыполненияНачисленийПриАктивацииСтроки(ЭтаФорма, "Удержания", Истина);
	РасчетЗарплатыРасширенныйКлиент.ДокументыВыполненияНачисленийПриАктивацииСтроки(ЭтаФорма, "НДФЛ", Истина);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьИдентификаторыСтрок(ТекущийОбъект, ОписаниеТаблицы)
	ТаблицаОбъекта = Объект[ОписаниеТаблицы.ИмяТаблицы];
	ДанныеФормыКоллекция = ТекущийОбъект[ОписаниеТаблицы.ИмяТаблицы];
	ИдентификаторСтроки = ОписаниеТаблицы.НомерТаблицы * 1000000 + 1;
	Индекс = 0;
	Пока Индекс < Объект[ОписаниеТаблицы.ИмяТаблицы].Количество() Цикл
		СтрокаФормы = ДанныеФормыКоллекция[Индекс];
		СтрокаТаблицы = ТаблицаОбъекта[Индекс];
		СтрокаФормы[ОписаниеТаблицы.ИмяРеквизитаИдентификаторСтроки] = ИдентификаторСтроки;
		СтрокаТаблицы[ОписаниеТаблицы.ИмяРеквизитаИдентификаторСтроки] = ИдентификаторСтроки;
		Индекс = Индекс + 1;
		ИдентификаторСтроки = ИдентификаторСтроки +1;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ЗарегистрироватьПроцентЕНВД() Экспорт

	ЗарегистрироватьПроцентЕНВДНаСервере(Объект.Организация, Объект.МесяцНачисления, ЭтаФорма.ПроцентЕНВД, ЭтаФорма.ПроцентЕНВДСтрока);

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗарегистрироватьПроцентЕНВДНаСервере(Организация, Месяц, ПроцентЕНВД, ПроцентЕНВДСтрока)
	
	ОтражениеЗарплатыВБухучетеРасширенный.ЗарегистрироватьПроцентЕНВДПослеРедактированияВФорме(Организация, Месяц, ПроцентЕНВД, ПроцентЕНВДСтрока);
	
КонецПроцедуры

&НаСервере
Функция ТекстПредупрежденияПриРедактированииПроцентаЕНВД() Экспорт

	ОрганизацияНаименование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Организация, "Наименование");
	МесяцНачисленияСтрокой  = ЗарплатаКадрыКлиентСервер.ПолучитьПредставлениеМесяца(Объект.МесяцНачисления);
	
	ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'При изменении введенного ранее процента деятельности ЕНВД необходимо
					|перезаполнить все документы начисления зарплаты за %1 г по ""%2""';
					|en = 'When changing the UTII activity percent entered previously, refill
					|all salary accounting documents from the year of %1 to the year of ""%2""'"),
					МесяцНачисленияСтрокой,
					ОрганизацияНаименование);
					
	Возврат ТекстПредупреждения;

КонецФункции

&НаКлиенте
Процедура Подключаемый_ПроцентЕНВДСтрокаПриИзменении(Элемент)
	
	ОтражениеЗарплатыВБухучетеКлиент.ПроцентЕНВДСтрокаПриИзменении(ЭтаФорма, Элемент, ЭтаФорма.ПроцентЕНВДСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроцентЕНВДСтрокаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОтражениеЗарплатыВБухучетеКлиент.ПроцентЕНВДСтрокаНачалоВыбора(ЭтаФорма, Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСотрудникаПриВыбореДоговораГПХ()
	
	ИдентификаторСтроки = Элементы.НачисленияПоДоговорам.ТекущаяСтрока;
	Если ИдентификаторСтроки <> Неопределено Тогда
		
		ТекущиеДанные = Объект.НачисленияПоДоговорам.НайтиПоИдентификатору(ИдентификаторСтроки);
		Если ТекущиеДанные <> Неопределено Тогда
			ТекущиеДанные.Сотрудник = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущиеДанные.ДокументОснование, "Сотрудник");
		КонецЕсли; 
		
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Функция КарточкаУчетаПоСтраховымВзносамНаСервере(ИдентификаторыСтрок)
	
	ФизическиеЛица = Новый Массив;
	Для каждого ИдентификаторСтроки Из ИдентификаторыСтрок Цикл
		ФизическиеЛица.Добавить(Объект.Взносы.НайтиПоИдентификатору(ИдентификаторСтроки).ФизическоеЛицо);
	КонецЦикла;
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	РеквизитыВДанные(ДокументОбъект);
	Возврат УчетСтраховыхВзносов.КарточкаУчетаПоСтраховымВзносамПодробнее(ДокументОбъект, Модифицированность, ФизическиеЛица, Объект.МесяцНачисления);
	
КонецФункции

&НаКлиенте
Процедура ПересчитатьДокументНаКлиенте() Экспорт
	
	ПересчитатьДокументНаСервере(СотрудникиКРасчету);
	РасчетЗарплатыРасширенныйКлиент.ОчиститьСписокСотрудниковКРасчету(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ПересчитатьДокументНаСервере(СотрудникиКРасчету)
	
	Для каждого ТаблицаССотрудниками Из СотрудникиКРасчету Цикл
		Сотрудники = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаССотрудниками.Значение.СписокСотрудников, "Ключ");
		РассчитатьСотрудникаНаСервере(Сотрудники, ТаблицаССотрудниками.Значение.ОписаниеТаблицы, Ложь);
	КонецЦикла;
	РассчитатьИтогиПоДокументу(ЭтаФорма);
	УстановитьОтображениеСообщенияОНеобходимостиПерерасчета();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписьюОкончание(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ПересчитатьДокументНаКлиенте();
		Записать(Параметры);
	Иначе
		ЗакрыватьПослеЗаписи = Ложь;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму()
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ВывестиРасчетныйЛисток(ИдентификаторыВыделенныхСтрок, ИмяТабличнойЧасти)
	
	СписокФизическихЛиц = Новый Массив;
	УникальныеФизическиеЛица = Новый Соответствие;
	Для Каждого ИдентификаторСтроки Из ИдентификаторыВыделенныхСтрок Цикл
		ФизическоеЛицо = Объект[ИмяТабличнойЧасти].НайтиПоИдентификатору(ИдентификаторСтроки).ФизическоеЛицо;
		Если УникальныеФизическиеЛица[ФизическоеЛицо] = Неопределено Тогда
			СписокФизическихЛиц.Добавить(ФизическоеЛицо);
			УникальныеФизическиеЛица.Вставить(ФизическоеЛицо, Истина);
		КонецЕсли;
	КонецЦикла;
	
	РасчетныйЛисток = Неопределено;
	Если СписокФизическихЛиц.Количество() = 1 Тогда
		РасчетныйЛисток = ПечатнаяФормаСотрудника(СписокФизическихЛиц[0], "РасчетныйЛисток");
	КонецЕсли;
	
	Если РасчетныйЛисток = Неопределено Тогда
		
		Если Объект.Проведен И Не Модифицированность Тогда
			РасчетныйЛисток = РасчетныйЛистокНаСервереБезКонтекста(Объект.Организация, СписокФизическихЛиц, Объект.МесяцНачисления);
		Иначе
			ДанныеДокумента = ДанныеДокументаФизическихЛиц(СписокФизическихЛиц);
			РасчетныйЛисток = РасчетныйЛистокНаСервереБезКонтекста(Объект.Организация, СписокФизическихЛиц, Объект.МесяцНачисления, Объект.Ссылка, ДанныеДокумента);
		КонецЕсли;
		
		Если СписокФизическихЛиц.Количество() = 1 Тогда
			ДобавитьВКоллекциюПечатнуюФормуСотрудника(СписокФизическихЛиц[0], "РасчетныйЛисток", РасчетныйЛисток);
		КонецЕсли;
		
	КонецЕсли;
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати("Отчет.АнализНачисленийИУдержаний", "РасчетныйЛистокПодробнее", 
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.Ссылка), ЭтотОбъект,
		Новый Структура("ПечатаемыйДокумент", РасчетныйЛисток));
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция РасчетныйЛистокНаСервереБезКонтекста(Организация, СписокФизическихЛиц, Месяц, Ссылка = Неопределено, ДанныеДокумента = Неопределено)
	
	Если ТипЗнч(СписокФизическихЛиц[0]) = Тип("СправочникСсылка.Сотрудники") Тогда 
		СписокФизическихЛиц = ОбщегоНазначения.ВыгрузитьКолонку(ОбщегоНазначения.ЗначениеРеквизитаОбъектов(СписокФизическихЛиц, "ФизическоеЛицо"), "Значение", Истина);
	КонецЕсли;
	
	Если ДанныеДокумента <> Неопределено Тогда
		УстановитьПривилегированныйРежим(Истина);
		НаборДанныхОтчета = ЗарплатаКадрыОтчетыРасширенный.НаборыВнешнихДанныхДокументаФизическихЛиц(ДанныеДокумента);
		УстановитьПривилегированныйРежим(Ложь);
	Иначе
		НаборДанныхОтчета = Неопределено;
	КонецЕсли;
	
	Возврат УчетНачисленнойЗарплаты.РасчетныйЛистокПоДаннымДокумента(Организация, СписокФизическихЛиц, Месяц, Ссылка, НаборДанныхОтчета);
	
КонецФункции

&НаКлиенте
Функция ДанныеДокументаФизическихЛиц(ФизическиеЛица)
	
	ТабличныеЧасти = Новый Структура("ТаблицыНачислений,ТаблицыУдержаний", Новый Структура, Новый Структура);
	
	ТабличныеЧасти.ТаблицыНачислений.Вставить("Начисления", ОписаниеТаблицыНачислений(ЭтотОбъект));
	ТабличныеЧасти.ТаблицыНачислений.Вставить("НачисленияПерерасчет", ОписаниеТаблицыПерерасчетов(ЭтотОбъект));
	ТабличныеЧасти.ТаблицыНачислений.Вставить("НачисленияПоДоговорам", ОписаниеТаблицыНачисленийПоДоговорам(ЭтотОбъект));
	ТабличныеЧасти.ТаблицыНачислений.Вставить("Пособия", ОписаниеТаблицыПособия(ЭтотОбъект));
	ТабличныеЧасти.ТаблицыНачислений.Вставить("ПособияПерерасчет", ОписаниеТаблицыПособияПерерасчет(ЭтотОбъект));
	ТабличныеЧасти.ТаблицыНачислений.Вставить("Льготы", ОписаниеТаблицыЛьгот(ЭтотОбъект));
	ТабличныеЧасти.ТаблицыНачислений.Вставить("ЛьготыПерерасчет", ОписаниеТаблицыЛьготыПерерасчет(ЭтотОбъект));
	
	ТабличныеЧасти.ТаблицыУдержаний.Вставить("НДФЛ", ОписаниеТаблицыНДФЛ());
	ТабличныеЧасти.ТаблицыУдержаний.Вставить("Удержания", ОписаниеТаблицыУдержаний());
	ТабличныеЧасти.ТаблицыУдержаний.Вставить("УдержанияПерерасчет", ОписаниеТаблицыУдержанияПерерасчет());
	ТабличныеЧасти.ТаблицыУдержаний.Вставить("ПогашениеЗаймов", ОписаниеТаблицыПогашениеЗаймов());
	
	НачисленияУдержанияПоСотрудникам = Новый Массив;
	СтруктураНачисленийУдержаний = Новый ФиксированнаяСтруктура("Период,Регистратор,Организация,ФизическоеЛицо,Сотрудник,
		|Подразделение,НачислениеУдержание,ГруппаНачисленияУдержанияВыплаты,ПериодДействия,ДокументОснование,ДатаНачала,
		|ДатаОкончания,Сумма,ОтработаноДней,ОтработаноЧасов,ОплаченоДней,ОплаченоЧасов,ВремяВЧасах");
	
	Для Каждого ФизическоеЛицо Из ФизическиеЛица Цикл
		СтрокиТаблицПоФизическомуЛицу = СтрокиТаблицДокументаПоФизическимЛицам.Получить(ФизическоеЛицо);
		Если СтрокиТаблицПоФизическомуЛицу = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого КлючИЗначение Из СтрокиТаблицПоФизическомуЛицу Цикл
			
			ИмяТаблицы = КлючИЗначение.Ключ;
			Идентификаторы = КлючИЗначение.Значение;
			Если ТабличныеЧасти.ТаблицыНачислений.Свойство(ИмяТаблицы) Тогда
				ОписаниеТаблицы = ТабличныеЧасти.ТаблицыНачислений[ИмяТаблицы];
			ИначеЕсли ТабличныеЧасти.ТаблицыУдержаний.Свойство(ИмяТаблицы) Тогда
				ОписаниеТаблицы = ТабличныеЧасти.ТаблицыУдержаний[ИмяТаблицы];
			Иначе
				Продолжить;
			КонецЕсли;
			
			ТабличнаяЧасть = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(
				ЭтотОбъект, ОписаниеТаблицы.ПутьКДанным);
			
			Для Каждого Идентификатор Из Идентификаторы Цикл
				
				СтрокаТаблицы = ТабличнаяЧасть.НайтиПоИдентификатору(Идентификатор);
				Если СтрокаТаблицы = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				Если ИмяТаблицы = "ПогашениеЗаймов" Тогда
					ЗаполнитьДанныеПогашенияЗаймовФизическогоЛица(НачисленияУдержанияПоСотрудникам, СтрокаТаблицы, СтруктураНачисленийУдержаний);
				ИначеЕсли ИмяТаблицы = "НДФЛ" Тогда
					ЗаполнитьДанныеНДФЛФизическогоЛица(НачисленияУдержанияПоСотрудникам, СтрокаТаблицы, СтруктураНачисленийУдержаний);	
				Иначе
					НоваяСтрока = НоваяСтрокаДанныхПоСотрудникам(НачисленияУдержанияПоСотрудникам, СтруктураНачисленийУдержаний);
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
					
					НоваяСтрока.Период = Объект.МесяцНачисления;
					НоваяСтрока.Организация = Объект.Организация;
					НоваяСтрока.Сумма = СтрокаТаблицы[ОписаниеТаблицы.ИмяПоляРезультат];
					
					Если ТабличныеЧасти.ТаблицыНачислений.Свойство(ИмяТаблицы) Тогда
						ЗаполнитьДанныеНачисленийФизическогоЛица(НоваяСтрока, СтрокаТаблицы, ОписаниеТаблицы);
					ИначеЕсли ТабличныеЧасти.ТаблицыУдержаний.Свойство(ИмяТаблицы) Тогда
						ЗаполнитьДанныеУдержанийФизическогоЛица(НоваяСтрока, СтрокаТаблицы, ОписаниеТаблицы);
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	Возврат НачисленияУдержанияПоСотрудникам;
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьДанныеПогашенияЗаймовФизическогоЛица(НачисленияУдержанияПоСотрудникам, СтрокаТаблицы, СтруктураНачисленийУдержаний)
	
	Если СтрокаТаблицы.МатериальнаяВыгода <> 0 Тогда
		НоваяСтрока = НоваяСтрокаДанныхПоСотрудникам(НачисленияУдержанияПоСотрудникам, СтруктураНачисленийУдержаний);
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		
		НоваяСтрока.ГруппаНачисленияУдержанияВыплаты = ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.Справочно");
		НоваяСтрока.Период = Объект.МесяцНачисления;
		НоваяСтрока.Организация = Объект.Организация;
		НоваяСтрока.Сумма = СтрокаТаблицы.МатериальнаяВыгода;
		НоваяСтрока.ПериодДействия = НоваяСтрока.Период;
		НоваяСтрока.НачислениеУдержание = ПредопределенноеЗначение("Перечисление.ВидыОсобыхНачисленийИУдержаний.МатериальнаяВыгодаПоЗаймам");
	КонецЕсли;
	
	Если СтрокаТаблицы.НалогНаМатериальнуюВыгоду <> 0 Тогда
		НоваяСтрока = НоваяСтрокаДанныхПоСотрудникам(НачисленияУдержанияПоСотрудникам, СтруктураНачисленийУдержаний);
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		
		НоваяСтрока.ГруппаНачисленияУдержанияВыплаты = ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.Удержано");
		НоваяСтрока.Период = Объект.МесяцНачисления;
		НоваяСтрока.Организация = Объект.Организация;
		НоваяСтрока.Сумма = СтрокаТаблицы.НалогНаМатериальнуюВыгоду;
		НоваяСтрока.ПериодДействия = НоваяСтрока.Период;
		НоваяСтрока.НачислениеУдержание = ПредопределенноеЗначение("Перечисление.ВидыОсобыхНачисленийИУдержаний.НДФЛ");
	КонецЕсли;
	
	Если СтрокаТаблицы.ПогашениеЗайма <> 0 Тогда
		НоваяСтрока = НоваяСтрокаДанныхПоСотрудникам(НачисленияУдержанияПоСотрудникам, СтруктураНачисленийУдержаний);
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		
		НоваяСтрока.ГруппаНачисленияУдержанияВыплаты = ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.Удержано");
		НоваяСтрока.Период = Объект.МесяцНачисления;
		НоваяСтрока.Организация = Объект.Организация;
		НоваяСтрока.Сумма = СтрокаТаблицы.ПогашениеЗайма;
		НоваяСтрока.ПериодДействия = НоваяСтрока.Период;
		НоваяСтрока.НачислениеУдержание = ПредопределенноеЗначение("Перечисление.ВидыОсобыхНачисленийИУдержаний.ПогашениеЗаймаИзЗарплаты");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДанныеНДФЛФизическогоЛица(НачисленияУдержанияПоСотрудникам, СтрокаТаблицы, СтруктураНачисленийУдержаний)
	
	ИменаРесурсовНалога = УчетНДФЛКлиентСервер.РесурсыИсчисленногоНалогаВМассиве("Налог", Ложь, Ложь);
	КолонкиДанныхПоВидамУдержаний = УчетНДФЛКлиентСервер.СоответствиеКолонокНалогаЗначениямВидовОсобыхНачисленийИУдержаний("Налог");
	ВидыУдержанийПоКолонкамДанных = Новый Соответствие;
	Для каждого КлючИЗначение Из КолонкиДанныхПоВидамУдержаний Цикл
	    ВидыУдержанийПоКолонкамДанных.Вставить(КлючИЗначение.Значение, КлючИЗначение.Ключ)
	КонецЦикла;
	Для каждого ИмяРесурса Из ИменаРесурсовНалога Цикл
		Если СтрокаТаблицы[ИмяРесурса] <> 0 Тогда
			НоваяСтрока = НоваяСтрокаДанныхПоСотрудникам(НачисленияУдержанияПоСотрудникам, СтруктураНачисленийУдержаний);
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			
			НоваяСтрока.ГруппаНачисленияУдержанияВыплаты = ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.Удержано");
			НоваяСтрока.Период = Объект.МесяцНачисления;
			НоваяСтрока.Организация = Объект.Организация;
			НоваяСтрока.Сумма = СтрокаТаблицы[ИмяРесурса];
			НоваяСтрока.ПериодДействия = НоваяСтрока.Период;
			НоваяСтрока.НачислениеУдержание = ВидыУдержанийПоКолонкамДанных[ИмяРесурса];
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДанныеНачисленийФизическогоЛица(НоваяСтрока, СтрокаТаблицы, ОписаниеТаблицы)
	
	Если ОписаниеТаблицы.ИмяТаблицы = "Льготы"
		Или ОписаниеТаблицы.ИмяТаблицы = "ЛьготыПерерасчет" Тогда
		
		НоваяСтрока.ГруппаНачисленияУдержанияВыплаты = ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.Льготы");
		
	Иначе
		НоваяСтрока.ГруппаНачисленияУдержанияВыплаты = ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.Начислено");
	КонецЕсли;
	
	Если Не ПустаяСтрока(ОписаниеТаблицы.ИмяРеквизитаВидРасчета) Тогда
		НоваяСтрока.НачислениеУдержание = СтрокаТаблицы[ОписаниеТаблицы.ИмяРеквизитаВидРасчета];
	ИначеЕсли ОписаниеТаблицы.ИмяТаблицы = "НачисленияПоДоговорам" Тогда
		НоваяСтрока.НачислениеУдержание = НачислениеДоговораГПХПоДокументуОснованию(СтрокаТаблицы.ДокументОснование);
	КонецЕсли;
	
	Если ОписаниеТаблицы.Свойство("ЗаполнятьОплаченноеВремяОтработанным") Тогда
		НоваяСтрока.ОплаченоДней = НоваяСтрока.ОтработаноДней;
		НоваяСтрока.ОплаченоЧасов = НоваяСтрока.ОплаченоЧасов;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(НоваяСтрока.ПериодДействия) Тогда
		НоваяСтрока.ПериодДействия = НачалоМесяца(НоваяСтрока.ДатаНачала);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДанныеУдержанийФизическогоЛица(НоваяСтрока, СтрокаТаблицы, ОписаниеТаблицы)
	
	Если ОписаниеТаблицы.Свойство("ИмяРеквизитаФизическоеЛицо") Тогда
		НоваяСтрока.ФизическоеЛицо = СтрокаТаблицы[ОписаниеТаблицы.ИмяРеквизитаФизическоеЛицо];
	КонецЕсли;
	
	НоваяСтрока.ГруппаНачисленияУдержанияВыплаты = ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.Удержано");
	НоваяСтрока.ПериодДействия = НоваяСтрока.Период;
	НоваяСтрока.НачислениеУдержание = СтрокаТаблицы[ОписаниеТаблицы.ИмяРеквизитаВидРасчета];
	
КонецПроцедуры

&НаСервере
Функция ОписанияТаблицНачисленийДляРасчетногоЛистка() Экспорт 
	
	ОписанияТаблиц = Новый Массив;
	
	Если ИспользоватьНачисление Тогда 
		ОписанияТаблиц.Добавить(ОписаниеТаблицыНачислений(ЭтаФорма));
		ОписанияТаблиц.Добавить(ОписаниеТаблицыПерерасчетов(ЭтаФорма));
	КонецЕсли;
	
	Если ИспользоватьДоговоры Тогда 
		ОписанияТаблиц.Добавить(ОписаниеТаблицыНачисленийПоДоговорам(ЭтаФорма));
	КонецЕсли;
	
	Если ИспользоватьПособия Тогда 
		ОписанияТаблиц.Добавить(ОписаниеТаблицыПособия(ЭтаФорма));
		ОписанияТаблиц.Добавить(ОписаниеТаблицыПособияПерерасчет(ЭтаФорма));
	КонецЕсли;
	
	Если ИспользоватьЛьготы Тогда 
		ОписанияТаблиц.Добавить(ОписаниеТаблицыЛьгот(ЭтаФорма));
		ОписанияТаблиц.Добавить(ОписаниеТаблицыЛьготыПерерасчет(ЭтаФорма));
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.УправленческаяЗарплата") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("УправленческаяЗарплатаКлиентСервер");
		Модуль.ПриЗаполненииМассиваОписанийТаблицНачисленияЗарплаты(ОписанияТаблиц, Ложь, ИспользоватьНачисление);
	КонецЕсли;
	
	Возврат ОписанияТаблиц;
	
КонецФункции

&НаСервере
Функция ОписанияТаблицУдержанийДляРасчетногоЛистка() Экспорт 
	
	ОписанияТаблиц = Новый Массив;
	
	Если ИспользоватьУдержания Тогда 
		ОписанияТаблиц.Добавить(ОписаниеТаблицыУдержаний());
		ОписанияТаблиц.Добавить(ОписаниеТаблицыУдержанияПерерасчет());
	КонецЕсли;
	
	Если ИспользоватьНДФЛ Тогда 
		ОписанияТаблиц.Добавить(ОписаниеТаблицыНДФЛ());
		ОписанияТаблиц.Добавить(ОписаниеТаблицыКорректировкиВыплаты());
	КонецЕсли;
	
	Если ИспользоватьЗаймы Тогда 
		ОписанияТаблиц.Добавить(ОписаниеТаблицыПогашениеЗаймов());
	КонецЕсли;
	
	Возврат ОписанияТаблиц;
	
КонецФункции

&НаКлиенте
Функция ЕстьИсправленныеСтроки() Экспорт
	Возврат ЕстьИсправленныеСтрокиНаСервере();
КонецФункции

&НаСервере
Функция ЕстьИсправленныеСтрокиНаСервере()
	Возврат РасчетЗарплатыРасширенныйФормы.ЕстьИсправленныеСтроки(ЭтаФорма);
КонецФункции

&НаКлиенте
Процедура ОповеститьОбИзмененииОбъекта()
	
	ОповеститьОбИзменении(Объект.Ссылка);
	
	ПараметрыОповещения = Новый Структура("Организация,МесяцНачисления,РежимДоначисления");
	ЗаполнитьЗначенияСвойств(ПараметрыОповещения, Объект);
	
	Оповестить("Запись_НачислениеЗарплаты", ПараметрыОповещения, Объект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ПересчитатьСотрудниковНаСервере()
	
	Если ЗначениеЗаполнено(Объект.Подразделение) Тогда
		СотрудникиДокумента = Объект.Начисления.Выгрузить(, "Сотрудник").ВыгрузитьКолонку("Сотрудник");
	Иначе
		СотрудникиДокумента = Неопределено;
	КонецЕсли;
	
	СотрудникиКПерерасчету = ПерерасчетЗарплаты.СотрудникиКПерерасчету(Объект.Организация, Объект.МесяцНачисления, СотрудникиДокумента);
	ПерезаполнитьДанныеФормыНаСервере(СотрудникиКПерерасчету, Ложь);
	
	ЗаполнениеВыполнено = Истина;
	РассчитатьИтогиПоДокументу(ЭтаФорма);
	УстановитьОтображениеСообщенияОНеобходимостиПерерасчета();
	
КонецПроцедуры

&НаСервере
Функция АдресСпискаСотрудниковВХранилище()
	Возврат ПоместитьВоВременноеХранилище(Объект.Начисления.Выгрузить(, "Сотрудник").ВыгрузитьКолонку("Сотрудник"), УникальныйИдентификатор);
КонецФункции

&НаСервере
Процедура ОчиститьТаблицыДокументаНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	Для Каждого ТабличнаяЧасть Из Метаданные.Документы.НачислениеЗарплаты.ТабличныеЧасти Цикл 
		Если Объект.Свойство(ТабличнаяЧасть.Имя) Тогда 
			Объект[ТабличнаяЧасть.Имя].Очистить();
		КонецЕсли;
	КонецЦикла;
	СтрокиТаблицДокументаПоФизическимЛицам = Неопределено;
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаСервере
Процедура УдалитьВыделенныеПерерасчеты(Знач ВыделенныеСтроки, Знач ТаблицаСотрудников, ПерерасчетНачислений = Ложь, ПерерасчетПособий = Ложь, ПерерасчетЛьгот = Ложь, ПерерасчетУдержаний = Ложь)
	
	Если Объект.Проведен Тогда
		Возврат;
	КонецЕсли;
	
	СотрудникиПериоды = Новый ТаблицаЗначений;
	СотрудникиПериоды.Колонки.Добавить("Сотрудник");
	СотрудникиПериоды.Колонки.Добавить("ПериодДействия");
	СотрудникиПериоды.Колонки.Добавить("Количество");
	Для каждого ИдентификаторСтроки Из ВыделенныеСтроки Цикл
		СтрокаТаблицы = ТаблицаСотрудников.НайтиПоИдентификатору(ИдентификаторСтроки);
		Если СтрокаТаблицы = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока = СотрудникиПериоды.Добавить();
		НоваяСтрока.Количество = 1;
		Если ПерерасчетУдержаний Тогда
			НоваяСтрока.Сотрудник = СтрокаТаблицы.ФизическоеЛицо;
			НоваяСтрока.ПериодДействия = НачалоМесяца(СтрокаТаблицы.ДатаНачала);
		Иначе
			НоваяСтрока.Сотрудник = СтрокаТаблицы.Сотрудник;
			НоваяСтрока.ПериодДействия = СтрокаТаблицы.ПериодДействия;
		КонецЕсли;
	КонецЦикла;
	
	СотрудникиПериоды.Свернуть("Сотрудник,ПериодДействия", "Количество"); 

	Для каждого СтрокаПериода Из СотрудникиПериоды Цикл
		СтруктураПоиска = Новый Структура;
		КоличествоНайденныхСтрок = 0;
		Если ПерерасчетУдержаний Тогда
			СтруктураПоиска.Вставить("ФизическоеЛицо", СтрокаПериода.Сотрудник);
			НайденныеСтроки = ТаблицаСотрудников.НайтиСтроки(СтруктураПоиска);
			Для каждого НайденнаяСтрока Из НайденныеСтроки Цикл
				Если НачалоМесяца(НайденнаяСтрока.ДатаНачала) = СтрокаПериода.ПериодДействия Тогда
					КоличествоНайденныхСтрок = КоличествоНайденныхСтрок + 1;
				КонецЕсли;
			КонецЦикла;
		Иначе
			СтруктураПоиска.Вставить("Сотрудник", СтрокаПериода.Сотрудник);
			СтруктураПоиска.Вставить("ПериодДействия", СтрокаПериода.ПериодДействия);
			НайденныеСтроки = ТаблицаСотрудников.НайтиСтроки(СтруктураПоиска);
			КоличествоНайденныхСтрок = НайденныеСтроки.Количество();
		КонецЕсли;
		
		Если СтрокаПериода.Количество <> КоличествоНайденныхСтрок Тогда
			Продолжить;
		КонецЕсли;
		
		Если ПерерасчетНачислений Тогда
			НайденныеСтрокиПособий = Объект.ПособияПерерасчет.НайтиСтроки(СтруктураПоиска);
			Если НайденныеСтрокиПособий.Количество() > 0 Тогда
				Продолжить;
			КонецЕсли;
		ИначеЕсли ПерерасчетПособий Тогда
			НайденныеСтрокиНачислений = Объект.НачисленияПерерасчет.НайтиСтроки(СтруктураПоиска);
			Если НайденныеСтрокиНачислений.Количество() > 0 Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		СтруктураПоискаПерерасчета = Новый Структура("Сотрудник,ПериодДействия");
		ЗаполнитьЗначенияСвойств(СтруктураПоискаПерерасчета, СтрокаПериода);
		СтруктураПоискаПерерасчета.Вставить("ПерерасчетНачислений", ПерерасчетНачислений Или ПерерасчетПособий);
		СтруктураПоискаПерерасчета.Вставить("ПерерасчетУдержаний", ПерерасчетУдержаний);
		СтруктураПоискаПерерасчета.Вставить("ПерерасчетЛьгот", ПерерасчетЛьгот);
		СтрокиКУдалению = Объект.СотрудникиПерерасчет.НайтиСтроки(СтруктураПоискаПерерасчета);
		Для каждого СтрокаУдаления Из СтрокиКУдалению Цикл
			Объект.СотрудникиПерерасчет.Удалить(СтрокаУдаления);	
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьКомандуУдалитьСотрудника()
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьРасчетЗарплатыРасширенная") Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеДокумента = ОписаниеДокумента(ЭтотОбъект);
	
	ТаблицыДокумента = Новый Массив;
	ТаблицыДокумента.Добавить("Начисления");
	ТаблицыДокумента.Добавить("НачисленияПоДоговорам");
	ТаблицыДокумента.Добавить("НачисленияПерерасчет");
	ТаблицыДокумента.Добавить("Пособия");
	ТаблицыДокумента.Добавить("ПособияПерерасчет");
	ТаблицыДокумента.Добавить("Льготы");
	ТаблицыДокумента.Добавить("ЛьготыПерерасчет");
	ТаблицыДокумента.Добавить("Удержания");
	ТаблицыДокумента.Добавить("УдержанияПерерасчет");
	ТаблицыДокумента.Добавить("НДФЛ");
	ТаблицыДокумента.Добавить("ПогашениеЗаймов");
	ТаблицыДокумента.Добавить("Взносы");
	
	Для Каждого ТаблицаДокумента Из ТаблицыДокумента Цикл
		
		ИмяТаблицы = ОписаниеДокумента[ТаблицаДокумента + "Имя"];
		Если ИмяТаблицы <> Неопределено Тогда
			
			ИмяКоманднойПанели = Неопределено;
			
			ОписаниеДокумента.Свойство(ТаблицаДокумента + "КоманднаяПанельИмя", ИмяКоманднойПанели);
			ДобавитьКомандуУдалитьСотрудникаВТаблицуДокумента(ИмяТаблицы, ИмяКоманднойПанели);

		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьКомандуУдалитьСотрудникаВТаблицуДокумента(ИмяТаблицы, ИмяКоманднойПанели)
	
	Если Элементы.Найти(ИмяТаблицы) = Неопределено Тогда
		// Такой таблицы нет в форме
		Возврат;
	КонецЕсли;
	
	Если Команды.Найти(ИмяТаблицы + "УдалитьСотрудника") <> Неопределено Тогда
		// Команда уже существует
		Возврат;
	КонецЕсли;
	
	// Добавляем команду формы
	КомандаУдалитьСотрудника = Команды.Добавить(ИмяТаблицы + "УдалитьСотрудника");
	КомандаУдалитьСотрудника.Действие = "Подключаемый_" + ИмяТаблицы + "УдалитьСотрудника";
	КомандаУдалитьСотрудника.Заголовок = НСтр("ru = 'Удалить сотрудника';
												|en = 'Remove employee'");
	КомандаУдалитьСотрудника.Подсказка = НСтр("ru = 'Данные по выбранному сотруднику удаляются со всех таблиц документа';
												|en = 'Data on the selected employee is deleted from all tables of the document'");
	КомандаУдалитьСотрудника.Картинка = БиблиотекаКартинок.Удалить;
	КомандаУдалитьСотрудника.Отображение = ОтображениеКнопки.КартинкаИТекст;
	КомандаУдалитьСотрудника.ИзменяетСохраняемыеДанные = Истина;
	
	// Добавляем элементы (группу кнопок и кнопку) в командную панель.
	Если ИмяКоманднойПанели = Неопределено Тогда
		ИмяКоманднойПанели = ИмяТаблицы + "КоманднаяПанель";
	КонецЕсли;
	
	КоманднаяПанельТаблицы = Элементы.Найти(ИмяКоманднойПанели);
	Если КоманднаяПанельТаблицы <> Неопределено Тогда
		Если КоманднаяПанельТаблицы.ПодчиненныеЭлементы.Количество() = 0 Тогда
			МестоВставки = Неопределено;
		Иначе
			МестоВставки = КоманднаяПанельТаблицы.ПодчиненныеЭлементы[0];
		КонецЕсли;
		
		ИмяЭлемента = ИмяТаблицы + "КоманднаяПанельПересчитатьСотрудникаГруппа";
		ГруппаКнопок = Элементы.Найти(ИмяЭлемента);
		Если ГруппаКнопок = Неопределено Тогда
			ГруппаКнопок = Элементы.Вставить(ИмяЭлемента, Тип("ГруппаФормы"), КоманднаяПанельТаблицы, МестоВставки);
			ГруппаКнопок.Вид = ВидГруппыФормы.ГруппаКнопок;
			ГруппаКнопок.Заголовок = НСтр("ru = 'Обновление строк документа';
											|en = 'Update document lines'");
		КонецЕсли;
		
		ИмяЭлемента = ИмяТаблицы + "КоманднаяПанельУдалитьСотрудника";
		Если Элементы.Найти(ИмяЭлемента) = Неопределено Тогда
			КнопкаУдалитьСотрудника = Элементы.Добавить(ИмяЭлемента, Тип("КнопкаФормы"), ГруппаКнопок);
			КнопкаУдалитьСотрудника.ИмяКоманды = ИмяТаблицы + "УдалитьСотрудника";
			КнопкаУдалитьСотрудника.ТолькоВоВсехДействиях = Истина;
		КонецЕсли;
		
		// Добавляем элементы в контекстное меню.
		КонтекстноеМеню = Элементы[ИмяТаблицы].КонтекстноеМеню;
		
		МестоВставки = Неопределено;
		Если КонтекстноеМеню.ПодчиненныеЭлементы.Количество() > 0 Тогда
			МестоВставки = КонтекстноеМеню.ПодчиненныеЭлементы[0];
		КонецЕсли;
		
		ИмяЭлемента = ИмяТаблицы + "КонтекстноеМенюПересчитатьСотрудникаГруппа";
		ГруппаКнопок = Элементы.Найти(ИмяЭлемента);
		Если ГруппаКнопок = Неопределено Тогда
			ГруппаКнопок = Элементы.Вставить(ИмяЭлемента, Тип("ГруппаФормы"), КонтекстноеМеню, МестоВставки);
			ГруппаКнопок.Вид = ВидГруппыФормы.ГруппаКнопок;
			ГруппаКнопок.Заголовок = НСтр("ru = 'Обновление строк документа';
											|en = 'Update document lines'");
		КонецЕсли;
		
		ИмяЭлемента = ИмяТаблицы + "КонтекстноеМенюУдалитьСотрудника";
		Если Элементы.Найти(ИмяЭлемента) = Неопределено Тогда
			КнопкаУдалитьСотрудника = Элементы.Добавить(ИмяЭлемента, Тип("КнопкаФормы"), ГруппаКнопок);
			КнопкаУдалитьСотрудника.ИмяКоманды = ИмяТаблицы + "УдалитьСотрудника";
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьСотрудникаНаСервере(Сотрудник)
	
	Если Не УдалитьДанныеСотрудникаНаСервере(Сотрудник) Тогда 
		РассчитатьСотрудникаНаСервере(Сотрудник);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция УдалитьДанныеСотрудникаНаСервере(СотрудникФизическоеЛицо)
	
	УдаляемыеСотрудники = Новый Соответствие;
	ДополнительныеСотрудники = Новый Соответствие;
	
	Если ТипЗнч(СотрудникФизическоеЛицо) = Тип("СправочникСсылка.Сотрудники") Тогда 
		УдаляемыеСотрудники.Вставить(СотрудникФизическоеЛицо, Истина);
		Если ЗначениеЗаполнено(СотрудникФизическоеЛицо) Тогда
			ФизическоеЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СотрудникФизическоеЛицо, "ФизическоеЛицо");
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Сотрудник", СотрудникФизическоеЛицо);
			Запрос.УстановитьПараметр("ФизическоеЛицо", ФизическоеЛицо);
			
			Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
			               |	Сотрудники.Ссылка
			               |ИЗ
			               |	Справочник.Сотрудники КАК Сотрудники
			               |ГДЕ
			               |	Сотрудники.Ссылка <> &Сотрудник
			               |	И Сотрудники.ФизическоеЛицо = &ФизическоеЛицо";
						   
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл 
				ДополнительныеСотрудники.Вставить(Выборка.Ссылка, Истина);
			КонецЦикла;
		Иначе
			ФизическоеЛицо = Справочники.ФизическиеЛица.ПустаяСсылка();
		КонецЕсли;
	Иначе 
		
		ФизическоеЛицо = СотрудникФизическоеЛицо;
		
		Запрос = Новый Запрос;
		
		Запрос.УстановитьПараметр("ФизическоеЛицо", ФизическоеЛицо);
		
		Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	Сотрудники.Ссылка
		               |ИЗ
		               |	Справочник.Сотрудники КАК Сотрудники
		               |ГДЕ
		               |	Сотрудники.ФизическоеЛицо = &ФизическоеЛицо";
					   
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл 
			УдаляемыеСотрудники.Вставить(Выборка.Ссылка, Истина);
		КонецЦикла;
		
	КонецЕсли;
	
	ОчиститьУдержания = Истина;
	
	ИменаТаблиц = Новый Массив;
	ИменаТаблиц.Добавить("Начисления");
	ИменаТаблиц.Добавить("НачисленияПерерасчет");
	ИменаТаблиц.Добавить("НачисленияПоДоговорам");
	ИменаТаблиц.Добавить("Пособия");
	ИменаТаблиц.Добавить("ПособияПерерасчет");
	ИменаТаблиц.Добавить("Льготы");
	ИменаТаблиц.Добавить("ЛьготыПерерасчет");
	Если Не Объект.Проведен Тогда
		ИменаТаблиц.Добавить("СотрудникиПерерасчет");
	КонецЕсли;
	
	Для Каждого ИмяТаблицы Из ИменаТаблиц Цикл 
	
		СтрокиСотрудника = Новый Массив;
		Для Каждого СтрокаСотрудника Из Объект[ИмяТаблицы] Цикл 
			Если УдаляемыеСотрудники[СтрокаСотрудника.Сотрудник] <> Неопределено Тогда 
		    	СтрокиСотрудника.Добавить(СтрокаСотрудника);
			ИначеЕсли ДополнительныеСотрудники[СтрокаСотрудника.Сотрудник] <> Неопределено Тогда 
				ОчиститьУдержания = Ложь;
			КонецЕсли;
		КонецЦикла;
	
		Для Каждого СтрокаСотрудника Из СтрокиСотрудника Цикл 
			Объект[ИмяТаблицы].Удалить(СтрокаСотрудника);
		КонецЦикла;
		
	КонецЦикла;
	
	Если Не ОчиститьУдержания Тогда 
		УстановитьДоступностьКомандыЗаполнитьССохранениемИсправлений();
		Возврат Ложь;
	КонецЕсли;
	
	ИменаТаблиц = Новый Массив;
	ИменаТаблиц.Добавить("Взносы");
	ИменаТаблиц.Добавить("НДФЛ");
	ИменаТаблиц.Добавить("ПримененныеВычетыНаДетейИИмущественные");
	ИменаТаблиц.Добавить("ПогашениеЗаймов");
	ИменаТаблиц.Добавить("Удержания");
	ИменаТаблиц.Добавить("УдержанияПерерасчет");
	ИменаТаблиц.Добавить("КорректировкиВыплаты");
	ИменаТаблиц.Добавить("УчтенныеПриРасчетеНДФЛДокументы");
	
	СтрокиТаблицФизическогоЛица = СтрокиТаблицФизическогоЛица(СтрокиТаблицДокументаПоФизическимЛицам, ФизическоеЛицо);
	Для Каждого ИмяТаблицы Из ИменаТаблиц Цикл 
		Если Не СтрокиТаблицФизическогоЛица.Свойство(ИмяТаблицы) Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокиФизическогоЛица = СтрокиТаблицФизическогоЛица[ИмяТаблицы];
		Для Каждого СтрокаФизическогоЛица Из СтрокиФизическогоЛица Цикл 
			СтрокаТаблицы = Объект[ИмяТаблицы].НайтиПоИдентификатору(СтрокаФизическогоЛица);
			Если СтрокаТаблицы = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			Объект[ИмяТаблицы].Удалить(СтрокаТаблицы);
		КонецЦикла;
	КонецЦикла;
	
	РассчитатьИтогиПоДокументу(ЭтаФорма);
	УстановитьОтображениеСообщенияОНеобходимостиПерерасчета();
	УстановитьДоступностьКомандыЗаполнитьССохранениемИсправлений();
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура УстановитьДоступностьКомандыЗаполнитьССохранениемИсправленийПослеУдаленияСтроки()
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗаполнитьССохранениемИсправлений", "Доступность", Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьКомандыЗаполнитьССохранениемИсправленийПриОкончанииРедактированияСтроки(ТекущиеДанные)
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ЕстьИсправления = ТекущиеДанные.Свойство("ФиксРасчет") И ТекущиеДанные.ФиксРасчет
		Или ТекущиеДанные.Свойство("ФиксРасчетВремени") И ТекущиеДанные.ФиксРасчетВремени
		Или ТекущиеДанные.Свойство("ФиксЗаполнение") И ТекущиеДанные.ФиксЗаполнение
		Или ТекущиеДанные.Свойство("ФиксСтрока") И ТекущиеДанные.ФиксСтрока;
		
	Если ЕстьИсправления Тогда 	
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗаполнитьССохранениемИсправлений", "Доступность", Истина);
	КонецЕсли;
		
КонецПроцедуры	

&НаСервере
Процедура УстановитьДоступностьКомандыЗаполнитьССохранениемИсправлений(ЕстьИсправленныеСтроки = Неопределено)
	
	Если ЕстьИсправленныеСтроки = Неопределено Тогда 
		ЕстьИсправленныеСтроки = ЕстьИсправленныеСтрокиНаСервере();
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, 
		"ЗаполнитьССохранениемИсправлений", "Доступность", ЕстьИсправленныеСтроки);
		
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьКомандыДозаполнить()
	
	ИменаТаблиц = Новый Массив;
	ИменаТаблиц.Добавить("Начисления");
	ИменаТаблиц.Добавить("НачисленияПерерасчет");
	ИменаТаблиц.Добавить("НачисленияПоДоговорам");
	ИменаТаблиц.Добавить("Пособия");
	ИменаТаблиц.Добавить("ПособияПерерасчет");
	ИменаТаблиц.Добавить("Льготы");
	ИменаТаблиц.Добавить("Взносы");
	ИменаТаблиц.Добавить("НДФЛ");
	ИменаТаблиц.Добавить("ПогашениеЗаймов");
	ИменаТаблиц.Добавить("Удержания");
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПустойДокумент = Истина;
	Для Каждого ИмяТаблицы Из ИменаТаблиц Цикл 
		Если Объект[ИмяТаблицы].Количество() > 0 Тогда 
			ПустойДокумент = Ложь;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Дозаполнить", "Доступность", Не ПустойДокумент);
	
КонецПроцедуры

&НаКлиенте
Процедура НачислениеЗарплатыВоеннослужащимПриИзменении(Элемент)
	
	ЗарплатаКадрыРасширенныйКлиент.КлючевыеРеквизитыЗаполненияФормыОчиститьТаблицы(ЭтаФорма);
	
КонецПроцедуры

// НДФЛ
&НаСервере
Функция НДФЛПодробнееНаСервере(ФизическиеЛица) Экспорт
	
	Если ТипЗнч(ФизическиеЛица) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		СписокФизическихЛиц = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФизическиеЛица);
	Иначе
		СписокФизическихЛиц = ФизическиеЛица;
	КонецЕсли;
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	РеквизитыВДанные(ДокументОбъект);
	
	Возврат УчетНДФЛФормы.РегистрНалоговогоУчетаПоНДФЛ(ДокументОбъект, Модифицированность, СписокФизическихЛиц, Объект.МесяцНачисления);
	
КонецФункции

&НаСервереБезКонтекста
Функция ТребуетсяЗаполнитьДатуВыплаты(МесяцНачисления, РежимДоначисления)
	
	Возврат УчетНДФЛ.ДоходыУчитываютсяТолькоПоДатеВыплаты(МесяцНачисления, -1) Или РежимДоначисления	
	
КонецФункции

&НаСервереБезКонтекста
Функция ДатаВыплаты(Организация, МесяцНачисления, ПланируемаяДатаВыплаты)
	
	Возврат ?(УчетНДФЛ.ДоходыУчитываютсяТолькоПоДатеВыплаты(МесяцНачисления, -1), ПланируемаяДатаВыплаты, РасчетЗарплатыРасширенныйКлиентСервер.ПланируемаяДатаВыплатыЗарплаты(Организация, МесяцНачисления));
	
КонецФункции

&НаСервере
Процедура ЗаполнитьВторичныеДанныеФормыПримененныеВычеты()
	
	УчетНДФЛФормы.ЗаполнитьВторичныеДанныеТабличныхЧастей(ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура РассчитатьИтогНачислено(Форма)
	
	ОписанияСоставляющихНачисления = Новый Массив;
	ОписанияСоставляющихНачисления.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеСуммируемогоРеквизита("Результат", НСтр("ru = 'зарплата';
																										|en = 'salary'")));
	
	ОписанияСоставляющихНачисленияПоДоговорам = Новый Массив;
	ОписанияСоставляющихНачисленияПоДоговорам.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеСуммируемогоРеквизита("Результат", НСтр("ru = 'по договорам ГПХ';
																										|en = 'by civil law contracts '")));
	
	ОписанияСоставляющихПособия = Новый Массив;
	ОписанияСоставляющихПособия.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеСуммируемогоРеквизита("Результат", НСтр("ru = 'пособия';
																										|en = 'allowances'")));
		
	ЛьготыСотрудниковСуществует = ОбщегоНазначенияБЗККлиентСервер.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ЛьготыСотрудников");
		
	Если ЛьготыСотрудниковСуществует Тогда
		ОписанияСоставляющихЛьготы = Новый Массив;
		ОписанияСоставляющихЛьготы.Добавить(
			ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеСуммируемогоРеквизита("Результат", НСтр("ru = 'льготы';
																											|en = 'benefits'")));
	КонецЕсли;
		
	ОписанияСоставляющих = Новый Массив;
	ОписанияСоставляющих.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеТабличнойЧасти("Объект.Начисления", ОписанияСоставляющихНачисления));
	ОписанияСоставляющих.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеТабличнойЧасти("Объект.НачисленияПоДоговорам", ОписанияСоставляющихНачисленияПоДоговорам));
	ОписанияСоставляющих.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеТабличнойЧасти("Объект.Пособия", ОписанияСоставляющихПособия));
		
	Если ЛьготыСотрудниковСуществует Тогда
		ОписанияСоставляющих.Добавить(
			ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеТабличнойЧасти("Объект.Льготы", ОписанияСоставляющихЛьготы));
	КонецЕсли;
		
	ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейРассчитатьИтог(Форма, "ИтогНачислено", ОписанияСоставляющих);
		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура РассчитатьИтогНачисленоПерерасчет(Форма)
	
	ОписанияСоставляющихНачисления = Новый Массив;
	ОписанияСоставляющихНачисления.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеСуммируемогоРеквизита("Результат", НСтр("ru = 'перерасчет зарплаты';
																										|en = 'salary recalculation'")));
	
	ОписанияСоставляющихПособия = Новый Массив;
	ОписанияСоставляющихПособия.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеСуммируемогоРеквизита("Результат", НСтр("ru = 'перерасчет пособий';
																										|en = 'allowance recalculation'")));
		
	ЛьготыСотрудниковСуществует = ОбщегоНазначенияБЗККлиентСервер.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ЛьготыСотрудников");
		
	Если ЛьготыСотрудниковСуществует Тогда
		ОписанияСоставляющихЛьготы = Новый Массив;
		ОписанияСоставляющихЛьготы.Добавить(
			ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеСуммируемогоРеквизита("Результат", НСтр("ru = 'перерасчет льгот';
																											|en = 'benefit recalculation'")));
	КонецЕсли;
		
	ОписанияСоставляющих = Новый Массив;
	ОписанияСоставляющих.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеТабличнойЧасти("Объект.НачисленияПерерасчет", ОписанияСоставляющихНачисления));
	ОписанияСоставляющих.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеТабличнойЧасти("Объект.ПособияПерерасчет", ОписанияСоставляющихПособия));
		
	Если ЛьготыСотрудниковСуществует Тогда
		ОписанияСоставляющих.Добавить(
			ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеТабличнойЧасти("Объект.ЛьготыПерерасчет", ОписанияСоставляющихЛьготы));
	КонецЕсли;
		
	ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейРассчитатьИтог(Форма, "ИтогНачисленоПерерасчет", ОписанияСоставляющих);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура РассчитатьИтогУдержано(Форма)
	
	ОписанияСоставляющихУдержания = Новый Массив;
	ОписанияСоставляющихУдержания.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеСуммируемогоРеквизита("Результат", НСтр("ru = 'удержания';
																										|en = 'deductions'")));

	ОписанияСоставляющихНДФЛ = Новый Массив;
	ОписанияСоставляющихНДФЛ.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеСуммируемогоРеквизита("Налог", НСтр("ru = 'НДФЛ';
																									|en = 'PIT'")));
	ОписанияСоставляющихНДФЛ.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеСуммируемогоРеквизита("НалогСПревышения", НСтр("ru = 'НДФЛ с прев. 15%';
																											|en = 'НДФЛ с прев. 15%'")));
	ОписанияСоставляющихНДФЛ.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеСуммируемогоРеквизита("НалогСПревышенияПоСтавке18", НСтр("ru = 'НДФЛ с прев. 18%';
																														|en = 'НДФЛ с прев. 18%'")));
	ОписанияСоставляющихНДФЛ.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеСуммируемогоРеквизита("НалогСПревышенияПоСтавке20", НСтр("ru = 'НДФЛ с прев. 20%';
																														|en = 'НДФЛ с прев. 20%'")));
	ОписанияСоставляющихНДФЛ.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеСуммируемогоРеквизита("НалогСПревышенияПоСтавке22", НСтр("ru = 'НДФЛ с прев. 22%';
																														|en = 'НДФЛ с прев. 22%'")));
	
	ОписанияСоставляющихПогашениеЗаймов = Новый Массив;
	ОписанияСоставляющихПогашениеЗаймов.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеСуммируемогоРеквизита("ПогашениеПроцентов", НСтр("ru = 'погашено процентов по займам';
																												|en = 'repaid loan interest'")));
	ОписанияСоставляющихПогашениеЗаймов.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеСуммируемогоРеквизита("ПогашениеЗайма", НСтр("ru = 'погашено займов';
																											|en = 'repaid loans'")));
	ОписанияСоставляющихПогашениеЗаймов.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеСуммируемогоРеквизита("НалогНаМатериальнуюВыгоду", НСтр("ru = 'налог на материальную выгоду';
																														|en = 'financial gain tax'")));
	
	ОписанияСоставляющих = Новый Массив;
	ОписанияСоставляющих.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеТабличнойЧасти("Объект.Удержания", ОписанияСоставляющихУдержания));
	ОписанияСоставляющих.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеТабличнойЧасти("Объект.УдержанияПерерасчет", ОписанияСоставляющихУдержания));	
	ОписанияСоставляющих.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеТабличнойЧасти("Объект.НДФЛ", ОписанияСоставляющихНДФЛ));
	ОписанияСоставляющих.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеТабличнойЧасти("Объект.ПогашениеЗаймов", ОписанияСоставляющихПогашениеЗаймов));
	
	ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейРассчитатьИтог(Форма, "ИтогУдержано", ОписанияСоставляющих);
		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура РассчитатьИтогВзносы(Форма)
	
	ПоляВзносов = УчетСтраховыхВзносовКлиентСервер.ОписаниеПолейВзносовВСоответствии(Форма.Объект.МесяцНачисления);
	
	ОписанияСоставляющихВзносы = Новый Массив;
	Для каждого ОписаниеПоля Из ПоляВзносов Цикл
		ОписанияСоставляющихВзносы.Добавить(
			ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеСуммируемогоРеквизита(ОписаниеПоля.Ключ, ОписаниеПоля.Значение));
	КонецЦикла;
	
	ОписанияСоставляющих = Новый Массив;
	ОписанияСоставляющих.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеТабличнойЧасти("Объект.Взносы", ОписанияСоставляющихВзносы));
	
	ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейРассчитатьИтог(Форма, "ИтогВзносы", ОписанияСоставляющих);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура РассчитатьИтогиПоДокументу(Форма)
	
	РассчитатьИтогНачислено(Форма);
	РассчитатьИтогНачисленоПерерасчет(Форма);
	РассчитатьИтогУдержано(Форма);
	РассчитатьИтогВзносы(Форма);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтображениеСообщенияОНеобходимостиПерерасчета()
	
	ПерерасчетЗарплаты.УстановитьОтображениеСообщенияОНеобходимостиПерерасчетаВФормеДокумента(ЭтаФорма, Объект.Ссылка);
	
КонецПроцедуры

// Контролируемые поля
&НаСервере
Функция ПолучитьКонтролируемыеПоля() Экспорт
	
	Возврат КонтролируемыеПоля();
	
КонецФункции

&НаСервереБезКонтекста
Функция КонтролируемыеПоля() Экспорт
	
	КонтролируемыеПоля = Новый Структура;
	
	// Начисления
	НачисленияФиксРасчет = Новый Массив;
	НачисленияФиксРасчет.Добавить("Результат");
	
	НачисленияФиксРасчетВремени = Новый Массив;
	НачисленияФиксРасчетВремени.Добавить("ОтработаноДней");
	НачисленияФиксРасчетВремени.Добавить("ОтработаноЧасов");
	НачисленияФиксРасчетВремени.Добавить("ОплаченоДней");
	НачисленияФиксРасчетВремени.Добавить("ОплаченоЧасов");
	НачисленияФиксРасчетВремени.Добавить("НормаДней");
	НачисленияФиксРасчетВремени.Добавить("НормаЧасов");
	
	НачисленияФиксСтрока = Новый Массив;
	НачисленияФиксСтрока.Добавить("Сотрудник");
	НачисленияФиксСтрока.Добавить("Подразделение");
	НачисленияФиксСтрока.Добавить("Начисление");
	НачисленияФиксСтрока.Добавить("ДатаНачала");
	НачисленияФиксСтрока.Добавить("ДатаОкончания");
	
	НачисленияФиксЗаполнение = Новый Массив;
	НачисленияФиксЗаполнение.Добавить("МестоПолученияДохода");
	
	НачисленияПоля = Новый Структура;
	НачисленияПоля.Вставить("ФиксРасчет", НачисленияФиксРасчет);
	НачисленияПоля.Вставить("ФиксЗаполнение", НачисленияФиксЗаполнение);
	НачисленияПоля.Вставить("ФиксСтрока", НачисленияФиксСтрока);
	НачисленияПоля.Вставить("ФиксРасчетВремени", НачисленияФиксРасчетВремени);
	
	КонтролируемыеПоля.Вставить("Начисления", НачисленияПоля);
	
	// Начисления перерасчет
	НачисленияПерерасчетПоля = Новый Структура;
	НачисленияПерерасчетПоля.Вставить("ФиксРасчет", НачисленияФиксРасчет);
	НачисленияПерерасчетПоля.Вставить("ФиксЗаполнение", НачисленияФиксЗаполнение);
	НачисленияПерерасчетПоля.Вставить("ФиксСтрока", НачисленияФиксСтрока);
	НачисленияПерерасчетПоля.Вставить("ФиксРасчетВремени", НачисленияФиксРасчетВремени);
	
	КонтролируемыеПоля.Вставить("НачисленияПерерасчет", НачисленияПерерасчетПоля);
	
	// Договоры
	ДоговорыФиксРасчет = Новый Массив;
	ДоговорыФиксРасчет.Добавить("Результат");
	ДоговорыФиксРасчет.Добавить("СуммаВычета");
	
	ДоговорыФиксСтрока = Новый Массив;
	ДоговорыФиксСтрока.Добавить("Сотрудник");
	ДоговорыФиксСтрока.Добавить("Подразделение");
	ДоговорыФиксСтрока.Добавить("ДокументОснование");
	ДоговорыФиксСтрока.Добавить("ДатаНачала");
	ДоговорыФиксСтрока.Добавить("ДатаОкончания");
	ДоговорыФиксСтрока.Добавить("КодДохода");
	ДоговорыФиксСтрока.Добавить("КодВычета");
	ДоговорыФиксСтрока.Добавить("ПланируемаяДатаВыплаты");
	
	ДоговорыПоля = Новый Структура;
	ДоговорыПоля.Вставить("ФиксРасчет", ДоговорыФиксРасчет);
	ДоговорыПоля.Вставить("ФиксСтрока", ДоговорыФиксСтрока);
	
	КонтролируемыеПоля.Вставить("НачисленияПоДоговорам", ДоговорыПоля);
	
	// Погашение займов
	ПогашениеЗаймовФиксРасчет = Новый Массив;
	ПогашениеЗаймовФиксРасчет.Добавить("НачисленоПроцентов");
	ПогашениеЗаймовФиксРасчет.Добавить("ПогашениеПроцентов");
	ПогашениеЗаймовФиксРасчет.Добавить("ПогашениеЗайма");
	ПогашениеЗаймовФиксРасчет.Добавить("МатериальнаяВыгода");
	ПогашениеЗаймовФиксРасчет.Добавить("НалогНаМатериальнуюВыгоду");
	ПогашениеЗаймовФиксРасчет.Добавить("СтавкаНалогообложенияМатвыгоды");
	
	ПогашениеЗаймовСтрока = Новый Массив;
	ПогашениеЗаймовСтрока.Добавить("ФизическоеЛицо");
	ПогашениеЗаймовСтрока.Добавить("ДоговорЗайма");
	ПогашениеЗаймовСтрока.Добавить("Подразделение");
	
	ПогашениеЗаймовПоля = Новый Структура;
	ПогашениеЗаймовПоля.Вставить("ФиксРасчет", ПогашениеЗаймовФиксРасчет);
	ПогашениеЗаймовПоля.Вставить("ФиксСтрока", ПогашениеЗаймовСтрока);
	
	КонтролируемыеПоля.Вставить("ПогашениеЗаймов", ПогашениеЗаймовПоля);
	
	// Пособия
	ПособияФиксРасчет = Новый Массив;
	ПособияФиксРасчет.Добавить("Результат");
	ПособияФиксРасчет.Добавить("РезультатВТомЧислеЗаСчетФБ");
	
	ПособияФиксСтрока = Новый Массив;
	ПособияФиксСтрока.Добавить("Сотрудник");
	ПособияФиксСтрока.Добавить("Подразделение");
	ПособияФиксСтрока.Добавить("Начисление");
	ПособияФиксСтрока.Добавить("ДатаНачала");
	ПособияФиксСтрока.Добавить("ДатаОкончания");
	
	ПособияПоля = Новый Структура;
	ПособияПоля.Вставить("ФиксРасчет", ПособияФиксРасчет);
	ПособияПоля.Вставить("ФиксЗаполнение", Новый Массив);
	ПособияПоля.Вставить("ФиксСтрока", ПособияФиксСтрока);
	
	КонтролируемыеПоля.Вставить("Пособия", ПособияПоля);
	
	// Пособия перерасчет
	ПособияПерерасчетПоля = Новый Структура;
	ПособияПерерасчетПоля.Вставить("ФиксРасчет", ПособияФиксРасчет);
	ПособияПерерасчетПоля.Вставить("ФиксЗаполнение", Новый Массив);
	ПособияПерерасчетПоля.Вставить("ФиксСтрока", ПособияФиксСтрока);
	
	КонтролируемыеПоля.Вставить("ПособияПерерасчет", ПособияПерерасчетПоля);
	
	// Удержания
	УдержанияФиксРасчет = Новый Массив;
	УдержанияФиксРасчет.Добавить("Результат");
	
	УдержанияФиксСтрока = Новый Массив;
	УдержанияФиксСтрока.Добавить("ФизическоеЛицо");
	УдержанияФиксСтрока.Добавить("Удержание");
	УдержанияФиксСтрока.Добавить("ДатаНачала");
	УдержанияФиксСтрока.Добавить("ДатаОкончания");
	
	УдержанияПоля = Новый Структура;
	УдержанияПоля.Вставить("ФиксРасчет", УдержанияФиксРасчет);
	УдержанияПоля.Вставить("ФиксЗаполнение", Новый Массив);
	УдержанияПоля.Вставить("ФиксСтрока", УдержанияФиксСтрока);
	
	КонтролируемыеПоля.Вставить("Удержания", УдержанияПоля);
	
	// Удержания перерасчет
	УдержанияФиксРасчет = Новый Массив;
	УдержанияФиксРасчет.Добавить("Результат");
	
	УдержанияФиксСтрока = Новый Массив;
	УдержанияФиксСтрока.Добавить("ФизическоеЛицо");
	УдержанияФиксСтрока.Добавить("Удержание");
	УдержанияФиксСтрока.Добавить("ДатаНачала");
	УдержанияФиксСтрока.Добавить("ДатаОкончания");
	
	УдержанияПоля = Новый Структура;
	УдержанияПоля.Вставить("ФиксРасчет", УдержанияФиксРасчет);
	УдержанияПоля.Вставить("ФиксЗаполнение", Новый Массив);
	УдержанияПоля.Вставить("ФиксСтрока", УдержанияФиксСтрока);
	
	КонтролируемыеПоля.Вставить("УдержанияПерерасчет", УдержанияПоля);
	
	КонтролируемыеПоля.Вставить("НДФЛ", УчетНДФЛРасширенный.КонтролируемыеПоляДляФиксацииРезультатов());
	КонтролируемыеПоля.Вставить("КорректировкиВыплаты", РасчетЗарплатыРасширенный.КонтролируемыеПоляКорректировкиВыплатыДляФиксацииРезультатов());
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ЛьготыСотрудников") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ЛьготыСотрудников");
		Модуль.ДополнитьСоставКонтролируемыхПолейНачисленияЗарплаты(КонтролируемыеПоля);
		Модуль.ДополнитьСоставКонтролируемыхПолейНачисленияЗарплаты(КонтролируемыеПоля, "ЛьготыПерерасчет");
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.УправленческаяЗарплата") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("УправленческаяЗарплата");
		Модуль.ДополнитьСоставКонтролируемыхПолейНачисленияЗарплаты(КонтролируемыеПоля);
	КонецЕсли;
	
	Возврат КонтролируемыеПоля;
	
КонецФункции

&НаКлиенте
Функция ПолучитьСтарыеЗначенияКонтролируемыхПолей() Экспорт
	Возврат СтарыеЗначенияКонтролируемыхПолей;
КонецФункции

&НаСервере
Процедура ЗагрузитьНастройки()

	РасчетЗарплатыРасширенныйФормы.УстановитьРежимОтображенияПодробноИзНастройки(ЭтаФорма, ОписаниеТаблицыНачислений(ЭтаФорма));
	РасчетЗарплатыРасширенныйФормы.УстановитьРежимОтображенияПодробноИзНастройки(ЭтаФорма, ОписаниеТаблицыПособия(ЭтаФорма));
	РасчетЗарплатыРасширенныйФормы.УстановитьРежимОтображенияПодробноИзНастройки(ЭтаФорма, ОписаниеТаблицыПособияПерерасчет(ЭтаФорма));
	РасчетЗарплатыРасширенныйФормы.УстановитьРежимОтображенияПодробноИзНастройки(ЭтаФорма, ОписаниеТаблицыПерерасчетов(ЭтаФорма));
	РасчетЗарплатыРасширенныйФормы.УстановитьРежимОтображенияПодробноИзНастройки(ЭтаФорма, ОписаниеТаблицыУдержаний());
	РасчетЗарплатыРасширенныйФормы.УстановитьРежимОтображенияПодробноИзНастройки(ЭтаФорма, ОписаниеТаблицыУдержанияПерерасчет());
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ЛьготыСотрудников") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ЛьготыСотрудников");
		Модуль.УстановитьРежимОтображенияПодробноИзНастройки(ЭтаФорма, ОписанияТаблицЛьгот(ЭтаФорма));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьБухучетРезультатовРасчетаНаСервере(ИсходныеДанные = Неопределено)
	
	МенеджерРасчета = РасчетЗарплатыРасширенный.СоздатьМенеджерРасчета(Объект.МесяцНачисления, Объект.Организация);
	ЗаполнитьНастройкиМенеджераРасчета(МенеджерРасчета);
	
	Если ИсходныеДанные = Неопределено Или ИсходныеДанные.Сотрудник = Неопределено Тогда
		ПозицииВставки = Неопределено;
		Отбор = Неопределено;
		СотрудникиКРасчету = Неопределено;
	Иначе	
		Отбор = СотрудникиФизическиеЛицаОтбор(ИсходныеДанные.Сотрудник);
		СотрудникиКРасчету = Отбор.СотрудникиКРасчету;
		ПозицииВставки = Новый Структура;
	КонецЕсли;
	
	ДанныеФормыВДанныеМенеджераРасчета(МенеджерРасчета, Отбор, ПозицииВставки);
	
	МенеджерРасчета.НастройкиУдержаний.РассчитыватьТолькоПоТекущемуДокументу = Ложь;
	МенеджерРасчета.НастройкиБухучета.ПроверятьНеобходимостьПересчетаВзносов = ИспользоватьВзносы;
	МенеджерРасчета.НастройкиБухучета.ПроверятьНеобходимостьПересчетаНДФЛ	 = ИспользоватьНДФЛ;
	МенеджерРасчета.ЗаполнитьНастройкиОбновленияБухучета(ИсходныеДанные);
	МенеджерРасчета.ОбновитьБухучетРезультатовРасчета();
	
	ДанныеЗаполнения = РасчетЗарплатыРасширенный.ОписаниеДанныхДляЗаполненияТаблицДокумента();
	ДанныеЗаполнения.Начисления = МенеджерРасчета.Зарплата.Начисления;
	ДанныеЗаполнения.НачисленияПоДоговорам = МенеджерРасчета.Зарплата.ДоговорыПодряда;
	ДанныеЗаполнения.Удержания = МенеджерРасчета.Зарплата.Удержания;
	ДанныеЗаполнения.НДФЛ = МенеджерРасчета.Зарплата.НДФЛ;
	ДанныеЗаполнения.ПогашениеЗаймов = МенеджерРасчета.Зарплата.ПогашениеЗаймов;
	ДанныеЗаполнения.Взносы = МенеджерРасчета.Зарплата.Взносы;
	ДанныеЗаполнения.КорректировкиВыплаты = МенеджерРасчета.Зарплата.КорректировкиВыплаты;
	
	ДанныеДляЗаполненияВДанныеФормы(ДанныеЗаполнения, Объект, ПозицииВставки,, Ложь);
	ЗаполнитьСтрокиТаблицПоКлючевымПолям(ПозицииВставки, СотрудникиКРасчету);
	
	УчетНДФЛФормы.ЗаполнитьВторичныеДанныеТабличныхЧастей(ЭтаФорма, , СотрудникиКРасчету);
	ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДатеВТабличнойЧасти(Объект.Взносы, "ДатаПолученияДохода", "ДатаПолученияДоходаСтрокой");
	
	МассивОписанийТаблицФормы = МассивОписанийТаблицФормы(ЭтаФорма);
	ЗарплатаКадрыРасширенный.ПериодНачисленияЗаполнитьИспользованиеПериода(ЭтаФорма, МассивОписанийТаблицФормы, СотрудникиКРасчету);
	ЗарплатаКадрыРасширенный.ЗаполнитьДоступностьМестаПолученияДохода(ЭтаФорма, МассивОписанийТаблицФормы, СотрудникиКРасчету);
	ЗарплатаКадрыРасширенныйКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтаФорма);
	ЗарплатаКадрыРасширенныйКлиентСервер.ПланируемаяДатаВыплатыУстановитьОтображениеПредупреждения(ЭтаФорма,"ПланируемаяДатаВыплаты",
												СтрокиТаблицДокументаПоФизическимЛицам);
	ЗарплатаКадрыРасширенный.ВводНачисленийЗаполнитьВторичныеДанныеПоказателей(ЭтаФорма, МассивОписанийТаблицФормы, СотрудникиКРасчету);
	РасчетЗарплатыРасширенныйФормы.ЗаполнитьПредставлениеРаспределенияПоТерриториямУсловиямТруда(ЭтаФорма, Объект.Организация, ОписанияТаблицСРаспределениемПоТерриториямУсловиямТруда(ЭтаФорма),СотрудникиКРасчету);
	
	Если ИсходныеДанные <> Неопределено И ЭтаФорма.Объект[ИсходныеДанные.ИмяИсходнойТаблицы].Количество() >= ИсходныеДанные.НомерСтрокиИсходнойТаблицы Тогда
		РасчетЗарплатыРасширенныйФормы.УстановитьТекущуюСтрокуТаблицыФормы(ЭтаФорма, ИсходныеДанные.ИмяИсходнойТаблицы, ИсходныеДанные.НомерСтрокиИсходнойТаблицы)
	КонецЕсли;
	
КонецПроцедуры

#Область ПечатныеФормыСотрудников

&НаКлиенте
Функция ПечатнаяФормаСотрудника(Сотрудник, Идентификатор)
	
	Если ТипЗнч(Сотрудник) = Тип("СправочникСсылка.Сотрудники") Тогда
		ФизическоеЛицоСотрудника = СотрудникиКлиентСерверПовтИсп.ФизическиеЛицаСотрудников(Сотрудник)[0];
	Иначе
		ФизическоеЛицоСотрудника = Сотрудник;
	КонецЕсли;
	
	Если СотрудникНеРассчитан(ФизическоеЛицоСотрудника) Тогда
		
		УдалитьПечатныеФормыСотрудника(ФизическоеЛицоСотрудника);
		Возврат Неопределено;
		
	КонецЕсли;
	
	ПодготовитьКоллекциюПечатныхФорм(Идентификатор);
	Возврат ПечатныеФормыСотрудников[Идентификатор].ПечатныеФормы.Получить(ФизическоеЛицоСотрудника);
	
КонецФункции

&НаКлиенте
Процедура ДобавитьВКоллекциюПечатнуюФормуСотрудника(Сотрудник, Идентификатор, ПечатнаяФорма)
	
	Если ТипЗнч(Сотрудник) = Тип("СправочникСсылка.Сотрудники") Тогда
		ФизическоеЛицоСотрудника = СотрудникиКлиентСерверПовтИсп.ФизическиеЛицаСотрудников(Сотрудник)[0];
	Иначе
		ФизическоеЛицоСотрудника = Сотрудник;
	КонецЕсли;
	
	Если Не СотрудникНеРассчитан(ФизическоеЛицоСотрудника) Тогда
		
		ПодготовитьКоллекциюПечатныхФорм(Идентификатор);
		ИмеющаясяПечатнаяФорма = ПечатныеФормыСотрудников[Идентификатор].ПечатныеФормы.Получить(ФизическоеЛицоСотрудника);
		
		ПечатныеФормыСотрудников[Идентификатор].ПечатныеФормы.Вставить(ФизическоеЛицоСотрудника, ПечатнаяФорма);
		Если ИмеющаясяПечатнаяФорма = Неопределено Тогда
			
			ПечатныеФормыСотрудников[Идентификатор].Порядок.Добавить(ФизическоеЛицоСотрудника);
			Если ПечатныеФормыСотрудников[Идентификатор].Порядок.Количество() > 100 Тогда
				ПечатныеФормыСотрудников[Идентификатор].Порядок.Удалить(0);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция СотрудникНеРассчитан(ФизическоеЛицоСотрудника)
	
	НеРассчитан = Ложь;
	
	Для каждого ТаблицаССотрудниками Из СотрудникиКРасчету Цикл
		
		Если ТаблицаССотрудниками.Значение.СписокСотрудников.Получить(ФизическоеЛицоСотрудника) <> Неопределено Тогда
			
			НеРассчитан = Истина;
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат НеРассчитан;
	
КонецФункции

&НаКлиенте
Процедура УдалитьПечатныеФормыСотрудника(Сотрудник)
	
	Если ТипЗнч(Сотрудник) = Тип("СправочникСсылка.Сотрудники") Тогда
		ФизическоеЛицоСотрудника = СотрудникиКлиентСерверПовтИсп.ФизическиеЛицаСотрудников(Сотрудник)[0];
	Иначе
		ФизическоеЛицоСотрудника = Сотрудник;
	КонецЕсли;
	
	Если ПечатныеФормыСотрудников <> Неопределено Тогда
		
		Для каждого ОписаниеПечатныхФорм Из ПечатныеФормыСотрудников Цикл
			
			Идентификатор = ОписаниеПечатныхФорм.Ключ;
			Если ПечатныеФормыСотрудников[Идентификатор].ПечатныеФормы.Получить(ФизическоеЛицоСотрудника) <> Неопределено Тогда
				
				ПечатныеФормыСотрудников[Идентификатор].ПечатныеФормы.Удалить(ФизическоеЛицоСотрудника);
				
				ИндексПоПорядку = ПечатныеФормыСотрудников[Идентификатор].Порядок.Найти(ФизическоеЛицоСотрудника);
				ПечатныеФормыСотрудников[Идентификатор].Порядок.Удалить(ИндексПоПорядку);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодготовитьКоллекциюПечатныхФорм(Идентификатор)
	
	Если ПечатныеФормыСотрудников = Неопределено Тогда
		ПечатныеФормыСотрудников = Новый Структура;
	КонецЕсли;
	
	Если Не ПечатныеФормыСотрудников.Свойство(Идентификатор) Тогда
		ПечатныеФормыСотрудников.Вставить(Идентификатор, Новый Структура("Порядок,ПечатныеФормы", Новый Массив, Новый Соответствие));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область РаспределениеПоТерриториямУсловиямТруда

&НаКлиенте
Процедура ПриНажатииПолеРаспределениеПоТерриториямУсловиямТруда(ОписаниеТаблицы, Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПараметрыДляВыбора = РасчетЗарплатыРасширенныйКлиент.ПараметрыДляВыбораПолеРаспределениеПоТерриториямУсловиямТруда();
	ПараметрыДляВыбора.Форма = ЭтаФорма;
	ПараметрыДляВыбора.ОписаниеДокумента = ОписаниеДокумента(ЭтотОбъект);
	ПараметрыДляВыбора.ОписаниеТаблицы = ОписаниеТаблицы;
	
	РасчетЗарплатыРасширенныйКлиент.ПриНажатииПолеРаспределениеПоТерриториямУсловиямТруда(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка, ПараметрыДляВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПараметрыФормыРаспределениеПоТерриториямУсловиямТруда(ПараметрыФормы, ОписаниеТаблицы, ВыбраннаяСтрока) Экспорт
	ЗаполнитьПараметрыФормыРаспределениеПоТерриториямУсловиямТрудаНаСервере(ПараметрыФормы, ОписаниеТаблицы, ВыбраннаяСтрока);
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПараметрыФормыРаспределениеПоТерриториямУсловиямТрудаНаСервере(ПараметрыФормы, ОписаниеТаблицы, ВыбраннаяСтрока)
	РасчетЗарплатыРасширенныйФормы.ЗаполнитьПараметрыФормыРаспределениеПоТерриториямУсловиямТруда(ЭтаФорма, ПараметрыФормы, ОписаниеТаблицы, ВыбраннаяСтрока);
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиРезультатыРедактированияРаспределенияПоТерриториямУсловиямТруда(РезультатРедактирования, ОписаниеТаблицы, ВыбраннаяСтрока) Экспорт
	ПеренестиРезультатыРедактированияРаспределенияПоТерриториямУсловиямТрудаНаСервере(РезультатРедактирования, ОписаниеТаблицы, ВыбраннаяСтрока);
КонецПроцедуры

&НаСервере
Процедура ПеренестиРезультатыРедактированияРаспределенияПоТерриториямУсловиямТрудаНаСервере(РезультатРедактирования, ОписаниеТаблицы, ВыбраннаяСтрока)
	РасчетЗарплатыРасширенныйФормы.ПеренестиРезультатыРедактированияРаспределенияПоТерриториямУсловиямТруда(ЭтаФорма, РезультатРедактирования, ОписаниеТаблицы, ВыбраннаяСтрока);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ОписанияТаблицСРаспределениемПоТерриториямУсловиямТруда(Форма)
	
	Описания = Новый Массив;
	Если Форма.ИспользоватьНачисление Тогда
		Описания.Добавить(ОписаниеТаблицыНачислений(Форма));
		Описания.Добавить(ОписаниеТаблицыПерерасчетов(Форма));
	КонецЕсли;
	
	Если Форма.ИспользоватьПособия Тогда
		Описания.Добавить(ОписаниеТаблицыПособия(Форма));
		Описания.Добавить(ОписаниеТаблицыПособияПерерасчет(Форма));
	КонецЕсли;
	
	Если Форма.ИспользоватьДоговоры Тогда
		Описания.Добавить(ОписаниеТаблицыНачисленийПоДоговорам(Форма));
	КонецЕсли;
	
	Если Форма.ИспользоватьЛьготы И ОбщегоНазначенияБЗККлиентСервер.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ЛьготыСотрудников") Тогда
		Модуль = ОбщегоНазначенияБЗККлиентСервер.ОбщийМодуль("ЛьготыСотрудниковКлиентСервер");
		Модуль.ДополнитьМассивОписанийТаблицФормыНачисленияЗарплаты(Описания, ОписанияТаблицЛьгот(Форма));
	КонецЕсли;
	
	Возврат Описания;
	
КонецФункции

#КонецОбласти


#Область КлючевыеРеквизитыЗаполненияФормы

// Функция возвращает описание таблиц формы подключенных к механизму ключевых реквизитов формы.
&НаСервере
Функция КлючевыеРеквизитыЗаполненияФормыТаблицыОчищаемыеПриИзменении() Экспорт
	
	Массив = КлючевыеРеквизитыТаблицыОчищаемыеПриИзменении();
	Возврат Массив;
	
КонецФункции 

// Функция возвращает массив реквизитов формы подключенных к механизму ключевых реквизитов формы.
&НаСервере
Функция КлючевыеРеквизитыЗаполненияФормыОписаниеКлючевыхРеквизитов() Экспорт
	
	Массив = КлючевыеРеквизитыОписаниеКлючевыхРеквизитов(ИспользоватьВоеннуюСлужбу);
	Возврат Массив;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция КлючевыеРеквизитыТаблицыОчищаемыеПриИзменении() 
	
	Массив = Новый Массив;
	Массив.Добавить("Объект.Начисления");
	Массив.Добавить("Объект.НачисленияПерерасчет");
	Массив.Добавить("Объект.НачисленияПоДоговорам");
	Массив.Добавить("Объект.Пособия");
	Массив.Добавить("Объект.ПособияПерерасчет");
	Массив.Добавить("Объект.НДФЛ");
	Массив.Добавить("Объект.Взносы");
	Массив.Добавить("Объект.ПогашениеЗаймов");
	Массив.Добавить("Объект.Удержания");
	Массив.Добавить("Объект.УдержанияПерерасчет");
	Массив.Добавить("Объект.ПримененныеВычетыНаДетейИИмущественные");
	Массив.Добавить("Объект.Льготы");
	Массив.Добавить("Объект.ЛьготыПерерасчет");
	Массив.Добавить("Объект.СотрудникиПерерасчет");
	
	Возврат Массив;
	
КонецФункции 

&НаКлиентеНаСервереБезКонтекста
Функция КлючевыеРеквизитыОписаниеКлючевыхРеквизитов(ИспользоватьНачислениеЗарплатыВоеннослужащим) 
	
	Массив = Новый Массив;
	Массив.Добавить(Новый Структура("ЭлементФормы, Представление", "Организация",					НСтр("ru = 'организации';
																											|en = 'companies'")));
	Массив.Добавить(Новый Структура("ЭлементФормы, Представление", "ПодразделениеОрганизации",		НСтр("ru = 'подразделения';
																											|en = 'business units'")));
	Массив.Добавить(Новый Структура("ЭлементФормы, Представление", "МесяцНачисленияСтрокой", 		НСтр("ru = 'месяца начисления';
																											|en = 'month of accrual'")));
	
	Если ИспользоватьНачислениеЗарплатыВоеннослужащим Тогда
		Массив.Добавить(Новый Структура("ЭлементФормы, Представление", "НачислениеЗарплатыВоеннослужащим", 		НСтр("ru = 'варианта начисления';
																														|en = 'accrual option'")));
	КонецЕсли;
	
	Возврат Массив;
	
КонецФункции

#КонецОбласти

&НаКлиенте
Функция ПустаяСтрокаНачисления(ИспользоватьДляПерерасчета = Ложь) Экспорт
	
	СтрокаНачисления = Новый Структура;
	СтрокаНачисления.Вставить("Организация");
	СтрокаНачисления.Вставить("Сотрудник");
	СтрокаНачисления.Вставить("ФизическоеЛицо");
	СтрокаНачисления.Вставить("Подразделение");
	СтрокаНачисления.Вставить("Начисление");
	СтрокаНачисления.Вставить("ДокументОснование");
	СтрокаНачисления.Вставить("ГрафикРаботы");
	СтрокаНачисления.Вставить("ОбщийГрафик");
	СтрокаНачисления.Вставить("ГрафикРаботыНорма");
	СтрокаНачисления.Вставить("ВидУчетаВремени");
	СтрокаНачисления.Вставить("ВидВремени");
	СтрокаНачисления.Вставить("ВремяВЧасах");
	СтрокаНачисления.Вставить("ВремяВЦеломЗаПериод");
	СтрокаНачисления.Вставить("ДатаНачала");
	СтрокаНачисления.Вставить("ДатаОкончания");
	СтрокаНачисления.Вставить("НачалоБазовогоПериода");
	СтрокаНачисления.Вставить("ОкончаниеБазовогоПериода");
	СтрокаНачисления.Вставить("ПериодДействия");
	СтрокаНачисления.Вставить("Строка");
	СтрокаНачисления.Вставить("ФиксСтрока");
	СтрокаНачисления.Вставить("ФиксЗаполнение");
	СтрокаНачисления.Вставить("ФиксРасчетВремени");
	СтрокаНачисления.Вставить("ФиксРасчет");
	СтрокаНачисления.Вставить("ПериодРегистрацииВремени");
	СтрокаНачисления.Вставить("ПериодРегистрацииНормыВремени");
	СтрокаНачисления.Вставить("Сумма");
	СтрокаНачисления.Вставить("Результат");
	СтрокаНачисления.Вставить("РезультатВТомЧислеЗаСчетФБ");
	СтрокаНачисления.Вставить("ИдентификаторСтроки");
	СтрокаНачисления.Вставить("ИдентификаторСтрокиВидаРасчета");
	СтрокаНачисления.Вставить("ОтработаноДней");
	СтрокаНачисления.Вставить("ОтработаноЧасов");
	СтрокаНачисления.Вставить("НормаДней");
	СтрокаНачисления.Вставить("НормаЧасов");
	СтрокаНачисления.Вставить("ОплаченоДней");
	СтрокаНачисления.Вставить("ОплаченоЧасов");
	СтрокаНачисления.Вставить("КодВычета");
	СтрокаНачисления.Вставить("КоличествоДетей");
	СтрокаНачисления.Вставить("СуммаВычета");
	СтрокаНачисления.Вставить("ФиксСуммаВычета");
	СтрокаНачисления.Вставить("СкидкаПоВзносам");
	СтрокаНачисления.Вставить("Сторно");
	СтрокаНачисления.Вставить("ФиксСторно");
	СтрокаНачисления.Вставить("ВидЗанятости");
	СтрокаНачисления.Вставить("РаспределениеПоТерриториямУсловиямТруда");
	СтрокаНачисления.Вставить("РезультатРаспределения");
	СтрокаНачисления.Вставить("ДоступенВыборОснования");
	СтрокаНачисления.Вставить("ИспользованиеПериода");
	СтрокаНачисления.Вставить("ЯвляетсяЛьготой");
	СтрокаНачисления.Вставить("СуммаВсегоПоУходуЗаПервымРебенком");
	СтрокаНачисления.Вставить("СуммаСверхНормПоУходуЗаПервымРебенком");
	СтрокаНачисления.Вставить("СуммаВсегоПоУходуЗаВторымРебенком");
	СтрокаНачисления.Вставить("СуммаСверхНормПоУходуЗаВторымРебенком");
	СтрокаНачисления.Вставить("СтраховойСлучай");
	СтрокаНачисления.Вставить("КоличествоПервыхДетей");
	СтрокаНачисления.Вставить("ФинансированиеФедеральнымБюджетом"); 
	
	Для Счетчик = 1 По ЗарплатаКадрыРасширенныйВызовСервера.МаксимальноеКоличествоОтображаемыхПоказателей() Цикл
		СтрокаНачисления.Вставить("Показатель" + Счетчик);
		СтрокаНачисления.Вставить("Значение" + Счетчик);
		СтрокаНачисления.Вставить("КомандаРасшифровки" + Счетчик);
	КонецЦикла;

	Возврат СтрокаНачисления;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСтрокиТаблицПоКлючевымПолям(ПозицииВставки = Неопределено, Сотрудники = Неопределено)
	
	Если ПозицииВставки = Неопределено Тогда
		СтрокиТаблицДокументаПоФизическимЛицам = Новый ФиксированноеСоответствие(СтрокиТаблицПоФизическимЛицам(Объект));
	Иначе
		ФизическиеЛицаСотрудников = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(Сотрудники, "ФизическоеЛицо");
		ПозицииСотрудника = Новый Соответствие;
		
		Для Каждого СтрокаСотрудника Из Объект.СотрудникиПерерасчет Цикл
			РасчетЗарплатыРасширенныйКлиентСервер.ДобавитьИдентификаторСтрокиСотрудника(
				ПозицииСотрудника, СтрокаСотрудника.ФизическоеЛицо, СтрокаСотрудника.ПолучитьИдентификатор());
		КонецЦикла;
		ПозицииВставки.Вставить("СотрудникиПерерасчет", ПозицииСотрудника);	
			
		Для Каждого СотрудникиТаблицы Из ПозицииВставки Цикл
			ИмяТаблицы = СотрудникиТаблицы.Ключ;
			ОбработанныеФизическиеЛица = Новый Соответствие;
			Для Каждого СтрокаСотрудника Из СотрудникиТаблицы.Значение Цикл
				Если ТипЗнч(СтрокаСотрудника.Ключ) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
					ФизическоеЛицо = СтрокаСотрудника.Ключ;
				ИначеЕсли ФизическиеЛицаСотрудников[СтрокаСотрудника.Ключ] <> Неопределено Тогда
					ФизическоеЛицо = ФизическиеЛицаСотрудников[СтрокаСотрудника.Ключ];
				Иначе
					ФизическоеЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаСотрудника.Ключ, "ФизическоеЛицо");
				КонецЕсли;
				
				Если ОбработанныеФизическиеЛица[ФизическоеЛицо] = Неопределено Тогда
					ДополнятьИдентификаторы = Ложь;
					ОбработанныеФизическиеЛица.Вставить(ФизическоеЛицо, Истина);
				Иначе
					ДополнятьИдентификаторы = Истина;
				КонецЕсли;
				ОбновитьИдентификаторыСтрокФизическогоЛица(ФизическоеЛицо, ИмяТаблицы, СтрокаСотрудника.Значение, ДополнятьИдентификаторы);
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СтрокиТаблицПоФизическимЛицам(Документ, ЗаполнятьИдентификаторы = Истина, ТаблицыДокумента = Неопределено)
	
	Если ТаблицыДокумента = Неопределено Тогда
		ТаблицыДокумента = ТаблицыДокументаСДаннымиФизическихЛиц();
	КонецЕсли;
	
	Если Не ЗаполнятьИдентификаторы Тогда
		ШаблонСтрок = НовыйСтруктураТаблицДокумента().ШаблонСтрокТаблиц;
	КонецЕсли;
	
	ФизическиеЛицаПоИдентификаторам = Новый Соответствие;
	СтрокиПоФизическимЛицам = Новый Соответствие;
	
	Для Каждого ИмяТаблицы Из ТаблицыДокумента Цикл
		Для Каждого СтрокаТаблицы Из Документ[ИмяТаблицы] Цикл
			Если ИмяТаблицы = "ПримененныеВычетыНаДетейИИмущественные" Тогда
				ФизическоеЛицо = ФизическиеЛицаПоИдентификаторам[СтрокаТаблицы.ИдентификаторСтрокиНДФЛ];
			Иначе
				ФизическоеЛицо = СтрокаТаблицы.ФизическоеЛицо;
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(ФизическоеЛицо) Тогда
				Продолжить;
			КонецЕсли;
			
			Если ИмяТаблицы = "НДФЛ" Тогда
				ФизическиеЛицаПоИдентификаторам.Вставить(СтрокаТаблицы.ИдентификаторСтрокиНДФЛ, ФизическоеЛицо);
			КонецЕсли;
			
			Если СтрокиПоФизическимЛицам[ФизическоеЛицо] = Неопределено Тогда
				ТаблицыФизическогоЛица = Новый Структура;
				СтрокиПоФизическимЛицам.Вставить(ФизическоеЛицо, ТаблицыФизическогоЛица);
			Иначе
				ТаблицыФизическогоЛица = СтрокиПоФизическимЛицам[ФизическоеЛицо];
			КонецЕсли;
			
			Если Не ТаблицыФизическогоЛица.Свойство(ИмяТаблицы) Тогда
				ТаблицыФизическогоЛица.Вставить(ИмяТаблицы, Новый Массив);
			КонецЕсли;
			
			Если ЗаполнятьИдентификаторы Тогда
				ТаблицыФизическогоЛица[ИмяТаблицы].Добавить(СтрокаТаблицы.ПолучитьИдентификатор());
			Иначе
				НоваяСтрока = НоваяСтрокаДанныхПоСотрудникам(ТаблицыФизическогоЛица[ИмяТаблицы], ШаблонСтрок[ИмяТаблицы]);
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат СтрокиПоФизическимЛицам;
	
КонецФункции

&НаКлиенте
Процедура ДобавитьИдентификаторСтрокиФизическогоЛица(ФизическоеЛицо, ИмяТаблицы, Идентификатор)
	ДобавитьИдентификаторСтрокиФизическогоЛицаБезКонтекста(СтрокиТаблицДокументаПоФизическимЛицам, ФизическоеЛицо, ИмяТаблицы, Идентификатор);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ДобавитьИдентификаторСтрокиФизическогоЛицаБезКонтекста(СтрокиТаблицДокументаПоФизическимЛицам, ФизическоеЛицо, ИмяТаблицы, Идентификатор)
	
	Если Не ЗначениеЗаполнено(ФизическоеЛицо) Тогда
		Возврат;
	КонецЕсли;
	
	СтрокиТаблицФизическогоЛица = СтрокиТаблицФизическогоЛица(СтрокиТаблицДокументаПоФизическимЛицам, ФизическоеЛицо);
	
	СтрокиТаблицы = Неопределено;
	Если Не СтрокиТаблицФизическогоЛица.Свойство(ИмяТаблицы, СтрокиТаблицы) Тогда
		СтрокиТаблицы = Новый Массив;
		СтрокиТаблицФизическогоЛица.Вставить(ИмяТаблицы, СтрокиТаблицы);
	КонецЕсли;
	
	Если СтрокиТаблицы.Найти(Идентификатор) = Неопределено Тогда
		СтрокиТаблицы.Добавить(Идентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьИдентификаторСтрокиФизическогоЛица(ФизическоеЛицо, ИмяТаблицы, Идентификатор)
	
	СтрокиТаблицФизическогоЛица = СтрокиТаблицДокументаПоФизическимЛицам.Получить(ФизическоеЛицо);
	Если СтрокиТаблицФизическогоЛица = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтрокиТаблицы = Неопределено;
	Если Не СтрокиТаблицФизическогоЛица.Свойство(ИмяТаблицы, СтрокиТаблицы) Тогда
		Возврат
	КонецЕсли;
	
	Индекс = СтрокиТаблицы.Найти(Идентификатор);
	Если Индекс = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтрокиТаблицы.Удалить(Индекс);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьИдентификаторыСтрокФизическогоЛица(ФизическоеЛицо, ИмяТаблицы, Идентификаторы, ДополнятьИдентификаторы)
	
	Если Не ЗначениеЗаполнено(ФизическоеЛицо) Тогда
		Возврат;
	КонецЕсли;
	
	СтрокиТаблицФизическогоЛица = СтрокиТаблицФизическогоЛица(СтрокиТаблицДокументаПоФизическимЛицам, ФизическоеЛицо);
	Если ДополнятьИдентификаторы Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СтрокиТаблицФизическогоЛица[ИмяТаблицы], Идентификаторы);
	Иначе
		СтрокиТаблицФизическогоЛица.Вставить(ИмяТаблицы, Идентификаторы);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СтрокиТаблицФизическогоЛица(СтрокиТаблицДокументаПоФизическимЛицам, ФизическоеЛицо)
	
	Если СтрокиТаблицДокументаПоФизическимЛицам = Неопределено Тогда
		СтрокиТаблицПоФизическимЛицам = Новый Соответствие;
	ИначеЕсли СтрокиТаблицДокументаПоФизическимЛицам.Получить(ФизическоеЛицо) = Неопределено Тогда
		СтрокиТаблицПоФизическимЛицам = Новый Соответствие(СтрокиТаблицДокументаПоФизическимЛицам);
	Иначе
		Возврат СтрокиТаблицДокументаПоФизическимЛицам[ФизическоеЛицо];
	КонецЕсли;
	
	СтрокиТаблицФизическогоЛица = Новый Структура;
	СтрокиТаблицПоФизическимЛицам.Вставить(ФизическоеЛицо, СтрокиТаблицФизическогоЛица);
	СтрокиТаблицДокументаПоФизическимЛицам = Новый ФиксированноеСоответствие(СтрокиТаблицПоФизическимЛицам);
	
	Возврат СтрокиТаблицДокументаПоФизическимЛицам[ФизическоеЛицо];
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТаблицыДокументаСДаннымиФизическихЛиц()
	
	ТаблицыДокумента = Новый Массив;
	ТаблицыДокумента.Добавить("Начисления");
	ТаблицыДокумента.Добавить("НачисленияПерерасчет");
	ТаблицыДокумента.Добавить("Пособия");
	ТаблицыДокумента.Добавить("ПособияПерерасчет");
	ТаблицыДокумента.Добавить("Льготы");
	ТаблицыДокумента.Добавить("ЛьготыПерерасчет");
	Если ОбщегоНазначенияБЗККлиентСервер.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.УправленческаяЗарплата") Тогда
		ТаблицыДокумента.Добавить("УправленческиеНачисления");
	КонецЕсли;
	ТаблицыДокумента.Добавить("НачисленияПоДоговорам");
	ТаблицыДокумента.Добавить("Удержания");
	ТаблицыДокумента.Добавить("УдержанияПерерасчет");
	ТаблицыДокумента.Добавить("НДФЛ");
	ТаблицыДокумента.Добавить("ПримененныеВычетыНаДетейИИмущественные");
	ТаблицыДокумента.Добавить("УчтенныеПриРасчетеНДФЛДокументы");
	ТаблицыДокумента.Добавить("КорректировкиВыплаты");
	ТаблицыДокумента.Добавить("Взносы");
	ТаблицыДокумента.Добавить("ПогашениеЗаймов");
	ТаблицыДокумента.Добавить("СотрудникиПерерасчет");
	
	Возврат ТаблицыДокумента;
	
КонецФункции

&НаКлиенте
Функция НачислениеДоговораГПХПоДокументуОснованию(ДокументОснование)
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ДоговорАвторскогоЗаказа") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ВидыОсобыхНачисленийИУдержаний.ДоговорАвторскогоЗаказа");
	КонецЕсли;
	
	Возврат ПредопределенноеЗначение("Перечисление.ВидыОсобыхНачисленийИУдержаний.ДоговорРаботыУслуги");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НоваяСтрокаДанныхПоСотрудникам(ТаблицаДанных, ШаблонСтроки)
	
	НоваяСтрока = Новый Структура(ШаблонСтроки);
	ТаблицаДанных.Добавить(НоваяСтрока);
	Возврат НоваяСтрока;
	
КонецФункции

&НаСервереБезКонтекста
Функция НовыйСтруктураТаблицДокумента()
	
	СтруктураТаблиц = Новый Структура("СодержимоеДокумента,ШаблонСтрокТаблиц", Новый Структура, Новый Структура);
	ТаблицыСодержимогоДокумента = РасчетЗарплатыРасширенный.НовыйСодержимоеДокументаРасчетаЗарплаты();
	Для Каждого КлючИЗначение Из ТаблицыСодержимогоДокумента Цикл
		ТаблицаЗначений = КлючИЗначение.Значение;
		СтруктураСтрокой = "";
		НужнаЗапятая = Ложь;
		Для Каждого Колонка Из ТаблицаЗначений.Колонки Цикл
			Если НужнаЗапятая Тогда
				СтруктураСтрокой = СтруктураСтрокой + ",";
			КонецЕсли;
			СтруктураСтрокой = СтруктураСтрокой + Колонка.Имя;
			НужнаЗапятая = Истина;
		КонецЦикла;
		СтруктураТаблиц.ШаблонСтрокТаблиц.Вставить(КлючИЗначение.Ключ, Новый ФиксированнаяСтруктура(СтруктураСтрокой));
		СтруктураТаблиц.СодержимоеДокумента.Вставить(КлючИЗначение.Ключ, Новый Массив);
	КонецЦикла;
	
	Возврат СтруктураТаблиц;
	
КонецФункции

&НаСервере
Процедура УстановитьВидимостьКолонокПрогрессивногоНалога(ДатаПолученияДохода)
	
	ВидимостьКолонок = ДатаПолученияДохода >= УчетНДФЛ.ДатаЗакона176ФЗ();
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаКолонокНалогПоПрогрессивнымСтавкам",
																"Видимость", ВидимостьКолонок);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "НДФЛНалогСПревышения",
																"Видимость", НЕ ВидимостьКолонок);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаКолонокЗачтеноАвансовВСчетСуммПоПрогрессивнымСтавкам",
																"Видимость", ВидимостьКолонок);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "НДФЛЗачтеноАвансовыхПлатежейСПревышения",
																"Видимость", НЕ ВидимостьКолонок);
	
КонецПроцедуры

#КонецОбласти

#Область Инициализация

СтарыеЗначенияКонтролируемыхПолей = Новый Соответствие;
СотрудникиКРасчету = Новый Соответствие;

#КонецОбласти
