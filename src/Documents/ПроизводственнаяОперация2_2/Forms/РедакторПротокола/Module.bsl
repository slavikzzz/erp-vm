
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Заголовок  = Заголовок + " " + Параметры.Заголовок;
	Количество = Параметры.Количество;
	Контроль   = Параметры.Контроль;
	Протокол.Загрузить(Параметры.Протокол.Выгрузить());
	
	Параметры.Свойство("Операция", Операция);
	Параметры.Свойство("НаОснованииНСИ", НаОснованииНСИ);
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	МожноОтменить       = Истина;
	Для Индекс = -(Параметры.Протокол.Количество() - 1) По 0 Цикл
		
		ТекущаяСтрока = Протокол[-Индекс];
		
		МожноОтменить = Мин(МожноОтменить, ТекущийПользователь = ТекущаяСтрока.Ответственный);
		ТекущаяСтрока.МожноОтменить = МожноОтменить;
		
	КонецЦикла;
	
	ЕдиницаИзмеренияПредставление = УправлениеПроизводствомКлиентСервер.ПредставлениеЕдиницыИзмеренияОперации(
		Неопределено,
		Количество);
		
	НастроитьПереключательПересчитатьНормативы();
		
	Документы.ПроизводственнаяОперация2_2.ПересчитатьИтоги(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		ПоказатьВопрос(
			Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект),
			НСтр("ru = 'Протокол был изменен. Сохранить изменения?';
				|en = 'The protocol was changed. Do you want to save the changes?'"),
			РежимДиалогаВопрос.ДаНетОтмена);
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Ложь;
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		СохранитьИЗакрыть();
	Иначе
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	Для каждого Строка Из Протокол Цикл
		Если Строка.МожноОтменить Тогда
			Строка.Отметка = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	Для каждого Строка Из Протокол Цикл
		Строка.Отметка = Ложь;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьВыбранные(Команда)
	
	УдалитьВыбранныеНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура Применить(Команда)
	
	Модифицированность = Ложь;
	
	СохранитьИЗакрыть();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПротоколОтменитьПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Протокол.ТекущиеДанные;
	
	Начало = ?(ТекущиеДанные.Отметка, Протокол.Индекс(ТекущиеДанные) + 1, 0);
	Конец  = ?(ТекущиеДанные.Отметка, Протокол.Количество() - 1, Протокол.Индекс(ТекущиеДанные) - 1);
	
	Для Индекс = Начало По Конец Цикл
		Протокол[Индекс].Отметка = ТекущиеДанные.Отметка;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СохранитьИЗакрыть()
	
	Результат = Новый Структура;
	Результат.Вставить("Протокол", Протокол);
	Результат.Вставить("ПересчитатьНормативы", ПересчитатьНормативы);
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПротоколОтменить.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Протокол.МожноОтменить");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
КонецПроцедуры

&НаСервере
Процедура УдалитьВыбранныеНаСервере()
	
	КоличествоЗаписей = Протокол.Количество();
	
	Для Индекс = -КоличествоЗаписей + 1 По 0 Цикл
		ЗаписьПротокола = Протокол[-Индекс];
		Если ЗаписьПротокола.Отметка Тогда
			Если НаОснованииНСИ
				И (ЗаписьПротокола.Событие = Перечисления.СобытияПротоколаПроизводственнойОперации.Отмена
					ИЛИ ЗаписьПротокола.Событие = Перечисления.СобытияПротоколаПроизводственнойОперации.Брак) Тогда
				ПересчитатьНормативы = Истина;
			КонецЕсли;
			Протокол.Удалить(-Индекс);
		КонецЕсли;
	КонецЦикла;
	
	Если КоличествоЗаписей <> Протокол.Количество() Тогда
		Документы.ПроизводственнаяОперация2_2.ПересчитатьИтоги(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьПереключательПересчитатьНормативы()
	
	Элементы.ПересчитатьНормативы.Видимость = НаОснованииНСИ;
	
	Если НаОснованииНСИ Тогда
		
		Элементы.ПересчитатьНормативы.Заголовок = Документы.ПроизводственнаяОперация2_2.ТекстПереключателяПересчитатьНормативы(Операция);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти