#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ОтзывСогласия, Параметры)
	ПараметрыЖурналаРегистрации = ПараметрыЖурналаРегистрации(ОтзывСогласия);
	ОткрытьФорму("Обработка.ЖурналРегистрации.Форма", ПараметрыЖурналаРегистрации);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПараметрыЖурналаРегистрации(ОтзывСогласия)
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ОтзывСогласия, "ДатаСоздания, Дата");
	Результат = Новый Структура("ДатаНачала, ДатаОкончания, Данные");
	Результат.Данные = ОтзывСогласия;
	Если ЗначениеЗаполнено(ЗначенияРеквизитов.ДатаСоздания) Тогда
		Результат.ДатаНачала = ЗначенияРеквизитов.ДатаСоздания - 60;
	Иначе
		Результат.ДатаНачала = ЗначенияРеквизитов.Дата;
	КонецЕсли;
	Результат.ДатаОкончания = ТекущаяДата(); // АПК:143 Для фильтрации событий в журнале регистрации требуется дата сервера.
	Возврат Результат;
КонецФункции

#КонецОбласти
