
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОсновнаяОрганизация           = Справочники.Организации.ОрганизацияПоУмолчанию();
	ОтборОрганизация              = ОсновнаяОрганизация;
	ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Организация");
	
	Если ЗначениеЗаполнено(Параметры.Заголовок) Тогда
		Заголовок     = Параметры.Заголовок;
		АвтоЗаголовок = Ложь;
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("Налог") И ЗначениеЗаполнено(Параметры.Отбор.Налог) Тогда
		Элементы.Налог.Видимость = Ложь;
		ОтборНалог = Параметры.Отбор.Налог;
	КонецЕсли;	
		
	ВидимостьНО = Справочники.Организации.ИспользуетсяНесколькоОрганизаций()
		Или Справочники.РегистрацииВНалоговомОргане.ПолучитьКоличествоПодчиненныхЭлементовПоВладельцу(ОсновнаяОрганизация) <> 1;
		
	Элементы.КодОтправителя.Видимость = ВидимостьНО;
	Элементы.РегистрацияВНалоговомОргане.Видимость = ВидимостьНО;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборОрганизацияИспользованиеПриИзменении(Элемент)
	
	Если ОтборОрганизацияИспользование И Не ЗначениеЗаполнено(ОтборОрганизация) Тогда
		ОтборОрганизацияИспользование = Ложь;
		ТекстСообщения = НСтр("ru = 'Выберите организацию.';
								|en = 'Select company.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,"ОтборОрганизация",,);
	Иначе
		ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Организация");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияПриИзменении(Элемент)
	
	ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Организация");
		
КонецПроцедуры

#КонецОбласти
