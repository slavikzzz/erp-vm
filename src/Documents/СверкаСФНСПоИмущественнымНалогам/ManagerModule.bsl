#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Находит существующую сверку расчета по ключевым полям.
// 
// Параметры:
//  Организация - СправочникСсылка.Организации
//  Налог - ПеречислениеСсылка.ВидыИмущественныхНалогов - налог, по которому нужно сверить расчет 
//  Период - Дата - любая дата в налоговом периоде, за который нужно сверить расчет
//  КодОтправителя - Строка - код налоговой инспекции, с которой выполняется сверка
//  ТекущийДокумент - ДокументСсылка.СверкаСФНСПоИмущественнымНалогам - указывается, если нужно найти дублирующую сверку
//  СообщениеФНС - СправочникСсылка.ДокументыРеализацииПолномочийНалоговыхОрганов - указывается,
//                 если выполняется поиск сверки, заполненной по указанному сообщению.
//  ИмяФайла - Строка - указывается, если выполняется поиск сверки, заполненной по указанному файлу
// 
// Возвращаемое значение:
//  Неопределено - если сверка не найдена, ДокументСсылка.СверкаСФНСПоИмущественнымНалогам - существующая сверка
//
Функция НайтиСуществующий(Организация, Налог, Период, КодОтправителя, ТекущийДокумент = Неопределено, СообщениеФНС = Неопределено, ИмяФайла = "") Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	СверкаСФНСПоИмущественнымНалогам.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.СверкаСФНСПоИмущественнымНалогам КАК СверкаСФНСПоИмущественнымНалогам
	|ГДЕ
	|	СверкаСФНСПоИмущественнымНалогам.Организация = &Организация
	|	И СверкаСФНСПоИмущественнымНалогам.Налог = &Налог
	|	И СверкаСФНСПоИмущественнымНалогам.НалоговыйПериод = &НалоговыйПериод
	|	И СверкаСФНСПоИмущественнымНалогам.КодОтправителя = &КодОтправителя
	|	И НЕ СверкаСФНСПоИмущественнымНалогам.ПометкаУдаления";

	Запрос.УстановитьПараметр("Организация",     Организация);
	Запрос.УстановитьПараметр("Налог",           Налог);
	Запрос.УстановитьПараметр("НалоговыйПериод", НачалоГода(Период));
	Запрос.УстановитьПараметр("КодОтправителя",  КодОтправителя);

	Если ЗначениеЗаполнено(ТекущийДокумент) Тогда
		СхемаЗапроса = Новый СхемаЗапроса;
		СхемаЗапроса.УстановитьТекстЗапроса(Запрос.Текст);
		СхемаЗапроса.ПакетЗапросов[0].Операторы[0].Отбор.Добавить(
			Новый ВыражениеСхемыЗапроса("СверкаСФНСПоИмущественнымНалогам.Ссылка <> &ТекущийДокумент"));
		Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
		Запрос.УстановитьПараметр("ТекущийДокумент", ТекущийДокумент);
	КонецЕсли;

	Если ЗначениеЗаполнено(СообщениеФНС) Тогда
		СхемаЗапроса = Новый СхемаЗапроса;
		СхемаЗапроса.УстановитьТекстЗапроса(Запрос.Текст);
		СхемаЗапроса.ПакетЗапросов[0].Операторы[0].Отбор.Добавить(
			Новый ВыражениеСхемыЗапроса("СверкаСФНСПоИмущественнымНалогам.СообщениеФНС = &СообщениеФНС"));
		Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
		Запрос.УстановитьПараметр("СообщениеФНС", СообщениеФНС);
	КонецЕсли;

	Если ЗначениеЗаполнено(ИмяФайла) Тогда
		СхемаЗапроса = Новый СхемаЗапроса;
		СхемаЗапроса.УстановитьТекстЗапроса(Запрос.Текст);
		СхемаЗапроса.ПакетЗапросов[0].Операторы[0].Отбор.Добавить(
			Новый ВыражениеСхемыЗапроса("СверкаСФНСПоИмущественнымНалогам.ИмяФайла = &ИмяФайла"));
		Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
		Запрос.УстановитьПараметр("ИмяФайла", ИмяФайла);
	КонецЕсли;

	РезультатЗапроса = Запрос.Выполнить();

	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;

	Выборка = РезультатЗапроса.Выбрать();

	Выборка.Следующий();

	Возврат Выборка.Ссылка;

КонецФункции

// Находит существующую сверку расчета, в которой есть объекты, учтенные в указанном налоговом органе.
// 
// Параметры:
//  Организация - СправочникСсылка.Организации
//  Налог - ПеречислениеСсылка.ВидыИмущественныхНалогов - налог, по которому нужно сверить расчет 
//  Период - Дата - любая дата в налоговом периоде, за который нужно сверить расчет
//  КодНалоговогоОргана - Строка - код налоговой инспекции, в которой учтены объекты налогообложения
// 
// Возвращаемое значение:
//  Неопределено - если сверка не найдена, ДокументСсылка.СверкаСФНСПоИмущественнымНалогам - существующая сверка
//
Функция НайтиСуществующийПоКодуНалоговогоОргана(Организация, Налог, Период, КодНалоговогоОргана) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	СверкаСФНСПоИмущественнымНалогам.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.СверкаСФНСПоИмущественнымНалогам КАК СверкаСФНСПоИмущественнымНалогам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СверкаСФНСПоИмущественнымНалогам.Сверка КАК ТаблицаСверки
	|		ПО СверкаСФНСПоИмущественнымНалогам.Ссылка = ТаблицаСверки.Ссылка
	|ГДЕ
	|	СверкаСФНСПоИмущественнымНалогам.Организация = &Организация
	|	И СверкаСФНСПоИмущественнымНалогам.Налог = &Налог
	|	И СверкаСФНСПоИмущественнымНалогам.НалоговыйПериод = &НалоговыйПериод
	|	И ТаблицаСверки.КодНалоговогоОргана = &КодНалоговогоОргана
	|	И НЕ СверкаСФНСПоИмущественнымНалогам.ПометкаУдаления";

	Запрос.УстановитьПараметр("Организация",     Организация);
	Запрос.УстановитьПараметр("Налог",           Налог);
	Запрос.УстановитьПараметр("НалоговыйПериод", НачалоГода(Период));
	Запрос.УстановитьПараметр("КодНалоговогоОргана", КодНалоговогоОргана);

	РезультатЗапроса = Запрос.Выполнить();

	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;

	Выборка = РезультатЗапроса.Выбрать();

	Выборка.Следующий();

	Возврат Выборка.Ссылка;

КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)

	Если ВидФормы <> "ФормаДокумента"
		И ВидФормы <> "ФормаОбъекта" Тогда
		Возврат;
	КонецЕсли;

	Если Параметры.Свойство("Ключ") И ЗначениеЗаполнено(Параметры.Ключ) Тогда
		СтандартнаяОбработка = Ложь;
		ВыбраннаяФорма = "ФормаСверки";
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьОбъектыСверки(ПараметрыЗаполнения) Экспорт
	
	КодыНалоговыхОрганов = ПараметрыЗаполнения.КодыНалоговыхОрганов;
	Объект = ПараметрыЗаполнения.Объект;
	НалоговыйПериод = Объект.НалоговыйПериод;

	Организация = Объект.Организация;
	Налог = Объект.Налог;

	Объект.Сверка.Очистить();
	Объект.Расчет.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	
	ПериодРасчета = СверкаСФНСПоИмущественнымНалогамФормыКлиентСервер.ПериодРасчетаНалога(НалоговыйПериод);
	
	ПараметрыРасчета = РасчетИмущественныхНалогов.ПараметрыРасчетаНалогаНаИмущество(Организация, ПериодРасчета);
	ПараметрыРасчета.МенеджерВременныхТаблиц = Запрос.МенеджерВременныхТаблиц;
	
	Если Налог = Перечисления.ВидыИмущественныхНалогов.ТранспортныйНалог Тогда
		
		РасчетИмущественныхНалогов.СформироватьТаблицуРегистрацияТранспортныхСредствЗаписи(ПараметрыРасчета,КонецГода(ПериодРасчета));
		РасчетИмущественныхНалогов.СформироватьТаблицуРегистрацияТранспортныхСредствСрезПоследних(ПараметрыРасчета,КонецГода(ПериодРасчета));
		
	ИначеЕсли Налог = Перечисления.ВидыИмущественныхНалогов.ЗемельныйНалог Тогда
		
		РасчетИмущественныхНалогов.СформироватьТаблицуРегистрацияЗемельныхУчастковЗаписи(
										ПараметрыРасчета,
										КонецГода(ПериодРасчета),
										Неопределено);
		РасчетИмущественныхНалогов.СформироватьТаблицуРегистрацияЗемельныхУчастковСрезПоследних(ПараметрыРасчета,
																								КонецГода(ПериодРасчета),
																								Неопределено);
		
	ИначеЕсли Налог = Перечисления.ВидыИмущественныхНалогов.НалогНаИмущество Тогда
		
		РасчетИмущественныхНалогов.СформироватьТаблицуСтавкиНалогаНаИмуществоПоОтдельнымОСЗаписи(ПараметрыРасчета,
																									НачалоГода(ПериодРасчета),
																									КонецГода(ПериодРасчета));
																									
		РасчетИмущественныхНалогов.СформироватьТаблицуСтавкиНалогаНаИмуществоПоОтдельнымОССрезПоследних(ПараметрыРасчета,
																											КонецГода(ПериодРасчета));
	КонецЕсли;
	
	ИнициализироватьВтКорректировок(ПараметрыРасчета, Налог, КодыНалоговыхОрганов);
	
	Если Налог = Перечисления.ВидыИмущественныхНалогов.ТранспортныйНалог Тогда
		Запрос.Текст = ТекстЗапросаТранспортныеСредства();
	ИначеЕсли Налог = Перечисления.ВидыИмущественныхНалогов.ЗемельныйНалог Тогда
		Запрос.Текст = ТекстЗапросаЗемельныеУчастки();
	ИначеЕсли Налог = Перечисления.ВидыИмущественныхНалогов.НалогНаИмущество Тогда
		Запрос.Текст = ТекстЗапросаИмущество();
		ИспользоватьПрежнийРасчетНалога = Ложь;
		Если ПериодРасчета < РасчетИмущественныхНалогов.НачалоПримененияНовогоРасчетаНалогаНаИмущество()
		 Или РасчетИмущественныхНалоговПереопределяемый.ИспользоватьПрежнийРасчетНалогаНаИмуществоВПереходныйПериод(Организация, ПериодРасчета) Тогда
		 	ИспользоватьПрежнийРасчетНалога = Истина;
		КонецЕсли;
		Запрос.УстановитьПараметр("ИспользоватьПрежнийРасчетНалога", ИспользоватьПрежнийРасчетНалога);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПериодРасчета", ПериодРасчета);
	Запрос.УстановитьПараметр("КодыНалоговыхОрганов", КодыНалоговыхОрганов);
	Запрос.УстановитьПараметр("ПериодОсобогоКонтроля",
		СверкаСФНСПоИмущественнымНалогамФормыКлиентСервер.ПериодИзмененийДляОсобогоКонтроля(НалоговыйПериод));
		
	ВыборкаОбъектыСверки = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаОбъектыСверки.Следующий() Цикл

		СтрокаСверки = Объект.Сверка.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаСверки, ВыборкаОбъектыСверки);
		
		// Учтем корректировки начисленного налога, сделанные документами Перерасчет имущественных налогов.
		СтрокаСверки.СуммаНалога = ВыборкаОбъектыСверки.СуммаНалога + ВыборкаОбъектыСверки.СуммаКорректировок;
		
		СтрокаСверки.ОбъектЕстьВРасчете = Истина;

		ИдентификаторСтроки = Новый УникальныйИдентификатор();
		СтрокаСверки.ИдентификаторСтроки = ИдентификаторСтроки;

		ВыборкаРасчет = ВыборкаОбъектыСверки.Выбрать();

		Пока ВыборкаРасчет.Следующий() Цикл

			СтрокаРасчета = Объект.Расчет.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаРасчета, ВыборкаРасчет);
			СтрокаРасчета.ИдентификаторСтроки = ИдентификаторСтроки;

			Если Налог = Перечисления.ВидыИмущественныхНалогов.НалогНаИмущество Тогда
				СтрокаРасчета.ДоляВПравеЧислитель = 
					?(ЗначениеЗаполнено(ВыборкаРасчет.ДоляВПравеОбщейСобственностиЧислитель), ВыборкаРасчет.ДоляВПравеОбщейСобственностиЧислитель, 1)
					* ?(ЗначениеЗаполнено(ВыборкаРасчет.ДоляСтоимостиЧислитель), ВыборкаРасчет.ДоляСтоимостиЧислитель, 1);
				СтрокаРасчета.ДоляВПравеЗнаменатель = 
					?(ЗначениеЗаполнено(ВыборкаРасчет.ДоляВПравеОбщейСобственностиЗнаменатель), ВыборкаРасчет.ДоляВПравеОбщейСобственностиЗнаменатель, 1)
					* ?(ЗначениеЗаполнено(ВыборкаРасчет.ДоляСтоимостиЗнаменатель), ВыборкаРасчет.ДоляСтоимостиЗнаменатель, 1);
				СтрокаРасчета.КоличествоМесяцевВладения = ВыборкаРасчет.КоличествоМесяцевИспользования;
			КонецЕсли;

		КонецЦикла;

	КонецЦикла;
	
	ПараметрыЗаполнения.Вставить("МенеджерВременныхТаблиц", ПараметрыРасчета.МенеджерВременныхТаблиц);

КонецПроцедуры

Процедура ИнициализироватьВтКорректировок(ПараметрыРасчета, Налог, КодыНалоговыхОрганов)
	// Учтем корректировки налога по документам Перерасчет имущественных налогов.
	ТекстЗапросаКорректировок =
	"ВЫБРАТЬ 
	|	&Организация КАК Организация,
	|	СУММА(Исправления.СуммаРегл) КАК СуммаНалога,
	|	Исправления.ОсновноеСредство КАК ОсновноеСредство, 
	|	Исправления.ОсновноеСредство.Наименование КАК Наименование,
	|	Исправления.КодНалоговогоОргана КАК КодНалоговогоОргана,
	|	Исправления.КодПоОКТМО КАК КодПоОКТМО
	|ПОМЕСТИТЬ ВтКорректировок
	|ИЗ
	|	(ВЫБРАТЬ
	|		-ПрочиеДоходы.СуммаРегл КАК СуммаРегл,
	|		ПерерасчетИмущественныхНалогов.ОсновноеСредство КАК ОсновноеСредство,
	|		ПерерасчетИмущественныхНалогов.НалоговыйОрган.Код КАК КодНалоговогоОргана,
	|		ПерерасчетИмущественныхНалогов.КодПоОКТМО КАК КодПоОКТМО
	|	ИЗ
	|		РегистрНакопления.ПрочиеДоходы КАК ПрочиеДоходы
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПерерасчетИмущественныхНалогов КАК ПерерасчетИмущественныхНалогов
	|			ПО ПрочиеДоходы.Регистратор = ПерерасчетИмущественныхНалогов.Ссылка
	|	ГДЕ
	|		ПрочиеДоходы.Регистратор ССЫЛКА Документ.ПерерасчетИмущественныхНалогов
	|		И ПрочиеДоходы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И ПрочиеДоходы.Организация = &Организация
	|		И ПрочиеДоходы.ХозяйственнаяОперация = &ХозяйственнаяОперацияВозмещенияНалога
	|
	|		И &Период МЕЖДУ НАЧАЛОПЕРИОДА(ПерерасчетИмущественныхНалогов.НачалоПериода, ГОД)
	|						И КОНЕЦПЕРИОДА(ПерерасчетИмущественныхНалогов.КонецПериода, ГОД)
	|
	|		И ПерерасчетИмущественныхНалогов.ВидНалога = &ВидНалога
	|		И ПерерасчетИмущественныхНалогов.НалоговыйОрган.Код В(&КодыНалоговыхОрганов)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ПрочиеРасходы.СуммаРегл,
	|		ПерерасчетИмущественныхНалогов.ОсновноеСредство,
	|		ПерерасчетИмущественныхНалогов.НалоговыйОрган.Код,
	|		ПерерасчетИмущественныхНалогов.КодПоОКТМО
	|	ИЗ
	|		РегистрНакопления.ПрочиеРасходы КАК ПрочиеРасходы
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПерерасчетИмущественныхНалогов КАК ПерерасчетИмущественныхНалогов
	|			ПО ПрочиеРасходы.Регистратор = ПерерасчетИмущественныхНалогов.Ссылка
	|	ГДЕ
	|		ПрочиеРасходы.Регистратор ССЫЛКА Документ.ПерерасчетИмущественныхНалогов
	|		И ПрочиеРасходы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И ПрочиеРасходы.Организация = &Организация
	|		И ПрочиеРасходы.ХозяйственнаяОперация = &ХозяйственнаяОперацияДоначисленияНалога
	|
	|		И &Период МЕЖДУ НАЧАЛОПЕРИОДА(ПерерасчетИмущественныхНалогов.НачалоПериода, ГОД)
	|						И КОНЕЦПЕРИОДА(ПерерасчетИмущественныхНалогов.КонецПериода, ГОД)
	|
	|		И ПерерасчетИмущественныхНалогов.ВидНалога = &ВидНалога
	|		И ПерерасчетИмущественныхНалогов.НалоговыйОрган.Код В(&КодыНалоговыхОрганов)) КАК Исправления
	|
	|СГРУППИРОВАТЬ ПО
	|	Исправления.ОсновноеСредство,
	|	Исправления.КодНалоговогоОргана,
	|	Исправления.КодПоОКТМО
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	КодНалоговогоОргана,
	|	КодПоОКТМО";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаКорректировок;
	
	Запрос.МенеджерВременныхТаблиц = ПараметрыРасчета.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Организация", ПараметрыРасчета.Организация);
	Запрос.УстановитьПараметр("Период", ПараметрыРасчета.ПериодРасчета);
	Запрос.УстановитьПараметр("ВидНалога", Налог);
	Запрос.УстановитьПараметр("КодыНалоговыхОрганов", КодыНалоговыхОрганов);
	
	Если Налог = Перечисления.ВидыИмущественныхНалогов.ТранспортныйНалог Тогда
		ХозяйственнаяОперацияДоначисленияНалога = Перечисления.ХозяйственныеОперации.ДоначислениеТранспортногоНалога;
		ХозяйственнаяОперацияВозмещенияНалога = Перечисления.ХозяйственныеОперации.ЗачетТранспортногоНалога;
	ИначеЕсли Налог = Перечисления.ВидыИмущественныхНалогов.ЗемельныйНалог Тогда
		ХозяйственнаяОперацияДоначисленияНалога = Перечисления.ХозяйственныеОперации.ДоначислениеЗемельногоНалога;
		ХозяйственнаяОперацияВозмещенияНалога = Перечисления.ХозяйственныеОперации.ЗачетЗемельногоНалога;
	ИначеЕсли Налог = Перечисления.ВидыИмущественныхНалогов.НалогНаИмущество Тогда
		ХозяйственнаяОперацияДоначисленияНалога = Перечисления.ХозяйственныеОперации.ДоначислениеНалогаНаИмущество;
		ХозяйственнаяОперацияВозмещенияНалога = Перечисления.ХозяйственныеОперации.ЗачетНалогаНаИмущество; 
	Иначе
		ХозяйственнаяОперацияДоначисленияНалога = Неопределено;
		ХозяйственнаяОперацияВозмещенияНалога   = Неопределено;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ХозяйственнаяОперацияДоначисленияНалога", ХозяйственнаяОперацияДоначисленияНалога);
	Запрос.УстановитьПараметр("ХозяйственнаяОперацияВозмещенияНалога", ХозяйственнаяОперацияВозмещенияНалога);

	Запрос.Выполнить();
	
КонецПроцедуры

Функция ТекстЗапросаТранспортныеСредства()

	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетТранспортногоНалога.ОсновноеСредство КАК ОсновноеСредство,
	|	РасчетТранспортногоНалога.ОсновноеСредство.Наименование КАК Наименование,
	|	РасчетТранспортногоНалога.РегистрационныйЗнак КАК РегистрационныйЗнак,
	|	РасчетТранспортногоНалога.ИдентификационныйНомер КАК ИдентификационныйНомер,
	|	МАКСИМУМ(РасчетТранспортногоНалога.КодВидаТранспортногоСредства) КАК КодВидаТС,
	|	МАКСИМУМ(РасчетТранспортногоНалога.ЕдиницаИзмерения) КАК ЕдиницаИзмерения,
	|	РасчетТранспортногоНалога.ИФНС.Код КАК КодНалоговогоОргана,
	|	РасчетТранспортногоНалога.КодПоОКТМО КАК КодПоОКТМО,
	|	СУММА(РасчетТранспортногоНалога.СуммаНалогаКУплате + РасчетТранспортногоНалога.СуммаАвансовыхПлатежей1Кв + РасчетТранспортногоНалога.СуммаАвансовыхПлатежей2Кв + РасчетТранспортногоНалога.СуммаАвансовыхПлатежей3Кв) КАК СуммаНалога,
	|	РасчетТранспортногоНалога.ДатаРегистрации КАК ДатаРегистрации,
	|	РасчетТранспортногоНалога.ДатаПрекращенияРегистрации КАК ДатаСнятияСРегистрации,
	|	РасчетТранспортногоНалога.НалоговаяБаза КАК НалоговаяБаза,
	|	РасчетТранспортногоНалога.НалоговаяСтавка КАК НалоговаяСтавка,
	|	ВЫБОР
	|		КОГДА РасчетТранспортногоНалога.ПовышающийКоэффициент = 0
	|			ТОГДА 1
	|		ИНАЧЕ РасчетТранспортногоНалога.ПовышающийКоэффициент
	|	КОНЕЦ КАК ПовышающийКоэффициент,
	|	СУММА(РасчетТранспортногоНалога.КоличествоМесяцевЛьготы) КАК КоличествоМесяцевЛьготы,
	|	СУММА(РасчетТранспортногоНалога.КоличествоМесяцевЛьготы) <> 0 КАК ЕстьЛьгота,
	|	СУММА(РасчетТранспортногоНалога.СуммаНалоговойЛьготы) КАК СуммаНалоговойЛьготы,
	|	СУММА(РасчетТранспортногоНалога.КоличествоМесяцевВладения) КАК КоличествоМесяцевВладения
	|ПОМЕСТИТЬ РасчетНалогаБезКорректировок
	|ИЗ
	|	РегистрСведений.РасчетТранспортногоНалога КАК РасчетТранспортногоНалога
	|ГДЕ
	|	РасчетТранспортногоНалога.Организация = &Организация
	|	И РасчетТранспортногоНалога.ПериодРасчета = &ПериодРасчета
	|	И РасчетТранспортногоНалога.ИФНС.Код В(&КодыНалоговыхОрганов)
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетТранспортногоНалога.ОсновноеСредство,
	|	РасчетТранспортногоНалога.ОсновноеСредство.Наименование,
	|	РасчетТранспортногоНалога.РегистрационныйЗнак,
	|	РасчетТранспортногоНалога.ИдентификационныйНомер,
	|	РасчетТранспортногоНалога.ДатаРегистрации,
	|	РасчетТранспортногоНалога.ДатаПрекращенияРегистрации,
	|	РасчетТранспортногоНалога.ИФНС,
	|	РасчетТранспортногоНалога.КодПоОКТМО,
	|	РасчетТранспортногоНалога.НалоговаяБаза,
	|	РасчетТранспортногоНалога.НалоговаяСтавка,
	|	ВЫБОР
	|		КОГДА РасчетТранспортногоНалога.ПовышающийКоэффициент = 0
	|			ТОГДА 1
	|		ИНАЧЕ РасчетТранспортногоНалога.ПовышающийКоэффициент
	|	КОНЕЦ,
	|	РасчетТранспортногоНалога.ИФНС.Код
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	КодНалоговогоОргана,
	|	КодПоОКТМО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетНалогаБезКорректировок.ОсновноеСредство КАК ОсновноеСредство,
	|	РасчетНалогаБезКорректировок.Наименование КАК Наименование,
	|	РасчетНалогаБезКорректировок.РегистрационныйЗнак КАК РегистрационныйЗнак,
	|	РасчетНалогаБезКорректировок.ИдентификационныйНомер КАК ИдентификационныйНомер,
	|	РасчетНалогаБезКорректировок.КодВидаТС КАК КодВидаТС,
	|	РасчетНалогаБезКорректировок.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	РасчетНалогаБезКорректировок.КодНалоговогоОргана КАК КодНалоговогоОргана,
	|	РасчетНалогаБезКорректировок.КодПоОКТМО КАК КодПоОКТМО,
	|	РасчетНалогаБезКорректировок.СуммаНалога КАК СуммаНалога,
	|	ЕСТЬNULL(ВтКорректировок.СуммаНалога, 0) КАК СуммаКорректировок,
	|	РасчетНалогаБезКорректировок.ДатаРегистрации КАК ДатаРегистрации,
	|	РасчетНалогаБезКорректировок.ДатаСнятияСРегистрации КАК ДатаСнятияСРегистрации,
	|	РасчетНалогаБезКорректировок.НалоговаяБаза КАК НалоговаяБаза,
	|	РасчетНалогаБезКорректировок.НалоговаяСтавка КАК НалоговаяСтавка,
	|	РасчетНалогаБезКорректировок.ПовышающийКоэффициент КАК ПовышающийКоэффициент,
	|	РасчетНалогаБезКорректировок.КоличествоМесяцевЛьготы КАК КоличествоМесяцевЛьготы,
	|	РасчетНалогаБезКорректировок.ЕстьЛьгота КАК ЕстьЛьгота,
	|	РасчетНалогаБезКорректировок.СуммаНалоговойЛьготы КАК СуммаНалоговойЛьготы,
	|	РасчетНалогаБезКорректировок.КоличествоМесяцевВладения КАК КоличествоМесяцевВладения
	|ПОМЕСТИТЬ РасчетНалога
	|ИЗ
	|	РасчетНалогаБезКорректировок КАК РасчетНалогаБезКорректировок
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтКорректировок КАК ВтКорректировок
	|		ПО РасчетНалогаБезКорректировок.ОсновноеСредство = ВтКорректировок.ОсновноеСредство
	|			И РасчетНалогаБезКорректировок.КодНалоговогоОргана = ВтКорректировок.КодНалоговогоОргана
	|			И РасчетНалогаБезКорректировок.КодПоОКТМО = ВтКорректировок.КодПоОКТМО
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВтКорректировок.ОсновноеСредство,
	|	ВтКорректировок.ОсновноеСредство.Наименование,
	|	РегистрацияТранспортныхСредств.РегистрационныйЗнак,
	|	РегистрацияТранспортныхСредств.ИдентификационныйНомер,
	|	РегистрацияТранспортныхСредств.КодВидаТранспортногоСредства,
	|	РегистрацияТранспортныхСредств.ЕдиницаИзмеренияНалоговойБазы,
	|	ВтКорректировок.КодНалоговогоОргана,
	|	ВтКорректировок.КодПоОКТМО,
	|	0,
	|	ЕСТЬNULL(ВтКорректировок.СуммаНалога, 0),
	|	ВЫБОР
	|		КОГДА РегистрацияТранспортныхСредств.ВидЗаписи = ЗНАЧЕНИЕ(Перечисление.ВидЗаписиОРегистрации.Регистрация)
	|			ТОГДА РегистрацияТранспортныхСредств.Период
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА РегистрацияТранспортныхСредств.ВидЗаписи = ЗНАЧЕНИЕ(Перечисление.ВидЗаписиОРегистрации.СнятиеСРегистрационногоУчета)
	|			ТОГДА РегистрацияТранспортныхСредств.Период
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|	КОНЕЦ,
	|	0,
	|	РегистрацияТранспортныхСредств.НалоговаяСтавка,
	|	1,
	|	0,
	|	ЛОЖЬ,
	|	0,
	|	0
	|ИЗ
	|	ВтКорректировок КАК ВтКорректировок
	|		ЛЕВОЕ СОЕДИНЕНИЕ РасчетНалогаБезКорректировок КАК РасчетНалогаБезКорректировок
	|		ПО (РасчетНалогаБезКорректировок.ОсновноеСредство = ВтКорректировок.ОсновноеСредство)
	|			И (РасчетНалогаБезКорректировок.КодНалоговогоОргана = ВтКорректировок.КодНалоговогоОргана)
	|			И (РасчетНалогаБезКорректировок.КодПоОКТМО = ВтКорректировок.КодПоОКТМО)
	|			И (РасчетНалогаБезКорректировок.КодПоОКТМО ЕСТЬ NULL)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрацияТранспортныхСредств_СрезПоследних КАК РегистрацияТранспортныхСредств
	|		ПО (РегистрацияТранспортныхСредств.Организация = ВтКорректировок.Организация)
	|			И (РегистрацияТранспортныхСредств.ОсновноеСредство = ВтКорректировок.ОсновноеСредство)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ РасчетНалогаБезКорректировок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегистрацияТранспортныхСредств.ОсновноеСредство КАК ОсновноеСредство,
	|	МАКСИМУМ(РегистрацияТранспортныхСредств.Период) КАК ДатаПоследнегоИзменения
	|ПОМЕСТИТЬ ДатыПоследнихИзменений
	|ИЗ
	|	РегистрацияТранспортныхСредств_Записи КАК РегистрацияТранспортныхСредств
	|ГДЕ
	|	РегистрацияТранспортныхСредств.Организация = &Организация
	|	И РегистрацияТранспортныхСредств.ОсновноеСредство В
	|			(ВЫБРАТЬ
	|				РасчетНалога.ОсновноеСредство
	|			ИЗ
	|				РасчетНалога КАК РасчетНалога)
	|
	|СГРУППИРОВАТЬ ПО
	|	РегистрацияТранспортныхСредств.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетНалога.ОсновноеСредство КАК ОсновноеСредство,
	|	РасчетНалога.РегистрационныйЗнак КАК РегистрационныйЗнак,
	|	РасчетНалога.ИдентификационныйНомер КАК ИдентификационныйНомер,
	|	РасчетНалога.КодНалоговогоОргана КАК КодНалоговогоОргана,
	|	РасчетНалога.КодПоОКТМО КАК КодПоОКТМО,
	|	АВТОНОМЕРЗАПИСИ() КАК Ключ
	|ПОМЕСТИТЬ ГруппировкаПоОбъектам
	|ИЗ
	|	РасчетНалога КАК РасчетНалога
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	РегистрационныйЗнак,
	|	ИдентификационныйНомер,
	|	КодНалоговогоОргана,
	|	КодПоОКТМО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетНалога.ОсновноеСредство КАК ОсновноеСредство,
	|	РасчетНалога.Наименование КАК НаименованиеОбъекта,
	|	РасчетНалога.РегистрационныйЗнак КАК РегистрационныйЗнак,
	|	РасчетНалога.ИдентификационныйНомер КАК ИдентификационныйНомер,
	|	РасчетНалога.КодВидаТС КАК КодВидаТС,
	|	РасчетНалога.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	РасчетНалога.КодНалоговогоОргана КАК КодНалоговогоОргана,
	|	РасчетНалога.КодПоОКТМО КАК КодПоОКТМО,
	|	РасчетНалога.НалоговаяБаза КАК НалоговаяБаза,
	|	РасчетНалога.НалоговаяСтавка КАК НалоговаяСтавка,
	|	РасчетНалога.КоличествоМесяцевВладения КАК КоличествоМесяцевВладения,
	|	РасчетНалога.ПовышающийКоэффициент КАК ПовышающийКоэффициент,
	|	РасчетНалога.СуммаНалоговойЛьготы КАК СуммаЛьгот,
	|	РасчетНалога.СуммаНалога КАК СуммаНалога,
	|	РасчетНалога.СуммаКорректировок КАК СуммаКорректировок,
	|	РасчетНалога.ДатаРегистрации КАК ДатаПостановкиНаУчет,
	|	РасчетНалога.ДатаСнятияСРегистрации КАК ДатаСнятияСУчета,
	|	ЕСТЬNULL(ДатыПоследнихИзменений.ДатаПоследнегоИзменения, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаПоследнегоИзменения,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ДатыПоследнихИзменений.ДатаПоследнегоИзменения, ДАТАВРЕМЯ(1, 1, 1)) >= &ПериодОсобогоКонтроля
	|			ТОГДА 0
	|		КОГДА РасчетНалога.ЕстьЛьгота
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК Порядок,
	|	ГруппировкаПоОбъектам.Ключ КАК КлючГруппировки
	|ИЗ
	|	РасчетНалога КАК РасчетНалога
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ГруппировкаПоОбъектам КАК ГруппировкаПоОбъектам
	|		ПО РасчетНалога.ОсновноеСредство = ГруппировкаПоОбъектам.ОсновноеСредство
	|			И РасчетНалога.РегистрационныйЗнак = ГруппировкаПоОбъектам.РегистрационныйЗнак
	|			И РасчетНалога.ИдентификационныйНомер = ГруппировкаПоОбъектам.ИдентификационныйНомер
	|			И РасчетНалога.КодНалоговогоОргана = ГруппировкаПоОбъектам.КодНалоговогоОргана
	|			И РасчетНалога.КодПоОКТМО = ГруппировкаПоОбъектам.КодПоОКТМО
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДатыПоследнихИзменений КАК ДатыПоследнихИзменений
	|		ПО РасчетНалога.ОсновноеСредство = ДатыПоследнихИзменений.ОсновноеСредство
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	ДатаПостановкиНаУчет,
	|	ОсновноеСредство
	|ИТОГИ
	|	МАКСИМУМ(ОсновноеСредство),
	|	МАКСИМУМ(НаименованиеОбъекта),
	|	МАКСИМУМ(РегистрационныйЗнак),
	|	МАКСИМУМ(ИдентификационныйНомер),
	|	МАКСИМУМ(КодВидаТС),
	|	МАКСИМУМ(ЕдиницаИзмерения),
	|	МАКСИМУМ(КодНалоговогоОргана),
	|	МАКСИМУМ(КодПоОКТМО),
	|	СУММА(СуммаЛьгот),
	|	СУММА(СуммаНалога),
	|	МАКСИМУМ(СуммаКорректировок),
	|	МИНИМУМ(ДатаПостановкиНаУчет),
	|	МИНИМУМ(ДатаСнятияСУчета),
	|	МАКСИМУМ(ДатаПоследнегоИзменения),
	|	МИНИМУМ(Порядок)
	|ПО
	|	КлючГруппировки";

	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаЗемельныеУчастки()

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РасчетЗемельногоНалога.Организация КАК Организация,
	|	РасчетЗемельногоНалога.ОсновноеСредство КАК ОсновноеСредство,
	|	РасчетЗемельногоНалога.ОсновноеСредство.Наименование КАК НаименованиеОбъекта,
	|	РасчетЗемельногоНалога.КадастровыйНомер КАК КадастровыйНомер,
	|	РасчетЗемельногоНалога.ИФНС.Код КАК КодНалоговогоОргана,
	|	РасчетЗемельногоНалога.КодПоОКТМО КАК КодПоОКТМО,
	|	РасчетЗемельногоНалога.КадастроваяСтоимость КАК НалоговаяБаза,
	|	ВЫБОР
	|		КОГДА РасчетЗемельногоНалога.ДоляВПравеОбщейСобственностиЧислитель = 0
	|			ТОГДА 1
	|		ИНАЧЕ РасчетЗемельногоНалога.ДоляВПравеОбщейСобственностиЧислитель
	|	КОНЕЦ КАК ДоляВПравеЧислитель,
	|	ВЫБОР
	|		КОГДА РасчетЗемельногоНалога.ДоляВПравеОбщейСобственностиЗнаменатель = 0
	|			ТОГДА 1
	|		ИНАЧЕ РасчетЗемельногоНалога.ДоляВПравеОбщейСобственностиЗнаменатель
	|	КОНЕЦ КАК ДоляВПравеЗнаменатель,
	|	РасчетЗемельногоНалога.НеоблагаемаяКадастроваяСтоимость КАК НалоговыйВычет,
	|	РасчетЗемельногоНалога.НалоговаяСтавка КАК НалоговаяСтавка,
	|	РасчетЗемельногоНалога.ПовышающийКоэффициент КАК ПовышающийКоэффициент,
	|	РасчетЗемельногоНалога.КоличествоМесяцевИспользования КАК КоличествоМесяцевВладения,
	|	РасчетЗемельногоНалога.ДатаРегистрации КАК ДатаПостановкиНаУчет,
	|	РасчетЗемельногоНалога.ДатаСнятияСУчета КАК ДатаСнятияСУчета,
	|	РасчетЗемельногоНалога.СуммаЛьгот КАК СуммаЛьгот,
	|	РасчетЗемельногоНалога.СуммаНалога КАК СуммаНалога,
	|	РасчетЗемельногоНалога.СуммаЛьгот <> 0 КАК ЕстьЛьготы
	|ПОМЕСТИТЬ РасчетНалогаБезКорректировок
	|ИЗ
	|	РегистрСведений.РасчетЗемельногоНалога КАК РасчетЗемельногоНалога
	|ГДЕ
	|	РасчетЗемельногоНалога.Организация = &Организация
	|	И РасчетЗемельногоНалога.ПериодРасчета = &ПериодРасчета
	|	И РасчетЗемельногоНалога.ИФНС.Код В (&КодыНалоговыхОрганов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетЗемельногоНалога.Организация КАК Организация,
	|	РасчетЗемельногоНалога.ОсновноеСредство КАК ОсновноеСредство,
	|	РасчетЗемельногоНалога.НаименованиеОбъекта КАК НаименованиеОбъекта,
	|	РасчетЗемельногоНалога.КадастровыйНомер КАК КадастровыйНомер,
	|	РасчетЗемельногоНалога.КодНалоговогоОргана КАК КодНалоговогоОргана,
	|	РасчетЗемельногоНалога.КодПоОКТМО КАК КодПоОКТМО,
	|	РасчетЗемельногоНалога.НалоговаяБаза КАК НалоговаяБаза,
	|	РасчетЗемельногоНалога.ДоляВПравеЧислитель КАК ДоляВПравеЧислитель,
	|	РасчетЗемельногоНалога.ДоляВПравеЗнаменатель КАК ДоляВПравеЗнаменатель,
	|	РасчетЗемельногоНалога.НалоговыйВычет КАК НалоговыйВычет,
	|	РасчетЗемельногоНалога.НалоговаяСтавка КАК НалоговаяСтавка,
	|	РасчетЗемельногоНалога.ПовышающийКоэффициент КАК ПовышающийКоэффициент,
	|	РасчетЗемельногоНалога.КоличествоМесяцевВладения КАК КоличествоМесяцевВладения,
	|	РасчетЗемельногоНалога.ДатаПостановкиНаУчет КАК ДатаПостановкиНаУчет,
	|	РасчетЗемельногоНалога.ДатаСнятияСУчета КАК ДатаСнятияСУчета,
	|	РасчетЗемельногоНалога.СуммаЛьгот КАК СуммаЛьгот,
	|	РасчетЗемельногоНалога.СуммаНалога КАК СуммаНалога,
	|	РасчетЗемельногоНалога.ЕстьЛьготы КАК ЕстьЛьготы,
	|	ЕСТЬNULL(ВтКорректировок.СуммаНалога, 0) КАК СуммаКорректировок
	|ПОМЕСТИТЬ РасчетНалога
	|
	|ИЗ
	|	РасчетНалогаБезКорректировок КАК РасчетЗемельногоНалога
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтКорректировок КАК ВтКорректировок
	|	ПО РасчетЗемельногоНалога.ОсновноеСредство = ВтКорректировок.ОсновноеСредство
	|			И РасчетЗемельногоНалога.КодНалоговогоОргана = ВтКорректировок.КодНалоговогоОргана
	|			И РасчетЗемельногоНалога.КодПоОКТМО = ВтКорректировок.КодПоОКТМО 
	|
	|ОБЪЕДИНИТЬ ВСЕ
	| 
	|ВЫБРАТЬ
	|	ВтКорректировок.Организация КАК Организация,
	|	ВтКорректировок.ОсновноеСредство КАК ОсновноеСредство,
	|	ВтКорректировок.ОсновноеСредство.Наименование КАК НаименованиеОбъекта,
	|	ПараметрыОС.КадастровыйНомер КАК КадастровыйНомер,
	|	ВтКорректировок.КодНалоговогоОргана КАК КодНалоговогоОргана,
	|	ВтКорректировок.КодПоОКТМО КАК КодПоОКТМО,
	|	0 КАК НалоговаяБаза,
	|	ПараметрыОС.ДоляВПравеОбщейСобственностиЧислитель КАК ДоляВПравеЧислитель,
	|	ПараметрыОС.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеЗнаменатель,
	|	ПараметрыОС.НеОблагаемаяНалогомСумма КАК НалоговыйВычет,
	|	ПараметрыОС.НалоговаяСтавка КАК НалоговаяСтавка,
	|	1 КАК ПовышающийКоэффициент,
	|	0 КАК КоличествоМесяцевВладения,
	|	ПараметрыОС.ДатаРегистрацииПравНаОбъектНедвижимости КАК ДатаПостановкиНаУчет,
	|
	|	ВЫБОР
	|		КОГДА ПараметрыОС.ВидЗаписи = ЗНАЧЕНИЕ(Перечисление.ВидЗаписиОРегистрации.СнятиеСРегистрационногоУчета) ТОГДА
	|			ПараметрыОС.Период
	|		ИНАЧЕ
	|			ДАТАВРЕМЯ(1,1,1)
	|		КОНЕЦ КАК ДатаСнятияСУчета,
	|
	|	0 КАК СуммаЛьгот,
	|	0 КАК СуммаНалога,
	|	ЛОЖЬ КАК ЕстьЛьготы,
	|	ЕСТЬNULL(ВтКорректировок.СуммаНалога, 0) КАК СуммаКорректировок
	|
	|ИЗ
	|	ВтКорректировок КАК ВтКорректировок
	|	ЛЕВОЕ СОЕДИНЕНИЕ РасчетНалогаБезКорректировок КАК РасчетЗемельногоНалога
	|	ПО РасчетЗемельногоНалога.ОсновноеСредство = ВтКорректировок.ОсновноеСредство
	|			И РасчетЗемельногоНалога.КодНалоговогоОргана = ВтКорректировок.КодНалоговогоОргана
	|			И РасчетЗемельногоНалога.КодПоОКТМО = ВтКорректировок.КодПоОКТМО
	|			И РасчетЗемельногоНалога.КодПоОКТМО ЕСТЬ NULL
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрацияЗемельныхУчастков_СрезПоследних КАК ПараметрыОС
	|	ПО ПараметрыОС.Организация = ВтКорректировок.Организация
	|	И  ПараметрыОС.ОсновноеСредство = ВтКорректировок.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегистрацияЗемельныхУчастков.ОсновноеСредство КАК ОсновноеСредство,
	|	МАКСИМУМ(РегистрацияЗемельныхУчастков.Период) КАК ДатаПоследнегоИзменения
	|ПОМЕСТИТЬ ДатыПоследнихИзменений
	|ИЗ
	|	РегистрСведений.ПараметрыНачисленияЗемельногоНалога КАК РегистрацияЗемельныхУчастков
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РасчетНалога КАК РасчетНалога
	|		ПО РегистрацияЗемельныхУчастков.Организация = РасчетНалога.Организация
	|		И РегистрацияЗемельныхУчастков.ОсновноеСредство = РасчетНалога.ОсновноеСредство
	|СГРУППИРОВАТЬ ПО
	|	РегистрацияЗемельныхУчастков.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетНалога.ОсновноеСредство КАК ОсновноеСредство,
	|	РасчетНалога.КадастровыйНомер КАК КадастровыйНомер,
	|	РасчетНалога.КодНалоговогоОргана КАК КодНалоговогоОргана,
	|	РасчетНалога.КодПоОКТМО КАК КодПоОКТМО,
	|	АВТОНОМЕРЗАПИСИ() КАК Ключ
	|ПОМЕСТИТЬ ГруппировкаПоОбъектам
	|ИЗ
	|	РасчетНалога КАК РасчетНалога
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	КадастровыйНомер,
	|	КодНалоговогоОргана,
	|	КодПоОКТМО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетНалога.ОсновноеСредство КАК ОсновноеСредство,
	|	РасчетНалога.НаименованиеОбъекта КАК НаименованиеОбъекта,
	|	РасчетНалога.КадастровыйНомер КАК КадастровыйНомер,
	|	РасчетНалога.КодНалоговогоОргана КАК КодНалоговогоОргана,
	|	РасчетНалога.КодПоОКТМО КАК КодПоОКТМО,
	|	РасчетНалога.НалоговаяБаза КАК НалоговаяБаза,
	|	РасчетНалога.ДоляВПравеЧислитель КАК ДоляВПравеЧислитель,
	|	РасчетНалога.ДоляВПравеЗнаменатель КАК ДоляВПравеЗнаменатель,
	|	РасчетНалога.НалоговыйВычет КАК НалоговыйВычет,
	|	РасчетНалога.НалоговаяСтавка КАК НалоговаяСтавка,
	|	РасчетНалога.ПовышающийКоэффициент КАК ПовышающийКоэффициент,
	|	РасчетНалога.КоличествоМесяцевВладения КАК КоличествоМесяцевВладения,
	|	РасчетНалога.ДатаПостановкиНаУчет КАК ДатаПостановкиНаУчет,
	|	РасчетНалога.ДатаСнятияСУчета КАК ДатаСнятияСУчета,
	|	ЕСТЬNULL(ДатыПоследнихИзменений.ДатаПоследнегоИзменения, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаПоследнегоИзменения,
	|	РасчетНалога.СуммаЛьгот КАК СуммаЛьгот,
	|	РасчетНалога.СуммаНалога КАК СуммаНалога,
	|	ЕСТЬNULL(РасчетНалога.СуммаКорректировок, 0) КАК СуммаКорректировок,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ДатыПоследнихИзменений.ДатаПоследнегоИзменения, ДАТАВРЕМЯ(1, 1, 1)) >= &ПериодОсобогоКонтроля
	|			ТОГДА 0
	|		КОГДА РасчетНалога.ЕстьЛьготы
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК Порядок,
	|	ГруппировкаПоОбъектам.Ключ КАК КлючГруппировки
	|ИЗ
	|	РасчетНалога КАК РасчетНалога
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ГруппировкаПоОбъектам КАК ГруппировкаПоОбъектам
	|		ПО РасчетНалога.ОсновноеСредство = ГруппировкаПоОбъектам.ОсновноеСредство
	|		И РасчетНалога.КадастровыйНомер = ГруппировкаПоОбъектам.КадастровыйНомер
	|		И РасчетНалога.КодНалоговогоОргана = ГруппировкаПоОбъектам.КодНалоговогоОргана
	|		И РасчетНалога.КодПоОКТМО = ГруппировкаПоОбъектам.КодПоОКТМО
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДатыПоследнихИзменений КАК ДатыПоследнихИзменений
	|		ПО РасчетНалога.ОсновноеСредство = ДатыПоследнихИзменений.ОсновноеСредство
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	ДатаПостановкиНаУчет,
	|	ОсновноеСредство
	|ИТОГИ
	|	МАКСИМУМ(ОсновноеСредство),
	|	МАКСИМУМ(НаименованиеОбъекта),
	|	МАКСИМУМ(КадастровыйНомер),
	|	МАКСИМУМ(КодНалоговогоОргана),
	|	МАКСИМУМ(КодПоОКТМО),
	|	МИНИМУМ(ДатаПостановкиНаУчет),
	|	МИНИМУМ(ДатаСнятияСУчета),
	|	МАКСИМУМ(ДатаПоследнегоИзменения),
	|	МАКСИМУМ(СуммаКорректировок),
	|	СУММА(СуммаЛьгот),
	|	СУММА(СуммаНалога),
	|	МИНИМУМ(Порядок)
	|ПО
	|	КлючГруппировки";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаИмущество()

	// Основную часть данных об имуществе берем из расчета налога. Сверяем только кадастровую недвижимость (недвижимость
	// по среднегодовой стоимости декларируется, и ФНС по ней расчет не присылает).
	// Но там отсутствуют данные о снятии с учета.
	// Для получения этих данных используем такой алгоритм:
	// 1. Выбираем данные из регистра расчета налога (из нового или из прежнего в зависимости от периода и наличия данных)
	//    См. врем таблицу РасчетНалога
	// 2. Для каждого отдельного периода действия набора параметров (т.е для каждой ДатаСведений) определяем дату прекращения
	//    его действия: это либо дата прекращения права собственности, либо следующая ДатаСведений
	//    См. врем таблицу ДатыПрекращенияРасчета
	// 3. Определяем даты последних изменений по объектам - это период последней записи в регистре настроек. Не отбираем
	//    по виду налоговой базы, т.к. объект теоретически мог быть переквалифицирован в облагаемый по среднегодовой стоимости.
	//    См. врем таблицу ДатыПоследнихИзменений 
	// 4. Под объектом сверки понимается сочетание ОсновноеСредство - код налогового органа - код ОКТМО. Для группировки
	//    итогового результата по сочетанию этих полей, используем временную таблицу группировок
	//    См. врем таблицу ГруппировкаПоОбъектам
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РасчетНалогаНаИмуществоПоКадастровойСтоимости.ОсновноеСредство КАК ОсновноеСредство,
	|	РасчетНалогаНаИмуществоПоКадастровойСтоимости.ОсновноеСредство.Наименование КАК Наименование,
	|	ВЫБОР
	|		КОГДА РасчетНалогаНаИмуществоПоКадастровойСтоимости.КадастровыйНомер <> """"
	|			ТОГДА РасчетНалогаНаИмуществоПоКадастровойСтоимости.КадастровыйНомер
	|		ИНАЧЕ РасчетНалогаНаИмуществоПоКадастровойСтоимости.КадастровыйНомерПомещения
	|	КОНЕЦ КАК КадастровыйНомер,
	|	РасчетНалогаНаИмуществоПоКадастровойСтоимости.ИФНС.Код КАК КодНалоговогоОргана,
	|	РасчетНалогаНаИмуществоПоКадастровойСтоимости.КодПоОКТМО КАК КодПоОКТМО,
	|	РасчетНалогаНаИмуществоПоКадастровойСтоимости.ДатаСведений КАК ДатаСведений,
	|	РасчетНалогаНаИмуществоПоКадастровойСтоимости.КадастроваяСтоимость КАК КадастроваяСтоимость,
	|	РасчетНалогаНаИмуществоПоКадастровойСтоимости.ДоляВПравеОбщейСобственностиЧислитель КАК ДоляВПравеОбщейСобственностиЧислитель,
	|	РасчетНалогаНаИмуществоПоКадастровойСтоимости.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеОбщейСобственностиЗнаменатель,
	|	РасчетНалогаНаИмуществоПоКадастровойСтоимости.ДоляСтоимостиЧислитель КАК ДоляСтоимостиЧислитель,
	|	РасчетНалогаНаИмуществоПоКадастровойСтоимости.ДоляСтоимостиЗнаменатель КАК ДоляСтоимостиЗнаменатель,
	|	РасчетНалогаНаИмуществоПоКадастровойСтоимости.НалоговаяСтавка КАК НалоговаяСтавка,
	|	РасчетНалогаНаИмуществоПоКадастровойСтоимости.КоличествоМесяцевИспользования КАК КоличествоМесяцевИспользования,
	|	РасчетНалогаНаИмуществоПоКадастровойСтоимости.СуммаЛьгот КАК СуммаЛьгот,
	|	РасчетНалогаНаИмуществоПоКадастровойСтоимости.СуммаНалога КАК СуммаНалога,
	|	НЕ РасчетНалогаНаИмуществоПоКадастровойСтоимости.ОснованиеЛьготы = ЗНАЧЕНИЕ(Справочник.ОснованияЛьготПоИмущественнымНалогам.ПустаяСсылка) КАК ЕстьЛьгота
	|ПОМЕСТИТЬ РасчетНалогаБезКорректировок
	|ИЗ
	|	РегистрСведений.РасчетНалогаНаИмуществоПоКадастровойСтоимости КАК РасчетНалогаНаИмуществоПоКадастровойСтоимости
	|ГДЕ
	|	НЕ &ИспользоватьПрежнийРасчетНалога
	|	И РасчетНалогаНаИмуществоПоКадастровойСтоимости.Организация = &Организация
	|	И РасчетНалогаНаИмуществоПоКадастровойСтоимости.ПериодРасчета = &ПериодРасчета
	|	И РасчетНалогаНаИмуществоПоКадастровойСтоимости.ИФНС.Код В(&КодыНалоговыхОрганов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасчетНалогаНаИмущество.ОсновноеСредство,
	|	РасчетНалогаНаИмущество.ОсновноеСредство.Наименование,
	|	ВЫБОР
	|		КОГДА РасчетНалогаНаИмущество.КадастровыйНомер <> """"
	|			ТОГДА РасчетНалогаНаИмущество.КадастровыйНомер
	|		ИНАЧЕ РасчетНалогаНаИмущество.КадастровыйНомерПомещения
	|	КОНЕЦ,
	|	РасчетНалогаНаИмущество.ИФНС.Код,
	|	РасчетНалогаНаИмущество.КодПоОКТМО,
	|	РасчетНалогаНаИмущество.ДатаСведений,
	|	РасчетНалогаНаИмущество.КадастроваяСтоимость,
	|	РасчетНалогаНаИмущество.ДоляВПравеОбщейСобственностиЧислитель,
	|	РасчетНалогаНаИмущество.ДоляВПравеОбщейСобственностиЗнаменатель,
	|	РасчетНалогаНаИмущество.ДоляСтоимостиЧислитель,
	|	РасчетНалогаНаИмущество.ДоляСтоимостиЗнаменатель,
	|	РасчетНалогаНаИмущество.НалоговаяСтавка,
	|	РасчетНалогаНаИмущество.КоличествоМесяцевИспользования,
	|	(ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА РасчетНалогаНаИмущество.НеоблагаемаяКадастроваяСтоимость >= РасчетНалогаНаИмущество.КадастроваяСтоимость
	|				ТОГДА ВЫРАЗИТЬ(РасчетНалогаНаИмущество.КадастроваяСтоимость КАК ЧИСЛО(15, 0))
	|			КОГДА РасчетНалогаНаИмущество.НеоблагаемаяКадастроваяСтоимость > 0
	|				ТОГДА ВЫРАЗИТЬ(РасчетНалогаНаИмущество.НеоблагаемаяКадастроваяСтоимость КАК ЧИСЛО(15, 0))
	|			ИНАЧЕ 0
	|		КОНЕЦ * ВЫБОР
	|			КОГДА РасчетНалогаНаИмущество.ДоляСтоимостиЧислитель > 0
	|					И РасчетНалогаНаИмущество.ДоляСтоимостиЗнаменатель > 0
	|				ТОГДА РасчетНалогаНаИмущество.ДоляСтоимостиЧислитель / РасчетНалогаНаИмущество.ДоляСтоимостиЗнаменатель
	|			ИНАЧЕ 1
	|		КОНЕЦ * ВЫБОР
	|			КОГДА РасчетНалогаНаИмущество.ДоляВПравеОбщейСобственностиЧислитель > 0
	|					И РасчетНалогаНаИмущество.ДоляВПравеОбщейСобственностиЗнаменатель > 0
	|				ТОГДА РасчетНалогаНаИмущество.ДоляВПравеОбщейСобственностиЧислитель / РасчетНалогаНаИмущество.ДоляВПравеОбщейСобственностиЗнаменатель
	|			ИНАЧЕ 1
	|		КОНЕЦ * РасчетНалогаНаИмущество.КоличествоМесяцевВладения / 12 * ВЫБОР
	|			КОГДА РасчетНалогаНаИмущество.КоличествоМесяцевВладения > 0
	|				ТОГДА РасчетНалогаНаИмущество.КоличествоМесяцевИспользования / РасчетНалогаНаИмущество.КоличествоМесяцевВладения
	|			ИНАЧЕ 1
	|		КОНЕЦ * РасчетНалогаНаИмущество.НалоговаяСтавка / 100 КАК ЧИСЛО(15, 0))) + РасчетНалогаНаИмущество.СуммаУменьшенияСуммыНалога,
	|	РасчетНалогаНаИмущество.СуммаНалогаКУплате + РасчетНалогаНаИмущество.СуммаАвансовыхПлатежей,
	|	НЕ РасчетНалогаНаИмущество.ОснованиеЛьготы = ЗНАЧЕНИЕ(Справочник.ОснованияЛьготПоИмущественнымНалогам.ПустаяСсылка)
	|ИЗ
	|	РегистрСведений.РасчетНалогаНаИмущество КАК РасчетНалогаНаИмущество
	|ГДЕ
	|	&ИспользоватьПрежнийРасчетНалога
	|	И РасчетНалогаНаИмущество.Организация = &Организация
	|	И РасчетНалогаНаИмущество.ПериодРасчета = &ПериодРасчета
	|	И РасчетНалогаНаИмущество.ИФНС.Код В(&КодыНалоговыхОрганов)
	|	И РасчетНалогаНаИмущество.ВидНалоговойБазы = ЗНАЧЕНИЕ(Перечисление.НалоговаяБазаПоНалогуНаИмущество.КадастроваяСтоимость)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	КодНалоговогоОргана,
	|	КодПоОКТМО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетНалогаБезКорректировок.ОсновноеСредство КАК ОсновноеСредство,
	|	РасчетНалогаБезКорректировок.Наименование КАК Наименование,
	|	РасчетНалогаБезКорректировок.КадастровыйНомер КАК КадастровыйНомер,
	|	РасчетНалогаБезКорректировок.КодНалоговогоОргана КАК КодНалоговогоОргана,
	|	РасчетНалогаБезКорректировок.КодПоОКТМО КАК КодПоОКТМО,
	|	РасчетНалогаБезКорректировок.ДатаСведений КАК ДатаСведений,
	|	РасчетНалогаБезКорректировок.КадастроваяСтоимость КАК КадастроваяСтоимость,
	|	РасчетНалогаБезКорректировок.ДоляВПравеОбщейСобственностиЧислитель КАК ДоляВПравеОбщейСобственностиЧислитель,
	|	РасчетНалогаБезКорректировок.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеОбщейСобственностиЗнаменатель,
	|	РасчетНалогаБезКорректировок.ДоляСтоимостиЧислитель КАК ДоляСтоимостиЧислитель,
	|	РасчетНалогаБезКорректировок.ДоляСтоимостиЗнаменатель КАК ДоляСтоимостиЗнаменатель,
	|	РасчетНалогаБезКорректировок.НалоговаяСтавка КАК НалоговаяСтавка,
	|	РасчетНалогаБезКорректировок.КоличествоМесяцевИспользования КАК КоличествоМесяцевИспользования,
	|	РасчетНалогаБезКорректировок.СуммаЛьгот КАК СуммаЛьгот,
	|	РасчетНалогаБезКорректировок.СуммаНалога КАК СуммаНалога,
	|	ЕСТЬNULL(ВтКорректировок.СуммаНалога, 0) КАК СуммаКорректировок,
	|	РасчетНалогаБезКорректировок.ЕстьЛьгота КАК ЕстьЛьгота
	|ПОМЕСТИТЬ РасчетНалога
	|ИЗ
	|	РасчетНалогаБезКорректировок КАК РасчетНалогаБезКорректировок
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтКорректировок КАК ВтКорректировок
	|		ПО РасчетНалогаБезКорректировок.ОсновноеСредство = ВтКорректировок.ОсновноеСредство
	|			И РасчетНалогаБезКорректировок.КодНалоговогоОргана = ВтКорректировок.КодНалоговогоОргана
	|			И РасчетНалогаБезКорректировок.КодПоОКТМО = ВтКорректировок.КодПоОКТМО
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВтКорректировок.ОсновноеСредство,
	|	ВтКорректировок.ОсновноеСредство.Наименование,
	|	ПараметрыОС.КадастровыйНомер,
	|	ВтКорректировок.КодНалоговогоОргана,
	|	ВтКорректировок.КодПоОКТМО,
	|	ПараметрыОС.Период,
	|	ПараметрыОС.КадастроваяСтоимость,
	|	ПараметрыОС.ДоляВПравеОбщейСобственностиЧислитель,
	|	ПараметрыОС.ДоляВПравеОбщейСобственностиЗнаменатель,
	|	ПараметрыОС.ДоляСтоимостиЧислитель,
	|	ПараметрыОС.ДоляСтоимостиЗнаменатель,
	|	ПараметрыОС.НалоговаяСтавка,
	|	0,
	|	0,
	|	0,
	|	ЕСТЬNULL(ВтКорректировок.СуммаНалога, 0),
	|	ЛОЖЬ
	|ИЗ
	|	ВтКорректировок КАК ВтКорректировок
	|		ЛЕВОЕ СОЕДИНЕНИЕ РасчетНалогаБезКорректировок КАК РасчетНалогаНаИмущество
	|		ПО (РасчетНалогаНаИмущество.ОсновноеСредство = ВтКорректировок.ОсновноеСредство)
	|			И (РасчетНалогаНаИмущество.КодНалоговогоОргана = ВтКорректировок.КодНалоговогоОргана)
	|			И (РасчетНалогаНаИмущество.КодПоОКТМО = ВтКорректировок.КодПоОКТМО)
	|			И (РасчетНалогаНаИмущество.КодПоОКТМО ЕСТЬ NULL)
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтавкиНалогаНаИмуществоПоОтдельнымОС_СрезПоследних КАК ПараметрыОС
	|		ПО (ПараметрыОС.Организация = ВтКорректировок.Организация)
	|			И (ПараметрыОС.ОсновноеСредство = ВтКорректировок.ОсновноеСредство)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	КадастровыйНомер,
	|	КодНалоговогоОргана,
	|	КодПоОКТМО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетНалога.ОсновноеСредство КАК ОсновноеСредство,
	|	РасчетНалога.ДатаСведений КАК ДатаСведений
	|ПОМЕСТИТЬ ВсеДатыСведений
	|ИЗ
	|	РасчетНалога КАК РасчетНалога
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаписиПрекращенияРасчета.ОсновноеСредство КАК ОсновноеСредство,
	|	ЗаписиПрекращенияРасчета.ДатаСведений КАК ДатаСведений,
	|	МИНИМУМ(ЗаписиПрекращенияРасчета.ДатаПрекращенияРасчета) КАК ДатаПрекращенияРасчета
	|ПОМЕСТИТЬ ДатыПрекращенияРасчета
	|ИЗ
	|	(ВЫБРАТЬ
	|		СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство КАК ОсновноеСредство,
	|		ВсеДатыСведений.ДатаСведений КАК ДатаСведений,
	|		ВЫБОР
	|			КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.ДатаПрекращенияПраваСобственности < ВсеДатыСведений.ДатаСведений
	|				ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.Период
	|			ИНАЧЕ СтавкиНалогаНаИмуществоПоОтдельнымОС.ДатаПрекращенияПраваСобственности
	|		КОНЕЦ КАК ДатаПрекращенияРасчета
	|	ИЗ
	|		СтавкиНалогаНаИмуществоПоОтдельнымОС_Записи КАК СтавкиНалогаНаИмуществоПоОтдельнымОС
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВсеДатыСведений КАК ВсеДатыСведений
	|			ПО СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство = ВсеДатыСведений.ОсновноеСредство
	|				И СтавкиНалогаНаИмуществоПоОтдельнымОС.Период >= ВсеДатыСведений.ДатаСведений
	|	ГДЕ
	|		СтавкиНалогаНаИмуществоПоОтдельнымОС.Организация = &Организация
	|		И СтавкиНалогаНаИмуществоПоОтдельнымОС.ДатаПрекращенияПраваСобственности <= &ПериодРасчета
	|		И СтавкиНалогаНаИмуществоПоОтдельнымОС.ДатаПрекращенияПраваСобственности <> ДАТАВРЕМЯ(1, 1, 1)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство,
	|		ВсеДатыСведений.ДатаСведений,
	|		СтавкиНалогаНаИмуществоПоОтдельнымОС.Период
	|	ИЗ
	|		СтавкиНалогаНаИмуществоПоОтдельнымОС_Записи КАК СтавкиНалогаНаИмуществоПоОтдельнымОС
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВсеДатыСведений КАК ВсеДатыСведений
	|			ПО СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство = ВсеДатыСведений.ОсновноеСредство
	|				И СтавкиНалогаНаИмуществоПоОтдельнымОС.Период >= ВсеДатыСведений.ДатаСведений
	|	ГДЕ
	|		СтавкиНалогаНаИмуществоПоОтдельнымОС.Организация = &Организация
	|		И СтавкиНалогаНаИмуществоПоОтдельнымОС.НеПодлежитНалогообложению
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВсеДатыСведений.ОсновноеСредство,
	|		ВсеДатыСведений.ДатаСведений,
	|		СледующиеДатыСведений.ДатаСведений
	|	ИЗ
	|		ВсеДатыСведений КАК ВсеДатыСведений
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВсеДатыСведений КАК СледующиеДатыСведений
	|			ПО ВсеДатыСведений.ОсновноеСредство = СледующиеДатыСведений.ОсновноеСредство
	|				И ВсеДатыСведений.ДатаСведений < СледующиеДатыСведений.ДатаСведений) КАК ЗаписиПрекращенияРасчета
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаписиПрекращенияРасчета.ОсновноеСредство,
	|	ЗаписиПрекращенияРасчета.ДатаСведений
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ДатаСведений
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство КАК ОсновноеСредство,
	|	МАКСИМУМ(СтавкиНалогаНаИмуществоПоОтдельнымОС.Период) КАК ДатаПоследнегоИзменения
	|ПОМЕСТИТЬ ДатыПоследнихИзменений
	|ИЗ
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС_Записи КАК СтавкиНалогаНаИмуществоПоОтдельнымОС
	|ГДЕ
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС.Организация = &Организация
	|	И СтавкиНалогаНаИмуществоПоОтдельнымОС.Период <= &ПериодРасчета
	|	И СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство В
	|			(ВЫБРАТЬ
	|				РасчетНалога.ОсновноеСредство
	|			ИЗ
	|				РасчетНалога КАК РасчетНалога)
	|
	|СГРУППИРОВАТЬ ПО
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетНалога.ОсновноеСредство КАК ОсновноеСредство,
	|	РасчетНалога.КадастровыйНомер КАК КадастровыйНомер,
	|	РасчетНалога.КодНалоговогоОргана КАК КодНалоговогоОргана,
	|	РасчетНалога.КодПоОКТМО КАК КодПоОКТМО,
	|	АВТОНОМЕРЗАПИСИ() КАК Ключ
	|ПОМЕСТИТЬ ГруппировкаПоОбъектам
	|ИЗ
	|	РасчетНалога КАК РасчетНалога
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	КадастровыйНомер,
	|	КодНалоговогоОргана,
	|	КодПоОКТМО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетНалога.ОсновноеСредство КАК ОсновноеСредство,
	|	РасчетНалога.Наименование КАК НаименованиеОбъекта,
	|	РасчетНалога.КадастровыйНомер КАК КадастровыйНомер,
	|	РасчетНалога.КодНалоговогоОргана КАК КодНалоговогоОргана,
	|	РасчетНалога.КодПоОКТМО КАК КодПоОКТМО,
	|	РасчетНалога.КадастроваяСтоимость КАК НалоговаяБаза,
	|	РасчетНалога.ДоляВПравеОбщейСобственностиЧислитель КАК ДоляВПравеОбщейСобственностиЧислитель,
	|	РасчетНалога.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеОбщейСобственностиЗнаменатель,
	|	РасчетНалога.ДоляСтоимостиЧислитель КАК ДоляСтоимостиЧислитель,
	|	РасчетНалога.ДоляСтоимостиЗнаменатель КАК ДоляСтоимостиЗнаменатель,
	|	РасчетНалога.НалоговаяСтавка КАК НалоговаяСтавка,
	|	РасчетНалога.КоличествоМесяцевИспользования КАК КоличествоМесяцевИспользования,
	|	РасчетНалога.СуммаЛьгот КАК СуммаЛьгот,
	|	РасчетНалога.СуммаНалога КАК СуммаНалога,
	|	РасчетНалога.СуммаКорректировок КАК СуммаКорректировок,
	|	РасчетНалога.ДатаСведений КАК ДатаПостановкиНаУчет,
	|	ЕСТЬNULL(ДатыПрекращенияРасчета.ДатаПрекращенияРасчета, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаСнятияСУчета,
	|	ЕСТЬNULL(ДатыПоследнихИзменений.ДатаПоследнегоИзменения, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаПоследнегоИзменения,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ДатыПоследнихИзменений.ДатаПоследнегоИзменения, ДАТАВРЕМЯ(1, 1, 1)) >= &ПериодОсобогоКонтроля
	|			ТОГДА 0
	|		КОГДА РасчетНалога.ЕстьЛьгота
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК Порядок,
	|	ГруппировкаПоОбъектам.Ключ КАК КлючГруппировки
	|ИЗ
	|	РасчетНалога КАК РасчетНалога
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ГруппировкаПоОбъектам КАК ГруппировкаПоОбъектам
	|		ПО РасчетНалога.ОсновноеСредство = ГруппировкаПоОбъектам.ОсновноеСредство
	|			И РасчетНалога.КадастровыйНомер = ГруппировкаПоОбъектам.КадастровыйНомер
	|			И РасчетНалога.КодНалоговогоОргана = ГруппировкаПоОбъектам.КодНалоговогоОргана
	|			И РасчетНалога.КодПоОКТМО = ГруппировкаПоОбъектам.КодПоОКТМО
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДатыПрекращенияРасчета КАК ДатыПрекращенияРасчета
	|		ПО РасчетНалога.ОсновноеСредство = ДатыПрекращенияРасчета.ОсновноеСредство
	|			И РасчетНалога.ДатаСведений = ДатыПрекращенияРасчета.ДатаСведений
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДатыПоследнихИзменений КАК ДатыПоследнихИзменений
	|		ПО РасчетНалога.ОсновноеСредство = ДатыПоследнихИзменений.ОсновноеСредство
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	ДатаПостановкиНаУчет,
	|	ОсновноеСредство
	|ИТОГИ
	|	МАКСИМУМ(ОсновноеСредство),
	|	МАКСИМУМ(НаименованиеОбъекта),
	|	МАКСИМУМ(КадастровыйНомер),
	|	МАКСИМУМ(КодНалоговогоОргана),
	|	МАКСИМУМ(КодПоОКТМО),
	|	СУММА(СуммаЛьгот),
	|	СУММА(СуммаНалога),
	|	МАКСИМУМ(СуммаКорректировок),
	|	МИНИМУМ(ДатаПостановкиНаУчет),
	|	МИНИМУМ(ДатаСнятияСУчета),
	|	МАКСИМУМ(ДатаПоследнегоИзменения),
	|	МИНИМУМ(Порядок)
	|ПО
	|	КлючГруппировки";


	Возврат ТекстЗапроса;

КонецФункции

// Обновить расчет.
// Заполняет заново документ по данным информационной базы и восстанавливает строки введенные вручную.
// 
// Параметры:
//  Объект - ДанныеФормыСтруктура - Сверка с ФНС по имущественным налогам
Процедура ОбновитьРасчет(Объект) Экспорт
	// Сохраним изменения внесенные пользователем вручную.
	СверкаДоОбновления = Объект.Сверка.Выгрузить();
	
	// Перезаполним таблицы сверки по данным программы.
	// При этом изменятся идентификаторы строк.
	КодыНалоговыхОргановСверки = СверкаДоОбновления.Скопировать(,"КодНалоговогоОргана");
	КодыНалоговыхОргановСверки.Свернуть("КодНалоговогоОргана", "");
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("КодыНалоговыхОрганов", КодыНалоговыхОргановСверки.ВыгрузитьКолонку("КодНалоговогоОргана"));
	ПараметрыЗаполнения.Вставить("Объект", Объект);
	
	ЗаполнитьОбъектыСверки(ПараметрыЗаполнения);
	
	// Заменим в таблице расчета ФНС идентификаторы строк на новые.
	СверкаПослеОбновления = Объект.Сверка.Выгрузить();
	Для Каждого СтрокаФНС Из Объект.РасчетФНС Цикл
		СтараяСтрока = СверкаДоОбновления.Найти(СтрокаФНС.ИдентификаторСтроки, "ИдентификаторСтроки");
		Если СтараяСтрока <> Неопределено Тогда
			НоваяСтрока = СверкаПослеОбновления.Найти(СтараяСтрока.ОсновноеСредство, "ОсновноеСредство");
			Если НоваяСтрока <> Неопределено Тогда
				СтрокаФНС.ИдентификаторСтроки = НоваяСтрока.ИдентификаторСтроки;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	// Восстановим данные, введенные пользователем до обновления.
	ПараметрыПоиска = Новый Структура();
	
	Для Каждого СтрокаСверки Из Объект.Сверка Цикл
		
		ПараметрыПоиска.Вставить("ОсновноеСредство",	СтрокаСверки.ОсновноеСредство);
		ПараметрыПоиска.Вставить("КодНалоговогоОргана",	СтрокаСверки.КодНалоговогоОргана);
		ПараметрыПоиска.Вставить("КодПоОКТМО",			СтрокаСверки.КодПоОКТМО);
		
		СтрокиДоОбновления = СверкаДоОбновления.НайтиСтроки(ПараметрыПоиска);
		
		Если СтрокиДоОбновления.Количество() > 0 Тогда
			СтрокаДоОбновления = СтрокиДоОбновления.Получить(0);
			
			СписокВосстанавливаемыхСвойств = "СуммаНалогаФНС,СуммаЛьготФНС,ОбъектЕстьВРасчетеФНС,ПравильныйРасчет,ПоляРасхождений";
			ЗаполнитьЗначенияСвойств(СтрокаСверки, СтрокаДоОбновления, СписокВосстанавливаемыхСвойств);
			
			// Строку обрабатывали до обновления вручную. После обновления суммы совпали.
			Если СтрокаСверки.СуммаНалогаФНС = СтрокаСверки.СуммаНалога
				И СтрокаСверки.ОбъектЕстьВРасчетеФНС
				И СтрокаДоОбновления.ПравильныйРасчет <> 0 Тогда
				
				СтрокаСверки.ПравильныйРасчет = 1; //В программе.
				СтрокаСверки.ПоляРасхождений = "";
				
			// Строку обрабатывали до обновления вручную. После обновления суммы не совпали.
			ИначеЕсли СтрокаСверки.СуммаНалогаФНС <> СтрокаСверки.СуммаНалога 
				И СтрокаСверки.ОбъектЕстьВРасчетеФНС
				И СтрокаДоОбновления.ПравильныйРасчет <> 0 Тогда
				
				// Требуется заново обработать строку.
				СтрокаСверки.ПоляРасхождений = "";
				СтрокаСверки.ПравильныйРасчет = 0;
				
			Иначе
				// Требуется заново обработать строку.
				СтрокаСверки.ПоляРасхождений = "";
				СтрокаСверки.ПравильныйРасчет = 0;
			КонецЕсли;
				
		КонецЕсли;
	КонецЦикла;
	
	Если Объект.ЗагруженРасчетФНС Тогда
		// Вернем строки, которых нет в данных расчета по программе.
		ПараметрыПоиска = Новый Структура();
		ПараметрыПоиска.Вставить("ОбъектЕстьВРасчете", Ложь);
		ПараметрыПоиска.Вставить("ОбъектЕстьВРасчетеФНС", Истина);
	Иначе
		// Вернем строки, добавленные вручную.
		ПараметрыПоиска = Новый Структура();
		ПараметрыПоиска.Вставить("ДобавленоВручную", Истина);
	КонецЕсли;
	
	СтрокиВручнуюДоОбновления = СверкаДоОбновления.НайтиСтроки(ПараметрыПоиска);
	
	ТекстЗапросаИсправленныеОС = 
	"ВЫБРАТЬ
	|	ВтКорректировок.СуммаНалога КАК СуммаНалога
	|ИЗ
	|	ВтКорректировок КАК ВтКорректировок
	|ГДЕ
	|	ВтКорректировок.ОсновноеСредство = &ОсновноеСредство
	|	И ВтКорректировок.КодНалоговогоОргана В(&КодыНалоговыхОрганов)
	|	И ВтКорректировок.КодПоОКТМО = &КодПоОКТМО";
	
	ЗапросКорректировок = Новый Запрос;
	ЗапросКорректировок.Текст = ТекстЗапросаИсправленныеОС;
	ЗапросКорректировок.МенеджерВременныхТаблиц = ПараметрыЗаполнения.МенеджерВременныхТаблиц;
	
	Для Каждого СтрокаВручную Из СтрокиВручнуюДоОбновления Цикл
		
		// Не восстанавливаем строки, введенные вручную или строки загруженные из файла по отсутствующим в учете ОСам,
		// если после их ввода были сделаны исправления в учете и перезакрыты периоды.
		// В результате перезакрытия периодов появилась информация по ОС в регистре расчета налога.
		СтрокиСверкиСДобавленнымВручнуюОС = Объект.Сверка.НайтиСтроки(Новый Структура("ОсновноеСредство", СтрокаВручную.ОсновноеСредство));
		Если СтрокиСверкиСДобавленнымВручнуюОС.Количество() <> 0 Тогда
			 Продолжить;
		КонецЕсли;
		
		// По строкам, добавленным вручную, или строкам, загруженным из файла, по отсутствующим в учете ОСам,
		// если после их ввода и создания исправительных документов перезакрытия периодов не производилось.
		// Сумма по программе не заполнена, т.к. ОС нет в регистре сведений расчета налога.
		// Обновим сумму налога по данным корректировок.
		ВосстановленнаяСтрокаВручную = Объект.Сверка.Добавить();
		ЗаполнитьЗначенияСвойств(ВосстановленнаяСтрокаВручную, СтрокаВручную);
		
		ЗапросКорректировок.УстановитьПараметр("ОсновноеСредство", СтрокаВручную.ОсновноеСредство);
		ЗапросКорректировок.УстановитьПараметр("КодыНалоговыхОрганов", ПараметрыЗаполнения.КодыНалоговыхОрганов);
		ЗапросКорректировок.УстановитьПараметр("КодПоОКТМО", СтрокаВручную.КодПоОКТМО);
		
		Корректировка = ЗапросКорректировок.Выполнить();
		
		Если НЕ Корректировка.Пустой() Тогда 
			СтрокиКорректировки = Корректировка.Выбрать();
			СтрокиКорректировки.Следующий();
			ВосстановленнаяСтрокаВручную.СуммаНалога = СтрокиКорректировки.СуммаНалога;
		КонецЕсли;
	
	КонецЦикла;
КонецПроцедуры

#Область СтандартныеПодсистемы_УправлениеДоступом
// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа
// 
// Параметры:
//  Ограничение - Запрос - Ограничение
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
 
 Ограничение.Текст =
 "РазрешитьЧтениеИзменение
 |ГДЕ
 | ЗначениеРазрешено(Организация)";

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли