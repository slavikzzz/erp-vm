#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	МеханизмыДокумента.Добавить("ОсновныеСредства");
	МеханизмыДокумента.Добавить("РеестрДокументов");
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	МеханизмыДокумента.Добавить("УчетДоходовРасходов");
	
	ПередачаОСВАрендуЛокализация.ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента);
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка, ДокументОбъект - ссылка на документ или объект, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура:
//     * Ключ - Строка - Имя таблицы.
//     * Значение - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Если ТипЗнч(Документ) = Тип("ДокументОбъект.ПередачаОСВАренду2_4") Тогда
		ДокументСсылка = Документ.Ссылка;
	Иначе
		ДокументСсылка = Документ;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда

		ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДопПараметры);
		
		ТекстЗапросаТаблицаМестонахождениеОС(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПорядокУчетаОС(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПорядокУчетаОСБУ(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПорядокУчетаОСУУ(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПереданныеВАрендуОС(ТекстыЗапроса, Регистры);
		
		ТекстЗапросаТаблицаГрафикОплатУслугПоДоходнойАренде(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаГрафикНачисленияУслугПоДоходнойАренде(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаГрафикНачисленияПроцентовПоДоходнойАренде(ТекстыЗапроса, Регистры);
		
		ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры);
	
		//++ НЕ УТКА
		ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаОтражениеДокументовВМеждународномУчете(ТекстыЗапроса, Регистры);
		//-- НЕ УТКА
	
		ПередачаОСВАрендуЛокализация.ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры);
	
	КонецЕсли;
	
	ТаблицыДвижений = ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
	Возврат ТаблицыДвижений;
	
КонецФункции

// Формирует таблицы движений при отложенном проведении.
//
// Параметры:
//  МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Содержит временные таблицы, которые используются при формировании движений.
//
// Возвращаемое значение:
//  см. ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения
//
Функция ТаблицыОтложенногоФормированияДвижений(МенеджерВременныхТаблиц) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;

	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения();
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла;
	
	ПолучитьДанныеДокумента(Запрос);
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализацииПоПериодам(Запрос, Запрос.Параметры,, "ВтСписокДокументов");
	
	ТекстыЗапроса = Новый СписокЗначений;
		
	ТекстЗапросаТаблицаСтоимостьОС(Запрос, ТекстыЗапроса);
	ТекстЗапросаТаблицаАмортизацияОС(ТекстыЗапроса);
	ТекстЗапросаТаблицаОбесценениеВНА(ТекстыЗапроса);
	ТекстЗапросаТаблицаДвиженияДоходыРасходыПрочиеАктивыПассивы(ТекстыЗапроса);
	
	ТекстЗапросаТаблицаПорядокУчетаОС(ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПорядокУчетаОСБУ(ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПорядокУчетаОСУУ(ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПереданныеВАрендуОС(ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПрочиеРасходы(ТекстыЗапроса, Неопределено);
	
	//++ НЕ УТКА
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаОтражениеДокументовВМеждународномУчете(ТекстыЗапроса, Неопределено);
	//-- НЕ УТКА
	
	ПередачаОСВАрендуЛокализация.ТаблицыОтложенногоФормированияДвижений(ТекстыЗапроса);
	
	ТаблицыДвижений = ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса);
	
	Возврат ТаблицыДвижений;
	
КонецФункции

// Формирует таблицы движений при выполнении операции "Расчет стоимости инвестиции в аренду".
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ПередачаОСВАренду2_4 - Документ, для которого формируются движения
//  МенеджерВременныхТаблиц	- МенеджерВременныхТаблиц - Содержит вспомогательные временные таблицы, которые могут использоваться для формирования движений.
//
// Возвращаемое значение:
//  см. ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения
//
Функция ТаблицыДвиженийПриРасчетеСтоимостиИнвестицииВАренду(ДокументСсылка, МенеджерВременныхТаблиц) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, Неопределено);
	
	РасчетСтоимостиИнвестицииВАренду.УстановитьПараметрыЗапроса(Запрос);
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализацииПоПериодам(Запрос, Запрос.Параметры,, "ВтСписокДокументов");
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	РасчетСтоимостиИнвестицииВАренду.ТекстЗапросаТаблицаПрочиеРасходы(ТекстыЗапроса);
	РасчетСтоимостиИнвестицииВАренду.ТекстЗапросаТаблицаПрочиеДоходы(ТекстыЗапроса);
	РасчетСтоимостиИнвестицииВАренду.ТекстЗапросаТаблицаГрафикНачисленияПроцентовПоДоходнойАренде(ТекстыЗапроса);
	ТекстЗапросаТаблицаИнвестицииВАренду(ТекстыЗапроса, Неопределено);
	
	ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	ДопПараметры.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ДопПараметры.ТолькоПомеченные = Истина;
	
	ТаблицыДляДвижений = ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
	Возврат ТаблицыДляДвижений;
	
КонецФункции

#КонецОбласти

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Документы.ВозвратОСИзАренды2_4.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
КонецПроцедуры

// Добавляет команду создания документа "Передача ОС в аренду".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
// Возвращаемое значение:
//	СтрокаТаблицыЗначений - добавленная команда
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт

	Если ПравоДоступа("Добавление", Метаданные.Документы.ПередачаОСВАренду2_4) Тогда
		
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.ПередачаОСВАренду2_4.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.ПередачаОСВАренду2_4);
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьВнеоборотныеАктивы2_4";
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт
	
	ИсточникиДанных = Новый Соответствие;
	Возврат ИсточникиДанных;
	
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.ПередачаОСВАренду2_4";
	
	ЗначенияПараметров = ЗначенияПараметровПроведения();
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("НомерНаПечать", """""");
	
	ТекстПоля = 
	"ВЫБОР КОГДА ДанныеДокумента.РасчетыМеждуОрганизациямиАрендатор
	|		ТОГДА ДанныеДокумента.Арендатор
	|		ИНАЧЕ ДанныеДокумента.Организация
	|	КОНЕЦ";
	ПереопределениеРасчетаПараметров.Вставить("ОрганизацияПолучатель", ТекстПоля);
	
	ВЗапросеЕстьИсточник = Истина;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	ИначеЕсли ИмяРегистра = "ДокументыПоОС" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';
								|en = 'In document %ПолноеИмяДокумента%, adaptation of request for generating records of register %ИмяРегистра% is not implemented.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов"
		ИЛИ ИмяРегистра = "ДокументыПоОС" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ВЗапросеЕстьИсточник,
										ПереопределениеРасчетаПараметров);
	Иначе	
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ПереопределениеРасчетаПараметров);
	КонецЕсли; 

	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДопПараметры)
	
	ПроведениеДокументов.ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДопПараметры);

	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения();
	Для Каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла; 
	
	СформироватьТаблицуВтСписокДокументов(Запрос);
	ПолучитьДанныеДокумента(Запрос);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Период                 КАК Период,
	|	ДанныеДокумента.Номер                  КАК Номер,
	|	ДанныеДокумента.Организация            КАК Организация,
	|	ДанныеДокумента.Договор                КАК Договор,
	|	ДанныеДокумента.ТипДоговора            КАК ТипДоговора,
	|	ДанныеДокумента.ВалютаВзаиморасчетов   КАК ВалютаВзаиморасчетов
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
		
КонецПроцедуры

Процедура СформироватьТаблицуВтСписокДокументов(Запрос)
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СписокДокументов.Ссылка КАК Ссылка,
	|	СписокДокументов.Организация КАК Организация,
	|	СписокДокументов.Дата КАК Период
	|
	|ПОМЕСТИТЬ ВтСписокДокументов
	|
	|ИЗ
	|	Документ.ПередачаОСВАренду2_4 КАК СписокДокументов
	|
	|ГДЕ
	|	СписокДокументов.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|";
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПолучитьДанныеДокумента(Запрос)
	
	СписокЗапросов = Новый Массив;
	
	#Область ДанныеДокументаРеквизиты
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                  КАК Ссылка,
	|	ДанныеДокумента.Дата                    КАК Период,
	|	ДанныеДокумента.Номер                   КАК Номер,
	|	ДанныеДокумента.Организация             КАК Организация,
	|	ДанныеДокумента.Организация.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ДанныеДокумента.Арендатор               КАК Арендатор,
	|	ДанныеДокумента.Договор                 КАК Договор,
	|	ДанныеДокумента.МОЛ                     КАК МОЛ,
	|	ДанныеДокумента.МОЛАрендатора           КАК МОЛАрендатора,
	|	ДанныеДокумента.ПодразделениеАрендатора КАК ПодразделениеАрендатора,
	|	ДанныеДокумента.АдресМестонахождения    КАК АдресМестонахождения,
	|	ДанныеДокумента.АдресМестонахожденияЗначение КАК АдресМестонахожденияЗначение,
	|	ДанныеДокумента.Ответственный           КАК Ответственный,
	|	ДанныеДокумента.Комментарий             КАК Комментарий,
	|	ДанныеДокумента.Проведен                КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления         КАК ПометкаУдаления,
	|	ДанныеДокумента.ОтражатьВРеглУчете      КАК ОтражатьВБУ,
	|	ЛОЖЬ                                    КАК ОтражатьВНУ,
	|	ДанныеДокумента.ОтражатьВРеглУчете      КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете       КАК ОтражатьВУпрУчете,
	|	ДанныеДокумента.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|	ДанныеДокумента.СтатьяРасходовБУ        КАК СтатьяРасходовБУ,
	|	ДанныеДокумента.АналитикаРасходовБУ     КАК АналитикаРасходовБУ,
	|	ДанныеДокумента.СтатьяДоходов           КАК СтатьяДоходов,
	|	ДанныеДокумента.АналитикаДоходов        КАК АналитикаДоходов,
	|	ДанныеДокумента.СтатьяРасходовУУ        КАК СтатьяРасходовУУ,
	|	ДанныеДокумента.АналитикаРасходовУУ     КАК АналитикаРасходовУУ,
	|	ДанныеДокумента.НачислениеАмортизацииБУ КАК НачислениеАмортизацииБУ,
	|	ДанныеДокумента.НачислениеАмортизацииУУ КАК НачислениеАмортизацииУУ,
	|	ДанныеДокумента.Подразделение           КАК Подразделение,
	|	ДанныеДокумента.СобытиеОС               КАК СобытиеОС,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.РасчетыМеждуОрганизациямиАрендатор
	|			ТОГДА ДанныеДокумента.Арендатор
	|		ИНАЧЕ ДанныеДокумента.Организация
	|	КОНЕЦ КАК ОрганизацияПолучатель,
	|
	|	ДанныеДокумента.ПодразделениеПолучатель КАК ПодразделениеПолучатель,
	|	ДанныеДокумента.РасчетыМеждуОрганизациямиАрендатор КАК РасчетыМеждуОрганизациямиАрендатор,
	|	ДанныеДокумента.НачислениеАмортизацииУУ = ЗНАЧЕНИЕ(Перечисление.ВариантыИзмененияНачисленияАмортизации.Действует) КАК НачислятьАмортизациюУУ,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаОСВАренду) КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Договор.ТипДоговора     КАК ТипДоговора,
	|	ДанныеДокумента.Договор.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ДанныеДокумента.Организация.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Договор.ТипДоговора В (&ТипыДоговоровПоФСБУ25)
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК КоэффициентФСБУ25
	|
	|ПОМЕСТИТЬ ДанныеДокументаРеквизиты
	|
	|ИЗ
	|	Документ.ПередачаОСВАренду2_4 КАК ДанныеДокумента
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка В (
	|		ВЫБРАТЬ
	|			ВтСписокДокументов.Ссылка
	|		ИЗ
	|			ВтСписокДокументов КАК ВтСписокДокументов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ДанныеДокументаТаблицаОС
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаДокумента.ОсновноеСредство КАК ОбъектУчета,
	|	ТаблицаДокумента.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаДокумента.ЗалоговаяСтоимость КАК ЗалоговаяСтоимость,
	|	ТаблицаДокумента.СправедливаяСтоимостьРегл КАК СправедливаяСтоимостьРегл,
	|	ТаблицаДокумента.СправедливаяСтоимостьУпр КАК СправедливаяСтоимостьУпр,
	|	ТаблицаДокумента.АрендныеПлатежи КАК АрендныеПлатежи,
	|	ТаблицаДокумента.АвансовыеПлатежи КАК АвансовыеПлатежи,
	|	ТаблицаДокумента.НГЛСРегл КАК НГЛСРегл,
	|	ТаблицаДокумента.НГЛСУпр КАК НГЛСУпр
	|
	|ПОМЕСТИТЬ ДанныеДокументаТаблицаОС
	|
	|ИЗ
	|	Документ.ПередачаОСВАренду2_4.ОС КАК ТаблицаДокумента
	|
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В (
	|		ВЫБРАТЬ
	|			ВтСписокДокументов.Ссылка
	|		ИЗ
	|			ВтСписокДокументов КАК ВтСписокДокументов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	ОсновноеСредство
	|";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ДанныеДокументаТаблицаГрафикОплатУслуг
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка					КАК Ссылка,
	|	ТаблицаДокумента.Дата					КАК Дата,
	|	ТаблицаДокумента.ОсновноеСредство		КАК ОсновноеСредство,
	|	ТаблицаДокумента.УслугаПоАренде			КАК УслугаПоАренде,
	|	ТаблицаДокумента.УслугаПоАрендеНДС		КАК УслугаПоАрендеНДС
	|
	|ПОМЕСТИТЬ ДанныеДокументаТаблицаГрафикОплатУслуг
	|
	|ИЗ
	|	Документ.ПередачаОСВАренду2_4.ГрафикОплатУслуг КАК ТаблицаДокумента
	|
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В (
	|		ВЫБРАТЬ
	|			ВтСписокДокументов.Ссылка
	|		ИЗ
	|			ВтСписокДокументов КАК ВтСписокДокументов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	ОсновноеСредство
	|";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ДанныеДокументаТаблицаГрафикНачисленияУслуг
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка					КАК Ссылка,
	|	ТаблицаДокумента.Дата					КАК Дата,
	|	ТаблицаДокумента.ОсновноеСредство		КАК ОсновноеСредство,
	|	ТаблицаДокумента.УслугаПоАренде			КАК УслугаПоАренде,
	|	ТаблицаДокумента.УслугаПоАрендеНДС		КАК УслугаПоАрендеНДС
	|
	|ПОМЕСТИТЬ ДанныеДокументаТаблицаГрафикНачисленияУслуг
	|
	|ИЗ
	|	Документ.ПередачаОСВАренду2_4.ГрафикНачисленияУслуг КАК ТаблицаДокумента
	|
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В (
	|		ВЫБРАТЬ
	|			ВтСписокДокументов.Ссылка
	|		ИЗ
	|			ВтСписокДокументов КАК ВтСписокДокументов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	ОсновноеСредство
	|";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ДанныеДокументаТаблицаГрафикНачисленияПроцентов
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка					КАК Ссылка,
	|	ТаблицаДокумента.Дата					КАК Дата,
	|	ТаблицаДокумента.ОсновноеСредство		КАК ОсновноеСредство,
	|	ТаблицаДокумента.Проценты				КАК Проценты
	|
	|ПОМЕСТИТЬ ДанныеДокументаТаблицаГрафикНачисленияПроцентов
	|
	|ИЗ
	|	Документ.ПередачаОСВАренду2_4.ГрафикНачисленияПроцентов КАК ТаблицаДокумента
	|
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В (
	|		ВЫБРАТЬ
	|			ВтСписокДокументов.Ссылка
	|		ИЗ
	|			ВтСписокДокументов КАК ВтСписокДокументов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	ОсновноеСредство
	|";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	Запрос.Текст = СтрСоединить(СписокЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ЗначенияПараметровПроведения()

	ЗначенияПараметровПроведения = Новый Структура;
	ВнеоборотныеАктивы.ДополнитьЗначенияПараметровПроведенияПриСписанииОС(ЗначенияПараметровПроведения);
	
	ЗначенияПараметровПроведения.Вставить("СтатьяВыбытияОС", ПланыВидовХарактеристик.СтатьиРасходов.РасходыПриСписанииОС);
	
	ЗначенияПараметровПроведения.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ПередачаОСВАренду2_4"));
	ЗначенияПараметровПроведения.Вставить("НазваниеДокумента", НСтр("ru = 'Передача ОС в аренду';
																	|en = 'Provide assets to lessee'"));
	
	ЗначенияПараметровПроведения.Вставить("ИспользоватьУчетПрочихДоходовРасходов",ПолучитьФункциональнуюОпцию("ИспользоватьУчетПрочихДоходовРасходов"));
	ЗначенияПараметровПроведения.Вставить("ИспользоватьУчетПрочихАктивовПассивов",ПолучитьФункциональнуюОпцию("ИспользоватьУчетПрочихАктивовПассивов"));
	ЗначенияПараметровПроведения.Вставить("ИспользоватьУчетПрослеживаемыхИмпортныхТоваров",ПолучитьФункциональнуюОпцию("ИспользоватьУчетПрослеживаемыхИмпортныхТоваров"));
	ЗначенияПараметровПроведения.Вставить("ВедетсяРегламентированныйУчетВНА", ВнеоборотныеАктивыСлужебный.ВедетсяРегламентированныйУчетВНА());
	ЗначенияПараметровПроведения.Вставить("ИдентификаторНеиспользуемойФинЗаписи", ПроведениеДокументов.ИдентификаторНеиспользуемойФинЗаписи());
	ЗначенияПараметровПроведения.Вставить("СтатьяАП_ОС", ПланыВидовХарактеристик.СтатьиАктивовПассивов.ОсновныеСредства);

	ЗначенияПараметровПроведения.Вставить("ХО_ПереоценкаФИ", Перечисления.ХозяйственныеОперации.ПереоценкаФинансовыхИнструментов);
	ЗначенияПараметровПроведения.Вставить("НастройкаХО_ПереоценкаФИ", Справочники.НастройкиХозяйственныхОпераций.ПереоценкаФинансовыхИнструментов);
	
	ЗначенияПараметровПроведения.Вставить("ХО_ПередачаОСВАренду", Перечисления.ХозяйственныеОперации.ПередачаОСВАренду);
	ЗначенияПараметровПроведения.Вставить("НастройкаХО_ПередачаОСВАренду", Справочники.НастройкиХозяйственныхОпераций.ПередачаОСВАренду);
	
	ЗначенияПараметровПроведения.Вставить("ХО_ИзменениеПараметровСтоимостиОС", Перечисления.ХозяйственныеОперации.ИзменениеПараметровСтоимостиОС);
	ЗначенияПараметровПроведения.Вставить("НастройкаХО_ИзменениеПараметровСтоимостиОС", Справочники.НастройкиХозяйственныхОпераций.ИзменениеПараметровСтоимостиОС);
	
	ЗначенияПараметровПроведения.Вставить("ХО_ИзменениеПараметровАмортизацииОС", Перечисления.ХозяйственныеОперации.ИзменениеПараметровАмортизацииОС);
	ЗначенияПараметровПроведения.Вставить("НастройкаХО_ИзменениеПараметровАмортизацииОС", Справочники.НастройкиХозяйственныхОпераций.ИзменениеПараметровАмортизацииОС);
	
	ЗначенияПараметровПроведения.Вставить("НастройкаХО_ВыделениеНГЛС", Справочники.НастройкиХозяйственныхОпераций.ВыделениеНГЛС);
	ЗначенияПараметровПроведения.Вставить("НастройкаХО_НачислениеПроцентовПоДоходнойАренде", Справочники.НастройкиХозяйственныхОпераций.НачислениеПроцентовПоДоходнойАренде);
	
	ЗначенияПараметровПроведения.Вставить("ВалютаУпр", ЗначениеНастроекПовтИсп.ВалютаУправленческогоУчета());
	
	ЗначенияПараметровПроведения.Вставить("ТипыДоговоровПоФСБУ25", УчетАрендованныхОСКлиентСервер.ТипыДоговоровДоходнойАренды());
	ЗначенияПараметровПроведения.Вставить("ПередачаВАрендуПоФСБУ25", ПолучитьФункциональнуюОпцию("ИспользуетсяПередачаВАрендуСубарендуПоФСБУ25"));

	Возврат ЗначенияПараметровПроведения;
	
КонецФункции

#Область ТекстыЗапросаДляПроведения

Функция ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДокументыПоОС";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(ТаблицаОС.НомерСтроки-1, 0) КАК НомерЗаписи,
	|	ДанныеДокумента.Дата             КАК Дата,
	|	ДанныеДокумента.Ссылка           КАК Ссылка,
	|	ДанныеДокумента.Организация      КАК Организация,
	|	ДанныеДокумента.Подразделение    КАК Подразделение,
	|	ДанныеДокумента.Проведен         КАК Проведен,
	|	&ХО_ПередачаОСВАренду            КАК ХозяйственнаяОперация,
	|	&ИдентификаторМетаданных         КАК ТипСсылки,
	|	ДанныеДокумента.СобытиеОС        КАК СобытиеОС,
	|	ТаблицаОС.ОсновноеСредство       КАК ОсновноеСредство,
	|	ДанныеДокумента.ОтражатьВРеглУчете КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете КАК ОтражатьВУпрУчете,
	|	ЛОЖЬ                             КАК ДополнительнаяЗапись
	|
	|ИЗ
	|	Документ.ПередачаОСВАренду2_4 КАК ДанныеДокумента
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПередачаОСВАренду2_4.ОС КАК ТаблицаОС
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ТаблицаОС.НомерСтроки-1, 0) КАК НомерЗаписи,
	|	ДанныеДокумента.Дата             КАК Дата,
	|	ДанныеДокумента.Ссылка           КАК Ссылка,
	|	ДанныеДокумента.Арендатор        КАК Организация,
	|	ДанныеДокумента.ПодразделениеПолучатель КАК Подразделение,
	|	ДанныеДокумента.Проведен         КАК Проведен,
	|	&ХО_ПередачаОСВАренду            КАК ХозяйственнаяОперация,
	|	&ИдентификаторМетаданных         КАК ТипСсылки,
	|	ДанныеДокумента.СобытиеОС        КАК СобытиеОС,
	|	ТаблицаОС.ОсновноеСредство       КАК ОсновноеСредство,
	|	ДанныеДокумента.ОтражатьВРеглУчете КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете КАК ОтражатьВУпрУчете,
	|	ИСТИНА                           КАК ДополнительнаяЗапись
	|
	|ИЗ
	|	Документ.ПередачаОСВАренду2_4 КАК ДанныеДокумента
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПередачаОСВАренду2_4.ОС КАК ТаблицаОС
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И ДанныеДокумента.Организация <> ДанныеДокумента.Арендатор
	|	И ДанныеДокумента.РасчетыМеждуОрганизациямиАрендатор";	
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Дата КАК ДатаДокументаИБ,
	|	ДанныеДокумента.Номер КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных КАК ТипСсылки,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Арендатор КАК Контрагент,
	|	ДанныеДокумента.Договор КАК Договор,
	|	&ХО_ПередачаОСВАренду КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ДанныеДокумента.Ответственный КАК Ответственный,
	|	ДанныеДокумента.Комментарий КАК Комментарий,
	|	ДанныеДокумента.Проведен КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления КАК ПометкаУдаления,
	|	ЛОЖЬ КАК ДополнительнаяЗапись,
	|	ДанныеДокумента.Дата КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать КАК НомерПервичногоДокумента,
	|	ЛОЖЬ КАК СторноИсправление,
	|	НЕОПРЕДЕЛЕНО КАК СторнируемыйДокумент,
	|	НЕОПРЕДЕЛЕНО КАК ИсправляемыйДокумент,
	|	ДанныеДокумента.Дата   КАК ДатаОтраженияВУчете,
	|	НЕОПРЕДЕЛЕНО КАК Приоритет
	|
	|ИЗ
	|	Документ.ПередачаОСВАренду2_4 КАК ДанныеДокумента
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка,
	|	ДанныеДокумента.Дата,
	|	ДанныеДокумента.Номер,
	|	&ИдентификаторМетаданных,
	|	ДанныеДокумента.Арендатор,
	|	ДанныеДокумента.Арендатор,
	|	ДанныеДокумента.Договор,
	|	&ХО_ПередачаОСВАренду,
	|	НЕОПРЕДЕЛЕНО,
	|	ДанныеДокумента.ПодразделениеПолучатель,
	|	ДанныеДокумента.Ответственный,
	|	ДанныеДокумента.Комментарий,
	|	ДанныеДокумента.Проведен,
	|	ДанныеДокумента.ПометкаУдаления,
	|	ИСТИНА,
	|	ДанныеДокумента.Дата,
	|	&НомерНаПечать,
	|	ЛОЖЬ КАК СторноИсправление,
	|	НЕОПРЕДЕЛЕНО КАК СторнируемыйДокумент,
	|	НЕОПРЕДЕЛЕНО КАК ИсправляемыйДокумент,
	|	ДанныеДокумента.Дата,
	|	НЕОПРЕДЕЛЕНО КАК Приоритет
	|
	|ИЗ
	|	Документ.ПередачаОСВАренду2_4 КАК ДанныеДокумента
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И ДанныеДокумента.РасчетыМеждуОрганизациямиАрендатор
	|	И ДанныеДокумента.Арендатор <> ДанныеДокумента.Организация
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ТекстЗапросаТаблицаМестонахождениеОС(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "МестонахождениеОС";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтРасширеннаяТаблицаОС(ТекстыЗапроса);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтМестонахождениеОС(ТекстыЗапроса, "ВтРасширеннаяТаблицаОС");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Период                          КАК Период,
	|	ДанныеДокумента.Организация                     КАК Организация,
	|	втМестонахождениеОС.ОсновноеСредство            КАК ОсновноеСредство,
	|	ДанныеДокумента.МОЛ                             КАК МОЛ,
	|	ДанныеДокумента.ПодразделениеПолучатель         КАК Местонахождение,
	|	ДанныеДокумента.Арендатор                       КАК Арендатор,
	|	ДанныеДокумента.ПодразделениеАрендатора         КАК ПодразделениеАрендатора,
	|	ДанныеДокумента.МОЛАрендатора                   КАК МОЛАрендатора,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.РасчетыМеждуОрганизациямиАрендатор И ДанныеДокумента.АдресМестонахождения <> """"	
	|			ТОГДА ДанныеДокумента.АдресМестонахождения
	|		ИНАЧЕ втМестонахождениеОС.АдресМестонахождения
	|	КОНЕЦ КАК АдресМестонахождения,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.РасчетыМеждуОрганизациямиАрендатор И ДанныеДокумента.АдресМестонахождения <> """"
	|			ТОГДА ДанныеДокумента.АдресМестонахожденияЗначение
	|		ИНАЧЕ втМестонахождениеОС.АдресМестонахожденияЗначение
	|	КОНЕЦ КАК АдресМестонахожденияЗначение
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втМестонахождениеОС КАК втМестонахождениеОС
	|		ПО втМестонахождениеОС.Ссылка = ДанныеДокумента.Ссылка
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПорядокУчетаОС(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПорядокУчетаОС";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтРасширеннаяТаблицаОС(ТекстыЗапроса);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПорядокУчетаОС(ТекстыЗапроса, "ВтРасширеннаяТаблицаОС");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                  КАК Регистратор,
	|	ДанныеДокумента.Период                  КАК Период,
	|	ДанныеДокумента.Организация             КАК Организация,
	|	ПорядокУчетаОС.ОсновноеСредство         КАК ОсновноеСредство,
	|	ДанныеДокумента.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|	ПорядокУчетаОС.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	ПорядокУчетаОС.ПоказательНаработки      КАК ПоказательНаработки,
	|	ПорядокУчетаОС.ОбъемНаработки           КАК ОбъемНаработки,
	|	ДанныеДокумента.ОтражатьВРеглУчете      КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете       КАК ОтражатьВУпрУчете
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаОС КАК ПорядокУчетаОС
	|		ПО ДанныеДокумента.Ссылка = ПорядокУчетаОС.Ссылка
	|ГДЕ
	|	ПорядокУчетаОС.ОсновноеСредство.ТипОС = ЗНАЧЕНИЕ(Перечисление.ТипыОС.ОбъектЭксплуатации)
	|	ИЛИ НЕ (&ПередачаВАрендуПоФСБУ25 И ДанныеДокумента.ТипДоговора В (&ТипыДоговоровПоФСБУ25))
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПорядокУчетаОСБУ(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПорядокУчетаОСБУ";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтРасширеннаяТаблицаОС(ТекстыЗапроса);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПорядокУчетаОСБУ(ТекстыЗапроса, "ВтРасширеннаяТаблицаОС");
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПервоначальныеСведенияОС(ТекстыЗапроса, "ВтРасширеннаяТаблицаОС");
	
	ТекстЗапроса = ПередачаОСВАрендуЛокализация.ТекстЗапросаТаблицаПорядокУчетаОСБУ();
	
	Если ТекстЗапроса = Неопределено Тогда
		
		ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтУчетнаяПолитикаФинансовогоУчета(ТекстыЗапроса);

		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка                  КАК Регистратор,
		|	ДанныеДокумента.Период                  КАК Период,
		|	ДанныеДокумента.Организация             КАК Организация,
		|	втПорядокУчетаОСБУ.ОсновноеСредство     КАК ОсновноеСредство,
		|
		|	ВЫБОР КОГДА РасширеннаяТаблицаОС.ОсновноеСредство.ТипОС В (
		|			ЗНАЧЕНИЕ(Перечисление.ТипыОС.Узел), ЗНАЧЕНИЕ(Перечисление.ТипыОС.КомпонентАмортизации))
		|			И &ПередачаВАрендуПоФСБУ25 И ДанныеДокумента.ТипДоговора В (&ТипыДоговоровПоФСБУ25)
		|		ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияОС.СнятоСУчета)
		|		ИНАЧЕ втПорядокУчетаОСБУ.СостояниеБУ
		|	КОНЕЦ КАК СостояниеБУ,
		|
		// НачислятьАмортизациюБУ
		|	ВЫБОР 
		|		КОГДА РасширеннаяТаблицаОС.ОсновноеСредство.ТипОС В (
		|			ЗНАЧЕНИЕ(Перечисление.ТипыОС.Узел), ЗНАЧЕНИЕ(Перечисление.ТипыОС.КомпонентАмортизации))
		|			И &ПередачаВАрендуПоФСБУ25 И ДанныеДокумента.ТипДоговора В (&ТипыДоговоровПоФСБУ25)
		|			ТОГДА ЛОЖЬ
		|		КОГДА ДанныеДокумента.НачислениеАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.ВариантыИзмененияНачисленияАмортизации.Действует)
		|			И (втПорядокУчетаОСБУ.НачислятьАмортизациюБУ
		|				ИЛИ втПервоначальныеСведенияОС.ПорядокУчетаБУ В (
		|					ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеАмортизации),
		|					ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.АмортизацияНачислена)))
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК НачислятьАмортизациюБУ,
		|
		|	ВЫБОР
		|		КОГДА &ПередачаВАрендуПоФСБУ25 И ДанныеДокумента.ТипДоговора В (&ТипыДоговоровПоФСБУ25)
		|			ТОГДА втПорядокУчетаОСБУ.СтатьяРасходовБУ
		|			ИНАЧЕ ДанныеДокумента.СтатьяРасходовБУ
		|	КОНЕЦ КАК СтатьяРасходовБУ,
		|	ВЫБОР
		|		КОГДА &ПередачаВАрендуПоФСБУ25 И ДанныеДокумента.ТипДоговора В (&ТипыДоговоровПоФСБУ25)
		|			ТОГДА втПорядокУчетаОСБУ.АналитикаРасходовБУ
		|			ИНАЧЕ ДанныеДокумента.АналитикаРасходовБУ
		|	КОНЕЦ КАК АналитикаРасходовБУ
		|
		|ИЗ
		|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасширеннаяТаблицаОС КАК РасширеннаяТаблицаОС
		|		ПО РасширеннаяТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаОСБУ КАК втПорядокУчетаОСБУ
		|		ПО втПорядокУчетаОСБУ.Ссылка = РасширеннаяТаблицаОС.Ссылка
		|			И втПорядокУчетаОСБУ.ОсновноеСредство = РасширеннаяТаблицаОС.ОсновноеСредство
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПервоначальныеСведенияОС КАК втПервоначальныеСведенияОС
		|		ПО втПервоначальныеСведенияОС.Ссылка = РасширеннаяТаблицаОС.Ссылка
		|			И втПервоначальныеСведенияОС.ОсновноеСредство = РасширеннаяТаблицаОС.ОсновноеСредство
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтУчетнаяПолитикаФинансовогоУчета КАК ВтУчетнаяПолитикаФинансовогоУчета
		|		ПО ВтУчетнаяПолитикаФинансовогоУчета.Ссылка = ДанныеДокумента.Ссылка
		|
		|ГДЕ
		|	ДанныеДокумента.ОтражатьВРеглУчете
		|	И ЕСТЬNULL(ВтУчетнаяПолитикаФинансовогоУчета.ПорядокУчетаВНАРегл, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАРегл.Независимый)
		|	И ДанныеДокумента.НачислениеАмортизацииБУ В (
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыИзмененияНачисленияАмортизации.Действует),
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыИзмененияНачисленияАмортизации.Остановлено))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка                  КАК Регистратор,
		|	ДанныеДокумента.Период                  КАК Период,
		|	ДанныеДокумента.Организация             КАК Организация,
		|	втПорядокУчетаОСБУ.ОсновноеСредство     КАК ОсновноеСредство,
		|
		|	ВЫБОР КОГДА РасширеннаяТаблицаОС.ОсновноеСредство.ТипОС В (
		|			ЗНАЧЕНИЕ(Перечисление.ТипыОС.Узел), ЗНАЧЕНИЕ(Перечисление.ТипыОС.КомпонентАмортизации))
		|			И &ПередачаВАрендуПоФСБУ25 И ДанныеДокумента.ТипДоговора В (&ТипыДоговоровПоФСБУ25)
		|		ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияОС.СнятоСУчета)
		|		ИНАЧЕ втПорядокУчетаОСБУ.СостояниеБУ
		|	КОНЕЦ КАК СостояниеБУ,
		|
		// НачислятьАмортизациюБУ
		|	ВЫБОР 
		|		КОГДА РасширеннаяТаблицаОС.ОсновноеСредство.ТипОС В (
		|			ЗНАЧЕНИЕ(Перечисление.ТипыОС.Узел), ЗНАЧЕНИЕ(Перечисление.ТипыОС.КомпонентАмортизации))
		|			И &ПередачаВАрендуПоФСБУ25 И ДанныеДокумента.ТипДоговора В (&ТипыДоговоровПоФСБУ25)
		|			ТОГДА ЛОЖЬ
		|		КОГДА ДанныеДокумента.НачислениеАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.ВариантыИзмененияНачисленияАмортизации.Действует)
		|			И (втПорядокУчетаОСБУ.НачислятьАмортизациюБУ
		|				ИЛИ втПервоначальныеСведенияОС.ПорядокУчетаБУ В (
		|					ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеАмортизации),
		|					ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.АмортизацияНачислена)))
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК НачислятьАмортизациюБУ,
		|
		|	ДанныеДокумента.СтатьяРасходовУУ         КАК СтатьяРасходовБУ,
		|	ДанныеДокумента.АналитикаРасходовУУ      КАК АналитикаРасходовБУ
		|
		|ИЗ
		|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасширеннаяТаблицаОС КАК РасширеннаяТаблицаОС
		|		ПО РасширеннаяТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаОСБУ КАК втПорядокУчетаОСБУ
		|		ПО втПорядокУчетаОСБУ.Ссылка = РасширеннаяТаблицаОС.Ссылка
		|			И втПорядокУчетаОСБУ.ОсновноеСредство = РасширеннаяТаблицаОС.ОсновноеСредство
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПервоначальныеСведенияОС КАК втПервоначальныеСведенияОС
		|		ПО втПервоначальныеСведенияОС.ОсновноеСредство = РасширеннаяТаблицаОС.Ссылка
		|			И втПервоначальныеСведенияОС.ОсновноеСредство = РасширеннаяТаблицаОС.ОсновноеСредство
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтУчетнаяПолитикаФинансовогоУчета КАК ВтУчетнаяПолитикаФинансовогоУчета
		|		ПО ВтУчетнаяПолитикаФинансовогоУчета.Ссылка = ДанныеДокумента.Ссылка
		|
		|ГДЕ
		|	ЕСТЬNULL(ВтУчетнаяПолитикаФинансовогоУчета.ПорядокУчетаВНАРегл, НЕОПРЕДЕЛЕНО) <> ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАРегл.Независимый)
		|	И ДанныеДокумента.НачислениеАмортизацииУУ В (
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыИзмененияНачисленияАмортизации.Действует),
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыИзмененияНачисленияАмортизации.Остановлено))
		|";

	КонецЕсли;
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПорядокУчетаОСУУ(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПорядокУчетаОСУУ";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтРасширеннаяТаблицаОС(ТекстыЗапроса);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПорядокУчетаОСУУ(ТекстыЗапроса, "ВтРасширеннаяТаблицаОС");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка               КАК Регистратор,
	|	ДанныеДокумента.Период               КАК Период,
	|	ДанныеДокумента.Организация          КАК Организация,
	|	РасширеннаяТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ВЫБОР КОГДА РасширеннаяТаблицаОС.ОсновноеСредство.ТипОС = ЗНАЧЕНИЕ(Перечисление.ТипыОС.ОбъектЭксплуатации)
	|	ИЛИ НЕ (&ПередачаВАрендуПоФСБУ25 И ДанныеДокумента.ТипДоговора В (&ТипыДоговоровПоФСБУ25))
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостоянияОС.СнятоСУчета)
	|	КОНЕЦ КАК Состояние,
	|	ВЫБОР КОГДА РасширеннаяТаблицаОС.ОсновноеСредство.ТипОС = ЗНАЧЕНИЕ(Перечисление.ТипыОС.ОбъектЭксплуатации)
	|	ИЛИ НЕ (&ПередачаВАрендуПоФСБУ25 И ДанныеДокумента.ТипДоговора В (&ТипыДоговоровПоФСБУ25))
	|		ТОГДА ДанныеДокумента.НачислятьАмортизациюУУ
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НачислятьАмортизациюУУ,
	|	ВЫБОР
	|		КОГДА &ПередачаВАрендуПоФСБУ25 И ДанныеДокумента.ТипДоговора В (&ТипыДоговоровПоФСБУ25)
	|			ТОГДА втПорядокУчетаОСУУ.СтатьяРасходов
	|			ИНАЧЕ ДанныеДокумента.СтатьяРасходовУУ
	|	КОНЕЦ КАК СтатьяРасходов,
	|	ВЫБОР
	|		КОГДА &ПередачаВАрендуПоФСБУ25 И ДанныеДокумента.ТипДоговора В (&ТипыДоговоровПоФСБУ25)
	|			ТОГДА втПорядокУчетаОСУУ.АналитикаРасходов
	|			ИНАЧЕ ДанныеДокумента.АналитикаРасходовУУ
	|	КОНЕЦ КАК АналитикаРасходов
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасширеннаяТаблицаОС КАК РасширеннаяТаблицаОС
	|		ПО РасширеннаяТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаОСУУ КАК втПорядокУчетаОСУУ
	|		ПО втПорядокУчетаОСУУ.Ссылка = РасширеннаяТаблицаОС.Ссылка
	|			И втПорядокУчетаОСУУ.ОсновноеСредство = РасширеннаяТаблицаОС.ОсновноеСредство
	|
	|ГДЕ
	|	ДанныеДокумента.ОтражатьВУпрУчете
	|	И ДанныеДокумента.НачислениеАмортизацииУУ В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыИзмененияНачисленияАмортизации.Действует),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыИзмененияНачисленияАмортизации.Остановлено))
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПереданныеВАрендуОС(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПереданныеВАрендуОС";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтРасширеннаяТаблицаОС(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка          КАК Регистратор,
	|	ДанныеДокумента.Период          КАК Период,
	|	ДанныеДокумента.Период          КАК ДатаПередачи,
	|	ДанныеДокумента.Организация     КАК Организация,
	|	ДанныеДокумента.Арендатор       КАК Контрагент,
	|	ДанныеДокумента.Договор         КАК Договор,
	|	ЕСТЬNULL(ТаблицаОС.ЗалоговаяСтоимость, 0) КАК ЗалоговаяСтоимость,
	|	РасширеннаяТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПереданоВАренду) КАК Состояние
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасширеннаяТаблицаОС КАК РасширеннаяТаблицаОС
	|		ПО РасширеннаяТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = РасширеннаяТаблицаОС.Ссылка
	|			И ТаблицаОС.ОсновноеСредство = РасширеннаяТаблицаОС.ОсновноеСредство
	|ГДЕ
	|	РасширеннаяТаблицаОС.ОсновноеСредство.ТипОС = ЗНАЧЕНИЕ(Перечисление.ТипыОС.ОбъектЭксплуатации)
	|	ИЛИ НЕ (&ПередачаВАрендуПоФСБУ25 И ДанныеДокумента.ТипДоговора В (&ТипыДоговоровПоФСБУ25))
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаСтоимостьОС(Запрос, ТекстыЗапроса, Регистры = Неопределено)

	ИмяРегистра = "СтоимостьОС";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	РасчетСтоимостиВНА.ТекстЗапросаПустаяВтРасчетСтоимости(Запрос, ТекстыЗапроса, "ВТ_СтоимостьВНА");
	ТекстЗапросаТаблицаВтТаблицаОС(ТекстыЗапроса);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыУчетаОС(ТекстыЗапроса, "ВтРасширеннаяТаблицаОС");
	
	СписокЗапросовОбъединение = Новый Массив;
	
	#Область СписаниеСтоимости
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	СписаниеСтоимостиОС.ВидДвижения                    КАК ВидДвижения,
	|	СписаниеСтоимостиОС.Регистратор                    КАК Регистратор,
	|	СписаниеСтоимостиОС.Период                         КАК Период,
	|	СписаниеСтоимостиОС.Организация                    КАК Организация,
	|	СписаниеСтоимостиОС.Подразделение                  КАК Подразделение,
	|	СписаниеСтоимостиОС.ОсновноеСредство               КАК ОсновноеСредство,
	|	СписаниеСтоимостиОС.Стоимость                      КАК Стоимость,
	|	СписаниеСтоимостиОС.СтоимостьРегл                  КАК СтоимостьРегл,
	|	СписаниеСтоимостиОС.СтоимостьНУ                    КАК СтоимостьНУ,
	|	СписаниеСтоимостиОС.СтоимостьПР                    КАК СтоимостьПР,
	|	СписаниеСтоимостиОС.СтоимостьВР                    КАК СтоимостьВР,
	|	СписаниеСтоимостиОС.СтоимостьЦФ                    КАК СтоимостьЦФ,
	|	СписаниеСтоимостиОС.СтоимостьНУЦФ                  КАК СтоимостьНУЦФ,
	|	СписаниеСтоимостиОС.СтоимостьПРЦФ                  КАК СтоимостьПРЦФ,
	|	СписаниеСтоимостиОС.СтоимостьВРЦФ                  КАК СтоимостьВРЦФ,
	|	СписаниеСтоимостиОС.ПредварительнаяСтоимость       КАК ПредварительнаяСтоимость,
	|	СписаниеСтоимостиОС.ПредварительнаяСтоимостьРегл   КАК ПредварительнаяСтоимостьРегл,
	|	СписаниеСтоимостиОС.АмортизационнаяПремия          КАК АмортизационнаяПремия,
	|	СписаниеСтоимостиОС.РезервПереоценкиСтоимости      КАК РезервПереоценкиСтоимости,
	|	СписаниеСтоимостиОС.РезервПереоценкиСтоимостиРегл  КАК РезервПереоценкиСтоимостиРегл,
	|	СписаниеСтоимостиОС.ПриростСтоимости               КАК ПриростСтоимости,
	|	СписаниеСтоимостиОС.ПриростСтоимостиРегл           КАК ПриростСтоимостиРегл,
	|	СписаниеСтоимостиОС.НеУчитываемаяСтоимостьНУ       КАК НеУчитываемаяСтоимостьНУ,
	|	СписаниеСтоимостиОС.ЗалоговаяСтоимость             КАК ЗалоговаяСтоимость,
	|	СписаниеСтоимостиОС.ГруппаФинансовогоУчета         КАК ГруппаФинансовогоУчета,
	|	СписаниеСтоимостиОС.НаправлениеДеятельности        КАК НаправлениеДеятельности,
	|	СписаниеСтоимостиОС.Арендатор                      КАК Арендатор,
	|	СписаниеСтоимостиОС.КорКонтрагент                  КАК КорКонтрагент,
	|	СписаниеСтоимостиОС.ХозяйственнаяОперация          КАК ХозяйственнаяОперация,
	|	СписаниеСтоимостиОС.ОтражатьВРеглУчете             КАК ОтражатьВРеглУчете,
	|	СписаниеСтоимостиОС.ОтражатьВУпрУчете              КАК ОтражатьВУпрУчете,
	|	СписаниеСтоимостиОС.ИдентификаторФинЗаписи         КАК ИдентификаторФинЗаписи,
	|	СписаниеСтоимостиОС.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации 
	|ИЗ &ТекстПодзапроса КАК СписаниеСтоимостиОС
	|
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|	ПО СписаниеСтоимостиОС.Регистратор = ДанныеДокумента.Ссылка
	|		И &ПередачаВАрендуПоФСБУ25
	|		И ДанныеДокумента.ТипДоговора В (&ТипыДоговоровПоФСБУ25)";
	
	ТекстПодзапроса = ВнеоборотныеАктивыСлужебный.ТекстЗапросаСписанияСтоимостиОС(ТекстыЗапроса);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстПодзапроса", "(" + ТекстПодзапроса + ")");
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	
	#КонецОбласти

	#Область ИзменениеГФУИАрендатора
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)             КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                             КАК Регистратор,
	|	ДанныеДокумента.Период                             КАК Период,
	|	ДанныеДокумента.Организация                        КАК Организация,
	|	ДанныеДокумента.Подразделение                      КАК Подразделение,
	|	РасширеннаяТаблицаОС.ОсновноеСредство              КАК ОсновноеСредство,
	|
	|	СтоимостьОС.Стоимость*ДанныеДокумента.КоэффициентФСБУ25     КАК Стоимость,
	|	СтоимостьОС.СтоимостьРегл*ДанныеДокумента.КоэффициентФСБУ25 КАК СтоимостьРегл,
	|	СтоимостьОС.СтоимостьНУ                            КАК СтоимостьНУ,
	|	СтоимостьОС.СтоимостьПР                            КАК СтоимостьПР,
	|	СтоимостьОС.СтоимостьВР
	|		- СтоимостьОС.СтоимостьРегл*(1 - ДанныеДокумента.КоэффициентФСБУ25) КАК СтоимостьВР,
	|	СтоимостьОС.СтоимостьЦФ                            КАК СтоимостьЦФ,
	|	СтоимостьОС.СтоимостьНУЦФ                          КАК СтоимостьНУЦФ,
	|	СтоимостьОС.СтоимостьПРЦФ                          КАК СтоимостьПРЦФ,
	|	СтоимостьОС.СтоимостьВРЦФ                          КАК СтоимостьВРЦФ,
	|
	|	СтоимостьОС.ПредварительнаяСтоимость               КАК ПредварительнаяСтоимость,
	|	СтоимостьОС.ПредварительнаяСтоимостьРегл           КАК ПредварительнаяСтоимостьРегл,
	|
	|	СтоимостьОС.АмортизационнаяПремия                  КАК АмортизационнаяПремия,
	|	СтоимостьОС.РезервПереоценкиСтоимости              КАК РезервПереоценкиСтоимости,
	|	СтоимостьОС.РезервПереоценкиСтоимостиРегл          КАК РезервПереоценкиСтоимостиРегл,
	|	0                                                  КАК ПриростСтоимости,
	|	0                                                  КАК ПриростСтоимостиРегл,
	|	0                                                  КАК НеУчитываемаяСтоимостьНУ,
	|	0                                                  КАК ЗалоговаяСтоимость,
	|
	|	ПараметрыУчетаОС.ГруппаФинансовогоУчета            КАК ГруппаФинансовогоУчета,
	|	ПараметрыУчетаОС.НаправлениеДеятельности           КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                       КАК Арендатор,
	|	НЕОПРЕДЕЛЕНО                                       КАК КорКонтрагент,
	|
	|	&ХО_ИзменениеПараметровСтоимостиОС                 КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.ОтражатьВРеглУчете                 КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете                  КАК ОтражатьВУпрУчете,
	|	
	|	РасширеннаяТаблицаОС.ИдентификаторСтроки           КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_ИзменениеПараметровСтоимостиОС        КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасширеннаяТаблицаОС КАК РасширеннаяТаблицаОС
	|		ПО РасширеннаяТаблицаОС.Ссылка = ДанныеДокумента.Ссылка 
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПараметрыУчетаОС КАК ПараметрыУчетаОС
	|		ПО ПараметрыУчетаОС.Ссылка = РасширеннаяТаблицаОС.Ссылка
	|			И ПараметрыУчетаОС.ОсновноеСредство = РасширеннаяТаблицаОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьОС
	|		ПО СтоимостьОС.Ссылка = РасширеннаяТаблицаОС.Ссылка
	|			И СтоимостьОС.ОбъектУчета = РасширеннаяТаблицаОС.ОсновноеСредство
	|ГДЕ
	|	РасширеннаяТаблицаОС.ОсновноеСредство.ТипОС = ЗНАЧЕНИЕ(Перечисление.ТипыОС.ОбъектЭксплуатации)
	|	ИЛИ НЕ (&ПередачаВАрендуПоФСБУ25 И ДанныеДокумента.ТипДоговора В (&ТипыДоговоровПоФСБУ25))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)             КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                             КАК Регистратор,
	|	ДанныеДокумента.Период                             КАК Период,
	|	ДанныеДокумента.Организация                        КАК Организация,
	|	ДанныеДокумента.ПодразделениеПолучатель            КАК Подразделение,
	|	РасширеннаяТаблицаОС.ОсновноеСредство              КАК ОсновноеСредство,
	|
	|	СтоимостьОС.Стоимость*ДанныеДокумента.КоэффициентФСБУ25     КАК Стоимость,
	|	СтоимостьОС.СтоимостьРегл*ДанныеДокумента.КоэффициентФСБУ25 КАК СтоимостьРегл,
	|	СтоимостьОС.СтоимостьНУ                            КАК СтоимостьНУ,
	|	СтоимостьОС.СтоимостьПР                            КАК СтоимостьПР,
	|	СтоимостьОС.СтоимостьВР 
	|		- СтоимостьОС.СтоимостьРегл*(1 - ДанныеДокумента.КоэффициентФСБУ25) КАК СтоимостьВР,
	|	СтоимостьОС.СтоимостьЦФ                            КАК СтоимостьЦФ,
	|	СтоимостьОС.СтоимостьНУЦФ                          КАК СтоимостьНУЦФ,
	|	СтоимостьОС.СтоимостьПРЦФ                          КАК СтоимостьПРЦФ,
	|	СтоимостьОС.СтоимостьВРЦФ                          КАК СтоимостьВРЦФ,
	|
	|	СтоимостьОС.ПредварительнаяСтоимость               КАК ПредварительнаяСтоимость,
	|	СтоимостьОС.ПредварительнаяСтоимостьРегл           КАК ПредварительнаяСтоимостьРегл,
	|
	|	СтоимостьОС.АмортизационнаяПремия                  КАК АмортизационнаяПремия,
	|	СтоимостьОС.РезервПереоценкиСтоимости              КАК РезервПереоценкиСтоимости,
	|	СтоимостьОС.РезервПереоценкиСтоимостиРегл          КАК РезервПереоценкиСтоимостиРегл,
	|	0                                                  КАК ПриростСтоимости,
	|	0                                                  КАК ПриростСтоимостиРегл,
	|	0                                                  КАК НеУчитываемаяСтоимостьНУ,
	|	0                                                  КАК ЗалоговаяСтоимость,
	|
	|	ДанныеДокумента.ГруппаФинансовогоУчета             КАК ГруппаФинансовогоУчета,
	|	ПараметрыУчетаОС.НаправлениеДеятельности           КАК НаправлениеДеятельности,
	|	ДанныеДокумента.Арендатор                          КАК Арендатор,
	|	НЕОПРЕДЕЛЕНО                                       КАК КорКонтрагент,
	|
	|	&ХО_ИзменениеПараметровСтоимостиОС                 КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.ОтражатьВРеглУчете                 КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете                  КАК ОтражатьВУпрУчете,
	|	
	|	РасширеннаяТаблицаОС.ИдентификаторСтроки           КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_ИзменениеПараметровСтоимостиОС        КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасширеннаяТаблицаОС КАК РасширеннаяТаблицаОС
	|		ПО РасширеннаяТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПараметрыУчетаОС КАК ПараметрыУчетаОС
	|		ПО ПараметрыУчетаОС.Ссылка = РасширеннаяТаблицаОС.Ссылка
	|			И ПараметрыУчетаОС.ОсновноеСредство = РасширеннаяТаблицаОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьОС
	|		ПО СтоимостьОС.Ссылка = РасширеннаяТаблицаОС.Ссылка
	|			И СтоимостьОС.ОбъектУчета = РасширеннаяТаблицаОС.ОсновноеСредство
	|ГДЕ
	|	РасширеннаяТаблицаОС.ОсновноеСредство.ТипОС = ЗНАЧЕНИЕ(Перечисление.ТипыОС.ОбъектЭксплуатации)
	|	ИЛИ НЕ (&ПередачаВАрендуПоФСБУ25 И ДанныеДокумента.ТипДоговора В (&ТипыДоговоровПоФСБУ25))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)             КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                             КАК Регистратор,
	|	ДанныеДокумента.Период                             КАК Период,
	|	ДанныеДокумента.Организация                        КАК Организация,
	|	ДанныеДокумента.ПодразделениеПолучатель            КАК Подразделение,
	|	РасширеннаяТаблицаОС.ОсновноеСредство              КАК ОсновноеСредство,
	|
	|	0                                                  КАК Стоимость,
	|	0                                                  КАК СтоимостьРегл,
	|	0                                                  КАК СтоимостьНУ,
	|	0                                                  КАК СтоимостьПР,
	|	0                                                  КАК СтоимостьВР,
	|	0                                                  КАК СтоимостьЦФ,
	|	0                                                  КАК СтоимостьНУЦФ,
	|	0                                                  КАК СтоимостьПРЦФ,
	|	0                                                  КАК СтоимостьВРЦФ,
	|
	|	0                                                  КАК ПредварительнаяСтоимость,
	|	0                                                  КАК ПредварительнаяСтоимостьРегл,
	|
	|	0                                                  КАК АмортизационнаяПремия,
	|	0                                                  КАК РезервПереоценкиСтоимости,
	|	0                                                  КАК РезервПереоценкиСтоимостиРегл,
	|	0                                                  КАК ПриростСтоимости,
	|	0                                                  КАК ПриростСтоимостиРегл,
	|	0                                                  КАК НеУчитываемаяСтоимостьНУ,
	|	ЕСТЬNULL(ТаблицаОС.ЗалоговаяСтоимость, 0)          КАК ЗалоговаяСтоимость,
	|
	|	ДанныеДокумента.ГруппаФинансовогоУчета             КАК ГруппаФинансовогоУчета,
	|	ПараметрыУчетаОС.НаправлениеДеятельности           КАК НаправлениеДеятельности,
	|	ДанныеДокумента.Арендатор                          КАК Арендатор,
	|	НЕОПРЕДЕЛЕНО                                       КАК КорКонтрагент,
	|
	|	&ХО_ИзменениеПараметровСтоимостиОС                 КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.ОтражатьВРеглУчете                 КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете                  КАК ОтражатьВУпрУчете,
	|	
	|	&ИдентификаторНеиспользуемойФинЗаписи              КАК ИдентификаторФинЗаписи,
	|	НЕОПРЕДЕЛЕНО                                       КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасширеннаяТаблицаОС КАК РасширеннаяТаблицаОС
	|		ПО РасширеннаяТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = РасширеннаяТаблицаОС.Ссылка
	|			И ТаблицаОС.ОсновноеСредство = РасширеннаяТаблицаОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПараметрыУчетаОС КАК ПараметрыУчетаОС
	|		ПО ПараметрыУчетаОС.Ссылка = РасширеннаяТаблицаОС.Ссылка
	|			И ПараметрыУчетаОС.ОсновноеСредство = РасширеннаяТаблицаОС.ОсновноеСредство
	|
	|ГДЕ
	|	ДанныеДокумента.РасчетыМеждуОрганизациямиАрендатор
	|";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(СписокЗапросовОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);

КонецПроцедуры

Процедура ТекстЗапросаТаблицаАмортизацияОС(ТекстыЗапроса)

	ИмяРегистра = "АмортизацияОС";
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтРасширеннаяТаблицаОС(ТекстыЗапроса);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыУчетаОС(ТекстыЗапроса, "ВтРасширеннаяТаблицаОС");
	
	СписокЗапросовОбъединение = Новый Массив;
	
	#Область СписаниеАмортизации
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	СписаниеАмортизацииОС.ВидДвижения              КАК ВидДвижения,
	|	СписаниеАмортизацииОС.Регистратор              КАК Регистратор,
	|	СписаниеАмортизацииОС.Период                   КАК Период,
	|	СписаниеАмортизацииОС.Организация              КАК Организация,
	|	СписаниеАмортизацииОС.Подразделение            КАК Подразделение,
	|	СписаниеАмортизацииОС.ОсновноеСредство         КАК ОсновноеСредство,
	|	СписаниеАмортизацииОС.Амортизация              КАК Амортизация,
	|	СписаниеАмортизацииОС.АмортизацияРегл          КАК АмортизацияРегл,
	|	СписаниеАмортизацииОС.АмортизацияНУ            КАК АмортизацияНУ,
	|	СписаниеАмортизацииОС.АмортизацияПР            КАК АмортизацияПР,
	|	СписаниеАмортизацииОС.АмортизацияВР            КАК АмортизацияВР,
	|	СписаниеАмортизацииОС.АмортизацияЦФ            КАК АмортизацияЦФ,
	|	СписаниеАмортизацииОС.АмортизацияНУЦФ          КАК АмортизацияНУЦФ,
	|	СписаниеАмортизацииОС.АмортизацияПРЦФ          КАК АмортизацияПРЦФ,
	|	СписаниеАмортизацииОС.АмортизацияВРЦФ          КАК АмортизацияВРЦФ,
	|	СписаниеАмортизацииОС.РезервПереоценкиАмортизации КАК РезервПереоценкиАмортизации,
	|	СписаниеАмортизацииОС.РезервПереоценкиАмортизацииРегл КАК РезервПереоценкиАмортизацииРегл,
	|	СписаниеАмортизацииОС.ГруппаФинансовогоУчета   КАК ГруппаФинансовогоУчета,
	|	СписаниеАмортизацииОС.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	СписаниеАмортизацииОС.ХозяйственнаяОперация    КАК ХозяйственнаяОперация,
	|	СписаниеАмортизацииОС.НачислятьИзнос           КАК НачислятьИзнос,
	|	СписаниеАмортизацииОС.ИдентификаторФинЗаписи   КАК ИдентификаторФинЗаписи,
	|	СписаниеАмортизацииОС.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|ИЗ &ТекстПодзапроса КАК СписаниеАмортизацииОС
	|
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|	ПО СписаниеАмортизацииОС.Регистратор = ДанныеДокумента.Ссылка
	|		И &ПередачаВАрендуПоФСБУ25
	|		И ДанныеДокумента.ТипДоговора В (&ТипыДоговоровПоФСБУ25)";
	
	ТекстПодзапроса = ВнеоборотныеАктивыСлужебный.ТекстЗапросаСписанияАмортизацииОС(ТекстыЗапроса);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстПодзапроса", "(" + ТекстПодзапроса + ")");
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	
	#КонецОбласти

	#Область ИзменениеГФУИАрендатора
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)    КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                    КАК Регистратор,
	|	ДанныеДокумента.Период                    КАК Период,
	|	ДанныеДокумента.Организация               КАК Организация,
	|	ДанныеДокумента.Подразделение             КАК Подразделение,
	|	РасширеннаяТаблицаОС.ОсновноеСредство     КАК ОсновноеСредство,
	|
	|	АмортизацияОС.Амортизация*ДанныеДокумента.КоэффициентФСБУ25     КАК Амортизация,
	|	АмортизацияОС.АмортизацияРегл*ДанныеДокумента.КоэффициентФСБУ25 КАК АмортизацияРегл,
	|	АмортизацияОС.АмортизацияНУ               КАК АмортизацияНУ,
	|	АмортизацияОС.АмортизацияПР               КАК АмортизацияПР,
	|	АмортизацияОС.АмортизацияВР
	|		- АмортизацияОС.АмортизацияРегл*(1-ДанныеДокумента.КоэффициентФСБУ25) КАК АмортизацияВР,
	|	АмортизацияОС.АмортизацияЦФ               КАК АмортизацияЦФ,
	|	АмортизацияОС.АмортизацияНУЦФ             КАК АмортизацияНУЦФ,
	|	АмортизацияОС.АмортизацияПРЦФ             КАК АмортизацияПРЦФ,
	|	АмортизацияОС.АмортизацияВРЦФ             КАК АмортизацияВРЦФ,
	|
	|	АмортизацияОС.РезервПереоценкиАмортизации     КАК РезервПереоценкиАмортизации,
	|	АмортизацияОС.РезервПереоценкиАмортизацииРегл КАК РезервПереоценкиАмортизацииРегл,
	|
	|	ПараметрыУчетаОС.ГруппаФинансовогоУчета   КАК ГруппаФинансовогоУчета,
	|	ПараметрыУчетаОС.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	&ХО_ИзменениеПараметровАмортизацииОС      КАК ХозяйственнаяОперация,
	|	АмортизацияОС.НачислятьИзнос              КАК НачислятьИзнос,
	|
	|	РасширеннаяТаблицаОС.ИдентификаторСтроки  КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_ИзменениеПараметровАмортизацииОС КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасширеннаяТаблицаОС КАК РасширеннаяТаблицаОС
	|		ПО РасширеннаяТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПараметрыУчетаОС КАК ПараметрыУчетаОС
	|		ПО ПараметрыУчетаОС.Ссылка = РасширеннаяТаблицаОС.Ссылка
	|			И ПараметрыУчетаОС.ОсновноеСредство = РасширеннаяТаблицаОС.ОсновноеСредство
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияОС
	|		ПО АмортизацияОС.Ссылка = РасширеннаяТаблицаОС.Ссылка
	|			И АмортизацияОС.ОбъектУчета = РасширеннаяТаблицаОС.ОсновноеСредство
	|ГДЕ
	|	РасширеннаяТаблицаОС.ОсновноеСредство.ТипОС = ЗНАЧЕНИЕ(Перечисление.ТипыОС.ОбъектЭксплуатации)
	|	ИЛИ НЕ (&ПередачаВАрендуПоФСБУ25 И ДанныеДокумента.ТипДоговора В (&ТипыДоговоровПоФСБУ25))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)    КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                    КАК Регистратор,
	|	ДанныеДокумента.Период                    КАК Период,
	|	ДанныеДокумента.Организация               КАК Организация,
	|	ДанныеДокумента.ПодразделениеПолучатель   КАК Подразделение,
	|	РасширеннаяТаблицаОС.ОсновноеСредство     КАК ОсновноеСредство,
	|
	|	АмортизацияОС.Амортизация*ДанныеДокумента.КоэффициентФСБУ25     КАК Амортизация,
	|	АмортизацияОС.АмортизацияРегл*ДанныеДокумента.КоэффициентФСБУ25 КАК АмортизацияРегл,
	|	АмортизацияОС.АмортизацияНУ               КАК АмортизацияНУ,
	|	АмортизацияОС.АмортизацияПР               КАК АмортизацияПР,
	|	АмортизацияОС.АмортизацияВР 
	|		- АмортизацияОС.АмортизацияРегл*(1-ДанныеДокумента.КоэффициентФСБУ25) КАК АмортизацияВР,
	|	АмортизацияОС.АмортизацияЦФ               КАК АмортизацияЦФ,
	|	АмортизацияОС.АмортизацияНУЦФ             КАК АмортизацияНУЦФ,
	|	АмортизацияОС.АмортизацияПРЦФ             КАК АмортизацияПРЦФ,
	|	АмортизацияОС.АмортизацияВРЦФ             КАК АмортизацияВРЦФ,
	|
	|	АмортизацияОС.РезервПереоценкиАмортизации     КАК РезервПереоценкиАмортизации,
	|	АмортизацияОС.РезервПереоценкиАмортизацииРегл КАК РезервПереоценкиАмортизацииРегл,
	|
	|	ДанныеДокумента.ГруппаФинансовогоУчета    КАК ГруппаФинансовогоУчета,
	|	ПараметрыУчетаОС.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	&ХО_ИзменениеПараметровАмортизацииОС      КАК ХозяйственнаяОперация,
	|	АмортизацияОС.НачислятьИзнос              КАК НачислятьИзнос,
	|
	|	РасширеннаяТаблицаОС.ИдентификаторСтроки  КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_ИзменениеПараметровАмортизацииОС КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасширеннаяТаблицаОС КАК РасширеннаяТаблицаОС
	|		ПО РасширеннаяТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПараметрыУчетаОС КАК ПараметрыУчетаОС
	|		ПО ПараметрыУчетаОС.Ссылка = РасширеннаяТаблицаОС.Ссылка
	|			И ПараметрыУчетаОС.ОсновноеСредство = РасширеннаяТаблицаОС.ОсновноеСредство
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияОС
	|		ПО АмортизацияОС.Ссылка = РасширеннаяТаблицаОС.Ссылка
	|			И АмортизацияОС.ОбъектУчета = РасширеннаяТаблицаОС.ОсновноеСредство
	|ГДЕ
	|	РасширеннаяТаблицаОС.ОсновноеСредство.ТипОС = ЗНАЧЕНИЕ(Перечисление.ТипыОС.ОбъектЭксплуатации)
	|	ИЛИ НЕ (&ПередачаВАрендуПоФСБУ25 И ДанныеДокумента.ТипДоговора В (&ТипыДоговоровПоФСБУ25))
	|";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(СписокЗапросовОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаОбесценениеВНА(ТекстыЗапроса)

	ИмяРегистра = "ОбесценениеВНА";
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтТаблицаОС(ТекстыЗапроса);

	ТекстЗапроса = ВнеоборотныеАктивыСлужебный.ТекстЗапросаСписанияОбесцененияОС(ТекстыЗапроса);
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаДвиженияДоходыРасходыПрочиеАктивыПассивы(ТекстыЗапроса)

	ИмяРегистра = "ДвиженияДоходыРасходыПрочиеАктивыПассивы";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтРасширеннаяТаблицаОС(ТекстыЗапроса);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыУчетаОС(ТекстыЗапроса, "ВтРасширеннаяТаблицаОС");
	
	ТекстЗапроса =
	"// Перемещение амортизации
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка            КАК Регистратор,
	|	ДанныеДокумента.Период            КАК Период,
	|	&ХО_ИзменениеПараметровАмортизацииОС КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация       КАК Организация,
	|
	|	ДанныеДокумента.Подразделение     КАК Подразделение,
	|	ПараметрыУчетаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК Статья,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаРасходов,
	|	РасширеннаяТаблицаОС.ОсновноеСредство КАК АналитикаАктивовПассивов,
	|	ПараметрыУчетаОС.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|
	|	ДанныеДокумента.ПодразделениеПолучатель КАК КорПодразделение,
	|	ПараметрыУчетаОС.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаРасходов,
	|	РасширеннаяТаблицаОС.ОсновноеСредство КАК КорАналитикаАктивовПассивов,
	|	ПараметрыУчетаОС.ГруппаФинансовогоУчета КАК КорГруппаФинансовогоУчета,
	|
	|	ДанныеДокумента.ОрганизацияПолучатель КАК КорОрганизация,
	|
	|	АмортизацияОС.Амортизация         КАК Сумма,
	|	АмортизацияОС.Амортизация         КАК СуммаУпр,
	|	АмортизацияОС.АмортизацияРегл + АмортизацияОС.АмортизацияЦФ КАК СуммаРегл
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасширеннаяТаблицаОС КАК РасширеннаяТаблицаОС
	|		ПО РасширеннаяТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПараметрыУчетаОС КАК ПараметрыУчетаОС
	|		ПО ПараметрыУчетаОС.Ссылка = РасширеннаяТаблицаОС.Ссылка
	|			И ПараметрыУчетаОС.ОсновноеСредство = РасширеннаяТаблицаОС.ОсновноеСредство
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияОС
	|		ПО АмортизацияОС.Ссылка = РасширеннаяТаблицаОС.Ссылка
	|			И АмортизацияОС.ОбъектУчета = РасширеннаяТаблицаОС.ОсновноеСредство
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Перемещение стоимости
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка            КАК Регистратор,
	|	ДанныеДокумента.Период            КАК Период,
	|	&ХО_ИзменениеПараметровСтоимостиОС КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация       КАК Организация,
	|
	|	ДанныеДокумента.Подразделение     КАК Подразделение,
	|	ПараметрыУчетаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК Статья,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаРасходов,
	|	РасширеннаяТаблицаОС.ОсновноеСредство КАК АналитикаАктивовПассивов,
	|	ПараметрыУчетаОС.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|
	|	ДанныеДокумента.ПодразделениеПолучатель КАК КорПодразделение,
	|	ПараметрыУчетаОС.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаРасходов,
	|	РасширеннаяТаблицаОС.ОсновноеСредство КАК КорАналитикаАктивовПассивов,
	|	ПараметрыУчетаОС.ГруппаФинансовогоУчета  КАК КорГруппаФинансовогоУчета,
	|
	|	ДанныеДокумента.ОрганизацияПолучатель КАК КорОрганизация,
	|
	|	СтоимостьОС.Стоимость             КАК Сумма,
	|	СтоимостьОС.Стоимость             КАК СуммаУпр,
	|	СтоимостьОС.СтоимостьРегл + СтоимостьОС.СтоимостьЦФ КАК СуммаРегл
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасширеннаяТаблицаОС КАК РасширеннаяТаблицаОС
	|		ПО РасширеннаяТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПараметрыУчетаОС КАК ПараметрыУчетаОС
	|		ПО ПараметрыУчетаОС.Ссылка = РасширеннаяТаблицаОС.Ссылка
	|			И ПараметрыУчетаОС.ОсновноеСредство = РасширеннаяТаблицаОС.ОсновноеСредство
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьОС
	|		ПО СтоимостьОС.Ссылка = РасширеннаяТаблицаОС.Ссылка
	|			И СтоимостьОС.ОбъектУчета = РасширеннаяТаблицаОС.ОсновноеСредство
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаГрафикОплатУслугПоДоходнойАренде(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ГрафикОплатУслугПоДоходнойАренде";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Период				КАК ДатаИзменения,
	|	ДанныеДокумента.Договор				КАК Договор,
	|	ДанныеДокумента.Ссылка				КАК АктуальныеУсловияДоговора,
	|	ТаблицаГрафика.Дата					КАК Дата,
	|	ТаблицаГрафика.ОсновноеСредство		КАК ОсновноеСредство,
	|	ТаблицаГрафика.УслугаПоАренде		КАК УслугаПоАренде,
	|	ТаблицаГрафика.УслугаПоАрендеНДС	КАК УслугаПоАрендеНДС
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаГрафикОплатУслуг КАК ТаблицаГрафика
	|		ПО ДанныеДокумента.Ссылка = ТаблицаГрафика.Ссылка
	|ГДЕ
	|	&ПередачаВАрендуПоФСБУ25
	|		И ДанныеДокумента.ТипДоговора В (&ТипыДоговоровПоФСБУ25)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаГрафикНачисленияУслугПоДоходнойАренде(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ГрафикНачисленияУслугПоДоходнойАренде";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Период				КАК ДатаИзменения,
	|	ДанныеДокумента.Договор				КАК Договор,
	|	ДанныеДокумента.Ссылка				КАК АктуальныеУсловияДоговора,
	|	ТаблицаГрафика.Дата					КАК Дата,
	|	ТаблицаГрафика.ОсновноеСредство		КАК ОсновноеСредство,
	|	ТаблицаГрафика.УслугаПоАренде		КАК УслугаПоАренде,
	|	ТаблицаГрафика.УслугаПоАрендеНДС	КАК УслугаПоАрендеНДС
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаГрафикНачисленияУслуг КАК ТаблицаГрафика
	|		ПО ДанныеДокумента.Ссылка = ТаблицаГрафика.Ссылка
	|ГДЕ
	|	&ПередачаВАрендуПоФСБУ25
	|		И ДанныеДокумента.ТипДоговора В (&ТипыДоговоровПоФСБУ25)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаГрафикНачисленияПроцентовПоДоходнойАренде(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ГрафикНачисленияПроцентовПоДоходнойАренде";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Период				КАК ДатаИзменения,
	|	ДанныеДокумента.Договор				КАК Договор,
	|	ДанныеДокумента.Ссылка				КАК АктуальныеУсловияДоговора,
	|	ТаблицаГрафика.Дата					КАК Дата,
	|	ТаблицаГрафика.ОсновноеСредство		КАК ОсновноеСредство,
	|	ТаблицаГрафика.Проценты				КАК Проценты
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаГрафикНачисленияПроцентов КАК ТаблицаГрафика
	|		ПО ДанныеДокумента.Ссылка = ТаблицаГрафика.Ссылка
	|ГДЕ
	|	&ПередачаВАрендуПоФСБУ25
	|		И ДанныеДокумента.ТипДоговора В (&ТипыДоговоровПоФСБУ25)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаИнвестицииВАренду(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ИнвестицииВАренду";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтТаблицаОСПоФСБУ25(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтПроценты(ТекстыЗапроса);
	
	СписокЗапросовОбъединение = Новый Массив;
	СписокЗапросовОбъединение.Добавить(РасчетСтоимостиИнвестицииВАренду.ТекстЗапросаТаблицаИнвестицииВАренду());

	#Область ОтнесениеВложенийВАрендуНаЧИА // Дт <58.АП> - Кт <58.ВА>
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                 КАК Регистратор,
	|	ДанныеДокумента.Период                 КАК Период,
	|	ДанныеДокумента.Организация            КАК Организация,
	|	ДанныеДокумента.Договор                КАК Договор,
	|	ДанныеДокумента.ВалютаВзаиморасчетов   КАК Валюта,
	|	ТаблицаОС.ОсновноеСредство             КАК ОсновноеСредство,
	|	ТаблицаОС.ГруппаФинансовогоУчета       КАК ГруппаФинансовогоУчета,
	|	0                                      КАК АрендныеПлатежи,
	|	СуммаРасходов.РасходыРегл              КАК АрендныеПлатежиРегл,
	|	СуммаРасходов.РасходыУпр               КАК АрендныеПлатежиУпр,
	|	0                                      КАК НГЛСРегл,
	|	0                                      КАК НГЛСУпр,
	|	0                                      КАК ПроцентныйДисконт,
	|	0                                      КАК ПроцентныйДисконтРегл,
	|	0                                      КАК ПроцентныйДисконтУпр,
	|	&НастройкаХО_ПередачаОСВАренду         КАК НастройкаХозяйственнойОперации,
	|	ТаблицаОС.ИдентификаторСтроки          КАК ИдентификаторФинЗаписи
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТаблицаОСПОФСБУ25 КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СуммаРасходов КАК СуммаРасходов
	|		ПО ТаблицаОС.ОсновноеСредство = СуммаРасходов.ОсновноеСредство
	|";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ВыделениеНГЛС // Дт <58.НГЛС> - Кт <58.АП>
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                 КАК Регистратор,
	|	ДанныеДокумента.Период                 КАК Период,
	|	ДанныеДокумента.Организация            КАК Организация,
	|	ДанныеДокумента.Договор                КАК Договор,
	|	ДанныеДокумента.ВалютаВзаиморасчетов   КАК Валюта,
	|	ТаблицаОС.ОсновноеСредство             КАК ОсновноеСредство,
	|	ТаблицаОС.ГруппаФинансовогоУчета       КАК ГруппаФинансовогоУчета,
	|	0                                      КАК АрендныеПлатежи,
	|	ТаблицаОС.НГЛСРегл                     КАК АрендныеПлатежиРегл,
	|	ТаблицаОС.НГЛСУпр                      КАК АрендныеПлатежиУпр,
	|	0                                      КАК НГЛСРегл,
	|	0                                      КАК НГЛСУпр,
	|	0                                      КАК ПроцентныйДисконт,
	|	0                                      КАК ПроцентныйДисконтУпр,
	|	0                                      КАК ПроцентныйДисконтРегл,
	|	&НастройкаХО_ВыделениеНГЛС             КАК НастройкаХозяйственнойОперации,
	|	ТаблицаОС.ИдентификаторСтроки          КАК ИдентификаторФинЗаписи
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТаблицаОСПОФСБУ25 КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|ГДЕ
	|	&ПередачаВАрендуПоФСБУ25
	|	И (ТаблицаОС.НГЛСРегл <> 0 ИЛИ ТаблицаОС.НГЛСУпр <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                 КАК Регистратор,
	|	ДанныеДокумента.Период                 КАК Период,
	|	ДанныеДокумента.Организация            КАК Организация,
	|	ДанныеДокумента.Договор                КАК Договор,
	|	ДанныеДокумента.ВалютаВзаиморасчетов   КАК Валюта,
	|	ТаблицаОС.ОсновноеСредство             КАК ОсновноеСредство,
	|	ТаблицаОС.ГруппаФинансовогоУчета       КАК ГруппаФинансовогоУчета,
	|	0                                      КАК АрендныеПлатежи,
	|	0                                      КАК АрендныеПлатежиРегл,
	|	0                                      КАК АрендныеПлатежиУпр,
	|	ТаблицаОС.НГЛСРегл                     КАК НГЛСРегл,
	|	ТаблицаОС.НГЛСУпр                      КАК НГЛСУпр,
	|	0                                      КАК ПроцентныйДисконт,
	|	0                                      КАК ПроцентныйДисконтУпр,
	|	0                                      КАК ПроцентныйДисконтРегл,
	|	&НастройкаХО_ВыделениеНГЛС             КАК НастройкаХозяйственнойОперации,
	|	ТаблицаОС.ИдентификаторСтроки          КАК ИдентификаторФинЗаписи
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТаблицаОСПОФСБУ25 КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|ГДЕ
	|	&ПередачаВАрендуПоФСБУ25
	|	И (ТаблицаОС.НГЛСРегл <> 0 ИЛИ ТаблицаОС.НГЛСУпр <> 0)";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ВыделениеПроцентов // Дт <58.АП> - Кт <58.%>
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                 КАК Регистратор,
	|	ДанныеДокумента.Период                 КАК Период,
	|	ДанныеДокумента.Организация            КАК Организация,
	|	ДанныеДокумента.Договор                КАК Договор,
	|	ДанныеДокумента.ВалютаВзаиморасчетов   КАК Валюта,
	|	ТаблицаОС.ОсновноеСредство             КАК ОсновноеСредство,
	|	ТаблицаОС.ГруппаФинансовогоУчета       КАК ГруппаФинансовогоУчета,
	|	ТаблицаОС.АрендныеПлатежи              КАК АрендныеПлатежи,
	|	втПроценты.ПроцентыРегл                КАК АрендныеПлатежиРегл,
	|	втПроценты.ПроцентыУпр                 КАК АрендныеПлатежиУпр,
	|	0                                      КАК НГЛСРегл,
	|	0                                      КАК НГЛСУпр,
	|	0                                      КАК ПроцентныйДисконт,
	|	0                                      КАК ПроцентныйДисконтРегл,
	|	0                                      КАК ПроцентныйДисконтУпр,
	|	&НастройкаХО_НачислениеПроцентовПоДоходнойАренде КАК НастройкаХозяйственнойОперации,
	|	ТаблицаОС.ИдентификаторСтроки          КАК ИдентификаторФинЗаписи
	|ИЗ
	|	втТаблицаОСПОФСБУ25 КАК ТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПроценты КАК втПроценты
	|		ПО ТаблицаОС.ОсновноеСредство = втПроценты.ОсновноеСредство
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                 КАК Регистратор,
	|	ДанныеДокумента.Период                 КАК Период,
	|	ДанныеДокумента.Организация            КАК Организация,
	|	ДанныеДокумента.Договор                КАК Договор,
	|	ДанныеДокумента.ВалютаВзаиморасчетов   КАК Валюта,
	|	ТаблицаОС.ОсновноеСредство             КАК ОсновноеСредство,
	|	ТаблицаОС.ГруппаФинансовогоУчета       КАК ГруппаФинансовогоУчета,
	|	0                                      КАК АрендныеПлатежи,
	|	0                                      КАК АрендныеПлатежиРегл,
	|	0                                      КАК АрендныеПлатежиУпр,
	|	0                                      КАК НГЛСРегл,
	|	0                                      КАК НГЛСУпр,
	|	втПроценты.Проценты                    КАК ПроцентныйДисконт,
	|	втПроценты.ПроцентыРегл                КАК ПроцентныйДисконтРегл,
	|	втПроценты.ПроцентыУпр                 КАК ПроцентныйДисконтУпр,
	|	&НастройкаХО_НачислениеПроцентовПоДоходнойАренде КАК НастройкаХозяйственнойОперации,
	|	ТаблицаОС.ИдентификаторСтроки          КАК ИдентификаторФинЗаписи
	|ИЗ
	|	втТаблицаОСПОФСБУ25 КАК ТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПроценты КАК втПроценты
	|		ПО ТаблицаОС.ОсновноеСредство = втПроценты.ОсновноеСредство
	|";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(СписокЗапросовОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеРасходы(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеРасходы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ДопПоля = "Регистратор";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(ТекстыЗапроса, ДопПоля);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаПрочиеРасходы(ДопПоля, Истина);
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область ФормированиеВременныхТаблицДляПроведения

Процедура ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(ТекстыЗапроса, ДопПоля)
	
	ИмяРегистра = "ВтИсходныеПрочиеРасходы";
	
	СписокЗапросовОбъединение = Новый Массив;
	СписокЗапросовОбъединение.Добавить(РегистрыНакопления.ПрочиеРасходы.ТекстОписаниеВтИсходныеПрочиеРасходы(ДопПоля));
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	ВыбытиеОС.Период КАК Период,
	|	ВыбытиеОС.ВидДвижения КАК ВидДвижения,
	|	ВыбытиеОС.Организация КАК Организация,
	|	ВыбытиеОС.Подразделение КАК Подразделение,
	|	ВыбытиеОС.СтатьяРасходов КАК СтатьяРасходов,
	|	ВыбытиеОС.АналитикаРасходов КАК АналитикаРасходов,
	|	ВыбытиеОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВыбытиеОС.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
	|	ВыбытиеОС.СуммаСНДС КАК СуммаСНДС,
	|	ВыбытиеОС.СуммаБезНДС КАК СуммаБезНДС,
	|	ВыбытиеОС.СуммаБезНДСУпр КАК СуммаБезНДСУпр,
	|	ВыбытиеОС.СуммаСНДСРегл КАК СуммаСНДСРегл,
	|	ВыбытиеОС.СуммаБезНДСРегл КАК СуммаБезНДСРегл,
	|	ВыбытиеОС.ПостояннаяРазница КАК ПостояннаяРазница,
	|	ВыбытиеОС.ВременнаяРазница КАК ВременнаяРазница,
	|	ВыбытиеОС.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ВыбытиеОС.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВыбытиеОС.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|	ВыбытиеОС.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации,
	|	ВыбытиеОС.Регистратор КАК Регистратор
	|ИЗ &ТекстПодзапроса КАК ВыбытиеОС
	|
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|	ПО ВыбытиеОС.Регистратор = ДанныеДокумента.Ссылка
	|		И &ПередачаВАрендуПоФСБУ25
	|		И ДанныеДокумента.ТипДоговора В (&ТипыДоговоровПоФСБУ25)";
	
	ТекстПодзапроса = ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтИсходныеПрочиеРасходыПриВыбытииОС(ТекстыЗапроса);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстПодзапроса", "(" + ТекстПодзапроса + ")");
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = СтрСоединить(СписокЗапросовОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаВтПрочиеРасходы(ТекстыЗапроса, ДопПоля)
	
	ИмяРегистра = "ВтПрочиеРасходы";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтИсходныеПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(ТекстыЗапроса, ДопПоля);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаВтПрочиеРасходы(ДопПоля, Истина);
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ТекстЗапросаТаблицаВтТаблицаОС(ТекстыЗапроса)
	
	ИмяТаблицы = "втТаблицаОС";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтРасширеннаяТаблицаОС(ТекстыЗапроса);
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПервоначальныеСведенияОС(ТекстыЗапроса, "ВтРасширеннаяТаблицаОС");
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПорядокУчетаОС(ТекстыЗапроса, "ВтРасширеннаяТаблицаОС");
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтМестонахождениеОС(ТекстыЗапроса, "ВтРасширеннаяТаблицаОС");
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтУзлыКомпонентыАмортизации(ТекстыЗапроса);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаОС.Ссылка                       КАК Ссылка,
	|	ТаблицаОС.НомерСтроки                  КАК НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство             КАК ОсновноеСредство,
	|	ЗНАЧЕНИЕ(Справочник.ОбъектыЭксплуатации.ПустаяСсылка) КАК ОСВладелец,
	|
	|	ПорядокУчета.ГруппаФинансовогоУчета    КАК ГруппаФинансовогоУчета,
	|	ПорядокУчета.НаправлениеДеятельности   КАК НаправлениеДеятельности,
	|	ПорядокУчета.ПоказательНаработки       КАК ПоказательНаработки,
	|	ПорядокУчета.ОбъемНаработки            КАК ОбъемНаработки,
	|
	|	МестонахождениеОС.Организация          КАК Организация,
	|	МестонахождениеОС.МОЛ                  КАК МОЛ,
	|	МестонахождениеОС.Местонахождение      КАК Местонахождение,
	|	МестонахождениеОС.АдресМестонахождения КАК АдресМестонахождения,
	|	МестонахождениеОС.Арендатор            КАК Арендатор,
	|
	|	ЛОЖЬ                                   КАК НачислятьИзнос,
	|
	|	НЕОПРЕДЕЛЕНО                           КАК СтатьяДоходов,
	|	НЕОПРЕДЕЛЕНО                           КАК АналитикаДоходов,
	|
	|	0                                      КАК СуммаСписанияУУ,
	|	0                                      КАК СуммаСписанияБУ,
	|	0                                      КАК СуммаСписанияНУ,
	|	0                                      КАК СуммаСписанияПР,
	|	0                                      КАК СуммаСписанияВР,
	|	
	|	ТаблицаОС.ИдентификаторСтроки 		   КАК ИдентификаторСтроки
	|	
	|ПОМЕСТИТЬ втТаблицаОС
	|
	|ИЗ
	|	ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаОС КАК ПорядокУчета
	|		ПО ПорядокУчета.Ссылка = ТаблицаОС.Ссылка
	|			И ПорядокУчета.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втМестонахождениеОС КАК МестонахождениеОС
	|		ПО МестонахождениеОС.Ссылка = ТаблицаОС.Ссылка
	|			И МестонахождениеОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаОС.Ссылка                       КАК Ссылка,
	|	ТаблицаОС.НомерСтроки                  КАК НомерСтроки,
	|	УзлыКомпоненты.УзелКомпонент           КАК ОсновноеСредство,
	|	ТаблицаОС.ОсновноеСредство             КАК ОСВладелец,
	|
	|	ПорядокУчета.ГруппаФинансовогоУчета    КАК ГруппаФинансовогоУчета,
	|	ПорядокУчета.НаправлениеДеятельности   КАК НаправлениеДеятельности,
	|	ПорядокУчета.ПоказательНаработки       КАК ПоказательНаработки,
	|	ПорядокУчета.ОбъемНаработки            КАК ОбъемНаработки,
	|
	|	МестонахождениеОС.Организация          КАК Организация,
	|	МестонахождениеОС.МОЛ                  КАК МОЛ,
	|	МестонахождениеОС.Местонахождение      КАК Местонахождение,
	|	МестонахождениеОС.АдресМестонахождения КАК АдресМестонахождения,
	|	МестонахождениеОС.Арендатор            КАК Арендатор,
	|
	|	ЛОЖЬ                                   КАК НачислятьИзнос,
	|
	|	НЕОПРЕДЕЛЕНО                           КАК СтатьяДоходов,
	|	НЕОПРЕДЕЛЕНО                           КАК АналитикаДоходов,
	|
	|	0                                      КАК СуммаСписанияУУ,
	|	0                                      КАК СуммаСписанияБУ,
	|	0                                      КАК СуммаСписанияНУ,
	|	0                                      КАК СуммаСписанияПР,
	|	0                                      КАК СуммаСписанияВР,
	|	
	|	УзлыКомпоненты.ИдентификаторСтроки     КАК ИдентификаторСтроки
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтУзлыКомпонентыАмортизации КАК УзлыКомпоненты
	|		ПО УзлыКомпоненты.Ссылка = ТаблицаОС.Ссылка
	|			И УзлыКомпоненты.ОсновноеСредство = ТаблицаОС.ОсновноеСредство 
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаОС КАК ПорядокУчета
	|		ПО ПорядокУчета.Ссылка = УзлыКомпоненты.Ссылка 
	|			И ПорядокУчета.ОсновноеСредство = УзлыКомпоненты.УзелКомпонент 
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втМестонахождениеОС КАК МестонахождениеОС
	|		ПО МестонахождениеОС.Ссылка = УзлыКомпоненты.Ссылка 
	|			И МестонахождениеОС.ОсновноеСредство = УзлыКомпоненты.УзелКомпонент 
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
	|		ПО ПервоначальныеСведенияОС.Ссылка = УзлыКомпоненты.Ссылка 
	|			И ПервоначальныеСведенияОС.ОсновноеСредство = УзлыКомпоненты.УзелКомпонент 
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	ОсновноеСредство
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтТаблицаОСПоФСБУ25(ТекстыЗапроса)
	
	ИмяТаблицы = "втТаблицаОСПоФСБУ25";

	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтРасширеннаяТаблицаОС(ТекстыЗапроса);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыУчетаОС(ТекстыЗапроса, "ВтРасширеннаяТаблицаОС");
	ТекстЗапросаТаблицаВтКоэффициентыПересчетаВалют(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаОС.СправедливаяСтоимостьРегл КАК СправедливаяСтоимостьРегл,
	|	ТаблицаОС.СправедливаяСтоимостьУпр КАК СправедливаяСтоимостьУпр,
	|	ТаблицаОС.НГЛСРегл КАК НГЛСРегл,
	|	ТаблицаОС.НГЛСУпр КАК НГЛСУпр,
	|	ТаблицаОС.АрендныеПлатежи КАК АрендныеПлатежи,
	|	ВЫРАЗИТЬ(ТаблицаОС.АрендныеПлатежи * ЕСТЬNULL(Коэффициенты.КоэффициентПересчетаРегл, 0) КАК ЧИСЛО(31, 2)) КАК АрендныеПлатежиРегл,
	|	ВЫРАЗИТЬ(ТаблицаОС.АрендныеПлатежи * ЕСТЬNULL(Коэффициенты.КоэффициентПересчетаУпр, 0) КАК ЧИСЛО(31, 2)) КАК АрендныеПлатежиУпр,
	|	ТаблицаОС.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ втТаблицаОСПоФСБУ25
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаОС КАК ТаблицаОС
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКоэффициентыПересчетаВалют КАК Коэффициенты
	|		ПО (Коэффициенты.Дата = ДанныеДокумента.Период)
	|			И (Коэффициенты.Организация = ДанныеДокумента.Организация)
	|			И (Коэффициенты.ВалютаВзаиморасчетов = ДанныеДокумента.ВалютаВзаиморасчетов)
	|ГДЕ
	|	&ПередачаВАрендуПоФСБУ25
	|	И ДанныеДокумента.ТипДоговора В(&ТипыДоговоровПоФСБУ25)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	ИдентификаторСтроки,
	|	ОсновноеСредство";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);

КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтПроценты(ТекстыЗапроса)
	
	ИмяТаблицы = "втПроценты";

	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтКоэффициентыПересчетаВалют(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ГрафикНачисленияПроцентов.Ссылка КАК Ссылка,
	|	ГрафикНачисленияПроцентов.ОсновноеСредство КАК ОсновноеСредство,
	|	СУММА(ГрафикНачисленияПроцентов.Проценты) КАК Проценты
	|ПОМЕСТИТЬ втПроцентыПоГрафику
	|ИЗ
	|	ТаблицаГрафикНачисленияПроцентов КАК ГрафикНачисленияПроцентов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|		ПО ДанныеДокумента.Ссылка = ГрафикНачисленияПроцентов.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ГрафикНачисленияПроцентов.Ссылка,
	|	ГрафикНачисленияПроцентов.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПроцентыПоГрафику.Ссылка КАК Ссылка,
	|	втПроцентыПоГрафику.ОсновноеСредство КАК ОсновноеСредство,
	|	втПроцентыПоГрафику.Проценты КАК Проценты,
	|	втПроцентыПоГрафику.Проценты * ЕСТЬNULL(Коэффициенты.КоэффициентПересчетаРегл, 0) КАК ПроцентыРегл,
	|	втПроцентыПоГрафику.Проценты * ЕСТЬNULL(Коэффициенты.КоэффициентПересчетаУпр, 0) КАК ПроцентыУпр
	|ПОМЕСТИТЬ втПроценты
	|ИЗ
	|	втПроцентыПоГрафику КАК втПроцентыПоГрафику
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|		ПО ДанныеДокумента.Ссылка = втПроцентыПоГрафику.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКоэффициентыПересчетаВалют КАК Коэффициенты
	|		ПО Коэффициенты.Организация = ДанныеДокумента.Организация
	|			И Коэффициенты.Дата = ДанныеДокумента.Период
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтКоэффициентыПересчетаВалют(ТекстыЗапроса)

	ИмяТаблицы = "втКоэффициентыПересчетаВалют";

	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ДанныеДокумента.Период КАК Дата
	|ПОМЕСТИТЬ ТаблицаПериодов
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ВалютаВзаиморасчетов,
	|	Дата";
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ТаблицаПериодов");
	
	ФинансовыеИнструменты.ТекстЗапросаВтКоэффициентыПересчетаВалют(ТекстыЗапроса, ИмяТаблицы, Истина);

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ПроведениеПоРеглУчету

Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	Возврат ПередачаОСВАрендуЛокализация.ТекстЗапросаВТОтраженияВРеглУчете();
	
КонецФункции

Функция ТекстОтраженияВРеглУчете() Экспорт
	
	Возврат ПередачаОСВАрендуЛокализация.ТекстОтраженияВРеглУчете();
	
КонецФункции

#КонецОбласти

#Область Печать

// Добавляет команды печати.
// 
// Параметры:
// 	КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	ПередачаОСВАрендуЛокализация.ДобавитьКомандыПечати(КомандыПечати);
	
КонецПроцедуры

Функция ПолучитьДанныеДляПечатнойФормыОС1(ПараметрыПечати, МассивОбъектов) Экспорт
	
	Возврат ПередачаОСВАрендуЛокализация.ПолучитьДанныеДляПечатнойФормыОС1(ПараметрыПечати, МассивОбъектов);
	
КонецФункции

#КонецОбласти

#Область Прочее

Функция ПараметрыВыбораСтатейИАналитик() Экспорт
	
	ПараметрыВыбораСтатейИАналитик = Новый Массив;
	
	// СтатьяРасходовУУ
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья = "СтатьяРасходовУУ";
	ПараметрыВыбора.ДоступностьПоОперации = Неопределено;
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходовУУ";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходовУУ");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходовУУ");
	
	ПараметрыВыбораСтатейИАналитик.Добавить(ПараметрыВыбора);
	
	// СтатьяРасходовБУ
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья = "СтатьяРасходовБУ";
	ПараметрыВыбора.ДоступностьПоОперации = Неопределено;
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходовБУ";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходовБУ");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходовБУ");
	
	ПараметрыВыбораСтатейИАналитик.Добавить(ПараметрыВыбора);
	
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья = "СтатьяРасходовБУ";
	ПараметрыВыбора.ДоступностьПоОперации = Неопределено;

	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходовБУ";

	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходов");

	ПараметрыВыбораСтатейИАналитик.Добавить(ПараметрыВыбора);
	
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья = "СтатьяДоходов";
	ПараметрыВыбора.ДоступностьПоОперации = Неопределено;
	
	ПараметрыВыбора.ВыборСтатьиДоходов = Истина;
	ПараметрыВыбора.АналитикаДоходов = "АналитикаДоходов";

	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяДоходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаДоходов.Добавить("АналитикаДоходов");
	
	ПараметрыВыбораСтатейИАналитик.Добавить(ПараметрыВыбора);
	
	Возврат ПараметрыВыбораСтатейИАналитик;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
