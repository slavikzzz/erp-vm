#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает имена блокируемых реквизитов для механизма блокирования реквизитов БСП.
//
// Возвращаемое значение:
//	Массив - имена блокируемых реквизитов.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	Результат = Новый Массив;
	Результат.Добавить("ТипРасходов");
	Результат.Добавить("ВариантРаспределенияРасходовУпр");
	Результат.Добавить("ВариантРаспределенияРасходовРегл");
	Результат.Добавить("ВариантРаспределенияРасходовНУ");
	Результат.Добавить("ТипЗначения; ТипЗначения");
	Результат.Добавить("ВидДеятельностиРасходов");
	Результат.Добавить("ВариантРаздельногоУчетаНДС");
	Результат.Добавить("ПризнаютсяВРасходахНДД");
	
	СтатьиРасходовЛокализация.ДополнитьБлокируемыеРеквизитыОбъекта(Результат);
	
	Возврат Результат;
	
КонецФункции

// Функция определяет реквизиты выбранной статьи расходов.
//
// Параметры:
//  СтатьяРасходов - ПланВидовХарактеристикСсылка.СтатьиРасходов - Ссылка на статью расходов.
//
// Возвращаемое значение:
//	Структура - реквизиты статьи расходов.
//
Функция ПолучитьРеквизитыСтатьиРасходов(Знач СтатьяРасходов) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	СтатьиРасходов.СпособРаспределенияПоНаправлениямДеятельности КАК СпособРаспределения,
	|	СтатьиРасходов.ТипЗначения КАК ТипЗначения,
	|	СтатьиРасходов.СтатьяРасходов КАК СтатьяРасходов
	|ИЗ
	|	ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|ГДЕ
	|	СтатьиРасходов.Ссылка = &СтатьяРасходов
	|");
	
	Запрос.УстановитьПараметр("СтатьяРасходов", СтатьяРасходов);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		СпособРаспределения = Выборка.СпособРаспределения;
		ТребуетсяСпособРаспределения = Не Выборка.ТипЗначения.СодержитТип(Тип("СправочникСсылка.НаправленияДеятельности"));
		ТипЗначения = Выборка.ТипЗначения;
		СтатьяРасходовРаспределение = Выборка.СтатьяРасходов;
	Иначе
		СпособРаспределения = Справочники.СпособыРаспределенияПоНаправлениямДеятельности.ПустаяСсылка();
		ТребуетсяСпособРаспределения = Ложь;
		ТипЗначения = Неопределено;
		СтатьяРасходовРаспределение = ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();
	КонецЕсли;
	
	СтруктураРеквизитов = Новый Структура("СпособРаспределения, ТребуетсяСпособРаспределения, ТипЗначения",
		СпособРаспределения,
		ТребуетсяСпособРаспределения,
		ТипЗначения);
	СтруктураРеквизитов.Вставить("СтатьяРасходов", СтатьяРасходовРаспределение);
	
	Возврат СтруктураРеквизитов;

КонецФункции

// Функция определяет правило распределения на себестоимость товаров для статьи расходов.
//
// Параметры:
//  СтатьяРасходов - ПланВидовХарактеристикСсылка.СтатьиРасходов - Ссылка на статью расходов.
//
// Возвращаемое значение:
//	ПеречислениеСсылка.ПравилаРаспределенияНаСебестоимостьТоваров - правило распределения статьи расходов.
//
Функция ПолучитьПравилоРаспределения(СтатьяРасходов) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	СтатьиРасходов.ПравилоРаспределенияНаСебестоимость КАК ПравилоРаспределения
	|ИЗ
	|	ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|ГДЕ
	|	СтатьиРасходов.Ссылка = &СтатьяРасходов
	|	И (СтатьиРасходов.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
	|		ИЛИ СтатьиРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров))
	|");
	
	Запрос.УстановитьПараметр("СтатьяРасходов", СтатьяРасходов);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ПравилоРаспределения = Выборка.ПравилоРаспределения;
	Иначе
		ПравилоРаспределения = Неопределено;
	КонецЕсли;
	
	Возврат ПравилоРаспределения;

КонецФункции

// Процедура заполняет список хозяйственных операций, для которых используется
// ограничение отбора статьей расходов.
//
// Параметры:
//	СписокОпераций - СписокЗначений - Список хозяйственных операций и их представлений.
//	ВариантРаспределенияРасходовУпр - ПеречислениеСсылка.ВариантыРаспределенияРасходов - вариант распределения в упр. учете
//	ВариантРаспределенияРасходовРегл - ПеречислениеСсылка.ВариантыРаспределенияРасходов - вариант распределения в регл. учете
//
Процедура ЗаполнитьСписокХозяйственныхОпераций(СписокОпераций, ВариантРаспределенияРасходовУпр = Неопределено, ВариантРаспределенияРасходовРегл = Неопределено) Экспорт
	
	СписокОпераций.Очистить();
	СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.АвансовыйОтчет, НСтр("ru = 'Прочие расходы подотчетного лица';
																					|en = 'Other expenses of advance holders'"));
	СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика, НСтр("ru = 'Услуги сторонних организаций';
																						|en = 'Third party services'"));
	СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.ПрочиеРасходы, НСтр("ru = 'Выдача денежных средств на прочие расходы';
																					|en = 'Cash issue for other expenses'"));
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьВнутреннееПотребление") Тогда
		СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.СписаниеТоваровПоТребованию, НСтр("ru = 'Внутреннее потребление товаров';
																									|en = 'Inventory consumption — Non-operational activities'"));
	КонецЕсли;
	Если ПолучитьФункциональнуюОпцию("ИспользоватьКачествоТоваров") Тогда
		СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.ПорчаТоваров, НСтр("ru = 'Изменение стоимости испорченного товара';
																						|en = 'Change of the spoiled goods cost'"));
	КонецЕсли;
	СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.ПересортицаТоваров, НСтр("ru = 'Изменение стоимости при пересортице';
																						|en = 'Cost change in case of stock reclass'"));
	СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.СписаниеТоваров, НСтр("ru = 'Списания недостач товаров';
																					|en = 'Inventory shortage write-offs'"));
	
	СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.РеализацияКлиенту, НСтр("ru = 'Реализация услуг и прочих активов';
																						|en = 'Customer invoice — Services and Assets'"));
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьВнеоборотныеАктивы2_2")
	 ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьВнеоборотныеАктивы2_4") Тогда
		СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.ПринятиеКУчетуОС, НСтр("ru = 'Принятие к учету ОС';
																							|en = 'Fixed assets — Initial recognition'"));
		СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.ИзменениеПараметровАмортизацииОС, НСтр("ru = 'Изменение параметров ОС';
																											|en = 'Adjust fixed asset financial details'"));
		СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.СписаниеОС, НСтр("ru = 'Списание ОС';
																					|en = 'Dispose fixed assets'"));
		СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.ПринятиеКУчетуНМА, НСтр("ru = 'Принятие к учету НМА';
																							|en = 'Intangible assets — Initial recognition'"));
		СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.ИзменениеПараметровАмортизацииНМА, НСтр("ru = 'Изменение параметров НМА';
																											|en = 'Adjust intangible asset financial details'"));
		СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.СписаниеНМА, НСтр("ru = 'Списание НМА';
																					|en = 'Dispose intangible assets'"));
	КонецЕсли;
	
	Если ВариантРаспределенияРасходовУпр = Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности
	 ИЛИ ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности Тогда
		СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.СписаниеДебиторскойЗадолженности, НСтр("ru = 'Списание задолженности партнеров';
																											|en = 'Partner AR/AP write-off'"));
		Если ПолучитьФункциональнуюОпцию("ИспользоватьОплатуПлатежнымиКартами") Тогда
			СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.ОтчетБанкаПоОперациямЭквайринга, НСтр("ru = 'Эквайринговая комиссия';
																											|en = 'Acquiring fee'"));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Производит проверку заполнения реквизитов аналитик статей расходов в переданном объекте.
//
// Параметры:
// 		Объект - СправочникОбъект, ДокументОбъект, ДанныеФормыСтруктура - Объект ИБ предназначенный для проверки
// 		Реквизиты - Строка, Структура, Массив - Реквизиты статей расходов для проверки
// 			<Строка> Перечисление пар реквизитов для проверки в формате "СтатьяРаходов1, АналитикаРасходов1, СтатьяРаходов2, АналитикаРасходов2, ..."
// 				Пустая строка трактуется как "СтатьяРасходов, АналитикаРасходов"
// 			<Структура> Ключ: Строка с именем табличной части; Значение - Строка в нотации как у параметра типа <Строка>
// 			<Массив> Массив, элементы которого либо структуры в нотации как у параметра с типа <Структура>, либо строки в нотации <Строка>
// 		НепроверяемыеРеквизиты - Массив - Массив для накопления не проверяемых реквизитов переданного объекта
// 		Отказ - Булево - Признак наличия ошибок заполнения аналитик расходов переданного объекта
// 		ДополнительныеПараметры - Структура - При наличии свойства "ПрограммнаяПроверка", ошибки записываются в эту структуру, пользователю не выводятся.
//
Процедура ПроверитьЗаполнениеАналитик(Объект, Реквизиты = "", НепроверяемыеРеквизиты = Неопределено, Отказ = Ложь,
	ДополнительныеПараметры = Неопределено) Экспорт
	
	Ошибки = Новый Структура;
	Ошибки.Вставить("СписокОшибок", Новый Массив);
	Ошибки.Вставить("ГруппыОшибок", Новый Соответствие);
	Ошибки.Вставить("ПрефиксОбъекта", ?(ТипЗнч(Объект)=Тип("ФормаКлиентскогоПриложения"), "", "Объект."));
	
	МассивОбработки = Новый Массив;
	Если ТипЗнч(Реквизиты) = Тип("Массив") Тогда
		МассивОбработки = Реквизиты;
	Иначе
		МассивОбработки.Добавить(Реквизиты);
	КонецЕсли;
	
	Для Каждого ЭлементМассива Из МассивОбработки Цикл
		
		Если ТипЗнч(ЭлементМассива) = Тип("Структура") Тогда
			ПроверкаЗаполненияАналитикТЧОбъекта(Объект,
												ЭлементМассива,
												НепроверяемыеРеквизиты,
												Ошибки,
												ДополнительныеПараметры);
		Иначе
			ЭтоОбъектНаФорме = Ложь;
			Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура")
				И ДополнительныеПараметры.Свойство("ЭтоОбъектНаФорме")Тогда
				ЭтоОбъектНаФорме = ДополнительныеПараметры.ЭтоОбъектНаФорме;
			КонецЕсли;
			ПроверкаЗаполненияАналитикОбъекта(Объект, ЭлементМассива, НепроверяемыеРеквизиты, Ошибки, ЭтоОбъектНаФорме);
		КонецЕсли;
		
	КонецЦикла;
	
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура")
		И ДополнительныеПараметры.Свойство("ПрограммнаяПроверка") Тогда
		ДополнительныеПараметры.Вставить("Ошибки", Ошибки);
	Иначе
		Если Ошибки.СписокОшибок.Количество() <> 0 Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Возвращает статьи, для которых требуется заполнение аналитики.
// 
// Параметры:
//  МассивСтатей - Массив из ПланВидовХарактеристикСсылка.СтатьиРасходов - Массив статей.
// 
// Возвращаемое значение:
//  Массив из ПланВидовХарактеристикСсылка.СтатьиРасходов - Статьи, для которых требуется заполнение аналитики.
Функция СтатьиСОбязательнымЗаполнениемАналитики(МассивСтатей) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Статьи.Ссылка КАК Ссылка
	|ИЗ
	|	ПланВидовХарактеристик.СтатьиРасходов КАК Статьи
	|ГДЕ
	|	Статьи.Ссылка В (&МассивСтатей)
	|	И Статьи.КонтролироватьЗаполнениеАналитики");
	
	Запрос.УстановитьПараметр("МассивСтатей", МассивСтатей);
	Результат = Запрос.Выполнить();

	СписокСтатей = Новый Массив;
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		СписокСтатей.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	Возврат СписокСтатей;
	
КонецФункции

// Возвращает статьи расходов, использование которых запрещено
//
// Возвращаемое значение:
// 	СписокЗначений - Список заблокированных статей расходов.
//
Функция ЗаблокированныеСтатьиРасходов() Экспорт
	
	ЗаблокированныеСтатьи = Новый СписокЗначений;
	ЗаблокированныеСтатьи.Добавить(ПланыВидовХарактеристик.СтатьиРасходов.СебестоимостьПродаж);
	Если ПолучитьФункциональнуюОпцию("БазоваяВерсия") Тогда
		ЗаблокированныеСтатьи.Добавить(ПланыВидовХарактеристик.СтатьиРасходов.КурсовыеРазницы);
		ЗаблокированныеСтатьи.Добавить(ПланыВидовХарактеристик.СтатьиРасходов.НачисленныйНДСПриВыкупеМногооборотнойТары);
		ЗаблокированныеСтатьи.Добавить(ПланыВидовХарактеристик.СтатьиРасходов.ПогрешностьРасчетаСебестоимости);
		ЗаблокированныеСтатьи.Добавить(ПланыВидовХарактеристик.СтатьиРасходов.ПрибыльУбытокПрошлыхЛет);
		ЗаблокированныеСтатьи.Добавить(ПланыВидовХарактеристик.СтатьиРасходов.РазницыСтоимостиВозвратаИФактическойСтоимостиТоваров);
		ЗаблокированныеСтатьи.Добавить(ПланыВидовХарактеристик.СтатьиРасходов.ФормированиеРезервовПоСомнительнымДолгам);
		ЗаблокированныеСтатьи.Добавить(ПланыВидовХарактеристик.СтатьиРасходов.НДСНалоговогоАгента);
	КонецЕсли;
	Если ПолучитьФункциональнуюОпцию("УправлениеТорговлей") Тогда
		ЗаблокированныеСтатьи.Добавить(ПланыВидовХарактеристик.СтатьиРасходов.ДоначислениеИмущественныхНалоговИАмортизации);
		ЗаблокированныеСтатьи.Добавить(ПланыВидовХарактеристик.СтатьиРасходов.СторноПроизводственныхЗатрат);
		ЗаблокированныеСтатьи.Добавить(ПланыВидовХарактеристик.СтатьиРасходов.ПроизводственныеРасходыПрошлыхПериодов);
		ЗаблокированныеСтатьи.Добавить(ПланыВидовХарактеристик.СтатьиРасходов.РасходыПриСписанииОС);
		ЗаблокированныеСтатьи.Добавить(ПланыВидовХарактеристик.СтатьиРасходов.РасходыПриПродажеОС);
	КонецЕсли;
	
	СтатьиРасходовЛокализация.ДополнитьСписокЗаблокированныхСтатей(ЗаблокированныеСтатьи);
	
	Возврат ЗаблокированныеСтатьи;
	
КонецФункции

// Определяет настройки начального заполнения элементов
//
// Параметры:
//  Настройки - Структура - настройки заполнения:
//   * ПриНачальномЗаполненииЭлемента - Булево - Если Истина, то для каждого элемента будет
//      вызвана процедура индивидуального заполнения ПриНачальномЗаполненииЭлемента.
Процедура ПриНастройкеНачальногоЗаполненияЭлементов(Настройки) Экспорт
	
	Настройки.ПриНачальномЗаполненииЭлемента = Истина;
	
КонецПроцедуры

// Вызывается при начальном заполнении справочника.
//
// Параметры:
//  КодыЯзыков - Массив - список языков конфигурации. Актуально для мультиязычных конфигураций.
//  Элементы   - ТаблицаЗначений - данные заполнения. Состав колонок соответствует набору реквизитов
//                                 справочника.
//  ТабличныеЧасти - Структура - Ключ - Имя табличной части объекта.
//                               Значение - Выгрузка в таблицу значений пустой табличной части.
//
Процедура ПриНачальномЗаполненииЭлементов(КодыЯзыков, Элементы, ТабличныеЧасти) Экспорт
	
	#Область ВыбытияТоваровВПрошлыхПериодах
	Элемент = Элементы.Добавить();
	Элемент.ИмяПредопределенныхДанных = "ВыбытияТоваровВПрошлыхПериодах";
	МультиязычностьСервер.ЗаполнитьМультиязычныйРеквизит(Элемент, "Наименование", "ru = 'Выбытия товаров в прошлых периодах';
																					|en = 'Outflows of goods in past periods'", КодыЯзыков); // @НСтр-2
	Элемент.Код = "000000012";
	Элемент.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.Номенклатура");
	Элемент.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности;
	Элемент.ВидДеятельностиРасходов = Перечисления.ВидыДеятельностиРасходов.ОсновнаяДеятельность;
	ДополнитьЭлементСтатьиРасходов(Элемент);
	#КонецОбласти
	
	#Область ДоначислениеИмущественныхНалоговИАмортизации
	Элемент = Элементы.Добавить();
	Элемент.ИмяПредопределенныхДанных = "ДоначислениеИмущественныхНалоговИАмортизации";
	МультиязычностьСервер.ЗаполнитьМультиязычныйРеквизит(Элемент, "Наименование", "ru = 'Доначисление имущественных налогов и амортизации';
																					|en = 'Additional accrual of property taxes and depreciation'", КодыЯзыков); // @НСтр-2
	Элемент.Код = "000000011";
	Элемент.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.ПрочиеРасходы");
	Элемент.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности;
	Элемент.ВидДеятельностиРасходов = Перечисления.ВидыДеятельностиРасходов.ОсновнаяДеятельность;
	ДополнитьЭлементСтатьиРасходов(Элемент);
	#КонецОбласти

	#Область КурсовыеРазницы
	Элемент = Элементы.Добавить();
	Элемент.ИмяПредопределенныхДанных = "КурсовыеРазницы";
	МультиязычностьСервер.ЗаполнитьМультиязычныйРеквизит(Элемент, "Наименование", "ru = 'Курсовые разницы';
																					|en = 'Exchange differences'", КодыЯзыков); // @НСтр-2
	Элемент.Код = "000000003";
	Элемент.ТипЗначения = Новый ОписаниеТипов("ПеречислениеСсылка.АналитикаКурсовыхРазниц");
	Элемент.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности;
	Элемент.ВидДеятельностиРасходов = Перечисления.ВидыДеятельностиРасходов.ПрочаяДеятельность;
	ДополнитьЭлементСтатьиРасходов(Элемент);
	#КонецОбласти

	#Область НачисленныйНДСПриВыкупеМногооборотнойТары
	Элемент = Элементы.Добавить();
	Элемент.ИмяПредопределенныхДанных = "НачисленныйНДСПриВыкупеМногооборотнойТары";
	МультиязычностьСервер.ЗаполнитьМультиязычныйРеквизит(Элемент, "Наименование", "ru = 'Начисленный НДС при выкупе многооборотной тары';
																					|en = 'VAT accrued at the redemption of reusable packaging'", КодыЯзыков); // @НСтр-2
	Элемент.Код = "000000005";
	Элемент.ТипРасходов = Перечисления.ТипыРасходов.ПродажаТоваров;
	Элемент.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.Партнеры");
	Элемент.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности;
	Элемент.ВидДеятельностиРасходов = Перечисления.ВидыДеятельностиРасходов.ОсновнаяДеятельность;
	ДополнитьЭлементСтатьиРасходов(Элемент);	
	#КонецОбласти

	#Область НДСНалоговогоАгента
	Элемент = Элементы.Добавить();
	Элемент.ИмяПредопределенныхДанных = "НДСНалоговогоАгента";
	МультиязычностьСервер.ЗаполнитьМультиязычныйРеквизит(Элемент, "Наименование", "ru = 'НДС Налогового агента';
																					|en = 'Tax agent VAT'", КодыЯзыков); // @НСтр-2
	Элемент.Код = "000000009";
	Элемент.ТипРасходов = Перечисления.ТипыРасходов.ЗакупкаТоваров;
	Элемент.ТипЗначения = Новый ОписаниеТипов("ДокументСсылка.ПриобретениеТоваровУслуг");
	Элемент.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров;
	Элемент.ПравилоРаспределенияНаСебестоимость = Перечисления.ПравилаРаспределенияНаСебестоимостьТоваров.ПропорциональноСебестоимости;
	Элемент.ВидДеятельностиРасходов = Перечисления.ВидыДеятельностиРасходов.ОсновнаяДеятельность;
	ДополнитьЭлементСтатьиРасходов(Элемент);	
	#КонецОбласти

	#Область ПогрешностьРасчетаСебестоимости
	Элемент = Элементы.Добавить();
	Элемент.ИмяПредопределенныхДанных = "ПогрешностьРасчетаСебестоимости";
	МультиязычностьСервер.ЗаполнитьМультиязычныйРеквизит(Элемент, "Наименование", "ru = 'Погрешность расчета себестоимости';
																					|en = 'Cost calculation error'", КодыЯзыков); // @НСтр-2
	Элемент.Код = "000000002";
	Элемент.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.НаправленияДеятельности");
	Элемент.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности;
	Элемент.ВидДеятельностиРасходов = Перечисления.ВидыДеятельностиРасходов.ОсновнаяДеятельность;
	ДополнитьЭлементСтатьиРасходов(Элемент);	
	#КонецОбласти

	#Область ПрибыльУбытокПрошлыхЛет
	Элемент = Элементы.Добавить();
	Элемент.ИмяПредопределенныхДанных = "ПрибыльУбытокПрошлыхЛет";
	МультиязычностьСервер.ЗаполнитьМультиязычныйРеквизит(Элемент, "Наименование", "ru = 'Прибыль (убыток) прошлых лет';
																					|en = 'Previous years profit (loss)'", КодыЯзыков); // @НСтр-2
	Элемент.Код = "000000007";
	Элемент.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.НаправленияДеятельности");
	Элемент.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности;
	Элемент.ВидДеятельностиРасходов = Перечисления.ВидыДеятельностиРасходов.ПрочаяДеятельность;
	ДополнитьЭлементСтатьиРасходов(Элемент);	
	#КонецОбласти

	#Область РазницыСтоимостиВозвратаИФактическойСтоимостиТоваров
	Элемент = Элементы.Добавить();
	Элемент.ИмяПредопределенныхДанных = "РазницыСтоимостиВозвратаИФактическойСтоимостиТоваров";
	МультиязычностьСервер.ЗаполнитьМультиязычныйРеквизит(Элемент, "Наименование", "ru = 'Разницы стоимости возврата и фактической стоимости товаров';
																					|en = 'Differences in the cost of return and the actual cost of goods'", КодыЯзыков); // @НСтр-2
	Элемент.Код = "000000004";
	Элемент.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.НаправленияДеятельности");
	Элемент.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности;
	Элемент.ВидДеятельностиРасходов = Перечисления.ВидыДеятельностиРасходов.ОсновнаяДеятельность;
	ДополнитьЭлементСтатьиРасходов(Элемент);	
	#КонецОбласти

	#Область РасходыЛизинговыеПлатежи
	Элемент = Элементы.Добавить();
	Элемент.ИмяПредопределенныхДанных = "РасходыЛизинговыеПлатежи";
	МультиязычностьСервер.ЗаполнитьМультиязычныйРеквизит(Элемент, "Наименование", "ru = 'Расходы по лизинговым платежам за вычетом сумм амортизации';
																					|en = 'Financial lease payment costs, net of depreciation'", КодыЯзыков); // @НСтр-2
	Элемент.Код = "000000015";
	Элемент.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.НаправленияДеятельности");
	Элемент.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности;
	Элемент.ВидДеятельностиРасходов = Перечисления.ВидыДеятельностиРасходов.ОсновнаяДеятельность;
	ДополнитьЭлементСтатьиРасходов(Элемент);	
	#КонецОбласти

	#Область РасходыНаПлатон
	Элемент = Элементы.Добавить();
	Элемент.ИмяПредопределенныхДанных = "РасходыНаПлатон";
	МультиязычностьСервер.ЗаполнитьМультиязычныйРеквизит(Элемент, "Наименование", "ru = 'Расходы на ""Платон""';
																					|en = 'Expenses for Platon'", КодыЯзыков); // @НСтр-2
	Элемент.Код = "000000013";
	Элемент.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.НаправленияДеятельности");
	Элемент.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности;
	Элемент.ВидДеятельностиРасходов = Перечисления.ВидыДеятельностиРасходов.ОсновнаяДеятельность;
	ДополнитьЭлементСтатьиРасходов(Элемент);	
	#КонецОбласти

	#Область РасходыПриКонвертацииВалюты
	Элемент = Элементы.Добавить();
	Элемент.ИмяПредопределенныхДанных = "РасходыПриКонвертацииВалюты";
	МультиязычностьСервер.ЗаполнитьМультиязычныйРеквизит(Элемент, "Наименование", "ru = 'Расходы при конвертации валюты';
																					|en = 'Currency conversion costs'", КодыЯзыков); // @НСтр-2
	Элемент.Код = "000000014";
	Элемент.ТипЗначения = Новый ОписаниеТипов("ПеречислениеСсылка.АналитикаКурсовыхРазниц");
	Элемент.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности;
	Элемент.ВидДеятельностиРасходов = Перечисления.ВидыДеятельностиРасходов.ПрочаяДеятельность;
	ДополнитьЭлементСтатьиРасходов(Элемент);	
	#КонецОбласти

	#Область СебестоимостьПродаж
	Элемент = Элементы.Добавить();
	Элемент.ИмяПредопределенныхДанных = "СебестоимостьПродаж";
	МультиязычностьСервер.ЗаполнитьМультиязычныйРеквизит(Элемент, "Наименование", "ru = 'Себестоимость продаж';
																					|en = 'COGS'", КодыЯзыков); // @НСтр-2
	Элемент.Код = "000000001";
	Элемент.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.НаправленияДеятельности");
	Элемент.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности;
	Элемент.ВидДеятельностиРасходов = Перечисления.ВидыДеятельностиРасходов.ОсновнаяДеятельность;
	ДополнитьЭлементСтатьиРасходов(Элемент);	
	#КонецОбласти

	#Область ТорговыйСбор
	Элемент = Элементы.Добавить();
	Элемент.ИмяПредопределенныхДанных = "ТорговыйСбор";
	МультиязычностьСервер.ЗаполнитьМультиязычныйРеквизит(Элемент, "Наименование", "ru = 'Торговый сбор';
																					|en = 'Sales charge'", КодыЯзыков); // @НСтр-2
	Элемент.Код = "000000010";
	Элемент.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.Склады");
	Элемент.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности;
	Элемент.ВидДеятельностиРасходов = Перечисления.ВидыДеятельностиРасходов.ОсновнаяДеятельность;
	ДополнитьЭлементСтатьиРасходов(Элемент);	
	#КонецОбласти

	#Область ФормированиеРезервовПоСомнительнымДолгам
	Элемент = Элементы.Добавить();
	Элемент.ИмяПредопределенныхДанных = "ФормированиеРезервовПоСомнительнымДолгам";
	МультиязычностьСервер.ЗаполнитьМультиязычныйРеквизит(Элемент, "Наименование", "ru = 'Формирование резервов по сомнительным долгам';
																					|en = 'Generate loss allowance'", КодыЯзыков); // @НСтр-2
	Элемент.Код = "000000008";
	Элемент.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.Организации");
	Элемент.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности;
	Элемент.ВидДеятельностиРасходов = Перечисления.ВидыДеятельностиРасходов.ОсновнаяДеятельность;
	ДополнитьЭлементСтатьиРасходов(Элемент);	
	#КонецОбласти

	#Область ОтклонениеВСтоимостиТоваров
	Элемент = Элементы.Добавить();
	Элемент.ИмяПредопределенныхДанных = "ОтклонениеВСтоимостиТоваров";
	МультиязычностьСервер.ЗаполнитьМультиязычныйРеквизит(Элемент, "Наименование", "ru = 'Отклонение в стоимости товаров';
																					|en = 'Goods value variance'", КодыЯзыков); // @НСтр-2
	Элемент.Код = "000000016";
	Элемент.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.НаправленияДеятельности");
	Элемент.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности;
	Элемент.ВидДеятельностиРасходов = Перечисления.ВидыДеятельностиРасходов.ОсновнаяДеятельность;
	ДополнитьЭлементСтатьиРасходов(Элемент);	
	#КонецОбласти
	
	#Область СторноПроизводственныхЗатрат
	Элемент = Элементы.Добавить();
	Элемент.ИмяПредопределенныхДанных = "СторноПроизводственныхЗатрат";
	МультиязычностьСервер.ЗаполнитьМультиязычныйРеквизит(Элемент, "Наименование", "ru = 'Сторно производственных затрат';
																					|en = 'Production costs storno'", КодыЯзыков); // @НСтр-2
	Элемент.Код = "000000017";
	//++ НЕ УТ	
	Элемент.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.Номенклатура");
	Элемент.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты;
	Элемент.ВидДеятельностиРасходов = Перечисления.ВидыДеятельностиРасходов.ОсновнаяДеятельность;
	Элемент.ХарактерПроизводственныхЗатрат = Перечисления.ХарактерПроизводственныхЗатрат.Переменные;
	ДополнитьЭлементСтатьиРасходов(Элемент);
	//-- НЕ УТ
	#КонецОбласти
	
	#Область ПроизводственныеРасходыПрошлыхПериодов
	Элемент = Элементы.Добавить();
	Элемент.ИмяПредопределенныхДанных = "ПроизводственныеРасходыПрошлыхПериодов";
	МультиязычностьСервер.ЗаполнитьМультиязычныйРеквизит(Элемент, "Наименование", "ru = 'Производственные расходы прошлых периодов';
																					|en = 'Production costs in past periods'", КодыЯзыков); // @НСтр-2
	Элемент.Код = "000000018";
	//++ НЕ УТ
	Элемент.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.Номенклатура");
	Элемент.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности;
	Элемент.ВидДеятельностиРасходов = Перечисления.ВидыДеятельностиРасходов.ОсновнаяДеятельность;
	Элемент.ХарактерПроизводственныхЗатрат = Перечисления.ХарактерПроизводственныхЗатрат.Переменные;
	ДополнитьЭлементСтатьиРасходов(Элемент);	
	//-- НЕ УТ
	#КонецОбласти
	
	#Область РасходыПриСписанииОС
	Элемент = Элементы.Добавить();
	Элемент.ИмяПредопределенныхДанных = "РасходыПриСписанииОС";
	МультиязычностьСервер.ЗаполнитьМультиязычныйРеквизит(Элемент, "Наименование", "ru = 'Расходы, связанные со списанием ОС';
																					|en = 'Fixed asset disposal expenses'", КодыЯзыков); // @НСтр-2
	Элемент.Код = "000000019";
	//++ НЕ УТ
	Элемент.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.ОбъектыЭксплуатации");
	Элемент.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НеРаспределять;
	Элемент.ВидДеятельностиРасходов = Перечисления.ВидыДеятельностиРасходов.ОсновнаяДеятельность;
	ДополнитьЭлементСтатьиРасходов(Элемент);	
	//-- НЕ УТ
	#КонецОбласти

	#Область РасходыПриПродажеОС
	Элемент = Элементы.Добавить();
	Элемент.ИмяПредопределенныхДанных = "РасходыПриПродажеОС";
	МультиязычностьСервер.ЗаполнитьМультиязычныйРеквизит(Элемент, "Наименование", "ru = 'Расходы, связанные с продажей ОС';
																					|en = 'Fixed asset sale expenses'", КодыЯзыков); // @НСтр-2
	Элемент.Код = "000000020";
	//++ НЕ УТ
	Элемент.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.ОбъектыЭксплуатации");
	Элемент.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НеРаспределять;
	Элемент.ВидДеятельностиРасходов = Перечисления.ВидыДеятельностиРасходов.ОсновнаяДеятельность;
	ДополнитьЭлементСтатьиРасходов(Элемент);	
	//-- НЕ УТ
	#КонецОбласти
	
КонецПроцедуры

// Вызывается при начальном заполнении создаваемого элемента.
//
// Параметры:
//  Объект                  - ПланВидовХарактеристикОбъект.СтатьиРасходов - заполняемый объект.
//  Данные                  - СтрокаТаблицыЗначений - данные заполнения.
//  ДополнительныеПараметры - Структура - Дополнительные параметры.
//
Процедура ПриНачальномЗаполненииЭлемента(Объект, Данные, ДополнительныеПараметры) Экспорт
	
	
	
КонецПроцедуры

// Возвращает соответствие статей, для которых нельзя изменить вариант распределения, значению варианта распределения.
// 
// Возвращаемое значение:
//	Соответствие - значения для статей
//		Ключ - ПланВидовХарактеристикСсылка.СтатьиРасходов - статья, для которой используется неизменяемый вариант распределния;
//		Значение - ПеречислениеСсылка.ВариантыРаспределенияРасходов - вариант распределения который должен использоваться для статьи.
//
Функция СоответствиеСтатейВариантамРаспределения() Экспорт
	
	СоответствиеСтатейВариантамРаспределения = Новый Соответствие;
	
	СоответствиеСтатейВариантамРаспределения.Вставить(ПланыВидовХарактеристик.СтатьиРасходов.КурсовыеРазницы, Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности);
	СоответствиеСтатейВариантамРаспределения.Вставить(ПланыВидовХарактеристик.СтатьиРасходов.НачисленныйНДСПриВыкупеМногооборотнойТары, Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности);
	СоответствиеСтатейВариантамРаспределения.Вставить(ПланыВидовХарактеристик.СтатьиРасходов.ПогрешностьРасчетаСебестоимости, Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности);
	СоответствиеСтатейВариантамРаспределения.Вставить(ПланыВидовХарактеристик.СтатьиРасходов.ПрибыльУбытокПрошлыхЛет, Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности);
	СоответствиеСтатейВариантамРаспределения.Вставить(ПланыВидовХарактеристик.СтатьиРасходов.РазницыСтоимостиВозвратаИФактическойСтоимостиТоваров, Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности);
	СоответствиеСтатейВариантамРаспределения.Вставить(ПланыВидовХарактеристик.СтатьиРасходов.СебестоимостьПродаж, Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности);
	СоответствиеСтатейВариантамРаспределения.Вставить(ПланыВидовХарактеристик.СтатьиРасходов.ФормированиеРезервовПоСомнительнымДолгам, Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности);
	
	СоответствиеСтатейВариантамРаспределения.Вставить(ПланыВидовХарактеристик.СтатьиРасходов.НДСНалоговогоАгента, Перечисления.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров);
	
	//++ НЕ УТ
	СоответствиеСтатейВариантамРаспределения.Вставить(ПланыВидовХарактеристик.СтатьиРасходов.РасходыПриСписанииОС, Перечисления.ВариантыРаспределенияРасходов.НеРаспределять);
	СоответствиеСтатейВариантамРаспределения.Вставить(ПланыВидовХарактеристик.СтатьиРасходов.РасходыПриПродажеОС, Перечисления.ВариантыРаспределенияРасходов.НеРаспределять);
	//-- НЕ УТ
	
	Возврат СоответствиеСтатейВариантамРаспределения;
	
КонецФункции

// Возвращает соответствие статей, для которых нельзя изменить тип расходов, соответствующему значению.
// 
// Возвращаемое значение:
//	Соответствие:
//		Ключ - ПланВидовХарактеристикСсылка.СтатьиРасходов - статья, для которой используется неизменяемый тип расходов;
//		Значение - ПеречислениеСсылка.ТипыРасходов - тип расхода который должен использоваться для статьи.
//
Функция СоответствиеСтатейТипамРасходов() Экспорт
	
	СоответствиеСтатейТипамРасходов = Новый Соответствие;
	
	//++ НЕ УТ
	СоответствиеСтатейТипамРасходов.Вставить(ПланыВидовХарактеристик.СтатьиРасходов.РасходыПриСписанииОС, Перечисления.ТипыРасходов.ПрочиеРасходы);
	СоответствиеСтатейТипамРасходов.Вставить(ПланыВидовХарактеристик.СтатьиРасходов.РасходыПриПродажеОС, Перечисления.ТипыРасходов.ПрочиеРасходы);
	//-- НЕ УТ
	
	Возврат СоответствиеСтатейТипамРасходов;
	
КонецФункции

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

//++ НЕ УТ

// Заполняет отбор в параметрах при обработке получения данных выбора и открытии формы выбора.
//
// Параметры:
// 		Параметры - Структура - Структура параметров в формате параметров формы выбора или процедуры "ОбработкаПолученияДанныхВыбора".
//
Процедура ОбработкаПараметровВыбора(Параметры) Экспорт
	
	ВнеоборотныеАктивы.ОбработкаПараметровВыбораСтатьиРасходов(Параметры);

	ТМЦВЭксплуатацииСервер.ОбработкаПараметровВыбораСтатьиРасходов(Параметры);
	
КонецПроцедуры

// Заполняет реквизиты параметров настройки счетов учета расходов, которые влияют на настройку,
// 	соответствующими им именам реквизитов аналитики учета.
//
// Параметры:
// 	СоответствиеИмен - Соответствие - ключом выступает имя реквизита, используемое в настройке счетов учета,
// 		значением является соответствующее имя реквизита аналитики учета.
// 
Процедура ЗаполнитьСоответствиеРеквизитовНастройкиСчетовУчета(СоответствиеИмен) Экспорт
	
	СоответствиеИмен.ВидДеятельностиРасходов = "ВидДеятельностиРасходов";
	СоответствиеИмен.ВариантРаспределенияРасходовРегл = "ВариантРаспределенияРасходовРегл";
	СоответствиеИмен.РасходыНаОбъектыЭксплуатации = "РасходыНаОбъектыЭксплуатации";
	СоответствиеИмен.РасходыНаОбъектыСтроительства = "РасходыНаОбъектыСтроительства";
	СоответствиеИмен.РасходыНаНМАиНИОКР = "РасходыНаНМАиНИОКР";
	СтатьиРасходовЛокализация.ЗаполнитьСоответствиеРеквизитовНастройкиСчетовУчета(СоответствиеИмен);
	
КонецПроцедуры

//-- НЕ УТ

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	ДоходыИРасходыСервер.ОбработкаПолученияДанныхВыбораПВХСтатьиРасходов(ДанныеВыбора, Параметры, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	// СтандартныеПодсистемы.БазоваяФункциональность
	МультиязычностьКлиентСервер.ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка);
	// Конец СтандартныеПодсистемы.БазоваяФункциональность
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	// СтандартныеПодсистемы.БазоваяФункциональность
	МультиязычностьКлиентСервер.ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка);
	// Конец СтандартныеПодсистемы.БазоваяФункциональность
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СлужебныеПроцедурыИФункцииЗаполненияАналитик

Функция ИменаРеквизитовСтатьиИАналитики(СтрокаРеквизитов, НепроверяемыеРеквизиты = Неопределено, ПрефиксТабличнойЧасти = "")
	
	Если ПустаяСтрока(СтрокаРеквизитов) Тогда
		Если НепроверяемыеРеквизиты <> Неопределено Тогда
			НепроверяемыеРеквизиты.Добавить(ПрефиксТабличнойЧасти + "АналитикаРасходов");
		КонецЕсли;
		Возврат Новый Структура("СтатьяРасходов", "АналитикаРасходов");
	КонецЕсли;
	
	СтруктураОбработки = Новый Структура(СтрокаРеквизитов);
	СтруктураВозврата = Новый Структура;
	ПредыдущийКлюч = Неопределено;
	Для Каждого КлючИЗначение Из СтруктураОбработки Цикл
		Если ПредыдущийКлюч = Неопределено Тогда
			ПредыдущийКлюч = КлючИЗначение.Ключ;
		Иначе
			СтруктураВозврата.Вставить(ПредыдущийКлюч, КлючИЗначение.Ключ);
			ПредыдущийКлюч = Неопределено;
			Если НепроверяемыеРеквизиты <> Неопределено Тогда
				НепроверяемыеРеквизиты.Добавить(ПрефиксТабличнойЧасти + КлючИЗначение.Ключ);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат СтруктураВозврата;
	
КонецФункции

Процедура ПроверкаЗаполненияАналитикОбъекта(Объект, Реквизиты, НепроверяемыеРеквизиты, Ошибки, ЭтоОбъектНаФорме)
	
	ДанныеОбъекта		= Объект;
	МассивСтатей		= Новый Массив;
	СтруктураРеквизитов	= ИменаРеквизитовСтатьиИАналитики(Реквизиты, НепроверяемыеРеквизиты);
	
	Если ЭтоОбъектНаФорме И ТипЗнч(Объект) = Тип("Структура")
		И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Объект, "Объект") Тогда
		
		ДанныеОбъекта = Объект.Объект;
		
	КонецЕсли;
	
	// Определим список статей для контроля
	Для Каждого КлючИЗначение Из СтруктураРеквизитов Цикл
		Статья = ДанныеОбъекта[КлючИЗначение.Ключ];
		
		Если ЗначениеЗаполнено(Статья) Тогда
			МассивСтатей.Добавить(Статья);
		КонецЕсли;
	КонецЦикла;
	
	// Проверим заполнение аналитики
	ОбязательныеСтатьи = СтатьиСОбязательнымЗаполнениемАналитики(МассивСтатей);
	Для Каждого КлючИЗначение Из СтруктураРеквизитов Цикл
		Статья		= ДанныеОбъекта[КлючИЗначение.Ключ];
		Аналитика	= ДанныеОбъекта[КлючИЗначение.Значение];
		
		Если Не (ОбязательныеСтатьи.Найти(Статья) = Неопределено
			Или ЗначениеЗаполнено(Аналитика)) Тогда
			
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(
				Ошибки,
				Ошибки.ПрефиксОбъекта + КлючИЗначение.Значение,
				НСтр("ru = 'Аналитика расходов не заполнена';
					|en = 'Expense dimension is required'"), "");
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверкаЗаполненияАналитикТЧОбъекта(Объект,
												Реквизиты,
												НепроверяемыеРеквизиты,
												Ошибки,
												ДополнительныеПараметры = Неопределено)
	
	// Определим список статей для контроля
	ОбщийМассивСтатей = Новый Массив;
	
	Для Каждого ОписаниеТЧ Из Реквизиты Цикл
		ИмяТЧ = ОписаниеТЧ.Ключ;
		
		СтруктураРеквизитов = ИменаРеквизитовСтатьиИАналитики(ОписаниеТЧ.Значение, НепроверяемыеРеквизиты, ИмяТЧ + ".");
		
		Для Каждого КлючИЗначение Из СтруктураРеквизитов Цикл
			МассивСтатей = Объект[ИмяТЧ].ВыгрузитьКолонку(КлючИЗначение.Ключ);
			
			
			Для Каждого Статья Из МассивСтатей Цикл
				ОбщийМассивСтатей.Добавить(Статья);
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	// Проверим заполнение аналитики
	ОбязательныеСтатьи = СтатьиСОбязательнымЗаполнениемАналитики(ОбщийМассивСтатей);
	
	Для Каждого ОписаниеТЧ Из Реквизиты Цикл // Табличные части
		ИмяТЧ	= ОписаниеТЧ.Ключ;
		ТЧ		= Объект[ИмяТЧ];
		
		Если ТипЗнч(ТЧ) = Тип("ТаблицаЗначений")
			И ТипЗнч(ДополнительныеПараметры) = Тип("Структура") Тогда
			
			ТабличнаяЧасть = Неопределено;
			
			ПредставлениеТЧ			= ДополнительныеПараметры.ПредставлениеТЧ;
			ПредставленияРеквизитов	= ДополнительныеПараметры.ПредставленияРеквизитов;
			
		Иначе
			МетаданныеОбъекта	= Объект.Метаданные();
			ТабличнаяЧасть		= МетаданныеОбъекта.ТабличныеЧасти.Найти(ИмяТЧ);
			
			ПредставлениеТЧ			= ТабличнаяЧасть.Синоним;
			ПредставленияРеквизитов	= Неопределено;
		КонецЕсли;
		
		СтруктураРеквизитов = ИменаРеквизитовСтатьиИАналитики(ОписаниеТЧ.Значение, Неопределено, ИмяТЧ + ".");
		
		Для Индекс = 0 По ТЧ.Количество() - 1 Цикл // Строки табличной части
			СтрокаТЧ = ТЧ[Индекс];
			
			Для Каждого КлючИЗначение Из СтруктураРеквизитов Цикл
				Если ТабличнаяЧасть = Неопределено Тогда
					ПредставлениеРеквизита = ПредставленияРеквизитов[КлючИЗначение.Значение];
				Иначе
					ПредставлениеРеквизита = ТабличнаяЧасть.Реквизиты.Найти(КлючИЗначение.Значение).Синоним;
				КонецЕсли;
				
				Статья		= СтрокаТЧ[КлючИЗначение.Ключ];
				Аналитика	= СтрокаТЧ[КлючИЗначение.Значение];
				
				Если Не (ОбязательныеСтатьи.Найти(Статья) = Неопределено
					Или ЗначениеЗаполнено(Аналитика)) Тогда
					
					ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(
						Ошибки,
						Ошибки.ПрефиксОбъекта + ИмяТЧ + "[%1]." + КлючИЗначение.Значение,
						СтрШаблон(НСтр("ru = 'Не заполнено поле ""%1"" в строке %2 списка ""%3""';
										|en = '""%1"" in line %2 of list ""%3"" is required'"),
							ПредставлениеРеквизита, СтрокаТЧ.НомерСтроки, ПредставлениеТЧ),
						ИмяТЧ,
						Индекс,
						СтрШаблон(НСтр("ru = 'Не заполнено поле ""%1"" в строке %2 списка ""%3""';
										|en = '""%1"" in line %2 of list ""%3"" is required'"),
							ПредставлениеРеквизита, СтрокаТЧ.НомерСтроки, ПредставлениеТЧ));
					
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Процедура ДополнитьЭлементСтатьиРасходов(Элемент)
	
	Если Не ЗначениеЗаполнено(Элемент.ТипРасходов) Тогда
		
		Элемент.ТипРасходов = Перечисления.ТипыРасходов.ПрочиеРасходы;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Элемент.ВариантРаспределенияРасходовУпр) Тогда
		
		Элемент.ВариантРаспределенияРасходовУпр = Элемент.ВариантРаспределенияРасходовРегл;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Элемент.ВариантРаспределенияРасходовНУ) Тогда
		
		Элемент.ВариантРаспределенияРасходовНУ = Элемент.ВариантРаспределенияРасходовРегл;
		
	КонецЕсли;
	
	// ВариантРаздельногоУчетаНДС
	Элемент.ВариантРаздельногоУчетаНДС = Перечисления.ВариантыРаздельногоУчетаНДС.ИзДокумента;
	
	Элемент.ПринятиеКНалоговомуУчету = Истина;
	
	СтатьиРасходовЛокализация.ПриНачальномЗаполненииЭлемента(Элемент);
	
	УказатьГФУПредопределеннойСтатьи(Элемент);
	
КонецПроцедуры

Процедура УказатьГФУПредопределеннойСтатьи(СтатьяРасходов)
	
	УказаннаяСтатья = Неопределено;
	
	Если Не СтатьяРасходов.ГруппаФинансовогоУчета.Пустая() Тогда
		УказаннаяСтатья = СтатьяРасходов.ГруппаФинансовогоУчета;
	КонецЕсли;
	
	Если Не СтатьяРасходов.ГруппаФинансовогоУчетаРегл.Пустая() Тогда
		УказаннаяСтатья = СтатьяРасходов.ГруппаФинансовогоУчетаРегл;
	КонецЕсли;
	
	//++ Локализация
	Если Не СтатьяРасходов.ГруппаФинансовогоУчетаНУ.Пустая() И УказаннаяСтатья = Неопределено Тогда
		УказаннаяСтатья = СтатьяРасходов.ГруппаФинансовогоУчетаНУ;
	КонецЕсли;
	//-- Локализация
	
	Если УказаннаяСтатья = Неопределено Тогда
		// Создадим ГФУ доходов/расходов:
		
		ШаблонНаименования = НСтр("ru = 'Настройки счетов для статьи расходов ""%1""';
									|en = 'Set up accounts for expense item ""%1""'");
		НаименованиеГФУ = СтрШаблон(ШаблонНаименования, СтатьяРасходов.Наименование);
		
		ГФУ = Справочники.ГруппыФинансовогоУчетаДоходовРасходов.СоздатьЭлемент();
		
		ГФУСсылка = Справочники.ГруппыФинансовогоУчетаДоходовРасходов.ПолучитьСсылку();
		ГФУ.УстановитьСсылкуНового(ГФУСсылка);
		
		ГФУ.Наименование = НаименованиеГФУ;
		ГФУ.Расходы = Истина;
		ГФУ.Доходы = Ложь;
		
		ШаблонОписания = НСтр("ru = 'Служебная группа настроек финансового учета доходов/расходов для счетов учета предопределенной статьи расходов ""%1""';
								|en = 'Internal income/expense accounting groups for ledger accounts of predefined expense item ""%1""'");
		ГФУ.Описание = СтрШаблон(ШаблонОписания, СтатьяРасходов.Наименование);
		
		ГФУ.ОбменДанными.Загрузка = Истина;
		ГФУ.Записать();
		
		//++ Локализация
		СтатьяРасходов.ГруппаФинансовогоУчета = ГФУСсылка;
		СтатьяРасходов.ГруппаФинансовогоУчетаРегл = ГФУСсылка;
		СтатьяРасходов.ГруппаФинансовогоУчетаНУ = ГФУСсылка;
		//-- Локализация
		
	Иначе
		// Заполним незаполненные ГФУ по уже указанной:
		
		Если СтатьяРасходов.ГруппаФинансовогоУчета.Пустая() Тогда
			СтатьяРасходов.ГруппаФинансовогоУчета = УказаннаяСтатья;
		КонецЕсли;
		
		Если СтатьяРасходов.ГруппаФинансовогоУчетаРегл.Пустая() Тогда
			СтатьяРасходов.ГруппаФинансовогоУчетаРегл = УказаннаяСтатья;
		КонецЕсли;
		
		//++ Локализация
		Если СтатьяРасходов.ГруппаФинансовогоУчетаНУ.Пустая() Тогда
			СтатьяРасходов.ГруппаФинансовогоУчетаНУ = УказаннаяСтатья;
		КонецЕсли;
		//-- Локализация
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
