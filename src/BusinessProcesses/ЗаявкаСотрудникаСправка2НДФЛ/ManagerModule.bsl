#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает структуру для инициализации бизнес-процесса.
//	Возвращаемое значение:
//		Структура - структура используемая при создании заявки сотрудника.
Функция СтруктураИнициализацииЗаявки() Экспорт
	
	СтруктураИнициализации = БизнесПроцессыЗаявокСотрудников.СтруктураИнициализацииЗаявки();
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(СтруктураИнициализации, БизнесПроцессыЗаявокСотрудников.СтруктураСпособаПолученияОтвета());

	СтруктураИнициализации.Вставить("Организация",				Справочники.Организации.ПустаяСсылка());
	СтруктураИнициализации.Вставить("КомментарийСотрудника",	"");
	СтруктураИнициализации.Вставить("СпособФормирования",		Перечисления.ПорядокФормированияСправкиОДоходахФизическогоЛица.ПустаяСсылка());
	СтруктураИнициализации.Вставить("НалоговыйПериод",			0);
	СтруктураИнициализации.Вставить("НачалоПериода",			Дата(1, 1, 1, 0, 0, 0));
	СтруктураИнициализации.Вставить("ОкончаниеПериода",			Дата(1, 1, 1, 0, 0, 0));
	
	Возврат СтруктураИнициализации
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ГрупповоеИзменениеОбъектов

// Возвращает реквизиты объекта, которые разрешается редактировать
// с помощью обработки группового изменения реквизитов.
//
// Возвращаемое значение:
//  Массив Из Строка -
Функция РеквизитыРедактируемыеВГрупповойОбработке() Экспорт
	Результат = Новый Массив;
	Результат.Добавить("Автор");
	Результат.Добавить("Важность");
	Результат.Добавить("Исполнитель");
	Результат.Добавить("ПроверитьВыполнение");
	Результат.Добавить("Проверяющий");
	Результат.Добавить("СрокИсполнения");
	Результат.Добавить("СрокПроверки");
	Возврат Результат;	
КонецФункции

// Конец СтандартныеПодсистемы.ГрупповоеИзменениеОбъектов

// СтандартныеПодсистемы.БизнесПроцессыИЗадачи

// Получить структуру с описанием формы выполнения задачи.
// Вызывается при открытии формы выполнения задачи.
//
// Параметры:
//   ЗадачаСсылка                - ЗадачаСсылка.ЗадачаИсполнителя - задача.
//   ТочкаМаршрутаБизнесПроцесса - ТочкаМаршрутаБизнесПроцессаСсылка - точка маршрута.
//
// Возвращаемое значение:
//   Структура   - структуру с описанием формы выполнения задачи.
//                 Ключ "ИмяФормы" содержит имя формы, передаваемое в метод контекста ОткрытьФорму(). 
//                 Ключ "ПараметрыФормы" содержит параметры формы. 
//
Функция ФормаВыполненияЗадачи(ЗадачаСсылка, ТочкаМаршрутаБизнесПроцесса) Экспорт
	Результат = Новый Структура;
	Результат.Вставить("ПараметрыФормы", Новый Структура("Ключ", ЗадачаСсылка));
	Результат.Вставить("ИмяФормы", "БизнесПроцесс.ЗаявкаСотрудникаСправка2НДФЛ.Форма.ДействиеВыполнить");
	Возврат Результат;	
КонецФункции

// Вызывается при перенаправлении задачи.
//
// Параметры:
//   ЗадачаСсылка  - ЗадачаСсылка.ЗадачаИсполнителя - перенаправляемая задача.
//   НоваяЗадачаСсылка  - ЗадачаСсылка.ЗадачаИсполнителя - задача для нового исполнителя.
//
Процедура ПриПеренаправленииЗадачи(ЗадачаСсылка, НоваяЗадачаСсылка) Экспорт
	БизнесПроцессыЗаявокСотрудников.ПриПеренаправленииЗадачи(ЗадачаСсылка, НоваяЗадачаСсылка);
КонецПроцедуры

// Вызывается при выполнении задачи из формы списка.
//
// Параметры:
//   ЗадачаСсылка  - ЗадачаСсылка.ЗадачаИсполнителя - задача.
//   БизнесПроцессСсылка - БизнесПроцессСсылка - бизнес-процесс, по которому сформирована задача ЗадачаСсылка.
//   ТочкаМаршрутаБизнесПроцесса - ТочкаМаршрутаБизнесПроцессаСсылка - точка маршрута.
//
Процедура ОбработкаВыполненияПоУмолчанию(ЗадачаСсылка, БизнесПроцессСсылка, ТочкаМаршрутаБизнесПроцесса) Экспорт
	БизнесПроцессыЗаявокСотрудников.ОбработкаВыполненияПоУмолчанию(ЗадачаСсылка,
																   БизнесПроцессСсылка,
																   ТочкаМаршрутаБизнесПроцесса);	
КонецПроцедуры	

// Конец СтандартныеПодсистемы.БизнесПроцессыИЗадачи

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"ПрисоединитьДополнительныеТаблицы
	|ЭтотСписок КАК Задание
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИсполнителиЗадач КАК ИсполнителиЗадач
	|ПО
	|	ИсполнителиЗадач.РольИсполнителя = Задание.Исполнитель
	|	И ИсполнителиЗадач.ОсновнойОбъектАдресации = Задание.ОсновнойОбъектАдресации
	|	И ИсполнителиЗадач.ДополнительныйОбъектАдресации = Задание.ДополнительныйОбъектАдресации
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИсполнителиЗадач КАК ПроверяющиеЗадач
	|ПО
	|	ПроверяющиеЗадач.РольИсполнителя = Задание.Проверяющий
	|	И ПроверяющиеЗадач.ОсновнойОбъектАдресации = Задание.ОсновнойОбъектАдресацииПроверяющий
	|	И ПроверяющиеЗадач.ДополнительныйОбъектАдресации = Задание.ДополнительныйОбъектАдресацииПроверяющий
	|;
	|РазрешитьЧтение
	|ГДЕ
	|	ЗначениеРазрешено(Автор)
	|	ИЛИ ЗначениеРазрешено(Исполнитель КРОМЕ Справочник.РолиИсполнителей)
	|	ИЛИ ЗначениеРазрешено(ИсполнителиЗадач.Исполнитель)
	|	ИЛИ ЗначениеРазрешено(Проверяющий КРОМЕ Справочник.РолиИсполнителей)
	|	ИЛИ ЗначениеРазрешено(ПроверяющиеЗадач.Исполнитель)
	|;
	|РазрешитьИзменениеЕслиРазрешеноЧтение
	|ГДЕ
	|	ЗначениеРазрешено(Автор)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// СтандартныеПодсистемы.ПодключаемыеКоманды

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
КонецПроцедуры

// Для использования в процедуре ДобавитьКомандыСозданияНаОсновании других модулей менеджеров объектов.
// Добавляет в список команд создания на основании этот объект.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
// Возвращаемое значение:
//  СтрокаТаблицыЗначений, Неопределено - описание добавленной команды.
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПодключаемыеКоманды") Тогда
		МодульСозданиеНаОсновании = ОбщегоНазначения.ОбщийМодуль("СозданиеНаОсновании");
		Команда = МодульСозданиеНаОсновании.ДобавитьКомандуСозданияНаОсновании(КомандыСозданияНаОсновании, Метаданные.БизнесПроцессы.Задание);
		Если Команда <> Неопределено Тогда
			Команда.ФункциональныеОпции = "ИспользоватьБизнесПроцессыИЗадачи";
		КонецЕсли;
		Возврат Команда;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Устанавливает флаг ожидает подписания по файлу справки 2-НДФЛ.
//	Параметры:
//		Справка2НДФЛ - СправочникСсылка.СправкаНДФЛПрисоединенныеФайлы - присоединенный файл справки 2-НДФЛ;
//		Удаление - булево - удаляется запись о файле.
Процедура ОтметитьОжидаетПодписание(Справка2НДФЛ, Удаление) Экспорт
	
	Запрос = Новый Запрос();
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗаявкаСотрудникаСправка2НДФЛСправкиНДФЛ.Ссылка КАК Ссылка
	               |ПОМЕСТИТЬ ВТЗаявкиСотрудников
	               |ИЗ
	               |	Справочник.СправкаНДФЛПрисоединенныеФайлы КАК СправкаНДФЛПрисоединенныеФайлы
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ БизнесПроцесс.ЗаявкаСотрудникаСправка2НДФЛ.СправкиНДФЛ КАК ЗаявкаСотрудникаСправка2НДФЛСправкиНДФЛ
	               |		ПО СправкаНДФЛПрисоединенныеФайлы.ВладелецФайла = ЗаявкаСотрудникаСправка2НДФЛСправкиНДФЛ.СправкаНДФЛ
	               |ГДЕ
	               |	СправкаНДФЛПрисоединенныеФайлы.Ссылка = &Справка2НДФЛ
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТЗаявкиСотрудников.Ссылка КАК ЗаявкаСотрудника,
	               |	ЗапланированныеДействияСФайламиДокументовКЭДО.ПрисоединенныйФайл КАК ПрисоединенныйФайл
	               |ИЗ
	               |	ВТЗаявкиСотрудников КАК ВТЗаявкиСотрудников
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ БизнесПроцесс.ЗаявкаСотрудникаСправка2НДФЛ.СправкиНДФЛ КАК ЗаявкаСотрудникаСправка2НДФЛСправкиНДФЛ
	               |			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СправкаНДФЛПрисоединенныеФайлы КАК СправкаНДФЛПрисоединенныеФайлы
	               |				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗапланированныеДействияСФайламиДокументовКЭДО КАК ЗапланированныеДействияСФайламиДокументовКЭДО
	               |				ПО СправкаНДФЛПрисоединенныеФайлы.Ссылка = ЗапланированныеДействияСФайламиДокументовКЭДО.ПрисоединенныйФайл
	               |					И (ЗапланированныеДействияСФайламиДокументовКЭДО.Действие = ЗНАЧЕНИЕ(Перечисление.ДействияСФайламиДокументовКЭДО.Подписать))
	               |			ПО ЗаявкаСотрудникаСправка2НДФЛСправкиНДФЛ.СправкаНДФЛ = СправкаНДФЛПрисоединенныеФайлы.ВладелецФайла
	               |		ПО ВТЗаявкиСотрудников.Ссылка = ЗаявкаСотрудникаСправка2НДФЛСправкиНДФЛ.Ссылка";
	
	Запрос.УстановитьПараметр("Справка2НДФЛ", Справка2НДФЛ);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ЗаявкаСотрудника = Неопределено;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если ЗаявкаСотрудника = Неопределено Тогда
			ЗаявкаСотрудника = Выборка.ЗаявкаСотрудника.ПолучитьОбъект();
		КонецЕсли;
		
		Если ЗаявкаСотрудника.ОжидаетПодписания <> Удаление Тогда
			Возврат;
		КонецЕсли;
		
		Если (Выборка.ПрисоединенныйФайл = Справка2НДФЛ Или Выборка.ПрисоединенныйФайл = Null) И Удаление Тогда
			Продолжить;
		КонецЕсли;
		
		БизнесПроцессыЗаявокСотрудников.ОтметитьОжидаетПодписаниеЗаявкиСотрудника(ЗаявкаСотрудника, Истина);
		Возврат;
		 
	КонецЦикла;	
	
	БизнесПроцессыЗаявокСотрудников.ОтметитьОжидаетПодписаниеЗаявкиСотрудника(ЗаявкаСотрудника, Ложь);
	
КонецПроцедуры

// Возвращает первый этап бизнес-процесса.
//	Возвращаемое значение:
//		СправочникСсылка.ЭтапыЗаявок - первый этап бизнес-процесса
Функция ПервыйЭтап() Экспорт
	Возврат Справочники.ЭтапыЗаявокСотрудников.ФормированиеСправка2НДФЛ;	
КонецФункции

Процедура ЗаполнитьДокументыПоЗаявке(Заявка, ДокументыПоЗаявке) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Заявка", Заявка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДокументыЗаявки.СправкаНДФЛ КАК Ссылка
	|ИЗ
	|	БизнесПроцесс.ЗаявкаСотрудникаСправка2НДФЛ.СправкиНДФЛ КАК ДокументыЗаявки
	|ГДЕ
	|	ДокументыЗаявки.Ссылка = &Заявка";
	ДокументыЗаявки = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	Для каждого ДокументЗаявки Из ДокументыЗаявки Цикл
		ДокументыПоЗаявке.Добавить(ДокументЗаявки);
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Устанавливает состояние элементов формы задачи.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - где:
//   * Элементы - ВсеЭлементыФормы - где:
//    ** Предмет - РасширениеПоляФормыДляПоляНадписи - 
// 
Процедура УстановитьСостояниеЭлементовФормыЗадачи(Форма) Экспорт
	
	Если Форма.Элементы.Найти("РезультатВыполнения") <> Неопределено 
		И Форма.Элементы.Найти("ИсторияВыполнения") <> Неопределено Тогда
			Форма.Элементы.ИсторияВыполнения.Картинка = ОбщегоНазначенияКлиентСервер.КартинкаКомментария(Форма.ЗаданиеРезультатВыполнения);
	КонецЕсли;
	
	Форма.Элементы.Предмет.Гиперссылка = Форма.Объект.Предмет <> Неопределено И НЕ Форма.Объект.Предмет.Пустая();
	Форма.ПредметСтрокой = ОбщегоНазначения.ПредметСтрокой(Форма.Объект.Предмет);	
	
КонецПроцедуры

#Область ОбработчикиОбновленияИнформационнойБазы

#Область ОбновлениеДляИспользованияКЭДО

Процедура ОбновлениеДляИспользованияКЭДО(ПараметрыОбновления = Неопределено) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьБизнесПроцессыЗаявокСотрудников") Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	РезультатЗапросаЗаявок = РезультатЗапросаЗаявокОбновлениеДляИспользованияКЭДО();
	Если РезультатЗапросаЗаявок.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	
	Выборка = РезультатЗапросаЗаявок.Выбрать();
	ОбработкаЗавершена = Истина; 
	Пока Выборка.Следующий() Цикл
		ОписаниеОшибки = "";
		НачатьТранзакцию();
		Попытка
						
			ТаблицаСправок = ТаблицаСправокОбновлениеДляИспользованияКЭДО(Выборка);
			
			Если ПараметрыОбновления <> Неопределено Тогда
				
				Блокировка = Новый БлокировкаДанных;
				ЭлементБлокировки = Блокировка.Добавить("БизнесПроцесс.ЗаявкаСотрудникаСправка2НДФЛ");
				ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
				
				Если ТаблицаСправок.Количество() > 0 Тогда
					ЭлементБлокировки = Блокировка.Добавить("Документ.СправкаНДФЛ");
					ЭлементБлокировки.ИсточникДанных = ТаблицаСправок;
					ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ссылка", "СправкаНДФЛ");
					ЭлементБлокировки = Блокировка.Добавить("Справочник.ФизическиеЛицаПрисоединенныеФайлы");
					ЭлементБлокировки.ИсточникДанных = ТаблицаСправок;
					ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ссылка", "УдалитьФайл");
				КонецЕсли;
				
				Попытка 
					Блокировка.Заблокировать();
				Исключение
					ОтменитьТранзакцию();
					ЗаписьЖурналаРегистрации(НСтр("ru = 'Обновление информационной базы.Ошибка блокировки';
													|en = 'Updating the infobase.Lock error'", ОбщегоНазначения.КодОсновногоЯзыка()),
					УровеньЖурналаРегистрации.Предупреждение, , Выборка.Ссылка, НСтр("ru = 'БизнесПроцесс.ЗаявкаСотрудникаСправка2НДФЛ';
																					|en = 'БизнесПроцесс.ЗаявкаСотрудникаСправка2НДФЛ'"));
					Продолжить;
				КонецПопытки;
				
			КонецЕсли;
			
			ПеренестиИСоздатьДокументыКЭДОФайловОтветаЗаявки(Выборка, ТаблицаСправок, ОписаниеОшибки);
			БизнесПроцессыЗаявокСотрудников.СоздатьИЗаписатьДокументКЭДООбновление(Выборка);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			ОтменитьТранзакцию();
			ОбработкаЗавершена = Ложь;
			
			Если Не ЗначениеЗаполнено(ОписаниеОшибки) Тогда
				ОписаниеОшибки = ОписаниеОшибки();
			КонецЕсли;
			
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
									 УровеньЖурналаРегистрации.Ошибка, , , ОписаниеОшибки);
		КонецПопытки;
	КонецЦикла;
	
	Если Не ОбработкаЗавершена И ПараметрыОбновления <> Неопределено Тогда
		ОписаниеОшибки = НСтр("ru = 'Ошибка обработки заявок сотрудников.';
								|en = 'An error occurred while processing employee requests.'", ОбщегоНазначения.КодОсновногоЯзыка());
		ВызватьИсключение ОписаниеОшибки;
	КонецЕсли;
		
КонецПроцедуры

Функция РезультатЗапросаЗаявокОбновлениеДляИспользованияКЭДО()
	
	Запрос = Новый Запрос();
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗаявкаСотрудника.Ссылка КАК Ссылка,
	               |	ЗаявкаСотрудника.Организация КАК Организация,
	               |	ЗаявкаСотрудника.ФизическоеЛицо КАК ФизическоеЛицо,
	               |	ЗаявкаСотрудника.Дата КАК Дата,
	               |	ЗаявкаСотрудника.Выполнено КАК Выполнено,
	               |	НЕ Изменения.ПредметПубликации ЕСТЬ NULL КАК ЗарегистрированаКПубликации,
	               |	ЗаявкаСотрудника.ИдентификаторЗаявки КАК ИдентификаторЗаявки,
	               |	ЗаявкаСотрудника.ВариантФормированияФайлаОтвета КАК ВариантФормированияФайлаОтвета,
	               |	ЗаявкаСотрудника.СправкиНДФЛ.(
	               |		СправкаНДФЛ КАК СправкаНДФЛ,
	               |		УдалитьФайл КАК УдалитьФайл,
	               |		СправкаНДФЛ.Дата КАК СправкаНДФЛДата,
	               |		СправкаНДФЛ.Организация КАК СправкаНДФЛОрганизация
	               |	) КАК СправкиНДФЛ
	               |ИЗ
	               |	БизнесПроцесс.ЗаявкаСотрудникаСправка2НДФЛ КАК ЗаявкаСотрудника
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ДокументКадровогоЭДО КАК ДокументКадровогоЭДО
	               |		ПО ЗаявкаСотрудника.Ссылка = ДокументКадровогоЭДО.ОснованиеДокумента
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УдалитьИзмененияЗаявокДляСервисаКабинетСотрудника КАК Изменения
	               |		ПО ЗаявкаСотрудника.Ссылка = Изменения.ПредметПубликации
	               |ГДЕ
	               |	ДокументКадровогоЭДО.Ссылка ЕСТЬ NULL
	               |	И ЗаявкаСотрудника.ФизическоеЛицо <> ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	               |	И ЗаявкаСотрудника.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Дата УБЫВ";
		
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция ТаблицаСправокОбновлениеДляИспользованияКЭДО(Заявка)
	
	ТаблицаСправок = Заявка.СправкиНДФЛ.Выгрузить();
	СтрокиКУдалению = Новый Массив;
	Для каждого СтрокаТЗ Из ТаблицаСправок Цикл
		Если Не ЗначениеЗаполнено(СтрокаТЗ.УдалитьФайл) Или Не ЗначениеЗаполнено(СтрокаТЗ.СправкаНДФЛ) Тогда
			СтрокиКУдалению.Добавить(СтрокаТЗ);
		КонецЕсли;
	КонецЦикла;
	Для каждого СтрокаТЗ Из СтрокиКУдалению Цикл
		ТаблицаСправок.Удалить(СтрокаТЗ);
	КонецЦикла;
	
	Возврат ТаблицаСправок;
	
КонецФункции

Процедура ПеренестиИСоздатьДокументыКЭДОФайловОтветаЗаявки(Заявка, ТаблицаСправок, ОписаниеОшибки)
	
	Для каждого СтрокаТЗ Из ТаблицаСправок Цикл
		
		Попытка
			ДанныеФайла = РаботаСФайлами.ДанныеФайла(СтрокаТЗ.УдалитьФайл, РаботаСФайламиКлиентСервер.ПараметрыДанныхФайла());
		Исключение
			ОписаниеОшибки = ОписаниеОшибки();
			ВызватьИсключение;
		КонецПопытки;
		
		ПараметрыНовогоФайла = РаботаСФайлами.ПараметрыДобавленияФайла("Описание");
		ПараметрыНовогоФайла.ИмяБезРасширения = 	ДанныеФайла.Наименование;
		ПараметрыНовогоФайла.РасширениеБезТочки = 	ДанныеФайла.Расширение;
		ПараметрыНовогоФайла.ВремяИзмененияУниверсальное = ТекущаяУниверсальнаяДата();
		ПараметрыНовогоФайла.ВладелецФайлов = СтрокаТЗ.СправкаНДФЛ;
			
		Попытка
			ФайлПослеПереноса = РаботаСФайлами.ДобавитьФайл(ПараметрыНовогоФайла, ДанныеФайла.СсылкаНаДвоичныеДанныеФайла);
		Исключение
			ОписаниеОшибки = ОписаниеОшибки();
			ВызватьИсключение;
		КонецПопытки;
				
		ФайлКУдалению = СтрокаТЗ.УдалитьФайл.ПолучитьОбъект();
		ФайлКУдалению.ПометкаУдаления = Истина;
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ФайлКУдалению);
		
		ДокументКЭДО = Документы.ДокументКадровогоЭДО.СоздатьДокумент();
		ДокументКЭДО.ОснованиеДокумента 	= СтрокаТЗ.СправкаНДФЛ;
		ДокументКЭДО.ЭлектронныйДокумент 	= ФайлПослеПереноса;
		ДокументКЭДО.Дата					= СтрокаТЗ.СправкаНДФЛДата;
		ДокументКЭДО.Организация 			= СтрокаТЗ.СправкаНДФЛОрганизация;
		ДокументКЭДО.КатегорияДокумента 	= Перечисления.КатегорииДокументовКадровогоЭДО.СправкаСотруднику;
		ДокументКЭДО.ВнешниеПодписанты.Добавить().ФизическоеЛицо = Заявка.ФизическоеЛицо;
		
		ДокументКЭДО.ДополнительныеСвойства.Вставить("РегистрироватьИзмененияКабинетСотрудника", Заявка.ЗарегистрированаКПубликации);
		
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументКЭДО,, Истина);
		
	КонецЦикла;

	ЗаявкаОбъект = Заявка.Ссылка.ПолучитьОбъект();
	Для каждого СтрокаТЧ Из ЗаявкаОбъект.СправкиНДФЛ Цикл
		СтрокаТЧ.УдалитьФайл = Справочники.ФизическиеЛицаПрисоединенныеФайлы.ПустаяСсылка();
	КонецЦикла;
	ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ЗаявкаОбъект);
	
КонецПроцедуры

#КонецОбласти

Процедура ПеренестиПрисоединенныеФайлыВложений(ПараметрыОбновления = Неопределено) Экспорт
	БизнесПроцессыЗаявокСотрудников.ПеренестиПрисоединенныеФайлыВложений("БизнесПроцесс.ЗаявкаСотрудникаСправка2НДФЛ",
																		 ПараметрыОбновления);	
КонецПроцедуры

Процедура ЗаполнитьСодержимоеДокументаКадровогоЭДОЗаявокСотрудников(ПараметрыОбновления = Неопределено) Экспорт
	БизнесПроцессыЗаявокСотрудников.ЗаполнитьСодержимоеДокументаКадровогоЭДОЗаявокСотрудников(
		"БизнесПроцесс.ЗаявкаСотрудникаСправка2НДФЛ",
		ПараметрыОбновления);	
КонецПроцедуры
	
#КонецОбласти

#КонецОбласти

#КонецЕсли