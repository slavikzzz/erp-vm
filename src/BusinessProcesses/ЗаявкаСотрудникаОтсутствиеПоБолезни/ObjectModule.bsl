#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступом.ЗаполнитьНаборыЗначенийДоступа.
//
// Параметры:
//   Таблица - см. УправлениеДоступом.ТаблицаНаборыЗначенийДоступа
//
Процедура ЗаполнитьНаборыЗначенийДоступа(Таблица) Экспорт
	ЗаполнитьНаборыЗначенийДоступаПоУмолчанию(Таблица);
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий бизнес-процесса.

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ДанныеЗаполнения <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если Автор <> Неопределено И Не Автор.Пустая() Тогда
		АвторСтрокой = Строка(Автор);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	ГруппаИсполнителейЗадач = ?(ТипЗнч(Исполнитель) = Тип("СправочникСсылка.РолиИсполнителей"),
		БизнесПроцессыИЗадачиСервер.ГруппаИсполнителейЗадач(Исполнитель, ОсновнойОбъектАдресации, ДополнительныйОбъектАдресации),
		Исполнитель);
	УстановитьПривилегированныйРежим(Ложь);
	
	Если НЕ ЭтоНовый() И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Предмет") <> Предмет Тогда
		ИзменитьПредметЗадач();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	ДатаЗавершения = '00010101000000';	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий элементов карты маршрута.

// Параметры:
// 	ТочкаМаршрутаБизнесПроцесса - ТочкаМаршрутаБизнесПроцессаСсылка.Задание - 
// 	ФормируемыеЗадачи - Массив из ЗадачаОбъект - 
// 	Отказ - Булево - 
// 
Процедура ВыполнитьПриСозданииЗадач(ТочкаМаршрутаБизнесПроцесса, ФормируемыеЗадачи, Отказ)
	
	Записать();
	
	// Устанавливаем реквизиты адресации и доп. реквизиты для каждой задачи.
	Для каждого Задача Из ФормируемыеЗадачи Цикл
		
		Задача.Автор = Автор;
		Задача.АвторСтрокой = Строка(Автор);
		Если ТипЗнч(Исполнитель) = Тип("СправочникСсылка.РолиИсполнителей") Тогда
			Задача.РольИсполнителя = Исполнитель;
			Задача.ОсновнойОбъектАдресации = ОсновнойОбъектАдресации;
			Задача.ДополнительныйОбъектАдресации = ДополнительныйОбъектАдресации;
			Задача.Исполнитель = Неопределено;
		Иначе	
			Задача.Исполнитель = Исполнитель;
		КонецЕсли;
		Задача.Наименование = НаименованиеЗадачиДляВыполнения(ТочкаМаршрутаБизнесПроцесса);
		Задача.СрокИсполнения = СрокИсполненияЗадачиДляВыполнения(ТочкаМаршрутаБизнесПроцесса);
		Задача.Важность = Важность;
		Задача.Предмет = Предмет;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ВыполнитьПередСозданиемЗадач(ТочкаМаршрутаБизнесПроцесса, ФормируемыеЗадачи, СтандартнаяОбработка)
	Если Предмет = Неопределено Или Предмет.Пустая() Тогда
		Возврат;
	КонецЕсли;	
КонецПроцедуры

Процедура ЗавершениеПриЗавершении(ТочкаМаршрутаБизнесПроцесса, Отказ)
	ДатаЗавершения = БизнесПроцессыИЗадачиСервер.ДатаЗавершенияБизнесПроцесса(Ссылка);
	Выполнено = Истина;
	Записать();	
КонецПроцедуры

Процедура ВыполненоПроверкаУсловия(ТочкаМаршрутаБизнесПроцесса, Результат)
	Результат = (СостояниеЗаявки = Перечисления.СостоянияЗаявокКабинетСотрудника.Выполнена);
КонецПроцедуры

Процедура СоздаватьБольничныйЛистПроверкаУсловия(ТочкаМаршрутаБизнесПроцесса, Результат)
	Результат = СозданиеБольничногоЛиста;
КонецПроцедуры

Процедура СоздаватьПрогулНеявкаПроверкаУсловия(ТочкаМаршрутаБизнесПроцесса, Результат)
	Результат = БизнесПроцессыЗаявокСотрудниковВнутренний.СоздаватьПрогулНеявкаПроверкаУсловия(ЭтотОбъект);
КонецПроцедуры

Процедура СогласоватьЗаявкуПриВыполнении(ТочкаМаршрутаБизнесПроцесса, Задача, Отказ)
	ЗадачаОбъект = Задача.ПолучитьОбъект();
	ЗадачаОбъект.ВыполнитьЗадачу();
КонецПроцедуры

Процедура СоздатьБольничныйПередСозданиемЗадач(ТочкаМаршрута, ФормируемыеЗадачи, СтандартнаяОбработка)
	Записать();
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ДоступноВыполнение() Экспорт
	
	ДокументПредмет = БизнесПроцессыЗаявокСотрудниковВнутренний.ДокументПредметЗаявкаСотрудникаОтсутствиеПоБолезни(ЭтотОбъект);
	ДоступноВыполнение = БизнесПроцессыЗаявокСотрудников.ДоступноВыполнение(ЭтотОбъект, ДокументПредмет);
	
	Запрос = Новый Запрос();
	
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	               |	ЗадачаИсполнителя.Ссылка КАК Ссылка,
	               |	ЗадачаИсполнителя.ТочкаМаршрута КАК ТочкаМаршрута
	               |ИЗ
	               |	Задача.ЗадачаИсполнителя КАК ЗадачаИсполнителя
	               |ГДЕ
	               |	ЗадачаИсполнителя.БизнесПроцесс = &БизнесПроцесс
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ЗадачаИсполнителя.Выполнена ВОЗР,
	               |	ЗадачаИсполнителя.Дата УБЫВ";
	
	Запрос.УстановитьПараметр("БизнесПроцесс", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ДоступноВыполнение = ДоступноВыполнение Или Не БизнесПроцессыЗаявокСотрудниковВнутренний.ДоступнаФункциональнаяОпцияЗаявкаСотрудникаОтсутствиеПоБолезни(Выборка.ТочкаМаршрута);
	КонецЕсли;
	
	СвязанныеЗаявки = БизнесПроцессыЗаявокСотрудников.СвязанныеЗаявкиСотрудника(Ссылка, ИдентификаторЗаявки);
	Если СвязанныеЗаявки.Количество() = 0 Тогда
		Возврат ДоступноВыполнение;
	КонецЕсли;
	
	Для Каждого СтрокаТЗ Из СвязанныеЗаявки Цикл
		ДоступноВыполнение = ДоступноВыполнение И (СтрокаТЗ.СостояниеЗаявки <> Перечисления.СостоянияЗаявокКабинетСотрудника.Отказ);
	КонецЦикла;
	
	Возврат ДоступноВыполнение;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ИзменитьПредметЗадач()

	УстановитьПривилегированныйРежим(Истина);
	НачатьТранзакцию();
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Задача.ЗадачаИсполнителя");
		ЭлементБлокировки.УстановитьЗначение("БизнесПроцесс", Ссылка);
		Блокировка.Заблокировать();
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	Задачи.Ссылка КАК Ссылка
			|ИЗ
			|	Задача.ЗадачаИсполнителя КАК Задачи
			|ГДЕ
			|	Задачи.БизнесПроцесс = &БизнесПроцесс
			|	И Задачи.Выполнена = ЛОЖЬ");

		Запрос.УстановитьПараметр("БизнесПроцесс", Ссылка);
		ВыборкаДетальныеЗаписи = Запрос.Выполнить().Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			ЗадачаОбъект = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект(); // ЗадачаОбъект
			ЗадачаОбъект.Предмет = Предмет;
			// Не выполняем предварительную блокировку данных для редактирования, т.к.
			// Это изменение имеет более высокий приоритет над открытыми формами задач.
			ЗадачаОбъект.Записать();
		КонецЦикла;
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;

КонецПроцедуры 

Функция НаименованиеЗадачиДляВыполнения(ТочкаМаршрутаБизнесПроцесса)
	
	Если ТочкаМаршрутаБизнесПроцесса = БизнесПроцессы.ЗаявкаСотрудникаОтсутствиеПоБолезни.ТочкиМаршрута.СоздатьБольничный Тогда
		НаименованиеФизическогоЛица = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ФизическоеЛицо, "Наименование");
		ШаблонБольничный = НСтр("ru = 'Создать больничный на основании неявки для %1';
								|en = 'Generate sick leave based on non-attendance for %1'");
		Возврат СтрШаблон(ШаблонБольничный, НаименованиеФизическогоЛица);
	ИначеЕсли ТочкаМаршрутаБизнесПроцесса = БизнесПроцессы.ЗаявкаСотрудникаОтсутствиеПоБолезни.ТочкиМаршрута.ЗарегистрироватьОтсутствие Тогда
		НаименованиеФизическогоЛица = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ФизическоеЛицо, "Наименование");
		Если ПоБеременности Тогда
			ШаблонОтпускПоБеременности = НСтр("ru = 'Зарегистрировать отпуск по беременности для %1';
												|en = 'Record maternity leave for %1'");
			Возврат СтрШаблон(ШаблонОтпускПоБеременности, НаименованиеФизическогоЛица);
		Иначе
			ШаблонОтсутствиеПоБолезни = НСтр("ru = 'Зарегистрировать отсутствие по болезни для %1';
											|en = 'Record sick leave for %1'"); 
			Возврат СтрШаблон(ШаблонОтсутствиеПоБолезни, НаименованиеФизическогоЛица);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Наименование;
	
КонецФункции

Функция СрокИсполненияЗадачиДляВыполнения(ТочкаМаршрутаБизнесПроцесса)
	Если ТочкаМаршрутаБизнесПроцесса.Имя = "СоздатьБольничный" Тогда
		Возврат ДатаОкончанияОтсутствия;
	Иначе
		Возврат СрокИсполнения;	
	КонецЕсли;
КонецФункции

Процедура ЗаполнитьНаборыЗначенийДоступаПоУмолчанию(Таблица)
	
	// Логика ограничения по умолчанию для
	// - чтения:    Автор ИЛИ Исполнитель (с учетом адресации) ИЛИ Проверяющий (с учетом адресации)
	// - изменения: Автор.
	
	// Если предмет не задан (т.е. бизнес-процесс без основания), тогда предмет не участвует в логике ограничения.
	
	// Чтение, Изменение: набор № 1.
	Строка = Таблица.Добавить();
	Строка.НомерНабора     = 1;
	Строка.Чтение          = Истина;
	Строка.Изменение       = Истина;
	Строка.ЗначениеДоступа = Автор;
	
	// Чтение: набор № 2.
	Строка = Таблица.Добавить();
	Строка.НомерНабора     = 2;
	Строка.Чтение          = Истина;
	Строка.ЗначениеДоступа = ГруппаИсполнителейЗадач;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли