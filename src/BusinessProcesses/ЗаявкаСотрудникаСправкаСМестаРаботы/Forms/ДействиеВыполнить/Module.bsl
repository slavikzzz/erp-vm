
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Для нового объекта выполняем код инициализации формы в ПриСозданииНаСервере.
	// Для существующего - в ПриЧтенииНаСервере.
	Ссылка = Объект.Ссылка;
	Если Ссылка.Пустая() Тогда
		ИнициализироватьФормуЗадачи();
	КонецЕсли;
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
			
	Если Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		ЗаблокироватьДанныеДляРедактирования(Ссылка, , УникальныйИдентификатор);
	Исключение
		ТолькоПросмотр = Истина;
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Заявка редактируется другим пользователем, форма открыта в режиме только для просмотра.';
													|en = 'The request is being edited by another user, the form is opened in the read-only mode.'"));
	КонецПопытки;
	
	УстановитьДоступностьКнопокДействий();
	
	// СтандартныеПодсистемы.РаботаСФайлами
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаСФайлами") Тогда
		МодульРаботаСФайлами = ОбщегоНазначения.ОбщийМодуль("РаботаСФайлами");
		ПараметрыГиперссылки = МодульРаботаСФайлами.ГиперссылкаФайлов();
		ПараметрыГиперссылки.Размещение = "КоманднаяПанель";
		ПараметрыГиперссылки.Владелец = "Объект.БизнесПроцесс";
		МодульРаботаСФайлами.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыГиперссылки);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.РаботаСФайлами

	Если ЗначениеЗаполнено(Задание.УдалитьФайлОтвета) Тогда
		ВызватьИсключение НСтр("ru = 'Заявка не доступна до окончания обновления.';
								|en = 'The request is not available till the update is finished.'");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	БизнесПроцессыИЗадачиКлиент.ОбновитьДоступностьКомандПринятияКИсполнению(ЭтотОбъект);
	
	// СтандартныеПодсистемы.РаботаСФайлами
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаСФайлами") Тогда
		МодульРаботаСФайламиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("РаботаСФайламиКлиент");
		МодульРаботаСФайламиКлиент.ПриОткрытии(ЭтотОбъект, Отказ);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.РаботаСФайлами
		
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Если Не ЗавершениеРаботы Тогда
		ПередЗакрытиемНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	БизнесПроцессыЗаявокСотрудниковФормы.ЗаписатьРеквизитыБизнесПроцесса(ЭтотОбъект, ТекущийОбъект);
		
	ВыполнитьЗадачу = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыЗаписи, "ВыполнитьЗадачу", Ложь);
	Если НЕ ВыполнитьЗадачу Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗаданиеВыполнено И НЕ ЗначениеЗаполнено(ТекущийОбъект.РезультатВыполнения) Тогда
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Укажите причину, по которой задача отклоняется.';
				|en = 'Please tell why you decline the task.'"),,
			"Объект.РезультатВыполнения",,
			Отказ);
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	БизнесПроцессыИЗадачиКлиент.ФормаЗадачиОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	Если ИмяСобытия = "Запись_Задание" Тогда
		Если (Источник = Объект.БизнесПроцесс ИЛИ (ТипЗнч(Источник) = Тип("Массив") 
			И Источник.Найти(Объект.БизнесПроцесс) <> Неопределено)) Тогда
			Прочитать();
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "ШаблонОтветаЗаписанСправочник" Тогда
		Если (Источник = ЭтотОбъект) Тогда
			ШаблонОтвета = Параметр;
			Элементы.ШаблонОтвета.Видимость = Истина;
			ШаблонОтветаПриИзмененииНаСервере();
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "ЗаписьПечатныхФормСЭЦП" Тогда
		ОтветПоЗаявке = Задание.ОтветПоЗаявке;
		Комментарий = Задание.Комментарий;
		Прочитать();
		ПоместитьДанныеИзБуфера(ОтветПоЗаявке, Комментарий);
	КонецЕсли;
	
	// СтандартныеПодсистемы.РаботаСФайлами
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаСФайлами") Тогда
		МодульРаботаСФайламиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("РаботаСФайламиКлиент");
		МодульРаботаСФайламиКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.РаботаСФайлами
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ИнициализироватьФормуЗадачи();
	Элементы.Содержание.Заголовок = ЗаданиеСодержание;
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ШаблонОтветаПриИзменении(Элемент)
	ШаблонОтветаПриИзмененииНаСервере();	
КонецПроцедуры

// СтандартныеПодсистемы.РаботаСФайлами
&НаКлиенте
Процедура Подключаемый_ПолеПредпросмотраНажатие(Элемент, СтандартнаяОбработка)
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаСФайлами") Тогда
		МодульРаботаСФайламиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("РаботаСФайламиКлиент");
		МодульРаботаСФайламиКлиент.ПолеПредпросмотраНажатие(ЭтотОбъект, Элемент, СтандартнаяОбработка);
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолеПредпросмотраПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаСФайлами") Тогда
		МодульРаботаСФайламиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("РаботаСФайламиКлиент");
		МодульРаботаСФайламиКлиент.ПолеПредпросмотраПроверкаПеретаскивания(ЭтотОбъект, Элемент,
			ПараметрыПеретаскивания, СтандартнаяОбработка);
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолеПредпросмотраПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаСФайлами") Тогда
		МодульРаботаСФайламиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("РаботаСФайламиКлиент");
		МодульРаботаСФайламиКлиент.ПолеПредпросмотраПеретаскивание(ЭтотОбъект, Элемент,
			ПараметрыПеретаскивания, СтандартнаяОбработка);
	КонецЕсли;	
КонецПроцедуры
// Конец СтандартныеПодсистемы.РаботаСФайлами

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполненоВыполнить(Команда)
	Если ПодписыватьЗаявкиСотрудника Тогда
		БизнесПроцессыЗаявокСотрудниковКлиент.ПодписатьЗаявкуЭП(ЭтотОбъект, "ВыполненоВыполнитьЗавершение");
	Иначе
		ВыполненоВыполнитьЗавершение(Неопределено, Неопределено);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Отказать(Команда)
	
	Отказ = Ложь;
	ОтказатьНаСервере(Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	Если ПодписыватьЗаявкиСотрудника Тогда
		БизнесПроцессыЗаявокСотрудниковКлиент.ПодписатьЗаявкуЭП(ЭтотОбъект, "ОтказатьЗавершение");
	Иначе
		ОтказатьЗавершение(Неопределено, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Дополнительно(Команда)
	БизнесПроцессыИЗадачиКлиент.ОткрытьДопИнформациюОЗадаче(Объект.Ссылка);	
КонецПроцедуры

&НаКлиенте
Процедура ПринятьКИсполнению(Команда)
	БизнесПроцессыЗаявокСотрудниковКлиент.ПринятьЗадачуКИсполнению(ЭтотОбъект, ТекущийПользователь);
	УстановитьДоступностьКнопокДействий();
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьПринятиеКИсполнению(Команда)
	БизнесПроцессыИЗадачиКлиент.ОтменитьПринятиеЗадачиКИсполнению(ЭтотОбъект);
	Прочитать();
	БизнесПроцессыИЗадачиКлиент.ОбновитьДоступностьКомандПринятияКИсполнению(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьЗадание(Команда)
	Если Модифицированность Тогда
		Записать();
	КонецЕсли;	
	ПоказатьЗначение(,Объект.БизнесПроцесс);	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыбратьФайлОтвета(Команда)
	
	Обработчик = Новый ОписаниеОповещения("ЗавершениеВыбратьФайлОтвета", ЭтотОбъект);
	
	ПараметрыЗагрузки = ФайловаяСистемаКлиент.ПараметрыЗагрузкиФайла();
	ПараметрыЗагрузки.Диалог.Фильтр = 
		НСтр("ru = 'Файлы MS Word (*.doc;*.docx)|*.doc;*.docx|Файлы PDF(*.pdf;*.PDF)|*.pdf;*.PDF|
			 |Архив (*.zip;*.rar;*.7z)|*.zip;*.rar;*.7z';
			 |en = 'MS Word files (*.doc;*.docx)|*.doc;*.docx|PDF files(*.pdf;*.PDF)|*.pdf;*.PDF|
			 |Archive (*.zip;*.rar;*.7z)|*.zip;*.rar;*.7z'");

	ПараметрыЗагрузки.ИдентификаторФормы = УникальныйИдентификатор;

	ФайловаяСистемаКлиент.ЗагрузитьФайл(Обработчик, ПараметрыЗагрузки);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УдалитьФайлНажатие(Элемент)
	ОписаниеОповещения = Новый ОписаниеОповещения("УдалитьПрисоединенныйФайлЗавершение", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, НСтр("ru = 'Справка будет удалена. Удалить?';
											|en = 'The statement will be deleted. Delete?'"), РежимДиалогаВопрос.ДаНет);	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ФайлНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОткрытьПрисоединенныйФайл(ЭтотОбъект[Элемент.Имя]);	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьФайл(Команда)
	СформироватьФайлНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПодписатьЭП(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершитьПодписатьЭП",
												  ЭтотОбъект,
												  Новый Структура("ПечатныеФормыОбъектов"));
												  ФайлыСправок = Новый Массив;
												  
	ФайлОтветаПоЗаявке = ФайлОтвета();
	БизнесПроцессыЗаявокСотрудниковКлиент.ПодписатьЭПФайлыОтвета(ЭтотОбъект, 
																 ОписаниеОповещения,
																 ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФайлОтветаПоЗаявке),
																 ЭтоФайлПечатнойФормы(ФайлОтветаПоЗаявке));
																 
КонецПроцедуры

&НаКлиенте
Процедура СохранитьШаблонОтвета(Команда)
	БизнесПроцессыЗаявокСотрудниковКлиент.СохранитьШаблонОтвета(ЭтотОбъект);
КонецПроцедуры

// СтандартныеПодсистемы.РаботаСФайлами
&НаКлиенте
Процедура Подключаемый_КомандаПанелиПрисоединенныхФайлов(Команда)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаСФайлами") Тогда
		МодульРаботаСФайламиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("РаботаСФайламиКлиент");
		МодульРаботаСФайламиКлиент.КомандаУправленияПрисоединеннымиФайлами(ЭтотОбъект, Команда);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрытьВыполнить()
	БизнесПроцессыИЗадачиКлиент.ЗаписатьИЗакрытьВыполнить(ЭтотОбъект);	
КонецПроцедуры

&НаКлиенте
Процедура Распечатать(Команда)
	КоллекцияПечатныхФорм = УправлениеПечатьюКлиент.НоваяКоллекцияПечатныхФорм(
		ИдентификаторПечатнойФормы());
	КоллекцияПечатныхФорм[0].ТабличныйДокумент = ПечатнаяФормаСправкаСМестаРаботы(Задание);
	УправлениеПечатьюКлиент.ПечатьДокументов(КоллекцияПечатныхФорм);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаСервереБезКонтекста
Функция ИдентификаторПечатнойФормы()
	Возврат БизнесПроцессы.ЗаявкаСотрудникаСправкаСМестаРаботы.ИдентификаторПечатнойФормы();
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СерверныеОбработчикиСобытийФормы

&НаСервере
Процедура ПередЗакрытиемНаСервере()
	РазблокироватьДанныеДляРедактирования(Объект.Ссылка, УникальныйИдентификатор);	
КонецПроцедуры

#КонецОбласти

#Область СерверныеОбработчикиСобытийЭлементовШапкиФормы

&НаСервере
Процедура ШаблонОтветаПриИзмененииНаСервере()
	БизнесПроцессыЗаявокСотрудниковФормы.ПослеВыбораШаблона(ЭтотОбъект, ШаблонОтвета, Неопределено);
КонецПроцедуры

#КонецОбласти

#Область СерверныеОбработчикиКомандФормы

&НаСервере
Процедура ОтказатьНаСервере(Отказ)
	Если ПустаяСтрока(Задание.ОтветПоЗаявке)
		 И КабинетСотрудника.ИспользоватьФормат302() Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не заполнена причина отказа.';
													|en = 'The refusal reason is not filled in.'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиОповещения

&НаКлиенте
Процедура ВыполненоВыполнитьЗавершение(Результат, ДополнительныеПараметры) Экспорт
	БизнесПроцессыЗаявокСотрудниковКлиент.ВыполненоВыполнитьЗавершение(ЭтотОбъект, Результат, ДополнительныеПараметры);
КонецПроцедуры

&НаКлиенте
Процедура ОтказатьЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено
		 И Результат.Свойство("Отказ")
		 И Результат.Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Ложь;
	ОтказатьЗавершениеНаСервере(Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;	
	
	БизнесПроцессыИЗадачиКлиент.ЗаписатьИЗакрытьВыполнить(ЭтотОбъект, Истина);

КонецПроцедуры	

&НаКлиенте
Процедура УдалитьПрисоединенныйФайлЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		УдалитьПрисоединенныйФайлЗавершениеНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеВыбратьФайлОтвета(ПомещенныйФайл, ДополнительныеПараметры) Экспорт
	
	Если ПомещенныйФайл = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Данные = ПолучитьИзВременногоХранилища(ПомещенныйФайл.Хранение);
	Если Данные.Размер() > ИнтеграцияУправлениеПерсоналомКлиентСервер.МаксимальныйРазмерПринимаемогоФайла() Тогда
		ПоказатьПредупреждение(,
			НСтр("ru = 'Сервис не может принять файлы размером более 5Мб. Выберите другой файл.';
				|en = 'The service cannot accept files larger than 5MB. Choose another file.'"));
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	
	ЗавершениеВыбратьФайлОтветаНаСервере(ПомещенныйФайл);	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьПодписатьЭП(Результат, ДополнительныеПараметры) Экспорт
	ЗавершитьПодписатьЭПНаСервере();	 	
КонецПроцедуры

#КонецОбласти

#Область РаботаСФайлами

&НаСервере
Процедура СформироватьФайлНаСервере()
	
	ТабДок = ПечатнаяФормаСправкаСМестаРаботы(Задание);
	
	ФайлОтветаПоЗаявке = ФайлОтвета();
	
	НачатьТранзакцию();
	Попытка
		ПредставлениеПечатнойФормы = НазваниеСправки(Задание.ВидСправки);
		Если ИспользуетсяКадровыйЭДО Тогда
			
			Если ЗначениеЗаполнено(ФайлОтветаПоЗаявке) Тогда
				КадровыйЭДОВызовСервера.УдалитьФайлыИзОбработкиПользователя(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФайлОтветаПоЗаявке));
			КонецЕсли;
			
			ДанныеПечатнойФормы = КадровыйЭДОВызовСервера.ДобавитьПечатнуюФорму(
				ТабДок,
				Задание.Ссылка,
				БизнесПроцессы.ЗаявкаСотрудникаСправкаСМестаРаботы.ИдентификаторПечатнойФормы(),
				ПредставлениеПечатнойФормы,
				Задание.Организация,
				Задание.ФизическоеЛицо);
				
			ФайлОтветаПоЗаявке = ДанныеПечатнойФормы.ФайлОбъекта.ПолучитьОбъект();
			ФайлОтветаПоЗаявке.ФайлОтвета = Истина;
			ФайлОтветаПоЗаявке.Записать();
			
			РегистрыСведений.ЗапланированныеДействияСФайламиДокументовКЭДО.ЗарегистрироватьОбработкуФайлов(
						ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДанныеПечатнойФормы.ФайлОбъекта),
						Перечисления.ДействияСФайламиДокументовКЭДО.ПередатьВКабинетСотрудников);
			
		Иначе
			
			Если ЗначениеЗаполнено(ФайлОтветаПоЗаявке) Тогда
				ФайлОтветаПоЗаявкеОбъект = ФайлОтветаПоЗаявке.ПолучитьОбъект();
				ФайлОтветаПоЗаявкеОбъект.ПометкаУдаления = Истина;
				ФайлОтветаПоЗаявкеОбъект.Записать();
			КонецЕсли;
			
			Поток = Новый ПотокВПамяти();
			ТабДок.Записать(Поток, КадровыйЭДОВызовСервера.ТипФайлаЭлектронногоДокумента());
			ДвоичныеДанные =  Поток.ЗакрытьИПолучитьДвоичныеДанные();
			АдресХранилища = ПоместитьВоВременноеХранилище(ДвоичныеДанные);
			
			ПараметрыФайла = РаботаСФайлами.ПараметрыДобавленияФайла("Описание, ФайлОтвета");
			ПараметрыФайла.ВладелецФайлов = Задание.Ссылка;
			ПараметрыФайла.ИмяБезРасширения = ПредставлениеПечатнойФормы;
			ПараметрыФайла.РасширениеБезТочки = "pdf";
			ПараметрыФайла.ВремяИзмененияУниверсальное = ТекущаяУниверсальнаяДата();
			ПараметрыФайла.Служебный = Истина;
			ПараметрыФайла.ФайлОтвета = Истина;
			ПараметрыФайла.Описание = НСтр("ru = 'Приложение к заявке:';
											|en = 'Attachment to application:'") + " " + Строка(Задание);
			
			ПрисоединенныйФайл = РаботаСФайлами.ДобавитьФайл(ПараметрыФайла, АдресХранилища);
			БизнесПроцессыЗаявокСотрудников.СоздатьИзменитьДокументКЭДОСправкаСотруднику(ПрисоединенныйФайл, Задание.Ссылка);
			
		КонецЕсли;
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Заявка сотрудника справка с места работы.Ошибка формирования файла справки';
										|en = 'Employee request statement of employment. An error occurred while generating the statement file'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,
			,
			,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не удалось сформировать файл';
													|en = 'Cannot generate file'"));
	КонецПопытки;
	
	ОтразитьФайлыОтвета();
	
КонецПроцедуры

&НаСервере
Процедура ЗавершениеВыбратьФайлОтветаНаСервере(ПомещенныйФайл)
	
	СтруктураИмениФайла = БизнесПроцессыЗаявокСотрудниковФормы.СтруктураИмениФайла(ПомещенныйФайл.Имя);
	Если СтруктураИмениФайла = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	Попытка
			
		АдресХранилища = ПомещенныйФайл.Хранение;
		
		ПараметрыФайла = РаботаСФайлами.ПараметрыДобавленияФайла("Описание, ФайлОтвета");
		ПараметрыФайла.ВладелецФайлов = Задание.Ссылка;
		ПараметрыФайла.ИмяБезРасширения = СтруктураИмениФайла.ИмяФайлаОтветаБезРасширения;
		ПараметрыФайла.РасширениеБезТочки = СтруктураИмениФайла.РасширениеФайлаОтветаБезТочки;
		ПараметрыФайла.ВремяИзмененияУниверсальное = ТекущаяУниверсальнаяДата();
		ПараметрыФайла.Служебный = Истина;
		ПараметрыФайла.ФайлОтвета = Истина;
		ПараметрыФайла.Описание = НСтр("ru = 'Приложение к заявке:';
										|en = 'Attachment to request:'") + " " + Строка(Задание.Ссылка);
			
		ПрисоединенныйФайл = РаботаСФайлами.ДобавитьФайл(ПараметрыФайла, АдресХранилища);
		
		БизнесПроцессыЗаявокСотрудников.СоздатьИзменитьДокументКЭДОСправкаСотруднику(ПрисоединенныйФайл, Задание.Ссылка);
		Если ИспользуетсяКадровыйЭДО Тогда
			РегистрыСведений.ЗапланированныеДействияСФайламиДокументовКЭДО.ЗарегистрироватьОбработкуФайлов(
				ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПрисоединенныйФайл),
				Перечисления.ДействияСФайламиДокументовКЭДО.ПередатьВКабинетСотрудников);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
	    ОтменитьТранзакцию();
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Заявка сотрудника справка с места работы.Ошибка прикрепления файла справки';
										|en = 'Employee request statement of employment. An error occurred while attaching the statement file'",
									  ОбщегоНазначения.КодОсновногоЯзыка()),
       							 УровеньЖурналаРегистрации.Ошибка,
        						 ,
        						 ,
        						 ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не удалось прикрепить файл';
													|en = 'Cannot attach file'"));
		Возврат;
	КонецПопытки;
	
	ОтразитьФайлыОтвета();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПрисоединенныйФайл(ПрисоединенныйФайл)
	БизнесПроцессыЗаявокСотрудниковКлиент.ОткрытьПрисоединенныйФайл(ЭтотОбъект, ПрисоединенныйФайл);	
КонецПроцедуры

&НаСервере
Процедура ОтразитьФайлыОтвета()
	
	ДобавляемыеРеквизиты = Новый Массив;
	ЗначенияРеквизитов = Новый Структура;

	ИмяГруппы = "ГруппаФайлОтвета";
	ИмяРеквизита = "ФайлОтвета";
	
	СтруктураРеквизита = БизнесПроцессыЗаявокСотрудниковФормы.НовыйСтруктураРеквизитаФайлаОтвета();
	СтруктураРеквизита.ИмяРеквизита						= ИмяРеквизита;
	СтруктураРеквизита.ИмяТаблицыПрисоединенногоФайла	= "ЗаявкаСотрудникаСправкаСМестаРаботыПрисоединенныеФайлы";
		
	ГруппаФайлОтвета = Элементы.Найти(ИмяГруппы);
	Если ГруппаФайлОтвета = Неопределено Тогда
		БизнесПроцессыЗаявокСотрудниковФормы.ДобавитьЭлементыФайлаОтвета(ЭтотОбъект,
																		 ИмяГруппы,
																		 СтруктураРеквизита,
																		 ДобавляемыеРеквизиты);
		ЭлементВыбратьФайлОтвета = Элементы["Выбрать" + ИмяРеквизита];																 
		ЭлементВыбратьФайлОтвета.Заголовок = НСтр("ru = 'Выбрать файл';
													|en = 'Select file'");
		Элементы.Переместить(ЭлементВыбратьФайлОтвета, Элементы.ГруппаСформироватьИПодписатьФайлы, Элементы.ПодписатьЭП);
	КонецЕсли;

	ФайлОтветаСсылка = ФайлОтвета();
	ФайлОтветаЭлемент = Элементы[ИмяРеквизита];
	БизнесПроцессыЗаявокСотрудниковФормы.ЗаполнитьЭлементыФайлаОтвета(ЭтотОбъект,
																	  ФайлОтветаЭлемент,
																	  ФайлОтветаСсылка,
																	  ИмяРеквизита,
																	  ЗначенияРеквизитов);
																	  
	ВБумажномВиде =
		(Задание.ВариантФормированияФайлаОтвета = Перечисления.ВариантыФормированияФайлаОтветаЗаявкиСотрудника.ВБумажномВиде);
	ФайлОтветаЭлемент.Видимость = ФайлОтветаЭлемент.Видимость И Не ВБумажномВиде;
																	  
	Если ДобавляемыеРеквизиты.Количество() > 0 Тогда
		ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	КонецЕсли;	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ЗначенияРеквизитов);
	
	УстановитьДоступностьКнопокДействий();
	
КонецПроцедуры

&НаСервере
Функция ФайлОтвета() 
	
	Запрос = Новый Запрос();
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗаявкаСотрудникаСправкаСМестаРаботыПрисоединенныеФайлы.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.ЗаявкаСотрудникаСправкаСМестаРаботыПрисоединенныеФайлы КАК ЗаявкаСотрудникаСправкаСМестаРаботыПрисоединенныеФайлы
	               |ГДЕ
	               |	ЗаявкаСотрудникаСправкаСМестаРаботыПрисоединенныеФайлы.ВладелецФайла = &ВладелецФайла
	               |	И ЗаявкаСотрудникаСправкаСМестаРаботыПрисоединенныеФайлы.ФайлОтвета = ИСТИНА
				   |	И ЗаявкаСотрудникаСправкаСМестаРаботыПрисоединенныеФайлы.ПометкаУдаления = ЛОЖЬ";
	
	Запрос.УстановитьПараметр("ВладелецФайла", Задание.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Справочники.ЗаявкаСотрудникаСправкаСМестаРаботыПрисоединенныеФайлы.ПустаяСсылка();
	
КонецФункции
 
&НаСервере
Процедура ЗавершитьПодписатьЭПНаСервере()
	
	ФайлОтветаПоЗаявке = ФайлОтвета();
	Если Не ЭтоФайлПечатнойФормы(ФайлОтветаПоЗаявке) Тогда
		НачатьТранзакцию();
		Попытка
			БизнесПроцессыЗаявокСотрудников.СоздатьИзменитьДокументКЭДОСправкаСотруднику(ФайлОтветаПоЗаявке, Задание.Ссылка);
			РегистрыСведений.ЗапланированныеДействияСФайламиДокументовКЭДО.ЗарегистрироватьОбработкуФайлов(
				ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФайлОтветаПоЗаявке),
				Перечисления.ДействияСФайламиДокументовКЭДО.ПередатьВКабинетСотрудников);
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Заявка сотрудника справка с места работы.Ошибка подписания файла справки';
											|en = 'Employee request statement of employment. An error occurred while signing the statement file'",
										  ОбщегоНазначения.КодОсновногоЯзыка()),
       								 УровеньЖурналаРегистрации.Ошибка,
        							 ,
        							 ,
        							 ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не удалось подписать файл';
														|en = 'Cannot sign file'"));
			Возврат;
		КонецПопытки;
	КонецЕсли;
	
	ОтветПоЗаявке = Задание.ОтветПоЗаявке;
	Комментарий = Задание.Комментарий;
	Прочитать();
	ПоместитьДанныеИзБуфера(ОтветПоЗаявке, Комментарий);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЭтоФайлПечатнойФормы(ПрисоединенныйФайл)
	Возврат КадровыйЭДО.ЭтоФайлПечатнойФормы(ПрисоединенныйФайл);	
КонецФункции

&НаСервере
Процедура УдалитьПрисоединенныйФайлЗавершениеНаСервере()
	
	Запрос = Новый Запрос();
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗаявкаСотрудникаСправкаСМестаРаботыПрисоединенныеФайлы.Ссылка КАК Ссылка,
	               |	ЕСТЬNULL(ДокументКадровогоЭДО.Ссылка, ЗНАЧЕНИЕ(Документ.ДокументКадровогоЭДО.ПустаяСсылка)) КАК ДокументКЭДО
	               |ИЗ
	               |	Справочник.ЗаявкаСотрудникаСправкаСМестаРаботыПрисоединенныеФайлы КАК ЗаявкаСотрудникаСправкаСМестаРаботыПрисоединенныеФайлы
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ДокументКадровогоЭДО КАК ДокументКадровогоЭДО
	               |		ПО ЗаявкаСотрудникаСправкаСМестаРаботыПрисоединенныеФайлы.Ссылка = ДокументКадровогоЭДО.ЭлектронныйДокумент
	               |ГДЕ
	               |	ЗаявкаСотрудникаСправкаСМестаРаботыПрисоединенныеФайлы.ВладелецФайла = &ВладелецФайла
	               |	И ЗаявкаСотрудникаСправкаСМестаРаботыПрисоединенныеФайлы.ФайлОтвета = ИСТИНА
	               |	И ЗаявкаСотрудникаСправкаСМестаРаботыПрисоединенныеФайлы.ПометкаУдаления = ЛОЖЬ";
	
	Запрос.УстановитьПараметр("ВладелецФайла", задание.Ссылка);
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ФайлОтветаОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ФайлОтветаОбъект.ПометкаУдаления = Истина;
		ФайлОтветаОбъект.Записать();
		
		Если Выборка.ДокументКЭДО.Пустая() Тогда
			Продолжить;	
		КонецЕсли;
		
		ДокументКЭДО = Выборка.ДокументКЭДО.ПолучитьОбъект();
		ДокументКЭДО.ПометкаУдаления = Истина;
		ДокументКЭДО.ДополнительныеСвойства.Вставить("РазрешенаПометкаУдаления", Истина);
		ДокументКЭДО.Записать();
		
	КонецЦикла;
	УстановитьПривилегированныйРежим(Ложь);
	
	ЗаданиеОбъект = РеквизитФормыВЗначение("Задание"); 
	БизнесПроцессыЗаявокСотрудников.ОтметитьОжидаетПодписаниеЗаявкиСотрудника(ЗаданиеОбъект, Ложь);
	ЗначениеВРеквизитФормы(ЗаданиеОбъект, "Задание");
	ЗначениеВРеквизитФормы(Объект.Ссылка.ПолучитьОбъект(), "Объект");
	
	ОтразитьФайлыОтвета();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПечатнаяФормаСправкаСМестаРаботы(Знач Заявка)
	
	ПараметрыПолучения = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
	ПараметрыПолучения.КадровыеДанные = "Организация,ВидСобытия";
	ПараметрыПолучения.СписокФизическихЛиц = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Заявка.ФизическоеЛицо);
	ПараметрыПолучения.ОкончаниеПериода = Заявка.Дата;
	
	УстановитьПривилегированныйРежим(Истина);
	КадровыеДанные = КадровыйУчет.СотрудникиОрганизации(Ложь, ПараметрыПолучения);
	УстановитьПривилегированныйРежим(Ложь);
	КадровыеДанные.Индексы.Добавить("ФизическоеЛицо,Организация");
	
	Отбор = Новый Структура("ФизическоеЛицо,Организация", Заявка.ФизическоеЛицо, Заявка.Организация);
	НайденныеСтроки = КадровыеДанные.НайтиСтроки(Отбор);
		
	СотрудникиФизическогоЛица = Новый Массив;
	Для каждого СтрокаТЗ Из НайденныеСтроки Цикл
		Если СтрокаТЗ.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Увольнение Тогда
			Продолжить;
		КонецЕсли;
		СотрудникиФизическогоЛица.Добавить(СтрокаТЗ.Сотрудник);
	КонецЦикла;
	
	Если СотрудникиФизическогоЛица.Количество() = 0 Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'На указанную дату нет действующего трудового договора, справка не может быть подготовлена.';
													|en = 'There is no valid employment contract on the specified date. Cannot prepare the certificate.'"));
		Возврат Новый ТабличныйДокумент;
	КонецЕсли;
	
	ТабличныйДокументСправки = Новый ТабличныйДокумент;
	Если ЗначениеЗаполнено(Заявка.ВидСправки) Тогда
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.СамообслуживаниеСотрудников") Тогда
			МодульСамообслуживаниеСотрудников = ОбщегоНазначения.ОбщийМодуль("СамообслуживаниеСотрудников");
			
			ПараметрыПолученияСправок = МодульСамообслуживаниеСотрудников.НовыеПараметрыПолученияСправок();
			СтрокаТаблицыПараметров = ПараметрыПолученияСправок.Добавить();
			СтрокаТаблицыПараметров.ИдентификаторЗапроса = Заявка.ИдентификаторЗаявки;
			СтрокаТаблицыПараметров.ФизическоеЛицо = Заявка.ФизическоеЛицо;
			СтрокаТаблицыПараметров.Организация = Заявка.Организация;
			СтрокаТаблицыПараметров.ВидСправки = Заявка.ВидСправки;
			СтрокаТаблицыПараметров.ДатаАктуальности = Заявка.Дата;
			ТабличныеДокументыСправок = 
				МодульСамообслуживаниеСотрудников.ПечатныеФормыПредоставляемыхСотрудникамСправок(ПараметрыПолученияСправок);
				
			ТабличныйДокументСправки = ТабличныеДокументыСправок.Найти(
				Заявка.ИдентификаторЗаявки,
				"ИдентификаторЗапроса").Результат.ТабличныйДокумент;
		КонецЕсли;
	Иначе
		ТабличныйДокументСправки = Обработки.ПечатьКадровыхПриказовРасширенная.ПечатнаяФормаСправкаСМестаРаботы(СотрудникиФизическогоЛица);
	КонецЕсли;
	
	Возврат ТабличныйДокументСправки;
	
КонецФункции	

#КонецОбласти

&НаСервере
Процедура ИнициализироватьФормуЗадачи()
	
	БизнесПроцессыЗаявокСотрудниковФормы.ИнициализироватьФормуЗадачи(ЭтотОбъект);
	ИспользуетсяКадровыйЭДО = ПолучитьФункциональнуюОпцию("ИспользуетсяКадровыйЭДОКабинетСотрудника");
	ОтразитьФайлыОтвета();
	
	Элементы.Содержание.Заголовок = ЗаданиеСодержание;
	Элементы.Содержание.Видимость = НЕ (ЗаданиеСодержание = "");
	
	СканСПодписьюИПечатью = 
		(Задание.ВариантФормированияФайлаОтвета = Перечисления.ВариантыФормированияФайлаОтветаЗаявкиСотрудника.СканСПодписьюИПечатью);
	ВБумажномВиде =
		(Задание.ВариантФормированияФайлаОтвета = Перечисления.ВариантыФормированияФайлаОтветаЗаявкиСотрудника.ВБумажномВиде);
	ФайлСЭП =
		(Задание.ВариантФормированияФайлаОтвета = Перечисления.ВариантыФормированияФайлаОтветаЗаявкиСотрудника.ФайлСЭП);
		
	Элементы.Распечатать.Видимость = СканСПодписьюИПечатью Или ВБумажномВиде;
	Элементы.СформироватьФайл.Видимость = Не СканСПодписьюИПечатью И Не ВБумажномВиде;
	Элементы.ПодписатьЭП.Видимость = ФайлСЭП;
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ВыбратьФайлОтвета",
		"Видимость",
		Не ВБумажномВиде);
		 
	Элементы.ГруппаСправкаСМестаРаботы.Заголовок = НазваниеСправки(Задание.ВидСправки);
	
КонецПроцедуры

&НаСервере
Процедура ОтказатьЗавершениеНаСервере(Отказ)
	
	БизнесПроцессыЗаявокСотрудниковФормы.ОтказатьБизнесПроцессЗаявки(Задание.Ссылка, СостояниеЗапроса, Исполнитель);
	
	Если ЗначениеЗаполнено(Комментарий) Тогда
		Объект.РезультатВыполнения = Комментарий;
	Иначе
		Объект.РезультатВыполнения = НСтр("ru = 'Отказ';
											|en = 'Cancel'");
	КонецЕсли;
	
	ЗаданиеВыполнено = Истина;

	// Удаляем документ КЭДО справки с места работы.
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	ДокументКадровогоЭДО.Ссылка КАК Ссылка
	               |ИЗ
	               |	Документ.ДокументКадровогоЭДО КАК ДокументКадровогоЭДО
	               |ГДЕ
	               |	ДокументКадровогоЭДО.ОснованиеДокумента = &ЗаявкаСотрудника
	               |	И ДокументКадровогоЭДО.КатегорияДокумента = ЗНАЧЕНИЕ(Перечисление.КатегорииДокументовКадровогоЭДО.СправкаСотруднику)
	               |	И ДокументКадровогоЭДО.ПометкаУдаления = ЛОЖЬ";
		
	Запрос.УстановитьПараметр("ЗаявкаСотрудника", Задание.Ссылка);
		
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ДокументКЭДООбъект = Выборка.Ссылка.ПолучитьОбъект();
	    ДокументКЭДООбъект.ПометкаУдаления = Истина;
		ДокументКЭДООбъект.ДополнительныеСвойства.Вставить("РазрешенаПометкаУдаления", Истина);
		ДокументКЭДООбъект.Записать();
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьКнопокДействий()
	
	Действия = Новый Массив;
	Действия.Добавить("Выполнено");
	Действия.Добавить("Отказать");
	Действия.Добавить("СформироватьФайл");
	Действия.Добавить("ПодписатьЭП");
	Действия.Добавить("ВыбратьФайл");
	Действия.Добавить("Распечатать");
	
	ДоступностьДействий = РеквизитФормыВЗначение("Задание").ДоступностьДействий(Действия, ЭтотОбъект);
	
	Элементы.Выполнено.Доступность 			= ДоступностьДействий["Выполнено"];
	Элементы.Отказать.Доступность			= ДоступностьДействий["Отказать"];
	Элементы.СформироватьФайл.Доступность	= ДоступностьДействий["СформироватьФайл"];
	Элементы.ПодписатьЭП.Доступность		= ДоступностьДействий["ПодписатьЭП"];
	Элементы.Распечатать.Доступность		= ДоступностьДействий["Распечатать"];
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ВыбратьФайлОтвета",
		"Доступность",
		ДоступностьДействий["ВыбратьФайл"]);
			
КонецПроцедуры

&НаСервереБезКонтекста
Функция НазваниеСправки(ВидСправки) 
	
	Если Не ЗначениеЗаполнено(ВидСправки) Тогда
		Возврат НСтр("ru = 'Справка с места работы';
					|en = 'Statement of employment'");
	КонецЕсли;
	
	ПредставлениеСправки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидСправки, "ПредставлениеСправки");
	ПредставлениеСправки = НРег(Лев(ПредставлениеСправки, 1)) + Прав(ПредставлениеСправки, СтрДлина(ПредставлениеСправки)-1);
	
	Возврат СтрШаблон(НСтр("ru = 'Справка %1';
							|en = 'Statement %1'"), ПредставлениеСправки);
	
КонецФункции

&НаСервере
Процедура ПоместитьДанныеИзБуфера(ОтветПоЗаявке, Комментарий)
	ЗаданиеОбъект = РеквизитФормыВЗначение("Задание");
	ЗаданиеОбъект.ОтветПоЗаявке = ОтветПоЗаявке;
	ЗаданиеОбъект.Комментарий = Комментарий;
	ЗначениеВРеквизитФормы(ЗаданиеОбъект, "Задание");
КонецПроцедуры

#КонецОбласти

