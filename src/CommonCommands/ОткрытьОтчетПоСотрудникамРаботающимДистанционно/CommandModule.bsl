
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ПользовательскиеНастройкиОтчета = ПользовательскиеНастройкиОтчетаПоСотрудникам();
	
	ПараметрыОтчета = Новый Структура("СформироватьПриОткрытии, КлючВарианта, ПользовательскиеНастройки",
		Истина, "СотрудникиНаДистанционнойРаботе", ПользовательскиеНастройкиОтчета);
	
	ОткрытьФорму("Отчет.СотрудникиНаДистанционнойРаботе.Форма", ПараметрыОтчета);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПользовательскиеНастройкиОтчетаПоСотрудникам()
	
	ОтчетОбъект = Отчеты.СотрудникиНаДистанционнойРаботе.Создать();
	ОтчетОбъект.КомпоновщикНастроек.ЗагрузитьНастройки(
		ОтчетОбъект.СхемаКомпоновкиДанных.ВариантыНастроек.Найти("СотрудникиНаДистанционнойРаботе").Настройки);
	
	ИдентификаторНастройки = ИдентификаторПользовательскойНастройкиПоИмени(
		ОтчетОбъект.КомпоновщикНастроек.Настройки.Отбор, "РабочееМесто.РаботаетДистанционно");
	Если Не ИдентификаторНастройки = Неопределено Тогда
		УстановитьОтборПоИдентификаторуПользовательскойНастройки(
			ОтчетОбъект.КомпоновщикНастроек.ПользовательскиеНастройки, 
			ИдентификаторНастройки, Истина);
	КонецЕсли;
	
	Возврат ОтчетОбъект.КомпоновщикНастроек.ПользовательскиеНастройки;
	
КонецФункции

&НаСервере
Функция ИдентификаторПользовательскойНастройкиПоИмени(Настройки, ИмяПоля)
	
	Для Каждого ЭлементНастройки Из Настройки.Элементы Цикл
		Если Не ТипЗнч(ЭлементНастройки) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
			Продолжить;
		КонецЕсли;
		Если ЭлементНастройки.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПоля) Тогда
			Возврат ЭлементНастройки.ИдентификаторПользовательскойНастройки;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

&НаСервере
Процедура УстановитьОтборПоИдентификаторуПользовательскойНастройки(Настройки, ИдентификаторНастройки, Значение)
	
	Для Каждого ЭлементНастройки Из Настройки.Элементы Цикл
		Если ЭлементНастройки.ИдентификаторПользовательскойНастройки = ИдентификаторНастройки Тогда
			ЭлементНастройки.Использование = Истина;
			ЭлементНастройки.ПравоеЗначение = Значение;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
