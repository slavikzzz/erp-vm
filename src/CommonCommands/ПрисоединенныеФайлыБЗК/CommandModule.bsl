#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ШаблонЗаголовка = НСтр("ru = 'Присоединенные файлы: %1';
							|en = 'Attached files: %1'");
	ТекстЗаголовка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонЗаголовка, ПараметрКоманды);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВладелецФайла",  ПараметрКоманды);
	ПараметрыФормы.Вставить("ТолькоПросмотр", ПараметрыВыполненияКоманды.Источник.ТолькоПросмотр);
	ПараметрыФормы.Вставить("ПростаяФорма", Истина);
	ПараметрыФормы.Вставить("ЗаголовокФормы", ТекстЗаголовка);
	
	// ИнтеграцияС1СДокументооборотом
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом") Тогда
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентПовтИсп = ОбщегоНазначенияКлиент.ОбщийМодуль(
			"ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентПовтИсп");
		Если МодульИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентПовтИсп.ИспользоватьПрисоединенныеФайлы1СДокументооборота() Тогда
			МодульИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент =
				ОбщегоНазначенияКлиент.ОбщийМодуль("ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент");
			ОписаниеОповещения = Новый ОписаниеОповещения("ПроверитьПодключениеЗавершение",
				ЭтотОбъект,
				Новый Структура("ПараметрыФормы, ПараметрыВыполненияКоманды", ПараметрыФормы, ПараметрыВыполненияКоманды));
			МодульИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ПроверитьПодключение(
				ОписаниеОповещения,
				ЭтотОбъект,
				Истина);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	// Конец ИнтеграцияС1СДокументооборотом
	
	ОткрытьФорму("Обработка.РаботаСФайлами.Форма.ПрисоединенныеФайлы",
		ПараметрыФормы,
		ПараметрыВыполненияКоманды.Источник,
		ПараметрыВыполненияКоманды.Уникальность,
		ПараметрыВыполненияКоманды.Окно);
	
КонецПроцедуры

// ИнтеграцияС1СДокументооборотом
&НаКлиенте
Процедура ПроверитьПодключениеЗавершение(Результат, Параметры) Экспорт
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом") Тогда
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервера = ОбщегоНазначенияКлиент.ОбщийМодуль(
			"ИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервера");
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль(
			"ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент");
		ПравилоИнтеграции = Неопределено;
		
		Если Результат
				И МодульИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервера.ИспользоватьПрисоединенныеФайлы1СДокументооборотаДляОбъекта(
					Параметры.ПараметрыФормы.ВладелецФайла, ПравилоИнтеграции) Тогда
			МодульИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОткрытьПрисоединенныеФайлы(
				Параметры.ПараметрыФормы.ВладелецФайла,,
				Параметры.ПараметрыФормы.ТолькоПросмотр,
				Параметры.ПараметрыВыполненияКоманды.Источник,
				Параметры.ПараметрыВыполненияКоманды.Уникальность,
				Параметры.ПараметрыВыполненияКоманды.Окно,
				ПравилоИнтеграции);
		Иначе
			ОткрытьФорму("Обработка.РаботаСФайлами.Форма.ПрисоединенныеФайлы",
				Параметры.ПараметрыФормы,
				Параметры.ПараметрыВыполненияКоманды.Источник,
				Параметры.ПараметрыВыполненияКоманды.Уникальность,
				Параметры.ПараметрыВыполненияКоманды.Окно);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
// Конец ИнтеграцияС1СДокументооборотом

#КонецОбласти