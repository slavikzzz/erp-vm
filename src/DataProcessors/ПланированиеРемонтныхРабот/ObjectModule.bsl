#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

Перем МаксимальныйКодРемонта; // Число - Счетчик номеров ремонтных работ, используемых как уникальные идентификаторы ремонтов в рамках одной процедуры планирования.
Перем НачалоПланирования; // Дата - Дата начала планирования ремонтов.

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Рассчитывает план ремонтных работ - заполняет табличные части "ЗаказыНаРемонт" и "Ремонты"
// 		по данным табличной части "ОбъектыРемонтаКПланированию".
//
Процедура РассчитатьПланРемонтныхРабот() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗаказыНаРемонт.Очистить();
	Ремонты.Очистить();
	МатериалыИРаботы.Очистить();
	Трудозатраты.Очистить();
	
	НачалоПланирования = НачалоДня(КонецДня(ТекущаяДатаСеанса())+1);
	ОкончаниеПланирования = НачалоПланирования;
	
	РезультатыЗапроса = ДанныеДляРасчетаПланаРемонтныхРабот();
	
	Если РезультатыЗапроса.ОбъектыПланирования.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	СписокГрафиков = РезультатыЗапроса.ГрафикиРаботы.Выгрузить().ВыгрузитьКолонку("ГрафикРаботы");
	
	РезультатЗапроса = РезультатыЗапроса.МаксимальнаяДатаРемонта; // РезультатЗапроса
	ВыборкаДатаОкончания = РезультатЗапроса.Выбрать();
	Если ВыборкаДатаОкончания.Следующий() Тогда
		ОкончаниеПланирования = ВыборкаДатаОкончания.МаксимальнаяДатаРемонта;
	КонецЕсли;
	
	РезультатЗапроса = РезультатыЗапроса.МаксимальнаяДлительностьРемонта; // РезультатЗапроса
	ВыборкаДлительность = РезультатЗапроса.Выбрать();
	Если ВыборкаДлительность.Следующий() Тогда
		ОкончаниеПланирования = ОкончаниеПланирования + Цел(ВыборкаДлительность.Длительность) * 86400;
	КонецЕсли;
	
	УправлениеРемонтами.СоздатьГрафикРаботыПоМинутам(
		МенеджерВременныхТаблиц, СписокГрафиков, НачалоПланирования, КонецДня(КонецДня(ОкончаниеПланирования) + 1));
	
	КэшированныеЗначения = Новый Структура;
	КэшированныеЗначения.Вставить("МенеджерВременныхТаблиц", МенеджерВременныхТаблиц);
	
	КэшированныеЗначения.Вставить("Выборки", Новый Структура);
	
	РезультатЗапроса = РезультатыЗапроса.ПредыдущиеРемонты; // РезультатЗапроса
	КэшированныеЗначения.Выборки.Вставить("ПредыдущиеРемонты", РезультатЗапроса.Выбрать());
	
	РезультатЗапроса = РезультатыЗапроса.Наработки; // РезультатЗапроса
	КэшированныеЗначения.Выборки.Вставить("Наработки", РезультатЗапроса.Выбрать());
	
	РезультатЗапроса = РезультатыЗапроса.ОбъектыПланирования; // РезультатЗапроса
	КэшированныеЗначения.Выборки.Вставить("ОбъектыПланирования", РезультатЗапроса.Выбрать());
	
	РезультатЗапроса = РезультатыЗапроса.РемонтныеЦиклы; // РезультатЗапроса
	КэшированныеЗначения.Выборки.Вставить("РемонтныеЦиклы", РезультатЗапроса.Выбрать());
	
	РезультатЗапроса = РезультатыЗапроса.ПрочиеРемонты; // РезультатЗапроса
	КэшированныеЗначения.Выборки.Вставить("ПрочиеРемонты", РезультатЗапроса.Выбрать());
	
	РезультатЗапроса = РезультатыЗапроса.НаработкиПослеРемонтов; // РезультатЗапроса
	КэшированныеЗначения.Выборки.Вставить("НаработкиПослеРемонтов", РезультатЗапроса.Выбрать());
	
	МаксимальныйКодРемонта = 1;
	
	ВыборкаОбъектовПланирования = КэшированныеЗначения.Выборки.ОбъектыПланирования;
	Пока ВыборкаОбъектовПланирования.Следующий() Цикл
		
		ЗаполнитьРемонтыПоЦиклу(ВыборкаОбъектовПланирования, КэшированныеЗначения);
		ЗаполнитьПрочиеРемонты(ВыборкаОбъектовПланирования, КэшированныеЗначения);
		
	КонецЦикла;
	
КонецПроцедуры

// Создает и заполняет документы заказы на ремонт
// 		по данным табличных частей "ЗаказыНаРемонт", "Ремонты", "МатериалыИРаботы", "Трудозатраты", "РабочиеЦентры".
//
// Параметры:
// 		УдаляемыеЗаказы - Массив - Массив ссылок на помеченные на удаление заказы, которые можно использовать при заполнении новых заказов.
//
// Возвращаемое значение:
// 		Структура - Структура с полями:
// 			"МассивЗаказовНаРемонт" - Массив созданных заказов
// 			"ПереопределенныеСообщения" - Соответствие массивов ошибок по ссылкам на созданные документы.
//
Функция СформироватьЗаказыНаРемонт(УдаляемыеЗаказы) Экспорт
	
	СтруктураВозвращаемыхЗначений = Новый Структура();
	
	СтруктураВозвращаемыхЗначений.Вставить("МассивЗаказовНаРемонт", Новый Массив);
	СтруктураВозвращаемыхЗначений.Вставить("ПереопределенныеСообщения", Новый Соответствие);
	
	
	СтруктураЗначенийРеквизитовДокумента = Новый Структура;
	СтруктураЗначенийРеквизитовДокумента.Вставить("Дата", ТекущаяДатаСеанса());
	СтруктураЗначенийРеквизитовДокумента.Вставить("Статус", Перечисления.СтатусыЗаказовНаРемонт.Создан);
	
	Для Каждого СтрокаЗаказНаРемонт Из ЗаказыНаРемонт Цикл
		
		Если Ремонты.НайтиСтроки(Новый Структура("КодЗаказаНаРемонт", СтрокаЗаказНаРемонт.КодЗаказаНаРемонт)).Количество() = 0 Тогда
			// Если отсутствуют запланированные ремонты по этому заказу
			Продолжить;
		КонецЕсли;
		
		ПроводитьДокумент = Истина;
		ПроведениеВыполнено = Истина;
		
		Если УдаляемыеЗаказы.Количество() = 0 Тогда
			ОбъектДокумента = Документы.ЗаказНаРемонт.СоздатьДокумент();
		Иначе
			ОбъектДокумента = УдаляемыеЗаказы[0].ПолучитьОбъект();
			ОбъектДокумента.ПометкаУдаления = Ложь;
			УдаляемыеЗаказы.Удалить(0);
		КонецЕсли;
		ОбъектДокумента.Ремонты.Очистить();
		ОбъектДокумента.МатериалыИРаботы.Очистить();
		ОбъектДокумента.Трудозатраты.Очистить();
		ОбъектДокумента.РабочиеЦентры.Очистить();
		ОбъектДокумента.УстраняемыеДефекты.Очистить();
		ОбъектДокумента.Заполнить(СтруктураЗначенийРеквизитовДокумента);
		
		ЗаполнитьЗначенияСвойств(
			ОбъектДокумента, 
			СтрокаЗаказНаРемонт, 
			"ОбъектЭксплуатации, ДатаНачала, ДатаЗавершения, Подразделение, РемонтноеМероприятие, ОбщийВидРемонта");
		
		ЗаполнитьТабличныеЧастиОбъектаДокумента(ОбъектДокумента, СтрокаЗаказНаРемонт.КодЗаказаНаРемонт);
		Документы.ЗаказНаРемонт.ЗаполнитьСкладПоНастройкамПередачиМатериалов(ОбъектДокумента);
		
		ПроводитьДокумент = ОбъектДокумента.ПроверитьЗаполнение();
		
		Если ПроводитьДокумент Тогда
			Попытка
				ОбъектДокумента.Записать(РежимЗаписиДокумента.Проведение);
			Исключение
				ПроведениеВыполнено = Ложь;
			КонецПопытки;
		Иначе
			ПроведениеВыполнено = Ложь;
		КонецЕсли;
		
		Если Не ПроведениеВыполнено Тогда
			Попытка
				ОбъектДокумента.Записать(РежимЗаписиДокумента.Запись);
			Исключение
				СтруктураВозвращаемыхЗначений.МассивЗаказовНаРемонт.Очистить();
				ВызватьИсключение НСтр("ru = 'Ошибка при попытке создания документа заказа на ремонт:';
										|en = 'An error occurred while trying to create the R&M order'") + " " + ИнформацияОбОшибке().Описание;
			КонецПопытки;
		КонецЕсли;
		
		СтруктураВозвращаемыхЗначений.МассивЗаказовНаРемонт.Добавить(ОбъектДокумента.Ссылка);
		
		МассивОшибок = Новый Массив;
		СтруктураВозвращаемыхЗначений.ПереопределенныеСообщения.Вставить(ОбъектДокумента.Ссылка, МассивОшибок);
		ОшибкиФормированияДокументов = ПолучитьСообщенияПользователю(Истина);
		Для Каждого ОшибкаФормированияДокумента Из ОшибкиФормированияДокументов Цикл
			ОшибкаФормированияДокумента.КлючДанных = ОбъектДокумента.Ссылка;
			МассивОшибок.Добавить(ОшибкаФормированияДокумента);
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат СтруктураВозвращаемыхЗначений;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// 
// Возвращаемое значение:
// 	Структура - см. ОбщегоНазначенияУТ.ПолучитьРезультатыЗапроса
Функция ДанныеДляРасчетаПланаРемонтныхРабот()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПланирования", НачалоПланирования);
	Запрос.УстановитьПараметр("ПоследняяДатаГода", НачалоДня(КонецГода(НачалоПланирования)));
	Запрос.УстановитьПараметр("ТекущийГод", Год(НачалоПланирования));
	Запрос.УстановитьПараметр("СледующийГод", Год(НачалоПланирования) + 1);
	Запрос.УстановитьПараметр("МассивОбъектовДляРемонта", ОбъектыРемонтаКПланированию.ВыгрузитьКолонку("ОбъектЭксплуатации"));
	Запрос.УстановитьПараметр("ОбщиеВидыРемонтов", ОбщиеВидыРемонтовКПланированию.ВыгрузитьКолонку("ОбщийВидРемонта"));
	Запрос.УстановитьПараметр("ЧастотыПланирования", ЧастотыКПланированию.ВыгрузитьКолонку("ЧастотаПланирования"));
	Запрос.УстановитьПараметр("ОсновнойКалендарьПредприятия", Константы.ОсновнойКалендарьПредприятия.Получить());
	
	СписокЗапросов = Новый СписокЗначений();
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ОбъектыРемонта.Ссылка КАК ОбъектЭксплуатации,
	|
	|	ВЫБОР 
	|		КОГДА ОбъектыРемонта.РемонтирующееПодразделение.СпособНастройкиГрафикаРаботы = ЗНАЧЕНИЕ(Перечисление.СпособыНастройкиГрафикаРаботыПодразделений.ОсновнойКалендарьПредприятия)
	|			ТОГДА &ОсновнойКалендарьПредприятия
	|		КОГДА ОбъектыРемонта.РемонтирующееПодразделение.СпособНастройкиГрафикаРаботы = ЗНАЧЕНИЕ(Перечисление.СпособыНастройкиГрафикаРаботыПодразделений.ИндивидуальныйГрафик)
	|			ТОГДА ЕСТЬNULL(ГрафикиПодразделения.ГрафикРаботы, ЗНАЧЕНИЕ(Справочник.Календари.ПустаяСсылка))
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ГрафикРаботы,
	|
	|	ВЫБОР
	|		КОГДА ОбъектыРемонта.Класс.ИспользуютсяПодклассы
	|			ТОГДА ОбъектыРемонта.Подкласс
	|		ИНАЧЕ ОбъектыРемонта.Класс
	|	КОНЕЦ КАК ХранилищеППР,
	|
	|	ОбъектыРемонта.ДатаВыпуска КАК ДатаВводаВЭксплуатацию
	|
	|ПОМЕСТИТЬ ОбъектыПланирования
	|ИЗ
	|	Справочник.ОбъектыЭксплуатации КАК ОбъектыРемонта
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтруктураПредприятия.ГрафикиРаботы КАК ГрафикиПодразделения
	|		ПО ГрафикиПодразделения.Ссылка = ОбъектыРемонта.РемонтирующееПодразделение
	|			И ГрафикиПодразделения.НомерСтроки = 1
	|ГДЕ
	|	ОбъектыРемонта.Ссылка В(&МассивОбъектовДляРемонта)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОбъектыРемонта.Ссылка,
	|
	|	ВЫБОР 
	|		КОГДА ОбъектыРемонта.РемонтирующееПодразделение.СпособНастройкиГрафикаРаботы = ЗНАЧЕНИЕ(Перечисление.СпособыНастройкиГрафикаРаботыПодразделений.ОсновнойКалендарьПредприятия)
	|			ТОГДА &ОсновнойКалендарьПредприятия
	|		КОГДА ОбъектыРемонта.РемонтирующееПодразделение.СпособНастройкиГрафикаРаботы = ЗНАЧЕНИЕ(Перечисление.СпособыНастройкиГрафикаРаботыПодразделений.ИндивидуальныйГрафик)
	|			ТОГДА ЕСТЬNULL(ГрафикиПодразделения.ГрафикРаботы, ЗНАЧЕНИЕ(Справочник.Календари.ПустаяСсылка))
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ГрафикРаботы,
	|
	|	ВЫБОР
	|		КОГДА ОбъектыРемонта.Класс.ИспользуютсяПодклассы
	|			ТОГДА ОбъектыРемонта.Подкласс
	|		ИНАЧЕ ОбъектыРемонта.Класс
	|	КОНЕЦ,
	|
	|	ОбъектыРемонта.Владелец.ДатаВыпуска
	|ИЗ
	|	Справочник.УзлыОбъектовЭксплуатации КАК ОбъектыРемонта
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтруктураПредприятия.ГрафикиРаботы КАК ГрафикиПодразделения
	|		ПО ГрафикиПодразделения.Ссылка = ОбъектыРемонта.РемонтирующееПодразделение
	|			И ГрафикиПодразделения.НомерСтроки = 1
	|ГДЕ
	|	ОбъектыРемонта.Ссылка В(&МассивОбъектовДляРемонта)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектЭксплуатации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыРемонтов.Ссылка КАК ВидРемонта,
	|	ВЫБОР
	|		КОГДА НЕ РемонтВТекущемГоду.ДатаГрафика ЕСТЬ NULL
	|			ТОГДА РемонтВТекущемГоду.ДатаГрафика
	|		КОГДА НЕ РемонтВСледующемГоду.ДатаГрафика ЕСТЬ NULL
	|			ТОГДА РемонтВСледующемГоду.ДатаГрафика
	|		ИНАЧЕ ДОБАВИТЬКДАТЕ(ВЫРАЗИТЬ(&НачалоПланирования КАК ДАТА), ДЕНЬ, ВидыРемонтов.Упреждение)
	|	КОНЕЦ КАК МаксимальнаяДатаРемонта,
	|	ВидыРемонтов.Длительность,
	|	ВидыРемонтов.ЕдиницаИзмеренияДлительности
	|ПОМЕСТИТЬ РазрешенныеВидыРемонтов
	|ИЗ
	|	Справочник.ВидыРемонтов КАК ВидыРемонтов
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КалендарныеГрафики КАК НачалоПланирования
	|		ПО НачалоПланирования.Календарь = &ОсновнойКалендарьПредприятия
	|			И НачалоПланирования.Год = &ТекущийГод
	|			И НачалоПланирования.ДатаГрафика = &НачалоПланирования
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КалендарныеГрафики КАК РемонтВТекущемГоду
	|		ПО РемонтВТекущемГоду.Календарь = &ОсновнойКалендарьПредприятия
	|		 И РемонтВТекущемГоду.ДеньВключенВГрафик
	|		 И РемонтВТекущемГоду.Год = &ТекущийГод
	|		 И РемонтВТекущемГоду.КоличествоДнейВГрафикеСНачалаГода
	|			= НачалоПланирования.КоличествоДнейВГрафикеСНачалаГода
	|			+ ВидыРемонтов.Упреждение
	|			+ ВЫБОР
	|				КОГДА НачалоПланирования.ДеньВключенВГрафик ТОГДА 0
	|				ИНАЧЕ 1
	|			КОНЕЦ
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КалендарныеГрафики КАК ПоследняяДатаГода
	|		ПО РемонтВТекущемГоду.ДатаГрафика ЕСТЬ NULL
	|			И ПоследняяДатаГода.Календарь = &ОсновнойКалендарьПредприятия
	|			И ПоследняяДатаГода.Год = &ТекущийГод
	|			И ПоследняяДатаГода.ДатаГрафика = &ПоследняяДатаГода
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КалендарныеГрафики КАК РемонтВСледующемГоду
	|		ПО РемонтВСледующемГоду.Календарь = &ОсновнойКалендарьПредприятия
	|			И РемонтВСледующемГоду.ДеньВключенВГрафик
	|			И РемонтВСледующемГоду.Год = &СледующийГод
	|			И РемонтВСледующемГоду.КоличествоДнейВГрафикеСНачалаГода
	|				= ВидыРемонтов.Упреждение
	|				- (ПоследняяДатаГода.КоличествоДнейВГрафикеСНачалаГода
	|					- НачалоПланирования.КоличествоДнейВГрафикеСНачалаГода)
	|				+ ВЫБОР
	|					КОГДА НачалоПланирования.ДеньВключенВГрафик ТОГДА 0
	|					ИНАЧЕ 1
	|				КОНЕЦ
	|ГДЕ
	|	ВидыРемонтов.ОбщийВидРемонта В(&ОбщиеВидыРемонтов)
	|	И ВидыРемонтов.ЧастотаПланирования В(&ЧастотыПланирования)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КлассыРемонтныйЦикл.Ссылка КАК ХранилищеППР,
	|	КлассыРемонтныйЦикл.НомерСтроки,
	|	КлассыРемонтныйЦикл.ВидРемонта,
	|	КлассыРемонтныйЦикл.Ссылка.РемонтныйЦиклПоНаработке,
	|	КлассыРемонтныйЦикл.ИнтервалВремени,
	|	КлассыРемонтныйЦикл.ЕдиницаВремени,
	|	КлассыРемонтныйЦикл.ИнтервалНаработки,
	|	КлассыРемонтныйЦикл.ПоказательНаработки,
	|	ВЫБОР
	|		КОГДА РазрешенныеВидыРемонтов.ВидРемонта ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Разрешен,
	|	ЕСТЬNULL(РазрешенныеВидыРемонтов.МаксимальнаяДатаРемонта, НЕОПРЕДЕЛЕНО) КАК МаксимальнаяДатаРемонта,
	|	ЕСТЬNULL(РазрешенныеВидыРемонтов.Длительность, 0) КАК Длительность,
	|	ЕСТЬNULL(РазрешенныеВидыРемонтов.ЕдиницаИзмеренияДлительности, НЕОПРЕДЕЛЕНО) КАК ЕдиницаИзмеренияДлительности
	|ПОМЕСТИТЬ РемонтныеЦиклы
	|ИЗ
	|	Справочник.КлассыОбъектовЭксплуатации.РемонтныйЦикл КАК КлассыРемонтныйЦикл
	|		ЛЕВОЕ СОЕДИНЕНИЕ РазрешенныеВидыРемонтов КАК РазрешенныеВидыРемонтов
	|		ПО КлассыРемонтныйЦикл.ВидРемонта = РазрешенныеВидыРемонтов.ВидРемонта
	|ГДЕ
	|	КлассыРемонтныйЦикл.Ссылка В
	|			(ВЫБРАТЬ
	|				ОбъектыПланирования.ХранилищеППР
	|			ИЗ
	|				ОбъектыПланирования КАК ОбъектыПланирования)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПодклассыРемонтныйЦикл.Ссылка,
	|	ПодклассыРемонтныйЦикл.НомерСтроки,
	|	ПодклассыРемонтныйЦикл.ВидРемонта,
	|	ПодклассыРемонтныйЦикл.Ссылка.РемонтныйЦиклПоНаработке,
	|	ПодклассыРемонтныйЦикл.ИнтервалВремени,
	|	ПодклассыРемонтныйЦикл.ЕдиницаВремени,
	|	ПодклассыРемонтныйЦикл.ИнтервалНаработки,
	|	ПодклассыРемонтныйЦикл.ПоказательНаработки,
	|	ВЫБОР
	|		КОГДА РазрешенныеВидыРемонтов.ВидРемонта ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ,
	|	ЕСТЬNULL(РазрешенныеВидыРемонтов.МаксимальнаяДатаРемонта, НЕОПРЕДЕЛЕНО),
	|	ЕСТЬNULL(РазрешенныеВидыРемонтов.Длительность, 0),
	|	ЕСТЬNULL(РазрешенныеВидыРемонтов.ЕдиницаИзмеренияДлительности, НЕОПРЕДЕЛЕНО)
	|ИЗ
	|	Справочник.ПодклассыОбъектовЭксплуатации.РемонтныйЦикл КАК ПодклассыРемонтныйЦикл
	|		ЛЕВОЕ СОЕДИНЕНИЕ РазрешенныеВидыРемонтов КАК РазрешенныеВидыРемонтов
	|		ПО ПодклассыРемонтныйЦикл.ВидРемонта = РазрешенныеВидыРемонтов.ВидРемонта
	|ГДЕ
	|	ПодклассыРемонтныйЦикл.Ссылка В
	|			(ВЫБРАТЬ
	|				ОбъектыПланирования.ХранилищеППР
	|			ИЗ
	|				ОбъектыПланирования КАК ОбъектыПланирования)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КлассыПрочиеРемонты.Ссылка КАК ХранилищеППР,
	|	КлассыПрочиеРемонты.НомерСтроки,
	|	КлассыПрочиеРемонты.ВидРемонта,
	|	КлассыПрочиеРемонты.ПоНаработке,
	|	КлассыПрочиеРемонты.ИнтервалВремени,
	|	КлассыПрочиеРемонты.ЕдиницаВремени,
	|	КлассыПрочиеРемонты.ИнтервалНаработки,
	|	КлассыПрочиеРемонты.ПоказательНаработки,
	|	РазрешенныеВидыРемонтов.МаксимальнаяДатаРемонта,
	|	РазрешенныеВидыРемонтов.Длительность,
	|	РазрешенныеВидыРемонтов.ЕдиницаИзмеренияДлительности
	|ПОМЕСТИТЬ ПрочиеРемонты
	|ИЗ
	|	Справочник.КлассыОбъектовЭксплуатации.ПрочиеРемонты КАК КлассыПрочиеРемонты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РазрешенныеВидыРемонтов КАК РазрешенныеВидыРемонтов
	|		ПО КлассыПрочиеРемонты.ВидРемонта = РазрешенныеВидыРемонтов.ВидРемонта
	|ГДЕ
	|	КлассыПрочиеРемонты.Ссылка В
	|			(ВЫБРАТЬ
	|				ОбъектыПланирования.ХранилищеППР
	|			ИЗ
	|				ОбъектыПланирования КАК ОбъектыПланирования)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПодклассыПрочиеРемонты.Ссылка,
	|	ПодклассыПрочиеРемонты.НомерСтроки,
	|	ПодклассыПрочиеРемонты.ВидРемонта,
	|	ПодклассыПрочиеРемонты.ПоНаработке,
	|	ПодклассыПрочиеРемонты.ИнтервалВремени,
	|	ПодклассыПрочиеРемонты.ЕдиницаВремени,
	|	ПодклассыПрочиеРемонты.ИнтервалНаработки,
	|	ПодклассыПрочиеРемонты.ПоказательНаработки,
	|	РазрешенныеВидыРемонтов.МаксимальнаяДатаРемонта,
	|	РазрешенныеВидыРемонтов.Длительность,
	|	РазрешенныеВидыРемонтов.ЕдиницаИзмеренияДлительности
	|ИЗ
	|	Справочник.ПодклассыОбъектовЭксплуатации.ПрочиеРемонты КАК ПодклассыПрочиеРемонты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РазрешенныеВидыРемонтов КАК РазрешенныеВидыРемонтов
	|		ПО ПодклассыПрочиеРемонты.ВидРемонта = РазрешенныеВидыРемонтов.ВидРемонта
	|ГДЕ
	|	ПодклассыПрочиеРемонты.Ссылка В
	|			(ВЫБРАТЬ
	|				ОбъектыПланирования.ХранилищеППР
	|			ИЗ
	|				ОбъектыПланирования КАК ОбъектыПланирования)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ПланРемонтов.Период) КАК Период,
	|	ПланРемонтов.ОбъектЭксплуатации КАК ОбъектЭксплуатации
	|ПОМЕСТИТЬ ПоследниеРемонтыПериод
	|ИЗ
	|	РегистрСведений.ПланРемонтов КАК ПланРемонтов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОбъектыПланирования КАК ОбъектыПланирования
	|		ПО ПланРемонтов.ОбъектЭксплуатации = ОбъектыПланирования.ОбъектЭксплуатации
	|ГДЕ
	|	ПланРемонтов.РемонтныйЦикл
	|		
	|СГРУППИРОВАТЬ ПО
	|	ПланРемонтов.ОбъектЭксплуатации
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	ОбъектЭксплуатации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПланРемонтов.ОбъектЭксплуатации КАК ОбъектЭксплуатации,
	|	ПланРемонтов.ВидРемонта КАК ВидРемонта,
	|	ПланРемонтов.РемонтныйЦикл КАК РемонтныйЦикл,
	|	ПланРемонтов.Период КАК ДатаРемонта
	|ПОМЕСТИТЬ ПоследниеРемонты
	|ИЗ
	|	РегистрСведений.ПланРемонтов КАК ПланРемонтов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПоследниеРемонтыПериод КАК Последние
	|		ПО ПланРемонтов.Период = Последние.Период
	|			И ПланРемонтов.ОбъектЭксплуатации = Последние.ОбъектЭксплуатации
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПланРемонтовСрезПоследних.ОбъектЭксплуатации,
	|	ПланРемонтовСрезПоследних.ВидРемонта,
	|	ПланРемонтовСрезПоследних.РемонтныйЦикл,
	|	ПланРемонтовСрезПоследних.Период
	|ИЗ
	|	РегистрСведений.ПланРемонтов.СрезПоследних(
	|			,
	|			НЕ РемонтныйЦикл
	|				И ОбъектЭксплуатации В
	|					(ВЫБРАТЬ
	|						ОбъектыПланирования.ОбъектЭксплуатации
	|					ИЗ
	|						ОбъектыПланирования КАК ОбъектыПланирования)) КАК ПланРемонтовСрезПоследних";
	СписокЗапросов.Добавить(ТекстЗапроса);

	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОбъектыПланирования.ГрафикРаботы КАК ГрафикРаботы
	|ИЗ
	|	ОбъектыПланирования КАК ОбъектыПланирования";
	СписокЗапросов.Добавить(ТекстЗапроса, "ГрафикиРаботы");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(МАКСИМУМ(РазрешенныеВидыРемонтов.МаксимальнаяДатаРемонта), &НачалоПланирования) КАК МаксимальнаяДатаРемонта
	|ИЗ
	|	РазрешенныеВидыРемонтов КАК РазрешенныеВидыРемонтов";
	СписокЗапросов.Добавить(ТекстЗапроса, "МаксимальнаяДатаРемонта");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(МАКСИМУМ(Ремонты.Длительность), 0) КАК Длительность
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА Ремонты.ЕдиницаИзмеренияДлительности = ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.День)
	|				ТОГДА Ремонты.Длительность
	|			КОГДА Ремонты.ЕдиницаИзмеренияДлительности = ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Час)
	|				ТОГДА Ремонты.Длительность / 24
	|			КОГДА Ремонты.ЕдиницаИзмеренияДлительности = ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Минута)
	|				ТОГДА Ремонты.Длительность / 24 / 60
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК Длительность
	|	ИЗ
	|		РемонтныеЦиклы КАК Ремонты
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА Ремонты.ЕдиницаИзмеренияДлительности = ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.День)
	|				ТОГДА Ремонты.Длительность
	|			КОГДА Ремонты.ЕдиницаИзмеренияДлительности = ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Час)
	|				ТОГДА Ремонты.Длительность / 24
	|			КОГДА Ремонты.ЕдиницаИзмеренияДлительности = ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Минута)
	|				ТОГДА Ремонты.Длительность / 24 / 60
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	ИЗ
	|		ПрочиеРемонты КАК Ремонты) КАК Ремонты";
	СписокЗапросов.Добавить(ТекстЗапроса, "МаксимальнаяДлительностьРемонта");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПоследниеРемонты.ОбъектЭксплуатации КАК ОбъектЭксплуатации,
	|	ПоследниеРемонты.ВидРемонта КАК ВидРемонта,
	|	ПоследниеРемонты.ДатаРемонта КАК Период
	|ИЗ
	|	ПоследниеРемонты КАК ПоследниеРемонты
	|ГДЕ
	|	НЕ ПоследниеРемонты.РемонтныйЦикл";
	СписокЗапросов.Добавить(ТекстЗапроса, "ПредыдущиеРемонты");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НаработкиСрезПоследних.ОбъектЭксплуатации КАК ОбъектЭксплуатации,
	|	НаработкиСрезПоследних.ПоказательНаработки КАК ПоказательНаработки,
	|	НаработкиСрезПоследних.Период КАК Период,
	|	НаработкиСрезПоследних.Значение КАК Значение,
	|	НаработкиСрезПоследних.СреднесуточноеЗначение КАК СреднесуточноеЗначение
	|ИЗ
	|	РегистрСведений.НаработкиОбъектовЭксплуатации.СрезПоследних(
	|			&НачалоПланирования,
	|			(ОбъектЭксплуатации, ПоказательНаработки) В
	|				(ВЫБРАТЬ
	|					ОбъектыПланирования.ОбъектЭксплуатации КАК ОбъектЭксплуатации,
	|					РемонтныеЦиклы.ПоказательНаработки
	|				ИЗ
	|					ОбъектыПланирования КАК ОбъектыПланирования
	|						ВНУТРЕННЕЕ СОЕДИНЕНИЕ РемонтныеЦиклы КАК РемонтныеЦиклы
	|						ПО
	|							ОбъектыПланирования.ХранилищеППР = РемонтныеЦиклы.ХранилищеППР
	|				ГДЕ
	|					РемонтныеЦиклы.РемонтныйЦиклПоНаработке
	|			
	|				ОБЪЕДИНИТЬ ВСЕ
	|			
	|				ВЫБРАТЬ
	|					ОбъектыПланирования.ОбъектЭксплуатации КАК ОбъектЭксплуатации,
	|					ПрочиеРемонты.ПоказательНаработки
	|				ИЗ
	|					ОбъектыПланирования КАК ОбъектыПланирования
	|						ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПрочиеРемонты КАК ПрочиеРемонты
	|						ПО
	|							ОбъектыПланирования.ХранилищеППР = ПрочиеРемонты.ХранилищеППР
	|				ГДЕ
	|					ПрочиеРемонты.ПоНаработке)) КАК НаработкиСрезПоследних";
	СписокЗапросов.Добавить(ТекстЗапроса, "Наработки");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(СправочникОбъектов.Ссылка, СправочникУзлов.Владелец) КАК ОбъектЭксплуатации,
	|	ЕСТЬNULL(СправочникУзлов.Ссылка, ЗНАЧЕНИЕ(Справочник.УзлыОбъектовЭксплуатации.ПустаяСсылка)) КАК Узел,
	|	ЕСТЬNULL(СправочникОбъектов.РемонтирующееПодразделение, СправочникУзлов.РемонтирующееПодразделение) КАК Подразделение,
	|	ОбъектыПланирования.ДатаВводаВЭксплуатацию КАК ДатаВводаВЭксплуатацию,
	|	ОбъектыПланирования.ГрафикРаботы КАК ГрафикРаботы,
	|	ОбъектыПланирования.ХранилищеППР КАК ХранилищеППР,
	|	ЕСТЬNULL(ПоследниеРемонты.ВидРемонта, ЗНАЧЕНИЕ(Справочник.ВидыРемонтов.ПустаяСсылка)) КАК ПредыдущийРемонтПоЦиклу,
	|	ЕСТЬNULL(ПоследниеРемонты.ДатаРемонта, ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)) КАК ДатаПредыдущегоРемонтаПоЦиклу
	|ИЗ
	|	ОбъектыПланирования КАК ОбъектыПланирования
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыЭксплуатации КАК СправочникОбъектов
	|		ПО ОбъектыПланирования.ОбъектЭксплуатации = СправочникОбъектов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УзлыОбъектовЭксплуатации КАК СправочникУзлов
	|		ПО ОбъектыПланирования.ОбъектЭксплуатации = СправочникУзлов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПоследниеРемонты КАК ПоследниеРемонты
	|		ПО ОбъектыПланирования.ОбъектЭксплуатации = ПоследниеРемонты.ОбъектЭксплуатации
	|			И (ПоследниеРемонты.РемонтныйЦикл)";
	СписокЗапросов.Добавить(ТекстЗапроса, "ОбъектыПланирования");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РемонтныеЦиклы.ХранилищеППР КАК ХранилищеППР,
	|	РемонтныеЦиклы.НомерСтроки КАК НомерСтроки,
	|	РемонтныеЦиклы.ВидРемонта,
	|	РемонтныеЦиклы.ВидРемонта.ОбщийВидРемонта КАК ОбщийВидРемонта,
	|	РемонтныеЦиклы.ВидРемонта.УчитыватьГрафикРаботы КАК УчитыватьГрафикРаботы,
	|	РемонтныеЦиклы.РемонтныйЦиклПоНаработке КАК ПоНаработке,
	|	РемонтныеЦиклы.ИнтервалВремени,
	|	РемонтныеЦиклы.ЕдиницаВремени,
	|	РемонтныеЦиклы.ИнтервалНаработки,
	|	РемонтныеЦиклы.ПоказательНаработки,
	|	РемонтныеЦиклы.Разрешен,
	|	РемонтныеЦиклы.МаксимальнаяДатаРемонта,
	|	РемонтныеЦиклы.Длительность,
	|	РемонтныеЦиклы.ЕдиницаИзмеренияДлительности,
	|	РемонтныеЦиклы.ВидРемонта.Упреждение КАК УпреждениеРемонта
	|ИЗ
	|	РемонтныеЦиклы КАК РемонтныеЦиклы
	|
	|УПОРЯДОЧИТЬ ПО
	|	ХранилищеППР,
	|	НомерСтроки";
	СписокЗапросов.Добавить(ТекстЗапроса, "РемонтныеЦиклы");

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПрочиеРемонты.ХранилищеППР КАК ХранилищеППР,
	|	ПрочиеРемонты.НомерСтроки КАК НомерСтроки,
	|	ПрочиеРемонты.ВидРемонта,
	|	ПрочиеРемонты.ВидРемонта.ОбщийВидРемонта КАК ОбщийВидРемонта,
	|	ПрочиеРемонты.ВидРемонта.УчитыватьГрафикРаботы КАК УчитыватьГрафикРаботы,
	|	ПрочиеРемонты.ПоНаработке,
	|	ПрочиеРемонты.ИнтервалВремени,
	|	ПрочиеРемонты.ЕдиницаВремени,
	|	ПрочиеРемонты.ИнтервалНаработки,
	|	ПрочиеРемонты.ПоказательНаработки,
	|	ПрочиеРемонты.МаксимальнаяДатаРемонта,
	|	ПрочиеРемонты.Длительность,
	|	ПрочиеРемонты.ЕдиницаИзмеренияДлительности,
	|	ЛОЖЬ КАК ПравилоРемонтногоЦикла,
	|	ПрочиеРемонты.ВидРемонта.Упреждение КАК УпреждениеРемонта
	|ИЗ
	|	ПрочиеРемонты КАК ПрочиеРемонты
	|
	|УПОРЯДОЧИТЬ ПО
	|	ХранилищеППР,
	|	НомерСтроки";
	СписокЗапросов.Добавить(ТекстЗапроса, "ПрочиеРемонты");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НаработкиПоследнихРемонтов.ОбъектЭксплуатации,
	|	НаработкиПоследнихРемонтов.ВидРемонта,
	|	НаработкиПоследнихРемонтов.РемонтныйЦикл,
	|	НаработкиПоследнихРемонтов.ПоказательНаработки,
	|	ПоследниеНаработки.Значение - Наработки.Значение КАК Значение
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПоследниеРемонты.ОбъектЭксплуатации КАК ОбъектЭксплуатации,
	|		ПоследниеРемонты.ВидРемонта КАК ВидРемонта,
	|		ПоследниеРемонты.РемонтныйЦикл КАК РемонтныйЦикл,
	|		Наработки.ПоказательНаработки КАК ПоказательНаработки,
	|		МАКСИМУМ(Наработки.Период) КАК Период
	|	ИЗ
	|		ПоследниеРемонты КАК ПоследниеРемонты
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НаработкиОбъектовЭксплуатации КАК Наработки
	|			ПО ПоследниеРемонты.ОбъектЭксплуатации = Наработки.ОбъектЭксплуатации
	|				И ПоследниеРемонты.ДатаРемонта > Наработки.Период
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ПоследниеРемонты.ОбъектЭксплуатации,
	|		ПоследниеРемонты.ВидРемонта,
	|		ПоследниеРемонты.РемонтныйЦикл,
	|		Наработки.ПоказательНаработки) КАК НаработкиПоследнихРемонтов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НаработкиОбъектовЭксплуатации КАК Наработки
	|		ПО НаработкиПоследнихРемонтов.ОбъектЭксплуатации = Наработки.ОбъектЭксплуатации
	|			И НаработкиПоследнихРемонтов.ПоказательНаработки = Наработки.ПоказательНаработки
	|			И НаработкиПоследнихРемонтов.Период = Наработки.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НаработкиОбъектовЭксплуатации.СрезПоследних(&НачалоПланирования, ) КАК ПоследниеНаработки
	|		ПО НаработкиПоследнихРемонтов.ОбъектЭксплуатации = ПоследниеНаработки.ОбъектЭксплуатации
	|			И НаработкиПоследнихРемонтов.ПоказательНаработки = ПоследниеНаработки.ПоказательНаработки";
	СписокЗапросов.Добавить(ТекстЗапроса, "НаработкиПослеРемонтов");
	
	РезультатыЗапроса = ОбщегоНазначенияУТ.ПолучитьРезультатыЗапроса(Запрос, СписокЗапросов);
	
	Возврат РезультатыЗапроса;
	
КонецФункции

// Возвращает дату предыдущего ремонта объекта
//
// Параметры:
// 		ОбъектЭксплуатации - СправочникСсылка.ОбъектыЭксплуатации, СправочникСсылка.УзлыОбъектовЭксплуатации - Ссылка на объект эксплуатации, по которому необходимо найти дату последнего ремонта
// 		ВидРемонта - СправочникСсылка.ВидыРемонтов - Вид ремонта, по которому необходимо найти дату
// 		Выборки - Структура - Структура с кэшированными выборками.
//
// Возвращаемое значение:
// 		Дата - Дата предыдущего ремонта.
//
Функция ПолучитьДатуПредыдущегоРемонта(ОбъектЭксплуатации, ВидРемонта, Выборки)
	
	ПредыдущиеРемонты = Выборки.ПредыдущиеРемонты;
	
	ПредыдущиеРемонты.Сбросить();
	Если ПредыдущиеРемонты.НайтиСледующий(Новый Структура("ОбъектЭксплуатации, ВидРемонта", ОбъектЭксплуатации, ВидРемонта)) Тогда
		
		Возврат ПредыдущиеРемонты.Период;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Возвращает величину показателя наработки с даты указанного ремонта
//
// Параметры:
// 		ОбъектЭксплуатации - СправочникСсылка.ОбъектыЭксплуатации, СправочникСсылка.УзлыОбъектовЭксплуатации - Ссылка на объект эксплуатации, по которому необходимо получить наработку
// 		ВидРемонта - СправочникСсылка.ВидыРемонтов - Вид ремонта, по которому необходимо получить наработку
// 		РемонтныйЦикл - Булево - Признак необходимости получения наработки на дату последнего ремонта по циклу
// 		ПоказательНаработки - СправочникСсылка.ПоказателиНаработки - Ссылка на показатель наработки, значения которого необходимо получить
// 		Выборки - Структура - Структура с кэшированными выборками.
//
// Возвращаемое значение:
// 		Число - Разность значений наработки от среза последних на дату указанного ремонта и от среза последних на текущую дату.
//
Функция ПолучитьНаработкуСДатыПоследнегоРемонта(ОбъектЭксплуатации, ВидРемонта, РемонтныйЦикл, ПоказательНаработки, Выборки)
	
	НаработкиПослеРемонтов = Выборки.НаработкиПослеРемонтов;
	СтруктураПоиска = Новый Структура(
		"ОбъектЭксплуатации, ПоказательНаработки",
		ОбъектЭксплуатации,
		ПоказательНаработки);
	
	Если РемонтныйЦикл Тогда
		СтруктураПоиска.Вставить("РемонтныйЦикл", Истина);
	Иначе
		СтруктураПоиска.Вставить("ВидРемонта", ВидРемонта);
	КонецЕсли;
	
	НаработкиПослеРемонтов.Сбросить();
	Если НаработкиПослеРемонтов.НайтиСледующий(СтруктураПоиска) Тогда
		Возврат Макс(НаработкиПослеРемонтов.Значение, 0);
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Возвращает структуру значений наработки объекта ремонта
//
// Параметры:
// 		ОбъектЭксплуатации - СправочникСсылка.ОбъектыЭксплуатации, СправочникСсылка.УзлыОбъектовЭксплуатации - Ссылка на объект эксплуатации, по которому необходимо получить наработку
// 		ПоказательНаработки - СправочникСсылка.ПоказателиНаработки - Ссылка на показатель наработки, значения которого необходимо получить
// 		Выборки - Структура - Структура с кэшированными выборками.
//
// Возвращаемое значение:
// 		Структура - Структура значений наработки.
//
Функция ПолучитьНаработку(ОбъектЭксплуатации, ПоказательНаработки, Выборки)
	
	Наработки = Выборки.Наработки;
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("ОбъектЭксплуатации", ОбъектЭксплуатации);
	СтруктураВозврата.Вставить("ПоказательНаработки", ПоказательНаработки);
	СтруктураВозврата.Вставить("Период", Дата(1, 1, 1, 0, 0, 0));
	СтруктураВозврата.Вставить("Значение", 0);
	СтруктураВозврата.Вставить("СреднесуточноеЗначение", 0);

	Наработки.Сбросить();
	Если Наработки.НайтиСледующий(Новый Структура("ОбъектЭксплуатации, ПоказательНаработки", ОбъектЭксплуатации, ПоказательНаработки)) Тогда
		ЗаполнитьЗначенияСвойств(СтруктураВозврата, Наработки);
	КонецЕсли;
	
	Возврат СтруктураВозврата;
	
КонецФункции

// Возвращает таблицу значений ремонтного цикла
//
// Параметры:
// 		ХранилищеППР - СправочникСсылка.КлассыОбъектовЭксплуатации, СправочникСсылка.ПодклассыОбъектовЭксплуатации - Ссылка на объект с правилами ремонтного цикла
// 		Выборки - Структура - Структура с кэшированными выборками.
//
// Возвращаемое значение:
// 		ТаблицаЗначений - Таблица ремонтного цикла.
//
Функция ПолучитьРемонтныйЦикл(ХранилищеППР, Выборки)
	
	РемонтныйЦикл = Новый ТаблицаЗначений;
	РемонтныйЦикл.Колонки.Добавить("НомерСтроки");
	РемонтныйЦикл.Колонки.Добавить("ПредыдущийВидРемонта");
	РемонтныйЦикл.Колонки.Добавить("ВидРемонта");
	РемонтныйЦикл.Колонки.Добавить("ОбщийВидРемонта");
	РемонтныйЦикл.Колонки.Добавить("ПоНаработке");
	РемонтныйЦикл.Колонки.Добавить("ИнтервалВремени");
	РемонтныйЦикл.Колонки.Добавить("ЕдиницаВремени");
	РемонтныйЦикл.Колонки.Добавить("ИнтервалНаработки");
	РемонтныйЦикл.Колонки.Добавить("ПоказательНаработки");
	РемонтныйЦикл.Колонки.Добавить("Разрешен");
	РемонтныйЦикл.Колонки.Добавить("МаксимальнаяДатаРемонта");
	РемонтныйЦикл.Колонки.Добавить("Длительность");
	РемонтныйЦикл.Колонки.Добавить("ЕдиницаИзмеренияДлительности");
	РемонтныйЦикл.Колонки.Добавить("ГрафикРаботы");
	РемонтныйЦикл.Колонки.Добавить("УчитыватьГрафикРаботы");
	РемонтныйЦикл.Колонки.Добавить("УпреждениеРемонта");
	РемонтныйЦикл.Колонки.Добавить("ПравилоРемонтногоЦикла");
	
	ПредыдущийВидРемонта = Справочники.ВидыРемонтов.ПустаяСсылка();
	
	РемонтныеЦиклы = Выборки.РемонтныеЦиклы;
	РемонтныеЦиклы.Сбросить();
	СтруктураПоиска = Новый Структура("ХранилищеППР", ХранилищеППР);
	Пока РемонтныеЦиклы.НайтиСледующий(СтруктураПоиска) Цикл
		
		ПравилоРемонтногоЦикла = РемонтныйЦикл.Добавить();
		ПравилоРемонтногоЦикла.ПредыдущийВидРемонта = ПредыдущийВидРемонта;
		ЗаполнитьЗначенияСвойств(ПравилоРемонтногоЦикла, РемонтныеЦиклы);
		
		ПредыдущийВидРемонта = РемонтныеЦиклы.ВидРемонта;
		
	КонецЦикла;
	
	Если РемонтныйЦикл.Количество() <> 0 Тогда
		РемонтныйЦикл[0].ПредыдущийВидРемонта = ПредыдущийВидРемонта;
	КонецЕсли;
	
	РемонтныйЦикл.ЗаполнитьЗначения(Истина, "ПравилоРемонтногоЦикла");
	
	Возврат РемонтныйЦикл;
	
КонецФункции

// Заполняет таблицу "Ремонты" по данным текущего объекта эксплуатации по правилам ремонтного цикла.
//
// Параметры:
// 		ВыборкаОбъектовПланирования - ВыборкаИзРезультатаЗапроса - Текущая строка выборки из результата запроса по планируемым объектам
// 		КэшированныеЗначения - Структура - Структура с кэшированными выборками.
//
Процедура ЗаполнитьРемонтыПоЦиклу(ВыборкаОбъектовПланирования, КэшированныеЗначения)
	
	Выборки = КэшированныеЗначения.Выборки;
	ПредыдущийРемонтПоЦиклу = ВыборкаОбъектовПланирования.ПредыдущийРемонтПоЦиклу;
	ДатаОкончанияПредыдущегоРемонта = ВыборкаОбъектовПланирования.ДатаПредыдущегоРемонтаПоЦиклу;
	
	РемонтныйЦикл = ПолучитьРемонтныйЦикл(ВыборкаОбъектовПланирования.ХранилищеППР, Выборки);
	
	Если РемонтныйЦикл.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДатаОкончанияПредыдущегоРемонта) Тогда
		ДатаОкончанияПредыдущегоРемонта = ВыборкаОбъектовПланирования.ДатаВводаВЭксплуатацию;
	КонецЕсли;
	
	ИндексОбходаЦикла = 0;
	Если ПредыдущийРемонтПоЦиклу <> Справочники.ВидыРемонтов.ПустаяСсылка() Тогда
		НайденныеСтроки = РемонтныйЦикл.НайтиСтроки(Новый Структура("ПредыдущийВидРемонта", ПредыдущийРемонтПоЦиклу));
		Если НайденныеСтроки.Количество() <> 0 Тогда
			ИндексОбходаЦикла = НайденныеСтроки[0].НомерСтроки - 1;
		КонецЕсли;
	КонецЕсли;
	
	Пока Истина Цикл
		
		Правило = РемонтныйЦикл[ИндексОбходаЦикла];
		Если НЕ Правило.Разрешен Тогда
			Прервать;
		КонецЕсли;
				
		СледующийРемонт = СледующийРемонтПоПравилу(
			ВыборкаОбъектовПланирования, Правило, ДатаОкончанияПредыдущегоРемонта, КэшированныеЗначения);
		
		Если СледующийРемонт.Отказ Тогда
			Прервать;
		КонецЕсли;
			
		НовыйРемонт = Ремонты.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйРемонт, ВыборкаОбъектовПланирования, "ОбъектЭксплуатации, Узел, Подразделение");
		ЗаполнитьЗначенияСвойств(НовыйРемонт, СледующийРемонт, "ВидРемонта, ОбщийВидРемонта, ДатаНачала, ДатаЗавершения, Длительность, ЕдиницаИзмеренияДлительности");
		
		НовыйРемонт.РемонтныйЦикл = Истина;
		НовыйРемонт.КодРемонта = МаксимальныйКодРемонта;
		МаксимальныйКодРемонта = МаксимальныйКодРемонта + 1;
		
		ПолучитьЗаказНаРемонт(НовыйРемонт);
		
		ПредыдущийРемонтПоЦиклу = СледующийРемонт.ВидРемонта;
		
		ДатаОкончанияПредыдущегоРемонта = СледующийРемонт.ДатаЗавершения;
			
		ИндексОбходаЦикла = ИндексОбходаЦикла + 1;
		Если ИндексОбходаЦикла = РемонтныйЦикл.Количество() Тогда
			ИндексОбходаЦикла = 0;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Заполняет таблицу "Ремонты" по данным текущего объекта ремонта по правилам прочих ремонтов.
//
// Параметры:
// 		ВыборкаОбъектовПланирования - ВыборкаИзРезультатаЗапроса - Текущая строка выборки из результата запроса по планируемым объектам
// 		КэшированныеЗначения - Структура - Структура с кэшированными выборками.
//
Процедура ЗаполнитьПрочиеРемонты(ВыборкаОбъектовПланирования, КэшированныеЗначения)
	
	Выборки = КэшированныеЗначения.Выборки;
	ВыборкаПрочихРемонтов = Выборки.ПрочиеРемонты;
	ВыборкаПрочихРемонтов.Сбросить();
	СтруктураОтбора = Новый Структура("ХранилищеППР", ВыборкаОбъектовПланирования.ХранилищеППР);
	
	РемонтируемыйОбъект = ?(ЗначениеЗаполнено(ВыборкаОбъектовПланирования.Узел), 
		ВыборкаОбъектовПланирования.Узел, 
		ВыборкаОбъектовПланирования.ОбъектЭксплуатации);
	
	Пока ВыборкаПрочихРемонтов.НайтиСледующий(СтруктураОтбора) Цикл
		
		Правило = ВыборкаПрочихРемонтов;
		ВидРемонта = Правило.ВидРемонта;
		
		ДатаОкончанияПредыдущегоРемонта = ПолучитьДатуПредыдущегоРемонта(РемонтируемыйОбъект, ВидРемонта, Выборки);
		Если Не ЗначениеЗаполнено(ДатаОкончанияПредыдущегоРемонта) Тогда
			ДатаОкончанияПредыдущегоРемонта = ВыборкаОбъектовПланирования.ДатаВводаВЭксплуатацию;
		КонецЕсли;
		
		Пока Истина Цикл
			
			СледующийРемонт = СледующийРемонтПоПравилу(
				ВыборкаОбъектовПланирования, Правило, ДатаОкончанияПредыдущегоРемонта, КэшированныеЗначения);
			
			Если СледующийРемонт.Отказ Тогда
				Прервать;
			КонецЕсли;
				
			НовыйРемонт = Ремонты.Добавить();
			ЗаполнитьЗначенияСвойств(НовыйРемонт, ВыборкаОбъектовПланирования, "ОбъектЭксплуатации, Узел, Подразделение");
			ЗаполнитьЗначенияСвойств(НовыйРемонт, СледующийРемонт, "ВидРемонта, ОбщийВидРемонта, ДатаНачала, ДатаЗавершения, Длительность, ЕдиницаИзмеренияДлительности");
			НовыйРемонт.РемонтныйЦикл = Ложь;
			НовыйРемонт.КодРемонта = МаксимальныйКодРемонта;
			МаксимальныйКодРемонта = МаксимальныйКодРемонта + 1;
			
			ПолучитьЗаказНаРемонт(НовыйРемонт);
			
			ДатаОкончанияПредыдущегоРемонта = СледующийРемонт.ДатаЗавершения;
				
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

// Заполняет реквизит "КодЗаказаНаРемонт" в переданной строке табличной части "Ремонты".
//
Процедура ПолучитьЗаказНаРемонт(СтрокаРемонта)
	
	СоздатьНовыйЗаказ = Истина;
	
	СтруктураПоискаРемонтов = Новый Структура("КодЗаказаНаРемонт, ОбъектЭксплуатации, Узел, ВидРемонта");
	ЗаполнитьЗначенияСвойств(СтруктураПоискаРемонтов, СтрокаРемонта);
	
	СтруктураПоиска = Новый Структура("ОбъектЭксплуатации, Подразделение");
	ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаРемонта);
	НайденныеСтроки = ЗаказыНаРемонт.НайтиСтроки(СтруктураПоиска);
	
	Если НайденныеСтроки.Количество() <> 0 Тогда
		
		Для Каждого ЗаказНаРемонт Из НайденныеСтроки Цикл
			
			СтруктураПоискаРемонтов.КодЗаказаНаРемонт = ЗаказНаРемонт.КодЗаказаНаРемонт;
			Если Ремонты.НайтиСтроки(СтруктураПоискаРемонтов).Количество() <> 0 Тогда
				Продолжить; // Если в заказе уже есть ремонт данного вида для этого ремонтируемого объекта
			КонецЕсли;
			
			ДлительностьЗаказ = ОбщегоНазначенияУТКлиентСервер.ВремяВСекундах(ЗаказНаРемонт.Длительность, ЗаказНаРемонт.ЕдиницаИзмеренияДлительности);
			ДлительностьРемонт = ОбщегоНазначенияУТКлиентСервер.ВремяВСекундах(СтрокаРемонта.Длительность, СтрокаРемонта.ЕдиницаИзмеренияДлительности);
			
			Если ЗаказНаРемонт.ДатаНачала <= СтрокаРемонта.ДатаНачала Тогда
				
				Если ЗаказНаРемонт.ДатаЗавершения >= СтрокаРемонта.ДатаНачала Тогда
					
					СоздатьНовыйЗаказ = Ложь;
					СтрокаРемонта.КодЗаказаНаРемонт = ЗаказНаРемонт.КодЗаказаНаРемонт;
					
					Если ДлительностьРемонт > ДлительностьЗаказ Тогда
						ЗаказНаРемонт.Длительность = СтрокаРемонта.Длительность;
						ЗаказНаРемонт.ЕдиницаИзмеренияДлительности = СтрокаРемонта.ЕдиницаИзмеренияДлительности;
					КонецЕсли;
					
					Прервать;
				
				КонецЕсли;
				
			ИначеЕсли СтрокаРемонта.ДатаНачала <= ЗаказНаРемонт.ДатаНачала Тогда
				
				Если СтрокаРемонта.ДатаЗавершения >= ЗаказНаРемонт.ДатаНачала Тогда
					
					СоздатьНовыйЗаказ = Ложь;
					СтрокаРемонта.КодЗаказаНаРемонт = ЗаказНаРемонт.КодЗаказаНаРемонт;
					ЗаказНаРемонт.ДатаНачала = СтрокаРемонта.ДатаНачала;
					
					Если ДлительностьРемонт > ДлительностьЗаказ Тогда
						ЗаказНаРемонт.Длительность = СтрокаРемонта.Длительность;
						ЗаказНаРемонт.ЕдиницаИзмеренияДлительности = СтрокаРемонта.ЕдиницаИзмеренияДлительности;
					КонецЕсли;
					
					Прервать;
				
				КонецЕсли;
			
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если СоздатьНовыйЗаказ Тогда
		
		КодЗаказаНаРемонт = ЗаказыНаРемонт.Количество() + 2;
		ЗаказНаРемонт = ЗаказыНаРемонт.Добавить();
		ЗаказНаРемонт.КодЗаказаНаРемонт = КодЗаказаНаРемонт;
		ЗаполнитьЗначенияСвойств(
			ЗаказНаРемонт, 
			СтрокаРемонта, 
			"ОбъектЭксплуатации, ДатаНачала, ДатаЗавершения, Подразделение, Длительность, ЕдиницаИзмеренияДлительности, ОбщийВидРемонта");
		
		СтрокаРемонта.КодЗаказаНаРемонт = КодЗаказаНаРемонт;
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает структуру следующего ремонта по правилу планирования
//
// Параметры:
// 		ВыборкаОбъектовПланирования - ВыборкаИзРезультатаЗапроса - Текущая строка выборки из результата запроса по планируемым объектам
// 		Правило - СтрокаТаблицыЗначений, ВыборкаИзРезультатаЗапроса - Строка описания правила планирования
// 		ДатаОкончанияПредыдущегоРемонта - Дата - Дата предыдущего ремонта по ремонтному циклу или по текущему правилу для прочих ремонтов.
// 		КэшированныеЗначения - Структура - Структура с кэшированными выборками.
//
// Возвращаемое значение:
// 	Структура - содержит:
// 		* ВидРемонта - СправочникСсылка.ВидыРемонтов -
// 		* ОбщийВидРемонта - СправочникСсылка.ОбщиеВидыРемонтов - 
// 		* ДатаНачала - Дата -
// 		* Длительность - Число -
// 		* ЕдиницаИзмеренияДлительности - ПеречислениеСсылка.ЕдиницыИзмеренияВремени - 
// 		* Отказ - Булево - 
Функция СледующийРемонтПоПравилу(ВыборкаОбъектовПланирования, Правило, ДатаОкончанияПредыдущегоРемонта, КэшированныеЗначения)
	
	СтруктураРемонта = Новый Структура("ВидРемонта, ОбщийВидРемонта, ДатаНачала, ДатаЗавершения, Длительность, ЕдиницаИзмеренияДлительности, Отказ");
	СтруктураРемонта.ВидРемонта = Правило.ВидРемонта;
	СтруктураРемонта.ОбщийВидРемонта = Правило.ОбщийВидРемонта;
	СтруктураРемонта.Длительность = Правило.Длительность;
	СтруктураРемонта.ЕдиницаИзмеренияДлительности = Правило.ЕдиницаИзмеренияДлительности;
	СтруктураРемонта.Отказ = Ложь;
	
	Если Правило.ПоНаработке Тогда
		
		РемонтируемыйОбъект = ?(ЗначениеЗаполнено(ВыборкаОбъектовПланирования.Узел), 
				ВыборкаОбъектовПланирования.Узел, 
				ВыборкаОбъектовПланирования.ОбъектЭксплуатации);
		
		СтруктураНаработки = ПолучитьНаработку(РемонтируемыйОбъект, Правило.ПоказательНаработки, КэшированныеЗначения.Выборки);
		НаработкаСПоследнегоРемонта = 0;
		
		Если ДатаОкончанияПредыдущегоРемонта < НачалоПланирования Тогда
			
			НаработкаСПоследнегоРемонта = ПолучитьНаработкуСДатыПоследнегоРемонта(
				РемонтируемыйОбъект,
				СтруктураРемонта.ВидРемонта,
				Правило.ПравилоРемонтногоЦикла,
				Правило.ПоказательНаработки,
				КэшированныеЗначения.Выборки);
			
			Если НаработкаСПоследнегоРемонта = Неопределено Тогда
				
				НаработкаСПоследнегоРемонта = СтруктураНаработки.Значение;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если НаработкаСПоследнегоРемонта >= Правило.ИнтервалНаработки Тогда
			
			СтруктураРемонта.ДатаНачала = НачалоПланирования;
			
		Иначе
			
			Если СтруктураНаработки.СреднесуточноеЗначение = 0
				ИЛИ (Правило.ИнтервалНаработки - НаработкаСПоследнегоРемонта) = 0 Тогда
				
				СтруктураРемонта.Отказ = Истина;
				
			Иначе
				
				ОтДаты = ?(НаработкаСПоследнегоРемонта = 0,
					ДатаОкончанияПредыдущегоРемонта,
					НачалоДня(СтруктураНаработки.Период));
				
				ИнтервалВремени = (Правило.ИнтервалНаработки - НаработкаСПоследнегоРемонта) / СтруктураНаработки.СреднесуточноеЗначение;
				Если ИнтервалВремени <> Цел(ИнтервалВремени) Тогда
					ИнтервалВремени = Цел(ИнтервалВремени) + 1;
				КонецЕсли;
							
				СтруктураРемонта.ДатаНачала = УправлениеРемонтами.ДобавитьКДатеВремяСУчетомГрафикаРаботы(
					ОтДаты, 
					ИнтервалВремени, 
					Перечисления.ЕдиницыИзмеренияВремени.День,
					ВыборкаОбъектовПланирования.ГрафикРаботы);
				 
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		Если ЗначениеЗаполнено(ДатаОкончанияПредыдущегоРемонта) Тогда
			СтруктураРемонта.ДатаНачала = УправлениеРемонтами.ДобавитьКДатеВремяСУчетомГрафикаРаботы(
				ДатаОкончанияПредыдущегоРемонта, 
				Правило.ИнтервалВремени, 
				Правило.ЕдиницаВремени,
				ВыборкаОбъектовПланирования.ГрафикРаботы);
		Иначе
			СтруктураРемонта.ДатаНачала = НачалоПланирования;
		КонецЕсли;
		
	КонецЕсли;
	
	Если СтруктураРемонта.Отказ Тогда
		Возврат СтруктураРемонта;
	КонецЕсли;
	
	Если СтруктураРемонта.ДатаНачала < НачалоПланирования Тогда
		СтруктураРемонта.ДатаНачала = НачалоПланирования;
	КонецЕсли;
	
	// Нужна дата с учетом расписания работы.
	СтруктураРемонта.ДатаНачала = УправлениеРемонтами.ДобавитьКДатеВремяСУчетомРасписанияРаботы(
			СтруктураРемонта.ДатаНачала, 
			0, 
			Перечисления.ЕдиницыИзмеренияВремени.Час, 
			ВыборкаОбъектовПланирования.ГрафикРаботы, 
			КэшированныеЗначения.МенеджерВременныхТаблиц);
	
	// Проверка вхождения в интервал планирования
	Если СтруктураРемонта.ДатаНачала > Правило.МаксимальнаяДатаРемонта Тогда
		СтруктураРемонта.Отказ = Истина;
		Возврат СтруктураРемонта;
	КонецЕсли;
	
	Если СтруктураРемонта.ЕдиницаИзмеренияДлительности = Перечисления.ЕдиницыИзмеренияВремени.День Тогда
		
		СтруктураРемонта.ДатаЗавершения = УправлениеРемонтами.ДобавитьКДатеВремяСУчетомГрафикаРаботы(
			СтруктураРемонта.ДатаНачала, 
			?(СтруктураРемонта.Длительность <> 0, СтруктураРемонта.Длительность - 1, 0),
			СтруктураРемонта.ЕдиницаИзмеренияДлительности,
			ВыборкаОбъектовПланирования.ГрафикРаботы);
			
		СтруктураРемонта.ДатаЗавершения = УправлениеРемонтами.КонецРабочегоДня(
			СтруктураРемонта.ДатаЗавершения,
			ВыборкаОбъектовПланирования.ГрафикРаботы,
			КэшированныеЗначения.МенеджерВременныхТаблиц)
		
	Иначе
		
		СтруктураРемонта.ДатаЗавершения = УправлениеРемонтами.ДобавитьКДатеВремяСУчетомРасписанияРаботы(
			СтруктураРемонта.ДатаНачала,
			СтруктураРемонта.Длительность,
			СтруктураРемонта.ЕдиницаИзмеренияДлительности,
			ВыборкаОбъектовПланирования.ГрафикРаботы, 
			КэшированныеЗначения.МенеджерВременныхТаблиц);
			
	КонецЕсли;
	
	Возврат СтруктураРемонта;
	
КонецФункции

// Заполняет табличные части документа "ЗаказНаРемонт" по данным табличных частей обработки.
//
// Параметры:
// 		ОбъектДокумента - ДокументОбъект.ЗаказНаРемонт - Объект документа заказа
// 		КодЗаказаНаРемонт - Число - Код заказа, который необходимо перенести в документ.
//
Процедура ЗаполнитьТабличныеЧастиОбъектаДокумента(ОбъектДокумента, КодЗаказаНаРемонт)
	
	СтруктураОтбораПоЗаказу = Новый Структура("КодЗаказаНаРемонт", КодЗаказаНаРемонт);
	
	Для Каждого Ремонт Из Ремонты.НайтиСтроки(СтруктураОтбораПоЗаказу) Цикл
		
		РемонтИзДокумента = ОбъектДокумента.Ремонты.Добавить();
		ЗаполнитьЗначенияСвойств(РемонтИзДокумента, Ремонт, "Узел, ВидРемонта, РемонтныйЦикл");
		ОбъектДокумента.МаксимальныйКодРемонта = ОбъектДокумента.МаксимальныйКодРемонта + 1;
		РемонтИзДокумента.КодРемонта = ОбъектДокумента.МаксимальныйКодРемонта;
		
		СтруктураОтбораПоРемонту = Новый Структура("КодРемонта", Ремонт.КодРемонта);
		
		НайденныеСтроки = МатериалыИРаботы.НайтиСтроки(СтруктураОтбораПоРемонту);
		Если НайденныеСтроки.Количество() = 0 Тогда
			ЗаполнитьМатериалыРаботыПоРемонту(ОбъектДокумента, РемонтИзДокумента);
		Иначе
			Для Каждого СтрокаМатериаловИРабот Из НайденныеСтроки Цикл
				НоваяСтрока = ОбъектДокумента.МатериалыИРаботы.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаМатериаловИРабот);
				ЗаполнитьЗначенияСвойств(НоваяСтрока, РемонтИзДокумента, "КодРемонта");
			КонецЦикла;
		КонецЕсли;

		НайденныеСтроки = РабочиеЦентры.НайтиСтроки(СтруктураОтбораПоРемонту);
		Если НайденныеСтроки.Количество() = 0 Тогда
			ЗаполнитьРабочиеЦентрыПоРемонту(ОбъектДокумента, РемонтИзДокумента);
		Иначе
			Для Каждого СтрокаРабочихЦентров Из НайденныеСтроки Цикл
				Если ОбъектДокумента.РабочиеЦентры.НайтиСтроки(Новый Структура("РабочийЦентр", СтрокаРабочихЦентров.РабочийЦентр)).Количество()=0 Тогда
					НоваяСтрока = ОбъектДокумента.РабочиеЦентры.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРабочихЦентров);
					ЗаполнитьЗначенияСвойств(НоваяСтрока, ОбъектДокумента, "ДатаНачала, ДатаЗавершения");
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
	ЗаполнитьСтатьиРасходов(ОбъектДокумента);
	
	Документы.ЗаказНаРемонт.ЗаполнитьСкладПоНастройкамПередачиМатериалов(ОбъектДокумента);
	
КонецПроцедуры

// Заполняет табличую часть материалов/работ по строке ремонта
//
// Параметры:
// 		ОбъектДокумента - ДокументОбъект.ЗаказНаРемонт - Объект документа заказа на ремонт
// 		Ремонт - СтрокаТабличнойЧасти - Строка таблицы ремонтов
//
Процедура ЗаполнитьМатериалыРаботыПоРемонту(ОбъектДокумента, Ремонт)
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ВидыРемонтовМатериалыИРаботы.Номенклатура,
		|	ВидыРемонтовМатериалыИРаботы.Характеристика,
		|	ВидыРемонтовМатериалыИРаботы.Упаковка,
		|	ВидыРемонтовМатериалыИРаботы.КоличествоУпаковок,
		|	ВидыРемонтовМатериалыИРаботы.Количество
		|ИЗ
		|	Справочник.ВидыРемонтов.МатериалыИРаботы КАК ВидыРемонтовМатериалыИРаботы
		|ГДЕ
		|	ВидыРемонтовМатериалыИРаботы.Ссылка = &ВидРемонта");
	
	Запрос.УстановитьПараметр("ВидРемонта", Ремонт.ВидРемонта);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураЗаполнения = Новый Структура("КодРемонта");
	ЗаполнитьЗначенияСвойств(СтруктураЗаполнения, Ремонт);
	
	СтруктураДействий = Новый Структура();
	ДобавитьДействияОбеспеченияНаСервере(СтруктураДействий, ОбъектДокумента);
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ОбъектДокумента.МатериалыИРаботы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтруктураЗаполнения);
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(НоваяСтрока, СтруктураДействий, Неопределено);
		
	КонецЦикла;
	
КонецПроцедуры

// Заполняет табличую часть рабочих центров по строке ремонта
//
//
// Параметры:
// 		ОбъектДокумента - ДокументОбъект.ЗаказНаРемонт - Объект документа заказа на ремонт
// 		Ремонт - СтрокаТабличнойЧасти - Строка таблицы ремонтов.
//
Процедура ЗаполнитьРабочиеЦентрыПоРемонту(ОбъектДокумента, Ремонт)
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	РабочиеЦентры.РабочийЦентр
		|ИЗ
		|	Справочник.ОбъектыЭксплуатации.РабочиеЦентры КАК РабочиеЦентры
		|ГДЕ
		|	РабочиеЦентры.Ссылка = &ОбъектЭксплуатации
		|	И (&ЭтоРемонтОбъекта
		|			ИЛИ ВЫРАЗИТЬ(&Узел КАК Справочник.УзлыОбъектовЭксплуатации).ВлияетНаДоступностьРЦ)
		|	И НЕ РабочиеЦентры.РабочийЦентр В (&РабочиеЦентры)");
	
	Запрос.УстановитьПараметр("ЭтоРемонтОбъекта", Не ЗначениеЗаполнено(Ремонт.Узел));
	Запрос.УстановитьПараметр("ОбъектЭксплуатации", ОбъектДокумента.ОбъектЭксплуатации);
	Запрос.УстановитьПараметр("Узел", Ремонт.Узел);
	Запрос.УстановитьПараметр("РабочиеЦентры", ОбъектДокумента.РабочиеЦентры.Выгрузить(,"РабочийЦентр"));
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = ОбъектДокумента.РабочиеЦентры.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ОбъектДокумента, "ДатаНачала, ДатаЗавершения");
	КонецЦикла;
	
КонецПроцедуры

// Заполняет статьи расходов по настройкам отражения расходов
//
Процедура ЗаполнитьСтатьиРасходов(ОбъектДокумента)
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ДанныеЗаполнения.НомерСтроки КАК ЧИСЛО) КАК НомерСтроки,
		|	ВЫРАЗИТЬ(&ОбъектЭксплуатации КАК Справочник.ОбъектыЭксплуатации) КАК ОбъектЭксплуатации,
		|	ВЫРАЗИТЬ(ДанныеЗаполнения.Узел КАК Справочник.УзлыОбъектовЭксплуатации) КАК Узел,
		|	ВЫРАЗИТЬ(ДанныеЗаполнения.ВидРемонта КАК Справочник.ВидыРемонтов) КАК ВидРемонта
		|ПОМЕСТИТЬ ДанныеЗаполнения
		|ИЗ
		|	&ДанныеЗаполнения КАК ДанныеЗаполнения
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ВидРемонта
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеЗаполнения.НомерСтроки - 1 КАК ИндексСтроки,
		|	ЕСТЬNULL(ОбщиеВидыРемонтов.СтатьяРасходов, ЕСТЬNULL(Узлы.СтатьяРасходов, ДанныеЗаполнения.ОбъектЭксплуатации.СтатьяРасходов)) КАК СтатьяРасходов
		|ИЗ
		|	ДанныеЗаполнения КАК ДанныеЗаполнения
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбщиеВидыРемонтов КАК ОбщиеВидыРемонтов
		|		ПО ДанныеЗаполнения.ВидРемонта.ОбщийВидРемонта = ОбщиеВидыРемонтов.Ссылка
		|			И (ОбщиеВидыРемонтов.ЗаданаСтатьяРасходов)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УзлыОбъектовЭксплуатации КАК Узлы
		|		ПО ДанныеЗаполнения.Узел = Узлы.Ссылка
		|			И (Узлы.СтатьяРасходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка))");
	Запрос.УстановитьПараметр("ДанныеЗаполнения", ОбъектДокумента.Ремонты.Выгрузить());
	Запрос.УстановитьПараметр("ОбъектЭксплуатации", ОбъектДокумента.ОбъектЭксплуатации);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ОбъектДокумента.Ремонты[Выборка.ИндексСтроки], Выборка);
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьДействияОбеспеченияНаСервере(СтруктураДействий, Объект)
	
	ПараметрыДокумента = Документы.ЗаказНаРемонт.ПараметрыДокументаДляДействийОбеспечения(Объект);
	ОбеспечениеВДокументахКлиентСервер.ДобавитьДействияОбеспечения(СтруктураДействий, "Вариантобеспечения", ПараметрыДокумента);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
