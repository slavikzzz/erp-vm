#Область ОписаниеПеременных

#Область ОбъявлениеПеременныхФормы
&НаКлиенте
Перем ПараметрыОбработчикаОжидания;
&НаКлиенте
Перем РезультатФоновогоЗадания;
#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Параметры.Свойство("Организация", Объект.Организация);
	Объект.Организация = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Объект.Организация);
	Если Параметры.Свойство("Период") Тогда
		Объект.НачалоПериода = НачалоМесяца(Параметры.Период);
		Объект.КонецПериода = НачалоМесяца(Параметры.Период);
	КонецЕсли;
	
	Объект.НачалоПериода = ?(ЗначениеЗаполнено(Объект.НачалоПериода), Объект.НачалоПериода, НачалоКвартала(ТекущаяДатаСеанса()));
	Объект.КонецПериода = ?(ЗначениеЗаполнено(Объект.КонецПериода), Объект.КонецПериода, КонецКвартала(ТекущаяДатаСеанса()));
	
	ПоказатьПоясненияКЭтапам = Истина;
	
	ПредставлениеНачалаПериода = Формат(Объект.НачалоПериода, "ДФ='MMMM yyyy'");
	ПредставлениеКонцаПериода = Формат(Объект.КонецПериода, "ДФ='MMMM yyyy'");
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьСтраницыВыполненияЗаданий();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если Параметры.Свойство("Организация") И ЗначениеЗаполнено(Параметры.Организация) Тогда
		Объект.Организация = Параметры.Организация;
	КонецЕсли;
	Если Параметры.Свойство("Период") И ЗначениеЗаполнено(Параметры.Период) Тогда
		Объект.НачалоПериода = НачалоМесяца(Параметры.Период);
		Объект.КонецПериода = НачалоМесяца(Параметры.Период);
	КонецЕсли;	
	
	ПредставлениеНачалаПериода = Формат(Объект.НачалоПериода, "ДФ='MMMM yyyy'");
	ПредставлениеКонцаПериода = Формат(Объект.КонецПериода, "ДФ='MMMM yyyy'");
	
	УправлениеФормой();
	УстановитьЗаголовокКомандыИВидимостьПояснений();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьДоходы" Тогда
		ПараметрыОтчета = Новый Структура("Организация, НачалоПериода, КонецПериода");
		ЗаполнитьЗначенияСвойств(ПараметрыОтчета, Объект);
		ПараметрыОтчета.НачалоПериода = НачалоГода(ПараметрыОтчета.НачалоПериода);
		ОткрытьФорму("Отчет.КнигаУчетаДоходовИРасходов.Форма.ФормаОтчета", ПараметрыОтчета);
	КонецЕсли;
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьЧисленность" Тогда
		ПараметрыОтбора = Новый Структура("Организация, Период", Объект.Организация, Объект.НачалоПериода);
		ПараметрыОтчета = Новый Структура("Отбор", ПараметрыОтбора);
		ОписаниеОповещения = Новый ОписаниеОповещения("ВводСреднесписочнойЧисленностиЗавершение", ЭтотОбъект);
		ОткрытьФорму("Документ.СтатистикаПерсонала.ФормаСписка", ПараметрыОтчета, ЭтотОбъект,
			УникальныйИдентификатор,,, ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#Область ОбработчикиСобытийПараметров

&НаКлиенте
Процедура КонецПериодаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ПараметрыВыбора = Новый Структура("НачалоПериода, КонецПериода", Объект.НачалоПериода, Объект.КонецПериода);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтотОбъект);
	ОткрытьФорму("Обработка.ПомощникФормированияКУДиР.Форма.ВыборПериода", ПараметрыВыбора, Элемент, , , , ОписаниеОповещения);
КонецПроцедуры

&НаКлиенте
Процедура КонецПериодаРегулирование(Элемент, Направление, СтандартнаяОбработка)
	ОбщегоНазначенияУТКлиент.РегулированиеПредставленияПериодаРегистрации(Направление, СтандартнаяОбработка, Объект.КонецПериода, ПредставлениеКонцаПериода);
	УстановитьНачалоПериодаНаОснованииКонцаПериода();
	ЗаполнитьСведенияОНалоговомПериоде();
	Элементы.ПояснениеРасширенныйНалоговыйПериод.Видимость = ОтчетЗаРасширенныйНалоговыйПериод(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	УправлениеФормой();
	ОбновитьСтраницыВыполненияЗаданий();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийПодготовкиДанных

&НаКлиенте
Процедура ДекорацияНастройкаПризнанияСтатейРасходовНажатие(Элемент)
	
	ОткрытьФорму("Обработка.ПомощникФормированияКУДиР.Форма.ФормаНастройкиПризнанияСтатейРасходов",, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияНастройкаГруппНоменклатурыНажатие(Элемент)
	
	ПараметрыФормы = Новый Структура("Организация", Объект.Организация);
	ПараметрыФормы.Вставить("ДатаОкончанияПериода", Новый СтандартнаяДатаНачала(Объект.КонецПериода));
	ПараметрыФормы.Вставить("НастройкаСчетовУчета", ПараметрыНастройкиСчетовУчетаМатериалов());
	ОткрытьФорму("Обработка.НастройкаОтраженияДокументовВРеглУчете.Форма.ФормаНастройки", ПараметрыФормы, ЭтотОбъект, "НастройкаГруппНоменклатуры_"+ЭтотОбъект.УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПризнаниеРасходовНажатие(Элемент)
	
	СтруктураПериод = Новый Структура("ДатаНачала, ДатаОкончания",
		НачалоМесяца(Объект.НачалоПериода), КонецМесяца(Объект.КонецПериода));
	ПараметрыФормы = Новый Структура("Период, ДатаОкончанияПериода",
		СтруктураПериод, КонецМесяца(Объект.КонецПериода));
	ТипОперации = ПредопределенноеЗначение("Перечисление.ТипыРегламентныхОпераций.ПризнаниеРасходовПриУСН");
	ПараметрыФормы.Вставить("Отбор", Новый Структура("Организация, ТипОперации", Объект.Организация, ТипОперации));
	ОткрытьФорму("Документ.РегламентнаяОперация.Форма.ФормаСписка", ПараметрыФормы, Элемент, "ПросмотрРеглОперацийПризнанияРасходовУСН_"+ЭтотОбъект.УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ДействиеПризнаниеРасходовОтменитьНажатие(Элемент)
	ОтменитьЗадание(РезультатФоновогоЗадания.ИдентификаторЗадания);
	ОтключитьОбработчикОжидания("ПроверитьВыполнениеФоновогоЗадания");
	СтатусАвтоматическогоПризнанияРасходов = 0;
	ОбновитьСтраницыВыполненияЗаданий();
КонецПроцедуры

&НаКлиенте
Процедура ДействиеПризнаниеРасходовНажатие(Элемент)
	ЗапуститьВыполнениеФоновогоЗадания("ПризнаниеРасходов", СтатусАвтоматическогоПризнанияРасходов);
КонецПроцедуры

&НаКлиенте
Процедура ДействиеРасчетНалогаНажатие(Элемент)
	ЗапуститьВыполнениеФоновогоЗадания("РасчетНалога", СтатусРасчетаНалога);
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияРасчетНалогаНажатие(Элемент)
	
	СтруктураПериод = Новый Структура("ДатаНачала, ДатаОкончания",
		НачалоМесяца(Объект.НачалоПериода), КонецМесяца(Объект.КонецПериода));
	ПараметрыФормы = Новый Структура("Период, ДатаОкончанияПериода",
		СтруктураПериод, КонецМесяца(Объект.КонецПериода));
	ТипОперации = ПредопределенноеЗначение("Перечисление.ТипыРегламентныхОпераций.РасчетНалогаУСН");
	ПараметрыФормы.Вставить("Отбор", Новый Структура("Организация, ТипОперации", Объект.Организация, ТипОперации));
	ОткрытьФорму("Документ.РегламентнаяОперация.Форма.ФормаСписка", ПараметрыФормы, Элемент, "ПросмотрРеглОперацийРасчетНалогаУСН_"+ЭтотОбъект.УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ДействиеРасчетНалогаОтменитьНажатие(Элемент)
	ОтменитьЗадание(РезультатФоновогоЗадания.ИдентификаторЗадания);
	ОтключитьОбработчикОжидания("ПроверитьВыполнениеФоновогоЗадания");
	СтатусРасчетаНалога = 0;
	ОбновитьСтраницыВыполненияЗаданий();
КонецПроцедуры

&НаКлиенте
Процедура ДействиеСторноДоходовОтменитьНажатие(Элемент)
	ОтменитьЗадание(РезультатФоновогоЗадания.ИдентификаторЗадания);
	ОтключитьОбработчикОжидания("ПроверитьВыполнениеФоновогоЗадания");
	СтатусСторноДоходов = 0;
	ОбновитьСтраницыВыполненияЗаданий();
КонецПроцедуры

&НаКлиенте
Процедура ДействиеСторноДоходовНажатие(Элемент)
	ЗапуститьВыполнениеФоновогоЗадания("СторноДоходов", СтатусСторноДоходов);
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияНастройкаКорректировкиОтраженияНажатие(Элемент)
	Период = Новый СтандартныйПериод(Объект.НачалоПериода, КонецМесяца(Объект.КонецПериода));
	ПараметрыФормы = Новый Структура("Организация, Период", Объект.Организация, Период);
	ОткрытьФорму("Обработка.ПомощникФормированияКУДиР.Форма.ФормаНастройкиПризнанияОпераций", ПараметрыФормы, ЭтотОбъект, "НастройкаКорректировкиОтражения_"+ЭтотОбъект.УникальныйИдентификатор);
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияНастрокаПатентовНажатие(Элемент)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация", Объект.Организация);
	
	// &ЗамерПроизводительности
	ОценкаПроизводительностиКлиент.ЗамерВремени("Справочник.Патенты.Форма.НастройкаПримененияПатентов");
	
	ОткрытьФорму("Справочник.Патенты.Форма.НастройкаПримененияПатентов", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияКорректировкаПатентовНажатие(Элемент)
	Период = Новый СтандартныйПериод(Объект.НачалоПериода, КонецМесяца(Объект.КонецПериода));
	ПараметрыФормы = Новый Структура("Организация, Период", Объект.Организация, Период);
	ОткрытьФорму("Обработка.ПомощникФормированияКУДиР.Форма.ФормаНастройкиПатентов", ПараметрыФормы, ЭтотОбъект, "НастройкаПатентов_"+ЭтотОбъект.УникальныйИдентификатор);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийПроверкиИАнализаДанных

&НаКлиенте
Процедура ДекорацияОткрытьРасходыНажатие(Элемент)
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("ПериодОтчета", Новый СтандартныйПериод(Объект.НачалоПериода, КонецМесяца(Объект.КонецПериода)));
	ПараметрыОтчета.Вставить("Отбор", Новый Структура("Организация", Объект.Организация));
	КлючУникальностиОтчета = СтрШаблон("%1:%2", "ПрочиеРасходы", Объект.Организация.УникальныйИдентификатор());

	ОткрытьФорму("Отчет.АнализРасходовПриУСН.Форма", ПараметрыФормыАнализаРасходов("ПрочиеРасходы", ПараметрыОтчета), ЭтотОбъект, КлючУникальностиОтчета);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОткрытьРасходыПоСебестоимостиНажатие(Элемент)
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("ПериодОтчета", Новый СтандартныйПериод(Объект.НачалоПериода, КонецМесяца(Объект.КонецПериода)));
	ПараметрыОтчета.Вставить("Отбор", Новый Структура("Организация", Объект.Организация));
	КлючУникальностиОтчета = СтрШаблон("%1:%2", "РасходыПоСебестоимости", Объект.Организация.УникальныйИдентификатор());

	ОткрытьФорму("Отчет.АнализРасходовПриУСН.Форма", ПараметрыФормыАнализаРасходов("РасходыПоСебестоимости", ПараметрыОтчета), ЭтотОбъект, КлючУникальностиОтчета);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОткрытьРасходыПоАмортизацииНажатие(Элемент)
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("ПериодОтчета", Новый СтандартныйПериод(Объект.НачалоПериода, КонецМесяца(Объект.КонецПериода)));
	ПараметрыОтчета.Вставить("Отбор", Новый Структура("Организация", Объект.Организация));
	КлючУникальностиОтчета = СтрШаблон("%1:%2", "РасходыПоАмортизации", Объект.Организация.УникальныйИдентификатор());

	ОткрытьФорму("Отчет.АнализРасходовПриУСН.Форма", ПараметрыФормыАнализаРасходов("РасходыПоАмортизации", ПараметрыОтчета), ЭтотОбъект, КлючУникальностиОтчета);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОткрытьСписокЗаписейКУДиРНажатие(Элемент)
	ОтборСпискаДокументов = Новый Структура("Организация, Дата", Объект.Организация, Объект.НачалоПериода);
	ПараметрыСпискаДокументов = Новый Структура("Отбор, РежимОтбораПоДате", ОтборСпискаДокументов, "СДаты");
	ОткрытьФорму("Документ.ЗаписьКУДиР.ФормаСписка", ПараметрыСпискаДокументов);
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОткрытьРасходыПоМатериаламНажатие(Элемент)
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("ПериодОтчета", Новый СтандартныйПериод(Объект.НачалоПериода, КонецМесяца(Объект.КонецПериода)));
	ПараметрыОтчета.Вставить("Отбор", Новый Структура("Организация", Объект.Организация));
	КлючУникальностиОтчета = СтрШаблон("%1:%2", "РасходыПоМатериалам", Объект.Организация.УникальныйИдентификатор());

	ОткрытьФорму("Отчет.АнализРасходовПриУСН.Форма", ПараметрыФормыАнализаРасходов("РасходыПоМатериалам", ПараметрыОтчета), ЭтотОбъект, КлючУникальностиОтчета);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийОтчетности

&НаКлиенте
Процедура ДекорацияОткрытьОтчетКнигаУчетаДоходовИРасходовНажатие(Элемент)
	ПараметрыОтчета = Новый Структура("Организация, НачалоПериода, КонецПериода");
	ЗаполнитьЗначенияСвойств(ПараметрыОтчета, Объект);
	ОткрытьФорму("Отчет.КнигаУчетаДоходовИРасходов.Форма.ФормаОтчета", ПараметрыОтчета);
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОткрытьОтчетДекларацияПоУСННажатие(Элемент)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("мДатаНачалаПериодаОтчета", Объект.НачалоПериода);
	ПараметрыФормы.Вставить("мДатаКонцаПериодаОтчета",  КонецМесяца(Объект.КонецПериода));
	ПараметрыФормы.Вставить("Организация",              Объект.Организация);
	ПараметрыФормы.Вставить("мВыбраннаяФорма",          "ФормаОтчета2014Кв1");
	ПараметрыФормы.Вставить("ДоступенМеханизмПечатиРеглОтчетностиСДвухмернымШтрихкодомPDF417",
		РегламентированнаяОтчетностьКлиент.ДоступенМеханизмПечатиРеглОтчетностиСДвухмернымШтрихкодомPDF417());
	
	ОткрытьФорму("Отчет.РегламентированныйОтчетУСН.Форма", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОткрытьОтчетСправкаРасчетНалогаУСННажатие(Элемент)
	
	ПараметрыОтчета = Новый Структура("Организация, НачалоПериода, КонецПериода");
	ЗаполнитьЗначенияСвойств(ПараметрыОтчета, Объект);
	ОткрытьФорму("Отчет.СправкаРасчетНалогаУСН.ФормаОбъекта", ПараметрыОтчета);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОткрытьОтчетКнигаУчетаДоходовПатентНажатие(Элемент)
	
	ПараметрыОтчета = Новый Структура("Организация, НачалоПериода, КонецПериода");
	ЗаполнитьЗначенияСвойств(ПараметрыОтчета, Объект);
	ОткрытьФорму("Отчет.КнигаУчетаДоходовПатент.Форма.ФормаОтчета", ПараметрыОтчета);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоясненияКЭтапам(Команда)
	
	ПоказатьПоясненияКЭтапам = Не ПоказатьПоясненияКЭтапам;
	
	УстановитьЗаголовокКомандыИВидимостьПояснений();
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	УправлениеФормой();
	ОбновитьСтраницыВыполненияЗаданий();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область РаботаСФормой

&НаСервере
Процедура УправлениеФормой()
	
	УстановитьСписокОрганизацийНаУСНЗаПериод(Объект.НачалоПериода, КонецМесяца(Объект.КонецПериода), Элементы.Организация.СписокВыбора, Объект.Организация);
	
	ОбщегоНазначенияБПКлиентСервер.УстановитьПараметрОрганизацияФункциональныхОпцийФормы(ЭтотОбъект, Объект.Организация, Объект.НачалоПериода);
	
	ПрименяетсяУСН = Ложь;
	ПрименяетсяПСН = Ложь;
	ИспользуетсяТрудНаемныхРаботников = Ложь;
	ПараметрыСистемыНалогообложения = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитикНаДату(
		"НастройкиСистемыНалогообложения", Объект.Организация, Объект.НачалоПериода);
	ПараметрыУчетнойПолитикиУСН = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитикНаДату(
		"НастройкиУчетаУСН", Объект.Организация, Объект.НачалоПериода);
	Если ПараметрыСистемыНалогообложения <> Неопределено Тогда
		ПрименяетсяУСН = ПараметрыСистемыНалогообложения.ПрименяетсяУСН;
		ПрименяетсяПСН = ПараметрыСистемыНалогообложения.ПрименяетсяПСН;
	КонецЕсли;   
	
	Если ПараметрыУчетнойПолитикиУСН <> Неопределено Тогда
		ПрименяетсяУСНДоходыМинусРасходы = ПараметрыУчетнойПолитикиУСН.ПрименяетсяУСНДоходыМинусРасходы;
		СтавкаНалогаУСН = ПараметрыУчетнойПолитикиУСН.СтавкаНалогаУСН;
		ИспользуетсяТрудНаемныхРаботников = ПараметрыУчетнойПолитикиУСН.ИспользуетсяТрудНаемныхРаботников;
	Иначе
		СтавкаНалогаУСН = УчетУСНПСНКлиентСервер.НалоговаяСтавкаПоУмолчанию(ПрименяетсяУСНДоходыМинусРасходы);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Организация) И ПрименяетсяУСН Тогда
		ПредставлениеОбъектаНалогообложения = НСтр("ru = 'Используется объект налогообложения ""Доходы%1"".';
													|en = 'Taxation object ""Income%1"" is used.'");
		ПредставлениеОбъектаНалогообложения = СтрШаблон(ПредставлениеОбъектаНалогообложения,
			?(ПрименяетсяУСНДоходыМинусРасходы, НСтр("ru = ', уменьшенные на величину расходов';
													|en = ', reduced by the expense amount'"), ""));
	Иначе
		ПредставлениеОбъектаНалогообложения = ?(ПрименяетсяУСН, НСтр("ru = 'Не выбрана организация.';
																	|en = 'Company is not selected.'"),
			НСтр("ru = 'Нет организаций, использующих УСН.';
				|en = 'No companies which use STS.'"));	
	КонецЕсли;
	
	Элементы.ГруппаОсновная.Видимость = ПрименяетсяУСН;
	Элементы.ГруппаПодготовкаСтатьиРасходов.Видимость = ПрименяетсяУСНДоходыМинусРасходы;
	Элементы.ГруппаПодготовкаПорядкаУчетаНоменклатуры.Видимость = ПрименяетсяУСНДоходыМинусРасходы;
	Элементы.ГруппаПроверкаРасходы.Видимость = ПрименяетсяУСНДоходыМинусРасходы;
	Элементы.ГруппаПодготовкаАвтоматическоеПризнаниеРасходов.Видимость = ПрименяетсяУСНДоходыМинусРасходы;
	Элементы.ГруппаПодготовкаДанныхСторноДоходов.Видимость = ПрименяетсяУСН;
	Элементы.ДекорацияОткрытьОтчетКнигаУчетаДоходовИРасходов.Видимость = ПравоДоступа("Просмотр", Метаданные.Отчеты.КнигаУчетаДоходовИРасходов);
	Элементы.ДекорацияОткрытьОтчетСправкаРасчетНалогаУСН.Видимость = ПравоДоступа("Просмотр", Метаданные.Отчеты.СправкаРасчетНалогаУСН);
	Элементы.ДекорацияОткрытьОтчетДекларацияПоУСН.Видимость = ПравоДоступа("Просмотр", Метаданные.Отчеты.РегламентированныйОтчетУСН)
		И ПравоДоступа("Чтение", Метаданные.Справочники.РегламентированныеОтчеты);
	Элементы.ГруппаПодготовкаПатентов.Видимость = ПрименяетсяПСН;
	Элементы.ГруппаКорректировкаПатентов.Видимость = ПрименяетсяПСН;
	Элементы.ГруппаПояснениеКорректировкаПатентов.Видимость = ПрименяетсяПСН;
	Элементы.ГруппаОтчетностьКнигаУчетаДоходовПатент.Видимость = ПрименяетсяПСН;
	Элементы.ГруппаСтавкиНалогаУСН.Видимость = Объект.НачалоПериода >= УчетУСНКлиентСервер.ДатаНачалаПрогрессивнойШкалы();
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачалоРасчетаОперацийПоУСН = УчетУСНПСНСервер.НачалоРасчетаОперацийПоУСН(Объект.КонецПериода, Объект.Организация);
	СтатусАвтоматическогоПризнанияРасходов = ?(НачалоРасчетаОперацийПоУСН.НачалоРасчетаПризнаниеРасходовПриУСН > КонецМесяца(Объект.КонецПериода), 2, 0);
	СтатусСторноДоходов = ?(НачалоРасчетаОперацийПоУСН.НачалоРасчетаСторноДоходовКУДиР > КонецМесяца(Объект.КонецПериода), 2, 0);
	
	Если НачалоРасчетаОперацийПоУСН.НачалоРасчетаПризнаниеРасходовПриУСН < Объект.НачалоПериода Тогда
		ТекстПредупреждения = НСтр("ru = 'Не выполнено признание расходов начиная с периода: %1 г. Признание расходов в текущем периоде невозможно.';
									|en = 'Expenses are not recognized starting from the period: %1. Cannot recognize expenses in the current period.'");
		МесяцНачалаРасчета = Формат(НачалоРасчетаОперацийПоУСН.НачалоРасчетаПризнаниеРасходовПриУСН, "ДФ='ММММ гггг'");
		ПредупреждениеБолееРаннегоПризнания = СтрШаблон(ТекстПредупреждения, МесяцНачалаРасчета);
		СтатусАвтоматическогоПризнанияРасходов = 3;
	Иначе
		ПредупреждениеБолееРаннегоПризнания = "";
	КонецЕсли;
	
	Если НачалоРасчетаОперацийПоУСН.НачалоРасчетаСторноДоходовКУДиР < Объект.НачалоПериода Тогда
		ТекстПредупреждения = НСтр("ru = 'Изменены взаиморасчеты с клиентами начиная с периода: %1 г. Доходы в Книге учета доходов и расходов могут быть не актуальными.';
									|en = 'Mutual settlements with customers are changed starting from the period: %1. Income in the Ledger of income and expenditure may be irrelevant. '");
		МесяцНачалаРасчета = Формат(НачалоРасчетаОперацийПоУСН.НачалоРасчетаСторноДоходовКУДиР, "ДФ='ММММ гггг'");
		ПредупреждениеБолееРаннегоСторно = СтрШаблон(ТекстПредупреждения, МесяцНачалаРасчета);
	Иначе
		ПредупреждениеБолееРаннегоСторно = "";
	КонецЕсли;
	
	Элементы.ГруппаПредупреждениеОНеобходимостиБолееРаннегоПризнания.Видимость = ЗначениеЗаполнено(ПредупреждениеБолееРаннегоПризнания);
	Элементы.ГруппаПредупреждениеОНеобходимостиБолееРаннегоСторно.Видимость = ЗначениеЗаполнено(ПредупреждениеБолееРаннегоСторно);
	
	НеобходимРасчетНалогаПриУСН = КонецМесяца(Объект.КонецПериода) = КонецКвартала(Объект.КонецПериода)
		И НачалоРасчетаОперацийПоУСН.НачалоРасчетаНалогаУСН <= КонецМесяца(Объект.КонецПериода);
	СтатусРасчетаНалога = ?(НеобходимРасчетНалогаПриУСН, 0, 2);
	
	ЗаполнитьСведенияОНалоговомПериоде();
	Элементы.ПояснениеРасширенныйНалоговыйПериод.Видимость = ОтчетЗаРасширенныйНалоговыйПериод(ЭтотОбъект);
	
	Если Объект.НачалоПериода >= УчетУСНКлиентСервер.ДатаНачалаПрогрессивнойШкалы() Тогда
		УстановкаНастроекСтавкиНалога(СтавкаНалогаУСН);
		Элементы.СреднесписочнаяЧисленность.Видимость = ИспользуетсяТрудНаемныхРаботников;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовокКомандыИВидимостьПояснений()
	
	Если ПоказатьПоясненияКЭтапам Тогда
		Элементы.ФормаПоясненияКЭтапам.Заголовок = НСтр("ru = 'Скрыть пояснения к этапам';
														|en = 'Hide explanations to steps'");
	Иначе
		Элементы.ФормаПоясненияКЭтапам.Заголовок = НСтр("ru = 'Показать пояснения к этапам';
														|en = 'Show explanations to steps'");
	КонецЕсли;
	
	Для каждого ЭлементФормы Из Элементы Цикл
		Если ЭлементФормы.Имя = "ГруппаПояснениеКорректировкаПатентов" И НЕ Элементы.ГруппаКорректировкаПатентов.Видимость Тогда
			// Для пояснения патентов, видимость еще зависит от применения патента, анализируем видимость главного элемента корректировки
			Продолжить;
		КонецЕсли;
		Если ТипЗнч(ЭлементФормы) = Тип("ГруппаФормы") И СтрНачинаетсяС(ЭлементФормы.Имя, "ГруппаПояснение") Тогда
			ЭлементФормы.Видимость = ПоказатьПоясненияКЭтапам;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьСписокОрганизацийНаУСНЗаПериод(НачалоПериода, КонецПериода, СписокОрганизаций, ТекущаяОрганизация)
	
	Обработки.ПомощникФормированияКУДиР.УстановитьСписокОрганизацийНаУСНЗаПериод(СписокОрганизаций, НачалоПериода, КонецПериода, ТекущаяОрганизация);		
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСтраницыВыполненияЗаданий()
	
	#Область ПризнаниеРасходов
	Если СтатусАвтоматическогоПризнанияРасходов = 0 Тогда // Не выполнено
		
		Элементы.КартинкиПризнаниеРасходов.ТекущаяСтраница = Элементы.ПризнаниеРасходовНеВыполнено;
		Элементы.ДействияПризнаниеРасходов.ТекущаяСтраница = Элементы.ДействиеПризнаниеРасходовВыполнить;
		
	ИначеЕсли СтатусАвтоматическогоПризнанияРасходов = 1 Тогда // Выполняется
		
		Элементы.КартинкиПризнаниеРасходов.ТекущаяСтраница = Элементы.ПризнаниеРасходовВыполняется;
		Элементы.ДействияПризнаниеРасходов.ТекущаяСтраница = Элементы.ДействиеПризнаниеРасходовВыполняется;
		
	ИначеЕсли СтатусАвтоматическогоПризнанияРасходов = 2 Тогда // Выполнено
		
		Элементы.КартинкиПризнаниеРасходов.ТекущаяСтраница = Элементы.ПризнаниеРасходовВыполненоУспешно;
		Элементы.ДействияПризнаниеРасходов.ТекущаяСтраница = Элементы.ДействиеПризнаниеРасходовВыполнить;
		
	ИначеЕсли СтатусАвтоматическогоПризнанияРасходов = 3 Тогда // Ожидает выполнения признания расходов предыдущих кварталов
		
		Элементы.КартинкиПризнаниеРасходов.ТекущаяСтраница = Элементы.ПризнаниеРасходовОжидается;
		Элементы.ДействияПризнаниеРасходов.ТекущаяСтраница = Элементы.ДействиеПризнаниеРасходовОжидается;
		
	КонецЕсли;	
	#КонецОбласти
	
	#Область СторноДоходов
	Если СтатусСторноДоходов = 0 Тогда // Не выполнено
		
		Элементы.КартинкиСторноДоходов.ТекущаяСтраница = Элементы.СторноДоходовНеВыполнено;
		Элементы.ДействияСторноДоходов.ТекущаяСтраница = Элементы.ДействиеСторноДоходовВыполнить;
		
	ИначеЕсли СтатусСторноДоходов = 1 Тогда // Выполняется
		
		Элементы.КартинкиСторноДоходов.ТекущаяСтраница = Элементы.СторноДоходовВыполняется;
		Элементы.ДействияСторноДоходов.ТекущаяСтраница = Элементы.ДействиеСторноДоходовВыполняется;
		
	ИначеЕсли СтатусСторноДоходов = 2 Тогда // Выполнено
		
		Элементы.КартинкиСторноДоходов.ТекущаяСтраница = Элементы.СторноДоходовВыполненоУспешно;
		Элементы.ДействияСторноДоходов.ТекущаяСтраница = Элементы.ДействиеСторноДоходовВыполнить;
		
	ИначеЕсли СтатусСторноДоходов = 3 Тогда // Ожидает выполнения сторно доходов предыдущих кварталов
		
		Элементы.КартинкиСторноДоходов.ТекущаяСтраница = Элементы.СторноДоходовОжидается;
		Элементы.ДействияСторноДоходов.ТекущаяСтраница = Элементы.ДействиеСторноДоходовОжидается;
		
	КонецЕсли;	
	#КонецОбласти
	
	#Область НачислениеНалога
	
	ПояснениеРасчетНалога = ПояснениеРасчетаНалога(Объект.КонецПериода, СтатусАвтоматическогоПризнанияРасходов, СтатусСторноДоходов);
		
	Если Не КонецМесяца(Объект.КонецПериода) = КонецКвартала(Объект.КонецПериода) Тогда // не требуется если конец периода не является концом квартала
		
		Элементы.КартинкиРасчетНалога.ТекущаяСтраница = Элементы.РасчетНалогаНеТребуется;
		Элементы.ДействияРасчетНалога.ТекущаяСтраница = Элементы.ДействиеРасчетНалогаНеТребуется;
		
	ИначеЕсли Не СтатусАвтоматическогоПризнанияРасходов = 2 И Элементы.ГруппаПодготовкаАвтоматическоеПризнаниеРасходов.Видимость
		ИЛИ Не СтатусСторноДоходов = 2 И Элементы.ГруппаПодготовкаДанныхСторноДоходов.Видимость Тогда // Ожидает выполнения признания расходов или сторно доходов
		
		Элементы.КартинкиРасчетНалога.ТекущаяСтраница = Элементы.РасчетНалогаОжидается;
		Элементы.ДействияРасчетНалога.ТекущаяСтраница = Элементы.ДействиеРасчетНалогаОжидается;
		
	ИначеЕсли СтатусРасчетаНалога = 0 Тогда // Не выполнено
		
		Элементы.КартинкиРасчетНалога.ТекущаяСтраница = Элементы.РасчетНалогаНеВыполнен;
		Элементы.ДействияРасчетНалога.ТекущаяСтраница = Элементы.ДействиеРасчетНалогаВыполнить;
		
	ИначеЕсли СтатусРасчетаНалога = 1 Тогда // Выполняется
		
		Элементы.КартинкиРасчетНалога.ТекущаяСтраница = Элементы.РасчетНалогаВыполняется;
		Элементы.ДействияРасчетНалога.ТекущаяСтраница = Элементы.ДействиеРасчетНалогаВыполняется;
		
	ИначеЕсли СтатусРасчетаНалога = 2 Тогда // Выполнено
		
		Элементы.КартинкиРасчетНалога.ТекущаяСтраница = Элементы.РасчетНалогаВыполненУспешно;
		Элементы.ДействияРасчетНалога.ТекущаяСтраница = Элементы.ДействиеРасчетНалогаВыполнить;
		УправлениеФормой();
		
	КонецЕсли;
	
	#КонецОбласти
			
КонецПроцедуры

#КонецОбласти

#Область ВыполнениеФоновогоЗадания

&НаКлиенте
Процедура ЗапуститьВыполнениеФоновогоЗадания(ИмяЗадания, СтатусВыполнения)
	
	ПараметрыФоновогоЗадания = Новый Структура("НачДата, КонДата, Организация", Объект.НачалоПериода, КонецМесяца(Объект.КонецПериода), Объект.Организация);
	
	Попытка
		
		Если ИмяЗадания = "РасчетНалога" Тогда
			РезультатФоновогоЗадания = ВыполнитьРасчетНалогаНаСервере(УникальныйИдентификатор, ПараметрыФоновогоЗадания);
		Иначе
			Если ИмяЗадания = "ПризнаниеРасходов" Тогда
				Метод = "УчетУСНПСНСервер.ПризнатьИОтразитьРасходыКУДиР";
				НаименованиеЗадания = НСтр("ru = 'Выполняется признание расходов по УСН';
											|en = 'Recognizing STS expenses'");
			Иначе
				Метод = "УчетУСНПСНСервер.СторноДоходовКУДиР";
				НаименованиеЗадания = НСтр("ru = 'Выполняется сторно доходов в КУДиР';
											|en = 'Performing income storno in LIE'");
			КонецЕсли;
			РезультатФоновогоЗадания = ВыполнитьРегламентныеОперацииНаСервере(УникальныйИдентификатор, ПараметрыФоновогоЗадания, Метод, НаименованиеЗадания);
		КонецЕсли;
		
		ОжидатьВыполнения = Не РезультатФоновогоЗадания.ЗаданиеВыполнено;
		СтатусВыполнения = ?(РезультатФоновогоЗадания.ЗаданиеВыполнено, 2, 1);

	Исключение
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
		ОжидатьВыполнения = Ложь;
		СтатусВыполнения = 0;
		
	КонецПопытки;
	
	Если ИмяЗадания = "ПризнаниеРасходов" Тогда
		СтатусАвтоматическогоПризнанияРасходов = СтатусВыполнения;
		// После признания расходов расчет налога всегда не выполнен, в виду того что регламентная операция признания расходов
		// в начале очищает свои движения (то есть записывает пустой регистр) и лишь затем заполняет его.
		СтатусРасчетаНалога = 0;
		ОбновитьСтраницыВыполненияЗаданий();
	ИначеЕсли ИмяЗадания = "СторноДоходов" Тогда
		СтатусСторноДоходов = СтатусВыполнения;
		// После сторно доходов расчет налога всегда не выполнен, в виду того что регламентная операция сторно доходов
		// в начале очищает свои движения (то есть записывает пустой регистр) и лишь затем заполняет его.
		СтатусРасчетаНалога = 0;
		ОбновитьСтраницыВыполненияЗаданий();
	Иначе
		СтатусРасчетаНалога = СтатусВыполнения;
		ОбновитьСтраницыВыполненияЗаданий();
	КонецЕсли;
	
	Если ОжидатьВыполнения Тогда
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("ПроверитьВыполнениеФоновогоЗадания", ПараметрыОбработчикаОжидания.ТекущийИнтервал);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаданиеВыполнено(ИдентификаторЗадания)
	
	Возврат ДлительныеОперации.ЗаданиеВыполнено(ИдентификаторЗадания);

КонецФункции

&НаСервереБезКонтекста
Процедура ОтменитьЗадание(ИдентификаторЗадания)
	
	ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыполнениеФоновогоЗадания()

	Попытка
		ЗаданиеЗавершено = ЗаданиеВыполнено(РезультатФоновогоЗадания.ИдентификаторЗадания);
		СтатусВыполнения = ?(ЗаданиеЗавершено, 2, 1);
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
		СтатусВыполнения = 0;
		ЗаданиеЗавершено = Истина;
	КонецПопытки;
	
	Если ЗаданиеЗавершено Тогда
			
		ОтключитьОбработчикОжидания("ПроверитьВыполнениеФоновогоЗадания");
		ПараметрыОбработчикаОжидания = Неопределено;
		РезультатФоновогоЗадания = Неопределено;
		
		Если СтатусАвтоматическогоПризнанияРасходов = 1 Тогда
			СтатусАвтоматическогоПризнанияРасходов = СтатусВыполнения;
			// После признания расходов расчет налога всегда не выполнен, в виду того что регламентная операция признания расходов
			// в начале очищает свои движения (то есть записывает пустой регистр) и лишь затем заполняет его.
			СтатусРасчетаНалога = 0;
			ОбновитьСтраницыВыполненияЗаданий();
		ИначеЕсли СтатусСторноДоходов = 1 Тогда
			СтатусСторноДоходов = СтатусВыполнения;
			// После сторно доходов расчет налога всегда не выполнен, в виду того что регламентная операция сторно доходов
			// в начале очищает свои движения (то есть записывает пустой регистр) и лишь затем заполняет его.
			СтатусРасчетаНалога = 0;
			ОбновитьСтраницыВыполненияЗаданий();
		ИначеЕсли СтатусРасчетаНалога = 1 Тогда
			СтатусРасчетаНалога = СтатусВыполнения;
			ОбновитьСтраницыВыполненияЗаданий();
		КонецЕсли;
			
	Иначе
		
		ОтключитьОбработчикОжидания("ПроверитьВыполнениеФоновогоЗадания");
		ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("ПроверитьВыполнениеФоновогоЗадания", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область АвтоматическоеПризнаниеРасходов

&НаСервереБезКонтекста
Функция ВыполнитьРегламентныеОперацииНаСервере(УникальныйИдентификатор, ПараметрыЭкспортнойПроцедуры, Метод, ИмяЗадания)
	
	РезультатФоновогоЗадания = ДлительныеОперации.ЗапуститьВыполнениеВФоне(УникальныйИдентификатор, Метод, ПараметрыЭкспортнойПроцедуры, ИмяЗадания);
	
	Возврат РезультатФоновогоЗадания;
	
КонецФункции

#КонецОбласти

#Область РасчетНалога

&НаСервереБезКонтекста
Функция ВыполнитьРасчетНалогаНаСервере(УникальныйИдентификатор, ПараметрыЭкспортнойПроцедуры, ПредставлениеРасчетаНалога = Неопределено)
	
	Если ПредставлениеРасчетаНалога = Неопределено Тогда
		ПредставлениеРасчетаНалога = НСтр("ru = 'расчет налога при УСН';
											|en = 'STS tax calculation '");
	КонецЕсли;
	
	НаименованиеЗадания = СтрШаблон(НСтр("ru = 'Выполняется %1';
										|en = 'In progress %1'"), ПредставлениеРасчетаНалога);
	
	РезультатФоновогоЗадания = ДлительныеОперации.ЗапуститьВыполнениеВФоне(УникальныйИдентификатор, 
		"УчетУСНПСНСервер.НачислитьНалогУСН", ПараметрыЭкспортнойПроцедуры, НаименованиеЗадания);
	
	Возврат РезультатФоновогоЗадания;
	
КонецФункции

&НаКлиенте
Функция ПояснениеРасчетаНалога(КонецПериода, СтатусПризнания, СтатусСторно)
	
	Если Не КонецМесяца(КонецПериода) = КонецКвартала(КонецПериода) Тогда
		
		Результат = НСтр("ru = 'Расчет начисления налога при УСН возможен только в конце квартала';
						|en = 'You can calculate STS tax accrual only at the end of quarter'");
		
	ИначеЕсли Не СтатусСторно = 2 Тогда
		
		Результат = НСтр("ru = 'Расчет начисления налога при УСН требует выполнения операции сторно доходов';
						|en = 'Calculation of STS tax accrual requires the storno income operation'");
		
	ИначеЕсли Не СтатусПризнания = 2 Тогда
		
		Результат = НСтр("ru = 'Расчет начисления налога при УСН требует выполнения операции признания расходов';
						|en = 'Calculation of STS tax accrual requires the expense recognition transaction'");

	ИначеЕсли КонецКвартала(КонецПериода) = КонецГода(КонецПериода) Тогда
		
		Результат = НСтр("ru = 'Расчет начисления налога при УСН за %1 год';
						|en = 'Calculation of STS tax accrual for %1'");
		
		Результат = СтрШаблон(Результат, Формат(КонецПериода, "ДФ=гггг"));
		
	ИначеЕсли КонецМесяца(КонецПериода) = КонецКвартала(КонецПериода) Тогда
		
		Результат = НСтр("ru = 'Расчет авансового платежа при УСН за %1';
						|en = 'Calculation of advance payment under STS for %1'");
		
		Результат = СтрШаблон(Результат, ПредставлениеПериода(НачалоГода(КонецПериода), КонецКвартала(КонецПериода), "ФП = Истина"));
		
		СтроковыеФункцииКлиентСервер.УдалитьПоследнийСимволВСтроке(Результат, 2);
		Результат = Результат + НСтр("ru = 'года';
									|en = 'year'");
		
	Иначе
		
		Результат = "";
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Прочее

&НаКлиенте
Процедура ВыбратьПериодЗавершение(РезультатВыбора, ДопПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Объект.КонецПериода = НачалоМесяца(РезультатВыбора.КонецПериода);
	УстановитьНачалоПериодаНаОснованииКонцаПериода();
	ПредставлениеКонцаПериода = ОбщегоНазначенияУТКлиент.ПолучитьПредставлениеПериодаРегистрации(Объект.КонецПериода);
	ЗаполнитьСведенияОНалоговомПериоде();
	Элементы.ПояснениеРасширенныйНалоговыйПериод.Видимость = ОтчетЗаРасширенныйНалоговыйПериод(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВводСреднесписочнойЧисленностиЗавершение(РезультатВыбора, ДопПараметры) Экспорт
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНачалоПериодаНаОснованииКонцаПериода()
	
	НовоеНачалоПериода = НачалоКвартала(Объект.КонецПериода);
	Если Не НовоеНачалоПериода = Объект.НачалоПериода ИЛИ Объект.НачалоПериода > Объект.КонецПериода Тогда
		Объект.НачалоПериода = НовоеНачалоПериода;
		ПредставлениеНачалаПериода = ОбщегоНазначенияУТКлиент.ПолучитьПредставлениеПериодаРегистрации(Объект.НачалоПериода);
	КонецЕсли;
	УправлениеФормой();
	ОбновитьСтраницыВыполненияЗаданий();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПараметрыФормыАнализаРасходов(КлючВарианта, Параметры)
	
	НастройкаРасшифровкиАнализаРасхода = Отчеты.АнализРасходовПриУСН.ПолучитьНастройкиОтчетаПоОтбору(Параметры, КлючВарианта);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("КлючВарианта", КлючВарианта);
	ПараметрыФормы.Вставить("КлючПользовательскихНастроек", КлючВарианта);
	ПараметрыФормы.Вставить("ПользовательскиеНастройки", НастройкаРасшифровкиАнализаРасхода.ПользовательскиеНастройки);
	ПараметрыФормы.Вставить("ФиксированныеНастройки", НастройкаРасшифровкиАнализаРасхода.ФиксированныеНастройки);
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	
	Возврат ПараметрыФормы;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ОтчетЗаРасширенныйНалоговыйПериод(Форма)
	
	Возврат Форма.НалоговыйПериодПропущен
		ИЛИ (Форма.НалоговыйПериодРасширен И Форма.Объект.НачалоПериода = НачалоГода(Форма.Объект.НачалоПериода));
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСведенияОНалоговомПериоде()
	
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		ДатаНачалаДеятельности = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Организация, "ДатаРегистрации");
	Иначе
		ДатаНачалаДеятельности = '0001-01-01';
	КонецЕсли;
	
	НалоговыйПериодРасширен = УчетУСНПСНСервер.НалоговыйПериодРасширен(Объект.Организация,
		Объект.КонецПериода, ДатаНачалаДеятельности);
	НалоговыйПериодПропущен = УчетУСНПСНСервер.НалоговыйПериодПропущен(Объект.Организация,
		Объект.КонецПериода, ДатаНачалаДеятельности);
	
	НачалоНалоговогоПериода = ?(НалоговыйПериодРасширен, НачалоДня(ДатаНачалаДеятельности), НачалоГода(Объект.КонецПериода));
	
	Элементы.ПояснениеРасширенныйНалоговыйПериод.Заголовок = ПояснениеПереносНалоговогоПериода();
	
КонецПроцедуры

&НаСервере
Функция ПояснениеПереносНалоговогоПериода()
	
	ФорматнаяСтрокаПериода = "ДЛФ=D";
	
	Если НалоговыйПериодПропущен Тогда
		
		ГодОтчета = Год(Объект.КонецПериода);
		
		ШаблонПодсказки = НСтр("ru = 'Сформированные данные с даты регистрации %2 по %3 включатся в отчет за %4 год. Так как книгу доходов и расходов за %1 год формировать не нужно.';
								|en = 'Generated data from registration date %2 to %3 will be included in the report for %4 as there is no need to generate the ledger of income and expenditure for %1.'");
		
		Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПодсказки,
			Формат(ГодОтчета, "ЧГ=0"),
			Формат(НачалоНалоговогоПериода, ФорматнаяСтрокаПериода),
			Формат(КонецГода(НачалоНалоговогоПериода), ФорматнаяСтрокаПериода),
			Формат(ГодОтчета + 1, "ЧГ=0"));
		
	ИначеЕсли НалоговыйПериодРасширен Тогда
		
		Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'В книгу учета доходов и расходов с %1 включатся также данные и с даты регистрации %2 организации %3.';
				|en = 'Since %1 ledger of income and expenditure also includes data from registration date %2 of the %3 company.'"),
				Формат(Объект.НачалоПериода, ФорматнаяСтрокаПериода),
				Формат(НачалоНалоговогоПериода, ФорматнаяСтрокаПериода),
				Объект.Организация);
		
	Иначе
		
		Возврат "";
		
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПараметрыНастройкиСчетовУчетаМатериалов()
	
	ПараметрыНастройки = НастройкаСчетовУчетаСервер.ПараметрыНастройкиСчетовУчета(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве("НоменклатураСобственная"));
	Для каждого НастройкаРаздела Из ПараметрыНастройки.НастройкиРазделов Цикл
		Для каждого СчетУчета Из НастройкаРаздела.Значение.ЭлементыНастройки Цикл
			СчетУчета.Пометка = СчетУчета.Значение = "НаСкладе";
		КонецЦикла;
	КонецЦикла;
	Возврат ПараметрыНастройки;
	
КонецФункции

&НаСервере
Процедура УстановкаНастроекСтавкиНалога(СтавкаНалогаУСН)
	
	СуммаДоходов = УчетУСНПСНСервер.ОборотыКУДиРЗаПериод(Объект.Организация, НачалоГода(Объект.НачалоПериода),
		КонецМесяца(Объект.КонецПериода)).Доходы;
	ЛимитДоходов = КонтрольПраваПримененияСпецрежимаКлиентСервер.ГраницаДоходовДляПримененияОсновнойСтавкиУСН(
		Объект.КонецПериода);
	СуммаДоходовСтрокой = СтрШаблон(НСТр("ru = '%1%2млн.%2руб.';
										|en = '%1%2 million%2rubles'"), Формат(Окр(СуммаДоходов / 1000000, 2), "ЧН=0; ЧГ=;"),
		Символы.НПП);
	Цвет = ?(СуммаДоходов >= ЛимитДоходов, ЦветаСтиля.ЦветТекстаПроблема, Неопределено);
	
	Если Объект.НачалоПериода < УчетНДС.ДатаНачалаПримененияНДСнаУСН() Тогда
		Доходы = Новый ФорматированнаяСтрока(СуммаДоходовСтрокой,, Цвет,, "ОткрытьДоходы");
		Элементы.Доходы.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Авто;
	Иначе
		Доходы = Неопределено;
		Элементы.Доходы.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	КонецЕсли;
	
	Численность = УчетУСНПСНСервер.СредняяЧисленностьРаботников(Объект.Организация, НачалоГода(Объект.НачалоПериода),
		КонецМесяца(Объект.КонецПериода));
	ЧисленностьСтрокой = ?(Численность = Неопределено, НСтр("ru = 'Не указана';
															|en = 'Not specified'"), 
		СтрШаблон(НСТр("ru = '%1%2чел.';
						|en = '%1%2persons'"), Формат(Численность, "ЧДЦ=0; ЧГ=;"), Символы.НПП));
	ЛимитЧисленности =
		КонтрольПраваПримененияСпецрежимаКлиентСервер.ГраницаЧисленностиРаботниковДляПримененияОсновнойСтавкиУСН(Объект.НачалоПериода);
	Цвет = ?(Численность <> Неопределено И Численность > ЛимитЧисленности, ЦветаСтиля.ЦветТекстаПроблема, Неопределено);
	СреднесписочнаяЧисленность = Новый ФорматированнаяСтрока(ЧисленностьСтрокой,, Цвет,, "ОткрытьЧисленность");
	Подсказка = НСтр("ru = 'по умолчанию считается в пределах %1 человек';
					|en = 'по умолчанию считается в пределах %1 человек'");
	Элементы.СреднесписочнаяЧисленность.Подсказка = СтрШаблон(Подсказка,ЛимитЧисленности);
	Элементы.СреднесписочнаяЧисленность.ОтображениеПодсказки = ?(Численность = Неопределено ИЛИ Численность = 0,
		ОтображениеПодсказки.Кнопка, ОтображениеПодсказки.Нет);
	
	СтавкаПоУмолчанию = УчетУСНПСНКлиентСервер.НалоговаяСтавкаПоУмолчанию(ПрименяетсяУСНДоходыМинусРасходы);
	ДолжнаПрименятьсяПовышеннаяСтавка = НачалоГода(Объект.НачалоПериода) < УчетНДС.ДатаНачалаПримененияНДСнаУСН()
										И (СуммаДоходов >= ЛимитДоходов
											Или Численность <> Неопределено И Численность > ЛимитЧисленности);
	ПрименяетсяПовышеннаяСтавка = ДолжнаПрименятьсяПовышеннаяСтавка И СтавкаНалогаУСН > СтавкаПоУмолчанию;
	Элементы.ПояснениеСтавкиНалогаУСН.Заголовок = УчетУСНПСНКлиентСервер.УточнениеПримененияСтавкиНалогаУСН(
		ПрименяетсяПовышеннаяСтавка, НачалоГода(Объект.НачалоПериода));
	
	Цвет = Неопределено;
	Если ПрименяетсяПовышеннаяСтавка Тогда
		СтавкаНалогаСтрокой = НСтр("ru = 'Применяется повышенная ставка налога (%1)';
									|en = 'Increased tax rate applies (%1)'");
		Цвет = ЦветаСтиля.ЦветТекстаПроблема;
	ИначеЕсли ДолжнаПрименятьсяПовышеннаяСтавка Тогда
		СтавкаНалогаСтрокой = НСтр("ru = 'Будет применяться повышенная ставка налога (%1)';
									|en = 'Increased tax rate will apply (%1)'");
		Цвет = ЦветаСтиля.ЦветТекстаПроблема;
		СтавкаНалогаУСН = УчетУСНПСНКлиентСервер.НалоговаяСтавкаПовышенная(ПрименяетсяУСНДоходыМинусРасходы);
	Иначе
		СтавкаНалогаСтрокой = НСтр("ru = 'Применяется основная ставка налога (%1)';
									|en = 'Basic tax rate (%1) applies'");
	КонецЕсли;
	
	Если НачалоМесяца(Объект.НачалоПериода) = НачалоГода(Объект.НачалоПериода) И СтавкаНалогаУСН > СтавкаПоУмолчанию
		И НЕ ДолжнаПрименятьсяПовышеннаяСтавка Тогда
		// В прошлом году применялась повышенная ставка, с началом нового года, прекращает свое действие:
		СтавкаНалогаУСН = СтавкаПоУмолчанию;
	КонецЕсли;
	
	СтавкаНалогаСтрокой = СтрШаблон(СтавкаНалогаСтрокой, Строка(СтавкаНалогаУСН) + "%");
	ПредставлениеСтавкиНалога = Новый ФорматированнаяСтрока(СтавкаНалогаСтрокой,, Цвет);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти