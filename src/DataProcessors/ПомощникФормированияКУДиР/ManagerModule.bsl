#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура УстановитьСписокОрганизацийНаУСНЗаПериод(СписокОрганизаций, НачалоПериода, КонецПериода, ТекущаяОрганизация) Экспорт
	
	ОрганизацииНаУСН = УчетнаяПолитикаПереопределяемый.ОрганизацииНаУСНЗаПериод(НачалоПериода, КонецПериода);
	СписокОрганизаций.ЗагрузитьЗначения(ОрганизацииНаУСН);
	ТекущаяОрганизацияПрименяетУСН = Не СписокОрганизаций.НайтиПоЗначению(ТекущаяОрганизация) = Неопределено;
	
	Если СписокОрганизаций.Количество() = 1 И Не ТекущаяОрганизацияПрименяетУСН Тогда
		ТекущаяОрганизация = СписокОрганизаций.Получить(0).Значение;
	ИначеЕсли СписокОрганизаций.Количество() > 1 И Не ТекущаяОрганизацияПрименяетУСН ИЛИ СписокОрганизаций.Количество() = 0 Тогда
		ТекущаяОрганизация = ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка");
	КонецЕсли;		
	
КонецПроцедуры

// Считает количество документов и помещает во временное хранилище, в которых:
//	указана патентная система налогообложения;
//	применяющие патент, но без указания конкретного патента;
//	применяющие патент, с указанием конкретного патента;
//	перечисление всех указанных патентов с количеством выбранных документов.
//	
// Параметры:
//	ПараметрыМетода - Массив из Структура - анализируется только первый переданных параметр, структура содержит:
//			Организация - СправочникСсылка.Организации - организация, по которой будут анализироваться документы;
//			НачалоПериода - Дата - период с которого будут анализироваться документы, если не указан, анализ с первого введенного документа;
//			КонецПериода - Дата - период по который будут анализироваться документы, если не указан, анализ до текущей даты;
//			Подразделение - СправочникСсылка.СтруктураПредприятия - если указан, анализируются документы с этим подразделением;
//			Ответственный - СправочникСсылка.Пользователи - если указан, анализируются документы с этим ответственным.
//	АдресХранилища - Строка - хранит адрес для помещения результата во временное хранилище.
//
Процедура КоличествоДокументовПоПатентам(ПараметрыМетода, АдресХранилища) Экспорт
	
	Параметры = ПараметрыМетода.Получить(0);
	
	Результат = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ КнигаУчетаДоходов.ДокументВозникновенияДоходовРасходов) КАК КоличествоДокументов,
	|	КнигаУчетаДоходов.Патент КАК Патент
	|ИЗ
	|	РегистрНакопления.КнигаУчетаДоходовИРасходов КАК КнигаУчетаДоходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеестрДокументов
	|		ПО КнигаУчетаДоходов.ДокументВозникновенияДоходовРасходов = РеестрДокументов.Ссылка
	|ГДЕ
	|	КнигаУчетаДоходов.ДоходПатент <> 0
	|	И (КнигаУчетаДоходов.Период >= &НачалоПериода
	|	ИЛИ &НачалоПериода = ДАТАВРЕМЯ(1, 1, 1))
	|	И (КнигаУчетаДоходов.Период <= &КонецПериода
	|	ИЛИ &КонецПериода = ДАТАВРЕМЯ(1, 1, 1))
	|	И КнигаУчетаДоходов.Организация = &Организация
	|	И КнигаУчетаДоходов.Активность
	|	И (РеестрДокументов.Подразделение >= &Подразделение
	|	ИЛИ &Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
	|	И (РеестрДокументов.Ответственный >= &Ответственный
	|	ИЛИ &Ответственный = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка))
	|СГРУППИРОВАТЬ ПО
	|	КнигаУчетаДоходов.Патент
	|ИТОГИ
	|	СУММА(КоличествоДокументов) КАК КоличествоДокументов
	|ПО
	|	ОБЩИЕ";
	
	Запрос.УстановитьПараметр("Организация", Параметры.Организация);
	Запрос.УстановитьПараметр("НачалоПериода", Параметры.НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", Параметры.КонецПериода);
	Запрос.УстановитьПараметр("Подразделение", Параметры.Подразделение);
	Запрос.УстановитьПараметр("Ответственный", Параметры.Ответственный);
	
	ВыборкаОбщая = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаОбщая.Следующий() Цикл
		Результат.Вставить("ВсеДокументы", ВыборкаОбщая.КоличествоДокументов);
		Выборка = ВыборкаОбщая.Выбрать();
		КоличествоЗаполненных = 0;
		Пока Выборка.Следующий() Цикл
			ПатентНеЗаполнен = Выборка.Патент.Пустая();
			КоличествоЗаполненных = КоличествоЗаполненных + ?(ПатентНеЗаполнен, 0, Выборка.КоличествоДокументов);
			Ключ = ?(ПатентНеЗаполнен, "БезПатента", Выборка.Патент);
			Результат.Вставить(Ключ, Выборка.КоличествоДокументов);
		КонецЦикла;
		Результат.Вставить("СПатентом", КоличествоЗаполненных);
	КонецЦикла;
	
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли