#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ВладелецМОД = Параметры.ВладелецМОД;
	КодФИАСв1С = Параметры.КодФИАСв1С;
	АдресВ1С = Параметры.АдресВ1С;
	
	Для Каждого ЭлементСписка Из Параметры.МестаОсуществленияДеятельности Цикл
		Строка = ТаблицаДанных.Добавить();
		Строка.КодФИАСвЧЗ = ЭлементСписка.Значение.КодФИАС;
		Строка.АдресВЧЗ = ЭлементСписка.Значение.Адрес;
	КонецЦикла;
	
	ТаблицаДанных.Сортировать("АдресВЧЗ");
	
	ОбщегоНазначенияСобытияФормИСПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаДанных

&НаКлиенте
Процедура ТаблицаДанныхВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПриВыброреАдреса();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьАдрес(Команда)
	
	ПриВыброреАдреса();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПриВыброреАдреса()
	
	ТекущиеДанные = Элементы.ТаблицаДанных.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьВопрос(
		Новый ОписаниеОповещения("ВопросПриВыбореАдреса", ЭтотОбъект),
		СтрШаблон(НСтр("ru = 'Подменить код ФИАС для адреса в 1С
			|%1
			|кодом ФИАС для адреса в системе ""Честный знак""
			|%2 ?';
			|en = 'Подменить код ФИАС для адреса в 1С
			|%1
			|кодом ФИАС для адреса в системе ""Честный знак""
			|%2 ?'"),
			АдресВ1С,
			ТекущиеДанные.АдресВЧЗ),
		РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПриВыбореАдреса(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ТекущиеДанные = Элементы.ТаблицаДанных.ТекущиеДанные;
		ДанныеЗаписи = Новый Структура;
		ДанныеЗаписи.Вставить("КодФИАСв1С", КодФИАСв1С);
		ДанныеЗаписи.Вставить("КодФИАСвЧЗ", ТекущиеДанные.КодФИАСвЧЗ);
		ДанныеЗаписи.Вставить("АдресВ1С",   АдресВ1С);
		ДанныеЗаписи.Вставить("АдресВЧЗ",   ТекущиеДанные.АдресВЧЗ);
		ЗаписатьУточнениеВРегистр(ДанныеЗаписи);
		Закрыть(Истина);
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаписатьУточнениеВРегистр(Данные)
	
	Запись = РегистрыСведений.УточнениеКодовФИАСПоМОДамИСМП.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(Запись, Данные);
	Запись.ХешАдресаВ1С = ИнтеграцияИС.ХешированиеДанныхSHA256(Запись.АдресВ1С);;
	Запись.Записать();
	
КонецПроцедуры

#КонецОбласти
