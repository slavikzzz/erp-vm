#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВыбранныеСтатьиАктивовПассивов.Загрузить(ПолучитьИзВременногоХранилища(Параметры.АдресВоВременномХранилище));
	ЗаполнитьДеревоСтатьиАктивовПассивов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоВидовФинансовыхОтчетов

&НаКлиенте
Процедура ДеревоВидовФинансовыхОтчетовПометкаПриИзменении(Элемент)
	
	ТекущиеДанные = ТекущийЭлемент.ТекущиеДанные;
	ПометитьВложенныеЭлементыРекурсивно(ТекущиеДанные);
	
	Если ТекущиеДанные.Пометка Тогда
		ОтметитьРодителейВДереве(ТекущиеДанные);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	Адрес = "";
	ГотовоСервер(Адрес);
	Закрыть(Адрес);
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВсе(Команда)
	ОтметитьЭлементыПоСтрокамДерева(ДеревоСтатьиАктивовПассивов.ПолучитьЭлементы(), Истина);
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсе(Команда)
	ОтметитьЭлементыПоСтрокамДерева(ДеревоСтатьиАктивовПассивов.ПолучитьЭлементы(), Ложь);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ГотовоСервер(Адрес)
	ОбновитьКоллекциюВыбранныеВидыФинансовыхОтчетов();
	Адрес = ПоместитьВоВременноеХранилище(ВыбранныеСтатьиАктивовПассивов.Выгрузить(), УникальныйИдентификатор)
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоСтатьиАктивовПассивов()
	
	СхемаКомпоновкиДанных = Обработки.ПомощникВыгрузкиЗагрузкиМоделиМеждународногоУчета.ПолучитьМакет("СтатьиАктивовПассивовИерархия");
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	Макет = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, СхемаКомпоновкиДанных.НастройкиПоУмолчанию,,,
		Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(Макет);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	Дерево = Новый ДеревоЗначений;
	ПроцессорВывода.УстановитьОбъект(Дерево);
	
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	Если Дерево.Строки.Количество() Тогда
		ОтметитьЭлементыПоВыбраннымЭлементам(Дерево.Строки);
		ЗначениеВРеквизитФормы(Дерево, "ДеревоСтатьиАктивовПассивов");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтметитьЭлементыПоВыбраннымЭлементам(Строки)
	
	Для Каждого Строка Из Строки Цикл
		
		Отбор = Новый Структура;
		Отбор.Вставить("СтатьяАктивовПассивов", Строка.СтатьяАктивовПассивов);
		НайденныеСтроки = ВыбранныеСтатьиАктивовПассивов.НайтиСтроки(Отбор);
		Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
			Строка.Пометка = НайденнаяСтрока.Пометка;
		КонецЦикла;
		
		ОтметитьЭлементыПоВыбраннымЭлементам(Строка.Строки);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ВыбранныеЭлементыДереваВСписок(Элементы)

	Для Каждого Элемент Из Элементы Цикл
		НоваяСтрока = ВыбранныеСтатьиАктивовПассивов.Добавить();
		НоваяСтрока.СтатьяАктивовПассивов = Элемент.СтатьяАктивовПассивов;
		НоваяСтрока.Пометка = Элемент.Пометка;
		
		ВыбранныеЭлементыДереваВСписок(Элемент.ПолучитьЭлементы());
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьКоллекциюВыбранныеВидыФинансовыхОтчетов()
	
	ВыбранныеСтатьиАктивовПассивов.Очистить();
	ВыбранныеЭлементыДереваВСписок(ДеревоСтатьиАктивовПассивов.ПолучитьЭлементы());
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьЭлементыПоСтрокамДерева(Строки, Пометка)
	
	Для Каждого Строка Из Строки Цикл
		Строка.Пометка = Пометка;
		ОтметитьЭлементыПоСтрокамДерева(Строка.ПолучитьЭлементы(), Пометка);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПометитьВложенныеЭлементыРекурсивно(ЭлементДерева)
	
	ВложенныеЭлементы = ЭлементДерева.ПолучитьЭлементы();
	
	Для Каждого ВложенныйЭлемент Из ВложенныеЭлементы Цикл
		ВложенныйЭлемент.Пометка = ЭлементДерева.Пометка;
		ПометитьВложенныеЭлементыРекурсивно(ВложенныйЭлемент);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьРодителейВДереве(ЭлементДерева)
	
	СтрокаРодитель = ЭлементДерева.ПолучитьРодителя();
	
	Если СтрокаРодитель <> Неопределено Тогда
		СтрокаРодитель.Пометка = ЭлементДерева.Пометка;
		ОтметитьРодителейВДереве(СтрокаРодитель);
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти
