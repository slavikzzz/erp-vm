#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

Перем МенеджерРасчетаЗарплаты;
Перем РассчитываемыйНабор;
Перем ДанныеВедущихЗаписейКаскадногоНормирования;
Перем ДанныеБазовыхНачислений;
Перем КэшЗависимостиВидовРасчета;
Перем КэшСвойстваВидовРасчетаРассчитываемогоНабора;
Перем КэшЗависимостиВидовРасчетаРассчитываемогоНабора;
Перем КэшТребованияНормируемыхПоВремениНачислений;
Перем АлгоритмРасчетаБазы;
Перем КоэффициентыНормированияПоВремениРассчитаны;
Перем ВыполненныеОчередностиКаскадногоРасчетаНачислений;
Перем ЗависимостиКаскадногоРасчета;
Перем РасчетПоДаннымТекущегоНабора;

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ИнициализироватьРасчетБазыНачислений(ОписаниеНабораРассчитываемыхЗаписей, УстанавливаемыйМенеджерРасчетаЗарплаты, НастройкиРасчета, ДанныеТекущегоНабораНачислений = Неопределено) Экспорт
	МенеджерРасчетаЗарплаты = УстанавливаемыйМенеджерРасчетаЗарплаты;	
	РасчетПоДаннымТекущегоНабора = НастройкиРасчета.РасчетПоДаннымТекущегоНабора;
	
	ИнициализироватьРасчетБазы(
		ОписаниеНабораРассчитываемыхЗаписей, 
		АлгоритмыПолученияБазы().БазаНачислений, 
		НастройкиРасчета.ПолучатьПодробнуюРасшифровку, 
		НастройкиРасчета.ИсключаемыйРегистратор,
		ДанныеТекущегоНабораНачислений);
КонецПроцедуры

Процедура ИнициализироватьРасчетБазыРезервов(ОписаниеНабораРассчитываемыхЗаписей, УстанавливаемыйМенеджерРасчетаЗарплаты, НастройкиРасчета, ДанныеТекущегоНабораНачислений = Неопределено) Экспорт
	МенеджерРасчетаЗарплаты = УстанавливаемыйМенеджерРасчетаЗарплаты;	
	РасчетПоДаннымТекущегоНабора = НастройкиРасчета.РасчетПоДаннымТекущегоНабора;
	
	ИнициализироватьРасчетБазы(
		ОписаниеНабораРассчитываемыхЗаписей, 
		АлгоритмыПолученияБазы().БазаРезервов, 
		НастройкиРасчета.ПолучатьПодробнуюРасшифровку, 
		НастройкиРасчета.ИсключаемыйРегистратор,
		ДанныеТекущегоНабораНачислений);
КонецПроцедуры	

Процедура ИнициализироватьРасчетБазыУдержаний(ОписаниеНабораРассчитываемыхЗаписей, УстанавливаемыйМенеджерРасчетаЗарплаты, НастройкиРасчета, ДанныеТекущегоНабораНачислений = Неопределено) Экспорт
	МенеджерРасчетаЗарплаты = УстанавливаемыйМенеджерРасчетаЗарплаты;	
	РасчетПоДаннымТекущегоНабора = НастройкиРасчета.РасчетПоДаннымТекущегоНабора;
	
	ИнициализироватьРасчетБазы(
		ОписаниеНабораРассчитываемыхЗаписей, 
		АлгоритмыПолученияБазы().БазаУдержаний, 
		Истина, 
		НастройкиРасчета.ИсключаемыйРегистратор, 
		ДанныеТекущегоНабораНачислений);
КонецПроцедуры	

Функция РасчетнаяБазаНачислений(ОчередностьРасчета = Неопределено, РезультатыРасчетаБазовыхНачислений = Неопределено) Экспорт	
	
	Алгоритмы = АлгоритмыПолученияБазы();
	
	РасчетнаяБаза = НовыйРасчетнаяБаза();
	
	Если РассчитываемыйНабор.Количество() = 0 Тогда
		Возврат РасчетнаяБаза;
	КонецЕсли;	
	
	Если Не КоэффициентыНормированияПоВремениРассчитаны Тогда
		РассчитатьКоэффициентыНормированияПоВремени(ДанныеБазовыхНачислений.ФактическиеПериоды);                         
		Если ДанныеВедущихЗаписейКаскадногоНормирования <> Неопределено Тогда
			РассчитатьКоэффициентыНормированияПоВремени(ДанныеВедущихЗаписейКаскадногоНормирования.ФактическиеПериоды);	  
		КонецЕсли;
		КоэффициентыНормированияПоВремениРассчитаны = Истина;
	КонецЕсли;	 
	
	Если РезультатыРасчетаБазовыхНачислений <> Неопределено Тогда 		
		УточнитьРезультатыБазовыхЗаписей(
			ДанныеБазовыхНачислений.Записи, РезультатыРасчетаБазовыхНачислений, АлгоритмыПолученияБазы().БазаНачислений);
		Если ДанныеВедущихЗаписейКаскадногоНормирования <> Неопределено Тогда
			УточнитьРезультатыБазовыхЗаписей(
				ДанныеВедущихЗаписейКаскадногоНормирования.ФактическиеПериоды,
				РезультатыРасчетаБазовыхНачислений,
				АлгоритмыПолученияБазы().БазаНачислений);
		КонецЕсли;	
	КонецЕсли;	
	
	Если ОчередностьРасчета = Неопределено Тогда
		ОчередностьКаскадногоНормирования = МаксимальнаяОчередностьНачислений();
	Иначе
		ОчередностьКаскадногоНормирования = ОчередностьРасчета - 1;
	КонецЕсли;	
	
	Если ДанныеВедущихЗаписейКаскадногоНормирования <> Неопределено Тогда 
		Для ТекущаяОчередность = 1 По ОчередностьКаскадногоНормирования Цикл
			Если ВыполненныеОчередностиКаскадногоРасчетаНачислений[ТекущаяОчередность] <>  Истина Тогда
				РассчитатьКоэффициентыЗаписейНормируемыхКаскадно(ДанныеВедущихЗаписейКаскадногоНормирования, ДанныеВедущихЗаписейКаскадногоНормирования, ТекущаяОчередность);
				РассчитатьКоэффициентыЗаписейНормируемыхКаскадно(ДанныеБазовыхНачислений, ДанныеВедущихЗаписейКаскадногоНормирования, ТекущаяОчередность);
				ВыполненныеОчередностиКаскадногоРасчетаНачислений.Вставить(ТекущаяОчередность, Истина);
			КонецЕсли;	
		КонецЦикла;	
	КонецЕсли;	
		
	СвойстваВидовРасчета = СвойстваВидовРасчетаРассчитываемогоНабора();
	ЗависимостиВР = ЗависимостиВидовРасчетаРассчитываемогоНабора();

	ОбщегоНазначенияБЗК.ДобавитьИндексКоллекции(ДанныеБазовыхНачислений.ФактическиеПериоды, "Регистратор, НомерСтроки");
	СтруктураПоискаПериодов = Новый Структура("Регистратор, НомерСтроки");

	ПоляПоиска = "Сотрудник, БазовыйПериодОсновнойЗаписиНачало, БазовыйПериодОсновнойЗаписиКонец, ОсновнаяЗаписьРассчитыватьПоРазовымНачислениямДокумента";
	ОбщегоНазначенияБЗК.ДобавитьИндексКоллекции(ДанныеБазовыхНачислений.Записи, ПоляПоиска);
	СтруктураПоиска = Новый Структура(ПоляПоиска);
	
	ПоляПоискаПоНачислениямДокумента = "Сотрудник, БазовыйПериодОсновнойЗаписиНачало, БазовыйПериодОсновнойЗаписиКонец, ОсновнаяЗаписьРассчитыватьПоРазовымНачислениямДокумента, ОсновнаяЗаписьРегистраторРазовогоНачисления";
	ОбщегоНазначенияБЗК.ДобавитьИндексКоллекции(ДанныеБазовыхНачислений.Записи, ПоляПоискаПоНачислениямДокумента);
	СтруктураПоискаПоНачислениямДокумента = Новый Структура(ПоляПоискаПоНачислениямДокумента);	
	
	Для Каждого РассчитываемаяЗапись Из РассчитываемыйНабор Цикл
		ЗависимостиРассчитываемогоВР = ЗависимостиВР[РассчитываемаяЗапись.ВидРасчета];
		Если ОчередностьРасчета <> Неопределено
			И СвойстваВидовРасчета[РассчитываемаяЗапись.ВидРасчета].ОчередностьРасчета <> ОчередностьРасчета
			Или ЗависимостиРассчитываемогоВР = Неопределено Тогда
				
			Продолжить;
		КонецЕсли;		
		
		Если РассчитываемаяЗапись.РассчитыватьПоРазовымНачислениямДокумента Тогда
			Отбор = СтруктураПоискаПоНачислениямДокумента;
			Отбор.ОсновнаяЗаписьРассчитыватьПоРазовымНачислениямДокумента = Истина;
			Отбор.ОсновнаяЗаписьРегистраторРазовогоНачисления = РассчитываемаяЗапись.РегистраторРазовогоНачисления; 
		Иначе
			Отбор = СтруктураПоиска;
			Отбор.ОсновнаяЗаписьРассчитыватьПоРазовымНачислениямДокумента = Ложь;
		КонецЕсли;	
		
		ЗаполнитьЗначенияСвойств(Отбор, РассчитываемаяЗапись);
		Отбор.БазовыйПериодОсновнойЗаписиНачало = РассчитываемаяЗапись.БазовыйПериодНачало;
		Отбор.БазовыйПериодОсновнойЗаписиКонец = РассчитываемаяЗапись.БазовыйПериодКонец;
		БазовыеЗаписи = ДанныеБазовыхНачислений.Записи.НайтиСтроки(Отбор);
		Для Каждого БазоваяЗапись Из БазовыеЗаписи Цикл
			Если ЗависимостиРассчитываемогоВР[БазоваяЗапись.ВидРасчета] <> Неопределено
				Или АлгоритмРасчетаБазы = Алгоритмы.БазаРезервов Тогда
				СтрокаРезультатаРасчета = РасчетнаяБаза.Добавить();
				СтрокаРезультатаРасчета.Регистратор = РассчитываемаяЗапись.Регистратор;
				СтрокаРезультатаРасчета.НомерСтроки = РассчитываемаяЗапись.НомерСтроки;
				СтрокаРезультатаРасчета.РегистраторРазрез = БазоваяЗапись.Регистратор;
				СтрокаРезультатаРасчета.НомерСтрокиРазрез = БазоваяЗапись.НомерСтроки;
				СтрокаРезультатаРасчета.ВидРасчетаРазрез = БазоваяЗапись.ВидРасчета;
				СтрокаРезультатаРасчета.СотрудникРазрез = БазоваяЗапись.Сотрудник;
				СтрокаРезультатаРасчета.ГоловнаяОрганизацияРазрез = БазоваяЗапись.ГоловнаяОрганизация;
				СтрокаРезультатаРасчета.ИдентификаторСтрокиРазрез = БазоваяЗапись.ИдентификаторСтроки;
				СтрокаРезультатаРасчета.ПериодРегистрацииРазрез = БазоваяЗапись.ПериодРегистрации;
				СтрокаРезультатаРасчета.ПериодДействияРазрез = БазоваяЗапись.ПериодДействия;
				СтрокаРезультатаРасчета.ПериодДействияНачалоРазрез = БазоваяЗапись.ПериодДействияНачало;
				СтрокаРезультатаРасчета.ПериодДействияКонецРазрез = БазоваяЗапись.ПериодДействияКонец;
				
				Если БазоваяЗапись.НеобходимоНормировать Тогда
					ЗаполнитьЗначенияСвойств(СтруктураПоискаПериодов, БазоваяЗапись);
					Периоды = ДанныеБазовыхНачислений.ФактическиеПериоды.НайтиСтроки(СтруктураПоискаПериодов);				
					Для Каждого ТекущийПериод Из Периоды Цикл
						Если ОбщегоНазначенияБЗК.ДатаВИнтервале(ТекущийПериод.ПериодДействияНачало, РассчитываемаяЗапись.БазовыйПериодНачало, РассчитываемаяЗапись.БазовыйПериодКонец) Тогда
							СтрокаРезультатаРасчета.РезультатБаза = СтрокаРезультатаРасчета.РезультатБаза + БазоваяЗапись.Результат * ТекущийПериод.Коэффициент;
						КонецЕсли;	
					КонецЦикла;
				Иначе
					СтрокаРезультатаРасчета.РезультатБаза = БазоваяЗапись.Результат; 	
				КонецЕсли;	
			КонецЕсли;
		КонецЦикла;	
	КонецЦикла;	
	
	Возврат РасчетнаяБаза;
КонецФункции	

Функция РасчетнаяБазаУдержаний(ОчередностьРасчета = Неопределено, РезультатыРасчетаБазовыхНачислений = Неопределено) Экспорт	
	РасчетнаяБаза = НовыйРасчетнаяБаза();
	РасчетнаяБаза.Колонки.Добавить("РезультатБазаВЦеломЗаМесяц", Новый ОписаниеТипов("Число"));
	
	Если РассчитываемыйНабор.Количество() = 0 Тогда
		Возврат РасчетнаяБаза;
	КонецЕсли;	
	
	Если Не КоэффициентыНормированияПоВремениРассчитаны Тогда
		РассчитатьКоэффициентыНормированияПоВремени(ДанныеБазовыхНачислений.ФактическиеПериоды);                          
		Если ДанныеВедущихЗаписейКаскадногоНормирования <> Неопределено Тогда
			РассчитатьКоэффициентыНормированияПоВремени(ДанныеВедущихЗаписейКаскадногоНормирования.ФактическиеПериоды);	  
		КонецЕсли;
		КоэффициентыНормированияПоВремениРассчитаны = Истина;
	КонецЕсли;	 
	
	Если РезультатыРасчетаБазовыхНачислений <> Неопределено Тогда 		
		УточнитьРезультатыБазовыхЗаписей(
			ДанныеБазовыхНачислений.Записи, РезультатыРасчетаБазовыхНачислений, АлгоритмыПолученияБазы().БазаУдержаний);
		Если ДанныеВедущихЗаписейКаскадногоНормирования <> Неопределено Тогда
			УточнитьРезультатыБазовыхЗаписей(
				ДанныеВедущихЗаписейКаскадногоНормирования.ФактическиеПериоды,
				РезультатыРасчетаБазовыхНачислений,
				АлгоритмыПолученияБазы().БазаУдержаний);
		КонецЕсли;	
	КонецЕсли;	
	
	ОчередностьКаскадногоНормирования = МаксимальнаяОчередностьНачислений();	
	
	Если ДанныеВедущихЗаписейКаскадногоНормирования <> Неопределено Тогда 
		Для ТекущаяОчередность = 0 По ОчередностьКаскадногоНормирования Цикл
			Если ВыполненныеОчередностиКаскадногоРасчетаНачислений[ТекущаяОчередность] <>  Истина Тогда
				РассчитатьКоэффициентыЗаписейНормируемыхКаскадно(ДанныеВедущихЗаписейКаскадногоНормирования, ДанныеВедущихЗаписейКаскадногоНормирования, ТекущаяОчередность);
				РассчитатьКоэффициентыЗаписейНормируемыхКаскадно(ДанныеБазовыхНачислений, ДанныеВедущихЗаписейКаскадногоНормирования, ТекущаяОчередность);
				ВыполненныеОчередностиКаскадногоРасчетаНачислений.Вставить(ТекущаяОчередность, Истина);
			КонецЕсли;	
		КонецЦикла;	
	КонецЕсли;	
	
	СвойстваВидовРасчета = СвойстваВидовРасчетаРассчитываемогоНабора();
	ЗависимостиВР = ЗависимостиВидовРасчетаРассчитываемогоНабора();
	
	ОбщегоНазначенияБЗК.ДобавитьИндексКоллекции(ДанныеБазовыхНачислений.ФактическиеПериоды, "Регистратор, НомерСтроки");
	СтруктураПоискаПериодов = Новый Структура("Регистратор, НомерСтроки");

	ПоляПоиска = "ФизическоеЛицо, ГоловнаяОрганизация, БазовыйПериодОсновнойЗаписиНачало, БазовыйПериодОсновнойЗаписиКонец";
	ОбщегоНазначенияБЗК.ДобавитьИндексКоллекции(ДанныеБазовыхНачислений.Записи, ПоляПоиска);
	СтруктураПоиска = Новый Структура(ПоляПоиска);
	
	ПоляПоискаПоСотруднику = "ФизическоеЛицо, Сотрудник, ГоловнаяОрганизация, БазовыйПериодОсновнойЗаписиНачало, БазовыйПериодОсновнойЗаписиКонец";
	ОбщегоНазначенияБЗК.ДобавитьИндексКоллекции(ДанныеБазовыхНачислений.Записи, ПоляПоискаПоСотруднику);
	СтруктураПоискаПоСотруднику = Новый Структура(ПоляПоискаПоСотруднику);
	
	Для Каждого РассчитываемаяЗапись Из РассчитываемыйНабор Цикл
		ЗависимостиРассчитываемогоВР = ЗависимостиВР[РассчитываемаяЗапись.ВидРасчета];
		Если ОчередностьРасчета <> Неопределено
			И СвойстваВидовРасчета[РассчитываемаяЗапись.ВидРасчета].ОчередностьРасчета <> ОчередностьРасчета
			Или ЗависимостиРассчитываемогоВР = Неопределено Тогда
				
			Продолжить;
		КонецЕсли;		
		
		Если ЗначениеЗаполнено(РассчитываемаяЗапись.Сотрудник) Тогда
			ТекущийОтбор = СтруктураПоискаПоСотруднику;
		Иначе
			ТекущийОтбор = СтруктураПоиска;
		КонецЕсли;	
		
		ЗаполнитьЗначенияСвойств(ТекущийОтбор, РассчитываемаяЗапись);
		ТекущийОтбор.БазовыйПериодОсновнойЗаписиНачало = РассчитываемаяЗапись.БазовыйПериодНачало;
		ТекущийОтбор.БазовыйПериодОсновнойЗаписиКонец = РассчитываемаяЗапись.БазовыйПериодКонец;
		ТекущийОтбор.ГоловнаяОрганизация  = РассчитываемаяЗапись.Организация; 
		
		БазовыеЗаписи = ДанныеБазовыхНачислений.Записи.НайтиСтроки(ТекущийОтбор);
		Для Каждого БазоваяЗапись Из БазовыеЗаписи Цикл
			Если ЗависимостиРассчитываемогоВР[БазоваяЗапись.ВидРасчета] <> Неопределено 
				И ЗаписьСоответствуетОтборуРазовыхНачисленийРегистратору(РассчитываемаяЗапись, БазоваяЗапись) Тогда
				
				СтрокаРезультатаРасчета = РасчетнаяБаза.Добавить();
				СтрокаРезультатаРасчета.Регистратор = РассчитываемаяЗапись.Регистратор;
				СтрокаРезультатаРасчета.НомерСтроки = РассчитываемаяЗапись.НомерСтроки;
				СтрокаРезультатаРасчета.ВидРасчетаРазрез = БазоваяЗапись.ВидРасчета;
				
				СтрокаРезультатаРасчета.РезультатБазаВЦеломЗаМесяц = БазоваяЗапись.Результат;
				Если БазоваяЗапись.НеобходимоНормировать Тогда
					ЗаполнитьЗначенияСвойств(СтруктураПоискаПериодов, БазоваяЗапись);
					Периоды = ДанныеБазовыхНачислений.ФактическиеПериоды.НайтиСтроки(СтруктураПоискаПериодов);				
					Для Каждого ТекущийПериод Из Периоды Цикл
						Если ОбщегоНазначенияБЗК.ДатаВИнтервале(ТекущийПериод.ПериодДействияНачало, РассчитываемаяЗапись.БазовыйПериодНачало, РассчитываемаяЗапись.БазовыйПериодКонец) Тогда
							СтрокаРезультатаРасчета.РезультатБаза = СтрокаРезультатаРасчета.РезультатБаза + БазоваяЗапись.Результат * ТекущийПериод.Коэффициент;
						КонецЕсли;	
					КонецЦикла;
				ИначеЕсли РассчитываемаяЗапись.МесяцУдержания = БазоваяЗапись.ПериодДействия Тогда
					Если ОбщегоНазначенияБЗК.ДатаВИнтервале(БазоваяЗапись.ПериодДействияНачало, РассчитываемаяЗапись.БазовыйПериодНачало, РассчитываемаяЗапись.БазовыйПериодКонец)
						Или ОбщегоНазначенияБЗК.ДатаВИнтервале(РассчитываемаяЗапись.БазовыйПериодНачало, БазоваяЗапись.ПериодДействияНачало, БазоваяЗапись.ПериодДействияКонец) Тогда
						
						СтрокаРезультатаРасчета.РезультатБаза = БазоваяЗапись.Результат;
					КонецЕсли;		
				ИначеЕсли РассчитываемаяЗапись.МесяцУдержания < БазоваяЗапись.ПериодДействия Тогда
					Если КонецДня(РассчитываемаяЗапись.БазовыйПериодКонец) = КонецМесяца(РассчитываемаяЗапись.БазовыйПериодКонец) Тогда
						СтрокаРезультатаРасчета.РезультатБаза = БазоваяЗапись.Результат;
					КонецЕсли;
				ИначеЕсли РассчитываемаяЗапись.МесяцУдержания > БазоваяЗапись.ПериодДействия	Тогда
					Если РассчитываемаяЗапись.БазовыйПериодНачало = НачалоМесяца(РассчитываемаяЗапись.БазовыйПериодНачало) Тогда
						СтрокаРезультатаРасчета.РезультатБаза = БазоваяЗапись.Результат;
					КонецЕсли;	
				КонецЕсли;	
			КонецЕсли;
		КонецЦикла;	
	КонецЦикла;	
	
	РасчетнаяБаза.Свернуть("Регистратор, НомерСтроки, ВидРасчетаРазрез", "РезультатБаза, РезультатБазаВЦеломЗаМесяц");
	
	Возврат РасчетнаяБаза;
КонецФункции	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура РазбитьПериодыНачислений(ПериодыНачислений, ДатыРазбиения) Экспорт
	ПериодыНачислений.Сортировать("Регистратор, НомерСтроки, ПериодДействияНачало", Новый СравнениеЗначений());
	ОбщегоНазначенияБЗК.ДобавитьИндексКоллекции(ДатыРазбиения, "Регистратор, НомерСтроки");
	СтруктураПоиска = Новый Структура("Регистратор, НомерСтроки");
	
	ПредыдущийПериод = Неопределено;
	ИндексТекущейСтроки = 0;
	Пока ИндексТекущейСтроки < ПериодыНачислений.Количество() Цикл
		ТекущийПериод = ПериодыНачислений[ИндексТекущейСтроки];
		Если ПредыдущийПериод = Неопределено
			Или ТекущийПериод.Регистратор <> ПредыдущийПериод.Регистратор
			Или ТекущийПериод.НомерСтроки <> ПредыдущийПериод.НомерСтроки Тогда
				
			ЗаполнитьЗначенияСвойств(СтруктураПоиска, ТекущийПериод);
			ДатаРазбиенияПоКлючу = ДатыРазбиения.Скопировать(СтруктураПоиска, "Дата");
			ДатаРазбиенияПоКлючу.Сортировать("Дата");
			
			ИндексТекущейСтрокиТаблицыДат = 0;
		КонецЕсли;		
		
		Пока ИндексТекущейСтрокиТаблицыДат <= ДатаРазбиенияПоКлючу.Количество() - 1 Цикл
			ТекущаяСтрокаТаблицыДат = ДатаРазбиенияПоКлючу[ИндексТекущейСтрокиТаблицыДат];
			Если НачалоДня(ТекущийПериод.ПериодДействияНачало) > НачалоДня(ТекущаяСтрокаТаблицыДат.Дата) Тогда
				ИндексТекущейСтрокиТаблицыДат = ИндексТекущейСтрокиТаблицыДат + 1;
				Продолжить;
			ИначеЕсли НачалоДня(ТекущийПериод.ПериодДействияКонец) < НачалоДня(ТекущаяСтрокаТаблицыДат.Дата) Тогда
				Прервать;
			ИначеЕсли НачалоДня(ТекущийПериод.ПериодДействияКонец) = НачалоДня(ТекущийПериод.ПериодДействияНачало) Тогда 	
				ИндексТекущейСтрокиТаблицыДат = ИндексТекущейСтрокиТаблицыДат + 1;
				Продолжить;			
			ИначеЕсли НачалоДня(ТекущаяСтрокаТаблицыДат.Дата) = НачалоДня(ТекущийПериод.ПериодДействияНачало) Тогда
				ИсходнаяДатаОкончания = ТекущийПериод.ПериодДействияКонец; 
				ТекущийПериод.ПериодДействияКонец = КонецДня(ТекущаяСтрокаТаблицыДат.Дата);	
				СтрокаНовогоПериода = ОбщегоНазначенияБЗК.ВставитьСтрокуВТаблицу(
										ПериодыНачислений, 
										ИндексТекущейСтроки + 1,
										ТекущийПериод);
					
				СтрокаНовогоПериода.ПериодДействияНачало = КонецДня(ТекущаяСтрокаТаблицыДат.Дата) + 1;
				СтрокаНовогоПериода.ПериодДействияКонец = ИсходнаяДатаОкончания;	
			Иначе	
				ИсходнаяДатаОкончания = ТекущийПериод.ПериодДействияКонец;
				ТекущийПериод.ПериодДействияКонец = НачалоДня(ТекущаяСтрокаТаблицыДат.Дата) - 1;
					
				СтрокаНовогоПериода = ОбщегоНазначенияБЗК.ВставитьСтрокуВТаблицу(
										ПериодыНачислений, 
										ИндексТекущейСтроки + 1,
										ТекущийПериод);
					
				СтрокаНовогоПериода.ПериодДействияНачало = НачалоДня(ТекущаяСтрокаТаблицыДат.Дата);
				СтрокаНовогоПериода.ПериодДействияКонец = ИсходнаяДатаОкончания;		
			КонецЕсли;	
			ИндексТекущейСтрокиТаблицыДат = ИндексТекущейСтрокиТаблицыДат + 1;		
		КонецЦикла;	
		ПредыдущийПериод = ТекущийПериод;
		ИндексТекущейСтроки  = ИндексТекущейСтроки + 1;
	КонецЦикла;	
КонецПроцедуры		

Функция НовыйРасчетнаяБаза()
	КвалификаторРезультата = Новый КвалификаторыЧисла(31, 8);
	
	РасчетнаяБазаНачислений = Новый ТаблицаЗначений();
	РасчетнаяБазаНачислений.Колонки.Добавить("Регистратор", РегистрыРасчета.Начисления.ОписаниеТиповПоляРегистратор());
	РасчетнаяБазаНачислений.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число"));
	РасчетнаяБазаНачислений.Колонки.Добавить("РегистраторРазрез", РегистрыРасчета.Начисления.ОписаниеТиповПоляРегистратор());
	РасчетнаяБазаНачислений.Колонки.Добавить("НомерСтрокиРазрез", Новый ОписаниеТипов("Число"));
	РасчетнаяБазаНачислений.Колонки.Добавить("ВидРасчетаРазрез", Новый ОписаниеТипов("ПланВидовРасчетаСсылка.Начисления"));
	РасчетнаяБазаНачислений.Колонки.Добавить("СотрудникРазрез", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	РасчетнаяБазаНачислений.Колонки.Добавить("ГоловнаяОрганизацияРазрез", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	РасчетнаяБазаНачислений.Колонки.Добавить("ИдентификаторСтрокиРазрез", Новый ОписаниеТипов("Число"));
	РасчетнаяБазаНачислений.Колонки.Добавить("ПериодРегистрацииРазрез", Новый ОписаниеТипов("Дата"));
	РасчетнаяБазаНачислений.Колонки.Добавить("ПериодДействияРазрез", Новый ОписаниеТипов("Дата"));
	РасчетнаяБазаНачислений.Колонки.Добавить("ПериодДействияНачалоРазрез", Новый ОписаниеТипов("Дата"));
	РасчетнаяБазаНачислений.Колонки.Добавить("ПериодДействияКонецРазрез", Новый ОписаниеТипов("Дата"));
	РасчетнаяБазаНачислений.Колонки.Добавить("РезультатБаза", Новый ОписаниеТипов("Число", КвалификаторРезультата));
	
	Возврат РасчетнаяБазаНачислений;
КонецФункции	

Процедура РассчитатьКоэффициентыЗаписейНормируемыхКаскадно(ДанныеНормируемыхЗаписей, ДанныеВедущихЗаписей, ОчередностьРасчета = Неопределено)
	КвалификаторЧисла = Новый КвалификаторыЧисла(31, 15);
	ДанныеНормируемыхЗаписей.ФактическиеПериоды.Колонки.Добавить("БазаРаспределения", Новый ОписаниеТипов("Число",,,КвалификаторЧисла));
	
	ОтборНормируемыхЗаписей = Новый Структура("НормироватьКаскадно", Истина);
	
	Если ОчередностьРасчета <> Неопределено Тогда
		ОтборНормируемыхЗаписей.Вставить("ОчередностьРасчета", ОчередностьРасчета);
	КонецЕсли;	
		
	НормируемыеЗаписиОчередности = ДанныеНормируемыхЗаписей.ФактическиеПериоды.НайтиСтроки(ОтборНормируемыхЗаписей);
	
	ОбщегоНазначенияБЗК.ДобавитьИндексКоллекции(ДанныеВедущихЗаписей.ФактическиеПериоды, "Сотрудник, ПериодДействия, ВидРасчета");
	СтруктураПоискаВедущихЗаписей = Новый Структура("Сотрудник, ПериодДействия, ВидРасчета");
			
	Для Каждого НормируемаяЗапись Из НормируемыеЗаписиОчередности Цикл
		БазовыеВидыРасчета = ЗависимостиКаскадногоРасчета[НормируемаяЗапись.ВидРасчета];
		Если БазовыеВидыРасчета = Неопределено Тогда
			Продолжить;
		КонецЕсли;	
		
		ЗаполнитьЗначенияСвойств(СтруктураПоискаВедущихЗаписей, НормируемаяЗапись);	
		
		Для Каждого ОписаниеБазовогоВидаРасчета Из БазовыеВидыРасчета Цикл
			СтруктураПоискаВедущихЗаписей.ВидРасчета = ОписаниеБазовогоВидаРасчета.Значение.ВидРасчета;
			
			ВедущиеЗаписи = ДанныеВедущихЗаписей.ФактическиеПериоды.НайтиСтроки(СтруктураПоискаВедущихЗаписей);
			 							
			Для Каждого ТекущаяВедущаяЗапись Из ВедущиеЗаписи Цикл
				Если ОбщегоНазначенияБЗК.ДатаВИнтервале(ТекущаяВедущаяЗапись.ПериодДействияНачало, НормируемаяЗапись.ПериодДействияНачало, НормируемаяЗапись.ПериодДействияКонец) 
					И ОбщегоНазначенияБЗК.ДатаВИнтервале(ТекущаяВедущаяЗапись.ПериодДействияКонец, НормируемаяЗапись.ПериодДействияНачало, НормируемаяЗапись.ПериодДействияКонец) 
					И ЗаписьСоответствуетОтборуРазовыхНачисленийРегистратору(НормируемаяЗапись, ТекущаяВедущаяЗапись) Тогда
					
					БазаРаспределения = ТекущаяВедущаяЗапись.Результат * ТекущаяВедущаяЗапись.Коэффициент;
					НормируемаяЗапись.БазаРаспределения = НормируемаяЗапись.БазаРаспределения + БазаРаспределения;
				КонецЕсли;	
			КонецЦикла;				
		КонецЦикла;	
	КонецЦикла;	
	
	ИтогиПоНормируемымЗаписям = ДанныеНормируемыхЗаписей.ФактическиеПериоды.Скопировать(НормируемыеЗаписиОчередности, "Регистратор, НомерСтроки, БазаРаспределения");
	ИтогиПоНормируемымЗаписям.Свернуть("Регистратор, НомерСтроки", "БазаРаспределения");
	ИтогиПоНормируемымЗаписям.Индексы.Добавить("Регистратор, НомерСтроки");
	СтруктураПоискаИтогов = Новый Структура("Регистратор, НомерСтроки");
	
	Для Каждого Период Из НормируемыеЗаписиОчередности Цикл
		Если Период.НормироватьКаскадно Тогда
			ЗаполнитьЗначенияСвойств(СтруктураПоискаИтогов, Период);
			СтрокаИтогов = ИтогиПоНормируемымЗаписям.НайтиСтроки(СтруктураПоискаИтогов)[0];
			Период.Коэффициент = ?(СтрокаИтогов.БазаРаспределения = 0, 0, Период.БазаРаспределения / СтрокаИтогов.БазаРаспределения);
		КонецЕсли;	
	КонецЦикла;			
	ДанныеНормируемыхЗаписей.ФактическиеПериоды.Колонки.Удалить("БазаРаспределения");
КонецПроцедуры	

Процедура РассчитатьКоэффициентыНормированияПоВремени(ПериодыНормируемыхНачислений)	
	Если ПериодыНормируемыхНачислений.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;	
	
	ПериодыНормируемыхНачислений.Колонки.Добавить("Время", Новый ОписаниеТипов("Число"));
	ПериодыНормируемыхНачислений.Колонки.Добавить("Коэффициент", Новый ОписаниеТипов("Число"));
	ПериодыНормируемыхНачислений.Колонки.Добавить("КалендарныеДни", Новый ОписаниеТипов("Число"));
	
	РабочееВремя = МенеджерРасчетаЗарплаты.РабочееВремя();
		
	ТребованияНачислений = ТребованияНормируемыхПоВремениНачислений();
		
	Для Каждого Период Из ПериодыНормируемыхНачислений Цикл
		Если Период.НормироватьКаскадно Тогда
				
			Продолжить;
		КонецЕсли;	
		
		ТребованияТекущегоНачисления = ТребованияНачислений[Период.ВидРасчета];
		                                                                       	
		ДанныеОВремениЗаПериод = МенеджерРасчетаЗарплаты.ДанныеОВремениЗаПериод(
			Период, 
			Период.ПериодДействияНачало,
			Период.ПериодДействияКонец, 
			РабочееВремя, 
			ТребованияТекущегоНачисления,
			Ложь);	
		
		Если Период.ВремяВЧасах Тогда
			Период.Время = ДанныеОВремениЗаПериод.Часов;
		Иначе
			Период.Время = ДанныеОВремениЗаПериод.Дней;
		КонецЕсли;	
		
		Если Период.Время = 0 
			И Не ТребованияТекущегоНачисления.ТребуетсяРасчетВремени Тогда
			
			Период.КалендарныеДни = День(Период.ПериодДействияКонец) - День(Период.ПериодДействияНачало) + 1	
		КонецЕсли;
	КонецЦикла;
	
	ИтогиПоНормируемымЗаписям = ПериодыНормируемыхНачислений.Скопировать();
	ИтогиПоНормируемымЗаписям.Свернуть("Регистратор, НомерСтроки", "Время, КалендарныеДни");
	ИтогиПоНормируемымЗаписям.Индексы.Добавить("Регистратор, НомерСтроки");
	СтруктураПоискаИтогов = Новый Структура("Регистратор, НомерСтроки");
	Для Каждого Период Из ПериодыНормируемыхНачислений Цикл
		Если Период.НормироватьКаскадно Тогда
			Продолжить;
		КонецЕсли;	
		ЗаполнитьЗначенияСвойств(СтруктураПоискаИтогов, Период);
		СтрокаИтогов = ИтогиПоНормируемымЗаписям.НайтиСтроки(СтруктураПоискаИтогов)[0];
		Если СтрокаИтогов.Время = 0
			И СтрокаИтогов.КалендарныеДни = 0 Тогда
			
			Период.Коэффициент = 0;
		ИначеЕсли СтрокаИтогов.Время <> 0 Тогда
			Период.Коэффициент = Период.Время / СтрокаИтогов.Время;
		Иначе 
			Период.Коэффициент = Период.КалендарныеДни / СтрокаИтогов.КалендарныеДни;	
		КонецЕсли;	
	КонецЦикла;
КонецПроцедуры	

Процедура УточнитьРезультатыБазовыхЗаписей(БазовыеЗаписи, РезультатыРасчетаБазовыхНачислений, ИспользуемыйАлгоритмРасчета)
	ОбщегоНазначенияБЗК.ДобавитьИндексКоллекции(РезультатыРасчетаБазовыхНачислений, "Регистратор, НомерСтроки");
	СтруктураПоиска = Новый Структура("Регистратор, НомерСтроки");
	
	Для Каждого ВедущаяЗапись Из БазовыеЗаписи Цикл
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, ВедущаяЗапись);
		НайденныеСтроки = РезультатыРасчетаБазовыхНачислений.НайтиСтроки(СтруктураПоиска);
		Если НайденныеСтроки.Количество() > 0 Тогда
			ВедущаяЗапись.Результат = НайденныеСтроки[0].Результат;
			Если ИспользуемыйАлгоритмРасчета = АлгоритмыПолученияБазы().БазаНачислений Тогда
				ВедущаяЗапись.Результат = ВедущаяЗапись.Результат + НайденныеСтроки[0].РезультатФСС;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;	
КонецПроцедуры	

Функция МаксимальнаяОчередностьНачислений()
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МАКСИМУМ(Начисления.ОчередностьРасчета) КАК ОчередностьРасчета
	|ИЗ
	|	ПланВидовРасчета.Начисления КАК Начисления";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ОчередностьРасчета;
	Иначе
		Возврат 0;
	КонецЕсли;		
КонецФункции	

Процедура ИнициализироватьРасчетБазы(ОписаниеНабораРассчитываемыхЗаписей, ИспользуемыйАлгоритмРасчета, ПолучатьПодробнуюРасшифровку, ИсключаемыйРегистратор = Неопределено, ДанныеТекущегоНабораНачислений = Неопределено) 
	АлгоритмРасчетаБазы = ИспользуемыйАлгоритмРасчета;

	КоэффициентыНормированияПоВремениРассчитаны = Ложь;
	
	УстановитьРассчитываемыйНабор(ОписаниеНабораРассчитываемыхЗаписей);
		
	ВидыРасчетаСПодробнойРасшифровкой = Новый Соответствие();	
		
	ДанныеБазовыхНачислений = ДанныеБазовыхНачислений(
								ОписаниеНабораРассчитываемыхЗаписей, 
								ИспользуемыйАлгоритмРасчета, 
								ВидыРасчетаСПодробнойРасшифровкой,
								ПолучатьПодробнуюРасшифровку,
								ДанныеТекущегоНабораНачислений, 
								ИсключаемыйРегистратор);
	
	
	ДатыРазбиения = ДатыРазбиенияПериодовНормируемыхНачислений(ДанныеБазовыхНачислений.Записи);
	РазбитьПериодыНачислений(ДанныеБазовыхНачислений.ФактическиеПериоды, ДатыРазбиения);   

	СтруктураПоискаНормируемыхЗаписей = Новый Структура("НеобходимоНормировать, НормироватьКаскадно", Истина, Истина);
	КаскадноНормируемыеЗаписи = ДанныеБазовыхНачислений.Записи.НайтиСтроки(СтруктураПоискаНормируемыхЗаписей);
	
	Если КаскадноНормируемыеЗаписи.Количество() > 0 Тогда
		ДанныеВедущихЗаписейКаскадногоНормирования = ДанныеВедущихЗаписейКаскадногоНормирования(ДанныеБазовыхНачислений, ДанныеТекущегоНабораНачислений, ИсключаемыйРегистратор);
		
		КаскадноНормируемыеВидыРасчета = ОбщегоНазначения.ВыгрузитьКолонку(КаскадноНормируемыеЗаписи, "ВидРасчета", Истина);
		
		КаскадноНормируемыеВедущиеЗаписи = ДанныеВедущихЗаписейКаскадногоНормирования.Записи.НайтиСтроки(СтруктураПоискаНормируемыхЗаписей);
		ВидыРасчета =  ОбщегоНазначения.ВыгрузитьКолонку(КаскадноНормируемыеВедущиеЗаписи, "ВидРасчета", Истина);
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(КаскадноНормируемыеВидыРасчета, ВидыРасчета, Истина);
		
		ОтборБазовыхВидовРасчета = ОбщегоНазначения.ВыгрузитьКолонку(ДанныеВедущихЗаписейКаскадногоНормирования.Записи, "ВидРасчета", Истина);
		
		ЗависимостиКаскадногоРасчета = ЗависимостиВидовРасчета("ПланВидовРасчета.Начисления.БазовыеВидыРасчета", КаскадноНормируемыеВидыРасчета, ОтборБазовыхВидовРасчета);
	КонецЕсли;	
	УстановитФильтрДанныхОВремени();
КонецПроцедуры	

Процедура ЗавершитьРасчетБазы() Экспорт 
	МенеджерРасчетаЗарплаты = Неопределено;	
КонецПроцедуры

Процедура УстановитьРассчитываемыйНабор(ОписаниеНабораРассчитываемыхЗаписей)
	Если ОписаниеНабораРассчитываемыхЗаписей.Тип = Тип("ТаблицаЗначений") Тогда
		РассчитываемыйНабор = ОписаниеНабораРассчитываемыхЗаписей.РассчитываемыеЗаписи;
	Иначе
		Запрос = Новый Запрос("ВЫБРАТЬ * ИЗ " + ОписаниеНабораРассчитываемыхЗаписей.ИмяВТРассчитываемыеЗаписи);
		Запрос.МенеджерВременныхТаблиц = ОписаниеНабораРассчитываемыхЗаписей.МенеджерВременныхТаблиц;
		РассчитываемыйНабор = Запрос.Выполнить().Выгрузить();	
	КонецЕсли;				
КонецПроцедуры	

Процедура УстановитФильтрДанныхОВремени()
	СтруктураПоиска = Новый Структура("НормироватьКаскадно", Ложь);
	БазовыеНачисленияНормируемыеПоВремени = ДанныеБазовыхНачислений.ФактическиеПериоды.НайтиСтроки(СтруктураПоиска);
		
	ТребованияНачислений = ТребованияНормируемыхПоВремениНачислений();
	
	МенеджерРасчетаЗарплаты.ЗаполнитьФильтрПолученияРабочегоВремени(
		БазовыеНачисленияНормируемыеПоВремени, 
		ТребованияНачислений,
		Ложь, 
		Ложь);
	
	Если ДанныеВедущихЗаписейКаскадногоНормирования <> Неопределено Тогда
		СтруктураПоиска = Новый Структура("НормироватьКаскадно", Ложь);
		БазовыеНачисленияНормируемыеПоВремени = ДанныеВедущихЗаписейКаскадногоНормирования.ФактическиеПериоды.НайтиСтроки(СтруктураПоиска);
		
		МенеджерРасчетаЗарплаты.ЗаполнитьФильтрПолученияРабочегоВремени(
			БазовыеНачисленияНормируемыеПоВремени, 
			ТребованияНачислений, 
			Ложь, 
			Ложь);
	КонецЕсли;		
КонецПроцедуры	

Функция ТребованияНормируемыхПоВремениНачислений()
	Если КэшТребованияНормируемыхПоВремениНачислений <> Неопределено Тогда
		Возврат КэшТребованияНормируемыхПоВремениНачислений;
	КонецЕсли;	
	
	СтруктураПоиска = Новый Структура("НормироватьКаскадно", Ложь);
	БазовыеНачисленияНормируемыеПоВремени = ДанныеБазовыхНачислений.ФактическиеПериоды.НайтиСтроки(СтруктураПоиска);
	
	ВидыРасчетаОтбор = ОбщегоНазначения.ВыгрузитьКолонку(БазовыеНачисленияНормируемыеПоВремени, "ВидРасчета");
	
	Если ДанныеВедущихЗаписейКаскадногоНормирования <> Неопределено Тогда
		ВидыРасчетаВедущихЗаписей = ОбщегоНазначения.ВыгрузитьКолонку(
										ДанныеВедущихЗаписейКаскадногоНормирования.Записи, 
										"ВидРасчета");
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ВидыРасчетаОтбор, ВидыРасчетаВедущихЗаписей, Истина);
	КонецЕсли;	

	КэшТребованияНормируемыхПоВремениНачислений = ПланыВидовРасчета.Начисления.ТребованияНачисленийТаблица(ВидыРасчетаОтбор);
	
	Возврат КэшТребованияНормируемыхПоВремениНачислений;
КонецФункции	

Функция СвойстваВидовРасчетаРассчитываемогоНабора()
	Если КэшСвойстваВидовРасчетаРассчитываемогоНабора <> Неопределено Тогда
		Возврат КэшСвойстваВидовРасчетаРассчитываемогоНабора;
	КонецЕсли;
	
	Если РассчитываемыйНабор = Неопределено Тогда
		ВызватьИсключение "ru = 'Не установлен набор рассчитываемых записей видов расчета.'";
	КонецЕсли;	
	
	ОтборВидовРасчета = РассчитываемыйНабор.ВыгрузитьКолонку("ВидРасчета");
	ОтборВидовРасчета = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ОтборВидовРасчета);
	
	КэшСвойстваВидовРасчетаРассчитываемогоНабора = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(ОтборВидовРасчета, "ОчередностьРасчета");
	
	Возврат КэшСвойстваВидовРасчетаРассчитываемогоНабора;
КонецФункции	

Функция ЗависимостиВидовРасчетаРассчитываемогоНабора()
	Если КэшЗависимостиВидовРасчета <> Неопределено Тогда
		Возврат КэшЗависимостиВидовРасчета;
	КонецЕсли;
	
	Если РассчитываемыйНабор = Неопределено Тогда
		ВызватьИсключение "ru = 'Не установлен набор рассчитываемых записей видов расчета.'";
	КонецЕсли;	
	
	ОтборВидовРасчета = РассчитываемыйНабор.ВыгрузитьКолонку("ВидРасчета");
	ОтборВидовРасчета = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ОтборВидовРасчета);
	ТаблицаБазовыхВР = ТаблицаБазовыхВидовРасчета(АлгоритмРасчетаБазы);	
	
	КэшЗависимостиВидовРасчета = ЗависимостиВидовРасчета(ТаблицаБазовыхВР, ОтборВидовРасчета);
	
	Возврат КэшЗависимостиВидовРасчета;
КонецФункции

Функция ДанныеБазовыхНачислений(ОписаниеНабораРассчитываемыхЗаписей, АлгоритмОтбораБазовыхЗаписей, ВидыРасчетаСПодробнойРасшифровкой, ПолучатьРасшифровкуБезусловно = Ложь, ДанныеТекущегоНабораНачислений = Неопределено, ИсключаемыйРегистратор = Неопределено)		
	Результат = Новый Структура("Записи, ФактическиеПериоды");	
	БазовыеЗаписи = НовыйБазовыеЗаписи();
	
	Если РассчитываемыйНабор.Количество() = 0 Тогда
		Результат.Записи = БазовыеЗаписи;
		Результат.ФактическиеПериоды = НовыйФактическиеПериоды();
		
		Возврат Результат;
	КонецЕсли;			
	
	Если Не РасчетПоДаннымТекущегоНабора Тогда
		ЗаполнитьБазовыеЗаписиПоДаннымИБ(БазовыеЗаписи, ОписаниеНабораРассчитываемыхЗаписей, АлгоритмОтбораБазовыхЗаписей, ИсключаемыйРегистратор, ПолучатьРасшифровкуБезусловно);
	КонецЕсли;	
	Если ДанныеТекущегоНабораНачислений <> Неопределено Тогда		
		ЗаполнитьБазовыеЗаписиНачисленийПоТекущемуНабору(
			ОписаниеНабораРассчитываемыхЗаписей, 
			БазовыеЗаписи, 
			ДанныеТекущегоНабораНачислений.ТаблицаНабора, 
			АлгоритмОтбораБазовыхЗаписей,
			ВидыРасчетаСПодробнойРасшифровкой);
	КонецЕсли;			
	
	ФактическиеПериоды = ФактическиеПериодыНормируемыхЗаписей(БазовыеЗаписи, ДанныеТекущегоНабораНачислений);	
	
	УточнитьНеобходимостьНормированияПоФПД(БазовыеЗаписи, ФактическиеПериоды);
	
	Результат.Записи = БазовыеЗаписи;
	Результат.ФактическиеПериоды = ФактическиеПериоды;
	
	Возврат Результат;
КонецФункции

Функция ДатыРазбиенияПериодовНормируемыхНачислений(БазовыеЗаписи)
	ДатыРазбиения = Новый ТаблицаЗначений();
	ДатыРазбиения.Колонки.Добавить("Регистратор");
	ДатыРазбиения.Колонки.Добавить("НомерСтроки");
	ДатыРазбиения.Колонки.Добавить("Дата");
	
	Для Каждого Запись Из БазовыеЗаписи Цикл
		Если Запись.НеобходимоНормировать Тогда
			СтрокаТаблицыДат = ДатыРазбиения.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаблицыДат, Запись, "Регистратор, НомерСтроки");
			СтрокаТаблицыДат.Дата = НачалоДня(Запись.БазовыйПериодОсновнойЗаписиНачало);
			
			СтрокаТаблицыДат = ДатыРазбиения.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаблицыДат, Запись, "Регистратор, НомерСтроки");
			СтрокаТаблицыДат.Дата = КонецДня(Запись.БазовыйПериодОсновнойЗаписиКонец) + 1;		
		КонецЕсли;	
	КонецЦикла;	
	
	ДатыРазбиения.Свернуть("Регистратор, НомерСтроки, Дата");
	
	Возврат ДатыРазбиения;
КонецФункции	

Функция ЗапросПолученияБазовыхЗаписей(ИмяВТРассчитываемыйНабор, АлгоритмОтбораБазовыхЗаписей, ИсключаемыйРегистратор = Неопределено, ПолучатьРасшифровкуБезусловно = Ложь)
	ОписаниеПакетаЗапросов = ЗарплатаКадрыПериодическиеРегистры.НовыйОписаниеПакетаЗапросовКРегистру();
	ОписаниеПакетаЗапросов.Параметры.Вставить("ИсключаемыйРегистратор", ИсключаемыйРегистратор);

	ОписаниеПакетаЗапросов.Параметры.Вставить("ВидыРасчетаСПодробнойРасшифровкой", Новый Массив());
	ОписаниеПакетаЗапросов.Параметры.Вставить("ПолучатьРасшифровкуБезусловно", ПолучатьРасшифровкуБезусловно);

	БазовыеИзмерения = БазовыеИзмерения(АлгоритмОтбораБазовыхЗаписей);
	БазовыеИзмеренияМассив = ОбщегоНазначения.ВыгрузитьКолонку(БазовыеИзмерения, "Ключ");
	ОписаниеФильтра = ЗарплатаКадрыПериодическиеРегистры.ОписаниеФильтраДляСоздатьВТИмяРегистраПоВременнойТаблице(ИмяВТРассчитываемыйНабор, БазовыеИзмеренияМассив);
	ОписаниеФильтра.СоответствиеИзмеренийРегистраИзмерениямФильтра = БазовыеИзмерения;
	
	ОписаниеРегистра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеРегистра("Начисления");
	
	ШаблонЗапроса =  
	"ВЫБРАТЬ
	|	БазовыеЗаписи.БазовыйПериодОсновнойЗаписиНачало КАК БазовыйПериодОсновнойЗаписиНачало,
	|	БазовыеЗаписи.БазовыйПериодОсновнойЗаписиКонец КАК БазовыйПериодОсновнойЗаписиКонец,
	|	БазовыеЗаписи.ОсновнаяЗаписьРассчитыватьПоРазовымНачислениямДокумента КАК ОсновнаяЗаписьРассчитыватьПоРазовымНачислениямДокумента,
	|	БазовыеЗаписи.ОсновнаяЗаписьРегистраторРазовогоНачисления КАК ОсновнаяЗаписьРегистраторРазовогоНачисления,
	|	БазовыеЗаписи.ВидРасчета КАК ВидРасчета,
	|	БазовыеЗаписи.Сотрудник КАК Сотрудник,
	|	БазовыеЗаписи.ФизическоеЛицо КАК ФизическоеЛицо,
	|	БазовыеЗаписи.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	БазовыеЗаписи.ПериодДействияБазовый КАК ПериодДействияБазовый,
	|	БазовыеЗаписи.ОчередностьРасчета КАК ОчередностьРасчета,
	|	БазовыеЗаписи.НеобходимоНормировать КАК НеобходимоНормировать,
	|	ВЫБОР
	|		КОГДА БазовыеЗаписи.ВремяВЦеломЗаПериод
	|			ТОГДА ЛОЖЬ
	|		КОГДА НЕ БазовыеЗаписи.НормироватьКаскадно
	|				И БазовыеЗаписи.ЕстьВытесняющиеНачисления
	|			ТОГДА ИСТИНА
	|		КОГДА БазовыеЗаписи.ПериодДействияБазовый
	|				И БазовыеЗаписи.ЕстьВытесняющиеНачисления
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НеобходимФПДПриНормировании,
	|	БазовыеЗаписи.НормироватьКаскадно КАК НормироватьКаскадно,
	|	БазовыеЗаписи.ЗаписьТекущегоНабора КАК ЗаписьТекущегоНабора,
	|	СУММА(БазовыеЗаписи.Результат) КАК Результат,
	|	ВЫБОР
	|		КОГДА БазовыеЗаписи.ПолучатьПодробнуюРасшифровку
	|			ТОГДА БазовыеЗаписи.Регистратор
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Регистратор,
	|	ВЫБОР
	|		КОГДА БазовыеЗаписи.ПолучатьПодробнуюРасшифровку
	|			ТОГДА БазовыеЗаписи.НомерСтроки
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НомерСтроки,
	|	ВЫБОР
	|		КОГДА БазовыеЗаписи.ПолучатьПодробнуюРасшифровку
	|			ТОГДА БазовыеЗаписи.ИдентификаторСтроки
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ИдентификаторСтроки,
	|	ВЫБОР
	|		КОГДА БазовыеЗаписи.ПолучатьПодробнуюРасшифровку
	|			ТОГДА БазовыеЗаписи.ПериодРегистрации
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|	КОНЕЦ КАК ПериодРегистрации,
	|	ВЫБОР
	|		КОГДА БазовыеЗаписи.ПолучатьПодробнуюРасшифровку
	|			ТОГДА БазовыеЗаписи.ПериодДействия
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|	КОНЕЦ КАК ПериодДействия,
	|	ВЫБОР
	|		КОГДА БазовыеЗаписи.ПолучатьПодробнуюРасшифровку
	|			ТОГДА БазовыеЗаписи.ПериодДействияНачало
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|	КОНЕЦ КАК ПериодДействияНачало,
	|	ВЫБОР
	|		КОГДА БазовыеЗаписи.ПолучатьПодробнуюРасшифровку
	|			ТОГДА БазовыеЗаписи.ПериодДействияКонец
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|	КОНЕЦ КАК ПериодДействияКонец,
	|	ВЫБОР
	|		КОГДА БазовыеЗаписи.ПолучатьПодробнуюРасшифровку
	|			ТОГДА БазовыеЗаписи.ГрафикРаботы
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ГрафикРаботы,
	|	ВЫБОР
	|		КОГДА БазовыеЗаписи.ПолучатьПодробнуюРасшифровку
	|			ТОГДА БазовыеЗаписи.ОбщийГрафик
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ОбщийГрафик,
	|	ВЫБОР
	|		КОГДА БазовыеЗаписи.ПолучатьПодробнуюРасшифровку
	|			ТОГДА БазовыеЗаписи.ПериодРегистрацииВремени
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|	КОНЕЦ КАК ПериодРегистрацииВремени,
	|	ВЫБОР
	|		КОГДА БазовыеЗаписи.ПолучатьПодробнуюРасшифровку
	|			ТОГДА БазовыеЗаписи.ВремяВЧасах
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ВремяВЧасах,
	|	ВЫБОР
	|		КОГДА БазовыеЗаписи.ПолучатьПодробнуюРасшифровку
	|			ТОГДА БазовыеЗаписи.ЕстьВытесняющиеНачисления
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЕстьВытесняющиеНачисления,
	|	ВЫБОР
	|		КОГДА БазовыеЗаписи.ПолучатьПодробнуюРасшифровку
	|			ТОГДА БазовыеЗаписи.НачисляетсяВЦеломЗаМесяц
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НачисляетсяВЦеломЗаМесяц,
	|	ВЫБОР
	|		КОГДА БазовыеЗаписи.ПолучатьПодробнуюРасшифровку
	|			ТОГДА БазовыеЗаписи.ВремяВЦеломЗаПериод
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ВремяВЦеломЗаПериод,
	|	ВЫБОР
	|		КОГДА БазовыеЗаписи.ПолучатьПодробнуюРасшифровку
	|			ТОГДА БазовыеЗаписи.РассчитыватьПоРазовымНачислениямДокумента
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК РассчитыватьПоРазовымНачислениямДокумента,
	|	ВЫБОР
	|		КОГДА БазовыеЗаписи.ПолучатьПодробнуюРасшифровку
	|			ТОГДА БазовыеЗаписи.РегистраторРазовогоНачисления
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК РегистраторРазовогоНачисления,
	|	ВЫБОР
	|		КОГДА БазовыеЗаписи.ПолучатьПодробнуюРасшифровку
	|			ТОГДА БазовыеЗаписи.ИсходныйДокумент
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ИсходныйДокумент,
	|	ВЫБОР
	|		КОГДА БазовыеЗаписи.ПолучатьПодробнуюРасшифровку
	|			ТОГДА БазовыеЗаписи.ДокументОснование
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ДокументОснование,
	|	ВЫБОР
	|		КОГДА БазовыеЗаписи.ПолучатьПодробнуюРасшифровку
	|			ТОГДА БазовыеЗаписи.Сторно
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Сторно
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ОсновныеЗаписи.БазовыйПериодНачало КАК БазовыйПериодОсновнойЗаписиНачало,
	|		ОсновныеЗаписи.БазовыйПериодКонец КАК БазовыйПериодОсновнойЗаписиКонец,
	|		ОсновныеЗаписи.РассчитыватьПоРазовымНачислениямДокумента КАК ОсновнаяЗаписьРассчитыватьПоРазовымНачислениямДокумента,
	|		ВЫБОР
	|			КОГДА ОсновныеЗаписи.РассчитыватьПоРазовымНачислениямДокумента
	|				ТОГДА ОсновныеЗаписи.РегистраторРазовогоНачисления
	|			ИНАЧЕ НЕОПРЕДЕЛЕНО
	|		КОНЕЦ КАК ОсновнаяЗаписьРегистраторРазовогоНачисления,
	|		БазовыеЗаписи.Регистратор КАК Регистратор,
	|		БазовыеЗаписи.НомерСтроки КАК НомерСтроки,
	|		БазовыеЗаписи.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|		БазовыеЗаписи.ПериодРегистрации КАК ПериодРегистрации,
	|		БазовыеЗаписи.ПериодДействия КАК ПериодДействия,
	|		БазовыеЗаписи.ПериодДействияНачало КАК ПериодДействияНачало,
	|		БазовыеЗаписи.ПериодДействияКонец КАК ПериодДействияКонец,
	|		БазовыеЗаписи.ВидРасчета КАК ВидРасчета,
	|		&ШаблонНеобходимоНормировать КАК НеобходимоНормировать,
	|		ВЫБОР
	|			КОГДА &НеобходимоНормировать
	|					ИЛИ ОсновныеЗаписи.ВидРасчета В (&ВидыРасчетаСПодробнойРасшифровкой)
	|					ИЛИ &ПолучатьРасшифровкуБезусловно
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ КАК ПолучатьПодробнуюРасшифровку,
	|		БазовыеЗаписи.ВидРасчета.НачисляетсяВЦеломЗаМесяц КАК НачисляетсяВЦеломЗаМесяц,
	|		БазовыеЗаписи.ВидРасчета.ЕстьВытесняющиеНачисления КАК ЕстьВытесняющиеНачисления,
	|		БазовыеЗаписи.Сотрудник КАК Сотрудник,
	|		БазовыеЗаписи.ФизическоеЛицо КАК ФизическоеЛицо,
	|		БазовыеЗаписи.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|		БазовыеЗаписи.ГоловнойСотрудник КАК ГоловнойСотрудник,
	|		&ШаблонРезультат КАК Результат,
	|		БазовыеЗаписи.ГрафикРаботы КАК ГрафикРаботы,
	|		БазовыеЗаписи.ОбщийГрафик КАК ОбщийГрафик,
	|		БазовыеЗаписи.ВремяВЧасах КАК ВремяВЧасах,
	|		БазовыеЗаписи.ПериодРегистрацииВремени КАК ПериодРегистрацииВремени,
	|		ВЫБОР
	|			КОГДА БазовыеЗаписи.ВидРасчета.ТребуетсяРасчетБазы
	|					И БазовыеЗаписи.ПериодДействия = НАЧАЛОПЕРИОДА(БазовыеЗаписи.БазовыйПериодНачало, МЕСЯЦ)
	|					И БазовыеЗаписи.ПериодДействия = НАЧАЛОПЕРИОДА(БазовыеЗаписи.БазовыйПериодКонец, МЕСЯЦ)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ КАК НормироватьКаскадно,
	|		ЛОЖЬ КАК ЗаписьТекущегоНабора,
	|		БазовыеЗаписи.ВидРасчета.ПериодДействияБазовый КАК ПериодДействияБазовый,
	|		БазовыеЗаписи.ВидРасчета.ОчередностьРасчета КАК ОчередностьРасчета,
	|		БазовыеЗаписи.ВремяВЦеломЗаПериод КАК ВремяВЦеломЗаПериод,
	|		БазовыеЗаписи.РассчитыватьПоРазовымНачислениямДокумента КАК РассчитыватьПоРазовымНачислениямДокумента,
	|		БазовыеЗаписи.РегистраторРазовогоНачисления КАК РегистраторРазовогоНачисления,
	|		БазовыеЗаписи.ИсходныйДокумент КАК ИсходныйДокумент,
	|		БазовыеЗаписи.ДокументОснование КАК ДокументОснование,
	|		БазовыеЗаписи.Сторно КАК Сторно
	|	ИЗ
	|		ВТОсновныеЗаписи КАК ОсновныеЗаписи
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрРасчета.Начисления КАК БазовыеЗаписи
	|			ПО (&ШаблонУсловиеСвязиИзмерениям)
	|				И (&ШаблонУсловиеСвязиПоПериоду)
	|				И (БазовыеЗаписи.Регистратор <> &ИсключаемыйРегистратор)
	|				И (ВЫБОР
	|					КОГДА ОсновныеЗаписи.РассчитыватьПоРазовымНачислениямДокумента
	|						ТОГДА ОсновныеЗаписи.РегистраторРазовогоНачисления = БазовыеЗаписи.РегистраторРазовогоНачисления
	|					ИНАЧЕ ИСТИНА
	|				КОНЕЦ)
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ #ИмяТаблицыБазовыхВР КАК БазовыеВидыРасчета
	|			ПО (БазовыеВидыРасчета.ВидРасчета = БазовыеЗаписи.ВидРасчета)
	|				И (БазовыеВидыРасчета.Ссылка = ОсновныеЗаписи.ВидРасчета)) КАК БазовыеЗаписи
	|
	|СГРУППИРОВАТЬ ПО
	|	БазовыеЗаписи.ВидРасчета,
	|	БазовыеЗаписи.Сотрудник,
	|	БазовыеЗаписи.ФизическоеЛицо,
	|	БазовыеЗаписи.ГоловнаяОрганизация,
	|	БазовыеЗаписи.ОчередностьРасчета,
	|	БазовыеЗаписи.ЗаписьТекущегоНабора,
	|	БазовыеЗаписи.БазовыйПериодОсновнойЗаписиНачало,
	|	БазовыеЗаписи.БазовыйПериодОсновнойЗаписиКонец,
	|	БазовыеЗаписи.ОсновнаяЗаписьРассчитыватьПоРазовымНачислениямДокумента,
	|	БазовыеЗаписи.ОсновнаяЗаписьРегистраторРазовогоНачисления,
	|	ВЫБОР
	|		КОГДА БазовыеЗаписи.ПолучатьПодробнуюРасшифровку
	|			ТОГДА БазовыеЗаписи.Регистратор
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА БазовыеЗаписи.ПолучатьПодробнуюРасшифровку
	|			ТОГДА БазовыеЗаписи.НомерСтроки
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА БазовыеЗаписи.ПолучатьПодробнуюРасшифровку
	|			ТОГДА БазовыеЗаписи.ИдентификаторСтроки
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА БазовыеЗаписи.ПолучатьПодробнуюРасшифровку
	|			ТОГДА БазовыеЗаписи.ПериодРегистрации
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА БазовыеЗаписи.ПолучатьПодробнуюРасшифровку
	|			ТОГДА БазовыеЗаписи.ПериодДействия
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА БазовыеЗаписи.ПолучатьПодробнуюРасшифровку
	|			ТОГДА БазовыеЗаписи.ПериодДействияНачало
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА БазовыеЗаписи.ПолучатьПодробнуюРасшифровку
	|			ТОГДА БазовыеЗаписи.ПериодДействияКонец
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА БазовыеЗаписи.ПолучатьПодробнуюРасшифровку
	|			ТОГДА БазовыеЗаписи.ГрафикРаботы
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА БазовыеЗаписи.ПолучатьПодробнуюРасшифровку
	|			ТОГДА БазовыеЗаписи.ОбщийГрафик
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА БазовыеЗаписи.ПолучатьПодробнуюРасшифровку
	|			ТОГДА БазовыеЗаписи.ПериодРегистрацииВремени
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА БазовыеЗаписи.ПолучатьПодробнуюРасшифровку
	|			ТОГДА БазовыеЗаписи.ВремяВЧасах
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	БазовыеЗаписи.НеобходимоНормировать,
	|	БазовыеЗаписи.ПериодДействияБазовый,
	|	БазовыеЗаписи.НормироватьКаскадно,
	|	ВЫБОР
	|		КОГДА БазовыеЗаписи.ПолучатьПодробнуюРасшифровку
	|			ТОГДА БазовыеЗаписи.ЕстьВытесняющиеНачисления
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА БазовыеЗаписи.ПолучатьПодробнуюРасшифровку
	|			ТОГДА БазовыеЗаписи.НачисляетсяВЦеломЗаМесяц
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА БазовыеЗаписи.ВремяВЦеломЗаПериод
	|			ТОГДА ЛОЖЬ
	|		КОГДА НЕ БазовыеЗаписи.НормироватьКаскадно
	|				И БазовыеЗаписи.ЕстьВытесняющиеНачисления
	|			ТОГДА ИСТИНА
	|		КОГДА БазовыеЗаписи.ПериодДействияБазовый
	|				И БазовыеЗаписи.ЕстьВытесняющиеНачисления
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА БазовыеЗаписи.ПолучатьПодробнуюРасшифровку
	|			ТОГДА БазовыеЗаписи.ВремяВЦеломЗаПериод
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА БазовыеЗаписи.ПолучатьПодробнуюРасшифровку
	|			ТОГДА БазовыеЗаписи.РассчитыватьПоРазовымНачислениямДокумента
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА БазовыеЗаписи.ПолучатьПодробнуюРасшифровку
	|			ТОГДА БазовыеЗаписи.РегистраторРазовогоНачисления
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА БазовыеЗаписи.ПолучатьПодробнуюРасшифровку
	|			ТОГДА БазовыеЗаписи.ИсходныйДокумент
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА БазовыеЗаписи.ПолучатьПодробнуюРасшифровку
	|			ТОГДА БазовыеЗаписи.ДокументОснование
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА БазовыеЗаписи.ПолучатьПодробнуюРасшифровку
	|			ТОГДА БазовыеЗаписи.Сторно
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ";
	
	ОписаниеВнешнегоЗапроса = ЗарплатаКадрыПериодическиеРегистры.ОписаниеЗапросаПоТексту(ШаблонЗапроса);
	ОписаниеПакетаЗапросов.ЗапросыПолученияДанных.Добавить(ОписаниеВнешнегоЗапроса);
	
	ОписаниеЗапросаДетальныхЗаписей = ЗарплатаКадрыПериодическиеРегистры.ОписаниеТаблицыЗапросаПоПсевдониму(ОписаниеВнешнегоЗапроса.Операторы[0], "БазовыеЗаписи").ОписаниеЗапроса;
	ОператорЗапросаДетальныхЗаписей = ОписаниеЗапросаДетальныхЗаписей.Операторы[0];
	
	Если АлгоритмРасчетаБазы <> АлгоритмыПолученияБазы().БазаРезервов Тогда
		ТаблицаБазовыхВидовРасчета = ТаблицаБазовыхВидовРасчета(АлгоритмОтбораБазовыхЗаписей);
	Иначе
		ТаблицаБазовыхВидовРасчета = "ВТБазовыеВидыРасчетов";
	КонецЕсли;
	ЗарплатаКадрыПериодическиеРегистры.ЗаменитьТаблицуВОператореЗапроса(ОператорЗапросаДетальныхЗаписей, "БазовыеВидыРасчета", ТаблицаБазовыхВидовРасчета);
	
	ОписаниеИспользованияФильтра = ЗарплатаКадрыПериодическиеРегистры.ОписаниеИспользованиеФильтра("ОсновныеЗаписи", "БазовыеЗаписи");	
	ЗарплатаКадрыПериодическиеРегистры.ИнициализироватьИспользованиеФильтра(
		ОписаниеИспользованияФильтра, 
		ОписаниеФильтра, 
		ОписаниеРегистра, 
		"БазовыйПериодНачало, БазовыйПериодКонец", 
		ОператорЗапросаДетальныхЗаписей);	
	
	ВыражениеПоляНеобходимоНормировать = ВыражениеПоляЗапросаНеобходимоНормировать(АлгоритмОтбораБазовыхЗаписей);
	ЗарплатаКадрыПериодическиеРегистры.ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапросаДетальныхЗаписей, 0, ВыражениеПоляНеобходимоНормировать, "НеобходимоНормировать");
	
	ВыражениеПоляПоучатьПодробнуюРасшифровку = ЗарплатаКадрыПериодическиеРегистры.ВыражениеПоляПоПсевдониму(ОператорЗапросаДетальныхЗаписей, "ПолучатьПодробнуюРасшифровку");
	ВыражениеПоляПоучатьПодробнуюРасшифровку = СтрЗаменить(ВыражениеПоляПоучатьПодробнуюРасшифровку, "&НеобходимоНормировать", ВыражениеПоляНеобходимоНормировать);
	ЗарплатаКадрыПериодическиеРегистры.ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапросаДетальныхЗаписей, 0, ВыражениеПоляПоучатьПодробнуюРасшифровку, "ПолучатьПодробнуюРасшифровку");
	
	ВыражениеПоляРезультат = ВыражениеПоляЗапросаРезультат(АлгоритмОтбораБазовыхЗаписей);
	ЗарплатаКадрыПериодическиеРегистры.ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапросаДетальныхЗаписей, 0, ВыражениеПоляРезультат, "Результат");
	
	УсловияОтбораПоПериоду = ВыраженияОтбораБазовыхЗаписейПоПериоду(АлгоритмОтбораБазовыхЗаписей);
	ЗарплатаКадрыПериодическиеРегистры.ДобавитьУсловиеСоединения(ОператорЗапросаДетальныхЗаписей, "БазовыеЗаписи", УсловияОтбораПоПериоду);
		
	Если ИсключаемыйРегистратор <> Неопределено Тогда
		ТекстУсловия = "БазовыеЗаписи.Регистратор <> &ИсключаемыйРегистратор";
		ЗарплатаКадрыПериодическиеРегистры.ДобавитьУсловие(ОператорЗапросаДетальныхЗаписей, ТекстУсловия);
		ОписаниеПакетаЗапросов.Параметры.Вставить("ИсключаемыйРегистратор", ИсключаемыйРегистратор);
	КонецЕсли;	
	
	ЗарплатаКадрыПериодическиеРегистры.УстановитьФильтрВОписаниеПакетаЗапросовКРегистру(ОписаниеПакетаЗапросов, ОписаниеФильтра, ОписаниеИспользованияФильтра);
			
	Запрос = ЗарплатаКадрыПериодическиеРегистры.ЗапросПоОписаниюПакета(ОписаниеПакетаЗапросов);	
	Если АлгоритмРасчетаБазы = АлгоритмыПолученияБазы().БазаРезервов Тогда
		ЗапросВТБазовыеВидыРасчетов =
			РезервыПоОплатеТрудаРасширенный.ЗапросВТБазовыеВидыРасчетов(ИмяВТРассчитываемыйНабор, МенеджерРасчетаЗарплаты.НастройкиРасчета.НастройкаРезервов);
		ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьЗапросы(ЗапросВТБазовыеВидыРасчетов, Запрос);
		Запрос = ЗапросВТБазовыеВидыРасчетов;
	КонецЕсли;
	
	Возврат Запрос;
КонецФункции	

Функция НовыйБазовыеЗаписи()		
	БазовыеЗаписи = Новый ТаблицаЗначений();
	
	БазовыеЗаписи.Колонки.Добавить("БазовыйПериодОсновнойЗаписиНачало", Новый ОписаниеТипов("Дата"));
	БазовыеЗаписи.Колонки.Добавить("БазовыйПериодОсновнойЗаписиКонец", Новый ОписаниеТипов("Дата"));
	БазовыеЗаписи.Колонки.Добавить("ОсновнаяЗаписьРассчитыватьПоРазовымНачислениямДокумента", Новый ОписаниеТипов("Булево"));
	БазовыеЗаписи.Колонки.Добавить("ОсновнаяЗаписьРегистраторРазовогоНачисления", Метаданные.ОпределяемыеТипы.РегистраторРазовогоНачисления.Тип);
	БазовыеЗаписи.Колонки.Добавить("Регистратор", РегистрыРасчета.Начисления.ОписаниеТиповПоляРегистратор());
	БазовыеЗаписи.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число"));
	БазовыеЗаписи.Колонки.Добавить("ВидРасчета", Новый ОписаниеТипов("ПланВидовРасчетаСсылка.Начисления"));
	БазовыеЗаписи.Колонки.Добавить("ОчередностьРасчета", Новый ОписаниеТипов("Число"));
	БазовыеЗаписи.Колонки.Добавить("ПериодДействияБазовый", Новый ОписаниеТипов("Булево"));
	БазовыеЗаписи.Колонки.Добавить("НеобходимоНормировать", Новый ОписаниеТипов("Булево"));
	БазовыеЗаписи.Колонки.Добавить("НеобходимФПДПриНормировании", Новый ОписаниеТипов("Булево"));
	БазовыеЗаписи.Колонки.Добавить("НачисляетсяВЦеломЗаМесяц", Новый ОписаниеТипов("Булево"));
	БазовыеЗаписи.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	БазовыеЗаписи.Колонки.Добавить("ФизическоеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	БазовыеЗаписи.Колонки.Добавить("ГоловнаяОрганизация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	БазовыеЗаписи.Колонки.Добавить("НормироватьКаскадно", Новый ОписаниеТипов("Булево"));
	БазовыеЗаписи.Колонки.Добавить("ЗаписьТекущегоНабора", Новый ОписаниеТипов("Булево"));
	БазовыеЗаписи.Колонки.Добавить("ИдентификаторСтроки", Новый ОписаниеТипов("Число"));
	БазовыеЗаписи.Колонки.Добавить("ПериодРегистрации", Новый ОписаниеТипов("Дата"));
	БазовыеЗаписи.Колонки.Добавить("ПериодДействия", Новый ОписаниеТипов("Дата"));
	БазовыеЗаписи.Колонки.Добавить("ПериодДействияНачало", Новый ОписаниеТипов("Дата"));
	БазовыеЗаписи.Колонки.Добавить("ПериодДействияКонец", Новый ОписаниеТипов("Дата"));
	БазовыеЗаписи.Колонки.Добавить("ГрафикРаботы", Новый ОписаниеТипов("СправочникСсылка.Сотрудники, СправочникСсылка.ГрафикиРаботыСотрудников"));
	БазовыеЗаписи.Колонки.Добавить("ОбщийГрафик", Новый ОписаниеТипов("СправочникСсылка.ГрафикиРаботыСотрудников"));
	БазовыеЗаписи.Колонки.Добавить("ПериодРегистрацииВремени", Новый ОписаниеТипов("Дата"));
	БазовыеЗаписи.Колонки.Добавить("ВремяВЧасах", Новый ОписаниеТипов("Булево"));   
	БазовыеЗаписи.Колонки.Добавить("ВремяВЦеломЗаПериод", Новый ОписаниеТипов("Булево"));
	БазовыеЗаписи.Колонки.Добавить("Результат", Новый ОписаниеТипов("Число"));
	БазовыеЗаписи.Колонки.Добавить("РассчитыватьПоРазовымНачислениямДокумента", Новый ОписаниеТипов("Булево"));
	БазовыеЗаписи.Колонки.Добавить("РегистраторРазовогоНачисления", Метаданные.ОпределяемыеТипы.РегистраторРазовогоНачисления.Тип);
	БазовыеЗаписи.Колонки.Добавить("СторнируемыйДокумент", Метаданные.ОпределяемыеТипы.СторнируемыйДокумент.Тип);
	БазовыеЗаписи.Колонки.Добавить("ДокументОснование", Метаданные.ОпределяемыеТипы.ОснованиеНачисления.Тип);
	БазовыеЗаписи.Колонки.Добавить("Сторно", Новый ОписаниеТипов("Число"));

	
	Возврат БазовыеЗаписи;
КонецФункции	

Функция ФактическиеПериодыНормируемыхЗаписей(БазовыеЗаписи, ДанныеТекущегоНабора = Неопределено)
	ФактическиеПериоды = ФПДНормируемыхЗаписей(БазовыеЗаписи, ДанныеТекущегоНабора); 
	УточнитьПериодСторноЗаписей(БазовыеЗаписи, ФактическиеПериоды);
	УточнитьФактическиПериодыЦеломесячныхЗаписей(ФактическиеПериоды);

	Возврат ФактическиеПериоды;
КонецФункции	

Функция НовыйФактическиеПериоды()
	ФактическиеПериоды = Новый ТаблицаЗначений();
	ФактическиеПериоды.Колонки.Добавить("Регистратор", РегистрыРасчета.Начисления.ОписаниеТиповПоляРегистратор());
	ФактическиеПериоды.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число"));
	ФактическиеПериоды.Колонки.Добавить("ВидРасчета", Новый ОписаниеТипов("ПланВидовРасчетаСсылка.Начисления"));
	ФактическиеПериоды.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	ФактическиеПериоды.Колонки.Добавить("ОчередностьРасчета", Новый ОписаниеТипов("Число"));
	ФактическиеПериоды.Колонки.Добавить("НачисляетсяВЦеломЗаМесяц", Новый ОписаниеТипов("Булево"));
	ФактическиеПериоды.Колонки.Добавить("НормироватьКаскадно", Новый ОписаниеТипов("Булево"));
	ФактическиеПериоды.Колонки.Добавить("ЗаписьТекущегоНабора", Новый ОписаниеТипов("Булево"));
	ФактическиеПериоды.Колонки.Добавить("ПериодДействия", Новый ОписаниеТипов("Дата"));
	ФактическиеПериоды.Колонки.Добавить("ПериодДействияНачало", Новый ОписаниеТипов("Дата"));
	ФактическиеПериоды.Колонки.Добавить("ПериодДействияКонец", Новый ОписаниеТипов("Дата"));
	ФактическиеПериоды.Колонки.Добавить("ГрафикРаботы", Новый ОписаниеТипов("СправочникСсылка.Сотрудники, СправочникСсылка.ГрафикиРаботыСотрудников"));
	ФактическиеПериоды.Колонки.Добавить("ОбщийГрафик", Новый ОписаниеТипов("СправочникСсылка.ГрафикиРаботыСотрудников"));
	ФактическиеПериоды.Колонки.Добавить("ПериодРегистрацииВремени", Новый ОписаниеТипов("Дата"));
	ФактическиеПериоды.Колонки.Добавить("ВремяВЧасах", Новый ОписаниеТипов("Булево"));   
	ФактическиеПериоды.Колонки.Добавить("ВремяВЦеломЗаПериод", Новый ОписаниеТипов("Булево"));
	ФактическиеПериоды.Колонки.Добавить("РассчитыватьПоРазовымНачислениямДокумента", Новый ОписаниеТипов("Булево"));
	ФактическиеПериоды.Колонки.Добавить("Результат", Новый ОписаниеТипов("Число"));
	ФактическиеПериоды.Колонки.Добавить("РегистраторРазовогоНачисления", Метаданные.ОпределяемыеТипы.РегистраторРазовогоНачисления.Тип);
	ФактическиеПериоды.Колонки.Добавить("СторнируемыйДокумент", Метаданные.ОпределяемыеТипы.СторнируемыйДокумент.Тип);
	
	Возврат ФактическиеПериоды;	
КонецФункции

Функция ФПДНормируемыхЗаписей(БазовыеЗаписи, ДанныеТекущегоНабора = Неопределено)
	ФактическиеПериоды = НовыйФактическиеПериоды();
	
	СтруктураПоиска = Новый Структура("Регистратор, НомерСтроки");
	ОтборДанныхИБ = БазовыеЗаписи.СкопироватьКолонки("Регистратор, НомерСтроки");
	Если ДанныеТекущегоНабора <> Неопределено Тогда
		ОбщегоНазначенияБЗК.ДобавитьИндексКоллекции(ДанныеТекущегоНабора.ФПД, "Регистратор, НомерСтроки");
	КонецЕсли;	
		
	Для Каждого БазоваяЗапись Из БазовыеЗаписи Цикл
		Если Не БазоваяЗапись.НеобходимоНормировать Тогда	
			Продолжить;
		КонецЕсли;	
		
		Если Не БазоваяЗапись.НеобходимФПДПриНормировании Тогда
			СтрокаТаблицыПериодов = ФактическиеПериоды.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаблицыПериодов, БазоваяЗапись);
		ИначеЕсли ДанныеТекущегоНабора <> Неопределено Тогда	
			ЗаполнитьЗначенияСвойств(СтруктураПоиска, БазоваяЗапись);
			СтрокиФПД = ДанныеТекущегоНабора.ФПД.НайтиСтроки(СтруктураПоиска);
			Для Каждого ТекущаяСтрокаФПД Из СтрокиФПД Цикл
				СтрокаТаблицыПериодов = ФактическиеПериоды.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТаблицыПериодов, БазоваяЗапись);
				ЗаполнитьЗначенияСвойств(СтрокаТаблицыПериодов, ТекущаяСтрокаФПД);
			КонецЦикла;			
	
			Если СтрокиФПД.Количество() = 0 Тогда
				ЗаполнитьЗначенияСвойств(ОтборДанныхИБ.Добавить(), БазоваяЗапись);
			КонецЕсли;
		Иначе
			ЗаполнитьЗначенияСвойств(ОтборДанныхИБ.Добавить(), БазоваяЗапись);	
		КонецЕсли;	
	КонецЦикла;

	
	ПоляГруппировки = ОбщегоНазначения.ВыгрузитьКолонку(ФактическиеПериоды.Колонки, "Имя");
	ФактическиеПериоды.Свернуть(СтрСоединить(ПоляГруппировки, ","));
	
	ОтборДанныхИБ.Свернуть("Регистратор, НомерСтроки");
	
	Если ОтборДанныхИБ.Количество() = 0 Тогда
		Возврат ФактическиеПериоды;
	КонецЕсли;
		
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Отбор", ОтборДанныхИБ);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Отбор.Регистратор,
	|	Отбор.НомерСтроки
	|ПОМЕСТИТЬ ВТОтбор
	|ИЗ
	|	&Отбор КАК Отбор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачисленияФактическийПериодДействия.Регистратор,
	|	НачисленияФактическийПериодДействия.НомерСтроки,
	|	НачисленияФактическийПериодДействия.ПериодДействияНачало КАК ПериодДействияНачало,
	|	НачисленияФактическийПериодДействия.ПериодДействияКонец КАК ПериодДействияКонец
	|ИЗ
	|	РегистрРасчета.Начисления.ФактическийПериодДействия((Регистратор, НомерСтроки) В
	|		(ВЫБРАТЬ
	|			Регистратор,
	|			НомерСтроки
	|		ИЗ
	|			ВТОтбор)) КАК НачисленияФактическийПериодДействия";		
	
	ОбщегоНазначенияБЗК.ДобавитьИндексКоллекции(БазовыеЗаписи, "Регистратор, НомерСтроки");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл		
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, Выборка);
		БазоваяЗапись = БазовыеЗаписи.НайтиСтроки(СтруктураПоиска)[0];

		СтрокаТаблицыПериодов = ФактическиеПериоды.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицыПериодов, БазоваяЗапись);
		ЗаполнитьЗначенияСвойств(СтрокаТаблицыПериодов, Выборка);
	КонецЦикла;	
		
	Возврат ФактическиеПериоды;
КонецФункции	

Процедура УточнитьФактическиПериодыЦеломесячныхЗаписей(ФактическиеПериоды)	
	ОтборПериодов = Новый ТаблицаЗначений();
	ОтборПериодов.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));	
	ОтборПериодов.Колонки.Добавить("ДатаНачала", Новый ОписаниеТипов("Дата"));
	ОтборПериодов.Колонки.Добавить("ДатаОкончания", Новый ОписаниеТипов("Дата"));
	
	ОтборСотрудников = Новый Соответствие();;
	
	Для Каждого Запись Из ФактическиеПериоды Цикл
		Если Запись.НачисляетсяВЦеломЗаМесяц Тогда	
			СтрокаОтбора = ОтборПериодов.Добавить();
			СтрокаОтбора.Сотрудник = Запись.Сотрудник;
			СтрокаОтбора.ДатаНачала = НачалоМесяца(Запись.ПериодДействияНачало);
			СтрокаОтбора.ДатаОкончания = КонецМесяца(СтрокаОтбора.ДатаНачала);	
			ОтборСотрудников.Вставить(Запись.Сотрудник);	
		КонецЕсли;	
	КонецЦикла;	
	
	Если ОтборСотрудников.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;	
	
	Фильтр = ЗарплатаКадрыПериодическиеРегистры.ОписаниеФильтраДляСоздатьВТИмяРегистраПоТаблицеЗначений(
				ОтборПериодов, 
				"Сотрудник");
	
	
	ПараметрыПостроения = ЗарплатаКадрыПериодическиеРегистры.ПараметрыПостроенияДляСоздатьВТИмяРегистра();
	ПараметрыПостроения.ВключатьЗаписиНаНачалоПериода = Истина;
	
	ПрименениеПлановыхНачислений = ЗарплатаКадрыПериодическиеРегистры.ТаблицаВТИмяРегистра(
										"ПрименениеПлановыхНачислений", 
										Новый МенеджерВременныхТаблиц, 
										Ложь, 
										Фильтр);

	
	УточнитьПериодыНачисленийЦеломесячныхПоПрименениюНачислений(ФактическиеПериоды, ПрименениеПлановыхНачислений);
	
	ПериодыРаботы = МенеджерРасчетаЗарплаты.ПериодыРаботыСотрудников(ОбщегоНазначения.ВыгрузитьКолонку(ОтборСотрудников, "Ключ"));
	
	УточнитьПериодыЦеломесячныхНачисленийПоПериодамРаботы(ФактическиеПериоды, ПериодыРаботы);
КонецПроцедуры	

Процедура УточнитьПериодыНачисленийЦеломесячныхПоПрименениюНачислений(ФактическиеПериоды, ПрименениеПлановыхНачислений)
	СтруктураПоискаЗаписей = Новый Структура("Регистратор, НомерСтроки");
	
	ФактическиеПериоды.Колонки.Добавить("Применение", Новый ОписаниеТипов("Булево"));
	ФактическиеПериоды.ЗаполнитьЗначения(Истина, "Применение");
	
	ФактическиеПериоды.Сортировать(
		"Регистратор, НомерСтроки, ПериодДействияНачало, ПериодДействияКонец",
		Новый СравнениеЗначений());
	
	ОбщегоНазначенияБЗК.ДобавитьИндексКоллекции(ПрименениеПлановыхНачислений, "Сотрудник");
		
	СтруктураПоиска = Новый Структура("Сотрудник");
	ПредыдущаяСтрокаПериода = Неопределено;
	ИндексТекущейСтрокиТаблицыПериодов = 0;
	Пока ИндексТекущейСтрокиТаблицыПериодов < ФактическиеПериоды.Количество() Цикл 
		СтрокаПериода = ФактическиеПериоды[ИндексТекущейСтрокиТаблицыПериодов];
		ЗаполнитьЗначенияСвойств(СтруктураПоискаЗаписей, СтрокаПериода);
		
		Если Не СтрокаПериода.НачисляетсяВЦеломЗаМесяц Тогда
			ИндексТекущейСтрокиТаблицыПериодов = ИндексТекущейСтрокиТаблицыПериодов + 1;
			Продолжить;
		КонецЕсли;
			
		Если ПредыдущаяСтрокаПериода = Неопределено
			Или СтрокаПериода.Сотрудник <> ПредыдущаяСтрокаПериода.Сотрудник Тогда
			
			СтруктураПоиска.Сотрудник = СтрокаПериода.Сотрудник;
			ПрименениеНачисленийПоСотруднику = ПрименениеПлановыхНачислений.Скопировать(СтруктураПоиска);
			ПрименениеНачисленийПоСотруднику.Сортировать("Период", Новый СравнениеЗначений());	
			
			ИндексТекущейСтрокиТаблицыПрименения = 0;
		ИначеЕсли СтрокаПериода.Регистратор <> ПредыдущаяСтрокаПериода.Регистратор
			Или СтрокаПериода.НомерСтроки <> ПредыдущаяСтрокаПериода.НомерСтроки Тогда 
		
			ИндексТекущейСтрокиТаблицыПрименения = 0;
		КонецЕсли;			
				
		Пока ИндексТекущейСтрокиТаблицыПрименения <= ПрименениеНачисленийПоСотруднику.Количество() - 1 Цикл
			ТекущаяСтрокаТаблицыПрименения = ПрименениеНачисленийПоСотруднику[ИндексТекущейСтрокиТаблицыПрименения];
			Если ОбщегоНазначенияБЗК.ДатаВИнтервале(ТекущаяСтрокаТаблицыПрименения.Период, СтрокаПериода.ПериодДействияНачало, СтрокаПериода.ПериодДействияКонец)
				И ТекущаяСтрокаТаблицыПрименения.Применение <> СтрокаПериода.Применение  Тогда
				
				Если НачалоДня(ТекущаяСтрокаТаблицыПрименения.Период) = СтрокаПериода.ПериодДействияНачало Тогда
					ТекущаяСтрокаТаблицыПрименения.Применение = ТекущаяСтрокаТаблицыПрименения.Применение;
				Иначе	
					ИсходнаяДатаОкончания = СтрокаПериода.ПериодДействияКонец;
					СтрокаПериода.ПериодДействияКонец = НачалоДня(ТекущаяСтрокаТаблицыПрименения.Период) - 1;
					
					СтрокаНовогоПериода = ОбщегоНазначенияБЗК.ВставитьСтрокуВТаблицу(
											ФактическиеПериоды, 
											ИндексТекущейСтрокиТаблицыПериодов + 1,
											СтрокаПериода);
					
					СтрокаНовогоПериода.ПериодДействияНачало = НачалоДня(ТекущаяСтрокаТаблицыПрименения.Период);
					СтрокаНовогоПериода.ПериодДействияКонец = ИсходнаяДатаОкончания;
					СтрокаНовогоПериода.Применение = ТекущаяСтрокаТаблицыПрименения.Применение;	
				КонецЕсли;		
			ИначеЕсли ТекущаяСтрокаТаблицыПрименения.Период > СтрокаПериода.ПериодДействияКонец Тогда 
				Прервать;
			КонецЕсли;	
			ИндексТекущейСтрокиТаблицыПрименения = ИндексТекущейСтрокиТаблицыПрименения + 1;		
		КонецЦикла;	
		ИндексТекущейСтрокиТаблицыПериодов  = ИндексТекущейСтрокиТаблицыПериодов + 1;
		ПредыдущаяСтрокаПериода = СтрокаПериода;
	КонецЦикла;	
	
	СтрокиКУдалению = ФактическиеПериоды.НайтиСтроки(Новый Структура("Применение", Ложь));
	Для Каждого УдаляемаяСтрока Из СтрокиКУдалению Цикл
		ФактическиеПериоды.Удалить(УдаляемаяСтрока);
	КонецЦикла;	
КонецПроцедуры	
	
Процедура УточнитьПериодыЦеломесячныхНачисленийПоПериодамРаботы(ФактическиеПериоды, ПериодыРаботыСотрудников)
	СтруктураПоискаЗаписей = Новый Структура("Регистратор, НомерСтроки");
	
	ОбщегоНазначенияБЗК.ДобавитьИндексКоллекции(ПериодыРаботыСотрудников, "Сотрудник");
	СтруктураПоиска = Новый Структура("Сотрудник");
	
	Для Каждого СтрокаПериода Из ФактическиеПериоды Цикл
		ЗаполнитьЗначенияСвойств(СтруктураПоискаЗаписей, СтрокаПериода);
		
		Если Не СтрокаПериода.НачисляетсяВЦеломЗаМесяц Тогда
			Продолжить;
		КонецЕсли;	
		
		СтруктураПоиска.Сотрудник = СтрокаПериода.Сотрудник;
		НайденныеПериодыРаботы = ПериодыРаботыСотрудников.НайтиСтроки(СтруктураПоиска);
		Если НайденныеПериодыРаботы.Количество() > 0 Тогда
			СтрокаПериода.ПериодДействияНачало = Макс(СтрокаПериода.ПериодДействияНачало, НайденныеПериодыРаботы[0].ДатаПриема);
			Если ЗначениеЗаполнено(НайденныеПериодыРаботы[0].ДатаЗавершенияРаботы) Тогда
				СтрокаПериода.ПериодДействияКонец = Мин(СтрокаПериода.ПериодДействияКонец, НайденныеПериодыРаботы[0].ДатаЗавершенияРаботы);
			КонецЕсли;
		КонецЕсли;	
	КонецЦикла;	
КонецПроцедуры	

Процедура УточнитьПериодСторноЗаписей(БазовыеЗаписи, ФактическиеПериоды)
	ОтработанныеЗаписи = Новый Соответствие;
	ОбщегоНазначенияБЗК.ДобавитьИндексКоллекции(БазовыеЗаписи, "Регистратор, ГоловнаяОрганизация, Сотрудник, ПериодДействия, ВидРасчета, ДокументОснование");
	ОбщегоНазначенияБЗК.ДобавитьИндексКоллекции(ФактическиеПериоды, "Регистратор, НомерСтроки");
	
	Для Каждого Запись Из БазовыеЗаписи Цикл
		Если Не НеобходимаКорректировкаФПДСторноЗаписи(Запись)
			Или ОтработанныеЗаписи[Запись] = Истина Тогда
			
			Продолжить;
		КонецЕсли;
		УточнитьПериодСторноЗаписи(Запись, БазовыеЗаписи, ФактическиеПериоды, ОтработанныеЗаписи);
	КонецЦикла;	
КонецПроцедуры	

Процедура УточнитьПериодСторноЗаписи(СторноЗапись, БазовыеЗаписи, ФактическиеПериоды, ОтработанныеЗаписи)
	ОтборЗаписей = Новый Структура("Регистратор, ГоловнаяОрганизация, Сотрудник, ПериодДействия, ВидРасчета, ДокументОснование");
	ЗаполнитьЗначенияСвойств(ОтборЗаписей, СторноЗапись);
	ОтборЗаписей.Регистратор = СторноЗапись.СторнируемыйДокумент;
	КандидатыВСторнируемыЗаписи = БазовыеЗаписи.НайтиСтроки(ОтборЗаписей);
	
	СторнируемаяЗапись = Неопределено;
	Для Каждого ЗапистКандидат Из КандидатыВСторнируемыЗаписи Цикл
		Если НачалоДня(СторноЗапись.ПериодДействияНачало) >= НачалоДня(ЗапистКандидат.ПериодДействияНачало)
			И НачалоДня(СторноЗапись.ПериодДействияКонец) <= НачалоДня(ЗапистКандидат.ПериодДействияКонец)
			И СторноЗапись.ПериодРегистрации > ЗапистКандидат.ПериодРегистрации Тогда
			
			СторнируемаяЗапись = ЗапистКандидат;
			Прервать;
		КонецЕсли;
	КонецЦикла;	
	
	Если СторнируемаяЗапись = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Если сторнируемая запись сама является сторно записью то для нее самой нужно скрректировать период
	Если НеобходимаКорректировкаФПДСторноЗаписи(СторнируемаяЗапись) 
		И ОтработанныеЗаписи[СторнируемаяЗапись] <> Истина Тогда
		
		УточнитьПериодСторноЗаписи(СторнируемаяЗапись, БазовыеЗаписи, ФактическиеПериоды, ОтработанныеЗаписи);
	КонецЕсли;	
	
	ОтборФПД = Новый Структура("Регистратор, НомерСтроки");
	ЗаполнитьЗначенияСвойств(ОтборФПД, СторноЗапись);
	УдаляемыйФПД = ФактическиеПериоды.НайтиСтроки(ОтборФПД);
	Для Каждого СтрокаФПД Из УдаляемыйФПД Цикл
		ФактическиеПериоды.Удалить(СтрокаФПД);
	КонецЦикла;	
	
	ЗаполнитьЗначенияСвойств(ОтборФПД, СторнируемаяЗапись);
	НовыйФПД = ФактическиеПериоды.НайтиСтроки(ОтборФПД);
	Для Каждого СтрокаФПД Из НовыйФПД Цикл
		Если ОбщегоНазначенияБЗК.ИнтервалыПересекаются(
			НачалоДня(СторноЗапись.ПериодДействияНачало), НачалоДня(СторноЗапись.ПериодДействияКонец), 
			НачалоДня(СтрокаФПД.ПериодДействияНачало), НачалоДня(СтрокаФПД.ПериодДействияКонец)) Тогда
			
			НоваяЗапись = ФактическиеПериоды.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, СторноЗапись);
			НоваяЗапись.ПериодДействияНачало = Макс(СторноЗапись.ПериодДействияНачало, СтрокаФПД.ПериодДействияНачало);
			НоваяЗапись.ПериодДействияКонец = Мин(СторноЗапись.ПериодДействияКонец, СтрокаФПД.ПериодДействияКонец);
		КонецЕсли;	
	КонецЦикла;	
	
	ОтработанныеЗаписи.Вставить(СторноЗапись, Истина);
КонецПроцедуры	

Функция НеобходимаКорректировкаФПДСторноЗаписи(Запись)
	Если Не Запись.Сторно 
		Или Не ЗначениеЗаполнено(Запись.СторнируемыйДокумент) Тогда
		
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;	
КонецФункции	

Процедура УточнитьНеобходимостьНормированияПоФПД(БазовыеЗаписи, ФактическиеПериоды)
	ОбщегоНазначенияБЗК.ДобавитьИндексКоллекции(ФактическиеПериоды, "Регистратор, НомерСтроки");
	СтруктураПоиска = Новый Структура("Регистратор, НомерСтроки");	
	
	БазовыеЗаписи.Сортировать("Регистратор, НомерСтроки, НеобходимоНормировать", Новый СравнениеЗначений);
	
	ПредыдущаяЗапись = Неопределено;
	УдалятьСтрокиФПД = Ложь;
	ФактическиеПериодыЗаписи = Новый Массив;
	Для Каждого ТекущаяБазоваяЗапись Из БазовыеЗаписи Цикл
		Если Не ТекущаяБазоваяЗапись.НеобходимоНормировать Тогда
			Продолжить;
		КонецЕсли;	
		
		Если ПредыдущаяЗапись = Неопределено 
			Или ТекущаяБазоваяЗапись.Регистратор <> ПредыдущаяЗапись.Регистратор
			Или ТекущаяБазоваяЗапись.НомерСтроки <> ПредыдущаяЗапись.НомерСтроки Тогда
			
			Если УдалятьСтрокиФПД Тогда
				Для Каждого УдаляемаяСтрока Из ФактическиеПериодыЗаписи Цикл
					ФактическиеПериоды.Удалить(УдаляемаяСтрока);
				КонецЦикла;			
			КонецЕсли;	
			
			ЗаполнитьЗначенияСвойств(СтруктураПоиска, ТекущаяБазоваяЗапись);
			ФактическиеПериодыЗаписи = ФактическиеПериоды.НайтиСтроки(СтруктураПоиска);
	
			УдалятьСтрокиФПД = Истина;
		КонецЕсли;	
		
		ТекущаяБазоваяЗапись.НеобходимоНормировать = Ложь;
		Для Каждого ПериодЗаписи Из ФактическиеПериодыЗаписи Цикл
			Если Не ОбщегоНазначенияБЗК.ДатаВИнтервале(ПериодЗаписи.ПериодДействияНачало, ТекущаяБазоваяЗапись.БазовыйПериодОсновнойЗаписиНачало, ТекущаяБазоваяЗапись.БазовыйПериодОсновнойЗаписиКонец) 
				Или Не ОбщегоНазначенияБЗК.ДатаВИнтервале(ПериодЗаписи.ПериодДействияКонец, ТекущаяБазоваяЗапись.БазовыйПериодОсновнойЗаписиНачало, ТекущаяБазоваяЗапись.БазовыйПериодОсновнойЗаписиКонец) Тогда
			
				ТекущаяБазоваяЗапись.НеобходимоНормировать = Истина;
				УдалятьСтрокиФПД = Ложь;
				Прервать;
			КонецЕсли;	
		КонецЦикла;	
		
		ПредыдущаяЗапись = ТекущаяБазоваяЗапись;
	КонецЦикла;	
	
	Если УдалятьСтрокиФПД Тогда
		Для Каждого УдаляемаяСтрока Из ФактическиеПериодыЗаписи Цикл
			ФактическиеПериоды.Удалить(УдаляемаяСтрока);
		КонецЦикла;			
	КонецЕсли;	
КонецПроцедуры		
	
Процедура ЗаполнитьБазовыеЗаписиПоДаннымИБ(БазовыеЗаписи, ОписаниеНабораРассчитываемыхЗаписей, АлгоритмОтбораБазовыхЗаписей, ИсключаемыйРегистратор = Неопределено, ПолучатьРасшифровкуБезусловно = Ложь)	
	Если ОписаниеНабораРассчитываемыхЗаписей.Тип = Тип("ТаблицаЗначений") Тогда
		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
		ЗарплатаКадры.СоздатьВТПоТаблицеЗначений(МенеджерВременныхТаблиц, ОписаниеНабораРассчитываемыхЗаписей.РассчитываемыеЗаписи, "ВТОсновныеЗаписи");	
		ИмяВТРассчитываемыйНабор = "ВТОсновныеЗаписи";
	Иначе
		МенеджерВременныхТаблиц = ОписаниеНабораРассчитываемыхЗаписей.МенеджерВременныхТаблиц;
		ИмяВТРассчитываемыйНабор = ОписаниеНабораРассчитываемыхЗаписей.ИмяВТРассчитываемыеЗаписи;	
	КонецЕсли;		
	
	ЗапросПолученияБазовыхЗаписей = ЗапросПолученияБазовыхЗаписей(ИмяВТРассчитываемыйНабор, АлгоритмОтбораБазовыхЗаписей, ИсключаемыйРегистратор, ПолучатьРасшифровкуБезусловно);
	ЗапросПолученияБазовыхЗаписей.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ВыборкаБазовыхЗаписей = ЗапросПолученияБазовыхЗаписей.Выполнить().Выбрать();	
	
	Пока ВыборкаБазовыхЗаписей.Следующий() Цикл
		СтрокаРезультат = БазовыеЗаписи.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаРезультат, ВыборкаБазовыхЗаписей);
	КонецЦикла;		
КонецПроцедуры	

Процедура ЗаполнитьБазовыеЗаписиНачисленийПоТекущемуНабору(ОписаниеНабораРассчитываемыхЗаписей, БазовыеЗаписи, ТаблицаНабораНачислений, АлгоритмОтбораНачислений, ВидыРасчетаСПодробнойРасшифровкой)
	ДоступныеАлгоритмы = АлгоритмыПолученияБазы();
	
	БазовыеЗаписи.Колонки.Добавить("РассчитыватьПоРазовымНачислениямДокументаОсновнуюЗапись", Новый ОписаниеТипов("Булево"));
	БазовыеЗаписи.Колонки.Добавить("РегистраторРазовогоНачисленияОсновнойЗаписи", Метаданные.ОпределяемыеТипы.РегистраторРазовогоНачисления.Тип);
	
	ПоляПоискаСуществующихЗаписей = "Регистратор, НомерСтроки, 
									|БазовыйПериодОсновнойЗаписиНачало, БазовыйПериодОсновнойЗаписиКонец, 
									|ОсновнаяЗаписьРассчитыватьПоРазовымНачислениямДокумента, ОсновнаяЗаписьРегистраторРазовогоНачисления";
	
	БазовыеЗаписи.Индексы.Добавить(ПоляПоискаСуществующихЗаписей);
	ОтборСуществующихЗаписей = Новый Структура(ПоляПоискаСуществующихЗаписей);
	
	СвойстваНачисленийНабора = СвойстваНачисленийТекущегоНабора(ТаблицаНабораНачислений, ДоступныеАлгоритмы);
	
	ЗависимостиВидовРасчета = ЗависимостиВидовРасчетаРассчитываемогоНабора();
	
	ПоляПоиска = ОбщегоНазначения.ВыгрузитьКолонку(БазовыеИзмерения(АлгоритмОтбораНачислений), "Ключ");
	ОбщегоНазначенияБЗК.ДобавитьИндексКоллекции(ТаблицаНабораНачислений, СтрСоединить(ПоляПоиска, ","));
	СтруктураПоиска = Новый Структура(СтрСоединить(ПоляПоиска, ","));
		
	Для Каждого СтрокаРассчитываемогоНабора Из РассчитываемыйНабор Цикл
		БазовыеВидыРасчетаТекущейСтроки = ЗависимостиВидовРасчета[СтрокаРассчитываемогоНабора.ВидРасчета]; 
		Если БазовыеВидыРасчетаТекущейСтроки = Неопределено Тогда
			Продолжить;
		КонецЕсли;	
		ЗаполнитьОтборБазовыхЗаписей(АлгоритмОтбораНачислений, СтруктураПоиска, СтрокаРассчитываемогоНабора, ДоступныеАлгоритмы);
		ЗаписиТекущегоНабора = ТаблицаНабораНачислений.НайтиСтроки(СтруктураПоиска);
			
		Для Каждого СтрокаТекущегоНабора Из ЗаписиТекущегоНабора Цикл
			ЗаполнитьЗначенияСвойств(ОтборСуществующихЗаписей, СтрокаТекущегоНабора, "Регистратор, НомерСтроки");
			ОтборСуществующихЗаписей.БазовыйПериодОсновнойЗаписиНачало = СтрокаРассчитываемогоНабора.БазовыйПериодНачало;
			ОтборСуществующихЗаписей.БазовыйПериодОсновнойЗаписиКонец = СтрокаРассчитываемогоНабора.БазовыйПериодКонец;
			ОтборСуществующихЗаписей.ОсновнаяЗаписьРассчитыватьПоРазовымНачислениямДокумента = СтрокаРассчитываемогоНабора.РассчитыватьПоРазовымНачислениямДокумента;
			Если СтрокаРассчитываемогоНабора.РассчитыватьПоРазовымНачислениямДокумента Тогда
				ОтборСуществующихЗаписей.ОсновнаяЗаписьРегистраторРазовогоНачисления = СтрокаРассчитываемогоНабора.РегистраторРазовогоНачисления;
			Иначе
				ОтборСуществующихЗаписей.ОсновнаяЗаписьРегистраторРазовогоНачисления = Неопределено;
			КонецЕсли;	
			
			Если АлгоритмРасчетаБазы <> ДоступныеАлгоритмы.БазаРезервов Тогда
				Если Не ЗаписьСоответствуетОтборуБазы(АлгоритмОтбораНачислений, СтрокаРассчитываемогоНабора, СтрокаТекущегоНабора, БазовыеВидыРасчетаТекущейСтроки, ДоступныеАлгоритмы) 
					Или БазовыеЗаписи.НайтиСтроки(ОтборСуществующихЗаписей).Количество() <> 0 Тогда	
					Продолжить;
				КонецЕсли;
			КонецЕсли;
						
			СвойстваБазовогоВР = СвойстваНачисленийНабора[СтрокаТекущегоНабора.ВидРасчета];	
						
			НоваяЗапись = БазовыеЗаписи.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, СтрокаТекущегоНабора);
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ОтборСуществующихЗаписей);
			НоваяЗапись.Результат = СтрокаТекущегоНабора.Результат;
			Если АлгоритмОтбораНачислений = ДоступныеАлгоритмы.БазаНачислений
				Или АлгоритмОтбораНачислений = ДоступныеАлгоритмы.БазаРезервов Тогда
				НоваяЗапись.Результат = НоваяЗапись.Результат + СтрокаТекущегоНабора.РезультатФСС;
			КонецЕсли;
			Если АлгоритмОтбораНачислений = ДоступныеАлгоритмы.БазаРезервов Тогда
				НоваяЗапись.ВидРасчета = СтрокаТекущегоНабора.ВидРасчетаДанных;
			КонецЕсли;
			НоваяЗапись.НеобходимоНормировать = НеобходимоНормироватьБазовуюЗапись(АлгоритмОтбораНачислений, СтрокаРассчитываемогоНабора, СтрокаТекущегоНабора, ДоступныеАлгоритмы);
			НоваяЗапись.ЗаписьТекущегоНабора = Истина;
			
			НоваяЗапись.НормироватьКаскадно = НормироватьЗаписьКаскадно(СтрокаТекущегоНабора, СтрокаРассчитываемогоНабора, СвойстваБазовогоВР);
			НоваяЗапись.ОчередностьРасчета = СвойстваБазовогоВР.ОчередностьРасчета;
			НоваяЗапись.НачисляетсяВЦеломЗаМесяц = СвойстваБазовогоВР.НачисляетсяВЦеломЗаМесяц;
			НоваяЗапись.НеобходимФПДПриНормировании = НеобходимФПДБазовойЗаписиДляНормирования(
				СтрокаТекущегоНабора, 
				СтрокаРассчитываемогоНабора, 
				НоваяЗапись.НормироватьКаскадно, 
				СвойстваНачисленийНабора[СтрокаТекущегоНабора.ВидРасчета]);
		КонецЦикла;				
	КонецЦикла;							
КонецПроцедуры	

Функция НормироватьЗаписьКаскадно(БазоваяЗапись, РассчитываемаяЗапись, СвойстваБазовогоВидаРасчета)
	Если СвойстваБазовогоВидаРасчета.ТребуетсяРасчетБазы
		И БазоваяЗапись.ПериодДействия = НачалоМесяца(БазоваяЗапись.БазовыйПериодНачало)
		И БазоваяЗапись.ПериодДействия = НачалоМесяца(БазоваяЗапись.БазовыйПериодКонец) Тогда
			
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;	
КонецФункции	

Функция НеобходимФПДБазовойЗаписиДляНормирования(БазоваяЗапись, РассчитываемаяЗапись, НормироватьКаскадно, СвойстваБазовогоВидаРасчета)
	Если БазоваяЗапись.ВремяВЦеломЗаПериод Тогда 
		Возврат Ложь;
	ИначеЕсли НормироватьКаскадно 
		И СвойстваБазовогоВидаРасчета.ПериодДействияБазовый 
		И СвойстваБазовогоВидаРасчета.ЕстьВытесняющиеНачисления Тогда
			
		Возврат Истина;
	ИначеЕсли Не НормироватьКаскадно
		И СвойстваБазовогоВидаРасчета.ЕстьВытесняющиеНачисления Тогда
	 	
 	 	Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
КонецФункции	

Функция ЗависимостиВидовРасчета(ИмяТаблицыЗависимыеВидыРасчета, ОтборЗависимыхВидовРасчета = Неопределено, ОтборВедущихВидовРасчета = Неопределено)			
	
	Если АлгоритмРасчетаБазы <> АлгоритмыПолученияБазы().БазаРезервов Тогда
		ЗависимостиНачислений = ЗависимостиВидовРасчетаНачисленийУдержаний(ИмяТаблицыЗависимыеВидыРасчета, ОтборЗависимыхВидовРасчета, ОтборВедущихВидовРасчета);
	Иначе
		ЗависимостиНачислений = РезервыПоОплатеТрудаРасширенный.ЗависимостиВидовРасчета(ИмяТаблицыЗависимыеВидыРасчета, ОтборЗависимыхВидовРасчета, ОтборВедущихВидовРасчета);
	КонецЕсли;
	
	Возврат ЗависимостиНачислений;
	
КонецФункции

Функция АлгоритмыПолученияБазы()
	АлгоритмыОтбораБазовыхЗаписей = Новый Структура();
	АлгоритмыОтбораБазовыхЗаписей.Вставить("БазаНачислений", 1);
	АлгоритмыОтбораБазовыхЗаписей.Вставить("БазаУдержаний",  2);
	АлгоритмыОтбораБазовыхЗаписей.Вставить("БазаРезервов",   3);
	
	Возврат АлгоритмыОтбораБазовыхЗаписей;		
КонецФункции	

Функция БазовыеИзмерения(АлгоритмПолученияБазы)
	Алгоритмы = АлгоритмыПолученияБазы();
	
	Если АлгоритмПолученияБазы = Алгоритмы.БазаНачислений
		Или АлгоритмПолученияБазы = Алгоритмы.БазаРезервов Тогда
		Возврат БазовыеИзмеренияНачислений();
	ИначеЕсли АлгоритмПолученияБазы = Алгоритмы.БазаУдержаний Тогда 
		Возврат БазовыеИзмеренияУдержаний();
	Иначе
		ВызватьИсключениеНеизвестныйАлгоритмПолученияБазы();
	КонецЕсли;	
КонецФункции

Функция БазовыеИзмеренияНачислений()
	БазовыеИзмерения = Новый Соответствие();	
	БазовыеИзмерения.Вставить("Сотрудник", "Сотрудник");
	
	Возврат БазовыеИзмерения;
КонецФункции

Функция БазовыеИзмеренияУдержаний()
	БазовыеИзмерения = Новый Соответствие();
	БазовыеИзмерения.Вставить("ФизическоеЛицо", "ФизическоеЛицо");
	БазовыеИзмерения.Вставить("ГоловнаяОрганизация", "Организация");
	
	Возврат БазовыеИзмерения;
КонецФункции			

Функция ВыраженияОтбораБазовыхЗаписейПоПериоду(АлгоритмПолученияБазы)
	Алгоритмы = АлгоритмыПолученияБазы();
	
	Если АлгоритмПолученияБазы = Алгоритмы.БазаНачислений
		Или АлгоритмПолученияБазы = Алгоритмы.БазаРезервов Тогда
		Возврат ВыраженияОтбораБазовыхЗаписейНачисленийПоПериоду();
	ИначеЕсли АлгоритмПолученияБазы = Алгоритмы.БазаУдержаний Тогда 
		Возврат ВыраженияОтбораБазовыхЗаписейУдержанийПоПериоду();
	Иначе
		ВызватьИсключениеНеизвестныйАлгоритмПолученияБазы();
	КонецЕсли;	
КонецФункции	

Функция ВыраженияОтбораБазовыхЗаписейНачисленийПоПериоду()
	ВыраженияУсловий = Новый Массив();
	
	// Условие на период регистрации.
	ТекстУсловия = "БазовыеЗаписи.ПериодРегистрации <= ОсновныеЗаписи.ПериодРегистрации
					|	ИЛИ БазовыеЗаписи.ПериодРегистрации <= ОсновныеЗаписи.БазовыйПериодКонец";							
	ВыраженияУсловий.Добавить(ТекстУсловия);
	
	ТекстУсловия = "БазовыеЗаписи.ПериодДействия >= НАЧАЛОПЕРИОДА(ОсновныеЗаписи.БазовыйПериодНачало, МЕСЯЦ)";				
	ВыраженияУсловий.Добавить(ТекстУсловия);	
	ТекстУсловия = "БазовыеЗаписи.ПериодДействия <= ОсновныеЗаписи.БазовыйПериодКонец";			
	ВыраженияУсловий.Добавить(ТекстУсловия);
	
	// условие на конкретные даты базовых начислений	
	ТекстУсловия = "ОсновныеЗаписи.БазовыйПериодНачало МЕЖДУ БазовыеЗаписи.ПериодДействияНачало И БазовыеЗаписи.ПериодДействияКонец
					|	ИЛИ БазовыеЗаписи.ПериодДействияНачало МЕЖДУ ОсновныеЗаписи.БазовыйПериодНачало И ОсновныеЗаписи.БазовыйПериодКонец";				
	ВыраженияУсловий.Добавить(ТекстУсловия);
	
	Возврат ВыраженияУсловий;	
КонецФункции

Функция ВыраженияОтбораБазовыхЗаписейУдержанийПоПериоду()
	ВыраженияУсловий = Новый Массив();
	
	ТекстУсловия = "БазовыеЗаписи.ПериодРегистрации = ОсновныеЗаписи.МесяцУдержания";						
	ВыраженияУсловий.Добавить(ТекстУсловия);
	
	ТекстУсловия = "ВЫБОР
					|	КОГДА ОсновныеЗаписи.Сотрудник <> ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка)
					|		ТОГДА ОсновныеЗаписи.Сотрудник = БазовыеЗаписи.Сотрудник
					|	ИНАЧЕ ИСТИНА
					|КОНЕЦ";						
	ВыраженияУсловий.Добавить(ТекстУсловия);
	
	Возврат ВыраженияУсловий;
КонецФункции

Функция ЗаписьСоответствуетОтборуБазы(АлгоритмПолученияБазы, РассчитываемаяЗапись, БазоваяЗапись, ЗависимостиВидовРасчета, ДоступныеАлгоритмы)
	Если АлгоритмПолученияБазы = ДоступныеАлгоритмы.БазаНачислений
		Или АлгоритмПолученияБазы = ДоступныеАлгоритмы.БазаРезервов Тогда
		Возврат ЗаписьСоответствуетОтборуБазыНачислений(РассчитываемаяЗапись, БазоваяЗапись, ЗависимостиВидовРасчета);
	ИначеЕсли АлгоритмПолученияБазы = ДоступныеАлгоритмы.БазаУдержаний Тогда
	 	Возврат ЗаписьСоответствуетОтборуБазыУдержаний(РассчитываемаяЗапись, БазоваяЗапись, ЗависимостиВидовРасчета);
	Иначе
		ВызватьИсключениеНеизвестныйАлгоритмПолученияБазы();
	КонецЕсли;		
КонецФункции

Функция ЗаписьСоответствуетОтборуБазыНачислений(РассчитываемаяЗапись, БазоваяЗапись, ЗависимостиВидовРасчета)
	Если ЗависимостиВидовРасчета[БазоваяЗапись.ВидРасчета] = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;	
	
	Если Не (БазоваяЗапись.ПериодРегистрации <= РассчитываемаяЗапись.ПериодРегистрации
		Или БазоваяЗапись.ПериодРегистрации <= РассчитываемаяЗапись.БазовыйПериодКонец) Тогда
			
		Возврат Ложь;
	КонецЕсли;	
	
	Если Не (ОбщегоНазначенияБЗК.ДатаВИнтервале(РассчитываемаяЗапись.БазовыйПериодНачало, БазоваяЗапись.ПериодДействияНачало, БазоваяЗапись.ПериодДействияКонец)
		Или ОбщегоНазначенияБЗК.ДатаВИнтервале(БазоваяЗапись.ПериодДействияНачало, РассчитываемаяЗапись.БазовыйПериодНачало, РассчитываемаяЗапись.БазовыйПериодКонец)) Тогда
			
		Возврат Ложь;
	КонецЕсли;	
	
	Если Не ЗаписьСоответствуетОтборуРазовыхНачисленийРегистратору(РассчитываемаяЗапись, БазоваяЗапись) Тогда
		Возврат Ложь;
	КонецЕсли;	
	
	Возврат Истина;			
КонецФункции

Функция ЗаписьСоответствуетОтборуБазыУдержаний(РассчитываемаяЗапись, БазоваяЗапись, ЗависимостиВидовРасчета)
	Если ЗависимостиВидовРасчета[БазоваяЗапись.ВидРасчета] = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(БазоваяЗапись.СторноТекущегоПериода) И РасчетПоДаннымТекущегоНабора И Не МенеджерРасчетаЗарплаты.РазрешеныДоначисленияВТекущемПериоде Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если БазоваяЗапись.ПериодРегистрации <> РассчитываемаяЗапись.МесяцУдержания Тогда
		Возврат Ложь;
	КонецЕсли;
		
	Если ЗначениеЗаполнено(РассчитываемаяЗапись.Сотрудник)
		И РассчитываемаяЗапись.Сотрудник <> БазоваяЗапись.Сотрудник Тогда
			
		Возврат Ложь;
	КонецЕсли;
	
	Если Не  ЗаписьСоответствуетОтборуРазовыхНачисленийРегистратору(РассчитываемаяЗапись, БазоваяЗапись) Тогда
		Возврат Ложь;
	КонецЕсли;	
	
	Возврат Истина;	
КонецФункции	

Функция ЗаписьСоответствуетОтборуРазовыхНачисленийРегистратору(РассчитываемаяЗапись, БазоваяЗапись)
	Если Не РассчитываемаяЗапись.РассчитыватьПоРазовымНачислениямДокумента Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если РассчитываемаяЗапись.РегистраторРазовогоНачисления = БазоваяЗапись.РегистраторРазовогоНачисления Тогда
		Возврат Истина;
	КонецЕсли;
		
	Возврат Ложь;
КонецФункции

Функция ВыражениеПоляЗапросаНеобходимоНормировать(АлгоритмПолученияБазы)
	Алгоритмы = АлгоритмыПолученияБазы();
	
	Если АлгоритмПолученияБазы = Алгоритмы.БазаНачислений
		Или АлгоритмПолученияБазы = Алгоритмы.БазаРезервов Тогда
		Возврат ВыражениеПоляЗапросаНеобходимоНормироватьБазуНачислений();
	ИначеЕсли АлгоритмПолученияБазы = Алгоритмы.БазаУдержаний Тогда 
		Возврат ВыражениеПоляЗапросаНеобходимоНормироватьБазуУдержаний();
	Иначе
		ВызватьИсключениеНеизвестныйАлгоритмПолученияБазы();
	КонецЕсли;		
КонецФункции	

Функция ВыражениеПоляЗапросаНеобходимоНормироватьБазуНачислений()
	ВыражениеПоляНеобходимоНормировать = 
	"	ВЫБОР
	|		КОГДА НЕ(БазовыеЗаписи.ПериодДействияНачало МЕЖДУ ОсновныеЗаписи.БазовыйПериодНачало И ОсновныеЗаписи.БазовыйПериодКонец)
	|				ИЛИ НЕ(БазовыеЗаписи.ПериодДействияКонец МЕЖДУ ОсновныеЗаписи.БазовыйПериодНачало И ОсновныеЗаписи.БазовыйПериодКонец)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ";
	
	Возврат ВыражениеПоляНеобходимоНормировать;
КонецФункции

Функция ВыражениеПоляЗапросаНеобходимоНормироватьБазуУдержаний()
	ВыражениеПоляНеобходимоНормировать = 
	"	ВЫБОР 
	|		КОГДА (ОсновныеЗаписи.БазовыйПериодНачало МЕЖДУ БазовыеЗаписи.ПериодДействияНачало И БазовыеЗаписи.ПериодДействияКонец
	|			ИЛИ БазовыеЗаписи.ПериодДействияНачало МЕЖДУ ОсновныеЗаписи.БазовыйПериодНачало И ОсновныеЗаписи.БазовыйПериодКонец)
	|		 И (НЕ (БазовыеЗаписи.ПериодДействияНачало МЕЖДУ ОсновныеЗаписи.БазовыйПериодНачало И ОсновныеЗаписи.БазовыйПериодКонец)
	|				ИЛИ НЕ (БазовыеЗаписи.ПериодДействияКонец МЕЖДУ ОсновныеЗаписи.БазовыйПериодНачало И ОсновныеЗаписи.БазовыйПериодКонец))
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ";
	
	Возврат ВыражениеПоляНеобходимоНормировать;
КонецФункции

Функция ВыражениеПоляЗапросаРезультат(АлгоритмПолученияБазы)
	Алгоритмы = АлгоритмыПолученияБазы();
	
	Если АлгоритмПолученияБазы = Алгоритмы.БазаНачислений
		Или АлгоритмПолученияБазы = Алгоритмы.БазаРезервов Тогда
		Возврат ВыражениеПоляЗапросаРезультатНачислений();
	ИначеЕсли АлгоритмПолученияБазы = Алгоритмы.БазаУдержаний Тогда 
		Возврат ВыражениеПоляЗапросаРезультатУдержаний();
	Иначе
		ВызватьИсключениеНеизвестныйАлгоритмПолученияБазы();
	КонецЕсли;		
КонецФункции	

Функция ВыражениеПоляЗапросаРезультатНачислений()
	ВыражениеПоляРезультат = 
	"	БазовыеЗаписи.Результат + БазовыеЗаписи.РезультатФСС";
	
	Возврат ВыражениеПоляРезультат;
КонецФункции

Функция ВыражениеПоляЗапросаРезультатУдержаний()
	ВыражениеПоляРезультат = 
	"	БазовыеЗаписи.Результат";
	
	Возврат ВыражениеПоляРезультат;
КонецФункции

Функция НеобходимоНормироватьБазовуюЗапись(АлгоритмПолученияБазы, РассчитываемаяЗапись, БазоваяЗапись, ДоступныеАлгоритмы)
	Если АлгоритмПолученияБазы = ДоступныеАлгоритмы.БазаНачислений
		Или АлгоритмПолученияБазы = ДоступныеАлгоритмы.БазаРезервов Тогда
		Возврат НеобходимоНормироватьБазовуюЗаписьНачислений(РассчитываемаяЗапись, БазоваяЗапись);
	ИначеЕсли АлгоритмПолученияБазы = ДоступныеАлгоритмы.БазаУдержаний Тогда 
		Возврат НеобходимоНормироватьБазовуюЗаписьУдержаний(РассчитываемаяЗапись, БазоваяЗапись);
	Иначе
		ВызватьИсключениеНеизвестныйАлгоритмПолученияБазы();
	КонецЕсли;		
КонецФункции	

Функция НеобходимоНормироватьБазовуюЗаписьНачислений(РассчитываемаяЗапись, БазоваяЗапись)
	Если ОбщегоНазначенияБЗК.ДатаВИнтервале(БазоваяЗапись.ПериодДействияНачало, РассчитываемаяЗапись.БазовыйПериодНачало, РассчитываемаяЗапись.БазовыйПериодКонец)
		И ОбщегоНазначенияБЗК.ДатаВИнтервале(БазоваяЗапись.ПериодДействияКонец, РассчитываемаяЗапись.БазовыйПериодНачало, РассчитываемаяЗапись.БазовыйПериодКонец) Тогда
			
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;	
КонецФункции

Функция НеобходимоНормироватьБазовуюЗаписьУдержаний(РассчитываемаяЗапись, БазоваяЗапись)
	
	Если ОбщегоНазначенияБЗК.ДатаВИнтервале(РассчитываемаяЗапись.БазовыйПериодНачало, БазоваяЗапись.ПериодДействияНачало, БазоваяЗапись.ПериодДействияКонец, Ложь)
		И ОбщегоНазначенияБЗК.ДатаВИнтервале(РассчитываемаяЗапись.БазовыйПериодКонец, БазоваяЗапись.ПериодДействияНачало, БазоваяЗапись.ПериодДействияКонец, Ложь) Тогда
		
		Возврат Истина;
	КонецЕсли;
	
	Если ОбщегоНазначенияБЗК.ДатаВИнтервале(БазоваяЗапись.ПериодДействияНачало, РассчитываемаяЗапись.БазовыйПериодНачало, РассчитываемаяЗапись.БазовыйПериодКонец)
		И Не ОбщегоНазначенияБЗК.ДатаВИнтервале(БазоваяЗапись.ПериодДействияКонец, РассчитываемаяЗапись.БазовыйПериодНачало, РассчитываемаяЗапись.БазовыйПериодКонец) Тогда
		
		Возврат Истина;
	КонецЕсли;
		
	Если ОбщегоНазначенияБЗК.ДатаВИнтервале(БазоваяЗапись.ПериодДействияКонец, РассчитываемаяЗапись.БазовыйПериодНачало, РассчитываемаяЗапись.БазовыйПериодКонец)
		И Не ОбщегоНазначенияБЗК.ДатаВИнтервале(БазоваяЗапись.ПериодДействияНачало, РассчитываемаяЗапись.БазовыйПериодНачало, РассчитываемаяЗапись.БазовыйПериодКонец) Тогда
		
		Возврат Истина;
	КонецЕсли;	 
		
	Возврат Ложь;
КонецФункции	 

Функция ТаблицаБазовыхВидовРасчета(АлгоритмПолученияБазы)
	Алгоритмы = АлгоритмыПолученияБазы();
	
	Если АлгоритмПолученияБазы = Алгоритмы.БазаНачислений
		Или АлгоритмПолученияБазы = Алгоритмы.БазаРезервов Тогда
		Возврат ТаблицаБазовыхВидовРасчетаНачислений();
	ИначеЕсли АлгоритмПолученияБазы = Алгоритмы.БазаУдержаний Тогда 
		Возврат ТаблицаБазовыхВидовРасчетаУдержаний();
	Иначе
		ВызватьИсключениеНеизвестныйАлгоритмПолученияБазы();
	КонецЕсли;		
КонецФункции	

Функция ТаблицаБазовыхВидовРасчетаНачислений()	
	Возврат "ПланВидовРасчета.Начисления.БазовыеВидыРасчета";
КонецФункции

Функция ТаблицаБазовыхВидовРасчетаУдержаний()
	Возврат "ПланВидовРасчета.Удержания.БазовыеВидыРасчета";
КонецФункции

Процедура ЗаполнитьОтборБазовыхЗаписей(АлгоритмПолученияБазы, СтруктураОтбора, РассчитываемаяСтрока, ДоступныеАлгоритмы)
	Если АлгоритмПолученияБазы = ДоступныеАлгоритмы.БазаНачислений
		Или АлгоритмПолученияБазы = ДоступныеАлгоритмы.БазаРезервов Тогда
		ЗаполнитьОтборБазовыхЗаписейНачислений(СтруктураОтбора, РассчитываемаяСтрока);
	ИначеЕсли АлгоритмПолученияБазы = ДоступныеАлгоритмы.БазаУдержаний Тогда 	
		ЗаполнитьОтборБазовыхЗаписейУдержаний(СтруктураОтбора, РассчитываемаяСтрока);
	Иначе
		ВызватьИсключениеНеизвестныйАлгоритмПолученияБазы();	
	КонецЕсли;	
КонецПроцедуры

Процедура ЗаполнитьОтборБазовыхЗаписейНачислений(СтруктураОтбора, РассчитываемаяСтрока)
	СтруктураОтбора.Сотрудник = РассчитываемаяСтрока.Сотрудник;
КонецПроцедуры	

Процедура ЗаполнитьОтборБазовыхЗаписейУдержаний(СтруктураОтбора, РассчитываемаяСтрока)
	СтруктураОтбора.ФизическоеЛицо = РассчитываемаяСтрока.ФизическоеЛицо;
	СтруктураОтбора.ГоловнаяОрганизация = РассчитываемаяСтрока.Организация;
КонецПроцедуры	

Процедура ВызватьИсключениеНеизвестныйАлгоритмПолученияБазы()
	ТекстИсключения = НСтр("ru = 'Не известный алгоритм получения расчетной базы.';
							|en = 'Unknown algorithm of the accounting base receipt.'");
	ВызватьИсключение ТекстИсключения;	
КонецПроцедуры	

Функция ДанныеВедущихЗаписейКаскадногоНормирования(ДанныеБазовыхНачислений, ДанныеТекущегоНабораНачислений = Неопределено, ИсключаемыйРегистратор = Неопределено)	
	ОтборНормируемыхЗаписей = Новый Структура("НеобходимоНормировать, НормироватьКаскадно", Истина, Истина);
	
	ОтборВедущихЗаписей = ДанныеБазовыхНачислений.Записи.Скопировать(ОтборНормируемыхЗаписей, "Сотрудник, ПериодРегистрации, ПериодДействия, ПериодДействияКонец, ВидРасчета");
	ОтборВедущихЗаписей.Свернуть("Сотрудник, ПериодРегистрации, ПериодДействия, ПериодДействияКонец, ВидРасчета");	
		
	ВедущиеЗаписиКаскадногоНормирования = ВедущиеЗаписиКаскадногоНормирования(ОтборВедущихЗаписей, ДанныеТекущегоНабораНачислений, ИсключаемыйРегистратор);
	ФактическиеПериодыВедущихЗаписей = ФактическиеПериодыНормируемыхЗаписей(ВедущиеЗаписиКаскадногоНормирования, ДанныеТекущегоНабораНачислений);	
	
	РазбитьПериодыВедущихНачислений(ФактическиеПериодыВедущихЗаписей, ДанныеБазовыхНачислений.ФактическиеПериоды); 

	Результат = Новый Структура("Записи, ФактическиеПериоды");
	Результат.Записи =  ВедущиеЗаписиКаскадногоНормирования;
	Результат.ФактическиеПериоды = ФактическиеПериодыВедущихЗаписей;
	
	Возврат Результат;
КонецФункции

Процедура РазбитьПериодыВедущихНачислений(ПериодыВедущихНачислений, ПериодыНачисленийНормируемыхКаскадно)
	ОбщегоНазначенияБЗК.ДобавитьИндексКоллекции(ПериодыНачисленийНормируемыхКаскадно, "Сотрудник, ПериодДействия");
	ОбщегоНазначенияБЗК.ДобавитьИндексКоллекции(ПериодыНачисленийНормируемыхКаскадно, "ВидРасчета, НормироватьКаскадно");
	
	ОбщегоНазначенияБЗК.ДобавитьИндексКоллекции(ПериодыВедущихНачислений, "Сотрудник, ПериодДействия");
	ОбщегоНазначенияБЗК.ДобавитьИндексКоллекции(ПериодыВедущихНачислений, "ВидРасчета");
	
	ОбщегоНазначенияБЗК.ДобавитьИндексКоллекции(ПериодыВедущихНачислений, "Регистратор, НомерСтроки");
	ОбщегоНазначенияБЗК.ДобавитьИндексКоллекции(ПериодыНачисленийНормируемыхКаскадно, "Регистратор, НомерСтроки");
		
	ОтборЗависимыхВидовРасчета = ПериодыНачисленийНормируемыхКаскадно.ВыгрузитьКолонку("ВидРасчета");
	ОтборЗависимыхВидовРасчета = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ОтборЗависимыхВидовРасчета);	
	
	ОтборВедущихВидовРасчета = ПериодыВедущихНачислений.ВыгрузитьКолонку("ВидРасчета");
	ОтборВедущихВидовРасчета = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ОтборВедущихВидовРасчета);
		
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ОтборЗависимыхВидовРасчета, ОтборВедущихВидовРасчета);
	
	Зависимости = ЗависимостиВидовРасчета("ПланВидовРасчета.Начисления.ВедущиеВидыРасчета", ОтборЗависимыхВидовРасчета, ОтборВедущихВидовРасчета);
		
	ДатыРазбиенияНачислений = Новый ТаблицаЗначений;
	ДатыРазбиенияНачислений.Колонки.Добавить("Регистратор");
	ДатыРазбиенияНачислений.Колонки.Добавить("НомерСтроки");
	ДатыРазбиенияНачислений.Колонки.Добавить("Дата");
	
	СтруктураПоискаПериодов = Новый Структура("Регистратор, НомерСтроки");
	
	ОтборСтрокНормируемыхЗаписей = Новый Структура("ВидРасчета, НормироватьКаскадно");
	ОтборСтрокНормируемыхЗаписей.НормироватьКаскадно = Истина;
	ОтборСтрокВедущихЗаписей = Новый Структура("ВидРасчета");
	Для Каждого ЗависимостиТекущегоВидаРасчета Из Зависимости Цикл
		ОтборСтрокНормируемыхЗаписей.ВидРасчета = ЗависимостиТекущегоВидаРасчета.Ключ;
		ОтборСтрокВедущихЗаписей.ВидРасчета = ЗависимостиТекущегоВидаРасчета.Ключ;
		
		// заполним даты изменения Зависимых записей
		ДатыЗависимыхНачислений = Новый ТаблицаЗначений;
		ДатыЗависимыхНачислений.Колонки.Добавить("Дата");
		ДатыЗависимыхНачислений.Колонки.Добавить("Сотрудник");
		ДатыЗависимыхНачислений.Колонки.Добавить("ПериодДействия");

		НормируемыеЗаписи = ПериодыНачисленийНормируемыхКаскадно.НайтиСтроки(ОтборСтрокНормируемыхЗаписей);
		Для Каждого ЗаписьНачисления Из НормируемыеЗаписи Цикл
			ЗаполнитьЗначенияСвойств(СтруктураПоискаПериодов, ЗаписьНачисления);
			ДобавитьСтрокиТаблицыДатЗависимыхЗаписей(ДатыЗависимыхНачислений, ЗаписьНачисления);	
		КонецЦикла;	
		
		ЗависимыеЗаписи = ПериодыВедущихНачислений.НайтиСтроки(ОтборСтрокВедущихЗаписей);
		Для Каждого ЗаписьНачисления Из ЗависимыеЗаписи Цикл
			ДобавитьСтрокиТаблицыДатЗависимыхЗаписей(ДатыЗависимыхНачислений, ЗаписьНачисления);		
		КонецЦикла;	
		
		// теперь определим даты разбиения ведущих начислений
		ДатыЗависимыхНачислений.Свернуть("Дата, Сотрудник, ПериодДействия");
		ДатыЗависимыхНачислений.Индексы.Добавить("Сотрудник, ПериодДействия");
		
		ЗаполнитьДатыРазбиенияВедущихНачислений(ДатыРазбиенияНачислений, ПериодыВедущихНачислений, ДатыЗависимыхНачислений, ЗависимостиТекущегоВидаРасчета.Значение);
	КонецЦикла;	
	
	ДатыРазбиенияНачислений.Свернуть("Регистратор, НомерСтроки, Дата");
	ДатыРазбиенияНачислений.Индексы.Добавить("Регистратор, НомерСтроки");
	
	РазбитьПериодыНачислений(ПериодыВедущихНачислений, ДатыРазбиенияНачислений);	
КонецПроцедуры	

Процедура ДобавитьСтрокиТаблицыДатЗависимыхЗаписей(ДатыВедущихЗаписей, ВедущаяЗапись)
	СтрокаТаблицыДатВедущихНачислений = ДатыВедущихЗаписей.Добавить();
	СтрокаТаблицыДатВедущихНачислений.Сотрудник = ВедущаяЗапись.Сотрудник;
	СтрокаТаблицыДатВедущихНачислений.ПериодДействия = ВедущаяЗапись.ПериодДействия;
	СтрокаТаблицыДатВедущихНачислений.Дата = ВедущаяЗапись.ПериодДействияНачало;
	
	СтрокаТаблицыДатВедущихНачислений = ДатыВедущихЗаписей.Добавить();
	СтрокаТаблицыДатВедущихНачислений.Сотрудник = ВедущаяЗапись.Сотрудник;
	СтрокаТаблицыДатВедущихНачислений.ПериодДействия = ВедущаяЗапись.ПериодДействия;
	СтрокаТаблицыДатВедущихНачислений.Дата = КонецДня(ВедущаяЗапись.ПериодДействияКонец) + 1;	
КонецПроцедуры

Процедура ЗаполнитьДатыРазбиенияВедущихНачислений(ДатыРазбиенияВедущихНачислений, ЗаписиВедущихНачислений, ДатыЗависимыхНачислений, ВедущиеВидыРасчета)
	ОтборСтрокТаблицыРазбиения = Новый Структура("Сотрудник, ПериодДействия");
	Для Каждого ОписаниеВедущегоВидаРасчета Из ВедущиеВидыРасчета Цикл 
		ВедущиеЗаписи = ЗаписиВедущихНачислений.НайтиСтроки(Новый Структура("ВидРасчета", ОписаниеВедущегоВидаРасчета.Ключ));
		Для Каждого ТекущаяЗаписьНачислений Из ВедущиеЗаписи Цикл
			ЗаполнитьЗначенияСвойств(ОтборСтрокТаблицыРазбиения, ТекущаяЗаписьНачислений);
			ДатыЗависимыхНачисленийПоСотрудникПериоду = ДатыЗависимыхНачислений.НайтиСтроки(ОтборСтрокТаблицыРазбиения);
			
			Для Каждого СтрокаТаблицыДатЗависимыхНачислений Из ДатыЗависимыхНачисленийПоСотрудникПериоду Цикл
				Если ОбщегоНазначенияБЗК.ДатаВИнтервале(СтрокаТаблицыДатЗависимыхНачислений.Дата, НачалоДня(ТекущаяЗаписьНачислений.ПериодДействияНачало), ТекущаяЗаписьНачислений.ПериодДействияКонец, Ложь) 
					И НачалоДня(ТекущаяЗаписьНачислений.ПериодДействияНачало) <> СтрокаТаблицыДатЗависимыхНачислений.Дата Тогда
					
					СтрокаТаблицыРазбиения = ДатыРазбиенияВедущихНачислений.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаТаблицыРазбиения, ТекущаяЗаписьНачислений);
					СтрокаТаблицыРазбиения.Дата = СтрокаТаблицыДатЗависимыхНачислений.Дата;
				КонецЕсли;
			КонецЦикла;			
		КонецЦикла;	
	КонецЦикла;		
КонецПроцедуры	

Функция ВедущиеЗаписиКаскадногоНормирования(ОтборНормируемыхЗаписей, ДанныеТекущегоНабораНачислений = Неопределено, ИсключаемыйРегистратор = Неопределено)
	Если Не РасчетПоДаннымТекущегоНабора Тогда
		ВедущиеЗаписи = ВедущиеЗаписиКаскадногоНормированияПоДаннымИБ(ОтборНормируемыхЗаписей, ИсключаемыйРегистратор);
	Иначе
		ВедущиеЗаписи = ВедущиеЗаписиКаскадногоНормированияПустая();	
	КонецЕсли;	
	
	Если ДанныеТекущегоНабораНачислений <> Неопределено Тогда
		ДополнитьВедущиеЗаписиКаскадногоНормированияПоТекущемуНабору(ВедущиеЗаписи, ОтборНормируемыхЗаписей, ДанныеТекущегоНабораНачислений.ТаблицаНабора); 
	КонецЕсли;
		
	Возврат ВедущиеЗаписи;
КонецФункции

Функция ВедущиеЗаписиКаскадногоНормированияПоДаннымИБ(ОтборНормируемыхЗаписей, ИсключаемыйРегистратор = Неопределено)
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	ЗарплатаКадры.СоздатьВТПоТаблицеЗначений(МенеджерВременныхТаблиц, ОтборНормируемыхЗаписей, "ВТКаскадноНормируемыеЗаписи");
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ИсключаемыйРегистратор", ИсключаемыйРегистратор);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =  
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	БазовыеЗаписи.Регистратор КАК Регистратор,
	|	БазовыеЗаписи.НомерСтроки КАК НомерСтроки,
	|	БазовыеЗаписи.ПериодДействия КАК ПериодДействия,
	|	БазовыеЗаписи.ПериодДействияНачало КАК ПериодДействияНачало,
	|	БазовыеЗаписи.ПериодДействияКонец КАК ПериодДействияКонец,
	|	БазовыеЗаписи.ВидРасчета КАК ВидРасчета,
	|	ИСТИНА КАК НеобходимоНормировать,
	|	БазовыеЗаписи.ВидРасчета.НачисляетсяВЦеломЗаМесяц КАК НачисляетсяВЦеломЗаМесяц,
	|	БазовыеЗаписи.ВидРасчета.ЕстьВытесняющиеНачисления
	|		И НЕ БазовыеЗаписи.ВремяВЦеломЗаПериод КАК НеобходимФПДПриНормировании,
	|	БазовыеЗаписи.Сотрудник КАК Сотрудник,
	|	БазовыеЗаписи.Результат КАК Результат,
	|	БазовыеЗаписи.ГрафикРаботы КАК ГрафикРаботы,
	|	БазовыеЗаписи.ОбщийГрафик КАК ОбщийГрафик,
	|	БазовыеЗаписи.ВремяВЧасах КАК ВремяВЧасах,
	|	БазовыеЗаписи.ПериодРегистрацииВремени КАК ПериодРегистрацииВремени,
	|	ВЫБОР
	|		КОГДА БазовыеЗаписи.ВидРасчета.ТребуетсяРасчетБазы
	|				И БазовыеЗаписи.ПериодДействия = НАЧАЛОПЕРИОДА(БазовыеЗаписи.БазовыйПериодНачало, МЕСЯЦ)
	|				И БазовыеЗаписи.ПериодДействия = НАЧАЛОПЕРИОДА(БазовыеЗаписи.БазовыйПериодКонец, МЕСЯЦ)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НормироватьКаскадно,
	|	ЛОЖЬ КАК ЗаписьТекущегоНабора,
	|	БазовыеЗаписи.ВидРасчета.ПериодДействияБазовый КАК ПериодДействияБазовый,
	|	БазовыеЗаписи.ВидРасчета.ОчередностьРасчета КАК ОчередностьРасчета,
	|	БазовыеЗаписи.ВремяВЦеломЗаПериод КАК ВремяВЦеломЗаПериод,
	|	БазовыеЗаписи.РассчитыватьПоРазовымНачислениямДокумента КАК РассчитыватьПоРазовымНачислениямДокумента,
	|	БазовыеЗаписи.РегистраторРазовогоНачисления КАК РегистраторРазовогоНачисления,
	|	БазовыеЗаписи.ДокументОснование КАК ДокументОснование,
	|	БазовыеЗаписи.Сторно КАК Сторно,
	|	БазовыеЗаписи.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	БазовыеЗаписи.СторнируемыйДокумент КАК СторнируемыйДокумент,
	|	БазовыеЗаписи.ПериодРегистрации КАК ПериодРегистрации
	|ИЗ
	|	ВТКаскадноНормируемыеЗаписи КАК ОсновныеЗаписи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрРасчета.Начисления КАК БазовыеЗаписи
	|		ПО ОсновныеЗаписи.Сотрудник = БазовыеЗаписи.Сотрудник
	|			И (БазовыеЗаписи.ПериодРегистрации <= ОсновныеЗаписи.ПериодРегистрации
	|				ИЛИ БазовыеЗаписи.ПериодРегистрации <= ОсновныеЗаписи.ПериодДействияКонец)
	|			И (БазовыеЗаписи.ПериодДействия = ОсновныеЗаписи.ПериодДействия)
	|			И (БазовыеЗаписи.Регистратор <> &ИсключаемыйРегистратор)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления.ВедущиеВидыРасчета КАК БазовыеВидыРасчета
	|		ПО (БазовыеВидыРасчета.ВидРасчета = БазовыеЗаписи.ВидРасчета)
	|			И (БазовыеВидыРасчета.Ссылка = ОсновныеЗаписи.ВидРасчета)";
	
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

Функция ВедущиеЗаписиКаскадногоНормированияПустая()
	ВедущиеЗаписиКаскадногоНормирования = Новый ТаблицаЗначений;
	ВедущиеЗаписиКаскадногоНормирования.Колонки.Добавить("Регистратор", РегистрыРасчета.Начисления.ОписаниеТиповПоляРегистратор());
	ВедущиеЗаписиКаскадногоНормирования.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число"));
	ВедущиеЗаписиКаскадногоНормирования.Колонки.Добавить("ПериодДействия", Новый ОписаниеТипов("Дата"));
	ВедущиеЗаписиКаскадногоНормирования.Колонки.Добавить("ПериодРегистрации", Новый ОписаниеТипов("Дата"));
	ВедущиеЗаписиКаскадногоНормирования.Колонки.Добавить("ПериодДействияНачало", Новый ОписаниеТипов("Дата"));
	ВедущиеЗаписиКаскадногоНормирования.Колонки.Добавить("ПериодДействияКонец", Новый ОписаниеТипов("Дата"));
	ВедущиеЗаписиКаскадногоНормирования.Колонки.Добавить("ВидРасчета", Новый ОписаниеТипов("ПланВидовРасчетаСсылка.Начисления"));
	ВедущиеЗаписиКаскадногоНормирования.Колонки.Добавить("НеобходимоНормировать", Новый ОписаниеТипов("Булево"));
	ВедущиеЗаписиКаскадногоНормирования.Колонки.Добавить("НеобходимФПДПриНормировании", Новый ОписаниеТипов("Булево"));
	ВедущиеЗаписиКаскадногоНормирования.Колонки.Добавить("НачисляетсяВЦеломЗаМесяц", Новый ОписаниеТипов("Булево"));
	ВедущиеЗаписиКаскадногоНормирования.Колонки.Добавить("ЕстьВытесняющиеНачисления", Новый ОписаниеТипов("Булево"));
	ВедущиеЗаписиКаскадногоНормирования.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	ВедущиеЗаписиКаскадногоНормирования.Колонки.Добавить("Результат", Новый ОписаниеТипов("Число"));
	ВедущиеЗаписиКаскадногоНормирования.Колонки.Добавить("ГрафикРаботы", Новый ОписаниеТипов("СправочникСсылка.Сотрудники, СправочникСсылка.ГрафикиРаботыСотрудников"));
	ВедущиеЗаписиКаскадногоНормирования.Колонки.Добавить("ОбщийГрафик", Новый ОписаниеТипов("СправочникСсылка.ГрафикиРаботыСотрудников"));
	ВедущиеЗаписиКаскадногоНормирования.Колонки.Добавить("ВремяВЧасах", Новый ОписаниеТипов("Булево"));
	ВедущиеЗаписиКаскадногоНормирования.Колонки.Добавить("ПериодРегистрацииВремени", Новый ОписаниеТипов("Дата"));
	ВедущиеЗаписиКаскадногоНормирования.Колонки.Добавить("НормироватьКаскадно", Новый ОписаниеТипов("Булево"));
	ВедущиеЗаписиКаскадногоНормирования.Колонки.Добавить("ЗаписьТекущегоНабора", Новый ОписаниеТипов("Булево"));
    ВедущиеЗаписиКаскадногоНормирования.Колонки.Добавить("ПериодДействияБазовый", Новый ОписаниеТипов("Булево"));
	ВедущиеЗаписиКаскадногоНормирования.Колонки.Добавить("ОчередностьРасчета", Новый ОписаниеТипов("Число"));
	ВедущиеЗаписиКаскадногоНормирования.Колонки.Добавить("РассчитыватьПоРазовымНачислениямДокумента", Новый ОписаниеТипов("Булево"));
	ВедущиеЗаписиКаскадногоНормирования.Колонки.Добавить("Сторно", Новый ОписаниеТипов("Булево"));
	ВедущиеЗаписиКаскадногоНормирования.Колонки.Добавить("СторнируемыйДокумент", РегистрыРасчета.Начисления.ОписаниеТиповПоляРегистратор());
	ВедущиеЗаписиКаскадногоНормирования.Колонки.Добавить("ДокументОснование", Метаданные.ОпределяемыеТипы.ОснованиеНачисления.Тип);
	ВедущиеЗаписиКаскадногоНормирования.Колонки.Добавить("ГоловнаяОрганизация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	
	Возврат ВедущиеЗаписиКаскадногоНормирования;	
КонецФункции

Процедура ДополнитьВедущиеЗаписиКаскадногоНормированияПоТекущемуНабору(ВедущиеЗаписиКаскадногоНормирования, ОтборНормируемыхЗаписей, ТаблицаНабораНачислений)
	ПоляПоискаСуществующихЗаписей = "Регистратор, НомерСтроки";
	ОбщегоНазначенияБЗК.ДобавитьИндексКоллекции(ВедущиеЗаписиКаскадногоНормирования, ПоляПоискаСуществующихЗаписей);
	ОтборСуществующихЗаписей = Новый Структура(ПоляПоискаСуществующихЗаписей);
		
	ОтборВидовРасчета = ОтборНормируемыхЗаписей.ВыгрузитьКолонку("ВидРасчета");
	ОтборВидовРасчета = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ОтборВидовРасчета);
	ТаблицаБазовыхВР = "ПланВидовРасчета.Начисления.ВедущиеВидыРасчета";
	
	НачисленияТекущегоНабора = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаНабораНачислений, "ВидРасчета", Истина);
	СвойстваНачисленийНабора = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(НачисленияТекущегоНабора, "ТребуетсяРасчетБазы, ПериодДействияБазовый, ОчередностьРасчета, ЕстьВытесняющиеНачисления, НачисляетсяВЦеломЗаМесяц");
	
	ЗависимостиВидовРасчета = ЗависимостиВидовРасчета(ТаблицаБазовыхВР, ОтборВидовРасчета);
		
	ОбщегоНазначенияБЗК.ДобавитьИндексКоллекции(ТаблицаНабораНачислений, "Сотрудник, ПериодДействия");
	СтруктураПоиска = Новый Структура("Сотрудник, ПериодДействия");
	
	Для Каждого СтрокаРассчитываемогоНабора Из ОтборНормируемыхЗаписей Цикл
		ВедущиеВидыРасчетаТекущейСтроки = ЗависимостиВидовРасчета[СтрокаРассчитываемогоНабора.ВидРасчета]; 
		Если ВедущиеВидыРасчетаТекущейСтроки = Неопределено Тогда
			Продолжить;
		КонецЕсли;	
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаРассчитываемогоНабора);
		ЗаписиТекущегоНабора = ТаблицаНабораНачислений.НайтиСтроки(СтруктураПоиска);
			
		Для Каждого СтрокаТекущегоНабора Из ЗаписиТекущегоНабора Цикл
			ЗаполнитьЗначенияСвойств(ОтборСуществующихЗаписей, СтрокаТекущегоНабора);
			
			Если  (СтрокаТекущегоНабора.ПериодРегистрации > СтрокаРассчитываемогоНабора.ПериодРегистрации 
				И СтрокаТекущегоНабора.ПериодРегистрации > СтрокаРассчитываемогоНабора.ПериодДействия)
				Или ВедущиеЗаписиКаскадногоНормирования.НайтиСтроки(ОтборСуществующихЗаписей).Количество() > 0 Тогда
				
				Продолжить;
			КонецЕсли;	
				
			СвойстваБазовогоВР = СвойстваНачисленийНабора[СтрокаТекущегоНабора.ВидРасчета];	
						
			НоваяЗапись = ВедущиеЗаписиКаскадногоНормирования.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, СтрокаТекущегоНабора);
			НоваяЗапись.НеобходимоНормировать = Истина;
			НоваяЗапись.ЗаписьТекущегоНабора = Истина;
			
			НоваяЗапись.НормироватьКаскадно = НормироватьЗаписьКаскадно(СтрокаТекущегоНабора, СтрокаРассчитываемогоНабора, СвойстваБазовогоВР);
			НоваяЗапись.ОчередностьРасчета = СвойстваБазовогоВР.ОчередностьРасчета;
			НоваяЗапись.НачисляетсяВЦеломЗаМесяц = СвойстваБазовогоВР.НачисляетсяВЦеломЗаМесяц;
			НоваяЗапись.НеобходимФПДПриНормировании  = СвойстваБазовогоВР.ЕстьВытесняющиеНачисления И Не СтрокаТекущегоНабора.ВремяВЦеломЗаПериод;
		КонецЦикла;				
	КонецЦикла;					
КонецПроцедуры				

Функция СвойстваНачисленийТекущегоНабора(ТаблицаНабораНачислений, ДоступныеАлгоритмы)
	
	ВидРасчетаМассив = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаНабораНачислений, "ВидРасчета", Истина);
	Если АлгоритмРасчетаБазы = ДоступныеАлгоритмы.БазаРезервов Тогда
		СвойстваНачисленийНабора = РезервыПоОплатеТрудаРасширенный.СвойстваНачисленийНабора(ТаблицаНабораНачислений);
	Иначе
		СвойстваНачисленийНабора = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(ВидРасчетаМассив, "ТребуетсяРасчетБазы, ПериодДействияБазовый, ОчередностьРасчета, ЕстьВытесняющиеНачисления, НачисляетсяВЦеломЗаМесяц");
	КонецЕсли;
	
	Возврат СвойстваНачисленийНабора;
	
КонецФункции

Функция ЗависимостиВидовРасчетаНачисленийУдержаний(ИмяТаблицыЗависимыеВидыРасчета, ОтборЗависимыхВидовРасчета = Неопределено, ОтборВедущихВидовРасчета = Неопределено)			
	
	ЗависимостиНачислений = Новый Соответствие;		
	
	Запрос = Новый Запрос;
		
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НачисленияВедущиеВидыРасчета.Ссылка КАК ВидРасчета,
	|	НачисленияВедущиеВидыРасчета.ВидРасчета КАК ВедущийВидРасчета,
	|	Начисления.ПериодДействияБазовый
	|		И Начисления.ТребуетсяРасчетБазы КАК НормироватьКаскадно
	|ИЗ
	|	ВТВедущиеВидыРасчета КАК НачисленияВедущиеВидыРасчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления КАК Начисления
	|		ПО НачисленияВедущиеВидыРасчета.ВидРасчета = Начисления.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВидРасчета";	
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТВедущиеВидыРасчета", ИмяТаблицыЗависимыеВидыРасчета);
	
	Схема = Новый СхемаЗапроса;
	Схема.УстановитьТекстЗапроса(Запрос.Текст);
	
	Если ОтборЗависимыхВидовРасчета <> Неопределено Тогда
		Схема.ПакетЗапросов[0].Операторы[0].Отбор.Добавить("НачисленияВедущиеВидыРасчета.Ссылка В(&ОтборЗависимыхВидовРасчета)");
		Запрос.УстановитьПараметр("ОтборЗависимыхВидовРасчета", ОтборЗависимыхВидовРасчета);
	КонецЕсли;
	
	Если ОтборВедущихВидовРасчета <> Неопределено Тогда
		Схема.ПакетЗапросов[0].Операторы[0].Отбор.Добавить("НачисленияВедущиеВидыРасчета.ВидРасчета В(&ОтборВедущихНачислений)");
		Запрос.УстановитьПараметр("ОтборВедущихНачислений", ОтборВедущихВидовРасчета);
	КонецЕсли;
	
	Запрос.Текст = Схема.ПолучитьТекстЗапроса();
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.СледующийПоЗначениюПоля("ВидРасчета") Цикл
		ВедущиеВидыРасчета = Новый Соответствие();
		ЗависимостиНачислений.Вставить(Выборка.ВидРасчета, ВедущиеВидыРасчета);
		
		Пока Выборка.Следующий() Цикл
			ОписаниеВедущегоВидаРасчета = Новый Структура("ВидРасчета, НормироватьКаскадно");
			ОписаниеВедущегоВидаРасчета.ВидРасчета = Выборка.ВедущийВидРасчета;
			ОписаниеВедущегоВидаРасчета.НормироватьКаскадно = Выборка.НормироватьКаскадно;
			ВедущиеВидыРасчета.Вставить(Выборка.ВедущийВидРасчета, ОписаниеВедущегоВидаРасчета);
		КонецЦикла;	
	КонецЦикла;	
	
	Возврат ЗависимостиНачислений;
КонецФункции	

#КонецОбласти

#Область Инициализация

ВыполненныеОчередностиКаскадногоРасчетаНачислений = Новый Соответствие();

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли