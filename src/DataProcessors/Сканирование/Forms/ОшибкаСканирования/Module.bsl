///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Заголовок = Параметры.Заголовок;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьТекстОшибки();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТекстОшибкиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	Если НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьНастройки" Тогда
		СтандартнаяОбработка = Ложь;
		РаботаСФайламиКлиент.ОткрытьФормуНастройкиСканирования();
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ТехническаяИнформация" Тогда
		СтандартнаяОбработка = Ложь;
		ПолучитьТехническуюИнформацию();
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "Запустить32БитныйКлиент" Тогда
		СтандартнаяОбработка = Ложь;
		Запустить32БитныйКлиент();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПовторитьСканирование(Команда)
	Закрыть("ПовторитьСканирование");
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьТекстОшибки()
	Рекомендации = Новый Массив;
	Рекомендации.Добавить(НСтр("ru = 'Проверьте подключение сканера и повторите попытку сканирования.';
								|en = 'Check scanner connection and try again.'"));
	Рекомендации.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Укажите в <a href = ""%1"">настройках сканирования</a> доступный сканер.';
			|en = 'Specify an available scanner in the <a href = ""%1"">scanning settings</a>.'"),
		"ОткрытьНастройки"));

	Если Не Параметры.ПоказыватьДиалогСканера И Не ОбщегоНазначенияКлиент.ЭтоLinuxКлиент() Тогда
		Рекомендации.Добавить(НСтр("ru = 'Смените способ задания настроек на <b>Расширенные в диалоге сканера</b>.';
									|en = 'Change the method of configuring the settings to <b>Advanced settings</b>.'"));
	КонецЕсли;
	
	Если Параметры.ПоказыватьДиалогСканера 
		Или Параметры.Разрешение = ПредопределенноеЗначение("Перечисление.РазрешенияСканированногоИзображения.dpi1200") Тогда
		Рекомендации.Добавить(НСтр("ru = 'Снизьте разрешение сканирования до <b>600 dpi</b>.';
									|en = 'Reduce the scanner resolution to <b>600 dpi</b>.'"));
	КонецЕсли;

	СистемнаяИнформация = Новый СистемнаяИнформация();
	Если СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Windows_x86_64 Тогда
		Рекомендации.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Установите и запустите <a href = ""%1"">тонкий клиент 1С:Предприятия для Windows (32-bit)</a>, 
				|  в котором доступно больше устройств и настроек сканирования.';
				|en = 'Install the <a href = ""%1"">1C:Enterprise thin client for Windows x86</a>.
				|It supports more scanners and settings.'"), 
				"Запустить32БитныйКлиент"));
	КонецЕсли;

	Если Параметры.РежимТребуетсяПомощь Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Сканирование выполняется с помощью %1.';
				|en = 'Scanning is performed by %1.'"), 
			Параметры.ИмяУстройстваСканирования);
	Иначе
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Сканер ""%1"" не обнаружен или не подключен.';
				|en = 'Scanner ""%1"" is not found or disconnected.'"), 
			Параметры.ИмяУстройстваСканирования);
	КонецЕсли;		

	ТекстОшибки = ТекстОшибки + Символы.ПС + Символы.ПС 
		+ НСтр("ru = 'Попробуйте следующие варианты:';
				|en = 'Try the following solutions:'") + Символы.ПС
		+ " • " + СтрСоединить(Рекомендации, Символы.ПС + " • ");
	ТекстОшибки = ТекстОшибки + Символы.ПС + Символы.ПС 
		+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Если проблема повторится, обратитесь в службу поддержки фирмы ""1С"", 
				|предоставив <a href = ""%1"">техническую информацию</a> о возникшей проблеме.';
				|en = 'If the issue persists, contact 1C technical support 
				|and provide <a href = ""%1"">technical information</a> about the issue.'"), 
		"ТехническаяИнформация");
	
	Элементы.ТекстОшибки.Заголовок = СтроковыеФункцииКлиент.ФорматированнаяСтрока(ТекстОшибки);
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьТехническуюИнформацию()
	РаботаСФайламиСлужебныйКлиент.ПолучитьТехническуюИнформацию(Параметры.ПодробноеПредставлениеОшибки);
КонецПроцедуры

&НаКлиенте
Процедура Запустить32БитныйКлиент()
#Если Не ВебКлиент Тогда
	КаталогПрограммы32 = СтрЗаменить(КаталогПрограммы(), "\Program Files\", "\Program Files (x86)\");
	ИмяПриложения = КаталогПрограммы32 + "1cv8.exe";
	ФайлПриложения = Новый Файл(ИмяПриложения);
	Если ФайлПриложения.Существует() Тогда 
		ФайловаяСистемаКлиент.ЗапуститьПрограмму(ИмяПриложения);
	Иначе
		СистемнаяИнформация = Новый СистемнаяИнформация();
		ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("https://releases.1c.ru/version_files?nick=Platform83&ver=" + СистемнаяИнформация.ВерсияПриложения);
	КонецЕсли;
#КонецЕсли
КонецПроцедуры

#КонецОбласти
