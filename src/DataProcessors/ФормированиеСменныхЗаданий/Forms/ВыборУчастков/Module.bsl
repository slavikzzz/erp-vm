
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Заголовок = Метаданные.НайтиПоТипу(Список.ТипЗначения.Типы()[0]).Синоним;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ПроизводственныеУчастки.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ПроизводственныеУчастки КАК ПроизводственныеУчастки
		|ГДЕ
		|	ПроизводственныеУчастки.Владелец = &Подразделение
		|	И НЕ ПроизводственныеУчастки.ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПроизводственныеУчастки.Наименование");
	Запрос.УстановитьПараметр("Подразделение", Параметры.Подразделение);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Список.Добавить(Выборка.Ссылка,,
			Параметры.ВыбранныеЗначения.НайтиПоЗначению(Выборка.Ссылка) <> Неопределено);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Результат = Новый СписокЗначений;
	
	Для каждого Элемент Из Список Цикл
		
		Если Элемент.Пометка Тогда
			
			Результат.Добавить(Элемент.Значение);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ОповеститьОВыборе(Результат);
	
КонецПроцедуры

#КонецОбласти
