#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(МассивСсылок, ПараметрыВыполненияКоманды)
	Результат = ПовторноПолучитьИзФССНаСервере(МассивСсылок);
	ЕстьНовыеСообщения = (Результат.НовыеСообщения.Количество() > 0);
	ЕстьОшибки         = (Результат.ТекстыОшибок.Количество() > 0);
	Если ЕстьНовыеСообщения Тогда
		СЭДОФССКлиент.ПовторноПолучитьСообщенияИзФСС(Результат.НовыеСообщения);
	КонецЕсли;
	Если ЕстьОшибки Тогда
		Подробно = СтрСоединить(Результат.ТекстыОшибок, Символы.ПС + Символы.ПС);
		ИнформированиеПользователяКлиент.ПоказатьПодробности(Подробно, БиблиотекаКартинок.Предупреждение32);
	ИначеЕсли Не ЕстьНовыеСообщения Тогда
		Если Результат.Всего = 1 Тогда
			Заголовок ="";
			Текст     = НСтр("ru = 'Сообщение СЭДО успешно обработано';
							|en = 'EDI message is successfully processed'");
		Иначе
			Заголовок = "";
			Текст     = СтрШаблон(НСтр("ru = 'Сообщения СЭДО (%1 шт.) успешно обработаны';
										|en = 'EDI messages (%1 pcs.) are successfully processed'"), Формат(Результат.Всего, "ЧГ="));
		КонецЕсли;
		ПоказатьОповещениеПользователя(Заголовок, , Текст, БиблиотекаКартинок.Успешно32);
	КонецЕсли;
	Оповестить(СЭДОФССКлиент.ИмяСобытияПослеПолученияСообщенийОтФСС());
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПовторноПолучитьИзФССНаСервере(Знач МассивСсылок)
	ДанныеСсылок = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(
		МассивСсылок,
		"Страхователь, ИдентификаторСообщения",
		Истина);
	
	Измерения = Метаданные.РегистрыСведений.ВходящиеСообщенияСЭДОФСС.Измерения;
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Страхователь",           Измерения.Организация.Тип);
	Таблица.Колонки.Добавить("ИдентификаторСообщения", Измерения.Идентификатор.Тип);
	Для Каждого КлючИЗначение Из ДанныеСсылок Цикл
		ДанныеДокумента = КлючИЗначение.Значение;
		Если Не ЗначениеЗаполнено(ДанныеДокумента.Страхователь) Тогда
			Текст = НСтр("ru = 'Не заполнено поле ""Страхователь""';
						|en = '""Insurant"" is not filled in'");
			СообщенияБЗК.СообщитьОПроблеме(Текст, КлючИЗначение.Ключ, "Страхователь");
			Продолжить;
		КонецЕсли;
		СтрокаТаблицы = Таблица.Добавить();
		СтрокаТаблицы.Страхователь           = ДанныеДокумента.Страхователь;
		СтрокаТаблицы.ИдентификаторСообщения = ДанныеДокумента.ИдентификаторСообщения;
	КонецЦикла;
	Таблица.Свернуть("Страхователь, ИдентификаторСообщения");
	Всего = Таблица.Количество();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Таблица", Таблица);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.Страхователь КАК Страхователь,
	|	Таблица.ИдентификаторСообщения КАК ИдентификаторСообщения
	|ПОМЕСТИТЬ Таблица
	|ИЗ
	|	&Таблица КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.Страхователь КАК Страхователь,
	|	Таблица.ИдентификаторСообщения КАК ИдентификаторСообщения,
	|	ВходящиеСообщенияСЭДОФСС.Идентификатор КАК Идентификатор,
	|	ВходящиеСообщенияСЭДОФСС.Организация КАК Организация,
	|	ВходящиеСообщенияСЭДОФСС.Содержимое КАК Содержимое,
	|	ВходящиеСообщенияСЭДОФСС.Дата КАК Дата,
	|	ВходящиеСообщенияСЭДОФСС.ДатаЗагрузки КАК ДатаЗагрузки,
	|	ВходящиеСообщенияСЭДОФСС.ДатаОтправкиПодтверждения КАК ДатаОтправкиПодтверждения,
	|	ВходящиеСообщенияСЭДОФСС.ДатаПолученияИзвещенияОПолученииПодтверждения КАК ДатаПолученияИзвещенияОПолученииПодтверждения,
	|	ВходящиеСообщенияСЭДОФСС.ДатаПолученияОшибкиПодтверждения КАК ДатаПолученияОшибкиПодтверждения,
	|	ВходящиеСообщенияСЭДОФСС.ДатаСоздания КАК ДатаСоздания,
	|	ВходящиеСообщенияСЭДОФСС.Новое КАК Новое,
	|	ВходящиеСообщенияСЭДОФСС.ОшибкаОбработкиСообщения КАК ОшибкаОбработкиСообщения,
	|	ВходящиеСообщенияСЭДОФСС.ОшибкаПодтверждения КАК ОшибкаПодтверждения,
	|	ВходящиеСообщенияСЭДОФСС.ПодтверждениеОтправлено КАК ПодтверждениеОтправлено,
	|	ВходящиеСообщенияСЭДОФСС.Получатель КАК Получатель,
	|	ВходящиеСообщенияСЭДОФСС.Тип КАК Тип,
	|	ВходящиеСообщенияСЭДОФСС.ТребуетсяПодтверждение КАК ТребуетсяПодтверждение,
	|	ВходящиеСообщенияСЭДОФСС.СрокДействия КАК СрокДействия
	|ИЗ
	|	Таблица КАК Таблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВходящиеСообщенияСЭДОФСС КАК ВходящиеСообщенияСЭДОФСС
	|		ПО Таблица.ИдентификаторСообщения = ВходящиеСообщенияСЭДОФСС.Идентификатор
	|			И Таблица.Страхователь = ВходящиеСообщенияСЭДОФСС.Организация";
	
	НовыеСообщения = Новый Соответствие;
	ТекстыОшибок = Новый Массив;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.Новое = Null Тогда
			Текст = НСтр("ru = 'Сообщение %1 не найдено в регистре входящих сообщений СЭДО';
						|en = 'The %1 message is not found in the Incoming EDI message register'");
			ТекстыОшибок.Добавить(СтрШаблон(Текст, Выборка.ИдентификаторСообщения));
			Продолжить;
		КонецЕсли;
		Если Выборка.Новое Тогда
			РезультатОбработки = Неопределено;
		Иначе
			РезультатОбработки = СЭДОФСС.ПовторноОбработатьВходящееСообщениеСЭДО(Выборка, Выборка.Содержимое.Получить());
		КонецЕсли;
		Если РезультатОбработки = Неопределено Тогда
			ИдентификаторыСообщений = НовыеСообщения[Выборка.Страхователь];
			Если ИдентификаторыСообщений = Неопределено Тогда
				ИдентификаторыСообщений = Новый Массив;
				НовыеСообщения.Вставить(Выборка.Страхователь, ИдентификаторыСообщений);
			КонецЕсли;
			Если ИдентификаторыСообщений.Найти(Выборка.ИдентификаторСообщения) = Неопределено Тогда
				ИдентификаторыСообщений.Добавить(Выборка.ИдентификаторСообщения);
			КонецЕсли;
		Иначе
			Если ЗначениеЗаполнено(РезультатОбработки.ОписаниеОшибки) Тогда
				ТекстыОшибок.Добавить(РезультатОбработки.ОписаниеОшибки);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Результат = Новый Структура("НовыеСообщения, ТекстыОшибок, Всего");
	Результат.НовыеСообщения = НовыеСообщения;
	Результат.ТекстыОшибок   = ТекстыОшибок;
	Результат.Всего          = Всего;
	Возврат Результат;
КонецФункции

#КонецОбласти
