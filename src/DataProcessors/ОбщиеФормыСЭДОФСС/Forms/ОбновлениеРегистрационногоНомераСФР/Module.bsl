#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры,
		"ВходящийДокументОтФонда, РегистрационныйНомерСФР, Страхователь, ТекущийРегистрационныйНомерСФР,
		|ИдентификаторСообщения, ДатаСообщения, ТипСообщения, ПредставлениеТипа");
	ЕстьВходящийДокумент = ЗначениеЗаполнено(ВходящийДокументОтФонда);
	ЕстьСообщениеСЭДО    = Не ЕстьВходящийДокумент И ЗначениеЗаполнено(ИдентификаторСообщения);
	Элементы.ЗаголовокВходящийДокументОтФонда.Видимость = ЕстьВходящийДокумент;
	Элементы.ВходящийДокументОтФонда.Видимость          = ЕстьВходящийДокумент;
	Элементы.ВходящийДокументРегномер.Видимость         = ЕстьВходящийДокумент;
	Элементы.ЗаголовокСообщениеСЭДО.Видимость           = ЕстьСообщениеСЭДО;
	Элементы.ГруппаСообщениеСЭДО1.Видимость             = ЕстьСообщениеСЭДО;
	Элементы.ГруппаСообщениеСЭДО2.Видимость             = ЕстьСообщениеСЭДО;
	Элементы.ГруппаСообщениеСЭДО2.Видимость             = ЕстьСообщениеСЭДО;
	Элементы.СообщениеСЭДОРегномер.Видимость            = ЕстьСообщениеСЭДО;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьНомерСФРВДанныеОрганизации(Команда)
	ЗаписатьНомерСФРВДанныеОрганизацииНаСервере();
	Оповестить("Запись_Организации", , Страхователь);
	Закрыть();
	Ссылка = ПолучитьНавигационнуюСсылку(Страхователь);
	ПоказатьОповещениеПользователя(Страхователь, Ссылка, НСтр("ru = 'Установлен регномер СФР';
																|en = 'Registration number in the Social Insurance Fund of Russia is set'"), БиблиотекаКартинок.Успешно32);
	ИсторияРаботыПользователя.Добавить(Ссылка);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаписатьНомерСФРВДанныеОрганизацииНаСервере()
	НачатьТранзакцию();
	Попытка
		ОрганизацияОбъект = Страхователь.ПолучитьОбъект();
		ОрганизацияОбъект.РегистрационныйНомерСФР = РегистрационныйНомерСФР;
		ОрганизацияОбъект.Записать();
		
		ЗначенияРесурсов = Новый Структура("РегистрационныйНомерСФР", РегистрационныйНомерСФР);
		РегистрыСведений.ИсторияРегистрацийВОрганеПФР.ЗаписатьДанные(Страхователь, ЗначенияРесурсов);
		
		Если ЗначениеЗаполнено(ВходящийДокументОтФонда) Тогда
			Комментарий = НСтр("ru = 'Установлен регномер СФР ""%1"" по данным документа ""%2""';
								|en = 'Установлен регномер СФР ""%1"" по данным документа ""%2""'");
			Комментарий = СтрШаблон(Комментарий, РегистрационныйНомерСФР, ВходящийДокументОтФонда);
		Иначе
			Комментарий = НСтр("ru = 'Установлен регномер СФР ""%1"" по данным сообщения СЭДО %2 от %3 (%4 %5)';
								|en = 'Установлен регномер СФР ""%1"" по данным сообщения СЭДО %2 от %3 (%4 %5)'");
			Комментарий = СтрШаблон(Комментарий,
				РегистрационныйНомерСФР,
				ИдентификаторСообщения,
				ДатаСообщения,
				ТипСообщения,
				ПредставлениеТипа);
		КонецЕсли;
		ЗаписьЖурналаРегистрации(
			СЭДОФСС.ИмяСобытияЖурнала(),
			УровеньЖурналаРегистрации.Информация,
			Страхователь.Метаданные(),
			Страхователь,
			Комментарий,
			РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
КонецПроцедуры

#КонецОбласти
