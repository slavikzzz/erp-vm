#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОписаниеПеременных

Перем БылиОшибки Экспорт;
Перем Таймаут Экспорт;
Перем Приложение Экспорт;
Перем ВерсияDTO Экспорт;
Перем ВерсияAPI Экспорт;
Перем МенеджерКонвертации Экспорт;
Перем ТипОбъектаРазмерПакета Экспорт;
Перем ИменаСобытийЖР Экспорт;

Перем ВестиПротокол;
Перем СтруктураURI;
Перем АдресТокена;
Перем HTTPСоединение;
Перем АдресРесурсаAPI;
Перем ТипОбъектаАдресРесурса;
Перем ТипОбъектаАдресРесурсаЗапрос;
Перем ОписаниеКлиентскогоПриложения;
Перем ЗарегистрированныеОшибкиНеНайденАдресРесурса;
Перем ЗарегистрированныеОшибкиНеНайденАдресРесурсаЗагрузкиОбъекта;
Перем АдресПриложенияПоИмени;

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Инициализация

Процедура Инициализировать(ПриложениеДляОбмена, ПодготовитьДанныеДляТехПоддержки) Экспорт
	
	БылиОшибки 	= Ложь;
	Таймаут 	= 100;
	
	Приложение 		= ПриложениеДляОбмена;
	ВестиПротокол 	= ПодготовитьДанныеДляТехПоддержки Или Константы.РегистрироватьВЖурналеСобытийЗапросы.Получить();
	ИменаСобытийЖР 	= ИнтеграцияУправлениеПерсоналом.ИменаСобытийЖР(Приложение);
	АдресТокена 	= ИнтеграцияУправлениеПерсоналом.АдресХраненияТокенаПриложения(Приложение);
	
	ОписаниеКлиентскогоПриложения = ИнтеграцияУправлениеПерсоналом.ОписаниеКлиентскогоПриложения();
	
	НастройкиПодключения = ИнтеграцияУправлениеПерсоналом.НастройкиПодключения(Приложение);
	АдресПриложения 		= НастройкиПодключения.АдресПриложения;
	АдресПриложенияПоИмени 	= НастройкиПодключения.АдресПриложенияПоИмени;
	ВерсияDTO 				= НастройкиПодключения.ВерсияDTO;
	ВерсияAPI 				= НастройкиПодключения.ВерсияAPI;
	
	СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI(АдресПриложения);
	
	ЗаполнитьТипОбъектаРазмерПакета();
	ОбновитьИнициализациюПриСмениВерсииAPI();
	СоздатьНовоеHTTPСоединение();
	
	МенеджерКонвертации = ИнтеграцияУправлениеПерсоналомОбмен.НовыйМенеджерКонвертации(Приложение); 
	ИнтеграцияУправлениеПерсоналомПереопределяемый.ИнициализацияПараметровМенеджераОбмена(ЭтотОбъект);

КонецПроцедуры

Процедура СоздатьНовоеHTTPСоединение()
	
	ИнтернетПрокси = Неопределено;
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
		МодульПолучениеФайловИзИнтернета = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернета");
		ИнтернетПрокси = МодульПолучениеФайловИзИнтернета.ПолучитьПрокси(СтруктураURI.Схема);
	КонецЕсли;
	ЗащищенноеСоединение = Неопределено;
	Если ВРег(СтруктураURI.Схема) = "HTTPS" Или ВРег(СтруктураURI.Схема) = "FTPS" Тогда
		ЗащищенноеСоединение = ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение();
	КонецЕсли;
	
	HTTPСоединение = Новый HTTPСоединение(СтруктураURI.Хост, СтруктураURI.Порт,,, ИнтернетПрокси, Таймаут, ЗащищенноеСоединение);
	
КонецПроцедуры

Процедура ОбновитьИнициализациюПриСмениВерсииAPI() Экспорт

	АдресРесурсаAPI = АдресРесурсаAPI();
	ЗаполнитьТипОбъектаАдресРесурса();
	ЗаполнитьТипОбъектаАдресРесурсаЗапрос();

КонецПроцедуры

Процедура ЗаполнитьТипОбъектаАдресРесурса()
	
	МенеджерТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом;
	
	Описание = Новый Соответствие;
	Описание.Вставить(МенеджерТипОбъекта.ФизическоеЛицо, 						РесурсФизическиеЛица());
	Описание.Вставить(МенеджерТипОбъекта.Организация, 							РесурсОрганизации());
	Описание.Вставить(МенеджерТипОбъекта.Подразделение, 						РесурсПодразделения());
	Описание.Вставить(МенеджерТипОбъекта.Должность, 							РесурсДолжности());
	Описание.Вставить(МенеджерТипОбъекта.ДолжностьПоШтатномуРасписанию, 		РесурсШтатноеРасписание());
	Описание.Вставить(МенеджерТипОбъекта.Сотрудник, 							РесурсСотрудники());
	Описание.Вставить(МенеджерТипОбъекта.ГрафикРаботы, 							РесурсГрафикиРаботы());
	Описание.Вставить(МенеджерТипОбъекта.ГрафикОтпусковПредприятия, 			РесурсГрафикОтпусковПредприятия());
	Описание.Вставить(МенеджерТипОбъекта.ВсеИзменения, 							РесурсПолучениеВсехИзменений());
	Описание.Вставить(МенеджерТипОбъекта.ВидПредоставляемойСотрудникамСправки, 	РесурсВидовПредоставляемыхСправок());
	Описание.Вставить(МенеджерТипОбъекта.ОграничениеДоступаКРабочимКонтактам, 	РесурсОграничениеДоступаКРабочимКонтактам());
	Описание.Вставить(МенеджерТипОбъекта.ДоступныеФункцииФизическогоЛица, 		РесурсДоступныеФункцииФизическогоЛица());
	Описание.Вставить(МенеджерТипОбъекта.ДокументНаПодпись, 					РесурсДокументыНаПодпись());
	Описание.Вставить(МенеджерТипОбъекта.СогласиеНаПрисоединениеККЭДО, 			РесурсСогласияНаПрисоединениеККЭДО());
	Описание.Вставить(МенеджерТипОбъекта.МашиночитаемаяДоверенность, 			РесурсМашиночитаемыеДоверенности());
	Описание.Вставить(МенеджерТипОбъекта.ГрафикОтпусков, 						РесурсГрафикОтпусков());
	Описание.Вставить(МенеджерТипОбъекта.ПрименяемыеВычетыНДФЛ, 				РесурсПрименяемыеВычетыНДФЛ());
	Описание.Вставить(МенеджерТипОбъекта.ПраздничныеДниГрафикаРаботы, 			РесурсПраздничныеДниГрафиковРаботы());
	Описание.Вставить(МенеджерТипОбъекта.РезультатСогласования, 				РесурсРезультатыСогласования());
	Описание.Вставить(МенеджерТипОбъекта.РасчетныйЛисток, 						РесурсРасчетныеЛистки());
	Описание.Вставить(МенеджерТипОбъекта.ПлановоеУдержание, 					РесурсПлановыеУдержания());
	Описание.Вставить(МенеджерТипОбъекта.БудущееИзменениеОстаткаОтпусков, 		РесурсБудущееИзменениеОстаткаОтпусков());
	Описание.Вставить(МенеджерТипОбъекта.ДанныеГрафикаРаботы, 					РесурсДанныеГрафикаРаботы());
	Описание.Вставить(МенеджерТипОбъекта.ИнформацияОбИспользованииОтпуска, 		РесурсИнформацияОбИспользованииОтпуска());
	Описание.Вставить(МенеджерТипОбъекта.ВидСоставнойЧастиЗарплаты, 			РесурсВидыСоставныхЧастейЗарплаты());
	Описание.Вставить(МенеджерТипОбъекта.ИнформацияОбИспользованииОтпуска, 		РесурсИнформацияОбИспользованииОтпуска());
	Описание.Вставить(МенеджерТипОбъекта.Справка2НДФЛ, 							РесурсСправки2НДФЛ());
	Описание.Вставить(МенеджерТипОбъекта.СправкаСМестаРаботы, 					РесурсСправкиСМестаРаботы());
	Описание.Вставить(МенеджерТипОбъекта.СправкаОстаткиОтпусков, 				РесурсСправкиОстаткиОтпусков());
	Описание.Вставить(МенеджерТипОбъекта.ИспользуемаяФункциональность, 			РесурсИспользуемаяФункциональность());
	Описание.Вставить(МенеджерТипОбъекта.НастройкиЗаявленийНаОтпуск, 			РесурсНастройкиЗаявленийНаОтпуск());
	Описание.Вставить(МенеджерТипОбъекта.ВидОтпуска, 							РесурсВидыОтпусков());
	Описание.Вставить(МенеджерТипОбъекта.НастройкиОтсутствий, 					РесурсНастройкиОтсутствий());
	Описание.Вставить(МенеджерТипОбъекта.ОстаткиОтпусков, 						РесурсОстаткиОтпусков());
	Описание.Вставить(МенеджерТипОбъекта.ИнформацияОПриложении, 				РесурсИнформацияОПриложении());
	Описание.Вставить(МенеджерТипОбъекта.НастройкиПриложения, 					РесурсНастройкиПриложения());
	Описание.Вставить(МенеджерТипОбъекта.ВнешнееФизическоеЛицо, 				РесурсАдминистратор());
	Описание.Вставить(МенеджерТипОбъекта.НастройкиСервисаПодписания,			РесурсНастройкиСервисаПодписания());
	Описание.Вставить(МенеджерТипОбъекта.РабочееМестоОхраныТруда,		 		РесурсРабочиеМестаОхраныТруда());
	
	ТипОбъектаАдресРесурса = Описание;
	
КонецПроцедуры

Процедура ЗаполнитьТипОбъектаАдресРесурсаЗапрос()
	
	МенеджерТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом;
	
	Описание = Новый Соответствие;
	Описание.Вставить(МенеджерТипОбъекта.ФизическоеЛицо, 				РесурсЗапросФизическиеЛица());
	Описание.Вставить(МенеджерТипОбъекта.Организация, 					РесурсЗапросОрганизации());
	Описание.Вставить(МенеджерТипОбъекта.Подразделение, 				РесурсЗапросПодразделения());
	Описание.Вставить(МенеджерТипОбъекта.Должность, 					РесурсЗапросДолжности());
	Описание.Вставить(МенеджерТипОбъекта.ДолжностьПоШтатномуРасписанию, РесурсЗапросШтатноеРасписание());
	Описание.Вставить(МенеджерТипОбъекта.Сотрудник, 					РесурсЗапросСотрудники());
	Описание.Вставить(МенеджерТипОбъекта.ГрафикРаботы, 					РесурсЗапросГрафикиРаботы());
	Описание.Вставить(МенеджерТипОбъекта.ЗапросСправки2НДФЛ, 			РесурсЗапросСправки2НДФЛ());
	Описание.Вставить(МенеджерТипОбъекта.ЗапросСправкиСМестаРаботы, 	РесурсЗапросСправкиСРаботы());
	Описание.Вставить(МенеджерТипОбъекта.ЗаявкаНаОтпуск, 				РесурсЗапросЗаявкаНаОтпуск());
	Описание.Вставить(МенеджерТипОбъекта.ЗаявкаНалоговыйВычет, 			РесурсЗапросЗаявкаНалоговыйВычет());
	Описание.Вставить(МенеджерТипОбъекта.ЗаявкаИзменениеЛичныхДанных, 	РесурсЗапросЗаявкаИзменениеЛичныхДанных());
	Описание.Вставить(МенеджерТипОбъекта.Отсутствие, 					РесурсЗапросОтсутствие());
	Описание.Вставить(МенеджерТипОбъекта.ЗаявкаОстаткиОтпусков, 		РесурсЗапросЗаявкаОстаткиОтпусков());
	Описание.Вставить(МенеджерТипОбъекта.ЗаявкаНаКомпенсациюОтпуска, 	РесурсЗапросЗаявкаНаКомпенсациюОтпуска());
	Описание.Вставить(МенеджерТипОбъекта.ЗаявкаДСВ, 					РесурсЗапросЗаявкаДСВ());
	Описание.Вставить(МенеджерТипОбъекта.РезультатСогласования, 		РесурсЗапросРезультатСогласования());
	Описание.Вставить(МенеджерТипОбъекта.ПравилоСогласования, 			РесурсЗапросПравилоСогласования());
	Описание.Вставить(МенеджерТипОбъекта.ГрафикОтпусковПредприятия, 	РесурсЗапросГрафикОтпусковПредприятия());
	Описание.Вставить(МенеджерТипОбъекта.ГрафикОтпусковПодразделения, 	РесурсЗапросГрафикОтпусковПодразделения());
	Описание.Вставить(МенеджерТипОбъекта.Оффер, 						РесурсЗапросОффера());
	Описание.Вставить(МенеджерТипОбъекта.ЗаявкаЗарплатаСразу, 			РесурсЗапросЗаявкаЗарплатаСразу());
	Описание.Вставить(МенеджерТипОбъекта.ОбращениеСотрудника, 			РесурсЗапросОбращениеСотрудника());
	Описание.Вставить(МенеджерТипОбъекта.ОстаткиОтпусков, 				РесурсЗапросОстаткиОтпусков());
	Описание.Вставить(МенеджерТипОбъекта.ЗаявкаПереносОтпуска, 			РесурсЗапросЗаявкаПереносОтпуска());
	Описание.Вставить(МенеджерТипОбъекта.ОтменаЗаявки, 					РесурсЗапросОтменаЗаявки());
	Описание.Вставить(МенеджерТипОбъекта.РабочееМестоОхраныТруда,		РесурсЗапросРабочиеМестаОхраныТруда());
	
	ТипОбъектаАдресРесурсаЗапрос = Описание; 
	
КонецПроцедуры 

Процедура ЗаполнитьТипОбъектаРазмерПакета()
	
	ТипОбъектаРазмерПакета = ИнтеграцияУправлениеПерсоналомОбмен.ТипОбъектаРазмерПакета(Приложение); 

КонецПроцедуры

Процедура ОбновитьВерсиюDTO()
	
	МенеджерКонвертации.ВерсияDTO = ВерсияDTO;
	МенеджерКонвертации.ОбновитьИнициализациюПриСмениВерсииDTO();
	
КонецПроцедуры

#КонецОбласти

#Область АдресаРесурсовAPI

Функция ВсеРесурсы() Экспорт

	ВсеРесурсы = Новый Массив;
	ВсеРесурсы.Добавить(РесурсФизическиеЛица());
	ВсеРесурсы.Добавить(РесурсЗапросФизическиеЛица());
	ВсеРесурсы.Добавить(РесурсФизическиеЛицаАктивные());
	ВсеРесурсы.Добавить(РесурсОрганизации());
	ВсеРесурсы.Добавить(РесурсЗапросОрганизации());
	ВсеРесурсы.Добавить(РесурсПодразделения());
	ВсеРесурсы.Добавить(РесурсЗапросПодразделения());
	ВсеРесурсы.Добавить(РесурсДолжности());
	ВсеРесурсы.Добавить(РесурсЗапросДолжности());
	ВсеРесурсы.Добавить(РесурсШтатноеРасписание());
	ВсеРесурсы.Добавить(РесурсЗапросШтатноеРасписание());
	ВсеРесурсы.Добавить(РесурсСотрудники());
	ВсеРесурсы.Добавить(РесурсЗапросСотрудники());
	ВсеРесурсы.Добавить(РесурсФайлы());
	ВсеРесурсы.Добавить(РесурсЗапросФайлы());
	ВсеРесурсы.Добавить(РесурсПолучениеВсехИзменений());
	ВсеРесурсы.Добавить(РесурсГрафикиРаботы());
	ВсеРесурсы.Добавить(РесурсЗапросГрафикиРаботы());
	ВсеРесурсы.Добавить(РесурсЗапросГрафикОтпусковПодразделения());
	ВсеРесурсы.Добавить(РесурсЗапросСправки2НДФЛ());
	ВсеРесурсы.Добавить(РесурсЗапросСправкиСРаботы());
	ВсеРесурсы.Добавить(РесурсЗапросОффера());
	ВсеРесурсы.Добавить(РесурсПинг());
	ВсеРесурсы.Добавить(РесурсЗапросЗаявкаНаОтпуск());
	ВсеРесурсы.Добавить(РесурсЗапросЗаявкаНалоговыйВычет());
	ВсеРесурсы.Добавить(РесурсЗапросЗаявкаИзменениеЛичныхДанных());
	ВсеРесурсы.Добавить(РесурсЗапросОтсутствие());
	ВсеРесурсы.Добавить(РесурсЗапросЗаявкаОстаткиОтпусков());
	ВсеРесурсы.Добавить(РесурсЗапросЗаявкаНаКомпенсациюОтпуска());
	ВсеРесурсы.Добавить(РесурсЗапросЗаявкаДСВ());
	ВсеРесурсы.Добавить(РесурсРезультатыСогласования());
	ВсеРесурсы.Добавить(РесурсЗапросРезультатСогласования());
	ВсеРесурсы.Добавить(РесурсЗапросПравилоСогласования());
	ВсеРесурсы.Добавить(РесурсГрафикОтпусковПредприятия());
	ВсеРесурсы.Добавить(РесурсЗапросГрафикОтпусковПредприятия());
	ВсеРесурсы.Добавить(РесурсПрименяемыеВычетыНДФЛ());
	ВсеРесурсы.Добавить(РесурсОграничениеДоступаКРабочимКонтактам());
	ВсеРесурсы.Добавить(РесурсДоступныеФункцииФизическогоЛица());
	ВсеРесурсы.Добавить(РесурсДокументыНаПодпись());
	ВсеРесурсы.Добавить(РесурсМашиночитаемыеДоверенности());
	ВсеРесурсы.Добавить(РесурсВидовПредоставляемыхСправок());
	ВсеРесурсы.Добавить(РесурсИнформацияОбИспользованииОтпуска());
	ВсеРесурсы.Добавить(РесурсГрафикОтпусков());
	ВсеРесурсы.Добавить(РесурсБудущееИзменениеОстаткаОтпусков());
	ВсеРесурсы.Добавить(РесурсПраздничныеДниГрафиковРаботы());
	ВсеРесурсы.Добавить(РесурсДанныеГрафикаРаботы());
	ВсеРесурсы.Добавить(РесурсПлановыеУдержания());
	ВсеРесурсы.Добавить(РесурсВидыСоставныхЧастейЗарплаты());
	ВсеРесурсы.Добавить(РесурсРасчетныеЛистки());
	ВсеРесурсы.Добавить(РесурсСправки2НДФЛ());
	ВсеРесурсы.Добавить(РесурсСправкиСМестаРаботы());
	ВсеРесурсы.Добавить(РесурсСправкиОстаткиОтпусков());
	ВсеРесурсы.Добавить(РесурсСогласияНаПрисоединениеККЭДО());
	ВсеРесурсы.Добавить(РесурсИспользуемаяФункциональность());
	ВсеРесурсы.Добавить(РесурсНастройкиЗаявленийНаОтпуск());
	ВсеРесурсы.Добавить(РесурсДоступностьПриложения());
	ВсеРесурсы.Добавить(РесурсНастройкиОтсутствий());
	
	Если ИспользуетсяВерсия("2.1") Тогда
		ВсеРесурсы.Добавить(РесурсШаблонДокумента());
		ВсеРесурсы.Добавить(РесурсШаблонДокументаЗапросСправки2НДФЛ());
		ВсеРесурсы.Добавить(РесурсШаблонДокументаЗаявкаОстаткиОтпусков());
		ВсеРесурсы.Добавить(РесурсШаблонДокументаЗапросСправкиСМестаРаботы());
		ВсеРесурсы.Добавить(РесурсШаблонДокументаЗаявкаНаКомпенсациюОтпуска());
		ВсеРесурсы.Добавить(РесурсШаблонДокументаЗаявкаНалоговыйВычет());
		ВсеРесурсы.Добавить(РесурсШаблонДокументаЗаявкаНаОтпуск());
		ВсеРесурсы.Добавить(РесурсШаблонДокументаЗаявкаДСВ());
		ВсеРесурсы.Добавить(РесурсШаблонДокументаЗаявкаИзменениеЛичныхДанных());
		ВсеРесурсы.Добавить(РесурсШаблонДокументаОтсутствие());
		ВсеРесурсы.Добавить(РесурсЗапросОбращениеСотрудника());
	КонецЕсли;
	
	Если ИспользуетсяВерсия("2.2") Тогда
		ВсеРесурсы.Добавить(РесурсВидыОтпусков());
		ВсеРесурсы.Добавить(РесурсОстаткиОтпусков());
		ВсеРесурсы.Добавить(РесурсНастройкиСервисаПодписания());
		ВсеРесурсы.Добавить(РесурсШаблонДокументаЗаявкаНаПереносОтпуска());
		ВсеРесурсы.Добавить(РесурсШаблонДокументаЗаявкаНаОтмену());
		ВсеРесурсы.Добавить(РесурсЗапросРабочиеМестаОхраныТруда());
		ВсеРесурсы.Добавить(РесурсРабочиеМестаОхраныТруда());
	КонецЕсли;
	
	Возврат ВсеРесурсы;

КонецФункции

Функция АдресРесурсаAPI()
	
	РесурсAPI = "/api";
	
	Если Не ПустаяСтрока(ВерсияAPI) Тогда
		РесурсAPI = СтрШаблон("%1/v%2", РесурсAPI, ВерсияAPI);
	КонецЕсли;
	
	Возврат РесурсAPI;
	
КонецФункции

Функция РесурсВерсияAPI()

	Возврат "/api/apiVersions";

КонецФункции

Функция РесурсФизическиеЛица()
	
	Возврат "/persons";
	
КонецФункции

Функция РесурсЗапросФизическиеЛица()
	
	Возврат "/persons/{ID}";
	
КонецФункции

Функция РесурсФизическиеЛицаАктивные()

	Возврат "/persons/active";

КонецФункции

Функция РесурсФизическиеЛицаНеактивные()

	Возврат "/persons/inactive"; 

КонецФункции

Функция РесурсОрганизации()
	
	Возврат "/employers";
	
КонецФункции 

Функция РесурсЗапросОрганизации()
	
	Возврат "/employers/{ID}";
	
КонецФункции

Функция РесурсПодразделения()
	
	Возврат "/divisions";
	
КонецФункции

Функция РесурсЗапросПодразделения()
	
	Возврат "/divisions/{ID}";
	
КонецФункции

Функция РесурсДолжности()
	
	Возврат "/positions";
	
КонецФункции

Функция РесурсЗапросДолжности()
	
	Возврат "/positions/{ID}";
	
КонецФункции

Функция РесурсШтатноеРасписание()
	
	Возврат "/stafflist-positions";
	
КонецФункции

Функция РесурсЗапросШтатноеРасписание()
	
	Возврат "/stafflist-positions/{ID}";
	
КонецФункции

Функция РесурсСотрудники()
	
	Возврат "/employees";
	
КонецФункции

Функция РесурсЗапросСотрудники()
	
	Возврат "/employees/{ID}";
	
КонецФункции

Функция РесурсФайлы()
	
	Возврат "/files";
	
КонецФункции

Функция РесурсЗапросФайлы()
	
	Возврат "/files/{ID}";
	
КонецФункции

Функция РесурсПолучениеВсехИзменений()
	
	Возврат "/data/updates";
	
КонецФункции

Функция РесурсГрафикиРаботы()
	
	Возврат "/work-schedules";
	
КонецФункции

Функция РесурсЗапросГрафикиРаботы()
	
	Возврат "/work-schedules/{ID}";
	
КонецФункции

Функция РесурсЗапросГрафикОтпусковПодразделения()
	
	Возврат "/vacations-schedule-draft/divisions/{ID}";
	
КонецФункции

Функция РесурсЗапросСправки2НДФЛ()
	
	Возврат "/forms-2-ndfl/requests/{ID}";
	
КонецФункции

Функция РесурсЗапросСправкиСРаботы()
	
	Возврат "/employment-certificates/requests/{ID}";
	
КонецФункции

Функция РесурсЗапросОффера()
	
	Возврат "/recruitment/offers/{ID}";
	
КонецФункции

Функция РесурсПинг()
	
	Возврат "/ping";
	
КонецФункции

Функция РесурсЗапросЗаявкаНаОтпуск()
	
	Возврат "/vacations/requests/{ID}";
	
КонецФункции

Функция РесурсЗапросЗаявкаНалоговыйВычет()
	
	Возврат "/tax-deductions/requests/{ID}";
	
КонецФункции

Функция РесурсЗапросЗаявкаИзменениеЛичныхДанных()
	
	Возврат "/office-owners/information-change-requests/{ID}";
	
КонецФункции 

Функция РесурсЗапросОтсутствие()
	
	Возврат "/absences/{ID}";
	
КонецФункции

Функция РесурсЗапросЗаявкаОстаткиОтпусков()
	
	Возврат "/vacations/balance-reports/requests/{ID}";
	
КонецФункции 

Функция РесурсЗапросЗаявкаНаКомпенсациюОтпуска()
	
	Возврат "/leave-encashment-requests/requests/{ID}";
	
КонецФункции

Функция РесурсЗапросЗаявкаДСВ()
	
	Возврат "/contributions/requests/{ID}";
	
КонецФункции

Функция РесурсРезультатыСогласования()
	
	Возврат "/agreement-results";
	
КонецФункции

Функция РесурсЗапросРезультатСогласования()
	
	Возврат "/agreement-results/{ID}";
	
КонецФункции

Функция РесурсЗапросПравилоСогласования()
	
	Возврат "/agreement-rules/{ID}";
	
КонецФункции 

Функция РесурсГрафикОтпусковПредприятия()

	Возврат "/vacations-schedule-draft/enterprise";

КонецФункции

Функция РесурсЗапросГрафикОтпусковПредприятия()
	
	Возврат "/vacations-schedule-draft/enterprise/{ID}";
	
КонецФункции

Функция РесурсПрименяемыеВычетыНДФЛ()
	
	Возврат "/tax-deductions";
	
КонецФункции

Функция РесурсОграничениеДоступаКРабочимКонтактам()
	
	Возврат "/office-owners/work-contacts-access";
	
КонецФункции

Функция РесурсДоступныеФункцииФизическогоЛица()

	Возврат "/office-owners/options";

КонецФункции

Функция РесурсДокументыНаПодпись()

	Возврат "/documents-to-be-signed";

КонецФункции

Функция РесурсМашиночитаемыеДоверенности()
	
	Возврат "/power-of-attorneys";
	
КонецФункции

Функция РесурсВидовПредоставляемыхСправок()

	Возврат "/employment-certificates/types";

КонецФункции

Функция РесурсИнформацияОбИспользованииОтпуска()
	
	Возврат "/vacations/summaries";
	
КонецФункции

Функция РесурсГрафикОтпусков() 
	
	Возврат "/vacations-schedule";
	
КонецФункции

Функция РесурсБудущееИзменениеОстаткаОтпусков()
	
	Возврат "/vacations/unused-days-predicted";
	
КонецФункции

Функция РесурсПраздничныеДниГрафиковРаботы()
	
	Возврат "/holidays";
	
КонецФункции

Функция РесурсДанныеГрафикаРаботы()

	Возврат "/work-schedules-data";

КонецФункции

Функция РесурсПлановыеУдержания()

	Возврат "/contributions";

КонецФункции

Функция РесурсВидыСоставныхЧастейЗарплаты()
	
	Возврат "/payslip-component-types";
	
КонецФункции

Функция РесурсРасчетныеЛистки()
	
	Возврат "/payslips";
	
КонецФункции

Функция РесурсСправки2НДФЛ()
	
	Возврат "/forms-2-ndfl";
	
КонецФункции

Функция РесурсСправкиСМестаРаботы()
	
	Возврат "/employment-certificates";
	
КонецФункции

Функция РесурсСправкиОстаткиОтпусков()
	
	Возврат "/vacations/balance-reports";
	
КонецФункции

Функция РесурсСогласияНаПрисоединениеККЭДО()
	
	Возврат "/join-to-personnel-edm-consent";
	
КонецФункции

Функция РесурсИспользуемаяФункциональность()

	Возврат "/used-features";

КонецФункции

Функция РесурсНастройкиЗаявленийНаОтпуск()

	Возврат "/vacations-settings";

КонецФункции

Функция РесурсДоступностьПриложения()

	Возврат "/application/access";

КонецФункции

Функция РесурсШаблонДокумента()
	
	Если ИспользуетсяВерсия("2.1") Тогда
		Возврат "/document-templates";
	Иначе
		Возврат "";
	КонецЕсли;

КонецФункции

Функция РесурсШаблонДокументаЗапросСправки2НДФЛ()
	
	Если ИспользуетсяВерсия("2.1") Тогда
		Возврат "/forms-2-ndfl/document-template";
	Иначе
		Возврат "";
	КонецЕсли;

КонецФункции

Функция РесурсШаблонДокументаЗаявкаОстаткиОтпусков()
	
	Если ИспользуетсяВерсия("2.1") Тогда
		Возврат "/vacations/balance-reports/document-template";
	Иначе
		Возврат "";
	КонецЕсли;

КонецФункции

Функция РесурсШаблонДокументаЗапросСправкиСМестаРаботы()
	
	Если ИспользуетсяВерсия("2.1") Тогда
		Возврат "/employment-certificates/document-template";
	Иначе
		Возврат "";
	КонецЕсли;

КонецФункции

Функция РесурсШаблонДокументаЗаявкаНаКомпенсациюОтпуска()
	
	Если ИспользуетсяВерсия("2.1") Тогда
		Возврат "/leave-encashment-requests/document-template";
	Иначе
		Возврат "";
	КонецЕсли;

КонецФункции

Функция РесурсШаблонДокументаЗаявкаНалоговыйВычет()
	
	Если ИспользуетсяВерсия("2.1") Тогда
		Возврат "/tax-deductions/document-template";
	Иначе
		Возврат "";
	КонецЕсли;

КонецФункции

Функция РесурсШаблонДокументаЗаявкаНаОтпуск()
	
	Если ИспользуетсяВерсия("2.1") Тогда
		Возврат "/vacations/document-template";
	Иначе
		Возврат "";
	КонецЕсли;

КонецФункции

Функция РесурсШаблонДокументаЗаявкаДСВ()
	
	Если ИспользуетсяВерсия("2.1") Тогда
		Возврат "/contributions/document-template";
	Иначе
		Возврат "";
	КонецЕсли;

КонецФункции

Функция РесурсШаблонДокументаЗаявкаИзменениеЛичныхДанных()
	
	Если ИспользуетсяВерсия("2.1") Тогда
		Возврат "/office-owners/document-template";
	Иначе
		Возврат "";
	КонецЕсли;

КонецФункции

Функция РесурсШаблонДокументаОтсутствие()
	
	Если ИспользуетсяВерсия("2.1") Тогда
		Возврат "/absences/document-template";
	Иначе
		Возврат "";
	КонецЕсли;

КонецФункции

Функция РесурсЗапросОбращениеСотрудника()
	
	Если ИспользуетсяВерсия("2.1") Тогда
		Возврат "/employee-documents/{ID}";
	Иначе
		Возврат "";
	КонецЕсли;

КонецФункции

Функция РесурсЗапросЗаявкаЗарплатаСразу()
	
	Если ИспользуетсяВерсия("2.1") Тогда
		Возврат "/payment-request/{ID}";
	Иначе
		Возврат "";
	КонецЕсли;

КонецФункции

Функция РесурсНастройкиОтсутствий()
	
	Возврат "/absences/settings";

КонецФункции

Функция РесурсВидыОтпусков()
	
	Если ИспользуетсяВерсия("2.2") Тогда
		Возврат "/vacations/vacation-types";
	Иначе
		Возврат "";
	КонецЕсли;

КонецФункции

Функция РесурсОстаткиОтпусков()
	
	Если ИспользуетсяВерсия("2.2") Тогда
		Возврат "/vacations/unused-days-current";
	Иначе
		Возврат "";
	КонецЕсли;

КонецФункции

Функция РесурсЗапросОстаткиОтпусков()
	
	Если ИспользуетсяВерсия("2.2") Тогда
		Возврат "/vacations/unused-days-current/{ID}";
	Иначе
		Возврат "";
	КонецЕсли;

КонецФункции

Функция РесурсЗапросЗаявкаПереносОтпуска()
	
	Если ИспользуетсяВерсия("2.2") Тогда
		Возврат "/change-vacation-requests/requests/{ID}";
	Иначе
		Возврат "";
	КонецЕсли;

КонецФункции

Функция РесурсЗапросОтменаЗаявки()
	
	Если ИспользуетсяВерсия("2.2") Тогда
		Возврат "/cancel-requests/requests/{ID}";
	Иначе
		Возврат "";
	КонецЕсли;

КонецФункции

Функция РесурсИнформацияОПриложении()

	Возврат "/application/info";

КонецФункции

Функция РесурсНастройкиПриложения()

	Возврат "/application/settings";

КонецФункции

Функция РесурсАдминистратор()

	Возврат "/administrators";

КонецФункции

Функция РесурсНастройкиСервисаПодписания()
	
	Если ИспользуетсяВерсия("2.2") Тогда
		Возврат "/xsign-settings";
	Иначе
		Возврат "";
	КонецЕсли;

КонецФункции

Функция РесурсШаблонДокументаЗаявкаНаПереносОтпуска()
	
	Если ИспользуетсяВерсия("2.2") Тогда
		Возврат "/change-vacation-requests/document-template";	
	Иначе
		Возврат "";
	КонецЕсли;

КонецФункции

Функция РесурсШаблонДокументаЗаявкаНаОтмену()
	
	Если ИспользуетсяВерсия("2.2") Тогда
		Возврат "/cancel-requests/document-template";
	Иначе
		Возврат "";
	КонецЕсли;

КонецФункции

Функция РесурсЗапросРабочиеМестаОхраныТруда()
	
	Если ИспользуетсяВерсия("2.2") Тогда
		Возврат "/workplaces-safety/{ID}";
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

Функция РесурсРабочиеМестаОхраныТруда()
	
	Если ИспользуетсяВерсия("2.2") Тогда
		Возврат "/workplaces-safety";
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область Обмен

// Выполняет проверку настроек подключения к приложению.
//
// Параметры:
// 		НастройкиПодключения - Структура
// 			* АдресПриложения - Строка
// 			* Ключи - Структура
// 				* ИдентификаторКлиента
// 				* СекретКлиента
//
Функция ПроверитьНастройкиПодключения(НастройкиПодключения) Экспорт
	
	Результат = Новый Структура("КодСостояния,СообщениеОбОшибке");
	
	АдресПриложения = НастройкиПодключения.АдресПриложения;
	СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI(АдресПриложения);
	СоздатьНовоеHTTPСоединение();
	
	Ответ = НовыйТокен(НастройкиПодключения.Ключи);
	Токен = Ответ.Токен;
	Если Ответ.НеактуальныеКлючи Тогда
		Результат.СообщениеОбОшибке = НСтр("ru = 'Неактуальные ключи аутентификации.';
											|en = 'Outdated authentication keys.'");
	ИначеЕсли Не ЗначениеЗаполнено(Ответ.Токен) Тогда
		Результат.СообщениеОбОшибке = НСтр("ru = 'Не удалось получить ответ сервера. Подробнее см в Журнале регистрации.';
											|en = 'Cannot receive a server response. For more information, see the event log.'");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Результат.СообщениеОбОшибке) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Ответ = Неопределено; 
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("User-Agent", ОписаниеКлиентскогоПриложения);
	Заголовки.Вставить("formatVersion", ВерсияDTO);
	Заголовки.Вставить("Authorization", СтрШаблон("Bearer %1", Токен));
	Заголовки.Вставить("Content-Type", "application/json");
	
	ПолныйАдресРесурса = СтрШаблон("/%1%2%3", СтруктураURI.ПутьНаСервере, АдресРесурсаAPI, РесурсПинг());
	Запрос = Новый HTTPЗапрос(ПолныйАдресРесурса, Заголовки);
	ИмяМетода = "GET";
	Попытка
		Ответ = HTTPСоединение.ВызватьHTTPМетод(ИмяМетода, Запрос);
	Исключение
		ЗаписатьОшибкуПолученияОтвета(ИмяМетода, Запрос, ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Результат.СообщениеОбОшибке = НСтр("ru = 'Не удалось получить ответ сервера. Подробнее см в Журнале регистрации.';
											|en = 'Cannot receive a server response. For more information, see the event log.'");
	КонецПопытки;
	
	ОбъектВерсииФормата = Неопределено;
	Если Ответ = Неопределено Тогда
		Результат.КодСостояния = Неопределено;
	Иначе
		Результат.КодСостояния = Ответ.КодСостояния;
		Если Ответ.КодСостояния >= 300 Тогда
			ЗаписатьОшибкуВызова(ИмяМетода, Запрос, Ответ, Ложь);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

Процедура ПроверитьВерсиюФорматаОбмена() Экспорт
	
	Ответ = РезультатЗапросаВерсииФормата();
	Если Ответ.Использовать = Истина Тогда
		Если ЗначениеЗаполнено(Ответ.ОбъектВерсииФормата) Тогда
			УстановитьВерсииФорматовОбмена(Ответ.ОбъектВерсииФормата);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция ВерсииФорматаОбменаПриложения() Экспорт
	
	Возврат РезультатЗапросаВерсииФормата();
	
КонецФункции

Функция РезультатВыгрузкиУдаления(ОбъектыКУдалению, ТипОбъекта) Экспорт
	
	Результат = НовыйРезультатВыгрузки();
	
	АдресРесурса = ТипОбъектаАдресРесурса[ТипОбъекта];
	Если Не ЗначениеЗаполнено(АдресРесурса) Тогда
		БылиОшибки = Истина;
		ЗаписатьОшибкуНеНайденАдресРесурса(ТипОбъекта);
		Возврат Результат;
	КонецЕсли;

	// Соответствие: Ключ - Ссылка, Значение - Идентификатор.
	СсылкаПубличныйИдентификатор = ИнтеграцияУправлениеПерсоналомОбмен.СсылкаПубличныйИдентификатор(ОбъектыКУдалению, ТипОбъекта);
	Для каждого Ссылка Из ОбъектыКУдалению Цикл
		Идентификатор = СсылкаПубличныйИдентификатор[Ссылка];
		Если Не ЗначениеЗаполнено(Идентификатор) Тогда
			Результат.ОтменитьРегистрацию(Ссылка);
		ИначеЕсли ВыгрузитьУдалениеОбъекта(АдресРесурса, Идентификатор) Тогда
			Результат.Выгружено.Добавить(Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	Ссылки = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Ссылки, Результат.Выгружено);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Ссылки, Результат.ОтменитьРегистрацию);
	
	УстановитьОтметкуНеВыгружался(Ссылки, ТипОбъекта);
	
	Возврат Результат;

КонецФункции

Функция РезультатВыгрузкиОбъектов(КоллекцияОбъектов, ТипОбъекта, АдресРесурса = "") Экспорт
	
	Результат = НовыйРезультатВыгрузки();
	
	Если Не ЗначениеЗаполнено(КоллекцияОбъектов) Тогда
		Возврат Результат;
	КонецЕсли;
	
	КоллекцияДляВыгрузки = КоллекцияОбъектов;
	Если ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ФизическоеЛицо Тогда
		ВыгрузитьФотографии(КоллекцияОбъектов);
	Иначе
		ИменаСвойствФайл = МенеджерКонвертации.ИменаСвойствФайл(ТипОбъекта);
		Если ЗначениеЗаполнено(ИменаСвойствФайл) Тогда
			КоллекцияДляВыгрузки = ВыгрузитьФайлы(КоллекцияОбъектов, ИменаСвойствФайл);
		КонецЕсли;
		Если ЕстьСвойствоЭлектронныйДокумент(КоллекцияОбъектов) Тогда
			КоллекцияДляВыгрузки = ВыгрузитьФыйлыЭлектронныхДокументов(КоллекцияДляВыгрузки);
		КонецЕсли;
	КонецЕсли;
	
	РезультатКонвертации = МенеджерКонвертации.ВыполнитьКонвертациюВОбъектыDTO(КоллекцияДляВыгрузки, ТипОбъекта);
	Если Не ЗначениеЗаполнено(РезультатКонвертации.Данные) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если ПустаяСтрока(АдресРесурса) Тогда
		АдресРесурса = ТипОбъектаАдресРесурса[ТипОбъекта];
	КонецЕсли;
	Если Не ЗначениеЗаполнено(АдресРесурса) Тогда
		Результат.БылиОшибки = Истина;
		ЗаписатьОшибкуНеНайденАдресРесурса(ТипОбъекта);
		Возврат Результат;
	КонецЕсли;
	
	РазмерПакета = ТипОбъектаРазмерПакета[ТипОбъекта];
	Если Не ЗначениеЗаполнено(РазмерПакета) Тогда
		РазмерПакета = 100;
	КонецЕсли;
	
	ОписаниеТипаОбъекта = МенеджерКонвертации.ОписаниеТипаОбъекта[ТипОбъекта];
	ПолеКлюча = "";
	ТипОбъектаКлюча = Неопределено;
	РегистрироватьВыгрузку = Ложь;
	Если ЗначениеЗаполнено(ОписаниеТипаОбъекта) Тогда
		ПолеКлюча 				= ОписаниеТипаОбъекта.ПолеКлюча;
		ТипОбъектаКлюча 		= ОписаниеТипаОбъекта.ТипОбъектаКлюча;
		РегистрироватьВыгрузку 	= ОписаниеТипаОбъекта.РегистрироватьВыгрузку;
	КонецЕсли;
	
	РезультатВыгрузки = РезультатВыгрузкиКоллекции(АдресРесурса, РезультатКонвертации.Данные, ПолеКлюча, РазмерПакета);
		
	Результат.БылиОшибки = РезультатВыгрузки.БылиОшибки;
	БылиОшибки = БылиОшибки Или РезультатВыгрузки.БылиОшибки;
	
	// Для результатов согласования и ответов по заявкам.
	// Не найденные в приложении объекты с таким идентификатором, регистрируем как успешно выгруженные.
	Если ЗначениеЗаполнено(РезультатВыгрузки.НеВыгружено) И ПроверятьОшибкуНаличияОбъекта(ТипОбъекта) Тогда
		УдалитьЗначение = Новый Массив;
		Для Каждого Ошибка Из РезультатВыгрузки.НеВыгружено Цикл
			Если (ЗначениеЗаполнено(Ошибка.Значение) И СтрНайти(Ошибка.Значение, "objectNotFound") > 0) Тогда
				РезультатВыгрузки.Выгружено.Добавить(Ошибка.Ключ);
				УдалитьЗначение.Добавить(Ошибка.Ключ);
			КонецЕсли;
		КонецЦикла;
		Для каждого ЭлементКоллекции Из УдалитьЗначение Цикл
			РезультатВыгрузки.НеВыгружено.Удалить(ЭлементКоллекции);
		КонецЦикла;
	КонецЕсли;
	
	НеВыгружено = Новый Массив;
	Для каждого КлючИЗначение Из РезультатВыгрузки.НеВыгружено Цикл
		НеВыгружено.Добавить(КлючИЗначение.Ключ);
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ТипОбъектаКлюча) Тогда
		СсылкиНаВыгруженныеОбъекты = ОбъектыПоИдентификаторам(Приложение, РезультатВыгрузки.Выгружено, ТипОбъектаКлюча, РегистрироватьВыгрузку);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат.Выгружено, СсылкиНаВыгруженныеОбъекты, Истина);
		СсылкиНаНеВыгруженныеОбъекты = ОбъектыПоИдентификаторам(Приложение, НеВыгружено, ТипОбъектаКлюча, Ложь);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат.НеВыгружено, СсылкиНаНеВыгруженныеОбъекты);
	Иначе
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат.Выгружено, РезультатВыгрузки.Выгружено);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат.НеВыгружено, НеВыгружено);
	КонецЕсли;

	Возврат Результат;
	
КонецФункции

Функция РезультатВыгрузкиШаблоновДокументов(КоллекцияОбъектов, ТипШаблона) Экспорт
	
	Результат = НовыйРезультатВыгрузки();
	
	АдресРесурса = АдресРесурсаШаблонаДокументов(ТипШаблона);
	Если Не ЗначениеЗаполнено(АдресРесурса) Тогда
		Результат.БылиОшибки = Истина;
		ШаблонОписания = НСтр("ru = 'Шаблон документа: %1';
								|en = 'Document template: %1'");
		Комментарий = СтрШаблон(ШаблонОписания, Строка(ТипШаблона));
		ЗаписатьОшибкуНеНайденАдресРесурса(Комментарий);
		Возврат Результат;
	КонецЕсли;
	
	ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ШаблонДокумента;
	Если ЗначениеЗаполнено(ТипШаблона) Тогда
		// Предопределенный шаблон выгружается как один объект.
		РезультатКонвертации = МенеджерКонвертации.ВыполнитьКонвертациюВОбъектыDTO(КоллекцияОбъектов, ТипОбъекта);
		Если Не ЗначениеЗаполнено(РезультатКонвертации.Данные) Тогда
			Возврат Результат;
		КонецЕсли;
		ОбъектDTO = РезультатКонвертации.Данные[0];
		
		ОбъектВыгружен = РезультатВыгрузкиОбъекта(АдресРесурса, ОбъектDTO);
		Результат.БылиОшибки = Не ОбъектВыгружен;
		
		Если ОбъектВыгружен Тогда 

			ОписаниеТипаОбъекта = МенеджерКонвертации.ОписаниеТипаОбъекта[ТипОбъекта];
			ПолеКлюча = "";
			ТипОбъектаКлюча = Неопределено;
			РегистрироватьВыгрузку = Ложь;
			Если ЗначениеЗаполнено(ОписаниеТипаОбъекта) Тогда
				ПолеКлюча 				= ОписаниеТипаОбъекта.ПолеКлюча;
				ТипОбъектаКлюча 		= ОписаниеТипаОбъекта.ТипОбъектаКлюча;
				РегистрироватьВыгрузку 	= ОписаниеТипаОбъекта.РегистрироватьВыгрузку;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ПолеКлюча) Тогда
				ВыгруженныйОбъект = ОбъектDTO[ПолеКлюча];
			Иначе
				ВыгруженныйОбъект = КоллекцияОбъектов[0];
			КонецЕсли;
			
			ВыгруженныеОбъекты = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ВыгруженныйОбъект);
			Если ЗначениеЗаполнено(ТипОбъектаКлюча) Тогда
				СсылкиНаВыгруженныеОбъекты = ОбъектыПоИдентификаторам(Приложение, ВыгруженныеОбъекты, ТипОбъектаКлюча, РегистрироватьВыгрузку);
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат.Выгружено, СсылкиНаВыгруженныеОбъекты, Истина);
			Иначе
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат.Выгружено, ВыгруженныеОбъекты);
			КонецЕсли;
			
		КонецЕсли;
		
		Возврат Результат;
	
	Иначе
		// Произвольные шаблоны выгружаются как коллекция.
		Возврат РезультатВыгрузкиОбъектов(КоллекцияОбъектов, ТипОбъекта, АдресРесурса);	
	КонецЕсли;

КонецФункции

Функция РезультатВыгрузкиНастроек(Объект, ТипОбъекта) Экспорт
	
	Результат = НовыйРезультатВыгрузки();
	
	Если Не ЗначениеЗаполнено(Объект) Тогда
		Возврат Результат;
	КонецЕсли;

	РезультатКонвертации = МенеджерКонвертации.ВыполнитьКонвертациюВОбъектыDTO(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект), ТипОбъекта);
	Если Не ЗначениеЗаполнено(РезультатКонвертации.Данные) Тогда
		Возврат Результат;
	КонецЕсли;
	
	ОбъектDTO = РезультатКонвертации.Данные[0];
	
	АдресРесурса = ТипОбъектаАдресРесурса[ТипОбъекта];
	Если Не ЗначениеЗаполнено(АдресРесурса) Тогда
		Результат.БылиОшибки = Истина;
		ЗаписатьОшибкуНеНайденАдресРесурса(ТипОбъекта);
		Возврат Результат;
	КонецЕсли;
	
	ОбъектВыгружен = РезультатВыгрузкиОбъекта(АдресРесурса, ОбъектDTO);
	Результат.БылиОшибки = Результат.БылиОшибки Или Не ОбъектВыгружен;
		
	Возврат Результат;
	
КонецФункции

Функция ПовторнаяЗагрузкаОбъектов(ТипОбъекта, Идентификаторы) Экспорт
	
	Результат = Новый Структура("БылиОшибки,ОбъектыDTO,НетОбъектов", Ложь);
	
	ШаблонАдресРесурса = ТипОбъектаАдресРесурсаЗапрос[ТипОбъекта];
	Если Не ЗначениеЗаполнено(ШаблонАдресРесурса) Тогда
		БылиОшибки = Истина;
		ЗаписатьОшибкуНеНайденАдресРесурсаЗагрузкиОбъекта(ТипОбъекта);
		Возврат Результат;
	КонецЕсли;
	
	ОбъектыDTO = Новый Массив;
	НетОбъектов = Новый Массив;
	Для каждого Идентификатор Из Идентификаторы Цикл
		
		АдресРесурса = СтрЗаменить(ШаблонАдресРесурса, "{ID}", Идентификатор);
		Ответ = HTTPОтветСервера(ПараметрыЗапроса(АдресРесурса, "GET"));
		Если Ответ = Неопределено Тогда
			Результат.БылиОшибки = Истина;
		ИначеЕсли Ответ.КодСостояния = 200 Тогда
			ЧтениеJSON = Новый ЧтениеJSON;
			ЧтениеJSON.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
			ИменаСвойствСоЗначениямиДата = МенеджерКонвертации.ИменаСвойствСоЗначениямиДата();
			ОбъектDTO = ПрочитатьJSON(ЧтениеJSON, Истина, ИменаСвойствСоЗначениямиДата,
				ФорматДатыJSON.ISO, "ВосстановлениеJSON", ЭтотОбъект, ПараметрыВосстановленияJSON(), ИменаСвойствСоЗначениямиДата);
			Если ТипЗнч(ОбъектDTO) = Тип("Массив") Тогда
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ОбъектыDTO, ОбъектDTO);
			Иначе
				ОбъектыDTO.Добавить(ОбъектDTO);
			КонецЕсли;
		ИначеЕсли Ответ.КодСостояния = 404 Тогда
			// объект не найден
			НетОбъектов.Добавить(Идентификатор);
		Иначе
			Результат.БылиОшибки = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Результат.ОбъектыDTO 	= ОбъектыDTO;
	Результат.НетОбъектов 	= НетОбъектов;
	
	Возврат Результат;

КонецФункции

Функция ЗагрузитьИзменения(ВерсияИзменений, Лимит) Экспорт

	Результат = ИнтеграцияУправлениеПерсоналомОбмен.НовыйРезультатЗагрузкиИзменений();
	
	Версия = Формат(ВерсияИзменений + 1, "ЧН=0; ЧГ=0");
	АдресРесурса = РесурсПолучениеВсехИзменений();
	АдресРесурса = СтрШаблон("%1?version=%2&limit=%3", АдресРесурса, Версия, Лимит);

	Ответ = HTTPОтветСервера(ПараметрыЗапроса(АдресРесурса, "GET"));
	Если Ответ = Неопределено Тогда
		Результат.БылиОшибки = Истина;
	ИначеЕсли Ответ.КодСостояния = 200 Тогда
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
		ИменаСвойствСоЗначениямиДата = МенеджерКонвертации.ИменаСвойствСоЗначениямиДата();
		Результат.ОбъектВсеИзменения = ПрочитатьJSON(ЧтениеJSON, Истина, ИменаСвойствСоЗначениямиДата,
			ФорматДатыJSON.ISO, "ВосстановлениеJSON", ЭтотОбъект, ПараметрыВосстановленияJSON(), ИменаСвойствСоЗначениямиДата);
	ИначеЕсли Ответ.КодСостояния = 204 Тогда
		// нет изменений
	Иначе
		Результат.БылиОшибки = Истина;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

Функция РезультатЗагрузкиФайла(ИдентификаторФайла) Экспорт
	
	Результат = Новый Структура("ОписаниеОшибки,ДвоичныеДанныеФайла");
	
	ШаблонРесурса = РесурсЗапросФайлы();
	РесурсСервиса = СтрЗаменить(ШаблонРесурса,"{ID}", ИдентификаторФайла);
	ПараметрыЗапроса = ПараметрыЗапроса(РесурсСервиса, "GET");
	ПараметрыЗапроса.ЗапросФайла = Истина;
	Ответ = HTTPОтветСервера(ПараметрыЗапроса);
	Если Ответ = Неопределено Или Не Ответ.КодСостояния = 200 Тогда
		ШаблонОписания = НСтр("ru = 'Ошибка загрузки файла %1 из приложения.';
								|en = 'An error occurred when importing the %1 file from the application.'");
		Результат.ОписаниеОшибки = СтрШаблон(ШаблонОписания, ИдентификаторФайла);
	Иначе
		Результат.ДвоичныеДанныеФайла = Ответ.ПолучитьТелоКакДвоичныеДанные();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция РезультатАктивацииФизическихЛиц(Данные) Экспорт

	АдресРесурса = РесурсФизическиеЛицаАктивные();
	
	ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ФизическоеЛицо;
	РазмерПакета = ТипОбъектаРазмерПакета[ТипОбъекта];
	Если Не ЗначениеЗаполнено(РазмерПакета) Тогда
		РазмерПакета = 100;
	КонецЕсли;
	
	РезультатВыгрузки = РезультатВыгрузкиКоллекции(АдресРесурса, Данные, "", РазмерПакета);
	
	БылиОшибки = БылиОшибки Или РезультатВыгрузки.БылиОшибки;
	
	Возврат РезультатВыгрузки;

КонецФункции

Функция РезультатДеактивацииФизическихЛиц(Данные) Экспорт

	АдресРесурса = РесурсФизическиеЛицаНеактивные();
	
	ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ФизическоеЛицо;
	РазмерПакета = ТипОбъектаРазмерПакета[ТипОбъекта];
	Если Не ЗначениеЗаполнено(РазмерПакета) Тогда
		РазмерПакета = 100;
	КонецЕсли;
	
	РезультатВыгрузки = РезультатВыгрузкиКоллекции(АдресРесурса, Данные, "", РазмерПакета);
	
	БылиОшибки = БылиОшибки Или РезультатВыгрузки.БылиОшибки;
	
	Возврат РезультатВыгрузки;

КонецФункции

Функция ДоступностьПриложения() Экспорт
	
	ОбъектКонфигурации = Неопределено;
	
	АдресРесурса = РесурсДоступностьПриложения();
	Ответ = HTTPОтветСервера(ПараметрыЗапроса(АдресРесурса, "GET"));
	Если Ответ <> Неопределено И Ответ.КодСостояния = 200 Тогда
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
		Попытка
			ОбъектDTO = ПрочитатьJSON(ЧтениеJSON, Истина);
			ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ДоступностьПриложения;
			ОбъектКонфигурации = МенеджерКонвертации.ВыполнитьКонвертациюИзОбъектовDTO(ОбъектDTO, ТипОбъекта);
		Исключение
			ЗаписатьОшибкуЧтенияJSON(Ответ.ПолучитьТелоКакСтроку(), ОписаниеОшибки());
		КонецПопытки;
			
	КонецЕсли;
	
	Возврат ОбъектКонфигурации;
	
КонецФункции

Функция ИдентификаторыАктивныхФизическихЛиц() Экспорт

	Идентификаторы = Неопределено;
	
	АдресРесурса = РесурсФизическиеЛицаАктивные();
	Ответ = HTTPОтветСервера(ПараметрыЗапроса(АдресРесурса, "GET"));
	Если Ответ <> Неопределено И Ответ.КодСостояния = 200 Тогда
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
		Попытка
			Идентификаторы = ПрочитатьJSON(ЧтениеJSON, Истина);
		Исключение
			ЗаписатьОшибкуЧтенияJSON(Ответ.ПолучитьТелоКакСтроку(), ОписаниеОшибки());
		КонецПопытки;
	КонецЕсли;
	
	Возврат Идентификаторы;

КонецФункции

Функция ПроверкаАвторизации() Экспорт
	
	Результат = Новый Структура("ТокенПолучен,НеактуальныеКлючи",Ложь,Ложь);
	
	Ответ = НовыйТокенАутентификации();
	
	Результат.ТокенПолучен = ЗначениеЗаполнено(Ответ.Токен);
	Результат.НеактуальныеКлючи = Ответ.НеактуальныеКлючи;
	
	Возврат Результат;

КонецФункции

Функция ДоступностьАдресаПоИмени() Экспорт

	СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI(АдресПриложенияПоИмени);
	СоздатьНовоеHTTPСоединение();
	
	АдресРесурса = РесурсПинг();
	Ответ = HTTPОтветСервера(ПараметрыЗапроса(АдресРесурса, "GET"));
	Если Ответ <> Неопределено И Ответ.КодСостояния = 200 Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;

КонецФункции

Функция ИнформацияОПриложении() Экспорт

	ОбъектКонфигурации = Неопределено;
	
	АдресРесурса = РесурсИнформацияОПриложении();
	Ответ = HTTPОтветСервера(ПараметрыЗапроса(АдресРесурса, "GET"));
	Если Ответ <> Неопределено И Ответ.КодСостояния = 200 Тогда
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
		Попытка
			ОбъектDTO = ПрочитатьJSON(ЧтениеJSON, Истина);
			ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ИнформацияОПриложении;
			ОбъектКонфигурации = МенеджерКонвертации.ВыполнитьКонвертациюИзОбъектовDTO(ОбъектDTO, ТипОбъекта);
		Исключение
			ЗаписатьОшибкуЧтенияJSON(Ответ.ПолучитьТелоКакСтроку(), ОписаниеОшибки());
		КонецПопытки;
	КонецЕсли;
	
	Возврат ОбъектКонфигурации;

КонецФункции

Функция СоздатьАдминистратора(Объект, ТипОбъекта) Экспорт
	
	Результат = Новый Структура("СсылкаДляАдминистратора,БылиОшибки",,Ложь);

	РезультатКонвертации = МенеджерКонвертации.ВыполнитьКонвертациюВОбъектыDTO(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект), ТипОбъекта);
	Если Не ЗначениеЗаполнено(РезультатКонвертации.Данные) Тогда
		Возврат Результат;
	КонецЕсли;
	
	ОбъектDTO = РезультатКонвертации.Данные[0];
	
	АдресРесурса = ТипОбъектаАдресРесурса[ТипОбъекта];
	Если Не ЗначениеЗаполнено(АдресРесурса) Тогда
		ЗаписатьОшибкуНеНайденАдресРесурса(ТипОбъекта);
		Возврат Результат;
	КонецЕсли;
	
	СтрокаТела = ИнтеграцияУправлениеПерсоналом.СформироватьJSON(ОбъектDTO);
	Ответ = HTTPОтветСервера(ПараметрыЗапроса(АдресРесурса, "POST", СтрокаТела));
	Если Ответ <> Неопределено И Ответ.КодСостояния = 200 Тогда
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
		Попытка
			ОбъектDTO = ПрочитатьJSON(ЧтениеJSON, Истина);
			Результат.СсылкаДляАдминистратора = ОбъектDTO["logonUrl"];
		Исключение
			ЗаписатьОшибкуЧтенияJSON(Ответ.ПолучитьТелоКакСтроку(), ОписаниеОшибки());
		КонецПопытки;
	Иначе
		Результат.БылиОшибки = Истина;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции


#КонецОбласти

#Область РегистрацияВЖурналеРегистрации

Функция ОписаниеТекстаЗапросаДляЖР(ИмяМетода, HTTPЗапрос)

	АдресСервера = СтрШаблон("%1://%2", СтруктураURI.Схема, СтруктураURI.ИмяСервера);
	ЗапросТекст = СтрШаблон("%1 %2%3%4", ИмяМетода, АдресСервера, HTTPЗапрос.АдресРесурса);
	Для Каждого Заголовок Из HTTPЗапрос.Заголовки Цикл
		ЗначениеЗаголовка = Заголовок.Значение;
		Если Заголовок.Ключ = "Authorization" Тогда
			ЗначениеЗаголовка = "*";
		ИначеЕсли Не ЗначениеЗаполнено(ЗначениеЗаголовка) Тогда
			ЗначениеЗаголовка = "<null>";
		КонецЕсли;
		ЗапросТекст = СтрШаблон("%1%2%3: %4",ЗапросТекст, Символы.ПС, Заголовок.Ключ, ЗначениеЗаголовка);
	КонецЦикла;
	
	Возврат ЗапросТекст;

КонецФункции

Функция ОписаниеОтветаЗапросаДляЖР(HTTPОтвет, ЗапросФайла)

	Если HTTPОтвет = Неопределено Тогда
		ОтветТекст = НСтр("ru = 'Не удалось получить ответ от сервера.';
							|en = 'Cannot get a response from the server.'");
	Иначе
		ОтветТекст = СтрШаблон(НСтр("ru = 'Сервер вернул код состояния: %1';
									|en = 'Server returned status code: %1'"), HTTPОтвет.КодСостояния);
		Если Не ЗапросФайла Тогда
			ТелоОтвета = HTTPОтвет.ПолучитьТелоКакСтроку();
			Если ЗначениеЗаполнено(ТелоОтвета) Тогда
				ОтветТекст = СтрШаблон("%1%2%2%4", ОтветТекст, Символы.ПС, Символы.ПС, ТелоОтвета);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ОтветТекст;

КонецФункции

Процедура ЗаписатьСобытиеЗапросВЖурналРегистрации(ИмяМетода, HTTPЗапрос, ОтправкаФайла)

	ЗапросТекст = ОписаниеТекстаЗапросаДляЖР(ИмяМетода, HTTPЗапрос);
	
	ТелоЗапроса = "";
	Если Не ОтправкаФайла Тогда
		ТелоЗапроса = HTTPЗапрос.ПолучитьТелоКакСтроку();
	КонецЕсли;
		
	Комментарий = НСтр(
	"ru = '%1
	|
	|%2';
	|en = '%1
	|
	|%2'");
	
	Комментарий = СтрШаблон(Комментарий, ЗапросТекст, ТелоЗапроса);
	ЗаписьЖурналаРегистрации(ИменаСобытийЖР.Запрос, УровеньЖурналаРегистрации.Информация,,,Комментарий);

КонецПроцедуры

Процедура ЗаписатьСобытиеОтветВЖурналРегистрации(ИмяМетода, HTTPЗапрос, HTTPОтвет, ЗапросФайла)

	ЗапросТекст = ОписаниеТекстаЗапросаДляЖР(ИмяМетода, HTTPЗапрос);
	ОтветТекст  = ОписаниеОтветаЗапросаДляЖР(HTTPОтвет, ЗапросФайла);
	
	Комментарий = НСтр(
	"ru = '%1
	|
	|%2';
	|en = '%1
	|
	|%2'");
	
	Комментарий = СтрШаблон(Комментарий, ЗапросТекст, ОтветТекст);
	ЗаписьЖурналаРегистрации(ИменаСобытийЖР.Ответ, УровеньЖурналаРегистрации.Информация,,,Комментарий);
	
КонецПроцедуры

Процедура ЗаписатьОшибкуПолученияОтвета(ИмяМетода, HTTPЗапрос, ПодробноеПредставлениеОшибки)
	
	ЗапросТекст = ОписаниеТекстаЗапросаДляЖР(ИмяМетода, HTTPЗапрос);
	ОтветТекст = НСтр("ru = 'Не удалось получить ответ от сервера.';
						|en = 'Cannot get a response from the server.'");

	ШаблонОписания = "%1
	|%2
	|%3";
	
	Комментарий = СтрШаблон(ШаблонОписания, ЗапросТекст, ОтветТекст, ПодробноеПредставлениеОшибки);
	ЗаписьЖурналаРегистрации(ИменаСобытийЖР.Обмен, УровеньЖурналаРегистрации.Ошибка,,, Комментарий);
	
КонецПроцедуры

Процедура ЗаписатьОшибкуВызова(ИмяМетода, HTTPЗапрос, HTTPОтвет, ЗапросФайла)
	
	ЗапросТекст = ОписаниеТекстаЗапросаДляЖР(ИмяМетода, HTTPЗапрос);
	ОтветТекст  = ОписаниеОтветаЗапросаДляЖР(HTTPОтвет, ЗапросФайла);
	Комментарий = НСтр(
	"ru = '%1
	|
	|%2';
	|en = '%1
	|
	|%2'");
	
	Комментарий = СтрШаблон(Комментарий, ЗапросТекст, ОтветТекст);
	ЗаписьЖурналаРегистрации(ИменаСобытийЖР.Обмен, УровеньЖурналаРегистрации.Ошибка,,, Комментарий);
	
КонецПроцедуры

Процедура ЗаписатьОшибкуЧтенияJSON(ОтветСтрока, ОписаниеОшибки)

	ШаблонОписания = НСтр(
	"ru = 'Ошибка чтения JSON:
	|Описание ошибки:
	|%1
	|Строка JSON:
	|%2';
	|en = 'JSON read error:
	|Error description:
	|%1
	|JSON line:
	|%2'");
	
	ОтветСтрокаПустой = НСтр("ru = '<пустая строка>';
							|en = '<empty string>'");
	ОтветСтрока = ?(Не ЗначениеЗаполнено(ОтветСтрока), ОтветСтрокаПустой, ОтветСтрока);
	Комментарий = СтрШаблон(ШаблонОписания, ОписаниеОшибки, ОтветСтрока);
	ЗаписьЖурналаРегистрации(ИменаСобытийЖР.Обмен, УровеньЖурналаРегистрации.Ошибка,,, Комментарий);

КонецПроцедуры

Процедура ЗаписатьОшибкуНеНайденАдресРесурса(ТипОбъекта)
	
	Если ЗарегистрированныеОшибкиНеНайденАдресРесурса[ТипОбъекта] <> Неопределено Тогда
		Возврат;
	КонецЕсли;

	ШаблонОписания = НСтр("ru = 'Ошибка обмена. Не найден адрес ресурса объекта: %1';
							|en = 'Exchange error. Object resource address is not found: %1'");
	Комментарий = СтрШаблон(ШаблонОписания, Строка(ТипОбъекта));
	ЗаписьЖурналаРегистрации(ИменаСобытийЖР.Обмен, УровеньЖурналаРегистрации.Ошибка,,, Комментарий);
	
	ЗарегистрированныеОшибкиНеНайденАдресРесурса.Вставить(ТипОбъекта, Истина);

КонецПроцедуры

Процедура ЗаписатьОшибкуНеНайденАдресРесурсаЗагрузкиОбъекта(ТипОбъекта)
	
	Если ЗарегистрированныеОшибкиНеНайденАдресРесурсаЗагрузкиОбъекта[ТипОбъекта] <> Неопределено Тогда
		Возврат;
	КонецЕсли;

	ШаблонОписания = НСтр("ru = 'Ошибка обмена. Отсутствует ресурс загрузки объекта: %1.';
							|en = 'Exchange error. Object import resource is missing: %1.'");
	Комментарий = СтрШаблон(ШаблонОписания, Строка(ТипОбъекта));
	ЗаписьЖурналаРегистрации(ИменаСобытийЖР.Обмен, УровеньЖурналаРегистрации.Ошибка,,,Комментарий);
	
	ЗарегистрированныеОшибкиНеНайденАдресРесурсаЗагрузкиОбъекта.Вставить(ТипОбъекта, Истина);

КонецПроцедуры

Процедура ЗаписатьОшибкуВыгрузкиФайла(Приложение, ИмяФайла, РасширениеФайла)
	
	ШаблонОписания = НСтр("ru = 'Ошибка выгрузки файла: %1.%2';
							|en = 'File export error: %1.%2'");
	Комментарий = СтрШаблон(ШаблонОписания, ИмяФайла, РасширениеФайла);
	ЗаписьЖурналаРегистрации(ИменаСобытийЖР.ПрочиеСобытия, УровеньЖурналаРегистрации.Ошибка,,, Комментарий);
	
КонецПроцедуры

#КонецОбласти

#Область ПолучениеТокенаАутентификации

Функция ТокенАутентификации()
	
	ДанныеВБезопасномХранилище = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(
		АдресТокена, "Токен,СрокГодностиТокена");
	
	Токен = Неопределено;
	Если ДанныеВБезопасномХранилище.Токен <> Неопределено
		И ДанныеВБезопасномХранилище.СрокГодностиТокена <> Неопределено Тогда
		
		Если ТекущаяУниверсальнаяДатаВМиллисекундах() > ДанныеВБезопасномХранилище.СрокГодностиТокена Тогда
			Токен = НовыйТокенАутентификации().Токен;
		Иначе
			Токен = ДанныеВБезопасномХранилище.Токен;
		КонецЕсли;
		
	Иначе
		Токен = НовыйТокенАутентификации().Токен;
	КонецЕсли;
	
	Если Токен = Неопределено Тогда
		ОписаниеОшибки = НСтр("ru = 'Ошибка авторизации.';
								|en = 'Authorization error.'");
		ВызватьИсключение ОписаниеОшибки;
	КонецЕсли;
	
	Возврат Токен;
	
КонецФункции

Функция НовыйТокенАутентификации()
	
	Результат = Новый Структура("Токен,НеактуальныеКлючи",,Ложь);
	
	КлючиПриложения = ИнтеграцияУправлениеПерсоналом.КлючиПриложения(Приложение);
	Если КлючиПриложения = Неопределено Тогда
		ОписаниеОшибки = НСтр("ru = 'Не заданы ключи приложения.';
								|en = 'Application keys not set.'");
		ВызватьИсключение ОписаниеОшибки;
	КонецЕсли;
	
	Ответ = НовыйТокен(КлючиПриложения);
	ЗаполнитьЗначенияСвойств(Результат, Ответ);
	Если ЗначениеЗаполнено(Ответ.Токен) Тогда
		УстановитьПривилегированныйРежим(Истина);
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(АдресТокена,Ответ.Токен, "Токен");
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(АдресТокена,Ответ.СрокГодностиТокена, "СрокГодностиТокена");
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

Функция НовыйТокен(КлючиПриложения)
	
	Результат = Новый Структура("Токен,СрокГодностиТокена,НеактуальныеКлючи",,,Ложь);
	
	СтрокаBase64 = Base64Строка(ПолучитьДвоичныеДанныеИзСтроки(СтрШаблон("%1:%2", КлючиПриложения.ИдентификаторКлиента, КлючиПриложения.СекретКлиента)));
	СтрокаBase64 = СтрЗаменить(СтрокаBase64, Символы.ПС, "");
	СтрокаBase64 = СтрЗаменить(СтрокаBase64, Символы.ВК, "");
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("User-Agent", ОписаниеКлиентскогоПриложения);
	Заголовки.Вставить("Content-Type", "application/x-www-form-urlencoded");
	Заголовки.Вставить("Authorization", СтрШаблон("Basic %1", СтрокаBase64));
	Запрос = Новый HTTPЗапрос("/auth/oidc/token", Заголовки);
	Запрос.УстановитьТелоИзСтроки("grant_type=client_credentials");
	
	Попытка
		Ответ = HTTPСоединение.ОтправитьДляОбработки(Запрос);
	Исключение
		ЗаписатьОшибкуПолученияОтвета("POST", Запрос, ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат Результат;
	КонецПопытки;
	
	Если Ответ.КодСостояния = 200 Тогда
		
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
		РезультатЧтенияJSON = ПрочитатьJSON(ЧтениеJSON);
		Если РезультатЧтенияJSON.Свойство("id_token") Тогда
			Результат.Токен = РезультатЧтенияJSON["id_token"];
			Результат.СрокГодностиТокена = ТекущаяУниверсальнаяДатаВМиллисекундах() + 3600*100;
		Иначе
			ШаблонТекста = НСтр("ru = 'Не удалось получить токен аутентификации.
				|Сервер вернул код:%1
				|%2';
				|en = 'Cannot to get authentication token.
				|Server returned code:%1
				|%2'");
			Комментарий = СтрШаблон(ШаблонТекста, Ответ.КодСостояния, Ответ.ПолучитьТелоКакСтроку());
			ЗаписьЖурналаРегистрации(ИменаСобытийЖР.Обмен, УровеньЖурналаРегистрации.Ошибка,,,Комментарий);
		КонецЕсли;
		
	ИначеЕсли Ответ.КодСостояния = 400 Тогда
		
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
		ОбъектОтвета = ПрочитатьJSON(ЧтениеJSON, Истина);
		ОтветОшибка = ОбъектОтвета["error"];
		Если ОтветОшибка <> Неопределено Тогда
			Сообщение = ОтветОшибка["message"];
			Если Сообщение <> Неопределено И СтрНайти(Сообщение, "User name or password is invalid") > 0 Тогда
				Комментарий = НСтр("ru = 'Не удалось получить токен аутентификации. Неправильные ключи.';
									|en = 'Cannot get an authorization token. Invalid keys.'");
				ЗаписьЖурналаРегистрации(ИменаСобытийЖР.Обмен, УровеньЖурналаРегистрации.Ошибка,,,Комментарий);
				Результат.НеактуальныеКлючи = Истина;
			КонецЕсли;
		КонецЕсли;
		
		Если Не Результат.НеактуальныеКлючи Тогда
			ШаблонТекста = НСтр("ru = 'Не удалось получить токен аутентификации.
				|Сервер вернул код:%1
				|%2';
				|en = 'Cannot to get authentication token.
				|Server returned code:%1
				|%2'");
			Комментарий = СтрШаблон(ШаблонТекста, Ответ.КодСостояния, Ответ.ПолучитьТелоКакСтроку());
			ЗаписьЖурналаРегистрации(ИменаСобытийЖР.Обмен,УровеньЖурналаРегистрации.Ошибка,,,Комментарий);
		КонецЕсли;
		
	ИначеЕсли Ответ.КодСостояния = 401 Тогда
		
		Комментарий = НСтр("ru = 'Не удалось получить токен аутентификации. Неправильные ключи.';
							|en = 'Cannot get an authorization token. Invalid keys.'");
		ЗаписьЖурналаРегистрации(ИменаСобытийЖР.Обмен,УровеньЖурналаРегистрации.Ошибка,,,Комментарий);
		Результат.НеактуальныеКлючи = Истина;
		
	Иначе
		
		ШаблонТекста = НСтр("ru = 'Не удалось получить токен аутентификации.
			|Сервер вернул код:%1
			|%2';
			|en = 'Cannot to get authentication token.
			|Server returned code:%1
			|%2'");
		Комментарий = СтрШаблон(ШаблонТекста, Ответ.КодСостояния, Ответ.ПолучитьТелоКакСтроку());
		ЗаписьЖурналаРегистрации(ИменаСобытийЖР.Обмен,УровеньЖурналаРегистрации.Ошибка,,,Комментарий);
		
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

#КонецОбласти

#Область ЗапросыКСерверу

Функция ПараметрыЗапроса(АдресРесурса, ИмяМетода, СтрокаТела = "", ОписаниеФайла = Неопределено)

	ПараметрыЗапроса = Новый Структура("
	|АдресРесурса,
	|ИмяМетода,
	|СтрокаТела,
	|ОписаниеФайла,
	|РазрешенныйКодОтвета,
	|ЗапросФайла");
	
	ПараметрыЗапроса.АдресРесурса 	= АдресРесурса;
	ПараметрыЗапроса.ИмяМетода 		= ИмяМетода;
	ПараметрыЗапроса.СтрокаТела 	= СтрокаТела;
	ПараметрыЗапроса.ОписаниеФайла 	= ОписаниеФайла;
	ПараметрыЗапроса.ЗапросФайла 	= Ложь;
	
	Возврат ПараметрыЗапроса;

КонецФункции

Функция HTTPОтветСервераВерсииФормата()
	
	РесурсПриложения = РесурсВерсияAPI();
	ИмяМетода = "GET";
	
	Ответ = Неопределено; 
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("User-Agent", ОписаниеКлиентскогоПриложения);
	
	Токен = ТокенАутентификации();
	Если Не ЗначениеЗаполнено(Токен) Тогда
		БылиОшибки  = Истина;
		Возврат Неопределено;
	КонецЕсли;
	Заголовки.Вставить("Authorization", СтрШаблон("Bearer %1", Токен));
	
	АдресРесурса = СтрШаблон("/%1%2", СтруктураURI.ПутьНаСервере, РесурсПриложения);
	Запрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
	
	Если ВестиПротокол Тогда
		ЗаписатьСобытиеЗапросВЖурналРегистрации(ИмяМетода, Запрос, Ложь);
	КонецЕсли;
	
	Попытка
		Ответ = HTTPСоединение.ВызватьHTTPМетод(ИмяМетода, Запрос);
	Исключение
		ЗаписатьОшибкуПолученияОтвета(ИмяМетода, Запрос, ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Ответ = Неопределено;
		БылиОшибки  = Истина;
	КонецПопытки;
	
	Если ВестиПротокол Тогда
		ЗаписатьСобытиеОтветВЖурналРегистрации(ИмяМетода, Запрос, Ответ, Ложь);
	КонецЕсли;
	
	Если Ответ <> Неопределено И Ответ.КодСостояния = 401 Тогда
		
		СоздатьНовоеHTTPСоединение();
		
		Токен = ТокенАутентификации();
		Если Не ЗначениеЗаполнено(Токен) Тогда
			Возврат Неопределено;
		КонецЕсли;
		Запрос.Заголовки.Вставить("Authorization", СтрШаблон("Bearer %1", Токен));
		Если ВестиПротокол Тогда
			ЗаписатьСобытиеЗапросВЖурналРегистрации(ИмяМетода, Запрос, Ложь);
		КонецЕсли;
		Попытка
			Ответ = HTTPСоединение.ВызватьHTTPМетод(ИмяМетода, Запрос);
		Исключение
			ЗаписатьОшибкуПолученияОтвета(ИмяМетода, Запрос, ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			Ответ = Неопределено;
		КонецПопытки;
		Если ВестиПротокол Тогда
			ЗаписатьСобытиеОтветВЖурналРегистрации(ИмяМетода, Запрос, Ответ, Ложь);
		КонецЕсли;
	КонецЕсли;
	
	Если Ответ <> Неопределено И Ответ.КодСостояния >= 300 И Ответ.КодСостояния <> 404 Тогда
		ЗаписатьОшибкуВызова(ИмяМетода, Запрос, Ответ, Ложь);
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции

Функция РезультатЗапросаВерсииФормата()

	Результат = Новый Структура("Использовать,ОбъектВерсииФормата", Ложь);
	Ответ = HTTPОтветСервераВерсииФормата();
	Если Ответ = Неопределено Тогда
		// не удалось получить ответ сервера
		Возврат Результат;
	Иначе
		Если Ответ.КодСостояния = 404 Тогда
			Результат.Использовать = Ложь;
		ИначеЕсли Ответ.КодСостояния = 200 Тогда
			ЧтениеJSON = Новый ЧтениеJSON;
			ЧтениеJSON.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
			Попытка
				ОбъектDTO = ПрочитатьJSON(ЧтениеJSON, Истина);
				ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ВерсииФормата;
				Результат.ОбъектВерсииФормата = МенеджерКонвертации.ВыполнитьКонвертациюИзОбъектовDTO(ОбъектDTO, ТипОбъекта);
				Результат.Использовать = Истина;
			Исключение
				ЗаписатьОшибкуЧтенияJSON(Ответ.ПолучитьТелоКакСтроку(), ОписаниеОшибки());
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

Функция HTTPОтветСервера(ПараметрыЗапроса)
	
	АдресРесурса 			= ПараметрыЗапроса.АдресРесурса;
	ИмяМетода 				= ПараметрыЗапроса.ИмяМетода;
	СтрокаТела 				= ПараметрыЗапроса.СтрокаТела;
	ОписаниеФайла 			= ПараметрыЗапроса.ОписаниеФайла;
	РазрешенныйКодОтвета 	= ПараметрыЗапроса.РазрешенныйКодОтвета;
	
	Ответ = Неопределено;
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("User-Agent", ОписаниеКлиентскогоПриложения);
	Если ЗначениеЗаполнено(ВерсияDTO) Тогда
		Заголовки.Вставить("formatVersion", ВерсияDTO);
	КонецЕсли;
	
	Токен = ТокенАутентификации();
	Если Не ЗначениеЗаполнено(Токен) Тогда
		Возврат Неопределено;
	КонецЕсли;
	Заголовки.Вставить("Authorization", СтрШаблон("Bearer %1", Токен));
	
	ДвоичныеДанныеФайла = Неопределено;
	Если ЗначениеЗаполнено(ОписаниеФайла) Тогда
		ДвоичныеДанныеФайла = ОписаниеФайла.ДвоичныеДанныеФайла;
		ТипКонтента = ТипКонтента(ОписаниеФайла.Расширение);
		Заголовки.Вставить("Content-Type", ТипКонтента);
	ИначеЕсли ЗначениеЗаполнено(СтрокаТела) Тогда
		Заголовки.Вставить("Content-Type", "application/json");
	КонецЕсли;
	
	ПолныйАдресРесурса = СтрШаблон("/%1%2%3", СтруктураURI.ПутьНаСервере, АдресРесурсаAPI, АдресРесурса);
	
	Запрос = Новый HTTPЗапрос(ПолныйАдресРесурса, Заголовки);
	
	ОтправкаФайла = Ложь;
	Если ЗначениеЗаполнено(ДвоичныеДанныеФайла) Тогда
		Запрос.УстановитьТелоИзДвоичныхДанных(ДвоичныеДанныеФайла);
		ОтправкаФайла = Истина;
	ИначеЕсли ЗначениеЗаполнено(СтрокаТела) Тогда
		Запрос.УстановитьТелоИзСтроки(СтрокаТела);
	КонецЕсли;
	
	Если ВестиПротокол Тогда
		ЗаписатьСобытиеЗапросВЖурналРегистрации(ИмяМетода, Запрос, ОтправкаФайла);
	КонецЕсли;
	
	Попытка
		Ответ = HTTPСоединение.ВызватьHTTPМетод(ИмяМетода, Запрос);
	Исключение
		ЗаписатьОшибкуПолученияОтвета(ИмяМетода, Запрос, ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Ответ = Неопределено;
		БылиОшибки  = Истина;
	КонецПопытки;
	
	Если ВестиПротокол Тогда
		ЗаписатьСобытиеОтветВЖурналРегистрации(ИмяМетода, Запрос, Ответ, ПараметрыЗапроса.ЗапросФайла);
	КонецЕсли;
	
	Если Ответ <> Неопределено И Ответ.КодСостояния = 401 Тогда
		СоздатьНовоеHTTPСоединение();
		Токен = ТокенАутентификации();
		Если Не ЗначениеЗаполнено(Токен) Тогда
			Возврат Неопределено;
		КонецЕсли;
		Запрос.Заголовки.Вставить("Authorization", СтрШаблон("Bearer %1", Токен));
		Если ВестиПротокол Тогда
			ЗаписатьСобытиеЗапросВЖурналРегистрации(ИмяМетода, Запрос, ОтправкаФайла);
		КонецЕсли;
		Попытка
			Ответ = HTTPСоединение.ВызватьHTTPМетод(ИмяМетода, Запрос);
		Исключение
			ЗаписатьОшибкуПолученияОтвета(ИмяМетода, Запрос, ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			Ответ = Неопределено;
		КонецПопытки;
		Если ВестиПротокол Тогда
			ЗаписатьСобытиеОтветВЖурналРегистрации(ИмяМетода, Запрос, Ответ, ПараметрыЗапроса.ЗапросФайла);
		КонецЕсли;
	КонецЕсли;
	
	Если Ответ <> Неопределено И Ответ.КодСостояния >= 300
		И Не (Ответ.КодСостояния = 404 И ИмяМетода = "DELETE")
		И Ответ.КодСостояния <> РазрешенныйКодОтвета Тогда
		ЗаписатьОшибкуВызова(ИмяМетода, Запрос, Ответ, ПараметрыЗапроса.ЗапросФайла);
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции

#КонецОбласти

#Область ВыгрузкаОбъектов

Функция РезультатВыгрузкиКоллекции(АдресРесурса, Данные, ПолеКлюча, РазмерПакета)

	РезультатВыгрузки = НовыйРезультатВыгрузкиКоллекции();
	ЕстьПолеКлюча = ЗначениеЗаполнено(ПолеКлюча);
	
	Пакеты = Новый Массив;
	Если Данные.Количество() > РазмерПакета Тогда
		ДанныеПакета = Новый Массив;
		СчПакета = 0;
		Для Ид = 0 По Данные.ВГраница() Цикл
			Если СчПакета = РазмерПакета Тогда
				Пакеты.Добавить(ДанныеПакета);
				СчПакета = 0;
				ДанныеПакета = Новый Массив;
			КонецЕсли;
			СчПакета = СчПакета + 1;
			ДанныеПакета.Добавить(Данные[Ид]);
		КонецЦикла;
		Пакеты.Добавить(ДанныеПакета);
	Иначе
		Пакеты.Добавить(Данные);
	КонецЕсли;
	
	Для каждого Пакет Из Пакеты Цикл
		
		СтрокаТела = ИнтеграцияУправлениеПерсоналом.СформироватьJSON(Пакет);
		Ответ = HTTPОтветСервера(ПараметрыЗапроса(АдресРесурса, "PUT", СтрокаТела));
		
		Если Ответ = Неопределено Тогда
			РезультатВыгрузки.БылиОшибки = Истина;
		ИначеЕсли Ответ.КодСостояния = 200 Или Ответ.КодСостояния = 204 Тогда
			Для Каждого ЭлементПакета Из Пакет Цикл
				Если ЕстьПолеКлюча Тогда
					ЭлементКоллекции = ЭлементПакета[ПолеКлюча];
				Иначе
					ЭлементКоллекции = ЭлементПакета;
				КонецЕсли;
				РезультатВыгрузки.Выгружено.Добавить(ЭлементКоллекции);
			КонецЦикла;
		ИначеЕсли Ответ.КодСостояния = 400 Тогда
			
			РезультатВыгрузки.БылиОшибки = Истина;
			ЧтениеJSON = Новый ЧтениеJSON;
			ЧтениеJSON.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
			
			Попытка
				ОбъектОтвета = ПрочитатьJSON(ЧтениеJSON, Истина);
			Исключение
				ЗаписатьОшибкуЧтенияJSON(Ответ.ПолучитьТелоКакСтроку(), ОписаниеОшибки());
				Продолжить;
			КонецПопытки;
			
			Результат = ОбъектОтвета["result"];
			Если Результат <> Неопределено Тогда
				ЭлементыКоллекции = Новый Соответствие;
				Для Каждого ЭлементРезультат Из Результат Цикл
					ЭлементПакета = Пакет[Число(ЭлементРезультат["position"]) - 1];
					Если ЕстьПолеКлюча Тогда
						ЭлементыКоллекции.Вставить(ЭлементПакета[ПолеКлюча]);
					Иначе
						ЭлементыКоллекции.Вставить(ЭлементПакета);
					КонецЕсли;
				КонецЦикла;
				ВыгруженныеЭлементы = ОбщегоНазначенияБЗККлиентСервер.КлючиСоответствия(ЭлементыКоллекции);
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(РезультатВыгрузки.Выгружено, ВыгруженныеЭлементы);
			КонецЕсли;
			
			КоличествоЭлементов = Пакет.Количество();
			Ошибки = ОбъектОтвета["errors"];
			Если Ошибки <> Неопределено Тогда
				Для Каждого ЭлементОшибки Из Ошибки Цикл
					ПозицияВКоллекции = ЭлементОшибки["position"];
					Если ПозицияВКоллекции <> Неопределено Тогда
						НомерПозиции = Число(ПозицияВКоллекции);
						Если НомерПозиции <= КоличествоЭлементов Тогда
							ЭлементПакета = Пакет[НомерПозиции - 1];
							Если ЕстьПолеКлюча Тогда
								ЭлементКоллекции = ЭлементПакета[ПолеКлюча];
							Иначе
								ЭлементКоллекции = ЭлементПакета;
							КонецЕсли;
							РезультатВыгрузки.НеВыгружено.Вставить(ЭлементКоллекции, ОписаниеОшибкиВыгрузки(ЭлементОшибки));
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
		Иначе
			РезультатВыгрузки.БылиОшибки = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат РезультатВыгрузки;
	
КонецФункции

Функция РезультатВыгрузкиОбъекта(АдресРесурса, ОбъектDTO)
	
	СтрокаТела = ИнтеграцияУправлениеПерсоналом.СформироватьJSON(ОбъектDTO);
	Ответ = HTTPОтветСервера(ПараметрыЗапроса(АдресРесурса, "PUT", СтрокаТела));
	
	Если Ответ <> Неопределено И Ответ.КодСостояния = 200 Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;

КонецФункции

Функция ОписаниеОшибкиВыгрузки(ОбъектОшибка)
	
	Попытка
		Результат = НСтр("ru = 'Тип ошибки: %1
						|Код ошибки: %2
						|Описание: %3
						|Подробно:';
						|en = 'Error type: %1
						|Error code: %2
						|Details: %3
						|More:'");
		Результат = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			Результат, ОбъектОшибка["error"]["type"], ОбъектОшибка["error"]["code"], ОбъектОшибка["error"]["description"]);
	
		Для Каждого КлючЗначение Из ОбъектОшибка["error"]["value"] Цикл
			Результат = Результат + Символы.ПС + КлючЗначение.Ключ + ": " + КлючЗначение.Значение;
		КонецЦикла;
	Исключение
		Результат = НСтр("ru = 'Неизвестное описание ошибки.';
						|en = 'Unknown error details.'");
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

Функция ВыгрузитьУдалениеОбъекта(АдресРесурса, ПубличныйИдентификатор)
	
	АдресРесурсаОбъекта = СтрШаблон("%1/%2", АдресРесурса, ПубличныйИдентификатор);
	
	Ответ = HTTPОтветСервера(ПараметрыЗапроса(АдресРесурсаОбъекта, "DELETE"));
	Если Ответ = Неопределено Тогда
		Возврат Ложь;
	ИначеЕсли Ответ.КодСостояния = 200 Или Ответ.КодСостояния = 204 Или Ответ.КодСостояния = 404 Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ВыгрузитьДвоичныеДанныеФайла(ДвоичныеДанные, Расширение)
	
	ОписаниеФайла = Новый Структура("ДвоичныеДанныеФайла,Расширение",ДвоичныеДанные,Расширение);
	Ответ = HTTPОтветСервера(ПараметрыЗапроса(РесурсФайлы(), "POST",,ОписаниеФайла));
	Если Ответ = Неопределено Или Ответ.КодСостояния >= 300 Тогда
		Возврат Неопределено;
	КонецЕсли;
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
	Попытка
		ОбъектОтвета = ПрочитатьJSON(ЧтениеJSON, Истина);
	Исключение
		ЗаписатьОшибкуЧтенияJSON(Ответ.ПолучитьТелоКакСтроку(), ОписаниеОшибки());
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат ОбъектОтвета.Получить("fileID");
	
КонецФункции

Процедура ВыгрузитьФотографии(ТаблицаДанных)
	
	Для Каждого СтрокаТЗ Из ТаблицаДанных Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаТЗ.АдресФотографии) Тогда
			Продолжить;
		КонецЕсли;
		
		ДвоичныеДанныеФотографии = ПолучитьИзВременногоХранилища(СтрокаТЗ.АдресФотографии);
		Если ТипЗнч(ДвоичныеДанныеФотографии) <> Тип("ДвоичныеДанные") Тогда
			Продолжить;
		КонецЕсли;
		
		РазмерФайла = ДвоичныеДанныеФотографии.Размер();
		
		МаксимальныйРазмерПринимаемогоФайла = ИнтеграцияУправлениеПерсоналомКлиентСервер.МаксимальныйРазмерПринимаемогоФайла();
		Если РазмерФайла > МаксимальныйРазмерПринимаемогоФайла Тогда
			МаксимальныйРазмер = МаксимальныйРазмерПринимаемогоФайла/1024/1024;
			РазмерФайлаДляСообщения = Окр(РазмерФайла/1024/1024, 2);
			ТекстСообщения = НСтр("ru = 'Размер фотографии %1Мб, превышает максимально допустимый %2Мб: %3.';
									|en = 'The photo size (%1 MB) exceeds the maximum size (%2 MB): %3.'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, РазмерФайлаДляСообщения, МаксимальныйРазмер, Строка(СтрокаТЗ.ФизическоеЛицо));
			ЗаписьЖурналаРегистрации(ИменаСобытийЖР.Обмен, УровеньЖурналаРегистрации.Предупреждение,,, ТекстСообщения);
			Продолжить;
		КонецЕсли;
		
		Расширение = ИнтеграцияУправлениеПерсоналом.РасширениеФайлаКартинки(ДвоичныеДанныеФотографии);
		Если Расширение = Неопределено Тогда
			ТекстСообщения = НСтр("ru = 'Неизвестный формат картинки фотографии: %1.';
									|en = 'Unknown picture format: %1.'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, Строка(СтрокаТЗ.ФизическоеЛицо));
			ЗаписьЖурналаРегистрации(ИменаСобытийЖР.Обмен, УровеньЖурналаРегистрации.Предупреждение,,, ТекстСообщения);
			Продолжить;
		КонецЕсли;
		
		ИдентификаторФайла = ВыгрузитьДвоичныеДанныеФайла(ДвоичныеДанныеФотографии, Расширение);
		Если ИдентификаторФайла = Неопределено Тогда
			ТекстСообщения = НСтр("ru = 'Ошибка публикации файла фотографии: %1.';
									|en = 'Photo file publication error: %1.'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, Строка(СтрокаТЗ.ФизическоеЛицо));
			ЗаписьЖурналаРегистрации(ИменаСобытийЖР.Обмен, УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
			Продолжить;
		КонецЕсли;
		
		ПостфиксИмени = НСтр("ru = 'Фотография';
							|en = 'Photo'");
		ИмяФайла = СтрШаблон("%1 %2.%3",Строка(СтрокаТЗ.ФизическоеЛицо), ПостфиксИмени, Расширение);
		
		ОписаниеФайла = Новый Структура("ИдентификаторФайла,ИмяФайла,Расширение,Размер");
		ОписаниеФайла.ИдентификаторФайла = ИдентификаторФайла;
		ОписаниеФайла.ИмяФайла 		= ИмяФайла;
		ОписаниеФайла.Расширение 	= Расширение;
		ОписаниеФайла.Размер 		= РазмерФайла;
		
		СтрокаТЗ.Фотография = ОписаниеФайла;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ВыгрузитьФыйлыЭлектронныхДокументов(КоллекцияОбъектов)
	
	КоллекцияМассив = Ложь;
	Если ТипЗнч(КоллекцияОбъектов) = Тип("Массив") Тогда
		НоваяКоллекция = Новый Массив;
		КоллекцияМассив = Истина;
	Иначе
		НоваяКоллекция = КоллекцияОбъектов.СкопироватьКолонки();
	КонецЕсли;
	
	Для каждого ОбъектКоллекции Из КоллекцияОбъектов Цикл
	
		ЭлектронныйДокумент = ОбъектКоллекции.ЭлектронныйДокумент;
		Если ЗначениеЗаполнено(ЭлектронныйДокумент) Тогда
			
			ОписаниеФайла = ЭлектронныйДокумент.ИсходныйДокумент;
			ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(ОписаниеФайла.АдресФайла);
			ИдентификаторФайла = ВыгрузитьДвоичныеДанныеФайла(ДвоичныеДанныеФайла, ОписаниеФайла.Расширение);
			Если ИдентификаторФайла = Неопределено Тогда
				ЗаписатьОшибкуВыгрузкиФайла(Приложение, ОписаниеФайла.ИмяФайла, ОписаниеФайла.Расширение);
				БылиОшибки = Истина;
				Продолжить;
			КонецЕсли;
			ОписаниеФайла.ИдентификаторФайла = ИдентификаторФайла;
			
			ПредставленияДокумента = ЭлектронныйДокумент.ПредставленияДокумента;
			Если ЗначениеЗаполнено(ПредставленияДокумента) Тогда
				Для каждого ОписаниеФайла Из ЭлектронныйДокумент.ПредставленияДокумента Цикл
					ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(ОписаниеФайла.АдресФайла);
					ИдентификаторФайла = ВыгрузитьДвоичныеДанныеФайла(ДвоичныеДанныеФайла, ОписаниеФайла.Расширение);
					Если ИдентификаторФайла = Неопределено Тогда
						ЗаписатьОшибкуВыгрузкиФайла(Приложение, ОписаниеФайла.ИмяФайла, ОписаниеФайла.Расширение);
						БылиОшибки = Истина;
						Продолжить;
					КонецЕсли;
					ОписаниеФайла.ИдентификаторФайла = ИдентификаторФайла;
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
		
		Если КоллекцияМассив Тогда
			НоваяКоллекция.Добавить(ОбъектКоллекции);
		Иначе
			ЗаполнитьЗначенияСвойств(НоваяКоллекция.Добавить(), ОбъектКоллекции);
		КонецЕсли;
	
	КонецЦикла;
	
	Возврат НоваяКоллекция;

КонецФункции

Функция ВыгрузитьФайлы(КоллекцияОбъектов, ИменаСвойствФайл)
	
	КоллекцияМассив = Ложь;
	Если ТипЗнч(КоллекцияОбъектов) = Тип("Массив") Тогда
		НоваяКоллекция = Новый Массив;
		КоллекцияМассив = Истина;
	Иначе
		НоваяКоллекция = КоллекцияОбъектов.СкопироватьКолонки();
	КонецЕсли;
	
	Для каждого ОбъектКоллекции Из КоллекцияОбъектов Цикл
		
		ФайлВыгружен = Истина;
		Для каждого ИмяСвойства Из ИменаСвойствФайл Цикл
			Если КоллекцияМассив Тогда
				ОписаниеФайла = ИнтеграцияУправлениеПерсоналомОбмен.ЗначениеСвойстваОбъекта(ОбъектКоллекции, ИмяСвойства);
			Иначе
				ОписаниеФайла = ОбъектКоллекции[ИмяСвойства];
			КонецЕсли;
			Если ЗначениеЗаполнено(ОписаниеФайла) Тогда
				ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(ОписаниеФайла.АдресФайла);
				ИдентификаторФайла = ВыгрузитьДвоичныеДанныеФайла(ДвоичныеДанныеФайла, ОписаниеФайла.Расширение);
				Если ИдентификаторФайла = Неопределено Тогда
					ЗаписатьОшибкуВыгрузкиФайла(Приложение, ОписаниеФайла.ИмяФайла, ОписаниеФайла.Расширение);
					ФайлВыгружен = Ложь;
					Продолжить;
				КонецЕсли;
				ОписаниеФайла.ИдентификаторФайла = ИдентификаторФайла;
			КонецЕсли;
		КонецЦикла;
		
		Если ФайлВыгружен Тогда
			Если КоллекцияМассив Тогда
				НоваяКоллекция.Добавить(ОбъектКоллекции);
			Иначе
				ЗаполнитьЗначенияСвойств(НоваяКоллекция.Добавить(), ОбъектКоллекции);
			КонецЕсли;
		КонецЕсли;
	
	КонецЦикла;
	
	Возврат НоваяКоллекция;

КонецФункции

#КонецОбласти 

#Область ВосстановлениеJSON

Функция ПараметрыВосстановленияJSON()
	
	ПараметрыВосстановления = Новый Структура("ИменаСвойствСоставногоТипа");
	ПараметрыВосстановления.ИменаСвойствСоставногоТипа = МенеджерКонвертации.ИменаСвойствСоставногоТипа();
	
	Возврат ПараметрыВосстановления;

КонецФункции

Функция ВосстановлениеJSON(Знач Свойство, Значение, ДополнительныеПараметры) Экспорт
	
	Результат = Неопределено;
	
	Попытка
		Результат = ПрочитатьДатуJSON(Значение, ФорматДатыJSON.ISO);
	Исключение
		Если ДополнительныеПараметры.ИменаСвойствСоставногоТипа[Свойство] = Истина Тогда
			Результат = Значение;
		Иначе
			Результат = Дата(1,1,1);
		КонецЕсли;
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ПроверкаУстановкаВерсийОбмена

Процедура УстановитьВерсииФорматовОбмена(НовыеВерсии)
	
	ТекущаяВерсияDTO = ВерсияDTO;
	ТекущаяВерсияAPI = ВерсияAPI;
	
	// Версия API обрабатывается до версии DTO.
	Если НовыеВерсии.ВерсияAPI <> ВерсияAPI Тогда
		НоваяВерсияAPI = ИнтеграцияУправлениеПерсоналом.ПодобратьВерсию(НовыеВерсии.ВерсияAPI, ИнтеграцияУправлениеПерсоналом.ВерсииAPI(Приложение));
		Если Не ЗначениеЗаполнено(НоваяВерсияAPI) Тогда
			ШаблонСообщения = НСтр("ru = 'Не удалось подобрать версию API, версия приложения: %1.';
									|en = 'Cannot find the API version, application version: %1.'");
			СообщениеОбОшибке = СтрШаблон(ШаблонСообщения, ВерсияAPI);
			ВызватьИсключение СообщениеОбОшибке;
		КонецЕсли;
		Если НоваяВерсияAPI <> ВерсияAPI Тогда
			ВерсияAPI = НоваяВерсияAPI;
			ОбновитьИнициализациюПриСмениВерсииAPI();
			ИнтеграцияУправлениеПерсоналом.УстановитьНовуюВерсиюAPI(Приложение, ТекущаяВерсияAPI, ВерсияAPI);
		КонецЕсли;
	КонецЕсли;
	
	Если НовыеВерсии.ВерсияDTO <> ВерсияDTO Тогда
		НоваяВерсияDTO = ИнтеграцияУправлениеПерсоналом.ПодобратьВерсию(НовыеВерсии.ВерсияDTO, ИнтеграцияУправлениеПерсоналом.ВерсииDTO(Приложение));
		Если Не ЗначениеЗаполнено(НоваяВерсияDTO) Тогда
			ШаблонСообщения = НСтр("ru = 'Не удалось подобрать версию DTO, версия приложения: %1.';
									|en = 'Cannot find the DTO version, application version: %1.'");
			СообщениеОбОшибке = СтрШаблон(ШаблонСообщения, ВерсияDTO);
			ВызватьИсключение СообщениеОбОшибке;
		КонецЕсли;
		Если НоваяВерсияDTO <> ВерсияDTO Тогда
			ВерсияDTO = НоваяВерсияDTO;
			ОбновитьВерсиюDTO();
			ИнтеграцияУправлениеПерсоналом.УстановитьНовуюВерсиюDTO(Приложение, ТекущаяВерсияDTO, ВерсияDTO);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область Прочие

Функция НовыйРезультатВыгрузки()
	
	Результат = Новый Структура;
	Результат.Вставить("Выгружено", 			Новый Массив);
	Результат.Вставить("ОтменитьРегистрацию", 	Новый Массив);
	Результат.Вставить("НеВыгружено", 			Новый Массив);
	Результат.Вставить("БылиОшибки", 			Ложь);
	
	Возврат Результат;
	
КонецФункции

Функция НовыйРезультатВыгрузкиКоллекции()
	
	Результат = Новый Структура;
	Результат.Вставить("Выгружено", 	Новый Массив);
	Результат.Вставить("НеВыгружено", 	Новый Соответствие);
	Результат.Вставить("БылиОшибки", 	Ложь);
	
	Возврат Результат;
	
КонецФункции

Функция ЕстьСвойствоЭлектронныйДокумент(КоллекцияОбъектов)
	
	Если Не ЗначениеЗаполнено(КоллекцияОбъектов) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(КоллекцияОбъектов[0], "ЭлектронныйДокумент");

КонецФункции

// Тип контента по расширению файла для заголовка Content-Type.
Функция ТипКонтента(Расширение)

	ТипКонтента = "application/octet-stream";
	РасширениеФайла = НРег(Расширение);
	Если РасширениеФайла = "jpg"
		Или РасширениеФайла = "jpeg"
		Или РасширениеФайла = "jpe"
		Или РасширениеФайла = "jfif" Тогда
		ТипКонтента = "image/jpeg";
	ИначеЕсли РасширениеФайла = "png" Тогда
		ТипКонтента = "image/png";
	ИначеЕсли РасширениеФайла = "gif" Тогда
		ТипКонтента = "image/gif";
	ИначеЕсли РасширениеФайла = "tiff" Тогда
		ТипКонтента = "image/tiff";
	ИначеЕсли РасширениеФайла = "webp" Тогда
		ТипКонтента = "image/webp";
	ИначеЕсли РасширениеФайла = "bmp" Тогда
		ТипКонтента = "image/vnd.wap.wbmp";
	КонецЕсли;
	
	Возврат ТипКонтента;

КонецФункции

Функция УстановитьОтметкуНеВыгружался(МассивСсылок, ТипОбъекта)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТипОбъекта", ТипОбъекта);
	Запрос.УстановитьПараметр("Приложение", Приложение);
	Запрос.УстановитьПараметр("Ссылки", МассивСсылок);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.Ссылка КАК Ссылка,
	|	Таблица.Приложение КАК Приложение,
	|	Таблица.ТипОбъекта КАК ТипОбъекта,
	|	ЛОЖЬ КАК Выгружался
	|ИЗ
	|	РегистрСведений.ВыгруженныеОбъектыУправлениеПерсоналом КАК Таблица
	|ГДЕ
	|	Таблица.ТипОбъекта = &ТипОбъекта
	|	И Таблица.Приложение = &Приложение
	|	И Таблица.Ссылка В(&Ссылки)
	|	И Таблица.Выгружался";
	Таблица = Запрос.Выполнить().Выгрузить();
	
	Если Таблица.Количество() > 0 Тогда
		
		НачатьТранзакцию();
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ВыгруженныеОбъектыУправлениеПерсоналом");
			ЭлементБлокировки.ИсточникДанных = Таблица;
			ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ссылка", "Ссылка");
			ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Приложение", "Приложение");
			ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ТипОбъекта", "ТипОбъекта");
			Блокировка.Заблокировать();
			
			Для каждого СтрокаТЗ Из Таблица Цикл
				МенеджерЗаписи = РегистрыСведений.ВыгруженныеОбъектыУправлениеПерсоналом.СоздатьМенеджерЗаписи();
				ЗаполнитьЗначенияСвойств(МенеджерЗаписи, СтрокаТЗ);
				МенеджерЗаписи.Записать();
			КонецЦикла;
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ВызватьИсключение;
		КонецПопытки;
		
	КонецЕсли;

КонецФункции

Функция ОбъектыПоИдентификаторам(Приложение, Идентификаторы, ТипОбъекта, РегистрироватьВыгрузку)
	
	Если Не ЗначениеЗаполнено(Идентификаторы) Тогда
		Возврат Новый Массив;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТипОбъекта", ТипОбъекта);
	Запрос.УстановитьПараметр("Приложение", Приложение);
	Запрос.УстановитьПараметр("Идентификаторы", Идентификаторы);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Идентификаторы.Ссылка КАК Ссылка,
	|	Идентификаторы.ТипОбъекта КАК ТипОбъекта,
	|	Идентификаторы.Идентификатор КАК Идентификатор,
	|	&Приложение КАК Приложение,
	|	ЕСТЬNULL(ВыгруженныеОбъекты.Выгружался, ЛОЖЬ) КАК Выгружался
	|ИЗ
	|	РегистрСведений.ПубличныеИдентификаторыОбъектовУправлениеПерсоналом КАК Идентификаторы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВыгруженныеОбъектыУправлениеПерсоналом КАК ВыгруженныеОбъекты
	|		ПО Идентификаторы.Ссылка = ВыгруженныеОбъекты.Ссылка
	|			И Идентификаторы.ТипОбъекта = ВыгруженныеОбъекты.ТипОбъекта
	|			И (ВыгруженныеОбъекты.Приложение = &Приложение)
	|ГДЕ
	|	Идентификаторы.ТипОбъекта = &ТипОбъекта
	|	И Идентификаторы.Идентификатор В(&Идентификаторы)";
	ТаблицаИдентификаторов = Запрос.Выполнить().Выгрузить();
	
	Если РегистрироватьВыгрузку Тогда
		
		НовыеОбъекты = ТаблицаИдентификаторов.Скопировать(Новый Структура("Выгружался", Ложь));
		Если НовыеОбъекты.Количество() > 0 Тогда
			
			НачатьТранзакцию();
			Попытка
				
				Блокировка = Новый БлокировкаДанных;
				ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ВыгруженныеОбъектыУправлениеПерсоналом");
				ЭлементБлокировки.ИсточникДанных = НовыеОбъекты;
				ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ссылка", "Ссылка");
				ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Приложение", "Приложение");
				ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ТипОбъекта", "ТипОбъекта");
				Блокировка.Заблокировать();
				
				Для каждого СтрокаТЗ Из НовыеОбъекты Цикл
					МенеджерЗаписи = РегистрыСведений.ВыгруженныеОбъектыУправлениеПерсоналом.СоздатьМенеджерЗаписи();
					ЗаполнитьЗначенияСвойств(МенеджерЗаписи, СтрокаТЗ);
					МенеджерЗаписи.Выгружался = Истина;
					МенеджерЗаписи.Записать();
				КонецЦикла;
				
				ЗафиксироватьТранзакцию();
			Исключение
				ОтменитьТранзакцию();
				ВызватьИсключение;
			КонецПопытки;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ТаблицаИдентификаторов.ВыгрузитьКолонку("Ссылка");
	
КонецФункции

Функция ПроверятьОшибкуНаличияОбъекта(ТипОбъекта)
	
	Возврат ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.РезультатСогласования
		Или ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.СправкаОстаткиОтпусков;
	
КонецФункции

Функция АдресРесурсаШаблонаДокументов(ТипШаблона)
	
	АдресРесурса = "";
	
	Если Не ЗначениеЗаполнено(ТипШаблона) Тогда
		АдресРесурса = РесурсШаблонДокумента();
	Иначе
		
		Шаблон = Неопределено;
		Попытка
			Шаблон = Справочники.ШаблоныДокументов[ТипШаблона];
		Исключение
			Возврат АдресРесурса;
		КонецПопытки;
		
		Если Шаблон = Справочники.ШаблоныДокументов.ЗапросСправки2НДФЛ Тогда
			АдресРесурса = РесурсШаблонДокументаЗапросСправки2НДФЛ();
		ИначеЕсли Шаблон = Справочники.ШаблоныДокументов.ЗапросСправкиПоОстаткамОтпуска Тогда
			АдресРесурса = РесурсШаблонДокументаЗаявкаОстаткиОтпусков();
		ИначеЕсли Шаблон = Справочники.ШаблоныДокументов.ЗапросСправкиСМестаРаботы Тогда
			АдресРесурса = РесурсШаблонДокументаЗапросСправкиСМестаРаботы();
		ИначеЕсли Шаблон = Справочники.ШаблоныДокументов.ЗаявлениеНаКомпенсациюОтпуска Тогда
			АдресРесурса = РесурсШаблонДокументаЗаявкаНаКомпенсациюОтпуска();
		ИначеЕсли Шаблон = Справочники.ШаблоныДокументов.ЗаявлениеНаНалоговыеВычеты Тогда
			АдресРесурса = РесурсШаблонДокументаЗаявкаНалоговыйВычет();
		ИначеЕсли Шаблон = Справочники.ШаблоныДокументов.ЗаявлениеНаОтпуск Тогда
			АдресРесурса = РесурсШаблонДокументаЗаявкаНаОтпуск();
		ИначеЕсли Шаблон = Справочники.ШаблоныДокументов.ЗаявлениеНаУдержаниеДСВ Тогда
			АдресРесурса = РесурсШаблонДокументаЗаявкаДСВ();
		ИначеЕсли Шаблон = Справочники.ШаблоныДокументов.ЗаявлениеОбИзмененииЛичныхДанных Тогда
			АдресРесурса = РесурсШаблонДокументаЗаявкаИзменениеЛичныхДанных();
		ИначеЕсли Шаблон = Справочники.ШаблоныДокументов.Отсутствие Тогда
			АдресРесурса = РесурсШаблонДокументаОтсутствие();
		ИначеЕсли Шаблон = Справочники.ШаблоныДокументов.ЗаявлениеНаПереносОтпуска Тогда
			АдресРесурса = РесурсШаблонДокументаЗаявкаНаПереносОтпуска();
		ИначеЕсли Шаблон = Справочники.ШаблоныДокументов.ЗаявлениеНаОтмену Тогда
			АдресРесурса = РесурсШаблонДокументаЗаявкаНаОтмену();
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат АдресРесурса;

КонецФункции

Функция ИспользуетсяВерсия(ВерсияДляСравнения)
	
	Если Не ЗначениеЗаполнено(ВерсияAPI) Тогда
		Возврат Ложь;
	Иначе
		Возврат ИнтеграцияУправлениеПерсоналом.СравнитьВерсии(ВерсияAPI, ВерсияДляСравнения) >= 0;
	КонецЕсли;

КонецФункции

#КонецОбласти

#КонецОбласти

#Область Инициализация

ЗарегистрированныеОшибкиНеНайденАдресРесурса = Новый Соответствие;
ЗарегистрированныеОшибкиНеНайденАдресРесурсаЗагрузкиОбъекта = Новый Соответствие;

#КонецОбласти

#КонецЕсли