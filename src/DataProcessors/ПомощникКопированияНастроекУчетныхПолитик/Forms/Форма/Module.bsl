#Область ОписаниеПеременных

&НаКлиенте
Перем ОбновитьФормуОрганизации;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	УстановитьУсловноеОформление();

	КопированиеИзДругойОрганизации = Параметры.КопированиеИзДругойОрганизации;
	Элементы.ОписаниеНастройкиСистемыНалогообложенияСлужебный.Видимость = Не КопированиеИзДругойОрганизации;
	
	Если КопированиеИзДругойОрганизации Тогда
		ОрганизацииПриемники.Добавить(Параметры.Организация);
		Элементы.ДекорацияОрганизации.Заголовок = НСтр("ru = 'Выберите организацию-источник копирования настроек.';
														|en = 'Select a source company to copy settings.'");
	Иначе
		ОрганизацияИсточник = Параметры.Организация;
		Элементы.ГруппаСтраницыПомощника.ТекущаяСтраница = Элементы.ГруппаСтраницаВыборНастроек;
		ЗаполнитьОписаниеНастроекУстановитьВидимость();
	КонецЕсли;
	
	Период = НачалоМесяца(ТекущаяДатаСеанса());
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОрганизацияЗаполнения", Параметры.Организация);
	Запрос.УстановитьПараметр("Период", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("КопированиеИзДругойОрганизации", КопированиеИзДругойОрганизации);
	
	Запрос.Текст = ТекстЗапросаОрганизацииДляКопированияНастроек();
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();

	Если РезультатЗапроса[2].Пустой() Тогда
		Отказ = Истина;
		ТекстОшиибки = СтрШаблон(
			НСтр("ru = 'У организации ""%1"" не заполнена система налогообложения, копирование настроек учетной политики невозможно.';
				|en = 'The tax system is not filled for the company ""%1"", unable to copy the account policy settings.'"), Параметры.Организация);
		ОбщегоНазначения.СообщитьПользователю(ТекстОшиибки);
		
	Иначе
		ТекущаяСистемаНалогообложения = РезультатЗапроса[2].Выгрузить()[0].СистемаНалогообложения;
		Элементы.ДекорацияОтборОрганизаций.Заголовок = СтрШаблон(Элементы.ДекорацияОтборОрганизаций.Заголовок, ТекущаяСистемаНалогообложения);
		Элементы.ОписаниеНастройкиСистемыНалогообложенияСлужебный.Заголовок = СтрШаблон(Элементы.ОписаниеНастройкиСистемыНалогообложенияСлужебный.Заголовок, 
			ТекущаяСистемаНалогообложения);
		
		Если РезультатЗапроса[3].Пустой() Тогда
			Отказ = Истина;
			ТекстОшибки = СтрШаблон(
				НСтр("ru = 'Отсутствуют организации с системой налогообложения ""%1"" для копирования настроек учетной политики.';
					|en = 'There are no companies with the tax system ""%1"" to copy the accounting policy settings.'"), ТекущаяСистемаНалогообложения);
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Иначе
			Выборка = РезультатЗапроса[3].Выбрать();
			Пока Выборка.Следующий() Цикл
				НоваяСтрока = ТаблицаОрганизации.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
				Если Выборка.КопироватьСистемуНалогообложения Тогда
					СкопироватьНастройкиСистемыНалогообложения = Истина;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ТаблицаОрганизацииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если КопированиеИзДругойОрганизации Тогда
		ПерейтиКВыборуНастроек();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Далее(Команда)

	Если КопированиеИзДругойОрганизации И Элементы.ГруппаСтраницыПомощника.ТекущаяСтраница = Элементы.ГруппаСтраницаВыборОрганизаций Тогда 
		ПерейтиКВыборуНастроек();
		
	ИначеЕсли НЕ КопированиеИзДругойОрганизации И Элементы.ГруппаСтраницыПомощника.ТекущаяСтраница = Элементы.ГруппаСтраницаВыборНастроек Тогда
		Если ЕстьНастройкиДляКопирования() Тогда
			Элементы.ГруппаСтраницыПомощника.ТекущаяСтраница = Элементы.ГруппаСтраницаВыборОрганизаций;
			Элементы.ФормаНазад.Доступность = Истина;
			Элементы.ФормаДалее.Заголовок = НСтр("ru = 'Далее';
												|en = 'Next'");
		КонецЕсли;
		
	ИначеЕсли КопированиеИзДругойОрганизации И Элементы.ГруппаСтраницыПомощника.ТекущаяСтраница = Элементы.ГруппаСтраницаВыборНастроек
		ИЛИ НЕ КопированиеИзДругойОрганизации И Элементы.ГруппаСтраницыПомощника.ТекущаяСтраница = Элементы.ГруппаСтраницаВыборОрганизаций Тогда

		Если ЕстьНастройкиДляКопирования() И СкопироватьНастройки() Тогда
			ОбновитьФормуОрганизации = КопированиеИзДругойОрганизации;
			Элементы.ФормаНазад.Доступность = Ложь;
			Элементы.ФормаДалее.Заголовок = НСтр("ru = 'Готово';
												|en = 'Finish'");
			Элементы.ГруппаСтраницыПомощника.ТекущаяСтраница = Элементы.ГруппаСтраницаИтог;
		КонецЕсли;
		
	ИначеЕсли Элементы.ГруппаСтраницыПомощника.ТекущаяСтраница = Элементы.ГруппаСтраницаИтог Тогда
		Закрыть(ОбновитьФормуОрганизации);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)

	Если КопированиеИзДругойОрганизации И Элементы.ГруппаСтраницыПомощника.ТекущаяСтраница = Элементы.ГруппаСтраницаВыборНастроек Тогда
		Элементы.ГруппаСтраницыПомощника.ТекущаяСтраница = Элементы.ГруппаСтраницаВыборОрганизаций;
		Элементы.ФормаНазад.Доступность = Ложь;
		
	ИначеЕсли Не КопированиеИзДругойОрганизации И Элементы.ГруппаСтраницыПомощника.ТекущаяСтраница = Элементы.ГруппаСтраницаВыборОрганизаций Тогда
		Элементы.ГруппаСтраницыПомощника.ТекущаяСтраница = Элементы.ГруппаСтраницаВыборНастроек;
		Элементы.ФормаНазад.Доступность = Ложь;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПерейтиКВыборуНастроек()
	
		Элементы.ГруппаСтраницыПомощника.ТекущаяСтраница = Элементы.ГруппаСтраницаВыборНастроек;
		Элементы.ФормаНазад.Доступность = Истина;
		Элементы.ФормаДалее.Заголовок = НСтр("ru = 'Далее';
											|en = 'Next'");
		ОрганизацияИсточник = Элементы.ТаблицаОрганизации.ТекущиеДанные.Организация;
		
		ЗаполнитьОписаниеНастроекУстановитьВидимость();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаОрганизацииПометка.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("КопированиеИзДругойОрганизации");;
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);

КонецПроцедуры

&НаСервере
Функция СкопироватьНастройки()

	ТЗ = РеквизитФормыВЗначение("ТаблицаОрганизации");
	
	Если Не КопированиеИзДругойОрганизации Тогда
		ВыбранныеОрганизации = ТЗ.Скопировать(Новый Структура("Пометка", Истина));
		
		Если Не ВыбранныеОрганизации.Количество() Тогда
			ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не выбрана ни одна организация для копирования настроек.';
														|en = 'No companies are selected to copy settings.'"));
			Возврат Ложь;
		Иначе
			ОрганизацииПриемники.Очистить();
			Для Каждого Организация Из ВыбранныеОрганизации Цикл
				ОрганизацииПриемники.Добавить(Организация.Организация,,Организация.КопироватьСистемуНалогообложения);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;

	//Считываем настройки из организации источника
	НастройкиДляКопирования = Новый СписокЗначений();

	НастройкиДляКопирования.Добавить("НастройкиСистемыНалогообложения",	НСтр("ru = 'Настройки системы налогообложения';
																				|en = 'Taxation system settings'"), СкопироватьНастройкиСистемыНалогообложения);
	НастройкиДляКопирования.Добавить("НастройкиУчетаНДС",				НСтр("ru = 'Настройки учета НДС';
																				|en = 'VAT accounting settings'"), СкопироватьНастройкиУчетаНДС);
	НастройкиДляКопирования.Добавить("НастройкиУчетаНДСПриУСН",			НСтр("ru = 'Настройки учета НДС при УСН';
																				|en = 'VAT accounting settings under STS'"), СкопироватьНастройкиУчетаНДС);
	//++ НЕ УТ
	НастройкиДляКопирования.Добавить("НастройкиУчетаНалогаНаПрибыль",	НСтр("ru = 'Настройки учета налога на прибыль';
																				|en = 'Income tax accounting settings'"), СкопироватьНастройкиУчетаНалогаНаПрибыль);
	НастройкиДляКопирования.Добавить("НастройкиУчетаУСН",				НСтр("ru = 'Настройки учета УСН';
																				|en = 'STS accounting settings'"), СкопироватьНастройкиУчетаУСН);
	НастройкиДляКопирования.Добавить("УчетнаяПолитикаБухУчета",			НСтр("ru = 'Учетная политика бухгалтерского учета';
																				|en = 'Accounting policy'"), СкопироватьУчетнаяПолитикаБухУчета);
	НастройкиДляКопирования.Добавить("СтавкиНалогаНаИмущество",			НСтр("ru = 'Ставки налога на имущество';
																				|en = 'Property tax rates'"), СкопироватьСтавкиНалогаНаИмущество);
	//-- НЕ УТ
	НастройкиДляКопирования.Добавить("УчетнаяПолитикаФинансовогоУчета",	НСтр("ru = 'Учетная политика финансового учета';
																				|en = 'Financial accounting policy'"), СкопироватьУчетнаяПолитикаФинансовогоУчета);

	// Формирование текстов запроса.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОрганизацияИсточник", ОрганизацияИсточник);
	Запрос.УстановитьПараметр("Период", ТекущаяДатаСеанса());
	
	ТекстыЗапроса = Новый СписокЗначений;
	МассивВыбранныхНастроек = Новый Массив();
	БлокировкаДанных = Новый БлокировкаДанных();

	Для Каждого НастройкаУчетнойПолитики Из НастройкиДляКопирования Цикл
		Если НастройкаУчетнойПолитики.Пометка Тогда
			ИмяНастройки = НастройкаУчетнойПолитики.Значение;
			ТекстыЗапроса.Добавить(ТекстЗапросаНастройки(ИмяНастройки), "Таблица"
				+ ИмяНастройки);
			МассивВыбранныхНастроек.Добавить(НастройкаУчетнойПолитики.Представление);
			
			ПространствоБлокировки = СтрШаблон("РегистрСведений.%1", ИмяНастройки);
			ЭлементБлокировки = БлокировкаДанных.Добавить(ПространствоБлокировки);
			ЭлементБлокировки.УстановитьЗначение("Организация", ОрганизацияИсточник);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
			
			Если НастройкаУчетнойПолитики.Значение = "УчетнаяПолитикаФинансовогоУчета" Тогда
				ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.СтавкиДисконтирования");
				ЭлементБлокировки.УстановитьЗначение("Организация", ОрганизацияИсточник);
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	//Копируем настройки учетной политики
	НачатьТранзакцию();
	
	Попытка
		
		// Инициализация таблиц.
		БлокировкаДанных.Заблокировать();
		ТаблицыРезультатов = ОбщегоНазначенияУТ.ВыгрузитьРезультатыЗапроса(Запрос, ТекстыЗапроса, ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Для Каждого ОрганизацияНастройка Из ОрганизацииПриемники Цикл
			Для Каждого НастройкаУчетнойПолитики Из ТаблицыРезультатов Цикл
				Если НастройкаУчетнойПолитики.Значение.Строки.Количество() 
					И (ОрганизацияНастройка.Пометка И НастройкаУчетнойПолитики.Ключ = "НастройкиСистемыНалогообложения"
						ИЛИ Не НастройкаУчетнойПолитики.Ключ = "НастройкиСистемыНалогообложения") Тогда
					
					Организация = ОрганизацияНастройка.Значение;
					ИмяРегистра = СтрЗаменить(НастройкаУчетнойПолитики.Ключ, "Таблица", "");
					КлючЗаписи = РегистрыСведений[ИмяРегистра].СоздатьМенеджерЗаписи();
					ЗаполнитьЗначенияСвойств(КлючЗаписи, НастройкаУчетнойПолитики.Значение.Строки[0]);
					КлючЗаписи.Организация = Организация;
					Если НастройкаУчетнойПолитики.Ключ = "ТаблицаНастройкиУчетаНДС" Тогда
						КлючЗаписи.Период = НачалоКвартала(Период);
					КонецЕсли;
					КлючЗаписи.Период = Период;
					КлючЗаписи.Записать(Истина);
					
					Если ИмяРегистра = "УчетнаяПолитикаФинансовогоУчета" Тогда
						НаборЗаписей = РегистрыСведений.СтавкиДисконтирования.СоздатьНаборЗаписей();
						НаборЗаписей.Отбор.Организация.Установить(ОрганизацияИсточник);
						НаборЗаписей.Отбор.Период.Установить(НастройкаУчетнойПолитики.Значение.Строки[0].Период);
						НаборЗаписей.Прочитать();
						Если НаборЗаписей.Выбран() Тогда 
							ТаблицаСтавок = НаборЗаписей.Выгрузить();
							ТаблицаСтавок.ЗаполнитьЗначения(Период, "Период");
							ТаблицаСтавок.ЗаполнитьЗначения(Организация, "Организация");
							НаборЗаписей = РегистрыСведений.СтавкиДисконтирования.СоздатьНаборЗаписей();
							НаборЗаписей.Отбор.Организация.Установить(Организация);
							НаборЗаписей.Отбор.Период.Установить(Период);
							НаборЗаписей.Загрузить(ТаблицаСтавок);
							НаборЗаписей.Записать(Истина);
						КонецЕсли;
					КонецЕсли;
					
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
		Элементы.ГруппаИтогУспешно.Видимость = Истина;
		
		УстановитьЗаголовокДекорацияИтоги(МассивВыбранныхНастроек, ВыбранныеОрганизации);

		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		УстановитьЗаголовокДекорацияИтоги();
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Копирование настроек учетной политики';
										|en = 'Copy accounting policy settings'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ОбщегоНазначения.СообщитьПользователю(ИнформацияОбОшибке());
	КонецПопытки;
	
	Возврат Истина;

КонецФункции

&НаКлиенте
Функция ЕстьНастройкиДляКопирования()
	
	ОчиститьСообщения();
	
	НастройкаВыделены = Ложь;
	Если СкопироватьНастройкиУчетаНалогаНаПрибыль
		Или СкопироватьНастройкиУчетаНДС
		Или СкопироватьНастройкиУчетаУСН
		Или СкопироватьСтавкиНалогаНаИмущество
		Или СкопироватьУчетнаяПолитикаБухУчета
		Или СкопироватьУчетнаяПолитикаФинансовогоУчета Тогда
		НастройкаВыделены = Истина;
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не выбрана ни одна настройка для копирования.';
														|en = 'No settings are selected for copying.'"));
	КонецЕсли;
	
	Возврат НастройкаВыделены;
КонецФункции

&НаСервере
Процедура УстановитьЗаголовокДекорацияИтоги(МассивНастроек = Неопределено, ОписанияОрганизаций = Неопределено)
	
	Элементы.ДекорацияИтоги.Видимость = Истина;
	Если МассивНастроек = Неопределено Тогда
		Элементы.ГруппаИтогОшибка.Видимость = Истина;
		Элементы.ДекорацияИтоги.Заголовок = НСтр("ru = 'Во время копирования настроек учетных политик произошла ошибка, подробности см. в Журнале регистрации.';
												|en = 'An error occurred while copying account policy settings; see the Event log for details.'");
	Иначе
		Отсутп = Новый ФорматированнаяСтрока(БиблиотекаКартинок.Пустая);
		МассивСтрок = Новый Массив();
		
		МассивСтрок.Добавить(НСтр("ru = 'Скопированы настройки:';
									|en = 'The settings were copied:'") + Символы.ПС);
		
		Для Каждого Настройка Из МассивНастроек Цикл
			МассивСтрок.Добавить(Отсутп);
			МассивСтрок.Добавить(Отсутп);
			МассивСтрок.Добавить(Новый ФорматированнаяСтрока(Настройка + Символы.ПС,,ЦветаСтиля.ЦветТекстаФормы));
		КонецЦикла;
		
		Если НЕ ОписанияОрганизаций = Неопределено Тогда
			МассивСтрок.Добавить(НСтр("ru = 'Для организаций:';
										|en = 'For companies:'") + Символы.ПС);
			
			Для Каждого ОписаниеОрганизации Из ОписанияОрганизаций Цикл
				МассивСтрок.Добавить(Отсутп);
				МассивСтрок.Добавить(Отсутп);
				МассивСтрок.Добавить(Новый ФорматированнаяСтрока(Строка(ОписаниеОрганизации.Организация) + Символы.ПС, , , , ПолучитьНавигационнуюСсылку(ОписаниеОрганизации.Организация)));
			КонецЦикла;
		КонецЕсли;
		
		Элементы.ДекорацияИтоги.Заголовок = Новый ФорматированнаяСтрока(МассивСтрок);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОписаниеНастроекУстановитьВидимость()
	
	СоответствиеЭлементов = Справочники.Организации.СоответствиеЭлементовФормыПараметрамНалоговУчетныхПолитик();
	НастройкиНалоговУчетныхПолитик.УправлениеВидимостьюЭлементовУчетныхПолитик(ЭтотОбъект, СоответствиеЭлементов, ОрганизацияИсточник);
	НастройкиНалоговУчетныхПолитик.ЗаполнитьОписаниеДействующихПараметров(ЭтотОбъект, ОрганизацияИсточник, "");
	
КонецПроцедуры

&НаСервере
Функция ТекстЗапросаОрганизацииДляКопированияНастроек()
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НастройкиСистемыНалогообложенияСрезПоследних.СистемаНалогообложения КАК СистемаНалогообложения
	|ПОМЕСТИТЬ НалообложениеОрганизацииЗаполнения
	|ИЗ
	|	РегистрСведений.НастройкиСистемыНалогообложения.СрезПоследних(&Период, Организация = &ОрганизацияЗаполнения
	|	И СистемаНалогообложения <> ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.ПустаяСсылка)) КАК
	|		НастройкиСистемыНалогообложенияСрезПоследних
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЕСТЬNULL(НастройкиСистемыНалогообложенияСрезПоследних.СистемаНалогообложения,
	|		ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.ПустаяСсылка)) КАК СистемаНалогообложения,
	|	Организации.ГоловнаяОрганизация КАК Организация,
	|	ВЫБОР
	|		КОГДА НастройкиСистемыНалогообложенияСрезПоследних.СистемаНалогообложения ЕСТЬ NULL
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК КопироватьСистемуНалогообложения
	|ПОМЕСТИТЬ Организации
	|ИЗ
	|	Справочник.Организации КАК Организации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиСистемыНалогообложения.СрезПоследних(&Период,
	|			Организация <> &ОрганизацияЗаполнения
	|		И Организация <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)) КАК
	|			НастройкиСистемыНалогообложенияСрезПоследних
	|		ПО (НастройкиСистемыНалогообложенияСрезПоследних.Организация = Организации.Ссылка)
	|ГДЕ
	|	Организации.Ссылка <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|	И Организации.ГоловнаяОрганизация <> &ОрганизацияЗаполнения
	|	И Организации.ОбособленноеПодразделение = ЛОЖЬ
	|	И Организации.ПометкаУдаления = ЛОЖЬ
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НалообложениеОрганизацииЗаполнения.СистемаНалогообложения КАК СистемаНалогообложения
	|ИЗ
	|	НалообложениеОрганизацииЗаполнения КАК НалообложениеОрганизацииЗаполнения
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Организации.Организация КАК Организация,
	|	Организации.КопироватьСистемуНалогообложения
	|ИЗ
	|	НалообложениеОрганизацииЗаполнения КАК НалообложениеОрганизации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Организации КАК Организации
	|		ПО НалообложениеОрганизации.СистемаНалогообложения = Организации.СистемаНалогообложения
	|		ИЛИ (НЕ &КопированиеИзДругойОрганизации
	|		И Организации.СистемаНалогообложения = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.ПустаяСсылка))
	|УПОРЯДОЧИТЬ ПО
	|	Организация
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервере
Функция ТекстЗапросаНастройки(Настройка)

	Если Настройка = "УчетнаяПолитикаФинансовогоУчета" Тогда
		ТекстЗапроса = "////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	*
		|ИЗ
		|	РегистрСведений.УчетнаяПолитикаФинансовогоУчета.СрезПоследних(&Период, Организация = ВЫРАЗИТЬ(&ОрганизацияИсточник
		|		КАК Справочник.Организации).ГоловнаяОрганизация) КАК УчетнаяПолитикаФинансовогоУчетаСрезПоследних";
		
	ИначеЕсли Настройка = "НастройкиСистемыНалогообложения" Тогда
			ТекстЗапроса = "
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	*
			|ИЗ
			|	РегистрСведений.НастройкиСистемыНалогообложения.СрезПоследних(&Период, Организация = ВЫРАЗИТЬ(&ОрганизацияИсточник КАК Справочник.Организации).ГоловнаяОрганизация) КАК НастройкиСистемыНалогообложенияСрезПоследних
			|;
			|";
		
	ИначеЕсли Настройка = "НастройкиУчетаНДС" Тогда
		ТекстЗапроса = "
			|		
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	*
			|ИЗ
			|	РегистрСведений.НастройкиУчетаНДС.СрезПоследних(&Период, Организация = ВЫРАЗИТЬ(&ОрганизацияИсточник КАК Справочник.Организации).ГоловнаяОрганизация) КАК НастройкиУчетаНДССрезПоследних
			|;
			|";
	ИначеЕсли Настройка = "НастройкиУчетаНДСПриУСН" Тогда
		ТекстЗапроса = "
			|		
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	*
			|ИЗ
			|	РегистрСведений.НастройкиУчетаНДСПриУСН.СрезПоследних(&Период, Организация = ВЫРАЗИТЬ(&ОрганизацияИсточник КАК Справочник.Организации).ГоловнаяОрганизация) КАК НастройкиУчетаНДСПриУСНСрезПоследних
			|;
			|";	
	//++ НЕ УТ	
	ИначеЕсли Настройка = "НастройкиУчетаНалогаНаПрибыль" Тогда
		ТекстЗапроса = "
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	*
			|ИЗ
			|	РегистрСведений.НастройкиУчетаНалогаНаПрибыль.СрезПоследних(&Период, Организация = ВЫРАЗИТЬ(&ОрганизацияИсточник КАК Справочник.Организации).ГоловнаяОрганизация) КАК НастройкиУчетаНалогаНаПрибыльСрезПоследних
			|;
			|";
			
	ИначеЕсли Настройка = "СтавкиНалогаНаИмущество" Тогда
		ТекстЗапроса = "
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	*
			|ИЗ
			|	РегистрСведений.СтавкиНалогаНаИмущество.СрезПоследних(&Период, Организация = &ОрганизацияИсточник) КАК СтавкиНалогаНаИмуществоСрезПоследних
			|;
			|";
			
	ИначеЕсли Настройка = "НастройкиУчетаУСН" Тогда
		ТекстЗапроса = "
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	*
			|ИЗ
			|	РегистрСведений.НастройкиУчетаУСН.СрезПоследних(&Период, Организация = ВЫРАЗИТЬ(&ОрганизацияИсточник КАК Справочник.Организации).ГоловнаяОрганизация) КАК НастройкиУчетаУСНСрезПоследних
			|;
			|";
		
	ИначеЕсли Настройка = "УчетнаяПолитикаБухУчета" Тогда
		ТекстЗапроса = "
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	*
			|ИЗ
			|	РегистрСведений.УчетнаяПолитикаБухУчета.СрезПоследних(&Период, Организация = ВЫРАЗИТЬ(&ОрганизацияИсточник КАК Справочник.Организации).ГоловнаяОрганизация) КАК УчетнаяПолитикаБухУчетаСрезПоследних
			|;
			|";
		
	ИначеЕсли Настройка = "УчетнаяПолитикаФинансовогоУчета" Тогда
		ТекстЗапроса = "
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	*
			|ИЗ
			|	РегистрСведений.УчетнаяПолитикаФинансовогоУчета.СрезПоследних(&Период, Организация = ВЫРАЗИТЬ(&ОрганизацияИсточник КАК Справочник.Организации).ГоловнаяОрганизация) КАК УчетнаяПолитикаФинансовогоУчетаСрезПоследних
			|;
			|";

	//-- НЕ УТ
	КонецЕсли;
	
	Возврат ТекстЗапроса;

КонецФункции

#КонецОбласти
