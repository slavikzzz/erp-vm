#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Процедура ОбработкаПроверкиЗаполненияДоговораИНастройкиПодключения(Объект, Отказ, СообщитьПользователюОбОшибках = Истина) Экспорт
	
	Если Не ЗначениеЗаполнено(Объект.ДоговорПодключенияКПлатежнойСистеме) Тогда
		Отказ = Истина;
		Если СообщитьПользователюОбОшибках Тогда
			ТекстСообщения = НСтр("ru = 'Укажите договор подключения к СБП.';
									|en = 'Specify the FPS connection contract.'");
			
			ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения,,,, Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.НастройкаПодключения) Тогда
		Отказ = Истина;
		Если СообщитьПользователюОбОшибках Тогда
			ШаблонСообщения = НСтр("ru = 'Не найдена настройка подключения к СБП для договора %1';
									|en = 'The FPS connection setting is not found for the %1 contract'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, Объект.ДоговорПодключенияКПлатежнойСистеме);
			
			ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения,,,, Отказ);
		КонецЕсли;
	Иначе
		ИмяРеквизита = Метаданные.Справочники.ДоговорыЭквайринга.Реквизиты.ДетальнаяСверкаТранзакций.Имя;
		ДетальнаяСверкаТранзакций = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ДоговорПодключенияКПлатежнойСистеме, ИмяРеквизита);
		Если Не ДетальнаяСверкаТранзакций Тогда
			Отказ = Истина;
			Если СообщитьПользователюОбОшибках Тогда
				ШаблонСообщения = НСтр("ru = 'Для загрузки отчета банка необходимо в договоре %1 установить признак:
										|Вести детальный учет транзакций / сверять транзакции в отчете банка по эквайрингу';
										|en = 'To import the bank report, specify the following flag in the %1 contract:
										|Keep detailed records of transactions / reconcile transactions in merchant statements'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, Объект.ДоговорПодключенияКПлатежнойСистеме);
				
				ОбщегоНазначения.СообщитьПользователю(
					ТекстСообщения,,,, Отказ);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли