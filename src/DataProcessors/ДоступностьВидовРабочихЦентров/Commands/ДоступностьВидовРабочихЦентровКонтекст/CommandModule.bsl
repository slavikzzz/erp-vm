
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ЗначенияРеквизитов = ПараметрыВидаРабочегоЦентра(ПараметрКоманды);
	
	Если НЕ ЗначениеЗаполнено(ПараметрКоманды) ИЛИ ЗначенияРеквизитов.ЭтоГруппа Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Команда не может быть выполнена для указанного объекта!';
										|en = 'Cannot execute this command for the specified object.'"));
		Возврат;
	КонецЕсли;
	
	УчетДоступности = ЗначенияРеквизитов.УчитыватьДоступностьПоГрафикуРаботы
		ИЛИ ЗначенияРеквизитов.ПланироватьРаботуРабочихЦентров;
	
	Если НЕ УчетДоступности Тогда
		
		ОчиститьСообщения();
		
		ТекстСообщения = СтрШаблон(
			НСтр("ru = 'Для вида рабочего центра ""%1"" не ведется учет доступности: не учитывается ограничение доступности в графике производства и не планируется работа рабочих центров.';
				|en = 'Capacity is not registered for the ""%1"" work center type: capacity restriction is not considered in the production schedule and work center operation is not planned.'"),
			ЗначенияРеквизитов.Представление);
			
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
		Возврат;
		
	КонецЕсли;
	
	Если ЗначенияРеквизитов.УчитыватьДоступностьПоГрафикуРаботы Тогда
		РежимРаботы = ПредопределенноеЗначение("Перечисление.РежимыРедактированияДоступностиВидовРЦ.ВводДоступностиДляФормированияГрафикаПроизводства");
	Иначе
		РежимРаботы = ПредопределенноеЗначение("Перечисление.РежимыРедактированияДоступностиВидовРЦ.ВводГрафикаРаботыРЦДляФормированияРасписанияРаботыРЦ");
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимРаботы", РежимРаботы);
	ПараметрыФормы.Вставить("ВидРабочегоЦентра", ПараметрКоманды);
	ПараметрыФормы.Вставить("НачалоПериода", ОбщегоНазначенияКлиент.ДатаСеанса());
	
	ОткрытьФорму(
		"Обработка.ДоступностьВидовРабочихЦентров.Форма",
		ПараметрыФормы, 
		ПараметрыВыполненияКоманды.Источник, 
		ПараметрыВыполненияКоманды.Уникальность, 
		ПараметрыВыполненияКоманды.Окно, 
		ПараметрыВыполненияКоманды.НавигационнаяСсылка);
	
КонецПроцедуры

&НаСервере
Функция ПараметрыВидаРабочегоЦентра(Ссылка)
	
	Реквизиты = "ЭтоГруппа,
				|Представление,
				|УчитыватьДоступностьПоГрафикуРаботы,
				|ПланироватьРаботуРабочихЦентров";
	
	Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, Реквизиты);
	
КонецФункции

#КонецОбласти
