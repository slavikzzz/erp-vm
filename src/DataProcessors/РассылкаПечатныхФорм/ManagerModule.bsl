#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Функция НовыйСводныйРезультатОтправки() Экспорт
	
	СводныйРезультат = Новый Структура;
	СводныйРезультат.Вставить("Отправлено", 0);
	СводныйРезультат.Вставить("Ошибок", 0);
	СводныйРезультат.Вставить("Обработано", 0);
	СводныйРезультат.Вставить("БезАдресов", 0);
	СводныйРезультат.Вставить("Детально", Новый Массив);
	
	Возврат СводныйРезультат;

КонецФункции

#КонецОбласти
	
#Область СлужебныеПроцедурыИФункции

Функция ОтправитьДокументыПолучателям(Получатели, ДляПечати, ДляОтправки) Экспорт

	Результаты = Неопределено;
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.РассылкаДокументов") Тогда
		МодульРассылкаПоШаблонам = ОбщегоНазначения.ОбщийМодуль("РассылкаПоШаблонам");
		Результаты = МодульРассылкаПоШаблонам.ОтправитьДокументыПолучателямПоШаблону(Получатели, ДляОтправки);
	КонецЕсли;

	Если Результаты <> Неопределено Тогда
		Возврат Результаты;
	КонецЕсли;

	ОписанияВложений = Новый Соответствие;
	ВыведенныеДокументыИдентификаторов = Новый Соответствие;
	
	ВыводимыеФайлы = КадровыйЭДОВызовСервера.ВыводимыеФайлыПечатныхФорм(ДляПечати.ВыводимыеФайлы);
	Для Каждого ВыводимыйФайл Из ВыводимыеФайлы Цикл
		
		ДобавитьОписаниеВложения(
			ОписанияВложений,
			ВыводимыйФайл.ФизическоеЛицо,
			ВыводимыйФайл.Владелец,
			ОписаниеВложения(
				ВыводимыйФайл.ИмяФайлаСРасширением,
				ВыводимыйФайл.АдресВоВременномХранилище));
		
		ВыведенныеДокументы = ВыведенныеДокументыИдентификаторов.Получить(ВыводимыйФайл.ИдентификаторПечатнойФормы);
		Если ВыведенныеДокументы = Неопределено Тогда
			ВыведенныеДокументы = Новый Массив;
			ВыведенныеДокументыИдентификаторов.Вставить(ВыводимыйФайл.ИдентификаторПечатнойФормы, ВыведенныеДокументы);
		КонецЕсли;
		
		ВыведенныеДокументы.Добавить(ВыводимыйФайл.Владелец);
		
	КонецЦикла;
	
	ДанныеПечатиВсехФорм = Новый Соответствие;
	Для Каждого ПечатнаяФорма Из ДляПечати.ПечатныеФормы Цикл
		Если ЗначениеЗаполнено(ПечатнаяФорма.ОбработчикПолученияДанных) Тогда
			
			ВсеРассылаемыеДокументы = Получатели.ВыгрузитьКолонку("РассылаемыйДокумент");
			ВыведенныеДокументыИдентификатора = ВыведенныеДокументыИдентификаторов.Получить(ПечатнаяФорма.Идентификатор);
			
			Если ВыведенныеДокументыИдентификатора = Неопределено Тогда
				РассылаемыеДокументы = ВсеРассылаемыеДокументы;
			Иначе
				РассылаемыеДокументы = ОбщегоНазначенияКлиентСервер.РазностьМассивов(
					ВсеРассылаемыеДокументы, ВыведенныеДокументыИдентификатора);
			КонецЕсли;
			
			Если РассылаемыеДокументы.Количество() > 0 Тогда
				
				ДанныеПечатиВсехФорм.Вставить(ПечатнаяФорма.Идентификатор, 
					ДанныеПечатиБезОтбора(ПечатнаяФорма.ОбработчикПолученияДанных, РассылаемыеДокументы));
				
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого СтрокаПолучателя Из Получатели Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаПолучателя.Адрес) Тогда
			Продолжить;
		КонецЕсли;
		
		ПечатныеФормы = Новый Массив;
		Для Каждого ПечатнаяФорма Из ДляПечати.ПечатныеФормы Цикл
			
			ВыведенныеДокументыИдентификатора = ВыведенныеДокументыИдентификаторов.Получить(ПечатнаяФорма.Идентификатор);
			Если ВыведенныеДокументыИдентификатора <> Неопределено
				И ВыведенныеДокументыИдентификатора.Найти(СтрокаПолучателя.РассылаемыйДокумент) <> Неопределено Тогда
				
				Продолжить;
			КонецЕсли;
			
			ПечатныеФормы.Добавить(ПечатнаяФорма);
			
			Если Не ЗначениеЗаполнено(ПечатнаяФорма.ОбработчикПолученияДанных) Тогда
				Продолжить;
			КонецЕсли;
			
			ОтборСтрок = Новый Структура("РассылаемыйДокумент, ФизическоеЛицо");
			ЗаполнитьЗначенияСвойств(ОтборСтрок, СтрокаПолучателя);
			ДанныеПечати = ДанныеПечатиВсехФорм[ПечатнаяФорма.Идентификатор].НайтиСтроки(ОтборСтрок);
			ПечатнаяФорма.ДополнительныеПараметры.Вставить("ДанныеПечати", ПоместитьВоВременноеХранилище(ДанныеПечати));
			
		КонецЦикла;
		
		Если ПечатныеФормы.Количество() > 0 Тогда
			
			ДляСохраненияВФайл = УправлениеПечатью.НастройкиСохранения();
			ДляСохраненияВФайл.ФорматыСохранения.Добавить(КадровыйЭДОВызовСервера.ТипФайлаЭлектронногоДокумента());
			ДляСохраненияВФайл.УпаковатьВАрхив = ДляПечати.УпаковатьВАрхив;
			
			ПечатныеДокументы = УправлениеПечатью.НапечататьВФайл(
				ПечатныеФормы, 
				ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СтрокаПолучателя.РассылаемыйДокумент),
				ДляСохраненияВФайл);
			
			Для Каждого ПечатныйДокумент Из ПечатныеДокументы Цикл
				
				ДобавитьОписаниеВложения(
					ОписанияВложений,
					СтрокаПолучателя.ФизическоеЛицо,
					СтрокаПолучателя.РассылаемыйДокумент,
					ОписаниеВложения(
						ПечатныйДокумент.ИмяФайла,
						ПоместитьВоВременноеХранилище(ПечатныйДокумент.ДвоичныеДанные)));
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ОтправитьВложенияПолучателям(Получатели, ОписанияВложений, ДляОтправки);
	
КонецФункции

Функция ОтправитьВложенияПолучателям(Получатели, ОписанияВложений, ДляОтправки)
	
	ДлительныеОперации.СообщитьПрогресс(5);
	
	Результаты = НовыйСводныйРезультатОтправки();
	ТаблицаРезультатов = ТаблицаРезультатовОтправки();
	
	Для Каждого СтрокаПолучателя Из Получатели Цикл
		Если Не ЗначениеЗаполнено(СтрокаПолучателя.Адрес) Тогда
			НакопитьЗначение(Результаты.Обработано, 1);
			НакопитьЗначение(Результаты.БезАдресов, 1);
			Продолжить;
		КонецЕсли;
		
		ПараметрыОтправки = ПараметрыПисьма();
		ПараметрыОтправки.Тема = ДляОтправки.ШаблонПисьма.Тема;
		ПараметрыОтправки.Тело = ДляОтправки.ШаблонПисьма.Текст;
		ПараметрыОтправки.Кому.Добавить(ОписаниеПолучателя(СтрокаПолучателя.Адрес, СтрокаПолучателя.ФизическоеЛицо));

		ВложенияФизическогоЛица = ОписанияВложений.Получить(СтрокаПолучателя.ФизическоеЛицо);
		Если ВложенияФизическогоЛица <> Неопределено Тогда
			
			ВложенияОбъекта = ВложенияФизическогоЛица.Получить(СтрокаПолучателя.РассылаемыйДокумент);
			Если ВложенияОбъекта <> Неопределено Тогда
				ПараметрыОтправки.Вложения = ВложенияОбъекта;
			КонецЕсли;
			
		КонецЕсли;
				
		РезультатОтправки = ОписаниеРезультатаОтправки();

		Ответ = РаботаСПочтовымиСообщениями.ОтправитьПисьмо(ДляОтправки.УчетнаяЗапись, 
			РаботаСПочтовымиСообщениями.ПодготовитьПисьмо(ДляОтправки.УчетнаяЗапись, ПараметрыОтправки));
		
		Если Ответ.ОшибочныеПолучатели.Количество() = 0 Тогда
			ЗаполнитьУспешнымРезультатом(РезультатОтправки);
			НакопитьЗначение(Результаты.Отправлено, 1);
		Иначе
			ПричиныОшибки = ПричиныОшибки(Ответ.ОшибочныеПолучатели);
			ЗаполнитьНеуспешнымРезультатом(РезультатОтправки, ПричиныОшибки);
			НакопитьЗначение(Результаты.Ошибок, 1);
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Рассылка документов.Сообщение не отправлено';
					|en = 'Sending documents. Message not sent'", ОбщегоНазначения.КодОсновногоЯзыка()), 
				УровеньЖурналаРегистрации.Предупреждение, , , ПричиныОшибки);
		КонецЕсли;
	
		ЗаписатьРезультатОтправки(СтрокаПолучателя.ФизическоеЛицо, СтрокаПолучателя.ФизическоеЛицо, 
			СтрокаПолучателя.РассылаемыйДокумент, РезультатОтправки);
		НоваяСтрокаРезультата = ТаблицаРезультатов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаРезультата, СтрокаПолучателя);
		НоваяСтрокаРезультата.Результат = РезультатОтправки;
			
		ДлительныеОперации.СообщитьПрогресс(ПрогрессОтправки(Результаты.Обработано, Получатели.Количество()));
		НакопитьЗначение(Результаты.Обработано, 1);
	КонецЦикла;
	
	Результаты.Детально = ОбщегоНазначения.ТаблицаЗначенийВМассив(ТаблицаРезультатов);
	
	Возврат Результаты;
	
КонецФункции

Процедура ДобавитьОписаниеВложения(ОписанияВложений, ФизическоеЛицо, РассылаемыйДокумент, ОписаниеВложения)
	
	ВложенияФизическогоЛица = ОписанияВложений.Получить(ФизическоеЛицо);
	Если ВложенияФизическогоЛица = Неопределено Тогда
		ВложенияФизическогоЛица = Новый Соответствие;
		ОписанияВложений.Вставить(ФизическоеЛицо, ВложенияФизическогоЛица);
	КонецЕсли;
	
	ВложенияОбъекта = ВложенияФизическогоЛица.Получить(РассылаемыйДокумент);
	Если ВложенияОбъекта = Неопределено Тогда
		ВложенияОбъекта = Новый Массив;
		ВложенияФизическогоЛица.Вставить(РассылаемыйДокумент, ВложенияОбъекта);
	КонецЕсли;
	
	ВложенияОбъекта.Добавить(ОписаниеВложения);
	
КонецПроцедуры

Функция ПрогрессОтправки(Обработано, Всего)
	Возврат Макс(5, Окр(Обработано / Всего * 100));
КонецФункции

Процедура НакопитьЗначение(Накопленное, Значение)
	Накопленное = Накопленное + Значение;
КонецПроцедуры

Функция ОписаниеПолучателя(Адрес, Представление)
	Возврат Новый Структура("Адрес, Представление", Адрес, Представление);
КонецФункции

Функция ПараметрыПисьма()
	
	ПараметрыПисьма = Новый Структура;
	ПараметрыПисьма.Вставить("Тема", "");
	ПараметрыПисьма.Вставить("Тело", "");
	ПараметрыПисьма.Вставить("Кому", Новый Массив);
	ПараметрыПисьма.Вставить("Вложения", Новый Массив);
	
	Возврат ПараметрыПисьма;
	
КонецФункции

Функция ОписаниеВложения(ИмяФайла, АдресВХранилище)
	Возврат Новый Структура("Представление, АдресВоВременномХранилище", ИмяФайла, АдресВХранилище);
КонецФункции

Функция ДанныеПечатиБезОтбора(Обработчик, МассивОбъектов) Экспорт
	
	ДанныеПечати = ОбщегоНазначения.ВычислитьВБезопасномРежиме(Обработчик + "(Параметры.МассивОбъектов)", 
		Новый Структура("МассивОбъектов", МассивОбъектов));
	
	ОбщегоНазначенияКлиентСервер.Проверить(
		ТипЗнч(ДанныеПечати) = Тип("ТаблицаЗначений") 
		И ДанныеПечати.Колонки.Найти("ФизическоеЛицо") <> Неопределено
		И ДанныеПечати.Колонки.Найти("РассылаемыйДокумент") <> Неопределено, 
		СтрШаблон(НСтр("ru = 'Данные печати должны быть подготовлены в виде таблицы значений, 
			 |в которой есть обязательные колонки ФизическоеЛицо и РассылаемыйДокумент.
			 |Проверьте возвращаемое значение функции %1';
			 |en = 'The print data must be prepared in the form of a table of values
			 |where there are mandatory columns ФизическоеЛицо and РассылаемыйДокумент.
			 |Check the return value of the function %1'"), 
			 Обработчик), 
		"РассылкаДокументов");
	
	Возврат ДанныеПечати;
	
КонецФункции

Функция ПричиныОшибки(ОшибочныеПолучатели)
	
	Причины = "";
	Для Каждого КлючИЗначение Из ОшибочныеПолучатели Цикл
		Если Не ПустаяСтрока(Причины) Тогда
			Причины = Причины + Символы.ПС;
		КонецЕсли;
		Причины = Причины + СтрШаблон(НСтр("ru = 'Проблема отправки по адресу %1, причина: «%2».';
											|en = 'Problem when sending to %1. Reason: «%2».'"), 
			КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла;
	Возврат Причины;
	
КонецФункции

#Область РезультатыОтправки

Функция ТаблицаРезультатовОтправки()
	Возврат РассылкаДокументов.ТаблицаРезультатовОтправки();
КонецФункции

Функция ОписаниеРезультатаОтправки()
	
	РезультатОтправки = Неопределено; 
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.РассылкаДокументов") Тогда
		МодульРезультатыРассылкиДокументов = ОбщегоНазначения.ОбщийМодуль("РезультатыРассылкиДокументов");
		РезультатОтправки = МодульРезультатыРассылкиДокументов.НовыйРезультатОтправки();
	КонецЕсли;
	Возврат РезультатОтправки;
	
КонецФункции

Процедура ЗаполнитьУспешнымРезультатом(РезультатОтправки)

	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.РассылкаДокументов") Тогда
		МодульРезультатыРассылкиДокументов = ОбщегоНазначения.ОбщийМодуль("РезультатыРассылкиДокументов");
		МодульРезультатыРассылкиДокументов.ЗаполнитьУспешнымРезультатом(РезультатОтправки);
	Иначе
		РезультатОтправки = Истина;
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьНеуспешнымРезультатом(РезультатОтправки, ПричиныОшибки)
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.РассылкаДокументов") Тогда
		МодульРезультатыРассылкиДокументов = ОбщегоНазначения.ОбщийМодуль("РезультатыРассылкиДокументов");
		МодульРезультатыРассылкиДокументов.ЗаполнитьНеуспешнымРезультатом(РезультатОтправки, ПричиныОшибки);
	Иначе
		РезультатОтправки = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаписатьРезультатОтправки(ФизическоеЛицо, Получатель, РассылаемыйДокумент, РезультатОтправки)
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.РассылкаДокументов") Тогда
		МодульРезультатыРассылкиДокументов = ОбщегоНазначения.ОбщийМодуль("РезультатыРассылкиДокументов");
		МодульРезультатыРассылкиДокументов.ЗаписатьРезультатОтправки(
			ФизическоеЛицо, Получатель, РассылаемыйДокумент, РезультатОтправки);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли