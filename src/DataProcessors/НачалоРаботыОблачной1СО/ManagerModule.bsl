#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ЮридическиеЛицаПоИННВФоне(Параметры, АдресРезультата) Экспорт
	
	МодульРаботаСКонтрагентами = ОбщегоНазначения.ОбщийМодуль("РаботаСКонтрагентами");
	
	ПоместитьВоВременноеХранилище(
		МодульРаботаСКонтрагентами.ЮридическиеЛицаПоИНН(Параметры.ИНН, Параметры.КодРегиона, Параметры.Адрес),
		АдресРезультата);
	
КонецПроцедуры

Процедура РеквизитыПредпринимателяПоИННВФоне(Параметры, АдресРезультата) Экспорт
	
	МодульРаботаСКонтрагентами = ОбщегоНазначения.ОбщийМодуль("РаботаСКонтрагентами");
	
	ПоместитьВоВременноеХранилище(
		МодульРаботаСКонтрагентами.РеквизитыПредпринимателяПоИНН(Параметры.ИНН),
		АдресРезультата);
	
КонецПроцедуры

Процедура ЮридическиеЛицаПоНаименованиюВФоне(Параметры, АдресРезультата) Экспорт
	
	МодульРаботаСКонтрагентами = ОбщегоНазначения.ОбщийМодуль("РаботаСКонтрагентами");
	
	ПоместитьВоВременноеХранилище(
		МодульРаботаСКонтрагентами.ЮридическиеЛицаПоНаименованию(Параметры.Наименование, Параметры.КодРегиона, Параметры.Адрес),
		АдресРезультата);
	
КонецПроцедуры
	
Процедура ИнформацияОПодразделенииЮридическогоЛицаПоУникальномуНомеруВФоне(Параметры, АдресРезультата) Экспорт
	
	МодульРаботаСКонтрагентами = ОбщегоНазначения.ОбщийМодуль("РаботаСКонтрагентами");
	
	ПоместитьВоВременноеХранилище(
		МодульРаботаСКонтрагентами.ИнформацияОПодразделенииЮридическогоЛицаПоУникальномуНомеру(Параметры.УникальныйНомер),
		АдресРезультата);
	
КонецПроцедуры
	
Процедура ИнформацияОПодразделенииЮридическогоЛицаПоИННКПП(Параметры, АдресРезультата) Экспорт
	
	МодульРаботаСКонтрагентами = ОбщегоНазначения.ОбщийМодуль("РаботаСКонтрагентами");
	
	ПоместитьВоВременноеХранилище(
		МодульРаботаСКонтрагентами.ИнформацияОПодразделенииЮридическогоЛицаПоИННКПП(Параметры.ИНН, Параметры.КПП),
		АдресРезультата);
	
КонецПроцедуры

Функция ПараметрыФормыДляКнопкиНазад(Форма, ТекущаяСтраница) Экспорт
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("РеквизитыФормы",  РеквизитыФормыВПараметр(Форма));
	ДополнительныеПараметры.Вставить("ЭлементыФормы",   ЭлементыФормыВПараметр(Форма));
	ДополнительныеПараметры.Вставить("ТекущаяСтраница", ТекущаяСтраница.Имя);
	
	Адрес = ПоместитьВоВременноеХранилище(ДополнительныеПараметры, Новый УникальныйИдентификатор);
	
	Возврат Адрес;
	
КонецФункции

Функция РеквизитыФормыВПараметр(Форма)
	
	Реквизиты = Новый Структура;
	Для каждого Строка Из Форма.ПолучитьРеквизиты() Цикл
		
		Значение = Форма[Строка.Имя];
		Реквизиты.Вставить(Строка.Имя, Значение);
	
	КонецЦикла;
	
	Возврат Реквизиты;
	
КонецФункции

Функция ЭлементыФормыВПараметр(Форма)
	
	Результат = Новый Структура;
	Для каждого Элемент Из Форма.Элементы Цикл
		
		Свойства = Новый Структура();
		
		ДобавитьЕслиЕсть(Свойства, Элемент, "Видимость");
		ДобавитьЕслиЕсть(Свойства, Элемент, "Заголовок");
		ДобавитьЕслиЕсть(Свойства, Элемент, "ЦветРамки");
		ДобавитьЕслиЕсть(Свойства, Элемент, "Картинка");
		
		// Запоминаем текущую страницу
		Если ТипЗнч(Элемент) = Тип("ГруппаФормы")
			И Элемент.Вид = ВидГруппыФормы.Страницы Тогда
			
			Свойства.Вставить("ТекущаяСтраница", Элемент.ТекущаяСтраница.Имя);
			
		КонецЕсли;
			
		Результат.Вставить(Элемент.Имя, Свойства);
	
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ДобавитьЕслиЕсть(Свойства, Элемент, ИмяСвойства)

	Попытка
		Если НЕ СтрНайти(Элемент.Имя, "РасширеннаяПодсказка") 
			И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Элемент, ИмяСвойства)  Тогда
			Свойства.Вставить(ИмяСвойства, Элемент[ИмяСвойства]);
		КонецЕсли;
	Исключение
		// Не требует специальной обработки
	КонецПопытки;
	
КонецФункции

Процедура РазобратьПараметрыФормыПриСозданииНаСервере(Форма, Параметры, ИмяПараметра) Экспорт
	
	Адрес = "";
	
	// Сюда зайдем при нажатии кнопки Назад в других формах (ФормаСозданияОрганизации, Заявление) 
	Если Параметры.Свойство(ИмяПараметра, Адрес)
		И ЭтоАдресВременногоХранилища(Адрес) Тогда
		
		Значение = ПолучитьИзВременногоХранилища(Адрес);
		ПараметрВРеквизитыФормы(Форма, Значение.РеквизитыФормы);
		
		// Повторяем заполнение параметров, т.к. строка выше их очищает
		Параметры.Свойство(ИмяПараметра, Форма[ИмяПараметра])
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПараметрВРеквизитыФормы(Форма, РеквизитыФормы)
	
	Реквизиты = Новый Структура;
	Для каждого Реквизиты Из Форма.ПолучитьРеквизиты() Цикл
		
		Если ТипЗнч(Форма[Реквизиты.Имя]) = Тип("ДанныеФормыКоллекция") Тогда
			
			Таблица = РеквизитыФормы[Реквизиты.Имя].Выгрузить();
			Форма[Реквизиты.Имя].Загрузить(Таблица);
			
		ИначеЕсли ТипЗнч(Форма[Реквизиты.Имя]) = Тип("ДанныеФормыСтруктура") Тогда
			
			ЗаполнитьЗначенияСвойств(Форма[Реквизиты.Имя], РеквизитыФормы[Реквизиты.Имя]);
			
		Иначе
			
			Форма[Реквизиты.Имя] = РеквизитыФормы[Реквизиты.Имя];
			
		КонецЕсли;
	
	КонецЦикла;
	
	Возврат Реквизиты;
	
КонецФункции

Функция ПараметрыВЭлементыФормы(Форма, ИмяПараметра) Экспорт
	
	Значение = ПолучитьИзВременногоХранилища(Форма[ИмяПараметра]);
	ЭлементыФормы = Значение.ЭлементыФормы;
	
	Результат = Новый Структура;
	Для каждого Элемент Из Форма.Элементы Цикл
		
		Свойства = ЭлементыФормы[Элемент.Имя];

		// Восстанавливаем текущую страницу
		Если ЭлементыФормы[Элемент.Имя].Свойство("ТекущаяСтраница") Тогда
			ЗаполнитьЗначенияСвойств(Элемент, Свойства,,"ТекущаяСтраница");
			Элемент.ТекущаяСтраница = Форма.Элементы[Свойства.ТекущаяСтраница];
		Иначе
			ЗаполнитьЗначенияСвойств(Элемент, Свойства);
		КонецЕсли;
	
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция УстановитьНастройкиНачалаРаботы(ОбновитьВсе = Истина) Экспорт
	
	ЭтоРежимОблачной1СО = ДокументооборотСКОПовтИсп.ЭтоРежимОблачной1СО();
	Если НЕ ЭтоРежимОблачной1СО Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ЕстьПанельРазделов = Облачная1СОСлужебныйПереопределяемый.ПроверитьНастройкиПервогоЗапуска(ОбновитьВсе);
	ОбновитьИнтерфейс  = НЕ ЕстьПанельРазделов;
	
	Возврат ОбновитьИнтерфейс;
	
КонецФункции

#КонецЕсли