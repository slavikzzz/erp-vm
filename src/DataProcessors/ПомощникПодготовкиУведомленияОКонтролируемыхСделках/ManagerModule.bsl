#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Устанавливает новые значения для списка объектов, согласно структуре переданных реквизитов
// Параметры:
//	Параметры - Структура:
//		* ИмяМетаданныхОбъекта - Строка - Полное имя метаданных объекта, для реквизитов которого будут устанавливаться новые значения
//		* ОбрабатываемыеОбъекты - Массив - массив ссылок, соответсвующих имени метаданных объекта, для которых будут устанавливаться новые значения
//		* ИзменяемыеРеквизиты - Структура - Структура, содержащая данные об изменяемых реквизитах, с произвольным количеством свойств такого формата:
//			* Ключ - Строка - Имя реквизита, для которого будет изменяться значение.
//			* Значение - Произвольный - Новое значение реквизита, тип которого соответсвует типу реквизита объекта.
//			Причем если изменения происходит в табличной части, то в качестве ключа будет выступать имя табличной части, а в качестве значения - точно такая же структура.
Процедура УстановитьНовыеЗначенияДляСпискаОбъектов(Параметры, АдресХранилища) Экспорт
	
	СтрокаРезультата = "";
	
	МассивСсылокДляИзменения = Параметры.ОбрабатываемыеОбъекты;
	МетаданныеИзменяемогоОбъекта = Метаданные.НайтиПоПолномуИмени(Параметры.ИмяМетаданныхОбъекта);
	СтруктураИзменяемыхРеквизитов = Параметры.ИзменяемыеРеквизиты;
	
	НомерПоПорядку = 0;
	ВсегоОбъектов = МассивСсылокДляИзменения.Количество();
	
	Для каждого СсылкаДляИзменения Из МассивСсылокДляИзменения Цикл
		
		Если Не МетаданныеИзменяемогоОбъекта = СсылкаДляИзменения.Метаданные() Тогда
			Продолжить;
		КонецЕсли;
		
		ПроцентВыполнения = Окр(100 * НомерПоПорядку / ВсегоОбъектов);
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Осталось обработать: %1.';
										|en = 'Left to process: %1.'"), ВсегоОбъектов - НомерПоПорядку);
		ДлительныеОперации.СообщитьПрогресс(ПроцентВыполнения, ТекстСообщения);
		НомерПоПорядку = НомерПоПорядку + 1;
		
		Если ОбщегоНазначения.ЭтоСправочник(МетаданныеИзменяемогоОбъекта)
			ИЛИ ОбщегоНазначения.ЭтоДокумент(МетаданныеИзменяемогоОбъекта)
			ИЛИ ОбщегоНазначения.ЭтоПланВидовХарактеристик(МетаданныеИзменяемогоОбъекта) Тогда
		
			ИзменяемыйОбъект = СсылкаДляИзменения.ПолучитьОбъект();
			
			// Блокировка объекта:
			Попытка
				ЗаблокироватьДанныеДляРедактирования(СсылкаДляИзменения);
			Исключение
				ТекстСообщения = НСтр("ru = 'Не удалось заблокировать для изменения объект <%ИзменяемыйОбъект%>, по причине: %ИнформацияОбОшибке%';
										|en = 'Cannot lock object <%ИзменяемыйОбъект%> for changing due to: %ИнформацияОбОшибке% '");
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ИзменяемыйОбъект%", СсылкаДляИзменения);
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ИнформацияОбОшибке%", ИнформацияОбОшибке());
				СтрокаРезультата = СтрокаРезультата + ТекстСообщения + Символы.ПС;
				Продолжить;
			КонецПопытки;
			
			// Изменение реквизитов объекта:
			
			Для каждого ИзменяемыйРеквизит Из СтруктураИзменяемыхРеквизитов Цикл
				
				Если Не МетаданныеИзменяемогоОбъекта.Реквизиты.Найти(ИзменяемыйРеквизит.Ключ) = Неопределено Тогда
					
					Попытка
						ИзменяемыйОбъект[ИзменяемыйРеквизит.Ключ] = ИзменяемыйРеквизит.Значение;
					Исключение
						ТекстСообщения = НСтр("ru = 'Не удалось изменить реквизит <%ИмяРеквизита%> в объекте <%ИзменяемыйОбъект%>, по причине: %ИнформацияОбОшибке%';
												|en = 'Cannot change attribute <%ИмяРеквизита%> in object <%ИзменяемыйОбъект%> due to: %ИнформацияОбОшибке%'");
						ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ИмяРеквизита%", ИзменяемыйРеквизит.Ключ);
						ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ИзменяемыйОбъект%", СсылкаДляИзменения);
						ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ИнформацияОбОшибке%", ИнформацияОбОшибке());
						СтрокаРезультата = СтрокаРезультата + ТекстСообщения + Символы.ПС;
						Продолжить;
					КонецПопытки;
					
				КонецЕсли;
				
				МетаданныеТабличнойЧасти = МетаданныеИзменяемогоОбъекта.ТабличныеЧасти.Найти(ИзменяемыйРеквизит.Ключ);
				Если Не МетаданныеТабличнойЧасти = Неопределено Тогда
					// изменяем табличную часть объекта
					
					Для каждого Стр Из ИзменяемыйОбъект[ИзменяемыйРеквизит.Ключ] Цикл
						
						Для каждого ИзменяемыйРеквизитТабличнойЧасти Из ИзменяемыйРеквизит.Значение Цикл
							
							Если Не МетаданныеТабличнойЧасти.Реквизиты.Найти(ИзменяемыйРеквизитТабличнойЧасти.Ключ) = Неопределено Тогда
								
								Попытка
									Стр[ИзменяемыйРеквизитТабличнойЧасти.Ключ] = ИзменяемыйРеквизитТабличнойЧасти.Значение;
								Исключение
									ТекстСообщения = НСтр("ru = 'Не удалось изменить реквизит <%ИмяРеквизита%> табличной части <%ИмяТабличнойЧасти%> объекта <%ИзменяемыйОбъект%>, по причине: %ИнформацияОбОшибке%';
															|en = 'Cannot change attribute <%ИмяРеквизита%> of tabular section <%ИмяТабличнойЧасти%> of object <%ИзменяемыйОбъект%> due to: %ИнформацияОбОшибке%'");
									ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ИмяРеквизита%", ИзменяемыйРеквизитТабличнойЧасти.Ключ);
									ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ИмяТабличнойЧасти%", ИзменяемыйРеквизит.Ключ);
									ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ИзменяемыйОбъект%", СсылкаДляИзменения);
									ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ИнформацияОбОшибке%", ИнформацияОбОшибке());
									СтрокаРезультата = СтрокаРезультата + ТекстСообщения + Символы.ПС;
									Продолжить;
								КонецПопытки;
								
							КонецЕсли;
							
						КонецЦикла;
						
					КонецЦикла;
					
				КонецЕсли;
				
			КонецЦикла;
			
			// Определение режима записи объекта:
			РежимЗаписи = Неопределено;
			Если ОбщегоНазначения.ЭтоДокумент(МетаданныеИзменяемогоОбъекта) Тогда
				РежимЗаписи = РежимЗаписиДокумента.Запись;
				Если ИзменяемыйОбъект.Проведен Тогда
					РежимЗаписи = РежимЗаписиДокумента.Проведение;
				КонецЕсли;
			КонецЕсли;
			
			// Запись объекта:
			Попытка
				Если РежимЗаписи = Неопределено Тогда
					ИзменяемыйОбъект.Записать();
				Иначе
					ИзменяемыйОбъект.Записать(РежимЗаписи);
				КонецЕсли;
			Исключение
				ТекстСообщения = НСтр("ru = 'Не удалось записать изменения для объекта <%ИзменяемыйОбъект%>, по причине: %ИнформацияОбОшибке%';
										|en = 'Cannot write changes for object <%ИзменяемыйОбъект%>, due to: %ИнформацияОбОшибке% '");
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ИзменяемыйОбъект%", СсылкаДляИзменения);
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ИнформацияОбОшибке%", ИнформацияОбОшибке());
				СтрокаРезультата = СтрокаРезультата + ТекстСообщения + Символы.ПС;
			КонецПопытки;
			
			РазблокироватьДанныеДляРедактирования(СсылкаДляИзменения);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ПоместитьВоВременноеХранилище(СтрокаРезультата, АдресХранилища);
	
КонецПроцедуры

Функция ПараметрыОбработкиИзменения() Экспорт
	
	ПараметрыОбработкиИзменения = Новый Структура;
	ПараметрыОбработкиИзменения.Вставить("ИмяМетаданныхОбъекта");
	ПараметрыОбработкиИзменения.Вставить("ИзменяемыеРеквизиты");
	ПараметрыОбработкиИзменения.Вставить("ОбрабатываемыеОбъекты");
	
	Возврат ПараметрыОбработкиИзменения;
	
КонецФункции

#КонецОбласти

#КонецЕсли