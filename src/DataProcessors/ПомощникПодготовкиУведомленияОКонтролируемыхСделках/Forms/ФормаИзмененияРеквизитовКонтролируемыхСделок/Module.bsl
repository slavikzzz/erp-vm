#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("Уведомление") Тогда
		ВерсияУведомления_2012 = Параметры.Уведомление.ВерсияУведомления <= КонтролируемыеСделкиКлиентСервер.ВерсияУведомления_2012();
		Элементы.ГруппаДанныхОКВЭД.Видимость = ВерсияУведомления_2012;
		Элементы.ГруппаДанныхОКВЭД2.Видимость = Не ВерсияУведомления_2012;
	КонецЕсли;
	
	СписокКодовНаименованийСделок = КонтролируемыеСделкиПовтИсп.СписокКодовНаименованийСделки();
	Элементы.КодНаименованияСделки.СписокВыбора.Очистить();
	Для Каждого Код Из СписокКодовНаименованийСделок Цикл
		НовыйКод = Элементы.КодНаименованияСделки.СписокВыбора.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйКод, Код);
	КонецЦикла;
	
	СписокКодовПоставки = КонтролируемыеСделкиПовтИсп.КодыУсловийПоставки();
	Элементы.КодУсловийПоставки.СписокВыбора.Очистить();
	Для Каждого Код Из СписокКодовПоставки Цикл
		НовыйКод = Элементы.КодУсловийПоставки.СписокВыбора.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйКод, Код);
	КонецЦикла;
	
	УправлениеДоступностьюФормы(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ФлагПриИзменении(Элемент)
	УправлениеДоступностьюФормы(ЭтаФорма);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИзменения(Команда)
	
	Результат = Новый Структура;
	Если ДоговорФлаг Тогда
		Результат.Вставить("НомерДоговора", НомерДоговора);
		Результат.Вставить("ДатаДоговора", ДатаДоговора);
	КонецЕсли;
	Если КодНаименованияФлаг Тогда
		Результат.Вставить("КодНаименованияСделки", КодНаименованияСделки);
	КонецЕсли;
	Если СпособОпределенияЦеныСделкиФлаг Тогда
		Результат.Вставить("СпособОпределенияЦеныСделки", СпособОпределенияЦеныСделки);
	КонецЕсли;
	Если КодОКВЭДФлаг Тогда
		Результат.Вставить("КодОКВЭД", КодОКВЭД);
	КонецЕсли;
	Если КодОКВЭД2Флаг Тогда
		Результат.Вставить("КодОКВЭД2", КодОКВЭД2);
	КонецЕсли;
	Если НовоеМорскоеМесторождениеФлаг Тогда
		Результат.Вставить("ОснованиеКонтролируемости136", НовоеМорскоеМесторождение);
	КонецЕсли;
	Если РазныеСтавкиПоНалогуНаПрибыльФлаг Тогда
		Результат.Вставить("ОснованиеКонтролируемости131", РазныеСтавкиПоНалогуНаПрибыль);
	КонецЕсли;
	Если НалогНаДопДоходФлаг Тогда
		Результат.Вставить("ОснованиеКонтролируемости140", НалогНаДопДоход);
	КонецЕсли;
	
	СтруктураРеквизитовТабличнойЧасти = Новый Структура;
	Если КодУсловийПоставкиФлаг Тогда
		СтруктураРеквизитовТабличнойЧасти.Вставить("КодУсловийПоставки", КодУсловийПоставки);
	КонецЕсли;
	Если АдресОтправкиТовараФлаг Тогда
		СтруктураРеквизитовТабличнойЧасти.Вставить("СтранаОтправкиТовара", СтранаОтправкиТовара);
		СтруктураРеквизитовТабличнойЧасти.Вставить("ГородОтправкиТовара", ГородОтправкиТовара);
		СтруктураРеквизитовТабличнойЧасти.Вставить("НаселенныйПунктОтправкиТовара", НаселенныйПунктОтправкиТовара);
	КонецЕсли;
	Если АдресСовершенияСделкиФлаг Тогда
		СтруктураРеквизитовТабличнойЧасти.Вставить("СтранаСовершенияСделки", СтранаСовершенияСделки);
		СтруктураРеквизитовТабличнойЧасти.Вставить("ГородСовершенияСделки", ГородСовершенияСделки);
		СтруктураРеквизитовТабличнойЧасти.Вставить("НаселенныйПунктСовершенияСделки", НаселенныйПунктСовершенияСделки);
	КонецЕсли;
	Если СтруктураРеквизитовТабличнойЧасти.Количество() Тогда
		Результат.Вставить("Сделки", СтруктураРеквизитовТабличнойЧасти);
	КонецЕсли;
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеДоступностьюФормы(Форма)
	
	// Договор:
	Форма.Элементы.НомерДоговора.Доступность = Форма.ДоговорФлаг;
	Форма.Элементы.ДатаДоговора.Доступность = Форма.ДоговорФлаг;
	
	// Реквизиты:
	Форма.Элементы.КодНаименованияСделки.Доступность = Форма.КодНаименованияФлаг;
	Форма.Элементы.ПризнакОпределенияЦеныСделки.Доступность = Форма.СпособОпределенияЦеныСделкиФлаг;
	Форма.Элементы.КодУсловийПоставки.Доступность = Форма.КодУсловийПоставкиФлаг;
	Форма.Элементы.НовоеМорскоеМесторождение.Доступность = Форма.НовоеМорскоеМесторождениеФлаг;
	Форма.Элементы.РазныеСтавкиПоНалогуНаПрибыль.Доступность = Форма.РазныеСтавкиПоНалогуНаПрибыльФлаг;
	Форма.Элементы.НалогНаДопДоход.Доступность = Форма.НалогНаДопДоходФлаг;
	
	// АдресОтправки:
	Форма.Элементы.СтранаОтправкиТовара.Доступность = Форма.АдресОтправкиТовараФлаг;
	Форма.Элементы.ГородОтправкиТовара.Доступность = Форма.АдресОтправкиТовараФлаг;
	Форма.Элементы.НаселенныйПунктОтправкиТовара.Доступность = Форма.АдресОтправкиТовараФлаг;
	
	// АдресСовершенияСделки:
	Форма.Элементы.СтранаСовершенияСделки.Доступность = Форма.АдресСовершенияСделкиФлаг;
	Форма.Элементы.ГородСовершенияСделки.Доступность = Форма.АдресСовершенияСделкиФлаг;
	Форма.Элементы.НаселенныйПунктСовершенияСделки.Доступность = Форма.АдресСовершенияСделкиФлаг;
	
	// ОКВЭД:
	Форма.Элементы.КодОКВЭД.Доступность = Форма.КодОКВЭДФлаг;
	Форма.Элементы.КодОКВЭД2.Доступность = Форма.КодОКВЭД2Флаг;
	
КонецПроцедуры

#КонецОбласти
