#Область ОписаниеПеременных

&НаКлиенте
Перем ПараметрыОбработчикаОжидания;
&НаКлиенте
Перем ФормаДлительнойОперации;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Объект.ДатаПерехода    = ПолучитьДатуПереходаНаЕНП();
	ДатаПереходаПредыдущая = Объект.ДатаПерехода;
	
	Используется1СОтчетность = РегламентированнаяОтчетность.Используется1СОтчетность();
	ЕстьПравоНаДОсКО         = ДокументооборотСКОВызовСервера.ЕстьПравоНаДОсКО(Ложь);
	Объект.Организация = Параметры.Организация;
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		Объект.Организация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	КонецЕсли;
	
	ОбщегоНазначенияБПВызовСервера.ЗаполнитьСписокОрганизаций(Элементы.ПолеОрганизация, СоответствиеОрганизаций);
	ОбщегоНазначенияБПКлиентСервер.УстановитьЗначениеПолеОрганизация(ПолеОрганизация, Объект.Организация, Ложь);
	
	ЗаполнитьСведенияОбОрганизации();
	ОпределитьДатуПерехода();
	
	ПервоначальноеЗаполнениеПеременныхФормы();
	ПервоначальноеЗаполнениеТаблицыОпераций();
	
	ЗаполнитьФормуНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ОперацияПоЕдиномуНалоговомуСчету"
		Или ИмяСобытия = "ИзменениеОперацииБух" Тогда
		ОбработкаИзмененияДокументовНаСервере();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПолеОрганизацияПриИзменении(Элемент)
	
	ПолеОрганизацияПриИзмененииНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеОрганизацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ОбщегоНазначенияБПКлиент.ПолеОрганизацияОбработкаВыбора(
		Элемент,
		ВыбранноеЗначение,
		СтандартнаяОбработка,
		СоответствиеОрганизаций,
		Объект.Организация,
		Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеОрганизацияОткрытие(Элемент, СтандартнаяОбработка)
	
	ОбщегоНазначенияБПКлиент.ПолеОрганизацияОткрытие(Элемент, СтандартнаяОбработка,
		ПолеОрганизация, СоответствиеОрганизаций);
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацияПереходаНажатие(Элемент)
	
	ИмяОперации = Элемент.Имя;
	
	СтрокиОперации = ТаблицаОпераций.НайтиСтроки(Новый Структура("ИмяОперации", ИмяОперации));
	Если СтрокиОперации.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	СтрокаОперации = СтрокиОперации[0];
	
	СписокКоманд = СписокКомандДляОперации(СтрокаОперации);
	
	Команда = Неопределено;
	Если СписокКоманд.Количество() > 0 Тогда
		Если СписокКоманд.Количество() = 1 И Элемент.Имя <> "ЗачетАвансов" Тогда
			Команда = СписокКоманд[0];
			ОбработкаНажатияКнопкиМеню(Команда.Значение, Элемент, СтрокаОперации);
		Иначе
			ДополнительныеПараметры = Новый Структура;
			ДополнительныеПараметры.Вставить("Элемент", Элемент);
			ДополнительныеПараметры.Вставить("СтрокаОперации", СтрокаОперации);
			ОповещениеВыбора = Новый ОписаниеОповещения("ВыборИзМенюЗавершение", ЭтотОбъект, ДополнительныеПараметры);
			ПоказатьВыборИзМеню(ОповещениеВыбора, СписокКоманд, Элемент);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеОрганизацияОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПереходаПриИзменении(Элемент)
	
	Если Объект.ДатаПерехода < ПолучитьДатуПереходаНаЕНП() Тогда
		Объект.ДатаПерехода = ДатаПереходаПредыдущая;
		ТекстСообщения = НСтр("ru = 'Дата начала расчетов по ЕНС не может быть ранее 1 января 2023 г.';
								|en = 'Start date of unified tax account AR/AP cannot be earlier than January 1, 2023'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Если ЗарегистрированыДокументыНаДатуПерехода() Тогда
		Оповещение = Новый ОписаниеОповещения("ВопросПереносДатыЗавершение", ЭтотОбъект);
		ТекстВопроса = СтрШаблон(НСтр("ru = 'На %1 уже введены документы по ЕНС.
			| 
			| Документы регистрации расчетов по ЕНС будут перенесены %2
			|
			| Изменить дату?';
			|en = 'Unified tax account documents are already registered on %1.
			|
			| Documents that register the unified tax account AR/AP will be transferred to %2
			|
			| Change the date?'"),
				Формат(ДатаПереходаПредыдущая, "ДЛФ=DD"),
				Формат(Объект.ДатаПерехода, "ДЛФ=DD"));
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
		
	Иначе
		ОбработатьИзменениеДаты();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПереходаОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьДействиеСверка(Команда)
	
	ОпределитьПараметрыСверки();
	
	Если ОписаниеДействияСверка.ИмяФормы = "Обработка.ОбщиеОбъектыРеглОтчетности.Форма.ПредложениеОформитьЗаявлениеНаПодключение" Тогда
		ОповещениеВыполнениеСверки = Новый ОписаниеОповещения("ОбработатьВыполнениеСверкиЗавершение1СОтчетность", ЭтотОбъект);
	Иначе
		ОповещениеВыполнениеСверки = Новый ОписаниеОповещения("ОбработатьВыполнениеСверки", ЭтотОбъект);
	КонецЕсли;
	
	ОткрытьФорму(ОписаниеДействияСверка.ИмяФормы, ОписаниеДействияСверка.ПараметрыФормы, ЭтотОбъект,,,,ОповещениеВыполнениеСверки);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодробнееНаИТС(Команда)
	
	АдресНаИТС = "https://its.1c.ru/bmk/ens_start";
	ПерейтиПоНавигационнойСсылке(АдресНаИТС);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПервоначальноеЗаполнениеПеременныхФормы()
	
	СтруктураКартинок = Новый Структура;
	СтруктураКартинок.Вставить("КартинкаТекущаяОперация",     БиблиотекаКартинок.СтрелкаНаправоЖелтая);
	СтруктураКартинок.Вставить("КартинкаОперацияВыполнена",   БиблиотекаКартинок.ОперацияВыполненаУспешно);
	СтруктураКартинок.Вставить("КартинкаОперацияНеВыполнена", Новый Картинка);
	СтруктураКартинок.Вставить("КартинкаОперацияСОшибками",	  БиблиотекаКартинок.ОперацияВыполненаСОшибками);
	СтруктураКартинок.Вставить("КартинкаОперацияУстарела",    БиблиотекаКартинок.ОперациюНеобходимоПовторить);
	СтруктураКартинок.Вставить("КартинкаОперацияНеВидна",     Новый Картинка);
	СтруктураКартинок.Вставить("КартинкаОперацияПропущена",   БиблиотекаКартинок.ОперацияПропущена);
	СтруктураКартинок.Вставить("КартинкаОтчет",               БиблиотекаКартинок.Отчет);
	
	Картинки = Новый ФиксированнаяСтруктура(СтруктураКартинок);
	
	СтруктураЦветаТекста = Новый Структура;
	СтруктураЦветаТекста.Вставить("ЦветТекстаПоУмолчанию",      Новый Цвет);
	СтруктураЦветаТекста.Вставить("ОперацияНеВыполнена",        Новый Цвет);
	СтруктураЦветаТекста.Вставить("ОперацияВыполнена",          WebЦвета.Зеленый);
	СтруктураЦветаТекста.Вставить("ОперацияУстарела",           WebЦвета.СероСиний);
	СтруктураЦветаТекста.Вставить("ОперацияПропущена",          ЦветаСтиля.ЦветТекстаФормы);
	СтруктураЦветаТекста.Вставить("ОперацияВыполненаСОшибками", ЦветаСтиля.ПросроченныеДанныеЦвет);
	
	ЦветаТекстаОпераций = Новый ФиксированнаяСтруктура(СтруктураЦветаТекста);
	
	СтруктураТипыОпераций = Новый Структура;
	СтруктураТипыОпераций.Вставить("ОбработкаДанных",       1);
	СтруктураТипыОпераций.Вставить("СписокДокументов",      2);
	СтруктураТипыОпераций.Вставить("Отчет",                 3);
	СтруктураТипыОпераций.Вставить("ПереходНаВнешнююФорму", 4);
	
	ТипыОпераций = Новый ФиксированнаяСтруктура(СтруктураТипыОпераций);
	
	СоответствиеСпособыОбработкиДокументов = Новый Соответствие;
	СоответствиеСпособыОбработкиДокументов.Вставить(0, "НеОбрабатывать");
	СоответствиеСпособыОбработкиДокументов.Вставить(1, "ПометитьНаУдаление");
	СоответствиеСпособыОбработкиДокументов.Вставить(2, "ОтменитьПроведение");
	
	СпособыОбработкиДокументов = Новый ФиксированноеСоответствие(СоответствиеСпособыОбработкиДокументов);
	
КонецПроцедуры

// Функция возвращает таблицу операций, "описательную часть" без анализа статусов выполнения
// При изменении состава операций на форме функция должна быть дополнена
&НаСервере
Процедура ПервоначальноеЗаполнениеТаблицыОпераций()
	
	Если ТаблицаОпераций.Количество() > 0 Тогда
		ТаблицаОпераций.Очистить();
	КонецЕсли;
	
	Если Не ВводОстатков Тогда
		СтрокаОперации                  = ТаблицаОпераций.Добавить();
		СтрокаОперации.ВидОперации      = "ОтчетПоНалогамВзносам";
		СтрокаОперации.ИмяОперации      = "ОтчетПоНалогамВзносам";
		СтрокаОперации.ТипОперации      = ТипыОпераций.Отчет;
		СтрокаОперации.ДействиеОперации = "Отчеты.ОборотноСальдоваяВедомостьПоСчету.СформироватьОтчетНаСервере";
		СтрокаОперации.Порядок          = ТаблицаОпераций.Количество();
		СтрокаОперации.Состояние        = "НеВыполнено";
		СтрокаОперации.Актуальность     = Истина;
		СтрокаОперации.ОчищатьСсылкиНаДокументыПриОтмене = Истина;
		СтрокаОперации.СпособОбработкиДокументовПриОтмене = 1; // ПометитьНаУдаление
	КонецЕсли;
	
	СтрокаОперации                  = ТаблицаОпераций.Добавить();
	СтрокаОперации.ИмяОперации      = "ПереносРасчетовПоНалогам";
	СтрокаОперации.ВидОперации      = Перечисления.ВидыОперацийПоЕдиномуНалоговомуСчету.НачислениеНалогов;
	СтрокаОперации.ТипОперации      = ТипыОпераций.ОбработкаДанных;
	СтрокаОперации.ДействиеОперации = "Обработки.ПомощникПереходаНаЕНС.ПереносРасчетовПоНалогамВзносам";
	СтрокаОперации.Порядок          = ТаблицаОпераций.Количество();
	СтрокаОперации.Состояние        = "НеВыполнено";
	СтрокаОперации.Актуальность     = Истина;
	СтрокаОперации.ОчищатьСсылкиНаДокументыПриОтмене = Истина;
	СтрокаОперации.СпособОбработкиДокументовПриОтмене = 1; // ПометитьНаУдаление
	
	СтрокаОперации                  = ТаблицаОпераций.Добавить();
	СтрокаОперации.ИмяОперации      = "ПереносРасчетовПоСанкциям";
	СтрокаОперации.ВидОперации      = Перечисления.ВидыОперацийПоЕдиномуНалоговомуСчету.НачислениеПенейШтрафов;
	СтрокаОперации.ТипОперации      = ТипыОпераций.ОбработкаДанных;
	СтрокаОперации.ДействиеОперации = "Обработки.ПомощникПереходаНаЕНС.ПереносРасчетовПоНалогамВзносам";
	СтрокаОперации.Порядок          = ТаблицаОпераций.Количество();
	СтрокаОперации.Состояние        = "НеВыполнено";
	СтрокаОперации.Актуальность     = Истина;
	СтрокаОперации.ОчищатьСсылкиНаДокументыПриОтмене = Истина;
	СтрокаОперации.СпособОбработкиДокументовПриОтмене = 1; // ПометитьНаУдаление
	
	СтрокаОперации                  = ТаблицаОпераций.Добавить();
	СтрокаОперации.ИмяОперации      = "ПереносПереплат";
	СтрокаОперации.ВидОперации      = Перечисления.ВидыОперацийПоЕдиномуНалоговомуСчету.КорректировкаСчета;
	СтрокаОперации.ТипОперации      = ТипыОпераций.ОбработкаДанных;
	СтрокаОперации.ДействиеОперации = "Обработки.ПомощникПереходаНаЕНС.ПереносРасчетовПоНалогамВзносам";
	СтрокаОперации.Порядок          = ТаблицаОпераций.Количество();
	СтрокаОперации.Состояние        = "НеВыполнено";
	СтрокаОперации.Актуальность     = Истина;
	СтрокаОперации.ОчищатьСсылкиНаДокументыПриОтмене = Истина;
	СтрокаОперации.СпособОбработкиДокументовПриОтмене = 1; // ПометитьНаУдаление
	
	СтрокаОперации                  = ТаблицаОпераций.Добавить();
	СтрокаОперации.ИмяОперации      = "ЗачетАвансов";
	СтрокаОперации.ВидОперации      = Перечисления.ВидыОперацийПоЕдиномуНалоговомуСчету.УплатаНалогов;
	СтрокаОперации.ТипОперации      = ТипыОпераций.ОбработкаДанных;
	СтрокаОперации.ДействиеОперации = "Обработки.ПомощникПереходаНаЕНС.ЗачетАвансов";
	СтрокаОперации.Порядок          = ТаблицаОпераций.Количество();
	СтрокаОперации.Состояние        = "НеВыполнено";
	СтрокаОперации.Актуальность     = Истина;
	СтрокаОперации.ОчищатьСсылкиНаДокументыПриОтмене = Истина;
	СтрокаОперации.СпособОбработкиДокументовПриОтмене = 1; // ПометитьНаУдаление
	
КонецПроцедуры

// Заполняет таблицу операций, устанавливает статусы в зависимости от реквизитов формы
//
&НаСервере
Процедура ЗаполнитьДанныеОпераций()

	ТабСостоянийОпераций = Новый ТаблицаЗначений;
	
	Если ПереходНаЕНСЗапущен Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ДокументыОперацийПереходаНаЕНС.ВидОперации КАК ВидОперации,
		|	ВЫБОР
		|		КОГДА ДокументыОперацийПереходаНаЕНС.Проведен
		|			ТОГДА ""Выполнено""
		|		КОГДА ДокументыОперацийПереходаНаЕНС.ПометкаУдаления
		|			ТОГДА ""НеВыполнено""
		|		ИНАЧЕ ""ВыполненоСОшибками""
		|	КОНЕЦ КАК Состояние,
		|	ИСТИНА КАК Актуальность,
		|	ДокументыОперацийПереходаНаЕНС.Ссылка КАК ДокументОперации
		|ИЗ
		|	Документ.КорректировкаРасчетовПоНалогам КАК ДокументыОперацийПереходаНаЕНС
		|ГДЕ
		|	ДокументыОперацийПереходаНаЕНС.Организация В(&Организации)
		|	И ДокументыОперацийПереходаНаЕНС.ВводОстатковРасчетовПоЕНС
		|";
		
		Запрос.УстановитьПараметр("ДатаПерехода", Объект.ДатаПерехода);
		Организация = СоответствиеОрганизаций[ПолеОрганизация].Организация;
		Если СоответствиеОрганизаций[ПолеОрганизация].ВключатьОбособленныеПодразделения Тогда
			Запрос.УстановитьПараметр("Организации", Организации);
		Иначе
			Запрос.УстановитьПараметр("Организации",Организация);
		КонецЕсли;
	
		ТабСостоянийОпераций = Запрос.Выполнить().Выгрузить();
		
		Для Каждого СтрокаТаблицы Из ТабСостоянийОпераций Цикл
			ДокументОперации = СтрокаТаблицы.ДокументОперации;
			Если ЗначениеЗаполнено(ДокументОперации) Тогда
				ДокОбъект = ДокументОперации.ПолучитьОбъект();
				Если ДокОбъект.ПометкаУдаления
					И СтрокаТаблицы.Состояние = "Выполнено" Тогда
					СтрокаТаблицы.Состояние = "ВыполненоСОшибками";
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	// проставим состояния операций
	
	Для Каждого СтрокаТаблицы Из ТаблицаОпераций Цикл
		
		ОпределитьНеобходимостьОперацииПерехода(СтрокаТаблицы);
		
		// проставим состояние для операции
		Если СтрокаТаблицы.ТипОперации = ТипыОпераций.Отчет Тогда // "особая" операция
			СтрокаТаблицы.Состояние = "Выполнено";
		Иначе
			Если ПереходНаЕНСЗапущен Тогда // анализируем состояния
				СтрокиСостояний = ТабСостоянийОпераций.НайтиСтроки(Новый Структура("ВидОперации", СтрокаТаблицы.ВидОперации));
				Если СтрокиСостояний.Количество() > 0 Тогда // есть данные о состоянии
					СтрокаСостояния = СтрокиСостояний[0];
					СтрокаТаблицы.Состояние = СтрокаСостояния.Состояние;
					СтрокаТаблицы.Актуальность = СтрокаСостояния.Актуальность;
				Иначе // данных о состоянии операции нет, считается не выполненной
					СтрокаТаблицы.Состояние = "НеВыполнено";
					СтрокаТаблицы.Актуальность = Истина;
				КонецЕсли;
			Иначе // анализировать состояния незачем, все операции по умолчанию не выполнены
				СтрокаТаблицы.Состояние = "НеВыполнено";
				СтрокаТаблицы.Актуальность = Истина;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеФормой()
	
	Элементы.ПолеПоясненияПоПереходу.Видимость = НЕ ПереходНаЕНСЗапущен;
	
	ОперацияГоловнойОрганизации = ЗначениеЗаполнено(ПолеОрганизация)
		И ((ЕстьОбособленныеПодразделения
			И ВключатьОбособленныеПодразделения)
				Или Не ЕстьОбособленныеПодразделения);
		
	Элементы.ДатаПерехода.Доступность = ОперацияГоловнойОрганизации;
	
	Элементы.ГруппаПереносРасчетовПоНалогам.Доступность  = Не ОперацияГоловнойОрганизации Или Не ЕстьОбособленныеПодразделения;
	Элементы.ГруппаПереносРасчетовПоСанкциям.Доступность = Не ОперацияГоловнойОрганизации Или Не ЕстьОбособленныеПодразделения;
	Элементы.ГруппаПереносПереплат.Доступность           = Не ОперацияГоловнойОрганизации Или Не ЕстьОбособленныеПодразделения;
	
	Элементы.ГруппаОтчетПоНалогамВзносам.Видимость = Не ВводОстатков;
	Элементы.ГруппаЗачетАвансов.Видимость          = Не ВводОстатков;
	
	Элементы.ГруппаОперацияВыполненаНоНеАктуальна.Видимость = Не ВводОстатков;
	
	ОбновитьСостояниеОперацийНаФорме();
	
	УправлениеФормойБлокСверка();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСостояниеОперацийНаФорме()
	
	Элементы.ГруппаОперации.Доступность = ЗначениеЗаполнено(Объект.Организация);
	ПорядокОперацииКВыполнению          = 0;
	
	Если НЕ ПереходНаЕНСЗапущен Тогда
		Элементы.ГруппаОперации.Доступность = Ложь;
	КонецЕсли;
	
	Для каждого СтрокаОперации Из ТаблицаОпераций Цикл
		
		ЭлементДействиеОперации = Элементы.Найти(СтрокаОперации.ИмяОперации);
		ЭлементСтатусОперации   = Элементы.Найти("Статус" + СтрокаОперации.ИмяОперации);
		ЭлементГруппаДействия   = ЭлементДействиеОперации.Родитель;
		
		Если СтрокаОперации.ТипОперации = ТипыОпераций.Отчет Тогда
			ЭлементСтатусОперации.Картинка = Картинки.КартинкаОтчет;
			ЭлементДействиеОперации.ЦветТекста = ЦветаТекстаОпераций.ЦветТекстаПоУмолчанию;
			Продолжить;
		КонецЕсли;
		
		ЭлементГруппаДействия.Видимость = СтрокаОперации.Использование;
		
		Если СтрокаОперации.Состояние = "НеВыполнено" Тогда
			
			ЭлементДействиеОперации.ЦветТекста = ЦветаТекстаОпераций.ОперацияНеВыполнена;
			
			Если ПорядокОперацииКВыполнению = 0 И СтрокаОперации.Использование Тогда
				ЭлементСтатусОперации.Картинка = Картинки.КартинкаТекущаяОперация;
				ПорядокОперацииКВыполнению = СтрокаОперации.Порядок;
			Иначе
				ЭлементСтатусОперации.Картинка = Картинки.КартинкаОперацияНеВыполнена;
			КонецЕсли;
			
		ИначеЕсли СтрокаОперации.Состояние = "Выполнено"
		 Или СтрокаОперации.Состояние = "Выполняется" Тогда
			
			Если СтрокаОперации.Актуальность Тогда
				ЭлементСтатусОперации.Картинка = Картинки.КартинкаОперацияВыполнена;
				ЭлементДействиеОперации.ЦветТекста = ЦветаТекстаОпераций.ОперацияВыполнена;
			Иначе
				ЭлементСтатусОперации.Картинка = Картинки.КартинкаОперацияУстарела;
				ЭлементДействиеОперации.ЦветТекста = ЦветаТекстаОпераций.ОперацияУстарела;
			КонецЕсли;
			
		ИначеЕсли СтрокаОперации.Состояние = "ВыполненоСОшибками" Тогда
			
			ЭлементСтатусОперации.Картинка = Картинки.КартинкаОперацияСОшибками;
			ЭлементДействиеОперации.ЦветТекста = ЦветаТекстаОпераций.ОперацияВыполненаСОшибками;
			
		ИначеЕсли СтрокаОперации.Состояние = "Пропущено" Тогда
			
			ЭлементСтатусОперации.Картинка = Картинки.КартинкаОперацияПропущена;
			ЭлементДействиеОперации.ЦветТекста = ЦветаТекстаОпераций.ОперацияПропущена;
			
		Иначе
			
			ЭлементГруппаДействия.Видимость = Ложь;
			
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ОбновитьПараметрыПерехода()
	
	ПереходНаЕНСЗапущен = Ложь;
	
	ТекстПоясненияПоПереходу  = НСтр("ru = 'Не указана организация';
									|en = 'Company is not specified'");
	
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		
		ПредыдущийПериодНачало = НачалоГода(НачалоГода(Объект.ДатаПерехода)-1);
		ПредыдущийПериодКонец  = КонецГода(ПредыдущийПериодНачало);
		
		ПлательщикЕНППредшествующийПериод = ПлатежиВБюджет.ИспользуетсяЕдиныйНалоговыйПлатеж(Объект.Организация, ПредыдущийПериодНачало);
		УчастникЭкспериментаЕНП  = Не ПлательщикЕНППредшествующийПериод И ПлатежиВБюджет.ИспользуетсяЕдиныйНалоговыйПлатеж(Объект.Организация, ПредыдущийПериодКонец);
		
		ПереходНаЕНСЗапущен = Не УчастникЭкспериментаЕНП;
		Если ПереходНаЕНСЗапущен Тогда
			ТекстПоясненияПоПереходу = "";
		Иначе
			ТекстПоясненияПоПереходу = НСтр("ru = 'Организация являлась участником эксперимента по ЕНП. Выполнять переход не требуется.';
											|en = 'Company took part in the unified tax payment experiment. Migration is not required.'");
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаполнитьЗависимостьОперацийПерехода();
	
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	ЗаполнитьФормуНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗависимостьОперацийПерехода()

	ЗависимостьОпераций.Очистить();
	
	СтрокаЗависимости = ЗависимостьОпераций.Добавить();
	СтрокаЗависимости.ПроверяемаяОперация = "ЗачетАвансов";
	СтрокаЗависимости.ТребуемаяОперация   = "ПереносРасчетовПоНалогам";
	
	СтрокаЗависимости = ЗависимостьОпераций.Добавить();
	СтрокаЗависимости.ПроверяемаяОперация = "ЗачетАвансов";
	СтрокаЗависимости.ТребуемаяОперация   = "ПереносРасчетовПоСанкциям";
	
	СтрокаЗависимости = ЗависимостьОпераций.Добавить();
	СтрокаЗависимости.ПроверяемаяОперация = "ЗачетАвансов";
	СтрокаЗависимости.ТребуемаяОперация   = "ПереносПереплат";
	
КонецПроцедуры

&НаСервере
Процедура ОпределитьНеобходимостьОперацииПерехода(СтрокаОперации)

	// по умолчанию - всегда выполняем операции
	СтрокаОперации.Использование = Истина;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьДокументыПоОперации(СтруктураПараметров)
	
	ДокументыОперации = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДокументыОперацийПереходаНаЕНС.Ссылка КАК ДокументОперации
	|ИЗ
	|	Документ.КорректировкаРасчетовПоНалогам КАК ДокументыОперацийПереходаНаЕНС
	|ГДЕ
	|	ДокументыОперацийПереходаНаЕНС.Организация = &Организация
	|	И ДокументыОперацийПереходаНаЕНС.ВводОстатковРасчетовПоЕНС
	|	И ДокументыОперацийПереходаНаЕНС.ВидОперации = &ВидОперации
	|";
	
	Запрос.УстановитьПараметр("Организация",  СтруктураПараметров.Организация);
	Запрос.УстановитьПараметр("ВидОперации",  СтруктураПараметров.ВидОперации);
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		ДокументыОперации = Результат.Выгрузить().ВыгрузитьКолонку("ДокументОперации");
	КонецЕсли;
	
	Возврат ДокументыОперации;
	
КонецФункции

&НаСервере
Функция ЗарегистрированыДокументыНаДатуПерехода()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДокументыОперацийПереходаНаЕНС.Ссылка КАК ДокументОперации
	|ИЗ
	|	Документ.КорректировкаРасчетовПоНалогам КАК ДокументыОперацийПереходаНаЕНС
	|ГДЕ
	|	ДокументыОперацийПереходаНаЕНС.Организация В(&Организации)
	|	И ДокументыОперацийПереходаНаЕНС.ВводОстатковРасчетовПоЕНС";

	Запрос.УстановитьПараметр("Организации",  Организации);
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

&НаСервере
Процедура ЗаполнитьФормуНаСервере(ОпределятьДату = Истина)
	
	МожноВыполнятьСверку = Ложь;
	
	ЗаполнитьСведенияОбОрганизации();
	Если ОпределятьДату Тогда
		ОпределитьДатуПерехода();
	КонецЕсли;
	ОпределитьПараметрыСверки();
	ОбновитьПараметрыПерехода();
	ЗаполнитьДанныеОпераций();
	
	УправлениеФормой();

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСведенияОбОрганизации()
	
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		ГоловнаяОрганизация = БухгалтерскийУчетПереопределяемый.ГоловнаяОрганизация(Объект.Организация);
		Организации         = Новый ФиксированныйМассив(БухгалтерскийУчетПереопределяемый.ВсяОрганизация(ГоловнаяОрганизация));
		ЕстьОбособленныеПодразделения = Организации.Количество() > 1;
		ЭтоГоловнаяОрганизация = (ЕстьОбособленныеПодразделения
			И ГоловнаяОрганизация = СоответствиеОрганизаций[ПолеОрганизация].Организация)
			Или Не ЕстьОбособленныеПодразделения;
		ВключатьОбособленныеПодразделения = СоответствиеОрганизаций[ПолеОрганизация].ВключатьОбособленныеПодразделения
			Или Не ЕстьОбособленныеПодразделения;
		ОпределитьПараметрыСверки();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОпределитьПараметрыСверки()
	
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		МожноВыполнятьСверку = ЭтоГоловнаяОрганизация И ВключатьОбособленныеПодразделения;
		Если МожноВыполнятьСверку Тогда
			ПараметрыСверки = Новый Структура("Организация, Период",
				ГоловнаяОрганизация, Объект.ДатаПерехода);
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, СведенияОСверке(ПараметрыСверки));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОпределитьДатуПерехода()
	
	Объект.ДатаПерехода = РеглУчетСервер.ДатаНачалаОбязательногоПримененияЕНП();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организации", Организации);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДокументыОперацийПереходаНаЕНС.Дата КАК ДатаПерехода
	|ИЗ
	|	Документ.КорректировкаРасчетовПоНалогам КАК ДокументыОперацийПереходаНаЕНС
	|ГДЕ
	|	ДокументыОперацийПереходаНаЕНС.Организация В (&Организации)
	|	И ВводОстатковРасчетовПоЕНС";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Объект.ДатаПерехода = Выборка.ДатаПерехода;
	КонецЕсли;
	ДатаПереходаПредыдущая = Объект.ДатаПерехода;
	
	ВводОстатков = ВводОстатков(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ВводОстатков(Форма)
	
	ВводОстатков = ЗначениеЗаполнено(Форма.Объект.ДатаПерехода)
		И Форма.Объект.ДатаПерехода <> ПолучитьДатуПереходаНаЕНП();
	
	Возврат ВводОстатков;
	
КонецФункции

&НаКлиенте
Процедура ВопросПереносДатыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ОбработатьИзменениеДаты();
	Иначе
		Объект.ДатаПерехода = ДатаПереходаПредыдущая;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаЗакрытияДокумента(Результат, ДополнительныеПараметры) Экспорт
	
	ОбработкаИзмененияДокументовНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаИзмененияДокументовНаСервере()
	
	ПервоначальноеЗаполнениеТаблицыОпераций();
	ЗаполнитьДанныеОпераций();
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеДаты()
	
	НачатьТранзакцию();
	Попытка
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДокументыОперацийПереходаНаЕНС.Ссылка КАК ДокументОперации,
		|	ДокументыОперацийПереходаНаЕНС.ВидОперации КАК ВидОперации
		|ИЗ
		|	Документ.КорректировкаРасчетовПоНалогам КАК ДокументыОперацийПереходаНаЕНС
		|ГДЕ
		|	ДокументыОперацийПереходаНаЕНС.Организация В (&Организации)
		|	И ДокументыОперацийПереходаНаЕНС.ВводОстатковРасчетовПоЕНС";
		
		Запрос.УстановитьПараметр("Организации",  Организации);
		
		Результат = Запрос.Выполнить().Выгрузить();
		Для Каждого СтрокаТаблицы Из Результат Цикл
			ДокументОперации = СтрокаТаблицы.ДокументОперации;
			ДокументОбъект = ДокументОперации.ПолучитьОбъект();
			ДокументОбъект.Дата = Объект.ДатаПерехода;
			Если ДокументОбъект.Проведен Тогда
				ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
			Иначе
				ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
			КонецЕсли;
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();	
	Исключение
		Объект.ДатаПерехода = ДатаПереходаПредыдущая;
		
		ШаблонСообщения = НСтр("ru = 'Не удалось перенести документ %1
								|%2';
								|en = 'Cannot post the document %1
								|%2'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, ДокументОперации, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ЗаписьЖурналаРегистрации(
			Обработки.ПомощникПереходаНаЕНС.ИмяСобытияЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Документы.КорректировкаРасчетовПоНалогам,, 
			ТекстСообщения);
			
		ОтменитьТранзакцию();
		ВызватьИсключение ОписаниеОшибки();	
	КонецПопытки;
		
	ДатаПереходаПредыдущая = Объект.ДатаПерехода;
	ВводОстатков = ВводОстатков(ЭтотОбъект);
	ПервоначальноеЗаполнениеТаблицыОпераций();
	ЗаполнитьФормуНаСервере(Ложь);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции обработки гиперссылок операций формы

&НаКлиенте
Функция СписокКомандДляОперации(СтрокаОперации)

	СписокКоманд = Новый СписокЗначений;
	
	// Общие команды
	Если СтрокаОперации.ТипОперации = ТипыОпераций.ОбработкаДанных Тогда
		
		ОперацияГоловнойОрганизации = ЗначениеЗаполнено(ПолеОрганизация)
			И ((ЕстьОбособленныеПодразделения
				И ВключатьОбособленныеПодразделения)
					Или Не ЕстьОбособленныеПодразделения);
					
		Если СтрокаОперации.ИмяОперации = "ЗачетАвансов" И ЕстьОбособленныеПодразделения И Не ОперацияГоловнойОрганизации Тогда
			СписокКоманд.Добавить("ПерейтиПоОрганизацииВЦелом", НСтр("ru = 'Перейти к операциям перехода в целом по организации';
																	|en = 'Go to migration operations for the whole company'"));
		ИначеЕсли СтрокаОперации.Состояние = "НеВыполнено" Тогда
			СписокКоманд.Добавить("Выполнить", НСтр("ru = 'Выполнить операцию';
													|en = 'Run operation'"));
		ИначеЕсли СтрокаОперации.Состояние = "Выполнено"
		 Или СтрокаОперации.Состояние = "Выполняется" Тогда
			Если СтрокаОперации.ИмяОперации = "ЗачетАвансов" Тогда
				СписокКоманд.Добавить("ОткрытьСписок", НСтр("ru = 'Открыть список документов';
															|en = 'Open document list'"));
			Иначе
				СписокКоманд.Добавить("ОткрытьДокумент", НСтр("ru = 'Открыть документ';
																|en = 'Open document'"));
			КонецЕсли;
			СписокКоманд.Добавить("ВыполнитьПовторно", НСтр("ru = 'Выполнить операцию повторно';
															|en = 'Repeat operation'"));
			СписокКоманд.Добавить("ОтменитьВыполнение", НСтр("ru = 'Отменить выполнение';
															|en = 'Cancel execution'"));
		ИначеЕсли СтрокаОперации.Состояние = "ВыполненоСОшибками" Тогда
			Если СтрокаОперации.ИмяОперации = "ЗачетАвансов" Тогда
				СписокКоманд.Добавить("ОткрытьСписок", НСтр("ru = 'Открыть список документов';
															|en = 'Open document list'"));
			Иначе
				СписокКоманд.Добавить("ОткрытьДокумент", НСтр("ru = 'Открыть документ';
																|en = 'Open document'"));
			КонецЕсли;
			СписокКоманд.Добавить("ВыполнитьПовторно", НСтр("ru = 'Выполнить операцию повторно';
															|en = 'Repeat operation'"));
			СписокКоманд.Добавить("ОтменитьВыполнение", НСтр("ru = 'Отменить выполнение';
															|en = 'Cancel execution'"));
		Иначе
			СписокКоманд.Добавить("Выполнить", НСтр("ru = 'Выполнить операцию';
													|en = 'Run operation'"));
			СписокКоманд.Добавить("ОтменитьВыполнение", НСтр("ru = 'Отменить выполнение';
															|en = 'Cancel execution'"));
		КонецЕсли;
		
	ИначеЕсли СтрокаОперации.ТипОперации = ТипыОпераций.Отчет Тогда
		
		СписокКоманд.Добавить("ОткрытьОтчетНалоги", НСтр("ru = 'Отчет по налогам';
														|en = 'Tax report'"));
		СписокКоманд.Добавить("ОткрытьОтчетВзносы", НСтр("ru = 'Отчет по взносам';
														|en = 'Contribution report'"));
		
	КонецЕсли;
	
	Возврат СписокКоманд;
	
КонецФункции

&НаКлиенте
Функция МожноВыполнитьОперацию(СтрокаОперации, Знач ПредставлениеОперации)

	СтрокиЗависимостей = ЗависимостьОпераций.НайтиСтроки(Новый Структура("ПроверяемаяОперация", СтрокаОперации.ИмяОперации));
	Если СтрокиЗависимостей.Количество() = 0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	МожноВыполнитьОперацию = Истина;
	Для Каждого СтрокаЗависимости Из СтрокиЗависимостей Цикл
		СтрокиПроверяемыхОпераций = ТаблицаОпераций.НайтиСтроки(Новый Структура("ИмяОперации", СтрокаЗависимости.ТребуемаяОперация));
		Если СтрокиПроверяемыхОпераций.Количество() <> 0 Тогда
			МожноВыполнитьОперацию = Ложь;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если МожноВыполнитьОперацию Тогда
		Возврат Истина;
	КонецЕсли;
	
	МожноВыполнитьОперацию = Истина;
	Для Каждого СтрокаЗависимости Из СтрокиЗависимостей Цикл
		Если Не МожноВыполнитьОперацию Тогда
			Прервать;
		КонецЕсли;
		СтрокиПроверяемыхОпераций = ТаблицаОпераций.НайтиСтроки(Новый Структура("ИмяОперации", СтрокаЗависимости.ТребуемаяОперация));
		Для Каждого СтрокаПроверяемойОперации Из СтрокиПроверяемыхОпераций Цикл
			Если Не СтрокаПроверяемойОперации.Актуальность
				Или (СтрокаПроверяемойОперации.Состояние <> "Выполнено"
				И СтрокаПроверяемойОперации.Состояние <> "Выполняется") Тогда
				МожноВыполнитьОперацию = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	Если МожноВыполнитьОперацию Тогда
		Возврат Истина;
	КонецЕсли;
		
	ТекстШаблон = Нстр("ru = 'Перед выполнением операции %1
						|требуется выполнить операцию %2.';
						|en = 'Before you execute the %1
						| operation, run the %2 operation.'");
						
	ЭлементТребуемойОперации       = Элементы.Найти(СтрокаПроверяемойОперации.ИмяОперации);
	ПредставлениеОперации          = """" + ПредставлениеОперации + """";
	ПредставлениеТребуемойОперации = """" + ЭлементТребуемойОперации.Заголовок + """";
	ТекстПредупреждения            = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ТекстШаблон, ПредставлениеОперации, ПредставлениеТребуемойОперации);
	
	ПоказатьПредупреждение( , ТекстПредупреждения, 180);
	
	ТекущийЭлемент = ЭлементТребуемойОперации;
			
	Возврат Ложь;

КонецФункции

&НаКлиенте
Процедура ОбработкаНажатияКнопкиМеню(Команда, Элемент, СтрокаОперации)
	
	Если Команда = "Выполнить" Тогда
		ВыполнитьОперациюОбработкаДанныхНаКлиенте(СтрокаОперации, Элемент.Заголовок);
	ИначеЕсли Команда = "ВыполнитьПовторно" Тогда
		ВыполнитьОперациюОбработкаДанныхНаКлиенте(СтрокаОперации, Элемент.Заголовок);
	ИначеЕсли Команда = "ОткрытьДокумент" Тогда
		ОткрытьДокументПоОперации(СтрокаОперации);
	ИначеЕсли Команда = "ОткрытьСписок" Тогда
		ОткрытьСпискиДокументовПоОперации(СтрокаОперации);
	ИначеЕсли Команда = "ОтметитьВыполненной" Тогда
		ОтметитьОперациюВыполненнойИПропустить(СтрокаОперации.ПолучитьИдентификатор());
	ИначеЕсли Команда = "ОтменитьВыполнение" Тогда
		ОтменитьОперацию(СтрокаОперации.ПолучитьИдентификатор());
	ИначеЕсли Команда = "ОткрытьОтчетНалоги" Тогда
		ОткрытьСформироватьОтчетНалоги(СтрокаОперации);
	ИначеЕсли Команда = "ОткрытьОтчетВзносы" Тогда
		ОткрытьСформироватьОтчетВзносы(СтрокаОперации);
	ИначеЕсли Команда = "ПерейтиПоОрганизацииВЦелом" Тогда
		ПерейтиПоОрганизацииВЦелом();
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции выполнения операций формы

&НаСервере
Процедура ЗафиксироватьВыполнениеОперацииПерехода(СтрокаОперации, ДокументыОперации = Неопределено,  ФиксироватьУстаревшие = Ложь, Отказ = Ложь)

	ПараметрыОперации = Новый Структура;
	
	ПараметрыОперации.Вставить("ВидОперации",         СтрокаОперации.ВидОперации);
	ПараметрыОперации.Вставить("Организация",         Объект.Организация);
	ПараметрыОперации.Вставить("ДатаПерехода",        Объект.ДатаПерехода);
	ПараметрыОперации.Вставить("Состояние",           СтрокаОперации.Состояние);
	
	Если ТипЗнч(ДокументыОперации) = Тип("Массив") Тогда
		ПараметрыОперации.Вставить("ДокументыОперации", ДокументыОперации);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтметитьОперациюВыполненнойИПропустить(ИдентификаторСтроки)

	СтрокаОперации = ТаблицаОпераций.НайтиПоИдентификатору(ИдентификаторСтроки);
	
	СтрокаОперации.Состояние = "Выполнено";
	ЗафиксироватьВыполнениеОперацииПерехода(СтрокаОперации, , Ложь);
	
	ЗаполнитьДанныеОпераций();
	ОбновитьСостояниеОперацийНаФорме();

КонецПроцедуры

&НаСервере
Процедура ОтменитьВыполнениеОперацииПерехода(СтрокаОперации, ФиксироватьУстаревшие = Ложь, Отказ = Ложь)

	ПараметрыОперации = Новый Структура;
	
	ПараметрыОперации.Вставить("ВидОперации",         СтрокаОперации.ВидОперации);
	ПараметрыОперации.Вставить("Организация",         Объект.Организация);
	ПараметрыОперации.Вставить("ДатаПерехода",        Объект.ДатаПерехода);
	ПараметрыОперации.Вставить("Состояние",           СтрокаОперации.Состояние);
	
	ПараметрыОперации.Вставить("ОбрабатыватьДокументыОперации", Истина);
	ПараметрыОперации.Вставить("ОчиститьСсылкиНаДокументы", СтрокаОперации.ОчищатьСсылкиНаДокументыПриОтмене);
	ПараметрыОперации.Вставить("СпособОбработкиДокументовОперации",
		СпособыОбработкиДокументов.Получить(СтрокаОперации.СпособОбработкиДокументовПриОтмене));
	
	Обработки.ПомощникПереходаНаЕНС.ОтменитьВыполнениеОперацииПереходаНаЕНС(ПараметрыОперации, ФиксироватьУстаревшие, Отказ);
	
	Если СтрокаОперации.ИмяОперации = "ЗачетАвансов" Тогда
		ПараметрыОперации.ВидОперации = Перечисления.ВидыОперацийПоЕдиномуНалоговомуСчету.ПогашениеПенейШтрафов;
		Обработки.ПомощникПереходаНаЕНС.ОтменитьВыполнениеОперацииПереходаНаЕНС(ПараметрыОперации, ФиксироватьУстаревшие, Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтменитьОперацию(ИдентификаторСтроки)

	СтрокаОперации = ТаблицаОпераций.НайтиПоИдентификатору(ИдентификаторСтроки);
	
	СтрокаОперации.Состояние = "НеВыполнено";
	ОтменитьВыполнениеОперацииПерехода(СтрокаОперации, Истина);
	
	ЗаполнитьДанныеОпераций();
	ОбновитьСостояниеОперацийНаФорме();

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСпискиДокументовПоОперации(СтрокаОперации)

	Если СтрокаОперации.ИмяОперации = "ПереносРасчетовПоНалогам" Тогда
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Дата",                Объект.ДатаПерехода);
		ПараметрыОтбора.Вставить("Организация",         Объект.Организация);
		ПараметрыОтбора.Вставить("ВидОперации",         ПредопределенноеЗначение("Перечисление.ВидыОперацийПоЕдиномуНалоговомуСчету.НачислениеНалогов"));
		ПараметрыОтбора.Вставить("ВводОстатковРасчетовПоЕНС", Истина);
		ПараметрыОтбора.Вставить("ПометкаУдаления",     Ложь);
		
		ПараметрыФормы = Новый Структура("Отбор, КлючНазначенияИспользования", ПараметрыОтбора, "ДокументыПереходаНаЕНС");
		ОткрытьФорму("Документ.КорректировкаРасчетовПоНалогам.ФормаСписка", ПараметрыФормы, ЭтаФорма, УникальныйИдентификатор);
		
	ИначеЕсли СтрокаОперации.ИмяОперации = "ПереносРасчетовПоСанкциям" Тогда
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Дата",                Объект.ДатаПерехода);
		ПараметрыОтбора.Вставить("Организация",         Объект.Организация);
		ПараметрыОтбора.Вставить("ВидОперации",         ПредопределенноеЗначение("Перечисление.ВидыОперацийПоЕдиномуНалоговомуСчету.НачислениеПенейШтрафов"));
		ПараметрыОтбора.Вставить("ВводОстатковРасчетовПоЕНС", Истина);
		ПараметрыОтбора.Вставить("ПометкаУдаления",     Ложь);
		
		ПараметрыФормы = Новый Структура("Отбор, КлючНазначенияИспользования", ПараметрыОтбора, "ДокументыПереходаНаЕНС");
		ОткрытьФорму("Документ.КорректировкаРасчетовПоНалогам.ФормаСписка", ПараметрыФормы, ЭтаФорма, УникальныйИдентификатор);
		
	ИначеЕсли СтрокаОперации.ИмяОперации = "ПереносПереплат" Тогда
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Дата",                Объект.ДатаПерехода);
		ПараметрыОтбора.Вставить("Организация",         Объект.Организация);
		ПараметрыОтбора.Вставить("ВидОперации",         ПредопределенноеЗначение("Перечисление.ВидыОперацийПоЕдиномуНалоговомуСчету.КорректировкаСчета"));
		ПараметрыОтбора.Вставить("ВводОстатковРасчетовПоЕНС", Истина);
		ПараметрыОтбора.Вставить("ПометкаУдаления",     Ложь);
		
		ПараметрыФормы = Новый Структура("Отбор, КлючНазначенияИспользования", ПараметрыОтбора, "ДокументыПереходаНаЕНС");
		ОткрытьФорму("Документ.КорректировкаРасчетовПоНалогам.ФормаСписка", ПараметрыФормы, ЭтаФорма, УникальныйИдентификатор);
		
	ИначеЕсли СтрокаОперации.ИмяОперации = "ЗачетАвансов" Тогда
		
		ОтборОперации = Новый СписокЗначений;
		ОтборОперации.Добавить(ПредопределенноеЗначение("Перечисление.ВидыОперацийПоЕдиномуНалоговомуСчету.УплатаНалогов"));
		ОтборОперации.Добавить(ПредопределенноеЗначение("Перечисление.ВидыОперацийПоЕдиномуНалоговомуСчету.ПогашениеПенейШтрафов"));
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Дата",            Объект.ДатаПерехода);
		ПараметрыОтбора.Вставить("Организация",     Объект.Организация);
		ПараметрыОтбора.Вставить("ВидОперации",     ОтборОперации);
		ПараметрыОтбора.Вставить("ВводОстатковРасчетовПоЕНС", Истина);
		ПараметрыОтбора.Вставить("ПометкаУдаления", Ложь);
		
		ПараметрыФормы = Новый Структура("Отбор, КлючНазначенияИспользования", ПараметрыОтбора, "ДокументыПереходаНаЕНС");
		ОткрытьФорму("Документ.КорректировкаРасчетовПоНалогам.ФормаСписка", ПараметрыФормы, ЭтаФорма, УникальныйИдентификатор);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьДокументПоОперации(СтрокаОперации)

	ПараметрыОперации = Новый Структура;
	ПараметрыОперации.Вставить("Организация",  Объект.Организация);
	ПараметрыОперации.Вставить("ВводОстатковРасчетовПоЕНС", Истина);
	ПараметрыОперации.Вставить("ВидОперации",  СтрокаОперации.ВидОперации);
	
	МассивДокументовПоОперации = ПолучитьДокументыПоОперации(ПараметрыОперации);
	
	Если МассивДокументовПоОперации.Количество() > 0 Тогда
		
		ДокументОперации = МассивДокументовПоОперации[0];
		ВидДокумента = "";
		ВидДокумента = "КорректировкаРасчетовПоНалогам";
		
		Если ЗначениеЗаполнено(ВидДокумента) Тогда
			Оповещение = Новый ОписаниеОповещения("ОбработкаЗакрытияДокумента", ЭтотОбъект);
			ОтборДокумента = Новый Структура("Ключ", ДокументОперации);
			ОткрытьФорму("Документ." + ВидДокумента + ".ФормаОбъекта",
				ОтборДокумента,
				ЭтаФорма,
				УникальныйИдентификатор,
				,
				,
				Оповещение,
				РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСформироватьОтчетНалоги(СтрокаОперации)
	
	Если СтрокаОперации.ИмяОперации = "ОтчетПоНалогамВзносам" Тогда
		ОткрытьСформироватьОтчет(Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСформироватьОтчетВзносы(СтрокаОперации)
	
	Если СтрокаОперации.ИмяОперации = "ОтчетПоНалогамВзносам" Тогда
		ОткрытьСформироватьОтчет(Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСформироватьОтчет(Налоги)
	
	Если Налоги Тогда
		Счет = ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.РасчетыПоНалогам");
	Иначе
		Счет = ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.РасчетыПоСоциальномуСтрахованию");
	КонецЕсли;
	
	// Инициализируем параметры расшифровки
	НастройкиРасшифровки = Новый Структура();
	
	// Инициализируем пользовательские настройки
	ПользовательскиеНастройки = Новый ПользовательскиеНастройкиКомпоновкиДанных;
	ДополнительныеСвойства = ПользовательскиеНастройки.ДополнительныеСвойства;
	ДополнительныеСвойства.Вставить("ПоказательБУ",         Истина);
	ДополнительныеСвойства.Вставить("РежимРасшифровки",     Истина);
	ДополнительныеСвойства.Вставить("Организация",          Объект.Организация);
	ДополнительныеСвойства.Вставить("НачалоПериода",        Объект.ДатаПерехода);
	ДополнительныеСвойства.Вставить("КонецПериода",         Объект.ДатаПерехода);
	ДополнительныеСвойства.Вставить("ПоСубсчетам",          Истина);
	ДополнительныеСвойства.Вставить("ПоСубсчетамКорСчетов", Истина);
	ДополнительныеСвойства.Вставить("ВключатьОбособленныеПодразделения", 
		ЕстьОбособленныеПодразделения И ВключатьОбособленныеПодразделения);
	
	// Инициализируем отборы
	ПользовательскиеОтборыДляСчета = ПользовательскиеНастройки.Элементы.Добавить(Тип("ОтборКомпоновкиДанных"));
	ПользовательскиеОтборыДляСчета.ИдентификаторПользовательскойНастройки = "Отбор";
	
	ДополнительныеСвойства.Вставить("Счет", Счет);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВидРасшифровки", 2);
	ПараметрыФормы.Вставить("ПользовательскиеНастройки", ПользовательскиеНастройки);
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	
	ОткрытьФорму("Отчет.АнализСчета.Форма.ФормаОтчета", ПараметрыФормы, ЭтаФорма, УникальныйИдентификатор);

КонецПроцедуры

&НаКлиенте
Процедура ПерейтиПоОрганизацииВЦелом()
	
	Если Не ЗначениеЗаполнено(ГоловнаяОрганизация) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ЗначениеСоответствия Из СоответствиеОрганизаций Цикл
		Если ЗначениеСоответствия.Значение.Организация = ГоловнаяОрганизация Тогда
			Если ЗначениеСоответствия.Значение.ВключатьОбособленныеПодразделения Тогда
				ПолеОрганизация = ЗначениеСоответствия.Ключ;
				Прервать;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ПолеОрганизацияПриИзмененииНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеОрганизацияПриИзмененииНаКлиенте()
	
	ОбщегоНазначенияБПКлиент.ПолеОрганизацияПриИзменении(
		Элементы.ПолеОрганизация,
		ПолеОрганизация,
		Объект.Организация,
		Ложь);
		
	ОрганизацияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОперациюОбработкаДанныхНаКлиенте(СтрокаОперации, ПредставлениеОперации)
	
	ОчиститьСообщения();
	
	Если Не МожноВыполнитьОперацию(СтрокаОперации, ПредставлениеОперации) Тогда
		Возврат;
	КонецЕсли;
	
	Результат = ВыполнитьОперациюОбработкаДанныхНаСервере(СтрокаОперации.ПолучитьИдентификатор());

	Если НЕ Результат.ЗаданиеВыполнено Тогда
		Если ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
			
			ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
			ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", 1, Истина);
			ФормаДлительнойОперации = ДлительныеОперацииКлиент.ОткрытьФормуДлительнойОперации(ЭтаФорма, ИдентификаторЗадания);
			
			ФормаДлительнойОперации.Заголовок = НСтр("ru = 'Переход на ЕНС';
													|en = 'Switch to Unified tax account'");
			ШаблонИнформации = НСтр("ru = 'Выполняется операция ""%1"".
									|Пожалуйста, подождите...';
									|en = 'Running the ""%1"" operation.
									|Please wait...'");
			ТекстИнформации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонИнформации, ПредставлениеОперации);
			ФормаДлительнойОперации.Элементы.ДекорацияПоясняющийТекстДлительнойОперации.Заголовок = ТекстИнформации;
			
		КонецЕсли;
	Иначе
		ЗагрузитьПодготовленныеДанныеНаКлиенте(Результат.СтруктураДанныхКлиента);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ВыполнитьОперациюОбработкаДанныхНаСервере(ИдентификаторСтроки)

	СтрокаОперации = ТаблицаОпераций.НайтиПоИдентификатору(ИдентификаторСтроки);
	ИмяДействия = СтрокаОперации.ДействиеОперации;
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Организация",  Объект.Организация);
	СтруктураПараметров.Вставить("ДатаПерехода", Объект.ДатаПерехода);
	СтруктураПараметров.Вставить("ВидОперации",  СтрокаОперации.ВидОперации);
	
	Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		
		АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
		
		Выполнить(ИмяДействия+"(СтруктураПараметров, АдресХранилища)");
		Результат = Новый Структура("ЗаданиеВыполнено", Истина);
		
	Иначе
		
		ШаблонФоновогоЗадания = НСтр("ru = 'Выполнение операции перехода на ЕНС: %1';
									|en = 'Switching to unified tax account: %1'");
		НаименованиеФоновогоЗадания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонФоновогоЗадания, ИмяДействия);
		Результат = ДлительныеОперации.ЗапуститьВыполнениеВФоне(
			УникальныйИдентификатор,
			ИмяДействия,
			СтруктураПараметров,
			НаименованиеФоновогоЗадания);
			
		АдресХранилища       = Результат.АдресХранилища;
		ИдентификаторЗадания = Результат.ИдентификаторЗадания;
		
	КонецЕсли;
	
	Если Результат.ЗаданиеВыполнено Тогда
		Результат.Вставить("СтруктураДанныхКлиента", ЗагрузитьПодготовленныеДанные());
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

&НаКлиенте
Процедура ЗагрузитьПодготовленныеДанныеНаКлиенте(СтруктураДанных)
	
	Если ТипЗнч(СтруктураДанных) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураДанных.Свойство("МассивСообщений") Тогда
		Для каждого Сообщение Из СтруктураДанных.МассивСообщений Цикл
			Сообщение.ИдентификаторНазначения = УникальныйИдентификатор;
			Сообщение.Сообщить();
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗагрузитьПодготовленныеДанные()
	
	СтруктураДанныхНаКлиенте = Новый Структура();
	
	СтруктураДанных = ПолучитьИзВременногоХранилища(АдресХранилища);
	Если ТипЗнч(СтруктураДанных) <> Тип("Структура") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если СтруктураДанных.Свойство("УспешноВыполнено") Тогда
		
		СтрокаОперации = ТаблицаОпераций.НайтиСтроки(Новый Структура("ВидОперации", СтруктураДанных.ВыполняемаяОперация))[0];
		
		Если СтруктураДанных.УспешноВыполнено Тогда
			Если СтруктураДанных.Свойство("ТребуетсяКонтроль") Тогда
				Если Не СтруктураДанных.ТребуетсяКонтроль Тогда
					СтрокаОперации.Состояние = "Выполнено";
				Иначе
					СтрокаОперации.Состояние = "ВыполненоСОшибками";
				КонецЕсли;
			Иначе
				СтрокаОперации.Состояние = "Выполнено";
			КонецЕсли;
			
			ДокументыОперации = ?(СтруктураДанных.Свойство("ДокументыОперации"), СтруктураДанных.ДокументыОперации, Неопределено);
			ЗафиксироватьВыполнениеОперацииПерехода(СтрокаОперации, ДокументыОперации, Истина);
			ЗаполнитьДанныеОпераций();
			
		КонецЕсли;
		
		ОбновитьСостояниеОперацийНаФорме();
		
		Если СтруктураДанных.Свойство("МассивСообщений")
			И СтруктураДанных.МассивСообщений.Количество() > 0 Тогда
			
			СтруктураДанныхНаКлиенте.Вставить("МассивСообщений", СтруктураДанных.МассивСообщений);
			
		ИначеЕсли НЕ ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
			
			ФоновоеЗадание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(ИдентификаторЗадания);
			Если ФоновоеЗадание <> Неопределено Тогда
				МассивСообщений = ФоновоеЗадание.ПолучитьСообщенияПользователю(Истина);
				Если МассивСообщений <> Неопределено Тогда
					СтруктураДанныхНаКлиенте.Вставить("МассивСообщений", МассивСообщений);
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СтруктураДанныхНаКлиенте;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗаданиеВыполнено(ИдентификаторЗадания)
	
	Возврат ДлительныеОперации.ЗаданиеВыполнено(ИдентификаторЗадания);
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеЗадания()
	
	Попытка
		Если ФормаДлительнойОперации.Открыта() 
			И ФормаДлительнойОперации.ИдентификаторЗадания = ИдентификаторЗадания Тогда
			Если ЗаданиеВыполнено(ИдентификаторЗадания) Тогда
				ЗагрузитьПодготовленныеДанныеНаКлиенте(ЗагрузитьПодготовленныеДанные());
				ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
			Иначе
				ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
				ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания",
					ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
			КонецЕсли;
		Иначе
			// Задание отменено
			ИдентификаторЗадания = Неопределено;
		КонецЕсли;
	Исключение
		ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборИзМенюЗавершение(Команда, ДополнительныеПараметры) Экспорт
	
	Если Не Команда = Неопределено Тогда
		ОбработкаНажатияКнопкиМеню(Команда.Значение, ДополнительныеПараметры.Элемент, ДополнительныеПараметры.СтрокаОперации);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеФормойБлокСверка()
	
	Если ЕстьПравоНаДОсКО И Используется1СОтчетность И МожноВыполнятьСверку Тогда
		
		Элементы.ГруппаСверка.Видимость = Истина;
		
		Если СверкаВыполнена Тогда
			Элементы.ВыполнитьДействиеСверка.Видимость = Ложь;
			Элементы.СсылкаНаСверку.Видимость = Истина;
			Элементы.СсылкаНаСверку.Заголовок = ЗапросНаСверкуНаименование;
		Иначе
			Элементы.ВыполнитьДействиеСверка.Видимость = Истина;
			Элементы.ВыполнитьДействиеСверка.Заголовок = ОписаниеДействияСверка.Наименование;
			Элементы.СсылкаНаСверку.Видимость = Ложь;
		КонецЕсли;
		
	Иначе
		Элементы.ГруппаСверка.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СведенияОСверке(ПараметрыСверки) Экспорт
	
	СведенияОСверке = Новый Структура();
	
	СведенияОСверке.Вставить("СверкаВыполнена",            Ложь);
	СведенияОСверке.Вставить("ОписаниеДействияСверка",     Новый Структура);
	СведенияОСверке.Вставить("ЗапросНаСверкуСсылка",       Неопределено);
	СведенияОСверке.Вставить("ЗапросНаСверкуНаименование", "");
	СведенияОСверке.Вставить("МожноВыполнятьСверку",       Истина);
	
	ЗапросНаСверкуНаименование = "";
	НаименованиеДействия       = "";
	
	КонтролирующийОрган  = Перечисления.ТипыКонтролирующихОрганов.ФНС;
	НаименованиеДействия = НСтр("ru = 'Запросить сверку с ФНС';
								|en = 'Request reconciliation with the Federal Tax Service'");
	
	// Проверим подключение документооборота с контролирующим органом.
	ДокументооборотПодключен = ИнтерфейсыВзаимодействияБРО.ПодключенДокументооборотСКонтролирующимОрганом(
			ПараметрыСверки.Организация,
			КонтролирующийОрган);
			
	Если НЕ ДокументооборотПодключен Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Организация", ПараметрыСверки.Организация);
		
		СведенияОСверке.ОписаниеДействияСверка.Вставить("Наименование",   НаименованиеДействия);
		СведенияОСверке.ОписаниеДействияСверка.Вставить("ИмяФормы",       "Обработка.ОбщиеОбъектыРеглОтчетности.Форма.ПредложениеОформитьЗаявлениеНаПодключение");
		СведенияОСверке.ОписаниеДействияСверка.Вставить("ПараметрыФормы", ПараметрыФормы);
		
		Возврат СведенияОСверке;
		
	КонецЕсли;
	
	РегистрацияВНалоговомОргане = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрыСверки.Организация, "РегистрацияВНалоговомОргане");
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация", ПараметрыСверки.Организация);
	ПараметрыФормы.Вставить("ПериодСобытия", НачалоГода(ПараметрыСверки.Период));
	ПараметрыФормы.Вставить("РегистрацияВНалоговомОргане", РегистрацияВНалоговомОргане);
	ПараметрыФормы.Вставить("ОКТМО", "");
	
	СписокКБК = Новый Массив;
	
	Если СписокКБК.Количество() > 0 Тогда
		ПараметрыФормы.Вставить("СписокКБК", СписокКБК);
	КонецЕсли;
	
	ПериодНачалаСобытия = ИнтерфейсыВзаимодействияБРОКлиентСервер.ДобавитьПериод(
		ПараметрыСверки.Период, Перечисления.Периодичность.Месяц);
	ПараметрыФормы.Вставить("ПериодНачалаСобытия", НачалоМесяца(ПараметрыСверки.Период));
	
	ОписаниеДействия = Новый Структура;
	ОписаниеДействия.Вставить("Наименование", НаименованиеДействия);
	ОписаниеДействия.Вставить("ИмяФормы", "Документ.ЗапросНаИнформационноеОбслуживаниеНалогоплательщика.Форма.ФормаДокумента");
	
	ЗапросНаСверкуСсылка = ЭлектронныйДокументооборотСКонтролирующимиОрганами.ПолучитьСуществующуюСверку(ПараметрыФормы, Строка(КонтролирующийОрган));
	Если ЗапросНаСверкуСсылка = Неопределено Тогда
		ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
	Иначе
		СведенияОСверке.СверкаВыполнена = Истина;
		ЗапросНаСверкуНаименование = ПредставлениеЗапросаНаСверку(ЗапросНаСверкуСсылка);
		ОписаниеДействия.Вставить("ПараметрыФормы", Новый Структура("Ключ", ЗапросНаСверкуСсылка));
	КонецЕсли;
	
	СведенияОСверке.ОписаниеДействияСверка     = ОписаниеДействия;
	СведенияОСверке.ЗапросНаСверкуСсылка       = ЗапросНаСверкуСсылка;
	СведенияОСверке.ЗапросНаСверкуНаименование = ЗапросНаСверкуНаименование;
	
	Возврат СведенияОСверке;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПредставлениеЗапросаНаСверку(ДокументСсылка)
	
	ТипДокумента = ТипЗнч(ДокументСсылка);
	
	Если ТипДокумента <> Тип("ДокументСсылка.ЗапросНаИнформационноеОбслуживаниеНалогоплательщика")
		И ТипДокумента <> Тип("ДокументСсылка.ЗапросНаИнформационноеОбслуживаниеСтрахователя") Тогда 
		Возврат "";
	КонецЕсли;
	
	СостояниеОтправки = ИнтерфейсыВзаимодействияБРО.ПредставлениеСостоянияДокумента(ДокументСсылка);
		
	ПредставлениеДокумента = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Запрос на сверку (%1)';
					|en = 'Reconciliation request (%1)'"),
				СостояниеОтправки);
	
	Возврат ПредставлениеДокумента;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДатуПереходаНаЕНП()
	
	Возврат РеглУчетСервер.ДатаНачалаОбязательногоПримененияЕНП();
	
КонецФункции

&НаКлиенте
Процедура ОбработатьВыполнениеСверки(РезультатЗакрытия = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	ПараметрыСверки = Новый Структура("Организация, Период",
		ГоловнаяОрганизация, Объект.ДатаПерехода);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, СведенияОСверке(ПараметрыСверки));
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыполнениеСверкиЗавершение1СОтчетность(РезультатЗакрытия = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если РезультатЗакрытия = Истина Тогда
		
		ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьФормуМастераЗаявленияНаПодключение(Объект.Организация,,,,,,Истина);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
