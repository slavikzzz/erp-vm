
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ОписаниеОперацийИТиповДокументов(ТЗХозОперацииИТипыДокументов) Экспорт
	
	#Область СборкаТоваров
	
	СтрокаСборкаТоваров = ТЗХозОперацииИТипыДокументов.Добавить();
	Строка = СтрокаСборкаТоваров;
	Строка.ХозяйственнаяОперация		= Перечисления.ХозяйственныеОперации.СборкаТоваров;
	Строка.КлючНазначенияИспользования	= "СборкиРазборкиТоваров";
	Строка.ЗаголовокРабочегоМеста		= НСтр("ru = 'Внутренние документы (сборки товаров)';
												|en = 'Internal documents (product kitting)'");
	Строка.ТипДокумента					= Тип("ДокументСсылка.СборкаТоваров");
	Строка.ПолноеИмяДокумента			= Метаданные.Документы.СборкаТоваров.ПолноеИмя();
	Строка.ИспользуютсяСтатусы			= ПолучитьФункциональнуюОпцию("ИспользоватьСтатусыСборокТоваров");
	Строка.ДобавитьКнопкуСоздать		= Истина;
	Строка.МенеджерРасчетаГиперссылкиКОформлению = "Обработка.ЖурналДокументовВнутреннегоТовародвижения";
	Строка.КлючевыеПоляШапки			= Документы.СборкаТоваров.КлючевыеПоляШапкиРаспоряжения();
	Строка.ЗаголовокФормыПереоформления	= НСтр("ru = 'Переоформление сборки товаров по выбранным распоряжениям';
													|en = 'Reregister product kitting according to the selected references'");
	
	Строка = ТЗХозОперацииИТипыДокументов.Добавить();
	ЗаполнитьЗначенияСвойств(Строка, СтрокаСборкаТоваров);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.РазборкаТоваров;
	Строка.ЗаголовокРабочегоМеста		= НСтр("ru = 'Внутренние документы (разборки товаров)';
												|en = 'Internal documents (reverse kitting of goods)'");
	Строка.ЗаголовокФормыПереоформления	= НСтр("ru = 'Переоформление разборки товаров по выбранным распоряжениям';
													|en = 'Reregister reverse kitting of goods  for the selected references'");
	
	#КонецОбласти
	
	#Область ВнутреннееПотребление
	
	СтрокаВнутреннееПотребление = ТЗХозОперацииИТипыДокументов.Добавить();
	Строка = СтрокаВнутреннееПотребление;
	Строка.ХозяйственнаяОперация		= Перечисления.ХозяйственныеОперации.СписаниеТоваровПоТребованию;
	Строка.КлючНазначенияИспользования	= "ВнутренниеПотребления";
	Строка.ЗаголовокРабочегоМеста		= НСтр("ru = 'Внутренние документы (внутренние потребления)';
												|en = 'Inventory consumption — Non-operational activities'");
	Строка.ТипДокумента					= Тип("ДокументСсылка.ВнутреннееПотребление");
	Строка.ПолноеИмяДокумента			= Метаданные.Документы.ВнутреннееПотребление.ПолноеИмя();
	Строка.ИспользуютсяСтатусы			= ПолучитьФункциональнуюОпцию("ИспользоватьСтатусыВнутреннихПотреблений");
	Строка.ДобавитьКнопкуСоздать		= Истина;
	Строка.МенеджерРасчетаГиперссылкиКОформлению = "Обработка.ЖурналДокументовВнутреннегоТовародвижения";
	Строка.КлючевыеПоляШапки			= Документы.ВнутреннееПотребление.КлючевыеПоляШапкиРаспоряжения();
	Строка.ЗаголовокФормыПереоформления	= НСтр("ru = 'Переоформление списания товаров по выбранным распоряжениям';
													|en = 'Reregister goods write-off of the selected references'");
	
	#КонецОбласти
	
	#Область ПеремещениеТоваров
	
	СтрокаПеремещениеТоваров = ТЗХозОперацииИТипыДокументов.Добавить();
	Строка = СтрокаПеремещениеТоваров;
	Строка.ХозяйственнаяОперация		= Перечисления.ХозяйственныеОперации.ПеремещениеТоваров;
	Строка.КлючНазначенияИспользования	= "ПеремещенияТоваров";
	Строка.ЗаголовокРабочегоМеста		= НСтр("ru = 'Внутренние документы (перемещения товаров)';
												|en = 'Internal documents (inventory transfers)'");
	Строка.ТипДокумента					= Тип("ДокументСсылка.ПеремещениеТоваров");
	Строка.ПолноеИмяДокумента			= Метаданные.Документы.ПеремещениеТоваров.ПолноеИмя();
	Строка.ИспользуютсяСтатусы			= ПолучитьФункциональнуюОпцию("ИспользоватьСтатусыПеремещенийТоваров");
	Строка.ДобавитьКнопкуСоздать		= Истина;
	Строка.МенеджерРасчетаГиперссылкиКОформлению = "Обработка.ЖурналДокументовВнутреннегоТовародвижения";
	Строка.КлючевыеПоляШапки			= Документы.ПеремещениеТоваров.КлючевыеПоляШапкиРаспоряжения();
	Строка.ЗаголовокФормыПереоформления	= НСтр("ru = 'Переоформление перемещения товаров по выбранным распоряжениям';
													|en = 'Reregister inventory transfer for the selected references'");
	
	Строка = ТЗХозОперацииИТипыДокументов.Добавить();
	ЗаполнитьЗначенияСвойств(Строка, СтрокаПеремещениеТоваров);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ПеремещениеТоваровМеждуФилиалами;
	Строка.ЗаголовокФормыПереоформления	= НСтр("ru = 'Переоформление перемещения товаров между филиалами по выбранным распоряжениям';
													|en = 'Reregister inventory transfer between branch offices for the selected references'");
	
	#КонецОбласти
	
	#Область АктОРасхожденияхПослеПеремещения
	
	СтрокаАктОРасхожденияхПослеПеремещения = ТЗХозОперацииИТипыДокументов.Добавить();
	Строка = СтрокаАктОРасхожденияхПослеПеремещения;
	Строка.ХозяйственнаяОперация		= Перечисления.ХозяйственныеОперации.ПеремещениеТоваров;
	Строка.КлючНазначенияИспользования	= "АктыОРасхожденияхПослеПеремещения";
	Строка.ЗаголовокРабочегоМеста		= НСтр("ru = 'Внутренние документы (акты о расхождениях после перемещения)';
												|en = 'Internal documents (stock transfer discrepancy reports)'");
	Строка.ТипДокумента					= Тип("ДокументСсылка.АктОРасхожденияхПослеПеремещения");
	Строка.ПолноеИмяДокумента			= Метаданные.Документы.АктОРасхожденияхПослеПеремещения.ПолноеИмя();
	Строка.ИспользуютсяСтатусы			= Истина;
	Строка.ДобавитьКнопкуСоздать		= Истина;
	
	Строка = ТЗХозОперацииИТипыДокументов.Добавить();
	ЗаполнитьЗначенияСвойств(Строка, СтрокаАктОРасхожденияхПослеПеремещения);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ПеремещениеТоваровМеждуФилиалами;
	
	#КонецОбласти
	
	#Область ПрочееОприходованиеТоваров
	
	СтрокаПрочееОприходованиеТоваров = ТЗХозОперацииИТипыДокументов.Добавить();
	Строка = СтрокаПрочееОприходованиеТоваров;
	Строка.ХозяйственнаяОперация		= Перечисления.ХозяйственныеОперации.ПоступлениеИзПроизводства;
	Строка.КлючНазначенияИспользования	= "ПрочиеОприходованияТоваров";
	Строка.ЗаголовокРабочегоМеста		= НСтр("ru = 'Внутренние документы (прочие оприходования товаров)';
												|en = 'Internal documents (other recording of goods as received)'");
	Строка.ТипДокумента					= Тип("ДокументСсылка.ПрочееОприходованиеТоваров");
	Строка.ПолноеИмяДокумента			= Метаданные.Документы.ПрочееОприходованиеТоваров.ПолноеИмя();
	Строка.ИспользуютсяСтатусы			= Ложь;
	Строка.ДобавитьКнопкуСоздать		= Истина;
	Строка.МенеджерРасчетаГиперссылкиКОформлению = "Обработка.ЖурналДокументовВнутреннегоТовародвижения";
	
	Строка = ТЗХозОперацииИТипыДокументов.Добавить();
	ЗаполнитьЗначенияСвойств(Строка, СтрокаПрочееОприходованиеТоваров);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.СторноСписанияНаРасходы;
	Строка.МенеджерРасчетаГиперссылкиКОформлению = "";
	
	Строка = ТЗХозОперацииИТипыДокументов.Добавить();
	ЗаполнитьЗначенияСвойств(Строка, СтрокаПрочееОприходованиеТоваров);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ОприходованиеЗаСчетДоходов;
	Строка.МенеджерРасчетаГиперссылкиКОформлению = "";
	
	Строка = ТЗХозОперацииИТипыДокументов.Добавить();
	ЗаполнитьЗначенияСвойств(Строка, СтрокаПрочееОприходованиеТоваров);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ОприходованиеЗаСчетРасходов;
	
	//++ НЕ УТ
	Строка = ТЗХозОперацииИТипыДокументов.Добавить();
	ЗаполнитьЗначенияСвойств(Строка, СтрокаПрочееОприходованиеТоваров);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ОприходованиеПриВыбытииОС;
	Строка.МенеджерРасчетаГиперссылкиКОформлению = "";
	//-- НЕ УТ
	#КонецОбласти
	
	#Область СкладскиеАкты
	
	// Оприходование излишков товаров.
	СтрокаСкладскиеАкты = ТЗХозОперацииИТипыДокументов.Добавить();
	Строка = СтрокаСкладскиеАкты;
	Строка.ХозяйственнаяОперация		= Перечисления.ХозяйственныеОперации.ОприходованиеТоваров;
	Строка.КлючНазначенияИспользования	= "";
	Строка.ЗаголовокРабочегоМеста		= НСтр("ru = 'Внутренние документы (складские акты)';
												|en = 'Internal documents (inventory adjustments)'");
	Строка.ТипДокумента					= Тип("ДокументСсылка.ОприходованиеИзлишковТоваров");
	Строка.ПолноеИмяДокумента			= Метаданные.Документы.ОприходованиеИзлишковТоваров.ПолноеИмя();
	Строка.ИспользуютсяСтатусы			= Ложь;
	Строка.ДобавитьКнопкуСоздать		= Истина;
	Строка.МенеджерРасчетаГиперссылкиКОформлению = "Обработка.ЖурналСкладскихАктов";
	
	// Пересортица товаров.
	Строка = ТЗХозОперацииИТипыДокументов.Добавить();
	ЗаполнитьЗначенияСвойств(Строка, СтрокаСкладскиеАкты);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ПересортицаТоваров;
	Строка.ТипДокумента					= Тип("ДокументСсылка.ПересортицаТоваров");
	Строка.ПолноеИмяДокумента			= Метаданные.Документы.ПересортицаТоваров.ПолноеИмя();
	
	// Порча товаров.
	Строка = ТЗХозОперацииИТипыДокументов.Добавить();
	ЗаполнитьЗначенияСвойств(Строка, СтрокаСкладскиеАкты);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ПорчаТоваров;
	Строка.ТипДокумента					= Тип("ДокументСсылка.ПорчаТоваров");
	Строка.ПолноеИмяДокумента			= Метаданные.Документы.ПорчаТоваров.ПолноеИмя();
	
	// Списание недостач товаров.
	Строка = ТЗХозОперацииИТипыДокументов.Добавить();
	ЗаполнитьЗначенияСвойств(Строка, СтрокаСкладскиеАкты);
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.СписаниеТоваров;
	Строка.ТипДокумента					= Тип("ДокументСсылка.СписаниеНедостачТоваров");
	Строка.ПолноеИмяДокумента			= Метаданные.Документы.СписаниеНедостачТоваров.ПолноеИмя();
	
	#КонецОбласти
	
	//++ НЕ УТ

	//++ Устарело_Производство21	
	#Область Производство21
	
	ИспользоватьПроизводство = ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеПроизводством");
	
	Если ИспользоватьПроизводство Тогда
								
		// Передача материалов в производство.
		Строка = ТЗХозОперацииИТипыДокументов.Добавить();
		Строка.ХозяйственнаяОперация		= Перечисления.ХозяйственныеОперации.ПередачаВПроизводство;
		Строка.КлючНазначенияИспользования	= "";
		Строка.ЗаголовокРабочегоМеста		= НСтр("ru = 'Внутренние документы (передачи материалов в производство)';
													|en = 'Internal documents (transfer of materials to production)'");
		Строка.ТипДокумента					= Тип("ДокументСсылка.ПередачаМатериаловВПроизводство");
		Строка.ПолноеИмяДокумента			= Метаданные.Документы.ПередачаМатериаловВПроизводство.ПолноеИмя();
		Строка.ИспользуютсяСтатусы			= Истина;
		Строка.ДобавитьКнопкуСоздать		= Истина;
		Строка.МенеджерРасчетаГиперссылкиКОформлению = "Обработка.ЖурналДокументовВнутреннегоТовародвижения";
		Строка.КлючевыеПоляШапки			= Документы.ПередачаМатериаловВПроизводство.КлючевыеПоляШапкиРаспоряжения();
		Строка.ЗаголовокФормыПереоформления	= НСтр("ru = 'Переоформление передачи в производство по выбранным распоряжениям';
														|en = 'Reregister transfer to production for the selected references'");
		
		// Возврат материалов из производства.
		Строка = ТЗХозОперацииИТипыДокументов.Добавить();
		Строка.ХозяйственнаяОперация		= Перечисления.ХозяйственныеОперации.ВозвратИзПроизводства;
		Строка.КлючНазначенияИспользования	= "";
		Строка.ЗаголовокРабочегоМеста		= НСтр("ru = 'Внутренние документы (возвраты материалов из производства)';
													|en = 'Internal documents (returns of materials from production)'");
		Строка.ТипДокумента					= Тип("ДокументСсылка.ВозвратМатериаловИзПроизводства");
		Строка.ПолноеИмяДокумента			= Метаданные.Документы.ВозвратМатериаловИзПроизводства.ПолноеИмя();
		Строка.ИспользуютсяСтатусы			= Истина;
		Строка.ДобавитьКнопкуСоздать		= Истина;
		
		// Выпуск продукции.
		Строка = ТЗХозОперацииИТипыДокументов.Добавить();
		Строка.ХозяйственнаяОперация		= Перечисления.ХозяйственныеОперации.ВыпускПродукцииНаСклад;
		Строка.КлючНазначенияИспользования	= "";
		Строка.ЗаголовокРабочегоМеста		= НСтр("ru = 'Внутренние документы (выпуски продукции)';
													|en = 'Internal documents (product releases)'");
		Строка.ТипДокумента					= Тип("ДокументСсылка.ВыпускПродукции");
		Строка.ПолноеИмяДокумента			= Метаданные.Документы.ВыпускПродукции.ПолноеИмя();
		Строка.ИспользуютсяСтатусы			= Ложь;
		Строка.ДобавитьКнопкуСоздать		= Истина;
		
	КонецЕсли;
	
	#КонецОбласти
	//-- Устарело_Производство21
	
	#Область ДвижениеПродукцииИМатериалов
	
	ДоступныеОперацииДокумента = Документы.ДвижениеПродукцииИМатериалов.СписокОпераций();
	
	СвойстваПоУмолчанию = Новый Структура(
		"КлючНазначенияИспользования,ЗаголовокРабочегоМеста,ТипДокумента,ПолноеИмяДокумента,ИспользуютсяСтатусы,
		|ДобавитьКнопкуСоздать,КлючевыеПоляШапки, ЗаголовокФормыПереоформления,МенеджерРасчетаГиперссылкиКОформлению");
	
	СвойстваПоУмолчанию.КлючНазначенияИспользования  = "";
	СвойстваПоУмолчанию.ЗаголовокРабочегоМеста       = НСтр("ru = 'Внутренние документы (движения продукции и материалов)';
															|en = 'Internal documents (transfer of products and materials)'");
	СвойстваПоУмолчанию.ТипДокумента                 = Тип("ДокументСсылка.ДвижениеПродукцииИМатериалов");
	СвойстваПоУмолчанию.ПолноеИмяДокумента           = Метаданные.Документы.ДвижениеПродукцииИМатериалов.ПолноеИмя();
	СвойстваПоУмолчанию.ИспользуютсяСтатусы          = ПолучитьФункциональнуюОпцию("ИспользоватьСтатусыДвиженийПродукцииИМатериалов");
	СвойстваПоУмолчанию.ДобавитьКнопкуСоздать        = Истина;
	СвойстваПоУмолчанию.КлючевыеПоляШапки            = Документы.ДвижениеПродукцииИМатериалов.КлючевыеПоляШапкиРаспоряжения();
	СвойстваПоУмолчанию.ЗаголовокФормыПереоформления = "";
	СвойстваПоУмолчанию.МенеджерРасчетаГиперссылкиКОформлению = "Обработка.ЖурналДокументовВнутреннегоТовародвижения";
	
	ТекущаяОперация = Перечисления.ХозяйственныеОперации.ВозвратМатериаловИзКладовой;
	Если ДоступныеОперацииДокумента.НайтиПоЗначению(ТекущаяОперация) <> Неопределено Тогда
		
		Строка = ТЗХозОперацииИТипыДокументов.Добавить();
		Строка.ХозяйственнаяОперация = ТекущаяОперация;
		ЗаполнитьЗначенияСвойств(Строка, СвойстваПоУмолчанию);
		Строка.ЗаголовокФормыПереоформления = "";
		
	КонецЕсли;
	
	ТекущаяОперация = Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзКладовой;
	Если ДоступныеОперацииДокумента.НайтиПоЗначению(ТекущаяОперация) <> Неопределено Тогда
		
		Строка = ТЗХозОперацииИТипыДокументов.Добавить();
		Строка.ХозяйственнаяОперация = ТекущаяОперация;
		ЗаполнитьЗначенияСвойств(Строка, СвойстваПоУмолчанию);
		Строка.ЗаголовокФормыПереоформления = "";
		
	КонецЕсли;
	
	ТекущаяОперация = Перечисления.ХозяйственныеОперации.ПеремещениеПолуфабрикатов;
	Если ДоступныеОперацииДокумента.НайтиПоЗначению(ТекущаяОперация) <> Неопределено Тогда
		
		Строка = ТЗХозОперацииИТипыДокументов.Добавить();
		Строка.ХозяйственнаяОперация = ТекущаяОперация;
		ЗаполнитьЗначенияСвойств(Строка, СвойстваПоУмолчанию);
		Строка.МенеджерРасчетаГиперссылкиКОформлению = "";
		Строка.ЗаголовокФормыПереоформления = "";
		
	КонецЕсли;
	
	ТекущаяОперация = Перечисления.ХозяйственныеОперации.ПеремещениеПолуфабрикатовМеждуФилиалами;
	Если ДоступныеОперацииДокумента.НайтиПоЗначению(ТекущаяОперация) <> Неопределено Тогда
		
		Строка = ТЗХозОперацииИТипыДокументов.Добавить();
		Строка.ХозяйственнаяОперация = ТекущаяОперация;
		ЗаполнитьЗначенияСвойств(Строка, СвойстваПоУмолчанию);
		Строка.МенеджерРасчетаГиперссылкиКОформлению = "";
		Строка.ЗаголовокФормыПереоформления = "";
		
	КонецЕсли;
	
	ТекущаяОперация = Перечисления.ХозяйственныеОперации.ПередачаМатериаловВПроизводство;
	Если ДоступныеОперацииДокумента.НайтиПоЗначению(ТекущаяОперация) <> Неопределено Тогда
		
		Строка = ТЗХозОперацииИТипыДокументов.Добавить();
		Строка.ХозяйственнаяОперация = ТекущаяОперация;
		ЗаполнитьЗначенияСвойств(Строка, СвойстваПоУмолчанию);
		Строка.ЗаголовокФормыПереоформления = НСтр("ru = 'Переоформление передачи в производство по выбранным распоряжениям';
													|en = 'Reregister transfer to production for the selected references'");
		
	КонецЕсли;
	
	ТекущаяОперация = Перечисления.ХозяйственныеОперации.ВозвратМатериаловИзПроизводства;
	Если ДоступныеОперацииДокумента.НайтиПоЗначению(ТекущаяОперация) <> Неопределено Тогда
		
		Строка = ТЗХозОперацииИТипыДокументов.Добавить();
		Строка.ХозяйственнаяОперация = ТекущаяОперация;
		ЗаполнитьЗначенияСвойств(Строка, СвойстваПоУмолчанию);
		Строка.ЗаголовокФормыПереоформления = НСтр("ru = 'Переоформление возврата из производства по выбранным распоряжениям';
													|en = 'Reregister return from production by selected references'");
		
	КонецЕсли;
	
	ТекущаяОперация = Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзПроизводства;
	Если ДоступныеОперацииДокумента.НайтиПоЗначению(ТекущаяОперация) <> Неопределено Тогда
		
		Строка = ТЗХозОперацииИТипыДокументов.Добавить();
		Строка.ХозяйственнаяОперация = ТекущаяОперация;
		ЗаполнитьЗначенияСвойств(Строка, СвойстваПоУмолчанию);
		Строка.ЗаголовокФормыПереоформления = НСтр("ru = 'Переоформление передачи из производства по выбранным распоряжениям';
													|en = 'Reregister transfer from production for the selected references'");
		
	КонецЕсли;
	
	ТекущаяОперация = Перечисления.ХозяйственныеОперации.ПередачаМатериаловВКладовую;
	Если ДоступныеОперацииДокумента.НайтиПоЗначению(ТекущаяОперация) <> Неопределено Тогда
		
		Строка = ТЗХозОперацииИТипыДокументов.Добавить();
		Строка.ХозяйственнаяОперация = ТекущаяОперация;
		ЗаполнитьЗначенияСвойств(Строка, СвойстваПоУмолчанию);
		Строка.ЗаголовокФормыПереоформления = НСтр("ru = 'Переоформление передачи в кладовую по выбранным распоряжениям';
													|en = 'Reregister transfer to storeroom for the selected references'");
		
	КонецЕсли;
	
	#КонецОбласти
	
	//-- НЕ УТ
	
	#Область ВнутреннееПотребление
	
	Если Не ПолучитьФункциональнуюОпцию("БазоваяВерсия") Тогда
		
		Строка = ТЗХозОперацииИТипыДокументов.Добавить();
		Строка.ХозяйственнаяОперация		= Перечисления.ХозяйственныеОперации.ПередачаВЭксплуатацию;
		Строка.КлючНазначенияИспользования	= "ВнутренниеПотребленияТоваров";
		Строка.ЗаголовокРабочегоМеста		= НСтр("ru = 'Внутренние документы (внутренние потребления товаров)';
													|en = 'Inventory documents (inventory consumption)'");
		Строка.ТипДокумента					= Тип("ДокументСсылка.ВнутреннееПотребление");
		Строка.ПолноеИмяДокумента			= Метаданные.Документы.ВнутреннееПотребление.ПолноеИмя();
		Строка.ИспользуютсяСтатусы			= ПолучитьФункциональнуюОпцию("ИспользоватьСтатусыВнутреннихПотреблений");
		Строка.ДобавитьКнопкуСоздать		= Истина;
		Строка.МенеджерРасчетаГиперссылкиКОформлению = "Обработка.ЖурналДокументовВнутреннегоТовародвижения";
		Строка.КлючевыеПоляШапки			= Документы.ВнутреннееПотребление.КлючевыеПоляШапкиРаспоряжения();
		Строка.ЗаголовокФормыПереоформления	= НСтр("ru = 'Переоформление передачи в эксплуатацию по выбранным распоряжениям';
														|en = 'Reregister commissioning under the selected references'");
		
	КонецЕсли;

	#КонецОбласти
	
	#Область ПрочееОприходованиеТоваров
	
	Строка = ТЗХозОперацииИТипыДокументов.Добавить();
	Строка.ХозяйственнаяОперация		= Перечисления.ХозяйственныеОперации.ВозвратИзЭксплуатации;
	Строка.КлючНазначенияИспользования	= "ПрочиеОприходованияТоваров";
	Строка.ЗаголовокРабочегоМеста		= НСтр("ru = 'Внутренние документы (прочие оприходования товаров)';
												|en = 'Internal documents (other recording of goods as received)'");
	Строка.ТипДокумента					= Тип("ДокументСсылка.ПрочееОприходованиеТоваров");
	Строка.ПолноеИмяДокумента			= Метаданные.Документы.ПрочееОприходованиеТоваров.ПолноеИмя();
	Строка.ИспользуютсяСтатусы			= Ложь;
	Строка.ДобавитьКнопкуСоздать		= Истина;
	Строка.МенеджерРасчетаГиперссылкиКОформлению = "Обработка.ЖурналДокументовВнутреннегоТовародвижения";
	
	#КонецОбласти
	
	Возврат ТЗХозОперацииИТипыДокументов;
	
КонецФункции

Функция СформироватьГиперссылкуКОформлению(Параметры) Экспорт
	
	ТекстГиперссылки = НСтр("ru = 'Накладные';
							|en = 'Invoices'");
	ИмяФормыРабочееМесто = "Обработка.ЖурналДокументовВнутреннегоТовародвижения.Форма.СписокДокументовКОформлению";
	
	Если ЕстьДокументыКОформлению(Параметры) Тогда
		Возврат Новый ФорматированнаяСтрока(ТекстГиперссылки,,,, ИмяФормыРабочееМесто);
	Иначе
		Возврат Новый ФорматированнаяСтрока(ТекстГиперссылки,,ЦветаСтиля.НезаполненноеПолеТаблицы,, ИмяФормыРабочееМесто);
	КонецЕсли;
	
КонецФункции

Процедура СформироватьГиперссылкуКОформлениюФоновоеЗадание(Параметры, АдресХранилища) Экспорт
	
	КОформлению = ОбщегоНазначенияУТ.СформироватьГиперссылкуКОформлению(Параметры[0], Параметры[1]);
	ПоместитьВоВременноеХранилище(КОформлению, АдресХранилища);
	
КонецПроцедуры

Функция ЕстьДокументыКОформлению(Параметры) Экспорт
	
	ПараметрыОтбора = НакладныеСервер.ПараметрыОтбораРаспоряжений(Параметры.Организация,, Параметры.Склад, 
		Параметры.ОтборХозяйственныеОперации, Параметры.Менеджер);
	
	Состояния = НакладныеСервер.СостоянияПоХозоперациям(Параметры.ОтборХозяйственныеОперации.ВыгрузитьЗначения());
	ИменаТаблиц = НакладныеСервер.ИменаВременныхТаблицПоСостояниям();
	Если Не ЗначениеЗаполнено(Состояния) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Пакет = Новый Массив;
	Выборка = Новый Массив;
	Текст = "ВЫБРАТЬ
		|	КОформлению
		|ИЗ &ИмяТаблицы";
	Для каждого КЗ Из Состояния Цикл
		Пакет.Добавить(НакладныеСервер.ТекстЗапросаСостояний(КЗ.Ключ, ПараметрыОтбора, КЗ.Значение, Истина));
		Выборка.Добавить(СтрЗаменить(Текст, "&ИмяТаблицы", ИменаТаблиц[КЗ.Ключ]));
	КонецЦикла;
	
	Пакет.Добавить(СтрСоединить(Выборка, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении()));
	
	Если ЗначениеЗаполнено(Параметры.Организация) Тогда
		СписокОрганизаций = Справочники.Организации.ФилиалыСРасчетамиЧерезГоловнуюОрганизацию(Параметры.Организация);
		СписокОрганизаций.Добавить(Параметры.Организация);
	Иначе
		СписокОрганизаций = Новый Массив;
	КонецЕсли;
	
	Запрос = Новый Запрос(СтрСоединить(Пакет, ОбщегоНазначения.РазделительПакетаЗапросов()));
	НакладныеСервер.ДобавитьПараметрыОтбораПоРееструДокументов(Запрос, ПараметрыОтбора);
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("НачалоТекущегоДня", НачалоДня(ТекущаяДатаСеанса()));
	Запрос.УстановитьПараметр("Организация",       СписокОрганизаций);
	Запрос.УстановитьПараметр("Склад",             Параметры.Склад);
	Запрос.УстановитьПараметр("Менеджер",          Параметры.Менеджер);
	Запрос.УстановитьПараметр("ХозОперация",       Параметры.ОтборХозяйственныеОперации.ВыгрузитьЗначения());
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

Функция КоличествоСтрокКОформлению(ХозОперации) Экспорт
	
	ПараметрыОтбора = НакладныеСервер.ПараметрыОтбораРаспоряжений(,,, ХозОперации);
	
	Пакет = Новый Массив;
	Выборка = Новый Массив;
	Состояния = НакладныеСервер.СостоянияПоХозоперациям(ХозОперации);
	Для каждого КЗ Из Состояния Цикл
		Пакет.Добавить(НакладныеСервер.ТекстЗапросаСостояний(КЗ.Ключ, ПараметрыОтбора, КЗ.Значение));
		
		Если КЗ.Ключ = "СостоянияРасходныхОрдеров" Тогда
			Выборка.Добавить(
			"ВЫБРАТЬ
			|	СостоянияРасходныхОрдеров.Распоряжение КАК Распоряжение,
			|	СостоянияРасходныхОрдеров.Склад КАК Склад,
			|	СостоянияРасходныхОрдеров.Склад КАК Отправитель,
			|	ВЫБОР
			|		КОГДА ТИПЗНАЧЕНИЯ(СостоянияРасходныхОрдеров.Распоряжение) = ТИП(Документ.ЗаказНаВнутреннееПотребление)
			//++ НЕ УТКА
			|			ИЛИ ТИПЗНАЧЕНИЯ(СостоянияРасходныхОрдеров.Распоряжение) = ТИП(Документ.ЗаказНаРемонт)
			//-- НЕ УТКА
			|			ТОГДА НЕОПРЕДЕЛЕНО
			|		ИНАЧЕ СостоянияРасходныхОрдеров.Получатель
			|	КОНЕЦ КАК Получатель,
			|	ВЫБОР
			//++ НЕ УТКА

			//++ Устарело_Производство21
			|		КОГДА ТИПЗНАЧЕНИЯ(СостоянияРасходныхОрдеров.Распоряжение) = ТИП(Документ.ЗаказНаПроизводство)
			|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаВПроизводство)
			//-- Устарело_Производство21
			|		КОГДА ТИПЗНАЧЕНИЯ(СостоянияРасходныхОрдеров.Распоряжение) = ТИП(Документ.ЭтапПроизводства2_2)
			|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаМатериаловВПроизводство)
			|		КОГДА ТИПЗНАЧЕНИЯ(СостоянияРасходныхОрдеров.Распоряжение) = ТИП(Документ.ЗаказНаРемонт)
			|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию)
			//-- НЕ УТКА
			|		КОГДА ИСТИНА
			|			ТОГДА NULL
			|	КОНЕЦ КАК ХозОперация
			|ИЗ
			|	ВТСостоянияРасходныхОрдеров КАК СостоянияРасходныхОрдеров");
		ИначеЕсли КЗ.Ключ = "СостоянияПриходныхОрдеров" Тогда
			Выборка.Добавить(
			"ВЫБРАТЬ
			|	СостоянияПриходныхОрдеров.Распоряжение КАК Распоряжение,
			|	СостоянияПриходныхОрдеров.Склад КАК Склад,
			|	ВЫБОР
			|		КОГДА ТИПЗНАЧЕНИЯ(СостоянияПриходныхОрдеров.Распоряжение) = ТИП(Документ.ПрочееОприходованиеТоваров)
			|			ТОГДА НЕОПРЕДЕЛЕНО
			|		ИНАЧЕ СостоянияПриходныхОрдеров.Отправитель
			|	КОНЕЦ КАК Отправитель,
			|	СостоянияПриходныхОрдеров.Склад КАК Получатель,
			|	ВЫБОР
			//++ НЕ УТКА
			|		КОГДА ТИПЗНАЧЕНИЯ(СостоянияПриходныхОрдеров.Распоряжение) = ТИП(Документ.ЭтапПроизводства2_2)
			|			ТОГДА СостоянияПриходныхОрдеров.ХозОперация
			//-- НЕ УТКА
			|		КОГДА ИСТИНА
			|			ТОГДА NULL
			|	КОНЕЦ КАК ХозОперация
			|ИЗ
			|	ВтСостоянияПриходныхОрдеров КАК СостоянияПриходныхОрдеров
			|ГДЕ
			|	НЕ ТИПЗНАЧЕНИЯ(СостоянияПриходныхОрдеров.Распоряжение) = ТИП(Документ.ЗаказНаПеремещение)
			|	И НЕ ТИПЗНАЧЕНИЯ(СостоянияПриходныхОрдеров.Распоряжение) = ТИП(Документ.ПеремещениеТоваров)
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	СостоянияПриходныхОрдеров.Распоряжение,
			|	СостоянияПриходныхОрдеров.Отправитель,
			|	СостоянияПриходныхОрдеров.Отправитель,
			|	СостоянияПриходныхОрдеров.Склад,
			|	NULL
			|ИЗ
			|	ВтСостоянияПриходныхОрдеров КАК СостоянияПриходныхОрдеров
			|ГДЕ
			|	(ТИПЗНАЧЕНИЯ(СостоянияПриходныхОрдеров.Распоряжение) = ТИП(Документ.ЗаказНаПеремещение)
			|			ИЛИ ТИПЗНАЧЕНИЯ(СостоянияПриходныхОрдеров.Распоряжение) = ТИП(Документ.ПеремещениеТоваров))
			|	И &ИспользуютсяАктыРасхожденийПослеПеремещения");
		ИначеЕсли КЗ.Ключ = "СостоянияПеремещений" Тогда
			Выборка.Добавить(
			"ВЫБРАТЬ
			|	СостоянияПеремещений.Распоряжение КАК Распоряжение,
			|	ДокЗаказНаПеремещение.СкладОтправитель КАК Склад,
			|	ДокЗаказНаПеремещение.СкладОтправитель КАК Отправитель,
			|	ДокЗаказНаПеремещение.СкладПолучатель КАК Получатель,
			|	NULL КАК ХозОперация
			|ИЗ
			|	ВтСостоянияПеремещений КАК СостоянияПеремещений
			|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПеремещение КАК ДокЗаказНаПеремещение
			|		ПО СостоянияПеремещений.Распоряжение = ДокЗаказНаПеремещение.Ссылка");
		ИначеЕсли КЗ.Ключ = "СостоянияСборок" Тогда
			Выборка.Добавить(
			"ВЫБРАТЬ
			|	СостоянияСборок.Распоряжение КАК Распоряжение,
			|	ДокЗаказНаСборку.Склад КАК Склад,
			|	ДокЗаказНаСборку.Склад КАК Отправитель,
			|	ДокЗаказНаСборку.Склад КАК Получатель,
			|	NULL КАК ХозОперация
			|ИЗ
			|	ВтСостоянияСборок КАК СостоянияСборок
			|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаСборку КАК ДокЗаказНаСборку
			|		ПО СостоянияСборок.Распоряжение = ДокЗаказНаСборку.Ссылка");
		ИначеЕсли КЗ.Ключ = "СостоянияВнутреннихПотреблений" Тогда
			Выборка.Добавить(
			"ВЫБРАТЬ
			|	СостоянияВнутреннихПотреблений.Распоряжение КАК Распоряжение,
			|	СостоянияВнутреннихПотреблений.Склад КАК Склад,
			|	СостоянияВнутреннихПотреблений.Склад КАК Отправитель,
			|	НЕОПРЕДЕЛЕНО КАК Получатель,
			|	ВЫБОР
			//++ НЕ УТКА
			|		КОГДА ТИПЗНАЧЕНИЯ(СостоянияВнутреннихПотреблений.Распоряжение) = ТИП(Документ.ЗаказНаРемонт)
			|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию)
			//-- НЕ УТКА
			|		КОГДА ИСТИНА
			|			ТОГДА NULL
			|	КОНЕЦ КАК ХозОперация
			|ИЗ
			|	ВтСостоянияВнутреннихПотреблений КАК СостоянияВнутреннихПотреблений");
		//++ НЕ УТ
		ИначеЕсли КЗ.Ключ = "СостоянияПередачВПроизводство" Тогда
			Выборка.Добавить(
			"ВЫБРАТЬ
			|	СостоянияПередачВПроизводство.Распоряжение КАК Распоряжение,
			|	СостоянияПередачВПроизводство.Склад КАК Склад,
			|	СостоянияПередачВПроизводство.Склад КАК Отправитель,
			|	СостоянияПередачВПроизводство.Получатель КАК Получатель,
			|	ВЫБОР
			//++ НЕ УТКА

			//++ Устарело_Производство21
			|		КОГДА ТИПЗНАЧЕНИЯ(СостоянияПередачВПроизводство.Распоряжение) = ТИП(Документ.ЗаказНаПроизводство)
			|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаВПроизводство)
			//-- Устарело_Производство21
			|		КОГДА ТИПЗНАЧЕНИЯ(СостоянияПередачВПроизводство.Распоряжение) = ТИП(Документ.ЭтапПроизводства2_2)
			|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаМатериаловВПроизводство)
			//-- НЕ УТКА
			|		КОГДА ИСТИНА
			|			ТОГДА NULL
			|	КОНЕЦ КАК ХозОперация
			|ИЗ
			|	ВтСостоянияПередачВПроизводство КАК СостоянияПередачВПроизводство");
		//-- НЕ УТ

		//++ НЕ УТКА
		ИначеЕсли КЗ.Ключ = "СостоянияПередачИзПроизводства" Тогда
			Выборка.Добавить(
			"ВЫБРАТЬ
			|	СостоянияПередачИзПроизводства.Распоряжение КАК Распоряжение,
			|	СостоянияПередачИзПроизводства.Склад КАК Склад,
			|	СостоянияПередачИзПроизводства.Отправитель КАК Отправитель,
			|	СостоянияПередачИзПроизводства.Склад КАК Получатель,
			|	СостоянияПередачИзПроизводства.ХозОперация КАК ХозОперация
			|ИЗ
			|	ВтСостоянияПередачИзПроизводства КАК СостоянияПередачИзПроизводства");
		//-- НЕ УТКА
		КонецЕсли;
		
	КонецЦикла;
	
	Пакет.Добавить(
		СтрЗаменить(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВложенныйЗапрос.Распоряжение КАК Распоряжение,
		|	ВложенныйЗапрос.Склад КАК Склад,
		|	ВложенныйЗапрос.Отправитель КАК Отправитель,
		|	ВложенныйЗапрос.Получатель КАК Получатель,
		|	ВложенныйЗапрос.ХозОперация КАК ХозОперация
		|ИЗ
		|	ТекстЗапросаВложенный КАК ВложенныйЗапрос",
		"ТекстЗапросаВложенный", "(" + СтрСоединить(Выборка, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении()) + ")"));
	
	Запрос = Новый Запрос(СтрСоединить(Пакет, ОбщегоНазначения.РазделительПакетаЗапросов()));
	НакладныеСервер.ДобавитьПараметрыОтбораПоРееструДокументов(Запрос, ПараметрыОтбора);
	Запрос.УстановитьПараметр("НачалоТекущегоДня", НачалоДня(ТекущаяДатаСеанса()));
	Запрос.УстановитьПараметр("ХозОперация", ХозОперации);
	Запрос.УстановитьПараметр("ИспользуютсяАктыРасхожденийПослеПеремещения",
		ПолучитьФункциональнуюОпцию("ИспользоватьАктыРасхожденийПослеПеремещения"));
	
	Возврат Запрос.Выполнить().Выбрать().Количество();
	
КонецФункции

#КонецОбласти

#КонецЕсли
