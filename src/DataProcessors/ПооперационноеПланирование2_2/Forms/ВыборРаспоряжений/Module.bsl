
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ИнициализацияФормыПриСозданииНаСервере();
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	ДанныеКорректны = РаспоряженияЗаполненыКорректно();
	Если ДанныеКорректны Тогда
		
		Результат = РезультатЗакрытияФормы();
		Закрыть(Результат);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ИнициализацияФормыПриСозданииНаСервере()
	
	Для Каждого Распоряжение Из Параметры.Распоряжения Цикл
		
		СтрокаЗначений = СписокРаспоряжений.Добавить();
		СтрокаЗначений.Распоряжение = Распоряжение;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция РаспоряженияЗаполненыКорректно()
	
	Результат = Истина;
	Ошибки = Неопределено;
	
	Для Каждого Строка Из СписокРаспоряжений Цикл
		Если Не ЗначениеЗаполнено(Строка.Распоряжение) Тогда
			
			Результат = Ложь;
			
			ИмяПоля = НСтр("ru = 'Распоряжение';
							|en = 'Reference'");
			НомерСтроки = СписокРаспоряжений.Индекс(Строка);
			ТекстОшибки = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Заполнение", ИмяПоля, НомерСтроки);
			
			Поле = "СписокРаспоряжений[%1].Распоряжение";
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, Поле, ТекстОшибки, Неопределено, НомерСтроки);
			
		КонецЕсли;
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция РезультатЗакрытияФормы()
	
	Результат = Новый Массив;
	Для Каждого Строка Из СписокРаспоряжений Цикл
		Если Результат.Найти(Строка.Распоряжение) = Неопределено Тогда
			Результат.Добавить(Строка.Распоряжение);
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;
	
КонецФункции

#КонецОбласти
