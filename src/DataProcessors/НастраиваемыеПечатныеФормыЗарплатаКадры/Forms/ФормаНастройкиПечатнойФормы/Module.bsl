#Область ОписаниеПеременных

&НаКлиенте
Перем ЭтоДоступноеПользовательскоеПоле;

&НаКлиенте
Перем ЭтоДоступныйПараметрДанных;

&НаКлиенте
Перем ТекущееВыбранноеПоле;

&НаКлиенте
Перем ИдентификаторыМакетаПечатнойФормы Экспорт;

&НаКлиенте
Перем ИдентификаторыОбластей Экспорт;

#КонецОбласти


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Заголовок = Параметры.Представление;
	
	ПутьКМакетуПечатнойФормы = Параметры.ПутьКМакетуПечатнойФормы;
	ПутьКВариантуОтчета = Параметры.ПутьКВариантуОтчета;
	РежимРедактированияШаблонаДляКабинетаСотрудников = Параметры.РежимРедактированияШаблонаДляКабинетаСотрудников;
	
	Параметры.Свойство("МакетПечатнойФормы", МакетПечатнойФормы);
	Параметры.Свойство("ВариантОтчета", ВариантОтчета);
	ВерсияШаблонаДокумента = Параметры.ВерсияШаблонаДокумента;
	Если ЗначениеЗаполнено(Параметры.ШаблонДокумента) Тогда
		ИмяПредопределенныхДанных = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.ШаблонДокумента, "ИмяПредопределенныхДанных");
		Если ЗначениеЗаполнено(ИмяПредопределенныхДанных) Тогда
			ИмяПредопределенныхДанных = "Справочник.ШаблоныДокументов." + ИмяПредопределенныхДанных;
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Свойство("Заголовок") Тогда
		Заголовок = Параметры.Заголовок;
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	УстановитьОтображениеПолейРедактора();
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"МакетПечатнойФормыПредварительныйПросмотр",
		"Видимость",
		Не Параметры.НеОтображатьКомандуПредварительныйПросмотр);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"НастройкиОбластей",
		"Видимость",
		РежимРедактированияШаблонаДляКабинетаСотрудников);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"НастройкиОтчета",
		"ОтображениеСтраниц",
		?(РежимРедактированияШаблонаДляКабинетаСотрудников,
			ОтображениеСтраницФормы.Авто,
			ОтображениеСтраницФормы.Нет));
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ФормаВыгрузитьШаблон",
		"Видимость",
		РежимРедактированияШаблонаДляКабинетаСотрудников
			И ОбщегоНазначения.РежимОтладки());
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ФормаЗагрузитьШаблон",
		"Видимость",
		РежимРедактированияШаблонаДляКабинетаСотрудников
			И ОбщегоНазначения.РежимОтладки());
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ВыбранныеПоляДобавитьГруппа",
		"Видимость",
		РежимРедактированияШаблонаДляКабинетаСотрудников
			И ОбщегоНазначения.РежимОтладки());
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ВыбранныеПоляДобавитьПользовательскоеПоле",
		"Видимость",
		Не (РежимРедактированияШаблонаДляКабинетаСотрудников
			И ОбщегоНазначения.РежимОтладки()));
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗапомнитьИдентификаторыМакетаПечатнойФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	// Проверка того, что данные изменены
	Если Модифицированность Тогда
		
		Отказ = Истина;
		Если ЗавершениеРаботы Тогда
			Возврат;
		КонецЕсли;
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ЗакрытьФорму", Истина);
		
		ЗадатьВопросОНеобходимостиСохранения(ДополнительныеПараметры);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура МакетПечатнойФормыПриИзмененииСодержимогоОбласти(Элемент, Область)
	
	ПриИзмененииСодержимогоОбласти(Область);
	
КонецПроцедуры

&НаКлиенте
Процедура МакетПечатнойФормыПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Область)
	
	СтандартнаяОбработка = Ложь;
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) <> Тип("Массив")
		Или ТипЗнч(ПараметрыПеретаскивания.Значение[0]) <> Тип("ДанныеФормыЭлементКоллекции") Тогда
		
		ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура МакетПечатнойФормыПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Область)
	
	ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
	
	Если ПараметрыПеретаскивания.Значение.Количество() > 0 Тогда
		
		ИменаРеквизитов = Новый Массив;
		Для Каждого ПараметрПеретаскивания Из ПараметрыПеретаскивания.Значение Цикл
			ИменаРеквизитов.Добавить(ПараметрПеретаскивания.ИмяПоля);
		КонецЦикла;
		
		ОбластьЯчеек = МакетПечатнойФормы.Область(Область.Верх, Область.Лево);
		ВставитьПараметрыВОбласть(ОбластьЯчеек, ИменаРеквизитов);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыДоступныеПоля

&НаКлиенте
Процедура ДоступныеПоляВыбораВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ДобавитьПолеВыбора(ВыбраннаяСтрока) Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДоступныеПоляВыбораПриАктивизацииСтроки(Элемент)
	
	ЭтоПользовательскоеПоле = Ложь;
	ЭтоПолеПараметра = Ложь;
	ЭтоПолеДокумента = Ложь;
	Если Элемент.ТекущаяСтрока <> Неопределено Тогда
		ЭтоПолеПараметра = СтрНачинаетсяС(Строка(Элемент.ТекущаяСтрока), "DataParameters.");
		Если ОбщегоНазначенияКлиент.РежимОтладки() И Не ЭтоПолеПараметра Тогда
			Если РежимРедактированияШаблонаДляКабинетаСотрудников
				И ЭтоДанныеРеквизитаДокумента(Элемент.ТекущаяСтрока) Тогда
				
				ТекущаяСтрока = КомпоновщикНастроекКД.Настройки.Выбор.ДоступныеПоляВыбора.ПолучитьОбъектПоИдентификатору(Элементы.ДоступныеПоляВыбора.ТекущаяСтрока);
				ЭтоПолеДокумента = Не ТекущаяСтрока.Папка;
			КонецЕсли;
		КонецЕсли;
		Если РежимРедактированияШаблонаДляКабинетаСотрудников Тогда
			ЭтоПользовательскоеПоле = ЭтоПолеПараметра Или ЭтоПолеДокумента;
		Иначе
			ЭтоПользовательскоеПоле =
				КомпоновщикНастроекКД.Настройки.ПользовательскиеПоля.ПолучитьОбъектПоИдентификатору(
					Элемент.ТекущаяСтрока) <> Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	Если ЭтоДоступноеПользовательскоеПоле <> ЭтоПользовательскоеПоле Тогда
		
		ЭтоДоступноеПользовательскоеПоле = ЭтоПользовательскоеПоле;
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ВыбранныеПоляИзменитьПользовательскоеПоле",
			"Доступность",
			ЭтоДоступноеПользовательскоеПоле);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ВыбранныеПоляСкопироватьПользовательскоеПоле",
			"Видимость",
			РежимРедактированияШаблонаДляКабинетаСотрудников);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ВыбранныеПоляСкопироватьПользовательскоеПоле",
			"Доступность",
			ЭтоДоступноеПользовательскоеПоле);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ДоступныеПоляВыбораКонтекстноеМенюИзменитьПользовательскоеПоле",
			"Видимость",
			ЭтоДоступноеПользовательскоеПоле);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ДоступныеПоляВыбораКонтекстноеМенюСкопироватьПользовательскоеПоле",
			"Видимость",
			ЭтоДоступноеПользовательскоеПоле
				И Не РежимРедактированияШаблонаДляКабинетаСотрудников);
		
	КонецЕсли;
	
	Если ЭтоДоступныйПараметрДанных <> ЭтоПолеПараметра Тогда
		ЭтоДоступныйПараметрДанных = ЭтоПолеПараметра;
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ДоступныеПоляВыбораКонтекстноеМенюУстановитьЗначениеПараметра",
			"Видимость",
			ЭтоДоступныйПараметрДанных
				И Не РежимРедактированияШаблонаДляКабинетаСотрудников);
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ВыбранныеПоляУдалитьПараметр",
		"Видимость",
		ЭтоПользовательскоеПоле
			И РежимРедактированияШаблонаДляКабинетаСотрудников);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ПорядокПользовательскихПолейГруппа",
		"Видимость",
			ЭтоПолеПараметра
			И РежимРедактированияШаблонаДляКабинетаСотрудников);
		
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыВыбранныеПоля

&НаКлиенте
Процедура ВыбранныеПоляПриАктивизацииСтроки(Элемент)
	
	Если ТекущееВыбранноеПоле <> Элементы.ВыбранныеПоля.ТекущиеДанные Тогда
		
		ТекущееВыбранноеПоле = Элементы.ВыбранныеПоля.ТекущиеДанные;
		ЗаполнитьВыделенныеОбластиМакетаПечатнойФормы(МакетПечатнойФормы);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныеПоляПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;

	Если ТипЗнч(ПараметрыПеретаскивания.Значение) <> Тип("Массив")
		Или ТипЗнч(ПараметрыПеретаскивания.Значение[0]) <> Тип("ДоступноеПолеКомпоновкиДанных") Тогда
		
		ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныеПоляПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	Если Строка = Неопределено Тогда
		ИндексСтрокиДляВставки = Неопределено;
	Иначе
		ИндексСтрокиДляВставки = ВыбранныеПоля.Индекс(ВыбранныеПоля.НайтиПоИдентификатору(Строка));
	КонецЕсли;
	
	ИдентификаторСтроки = ВставитьВыбранныеПоляВКоллекцию(ПараметрыПеретаскивания.Значение, ИндексСтрокиДляВставки);
	
	Элементы.ВыбранныеПоля.ТекущаяСтрока = ИдентификаторСтроки;
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыПараметрыМакета

&НаКлиенте
Процедура ПараметрыМакетаПриАктивизацииСтроки(Элемент)
	
	Если ТекущееВыбранноеПоле <> Элементы.ПараметрыМакета.ТекущиеДанные Тогда
		
		ТекущееВыбранноеПоле = Элементы.ПараметрыМакета.ТекущиеДанные;
		ЗаполнитьВыделенныеОбластиМакетаПечатнойФормы(МакетПечатнойФормы, Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыКомпоновщикНастроекКДНастройкиОтбор

&НаКлиенте
Процедура КомпоновщикНастроекКДНастройкиОтборПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если Не ОтменаРедактирования Тогда
		Если Элементы.КомпоновщикНастроекКДНастройкиОтбор.ТекущаяСтрока <> Неопределено Тогда
			ТекущиеДанные = КомпоновщикНастроекКД.Настройки.Отбор.ПолучитьОбъектПоИдентификатору(
				Элементы.КомпоновщикНастроекКДНастройкиОтбор.ТекущаяСтрока);
			Если ТекущиеДанные <> Неопределено Тогда
				Родитель = ТекущиеДанные.Родитель;
				Если Родитель = Неопределено Тогда
					Родитель = ТекущиеДанные;
				КонецЕсли;
				Если ТипЗнч(Родитель) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
					УстановитьПредставлениеГруппыОтбора(Родитель);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомпоновщикНастроекКДНастройкиОтборПередНачаломИзменения(Элемент, Отказ)
	
	Если Элементы.КомпоновщикНастроекКДНастройкиОтбор.ТекущаяСтрока <> Неопределено Тогда
		ТекущиеДанные = КомпоновщикНастроекКД.Настройки.Отбор.ПолучитьОбъектПоИдентификатору(
			Элементы.КомпоновщикНастроекКДНастройкиОтбор.ТекущаяСтрока);
		Если ТекущиеДанные <> Неопределено
			И ТекущиеДанные.Родитель = Неопределено Тогда
			
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомпоновщикНастроекКДНастройкиОтборПередУдалением(Элемент, Отказ)
	
	Если Элементы.КомпоновщикНастроекКДНастройкиОтбор.ТекущаяСтрока <> Неопределено Тогда
		ТекущиеДанные = КомпоновщикНастроекКД.Настройки.Отбор.ПолучитьОбъектПоИдентификатору(
			Элементы.КомпоновщикНастроекКДНастройкиОтбор.ТекущаяСтрока);
		Если ТекущиеДанные <> Неопределено
			И ТекущиеДанные.Родитель = Неопределено Тогда
			
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомпоновщикНастроекКДНастройкиОтборПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = ПустаяСтрока(Элемент.ТекущаяСтрока);
КонецПроцедуры

&НаКлиенте
Процедура КомпоновщикНастроекКДНастройкиОтборЛевоеЗначениеПриИзменении(Элемент)
	ЭлементТаблица = ЭлементТаблицаФормы(Элемент);
	Если ЭлементТаблица <> Неопределено Тогда
		ТекущиеДанные = ЭлементТаблица.ТекущиеДанные;
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущиеДанные, "ПравоеЗначение") Тогда
			Если ТекущиеДанные.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПодразделениеОтбор") Тогда
				Если ПубликоватьСтруктуруЮридическихЛиц() Тогда
					Элементы.КомпоновщикНастроекКДНастройкиОтборПравоеЗначение.ОграничениеТипа =
						Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций");
				Иначе
					Элементы.КомпоновщикНастроекКДНастройкиОтборПравоеЗначение.ОграничениеТипа =
						Новый ОписаниеТипов("СправочникСсылка.СтруктураПредприятия");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомпоновщикНастроекКДНастройкиОтборПравоеЗначениеНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	Если ТипЗнч(Элемент) = Тип("ПолеФормы") Тогда
		ПараметрыВыбораПоля = ПараметрыВыбораПоляФормы(Элемент);
		Элемент.ПараметрыВыбора = ПараметрыВыбораПоля;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	Если Модифицированность Тогда
		
		ВосстановитьОформлениеМакета();
		СохранитьИзменения();
		Закрыть(ПараметрЗакрытияФормы());
		
	Иначе
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВернутьсяКМакетуПоУмолчанию(Команда)
	
	Если РежимРедактированияШаблонаДляКабинетаСотрудников Тогда
		ПараметрыОткрытия = Новый Структура();
		ПараметрыОткрытия.Вставить("РежимВыбора", Истина);
		ПараметрыОткрытия.Вставить("ШаблонДокумента", Параметры.ШаблонДокумента);
		
		Оповещение = Новый ОписаниеОповещения("ВернутьсяКШаблонуПоУмолчанию", ЭтотОбъект);
		ОткрытьФорму("Справочник.ШаблоныДокументов.Форма.ФормаСпискаВерсий", ПараметрыОткрытия, ЭтотОбъект, Истина
			, , ,Оповещение , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Иначе
		ВернутьсяКМакетуПоУмолчаниюНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВернутьсяКШаблонуПоУмолчанию(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ВерсияШаблонаДокумента = Результат.Версия;
		ВернутьсяКМакетуПоУмолчаниюНаСервере(Результат.АдресВХранилище);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПоле(Команда)
	
	Если Элементы.ДоступныеПоляВыбора.ТекущаяСтрока <> Неопределено Тогда
		ДобавитьПолеВыбора(Элементы.ДоступныеПоляВыбора.ТекущаяСтрока);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьВыборПоля(Команда)
	
	Если Элементы.ВыбранныеПоля.ВыделенныеСтроки.Количество() > 0 Тогда
		
		Пока Элементы.ВыбранныеПоля.ВыделенныеСтроки.Количество() > 0 Цикл
			
			ВыбранноеПоле = ВыбранныеПоля.НайтиПоИдентификатору(Элементы.ВыбранныеПоля.ВыделенныеСтроки[0]);
			ИмяПоля = ВыбранноеПоле.ИмяПоля;
			
			ВыбранныеПоля.Удалить(ВыбранноеПоле);
			
			СтрокиКоллекции = ПараметрыМакета.НайтиСтроки(Новый Структура("ИмяПоля,Выбрано", ИмяПоля, Истина));
			Если СтрокиКоллекции.Количество() > 0 Тогда
				СтрокиКоллекции[0].Выбрано = Ложь;
			КонецЕсли;
			
			Модифицированность = Истина;
			
		КонецЦикла;
		
		Элементы.ПараметрыМакета.ОтборСтрок = Новый ФиксированнаяСтруктура(Новый Структура("Выбрано", Ложь));
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КопироватьИдентификатор(Команда)
	
	Если Элементы.ВыбранныеПоля.ТекущаяСтрока = Неопределено Тогда
		ИдентификаторСтрокиВыбранногоПоля = Неопределено;
	Иначе
		ИдентификаторСтрокиВыбранногоПоля = Элементы.ВыбранныеПоля.ТекущаяСтрока;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьИдентификатор(Команда)
	
	Если ИдентификаторСтрокиВыбранногоПоля <> Неопределено Тогда
		
		ВыбранноеПоле = ВыбранныеПоля.НайтиПоИдентификатору(ИдентификаторСтрокиВыбранногоПоля);
		Если ВыбранноеПоле = Неопределено Тогда
			ИдентификаторСтрокиВыбранногоПоля = Неопределено;
		Иначе
			
			ИменаРеквизитов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ВыбранноеПоле.ИмяПоля);
			ВставитьПараметрыВОбласть(Элементы.МакетПечатнойФормы.ТекущаяОбласть, ИменаРеквизитов);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьДанныеДокумента(Команда)
	
	Если (СтрНачинаетсяС(Строка(Элементы.ДоступныеПоляВыбора.ТекущаяСтрока), НСтр("ru = 'ДанныеДокумента';
																					|en = 'ДанныеДокумента'"))
		Или СтрНачинаетсяС(Строка(Элементы.ДоступныеПоляВыбора.ТекущаяСтрока), НСтр("ru = 'ДанныеСотрудника';
																					|en = 'ДанныеСотрудника'"))) Тогда
		
		ТекущаяСтрока = КомпоновщикНастроекКД.Настройки.Выбор.ДоступныеПоляВыбора.ПолучитьОбъектПоИдентификатору(Элементы.ДоступныеПоляВыбора.ТекущаяСтрока);
		Пока ТекущаяСтрока.Родитель <> Неопределено Цикл
			ТекущаяСтрока = ТекущаяСтрока.Родитель;
		КонецЦикла;
		ТекущаяСтрока = ТекущаяСтрока.Поле;
	Иначе
		ТекущаяСтрока = Неопределено;
	КонецЕсли;
	ОткрытьРедакторРеквизита(Истина, ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПользовательскоеПоле(Команда)
	
	ОткрытьРедакторПользовательскогоПоля();
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьПользовательскоеПоле(Команда)
	
	ОткрытьРедакторПользовательскогоПоля(Элементы.ДоступныеПоляВыбора.ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура СкопироватьПользовательскоеПоле(Команда)
	
	ОткрытьРедакторПользовательскогоПоля(Элементы.ДоступныеПоляВыбора.ТекущаяСтрока, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗначениеПараметра(Команда)
	
	Если ЭтоДоступныйПараметрДанных Тогда
		
		ИмяПараметра = ИмяТекущегоПараметра(Строка(Элементы.ДоступныеПоляВыбора.ТекущаяСтрока));
		
		ПараметрНастроек = КомпоновщикНастроекКД.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных(ИмяПараметра));
		Если ПараметрНастроек <> Неопределено Тогда
			
			Если Не ЗначениеЗаполнено(ПараметрНастроек.ИдентификаторПользовательскойНастройки) Тогда
				ПараметрНастроек.ИдентификаторПользовательскойНастройки = Новый УникальныйИдентификатор;
			КонецЕсли;
			
			ПараметрПользовательскихНастроек = Неопределено;
			Для Каждого ЭлементНастройки Из КомпоновщикНастроекКД.ПользовательскиеНастройки.Элементы Цикл
				
				Если ТипЗнч(ЭлементНастройки) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных")
					И ЭлементНастройки.Параметр = ПараметрНастроек.Параметр Тогда
					
					ПараметрПользовательскихНастроек = ЭлементНастройки;
					Прервать;
					
				КонецЕсли;
				
			КонецЦикла;
			
			Если ПараметрПользовательскихНастроек <> Неопределено Тогда
				
				ДоступныйПараметр = КомпоновщикНастроекКД.Настройки.ПараметрыДанных.ДоступныеПараметры.НайтиПараметр(ПараметрНастроек.Параметр);
				
				ДополнительныеПараметры = Новый Структура;
				ДополнительныеПараметры.Вставить("ПараметрНастроек", ПараметрПользовательскихНастроек);
				
				Оповещение = Новый ОписаниеОповещения("УстановитьЗначениеПараметраЗавершение", ЭтотОбъект, ДополнительныеПараметры);
				ПоказатьВводЗначения(Оповещение, ПараметрНастроек.Значение, ДоступныйПараметр.Заголовок, ДоступныйПараметр.ТипЗначения);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредварительныйПросмотр(Команда)
	
	ВосстановитьОформлениеМакета();
	
	Если РежимРедактированияШаблонаДляКабинетаСотрудников Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Заголовок", Заголовок);
		ПараметрыФормы.Вставить("МакетДокумента", МакетПечатнойФормы);
		ПараметрыФормы.Вставить("АдресСхемы", АдресСхемы);
		ПараметрыФормы.Вставить("ЗакрыватьПриЗакрытииВладельца", Истина);
		
		Настройки = ПолучитьНастройки(КомпоновщикНастроекКД);
		УдалитьПустыеГруппыОтбораОбластей(Настройки);

		ПараметрыФормы.Вставить("НастройкиПечатнойФормы", Настройки);
		
		ОткрытьФорму("Справочник.ШаблоныДокументов.Форма.ВизуализаторДокумента", ПараметрыФормы, ЭтотОбъект, Истина);
	Иначе
		ЧастиПути = СтрРазделить(ПутьКВариантуОтчета, ".");
		КлючВарианта = ЧастиПути[ЧастиПути.Количество() - 1];
		
		ЧастиПути.Удалить(ЧастиПути.Количество() - 1);
		
		ВыбранныеПоляВНастройки(ЭтотОбъект);
		ПользовательскиеНастройки = КомпоновщикНастроекКД.ПользовательскиеНастройки;
		ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("МакетПечатнойФормы", МакетПечатнойФормы);
		ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("НастройкиПечатнойФормы", ПолучитьНастройки(КомпоновщикНастроекКД));
		ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ПользовательскиеНастройкиУстановлены", Истина);
		
		Если Не ЗначениеЗаполнено(ВариантОтчета) Тогда
			ВариантОтчета = ВариантОтчетов(СтрСоединить(ЧастиПути, "."), КлючВарианта);
		КонецЕсли;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ПользовательскиеНастройки", ПользовательскиеНастройки);
		
		ВариантыОтчетовКлиент.ОткрытьФормуОтчета(ЭтотОбъект, ВариантОтчета, ПараметрыФормы);
		
	КонецЕсли;
	
	ЗаполнитьВыделенныеОбластиМакетаПечатнойФормы(МакетПечатнойФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьПараметр(Команда)
	
	Если ОбщегоНазначенияКлиент.РежимОтладки()
		И РежимРедактированияШаблонаДляКабинетаСотрудников
		И ЭтоДанныеРеквизитаДокумента(Элементы.ДоступныеПоляВыбора.ТекущаяСтрока) Тогда
		
		УдалитьРеквизитДокументаНаСервере();
	Иначе
		УдалитьПараметрНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьШаблон(Команда)
	
	ВосстановитьОформлениеМакета();
	ОчиститьВыделениеПотерянныхОбластей(МакетПечатнойФормы);
	ВыгрузитьШаблонНаСервере().Показать(Заголовок + ".json");
	ЗаполнитьВыделенныеОбластиМакетаПечатнойФормы(МакетПечатнойФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьШаблон(Команда)
	
	Если Модифицированность Тогда
		
		ТекстВопроса = НСтр("ru = 'При загрузке шаблона документа из файла,
			|выполненные изменения будут потеряны.
			|Продолжить?';
			|en = 'При загрузке шаблона документа из файла,
			|выполненные изменения будут потеряны.
			|Продолжить?'");
		
		Оповещение = Новый ОписаниеОповещения("ЗагрузитьШаблонЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		
	Иначе
		ЗагрузитьШаблонЗавершение(КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПользовательскоеПодеВниз(Команда)
	
	ИзменитьПорядокПараметраНаСервере(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПользовательскоеПолеВверх(Команда)
	
	ИзменитьПорядокПараметраНаСервере(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбиратьИзПолныхСписковЗначений(Команда)
	
	Элементы.КомпоновщикНастроекКДНастройкиОтборВыбиратьИзПолныхСписковЗначений.Пометка =
		Не Элементы.КомпоновщикНастроекКДНастройкиОтборВыбиратьИзПолныхСписковЗначений.Пометка;
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьЗначениеПараметраЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		Если Не ДополнительныеПараметры.ПараметрНастроек.Использование
			Или ДополнительныеПараметры.ПараметрНастроек.Значение <> Результат Тогда
			
			ДополнительныеПараметры.ПараметрНастроек.Значение = Результат;
			ДополнительныеПараметры.ПараметрНастроек.Использование = Истина;
			
			Модифицированность = Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ЭтоДанныеРеквизитаДокумента(ТекущийЭлемент)
	Возврат СтрНачинаетсяС(Строка(ТекущийЭлемент), НСтр("ru = 'ДанныеДокумента';
														|en = 'ДанныеДокумента'"))
		Или СтрНачинаетсяС(Строка(ТекущийЭлемент), НСтр("ru = 'ДанныеСотрудника';
														|en = 'ДанныеСотрудника'"));
КонецФункции

&НаКлиенте
Процедура ОткрытьРедакторПользовательскогоПоля(ТекущийЭлемент = Неопределено, Скопировать = Ложь)
	
	Если РежимРедактированияШаблонаДляКабинетаСотрудников Тогда
		ОткрытьРедакторРеквизита(ЭтоДанныеРеквизитаДокумента(ТекущийЭлемент), ТекущийЭлемент);
	Иначе
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("КомпоновщикНастроекКД", КомпоновщикНастроекКД);
		
		ДополнительныеПараметры = Новый Структура;
		
		Если ТекущийЭлемент <> Неопределено Тогда
			
			ПараметрыОткрытия.Вставить("ТекущийЭлемент", ТекущийЭлемент);
			
			Если Скопировать Тогда
				ПараметрыОткрытия.Вставить("Скопировать", Истина);
			Иначе
				ДополнительныеПараметры.Вставить("ТекущийЭлемент", ТекущийЭлемент);
			КонецЕсли;
			
		КонецЕсли;
		
		Оповещение = Новый ОписаниеОповещения("ОбработкаРезультатаРедактированияПользовательскогоПоля", ЭтотОбъект, ДополнительныеПараметры);
		ОткрытьФорму("Обработка.НастраиваемыеПечатныеФормыЗарплатаКадры.Форма.ФормаПользовательскогоПоля", ПараметрыОткрытия, ЭтотОбъект,
			Истина, , , Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьРедакторРеквизита(ДанныеРеквизитаДокумента, ТекущийЭлемент = Неопределено)
	
	ПараметрыОткрытия = ПараметрыОткрытияРедактораПараметра(ДанныеРеквизитаДокумента, АдресСхемы, ТекущийЭлемент);
	Если ДанныеРеквизитаДокумента Тогда
		Оповещение = Новый ОписаниеОповещения("ОбработкаРезультатаРедактированияРеквизита", ЭтотОбъект);
	Иначе
		Оповещение = Новый ОписаниеОповещения("ОбработкаРезультатаРедактированияПараметра", ЭтотОбъект);
	КонецЕсли;
	ОткрытьФорму("Обработка.НастраиваемыеПечатныеФормыЗарплатаКадры.Форма.ФормаПараметра", ПараметрыОткрытия, ЭтотОбъект,
		Истина, , , Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПараметрыОткрытияРедактораПараметра(ДанныеРеквизитаДокумента, АдресСхемы, ТекущийРеквизит)
	
	Если ТекущийРеквизит = Неопределено Тогда
		ИмяПараметра = "";
		Если ДанныеРеквизитаДокумента Тогда
			ИмяПараметра = "ДанныеДокумента";
		КонецЕсли;
	Иначе
		Если ДанныеРеквизитаДокумента Тогда
			ИмяПараметра = Строка(ТекущийРеквизит);
		Иначе
			ИмяПараметра = СтрЗаменить(ТекущийРеквизит, "DataParameters.", "");
		КонецЕсли;
	КонецЕсли;
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Имя", ИмяПараметра);
	ПараметрыОткрытия.Вставить("ДанныеРеквизитаДокумента", ДанныеРеквизитаДокумента);
	ПараметрыОткрытия.Вставить("ТекущийРеквизит", Строка(ТекущийРеквизит));
	Если Не ПустаяСтрока(ИмяПараметра) Тогда
		Схема = ПолучитьИзВременногоХранилища(АдресСхемы);
		Если ДанныеРеквизитаДокумента Тогда
			Реквизит = Схема.НаборыДанных.ДанныеСервиса.Поля.Найти(Строка(ТекущийРеквизит));
			Если Реквизит <> Неопределено Тогда
				Если Не ПустаяСтрока(Реквизит.Заголовок) Тогда
					ПараметрыОткрытия.Вставить("Заголовок", Реквизит.Заголовок);
				Иначе
					Если СтрНачинаетсяС(ИмяПараметра, НСтр("ru = 'ДанныеДокумента';
															|en = 'ДанныеДокумента'")) Тогда
						ТолькоИмяПараметра = СтрЗаменить(ИмяПараметра, "ДанныеДокумента.", "");
					Иначе
						ТолькоИмяПараметра = СтрЗаменить(ИмяПараметра, "ДанныеСотрудника.", "");
					КонецЕсли;
					ПараметрыОткрытия.Вставить("Заголовок", ЗаголовокИзИдентификатора(ТолькоИмяПараметра));
				КонецЕсли;
				ПараметрыОткрытия.Вставить("ТипЗначения", Реквизит.ТипЗначения);
				ДоступныеЗначения = Реквизит.ПолучитьДоступныеЗначения();
				Если ЗначениеЗаполнено(ДоступныеЗначения) Тогда
					ПараметрыОткрытия.Вставить("ДоступныеЗначения", ДоступныеЗначения);
				КонецЕсли;
			Иначе
				ПараметрыОткрытия.Имя = ПараметрыОткрытия.Имя + ".";
			КонецЕсли;
		Иначе
			Параметр = Схема.Параметры.Найти(ИмяПараметра);
			Если Параметр <> Неопределено Тогда
				ПараметрыОткрытия.Вставить("Заголовок", Параметр.Заголовок);
				ПараметрыОткрытия.Вставить("ТипЗначения", Параметр.ТипЗначения);
				ДоступныеЗначения = Параметр.ПолучитьДоступныеЗначения();
				Если ЗначениеЗаполнено(ДоступныеЗначения) Тогда
					ПараметрыОткрытия.Вставить("ДоступныеЗначения", ДоступныеЗначения);
				КонецЕсли;
				ПараметрыОткрытия.Вставить("Значение", Параметр.Значение);
				ПараметрыОткрытия.Вставить("Обязательный", Параметр.ЗапрещатьНезаполненныеЗначения);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Возврат ПараметрыОткрытия;
	
КонецФункции

&НаКлиенте
Процедура ОбработкаРезультатаРедактированияПользовательскогоПоля(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		Если ДополнительныеПараметры.Свойство("ТекущийЭлемент") Тогда
			
			ВставитьВыбранноеПоле = Ложь;
			ПользовательскоеПоле = КомпоновщикНастроекКД.Настройки.ПользовательскиеПоля.ПолучитьОбъектПоИдентификатору(ДополнительныеПараметры.ТекущийЭлемент);
			
		Иначе
			
			ВставитьВыбранноеПоле = Истина;
			ПользовательскоеПоле = КомпоновщикНастроекКД.Настройки.ПользовательскиеПоля.Элементы.Добавить(Тип("ПользовательскоеПолеВыражениеКомпоновкиДанных"));
			
		КонецЕсли;
		
		ПользовательскоеПоле.Заголовок = Результат.Заголовок;
		
		ПользовательскоеПоле.УстановитьВыражениеДетальныхЗаписей(Результат.ВыражениеДетальныхЗаписей);
		
		Если ЗначениеЗаполнено(Результат.ВыражениеИтоговыхЗаписей) Тогда
			ПользовательскоеПоле.УстановитьВыражениеИтоговыхЗаписей(Результат.ВыражениеИтоговыхЗаписей);
		КонецЕсли;
		
		Если ВставитьВыбранноеПоле Тогда
			
			Элементы.ВыбранныеПоля.ТекущаяСтрока = ВставитьВыбранноеПолеВКоллекцию(
				ВыбранныеПоля, КомпоновщикНастроекКД, Новый ПолеКомпоновкиДанных(ПользовательскоеПоле.ПутьКДанным), ПараметрыМакета);
			
		КонецЕсли;
		
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаРезультатаРедактированияПараметра(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ДобавитьПараметрВСхему(Результат);
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаРезультатаРедактированияРеквизита(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ДобавитьРеквизитВСхему(Результат);
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтображениеПолейРедактора(УстановитьМакетПоУмолчанию = Ложь, АдресШаблонаНаСервере = Неопределено)
	
	ИдентификаторСтрокиВыбранногоПоля = Неопределено;
	Менеджер = Неопределено;
	ИмяПредопределенного = "";
	ШаблонДокументаПоУмолчанию = Неопределено;
	
	ИмяОтчета = "";
	КлючВарианта = "";
	Если ЗначениеЗаполнено(ПутьКВариантуОтчета) Тогда
		ИмяОтчета = ЗарплатаКадрыОтчеты.ИмяОтчетаИзПутиКВарианту(ПутьКВариантуОтчета);
		КлючВарианта = ЗарплатаКадрыОтчеты.КлючВариантаИзПутиКВарианту(ПутьКВариантуОтчета);
	ИначеЕсли ЗначениеЗаполнено(ВариантОтчета) Тогда
		РеквизитыВариантаОтчета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВариантОтчета, "Отчет,КлючВарианта");
		ИмяОтчета = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеквизитыВариантаОтчета.Отчет, "ПолноеИмя");
		КлючВарианта = РеквизитыВариантаОтчета.КлючВарианта;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПутьКМакетуПечатнойФормы)
		И (УстановитьМакетПоУмолчанию Или МакетПечатнойФормы.ВысотаТаблицы = 0) Тогда
		
		// Установка макета печатной формы и настроек СКД
		Если ЗначениеЗаполнено(ИмяПредопределенныхДанных) Тогда
			ИмяПредопределенного = ИмяПредопределенногоЭлемента(ИмяПредопределенныхДанных);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(АдресШаблонаНаСервере) Тогда
			ШаблонДокументаПоУмолчанию = Справочники.ШаблоныДокументов.ШаблонДокументаИзХранилища(
				АдресШаблонаНаСервере, "ПоУмолчанию");
			МакетПечатнойФормы = ШаблонДокументаПоУмолчанию.Макет;
		ИначеЕсли ЗначениеЗаполнено(ИмяПредопределенного) Тогда
			Менеджер = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ИмяПредопределенныхДанных);
			ШаблонДокументаПоУмолчанию = Менеджер.ШаблонДокументаПоУмолчанию(ИмяПредопределенного, КлючВарианта);
			МакетПечатнойФормы = ШаблонДокументаПоУмолчанию.Макет;
		Иначе
			МакетПечатнойФормы = УправлениеПечатью.МакетПечатнойФормы(ПутьКМакетуПечатнойФормы);
		КонецЕсли;
		
	КонецЕсли;
	
	ОбъектОтчета = ОбщегоНазначения.ОбъектПоПолномуИмени(ИмяОтчета);
	
	ОбъектОтчета.КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("СообщатьОРасхожденияхВПутяхКДанным", Истина);
	ОбъектОтчета.ИнициализироватьОтчет();
	ОбъектОтчета.КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Удалить("СообщатьОРасхожденияхВПутяхКДанным");
	
	Если РежимРедактированияШаблонаДляКабинетаСотрудников Тогда
		
		ОбновитьИзСхемыПоУмолчанию = Ложь;
		Если УстановитьМакетПоУмолчанию Тогда
			Если ШаблонДокументаПоУмолчанию <> Неопределено Тогда
				НастройкиПечатнойФормы = ШаблонДокументаПоУмолчанию.Настройки
			Иначе
				Если ЗначениеЗаполнено(ИмяПредопределенного) Тогда
					НастройкиПечатнойФормы = Менеджер.НастройкиВариантаОтчетаПоУмолчанию(КлючВарианта, ИмяПредопределенного);
				Иначе
					НастройкиПечатнойФормы = ЗарплатаКадрыОтчеты.НастройкиШаблонаДляКабинетаСотрудников(КлючВарианта, Истина);
				КонецЕсли;
			КонецЕсли;
		Иначе
			НастройкиПечатнойФормы = ЗарплатаКадрыОтчеты.НастройкиШаблонаДляКабинетаСотрудников(КлючВарианта);
			ОбновитьИзСхемыПоУмолчанию = Истина;
		КонецЕсли;
		
		Справочники.ШаблоныДокументов.ДобавитьРасширенияПолейСхемыКомпоновкиДанных(
			НастройкиПечатнойФормы.СхемаКомпоновкиДанных, ОбновитьИзСхемыПоУмолчанию);
		АдресСхемы = ПоместитьВоВременноеХранилище(НастройкиПечатнойФормы.СхемаКомпоновкиДанных, УникальныйИдентификатор);
		ЗарплатаКадрыОтчеты.ИнициализироватьИЗагрузитьНастройкиВКомпоновщикКД(КомпоновщикНастроекКД, НастройкиПечатнойФормы)
	Иначе
		ЗарплатаКадрыОтчеты.ЗагрузитьНастройкиВКомпоновщикКД(КомпоновщикНастроекКД, ОбъектОтчета, КлючВарианта);
	КонецЕсли;
	
	ЗаполнитьВыбранныеПоля();
	
	Элементы.ПараметрыМакета.ОтборСтрок = Новый ФиксированнаяСтруктура(Новый Структура("Выбрано", Ложь));
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ИмяПредопределенногоЭлемента(ИмяПредопределенныхДанных)
	Если ЗначениеЗаполнено(ИмяПредопределенныхДанных) Тогда
		СловаИмени = СтрРазделить(ИмяПредопределенныхДанных, ".");
		Возврат СловаИмени[СловаИмени.Количество() - 1];
	КонецЕсли;
	Возврат "";
КонецФункции

&НаКлиенте
Процедура ЗадатьВопросОНеобходимостиСохранения(ДополнительныеПараметры)
	
	Оповещение = Новый ОписаниеОповещения("ОбработкаНеобходимостиСохранитьИзмененияНастроекПечатнойФормы", ЭтотОбъект, ДополнительныеПараметры);
	
	ТекстВопроса = НСтр("ru = 'Настройка печатной формы была изменена.
		|Сохранить изменения?';
		|en = 'The print form setting is changed.
		|Save the changes?'");
	
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНеобходимостиСохранитьИзмененияНастроекПечатнойФормы(Результат, ДополнительныеПараметры) Экспорт
	
	ВосстановитьОформлениеМакета();
	
	Попытка
		
		Если Результат = КодВозвратаДиалога.Да Тогда
			СохранитьИзменения();
			Если ДополнительныеПараметры.Свойство("ЗакрытьФорму") Тогда
				Закрыть(ПараметрЗакрытияФормы());
				Возврат;
			КонецЕсли;
		ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
			Если ДополнительныеПараметры.Свойство("ЗакрытьФорму") Тогда
				Модифицированность = Ложь;
				Закрыть();
				Возврат;
			КонецЕсли;
		КонецЕсли;
	
	Исключение
		ВызватьИсключение ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьИзменения()
	
	УстановитьПривилегированныйРежим(Истина);
	
	УдалитьПустыеГруппыОтбораОбластей(КомпоновщикНастроекКД.Настройки);
	ВыбранныеПоляВНастройки(ЭтотОбъект);
	
	КомпоновщикНастроекКД.ПользовательскиеНастройки.ДополнительныеСвойства.Очистить();
	
	НастройкиСхемы = ЗарплатаКадрыОтчеты.НоваяСтруктураНастроекПечатнойФормы();
	НастройкиСхемы.Настройки = КомпоновщикНастроекКД.Настройки;
	НастройкиСхемы.ПользовательскиеНастройки = КомпоновщикНастроекКД.ПользовательскиеНастройки;
	Если РежимРедактированияШаблонаДляКабинетаСотрудников Тогда
		НастройкиСхемы.СхемаКомпоновкиДанных = ПолучитьИзВременногоХранилища(АдресСхемы);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВариантОтчета) Тогда
		КлючВарианта = ЗарплатаКадрыОтчеты.КлючВариантаИзПутиКВарианту(ПутьКВариантуОтчета);
		ПолноеИмяОбъектаМетаданныхОтчета = ЗарплатаКадрыОтчеты.ИмяОтчетаИзПутиКВарианту(ПутьКВариантуОтчета);
		ВариантОтчета = ЗарплатаКадрыОтчеты.ВариантОтчетаПечатнойФормыПоИмениОбъекта(ПолноеИмяОбъектаМетаданныхОтчета, КлючВарианта);
	КонецЕсли;
	
	ЗарплатаКадрыОтчеты.ЗапомнитьНастройкиВариантаОтчета(ВариантОтчета, НастройкиСхемы);
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Не РежимРедактированияШаблонаДляКабинетаСотрудников Тогда
		// Сохранение настоек макета печатной формы
		ОписаниеМакета = УправлениеПечатью.ОписаниеМакета();
		ОписаниеМакета.ИмяОбъектаМетаданныхМакета = ПутьКМакетуПечатнойФормы;
		ОписаниеМакета.АдресМакетаВоВременномХранилище = ПоместитьВоВременноеХранилище(МакетПечатнойФормы);
		ОписаниеМакета.ИсточникиДанных = Новый Массив;
		УправлениеПечатью.ЗаписатьМакет(ОписаниеМакета);
	КонецЕсли;
	
	ЗарплатаКадрыОтчеты.ОчиститьМакетОтчетаПоСсылке(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВариантОтчета, "Отчет"), КлючВарианта);
	
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаСервере
Функция ПараметрЗакрытияФормы()
	
	ПараметрЗакрытия = Новый Структура;
	ПараметрЗакрытия.Вставить("Результат", Истина);
	ПараметрЗакрытия.Вставить("МакетПечатнойФормы", МакетПечатнойФормы);	
	ПараметрЗакрытия.Вставить("ВыбранныеПоля", ПараметрВыбранныеПоля());
	Если РежимРедактированияШаблонаДляКабинетаСотрудников Тогда
		ПараметрЗакрытия.Вставить("ВерсияШаблонаДокумента", ВерсияШаблонаДокумента);
	КонецЕсли;
	
	Возврат ПараметрЗакрытия;
	
КонецФункции

&НаСервере
Функция ПараметрВыбранныеПоля()
	
	ПараметрВыбранныеПоля = Новый Массив;
	Для Каждого ВыбранноеПоле Из ВыбранныеПоля Цикл
		ПараметрВыбранноеПоле = Новый Структура;
		ПараметрВыбранноеПоле.Вставить("ПользовательскоеПоле",(СтрНайти(ВыбранноеПоле.ПутьКДанным, "ПользовательскиеПоля") <> 0));
		ПараметрВыбранноеПоле.Вставить("ПутьКДанным", ВыбранноеПоле.ПутьКДанным);
		ПараметрВыбранноеПоле.Вставить("Имя", ВыбранноеПоле.ИмяПоля);
		ПараметрВыбранныеПоля.Добавить(ПараметрВыбранноеПоле);
	КонецЦикла;
	
	Возврат ПараметрВыбранныеПоля;
	
КонецФункции

#Область РаботаСПредставлениемМакетаПечатнойФормы

&НаКлиенте
Процедура ПриИзмененииСодержимогоОбласти(Область)
	
	ДобавитьПараметрыОбласти(ИдентификаторыМакетаПечатнойФормы, Область, Истина);
	ЗаполнитьВыделенныеОбластиМакетаПечатнойФормы(МакетПечатнойФормы);
	
	ПараметрыМакета.Сортировать("ИмяПоля");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьВыделенныеОбластиМакетаПечатнойФормы(МакетПечатнойФормы, ВыбранноеПоле = Истина)
	
	ВосстановитьОформлениеМакета();
	
	Если Не ЗначениеЗаполнено(ИдентификаторыМакетаПечатнойФормы) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущееВыбранноеПоле = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяПоля = ТекущееВыбранноеПоле.ИмяПоля;
	Если ВыбранноеПоле Тогда
		
		ЦветРамки = WebЦвета.Зеленый;
		ЦветФона = WebЦвета.СветлоЗеленый;
		
	Иначе
		
		ЦветРамки = WebЦвета.ТемноОранжевый;
		ЦветФона = WebЦвета.Оранжевый;
		
	КонецЕсли;
	
	ИдентификаторыОбластей = Новый Соответствие;
	
	Если Не ПустаяСтрока(ИмяПоля) Тогда
		
		СтрокиПараметра = ИдентификаторыМакетаПечатнойФормы.Получить(ИмяПоля);
		Если СтрокиПараметра <> Неопределено Тогда
			
			Для Каждого ОписаниеСтроки Из СтрокиПараметра Цикл
				
				НомерСтроки = ОписаниеСтроки.Ключ;
				Для Каждого ОписаниеКолонки Из ОписаниеСтроки.Значение Цикл
					
					НомерКолонки = ОписаниеКолонки.Ключ;
					Область = МакетПечатнойФормы.Область(НомерСтроки, НомерКолонки);
					
					// Запоминание настроек исходного макета
					НастройкиОбласти = Новый Структура;
					НастройкиОбласти.Вставить("Узор", Область.Узор);
					НастройкиОбласти.Вставить("ЦветУзора", Область.ЦветУзора);
					НастройкиОбласти.Вставить("ГраницаСверху", Область.ГраницаСверху);
					НастройкиОбласти.Вставить("ГраницаСлева", Область.ГраницаСлева);
					НастройкиОбласти.Вставить("ГраницаСнизу", Область.ГраницаСнизу);
					НастройкиОбласти.Вставить("ГраницаСправа", Область.ГраницаСправа);
					НастройкиОбласти.Вставить("ЦветРамки", Область.ЦветРамки);
					
					ИдентификаторыОбластей.Вставить(Область, НастройкиОбласти);
					
					// Выделение ячейки, содержащей выбранный параметр
					Область.Узор = ТипУзораТабличногоДокумента.Узор14;
					Область.ЦветУзора = ЦветФона;
					
					ЛинияВыделения = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная, 2);
					Область.ГраницаСверху = ЛинияВыделения;
					Область.ГраницаСлева = ЛинияВыделения;
					Область.ГраницаСнизу = ЛинияВыделения;
					Область.ГраницаСправа = ЛинияВыделения;
					
					Область.ЦветРамки = ЦветРамки;
					
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОчиститьВыделениеПотерянныхОбластей(МакетПечатнойФормы)
	
	Для НомерСтроки = 1 По МакетПечатнойФормы.ВысотаТаблицы Цикл
		Для НомерКолонки = 1 По МакетПечатнойФормы.ШиринаТаблицы Цикл
			Область = МакетПечатнойФормы.Область(НомерСтроки, НомерКолонки);
			Если Область.Узор = ТипУзораТабличногоДокумента.Узор14
				И (Область.ЦветУзора = WebЦвета.СветлоЗеленый
					Или Область.ЦветУзора = WebЦвета.Оранжевый)
				И (Область.ЦветРамки = WebЦвета.Зеленый
					Или Область.ЦветРамки = WebЦвета.ТемноОранжевый) Тогда
					
				Область.Узор = Неопределено;
				Область.ЦветУзора = Новый Цвет();
				Область.ЦветРамки = Новый Цвет();
				
				ОбластьСлева = Неопределено;
				Если НомерКолонки > 1 Тогда
					ОбластьСлева = МакетПечатнойФормы.Область(НомерСтроки, НомерКолонки - 1);
				КонецЕсли;
				ОбластьСправа = Неопределено;
				Если НомерКолонки < МакетПечатнойФормы.ШиринаТаблицы Тогда
					ОбластьСправа = МакетПечатнойФормы.Область(НомерСтроки, НомерКолонки + 1);
					Если ОбластьСправа.Право = МакетПечатнойФормы.ШиринаТаблицы Тогда
						ОбластьСправа = Неопределено;
					КонецЕсли;
				КонецЕсли;
				ОбластьСверху = Неопределено;
				Если НомерСтроки > 1 Тогда
					ОбластьСверху = МакетПечатнойФормы.Область(НомерСтроки - 1, НомерКолонки);
				КонецЕсли;
				ОбластьСнизу = Неопределено;
				Если НомерСтроки < МакетПечатнойФормы.ВысотаТаблицы Тогда
					ОбластьСнизу = МакетПечатнойФормы.Область(НомерСтроки + 1, НомерКолонки);
				КонецЕсли;
				Если ОбластьСлева = Неопределено И ОбластьСправа = Неопределено Тогда
					Область.ГраницаСверху = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.НетЛинии);
					Область.ГраницаСнизу = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.НетЛинии);
				Иначе
					Область.ГраницаСверху = ?(ОбластьСлева = Неопределено, ОбластьСправа.ГраницаСверху, ОбластьСлева.ГраницаСверху);
					Область.ГраницаСнизу = ?(ОбластьСлева = Неопределено, ОбластьСправа.ГраницаСнизу, ОбластьСлева.ГраницаСнизу);
				КонецЕсли;
				
				Если ОбластьСверху = Неопределено И ОбластьСнизу = Неопределено Тогда
					Область.ГраницаСлева = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.НетЛинии);
					Область.ГраницаСправа = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.НетЛинии);
				Иначе
					Область.ГраницаСлева = ?(ОбластьСверху = Неопределено, ОбластьСнизу.ГраницаСлева, ОбластьСверху.ГраницаСлева);
					Область.ГраницаСправа = ?(ОбластьСверху = Неопределено, ОбластьСнизу.ГраницаСправа, ОбластьСверху.ГраницаСправа);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВосстановитьОформлениеМакета()
	
	Если ИдентификаторыОбластей <> Неопределено Тогда
		
		Для Каждого ОписаниеОформления Из ИдентификаторыОбластей Цикл
			
			Область = ОписаниеОформления.Ключ;
			ЗаполнитьЗначенияСвойств(Область, ОписаниеОформления.Значение);
			
		КонецЦикла;
		
	КонецЕсли;
	
	ИдентификаторыОбластей = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьПараметрыВОбласть(ОбластьЯчеек, ИменаРеквизитов)
	
	Если ОбластьЯчеек.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Текст Тогда
		ОбластьЯчеек.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Шаблон;
	ИначеЕсли ОбластьЯчеек.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Параметр Тогда
		ТекстОбласти = "[" + ОбластьЯчеек.Параметр + "]";
		ОбластьЯчеек.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Шаблон;
		ОбластьЯчеек.Текст = ТекстОбласти;
	КонецЕсли;
	
	Для Каждого ИмяРеквизита Из ИменаРеквизитов Цикл
		ОбластьЯчеек.Текст = ОбластьЯчеек.Текст + " [" + ИмяРеквизита + "]";
	КонецЦикла;
	
	Модифицированность = Истина;
	
	ПриИзмененииСодержимогоОбласти(ОбластьЯчеек);
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСИдентификаторамиМакетаПечатнойФормы

&НаКлиенте
Процедура ЗапомнитьИдентификаторыМакетаПечатнойФормы()
	
	ИдентификаторыМакетаПечатнойФормы = Новый Соответствие;
	Для НомерСтроки = 1 По МакетПечатнойФормы.ВысотаТаблицы Цикл
		
		Для НомерКолонки = 1 По МакетПечатнойФормы.ШиринаТаблицы Цикл
			
			ОбластьЯчейки = МакетПечатнойФормы.Область(НомерСтроки, НомерКолонки);
			Если ОбластьЯчейки.Верх < НомерСтроки Или ОбластьЯчейки.Лево < НомерКолонки Тогда
				Продолжить;
			КонецЕсли;
			
			ДобавитьПараметрыОбласти(ИдентификаторыМакетаПечатнойФормы, ОбластьЯчейки, Ложь);
			
		КонецЦикла;
		
	КонецЦикла;
	
	ПараметрыМакета.Сортировать("ИмяПоля");
	ЗаполнитьОбластиДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПараметрыОбласти(ИдентификаторыМакета, ОбластьЯчеек, ОчищатьПредыдущие)
	
	Если ОчищатьПредыдущие Тогда
		УдалитьУпоминаниеОбласти(ИдентификаторыМакета, ОбластьЯчеек);
	КонецЕсли;
	
	Если ОбластьЯчеек.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Параметр Тогда
		ДобавитьПараметрМакета(ИдентификаторыМакета, ОбластьЯчеек.Параметр, ОбластьЯчеек.Верх, ОбластьЯчеек.Лево);
	ИначеЕсли ОбластьЯчеек.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Шаблон Тогда
		ДобавитьПараметрыШаблона(ИдентификаторыМакета, ОбластьЯчеек.Текст, ОбластьЯчеек.Верх, ОбластьЯчеек.Лево);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьУпоминаниеОбласти(ИдентификаторыМакета, ОбластьЯчеек)
	
	Для Каждого ОписаниеАдресов Из ИдентификаторыМакета Цикл
		
		КолонкиСтроки = ОписаниеАдресов.Значение.Получить(ОбластьЯчеек.Верх);
		Если КолонкиСтроки <> Неопределено Тогда
			
			ОписаниеКолонки = КолонкиСтроки.Получить(ОбластьЯчеек.Лево);
			Если ОписаниеКолонки <> Неопределено Тогда
				
				КолонкиСтроки.Удалить(ОбластьЯчеек.Лево);
				Если КолонкиСтроки.Количество() = 0 Тогда
					ОписаниеАдресов.Значение.Удалить(ОбластьЯчеек.Верх);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если ОписаниеАдресов.Значение.Количество() = 0 Тогда
			
			СтрокиКоллекции = ПараметрыМакета.НайтиСтроки(Новый Структура("ИмяПоля", ОписаниеАдресов.Ключ));
			Если СтрокиКоллекции.Количество() > 0 Тогда
				ПараметрыМакета.Удалить(СтрокиКоллекции[0]);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПараметрМакета(ИдентификаторыМакета, ИмяПараметра, НомерСтроки, НомерКолонки)
	
	СтрокиКоллекции = ПараметрыМакета.НайтиСтроки(Новый Структура("ИмяПоля", ИмяПараметра));
	Если СтрокиКоллекции.Количество() = 0 Тогда
		
		НоваяСтрока = ПараметрыМакета.Добавить();
		НоваяСтрока.ИмяПоля = ИмяПараметра;
		НоваяСтрока.Выбрано = ВыбранныеПоля.НайтиСтроки(Новый Структура("ИмяПоля", НоваяСтрока.ИмяПоля)).Количество() > 0;
		
	КонецЕсли;
	
	АдресаПараметра = ИдентификаторыМакета.Получить(ИмяПараметра);
	Если АдресаПараметра = Неопределено Тогда
		АдресаПараметра = Новый Соответствие;
		ИдентификаторыМакета.Вставить(ИмяПараметра, АдресаПараметра);
	КонецЕсли;
	
	КолонкиСтроки = АдресаПараметра.Получить(НомерСтроки);
	Если КолонкиСтроки = Неопределено Тогда
		КолонкиСтроки = Новый Соответствие;
		АдресаПараметра.Вставить(НомерСтроки, КолонкиСтроки);
	КонецЕсли;
	
	КолонкиСтроки.Вставить(НомерКолонки, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПараметрыШаблона(ИдентификаторыМакета, ТекстШаблона, НомерСтроки, НомерКолонки)
	
	КоличествоОткрывающихСкобок = СтрЧислоВхождений(ТекстШаблона, "[");
	Для НомерВхождения =1 По КоличествоОткрывающихСкобок Цикл
		
		ПозицияОткрывающейСкобки = СтрНайти(ТекстШаблона, "[", , , НомерВхождения);
		ПозицияЗакрывающейСкобки = СтрНайти(ТекстШаблона, "]", , ПозицияОткрывающейСкобки);
		
		Если ПозицияЗакрывающейСкобки <> 0 Тогда
			
			ИмяПараметра = Сред(ТекстШаблона, ПозицияОткрывающейСкобки + 1, ПозицияЗакрывающейСкобки - ПозицияОткрывающейСкобки - 1);
			ДобавитьПараметрМакета(ИдентификаторыМакета, ИмяПараметра, НомерСтроки, НомерКолонки);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьПользовательскийМакет()
	
	Если ЗначениеЗаполнено(ПутьКВариантуОтчета) Тогда
		УправлениеПечатью.УдалитьМакет(ПутьКМакетуПечатнойФормы);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВыбранныеПоля()
	
	ВыбранныеПоля.Очистить();
	
	ЗагруженыПользовательскиеНастройки = Ложь;
	Для Каждого ЭлементКоллекции Из КомпоновщикНастроекКД.ПользовательскиеНастройки.Элементы Цикл
		
		Если ТипЗнч(ЭлементКоллекции) = Тип("ВыбранныеПоляКомпоновкиДанных") Тогда
			
			ЗагруженыПользовательскиеНастройки = Истина;
			Для Каждого Элемент Из ЭлементКоллекции.Элементы Цикл
				
				ВставитьВыбранноеПолеВКоллекцию(
					ВыбранныеПоля, КомпоновщикНастроекКД, Элемент.Поле, ПараметрыМакета, , Элемент.Использование);
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если Не ЗагруженыПользовательскиеНастройки Тогда
		
		Для Каждого Элемент Из КомпоновщикНастроекКД.Настройки.Выбор.Элементы Цикл
			
			ВставитьВыбранноеПолеВКоллекцию(
				ВыбранныеПоля, КомпоновщикНастроекКД, Элемент.Поле, ПараметрыМакета, , Элемент.Использование);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ДобавитьПолеВыбора(ВыбраннаяСтрока)
	
	ОбъектКомпоновки = КомпоновщикНастроекКД.Настройки.ДоступныеПоляВыбора.ПолучитьОбъектПоИдентификатору(ВыбраннаяСтрока);
	Если ОбъектКомпоновки <> Неопределено Тогда
		Если ОбъектКомпоновки.Папка Тогда
			ИдентификаторСтроки = ВставитьВыбранныеПоляВКоллекцию(ОбъектКомпоновки.Элементы);
		Иначе
			
			ИдентификаторСтроки = ВставитьВыбранноеПолеВКоллекцию(
				ВыбранныеПоля, КомпоновщикНастроекКД, ОбъектКомпоновки.Поле, ПараметрыМакета);
			
		КонецЕсли;
		
		Элементы.ВыбранныеПоля.ТекущаяСтрока = ИдентификаторСтроки;
		
		Модифицированность = Истина;
		
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь
	
КонецФункции

&НаКлиенте
Функция ВставитьВыбранныеПоляВКоллекцию(ВставляемыеПоля, ИндексСтрокиДляВставки = Неопределено)
	
	Модифицированность = Истина;
	
	Для Каждого ВставляемыйЭлемент Из ВставляемыеПоля Цикл
		
		Если ВставляемыйЭлемент.Папка Тогда
			ИдентификаторСтроки = ВставитьВыбранныеПоляВКоллекцию(ВставляемыйЭлемент.Элементы, ИндексСтрокиДляВставки);
		Иначе
			ИдентификаторСтроки = ВставитьВыбранноеПолеВКоллекцию(
				ВыбранныеПоля, КомпоновщикНастроекКД, ВставляемыйЭлемент.Поле, ПараметрыМакета, ИндексСтрокиДляВставки);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ИдентификаторСтроки;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ВставитьВыбранноеПолеВКоллекцию(КоллекцияВыбранныхПолей, КомпоновщикНастроекКД, Поле, ПараметрыМакета, ИндексСтрокиДляВставки = Неопределено, Использование = Истина)
	
	ИдентификаторПоля = 0;
	
	ДоступныеПоляВыбора = КомпоновщикНастроекКД.Настройки.ДоступныеПоляВыбора;
	ДоступноеПоле = ДоступныеПоляВыбора.НайтиПоле(Поле);
	
	Если ДоступноеПоле <> Неопределено Тогда
		
		Если ИндексСтрокиДляВставки = Неопределено
			Или ИндексСтрокиДляВставки < 0 Тогда
			
			НоваяСтрока = КоллекцияВыбранныхПолей.Добавить();
			
		Иначе
			НоваяСтрока = КоллекцияВыбранныхПолей.Вставить(ИндексСтрокиДляВставки);
			ИндексСтрокиДляВставки = ИндексСтрокиДляВставки + 1;
		КонецЕсли;
		
		НоваяСтрока.Использование = Использование;
		НоваяСтрока.ПутьКДанным = Поле;
		
		НоваяСтрока.Представление = ДоступноеПоле.Заголовок;
		
		Если Лев(Строка(Поле), 21) = "ПользовательскиеПоля." Тогда
			НоваяСтрока.ИмяПоля = ЗарплатаКадрыКлиентСервер.ИдентификаторМакетаПечатнойФормы(НоваяСтрока.Представление);
		Иначе
			НоваяСтрока.ИмяПоля = ЗарплатаКадрыКлиентСервер.ИдентификаторМакетаПечатнойФормы(Строка(Поле));
		КонецЕсли;
		
		СтрокиКоллекции = ПараметрыМакета.НайтиСтроки(Новый Структура("ИмяПоля", НоваяСтрока.ИмяПоля));
		Если СтрокиКоллекции.Количество() > 0 Тогда
			СтрокиКоллекции[0].Выбрано = Истина;
		КонецЕсли;
		
		ИдентификаторПоля = НоваяСтрока.ПолучитьИдентификатор();
		
	КонецЕсли;
	
	Возврат ИдентификаторПоля;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьНастройки(КомпоновщикНастроекКД)
	Возврат КомпоновщикНастроекКД.ПолучитьНастройки();
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ВыбранныеПоляВНастройки(Форма)
	
	УстанавливалсяИдентификаторПользовательскойНастройки = Ложь;
	Если Не ЗначениеЗаполнено(Форма.КомпоновщикНастроекКД.Настройки.Выбор.ИдентификаторПользовательскойНастройки) Тогда
		Форма.КомпоновщикНастроекКД.Настройки.Выбор.ИдентификаторПользовательскойНастройки = Новый УникальныйИдентификатор;
		УстанавливалсяИдентификаторПользовательскойНастройки = Истина;
	КонецЕсли;
	
	Для Каждого Элемент Из Форма.КомпоновщикНастроекКД.ПользовательскиеНастройки.Элементы Цикл
		
		Если ТипЗнч(Элемент) = Тип("ВыбранныеПоляКомпоновкиДанных") Тогда
			
			ЭлементВыбор = Элемент;
			Если УстанавливалсяИдентификаторПользовательскойНастройки Тогда
				ЭлементВыбор.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Обычный;
			Иначе
				ЭлементВыбор.Элементы.Очистить();
			КонецЕсли;
			
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого ВыбранноеПоле Из Форма.ВыбранныеПоля Цикл
		
		ВыбранноеПолеНастроек = Неопределено;
		Для Каждого ВыбранныйЭлемент Из ЭлементВыбор.Элементы Цикл
			Если ВыбранныйЭлемент.Поле = Новый ПолеКомпоновкиДанных(ВыбранноеПоле.ПутьКДанным) Тогда
				ВыбранноеПолеНастроек = ВыбранныйЭлемент;
				Продолжить;
			КонецЕсли;
		КонецЦикла;
		
		Если ВыбранноеПолеНастроек = Неопределено Тогда
			ВыбранноеПолеНастроек = ЭлементВыбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
			ВыбранноеПолеНастроек.Поле = Новый ПолеКомпоновкиДанных(ВыбранноеПоле.ПутьКДанным);
		КонецЕсли;
		
		ВыбранноеПолеНастроек.Использование = ВыбранноеПоле.Использование;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВариантОтчетов(ИмяОбъекта, КлючВарианта)
	
	Возврат ЗарплатаКадрыОтчеты.ВариантОтчетаПечатнойФормыПоИмениОбъекта(ИмяОбъекта, КлючВарианта);
	
КонецФункции

#КонецОбласти

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Использование = Истина;
	
	ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.Использование		= Истина;
	ЭлементОтбора.ЛевоеЗначение		= Новый ПолеКомпоновкиДанных("ВыбранныеПоля.ПутьКДанным");
	ЭлементОтбора.ВидСравнения		= ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение	= Новый ПолеКомпоновкиДанных("ВыбранныеПоля.ПутьКДанным");
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Текст",
		Новый ПолеКомпоновкиДанных("ВыбранныеПоля.Представление"));
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ВыбранныеПоляПутьКДанным");
	ОформляемоеПоле.Использование = Истина;
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Использование = Истина;
	
	ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.Использование		= Истина;
	ЭлементОтбора.ЛевоеЗначение		= Новый ПолеКомпоновкиДанных("ВыбранныеПоля.ПутьКДанным");
	ЭлементОтбора.ВидСравнения		= ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ВыбранныеПоляИспользование");
	ОформляемоеПоле.Использование = Истина;
	
	// Условное оформление полей отборов
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Использование = Истина;
	
	ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.Использование		= Истина;
	ЭлементОтбора.ЛевоеЗначение		= Новый ПолеКомпоновкиДанных("КомпоновщикНастроекКД.Настройки.Отбор.Представление");
	ЭлементОтбора.ВидСравнения		= ВидСравненияКомпоновкиДанных.Заполнено;
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Текст", Новый ПолеКомпоновкиДанных("КомпоновщикНастроекКД.Настройки.Отбор.Представление"));
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("КомпоновщикНастроекКДНастройкиОтборТипГруппы");
	ОформляемоеПоле.Использование = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОбластиДокумента()
	
	ПорядокОбластей = Новый ТаблицаЗначений;
	ПорядокОбластей.Колонки.Добавить("Имя", Новый ОписаниеТипов("Строка"));
	ПорядокОбластей.Колонки.Добавить("Верх", Новый ОписаниеТипов("Число"));
	СтрокаПорядка = ПорядокОбластей.Добавить();
	СтрокаПорядка.Имя = НСтр("ru = '<сам документ>';
							|en = '<the document>'");
	СтрокаПорядка.Верх = -1;
	
	Для Каждого ОбластьМакета Из МакетПечатнойФормы.Области Цикл
		
		Если ОбластьМакета.Низ = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаПорядка = ПорядокОбластей.Добавить();
		СтрокаПорядка.Имя = ОбластьМакета.Имя;
		СтрокаПорядка.Верх = ОбластьМакета.Верх;
		
	КонецЦикла;
	
	ПорядокОбластей.Сортировать("Верх");
	ИзвестныеГруппы = Новый Соответствие;
	ЛишниеГруппы = Новый Массив;
	УсловияВКорне = Новый Массив;
	Для Каждого ЭлементОтбора Из КомпоновщикНастроекКД.Настройки.Отбор.Элементы Цикл
		Если ТипЗнч(ЭлементОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных")
			И ЗначениеЗаполнено(ЭлементОтбора.Представление) Тогда
			
			ПредставлениеГруппы = ПредставлениеГруппыОтбораОбласти(ЭлементОтбора);
			Если ЗначениеЗаполнено(ПорядокОбластей.НайтиСтроки(Новый Структура("Имя", ПредставлениеГруппы))) Тогда
				ИзвестныеГруппы.Вставить(ПредставлениеГруппы, ЭлементОтбора);
			Иначе
				ЛишниеГруппы.Добавить(ЭлементОтбора);
			КонецЕсли;
		Иначе
			УсловияВКорне.Добавить(ЭлементОтбора);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ЛишняяГруппа Из ЛишниеГруппы Цикл
		КомпоновщикНастроекКД.Настройки.Отбор.Элементы.Удалить(ЛишняяГруппа);
	КонецЦикла;
	
	Для Каждого ПорядокОбласти Из ПорядокОбластей Цикл
		ДобавитьГруппуОтбора(ПорядокОбласти.Имя, ИзвестныеГруппы);
	КонецЦикла;
	
	Для Каждого ЭлементОтбора Из УсловияВКорне Цикл
		Индекс = КомпоновщикНастроекКД.Настройки.Отбор.Элементы.Индекс(ЭлементОтбора);
		ИндексПорядка = УсловияВКорне.Найти(ЭлементОтбора);
		Если Индекс <> ИндексПорядка Тогда
			КомпоновщикНастроекКД.Настройки.Отбор.Элементы.Сдвинуть(ЭлементОтбора, ИндексПорядка - Индекс);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ПорядокОбласти Из ПорядокОбластей Цикл
		ГруппаУсловий = ИзвестныеГруппы.Получить(ПорядокОбласти.Имя);
		Если ГруппаУсловий = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Индекс = КомпоновщикНастроекКД.Настройки.Отбор.Элементы.Индекс(ГруппаУсловий);
		ИндексПорядка = ПорядокОбластей.Индекс(ПорядокОбласти) + УсловияВКорне.Количество();
		Если Индекс <> ИндексПорядка Тогда
			КомпоновщикНастроекКД.Настройки.Отбор.Элементы.Сдвинуть(ГруппаУсловий, ИндексПорядка - Индекс);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьГруппуОтбора(Представление, ИзвестныеГруппы)
	
	ГруппаОтбора = ИзвестныеГруппы.Получить(Представление);
	Если ГруппаОтбора = Неопределено Тогда
		ГруппаОтбора = КомпоновщикНастроекКД.Настройки.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
		ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
		ГруппаОтбора.Представление = Представление;
		ГруппаОтбора.Использование = Истина;
		ИзвестныеГруппы.Вставить(Представление, ГруппаОтбора);
	КонецЕсли;
	УстановитьПредставлениеГруппыОтбора(ГруппаОтбора);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПредставлениеГруппыОтбора(ГруппаОтбора)
	
	Представление = ПредставлениеГруппыОтбораОбласти(ГруппаОтбора);
	Если ГруппаОтбора.Элементы.Количество() = 0 Тогда
		ГруппаОтбора.Представление = Представление;
	Иначе
		ГруппаОтбора.Представление = СтрШаблон("%1 (%2)",
			Представление,
			ГруппаОтбора.Элементы.Количество());
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеГруппыОтбораОбласти(ГруппаОтбора)
	
	ПозицияСкобки = СтрНайти(ГруппаОтбора.Представление, "(");
	Если ПозицияСкобки = 0 Тогда
		Представление = ГруппаОтбора.Представление;
	Иначе
		Представление = СокрЛП(Лев(ГруппаОтбора.Представление, ПозицияСкобки - 1));
	КонецЕсли;
	
	Возврат Представление;
	
КонецФункции

&НаСервере
Процедура ВернутьсяКМакетуПоУмолчаниюНаСервере(АдресШаблонаНаСервере = Неопределено)
	
	УдалитьПользовательскийМакет();
	УстановитьОтображениеПолейРедактора(Истина, АдресШаблонаНаСервере);
	ЗаполнитьОбластиДокумента();
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура МакетПечатнойФормыПриИзменении(Элемент)
	
	ЗаполнитьОбластиДокумента();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УдалитьПустыеГруппыОтбораОбластей(Настройки)
	
	УдаляемыеГруппы = Новый Массив;
	Для Каждого ЭлементОтбора Из Настройки.Отбор.Элементы Цикл
		Если ТипЗнч(ЭлементОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			Если ЭлементОтбора.Элементы.Количество() = 0 Тогда
				УдаляемыеГруппы.Добавить(ЭлементОтбора);
			Иначе
				ЭлементОтбора.Представление = ПредставлениеГруппыОтбораОбласти(ЭлементОтбора);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого УдаляемаяГруппа Из УдаляемыеГруппы Цикл
		Настройки.Отбор.Элементы.Удалить(УдаляемаяГруппа);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьПараметрВСхему(ОписаниеПараметра)
	
	Схема = ПолучитьИзВременногоХранилища(АдресСхемы);
	
	ПараметрСхемы = Схема.Параметры.Найти(ОписаниеПараметра.ИмяДоРедактирования);
	Если ПараметрСхемы = Неопределено Тогда
		ПараметрСхемы = Схема.Параметры.Найти(ОписаниеПараметра.Имя);
	КонецЕсли;
	ЭтоНовыйПараметр = ПараметрСхемы = Неопределено;
	Если ЭтоНовыйПараметр Тогда
		ПараметрСхемы = Схема.Параметры.Добавить();
		ПараметрСхемы.Имя = ОписаниеПараметра.Имя;
	КонецЕсли;
	
	ПараметрСхемы.Заголовок = ОписаниеПараметра.Заголовок;
	ПараметрСхемы.ТипЗначения = ОписаниеПараметра.ТипЗначения;
	ПараметрСхемы.Использование = ИспользованиеПараметраКомпоновкиДанных.Всегда;
	Если ОписаниеПараметра.Свойство("ДоступныеЗначения") Тогда
		ПараметрСхемы.УстановитьДоступныеЗначения(ОписаниеПараметра.ДоступныеЗначения);
	КонецЕсли;
	ПараметрСхемы.Значение = ОписаниеПараметра.Значение;
	ПараметрСхемы.ЗапрещатьНезаполненныеЗначения = ОписаниеПараметра.Обязательный;
	
	ИнициализироватьКомпоновщикНастроекКДСхемой(Схема);
	
	Если ЭтоНовыйПараметр Тогда
		ИдентификаторСтроки = ВставитьВыбранноеПолеВКоллекцию(ВыбранныеПоля, КомпоновщикНастроекКД, Новый ПолеКомпоновкиДанных("ПараметрыДанных." + ПараметрСхемы.Имя), ПараметрыМакета);
		Если ИдентификаторСтроки <> Неопределено Тогда
			Элементы.ВыбранныеПоля.ТекущаяСтрока = ИдентификаторСтроки;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьРеквизитВСхему(ОписаниеРеквизита)
	
	Схема = ПолучитьИзВременногоХранилища(АдресСхемы);
	
	НаборДанных = Схема.НаборыДанных[0];
		
	ПолеНабораДанных = НаборДанных.Поля.Найти(ОписаниеРеквизита.ТекущийРеквизит);
	ЭтоНовыйПараметр = ПолеНабораДанных = Неопределено;
	Если ЭтоНовыйПараметр Тогда
		ПолеНабораДанных = НаборДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
	КонецЕсли;
	СловаИмени = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивСлов(ОписаниеРеквизита.Имя);
	ПолеНабораДанных.Поле			= СловаИмени[СловаИмени.Количество() - 1];
	ПолеНабораДанных.ПутьКДанным	= ОписаниеРеквизита.Имя;
	ПолеНабораДанных.ТипЗначения	= ОписаниеРеквизита.ТипЗначения;
	ПолеНабораДанных.ОграничениеИспользования.Поле = Ложь;
	ПолеНабораДанных.ОграничениеИспользования.Условие = Ложь;
	ПолеНабораДанных.ОграничениеИспользования.Группировка = Истина;
	ПолеНабораДанных.ОграничениеИспользования.Порядок = Истина;
	ПолеНабораДанных.ОграничениеИспользованияРеквизитов.Группировка = Истина;
	ПолеНабораДанных.ОграничениеИспользованияРеквизитов.Поле = Ложь;
	ПолеНабораДанных.ОграничениеИспользованияРеквизитов.Условие = Истина;
	ПолеНабораДанных.ОграничениеИспользованияРеквизитов.Порядок = Истина;
	Если ОписаниеРеквизита.Свойство("ДоступныеЗначения") Тогда
		ПолеНабораДанных.УстановитьДоступныеЗначения(ОписаниеРеквизита.ДоступныеЗначения);
	КонецЕсли;

	ИнициализироватьКомпоновщикНастроекКДСхемой(Схема);
	
	Если ЭтоНовыйПараметр Тогда
		ИдентификаторСтроки = ВставитьВыбранноеПолеВКоллекцию(ВыбранныеПоля, КомпоновщикНастроекКД, Новый ПолеКомпоновкиДанных(ПолеНабораДанных.ПутьКДанным), ПараметрыМакета);
		Если ИдентификаторСтроки <> Неопределено Тогда
			Элементы.ВыбранныеПоля.ТекущаяСтрока = ИдентификаторСтроки;
		КонецЕсли;
	Иначе
		ВыбранныеСтроки = ВыбранныеПоля.НайтиСтроки(Новый Структура("ПутьКДанным", ОписаниеРеквизита.ТекущийРеквизит));
		Для Каждого ВыбраннаяСтрока Из ВыбранныеСтроки Цикл
			ВыбраннаяСтрока.ПутьКДанным = ПолеНабораДанных.ПутьКДанным;
			ВыбраннаяСтрока.Представление = ПолеНабораДанных.Заголовок;
			ВыбраннаяСтрока.ИмяПоля = ЗарплатаКадрыКлиентСервер.ИдентификаторМакетаПечатнойФормы(Строка(ВыбраннаяСтрока.ПутьКДанным));
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере

Процедура УдалитьРеквизитДокументаНаСервере()
	
	Схема = ПолучитьИзВременногоХранилища(АдресСхемы);
	ПолеНабораДанных = Схема.НаборыДанных[0].Поля.Найти(Элементы.ДоступныеПоляВыбора.ТекущаяСтрока);
	Если ПолеНабораДанных <> Неопределено Тогда
		Схема.НаборыДанных[0].Поля.Удалить(ПолеНабораДанных);
		ИнициализироватьКомпоновщикНастроекКДСхемой(Схема);
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьПараметрНаСервере()
	
	Схема = ПолучитьИзВременногоХранилища(АдресСхемы);
	
	ИмяПараметра = ИмяТекущегоПараметра(Строка(Элементы.ДоступныеПоляВыбора.ТекущаяСтрока));
	ПараметрСхемы = Схема.Параметры.Найти(ИмяПараметра);
	Если ПараметрСхемы <> Неопределено Тогда
		Схема.Параметры.Удалить(ПараметрСхемы);
		ИнициализироватьКомпоновщикНастроекКДСхемой(Схема);
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьПорядокПараметраНаСервере(СдвинутьВверх)
	
	Схема = ПолучитьИзВременногоХранилища(АдресСхемы);
	
	ИмяПараметра = ИмяТекущегоПараметра(Строка(Элементы.ДоступныеПоляВыбора.ТекущаяСтрока));
	ПараметрСхемы = Схема.Параметры.Найти(ИмяПараметра);
	Если ПараметрСхемы <> Неопределено Тогда
		Если СдвинутьВверх Тогда
			Смещение = -1;
		Иначе
			Смещение = 1;
		КонецЕсли;
		Схема.Параметры.Сдвинуть(ПараметрСхемы, Смещение);
		ИнициализироватьКомпоновщикНастроекКДСхемой(Схема);
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ИмяТекущегоПараметра(ПутьКПараметру)
	ЧастиТекущегоИдентификатора = СтрРазделить(ПутьКПараметру, ".");
	Возврат ЧастиТекущегоИдентификатора[ЧастиТекущегоИдентификатора.Количество() - 1];
КонецФункции

&НаСервере
Процедура ИнициализироватьКомпоновщикНастроекКДСхемой(Схема)
	
	Справочники.ШаблоныДокументов.ДобавитьРасширенияПолейСхемыКомпоновкиДанных(Схема);
	
	#Если ТолстыйКлиентУправляемоеПриложение Тогда
		КомпоновщикНастроекКД.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(Схема));
	#Иначе
		АдресСхемы = ПоместитьВоВременноеХранилище(Схема, УникальныйИдентификатор);
		КомпоновщикНастроекКД.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемы));
	#КонецЕсли

	ОтчетыКлиентСервер.ЗагрузитьНастройки(КомпоновщикНастроекКД, КомпоновщикНастроекКД.Настройки, КомпоновщикНастроекКД.ПользовательскиеНастройки);
	КомпоновщикНастроекКД.ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ИспользуютсяПользовательскиеНастройкиПечати", Истина);
	
КонецПроцедуры

&НаСервере
Функция ВыгрузитьШаблонНаСервере()
	
	ТекущаяСхема = ПолучитьИзВременногоХранилища(АдресСхемы);
	
	Настройки = КомпоновщикНастроекКД.ПолучитьНастройки();
	УдалитьПустыеГруппыОтбораОбластей(Настройки);
	
	Текст = Новый ТекстовыйДокумент;
	Текст.УстановитьТекст(Справочники.ШаблоныДокументов.JSONШаблонаДокумента(МакетПечатнойФормы, ТекущаяСхема, Настройки));
	
	Возврат Текст;
	
КонецФункции

&НаКлиенте
Процедура ЗагрузитьШаблонЗавершение(РезультатВопроса, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПрочитатьФайлШаблонаДокумента", ЭтотОбъект);
	
	ПараметрыЗагрузки = ФайловаяСистемаКлиент.ПараметрыЗагрузкиФайла();
	ПараметрыЗагрузки.ИдентификаторФормы = УникальныйИдентификатор;
	ПараметрыЗагрузки.Диалог.Фильтр      = НСтр("ru = 'Файл шаблона документа (*.json)|*.json';
												|en = 'Файл шаблона документа (*.json)|*.json'");
	ПараметрыЗагрузки.Диалог.Заголовок   = НСтр("ru = 'Выберите файл шаблона';
												|en = 'Выберите файл шаблона'");
	
	ФайловаяСистемаКлиент.ЗагрузитьФайл(Оповещение, ПараметрыЗагрузки);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьФайлШаблонаДокумента(ОписаниеФайла, ДополнительныеПараметры) Экспорт
	
	Если ОписаниеФайла = Неопределено Или Не ЗначениеЗаполнено(ОписаниеФайла.Хранение) Тогда
		Возврат;
	КонецЕсли;
	
	ВернутьсяКМакетуПоУмолчаниюНаСервере(ОписаниеФайла.Хранение);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЗаголовокИзИдентификатора(Идентификатор)
	
	ЗаголовокПоля = Лев(Идентификатор, 1);
	Для НомерСимвола = 2 По СтрДлина(Идентификатор) Цикл
		СимволИмени = Сред(Идентификатор, НомерСимвола, 1);
		Если Не СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(СимволИмени)
			И ВРег(СимволИмени) = СимволИмени Тогда
			
			ЗаголовокПоля = ЗаголовокПоля + " " + НРег(СимволИмени);
		Иначе
			ЗаголовокПоля = ЗаголовокПоля + СимволИмени;
		КонецЕсли;
	КонецЦикла;
	Возврат ЗаголовокПоля;
	
КонецФункции

&НаКлиенте
Функция ПараметрыВыбораПоляФормы(Элемент)
	ПараметрыВыбора = Новый Массив();
	Если Не Элементы.КомпоновщикНастроекКДНастройкиОтборВыбиратьИзПолныхСписковЗначений.Пометка Тогда
		ЭлементТаблица = ЭлементТаблицаФормы(Элемент);
		Если ЭлементТаблица <> Неопределено Тогда
			ТекущиеДанные = ЭлементТаблица.ТекущиеДанные;
			Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущиеДанные, "ПравоеЗначение") Тогда
				ТипВыбираемогоЗначения = ТипЗнч(ТекущиеДанные.ПравоеЗначение);
				Если ТипВыбираемогоЗначения = Тип("СправочникСсылка.Должности")			
					Или ТипВыбираемогоЗначения = Тип("СправочникСсылка.Организации")
					Или ТипВыбираемогоЗначения = Тип("СправочникСсылка.ПодразделенияОрганизаций")
					Или ТипВыбираемогоЗначения = Тип("СправочникСсылка.СтруктураПредприятия")
					Или ТипВыбираемогоЗначения = Тип("СправочникСсылка.ШтатноеРасписание") Тогда
					
					ДоступныеЗначения = ВыгружаемыеОбъектыТипа(ТипВыбираемогоЗначения);
					Если ЗначениеЗаполнено(ДоступныеЗначения) Тогда
						ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", ДоступныеЗначения));
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Возврат Новый ФиксированныйМассив(ПараметрыВыбора);
КонецФункции

&НаКлиенте
Функция ЭлементТаблицаФормы(Элемент)
	Если ТипЗнч(Элемент) = Тип("ТаблицаФормы") Тогда
		Возврат Элемент;
	КонецЕсли;                        
	Если Элемент.Родитель = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	Возврат ЭлементТаблицаФормы(Элемент.Родитель);
КонецФункции

&НаСервереБезКонтекста
Функция ВыгружаемыеОбъектыТипа(ТипОбъекта)
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТипОбъекта", ТипОбъекта);
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВыгружаемыеОбъектыКабинетСотрудника.Ссылка КАК Ссылка
		|ИЗ
		|	РегистрСведений.ВыгружаемыеОбъектыКабинетСотрудника КАК ВыгружаемыеОбъектыКабинетСотрудника
		|ГДЕ
		|	ТИПЗНАЧЕНИЯ(ВыгружаемыеОбъектыКабинетСотрудника.Ссылка) = &ТипОбъекта";
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
КонецФункции

&НаСервереБезКонтекста
Функция ПубликоватьСтруктуруЮридическихЛиц()
	Возврат ИнтеграцияУправлениеПерсоналом.ПубликоватьСтруктуруЮридическихЛиц();
КонецФункции

#КонецОбласти