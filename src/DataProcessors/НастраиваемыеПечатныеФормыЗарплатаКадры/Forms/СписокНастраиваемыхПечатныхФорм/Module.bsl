#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьСписокПечатныхФорм();
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыПечатныеФормы

&НаКлиенте
Процедура ПечатныеФормыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФормуНастройкиПечатнойФормы(ВыбраннаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатныеФормыПриАктивизацииСтроки(Элемент)
	
	УстановитьДоступностьКомандНастройкиПоУмолчанию(ЭтотОбъект, Элемент.ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатныеФормыПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	ОткрытьФормуНастройкиПечатнойФормы(Элементы.ПечатныеФормы.ТекущаяСтрока);
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИспользоватьНастройкиПечатиПоУмолчанию(Команда)
	
	ТекстВопроса = НСтр("ru = 'Вернуться к использованию настроек печати ""По умолчанию""?';
						|en = 'Do you want to get back to using the ""Default"" print settings?'");
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИдентификаторСтроки", Элементы.ПечатныеФормы.ТекущаяСтрока);
	
	Оповещение = Новый ОписаниеОповещения("ИспользоватьНастройкиПечатиПоУмолчаниюЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ИспользоватьНастройкиПечатиПоУмолчаниюЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		УстановитьИспользованиеНастроекПечатиПоУмолчанию(ДополнительныеПараметры.ИдентификаторСтроки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьКомандНастройкиПоУмолчанию(Форма, ИдентификаторСтроки)
	
	Элементы = Форма.Элементы;
	ДоступностьКнопкиНастроекПоУмолчанию = Ложь;
	
	Если ИдентификаторСтроки <> Неопределено Тогда
		
		ТекущиеДанные = Форма.ПечатныеФормы.НайтиПоИдентификатору(ИдентификаторСтроки);
		Если ТекущиеДанные <> Неопределено Тогда
			ДоступностьКнопкиНастроекПоУмолчанию = ТекущиеДанные.Картинка = 1;
		КонецЕсли;
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ПечатныеФормыИспользоватьНастройкиПечатиПоУмолчанию",
		"Доступность",
		ДоступностьКнопкиНастроекПоУмолчанию);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ПечатныеФормыПредставлениеКонтекстноеМенюИспользоватьНастройкиПечатиПоУмолчанию",
		"Доступность",
		ДоступностьКнопкиНастроекПоУмолчанию);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуНастройкиПечатнойФормы(ИдентификаторСтроки)
	
	Если ИдентификаторСтроки <> Неопределено Тогда
		
		ТекущиеДанные = ПечатныеФормы.НайтиПоИдентификатору(ИдентификаторСтроки);
		Если ТекущиеДанные <> Неопределено Тогда
			
			ДополнительныеПараметры = Новый Структура;
			ДополнительныеПараметры.Вставить("ВыбраннаяСтрока", ИдентификаторСтроки);
			
			ПараметрыОткрытия = Новый Структура;
			ПараметрыОткрытия.Вставить("Представление", ТекущиеДанные.Представление);
			ПараметрыОткрытия.Вставить("ПутьКВариантуОтчета", ТекущиеДанные.ПутьКВариантуОтчета);
			ПараметрыОткрытия.Вставить("ПутьКМакетуПечатнойФормы", ТекущиеДанные.ПутьКМакетуПечатнойФормы);
			
			Оповещение = Новый ОписаниеОповещения("ПечатныеФормыЗавершениеИзменения", ЭтотОбъект, ДополнительныеПараметры);
			ОткрытьФорму("Обработка.НастраиваемыеПечатныеФормыЗарплатаКадры.Форма.ФормаНастройкиПечатнойФормы", ПараметрыОткрытия, ЭтотОбъект, ТекущиеДанные.ПутьКВариантуОтчета, , , Оповещение);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокПечатныхФорм()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПользовательскиеМакетыПечати.ИмяМакета КАК ИмяМакета,
		|	ПользовательскиеМакетыПечати.Объект КАК ИмяОбъекта
		|ИЗ
		|	РегистрСведений.ПользовательскиеМакетыПечати КАК ПользовательскиеМакетыПечати
		|ГДЕ
		|	ПользовательскиеМакетыПечати.Использование";
	
	ПользовательскиеМакеты = Запрос.Выполнить().Выгрузить();
	
	МакетыВариантовОтчетов = ЗарплатаКадрыОтчеты.МакетыВариантовОтчетовПечатныхФорм();
	
	Для Каждого ОписаниеВарианта Из МакетыВариантовОтчетов Цикл
		
		СтрокаОтчета = ПечатныеФормы.Добавить();
		СтрокаОтчета.ПутьКВариантуОтчета = ОписаниеВарианта.Ключ;
		СтрокаОтчета.ПутьКМакетуПечатнойФормы = ОписаниеВарианта.Значение;
		
		Если ВРег(Лев(СтрокаОтчета.ПутьКМакетуПечатнойФормы, 11)) = ВРег("ОбщийМакет.") Тогда
			
			СтрокаОтчета.Представление =
				Метаданные.ОбщиеМакеты[ЗарплатаКадрыОтчеты.ИмяМакетаИзПутиКМакетуПечатнойФормы(СтрокаОтчета.ПутьКМакетуПечатнойФормы)].Синоним;
			
		Иначе
			
			СтрокаОтчета.Представление =
				Метаданные.НайтиПоПолномуИмени(ЗарплатаКадрыОтчеты.ИмяОтчетаИзПутиКВарианту(СтрокаОтчета.ПутьКВариантуОтчета)).
					Макеты[ЗарплатаКадрыОтчеты.ИмяМакетаИзПутиКМакетуПечатнойФормы(СтрокаОтчета.ПутьКМакетуПечатнойФормы)].Синоним;
			
		КонецЕсли;
		
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("ИмяОбъекта", ЗарплатаКадрыОтчеты.ИмяОбъектаИзПутиКМакетуПечатнойФормы(СтрокаОтчета.ПутьКМакетуПечатнойФормы));
		СтруктураПоиска.Вставить("ИмяМакета", ЗарплатаКадрыОтчеты.ИмяМакетаИзПутиКМакетуПечатнойФормы(СтрокаОтчета.ПутьКМакетуПечатнойФормы));
		
		Если ПользовательскиеМакеты.НайтиСтроки(СтруктураПоиска).Количество() = 0 Тогда
			СтрокаОтчета.Картинка = 0;
		Иначе
			СтрокаОтчета.Картинка = 1;
		КонецЕсли;
		
	КонецЦикла;

	ПечатныеФормы.Сортировать("Представление");
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатныеФормыЗавершениеИзменения(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ТекущиеДанные = ПечатныеФормы.НайтиПоИдентификатору(ДополнительныеПараметры.ВыбраннаяСтрока);
		Если ТекущиеДанные <> Неопределено Тогда
			Если Результат.Результат = Истина Тогда
				ТекущиеДанные.Картинка = 1;
			КонецЕсли;
		КонецЕсли;
		УстановитьДоступностьКомандНастройкиПоУмолчанию(ЭтотОбъект, ДополнительныеПараметры.ВыбраннаяСтрока);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьИспользованиеНастроекПечатиПоУмолчанию(ИдентификаторСтроки)
	
	Если ИдентификаторСтроки <> Неопределено Тогда
		
		ТекущиеДанные = ПечатныеФормы.НайтиПоИдентификатору(ИдентификаторСтроки);
		Если ТекущиеДанные <> Неопределено Тогда
			
			ПолноеИмяОбъектаМетаданныхОтчета = ЗарплатаКадрыОтчеты.ИмяОтчетаИзПутиКВарианту(ТекущиеДанные.ПутьКВариантуОтчета);
			КлючВарианта = ЗарплатаКадрыОтчеты.КлючВариантаИзПутиКВарианту(ТекущиеДанные.ПутьКВариантуОтчета);
			
			// Очистка настроек варианта отчета
			ВариантОтчета = ЗарплатаКадрыОтчеты.ВариантОтчетаПечатнойФормыПоИмениОбъекта(
				ПолноеИмяОбъектаМетаданныхОтчета,
				КлючВарианта);
			
			ЗарплатаКадрыОтчеты.ОчиститьНастройкиВариантаОтчета(ВариантОтчета);
			
			// Установка использования макета печатной формы по умолчанию
			МенеджерЗаписи = РегистрыСведений.ПользовательскиеМакетыПечати.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Объект = ЗарплатаКадрыОтчеты.ИмяОбъектаИзПутиКМакетуПечатнойФормы(ТекущиеДанные.ПутьКМакетуПечатнойФормы);
			МенеджерЗаписи.ИмяМакета = ЗарплатаКадрыОтчеты.ИмяМакетаИзПутиКМакетуПечатнойФормы(ТекущиеДанные.ПутьКМакетуПечатнойФормы);
			МенеджерЗаписи.Удалить();
			
			ТекущиеДанные.Картинка = 0;
			
			ЗарплатаКадрыОтчеты.ОчиститьМакетОтчета(Метаданные.НайтиПоПолномуИмени(ПолноеИмяОбъектаМетаданныхОтчета), КлючВарианта);
			
		КонецЕсли;
		
		УстановитьДоступностьКомандНастройкиПоУмолчанию(ЭтотОбъект, ИдентификаторСтроки)
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
