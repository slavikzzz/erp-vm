
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ТабличныйДокумент", ТабличныйДокумент);
	Параметры.Свойство("ЭтоОтчетОбОбновленных", ЭтоОтчетОбОбновленных);
	
	Если ЭтоОтчетОбОбновленных Тогда
		
		Заголовок = НСтр("ru = 'Отчет об обновленных элементах';
						|en = 'Updated items report'");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТабличныйДокументОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	
	СтандартнаяОбработка = Ложь;
	Если ЗначениеЗаполнено(Расшифровка) Тогда 
		ПоказатьЗначение(,Расшифровка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СохранитьТаблицу(Команда)
	
	Если ЭтоОтчетОбОбновленных Тогда
		
		ЗаголовокДиалогаВыбораФала = НСтр("ru = 'Укажите имя файла для сохранения таблицы обновленных элементов';
											|en = 'To save the updated items table, specify the file name'");
		ПолноеИмяФайла = НСтр("ru = 'Таблица обновленных элементов';
								|en = 'Updated items table'");
		
	Иначе
		
		ЗаголовокДиалогаВыбораФала = НСтр("ru = 'Укажите имя файла для сохранения таблицы созданных элементов';
											|en = 'To save the created items table, specify the file name'");
		ПолноеИмяФайла = НСтр("ru = 'Таблица созданных элементов';
								|en = 'Created items table'");
		
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеВыбораФайлаДляСохранения", ЭтотОбъект);
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ДиалогВыбораФайла.Фильтр = НСтр("ru = 'Книга Excel 97 (*.xls)|*.xls|Книга Excel 2007 (*.xlsx)|*.xlsx|Электронная таблица OpenDocument (*.ods)|*.ods|Табличный документ (*.mxl)|*.mxl';
									|en = 'Microsoft Excel 97 (*.xls)|*.xls|Microsoft Excel 2007 (*.xlsx)|*.xlsx|OpenDocument Spreadsheet (*.ods)|*.ods|Spreadsheet document (*.mxl)|*.mxl'");
	ДиалогВыбораФайла.Заголовок = ЗаголовокДиалогаВыбораФала;
	ДиалогВыбораФайла.Расширение = "xls";
	ДиалогВыбораФайла.МножественныйВыбор = Ложь;
	ДиалогВыбораФайла.ПолноеИмяФайла = ПолноеИмяФайла;
	ДиалогВыбораФайла.ИндексФильтра = 0;
	ФайловаяСистемаКлиент.ПоказатьДиалогВыбора(Оповещение, ДиалогВыбораФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура Готово(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПослеВыбораФайлаДляСохранения(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ Результат = Неопределено Тогда
		
		АдресВоВременномХранилище = СформироватьФайлНаСервере(ТабличныйДокумент, Результат[0]);
		ФайловаяСистемаКлиент.СохранитьФайл(Неопределено, АдресВоВременномХранилище, Результат[0]);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СформироватьФайлНаСервере(ТабличныйДокумент, ПолноеИмяФайла)
	
	РасширениеФайла = ОбщегоНазначенияКлиентСервер.РасширениеБезТочки(ОбщегоНазначенияКлиентСервер.ПолучитьРасширениеИмениФайла(ПолноеИмяФайла));;
	
	ИмяФайла = ПолучитьИмяВременногоФайла(РасширениеФайла);
	Если РасширениеФайла = "xlsx" Тогда
		
		ТабличныйДокумент.Записать(ИмяФайла, ТипФайлаТабличногоДокумента.XLSX);
		
	ИначеЕсли РасширениеФайла = "xls" Тогда
		
		ТабличныйДокумент.Записать(ИмяФайла, ТипФайлаТабличногоДокумента.XLS);
		
	ИначеЕсли РасширениеФайла = "ods" Тогда
		
		ТабличныйДокумент.Записать(ИмяФайла, ТипФайлаТабличногоДокумента.ODS);
		
	Иначе 
		
		ТабличныйДокумент.Записать(ИмяФайла, ТипФайлаТабличногоДокумента.MXL);
		
	КонецЕсли;
	
	ДвоичныеДанные = Новый ДвоичныеДанные(ИмяФайла);
	
	АдресВоВременномХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанные, АдресВоВременномХранилище);
	
	ФайловаяСистема.УдалитьВременныйФайл(ИмяФайла);
	
	Возврат АдресВоВременномХранилище;
	
КонецФункции

#КонецОбласти