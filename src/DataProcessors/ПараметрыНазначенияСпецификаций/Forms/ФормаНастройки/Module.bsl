
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ПрочитатьНастройки();
	
	НастроитьЭлементыФормы();
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
		
		НоваяСтрока = ТаблицаПараметров.Добавить();
		НоваяСтрока.ВидПараметра          = ВыбранноеЗначение.ВидПараметра;
		НоваяСтрока.Реквизит              = ВыбранноеЗначение.Реквизит;
		НоваяСтрока.ПараметрПредставление = ПредставлениеПараметраНазначения(НоваяСтрока.ВидПараметра, НоваяСтрока.Реквизит);
		
		Модифицированность = Истина;
		
		НастроитьЗависимыеЭлементыФормы(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("ЗаписатьИЗакрыть", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповещение, Отказ, ЗавершениеРаботы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаПараметров

&НаКлиенте
Процедура ТаблицаПараметровПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	Если НЕ Копирование Тогда
		ДобавитьПараметр();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПараметровПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПараметровПослеУдаления(Элемент)
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Добавить(Команда)
	
	ДобавитьПараметр();
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	ЗаписатьИЗакрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ДобавитьПараметр()
	
	Если НЕ ДоступноДобавлениеПараметра(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	ИсключитьРеквизиты = Новый Массив;
	Для каждого Строка Из ТаблицаПараметров Цикл
		Параметр = Новый Структура("ВидПараметра,Реквизит");
		ЗаполнитьЗначенияСвойств(Параметр, Строка);
		ИсключитьРеквизиты.Добавить(Параметр);
	КонецЦикла;
	
	ПараметрыФормы = Новый Структура("ИсключитьРеквизиты", ИсключитьРеквизиты);
	
	ОткрытьФорму("Обработка.ПараметрыНазначенияСпецификаций.Форма.ФормаДоступныхПараметров",
		ПараметрыФормы,
		ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	ЗаписатьНастройки();
	
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьНастройки()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Т.ВидПараметра КАК ВидПараметра,
	|	Т.Реквизит     КАК Реквизит
	|ИЗ
	|	РегистрСведений.ПараметрыНазначенияСпецификаций КАК Т
	|
	|УПОРЯДОЧИТЬ ПО
	|	Т.Приоритет УБЫВ";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = ТаблицаПараметров.Добавить();
		НоваяСтрока.ВидПараметра          = Выборка.ВидПараметра;
		НоваяСтрока.Реквизит              = Выборка.Реквизит;
		НоваяСтрока.ПараметрПредставление = ПредставлениеПараметраНазначения(Выборка.ВидПараметра, Выборка.Реквизит);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНастройки()
	
	НаборЗаписей = РегистрыСведений.ПараметрыНазначенияСпецификаций.СоздатьНаборЗаписей();
	
	Приоритет = ТаблицаПараметров.Количество();
	Для каждого Строка Из ТаблицаПараметров Цикл
		НоваяЗапись = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, Строка);
		НоваяЗапись.Приоритет = Pow(2, Приоритет);
		Приоритет = Приоритет - 1;
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементыФормы()
	
	ТолькоПросмотр = НЕ (ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеПроизводством2_2") 
		И НЕ ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеПроизводством"));
	
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЗависимыеЭлементыФормы(Форма)
	
	Форма.Элементы.ТаблицаПараметровДобавить.Доступность = ДоступноДобавлениеПараметра(Форма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ДоступноДобавлениеПараметра(Форма)
	
	Возврат Форма.ТаблицаПараметров.Количество() < 10;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПредставлениеПараметраНазначения(ВидПараметра, Реквизит)
	
	Возврат УправлениеДаннымиОбИзделиях.ПредставлениеПараметраНазначения(ВидПараметра, Реквизит);
	
КонецФункции

#КонецОбласти