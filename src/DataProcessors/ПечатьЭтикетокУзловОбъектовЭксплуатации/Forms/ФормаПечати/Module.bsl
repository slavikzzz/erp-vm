
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	Объект.КоличествоЭкземпляров = 1;
	Объект.СформироватьШтрихкоды = Истина;
	
	ШаблонПоУмолчанию = Справочники.ШаблоныЭтикетокИЦенников.ШаблонПоУмолчанию(
		Перечисления.НазначенияШаблоновЭтикетокИЦенников.ЭтикеткаУзлаОбъектаЭксплуатацииЛента);
		
	Если НЕ ЗначениеЗаполнено(ШаблонПоУмолчанию) Тогда
		ШаблонПоУмолчанию = Справочники.ШаблоныЭтикетокИЦенников.ШаблонПоУмолчанию(
			Перечисления.НазначенияШаблоновЭтикетокИЦенников.ЭтикеткаУзлаОбъектаЭксплуатацииБумага);
	КонецЕсли;
	
	Объект.ШаблонЭтикетки = ШаблонПоУмолчанию;
	
	Если Параметры.Свойство("Узлы") Тогда
		ДобавитьУзлыОбъектовЭксплуатации(Параметры.Узлы);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУзлы

&НаКлиенте
Процедура УзлыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ВнеоборотныеАктивыКлиентСервер.ОбработкаВыбораЭлемента(
		Объект.Узлы, "Узел", ВыбранноеЗначение);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подобрать(Команда)
	
	ПараметрыПодбора = ОбщегоНазначенияУТКлиентСервер.ПараметрыПодбора(Элементы.УзлыУзел, ЭтотОбъект);
	
	ОткрытьФорму("Справочник.УзлыОбъектовЭксплуатации.ФормаВыбора", 
					ПараметрыПодбора, Элементы.Узлы,,,,, 
					РежимОткрытияОкнаФормы.БлокироватьОкноВладельца)
	
КонецПроцедуры

&НаКлиенте
Процедура Печать(Команда)
	
	ОчиститьСообщения();
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрКоманды = Новый Массив;
	ПараметрКоманды.Добавить(ПредопределенноеЗначение("Справочник.УзлыОбъектовЭксплуатации.ПустаяСсылка"));
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
		"Обработка.ПечатьЭтикетокИЦенников",
		"ЭтикеткаУзлаОбъектаЭксплуатации",
		ПараметрКоманды,
		ЭтотОбъект,
		ПолучитьПараметрыПечати());

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедуры

&НаСервере
Функция ПолучитьПараметрыПечати()
	
	ДанныеПечати = Объект.Узлы.Выгрузить();
	СписокУзлов = ДанныеПечати.ВыгрузитьКолонку("Узел");
	
	Если Объект.СформироватьШтрихкоды Тогда
		УправлениеРемонтамиВызовСервера.СгенерироватьШтрихкодыУзлов(СписокУзлов, Истина);
	КонецЕсли;
	
	ШтрихкодыОбъектов = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(СписокУзлов, "Штрихкод");
	Для каждого ЭлементКоллекции Из ДанныеПечати Цикл
		ЭлементКоллекции.Штрихкод = ШтрихкодыОбъектов.Получить(ЭлементКоллекции.Узел);
	КонецЦикла;
	
	ПараметрыПечати = Новый Структура;
	ПараметрыПечати.Вставить("АдресВХранилище", ПоместитьВоВременноеХранилище(ДанныеПечати, УникальныйИдентификатор));
	ПараметрыПечати.Вставить("ШаблонЭтикетки", Объект.ШаблонЭтикетки);
	ПараметрыПечати.Вставить("КоличествоЭкземпляров", Объект.КоличествоЭкземпляров);
	
	Возврат ПараметрыПечати;
	
КонецФункции

&НаСервере
Процедура ДобавитьУзлыОбъектовЭксплуатации(Узлы)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	УзлыОбъектовЭксплуатации.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.УзлыОбъектовЭксплуатации КАК УзлыОбъектовЭксплуатации
	|ГДЕ
	|	УзлыОбъектовЭксплуатации.Ссылка В ИЕРАРХИИ(&Узлы)
	|
	|УПОРЯДОЧИТЬ ПО
	|	УзлыОбъектовЭксплуатации.Наименование,
	|	УзлыОбъектовЭксплуатации.Штрихкод";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Узлы", Узлы);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СтрТабл = Объект.Узлы.Добавить();
		СтрТабл.Узел = Выборка.Ссылка; 
	КонецЦикла;

КонецПроцедуры
 
#КонецОбласти
