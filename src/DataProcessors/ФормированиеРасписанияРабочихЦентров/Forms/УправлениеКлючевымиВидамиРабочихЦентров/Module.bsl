//++ Устарело_Производство21
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ОтборПодразделение = Параметры.Подразделение;
	ПараметрыПодразделения = ПроизводствоСервер.ПараметрыПроизводственногоПодразделения(ОтборПодразделение);
	ИнтервалПланирования = ПараметрыПодразделения.ИнтервалПланирования;
	
	ВыбранныйПериодНачало = Параметры.Период;
	
	УправлениеВводомПериода();
	
	ОбновитьДанные();
	
	Если Параметры.Свойство("ВидРабочегоЦентра") Тогда
		СтруктураПоиска = Новый Структура("ВидРабочегоЦентра,Период", Параметры.ВидРабочегоЦентра, Параметры.Период);
		СписокСтрок = КлючевыеВидыРабочихЦентров.НайтиСтроки(СтруктураПоиска);
		Если СписокСтрок.Количество() <> 0 Тогда
			Элементы.КлючевыеВидыРабочихЦентров.ТекущаяСтрока = СписокСтрок[0].ПолучитьИдентификатор();
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	ОбновитьДанные();
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПериодНачалоВыбораЗавершение", ЭтотОбъект);
	
	ОбщегоНазначенияУТКлиент.ВыбратьПериодИзСписка(
				ЭтаФорма,
				Элемент,
				ВидПериода,
				ВыбранныйПериодНачало,
				ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыКлючевыеВидыРабочихЦентров

&НаКлиенте
Процедура КлючевыеВидыРабочихЦентровКлючевойПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.КлючевыеВидыРабочихЦентров.ТекущиеДанные;
	СохранитьРедактированиеКлючевыхВидовРЦ(ТекущиеДанные.КлючСвязи);
	
	ОперативныйУчетПроизводстваКлиент.ОповеститьОбИзмененииКлючевогоВидаРабочегоЦентра();
	
КонецПроцедуры

&НаКлиенте
Процедура КлючевыеВидыРабочихЦентровПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.КлючевыеВидыРабочихЦентров.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		КлючСвязиВидовРЦ = ТекущиеДанные.КлючСвязи;
	Иначе
		КлючСвязиВидовРЦ = 0;
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.КлючевыеВидыРабочихЦентровРасписаниеРаботы.Имя);

	ГруппаОтбора1 = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора1.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;

	ОтборЭлемента = ГруппаОтбора1.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("КлючевыеВидыРабочихЦентров.СоставлятьРасписание");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	ОтборЭлемента = ГруппаОтбора1.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("КлючевыеВидыРабочихЦентров.Ключевой");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Не требуется';
																|en = 'Not required'"));

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.КлючевыеВидыРабочихЦентровРасписаниеРаботы.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("КлючевыеВидыРабочихЦентров.РасписаниеРаботы");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.СостоянияРасписанияРабочихЦентров.СформированоПолностью;

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("КлючевыеВидыРабочихЦентров.СоставлятьРасписание");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("КлючевыеВидыРабочихЦентров.Ключевой");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ОтметкаПоложительногоВыполненияЗадачи);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.КлючевыеВидыРабочихЦентровПериодСтрока.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("КлючевыеВидыРабочихЦентров.СоставлятьРасписание");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("КлючевыеВидыРабочихЦентров.РасписаниеРаботы");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
	СписокЗначений = Новый СписокЗначений;
	СписокЗначений.Добавить(Перечисления.СостоянияРасписанияРабочихЦентров.НеСформировано);
	СписокЗначений.Добавить(Перечисления.СостоянияРасписанияРабочихЦентров.СформированоЧастично);
	ОтборЭлемента.ПравоеЗначение = СписокЗначений;

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("КлючевыеВидыРабочихЦентров.Период");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.МеньшеИлиРавно;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("КонецТекущегоДня");

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.SpecialTextColor);

КонецПроцедуры

#Область ЗаполнениеДанными

&НаСервере
Процедура ОбновитьДанные()

	КонецТекущегоДня = КонецДня(ТекущаяДатаСеанса());
	ЗаполнитьСписокКлючевыхВидовРабочихЦентров();
	
КонецПроцедуры
 
&НаСервере
Процедура ЗаполнитьСписокКлючевыхВидовРабочихЦентров()

	КлючевыеВидыРабочихЦентров.Очистить();
	
	КлючевыеВидыРабочихЦентровТаблица = ОперативныйУчетПроизводства.КлючевыеВидыРабочихЦентров(
												ОтборПодразделение, 
												ВыбранныйПериодНачало,
												ВыбранныйПериодОкончание, 
												Истина);
												
	КлючевыеВидыРабочихЦентров.Загрузить(КлючевыеВидыРабочихЦентровТаблица);
	
	// Удалим старые настройки оформления
	Для Сч = -УсловноеОформление.Элементы.Количество()+1 По 0 Цикл
		ЭлементУсловноеОформление = УсловноеОформление.Элементы[-Сч];
		Если ЭлементУсловноеОформление.Представление = "#Служебный_Ключевой#" Тогда
			УсловноеОформление.Элементы.Удалить(ЭлементУсловноеОформление);
		КонецЕсли; 
	КонецЦикла; 
	
	Для каждого СтрокаВидРЦ Из КлючевыеВидыРабочихЦентров Цикл
		
		СтрокаВидРЦ.ПериодСтрока = ОперативныйУчетПроизводства.ИнтервалПланированияСтрокой(
										СтрокаВидРЦ.Период,
										СтрокаВидРЦ.ИнтервалПланирования);
										
		// Сформируем условное оформление для отображения индикатора связи
		// Для этого нужно получить этапы в которых используется вид рабочего центра
		// По этим этапам получить их виды рабочих центров
		// По полученным видам рабочих центров найти их ключ связи.
		ЭлементУсловноеОформление = УсловноеОформление.Элементы.Добавить(); 
		ЭлементУсловноеОформление.Использование = Истина;
		ЭлементУсловноеОформление.Представление = "#Служебный_Ключевой#";
		
		ОтборОформления = ЭлементУсловноеОформление.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборОформления.Использование  = Истина;
		ОтборОформления.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("КлючСвязиВидовРЦ");
		ОтборОформления.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
		ОтборОформления.ПравоеЗначение = СтрокаВидРЦ.КлючСвязи;
		
		ОтборОформления = ЭлементУсловноеОформление.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборОформления.Использование  = Истина;
		ОтборОформления.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("КлючевыеВидыРабочихЦентров.КлючСвязи");
		ОтборОформления.ВидСравнения   = ВидСравненияКомпоновкиДанных.НеВСписке;
		ОтборОформления.ПравоеЗначение = СтрокаВидРЦ.СписокЗависимыхВидовРЦ.Скопировать();
		
		ПолеОформление               = ЭлементУсловноеОформление.Поля.Элементы.Добавить(); 
		ПолеОформление.Использование = Истина;
		ПолеОформление.Поле          = Новый ПолеКомпоновкиДанных("КлючевыеВидыРабочихЦентровКартинкаСвязи");
		
		ОтображатьОформления = ЭлементУсловноеОформление.Оформление.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Отображать")); 
		ОтображатьОформления.Значение      = Ложь; 
		ОтображатьОформления.Использование = Истина;
		
	КонецЦикла; 
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокЗависимыхВидовРЦ(КлючСвязи, СписокЗависимыхВидовРЦ)

	Если СписокЗависимыхВидовРЦ.Найти(КлючСвязи) <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураПоиска = Новый Структура("КлючСвязи", КлючСвязи);
 	СписокСтрок = КлючевыеВидыРабочихЦентров.НайтиСтроки(СтруктураПоиска);
	
	Для каждого СтрокаВидРЦ Из СписокСтрок Цикл
		СписокЗависимыхВидовРЦ.Добавить(СтрокаВидРЦ.КлючСвязи);
		Для каждого СтрокаЗависимыйВидРЦ Из СтрокаВидРЦ.СписокЗависимыхВидовРЦ Цикл
			ЗаполнитьСписокЗависимыхВидовРЦ(СтрокаЗависимыйВидРЦ.Значение, СписокЗависимыхВидовРЦ);
		КонецЦикла; 
	КонецЦикла; 

КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаСервере
Процедура УправлениеВводомПериода()

	Если ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.Час Тогда
		
		ВидПериода = Перечисления.ДоступныеПериодыОтчета.День;
		
	ИначеЕсли ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.День 
		ИЛИ ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.Неделя  Тогда
		
		ВидПериода = Перечисления.ДоступныеПериодыОтчета.Месяц;
		
	Иначе
		
		ВидПериода = Перечисления.ДоступныеПериодыОтчета.Год;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВыбранныйПериодНачало) Тогда
		ВыбранныйПериодНачало    = ОбщегоНазначенияУТКлиентСервер.НачалоПериодаОтчета(ВидПериода, ВыбранныйПериодНачало);
		ВыбранныйПериодОкончание = ОбщегоНазначенияУТКлиентСервер.КонецПериодаОтчета(ВидПериода, ВыбранныйПериодНачало);
	Иначе
		ВыбранныйПериодНачало    = Неопределено;
		ВыбранныйПериодОкончание = Неопределено;
	КонецЕсли;
	
	Список = ОбщегоНазначенияУТКлиентСервер.ДоступныеЗначенияПериодаПоВидуПериода(ВыбранныйПериодНачало, ВидПериода);
	ЭлементСписка = Список.НайтиПоЗначению(ВыбранныйПериодНачало);
	Если ЭлементСписка <> Неопределено Тогда
		Период = ЭлементСписка.Представление;
	Иначе
		Период = Неопределено;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура СохранитьРедактированиеКлючевыхВидовРЦ(КлючСвязи)

	СтруктураПоиска = Новый Структура("КлючСвязи", КлючСвязи);
 	СтрокаТекущийВидРЦ = КлючевыеВидыРабочихЦентров.НайтиСтроки(СтруктураПоиска)[0];
	
	СтрокаТекущийВидРЦ.РучноеРедактированиеКлючевого = Истина;
	
	ЗаписьКлючевой = РегистрыСведений.КлючевыеВидыРабочихЦентров.СоздатьМенеджерЗаписи();
	ЗаписьКлючевой.ВидРабочегоЦентра = СтрокаТекущийВидРЦ.ВидРабочегоЦентра;
	ЗаписьКлючевой.ПериодРаботы      = СтрокаТекущийВидРЦ.Период;
	ЗаписьКлючевой.Ключевой          = СтрокаТекущийВидРЦ.Ключевой;
	ЗаписьКлючевой.Записать();
	
	Для каждого КлючЗависимогоВидаРЦ Из СтрокаТекущийВидРЦ.СписокЗависимыхВидовРЦ Цикл
		СтруктураПоиска = Новый Структура("КлючСвязи", КлючЗависимогоВидаРЦ.Значение);
	 	СписокСтрок = КлючевыеВидыРабочихЦентров.НайтиСтроки(СтруктураПоиска);
		Для каждого СтрокаВидРЦ Из СписокСтрок Цикл
			Если СтрокаВидРЦ.Ключевой И СтрокаВидРЦ.КлючСвязи <> СтрокаТекущийВидРЦ.КлючСвязи Тогда
				СтрокаВидРЦ.Ключевой = Ложь;
			КонецЕсли;
			
			СтрокаВидРЦ.РучноеРедактированиеКлючевого = Истина;
			
			ЗаписьКлючевой = РегистрыСведений.КлючевыеВидыРабочихЦентров.СоздатьМенеджерЗаписи();
			ЗаписьКлючевой.ВидРабочегоЦентра = СтрокаВидРЦ.ВидРабочегоЦентра;
			ЗаписьКлючевой.ПериодРаботы      = СтрокаВидРЦ.Период;
			ЗаписьКлючевой.Ключевой          = СтрокаВидРЦ.Ключевой;
			ЗаписьКлючевой.Записать();
		КонецЦикла;
	КонецЦикла; 

КонецПроцедуры

&НаКлиенте
Процедура ПериодНачалоВыбораЗавершение(ВыбранныйПериод, ДополнительныеПараметры) Экспорт

	Если ВыбранныйПериод <> Неопределено Тогда
		
		Период = ВыбранныйПериод.Представление;
		
		ВыбранныйПериодНачало     = ВыбранныйПериод.Значение;
		ВыбранныйПериодОкончание  = ОбщегоНазначенияУТКлиентСервер.КонецПериодаОтчета(ВидПериода, ВыбранныйПериод.Значение);
		
		ОбновитьДанные();
	
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
//-- Устарело_Производство21