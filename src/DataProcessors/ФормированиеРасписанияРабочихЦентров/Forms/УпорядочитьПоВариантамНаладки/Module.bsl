//++ Устарело_Производство21
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	Параметры.Свойство("ВариантНаладкиПрошлогоМЛ", ВариантНаладкиПрошлогоМЛ);
	Если НЕ ЗначениеЗаполнено(ВариантНаладкиПрошлогоМЛ) Тогда
		ВариантНаладкиПрошлогоМЛ = НСтр("ru = '<не использовался>';
										|en = '<have not been used>'");
	КонецЕсли;
	
	Если Параметры.Свойство("ПараметрыОтбора") Тогда
		ТаблицаИсточник = Параметры.ТаблицаИсточник.Выгрузить(Параметры.ПараметрыОтбора);
	Иначе
		ТаблицаИсточник = Параметры.ТаблицаИсточник.Выгрузить();
	КонецЕсли; 
	
	ЕдиницаВремениПереналадки = Параметры.ЕдиницаВремениПереналадки;
	
	ТаблицаИсточник.Свернуть("ВариантНаладки");
	ВариантыНаладки.Загрузить(ТаблицаИсточник);
	
	ТаблицаДлительностьПереналадки.Загрузить(Параметры.ТаблицаДлительностьПереналадки.Выгрузить());
	
	ЗаполнитьДлительностьПереналадки();
	
	УстановитьОформлениеЦветомВариантовНаладки(Параметры.ОформлениеВариантовНаладки);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыВариантыНаладки

&НаКлиенте
Процедура ВариантыНаладкиПриИзменении(Элемент)
	
	ЗаполнитьДлительностьПереналадки();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)

	Закрыть(ВариантыНаладки);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьДлительностьПереналадки()

	ОбщееВремяНаПереналадку = 0;
	
	Если ЗначениеЗаполнено(ВариантНаладкиПрошлогоМЛ) 
		И ТипЗнч(ВариантНаладкиПрошлогоМЛ) = Тип("СправочникСсылка.ВариантыНаладки") Тогда
		ТекущийВариантНаладки = ВариантНаладкиПрошлогоМЛ;
	Иначе
		ТекущийВариантНаладки = Справочники.ВариантыНаладки.ПустаяСсылка();
	КонецЕсли; 
	Для каждого ЭлементКоллекции Из ВариантыНаладки Цикл
		ЭлементКоллекции.ВремяПереналадки = 0;
		
		Если ТекущийВариантНаладки <> ЭлементКоллекции.ВариантНаладки 
			И ЗначениеЗаполнено(ТекущийВариантНаладки) Тогда
			
			ЭлементКоллекции.ВремяПереналадки = ОперативныйУчетПроизводстваКлиентСервер.ВремяПереналадки(
														ТекущийВариантНаладки, 
														ЭлементКоллекции.ВариантНаладки,
														ТаблицаДлительностьПереналадки) / 3600;
												
		КонецЕсли; 
		ТекущийВариантНаладки = ЭлементКоллекции.ВариантНаладки;
	КонецЦикла; 

КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеЦветомВариантовНаладки(ОформлениеВариантовНаладки)

	Для каждого ОформлениеВариантаНаладки Из ОформлениеВариантовНаладки Цикл
		Элемент = УсловноеОформление.Элементы.Добавить();
		Элемент.Представление = "ЦветОформления";

		ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ВариантыНаладки.Имя);

		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ВариантыНаладки.ВариантНаладки");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = ОформлениеВариантаНаладки.ВариантНаладки;

		Элемент.Оформление.УстановитьЗначениеПараметра("ЦветФона", ОформлениеВариантаНаладки.ЦветОформления);
	КонецЦикла; 

КонецПроцедуры

#КонецОбласти
//-- Устарело_Производство21