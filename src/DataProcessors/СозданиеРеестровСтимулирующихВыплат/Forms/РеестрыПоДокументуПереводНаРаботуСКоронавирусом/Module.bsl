
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ДокументПереводНаРаботуСКоронавирусом", ДокументПереводНаРаботуСКоронавирусом) Тогда
		
		Элементы.ДокументПереводНаРаботуСКоронавирусом.ТолькоПросмотр = Истина;
		Элементы.ДокументПереводНаРаботуСКоронавирусом.КнопкаОткрытия = Ложь;
		Элементы.ДокументПереводНаРаботуСКоронавирусом.КнопкаВыбора   = Ложь;
		
		Если НастроитьПоДокументПереводНаРаботуСКоронавирусом() Тогда
			СоздатьРеестрыНаСервере();
		Иначе	
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
	Иначе
		
		Элементы.ДокументПереводНаРаботуСКоронавирусом.ТолькоПросмотр = Ложь;
		Элементы.ДокументПереводНаРаботуСКоронавирусом.КнопкаОткрытия = Истина;
		Элементы.ДокументПереводНаРаботуСКоронавирусом.КнопкаВыбора   = Истина;
		
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДокументПереводНаРаботуСКоронавирусомПриИзменении(Элемент)
	Если ЗначениеЗаполнено(ДокументПереводНаРаботуСКоронавирусом) Тогда
		ДокументПереводНаРаботуСКоронавирусомПриИзмененииНаСервере();
	КонецЕсли	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

&НаКлиенте
Процедура РеестрыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ОткрытьТекущуюВедомость();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьРеестры(Команда)
	СоздатьРеестры(Истина)
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьРеестр(Команда)
	ОткрытьТекущуюВедомость();
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ОчиститьСообщения();
	
	Если ЗаписатьИЗакрытьНаСервере() Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция НастроитьПоДокументПереводНаРаботуСКоронавирусом()
	
	// Документ должен быть проведен
	Если НЕ ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументПереводНаРаботуСКоронавирусом, "Проведен") Тогда
		ТекстСообщения = НСтр("ru = 'Формирование возможно только по проведенному документу';
								|en = 'Generation is only available based on the posted document'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат Ложь
	КонецЕсли;
	
	Возврат Истина
	
КонецФункции

&НаСервере
Процедура ДокументПереводНаРаботуСКоронавирусомПриИзмененииНаСервере()
	
	Если НастроитьПоДокументПереводНаРаботуСКоронавирусом() Тогда
		СоздатьРеестрыНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьРеестры(СообщитьПользователю = Ложь)
	ОчиститьСообщения();
	СоздатьРеестрыНаСервере(СообщитьПользователю);
КонецПроцедуры

&НаСервере
Процедура СоздатьРеестрыНаСервере(СообщитьПользователю = Ложь)
	
	ПравилаПроверки = Новый СписокЗначений;
	ПравилаПроверки.Добавить("ДокументПереводНаРаботуСКоронавирусом", НСтр("ru = 'Не выбран документ';
																			|en = 'No document is selected'"));

	МожноСоздатьРеестры = ЗарплатаКадры.СвойстваФормыЗаполнены(ЭтотОбъект, ПравилаПроверки, СообщитьПользователю);

	Если МожноСоздатьРеестры Тогда
		
		РеестрыПоДокументуПереводНаРаботуСКоронавирусом = 
			Обработки.СозданиеРеестровСтимулирующихВыплат.СоздатьРеестрыПоДокументуПереводНаРаботуСКоронавирусом(
				ДокументПереводНаРаботуСКоронавирусом);
		
		ЗаполнитьСписокРеестров(РеестрыПоДокументуПереводНаРаботуСКоронавирусом);
		
	КонецЕсли	
	
КонецПроцедуры	

&НаСервере
Процедура ЗаполнитьСписокРеестров(РеестрыПоДокументуПереводНаРаботуСКоронавирусом)
	
	Реестры.Очистить();
	
	Для Каждого Реестр Из РеестрыПоДокументуПереводНаРаботуСКоронавирусом Цикл
		
		Строка = Реестры.Добавить();
		
		Строка.Адрес = 
			ПоместитьВоВременноеХранилище(
				ОбщегоНазначения.ЗначениеВСтрокуXML(Реестр), 
				УникальныйИдентификатор);
				
		Строка.Ссылка = Реестр.ПолучитьСсылкуНового();
		Строка.НачалоПериода	= Реестр.НачалоПериода;
		Строка.ОкончаниеПериода = Реестр.ОкончаниеПериода; 
		
		Сотрудники = Реестр.Сотрудники.Выгрузить(, "Сотрудник");
		Сотрудники.Свернуть("Сотрудник");
		Сотрудники = Сотрудники.ВыгрузитьКолонку("Сотрудник");
		
		Если Сотрудники.Количество() <= 3 Тогда
			Строка.Сотрудники  = СтрСоединить(Сотрудники, Символы.ПС);
		Иначе	
			Строка.Сотрудники  = ЗарплатаКадры.КраткийСоставСотрудников(Сотрудники, Реестр.Дата);
		КонецЕсли;
				
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьТекущуюВедомость()
	
	ТекущиеДанные = Элементы.Реестры.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		ПараметрыФормы = Новый Структура("АдресВоВременномХранилище", ТекущиеДанные.Адрес);
		ОткрытьФорму("Обработка.СозданиеРеестровСтимулирующихВыплат.Форма.Реестр", ПараметрыФормы, ЭтаФорма);
	КонецЕсли	
	
КонецПроцедуры

&НаСервере
Функция ЗаписатьИЗакрытьНаСервере()
		
	Если СохранитьРеестры(РежимЗаписиДокумента.Проведение) Тогда
		Возврат Истина;
	Иначе
		ОбработатьСообщенияПользователю();
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция СохранитьРеестры(РежимЗаписи)
	
	РеестрыПоДокументуПереводНаРаботуСКоронавирусом = Новый Массив;
	Для Каждого Строка Из Реестры Цикл
		СтрокаXML = ПолучитьИзВременногоХранилища(Строка.Адрес);
		Реестр = ОбщегоНазначения.ЗначениеИзСтрокиXML(СтрокаXML);
		РеестрыПоДокументуПереводНаРаботуСКоронавирусом.Добавить(Реестр);
	КонецЦикла;
	
	Если Обработки.СозданиеРеестровСтимулирующихВыплат.ПроверитьРеестры(РеестрыПоДокументуПереводНаРаботуСКоронавирусом) Тогда
		Попытка 
			Обработки.СозданиеРеестровСтимулирующихВыплат.СохранитьРеестры(РеестрыПоДокументуПереводНаРаботуСКоронавирусом, РежимЗаписи);
			Возврат Истина
		Исключение
			ОбщегоНазначения.СообщитьПользователю(
				СтрШаблон(
					НСтр("ru = 'Не удалось сохранить реестры по причине:
					           | %1';
					           |en = 'Cannot save the registers. Reason:
					           | %1'"),
					КраткоеПредставлениеОшибки(ИнформацияОбОшибке())));
			Возврат Ложь
		КонецПопытки;
	Иначе
		Возврат Ложь
	КонецЕсли
	
КонецФункции

&НаСервере
Процедура ОбработатьСообщенияПользователю()
	
	Сообщения = ПолучитьСообщенияПользователю(Ложь);
	
	Для Каждого Сообщение Из Сообщения Цикл
		СтрокиВедомости = Реестры.НайтиСтроки(Новый Структура("Ссылка", Сообщение.КлючДанных));
		Если СтрокиВедомости.Количество() > 0 Тогда
			Строка = СтрокиВедомости[0];
			Сообщение.КлючДанных = Неопределено;
			Сообщение.Поле = СтрШаблон("Ведомости[%1].Тип", Реестры.Индекс(Строка));
		КонецЕсли
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
