#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Ссылка = Параметры.Ссылка;
	
	РеквизитыСсылки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		Ссылка,
		"Количество,КоличествоНаКонтроле,КоличествоНаДоработке,КоличествоБрак,КоличествоФакт,КоличествоОтменено,Подразделение, Операция.ЕдиницаИзмерения");
	
	Количество = РеквизитыСсылки.Количество - РеквизитыСсылки.КоличествоОтменено;
	
	Подразделение = РеквизитыСсылки.Подразделение;
	
	Элементы.Количество.МаксимальноеЗначение = Количество;
	Элементы.Количество.МинимальноеЗначение  = РеквизитыСсылки.КоличествоНаКонтроле
		+ РеквизитыСсылки.КоличествоНаДоработке
		+ РеквизитыСсылки.КоличествоБрак
		+ РеквизитыСсылки.КоличествоФакт;
	
	ЕдиницаИзмеренияОперации = УправлениеПроизводствомКлиентСервер.ПредставлениеЕдиницыИзмеренияОперации(
		РеквизитыСсылки.ОперацияЕдиницаИзмерения,
		,
		Истина);
		
	Элементы.ОтразитьНевыполнение.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Отразить невыполнение операции в количестве 0 %1';
			|en = 'Record the operation failure in the amount of 0 %1'"),
		ЕдиницаИзмеренияОперации);
	
	УстановитьСтатус();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	Если Количество < Элементы.Количество.МаксимальноеЗначение И Количество > 0 Тогда
		
		ЗаписатьИЗакрытьНаСервере(Ссылка, Количество, СтатусНовойОперации);
		ОперативныйУчетПроизводстваКлиент.ОповеститьОЗаписиПроизводственныхОпераций(Подразделение,, Ссылка);
		
		Закрыть();
		
	Иначе
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = Нстр("ru = 'Введено неверное количество';
								|en = 'Invalid number entered'");
		Сообщение.Сообщить();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаписатьИЗакрытьНаСервере(Ссылка, Выполнено, СтатусНовой)
	Документы.ПроизводственнаяОперация2_2.ВыполнитьЧастично(Ссылка, Выполнено, СтатусНовой);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КоличествоПриИзменении(Элемент)
	
	КоличествоОсталось = Элементы.Количество.МаксимальноеЗначение - Количество;
	
	Элементы.ОтразитьНевыполнение.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Отразить невыполнение операции в количестве %1 %2';
			|en = 'Record the operation failure in the amount of %1%2'"),
		КоличествоОсталось,
		ЕдиницаИзмеренияОперации);
		
КонецПроцедуры

&НаКлиенте
Процедура ОтразитьНевыполнениеПриИзменении(Элемент)
	УстановитьСтатус();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьСтатус()
	
	Статус = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Статус");
	
	Если Статус = Перечисления.СтатусыПроизводственныхОпераций.Выполняется Тогда
		ТекущийСтатус = "Выполняется";
	ИначеЕсли Статус = Перечисления.СтатусыПроизводственныхОпераций.Создана Тогда
		ТекущийСтатус = "Создана";
	КонецЕсли;
	
	СтатусНовойОперации = ?(ОтразитьНевыполнение, "НеВыполнена", ТекущийСтатус);
	
КонецПроцедуры


#КонецОбласти
