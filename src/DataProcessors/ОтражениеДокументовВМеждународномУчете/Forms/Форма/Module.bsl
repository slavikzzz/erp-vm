#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	Если Параметры.Свойство("ДатаОкончанияПериода") Тогда
		ДатаОкончанияПериода = Параметры.ДатаОкончанияПериода;
	КонецЕсли;
	
	Если Параметры.Свойство("Организация") Тогда
		Организация = Параметры.Организация;
		УправлениеЭлементамиФормыПриИзмененииОрганизации();
	КонецЕсли;
	
	Если Параметры.Свойство("ПланСчетов") Тогда
		ПланСчетов = Параметры.ПланСчетов;
	КонецЕсли;
	
	ДанныеФО = Новый Структура;
	ДанныеФО.Вставить("ПроверкаДокументов", ПолучитьФункциональнуюОпцию("ИспользоватьПроверкуФинансовыхДокументов"));
	
	Элементы.ГруппаСостояниеПроверкиДокументовКПовторнойПроверке.Видимость = ДанныеФО.ПроверкаДокументов;
	
	ДополнительныеПараметры = Новый Структура;
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка, ДополнительныеПараметры);
	
	ПолучитьСостояниеРегламентногоЗадания();
	УправлениеЭлементамиФормыПриИзмененииАвтоматическогоОтраженияВУчете();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьПредставлениеРасписания();
	ОбновитьДанныеОСостоянииДокументовИНастройкахНастройкиФормированияПроводок();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	ВнешниеСобытия = Новый Массив;
	ВнешниеСобытия.Добавить("Запись_ОтражениеДокументовВМеждународномУчете");
	ВнешниеСобытия.Добавить("ЗаконченаНастройкаШаблоновПроводокМеждународногоУчета");
	ВнешниеСобытия.Добавить("ЗакрытаФормаНастройкиНеобходимыхПравилОтраженияВУчете");
	ВнешниеСобытия.Добавить("ИзмененаДатаЗапретаФормированияПроводок");
	
	Если ВнешниеСобытия.Найти(ИмяСобытия) <> Неопределено Тогда
		ОбработкаОповещенияСервер(ИмяСобытия);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если Параметры.Свойство("ДатаОкончанияПериода") Тогда
		ДатаОкончанияПериода = Параметры.ДатаОкончанияПериода;
	КонецЕсли;
	
	Если Параметры.Свойство("Организация") Тогда
		Организация = Параметры.Организация;
	Иначе
		УправлениеЭлементамиФормыПриИзмененииОрганизации();
	КонецЕсли;

	Если Параметры.Свойство("ПланСчетов") Тогда
		ПланСчетов = Параметры.ПланСчетов;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОрганизацияПриИзмененииСервер();
	ОбновитьДанныеОСостоянииДокументовИНастройкахНастройкиФормированияПроводок();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияПериодаПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(ДатаОкончанияПериода) Тогда
		ДатаОкончанияПериода = КонецДня(ДатаОкончанияПериода);
	КонецЕсли;
	ОбновитьДанныеОСостоянииДокументовИНастройкахНастройкиФормированияПроводок();
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусКОтражениюВУчетеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СтатусОтражения = ПредопределенноеЗначение("Перечисление.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчете");
	ПараметрыФормы = ИнициализироватьПараметрыФормыСпискаДокументов(СтатусОтражения);
	ОткрытьФорму("Обработка.ОтражениеДокументовВМеждународномУчете.Форма.ЖурналДокументов", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусОтсутствуютПравилаОтраженияВУчетеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СтатусОтражения = ПредопределенноеЗначение("Перечисление.СтатусыОтраженияВМеждународномУчете.ОтсутствуютПравилаОтраженияВУчете");
	ПараметрыФормы = ИнициализироватьПараметрыФормыСпискаДокументов(СтатусОтражения);
	ОткрытьФорму("Обработка.ОтражениеДокументовВМеждународномУчете.Форма.ЖурналДокументов", ПараметрыФормы, ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура СтатусОжидаетсяОтражениеВРеглУчетеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СтатусОтражения = ПредопределенноеЗначение("Перечисление.СтатусыОтраженияВМеждународномУчете.ОжидаетсяОтражениеВРеглУчете");
	ПараметрыФормы = ИнициализироватьПараметрыФормыСпискаДокументов(СтатусОтражения);
	ОткрытьФорму("Обработка.ОтражениеДокументовВМеждународномУчете.Форма.ЖурналДокументов", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусНарушенаДвойнаяЗаписьФинансовыхРегистровНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СтатусОтражения = ПредопределенноеЗначение("Перечисление.СтатусыОтраженияВМеждународномУчете.НарушенаДвойнаяЗаписьФинансовыхРегистров");
	ПараметрыФормы = ИнициализироватьПараметрыФормыСпискаДокументов(СтатусОтражения);
	ОткрытьФорму("Обработка.ОтражениеДокументовВМеждународномУчете.Форма.ЖурналДокументов", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ТребуетсяНастроитьПравилаОтраженияВУчетеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация", Организация);
	ПараметрыФормы.Вставить("АдресРезультатаПроверки", АдресРезультатаПроверки);
	ОткрытьФорму("Обработка.ОтражениеДокументовВМеждународномУчете.Форма.НеобходимыеПравилаОтражения", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусОтраженоВУчетеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СтатусОтражения = ПредопределенноеЗначение("Перечисление.СтатусыОтраженияВМеждународномУчете.ОтраженоВУчете");
	ПараметрыФормы = ИнициализироватьПараметрыФормыСпискаДокументов(СтатусОтражения);
	ОткрытьФорму("Обработка.ОтражениеДокументовВМеждународномУчете.Форма.ЖурналДокументов", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусКОтражениюВЗакрытомПериодеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СтатусОтражения = ПредопределенноеЗначение("Перечисление.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчете");
	ПараметрыФормы = ИнициализироватьПараметрыФормыСпискаДокументов(СтатусОтражения);
	ПараметрыФормы.Вставить("ДатаЗапрета", ДатаЗапрета);
	ОткрытьФорму("Обработка.ОтражениеДокументовВМеждународномУчете.Форма.ЖурналДокументов", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусКОтражениюВУчетеВручнуюНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СтатусОтражения = ПредопределенноеЗначение("Перечисление.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчетеВручную");
	ПараметрыФормы = ИнициализироватьПараметрыФормыСпискаДокументов(СтатусОтражения);
	ОткрытьФорму("Обработка.ОтражениеДокументовВМеждународномУчете.Форма.ЖурналДокументов", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусОтраженоВУчетеВручнуюНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СтатусОтражения = ПредопределенноеЗначение("Перечисление.СтатусыОтраженияВМеждународномУчете.ОтраженоВУчетеВручную");
	ПараметрыФормы = ИнициализироватьПараметрыФормыСпискаДокументов(СтатусОтражения);
	ОткрытьФорму("Обработка.ОтражениеДокументовВМеждународномУчете.Форма.ЖурналДокументов", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьАвтоматическоеОтражениеВУчетеПриИзменении(Элемент)
	
	СохранитьРеквизитыРегламентногоЗадания();
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеРасписанияНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РедактированиеРасписанияРегламентногоЗадания();
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусПроверенныхДокументовНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = ИнициализироватьПараметрыФормыСпискаДокументов();
	СтатусПроверки = ПредопределенноеЗначение("Перечисление.СтатусыПроверкиФинансовыхДокументов.Проверен");
	ПараметрыФормы.Отбор.Вставить("СтатусПроверки", СтатусПроверки);
	
	ОткрытьФорму("РегистрСведений.СтатусыПроверкиДокументов.Форма.ЖурналДокументов", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусНеПроверенныхДокументовНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = ИнициализироватьПараметрыФормыСпискаДокументов();
	СтатусПроверки = ПредопределенноеЗначение("Перечисление.СтатусыПроверкиФинансовыхДокументов.НеПроверен");
	ПараметрыФормы.Отбор.Вставить("СтатусПроверки", СтатусПроверки);
	
	ОткрытьФорму("РегистрСведений.СтатусыПроверкиДокументов.Форма.ЖурналДокументов", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусДокументовКПроверкеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = ИнициализироватьПараметрыФормыСпискаДокументов();
	СтатусПроверки = ПредопределенноеЗначение("Перечисление.СтатусыПроверкиФинансовыхДокументов.КПовторнойПроверке");
	ПараметрыФормы.Отбор.Вставить("СтатусПроверки", СтатусПроверки);
	
	ОткрытьФорму("РегистрСведений.СтатусыПроверкиДокументов.Форма.ЖурналДокументов", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПланСчетовПриИзменении(Элемент)
	
	ОбновитьДанныеОСостоянииДокументовИНастройкахНастройкиФормированияПроводок();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьСостояние(Команда)
	
	ОбновитьДанныеОСостоянииДокументовИНастройкахНастройкиФормированияПроводок();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтразитьДокументыВМеждународномУчете(Команда)
	
	ДлительнаяОперация = НачатьОтражениеДокументовВМеждународномУчете(Организация, ДатаОкончанияПериода, УникальныйИдентификатор);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработатьРезультатОтраженияДокументов", ЭтотОбъект);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.Заголовок = НСтр("ru = 'Отражение документов в международном учете';
										|en = 'Record documents in financial accounting'");
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
 
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
 
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДатуЗапрета(Команда)
	
	ОткрытьФорму("РегистрСведений.ДатыЗапретаФормированияПроводокМеждународныйУчет.Форма.ДатыЗапретаФормирования", , ЭтаФорма);
	
КонецПроцедуры


&НаКлиенте
Процедура НастроитьШаблоныПроводок(Команда)
	
	ПараметрыФормы = МеждународныйУчетКлиент.ПараметрыОткрытияФормНастройкиПроводок();
	ПараметрыФормы.Организация = Организация;
	ПараметрыФормы.ПланСчетов = ПланСчетов;
	МеждународныйУчетПоДаннымОперативногоУчетаКлиент.ОткрытьНастройкуШаблоновПроводок(ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкуСчетовПоОбъектамУчета(Команда)
	
	ПараметрыФормы = МеждународныйУчетКлиент.ПараметрыОткрытияФормНастройкиПроводок();
	ПараметрыФормы.Организация = Организация;
	ПараметрыФормы.ПланСчетов = ПланСчетов;
	МеждународныйУчетПоДаннымФинансовыхРегистровКлиент.ОткрытьНастройкуСчетовПоОбъектамУчета(ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьСоответствиеСчетовИОборотов(Команда)
	
	ПараметрыФормы = МеждународныйУчетКлиент.ПараметрыОткрытияФормНастройкиПроводок();
	ПараметрыФормы.Организация = Организация;
	ПараметрыФормы.ПланСчетов = ПланСчетов;
	МеждународныйУчетПоДаннымРеглУчетаКлиент.ОткрытьНастройкуСоответствияСчетовИОборотов(ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Общие

&НаСервереБезКонтекста
Функция НачатьОтражениеДокументовВМеждународномУчете(Организация, ДатаОкончания, ИдентификаторФормы)
	
	ПараметрыОтражения = МеждународныйУчетПроведениеСервер.ПараметрыОтраженияВМеждународномУчете();
	ПараметрыОтражения.Организации = Организация;
	ПараметрыОтражения.ДатаОкончания = ДатаОкончания;
	
	Возврат МеждународныйУчетПроведениеСервер.НачатьОтражениеВМеждународномУчете(ПараметрыОтражения, ИдентификаторФормы);
	
КонецФункции

&НаКлиенте
Процедура ОбработатьРезультатОтраженияДокументов(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено ИЛИ Результат.Статус = "Ошибка" Тогда
		ТекстСообщения = НСтр("ru = 'При отражении документов в международном учете произошла ошибка:
			|%1';
			|en = 'An error occurred when recording documents in financial accounting:
			|%1'");
		
		Если Результат = Неопределено Тогда
			ТекстРасшифровки = НСтр("ru = 'Не найдено выполняемое задание.';
									|en = 'Job in progress is not found.'");
		ИначеЕсли ЗначениеЗаполнено(Результат.КраткоеПредставлениеОшибки) Тогда
			ТекстРасшифровки = Результат.КраткоеПредставлениеОшибки;
		Иначе
			ТекстРасшифровки = НСтр("ru = 'Подробности см. в Журнале регистрации.';
									|en = 'For more information, see the event log.'");
		КонецЕсли;
		
		ТекстСообщения = СтрШаблон(ТекстСообщения, ТекстРасшифровки);

		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	ОбработатьРезультатОтраженияДокументовНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьРезультатОтраженияДокументовНаСервере()
	
	ПолучитьСостояниеОтраженияДокументов();
	
	ПолучитьОперацииТребующиеНастройкиШаблонов();
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьСостояниеОтраженияДокументов()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОтражениеДокументов.ДатаОтражения КАК ДатаОтражения,
	|	ОтражениеДокументов.Регистратор КАК Регистратор,
	|	ОтражениеДокументов.Статус КАК Статус
	|ПОМЕСТИТЬ ОтражениеДокументов
	|ИЗ
	|	РегистрСведений.ОтражениеДокументовВМеждународномУчете КАК ОтражениеДокументов
	|ГДЕ
	|	(&ЛюбаяОрганизация ИЛИ ОтражениеДокументов.Организация = &Организация)
	|	И (&ЛюбойПланСчетов ИЛИ ОтражениеДокументов.ПланСчетов = &ПланСчетов)
	|	И ОтражениеДокументов.ДатаОтражения <= &ДатаОкончания
	|	И ТИПЗНАЧЕНИЯ(ОтражениеДокументов.Регистратор) в (&СписокТиповДокументов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ОтражениеДокументов.ДатаОтражения) КАК ДатаНачала
	|ИЗ
	|	ОтражениеДокументов КАК ОтражениеДокументов
	|ГДЕ
	|	ОтражениеДокументов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.ОтраженоВУчетеВручную)
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(ОтражениеДокументов.Регистратор) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ОтражениеДокументов.ДатаОтражения) КАК ДатаНачала,
	|	КОЛИЧЕСТВО(ОтражениеДокументов.Регистратор) КАК Количество
	|ИЗ
	|	ОтражениеДокументов КАК ОтражениеДокументов
	|ГДЕ
	|	ОтражениеДокументов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчетеВручную)
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(ОтражениеДокументов.Регистратор) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ОтражениеДокументов.ДатаОтражения) КАК ДатаНачала
	|ИЗ
	|	ОтражениеДокументов КАК ОтражениеДокументов
	|ГДЕ
	|	ОтражениеДокументов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.ОтраженоВУчете)
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(ОтражениеДокументов.Регистратор) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ОтражениеДокументов.ДатаОтражения) КАК ДатаНачала,
	|	КОЛИЧЕСТВО(ОтражениеДокументов.Регистратор) КАК Количество
	|ИЗ
	|	ОтражениеДокументов КАК ОтражениеДокументов
	|ГДЕ
	|	ОтражениеДокументов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчете)
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(ОтражениеДокументов.Регистратор) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ОтражениеДокументов.ДатаОтражения) КАК ДатаНачала,
	|	КОЛИЧЕСТВО(ОтражениеДокументов.Регистратор) КАК Количество
	|ИЗ
	|	ОтражениеДокументов КАК ОтражениеДокументов
	|ГДЕ
	|	ОтражениеДокументов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.ОжидаетсяОтражениеВРеглУчете)
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(ОтражениеДокументов.Регистратор) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ОтражениеДокументов.ДатаОтражения) КАК ДатаНачала,
	|	КОЛИЧЕСТВО(ОтражениеДокументов.Регистратор) КАК Количество
	|ИЗ
	|	ОтражениеДокументов КАК ОтражениеДокументов
	|ГДЕ
	|	ОтражениеДокументов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.ОтсутствуютПравилаОтраженияВУчете)
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(ОтражениеДокументов.Регистратор) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ОтражениеДокументов.ДатаОтражения) КАК ДатаНачала,
	|	КОЛИЧЕСТВО(ОтражениеДокументов.Регистратор) КАК Количество
	|ИЗ
	|	ОтражениеДокументов КАК ОтражениеДокументов
	|ГДЕ
	|	ОтражениеДокументов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчете)
	|	И ОтражениеДокументов.ДатаОтражения <= &ДатаЗапрета
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ОтражениеДокументов.Регистратор) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ОтражениеДокументов.ДатаОтражения) КАК ДатаНачала,
	|	КОЛИЧЕСТВО(ОтражениеДокументов.Регистратор) КАК Количество
	|ИЗ
	|	ОтражениеДокументов КАК ОтражениеДокументов
	|ГДЕ
	|	ОтражениеДокументов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.НарушенаДвойнаяЗаписьФинансовыхРегистров)
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(ОтражениеДокументов.Регистратор) > 0
	|";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ЛюбаяОрганизация", Не ЗначениеЗаполнено(Организация));
	Запрос.УстановитьПараметр("ПланСчетов", ПланСчетов);
	Запрос.УстановитьПараметр("ЛюбойПланСчетов", Не ЗначениеЗаполнено(ПланСчетов));
	Запрос.УстановитьПараметр("ДатаОкончания", ?(ЗначениеЗаполнено(ДатаОкончанияПериода), ДатаОкончанияПериода, Дата(2399, 1, 1)));
	Запрос.УстановитьПараметр("ДатаЗапрета", ДатаЗапрета);
	Запрос.УстановитьПараметр("СписокТиповДокументов", Обработки.ОтражениеДокументовВМеждународномУчете.ТипыДокументовДляОтбора());
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	Выборка = МассивРезультатов[1].Выбрать();
	Если Выборка.Следующий() Тогда
		ТекстСтатуса = НСтр("ru = 'Документы, отраженные в учете вручную, по %Дата%';
							|en = 'Documents posted in financial accounting manually before %Дата%'");
		СтатусОтраженоВУчетеВручную = СтрЗаменить(ТекстСтатуса, "%Дата%", Формат(Выборка.ДатаНачала, "ДЛФ=Д"));
		Элементы.СтатусОтраженоВУчетеВручную.ЦветТекста = ЦветаСтиля.ГиперссылкаЦвет;
		Элементы.СтатусОтраженоВУчетеВручную.Гиперссылка = Истина;
	Иначе
		СтатусОтраженоВУчетеВручную = НСтр("ru = 'Нет документов, отраженных в международном учете вручную.';
											|en = 'No documents which are manually recorded in financial accounting.'");
		Элементы.СтатусОтраженоВУчетеВручную.ЦветТекста = ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента;
		Элементы.СтатусОтраженоВУчетеВручную.Гиперссылка = Ложь;
	КонецЕсли;
	
	Выборка = МассивРезультатов[2].Выбрать();
	Если Выборка.Следующий() Тогда
		ТекстСтатуса = НСтр("ru = 'Документы, ожидающие ручного отражения в международном учете (%Количество%), с %Дата%';
							|en = 'Documents awaiting manual financial accounting recording (%Количество%), from %Дата%'");
		ТекстСтатуса = СтрЗаменить(ТекстСтатуса, "%Количество%", Выборка.Количество);
		СтатусКОтражениюВУчетеВручную = СтрЗаменить(ТекстСтатуса, "%Дата%", Формат(Выборка.ДатаНачала, "ДЛФ=Д"));
		Элементы.СтатусКОтражениюВУчетеВручную.ЦветТекста = ЦветаСтиля.ГиперссылкаЦвет;
		Элементы.СтатусКОтражениюВУчетеВручную.Гиперссылка = Истина;
	Иначе
		СтатусКОтражениюВУчетеВручную = НСтр("ru = 'Нет документов, ожидающих ручного отражения в международном учете.';
											|en = 'No documents awaiting manual financial accounting recording.'");
		Элементы.СтатусКОтражениюВУчетеВручную.ЦветТекста = ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента;
		Элементы.СтатусКОтражениюВУчетеВручную.Гиперссылка = Ложь;
	КонецЕсли;
	
	Выборка = МассивРезультатов[3].Выбрать();
	Если Выборка.Следующий() Тогда
		ТекстСтатуса = НСтр("ru = 'Отраженные в учете документы, по %Дата%';
							|en = 'Documents recorded in accounting, up to %Дата%'");
		СтатусОтраженоВУчете = СтрЗаменить(ТекстСтатуса, "%Дата%", Формат(Выборка.ДатаНачала, "ДЛФ=Д"));
		Элементы.СтатусОтраженоВУчете.ЦветТекста = ЦветаСтиля.ГиперссылкаЦвет;
		Элементы.СтатусОтраженоВУчете.Гиперссылка = Истина;
		
		ПроводкиМогутИзмениться = "";
		НевыполненныеЗадания = МеждународныйУчетЗакрытиеМесяца.НевыполненныеОтложенныеЗадания(
			Перечисления.СтатусыОтраженияВМеждународномУчете.ОтраженоВУчете, Выборка.ДатаНачала, Организация, Неопределено, Истина);
		Если НевыполненныеЗадания.Количество() > 0 Тогда
			НевыполненныеЗадания.Добавить(НСтр("ru = 'Проводки отраженных в учете документов могут измениться.';
												|en = 'Entries of the documents recorded in accounting might change.'"));
			ПроводкиМогутИзмениться = Новый ФорматированнаяСтрока(
				СтрСоединить(НевыполненныеЗадания, " "),,
				ЦветаСтиля.ТекстИнформационнойНадписи);
		КонецЕсли;
		Элементы.ГруппаПроводкиМогутИзмениться.Видимость = ЗначениеЗаполнено(ПроводкиМогутИзмениться);
	Иначе
		СтатусОтраженоВУчете = НСтр("ru = 'Нет документов, отраженных в международном учете.';
									|en = 'No documents recorded in financial accounting.'");
		Элементы.СтатусОтраженоВУчете.ЦветТекста = ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента;
		Элементы.СтатусОтраженоВУчете.Гиперссылка = Ложь;
		Элементы.ГруппаПроводкиМогутИзмениться.Видимость = Ложь;
	КонецЕсли;
	
	Выборка = МассивРезультатов[4].Выбрать();
	Если Выборка.Следующий() Тогда
		ТекстСтатуса = НСтр("ru = 'Документы, ожидающие автоматического отражения в международном учете (%Количество%), с %Дата%';
							|en = 'Documents awaiting automatic financial accounting recording (%Количество%), from %Дата%'");
		ТекстСтатуса = СтрЗаменить(ТекстСтатуса, "%Количество%", Выборка.Количество);
		СтатусКОтражениюВУчете = СтрЗаменить(ТекстСтатуса, "%Дата%", Формат(Выборка.ДатаНачала, "ДЛФ=Д"));
		Элементы.СтатусКОтражениюВУчете.ЦветТекста = ЦветаСтиля.ГиперссылкаЦвет;
		Элементы.СтатусКОтражениюВУчете.Гиперссылка = Истина;
	Иначе
		СтатусКОтражениюВУчете = НСтр("ru = 'Нет документов, ожидающих автоматического отражения в международном учете.';
										|en = 'No documents awaiting automatic financial accounting recording.'");
		Элементы.СтатусКОтражениюВУчете.ЦветТекста = ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента;
		Элементы.СтатусКОтражениюВУчете.Гиперссылка = Ложь;
	КонецЕсли;
	
	Выборка = МассивРезультатов[5].Выбрать();
	Если Выборка.Следующий() Тогда
		ТекстСтатуса = НСтр("ru = 'Документы не отраженные в учете из-за ожидания отражения в регл. учете (%Количество%), с %Дата%';
							|en = 'Documents not recorded awaiting recording in local accounting (%Количество%), from %Дата%'");
		ТекстСтатуса = СтрЗаменить(ТекстСтатуса, "%Количество%", Выборка.Количество);
		СтатусОжидаетсяОтражениеВРеглУчете = СтрЗаменить(ТекстСтатуса, "%Дата%", Формат(Выборка.ДатаНачала, "ДЛФ=Д"));
		Элементы.СтатусОжидаетсяОтражениеВРеглУчете.ЦветТекста = ЦветаСтиля.ГиперссылкаЦвет;
		Элементы.СтатусОжидаетсяОтражениеВРеглУчете.Гиперссылка = Истина;
		Элементы.СтатусОжидаетсяОтражениеВРеглУчете.Видимость = Истина;
	Иначе
		Элементы.СтатусОжидаетсяОтражениеВРеглУчете.Видимость = Ложь;
	КонецЕсли;
	
	Выборка = МассивРезультатов[6].Выбрать();
	Если Выборка.Следующий() Тогда
		ТекстСтатуса = НСтр("ru = 'Документы не отраженные в учете из-за ошибок (%Количество%), с %Дата%';
							|en = 'Documents not recorded in accounting due to errors (%Количество%), from %Дата%'");
		ТекстСтатуса = СтрЗаменить(ТекстСтатуса, "%Количество%", Выборка.Количество);
		СтатусОтсутствуютПравилаОтраженияВУчете = СтрЗаменить(ТекстСтатуса, "%Дата%", Формат(Выборка.ДатаНачала, "ДЛФ=Д"));
		Элементы.СтатусОтсутствуютПравилаОтраженияВУчете.ЦветТекста = ЦветаСтиля.ПросроченныеДанныеЦвет;
		Элементы.СтатусОтсутствуютПравилаОтраженияВУчете.Гиперссылка = Истина;
		Элементы.СтатусОтсутствуютПравилаОтраженияВУчете.Видимость = Истина;
	Иначе
		Элементы.СтатусОтсутствуютПравилаОтраженияВУчете.Видимость = Ложь;
	КонецЕсли;

	Выборка = МассивРезультатов[7].Выбрать();
	Если Выборка.Следующий() Тогда
		ТекстСтатуса = НСтр("ru = 'в том числе в закрытом периоде (%Количество%)';
							|en = 'including in the closed period (%Количество%)'");
		ТекстСтатуса = СтрЗаменить(ТекстСтатуса, "%Количество%", Выборка.Количество);
		ТекстСтатуса = СтрЗаменить(ТекстСтатуса, "%Дата%", Формат(ДатаЗапрета, "ДЛФ=Д"));
		СтатусКОтражениюВЗакрытомПериоде = ТекстСтатуса;
		Элементы.СтатусКОтражениюВЗакрытомПериоде.ЦветТекста = ЦветаСтиля.ПросроченныеДанныеЦвет;
		Элементы.ГруппаЗакрытыйПериод.Видимость = Истина;
	Иначе
		Элементы.ГруппаЗакрытыйПериод.Видимость = Ложь;
	КонецЕсли;
	
	Выборка = МассивРезультатов[8].Выбрать();
	Если Выборка.Следующий() Тогда
		ТекстСтатуса = НСтр("ru = 'Документы не отраженные в учете из-за нарушения двойной записи в финансовых регистрах (%Количество%), с %Дата%';
							|en = 'Documents not recorded in accounting due to violation of double entry in financial registers (%Количество%), from %Дата%'");
		ТекстСтатуса = СтрЗаменить(ТекстСтатуса, "%Количество%", Выборка.Количество);
		СтатусНарушенаДвойнаяЗаписьФинансовыхРегистров = СтрЗаменить(ТекстСтатуса, "%Дата%", Формат(Выборка.ДатаНачала, "ДЛФ=Д"));
		Элементы.СтатусНарушенаДвойнаяЗаписьФинансовыхРегистров.ЦветТекста = ЦветаСтиля.ПросроченныеДанныеЦвет;
		Элементы.СтатусНарушенаДвойнаяЗаписьФинансовыхРегистров.Гиперссылка = Истина;
		Элементы.СтатусНарушенаДвойнаяЗаписьФинансовыхРегистров.Видимость = Истина;
	Иначе
		Элементы.СтатусНарушенаДвойнаяЗаписьФинансовыхРегистров.Видимость = Ложь;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);

КонецПроцедуры

&НаСервере
Процедура ПолучитьОперацииТребующиеНастройкиШаблонов()
	
	ПараметрыПроверки = Обработки.ОтражениеДокументовВМеждународномУчете.ПараметрыПроверкиПравилОтражения();
	Если ЗначениеЗаполнено(Организация) Тогда
		ПараметрыПроверки.Организация = Организация;
	КонецЕсли;
	Результат = Обработки.ОтражениеДокументовВМеждународномУчете.ПроверитьНастройкуПравилОтраженияУчете(ПараметрыПроверки);
	
	АдресРезультатаПроверки = ПоместитьВоВременноеХранилище(Результат, УникальныйИдентификатор);
	
	КоличествоОшибок = 
		Результат.ХозяйственныеОперацииБезПравилОтражения.Количество() + Результат.СчетаБезПравилОтражения.Количество();
	
	Если КоличествоОшибок > 0 Тогда
		ТекстСтатуса = НСтр("ru = 'Требуется настроить правила отражения в учете (%Количество%)';
							|en = 'Configure rules of posting in accounting (%Количество%)'");
		ТекстСтатуса = СтрЗаменить(ТекстСтатуса, "%Количество%", КоличествоОшибок);
		ТребуетсяНастроитьПравилаОтраженияВУчете = ТекстСтатуса;
		Элементы.ТребуетсяНастроитьПравилаОтраженияВУчете.Видимость = Истина;
		Элементы.ТребуетсяНастроитьПравилаОтраженияВУчете.ЦветТекста = ЦветаСтиля.ПросроченныеДанныеЦвет;
	Иначе
		Элементы.ТребуетсяНастроитьПравилаОтраженияВУчете.Видимость = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеОСостоянииДокументовИНастройкахНастройкиФормированияПроводок()
	
	ОбновитьДатуЗапретаОтраженияНаСервере();
	ПолучитьСостояниеОтраженияДокументов();
	ПолучитьОперацииТребующиеНастройкиШаблонов();
	ПолучитьСостояниеРегламентногоЗадания();
	СформироватьПредставлениеНастройкиФормированияПроводок();
	ЗапуститьЗаданияПоОбновлениюДанныхСостоянияПроверкиДокументов();
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	Для Каждого КлючИЗначение Из СоответствиеОбновленияСостоянийОтражения Цикл
		
		Если КлючИЗначение.Ключ = ПредопределенноеЗначение("Перечисление.СтатусыПроверкиФинансовыхДокументов.Проверен") Тогда
			
			ОповещениеОЗавершенииОбновленияСостоянияПроверкиДокументов = Новый ОписаниеОповещения("ОбработатьРезультатОбновленияСостоянияПроверкиДокументов_Проверенные", ЭтотОбъект);
			
		ИначеЕсли КлючИЗначение.Ключ = ПредопределенноеЗначение("Перечисление.СтатусыПроверкиФинансовыхДокументов.НеПроверен") Тогда
			
			ОповещениеОЗавершенииОбновленияСостоянияПроверкиДокументов = Новый ОписаниеОповещения("ОбработатьРезультатОбновленияСостоянияПроверкиДокументов_НеПроверенные", ЭтотОбъект);
			
		ИначеЕсли КлючИЗначение.Ключ = ПредопределенноеЗначение("Перечисление.СтатусыПроверкиФинансовыхДокументов.КПовторнойПроверке") Тогда
			
			ОповещениеОЗавершенииОбновленияСостоянияПроверкиДокументов = Новый ОписаниеОповещения("ОбработатьРезультатОбновленияСостоянияПроверкиДокументов_КПовторнойПроверке", ЭтотОбъект);
			
		Иначе
			
			Продолжить;
			
		КонецЕсли;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(КлючИЗначение.Значение, ОповещениеОЗавершенииОбновленияСостоянияПроверкиДокументов, ПараметрыОжидания);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция ИнициализироватьПараметрыФормыСпискаДокументов(СтатусОтражения=Неопределено)

	ПараметрыФормы = Новый Структура();
	
	Отбор = Новый Структура();
	Если ЗначениеЗаполнено(Организация) Тогда
		Отбор.Вставить("Организация", Организация);
	КонецЕсли;
	Если ЗначениеЗаполнено(ПланСчетов) Тогда
		Отбор.Вставить("ПланСчетов", ПланСчетов);
	КонецЕсли;
	Если ЗначениеЗаполнено(СтатусОтражения) Тогда
		Отбор.Вставить("СтатусОтражения", СтатусОтражения);
	КонецЕсли;
	ПараметрыФормы.Вставить("Отбор", Отбор);
	
	ПараметрыФормы.Вставить("ДатаОкончанияПериода", ?(ЗначениеЗаполнено(ДатаОкончанияПериода), ДатаОкончанияПериода, '39991231'));
	
	Возврат ПараметрыФормы;

КонецФункции

&НаСервере
Процедура ОбработкаОповещенияСервер(ИмяСобытия)

	Если ИмяСобытия = "Запись_ОтражениеДокументовВМеждународномУчете" Тогда
		ПолучитьСостояниеОтраженияДокументов();
		ПолучитьОперацииТребующиеНастройкиШаблонов();
	ИначеЕсли ИмяСобытия = "ЗаконченаНастройкаШаблоновПроводокМеждународногоУчета"
		ИЛИ ИмяСобытия = "ЗакрытаФормаНастройкиНеобходимыхПравилОтраженияВУчете" Тогда
		ПолучитьОперацииТребующиеНастройкиШаблонов();
	ИначеЕсли ИмяСобытия = "ИзмененаДатаЗапретаФормированияПроводок" Тогда
		ОбновитьДанныеЗакрытогоПериода();
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция МассивДоступныхОрганизаций(Организация)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДанныеСправочника.Ссылка КАК Организация
	|ИЗ
	|	Справочник.Организации КАК ДанныеСправочника
	|ГДЕ
	|	ДанныеСправочника.Ссылка = &Организация
	|	ИЛИ &Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|	И НЕ ДанныеСправочника.Предопределенный";
	Запрос.УстановитьПараметр("Организация", Организация);
	МассивОрганизаций = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Организация");
	
	Возврат МассивОрганизаций;
	
КонецФункции

#КонецОбласти

#Область АвтоматическоеОтражениеВУчете

&НаКлиенте
Процедура ОбновитьПредставлениеРасписания()
	
	Если РасписаниеРегламентногоЗадания = Неопределено
	 ИЛИ РасписаниеРегламентногоЗадания = Новый РасписаниеРегламентногоЗадания Тогда
		ПредставлениеРасписания = НСтр("ru = 'Расписание не задано';
										|en = 'Schedule is not set'");
	Иначе
		ПредставлениеРасписания = Строка(РасписаниеРегламентногоЗадания);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьСостояниеРегламентногоЗадания()

	УстановитьПривилегированныйРежим(Истина);
	
	Задание = РегламентныеЗаданияСервер.Задание(Метаданные.РегламентныеЗадания.ОтражениеДокументовВМеждународномУчете);
	Если ОбщегоНазначения.РазделениеВключено() И ЗначениеЗаполнено(Задание.Шаблон) Тогда
		Расписание = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Задание.Шаблон, "Расписание"); //ХранилищеЗначения
		РасписаниеРегламентногоЗадания = Расписание.Получить();
	Иначе
		РасписаниеРегламентногоЗадания	= Задание.Расписание;
	КонецЕсли;
	ИспользоватьАвтоматическоеОтражениеВУчете = Задание.Использование;
	
	СвойстваПоследнегоФоновогоЗадания = ОбщегоНазначенияУТ.ПолучитьСостояниеПоследнегоЗадания(Задание);
	Если СвойстваПоследнегоФоновогоЗадания = Неопределено
		ИЛИ НЕ ЗначениеЗаполнено(СвойстваПоследнегоФоновогоЗадания.ДатаЗавершения) Тогда
		СостояниеАвтоматическогоОтраженияВУчете = НСтр("ru = 'Не выполнялось';
														|en = 'Not run'");
	Иначе
		СостояниеАвтоматическогоОтраженияВУчете = Строка(СвойстваПоследнегоФоновогоЗадания.Состояние) + ": "
			+ Строка(СвойстваПоследнегоФоновогоЗадания.ДатаЗавершения);
	КонецЕсли;
	
	Элементы.ИспользоватьАвтоматическоеОтражениеВУчете.Видимость = НЕ ОбщегоНазначения.РазделениеВключено();
	Элементы.ПредставлениеРасписания.Гиперссылка = НЕ ОбщегоНазначения.РазделениеВключено();
	
	УстановитьПривилегированныйРежим(Ложь);

КонецПроцедуры

&НаКлиенте
Процедура РедактированиеРасписанияРегламентногоЗадания()
	
	Если РасписаниеРегламентногоЗадания = Неопределено Тогда
		РасписаниеРегламентногоЗадания = Новый РасписаниеРегламентногоЗадания;
	КонецЕсли;
	
	Диалог = Новый ДиалогРасписанияРегламентногоЗадания(РасписаниеРегламентногоЗадания);
	Диалог.Показать(Новый ОписаниеОповещения("ОбработкаВыбораРасписания", ЭтотОбъект, Новый Структура("Диалог", Диалог)));
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораРасписания(Расписание, ДополнительныеПараметры) Экспорт
	
	Если Расписание <> Неопределено Тогда
		РасписаниеРегламентногоЗадания = Расписание;
		СохранитьРеквизитыРегламентногоЗадания();
	КонецЕсли;
	ОбновитьПредставлениеРасписания();
	
КонецПроцедуры

&НаСервере
Процедура СохранитьРеквизитыРегламентногоЗадания()

	УстановитьПривилегированныйРежим(Истина);
	
	Задание = РегламентныеЗаданияСервер.Задание(Метаданные.РегламентныеЗадания.ОтражениеДокументовВМеждународномУчете);
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("Использование", ИспользоватьАвтоматическоеОтражениеВУчете);
	ПараметрыЗадания.Вставить("Расписание", РасписаниеРегламентногоЗадания);
	РегламентныеЗаданияСервер.ИзменитьЗадание(Задание, ПараметрыЗадания);
	
	УстановитьПривилегированныйРежим(Ложь);
	
	УправлениеЭлементамиФормыПриИзмененииАвтоматическогоОтраженияВУчете();
	
КонецПроцедуры

#КонецОбласти

#Область НастройкиФормированияПроводок

&НаСервере
Процедура СформироватьПредставлениеНастройкиФормированияПроводок()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СУММА(1) КАК Количество
	|ИЗ
	|	РегистрСведений.ПланыСчетовМеждународногоУчетаОрганизаций.СрезПоследних(
	|		&ДатаОкончанияПериода,
	|		Организация = &Организация ИЛИ &ВсеОрганизации) КАК ПланыСчетовМеждународногоУчета
	|ГДЕ
	|	ПланыСчетовМеждународногоУчета.НастройкаФормированияПроводок <> ЗНАЧЕНИЕ(Справочник.НастройкиФормированияПроводокМеждународногоУчета.ПустаяСсылка)
	|";
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ДатаОкончанияПериода", ДатаОкончанияПериода);
	Запрос.УстановитьПараметр("ВсеОрганизации", НЕ ЗначениеЗаполнено(Организация));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() И Выборка.Количество <> Null Тогда
		КоличествоЗаписей = Выборка.Количество;
	Иначе
		КоличествоЗаписей = 0;
	КонецЕсли;
	
	ТекстСостояния = "";
	ЦветТекста = Неопределено;
	Если ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоПлановСчетовВМеждународномУчете") Тогда
		Если КоличествоЗаписей > 0 Тогда
			ТекстСостояния = СтрШаблон(НСтр("ru = 'Использование планов счетов (%1)';
											|en = 'Use charts of accounts (%1)'"), КоличествоЗаписей);
			ЦветТекста = ЦветаСтиля.ГиперссылкаЦвет;
		Иначе
			ТекстСостояния = СтрШаблон(НСтр("ru = 'Нет настроек использования планов счетов';
											|en = 'No settings for using charts of accounts'"));
			ЦветТекста = ЦветаСтиля.ПросроченныеДанныеЦвет;
		КонецЕсли;
		НавигационнаяСсылка = "ОткрытьПланыСчетовМеждународногоУчетаОрганизаций";
	Иначе
		Если КоличествоЗаписей > 0 Тогда
			ТекстСостояния = СтрШаблон(НСтр("ru = 'Использование международного учета (%1)';
											|en = 'Use of financial accounting (%1)'"), КоличествоЗаписей);
			ЦветТекста = ЦветаСтиля.ГиперссылкаЦвет;
		Иначе
			ТекстСостояния = СтрШаблон(НСтр("ru = 'Нет настроек использования международного учета';
											|en = 'No financial accounting use settings'"));
			ЦветТекста = ЦветаСтиля.ПросроченныеДанныеЦвет;
		КонецЕсли;
		НавигационнаяСсылка = "ОткрытьПланыСчетовМеждународногоУчетаОрганизаций";
	КонецЕсли;
	СостояниеНастройкиФормированияПроводок = Новый ФорматированнаяСтрока(ТекстСостояния, , ЦветТекста, , НавигационнаяСсылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьПланыСчетовМеждународногоУчетаОрганизаций" Тогда
		СтандартнаяОбработка = Ложь;
		
		ОткрытьФорму("РегистрСведений.ПланыСчетовМеждународногоУчетаОрганизаций.Форма.РабочееМесто",
			Новый Структура("Организация", Организация));
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область УправлениеЭлементамиФормы

&НаСервере
Процедура ОрганизацияПриИзмененииСервер()
	
	УправлениеЭлементамиФормыПриИзмененииОрганизации();
	
КонецПроцедуры

&НаСервере
Процедура УправлениеЭлементамиФормыПриИзмененииОрганизации()

	Если Не ЗначениеЗаполнено(Организация) Тогда
		
		Элементы.ГруппаЗакрытыйПериод.Видимость = Ложь;
		ОбновитьДатуЗапретаОтраженияНаСервере();
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УправлениеЭлементамиФормыПриИзмененииАвтоматическогоОтраженияВУчете()

	Элементы.ПредставлениеРасписания.Доступность = ИспользоватьАвтоматическоеОтражениеВУчете;
	Элементы.ПредставлениеРасписания.Гиперссылка = НЕ ОбщегоНазначения.РазделениеВключено();
	Элементы.СостояниеАвтоматическогоОтраженияВУчете.Доступность = ИспользоватьАвтоматическоеОтражениеВУчете;

КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеЗакрытогоПериода()
	
	ОбновитьДатуЗапретаОтраженияНаСервере();
	Если ЗначениеЗаполнено(Организация) Тогда
		ПолучитьСостояниеОтраженияДокументов();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДатуЗапретаОтраженияНаСервере()
	
	ДатаЗапрета = МеждународныйУчетОбщегоНазначения.ДатаЗапретаФормированияПроводок(Организация);
	Элементы.УстановитьДатуЗапрета.Заголовок = МеждународныйУчетОбщегоНазначения.ПредставлениеКомандыУстановитьДатуЗапрета(ДатаЗапрета);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеПоОтражениюИПроверкеДокументовВУчете(РезультатВыполненияФоновогоЗадания, ВидОперации)
	
	#Область УстановкаТекстаСообщенийОНаличииДокументов
	СоответствиеСообщенияНаличияДокументовВидуОперации = Новый Соответствие;
	СоответствиеСообщенияНаличияДокументовВидуОперации.Вставить(ПредопределенноеЗначение("Перечисление.СтатусыПроверкиФинансовыхДокументов.Проверен") ,
		НСтр("ru = 'Проверенные документы (%Количество%), по %Дата%';
			|en = 'Checked documents (%Количество%), till %Дата%'"));
	СоответствиеСообщенияНаличияДокументовВидуОперации.Вставить(ПредопределенноеЗначение("Перечисление.СтатусыПроверкиФинансовыхДокументов.НеПроверен"),
		НСтр("ru = 'Документы, требующие проверки (%Количество%), с %Дата%';
			|en = 'Documents requiring check (%Количество%), from %Дата%'"));
	СоответствиеСообщенияНаличияДокументовВидуОперации.Вставить(ПредопределенноеЗначение("Перечисление.СтатусыПроверкиФинансовыхДокументов.КПовторнойПроверке"),
		НСтр("ru = 'Документы, требующие повторной проверки (%Количество%), с %Дата%';
			|en = 'Documents requiring recheck (%Количество%), from %Дата%'"));
	#КонецОбласти
	
	#Область УстановкаТекстаСообщенийОбОтсутствииДокументов
	СоответствиеСообщенияОбОтсутствииДокументовВидуОперации = Новый Соответствие;
	СоответствиеСообщенияОбОтсутствииДокументовВидуОперации.Вставить(ПредопределенноеЗначение("Перечисление.СтатусыПроверкиФинансовыхДокументов.Проверен") ,
		НСтр("ru = 'Нет проверенных документов.';
			|en = 'No checked documents.'"));
	СоответствиеСообщенияОбОтсутствииДокументовВидуОперации.Вставить(ПредопределенноеЗначение("Перечисление.СтатусыПроверкиФинансовыхДокументов.НеПроверен"),
		НСтр("ru = 'Нет документов, требующих проверки.';
			|en = 'No documents requiring check.'"));
	СоответствиеСообщенияОбОтсутствииДокументовВидуОперации.Вставить(ПредопределенноеЗначение("Перечисление.СтатусыПроверкиФинансовыхДокументов.КПовторнойПроверке"),
		НСтр("ru = 'Нет документов, требующих повторной проверки.';
			|en = 'No documents requiring recheck.'"));
	#КонецОбласти
	
	#Область УстановкаТекстаСообщенийОбОшибкеПриВыполнении
	СоответствиеСообщенияОбОшибкеПриВыполненииВидуОперации = Новый Соответствие;
	СоответствиеСообщенияОбОшибкеПриВыполненииВидуОперации.Вставить(ПредопределенноеЗначение("Перечисление.СтатусыПроверкиФинансовыхДокументов.Проверен") ,
		НСтр("ru = 'Получение данных о непроверенных документах завершилось с ошибкой.';
			|en = 'An error occurred when receiving unchecked document data.'"));
	СоответствиеСообщенияОбОшибкеПриВыполненииВидуОперации.Вставить(ПредопределенноеЗначение("Перечисление.СтатусыПроверкиФинансовыхДокументов.НеПроверен"),
		НСтр("ru = 'Получение данных о документах, требующих проверки, завершилось с ошибкой.';
			|en = 'An error occurred when receiving data of documents requiring check.'"));
	СоответствиеСообщенияОбОшибкеПриВыполненииВидуОперации.Вставить(ПредопределенноеЗначение("Перечисление.СтатусыПроверкиФинансовыхДокументов.КПовторнойПроверке"),
		НСтр("ru = 'Получение данных о документах, требующих повторной проверки, завершилось с ошибкой.';
			|en = 'An error occurred when receiving data of the documents requiring recheck.'"));
	#КонецОбласти
	
	#Область УстановкаТекстаСообщенийОбОбновленииДанных
	СоответствиеСообщенияОбОбновленииДанныхВидуОперации = Новый Соответствие;
	СоответствиеСообщенияОбОбновленииДанныхВидуОперации.Вставить(ПредопределенноеЗначение("Перечисление.СтатусыПроверкиФинансовыхДокументов.Проверен") ,
		НСтр("ru = 'Получение проверенных документов...';
			|en = 'Receiving checked documents...'"));
	СоответствиеСообщенияОбОбновленииДанныхВидуОперации.Вставить(ПредопределенноеЗначение("Перечисление.СтатусыПроверкиФинансовыхДокументов.НеПроверен"),
		НСтр("ru = 'Получение документов, требующих проверки...';
			|en = 'Receiving documents requiring check...'"));
	СоответствиеСообщенияОбОбновленииДанныхВидуОперации.Вставить(ПредопределенноеЗначение("Перечисление.СтатусыПроверкиФинансовыхДокументов.КПовторнойПроверке"),
		НСтр("ru = 'Получение документов, требующих повторной проверки...';
			|en = 'Receiving documents requiring recheck...'"));
	#КонецОбласти
	
	#Область УстановкаИменРеквизитов
	СоответствиеИменРеквизитовВидуОперации = Новый Соответствие;
	СоответствиеИменРеквизитовВидуОперации.Вставить(ПредопределенноеЗначение("Перечисление.СтатусыПроверкиФинансовыхДокументов.Проверен") , "СтатусПроверенныхДокументов");
	СоответствиеИменРеквизитовВидуОперации.Вставить(ПредопределенноеЗначение("Перечисление.СтатусыПроверкиФинансовыхДокументов.НеПроверен"), "СтатусНеПроверенныхДокументов");
	СоответствиеИменРеквизитовВидуОперации.Вставить(ПредопределенноеЗначение("Перечисление.СтатусыПроверкиФинансовыхДокументов.КПовторнойПроверке"), "СтатусДокументовКПроверке");
	#КонецОбласти
	
	ИмяРеквизита = СоответствиеИменРеквизитовВидуОперации.Получить(ВидОперации);
		
	Если РезультатВыполненияФоновогоЗадания.Статус = "Выполнено" Тогда
	
		Результат = ПолучитьИзВременногоХранилища(РезультатВыполненияФоновогоЗадания.АдресРезультата); // РезультатЗапроса -
		
		Выборка = Результат.Выбрать();
		Если Выборка.Следующий() И Выборка.Количество <> 0 Тогда
			ТекстСтатуса = СоответствиеСообщенияНаличияДокументовВидуОперации.Получить(ВидОперации);
			ТекстСтатуса = СтрЗаменить(ТекстСтатуса, "%Количество%", Выборка.Количество);
			ЭтотОбъект[ИмяРеквизита] = СтрЗаменить(ТекстСтатуса, "%Дата%", Формат(Выборка.ДатаНачала, "ДЛФ=Д"));
			Элементы[ИмяРеквизита].ЦветТекста = ?(ВидОперации = "НеУказаныСчетаУчета", ЦветаСтиля.ПросроченныеДанныеЦвет, ЦветаСтиля.ГиперссылкаЦвет);
			Элементы[ИмяРеквизита].Гиперссылка = Истина;
		Иначе
			ЭтотОбъект[ИмяРеквизита] = СоответствиеСообщенияОбОтсутствииДокументовВидуОперации.Получить(ВидОперации);
			Элементы[ИмяРеквизита].ЦветТекста = ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента;
			Элементы[ИмяРеквизита].Гиперссылка = Ложь;
		КонецЕсли;
		
	ИначеЕсли РезультатВыполненияФоновогоЗадания.Статус = "Ошибка" Тогда
		ЭтотОбъект[ИмяРеквизита] = СоответствиеСообщенияОбОшибкеПриВыполненииВидуОперации.Получить(ВидОперации) + 
			" " + НСтр("ru = 'Подробнее см. в журнале регистрации.';
						|en = 'For more information, see the event log.'");
		Элементы[ИмяРеквизита].ЦветТекста = ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи;
		Элементы[ИмяРеквизита].Гиперссылка = Ложь;
	Иначе
		ЭтотОбъект[ИмяРеквизита] = СоответствиеСообщенияОбОбновленииДанныхВидуОперации.Получить(ВидОперации);
		Элементы[ИмяРеквизита].ЦветТекста = ЦветаСтиля.ТекстСправочнойНадписи;
		Элементы[ИмяРеквизита].Гиперссылка = Ложь;
	КонецЕсли;
	
	Если ВидОперации = ПредопределенноеЗначение("Перечисление.СтатусыПроверкиФинансовыхДокументов.Проверен") Тогда
		
		Элементы.КартинкаОбновлениеСостояниеПроверенные.Видимость = Не (РезультатВыполненияФоновогоЗадания.Статус = "Выполнено"
																	ИЛИ РезультатВыполненияФоновогоЗадания.Статус = "Ошибка");
		
	ИначеЕсли ВидОперации = ПредопределенноеЗначение("Перечисление.СтатусыПроверкиФинансовыхДокументов.НеПроверен") Тогда
		
		Элементы.КартинкаОбновлениеСостояниеНеПроверенные.Видимость = Не (РезультатВыполненияФоновогоЗадания.Статус = "Выполнено"
																	ИЛИ РезультатВыполненияФоновогоЗадания.Статус = "Ошибка");
		
	ИначеЕсли ВидОперации = ПредопределенноеЗначение("Перечисление.СтатусыПроверкиФинансовыхДокументов.КПовторнойПроверке") Тогда
		
		Элементы.КартинкаОбновлениеСостояниеКПовторнойПроверке.Видимость = Не (РезультатВыполненияФоновогоЗадания.Статус = "Выполнено"
																	ИЛИ РезультатВыполненияФоновогоЗадания.Статус = "Ошибка");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ФоновыеЗаданияПоОбновлениюДанных

&НаКлиенте
Процедура ОбработатьРезультатОбновленияСостоянияПроверкиДокументов_Проверенные(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ Результат = Неопределено Тогда
		
		ОбновитьДанные(ПредопределенноеЗначение("Перечисление.СтатусыПроверкиФинансовыхДокументов.Проверен"), Результат);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатОбновленияСостоянияПроверкиДокументов_НеПроверенные(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ Результат = Неопределено Тогда
		
		ОбновитьДанные(ПредопределенноеЗначение("Перечисление.СтатусыПроверкиФинансовыхДокументов.НеПроверен"), Результат);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатОбновленияСостоянияПроверкиДокументов_КПовторнойПроверке(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ Результат = Неопределено Тогда
		
		ОбновитьДанные(ПредопределенноеЗначение("Перечисление.СтатусыПроверкиФинансовыхДокументов.КПовторнойПроверке"), Результат);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИнициализироватьСоответствиеСтатусовДляПроверкиДокументов(ДанныеФункциональныхОпций)
	
	СоответствиеСтатусов = Новый Соответствие;
	Если ДанныеФункциональныхОпций.ПроверкаДокументов Тогда
		
		СоответствиеСтатусов.Вставить(ПредопределенноеЗначение("Перечисление.СтатусыПроверкиФинансовыхДокументов.Проверен"));
		СоответствиеСтатусов.Вставить(ПредопределенноеЗначение("Перечисление.СтатусыПроверкиФинансовыхДокументов.НеПроверен"));
		СоответствиеСтатусов.Вставить(ПредопределенноеЗначение("Перечисление.СтатусыПроверкиФинансовыхДокументов.КПовторнойПроверке"));
		
	КонецЕсли;
	
	Возврат СоответствиеСтатусов;
	
КонецФункции

&НаСервере
Процедура ЗапуститьЗаданияПоОбновлениюДанныхСостоянияПроверкиДокументов()
	
	ЗаданияКЗапуску = ИнициализироватьСоответствиеСтатусовДляПроверкиДокументов(ДанныеФО);
	ПараметрыФоновогоЗадания = Обработки.ОтражениеДокументовВМеждународномУчете.ПараметрыПодсчетаКоличестваДокументов();
	
	Если ЗначениеЗаполнено(Организация) Тогда
		
		ПараметрыФоновогоЗадания.МассивОрганизаций = Организация;
		
	Иначе
		
		ПараметрыФоновогоЗадания.МассивОрганизаций = МассивДоступныхОрганизаций(Организация);
		
	КонецЕсли;

	ПараметрыФоновогоЗадания.ДатаОкончанияПериода = ДатаОкончанияПериода;
	ПараметрыФоновогоЗадания.ПланСчетов = ПланСчетов;
	
	ВыполненныеЗадания = Новый Массив;
	
	Для Каждого Задание Из ЗаданияКЗапуску Цикл
		
		ПараметрыФоновогоЗадания.ВидЗадания = Задание.Ключ;
		
		РезультатВыполнения = ЗапуститьВыполнениеВФоне("Обработки.ОтражениеДокументовВМеждународномУчете.СостояниеПроверкиДокументов", ПараметрыФоновогоЗадания);
		ОбновитьДанные(Задание.Ключ, РезультатВыполнения);
		
		Если РезультатВыполнения.Статус = "Выполнено" 
			ИЛИ РезультатВыполнения.Статус = "Ошибка" Тогда
			ВыполненныеЗадания.Добавить(Задание.Ключ);
		Иначе
			ЗаданияКЗапуску.Вставить(Задание.Ключ, РезультатВыполнения);
		КонецЕсли;
		
	КонецЦикла;
	
	Для каждого ВыполненноеЗадание Из ВыполненныеЗадания Цикл
		ЗаданияКЗапуску.Удалить(ВыполненноеЗадание);
	КонецЦикла;
	
	СоответствиеОбновленияСостоянийОтражения = Новый ФиксированноеСоответствие(ЗаданияКЗапуску);
	
КонецПроцедуры

&НаСервере
Функция ЗапуститьВыполнениеВФоне(ВыполняемыйМетод, ПараметрыОбработки)
	
	ПараметрыФоновогоЗадания = ДлительныеОперации.ПараметрыВыполненияФункции(ЭтотОбъект.УникальныйИдентификатор);
	РезультатФоновогоЗадания = ДлительныеОперации.ВыполнитьФункцию(ПараметрыФоновогоЗадания, ВыполняемыйМетод, ПараметрыОбработки);
	
	Возврат РезультатФоновогоЗадания;
	
КонецФункции

&НаСервере
Процедура ОбновитьДанные(ИмяОперации, РезультатВыполнения)
	
	ОбновитьДанныеПоОтражениюИПроверкеДокументовВУчете(РезультатВыполнения, ИмяОперации);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
