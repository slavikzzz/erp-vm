#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область Печать

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ОграниченияСвертки") Тогда

		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"ОграниченияСвертки",
			НСтр("ru = 'Ограничения свертки';
				|en = 'Rollup restrictions'"),
			СформироватьТабличныйДокументОграниченияСвертки(
				МассивОбъектов,
				ОбъектыПечати,
				ПараметрыПечати));
	КонецЕсли;
КонецПроцедуры

Функция СформироватьТабличныйДокументОграниченияСвертки(МассивОбъектов, ОбъектыПечати, ПараметрыПечати) Экспорт
	УстановитьПривилегированныйРежим(Истина);

	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ОриентацияСтраницы  = ОриентацияСтраницы.Ландшафт;
	ТабДок.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ОграниченияСвертки";
	ТабДок.ОтображатьЗаголовки = Ложь;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.ТолькоПросмотр = Истина;
	
	ДатаСверткиИБ = ПараметрыПечати.ДатаСверткиИБ;
	
	СтруктураИсходныеДанные = ПолучитьИсходныеДанныеОбОграниченияхСвертки(ДатаСверткиИБ);
	ВозможныеОграничения = СтруктураИсходныеДанные.ТаблицаОграничений;
	ЕстьОграничения = СтруктураИсходныеДанные.ЕстьОграничения;
	МенеджерВТ = СтруктураИсходныеДанные.МенеджерВТ;
	
	Макет = ПолучитьМакет("МакетОграниченияСвертки");
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ТабДок.Вывести(ОбластьЗаголовок);
	
	//Это для страховки. Если ограничений нет, гиперссылка с отчетом не видна на форме.
	Если НЕ ЕстьОграничения Тогда
		Область = Макет.ПолучитьОбласть("НетОграничений");
		ТабДок.Вывести(Область);
		УстановитьПривилегированныйРежим(Ложь);
		Возврат ТабДок;
	КонецЕсли;
	//Вывод списка ограничений с рекомендациями
	Область = Макет.ПолучитьОбласть("Шапка_ОбщийСписок");
	ТабДок.Вывести(Область);
	Для Каждого СтрокаОграничения ИЗ ВозможныеОграничения Цикл
		Если НЕ СтрокаОграничения.Обнаружено Тогда
			Продолжить;
		КонецЕсли;
		Область = Макет.ПолучитьОбласть("Строка_ОбщийСписок");
		Область.Параметры.Заполнить(СтрокаОграничения);
		ТабДок.Вывести(Область);
	КонецЦикла;
	//Вывод детальных данных по ограничениям
	Для Каждого СтрокаОграничения ИЗ ВозможныеОграничения Цикл
		Если НЕ СтрокаОграничения.Обнаружено Тогда
			Продолжить;
		КонецЕсли;
		Область = Макет.ПолучитьОбласть("Заголовок_Ограничение");
		Область.Параметры.Заполнить(СтрокаОграничения);
		ТабДок.Вывести(Область);
		
		Область = Макет.ПолучитьОбласть("Шапка_"+СтрокаОграничения.Ограничение);
		ТабДок.Вывести(Область);
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
		Запрос.Текст = "ВЫБРАТЬ * ИЗ " + СтрокаОграничения.Ограничение;
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Область = Макет.ПолучитьОбласть("Строка_"+СтрокаОграничения.Ограничение);
			Область.Параметры.Заполнить(Выборка);
			ТабДок.Вывести(Область);
		КонецЦикла;
	КонецЦикла;

	УстановитьПривилегированныйРежим(Ложь);

	Возврат ТабДок;

КонецФункции

#КонецОбласти

#Область ДанныеОбОграниченияхСвертки
Функция ПолучитьИсходныеДанныеОбОграниченияхСвертки(ДатаСверткиИБ, ДляОтчета = Истина) Экспорт
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	//Запрос на получение исходных данных
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ %НеДляОтчета%
	|ДокументРеализации КАК Документ
	|ПОМЕСТИТЬ НеподтвержденныйНДС
	|ИЗ РегистрСведений.НДССостояниеРеализации0
	|ГДЕ ДатаПодтверждения > &ДатаОст И ДатаРеализации <= &ДатаОст
	|;
	|/////////////////////////////////////
	|ВЫБРАТЬ %НеДляОтчета%
	|ТабРегОстатки.ОрганизацияВладелец КАК Организация,
	|ТабРегОстатки.АналитикаУчетаНоменклатуры.МестоХранения КАК Склад
	|ПОМЕСТИТЬ ПередачаТоваров
	|ИЗ РегистрНакопления.ТоварыОрганизацийКПередаче.Остатки(&ГраницаОст) КАК ТабРегОстатки
	|ЛЕВОЕ СОЕДИНЕНИЕ 
	|	РегистрНакопления.ТоварыОрганизацийКПередаче.Обороты(,&ГраницаОст,Регистратор) КАК ТабРегОбороты
	|	ПО ТабРегОстатки.ОрганизацияВладелец = ТабРегОбороты.ОрганизацияВладелец 
	|	И ТабРегОстатки.АналитикаУчетаНоменклатуры = ТабРегОбороты.АналитикаУчетаНоменклатуры 
	|	И ТабРегОстатки.ВидЗапасовПродавца = ТабРегОбороты.ВидЗапасовПродавца 
	|ГДЕ ТабРегОстатки.КоличествоОстаток > 0 И ТабРегОбороты.КоличествоПриход > 0
	|СГРУППИРОВАТЬ ПО
	|ТабРегОстатки.ОрганизацияВладелец,
	|ТабРегОстатки.АналитикаУчетаНоменклатуры.МестоХранения
	|;
	|/////////////////////////////////////
	|ВЫБРАТЬ %НеДляОтчета%
	|ТабРегОстатки.Организация КАК Организация,
	|ТабРегОстатки.Поставщик КАК Поставщик,
	|ТабРегОстатки.ДокументПоступления КАК Документ
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ РегистрНакопления.ТоварыКОформлениюДокументовИмпорта.Остатки(&ГраницаОст) КАК ТабРегОстатки
	|ГДЕ ТабРегОстатки.КоличествоОстаток > 0 
	|СГРУППИРОВАТЬ ПО
	|ТабРегОстатки.Организация, 
	|ТабРегОстатки.Поставщик, 
	|ТабРегОстатки.ДокументПоступления
	|; 
	|/////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ %НеДляОтчета%
	|ВидЗапасов.ВладелецТовара КАК Комитент
	|ПОМЕСТИТЬ ОтчетКомитенту
	|ИЗ РегистрНакопления.ТоварыКОформлениюОтчетовКомитенту.Остатки(&ГраницаОст)
	|ГДЕ КоличествоОстаток > 0
	|;
	|/////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ %НеДляОтчета%
	|АналитикаУчетаНоменклатуры.Номенклатура КАК Услуга,
	|Соглашение КАК Соглашение
	|ПОМЕСТИТЬ УслугиКОформлению
	|ИЗ РегистрНакопления.УслугиКОформлениюОтчетовПринципалу.Остатки(&ГраницаОст)
	|ГДЕ КоличествоОстаток > 0
	|; 
	|/////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ %НеДляОтчета%
	|Склад КАК Склад
	|ПОМЕСТИТЬ ИзлишкиНедостачи
	|ИЗ РегистрНакопления.ТоварыКОформлениюИзлишковНедостач.Остатки(&ГраницаОст)
	|ГДЕ КОформлениюАктовОстаток > 0 
	|;
	|/////////////////////////////////////
	|ВЫБРАТЬ %НеДляОтчета%
	|Организация КАК Организация,
	|ВидПереводаДенежныхСредств КАК ВидПереводаДенежныхСредств,
	|Получатель КАК Получатель,
	|Отправитель КАК Отправитель
	|ПОМЕСТИТЬ ДенежныеСредстваВПути
	|ИЗ РегистрНакопления.ДенежныеСредстваВПути.Остатки(&ГраницаОст)
	|ГДЕ СуммаОстаток > 0 И ВидПереводаДенежныхСредств <> ЗНАЧЕНИЕ(Перечисление.ВидыПереводовДенежныхСредств.ПоступлениеОтБанкаПоЭквайрингу)
	|СГРУППИРОВАТЬ ПО Организация, ВидПереводаДенежныхСредств, Получатель, Отправитель
	|;
	|/////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ %НеДляОтчета%
	|Ссылка КАК Документ
	|ПОМЕСТИТЬ НеУдаленныеЧекиККМ
	|ИЗ Документ.ЧекККМ
	|ГДЕ Ссылка.Дата <= &ДатаОст
	|;
	|/////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ %НеДляОтчета%
	|Организация КАК Организация,
	|АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ ОтгрузкаБезПереходаПраваСобственности
	|ИЗ РегистрНакопления.СебестоимостьТоваров.Остатки(&ГраницаОст, РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыВПути))
	|ГДЕ КоличествоОстаток > 0 
	|;
	//++ НЕ УТ
	|/////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ %НеДляОтчета%
	|Организация,
	|ФизическоеЛицо
	|ПОМЕСТИТЬ ВзаиморасчетыСКонтрагентамиАкционерами
	|ИЗ РегистрНакопления.ВзаиморасчетыСКонтрагентамиАкционерами.Остатки(&ГраницаОст)
	|ГДЕ СуммаВзаиморасчетовОстаток <> 0
	|;
	|/////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ %НеДляОтчета%
	|Организация,
	|ЗаказНаПроизводство
	|ПОМЕСТИТЬ ПартииНЗП
	|ИЗ РегистрНакопления.ПартииНезавершенногоПроизводства.Остатки(&ГраницаОст)
	|ГДЕ КоличествоОстаток > 0
	|;
	|/////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ %НеДляОтчета%
	|Организация,
	|Подразделение,
	|Распоряжение
	|ПОМЕСТИТЬ РаспоряженияНормативы
	|ИЗ РегистрНакопления.РаспоряженияНаСписаниеПоНормативам.Остатки(&ГраницаОст)
	|ГДЕ КоличествоОстаток > 0
	|;
	|/////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ %НеДляОтчета%
	|ДокументПоступления.Организация КАК Организация,
	|ДокументПоступления
	|ПОМЕСТИТЬ ЗаявленияОВвозе
	|ИЗ РегистрНакопления.ТоварыКОформлениюЗаявленийОВвозе.Остатки(&ГраницаОст)
	|ГДЕ КОформлениюОстаток > 0
	|;
	|/////////////////////////////////////
	|ВЫБРАТЬ 
	|	ИспользоватьВнеоборотныеАктивы2_2.Значение КАК ИспользоватьВА2_2,
	|	ДатаНачалаУчетаВнеоборотныхАктивов2_4.Значение КАК ДатаНачалаУчетаВА2_4
	|ПОМЕСТИТЬ ВА22
	|ИЗ
	|	Константа.ИспользоватьВнеоборотныеАктивы2_2 КАК ИспользоватьВнеоборотныеАктивы2_2
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|	Константа.ДатаНачалаУчетаВнеоборотныхАктивов2_4 КАК ДатаНачалаУчетаВнеоборотныхАктивов2_4
	|ПО Истина
	|ГДЕ ИспользоватьВнеоборотныеАктивы2_2.Значение = ИСТИНА
	|	И (ДатаНачалаУчетаВнеоборотныхАктивов2_4.Значение > &ДатаОст ИЛИ ДатаНачалаУчетаВнеоборотныхАктивов2_4.Значение = &ПустаяДата)
	|;
	|/////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ %НеДляОтчета%
	|	ХозрасчетныйОстатки.Организация КАК Организация,
	|	ХозрасчетныйОстатки.Субконто1 КАК ОсновноеСредство
	|ПОМЕСТИТЬ ВыбытиеОС
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&ГраницаОст, Счет В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ВыбытиеОС))) КАК ХозрасчетныйОстатки
	|ГДЕ
	|	ЕстьNULL(ХозрасчетныйОстатки.СуммаОстатокДт,0) <> 0
	|;
	//-- НЕ УТ

	//++ НЕ УТКА
	|/////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ %НеДляОтчета%
	|Подразделение,
	|Распоряжение
	|ПОМЕСТИТЬ РаспоряженияВыпуск
	|ИЗ РегистрНакопления.РаспоряженияНаВыпускПродукции.Остатки(&ГраницаОст)
	|ГДЕ КОформлениюОстаток > 0
	|;
	|/////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ %НеДляОтчета%
	|МаршрутныйЛист.Организация КАК Организация,
	|МаршрутныйЛист.Подразделение КАК Подразделение
	|ПОМЕСТИТЬ БракВПроизводстве
	|ИЗ РегистрНакопления.БракВПроизводстве.Остатки(&ГраницаОст)
	|ГДЕ КоличествоОстаток > 0
	|;
	|/////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ %НеДляОтчета%
	|Отправитель,
	|Получатель,
	|Распоряжение
	|ПОМЕСТИТЬ РаспоряженияИзПроизводства
	|ИЗ РегистрНакопления.РаспоряженияНаПередачуИзПроизводства.Остатки(&ГраницаОст)
	|ГДЕ КоличествоОстаток > 0
	|;
	//-- НЕ УТКА
	|";
	Если ДляОтчета Тогда
		//Нужны все данные
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%НеДляОтчета%", "");
	Иначе
		//Нужны данные только о том, какие ограничения есть
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%НеДляОтчета%", "ПЕРВЫЕ 1");
	КонецЕсли;
	Запрос.УстановитьПараметр("ДатаОст", КонецДня(ДатаСверткиИБ));
	Запрос.УстановитьПараметр("ПустаяДата", Дата("00010101"));
	Запрос.УстановитьПараметр("ГраницаОст", Новый Граница(КонецДня(ДатаСверткиИБ),ВидГраницы.Включая));

	Запрос.Выполнить();
	
	//Заполнение списка ограничений
	ВозможныеОграничения = Новый ТаблицаЗначений;
	ВозможныеОграничения.Колонки.Добавить("Ограничение");
	ВозможныеОграничения.Колонки.Добавить("Содержание");
	ВозможныеОграничения.Колонки.Добавить("Рекомендация");
	ВозможныеОграничения.Колонки.Добавить("Обнаружено");
	
	ДобавитьВозможноеОграничение(ВозможныеОграничения,"НеподтвержденныйНДС",
								НСтр("ru = 'Неподтвержденный НДС по ставке 0%';
									|en = 'Unconfirmed VAT at a rate of 0%'"),
								НСтр("ru = 'Данные будут потеряны. Не рекомендуется выполнять свертку';
									|en = 'The data will be lost. It is not recommended that you execute rollup'"));
								
	ДобавитьВозможноеОграничение(ВозможныеОграничения,"ТоварыКОформлению",
								НСтр("ru = 'Поступившие импортные товары, на которые не оформлена ГТД';
									|en = 'Received imported goods CCD is not registered for'"),
								НСтр("ru = 'Данные будут потеряны. Не рекомендуется выполнять свертку';
									|en = 'The data will be lost. It is not recommended that you execute rollup'"));


	ДобавитьВозможноеОграничение(ВозможныеОграничения,"ИзлишкиНедостачи",
								НСтр("ru = 'Не оформлены излишки и недостачи товаров';
									|en = 'Surplus and shortages of products are not registered'"),
								НСтр("ru = 'Перед сверткой оформить излишки и недостачи';
									|en = 'Register surplus and shortages before rollup '"));

	ДобавитьВозможноеОграничение(ВозможныеОграничения,"ОтчетКомитенту",
								НСтр("ru = 'Не оформлены отчеты комитенту о продажах';
									|en = 'Account sales to consignor are not registered'"),
								НСтр("ru = 'Перед сверткой оформить отчеты комитенту';
									|en = 'Register reports to consignor before rollup '"));
	ДобавитьВозможноеОграничение(ВозможныеОграничения,"УслугиКОформлению",
								НСтр("ru = 'Не оформлены отчеты о продаже агентских услуг';
									|en = 'Reports on agency service sale are not registered'"),
								НСтр("ru = 'Перед сверткой оформить отчеты о продаже агентских услуг';
									|en = 'Register reports on agency service sale before rollup'"));

	ДобавитьВозможноеОграничение(ВозможныеОграничения,"ПередачаТоваров",
								НСтр("ru = 'Не оформлена передача товаров между организациями';
									|en = 'Goods transfer between the companies is not registered'"),
								НСтр("ru = 'Перед сверткой оформить передачу товаров между организациями';
									|en = 'Register goods transfer between companies before rollup '"));
								
	ДобавитьВозможноеОграничение(ВозможныеОграничения,"ДенежныеСредстваВПути",
								НСтр("ru = 'Денежные средства в пути';
									|en = 'Cash-in-transit'"),
								НСтр("ru = 'Данные будут потеряны. Не рекомендуется выполнять свертку';
									|en = 'The data will be lost. It is not recommended that you execute rollup'"));

	ДобавитьВозможноеОграничение(ВозможныеОграничения,"НеУдаленныеЧекиККМ",
								НСтр("ru = 'Не выполнено архивирование и удаление чеков ККМ';
									|en = 'Cash receipts are not archived and deleted'"),
								НСтр("ru = 'Перед сверткой выполнить архивирование и удаление чеков ККМ';
									|en = 'Archive and delete cash receipts before rollup'"));
	ДобавитьВозможноеОграничение(ВозможныеОграничения,"ОтгрузкаБезПереходаПраваСобственности",
								НСтр("ru = 'Отгрузка без перехода права собственности';
									|en = 'Shipment without transfer of title to goods'"),
								НСтр("ru = 'Данные будут потеряны. Не рекомендуется выполнять свертку';
									|en = 'The data will be lost. It is not recommended that you execute rollup'"));

								
	//++ НЕ УТ
	ДобавитьВозможноеОграничение(ВозможныеОграничения,"ВА22",
							НСтр("ru = 'Учет внеоборотных активов версии 2.2';
								|en = 'Capital asset accounting 2.2'"),
							НСтр("ru = 'Данные будут потеряны. Не рекомендуется выполнять свертку';
								|en = 'The data will be lost. It is not recommended that you execute rollup'"));
	ДобавитьВозможноеОграничение(ВозможныеОграничения,"ВыбытиеОС",
							НСтр("ru = 'Выбывающие ОС (счет 01.09)';
								|en = 'Retiring FA (account 01.09)'"),
							НСтр("ru = 'Данные будут потеряны. Не рекомендуется выполнять свертку';
								|en = 'The data will be lost. It is not recommended that you execute rollup'"));


	ДобавитьВозможноеОграничение(ВозможныеОграничения,"ВзаиморасчетыСКонтрагентамиАкционерами",
							НСтр("ru = 'Взаиморасчеты с контрагентами акционерами';
								|en = 'Mutual settlements with counterparties and stockholders'"),
							НСтр("ru = 'Данные будут потеряны. Не рекомендуется выполнять свертку';
								|en = 'The data will be lost. It is not recommended that you execute rollup'"));
	ДобавитьВозможноеОграничение(ВозможныеОграничения,"ПартииНЗП",
							НСтр("ru = 'Партии незавершенного производства';
								|en = 'Unfinished production batches'"),
							НСтр("ru = 'Данные будут потеряны. Не рекомендуется выполнять свертку';
								|en = 'The data will be lost. It is not recommended that you execute rollup'"));

	ДобавитьВозможноеОграничение(ВозможныеОграничения,"РаспоряженияНормативы",
								НСтр("ru = 'Не оформлены списания по нормативам';
									|en = 'Standard write-offs are not registered'"),
								НСтр("ru = 'Перед сверткой оформить списание по нормативам';
									|en = 'Register standard write-off before rollup'"));
	ДобавитьВозможноеОграничение(ВозможныеОграничения,"ЗаявленияОВвозе",
								НСтр("ru = 'Не оформлены заявления о ввозе товаров';
									|en = 'Applications for goods import are not registered'"),
								НСтр("ru = 'Перед сверткой оформить заявления о ввозе товаров из ЕАЭС';
									|en = 'Register applications for goods import from EEU before rollup'"));
	//-- НЕ УТ

	//++ НЕ УТКА
	ДобавитьВозможноеОграничение(ВозможныеОграничения,"РаспоряженияИзПроизводства",
								НСтр("ru = 'Не оформлена передача из производства по распоряжениям';
									|en = 'Transfer from production is not registered for references'"),
								НСтр("ru = 'Перед сверткой оформить передачу из производства';
									|en = 'Register transfer from production before rollup'"));
	ДобавитьВозможноеОграничение(ВозможныеОграничения,"БракВПроизводстве",
							НСтр("ru = 'Брак в производстве';
								|en = 'Defective goods in production'"),
							НСтр("ru = 'Данные будут потеряны. Не рекомендуется выполнять свертку';
								|en = 'The data will be lost. It is not recommended that you execute rollup'"));

	ДобавитьВозможноеОграничение(ВозможныеОграничения,"РаспоряженияВыпуск",
								НСтр("ru = 'Не оформлен выпуск продукции по распоряжениям';
									|en = 'Product release by several references is not registered'"),
								НСтр("ru = 'Перед сверткой оформить выпуск продукции по распоряжениям';
									|en = 'Before rollup register product release by references'"));
	//-- НЕ УТКА
	ЕстьОграничения = Ложь;
	Для Каждого СтрокаОграничения ИЗ ВозможныеОграничения Цикл
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1 * ИЗ "+СтрокаОграничения.Ограничение;
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			СтрокаОграничения.Обнаружено = Истина;
			ЕстьОграничения = Истина;
			Если НЕ ДляОтчета Тогда
				Прервать;
			КонецЕсли;
		Иначе
			СтрокаОграничения.Обнаружено = Ложь;
		КонецЕсли;
	КонецЦикла;
	Если ДляОтчета Тогда
		Возврат Новый Структура("ЕстьОграничения, ТаблицаОграничений, МенеджерВТ", ЕстьОграничения, ВозможныеОграничения, МенеджерВТ);
	Иначе
		Возврат Новый Структура("ЕстьОграничения", ЕстьОграничения);
	КонецЕсли;
	
КонецФункции

Процедура ДобавитьВозможноеОграничение(ВозможныеОграничения,Ограничение, Содержание, Рекомендация)
	СтрокаОграничения = ВозможныеОграничения.Добавить();
	СтрокаОграничения.Ограничение = Ограничение;
	СтрокаОграничения.Содержание = Содержание;
	СтрокаОграничения.Рекомендация = Рекомендация;
КонецПроцедуры

#КонецОбласти
#КонецОбласти

#КонецЕсли