
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	УстановитьУсловноеОформление();
	
	Организация = Параметры.Организация;
	ПланСчетов = Параметры.ПланСчетов;
	НастройкаФормированияПроводок = Параметры.НастройкаФормированияПроводок;
	Если Параметры.Свойство("СчетРеглУчета") 
		И ЗначениеЗаполнено(Параметры.СчетРеглУчета) Тогда
		Элементы.ПланСчетовРеглУчета.ТекущаяСтрока = Параметры.СчетРеглУчета
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НастройкаФормированияПроводок) И Не ЗначениеЗаполнено(ПланСчетов) Тогда
		ПланСчетов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НастройкаФормированияПроводок, "Владелец");
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Организация = Справочники.Организации.ПолучитьОрганизациюПоУмолчанию();
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПланСчетов) Тогда
		ПланСчетов = Справочники.ПланыСчетовМеждународногоУчета.ПланСчетовПоУмолчанию();
	КонецЕсли;
	
	НастроитьДинамическиеСпискиПоПлануСчетов();
	
	Если Не МеждународныйУчетОбщегоНазначения.НастройкаПроводокПоОбъектамУчетаИлиРеглУчету() Тогда
		ПараметрВыбора = Новый ПараметрВыбора("Отбор.СпособАннулированияПроводок", Перечисления.СпособыАннулированияПроводокМеждународногоУчета.СторноПроводками);
		ПараметрыВыбора = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПараметрВыбора);
		Элементы.НастройкаФормированияПроводок.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(НастройкаФормированияПроводок) Тогда
		НастройкаФормированияПроводок = 
			Справочники.НастройкиФормированияПроводокМеждународногоУчета.НастройкаФормированияПроводокПоУмолчанию(ПланСчетов);
	КонецЕсли;
	
	НастройкаФормированияПроводокПриИзмененииСервер();
	
	ТекущийМесяц = НачалоМесяца(ТекущаяДатаСеанса());
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	СписокТипов = СоответствияСчетовМеждународногоУчета.КомпоновщикНастроек.Настройки.Выбор.ДоступныеПоляВыбора.НайтиПоле(Новый ПолеКомпоновкиДанных("Ссылка")).Тип;
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.Источники = СписокТипов;
	ПараметрыРазмещения.ПрефиксГрупп = "СоответствияСчетовМеждународногоУчета";
	ПараметрыРазмещения.КоманднаяПанель = Элементы.Группа1;
	
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	
	СписокТипов = СоответствияОборотовМеждународногоУчета.КомпоновщикНастроек.Настройки.Выбор.ДоступныеПоляВыбора.НайтиПоле(Новый ПолеКомпоновкиДанных("Ссылка")).Тип;
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.Источники = СписокТипов;
	ПараметрыРазмещения.ПрефиксГрупп = "СоответствияОборотовМеждународногоУчета";
	ПараметрыРазмещения.КоманднаяПанель = Элементы.СоответствияОборотовМеждународногоУчетаКоманднаяПанель;
	
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	НастроитьДинамическиеСпискиПоНастройкеФормированияПроводок();
	УстановитьОтборПоСчетуРеглУчета();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)
	
	НеЗагружатьНастройки = Ложь;
	Если ЗначениеЗаполнено(ПланСчетов) 
			Или ЗначениеЗаполнено(НастройкаФормированияПроводок) 
			Или ЗначениеЗаполнено(Организация) Тогда
		НеЗагружатьНастройки = Истина;
	КонецЕсли;
	
	Если НеЗагружатьНастройки И Настройки["Организация"] <> Неопределено Тогда
		Настройки.Удалить("Организация");
	КонецЕсли;
	
	Если НеЗагружатьНастройки И Настройки["ПланСчетов"] <> Неопределено Тогда
		Настройки.Удалить("ПланСчетов");
	КонецЕсли;
	
	Если НеЗагружатьНастройки И Настройки["НастройкаФормированияПроводок"] <> Неопределено Тогда
		Настройки.Удалить("НастройкаФормированияПроводок");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	НастроитьДинамическиеСпискиПоПлануСчетов();
	НастройкаФормированияПроводокПриИзмененииСервер();
	
	НастроитьДинамическиеСпискиПоНастройкеФормированияПроводок();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗаписьСоответствияСчетов"
			Или ИмяСобытия = "ЗаписьСоответствияОборотов"
			Или ИмяСобытия = "ИзмененоИспользованиеВПравилахОтраженияВМеждународномУчете" Тогда
			
		ОбработатьОповещениеСервер(ИмяСобытия, Источник);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьОповещениеСервер(ИмяСобытия, Источник) 
	
	Если ИмяСобытия = "ЗаписьСоответствияСчетов"
		Или (ИмяСобытия = "ИзмененоИспользованиеВПравилахОтраженияВМеждународномУчете" 
			И ТипЗнч(Источник) = Тип("СправочникСсылка.СоответствияСчетовМеждународногоУчета")) Тогда
		Элементы.ПланСчетовРеглУчета.Обновить();
		Элементы.СоответствияСчетовМеждународногоУчета.Обновить();
		Элементы.ПланСчетовМеждународногоУчета.Обновить();
	КонецЕсли;
	
	Если ИмяСобытия = "ЗаписьСоответствияОборотов"
		Или (ИмяСобытия = "ИзмененоИспользованиеВПравилахОтраженияВМеждународномУчете" 
			И ТипЗнч(Источник) = Тип("СправочникСсылка.СоответствияОборотовМеждународногоУчета")) Тогда
		Элементы.ПланСчетовМеждународногоУчета.Обновить();
		Элементы.СоответствияОборотовМеждународногоУчета.Обновить();
	КонецЕсли;
	
	ТекущаяСтрока = Элементы.ПланСчетовМеждународногоУчета.ТекущаяСтрока;
	Если ТекущаяСтрока <> Неопределено Тогда
		ЗаполнитьНастройкиПоСчетуМеждународногоУчета(ТекущаяСтрока);
	КонецЕсли;
		
КонецПроцедуры 

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	Оповестить("ЗаконченаНастройкаСоотвествияСчетовИОборотовМеждународногоУчета");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НастройкаФормированияПроводокПриИзменении(Элемент)
	
	НастройкаФормированияПроводокПриИзмененииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОрганизацияПриИзмененииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ПланСчетовПриИзменении(Элемент)
	
	ПланСчетовПриИзмененииСервер();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СтатусНастройкиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Отбор = Новый Структура;
	Отбор.Вставить("Организация", Организация);
	Отбор.Вставить("ПланСчетов",   ПланСчетов);
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "СоздатьЗаписьПланыСчетовОрганизаций" Тогда
		
		ОбработкаОповещения = Новый ОписаниеОповещения("ВыборПериодаЗаписиПланыСчетовОрганизаций", ЭтотОбъект);
		ПоказатьВводДаты(ОбработкаОповещения, ТекущийМесяц, Нстр("ru = 'Включить настройку с';
																|en = 'Enable setting since'"), ЧастиДаты.Дата);
		
		СтандартнаяОбработка = Ложь;
	
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьИсториюПланыСчетовОрганизаций" Тогда
			
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Отбор", Отбор);
		ОткрытьФорму("РегистрСведений.ПланыСчетовМеждународногоУчетаОрганизаций.Форма.ФормаСписка", 
			ПараметрыФормы, ЭтотОбъект, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
		СтандартнаяОбработка = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ПланСчетовРеглУчетаПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("ОбработчикПланСчетовРеглУчетаПриАктивизацииСтроки", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПланСчетовМеждународногоУчетаПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("ОбработчикПланСчетовМеждународногоУчетаПриАктивизацииСтроки", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПланСчетовМеждународногоУчетаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", ВыбраннаяСтрока);
	ПараметрыФормы.Вставить("ОтображатьПризнакРекласс", Истина);
	
	ОткрытьФорму("ПланСчетов.Международный.Форма.ФормаСчета", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СоответствияСчетовМеждународногоУчетаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Не ЗначениеЗаполнено(ТекущийСчетРеглУчета) Тогда
		Отказ = Истина;
		ПоказатьПредупреждение(, НСтр("ru = 'Для добавления соответствия выберете счет';
										|en = 'To add correspondence, select an account'"));
		Возврат;
	КонецЕсли;
	
	Если Элементы.ПланСчетовРеглУчета.ДанныеСтроки(ТекущийСчетРеглУчета).ЗапретитьИспользоватьВПроводках Тогда
		Отказ = Истина;
		ПоказатьПредупреждение(, НСтр("ru = 'Для добавления соответствия выберете счет, который используется в проводках';
										|en = 'To add correspondence, select an account used in postings'"));
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(НастройкаФормированияПроводок) Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НастройкаФормированияПроводок", НастройкаФормированияПроводок);
	Если Копирование Тогда
		ПараметрыФормы.Вставить("ЗначениеКопирования", Элемент.ТекущаяСтрока);
	Иначе
		ЗначенияЗаполнения = Новый Структура;
		ЗначенияЗаполнения.Вставить("СчетРеглУчета", ТекущийСчетРеглУчета);
		ЗначенияЗаполнения.Вставить("ПланСчетов", ПланСчетов);
		ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	КонецЕсли;
	
	ОткрытьФорму("Справочник.СоответствияСчетовМеждународногоУчета.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоНастроекПоСчетуМеждународногоУчетаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = ДеревоНастроекПоСчетуМеждународногоУчета.НайтиПоИдентификатору(ВыбраннаяСтрока);
	
	ПараметрыФормы = Новый Структура;
	Если ЗначениеЗаполнено(ТекущиеДанные.СоответствиеОборотов) Тогда
		ПараметрыФормы.Вставить("Ключ", ТекущиеДанные.СоответствиеОборотов);
		ОткрытьФорму("Справочник.СоответствияОборотовМеждународногоУчета.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
	ИначеЕсли ЗначениеЗаполнено(ТекущиеДанные.СоответствиеСчетов) Тогда
		ПараметрыФормы.Вставить("Ключ", ТекущиеДанные.СоответствиеСчетов);
		ОткрытьФорму("Справочник.СоответствияСчетовМеждународногоУчета.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСчетовРеглУчетаПриИзменении(Элемент)
	
	ПриИзмененииОтбораСчетовСервер("ПланСчетовРеглУчета", ОтборСчетовРеглУчета);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСчетовМеждународногоУчетаПриИзменении(Элемент)
	
	ПриИзмененииОтбораСчетовСервер("ПланСчетовМеждународногоУчета", ОтборСчетовМеждународногоУчета);
	
КонецПроцедуры

&НаКлиенте
Процедура СоответствияОборотовМеждународногоУчетаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Не ЗначениеЗаполнено(НастройкаФормированияПроводок) Или Группа Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НастройкаФормированияПроводок", НастройкаФормированияПроводок);
	Если Копирование Тогда
		ПараметрыФормы.Вставить("ЗначениеКопирования", Элемент.ТекущаяСтрока);
	Иначе
		ЗначенияЗаполнения = Новый Структура;
		ЗначенияЗаполнения.Вставить("ПланСчетов", ПланСчетов);
		ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	КонецЕсли;
	
	ОткрытьФорму("Справочник.СоответствияОборотовМеждународногоУчета.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура ВключитьСчетВИгнорируемыеПриОтраженииВМеждународномУчете(Команда)
	
	СчетаРеглУчета = Элементы.ПланСчетовРеглУчета.ВыделенныеСтроки;
	Если СчетаРеглУчета.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ИзменитьИгнорированиеСчетаПриОтраженииВМеждународномУчетеСервер(СчетаРеглУчета, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьСчетИзИгнорируемыхПриОтраженииВМеждународномУчете(Команда)
	
	СчетаРеглУчета = Элементы.ПланСчетовРеглУчета.ВыделенныеСтроки;
	Если СчетаРеглУчета.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ИзменитьИгнорированиеСчетаПриОтраженииВМеждународномУчетеСервер(СчетаРеглУчета, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ВключитьСоответствиеСчетовВНастройкуФормированияПроводок(Команда)
	
	МассивСоотвествий = Элементы.СоответствияСчетовМеждународногоУчета.ВыделенныеСтроки;
	Если МассивСоотвествий.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СпискиДляОбновления = Новый Массив;
	СпискиДляОбновления.Добавить("ПланСчетовРеглУчета");
	СпискиДляОбновления.Добавить("СоответствияСчетовМеждународногоУчета");
	СпискиДляОбновления.Добавить("ПланСчетовМеждународногоУчета");
	
	ИзменитьВключениеВНастройкуФормированияПроводокНаСервере(МассивСоотвествий, Истина, СпискиДляОбновления);
	
КонецПроцедуры

&НаКлиенте
Процедура ВключитьСоответствиеОборотовВНастройкуФормированияПроводок(Команда)
	
	ВыбранныеСтроки = Элементы.СоответствияОборотовМеждународногоУчета.ВыделенныеСтроки;
	
	Соответствия = Новый Массив;
	Для каждого ВыбраннаяСтрока Из ВыбранныеСтроки Цикл
		ДанныеСтроки = Элементы.СоответствияОборотовМеждународногоУчета.ДанныеСтроки(ВыбраннаяСтрока);
		Если Не ДанныеСтроки.ЭтоГруппа Тогда
			Соответствия.Добавить(ВыбраннаяСтрока);
		КонецЕсли;
	КонецЦикла;
	
	Если Соответствия.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СпискиДляОбновления = Новый Массив;
	СпискиДляОбновления.Добавить("СоответствияОборотовМеждународногоУчета");
	СпискиДляОбновления.Добавить("ПланСчетовМеждународногоУчета");
	
	ИзменитьВключениеВНастройкуФормированияПроводокНаСервере(Соответствия, Истина, СпискиДляОбновления);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьСоответствиеСчетовИзНастройкиФормированияПроводок(Команда)
	
	ВыбранныеСтроки = Элементы.СоответствияСчетовМеждународногоУчета.ВыделенныеСтроки;
	Если ВыбранныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СпискиДляОбновления = Новый Массив;
	СпискиДляОбновления.Добавить("ПланСчетовРеглУчета");
	СпискиДляОбновления.Добавить("СоответствияСчетовМеждународногоУчета");
	СпискиДляОбновления.Добавить("ПланСчетовМеждународногоУчета");
	
	ИзменитьВключениеВНастройкуФормированияПроводокНаСервере(ВыбранныеСтроки, Ложь, СпискиДляОбновления);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьСоответствиеОборотовИзНастройкиФормированияПроводок(Команда)
	
	ВыбранныеСтроки = Элементы.СоответствияОборотовМеждународногоУчета.ВыделенныеСтроки;
	
	Соответствия = Новый Массив;
	Для каждого ВыбраннаяСтрока Из ВыбранныеСтроки Цикл
		ДанныеСтроки = Элементы.СоответствияОборотовМеждународногоУчета.ДанныеСтроки(ВыбраннаяСтрока);
		Если Не ДанныеСтроки.ЭтоГруппа Тогда
			Соответствия.Добавить(ВыбраннаяСтрока);
		КонецЕсли;
	КонецЦикла;
	
	Если Соответствия.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СпискиДляОбновления = Новый Массив;
	СпискиДляОбновления.Добавить("СоответствияОборотовМеждународногоУчета");
	СпискиДляОбновления.Добавить("ПланСчетовМеждународногоУчета");
	
	ИзменитьВключениеВНастройкуФормированияПроводокНаСервере(Соответствия, Ложь, СпискиДляОбновления);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСоответствиеПоСчету(Команда)
	
	ТекущиеДанные = Элементы.ДеревоНастроекПоСчетуМеждународногоУчета.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено 
		Или (Не ЗначениеЗаполнено(ТекущиеДанные.СоответствиеОборотов) 
				И Не ЗначениеЗаполнено(ТекущиеДанные.СоответствиеСчетов)) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не выбрано соответствие счетов или оборотов';
														|en = 'Correspondence of accounts or turnovers is not selected'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	Если ЗначениеЗаполнено(ТекущиеДанные.СоответствиеОборотов) Тогда
		ПараметрыФормы.Вставить("Ключ", ТекущиеДанные.СоответствиеОборотов);
		ОткрытьФорму("Справочник.СоответствияОборотовМеждународногоУчета.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
	ИначеЕсли ЗначениеЗаполнено(ТекущиеДанные.СоответствиеСчетов) Тогда
		ПараметрыФормы.Вставить("Ключ", ТекущиеДанные.СоответствиеСчетов);
		ОткрытьФорму("Справочник.СоответствияСчетовМеждународногоУчета.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаФормированияПроводок(Команда)
	Если ПроверитьЗаполнение() Тогда
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("Ключ", НастройкаФормированияПроводок);
		ОткрытьФорму("Справочник.НастройкиФормированияПроводокМеждународногоУчета.ФормаОбъекта", 
			ПараметрыФормы, ЭтотОбъект);
	КонецЕсли;
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаНастройкаСоответствияСчетов Тогда
		ТекущийСписок = Элементы.СоответствияСчетовМеждународногоУчета;
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаНастройкаСоответствияОборотов Тогда
		ТекущийСписок = Элементы.СоответствияОборотовМеждународногоУчета;
	КонецЕсли;
	
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, ТекущийСписок);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаНастройкаСоответствияСчетов Тогда
		ТекущийСписок = Элементы.СоответствияСчетовМеждународногоУчета;
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаНастройкаСоответствияОборотов Тогда
		ТекущийСписок = Элементы.СоответствияОборотовМеждународногоУчета;
	КонецЕсли;
	
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, ТекущийСписок, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаНастройкаСоответствияСчетов Тогда
		ТекущийСписок = Элементы.СоответствияСчетовМеждународногоУчета;
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаНастройкаСоответствияОборотов Тогда
		ТекущийСписок = Элементы.СоответствияОборотовМеждународногоУчета;
	КонецЕсли;
	
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, ТекущийСписок);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область ПрочиеПроцедурыИФункции

&НаСервере
Функция ПроверитьЗаполнениеНастройкиФормированияПроводок()
	
	Результат = ЗначениеЗаполнено(НастройкаФормированияПроводок);
	
	Если Не Результат Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не заполнено поле ""Настройка формирования проводок""';
																|en = 'The ""Entries generation setting"" field cannot be blank'"), ,"НастройкаФормированияПроводок");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура УстановитьУсловноеОформление() 
	
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПланСчетовРеглУчета.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПланСчетовРеглУчета.ИгнорируетсяПриОтраженииВМеждународномУчете");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СоответствияСчетовМеждународногоУчета.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СоответствияСчетовМеждународногоУчета.ДействуетВНастройкеФормированияПроводок");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СоответствияОборотовМеждународногоУчета.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СоответствияОборотовМеждународногоУчета.ДействуетВНастройкеФормированияПроводок");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СоответствияОборотовМеждународногоУчета.ЭтоГруппа");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоНастроекПоСчетуМеждународногоУчета.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоНастроекПоСчетуМеждународногоУчета.ДействуетВНастройкеФормированияПроводок");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоНастроекПоСчетуМеждународногоУчета.ГруппаНастроек");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СоответствияСчетовМеждународногоУчетаПредставлениеОтбора.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СоответствияСчетовМеждународногоУчета.ПредставлениеОтбора");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<без отбора>';
																|en = '<No filter>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненноеПолеТаблицы);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СоответствияОборотовМеждународногоУчетаПредставлениеОтбора.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СоответствияОборотовМеждународногоУчета.ПредставлениеОтбора");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<без отбора>';
																|en = '<No filter>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненноеПолеТаблицы);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СоответствияОборотовМеждународногоУчетаВариантСовместногоПрименения.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СоответствияОборотовМеждународногоУчета.ЭтоГруппа");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СоответствияОборотовМеждународногоУчетаСчетРеглУчетаДт.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СоответствияОборотовМеждународногоУчетаНаименованиеСчетРеглУчетаДт.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СоответствияОборотовМеждународногоУчетаСчетРеглУчетаКт.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СоответствияОборотовМеждународногоУчетаНаименованиеСчетРеглУчетаКт.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СоответствияОборотовМеждународногоУчета.ЭтоГруппа");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СоответствияОборотовМеждународногоУчетаВариантСовместногоПрименения.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СоответствияОборотовМеждународногоУчета.ЭтоГруппа");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СоответствияОборотовМеждународногоУчета.ВариантСовместногоПрименения");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.ВариантыСовместногоПримененияШаблоновПроводок.Вытеснение;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Только первое подходящее по условиям отбора соответствие группы';
																|en = 'Only the first group correspondence suitable by filter conditions '"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстСправочнойНадписи);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СоответствияОборотовМеждународногоУчетаВариантСовместногоПрименения.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СоответствияОборотовМеждународногоУчета.ЭтоГруппа");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СоответствияОборотовМеждународногоУчета.ВариантСовместногоПрименения");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.ВариантыСовместногоПримененияШаблоновПроводок.Все;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Все соответствия группы';
																|en = 'All group compliances'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстСправочнойНадписи);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьДинамическиеСпискиПоНастройкеФормированияПроводок()
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		СоответствияОборотовМеждународногоУчета,
		"НастройкаФормированияПроводок", 
		НастройкаФормированияПроводок,
		Истина);
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		СоответствияСчетовМеждународногоУчета,
		"НастройкаФормированияПроводок",
		НастройкаФормированияПроводок,
		Истина);
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		ПланСчетовРеглУчета,
		"НастройкаФормированияПроводок",
		НастройкаФормированияПроводок, Истина);
		
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоСчетуРеглУчета()
	
	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(
		СоответствияСчетовМеждународногоУчета, "СчетРеглУчета", ТекущийСчетРеглУчета, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикПланСчетовРеглУчетаПриАктивизацииСтроки()
	
	ТекущийСчетРеглУчета = Элементы.ПланСчетовРеглУчета.ТекущаяСтрока;
	УстановитьОтборПоСчетуРеглУчета();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикПланСчетовМеждународногоУчетаПриАктивизацииСтроки()
	
	ЗаполнитьНастройкиПоСчетуМеждународногоУчета(Элементы.ПланСчетовМеждународногоУчета.ТекущаяСтрока);
	МеждународныйУчетКлиентСервер.РазвернутьДерево(
		ЭтаФорма, "ДеревоНастроекПоСчетуМеждународногоУчета", ДеревоНастроекПоСчетуМеждународногоУчета, Истина);
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьИгнорированиеСчетаПриОтраженииВМеждународномУчетеСервер(СчетаРеглУчета, Игнорировать)
	
	Если Не ПроверитьЗаполнениеНастройкиФормированияПроводок() Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписейИгнорируемыеСчета = РегистрыСведений.СчетаРеглУчетаИгнорируемыеПриОтраженииВМеждународномУчете.СоздатьНаборЗаписей();
	НаборЗаписейИгнорируемыеСчета.Отбор.НастройкаФормированияПроводок.Установить(НастройкаФормированияПроводок);
	НаборЗаписейИгнорируемыеСчета.Прочитать();
	ТаблицаЗаписей = НаборЗаписейИгнорируемыеСчета.Выгрузить();
	
	НаборЗаписейПравилаОтражения = РегистрыСведений.ПравилаОтраженияВМеждународномУчете.СоздатьНаборЗаписей();
	НаборЗаписейПравилаОтражения.Отбор.НастройкаФормированияПроводок.Установить(НастройкаФормированияПроводок);
	НаборЗаписейПравилаОтражения.Прочитать();
	ТаблицаПравилОтражения = НаборЗаписейПравилаОтражения.Выгрузить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Хозрасчетный.Ссылка КАК Ссылка
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.Ссылка В ИЕРАРХИИ(&СчетаРеглУчета)
	|;
	|
	|/////////////////////////////////////
	|ВЫБРАТЬ
	|	СоответствияСчетов.Ссылка КАК Ссылка,
	|	СоответствияСчетов.СчетРеглУчета КАК СчетРеглУчета 
	|ИЗ
	|	Справочник.СоответствияСчетовМеждународногоУчета КАК СоответствияСчетов
	|ГДЕ
	|	СоответствияСчетов.СчетРеглУчета В (&СчетаРеглУчета)";
	Запрос.УстановитьПараметр("СчетаРеглУчета", СчетаРеглУчета);
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	СчетаРеглУчетаКОбработке = РезультатыЗапроса[0].Выгрузить().ВыгрузитьКолонку("Ссылка");
	ТаблицаСоответствийСчетов = РезультатыЗапроса[1].Выгрузить();
	ТаблицаСоответствийСчетов.Индексы.Добавить("СчетРеглУчета");
	
	Для каждого СчетРеглУчета Из СчетаРеглУчетаКОбработке Цикл
		Запись = ТаблицаЗаписей.Найти(СчетРеглУчета, "СчетРеглУчета");
		Если Запись = Неопределено И Игнорировать Тогда
			НоваяСтрока = ТаблицаЗаписей.Добавить();
			НоваяСтрока.НастройкаФормированияПроводок = НастройкаФормированияПроводок;
			НоваяСтрока.СчетРеглУчета = СчетРеглУчета;
			
			ОтборПоСчету = Новый Структура("СчетРеглУчета", СчетРеглУчета);
			СоответствияСчетов = ТаблицаСоответствийСчетов.НайтиСтроки(ОтборПоСчету);
			Для каждого СоответствиеСчетов Из СоответствияСчетов Цикл
				ЗаписьПоСоответствиюСчетов = ТаблицаПравилОтражения.Найти(СоответствиеСчетов.Ссылка, "ШаблонПроводки");
				Если ЗаписьПоСоответствиюСчетов <> Неопределено Тогда
					ТаблицаПравилОтражения.Удалить(ЗаписьПоСоответствиюСчетов);
				КонецЕсли;
			КонецЦикла;
			
		ИначеЕсли Запись <> Неопределено И Не Игнорировать Тогда
			ТаблицаЗаписей.Удалить(Запись);
		КонецЕсли;
	КонецЦикла;
	
	НаборЗаписейИгнорируемыеСчета.Загрузить(ТаблицаЗаписей);
	НаборЗаписейПравилаОтражения.Загрузить(ТаблицаПравилОтражения);
	
	НачатьТранзакцию();
	Попытка
		НаборЗаписейПравилаОтражения.Записать();
		НаборЗаписейИгнорируемыеСчета.Записать();
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;

	
	ОбновляемыеСчета = ТаблицаСоответствийСчетов.ВыгрузитьКолонку("Ссылка");
	Если ОбновляемыеСчета.Количество() > 0 И Не Игнорировать Тогда
		
		СпискиДляОбновления = Новый Массив;
		СпискиДляОбновления.Добавить("ПланСчетовРеглУчета");
		СпискиДляОбновления.Добавить("СоответствияСчетовМеждународногоУчета");
		СпискиДляОбновления.Добавить("ПланСчетовМеждународногоУчета");
		
		ИзменитьВключениеВНастройкуФормированияПроводокНаСервере(ОбновляемыеСчета, Истина, СпискиДляОбновления);
		
	КонецЕсли;
	
	Элементы.СоответствияСчетовМеждународногоУчета.Обновить();
	Элементы.ПланСчетовРеглУчета.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНастройкиПоСчетуМеждународногоУчета(СчетМеждународногоУчета)
	
	ДеревоНастроекПоСчетуМеждународногоУчета.ПолучитьЭлементы().Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫБОР 
	|		КОГДА СоответствияСчетов.ПометкаУдаления 
	|			ТОГДА 4
	|		ИНАЧЕ 3
	|	КОНЕЦ КАК ИндексКартинки,
	|	СоответствияСчетов.Ссылка КАК СоответствиеСчетов,
	|	СоответствияСчетов.СчетРеглУчета КАК СчетРеглУчета,
	|	СоответствияСчетов.ПредставлениеОтбора КАК ПредставлениеОтбора,
	|	СоответствияСчетов.УстановленДополнительныйОтбор КАК УстановленДополнительныйОтбор,
	|	ВЫБОР
	|		КОГДА ПравилаОтражения.ШаблонПроводки ЕСТЬ НЕ NULL 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ДействуетВНастройкеФормированияПроводок
	|ИЗ
	|	Справочник.СоответствияСчетовМеждународногоУчета КАК СоответствияСчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПравилаОтраженияВМеждународномУчете КАК ПравилаОтражения
	|		ПО (ПравилаОтражения.НастройкаФормированияПроводок = &НастройкаФормированияПроводок)
	|			И СоответствияСчетов.Ссылка = ПравилаОтражения.ШаблонПроводки
	|ГДЕ
	|	СоответствияСчетов.СчетМеждународногоУчета = &СчетМеждународногоУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР 
	|		КОГДА СоответсвиеОборотов.ПометкаУдаления 
	|			ТОГДА 4
	|		ИНАЧЕ 3
	|	КОНЕЦ КАК ИндексКартинки,
	|	СоответсвиеОборотов.Ссылка КАК СоответствиеОборотов,
	|	СоответсвиеОборотов.СчетРеглУчетаДт,
	|	СоответсвиеОборотов.СчетРеглУчетаКт,
	|	СоответсвиеОборотов.ПредставлениеОтбора,
	|	СоответсвиеОборотов.УстановленДополнительныйОтбор,
	|	СоответсвиеОборотов.РучноеУточнениеПроводки,
	|	ВЫБОР
	|		КОГДА СоответсвиеОборотов.СчетМеждународногоУчетаДт = &СчетМеждународногоУчета
	|				И СоответсвиеОборотов.СчетМеждународногоУчетаКт = &СчетМеждународногоУчета
	|			ТОГДА &ДтКт
	|		КОГДА СоответсвиеОборотов.СчетМеждународногоУчетаДт = &СчетМеждународногоУчета
	|			ТОГДА &Дт
	|		ИНАЧЕ &Кт
	|	КОНЕЦ КАК ПоложениеСчета,
	|	ВЫБОР
	|		КОГДА ПравилаОтражения.ШаблонПроводки ЕСТЬ НЕ NULL 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ДействуетВНастройкеФормированияПроводок
	|ИЗ
	|	Справочник.СоответствияОборотовМеждународногоУчета КАК СоответсвиеОборотов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПравилаОтраженияВМеждународномУчете КАК ПравилаОтражения
	|		ПО (ПравилаОтражения.НастройкаФормированияПроводок = &НастройкаФормированияПроводок)
	|			И СоответсвиеОборотов.Ссылка = ПравилаОтражения.ШаблонПроводки
	|ГДЕ
	|	(СоответсвиеОборотов.СчетМеждународногоУчетаДт = &СчетМеждународногоУчета
	|			ИЛИ СоответсвиеОборотов.СчетМеждународногоУчетаКт = &СчетМеждународногоУчета)";
	
	Запрос.УстановитьПараметр("НастройкаФормированияПроводок", НастройкаФормированияПроводок);
	Запрос.УстановитьПараметр("СчетМеждународногоУчета", СчетМеждународногоУчета);
	Запрос.УстановитьПараметр("ДтКт", НСтр("ru = 'Дт/Кт';
											|en = 'Dr/Cr'"));
	Запрос.УстановитьПараметр("Дт", НСтр("ru = 'Дт';
										|en = 'Dr'"));
	Запрос.УстановитьПараметр("Кт", НСтр("ru = 'Кт';
										|en = 'Cr'"));
	
	РезультатыЗапросов = Запрос.ВыполнитьПакет();
	
	ВыборкаШаблоныПроводок = РезультатыЗапросов[1].Выбрать();
	Если ВыборкаШаблоныПроводок.Количество() Тогда
		ЭлементШаблоныПроводок = ДеревоНастроекПоСчетуМеждународногоУчета.ПолучитьЭлементы().Добавить();
		ЭлементШаблоныПроводок.ГруппаНастроек = НСтр("ru = 'Соответствия оборотов';
													|en = 'Turnover compliance'");
		ЭлементыШаблоныПроводок = ЭлементШаблоныПроводок.ПолучитьЭлементы();
		Пока ВыборкаШаблоныПроводок.Следующий() Цикл
			НовыйЭлемент = ЭлементыШаблоныПроводок.Добавить();
			ЗаполнитьЗначенияСвойств(НовыйЭлемент, ВыборкаШаблоныПроводок);
		КонецЦикла;
	КонецЕсли;
	
	ВыборкаСоответствияСчетов = РезультатыЗапросов[0].Выбрать();
	Если ВыборкаСоответствияСчетов.Количество() Тогда
		ЭлементСоответствиеСчетов = ДеревоНастроекПоСчетуМеждународногоУчета.ПолучитьЭлементы().Добавить();
		ЭлементСоответствиеСчетов.ГруппаНастроек = НСтр("ru = 'Соответствия счетов';
														|en = 'Correspondences of accounts'");
		ЭлементыСоответствиеСчетов = ЭлементСоответствиеСчетов.ПолучитьЭлементы();
		Пока ВыборкаСоответствияСчетов.Следующий() Цикл
			НовыйЭлемент = ЭлементыСоответствиеСчетов.Добавить();
			ЗаполнитьЗначенияСвойств(НовыйЭлемент, ВыборкаСоответствияСчетов);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьВключениеВНастройкуФормированияПроводокНаСервере(Соответствия, ВключитьВНастройкуФормированияПроводок, СпискиДляОбновления)
	
	Если Не ПроверитьЗаполнениеНастройкиФормированияПроводок() Тогда
		Возврат;
	КонецЕсли;

	НаборЗаписей = РегистрыСведений.ПравилаОтраженияВМеждународномУчете.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.НастройкаФормированияПроводок.Установить(НастройкаФормированияПроводок);
	
	НаборЗаписей.Прочитать();
	ТаблицаЗаписей = НаборЗаписей.Выгрузить();
	
	Для каждого Соответствие Из Соответствия Цикл
		
		Запись = ТаблицаЗаписей.Найти(Соответствие, "ШаблонПроводки");
		Если Запись = Неопределено И ВключитьВНастройкуФормированияПроводок Тогда
			НоваяСтрока = ТаблицаЗаписей.Добавить();
			НоваяСтрока.НастройкаФормированияПроводок = НастройкаФормированияПроводок;
			НоваяСтрока.ШаблонПроводки = Соответствие;
		ИначеЕсли Запись <> Неопределено И Не ВключитьВНастройкуФормированияПроводок Тогда
			ТаблицаЗаписей.Удалить(Запись);
		КонецЕсли;
	КонецЦикла;
	
	НаборЗаписей.Загрузить(ТаблицаЗаписей);
	НаборЗаписей.Записать();
	
	Для каждого Список Из СпискиДляОбновления Цикл
		Элементы[Список].Обновить();
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииОтбораСчетовСервер(СписокСчетов, ЗначениеОтбора)
	
	ИспользоватьОтборНастроеноСоответствие = ?(ЗначениеОтбора = "СНастроеннымСоответствием", Истина, Ложь);
	ИспользоватьОтборТребуетсяНастройка = ?(ЗначениеОтбора = "ТребуетсяНастройкаСоответствия", Истина, Ложь);
	УстановитьОтбор =  ЗначениеОтбора <> "";
	
	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(
		ЭтаФорма[СписокСчетов], "НастроеноСоответствие", Истина, ИспользоватьОтборНастроеноСоответствие);
	
	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(
		ЭтаФорма[СписокСчетов], "ТребуетсяНастройка", Истина, ИспользоватьОтборТребуетсяНастройка);
		
	Если УстановитьОтбор Тогда
		Элементы[СписокСчетов].Отображение = ОтображениеТаблицы.Список;
	Иначе
		Элементы[СписокСчетов].Отображение = ОтображениеТаблицы.Дерево;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииСервер()
	
	ПолучитьСтатусНастройки();
	
КонецПроцедуры

&НаСервере
Процедура ПланСчетовПриИзмененииСервер()
	
	НастроитьДинамическиеСпискиПоПлануСчетов();
	
	НастройкаФормированияПроводок = 
		Справочники.НастройкиФормированияПроводокМеждународногоУчета.НастройкаФормированияПроводокПоУмолчанию(ПланСчетов);
	НастройкаФормированияПроводокПриИзмененииСервер();
	
КонецПроцедуры

&НаСервере
Процедура НастройкаФормированияПроводокПриИзмененииСервер()
	
	ПолучитьСтатусНастройки();
	НастроитьДинамическиеСпискиПоНастройкеФормированияПроводок();
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьСтатусНастройки()
	
	Если Не ЗначениеЗаполнено(ПланСчетов) 
			Или Не ЗначениеЗаполнено(Организация)
			Или Не ЗначениеЗаполнено(НастройкаФормированияПроводок) Тогда
		СтатусНастройки = Неопределено;
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МАКСИМУМ(ПланыСчетовМеждународногоУчета.Период) КАК Период
	|ПОМЕСТИТЬ ВтПрименяетсяС
	|ИЗ
	|	РегистрСведений.ПланыСчетовМеждународногоУчетаОрганизаций КАК ПланыСчетовМеждународногоУчета
	|ГДЕ
	|	ПланыСчетовМеждународногоУчета.Организация = &Организация
	|	И ПланыСчетовМеждународногоУчета.ПланСчетов = &ПланСчетов
	|	И ПланыСчетовМеждународногоУчета.НастройкаФормированияПроводок = &НастройкаФормированияПроводок
	|;
	|
	|/////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрименяетсяС.Период КАК ПериодС,
	|	МИНИМУМ(ЕСТЬNULL(ПланыСчетовМеждународногоУчета.Период, &ПустаяДата)) КАК ПериодПо
	|ИЗ
	|	ВтПрименяетсяС КАК ПрименяетсяС
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		РегистрСведений.ПланыСчетовМеждународногоУчетаОрганизаций КАК ПланыСчетовМеждународногоУчета
	|	ПО
	|		ПланыСчетовМеждународногоУчета.Организация = &Организация
	|		И ПланыСчетовМеждународногоУчета.ПланСчетов = &ПланСчетов
	|		И ПланыСчетовМеждународногоУчета.НастройкаФормированияПроводок <> &НастройкаФормированияПроводок
	|		И ПланыСчетовМеждународногоУчета.Период >ПрименяетсяС.Период
	|ГДЕ
	|	ПрименяетсяС.Период <> &ПустаяДата
	|
	|СГРУППИРОВАТЬ ПО
	|	ПрименяетсяС.Период";
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПланСчетов", ПланСчетов);
	Запрос.УстановитьПараметр("НастройкаФормированияПроводок", НастройкаФормированияПроводок);
	Запрос.УстановитьПараметр("ПустаяДата", Дата(1,1,1));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		Если ЗначениеЗаполнено(Выборка.ПериодПо) Тогда
			Представление = СтрШаблон(НСтр("ru = 'Применялась с %1 по %2';
											|en = 'Has been applied since %1 till %2'"), 
													Формат(Выборка.ПериодС, "ДЛФ=D"), Формат(Выборка.ПериодПо, "ДЛФ=D"));
			СтатусНастройки = Новый ФорматированнаяСтрока(
													Представление, , , , "ОткрытьИсториюПланыСчетовОрганизаций");
		Иначе
			Представление = СтрШаблон(НСтр("ru = 'Применяется с %1';
											|en = 'Applied since %1'"), Формат(Выборка.ПериодС, "ДЛФ=D"));
			СтатусНастройки = Новый ФорматированнаяСтрока(
													Представление, , , , "ОткрытьИсториюПланыСчетовОрганизаций");
		КонецЕсли;
		
	ИначеЕсли ПравоДоступа("Изменение", Метаданные.РегистрыСведений.ПланыСчетовМеждународногоУчетаОрганизаций) Тогда 
		Представление = СтрШаблон(НСтр("ru = 'Включить для организации с %1';
										|en = 'Enable for company since %1'"), Формат(ТекущийМесяц, "ДЛФ=D"));
		СтатусНастройки = Новый ФорматированнаяСтрока(Представление, , ЦветаСтиля.ПоясняющийОшибкуТекст, , "СоздатьЗаписьПланыСчетовОрганизаций");
	Иначе
		Представление = СтрШаблон(НСтр("ru = 'Не применяется для организации';
										|en = 'Not applied for the company'"));
		СтатусНастройки = Новый ФорматированнаяСтрока(Представление, , ЦветаСтиля.ПоясняющийОшибкуТекст);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборПериодаЗаписиПланыСчетовОрганизаций(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;
	
	СоздатьЗаписьПланыСчетовОрганизаций(Результат);
	
КонецПроцедуры

&НаСервере
Процедура СоздатьЗаписьПланыСчетовОрганизаций(Период)
	
	МенеджерЗаписи = РегистрыСведений.ПланыСчетовМеждународногоУчетаОрганизаций.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Период = Период;
	МенеджерЗаписи.ПланСчетов = ПланСчетов;
	МенеджерЗаписи.Организация = Организация;
	МенеджерЗаписи.НастройкаФормированияПроводок = НастройкаФормированияПроводок;
	МенеджерЗаписи.Записать();
	
	ПолучитьСтатусНастройки();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьДинамическиеСпискиПоПлануСчетов()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		СоответствияСчетовМеждународногоУчета, 
		"ПланСчетов",
		ПланСчетов,
		,
		,
		Истина,
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		СоответствияОборотовМеждународногоУчета,
		"ПланСчетов",
		ПланСчетов,
		,
		,
		Истина,
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		ПланСчетовМеждународногоУчета,
		"ПланСчетов",
		ПланСчетов,
		,
		,
		Истина,
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		ПланСчетовРеглУчета,
		"ПланСчетов",
		ПланСчетов,
		Истина);
		
КонецПроцедуры


#КонецОбласти




