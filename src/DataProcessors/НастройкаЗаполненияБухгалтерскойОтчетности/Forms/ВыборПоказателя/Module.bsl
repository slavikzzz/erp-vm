#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗначениеВРеквизитФормы(ПолучитьИзВременногоХранилища(Параметры.Показатели), "Показатели");
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПоказатели

&НаКлиенте
Процедура ПоказателиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Не ЗавершитьВыбор(ВыбраннаяСтрока) Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьПоказатель(Команда)
	
	ЗавершитьВыбор(Элементы.Показатели.ТекущаяСтрока);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, "ПоказателиНаименование");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		Элемент.Отбор,
		"Показатели.Картинка",
		ВидСравненияКомпоновкиДанных.Равно,
		ИндексКартинкиПоУмолчанию());
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", Новый Шрифт( , , Истина));
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИндексКартинкиПоУмолчанию()
	Возврат -1;
КонецФункции

&НаКлиенте
Функция ЗавершитьВыбор(ВыбраннаяСтрока)
	
	Показатель = Показатели.НайтиПоИдентификатору(ВыбраннаяСтрока);
	Если Показатель = Неопределено Или Не Показатель.ВыборРазрешен Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Переместить в эту строку нельзя.
                                  |Показатели можно перемещать между группами, входящие в них объекты - между показателями.';
                                  |en = 'Переместить в эту строку нельзя.
                                  |Показатели можно перемещать между группами, входящие в них объекты - между показателями.'"));
		Возврат Ложь;
	КонецЕсли;
	
	Закрыть(Показатель.ВыбраннаяСтрока);
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти
