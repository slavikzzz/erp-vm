//++ Устарело_Производство21
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// Проверка возможности открытия формы /отказ от открытия формы.
	Если Не ЗначениеЗаполнено(Параметры.Номенклатура) Тогда
		ТекстИсключения = НСтр("ru = 'Форма не предназначена для непосредственного открытия.';
								|en = 'Form is not intended for direct opening.'");
		ВызватьИсключение ТекстИсключения;
		Возврат;
	КонецЕсли;
	
	// Инициализация реквизитов ЕдиницаИзмерения, Характеристика и ВладелецХарактеристик.
	УправляющиеДанныеПоНоменклатуре = УправляющиеДанныеПоНоменклатуре(Параметры.Номенклатура);
	ЗаполнитьЗначенияСвойств(ЭтаФорма, УправляющиеДанныеПоНоменклатуре, "ЕдиницаИзмерения, Характеристика, ВладелецХарактеристик");
	Если ВладелецХарактеристик <> Неопределено Тогда
		КэшируемаяХарактеристика = Характеристика;
		Характеристика = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
	КонецЕсли;
	
	ЗаполнитьНастройкуПередачиМатериалов();
	
	МассивИменЭлементов = Новый Массив();
	Если ВладелецХарактеристик = Неопределено Тогда
		
		МассивИменЭлементов.Добавить("ГруппаВыбораРежимаДляНастройкиПередачиМатериалов");
		МассивИменЭлементов.Добавить("РасшифроватьПоХарактеристикамНастройкуПередачиМатериалов");
		МассивИменЭлементов.Добавить("НастройкаПередачиМатериаловВПроизводствоКонтекстноеМенюРасшифроватьПоХарактеристикам");
		
	КонецЕсли;
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивИменЭлементов, "Видимость", Ложь);
	
	// Устанавливаем параметры выбора характеристик
	Если ВладелецХарактеристик <> Неопределено Тогда
		
		ПараметрВладелец = Новый ПараметрВыбора("Отбор.Владелец", ВладелецХарактеристик);
		ПараметрыВыбора = Новый Массив();
		ПараметрыВыбора.Добавить(ПараметрВладелец);
		
		Элементы.ХарактеристикаДляНастройкиПередачиМатериалов.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "НастройкаПередачиМатериаловВПроизводство", "ТолькоПросмотр",
		НЕ ПравоДоступа("Изменение", Метаданные.РегистрыСведений.НастройкаПередачиМатериаловВПроизводство));
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ВладелецХарактеристик <> Неопределено Тогда
		РежимНоменклатурыВЦелом = "Да";
		УстановитьТекущуюСтраницуХарактеристики();
	Иначе
		РежимНоменклатурыВЦелом = "Нет";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_НастройкаПередачиМатериаловВПроизводство" Тогда
		ЗаполнитьНастройкуПередачиМатериалов();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

&НаКлиенте
Процедура РежимНоменклатурыВЦеломДляНастройкиПередачиМатериаловПриИзменении(Элемент)
	
	УстановитьТекущуюСтраницуХарактеристики();
	
	Если РежимНоменклатурыВЦелом = "Да" Тогда
		КэшируемаяХарактеристика = Характеристика;
		Характеристика = ПредопределенноеЗначение("Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка");
	Иначе
		Характеристика = КэшируемаяХарактеристика;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Характеристика) Или РежимНоменклатурыВЦелом = "Да" Тогда
		ЗаполнитьНастройкуПередачиМатериалов();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ХарактеристикаДляНастройкиПередачиМатериаловОчистка(Элемент, СтандартнаяОбработка)
	
	Стандартнаяобработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ХарактеристикаДляНастройкиПередачиМатериаловПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Характеристика) Или РежимНоменклатурыВЦелом = "Да" Тогда
		ЗаполнитьНастройкуПередачиМатериалов();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНастройкаПередачиМатериаловВПроизводство

&НаКлиенте
Процедура НастройкаПередачиМатериаловВПроизводствоВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ЭтоРежимНоменклатурыВЦелом = РежимНоменклатурыВЦелом = "Да";
	
	Если ЭтоРежимНоменклатурыВЦелом Тогда
		
		ТекстСообщения = НСтр("ru = 'Изменение настройки возможно только для конкретной характеристики.';
								|en = 'You can change the setting only for a specific characteristic.'");
		ОчиститьСообщения();
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "ХарактеристикаДляНастройкиПередачиМатериалов");
		Возврат;
		
	КонецЕсли;
	
	ТекущиеДанные = Элементы.НастройкаПередачиМатериаловВПроизводство.ТекущиеДанные;
	
	ПараметрыФормы = Новый Структура("Подразделение,Номенклатура,Характеристика",
							ТекущиеДанные.Подразделение,
							Параметры.Номенклатура,
							Характеристика);
	ОткрытьФорму("РегистрСведений.НастройкаПередачиМатериаловВПроизводство.Форма.ФормаНастройки",
		ПараметрыФормы, ЭтаФорма,,, УникальныйИдентификатор, Неопределено, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаПередачиМатериаловВПроизводствоПодразделениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаПередачиМатериаловВПроизводствоПодразделениеОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаЗаполнитьСкладНастройкиПередачиМатериалов(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("КомандаЗаполнитьСкладНастройкиПередачиМатериаловЗавершение", ЭтотОбъект);
	ОткрытьФорму("Справочник.Склады.ФормаВыбора",, 
					ЭтаФорма,,, 
					УникальныйИдентификатор, 
					ОписаниеОповещения, 
					РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаполнитьОснованиеЗначениемПоЗаказуНаВнутреннееПотребление(Команда)
	
	ЗаполнитьОснованиеНаСервере("ПоЗаказуНаВнутреннееПотребление");
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаполнитьОснованиеЗначениемПоЗаказуНаПроизводство(Команда)
	
	ЗаполнитьОснованиеНаСервере("ПоЗаказуНаПроизводство");
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифроватьПоХарактеристикамНастройкуПередачиМатериалов(Команда)
	
	СтрокаНастройки = Элементы.НастройкаПередачиМатериаловВПроизводство.ТекущиеДанные;
	
	Если СтрокаНастройки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("Подразделение,Номенклатура,Характеристика", 
					СтрокаНастройки.Подразделение, 
					Параметры.Номенклатура,
					Характеристика);
					
	ОткрытьФорму("Обработка.НастройкаПередачиМатериаловВПроизводство.Форма",
		ПараметрыФормы, ЭтаФорма,,, УникальныйИдентификатор, Неопределено, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ИнициализацияДанныхФормы

// Функция УправляющиеДанныеПоНоменклатуре возвращает структуру с полями ЕдиницаИзмерения, Характеристика и ВладелецХарактеристик.
// Предназначена для инициализации реквизитов формы, активно используемых для реализации логики работы с таблицей НастройкаПередачиМатериаловВПроизводство.
//
&НаСервере
Функция УправляющиеДанныеПоНоменклатуре(Номенклатура)
	
	УправляющиеДанныеПоНоменклатуре = Новый Структура("Характеристика, ВладелецХарактеристик, ЕдиницаИзмерения");
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Ссылка", Номенклатура);
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ВЫБОР Номенклатура.ИспользованиеХарактеристик
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеДляВидаНоменклатуры) ТОГДА
		|			ВидыНоменклатуры.Ссылка
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ИндивидуальныеДляНоменклатуры) ТОГДА
		|			&Ссылка
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеСДругимВидомНоменклатуры) ТОГДА
		|			Номенклатура.ВладелецХарактеристик
		|		ИНАЧЕ
		|			НЕОПРЕДЕЛЕНО
		|	КОНЕЦ                          КАК ВладелецХарактеристик,
		|	Характеристики.Ссылка          КАК Характеристика
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
		|		ПО Номенклатура.ВидНоменклатуры = ВидыНоменклатуры.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК Характеристики
		|		ПО Характеристики.Владелец
		|			= ВЫБОР Номенклатура.ИспользованиеХарактеристик
		|				КОГДА ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеДляВидаНоменклатуры) ТОГДА
		|					ВидыНоменклатуры.Ссылка
		|				КОГДА ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ИндивидуальныеДляНоменклатуры) ТОГДА
		|					&Ссылка
		|				КОГДА ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеСДругимВидомНоменклатуры) ТОГДА
		|					Номенклатура.ВладелецХарактеристик
		|			КОНЕЦ
		|ГДЕ
		|	Номенклатура.Ссылка = &Ссылка";
		
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	ЗаполнитьЗначенияСвойств(УправляющиеДанныеПоНоменклатуре, Выборка);
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристикиНоменклатуры") Тогда
		УправляющиеДанныеПоНоменклатуре.ВладелецХарактеристик = Неопределено;
	КонецЕсли;
	
	Возврат УправляющиеДанныеПоНоменклатуре;
	
КонецФункции

#КонецОбласти

#Область Прочие

&НаСервере
Процедура ЗаполнитьНастройкуПередачиМатериалов()

	Подразделение = Неопределено;
	ТекущаяСтрока = Элементы.НастройкаПередачиМатериаловВПроизводство.ТекущаяСтрока;
	Если ТекущаяСтрока <> Неопределено Тогда
		ТекущиеДанные = НастройкаПередачиМатериаловВПроизводство.НайтиПоИдентификатору(ТекущаяСтрока);
		Подразделение = ТекущиеДанные.Подразделение;
	КонецЕсли;  
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СтруктураПредприятия.Ссылка КАК Подразделение,
	|	СтруктураПредприятия.Наименование КАК ПодразделениеНаименование,
	|	ЕСТЬNULL(НастройкаПередачиХарактеристика.Склад, 
	|		ЕСТЬNULL(НастройкаПередачиНоменклатура.Склад, 
	|			ЕСТЬNULL(НастройкаПередачиСклад.Склад, ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)))
	|	) КАК Склад,
	|	ЕСТЬNULL(НастройкаПередачиХарактеристика.ОснованиеДляПолучения, 
	|		ЕСТЬNULL(НастройкаПередачиНоменклатура.ОснованиеДляПолучения, 
	|			ЕСТЬNULL(НастройкаПередачиСклад.ОснованиеДляПолучения, 
	|				ЗНАЧЕНИЕ(Перечисление.ОснованияДляПолученияМатериаловВПроизводстве.ПоЗаказуНаПроизводство)))
	|	) КАК ОснованиеДляПолучения,
	|	ВЫБОР
	|		КОГДА НЕ НастройкаПередачиХарактеристика.ОснованиеДляПолучения ЕСТЬ NULL
	|			ТОГДА &НастраиваетсяИндивидуально
	|		ИНАЧЕ &ОпределяетсяНастройкойДляПодразделения 
	|	КОНЕЦ КАК СпособНастройки
	|ИЗ
	|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаПередачиМатериаловВПроизводство КАК НастройкаПередачиХарактеристика
	|		ПО (НастройкаПередачиХарактеристика.Подразделение = СтруктураПредприятия.Ссылка)
	|			И (НастройкаПередачиХарактеристика.Номенклатура = &Номенклатура)
	|			И (НастройкаПередачиХарактеристика.Характеристика = &Характеристика)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаПередачиМатериаловВПроизводство КАК НастройкаПередачиНоменклатура
	|		ПО (НастройкаПередачиНоменклатура.Подразделение = СтруктураПредприятия.Ссылка)
	|			И (НастройкаПередачиНоменклатура.Номенклатура = &Номенклатура)
	|			И (НастройкаПередачиНоменклатура.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
	|			И (НастройкаПередачиХарактеристика.Подразделение ЕСТЬ NULL )
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаПередачиМатериаловВПроизводство КАК НастройкаПередачиСклад
	|		ПО (НастройкаПередачиСклад.Подразделение = СтруктураПредприятия.Ссылка)
	|			И (НастройкаПередачиСклад.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка))
	|			И (НастройкаПередачиСклад.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
	|			И (НастройкаПередачиХарактеристика.Подразделение ЕСТЬ NULL )
	|			И (НастройкаПередачиНоменклатура.Подразделение ЕСТЬ NULL )
	|ГДЕ 
	|	НЕ СтруктураПредприятия.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПодразделениеНаименование";
	
	Запрос.УстановитьПараметр("Номенклатура",   Параметры.Номенклатура);
	Запрос.УстановитьПараметр("Характеристика", Характеристика);
	Запрос.УстановитьПараметр("ОпределяетсяНастройкойДляПодразделения", НСтр("ru = '<как для подразделения в целом>';
																			|en = '<as for business unit as a whole>'"));
	Запрос.УстановитьПараметр("НастраиваетсяИндивидуально", НСтр("ru = '<настраивается индивидуально>';
																|en = '<set individually>'"));
	
	НастройкаПередачиМатериаловВПроизводство.Загрузить(Запрос.Выполнить().Выгрузить());

	Если Подразделение <> Неопределено Тогда
		Для каждого ЭлементКоллекции Из НастройкаПередачиМатериаловВПроизводство Цикл
			Если ЭлементКоллекции.Подразделение = Подразделение Тогда
				Элементы.НастройкаПередачиМатериаловВПроизводство.ТекущаяСтрока = ЭлементКоллекции.ПолучитьИдентификатор();
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСкладНаСервере(Склад)

	Для каждого ИдентификаторСтроки Из Элементы.НастройкаПередачиМатериаловВПроизводство.ВыделенныеСтроки Цикл
		
		ДанныеСтроки = НастройкаПередачиМатериаловВПроизводство.НайтиПоИдентификатору(ИдентификаторСтроки);
		
		ДанныеСтроки.Склад = Склад;
		ДанныеСтроки.СпособНастройки = НСтр("ru = '<индивидуально для материала>';
											|en = '<individually for materials>'");
		
		МенеджерЗаписи = РегистрыСведений.НастройкаПередачиМатериаловВПроизводство.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Подразделение  = ДанныеСтроки.Подразделение;
		МенеджерЗаписи.Номенклатура   = Параметры.Номенклатура;
		МенеджерЗаписи.Характеристика = Характеристика;
		МенеджерЗаписи.Склад          = Склад;
		МенеджерЗаписи.ОснованиеДляПолучения = ДанныеСтроки.ОснованиеДляПолучения;
		
		МенеджерЗаписи.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОснованиеНаСервере(ОснованиеДляПолученияИмя)

	ОснованиеДляПолучения = Перечисления.ОснованияДляПолученияМатериаловВПроизводстве[ОснованиеДляПолученияИмя];
	
	Для каждого ИдентификаторСтроки Из Элементы.НастройкаПередачиМатериаловВПроизводство.ВыделенныеСтроки Цикл
		
		ДанныеСтроки = НастройкаПередачиМатериаловВПроизводство.НайтиПоИдентификатору(ИдентификаторСтроки);
		
		ДанныеСтроки.ОснованиеДляПолучения = ОснованиеДляПолучения;
		ДанныеСтроки.СпособНастройки = НСтр("ru = '<индивидуально для материала>';
											|en = '<individually for materials>'");
		
		МенеджерЗаписи = РегистрыСведений.НастройкаПередачиМатериаловВПроизводство.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Подразделение  = ДанныеСтроки.Подразделение;
		МенеджерЗаписи.Номенклатура   = Параметры.Номенклатура;
		МенеджерЗаписи.Характеристика = Характеристика;
		МенеджерЗаписи.Склад          = ДанныеСтроки.Склад;
		МенеджерЗаписи.ОснованиеДляПолучения = ОснованиеДляПолучения;
		
		МенеджерЗаписи.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьТекущуюСтраницуХарактеристики()
	
	Элементы.СтраницыДоступностьХарактеристикиДляНастройкиПередачиМатериалов.ТекущаяСтраница = ?(РежимНоменклатурыВЦелом = "Да",
		Элементы.СтраницаХарактеристикаНедоступнаДляНастройкиПередачиМатериалов,
		Элементы.СтраницаХарактеристикаДоступнаДляНастройкиПередачиМатериалов);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаполнитьСкладНастройкиПередачиМатериаловЗавершение(Результат, ДополнительныеПараметры) Экспорт

	РезультатВыбора = Результат;
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСкладНаСервере(РезультатВыбора);

КонецПроцедуры

#КонецОбласти

#КонецОбласти
//-- Устарело_Производство21