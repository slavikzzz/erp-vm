
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// Параметры выбора Работы
	ПараметрыВыбора = Новый Массив;
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.ТипНоменклатуры", Перечисления.ТипыНоменклатуры.Работа));
	
	СписокВариантов = Новый СписокЗначений();
	СписокВариантов.Добавить(Перечисления.ВариантыИспользованияХарактеристикНоменклатуры.ИндивидуальныеДляНоменклатуры);
	СписокВариантов.Добавить(Перечисления.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеДляВидаНоменклатуры);
	СписокВариантов.Добавить(Перечисления.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеСДругимВидомНоменклатуры);
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.ИспользованиеХарактеристик", СписокВариантов));
	
	Элементы.Работа.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
	
	// Настройки системы
	ИспользоватьКачество = ИспользоватьКачество();
	
	// Чтение общих настроек
	УстановитьПривилегированныйРежим(Истина);
	ЗначениеНастроек = Константы.НастройкиДеленияПартийПроизводства.Получить().Получить();
	УстановитьПривилегированныйРежим(Ложь);
	Если ТипЗнч(ЗначениеНастроек) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ЗначениеНастроек);
	КонецЕсли;
	
	// Чтение пользовательских настроек
	ЗначениеНастроек = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("Обработка.ДелениеПартииПроизводства", "");
	Если ТипЗнч(ЗначениеНастроек) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ЗначениеНастроек);
	КонецЕсли;
	
	// Настройка элементов
	Элементы.ГруппаАналоги.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьАналогиМатериалов");
	ЗаполнитьПоСпецификацииФорма = ЗаполнитьПоСпецификации;
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Не ИспользоватьРаботу Тогда
		ПорядковыйНомер = ПроверяемыеРеквизиты.Найти("Работа");
		Если ПорядковыйНомер <> Неопределено Тогда
			ПроверяемыеРеквизиты.Удалить(ПорядковыйНомер);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_НаборКонстант"
		И Источник = "ИспользоватьКачествоТоваров" Тогда
		
		ИспользоватьКачество = ИспользоватьКачество();
		
		Если Не ИспользоватьКачество
			И ИспользоватьТоварыДругогоКачества Тогда
			ИспользоватьТоварыДругогоКачества = Ложь;
		КонецЕсли;
		
		НастроитьЗависимыеЭлементыФормы(ЭтотОбъект, "ИспользоватьКачество");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИспользоватьРаботуПриИзменении(Элемент)
	
	Если Не ИспользоватьРаботу Тогда
		Работа = Неопределено
	КонецЕсли;
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект, "ИспользоватьРаботу");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоСпецификацииФормаПриИзменении(Элемент)
	
	ЗаполнитьПоСпецификации = ЗаполнитьПоСпецификацииФорма;
	
	Если Не ЗаполнитьПоСпецификации Тогда
		РезервироватьМатериалы = Ложь;
		ЗаменятьМатериалыНаАналоги = Ложь;
	КонецЕсли;
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект, "ЗаполнитьПоСпецификации");
	
КонецПроцедуры

&НаКлиенте
Процедура РезервироватьМатериалыПриИзменении(Элемент)
	
	Если Не РезервироватьМатериалы Тогда
		ЗаменятьМатериалыНаАналоги = Ложь;
	КонецЕсли;
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект, "РезервироватьМатериалы");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Применить(Команда)
	
	Отказ = Ложь;
	СохранитьНаСервере(Отказ);
	
	Если Отказ Тогда
		Возврат;
	Иначе
		Закрыть(Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЗависимыеЭлементыФормы(Форма, СписокРеквизитов = "")
	
	Элементы = Форма.Элементы;
	
	Инициализация = ПустаяСтрока(СписокРеквизитов);
	СтруктураРеквизитов = Новый Структура(СписокРеквизитов);
	
	Если СтруктураРеквизитов.Свойство("ИспользоватьРаботу")
		Или Инициализация Тогда
		
		Элементы.Работа.Доступность = Форма.ИспользоватьРаботу;
		
	КонецЕсли;
	
	Если СтруктураРеквизитов.Свойство("ИспользоватьКачество")
		Или Инициализация Тогда
		
		Элементы.ИспользоватьТоварыДругогоКачества.Доступность = Форма.ИспользоватьКачество;
		Элементы.ИспользоватьКачествоПредупреждение.Видимость = Не Форма.ИспользоватьКачество;
		
	КонецЕсли;
	
	Если СтруктураРеквизитов.Свойство("ЗаполнитьПоСпецификации")
		Или Инициализация Тогда
		
		Элементы.РезервироватьМатериалы.Доступность = Форма.ЗаполнитьПоСпецификации;
		
	КонецЕсли;
	
	Если СтруктураРеквизитов.Свойство("ЗаполнитьПоСпецификации")
		Или СтруктураРеквизитов.Свойство("РезервироватьМатериалы")
		Или Инициализация Тогда
			
		Элементы.ЗаменятьМатериалыНаАналоги.Доступность = Форма.ЗаполнитьПоСпецификации
			И Форма.РезервироватьМатериалы;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИспользоватьКачество()
	
	Возврат ПолучитьФункциональнуюОпцию("ИспользоватьКачествоТоваров");
	
КонецФункции

&НаСервере
Процедура СохранитьНаСервере(Отказ)
	
	Если Не ПроверитьЗаполнение() Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	// Сохранение общих настроек
	ЗначениеНастроек = Новый Структура;
	ЗначениеНастроек.Вставить("ИспользоватьРаботу", ИспользоватьРаботу);
	ЗначениеНастроек.Вставить("Работа", Работа);
	ЗначениеНастроек.Вставить("ИспользоватьТоварыДругогоКачества", ИспользоватьТоварыДругогоКачества);
	
	УстановитьПривилегированныйРежим(Истина);
	Константы.НастройкиДеленияПартийПроизводства.Установить(Новый ХранилищеЗначения(ЗначениеНастроек));
	УстановитьПривилегированныйРежим(Ложь);
	
	// Сохранение пользовательских настроек
	ЗначениеНастроек = Новый Структура;
	ЗначениеНастроек.Вставить("ЗаполнитьПоСпецификации", ЗаполнитьПоСпецификации);
	ЗначениеНастроек.Вставить("РезервироватьМатериалы", РезервироватьМатериалы);
	ЗначениеНастроек.Вставить("ЗаменятьМатериалыНаАналоги", ЗаменятьМатериалыНаАналоги);
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("Обработка.ДелениеПартииПроизводства", "", ЗначениеНастроек);
	
КонецПроцедуры

#КонецОбласти
