#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции 

#Область Печать

// Сформировать печатные формы объектов
//
// Параметры:
// 		МассивОбъектов - Массив - Массив ссылок на объекты которые нужно распечатать
// 		ПараметрыПечати - Структура - Структура дополнительных параметров печати
// 		КоллекцияПечатныхФорм - ТаблицаЗначений - Сформированные табличные документы
// 		ОбъектыПечати - Объекты печати
// 		ПараметрыВывода - Структура - Параметры сформированных табличных документов.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СправкаРасчетНачисленияПроцентов") Тогда
		
		ТабличныйДокумент = СформироватьПечатнуюФормуСправкиРасчета(МассивОбъектов, ОбъектыПечати, ПараметрыПечати);
		ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СПРАВКАРАСЧЕТПРОЦЕНТЫ";
		
		СинонимМакета = НСтр("ru = 'Справка-расчет начисления процентов по переданным в аренду ОС';
							|en = 'Sheet of charged interest on leased fixed assets'");
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"СправкаРасчетНачисленияПроцентов",
			СинонимМакета,
			ТабличныйДокумент);
		
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьПечатнуюФормуСправкиРасчета(Документ, ОбъектыПечати, ПараметрыПечати)

	УстановитьПривилегированныйРежим(Истина);
	
	ДанныеДокумента = ПолучитьДанныеСправкиРасчета(Документ, ПараметрыПечати.ДанныеДляПечати);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ОриентацияСтраницы  = ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СПРАВКАРАСЧЕТПРОЦЕНТЫ";
	ТабличныйДокумент.ТолькоПросмотр = Истина;
	
	Макет = Обработки.ПечатьСправкиРасчетаНачисленияПроцентов.ПолучитьМакет("ПФ_MXL_СправкаРасчетНачисленияПроцентов");
	
	ОбластьШапкиСправки = Макет.ПолучитьОбласть("Шапка_Справка");
	ВыборкаПоДокументам = ДанныеДокумента.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаПоДокументам.Следующий() Цикл

		ДанныеПечати = Новый Структура("Дата, Номер, Представление");
		ЗаполнитьЗначенияСвойств(ДанныеПечати, ВыборкаПоДокументам);
		ОбластьШапкиСправки.Параметры.Документ = ОбщегоНазначенияУТКлиентСервер.СформироватьЗаголовокДокумента(
			ДанныеПечати);

		ТабличныйДокумент.Вывести(ОбластьШапкиСправки);

		ОбластьШапкаОС  = Макет.ПолучитьОбласть("Шапка_ОС");
		ОбластьСтрокаОС = Макет.ПолучитьОбласть("Строка_ОС");
		ОбластьИтогоОС  = Макет.ПолучитьОбласть("Итого_ОС");

		ВыборкаПоОС = ВыборкаПоДокументам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

		Пока ВыборкаПоОС.Следующий() Цикл
			ОбластьШапкаОС.Параметры.Заполнить(ВыборкаПоОС);
			ТабличныйДокумент.Вывести(ОбластьШапкаОС);
			ИтогоДнейДолга = 0;

			ВыборкаПоДатам = ВыборкаПоОС.Выбрать();
			
			// Расчет суммы оставшегося долга и количество дней долга выполним в процессе вывода.
			ЭтоПерваяСтрокаПоОС = Истина;
			Пока ВыборкаПоДатам.Следующий() Цикл

				Если ЭтоПерваяСтрокаПоОС Тогда
					СуммаДолгаБезНДС = ВыборкаПоДатам.ПриведеннаяСтоимость + ВыборкаПоДатам.НГЛС;
					ПредыдущаяДата = НачалоДня(Документ.Дата);
					ЭтоПерваяСтрокаПоОС = Ложь;
				КонецЕсли;

				ДнейДолга = (ВыборкаПоДатам.ДатаГрафика - ПредыдущаяДата) / 86400;
				ИтогоДнейДолга = ИтогоДнейДолга + ДнейДолга;
				ПредыдущаяДата = ВыборкаПоДатам.ДатаГрафика;

				ОбластьСтрокаОС.Параметры.Заполнить(ВыборкаПоДатам);
				ОбластьСтрокаОС.Параметры.ДнейДолга = ДнейДолга;
				ОбластьСтрокаОС.Параметры.СуммаДолгаБезНДС = СуммаДолгаБезНДС;

				ТабличныйДокумент.Вывести(ОбластьСтрокаОС);

				СуммаДолгаБезНДС = Макс(СуммаДолгаБезНДС - ВыборкаПоДатам.СуммаПлатежаБезНДС + ВыборкаПоДатам.Проценты,
					0);

			КонецЦикла;

			ОбластьИтогоОС.Параметры.Заполнить(ВыборкаПоОС);
			ОбластьИтогоОС.Параметры.ИтогоДнейДолга = ИтогоДнейДолга;
			ТабличныйДокумент.Вывести(ОбластьИтогоОС);
		КонецЦикла;

	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ПолучитьДанныеСправкиРасчета(Документ, ДанныеДляПечати) 

	// Конвертируем массив структур дополнительных данных с формы документа в таблицу значений.
	ОписаниеСтавкиДоходности  = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,4,ДопустимыйЗнак.Неотрицательный));
	ОписаниеСумм = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(30,2,ДопустимыйЗнак.Неотрицательный)); 
	
	ТаблицаДополнительныхДанных = Новый ТаблицаЗначений;
	ТаблицаДополнительныхДанных.Колонки.Добавить("ОсновноеСредство", Новый ОписаниеТипов("СправочникСсылка.ОбъектыЭксплуатации"));
	ТаблицаДополнительныхДанных.Колонки.Добавить("СтавкаДоходности", ОписаниеСтавкиДоходности);
	ТаблицаДополнительныхДанных.Колонки.Добавить("АвансовыеПлатежи", ОписаниеСумм);
	ТаблицаДополнительныхДанных.Колонки.Добавить("ПриведеннаяСтоимость", ОписаниеСумм);
	ТаблицаДополнительныхДанных.Колонки.Добавить("НГЛС", ОписаниеСумм);
	
	Для Каждого СтрокаОС Из ДанныеДляПечати Цикл
		СтрокаСтавокДоходности = ТаблицаДополнительныхДанных.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаСтавокДоходности, СтрокаОС);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаДополнительныхДанных", ТаблицаДополнительныхДанных);
	Запрос.УстановитьПараметр("Документ", Документ);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаДополнительныхДанных.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаДополнительныхДанных.СтавкаДоходности КАК СтавкаДоходности,
	|	ТаблицаДополнительныхДанных.АвансовыеПлатежи КАК АвансовыеПлатежи,
	|	ТаблицаДополнительныхДанных.ПриведеннаяСтоимость КАК ПриведеннаяСтоимость,
	|	ТаблицаДополнительныхДанных.НГЛС КАК НГЛС
	|ПОМЕСТИТЬ ВТ_ДополнительныеДанные
	|ИЗ
	|	&ТаблицаДополнительныхДанных КАК ТаблицаДополнительныхДанных
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПередачаОСВАренду2_4.Ссылка КАК Ссылка,
	|	ПередачаОСВАренду2_4.Организация КАК Организация,
	|	ПередачаОСВАренду2_4.Арендатор КАК Контрагент,
	|	ПередачаОСВАренду2_4.Договор КАК Договор,
	|	ПередачаОСВАренду2_4.Договор.СтавкаНДС КАК СтавкаНДС,
	|	ПередачаОСВАренду2_4.Договор.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ВТ_ДополнительныеДанные.ОсновноеСредство КАК ОсновноеСредство,
	|	ВТ_ДополнительныеДанные.СтавкаДоходности КАК СтавкаДоходности,
	|	ВТ_ДополнительныеДанные.АвансовыеПлатежи КАК АвансовыеПлатежи,
	|	ВТ_ДополнительныеДанные.ПриведеннаяСтоимость КАК ПриведеннаяСтоимость,
	|	ВТ_ДополнительныеДанные.НГЛС КАК НГЛС
	|ПОМЕСТИТЬ ВТ_ДанныеШапки
	|ИЗ
	|	Документ.ПередачаОСВАренду2_4 КАК ПередачаОСВАренду2_4,
	|	ВТ_ДополнительныеДанные КАК ВТ_ДополнительныеДанные
	|ГДЕ
	|	ПередачаОСВАренду2_4.Ссылка = &Документ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеГрафиков.ОсновноеСредство КАК ОсновноеСредство,
	|	ДанныеГрафиков.Дата КАК Дата,
	|	СУММА(ДанныеГрафиков.УслугаПоАренде) КАК УслугаПоАренде,
	|	СУММА(ДанныеГрафиков.УслугаПоАрендеНДС) КАК УслугаПоАрендеНДС,
	|	СУММА(ДанныеГрафиков.Проценты) КАК Проценты
	|ПОМЕСТИТЬ ВТ_ДанныеГрафиков
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПередачаОСВАренду2_4ГрафикОплатУслуг.ОсновноеСредство КАК ОсновноеСредство,
	|		ПередачаОСВАренду2_4ГрафикОплатУслуг.Дата КАК Дата,
	|		ПередачаОСВАренду2_4ГрафикОплатУслуг.УслугаПоАренде КАК УслугаПоАренде,
	|		ПередачаОСВАренду2_4ГрафикОплатУслуг.УслугаПоАрендеНДС КАК УслугаПоАрендеНДС,
	|		0 КАК Проценты
	|	ИЗ
	|		Документ.ПередачаОСВАренду2_4.ГрафикОплатУслуг КАК ПередачаОСВАренду2_4ГрафикОплатУслуг
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ДополнительныеДанные КАК ВТ_ДополнительныеДанные
	|			ПО ПередачаОСВАренду2_4ГрафикОплатУслуг.ОсновноеСредство = ВТ_ДополнительныеДанные.ОсновноеСредство
	|	ГДЕ
	|		ПередачаОСВАренду2_4ГрафикОплатУслуг.Ссылка = &Документ
	|		И ПередачаОСВАренду2_4ГрафикОплатУслуг.Дата >= НАЧАЛОПЕРИОДА(ПередачаОСВАренду2_4ГрафикОплатУслуг.Ссылка.Дата, ДЕНЬ)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ГрафикНачисленияПроцентов.ОсновноеСредство,
	|		ГрафикНачисленияПроцентов.Дата,
	|		0,
	|		0,
	|		ГрафикНачисленияПроцентов.Проценты - ГрафикНачисленияПроцентов.КорректировкаПроцентов
	|	ИЗ
	|		РегистрСведений.ГрафикНачисленияПроцентовПоДоходнойАренде КАК ГрафикНачисленияПроцентов
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ДополнительныеДанные КАК ВТ_ДополнительныеДанные
	|			ПО ГрафикНачисленияПроцентов.ОсновноеСредство = ВТ_ДополнительныеДанные.ОсновноеСредство
	|	ГДЕ
	|		ГрафикНачисленияПроцентов.Регистратор = &Документ
	|		И ГрафикНачисленияПроцентов.Активность
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ПередачаОСВАренду2_4ГрафикНачисленияПроцентов.ОсновноеСредство,
	|		ПередачаОСВАренду2_4ГрафикНачисленияПроцентов.Дата,
	|		0,
	|		0,
	|		ПередачаОСВАренду2_4ГрафикНачисленияПроцентов.Проценты
	|	ИЗ
	|		Документ.ПередачаОСВАренду2_4.ГрафикНачисленияПроцентов КАК ПередачаОСВАренду2_4ГрафикНачисленияПроцентов
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ДополнительныеДанные КАК ВТ_ДополнительныеДанные
	|			ПО ПередачаОСВАренду2_4ГрафикНачисленияПроцентов.ОсновноеСредство = ВТ_ДополнительныеДанные.ОсновноеСредство
	|	ГДЕ
	|		ПередачаОСВАренду2_4ГрафикНачисленияПроцентов.Ссылка = &Документ
	|		И ПередачаОСВАренду2_4ГрафикНачисленияПроцентов.Дата >= НАЧАЛОПЕРИОДА(ПередачаОСВАренду2_4ГрафикНачисленияПроцентов.Ссылка.Дата, ДЕНЬ)
	|		И НЕ ПередачаОСВАренду2_4ГрафикНачисленияПроцентов.Ссылка.Проведен
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ПередачаОСВАренду2_4ОС.ОсновноеСредство,
	|		ПередачаОСВАренду2_4ОС.ДатаОкончанияАренды,
	|		ВТ_ДополнительныеДанные.НГЛС,
	|		0,
	|		0
	|	ИЗ
	|		Документ.ПередачаОСВАренду2_4.ОС КАК ПередачаОСВАренду2_4ОС
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ДополнительныеДанные КАК ВТ_ДополнительныеДанные
	|			ПО ПередачаОСВАренду2_4ОС.ОсновноеСредство = ВТ_ДополнительныеДанные.ОсновноеСредство
	|	ГДЕ
	|		ПередачаОСВАренду2_4ОС.Ссылка = &Документ
	|		И ПередачаОСВАренду2_4ОС.ДатаОкончанияАренды <> ДАТАВРЕМЯ(1,1,1)
	|	) КАК ДанныеГрафиков
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеГрафиков.ОсновноеСредство,
	|	ДанныеГрафиков.Дата
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеГрафиков.ОсновноеСредство КАК ОсновноеСредство,
	|	СУММА(ВТ_ДанныеГрафиков.УслугаПоАренде) КАК СуммаДолга
	|ПОМЕСТИТЬ ВТ_СуммыДолга
	|ИЗ
	|	ВТ_ДанныеГрафиков КАК ВТ_ДанныеГрафиков
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ДанныеГрафиков.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕ(ВТ_ДанныеШапки.Ссылка) КАК Представление,
	|	ВТ_ДанныеШапки.Ссылка.Номер КАК Номер,
	|	ВТ_ДанныеШапки.Ссылка.Дата КАК Дата,
	|	ВТ_ДанныеШапки.Организация КАК Организация,
	|	ВТ_ДанныеШапки.Контрагент КАК Контрагент,
	|	ПРЕДСТАВЛЕНИЕ(ВТ_ДанныеШапки.Контрагент) КАК КонтрагентПредставление,
	|	ВТ_ДанныеШапки.Договор КАК Договор,
	|	ПРЕДСТАВЛЕНИЕ(ВТ_ДанныеШапки.Договор) КАК ДоговорПредставление,
	|	ПРЕДСТАВЛЕНИЕ(ВТ_ДанныеШапки.СтавкаНДС) КАК СтавкаНДС,
	|	ПРЕДСТАВЛЕНИЕ(ВТ_ДанныеШапки.ВалютаВзаиморасчетов) КАК ВалютаВзаиморасчетов,
	|	ВТ_ДанныеШапки.ОсновноеСредство КАК ОсновноеСредство,
	|	ПРЕДСТАВЛЕНИЕ(ВТ_ДанныеШапки.ОсновноеСредство) КАК ОсновноеСредствоПредставление,
	|	ВТ_ДанныеШапки.СтавкаДоходности КАК СтавкаДоходности,
	|	ВТ_ДанныеШапки.АвансовыеПлатежи КАК АвансовыеПлатежи,
	|	ВТ_ДанныеШапки.ПриведеннаяСтоимость - ВТ_ДанныеШапки.НГЛС КАК ПриведеннаяСтоимость,
	|	ВТ_ДанныеШапки.НГЛС КАК НГЛС,
	|	ЕСТЬNULL(ВТ_ДанныеГрафиков.Дата, ДАТАВРЕМЯ(1,1,1)) КАК ДатаГрафика,
	|	СУММА(ЕСТЬNULL(ВТ_ДанныеГрафиков.УслугаПоАренде, 0)) КАК СуммаПлатежаСНДС,
	|	СУММА(ЕСТЬNULL(ВТ_ДанныеГрафиков.УслугаПоАренде, 0))
	|		- СУММА(ЕСТЬNULL(ВТ_ДанныеГрафиков.УслугаПоАрендеНДС, 0)) КАК СуммаПлатежаБезНДС,
	|	СУММА(ЕСТЬNULL(ВТ_ДанныеГрафиков.УслугаПоАрендеНДС, 0)) КАК УслугаПоАрендеНДС,
	|	СУММА(ЕСТЬNULL(ВТ_ДанныеГрафиков.Проценты, 0)) КАК Проценты,
	|	ЕСТЬNULL(ВТ_СуммыДолга.СуммаДолга, 0) КАК СуммаДолга
	|ИЗ
	|	ВТ_ДанныеШапки КАК ВТ_ДанныеШапки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДанныеГрафиков КАК ВТ_ДанныеГрафиков
	|		ПО ВТ_ДанныеШапки.ОсновноеСредство = ВТ_ДанныеГрафиков.ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СуммыДолга КАК ВТ_СуммыДолга
	|		ПО ВТ_ДанныеШапки.ОсновноеСредство = ВТ_СуммыДолга.ОсновноеСредство
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ДанныеШапки.Ссылка,
	|	ВТ_ДанныеШапки.ОсновноеСредство,
	|	ВТ_ДанныеШапки.Организация,
	|	ВТ_ДанныеШапки.Контрагент,
	|	ВТ_ДанныеШапки.Договор,
	|	ВТ_ДанныеШапки.СтавкаДоходности,
	|	ВТ_ДанныеШапки.АвансовыеПлатежи,
	|	ВТ_ДанныеШапки.ПриведеннаяСтоимость,
	|	ВТ_ДанныеШапки.НГЛС,
	|	ЕСТЬNULL(ВТ_ДанныеГрафиков.Дата, ДАТАВРЕМЯ(1,1,1)),
	|	ЕСТЬNULL(ВТ_СуммыДолга.СуммаДолга, 0),
	|	ПРЕДСТАВЛЕНИЕ(ВТ_ДанныеШапки.СтавкаНДС),
	|	ПРЕДСТАВЛЕНИЕ(ВТ_ДанныеШапки.ВалютаВзаиморасчетов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОсновноеСредство,
	|	ДатаГрафика
	|ИТОГИ
	|	МАКСИМУМ(Контрагент),
	|	МАКСИМУМ(Договор),
	|	МАКСИМУМ(СтавкаНДС),
	|	МАКСИМУМ(ВалютаВзаиморасчетов),
	|	МАКСИМУМ(СтавкаДоходности),
	|	МАКСИМУМ(АвансовыеПлатежи),
	|	МАКСИМУМ(ПриведеннаяСтоимость),
	|	МАКСИМУМ(НГЛС),
	|	СУММА(СуммаПлатежаСНДС),
	|	СУММА(СуммаПлатежаБезНДС),
	|	СУММА(Проценты)
	|ПО
	|	Ссылка,
	|	ОсновноеСредство";
	
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.ИзменениеУсловийПередачиВАренду") Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПередачаОСВАренду2_4", "ИзменениеУсловийПередачиВАренду");
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	Результат = Запрос.Выполнить();
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
