
#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПереключательПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Организация) Тогда
		Если Переключатель = ТекущийФорматВыгрузки(Организация) Тогда
			Модифицированность = Ложь;
		Иначе
			Модифицированность = Истина;
		КонецЕсли;
	Иначе
		Модифицированность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиТекущегоПользователя()
	СтруктураФорматовВыгрузки = ХранилищеНастроекДанныхФорм.Загрузить(
		"Обработка.ОбщиеОбъектыРеглОтчетности.Форма.НастройкиФорматаВыгрузкиОтчетовСтатистики",
		"ФорматыВыгрузкиСтатистическойОтчетностиВЭлектронномВиде");
	
	Если ТипЗнч(СтруктураФорматовВыгрузки) <> Тип("Структура") Тогда
		СтруктураФорматовВыгрузки = Новый Структура;
		СтруктураФорматовВыгрузки.Вставить("Формат1", Новый Массив);
		СтруктураФорматовВыгрузки.Вставить("Формат2", Новый Массив);
	КонецЕсли;
	
	МассивОрганизацийСФорматомВыгрузки1 = СтруктураФорматовВыгрузки["Формат1"];
	МассивОрганизацийСФорматомВыгрузки2 = СтруктураФорматовВыгрузки["Формат2"];
	
	Если Переключатель = 1 Тогда
		Если МассивОрганизацийСФорматомВыгрузки1.Найти(Организация) = Неопределено Тогда
			МассивОрганизацийСФорматомВыгрузки1.Добавить(Организация);
		КонецЕсли;
		Если МассивОрганизацийСФорматомВыгрузки2.Найти(Организация) <> Неопределено Тогда
			МассивОрганизацийСФорматомВыгрузки2.Удалить(МассивОрганизацийСФорматомВыгрузки2.Найти(Организация));
		КонецЕсли;
	ИначеЕсли Переключатель = 2 Тогда
		Если МассивОрганизацийСФорматомВыгрузки2.Найти(Организация) = Неопределено Тогда
			МассивОрганизацийСФорматомВыгрузки2.Добавить(Организация);
		КонецЕсли;
		Если МассивОрганизацийСФорматомВыгрузки1.Найти(Организация) <> Неопределено Тогда
			МассивОрганизацийСФорматомВыгрузки1.Удалить(МассивОрганизацийСФорматомВыгрузки1.Найти(Организация));
		КонецЕсли;
	КонецЕсли;
	
	РегламентированнаяОтчетность.УстановитьФорматВыгрузкиСтатистическойОтчетностиВЭлектронномВиде(Организация, Переключатель);
	ХранилищеНастроекДанныхФорм.Сохранить(
		"Обработка.ОбщиеОбъектыРеглОтчетности.Форма.НастройкиФорматаВыгрузкиОтчетовСтатистики",
		"ФорматыВыгрузкиСтатистическойОтчетностиВЭлектронномВиде",
		СтруктураФорматовВыгрузки);
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиВсемПользователям()
	Для Каждого ПользовательИБ Из ПользователиИнформационнойБазы.ПолучитьПользователей() Цикл
		Если ПользователиИнформационнойБазы.ТекущийПользователь().УникальныйИдентификатор = ПользовательИБ.УникальныйИдентификатор Тогда 
			Продолжить;
		КонецЕсли;
		
		СтруктураФорматовВыгрузки = ХранилищеНастроекДанныхФорм.Загрузить(
			"Обработка.ОбщиеОбъектыРеглОтчетности.Форма.НастройкиФорматаВыгрузкиОтчетовСтатистики",
			"ФорматыВыгрузкиСтатистическойОтчетностиВЭлектронномВиде", , ПользовательИБ.Имя);
		
		Если ТипЗнч(СтруктураФорматовВыгрузки) <> Тип("Структура") Тогда
			СтруктураФорматовВыгрузки = Новый Структура;
			СтруктураФорматовВыгрузки.Вставить("Формат1", Новый Массив);
			СтруктураФорматовВыгрузки.Вставить("Формат2", Новый Массив);
		КонецЕсли;
		
		МассивОрганизацийСФорматомВыгрузки1 = СтруктураФорматовВыгрузки["Формат1"];
		МассивОрганизацийСФорматомВыгрузки2 = СтруктураФорматовВыгрузки["Формат2"];
		
		Если Переключатель = 1 Тогда
			Если МассивОрганизацийСФорматомВыгрузки1.Найти(Организация) = Неопределено Тогда
				МассивОрганизацийСФорматомВыгрузки1.Добавить(Организация);
			КонецЕсли;
			Если МассивОрганизацийСФорматомВыгрузки2.Найти(Организация) <> Неопределено Тогда
				МассивОрганизацийСФорматомВыгрузки2.Удалить(МассивОрганизацийСФорматомВыгрузки2.Найти(Организация));
			КонецЕсли;
		ИначеЕсли Переключатель = 2 Тогда
			Если МассивОрганизацийСФорматомВыгрузки2.Найти(Организация) = Неопределено Тогда
				МассивОрганизацийСФорматомВыгрузки2.Добавить(Организация);
			КонецЕсли;
			Если МассивОрганизацийСФорматомВыгрузки1.Найти(Организация) <> Неопределено Тогда
				МассивОрганизацийСФорматомВыгрузки1.Удалить(МассивОрганизацийСФорматомВыгрузки1.Найти(Организация));
			КонецЕсли;
		КонецЕсли;
		
		ХранилищеНастроекДанныхФорм.Сохранить(
			"Обработка.ОбщиеОбъектыРеглОтчетности.Форма.НастройкиФорматаВыгрузкиОтчетовСтатистики",
			"ФорматыВыгрузкиСтатистическойОтчетностиВЭлектронномВиде",
			СтруктураФорматовВыгрузки, , ПользовательИБ.Имя);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройки()
	Если УстановитьДляВсехПользователей Тогда 
		Попытка
			СохранитьНастройкиТекущегоПользователя();
			СохранитьНастройкиВсемПользователям();
		Исключение
			ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не удалось сохранить настройки для всех пользователей, сохранено только для текущего';
														|en = 'Cannot save settings for all users, settings saved for the current user only'"));
			СохранитьНастройкиТекущегоПользователя();
		КонецПопытки;
	Иначе
		СохранитьНастройкиТекущегоПользователя();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Организация = Параметры.Организация;
	ПредупредитьОПовторномВыполнении = Параметры.ПредупредитьОПовторномВыполнении;
	Элементы.Организация.Доступность = Параметры.ОрганизацияДоступна;
	
	Если ЗначениеЗаполнено(Организация) Тогда
		
		ТекущийФорматВыгрузки = ТекущийФорматВыгрузки(Организация);
		
		Если ТекущийФорматВыгрузки = Неопределено Тогда
			Переключатель = 2;
			Модифицированность = Истина;
		Иначе
			Переключатель = ТекущийФорматВыгрузки;
		КонецЕсли;
		
	КонецЕсли;
	
	Попытка
		Элементы.УстановитьДляВсехПользователей.Видимость = РольДоступна("АдминистраторСистемы");
		УстановитьДляВсехПользователей = РольДоступна("АдминистраторСистемы");
	Исключение
	КонецПопытки;
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ТекущийФорматВыгрузки = ТекущийФорматВыгрузки(Организация);
		Если ТекущийФорматВыгрузки = 2 Тогда
			Переключатель = 2;
			Модифицированность = Ложь;
		ИначеЕсли ТекущийФорматВыгрузки = 1 Тогда
			Переключатель = 1;
			Модифицированность = Ложь;
		Иначе
			Переключатель = 2;
			Модифицированность = Истина;
		КонецЕсли;
	Иначе
		Переключатель = 2;
		Модифицированность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОчистка(Элемент, СтандартнаяОбработка)
	
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаСервере
Функция ТекущийФорматВыгрузки(Организация)
	
	Возврат РегламентированнаяОтчетность.ФорматВыгрузкиСтатистическойОтчетностиВЭлектронномВиде(Организация);
	
КонецФункции

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Перем ТекущийФорматВыгрузки;
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ТекущийФорматВыгрузки = ТекущийФорматВыгрузки(Организация);
		Если ТекущийФорматВыгрузки = Неопределено Тогда
			Модифицированность = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если Модифицированность Тогда
		
		Если Не ЗначениеЗаполнено(Организация) Тогда
			ПоказатьПредупреждение(,НСтр("ru = 'Не указана организация/подразделение, настройки формата выгрузки статистических отчетов не сохранены.';
										|en = 'Company/business unit is not specified; settings of statistical report export format are not saved.'"));
			Возврат;
		КонецЕсли;
		
		Отказ = Истина;
		
		ДополнительныеПараметры = Новый Структура("ТекущийФорматВыгрузки", ТекущийФорматВыгрузки);
		ОписаниеОповещения = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(ОписаниеОповещения, НСтр("ru = 'Настройки были изменены. Сохранить изменения?';
												|en = 'Settings were changed. Save changes?'"), РежимДиалогаВопрос.ДаНетОтмена);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ПередЗакрытиемЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	ТекущийФорматВыгрузки = ДополнительныеПараметры.ТекущийФорматВыгрузки;
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Если Переключатель = 2 Тогда
			ТекстПредупреждения = НСтр("ru='Для организации """ + СокрЛП(Организация)
				+ """ сохранены настройки выгрузки стат. отчетности в формате ПО ""Заполнение форм статистической отчетности"", версии 2.4.4.'");
		ИначеЕсли Переключатель = 1 Тогда
			ТекстПредупреждения = НСтр("ru='Для организации """ + СокрЛП(Организация)
				+ """ сохранены настройки выгрузки стат. отчетности в формате ПО ""Формы статотчетности (предприятие)"", версии 1.21.'");
		КонецЕсли;
		
		Если ПредупредитьОПовторномВыполнении Тогда
			ТекстПредупреждения = ТекстПредупреждения + Символы.ПС + Символы.ПС +"Повторно выполните операцию выгрузки.";
		КонецЕсли;
		
		Ждать ПредупреждениеАсинх(ТекстПредупреждения);
		СохранитьНастройки();
		Модифицированность = Ложь;
		Закрыть();
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
		Если ТекущийФорматВыгрузки = Неопределено Тогда
			Переключатель = 1;
			ОписаниеОповещения = Новый ОписаниеОповещения("СохранитьИЗакрытьЗавершение", ЭтотОбъект);
			
			ТекстПредупреждения = НСтр("ru='Для организации """ + СокрЛП(Организация)
				+ """ сохранены настройки выгрузки стат. отчетности в формате ПО ""Формы статотчетности (предприятие)"", версии 1.21.
				|Для изменения формата выгрузки перейдите в форму настроек журнала ""Регламентированная и финансовая отчетность"".'");
			
			Если ПредупредитьОПовторномВыполнении Тогда
				ТекстПредупреждения = ТекстПредупреждения + Символы.ПС + Символы.ПС +"Повторно выполните операцию выгрузки.";
			КонецЕсли;
			
			Ждать ПредупреждениеАсинх(ТекстПредупреждения);
			СохранитьНастройки();
			Модифицированность = Ложь;
			Закрыть();
		Иначе
			Модифицированность = Ложь;
			Закрыть();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти