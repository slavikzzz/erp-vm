//++ Устарело_Производство21
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	МассивНепроверяемыхРеквизитов.Добавить("ВыходныеИзделия.Получатель");
	МассивНепроверяемыхРеквизитов.Добавить("ВыходныеИзделия.ДоляСтоимости");
	МассивНепроверяемыхРеквизитов.Добавить("ВыходныеИзделия.КоличествоУпаковок");
	МассивНепроверяемыхРеквизитов.Добавить("ВыходныеИзделия.Характеристика");
	
	МассивНепроверяемыхРеквизитов.Добавить("Трудозатраты.Количество");
	
	МассивНепроверяемыхРеквизитов.Добавить("МатериалыИУслуги.КоличествоУпаковок");
	МассивНепроверяемыхРеквизитов.Добавить("МатериалыИУслуги.Характеристика");
	МассивНепроверяемыхРеквизитов.Добавить("МатериалыИУслуги.ДатаРасхода");
	
	ПроверитьЗаполнениеКоличества(ПроверяемыеРеквизиты, Отказ);
	
	ПроверитьЗаполнениеДокументаВЗависимостиОтСтатуса(МассивНепроверяемыхРеквизитов, Отказ);
	
	ПроверитьЗаполнениеОтклонений("ВыходныеИзделия",  Отказ);
	ПроверитьЗаполнениеОтклонений("ВозвратныеОтходы", Отказ);
	ПроверитьЗаполнениеОтклонений("МатериалыИУслуги", Отказ);
	ПроверитьЗаполнениеОтклонений("Трудозатраты",     Отказ);
	
	ПроверитьНормативыТрудозатрат(Отказ);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры
 
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьЗаполнениеСклада(Отказ)

	ШаблонСообщения = НСтр("ru = 'Не определено направление выпуска в строке %1 списка ""Продукция"".';
							|en = 'Release direction in line %1 of the ""Products"" list is not determined.'");
	
	Для каждого СтрокаТаблицы Из ВыходныеИзделия Цикл
		
		Если ЗначениеЗаполнено(СтрокаТаблицы.Получатель) Тогда
			Продолжить;
		КонецЕсли;
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, Формат(СтрокаТаблицы.НомерСтроки, "ЧГ="));
		Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ВыходныеИзделия", СтрокаТаблицы.НомерСтроки, "Склад");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, Поле, "Объект", Отказ);
		
	КонецЦикла;
	
	ШаблонСообщения = НСтр("ru = 'Не определено направление выпуска в строке %1 списка ""Возвратные отходы"".';
							|en = 'Release direction in line %1 of the ""Recyclable waste"" list is not determined.'");
	
	Для каждого СтрокаТаблицы Из ВозвратныеОтходы Цикл
		
		Если ЗначениеЗаполнено(СтрокаТаблицы.Получатель) Тогда
			Продолжить;
		КонецЕсли;
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, Формат(СтрокаТаблицы.НомерСтроки, "ЧГ="));
		Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ВыходныеИзделия", СтрокаТаблицы.НомерСтроки, "Склад");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, Поле, "Объект", Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеДолиСтоимости(Отказ)
	
	Если ВыходныеИзделия.Количество() < 2 Тогда
		Возврат;
	КонецЕсли;
	
	ШаблонСообщения = НСтр("ru = 'Не заполнена колонка ""Доля стоимости"" в строке %1 списка ""Продукция""';
							|en = 'The ""Cost share"" field is not populated in line %1 of the ""Products"" list'");
	Для каждого СтрокаТаблицы Из ВыходныеИзделия Цикл
		Если СтрокаТаблицы.ДоляСтоимости = 0 Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, 
																						Формат(СтрокаТаблицы.НомерСтроки, "ЧГ="));
																						
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ВыходныеИзделия", СтрокаТаблицы.НомерСтроки, "ДоляСтоимости");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле,, Отказ);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеОтклонений(ИмяТаблицы, Отказ)
	
	Если НРег(ИмяТаблицы) = НРег("ВыходныеИзделия") Тогда
		ИмяРеквизитаКоличество = "КоличествоУпаковокФакт";
		ИмяРеквизита           = "КоличествоУпаковокОтклонение";
		
		ШаблонСообщения = НСтр("ru = 'Отклонение от норматива не должно приводить к отрицательному выпуску.';
								|en = 'Variance from standard should not result in a negative release.'");
		ШаблонСообщенияКоличество = НСтр("ru = 'Необходимо заполнить колонку ""Факт"" или ""Отклонение"" в строке %1 списка ""Продукция"".';
										|en = 'Fill in the ""Fact"" or ""Variance"" column in line %1 of the ""Products"" list.'");
		
	ИначеЕсли НРег(ИмяТаблицы) = НРег("ВозвратныеОтходы") Тогда
		ИмяРеквизитаКоличество = "КоличествоУпаковокФакт";
		ИмяРеквизита           = "КоличествоУпаковокОтклонение";
		
		ШаблонСообщения = НСтр("ru = 'Отклонение от норматива не должно приводить к отрицательному выпуску.';
								|en = 'Variance from standard should not result in a negative release.'");
		ШаблонСообщенияКоличество = НСтр("ru = 'Необходимо заполнить колонку ""Факт"" или ""Отклонение"" в строке %1 списка ""Возвратные отходы"".';
										|en = 'Fill in the ""Fact"" or ""Variance"" column in line %1 of the ""Recyclable waste"" list.'");
		
	ИначеЕсли НРег(ИмяТаблицы) = НРег("МатериалыИУслуги") Тогда
		ИмяРеквизитаКоличество = "КоличествоУпаковокФакт";
		ИмяРеквизита           = "КоличествоУпаковокОтклонение";
		
		ШаблонСообщения = НСтр("ru = 'Отклонение от норматива не должно приводить к отрицательному потреблению.';
								|en = 'Variance from standard should not result in a negative consumption.'");
		ШаблонСообщенияКоличество = НСтр("ru = 'Необходимо заполнить колонку ""Факт"" или ""Отклонение"" в строке %1 списка ""Материалы и работы"".';
										|en = 'Fill in the ""Fact"" or ""Variance"" column in line %1 of the ""Materials and works"" list.'");
		
	ИначеЕсли НРег(ИмяТаблицы) = НРег("Трудозатраты") Тогда
		ИмяРеквизитаКоличество = "КоличествоФакт";
		ИмяРеквизита           = "КоличествоОтклонение";
		
		ШаблонСообщения = НСтр("ru = 'Отклонение от норматива не должно приводить к отрицательным трудозатратам.';
								|en = 'Variance from standard should not result in negative labor costs.'");
		ШаблонСообщенияКоличество = НСтр("ru = 'Необходимо заполнить колонку ""Факт"" или ""Отклонение"" в строке %1 списка ""Трудозатраты"".';
										|en = 'Fill in the ""Fact"" or ""Variance"" column in line %1 of the ""Labor costs"" list.'");
		
	КонецЕсли; 
	
	Для каждого ДанныеСтроки Из ЭтотОбъект[ИмяТаблицы] Цикл
		
		Если ДанныеСтроки.Количество + ДанныеСтроки.КоличествоОтклонение < 0 Тогда
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, ДанныеСтроки.НомерСтроки);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТаблицы, ДанныеСтроки.НомерСтроки, ИмяРеквизита);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, Поле, "Объект", Отказ);
			
		ИначеЕсли ДанныеСтроки.КоличествоФакт = 0 И ДанныеСтроки.КоличествоОтклонение = 0 Тогда
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщенияКоличество, ДанныеСтроки.НомерСтроки);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТаблицы, ДанныеСтроки.НомерСтроки, ИмяРеквизитаКоличество);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, Поле, "Объект", Отказ);
			
		КонецЕсли;
		
	КонецЦикла; 
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеДокументаВЗависимостиОтСтатуса(МассивНепроверяемыхРеквизитов, Отказ)
	
	ПроверитьЗаполнениеСклада(Отказ);
	ПроверитьЗаполнениеДолиСтоимости(Отказ);
	
	ПараметрыПроверки = НоменклатураСервер.ПараметрыПроверкиЗаполненияХарактеристик();
	ПараметрыПроверки.ИмяТЧ = "ВыходныеИзделия";
	НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект, МассивНепроверяемыхРеквизитов, Отказ, ПараметрыПроверки);

	ПараметрыПроверки.ИмяТЧ = "ВозвратныеОтходы";
	НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект, МассивНепроверяемыхРеквизитов, Отказ, ПараметрыПроверки);

	ПараметрыПроверки.ИмяТЧ = "МатериалыИУслуги";
	НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект, МассивНепроверяемыхРеквизитов, Отказ, ПараметрыПроверки);
	
	ПроверитьДатуРасходаМатериалов(Отказ);
	
	// Проверка заполнения серий
	ПараметрыУказанияСерий = Документы.МаршрутныйЛистПроизводства.ПараметрыУказанияСерий(ЭтотОбъект);
	НоменклатураСервер.ПроверитьЗаполнениеСерий(ЭтотОбъект, ПараметрыУказанияСерий.МатериалыИУслуги, Отказ, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПроверитьНормативыТрудозатрат(Отказ)

	ШаблонСообщения = НСтр("ru = 'Норматив по виду работ ""%1"" в сумме по всем бигадам должен быть равен %2 (сейчас сумма %3).';
							|en = 'The total standard of the ""%1"" activity kind by all teams should be equal to %2 (the current amount is %3).'");
	
	Для каждого СтрокаТрудозатратыИсходные Из ТрудозатратыИсходные Цикл
		СтруктураПоиска = Новый Структура("ВидРабот", СтрокаТрудозатратыИсходные.ВидРабот);
		СписокСтрок = Трудозатраты.НайтиСтроки(СтруктураПоиска);
		Норматив = 0;
		Для каждого СтрокаТрудозатраты Из СписокСтрок Цикл
			Норматив = Норматив + СтрокаТрудозатраты.Количество;
		КонецЦикла;
		Если Норматив <> СтрокаТрудозатратыИсходные.Количество Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						ШаблонСообщения, 
						СтрокаТрудозатратыИсходные.ВидРабот,
						Формат(СтрокаТрудозатратыИсходные.Количество, "ЧН=0; ЧГ="),
						Формат(Норматив, "ЧН=0; ЧГ="));
						
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,, "Объект", Отказ);
		КонецЕсли; 
	КонецЦикла; 
	
КонецПроцедуры

Процедура ПроверитьДатуРасходаМатериалов(Отказ)

	Если СтатусВыполненияОперации <> Перечисления.СтатусыВыполненияОпераций.Завершено Тогда
		Возврат;
	КонецЕсли;
	
	ШаблонСообщения = НСтр("ru = 'Материал (работа) фактически израсходован, но не указана дата расхода в строке %1 списка ""Материалы и работы""';
							|en = 'Material (work) is actually consumed but the consumption date is not specified in line %1 of the ""Materials and works"" list'");
	
	Для каждого СтрокаТаблицы Из МатериалыИУслуги Цикл
		Если СтрокаТаблицы.ДатаРасхода = '000101010000'
			И СтрокаТаблицы.КоличествоФакт <> 0 Тогда
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, 
																						Формат(СтрокаТаблицы.НомерСтроки, "ЧГ="));
																						
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("МатериалыИУслуги", СтрокаТаблицы.НомерСтроки, "ДатаРасхода");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, Поле,"Объект", Отказ);
		КонецЕсли; 	
	КонецЦикла; 
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеКоличества(ПроверяемыеРеквизиты, Отказ)
	
	// Нужно удалить проверку количества, т.к. в документе особая логика проверки
	ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("ВыходныеИзделия.КоличествоУпаковокФакт"));
	ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("ВозвратныеОтходы.КоличествоУпаковокФакт"));
	ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("МатериалыИУслуги.КоличествоУпаковокФакт"));
	ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("ВыходныеИзделия.КоличествоУпаковок"));
	ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("ВозвратныеОтходы.КоличествоУпаковок"));
	ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("МатериалыИУслуги.КоличествоУпаковок"));
	
	ПараметрыПроверки = НоменклатураСервер.ПараметрыПроверкиЗаполненияКоличества();
	ПараметрыПроверки.ИмяТЧ = "ВыходныеИзделия";
	ПараметрыПроверки.ИменаПолейССуффиксом.Вставить("Количество", "КоличествоФакт");
	ПараметрыПроверки.ИменаПолейССуффиксом.Вставить("КоличествоУпаковок", "КоличествоУпаковокФакт");
	ПараметрыПроверки.УсловиеОтбораСтрокДляОкругления = "ВыходныеИзделия.Получатель ССЫЛКА Справочник.Склады";
	НоменклатураСервер.ПроверитьЗаполнениеКоличества(ЭтотОбъект, ПроверяемыеРеквизиты, Отказ, ПараметрыПроверки);
	
	ПараметрыПроверки = НоменклатураСервер.ПараметрыПроверкиЗаполненияКоличества();
	ПараметрыПроверки.ИмяТЧ = "ВозвратныеОтходы";
	ПараметрыПроверки.ИменаПолейССуффиксом.Вставить("Количество", "КоличествоФакт");
	ПараметрыПроверки.ИменаПолейССуффиксом.Вставить("КоличествоУпаковок", "КоличествоУпаковокФакт");
	ПараметрыПроверки.УсловиеОтбораСтрокДляОкругления = "ВозвратныеОтходы.Получатель ССЫЛКА Справочник.Склады";
	НоменклатураСервер.ПроверитьЗаполнениеКоличества(ЭтотОбъект, ПроверяемыеРеквизиты, Отказ, ПараметрыПроверки);
	
	ПараметрыПроверки = НоменклатураСервер.ПараметрыПроверкиЗаполненияКоличества();
	ПараметрыПроверки.ИмяТЧ = "МатериалыИУслуги";
	ПараметрыПроверки.ПроверитьВозможностьОкругления = Ложь;
	НоменклатураСервер.ПроверитьЗаполнениеКоличества(ЭтотОбъект, ПроверяемыеРеквизиты, Отказ, ПараметрыПроверки);

КонецПроцедуры

#КонецОбласти

#КонецЕсли
//-- Устарело_Производство21