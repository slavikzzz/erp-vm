
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры.ДанныеОперации);
	
	ТаблицаОпераций = ПолучитьИзВременногоХранилища(Параметры.АдресВХранилище);
	Для Каждого СтрокаТаблицы Из ТаблицаОпераций Цикл
		Если СтрокаТаблицы.Организация = Организация
			И ((Расширенная И СтрокаТаблицы.ВидЗапасов.ТипЗапасов = ТипЗапасов)
				ИЛИ (НЕ Расширенная И СтрокаТаблицы.ТипЗапасов = ТипЗапасов)) Тогда
			ЗаполнитьЗначенияСвойств(ТаблицаРедактирования.Добавить(), СтрокаТаблицы);
		КонецЕсли;
	КонецЦикла;
	
	Элементы.ПростаяОперация.Видимость  = Не Расширенная;
	Элементы.РасширеннаяОперация.Видимость = Расширенная;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)
	
	Если Модифицированность Тогда
		Результат = Новый Соответствие;
		Для Каждого СтрокаТаблицы Из ТаблицаРедактирования Цикл
			ЗначенияРеквизитов = Новый Структура("Контрагент, КодОперации, ОтражениеВОтчетности, ТипДокументаВПрослеживаемости, СтоимостьПоРНПТ, КоличествоПоРНПТ");
			ЗаполнитьЗначенияСвойств(ЗначенияРеквизитов, СтрокаТаблицы);
			ЗначенияРеквизитов.Вставить("РучноеРедактирование", Истина);
			Результат.Вставить(СтрокаТаблицы.ИдентификаторЗаписи, ЗначенияРеквизитов);
		КонецЦикла;
		Закрыть(Результат);
	Иначе
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
