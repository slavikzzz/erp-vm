
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	Объект.КоличествоЭкземпляров = 1;
	
	ШаблонПоУмолчанию = Справочники.ШаблоныЭтикетокИЦенников.ШаблонПоУмолчанию(
		Перечисления.НазначенияШаблоновЭтикетокИЦенников.ЭтикеткаОбъектаЭксплуатацииЛента);
		
	Если НЕ ЗначениеЗаполнено(ШаблонПоУмолчанию) Тогда
		ШаблонПоУмолчанию = Справочники.ШаблоныЭтикетокИЦенников.ШаблонПоУмолчанию(
			Перечисления.НазначенияШаблоновЭтикетокИЦенников.ЭтикеткаОбъектаЭксплуатацииБумага);
	КонецЕсли;
	
	Объект.ШаблонЭтикетки = ШаблонПоУмолчанию;
		
	Если Параметры.Свойство("ОбъектыЭксплуатации") Тогда
		ДобавитьОбъектыЭксплуатации(Параметры.ОбъектыЭксплуатации);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыОбъектыЭксплуатации

&НаКлиенте
Процедура ОбъектыЭксплуатацииОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ВнеоборотныеАктивыКлиентСервер.ОбработкаВыбораЭлемента(Объект.ОбъектыЭксплуатации, "ОбъектЭксплуатации", ВыбранноеЗначение);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подобрать(Команда)
	
	ПараметрыПодбора = ОбщегоНазначенияУТКлиентСервер.ПараметрыПодбора(Элементы.ОбъектыЭксплуатацииОбъектЭксплуатации, ЭтаФорма);
	
	ОткрытьФорму("Справочник.ОбъектыЭксплуатации.ФормаВыбора", 
					ПараметрыПодбора, Элементы.ОбъектыЭксплуатации,,,,, 
					РежимОткрытияОкнаФормы.БлокироватьОкноВладельца)
	
КонецПроцедуры

&НаКлиенте
Процедура Печать(Команда)
	
	ОчиститьСообщения();
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрКоманды = Новый Массив;
	ПараметрКоманды.Добавить(ПредопределенноеЗначение("Справочник.ОбъектыЭксплуатации.ПустаяСсылка"));
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
		"Обработка.ПечатьЭтикетокИЦенников",
		"ЭтикеткаОбъектовЭксплуатации",
		ПараметрКоманды,
		ЭтаФорма,
		ПолучитьПараметрыПечати());

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедуры

&НаСервере
Функция ПолучитьПараметрыПечати()
	
	ДанныеПечати = Объект.ОбъектыЭксплуатации.Выгрузить();
	ИнвентарныеНомераОбъектов = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(ДанныеПечати.ВыгрузитьКолонку("ОбъектЭксплуатации"), "ИнвентарныйНомер");
	Для каждого ЭлементКоллекции Из ДанныеПечати Цикл
		ЭлементКоллекции.Штрихкод = ИнвентарныеНомераОбъектов.Получить(ЭлементКоллекции.ОбъектЭксплуатации);
	КонецЦикла;
	
	ПараметрыПечати = Новый Структура;
	ПараметрыПечати.Вставить("АдресВХранилище", ПоместитьВоВременноеХранилище(ДанныеПечати, УникальныйИдентификатор));
	ПараметрыПечати.Вставить("ШаблонЭтикетки", Объект.ШаблонЭтикетки);
	ПараметрыПечати.Вставить("КоличествоЭкземпляров", Объект.КоличествоЭкземпляров);
	
	Возврат ПараметрыПечати;
	
КонецФункции

&НаСервере
Процедура ДобавитьОбъектыЭксплуатации(ОбъектыЭксплуатации)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ОбъектыЭксплуатации.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ОбъектыЭксплуатации КАК ОбъектыЭксплуатации
	|ГДЕ
	|	ОбъектыЭксплуатации.Ссылка В ИЕРАРХИИ(&ОбъектыЭксплуатации)
	|	И НЕ ОбъектыЭксплуатации.ЭтоГруппа
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОбъектыЭксплуатации.Наименование,
	|	ОбъектыЭксплуатации.ИнвентарныйНомер";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ОбъектыЭксплуатации", ОбъектыЭксплуатации);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СтрТабл = Объект.ОбъектыЭксплуатации.Добавить();
		СтрТабл.ОбъектЭксплуатации = Выборка.Ссылка; 
	КонецЦикла;
	

КонецПроцедуры
 
#КонецОбласти
