#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТолькоОбеспеченные = ?(Параметры.ОтборОбеспечение = "МатериалыОбеспечены", Истина, Ложь);
	Подразделение      = Параметры.Подразделение;
	Заказ              = Параметры.Заказ;
	Изделие            = Параметры.Изделие;
	Партия             = Параметры.Партия;
	Этап               = Параметры.Этап;
	
	Состояние = Параметры.ОтборСостояние;
	
	Если Состояние.Количество() > 0 Тогда
		
		Если Состояние.Количество() = 1 
			И Состояние[0].Значение = ПредопределенноеЗначение("Перечисление.СостоянияЭтаповНаВнутрицеховомУровне.Начат") Тогда
			
			МожноВыполнять = Ложь;
			Начат = Истина;
			
		Иначе
			МожноВыполнять = Истина;
			Начат = Ложь;
		КонецЕсли;
		
	Иначе
		МожноВыполнять = Ложь;
		Начат = Ложь;
	КонецЕсли;
	
	ИспользуетсяГрафик = УправлениеПроизводствомПовтИсп.ИспользуетсяГрафикПроизводства();
	
	Если ИспользуетсяГрафик Тогда
		Период = Параметры.Период;
	КонецЕсли;
	
	Элементы.Период.Видимость = ИспользуетсяГрафик;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура МожноВыполнятьПриИзменении(Элемент)
	
	Если МожноВыполнять Тогда
		Начат = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатПриИзменении(Элемент)
	
	Если Начат Тогда
		МожноВыполнять = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьОтборы(Команда)
	
	ПараметрыОтбора = Новый Структура();
	
	ПараметрыОтбора.Вставить("МожноВыполнять", МожноВыполнять);
	ПараметрыОтбора.Вставить("Начат", Начат);
	ПараметрыОтбора.Вставить("ТолькоОбеспеченные", ТолькоОбеспеченные);
	ПараметрыОтбора.Вставить("ОтборПодразделение", Подразделение);
	ПараметрыОтбора.Вставить("ОтборЗаказ", Заказ);
	ПараметрыОтбора.Вставить("ОтборИзделие", Изделие);
	ПараметрыОтбора.Вставить("ОтборПартия", Партия);
	ПараметрыОтбора.Вставить("ОтборЭтап", Этап);
	ПараметрыОтбора.Вставить("ОтборПериод", Период);
	
	Закрыть(ПараметрыОтбора);
	
КонецПроцедуры

&НаКлиенте
Процедура СбросОтборов(Команда)
	
	Заказ         = Неопределено;
	Изделие       = Неопределено;
	Партия        = Неопределено;
	Подразделение = Неопределено;
	Этап          = Неопределено;
	
	Период = Новый СтандартныйПериод();
	
	ТолькоОбеспеченные = Ложь;
	МожноВыполнять     = Ложь;
	Начат              = Ложь;
	
КонецПроцедуры

#КонецОбласти
