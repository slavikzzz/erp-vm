//++ Устарело_Производство21
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область Печать

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СменноСуточноеЗадание") Тогда
		
		ТабличныйДокумент = СформироватьСменноСуточноеЗадание(ПараметрыПечати, ОбъектыПечати);
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
									КоллекцияПечатныхФорм, 
									"СменноСуточноеЗадание", 
									НСтр("ru = 'Сменно-суточное задание';
										|en = 'Shift-day job'"), 
									ТабличныйДокумент);
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьСменноСуточноеЗадание(ПараметрыПечати, ОбъектыПечати)
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Обработка.ВыполнениеМаршрутныхЛистов.ПФ_MXL_СменноСуточноеЗадание");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДатыЗаданий.ДатаЗадания КАК НачалоЗадания,
	|	КОНЕЦПЕРИОДА(ДатыЗаданий.ДатаЗадания, ДЕНЬ) КАК ОкончаниеЗадания
	|ПОМЕСТИТЬ ДатыЗаданий
	|ИЗ
	|	&ДатыЗаданий КАК ДатыЗаданий
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	МаршрутныйЛистПроизводства.Ссылка КАК Ссылка,
	|	МаршрутныйЛистПроизводстваОперации.КлючСвязи КАК КлючСвязиОперации,
	|	МаршрутныйЛистПроизводстваТрудозатраты.Бригада КАК Бригада,
	|	ДатыЗаданий.НачалоЗадания КАК ДатаЗадания
	|ПОМЕСТИТЬ ВыборкаМаршрутныхЛистов
	|ИЗ
	|	Документ.МаршрутныйЛистПроизводства КАК МаршрутныйЛистПроизводства
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛистПроизводства.Операции КАК МаршрутныйЛистПроизводстваОперации
	|		ПО (МаршрутныйЛистПроизводстваОперации.Ссылка = МаршрутныйЛистПроизводства.Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДатыЗаданий КАК ДатыЗаданий
	|		ПО (ДатыЗаданий.ОкончаниеЗадания >= МаршрутныйЛистПроизводстваОперации.Начало)
	|			И (ДатыЗаданий.НачалоЗадания <= МаршрутныйЛистПроизводстваОперации.Окончание)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛистПроизводства.Трудозатраты КАК МаршрутныйЛистПроизводстваТрудозатраты
	|		ПО (МаршрутныйЛистПроизводстваТрудозатраты.Ссылка = МаршрутныйЛистПроизводстваОперации.Ссылка)
	|			И (МаршрутныйЛистПроизводстваТрудозатраты.КлючСвязиОперации = МаршрутныйЛистПроизводстваОперации.КлючСвязи)
	|			И (МаршрутныйЛистПроизводстваТрудозатраты.Бригада В (&Бригады))
	|ГДЕ
	|	МаршрутныйЛистПроизводства.Проведен
	|	И МаршрутныйЛистПроизводства.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыМаршрутныхЛистовПроизводства.КВыполнению),
	|											ЗНАЧЕНИЕ(Перечисление.СтатусыМаршрутныхЛистовПроизводства.Выполняется),
	|											ЗНАЧЕНИЕ(Перечисление.СтатусыМаршрутныхЛистовПроизводства.Выполнен))
	|	И МаршрутныйЛистПроизводства.СостояниеРасписания = ЗНАЧЕНИЕ(Перечисление.СостоянияРасписанияРабочихЦентров.Сформировано)
	|	И МаршрутныйЛистПроизводства.Подразделение = &Подразделение
	|	И (&РабочийЦентр = ЗНАЧЕНИЕ(Справочник.РабочиеЦентры.ПустаяСсылка)
	|			ИЛИ МаршрутныйЛистПроизводстваОперации.РабочийЦентрПоРасписанию В ИЕРАРХИИ (&РабочийЦентр))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВыборкаМаршрутныхЛистов.ДатаЗадания КАК ДатаЗадания,
	|	ВыборкаМаршрутныхЛистов.Бригада КАК Бригада,
	|	МаршрутныйЛистПроизводства.Ссылка КАК МаршрутныйЛист,
	|	МаршрутныйЛистПроизводства.Номер КАК Номер,
	|	МаршрутныйЛистПроизводства.Дата КАК Дата,
	|	МаршрутныйЛистПроизводства.Запланировано КАК Запланировано,
	|	МаршрутныйЛистПроизводства.Этап.Представление КАК Этап,
	|	МаршрутныйЛистПроизводства.Спецификация.Представление КАК Спецификация,
	|	МаршрутныйЛистПроизводства.Спецификация.МногоэтапныйПроизводственныйПроцесс КАК МногоэтапныйПроизводственныйПроцесс,
	|	ВЫБОР
	|		КОГДА МаршрутныйЛистПроизводстваОперации.Операция ССЫЛКА Справочник.ТехнологическиеОперации
	|			ТОГДА ВЫРАЗИТЬ(МаршрутныйЛистПроизводстваОперации.Операция КАК Справочник.ТехнологическиеОперации).Представление
	|		ИНАЧЕ МаршрутныйЛистПроизводстваОперации.Операция
	|	КОНЕЦ КАК Операция,
	|	МаршрутныйЛистПроизводстваОперации.РабочийЦентрПоРасписанию.Представление КАК РабочийЦентр,
	|	МаршрутныйЛистПроизводстваОперации.Начало КАК Начало,
	|	МаршрутныйЛистПроизводстваОперации.Окончание КАК Окончание
	|ИЗ
	|	ВыборкаМаршрутныхЛистов КАК ВыборкаМаршрутныхЛистов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛистПроизводства КАК МаршрутныйЛистПроизводства
	|		ПО (МаршрутныйЛистПроизводства.Ссылка = ВыборкаМаршрутныхЛистов.Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛистПроизводства.Операции КАК МаршрутныйЛистПроизводстваОперации
	|		ПО (МаршрутныйЛистПроизводстваОперации.Ссылка = ВыборкаМаршрутныхЛистов.Ссылка)
	|			И (МаршрутныйЛистПроизводстваОперации.КлючСвязи = ВыборкаМаршрутныхЛистов.КлючСвязиОперации)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Бригада,
	|	ДатаЗадания,
	|	Начало
	|ИТОГИ ПО
	|	Бригада,
	|	ДатаЗадания";
	
	ДатыЗаданий = Новый ТаблицаЗначений;
	ДатыЗаданий.Колонки.Добавить("ДатаЗадания", Новый ОписаниеТипов("Дата"));
	Для каждого ДатаЗадания Из ПараметрыПечати.ДатыЗаданий Цикл
		НоваяСтока = ДатыЗаданий.Добавить();
		НоваяСтока.ДатаЗадания = ДатаЗадания;
	КонецЦикла;
	
	Запрос.УстановитьПараметр("Подразделение",  ПараметрыПечати.Подразделение);
	Запрос.УстановитьПараметр("РабочийЦентр",   ПараметрыПечати.РабочийЦентр);
	Запрос.УстановитьПараметр("ДатыЗаданий",    ДатыЗаданий);
	Запрос.УстановитьПараметр("ТекущийДень",    НачалоДня(ТекущаяДатаСеанса()));
	Запрос.УстановитьПараметр("Бригады",        ПараметрыПечати.Бригады);
	
	Результат = Запрос.Выполнить();
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ПолеСлева = 5;
	ТабличныйДокумент.ПолеСправа = 5;
	ТабличныйДокумент.РазмерКолонтитулаСверху = 0;
	ТабличныйДокумент.РазмерКолонтитулаСнизу = 0;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СменноСуточноеЗадание";
	
	ОбластьЗаголовокТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьМакета           = Макет.ПолучитьОбласть("Строка");
	
	ВыборкаБригады = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ЗаданияВыдал = Пользователи.ТекущийПользователь();
	
	ПервыйДокумент = Истина;
	
	Пока ВыборкаБригады.Следующий() Цикл
		
		ВыборкаДатыЗаданий = ВыборкаБригады.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаДатыЗаданий.Следующий() Цикл
			
			Если Не ПервыйДокумент Тогда
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			
			ПервыйДокумент    = Ложь;
			НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
			
			// Выводим общие реквизиты шапки
			ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
			ОбластьЗаголовок.Параметры.ТекстЗаголовка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
																		НСтр("ru = 'Сменно-суточное задание на %1';
																			|en = 'Shift-day job on %1'"),
																		Формат(ВыборкаДатыЗаданий.ДатаЗадания, "ДЛФ=D"));
																		
			ОбластьЗаголовок.Параметры.Бригада = ВыборкаБригады.Бригада;
			ОбластьЗаголовок.Параметры.Выдал   = ЗаданияВыдал;
			
			ТабличныйДокумент.Вывести(ОбластьЗаголовок);
			
			СсылкаОбластьПечати = ОбластьЗаголовок.Параметры.ТекстЗаголовка + " (" + ОбластьЗаголовок.Параметры.Бригада + ")";
									
			НомерСтраницы = 1;
			
			// Создаем массив для проверки вывода
			МассивВыводимыхОбластей = Новый Массив;
			
			НомерСтроки = 0;
			
			СтрокаОперация = ВыборкаДатыЗаданий.Выбрать();
			Пока СтрокаОперация.Следующий() Цикл
				
				НомерСтроки = НомерСтроки + 1;
				
				ОбластьМакета.Параметры.Заполнить(СтрокаОперация);
				
				НомерНаПечать = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(СтрокаОперация.Номер, Ложь, Истина);
				ОбластьМакета.Параметры.МаршрутныйЛистСтрокой = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
																	НСтр("ru = '№%1 от %2';
																		|en = 'No. %1 from %2'"), 
																	НомерНаПечать, 
																	Формат(СтрокаОперация.Дата, "ДЛФ=D"));
																	
				СпецификацияСтрока = СтрокаОперация.Спецификация;
				Если СтрокаОперация.МногоэтапныйПроизводственныйПроцесс Тогда
					СпецификацияСтрока = СпецификацияСтрока + ", " + СтрокаОперация.Этап;
				КонецЕсли;
				ОбластьМакета.Параметры.СпецификацияСтрока = СпецификацияСтрока;
				ОбластьМакета.Параметры.НомерСтроки = Формат(НомерСтроки, "ЧГ=");
				
				Если НомерСтроки = 1 Тогда
				
					ТабличныйДокумент.Вывести(ОбластьЗаголовокТаблицы);
					
				Иначе
				
					МассивВыводимыхОбластей.Очистить();
					МассивВыводимыхОбластей.Добавить(ОбластьМакета);
					
					Если НомерСтроки <> 1 И Не ТабличныйДокумент.ПроверитьВывод(МассивВыводимыхОбластей) Тогда
						
						НомерСтраницы = НомерСтраницы + 1;
						ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
						ТабличныйДокумент.Вывести(ОбластьЗаголовокТаблицы);
						
					КонецЕсли;
					
				КонецЕсли;
					
				ТабличныйДокумент.Вывести(ОбластьМакета);
				
			КонецЦикла;
		
			ОбластьПодписи = Макет.ПолучитьОбласть("Подписи");
			ТабличныйДокумент.Вывести(ОбластьПодписи);
			
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, СсылкаОбластьПечати);
			
		КонецЦикла; 
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

#КонецОбласти
 
#КонецОбласти

#КонецЕсли
//-- Устарело_Производство21