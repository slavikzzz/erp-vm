#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции
	
#Область ПравилаПубликации

Процедура ЗаполнитьДеревоНастроекПубликации(Параметры, АдресХранилища) Экспорт

	Приложение = Параметры.Приложение;
	
	Результат = Новый Структура("РезультатЗапроса,ИспользоватьШтатноеРасписание,ДанныеДоступны");
	
	ИспользоватьШтатноеРасписание = Ложь;
	
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	УстановитьПривилегированныйРежим(Истина);
	ИнтеграцияУправлениеПерсоналом.СоздатьВТШтатноеРасписание(МенеджерВТ, ИспользоватьШтатноеРасписание);
	УстановитьПривилегированныйРежим(Ложь);
	
	Результат.ИспользоватьШтатноеРасписание = ИспользоватьШтатноеРасписание;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Приложение", Приложение);
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ШтатноеРасписание.Организация КАК Организация,
	|	ШтатноеРасписание.Подразделение КАК Подразделение,
	|	ШтатноеРасписание.ПозицияШтатногоРасписания КАК ПозицияШтатногоРасписания,
	|	ШтатноеРасписание.Должность КАК Должность,
	|	НЕ ШтатноеРасписание.Утверждена КАК НеУтверждена,
	|	ШтатноеРасписание.ДатаУтверждения КАК ДатаУтверждения,
	|	ШтатноеРасписание.Закрыта КАК Закрыта,
	|	ШтатноеРасписание.ДатаЗакрытия КАК ДатаЗакрытия,
	|	ШтатноеРасписание.Подразделение.РеквизитДопУпорядочиванияИерархического КАК ПорядокПодразделений,
	|	ШтатноеРасписание.Должность.РеквизитДопУпорядочивания КАК ПорядокДолжностей,
	|	ВЫБОР
	|		КОГДА ПравилаВыгрузки.Ссылка ЕСТЬ NULL
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Выгружать
	|ИЗ
	|	ВТШтатноеРасписание КАК ШтатноеРасписание
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПравилаВыгрузкиУправлениеПерсоналом КАК ПравилаВыгрузки
	|		ПО ШтатноеРасписание.ПозицияШтатногоРасписания = ПравилаВыгрузки.Ссылка
	|			И (ПравилаВыгрузки.Ссылка ССЫЛКА Справочник.ШтатноеРасписание)
	|			И (ПравилаВыгрузки.Приложение = &Приложение)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ШтатноеРасписание.Организация.Наименование,
	|	ШтатноеРасписание.Подразделение.РеквизитДопУпорядочиванияИерархического,
	|	ШтатноеРасписание.Должность.РеквизитДопУпорядочивания
	|ИТОГИ ПО
	|	Организация,
	|	Подразделение ИЕРАРХИЯ";
	Если Не ИспользоватьШтатноеРасписание Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "НЕ ШтатноеРасписание.Утверждена", "ЛОЖЬ");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ШтатноеРасписание.ДатаУтверждения", """");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ШтатноеРасписание.Закрыта", "ЛОЖЬ");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ШтатноеРасписание.ДатаЗакрытия", """");
	КонецЕсли;
	
	Попытка
		РезультатЗапроса = Запрос.Выполнить();
		Результат.ДанныеДоступны = Истина;
		Результат.РезультатЗапроса = РезультатЗапроса;
	Исключение
		СообщениеОбОшибке = НСтр("ru = 'Настройка правил. Пользователю недоступны организации и/или позиции штатного расписания.';
								|en = 'Rule settings. Companies or headcount positions are not available to a user.'");
		Результат.ДанныеДоступны = Ложь;
	КонецПопытки;
	
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);

КонецПроцедуры

Процедура СохранитьПравилаВыгрузки(Параметры, АдресХранилища) Экспорт
	
	Приложение = Параметры.Приложение;
	УстановитьПривилегированныйРежим(Истина);
	
	Попытка
		ИнтеграцияУправлениеПерсоналом.СохранитьНовыеПравилаВыгрузки(Приложение, Параметры.НовыеНастройки);
	Исключение
		ШаблонОписания = НСтр("ru = 'Ошибка при записи правил приложения %1
		|%2';
		|en = 'An error occurred when saving the application rules %1
		|%2'");
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Комментарий = СтрШаблон(ШаблонОписания, Строка(Приложение), ПодробноеПредставлениеОшибки);
		ИмяСобытия = ИнтеграцияУправлениеПерсоналом.ИменаСобытийЖР(Приложение).ПрочиеСобытия;
		ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка,,, Комментарий);
		// Текст ошибки обрабатывается при завершении фонового задания.
		ВызватьИсключение ОбработкаОшибок.ПредставлениеОшибкиДляПользователя(ИнформацияОбОшибке());
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли

