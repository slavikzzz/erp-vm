#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ВидКИМобильныйТелефон", ВидКИМобильныйТелефон);
	Параметры.Свойство("ВидКИАдресЭлектроннойПочты", ВидКИАдресЭлектроннойПочты);
	
	КартинкиВидыКонтактнойИнформации = БиблиотекаКартинок.ВидыКонтактнойИнформации;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВидыКонтактнойИнформации.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ВидыКонтактнойИнформации КАК ВидыКонтактнойИнформации
	|ГДЕ
	|	ВидыКонтактнойИнформации.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты)
	|	И ВидыКонтактнойИнформации.Родитель = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.СправочникФизическиеЛица)
	|	И НЕ ВидыКонтактнойИнформации.Предопределенный
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыКонтактнойИнформации.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ВидыКонтактнойИнформации КАК ВидыКонтактнойИнформации
	|ГДЕ
	|	ВидыКонтактнойИнформации.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон)
	|	И ВидыКонтактнойИнформации.Родитель = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.СправочникФизическиеЛица)
	|	И НЕ ВидыКонтактнойИнформации.Предопределенный
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка";
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ВидыКИАдресЭлектроннойПочты = РезультатЗапроса[0].Выгрузить().ВыгрузитьКолонку("Ссылка");
	ВидыКИАдресЭлектроннойПочты.Вставить(0, Справочники.ВидыКонтактнойИнформации.EMailФизическиеЛица);
	Элементы.ВидКИАдресЭлектроннойПочты.СписокВыбора.ЗагрузитьЗначения(ВидыКИАдресЭлектроннойПочты);
	
	ВидыКИМобильныйТелефон = РезультатЗапроса[1].Выгрузить().ВыгрузитьКолонку("Ссылка");
	ВидыКИМобильныйТелефон.Вставить(0, Справочники.ВидыКонтактнойИнформации.ТелефонМобильныйФизическиеЛица);
	Элементы.ВидКИМобильныйТелефон.СписокВыбора.ЗагрузитьЗначения(ВидыКИМобильныйТелефон);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Изменить(Команда)
	
	Отказ = Ложь;
	Если Не ЗначениеЗаполнено(ВидКИМобильныйТелефон) Или Не ЗначениеЗаполнено(ВидКИАдресЭлектроннойПочты) Тогда
		Отказ = Истина;
	Иначе
		Результат = Новый Структура();
		Результат.Вставить("ВидКИМобильныйТелефон", 		ВидКИМобильныйТелефон);
		Результат.Вставить("ВидКИАдресЭлектроннойПочты", 	ВидКИАдресЭлектроннойПочты);
	КонецЕсли;
	
	Если Не Отказ Тогда
		Закрыть(Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отменить(Команда)
	Закрыть();
КонецПроцедуры

#КонецОбласти
