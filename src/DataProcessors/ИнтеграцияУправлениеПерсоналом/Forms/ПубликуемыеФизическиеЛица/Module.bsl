#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	
	Если Параметры.ТолькоОшибкиЗаполнения Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "БлокирующаяОшибка", Истина);
	КонецЕсли;
	
	Если Параметры.Приложение = Перечисления.ПриложенияДляИнтеграции.УправлениеПерсоналом Тогда
		ЗаголовокФормы = НСтр("ru = 'Выгружаемые физические лица';
								|en = 'Persons to export'");
		Список.Параметры.УстановитьЗначениеПараметра("ОкончаниеВыгрузки", ТекущаяДатаСеанса());
		Список.Параметры.УстановитьЗначениеПараметра("ДнейСохраненияПубликации", 0);
		Элементы.СписокОкончаниеВыгрузки.Видимость = Ложь;
	Иначе
		
		ТекстЗапроса = СтрЗаменить(Список.ТекстЗапроса, "ВыгружаемыеОбъекты1СПерсонал", "ВыгружаемыеОбъектыКабинетСотрудника");
		Список.ТекстЗапроса = ТекстЗапроса;
		
		Настройки = РегистрыСведений.НастройкиИнтеграцииКабинетСотрудника.НастройкиИнтеграции();
		Список.Параметры.УстановитьЗначениеПараметра("ОкончаниеВыгрузки",		ТекущаяДатаСеанса() - Настройки.ДнейСохраненияПубликации * 86400);
		Список.Параметры.УстановитьЗначениеПараметра("ДнейСохраненияПубликации",Настройки.ДнейСохраненияПубликации);
		
		Если Параметры.ТолькоОшибкиЗаполнения Тогда
			ЗаголовокФормы = НСтр("ru = 'Публикуемые кабинеты сотрудников с ошибками заполнения';
									|en = 'Employee accounts to publish with filling errors'");
		Иначе
			ЗаголовокФормы = НСтр("ru = 'Публикуемые кабинеты сотрудников';
									|en = 'Employee accounts to publish'");
		КонецЕсли; 
		
	КонецЕсли;
	Заголовок = ЗаголовокФормы;
	
	Настройки = РегистрыСведений.НастройкиИнтеграцииУправлениеПерсоналом.Настройки();
	Список.Параметры.УстановитьЗначениеПараметра("ВидКИАдресПочты", 		Настройки.ВидКИАдресЭлектроннойПочты);
	Список.Параметры.УстановитьЗначениеПараметра("ВидКИМобильныйТелефон",	Настройки.ВидКИМобильныйТелефон);
	Список.Параметры.УстановитьЗначениеПараметра("Приложение", 				Параметры.Приложение);
	
КонецПроцедуры

#КонецОбласти
