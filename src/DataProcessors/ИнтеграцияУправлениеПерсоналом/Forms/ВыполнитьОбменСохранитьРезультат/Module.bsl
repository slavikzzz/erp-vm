
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СобытиеОбмена 		= Параметры.СобытиеОбмена;
	Приложение 			= Параметры.Приложение;
	ИмяФайла 			= Параметры.ИмяФайла;
	КлючФоновогоЗадания = Параметры.КлючФоновогоЗадания;
	
	Если Не ЗначениеЗаполнено(СобытиеОбмена) Или ПустаяСтрока(ИмяФайла) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ИнтервалОжидания = ?(ОбщегоНазначения.ИнформационнаяБазаФайловая(),1,10); 
	
	ПараметрыМетода = Новый Структура;
	ПараметрыМетода.Вставить("ПодготовитьДанныеДляТехПоддержки", Истина);
	Если ЗначениеЗаполнено(Параметры.ПараметрыМетода) Тогда
		Для каждого ЭлементКоллекции Из Параметры.ПараметрыМетода Цикл
			ПараметрыМетода.Вставить(ЭлементКоллекции.Ключ, ЭлементКоллекции.Значение);
		КонецЦикла;
	КонецЕсли;
	
	ЗапуститьФоновоеЗадание("Обмен", Параметры.ИмяМетода, ПараметрыМетода);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПриНачалеЗадания();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если АктивноеЗадание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	ОтменитьВыполнениеЗадания(АктивноеЗадание.ДлительнаяОперация.ИдентификаторЗадания);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если АктивноеЗадание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	ОтменитьВыполнениеЗадания(АктивноеЗадание.ДлительнаяОперация.ИдентификаторЗадания);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОтменитьВыполнениеЗадания(ИдентификаторЗадания)
	
	ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	
КонецПроцедуры

&НаКлиенте
Функция РезультатВыполнения()
	
	Результат = Новый Структура;
	Результат.Вставить("ИмяСобытияОбмена", 			СобытиеОбмена);
	Результат.Вставить("РезультатВыполненияОбмена", РезультатВыполненияОбмена);
	Результат.Вставить("ПолноеИмяФайла", 			ПолноеИмяФайла);
	Результат.Вставить("СообщениеОбОшибке");
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ЗапуститьФоновоеЗадание(ИмяЗадания, ИмяМетода, ПараметрыПроцедуры)
	
	ПараметрыВыполненияВФоне = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполненияВФоне.ОжидатьЗавершение = 0;
	Если ЗначениеЗаполнено(КлючФоновогоЗадания) Тогда
		ПараметрыВыполненияВФоне.КлючФоновогоЗадания = КлючФоновогоЗадания;
	КонецЕсли;
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(ИмяМетода, ПараметрыПроцедуры, ПараметрыВыполненияВФоне);
	
	НовоеЗадание = Новый Структура("Имя,ДлительнаяОперация");
	НовоеЗадание.Имя 				= ИмяЗадания;
	НовоеЗадание.ДлительнаяОперация = ДлительнаяОперация;
	
	АктивноеЗадание = Новый ФиксированнаяСтруктура(НовоеЗадание);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриНачалеЗадания()
	
	Если ЗначениеЗаполнено(АктивноеЗадание) Тогда
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		ДлительныеОперацииКлиент.ОжидатьЗавершение(
			АктивноеЗадание.ДлительнаяОперация,
			Новый ОписаниеОповещения("ПриЗавершенииЗадания", ЭтотОбъект, АктивноеЗадание.Имя),
			ПараметрыОжидания);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗавершенииЗадания(ФоновоеЗадание, ИмяЗадания) Экспорт
	
	АктивноеЗадание = Неопределено;
	
	Если ФоновоеЗадание = Неопределено Тогда
		Закрыть(РезультатВыполнения());
	ИначеЕсли ФоновоеЗадание.Статус = "Ошибка" Тогда
		ТекстСообщения = НСтр("ru = 'При выполнении фонового задания возникла ошибка:';
								|en = 'An error occurred while executing a background job:'") + Символы.ПС + ФоновоеЗадание.КраткоеПредставлениеОшибки;
		РезультатВыполнения = РезультатВыполнения();
		РезультатВыполнения.СообщениеОбОшибке = ТекстСообщения;
		Закрыть(РезультатВыполнения);
	ИначеЕсли ФоновоеЗадание.Статус = "Выполнено" Тогда
		
		Если ИмяЗадания = "ВыгрузкаЖурналаРегистрации" Тогда
			ВыгрузкаЖурналаРегистрацииЗавершение(ФоновоеЗадание);
		Иначе
			РезультатВыполнения = ПолучитьИзВременногоХранилища(ФоновоеЗадание.АдресРезультата);
			РезультатВыполненияОбмена = Новый ФиксированнаяСтруктура(РезультатВыполнения);
			ПодключитьОбработчикОжидания("ВыгрузкаЖурналаРегистрации", ИнтервалОжидания, Истина);
		КонецЕсли;
		
	Иначе
		Закрыть(РезультатВыполнения());
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузкаЖурналаРегистрации()

	ПараметрыПроцедуры = ПараметрыОтбораЖР();
	ИмяМетода = "ИнтеграцияУправлениеПерсоналом.ВыгрузкаЖурналаРегистрацииФоновоеЗадание";
	ЗапуститьФоновоеЗадание("ВыгрузкаЖурналаРегистрации", ИмяМетода, ПараметрыПроцедуры);
	
	ПриНачалеЗадания();
	
КонецПроцедуры

&НаСервере
Функция ПараметрыОтбораЖР()

	ИменаСобытий = ИнтеграцияУправлениеПерсоналом.ИменаСобытийЖР(Приложение);
	СобытияЖР = Новый Массив;
	Для каждого ЭлементКоллекции Из ИменаСобытий Цикл
		СобытияЖР.Добавить(ЭлементКоллекции.Значение);
	КонецЦикла;
	ОтборПоДатам = Новый Структура("ДатаНачала,ДатаОкончания");
	ОтборПоДатам.ДатаНачала 	= РезультатВыполненияОбмена.ДатаНачала;
	ОтборПоДатам.ДатаОкончания 	= РезультатВыполненияОбмена.ДатаОкончания;
	ОтборЖурналаРегистрации = ИнтеграцияУправлениеПерсоналом.ОтборЖурналаРегистрацииДляВыгрузки(СобытияЖР, ОтборПоДатам);
	
	Возврат Новый Структура("ОтборЖурналаРегистрации", ОтборЖурналаРегистрации);

КонецФункции

&НаКлиенте
Процедура ВыгрузкаЖурналаРегистрацииЗавершение(ФоновоеЗадание)
	
	ОбработчикЗавершения = Новый ОписаниеОповещения("СохранитьФайлЗавершение", ЭтотОбъект);
	
	ПараметрыСохраненияФайла = ФайловаяСистемаКлиент.ПараметрыСохраненияФайла();
	ПараметрыСохраненияФайла.Диалог.Фильтр = НСтр("ru = 'Данные журнала регистрации';
													|en = 'Event Log data'") + "(*.zip)|*.zip";
	ИмяФайлаСРасширением = СтрШаблон("%1.%2", ИмяФайла, "zip");
	ФайловаяСистемаКлиент.СохранитьФайл(ОбработчикЗавершения, ФоновоеЗадание.АдресРезультата, ИмяФайлаСРасширением, ПараметрыСохраненияФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьФайлЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если Результат <> Неопределено Тогда
		ПолноеИмяФайла = Результат[0].ПолноеИмя;
	КонецЕсли;
	
	Закрыть(РезультатВыполнения());

КонецПроцедуры

#КонецОбласти






