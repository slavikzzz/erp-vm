
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ПриложениеВЛокальнойСети", ПриложениеВЛокальнойСети);
	
	ЦветСтиляПоясняющийОшибкуТекст 	= ЦветаСтиля.ПоясняющийОшибкуТекст;
	ЦветСтиляИнформационнойНадписи 	= ЦветаСтиля.ТекстИнформационнойНадписи;
	
	ОбновитьЭУОтветственный(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ФизическиеЛица" И Источник = Ответственный Тогда
		ЗаполнитьРеквизитыОтветственного(Ответственный, Логин, ЭлектроннаяПочта, МобильныйТелефон, Пол);
		ОбновитьЭУОтветственный(ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтветственныйПриИзменении(Элемент)
	
	ЗаполнитьРеквизитыОтветственного(Ответственный, Логин, ЭлектроннаяПочта, МобильныйТелефон, Пол);
	ОбновитьЭУОтветственный(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)
	
	Отказ = Ложь;
	Если ЕстьОшибкиЗаполнения() Тогда
		Отказ = Истина;
	Иначе
		Результат = РезультатРедактирования();
	КонецЕсли;
	
	Если Не Отказ Тогда
		Закрыть(Результат);
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьЭУОтветственный(Форма)
	
	Элементы = Форма.Элементы;
	Форма.ДанныеОтветственногоЗаполнены = Ложь;
	
	Если Не ЗначениеЗаполнено(Форма.Ответственный) Тогда
		Элементы.ДекорацияТелефонПочтаОтветственного.Заголовок = "";
		Элементы.ДекорацияРеквизитыОтветственногоПояснение.Заголовок = "";
		Элементы.ДекорацияРеквизитыОтветственногоПояснение.ЦветТекста = Форма.ЦветСтиляИнформационнойНадписи;
	Иначе
		
		ТелефонУказан = Ложь;
		ТелефонСоответствуетТребованиям = Ложь;
		Если ЗначениеЗаполнено(Форма.МобильныйТелефон) Тогда
			СтрокаТелефон = СОКРЛП(Форма.МобильныйТелефон);
			ТелефонУказан = Истина;
			ТелефонСоответствуетТребованиям = (Лев(СтрокаТелефон,1) = "+") И СтрНайти("123456789", Сред(СтрокаТелефон, 2, 1)) <> 0;
		Иначе
			СтрокаТелефон = НСтр("ru = '<телефон не указан>';
								|en = '<телефон не указан>'");
		КонецЕсли;
		
		ПочтаУказана = Ложь;
		ПочтаСоответствуетТребованиям = Ложь;
		Если ЗначениеЗаполнено(Форма.ЭлектроннаяПочта) Тогда
			СтрокаПочта = СОКРЛП(Форма.ЭлектроннаяПочта);
			ПочтаУказана = Истина;
			ПочтаСоответствуетТребованиям = ОбщегоНазначенияКлиентСервер.АдресЭлектроннойПочтыСоответствуетТребованиям(Форма.ЭлектроннаяПочта);
		Иначе
			СтрокаПочта = НСтр("ru = '<Email не указан>';
								|en = '<Email не указан>'");
		КонецЕсли;
		
		СтрокаТелефонПочта = СтрШаблон(НСтр("ru = 'Мобильный телефон и почта: %1, %2';
											|en = 'Мобильный телефон и почта: %1, %2'"), СтрокаТелефон, СтрокаПочта);
		Элементы.ДекорацияТелефонПочтаОтветственного.Заголовок = СтрокаТелефонПочта;
		
		Если ТелефонУказан И ТелефонСоответствуетТребованиям И ПочтаУказана И ПочтаСоответствуетТребованиям И ЗначениеЗаполнено(Форма.Пол) Тогда
			Элементы.ДекорацияРеквизитыОтветственногоПояснение.ЦветТекста = Форма.ЦветСтиляИнформационнойНадписи;
			Элементы.ДекорацияРеквизитыОтветственногоПояснение.Заголовок = НСтр("ru = 'Убедитесь в том, что у ответственного лица задан действующий мобильный телефон и адрес электронной почты';
																				|en = 'Убедитесь в том, что у ответственного лица задан действующий мобильный телефон и адрес электронной почты'");
			Форма.ДанныеОтветственногоЗаполнены = Истина;
		Иначе
			
			Если ТелефонУказан И ТелефонСоответствуетТребованиям И ПочтаУказана И ПочтаСоответствуетТребованиям Тогда
				СообщениеОбОшибке =НСтр("ru = 'Заполните, пожалуйста, пол ответственного';
										|en = 'Заполните, пожалуйста, пол ответственного'");
			Иначе
				
				СообщениеНеУказанПол = "";
				Если Не ЗначениеЗаполнено(Форма.Пол) Тогда
					СообщениеНеУказанПол = НСтр("ru = ', не указан пол';
												|en = ', не указан пол'");
				КонецЕсли;
				
				СообщениеПочтаСоответствуетТребованиям = "";
				Если ПочтаУказана И Не ПочтаСоответствуетТребованиям Тогда
					СообщениеПочтаСоответствуетТребованиям = НСтр("ru = ', адрес электронной почты указан некорректно';
																	|en = ', адрес электронной почты указан некорректно'");
				КонецЕсли;
				
				СообщениеТелефонСоответствуетТребованиям = "";
				Если ТелефонУказан И Не ТелефонСоответствуетТребованиям Тогда
					Если (Лев(СтрокаТелефон,1) <> "+") Тогда
						СообщениеТелефонСоответствуетТребованиям = НСтр("ru = ', номер телефон должен начинаться с символа ""+""';
																		|en = ', номер телефон должен начинаться с символа ""+""'");
					Иначе
						СообщениеТелефонСоответствуетТребованиям = НСтр("ru = ', код страны в мобильном телефоне указан некорректно';
																		|en = ', код страны в мобильном телефоне указан некорректно'");
					КонецЕсли;
				КонецЕсли;
				
				СообщениеОбОшибке = СтрШаблон(
					НСтр("ru = 'Заполните, пожалуйста, контактные данные ответственного%1%2%3';
						|en = 'Заполните, пожалуйста, контактные данные ответственного%1%2%3'"),
					СообщениеТелефонСоответствуетТребованиям,
					СообщениеПочтаСоответствуетТребованиям,
					СообщениеНеУказанПол);
				
			КонецЕсли;
			
			Элементы.ДекорацияРеквизитыОтветственногоПояснение.ЦветТекста = Форма.ЦветСтиляПоясняющийОшибкуТекст;
			Элементы.ДекорацияРеквизитыОтветственногоПояснение.Заголовок = СообщениеОбОшибке;
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьРеквизитыОтветственного(Ответственный, Логин, ЭлектроннаяПочта, МобильныйТелефон, Пол)
	
	Логин 			 = "";
	ЭлектроннаяПочта = "";
	МобильныйТелефон = "";
	Пол = Перечисления.ПолФизическогоЛица.ПустаяСсылка();
	
	Если Не ЗначениеЗаполнено(Ответственный) Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыОбъекта =  ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ответственный, "Фамилия,Инициалы,Пол");
	Пол = РеквизитыОбъекта["Пол"];
	Инициалы = СтрЗаменить(РеквизитыОбъекта["Инициалы"],".","");
	Инициалы = СтрЗаменить(Инициалы," ","");
	Логин = СтрШаблон("%1%2", РеквизитыОбъекта["Фамилия"], Инициалы);
	
	КонтактнаяИнформация = ИнтеграцияУправлениеПерсоналом.КонтактнаяИнформацияФизическихЛиц(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Ответственный));
	МобильныйТелефон = КонтактнаяИнформация.МобильныеТелефоны[Ответственный];
	ЭлектроннаяПочта = КонтактнаяИнформация.АдресаПочты[Ответственный];
	
КонецПроцедуры

&НаКлиенте
Функция РезультатРедактирования()

	Результат = Новый Структура();
	Результат.Вставить("ИмяПриложения", 	ИмяПриложения);
	Результат.Вставить("Ответственный", 	Ответственный);
	Результат.Вставить("Логин", 			Логин);
	Результат.Вставить("МобильныйТелефон", 	МобильныйТелефон);
	Результат.Вставить("ЭлектроннаяПочта", 	ЭлектроннаяПочта);
	
	Возврат Результат;

КонецФункции

&НаКлиенте
Функция ЕстьОшибкиЗаполнения()

	Если ЗначениеЗаполнено(Ответственный) И Не ДанныеОтветственногоЗаполнены Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;

КонецФункции

#КонецОбласти