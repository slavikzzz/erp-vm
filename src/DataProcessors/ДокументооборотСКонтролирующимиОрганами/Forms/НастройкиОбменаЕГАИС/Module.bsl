
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.ТекстОшибки) Тогда
		Элементы.ДекорацияТекстОшибки.Заголовок = Параметры.ТекстОшибки;
		Элементы.ДекорацияТекстОшибки.Видимость = Истина;
	КонецЕсли;
	
	ОрганизацияСсылка = Параметры.ОрганизацияСсылка;
	
	ЗаполнениеПоддерживается = ДокументооборотСКОВызовСервера.НастройкиОбменаЕГАИС(
		Параметры.ОрганизацияСсылка,
		НастройкиОбмена);
	Если НЕ ЗаполнениеПоддерживается Тогда
		Элементы.АдресУТМ.КнопкаВыбора = Неопределено;
		ИсходнаяШирина = Элементы.АдресУТМ.Ширина;
		Элементы.АдресУТМ.Ширина = ИсходнаяШирина + 2;
		Элементы.АдресУТМ.МаксимальнаяШирина = ИсходнаяШирина + 2;
	КонецЕсли;
	
	ЭлементНастроекОбмена = Параметры.НастройкиОбмена;
	Если ЭлементНастроекОбмена = Неопределено Тогда
		СохраненныеНастройкиОбмена =
			ХранилищеОбщихНастроек.Загрузить("ДокументооборотСКонтролирующимиОрганами_НастройкиОбменаЕГАИС");
		
		Если СохраненныеНастройкиОбмена = Неопределено Тогда
			СохраненныеНастройкиОбмена = Новый Соответствие;
		КонецЕсли;
		
		ЭлементНастроекОбмена = СохраненныеНастройкиОбмена[Параметры.ОрганизацияСсылка];
		Если ЭлементНастроекОбмена = Неопределено И НастройкиОбмена.Количество() = 1 Тогда
			ЭлементНастроекОбмена = НастройкиОбмена[0].Значение;
		КонецЕсли;
	КонецЕсли;
		
	Если ЭлементНастроекОбмена = Неопределено Тогда
		АдресУТМ = "127.0.0.1";
		ПортУТМ = 8080;
		Таймаут = 60;
		ОбменНаСервере = 1;
		
	Иначе
		АдресУТМ = ЭлементНастроекОбмена.АдресУТМ;
		ПортУТМ = ЭлементНастроекОбмена.ПортУТМ;
		Таймаут = ЭлементНастроекОбмена.Таймаут;
		ОбменНаСервере = ?(ЭлементНастроекОбмена.ОбменНаСервере, 1, 0);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	РезультатВыбора = Новый Структура;
	РезультатВыбора.Вставить("АдресУТМ", 		АдресУТМ);
	РезультатВыбора.Вставить("ПортУТМ", 		ПортУТМ);
	РезультатВыбора.Вставить("Таймаут", 		Таймаут);
	РезультатВыбора.Вставить("ОбменНаСервере", 	ОбменНаСервере <> 0);
	
	Закрыть(РезультатВыбора);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДекорацияИнформацияОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "#МашиночитаемыеДоверенности" Тогда
		СтандартнаяОбработка = Ложь;
		Если ДокументооборотСКОКлиентСервер.ПодсистемаЦПРРМЧДСуществует() Тогда
			ИмяСправочникаМашиночитаемыеДоверенности = "МашиночитаемыеДоверенности";
			ОткрытьФорму("Справочник." + ИмяСправочникаМашиночитаемыеДоверенности + ".ФормаСписка");
		Иначе
			ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("https://fsrar.gov.ru/egais/mchd");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АдресУТМНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("АдресУТМНачалоВыбораПослеВыбора", ЭтотОбъект);
	ДокументооборотСКОКлиент.ВыбратьНастройкиОбменаЕГАИС(
		ОписаниеОповещения,
		ОрганизацияСсылка,
		НастройкиОбмена);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура АдресУТМНачалоВыбораПослеВыбора(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЭлемент <> Неопределено Тогда
		АдресУТМ 		= ВыбранныйЭлемент.Значение.АдресУТМ;
		ПортУТМ 		= ВыбранныйЭлемент.Значение.ПортУТМ;
		Таймаут 		= ВыбранныйЭлемент.Значение.Таймаут;
		ОбменНаСервере 	= ВыбранныйЭлемент.Значение.ОбменНаСервере;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
