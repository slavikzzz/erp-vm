&НаКлиенте
Перем КонтекстЭДОКлиент;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	АдресВременногоХранилища = Параметры.Адрес;
	Если ЭтоАдресВременногоХранилища(АдресВременногоХранилища) Тогда
		
		ПереданныеОтчеты = ПолучитьИзВременногоХранилища(Параметры.Адрес);
		Отчеты.Загрузить(ПереданныеОтчеты);
		
		НапомнитьПозжеОНеобходимостиДублированияОтправок(Истина);
		
	Иначе
		
		Отказ = Истина;
		
	КонецЕслИ;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииЗавершение", ЭтотОбъект);
	ДокументооборотСКОКлиент.ПолучитьКонтекстЭДО(ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

&НаКлиенте
Процедура ОтчетыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элементы.Отчеты.ТекущиеДанные <> Неопределено Тогда 
		СтандартнаяОбработка = Ложь;
		ПоказатьЗначение(, Элементы.Отчеты.ТекущиеДанные.Отчет);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьОтчет(Команда)
	
	Если Элементы.Отчеты.ТекущиеДанные <> Неопределено Тогда
		ПоказатьЗначение(, Элементы.Отчеты.ТекущиеДанные.Отчет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НапомнитьПозжеКоманда(Команда)
	
	НапомнитьПозжеОНеобходимостиДублированияОтправок(Истина);
	ПрограммноеЗакрытие = Истина;
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура БольшеНеПоказыватьКоманда(Команда)
	
	НапомнитьПозжеОНеобходимостиДублированияОтправок(Ложь);
	ПрограммноеЗакрытие = Истина;
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПриОткрытииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	КонтекстЭДОКлиент = Результат.КонтекстЭДО;
	
КонецПроцедуры

&НаСервере
Процедура НапомнитьПозжеОНеобходимостиДублированияОтправок(Ответ)
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	КонтекстЭДОСервер.НапомнитьПозжеОНеобходимостиДублированияОтправок(Ответ);
	
КонецПроцедуры

#КонецОбласти




