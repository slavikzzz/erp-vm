
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Заголовок = Параметры.ЗаголовокСписка;
	
	СписокЗначенийСсылок = Параметры.СписокЗначенийСсылок;
	Для каждого ЭлементСписка Из СписокЗначенийСсылок Цикл
		ЗаписьСпискаЗначений = ЗаписиСпискаЗначений.Добавить();
		ЗаписьСпискаЗначений.Представление = ?(ЗначениеЗаполнено(ЭлементСписка.Представление),
			ЭлементСписка.Представление, Строка(ЭлементСписка.Значение));
		ЗаписьСпискаЗначений.СсылкаНаОбъект = ЭлементСписка.Значение;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

&НаКлиенте
Процедура ЗаписиСпискаЗначенийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элементы.ЗаписиСпискаЗначений.ТекущиеДанные <> Неопределено Тогда
		Если Поле.Имя = "ЗаписиСпискаЗначенийПредставление" Тогда
			СтандартнаяОбработка = Ложь;
			ПоказатьЗначение(, Элементы.ЗаписиСпискаЗначений.ТекущиеДанные.СсылкаНаОбъект);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьЗапись(Команда)
	
	Если Элементы.ЗаписиСпискаЗначений.ТекущиеДанные <> Неопределено Тогда
		ПоказатьЗначение(, Элементы.ЗаписиСпискаЗначений.ТекущиеДанные.СсылкаНаОбъект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
