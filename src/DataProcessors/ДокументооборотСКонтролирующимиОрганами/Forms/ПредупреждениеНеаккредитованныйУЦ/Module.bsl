
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Организация 					= Параметры.Организация;
	ОрганизацияНеаккредитованногоУЦ = Параметры.ОрганизацияНеаккредитованногоУЦ;
	ОтпечатокСертификата 			= Параметры.ОтпечатокСертификата;
	
	ТекстОшибкиОНеаккредитованномУЦ = СтрШаблон(
		НСтр("ru = 'Ваш сертификат издан удостоверяющим центром %1,
		|который не прошел аккредитацию по новым правилам, согласно
		|Федеральному закону № 63 ""Об электронной подписи""
		|(в ред. от 27.12.2019 № 476-ФЗ). Поэтому сертификат не может быть
		|использован для подписания отчетности с 01.01.2022 и подлежит
		|обязательной замене.';
		|en = 'Ваш сертификат издан удостоверяющим центром %1,
		|который не прошел аккредитацию по новым правилам, согласно
		|Федеральному закону № 63 ""Об электронной подписи""
		|(в ред. от 27.12.2019 № 476-ФЗ). Поэтому сертификат не может быть
		|использован для подписания отчетности с 01.01.2022 и подлежит
		|обязательной замене.'"),
		ОрганизацияНеаккредитованногоУЦ);
	
	ПолужирныйШрифт = Новый Шрифт(Элементы.ДекорацияПредупреждение.Шрифт,,, Истина);
	ДокументНеБудетПринятПолужирным = Новый ФорматированнаяСтрока(
		НСтр("ru = 'Вероятно, документ не будет принят контролирующим органом.';
			|en = 'Вероятно, документ не будет принят контролирующим органом.'"), ПолужирныйШрифт);
	
	Элементы.ДекорацияПредупреждение.Заголовок = Новый ФорматированнаяСтрока(
		ТекстОшибкиОНеаккредитованномУЦ + "
		|
		|",
		ДокументНеБудетПринятПолужирным,
		"
		|" + НСтр("ru = 'Вы уверены, что хотите продолжить отправку?';
					|en = 'Вы уверены, что хотите продолжить отправку?'"));
	
	ДатаСеанса = ТекущаяДатаСеанса();
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	Элементы.ПодготовитьЗаявление.Видимость = ЗначениеЗаполнено(Организация)
		И (НЕ ЗначениеЗаполнено(ОтпечатокСертификата)
		ИЛИ НЕ КонтекстЭДОСервер.ЗаявлениеАбонентаДляЗаменыСертификатаБылоСоздано(ОтпечатокСертификата, ДатаСеанса));
	
	Если НЕ Элементы.ПодготовитьЗаявление.Видимость Тогда
		Элементы.Отмена.КнопкаПоУмолчанию = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодготовитьЗаявление(Команда)
	
	ПараметрыОткрытияФормы = Новый Структура();
	ПараметрыОткрытияФормы.Вставить("ПриОткрытииЗапрошеноПродлениеСертификата", Истина);
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьФормуМастераЗаявленияНаПодключение(
		Организация,
		ЭтаФорма,,
		ПредопределенноеЗначение("Перечисление.ТипыЗаявленияАбонентаСпецоператораСвязи.Изменение"),
		ПараметрыОткрытияФормы);
	
КонецПроцедуры

#КонецОбласти
