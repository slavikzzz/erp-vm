
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ПоказатьТолькоОтличия = Истина;
	Элементы.ФормаПоказатьТолькоОтличия.Пометка = ПоказатьТолькоОтличия;
	
	Если Параметры.Свойство("СписокИсточников") Тогда
		Для каждого ЭлементКоллекции Из Параметры.СписокИсточников Цикл
			СписокСпецификаций.Добавить(ЭлементКоллекции);
		КонецЦикла; 
		
		ВыполнитьСравнениеНаСервере();
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)
	
	РежимСравненияИзделийИМатериалов = Настройки.Получить("РежимСравненияИзделийИМатериалов");
	ПометитьКомандуИзмененияРежимаСравнения(Элементы, РежимСравненияИзделийИМатериалов);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьСравнение(Команда)
	
	ВыполнитьСравнениеНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьТолькоОтличия(Команда)
	
	ПоказатьТолькоОтличия = НЕ ПоказатьТолькоОтличия;
	Элементы.ФормаПоказатьТолькоОтличия.Пометка = ПоказатьТолькоОтличия;
	
	ВывестиОтчетНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьРежимСравненияВЕдиницахХранения(Команда)
	
	РежимСравненияИзделийИМатериалов = 0;
	ПометитьКомандуИзмененияРежимаСравнения(Элементы, РежимСравненияИзделийИМатериалов);
	
	ВывестиОтчетНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьРежимСравненияВЕдиницахСпецификаций(Команда)
	
	РежимСравненияИзделийИМатериалов = 1;
	ПометитьКомандуИзмененияРежимаСравнения(Элементы, РежимСравненияИзделийИМатериалов);
	
	ВывестиОтчетНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ВывестиОтчетНаСервере(Знач ХранилищеДанныхСравнения = Неопределено)

	РезультатСравнения.Очистить();
	
	Если ХранилищеДанныхСравнения = Неопределено Тогда
		Если НЕ ЭтоАдресВременногоХранилища(АдресРезультатаСравнения) Тогда
			Возврат;
		КонецЕсли;
		
		ХранилищеДанныхСравнения = ПолучитьИзВременногоХранилища(АдресРезультатаСравнения);
	КонецЕсли; 
	
	ЕстьОтличия = Обработки.СравнениеСпецификаций.ВывестиОтчет(
							РезультатСравнения, 
							ХранилищеДанныхСравнения.СписокСпецификаций, 
							ПоказатьТолькоОтличия, 
							ХранилищеДанныхСравнения.ДанныеСравнения);
							
	Обработки.СравнениеСпецификаций.ЗавершитьВыводОтчета(РезультатСравнения, ЕстьОтличия);

КонецПроцедуры
 
&НаСервере
Процедура ВыполнитьСравнениеНаСервере()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РесурсныеСпецификации.Ссылка,
	|	РесурсныеСпецификации.Представление
	|ИЗ
	|	Справочник.РесурсныеСпецификации КАК РесурсныеСпецификации
	|ГДЕ
	|	РесурсныеСпецификации.Ссылка В ИЕРАРХИИ(&СписокСпецификаций)
	|	И НЕ РесурсныеСпецификации.ЭтоГруппа
	|
	|УПОРЯДОЧИТЬ ПО
	|	РесурсныеСпецификации.Наименование";
	
	Запрос.УстановитьПараметр("СписокСпецификаций", СписокСпецификаций);
	
	ТаблицаСпецификаций = Запрос.Выполнить().Выгрузить(); // см. ПроизводствоСервер.СлужебнаяСтруктураТаблицыЗначений
	
	// Нужно сформировать список в нужном порядке
	СписокСпецификацийДляСравнения = Новый СписокЗначений;
	Для каждого ВыбраннаяСпецификация Из СписокСпецификаций Цикл
		
		Если НЕ ЗначениеЗаполнено(ВыбраннаяСпецификация.Значение) Тогда
			Продолжить;
		КонецЕсли; 
		
		Для каждого СтрокаСпецификация Из ТаблицаСпецификаций Цикл
			Если ВыбраннаяСпецификация.Значение.ЭтоГруппа Тогда
				Если СтрокаСпецификация.Ссылка.ПринадлежитЭлементу(ВыбраннаяСпецификация.Значение) Тогда
					СписокСпецификацийДляСравнения.Добавить(СтрокаСпецификация.Ссылка, СтрокаСпецификация.Представление);
				КонецЕсли;
			Иначе
				Если СтрокаСпецификация.Ссылка = ВыбраннаяСпецификация.Значение Тогда
					СписокСпецификацийДляСравнения.Добавить(СтрокаСпецификация.Ссылка, СтрокаСпецификация.Представление);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла; 
		
	КонецЦикла;
	
	ДанныеСравнения = Обработки.СравнениеСпецификаций.ВыполнитьСравнениеСпецификаций(
						СписокСпецификацийДляСравнения, РежимСравненияИзделийИМатериалов);
	
	ХранилищеДанныхСравнения = Новый Структура;
	ХранилищеДанныхСравнения.Вставить("ДанныеСравнения", ДанныеСравнения);
	ХранилищеДанныхСравнения.Вставить("СписокСпецификаций", СписокСпецификацийДляСравнения);
	
	АдресРезультатаСравнения = ПоместитьВоВременноеХранилище(ХранилищеДанныхСравнения, УникальныйИдентификатор);
	
	ВывестиОтчетНаСервере(ХранилищеДанныхСравнения);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПометитьКомандуИзмененияРежимаСравнения(Элементы, РежимСравненияИзделийИМатериалов)
	
	РежимСравненияВЕдиницахХранения = РежимСравненияИзделийИМатериалов = 0;
	Элементы.ФормаУстановитьРежимСравненияВЕдиницахХранения.Пометка     = РежимСравненияВЕдиницахХранения;
	Элементы.ФормаУстановитьРежимСравненияВЕдиницахСпецификаций.Пометка = Не РежимСравненияВЕдиницахХранения;
	
КонецПроцедуры

#КонецОбласти
