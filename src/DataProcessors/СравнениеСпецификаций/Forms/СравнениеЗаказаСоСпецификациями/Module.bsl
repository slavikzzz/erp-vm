
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	МассивСтрокЭтапы            = Новый Массив;
	МассивСтрокМатериалыИУслуги = Новый Массив;
	МассивСтрокВыходныеИзделия  = Новый Массив;
	МассивСтрокВозвратныеОтходы = Новый Массив;
	МассивСтрокВидыРЦ           = Новый Массив;
	МассивНоменклатуры          = Новый Массив;
	Для каждого ЭлКоллекции Из Параметры.ДанныеЗаказа Цикл
		
		// Ищем этапы которые входят в спецификацию
		СтруктураПоискаЭтапы = Новый Структура("КлючСвязиПродукция", ЭлКоллекции.Значение.КлючСвязиПродукция);
		
		СтрокиЭтапы = Параметры.Этапы.НайтиСтроки(СтруктураПоискаЭтапы);
		Для каждого НайденнаяСтрока Из СтрокиЭтапы Цикл
			МассивСтрокЭтапы.Добавить(НайденнаяСтрока);
			
			СтруктураПоискаДанныеЭтапа = Новый Структура("КлючСвязиЭтапы", НайденнаяСтрока.КлючСвязи);
			
			СтрокиМатериалыИУслуги = Параметры.МатериалыИУслуги.НайтиСтроки(СтруктураПоискаДанныеЭтапа);
			Для каждого НайденнаяСтрока Из СтрокиМатериалыИУслуги Цикл
				МассивСтрокМатериалыИУслуги.Добавить(НайденнаяСтрока);
				МассивНоменклатуры.Добавить(НайденнаяСтрока.Номенклатура);
			КонецЦикла;
			
			СтрокиВыходныеИзделия = Параметры.ВыходныеИзделия.НайтиСтроки(СтруктураПоискаДанныеЭтапа);
			Для каждого НайденнаяСтрока Из СтрокиВыходныеИзделия Цикл
				МассивСтрокВыходныеИзделия.Добавить(НайденнаяСтрока);
				МассивНоменклатуры.Добавить(НайденнаяСтрока.Номенклатура);
			КонецЦикла;
			
			СтрокиВозвратныеОтходы = Параметры.ВозвратныеОтходы.НайтиСтроки(СтруктураПоискаДанныеЭтапа);
			Для каждого НайденнаяСтрока Из СтрокиВозвратныеОтходы Цикл
				МассивСтрокВозвратныеОтходы.Добавить(НайденнаяСтрока);
				МассивНоменклатуры.Добавить(НайденнаяСтрока.Номенклатура);
			КонецЦикла;
			
			СтрокиВидыРЦ = Параметры.ВидыРабочихЦентров.НайтиСтроки(СтруктураПоискаДанныеЭтапа);
			Для каждого НайденнаяСтрока Из СтрокиВидыРЦ Цикл
				МассивСтрокВидыРЦ.Добавить(НайденнаяСтрока);
			КонецЦикла;
		КонецЦикла;
		
	КонецЦикла;
	
	
	// Этапы
	
	//
	// Ключ этапа это строка формата ИдентификаторЭтапа_КодСпецификации
	//
	
	Этапы = Параметры.Этапы.Выгрузить(МассивСтрокЭтапы); // ТаблицаЗначений
	Этапы.Колонки.Добавить("КлючЭтапа",                      Новый ОписаниеТипов("Строка"));
	Этапы.Колонки.Добавить("СпецификацияПредставление",      Новый ОписаниеТипов("Строка"));
	Этапы.Колонки.Добавить("МаршрутнаяКартаПредставление",   Новый ОписаниеТипов("Строка"));
	Этапы.Колонки.Добавить("ПодразделениеПредставление",     Новый ОписаниеТипов("Строка"));
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Этапы.Этап КАК Этап
	|ПОМЕСТИТЬ ВТЭтапы
	|ИЗ
	|	&Этапы КАК Этапы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТЭтапы.Этап КАК Этап,
	|	ВТЭтапы.Этап.Владелец.Представление КАК СпецификацияПредставление,
	|	ВТЭтапы.Этап.Владелец.Код КАК КодСпецификации
	|ИЗ
	|	ВТЭтапы КАК ВТЭтапы");
	
	Запрос.УстановитьПараметр("Этапы", Этапы);
	ДанныеЭтапов = Запрос.Выполнить().Выбрать();
	СтруктураОтбора = Новый Структура("Этап");
	
	Для каждого ЭлКоллекции Из Этапы Цикл
		
		СтруктураОтбора.Этап = ЭлКоллекции.Этап;
		ДанныеЭтапов.НайтиСледующий(СтруктураОтбора);
		
		ЭлКоллекции.КлючЭтапа                    = Строка(ЭлКоллекции.Этап.УникальныйИдентификатор()) + "_" + ДанныеЭтапов.КодСпецификации;
		ЭлКоллекции.СпецификацияПредставление    = ДанныеЭтапов.СпецификацияПредставление;
		ЭлКоллекции.МаршрутнаяКартаПредставление = Строка(ЭлКоллекции.МаршрутнаяКарта);
		ЭлКоллекции.ПодразделениеПредставление   = Строка(ЭлКоллекции.Подразделение);
		
		ДанныеЭтапов.Сбросить();
		
	КонецЦикла;
	
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивНоменклатуры, "ВидНоменклатуры,ЕдиницаИзмерения");
	
	// МатериалыИУслуги
	МатериалыИУслуги = Параметры.МатериалыИУслуги.Выгрузить(МассивСтрокМатериалыИУслуги); // ТаблицаЗначений
	МатериалыИУслуги.Колонки.Добавить("НоменклатураПредставление",   Новый ОписаниеТипов("Строка"));
	МатериалыИУслуги.Колонки.Добавить("ХарактеристикаПредставление", Новый ОписаниеТипов("Строка"));
	МатериалыИУслуги.Колонки.Добавить("ЕдИзм",                       Новый ОписаниеТипов("Строка"));
	Для каждого ЭлКоллекции Из МатериалыИУслуги Цикл
		Если НЕ ЗначениеЗаполнено(ЭлКоллекции.Номенклатура) Тогда
			Продолжить;
		КонецЕсли;
		ЭлКоллекции.НоменклатураПредставление   = Строка(ЭлКоллекции.Номенклатура);
		ЭлКоллекции.ХарактеристикаПредставление = Строка(ЭлКоллекции.Характеристика);
		Если ЗначениеЗаполнено(ЭлКоллекции.Упаковка) Тогда
			ЭлКоллекции.ЕдИзм = ЭлКоллекции.Упаковка;
		Иначе
			ЭлКоллекции.ЕдИзм = ЗначенияРеквизитов[ЭлКоллекции.Номенклатура].ЕдиницаИзмерения;
		КонецЕсли; 
	КонецЦикла; 
	
	// ВыходныеИзделия
	ВыходныеИзделия = Параметры.ВыходныеИзделия.Выгрузить(МассивСтрокВыходныеИзделия); // ТаблицаЗначений
	ВыходныеИзделия.Колонки.Добавить("НоменклатураПредставление",   Новый ОписаниеТипов("Строка"));
	ВыходныеИзделия.Колонки.Добавить("ХарактеристикаПредставление", Новый ОписаниеТипов("Строка"));
	ВыходныеИзделия.Колонки.Добавить("ЕдИзм",                       Новый ОписаниеТипов("Строка"));
	ВыходныеИзделия.Колонки.Добавить("ВидНоменклатурыПредставление",Новый ОписаниеТипов("Строка"));
	ВыходныеИзделия.Колонки.Добавить("ВидНоменклатуры",             Новый ОписаниеТипов("СправочникСсылка.ВидыНоменклатуры"));
	Для каждого ЭлКоллекции Из ВыходныеИзделия Цикл
		Если НЕ ЗначениеЗаполнено(ЭлКоллекции.Номенклатура) Тогда
			Продолжить;
		КонецЕсли;
		ЭлКоллекции.ВидНоменклатуры              = ЗначенияРеквизитов[ЭлКоллекции.Номенклатура].ВидНоменклатуры;
		ЭлКоллекции.НоменклатураПредставление    = Строка(ЭлКоллекции.Номенклатура);
		ЭлКоллекции.ХарактеристикаПредставление  = Строка(ЭлКоллекции.Характеристика);
		ЭлКоллекции.ВидНоменклатурыПредставление = Строка(ЭлКоллекции.ВидНоменклатуры);
		Если ЗначениеЗаполнено(ЭлКоллекции.Упаковка) Тогда
			ЭлКоллекции.ЕдИзм = ЭлКоллекции.Упаковка;
		Иначе
			ЭлКоллекции.ЕдИзм = ЗначенияРеквизитов[ЭлКоллекции.Номенклатура].ЕдиницаИзмерения;
		КонецЕсли;
	КонецЦикла;
	
	// ВозвратныеОтходы
	ВозвратныеОтходы = Параметры.ВозвратныеОтходы.Выгрузить(МассивСтрокВозвратныеОтходы); // ТаблицаЗначений
	ВозвратныеОтходы.Колонки.Добавить("НоменклатураПредставление",   Новый ОписаниеТипов("Строка"));
	ВозвратныеОтходы.Колонки.Добавить("ХарактеристикаПредставление", Новый ОписаниеТипов("Строка"));
	ВозвратныеОтходы.Колонки.Добавить("ЕдИзм",                       Новый ОписаниеТипов("Строка"));
	Для каждого ЭлКоллекции Из ВозвратныеОтходы Цикл
		Если НЕ ЗначениеЗаполнено(ЭлКоллекции.Номенклатура) Тогда
			Продолжить;
		КонецЕсли;
		ЭлКоллекции.НоменклатураПредставление   = Строка(ЭлКоллекции.Номенклатура);
		ЭлКоллекции.ХарактеристикаПредставление = Строка(ЭлКоллекции.Характеристика);
		Если ЗначениеЗаполнено(ЭлКоллекции.Упаковка) Тогда
			ЭлКоллекции.ЕдИзм = ЭлКоллекции.Упаковка;
		Иначе
			ЭлКоллекции.ЕдИзм = ЗначенияРеквизитов[ЭлКоллекции.Номенклатура].ЕдиницаИзмерения;
		КонецЕсли;
	КонецЦикла;
	
	// ВидыРабочихЦентров
	ВидыРабочихЦентров = Параметры.ВидыРабочихЦентров.Выгрузить(МассивСтрокВидыРЦ); // ТаблицаЗначений
	ВидыРабочихЦентров.Колонки.Добавить("ВидРабочегоЦентраПредставление", Новый ОписаниеТипов("Строка"));
	Для каждого ЭлКоллекции Из ВидыРабочихЦентров Цикл
		ЭлКоллекции.ВидРабочегоЦентраПредставление = Строка(ЭлКоллекции.ВидРабочегоЦентра);
	КонецЦикла; 
	
	// Получим строки заказа в нужном порядке
	СписокСтрокЗаказа = Новый СписокЗначений;
	Для каждого ЭлКоллекции Из Параметры.ДанныеЗаказа Цикл
		ЗначениеЭлемента = ЭлКоллекции.Значение; // см. ПроизводствоСервер.СлужебнаяСтруктураСтруктуры
		СписокСтрокЗаказа.Добавить(ЗначениеЭлемента.НомерСтроки);
	КонецЦикла;
	СписокСтрокЗаказа.СортироватьПоЗначению();
	
	ЕстьОтличия = Ложь;
	
	// Для каждой строки заказа выведем отчет о сравнении
	ЗаголовокОтчета = НСтр("ru = 'Сравнение заказа со спецификациями';
							|en = 'Compare order to bill of materials'");
	Для каждого ЭлКоллекции Из СписокСтрокЗаказа Цикл
		ДанныеЗаказа = Параметры.ДанныеЗаказа.Получить(ЭлКоллекции.Значение); // см. ПроизводствоСервер.СлужебнаяСтруктураСтруктуры
		
		СписокСпецификаций = Новый СписокЗначений;
		СписокСпецификаций.Добавить(ДанныеЗаказа.Спецификация, НСтр("ru = 'По спецификации';
																	|en = 'By bill of materials'"));
		СписокСпецификаций.Добавить("По заказу",               НСтр("ru = 'По заказу';
																	|en = 'Against order'"));
		
		ДанныеСравнения = Обработки.СравнениеСпецификаций.ВыполнитьСравнениеЗаказаСоСпецификациями(	
																ДанныеЗаказа, 
																Этапы,
																МатериалыИУслуги,
																ВыходныеИзделия,
																ВозвратныеОтходы,
																ВидыРабочихЦентров);
																
		СтрокаЗаказа = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Строка №%1';
								|en = 'Line No. %1'"),
							ДанныеЗаказа.НомерСтроки);
							
		ЕстьОтличияПоСтрокеЗаказа = Обработки.СравнениеСпецификаций.ВывестиОтчет(
									РезультатСравнения, 
									СписокСпецификаций, 
									Истина, 
									ДанныеСравнения, 
									ЗаголовокОтчета, 
									СтрокаЗаказа);
									
		ЕстьОтличия = ЕстьОтличия ИЛИ ЕстьОтличияПоСтрокеЗаказа;
		
		ЗаголовокОтчета = Неопределено;
	КонецЦикла;
	
	Обработки.СравнениеСпецификаций.ЗавершитьВыводОтчета(РезультатСравнения, ЕстьОтличия);
	
КонецПроцедуры

#КонецОбласти
