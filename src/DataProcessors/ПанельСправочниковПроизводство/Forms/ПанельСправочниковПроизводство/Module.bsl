
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	УправлениеЭлементами();
	
КонецПроцедуры 

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_НаборКонстант" Тогда
		// Изменены настройки программы в панелях администрирования
		УправлениеЭлементами();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьСтруктуруПредприятия(Команда)
	
	ПараметрыФормы = Новый Структура("ТолькоПроизводственныеПодразделения", Истина);
	ОткрытьФорму("Справочник.СтруктураПредприятия.ФормаСписка", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКалендари(Команда)
	
	ОткрытьФорму("Справочник.Календари.ФормаСписка", , ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОчередьПроизводственныхПодразделений(Команда)
	
	ОткрытьФорму("Справочник.СтруктураПредприятия.Форма.НастройкаПриоритетовПроизводственныхПодразделений", , ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПричиныЗадержкиВыполненияЭтаповПроизводства(Команда)
	
	//++ НЕ УТКА
	ОткрытьФорму("Справочник.ПричиныЗадержкиВыполненияЭтаповПроизводства.ФормаСписка", , ЭтаФорма);
	//-- НЕ УТКА
	Возврат; // в КА2 не используется
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПараметрыМежоперационныхПереходов(Команда)
	
	//++ НЕ УТКА
	ОткрытьФорму("РегистрСведений.ПараметрыМежоперационныхПереходов.ФормаСписка", , ЭтаФорма);
	//-- НЕ УТКА
	Возврат; // в КА2 не используется
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПричиныОтменыПроизводства(Команда)
	
	//++ НЕ УТКА
	ОткрытьФорму("Справочник.ПричиныОтменыПроизводства.ФормаСписка", , ЭтаФорма);
	//-- НЕ УТКА
	Возврат; // в КА2 не используется
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьМоделиПооперационногоПланирования(Команда)
	
	//++ НЕ УТКА
	ОткрытьФорму("Справочник.МоделиПооперационногоПланирования.ФормаСписка", , ЭтаФорма);
	//-- НЕ УТКА
	Возврат; // в КА2 не используется
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСценарииПооперационногоПланирования(Команда)
	
	//++ НЕ УТКА
	ОткрытьФорму("Справочник.СценарииПооперационногоПланирования.ФормаСписка", , ЭтаФорма);
	//-- НЕ УТКА
	Возврат; // в КА2 не используется
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УправлениеЭлементами()
	
	УправлениеПредприятием = ПолучитьФункциональнуюОпцию("УправлениеПредприятием");
	
	#Область ПроизводственнаяСтруктура
	
	//
	ПравоДоступаПодразделения = ПравоДоступа("Просмотр", Метаданные.Справочники.СтруктураПредприятия);
	Элементы.ГруппаСтруктураПредприятия.Видимость = ПравоДоступаПодразделения;
	
	//
	ПравоДоступаРабочиеЦентры  = Ложь;
//++ НЕ УТКА
	ПравоДоступаРабочиеЦентры  = ПравоДоступа("Просмотр", Метаданные.Справочники.РабочиеЦентры);
//-- НЕ УТКА
	Элементы.ГруппаСтруктураРабочихЦентров.Видимость = ПравоДоступаРабочиеЦентры И УправлениеПредприятием;
	
	//
	ПравоДоступаКалендари = ПравоДоступа("Просмотр", Метаданные.Справочники.Календари);
	Элементы.ГруппаКалендари.Видимость = ПравоДоступаКалендари;
	
	// Заголовок группы
	Элементы.ГруппаПроизводственнаяСтруктура.Видимость =
		ПравоДоступаПодразделения
		ИЛИ ПравоДоступаРабочиеЦентры
		ИЛИ ПравоДоступаКалендари;
	#КонецОбласти
	
	#Область МежцеховоеУправление
	
	Элементы.ОткрытьНастройкуОчередиПроизводственныхПодразделений.Видимость = УправлениеПредприятием
		И ПравоДоступа("Просмотр", Метаданные.Справочники.СтруктураПредприятия);
		
	Элементы.ГруппаПричиныЗадержкиЭтаповПроизводства.Видимость = УправлениеПредприятием
	//++ НЕ УТКА
		И ПравоДоступа("Просмотр", Метаданные.Справочники.ПричиныЗадержкиВыполненияЭтаповПроизводства)
	//-- НЕ УТКА
		;
	
	Элементы.ГруппаПричиныОтменыПроизводства.Видимость = УправлениеПредприятием
	//++ НЕ УТКА
		И ПравоДоступа("Просмотр", Метаданные.Справочники.ПричиныОтменыПроизводства)
	//-- НЕ УТКА
		;
	
	#КонецОбласти
	
	#Область ВнутрицеховоеУправление
//++ НЕ УТКА
	ИспользуетсяПооперационноеПланирование = ПолучитьФункциональнуюОпцию("ИспользоватьПооперационноеПланирование")
		//++ Устарело_Производство21 
		ИЛИ ПроизводствоСервер.ИспользуетсяПроизводство21()
		//-- Устарело_Производство21
		;
	
	//
	ПравоПараметрыМежоперационныхПереходов = ИспользуетсяПооперационноеПланирование
		И ПравоДоступа("Просмотр", Метаданные.РегистрыСведений.ПараметрыМежоперационныхПереходов);
	Элементы.ГруппаПараметрыМежоперационныхПереходов.Видимость = ПравоПараметрыМежоперационныхПереходов;
	
	//
	ПравоМоделиПооперационногоПланирования = ИспользуетсяПооперационноеПланирование
		И ПравоДоступа("Просмотр", Метаданные.Справочники.МоделиПооперационногоПланирования);
	Элементы.ГруппаМоделиПооперационногоПланирования.Видимость = ПравоМоделиПооперационногоПланирования;
	
	//
	ПравоСценарииПооперационногоПланирования = ИспользуетсяПооперационноеПланирование
		И ПравоДоступа("Просмотр", Метаданные.Справочники.СценарииПооперационногоПланирования);
	Элементы.ГруппаСценарииПооперационногоПланирования.Видимость = ПравоСценарииПооперационногоПланирования;
//-- НЕ УТКА
	
	Элементы.ГруппаВнутрицеховоеУправление.Видимость = 
		УправлениеПредприятием
//++ НЕ УТКА
		И (ПравоПараметрыМежоперационныхПереходов
			ИЛИ ПравоМоделиПооперационногоПланирования
			ИЛИ ПравоСценарииПооперационногоПланирования)
//-- НЕ УТКА
			;
	#КонецОбласти
		
КонецПроцедуры

#КонецОбласти
