#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий	
	
Процедура ПередЗаписью(Отказ)
	
	ПодсистемаСуществует = ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба");
	Если Не ПодсистемаСуществует Тогда
		Если Значение Тогда
			ВызватьИсключение НСтр("ru = 'Нельзя установить значение ИспользоватьВоеннуюСлужбу';
									|en = 'You cannot set value ИспользоватьВоеннуюСлужбу'");
		КонецЕсли;
	КонецЕсли;
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ПодсистемаСуществует Тогда
		ИспользоватьМуниципальнуюСлужбу = Константы.ИспользоватьМуниципальнуюСлужбу.Получить();
		Если Значение И ИспользоватьМуниципальнуюСлужбу Тогда
			ВызватьИсключение НСтр("ru = 'В программе уже ведется расчет денежного содержания Муниципальных служащих, не допускается использовать одновременно расчет денежного довольствия военнослужащих и денежного содержания муниципальных служащих';
									|en = 'Monetary pay of Municipal employees is already being calculated in the application, you cannot use calculation of allowance of military personnel and monetary pay of Public service employees simultaneously'");	
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	// Подключаемые Характеристики
	ИсточникиХарактеристик = Новый Массив;
	ИсточникиХарактеристик.Добавить("СтрокиОтчетностиРасходовИЧисленностиРаботниковГосударственныхОрганов");
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба") Тогда
		
		ИсточникиХарактеристик.Добавить("СвойстваДолжностейГосударственнойСлужбы");
		ИсточникиХарактеристик.Добавить("СвойстваНачисленийГосударственныхСлужащих");
		
		Если Значение Тогда
			
			МодульГосударственнаяСлужба = ОбщегоНазначения.ОбщийМодуль("ГосударственнаяСлужба");
			МодульГосударственнаяСлужба.ЗаполнитьДополнительныеСвойстваНачисленийРКиСН();
			
			Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ВыплатыБывшимВоеннослужащим") Тогда
				МодульВыплатыБывшимВоеннослужащим = ОбщегоНазначения.ОбщийМодуль("ВыплатыБывшимВоеннослужащим");
				МодульВыплатыБывшимВоеннослужащим.ДобавитьВидПрочегоДоходаДляБывшихВоеннослужащих();
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ПодключаемыеХарактеристикиЗарплатаКадры.ОбновитьНаборыПодключаемыхХарактеристик(Значение, ИсточникиХарактеристик);
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли