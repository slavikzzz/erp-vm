
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	//++ НЕ УТКА

	// При отключении опции, если включено производство 2.1, а текущий вариант обособления в производстве - по этапу или заказу.
	Если Не ЭтотОбъект.Значение И Константы.ИспользоватьУправлениеПроизводством.Получить()
		И (Константы.ВариантОбособленияМатериаловВПроизводстве.Получить() =
			Перечисления.ВариантыОбособленияМатериаловВПроизводстве.ЗаказНаПроизводство
			Или Константы.ВариантОбособленияМатериаловВПроизводстве.Получить() =
				Перечисления.ВариантыОбособленияМатериаловВПроизводстве.ЭтапПроизводства) Тогда
		
		Константы.ВариантОбособленияМатериаловВПроизводстве.Установить(Перечисления.ВариантыОбособленияМатериаловВПроизводстве.НазначениеПродукции);
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Справочники.СтруктураПредприятия.ЗаполнитьИспользуемыеМетодикиУправления();
	//-- НЕ УТКА
	
	//++ НЕ УТ
	Если Не ДополнительныеСвойства.Свойство("ИспользоватьПроизводство") Тогда
		
		ЗначениеКонстанты = Значение Или Константы.ИспользоватьУправлениеПроизводством.Получить();
		
		МенеджерЗначения = Константы.ИспользоватьПроизводство.СоздатьМенеджерЗначения();
		МенеджерЗначения.Прочитать();
		Если МенеджерЗначения.Значение <> ЗначениеКонстанты Тогда
			МенеджерЗначения.Значение = ЗначениеКонстанты;
			МенеджерЗначения.ДополнительныеСвойства.Вставить("ИспользоватьУправлениеПроизводством2_2");
			МенеджерЗначения.Записать();
		КонецЕсли;
		
	КонецЕсли;
	
	ПараметрыНабора = УправлениеСвойствами.СтруктураПараметровНабораСвойств();
	ПараметрыНабора.Используется = Константы.ИспользоватьУправлениеПроизводством2_2.Получить();
	
	УправлениеСвойствами.УстановитьПараметрыНабораСвойств("Справочник_ПартииПроизводства", ПараметрыНабора);
	УправлениеСвойствами.УстановитьПараметрыНабораСвойств("Документ_ПроизводствоБезЗаказа", ПараметрыНабора);
	УправлениеСвойствами.УстановитьПараметрыНабораСвойств("Документ_РаспределениеВозвратныхОтходов", ПараметрыНабора);
	//++ НЕ УТКА
	УправлениеСвойствами.УстановитьПараметрыНабораСвойств("Документ_ЗаказНаПроизводство2_2", ПараметрыНабора);
	УправлениеСвойствами.УстановитьПараметрыНабораСвойств("Документ_ЭтапПроизводства2_2", ПараметрыНабора);
	//-- НЕ УТКА

	//-- НЕ УТ
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
