#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура УстановитьИспользованиеОсобыхУсловийТруда(Знач ЗначениеФункциональнойОпции) Экспорт
	
	ИспользоватьОсобыеУсловияТруда = ПолучитьФункциональнуюОпцию("ИспользоватьОсобыеУсловияТруда");
	
	Если ЗначениеФункциональнойОпции Тогда
		Если Не ИспользоватьОсобыеУсловияТруда Тогда
			Константы.ИспользоватьОсобыеУсловияТруда.Установить(Истина);
		КонецЕсли;
	Иначе
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.РабочиеМестаОхраныТруда") Тогда
			МодульРабочиеМестаОхраныТруда = ОбщегоНазначения.ОбщийМодуль("РабочиеМестаОхраныТруда");
			Если МодульРабочиеМестаОхраныТруда.РабочиеМестаОхраныТрудаИспользуются() Тогда
				Если Не ИспользоватьОсобыеУсловияТруда Тогда
					Константы.ИспользоватьОсобыеУсловияТруда.Установить(Истина);
				КонецЕсли;
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		Если ИспользоватьОсобыеУсловияТруда Тогда
			НаборНастроек = РегистрыСведений.НастройкиЗарплатаКадрыРасширенная.СоздатьНаборЗаписей();
			НаборНастроек.Прочитать();
			Для Каждого Настройка Из НаборНастроек Цикл
				Если Настройка.ИспользоватьОсобыеУсловияТруда Тогда
					Возврат;
				КонецЕсли;
			КонецЦикла;
			Константы.ИспользоватьОсобыеУсловияТруда.Установить(Ложь);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли