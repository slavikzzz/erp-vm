#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Константы.ИспользуетсяСборкаРазборкаИСерииНоменклатуры.Установить(
		(Константы.ИспользоватьСборкуРазборку.Получить()
		//++ НЕ УТ
			ИЛИ Константы.ИспользоватьПроизводство.Получить()
			ИЛИ Константы.ИспользоватьПроизводствоНаСтороне2_5.Получить()
		//-- НЕ УТ
			ИЛИ Ложь)
		И Константы.ИспользоватьСерииНоменклатуры.Получить());
	
//++ НЕ УТ
	УстановитьЗначениеСвязанныхКонстант = Не ДополнительныеСвойства.Свойство("ИспользоватьУправлениеПроизводством2_2");
	//++ Устарело_Производство21
	УстановитьЗначениеСвязанныхКонстант = УстановитьЗначениеСвязанныхКонстант
										  И Не ДополнительныеСвойства.Свойство("ИспользоватьУправлениеПроизводством");
	//-- Устарело_Производство21
	
	Если УстановитьЗначениеСвязанныхКонстант Тогда
		
		//++ Устарело_Производство21
		ЗначениеКонстанты = Значение И НастройкиСистемыЛокализация.Производство21ИспользовалосьРанее();
		
		МенеджерЗначения = Константы.ИспользоватьУправлениеПроизводством.СоздатьМенеджерЗначения();
		МенеджерЗначения.Прочитать();
		Если МенеджерЗначения.Значение <> ЗначениеКонстанты Тогда
			МенеджерЗначения.Значение = ЗначениеКонстанты;
			МенеджерЗначения.ДополнительныеСвойства.Вставить("ИспользоватьПроизводство");
			МенеджерЗначения.Записать();
		КонецЕсли;
		//-- Устарело_Производство21
		
		ЗначениеКонстанты = Значение;
		
		МенеджерЗначения = Константы.ИспользоватьУправлениеПроизводством2_2.СоздатьМенеджерЗначения();
		МенеджерЗначения.Прочитать();
		Если МенеджерЗначения.Значение <> ЗначениеКонстанты Тогда
			МенеджерЗначения.Значение = ЗначениеКонстанты;
			МенеджерЗначения.ДополнительныеСвойства.Вставить("ИспользоватьПроизводство");
			МенеджерЗначения.Записать();
		КонецЕсли;
		
	КонецЕсли;
	
	ПроизводствоСервер.УстановитьИспользованиеСвойствСправочников();
//-- НЕ УТ
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли