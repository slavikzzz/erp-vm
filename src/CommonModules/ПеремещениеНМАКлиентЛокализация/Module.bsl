
#Область СлужебныйПрограммныйИнтерфейс

// Параметры:
// 	ИмяЭлемента - Строка - 
// 	Форма - ФормаКлиентскогоПриложения -
//  ДополнительныеПараметры - Структура - 
Процедура ПриИзмененииРеквизита(ИмяЭлемента, Форма, ДополнительныеПараметры = Неопределено) Экспорт

	ТребуетсяВызовСервера = Ложь;
	ПродолжитьИзменениеРеквизита = Истина;
	
	//++ Локализация
	
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;

	Если ИмяЭлемента = Элементы.ХозяйственнаяОперация.Имя Тогда	
		
		ХозяйственнаяОперацияПриИзменении(Форма, ДополнительныеПараметры);
		
	ИначеЕсли ИмяЭлемента = Элементы.ДокументНаОсновании.Имя Тогда	
		
		ДокументНаОснованииПриИзменении(Форма, ПродолжитьИзменениеРеквизита);
		
	ИначеЕсли ИмяЭлемента = Элементы.ОрганизацияСдатчик.Имя Тогда	
		
		ОрганизацияСдатчикПриИзменении(Объект, ПродолжитьИзменениеРеквизита);
		                                  
	КонецЕсли; 
	
	//-- Локализация
	
	Если ПродолжитьИзменениеРеквизита Тогда
		
		ОбщегоНазначенияУТКлиент.ПродолжитьИзменениеРеквизита(
			Форма, 
			ИмяЭлемента, 
			ТребуетсяВызовСервера,
			ДополнительныеПараметры);
			
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//++ Локализация

Процедура ХозяйственнаяОперацияПриИзменении(Форма, ДополнительныеПараметры)
	
	Объект = Форма.Объект;
	
	Если Объект.ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ПеремещениеНМАвПодразделениеВыделенноеНаБаланс") Тогда
		Если Объект.Организация = Объект.ОрганизацияПолучатель Тогда
			Объект.ОрганизацияПолучатель = Неопределено;
		КонецЕсли;
		Если ЗначениеЗаполнено(Объект.Организация)
			И НЕ ВнеоборотныеАктивыВызовСервера.ОрганизацияВзаимосвязанаСДругимиОрганизациями(Объект.Организация) Тогда
			Объект.Организация = Неопределено;
		КонецЕсли; 
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Выполнить_НастроитьЗависимыеЭлементыФормы", "ХозяйственнаяОперация");
	
КонецПроцедуры

Процедура ДокументНаОснованииПриИзменении(Форма, ПродолжитьИзменениеРеквизита)
	
	Объект = Форма.Объект;
	
	Если Объект.ДокументНаОсновании Тогда
		
		ОтборСписка = Новый Структура;
		ОтборСписка.Вставить("Проведен", Истина);
		
		Если Объект.ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ПеремещениеНМАвПодразделениеВыделенноеНаБаланс") Тогда
			Если ЗначениеЗаполнено(Объект.ОрганизацияПолучатель) Тогда
				ОтборСписка.Вставить("Организация", Объект.ОрганизацияПолучатель);
			КонецЕсли;
		Иначе
			Если ЗначениеЗаполнено(Объект.Организация) Тогда
				ОтборСписка.Вставить("Организация", Объект.Организация);
			КонецЕсли;
		КонецЕсли; 
		
		Если ЗначениеЗаполнено(Объект.ПодразделениеПолучатель) Тогда
			ОтборСписка.Вставить("Подразделение", Объект.ПодразделениеПолучатель);
		КонецЕсли;
		
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Форма", Форма);
		ОписаниеОповещения = Новый ОписаниеОповещения("ДокументНаОснованииПриИзмененииЗавершение", ЭтотОбъект, ПараметрыОповещения);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Отбор", ОтборСписка);
		ОткрытьФорму("Документ.ИнвентаризацияНМА.ФормаВыбора", ПараметрыФормы,,,,, ОписаниеОповещения);
		
	Иначе
		
		Объект.ДокументОснование = Неопределено;
		
	КонецЕсли; 
	
	ПродолжитьИзменениеРеквизита = Ложь;
	
КонецПроцедуры

Процедура ДокументНаОснованииПриИзмененииЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт

	Форма = ДополнительныеПараметры.Форма;
	Объект = Форма.Объект;
	
	Если ЗначениеЗаполнено(РезультатЗакрытия) Тогда
		Объект.ДокументОснование = РезультатЗакрытия;
	Иначе
		Объект.ДокументНаОсновании = Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		ТекстВопроса = НСтр("ru = 'Заполнить документ по инвентаризации?';
							|en = 'Fill in the stocktaking document?'");
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Форма", Форма);
		ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьПоДаннымОснованияЗавершение", ЭтотОбъект, ПараметрыОповещения);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет); 
	КонецЕсли; 
	
КонецПроцедуры

Процедура ЗаполнитьПоДаннымОснованияЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		ОбщегоНазначенияУТКлиент.ПродолжитьИзменениеРеквизита(
			ДополнительныеПараметры.Форма, 
			"ДокументНаОсновании", 
			Истина);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОрганизацияСдатчикПриИзменении(Объект, ПродолжитьИзменениеРеквизита)
	
	ПродолжитьИзменениеРеквизита = Ложь;
	
	Если Объект.ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ПеремещениеНМАвПодразделениеВыделенноеНаБаланс")
		И ЗначениеЗаполнено(Объект.Организация)
		И ЗначениеЗаполнено(Объект.ОрганизацияПолучатель) Тогда
		Если НЕ ВнеоборотныеАктивыВызовСервера.ОрганизацииВзаимосвязаныПоОрганизационнойСтруктуре(Объект.ОрганизацияПолучатель, Объект.Организация) Тогда
			Объект.ОрганизацияПолучатель = ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

//-- Локализация

#КонецОбласти

