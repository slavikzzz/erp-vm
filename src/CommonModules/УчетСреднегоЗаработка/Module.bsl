////////////////////////////////////////////////////////////////////////////////
// Подсистема «Учет среднего заработка».
// 
// Серверные процедуры и функции.
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает сведения о начислениях, отработанном времени и коэффициентах индексации 
// для расчета среднего заработка.
//      	 
// Параметры:
//  ДанныеСотрудника - Структура - см. функцию ИсходныеДанныеСотрудникаДляРасчетаОбщегоСреднегоЗаработка.
//	ОтборМесяцев - Массив - (необязательный) если определен, данные будут получены только за указанные месяцы.
//	ИсключаемыйРегистратор - (необязательный) документ, движения которого будут исключены из полученных данных.
//	УчитыватьКорректировки - Булево - учет ручных корректировок при получении данных для расчета среднего.
//
// Возвращаемое значение:
//  Структура - со свойствами:
//     * ДанныеОНачислениях - ТаблицаЗначений - содержит колонки:
//        ** Сотрудник - СправочникСсылка.Сотрудники
//        ** Период - Дата
//        ** ПорядокРасчета - ПеречислениеСсылка.ПорядокРасчетаСреднегоЗаработкаОбщий
//        ** СоставнаяЧасть - ПеречислениеСсылка.УчетНачисленийВСреднемЗаработкеОбщий
//        ** Индексируется - Булево
//        ** Сумма - Число
//        ** Год - Число
//        ** ДатаНачалаБазовогоПериода - Дата
//        ** КоличествоМесяцев - Число
//        ** СтатьяФинансирования - СправочникСсылка.СтатьиФинансированияЗарплата
//        ** СпособОтраженияЗарплатыВБухучете - СправочникСсылка.СпособыОтраженияЗарплатыВБухУчете
//        ** СтатьяРасходов - СправочникСсылка.СтатьиРасходовЗарплата
//        ** ОблагаетсяЕНВД - Булево
//        ** Источник - ПеречислениеСсылка.ИсточникиДанныхДляРасчетаСреднегоЗаработка
//     * ДанныеОВремени - ТаблицаЗначений - содержит колонки:
//        ** Сотрудник - СправочникСсылка.Сотрудники
//        ** Период - Дата
//        ** ПорядокРасчета - ПеречислениеСсылка.ПорядокРасчетаСреднегоЗаработкаОбщий
//        ** ОтработаноДней - Число
//        ** ОтработаноЧасов - Число
//        ** ОтработаноДнейПятидневка - Число
//        ** ОтработаноЧасовПятидневка - Число
//        ** ОтработаноДнейКалендарных - Число
//        ** ОтработаноДнейШестидневка - Число
//        ** НормаДнейПроизводственныйКалендарь - Число
//        ** НормаЧасовПроизводственныйКалендарь - Число
//        ** Источник - ПеречислениеСсылка.ИсточникиДанныхДляРасчетаСреднегоЗаработка
//     * ДанныеОбИндексации - ТаблицаЗначений - содержит колонки:
//        ** Сотрудник - СправочникСсылка.Сотрудники
//        ** Период - Дата
//        ** КоэффициентИндексации - Число 
//
Функция ДанныеДляРасчетаОбщегоСреднегоЗаработкаСотрудника(
	ДанныеСотрудника, 
	ОтборМесяцев = Неопределено, 
	ИсключаемыйРегистратор = Неопределено, 
	УчитыватьКорректировки = Истина) Экспорт
	
	Возврат ДанныеДляРасчетаОбщегоСреднегоЗаработкаСотрудникаСлужебный(
				ДанныеСотрудника.Сотрудник, 
				ДанныеСотрудника.ДатаНачалаСобытия,	
				ДанныеСотрудника.НачалоПериодаРасчетаСреднего, 
				ДанныеСотрудника.ОкончаниеПериодаРасчетаСреднего,
				ДанныеСотрудника.ПорядокРасчета, 
				ОтборМесяцев, 
				ИсключаемыйРегистратор,
				УчитыватьКорректировки);
		
КонецФункции

// Возвращает структуру параметров для ДанныеДляРасчетаОбщегоСреднегоЗаработкаСотрудника.
//
// Возвращаемое значение:
//  Структура - со свойствами:
//     * Сотрудник - СправочникСсылка.Сотрудники - сотрудник организации.
//     * ПорядокРасчета - ПеречислениеСсылка.ПорядокРасчетаСреднегоЗаработкаОбщий - правила расчета общего среднего заработка.
//     * ДатаНачалаСобытия - Дата - дата начала отпуска, командировки и т. д.
//     * НачалоПериодаРасчетаСреднего - Дата - начало периода расчета среднего заработка.
//     * ОкончаниеПериодаРасчетаСреднего - Дата - окончание периода расчета среднего заработка.
//
Функция ИсходныеДанныеСотрудникаДляРасчетаОбщегоСреднегоЗаработка() Экспорт
	
	ДанныеСотрудника = Новый Структура;
	ДанныеСотрудника.Вставить("Сотрудник", Справочники.Сотрудники.ПустаяСсылка());
	ДанныеСотрудника.Вставить("ПорядокРасчета", Перечисления.ПорядокРасчетаСреднегоЗаработкаОбщий.ПустаяСсылка());
	ДанныеСотрудника.Вставить("ДатаНачалаСобытия", Дата(1, 1, 1));
	ДанныеСотрудника.Вставить("НачалоПериодаРасчетаСреднего", Дата(1, 1, 1));
	ДанныеСотрудника.Вставить("ОкончаниеПериодаРасчетаСреднего", Дата(1, 1, 1));
	
	Возврат ДанныеСотрудника;
	
КонецФункции

// Возвращает сведения о начислениях, отработанном времени и коэффициентах индексации 
// для расчета среднего заработка.
//      	 
// Параметры:
//  ДанныеСотрудников - ТаблицаЗначений - см. функцию ИсходныеДанныеСотрудниковДляРасчетаОбщегоСреднегоЗаработка.
//	ОтборМесяцев - Массив - (необязательный) если определен, данные будут получены только за указанные месяцы.
//	ИсключаемыйРегистратор - (необязательный) документ, движения которого будут исключены из полученных данных.
//	УчитыватьКорректировки - Булево - учет ручных корректировок при получении данных для расчета среднего.
//
// Возвращаемое значение:
//  Структура - со свойствами:
//     * ДанныеОНачислениях - ТаблицаЗначений - содержит колонки:
//        ** Сотрудник - СправочникСсылка.Сотрудники
//        ** Период - Дата
//        ** ПорядокРасчета - ПеречислениеСсылка.ПорядокРасчетаСреднегоЗаработкаОбщий
//        ** СоставнаяЧасть - ПеречислениеСсылка.УчетНачисленийВСреднемЗаработкеОбщий
//        ** Индексируется - Булево
//        ** Сумма - Число
//        ** Год - Число
//        ** ДатаНачалаБазовогоПериода - Дата
//        ** КоличествоМесяцев - Число
//        ** СтатьяФинансирования - СправочникСсылка.СтатьиФинансированияЗарплата
//        ** СпособОтраженияЗарплатыВБухучете - СправочникСсылка.СпособыОтраженияЗарплатыВБухУчете
//        ** СтатьяРасходов - СправочникСсылка.СтатьиРасходовЗарплата
//        ** ОблагаетсяЕНВД - Булево
//        ** Источник - ПеречислениеСсылка.ИсточникиДанныхДляРасчетаСреднегоЗаработка
//     * ДанныеОВремени - ТаблицаЗначений - содержит колонки:
//        ** Сотрудник - СправочникСсылка.Сотрудники
//        ** Период - Дата
//        ** ПорядокРасчета - ПеречислениеСсылка.ПорядокРасчетаСреднегоЗаработкаОбщий
//        ** ОтработаноДней - Число
//        ** ОтработаноЧасов - Число
//        ** ОтработаноДнейПятидневка - Число
//        ** ОтработаноЧасовПятидневка - Число
//        ** ОтработаноДнейКалендарных - Число
//        ** ОтработаноДнейШестидневка - Число
//        ** НормаДнейПроизводственныйКалендарь - Число
//        ** НормаЧасовПроизводственныйКалендарь - Число
//        ** Источник - ПеречислениеСсылка.ИсточникиДанныхДляРасчетаСреднегоЗаработка
//     * ДанныеОбИндексации - ТаблицаЗначений - содержит колонки:
//        ** Сотрудник - СправочникСсылка.Сотрудники
//        ** Период - Дата
//        ** КоэффициентИндексации - Число 
//
Функция ДанныеДляРасчетаОбщегоСреднегоЗаработкаСотрудников(
	ДанныеСотрудников, 
	ОтборМесяцев = Неопределено, 
	ИсключаемыйРегистратор = Неопределено, 
	УчитыватьКорректировки = Истина) Экспорт
	
	Возврат ДанныеДляРасчетаОбщегоСреднегоЗаработкаСотрудниковСлужебный(ДанныеСотрудников, ОтборМесяцев, ИсключаемыйРегистратор, УчитыватьКорректировки);
	
КонецФункции

// Возвращает пустую таблицу для ДанныеДляРасчетаОбщегоСреднегоЗаработкаСотрудников.
//
// Возвращаемое значение:
//  ТаблицаЗначений - пустая таблица с колонками:
//     * Сотрудник - СправочникСсылка.Сотрудники - сотрудник организации.
//     * ПорядокРасчета - ПеречислениеСсылка.ПорядокРасчетаСреднегоЗаработкаОбщий - правила расчета общего среднего заработка.
//     * ДатаНачалаСобытия - Дата - дата начала отпуска, командировки и т. д.
//     * НачалоПериодаРасчетаСреднего - Дата - начало периода расчета среднего заработка.
//     * ОкончаниеПериодаРасчетаСреднего - Дата - окончание периода расчета среднего заработка.
//
Функция ИсходныеДанныеСотрудниковДляРасчетаОбщегоСреднегоЗаработка() Экспорт
	
	Возврат ПустаяТаблицаИсходныхДанныхРасчетаОбщегоСреднегоЗаработка();
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Предоставляет значение среднего заработка.
//
// Параметры
//	Сотрудник - тип СправочникСсылка.Сотрудники,
//	ДатаНачалаСобытия - тип Дата
//	ДополнительныеПараметры - тип Структура, см. УчетСреднегоЗаработкаКлиентСервер.ДополнительныеПараметрыРасчетаСреднегоЗаработка.
//
// Возвращаемое значение - Число, значение среднедневного или среднечасового заработка.
//
Функция СреднийЗаработок(Сотрудник, ДатаНачалаСобытия, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ДополнительныеПараметры = Неопределено Тогда
		ДополнительныеПараметры = УчетСреднегоЗаработкаКлиентСервер.ДополнительныеПараметрыРасчетаСреднегоЗаработка();
	КонецЕсли;
	
	ДополнительныеПараметры.ДатаНачалаСобытия = ДатаНачалаСобытия;
	
	Если Не ЗначениеЗаполнено(ДополнительныеПараметры.НачалоПериода) 
		Или Не ЗначениеЗаполнено(ДополнительныеПараметры.ОкончаниеПериода) Тогда
		ПериодРасчета = ПериодРасчетаОбщегоСреднегоЗаработкаСотрудника(
			ДатаНачалаСобытия, 
			Сотрудник, 
			ДополнительныеПараметры.Начисление);
		ДополнительныеПараметры.НачалоПериода = ПериодРасчета.ДатаНачала;
		ДополнительныеПараметры.ОкончаниеПериода = ПериодРасчета.ДатаОкончания;
	КонецЕсли;
		
	Если ДополнительныеПараметры.ПорядокРасчета = Неопределено Тогда
		ДополнительныеПараметры.ПорядокРасчета = УчетСреднегоЗаработкаКлиентСервер.ПорядокРасчетаОбщегоСреднегоЗаработка(ДатаНачалаСобытия);
	КонецЕсли;
	
	ДанныеДляРасчета = ДанныеДляРасчетаОбщегоСреднегоЗаработкаСотрудникаСлужебный(
		Сотрудник, 
		ДатаНачалаСобытия, 
		ДополнительныеПараметры.НачалоПериода, 
		ДополнительныеПараметры.ОкончаниеПериода, 
		ДополнительныеПараметры.ПорядокРасчета);
		
	ДополнительныеПараметры.Индексации = ДанныеДляРасчета.ДанныеОбИндексации;
	
	СреднийЗаработок = УчетСреднегоЗаработкаКлиентСервер.СреднийЗаработокОбщий(ДанныеДляРасчета.ДанныеОНачислениях, ДанныеДляРасчета.ДанныеОВремени, ДополнительныеПараметры);
	
	Возврат СреднийЗаработок;
	
КонецФункции

// Процедура выполняет регистрацию данных о начисленных суммах и отработанном времени
//  для использования при расчете среднего заработка.
//
// ВНИМАНИЕ. Перед выполнением метода движения по регистру расчетов Начисления и отработанному времени 
//	(в частности по корректировкам отработанного времени) должны быть записаны.
//
//	Параметры:
//		Движения - коллекция движений документа.
//		Отказ
//		МенеджерВременныхТаблиц - менеджер временных таблиц, содержащий таблицу.
//		ВТНачисления - с полями.
//			Сотрудник
//			ДатаДействия
//			Начисление - ПВР Начисления
//			Сумма
//
Процедура ЗарегистрироватьДанныеСреднегоЗаработка(Движения, Отказ, Начисления, ЗаписыватьДвижения = Ложь) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	СоздатьВТНачисленияПоТаблицеНачисления(МенеджерВременныхТаблиц, Начисления);

	УчетПособийСоциальногоСтрахованияРасширенный.ЗарегистрироватьДанныеСреднегоЗаработкаФСС(Движения, Отказ, МенеджерВременныхТаблиц, ЗаписыватьДвижения);
	Если ОбщегоНазначенияБЗК.ЗначениеСвойства(Движения, "ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий") <> Неопределено Тогда
		ЗарегистрироватьДанныеОбщегоСреднегоЗаработка(Движения, Отказ, МенеджерВременныхТаблиц, ЗаписыватьДвижения);
	КонецЕсли;
	
КонецПроцедуры

// Конструирует структуру параметров для регистрации данных документа в учете среднего заработка.
//
Функция ДополнительныеПараметрыРегистрацииДанныхСреднегоЗаработка(ИменаТаблиц = "Начисления,НачисленияПерерасчет") Экспорт
	
	МассивИмен = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИменаТаблиц);
	
	ДополнительныеПараметры = Новый Структура(
		"МесяцНачисления,
		|Таблицы");
	ДополнительныеПараметры.МесяцНачисления = "Ссылка.МесяцНачисления";
	
	ДополнительныеПараметры.Таблицы = Новый Структура;
	Для Каждого ИмяТаблицы Из МассивИмен Цикл
		ИменаПолей = Новый Структура(
			"ДатаДействия, 
			|Начисление,
			|ПоказательПремирования,
			|НачалоБазовогоПериода, 
			|ОкончаниеБазовогоПериода,
			|ДатаНачала, 
			|ДатаОкончания");
		ИменаПолей.ДатаДействия = "ДатаНачала";
		ИменаПолей.Начисление = "Начисление";
		ИменаПолей.ПоказательПремирования = Неопределено; // Если не переопределен документом, то будет назначен автоматически.
		ИменаПолей.НачалоБазовогоПериода = "ДатаНачала";
		ИменаПолей.ОкончаниеБазовогоПериода = "ДатаОкончания";
		ИменаПолей.ДатаНачала = "ДатаНачала";
		ИменаПолей.ДатаОкончания = "ДатаОкончания";
		ДополнительныеПараметры.Таблицы.Вставить(ИмяТаблицы, ИменаПолей);
	КонецЦикла;
	
	Возврат ДополнительныеПараметры;
	
КонецФункции

// Функция формирует временные таблицы с данными документа начисления
//  для регистрации данных учета среднего заработка.
//
// Параметры:
//	- ДанныеДляПроведения - структура данных, накапливающая данные для проведения.
//	- ДокументСсылка - ссылка на документ, выполняющий начисления.
//	- ТаблицаНачислений - строка с именем (или именами через запятую) 
//			табличных частей документа с начислениями.
//	- ПолеДатыДействия - имя поля, содержащего период регистрации записей.
//	- ПолеГод - имя поля, содержащего год, за который регистрируются записи.
//
Процедура ЗаполнитьТаблицыДляРегистрацииДанныхСреднегоЗаработка(ДанныеДляПроведения, ДокументСсылка, ДополнительныеПараметры = Неопределено, СписокФизическихЛиц = Неопределено) Экспорт

	Если ДополнительныеПараметры = Неопределено Тогда
		ДополнительныеПараметры = ДополнительныеПараметрыРегистрацииДанныхСреднегоЗаработка();
	КонецЕсли;
	
	// Метаданные документа используем для обращения к таблице.
	МетаданныеДокумента = Метаданные.НайтиПоТипу(ТипЗнч(ДокументСсылка));
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	
	// Составляем текст запроса создания временной таблицы.
	ТекстЗапроса = "";
	ПерваяТаблица = Истина;
	Для Каждого КлючИЗначение Из ДополнительныеПараметры.Таблицы Цикл
		ИмяТаблицы = КлючИЗначение.Ключ;
		ИменаПолей = КлючИЗначение.Значение;
		
		МетаданныеТаблицы = МетаданныеДокумента.ТабличныеЧасти[ИмяТаблицы];
		
		// Показатель премирования берется по ссылке от начисления, если это не переопределено документом.
		Если ИменаПолей.ПоказательПремирования = Неопределено Тогда
			Если СтрНайти(ИмяТаблицы, "Перерасчет") Тогда
				ИменаПолей.ПоказательПремирования = ВыражениеВыбораПоказателяПремированияПоТабличнойЧасти(МетаданныеТаблицы);
			Иначе
				ИменаПолей.ПоказательПремирования = ИменаПолей.Начисление + ".ПоказательПремирования";
			КонецЕсли;
		КонецЕсли;
		
		Если Не ПерваяТаблица Тогда
			ТекстЗапроса = ТекстЗапроса + "
				|ОБЪЕДИНИТЬ ВСЕ
				|";
		КонецЕсли;
		ТекстОбъединения = 
			"ВЫБРАТЬ
			|	ТаблицаНачислений.Ссылка КАК ДокументСсылка,
			|	ТаблицаНачислений.ДатаДействия КАК ДатаДействия,
			|	ТаблицаНачислений.ПериодДействия КАК ПериодДействия,
			|	ТаблицаНачислений.МесяцНачисления КАК МесяцНачисления,
			|	ТаблицаНачислений.Начисление КАК Начисление,
			|	ТаблицаНачислений.ПоказательПремирования КАК ПоказательПремирования,
			|	ТаблицаНачислений.НачалоБазовогоПериода КАК НачалоБазовогоПериода,
			|	ТаблицаНачислений.ОкончаниеБазовогоПериода КАК ОкончаниеБазовогоПериода,
			|	ТаблицаНачислений.ДатаНачала КАК ДатаНачала,
			|	ТаблицаНачислений.ДатаОкончания КАК ДатаОкончания,
			|	ТаблицаНачислений.НомерСтроки КАК НомерСтроки,
			|	ТаблицаНачислений.Сотрудник КАК Сотрудник,
			|	ТаблицаНачислений.Сотрудник.ФизическоеЛицо КАК ФизическоеЛицо,
			|	ТаблицаНачислений.Ссылка.Организация КАК Организация,
			|	ТаблицаНачислений.Результат КАК Сумма,
			|	ТаблицаНачислений.РанееНачислено КАК РанееНачислено,
			|	ТаблицаНачислений.Подразделение КАК Подразделение,
			|	ТаблицаНачислений.ДокументОснование КАК ДокументОснование,
			|	ТаблицаНачислений.ИдентификаторСтрокиВидаРасчета КАК ИдентификаторСтроки,
			|	ТаблицаНачислений.ИсходныйДокумент КАК ИсходныйДокумент,
			|	ТаблицаНачислений.Сторно КАК Сторно,
			|	ТаблицаНачислений.ФиксСторно КАК ФиксСторно,
			|	ТаблицаНачислений.СторнируемыйДокумент КАК СторнируемыйДокумент
			|ПОМЕСТИТЬ ВТЗаписиНачислений
			|ИЗ
			|	#ТаблицаНачислений КАК ТаблицаНачислений
			|ГДЕ
			|	ТаблицаНачислений.Ссылка = &ДокументСсылка";
		
		Если СписокФизическихЛиц <> Неопределено Тогда
			
			ТекстОбъединения = ТекстОбъединения + "
				|	И ТаблицаНачислений.Сотрудник.ФизическоеЛицо В (&СписокФизическихЛиц)";
			
		КонецЕсли;
		
		Если Не ПерваяТаблица Тогда
			
			ТекстОбъединения = СтрЗаменить(ТекстОбъединения, "ПОМЕСТИТЬ ВТЗаписиНачислений", "");
			
			Если СписокФизическихЛиц <> Неопределено Тогда
				Запрос.УстановитьПараметр("СписокФизическихЛиц", СписокФизическихЛиц);
			КонецЕсли;
			
		КонецЕсли;
		
		ТекстОбъединения = СтрЗаменить(ТекстОбъединения, "#ТаблицаНачислений", МетаданныеДокумента.ПолноеИмя() + "." + ИмяТаблицы);
		Для Каждого КлючИЗначениеПолей Из ИменаПолей Цикл
			ИмяПоля = КлючИЗначениеПолей.Ключ;
			ТекстОбращения = КлючИЗначениеПолей.Значение;
			Если ТекстОбращения = Неопределено Тогда
				ТекстОбъединения = СтрЗаменить(ТекстОбъединения, "ТаблицаНачислений." + ИмяПоля + " КАК " + ИмяПоля, "НЕОПРЕДЕЛЕНО КАК " + ИмяПоля);
			Иначе
				Если СтрНачинаетсяС(ТекстОбращения, "#") Тогда
					НовыйТекст = Сред(ТекстОбращения, 2) + " КАК " + ИмяПоля;
				Иначе
					НовыйТекст = "ТаблицаНачислений." + ТекстОбращения + " КАК " + ИмяПоля;
				КонецЕсли;
				ТекстОбъединения = СтрЗаменить(ТекстОбъединения, "ТаблицаНачислений." + ИмяПоля + " КАК " + ИмяПоля, НовыйТекст);
			КонецЕсли;
		КонецЦикла;
		ТекстОбъединения = СтрЗаменить(ТекстОбъединения, "ТаблицаНачислений.МесяцНачисления КАК МесяцНачисления", "ТаблицаНачислений." + ДополнительныеПараметры.МесяцНачисления + " КАК МесяцНачисления");
		РеквизитыТабличнойЧасти = МетаданныеТаблицы.Реквизиты;
		Если РеквизитыТабличнойЧасти.Найти("Сторно") = Неопределено Тогда
			ТекстОбъединения = СтрЗаменить(ТекстОбъединения, "ТаблицаНачислений.Сторно КАК Сторно", "ЛОЖЬ КАК Сторно");
		КонецЕсли;
		Если РеквизитыТабличнойЧасти.Найти("ФиксСторно") = Неопределено Тогда
			ТекстОбъединения = СтрЗаменить(ТекстОбъединения, "ТаблицаНачислений.ФиксСторно КАК ФиксСторно", "ЛОЖЬ КАК ФиксСторно");
		КонецЕсли;
		Если РеквизитыТабличнойЧасти.Найти("СторнируемыйДокумент") = Неопределено Тогда
			ТекстОбъединения = СтрЗаменить(ТекстОбъединения, "ТаблицаНачислений.СторнируемыйДокумент", "НЕОПРЕДЕЛЕНО");
		КонецЕсли;
		Если РеквизитыТабличнойЧасти.Найти("РанееНачислено") = Неопределено Тогда
			ТекстОбъединения = СтрЗаменить(ТекстОбъединения, "ТаблицаНачислений.РанееНачислено КАК РанееНачислено", "0 КАК РанееНачислено");
		КонецЕсли;
		Если РеквизитыТабличнойЧасти.Найти("ДокументОснование") = Неопределено Тогда
			ТекстОбъединения = СтрЗаменить(ТекстОбъединения, "ТаблицаНачислений.ДокументОснование КАК ДокументОснование", "НЕОПРЕДЕЛЕНО КАК ДокументОснование");
		КонецЕсли;
		Если РеквизитыТабличнойЧасти.Найти("ИсходныйДокумент") = Неопределено Тогда
			ТекстОбъединения = СтрЗаменить(ТекстОбъединения, "ТаблицаНачислений.ИсходныйДокумент КАК ИсходныйДокумент", "НЕОПРЕДЕЛЕНО КАК ИсходныйДокумент");
		КонецЕсли;
		
		// Добавляем объединение в общий запрос.
		ТекстЗапроса = ТекстЗапроса + ТекстОбъединения;
		ПерваяТаблица = Ложь;
	КонецЦикла;
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.Выполнить();
	
	// Если используются источники финансирования дополняем результатом распределения начислений.
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
		// Дополнить данными о распределении начислений по источникам финансирования.
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	РаспределениеНачислений.СтатьяФинансирования,
			|	РаспределениеНачислений.СтатьяРасходов,
			|	РаспределениеНачислений.СпособОтраженияЗарплатыВБухучете,
			|	РаспределениеНачислений.ОблагаетсяЕНВД,
			|	ЕСТЬNULL(РаспределениеНачислений.Результат, ЗаписиНачислений.Сумма) КАК Сумма,
			|	ЗаписиНачислений.*
			|ИЗ
			|	ВТЗаписиНачислений КАК ЗаписиНачислений
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ #РаспределениеРезультатовНачислений КАК РаспределениеНачислений
			|		ПО ЗаписиНачислений.ИдентификаторСтроки = РаспределениеНачислений.ИдентификаторСтроки
			|			И ЗаписиНачислений.ДокументСсылка = РаспределениеНачислений.Ссылка";
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#РаспределениеРезультатовНачислений", МетаданныеДокумента.ПолноеИмя() + ".РаспределениеРезультатовНачислений");
	Иначе
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка) КАК СтатьяФинансирования,
			|	ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка) КАК СтатьяРасходов,
			|	ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка) КАК СпособОтраженияЗарплатыВБухучете,
			|	ЛОЖЬ КАК ОблагаетсяЕНВД,
			|	ЗаписиНачислений.Сумма КАК Сумма,
			|	ЗаписиНачислений.*
			|ИЗ
			|	ВТЗаписиНачислений КАК ЗаписиНачислений";
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	РезультатЗапроса = Запрос.Выполнить();
	
	ДанныеДляПроведения.НачисленияДляСреднегоЗаработка = ПустаяТаблицаДляРегистрацииДанныхСреднегоЗаработка();
	
	// Проверяем тип регистратора
	Если Не ДанныеДляПроведения.НачисленияДляСреднегоЗаработка.Колонки.ДокументСсылка.ТипЗначения.СодержитТип(ТипЗнч(ДокументСсылка)) Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Тип «%1» не включен в состав типов колонки ДокументСсылка таблицы для регистрации данных среднего заработка.';
				|en = 'The ""%1"" type is not included in the type content of column ДокументСсылка of the table to register average earnings data.'"),
			ТипЗнч(ДокументСсылка));
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ДанныеДляПроведения.НачисленияДляСреднегоЗаработка.Добавить(), Выборка);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьДанныеДляРегистрацииСреднегоЗаработкаПоГПХ(ДанныеДляПроведения, РеквизитыДляПроведения, СписокФизическихЛиц = Неопределено) Экспорт
	
	Если Не ЗарплатаКадры.ВТСуществует(ДанныеДляПроведения.МенеджерВременныхТаблиц, "ВТНачисленияПоДоговорамСРаспределением") Тогда
		РасчетЗарплатыРасширенный.СоздатьВТНачисленияПоДоговорамСРаспределением(ДанныеДляПроведения, РеквизитыДляПроведения, СписокФизическихЛиц);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ДанныеДляПроведения.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	НачисленияПоДоговорам.ДатаНачала КАК ДатаНачала,
		|	НачисленияПоДоговорам.ДатаОкончания КАК ДатаОкончания,
		|	НачисленияПоДоговорам.Сотрудник КАК Сотрудник,
		|	НачисленияПоДоговорам.ФизическоеЛицо КАК ФизическоеЛицо,
		|	НачисленияПоДоговорам.Начисление КАК Начисление,
		|	СУММА(НачисленияПоДоговорам.Сумма) КАК Сумма,
		|	НачисленияПоДоговорам.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	НачисленияПоДоговорам.СтатьяФинансирования КАК СтатьяФинансирования,
		|	НачисленияПоДоговорам.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	НачисленияПоДоговорам.СтатьяРасходов КАК СтатьяРасходов,
		|	НачисленияПоДоговорам.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	ВЫБОР
		|		КОГДА НачисленияПоДоговорам.МестоПолученияДохода ЕСТЬ НЕ NULL 
		|				И НачисленияПоДоговорам.МестоПолученияДохода <> НЕОПРЕДЕЛЕНО
		|				И НачисленияПоДоговорам.МестоПолученияДохода <> ЗНАЧЕНИЕ(Справочник.ТерриторииВыполненияРабот.ПустаяСсылка)
		|				И НачисленияПоДоговорам.МестоПолученияДохода <> ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
		|			ТОГДА НачисленияПоДоговорам.МестоПолученияДохода
		|		ИНАЧЕ НачисленияПоДоговорам.ТерриторияВыполненияРаботВОрганизации
		|	КОНЕЦ КАК Подразделение,
		|	НачисленияПоДоговорам.ДокументОснование КАК ДокументОснование
		|ИЗ
		|	ВТНачисленияПоДоговорамСРаспределением КАК НачисленияПоДоговорам
		|ГДЕ
		|	НачисленияПоДоговорам.ДатаОкончания >= &ДатаОбъединенияВзносов
		|
		|СГРУППИРОВАТЬ ПО
		|	НачисленияПоДоговорам.ДатаНачала,
		|	НачисленияПоДоговорам.ДатаОкончания,
		|	НачисленияПоДоговорам.Сотрудник,
		|	НачисленияПоДоговорам.ФизическоеЛицо,
		|	НачисленияПоДоговорам.Начисление,
		|	НачисленияПоДоговорам.ИдентификаторСтроки,
		|	НачисленияПоДоговорам.СтатьяФинансирования,
		|	НачисленияПоДоговорам.СпособОтраженияЗарплатыВБухучете,
		|	НачисленияПоДоговорам.СтатьяРасходов,
		|	НачисленияПоДоговорам.ОблагаетсяЕНВД,
		|	ВЫБОР
		|		КОГДА НачисленияПоДоговорам.МестоПолученияДохода ЕСТЬ НЕ NULL 
		|				И НачисленияПоДоговорам.МестоПолученияДохода <> НЕОПРЕДЕЛЕНО
		|				И НачисленияПоДоговорам.МестоПолученияДохода <> ЗНАЧЕНИЕ(Справочник.ТерриторииВыполненияРабот.ПустаяСсылка)
		|				И НачисленияПоДоговорам.МестоПолученияДохода <> ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
		|			ТОГДА НачисленияПоДоговорам.МестоПолученияДохода
		|		ИНАЧЕ НачисленияПоДоговорам.ТерриторияВыполненияРаботВОрганизации
		|	КОНЕЦ,
		|	НачисленияПоДоговорам.ДокументОснование";
	
	Запрос.УстановитьПараметр("ДатаОбъединенияВзносов",	УчетСтраховыхВзносов.ДатаОбъединенияВзносов());
	
	Если ДанныеДляПроведения.НачисленияДляСреднегоЗаработка = Неопределено Тогда
		ДанныеДляПроведения.НачисленияДляСреднегоЗаработка = ПустаяТаблицаДляРегистрацииДанныхСреднегоЗаработка();
	КонецЕсли;
	
	Таблица = Запрос.Выполнить().Выгрузить();
	Для Каждого СтрокаТаблицы Из Таблица Цикл
		// Не заполняются: НачалоБазовогоПериода, ОкончаниеБазовогоПериода, Сторно, ФиксСторно,
		// РанееНачислено, ПоказательПремирования, СторнируемыйДокумент.
		СтрокаСреднего = ДанныеДляПроведения.НачисленияДляСреднегоЗаработка.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаСреднего, СтрокаТаблицы);
		СтрокаСреднего.ДокументСсылка   = РеквизитыДляПроведения.Ссылка;
		СтрокаСреднего.ДатаДействия     = РеквизитыДляПроведения.МесяцНачисления;
		СтрокаСреднего.ПериодДействия   = РеквизитыДляПроведения.МесяцНачисления;
		СтрокаСреднего.МесяцНачисления  = РеквизитыДляПроведения.МесяцНачисления;
		СтрокаСреднего.Организация      = РеквизитыДляПроведения.Организация;
		СтрокаСреднего.ИсходныйДокумент = РеквизитыДляПроведения.Ссылка;
		СтрокаСреднего.НомерСтроки      = СтрокаТаблицы.ИдентификаторСтроки;
	КонецЦикла;
	
КонецПроцедуры

// Процедура выполняет заполнение значений специализированных показателей 
//  учета среднего заработка.
//
Процедура ЗаполнитьЗначенияПоказателейРасчетаЗарплаты(МенеджерВременныхТаблиц, ТаблицаПоказателей) Экспорт
	
	ПоказателиОбщегоСреднегоЗаработка = УчетСреднегоЗаработкаКлиентСервер.ПоказателиОбщегоСреднегоЗаработка();
	ПоказателиСреднегоЗаработка = УчетСреднегоЗаработкаКлиентСервер.ПоказателиРасчетаСреднегоЗаработка();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("СреднийЗаработокПоказатели", ПоказателиОбщегоСреднегоЗаработка);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДополнительныеПоказатели.ИдентификаторСтроки,
		|	ДополнительныеПоказатели.Сотрудник,
		|	ДополнительныеПоказатели.Показатель,
		|	ДополнительныеПоказатели.ДатаНачала,
		|	ДополнительныеПоказатели.ВремяВЧасах
		|ИЗ
		|	ВТДополнительныеПоказатели КАК ДополнительныеПоказатели
		|ГДЕ
		|	ДополнительныеПоказатели.Показатель В(&СреднийЗаработокПоказатели)";
		
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ДополнительныеПараметры = УчетСреднегоЗаработкаКлиентСервер.ДополнительныеПараметрыРасчетаСреднегоЗаработка();
		ДополнительныеПараметры.ПоЧасам = Выборка.ВремяВЧасах;
		ЗначенияПоказателей = ЗначенияПоказателейСреднегоЗаработка(Выборка.Сотрудник, Выборка.ДатаНачала, ДополнительныеПараметры);
		СреднийЗаработок = ЗначенияПоказателей.СреднийЗаработокОбщий;
		Если ПолучитьФункциональнуюОпцию("ИндексироватьСреднийЗаработокЧастично") Тогда 
			Если Выборка.Показатель = ПоказателиСреднегоЗаработка["СреднийЗаработокИндексируемый"] Тогда 
				СреднийЗаработок = ЗначенияПоказателей.СреднийЗаработокИндексируемый;
			ИначеЕсли Выборка.Показатель = ПоказателиСреднегоЗаработка["СреднийЗаработокНеиндексируемый"] Тогда  
				СреднийЗаработок = ЗначенияПоказателей.СреднийЗаработокНеиндексируемый;
			КонецЕсли;
		КонецЕсли;
		НоваяСтрока = ТаблицаПоказателей.Добавить();
		НоваяСтрока.ИдентификаторСтроки = Выборка.ИдентификаторСтроки;
		НоваяСтрока.Показатель = Выборка.Показатель;
		НоваяСтрока.Значение = СреднийЗаработок;
	КонецЦикла;
	
КонецПроцедуры

// Процедура выполняет заполнение значений специализированных показателей 
//  учета среднего заработка.
//
Процедура ЗаполнитьЗначенияПоказателейРасчетаЗарплатыПоТаблицеЗначений(ДополнительныеПоказатели, ТаблицаПоказателей) Экспорт
	
	ПоказателиОбщегоСреднегоЗаработка = УчетСреднегоЗаработкаКлиентСервер.ПоказателиОбщегоСреднегоЗаработка();
	ПоказателиСреднегоЗаработка = УчетСреднегоЗаработкаКлиентСервер.ПоказателиРасчетаСреднегоЗаработка();
	
	Для Каждого СтрокаПоказателя Из ДополнительныеПоказатели Цикл
		Если ПоказателиОбщегоСреднегоЗаработка.Найти(СтрокаПоказателя.Показатель) = Неопределено Тогда
			Продолжить;
		КонецЕсли;	
		
		ДополнительныеПараметры = УчетСреднегоЗаработкаКлиентСервер.ДополнительныеПараметрыРасчетаСреднегоЗаработка();
		ДополнительныеПараметры.ПоЧасам = СтрокаПоказателя.ВремяВЧасах;
		ЗначенияПоказателей = ЗначенияПоказателейСреднегоЗаработка(СтрокаПоказателя.Сотрудник, СтрокаПоказателя.ДатаНачала, ДополнительныеПараметры);
		СреднийЗаработок = ЗначенияПоказателей.СреднийЗаработокОбщий;
		Для Каждого КлючИЗначение Из ЗначенияПоказателей Цикл
			Если СтрокаПоказателя.Показатель = ПоказателиСреднегоЗаработка[КлючИЗначение.Ключ] Тогда
				СреднийЗаработок = КлючИЗначение.Значение;
				Прервать;
			КонецЕсли;
		КонецЦикла;  
		НоваяСтрока = ТаблицаПоказателей.Добавить();
		НоваяСтрока.ИдентификаторСтроки = СтрокаПоказателя.ИдентификаторСтроки;
		НоваяСтрока.Показатель = СтрокаПоказателя.Показатель;
		НоваяСтрока.Значение = СреднийЗаработок;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриОпределенииДополнительныхПоказателей(ДополнительныеПоказатели) Экспорт
	ПоказателиОбщегоСреднегоЗаработка = УчетСреднегоЗаработкаКлиентСервер.ПоказателиОбщегоСреднегоЗаработка();
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ДополнительныеПоказатели, ПоказателиОбщегоСреднегоЗаработка); 	
КонецПроцедуры

// Процедура выполняет заполнение представления команды расшифровки специализированных показателей 
//  учета среднего заработка.
//
// Параметры:
//		ДокументСсылка
//		ВидРасчетаИнфо - Информация о виде расчета, полученная с помощью метода
//		                 ЗарплатаКадрыРасширенныйПовтИсп.ПолучитьИнформациюОВидеРасчета.
//		СтрокаНачислений - строка таблицы "Начисления".
//		ДанныеПоказателей - Таблица показателей.
//		РежимРаботы - режим работы таблицы с видами расчетов
//			0 - режим ввода штатного расписания - вводятся максимальные 
//				и минимальные значения ("вилка") условно-постоянных показателей
//			1 - режим ввода плановых начислений - вводятся значения 
//				условно-постоянных показателей
//			2 - режим ввода начислений в документе-начислятеле - вводятся значения всех 
//				показателей, отображаемых при виде расчета.
//		ОтображатьТекущиеЗначения - признак того, что в форме отображаются действующие на настоящий 
//			момент показатели начислений. Применяется, например, в документах кадровых переводов.
//			По умолчанию - Ложь
//		ДокументСсылка - расчетный документ.
//
Процедура ЗаполнитьДанныеПоказателейРасчетаЗарплаты(ВидРасчетаИнфо, СтрокаНачислений, ДанныеПоказателей, РежимРаботы, ОтображатьТекущиеЗначения = Ложь, ДокументСсылка = Неопределено) Экспорт
	
	Если РежимРаботы = 0 Или РежимРаботы = 1 Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипыДокументовРасчетаПоСреднемуЗаработку().Найти(ТипЗнч(ДокументСсылка)) <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПоказателиСреднего = УчетСреднегоЗаработкаКлиентСервер.ПоказателиОбщегоСреднегоЗаработка();
	ПоказателиСреднего.Добавить(ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ПоказателиРасчетаЗарплаты.СреднийЗаработокФСС"));
	
	СчетчикПоказателей = 1;
	Для Каждого СтрокаПоказателя Из ВидРасчетаИнфо.Показатели Цикл
		Если ПоказателиСреднего.Найти(СтрокаПоказателя.Показатель) <> Неопределено Тогда
			ОтображатьРасшифровку = Истина;
			Если РасчетЗарплатыРасширенный.ЕстьПолеВСтрокеКоллекции(СтрокаНачислений, "ДокументОснование")
				И ЗначениеЗаполнено(СтрокаНачислений.ДокументОснование) Тогда
				
				ОтображатьРасшифровку = Ложь;
			КонецЕсли;
			Если ОтображатьРасшифровку Тогда
				СтрокаНачислений["КомандаРасшифровки" + СчетчикПоказателей] = УчетСреднегоЗаработкаКлиентСервер.ПредставлениеКомандыРасшифровки();
			КонецЕсли;
		КонецЕсли;			
		СчетчикПоказателей = СчетчикПоказателей + 1;
	КонецЦикла;
	
КонецПроцедуры

// Предназначена для получения настроек подсистемы.
//
// Возвращаемое значение - структура с именем настройки в качестве ключа.
//
Функция НастройкиРасчетаСреднегоЗаработка() Экспорт
	
	НастройкиРасчетаСреднегоЗаработка = РегистрыСведений.НастройкиРасчетаСреднегоЗаработка.СоздатьМенеджерЗаписи();
	НастройкиРасчетаСреднегоЗаработка.Прочитать();
	
	СтруктураНастроек = ОбщегоНазначения.СтруктураПоМенеджеруЗаписи(
							НастройкиРасчетаСреднегоЗаработка, Метаданные.РегистрыСведений.НастройкиРасчетаСреднегоЗаработка);
	
	Возврат СтруктураНастроек;
	
КонецФункции

// Процедура заполняет настройки учета среднего заработка в зависимости 
//  от количества в информационной базе специализированных начислений.
//
Процедура ЗаполнитьНастройкиУчетаСреднегоЗаработка() Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК СреднийЗаработокФССРазличаетсяПоПорядкуРасчета
	|ПОМЕСТИТЬ ВТНастройкаСреднегоФСС
	|ИЗ
	|	ПланВидовРасчета.Начисления.СреднийЗаработокФСС КАК НастройкаСреднегоФСС
	|
	|СГРУППИРОВАТЬ ПО
	|	НастройкаСреднегоФСС.Ссылка
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ НастройкаСреднегоФСС.ПорядокРасчета) < &КоличествоПорядковРасчетаФСС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК РассчитыватьОтдельноСреднийЗаработокФССДляБольничногоЗаСчетРаботодателя
	|ПОМЕСТИТЬ ВТБольничныйЗаСчетРаботодателя
	|ИЗ
	|	ПланВидовРасчета.Начисления.СреднийЗаработокФСС КАК НачисленияСреднийЗаработокФСС
	|ГДЕ
	|	НачисленияСреднийЗаработокФСС.Значение = ЗНАЧЕНИЕ(Перечисление.УчетНачисленийВСреднемЗаработкеФСС.НеУчитыватьПриОплатеБольничногоЗаСчетРаботодателя)
	|	И НЕ НачисленияСреднийЗаработокФСС.Ссылка.ПометкаУдаления
	|	И НЕ НачисленияСреднийЗаработокФСС.Ссылка.ВАрхиве
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВЫБОР
	|			КОГДА НастройкаСреднегоОбщего.Значение = ЗНАЧЕНИЕ(Перечисление.УчетНачисленийВСреднемЗаработкеОбщий.ОбщийЗаработок)
	|					И НЕ НастройкаСреднегоОбщего.Индексируется
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ИспользоватьОсновныеНачисленияБезИндексацииИстина,
	|	СУММА(ВЫБОР
	|			КОГДА НастройкаСреднегоОбщего.Значение = ЗНАЧЕНИЕ(Перечисление.УчетНачисленийВСреднемЗаработкеОбщий.ПремияПроцентом)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ИспользоватьПремииПроцентомИстина,
	|	СУММА(ВЫБОР
	|			КОГДА НастройкаСреднегоОбщего.Значение = ЗНАЧЕНИЕ(Перечисление.УчетНачисленийВСреднемЗаработкеОбщий.ПремияПроцентом)
	|					И НЕ НастройкаСреднегоОбщего.Индексируется
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ИспользоватьПремииПроцентомБезИндексацииИстина,
	|	СУММА(ВЫБОР
	|			КОГДА НастройкаСреднегоОбщего.Значение = ЗНАЧЕНИЕ(Перечисление.УчетНачисленийВСреднемЗаработкеОбщий.ПремияГодоваяПроцентом)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ИспользоватьПремииЗаГодПроцентомИстина,
	|	СУММА(ВЫБОР
	|			КОГДА НастройкаСреднегоОбщего.Значение = ЗНАЧЕНИЕ(Перечисление.УчетНачисленийВСреднемЗаработкеОбщий.ПремияГодоваяПроцентом)
	|					И НЕ НастройкаСреднегоОбщего.Индексируется
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ИспользоватьПремииЗаГодПроцентомБезИндексацииИстина,
	|	СУММА(ВЫБОР
	|			КОГДА НастройкаСреднегоОбщего.Значение = ЗНАЧЕНИЕ(Перечисление.УчетНачисленийВСреднемЗаработкеОбщий.ПремияФиксированнойСуммой)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ИспользоватьПремииСуммойИстина,
	|	СУММА(ВЫБОР
	|			КОГДА НастройкаСреднегоОбщего.Значение = ЗНАЧЕНИЕ(Перечисление.УчетНачисленийВСреднемЗаработкеОбщий.ПремияФиксированнойСуммой)
	|					И НЕ НастройкаСреднегоОбщего.Индексируется
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ИспользоватьПремииСуммойБезИндексацииИстина,
	|	СУММА(ВЫБОР
	|			КОГДА НастройкаСреднегоОбщего.Значение = ЗНАЧЕНИЕ(Перечисление.УчетНачисленийВСреднемЗаработкеОбщий.ПремияГодоваяФиксированнойСуммой)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ИспользоватьПремииЗаГодСуммойИстина,
	|	СУММА(ВЫБОР
	|			КОГДА НастройкаСреднегоОбщего.Значение = ЗНАЧЕНИЕ(Перечисление.УчетНачисленийВСреднемЗаработкеОбщий.ПремияГодоваяФиксированнойСуммой)
	|					И НЕ НастройкаСреднегоОбщего.Индексируется
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ИспользоватьПремииЗаГодСуммойБезИндексацииИстина,
	|	СУММА(ВЫБОР
	|			КОГДА ПоказателиНачисления.Показатель = &СреднийЗаработокОбщий
	|					И НЕ Начисления.УчетВремениВЧасах
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ИспользоватьОтработаноДнейИстина,
	|	СУММА(ВЫБОР
	|			КОГДА ПоказателиНачисления.Показатель = &СреднийЗаработокОбщий
	|					И Начисления.УчетВремениВЧасах
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ИспользоватьОтработаноЧасовИстина,
	|	СУММА(ВЫБОР
	|			КОГДА НастройкаСреднегоОбщего.Значение В (&Премии)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ИспользоватьОтработаноПоПятидневкеИстина,
	|	СУММА(ВЫБОР
	|			КОГДА ВидыОтпусков.СпособРасчетаОтпуска В (ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОтпуска.ВРабочихДнях), ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОтпуска.ВКалендарныхИлиРабочихДняхВЗависимостиОтТрудовогоДоговора))
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ИспользоватьОтработаноПоШестидневкеИстина,
	|	СУММА(ВЫБОР
	|			КОГДА ПоказателиНачисления.Показатель В(&СреднийЗаработокИндексируемый, &СреднийЗаработокНеиндексируемый)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ИндексироватьСреднийЗаработокЧастичноИстина,
	|	ЕСТЬNULL(МАКСИМУМ(НастройкаСреднегоФСС.СреднийЗаработокФССРазличаетсяПоПорядкуРасчета), ЛОЖЬ) КАК СреднийЗаработокФССРазличаетсяПоПорядкуРасчета,
	|	ЕСТЬNULL(БольничныйЗаСчетРаботодателя.РассчитыватьОтдельноСреднийЗаработокФССДляБольничногоЗаСчетРаботодателя, ЛОЖЬ) КАК РассчитыватьОтдельноСреднийЗаработокФССДляБольничногоЗаСчетРаботодателя
	|ПОМЕСТИТЬ ВТНачисления
	|ИЗ
	|	ПланВидовРасчета.Начисления КАК Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления.СреднийЗаработокОбщий КАК НастройкаСреднегоОбщего
	|		ПО (НастройкаСреднегоОбщего.Ссылка = Начисления.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления.Показатели КАК ПоказателиНачисления
	|		ПО (ПоказателиНачисления.Ссылка = Начисления.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыОтпусков КАК ВидыОтпусков
	|		ПО (ВидыОтпусков.Ссылка = Начисления.ВидОтпуска)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкаСреднегоФСС КАК НастройкаСреднегоФСС
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБольничныйЗаСчетРаботодателя КАК БольничныйЗаСчетРаботодателя
	|		ПО (ИСТИНА)
	|ГДЕ
	|	НЕ Начисления.ПометкаУдаления
	|	И НЕ Начисления.ВАрхиве
	|
	|СГРУППИРОВАТЬ ПО
	|	ЕСТЬNULL(БольничныйЗаСчетРаботодателя.РассчитыватьОтдельноСреднийЗаработокФССДляБольничногоЗаСчетРаботодателя, ЛОЖЬ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА Начисления.ИспользоватьОсновныеНачисленияБезИндексацииИстина > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ИспользоватьОсновныеНачисленияБезИндексации,
	|	ВЫБОР
	|		КОГДА Начисления.ИспользоватьПремииПроцентомИстина > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ИспользоватьПремииПроцентом,
	|	ВЫБОР
	|		КОГДА Начисления.ИспользоватьПремииПроцентомБезИндексацииИстина > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ИспользоватьПремииПроцентомБезИндексации,
	|	ВЫБОР
	|		КОГДА Начисления.ИспользоватьПремииЗаГодПроцентомИстина > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ИспользоватьПремииЗаГодПроцентом,
	|	ВЫБОР
	|		КОГДА Начисления.ИспользоватьПремииЗаГодПроцентомБезИндексацииИстина > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ИспользоватьПремииЗаГодПроцентомБезИндексации,
	|	ВЫБОР
	|		КОГДА Начисления.ИспользоватьПремииСуммойИстина > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ИспользоватьПремииСуммой,
	|	ВЫБОР
	|		КОГДА Начисления.ИспользоватьПремииСуммойБезИндексацииИстина > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ИспользоватьПремииСуммойБезИндексации,
	|	ВЫБОР
	|		КОГДА Начисления.ИспользоватьПремииЗаГодСуммойИстина > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ИспользоватьПремииЗаГодСуммой,
	|	ВЫБОР
	|		КОГДА Начисления.ИспользоватьПремииЗаГодСуммойБезИндексацииИстина > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ИспользоватьПремииЗаГодСуммойБезИндексации,
	|	ВЫБОР
	|		КОГДА Начисления.ИспользоватьОтработаноДнейИстина > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ИспользоватьОтработаноДней,
	|	ВЫБОР
	|		КОГДА Начисления.ИспользоватьОтработаноЧасовИстина > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ИСТИНА В
	|						(ВЫБРАТЬ ПЕРВЫЕ 1
	|							ИСТИНА
	|						ИЗ
	|							Справочник.ГрафикиРаботыСотрудников КАК ГрафикиРаботыСотрудников
	|						ГДЕ
	|							ГрафикиРаботыСотрудников.СуммированныйУчетРабочегоВремени)
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ
	|	КОНЕЦ КАК ИспользоватьОтработаноЧасов,
	|	ВЫБОР
	|		КОГДА Начисления.ИспользоватьОтработаноПоПятидневкеИстина > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ИспользоватьОтработаноПоПятидневке,
	|	ВЫБОР
	|		КОГДА Начисления.ИспользоватьОтработаноПоШестидневкеИстина > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ИспользоватьОтработаноПоШестидневке,
	|	ВЫБОР
	|		КОГДА Начисления.ИндексироватьСреднийЗаработокЧастичноИстина > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ИндексироватьСреднийЗаработокЧастично,
	|	Начисления.СреднийЗаработокФССРазличаетсяПоПорядкуРасчета КАК СреднийЗаработокФССРазличаетсяПоПорядкуРасчета,
	|	Начисления.РассчитыватьОтдельноСреднийЗаработокФССДляБольничногоЗаСчетРаботодателя КАК РассчитыватьОтдельноСреднийЗаработокФССДляБольничногоЗаСчетРаботодателя
	|ИЗ
	|	ВТНачисления КАК Начисления");
	
	Запрос.УстановитьПараметр("СреднийЗаработокОбщий", ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ПоказателиРасчетаЗарплаты.СреднийЗаработокОбщий"));
	Запрос.УстановитьПараметр("КоличествоПорядковРасчетаФСС", Метаданные.Перечисления.ПорядокРасчетаСреднегоЗаработкаФСС.ЗначенияПеречисления.Количество());
	Запрос.УстановитьПараметр("Премии", Перечисления.УчетНачисленийВСреднемЗаработкеОбщий.Премии());
	Запрос.УстановитьПараметр("СреднийЗаработокИндексируемый", ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ПоказателиРасчетаЗарплаты.СреднийЗаработокИндексируемый"));
	Запрос.УстановитьПараметр("СреднийЗаработокНеиндексируемый", ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ПоказателиРасчетаЗарплаты.СреднийЗаработокНеиндексируемый"));
	
	УстановитьПривилегированныйРежим(Истина);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Не Выборка.Следующий() Тогда
		Возврат;
	КонецЕсли;
	
	// Запись настроек в регистр сведений.
	Настройки = РегистрыСведений.НастройкиРасчетаСреднегоЗаработка.СоздатьМенеджерЗаписи();
	Настройки.Прочитать();
	
	ЗаполнитьЗначенияСвойств(Настройки, Выборка);
	
	Настройки.Записать();
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Функция запрашивает период расчета среднего заработка для указанного начисления (или удержания)
// с учетом периода работы сотрудника.
//
Функция ПериодРасчетаОбщегоСреднегоЗаработкаСотрудника(ДатаНачалаСобытия, Сотрудник, ВидРасчета = Неопределено) Экспорт
	
	// Период, определяемый по начислению корректируется с учетом даты приема на работу сотрудника.
	ПериодРасчета = Новый СтандартныйПериод;
	
	// Применяются следующие условия.
	// 1. Если сотрудник принят позже, чем начало периода расчета среднего заработка, 
	// то началом периода расчета становится дата его приема на работу.
	// 2. Если сотрудник принят в том же месяце, что и начало события, 
	// то периодом расчета среднего заработка является месяц начала события.
	// Определим дату приема на работу сотрудника и ограничим ею начало периода расчета среднего заработка.
	// Если же дата приема на работу не заполнена, заполняем период без ограничений.
	
	КадровыеДанные = КадровыйУчет.КадровыеДанныеСотрудников(Истина, Сотрудник, "ФамилияИО, ДатаПриема");
	Если КадровыеДанные.Количество() = 0 Тогда
		// Сотрудник не принят на работу.
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось определить период расчета среднего заработка, так как %1 не принят на работу.';
				|en = 'Cannot determine a period of average earnings calculation as %1 is not hired.'"),
			Сотрудник);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	ДатаПриема = КадровыеДанные[0].ДатаПриема;
	Если ЗначениеЗаполнено(ДатаПриема) И ДатаПриема > ДатаНачалаСобытия Тогда
		// Дата начала события раньше, чем сотрудник принят на работу.
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось определить период расчета среднего заработка, так как %1 принят на работу %2 позже даты события %3.';
				|en = 'Cannot determine a period of average earnings calculation as %1 is hired %2 later than the event date %3.'"),
			КадровыеДанные[0].ФамилияИО, 
			Формат(ДатаПриема, "ДЛФ=D"), 
			Формат(ДатаНачалаСобытия, "ДЛФ=D"));
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	// Устанавливаем привилегированный режим, т.к. возможны обращения к плану видов расчета.
	УстановитьПривилегированныйРежим(Истина);
	
	ПериодПоВидуРасчета = УчетСреднегоЗаработкаКлиентСервер.ПериодРасчетаОбщегоСреднегоЗаработка(ДатаНачалаСобытия, ВидРасчета);
	
	// Корректируем период расчета.
	ПериодРасчета.ДатаНачала = ПериодПоВидуРасчета.ДатаНачала;
	ПериодРасчета.ДатаОкончания = ПериодПоВидуРасчета.ДатаОкончания;
	Если ПериодРасчета.ДатаОкончания < КадровыеДанные[0].ДатаПриема Тогда
		ПериодРасчета.ДатаНачала = НачалоМесяца(КадровыеДанные[0].ДатаПриема);
		ПериодРасчета.ДатаОкончания = КонецМесяца(КадровыеДанные[0].ДатаПриема);
	ИначеЕсли КадровыеДанные[0].ДатаПриема < НачалоМесяца(ДатаНачалаСобытия) Тогда 
		Состояния = Новый Массив;
		Состояния.Добавить(Перечисления.СостоянияСотрудника.Работа);
		Состояния.Добавить(Перечисления.СостоянияСотрудника.РаботаВОтпускеПоУходуЗаРебенком);
		Сотрудники = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудник);
		ДанныеСостояний = СостоянияСотрудников.СостоянияСотрудников(Сотрудники, Состояния, КадровыеДанные[0].ДатаПриема, НачалоМесяца(ДатаНачалаСобытия) - 1);
		Если ДанныеСостояний.Количество() = 0 Тогда 
			ПериодРасчета.ДатаНачала = НачалоМесяца(ДатаНачалаСобытия);
			ПериодРасчета.ДатаОкончания = КонецМесяца(ДатаНачалаСобытия);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ПериодРасчета;
	
КонецФункции

// Добавляет в структуру данных для проведения поля, необходимые для заполнения результатов расчета займов.
//
// Параметры:
//	ДанныеДляПроведения - см. РасчетЗарплатыРасширенный.СоздатьДанныеДляПроведенияНачисленияЗарплаты.
//
Процедура ДополнитьОписаниеДанныхДляПроведения(ДанныеДляПроведения) Экспорт
	
	ДанныеДляПроведения.Вставить("НачисленияДляСреднегоЗаработка");
	
КонецПроцедуры

// Функция определяет факт использования в формуле начисления показателя среднечасовой заработок.
//
// Параметры
//	Начисление - ПланВидовРасчетаСсылка.Начисления.
//
// Возвращаемое значение - булево, Истина - если используется, Ложь в противном случае.
//
Функция НачислениеИспользуетСреднечасовойЗаработок(Начисление) Экспорт
	
	СреднечасовойЗаработок = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ПоказателиРасчетаЗарплаты.СреднечасовойЗаработок");
	
	Если СреднечасовойЗаработок = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА КАК Поле1
		|ИЗ
		|	ПланВидовРасчета.Начисления.Показатели КАК Показатели
		|ГДЕ
		|	Показатели.Ссылка = &Начисление
		|	И Показатели.Показатель = &СреднечасовойЗаработок");
		
	Запрос.УстановитьПараметр("Начисление", Начисление);
	Запрос.УстановитьПараметр("СреднечасовойЗаработок", СреднечасовойЗаработок);
	
	Возврат Не Запрос.Выполнить().Пустой();
		
КонецФункции

// Определяет статью финансирования, используемую для сотрудника по умолчанию.
//
Функция ОтражениеВБухучетеПоУмолчанию(Сотрудник, ДатаАктуальности) Экспорт
	
	ОтражениеВБухучете = Новый Структура("СтатьяФинансирования,СпособОтраженияЗарплатыВБухучете,СтатьяРасходов,ОблагаетсяЕНВД");
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
		Возврат ОтражениеВБухучете;
	КонецЕсли;
	
	БухучетСотрудникаПоУмолчанию = ОтражениеЗарплатыВБухучетеРасширенный.БухучетСотрудникаПоУмолчанию(Сотрудник, ДатаАктуальности);
	ЗаполнитьЗначенияСвойств(ОтражениеВБухучете, БухучетСотрудникаПоУмолчанию);
	
	Возврат ОтражениеВБухучете;
	
КонецФункции

// Получает коэффициенты распределения среднего заработка по статьям финансирования из таблицы СреднийЗаработокОбщий.
//	Параметры
//		Объект - ДокументОбъект, Структура, ДанныеФормыКоллекция
//		ОписаниеДокумента - структура, описание см РасчетЗарплатыРасширенныйКлиентСервер.ОписаниеРасчетногоДокумента.
//
//	Возвращаемое значение, соответствие с коэффициентами для каждого способа расчета среднего заработка, указанного в описании документа 
//		Соответствие
//			Ключ - значение перечисления СпособыРасчетаНачислений
//			Значение - таблица значений с коэффициентами, колонки таблицы
//				* СтатьяФинансирования - СправочникСсылка.СтатьиФинансированияЗарплата
//				* Коэффициент - Число
//
Функция КоэффициентыРаспределенияСреднегоЗаработкаДокумента(Объект, ОписаниеДокумента, ЭтоСреднеЧасовойЗаработок = Неопределено) Экспорт	
	КоэффициентыРаспределения = Новый Соответствие;
	
	СпособыРасчетаСреднегоЗаработка = Новый Соответствие;
	Если ОписаниеДокумента.СпособыРасчетаСреднегоЗаработка = Неопределено Тогда
		СпособыРасчетаСреднегоЗаработка.Вставить(Перечисления.СпособыРасчетаНачислений.ПустаяСсылка(),Неопределено);
	Иначе
		Для каждого ЭлементСтруктуры Из ОписаниеДокумента.СпособыРасчетаСреднегоЗаработка Цикл
			СпособыРасчетаСреднегоЗаработка.Вставить(ЭлементСтруктуры.Значение, ЭлементСтруктуры.Значение);
		КонецЦикла;
		СпособыРасчетаСреднегоЗаработка.Вставить(Перечисления.СпособыРасчетаНачислений.ПустаяСсылка(),Перечисления.СпособыРасчетаНачислений.ПустаяСсылка());
	КонецЕсли;
	
	Для каждого ЭлементКоллекции Из СпособыРасчетаСреднегоЗаработка Цикл
		
		ТаблицаКоэффициентов = ОтражениеЗарплатыВБухучетеРасширенный.НоваяТаблицаКоэффициентыРаспределенияСреднегоЗаработка();
		
		Если ЭтоСреднеЧасовойЗаработок = Неопределено Тогда
			ЭтоСреднеЧасовойЗаработок = ОбщегоНазначенияБЗК.ЗначениеСвойства(ОписаниеДокумента, "ЭтоСреднеЧасовойЗаработок");
			Если ЭтоСреднеЧасовойЗаработок = Неопределено Тогда
				ЭтоСреднеЧасовойЗаработок = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		ПараметрыРасчетаСреднего = УчетСреднегоЗаработкаКлиентСервер.ДополнительныеПараметрыРасчетаСреднегоЗаработка();
		ПараметрыРасчетаСреднего.Индексации = Объект.ДанныеОбИндексации;
		ПараметрыРасчетаСреднего.ДатаНачалаСобытия = Объект[ОписаниеДокумента.ДатаНачалаСобытияИмя];
		ПараметрыРасчетаСреднего.НачалоПериода = Объект.ПериодРасчетаСреднегоЗаработкаНачало;
		ПараметрыРасчетаСреднего.ОкончаниеПериода = Объект.ПериодРасчетаСреднегоЗаработкаОкончание;
		ПараметрыРасчетаСреднего.ПоЧасам = ЭтоСреднеЧасовойЗаработок;
		ПараметрыРасчетаСреднего.СпособРасчетаОтпуска = ЭлементКоллекции.Значение;
		ПараметрыРасчетаСреднего.ПоСтатьямФинансирования = Истина;
		ПараметрыРасчетаСреднего.ПоНачислениям = Истина;
		
		ДанныеДляРасчета = УчетСреднегоЗаработкаКлиентСервер.ДанныеДляРасчетаСреднегоЗаработка(
		Объект.СреднийЗаработокОбщий, 
		Объект.ОтработанноеВремяДляСреднегоОбщий, 
		ПараметрыРасчетаСреднего);
		
		Для Каждого СтрокаНачислений Из ДанныеДляРасчета["Заработок"] Цикл
			НоваяСтрока	 = ТаблицаКоэффициентов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНачислений);
			НоваяСтрока.Коэффициент = СтрокаНачислений["Учтено"];
		КонецЦикла;
		ОтражениеЗарплатыВБухучетеРасширенный.СвернутьТаблицуКоэффициентовРаспределенияСреднегоЗаработка(ТаблицаКоэффициентов);
		
		КоэффициентыРаспределения.Вставить(ЭлементКоллекции.Ключ, ТаблицаКоэффициентов);
		
	КонецЦикла;
	
	Возврат КоэффициентыРаспределения;

КонецФункции

// Обслуживание проведения документа ПереносЗатратНаПерсоналМеждуСтатьями.
// Формирует таблицу значений с движениями по регистру ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.
//		Параметры
//			НачисленияСотрудников - таблица значений, соответствует структуре РН БухучетНачисленияУдержанияПоСотрудникам
//									дополнительно имеет колонки Сторно и ИдентификаторСтрокиЗатрат
//			ТаблицаПереносы - таблица значений, соответствует структуре табличной части Переносы документа ПереносЗатратНаПерсоналМеждуСтатьями
//		Возвращаемое значение
//			Таблица значений со структурой регистра ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.
//
Функция ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаПослеПереносаЗатрат(НачисленияСотрудников, ТаблицаПереносы) Экспорт

	// Таблица отбора, используется для получения данных из РН ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.
	ТаблицаОтбора = НачисленияСотрудников.Скопировать(,"Сотрудник,Начисление,Регистратор");
	ТаблицаОтбора.Свернуть("Сотрудник,Начисление,Регистратор");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаОтбора", ТаблицаОтбора);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НастройкиНачислений.Ссылка КАК Начисление,
	|	НастройкиНачислений.ПорядокРасчета КАК ПорядокРасчета,
	|	НастройкиНачислений.Значение КАК СоставнаяЧасть
	|ПОМЕСТИТЬ ВТНачисленияПорядокРасчетаОбщегоСреднегоЗаработка
	|ИЗ
	|	ПланВидовРасчета.Начисления.СреднийЗаработокОбщий КАК НастройкиНачислений
	|ГДЕ
	|	НастройкиНачислений.ПорядокРасчета <> ЗНАЧЕНИЕ(Перечисление.УчетНачисленийВСреднемЗаработкеОбщий.НеВключать)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОтбора.Сотрудник КАК Сотрудник,
	|	ТаблицаОтбора.Начисление КАК Начисление,
	|	ТаблицаОтбора.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВТТаблицаОтбора
	|ИЗ
	|	&ТаблицаОтбора КАК ТаблицаОтбора
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаОтбора.Сотрудник КАК Сотрудник,
	|	ТаблицаОтбора.Регистратор КАК Регистратор,
	|	ВТНачисленияПорядокРасчетаОбщегоСреднегоЗаработка.ПорядокРасчета КАК ПорядокРасчета,
	|	ВТНачисленияПорядокРасчетаОбщегоСреднегоЗаработка.СоставнаяЧасть КАК СоставнаяЧасть
	|ПОМЕСТИТЬ ВТОтборДанныхОбщегоСреднего
	|ИЗ
	|	ВТТаблицаОтбора КАК ТаблицаОтбора
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНачисленияПорядокРасчетаОбщегоСреднегоЗаработка КАК ВТНачисленияПорядокРасчетаОбщегоСреднегоЗаработка
	|		ПО ТаблицаОтбора.Начисление = ВТНачисленияПорядокРасчетаОбщегоСреднегоЗаработка.Начисление
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачисленияПорядокРасчетаОбщегоСреднегоЗаработка.Начисление КАК Начисление,
	|	НачисленияПорядокРасчетаОбщегоСреднегоЗаработка.ПорядокРасчета КАК ПорядокРасчета,
	|	НачисленияПорядокРасчетаОбщегоСреднегоЗаработка.СоставнаяЧасть КАК СоставнаяЧасть
	|ИЗ
	|	ВТНачисленияПорядокРасчетаОбщегоСреднегоЗаработка КАК НачисленияПорядокРасчетаОбщегоСреднегоЗаработка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеОНачислениях.Период КАК Период,
	|	ДанныеОНачислениях.Регистратор КАК Регистратор,
	|	ДанныеОНачислениях.Сотрудник КАК Сотрудник,
	|	ДанныеОНачислениях.ПорядокРасчета КАК ПорядокРасчета,
	|	ДанныеОНачислениях.СоставнаяЧасть КАК СоставнаяЧасть,
	|	ДанныеОНачислениях.СтатьяФинансирования КАК СтатьяФинансирования,
	|	ДанныеОНачислениях.СтатьяРасходов КАК СтатьяРасходов,
	|	ДанныеОНачислениях.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	ДанныеОНачислениях.Индексируется КАК Индексируется,
	|	ДанныеОНачислениях.Год КАК Год,
	|	ДанныеОНачислениях.ДатаНачалаБазовогоПериода КАК ДатаНачалаБазовогоПериода,
	|	ДанныеОНачислениях.КоличествоМесяцев КАК КоличествоМесяцев,
	|	ДанныеОНачислениях.Организация КАК Организация,
	|	ДанныеОНачислениях.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ДанныеОНачислениях.Сумма КАК Сумма
	|ИЗ
	|	РегистрНакопления.ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий КАК ДанныеОНачислениях
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОтборДанныхОбщегоСреднего КАК ОтборДанныхОбщегоСреднего
	|		ПО ДанныеОНачислениях.Регистратор = ОтборДанныхОбщегоСреднего.Регистратор
	|			И ДанныеОНачислениях.Сотрудник = ОтборДанныхОбщегоСреднего.Сотрудник
	|			И ДанныеОНачислениях.ПорядокРасчета = ОтборДанныхОбщегоСреднего.ПорядокРасчета
	|			И ДанныеОНачислениях.СоставнаяЧасть = ОтборДанныхОбщегоСреднего.СоставнаяЧасть
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаОтбора.Сотрудник КАК Сотрудник,
	|	ТаблицаОтбора.Регистратор КАК Регистратор,
	|	ТаблицаОтбора.Начисление КАК Начисление
	|ИЗ
	|	ВТТаблицаОтбора КАК ТаблицаОтбора
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНачисленияПорядокРасчетаОбщегоСреднегоЗаработка КАК ВТНачисленияПорядокРасчетаОбщегоСреднегоЗаработка
	|		ПО ТаблицаОтбора.Начисление = ВТНачисленияПорядокРасчетаОбщегоСреднегоЗаработка.Начисление";
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	КоличествоРезультатов = РезультатЗапроса.ВГраница();
	ТаблицаОтбора = РезультатЗапроса[КоличествоРезультатов].Выгрузить();
	ТекущиеДанныеОНачислениях = РезультатЗапроса[КоличествоРезультатов-1].Выгрузить();
	ОписанияНачислений = РезультатЗапроса[КоличествоРезультатов-2].Выгрузить();
	
	// таблица которую будем возвращать
	НовыеДанныеОНачислениях    = ТекущиеДанныеОНачислениях.СкопироватьКолонки();
	
	// массив используемых порядков расчета
	ИспользуемыеПорядкиРасчета = ТекущиеДанныеОНачислениях.ВыгрузитьКолонку("ПорядокРасчета");
	ИспользуемыеПорядкиРасчета = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ИспользуемыеПорядкиРасчета);
	
	// Отберем из таблицы НачисленияСотрудников строки по тем начислениям
	// которые входят в расчет среднего заработка и поместим в таблицу НачисленияСписать.
	НачисленияСписать  = НачисленияСотрудников.СкопироватьКолонки();
	Отбор = Новый Структура("Сотрудник,Регистратор,Начисление,СторнируемаяСтрока");
	Для каждого СтрокаТЗ Из ТаблицаОтбора Цикл
		ЗаполнитьЗначенияСвойств(Отбор, СтрокаТЗ);
		Отбор.СторнируемаяСтрока = Истина;
		НайденныеСтроки = НачисленияСотрудников.НайтиСтроки(Отбор);
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(НайденныеСтроки, НачисленияСписать);
	КонецЦикла;
	НачисленияСписать.Свернуть("Сотрудник,Регистратор,Начисление,СтатьяФинансирования,СтатьяРасходов,СпособОтраженияЗарплатыВБухучете,ИдентификаторСтрокиЗатрат","Сумма");
	
	// отбор для поиска по таблице ОписанияНачислений
	ОтборНачислений = Новый Структура("Начисление,ПорядокРасчета");
	// отбор для поиска строк в таблице ТаблицаПереносы
	ОтборСтрокПереноса = Новый Структура("ИдентификаторСтрокиЗатрат");
	// отбор для поиска строк в таблице ТекущиеДанныеОНачислениях
	ОтборТекущиеДанныеОНачислениях = Новый Структура("Сотрудник,Регистратор,СоставнаяЧасть,СтатьяФинансирования,СтатьяРасходов,СпособОтраженияЗарплатыВБухучете");
	// отбор для поиска строк в таблице ВремТаблицаДобавить
	ОтборСтрокДобавить = Новый Структура("Сотрудник,Регистратор,СоставнаяЧасть,СтатьяФинансирования,СтатьяРасходов,СпособОтраженияЗарплатыВБухучете");
	
	// описание таблицы по которой будут списываться затраты
	ОписаниеТаблицыСписать = НачисленияСписать.СкопироватьКолонки();
	ОписаниеТаблицыСписать.Колонки.Добавить("СоставнаяЧасть", Новый ОписаниеТипов("ПеречислениеСсылка.УчетНачисленийВСреднемЗаработкеОбщий"));
	
	// описание таблицы по которой будут добавляться затраты
	ОписаниеТаблицыДобавит = ОписаниеТаблицыСписать.СкопироватьКолонки();
	ОписаниеТаблицыДобавит.Колонки.Добавить("НоваяСтатьяФинансирования", Новый ОписаниеТипов("СправочникСсылка.СтатьиФинансированияЗарплата"));
	ОписаниеТаблицыДобавит.Колонки.Добавить("НоваяСтатьяРасходов", Новый ОписаниеТипов("СправочникСсылка.СтатьиРасходовЗарплата"));
	ОписаниеТаблицыДобавит.Колонки.Добавить("НовыйСпособОтраженияЗарплатыВБухучете", Новый ОписаниеТипов("СправочникСсылка.СпособыОтраженияЗарплатыВБухУчете"));
	
	Для каждого ПорядокРасчета Из ИспользуемыеПорядкиРасчета Цикл
		
		ОтборНачислений.ПорядокРасчета = ПорядокРасчета;
		ВремТаблицаСписать  = ОписаниеТаблицыСписать.СкопироватьКолонки();
		ВремТаблицаДобавить = ОписаниеТаблицыДобавит.СкопироватьКолонки();
		
		Для каждого СтрокаТЗ Из НачисленияСписать Цикл
			
			ОтборНачислений.Начисление = СтрокаТЗ.Начисление;
			НайденныеСтроки = ОписанияНачислений.НайтиСтроки(ОтборНачислений);
			СоставнаяЧасть = НайденныеСтроки[0].СоставнаяЧасть;
			
			НоваяСтрока = ВремТаблицаСписать.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаТЗ);
			НоваяСтрока.СоставнаяЧасть = СоставнаяЧасть;
			
			ОтборСтрокПереноса.ИдентификаторСтрокиЗатрат = СтрокаТЗ.ИдентификаторСтрокиЗатрат;
			СтрокиПереноса = ТаблицаПереносы.НайтиСтроки(ОтборСтрокПереноса);
			КоэффициентыПереноса = Новый Массив;
			Для каждого СтрокаПереноса Из СтрокиПереноса Цикл
				КоэффициентыПереноса.Добавить(СтрокаПереноса.Сумма);
			КонецЦикла;
			РезультатыПереноса = ЗарплатаКадрыКлиентСервер.РаспределитьПропорциональноКоэффициентам(НоваяСтрока.Сумма, КоэффициентыПереноса);
			Если РезультатыПереноса = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Индекс = 0;
			Для каждого СтрокаПереноса Из СтрокиПереноса Цикл
				НоваяСтрока = ВремТаблицаДобавить.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаТЗ);
				НоваяСтрока.НоваяСтатьяФинансирования = СтрокаПереноса.СтатьяФинансирования;
				НоваяСтрока.НоваяСтатьяРасходов 				  = СтрокаПереноса.СтатьяРасходов;
				НоваяСтрока.НовыйСпособОтраженияЗарплатыВБухучете = СтрокаПереноса.СпособОтраженияЗарплатыВБухучете;
				НоваяСтрока.СоставнаяЧасть = СоставнаяЧасть;
				НоваяСтрока.Сумма = -РезультатыПереноса[Индекс];
				Индекс = Индекс+1;
			КонецЦикла;
		
		КонецЦикла;
		
		ВремТаблицаСписать.Свернуть("Сотрудник,Регистратор,СоставнаяЧасть,СтатьяФинансирования,СтатьяРасходов,СпособОтраженияЗарплатыВБухучете","Сумма");
		ВремТаблицаДобавить.Свернуть("Сотрудник,Регистратор,СоставнаяЧасть,СтатьяФинансирования,СтатьяРасходов,СпособОтраженияЗарплатыВБухучете,НоваяСтатьяФинансирования,НоваяСтатьяРасходов,НовыйСпособОтраженияЗарплатыВБухучете","Сумма");
		
		Для каждого СтрокаТЗ Из ВремТаблицаСписать Цикл
			
			ЗаполнитьЗначенияСвойств(ОтборТекущиеДанныеОНачислениях, СтрокаТЗ);
			НайденныеСтроки = ТекущиеДанныеОНачислениях.НайтиСтроки(ОтборТекущиеДанныеОНачислениях);
			Коэффициенты = Новый Массив;
			СуммаТекущихДанных = 0;
			Для каждого СтрокаТекДанных Из НайденныеСтроки Цикл
				Коэффициенты.Добавить(СтрокаТекДанных.Сумма);
				СуммаТекущихДанных = СуммаТекущихДанных + СтрокаТекДанных.Сумма;
			КонецЦикла;
			
			// Если сумма из таблицы начислений меньше суммы списания, тогда надо будет дополнительно уменьшить суммы.
			НормироватьСумму = (?(СуммаТекущихДанных<0,-СуммаТекущихДанных,СуммаТекущихДанных) < ?(СтрокаТЗ.Сумма<0,-СтрокаТЗ.Сумма,СтрокаТЗ.Сумма));
			
			Если НормироватьСумму Тогда
				Если (СтрокаТЗ.Сумма<0 И СуммаТекущихДанных>0) Или (СтрокаТЗ.Сумма>0 И СуммаТекущихДанных<0) Тогда
					СуммаСписания = -СуммаТекущихДанных;
				Иначе
					СуммаСписания = СуммаТекущихДанных;
				КонецЕсли;
			Иначе
				СуммаСписания = СтрокаТЗ.Сумма;
			КонецЕсли;
			Результаты = ЗарплатаКадрыКлиентСервер.РаспределитьПропорциональноКоэффициентам(СуммаСписания, Коэффициенты);
			Если Результаты = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Индекс = 0;
			Для каждого СтрокаТекДанных Из НайденныеСтроки Цикл
				НоваяСтрока = НовыеДанныеОНачислениях.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаТекДанных);
				НоваяСтрока.СтатьяФинансирования 			 = СтрокаТЗ.СтатьяФинансирования;
				НоваяСтрока.СтатьяРасходов 					 = СтрокаТЗ.СтатьяРасходов;
				НоваяСтрока.СпособОтраженияЗарплатыВБухучете = СтрокаТЗ.СпособОтраженияЗарплатыВБухучете;
				НоваяСтрока.Сумма = Результаты[Индекс];
				Индекс = Индекс+1;
			КонецЦикла;
			
			ЗаполнитьЗначенияСвойств(ОтборСтрокДобавить, СтрокаТЗ);
			НайденныеСтрокиПереноса = ВремТаблицаДобавить.НайтиСтроки(ОтборСтрокДобавить);
			Если НормироватьСумму Тогда
				
				КоэффициентыПереноса = Новый Массив;
				Для каждого СтрокаПереноса Из НайденныеСтрокиПереноса Цикл
					КоэффициентыПереноса.Добавить(СтрокаПереноса.Сумма);
				КонецЦикла;
				
				Результаты = ЗарплатаКадрыКлиентСервер.РаспределитьПропорциональноКоэффициентам(СуммаТекущихДанных, КоэффициентыПереноса);
				Если Результаты = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				Индекс = 0;
				Для каждого СтрокаПереноса Из НайденныеСтрокиПереноса Цикл
					СтрокаПереноса.Сумма = Результаты[Индекс];
					Индекс = Индекс+1;
				КонецЦикла;
				
			КонецЕсли;
			
			Для каждого СтрокаПереноса Из НайденныеСтрокиПереноса Цикл
				
				Результаты = ЗарплатаКадрыКлиентСервер.РаспределитьПропорциональноКоэффициентам(СтрокаПереноса.Сумма, Коэффициенты);
				Если Результаты = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				Индекс = 0;
				Для каждого СтрокаТекДанных Из НайденныеСтроки Цикл
					НоваяСтрока = НовыеДанныеОНачислениях.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаТекДанных);
					НоваяСтрока.СтатьяФинансирования 			 = СтрокаПереноса.НоваяСтатьяФинансирования;
					НоваяСтрока.СтатьяРасходов 					 = СтрокаПереноса.НоваяСтатьяРасходов;
					НоваяСтрока.СпособОтраженияЗарплатыВБухучете = СтрокаПереноса.НовыйСпособОтраженияЗарплатыВБухучете;
					НоваяСтрока.Сумма = Результаты[Индекс];
					Индекс = Индекс+1;
				КонецЦикла;
				
			КонецЦикла;
		
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат НовыеДанныеОНачислениях;
	
КонецФункции

// Формирует сторно записи отменяющие движения исправленного документа по регистрам подсистемы.
//
// Параметры:
//  Движения			 - КоллекцияДвижений, Структура	 - Коллекция движений в которую будут добавлены сторно записи.
//  ИсправленныйДокумент - ДокументСсылка				 - Документ, записи которого необходимо сторнировать.
//  Записывать			 - Булево						 - Если Истина, то наборы будут записаны сразу, если Ложь, то наборам будет установлен признак Записывать = Истина.
//
Процедура СторнироватьДвиженияДокумента(Движения, ИсправленныйДокумент, Записывать = Ложь) Экспорт
	
	ИмяУчета = "УчетСреднегоЗаработка";
	МетаданныеРегистров = РегистрыСреднегоЗаработка();
	
	ДвиженияВСтруктуре = ТипЗнч(Движения) = Тип("Структура");
	Набор = Неопределено;
	
	Для Каждого МетаданныеРегистра Из МетаданныеРегистров Цикл
		
		Если ДвиженияВСтруктуре Тогда 
			Движения.Свойство(МетаданныеРегистра.Имя, Набор);
		Иначе 
			Набор = Движения.Найти(МетаданныеРегистра.Имя);
		КонецЕсли;
		
		Если Набор = Неопределено Или Не ИсправлениеДокументовЗарплатаКадры.ИзолироватьУчетом(Набор, ИмяУчета) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ОбщегоНазначения.ЭтоРегистрНакопления(МетаданныеРегистра) Тогда
			ИсправлениеДокументовЗарплатаКадры.СторнироватьДвиженияВРегистреНакопления(Набор, ИсправленныйДокумент, МетаданныеРегистра, Записывать);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗарегистрироватьДанныеКоэффициентовИндексации(Движения, КоэффициентыИндексации) Экспорт
	
	Движения.КоэффициентИндексацииЗаработка.Загрузить(КоэффициентыИндексации);
	Движения.КоэффициентИндексацииЗаработка.Записывать = Истина;
	
КонецПроцедуры

Процедура ЗарегистрироватьДанныеКоэффициентовПовышенияЗаработка(Движения, КоэффициентыИндексации) Экспорт
	
	Движения.КоэффициентыПовышенияЗаработкаУчитываемыеПриИндексации.Загрузить(КоэффициентыИндексации);
	Движения.КоэффициентыПовышенияЗаработкаУчитываемыеПриИндексации.Записывать = Истина;
	
КонецПроцедуры

// Создает временную таблицу ВТИсключаемыеВПериодКомандировки, если есть исключаемые периоды,
// будет создана таблица ВТПериодыКомандировок.
//
// 	Параметры:
// 		МенеджерВременныхТаблиц - менеджер временных таблиц, должен содержать таблицу ВТНачисления
// 									с полями Сотрудник,Начисление,ПериодДействия
// 		ИсключатьНачисленияВПериодКомандировок - Булево, переменная для оповещение вызывающей стороны о результатах работы.
//
Процедура СоздатьВТДляИсключенияКомандировок(МенеджерВременныхТаблиц, ИсключатьНачисленияВПериодКомандировок) Экспорт
	
	УдалитьВТ = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("КатегорииПрогула", ПланыВидовРасчета.Начисления.КатегорииПрогула());
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВидыКомандировок.Ссылка,
		|	Начисления.Начисление КАК ВидРасчета,
		|	ВЫБОР
		|		КОГДА ИсключаемыеВПериодКомандировки.ЗачетОтработанногоВремени
		|				ИЛИ ИсключаемыеВПериодКомандировки.КатегорияНачисленияИлиНеоплаченногоВремени В (&КатегорииПрогула)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ИсключатьВремя
		|ПОМЕСТИТЬ ВТИсключаемыеВПериодКомандировки
		|ИЗ
		|	ВТНачисления КАК Начисления
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления КАК ВидыКомандировок
		|		ПО (ВидыКомандировок.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ОплатаКомандировки)
		|			ИЛИ ВидыКомандировок.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ДенежноеСодержаниеНаПериодКомандировки)
		|			ИЛИ ВидыКомандировок.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ОплатаПоСреднемуЗаработку))
		|			И (ВидыКомандировок.ЗачетНормыВремени)
		|			И (ВидыКомандировок.Ссылка <> Начисления.Начисление)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления КАК ИсключаемыеВПериодКомандировки
		|		ПО (ИсключаемыеВПериодКомандировки.Ссылка = Начисления.Начисление)
		|			И (ИсключаемыеВПериодКомандировки.ТребуетсяРасчетВремени)
		|			И (НЕ ИСТИНА В
		|					(ВЫБРАТЬ ПЕРВЫЕ 1
		|						ИСТИНА
		|					ИЗ
		|						ПланВидовРасчета.Начисления.ВытесняющиеВидыРасчета КАК ВытесняющиеВидыРасчета
		|					ГДЕ
		|						ВытесняющиеВидыРасчета.Ссылка = Начисления.Начисление
		|						И ВытесняющиеВидыРасчета.ВидРасчета = ВидыКомандировок.Ссылка))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА КАК Поле1
		|ИЗ
		|	ВТИсключаемыеВПериодКомандировки КАК ИсключаемыеВПериодКомандировки";
		
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ИсключатьНачисленияВПериодКомандировок = Ложь;
		Возврат;
	КонецЕсли;
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ФПДКомандировки.Регистратор КАК Регистратор,
		|	ФПДКомандировки.НомерСтроки КАК НомерСтроки,
		|	ФПДКомандировки.Сотрудник КАК Сотрудник,
		|	ФПДКомандировки.ВидРасчета КАК ВидРасчета,
		|	ФПДКомандировки.ПериодДействияНачало КАК Начало,
		|	ФПДКомандировки.ПериодДействияКонец КАК Окончание,
		|	ФПДКомандировки.СторнируемыйДокумент КАК СторнируемыйДокумент
		|ПОМЕСТИТЬ ВТПериодыКомандировокПредварительно
		|ИЗ
		|	РегистрРасчета.Начисления.ФактическийПериодДействия(
		|			(Сотрудник, ПериодДействия) В
		|					(ВЫБРАТЬ
		|						Начисления.Сотрудник,
		|						Начисления.ПериодДействия
		|					ИЗ
		|						ВТНачисления КАК Начисления)
		|				И ВидРасчета В
		|					(ВЫБРАТЬ
		|						ВидыКомандировок.Ссылка
		|					ИЗ
		|						ВТИсключаемыеВПериодКомандировки КАК ВидыКомандировок)
		|				И СторноТекущегоПериода = НЕОПРЕДЕЛЕНО) КАК ФПДКомандировки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПериодыКомандировокСторно.СторнируемыйДокумент КАК СторнируемыйДокумент,
		|	ПериодыКомандировокСторно.Сотрудник КАК Сотрудник     
		|ПОМЕСТИТЬ ВТСторнируемыеДокументы
		|ИЗ
		|	ВТПериодыКомандировокПредварительно КАК ПериодыКомандировокСторно
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПериодыКомандировокПредварительно КАК ПериодыКомандировокПредварительно
		|		ПО ПериодыКомандировокСторно.СторнируемыйДокумент = ПериодыКомандировокПредварительно.Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПериодыКомандировокПредварительно.Регистратор КАК Регистратор,
		|	ПериодыКомандировокПредварительно.НомерСтроки КАК НомерСтроки,
		|	ПериодыКомандировокПредварительно.Сотрудник КАК Сотрудник,
		|	ПериодыКомандировокПредварительно.ВидРасчета КАК ВидРасчета,
		|	ПериодыКомандировокПредварительно.Начало КАК Начало,
		|	ПериодыКомандировокПредварительно.Окончание КАК Окончание
		|ПОМЕСТИТЬ ВТПериодыКомандировок
		|ИЗ
		|	ВТПериодыКомандировокПредварительно КАК ПериодыКомандировокПредварительно
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСторнируемыеДокументы КАК СторнируемыеДокументы
		|		ПО ПериодыКомандировокПредварительно.Регистратор = СторнируемыеДокументы.СторнируемыйДокумент
		|			И ПериодыКомандировокПредварительно.Сотрудник = СторнируемыеДокументы.Сотрудник             
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСторнируемыеДокументы КАК СторнирующиеДокументы
		|		ПО ПериодыКомандировокПредварительно.СторнируемыйДокумент = СторнирующиеДокументы.СторнируемыйДокумент
		|			И ПериодыКомандировокПредварительно.Сотрудник = СторнирующиеДокументы.Сотрудник            
		|ГДЕ
		|	СторнируемыеДокументы.СторнируемыйДокумент ЕСТЬ NULL
		|	И СторнирующиеДокументы.СторнируемыйДокумент ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА КАК Поле1
		|ИЗ
		|	ВТПериодыКомандировок КАК ФПДКомандировки";
		
	РезультатЗапроса = Запрос.Выполнить();
	
	УдалитьВТ.Добавить("ВТПериодыКомандировокПредварительно");
	УдалитьВТ.Добавить("ВТСторнируемыеДокументы");
	ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, УдалитьВТ);
	
	Если РезультатЗапроса.Пустой() Тогда
		ИсключатьНачисленияВПериодКомандировок = Ложь;
		Возврат;
	КонецЕсли;
	
	ИсключатьНачисленияВПериодКомандировок = Истина;
	
КонецПроцедуры

Функция ЗначенияПоказателейСреднегоЗаработка(Сотрудник, ДатаНачалаСобытия, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ДополнительныеПараметры = Неопределено Тогда
		ДополнительныеПараметры = УчетСреднегоЗаработкаКлиентСервер.ДополнительныеПараметрыРасчетаСреднегоЗаработка();
	КонецЕсли;
	
	ДополнительныеПараметры.ДатаНачалаСобытия = ДатаНачалаСобытия;
	
	Если Не ЗначениеЗаполнено(ДополнительныеПараметры.НачалоПериода) 
		Или Не ЗначениеЗаполнено(ДополнительныеПараметры.ОкончаниеПериода) Тогда
		ПериодРасчета = ПериодРасчетаОбщегоСреднегоЗаработкаСотрудника(
			ДатаНачалаСобытия, 
			Сотрудник, 
			ДополнительныеПараметры.Начисление);
		ДополнительныеПараметры.НачалоПериода = ПериодРасчета.ДатаНачала;
		ДополнительныеПараметры.ОкончаниеПериода = ПериодРасчета.ДатаОкончания;
	КонецЕсли;
		
	Если ДополнительныеПараметры.ПорядокРасчета = Неопределено Тогда
		ДополнительныеПараметры.ПорядокРасчета = УчетСреднегоЗаработкаКлиентСервер.ПорядокРасчетаОбщегоСреднегоЗаработка(ДатаНачалаСобытия);
	КонецЕсли;
	
	ДанныеДляРасчета = ДанныеДляРасчетаОбщегоСреднегоЗаработкаСотрудникаСлужебный(
		Сотрудник, 
		ДатаНачалаСобытия, 
		ДополнительныеПараметры.НачалоПериода, 
		ДополнительныеПараметры.ОкончаниеПериода, 
		ДополнительныеПараметры.ПорядокРасчета);
		
	ДополнительныеПараметры.Индексации = ДанныеДляРасчета.ДанныеОбИндексации;
	
	ЗначенияПоказателей = УчетСреднегоЗаработкаКлиентСервер.ЗначенияПоказателейСреднегоЗаработкаОбщего(ДанныеДляРасчета.ДанныеОНачислениях, ДанныеДляРасчета.ДанныеОВремени, ДополнительныеПараметры);
	
	Возврат ЗначенияПоказателей;
	
КонецФункции

Процедура ДобавитьЗначенияПоказателейСреднегоЗаработкаОтпусков(МенеджерРасчета, ДанныеСреднегоЗаработка, СтрокаНачислений, ПоКалендарнымДням)  Экспорт
	
	Если ДанныеСреднегоЗаработка = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если ПоКалендарнымДням Тогда
		ЗначениеПоказателяИндексируемый = ДанныеСреднегоЗаработка.ИндексируемыйПоКалендарнымДням;
		ЗначениеПоказателяНеиндексируемый = ДанныеСреднегоЗаработка.НеиндексируемыйПоКалендарнымДням;
	Иначе
		ЗначениеПоказателяИндексируемый = ДанныеСреднегоЗаработка.ИндексируемыйПоРабочимДням;
		ЗначениеПоказателяНеиндексируемый = ДанныеСреднегоЗаработка.НеиндексируемыйПоРабочимДням;
	КонецЕсли;
	МенеджерРасчета.ДобавитьИзвестноеЗначениеПоказателя(СтрокаНачислений, ДанныеСреднегоЗаработка.ПоказательИндексируемыйЗаработок, ЗначениеПоказателяИндексируемый);
	МенеджерРасчета.ДобавитьИзвестноеЗначениеПоказателя(СтрокаНачислений, ДанныеСреднегоЗаработка.ПоказательНеиндексируемыйЗаработок, ЗначениеПоказателяНеиндексируемый);
	
КонецПроцедуры

Процедура ДобавитьЗначенияПоказателейЧастичнойИндексации(Объект, МенеджерРасчета, СтрокаНачислений) Экспорт 
	
	ПоказательИндексируемыйЗаработок = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ПоказателиРасчетаЗарплаты.СреднийЗаработокИндексируемый");
	ПоказательНеиндексируемыйЗаработок = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ПоказателиРасчетаЗарплаты.СреднийЗаработокНеиндексируемый");
	
	МенеджерРасчета.ДобавитьИзвестноеЗначениеПоказателя(СтрокаНачислений, 
		ПоказательИндексируемыйЗаработок, 
		Объект.СреднийЗаработокИндексируемый);
		
	МенеджерРасчета.ДобавитьИзвестноеЗначениеПоказателя(СтрокаНачислений, 
		ПоказательНеиндексируемыйЗаработок, 
		Объект.СреднийЗаработокНеиндексируемый);
	
КонецПроцедуры

Процедура ЗаполнитьДанныеЧастичнойИндексацииОтпусков(Объект, ЗначенияПоказателей, ИмяРеквизитаСреднийЗаработок) Экспорт 
	
	ИмяРеквизитаИндексируемый = СтрЗаменить(ИмяРеквизитаСреднийЗаработок, "СреднийЗаработок", "СреднийЗаработокИндексируемый");
	ИмяРеквизитаНеиндексируемый = СтрЗаменить(ИмяРеквизитаСреднийЗаработок, "СреднийЗаработок", "СреднийЗаработокНеиндексируемый");
	Объект[ИмяРеквизитаИндексируемый] = ЗначенияПоказателей.СреднийЗаработокИндексируемый;
	Объект[ИмяРеквизитаНеиндексируемый] = ЗначенияПоказателей.СреднийЗаработокНеиндексируемый;
	
КонецПроцедуры

Процедура ДобавитьЗначенияПоказателейЧастичнойИндексацииОтпусков(Объект, МенеджерРасчета, СтрокаНачислений, ПоКалендарнымДням) Экспорт 
	
	ПоказательИндексируемыйЗаработок = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ПоказателиРасчетаЗарплаты.СреднийЗаработокИндексируемый");
	ПоказательНеиндексируемыйЗаработок = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ПоказателиРасчетаЗарплаты.СреднийЗаработокНеиндексируемый");
	
	Если ПоКалендарнымДням Тогда 
		МенеджерРасчета.ДобавитьИзвестноеЗначениеПоказателя(СтрокаНачислений, 
			ПоказательИндексируемыйЗаработок, 
			Объект.СреднийЗаработокИндексируемый);
		МенеджерРасчета.ДобавитьИзвестноеЗначениеПоказателя(СтрокаНачислений, 
			ПоказательНеиндексируемыйЗаработок, 
			Объект.СреднийЗаработокНеиндексируемый);
	Иначе 
		МенеджерРасчета.ДобавитьИзвестноеЗначениеПоказателя(СтрокаНачислений, 
			ПоказательИндексируемыйЗаработок, 
			Объект.СреднийЗаработокИндексируемыйПоРабочимДням);
		МенеджерРасчета.ДобавитьИзвестноеЗначениеПоказателя(СтрокаНачислений, 
			ПоказательНеиндексируемыйЗаработок, 
			Объект.СреднийЗаработокНеиндексируемыйПоРабочимДням);
	КонецЕсли;
	
КонецПроцедуры

#Область ПолучениеДанныхДляРасчетаСреднегоЗаработка

// Возвращает структуру содержащую таблицы значений
// с данными о начислениях, отработанном времени и
// коэффициентах для расчета среднего заработка.
//      	 
// Параметры:
//      Сотрудник 
//		НачалоПериода - начало периода расчета среднего заработка.
//		ОкончаниеПериода - окончание периода расчета среднего заработка.
//		ПорядокРасчета - правила расчета общего среднего заработка.
//		ОтборМесяцев - необязательный, массив дат начала месяцев, 
//			если указан, данные будут получены только за эти месяцы.
//
// Возвращаемое значение:
// 		ДанныеДляРасчетаСреднего - структура, содержащая следующие поля:
//		ДанныеОНачислениях - таблица значений с колонками.
//			Сотрудник
//			Период
//			ПорядокРасчета
//			СоставнаяЧасть
//			Индексируется
//			Сумма
//			Год
//			ДатаНачалаБазовогоПериода
//			КоличествоМесяцев.
//
//		ДанныеОВремени - таблица значений с колонками.
//			Сотрудник
//			Период
//			ПорядокРасчета
//			ОтработаноДнейПятидневка
//			ОтработаноЧасовПятидневка
//			НормаДнейПроизводственныйКалендарь
//			НормаЧасовПроизводственныйКалендарь
//			ОтработаноДнейКалендарных
//			ОтработаноДней
//			ОтработаноДнейШестидневка.
//
//		ДанныеОбИндексации - таблица значений с колонками.
//			Сотрудник
//			Период
//			КоэффициентИндексации 
//
Функция ДанныеДляРасчетаОбщегоСреднегоЗаработкаСотрудникаСлужебный(Сотрудник, ДатаНачалаСобытия, НачалоПериода, ОкончаниеПериода, ПорядокРасчета, ОтборМесяцев = Неопределено, ИсключаемыйРегистратор = Неопределено, УчитыватьКорректировки = Истина) Экспорт
	
	ИсходныеДанные = ПустаяТаблицаИсходныхДанныхРасчетаОбщегоСреднегоЗаработка();
	
	СтрокаТаблицы = ИсходныеДанные.Добавить();
	СтрокаТаблицы.Сотрудник = Сотрудник;
	СтрокаТаблицы.ПорядокРасчета = ПорядокРасчета;
	СтрокаТаблицы.ДатаНачалаСобытия = ДатаНачалаСобытия;
	СтрокаТаблицы.НачалоПериодаРасчетаСреднего = НачалоПериода;
	СтрокаТаблицы.ОкончаниеПериодаРасчетаСреднего = ОкончаниеПериода;
	
	Возврат ДанныеДляРасчетаОбщегоСреднегоЗаработкаСотрудниковСлужебный(ИсходныеДанные, ОтборМесяцев, ИсключаемыйРегистратор, УчитыватьКорректировки);
	
КонецФункции

#Область ИсключениеДублирующихПремий

Функция ИмяВариантаИсключенияПремийБезИсключения() Экспорт
	Возврат "БезИсключения";
КонецФункции

Функция ИмяВариантаИсключенияПремийТиповаяПолитика() Экспорт
	Возврат "Типовая";
КонецФункции

Функция ОписаниеПолитикиИсключенияДублирующихПремий(ИмяВарианта) Экспорт
	
	Если ИмяВарианта = ИмяВариантаИсключенияПремийТиповаяПолитика() Тогда
		Описание = ОписаниеВариантаПолитикиИсключенияДублирующихПремий();
		
		Описание.Описание = НСтр("ru = 'Учитываются только премии за разные достижения.';
								|en = 'Only bonuses for various achievements are taken into account.'");
		Описание.ПодробноеОписание =
			НСтр("ru = 'Некоторые премии исключаются из среднего заработка в соответствии с п. 15 Постановления № 922 ""Об особенностях порядка исчисления средней заработной платы"".
					   |1. Исключаются дублирующие премии, т.е. премии начисленные за одно и тоже (один и тот же показатель за один и тот же период работы).
					   |Исключается минимальная премия из дублирующихся.
					   |2. Премии без показателя премирования не исключаются.';
					   |en = 'Some bonuses are excluded from average earnings in accordance with paragraph 15 of Ordinance No. 922 ""On the Features of the Average Salary Calculation Procedure.""
					   |1. Duplicate bonuses are excluded, i.e. bonuses accrued for the same (the same indicator for the same period of work).
					   |The minimum bonus from duplicate ones is excluded.
					   |2. Bonuses without a bonus indicator are not excluded.'");
		
	ИначеЕсли ИмяВарианта = ИмяВариантаИсключенияПремийБезИсключения() Тогда
		Описание = ОписаниеВариантаПолитикиИсключенияДублирующихПремий();
		Описание.Описание = НСтр("ru = 'Учитываются все премии';
								|en = 'All bonuses are taken into account'");
		Описание.ПодробноеОписание =
			НСтр("ru = 'Дублирующие премии не исключаются.
					   |Для видов расчета не заданы показатели премирования.';
					   |en = 'Duplicate bonuses are not excluded.
					   |Bonus indicators are not specified for calculation types.'");
		
	Иначе
		Описание = Неопределено;
	КонецЕсли;
	
	Возврат Описание;
	
КонецФункции

Функция ИмяПолитикиИсключенияДублирующихПремий() Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Начисления.Ссылка КАК Ссылка
		|ИЗ
		|	ПланВидовРасчета.Начисления КАК Начисления
		|ГДЕ
		|	Начисления.ПоказательПремирования <> ЗНАЧЕНИЕ(Справочник.ПоказателиПремирования.ПустаяСсылка)");
	
	Если Запрос.Выполнить().Пустой() Тогда
		ИмяВарианта = ИмяВариантаИсключенияПремийБезИсключения();
	Иначе
		ИмяВарианта = ИмяВариантаИсключенияПремийТиповаяПолитика();
	КонецЕсли;
	
	Возврат ИмяВарианта;
	
	// Это точка подключения расширения.
	
КонецФункции

#КонецОбласти

// Возвращает структуру, содержащую таблицы значений с данными о начислениях, 
// отработанном времени и коэффициентах индексации для расчета среднего заработка.
//      	 
// Параметры:
//      ДокументСсылка - ссылка на документ расчета.
//		ТаблицаСотрудники - таблица значений с колонками.
//			Сотрудник: должно быть непустым
//			ДатаНачалаСобытия
//          НачалоПериодаРасчетаСреднего
//			ОкончаниеПериодаРасчетаСреднего.
//
// Возвращаемое значение:
// 		ДанныеДляРасчетаСреднего - структура, содержащая следующие поля:
//		ДанныеОНачислениях - таблица значений с колонками.
//			Сотрудник
//			Период
//			ПорядокРасчета
//			СоставнаяЧасть
//			Индексируется
//			Сумма
//			Год
//			ДатаНачалаБазовогоПериода
//			КоличествоМесяцев.
//
//		ДанныеОВремени - таблица значений с колонками.
//			Сотрудник
//			Период
//			ПорядокРасчета
//			ОтработаноДнейПятидневка
//			ОтработаноЧасовПятидневка
//			НормаДнейПроизводственныйКалендарь
//			НормаЧасовПроизводственныйКалендарь
//			ОтработаноДнейКалендарных
//			ОтработаноДней
//			ОтработаноДнейШестидневка.
//
//		ДанныеОбИндексации - таблица значений с колонками.
//			Сотрудник
//			Период
//			КоэффициентИндексации 
//
Функция ДанныеДляРасчетаОбщегоСреднегоЗаработкаСотрудниковСлужебный(ТаблицаСотрудники, ОтборМесяцев = Неопределено, ИсключаемыйРегистратор = Неопределено, УчитыватьКорректировки = Истина) Экспорт
	
	Перем МинимальнаяДатаНачала;
	Перем МаксимальнаяДатаОкончания;
	
	МесяцыРасчетаСотрудников = Новый ТаблицаЗначений;
	МесяцыРасчетаСотрудников.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	МесяцыРасчетаСотрудников.Колонки.Добавить("ПорядокРасчета", Новый ОписаниеТипов("ПеречислениеСсылка.ПорядокРасчетаСреднегоЗаработкаОбщий"));
	МесяцыРасчетаСотрудников.Колонки.Добавить("ПорядокРасчетаСреднегоОтбор", Новый ОписаниеТипов("ПеречислениеСсылка.ПорядокРасчетаСреднегоЗаработкаОбщий"));
	МесяцыРасчетаСотрудников.Колонки.Добавить("Месяц", Новый ОписаниеТипов("Дата"));
	
	// Определяем границы расчета среднего.
	НайтиГраницыПериодаРасчетаОбщегоСреднегоЗаработка(ТаблицаСотрудники, МинимальнаяДатаНачала, МаксимальнаяДатаОкончания);
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТПериоды(МенеджерВременныхТаблиц, МинимальнаяДатаНачала, МаксимальнаяДатаОкончания);
	СоздатьВТПериодыРасчетаСреднегоСотрудников(МенеджерВременныхТаблиц, ТаблицаСотрудники, ОтборМесяцев);
	
	// Данные о начислениях.
	ДанныеНачислений = ДанныеОНачисленияхСреднегоЗаработка(
		МенеджерВременныхТаблиц,
		МинимальнаяДатаНачала,
		МаксимальнаяДатаОкончания,
		ИсключаемыйРегистратор,
		УчитыватьКорректировки);
		
	// Данные отработанного времени.
	ДанныеВремени = ДанныеОтработанногоВремени(
		МенеджерВременныхТаблиц,
		МинимальнаяДатаНачала,
		МаксимальнаяДатаОкончания,
		ИсключаемыйРегистратор,
		УчитыватьКорректировки);
	
	ДанныеИндексации = ДанныеИндексации(ТаблицаСотрудники);
	
	ДанныеДляРасчета = Новый Структура(
		"ДанныеОНачислениях, 
		|ДанныеОВремени, 
		|ДанныеОбИндексации,
		|ДанныеОПремиях");
		
	ДанныеДляРасчета.ДанныеОНачислениях = ДанныеНачислений.ДанныеОНачислениях;
	ДанныеДляРасчета.ДанныеОВремени = ДанныеВремени;
	ДанныеДляРасчета.ДанныеОбИндексации = ДанныеИндексации;
	ДанныеДляРасчета.ДанныеОПремиях = ДанныеНачислений.ДанныеОПремиях;
	
	Возврат ДанныеДляРасчета;
	
КонецФункции

Функция ДанныеОНачисленияхСреднегоЗаработкаДляАнализа(ТаблицаСотрудники, ДатаНачала, ДатаОкончания, ИсключаемыйРегистратор = Неопределено) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТПериоды(МенеджерВременныхТаблиц, ДатаНачала, ДатаОкончания);
	СоздатьВТПериодыРасчетаСреднегоСотрудников(МенеджерВременныхТаблиц, ТаблицаСотрудники);
	
	Результат = ДанныеОНачисленияхСреднегоЗаработка(
		МенеджерВременныхТаблиц,
		ДатаНачала,
		ДатаОкончания,
		ИсключаемыйРегистратор);
		
	Возврат Результат;
	
КонецФункции

// Получает таблицу коэффициентов по месяцам, 
// которые необходимо применить к среднему заработку из-за проводимой индексации заработка.
//
// Параметры:
//	ИсходныеДанные - таблица значений, 
//		созданная конструктором см. ПустаяТаблицаИсходныхДанныхРасчетаОбщегоСреднегоЗаработка
//	КоэффициентУчитываетПоследующиеИндексации 
//		- если Истина, коэффициент на конкретный месяц учитывает все индексации произошедшие ПОСЛЕ него (для расчета
//		показателя КоэффициентИндексации)
//		- если Ложь, коэффициент на конкретный месяц учитывает все индексации произошедшие ДО него.
//
Функция ДанныеИндексации(ИсходныеДанные, КоэффициентУчитываетПоследующиеИндексации = Ложь) Экспорт
	
	ДанныеИндексации = Новый ТаблицаЗначений;
	ДанныеИндексации.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	ДанныеИндексации.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	ДанныеИндексации.Колонки.Добавить("КоэффициентИндексации", Новый ОписаниеТипов("Число"));
	
	Если ИсходныеДанные.Количество() = 0 Тогда
		Возврат ДанныеИндексации;
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИсходныеДанные.Сотрудник,
		|	ИсходныеДанные.НачалоПериодаРасчетаСреднего,
		|	ИсходныеДанные.ОкончаниеПериодаРасчетаСреднего,
		|	ИсходныеДанные.ДатаНачалаСобытия,
		|	ИсходныеДанные.ПорядокРасчета
		|ПОМЕСТИТЬ ВТИсходныеДанные
		|ИЗ
		|	&ИсходныеДанные КАК ИсходныеДанные
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МИНИМУМ(ИсходныеДанные.НачалоПериодаРасчетаСреднего) КАК НачалоПервогоИнтервала,
		|	МАКСИМУМ(ИсходныеДанные.ОкончаниеПериодаРасчетаСреднего) КАК ОкончаниеПоследнегоИнтервала
		|ИЗ
		|	ВТИсходныеДанные КАК ИсходныеДанные";
	
	Запрос.УстановитьПараметр("ИсходныеДанные", ИсходныеДанные);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ДанныеИндексации;
	КонецЕсли;

	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	// Создаем ВТ с месяцами
	ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТПериоды(МенеджерВременныхТаблиц, Выборка.НачалоПервогоИнтервала, Выборка.ОкончаниеПоследнегоИнтервала);
	
	// Выбираем коэффициенты индексации заработка: 
	// - для индексации выплат, учитываемых при расчете показателя среднего заработка, 
	// для каждого месяца выбираем коэффициенты, всех индексаций, случившихся ДО него
	// - для расчета показателя КоэффициентИндексации - для каждого месяца 
	// выборка коэффициентов всех индексаций, случившихся ПОСЛЕ него.
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ИсходныеДанные.Сотрудник КАК Сотрудник,
		|	ИсходныеДанные.НачалоПериодаРасчетаСреднего КАК НачалоПериодаРасчетаСреднего,
		|	КоэффициентИндексацииЗаработка.Период КАК Период,
		|	КоэффициентИндексацииЗаработка.Коэффициент КАК Коэффициент
		|ПОМЕСТИТЬ ВТИндексацияЗаработка
		|ИЗ
		|	ВТИсходныеДанные КАК ИсходныеДанные
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КоэффициентИндексацииЗаработка КАК КоэффициентИндексацииЗаработка
		|		ПО ИсходныеДанные.Сотрудник = КоэффициентИндексацииЗаработка.Сотрудник
		|			И (КоэффициентИндексацииЗаработка.Период МЕЖДУ ИсходныеДанные.НачалоПериодаРасчетаСреднего И ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ИсходныеДанные.ДатаНачалаСобытия, МЕСЯЦ), СЕКУНДА, -1))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	КоэффициентыПовышения.Сотрудник КАК Сотрудник,
		|	КоэффициентыПовышения.Период КАК Период,
		|	КоэффициентыПовышения.Коэффициент КАК Коэффициент
		|ПОМЕСТИТЬ ВТКоэффициентыИндексации
		|ИЗ
		|	ВТИндексацияЗаработка КАК ИндексацияЗаработка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КоэффициентыПовышенияЗаработкаУчитываемыеПриИндексации КАК КоэффициентыПовышения
		|		ПО ИндексацияЗаработка.Сотрудник = КоэффициентыПовышения.Сотрудник
		|			И (КоэффициентыПовышения.Период МЕЖДУ ИндексацияЗаработка.НачалоПериодаРасчетаСреднего И ИндексацияЗаработка.Период)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ИндексацияЗаработка.Сотрудник,
		|	ИндексацияЗаработка.Период,
		|	ИндексацияЗаработка.Коэффициент
		|ИЗ
		|	ВТИндексацияЗаработка КАК ИндексацияЗаработка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИсходныеДанные.Сотрудник КАК Сотрудник,
		|	Месяцы.Период КАК Период,
		|	ЕСТЬNULL(КоэффициентИндексацииЗаработка.Коэффициент, 1) КАК КоэффициентИндексации
		|ИЗ
		|	ВТИсходныеДанные КАК ИсходныеДанные
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПериоды КАК Месяцы
		|		ПО (Месяцы.Период МЕЖДУ ИсходныеДанные.НачалоПериодаРасчетаСреднего И ИсходныеДанные.ОкончаниеПериодаРасчетаСреднего)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКоэффициентыИндексации КАК КоэффициентИндексацииЗаработка
		|		ПО ИсходныеДанные.Сотрудник = КоэффициентИндексацииЗаработка.Сотрудник
		|			И (Месяцы.Период >= НАЧАЛОПЕРИОДА(КоэффициентИндексацииЗаработка.Период, МЕСЯЦ))
		|			И (КоэффициентИндексацииЗаработка.Период >= ИсходныеДанные.ДатаНачалаСобытия)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Сотрудник,
		|	Период УБЫВ
		|ИТОГИ ПО
		|	Сотрудник,
		|	Период";
	
	Если Не КоэффициентУчитываетПоследующиеИндексации Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, ">=", "<");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Период УБЫВ", "Период ВОЗР");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	// Обходя результаты запроса агрегируем коэффициенты.
	
	ВыборкаПоСотрудникам = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоСотрудникам.Следующий() Цикл
		ВыборкаПоМесяцам = ВыборкаПоСотрудникам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПоМесяцам.Следующий() Цикл
			Коэффициент = 1;
			Выборка = ВыборкаПоМесяцам.Выбрать();
			Пока Выборка.Следующий() Цикл
				Коэффициент = Коэффициент * Выборка.КоэффициентИндексации;
			КонецЦикла;
			Если Коэффициент < 1 Тогда 
				Коэффициент = 1;
			КонецЕсли;
			СтрокаИндексации = ДанныеИндексации.Добавить();
			СтрокаИндексации.Сотрудник = ВыборкаПоСотрудникам.Сотрудник;
			СтрокаИндексации.Период = ВыборкаПоМесяцам.Период;
			СтрокаИндексации.КоэффициентИндексации = Коэффициент;
		КонецЦикла;
	КонецЦикла;
	
	Возврат ДанныеИндексации;
	
КонецФункции

// По переданному массиву ссылок возвращает структуру, содержащую таблицы значений с данными о начислениях, 
// отработанном времени и коэффициентах индексации для расчета среднего заработка.
//
Функция ТаблицыДанныхОСреднемЗаработке(ИмяДокумента, МассивСсылок) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДокументДанныеОЗаработке.Ссылка,
		|	ДокументДанныеОЗаработке.Сотрудник,
		|	ДокументДанныеОЗаработке.ПорядокРасчета,
		|	ДокументДанныеОЗаработке.СоставнаяЧасть,
		|	ДокументДанныеОЗаработке.СтатьяФинансирования,
		|	ДокументДанныеОЗаработке.СпособОтраженияЗарплатыВБухучете,
		|	ДокументДанныеОЗаработке.СтатьяРасходов,
		|	ДокументДанныеОЗаработке.ОблагаетсяЕНВД,
		|	ДокументДанныеОЗаработке.Период,
		|	ДокументДанныеОЗаработке.Индексируется,
		|	ДокументДанныеОЗаработке.Сумма,
		|	ДокументДанныеОЗаработке.Источник,
		|	ДокументДанныеОЗаработке.Год,
		|	ДокументДанныеОЗаработке.ДатаНачалаБазовогоПериода,
		|	ДокументДанныеОЗаработке.КоличествоМесяцев,
		|	ДокументДанныеОЗаработке.ИсключеннаяЧасть
		|ИЗ
		|	Документ.#ИмяДокумента#.СреднийЗаработокОбщий КАК ДокументДанныеОЗаработке
		|ГДЕ
		|	ДокументДанныеОЗаработке.Ссылка В (&МассивСсылок)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДокументОтработанноеВремя.Ссылка,
		|	ДокументОтработанноеВремя.Сотрудник,
		|	ДокументОтработанноеВремя.ПорядокРасчета,
		|	ДокументОтработанноеВремя.Период,
		|	ДокументОтработанноеВремя.ОтработаноДней,
		|	ДокументОтработанноеВремя.ОтработаноДнейПятидневка,
		|	ДокументОтработанноеВремя.ОтработаноЧасов,
		|	ДокументОтработанноеВремя.ОтработаноДнейШестидневка,
		|	ДокументОтработанноеВремя.ОтработаноДнейКалендарных,
		|	ДокументОтработанноеВремя.НормаДнейПроизводственныйКалендарь,
		|	ДокументОтработанноеВремя.ОтработаноЧасовПятидневка,
		|	ДокументОтработанноеВремя.НормаЧасовПроизводственныйКалендарь,
		|	ДокументОтработанноеВремя.Источник
		|ИЗ
		|	Документ.#ИмяДокумента#.ОтработанноеВремяДляСреднегоОбщий КАК ДокументОтработанноеВремя
		|ГДЕ
		|	ДокументОтработанноеВремя.Ссылка В (&МассивСсылок)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДокументДанныеОбИндексации.Ссылка,
		|	ДокументДанныеОбИндексации.Сотрудник,
		|	ДокументДанныеОбИндексации.Период,
		|	ДокументДанныеОбИндексации.КоэффициентИндексации
		|ИЗ
		|	Документ.#ИмяДокумента#.ДанныеОбИндексации КАК ДокументДанныеОбИндексации
		|ГДЕ
		|	ДокументДанныеОбИндексации.Ссылка В (&МассивСсылок)";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ИмяДокумента#", ИмяДокумента);	
	
	Запрос.Текст = ТекстЗапроса;
	
	Результат = Запрос.ВыполнитьПакет();
	
	ДанныеОНачислениях 	= Результат[0].Выгрузить();
	ДанныеВремени 		= Результат[1].Выгрузить();
	ДанныеИндексации	= Результат[2].Выгрузить();
	ДанныеДляРасчета 	= Новый Структура("ДанныеОНачислениях,ДанныеОВремени,ДанныеОбИндексации", ДанныеОНачислениях, ДанныеВремени, ДанныеИндексации);
	
	Возврат ДанныеДляРасчета;
	
КонецФункции	

// Создает новую таблицу значений и копирует в нее данные отобранные по значению в указанном поле.
//
Функция ТаблицаОтобраннаяПоПолю(ИсходнаяТаблица, Поле, ЗначениеОтбора) Экспорт
	
	ОтобраннаяТаблица = ИсходнаяТаблица.СкопироватьКолонки();
	
	Для каждого Строка Из ИсходнаяТаблица Цикл
		Если Строка[Поле] = ЗначениеОтбора Тогда
			ЗаполнитьЗначенияСвойств(ОтобраннаяТаблица.Добавить(), Строка);
		КонецЕсли;
	КонецЦикла;

	Возврат ОтобраннаяТаблица;

КонецФункции

#КонецОбласти

#Область МетодыОбслуживанияФормДокументовДляРасчетаСреднегоЗаработка

// Процедура заполняет таблицы документа данными учета среднего заработка
// по результатам их редактирования.
//
Процедура ЗаполнитьДанныеУчетаОбщегоСреднегоЗаработка(ДанныеОНачислениях, ДанныеОВремени, ДанныеОбИндексации, РедактируемыеДанные, Модифицированность = Ложь) Экспорт
	
	ОчиститьДанныеОСреднемДокумента(
		РедактируемыеДанные.Сотрудник, 
		РедактируемыеДанные.ПорядокРасчета, 
		РедактируемыеДанные.НачалоПериодаРасчета, 
		РедактируемыеДанные.ОкончаниеПериодаРасчета, 
		ДанныеОНачислениях, 
		ДанныеОВремени, 
		ДанныеОбИндексации);	
	
	РедактируемыеНачисления = ПолучитьИзВременногоХранилища(РедактируемыеДанные.ДанныеОНачислениях);
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(РедактируемыеНачисления, ДанныеОНачислениях);
	
	Если РедактируемыеДанные.Свойство("ДанныеОВремени") И ЗначениеЗаполнено(РедактируемыеДанные.ДанныеОВремени) Тогда 
		РедактируемоеВремя = ПолучитьИзВременногоХранилища(РедактируемыеДанные.ДанныеОВремени);
		Если РедактируемоеВремя <> Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(РедактируемоеВремя, ДанныеОВремени);
		КонецЕсли;	
	КонецЕсли;	
	
	Если ДанныеОбИндексации <> Неопределено И РедактируемыеДанные.Свойство("ДанныеОбИндексации") Тогда
		РедактируемаяИндексация = ПолучитьИзВременногоХранилища(РедактируемыеДанные.ДанныеОбИндексации);
		Если РедактируемаяИндексация <> Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(РедактируемаяИндексация, ДанныеОбИндексации);
		КонецЕсли;	
	КонецЕсли;	
	
	Модифицированность = Истина;
	
КонецПроцедуры	

// Функция выполняет обновление коллекций данных среднего заработка из данных учета.
// Обновление производится для добавленных сотрудников, для удаленных сотрудников строки коллекций удаляются.
//
// Параметры:
//	ДанныеДокумента - структура с полями ДанныеОНачислениях, ДанныеОВремени, ДанныеОбИндексации (необязательный),
//	ДатаНачалаСобытия,
//	НачалоПериода - начало периода расчета среднего заработка,
//	ОкончаниеПериода - окончание периода расчета среднего заработка,
//	СотрудникиДокумента - массив сотрудников, данные для которых должны быть в документе.
//	ПроверятьПериодРасчетаСреднегоЗаработка - если Истина, то будут обновлены данные среднего для всех сотрудников, 
// 			чьи данные не покрывают период расчета среднего заработка.
// 
// Возвращаемое значение - массив сотрудников, данные для расчета среднего которых были обновлены.
//
Функция ОбновитьДанныеОбщегоСреднегоЗаработка(ДанныеДокумента, ДатаНачалаСобытия, НачалоПериода, ОкончаниеПериода, СотрудникиДокумента, ПроверятьПериодРасчетаСреднего = Истина, ИсключаемыйРегистратор = Неопределено, ОбновлятьДанныеВсехСотрудников = Ложь) Экспорт
	
	// Различаем сценарии
	// - изменился состав сотрудников
	// - изменился состав периодов среднего заработка.
	
	// Если изменился состав периодов расчета среднего заработка
	// - удаляем данные вне периода, если это не ручные корректировки
	// - получаем данные за новый период
	// - переносим имеющиеся ручные корректировки в полученные данные.
	
	// Выявляем состав сотрудников, 
	// - данные которых нужно удалить
	// - данных для которых не хватает.
	
	СотрудникиДанныхСреднего = ОбщегоНазначения.ВыгрузитьКолонку(ДанныеДокумента.ДанныеОНачислениях, "Сотрудник", Истина);
	
	СотрудникиДобавить = ОбщегоНазначенияКлиентСервер.РазностьМассивов(СотрудникиДокумента, СотрудникиДанныхСреднего);
	СотрудникиУдалить = ОбщегоНазначенияКлиентСервер.РазностьМассивов(СотрудникиДанныхСреднего, СотрудникиДокумента);
	
	ОбновляемыеСотрудники = Новый Массив;
	
	// Удаляем строки сотрудников удаленных сотрудников.
	Если СотрудникиУдалить <> Неопределено Тогда
		ОчиститьДанныеДляРасчетаСреднего(ДанныеДокумента.ДанныеОНачислениях, СотрудникиУдалить);
		ОчиститьДанныеДляРасчетаСреднего(ДанныеДокумента.ДанныеОВремени, СотрудникиУдалить);
	КонецЕсли;
	
	// Таблица для получения данных среднего заработка.
	ТаблицаСотрудники = ПустаяТаблицаИсходныхДанныхРасчетаОбщегоСреднегоЗаработка();

	ПорядокРасчета = УчетСреднегоЗаработкаКлиентСервер.ПорядокРасчетаОбщегоСреднегоЗаработка(ДатаНачалаСобытия);
	
	Если СотрудникиДобавить <> Неопределено Тогда
		// Заполняем таблицу добавленными сотрудниками.
		Для Каждого Сотрудник Из СотрудникиДобавить Цикл
			НоваяСтрока = ТаблицаСотрудники.Добавить();
			НоваяСтрока.Сотрудник = Сотрудник;
			НоваяСтрока.ПорядокРасчета = ПорядокРасчета;
			НоваяСтрока.ДатаНачалаСобытия = ДатаНачалаСобытия;
			НоваяСтрока.НачалоПериодаРасчетаСреднего = НачалоПериода;
			НоваяСтрока.ОкончаниеПериодаРасчетаСреднего = ОкончаниеПериода;
			ОбновляемыеСотрудники.Добавить(Сотрудник);
		КонецЦикла;
	КонецЕсли;
	
	// Проверяем следствия изменения периода расчета среднего заработка.
	Если ПроверятьПериодРасчетаСреднего Тогда
		// Выявляем сотрудников, для которых нужно дополнить данные в связи с изменением периода.
		// Считаем, что дополнять данные нужно, если минимальный для сотрудника месяц или максимальный 
		// выходят за границы периода расчета среднего заработка.
		ПериодыСотрудников = Новый Соответствие;
		Для Каждого СтрокаДанныхСреднего Из ДанныеДокумента.ДанныеОНачислениях Цикл
			ПериодыСотрудника = ПериодыСотрудников[СтрокаДанныхСреднего.Сотрудник];
			Если ПериодыСотрудников[СтрокаДанныхСреднего.Сотрудник] = Неопределено Тогда
				ПериодыСотрудника = Новый Структура("Минимум, Максимум");
			КонецЕсли;
			Если ПериодыСотрудника.Минимум = Неопределено 
				Или СтрокаДанныхСреднего.Период < ПериодыСотрудника.Минимум Тогда
				ПериодыСотрудника.Минимум = СтрокаДанныхСреднего.Период;
			КонецЕсли;
			Если ПериодыСотрудника.Максимум = Неопределено 
				Или СтрокаДанныхСреднего.Период > ПериодыСотрудника.Максимум Тогда
				ПериодыСотрудника.Максимум = СтрокаДанныхСреднего.Период;
			КонецЕсли;
			ПериодыСотрудников.Вставить(СтрокаДанныхСреднего.Сотрудник, ПериодыСотрудника);
		КонецЦикла;
		
		// Если для сотрудника границы периода данных среднего не покрывают нового периода
		// нужно получить новые данные.
		Для Каждого КлючИЗначение Из ПериодыСотрудников Цикл
			Сотрудник = КлючИЗначение.Ключ;
			ПериодыСотрудника = КлючИЗначение.Значение;
			Если ПериодыСотрудника.Минимум > НачалоПериода 
				Или ПериодыСотрудника.Максимум < НачалоМесяца(ОкончаниеПериода) Тогда 
				НоваяСтрока = ТаблицаСотрудники.Добавить();
				НоваяСтрока.Сотрудник = Сотрудник;
				НоваяСтрока.ПорядокРасчета = ПорядокРасчета;
				НоваяСтрока.ДатаНачалаСобытия = ДатаНачалаСобытия;
				НоваяСтрока.НачалоПериодаРасчетаСреднего = НачалоПериода;
				НоваяСтрока.ОкончаниеПериодаРасчетаСреднего = ОкончаниеПериода;
				ОбновляемыеСотрудники.Добавить(Сотрудник);
			КонецЕсли;
		КонецЦикла;
		
		// Удаляем из коллекций данные, находящиеся вне периода расчета среднего заработка.
		ГодГодовыхПремий = УчетСреднегоЗаработкаКлиентСервер.ГодГодовыхПремий(ДатаНачалаСобытия);
		УдалитьДанныеВнеПериодаРасчетаСреднегоЗаработка(ДанныеДокумента.ДанныеОНачислениях, НачалоПериода, ОкончаниеПериода, , ГодГодовыхПремий);
		УдалитьДанныеВнеПериодаРасчетаСреднегоЗаработка(ДанныеДокумента.ДанныеОВремени, НачалоПериода, ОкончаниеПериода);
		
	КонецЕсли;
	
	Если ОбновлятьДанныеВсехСотрудников Тогда
		// Заполняем таблицу добавленными сотрудниками.
		Для Каждого Сотрудник Из СотрудникиДокумента Цикл
			Отбор = Новый Структура("Сотрудник", Сотрудник);
			Если ТаблицаСотрудники.НайтиСтроки(Отбор).Количество() <> 0 Тогда 
				Продолжить;
			КонецЕсли;
			НоваяСтрока = ТаблицаСотрудники.Добавить();
			НоваяСтрока.Сотрудник = Сотрудник;
			НоваяСтрока.ПорядокРасчета = ПорядокРасчета;
			НоваяСтрока.ДатаНачалаСобытия = ДатаНачалаСобытия;
			НоваяСтрока.НачалоПериодаРасчетаСреднего = НачалоПериода;
			НоваяСтрока.ОкончаниеПериодаРасчетаСреднего = ОкончаниеПериода;
			ОбновляемыеСотрудники.Добавить(Сотрудник);
		КонецЦикла;
	КонецЕсли;
	
	Если ДанныеДокумента.Свойство("ДанныеОбИндексации") Тогда
		// Данные индексации перезаполняем полностью.
		ОчиститьДанныеДляРасчетаСреднего(ДанныеДокумента.ДанныеОбИндексации, ОбновляемыеСотрудники);
	КонецЕсли;
	
	Если ТаблицаСотрудники.Количество() = 0 Тогда
		Возврат ОбновляемыеСотрудники;
	КонецЕсли;
	
	// Получаем данные для расчета среднего заработка.
	ДанныеДляРасчета = ДанныеДляРасчетаОбщегоСреднегоЗаработкаСотрудниковСлужебный(ТаблицаСотрудники, , ИсключаемыйРегистратор);
	
	// Объединение данных, полученных из учета, с данными документа осуществляется по следующим правилам
	// Данные в документе заменяются данными из учета в одном из следующих случаев
	//	- источник данных в документе не относится к группе источников "Результаты редактирования" (СведенияДоНачалаЭксплуатации и Исправления)
	//	- порядковый номер источника из учета (приоритет) выше, чем в документе.
	
	// Следовательно, необходимо в разрезе сотрудника и месяца для каждой коллекции определить максимальный приоритет источника 
	// и наличие результатов редактирования.
	
	ПриоритетИсточников = Перечисления.ИсточникиДанныхДляРасчетаСреднегоЗаработка.ПриоритетИсточников();
	СохраняемыеЗначения = Перечисления.ИсточникиДанныхДляРасчетаСреднегоЗаработка.РезультатыРедактирования();
	
	СочетанияЗамены = Новый ТаблицаЗначений;
	СочетанияЗамены.Колонки.Добавить("Сотрудник");
	СочетанияЗамены.Колонки.Добавить("Период");
	
	ОтборСтрок = Новый Структура("Сотрудник, Период");
	
	СоответствиеКоллекций = Новый Соответствие;
	СоответствиеКоллекций.Вставить(ДанныеДляРасчета.ДанныеОНачислениях, ДанныеДокумента.ДанныеОНачислениях);
	СоответствиеКоллекций.Вставить(ДанныеДляРасчета.ДанныеОВремени, ДанныеДокумента.ДанныеОВремени);
	
	Для Каждого КлючИЗначение Из СоответствиеКоллекций Цикл
		КоллекцияУчета = КлючИЗначение.Ключ;
		КоллекцияДокумента = КлючИЗначение.Значение;
		СочетанияЗамены.Очистить();
		Для Каждого СтрокаДанных Из КоллекцияУчета Цикл
			ЗаполнитьЗначенияСвойств(ОтборСтрок, СтрокаДанных);
			НайденныеСочетания = СочетанияЗамены.НайтиСтроки(ОтборСтрок);
			Если НайденныеСочетания.Количество() > 0 Тогда
				// Такое сочетание уже есть, пропускаем.
				Продолжить;
			КонецЕсли;
			НайденныеСтроки = КоллекцияДокумента.НайтиСтроки(ОтборСтрок);
			Если НайденныеСтроки.Количество() = 0 Тогда
				// Строк за этот месяц вовсе нет в документе, добавляем.
				ЗаполнитьЗначенияСвойств(СочетанияЗамены.Добавить(), СтрокаДанных);
				Продолжить;
			КонецЕсли;
			ЕстьСохраняемыеЗначения = Ложь;
			ПриоритетИзДокумента = 0;
			Для Каждого СтрокаДокумента Из НайденныеСтроки Цикл
				Если СохраняемыеЗначения.Найти(СтрокаДокумента.Источник) <> Неопределено Тогда
					// Источник относится к сохраняемым значениям, пропускаем.
					ЕстьСохраняемыеЗначения = Истина;
				КонецЕсли;
				Приоритет = ПриоритетИсточников[СтрокаДокумента.Источник];
				Если ПриоритетИзДокумента < Приоритет Тогда
					ПриоритетИзДокумента = Приоритет;
				КонецЕсли;
			КонецЦикла;
			Если Не ЕстьСохраняемыеЗначения Тогда
				ЗаполнитьЗначенияСвойств(СочетанияЗамены.Добавить(), СтрокаДанных);
				Продолжить;
			КонецЕсли;
			// Сравниваем приоритет.
			ПриоритетИзУчета = ПриоритетИсточников[СтрокаДанных.Источник];
			Если ПриоритетИзУчета > ПриоритетИзДокумента Тогда
				ЗаполнитьЗначенияСвойств(СочетанияЗамены.Добавить(), СтрокаДанных);
			КонецЕсли;
		КонецЦикла;
		
		// Удаляем данные, которые есть в документе, но отсутствуют в данных учета.
		Для Каждого СтрокаДокумента Из КоллекцияДокумента Цикл
			ЗаполнитьЗначенияСвойств(ОтборСтрок, СтрокаДокумента);
			НайденныеСтроки = КоллекцияУчета.НайтиСтроки(ОтборСтрок);
			Если НайденныеСтроки.Количество() <> 0 Тогда
				Продолжить;
			КонецЕсли;
			Если СохраняемыеЗначения.Найти(СтрокаДокумента.Источник) <> Неопределено Тогда
				// Источник относится к сохраняемым значениям, пропускаем.
				Продолжить;
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(СочетанияЗамены.Добавить(), СтрокаДокумента);
		КонецЦикла;
		
		// Удаляем все строки, соответствующие отобранным сочетаниям Сотрудник + Период.
		Для Каждого Сочетание Из СочетанияЗамены Цикл
			ЗаполнитьЗначенияСвойств(ОтборСтрок, Сочетание);
			// Переносим.
			// Удаляем существующие строки...
			СтрокиДокумента = КоллекцияДокумента.НайтиСтроки(ОтборСтрок);
			Для Каждого СтрокаДокумента Из СтрокиДокумента Цикл
				КоллекцияДокумента.Удалить(СтрокаДокумента);
			КонецЦикла;
			// ..и добавляем из данных расчета.
			СтрокиУчета = КоллекцияУчета.НайтиСтроки(ОтборСтрок);
			Для Каждого СтрокаУчета Из СтрокиУчета Цикл
				ЗаполнитьЗначенияСвойств(КоллекцияДокумента.Добавить(), СтрокаУчета);
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	Если ДанныеДокумента.Свойство("ДанныеОбИндексации") Тогда
		// Заполняем таблицы документа с данными среднего заработка.
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ДанныеДляРасчета.ДанныеОбИндексации, ДанныеДокумента.ДанныеОбИндексации);
	КонецЕсли;
	
	Возврат ОбновляемыеСотрудники;
	
КонецФункции

// Выполняет упаковку данных общего среднего заработка формы документа для передачи в форму редактирования.
//
Процедура ЗаполнитьТаблицыДанныхСреднегоЗаработкаПоДокументу(ДанныеФормыОбъект, ПараметрыРедактирования) Экспорт
	
	// Переносим содержимое табличных частей в таблицы значений и помещаем последние во временное хранилище
	// - начисления.
	ДанныеНачислений = ПустаяТаблицаНачисленийСреднийЗаработокОбщий();
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ДанныеФормыОбъект.СреднийЗаработокОбщий, ДанныеНачислений);
	ПараметрыРедактирования.ДанныеОНачислениях = ПоместитьВоВременноеХранилище(ДанныеНачислений);
	
	// - отработанное время
	ОтработанноеВремя = ПустаяТаблицаОтработанноеВремяСреднийЗаработокОбщий();
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ДанныеФормыОбъект.ОтработанноеВремяДляСреднегоОбщий, ОтработанноеВремя);
	ПараметрыРедактирования.ДанныеОВремени = ПоместитьВоВременноеХранилище(ОтработанноеВремя);
	
	// - данные индексации
	ДанныеИндексации = ПустаяТаблицаДанныеИндексации();
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ДанныеФормыОбъект.ДанныеОбИндексации, ДанныеИндексации);
	ПараметрыРедактирования.ДанныеОбИндексации = ПоместитьВоВременноеХранилище(ДанныеИндексации);
	
КонецПроцедуры

// Добавляет команду печати "Расчет среднего заработка", вызывается из модулей менеджеров документов.
//
Процедура ДобавитьКомандуПечатиРасчетаСреднегоЗаработка(КомандыПечати, МенеджерПечати, ФункциональныеОпции = "", ИдентификаторыПФ = Неопределено) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьРасчетЗарплатыРасширенная")
		И Пользователи.РолиДоступны("ПолныеПрава,ДобавлениеИзменениеРабочегоВремениНачисленнойЗарплаты,ЧтениеРабочегоВремениНачисленнойЗарплаты", , Ложь) Тогда
		
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Обработчик = "УправлениеПечатьюБЗККлиент.ВыполнитьКомандуПечати";
		КомандаПечати.МенеджерПечати = МенеджерПечати;
		КомандаПечати.Идентификатор = "РасчетСреднегоЗаработка";
		КомандаПечати.Представление = НСтр("ru = 'Расчет среднего заработка';
											|en = 'Average earning calculation'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.ДополнительныеПараметры.Вставить("ТребуетсяЧтениеБезОграничений", Истина);
		КомандаПечати.ФункциональныеОпции = ФункциональныеОпции;
		КомандаПечати.ДополнительныеПараметры.Вставить("РеквизитыДетализации", "Сотрудник");
		
		Если ИдентификаторыПФ <> Неопределено Тогда
			ЗарплатаКадры.ДобавитьИдентификаторКомандыДляПечатиВПакетномРежиме(ИдентификаторыПФ, КомандаПечати);
		КонецЕсли;
		
	КонецЕсли; 
	
КонецПроцедуры

// Добавляет команду печати "Записка-расчет (0504425)", вызывается из модулей менеджеров документов.
//
Процедура ДобавитьКомандуПечатиРасчетаСреднегоЗаработка0504425(КомандыПечати, МенеджерПечати, ФункциональныеОпции = "", ИдентификаторыПФ = Неопределено) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.УчетБюджетныхУчреждений") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("УчетСреднегоЗаработкаБюджетныхУчреждений");
		Модуль.ДобавитьКомандуПечатиРасчетаСреднегоЗаработка0504425(КомандыПечати, МенеджерПечати, ФункциональныеОпции, ИдентификаторыПФ)
	КонецЕсли;
	
КонецПроцедуры

// Добавляет команду печати "Расчет среднего заработка (для выходного пособия)", вызывается из модулей
// менеджеров документов.
//
Процедура ДобавитьКомандуПечатиРасчетаСреднегоЗаработкаВыходногоПособия(КомандыПечати, МенеджерПечати, ИдентификаторыПФ = Неопределено) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьРасчетЗарплатыРасширенная")
		И Пользователи.РолиДоступны("ПолныеПрава,ДобавлениеИзменениеРабочегоВремениНачисленнойЗарплаты,ЧтениеРабочегоВремениНачисленнойЗарплаты", , Ложь) Тогда
		
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Обработчик = "УправлениеПечатьюБЗККлиент.ВыполнитьКомандуПечати";
		КомандаПечати.МенеджерПечати = МенеджерПечати;
		КомандаПечати.Идентификатор = "РасчетСреднегоЗаработкаВыходногоПособия";
		КомандаПечати.Представление = НСтр("ru = 'Расчет среднего заработка (для выходного пособия)';
											|en = 'Average earnings calculation (for severance pay)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.ДополнительныеПараметры.Вставить("ТребуетсяЧтениеБезОграничений", Истина);
		КомандаПечати.ДополнительныеПараметры.Вставить("РеквизитыДетализации", "Сотрудник");
		
		Если ИдентификаторыПФ <> Неопределено Тогда
			ЗарплатаКадры.ДобавитьИдентификаторКомандыДляПечатиВПакетномРежиме(ИдентификаторыПФ, КомандаПечати);
		КонецЕсли;
		
	КонецЕсли; 
	
КонецПроцедуры

#Область ИнформацияОЗаполненностиДанныхСреднегоЗаработка

// Функция формирует структуру информационных сведений о заполненности данных 
//  для расчета общего среднего заработка сотрудника.
//
// Параметры:
//	Ссылка				- ДокументСсылка на документ а котором рассчитывается средний заработок
//	ЗаполнениеВыполнено	- Булево, Истина, если в документе произведено заполнение расчета среднего заработка
// 	Сотрудник
//	НачалоРасчета - дата начала периода расчета среднего заработка.
//	ОкончаниеРасчета - дата окончания периода расчета среднего заработка.
//	ДанныеНачислений - данные формы табличной части начислений общего среднего заработка.
//	ДанныеОВремени - данные формы табличной части времени общего среднего заработка.
//	УчитыватьДанныеОВремени - булево, необходимость проверки заполненности данных о времени.
//
Функция ИнформацияОЗаполненностиДанныхСреднегоЗаработка(Ссылка, ЗаполнениеВыполнено, Сотрудник, ДатаНачалаСобытия, НачалоПериодаРасчета, ОкончаниеПериодаРасчета, ДанныеНачислений, ДанныеОВремени, УчитыватьДанныеОВремени = Истина) Экспорт
	
	Если ТребуетсяПерезаполнитьСведенияОСреднемЗаработке(Ссылка, ЗаполнениеВыполнено) Тогда
		
		СтруктураИнфонадписи = ИнфонадписьТребуетсяПерезаполнитьСведенияОСреднемЗаработке();
		
	ИначеЕсли ТребуетсяДозаполнитьСведенияОСреднемЗаработке(Сотрудник, НачалоПериодаРасчета, ОкончаниеПериодаРасчета, ДатаНачалаСобытия, ДанныеНачислений, ДанныеОВремени, УчитыватьДанныеОВремени) Тогда
		
		СтруктураИнфонадписи = ИнфонадписьТребуетсяДозаполнитьСведенияОСреднемЗаработке();
		
	Иначе 
		
		СтруктураИнфонадписи = ИнфонадписьИспользованныеДанныеОСреднемЗаработке(НачалоПериодаРасчета, ОкончаниеПериодаРасчета);
		
	КонецЕсли;
	
	Возврат СтруктураИнфонадписи;
	
КонецФункции

// Функция проверяет есть ли в указанном периоде месяцы данные по которым не заполнены.
//
// Параметры:
//	НачалоРасчета - дата начала периода расчета среднего заработка.
//	ОкончаниеРасчета - дата окончания периода расчета среднего заработка.
//	ДанныеНачислений - данные формы табличной части начислений общего среднего заработка.
//	ДанныеОВремени - данные формы табличной части времени общего среднего заработка.
//	УчитыватьДанныеОВремени - булево, необходимость проверки заполненности данных о времени.
//
Функция ЕстьНеЗаполненныеПериодыРасчетаСреднегоЗаработка(НачалоПериодаРасчета, ОкончаниеПериодаРасчета, ДанныеНачислений, ДанныеОВремени, УчитыватьДанныеОВремени, МесяцыКорректировкиСреднегоЗаработка)
	
	ЕстьНеЗаполненныеДанные = Ложь;
	
	Если ДанныеНачислений.Количество() = 0 
		Или ДанныеОВремени.Количество() = 0 Тогда
		Если МесяцыКорректировкиСреднегоЗаработка.Количество() > 0 Тогда
			ЕстьНеЗаполненныеДанные = Истина;
		КонецЕсли;
	Иначе
		// Необходимо проверить на все ли месяцы интервала 
		// заполнены данные о начислениях и отработанном времени.
		// Проверяем по таблице начислений.
		ПериодыНезаполненныхДанных = ПериодыНезаполненногоСреднегоЗаработка(НачалоПериодаРасчета, ОкончаниеПериодаРасчета, ДанныеНачислений);
		Для Каждого Месяц Из ПериодыНезаполненныхДанных Цикл
			Если МесяцыКорректировкиСреднегоЗаработка.Найти(Месяц) <> Неопределено Тогда
				ЕстьНеЗаполненныеДанные = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		// Проверяем по таблице времени.
		Если УчитыватьДанныеОВремени И Не ЕстьНеЗаполненныеДанные Тогда
			ПериодыНезаполненныхДанных = ПериодыНезаполненногоСреднегоЗаработка(НачалоПериодаРасчета, ОкончаниеПериодаРасчета, ДанныеОВремени);
			Для Каждого Месяц Из ПериодыНезаполненныхДанных Цикл
				Если МесяцыКорректировкиСреднегоЗаработка.Найти(Месяц) <> Неопределено Тогда
					ЕстьНеЗаполненныеДанные = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЕстьНеЗаполненныеДанные;
	
КонецФункции

// Функция возвращает структуру для заполнения инфонадписи о заполненности данных для расчета
// среднего заработка сотрудника.
//
Функция ИнфонадписьТребуетсяПерезаполнитьСведенияОСреднемЗаработке() Экспорт
	
	ТекстСообщения = НСтр("ru = 'Требуется перезаполнить сведения
		|о среднем заработке.';
		|en = 'Refilling of average earnings
		|information is required.'");
	
	ЧастиСообщения = Новый Массив;
	ЧастиСообщения.Добавить(Новый ФорматированнаяСтрока(ТекстСообщения, , ЦветаСтиля.ПоясняющийОшибкуТекст));
	ЧастиСообщения.Добавить(" ");
	ЧастиСообщения.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Подробнее';
															|en = 'Details'") + "...", , , , "Подробнее"));
		
	Картинка = БиблиотекаКартинок.Предупреждение;
	
	Возврат ЗаполненнаяСтруктураИнфонадписиОСреднемЗаработке(Новый ФорматированнаяСтрока(ЧастиСообщения), Картинка);
	
КонецФункции

// Функция возвращает структуру для заполнения инфонадписи о заполненности данных для расчета
// среднего заработка сотрудника.
//
Функция ИнфонадписьТребуетсяДозаполнитьСведенияОСреднемЗаработке() Экспорт
	
	ТекстСообщения = Новый ФорматированнаяСтрока(НСтр("ru = 'Данные о заработке неполные.
		|Для ввода недостающих данных используйте команду «Изменить»';
		|en = 'Incomplete earnings data.
		|To enter the missing data, click ""Change""'"));
	
	Картинка = БиблиотекаКартинок.Предупреждение;
	
	Возврат ЗаполненнаяСтруктураИнфонадписиОСреднемЗаработке(ТекстСообщения, Картинка);
	
КонецФункции

// Функция возвращает структуру для заполнения инфонадписи о заполненности данных для расчета
// среднего заработка сотрудника.
//
// Параметры:
//	НачалоРасчета - дата начала периода расчета среднего заработка.
//	ОкончаниеРасчета - дата окончания периода расчета среднего заработка.
//
Функция ИнфонадписьИспользованныеДанныеОСреднемЗаработке(НачалоПериодаРасчета, ОкончаниеПериодаРасчета) Экспорт 
	
	Если НачалоМесяца(НачалоПериодаРасчета) = НачалоМесяца(ОкончаниеПериодаРасчета) Тогда
		// За один месяц
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Использованы данные о заработке за %1';
			|en = 'Earnings data for %1 is used'"), 
		Формат(НачалоПериодаРасчета, "ДФ='ММММ гггг'"));
	Иначе
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Использованы данные о заработке за период %1 - %2';
			|en = 'Earnings data for period %1 - %2 is used'"), 
		Формат(НачалоПериодаРасчета, "ДФ='ММММ гггг'"), 
		Формат(ОкончаниеПериодаРасчета, "ДФ='ММММ гггг'"));
	КонецЕсли;
	
	Картинка = БиблиотекаКартинок.Информация;
	
	Возврат ЗаполненнаяСтруктураИнфонадписиОСреднемЗаработке(ТекстСообщения, Картинка);
	
КонецФункции

// Инкапсулирует текст и картинку инфонадписи в структуру.
//
// Параметры:
//	Текст - текст сообщения инфонадписи.
//	Картинка - картинка для инфонадписи.
//
Функция ЗаполненнаяСтруктураИнфонадписиОСреднемЗаработке(Текст, Картинка) Экспорт
	
	СтруктураИнфонадписи = Новый Структура;
	СтруктураИнфонадписи.Вставить("Текст", СтроковыеФункции.ФорматированнаяСтрока(Текст));
	СтруктураИнфонадписи.Вставить("Картинка", Картинка);
	
	Возврат СтруктураИнфонадписи;

КонецФункции

Функция ТребуетсяПерезаполнитьСведенияОСреднемЗаработке(СсылкаНаДокумент, ЗаполнениеВыполнено) Экспорт
	
	ТребуетсяПерезаполнитьСведенияОСреднемЗаработке = Не ЗаполнениеВыполнено И ЗначениеЗаполнено(СсылкаНаДокумент);
	
	Если ТребуетсяПерезаполнитьСведенияОСреднемЗаработке Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("СсылкаНаДокумент", СсылкаНаДокумент);	
		
		Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
			|	ПерерасчетСреднегоЗаработка.ДокументСреднегоЗаработка
			|ИЗ
			|	РегистрСведений.ПерерасчетСреднегоЗаработка КАК ПерерасчетСреднегоЗаработка
			|ГДЕ
			|	ПерерасчетСреднегоЗаработка.ДокументСреднегоЗаработка = &СсылкаНаДокумент";
			
		ТребуетсяПерезаполнитьСведенияОСреднемЗаработке = Не Запрос.Выполнить().Пустой(); 
		
	КонецЕсли;
	
	Возврат ТребуетсяПерезаполнитьСведенияОСреднемЗаработке;
	
КонецФункции

Функция ТребуетсяДозаполнитьСведенияОСреднемЗаработке(Сотрудник, НачалоПериодаРасчета, ОкончаниеПериодаРасчета, ДатаНачалаСобытия, ДанныеНачислений, ДанныеОВремени, УчитыватьДанныеОВремени) Экспорт
	
	КадровыеДанные = КадровыйУчет.КадровыеДанныеСотрудников(Истина, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудник), "ДатаПриема");
	Если КадровыеДанные.Количество() > 0 Тогда
		ДатаПриемаНаРаботу = КадровыеДанные[0].ДатаПриема;
	Иначе
		ДатаПриемаНаРаботу = '00010101';
	КонецЕсли;	
	
	МесяцыКорректировкиСреднегоЗаработка = МесяцыКорректировкиСреднегоЗаработка(Сотрудник, НачалоПериодаРасчета, ОкончаниеПериодаРасчета, ДатаПриемаНаРаботу, ДатаНачалаСобытия);
	
	ЕстьНеЗаполненныеДанные = ЕстьНеЗаполненныеПериодыРасчетаСреднегоЗаработка(НачалоПериодаРасчета, ОкончаниеПериодаРасчета, ДанныеНачислений, ДанныеОВремени, УчитыватьДанныеОВремени, МесяцыКорректировкиСреднегоЗаработка);
	
	Возврат ЕстьНеЗаполненныеДанные;
	
КонецФункции

#КонецОбласти

Процедура УстановитьДоступностьИЗначениеПоУмолчаниюСвойстваУчитыватьМРОТ(Форма, Сотрудник, ДатаСобытия, ВидыРасчета, УстановитьЗначение, ИмяЭлемента = "УчитыватьМРОТПриОплатеПоСреднемуЗаработку", ПутьРеквизита = "Объект.УчитыватьМРОТПриОплатеПоСреднемуЗаработку") Экспорт 
	
	ПоказательНайден = Ложь;
	ПоказательУчитыватьМРОТ = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ПоказателиРасчетаЗарплаты.УчитыватьМРОТ");
	
	Для Каждого ВидРасчета Из ВидыРасчета Цикл  
		ВидРасчетаИнфо = ЗарплатаКадрыРасширенныйПовтИсп.ПолучитьИнформациюОВидеРасчета(ВидРасчета);
		Для Каждого ДанныеПоказателя Из ВидРасчетаИнфо.Показатели Цикл 
			Если ДанныеПоказателя.Показатель = ПоказательУчитыватьМРОТ Тогда 
				ПоказательНайден = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Если Не ПоказательНайден Тогда 
		Если УстановитьЗначение Тогда 
			ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(Форма, ПутьРеквизита, Ложь);
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, ИмяЭлемента, "ТолькоПросмотр", Истина);
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, ИмяЭлемента, "ТолькоПросмотр", Ложь);
	
	Если Не УстановитьЗначение Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Сотрудник) Тогда
		Возврат;
	КонецЕсли;
	
	КадровыеДанные = КадровыйУчет.КадровыеДанныеСотрудников(Истина, Сотрудник, "ГрафикРаботы,КоличествоСтавок", ДатаСобытия);
	Если КадровыеДанные.Количество() > 0 Тогда 
		Если Не ЗначениеЗаполнено(КадровыеДанные[0].КоличествоСтавок) Или КадровыеДанные[0].КоличествоСтавок < 1 Тогда 
			ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(Форма, ПутьРеквизита, Ложь);
			Возврат;
		КонецЕсли;
		Если ЗначениеЗаполнено(КадровыеДанные[0].ГрафикРаботы)
			И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КадровыеДанные[0].ГрафикРаботы, "НеполноеРабочееВремя") Тогда 
			ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(Форма, ПутьРеквизита, Ложь);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(Форма, ПутьРеквизита, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область РегистрацияРучныхКорректировок

Функция КорректировкиОбщегоСреднегоЗаработкаДокумента(ДокументСсылка) Экспорт
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Начисления.Сотрудник,
		|	Начисления.ПорядокРасчета,
		|	Начисления.Начисление,
		|	Начисления.СоставнаяЧасть,
		|	Начисления.СтатьяФинансирования,
		|	Начисления.СпособОтраженияЗарплатыВБухучете,
		|	Начисления.СтатьяРасходов,
		|	Начисления.ОблагаетсяЕНВД,
		|	Начисления.Период,
		|	Начисления.Индексируется,
		|	Начисления.Сумма,
		|	Начисления.Год,
		|	Начисления.ДатаНачалаБазовогоПериода,
		|	Начисления.КоличествоМесяцев,
		|	Начисления.Ссылка.Организация КАК Организация,
		|	Начисления.Сотрудник.ФизическоеЛицо КАК ФизическоеЛицо
		|ИЗ
		|	#ТабличнаяЧастьНачисления КАК Начисления
		|ГДЕ
		|	Начисления.Ссылка = &ДокументСсылка
		|	И Начисления.Источник = ЗНАЧЕНИЕ(Перечисление.ИсточникиДанныхДляРасчетаСреднегоЗаработка.СведенияДоНачалаЭксплуатации)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Время.Сотрудник,
		|	Время.ПорядокРасчета,
		|	Время.Период,
		|	Время.ОтработаноДней,
		|	Время.ОтработаноДнейПятидневка,
		|	Время.ОтработаноЧасовПятидневка,
		|	Время.ОтработаноЧасов,
		|	Время.ОтработаноДнейШестидневка,
		|	Время.ОтработаноДнейКалендарных,
		|	Время.НормаДнейПроизводственныйКалендарь,
		|	Время.НормаЧасовПроизводственныйКалендарь
		|ИЗ
		|	#ТабличнаяЧастьВремя КАК Время
		|ГДЕ
		|	Время.Ссылка = &ДокументСсылка
		|	И Время.Источник = ЗНАЧЕНИЕ(Перечисление.ИсточникиДанныхДляРасчетаСреднегоЗаработка.СведенияДоНачалаЭксплуатации)";

	ИмяТаблицыДокумента = ДокументСсылка.Метаданные().ПолноеИмя();
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТабличнаяЧастьНачисления", ИмяТаблицыДокумента + ".СреднийЗаработокОбщий");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТабличнаяЧастьВремя", ИмяТаблицыДокумента + ".ОтработанноеВремяДляСреднегоОбщий");
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	Корректировки = Новый Структура("КорректировкиНачислений, КорректировкиВремени");
	Корректировки.КорректировкиНачислений = РезультатыЗапроса[0].Выгрузить();
	Корректировки.КорректировкиВремени = РезультатыЗапроса[1].Выгрузить();
	
	Возврат Корректировки;
	
КонецФункции

Функция ДополнительныеПараметрыЗаписиКорректировокОбщегоСреднегоЗаработка() Экспорт
	
	Параметры = Новый Структура(
		"Организация, 
		|ФизическоеЛицо, 
		|Сотрудник, 
		|ПорядокРасчета");
	Параметры.ПорядокРасчета = Перечисления.ПорядокРасчетаСреднегоЗаработкаОбщий.Постановление2010;
	
	Возврат Параметры;
	
КонецФункции

// Процедура выполняет запись сведений об общем среднем заработке 
//  на основе данных, введенных пользователем.
//
// Параметры:
//	КорректировкиНачислений - таблица значений, структуры аналогичной набору записей 
//								регистра сведений СведенияОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.
//	КорректировкиВремени	- таблица значений, структуры аналогичной набору записей 
//								регистра сведений СведенияОВремениДляРасчетаСреднегоОбщий.
//
Процедура ЗаписатьКорректировкиОбщегоСреднегоЗаработка(Корректировки, НачалоПериода, ОкончаниеПериода, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ДополнительныеПараметры = Неопределено Тогда
		ДополнительныеПараметры = ДополнительныеПараметрыЗаписиКорректировокОбщегоСреднегоЗаработка();
	КонецЕсли;
	
	КорректировкиНачислений = Корректировки.КорректировкиНачислений;
	КорректировкиВремени = Корректировки.КорректировкиВремени;
	
	// Корректировки начислений
	ПереименоватьКолонкуПериодВМесяц(КорректировкиНачислений);
	
	ИзмеренияНачислений = 
		"Сотрудник, 
		|Организация, 
		|ФизическоеЛицо, 
		|ПорядокРасчета, 
		|Месяц, 
		|Начисление, 
		|СоставнаяЧасть, 
		|СтатьяФинансирования, 
		|СпособОтраженияЗарплатыВБухучете, 
		|СтатьяРасходов, 
		|ОблагаетсяЕНВД, 
		|Индексируется, 
		|Год, 
		|ДатаНачалаБазовогоПериода, 
		|КоличествоМесяцев";
	КорректировкиНачислений.Свернуть(ИзмеренияНачислений, "Сумма");
	
	ЗначенияПоУмолчанию = Новый Структура(
		"Организация, 
		|ФизическоеЛицо, 
		|Сотрудник, 
		|ПорядокРасчета");
	ЗаполнитьЗначенияСвойств(ЗначенияПоУмолчанию, ДополнительныеПараметры);
	
	ЗаписатьТаблицуЗначенийВРегистрСведений(
		КорректировкиНачислений, 
		РегистрыСведений.СведенияОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.СоздатьНаборЗаписей(), 
		"Сотрудник, ПорядокРасчета, Месяц",
		НачалоПериода, 
		ОкончаниеПериода, 
		Истина,
		ЗначенияПоУмолчанию);
		
	// Корректировки времени
	ПереименоватьКолонкуПериодВМесяц(КорректировкиВремени);
	
	ИзмеренияВремени = "Сотрудник, ПорядокРасчета, Месяц";
	КорректировкиВремени.Свернуть(ИзмеренияВремени, "ОтработаноДней, ОтработаноЧасов, ОтработаноДнейПятидневка, ОтработаноЧасовПятидневка, ОтработаноДнейШестидневка, ОтработаноДнейКалендарных");
	
	ЗаписатьТаблицуЗначенийВРегистрСведений(
		КорректировкиВремени, 
		РегистрыСведений.СведенияОВремениДляРасчетаСреднегоОбщий.СоздатьНаборЗаписей(), 
		ИзмеренияВремени,
		НачалоПериода, 
		ОкончаниеПериода,
		Истина, 
		ЗначенияПоУмолчанию);
		
	Если РасчетЗарплатыРасширенный.НастройкиРасчетаЗарплаты().ПереноситьДанныеВДругойУчетСреднегоЗаработка Тогда
		УчетПособийСоциальногоСтрахованияРасширенный.ПеренестиКорректировкиВСреднийЗаработокФСС(КорректировкиНачислений, КорректировкиВремени, НачалоПериода, ОкончаниеПериода);
	КонецЕсли;
		
КонецПроцедуры	

Процедура ПеренестиКорректировкиВСреднийЗаработокОбщий(КорректировкиНачислений, КорректировкиВремени, Организация, НачалоПериода, ОкончаниеПериода) Экспорт
	
	КорректировкиОбщегоЗаработка = СведенияОбщегоСреднегоЗаработкаПоКорректировкамФСС(Организация, КорректировкиНачислений, КорректировкиВремени);
	Если КорректировкиОбщегоЗаработка.Свойство("КорректировкиНачислений") Тогда
		ЗаписатьТаблицуЗначенийВРегистрСведений(
			КорректировкиОбщегоЗаработка["КорректировкиНачислений"],
			РегистрыСведений.СведенияОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.СоздатьНаборЗаписей(), 
			"Сотрудник, ПорядокРасчета, Месяц",
			НачалоПериода, 
			ОкончаниеПериода, 
			Ложь);
	КонецЕсли;
	
	Если КорректировкиОбщегоЗаработка.Свойство("КорректировкиВремени") Тогда
		ЗаписатьТаблицуЗначенийВРегистрСведений(
			КорректировкиОбщегоЗаработка["КорректировкиВремени"], 
			РегистрыСведений.СведенияОВремениДляРасчетаСреднегоОбщий.СоздатьНаборЗаписей(), 
			"Сотрудник, ПорядокРасчета, Месяц",
			НачалоПериода, 
			ОкончаниеПериода,
			Ложь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область КонструкторыТаблицДанныхСреднегоЗаработка

// Создает таблицу значений со структурой аналогичной структуре табличной части СреднийЗаработокОбщий
// для хранения данных среднего заработка в документе.
//
Функция ПустаяТаблицаНачисленийСреднийЗаработокОбщий() Экспорт
	
	ТаблицаДанных = Новый ТаблицаЗначений;
	ТаблицаДанных.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	ТаблицаДанных.Колонки.Добавить("ФизическоеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	ТаблицаДанных.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаДанных.Колонки.Добавить("ПорядокРасчета", Новый ОписаниеТипов("ПеречислениеСсылка.ПорядокРасчетаСреднегоЗаработкаОбщий"));
	ТаблицаДанных.Колонки.Добавить("Начисление", Новый ОписаниеТипов("ПланВидовРасчетаСсылка.Начисления"));
	ТаблицаДанных.Колонки.Добавить("СоставнаяЧасть", Новый ОписаниеТипов("ПеречислениеСсылка.УчетНачисленийВСреднемЗаработкеОбщий"));
	ТаблицаДанных.Колонки.Добавить("СтатьяФинансирования", Новый ОписаниеТипов("СправочникСсылка.СтатьиФинансированияЗарплата"));
	ТаблицаДанных.Колонки.Добавить("СпособОтраженияЗарплатыВБухучете", Новый ОписаниеТипов("СправочникСсылка.СпособыОтраженияЗарплатыВБухУчете"));
	ТаблицаДанных.Колонки.Добавить("СтатьяРасходов", Новый ОписаниеТипов("СправочникСсылка.СтатьиРасходовЗарплата"));
	ТаблицаДанных.Колонки.Добавить("ОблагаетсяЕНВД", Новый ОписаниеТипов("Булево"));
	ТаблицаДанных.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	ТаблицаДанных.Колонки.Добавить("Индексируется", Новый ОписаниеТипов("Булево"));
	ТаблицаДанных.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(15, 2));
	ТаблицаДанных.Колонки.Добавить("Год", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(4, 0));
	ТаблицаДанных.Колонки.Добавить("ДатаНачалаБазовогоПериода", Новый ОписаниеТипов("Дата"));
	ТаблицаДанных.Колонки.Добавить("КоличествоМесяцев", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(3, 0));
	ТаблицаДанных.Колонки.Добавить("Источник", Новый ОписаниеТипов("ПеречислениеСсылка.ИсточникиДанныхДляРасчетаСреднегоЗаработка"));
	ТаблицаДанных.Колонки.Добавить("ИсключеннаяЧасть", Новый ОписаниеТипов("Булево"));
	ТаблицаДанных.Колонки.Добавить("ИсходныйДокумент", Метаданные.ОпределяемыеТипы.СторнируемыйДокумент.Тип);
	ТаблицаДанных.Колонки.Добавить("КлючНачисления", Новый ОписаниеТипов("Число"));
	
	Возврат ТаблицаДанных;
	
КонецФункции

Функция ПустаяТаблицаПремий() Экспорт
	
	ТаблицаДанных = Новый ТаблицаЗначений;
	ТаблицаДанных.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	ТаблицаДанных.Колонки.Добавить("ФизическоеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	ТаблицаДанных.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаДанных.Колонки.Добавить("ПорядокРасчета", Новый ОписаниеТипов("ПеречислениеСсылка.ПорядокРасчетаСреднегоЗаработкаОбщий"));
	ТаблицаДанных.Колонки.Добавить("Начисление", Новый ОписаниеТипов("ПланВидовРасчетаСсылка.Начисления"));
	ТаблицаДанных.Колонки.Добавить("СоставнаяЧасть", Новый ОписаниеТипов("ПеречислениеСсылка.УчетНачисленийВСреднемЗаработкеОбщий"));
	ТаблицаДанных.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	ТаблицаДанных.Колонки.Добавить("Индексируется", Новый ОписаниеТипов("Булево"));
	ТаблицаДанных.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(15, 2));
	ТаблицаДанных.Колонки.Добавить("Год", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(4, 0));
	ТаблицаДанных.Колонки.Добавить("ДатаНачалаБазовогоПериода", Новый ОписаниеТипов("Дата"));
	ТаблицаДанных.Колонки.Добавить("КоличествоМесяцев", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(3, 0));
	ТаблицаДанных.Колонки.Добавить("Источник", Новый ОписаниеТипов("ПеречислениеСсылка.ИсточникиДанныхДляРасчетаСреднегоЗаработка"));
	ТаблицаДанных.Колонки.Добавить("Дублирующая", Новый ОписаниеТипов("Булево"));
	ТаблицаДанных.Колонки.Добавить("Исключенная", Новый ОписаниеТипов("Булево"));
	ТаблицаДанных.Колонки.Добавить("ПоказательПремирования", Новый ОписаниеТипов("СправочникСсылка.ПоказателиПремирования"));
	ТаблицаДанных.Колонки.Добавить("Регистратор");
	ТаблицаДанных.Колонки.Добавить("КлючНачисления", Новый ОписаниеТипов("Число"));
	ТаблицаДанных.Колонки.Добавить("ЭтоПерерасчет", Новый ОписаниеТипов("Булево"));
	
	Возврат ТаблицаДанных;
	
КонецФункции

// Создает таблицу значений со структурой аналогичной структуре табличной части СреднийЗаработокОбщий
// для хранения данных среднего заработка в документе.
//
Функция ПустаяТаблицаГодовыхПремий() Экспорт
	
	ТаблицаДанных = Новый ТаблицаЗначений;
	ТаблицаДанных.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	ТаблицаДанных.Колонки.Добавить("ФизическоеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	ТаблицаДанных.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаДанных.Колонки.Добавить("ПорядокРасчета", Новый ОписаниеТипов("ПеречислениеСсылка.ПорядокРасчетаСреднегоЗаработкаОбщий"));
	ТаблицаДанных.Колонки.Добавить("СоставнаяЧасть", Новый ОписаниеТипов("ПеречислениеСсылка.УчетНачисленийВСреднемЗаработкеОбщий"));
	ТаблицаДанных.Колонки.Добавить("Год", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(4, 0));
	ТаблицаДанных.Колонки.Добавить("СтатьяФинансирования", Новый ОписаниеТипов("СправочникСсылка.СтатьиФинансированияЗарплата"));
	ТаблицаДанных.Колонки.Добавить("СпособОтраженияЗарплатыВБухучете", Новый ОписаниеТипов("СправочникСсылка.СпособыОтраженияЗарплатыВБухУчете"));
	ТаблицаДанных.Колонки.Добавить("СтатьяРасходов", Новый ОписаниеТипов("СправочникСсылка.СтатьиРасходовЗарплата"));
	ТаблицаДанных.Колонки.Добавить("ОблагаетсяЕНВД", Новый ОписаниеТипов("Булево"));
	ТаблицаДанных.Колонки.Добавить("Индексируется", Новый ОписаниеТипов("Булево"));
	ТаблицаДанных.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(15, 2));
	ТаблицаДанных.Колонки.Добавить("Источник", Новый ОписаниеТипов("ПеречислениеСсылка.ИсточникиДанныхДляРасчетаСреднегоЗаработка"));
	
	Возврат ТаблицаДанных;
	
КонецФункции

// Создает таблицу значений со структурой аналогичной структуре табличной части ОтработанноеВремяДляСреднегоОбщий
// для хранения данных среднего заработка в документе.
//
Функция ПустаяТаблицаОтработанноеВремяСреднийЗаработокОбщий() Экспорт
	
	ТаблицаДанных = Новый ТаблицаЗначений;
	ТаблицаДанных.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	ТаблицаДанных.Колонки.Добавить("ПорядокРасчета", Новый ОписаниеТипов("ПеречислениеСсылка.ПорядокРасчетаСреднегоЗаработкаОбщий"));
	ТаблицаДанных.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	ТаблицаДанных.Колонки.Добавить("ОтработаноДней", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(7, 2));
	ТаблицаДанных.Колонки.Добавить("ОтработаноЧасов", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(7, 2));
	ТаблицаДанных.Колонки.Добавить("ОтработаноДнейПятидневка", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(7, 2));
	ТаблицаДанных.Колонки.Добавить("ОтработаноЧасовПятидневка", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(7, 2));
	ТаблицаДанных.Колонки.Добавить("ОтработаноДнейШестидневка", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(7, 2));
	ТаблицаДанных.Колонки.Добавить("ОтработаноДнейКалендарных", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(7, 2));
	ТаблицаДанных.Колонки.Добавить("НормаДнейПроизводственныйКалендарь", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(7, 2));
	ТаблицаДанных.Колонки.Добавить("НормаЧасовПроизводственныйКалендарь", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(7, 2));
	ТаблицаДанных.Колонки.Добавить("Источник", Новый ОписаниеТипов("ПеречислениеСсылка.ИсточникиДанныхДляРасчетаСреднегоЗаработка"));
	
	Возврат ТаблицаДанных;
	
КонецФункции

// Создает таблицу значений со структурой аналогичной структуре табличной части ДанныеОбИндексации
// для хранения данных среднего заработка в документе.
//
Функция ПустаяТаблицаДанныеИндексации() Экспорт
	
	ТаблицаДанных = Новый ТаблицаЗначений;
	ТаблицаДанных.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	ТаблицаДанных.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	ТаблицаДанных.Колонки.Добавить("КоэффициентИндексации", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(12, 10));
	
	Возврат ТаблицаДанных;
	
КонецФункции

Функция ПустаяТаблицаИсходныхДанныхРасчетаОбщегоСреднегоЗаработка() Экспорт
	
	ИсходныеДанные = Новый ТаблицаЗначений;
	
	ИсходныеДанные.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	ИсходныеДанные.Колонки.Добавить("ПорядокРасчета", Новый ОписаниеТипов("ПеречислениеСсылка.ПорядокРасчетаСреднегоЗаработкаОбщий"));
	ИсходныеДанные.Колонки.Добавить("ДатаНачалаСобытия", Новый ОписаниеТипов("Дата"));
	ИсходныеДанные.Колонки.Добавить("НачалоПериодаРасчетаСреднего", Новый ОписаниеТипов("Дата"));
	ИсходныеДанные.Колонки.Добавить("ОкончаниеПериодаРасчетаСреднего", Новый ОписаниеТипов("Дата"));
	
	Возврат ИсходныеДанные;
	
КонецФункции

Функция ПустаяТаблицаДляРегистрацииДанныхСреднегоЗаработка() Экспорт
	
	ТаблицаДанных = Новый ТаблицаЗначений;
	
	ТипыРегистраторов = Новый Массив;
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.БольничныйЛист"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.ДоходВНатуральнойФорме"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.Командировка"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.МатериальнаяПомощь"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.НачислениеЗарплаты"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.НачислениеПоДоговорам"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.ОплатаДнейУходаЗаДетьмиИнвалидами"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.ОплатаПоСреднемуЗаработку"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.Отгул"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.ОтгулСписком"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.Отпуск"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.ОтпускБезСохраненияОплаты"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.ОтпускБезСохраненияОплатыСписком"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.ОтпускПоУходуЗаРебенком"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.ПереносДанных"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.Премия"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.ПрогулНеявка"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.ПрогулНеявкаСписком"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.ПростойСотрудников"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.РазовоеНачисление"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.СторнированиеНачислений"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.Увольнение"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.УвольнениеСписком"));
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ГосударственнаяСлужба");
		Модуль.ДобавитьТипыРегистраторовСреднегоЗаработка(ТипыРегистраторов);
	КонецЕсли;
	
	ТаблицаДанных.Колонки.Добавить("ДокументСсылка", Новый ОписаниеТипов(ТипыРегистраторов));
	
	ТаблицаДанных.Колонки.Добавить("ДатаДействия", Новый ОписаниеТипов("Дата"));
	ТаблицаДанных.Колонки.Добавить("ПериодДействия", Новый ОписаниеТипов("Дата"));
	ТаблицаДанных.Колонки.Добавить("МесяцНачисления", Новый ОписаниеТипов("Дата"));
	ТаблицаДанных.Колонки.Добавить("ДатаНачала", Новый ОписаниеТипов("Дата"));
	ТаблицаДанных.Колонки.Добавить("ДатаОкончания", Новый ОписаниеТипов("Дата"));
	ТаблицаДанных.Колонки.Добавить("НачалоБазовогоПериода", Новый ОписаниеТипов("Дата"));
	ТаблицаДанных.Колонки.Добавить("ОкончаниеБазовогоПериода", Новый ОписаниеТипов("Дата"));
	ТаблицаДанных.Колонки.Добавить("Сторно", Новый ОписаниеТипов("Булево"));
	ТаблицаДанных.Колонки.Добавить("ФиксСторно", Новый ОписаниеТипов("Булево"));
	ТаблицаДанных.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	ТаблицаДанных.Колонки.Добавить("ФизическоеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	ТаблицаДанных.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаДанных.Колонки.Добавить("Начисление", Новый ОписаниеТипов("ПланВидовРасчетаСсылка.Начисления, ПеречислениеСсылка.ВидыОсобыхНачисленийИУдержаний"));
	ТаблицаДанных.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(15, 2));
	ТаблицаДанных.Колонки.Добавить("РанееНачислено", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(15, 2));
	ТаблицаДанных.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число"));
	ТаблицаДанных.Колонки.Добавить("ИдентификаторСтроки", Новый ОписаниеТипов("Число"));
	ТаблицаДанных.Колонки.Добавить("СтатьяФинансирования", Новый ОписаниеТипов("СправочникСсылка.СтатьиФинансированияЗарплата"));
	ТаблицаДанных.Колонки.Добавить("СпособОтраженияЗарплатыВБухучете", Новый ОписаниеТипов("СправочникСсылка.СпособыОтраженияЗарплатыВБухУчете"));
	ТаблицаДанных.Колонки.Добавить("СтатьяРасходов", Новый ОписаниеТипов("СправочникСсылка.СтатьиРасходовЗарплата"));
	ТаблицаДанных.Колонки.Добавить("ОблагаетсяЕНВД", Новый ОписаниеТипов("Булево"));
	ТаблицаДанных.Колонки.Добавить("ПоказательПремирования", Новый ОписаниеТипов("СправочникСсылка.ПоказателиПремирования"));
	ТаблицаДанных.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	ТаблицаДанных.Колонки.Добавить("ДокументОснование", Метаданные.ОпределяемыеТипы.ОснованиеНачисления.Тип);
	ТаблицаДанных.Колонки.Добавить("ИсходныйДокумент", Метаданные.ОпределяемыеТипы.СторнируемыйДокумент.Тип);
	ТаблицаДанных.Колонки.Добавить("СторнируемыйДокумент", Метаданные.ОпределяемыеТипы.СторнируемыйДокумент.Тип);
	
	Возврат ТаблицаДанных;
	
КонецФункции

#КонецОбласти

#Область ИсключениеДублирующихПремий

Функция ДокументМожетМенятьПоказательПремирования(Документ) Экспорт
	
	Возврат Документ.Метаданные().Реквизиты.Найти("ПоказательПремирования") <> Неопределено;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УдалитьПричиныПерерасчетов(СсылкаНаДокумент, ДополнительныеПараметры) Экспорт
	
	Если ДополнительныеПараметры = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Получение признака о том, что нужно удалить перерасчеты текущего периода
	УдалитьПерерасчетыСреднегоЗаработка = Неопределено;
	ДополнительныеПараметры.Свойство("УдалитьПерерасчетыСреднегоЗаработка", УдалитьПерерасчетыСреднегоЗаработка);
	Если УдалитьПерерасчетыСреднегоЗаработка <> Истина Тогда
		Возврат;
	КонецЕсли;
	
	Сотрудники = Неопределено;
	ДополнительныеПараметры.Свойство("СотрудникиПерерасчетаЗаработка", Сотрудники);
	
	НаборЗаписей = РегистрыСведений.ПерерасчетСреднегоЗаработка.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ДокументСреднегоЗаработка.Установить(СсылкаНаДокумент);
	
	Если Сотрудники <> Неопределено Тогда
		
		Если ТипЗнч(Сотрудники) = Тип("СправочникСсылка.Сотрудники") Тогда
			СписокСотрудников = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудники);
		Иначе
			СписокСотрудников = Сотрудники;
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("СписокСотрудников", СписокСотрудников);
		Запрос.УстановитьПараметр("СсылкаНаДокумент", СсылкаНаДокумент);
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ПерерасчетСреднегоЗаработка.Организация,
			|	ПерерасчетСреднегоЗаработка.Сотрудник,
			|	ПерерасчетСреднегоЗаработка.ДокументСреднегоЗаработка,
			|	ПерерасчетСреднегоЗаработка.ДокументОснование
			|ИЗ
			|	РегистрСведений.ПерерасчетСреднегоЗаработка КАК ПерерасчетСреднегоЗаработка
			|ГДЕ
			|	НЕ ПерерасчетСреднегоЗаработка.Сотрудник В (&СписокСотрудников)
			|	И ПерерасчетСреднегоЗаработка.ДокументСреднегоЗаработка = &СсылкаНаДокумент";
		
		НаборЗаписей.Загрузить(Запрос.Выполнить().Выгрузить());
		
	КонецЕсли;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

Функция РегистрыСреднегоЗаработка() Экспорт
	
	МассивРегистров = Новый Массив;
	
	УчетПособийСоциальногоСтрахованияРасширенный.РегистрыУчетаПособийСоциальногоСтрахованияРасширенная(МассивРегистров);
	
	МассивРегистров.Добавить(Метаданные.РегистрыНакопления.ДанныеОВремениДляРасчетаСреднегоОбщий);
	МассивРегистров.Добавить(Метаданные.РегистрыНакопления.ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий);
	МассивРегистров.Добавить(Метаданные.РегистрыНакопления.ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям);
			
	Возврат МассивРегистров;

КонецФункции

#Область РегистрацияДанныхДляРасчетаСреднегоЗаработка

// Процедура выполняет регистрацию данных о начисленных суммах и отработанном времени
//  для использования при расчете общего среднего заработка.
//
//	Параметры: см. комментарий к методу ЗарегистрироватьДанныеСреднегоЗаработка.
//
Процедура ЗарегистрироватьДанныеОбщегоСреднегоЗаработка(Движения, Отказ, МенеджерВременныхТаблиц, ЗаписыватьДвижения = Ложь)
	
	// Время нахождения работника в служебной командировке, 
	// а также начисленные за это время суммы исключаются при исчислении среднего заработка.
	// - определяем начисления, которые зависят от расчета времени и при этом не вытесняются командировкой,
	// - определяем фактический период действия командировок этих сотрудников в выбранных периодах действия, 
	// - при регистрации начислений:
	//	* получаем расчетную базу таких начислений за периоды командировок, 
	//	* вычитаем расчетную базу из регистрируемых начислений,
	// - при регистрации времени:
	//	* получаем данные времени выбранных начислений за фактический период действия командировок,
	//	* вычитаем количество дней и часов из регистрируемого отработанного времени.
	// Если нет таких начислений или не случилось командировок, ничего не делаем, добавляем пустышку в запрос.
	
	ИсключатьВПериодКомандировок = Истина;
	СоздатьВТДляИсключенияКомандировок(МенеджерВременныхТаблиц, ИсключатьВПериодКомандировок);
	
	ЗарегистрироватьНачисленияДляРасчетаОбщегоСреднегоЗаработка(Движения, Отказ, МенеджерВременныхТаблиц, ИсключатьВПериодКомандировок, ЗаписыватьДвижения);
	
	ЗарегистрироватьДанныеВремениДляРасчетаОбщегоСреднегоЗаработка(Движения, Отказ, МенеджерВременныхТаблиц, ИсключатьВПериодКомандировок, ЗаписыватьДвижения);
	
КонецПроцедуры

Процедура ИсключитьНачисленияВПериодКомандировок(МенеджерВременныхТаблиц, Регистратор, ИсключатьВПериодКомандировок)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		
	// Определяем суммы начислений, попадающих в период командировки.
	Если Не ИсключатьВПериодКомандировок Тогда
		Запрос.Текст = 
			"ВЫБРАТЬ
				|	*
				|ПОМЕСТИТЬ ВТНачисленияПринимаемые
				|ИЗ
				|	ВТНачисления КАК Начисления";
		Запрос.Выполнить();
		Возврат;
	КонецЕсли;
	
	// Определяем максимальный период действия для ограничения базовых записей.
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Начисления.ПериодДействия КАК ПериодДействия
		|ПОМЕСТИТЬ ВТПериодыДействияНачислений
		|ИЗ
		|	ВТНачисления КАК Начисления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПериодыДействияНачислений.МесяцНачисления
		|ИЗ
		|	ВТНачисления КАК ПериодыДействияНачислений
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(Начисления.ПериодДействия) КАК МаксимальныйПериодДействия
		|ИЗ
		|	ВТПериодыДействияНачислений КАК Начисления";
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Запрос.УстановитьПараметр("МаксимальныйПериодДействия", Выборка.МаксимальныйПериодДействия);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	&МаксимальныйПериодДействия КАК ПериодРегистрации,
		|	ПериодыКомандировок.Регистратор,
		|	ПериодыКомандировок.НомерСтроки,
		|	ПериодыКомандировок.Сотрудник,
		|	ПериодыКомандировок.ВидРасчета,
		|	ПериодыКомандировок.Начало КАК БазовыйПериодНачало,
		|	ПериодыКомандировок.Окончание КАК БазовыйПериодКонец
		|ПОМЕСТИТЬ ВТОсновныеЗаписи
		|ИЗ
		|	ВТПериодыКомандировок КАК ПериодыКомандировок";
	Запрос.Выполнить();
	
	// Готовим запрос к расчетной базе начислений.
	ИменаИзмерений = РасчетЗарплатыРасширенный.ИменаИзмеренийРасчетнойБазыНачислений();
	ИменаИзмерений.Сотрудник = "Сотрудник";
	
	ОтборБазовыхЗаписей = Новый Массив;
	ОтборБазовыхЗаписей.Добавить(РасчетЗарплатыРасширенный.ЭлементОтбораБазовыхЗаписей("Регистратор", Регистратор));
	
	РасчетЗарплатыРасширенный.СоздатьВТРасчетнаяБаза(МенеджерВременныхТаблиц, ИменаИзмерений, "ВТИсключаемыеВПериодКомандировки", ОтборБазовыхЗаписей);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИсключаемыеСуммы.ИдентификаторСтрокиРазрез КАК ИдентификаторСтроки,
		|	СУММА(ИсключаемыеСуммы.РезультатБаза) КАК Сумма
		|ПОМЕСТИТЬ ВТИсключаемыеВПериодКомандировкиСуммы
		|ИЗ
		|	ВТРасчетнаяБаза КАК ИсключаемыеСуммы
		|
		|СГРУППИРОВАТЬ ПО
		|	ИсключаемыеСуммы.ИдентификаторСтрокиРазрез
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТОсновныеЗаписи
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТРасчетнаяБаза";
	Запрос.Выполнить();
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
	
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ИсключаемыеСуммы.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	ИсключаемыеСуммы.Сумма КАК Сумма,
			|	Начисления.Сумма КАК СуммаНачисления,
			|	Начисления.СтатьяФинансирования КАК СтатьяФинансирования,
			|	Начисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
			|	Начисления.СтатьяРасходов КАК СтатьяРасходов
			|ИЗ
			|	ВТИсключаемыеВПериодКомандировкиСуммы КАК ИсключаемыеСуммы
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНачисления КАК Начисления
			|		ПО ИсключаемыеСуммы.ИдентификаторСтроки = Начисления.ИдентификаторСтроки
			|
			|УПОРЯДОЧИТЬ ПО
			|	ИдентификаторСтроки";
		Выборка = Запрос.Выполнить().Выбрать();
		
		ИсключаемыеСуммы = Новый ТаблицаЗначений;
		ИсключаемыеСуммы.Колонки.Добавить("ИдентификаторСтроки", Новый ОписаниеТипов("Число"));
		ИсключаемыеСуммы.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число"));
		ИсключаемыеСуммы.Колонки.Добавить("СтатьяФинансирования", Новый ОписаниеТипов("СправочникСсылка.СтатьиФинансированияЗарплата"));
		ИсключаемыеСуммы.Колонки.Добавить("СпособОтраженияЗарплатыВБухучете", Новый ОписаниеТипов("СправочникСсылка.СпособыОтраженияЗарплатыВБухУчете"));
		ИсключаемыеСуммы.Колонки.Добавить("СтатьяРасходов", Новый ОписаниеТипов("СправочникСсылка.СтатьиРасходовЗарплата"));
		
		Пока Выборка.СледующийПоЗначениюПоля("ИдентификаторСтроки") Цикл
			
			ИдентификаторСтроки = Выборка.ИдентификаторСтроки;
			Сумма = Выборка.Сумма;
			
			Если Сумма = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Коэффициенты = Новый Массив;
			ИсключаемыеСуммыНачислений = ИсключаемыеСуммы.СкопироватьКолонки();
			
			Пока Выборка.Следующий() Цикл 
				ЗаполнитьЗначенияСвойств(ИсключаемыеСуммыНачислений.Добавить(), Выборка);
				Коэффициенты.Добавить(Выборка.СуммаНачисления);
			КонецЦикла;
			
			Суммы = ЗарплатаКадрыКлиентСервер.РаспределитьПропорциональноКоэффициентам(Сумма, Коэффициенты);
			
			Если Суммы = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Индекс = 0;
			Для Каждого ИсключаемыеСуммыСтрока Из ИсключаемыеСуммыНачислений Цикл
				ИсключаемыеСуммыСтрока.Сумма = Суммы[Индекс];
				Индекс = Индекс + 1;
			КонецЦикла;
			
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ИсключаемыеСуммыНачислений, ИсключаемыеСуммы);
			
		КонецЦикла;
		
		Запрос.УстановитьПараметр("ИсключаемыеСуммы", ИсключаемыеСуммы);
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ИсключаемыеСуммы.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	ИсключаемыеСуммы.Сумма КАК Сумма,
			|	ИсключаемыеСуммы.СтатьяФинансирования КАК СтатьяФинансирования,
			|	ИсключаемыеСуммы.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
			|	ИсключаемыеСуммы.СтатьяРасходов КАК СтатьяРасходов
			|ПОМЕСТИТЬ ВТИсключаемыеСуммыПоСтатьямФинансирования
			|ИЗ
			|	&ИсключаемыеСуммы КАК ИсключаемыеСуммы
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	*
			|ПОМЕСТИТЬ ВТНачисленияПринимаемые
			|ИЗ
			|	ВТНачисления КАК Начисления
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ВТНачисления
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Начисления.Сумма КАК Сумма,
			|	Начисления.*
			|ПОМЕСТИТЬ ВТНачисления
			|ИЗ
			|	ВТНачисленияПринимаемые КАК Начисления
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	-ИсключаемыеСуммы.Сумма,
			|	Начисления.*
			|ИЗ
			|	ВТНачисленияПринимаемые КАК Начисления
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТИсключаемыеСуммыПоСтатьямФинансирования КАК ИсключаемыеСуммы
			|		ПО Начисления.ИдентификаторСтроки = ИсключаемыеСуммы.ИдентификаторСтроки
			|			И Начисления.СтатьяФинансирования = ИсключаемыеСуммы.СтатьяФинансирования
			|			И Начисления.СпособОтраженияЗарплатыВБухучете = ИсключаемыеСуммы.СпособОтраженияЗарплатыВБухучете
			|			И Начисления.СтатьяРасходов = ИсключаемыеСуммы.СтатьяРасходов";
		Запрос.Выполнить();
		
	Иначе
		
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ИсключаемыеСуммы.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	ИсключаемыеСуммы.Сумма КАК Сумма,
			|	ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка) КАК СтатьяФинансирования,
			|	ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка) КАК СпособОтраженияЗарплатыВБухучете,
			|	ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка) КАК СтатьяРасходов
			|ПОМЕСТИТЬ ВТИсключаемыеСуммыПоСтатьямФинансирования
			|ИЗ
			|	ВТИсключаемыеВПериодКомандировкиСуммы КАК ИсключаемыеСуммы";
		Запрос.Выполнить();
		
		// Регистрация сумм начислений для общего среднего заработка.
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	*
			|ПОМЕСТИТЬ ВТНачисленияПринимаемые
			|ИЗ
			|	ВТНачисления КАК Начисления
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ВТНачисления
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Начисления.Сумма КАК Сумма,
			|	Начисления.*
			|ПОМЕСТИТЬ ВТНачисления
			|ИЗ
			|	ВТНачисленияПринимаемые КАК Начисления
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	-ИсключаемыеСуммы.Сумма,
			|	Начисления.*
			|ИЗ
			|	ВТНачисленияПринимаемые КАК Начисления
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТИсключаемыеВПериодКомандировкиСуммы КАК ИсключаемыеСуммы
			|		ПО (ИсключаемыеСуммы.ИдентификаторСтроки = Начисления.ИдентификаторСтроки)";
		Запрос.Выполнить();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗарегистрироватьНачисленияДляРасчетаОбщегоСреднегоЗаработка(Движения, Отказ, МенеджерВременныхТаблиц, ИсключатьВПериодКомандировок, ЗаписыватьДвижения = Ложь)
		
	Регистратор = Движения.ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Отбор.Регистратор.Значение;
	
	ИсключитьНачисленияВПериодКомандировок(МенеджерВременныхТаблиц, Регистратор, ИсключатьВПериодКомандировок);	
	
	СоздатьВТПорядокРасчетаОбщегоСреднегоЗаработка(МенеджерВременныхТаблиц);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	КОНЕЦПЕРИОДА(Начисления.МесяцНачисления, МЕСЯЦ) КАК Период,
	               |	Начисления.Сотрудник КАК Сотрудник
	               |ПОМЕСТИТЬ ВТСписокСотрудников
	               |ИЗ
	               |	ВТНачисления КАК Начисления";
				   
	Запрос.Выполнить();			   
	
	ОписательВременныхТаблиц = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(
		Запрос.МенеджерВременныхТаблиц, "ВТСписокСотрудников");
	
	КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(ОписательВременныхТаблиц, Истина, "ДатаПриема");
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Начисления.ДатаДействия КАК ДатаДействия,
		|	Начисления.ПериодДействия КАК ПериодДействия,
		|	Начисления.ДатаНачала КАК ДатаНачала,
		|	Начисления.ДатаОкончания КАК ДатаОкончания,
		|	Начисления.МесяцНачисления КАК МесяцНачисления,
		|	Начисления.НомерСтроки КАК НомерСтроки,
		|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	Начисления.Сотрудник КАК Сотрудник,
		|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
		|	Начисления.Организация КАК Организация,
		|	Начисления.СтатьяФинансирования КАК СтатьяФинансирования,
		|	Начисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	Начисления.СтатьяРасходов КАК СтатьяРасходов,
		|	Начисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	Начисления.Начисление КАК Начисление,
		|	Начисления.НачалоБазовогоПериода КАК НачалоБазовогоПериода,
		|	Начисления.ОкончаниеБазовогоПериода КАК ОкончаниеБазовогоПериода,
		|	Начисления.ДокументСсылка КАК ДокументСсылка,
		|	Начисления.Сумма КАК Сумма,
		|	Начисления.РанееНачислено КАК РанееНачислено,
		|	ВЫБОР
		|		КОГДА Начисления.Сторно
		|				ИЛИ Начисления.ФиксСторно
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Сторно,
		|	Начисления.ФиксСторно КАК ФиксСторно,
		|	Начисления.ИсходныйДокумент КАК ИсходныйДокумент,
		|	Начисления.СторнируемыйДокумент КАК СторнируемыйДокумент,
		|	Начисления.ПоказательПремирования КАК ПоказательПремирования,
		|	ПорядокРасчетаДляУчетаНачислений.ПорядокРасчета КАК ПорядокРасчета,
		|	НастройкиНачисления.Значение КАК СоставнаяЧасть,
		|	НастройкиНачисления.Индексируется КАК Индексируется,
		|	ЕСТЬNULL(КадровыеДанные.ДатаПриема, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаПриема
		|ПОМЕСТИТЬ ВТНачисленияОбщегоСреднегоЗаработка
		|ИЗ
		|	ВТНачисления КАК Начисления
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПорядокРасчетаОбщегоСреднегоЗаработка КАК ПорядокРасчетаДляУчетаНачислений
		|		ПО (ПорядокРасчетаДляУчетаНачислений.ДатаДействия = Начисления.ДатаДействия)
		|			И (ПорядокРасчетаДляУчетаНачислений.Начисление = Начисления.Начисление)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления.СреднийЗаработокОбщий КАК НастройкиНачисления
		|		ПО (НастройкиНачисления.Ссылка = Начисления.Начисление)
		|			И (НастройкиНачисления.ПорядокРасчета = ПорядокРасчетаДляУчетаНачислений.ПорядокРасчета)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанныеСотрудников КАК КадровыеДанные
		|		ПО Начисления.Сотрудник = КадровыеДанные.Сотрудник
		|			И (КОНЕЦПЕРИОДА(Начисления.МесяцНачисления, МЕСЯЦ) = КадровыеДанные.Период)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТНачисления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	Начисления.ДокументСсылка КАК ДокументСсылка,
		|	Начисления.Начисление КАК Начисление,
		|	ВЫБОР
		|		КОГДА ВидыРасчета.ИспользованиеПериода <> ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияПериодаНачисления.ПериодДействия)
		|			ТОГДА Начисления.ПериодДействия
		|		ИНАЧЕ НАЧАЛОПЕРИОДА(Начисления.ДатаДействия, МЕСЯЦ)
		|	КОНЕЦ КАК Период,
		|	Начисления.ДатаДействия КАК ДатаДействия,
		|	Начисления.Сотрудник КАК Сотрудник,
		|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
		|	Начисления.Организация КАК Организация,
		|	Начисления.ПорядокРасчета КАК ПорядокРасчета,
		|	Начисления.СоставнаяЧасть КАК СоставнаяЧасть,
		|	Начисления.СтатьяФинансирования КАК СтатьяФинансирования,
		|	Начисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	Начисления.СтатьяРасходов КАК СтатьяРасходов,
		|	Начисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	Начисления.Индексируется КАК Индексируется,
		|	ВЫБОР
		|		КОГДА Начисления.СоставнаяЧасть В (&ГодовыеПремии)
		|			ТОГДА ГОД(Начисления.НачалоБазовогоПериода)
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Год,
		|	ВЫБОР
		|		КОГДА Начисления.СоставнаяЧасть В (&Премии)
		|			ТОГДА ВЫБОР
		|					КОГДА Начисления.СоставнаяЧасть В (&ГодовыеПремии)
		|						ТОГДА 12
		|					КОГДА Начисления.ДатаПриема > Начисления.НачалоБазовогоПериода
		|						ТОГДА ВЫБОР
		|								КОГДА НАЧАЛОПЕРИОДА(Начисления.ОкончаниеБазовогоПериода, МЕСЯЦ) >= НАЧАЛОПЕРИОДА(Начисления.ДатаПриема, МЕСЯЦ)
		|									ТОГДА РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(Начисления.ДатаПриема, МЕСЯЦ), НАЧАЛОПЕРИОДА(Начисления.ОкончаниеБазовогоПериода, МЕСЯЦ), МЕСЯЦ) + 1
		|								ИНАЧЕ 1
		|							КОНЕЦ
		|					ИНАЧЕ РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(Начисления.НачалоБазовогоПериода, МЕСЯЦ), НАЧАЛОПЕРИОДА(Начисления.ОкончаниеБазовогоПериода, МЕСЯЦ), МЕСЯЦ) + 1
		|				КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК КоличествоМесяцев,
		|	ВЫБОР
		|		КОГДА Начисления.СоставнаяЧасть В (&Премии)
		|			ТОГДА ВЫБОР
		|					КОГДА Начисления.ДатаПриема > Начисления.НачалоБазовогоПериода
		|						ТОГДА Начисления.ДатаПриема
		|					ИНАЧЕ Начисления.НачалоБазовогоПериода
		|				КОНЕЦ
		|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
		|	КОНЕЦ КАК ДатаНачалаБазовогоПериода,
		|	Начисления.Сумма КАК Сумма,
		|	Начисления.РанееНачислено КАК РанееНачислено,
		|	Начисления.ПериодДействия КАК ПериодДействия,
		|	Начисления.ДатаНачала КАК ДатаНачала,
		|	Начисления.ДатаОкончания КАК ДатаОкончания,
		|	Начисления.МесяцНачисления КАК МесяцНачисления,
		|	Начисления.НомерСтроки КАК НомерСтроки,
		|	Начисления.НачалоБазовогоПериода КАК НачалоБазовогоПериода,
		|	Начисления.ОкончаниеБазовогоПериода КАК ОкончаниеБазовогоПериода,
		|	Начисления.Сторно КАК Сторно,
		|	Начисления.ИсходныйДокумент КАК ИсходныйДокумент,
		|	Начисления.СторнируемыйДокумент КАК СторнируемыйДокумент,
		|	Начисления.ПоказательПремирования КАК ПоказательПремирования,
		|	Начисления.ФиксСторно КАК ФиксСторно,
		|	Начисления.ДатаПриема КАК ДатаПриема
		|ПОМЕСТИТЬ ВТНачисленияПредварительно
		|ИЗ
		|	ВТНачисленияОбщегоСреднегоЗаработка КАК Начисления
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления КАК ВидыРасчета
		|		ПО (ВидыРасчета.Ссылка = Начисления.Начисление)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Начисления.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ВТРаспределяемыеНачисления
		|ИЗ
		|	ПланВидовРасчета.Начисления КАК Начисления
		|ГДЕ
		|	Начисления.СтратегияОтраженияВСреднемЗаработке = ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазовымРасчетам)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НачисленияПредварительно.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	НачисленияПредварительно.ДокументСсылка КАК ДокументСсылка,
		|	НачисленияПредварительно.Начисление КАК Начисление,
		|	НачисленияПредварительно.Период КАК Период,
		|	НачисленияПредварительно.ДатаДействия КАК ДатаДействия,
		|	НачисленияПредварительно.Сотрудник КАК Сотрудник,
		|	НачисленияПредварительно.ФизическоеЛицо КАК ФизическоеЛицо,
		|	НачисленияПредварительно.Организация КАК Организация,
		|	ЕСТЬNULL(ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям.ПорядокРасчета, НачисленияПредварительно.ПорядокРасчета) КАК ПорядокРасчета,
		|	ЕСТЬNULL(ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям.СоставнаяЧасть, НачисленияПредварительно.СоставнаяЧасть) КАК СоставнаяЧасть,
		|	НачисленияПредварительно.СтатьяФинансирования КАК СтатьяФинансирования,
		|	НачисленияПредварительно.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	НачисленияПредварительно.СтатьяРасходов КАК СтатьяРасходов,
		|	ЕСТЬNULL(ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям.ОблагаетсяЕНВД, НачисленияПредварительно.ОблагаетсяЕНВД) КАК ОблагаетсяЕНВД,
		|	ЕСТЬNULL(ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям.Индексируется, НачисленияПредварительно.Индексируется) КАК Индексируется,
		|	ЕСТЬNULL(ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям.Год, НачисленияПредварительно.Год) КАК Год,
		|	ЕСТЬNULL(ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям.КоличествоМесяцев, НачисленияПредварительно.КоличествоМесяцев) КАК КоличествоМесяцев,
		|	ЕСТЬNULL(ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям.ДатаНачалаБазовогоПериода, НачисленияПредварительно.ДатаНачалаБазовогоПериода) КАК ДатаНачалаБазовогоПериода,
		|	НачисленияПредварительно.Сумма КАК Сумма,
		|	НачисленияПредварительно.РанееНачислено КАК РанееНачислено,
		|	НачисленияПредварительно.ПериодДействия КАК ПериодДействия,
		|	НачисленияПредварительно.ДатаНачала КАК ДатаНачала,
		|	НачисленияПредварительно.ДатаОкончания КАК ДатаОкончания,
		|	НачисленияПредварительно.МесяцНачисления КАК МесяцНачисления,
		|	НачисленияПредварительно.НомерСтроки КАК НомерСтроки,
		|	НачисленияПредварительно.НачалоБазовогоПериода КАК НачалоБазовогоПериода,
		|	НачисленияПредварительно.ОкончаниеБазовогоПериода КАК ОкончаниеБазовогоПериода,
		|	НачисленияПредварительно.Сторно КАК Сторно,
		|	НачисленияПредварительно.ИсходныйДокумент КАК ИсходныйДокумент,
		|	НачисленияПредварительно.СторнируемыйДокумент КАК СторнируемыйДокумент,
		|	ЕСТЬNULL(ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям.ПоказательПремирования, НачисленияПредварительно.ПоказательПремирования) КАК ПоказательПремирования,
		|	НачисленияПредварительно.ФиксСторно КАК ФиксСторно,
		|	НачисленияПредварительно.ДатаПриема КАК ДатаПриема,
		|	ВЫБОР
		|		КОГДА ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям.Регистратор ЕСТЬ NULL
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК РаспределятьПоБазе,
		|	-ЕСТЬNULL(ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям.Сумма, 0) КАК СуммаСторно
		|ПОМЕСТИТЬ ВТСторноСтрокиРаспределяемыхНачислений
		|ИЗ
		|	ВТНачисленияПредварительно КАК НачисленияПредварительно
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям КАК ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям
		|		ПО НачисленияПредварительно.СторнируемыйДокумент = ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям.Регистратор
		|			И НачисленияПредварительно.Сотрудник В(ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям.Сотрудник, ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям.СовмещающийСотрудник)
		|			И НачисленияПредварительно.Начисление = ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям.Начисление
		|			И НачисленияПредварительно.СпособОтраженияЗарплатыВБухучете = ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям.СпособОтраженияЗарплатыВБухучете
		|			И НачисленияПредварительно.СтатьяРасходов = ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям.СтатьяРасходов
		|			И НачисленияПредварительно.СтатьяФинансирования = ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям.СтатьяФинансирования
		|			И НачисленияПредварительно.Период = ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям.Период
		|			И НачисленияПредварительно.ДатаНачала = ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям.ДатаНачала
		|			И НачисленияПредварительно.ДатаОкончания = ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям.ДатаОкончания
		|			И (НЕ ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям.Сторно)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРаспределяемыеНачисления КАК РаспределяемыеНачисления
		|		ПО НачисленияПредварительно.Начисление = РаспределяемыеНачисления.Ссылка
		|ГДЕ
		|	(НачисленияПредварительно.ФиксСторно
		|			ИЛИ НачисленияПредварительно.Сторно)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НачисленияПредварительно.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	НачисленияПредварительно.ДокументСсылка КАК ДокументСсылка,
		|	НачисленияПредварительно.Начисление КАК Начисление,
		|	НачисленияПредварительно.Период КАК Период,
		|	НачисленияПредварительно.ДатаДействия КАК ДатаДействия,
		|	НачисленияПредварительно.Сотрудник КАК Сотрудник,
		|	НачисленияПредварительно.ФизическоеЛицо КАК ФизическоеЛицо,
		|	НачисленияПредварительно.Организация КАК Организация,
		|	НачисленияПредварительно.ПорядокРасчета КАК ПорядокРасчета,
		|	НачисленияПредварительно.СоставнаяЧасть КАК СоставнаяЧасть,
		|	НачисленияПредварительно.СтатьяФинансирования КАК СтатьяФинансирования,
		|	НачисленияПредварительно.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	НачисленияПредварительно.СтатьяРасходов КАК СтатьяРасходов,
		|	НачисленияПредварительно.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	НачисленияПредварительно.Индексируется КАК Индексируется,
		|	НачисленияПредварительно.Год КАК Год,
		|	НачисленияПредварительно.КоличествоМесяцев КАК КоличествоМесяцев,
		|	НачисленияПредварительно.ДатаНачалаБазовогоПериода КАК ДатаНачалаБазовогоПериода,
		|	НачисленияПредварительно.Сумма КАК Сумма,
		|	НачисленияПредварительно.РанееНачислено КАК РанееНачислено,
		|	НачисленияПредварительно.ПериодДействия КАК ПериодДействия,
		|	НачисленияПредварительно.ДатаНачала КАК ДатаНачала,
		|	НачисленияПредварительно.ДатаОкончания КАК ДатаОкончания,
		|	НачисленияПредварительно.МесяцНачисления КАК МесяцНачисления,
		|	НачисленияПредварительно.НомерСтроки КАК НомерСтроки,
		|	НачисленияПредварительно.НачалоБазовогоПериода КАК НачалоБазовогоПериода,
		|	НачисленияПредварительно.ОкончаниеБазовогоПериода КАК ОкончаниеБазовогоПериода,
		|	НачисленияПредварительно.Сторно КАК Сторно,
		|	НачисленияПредварительно.ИсходныйДокумент КАК ИсходныйДокумент,
		|	НачисленияПредварительно.СторнируемыйДокумент КАК СторнируемыйДокумент,
		|	НачисленияПредварительно.ПоказательПремирования КАК ПоказательПремирования,
		|	НачисленияПредварительно.ФиксСторно КАК ФиксСторно,
		|	НачисленияПредварительно.ДатаПриема КАК ДатаПриема
		|ПОМЕСТИТЬ ВТНачисления
		|ИЗ
		|	ВТНачисленияПредварительно КАК НачисленияПредварительно
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРаспределяемыеНачисления КАК РаспределяемыеНачисления
		|		ПО НачисленияПредварительно.Начисление = РаспределяемыеНачисления.Ссылка
		|			И (НачисленияПредварительно.Сторно
		|				ИЛИ НачисленияПредварительно.ФиксСторно)
		|ГДЕ
		|	РаспределяемыеНачисления.Ссылка ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СторноСтрокиРаспределяемыхНачислений.ИдентификаторСтроки,
		|	СторноСтрокиРаспределяемыхНачислений.ДокументСсылка,
		|	СторноСтрокиРаспределяемыхНачислений.Начисление,
		|	СторноСтрокиРаспределяемыхНачислений.Период,
		|	СторноСтрокиРаспределяемыхНачислений.ДатаДействия,
		|	СторноСтрокиРаспределяемыхНачислений.Сотрудник,
		|	СторноСтрокиРаспределяемыхНачислений.ФизическоеЛицо,
		|	СторноСтрокиРаспределяемыхНачислений.Организация,
		|	СторноСтрокиРаспределяемыхНачислений.ПорядокРасчета,
		|	СторноСтрокиРаспределяемыхНачислений.СоставнаяЧасть,
		|	СторноСтрокиРаспределяемыхНачислений.СтатьяФинансирования,
		|	СторноСтрокиРаспределяемыхНачислений.СпособОтраженияЗарплатыВБухучете,
		|	СторноСтрокиРаспределяемыхНачислений.СтатьяРасходов,
		|	СторноСтрокиРаспределяемыхНачислений.ОблагаетсяЕНВД,
		|	СторноСтрокиРаспределяемыхНачислений.Индексируется,
		|	СторноСтрокиРаспределяемыхНачислений.Год,
		|	СторноСтрокиРаспределяемыхНачислений.КоличествоМесяцев,
		|	СторноСтрокиРаспределяемыхНачислений.ДатаНачалаБазовогоПериода,
		|	СторноСтрокиРаспределяемыхНачислений.Сумма,
		|	СторноСтрокиРаспределяемыхНачислений.РанееНачислено,
		|	СторноСтрокиРаспределяемыхНачислений.ПериодДействия,
		|	СторноСтрокиРаспределяемыхНачислений.ДатаНачала,
		|	СторноСтрокиРаспределяемыхНачислений.ДатаОкончания,
		|	СторноСтрокиРаспределяемыхНачислений.МесяцНачисления,
		|	СторноСтрокиРаспределяемыхНачислений.НомерСтроки,
		|	СторноСтрокиРаспределяемыхНачислений.НачалоБазовогоПериода,
		|	СторноСтрокиРаспределяемыхНачислений.ОкончаниеБазовогоПериода,
		|	СторноСтрокиРаспределяемыхНачислений.Сторно,
		|	СторноСтрокиРаспределяемыхНачислений.ИсходныйДокумент,
		|	СторноСтрокиРаспределяемыхНачислений.СторнируемыйДокумент,
		|	СторноСтрокиРаспределяемыхНачислений.ПоказательПремирования,
		|	СторноСтрокиРаспределяемыхНачислений.ФиксСторно,
		|	СторноСтрокиРаспределяемыхНачислений.ДатаПриема
		|ИЗ
		|	ВТСторноСтрокиРаспределяемыхНачислений КАК СторноСтрокиРаспределяемыхНачислений
		|ГДЕ
		|	НЕ 1 В
		|				(ВЫБРАТЬ ПЕРВЫЕ 1
		|					1
		|				ИЗ
		|					ВТСторноСтрокиРаспределяемыхНачислений КАК СторноСтроки
		|				ГДЕ
		|					СторноСтроки.СторнируемыйДокумент = СторноСтрокиРаспределяемыхНачислений.СторнируемыйДокумент
		|					И НЕ СторноСтроки.РаспределятьПоБазе)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТНачисленияПредварительно";
		
	Запрос.УстановитьПараметр("Премии", Перечисления.УчетНачисленийВСреднемЗаработкеОбщий.Премии());
	Запрос.УстановитьПараметр("ГодовыеПремии", Перечисления.УчетНачисленийВСреднемЗаработкеОбщий.ГодовыеПремии());
	Запрос.Выполнить();
	
	РаспределитьВТНачисленияПоБазе(МенеджерВременныхТаблиц, Регистратор, ИсключатьВПериодКомандировок);
	
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СторноСтроки.ИдентификаторСтроки КАК ИдентификаторСтроки
		|ИЗ
		|	ВТСторноСтрокиРаспределяемыхНачислений КАК СторноСтроки";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		ДополнитьВТНачисленияНераспределяемымиСторно(МенеджерВременныхТаблиц);
	КонецЕсли;
	
	Если ИсключатьОплачиваемоеНерабочееВремяИСуммы(МенеджерВременныхТаблиц) Тогда
		
		Коэффициенты = РасчетЗарплатыРасширенный.КоэффициентыОплаченногоРабочегоВремениНачислений(МенеджерВременныхТаблиц);
		
		Запрос.УстановитьПараметр("Коэффициенты", Коэффициенты);
		
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Коэффициенты.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	Коэффициенты.Коэффициент КАК Коэффициент
			|ПОМЕСТИТЬ ВТКоэффициенты
			|ИЗ
			|	&Коэффициенты КАК Коэффициенты
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Начисления.Период КАК Период,
			|	Сотрудники.ГоловнойСотрудник КАК Сотрудник,
			|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
			|	Начисления.Организация КАК Организация,
			|	Начисления.ПорядокРасчета КАК ПорядокРасчета,
			|	Начисления.СоставнаяЧасть КАК СоставнаяЧасть,
			|	Начисления.СтатьяФинансирования КАК СтатьяФинансирования,
			|	Начисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
			|	Начисления.СтатьяРасходов КАК СтатьяРасходов,
			|	Начисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
			|	Начисления.Индексируется КАК Индексируется,
			|	Начисления.Год КАК Год,
			|	Начисления.КоличествоМесяцев КАК КоличествоМесяцев,
			|	Начисления.ДатаНачалаБазовогоПериода КАК ДатаНачалаБазовогоПериода,
			|	Начисления.Начисление КАК Начисление,
			|	СУММА(Начисления.Сумма * ЕСТЬNULL(Коэффициенты.Коэффициент, 1)) КАК Сумма,
			|	Начисления.Сторно КАК Сторно,
			|	ВЫБОР
			|		КОГДА Начисления.Сотрудник <> Сотрудники.ГоловнойСотрудник
			|			ТОГДА Начисления.Сотрудник
			|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка)
			|	КОНЕЦ КАК СовмещающийСотрудник,
			|	Начисления.ДатаНачала КАК ДатаНачала,
			|	Начисления.ДатаОкончания КАК ДатаОкончания
			|ИЗ
			|	ВТНачисления КАК Начисления
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
			|		ПО (Сотрудники.Ссылка = Начисления.Сотрудник)
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКоэффициенты КАК Коэффициенты
			|		ПО Начисления.ИдентификаторСтроки = Коэффициенты.ИдентификаторСтроки
			|			И (НЕ(Начисления.Начисление.КатегорияНачисленияИлиНеоплаченногоВремени В (ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.Премия), ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.КвартальнаяПремия))
			|					И Начисления.Начисление.КоличествоМесяцевБазовогоПериода > 1))
			|
			|СГРУППИРОВАТЬ ПО
			|	Начисления.СоставнаяЧасть,
			|	Сотрудники.ГоловнойСотрудник,
			|	Начисления.СтатьяФинансирования,
			|	Начисления.СпособОтраженияЗарплатыВБухучете,
			|	Начисления.СтатьяРасходов,
			|	Начисления.ОблагаетсяЕНВД,
			|	Начисления.ПорядокРасчета,
			|	Начисления.Период,
			|	Начисления.Индексируется,
			|	Начисления.Сотрудник,
			|	Начисления.ФизическоеЛицо,
			|	Начисления.Организация,
			|	Начисления.ДатаНачалаБазовогоПериода,
			|	Начисления.Год,
			|	Начисления.КоличествоМесяцев,
			|	Начисления.Начисление,
			|	Начисления.Сторно,
			|	Начисления.ДатаНачала,
			|	Начисления.ДатаОкончания
			|
			|ИМЕЮЩИЕ
			|	СУММА(Начисления.Сумма * ЕСТЬNULL(Коэффициенты.Коэффициент, 1)) <> 0
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Начисления.Период КАК Период,
			|	Сотрудники.ГоловнойСотрудник КАК Сотрудник,
			|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
			|	Начисления.Организация КАК Организация,
			|	Начисления.ПорядокРасчета КАК ПорядокРасчета,
			|	Начисления.СоставнаяЧасть КАК СоставнаяЧасть,
			|	Начисления.СтатьяФинансирования КАК СтатьяФинансирования,
			|	Начисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
			|	Начисления.СтатьяРасходов КАК СтатьяРасходов,
			|	Начисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
			|	Начисления.Индексируется КАК Индексируется,
			|	Начисления.Год КАК Год,
			|	Начисления.КоличествоМесяцев КАК КоличествоМесяцев,
			|	Начисления.ДатаНачалаБазовогоПериода КАК ДатаНачалаБазовогоПериода,
			|	СУММА(Начисления.Сумма * ЕСТЬNULL(Коэффициенты.Коэффициент, 1)) КАК Сумма
			|ИЗ
			|	ВТНачисления КАК Начисления
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
			|		ПО (Сотрудники.Ссылка = Начисления.Сотрудник)
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКоэффициенты КАК Коэффициенты
			|		ПО Начисления.ИдентификаторСтроки = Коэффициенты.ИдентификаторСтроки
			|			И (НЕ(Начисления.Начисление.КатегорияНачисленияИлиНеоплаченногоВремени В (ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.Премия), ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.КвартальнаяПремия))
			|					И Начисления.Начисление.КоличествоМесяцевБазовогоПериода > 1))
			|
			|СГРУППИРОВАТЬ ПО
			|	Начисления.СоставнаяЧасть,
			|	Сотрудники.ГоловнойСотрудник,
			|	Начисления.СтатьяФинансирования,
			|	Начисления.СпособОтраженияЗарплатыВБухучете,
			|	Начисления.СтатьяРасходов,
			|	Начисления.ОблагаетсяЕНВД,
			|	Начисления.ПорядокРасчета,
			|	Начисления.Период,
			|	Начисления.Индексируется,
			|	Начисления.Сотрудник,
			|	Начисления.ФизическоеЛицо,
			|	Начисления.Организация,
			|	Начисления.ДатаНачалаБазовогоПериода,
			|	Начисления.Год,
			|	Начисления.КоличествоМесяцев
			|
			|ИМЕЮЩИЕ
			|	СУММА(Начисления.Сумма * ЕСТЬNULL(Коэффициенты.Коэффициент, 1)) <> 0";
		
	Иначе	
	
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Начисления.Период КАК Период,
			|	Сотрудники.ГоловнойСотрудник КАК Сотрудник,
			|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
			|	Начисления.Организация КАК Организация,
			|	Начисления.ПорядокРасчета КАК ПорядокРасчета,
			|	Начисления.СоставнаяЧасть КАК СоставнаяЧасть,
			|	Начисления.СтатьяФинансирования КАК СтатьяФинансирования,
			|	Начисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
			|	Начисления.СтатьяРасходов КАК СтатьяРасходов,
			|	Начисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
			|	Начисления.Индексируется КАК Индексируется,
			|	Начисления.Год КАК Год,
			|	Начисления.КоличествоМесяцев КАК КоличествоМесяцев,
			|	Начисления.ДатаНачалаБазовогоПериода КАК ДатаНачалаБазовогоПериода,
			|	Начисления.ИсходныйДокумент КАК ИсходныйДокумент,
			|	Начисления.ПоказательПремирования КАК ПоказательПремирования,
			|	Начисления.Начисление КАК Начисление,
			|	СУММА(Начисления.Сумма) КАК Сумма,
			|	Начисления.Сторно КАК Сторно,
			|	ВЫБОР
			|		КОГДА Начисления.Сотрудник <> Сотрудники.ГоловнойСотрудник
			|			ТОГДА Начисления.Сотрудник
			|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка)
			|	КОНЕЦ КАК СовмещающийСотрудник,
			|	Начисления.ДатаНачала КАК ДатаНачала,
			|	Начисления.ДатаОкончания КАК ДатаОкончания
			|ИЗ
			|	ВТНачисления КАК Начисления
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
			|		ПО (Сотрудники.Ссылка = Начисления.Сотрудник)
			|
			|СГРУППИРОВАТЬ ПО
			|	Начисления.СоставнаяЧасть,
			|	Сотрудники.ГоловнойСотрудник,
			|	Начисления.СтатьяФинансирования,
			|	Начисления.СпособОтраженияЗарплатыВБухучете,
			|	Начисления.СтатьяРасходов,
			|	Начисления.ОблагаетсяЕНВД,
			|	Начисления.ПорядокРасчета,
			|	Начисления.Период,
			|	Начисления.Индексируется,
			|	Начисления.Сотрудник,
			|	Начисления.ФизическоеЛицо,
			|	Начисления.Организация,
			|	Начисления.ДатаНачалаБазовогоПериода,
			|	Начисления.Год,
			|	Начисления.ИсходныйДокумент,
			|	Начисления.ПоказательПремирования,
			|	Начисления.КоличествоМесяцев,
			|	Начисления.Начисление,
			|	Начисления.Сторно,
			|	Начисления.ДатаНачала,
			|	Начисления.ДатаОкончания
			|
			|ИМЕЮЩИЕ
			|	СУММА(Начисления.Сумма) <> 0
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Начисления.Период КАК Период,
			|	Сотрудники.ГоловнойСотрудник КАК Сотрудник,
			|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
			|	Начисления.Организация КАК Организация,
			|	Начисления.ПорядокРасчета КАК ПорядокРасчета,
			|	Начисления.СоставнаяЧасть КАК СоставнаяЧасть,
			|	Начисления.СтатьяФинансирования КАК СтатьяФинансирования,
			|	Начисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
			|	Начисления.СтатьяРасходов КАК СтатьяРасходов,
			|	Начисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
			|	Начисления.Индексируется КАК Индексируется,
			|	Начисления.Год КАК Год,
			|	Начисления.КоличествоМесяцев КАК КоличествоМесяцев,
			|	Начисления.ДатаНачалаБазовогоПериода КАК ДатаНачалаБазовогоПериода,
			|	Начисления.ИсходныйДокумент КАК ИсходныйДокумент,
			|	Начисления.ПоказательПремирования КАК ПоказательПремирования,
			|	СУММА(Начисления.Сумма) КАК Сумма
			|ИЗ
			|	ВТНачисления КАК Начисления
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
			|		ПО (Сотрудники.Ссылка = Начисления.Сотрудник)
			|
			|СГРУППИРОВАТЬ ПО
			|	Начисления.СоставнаяЧасть,
			|	Сотрудники.ГоловнойСотрудник,
			|	Начисления.СтатьяФинансирования,
			|	Начисления.СпособОтраженияЗарплатыВБухучете,
			|	Начисления.СтатьяРасходов,
			|	Начисления.ОблагаетсяЕНВД,
			|	Начисления.ПорядокРасчета,
			|	Начисления.Период,
			|	Начисления.Индексируется,
			|	Начисления.Сотрудник,
			|	Начисления.ФизическоеЛицо,
			|	Начисления.Организация,
			|	Начисления.ДатаНачалаБазовогоПериода,
			|	Начисления.Год,
			|	Начисления.ИсходныйДокумент,
			|	Начисления.ПоказательПремирования,
			|	Начисления.КоличествоМесяцев
			|
			|ИМЕЮЩИЕ
			|	СУММА(Начисления.Сумма) <> 0";
		
	КонецЕсли;
		
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	КоличествоПодзапросов = РезультатЗапроса.Количество();
	Если Не РезультатЗапроса[КоличествоПодзапросов - 1].Пустой() Тогда
		Движения.ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Записывать = Истина;
		Движения.ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям.Записывать = Истина;
	КонецЕсли;
	
	ВыборкаДанныеПоНачислениям = РезультатЗапроса[КоличествоПодзапросов - 2].Выбрать();
	Пока ВыборкаДанныеПоНачислениям.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Движения.ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям.Добавить(), ВыборкаДанныеПоНачислениям);
	КонецЦикла;
	
	ВыборкаДанныеОбщие = РезультатЗапроса[КоличествоПодзапросов - 1].Выбрать();
	Пока ВыборкаДанныеОбщие.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Движения.ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Добавить(), ВыборкаДанныеОбщие);
	КонецЦикла;
	
	Если ЗаписыватьДвижения Тогда
		Движения.ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Записать();
		Движения.ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Записывать = Ложь;
		
		Движения.ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям.Записать();
		Движения.ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям.Записывать = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗарегистрироватьДанныеВремениДляРасчетаОбщегоСреднегоЗаработка(Движения, Отказ, МенеджерВременныхТаблиц, ИсключатьВПериодКомандировок, ЗаписыватьДвижения = Ложь)
	
	// Вызов общий, и при этом отработанное время регистрируется не всеми документами,
	// поэтому делаем проверку.
	Если ТипЗнч(Движения) = Тип("Структура") Тогда
		// для типа Структура
		Если Не Движения.Свойство("ДанныеОВремениДляРасчетаСреднегоОбщий") Тогда
			Возврат;
		КонецЕсли;
	Иначе
		// для типа КоллекцияДвижений
		Если Движения.Найти("ДанныеОВремениДляРасчетаСреднегоОбщий") = Неопределено Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	// При этом время необходимо писать только по основному сотруднику, по подработкам не писать, 
	// т.к. считается, что он подработки выполняет в течение времени основной работы.
	
	СоздатьВТВидыНачисленийДляРегистрацииОбщегоСреднегоЗаработка(МенеджерВременныхТаблиц);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Движения.ДанныеОВремениДляРасчетаСреднегоОбщий.Отбор.Регистратор.Значение);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ФПД.Регистратор КАК Регистратор,
		|	ФПД.Сотрудник КАК Сотрудник,
		|	ФПД.Сторно КАК Сторно,
		|	ФПД.ВидРасчета КАК ВидРасчета,
		|	ФПД.ГрафикРаботы КАК ГрафикРаботы,
		|	ФПД.ПериодДействия КАК Месяц,
		|	ФПД.ПериодДействияНачало КАК НачалоПериода,
		|	ФПД.ПериодДействияКонец КАК ОкончаниеПериода,
		|	ФПД.ПериодРегистрацииВремени КАК ПериодРегистрацииВремени
		|ПОМЕСТИТЬ ВТФактическийПериодДействия
		|ИЗ
		|	РегистрРасчета.Начисления.ФактическийПериодДействия(
		|			Регистратор = &Регистратор
		|				И Сотрудник.ГоловнойСотрудник = Сотрудник
		|				И ВидРасчета В
		|					(ВЫБРАТЬ
		|						ВТВидыНачислений.Ссылка
		|					ИЗ
		|						ВТВидыНачислений)) КАК ФПД";	
		
	Запрос.Выполнить();
	
	ИсключатьОплачиваемыеНерабочиеДни = ИсключатьОплачиваемоеНерабочееВремяИСуммы(МенеджерВременныхТаблиц);
	ИсключитьОплачиваемыеНерабочиеДни(МенеджерВременныхТаблиц, ИсключатьОплачиваемыеНерабочиеДни);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ФПД.Регистратор,
		|	ФПД.Сотрудник,
		|	ФПД.Сторно,
		|	ФПД.ГрафикРаботы,
		|	ФПД.Месяц,
		|	ФПД.НачалоПериода,
		|	ФПД.ОкончаниеПериода,
		|	ВидыНачислений.Прогул,
		|	ВидыНачислений.Отгул,
		|	ВидыНачислений.ЗачетНормыВремени,
		|	ВидыНачислений.ЗачетОтработанногоВремени,
		|	ВидыНачислений.ЕжегодныйОтпуск
		|ПОМЕСТИТЬ ВТФПДСотрудников
		|ИЗ
		|	ВТФактическийПериодДействия КАК ФПД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТВидыНачислений КАК ВидыНачислений
		|		ПО (ВидыНачислений.Ссылка = ФПД.ВидРасчета)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПериодыГрафиков.ГрафикРаботы,
		|	ПериодыГрафиков.НачалоПериода,
		|	ПериодыГрафиков.ОкончаниеПериода
		|ПОМЕСТИТЬ ВТПериодыГрафиков
		|ИЗ
		|	ВТФПДСотрудников КАК ПериодыГрафиков
		|ГДЕ
		|	ПериодыГрафиков.ЗачетНормыВремени";
	
	Запрос.УстановитьПараметр("Регистратор", Движения.ДанныеОВремениДляРасчетаСреднегоОбщий.Отбор.Регистратор.Значение);
	Запрос.Выполнить();
	
	УчетРабочегоВремениРасширенный.СоздатьВТДанныеПроизводственногоКалендаряПоГрафикам(МенеджерВременныхТаблиц);
	
	Если Не ИсключатьВПериодКомандировок Тогда
		Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 0
			|	ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка) КАК Сотрудник,
			|	ДАТАВРЕМЯ(1, 1, 1) КАК Месяц,
			|	0 КАК Дней,
			|	0 КАК Часов
			|ПОМЕСТИТЬ ВТИсключаемоеВремя";
		Запрос.Выполнить();
	Иначе
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	СвязьСГрафиком.ГрафикРаботы,
			|	СвязьСГрафиком.ПериодРегистрацииВремени,
			|	СвязьСГрафиком.ИдентификаторСтроки,
			|	СвязьСГрафиком.Сторно,
			|	СвязьСГрафиком.Регистратор
			|ПОМЕСТИТЬ ВТСвязьСГрафиком
			|ИЗ
			|	РегистрРасчета.Начисления КАК СвязьСГрафиком
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТИсключаемыеВПериодКомандировки КАК ИсключаемыеНачисления
			|		ПО (ИсключаемыеНачисления.ВидРасчета = СвязьСГрафиком.ВидРасчета)
			|			И (СвязьСГрафиком.Регистратор = &Регистратор)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ПериодыКомандировок.Сотрудник,
			|	НАЧАЛОПЕРИОДА(ПериодыКомандировок.Начало, МЕСЯЦ) КАК Месяц,
			|	ПериодыКомандировок.Начало КАК Начало,
			|	ПериодыКомандировок.Окончание КАК Окончание,
			|	ПериодыКомандировок.ВидРасчета КАК ВидКомандировки,
			|	ИсключаемыеНачисления.ВидРасчета КАК Начисление,
			|	СвязьСГрафиком.ГрафикРаботы,
			|	СвязьСГрафиком.ПериодРегистрацииВремени,
			|	СвязьСГрафиком.Сторно,
			|	ВидыВремени.ВидВремени,
			|	ВЫБОР
			|		КОГДА ВидыВремени.Ссылка.ТребуетсяРасчетСверхурочных = ИСТИНА
			|			ТОГДА ЛОЖЬ
			|		ИНАЧЕ ИСТИНА
			|	КОНЕЦ КАК ИсключатьДни
			|ПОМЕСТИТЬ ВТИсключаемыеПериоды
			|ИЗ
			|	ВТПериодыКомандировок КАК ПериодыКомандировок
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТИсключаемыеВПериодКомандировки КАК ИсключаемыеНачисления
			|		ПО (ИсключаемыеНачисления.Ссылка = ПериодыКомандировок.ВидРасчета)
			|			И (ИсключаемыеНачисления.ИсключатьВремя = ИСТИНА)
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНачисления КАК Начисления
			|		ПО (Начисления.Начисление = ИсключаемыеНачисления.ВидРасчета)
			|			И (Начисления.Сотрудник = ПериодыКомандировок.Сотрудник)
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСвязьСГрафиком КАК СвязьСГрафиком
			|		ПО (СвязьСГрафиком.Регистратор = Начисления.ДокументСсылка)
			|			И (СвязьСГрафиком.ИдентификаторСтроки = Начисления.ИдентификаторСтроки)
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления.ВидыВремени КАК ВидыВремени
			|		ПО (ВидыВремени.Ссылка = ИсключаемыеНачисления.ВидРасчета)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ИсключаемыеПериоды.Сотрудник,
			|	ИсключаемыеПериоды.Месяц,
			|	ИсключаемыеПериоды.Начисление,
			|	ИсключаемыеПериоды.ГрафикРаботы,
			|	ИсключаемыеПериоды.ПериодРегистрацииВремени,
			|	ИсключаемыеПериоды.ВидВремени,
			|	ИсключаемыеПериоды.ИсключатьДни,
			|	ИсключаемыеПериоды.Сторно,
			|	ДанныеГрафика.Дата
			|ПОМЕСТИТЬ ВТИсключаемыеДаты
			|ИЗ
			|	ВТИсключаемыеПериоды КАК ИсключаемыеПериоды
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикиРаботыПоВидамВремени КАК ДанныеГрафика
			|		ПО ИсключаемыеПериоды.ГрафикРаботы = ДанныеГрафика.ГрафикРаботы
			|			И (ДанныеГрафика.Месяц = ИсключаемыеПериоды.Месяц)
			|			И (ДанныеГрафика.Дата МЕЖДУ ИсключаемыеПериоды.Начало И ИсключаемыеПериоды.Окончание)
			|			И ИсключаемыеПериоды.ВидВремени = ДанныеГрафика.ВидУчетаВремени
			|			И ИсключаемыеПериоды.ПериодРегистрацииВремени = ДанныеГрафика.ПериодРегистрации
			|			И (ДанныеГрафика.ВремяВЧасах = ЛОЖЬ)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ИсключаемыеДаты.Сотрудник,
			|	ИсключаемыеДаты.Месяц,
			|	СУММА(ВЫБОР
			|			КОГДА ИсключаемыеДаты.ИсключатьДни = ИСТИНА
			|				ТОГДА ВЫБОР
			|						КОГДА ИсключаемыеДаты.Сторно = ИСТИНА
			|							ТОГДА -ДанныеГрафика.ОсновноеЗначение
			|						ИНАЧЕ ДанныеГрафика.ОсновноеЗначение
			|					КОНЕЦ
			|			ИНАЧЕ 0
			|		КОНЕЦ) КАК Дней,
			|	СУММА(ВЫБОР
			|			КОГДА ИсключаемыеДаты.Сторно = ИСТИНА
			|				ТОГДА -ДанныеГрафика.ДополнительноеЗначение
			|			ИНАЧЕ ДанныеГрафика.ДополнительноеЗначение
			|		КОНЕЦ) КАК Часов
			|ПОМЕСТИТЬ ВТИсключаемоеВремя
			|ИЗ
			|	ВТИсключаемыеДаты КАК ИсключаемыеДаты
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикиРаботыПоВидамВремени КАК ДанныеГрафика
			|		ПО ИсключаемыеДаты.ГрафикРаботы = ДанныеГрафика.ГрафикРаботы
			|			И (ДанныеГрафика.Месяц = ИсключаемыеДаты.Месяц)
			|			И (ДанныеГрафика.Дата = ИсключаемыеДаты.Дата)
			|			И ИсключаемыеДаты.ВидВремени = ДанныеГрафика.ВидУчетаВремени
			|			И ИсключаемыеДаты.ПериодРегистрацииВремени = ДанныеГрафика.ПериодРегистрации
			|			И (ДанныеГрафика.ВремяВЧасах = ЛОЖЬ)
			|
			|СГРУППИРОВАТЬ ПО
			|	ИсключаемыеДаты.Сотрудник,
			|	ИсключаемыеДаты.Месяц";
		Запрос.Выполнить();
	КонецЕсли;
	
	ДополнитьВТИсключаемоеВремя(МенеджерВременныхТаблиц, ИсключатьОплачиваемыеНерабочиеДни);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДанныеВремени.Сотрудник,
		|	ДанныеВремени.Месяц КАК Период,
		|	ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетаСреднегоЗаработкаОбщий.Постановление2010) КАК ПорядокРасчета,
		|	СУММА(ДанныеВремени.ОтработаноДней) КАК ОтработаноДней,
		|	СУММА(ДанныеВремени.ОтработаноЧасов) КАК ОтработаноЧасов,
		|	СУММА(ДанныеВремени.ДнейПоПятидневке) КАК ОтработаноДнейПятидневка,
		|	СУММА(ДанныеВремени.ЧасовПоПятидневке) КАК ОтработаноЧасовПятидневка,
		|	СУММА(ДанныеВремени.ДнейПоШестидневке) КАК ОтработаноДнейШестидневка,
		|	СУММА(ДанныеВремени.ДнейКалендарных) КАК ОтработаноДнейКалендарных
		|ИЗ
		|	(ВЫБРАТЬ
		|		ФПД.Сотрудник КАК Сотрудник,
		|		ФПД.Месяц КАК Месяц,
		|		0 КАК ОтработаноДней,
		|		0 КАК ОтработаноЧасов,
		|		СУММА(ВЫБОР
		|				КОГДА ФПД.Прогул = ЛОЖЬ
		|					ТОГДА ВЫБОР
		|							КОГДА ФПД.Сторно = ИСТИНА
		|								ТОГДА -ДанныеКалендаряПоФПД.ДнейПоПятидневке
		|							ИНАЧЕ ДанныеКалендаряПоФПД.ДнейПоПятидневке
		|						КОНЕЦ
		|				ИНАЧЕ 0
		|			КОНЕЦ) КАК ДнейПоПятидневке,
		|		СУММА(ВЫБОР
		|				КОГДА ФПД.Прогул = ЛОЖЬ
		|					ТОГДА ВЫБОР
		|							КОГДА ФПД.Сторно = ИСТИНА
		|								ТОГДА -ДанныеКалендаряПоФПД.ЧасовПоПятидневке
		|							ИНАЧЕ ДанныеКалендаряПоФПД.ЧасовПоПятидневке
		|						КОНЕЦ
		|				ИНАЧЕ 0
		|			КОНЕЦ) КАК ЧасовПоПятидневке,
		|		СУММА(ВЫБОР
		|				КОГДА ФПД.Сторно = ИСТИНА
		|					ТОГДА -ДанныеКалендаряПоФПД.ДнейПоШестидневке
		|				ИНАЧЕ ДанныеКалендаряПоФПД.ДнейПоШестидневке
		|			КОНЕЦ) КАК ДнейПоШестидневке,
		|		СУММА(ВЫБОР
		|				КОГДА ФПД.Сторно = ИСТИНА
		|					ТОГДА -ДанныеКалендаряПоФПД.ДнейКалендарных
		|				ИНАЧЕ ДанныеКалендаряПоФПД.ДнейКалендарных
		|			КОНЕЦ) КАК ДнейКалендарных
		|	ИЗ
		|		ВТФПДСотрудников КАК ФПД
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДанныеПроизводственногоКалендаряПоГрафикам КАК ДанныеКалендаряПоФПД
		|			ПО (ДанныеКалендаряПоФПД.НачалоПериода = ФПД.НачалоПериода)
		|				И (ДанныеКалендаряПоФПД.ОкончаниеПериода = ФПД.ОкончаниеПериода)
		|				И (ДанныеКалендаряПоФПД.ГрафикРаботы = ФПД.ГрафикРаботы)
		|				И (ФПД.ЗачетНормыВремени)
		|				И (ФПД.ЗачетОтработанногоВремени
		|					ИЛИ ФПД.Отгул)
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ФПД.Сотрудник,
		|		ФПД.Месяц
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ФПД.Сотрудник,
		|		ФПД.Месяц,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		СУММА(ВЫБОР
		|				КОГДА ФПД.Сторно = ИСТИНА
		|					ТОГДА -ДанныеКалендаряПоФПД.Праздников
		|				ИНАЧЕ ДанныеКалендаряПоФПД.Праздников
		|			КОНЕЦ)
		|	ИЗ
		|		ВТФПДСотрудников КАК ФПД
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДанныеПроизводственногоКалендаряПоГрафикам КАК ДанныеКалендаряПоФПД
		|			ПО (ДанныеКалендаряПоФПД.НачалоПериода = ФПД.НачалоПериода)
		|				И (ДанныеКалендаряПоФПД.ОкончаниеПериода = ФПД.ОкончаниеПериода)
		|				И (ДанныеКалендаряПоФПД.ГрафикРаботы = ФПД.ГрафикРаботы)
		|				И (ФПД.ЕжегодныйОтпуск)
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ФПД.Сотрудник,
		|		ФПД.Месяц
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ОтработанноеВремя.Сотрудник,
		|		ОтработанноеВремя.ПериодДействия,
		|		СУММА(ОтработанноеВремя.ОтработаноДней),
		|		СУММА(ОтработанноеВремя.ОтработаноЧасов),
		|		0,
		|		0,
		|		0,
		|		0
		|	ИЗ
		|		РегистрНакопления.ОтработанноеВремяПоСотрудникам КАК ОтработанноеВремя
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТВидыНачислений КАК ВидыНачислений
		|			ПО (ВидыНачислений.Ссылка = ОтработанноеВремя.Начисление)
		|				И (ОтработанноеВремя.Регистратор = &Регистратор)
		|				И (ОтработанноеВремя.Сотрудник = ОтработанноеВремя.Сотрудник.ГоловнойСотрудник)
		|				И (ВидыНачислений.ЗачетОтработанногоВремени
		|					ИЛИ ВидыНачислений.Прогул)
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ОтработанноеВремя.Регистратор,
		|		ОтработанноеВремя.ПериодДействия,
		|		ОтработанноеВремя.Сотрудник
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ИсключаемоеВремя.Сотрудник,
		|		ИсключаемоеВремя.Месяц,
		|		-ИсключаемоеВремя.Дней,
		|		-ИсключаемоеВремя.Часов,
		|		0,
		|		0,
		|		0,
		|		0
		|	ИЗ
		|		ВТИсключаемоеВремя КАК ИсключаемоеВремя) КАК ДанныеВремени
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеВремени.Сотрудник,
		|	ДанныеВремени.Месяц";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Движения.ДанныеОВремениДляРасчетаСреднегоОбщий.Записывать = Истина;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Движения.ДанныеОВремениДляРасчетаСреднегоОбщий.Добавить(), Выборка);
	КонецЦикла;
	
	Если ЗаписыватьДвижения Тогда
		Движения.ДанныеОВремениДляРасчетаСреднегоОбщий.Записать();
		Движения.ДанныеОВремениДляРасчетаСреднегоОбщий.Записывать = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Процедура ИсключитьОплачиваемыеНерабочиеДни(МенеджерВременныхТаблиц, ИсключатьОплачиваемыеНерабочиеДни)
	
	Если Не ИсключатьОплачиваемыеНерабочиеДни Тогда 
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ФактическийПериодДействия.Сотрудник КАК Сотрудник,
		|	ФактическийПериодДействия.Месяц КАК Месяц,
		|	ФактическийПериодДействия.ГрафикРаботы КАК ГрафикРаботы,
		|	ФактическийПериодДействия.НачалоПериода КАК НачалоПериода,
		|	ФактическийПериодДействия.ОкончаниеПериода КАК ОкончаниеПериода,
		|	ФактическийПериодДействия.Сторно КАК Сторно,
		|	ФактическийПериодДействия.ПериодРегистрацииВремени КАК ДатаАктуальности
		|ПОМЕСТИТЬ ВТСотрудникиДляРасчетаВремени
		|ИЗ
		|	ВТФактическийПериодДействия КАК ФактическийПериодДействия
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТВидыНачислений КАК ВидыНачислений
		|		ПО ФактическийПериодДействия.ВидРасчета = ВидыНачислений.Ссылка
		|			И (НЕ ВидыНачислений.ЕжегодныйОтпуск)
		|			И (ВидыНачислений.ЗачетНормыВремени)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СотрудникиДляРасчетаВремени.Сотрудник КАК Сотрудник,
		|	СотрудникиДляРасчетаВремени.Месяц КАК Месяц,
		|	СотрудникиДляРасчетаВремени.ДатаАктуальности КАК ДатаАктуальности,
		|	СотрудникиДляРасчетаВремени.Месяц КАК ДатаНачала,
		|	КОНЕЦПЕРИОДА(СотрудникиДляРасчетаВремени.Месяц, МЕСЯЦ) КАК ДатаОкончания
		|ПОМЕСТИТЬ ВТСотрудники
		|ИЗ
		|	ВТСотрудникиДляРасчетаВремени КАК СотрудникиДляРасчетаВремени";
	
	Запрос.Выполнить();
	
	Периоды = УчетРабочегоВремениРасширенный.ОплачиваемыеКоронавирусныеПериоды(МенеджерВременныхТаблиц);
	
	Запрос.УстановитьПараметр("Периоды", Периоды);
	Запрос.УстановитьПараметр("ОплачиваемыеНерабочиеДни", ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыИспользованияРабочегоВремени.ОплачиваемыеНерабочиеДни"));
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Периоды.Сотрудник КАК Сотрудник,
		|	Периоды.ДатаАктуальности КАК ДатаАктуальности,
		|	Периоды.ДатаНачала КАК ДатаНачала,
		|	Периоды.ДатаОкончания КАК ДатаОкончания
		|ПОМЕСТИТЬ ВТПериодыОплачиваемыхНерабочихДней
		|ИЗ
		|	&Периоды КАК Периоды
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СотрудникиДляРасчетаВремени.Сотрудник КАК Сотрудник,
		|	СотрудникиДляРасчетаВремени.Месяц КАК Месяц,
		|	СотрудникиДляРасчетаВремени.ДатаАктуальности КАК ДатаАктуальности,
		|	ДанныеГрафика.Дата КАК Дата,
		|	ВЫБОР
		|		КОГДА СотрудникиДляРасчетаВремени.Сторно
		|			ТОГДА -ДанныеГрафика.ОсновноеЗначение
		|		ИНАЧЕ ДанныеГрафика.ОсновноеЗначение
		|	КОНЕЦ КАК Дней,
		|	ВЫБОР
		|		КОГДА СотрудникиДляРасчетаВремени.Сторно
		|			ТОГДА -ДанныеГрафика.ДополнительноеЗначение
		|		ИНАЧЕ ДанныеГрафика.ДополнительноеЗначение
		|	КОНЕЦ КАК Часов
		|ПОМЕСТИТЬ ВТИсключаемоеОплачиваемоеНерабочееВремяПредварительно
		|ИЗ
		|	РегистрСведений.ГрафикиРаботыПоВидамВремени КАК ДанныеГрафика
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудникиДляРасчетаВремени КАК СотрудникиДляРасчетаВремени
		|		ПО ДанныеГрафика.ГрафикРаботы = СотрудникиДляРасчетаВремени.ГрафикРаботы
		|			И ДанныеГрафика.Месяц = СотрудникиДляРасчетаВремени.Месяц
		|			И (ДанныеГрафика.ВидУчетаВремени = &ОплачиваемыеНерабочиеДни)
		|			И (ДанныеГрафика.ВремяВЧасах = ЛОЖЬ)
		|			И (ДанныеГрафика.ПериодРегистрации = СотрудникиДляРасчетаВремени.ДатаАктуальности)
		|			И (ДанныеГрафика.Дата МЕЖДУ СотрудникиДляРасчетаВремени.НачалоПериода И СотрудникиДляРасчетаВремени.ОкончаниеПериода)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДанныеГрафика.Сотрудник КАК Сотрудник,
		|	ДанныеГрафика.Месяц КАК Месяц,
		|	ДанныеГрафика.ДатаАктуальности КАК ДатаАктуальности,
		|	СУММА(ДанныеГрафика.Дней) КАК Дней,
		|	СУММА(ДанныеГрафика.Часов) КАК Часов
		|ПОМЕСТИТЬ ВТИсключаемоеОплачиваемоеНерабочееВремя
		|ИЗ
		|	ВТИсключаемоеОплачиваемоеНерабочееВремяПредварительно КАК ДанныеГрафика
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПериодыОплачиваемыхНерабочихДней КАК Периоды
		|		ПО ДанныеГрафика.Сотрудник = Периоды.Сотрудник
		|			И ДанныеГрафика.ДатаАктуальности = Периоды.ДатаАктуальности
		|			И (ДанныеГрафика.Дата МЕЖДУ Периоды.ДатаНачала И Периоды.ДатаОкончания)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеГрафика.Сотрудник,
		|	ДанныеГрафика.Месяц,
		|	ДанныеГрафика.ДатаАктуальности";
	
	Запрос.Выполнить();
	
	ФактическийПериодДействия = Новый ТаблицаЗначений;
	ФактическийПериодДействия.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	ФактическийПериодДействия.Колонки.Добавить("ДатаАктуальности", Новый ОписаниеТипов("Дата"));
	ФактическийПериодДействия.Колонки.Добавить("НачалоПериода", Новый ОписаниеТипов("Дата"));
	ФактическийПериодДействия.Колонки.Добавить("ОкончаниеПериода", Новый ОписаниеТипов("Дата"));
	ФактическийПериодДействия.Колонки.Добавить("ДатаНачала", Новый ОписаниеТипов("Дата"));
	ФактическийПериодДействия.Колонки.Добавить("ДатаОкончания", Новый ОписаниеТипов("Дата"));
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ФактическийПериодДействия.Сотрудник КАК Сотрудник,
		|	ФактическийПериодДействия.ПериодРегистрацииВремени КАК ДатаАктуальности,
		|	ФактическийПериодДействия.НачалоПериода КАК НачалоПериода,
		|	ФактическийПериодДействия.ОкончаниеПериода КАК ОкончаниеПериода
		|ИЗ
		|	ВТФактическийПериодДействия КАК ФактическийПериодДействия
		|
		|УПОРЯДОЧИТЬ ПО
		|	Сотрудник,
		|	ПериодРегистрацииВремени,
		|	НачалоПериода";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Сотрудник") Цикл
		Пока Выборка.СледующийПоЗначениюПоля("ДатаАктуальности") Цикл
			ПериодыСотрудника = Периоды.НайтиСтроки(Новый Структура("Сотрудник, ДатаАктуальности", Выборка.Сотрудник, Выборка.ДатаАктуальности));
			Пока Выборка.Следующий() Цикл
				ДатаНачала = Выборка.НачалоПериода;
				ДатаОкончания = Выборка.ОкончаниеПериода;
				Для Каждого ПериодСотрудника Из ПериодыСотрудника Цикл
					Если ПериодСотрудника.ДатаНачала <= ДатаОкончания И ПериодСотрудника.ДатаОкончания >= ДатаНачала Тогда
						Если ДатаНачала < ПериодСотрудника.ДатаНачала Тогда
							НоваяСтрока = ФактическийПериодДействия.Добавить();
							ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
							НоваяСтрока.ДатаНачала = ДатаНачала;
							НоваяСтрока.ДатаОкончания = ПериодСотрудника.ДатаНачала - 86400;
						КонецЕсли;
						ДатаНачала = КонецДня(ПериодСотрудника.ДатаОкончания) + 1;
					КонецЕсли;
				КонецЦикла;
				Если ДатаНачала <= ДатаОкончания Тогда
					НоваяСтрока = ФактическийПериодДействия.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
					НоваяСтрока.ДатаНачала = ДатаНачала;
					НоваяСтрока.ДатаОкончания = ДатаОкончания;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ФактическийПериодИсправленный", ФактическийПериодДействия);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ФактическийПериодИсправленный.Сотрудник КАК Сотрудник,
		|	ФактическийПериодИсправленный.ДатаАктуальности КАК ДатаАктуальности,
		|	ФактическийПериодИсправленный.НачалоПериода КАК НачалоПериода,
		|	ФактическийПериодИсправленный.ОкончаниеПериода КАК ОкончаниеПериода,
		|	ФактическийПериодИсправленный.ДатаНачала КАК ДатаНачала,
		|	ФактическийПериодИсправленный.ДатаОкончания КАК ДатаОкончания
		|ПОМЕСТИТЬ ВТФактическийПериодИсправленный
		|ИЗ
		|	&ФактическийПериодИсправленный КАК ФактическийПериодИсправленный
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ФактическийПериодДействия.Регистратор КАК Регистратор,
		|	ФактическийПериодДействия.Сотрудник КАК Сотрудник,
		|	ФактическийПериодДействия.Сторно КАК Сторно,
		|	ФактическийПериодДействия.ВидРасчета КАК ВидРасчета,
		|	ФактическийПериодДействия.ГрафикРаботы КАК ГрафикРаботы,
		|	ФактическийПериодДействия.Месяц КАК Месяц,
		|	ФактическийПериодИсправленный.ДатаНачала КАК НачалоПериода,
		|	ФактическийПериодИсправленный.ДатаОкончания КАК ОкончаниеПериода
		|ПОМЕСТИТЬ ВТФактическийПериодДействияПредварительно
		|ИЗ
		|	ВТФактическийПериодДействия КАК ФактическийПериодДействия
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТФактическийПериодИсправленный КАК ФактическийПериодИсправленный
		|		ПО ФактическийПериодДействия.Сотрудник = ФактическийПериодИсправленный.Сотрудник
		|			И ФактическийПериодДействия.ПериодРегистрацииВремени = ФактическийПериодИсправленный.ДатаАктуальности
		|			И ФактическийПериодДействия.НачалоПериода = ФактическийПериодИсправленный.НачалоПериода
		|			И ФактическийПериодДействия.ОкончаниеПериода = ФактическийПериодИсправленный.ОкончаниеПериода
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТФактическийПериодДействия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ФактическийПериодДействия.Регистратор КАК Регистратор,
		|	ФактическийПериодДействия.Сотрудник КАК Сотрудник,
		|	ФактическийПериодДействия.Сторно КАК Сторно,
		|	ФактическийПериодДействия.ВидРасчета КАК ВидРасчета,
		|	ФактическийПериодДействия.ГрафикРаботы КАК ГрафикРаботы,
		|	ФактическийПериодДействия.Месяц КАК Месяц,
		|	ФактическийПериодДействия.НачалоПериода КАК НачалоПериода,
		|	ФактическийПериодДействия.ОкончаниеПериода КАК ОкончаниеПериода
		|ПОМЕСТИТЬ ВТФактическийПериодДействия
		|ИЗ
		|	ВТФактическийПериодДействияПредварительно КАК ФактическийПериодДействия";
	
	Запрос.Выполнить();
	                                          
	ИменаВТ = Новый Массив;
	ИменаВТ.Добавить("ВТСотрудники");
	ИменаВТ.Добавить("ВТСотрудникиДляРасчетаВремени");
	ИменаВТ.Добавить("ВТПериодыОплачиваемыхНерабочихДней");
	ИменаВТ.Добавить("ВТИсключаемоеОплачиваемоеНерабочееВремяПредварительно");
	ИменаВТ.Добавить("ВТФактическийПериодИсправленный");
	ИменаВТ.Добавить("ВТФактическийПериодДействияПредварительно");
	ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, ИменаВТ);
	
КонецПроцедуры

Процедура ДополнитьВТИсключаемоеВремя(МенеджерВременныхТаблиц, ИсключатьОплачиваемыеНерабочиеДни)
	
	Если Не ИсключатьОплачиваемыеНерабочиеДни Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИсключаемоеОплачиваемоеНерабочееВремя.Сотрудник КАК Сотрудник,
		|	ИсключаемоеОплачиваемоеНерабочееВремя.Месяц КАК Месяц,
		|	ИсключаемоеОплачиваемоеНерабочееВремя.Дней КАК Дней,
		|	ИсключаемоеОплачиваемоеНерабочееВремя.Часов КАК Часов
		|ПОМЕСТИТЬ ВТИсключаемоеВремяПредварительно
		|ИЗ
		|	ВТИсключаемоеОплачиваемоеНерабочееВремя КАК ИсключаемоеОплачиваемоеНерабочееВремя
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ИсключаемоеВремя.Сотрудник,
		|	ИсключаемоеВремя.Месяц,
		|	ИсключаемоеВремя.Дней,
		|	ИсключаемоеВремя.Часов
		|ИЗ
		|	ВТИсключаемоеВремя КАК ИсключаемоеВремя
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТИсключаемоеВремя
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТИсключаемоеВремяПредварительно.Сотрудник КАК Сотрудник,
		|	ВТИсключаемоеВремяПредварительно.Месяц КАК Месяц,
		|	СУММА(ВТИсключаемоеВремяПредварительно.Дней) КАК Дней,
		|	СУММА(ВТИсключаемоеВремяПредварительно.Часов) КАК Часов
		|ПОМЕСТИТЬ ВТИсключаемоеВремя
		|ИЗ
		|	ВТИсключаемоеВремяПредварительно КАК ВТИсключаемоеВремяПредварительно
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТИсключаемоеВремяПредварительно.Сотрудник,
		|	ВТИсключаемоеВремяПредварительно.Месяц";
	
	Запрос.Выполнить();
	
	ИменаВТ = Новый Массив;
	ИменаВТ.Добавить("ВТИсключаемоеОплачиваемоеНерабочееВремя");
	ИменаВТ.Добавить("ВТИсключаемоеВремяПредварительно");
	ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, ИменаВТ);
	
КонецПроцедуры

Функция ИсключатьОплачиваемоеНерабочееВремяИСуммы(МенеджерВременныхТаблиц, ИмяТаблицы = "ВТНачисления", ИмяПоляПериод = "ПериодДействия")
	
	Если ПолучитьФункциональнуюОпцию("УчитыватьОплачиваемоеНерабочееВремяИСуммыВСреднемЗаработке") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ОсновнойПроизводственныйКалендарь = КалендарныеГрафики.ОсновнойПроизводственныйКалендарь();
	ОплачиваемыеНерабочиеПериоды = УчетРабочегоВремениРасширенный.ОплачиваемыеНерабочиеПериоды(ОсновнойПроизводственныйКалендарь);

	Если ОплачиваемыеНерабочиеПериоды.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Периоды = Новый ТаблицаЗначений;
	Периоды.Колонки.Добавить("ДатаНачала", Новый ОписаниеТипов("Дата"));
	Периоды.Колонки.Добавить("ДатаОкончания", Новый ОписаниеТипов("Дата"));
	
	Для Каждого НерабочийПериод Из ОплачиваемыеНерабочиеПериоды Цикл
		НоваяСтрока = Периоды.Добавить();
		НоваяСтрока.ДатаНачала = НачалоМесяца(НерабочийПериод.Период.ДатаНачала);
		НоваяСтрока.ДатаОкончания = КонецМесяца(НерабочийПериод.Период.ДатаОкончания);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Периоды", Периоды);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Периоды.ДатаНачала КАК ДатаНачала,
		|	Периоды.ДатаОкончания КАК ДатаОкончания
		|ПОМЕСТИТЬ ВТОплачиваемыеНерабочиеПериоды
		|ИЗ
		|	&Периоды КАК Периоды
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА КАК ЗначениеИстина
		|ИЗ
		|	#ИмяТаблицы КАК Начисления
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОплачиваемыеНерабочиеПериоды КАК ОплачиваемыеНерабочиеПериоды
		|		ПО Начисления.#ИмяПоляПериод >= ОплачиваемыеНерабочиеПериоды.ДатаНачала
		|			И Начисления.#ИмяПоляПериод <= ОплачиваемыеНерабочиеПериоды.ДатаОкончания";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ИмяТаблицы", ИмяТаблицы);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ИмяПоляПериод", ИмяПоляПериод);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ИменаВТ = Новый Массив;
	ИменаВТ.Добавить("ВТОплачиваемыеНерабочиеПериоды");
	ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, ИменаВТ);
	
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

// Выполняет пересчет и регистрацию данных об отработанном времени для учета общего среднего заработка
// для указанных сотрудников в указанных месяцах.
//
// Параметры:
//	- МенеджерВременныхТаблиц - менеджер таблиц, в котором есть таблица ВТСотрудникиМесяцы с полями Сотрудник и Месяц.
//
Процедура ОбновитьДанныеКорректировкиДнейДляРасчетаОбщегоСреднегоЗаработка(МенеджерВременныхТаблиц, ИсключаемыйРегистратор = Неопределено) Экспорт
	
	СоздатьВТВидыНачисленийДляРегистрацииОбщегоСреднегоЗаработка(МенеджерВременныхТаблиц);
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ИсключаемыйРегистратор", ИсключаемыйРегистратор);
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СотрудникиМесяцы.Сотрудник КАК Сотрудник,
		|	СотрудникиМесяцы.Месяц КАК Месяц
		|ПОМЕСТИТЬ ВТСотрудникиМесяцыБезНачислений
		|ИЗ
		|	ВТСотрудникиМесяцы КАК СотрудникиМесяцы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.Начисления КАК Начисления
		|		ПО (Начисления.Сотрудник = СотрудникиМесяцы.Сотрудник)
		|			И (Начисления.ПериодДействия = СотрудникиМесяцы.Месяц)
		|			И (Начисления.ВидРасчета.ЗачетНормыВремени = ИСТИНА)
		|ГДЕ
		|	Начисления.ПериодДействия ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ФПД.Регистратор КАК Регистратор,
		|	ФПД.Сотрудник КАК Сотрудник,
		|	ВЫБОР
		|		КОГДА ФПД.Сторно
		|				ИЛИ ФПД.ФиксСторно
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Сторно,
		|	ФПД.ГрафикРаботы КАК ГрафикРаботы,
		|	ФПД.ПериодДействия КАК Месяц,
		|	ФПД.ВидРасчета КАК ВидРасчета,
		|	ФПД.ПериодДействияНачало КАК НачалоПериода,
		|	ФПД.ПериодДействияКонец КАК ОкончаниеПериода,
		|	ФПД.ПериодРегистрацииВремени КАК ПериодРегистрацииВремени
		|ПОМЕСТИТЬ ВТФактическийПериодДействия
		|ИЗ
		|	РегистрРасчета.Начисления.ФактическийПериодДействия(
		|			Регистратор <> &ИсключаемыйРегистратор
		|			И СторноТекущегоПериода = НЕОПРЕДЕЛЕНО
		|				И (Сотрудник, ПериодДействия) В
		|					(ВЫБРАТЬ
		|						СотрудникиМесяцы.Сотрудник,
		|						СотрудникиМесяцы.Месяц
		|					ИЗ
		|						ВТСотрудникиМесяцы КАК СотрудникиМесяцы)
		|				И ВидРасчета В
		|					(ВЫБРАТЬ
		|						ВТВидыНачислений.Ссылка
		|					ИЗ
		|						ВТВидыНачислений)) КАК ФПД";
		
	Запрос.Выполнить();	
		
	ИсключатьОплачиваемыеНерабочиеДни = ИсключатьОплачиваемоеНерабочееВремяИСуммы(МенеджерВременныхТаблиц, "ВТФактическийПериодДействия", "Месяц");
	ИсключитьОплачиваемыеНерабочиеДни(МенеджерВременныхТаблиц, ИсключатьОплачиваемыеНерабочиеДни);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ФПД.Сотрудник КАК Сотрудник,
		|	ФПД.Сторно КАК Сторно,
		|	ФПД.ГрафикРаботы КАК ГрафикРаботы,
		|	ФПД.Месяц КАК Месяц,
		|	ФПД.НачалоПериода КАК НачалоПериода,
		|	ФПД.ОкончаниеПериода КАК ОкончаниеПериода,
		|	ВидыНачислений.Прогул КАК Прогул,
		|	ВидыНачислений.Отгул КАК Отгул,
		|	ВидыНачислений.ЗачетНормыВремени КАК ЗачетНормыВремени,
		|	ВидыНачислений.ЗачетОтработанногоВремени КАК ЗачетОтработанногоВремени,
		|	ВидыНачислений.ЕжегодныйОтпуск КАК ЕжегодныйОтпуск
		|ПОМЕСТИТЬ ВТФПДСотрудников
		|ИЗ
		|	ВТФактическийПериодДействия КАК ФПД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТВидыНачислений КАК ВидыНачислений
		|		ПО (ВидыНачислений.Ссылка = ФПД.ВидРасчета)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПериодыГрафиков.ГрафикРаботы КАК ГрафикРаботы,
		|	ПериодыГрафиков.НачалоПериода КАК НачалоПериода,
		|	ПериодыГрафиков.ОкончаниеПериода КАК ОкончаниеПериода
		|ПОМЕСТИТЬ ВТПериодыГрафиков
		|ИЗ
		|	ВТФПДСотрудников КАК ПериодыГрафиков
		|ГДЕ
		|	ПериодыГрафиков.ЗачетНормыВремени";
	
	Запрос.Выполнить();
	
	УчетРабочегоВремениРасширенный.СоздатьВТДатыПроизводственногоКалендаряПоГрафикам(МенеджерВременныхТаблиц);
	
	// Данные по календарю (сколько всего должно быть зарегистрировано дней в учете среднего заработка) соотносим с тем, 
	// что уже накоплено в ДанныеОВремениДляРасчетаСреднегоОбщий.
	// Разницу записываем с минусом в ДанныеОВремениДляРасчетаСреднегоОбщийКорректировка.
	// Корректировку отработанных дней получаем "как есть" из учета начисленной зарплаты.
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ФПД.Сотрудник КАК Сотрудник,
		|	ФПД.Месяц КАК Месяц,
		|	ФПД.Сторно КАК Сторно,
		|	ДатыКалендаряПоФПД.Дата КАК Дата,
		|	ДатыКалендаряПоФПД.ДеньПоПятидневке КАК ДеньПоПятидневке,
		|	ДатыКалендаряПоФПД.ДеньПоШестидневке КАК ДеньПоШестидневке,
		|	ИСТИНА КАК ДеньКалендарный,
		|	ДатыКалендаряПоФПД.ЧасовПоПятидневке КАК ЧасовПоПятидневке
		|ПОМЕСТИТЬ ВТДатыПроизводственногоКалендаряПоСотрудникам
		|ИЗ
		|	ВТФПДСотрудников КАК ФПД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДатыПроизводственногоКалендаряПоГрафикам КАК ДатыКалендаряПоФПД
		|		ПО (ДатыКалендаряПоФПД.НачалоПериода = ФПД.НачалоПериода)
		|			И (ДатыКалендаряПоФПД.ОкончаниеПериода = ФПД.ОкончаниеПериода)
		|			И (ДатыКалендаряПоФПД.ГрафикРаботы = ФПД.ГрафикРаботы)
		|			И (ФПД.ЗачетНормыВремени)
		|			И (ФПД.ЗачетОтработанногоВремени
		|				ИЛИ ФПД.Отгул)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ФПД.Сотрудник,
		|	ФПД.Месяц,
		|	ФПД.Сторно,
		|	ДатыКалендаряПоФПД.Дата,
		|	ЛОЖЬ,
		|	ЛОЖЬ,
		|	ИСТИНА,
		|	0
		|ИЗ
		|	ВТФПДСотрудников КАК ФПД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДатыПроизводственногоКалендаряПоГрафикам КАК ДатыКалендаряПоФПД
		|		ПО (ДатыКалендаряПоФПД.НачалоПериода = ФПД.НачалоПериода)
		|			И (ДатыКалендаряПоФПД.ОкончаниеПериода = ФПД.ОкончаниеПериода)
		|			И (ДатыКалендаряПоФПД.ГрафикРаботы = ФПД.ГрафикРаботы)
		|			И (ФПД.Прогул)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ФПД.Сотрудник,
		|	ФПД.Месяц,
		|	ФПД.Сторно,
		|	ДатыКалендаряПоФПД.Дата,
		|	ЛОЖЬ,
		|	ЛОЖЬ,
		|	ИСТИНА,
		|	0
		|ИЗ
		|	ВТФПДСотрудников КАК ФПД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДатыПроизводственногоКалендаряПоГрафикам КАК ДатыКалендаряПоФПД
		|		ПО (ДатыКалендаряПоФПД.НачалоПериода = ФПД.НачалоПериода)
		|			И (ДатыКалендаряПоФПД.ОкончаниеПериода = ФПД.ОкончаниеПериода)
		|			И (ДатыКалендаряПоФПД.ГрафикРаботы = ФПД.ГрафикРаботы)
		|			И (ФПД.ЕжегодныйОтпуск)
		|			И (ДатыКалендаряПоФПД.ДеньПраздничный)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОтработанноеВремяКорректировка.Сотрудник КАК Сотрудник,
		|	ОтработанноеВремяКорректировка.ПериодДействия КАК Месяц,
		|	СУММА(ОтработанноеВремяКорректировка.ОтработаноДней) КАК ОтработаноДнейКорректировка
		|ПОМЕСТИТЬ ВТОтработанноеВремяКорректировка
		|ИЗ
		|	РегистрНакопления.ОтработанноеВремяПоСотрудникамКорректировка КАК ОтработанноеВремяКорректировка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудникиМесяцы КАК СотрудникиМесяцы
		|		ПО (СотрудникиМесяцы.Сотрудник = ОтработанноеВремяКорректировка.Сотрудник)
		|			И ОтработанноеВремяКорректировка.ПериодДействия = СотрудникиМесяцы.Месяц
		|			И (ОтработанноеВремяКорректировка.Регистратор <> &ИсключаемыйРегистратор)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТВидыНачислений КАК ВидыНачислений
		|		ПО (ВидыНачислений.Ссылка = ОтработанноеВремяКорректировка.Начисление)
		|			И (ВидыНачислений.ЗачетОтработанногоВремени)
		|
		|СГРУППИРОВАТЬ ПО
		|	ОтработанноеВремяКорректировка.ПериодДействия,
		|	ОтработанноеВремяКорректировка.Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НакопленныеДанныеВремени.Сотрудник КАК Сотрудник,
		|	НАЧАЛОПЕРИОДА(НакопленныеДанныеВремени.Период, МЕСЯЦ) КАК Месяц,
		|	СУММА(НакопленныеДанныеВремени.ОтработаноДней) КАК ОтработаноДней,
		|	СУММА(НакопленныеДанныеВремени.ОтработаноДнейПятидневка) КАК ОтработаноДнейПятидневка,
		|	СУММА(НакопленныеДанныеВремени.ОтработаноЧасовПятидневка) КАК ОтработаноЧасовПятидневка,
		|	СУММА(НакопленныеДанныеВремени.ОтработаноДнейШестидневка) КАК ОтработаноДнейШестидневка,
		|	СУММА(НакопленныеДанныеВремени.ОтработаноДнейКалендарных) КАК ОтработаноДнейКалендарных
		|ПОМЕСТИТЬ ВТНакопленныеДанные
		|ИЗ
		|	РегистрНакопления.ДанныеОВремениДляРасчетаСреднегоОбщий КАК НакопленныеДанныеВремени
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудникиМесяцы КАК СотрудникиМесяцы
		|		ПО НакопленныеДанныеВремени.Сотрудник = СотрудникиМесяцы.Сотрудник
		|			И НакопленныеДанныеВремени.Период >= СотрудникиМесяцы.Месяц
		|			И (НакопленныеДанныеВремени.Период <= КОНЕЦПЕРИОДА(СотрудникиМесяцы.Месяц, МЕСЯЦ))
		|			И (НакопленныеДанныеВремени.Регистратор <> &ИсключаемыйРегистратор)
		|
		|СГРУППИРОВАТЬ ПО
		|	НакопленныеДанныеВремени.Сотрудник,
		|	НАЧАЛОПЕРИОДА(НакопленныеДанныеВремени.Период, МЕСЯЦ)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ФПД.Сотрудник КАК Сотрудник,
		|	ФПД.Месяц КАК Месяц,
		|	ДатыКалендаряПоФПД.Дата КАК Дата,
		|	ДатыКалендаряПоФПД.ЧасовПоПятидневке КАК ЧасовПоПятидневке
		|ПОМЕСТИТЬ ВТЧасовПоДатам
		|ИЗ
		|	ВТФПДСотрудников КАК ФПД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДатыПроизводственногоКалендаряПоГрафикам КАК ДатыКалендаряПоФПД
		|		ПО (ДатыКалендаряПоФПД.НачалоПериода = ФПД.НачалоПериода)
		|			И (ДатыКалендаряПоФПД.ОкончаниеПериода = ФПД.ОкончаниеПериода)
		|			И (ДатыКалендаряПоФПД.ГрафикРаботы = ФПД.ГрафикРаботы)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РазличныеДатыПоСотрудникам.Сотрудник КАК Сотрудник,
		|	РазличныеДатыПоСотрудникам.Месяц КАК Месяц,
		|	РазличныеДатыПоСотрудникам.Дата КАК Дата,
		|	СУММА(ВЫБОР
		|			КОГДА РазличныеДатыПоСотрудникам.ДеньПоПятидневке
		|				ТОГДА ВЫБОР
		|						КОГДА РазличныеДатыПоСотрудникам.Сторно
		|							ТОГДА -1
		|						ИНАЧЕ 1
		|					КОНЕЦ
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОтработаноНаДатуДнейПятидневка,
		|	СУММА(ВЫБОР
		|			КОГДА РазличныеДатыПоСотрудникам.ДеньПоШестидневке
		|				ТОГДА ВЫБОР
		|						КОГДА РазличныеДатыПоСотрудникам.Сторно
		|							ТОГДА -1
		|						ИНАЧЕ 1
		|					КОНЕЦ
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОтработаноНаДатуДнейШестидневка,
		|	СУММА(ВЫБОР
		|			КОГДА РазличныеДатыПоСотрудникам.ДеньКалендарный
		|				ТОГДА ВЫБОР
		|						КОГДА РазличныеДатыПоСотрудникам.Сторно
		|							ТОГДА -1
		|						ИНАЧЕ 1
		|					КОНЕЦ
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОтработаноНаДатуДнейКалендарных
		|ПОМЕСТИТЬ ВТОтработаноДнейПоДатам
		|ИЗ
		|	ВТДатыПроизводственногоКалендаряПоСотрудникам КАК РазличныеДатыПоСотрудникам
		|
		|СГРУППИРОВАТЬ ПО
		|	РазличныеДатыПоСотрудникам.Сотрудник,
		|	РазличныеДатыПоСотрудникам.Месяц,
		|	РазличныеДатыПоСотрудникам.Дата
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РазличныеДатыПоСотрудникам.Сотрудник КАК Сотрудник,
		|	РазличныеДатыПоСотрудникам.Месяц КАК Месяц,
		|	СУММА(ВЫБОР
		|			КОГДА РазличныеДатыПоСотрудникам.ОтработаноНаДатуДнейПятидневка > 0
		|				ТОГДА 1
		|			КОГДА РазличныеДатыПоСотрудникам.ОтработаноНаДатуДнейПятидневка < 0
		|				ТОГДА -1
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОтработаноДнейПятидневка,
		|	СУММА(ВЫБОР
		|			КОГДА РазличныеДатыПоСотрудникам.ОтработаноНаДатуДнейПятидневка > 0
		|				ТОГДА 1
		|			КОГДА РазличныеДатыПоСотрудникам.ОтработаноНаДатуДнейПятидневка < 0
		|				ТОГДА -1
		|			ИНАЧЕ 0
		|		КОНЕЦ) * ЧасовПоДатам.ЧасовПоПятидневке КАК ОтработаноЧасовПятидневка,
		|	СУММА(ВЫБОР
		|			КОГДА РазличныеДатыПоСотрудникам.ОтработаноНаДатуДнейШестидневка > 0
		|				ТОГДА 1
		|			КОГДА РазличныеДатыПоСотрудникам.ОтработаноНаДатуДнейШестидневка < 0
		|				ТОГДА -1
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОтработаноДнейШестидневка,
		|	СУММА(ВЫБОР
		|			КОГДА РазличныеДатыПоСотрудникам.ОтработаноНаДатуДнейКалендарных > 0
		|				ТОГДА 1
		|			КОГДА РазличныеДатыПоСотрудникам.ОтработаноНаДатуДнейКалендарных < 0
		|				ТОГДА -1
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОтработаноДнейКалендарных
		|ПОМЕСТИТЬ ВТДанныеПоФПД
		|ИЗ
		|	ВТОтработаноДнейПоДатам КАК РазличныеДатыПоСотрудникам
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЧасовПоДатам КАК ЧасовПоДатам
		|		ПО (ЧасовПоДатам.Сотрудник = РазличныеДатыПоСотрудникам.Сотрудник)
		|			И (ЧасовПоДатам.Месяц = РазличныеДатыПоСотрудникам.Месяц)
		|			И (ЧасовПоДатам.Дата = РазличныеДатыПоСотрудникам.Дата)
		|
		|СГРУППИРОВАТЬ ПО
		|	РазличныеДатыПоСотрудникам.Сотрудник,
		|	РазличныеДатыПоСотрудникам.Месяц,
		|	ЧасовПоДатам.ЧасовПоПятидневке
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеВремени.Сотрудник КАК Сотрудник,
		|	ДанныеВремени.Месяц КАК Месяц,
		|	СУММА(ДанныеВремени.ОтработаноДней) КАК ОтработаноДней,
		|	СУММА(ДанныеВремени.ОтработаноЧасов) КАК ОтработаноЧасов,
		|	СУММА(ДанныеВремени.ОтработаноДнейПятидневка) КАК ОтработаноДнейПятидневка,
		|	СУММА(ДанныеВремени.ОтработаноЧасовПятидневка) КАК ОтработаноЧасовПятидневка,
		|	СУММА(ДанныеВремени.ОтработаноДнейШестидневка) КАК ОтработаноДнейШестидневка,
		|	СУММА(ДанныеВремени.ОтработаноДнейКалендарных) КАК ОтработаноДнейКалендарных
		|ПОМЕСТИТЬ ВТДанныеВремени
		|ИЗ
		|	(ВЫБРАТЬ
		|		ОтработанноеВремяКорректировка.Сотрудник КАК Сотрудник,
		|		ОтработанноеВремяКорректировка.Месяц КАК Месяц,
		|		ОтработанноеВремяКорректировка.ОтработаноДнейКорректировка КАК ОтработаноДней,
		|		0 КАК ОтработаноЧасов,
		|		0 КАК ОтработаноДнейПятидневка,
		|		0 КАК ОтработаноЧасовПятидневка,
		|		0 КАК ОтработаноДнейШестидневка,
		|		0 КАК ОтработаноДнейКалендарных
		|	ИЗ
		|		ВТОтработанноеВремяКорректировка КАК ОтработанноеВремяКорректировка
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ДанныеПоФПД.Сотрудник,
		|		ДанныеПоФПД.Месяц,
		|		0,
		|		0,
		|		ДанныеПоФПД.ОтработаноДнейПятидневка,
		|		ДанныеПоФПД.ОтработаноЧасовПятидневка,
		|		ДанныеПоФПД.ОтработаноДнейШестидневка,
		|		ДанныеПоФПД.ОтработаноДнейКалендарных
		|	ИЗ
		|		ВТДанныеПоФПД КАК ДанныеПоФПД
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		НакопленныеДанные.Сотрудник,
		|		НакопленныеДанные.Месяц,
		|		0,
		|		0,
		|		-НакопленныеДанные.ОтработаноДнейПятидневка,
		|		-НакопленныеДанные.ОтработаноЧасовПятидневка,
		|		-НакопленныеДанные.ОтработаноДнейШестидневка,
		|		-НакопленныеДанные.ОтработаноДнейКалендарных
		|	ИЗ
		|		ВТНакопленныеДанные КАК НакопленныеДанные) КАК ДанныеВремени
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеВремени.Сотрудник,
		|	ДанныеВремени.Месяц
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СотрудникиМесяцы.Сотрудник КАК Сотрудник,
		|	СотрудникиМесяцы.Месяц КАК Месяц,
		|	ДанныеВремени.ОтработаноДней КАК ОтработаноДней,
		|	ДанныеВремени.ОтработаноЧасов КАК ОтработаноЧасов,
		|	ДанныеВремени.ОтработаноДнейПятидневка КАК ОтработаноДнейПятидневка,
		|	ДанныеВремени.ОтработаноЧасовПятидневка КАК ОтработаноЧасовПятидневка,
		|	ДанныеВремени.ОтработаноДнейШестидневка КАК ОтработаноДнейШестидневка,
		|	ДанныеВремени.ОтработаноДнейКалендарных КАК ОтработаноДнейКалендарных
		|ИЗ
		|	ВТСотрудникиМесяцы КАК СотрудникиМесяцы
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеВремени КАК ДанныеВремени
		|		ПО СотрудникиМесяцы.Сотрудник = ДанныеВремени.Сотрудник
		|			И СотрудникиМесяцы.Месяц = ДанныеВремени.Месяц
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСотрудникиМесяцыБезНачислений КАК СотрудникиМесяцыБезНачислений
		|		ПО (СотрудникиМесяцыБезНачислений.Сотрудник = СотрудникиМесяцы.Сотрудник)
		|			И (СотрудникиМесяцыБезНачислений.Месяц = СотрудникиМесяцы.Месяц)
		|ГДЕ
		|	СотрудникиМесяцыБезНачислений.Месяц ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СотрудникиМесяцыБезНачислений.Сотрудник,
		|	СотрудникиМесяцыБезНачислений.Месяц,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0
		|ИЗ
		|	ВТСотрудникиМесяцыБезНачислений КАК СотрудникиМесяцыБезНачислений
		|
		|УПОРЯДОЧИТЬ ПО
		|	Сотрудник,
		|	Месяц";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	НаборЗаписей = РегистрыСведений.ДанныеОВремениДляРасчетаСреднегоОбщийКорректировка.СоздатьНаборЗаписей();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Сотрудник") Цикл
		Пока Выборка.СледующийПоЗначениюПоля("Месяц") Цикл
			НаборЗаписей.Очистить();
			Пока Выборка.Следующий() Цикл
				Если Выборка.ОтработаноДней = 0 
					И Выборка.ОтработаноЧасов = 0 
					И Выборка.ОтработаноДнейПятидневка = 0 
					И Выборка.ОтработаноДнейШестидневка = 0 
					И Выборка.ОтработаноДнейКалендарных = 0 Тогда
					// Не записываем пустые строки.
					Продолжить;
				КонецЕсли;
				НоваяСтрока = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
				НоваяСтрока.ПорядокРасчета = Перечисления.ПорядокРасчетаСреднегоЗаработкаОбщий.Постановление2010;
			КонецЦикла;
			НаборЗаписей.Отбор.Сотрудник.Установить(Выборка.Сотрудник);
			НаборЗаписей.Отбор.Месяц.Установить(Выборка.Месяц);
			НаборЗаписей.Записать();
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

// Процедура добавляет в менеджер таблиц временную таблицу 
//  с порядком расчета общего среднего заработка, 
//  исходя из периода и других данных начисления.
//
Процедура СоздатьВТПорядокРасчетаОбщегоСреднегоЗаработка(МенеджерВременныхТаблиц) 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Начисления.ДатаДействия,
		|	Начисления.Начисление,
		|	НастройкиНачислений.ПорядокРасчета
		|ПОМЕСТИТЬ ВТПорядокРасчетаОбщегоСреднегоЗаработка
		|ИЗ
		|	ВТНачисления КАК Начисления
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления.СреднийЗаработокОбщий КАК НастройкиНачислений
		|		ПО (НастройкиНачислений.Ссылка = Начисления.Начисление)
		|			И (НастройкиНачислений.Значение <> ЗНАЧЕНИЕ(Перечисление.УчетНачисленийВСреднемЗаработкеОбщий.НеВключать))";
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура СоздатьВТВидыНачисленийДляРегистрацииОбщегоСреднегоЗаработка(МенеджерВременныхТаблиц)
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Начисления.Ссылка КАК Ссылка,
		|	Начисления.ЗачетНормыВремени КАК ЗачетНормыВремени,
		|	Начисления.ЗачетОтработанногоВремени КАК ЗачетОтработанногоВремени,
		|	ЛОЖЬ КАК Прогул,
		|	ЛОЖЬ КАК Отгул,
		|	ЛОЖЬ КАК ЕжегодныйОтпуск
		|ПОМЕСТИТЬ ВТВидыНачислений
		|ИЗ
		|	ПланВидовРасчета.Начисления КАК Начисления
		|ГДЕ
		|	Начисления.ЗачетОтработанногоВремени
		|	И ИСТИНА В
		|			(ВЫБРАТЬ ПЕРВЫЕ 1
		|				ИСТИНА
		|			ИЗ
		|				ПланВидовРасчета.Начисления.СреднийЗаработокОбщий КАК НастройкаСреднегоЗаработка
		|			ГДЕ
		|				НастройкаСреднегоЗаработка.Ссылка = Начисления.Ссылка
		|				И НастройкаСреднегоЗаработка.Значение <> ЗНАЧЕНИЕ(Перечисление.УчетНачисленийВСреднемЗаработкеОбщий.НеВключать))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Начисления.Ссылка,
		|	ИСТИНА,
		|	ЛОЖЬ,
		|	ИСТИНА,
		|	ЛОЖЬ,
		|	ЛОЖЬ
		|ИЗ
		|	ПланВидовРасчета.Начисления КАК Начисления
		|ГДЕ
		|	Начисления.КатегорияНачисленияИлиНеоплаченногоВремени В(&КатегорииПрогула)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Начисления.Ссылка,
		|	ИСТИНА,
		|	ЛОЖЬ,
		|	ЛОЖЬ,
		|	ИСТИНА,
		|	ЛОЖЬ
		|ИЗ
		|	ПланВидовРасчета.Начисления КАК Начисления
		|ГДЕ
		|	Начисления.КатегорияНачисленияИлиНеоплаченногоВремени В(&КатегорииОтгула)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Начисления.Ссылка,
		|	ИСТИНА,
		|	ЛОЖЬ,
		|	ЛОЖЬ,
		|	ЛОЖЬ,
		|	ИСТИНА
		|ИЗ
		|	ПланВидовРасчета.Начисления КАК Начисления
		|ГДЕ
		|	(Начисления.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ОплатаОтпуска)
		|	ИЛИ Начисления.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ДенежноеСодержаниеНаПериодОтпуска))
		|	И Начисления.ВидОтпуска.ОтпускЯвляетсяЕжегодным
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Начисления.Ссылка,
		|	ИСТИНА,
		|	ЛОЖЬ,
		|	ЛОЖЬ,
		|	ЛОЖЬ,
		|	ИСТИНА
		|ИЗ
		|	ПланВидовРасчета.Начисления КАК Начисления
		|ГДЕ
		|	Начисления.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ОтпускБезОплаты)
		|	И Начисления.ВидОтпуска = &ОтпускЧАЭС
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Начисления.Ссылка";
		
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("КатегорииПрогула", ПланыВидовРасчета.Начисления.КатегорииПрогула());
	Запрос.УстановитьПараметр("КатегорииОтгула", ПланыВидовРасчета.Начисления.КатегорииОтгула());
	Запрос.УстановитьПараметр("ВидыВремениРаботаВыходныеПраздничные", УчетРабочегоВремениРасширенный.ВидыВремениРаботаВыходныеПраздничные());
	Запрос.УстановитьПараметр("ОтпускЧАЭС", ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыОтпусков.ОтпускПострадавшимВАварииЧАЭС"));
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура СоздатьВТНачисленияПоТаблицеНачисления(МенеджерВременныхТаблиц, Начисления)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Начисления", Начисления);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Начисления.ДокументСсылка КАК ДокументСсылка,
		|	Начисления.Сотрудник КАК Сотрудник,
		|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
		|	Начисления.Организация КАК Организация,
		|	Начисления.Начисление КАК Начисление,
		|	Начисления.ДатаДействия КАК ДатаДействия,
		|	Начисления.МесяцНачисления КАК МесяцНачисления,
		|	Начисления.ПериодДействия КАК ПериодДействия,
		|	Начисления.ДатаНачала КАК ДатаНачала,
		|	Начисления.ДатаОкончания КАК ДатаОкончания,
		|	Начисления.НачалоБазовогоПериода КАК НачалоБазовогоПериода,
		|	Начисления.ОкончаниеБазовогоПериода КАК ОкончаниеБазовогоПериода,
		|	Начисления.Сумма КАК Сумма,
		|	Начисления.РанееНачислено КАК РанееНачислено,
		|	Начисления.Подразделение КАК Подразделение,
		|	Начисления.ДокументОснование КАК ДокументОснование,
		|	Начисления.Сторно КАК Сторно,
		|	Начисления.ФиксСторно КАК ФиксСторно,
		|	Начисления.НомерСтроки КАК НомерСтроки,
		|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	Начисления.СтатьяФинансирования КАК СтатьяФинансирования,
		|	Начисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	Начисления.СтатьяРасходов КАК СтатьяРасходов,
		|	Начисления.ИсходныйДокумент КАК ИсходныйДокумент,
		|	Начисления.СторнируемыйДокумент КАК СторнируемыйДокумент,
		|	Начисления.ПоказательПремирования КАК ПоказательПремирования,
		|	Начисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД
		|ПОМЕСТИТЬ ВТНачисленияПредварительно
		|ИЗ
		|	&Начисления КАК Начисления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НачисленияПредварительно.ДокументСсылка КАК ДокументСсылка,
		|	НачисленияПредварительно.Сотрудник КАК Сотрудник,
		|	НачисленияПредварительно.ФизическоеЛицо КАК ФизическоеЛицо,
		|	НачисленияПредварительно.Организация КАК Организация,
		|	НачисленияПредварительно.Начисление КАК Начисление,
		|	НачисленияПредварительно.ДатаДействия КАК ДатаДействия,
		|	НачисленияПредварительно.МесяцНачисления КАК МесяцНачисления,
		|	НачисленияПредварительно.ПериодДействия КАК ПериодДействия,
		|	НачисленияПредварительно.ДатаНачала КАК ДатаНачала,
		|	НачисленияПредварительно.ДатаОкончания КАК ДатаОкончания,
		|	НачисленияПредварительно.НачалоБазовогоПериода КАК НачалоБазовогоПериода,
		|	НачисленияПредварительно.ОкончаниеБазовогоПериода КАК ОкончаниеБазовогоПериода,
		|	СУММА(НачисленияПредварительно.Сумма) КАК Сумма,
		|	СУММА(НачисленияПредварительно.РанееНачислено) КАК РанееНачислено,
		|	НачисленияПредварительно.Сторно КАК Сторно,
		|	НачисленияПредварительно.ФиксСторно КАК ФиксСторно,
		|	НачисленияПредварительно.НомерСтроки КАК НомерСтроки,
		|	НачисленияПредварительно.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	НачисленияПредварительно.СтатьяФинансирования КАК СтатьяФинансирования,
		|	НачисленияПредварительно.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	НачисленияПредварительно.СтатьяРасходов КАК СтатьяРасходов,
		|	НачисленияПредварительно.ИсходныйДокумент КАК ИсходныйДокумент,
		|	НачисленияПредварительно.СторнируемыйДокумент КАК СторнируемыйДокумент,
		|	НачисленияПредварительно.ПоказательПремирования КАК ПоказательПремирования,
		|	НачисленияПредварительно.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД
		|ПОМЕСТИТЬ ВТНачисления
		|ИЗ
		|	ВТНачисленияПредварительно КАК НачисленияПредварительно
		|
		|СГРУППИРОВАТЬ ПО
		|	НачисленияПредварительно.ДокументСсылка,
		|	НачисленияПредварительно.Сотрудник,
		|	НачисленияПредварительно.ФизическоеЛицо,
		|	НачисленияПредварительно.Организация,
		|	НачисленияПредварительно.Начисление,
		|	НачисленияПредварительно.ДатаДействия,
		|	НачисленияПредварительно.МесяцНачисления,
		|	НачисленияПредварительно.ПериодДействия,
		|	НачисленияПредварительно.ДатаНачала,
		|	НачисленияПредварительно.ДатаОкончания,
		|	НачисленияПредварительно.НачалоБазовогоПериода,
		|	НачисленияПредварительно.ОкончаниеБазовогоПериода,
		|	НачисленияПредварительно.Сторно,
		|	НачисленияПредварительно.ФиксСторно,
		|	НачисленияПредварительно.НомерСтроки,
		|	НачисленияПредварительно.ИдентификаторСтроки,
		|	НачисленияПредварительно.СтатьяФинансирования,
		|	НачисленияПредварительно.СпособОтраженияЗарплатыВБухучете,
		|	НачисленияПредварительно.СтатьяРасходов,
		|	НачисленияПредварительно.ИсходныйДокумент,
		|	НачисленияПредварительно.СторнируемыйДокумент,
		|	НачисленияПредварительно.ПоказательПремирования,
		|	НачисленияПредварительно.ОблагаетсяЕНВД";
		
	Запрос.Выполнить();
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Начисления.Сотрудник КАК Сотрудник,
		|	Начисления.Организация КАК Организация,
		|	Начисления.Подразделение КАК Подразделение,
		|	Начисления.Начисление КАК Начисление,
		|	Начисления.ДатаНачала КАК ДатаНачала,
		|	Начисления.ДатаОкончания КАК ДатаОкончания,
		|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	Начисления.ПериодДействия КАК ПериодДействия,
		|	Начисления.МесяцНачисления КАК МесяцНачисления,
		|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
		|	Начисления.ДокументОснование КАК ДокументОснование,
		|	СУММА(Начисления.РанееНачислено) КАК РанееНачислено,
		|	ВЫБОР
		|		КОГДА Начисления.Сторно
		|				ИЛИ Начисления.ФиксСторно
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Сторно
		|ПОМЕСТИТЬ ВТРанееНачисленоПоБазовымРасчетам
		|ИЗ
		|	ВТНачисленияПредварительно КАК Начисления
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления КАК ВидыРасчета
		|		ПО (ВидыРасчета.Ссылка = Начисления.Начисление)
		|			И (ВидыРасчета.СтратегияОтраженияВСреднемЗаработке = ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазовымРасчетам))
		|			И (Начисления.РанееНачислено <> 0)
		|
		|СГРУППИРОВАТЬ ПО
		|	Начисления.Сотрудник,
		|	Начисления.Организация,
		|	Начисления.Подразделение,
		|	Начисления.Начисление,
		|	Начисления.ДатаНачала,
		|	Начисления.ДатаОкончания,
		|	Начисления.ИдентификаторСтроки,
		|	Начисления.ПериодДействия,
		|	Начисления.МесяцНачисления,
		|	Начисления.ФизическоеЛицо,
		|	Начисления.ДокументОснование,
		|	ВЫБОР
		|		КОГДА Начисления.Сторно
		|				ИЛИ Начисления.ФиксСторно
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ";
	
	Запрос.Выполнить();
	
	ИменаВТ = Новый Массив;
	ИменаВТ.Добавить("ВТНачисленияПредварительно");
	ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, ИменаВТ);
	
КонецПроцедуры

Процедура ДополнитьВТНачисленияОтражаемыеПоБазе(МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РанееНачисленоПоБазовымРасчетам.*
		|ИЗ
		|	ВТРанееНачисленоПоБазовымРасчетам КАК РанееНачисленоПоБазовымРасчетам
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИсключаемыеНачисления КАК ИсключаемыеНачисления
		|		ПО РанееНачисленоПоБазовымРасчетам.ИдентификаторСтроки = ИсключаемыеНачисления.ИдентификаторСтроки
		|ГДЕ
		|	ИсключаемыеНачисления.ИдентификаторСтроки ЕСТЬ NULL";
	
	РанееНачисленоПоБазовымРасчетам = Запрос.Выполнить().Выгрузить();
	
	Если РанееНачисленоПоБазовымРасчетам.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	РанееНачисленоДетализация = РасчетЗарплатыРасширенный.РаспределениеРанееНачисленныхСумм(РанееНачисленоПоБазовымРасчетам);
	
	Запрос.УстановитьПараметр("РанееНачисленоДетализация", РанееНачисленоДетализация);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РанееНачисленоДетализация.Регистратор КАК Регистратор,
		|	РанееНачисленоДетализация.ИдентификаторСтрокиБазы КАК ИдентификаторСтроки,
		|	РанееНачисленоДетализация.ИдентификаторСтроки КАК ИдентификаторСтрокиДокумента,
		|	РанееНачисленоДетализация.Начисление КАК Начисление,
		|	РанееНачисленоДетализация.Сотрудник КАК Сотрудник,
		|	РанееНачисленоДетализация.ПериодДействия КАК Период,
		|	РанееНачисленоДетализация.Результат КАК Сумма,
		|	РанееНачисленоДетализация.ПериодРегистрации КАК МесяцНачисления,
		|	РанееНачисленоДетализация.Сторно КАК Сторно,
		|	РанееНачисленоДетализация.ФиксСторно КАК ФиксСторно
		|ПОМЕСТИТЬ ВТРанееНачисленоДетализация
		|ИЗ
		|	&РанееНачисленоДетализация КАК РанееНачисленоДетализация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	Начисления.ИдентификаторСтрокиДокумента КАК ИдентификаторСтрокиДокумента,
		|	Начисления.СтатьяФинансирования КАК СтатьяФинансирования,
		|	Начисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	Начисления.СтатьяРасходов КАК СтатьяРасходов,
		|	Начисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	Начисления.Сотрудник КАК Сотрудник,
		|	Начисления.Период КАК Период,
		|	Начисления.ДокументСсылка КАК ДокументСсылка,
		|	Начисления.Сумма КАК Сумма,
		|	Начисления.МесяцНачисления КАК МесяцНачисления,
		|	Начисления.Сторно КАК Сторно
		|ПОМЕСТИТЬ ВТНачисленияОтражаемыеПоБазеПредварительно
		|ИЗ
		|	ВТНачисленияОтражаемыеПоБазе КАК Начисления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РанееНачисленоДетализация.ИдентификаторСтроки,
		|	РанееНачисленоДетализация.ИдентификаторСтрокиДокумента,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка),
		|	ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка),
		|	ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка),
		|	ЛОЖЬ,
		|	РанееНачисленоДетализация.Сотрудник,
		|	РанееНачисленоДетализация.Период,
		|	РанееНачисленоДетализация.Регистратор,
		|	РанееНачисленоДетализация.Сумма,
		|	РанееНачисленоДетализация.МесяцНачисления,
		|	ВЫБОР
		|		КОГДА РанееНачисленоДетализация.Сторно
		|				ИЛИ РанееНачисленоДетализация.ФиксСторно
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ
		|ИЗ
		|	ВТРанееНачисленоДетализация КАК РанееНачисленоДетализация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТНачисленияОтражаемыеПоБазе
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	Начисления.ИдентификаторСтрокиДокумента КАК ИдентификаторСтрокиДокумента,
		|	Начисления.СтатьяФинансирования КАК СтатьяФинансирования,
		|	Начисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	Начисления.СтатьяРасходов КАК СтатьяРасходов,
		|	Начисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	Начисления.Сотрудник КАК Сотрудник,
		|	Начисления.Период КАК Период,
		|	Начисления.ДокументСсылка КАК ДокументСсылка,
		|	Начисления.Сумма КАК Сумма,
		|	Начисления.МесяцНачисления КАК МесяцНачисления,
		|	Начисления.Сторно КАК Сторно
		|ПОМЕСТИТЬ ВТНачисленияОтражаемыеПоБазе
		|ИЗ
		|	ВТНачисленияОтражаемыеПоБазеПредварительно КАК Начисления";
	Запрос.Выполнить();
	
	ИменаВТ = Новый Массив;
	ИменаВТ.Добавить("ВТРанееНачисленоДетализация");
	ИменаВТ.Добавить("ВТНачисленияОтражаемыеПоБазеПредварительно");
	ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, ИменаВТ);
		
КонецПроцедуры

Процедура ДополнитьИсходнымиЗаписямиДляСторно(МенеджерВременныхТаблиц)
	
	СоздатьВТИсходныеЗаписиДляФиксСторно(МенеджерВременныхТаблиц, "ВТНачисления");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЕСТЬNULL(ИсходныеЗаписи.ИдентификаторСтроки, Начисления.ИдентификаторСтроки) КАК ИдентификаторСтроки,
		|	ЕСТЬNULL(ИсходныеЗаписи.ИдентификаторСтрокиДокумента, Начисления.ИдентификаторСтрокиДокумента) КАК ИдентификаторСтрокиДокумента,
		|	Начисления.СтатьяФинансирования КАК СтатьяФинансирования,
		|	Начисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	Начисления.СтатьяРасходов КАК СтатьяРасходов,
		|	Начисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	Начисления.Сотрудник КАК Сотрудник,
		|	Начисления.Период КАК Период,
		|	ЕСТЬNULL(ИсходныеЗаписи.ДокументСсылка, Начисления.ДокументСсылка) КАК ДокументСсылка,
		|	Начисления.Сумма КАК Сумма,
		|	Начисления.МесяцНачисления КАК МесяцНачисления,
		|	Начисления.Сторно КАК Сторно
		|ПОМЕСТИТЬ ВТНачисленияОтражаемыеПоБазеПредварительно
		|ИЗ
		|	ВТНачисленияОтражаемыеПоБазе КАК Начисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИсходныеЗаписиДляФиксСторно КАК ИсходныеЗаписи
		|		ПО Начисления.ДокументСсылка = ИсходныеЗаписи.ДокументСсылкаСторно
		|			И Начисления.ИдентификаторСтроки = ИсходныеЗаписи.ИдентификаторСтрокиСторно
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТНачисленияОтражаемыеПоБазе
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	Начисления.ИдентификаторСтрокиДокумента КАК ИдентификаторСтрокиДокумента,
		|	Начисления.СтатьяФинансирования КАК СтатьяФинансирования,
		|	Начисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	Начисления.СтатьяРасходов КАК СтатьяРасходов,
		|	Начисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	Начисления.Сотрудник КАК Сотрудник,
		|	Начисления.Период КАК Период,
		|	Начисления.ДокументСсылка КАК ДокументСсылка,
		|	Начисления.Сумма КАК Сумма,
		|	Начисления.МесяцНачисления КАК МесяцНачисления,
		|	Начисления.Сторно КАК Сторно
		|ПОМЕСТИТЬ ВТНачисленияОтражаемыеПоБазе
		|ИЗ
		|	ВТНачисленияОтражаемыеПоБазеПредварительно КАК Начисления";
	Запрос.Выполнить();
	
	ИменаВТ = Новый Массив;
	ИменаВТ.Добавить("ВТНачисленияОтражаемыеПоБазеПредварительно");
	ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, ИменаВТ);
		
КонецПроцедуры

Процедура СоздатьВТИсходныеЗаписиДляФиксСторно(МенеджерВременныхТаблиц, ВТНачисления)

    Текст =
        "ВЫБРАТЬ
        |    СторноСтроки.ДокументСсылка КАК ДокументСсылка,
        |    СторноСтроки.Начисление КАК Начисление,
        |    СторноСтроки.СторнируемыйДокумент КАК СторнируемыйДокумент,
        |    СторноСтроки.ИдентификаторСтроки КАК ИдентификаторСтроки,
        |    СторноСтроки.НомерСтроки КАК НомерСтроки,
        |    СторноСтроки.ДокументСсылка КАК ДокументСсылкаСторно,
        |    СторноСтроки.СтатьяРасходов КАК СтатьяРасходов,
        |    СторноСтроки.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
        |    СторноСтроки.Сотрудник КАК Сотрудник,
        |    СторноСтроки.Период КАК Период,
        |    СУММА(СторноСтроки.Сумма) КАК Сумма,
        |    СторноСтроки.МесяцНачисления КАК МесяцНачисления
        |ПОМЕСТИТЬ ВТСторноСтроки
        |ИЗ
        |    ВТНачисления КАК СторноСтроки
        |ГДЕ
        |    СторноСтроки.ФиксСторно
        |
        |СГРУППИРОВАТЬ ПО
        |    СторноСтроки.ДокументСсылка,
        |    СторноСтроки.Начисление,
        |    СторноСтроки.СторнируемыйДокумент,
        |    СторноСтроки.ИдентификаторСтроки,
        |    СторноСтроки.НомерСтроки,
        |    СторноСтроки.СтатьяРасходов,
        |    СторноСтроки.ОблагаетсяЕНВД,
        |    СторноСтроки.Сотрудник,
        |    СторноСтроки.Период,
        |    СторноСтроки.МесяцНачисления,
        |    СторноСтроки.ДокументСсылка
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |    Начисления.Регистратор КАК ДокументСсылка,
        |    Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
        |    СторноСтроки.НомерСтроки КАК ИдентификаторСтрокиДокумента,
        |    СторноСтроки.ИдентификаторСтроки КАК ИдентификаторСтрокиСторно,
        |    СторноСтроки.ДокументСсылка КАК ДокументСсылкаСторно,
        |    СторноСтроки.СтатьяРасходов КАК СтатьяРасходов,
        |    СторноСтроки.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
        |    СторноСтроки.Сотрудник КАК Сотрудник,
        |    СторноСтроки.Период КАК Период,
        |    СторноСтроки.Сумма КАК Сумма,
        |    СторноСтроки.МесяцНачисления КАК МесяцНачисления
        |ПОМЕСТИТЬ ВТИсходныеЗаписиДляФиксСторно
        |ИЗ
        |    ВТСторноСтроки КАК СторноСтроки
        |        ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрРасчета.Начисления КАК Начисления
        |        ПО (Начисления.Регистратор = СторноСтроки.СторнируемыйДокумент)
        |            И (Начисления.ВидРасчета = СторноСтроки.Начисление)
        |            И (Начисления.Сотрудник = СторноСтроки.Сотрудник)
        |            И (Начисления.Результат = -СторноСтроки.Сумма)
        |            И (НЕ Начисления.Сторно)
        |            И (НЕ Начисления.ФиксСторно)
        |            И (Начисления.СторноТекущегоПериода = НЕОПРЕДЕЛЕНО)
        |        ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления КАК ВидыРасчета
        |        ПО СторноСтроки.Начисление = ВидыРасчета.Ссылка
        |            И (ВидыРасчета.СтратегияОтраженияВСреднемЗаработке = ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазовымРасчетам))";
    Текст = СтрЗаменить(Текст, "ВТНачисления", ВТНачисления);

    Запрос = Новый Запрос(Текст);
    Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
    Запрос.Выполнить();
	
	ИменаВТ = Новый Массив;
	ИменаВТ.Добавить("ВТСторноСтроки");
	ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, ИменаВТ);
	
КонецПроцедуры

Процедура РаспределитьВТНачисленияПоБазе(МенеджерВременныхТаблиц, Регистратор, ИсключатьВПериодКомандировок)
	
	// Если начисление отражается в среднем заработке пропорционально базовым начислениям, получаем расчетную базу и
	// выполняем распределение.
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	
	// Определяем, вообще есть ли в регистрируемом наборе подобные начисления.
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтрокиДокумента,
		|	Начисления.Начисление КАК Начисление,
		|	Начисления.СтатьяФинансирования КАК СтатьяФинансирования,
		|	Начисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	Начисления.СтатьяРасходов КАК СтатьяРасходов,
		|	Начисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	Начисления.Сотрудник КАК Сотрудник,
		|	Начисления.Период КАК Период,
		|	Начисления.ДокументСсылка КАК ДокументСсылка,
		|	Начисления.Сумма КАК Сумма,
		|	Начисления.МесяцНачисления КАК МесяцНачисления,
		|	ВЫБОР
		|		КОГДА Начисления.Сторно
		|				ИЛИ Начисления.ФиксСторно
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Сторно
		|ИЗ
		|	ВТНачисления КАК Начисления
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления КАК ВидыРасчета
		|		ПО (ВидыРасчета.Ссылка = Начисления.Начисление)
		|			И (ВидыРасчета.СтратегияОтраженияВСреднемЗаработке = ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазовымРасчетам))";
	
	Начисления = Запрос.Выполнить().Выгрузить();
	
	ИменаКолонок = Новый Массив;
	Для Каждого Колонка Из Начисления.Колонки Цикл 
		Если Колонка.Имя <> "Сумма" И Колонка.Имя <> "Сторно" Тогда 
			ИменаКолонок.Добавить(Колонка.Имя);
		КонецЕсли;
	КонецЦикла;
	
	ИсключаемыеСтроки = Новый Массив;
	Для Каждого СтрокаНачислений Из Начисления Цикл 
		Если ИсключаемыеСтроки.Найти(СтрокаНачислений) <> Неопределено Тогда 
			Продолжить;
		КонецЕсли;
		Для Каждого ТекущаяСтрока Из Начисления Цикл
			Если СтрокаНачислений = ТекущаяСтрока Тогда
				Продолжить;
			КонецЕсли;
			Если ИсключаемыеСтроки.Найти(ТекущаяСтрока) <> Неопределено Тогда 
				Продолжить;
			КонецЕсли;
			ЗначенияКолонокСовпадают = Истина;
			Для Каждого ИмяКолонки Из ИменаКолонок Цикл 
				Если СтрокаНачислений[ИмяКолонки] <> ТекущаяСтрока[ИмяКолонки] Тогда
					ЗначенияКолонокСовпадают = Ложь;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если Не ЗначенияКолонокСовпадают Тогда 
				Продолжить;
			КонецЕсли;
			Если СтрокаНачислений.Сумма = -ТекущаяСтрока.Сумма Тогда 
				ИсключаемыеСтроки.Добавить(СтрокаНачислений);
				ИсключаемыеСтроки.Добавить(ТекущаяСтрока);
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	ИсключаемыеНачисления = Начисления.Скопировать(ИсключаемыеСтроки);
	Для Каждого ИсключаемаяСтрока Из ИсключаемыеСтроки Цикл 
		Начисления.Удалить(ИсключаемаяСтрока);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("Начисления", Начисления);
	Запрос.УстановитьПараметр("ИсключаемыеНачисления", ИсключаемыеНачисления);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИсключаемыеНачисления.ИдентификаторСтроки КАК ИдентификаторСтроки
		|ПОМЕСТИТЬ ВТИсключаемыеНачисления
		|ИЗ
		|	&ИсключаемыеНачисления КАК ИсключаемыеНачисления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтрокиДокумента,
		|	Начисления.СтатьяФинансирования КАК СтатьяФинансирования,
		|	Начисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	Начисления.СтатьяРасходов КАК СтатьяРасходов,
		|	Начисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	Начисления.Сотрудник КАК Сотрудник,
		|	Начисления.Период КАК Период,
		|	Начисления.ДокументСсылка КАК ДокументСсылка,
		|	Начисления.Сумма КАК Сумма,
		|	Начисления.МесяцНачисления КАК МесяцНачисления,
		|	Начисления.Сторно КАК Сторно
		|ПОМЕСТИТЬ ВТНачисленияОтражаемыеПоБазе
		|ИЗ
		|	&Начисления КАК Начисления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА КАК Поле1
		|ИЗ
		|	ВТНачисленияОтражаемыеПоБазе КАК НачисленияОтражаемыеПоБазе";
	РезультатЗапроса = Запрос.Выполнить();
	
	// Если нет, то создаем пустышку и ничего далее не делаем.
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ДополнитьВТНачисленияОтражаемыеПоБазе(МенеджерВременныхТаблиц);
	ДополнитьИсходнымиЗаписямиДляСторно(МенеджерВременныхТаблиц);
	
	// Выполняем расчет базы для получения расшифровки.
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Начисления.Регистратор КАК Регистратор,
		|	Начисления.НомерСтроки КАК НомерСтроки,
		|	НачисленияОтражаемыеПоБазе.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	НачисленияОтражаемыеПоБазе.ИдентификаторСтрокиДокумента КАК ИдентификаторСтрокиДокумента,
		|	НачисленияОтражаемыеПоБазе.СтатьяФинансирования КАК СтатьяФинансирования,
		|	НачисленияОтражаемыеПоБазе.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	Начисления.ВидРасчета.СтратегияОтраженияВУчете КАК СтратегияОтраженияВУчете,
		|	НачисленияОтражаемыеПоБазе.СтатьяРасходов КАК СтатьяРасходов,
		|	НачисленияОтражаемыеПоБазе.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	НачисленияОтражаемыеПоБазе.Сотрудник КАК Сотрудник,
		|	НачисленияОтражаемыеПоБазе.Период КАК Период,
		|	НачисленияОтражаемыеПоБазе.Сумма КАК Сумма,
		|	НачисленияОтражаемыеПоБазе.МесяцНачисления КАК МесяцНачисления,
		|	НачисленияОтражаемыеПоБазе.Сторно КАК Сторно,
		|	ЕСТЬNULL(ИсходныеЗаписи.ДокументСсылкаСторно, НачисленияОтражаемыеПоБазе.ДокументСсылка) КАК ИсходныйРегистратор,
		|	ЕСТЬNULL(ИсходныеЗаписи.ИдентификаторСтрокиСторно, НачисленияОтражаемыеПоБазе.ИдентификаторСтроки) КАК ИсходныйИдентификаторСтроки
		|ПОМЕСТИТЬ ВТРаспределяемыеСуммыПредварительно
		|ИЗ
		|	ВТНачисленияОтражаемыеПоБазе КАК НачисленияОтражаемыеПоБазе
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрРасчета.Начисления КАК Начисления
		|		ПО НачисленияОтражаемыеПоБазе.ДокументСсылка = Начисления.Регистратор
		|			И НачисленияОтражаемыеПоБазе.ИдентификаторСтроки = Начисления.ИдентификаторСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИсходныеЗаписиДляФиксСторно КАК ИсходныеЗаписи
		|		ПО НачисленияОтражаемыеПоБазе.ДокументСсылка = ИсходныеЗаписи.ДокументСсылка
		|			И НачисленияОтражаемыеПоБазе.ИдентификаторСтроки = ИсходныеЗаписи.ИдентификаторСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РаспределяемыеСуммы.Регистратор КАК Регистратор,
		|	РаспределяемыеСуммы.НомерСтроки КАК НомерСтроки,
		|	ДанныеДокумента.НомерСтроки КАК НомерСтрокиДокумента
		|ПОМЕСТИТЬ ВТИсключаемыеСтрокиОтбор
		|ИЗ
		|	ВТРаспределяемыеСуммыПредварительно КАК РаспределяемыеСуммы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРаспределяемыеСуммыПредварительно КАК ДанныеДокумента
		|		ПО РаспределяемыеСуммы.Сотрудник = ДанныеДокумента.Сотрудник
		|			И РаспределяемыеСуммы.ИдентификаторСтрокиДокумента = ДанныеДокумента.ИсходныйИдентификаторСтроки
		|			И (РаспределяемыеСуммы.ИсходныйРегистратор <> &Регистратор)
		|			И (ДанныеДокумента.ИсходныйРегистратор = &Регистратор)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РаспределяемыеСуммы.Регистратор КАК Регистратор,
		|	РаспределяемыеСуммы.НомерСтроки КАК НомерСтроки,
		|	РаспределяемыеСуммы.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	РаспределяемыеСуммы.Сотрудник КАК Сотрудник,
		|	РаспределяемыеСуммы.Период КАК Период,
		|	РаспределяемыеСуммы.ИсходныйРегистратор КАК ИсходныйРегистратор,
		|	РаспределяемыеСуммы.ИсходныйИдентификаторСтроки КАК ИсходныйИдентификаторСтроки
		|ПОМЕСТИТЬ ВТОтборНачислений
		|ИЗ
		|	ВТРаспределяемыеСуммыПредварительно КАК РаспределяемыеСуммы";
	Запрос.Выполнить();
	
	РасчетЗарплатыРасширенный.СоздатьВТРасчетнаяБазаНачисленийПоВременнойТаблицеКаскадно(МенеджерВременныхТаблиц);
	
	// Удаляем из расчетной базы строки, по которым распределяемые суммы были начислены ранее.
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасчетнаяБаза.Регистратор КАК Регистратор,
		|	РасчетнаяБаза.НомерСтрокиРазрез КАК НомерСтрокиРазрез,
		|	ИсключаемыеСтрокиОтбор.НомерСтрокиДокумента КАК НомерСтроки
		|ПОМЕСТИТЬ ВТИсключаемыеСтроки
		|ИЗ
		|	ВТИсключаемыеСтрокиОтбор КАК ИсключаемыеСтрокиОтбор
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРасчетнаяБаза КАК РасчетнаяБаза
		|		ПО ИсключаемыеСтрокиОтбор.Регистратор = РасчетнаяБаза.Регистратор
		|			И ИсключаемыеСтрокиОтбор.НомерСтроки = РасчетнаяБаза.НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РасчетнаяБаза.Регистратор КАК Регистратор,
		|	РасчетнаяБаза.НомерСтроки КАК НомерСтроки,
		|	РасчетнаяБаза.ИдентификаторСтрокиРазрез КАК ИдентификаторСтрокиРазрез,
		|	РасчетнаяБаза.ПериодРегистрацииРазрез КАК ПериодРегистрацииРазрез,
		|	РасчетнаяБаза.СотрудникРазрез КАК СотрудникРазрез,
		|	РасчетнаяБаза.ВидРасчетаРазрез КАК ВидРасчетаРазрез,
		|	РасчетнаяБаза.РегистраторРазрез КАК РегистраторРазрез,
		|	РасчетнаяБаза.РезультатБаза КАК РезультатБаза
		|ПОМЕСТИТЬ ВТРасчетнаяБазаПредварительно
		|ИЗ
		|	ВТРасчетнаяБаза КАК РасчетнаяБаза
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИсключаемыеСтроки КАК ИсключаемыеСтроки
		|		ПО РасчетнаяБаза.РегистраторРазрез = ИсключаемыеСтроки.Регистратор
		|			И РасчетнаяБаза.НомерСтрокиРазрез = ИсключаемыеСтроки.НомерСтрокиРазрез
		|			И РасчетнаяБаза.НомерСтроки = ИсключаемыеСтроки.НомерСтроки
		|ГДЕ
		|	ИсключаемыеСтроки.НомерСтроки ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТРасчетнаяБаза
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РасчетнаяБазаПредварительно.Регистратор КАК Регистратор,
		|	РасчетнаяБазаПредварительно.НомерСтроки КАК НомерСтроки,
		|	РасчетнаяБазаПредварительно.ИдентификаторСтрокиРазрез КАК ИдентификаторСтрокиРазрез,
		|	РасчетнаяБазаПредварительно.ПериодРегистрацииРазрез КАК ПериодРегистрацииРазрез,
		|	РасчетнаяБазаПредварительно.СотрудникРазрез КАК СотрудникРазрез,
		|	РасчетнаяБазаПредварительно.ВидРасчетаРазрез КАК ВидРасчетаРазрез,
		|	РасчетнаяБазаПредварительно.РегистраторРазрез КАК РегистраторРазрез,
		|	РасчетнаяБазаПредварительно.РезультатБаза КАК РезультатБаза
		|ПОМЕСТИТЬ ВТРасчетнаяБаза
		|ИЗ
		|	ВТРасчетнаяБазаПредварительно КАК РасчетнаяБазаПредварительно
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОтборНачислений КАК ОтборНачислений
		|		ПО РасчетнаяБазаПредварительно.Регистратор = ОтборНачислений.Регистратор
		|			И РасчетнаяБазаПредварительно.НомерСтроки = ОтборНачислений.НомерСтроки
		|			И (ОтборНачислений.ИсходныйРегистратор = &Регистратор)";
	Запрос.Выполнить();
	
	ИменаВТ = Новый Массив;
	ИменаВТ.Добавить("ВТИсключаемыеСтроки");
	ИменаВТ.Добавить("ВТРасчетнаяБазаПредварительно");
	ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, ИменаВТ);
	
	// РК и СН с начислений, не входящих в средний заработок, также не должны входить в средний.
	Если Не ИсключатьВПериодКомандировок Тогда 
		Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 0
			|	0 КАК ИдентификаторСтроки,
			|	0 КАК Сумма
			|ПОМЕСТИТЬ ВТИсключаемыеВПериодКомандировкиСуммы
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	0 КАК ИдентификаторСтроки,
			|	0 КАК Сумма,
			|	ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка) КАК СтатьяФинансирования,
			|	ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка) КАК СпособОтраженияЗарплатыВБухучете,
			|	ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка) КАК СтатьяРасходов
			|ПОМЕСТИТЬ ВТИсключаемыеСуммыПоСтатьямФинансирования";
		Запрос.Выполнить();
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
	
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	РасчетнаяБаза.Регистратор КАК Регистратор,
			|	РасчетнаяБаза.НомерСтроки КАК НомерСтроки,
			|	РасчетнаяБаза.ИдентификаторСтрокиРазрез КАК ИдентификаторСтрокиРазрез,
			|	РасчетнаяБаза.ВидРасчетаРазрез КАК ВидРасчетаРазрез,
			|	РасчетнаяБаза.РегистраторРазрез КАК РегистраторРазрез,
			|	Начисления.СтатьяФинансирования КАК СтатьяФинансирования,
			|	Начисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
			|	Начисления.СтатьяРасходов КАК СтатьяРасходов,
			|	Начисления.Сумма КАК Сумма
			|ПОМЕСТИТЬ ВТРасчетнаяБазаПоСтатьямФинансирования
			|ИЗ
			|	ВТРасчетнаяБаза КАК РасчетнаяБаза
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНачисленияПринимаемые КАК Начисления
			|		ПО РасчетнаяБаза.ИдентификаторСтрокиРазрез = Начисления.ИдентификаторСтроки
			|			И (РасчетнаяБаза.РегистраторРазрез = &Регистратор)
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	РасчетнаяБаза.Регистратор,
			|	РасчетнаяБаза.НомерСтроки,
			|	РасчетнаяБаза.ИдентификаторСтрокиРазрез,
			|	РасчетнаяБаза.ВидРасчетаРазрез,
			|	РасчетнаяБаза.РегистраторРазрез,
			|	НачисленияУдержания.СтатьяФинансирования,
			|	НачисленияУдержания.СпособОтраженияЗарплатыВБухучете,
			|	НачисленияУдержания.СтатьяРасходов,
			|	НачисленияУдержания.Сумма
			|ИЗ
			|	ВТРасчетнаяБаза КАК РасчетнаяБаза
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.БухучетНачисленияУдержанияПоСотрудникам КАК НачисленияУдержания
			|		ПО РасчетнаяБаза.РегистраторРазрез = НачисленияУдержания.Регистратор
			|			И РасчетнаяБаза.СотрудникРазрез = НачисленияУдержания.Сотрудник
			|			И РасчетнаяБаза.ВидРасчетаРазрез = НачисленияУдержания.НачислениеУдержание
			|			И (РасчетнаяБаза.РегистраторРазрез <> &Регистратор)";
		Запрос.Выполнить();
	
	Иначе
	
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	РасчетнаяБаза.Регистратор КАК Регистратор,
			|	РасчетнаяБаза.НомерСтроки КАК НомерСтроки,
			|	РасчетнаяБаза.ИдентификаторСтрокиРазрез КАК ИдентификаторСтрокиРазрез,
			|	РасчетнаяБаза.ВидРасчетаРазрез КАК ВидРасчетаРазрез,
			|	РасчетнаяБаза.РегистраторРазрез КАК РегистраторРазрез,
			|	ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка) КАК СтатьяФинансирования,
			|	ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка) КАК СпособОтраженияЗарплатыВБухучете,
			|	ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка) КАК СтатьяРасходов,
			|	РасчетнаяБаза.РезультатБаза КАК Сумма
			|ПОМЕСТИТЬ ВТРасчетнаяБазаПоСтатьямФинансирования
			|ИЗ
			|	ВТРасчетнаяБаза КАК РасчетнаяБаза";
		Запрос.Выполнить();
		
	КонецЕсли;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасчетнаяБаза.Регистратор КАК Регистратор,
		|	РасчетнаяБаза.РегистраторРазрез КАК РегистраторРазрез,
		|	РасчетнаяБаза.НомерСтроки КАК НомерСтроки,
		|	РасчетнаяБаза.ИдентификаторСтрокиРазрез КАК ИдентификаторСтрокиРазрез,
		|	РасчетнаяБаза.СтатьяФинансирования КАК СтатьяФинансирования,
		|	РасчетнаяБаза.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	РасчетнаяБаза.СтатьяРасходов КАК СтатьяРасходов,
		|	СУММА(РасчетнаяБаза.Сумма) КАК Сумма
		|ПОМЕСТИТЬ ВТРасчетнаяБазаСреднегоЗаработкаПредварительно
		|ИЗ
		|	ВТРасчетнаяБазаПоСтатьямФинансирования КАК РасчетнаяБаза
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления.СреднийЗаработокОбщий КАК НастройкиВидаРасчета
		|		ПО РасчетнаяБаза.ВидРасчетаРазрез = НастройкиВидаРасчета.Ссылка
		|			И (НастройкиВидаРасчета.Значение <> ЗНАЧЕНИЕ(Перечисление.УчетНачисленийВСреднемЗаработкеОбщий.НеВключать))
		|
		|СГРУППИРОВАТЬ ПО
		|	РасчетнаяБаза.Регистратор,
		|	РасчетнаяБаза.РегистраторРазрез,
		|	РасчетнаяБаза.НомерСтроки,
		|	РасчетнаяБаза.ИдентификаторСтрокиРазрез,
		|	РасчетнаяБаза.СтатьяФинансирования,
		|	РасчетнаяБаза.СпособОтраженияЗарплатыВБухучете,
		|	РасчетнаяБаза.СтатьяРасходов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РасчетнаяБаза.Регистратор КАК Регистратор,
		|	РасчетнаяБаза.НомерСтроки КАК НомерСтроки,
		|	РасчетнаяБаза.СтатьяФинансирования КАК СтатьяФинансирования,
		|	РасчетнаяБаза.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	РасчетнаяБаза.СтатьяРасходов КАК СтатьяРасходов,
		|	СУММА(РасчетнаяБаза.Сумма - ЕСТЬNULL(ИсключаемыеСуммы.Сумма, 0)) КАК Сумма
		|ПОМЕСТИТЬ ВТРасчетнаяБазаСреднегоЗаработка
		|ИЗ
		|	ВТРасчетнаяБазаСреднегоЗаработкаПредварительно КАК РасчетнаяБаза
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИсключаемыеСуммыПоСтатьямФинансирования КАК ИсключаемыеСуммы
		|		ПО РасчетнаяБаза.ИдентификаторСтрокиРазрез = ИсключаемыеСуммы.ИдентификаторСтроки
		|			И РасчетнаяБаза.СтатьяФинансирования = ИсключаемыеСуммы.СтатьяФинансирования
		|			И РасчетнаяБаза.СпособОтраженияЗарплатыВБухучете = ИсключаемыеСуммы.СпособОтраженияЗарплатыВБухучете
		|			И РасчетнаяБаза.СтатьяРасходов = ИсключаемыеСуммы.СтатьяРасходов
		|			И (РасчетнаяБаза.РегистраторРазрез = &Регистратор)
		|
		|СГРУППИРОВАТЬ ПО
		|	РасчетнаяБаза.Регистратор,
		|	РасчетнаяБаза.НомерСтроки,
		|	РасчетнаяБаза.СтатьяФинансирования,
		|	РасчетнаяБаза.СпособОтраженияЗарплатыВБухучете,
		|	РасчетнаяБаза.СтатьяРасходов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РасчетнаяБаза.Регистратор КАК Регистратор,
		|	РасчетнаяБаза.НомерСтроки КАК НомерСтроки,
		|	СУММА(РасчетнаяБаза.Сумма) КАК Сумма
		|ПОМЕСТИТЬ ВТРасчетнаяБазаВсего
		|ИЗ
		|	ВТРасчетнаяБазаПоСтатьямФинансирования КАК РасчетнаяБаза
		|
		|СГРУППИРОВАТЬ ПО
		|	РасчетнаяБаза.Регистратор,
		|	РасчетнаяБаза.НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РаспределяемыеСуммыПредварительно.Регистратор КАК Регистратор,
		|	РаспределяемыеСуммыПредварительно.НомерСтроки КАК НомерСтроки,
		|	РаспределяемыеСуммыПредварительно.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	РаспределяемыеСуммыПредварительно.ИсходныйРегистратор КАК ИсходныйРегистратор,
		|	РаспределяемыеСуммыПредварительно.ИсходныйИдентификаторСтроки КАК ИсходныйИдентификаторСтроки,
		|	РаспределяемыеСуммыПредварительно.СтратегияОтраженияВУчете КАК СтратегияОтраженияВУчете,
		|	РаспределяемыеСуммыПредварительно.СтатьяФинансирования КАК СтатьяФинансирования,
		|	РаспределяемыеСуммыПредварительно.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	РаспределяемыеСуммыПредварительно.СтатьяРасходов КАК СтатьяРасходов,
		|	РаспределяемыеСуммыПредварительно.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	РаспределяемыеСуммыПредварительно.Сотрудник КАК Сотрудник,
		|	РаспределяемыеСуммыПредварительно.Период КАК Период,
		|	РаспределяемыеСуммыПредварительно.МесяцНачисления КАК МесяцНачисления,
		|	РаспределяемыеСуммыПредварительно.Сторно КАК Сторно,
		|	ВЫРАЗИТЬ(СУММА(РаспределяемыеСуммыПредварительно.Сумма) КАК ЧИСЛО(15,2)) КАК Сумма
		|ПОМЕСТИТЬ ВТРаспределяемыеСуммы
		|ИЗ
		|	(ВЫБРАТЬ
		|		РаспределяемыеСуммыПредварительно.Регистратор КАК Регистратор,
		|		РаспределяемыеСуммыПредварительно.НомерСтроки КАК НомерСтроки,
		|		РаспределяемыеСуммыПредварительно.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|		РаспределяемыеСуммыПредварительно.ИсходныйРегистратор КАК ИсходныйРегистратор,
		|		РаспределяемыеСуммыПредварительно.ИсходныйИдентификаторСтроки КАК ИсходныйИдентификаторСтроки,
		|		РаспределяемыеСуммыПредварительно.СтратегияОтраженияВУчете КАК СтратегияОтраженияВУчете,
		|		ВЫБОР
		|			КОГДА РаспределяемыеСуммыПредварительно.СтратегияОтраженияВУчете В (ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоДаннымОСотрудникеИЕгоПлановыхНачислениях), ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.КакЗаданоВидуРасчета))
		|				ТОГДА РаспределяемыеСуммыПредварительно.СтатьяФинансирования
		|			ИНАЧЕ РасчетнаяБазаСреднегоЗаработка.СтатьяФинансирования
		|		КОНЕЦ КАК СтатьяФинансирования,
		|		ВЫБОР
		|			КОГДА РаспределяемыеСуммыПредварительно.СтратегияОтраженияВУчете В (ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоДаннымОСотрудникеИЕгоПлановыхНачислениях), ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.КакЗаданоВидуРасчета))
		|				ТОГДА РаспределяемыеСуммыПредварительно.СпособОтраженияЗарплатыВБухучете
		|			ИНАЧЕ РасчетнаяБазаСреднегоЗаработка.СпособОтраженияЗарплатыВБухучете
		|		КОНЕЦ КАК СпособОтраженияЗарплатыВБухучете,
		|		ВЫБОР
		|			КОГДА РаспределяемыеСуммыПредварительно.СтратегияОтраженияВУчете В (ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоДаннымОСотрудникеИЕгоПлановыхНачислениях), ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.КакЗаданоВидуРасчета))
		|				ТОГДА РаспределяемыеСуммыПредварительно.СтатьяРасходов
		|			ИНАЧЕ РасчетнаяБазаСреднегоЗаработка.СтатьяРасходов
		|		КОНЕЦ КАК СтатьяРасходов,
		|		РаспределяемыеСуммыПредварительно.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|		РаспределяемыеСуммыПредварительно.Сотрудник КАК Сотрудник,
		|		РаспределяемыеСуммыПредварительно.Период КАК Период,
		|		ВЫБОР
		|			КОГДА РасчетнаяБазаВсего.Сумма = 0
		|				ТОГДА 0
		|			ИНАЧЕ РаспределяемыеСуммыПредварительно.Сумма * РасчетнаяБазаСреднегоЗаработка.Сумма / РасчетнаяБазаВсего.Сумма
		|		КОНЕЦ КАК Сумма,
		|		РаспределяемыеСуммыПредварительно.МесяцНачисления КАК МесяцНачисления,
		|		РаспределяемыеСуммыПредварительно.Сторно КАК Сторно
		|	ИЗ
		|		ВТРаспределяемыеСуммыПредварительно КАК РаспределяемыеСуммыПредварительно
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРасчетнаяБазаВсего КАК РасчетнаяБазаВсего
		|			ПО РаспределяемыеСуммыПредварительно.Регистратор = РасчетнаяБазаВсего.Регистратор
		|				И РаспределяемыеСуммыПредварительно.НомерСтроки = РасчетнаяБазаВсего.НомерСтроки
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРасчетнаяБазаСреднегоЗаработка КАК РасчетнаяБазаСреднегоЗаработка
		|			ПО РаспределяемыеСуммыПредварительно.Регистратор = РасчетнаяБазаСреднегоЗаработка.Регистратор
		|				И РаспределяемыеСуммыПредварительно.НомерСтроки = РасчетнаяБазаСреднегоЗаработка.НомерСтроки) КАК РаспределяемыеСуммыПредварительно
		|
		|СГРУППИРОВАТЬ ПО
		|	РаспределяемыеСуммыПредварительно.Регистратор,
		|	РаспределяемыеСуммыПредварительно.НомерСтроки,
		|	РаспределяемыеСуммыПредварительно.ИдентификаторСтроки,
		|	РаспределяемыеСуммыПредварительно.ИсходныйРегистратор,
		|	РаспределяемыеСуммыПредварительно.ИсходныйИдентификаторСтроки,
		|	РаспределяемыеСуммыПредварительно.СтратегияОтраженияВУчете,
		|	РаспределяемыеСуммыПредварительно.СтатьяФинансирования,
		|	РаспределяемыеСуммыПредварительно.СпособОтраженияЗарплатыВБухучете,
		|	РаспределяемыеСуммыПредварительно.СтатьяРасходов,
		|	РаспределяемыеСуммыПредварительно.ОблагаетсяЕНВД,
		|	РаспределяемыеСуммыПредварительно.Сотрудник,
		|	РаспределяемыеСуммыПредварительно.Период,
		|	РаспределяемыеСуммыПредварительно.МесяцНачисления,
		|	РаспределяемыеСуммыПредварительно.Сторно";
	
	Запрос.Выполнить();
	
	ИменаВТ = Новый Массив;
	ИменаВТ.Добавить("ВТРасчетнаяБазаСреднегоЗаработкаПредварительно");
	ИменаВТ.Добавить("ВТРасчетнаяБазаСреднегоЗаработка");
	ИменаВТ.Добавить("ВТРасчетнаяБазаВсего");
	ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, ИменаВТ);
	
	// Получив расчетную базу, мы можем понять, из каких начислений состоит результат распределяемых сумм,
	// но для аналогичного отражения его в среднем заработке нужно понять, как именно эти базовые начисления были отражены
	// в среднем заработке.
	
	// Выполняем поиск по ключу Регистратор + Сотрудник + СоставнаяЧасть + ПорядокРасчета + Индексируется и распределяем суммы, 
	// входящие в базу по тому, что удалось получить (т.к. могут быть несколько записей, по статьям финансирования и др.).
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОсновныеЗаписи.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	ОсновныеЗаписи.Сотрудник КАК Сотрудник,
		|	ОсновныеЗаписи.Период КАК Период,
		|	ОсновныеЗаписи.ИсходныйИдентификаторСтроки КАК ИсходныйИдентификаторСтроки,
		|	РасчетнаяБаза.РегистраторРазрез КАК Регистратор,
		|	РасчетнаяБаза.ПериодРегистрацииРазрез КАК ПериодРегистрации,
		|	НастройкиВидаРасчета.ПорядокРасчета КАК ПорядокРасчета,
		|	НастройкиВидаРасчета.Значение КАК СоставнаяЧасть,
		|	НастройкиВидаРасчета.Индексируется КАК Индексируется,
		|	СУММА(РасчетнаяБаза.РезультатБаза - ЕСТЬNULL(ИсключаемыеСуммы.Сумма, 0)) КАК Сумма
		|ПОМЕСТИТЬ ВТРасшифровкаБазы
		|ИЗ
		|	ВТРасчетнаяБаза КАК РасчетнаяБаза
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОтборНачислений КАК ОсновныеЗаписи
		|		ПО (ОсновныеЗаписи.Регистратор = РасчетнаяБаза.Регистратор)
		|			И (ОсновныеЗаписи.НомерСтроки = РасчетнаяБаза.НомерСтроки)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления.СреднийЗаработокОбщий КАК НастройкиВидаРасчета
		|		ПО (НастройкиВидаРасчета.Ссылка = РасчетнаяБаза.ВидРасчетаРазрез)
		|			И (НастройкиВидаРасчета.Значение <> ЗНАЧЕНИЕ(Перечисление.УчетНачисленийВСреднемЗаработкеОбщий.НеВключать))
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИсключаемыеВПериодКомандировкиСуммы КАК ИсключаемыеСуммы
		|		ПО РасчетнаяБаза.ИдентификаторСтрокиРазрез = ИсключаемыеСуммы.ИдентификаторСтроки
		|			И (РасчетнаяБаза.РегистраторРазрез = &Регистратор)
		|
		|СГРУППИРОВАТЬ ПО
		|	ОсновныеЗаписи.ИдентификаторСтроки,
		|	ОсновныеЗаписи.Сотрудник,
		|	ОсновныеЗаписи.Период,
		|	ОсновныеЗаписи.ИсходныйИдентификаторСтроки,
		|	РасчетнаяБаза.РегистраторРазрез,
		|	РасчетнаяБаза.ПериодРегистрацииРазрез,
		|	НастройкиВидаРасчета.ПорядокРасчета,
		|	НастройкиВидаРасчета.Значение,
		|	НастройкиВидаРасчета.Индексируется
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Начисления.Сотрудник КАК Сотрудник,
		|	Начисления.Период КАК Период,
		|	РасшифровкаБазы.Регистратор КАК Регистратор,
		|	РасшифровкаБазы.ПорядокРасчета КАК ПорядокРасчета,
		|	РасшифровкаБазы.СоставнаяЧасть КАК СоставнаяЧасть,
		|	РасшифровкаБазы.Индексируется КАК Индексируется
		|ПОМЕСТИТЬ ВТРаспределяемыеСочетания
		|ИЗ
		|	ВТРасшифровкаБазы КАК РасшифровкаБазы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНачисления КАК Начисления
		|		ПО (Начисления.ИдентификаторСтроки = РасшифровкаБазы.ИсходныйИдентификаторСтроки)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Начисления.Сотрудник КАК Сотрудник,
		|	Начисления.Период КАК Период,
		|	Начисления.ДокументСсылка КАК Регистратор,
		|	Начисления.ПорядокРасчета КАК ПорядокРасчета,
		|	Начисления.СоставнаяЧасть КАК СоставнаяЧасть,
		|	Начисления.Индексируется КАК Индексируется,
		|	Начисления.СтатьяФинансирования КАК СтатьяФинансирования,
		|	Начисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	Начисления.СтатьяРасходов КАК СтатьяРасходов,
		|	Начисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	Начисления.Год КАК Год,
		|	Начисления.ДатаНачалаБазовогоПериода КАК ДатаНачалаБазовогоПериода,
		|	Начисления.КоличествоМесяцев КАК КоличествоМесяцев,
		|	Начисления.Организация КАК Организация,
		|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
		|	Начисления.Сумма КАК Сумма
		|ПОМЕСТИТЬ ВТОснованияРаспределения
		|ИЗ
		|	ВТРаспределяемыеСочетания КАК Сочетания
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНачисления КАК Начисления
		|		ПО (Начисления.ДокументСсылка = Сочетания.Регистратор)
		|			И (Начисления.Сотрудник = Сочетания.Сотрудник)
		|			И (Начисления.Период = Сочетания.Период)
		|			И (Начисления.ПорядокРасчета = Сочетания.ПорядокРасчета)
		|			И (Начисления.СоставнаяЧасть = Сочетания.СоставнаяЧасть)
		|			И (Начисления.Индексируется = Сочетания.Индексируется)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОтборНачислений КАК ОтборНачислений
		|		ПО (Начисления.ДокументСсылка = ОтборНачислений.Регистратор)
		|			И (Начисления.ИдентификаторСтроки = ОтборНачислений.ИсходныйИдентификаторСтроки)
		|ГДЕ
		|	ОтборНачислений.ИсходныйИдентификаторСтроки ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеНачислений.Сотрудник,
		|	ДанныеНачислений.Период,
		|	ДанныеНачислений.Регистратор,
		|	ДанныеНачислений.ПорядокРасчета,
		|	ДанныеНачислений.СоставнаяЧасть,
		|	ДанныеНачислений.Индексируется,
		|	ДанныеНачислений.СтатьяФинансирования,
		|	ДанныеНачислений.СпособОтраженияЗарплатыВБухучете,
		|	ДанныеНачислений.СтатьяРасходов,
		|	ДанныеНачислений.ОблагаетсяЕНВД,
		|	ДанныеНачислений.Год,
		|	ДанныеНачислений.ДатаНачалаБазовогоПериода,
		|	ДанныеНачислений.КоличествоМесяцев,
		|	ДанныеНачислений.Организация,
		|	ДанныеНачислений.ФизическоеЛицо,
		|	ДанныеНачислений.Сумма
		|ИЗ
		|	РегистрНакопления.ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий КАК ДанныеНачислений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРаспределяемыеСочетания КАК Сочетания
		|		ПО ДанныеНачислений.Регистратор = Сочетания.Регистратор
		|			И ДанныеНачислений.Сотрудник = Сочетания.Сотрудник
		|			И ДанныеНачислений.Период = Сочетания.Период
		|			И ДанныеНачислений.ПорядокРасчета = Сочетания.ПорядокРасчета
		|			И ДанныеНачислений.СоставнаяЧасть = Сочетания.СоставнаяЧасть
		|			И ДанныеНачислений.Индексируется = Сочетания.Индексируется
		|			И (ДанныеНачислений.Регистратор <> &Регистратор)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РасшифровкаБазы.ИсходныйИдентификаторСтроки КАК ИсходныйИдентификаторСтроки,
		|	РасшифровкаБазы.Сотрудник КАК Сотрудник,
		|	РасшифровкаБазы.Период КАК Период,
		|	РасшифровкаБазы.Регистратор КАК Регистратор,
		|	РасшифровкаБазы.ПорядокРасчета КАК ПорядокРасчета,
		|	РасшифровкаБазы.СоставнаяЧасть КАК СоставнаяЧасть,
		|	РасшифровкаБазы.Индексируется КАК Индексируется,
		|	СУММА(1) КАК Количество
		|ПОМЕСТИТЬ ВТКоличествоСтрок
		|ИЗ
		|	ВТРасшифровкаБазы КАК РасшифровкаБазы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОснованияРаспределения КАК ОснованияРаспределения
		|		ПО (ОснованияРаспределения.ПорядокРасчета = РасшифровкаБазы.ПорядокРасчета)
		|			И (ОснованияРаспределения.СоставнаяЧасть = РасшифровкаБазы.СоставнаяЧасть)
		|			И (ОснованияРаспределения.Индексируется = РасшифровкаБазы.Индексируется)
		|			И (ОснованияРаспределения.Регистратор = РасшифровкаБазы.Регистратор)
		|			И (ОснованияРаспределения.Сотрудник = РасшифровкаБазы.Сотрудник)
		|			И (ОснованияРаспределения.Период = РасшифровкаБазы.Период)
		|
		|СГРУППИРОВАТЬ ПО
		|	РасшифровкаБазы.ИсходныйИдентификаторСтроки,
		|	РасшифровкаБазы.Сотрудник,
		|	РасшифровкаБазы.Период,
		|	РасшифровкаБазы.Регистратор,
		|	РасшифровкаБазы.ПорядокРасчета,
		|	РасшифровкаБазы.СоставнаяЧасть,
		|	РасшифровкаБазы.Индексируется
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РасшифровкаБазы.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	РасшифровкаБазы.ИсходныйИдентификаторСтроки КАК ИсходныйИдентификаторСтроки,
		|	РасшифровкаБазы.Сотрудник КАК Сотрудник,
		|	РасшифровкаБазы.Период КАК Период,
		|	РасшифровкаБазы.Регистратор КАК Регистратор,
		|	РасшифровкаБазы.ПериодРегистрации КАК ПериодРегистрации,
		|	РасшифровкаБазы.ПорядокРасчета КАК ПорядокРасчета,
		|	РасшифровкаБазы.СоставнаяЧасть КАК СоставнаяЧасть,
		|	РасшифровкаБазы.Индексируется КАК Индексируется,
		|	РасшифровкаБазы.Сумма КАК Сумма,
		|	ЕСТЬNULL(КоличествоСтрок.Количество, 0) КАК КоличествоСтрок
		|ИЗ
		|	ВТРасшифровкаБазы КАК РасшифровкаБазы
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКоличествоСтрок КАК КоличествоСтрок
		|		ПО (КоличествоСтрок.ИсходныйИдентификаторСтроки = РасшифровкаБазы.ИсходныйИдентификаторСтроки)
		|			И (КоличествоСтрок.Регистратор = РасшифровкаБазы.Регистратор)
		|			И (КоличествоСтрок.ПорядокРасчета = РасшифровкаБазы.ПорядокРасчета)
		|			И (КоличествоСтрок.СоставнаяЧасть = РасшифровкаБазы.СоставнаяЧасть)
		|			И (КоличествоСтрок.Индексируется = РасшифровкаБазы.Индексируется)
		|			И (КоличествоСтрок.Сотрудник = РасшифровкаБазы.Сотрудник)
		|			И (КоличествоСтрок.Период = РасшифровкаБазы.Период)
		|
		|УПОРЯДОЧИТЬ ПО
		|	РасшифровкаБазы.Регистратор,
		|	РасшифровкаБазы.ИсходныйИдентификаторСтроки,
		|	РасшифровкаБазы.Сотрудник,
		|	РасшифровкаБазы.Период,
		|	РасшифровкаБазы.ПорядокРасчета,
		|	РасшифровкаБазы.СоставнаяЧасть,
		|	РасшифровкаБазы.Индексируется
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РасшифровкаБазы.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	РасшифровкаБазы.ИсходныйИдентификаторСтроки КАК ИсходныйИдентификаторСтроки,
		|	ОснованияРаспределения.Сотрудник КАК Сотрудник,
		|	ОснованияРаспределения.Период КАК Период,
		|	ОснованияРаспределения.Регистратор КАК Регистратор,
		|	ОснованияРаспределения.ПорядокРасчета КАК ПорядокРасчета,
		|	ОснованияРаспределения.СоставнаяЧасть КАК СоставнаяЧасть,
		|	ОснованияРаспределения.Индексируется КАК Индексируется,
		|	ОснованияРаспределения.СтатьяФинансирования КАК СтатьяФинансирования,
		|	ОснованияРаспределения.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	ОснованияРаспределения.СтатьяРасходов КАК СтатьяРасходов,
		|	ОснованияРаспределения.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	ОснованияРаспределения.Год КАК Год,
		|	ОснованияРаспределения.ДатаНачалаБазовогоПериода КАК ДатаНачалаБазовогоПериода,
		|	ОснованияРаспределения.КоличествоМесяцев КАК КоличествоМесяцев,
		|	ОснованияРаспределения.Организация КАК Организация,
		|	ОснованияРаспределения.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ОснованияРаспределения.Сумма КАК Сумма
		|ИЗ
		|	ВТОснованияРаспределения КАК ОснованияРаспределения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРасшифровкаБазы КАК РасшифровкаБазы
		|		ПО ОснованияРаспределения.ПорядокРасчета = РасшифровкаБазы.ПорядокРасчета
		|			И ОснованияРаспределения.СоставнаяЧасть = РасшифровкаБазы.СоставнаяЧасть
		|			И ОснованияРаспределения.Индексируется = РасшифровкаБазы.Индексируется
		|			И ОснованияРаспределения.Регистратор = РасшифровкаБазы.Регистратор
		|			И ОснованияРаспределения.Сотрудник = РасшифровкаБазы.Сотрудник
		|			И ОснованияРаспределения.Период = РасшифровкаБазы.Период
		|
		|УПОРЯДОЧИТЬ ПО
		|	ОснованияРаспределения.Регистратор,
		|	РасшифровкаБазы.ИсходныйИдентификаторСтроки,
		|	ОснованияРаспределения.Сотрудник,
		|	ОснованияРаспределения.Период,
		|	ОснованияРаспределения.ПорядокРасчета,
		|	ОснованияРаспределения.СоставнаяЧасть,
		|	ОснованияРаспределения.Индексируется";

	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаСумм = РезультатыЗапроса[РезультатыЗапроса.Количество() - 2].Выбрать();
	ВыборкаРасшифровки = РезультатыЗапроса[РезультатыЗапроса.Количество() - 1].Выбрать();
	
	// Выполняем распределение сумм пропорционально базе.
	БазаРаспределения = Новый ТаблицаЗначений;
	БазаРаспределения.Колонки.Добавить("ИдентификаторСтроки", Новый ОписаниеТипов("Число"));
	БазаРаспределения.Колонки.Добавить("ИсходныйИдентификаторСтроки", Новый ОписаниеТипов("Число"));
	БазаРаспределения.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	БазаРаспределения.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	БазаРаспределения.Колонки.Добавить("ПорядокРасчета", Новый ОписаниеТипов("ПеречислениеСсылка.ПорядокРасчетаСреднегоЗаработкаОбщий"));
	БазаРаспределения.Колонки.Добавить("СоставнаяЧасть", Новый ОписаниеТипов("ПеречислениеСсылка.УчетНачисленийВСреднемЗаработкеОбщий"));
	БазаРаспределения.Колонки.Добавить("СтатьяФинансирования", Новый ОписаниеТипов("СправочникСсылка.СтатьиФинансированияЗарплата"));
	БазаРаспределения.Колонки.Добавить("СпособОтраженияЗарплатыВБухучете", Новый ОписаниеТипов("СправочникСсылка.СпособыОтраженияЗарплатыВБухУчете"));
	БазаРаспределения.Колонки.Добавить("СтатьяРасходов", Новый ОписаниеТипов("СправочникСсылка.СтатьиРасходовЗарплата"));
	БазаРаспределения.Колонки.Добавить("ОблагаетсяЕНВД", Новый ОписаниеТипов("Булево"));
	БазаРаспределения.Колонки.Добавить("Индексируется", Новый ОписаниеТипов("Булево"));
	БазаРаспределения.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(15, 2));
	БазаРаспределения.Колонки.Добавить("Год", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(4, 0));
	БазаРаспределения.Колонки.Добавить("ДатаНачалаБазовогоПериода", Новый ОписаниеТипов("Дата"));
	БазаРаспределения.Колонки.Добавить("КоличествоМесяцев", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(3, 0));
	БазаРаспределения.Колонки.Добавить("ФизическоеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	БазаРаспределения.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	БазаРаспределения.Колонки.Добавить("ПериодРегистрации", Новый ОписаниеТипов("Дата"));
	
	Пока ВыборкаСумм.Следующий() Цикл
		Если ВыборкаСумм.КоличествоСтрок = 0 Тогда
			Продолжить;
		КонецЕсли;
		СтрокиРаспределения = Новый Массив;
		КоэффициентыРаспределения = Новый Массив;
		Для Индекс = 1 По ВыборкаСумм.КоличествоСтрок Цикл
			ВыборкаРасшифровки.Следующий();
			НоваяСтрока = БазаРаспределения.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаРасшифровки);
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаСумм);
			СтрокиРаспределения.Добавить(НоваяСтрока);
			КоэффициентыРаспределения.Добавить(ВыборкаРасшифровки.Сумма);
		КонецЦикла;
		Если ВыборкаСумм.КоличествоСтрок < 2 Тогда
			Продолжить;
		КонецЕсли;
		РаспределенныеСуммы = ЗарплатаКадрыКлиентСервер.РаспределитьПропорциональноКоэффициентам(ВыборкаСумм.Сумма, КоэффициентыРаспределения);
		Если РаспределенныеСуммы <> Неопределено Тогда
			Индекс = 0;
			Пока Индекс < СтрокиРаспределения.Количество() Цикл
				СтрокиРаспределения[Индекс].Сумма = РаспределенныеСуммы[Индекс];
				Индекс = Индекс + 1;	
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	ИменаКолонок = "";
	Для Каждого ДанныеКолонки Из БазаРаспределения.Колонки Цикл
		Если ДанныеКолонки.Имя <> "Сумма" Тогда 
			ИменаКолонок = ИменаКолонок + ?(ЗначениеЗаполнено(ИменаКолонок), ", ", "") + ДанныеКолонки.Имя;
		КонецЕсли;
	КонецЦикла;
	
	БазаРаспределения.Свернуть(ИменаКолонок, "Сумма");
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	БазаРаспределения.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	БазаРаспределения.ИсходныйИдентификаторСтроки КАК ИсходныйИдентификаторСтроки,
		|	БазаРаспределения.ПериодРегистрации КАК ПериодРегистрации,
		|	БазаРаспределения.Сотрудник КАК Сотрудник,
		|	БазаРаспределения.Период КАК Период,
		|	БазаРаспределения.ПорядокРасчета КАК ПорядокРасчета,
		|	БазаРаспределения.СоставнаяЧасть КАК СоставнаяЧасть,
		|	БазаРаспределения.Индексируется КАК Индексируется,
		|	БазаРаспределения.СтатьяФинансирования КАК СтатьяФинансирования,
		|	БазаРаспределения.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	БазаРаспределения.СтатьяРасходов КАК СтатьяРасходов,
		|	БазаРаспределения.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	БазаРаспределения.Год КАК Год,
		|	БазаРаспределения.ДатаНачалаБазовогоПериода КАК ДатаНачалаБазовогоПериода,
		|	БазаРаспределения.КоличествоМесяцев КАК КоличествоМесяцев,
		|	БазаРаспределения.Организация КАК Организация,
		|	БазаРаспределения.ФизическоеЛицо КАК ФизическоеЛицо,
		|	БазаРаспределения.Сумма КАК Сумма
		|ПОМЕСТИТЬ ВТБазаРаспределенияПредварительно
		|ИЗ
		|	&БазаРаспределения КАК БазаРаспределения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	БазаРаспределения.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	БазаРаспределения.ИсходныйИдентификаторСтроки КАК ИсходныйИдентификаторСтроки,
		|	БазаРаспределения.ПериодРегистрации КАК ПериодРегистрации,
		|	БазаРаспределения.Сотрудник КАК Сотрудник,
		|	БазаРаспределения.Период КАК Период,
		|	БазаРаспределения.ПорядокРасчета КАК ПорядокРасчета,
		|	БазаРаспределения.СоставнаяЧасть КАК СоставнаяЧасть,
		|	БазаРаспределения.Индексируется КАК Индексируется,
		|	ВЫБОР
		|		КОГДА РаспределяемыеСуммы.СтратегияОтраженияВУчете В (ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоДаннымОСотрудникеИЕгоПлановыхНачислениях), ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.КакЗаданоВидуРасчета))
		|			ТОГДА РаспределяемыеСуммы.СтатьяФинансирования
		|		ИНАЧЕ БазаРаспределения.СтатьяФинансирования
		|	КОНЕЦ КАК СтатьяФинансирования,
		|	ВЫБОР
		|		КОГДА РаспределяемыеСуммы.СтратегияОтраженияВУчете В (ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоДаннымОСотрудникеИЕгоПлановыхНачислениях), ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.КакЗаданоВидуРасчета))
		|			ТОГДА РаспределяемыеСуммы.СпособОтраженияЗарплатыВБухучете
		|		ИНАЧЕ БазаРаспределения.СпособОтраженияЗарплатыВБухучете
		|	КОНЕЦ КАК СпособОтраженияЗарплатыВБухучете,
		|	ВЫБОР
		|		КОГДА РаспределяемыеСуммы.СтратегияОтраженияВУчете В (ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоДаннымОСотрудникеИЕгоПлановыхНачислениях), ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.КакЗаданоВидуРасчета))
		|			ТОГДА РаспределяемыеСуммы.СтатьяРасходов
		|		ИНАЧЕ БазаРаспределения.СтатьяРасходов
		|	КОНЕЦ КАК СтатьяРасходов,
		|	ВЫБОР
		|		КОГДА РаспределяемыеСуммы.СтратегияОтраженияВУчете В (ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоДаннымОСотрудникеИЕгоПлановыхНачислениях), ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.КакЗаданоВидуРасчета))
		|			ТОГДА РаспределяемыеСуммы.ОблагаетсяЕНВД
		|		ИНАЧЕ БазаРаспределения.ОблагаетсяЕНВД
		|	КОНЕЦ КАК ОблагаетсяЕНВД,
		|	БазаРаспределения.Год КАК Год,
		|	БазаРаспределения.ДатаНачалаБазовогоПериода КАК ДатаНачалаБазовогоПериода,
		|	БазаРаспределения.КоличествоМесяцев КАК КоличествоМесяцев,
		|	БазаРаспределения.Организация КАК Организация,
		|	БазаРаспределения.ФизическоеЛицо КАК ФизическоеЛицо,
		|	БазаРаспределения.Сумма КАК Сумма
		|ПОМЕСТИТЬ ВТБазаРаспределения
		|ИЗ
		|	ВТБазаРаспределенияПредварительно КАК БазаРаспределения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРаспределяемыеСуммы КАК РаспределяемыеСуммы
		|		ПО БазаРаспределения.ИсходныйИдентификаторСтроки = РаспределяемыеСуммы.ИсходныйИдентификаторСтроки
		|			И (ВЫБОР
		|				КОГДА РаспределяемыеСуммы.Сторно
		|					ТОГДА РаспределяемыеСуммы.МесяцНачисления > БазаРаспределения.ПериодРегистрации
		|				ИНАЧЕ ИСТИНА
		|			КОНЕЦ)
		|			И (РаспределяемыеСуммы.СтратегияОтраженияВУчете В (ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоДаннымОСотрудникеИЕгоПлановыхНачислениях), ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.КакЗаданоВидуРасчета))
		|				ИЛИ БазаРаспределения.СтатьяФинансирования = РаспределяемыеСуммы.СтатьяФинансирования
		|					И БазаРаспределения.СпособОтраженияЗарплатыВБухучете = РаспределяемыеСуммы.СпособОтраженияЗарплатыВБухучете
		|					И БазаРаспределения.СтатьяРасходов = РаспределяемыеСуммы.СтатьяРасходов
		|					И БазаРаспределения.ОблагаетсяЕНВД = РаспределяемыеСуммы.ОблагаетсяЕНВД)";
	
	Запрос.УстановитьПараметр("БазаРаспределения", БазаРаспределения);
	Запрос.Выполнить();
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	БазаРаспределения.ИсходныйИдентификаторСтроки КАК ИсходныйИдентификаторСтроки,
		|	БазаРаспределения.СтатьяФинансирования КАК СтатьяФинансирования,
		|	БазаРаспределения.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	БазаРаспределения.СтатьяРасходов КАК СтатьяРасходов,
		|	БазаРаспределения.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	СУММА(1) КАК КоличествоСтрок
		|ПОМЕСТИТЬ ВТКоличествоСтрокБазыРаспределения
		|ИЗ
		|	ВТБазаРаспределения КАК БазаРаспределения
		|
		|СГРУППИРОВАТЬ ПО
		|	БазаРаспределения.ИсходныйИдентификаторСтроки,
		|	БазаРаспределения.СтатьяФинансирования,
		|	БазаРаспределения.СпособОтраженияЗарплатыВБухучете,
		|	БазаРаспределения.СтатьяРасходов,
		|	БазаРаспределения.ОблагаетсяЕНВД
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РаспределяемыеСуммы.ИсходныйИдентификаторСтроки КАК ИсходныйИдентификаторСтроки,
		|	РаспределяемыеСуммы.СтатьяФинансирования КАК СтатьяФинансирования,
		|	РаспределяемыеСуммы.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	РаспределяемыеСуммы.СтатьяРасходов КАК СтатьяРасходов,
		|	РаспределяемыеСуммы.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	РаспределяемыеСуммы.Сумма КАК Сумма,
		|	ЕСТЬNULL(КоличествоСтрокБазыРаспределения.КоличествоСтрок, 0) КАК КоличествоСтрок
		|ИЗ
		|	ВТРаспределяемыеСуммы КАК РаспределяемыеСуммы
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКоличествоСтрокБазыРаспределения КАК КоличествоСтрокБазыРаспределения
		|		ПО (КоличествоСтрокБазыРаспределения.ИсходныйИдентификаторСтроки = РаспределяемыеСуммы.ИсходныйИдентификаторСтроки)
		|			И (КоличествоСтрокБазыРаспределения.СтатьяФинансирования = РаспределяемыеСуммы.СтатьяФинансирования)
		|			И (КоличествоСтрокБазыРаспределения.СпособОтраженияЗарплатыВБухучете = РаспределяемыеСуммы.СпособОтраженияЗарплатыВБухучете)
		|			И (КоличествоСтрокБазыРаспределения.СтатьяРасходов = РаспределяемыеСуммы.СтатьяРасходов)
		|			И (КоличествоСтрокБазыРаспределения.ОблагаетсяЕНВД = РаспределяемыеСуммы.ОблагаетсяЕНВД)
		|
		|УПОРЯДОЧИТЬ ПО
		|	РаспределяемыеСуммы.ИсходныйИдентификаторСтроки,
		|	РаспределяемыеСуммы.СтатьяФинансирования,
		|	РаспределяемыеСуммы.СпособОтраженияЗарплатыВБухучете,
		|	РаспределяемыеСуммы.СтатьяРасходов,
		|	РаспределяемыеСуммы.ОблагаетсяЕНВД
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	БазаРаспределения.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	БазаРаспределения.ИсходныйИдентификаторСтроки КАК ИсходныйИдентификаторСтроки,
		|	БазаРаспределения.Сотрудник КАК Сотрудник,
		|	БазаРаспределения.Период КАК Период,
		|	БазаРаспределения.ПорядокРасчета КАК ПорядокРасчета,
		|	БазаРаспределения.СоставнаяЧасть КАК СоставнаяЧасть,
		|	БазаРаспределения.Индексируется КАК Индексируется,
		|	БазаРаспределения.СтатьяФинансирования КАК СтатьяФинансирования,
		|	БазаРаспределения.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	БазаРаспределения.СтатьяРасходов КАК СтатьяРасходов,
		|	БазаРаспределения.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	БазаРаспределения.Год КАК Год,
		|	БазаРаспределения.ДатаНачалаБазовогоПериода КАК ДатаНачалаБазовогоПериода,
		|	БазаРаспределения.КоличествоМесяцев КАК КоличествоМесяцев,
		|	БазаРаспределения.Организация КАК Организация,
		|	БазаРаспределения.ФизическоеЛицо КАК ФизическоеЛицо,
		|	БазаРаспределения.Сумма КАК Сумма
		|ИЗ
		|	ВТБазаРаспределения КАК БазаРаспределения
		|
		|УПОРЯДОЧИТЬ ПО
		|	БазаРаспределения.ИсходныйИдентификаторСтроки,
		|	БазаРаспределения.СтатьяФинансирования,
		|	БазаРаспределения.СпособОтраженияЗарплатыВБухучете,
		|	БазаРаспределения.СтатьяРасходов,
		|	БазаРаспределения.ОблагаетсяЕНВД";
		
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	ИменаВТ = Новый Массив;  
	ИменаВТ.Добавить("ВТРаспределяемыеСочетания"); 
	ИменаВТ.Добавить("ВТОснованияРаспределения"); 
	ИменаВТ.Добавить("ВТКоличествоСтрок");
	ИменаВТ.Добавить("ВТБазаРаспределенияПредварительно");
	ИменаВТ.Добавить("ВТБазаРаспределения"); 
	ИменаВТ.Добавить("ВТКоличествоСтрокБазыРаспределения");
	ИменаВТ.Добавить("ВТРасшифровкаБазы"); 
	ИменаВТ.Добавить("ВТРаспределяемыеСуммы");
	ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, ИменаВТ);
	
	ВыборкаСумм = РезультатыЗапроса[РезультатыЗапроса.Количество() - 2].Выбрать();
	ВыборкаРасшифровки = РезультатыЗапроса[РезультатыЗапроса.Количество() - 1].Выбрать();
	
	// Выполняем распределение сумм пропорционально полученной базе распределения.
	Распределение = БазаРаспределения;
	Распределение.Очистить();
	
	Пока ВыборкаСумм.Следующий() Цикл
		Если ВыборкаСумм.КоличествоСтрок = 0 Тогда
			Продолжить;
		КонецЕсли;
		СтрокиРаспределения = Новый Массив;
		КоэффициентыРаспределения = Новый Массив;
		Для Индекс = 1 По ВыборкаСумм.КоличествоСтрок Цикл
			ВыборкаРасшифровки.НайтиСледующий(ВыборкаСумм.ИсходныйИдентификаторСтроки, "ИсходныйИдентификаторСтроки");
			НоваяСтрока = Распределение.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаРасшифровки);
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаСумм);
			СтрокиРаспределения.Добавить(НоваяСтрока);
			КоэффициентыРаспределения.Добавить(ВыборкаРасшифровки.Сумма);
		КонецЦикла;
		Если ВыборкаСумм.КоличествоСтрок < 2 Тогда
			Продолжить;
		КонецЕсли;
		РаспределенныеСуммы = ЗарплатаКадрыКлиентСервер.РаспределитьПропорциональноКоэффициентам(ВыборкаСумм.Сумма, КоэффициентыРаспределения);
		Если РаспределенныеСуммы <> Неопределено Тогда
			Индекс = 0;
			Пока Индекс < СтрокиРаспределения.Количество() Цикл
				СтрокиРаспределения[Индекс].Сумма = РаспределенныеСуммы[Индекс];
				Индекс = Индекс + 1;	
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Распределение.Свернуть(ИменаКолонок, "Сумма");
	
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ОсновныеЗаписи.ИсходныйИдентификаторСтроки КАК ИдентификаторСтроки
	               |ПОМЕСТИТЬ ВТИдентификаторыРасчетнойБазы
	               |ИЗ
	               |	ВТОтборНачислений КАК ОсновныеЗаписи
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРасчетнаяБаза КАК РасчетнаяБаза
	               |		ПО ОсновныеЗаписи.Регистратор = РасчетнаяБаза.Регистратор
	               |			И ОсновныеЗаписи.НомерСтроки = РасчетнаяБаза.НомерСтроки";
	
	Запрос.Выполнить();
	
	ИменаВТ = Новый Массив;
	ИменаВТ.Добавить("ВТОтборНачислений"); 
	ИменаВТ.Добавить("ВТРасчетнаяБазаПоСтатьямФинансирования");
	ИменаВТ.Добавить("ВТРасчетнаяБаза");
	ИменаВТ.Добавить("ВТРаспределяемыеСуммыПредварительно");
	ИменаВТ.Добавить("ВТИсходныеЗаписиДляФиксСторно");
	ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, ИменаВТ);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Распределение.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	Распределение.ИсходныйИдентификаторСтроки КАК ИсходныйИдентификаторСтроки,
		|	Распределение.Сотрудник КАК Сотрудник,
		|	Распределение.Период КАК Период,
		|	Распределение.ПорядокРасчета КАК ПорядокРасчета,
		|	Распределение.СоставнаяЧасть КАК СоставнаяЧасть,
		|	Распределение.Индексируется КАК Индексируется,
		|	Распределение.СтатьяФинансирования КАК СтатьяФинансирования,
		|	Распределение.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	Распределение.СтатьяРасходов КАК СтатьяРасходов,
		|	Распределение.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	Распределение.Год КАК Год,
		|	Распределение.ДатаНачалаБазовогоПериода КАК ДатаНачалаБазовогоПериода,
		|	Распределение.КоличествоМесяцев КАК КоличествоМесяцев,
		|	Распределение.Организация КАК Организация,
		|	Распределение.ФизическоеЛицо КАК ФизическоеЛицо,
		|	Распределение.Сумма КАК Сумма
		|ПОМЕСТИТЬ ВТРаспределениеПоБазовымНачислениям
		|ИЗ
		|	&Распределение КАК Распределение";
		
	Запрос.УстановитьПараметр("Распределение", Распределение);
	Запрос.Выполнить();
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Начисления.ИдентификаторСтроки,
		|	Начисления.Сотрудник,
		|	Начисления.Период,
		|	Начисления.ПорядокРасчета,
		|	Начисления.СоставнаяЧасть,
		|	Начисления.Индексируется,
		|	Начисления.СтатьяФинансирования,
		|	Начисления.СпособОтраженияЗарплатыВБухучете,
		|	Начисления.СтатьяРасходов,
		|	Начисления.ОблагаетсяЕНВД,
		|	Начисления.Год,
		|	Начисления.ДатаНачалаБазовогоПериода,
		|	Начисления.КоличествоМесяцев,
		|	Начисления.Организация,
		|	Начисления.ФизическоеЛицо,
		|	Начисления.Сумма,
		|	Начисления.*
		|ПОМЕСТИТЬ ВТНачисленияРаспределяемые
		|ИЗ
		|	ВТНачисления КАК Начисления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТНачисления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Начисления.ИдентификаторСтроки,
		|	Начисления.Сотрудник,
		|	Начисления.Период,
		|	Начисления.ПорядокРасчета,
		|	Начисления.СоставнаяЧасть,
		|	Начисления.Индексируется,
		|	Начисления.СтатьяФинансирования,
		|	Начисления.СпособОтраженияЗарплатыВБухучете,
		|	Начисления.СтатьяРасходов,
		|	Начисления.ОблагаетсяЕНВД,
		|	Начисления.Год,
		|	Начисления.ДатаНачалаБазовогоПериода,
		|	Начисления.КоличествоМесяцев,
		|	Начисления.Организация,
		|	Начисления.ФизическоеЛицо,
		|	Начисления.Сумма,
		|	Начисления.*
		|ПОМЕСТИТЬ ВТНачисления
		|ИЗ
		|	ВТНачисленияРаспределяемые КАК Начисления
		|ГДЕ
		|	НЕ Начисления.ИдентификаторСтроки В
		|				(ВЫБРАТЬ
		|					ИдентификаторыРасчетнойБазы.ИдентификаторСтроки
		|				ИЗ
		|					ВТИдентификаторыРасчетнойБазы КАК ИдентификаторыРасчетнойБазы)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Начисления.ИдентификаторСтроки,
		|	Начисления.Сотрудник,
		|	Начисления.Период,
		|	Распределение.ПорядокРасчета,
		|	Распределение.СоставнаяЧасть,
		|	Распределение.Индексируется,
		|	Распределение.СтатьяФинансирования,
		|	Распределение.СпособОтраженияЗарплатыВБухучете,
		|	Распределение.СтатьяРасходов,
		|	Распределение.ОблагаетсяЕНВД,
		|	Распределение.Год,
		|	Распределение.ДатаНачалаБазовогоПериода,
		|	Распределение.КоличествоМесяцев,
		|	Распределение.Организация,
		|	Распределение.ФизическоеЛицо,
		|	Распределение.Сумма,
		|	Начисления.*
		|ИЗ
		|	ВТНачисленияРаспределяемые КАК Начисления
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРаспределениеПоБазовымНачислениям КАК Распределение
		|		ПО (Распределение.ИсходныйИдентификаторСтроки = Начисления.ИдентификаторСтроки)
		|			И (Распределение.СтатьяФинансирования = Начисления.СтатьяФинансирования)
		|			И (Распределение.СпособОтраженияЗарплатыВБухучете = Начисления.СпособОтраженияЗарплатыВБухучете)
		|			И (Распределение.СтатьяРасходов = Начисления.СтатьяРасходов)
		|			И (Распределение.ОблагаетсяЕНВД = Начисления.ОблагаетсяЕНВД)";
		
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ДополнитьВТНачисленияНераспределяемымиСторно(МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	Начисления.ДокументСсылка КАК ДокументСсылка,
		|	Начисления.Начисление КАК Начисление,
		|	Начисления.Период КАК Период,
		|	Начисления.ДатаДействия КАК ДатаДействия,
		|	Начисления.Сотрудник КАК Сотрудник,
		|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
		|	Начисления.Организация КАК Организация,
		|	Начисления.ПорядокРасчета КАК ПорядокРасчета,
		|	Начисления.СоставнаяЧасть КАК СоставнаяЧасть,
		|	Начисления.СтатьяФинансирования КАК СтатьяФинансирования,
		|	Начисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	Начисления.СтатьяРасходов КАК СтатьяРасходов,
		|	Начисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	Начисления.Индексируется КАК Индексируется,
		|	Начисления.Год КАК Год,
		|	Начисления.КоличествоМесяцев КАК КоличествоМесяцев,
		|	Начисления.ДатаНачалаБазовогоПериода КАК ДатаНачалаБазовогоПериода,
		|	Начисления.Сумма КАК Сумма,
		|	Начисления.РанееНачислено КАК РанееНачислено,
		|	Начисления.ПериодДействия КАК ПериодДействия,
		|	Начисления.ДатаНачала КАК ДатаНачала,
		|	Начисления.ДатаОкончания КАК ДатаОкончания,
		|	Начисления.МесяцНачисления КАК МесяцНачисления,
		|	Начисления.НомерСтроки КАК НомерСтроки,
		|	Начисления.НачалоБазовогоПериода КАК НачалоБазовогоПериода,
		|	Начисления.ОкончаниеБазовогоПериода КАК ОкончаниеБазовогоПериода,
		|	Начисления.Сторно КАК Сторно,
		|	Начисления.ИсходныйДокумент КАК ИсходныйДокумент,
		|	Начисления.СторнируемыйДокумент КАК СторнируемыйДокумент,
		|	Начисления.ПоказательПремирования КАК ПоказательПремирования,
		|	Начисления.ФиксСторно КАК ФиксСторно,
		|	Начисления.ДатаПриема КАК ДатаПриема
		|ПОМЕСТИТЬ ВТНачисленияПредварительно
		|ИЗ
		|	ВТНачисления КАК Начисления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТНачисления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НачисленияПредварительно.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	НачисленияПредварительно.ДокументСсылка КАК ДокументСсылка,
		|	НачисленияПредварительно.Начисление КАК Начисление,
		|	НачисленияПредварительно.Период КАК Период,
		|	НачисленияПредварительно.ДатаДействия КАК ДатаДействия,
		|	НачисленияПредварительно.Сотрудник КАК Сотрудник,
		|	НачисленияПредварительно.ФизическоеЛицо КАК ФизическоеЛицо,
		|	НачисленияПредварительно.Организация КАК Организация,
		|	НачисленияПредварительно.ПорядокРасчета КАК ПорядокРасчета,
		|	НачисленияПредварительно.СоставнаяЧасть КАК СоставнаяЧасть,
		|	НачисленияПредварительно.СтатьяФинансирования КАК СтатьяФинансирования,
		|	НачисленияПредварительно.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	НачисленияПредварительно.СтатьяРасходов КАК СтатьяРасходов,
		|	НачисленияПредварительно.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	НачисленияПредварительно.Индексируется КАК Индексируется,
		|	НачисленияПредварительно.Год КАК Год,
		|	НачисленияПредварительно.КоличествоМесяцев КАК КоличествоМесяцев,
		|	НачисленияПредварительно.ДатаНачалаБазовогоПериода КАК ДатаНачалаБазовогоПериода,
		|	НачисленияПредварительно.Сумма КАК Сумма,
		|	НачисленияПредварительно.РанееНачислено КАК РанееНачислено,
		|	НачисленияПредварительно.ПериодДействия КАК ПериодДействия,
		|	НачисленияПредварительно.ДатаНачала КАК ДатаНачала,
		|	НачисленияПредварительно.ДатаОкончания КАК ДатаОкончания,
		|	НачисленияПредварительно.МесяцНачисления КАК МесяцНачисления,
		|	НачисленияПредварительно.НомерСтроки КАК НомерСтроки,
		|	НачисленияПредварительно.НачалоБазовогоПериода КАК НачалоБазовогоПериода,
		|	НачисленияПредварительно.ОкончаниеБазовогоПериода КАК ОкончаниеБазовогоПериода,
		|	НачисленияПредварительно.Сторно КАК Сторно,
		|	НачисленияПредварительно.ИсходныйДокумент КАК ИсходныйДокумент,
		|	НачисленияПредварительно.СторнируемыйДокумент КАК СторнируемыйДокумент,
		|	НачисленияПредварительно.ПоказательПремирования КАК ПоказательПремирования,
		|	НачисленияПредварительно.ФиксСторно КАК ФиксСторно,
		|	НачисленияПредварительно.ДатаПриема КАК ДатаПриема
		|ПОМЕСТИТЬ ВТНачисления
		|ИЗ
		|	ВТНачисленияПредварительно КАК НачисленияПредварительно
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СторноСтрокиРаспределяемыхНачислений.ИдентификаторСтроки,
		|	СторноСтрокиРаспределяемыхНачислений.ДокументСсылка,
		|	СторноСтрокиРаспределяемыхНачислений.Начисление,
		|	СторноСтрокиРаспределяемыхНачислений.Период,
		|	СторноСтрокиРаспределяемыхНачислений.ДатаДействия,
		|	СторноСтрокиРаспределяемыхНачислений.Сотрудник,
		|	СторноСтрокиРаспределяемыхНачислений.ФизическоеЛицо,
		|	СторноСтрокиРаспределяемыхНачислений.Организация,
		|	СторноСтрокиРаспределяемыхНачислений.ПорядокРасчета,
		|	СторноСтрокиРаспределяемыхНачислений.СоставнаяЧасть,
		|	СторноСтрокиРаспределяемыхНачислений.СтатьяФинансирования,
		|	СторноСтрокиРаспределяемыхНачислений.СпособОтраженияЗарплатыВБухучете,
		|	СторноСтрокиРаспределяемыхНачислений.СтатьяРасходов,
		|	СторноСтрокиРаспределяемыхНачислений.ОблагаетсяЕНВД,
		|	СторноСтрокиРаспределяемыхНачислений.Индексируется,
		|	СторноСтрокиРаспределяемыхНачислений.Год,
		|	СторноСтрокиРаспределяемыхНачислений.КоличествоМесяцев,
		|	СторноСтрокиРаспределяемыхНачислений.ДатаНачалаБазовогоПериода,
		|	СторноСтрокиРаспределяемыхНачислений.СуммаСторно,
		|	СторноСтрокиРаспределяемыхНачислений.РанееНачислено,
		|	СторноСтрокиРаспределяемыхНачислений.ПериодДействия,
		|	СторноСтрокиРаспределяемыхНачислений.ДатаНачала,
		|	СторноСтрокиРаспределяемыхНачислений.ДатаОкончания,
		|	СторноСтрокиРаспределяемыхНачислений.МесяцНачисления,
		|	СторноСтрокиРаспределяемыхНачислений.НомерСтроки,
		|	СторноСтрокиРаспределяемыхНачислений.НачалоБазовогоПериода,
		|	СторноСтрокиРаспределяемыхНачислений.ОкончаниеБазовогоПериода,
		|	СторноСтрокиРаспределяемыхНачислений.Сторно,
		|	СторноСтрокиРаспределяемыхНачислений.ИсходныйДокумент,
		|	СторноСтрокиРаспределяемыхНачислений.СторнируемыйДокумент,
		|	СторноСтрокиРаспределяемыхНачислений.ПоказательПремирования,
		|	СторноСтрокиРаспределяемыхНачислений.ФиксСторно,
		|	СторноСтрокиРаспределяемыхНачислений.ДатаПриема
		|ИЗ
		|	ВТСторноСтрокиРаспределяемыхНачислений КАК СторноСтрокиРаспределяемыхНачислений
		|ГДЕ
		|	НЕ 1 В
		|				(ВЫБРАТЬ ПЕРВЫЕ 1
		|					1
		|				ИЗ
		|					ВТНачисленияПредварительно КАК НачисленияПредварительно
		|				ГДЕ
		|					НачисленияПредварительно.ИдентификаторСтроки = СторноСтрокиРаспределяемыхНачислений.ИдентификаторСтроки)";
	
	Запрос.Выполнить();
	
КонецПроцедуры

#КонецОбласти

#Область ВспомогательныеПроцедурыИФункцииПолученияДанныхСреднегоЗаработка

Процедура НайтиГраницыПериодаРасчетаОбщегоСреднегоЗаработка(ТаблицаДанных, МинимальнаяДата, МаксимальнаяДата)
	
	МинимальнаяДата		= Дата(1, 1, 1);
	МаксимальнаяДата	= Дата(1, 1, 1);
	
	Для Каждого СтрокаТаблицы Из ТаблицаДанных Цикл
		Если Не ЗначениеЗаполнено(МинимальнаяДата) Тогда
			МинимальнаяДата = СтрокаТаблицы.НачалоПериодаРасчетаСреднего;
		КонецЕсли;
		МинимальнаяДата		= Мин(МинимальнаяДата, СтрокаТаблицы.НачалоПериодаРасчетаСреднего);
		МаксимальнаяДата	= Макс(МаксимальнаяДата, СтрокаТаблицы.ОкончаниеПериодаРасчетаСреднего);
	КонецЦикла;
	
КонецПроцедуры

Процедура СоздатьВТПериодыРасчетаСреднегоСотрудников(МенеджерВременныхТаблиц, ТаблицаСотрудники, ОтборМесяцев = Неопределено)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Премии", Перечисления.УчетНачисленийВСреднемЗаработкеОбщий.Премии());
	Запрос.УстановитьПараметр("ПериодыРасчетаСреднегоСотрудников", ТаблицаСотрудники);
	Запрос.УстановитьПараметр("ОтборМесяцев", ОтборМесяцев);
	Запрос.УстановитьПараметр("ПоВсемМесяцам", ОтборМесяцев = Неопределено);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПериодыРасчета.Сотрудник КАК Сотрудник,
		|	ПериодыРасчета.ДатаНачалаСобытия КАК ДатаНачалаСобытия,
		|	ПериодыРасчета.НачалоПериодаРасчетаСреднего КАК НачалоПериодаРасчетаСреднего,
		|	ПериодыРасчета.ОкончаниеПериодаРасчетаСреднего КАК ОкончаниеПериодаРасчетаСреднего,
		|	ПериодыРасчета.ПорядокРасчета КАК ПорядокРасчета
		|ПОМЕСТИТЬ ВТПериодыРасчетаСреднегоСотрудников
		|ИЗ
		|	&ПериодыРасчетаСреднегоСотрудников КАК ПериодыРасчета
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Месяцы.Период КАК Месяц
		|ПОМЕСТИТЬ ВТМесяцы
		|ИЗ
		|	ВТПериоды КАК Месяцы
		|ГДЕ
		|	(Месяцы.Период В (&ОтборМесяцев)
		|			ИЛИ &ПоВсемМесяцам)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПериодыРасчетаСреднегоСотрудников.Сотрудник КАК Сотрудник,
		|	ПериодыРасчетаСреднегоСотрудников.ПорядокРасчета КАК ПорядокРасчета,
		|	Месяцы.Месяц КАК Месяц
		|ПОМЕСТИТЬ ВТМесяцыРасчетаСотрудников
		|ИЗ
		|	ВТПериодыРасчетаСреднегоСотрудников КАК ПериодыРасчетаСреднегоСотрудников
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТМесяцы КАК Месяцы
		|		ПО ПериодыРасчетаСреднегоСотрудников.НачалоПериодаРасчетаСреднего <= Месяцы.Месяц
		|			И ПериодыРасчетаСреднегоСотрудников.ОкончаниеПериодаРасчетаСреднего >= Месяцы.Месяц";
		
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ДанныеОНачисленияхСреднегоЗаработка(МенеджерВременныхТаблиц, ДатаНачала, ДатаОкончания, ИсключаемыйРегистратор = Неопределено, УчитыватьКорректировки = Истина)
	
	Результат = Новый Структура("ДанныеОНачислениях, ДанныеОПремиях");
	
	// Формирование данных для анализа премий и исключения дублирующих.
	СоздатьВТПолныеСуммыПремий(МенеджерВременныхТаблиц, ИсключаемыйРегистратор);
	СоздатьВТДанныеОПремиях(МенеджерВременныхТаблиц, ИсключаемыйРегистратор);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	Запрос.УстановитьПараметр("ИсключаемыйРегистратор", ИсключаемыйРегистратор);
	Запрос.УстановитьПараметр("УчитыватьКорректировки", УчитыватьКорректировки);
	Запрос.УстановитьПараметр("ГодовыеПремии", Перечисления.УчетНачисленийВСреднемЗаработкеОбщий.ГодовыеПремии());
	
	// Общий заработок, без учета годовых премий.
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОтборГодовыхПремий.Сотрудник КАК Сотрудник,
		|	ГОД(ДОБАВИТЬКДАТЕ(ОтборГодовыхПремий.ДатаНачалаСобытия, ГОД, -1)) КАК ГодовыеПремииЗаГод,
		|	ОтборГодовыхПремий.ПорядокРасчета КАК ПорядокРасчета
		|ПОМЕСТИТЬ ВТОтборГодовыхПремий
		|ИЗ
		|	ВТПериодыРасчетаСреднегоСотрудников КАК ОтборГодовыхПремий
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВТОтборГодовыхПремий.Сотрудник КАК Сотрудник
		|ПОМЕСТИТЬ ВТОтборСотрудников
		|ИЗ
		|	ВТОтборГодовыхПремий КАК ВТОтборГодовыхПремий
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Период КАК Период,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Регистратор КАК Регистратор,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.НомерСтроки КАК НомерСтроки
		|ПОМЕСТИТЬ ВТ_ОтборДляРегистра_ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий
		|ИЗ
		|	ВТОтборСотрудников КАК ВТОтборСотрудников
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий КАК ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий
		|		ПО ВТОтборСотрудников.Сотрудник = ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Сотрудник
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Период,
		|	Регистратор,
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Период КАК Период,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Регистратор КАК Регистратор,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Сотрудник КАК Сотрудник,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.ПорядокРасчета КАК ПорядокРасчета,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.СоставнаяЧасть КАК СоставнаяЧасть,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.СтатьяФинансирования КАК СтатьяФинансирования,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.СтатьяРасходов КАК СтатьяРасходов,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Индексируется КАК Индексируется,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Год КАК Год,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.ДатаНачалаБазовогоПериода КАК ДатаНачалаБазовогоПериода,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.КоличествоМесяцев КАК КоличествоМесяцев,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Организация КАК Организация,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.ИсходныйДокумент КАК ИсходныйДокумент,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.ПоказательПремирования КАК ПоказательПремирования,
		|	СУММА(ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Сумма) КАК Сумма
		|ПОМЕСТИТЬ ВТ_ДанныеРегистра_ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий
		|ИЗ
		|	ВТ_ОтборДляРегистра_ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий КАК ВТ_ОтборДляРегистра_ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий КАК ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий
		|		ПО ВТ_ОтборДляРегистра_ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Период = ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Период
		|			И ВТ_ОтборДляРегистра_ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Регистратор = ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Регистратор
		|			И ВТ_ОтборДляРегистра_ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.НомерСтроки = ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.НомерСтроки
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Период,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Регистратор,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Сотрудник,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.ПорядокРасчета,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.СоставнаяЧасть,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.СтатьяФинансирования,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.СпособОтраженияЗарплатыВБухучете,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.СтатьяРасходов,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.ОблагаетсяЕНВД,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Индексируется,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.ДатаНачалаБазовогоПериода,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Организация,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.ФизическоеЛицо,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.ИсходныйДокумент,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.ПоказательПремирования,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Год,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.КоличествоМесяцев
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Сотрудник,
		|	Регистратор,
		|	Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеРегистра.Период КАК Период,
		|	ДанныеРегистра.Сотрудник КАК Сотрудник,
		|	ДанныеРегистра.Начисление КАК Начисление,
		|	ДанныеРегистра.ПорядокРасчета КАК ПорядокРасчета,
		|	ДанныеРегистра.СоставнаяЧасть КАК СоставнаяЧасть,
		|	ДанныеРегистра.СтатьяФинансирования КАК СтатьяФинансирования,
		|	ДанныеРегистра.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	ДанныеРегистра.СтатьяРасходов КАК СтатьяРасходов,
		|	ДанныеРегистра.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	ДанныеРегистра.Индексируется КАК Индексируется,
		|	ДанныеРегистра.Год КАК Год,
		|	ДанныеРегистра.ДатаНачалаБазовогоПериода КАК ДатаНачалаБазовогоПериода,
		|	ДанныеРегистра.КоличествоМесяцев КАК КоличествоМесяцев,
		|	ДанныеРегистра.ПереносДанных КАК ПереносДанных,
		|	ДанныеРегистра.ПоказательПремирования КАК ПоказательПремирования,
		|	ДанныеРегистра.ИсключеннаяЧасть КАК ИсключеннаяЧасть,
		|	ДанныеРегистра.КлючНачисления КАК КлючНачисления,
		|	СУММА(ДанныеРегистра.Сумма) КАК Сумма
		|ПОМЕСТИТЬ ВТДанныеДляРасчетаСреднего
		|ИЗ
		|	(ВЫБРАТЬ
		|		НАЧАЛОПЕРИОДА(ДанныеРегистра.Период, МЕСЯЦ) КАК Период,
		|		ДанныеРегистра.Сотрудник КАК Сотрудник,
		|		ЕСТЬNULL(ДанныеПоНачислениям.Начисление, ЗНАЧЕНИЕ(ПланВидовРасчета.Начисления.ПустаяСсылка)) КАК Начисление,
		|		ДанныеРегистра.ПорядокРасчета КАК ПорядокРасчета,
		|		ДанныеРегистра.СоставнаяЧасть КАК СоставнаяЧасть,
		|		ДанныеРегистра.СтатьяФинансирования КАК СтатьяФинансирования,
		|		ДанныеРегистра.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|		ДанныеРегистра.СтатьяРасходов КАК СтатьяРасходов,
		|		ДанныеРегистра.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|		ДанныеРегистра.Индексируется КАК Индексируется,
		|		ДанныеРегистра.Год КАК Год,
		|		ДанныеРегистра.ДатаНачалаБазовогоПериода КАК ДатаНачалаБазовогоПериода,
		|		ДанныеРегистра.КоличествоМесяцев КАК КоличествоМесяцев,
		|		ДанныеРегистра.ПоказательПремирования КАК ПоказательПремирования,
		|		ВЫБОР
		|			КОГДА ДанныеРегистра.Регистратор ССЫЛКА Документ.ПереносДанных
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ КАК ПереносДанных,
		|		ЕСТЬNULL(ДанныеОПремиях.Дублирующая, ЛОЖЬ) КАК ИсключеннаяЧасть,
		|		ЕСТЬNULL(ДанныеОПремиях.КлючНачисления, 0) КАК КлючНачисления,
		|		ЕСТЬNULL(ДанныеПоНачислениям.Сумма, ДанныеРегистра.Сумма) КАК Сумма
		|	ИЗ
		|		ВТ_ДанныеРегистра_ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий КАК ДанныеРегистра
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеОПремиях КАК ДанныеОПремиях
		|			ПО ДанныеРегистра.Регистратор = ДанныеОПремиях.Регистратор
		|				И ДанныеРегистра.Сотрудник = ДанныеОПремиях.Сотрудник
		|				И ДанныеРегистра.ПорядокРасчета = ДанныеОПремиях.ПорядокРасчета
		|				И ДанныеРегистра.СоставнаяЧасть = ДанныеОПремиях.СоставнаяЧасть
		|				И (ДанныеОПремиях.ДатаНачалаБазовогоПериода = НАЧАЛОПЕРИОДА(ДанныеРегистра.ДатаНачалаБазовогоПериода, МЕСЯЦ))
		|				И ДанныеРегистра.Период = ДанныеОПремиях.Период
		|				И ДанныеРегистра.Год = ДанныеОПремиях.Год
		|				И ДанныеРегистра.КоличествоМесяцев = ДанныеОПремиях.КоличествоМесяцев
		|				И ДанныеРегистра.Организация = ДанныеОПремиях.Организация
		|				И ДанныеРегистра.ФизическоеЛицо = ДанныеОПремиях.ФизическоеЛицо
		|				И ДанныеРегистра.ПоказательПремирования = ДанныеОПремиях.ПоказательПремирования
		|				И ДанныеРегистра.Сумма = ДанныеОПремиях.Сумма
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям КАК ДанныеПоНачислениям
		|			ПО ДанныеРегистра.Регистратор = ДанныеПоНачислениям.Регистратор
		|				И ДанныеРегистра.Период = ДанныеПоНачислениям.Период
		|				И ДанныеРегистра.Сотрудник = ДанныеПоНачислениям.Сотрудник
		|				И ДанныеРегистра.ПорядокРасчета = ДанныеПоНачислениям.ПорядокРасчета
		|				И ДанныеРегистра.СоставнаяЧасть = ДанныеПоНачислениям.СоставнаяЧасть
		|				И ДанныеРегистра.СтатьяФинансирования = ДанныеПоНачислениям.СтатьяФинансирования
		|				И ДанныеРегистра.СпособОтраженияЗарплатыВБухучете = ДанныеПоНачислениям.СпособОтраженияЗарплатыВБухучете
		|				И ДанныеРегистра.СтатьяРасходов = ДанныеПоНачислениям.СтатьяРасходов
		|				И ДанныеРегистра.ОблагаетсяЕНВД = ДанныеПоНачислениям.ОблагаетсяЕНВД
		|				И ДанныеРегистра.Индексируется = ДанныеПоНачислениям.Индексируется
		|				И ДанныеРегистра.Год = ДанныеПоНачислениям.Год
		|				И ДанныеРегистра.ДатаНачалаБазовогоПериода = ДанныеПоНачислениям.ДатаНачалаБазовогоПериода
		|				И ДанныеРегистра.КоличествоМесяцев = ДанныеПоНачислениям.КоличествоМесяцев
		|				И ДанныеРегистра.Организация = ДанныеПоНачислениям.Организация
		|				И ДанныеРегистра.ФизическоеЛицо = ДанныеПоНачислениям.ФизическоеЛицо
		|				И ДанныеРегистра.ИсходныйДокумент = ДанныеПоНачислениям.ИсходныйДокумент
		|				И ДанныеРегистра.ПоказательПремирования = ДанныеПоНачислениям.ПоказательПремирования
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОтборГодовыхПремий КАК Отбор
		|			ПО ДанныеРегистра.Сотрудник = Отбор.Сотрудник
		|				И ДанныеРегистра.ПорядокРасчета = Отбор.ПорядокРасчета
		|				И (ДанныеРегистра.Регистратор <> &ИсключаемыйРегистратор)
		|				И (ВЫБОР
		|					КОГДА ДанныеРегистра.СоставнаяЧасть В (&ГодовыеПремии)
		|						ТОГДА Отбор.ГодовыеПремииЗаГод = ДанныеРегистра.Год
		|					ИНАЧЕ ДанныеРегистра.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
		|				КОНЕЦ)) КАК ДанныеРегистра
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеРегистра.Период,
		|	ДанныеРегистра.Сотрудник,
		|	ДанныеРегистра.Начисление,
		|	ДанныеРегистра.ПорядокРасчета,
		|	ДанныеРегистра.СоставнаяЧасть,
		|	ДанныеРегистра.СтатьяФинансирования,
		|	ДанныеРегистра.СпособОтраженияЗарплатыВБухучете,
		|	ДанныеРегистра.СтатьяРасходов,
		|	ДанныеРегистра.ОблагаетсяЕНВД,
		|	ДанныеРегистра.Индексируется,
		|	ДанныеРегистра.Год,
		|	ДанныеРегистра.ДатаНачалаБазовогоПериода,
		|	ДанныеРегистра.КоличествоМесяцев,
		|	ДанныеРегистра.ПереносДанных,
		|	ДанныеРегистра.ПоказательПремирования,
		|	ДанныеРегистра.ИсключеннаяЧасть,
		|	ДанныеРегистра.КлючНачисления
		|
		|ИМЕЮЩИЕ
		|	СУММА(ДанныеРегистра.Сумма) <> 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТОтборСотрудников
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_ОтборДляРегистра_ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_ДанныеРегистра_ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий";

	Запрос.Выполнить();
	
	// Данные начислений (основной заработок).
	Запрос.Текст =
		"ВЫБРАТЬ
		|	1 КАК Приоритет,
		|	ДанныеНачислений.Сотрудник КАК Сотрудник,
		|	ДанныеНачислений.Период КАК Период,
		|	ДанныеНачислений.Начисление КАК Начисление,
		|	ДанныеНачислений.СоставнаяЧасть КАК СоставнаяЧасть,
		|	ДанныеНачислений.ПорядокРасчета КАК ПорядокРасчета,
		|	ДанныеНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
		|	ДанныеНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	ДанныеНачислений.СтатьяРасходов КАК СтатьяРасходов,
		|	ДанныеНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	ДанныеНачислений.Год КАК Год,
		|	ДанныеНачислений.ДатаНачалаБазовогоПериода КАК ДатаНачалаБазовогоПериода,
		|	ДанныеНачислений.КоличествоМесяцев КАК КоличествоМесяцев,
		|	ДанныеНачислений.Индексируется КАК Индексируется,
		|	ДанныеНачислений.Сумма КАК Сумма,
		|	ДанныеНачислений.ИсключеннаяЧасть КАК ИсключеннаяЧасть,
		|	ДанныеНачислений.КлючНачисления КАК КлючНачисления,
		|	ЗНАЧЕНИЕ(Перечисление.ИсточникиДанныхДляРасчетаСреднегоЗаработка.Начисления) КАК Источник
		|ПОМЕСТИТЬ ВТНачисленияСреднегоЗаработка
		|ИЗ
		|	ВТДанныеДляРасчетаСреднего КАК ДанныеНачислений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТМесяцыРасчетаСотрудников КАК МесяцыРасчетаСотрудников
		|		ПО (МесяцыРасчетаСотрудников.Месяц = ДанныеНачислений.Период)
		|			И (МесяцыРасчетаСотрудников.ПорядокРасчета = ДанныеНачислений.ПорядокРасчета)
		|			И (МесяцыРасчетаСотрудников.Сотрудник = ДанныеНачислений.Сотрудник)
		|			И (ДанныеНачислений.ПереносДанных = ЛОЖЬ)
		|			И (НЕ ДанныеНачислений.СоставнаяЧасть В (&ГодовыеПремии))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	2,
		|	СведенияНачислений.Сотрудник,
		|	СведенияНачислений.Месяц,
		|	СведенияНачислений.Начисление,
		|	СведенияНачислений.СоставнаяЧасть,
		|	СведенияНачислений.ПорядокРасчета,
		|	СведенияНачислений.СтатьяФинансирования,
		|	СведенияНачислений.СпособОтраженияЗарплатыВБухучете,
		|	СведенияНачислений.СтатьяРасходов,
		|	СведенияНачислений.ОблагаетсяЕНВД,
		|	СведенияНачислений.Год,
		|	СведенияНачислений.ДатаНачалаБазовогоПериода,
		|	СведенияНачислений.КоличествоМесяцев,
		|	СведенияНачислений.Индексируется,
		|	СведенияНачислений.Сумма,
		|	ЛОЖЬ,
		|	0,
		|	ЗНАЧЕНИЕ(Перечисление.ИсточникиДанныхДляРасчетаСреднегоЗаработка.СведенияДоНачалаЭксплуатации)
		|ИЗ
		|	РегистрСведений.СведенияОНачисленияхДляРасчетаСреднегоЗаработкаОбщий КАК СведенияНачислений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТМесяцыРасчетаСотрудников КАК МесяцыРасчетаСотрудников
		|		ПО (МесяцыРасчетаСотрудников.Месяц = СведенияНачислений.Месяц)
		|			И (МесяцыРасчетаСотрудников.ПорядокРасчета = СведенияНачислений.ПорядокРасчета)
		|			И (МесяцыРасчетаСотрудников.Сотрудник = СведенияНачислений.Сотрудник)
		|			И (НЕ СведенияНачислений.СоставнаяЧасть В (&ГодовыеПремии))
		|			И (СведенияНачислений.Сумма <> 0)
		|			И (&УчитыватьКорректировки = ИСТИНА
		|				ИЛИ НЕ ИСТИНА В
		|						(ВЫБРАТЬ ПЕРВЫЕ 1
		|							ИСТИНА
		|						ИЗ
		|							ВТДанныеДляРасчетаСреднего КАК ДанныеНачислений
		|						ГДЕ
		|							ДанныеНачислений.Сотрудник = СведенияНачислений.Сотрудник
		|							И ДанныеНачислений.Период = СведенияНачислений.Месяц
		|							И ДанныеНачислений.ПорядокРасчета = СведенияНачислений.ПорядокРасчета
		|							И НЕ ДанныеНачислений.СоставнаяЧасть В (&ГодовыеПремии)))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	3,
		|	ДанныеНачислений.Сотрудник,
		|	ДанныеНачислений.Период,
		|	ДанныеНачислений.Начисление,
		|	ДанныеНачислений.СоставнаяЧасть,
		|	ДанныеНачислений.ПорядокРасчета,
		|	ДанныеНачислений.СтатьяФинансирования,
		|	ДанныеНачислений.СпособОтраженияЗарплатыВБухучете,
		|	ДанныеНачислений.СтатьяРасходов,
		|	ДанныеНачислений.ОблагаетсяЕНВД,
		|	ДанныеНачислений.Год,
		|	ДанныеНачислений.ДатаНачалаБазовогоПериода,
		|	ДанныеНачислений.КоличествоМесяцев,
		|	ДанныеНачислений.Индексируется,
		|	ДанныеНачислений.Сумма,
		|	ДанныеНачислений.ИсключеннаяЧасть,
		|	ДанныеНачислений.КлючНачисления,
		|	ЗНАЧЕНИЕ(Перечисление.ИсточникиДанныхДляРасчетаСреднегоЗаработка.Начисления)
		|ИЗ
		|	ВТДанныеДляРасчетаСреднего КАК ДанныеНачислений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТМесяцыРасчетаСотрудников КАК МесяцыРасчетаСотрудников
		|		ПО (МесяцыРасчетаСотрудников.Месяц = ДанныеНачислений.Период)
		|			И (МесяцыРасчетаСотрудников.ПорядокРасчета = ДанныеНачислений.ПорядокРасчета)
		|			И (МесяцыРасчетаСотрудников.Сотрудник = ДанныеНачислений.Сотрудник)
		|			И (ДанныеНачислений.ПереносДанных = ИСТИНА)
		|			И (НЕ ДанныеНачислений.СоставнаяЧасть В (&ГодовыеПремии))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Начисления.Сотрудник КАК Сотрудник,
		|	Начисления.Период КАК Период,
		|	МИНИМУМ(Начисления.Приоритет) КАК Приоритет
		|ПОМЕСТИТЬ ВТНачисленияСреднегоЗаработкаМинимальныйПриоритет
		|ИЗ
		|	ВТНачисленияСреднегоЗаработка КАК Начисления
		|
		|СГРУППИРОВАТЬ ПО
		|	Начисления.Сотрудник,
		|	Начисления.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеНачислений.Сотрудник КАК Сотрудник,
		|	ДанныеНачислений.ПорядокРасчета КАК ПорядокРасчета,
		|	ДанныеНачислений.Период КАК Период,
		|	ДанныеНачислений.Начисление КАК Начисление,
		|	ДанныеНачислений.СоставнаяЧасть КАК СоставнаяЧасть,
		|	ДанныеНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
		|	ДанныеНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	ДанныеНачислений.СтатьяРасходов КАК СтатьяРасходов,
		|	ДанныеНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	ДанныеНачислений.ДатаНачалаБазовогоПериода КАК ДатаНачалаБазовогоПериода,
		|	ДанныеНачислений.Индексируется КАК Индексируется,
		|	ДанныеНачислений.Источник КАК Источник,
		|	ДанныеНачислений.Год КАК Год,
		|	ДанныеНачислений.КоличествоМесяцев КАК КоличествоМесяцев,
		|	ДанныеНачислений.ИсключеннаяЧасть КАК ИсключеннаяЧасть,
		|	ДанныеНачислений.КлючНачисления КАК КлючНачисления,
		|	СУММА(ДанныеНачислений.Сумма) КАК Сумма
		|ИЗ
		|	(ВЫБРАТЬ
		|		ДанныеНачислений.Сотрудник КАК Сотрудник,
		|		ДанныеНачислений.ПорядокРасчета КАК ПорядокРасчета,
		|		ДанныеНачислений.Период КАК Период,
		|		ДанныеНачислений.Начисление КАК Начисление,
		|		ДанныеНачислений.СоставнаяЧасть КАК СоставнаяЧасть,
		|		ДанныеНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
		|		ДанныеНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|		ДанныеНачислений.СтатьяРасходов КАК СтатьяРасходов,
		|		ДанныеНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|		ДанныеНачислений.Год КАК Год,
		|		ДанныеНачислений.ДатаНачалаБазовогоПериода КАК ДатаНачалаБазовогоПериода,
		|		ДанныеНачислений.КоличествоМесяцев КАК КоличествоМесяцев,
		|		ДанныеНачислений.Индексируется КАК Индексируется,
		|		ДанныеНачислений.Источник КАК Источник,
		|		ДанныеНачислений.ИсключеннаяЧасть КАК ИсключеннаяЧасть,
		|		ДанныеНачислений.КлючНачисления КАК КлючНачисления,
		|		ДанныеНачислений.Сумма КАК Сумма
		|	ИЗ
		|		ВТНачисленияСреднегоЗаработка КАК ДанныеНачислений
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНачисленияСреднегоЗаработкаМинимальныйПриоритет КАК МинимальныйПриоритет
		|			ПО (МинимальныйПриоритет.Сотрудник = ДанныеНачислений.Сотрудник)
		|				И (МинимальныйПриоритет.Период = ДанныеНачислений.Период)
		|				И (МинимальныйПриоритет.Приоритет = ДанныеНачислений.Приоритет)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ДанныеНачислений.Сотрудник,
		|		ДанныеНачислений.ПорядокРасчета,
		|		ДанныеНачислений.Период,
		|		ДанныеНачислений.Начисление,
		|		ДанныеНачислений.СоставнаяЧасть,
		|		ДанныеНачислений.СтатьяФинансирования,
		|		ДанныеНачислений.СпособОтраженияЗарплатыВБухучете,
		|		ДанныеНачислений.СтатьяРасходов,
		|		ДанныеНачислений.ОблагаетсяЕНВД,
		|		ДанныеНачислений.Год,
		|		ДанныеНачислений.ДатаНачалаБазовогоПериода,
		|		ДанныеНачислений.КоличествоМесяцев,
		|		ДанныеНачислений.Индексируется,
		|		ДанныеНачислений.Источник,
		|		ДанныеНачислений.ИсключеннаяЧасть,
		|		ДанныеНачислений.КлючНачисления,
		|		ДанныеНачислений.Сумма
		|	ИЗ
		|		ВТНачисленияСреднегоЗаработка КАК ДанныеНачислений
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНачисленияСреднегоЗаработкаМинимальныйПриоритет КАК МинимальныйПриоритет
		|			ПО (МинимальныйПриоритет.Сотрудник = ДанныеНачислений.Сотрудник)
		|				И (МинимальныйПриоритет.Период = ДанныеНачислений.Период)
		|				И (МинимальныйПриоритет.Приоритет = 1)
		|				И (ДанныеНачислений.Приоритет = 3)) КАК ДанныеНачислений
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеНачислений.Сотрудник,
		|	ДанныеНачислений.ПорядокРасчета,
		|	ДанныеНачислений.Период,
		|	ДанныеНачислений.Начисление,
		|	ДанныеНачислений.СоставнаяЧасть,
		|	ДанныеНачислений.СтатьяФинансирования,
		|	ДанныеНачислений.СпособОтраженияЗарплатыВБухучете,
		|	ДанныеНачислений.СтатьяРасходов,
		|	ДанныеНачислений.ОблагаетсяЕНВД,
		|	ДанныеНачислений.ДатаНачалаБазовогоПериода,
		|	ДанныеНачислений.Индексируется,
		|	ДанныеНачислений.Источник,
		|	ДанныеНачислений.Год,
		|	ДанныеНачислений.КоличествоМесяцев,
		|	ДанныеНачислений.ИсключеннаяЧасть,
		|	ДанныеНачислений.КлючНачисления
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДанныеНачислений.Сотрудник,
		|	ДанныеНачислений.Период,
		|	ДанныеНачислений.СоставнаяЧасть,
		|	ДанныеНачислений.Индексируется УБЫВ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеОПремиях.Период КАК Период,
		|	ДанныеОПремиях.Регистратор КАК Регистратор,
		|	ДанныеОПремиях.Сотрудник КАК Сотрудник,
		|	ДанныеОПремиях.ПорядокРасчета КАК ПорядокРасчета,
		|	ДанныеОПремиях.Начисление КАК Начисление,
		|	ДанныеОПремиях.СоставнаяЧасть КАК СоставнаяЧасть,
		|	ДанныеОПремиях.Год КАК Год,
		|	ДанныеОПремиях.ДатаНачалаБазовогоПериода КАК ДатаНачалаБазовогоПериода,
		|	ДанныеОПремиях.КоличествоМесяцев КАК КоличествоМесяцев,
		|	ДанныеОПремиях.Организация КАК Организация,
		|	ДанныеОПремиях.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ДанныеОПремиях.ПоказательПремирования КАК ПоказательПремирования,
		|	ДанныеОПремиях.Сумма КАК Сумма,
		|	ДанныеОПремиях.Дублирующая КАК Дублирующая,
		|	ДанныеОПремиях.Дублирующая КАК Исключенная,
		|	ДанныеОПремиях.ЭтоПерерасчет КАК ЭтоПерерасчет,
		|	ДанныеОПремиях.КлючНачисления КАК КлючНачисления
		|ИЗ
		|	ВТДанныеОПремиях КАК ДанныеОПремиях";

	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаНачислений = РезультатЗапроса[2].Выбрать();
	ДанныеНачислений = ПустаяТаблицаНачисленийСреднийЗаработокОбщий();
	Пока ВыборкаНачислений.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ДанныеНачислений.Добавить(), ВыборкаНачислений);
	КонецЦикла;
	
	ВыборкаПремий = РезультатЗапроса[3].Выбрать();
	ДанныеОПремиях = ПустаяТаблицаПремий();
	Пока ВыборкаПремий.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ДанныеОПремиях.Добавить(), ВыборкаПремий);
	КонецЦикла;
	
	// Годовые премии.
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	1 КАК Приоритет,
		|	ГодовыеПремии.Сотрудник КАК Сотрудник,
		|	ГодовыеПремии.Начисление КАК Начисление,
		|	ГодовыеПремии.СоставнаяЧасть КАК СоставнаяЧасть,
		|	ГодовыеПремии.ПорядокРасчета КАК ПорядокРасчета,
		|	ГодовыеПремии.СтатьяФинансирования КАК СтатьяФинансирования,
		|	ГодовыеПремии.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	ГодовыеПремии.СтатьяРасходов КАК СтатьяРасходов,
		|	ГодовыеПремии.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	ГодовыеПремии.Год КАК Год,
		|	ГодовыеПремии.Период КАК Период,
		|	ГодовыеПремии.Индексируется КАК Индексируется,
		|	ГодовыеПремии.ДатаНачалаБазовогоПериода КАК ДатаНачалаБазовогоПериода,
		|	ГодовыеПремии.Сумма КАК Сумма,
		|	ЗНАЧЕНИЕ(Перечисление.ИсточникиДанныхДляРасчетаСреднегоЗаработка.Начисления) КАК Источник
		|ПОМЕСТИТЬ ВТГодовыеПремии
		|ИЗ
		|	ВТДанныеДляРасчетаСреднего КАК ГодовыеПремии
		|ГДЕ
		|	ГодовыеПремии.СоставнаяЧасть В(&ГодовыеПремии)
		|	И ГодовыеПремии.ПереносДанных = ЛОЖЬ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	2,
		|	СведенияНачислений.Сотрудник,
		|	ЗНАЧЕНИЕ(ПланВидовРасчета.Начисления.ПустаяСсылка),
		|	СведенияНачислений.СоставнаяЧасть,
		|	СведенияНачислений.ПорядокРасчета,
		|	СведенияНачислений.СтатьяФинансирования,
		|	СведенияНачислений.СпособОтраженияЗарплатыВБухучете,
		|	СведенияНачислений.СтатьяРасходов,
		|	СведенияНачислений.ОблагаетсяЕНВД,
		|	СведенияНачислений.Год,
		|	СведенияНачислений.Месяц,
		|	СведенияНачислений.Индексируется,
		|	СведенияНачислений.ДатаНачалаБазовогоПериода,
		|	СведенияНачислений.Сумма,
		|	ЗНАЧЕНИЕ(Перечисление.ИсточникиДанныхДляРасчетаСреднегоЗаработка.СведенияДоНачалаЭксплуатации)
		|ИЗ
		|	РегистрСведений.СведенияОНачисленияхДляРасчетаСреднегоЗаработкаОбщий КАК СведенияНачислений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОтборГодовыхПремий КАК ОтборГодовыхПремий
		|		ПО (ОтборГодовыхПремий.Сотрудник = СведенияНачислений.Сотрудник)
		|			И (ОтборГодовыхПремий.ПорядокРасчета = СведенияНачислений.ПорядокРасчета)
		|			И (ОтборГодовыхПремий.ГодовыеПремииЗаГод = СведенияНачислений.Год)
		|			И (СведенияНачислений.СоставнаяЧасть В (&ГодовыеПремии))
		|			И (СведенияНачислений.Сумма <> 0)
		|			И (&УчитыватьКорректировки = ИСТИНА
		|				ИЛИ НЕ ИСТИНА В
		|						(ВЫБРАТЬ ПЕРВЫЕ 1
		|							ИСТИНА
		|						ИЗ
		|							ВТДанныеДляРасчетаСреднего КАК ДанныеНачислений
		|						ГДЕ
		|							ДанныеНачислений.Сотрудник = СведенияНачислений.Сотрудник
		|							И ДанныеНачислений.Период = СведенияНачислений.Месяц
		|							И ДанныеНачислений.ПорядокРасчета = СведенияНачислений.ПорядокРасчета
		|							И ДанныеНачислений.СоставнаяЧасть В (&ГодовыеПремии)))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	3,
		|	ГодовыеПремии.Сотрудник,
		|	ГодовыеПремии.Начисление,
		|	ГодовыеПремии.СоставнаяЧасть,
		|	ГодовыеПремии.ПорядокРасчета,
		|	ГодовыеПремии.СтатьяФинансирования,
		|	ГодовыеПремии.СпособОтраженияЗарплатыВБухучете,
		|	ГодовыеПремии.СтатьяРасходов,
		|	ГодовыеПремии.ОблагаетсяЕНВД,
		|	ГодовыеПремии.Год,
		|	ГодовыеПремии.Период,
		|	ГодовыеПремии.Индексируется,
		|	ГодовыеПремии.ДатаНачалаБазовогоПериода,
		|	ГодовыеПремии.Сумма,
		|	ЗНАЧЕНИЕ(Перечисление.ИсточникиДанныхДляРасчетаСреднегоЗаработка.Начисления)
		|ИЗ
		|	ВТДанныеДляРасчетаСреднего КАК ГодовыеПремии
		|ГДЕ
		|	ГодовыеПремии.СоставнаяЧасть В(&ГодовыеПремии)
		|	И ГодовыеПремии.ПереносДанных = ИСТИНА
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ГодовыеПремии.Сотрудник КАК Сотрудник,
		|	ГодовыеПремии.Год КАК Год,
		|	МИНИМУМ(ГодовыеПремии.Приоритет) КАК Приоритет
		|ПОМЕСТИТЬ ВТГодовыеПремииМинимальныйПриоритет
		|ИЗ
		|	ВТГодовыеПремии КАК ГодовыеПремии
		|
		|СГРУППИРОВАТЬ ПО
		|	ГодовыеПремии.Сотрудник,
		|	ГодовыеПремии.Год
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ГодовыеПремии.Сотрудник КАК Сотрудник,
		|	ГодовыеПремии.ПорядокРасчета КАК ПорядокРасчета,
		|	ГодовыеПремии.Год КАК Год,
		|	ГодовыеПремии.Период КАК Период,
		|	ГодовыеПремии.Начисление КАК Начисление,
		|	ГодовыеПремии.СоставнаяЧасть КАК СоставнаяЧасть,
		|	ГодовыеПремии.СтатьяФинансирования КАК СтатьяФинансирования,
		|	ГодовыеПремии.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	ГодовыеПремии.СтатьяРасходов КАК СтатьяРасходов,
		|	ГодовыеПремии.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	ГодовыеПремии.Индексируется КАК Индексируется,
		|	ГодовыеПремии.ДатаНачалаБазовогоПериода КАК ДатаНачалаБазовогоПериода,
		|	ГодовыеПремии.Источник КАК Источник,
		|	СУММА(ГодовыеПремии.Сумма) КАК Сумма
		|ИЗ
		|	(ВЫБРАТЬ
		|		ГодовыеПремии.Сотрудник КАК Сотрудник,
		|		ГодовыеПремии.ПорядокРасчета КАК ПорядокРасчета,
		|		ГодовыеПремии.Начисление КАК Начисление,
		|		ГодовыеПремии.СоставнаяЧасть КАК СоставнаяЧасть,
		|		ГодовыеПремии.СтатьяФинансирования КАК СтатьяФинансирования,
		|		ГодовыеПремии.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|		ГодовыеПремии.СтатьяРасходов КАК СтатьяРасходов,
		|		ГодовыеПремии.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|		ГодовыеПремии.Год КАК Год,
		|		ГодовыеПремии.Период КАК Период,
		|		ГодовыеПремии.Индексируется КАК Индексируется,
		|		ГодовыеПремии.ДатаНачалаБазовогоПериода КАК ДатаНачалаБазовогоПериода,
		|		ГодовыеПремии.Источник КАК Источник,
		|		ГодовыеПремии.Сумма КАК Сумма
		|	ИЗ
		|		ВТГодовыеПремии КАК ГодовыеПремии
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТГодовыеПремииМинимальныйПриоритет КАК МинимальныйПриоритет
		|			ПО (МинимальныйПриоритет.Сотрудник = ГодовыеПремии.Сотрудник)
		|				И (МинимальныйПриоритет.Год = ГодовыеПремии.Год)
		|				И (МинимальныйПриоритет.Приоритет = ГодовыеПремии.Приоритет)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ГодовыеПремии.Сотрудник,
		|		ГодовыеПремии.ПорядокРасчета,
		|		ГодовыеПремии.Начисление,
		|		ГодовыеПремии.СоставнаяЧасть,
		|		ГодовыеПремии.СтатьяФинансирования,
		|		ГодовыеПремии.СпособОтраженияЗарплатыВБухучете,
		|		ГодовыеПремии.СтатьяРасходов,
		|		ГодовыеПремии.ОблагаетсяЕНВД,
		|		ГодовыеПремии.Год,
		|		ГодовыеПремии.Период,
		|		ГодовыеПремии.Индексируется,
		|		ГодовыеПремии.ДатаНачалаБазовогоПериода,
		|		ГодовыеПремии.Источник,
		|		ГодовыеПремии.Сумма
		|	ИЗ
		|		ВТГодовыеПремии КАК ГодовыеПремии
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТГодовыеПремииМинимальныйПриоритет КАК МинимальныйПриоритет
		|			ПО (МинимальныйПриоритет.Сотрудник = ГодовыеПремии.Сотрудник)
		|				И (МинимальныйПриоритет.Год = ГодовыеПремии.Год)
		|				И (МинимальныйПриоритет.Приоритет = 1)
		|				И (ГодовыеПремии.Приоритет = 3)) КАК ГодовыеПремии
		|
		|СГРУППИРОВАТЬ ПО
		|	ГодовыеПремии.Сотрудник,
		|	ГодовыеПремии.ПорядокРасчета,
		|	ГодовыеПремии.Начисление,
		|	ГодовыеПремии.СоставнаяЧасть,
		|	ГодовыеПремии.СтатьяФинансирования,
		|	ГодовыеПремии.СпособОтраженияЗарплатыВБухучете,
		|	ГодовыеПремии.СтатьяРасходов,
		|	ГодовыеПремии.ОблагаетсяЕНВД,
		|	ГодовыеПремии.Индексируется,
		|	ГодовыеПремии.ДатаНачалаБазовогоПериода,
		|	ГодовыеПремии.Источник,
		|	ГодовыеПремии.Год,
		|	ГодовыеПремии.Период
		|
		|УПОРЯДОЧИТЬ ПО
		|	ГодовыеПремии.Сотрудник,
		|	ГодовыеПремии.Год,
		|	ГодовыеПремии.СоставнаяЧасть,
		|	ГодовыеПремии.Индексируется УБЫВ";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ДанныеНачислений.Добавить(), Выборка);
	КонецЦикла;
	
	Результат.ДанныеОНачислениях = ДанныеНачислений;
	Результат.ДанныеОПремиях = ДанныеОПремиях;
	
	Возврат Результат;
	
КонецФункции

Функция ДанныеОтработанногоВремени(МенеджерВременныхТаблиц, ДатаНачала, ДатаОкончания, ИсключаемыйРегистратор, УчитыватьКорректировки)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СотрудникиМесяцы.Сотрудник,
		|	МАКСИМУМ(КОНЕЦПЕРИОДА(СотрудникиМесяцы.Месяц, МЕСЯЦ)) КАК Период
		|ПОМЕСТИТЬ ВТПериодыКадровыхДанных
		|ИЗ
		|	ВТМесяцыРасчетаСотрудников КАК СотрудникиМесяцы
		|
		|СГРУППИРОВАТЬ ПО
		|	СотрудникиМесяцы.Сотрудник";
	
	Запрос.Выполнить();
	
	ОписательВременныхТаблиц = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(
		Запрос.МенеджерВременныхТаблиц,
		"ВТПериодыКадровыхДанных");
	КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(ОписательВременныхТаблиц, Истина, "ДатаПриема");
	
	Запрос.УстановитьПараметр("ОграничиватьНачалоРасчетногоПериода", ПолучитьФункциональнуюОпцию("ОграничиватьНачалоРасчетногоПериодаСреднегоЗаработкаДатойПриема"));
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	Запрос.УстановитьПараметр("ИсключаемыйРегистратор", ИсключаемыйРегистратор);
	Запрос.УстановитьПараметр("УчитыватьКорректировки", УчитыватьКорректировки);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СотрудникиМесяцы.Сотрудник КАК Сотрудник,
		|	ВЫБОР
		|		КОГДА &ОграничиватьНачалоРасчетногоПериода
		|			ТОГДА ВЫБОР
		|					КОГДА ЕСТЬNULL(КадровыеДанные.ДатаПриема, СотрудникиМесяцы.Месяц) <= СотрудникиМесяцы.Месяц
		|						ТОГДА СотрудникиМесяцы.Месяц
		|					ИНАЧЕ ЕСТЬNULL(КадровыеДанные.ДатаПриема, СотрудникиМесяцы.Месяц)
		|				КОНЕЦ
		|		ИНАЧЕ СотрудникиМесяцы.Месяц
		|	КОНЕЦ КАК ДатаНачала,
		|	КОНЕЦПЕРИОДА(СотрудникиМесяцы.Месяц, МЕСЯЦ) КАК ДатаОкончания
		|ПОМЕСТИТЬ ВТФильтрДанныеПроизводственногоКалендаря
		|ИЗ
		|	ВТМесяцыРасчетаСотрудников КАК СотрудникиМесяцы
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанныеСотрудников КАК КадровыеДанные
		|		ПО СотрудникиМесяцы.Сотрудник = КадровыеДанные.Сотрудник
		|ГДЕ
		|	(НЕ &ОграничиватьНачалоРасчетногоПериода
		|			ИЛИ НАЧАЛОПЕРИОДА(ЕСТЬNULL(КадровыеДанные.ДатаПриема, СотрудникиМесяцы.Месяц), МЕСЯЦ) <= СотрудникиМесяцы.Месяц)";
	
	Запрос.Выполнить();
	
	ОписаниеФильтра = УчетРабочегоВремениРасширенный.ОписаниеФильтраВТДанныеПроизводственногоКалендаряПоСотрудникам();
	ОписаниеФильтра.ИмяВТ = "ВТФильтрДанныеПроизводственногоКалендаря";
	УчетРабочегоВремениРасширенный.СоздатьВТДанныеПроизводственногоКалендаряПоСотрудникам(МенеджерВременныхТаблиц, ОписаниеФильтра);
	
	ИменаУдаляемыхТаблиц = Новый Массив;
	ИменаУдаляемыхТаблиц.Добавить("ВТПериодыКадровыхДанных");
	ИменаУдаляемыхТаблиц.Добавить("ВТКадровыеДанныеСотрудников");
	ИменаУдаляемыхТаблиц.Добавить("ВТФильтрДанныеПроизводственногоКалендаря");
	ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, ИменаУдаляемыхТаблиц);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДанныеВремени.Период,
		|	ДанныеВремени.Сотрудник,
		|	ДанныеВремени.ПорядокРасчета,
		|	ДанныеВремени.ПереносДанных,
		|	СУММА(ДанныеВремени.ОтработаноДней) КАК ОтработаноДней,
		|	СУММА(ДанныеВремени.ОтработаноЧасов) КАК ОтработаноЧасов,
		|	СУММА(ДанныеВремени.ОтработаноДнейШестидневка) КАК ОтработаноДнейШестидневка,
		|	СУММА(ДанныеВремени.ОтработаноДнейПятидневка) КАК ОтработаноДнейПятидневка,
		|	СУММА(ДанныеВремени.ОтработаноЧасовПятидневка) КАК ОтработаноЧасовПятидневка,
		|	СУММА(ДанныеВремени.ОтработаноДнейКалендарных) КАК ОтработаноДнейКалендарных
		|ПОМЕСТИТЬ ВТНакопленияВремени
		|ИЗ
		|	(ВЫБРАТЬ
		|		ДанныеВремени.Период КАК Период,
		|		ДанныеВремени.Сотрудник КАК Сотрудник,
		|		ДанныеВремени.ПорядокРасчета КАК ПорядокРасчета,
		|		ВЫБОР
		|			КОГДА ДанныеВремени.Регистратор ССЫЛКА Документ.ПереносДанных
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ КАК ПереносДанных,
		|		ДанныеВремени.ОтработаноДней КАК ОтработаноДней,
		|		ДанныеВремени.ОтработаноЧасов КАК ОтработаноЧасов,
		|		ДанныеВремени.ОтработаноДнейШестидневка КАК ОтработаноДнейШестидневка,
		|		ДанныеВремени.ОтработаноДнейПятидневка КАК ОтработаноДнейПятидневка,
		|		ДанныеВремени.ОтработаноЧасовПятидневка КАК ОтработаноЧасовПятидневка,
		|		ДанныеВремени.ОтработаноДнейКалендарных КАК ОтработаноДнейКалендарных
		|	ИЗ
		|		РегистрНакопления.ДанныеОВремениДляРасчетаСреднегоОбщий КАК ДанныеВремени
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПериодыРасчетаСреднегоСотрудников КАК ПериодыРасчета
		|			ПО (ПериодыРасчета.Сотрудник = ДанныеВремени.Сотрудник)
		|				И (ПериодыРасчета.ПорядокРасчета = ДанныеВремени.ПорядокРасчета)
		|				И (ДанныеВремени.Период МЕЖДУ &ДатаНачала И &ДатаОкончания)
		|				И (ДанныеВремени.Регистратор <> &ИсключаемыйРегистратор)) КАК ДанныеВремени
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеВремени.Период,
		|	ДанныеВремени.Сотрудник,
		|	ДанныеВремени.ПорядокРасчета,
		|	ДанныеВремени.ПереносДанных
		|
		|ИМЕЮЩИЕ
		|	(СУММА(ДанныеВремени.ОтработаноДней) <> 0
		|		ИЛИ СУММА(ДанныеВремени.ОтработаноЧасов) <> 0
		|		ИЛИ СУММА(ДанныеВремени.ОтработаноДнейШестидневка) <> 0
		|		ИЛИ СУММА(ДанныеВремени.ОтработаноДнейПятидневка) <> 0
		|		ИЛИ СУММА(ДанныеВремени.ОтработаноЧасовПятидневка) <> 0
		|		ИЛИ СУММА(ДанныеВремени.ОтработаноДнейКалендарных) <> 0)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	1 КАК Приоритет,
		|	ДанныеВремени.Период КАК Период,
		|	ДанныеВремени.Сотрудник,
		|	ДанныеВремени.ПорядокРасчета,
		|	ДанныеВремени.ОтработаноДней,
		|	ДанныеВремени.ОтработаноЧасов,
		|	ДанныеВремени.ОтработаноДнейШестидневка,
		|	ДанныеВремени.ОтработаноДнейПятидневка,
		|	ДанныеВремени.ОтработаноЧасовПятидневка,
		|	ДанныеВремени.ОтработаноДнейКалендарных,
		|	ЗНАЧЕНИЕ(Перечисление.ИсточникиДанныхДляРасчетаСреднегоЗаработка.Начисления) КАК Источник
		|ПОМЕСТИТЬ ВТВремяДляРасчетаСреднего
		|ИЗ
		|	ВТНакопленияВремени КАК ДанныеВремени
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТМесяцыРасчетаСотрудников КАК СотрудникиМесяцы
		|		ПО (СотрудникиМесяцы.Сотрудник = ДанныеВремени.Сотрудник)
		|			И (СотрудникиМесяцы.Месяц = ДанныеВремени.Период)
		|			И (СотрудникиМесяцы.ПорядокРасчета = ДанныеВремени.ПорядокРасчета)
		|			И (ДанныеВремени.ПереносДанных = ЛОЖЬ)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	2,
		|	СведенияВремени.Месяц,
		|	СведенияВремени.Сотрудник,
		|	СведенияВремени.ПорядокРасчета,
		|	СведенияВремени.ОтработаноДней,
		|	СведенияВремени.ОтработаноЧасов,
		|	СведенияВремени.ОтработаноДнейШестидневка,
		|	СведенияВремени.ОтработаноДнейПятидневка,
		|	СведенияВремени.ОтработаноЧасовПятидневка,
		|	СведенияВремени.ОтработаноДнейКалендарных,
		|	ЗНАЧЕНИЕ(Перечисление.ИсточникиДанныхДляРасчетаСреднегоЗаработка.СведенияДоНачалаЭксплуатации)
		|ИЗ
		|	РегистрСведений.СведенияОВремениДляРасчетаСреднегоОбщий КАК СведенияВремени
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТМесяцыРасчетаСотрудников КАК СотрудникиМесяцы
		|		ПО (СотрудникиМесяцы.Сотрудник = СведенияВремени.Сотрудник)
		|			И (СотрудникиМесяцы.Месяц = СведенияВремени.Месяц)
		|			И (СотрудникиМесяцы.ПорядокРасчета = СведенияВремени.ПорядокРасчета)
		|			И (СведенияВремени.ОтработаноДней <> 0
		|				ИЛИ СведенияВремени.ОтработаноЧасов <> 0
		|				ИЛИ СведенияВремени.ОтработаноЧасов <> 0
		|				ИЛИ СведенияВремени.ОтработаноДнейШестидневка <> 0
		|				ИЛИ СведенияВремени.ОтработаноДнейПятидневка <> 0
		|				ИЛИ СведенияВремени.ОтработаноЧасовПятидневка <> 0
		|				ИЛИ СведенияВремени.ОтработаноДнейКалендарных <> 0)
		|			И (&УчитыватьКорректировки = ИСТИНА
		|				ИЛИ НЕ ИСТИНА В
		|						(ВЫБРАТЬ ПЕРВЫЕ 1
		|							ИСТИНА
		|						ИЗ
		|							ВТНакопленияВремени КАК НакопленияВремени
		|						ГДЕ
		|							НакопленияВремени.Сотрудник = СведенияВремени.Сотрудник
		|							И НакопленияВремени.Период = СведенияВремени.Месяц
		|							И НакопленияВремени.ПорядокРасчета = СведенияВремени.ПорядокРасчета))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	3,
		|	ДанныеВремени.Период,
		|	ДанныеВремени.Сотрудник,
		|	ДанныеВремени.ПорядокРасчета,
		|	ДанныеВремени.ОтработаноДней,
		|	ДанныеВремени.ОтработаноЧасов,
		|	ДанныеВремени.ОтработаноДнейШестидневка,
		|	ДанныеВремени.ОтработаноДнейПятидневка,
		|	ДанныеВремени.ОтработаноЧасовПятидневка,
		|	ДанныеВремени.ОтработаноДнейКалендарных,
		|	ЗНАЧЕНИЕ(Перечисление.ИсточникиДанныхДляРасчетаСреднегоЗаработка.Начисления)
		|ИЗ
		|	ВТНакопленияВремени КАК ДанныеВремени
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТМесяцыРасчетаСотрудников КАК СотрудникиМесяцы
		|		ПО (СотрудникиМесяцы.Сотрудник = ДанныеВремени.Сотрудник)
		|			И (СотрудникиМесяцы.Месяц = ДанныеВремени.Период)
		|			И (СотрудникиМесяцы.ПорядокРасчета = ДанныеВремени.ПорядокРасчета)
		|			И (ДанныеВремени.ПереносДанных = ИСТИНА)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Время.Сотрудник,
		|	Время.Период,
		|	МИНИМУМ(Время.Приоритет) КАК Приоритет
		|ПОМЕСТИТЬ ВТВремяДляРасчетаСреднегоМинимальныйПриоритет
		|ИЗ
		|	ВТВремяДляРасчетаСреднего КАК Время
		|
		|СГРУППИРОВАТЬ ПО
		|	Время.Сотрудник,
		|	Время.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеВремени.Месяц,
		|	ДанныеВремени.Сотрудник,
		|	ДанныеВремени.ПорядокРасчета,
		|	ДанныеВремени.Источник,
		|	СУММА(ДанныеВремени.ОтработаноДней) КАК ОтработаноДней,
		|	СУММА(ДанныеВремени.ОтработаноЧасов) КАК ОтработаноЧасов,
		|	СУММА(ДанныеВремени.ОтработаноДнейШестидневка) КАК ОтработаноДнейШестидневка,
		|	СУММА(ДанныеВремени.ОтработаноДнейПятидневка) КАК ОтработаноДнейПятидневка,
		|	СУММА(ДанныеВремени.ОтработаноЧасовПятидневка) КАК ОтработаноЧасовПятидневка,
		|	СУММА(ДанныеВремени.ОтработаноДнейКалендарных) КАК ОтработаноДнейКалендарных
		|ПОМЕСТИТЬ ВТДанныеВремениПоСотрудникам
		|ИЗ
		|	(ВЫБРАТЬ
		|		ДанныеВремени.Период КАК Месяц,
		|		ДанныеВремени.Сотрудник КАК Сотрудник,
		|		ДанныеВремени.ПорядокРасчета КАК ПорядокРасчета,
		|		ДанныеВремени.Источник КАК Источник,
		|		ДанныеВремени.ОтработаноДней КАК ОтработаноДней,
		|		ДанныеВремени.ОтработаноЧасов КАК ОтработаноЧасов,
		|		ДанныеВремени.ОтработаноДнейШестидневка КАК ОтработаноДнейШестидневка,
		|		ДанныеВремени.ОтработаноДнейПятидневка КАК ОтработаноДнейПятидневка,
		|		ДанныеВремени.ОтработаноЧасовПятидневка КАК ОтработаноЧасовПятидневка,
		|		ДанныеВремени.ОтработаноДнейКалендарных КАК ОтработаноДнейКалендарных
		|	ИЗ
		|		ВТВремяДляРасчетаСреднего КАК ДанныеВремени
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТВремяДляРасчетаСреднегоМинимальныйПриоритет КАК МинимальныйПриоритет
		|			ПО ДанныеВремени.Сотрудник = МинимальныйПриоритет.Сотрудник
		|				И ДанныеВремени.Период = МинимальныйПриоритет.Период
		|				И ДанныеВремени.Приоритет = МинимальныйПриоритет.Приоритет
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ДанныеВремени.Период,
		|		ДанныеВремени.Сотрудник,
		|		ДанныеВремени.ПорядокРасчета,
		|		ДанныеВремени.Источник,
		|		ДанныеВремени.ОтработаноДней,
		|		ДанныеВремени.ОтработаноЧасов,
		|		ДанныеВремени.ОтработаноДнейШестидневка,
		|		ДанныеВремени.ОтработаноДнейПятидневка,
		|		ДанныеВремени.ОтработаноЧасовПятидневка,
		|		ДанныеВремени.ОтработаноДнейКалендарных
		|	ИЗ
		|		ВТВремяДляРасчетаСреднего КАК ДанныеВремени
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТВремяДляРасчетаСреднегоМинимальныйПриоритет КАК МинимальныйПриоритет
		|			ПО ДанныеВремени.Сотрудник = МинимальныйПриоритет.Сотрудник
		|				И ДанныеВремени.Период = МинимальныйПриоритет.Период
		|				И (МинимальныйПриоритет.Приоритет = 1)
		|				И (ДанныеВремени.Приоритет = 3)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ДанныеВремениКорректировка.Месяц,
		|		ДанныеВремениКорректировка.Сотрудник,
		|		ДанныеВремениКорректировка.ПорядокРасчета,
		|		ЗНАЧЕНИЕ(Перечисление.ИсточникиДанныхДляРасчетаСреднегоЗаработка.Начисления),
		|		ДанныеВремениКорректировка.ОтработаноДней,
		|		0,
		|		ДанныеВремениКорректировка.ОтработаноДнейШестидневка,
		|		ДанныеВремениКорректировка.ОтработаноДнейПятидневка,
		|		ДанныеВремениКорректировка.ОтработаноЧасовПятидневка,
		|		ДанныеВремениКорректировка.ОтработаноДнейКалендарных
		|	ИЗ
		|		РегистрСведений.ДанныеОВремениДляРасчетаСреднегоОбщийКорректировка КАК ДанныеВремениКорректировка
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТМесяцыРасчетаСотрудников КАК СотрудникиМесяцы
		|			ПО (СотрудникиМесяцы.Сотрудник = ДанныеВремениКорректировка.Сотрудник)
		|				И (СотрудникиМесяцы.ПорядокРасчета = ДанныеВремениКорректировка.ПорядокРасчета)
		|				И (СотрудникиМесяцы.Месяц = ДанныеВремениКорректировка.Месяц)
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВТВремяДляРасчетаСреднегоМинимальныйПриоритет КАК МинимальныйПриоритет
		|			ПО (МинимальныйПриоритет.Сотрудник = ДанныеВремениКорректировка.Сотрудник)
		|				И (МинимальныйПриоритет.Период = ДанныеВремениКорректировка.Месяц)
		|				И (МинимальныйПриоритет.Приоритет = 2)
		|	ГДЕ
		|		МинимальныйПриоритет.Период ЕСТЬ NULL ) КАК ДанныеВремени
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеВремени.Месяц,
		|	ДанныеВремени.Сотрудник,
		|	ДанныеВремени.ПорядокРасчета,
		|	ДанныеВремени.Источник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеВремени.Сотрудник,
		|	ДанныеВремени.ПорядокРасчета,
		|	ДанныеВремени.Период,
		|	СУММА(ДанныеВремени.ОтработаноДней) КАК ОтработаноДней,
		|	СУММА(ДанныеВремени.ОтработаноЧасов) КАК ОтработаноЧасов,
		|	СУММА(ДанныеВремени.ОтработаноДнейПятидневка) КАК ОтработаноДнейПятидневка,
		|	СУММА(ДанныеВремени.ОтработаноЧасовПятидневка) КАК ОтработаноЧасовПятидневка,
		|	СУММА(ДанныеВремени.ОтработаноДнейКалендарных) КАК ОтработаноДнейКалендарных,
		|	СУММА(ДанныеВремени.ОтработаноДнейШестидневка) КАК ОтработаноДнейШестидневка,
		|	СУММА(ДанныеВремени.НормаДнейПроизводственныйКалендарь) КАК НормаДнейПроизводственныйКалендарь,
		|	СУММА(ДанныеВремени.НормаЧасовПроизводственныйКалендарь) КАК НормаЧасовПроизводственныйКалендарь
		|ПОМЕСТИТЬ ВТДанныеВремени
		|ИЗ
		|	(ВЫБРАТЬ
		|		ДанныеВремени.Сотрудник КАК Сотрудник,
		|		ДанныеВремени.ПорядокРасчета КАК ПорядокРасчета,
		|		ДанныеВремени.Источник КАК Источник,
		|		ДанныеВремени.Месяц КАК Период,
		|		ДанныеВремени.ОтработаноДней КАК ОтработаноДней,
		|		ДанныеВремени.ОтработаноЧасов КАК ОтработаноЧасов,
		|		ДанныеВремени.ОтработаноДнейПятидневка КАК ОтработаноДнейПятидневка,
		|		ДанныеВремени.ОтработаноЧасовПятидневка КАК ОтработаноЧасовПятидневка,
		|		ДанныеВремени.ОтработаноДнейКалендарных КАК ОтработаноДнейКалендарных,
		|		ДанныеВремени.ОтработаноДнейШестидневка КАК ОтработаноДнейШестидневка,
		|		0 КАК НормаДнейПроизводственныйКалендарь,
		|		0 КАК НормаЧасовПроизводственныйКалендарь
		|	ИЗ
		|		ВТДанныеВремениПоСотрудникам КАК ДанныеВремени
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		СотрудникиМесяцы.Сотрудник,
		|		СотрудникиМесяцы.ПорядокРасчета,
		|		ЗНАЧЕНИЕ(Перечисление.ИсточникиДанныхДляРасчетаСреднегоЗаработка.ПустаяСсылка),
		|		СотрудникиМесяцы.Месяц,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		КалендарноеВремя.НормаДнейПоПроизводственномуКалендарю,
		|		КалендарноеВремя.НормаЧасовПоПроизводственномуКалендарю
		|	ИЗ
		|		ВТМесяцыРасчетаСотрудников КАК СотрудникиМесяцы
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеПроизводственногоКалендаряПоСотрудникам КАК КалендарноеВремя
		|			ПО (КалендарноеВремя.Сотрудник = СотрудникиМесяцы.Сотрудник)
		|				И (НАЧАЛОПЕРИОДА(КалендарноеВремя.ДатаНачала, МЕСЯЦ) = СотрудникиМесяцы.Месяц)) КАК ДанныеВремени
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеВремени.Сотрудник,
		|	ДанныеВремени.ПорядокРасчета,
		|	ДанныеВремени.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеВремени.Сотрудник,
		|	ДанныеВремени.ПорядокРасчета,
		|	ДанныеВремени.Период,
		|	ЕСТЬNULL(ИсточникиДанных.Источник, ЗНАЧЕНИЕ(Перечисление.ИсточникиДанныхДляРасчетаСреднегоЗаработка.ПустаяСсылка)) КАК Источник,
		|	ДанныеВремени.ОтработаноДней,
		|	ДанныеВремени.ОтработаноЧасов,
		|	ДанныеВремени.ОтработаноДнейПятидневка,
		|	ДанныеВремени.ОтработаноЧасовПятидневка,
		|	ДанныеВремени.ОтработаноДнейКалендарных,
		|	ДанныеВремени.ОтработаноДнейШестидневка,
		|	ДанныеВремени.НормаДнейПроизводственныйКалендарь,
		|	ДанныеВремени.НормаЧасовПроизводственныйКалендарь
		|ИЗ
		|	ВТДанныеВремени КАК ДанныеВремени
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВремяДляРасчетаСреднего КАК ИсточникиДанных
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТВремяДляРасчетаСреднегоМинимальныйПриоритет КАК МинимальныйПриоритет
		|			ПО (МинимальныйПриоритет.Сотрудник = ИсточникиДанных.Сотрудник)
		|				И (МинимальныйПриоритет.Период = ИсточникиДанных.Период)
		|		ПО (ИсточникиДанных.Сотрудник = ДанныеВремени.Сотрудник)
		|			И (ИсточникиДанных.Период = ДанныеВремени.Период)
		|			И (ИсточникиДанных.Приоритет = МинимальныйПриоритет.Приоритет)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДанныеВремени.Сотрудник,
		|	ДанныеВремени.Период";

	ВыборкаВремени = Запрос.Выполнить().Выбрать();
	ДанныеВремени = ПустаяТаблицаОтработанноеВремяСреднийЗаработокОбщий();
	Пока ВыборкаВремени.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ДанныеВремени.Добавить(), ВыборкаВремени);
	КонецЦикла;
	
	Возврат ДанныеВремени;
	
КонецФункции

#КонецОбласти

#Область ВспомогательныеПроцедурыИФункцииРегистрацииКорректировок

Процедура ЗаписатьТаблицуЗначенийВРегистрСведений(ИсходнаяТаблицаЗначений, НаборЗаписей, ИменаПолейОтбора, НачалоПериода, ОкончаниеПериода, Замещать = Истина, ЗначенияПоУмолчанию = Неопределено) Экспорт
	
	ПоляОтбора = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИменаПолейОтбора);
	
	// Записываем нулевые наборы записей (набор записей, в котором всего одна заполненная пустыми значениями строка) на месяцы, 
	// для которых в таблице ИсходнаяТаблицаЗначений нет строк за период с НачалоПериода по ОкончаниеПериода.
	Если Замещать Тогда
		Отбор = Новый Структура;
		СочетанияОтбора = Новый ТаблицаЗначений;
		Для Каждого ПолеОтбора Из ПоляОтбора Цикл
			Если СокрЛП(ПолеОтбора) = "Месяц" Тогда
				// Сочетания выбираем без учета месяца.
				Продолжить;
			КонецЕсли;
			Отбор.Вставить(СокрЛП(ПолеОтбора));
			СочетанияОтбора.Колонки.Добавить(СокрЛП(ПолеОтбора));
		КонецЦикла;
		// Заполняем таблицу сочетаний.
		Если ИсходнаяТаблицаЗначений.Количество() = 0 И ЗначенияПоУмолчанию <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(СочетанияОтбора.Добавить(), ЗначенияПоУмолчанию);
		КонецЕсли;
		Для Каждого СтрокаТаблицы Из ИсходнаяТаблицаЗначений Цикл
			ЗаполнитьЗначенияСвойств(Отбор, СтрокаТаблицы);
			Если СочетанияОтбора.НайтиСтроки(Отбор).Количество() = 0 Тогда
				ЗаполнитьЗначенияСвойств(СочетанияОтбора.Добавить(), Отбор);
			КонецЕсли;
		КонецЦикла;
		// Записываем или пустые, или нулевые наборы на всем интервале.
		Отбор.Вставить("Месяц");
		МесяцОбхода = НачалоПериода;
		Пока МесяцОбхода < ОкончаниеПериода Цикл
			Для Каждого СтрокаСочетания Из СочетанияОтбора Цикл
				ЗаполнитьЗначенияСвойств(Отбор, СтрокаСочетания);
				Отбор.Месяц = МесяцОбхода;
				Если ИсходнаяТаблицаЗначений.НайтиСтроки(Отбор).Количество() = 0 Тогда
					// Очищаем пространство в регистре сведений.
					Для Каждого КлючИЗначение Из Отбор Цикл
						НаборЗаписей.Отбор[КлючИЗначение.Ключ].Установить(КлючИЗначение.Значение);
					КонецЦикла;
					// Если запись выполняется в режиме замещения, то не оставляем пустых месяцев, если нет данных записываем нулевую запись.
					// Это позволит в дальнейшем отличать пустые места от отсутствующих данных.
					ПустаяСтрока = НаборЗаписей.Добавить();
					Если ЗначенияПоУмолчанию <> Неопределено Тогда
						ЗаполнитьЗначенияСвойств(ПустаяСтрока, ЗначенияПоУмолчанию);
					КонецЕсли;
					ЗаполнитьЗначенияСвойств(ПустаяСтрока, Отбор);
					НаборЗаписей.Записать();
					НаборЗаписей.Очистить();
				КонецЕсли;
			КонецЦикла;
			МесяцОбхода = ДобавитьМесяц(МесяцОбхода, 1);
		КонецЦикла;
	КонецЕсли;
	
	// Записываем таблицу значений.
	ТаблицаЗначений = ИсходнаяТаблицаЗначений.Скопировать();
	
	СтруктураОтбора = Новый Структура(ИменаПолейОтбора);
	Пока ТаблицаЗначений.Количество() > 0 Цикл
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, ТаблицаЗначений[0]);
		СтрокиПоОтбору = ТаблицаЗначений.НайтиСтроки(СтруктураОтбора);
		Для Каждого СтрокаТаблицы Из СтрокиПоОтбору Цикл
			ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), СтрокаТаблицы);
			ТаблицаЗначений.Удалить(СтрокаТаблицы);
		КонецЦикла;
		Для Каждого КлючИЗначение Из СтруктураОтбора Цикл
			НаборЗаписей.Отбор[КлючИЗначение.Ключ].Установить(КлючИЗначение.Значение);
		КонецЦикла;
		НаборЗаписей.Записать();
		НаборЗаписей.Очистить();
	КонецЦикла;
	
КонецПроцедуры

Процедура ПереименоватьКолонкуПериодВМесяц(ТаблицаЗначений)
	
	КолонкаПериод = ТаблицаЗначений.Колонки.Найти("Период");
	
	Если КолонкаПериод <> Неопределено Тогда
		КолонкаПериод.Имя = "Месяц";
	КонецЕсли;
	
КонецПроцедуры

// Функция выполняет преобразование сведений о среднем заработке, 
//  введенных по правилам ФСС в сведения по правилам общего среднего заработка.
//
Функция СведенияОбщегоСреднегоЗаработкаПоКорректировкамФСС(Организация, КорректировкиНачисленийФСС, КорректировкиВремениФСС = Неопределено)
	
	КорректировкиОбщегоЗаработка = Новый Структура;
	
	Если КорректировкиНачисленийФСС.Количество() > 0 
		Или (КорректировкиВремениФСС <> Неопределено И КорректировкиВремениФСС.Количество() > 0) Тогда
		
		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;

		МассивФизическихЛиц = ОбщегоНазначения.ВыгрузитьКолонку(КорректировкиНачисленийФСС, "ФизическоеЛицо", Истина);
		Если КорректировкиВремениФСС <> Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивФизическихЛиц, 
				ОбщегоНазначения.ВыгрузитьКолонку(КорректировкиНачисленийФСС, "ФизическоеЛицо", Истина), Истина);
		КонецЕсли;
		
		// Данные ФСС определены для физического лица в целом, 
		// поэтому растиражируем их в учете общего среднего по всем сотрудникам физического лица в этой головной организации.
		ПараметрыПолученияСотрудников = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
		ПараметрыПолученияСотрудников.ОтбиратьПоГоловнойОрганизации = Истина;
		ПараметрыПолученияСотрудников.Организация = ЗарплатаКадры.ГоловнаяОрганизация(Организация);
		ПараметрыПолученияСотрудников.СписокФизическихЛиц = МассивФизическихЛиц;
		
		КадровыйУчет.СоздатьВТСотрудникиОрганизации(МенеджерВременныхТаблиц, Ложь, ПараметрыПолученияСотрудников);
		
	КонецЕсли;
	
	Если КорректировкиНачисленийФСС.Количество() > 0 Тогда
		
		// Перенос выполняем только тех сведений, 
		// для которых в учете общего среднего заработка нет «собственных» корректировок.
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	КорректировкиНачисленийФСС.ФизическоеЛицо,
			|	КорректировкиНачисленийФСС.ГоловнаяОрганизация,
			|	КорректировкиНачисленийФСС.Месяц,
			|	КорректировкиНачисленийФСС.ПорядокРасчета,
			|	КорректировкиНачисленийФСС.Сумма
			|ПОМЕСТИТЬ КорректировкиНачисленийФСС
			|ИЗ
			|	&КорректировкиНачисленийФСС КАК КорректировкиНачисленийФСС
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Сотрудники.Сотрудник,
			|	КорректировкиНачисленийФСС.ГоловнаяОрганизация КАК Организация,
			|	КорректировкиНачисленийФСС.ФизическоеЛицо,
			|	КорректировкиНачисленийФСС.Месяц,
			|	ПорядокРасчетаОбщегоСреднегоЗаработка.Ссылка КАК ПорядокРасчета,
			|	ЗНАЧЕНИЕ(Перечисление.УчетНачисленийВСреднемЗаработкеОбщий.ОбщийЗаработок) КАК СоставнаяЧасть,
			|	ИСТИНА КАК Индексируется,
			|	КорректировкиНачисленийФСС.Сумма
			|ПОМЕСТИТЬ ВТКорректировкиОбщегоСреднегоЗаработка
			|ИЗ
			|	КорректировкиНачисленийФСС КАК КорректировкиНачисленийФСС
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Перечисление.ПорядокРасчетаСреднегоЗаработкаОбщий КАК ПорядокРасчетаОбщегоСреднегоЗаработка
			|		ПО (ИСТИНА)
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудникиОрганизации КАК Сотрудники
			|		ПО (Сотрудники.ФизическоеЛицо = КорректировкиНачисленийФСС.ФизическоеЛицо)
			|ГДЕ
			|	КорректировкиНачисленийФСС.ПорядокРасчета = &ПорядокРасчетаФСС
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	КорректировкиОбщегоСреднегоЗаработка.Сотрудник,
			|	КорректировкиОбщегоСреднегоЗаработка.Месяц
			|ПОМЕСТИТЬ ВТСуществующиеЗаписи
			|ИЗ
			|	ВТКорректировкиОбщегоСреднегоЗаработка КАК КорректировкиОбщегоСреднегоЗаработка
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОНачисленияхДляРасчетаСреднегоЗаработкаОбщий КАК СведенияОНачислениях
			|		ПО (СведенияОНачислениях.Сотрудник = КорректировкиОбщегоСреднегоЗаработка.Сотрудник)
			|			И (СведенияОНачислениях.Месяц = КорректировкиОбщегоСреднегоЗаработка.Месяц)
			|			И (СведенияОНачислениях.ДанныеИзУчетаСреднегоФСС = ЛОЖЬ)
			|
			|ОБЪЕДИНИТЬ
			|
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	КорректировкиОбщегоСреднегоЗаработка.Сотрудник,
			|	КорректировкиОбщегоСреднегоЗаработка.Месяц
			|ИЗ
			|	ВТКорректировкиОбщегоСреднегоЗаработка КАК КорректировкиОбщегоСреднегоЗаработка
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий КАК ДанныеНачислений
			|		ПО (ДанныеНачислений.Сотрудник = КорректировкиОбщегоСреднегоЗаработка.Сотрудник)
			|			И (НАЧАЛОПЕРИОДА(ДанныеНачислений.Период, МЕСЯЦ) = КорректировкиОбщегоСреднегоЗаработка.Месяц)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	КорректировкиОбщегоСреднегоЗаработка.Сотрудник,
			|	КорректировкиОбщегоСреднегоЗаработка.Организация,
			|	КорректировкиОбщегоСреднегоЗаработка.ФизическоеЛицо,
			|	КорректировкиОбщегоСреднегоЗаработка.Месяц,
			|	КорректировкиОбщегоСреднегоЗаработка.ПорядокРасчета,
			|	КорректировкиОбщегоСреднегоЗаработка.СоставнаяЧасть,
			|	КорректировкиОбщегоСреднегоЗаработка.Индексируется,
			|	КорректировкиОбщегоСреднегоЗаработка.Сумма,
			|	ИСТИНА КАК ДанныеИзУчетаСреднегоФСС
			|ИЗ
			|	ВТКорректировкиОбщегоСреднегоЗаработка КАК КорректировкиОбщегоСреднегоЗаработка
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСуществующиеЗаписи КАК СуществующиеЗаписи
			|		ПО (СуществующиеЗаписи.Сотрудник = КорректировкиОбщегоСреднегоЗаработка.Сотрудник)
			|			И (СуществующиеЗаписи.Месяц = КорректировкиОбщегоСреднегоЗаработка.Месяц)
			|ГДЕ
			|	СуществующиеЗаписи.Месяц ЕСТЬ NULL ";
		
		// Заполняем колонки, имеющие смысл для общего среднего заработка
		// кроме того, данные корректировок представлены для разных порядков расчета ФСС,
		// необходимо данные по одному из порядков (любому, т.к. по всем одинаковые данные) 
		// записать по всем (по которым это имеет смысл) существующим порядкам расчета 
		// общего среднего заработка.
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("КорректировкиНачисленийФСС", КорректировкиНачисленийФСС);
		Запрос.УстановитьПараметр("ПорядокРасчетаФСС", КорректировкиНачисленийФСС[0]["ПорядокРасчета"]);
		
		КорректировкиОбщегоЗаработка.Вставить("КорректировкиНачислений", Запрос.Выполнить().Выгрузить());
		
		Запрос.Текст = 
			"УНИЧТОЖИТЬ ВТСуществующиеЗаписи";
		Запрос.Выполнить();
		
	КонецЕсли;
	
	Если КорректировкиВремениФСС <> Неопределено И КорректировкиВремениФСС.Количество() > 0 Тогда
		
		ТекстЗапроса =
			"ВЫБРАТЬ
			|	КорректировкиВремениФСС.ФизическоеЛицо,
			|	КорректировкиВремениФСС.Месяц,
			|	КорректировкиВремениФСС.ОтработаноДнейКалендарных
			|ПОМЕСТИТЬ КорректировкиВремениФСС
			|ИЗ
			|	&КорректировкиВремениФСС КАК КорректировкиВремениФСС
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Сотрудники.Сотрудник,
			|	КорректировкиВремениФСС.Месяц,
			|	ПорядокРасчетаОбщегоСреднегоЗаработка.Ссылка КАК ПорядокРасчета,
			|	КорректировкиВремениФСС.ОтработаноДнейКалендарных
			|ПОМЕСТИТЬ ВТКорректировкиВремениОбщегоСреднегоЗаработка
			|ИЗ
			|	КорректировкиВремениФСС КАК КорректировкиВремениФСС
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудникиОрганизации КАК Сотрудники
			|		ПО (Сотрудники.ФизическоеЛицо = КорректировкиВремениФСС.ФизическоеЛицо)
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Перечисление.ПорядокРасчетаСреднегоЗаработкаОбщий КАК ПорядокРасчетаОбщегоСреднегоЗаработка
			|		ПО (ИСТИНА)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Корректировки.Сотрудник,
			|	Корректировки.Месяц
			|ПОМЕСТИТЬ ВТСуществующиеЗаписи
			|ИЗ
			|	ВТКорректировкиВремениОбщегоСреднегоЗаработка КАК Корректировки
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОВремениДляРасчетаСреднегоОбщий КАК СведенияОВремени
			|		ПО (СведенияОВремени.Сотрудник = Корректировки.Сотрудник)
			|			И (СведенияОВремени.Месяц = Корректировки.Месяц)
			|			И (СведенияОВремени.ДанныеИзУчетаСреднегоФСС = ЛОЖЬ)
			|
			|ОБЪЕДИНИТЬ
			|
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Корректировки.Сотрудник,
			|	Корректировки.Месяц
			|ИЗ
			|	ВТКорректировкиВремениОбщегоСреднегоЗаработка КАК Корректировки
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ДанныеОВремениДляРасчетаСреднегоОбщий КАК ДанныеВремени
			|		ПО (ДанныеВремени.Сотрудник = Корректировки.Сотрудник)
			|			И (НАЧАЛОПЕРИОДА(ДанныеВремени.Период, МЕСЯЦ) = Корректировки.Месяц)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Корректировки.Сотрудник,
			|	Корректировки.Месяц,
			|	Корректировки.ПорядокРасчета,
			|	Корректировки.ОтработаноДнейКалендарных,
			|	ИСТИНА КАК ДанныеИзУчетаСреднегоФСС
			|ИЗ
			|	ВТКорректировкиВремениОбщегоСреднегоЗаработка КАК Корректировки
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСуществующиеЗаписи КАК СуществующиеЗаписи
			|		ПО (СуществующиеЗаписи.Сотрудник = Корректировки.Сотрудник)
			|			И (СуществующиеЗаписи.Месяц = Корректировки.Месяц)
			|ГДЕ
			|	СуществующиеЗаписи.Месяц ЕСТЬ NULL ";
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		
		Запрос.УстановитьПараметр("КорректировкиВремениФСС", КорректировкиВремениФСС);
		
		КорректировкиОбщегоЗаработка.Вставить("КорректировкиВремени", Запрос.Выполнить().Выгрузить());
		
	КонецЕсли;
	
	Возврат КорректировкиОбщегоЗаработка;
	
КонецФункции

#КонецОбласти

#Область ПроцедурыИФункцииФормыКалькулятораСреднегоЗаработка

// Процедура заполняет заголовки таблицы представлениями месяцев.
//
Процедура ЗаполнитьМесяцыВЗаголовкахКолонок(ЭлементыФормы, НомераМесяцев, ВыводитьГод) Экспорт
	
	ФорматнаяСтрока = ?(ВыводитьГод, 
						СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							"ДФ='ММММ%1гггг'", 
							Символы.ПС), 
						"ДФ='ММММ'");
						
	Для Каждого КлючИЗначение Из НомераМесяцев Цикл
		НачалоМесяца = КлючИЗначение.Ключ;
		НомерМесяца = КлючИЗначение.Значение;
		ЭлементыФормы["СреднийЗаработокСумма" + НомерМесяца].Заголовок = Формат(НачалоМесяца, ФорматнаяСтрока);
	КонецЦикла;

	// Колонки, которые не входят в период расчета среднего - делаем невидимыми.
	НомерМесяца = 12 - НомераМесяцев.Количество();
	Пока НомерМесяца > 0 Цикл
		ЭлементыФормы["СреднийЗаработокСумма" + НомерМесяца].Видимость = Ложь;
		НомерМесяца = НомерМесяца - 1;
	КонецЦикла;
	
КонецПроцедуры	

Функция СтрокаСреднегоЗаработка(СреднийЗаработок, Идентификатор) Экспорт
	
	НайденныеСтроки = СреднийЗаработок.НайтиСтроки(Новый Структура("Идентификатор", Идентификатор));
	
	Возврат ?(НайденныеСтроки.Количество() = 0, Неопределено, НайденныеСтроки[0]);
	
КонецФункции

// Выбирает из указанного периода месяцы, в которые возможны корректировки, 
// то есть ручной ввод значений в форме калькулятора.
// Корректировки доступны начиная с даты приема на работу и вплоть до первого месяца, 
// в котором есть расчеты.
//
// Параметры:
//	Сотрудник
//	НачалоПериодаРасчета
//	ОкончаниеПериодаРасчета
//
// Возвращаемое значение - фиксированный массив.
//
Функция МесяцыКорректировкиСреднегоЗаработка(Сотрудник, НачалоПериодаРасчета, ОкончаниеПериодаРасчета, ДатаПриемаНаРаботу, ДатаНачалаСобытия) Экспорт
	
	// Метод определяет доступный для редактирования период, 
	// опираясь на наличие данных расчета зарплаты.
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	МИНИМУМ(НАЧАЛОПЕРИОДА(Начисления.ПериодДействияНачало, МЕСЯЦ)) КАК Месяц
		|ИЗ
		|	РегистрРасчета.Начисления КАК Начисления
		|ГДЕ
		|	Начисления.Сотрудник = &Сотрудник
		|	И НЕ Начисления.Регистратор ССЫЛКА Документ.ПереносДанных";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	
	// Месяцы, в которых доступен ручной ввод, т.е. корректировка.
	МесяцыКорректировкиМассив = Новый Массив;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	МесяцОбхода = НачалоПериодаРасчета;
	Пока МесяцОбхода < ОкончаниеПериодаРасчета Цикл
		Если НачалоМесяца(МесяцОбхода) = НачалоМесяца(ДатаНачалаСобытия) Тогда
			// Если период расчета захватывает дату начала события, 
			// значит это месяц приема на работу, зарплата еще не рассчитана, нужно дать возможность корректировок.
			МесяцыКорректировкиМассив.Добавить(МесяцОбхода);
		КонецЕсли;
		Если ЗначениеЗаполнено(Выборка.Месяц) И МесяцОбхода >= Выборка.Месяц Тогда
			// Если начались расчеты, значит месяцы корректировок закончились.
			Прервать;
		КонецЕсли;
		Если КонецМесяца(МесяцОбхода) >= ДатаПриемаНаРаботу Тогда
			МесяцыКорректировкиМассив.Добавить(МесяцОбхода);
		КонецЕсли;
		МесяцОбхода = ДобавитьМесяц(МесяцОбхода, 1);
	КонецЦикла;
	
	Возврат Новый ФиксированныйМассив(МесяцыКорректировкиМассив);
	
КонецФункции

Процедура ЗаполнитьДатуПриемаНаРаботуСотрудника(ДатаПриема, Сотрудник, ДатаНачалаСобытия, Отказ) Экспорт
	
	// Определим дату приема на работу сотрудника и ограничим ею начало периода расчета среднего заработка.
	КадровыеДанные = КадровыйУчет.КадровыеДанныеСотрудников(Истина, Сотрудник, "ДатаПриема,ОформленПоТрудовомуДоговору");
	Если КадровыеДанные.Количество() = 0 Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Сведения о сотруднике недоступны.';
													|en = 'Information on the employee is unavailable.'"), , , , Отказ);
	Иначе
		Если Не КадровыеДанные[0].ОформленПоТрудовомуДоговору Тогда
			Если НЕ КадровыйУчет.ЕстьДоговорыГПХ(КадровыеДанные[0].ФизическоеЛицо) Тогда
				ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Сотрудник не принят на работу.';
															|en = 'Employee is not hired.'"), , , , Отказ);
			КонецЕсли;
		Иначе
			Если Не ЗначениеЗаполнено(ДатаНачалаСобытия) Тогда
				ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Дата начала события не заполнена.';
															|en = 'Start date of event is required.'"), , , , Отказ);
			Иначе
				Если КадровыеДанные[0].ДатаПриема > ДатаНачалаСобытия Тогда
					ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Дата начала события раньше, чем сотрудник принят на работу.';
																|en = 'Start date of event is less than the employee is hired. '"), , , , Отказ);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ДатаПриема = КадровыеДанные[0].ДатаПриема;
	
КонецПроцедуры

#КонецОбласти

#Область ПрочиеВспомогательныеПроцедурыИФункции

// Функция составляет массив периодов, входящих в указанный интервал, 
//  для которых отсутствуют строки в коллекции данных среднего заработка.
//
// Параметры:
//  НачалоРасчета - дата начала интервала, в котором определяются не заполненные периоды.
//	ОкончаниеРасчета - дата окончания интервала, в котором определяются не заполненные периоды.
//
// Возвращаемое значение
//	Массив - содержащий периоды, для которых отсутствуют данные среднего заработка.
//
Функция ПериодыНезаполненногоСреднегоЗаработка(НачалоРасчета, ОкончаниеРасчета, ДанныеНачислений)
	
	МассивПериодов = Новый Массив;
	
	ТекущийПериод = НачалоРасчета;
	
	Пока ТекущийПериод < ОкончаниеРасчета Цикл
		Если ДанныеНачислений.НайтиСтроки(Новый Структура("Период", ТекущийПериод)).Количество() = 0 Тогда
			МассивПериодов.Добавить(ТекущийПериод);
		КонецЕсли;
		ТекущийПериод = ДобавитьМесяц(ТекущийПериод, 1);		
	КонецЦикла;	
	
	Возврат МассивПериодов;
	
КонецФункции

Процедура ОчиститьДанныеОСреднемДокумента(Сотрудник, ПорядокРасчета, НачалоПериода, ОкончаниеПериода, ДанныеОНачислениях, ДанныеОВремени, ДанныеОбИндексации = Неопределено)
	
	ТекущийМесяц = НачалоМесяца(НачалоПериода);
	
	Пока ТекущийМесяц <= НачалоМесяца(ОкончаниеПериода) Цикл 
		
		СтруктураПоиска = Новый Структура("Сотрудник, ПорядокРасчета, Период");
		СтруктураПоиска.Сотрудник = Сотрудник;
		СтруктураПоиска.ПорядокРасчета = ПорядокРасчета;
		СтруктураПоиска.Период = ТекущийМесяц;
		
		СтрокиДокументаНачисления = ДанныеОНачислениях.НайтиСтроки(СтруктураПоиска);
		Для Каждого НайденнаяСтрока Из СтрокиДокументаНачисления Цикл
			ДанныеОНачислениях.Удалить(ДанныеОНачислениях.Индекс(НайденнаяСтрока));
		КонецЦикла;
		
		Если ТипЗнч(ПорядокРасчета) = Тип("ПеречислениеСсылка.ПорядокРасчетаСреднегоЗаработкаФСС") Тогда
			СтруктураПоиска = Новый Структура("Сотрудник,  Период", Сотрудник,  ТекущийМесяц);	
		КонецЕсли;	
		
		СтрокиДокументаВремя = ДанныеОВремени.НайтиСтроки(СтруктураПоиска);
		Для Каждого НайденнаяСтрока Из СтрокиДокументаВремя Цикл
			ДанныеОВремени.Удалить(ДанныеОВремени.Индекс(НайденнаяСтрока));
		КонецЦикла;
		
		Если ДанныеОбИндексации <> Неопределено Тогда
			УдаляемыеСтрокиИндексации = ДанныеОбИндексации.НайтиСтроки(Новый Структура("Сотрудник, Период", Сотрудник, ТекущийМесяц));
			Для Каждого УдаляемыеСтрока Из УдаляемыеСтрокиИндексации Цикл 
				ДанныеОбИндексации.Удалить(ДанныеОбИндексации.Индекс(УдаляемыеСтрока));
			КонецЦикла;	
		КонецЕсли;	

		ТекущийМесяц = ДобавитьМесяц(ТекущийМесяц, 1);
		
	КонецЦикла;	
	
КонецПроцедуры	

Процедура УдалитьДанныеВнеПериодаРасчетаСреднегоЗаработка(КоллекцияДанныхСреднего, НачалоПериода, ОкончаниеПериода, ПорядокРасчета = Неопределено, ГодГодовыхПремий = Неопределено) Экспорт
	
	СохраняемыеЗначения = Перечисления.ИсточникиДанныхДляРасчетаСреднегоЗаработка.РезультатыРедактирования();
	
 	УдаляемыеСтроки = Новый Массив;
	Для Каждого СтрокаКоллекции Из КоллекцияДанныхСреднего Цикл
		ПодходитПоПериоду = СтрокаКоллекции.Период >= НачалоПериода И СтрокаКоллекции.Период <= ОкончаниеПериода;
		Если ГодГодовыхПремий <> Неопределено Тогда
			Если Перечисления.УчетНачисленийВСреднемЗаработкеОбщий.ГодовыеПремии().Найти(СтрокаКоллекции.СоставнаяЧасть) <> Неопределено Тогда
				ПодходитПоПериоду = СтрокаКоллекции.Год = ГодГодовыхПремий;
			КонецЕсли;
		КонецЕсли;
		
		// Если порядок расчета не указан, то это значит, что его не нужно учитывать, тогда:
		// - корректировки оставляем в любом случае
		// - не корректировки оставляем, если проходит по периоду (если не проходит - удаляем).
		
		// Если порядок расчета указан
		// - корректировки переносим (заменяем порядок расчета)
		// - не корректировки оставляем, если проходит по периоду.
		
		// Если не подходит по периоду, удаляем, но корректировки оставляем.
		Если Не ПодходитПоПериоду И СохраняемыеЗначения.Найти(СтрокаКоллекции.Источник) = Неопределено Тогда
			УдаляемыеСтроки.Добавить(СтрокаКоллекции);
			Продолжить;
		КонецЕсли;	
		
		Если ПорядокРасчета <> Неопределено Тогда
			Если СтрокаКоллекции.ПорядокРасчета <> ПорядокРасчета Тогда
				Если СохраняемыеЗначения.Найти(СтрокаКоллекции.Источник) <> Неопределено Тогда
					СтрокаКоллекции.ПорядокРасчета = ПорядокРасчета;
				Иначе
					// Не корректировка, которая не совпадает по порядку расчета
					// ее удаляем даже несмотря на то, что она проходит по периоду.
					УдаляемыеСтроки.Добавить(СтрокаКоллекции);
				КонецЕсли;
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		
		Если Не ПодходитПоПериоду Тогда
			УдаляемыеСтроки.Добавить(СтрокаКоллекции);
		КонецЕсли;
	КонецЦикла;	
	
	Для Каждого УдаляемаяСтрока Из УдаляемыеСтроки Цикл
		КоллекцияДанныхСреднего.Удалить(УдаляемаяСтрока);
	КонецЦикла;
	
КонецПроцедуры

Процедура ОчиститьДанныеДляРасчетаСреднего(КоллекцияСтрок, Сотрудники)
	
	// Очищает данные для расчета среднего по указанным сотрудникам.
	Для Каждого Сотрудник Из Сотрудники Цикл
		ОтборСтрок = Новый Структура("Сотрудник", Сотрудник);
		УдаляемыеСтроки = КоллекцияСтрок.НайтиСтроки(ОтборСтрок);
		Для Каждого УдаляемаяСтрока Из УдаляемыеСтроки Цикл
			КоллекцияСтрок.Удалить(УдаляемаяСтрока);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

// Составляет массив типов документов, в которых осуществляется локальный расчет показателей среднего заработка.
//
Функция ТипыДокументовРасчетаПоСреднемуЗаработку() Экспорт 
	Возврат Метаданные.ОпределяемыеТипы.ДокументСреднегоЗаработка.Тип.Типы();;
КонецФункции

// Устарела. 
// см. УчетСреднегоЗаработкаКлиентСервер.ПараметрыРедактированияОбщегоСреднегоЗаработкаПоДокументу.
//
Функция ПараметрыРедактированияОбщегоСреднегоЗаработкаПоДокументу() Экспорт
	Возврат УчетСреднегоЗаработкаКлиентСервер.ПараметрыРедактированияОбщегоСреднегоЗаработкаПоДокументу();
КонецФункции

#КонецОбласти

Процедура ЗаписатьДатуНачалаСобытия(ДокументСсылка, Сотрудник, ДатаНачалаСобытия) Экспорт
		
	Сведения = СведенияДокументаСреднегоЗаработка();
	Сведения.ДатаНачалаСобытия = ДатаНачалаСобытия;
	ЗаписатьСведенияДокументаПоСотруднику(ДокументСсылка, Сотрудник, Сведения);

КонецПроцедуры

Функция СведенияДокументаСреднегоЗаработка() Экспорт
	
	СведенияДокумента = Новый Структура("ДатаНачалаСобытия");
	Возврат СведенияДокумента;
	
КонецФункции

Процедура ЗаписатьСведенияДокументаПоСотруднику(ДокументСсылка, Сотрудник, СведенияДокумента) Экспорт
	
	ТаблицаСведений = ТаблицаСведенийДокументаСреднегоЗаработка();
	
	НоваяСтрока = ТаблицаСведений.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока, СведенияДокумента);
	НоваяСтрока.Сотрудник = Сотрудник;
	
	ЗаписатьСведенияДокументаПоСотрудникам(ДокументСсылка, ТаблицаСведений);
	
КонецПроцедуры

Функция ТаблицаСведенийДокументаСреднегоЗаработка() Экспорт
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	
	ПоляСведений = СведенияДокументаСреднегоЗаработка();
	Для Каждого КлючИЗначение Из ПоляСведений Цикл
		Таблица.Колонки.Добавить(КлючИЗначение.Ключ);
	КонецЦикла;
	
	Возврат Таблица;
	
КонецФункции

Процедура ЗаписатьСведенияДокументаПоСотрудникам(ДокументСсылка, СведенияПоСотрудникам) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = РегистрыСведений.ДокументыСреднегоЗаработка.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ДокументОснование.Установить(ДокументСсылка);
	
	Для Каждого СтрокаТаблицы Из СведенияПоСотрудникам Цикл
		НоваяСтрока = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		НоваяСтрока.ДокументОснование = ДокументСсылка;
	КонецЦикла;		
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

Процедура СоздатьВТДокументыСреднегоЗаработкаПоДатеНачалаСобытия(МенеджерВременныхТаблиц, ДокументСсылка, Сотрудник, ДатаНачалаСобытия) Экспорт
	
	НаборЗаписей = РегистрыСведений.ДокументыСреднегоЗаработка.СоздатьНаборЗаписей();
	
	НоваяСтрока = НаборЗаписей.Добавить();
	НоваяСтрока.Сотрудник = Сотрудник;
	НоваяСтрока.ДокументОснование = ДокументСсылка;
	НоваяСтрока.ДатаНачалаСобытия = ДатаНачалаСобытия;

	ЗарплатаКадры.СоздатьВТПоНаборуЗаписей(МенеджерВременныхТаблиц, НаборЗаписей);
	
КонецПроцедуры

#Область ИсключениеДублирующихПремий

Функция ОписаниеВариантаПолитикиИсключенияДублирующихПремий()
	
	Результат = Новый Структура;
	Результат.Вставить("Описание");
	Результат.Вставить("ПодробноеОписание");
	
	Возврат Результат;
	
КонецФункции

Процедура СоздатьВТПолныеСуммыПремий(МенеджерВременныхТаблиц, ИсключаемыйРегистратор)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Премии", Перечисления.УчетНачисленийВСреднемЗаработкеОбщий.Премии());
	Запрос.УстановитьПараметр("ГодовыеПремии", Перечисления.УчетНачисленийВСреднемЗаработкеОбщий.ГодовыеПремии());
	Запрос.УстановитьПараметр("ИсключаемыйРегистратор", ИсключаемыйРегистратор);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Период КАК Период,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Регистратор КАК Регистратор,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.НомерСтроки КАК НомерСтроки
		|ПОМЕСТИТЬ ВТОтборРегистра_ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий
		|ИЗ
		|	ВТПериодыРасчетаСреднегоСотрудников КАК ВТПериодыРасчетаСреднегоСотрудников
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий КАК ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий
		|		ПО ВТПериодыРасчетаСреднегоСотрудников.Сотрудник = ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Сотрудник
		|			И (ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Регистратор <> &ИсключаемыйРегистратор)
		|			И (ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Период МЕЖДУ ВТПериодыРасчетаСреднегоСотрудников.НачалоПериодаРасчетаСреднего И ВТПериодыРасчетаСреднегоСотрудников.ОкончаниеПериодаРасчетаСреднего)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Период,
		|	Регистратор,
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Период КАК Период,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Регистратор КАК Регистратор,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Сотрудник КАК Сотрудник,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.ПорядокРасчета КАК ПорядокРасчета,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.СоставнаяЧасть КАК СоставнаяЧасть,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Год КАК Год,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.ДатаНачалаБазовогоПериода КАК ДатаНачалаБазовогоПериода,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.КоличествоМесяцев КАК КоличествоМесяцев,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Организация КАК Организация,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.ИсходныйДокумент КАК ИсходныйДокумент,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.ПоказательПремирования КАК ПоказательПремирования,
		|	ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Сумма КАК Сумма
		|ПОМЕСТИТЬ ВТДанныеРегистра_ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий
		|ИЗ
		|	ВТОтборРегистра_ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий КАК ВТОтборРегистра_ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий КАК ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий
		|		ПО ВТОтборРегистра_ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Период = ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Период
		|			И ВТОтборРегистра_ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Регистратор = ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Регистратор
		|			И ВТОтборРегистра_ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.НомерСтроки = ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.НомерСтроки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Сотрудник,
		|	ПорядокРасчета,
		|	Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПолныеСуммыПремий.Период КАК Период,
		|	ПолныеСуммыПремий.Сотрудник КАК Сотрудник,
		|	ПолныеСуммыПремий.ПорядокРасчета КАК ПорядокРасчета,
		|	ПолныеСуммыПремий.Начисление КАК Начисление,
		|	ПолныеСуммыПремий.СоставнаяЧасть КАК СоставнаяЧасть,
		|	ПолныеСуммыПремий.Год КАК Год,
		|	НАЧАЛОПЕРИОДА(ПолныеСуммыПремий.ДатаНачалаБазовогоПериода, МЕСЯЦ) КАК ДатаНачалаБазовогоПериода,
		|	ПолныеСуммыПремий.КоличествоМесяцев КАК КоличествоМесяцев,
		|	ПолныеСуммыПремий.Организация КАК Организация,
		|	ПолныеСуммыПремий.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ПолныеСуммыПремий.ИсходныйДокумент КАК ИсходныйДокумент,
		|	ПолныеСуммыПремий.ПоказательПремирования КАК ПоказательПремирования,
		|	ВЫБОР
		|		КОГДА ПолныеСуммыПремий.ПоказательПремирования <> ЗНАЧЕНИЕ(Справочник.ПоказателиПремирования.ПустаяСсылка)
		|				И ПолныеСуммыПремий.Сумма <> 0
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ПроверитьДублирование,
		|	ПолныеСуммыПремий.Сумма КАК Сумма,
		|	АВТОНОМЕРЗАПИСИ() КАК КлючНачисления
		|ПОМЕСТИТЬ ВТПолныеСуммыПремий
		|ИЗ
		|	(ВЫБРАТЬ
		|		ДанныеРегистра.Период КАК Период,
		|		ДанныеРегистра.Сотрудник КАК Сотрудник,
		|		ДанныеРегистра.ПорядокРасчета КАК ПорядокРасчета,
		|		ЕСТЬNULL(ДанныеПоНачислениям.Начисление, ЗНАЧЕНИЕ(ПланВидовРасчета.Начисления.ПустаяСсылка)) КАК Начисление,
		|		ДанныеРегистра.СоставнаяЧасть КАК СоставнаяЧасть,
		|		ДанныеРегистра.Год КАК Год,
		|		МИНИМУМ(ДанныеРегистра.ДатаНачалаБазовогоПериода) КАК ДатаНачалаБазовогоПериода,
		|		ДанныеРегистра.КоличествоМесяцев КАК КоличествоМесяцев,
		|		ДанныеРегистра.Организация КАК Организация,
		|		ДанныеРегистра.ФизическоеЛицо КАК ФизическоеЛицо,
		|		ВЫБОР
		|			КОГДА ДанныеРегистра.ИсходныйДокумент = НЕОПРЕДЕЛЕНО
		|				ТОГДА ДанныеРегистра.Регистратор
		|			ИНАЧЕ ДанныеРегистра.ИсходныйДокумент
		|		КОНЕЦ КАК ИсходныйДокумент,
		|		ДанныеРегистра.ПоказательПремирования КАК ПоказательПремирования,
		|		СУММА(ЕСТЬNULL(ДанныеПоНачислениям.Сумма, ДанныеРегистра.Сумма)) КАК Сумма
		|	ИЗ
		|		ВТДанныеРегистра_ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий КАК ДанныеРегистра
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПериодыРасчетаСреднегоСотрудников КАК ПериодыРасчетаСреднегоСотрудников
		|			ПО ДанныеРегистра.Сотрудник = ПериодыРасчетаСреднегоСотрудников.Сотрудник
		|				И ДанныеРегистра.ПорядокРасчета = ПериодыРасчетаСреднегоСотрудников.ПорядокРасчета
		|				И (ДанныеРегистра.Период МЕЖДУ ПериодыРасчетаСреднегоСотрудников.НачалоПериодаРасчетаСреднего И ПериодыРасчетаСреднегоСотрудников.ОкончаниеПериодаРасчетаСреднего)
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям КАК ДанныеПоНачислениям
		|			ПО ДанныеРегистра.Регистратор = ДанныеПоНачислениям.Регистратор
		|				И ДанныеРегистра.Период = ДанныеПоНачислениям.Период
		|				И ДанныеРегистра.Сотрудник = ДанныеПоНачислениям.Сотрудник
		|				И ДанныеРегистра.ПорядокРасчета = ДанныеПоНачислениям.ПорядокРасчета
		|				И ДанныеРегистра.СоставнаяЧасть = ДанныеПоНачислениям.СоставнаяЧасть
		|				И ДанныеРегистра.Год = ДанныеПоНачислениям.Год
		|				И ДанныеРегистра.ДатаНачалаБазовогоПериода = ДанныеПоНачислениям.ДатаНачалаБазовогоПериода
		|				И ДанныеРегистра.КоличествоМесяцев = ДанныеПоНачислениям.КоличествоМесяцев
		|				И ДанныеРегистра.Организация = ДанныеПоНачислениям.Организация
		|				И ДанныеРегистра.ФизическоеЛицо = ДанныеПоНачислениям.ФизическоеЛицо
		|				И ДанныеРегистра.ИсходныйДокумент = ДанныеПоНачислениям.ИсходныйДокумент
		|				И ДанныеРегистра.ПоказательПремирования = ДанныеПоНачислениям.ПоказательПремирования
		|	ГДЕ
		|		ДанныеРегистра.СоставнаяЧасть В(&Премии)
		|		И НЕ ДанныеРегистра.СоставнаяЧасть В (&ГодовыеПремии)
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ДанныеРегистра.Период,
		|		ДанныеРегистра.Сотрудник,
		|		ДанныеРегистра.ПорядокРасчета,
		|		ЕСТЬNULL(ДанныеПоНачислениям.Начисление, ЗНАЧЕНИЕ(ПланВидовРасчета.Начисления.ПустаяСсылка)),
		|		ДанныеРегистра.СоставнаяЧасть,
		|		ДанныеРегистра.Год,
		|		ДанныеРегистра.КоличествоМесяцев,
		|		ДанныеРегистра.Организация,
		|		ДанныеРегистра.ФизическоеЛицо,
		|		ВЫБОР
		|			КОГДА ДанныеРегистра.ИсходныйДокумент = НЕОПРЕДЕЛЕНО
		|				ТОГДА ДанныеРегистра.Регистратор
		|			ИНАЧЕ ДанныеРегистра.ИсходныйДокумент
		|		КОНЕЦ,
		|		ДанныеРегистра.ПоказательПремирования) КАК ПолныеСуммыПремий
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТОтборРегистра_ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТДанныеРегистра_ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий";
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура СоздатьВТОтборУникальныхПремий_Типовая(МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПолныеСуммыПремий.Сотрудник КАК Сотрудник,
		|	ПолныеСуммыПремий.ПорядокРасчета КАК ПорядокРасчета,
		|	ПолныеСуммыПремий.СоставнаяЧасть КАК СоставнаяЧасть,
		|	ПолныеСуммыПремий.ДатаНачалаБазовогоПериода КАК ДатаНачалаБазовогоПериода,
		|	ПолныеСуммыПремий.КоличествоМесяцев КАК КоличествоМесяцев,
		|	ПолныеСуммыПремий.Организация КАК Организация,
		|	ПолныеСуммыПремий.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ПолныеСуммыПремий.ПоказательПремирования КАК ПоказательПремирования,
		|	МАКСИМУМ(ПолныеСуммыПремий.Сумма) КАК Сумма
		|ПОМЕСТИТЬ ВТМаксимальныеПремии
		|ИЗ
		|	ВТПолныеСуммыПремий КАК ПолныеСуммыПремий
		|ГДЕ
		|	ПолныеСуммыПремий.ПроверитьДублирование
		|
		|СГРУППИРОВАТЬ ПО
		|	ПолныеСуммыПремий.Сотрудник,
		|	ПолныеСуммыПремий.ПорядокРасчета,
		|	ПолныеСуммыПремий.СоставнаяЧасть,
		|	ПолныеСуммыПремий.ДатаНачалаБазовогоПериода,
		|	ПолныеСуммыПремий.КоличествоМесяцев,
		|	ПолныеСуммыПремий.Организация,
		|	ПолныеСуммыПремий.ФизическоеЛицо,
		|	ПолныеСуммыПремий.ПоказательПремирования
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(ПолныеСуммыПремий.КлючНачисления) КАК КлючНачисления
		|ПОМЕСТИТЬ ВТОтборУникальныхПремий
		|ИЗ
		|	ВТМаксимальныеПремии КАК МаксимальныеПремии
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПолныеСуммыПремий КАК ПолныеСуммыПремий
		|		ПО (ПолныеСуммыПремий.Сотрудник = МаксимальныеПремии.Сотрудник)
		|			И (ПолныеСуммыПремий.ПорядокРасчета = МаксимальныеПремии.ПорядокРасчета)
		|			И (ПолныеСуммыПремий.СоставнаяЧасть = МаксимальныеПремии.СоставнаяЧасть)
		|			И (ПолныеСуммыПремий.ДатаНачалаБазовогоПериода = МаксимальныеПремии.ДатаНачалаБазовогоПериода)
		|			И (ПолныеСуммыПремий.КоличествоМесяцев = МаксимальныеПремии.КоличествоМесяцев)
		|			И (ПолныеСуммыПремий.Организация = МаксимальныеПремии.Организация)
		|			И (ПолныеСуммыПремий.ФизическоеЛицо = МаксимальныеПремии.ФизическоеЛицо)
		|			И (ПолныеСуммыПремий.ПоказательПремирования = МаксимальныеПремии.ПоказательПремирования)
		|			И (ПолныеСуммыПремий.Сумма = МаксимальныеПремии.Сумма)
		|
		|СГРУППИРОВАТЬ ПО
		|	ПолныеСуммыПремий.Сотрудник,
		|	ПолныеСуммыПремий.ПорядокРасчета,
		|	ПолныеСуммыПремий.СоставнаяЧасть,
		|	ПолныеСуммыПремий.ДатаНачалаБазовогоПериода,
		|	ПолныеСуммыПремий.КоличествоМесяцев,
		|	ПолныеСуммыПремий.Организация,
		|	ПолныеСуммыПремий.ФизическоеЛицо,
		|	ПолныеСуммыПремий.ПоказательПремирования,
		|	ПолныеСуммыПремий.Сумма
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТМаксимальныеПремии";
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура СоздатьВТОтборУникальныхПремий_БезИсключения(МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПолныеСуммыПремий.КлючНачисления КАК КлючНачисления
		|ПОМЕСТИТЬ ВТОтборУникальныхПремий
		|ИЗ
		|	ВТПолныеСуммыПремий КАК ПолныеСуммыПремий";
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура СоздатьВТОтборУникальныхПремий(МенеджерВременныхТаблиц)
	
	ИмяВарианта = ИмяПолитикиИсключенияДублирующихПремий();
	
	Если ИмяВарианта = ИмяВариантаИсключенияПремийТиповаяПолитика() Тогда
		СоздатьВТОтборУникальныхПремий_Типовая(МенеджерВременныхТаблиц);
	ИначеЕсли ИмяВарианта = ИмяВариантаИсключенияПремийБезИсключения() Тогда
		СоздатьВТОтборУникальныхПремий_БезИсключения(МенеджерВременныхТаблиц);
	КонецЕсли;
	
	// Это точка подключения расширений.
	
КонецПроцедуры

Процедура СоздатьВТДанныеОПремиях(МенеджерВременныхТаблиц, ИсключаемыйРегистратор)
	
	СоздатьВТОтборУникальныхПремий(МенеджерВременныхТаблиц);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Премии", Перечисления.УчетНачисленийВСреднемЗаработкеОбщий.Премии());
	Запрос.УстановитьПараметр("ИсключаемыйРегистратор", ИсключаемыйРегистратор);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПолныеСуммыПремий.Период КАК Период,
		|	ДанныеРегистра.Регистратор КАК Регистратор,
		|	ПолныеСуммыПремий.Сотрудник КАК Сотрудник,
		|	ПолныеСуммыПремий.ПорядокРасчета КАК ПорядокРасчета,
		|	ПолныеСуммыПремий.Начисление КАК Начисление,
		|	ПолныеСуммыПремий.СоставнаяЧасть КАК СоставнаяЧасть,
		|	ПолныеСуммыПремий.Год КАК Год,
		|	ПолныеСуммыПремий.ДатаНачалаБазовогоПериода КАК ДатаНачалаБазовогоПериода,
		|	ПолныеСуммыПремий.КоличествоМесяцев КАК КоличествоМесяцев,
		|	ПолныеСуммыПремий.Организация КАК Организация,
		|	ПолныеСуммыПремий.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ПолныеСуммыПремий.ПоказательПремирования КАК ПоказательПремирования,
		|	СУММА(ДанныеРегистра.Сумма) КАК Сумма,
		|	ПолныеСуммыПремий.КлючНачисления КАК КлючНачисления,
		|	ВЫБОР
		|		КОГДА ОтборУникальныхПремий.КлючНачисления ЕСТЬ NULL
		|			ТОГДА ПолныеСуммыПремий.ПроверитьДублирование
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Дублирующая,
		|	ВЫБОР
		|		КОГДА ДанныеРегистра.ИсходныйДокумент <> НЕОПРЕДЕЛЕНО
		|				И ДанныеРегистра.ИсходныйДокумент <> ДанныеРегистра.Регистратор
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоПерерасчет
		|ПОМЕСТИТЬ ВТДанныеОПремиях
		|ИЗ
		|	ВТПолныеСуммыПремий КАК ПолныеСуммыПремий
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОтборУникальныхПремий КАК ОтборУникальныхПремий
		|		ПО (ОтборУникальныхПремий.КлючНачисления = ПолныеСуммыПремий.КлючНачисления)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям КАК ДанныеРегистра
		|		ПО (ДанныеРегистра.Регистратор <> &ИсключаемыйРегистратор)
		|			И (ДанныеРегистра.Сотрудник = ПолныеСуммыПремий.Сотрудник)
		|			И (ДанныеРегистра.Начисление = ПолныеСуммыПремий.Начисление)
		|			И (ДанныеРегистра.ПорядокРасчета = ПолныеСуммыПремий.ПорядокРасчета)
		|			И (ДанныеРегистра.СоставнаяЧасть = ПолныеСуммыПремий.СоставнаяЧасть)
		|			И (ДанныеРегистра.Год = ПолныеСуммыПремий.Год)
		|			И (НАЧАЛОПЕРИОДА(ДанныеРегистра.ДатаНачалаБазовогоПериода, МЕСЯЦ) = ПолныеСуммыПремий.ДатаНачалаБазовогоПериода)
		|			И (ДанныеРегистра.Период = ПолныеСуммыПремий.Период)
		|			И (ДанныеРегистра.КоличествоМесяцев = ПолныеСуммыПремий.КоличествоМесяцев)
		|			И (ДанныеРегистра.Организация = ПолныеСуммыПремий.Организация)
		|			И (ДанныеРегистра.ФизическоеЛицо = ПолныеСуммыПремий.ФизическоеЛицо)
		|			И (ДанныеРегистра.ИсходныйДокумент = ПолныеСуммыПремий.ИсходныйДокумент
		|				ИЛИ ДанныеРегистра.ИсходныйДокумент = НЕОПРЕДЕЛЕНО
		|					И ДанныеРегистра.Регистратор = ПолныеСуммыПремий.ИсходныйДокумент)
		|			И (ДанныеРегистра.ПоказательПремирования = ПолныеСуммыПремий.ПоказательПремирования)
		|
		|СГРУППИРОВАТЬ ПО
		|	ПолныеСуммыПремий.Период,
		|	ДанныеРегистра.Регистратор,
		|	ПолныеСуммыПремий.Сотрудник,
		|	ПолныеСуммыПремий.ПорядокРасчета,
		|	ПолныеСуммыПремий.Начисление,
		|	ПолныеСуммыПремий.СоставнаяЧасть,
		|	ПолныеСуммыПремий.Год,
		|	ПолныеСуммыПремий.ДатаНачалаБазовогоПериода,
		|	ПолныеСуммыПремий.КоличествоМесяцев,
		|	ПолныеСуммыПремий.Организация,
		|	ПолныеСуммыПремий.ФизическоеЛицо,
		|	ПолныеСуммыПремий.ПоказательПремирования,
		|	ПолныеСуммыПремий.КлючНачисления,
		|	ВЫБОР
		|		КОГДА ОтборУникальныхПремий.КлючНачисления ЕСТЬ NULL
		|			ТОГДА ПолныеСуммыПремий.ПроверитьДублирование
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ДанныеРегистра.ИсходныйДокумент <> НЕОПРЕДЕЛЕНО
		|				И ДанныеРегистра.ИсходныйДокумент <> ДанныеРегистра.Регистратор
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ";
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ВыражениеВыбораПоказателяПремированияПоТабличнойЧасти(МетаданныеТаблицы)
	
	ПоСторно = МетаданныеТаблицы.Реквизиты.Найти("СторнируемыйДокумент") <> Неопределено;
	ПоРегистраторуРазовогоНачисления = МетаданныеТаблицы.Реквизиты.Найти("РегистраторРазовогоНачисления") <> Неопределено;
	
	Если ПоСторно Или ПоРегистраторуРазовогоНачисления Тогда
		
		Строки = Новый Массив();
		Строки.Добавить("#ВЫБОР"); // # - Означает что выражение "ТаблицаНачислений.ПоказательПремирования" будет заменено целиком.
		
		Если ПоСторно Тогда
			Если МетаданныеТаблицы.Реквизиты.Найти("Сторно") <> Неопределено Тогда
				Строки.Добавить(
					"КОГДА Сторно ТОГДА
					|	ВЫБОР
					|		КОГДА СторнируемыйДокумент ССЫЛКА Документ.Премия 
	        		|			ТОГДА ВЫРАЗИТЬ(СторнируемыйДокумент КАК Документ.Премия).ПоказательПремирования 
					|		ИНАЧЕ Начисление.ПоказательПремирования
					|	КОНЕЦ");
			Иначе
				Строки.Добавить(
					"КОГДА СторнируемыйДокумент ССЫЛКА Документ.Премия ТОГДА
	        		|	ВЫРАЗИТЬ(СторнируемыйДокумент КАК Документ.Премия).ПоказательПремирования");
			КонецЕсли;
		КонецЕсли;
		
		Если ПоРегистраторуРазовогоНачисления Тогда
			Строки.Добавить(
				"КОГДА РегистраторРазовогоНачисления ССЫЛКА Документ.Премия
				|	ТОГДА ВЫРАЗИТЬ(РегистраторРазовогоНачисления КАК Документ.Премия).ПоказательПремирования");
		КонецЕсли;
		
		Строки.Добавить("ИНАЧЕ Начисление.ПоказательПремирования");
		Строки.Добавить("КОНЕЦ");
		Результат = СтрСоединить(Строки, Символы.ПС);
		
	Иначе
		Результат = "Начисление.ПоказательПремирования";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область БлокФункцийПервоначальногоЗаполненияИОбновленияИБ

// Добавляет в список Обработчики процедуры-обработчики обновления,
// необходимые данной подсистеме.
//
// Параметры:
//   Обработчики - ТаблицаЗначений - см. описание функции НоваяТаблицаОбработчиковОбновления
//                                   общего модуля ОбновлениеИнформационнойБазы.
// 
Процедура ЗарегистрироватьОбработчикиОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.27.87";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Процедура = "УчетСреднегоЗаработка.ЗаполнитьКоэффициентыПовышенияЗаработкаУчитываемыеПриИндексации";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("65f1b73e-2bc6-4790-8ba2-6ce083570eb7");
	Обработчик.Комментарий = НСтр("ru = 'Заполнение регистра Коэффициенты повышения заработка учитываемые при индексации.';
									|en = 'Fill the ""Coefficients of earnings increase applied upon indexing"" register.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.27.88";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Процедура = "УчетСреднегоЗаработка.УдалитьКоэффициентыПовышенияИзКоэффициентовИндексацииЗаработка";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("bcc7d5b2-14be-4c3c-b0fc-dd8d4ac5e5f3");
	Обработчик.Комментарий = НСтр("ru = 'Корректировка записей регистра Коэффициент индексации заработка.';
									|en = 'Adjust the ""Wage indexation factor"" register records.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.29.18";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Процедура = "УчетСреднегоЗаработка.ЗаполнитьДанныеСреднегоЗаработкаПоНачислениямГПХ";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("f2311f62-9e90-4436-85a8-16828a433ab4");
	Обработчик.Комментарий = НСтр("ru = 'Заполнение данных среднего заработка начислениями по договорам ГПХ.';
									|en = 'Fill average earnings data with accruals under civil law contracts.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия          = "3.1.30.113";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор   = Новый УникальныйИдентификатор("ab22c6f6-bfea-4bb7-b832-ab753a61aa91");
	Обработчик.Процедура       = "УчетСреднегоЗаработка.ДобавитьСторноДанныеДляРасчетаОбщегоСреднегоЗаработкаПоНачислениям";
	Обработчик.Комментарий     = НСтр("ru = 'Заполнение регистра ""Данные для расчета среднего заработка по начислениям"".';
										|en = 'Заполнение регистра ""Данные для расчета среднего заработка по начислениям"".'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия          = "3.1.30.114";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор   = Новый УникальныйИдентификатор("e2b39b46-3208-4799-85aa-d27fa35fba31");
	Обработчик.Процедура       = "УчетСреднегоЗаработка.ЗаполнитьДанныеДляРасчетаОбщегоСреднегоЗаработкаПоНачислениям";
	Обработчик.Комментарий     = НСтр("ru = 'Заполнение регистра ""Данные для расчета среднего заработка по начислениям"".';
										|en = 'Заполнение регистра ""Данные для расчета среднего заработка по начислениям"".'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия          = "3.1.30.115";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор   = Новый УникальныйИдентификатор("3240d454-a611-4284-9697-10a9a9406b68");
	Обработчик.Процедура       = "УчетСреднегоЗаработка.УдалитьТехноЗаписиДанныеДляРасчетаОбщегоСреднегоЗаработкаПоНачислениям";
	Обработчик.Комментарий     = НСтр("ru = 'Удаление записей регистра ""Данные для расчета среднего заработка по начислениям"".';
										|en = 'Удаление записей регистра ""Данные для расчета среднего заработка по начислениям"".'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.30.150";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Процедура = "УчетСреднегоЗаработка.ЗаполнитьПериодыДействияВДанныхДляРасчетаСреднегоЗаработкаПоНачислениям";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("a2441e12-9e90-4436-85a8-16828a222ab4");
	Обработчик.Комментарий = НСтр("ru = 'Заполнение периода действия в данных для расчета общего среднего заработка по начислениям.';
									|en = 'Заполнение периода действия в данных для расчета общего среднего заработка по начислениям.'");
	
КонецПроцедуры

Процедура ЗаполнитьКоэффициентыПовышенияЗаработкаУчитываемыеПриИндексации(ПараметрыОбновления = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
		|	КоэффициентИндексацииЗаработка.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ ВТРегистраторы
		|ИЗ
		|	РегистрСведений.КоэффициентИндексацииЗаработка КАК КоэффициентИндексацииЗаработка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КоэффициентыПовышенияЗаработкаУчитываемыеПриИндексации КАК КоэффициентыПовышения
		|		ПО КоэффициентИндексацииЗаработка.Регистратор = КоэффициентыПовышения.Регистратор
		|ГДЕ
		|	КоэффициентыПовышения.Регистратор ЕСТЬ NULL
		|	И (КоэффициентИндексацииЗаработка.Регистратор ССЫЛКА Документ.ИзменениеКвалификационногоРазряда
		|			ИЛИ КоэффициентИндексацииЗаработка.Регистратор ССЫЛКА Документ.ИзменениеОплатыТруда
		|			ИЛИ КоэффициентИндексацииЗаработка.Регистратор ССЫЛКА Документ.КадровыйПеревод
		|			ИЛИ КоэффициентИндексацииЗаработка.Регистратор ССЫЛКА Документ.КадровыйПереводСписком
		|			ИЛИ КоэффициентИндексацииЗаработка.Регистратор ССЫЛКА Документ.НазначениеПлановогоНачисления
		|			ИЛИ &УсловиеРегистратор)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КоэффициентИндексацииЗаработка.Период КАК Период,
		|	КоэффициентИндексацииЗаработка.Регистратор КАК Регистратор,
		|	КоэффициентИндексацииЗаработка.Сотрудник КАК Сотрудник,
		|	КоэффициентИндексацииЗаработка.Коэффициент КАК Коэффициент
		|ИЗ
		|	РегистрСведений.КоэффициентИндексацииЗаработка КАК КоэффициентИндексацииЗаработка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРегистраторы КАК Регистраторы
		|		ПО КоэффициентИндексацииЗаработка.Регистратор = Регистраторы.Регистратор
		|
		|УПОРЯДОЧИТЬ ПО
		|	Регистратор,
		|	Период,
		|	Сотрудник";
	
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1000", "");
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба.КлассныеЧиныРанги") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеРегистратор", "КоэффициентИндексацииЗаработка.Регистратор ССЫЛКА Документ.ПрисвоениеКлассногоЧинаРанга ИЛИ КоэффициентИндексацииЗаработка.Регистратор ССЫЛКА Документ.ПрисвоениеКлассногоЧинаРангаСписком");
	Иначе 
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеРегистратор", "ЛОЖЬ");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 0 Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	
	Пока Выборка.СледующийПоЗначениюПоля("Регистратор") Цикл 
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрСведений.КоэффициентыПовышенияЗаработкаУчитываемыеПриИндексации.НаборЗаписей", "Регистратор", Выборка.Регистратор) Тогда
			Продолжить;
		КонецЕсли;
		НаборЗаписей = РегистрыСведений.КоэффициентыПовышенияЗаработкаУчитываемыеПриИндексации.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
		Пока Выборка.Следующий() Цикл
			НоваяЗапись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
		КонецЦикла;
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
	КонецЦикла;
	
КонецПроцедуры

Процедура УдалитьКоэффициентыПовышенияИзКоэффициентовИндексацииЗаработка(ПараметрыОбновления = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
		|	КоэффициентИндексацииЗаработка.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрСведений.КоэффициентИндексацииЗаработка КАК КоэффициентИндексацииЗаработка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КоэффициентыПовышенияЗаработкаУчитываемыеПриИндексации КАК КоэффициентыПовышения
		|		ПО КоэффициентИндексацииЗаработка.Регистратор = КоэффициентыПовышения.Регистратор";
	
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1000", "");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 0 Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	
	Пока Выборка.СледующийПоЗначениюПоля("Регистратор") Цикл 
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрСведений.КоэффициентИндексацииЗаработка.НаборЗаписей", "Регистратор", Выборка.Регистратор) Тогда
			Продолжить;
		КонецЕсли;
		НаборЗаписей = РегистрыСведений.КоэффициентИндексацииЗаработка.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьДанныеСреднегоЗаработкаПоНачислениямГПХ(ПараметрыОбновления = Неопределено) Экспорт
	
	ПериодОбработки = Новый СтандартныйПериод();
	ПериодОбработки.ДатаНачала = УчетСтраховыхВзносов.ДатаОбъединенияВзносов();
	ПериодОбработки.ДатаОкончания = КонецМесяца(ТекущаяДатаСеанса());
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.ВидыОсобыхНачисленийИУдержаний.ДоговорАвторскогоЗаказа) КАК Начисление
		|ПОМЕСТИТЬ ВТНачисленияПоДоговорам
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.ВидыОсобыхНачисленийИУдержаний.ДоговорРаботыУслуги)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НачисленияУдержанияПоСотрудникамОбороты.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрНакопления.НачисленияУдержанияПоСотрудникам.Обороты(
		|			&ДатаНачала,
		|			&ДатаОкончания,
		|			Регистратор,
		|			НачислениеУдержание В
		|				(ВЫБРАТЬ
		|					НачисленияПоДоговорам.Начисление
		|				ИЗ
		|					ВТНачисленияПоДоговорам КАК НачисленияПоДоговорам)) КАК НачисленияУдержанияПоСотрудникамОбороты");
	
	Запрос.УстановитьПараметр("ДатаНачала", ПериодОбработки.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ПериодОбработки.ДатаОкончания);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументовПоВидамУчета = Обработки.ПроведениеДокументовПоВидамУчета;
	
	Параметры = ПроведениеДокументовПоВидамУчета.ПараметрыПроведенияПоУчетам();
	Параметры.ВидыУчета = "ДанныеДляРасчетаСреднего";
	Параметры.Период.ДатаНачала = ПериодОбработки.ДатаНачала;
	Параметры.Период.ДатаОкончания = ПериодОбработки.ДатаОкончания;
	Параметры.Вставить("ДокументыДляОбработки", РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Регистратор"));

	ПроведениеДокументовПоВидамУчета.ПровестиДокументыПоУчетам(Параметры);

	Если ПараметрыОбновления <> Неопределено Тогда
		ПараметрыОбновления.ОбработкаЗавершена = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьСторноДанныеДляРасчетаОбщегоСреднегоЗаработкаПоНачислениям(ПараметрыОбновления = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеПоНачислениям.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ ВТДанныеПоНачислениямРегистраторы
		|ИЗ
		|	РегистрНакопления.ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям КАК ДанныеПоНачислениям
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеСреднегоЗаработка.Регистратор КАК Регистратор,
		|	МАКСИМУМ(ЕСТЬNULL(ДанныеСреднегоЗаработка.Регистратор.Дата, ДанныеСреднегоЗаработка.Период)) КАК Дата
		|ПОМЕСТИТЬ ВТДанныеСторно
		|ИЗ
		|	РегистрНакопления.ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий КАК ДанныеСреднегоЗаработка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДанныеПоНачислениямРегистраторы КАК ДанныеПоНачислениямРегистраторы
		|		ПО ДанныеСреднегоЗаработка.Регистратор = ДанныеПоНачислениямРегистраторы.Регистратор
		|			И (ДанныеСреднегоЗаработка.Сторно)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеСреднегоЗаработка.Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеСторно.Регистратор КАК Регистратор
		|ИЗ
		|	ВТДанныеСторно КАК ДанныеСторно
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям КАК ДанныеПоНачислениям
		|		ПО ДанныеСторно.Регистратор = ДанныеПоНачислениям.Регистратор
		|			И (ДанныеПоНачислениям.Сторно)
		|ГДЕ
		|	ДанныеПоНачислениям.Регистратор ЕСТЬ NULL
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДанныеСторно.Дата";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 0 Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	
	ВидыУчета = "ДанныеДляРасчетаСреднего";
	РегистрыСреднегоЗаработка = УчетСреднегоЗаработка.РегистрыСреднегоЗаработка();
	
	Пока Выборка.СледующийПоЗначениюПоля("Регистратор") Цикл 
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрНакопления.ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям.НаборЗаписей", "Регистратор", Выборка.Регистратор) Тогда
			Продолжить;
		КонецЕсли;
		Попытка
			Движения = Новый Структура;
			ДвиженияМетаданные = Выборка.Регистратор.Метаданные().Движения;
			Для Каждого Движение Из ДвиженияМетаданные Цикл
				Если РегистрыСреднегоЗаработка.Найти(Движение) <> Неопределено Тогда 
					МенеджерРегистра = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(Движение.ПолноеИмя());
					РегистрНаборЗаписей = МенеджерРегистра.СоздатьНаборЗаписей();
					РегистрНаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
					Движения.Вставить(Движение.Имя, РегистрНаборЗаписей);
				КонецЕсли;
			КонецЦикла;
			Отказ = Ложь;
			МенеджерДокумента = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Выборка.Регистратор); 
			МенеджерДокумента.ПровестиПоУчетам(Выборка.Регистратор, РежимПроведенияДокумента.Неоперативный, Отказ, ВидыУчета, Движения);
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(Движения.ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям);
		Исключение
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка при формировании движений документа по регистру Данные для расчета среднего заработка по начислениям: %1';
					|en = 'Ошибка при формировании движений документа по регистру Данные для расчета среднего заработка по начислениям: %1'"),
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.РегистрыНакопления.ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям, ,
				ТекстСообщения);
		КонецПопытки;
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
	КонецЦикла;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
	
КонецПроцедуры

Процедура ЗаполнитьДанныеДляРасчетаОбщегоСреднегоЗаработкаПоНачислениям(ПараметрыОбновления = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеПоНачислениям.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ ВТДанныеПоНачислениямРегистраторы
		|ИЗ
		|	РегистрНакопления.ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям КАК ДанныеПоНачислениям
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеСреднегоЗаработка.Регистратор КАК Регистратор,
		|	МАКСИМУМ(ЕСТЬNULL(ДанныеСреднегоЗаработка.Регистратор.Дата, ДанныеСреднегоЗаработка.Период)) КАК Дата
		|ПОМЕСТИТЬ ВТДатыРегистраторов
		|ИЗ
		|	РегистрНакопления.ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий КАК ДанныеСреднегоЗаработка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеПоНачислениямРегистраторы КАК ДанныеПоНачислениям
		|		ПО ДанныеСреднегоЗаработка.Регистратор = ДанныеПоНачислениям.Регистратор
		|ГДЕ
		|	ДанныеПоНачислениям.Регистратор ЕСТЬ NULL
		|	И НЕ ДанныеСреднегоЗаработка.Регистратор ССЫЛКА Документ.ПереносДанных
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеСреднегоЗаработка.Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1000
		|	ДатыРегистраторов.Регистратор КАК Регистратор,
		|	ДатыРегистраторов.Дата КАК Дата
		|ПОМЕСТИТЬ ВТДанныеСреднегоЗаработкаРегистраторы
		|ИЗ
		|	ВТДатыРегистраторов КАК ДатыРегистраторов
		|
		|УПОРЯДОЧИТЬ ПО
		|	Дата
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеСреднегоЗаработка.Регистратор КАК Регистратор
		|ИЗ
		|	ВТДанныеСреднегоЗаработкаРегистраторы КАК ДанныеСреднегоЗаработка";
	
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1000", "");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 0 Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	
	ЗавершитьОбработку = Ложь;
	Если Выборка.Количество() < 1000 Тогда
		ЗавершитьОбработку = Истина;
	КонецЕсли;
	
	ВидыУчета = "ДанныеДляРасчетаСреднего";
	РегистрыСреднегоЗаработка = УчетСреднегоЗаработка.РегистрыСреднегоЗаработка();
	
	Пока Выборка.СледующийПоЗначениюПоля("Регистратор") Цикл 
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрНакопления.ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям.НаборЗаписей", "Регистратор", Выборка.Регистратор) Тогда
			Продолжить;
		КонецЕсли;
		Попытка
			Движения = Новый Структура;
			ДвиженияМетаданные = Выборка.Регистратор.Метаданные().Движения;
			Для Каждого Движение Из ДвиженияМетаданные Цикл
				Если РегистрыСреднегоЗаработка.Найти(Движение) <> Неопределено Тогда 
					МенеджерРегистра = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(Движение.ПолноеИмя());
					РегистрНаборЗаписей = МенеджерРегистра.СоздатьНаборЗаписей();
					РегистрНаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
					Движения.Вставить(Движение.Имя, РегистрНаборЗаписей);
				КонецЕсли;
			КонецЦикла;
			Отказ = Ложь;
			МенеджерДокумента = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Выборка.Регистратор); 
			МенеджерДокумента.ПровестиПоУчетам(Выборка.Регистратор, РежимПроведенияДокумента.Неоперативный, Отказ, ВидыУчета, Движения);
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(Движения.ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям);
		Исключение
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка при формировании движений документа по регистру Данные для расчета среднего заработка по начислениям: %1';
					|en = 'Ошибка при формировании движений документа по регистру Данные для расчета среднего заработка по начислениям: %1'"),
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.РегистрыНакопления.ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям, ,
				ТекстСообщения);
		КонецПопытки;
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
	КонецЦикла;
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеПоНачислениям.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ ВТТекущиеРегистраторы
		|ИЗ
		|	РегистрНакопления.ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям КАК ДанныеПоНачислениям
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеСреднегоЗаработкаРегистраторы.Регистратор КАК Регистратор
		|ИЗ
		|	ВТДанныеСреднегоЗаработкаРегистраторы КАК ДанныеСреднегоЗаработкаРегистраторы
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТекущиеРегистраторы КАК ТекущиеРегистраторы
		|		ПО ДанныеСреднегоЗаработкаРегистраторы.Регистратор = ТекущиеРегистраторы.Регистратор
		|ГДЕ
		|	ТекущиеРегистраторы.Регистратор ЕСТЬ NULL";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ДатаСеанса = ТекущаяДатаСеанса();
	Сотрудник = Справочники.Сотрудники.ПолучитьСсылку(Новый УникальныйИдентификатор("406fc5e6-8ebe-45db-b725-4daf404d1b2d"));
	ФизическоеЛицо = Справочники.ФизическиеЛица.ПолучитьСсылку();
	Организация = Справочники.Организации.ПолучитьСсылку();
	Начисление = ПланыВидовРасчета.Начисления.ПолучитьСсылку();
	
	Пока Выборка.Следующий() Цикл 
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрНакопления.ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям.НаборЗаписей", "Регистратор", Выборка.Регистратор) Тогда
			Продолжить;
		КонецЕсли;
		Попытка
			НаборЗаписей = РегистрыНакопления.ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.Период = ДатаСеанса;
			НоваяЗапись.Сотрудник = Сотрудник;
			НоваяЗапись.ФизическоеЛицо = ФизическоеЛицо;
			НоваяЗапись.Организация = Организация;
			НоваяЗапись.Начисление = Начисление;
			НоваяЗапись.ПорядокРасчета = Перечисления.ПорядокРасчетаСреднегоЗаработкаОбщий.Постановление2010;
			НоваяЗапись.СоставнаяЧасть = Перечисления.УчетНачисленийВСреднемЗаработкеОбщий.ОбщийЗаработок;
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
		Исключение
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка при формировании движений документа по регистру Данные для расчета среднего заработка по начислениям: %1';
					|en = 'Ошибка при формировании движений документа по регистру Данные для расчета среднего заработка по начислениям: %1'"),
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.РегистрыНакопления.ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям, ,
				ТекстСообщения);
		КонецПопытки;
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
	КонецЦикла;
	
	Если ЗавершитьОбработку Тогда 
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура УдалитьТехноЗаписиДанныеДляРасчетаОбщегоСреднегоЗаработкаПоНачислениям(ПараметрыОбновления = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	Сотрудник = Справочники.Сотрудники.ПолучитьСсылку(Новый УникальныйИдентификатор("406fc5e6-8ebe-45db-b725-4daf404d1b2d"));
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
		|	ДанныеПоНачислениям.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрНакопления.ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям КАК ДанныеПоНачислениям
		|ГДЕ
		|	ДанныеПоНачислениям.Сотрудник = &Сотрудник";
	
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1000", "");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 0 Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	
	Пока Выборка.Следующий() Цикл 
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрНакопления.ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям.НаборЗаписей", "Регистратор", Выборка.Регистратор) Тогда
			Продолжить;
		КонецЕсли;
		Попытка
			НаборЗаписей = РегистрыНакопления.ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
		Исключение
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка при формировании движений документа по регистру Данные для расчета среднего заработка по начислениям: %1';
					|en = 'Ошибка при формировании движений документа по регистру Данные для расчета среднего заработка по начислениям: %1'"),
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.РегистрыНакопления.ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям, ,
				ТекстСообщения);
		КонецПопытки;
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
	КонецЦикла;
	
	Если Выборка.Количество() < 1000 Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПериодыДействияВДанныхДляРасчетаСреднегоЗаработкаПоНачислениям(ПараметрыОбновления = Неопределено) Экспорт
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
		|	ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям.Регистратор КАК Регистратор,
		|	ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям.Период КАК Период
		|ИЗ
		|	РегистрНакопления.ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям КАК ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям
		|ГДЕ
		|	ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям.Период >= &ДатаНачалаОбработки
		|	И НЕ ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям.Регистратор ССЫЛКА Документ.ПереносДанных
		|	И ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям.ДатаНачала = ДАТАВРЕМЯ(1, 1, 1)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период";
	
	Запрос.УстановитьПараметр("ДатаНачалаОбработки", '20240101');
	
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1000", "");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 0 Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	
	ЗавершитьОбработку = Ложь;
	Если Выборка.Количество() < 1000 Тогда
		ЗавершитьОбработку = Истина;
	КонецЕсли;
	
	ВидыУчета = "ДанныеДляРасчетаСреднего";
	РегистрыСреднегоЗаработка = УчетСреднегоЗаработка.РегистрыСреднегоЗаработка();
	
	Пока Выборка.СледующийПоЗначениюПоля("Регистратор") Цикл 
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрНакопления.ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям.НаборЗаписей", "Регистратор", Выборка.Регистратор) Тогда
			Продолжить;
		КонецЕсли;
		Попытка
			Движения = Новый Структура;
			ДвиженияМетаданные = Выборка.Регистратор.Метаданные().Движения;
			Для Каждого Движение Из ДвиженияМетаданные Цикл
				Если РегистрыСреднегоЗаработка.Найти(Движение) <> Неопределено Тогда 
					МенеджерРегистра = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(Движение.ПолноеИмя());
					РегистрНаборЗаписей = МенеджерРегистра.СоздатьНаборЗаписей();
					РегистрНаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
					Движения.Вставить(Движение.Имя, РегистрНаборЗаписей);
				КонецЕсли;
			КонецЦикла;
			Отказ = Ложь;
			МенеджерДокумента = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Выборка.Регистратор); 
			МенеджерДокумента.ПровестиПоУчетам(Выборка.Регистратор, РежимПроведенияДокумента.Неоперативный, Отказ, ВидыУчета, Движения);
			Для Каждого ЗаписьНабора Из Движения.ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям Цикл
				Если ЗаписьНабора.ДатаНачала = '00010101' Тогда
					ЗаписьНабора.ДатаНачала = ЗаписьНабора.Период;
					ЗаписьНабора.ДатаОкончания = НачалоДня(КонецМесяца(ЗаписьНабора.Период));
				КонецЕсли;
			КонецЦикла;
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(Движения.ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям);
		Исключение
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка при формировании движений документа по регистру Данные для расчета среднего заработка по начислениям: %1';
					|en = 'Ошибка при формировании движений документа по регистру Данные для расчета среднего заработка по начислениям: %1'"),
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.РегистрыНакопления.ДанныеДляРасчетаСреднегоЗаработкаПоНачислениям, ,
				ТекстСообщения);
		КонецПопытки;
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
	КонецЦикла;
		
	Если ЗавершитьОбработку Тогда 
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
