#Область СлужебныеПроцедурыИФункции

Процедура НастроитьПолеВидДоходаИсполнительногоПроизводства(Форма, ИмяЭлемента = "ВидДоходаИсполнительногоПроизводства") Экспорт
	ОбщегоНазначенияБЗККлиентСервер.УстановитьОбязательностьПоляВводаФормы(
		Форма, ИмяЭлемента, 
		ВидДоходаИсполнительногоПроизводстваОбязателен(Форма.Объект));
КонецПроцедуры

Функция ВидДоходаИсполнительногоПроизводстваОбязателен(Ведомость) Экспорт
	Возврат ВидыДоходовИсполнительногоПроизводстваКлиентСервер.ВидДоходаОбязателенДляБанков(ДатаВыплаты(Ведомость));
КонецФункции

Процедура УстановитьПредставлениеОснований(Форма) Экспорт
	
	Если ЗначениеЗаполнено(Форма.Объект.СпособВыплаты) Тогда
		Если Форма.Объект.Основания.Количество() = 0 Тогда
			Форма.ОснованияПредставление = НСтр("ru = 'Не выбраны, заполнение по всем документам';
												|en = 'Not selected, fill in by all documents'");
		Иначе	
			Форма.ОснованияПредставление = ЧислоПрописью(Форма.Объект.Основания.Количество(),,НСтр("ru = 'документ, документа, документов, м, ,,,,0';
																									|en = 'document, documents,,,0'"));
		КонецЕсли;	
	Иначе
		Форма.ОснованияПредставление = "";
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьОтображениеВХО(Форма) Экспорт
	
	Если Форма.Объект.ПеречислениеНДФЛВыполнено Тогда
		Форма.Элементы.ПеречислениеНДФЛСтраницы.ТекущаяСтраница = Форма.Элементы.ПеречислениеНДФЛСтраницаПолностью
	Иначе
		Форма.Элементы.ПеречислениеНДФЛСтраницы.ТекущаяСтраница = Форма.Элементы.ПеречислениеНДФЛСтраницаЧастично
	КонецЕсли;
	
	УстановитьОтображениеВХОВыплатыДоходов(Форма);
	
КонецПроцедуры

Процедура УстановитьОтображениеВХОВыплатыДоходов(Форма)
	
	Ведомость = Форма.Объект.Ссылка;
	
	Форма.ДатаВыплаты = ДатаВыплаты(Форма.Объект);
	
	ЕстьПодтверждениеВыплатыДоходов = ЗарплатаКадрыПриложенияВызовСервера.ЕстьПодтверждениеВыплатыДоходовПоВедомости(Ведомость);
	Если ЕстьПодтверждениеВыплатыДоходов Тогда
		Форма.Элементы.УточнениеДатыВыплаты.ТекущаяСтраница = Форма.Элементы.ПодтверждениеВыплатыГруппа
	Иначе
		Форма.Элементы.УточнениеДатыВыплаты.ТекущаяСтраница = Форма.Элементы.ДатаВыплатыГруппа
	КонецЕсли;
	
КонецПроцедуры

Функция ДатаВыплаты(Ведомость) Экспорт
	Возврат ?(ЗначениеЗаполнено(Ведомость.ДатаВыплаты), Ведомость.ДатаВыплаты, НачалоДня(Ведомость.Дата));
КонецФункции

#КонецОбласти

