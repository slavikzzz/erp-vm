#Область ПрограммныйИнтерфейс

#Область НастройкаСчетовУчетаПрочихОпераций

// Очищает статью и аналитику в строке таблицы, если они не доступны в строке ТЧ.
//
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - Форма объекта.
// 	СтрокаТаблицы - ДанныеФормыЭлементКоллекции - Строка таблицы.
// 	ПутьКДаннымТаблицы - Строка - Путь к данным таблицы формы, например "Объект.Товары".
//
Процедура ПриИзмененииРеквизитаДоступностиСчетаУчетаВСтроке(Форма, СтрокаТаблицы, ПутьКДаннымТаблицы) Экспорт
	
	//++ НЕ УТ
	ДанныеНастройкиСчетовУчета = ОбщегоНазначенияУТКлиентСервер.ПолучитьДанныеМеханизмаИзКэшаФормы(Форма,
		"НастройкаСчетовУчета");

	Если ДанныеНастройкиСчетовУчета = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Для Каждого Параметры Из ДанныеНастройкиСчетовУчета.МассивПараметровНастройки Цикл
		Если Параметры.ПутьКДанным = ПутьКДаннымТаблицы Тогда
			ДоступностьВСтроке = Истина;
			Для Каждого КлючИЗначение Из Параметры.УсловияДоступностиСчетаУчетаВСтроках Цикл
				Если СтрокаТаблицы[КлючИЗначение.Ключ] <> КлючИЗначение.Значение Тогда
					ДоступностьВСтроке = Ложь;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если Не ДоступностьВСтроке Тогда
				Если ЗначениеЗаполнено(СтрокаТаблицы[Параметры.НастройкаСчетовУчета]) Тогда
					СтрокаТаблицы[Параметры.НастройкаСчетовУчета] = Неопределено;
					Если ДанныеНастройкиСчетовУчета.ДоступнаНастройкаСчетовУчета Тогда
						СтрокаТаблицы[Параметры.Представление] = ПредставлениеНастройкиОтражения(СтрокаТаблицы,
							Параметры, ДанныеНастройкиСчетовУчета);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	//-- НЕ УТ
КонецПроцедуры

// Устанавливает видимость настройки счетов учета при изменении типа статьи, заполняет настройки счетов учета статьи по умолчанию.
// Вызывается из механизма выбора статей и аналитик (См. ДоходыИРасходыСервер).
//
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - Форма объекта.
// 	ПутьКДанным - Строка - Значение поля "ПутьКДанным" параметров выбора статей и аналитик (См. ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики).
// 	ТипСтатьи - Строка - Значение поля "ТипСтатьи" параметров выбора статей и аналитик (См. ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики).
// 	ИдентификаторыСтрок - Число, Массив, Неопределено - Идентификаторы строк таблицы, в которых изменился тип статьи.
//
Процедура ПриИзмененииСтатьи(Форма, ПутьКДанным, ТипСтатьи, ИдентификаторыСтрок = Неопределено) Экспорт
	
	//++ НЕ УТ
	ДанныеНастройкиСчетовУчета = ОбщегоНазначенияУТКлиентСервер.ПолучитьДанныеМеханизмаИзКэшаФормы(Форма,
		"НастройкаСчетовУчета");

	Если ДанныеНастройкиСчетовУчета = Неопределено Тогда
		Возврат;
	ИначеЕсли Не ДанныеНастройкиСчетовУчета.ДоступнаНастройкаСчетовУчета Тогда
		Возврат;
	КонецЕсли;

	Если ИдентификаторыСтрок <> Неопределено И ТипЗнч(ИдентификаторыСтрок) <> Тип("Массив") Тогда
		ИдентификаторыСтрок = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИдентификаторыСтрок);
	КонецЕсли;

	Для Каждого ПараметрыНастройки Из ДанныеНастройкиСчетовУчета.МассивПараметровНастройки Цикл
		Если ПутьКДанным = ПараметрыНастройки.ПутьКДанным Тогда
			Если ЗначениеЗаполнено(ТипСтатьи) И ТипСтатьи = ПараметрыНастройки.ТипСтатьи Тогда
				УстановитьВидимостьЭлементовПоПараметрам(Форма, ПараметрыНастройки, ДанныеНастройкиСчетовУчета);
			КонецЕсли;
			ЗаполнитьНастройкиСчетовУчетаПоУмолчанию(Форма, ПараметрыНастройки, ИдентификаторыСтрок);
			ЗаполнитьПредставлениеНастройкиПоПараметрам(Форма, ПараметрыНастройки, ИдентификаторыСтрок);
		КонецЕсли;
	КонецЦикла;
	//-- НЕ УТ

КонецПроцедуры

// Дополняет структуру действий механизма ОбработкаТабличнойЧасти действием заполнения настройки счетов учета по умолчанию
// по данным статьи активов и пассивов.
// 
// Параметры:
//  СтруктураДействий - Структура - Структура действий
//  ФормаИлиПараметры - ФормаКлиентскогоПриложения, Структура, Массив - При вызове из формы передается форма объекта. Из формы получаются закэшированные данные настройки счетов учета. 
//                                                             При вызове из объекта - параметры настройки счетов (см. НастройкаСчетовУчетаСервер.ПараметрыНастройки).
//  ПутьКДанным - Строка - Путь к данным настройки счетов учета. Значение по умолчанию "Объект.Товары".
//
Процедура ВставитьДействиеЗаполнитьНастройкуСчетовУчетаПоУмолчанию(СтруктураДействий, ФормаИлиПараметры, ПутьКДанным = "Объект.Товары") Экспорт
	
	//++ НЕ УТ
	Если ТипЗнч(ФормаИлиПараметры) = Тип("ФормаКлиентскогоПриложения") Тогда
		ДанныеНастройкиСчетовУчета = ОбщегоНазначенияУТКлиентСервер.ПолучитьДанныеМеханизмаИзКэшаФормы(ФормаИлиПараметры, "НастройкаСчетовУчета");
		ДействиеВФорме = Истина;
	Иначе
		ДанныеНастройкиСчетовУчета = НовыеДанныеНастройкиСчетовУчета();
		ДанныеНастройкиСчетовУчета.ДоступнаНастройкаСчетовУчета = НастройкаСчетовУчетаСлужебныйВызовСервера.ДоступнаНастройкаСчетовУчета();
		Если ТипЗнч(ФормаИлиПараметры) = Тип("Массив") Тогда
			ДанныеНастройкиСчетовУчета.МассивПараметровНастройки = ФормаИлиПараметры;
		Иначе
			ДанныеНастройкиСчетовУчета.МассивПараметровНастройки = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФормаИлиПараметры);
		КонецЕсли;
		ДействиеВФорме = Ложь;
	КонецЕсли;

	Если ДанныеНастройкиСчетовУчета = Неопределено Тогда
		Возврат;
	ИначеЕсли НЕ ДанныеНастройкиСчетовУчета.ДоступнаНастройкаСчетовУчета Тогда
		Возврат;
	КонецЕсли;
	
	КолонкиТабличнойЧасти     = Новый Массив();
	МассивПараметровНастройки = Новый Массив();
	
	Для Каждого ПараметрыНастройки Из ДанныеНастройкиСчетовУчета.МассивПараметровНастройки Цикл
		Если ПутьКДанным <> ПараметрыНастройки.ПутьКДанным Тогда
			Продолжить;
		КонецЕсли;
		
		МассивПараметровНастройки.Добавить(ПараметрыНастройки);
		
		КолонкиТабличнойЧасти.Добавить(ПараметрыНастройки.НастройкаСчетовУчета);
		КолонкиТабличнойЧасти.Добавить(ПараметрыНастройки.СтатьяАктивовПассивов);
		Если ЗначениеЗаполнено(ПараметрыНастройки.ТипСтатьи) Тогда
			КолонкиТабличнойЧасти.Добавить(ПараметрыНастройки.ТипСтатьи);
		КонецЕсли;
	КонецЦикла;
	
	ПараметрыДействия = Новый Структура();
	ПараметрыДействия.Вставить("ДействиеВФорме", ДействиеВФорме);
	ПараметрыДействия.Вставить("КолонкиТабличнойЧасти", КолонкиТабличнойЧасти);
	ПараметрыДействия.Вставить("ДанныеНастройкиСчетовУчета", НовыеДанныеНастройкиСчетовУчета());
	ПараметрыДействия.ДанныеНастройкиСчетовУчета.МассивПараметровНастройки = МассивПараметровНастройки;
	ПараметрыДействия.ДанныеНастройкиСчетовУчета.ДоступнаНастройкаСчетовУчета = Истина;
	
	Если ДействиеВФорме Тогда
		ПараметрыДействия.ДанныеНастройкиСчетовУчета.НастройкиСчетовУчета = ДанныеНастройкиСчетовУчета.НастройкиСчетовУчета;
		ПараметрыДействия.ДанныеНастройкиСчетовУчета.ИспользуетсяРегламентированныйУчет = ДанныеНастройкиСчетовУчета.ИспользуетсяРегламентированныйУчет;
		ПараметрыДействия.ДанныеНастройкиСчетовУчета.ИспользуетсяМеждународныйУчет = ДанныеНастройкиСчетовУчета.ИспользуетсяМеждународныйУчет;
		ПараметрыДействия.ДанныеНастройкиСчетовУчета.СтатьиСНастройкамиПоОбъектам = ДанныеНастройкиСчетовУчета.СтатьиСНастройкамиПоОбъектам;
	КонецЕсли;
	
	СтруктураДействий.Вставить("ЗаполнитьНастройкуСчетовУчетаПоУмолчанию", ПараметрыДействия);
	
	Если ДействиеВФорме Тогда
		ВставитьДействиеЗаполнитьПредставлениеНастройкиСчетовУчета(СтруктураДействий, ФормаИлиПараметры, ПутьКДанным);
	КонецЕсли;
	//-- НЕ УТ
	
КонецПроцедуры

// Дополняет структуру действий механизма ОбработкаТабличнойЧасти действием заполнения представления настройки счетов учета.
// Действие возможно использовать только в форме.
// 
// Параметры:
// 	СтруктураДействий - Структура - Структура действий
// 	Форма - ФормаКлиентскогоПриложения - Из формы получаются закэшированные параметры настройки счетов учета. 
// 	ПутьКДанным - Строка - Путь к данным настройки счетов учета. Значение по умолчанию "Объект.Товары".
//
Процедура ВставитьДействиеЗаполнитьПредставлениеНастройкиСчетовУчета(СтруктураДействий, Форма, ПутьКДанным = "Объект.Товары") Экспорт
	
	//++ НЕ УТ
	ДанныеНастройкиСчетовУчета = ОбщегоНазначенияУТКлиентСервер.ПолучитьДанныеМеханизмаИзКэшаФормы(Форма, "НастройкаСчетовУчета");
	
	Если ДанныеНастройкиСчетовУчета = Неопределено Тогда
		Возврат;
	ИначеЕсли НЕ ДанныеНастройкиСчетовУчета.ДоступнаНастройкаСчетовУчета Тогда
		Возврат;
	КонецЕсли;
	
	КолонкиТабличнойЧасти     = Новый Массив;
	МассивПараметровНастройки = Новый Массив;
	
	Для каждого ПараметрыНастройки Из ДанныеНастройкиСчетовУчета.МассивПараметровНастройки Цикл
		
		Если ПутьКДанным <> ПараметрыНастройки.ПутьКДанным Тогда
			Продолжить;
		КонецЕсли;
		
		МассивПараметровНастройки.Добавить(ПараметрыНастройки);
		
		КолонкиТабличнойЧасти.Добавить(ПараметрыНастройки.НастройкаСчетовУчета);
		КолонкиТабличнойЧасти.Добавить(ПараметрыНастройки.Представление);
		Если ЗначениеЗаполнено(ПараметрыНастройки.ТипСтатьи) Тогда
			КолонкиТабличнойЧасти.Добавить(ПараметрыНастройки.ТипСтатьи);
		КонецЕсли;
		
	КонецЦикла;
	
	ПараметрыДействия = Новый Структура;
	
	ПараметрыДействия.Вставить("КолонкиТабличнойЧасти", КолонкиТабличнойЧасти);
	ПараметрыДействия.Вставить("ДанныеНастройкиСчетовУчета", НовыеДанныеНастройкиСчетовУчета());
	ПараметрыДействия.ДанныеНастройкиСчетовУчета.МассивПараметровНастройки = МассивПараметровНастройки;
	ПараметрыДействия.ДанныеНастройкиСчетовУчета.НастройкиСчетовУчета = ДанныеНастройкиСчетовУчета.НастройкиСчетовУчета;
	ПараметрыДействия.ДанныеНастройкиСчетовУчета.ИспользуетсяРегламентированныйУчет = ДанныеНастройкиСчетовУчета.ИспользуетсяРегламентированныйУчет;
	ПараметрыДействия.ДанныеНастройкиСчетовУчета.ИспользуетсяМеждународныйУчет = ДанныеНастройкиСчетовУчета.ИспользуетсяМеждународныйУчет;
	ПараметрыДействия.ДанныеНастройкиСчетовУчета.СтатьиСНастройкамиПоОбъектам = ДанныеНастройкиСчетовУчета.СтатьиСНастройкамиПоОбъектам;
	ПараметрыДействия.ДанныеНастройкиСчетовУчета.ДоступнаНастройкаСчетовУчета = Истина;
	
	СтруктураДействий.Вставить("ЗаполнитьПредставлениеНастройкиСчетовУчета", ПараметрыДействия);
	//-- НЕ УТ
	
КонецПроцедуры

// Возвращает структуру полей настройки счетов учета операции.
// 
// Возвращаемое значение:
//  Структура - Описание:
//++ Локализация
//  * СчетУчета - ПланСчетовСсылка.Хозрасчетный - счет регламентированного учета.
//  * Субконто1 - Произвольный - субконто 1 счета регламентированного учета
//  * Субконто2 - Произвольный - субконто 2 счета регламентированного учета
//  * Субконто3 - Произвольный - субконто 3 счета регламентированного учета
//-- Локализация
//  * НастройкиМеждународногоУчета - Массив - настройки счетов международного учета, см. СоставНастройкиСчетовМеждународногоУчета
//
Функция СоставНастройкиСчетовУчета() Экспорт

	СоставНастройкиСчетов = Новый Структура();
	//++ НЕ УТ

	//++ Локализация
	СоставНастройкиСчетов.Вставить("СчетУчета", ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.ПустаяСсылка"));
	СоставНастройкиСчетов.Вставить("Субконто1", Неопределено);
	СоставНастройкиСчетов.Вставить("Субконто2", Неопределено);
	СоставНастройкиСчетов.Вставить("Субконто3", Неопределено);
	//-- Локализация

	//++ НЕ УТКА
	СоставНастройкиСчетов.Вставить("НастройкиМеждународногоУчета", Новый Массив);
	//-- НЕ УТКА

	//-- НЕ УТ

	Возврат СоставНастройкиСчетов;

КонецФункции

// Возвращает структуру полей настройки учета операции на счете международного учета.
// Используется для заполнения свойства НастройкиМеждународногоУчета состава настройки счетов учета операции.
// 
// Возвращаемое значение:
//  Структура - Описание:
//  * ПланСчетов - СправочникСсылка.ПланыСчетовМеждународногоУчета - план счетов международного учета.
//  * СчетУчета - ПланСчетовСсылка.Международный - счет международного учета.
//  * Субконто1 - Произвольный - субконто 1 счета международного учета
//  * Субконто2 - Произвольный - субконто 2 счета международного учета
//  * Субконто3 - Произвольный - субконто 3 счета международного учета
//
Функция СоставНастройкиСчетовМеждународногоУчета() Экспорт

	СоставНастройкиСчетов = Новый Структура();
	//++ НЕ УТКА
	СоставНастройкиСчетов.Вставить("ПланСчетов", ПредопределенноеЗначение("Справочник.ПланыСчетовМеждународногоУчета.ПустаяСсылка"));
	СоставНастройкиСчетов.Вставить("СчетУчета",  ПредопределенноеЗначение("ПланСчетов.Международный.ПустаяСсылка"));
	СоставНастройкиСчетов.Вставить("Субконто1",  Неопределено);
	СоставНастройкиСчетов.Вставить("Субконто2",  Неопределено);
	СоставНастройкиСчетов.Вставить("Субконто3",  Неопределено);
	//-- НЕ УТКА

	Возврат СоставНастройкиСчетов;

КонецФункции

#КонецОбласти

// Выполняет обработку изменения реквизитов аналитики учета, влияющих на настройку счетов учета.
// 
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - Форма объекта настройки
// 	ИмяЭлемента - Строка - Имя элемента реквизита
//
Процедура ПриИзмененииРеквизита(Форма, ИмяЭлемента = Неопределено) Экспорт
	//++ Локализация
	ДанныеНастройкиСчетовУчета = ОбщегоНазначенияУТКлиентСервер.ПолучитьДанныеМеханизмаИзКэшаФормы(Форма,
		"НастройкаСчетовУчета");
	Если ДанныеНастройкиСчетовУчета = Неопределено Тогда
		Возврат;
	КонецЕсли;
	//++ НЕ УТ
	НастройкаСчетовУчетаКлиентСервер.ЗаполнитьРеквизитыНастройкиСчетовУчетаПоДаннымОбъектаАналитики(Форма, Форма.Объект);
	НастройкаСчетовУчетаКлиентСервер.УстановитьСвойстваЭлементовНастройкиСчетовУчета(Форма, Истина);
	//-- НЕ УТ

	//-- Локализация
КонецПроцедуры

//++ Локализация

// Возвращает виды счетов регл. учета, для которых будет добавлена возможность настройки долгосрочного счета учета.
//
// Возвращаемое значение:
//	Массив - массив из Строка:
//		Имя вида счета регл. учета строкой, для которого надо будет дать возможность настройки долгосрочного счета учета.
//
Функция ВидыСчетовСНастройкамиДолгосрочнойЗадолженности() Экспорт
	
	МассивВозврата = Новый Массив;
	
	МассивВозврата.Добавить("РасчетыСКлиентами");
	МассивВозврата.Добавить("АвансыПолученные");
	МассивВозврата.Добавить("РасчетыСПоставщиками");
	МассивВозврата.Добавить("АвансыВыданные");
	МассивВозврата.Добавить("ПроцентыПоДисконтированию");
	
	МассивВозврата.Добавить("РасчетыСКредиторамиОсновнойДолг");
	
	МассивВозврата.Добавить("РасчетыСДебиторамиОсновнойДолг");
	
	МассивВозврата.Добавить("АрендныеОбязательства");
	
	Возврат МассивВозврата;
	
КонецФункции

//-- Локализация

#Область НастройкаВыбораСубконто

// Изменяет параметры выбора полей субконто.
//
// Параметры:
//	Форма - ФормаКлиентскогоПриложения - Форма, на которой расположены поля субконто.
//	Объект - ДанныеФормыСтруктура - Объект, форма которого отображается.
//	ШаблонИмяПоляОбъекта - Строка - Шаблон имени поля объекта, содержащего субконто.
//	ШаблонИмяЭлементаФормы - Строка - Шаблон имени поля формы, в который выводится субконто.
//	СписокПараметров - Структура - Содержит ключи со значениями отборов.
//
Процедура ИзменитьПараметрыВыбораПолейСубконто(Форма, Объект, ШаблонИмяПоляОбъекта, ШаблонИмяЭлементаФормы,
	СписокПараметров) Экспорт

	ВидыПараметров = Новый Соответствие;
	ВидыПараметров.Вставить(Тип("СправочникСсылка.БанковскиеСчетаОрганизаций"), "БанковскийСчетОрганизации");
	ВидыПараметров.Вставить(Тип("СправочникСсылка.БанковскиеСчетаКонтрагентов"), "БанковскийСчетКонтрагента");
	ВидыПараметров.Вставить(Тип("СправочникСсылка.СтруктураПредприятия"), "Подразделение");
	ВидыПараметров.Вставить(Тип("СправочникСсылка.ДоговорыКонтрагентов"), "Договор");
	ВидыПараметров.Вставить(Тип("СправочникСсылка.ДоговорыАренды"), "Договор");
	ВидыПараметров.Вставить(Тип("СправочникСсылка.ДоговорыКредитовИДепозитов"), "Договор");
	ВидыПараметров.Вставить(Тип("СправочникСсылка.Партнеры"), "Партнер");
	ВидыПараметров.Вставить(Тип("СправочникСсылка.Субконто"), "Субконто");
	ВидыПараметров.Вставить(Тип("СправочникСсылка.НомераГТД"), "НомераГТД");
	ВидыПараметров.Вставить(Тип("СправочникСсылка.Номенклатура"), "Номенклатура");
	
	//++ Локализация
	ВидыПараметров.Вставить(Тип("СправочникСсылка.РегистрацииВНалоговомОргане"), "РегистрацияВИФНС");
	ВидыПараметров.Вставить(Тип("СправочникСсылка.ПартииТМЦВЭксплуатации"), "ПартииТМЦВЭксплуатации");
	ВидыПараметров.Вставить(Тип("СправочникСсылка.Резервы"), "ВидыРезервов");
	//-- Локализация

	ОписаниеТипаВсеДокументы = НастройкаСчетовУчетаСлужебныйВызовСервера.ОписаниеТипаВсеДокументы();

	ОчищатьСвязанныеСубконто = Ложь;
	ТипыСвязанныхСубконто    = Неопределено;
	Если ТипЗнч(Форма.ТекущийЭлемент) = Тип("ТаблицаФормы") Тогда
		ТекущийЭлемент = Форма.ТекущийЭлемент.ТекущийЭлемент;
	Иначе
		ТекущийЭлемент = Форма.ТекущийЭлемент;
	КонецЕсли;
	ИмяТекущегоЭлемента = ?(ТипЗнч(ТекущийЭлемент) = Тип("ПолеФормы"), ТекущийЭлемент.Имя, "");

	Для Индекс = 1 По 3 Цикл
		ИмяЭлементаФормы = СтрЗаменить(ШаблонИмяЭлементаФормы, "%Индекс%", Индекс);
		ИмяПоляОбъекта   = СтрЗаменить(ШаблонИмяПоляОбъекта, "%Индекс%", Индекс);
		ТипПоляОбъекта   = ТипЗнч(Объект[ИмяПоляОбъекта]);

		МассивПараметров = Новый Массив;

		ВидПараметра = ВидыПараметров[ТипПоляОбъекта];

		Если ВидПараметра <> Неопределено Тогда

			Если ВидПараметра = "Договор" Тогда

				Если СписокПараметров.Свойство("Организация") Тогда
					МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Организация", СписокПараметров.Организация));
				КонецЕсли;
				Если СписокПараметров.Свойство("Контрагент") Тогда
					МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Контрагент", СписокПараметров.Контрагент));
				КонецЕсли;
				Если СписокПараметров.Свойство("Партнер") Тогда
					МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Партнер", СписокПараметров.Партнер));
				КонецЕсли;

			ИначеЕсли ВидПараметра = "БанковскийСчетОрганизации" Тогда

				МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Владелец", СписокПараметров.Организация));

			ИначеЕсли ВидПараметра = "БанковскийСчетКонтрагента" Тогда

				Если СписокПараметров.Свойство("Контрагент") Тогда
					МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Владелец", СписокПараметров.Контрагент));
				ИначеЕсли СписокПараметров.Свойство("ФизическоеЛицо") Тогда
					МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Владелец", СписокПараметров.ФизическоеЛицо));
				КонецЕсли;

			ИначеЕсли ВидПараметра = "НомераГТД" Тогда

				Если СписокПараметров.Свойство("СтранаПроисхождения") Тогда
					МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.СтранаПроисхождения",
						СписокПараметров.СтранаПроисхождения));
				КонецЕсли;

			ИначеЕсли ВидПараметра = "Номенклатура" Тогда

				Если СписокПараметров.Свойство("ГруппаАналитическогоУчетаНоменклатуры") Тогда
					МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.ГруппаАналитическогоУчета",
						СписокПараметров.ГруппаАналитическогоУчетаНоменклатуры));
				КонецЕсли;
				Если СписокПараметров.Свойство("ГруппаФинУчетаНоменклатуры") Тогда
					МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.ГруппаФинансовогоУчета",
						СписокПараметров.ГруппаФинУчетаНоменклатуры));
				КонецЕсли;
				Если СписокПараметров.Свойство("ТипНоменклатуры") Тогда
					МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.ТипНоменклатуры",
						СписокПараметров.ТипНоменклатуры));
				КонецЕсли;

			ИначеЕсли ВидПараметра = "Субконто" И СписокПараметров.Свойство("СчетУчета") Тогда

				СвойстваСчета = НастройкаСчетовУчетаСлужебныйВызовСервера.ПолучитьСвойстваСчета(
					СписокПараметров.СчетУчета);
				МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Владелец", СвойстваСчета["ВидСубконто" + Индекс]));
			
			//++ Локализация
			ИначеЕсли ВидПараметра = "РегистрацияВИФНС" Тогда

				Если СписокПараметров.Свойство("Организация") Тогда
					ГоловнаяОрганизация = ОбщегоНазначенияБПВызовСервераПовтИсп.ГоловнаяОрганизация(
						СписокПараметров.Организация);
					МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Владелец", ГоловнаяОрганизация));
				КонецЕсли;

			ИначеЕсли ВидПараметра = "ПартииТМЦВЭксплуатации" Тогда

				ОграничениеВыбора = Ложь;
				Если СписокПараметров.Свойство("Организация") Тогда
					МассивПараметров.Добавить(Новый ПараметрВыбора("Организация", СписокПараметров.Организация));
					ОграничениеВыбора = Истина;
				КонецЕсли;
				Если СписокПараметров.Свойство("Номенклатура") Тогда
					МассивПараметров.Добавить(Новый ПараметрВыбора("Номенклатура", СписокПараметров.Номенклатура));
					ОграничениеВыбора = Истина;
				КонецЕсли;
				Если СписокПараметров.Свойство("ФизическоеЛицо") Тогда
					МассивПараметров.Добавить(Новый ПараметрВыбора("ФизическоеЛицо", СписокПараметров.ФизическоеЛицо));
					ОграничениеВыбора = Истина;
				КонецЕсли;
				Если ОграничениеВыбора Тогда
					МассивПараметров.Добавить(Новый ПараметрВыбора("ОграничениеВыбора", Истина));
				КонецЕсли;  
				
			ИначеЕсли ВидПараметра = "ВидыРезервов" Тогда

				СвойстваСчета = НастройкаСчетовУчетаСлужебныйВызовСервера.ПолучитьСвойстваСчета(
					СписокПараметров.СчетУчета);
				Если СвойстваСчета.Родитель = ПредопределенноеЗначение(
					"ПланСчетов.Хозрасчетный.ОценочныеОбязательстваПоВознаграждениямРаботникам") Тогда
					МассивПараметров.Добавить(Новый ПараметрВыбора("КлючНазначенияИспользования", "РезервыПоОплатеТруда"));
				КонецЕсли;
			//-- Локализация
			КонецЕсли;

		КонецЕсли;

		Если МассивПараметров.Количество() = 0 И ОписаниеТипаВсеДокументы.СодержитТип(ТипПоляОбъекта) Тогда

			Если СписокПараметров.Свойство("Организация") Тогда
				МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Организация", СписокПараметров.Организация));
			КонецЕсли;
			Если СписокПараметров.Свойство("Контрагент") Тогда
				МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Контрагент", СписокПараметров.Контрагент));
			КонецЕсли;
			Если СписокПараметров.Свойство("Партнер") Тогда
				МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Партнер", СписокПараметров.Партнер));
			КонецЕсли;
			Если СписокПараметров.Свойство("ФизическоеЛицо") Тогда
				МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.ПодотчетноеЛицо",
					СписокПараметров.ФизическоеЛицо));
			КонецЕсли;

		КонецЕсли;

		Если МассивПараметров.Количество() > 0 Тогда
			ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
			Форма.Элементы[ИмяЭлементаФормы].ПараметрыВыбора = ПараметрыВыбора;
		ИначеЕсли Форма.Элементы[ИмяЭлементаФормы].ПараметрыВыбора.Количество() > 0 Тогда
			Форма.Элементы[ИмяЭлементаФормы].ПараметрыВыбора = Новый ФиксированныйМассив(Новый Массив);
		КонецЕсли;

		Если ОчищатьСвязанныеСубконто И ЗначениеЗаполнено(Объект[ИмяПоляОбъекта]) Тогда

			Если ТипыСвязанныхСубконто = Неопределено Тогда
				ВсеТипыСвязанныхСубконто = НастройкаСчетовУчетаСлужебныйВызовСервера.ВсеТипыСвязанныхСубконто();
				ТипыСвязанныхСубконто    = Новый ОписаниеТипов(Новый Массив);
				Для Каждого Параметр Из СписокПараметров Цикл
					Если ВсеТипыСвязанныхСубконто[Параметр.Ключ] <> Неопределено Тогда
						ТипыСвязанныхСубконто = Новый ОписаниеТипов(ТипыСвязанныхСубконто,
							ВсеТипыСвязанныхСубконто[Параметр.Ключ].Типы());
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;

			Если ТипыСвязанныхСубконто.СодержитТип(ТипПоляОбъекта) Тогда
				Объект[ИмяПоляОбъекта] = Новый (ТипПоляОбъекта);
			КонецЕсли;

		КонецЕсли;

		Если ИмяТекущегоЭлемента = ИмяЭлементаФормы Тогда
			ОчищатьСвязанныеСубконто = Истина; // Очищаются только субконто с номером больше текущего
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

// Функция формирует структуру ключей со значениями отбора для субконто.
//
// Параметры:
//	Организация - СправочникСсылка.Организации - Организация, для который выполняется настройка субконто.
//	ТекущиеДанные - РегистрБухгалтерииЗаписьИмяРегистраБухгалтерии - Запись, для которой настраиваются параметры выбора.
//	ШаблонИмяПоляОбъекта - Строка - Шаблон имени поля объекта, содержащего субконто.
//	ИмяПоляСчетУчета - Строка - Шаблон имени поля формы, в который выводится счет.
//
//Возвращаемое значение:
//   Структура - Содержит ключи со значениями отборов.
//
Функция ПараметрыВыбораСубконто(Организация, ТекущиеДанные, ШаблонИмяПоляОбъекта, ИмяПоляСчетУчета) Экспорт

	СписокПараметров = Новый Структура;
	СписокПараметров.Вставить("ОстаткиОбороты", "Кт");
	СписокПараметров.Вставить("СчетУчета", ТекущиеДанные[ИмяПоляСчетУчета]);
	
	Если ЗначениеЗаполнено(Организация) Тогда
		СписокПараметров.Вставить("Организация", Организация);
	КонецЕсли;
	
	Для Индекс = 1 По 3 Цикл
		ИмяПоля = СтрЗаменить(ШаблонИмяПоляОбъекта, "%Индекс%", Индекс);

		Если ТипЗнч(ТекущиеДанные[ИмяПоля]) = Тип("СправочникСсылка.Контрагенты") Тогда
			СписокПараметров.Вставить("Контрагент", ТекущиеДанные[ИмяПоля]);
		ИначеЕсли ТипЗнч(ТекущиеДанные[ИмяПоля]) = Тип("СправочникСсылка.Партнеры") Тогда
			СписокПараметров.Вставить("Партнер", ТекущиеДанные[ИмяПоля]);
		ИначеЕсли ТипЗнч(ТекущиеДанные[ИмяПоля]) = Тип("СправочникСсылка.Организации") Тогда
			МассивОрганизаций = Новый Массив();
			Если ЗначениеЗаполнено(Организация) Тогда
				МассивОрганизаций.Добавить(Организация);
			КонецЕсли;
			МассивОрганизаций.Добавить(ТекущиеДанные[ИмяПоля]);
			СписокПараметров.Вставить("Организация", Новый ФиксированныйМассив(МассивОрганизаций));
		ИначеЕсли НастройкаСчетовУчетаСлужебныйВызовСервера.ОписаниеТиповДоговоров(Истина).СодержитТип(ТипЗнч(
			ТекущиеДанные[ИмяПоля])) Тогда
			СписокПараметров.Вставить("ДоговорКонтрагента", ТекущиеДанные[ИмяПоля]);
		ИначеЕсли ТипЗнч(ТекущиеДанные[ИмяПоля]) = Тип("СправочникСсылка.Номенклатура") Тогда
			СписокПараметров.Вставить("Номенклатура", ТекущиеДанные[ИмяПоля]);
		ИначеЕсли ТипЗнч(ТекущиеДанные[ИмяПоля]) = Тип("СправочникСсылка.Склады") Тогда
			СписокПараметров.Вставить("Склад", ТекущиеДанные[ИмяПоля]);
		ИначеЕсли ТипЗнч(ТекущиеДанные[ИмяПоля]) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
			СписокПараметров.Вставить("ФизическоеЛицо", ТекущиеДанные[ИмяПоля]);
		ИначеЕсли ТипЗнч(ТекущиеДанные[ИмяПоля]) = Тип("СправочникСсылка.СтраныМира") Тогда
			СписокПараметров.Вставить("СтранаПроисхождения", ТекущиеДанные[ИмяПоля]);
		ИначеЕсли ТипЗнч(ТекущиеДанные[ИмяПоля]) = Тип("СправочникСсылка.ГруппыАналитическогоУчетаНоменклатуры") Тогда
			СписокПараметров.Вставить("ГруппаАналитическогоУчетаНоменклатуры", ТекущиеДанные[ИмяПоля]);
		ИначеЕсли ТипЗнч(ТекущиеДанные[ИмяПоля]) = Тип("СправочникСсылка.ГруппыФинансовогоУчетаНоменклатуры") Тогда
			СписокПараметров.Вставить("ГруппаФинУчетаНоменклатуры", ТекущиеДанные[ИмяПоля]);
		ИначеЕсли ТипЗнч(ТекущиеДанные[ИмяПоля]) = Тип("ПеречислениеСсылка.ТипыНоменклатуры") Тогда
			СписокПараметров.Вставить("ТипНоменклатуры", ТекущиеДанные[ИмяПоля]);
		КонецЕсли;

	КонецЦикла;

	Возврат СписокПараметров;

КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область НастройкаСчетовУчетаПрочихОпераций

// Возвращает строку из имен реквизитов настройки счетов учета в формате csv
//
// Возвращаемое значение:
//  Строка - имена реквизитов, разделенные запятыми
//
Функция РеквизитыНастройкиСчетаУчета() Экспорт

	Возврат "СчетУчета,Субконто1,Субконто2,Субконто3";

КонецФункции

// Возвращает структуру данных настроек счетов учета, используемую в механизме настроек счетов учета
//
// Возвращаемое значение:
//  Структура:
//  * МассивПараметровНастройки - Массив из см. НастройкаСчетовУчетаСервер.ПараметрыНастройки
//  * СоответствиеПараметровНастройкиЭлементамФормы - Структура:
//   ** Ключ - Строка - Имя элемента управления формы, указанного в параметрах настройки
//   ** Значение - Число - Индекс параметров настройки в массиве параметров настройки
//  * ДоступнаНастройкаСчетовУчета - Булево - Признак доступности редактирования настройки пользователем
//  * НастройкиСчетовУчета - Соответствие из КлючИЗначение:
//   ** Ключ - СправочникСсылка.НастройкиСчетовУчетаПрочихОпераций - ссылка на элемент справочника настроек
//   ** Значение см. НастройкаСчетовУчетаКлиентСервер.СоставНастройкиСчетовУчета
//  * ИспользуетсяРегламентированныйУчет - Булево - Признак использования регламентированного учета в информационной базе
//  * ИспользуетсяМеждународныйУчет - Булево - Признак использования международного учета в информационной базе
//  * СтатьиСНастройкамиПоОбъектам - Массив из ПланВидовХарактеристикСсылка.СтатьиАктивовПассивов - Статьи активов и пассивов, которым не требуется указание счетов учета в документах
//
Функция НовыеДанныеНастройкиСчетовУчета() Экспорт
	
	ДанныеНастройкиСчетовУчета = Новый Структура();
	ДанныеНастройкиСчетовУчета.Вставить("МассивПараметровНастройки", Новый Массив());
	ДанныеНастройкиСчетовУчета.Вставить("СоответствиеПараметровНастройкиЭлементамФормы", Новый Структура());
	ДанныеНастройкиСчетовУчета.Вставить("ДоступнаНастройкаСчетовУчета", Ложь);
	ДанныеНастройкиСчетовУчета.Вставить("НастройкиСчетовУчета", Новый Соответствие());
	ДанныеНастройкиСчетовУчета.Вставить("ИспользуетсяРегламентированныйУчет", Ложь);
	ДанныеНастройкиСчетовУчета.Вставить("ИспользуетсяМеждународныйУчет", Ложь);
	ДанныеНастройкиСчетовУчета.Вставить("СтатьиСНастройкамиПоОбъектам", Новый Массив());
	
	Возврат ДанныеНастройкиСчетовУчета;
	
КонецФункции

// Возвращает текст гиперссылки элемента управления настройкой счетов учета, когда настройка счетов не требуется
//
// Возвращаемое значение:
//  Строка - Текст гиперссылки
//
Функция ТекстГиперссылкиНастройкаСчетовНеТребуется() Экспорт
	
	Возврат НСтр("ru = 'Не требуется';
				|en = 'Not required'");
	
КонецФункции

// Описание
// 
// Параметры:
// 	ФормаОбъект - ФормаКлиентскогоПриложения, ДанныеФормыСтруктура -
// 	ПутьКДанным - Строка -
// 	ИдентификаторСтроки - Неопределено, Число -
// Возвращаемое значение:
// 	ДанныеФормыКоллекция, Произвольный, Неопределено - Описание
Функция ДанныеПоПути(ФормаОбъект, Знач ПутьКДанным, Знач ИдентификаторСтроки = Неопределено) Экспорт

	ЧастиПутиКДанным = СтрРазделить(ПутьКДанным, ".", Ложь);
	Если ТипЗнч(ФормаОбъект) <> Тип("ФормаКлиентскогоПриложения") И ЧастиПутиКДанным[0] = "Объект" Тогда
		ЧастиПутиКДанным.Удалить(0);
	КонецЕсли;

	Если ЧастиПутиКДанным.Количество() > 1 Тогда
		Данные = ФормаОбъект[ЧастиПутиКДанным[0]];
		Если ИдентификаторСтроки <> Неопределено И ТипЗнч(Данные) = Тип("ДанныеФормыКоллекция") Тогда
			Данные = Данные.НайтиПоИдентификатору(ИдентификаторСтроки);
			Если ЧастиПутиКДанным.Количество() = 2 Тогда
				Данные = Данные[ЧастиПутиКДанным[1]];
			КонецЕсли;
		Иначе
			Данные = ФормаОбъект[ЧастиПутиКДанным[0]][ЧастиПутиКДанным[1]];
			Если ИдентификаторСтроки <> Неопределено И ТипЗнч(Данные) = Тип("ДанныеФормыКоллекция") Тогда
				Данные = Данные.НайтиПоИдентификатору(ИдентификаторСтроки);
				Если ЧастиПутиКДанным.Количество() = 3 Тогда
					Данные = Данные[ЧастиПутиКДанным[2]];
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ЧастиПутиКДанным.Количество() = 1 Тогда
		Данные = ФормаОбъект[ЧастиПутиКДанным[0]];
		Если ИдентификаторСтроки <> Неопределено И ТипЗнч(Данные) = Тип("ДанныеФормыКоллекция") Тогда
			Данные = Данные.НайтиПоИдентификатору(ИдентификаторСтроки);
		КонецЕсли;
	Иначе
		Данные = ФормаОбъект;
	КонецЕсли;

	Возврат Данные;

КонецФункции

Функция КонтекстНастройки(Форма, ПараметрыНастройки, ВыбраннаяСтрока = Неопределено) Экспорт

	Результат = Новый Структура;

	Организация = Неопределено;
	Если ЗначениеЗаполнено(ПараметрыНастройки.Организация) Тогда
		Организация = ДанныеПоПути(Форма, ПараметрыНастройки.Организация, ВыбраннаяСтрока);
	КонецЕсли;
	Результат.Вставить("Организация", Организация);

	СтатьяАктивовПассивов = Неопределено;
	Если ЗначениеЗаполнено(ПараметрыНастройки.СтатьяАктивовПассивов) Тогда
		СтатьяАктивовПассивов = ДанныеПоПути(Форма,
			ПараметрыНастройки.ПутьКДанным + "." + ПараметрыНастройки.СтатьяАктивовПассивов, ВыбраннаяСтрока);
	КонецЕсли;
	Результат.Вставить("СтатьяАктивовПассивов", СтатьяАктивовПассивов);

	АналитикаАктивовПассивов = Неопределено;
	Если ЗначениеЗаполнено(ПараметрыНастройки.АналитикаАктивовПассивов) Тогда
		АналитикаАктивовПассивов = ДанныеПоПути(Форма,
			ПараметрыНастройки.ПутьКДанным + "." + ПараметрыНастройки.АналитикаАктивовПассивов, ВыбраннаяСтрока);
	КонецЕсли;
	Результат.Вставить("АналитикаАктивовПассивов", АналитикаАктивовПассивов);

	Возврат Результат;

КонецФункции

Функция ПредставлениеНастройкиОтражения(КонтекстНастройки, ПараметрыНастройки, ДанныеНастройкиСчетовУчета) Экспорт

	Результат = НСтр("ru = 'Настроить';
					|en = 'Configure'");
	
	Если ПараметрыНастройки <> Неопределено Тогда
		Если НЕ ДанныеНастройкиСчетовУчета.ИспользуетсяРегламентированныйУчет
			И (НЕ ЗначениеЗаполнено(ПараметрыНастройки.СтатьяАктивовПассивов)
			ИЛИ ДанныеНастройкиСчетовУчета.СтатьиСНастройкамиПоОбъектам.Найти(КонтекстНастройки[ПараметрыНастройки.СтатьяАктивовПассивов]) <> Неопределено) Тогда
			Результат = ТекстГиперссылкиНастройкаСчетовНеТребуется();
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(КонтекстНастройки[ПараметрыНастройки.НастройкаСчетовУчета]) Тогда
			Возврат Результат;
		КонецЕсли;
		
		ДанныеНастройки = ДанныеНастройкиСчетовУчета.НастройкиСчетовУчета[КонтекстНастройки[ПараметрыНастройки.НастройкаСчетовУчета]];
		Если ДанныеНастройки = Неопределено Тогда
			Возврат Результат;
		КонецЕсли;
	Иначе
		ДанныеНастройки = КонтекстНастройки;
		ДанныеНастройкиСчетовУчета = НовыеДанныеНастройкиСчетовУчета();
		ДанныеНастройкиСчетовУчета.ДоступнаНастройкаСчетовУчета = Истина;
		//++ Локализация
		ДанныеНастройкиСчетовУчета.ИспользуетсяРегламентированныйУчет = Истина;
		//-- Локализация

		//++ НЕ УТКА
		ДанныеНастройкиСчетовУчета.ИспользуетсяМеждународныйУчет = Истина;
		//-- НЕ УТКА
	КонецЕсли;
	
	КоличествоНастроек = 0;
	
	//++ Локализация
	Если ДанныеНастройкиСчетовУчета.ИспользуетсяРегламентированныйУчет
		И ЗначениеЗаполнено(ДанныеНастройки.СчетУчета) Тогда
		КоличествоНастроек = КоличествоНастроек + 1;
	КонецЕсли;
	//-- Локализация
	
	//++ НЕ УТКА
	Если ДанныеНастройкиСчетовУчета.ИспользуетсяМеждународныйУчет Тогда
		Для Каждого ДанныеМеждународнойНастройки Из ДанныеНастройки.НастройкиМеждународногоУчета Цикл
			Если ЗначениеЗаполнено(ДанныеМеждународнойНастройки.СчетУчета) Тогда
				КоличествоНастроек = КоличествоНастроек + 1;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	//-- НЕ УТКА
	
	Если КоличествоНастроек = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	МассивПолей = Новый Массив;
	
	//++ Локализация
	Если ДанныеНастройкиСчетовУчета.ИспользуетсяРегламентированныйУчет
		И ЗначениеЗаполнено(ДанныеНастройки.СчетУчета) Тогда
		МассивПолей.Добавить(Строка(ДанныеНастройки.СчетУчета));
		Если КоличествоНастроек = 1 Тогда
			ДобавитьСубконтоВПредставлениеНастройки(МассивПолей, ДанныеНастройки, ПараметрыНастройки);
		КонецЕсли;
	КонецЕсли;
	//-- Локализация
	
	//++ НЕ УТКА
	Если ДанныеНастройкиСчетовУчета.ИспользуетсяМеждународныйУчет Тогда
		Для Каждого ДанныеМеждународнойНастройки Из ДанныеНастройки.НастройкиМеждународногоУчета Цикл
			Если ЗначениеЗаполнено(ДанныеМеждународнойНастройки.СчетУчета) Тогда
				МассивПолей.Добавить(Строка(ДанныеМеждународнойНастройки.СчетУчета));
				Если КоличествоНастроек = 1 Тогда
					ДобавитьСубконтоВПредставлениеНастройки(МассивПолей, ДанныеМеждународнойНастройки, ПараметрыНастройки);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	//-- НЕ УТКА

	Если МассивПолей.Количество() > 0 Тогда
		Результат = СтрСоединить(МассивПолей, ", ");
	КонецЕсли;

	Возврат Результат;

КонецФункции

Процедура ЗаполнитьПредставлениеНастройки(Форма, МассивПараметровНастройки, ПутьКДанным = Неопределено,
	ИдентификаторыСтрок = Неопределено) Экспорт

	Если ИдентификаторыСтрок <> Неопределено И ТипЗнч(ИдентификаторыСтрок) <> Тип("Массив") Тогда
		ИдентификаторыСтрок = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИдентификаторыСтрок);
	КонецЕсли;

	Для Каждого ПараметрыНастройки Из МассивПараметровНастройки Цикл
		Если ПутьКДанным <> Неопределено И ПараметрыНастройки.ПутьКДанным <> ПутьКДанным Тогда
			Продолжить;
		КонецЕсли;
		ЗаполнитьПредставлениеНастройкиПоПараметрам(Форма, ПараметрыНастройки, ИдентификаторыСтрок);
	КонецЦикла;

КонецПроцедуры

Процедура ЗаполнитьПредставлениеНастройкиПоПараметрам(Форма, ПараметрыНастройки, ИдентификаторыСтрок = Неопределено) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ПараметрыНастройки.Представление) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеНастройкиСчетовУчета = ОбщегоНазначенияУТКлиентСервер.ПолучитьДанныеМеханизмаИзКэшаФормы(Форма, "НастройкаСчетовУчета");

	КонтекстНастройки = ДанныеПоПути(Форма, ПараметрыНастройки.ПутьКДанным);
	Если ТипЗнч(КонтекстНастройки) = Тип("ДанныеФормыКоллекция") Тогда
		Если ИдентификаторыСтрок <> Неопределено Тогда
			// Обновим только по переданным строкам
			Для Каждого ИдентификаторСтроки Из ИдентификаторыСтрок Цикл
				СтрокаТаблицы = КонтекстНастройки.НайтиПоИдентификатору(ИдентификаторСтроки);
				Представление = ПредставлениеНастройкиОтражения(СтрокаТаблицы, ПараметрыНастройки, ДанныеНастройкиСчетовУчета);
				СтрокаТаблицы[ПараметрыНастройки.Представление] = Представление;
			КонецЦикла;
		Иначе
			// Обновим все строки таблицы
			Для Каждого СтрокаТаблицы Из КонтекстНастройки Цикл
				Представление = ПредставлениеНастройкиОтражения(СтрокаТаблицы, ПараметрыНастройки, ДанныеНастройкиСчетовУчета);
				СтрокаТаблицы[ПараметрыНастройки.Представление] = Представление;
			КонецЦикла;
		КонецЕсли;
	Иначе
		Представление = ПредставлениеНастройкиОтражения(КонтекстНастройки, ПараметрыНастройки, ДанныеНастройкиСчетовУчета);
		Форма[ПараметрыНастройки.Представление] = Представление;
	КонецЕсли;

КонецПроцедуры

Процедура УстановитьВидимостьЭлементовФормы(Форма, ДанныеНастройкиСчетовУчета) Экспорт

	Для Каждого ПараметрыНастройки Из ДанныеНастройкиСчетовУчета.МассивПараметровНастройки Цикл
		УстановитьВидимостьЭлементовПоПараметрам(Форма, ПараметрыНастройки, ДанныеНастройкиСчетовУчета);
	КонецЦикла;

КонецПроцедуры

Функция ТребуетсяЗаполнениеНастройкиСчетовУчетаПоУмолчанию(Форма, КонтекстНастройки, ПараметрыНастройки) Экспорт
	
	ТребуетсяЗаполнениеПоУмолчанию = ЗначениеЗаполнено(ПараметрыНастройки.СтатьяАктивовПассивов)
		И ЗначениеЗаполнено(КонтекстНастройки[ПараметрыНастройки.СтатьяАктивовПассивов])
		И НЕ ЗначениеЗаполнено(КонтекстНастройки[ПараметрыНастройки.НастройкаСчетовУчета]);
	
	Если НЕ ТребуетсяЗаполнениеПоУмолчанию Тогда
	ИначеЕсли НЕ ЗначениеЗаполнено(ПараметрыНастройки.ТипСтатьи) Тогда
	ИначеЕсли ТипЗнч(КонтекстНастройки) = Тип("Структура")
		ИЛИ ТипЗнч(КонтекстНастройки) = Тип("ДанныеФормыЭлементКоллекции") Тогда
		ТребуетсяЗаполнениеПоУмолчанию = КонтекстНастройки[ПараметрыНастройки.ТипСтатьи] = 3;
	ИначеЕсли ТипЗнч(Форма) = Тип("ДанныеФормыСтруктура")
		ИЛИ ТипЗнч(Форма) = Тип("ФормаКлиентскогоПриложения") Тогда
		ТребуетсяЗаполнениеПоУмолчанию = Форма[ПараметрыНастройки.ТипСтатьи] = 3;
	Иначе
		ТребуетсяЗаполнениеПоУмолчанию =
			ТипЗнч(КонтекстНастройки[ПараметрыНастройки.СтатьяАктивовПассивов]) =
				Тип("ПланВидовХарактеристикСсылка.СтатьиАктивовПассивов");
	КонецЕсли;
	
	Возврат ТребуетсяЗаполнениеПоУмолчанию;
	
КонецФункции

Процедура ЗаполнитьНастройкуСчетовУчетаПоУмолчанию(КонтекстНастройки, ПараметрыНастройки, НастройкиПоУмолчанию, ДанныеНастройкиСчетовУчета) Экспорт
	
	Если ЗначениеЗаполнено(ПараметрыНастройки.СтатьяАктивовПассивов) Тогда
		НастройкаПоУмолчанию = НастройкиПоУмолчанию[КонтекстНастройки[ПараметрыНастройки.СтатьяАктивовПассивов]];
		Если НастройкаПоУмолчанию <> Неопределено Тогда
			КонтекстНастройки[ПараметрыНастройки.НастройкаСчетовУчета] = НастройкаПоУмолчанию.НастройкаСчетовУчета;
			ДанныеНастройкиСчетовУчета.НастройкиСчетовУчета.Вставить(НастройкаПоУмолчанию.НастройкаСчетовУчета, НастройкаПоУмолчанию.ДанныеНастройки);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

//Очищает неактуальные настройки счетов учета в таблице формы.
//
// Параметры:
//	Форма - ФормаКлиентскогоПриложения - Форма, на которой расположены поля субконто.
//	МассивПараметровНастройки - Массив из см. НастройкаСчетовУчетаСервер.ПараметрыНастройкиСчетаУчетаОперации - Параметры настройки.
//
Процедура ОчиститьНеактуальныеНастройкиОтраженияВДанныхФормы(Форма, МассивПараметровНастройки) Экспорт
	
	Для каждого ПараметрыНастройки Из МассивПараметровНастройки Цикл
		
		Данные = ДанныеПоПути(Форма, ПараметрыНастройки.ПутьКДанным);
		Если ТипЗнч(Данные) = Тип("ДанныеФормыКоллекция") Тогда
			Для каждого СтрокаТаблицы Из Данные Цикл
				ТипСтатьи = ?(ЗначениеЗаполнено(ПараметрыНастройки.ТипСтатьи), СтрокаТаблицы[ПараметрыНастройки.ТипСтатьи], Неопределено);
				ОчиститьНастройкиОтражения(СтрокаТаблицы, ПараметрыНастройки, ТипСтатьи);
			КонецЦикла;
		Иначе
			ТипСтатьи = ?(ЗначениеЗаполнено(ПараметрыНастройки.ТипСтатьи), Форма[ПараметрыНастройки.ТипСтатьи], Неопределено);
			ОчиститьНастройкиОтражения(Данные, ПараметрыНастройки, ТипСтатьи);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

//++ Локализация

Функция ИспользуетсяДолгосрочныйСчет(ВидСчета) Экспорт
	
	Возврат ВидыСчетовСНастройкамиДолгосрочнойЗадолженности().Найти(ВидСчета) <> Неопределено;
	
КонецФункции

Функция ИспользуютсяМалоценныеТМЦ(ВидСчета) Экспорт
	
	Возврат ВидСчета = "НаСкладе";
	
КонецФункции

Функция СчетУчетаДляКоторогоИспользуетсяДопПоле(ИмяДопПоля) Экспорт
	
	Возврат "НаСкладе";
	
КонецФункции

// Устанавливает свойства измерений на форме
// 
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - 
// 	РазделУчета - Неопределено, Произвольный -
Процедура УстановитьСвойстваИзмеренийНаФорме(Форма, РазделУчета = Неопределено) Экспорт

	Настройка = Форма.КэшДанныхМеханизмов.НастройкаСчетовУчета;
	Если РазделУчета = Неопределено Тогда
		// Сбрасываем все предыдущие настройки элементов формы для последующего их определения на основании разделов:
		УстановитьСвойствоЭлементаИзмерения(Форма.Элементы[ИмяЭлементаНаФорме(Настройка, , "АналитикаУчета",
			"Реквизит")]);
		УстановитьСвойствоЭлементаИзмерения(Форма.Элементы[ИмяЭлементаНаФорме(Настройка, , "МестоУчета", "Реквизит")]);
		УстановитьСвойствоЭлементаИзмерения(Форма.Элементы[ИмяЭлементаНаФорме(Настройка, , "Организация", "Реквизит")]);
	Иначе
		ИмяЭлемента = ИмяЭлементаНаФорме(Настройка, РазделУчета, "АналитикаУчета", "Реквизит");
		СвойстваИзмерения = Настройка.НастройкиРазделов.Получить(РазделУчета).СвойстваАналитики;
		УстановитьСвойствоЭлементаИзмерения(Форма.Элементы[ИмяЭлемента], СвойстваИзмерения);
		ИмяЭлемента = ИмяЭлементаНаФорме(Настройка, РазделУчета, "МестоУчета", "Реквизит");
		СвойстваИзмерения = Настройка.НастройкиРазделов.Получить(РазделУчета).СвойстваМестаУчета;
		УстановитьСвойствоЭлементаИзмерения(Форма.Элементы[ИмяЭлемента], СвойстваИзмерения);
		ИмяЭлемента = ИмяЭлементаНаФорме(Настройка, РазделУчета, "Организация", "Реквизит");
		СвойстваИзмерения = Настройка.НастройкиРазделов.Получить(РазделУчета).СвойстваОрганизации;
		ОтмеченныеРазделы = ОбщегоНазначенияКлиентСервер.ОтмеченныеЭлементы(Настройка.СписокРазделовУчета);
		УстановитьСвойствоЭлементаИзмерения(Форма.Элементы[ИмяЭлемента], СвойстваИзмерения);
		Если ОтмеченныеРазделы.Количество() = 1 И (ОтмеченныеРазделы.Получить(0) = "ДенежныеСредства" ИЛИ ОтмеченныеРазделы.Получить(0) = "Эквайринг") Тогда
			Форма.Элементы[ИмяЭлемента].ТолькоПросмотр = Истина;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура УстановитьСвойстваСчетовНаФорме(Форма, ИмяЭлементаНастройки, ПараметрыСоздания = Неопределено, ТипЭлемента = "СчетУчета") Экспорт
	
	Настройка = Форма.КэшДанныхМеханизмов.НастройкаСчетовУчета;
	Раздел = ?(ПараметрыСоздания = Неопределено, РазделУчетаПоНастройкам(Настройка), ПараметрыСоздания.РазделУчета);
	ИмяЭлементаНаФорме = ИмяЭлементаНаФорме(Настройка, Раздел, ИмяЭлементаНастройки, ТипЭлемента);
	ПринадлежитГруппе = ПараметрыСоздания <> Неопределено И ПараметрыСоздания.ГруппаДляДобавления <> Неопределено;
	
	Если (ИмяЭлементаНастройки = "ВыручкаОтПродаж" Или ИмяЭлементаНастройки = "СебестоимостьПродаж")
		И Настройка.РедактированиеВСписке Тогда
		
		Если ПринадлежитГруппе И Раздел = "НоменклатураСобственная" Тогда
			Форма.Элементы[ИмяЭлементаНаФорме].Заголовок = НСтр("ru = 'ОСНО';
																|en = 'GTS'");
		КонецЕсли;
		
	КонецЕсли;

	Если (ИмяЭлементаНастройки = "ВыручкаОтПродажЕНВД" Или ИмяЭлементаНастройки = "СебестоимостьПродажЕНВД")
		И Настройка.РедактированиеВСписке Тогда

		Если ПринадлежитГруппе И Раздел = "НоменклатураСобственная" Тогда
			Форма.Элементы[ИмяЭлементаНаФорме].Заголовок = НСтр("ru = 'ЕНВД';
																|en = 'UTII'");
		КонецЕсли;

	КонецЕсли;
	
	Если ИмяЭлементаНастройки = "ВыручкаРаздельныйНУ" И Настройка.РедактированиеВСписке Тогда
		
		Если ПринадлежитГруппе И Раздел = "НоменклатураСобственная" Тогда
			Форма.Элементы[ИмяЭлементаНаФорме].Заголовок = НСтр("ru = 'варианты НУ';
																|en = 'TA options'");
		КонецЕсли;
		
	КонецЕсли;

	Если ИмяЭлементаНастройки = "ОтноситсяКМалоценнымТМЦ" Тогда
		
		Если ПринадлежитГруппе И Раздел = "НоменклатураСобственная" И Настройка.РедактированиеВСписке Тогда
			Форма.Элементы[ИмяЭлементаНаФорме].Заголовок = НСтр("ru = 'Малоценные';
																|en = 'Low-cost'");
		КонецЕсли;
		
		Если Не Настройка.РедактированиеВСписке Тогда
			Форма.Элементы[ИмяЭлементаНаФорме].ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
			Форма.Элементы[ИмяЭлементаНаФорме].Подсказка = НСтр("ru = 'Обособленное списание ТМЦ выполняется, если в учетной политике бухучета включена настройка ""Списывать стоимость малоценных ТМЦ на расходы в месяце приобретения""';
																|en = 'Inventory is written off separately if the ""Write off cost of low-cost inventory as expenses in the purchase month"" setting is enabled in the accounting policy'");
		КонецЕсли;
		
		Форма.Элементы[ИмяЭлементаНаФорме].Видимость = Настройка.ОбщиеРеквизиты.ИспользуютсяМалоценные;
		
	КонецЕсли;

	Если СчетИспользуетСубконто(ИмяЭлементаНастройки) И ТипЭлемента = "Субконто" И Не Настройка.РедактированиеВСписке Тогда

		ИмяРеквизитаСчета = ИмяРеквизитаНаФорме(Настройка, Раздел, ИмяЭлементаНастройки);
		ЗначениеСчетаУчета = ?(Настройка.Свойство("НастройкаПоОдному"), Форма.Запись[ИмяРеквизитаСчета], Форма[ИмяРеквизитаСчета]);
		ЭтоСчетПрочиеДоходыРасходы = БухгалтерскийУчетВызовСервераПовтИсп.ЭтоСчетПрочиеДоходы(ЗначениеСчетаУчета)
			ИЛИ БухгалтерскийУчетВызовСервераПовтИсп.ЭтоСчетПрочиеРасходы(ЗначениеСчетаУчета);
		Форма.Элементы[ИмяЭлементаНаФорме].Доступность = ЭтоСчетПрочиеДоходыРасходы;
		ИмяРеквизита = ИмяРеквизитаНаФорме(Настройка, Раздел, ИмяЭлементаНастройки, ТипЭлемента);
		ЗначениеРеквизита = ?(Настройка.Свойство("НастройкаПоОдному"), Форма.Запись[ИмяРеквизита], Форма[ИмяРеквизита]);
		Форма.Элементы[ИмяЭлементаНаФорме].ОтметкаНезаполненного = ЭтоСчетПрочиеДоходыРасходы И Не ЗначениеЗаполнено(
			ЗначениеРеквизита);
		Если Не ЭтоСчетПрочиеДоходыРасходы Тогда
			ЗначениеРеквизита = Неопределено;
		КонецЕсли;
		Если Настройка.СчетаСДинамическимиСвойствами.Найти(ТипЭлемента + "_" + ИмяЭлементаНастройки) = Неопределено Тогда
			Настройка.СчетаСДинамическимиСвойствами.Добавить(ТипЭлемента + "_" + ИмяЭлементаНастройки);
		КонецЕсли;

	КонецЕсли;

	Если Раздел = "Резервы" И Не Настройка.РедактированиеВСписке Тогда
		Форма.Элементы[ИмяЭлементаНаФорме].Видимость = Настройка.ОбщиеРеквизиты.ФормироватьРезервыПредстоящихРасходов
			И Настройка.РеквизитыАналитики.ВидРезервов <> ПредопределенноеЗначение(
			"Справочник.Резервы.РезервЕжегодныхОтпусков");
		Если Настройка.СчетаСДинамическимиСвойствами.Найти(ИмяЭлементаНастройки) = Неопределено Тогда
			Настройка.СчетаСДинамическимиСвойствами.Добавить(ИмяЭлементаНастройки);
		КонецЕсли;
	КонецЕсли;

	Если ИмяЭлементаНастройки = "Резервы" И Настройка.РедактированиеВСписке Тогда
		Форма.Элементы[ИмяЭлементаНаФорме].Заголовок = НСтр("ru = 'Резервы';
															|en = 'Provisions'");
	КонецЕсли;

	Если ИмяЭлементаНастройки = "ВыбытиеВНА" И Не Настройка.РедактированиеВСписке Тогда
		Форма.Элементы[ИмяЭлементаНаФорме].Видимость = Настройка.РеквизитыАналитики.ВидАктива = ПредопределенноеЗначение(
			"Перечисление.ВидыВнеоборотныхАктивов.ОсновноеСредство") Или Настройка.РеквизитыАналитики.ВидАктива
			= ПредопределенноеЗначение("Перечисление.ВидыВнеоборотныхАктивов.ИнвестиционноеИмущество");
		Если Настройка.СчетаСДинамическимиСвойствами.Найти(ИмяЭлементаНастройки) = Неопределено Тогда
			Настройка.СчетаСДинамическимиСвойствами.Добавить(ИмяЭлементаНастройки);
		КонецЕсли;
	КонецЕсли;

	Если ИмяЭлементаНастройки = "АмортизацияВНА" И Не Настройка.РедактированиеВСписке Тогда
		Форма.Элементы[ИмяЭлементаНаФорме].Видимость = Настройка.РеквизитыАналитики.ВидАктива = ПредопределенноеЗначение(
			"Перечисление.ВидыВнеоборотныхАктивов.ОсновноеСредство") Или Настройка.РеквизитыАналитики.ВидАктива
			= ПредопределенноеЗначение("Перечисление.ВидыВнеоборотныхАктивов.ИнвестиционноеИмущество")
			Или Настройка.РеквизитыАналитики.ВидАктива = ПредопределенноеЗначение(
			"Перечисление.ВидыВнеоборотныхАктивов.НМА");
		Если Настройка.СчетаСДинамическимиСвойствами.Найти(ИмяЭлементаНастройки) = Неопределено Тогда
			Настройка.СчетаСДинамическимиСвойствами.Добавить(ИмяЭлементаНастройки);
		КонецЕсли;
	КонецЕсли;

	Если ИмяЭлементаНастройки = "ЗабалансовыйУчетВНА" И Не Настройка.РедактированиеВСписке Тогда
		Форма.Элементы[ИмяЭлементаНаФорме].Видимость = Настройка.РеквизитыАналитики.ВидАктива = ПредопределенноеЗначение(
			"Перечисление.ВидыВнеоборотныхАктивов.ОсновноеСредство") Или Настройка.РеквизитыАналитики.ВидАктива
			= ПредопределенноеЗначение("Перечисление.ВидыВнеоборотныхАктивов.ИнвестиционноеИмущество");
		Если Настройка.СчетаСДинамическимиСвойствами.Найти(ИмяЭлементаНастройки) = Неопределено Тогда
			Настройка.СчетаСДинамическимиСвойствами.Добавить(ИмяЭлементаНастройки);
		КонецЕсли;
	КонецЕсли;

	Если ИмяЭлементаНастройки = "АмортизацияВНА_ЦФ" И Не Настройка.РедактированиеВСписке Тогда
		Форма.Элементы[ИмяЭлементаНаФорме].Видимость = Настройка.РеквизитыАналитики.ВидАктива <> ПредопределенноеЗначение(
			"Перечисление.ВидыВнеоборотныхАктивов.НИОКР");
		Если Настройка.СчетаСДинамическимиСвойствами.Найти(ИмяЭлементаНастройки) = Неопределено Тогда
			Настройка.СчетаСДинамическимиСвойствами.Добавить(ИмяЭлементаНастройки);
		КонецЕсли;
	КонецЕсли;

	Если ИмяЭлементаНастройки = "РезервДооценкиВНА" И Не Настройка.РедактированиеВСписке Тогда
		Форма.Элементы[ИмяЭлементаНаФорме].Видимость = Настройка.РеквизитыАналитики.ВидАктива <> ПредопределенноеЗначение(
			"Перечисление.ВидыВнеоборотныхАктивов.НИОКР");
		Если Настройка.СчетаСДинамическимиСвойствами.Найти(ИмяЭлементаНастройки) = Неопределено Тогда
			Настройка.СчетаСДинамическимиСвойствами.Добавить(ИмяЭлементаНастройки);
		КонецЕсли;
	КонецЕсли;
	
	Если (ИмяЭлементаНастройки = "АрендныеПлатежи" ИЛИ ИмяЭлементаНастройки = "НГЛС"
		ИЛИ ИмяЭлементаНастройки = "ПроцентныйДисконт") И Не Настройка.РедактированиеВСписке Тогда
		Форма.Элементы[ИмяЭлементаНаФорме].Видимость = Настройка.РеквизитыАналитики.ВидАктива = ПредопределенноеЗначение(
			"Перечисление.ВидыВнеоборотныхАктивов.ОсновноеСредство");
		Если Настройка.СчетаСДинамическимиСвойствами.Найти(ИмяЭлементаНастройки) = Неопределено Тогда
			Настройка.СчетаСДинамическимиСвойствами.Добавить(ИмяЭлементаНастройки);
		КонецЕсли;
	КонецЕсли;
	
	Если ИмяЭлементаНастройки = "НГЛС" Тогда
		Форма.Элементы[ИмяЭлементаНаФорме].Подсказка = НСтр("ru = 'Негарантированная ликвидационная стоимость';
															|en = 'Non-guaranteed residual value'");
	КонецЕсли;

КонецПроцедуры

Процедура УстановитьВидимостьРаздела(Форма, РазделыСчетовУчета) Экспорт

	Настройка = Форма.КэшДанныхМеханизмов.НастройкаСчетовУчета;

	Если Не Настройка.РазбитьПоРазделамЭлементы Тогда
		Возврат;
	КонецЕсли;

	КоличествоОтмеченных = ОбщегоНазначенияКлиентСервер.ОтмеченныеЭлементы(РазделыСчетовУчета).Количество();
	Для Каждого Раздел Из РазделыСчетовУчета Цикл
		Если ОбщийРазделУчетаПоИмениТекущегоРаздела(Раздел.Значение) = "Номенклатура" 
			И Не Настройка.РедактированиеВСписке Тогда
			СтруктураВидимости = Новый Структура;
			Для Каждого НастройкаРазделаУчета Из Настройка.НастройкиРазделов Цикл
				Если НастройкаРазделаУчета.Ключ = Раздел.Значение Тогда
					ЭлементыНастройки = ОбщегоНазначенияКлиентСервер.ОтмеченныеЭлементы(НастройкаРазделаУчета.Значение.ЭлементыНастройки);
					Для Каждого ЭлементНастройки Из ЭлементыНастройки Цикл
						ТипЭлемента = НастройкаСчетовУчетаКлиентСервер.ПрефиксЭлементаНастройки(ЭлементНастройки.Значение);
						ИмяЭлемента = СтрЗаменить(ЭлементНастройки.Значение, ТипЭлемента + "_", "");
						ИмяЭлементаНаФорме = ИмяЭлементаНаФорме(Настройка, Раздел.Значение, ИмяЭлемента, ТипЭлемента);
						Если СтруктураВидимости.Свойство(ИмяЭлементаНаФорме) Тогда
							СтруктураВидимости.Вставить(ИмяЭлементаНаФорме, СтруктураВидимости[ИмяЭлементаНаФорме] Или Раздел.Пометка);
						Иначе
							СтруктураВидимости.Вставить(ИмяЭлементаНаФорме, Раздел.Пометка);
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
			Для Каждого ВидимостьЭлемента Из СтруктураВидимости Цикл
				Форма.Элементы[ВидимостьЭлемента.Ключ].Видимость = ВидимостьЭлемента.Значение;
			КонецЦикла;
		Иначе
			ИмяЭлементаНаФорме = ИмяЭлементаНаФорме(Настройка, Раздел.Значение, "", "Группа");
			Форма.Элементы[ИмяЭлементаНаФорме].Видимость = Раздел.Пометка;
			Если Настройка.РедактированиеВСписке Тогда
				Форма.Элементы[ИмяЭлементаНаФорме].ОтображатьВШапке = КоличествоОтмеченных > 1;
			Иначе
				Форма.Элементы[ИмяЭлементаНаФорме].ОтображатьЗаголовок = КоличествоОтмеченных > 1;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

Функция ПараметрыВыбораСчетаУчета(ИмяВидаСчета, Настройки, ПризнакДолгосрочности, Реквизиты = Неопределено) Экспорт

	Если Реквизиты = Неопределено Тогда
		Реквизиты = Настройки.РеквизитыАналитики;
	КонецЕсли;

	РазделУчета = РазделУчетаПоИмениСчета(ИмяВидаСчета);

	СоответствиеИменДоступныхСчетовИменамВидаСчета = Новый Соответствие;
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("Доходы", "СчетаПрочихДоходов");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("НаСкладе", "СчетаУчетаНаСкладе");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("НоменклатураПереданная", "СчетаУчетаПередачиНаКомиссию");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("РеализацияБезПереходаПраваСобственности",
		"СчетаУчетаПередачиНаКомиссию");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("ПередачаВПереработку", "СчетаУчетаПередачиВПереработку");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("ЗатратыНаПриобретениеТМЦ",
		"СчетаУчетаЗатратыНаПриобретение");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("ВыручкаОтПродаж", "СчетаУчетаВыручкиОтПродаж");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("ВыручкаОтПродажЕНВД", "СчетаУчетаВыручкиОтПродаж");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("ВыручкаРаздельныйНУ", "СчетаУчетаВыручкиОтПродаж");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("СебестоимостьПродаж", "СчетаУчетаСебестоимостиПродаж");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("СебестоимостьПродажЕНВД", "СчетаУчетаСебестоимостиПродаж");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("НДСПриПродаже", "СчетаУчетаНДСПриПродаже");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("НДСПриОтгрузкеБезПереходаПраваСобственности",
		"СчетаУчетаНДСВПути");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("ТМЦНаСкладах",
		"СчетаУчетаТоваровПринятыхНаОтветственноеХранение");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("ТМЦВПроизводстве",
		"СчетаУчетаТоваровПринятыхНаОтветственноеХранение");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("ТМЦПотребленныеВПроизводстве",
		"СчетаУчетаТоваровПринятыхНаОтветственноеХранение");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("ТМЦУХранителей",
		"СчетаУчетаТоваровПринятыхНаОтветственноеХранение");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("ТМЦКСписанию",
		"СчетаУчетаТоваровПринятыхНаОтветственноеХранение");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("МатериалыПринятыеВПереработку",
		"СчетаУчетаМатериаловПринятыхВПереработку");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("МатериалыПринятыеВПереработкуВПроизводстве",
		"СчетаУчетаМатериаловПринятыхВПереработку");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("МатериалыПотребленныеВПроизводстве",
		"СчетаУчетаМатериаловПринятыхВПереработку");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("МатериалыПринятыеВПереработкуУХранителей",
		"СчетаУчетаМатериаловПринятыхВПереработку");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("МатериалыПринятыеВПереработкуКСписанию",
		"СчетаУчетаМатериаловПринятыхВПереработку");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("ТоварыНаСкладе", "СчетаУчетаТоваровПринятыхНаКомиссию");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("ТоварыПереданныеНаКомиссию",
		"СчетаУчетаТоваровПринятыхНаКомиссию");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("Производство", "СчетаУчетаНЗП");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("ТМЦВЭксплуатации", "СчетаУчетаТМЦВЭксплуатации");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("ТМЦВЭксплуатацииЗаБалансом", "СчетаЗабалансовогоУчета");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("РасчетыПоПодарочнымСертификатам",
		"СчетаРасчетовПоАвансамПолученным");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("АвансыПолученные", "СчетаРасчетовПоАвансамПолученным");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("АвансыВыданные", "СчетаРасчетовПоАвансамВыданным");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("РасчетыСКлиентами", "СчетаРасчетовСКлиентами");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("РасчетыСКлиентамиТара", "СчетаРасчетовСКлиентами");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("РасчетыСПоставщиками", "СчетаРасчетовСПоставщиками");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("РасчетыСПоставщикамиТара", "СчетаРасчетовСПоставщиками");

	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("АрендныеОбязательства", "СчетаРасчетовПоАренде");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("УслугаПоАренде", "СчетаРасчетовПоАренде");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("ОбеспечительныйПлатеж", "СчетаРасчетовПоАренде");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("ВыкупнаяСтоимость", "СчетаРасчетовПоАренде");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("НачислениеПроцентовПоАренде", "СчетаРасчетовПоАренде");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("НДСПриАренде", "СчетаРасчетовПоАренде");
	
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("РасчетыСКредиторамиОсновнойДолг",
		"СчетаРасчетовСКредиторамиОсновнойДолг");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("РасчетыСКредиторамиПроценты",
		"СчетаРасчетовСКредиторамиПроценты");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("РасчетыСКредиторамиКомиссия",
		"СчетаРасчетовСКредиторамиКомиссия");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("РасчетыСДебиторамиОсновнойДолг",
		"СчетаРасчетовСДебиторамиОсновнойДолг");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("РасчетыСДебиторамиПроценты",
		"СчетаРасчетовСДебиторамиОсновнойДолг");
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("РасчетыСДебиторамиКомиссия",
		"СчетаРасчетовСДебиторамиОсновнойДолг");
	
	СоответствиеИменДоступныхСчетовИменамВидаСчета.Вставить("РасчетыПоБронированию", "СчетаРасчетовПоБронированию");
	
	МассивПараметровВыбора = Новый Массив;
	МассивДоступныхСчетов = Неопределено;
	
	Если РазделУчета = "Расходы" Тогда
		МассивДоступныхСчетов = МассивДоступныхСчетовРасходов(ИмяВидаСчета, Реквизиты, Настройки.ДоступныеСчетаУчета);
	ИначеЕсли РазделУчета = "ДенежныеСредства" Тогда
 		МассивДоступныхСчетов = МассивДоступныхСчетовДенежныхСредств(Реквизиты, Настройки.ДоступныеСчетаУчета);
		Если Настройки.СчетаСДинамическимиПараметрамиВыбора.Найти(ИмяВидаСчета) = Неопределено Тогда
			Настройки.СчетаСДинамическимиПараметрамиВыбора.Добавить(ИмяВидаСчета);
		КонецЕсли;
	ИначеЕсли РазделУчета = "ВнеоборотныеАктивы" Тогда
		МассивДоступныхСчетов = МассивДоступныхСчетовВнеоборотныхАктивов(ИмяВидаСчета, Реквизиты,
			Настройки.ДоступныеСчетаУчета);
	Иначе
		ИмяДоступныхСчетов = СоответствиеИменДоступныхСчетовИменамВидаСчета.Получить(ИмяВидаСчета);
		Если ИмяДоступныхСчетов = Неопределено Тогда
			Настройки.ДоступныеСчетаУчета.Свойство(ИмяВидаСчета, МассивДоступныхСчетов);
		Иначе
			Настройки.ДоступныеСчетаУчета.Свойство(ИмяДоступныхСчетов, МассивДоступныхСчетов);
		КонецЕсли;
	КонецЕсли;
	
	Если РазделСДинамическимиПараметрамиВыбораСчета(РазделУчета) Тогда
		Если Настройки.СчетаСДинамическимиПараметрамиВыбора.Найти(ИмяВидаСчета) = Неопределено Тогда
			Настройки.СчетаСДинамическимиПараметрамиВыбора.Добавить(ИмяВидаСчета);
		КонецЕсли;
	КонецЕсли;
	
	Если МассивДоступныхСчетов = Неопределено Тогда
		МассивДоступныхСчетов = Новый Массив;
	КонецЕсли;
	
	МассивПараметровВыбора.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", МассивДоступныхСчетов));
	Если СчетаБезОтбораПоВалюте().Найти(ИмяВидаСчета) = Неопределено Тогда
		УстановитьОтборВалютныхСчетов(МассивПараметровВыбора, Реквизиты, Настройки.ОбщиеРеквизиты);
	КонецЕсли;
	МассивПараметровВыбора.Добавить(Новый ПараметрВыбора("Отбор.Долгосрочный", ПризнакДолгосрочности));
	
	Возврат Новый ФиксированныйМассив(МассивПараметровВыбора);

КонецФункции

Процедура ОбработкаЗаполнения(Форма) Экспорт

	РазделУчета = РазделУчетаПоНастройкам(Форма.КэшДанныхМеханизмов.НастройкаСчетовУчета);

	Если РазделУчета = "ДенежныеСредства" Тогда
		ЗаполнитьСчетаУчетаПоУмолчаниюДенежныеСредства(Форма);
	КонецЕсли;
	Если РазделУчета = "ВнеоборотныеАктивы" Тогда
		ЗаполнитьСчетаУчетаПоУмолчаниюВнеоборотныеАктивы(Форма);
	КонецЕсли;
	Если РазделУчета = "Эквайринг" Тогда
		ЗаполнитьСчетаУчетаПоУмолчаниюЭквайринг(Форма);
	КонецЕсли;
	
КонецПроцедуры

Функция ТипСубконтоПоИмениСчета(ИмяСчета) Экспорт
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("ПланВидовХарактеристикСсылка.СтатьиДоходов"));
	МассивТипов.Добавить(Тип("ПланВидовХарактеристикСсылка.СтатьиРасходов"));
	ТипДоходыРасходы = Новый ОписаниеТипов(МассивТипов);
	
	Если СчетИспользуетСубконто(ИмяСчета) Тогда
		Возврат ТипДоходыРасходы;
	КонецЕсли;

	Возврат Неопределено;

КонецФункции

Функция СчетаИспользующиеСубконто() Экспорт

	СчетаИмеющиеСубконто = Новый СписокЗначений;

	СчетаИмеющиеСубконто.Добавить("ВыручкаОтПродаж", НСтр("ru = 'Статья учета выручки (ОСНО)';
															|en = 'Revenue accounting item (GTS)'"));
	СчетаИмеющиеСубконто.Добавить("ВыручкаРаздельныйНУ", НСтр("ru = 'Статья учета выручки (раздельный НУ)';
																|en = 'Revenue accounting item (separate TA)'"));
	СчетаИмеющиеСубконто.Добавить("СебестоимостьПродаж", НСтр("ru = 'Статья учета себестоимости (ОСНО)';
																|en = 'Cost accounting item (GTS)'"));
	СчетаИмеющиеСубконто.Добавить("НДСПриПродаже", НСтр("ru = 'Статья учета';
														|en = 'Accounting item'"));
	СчетаИмеющиеСубконто.Добавить("ВыручкаОтПродажЕНВД", НСтр("ru = 'Статья учета выручки (ЕНВД)';
																|en = 'Revenue accounting item (UTII)'"));
	СчетаИмеющиеСубконто.Добавить("СебестоимостьПродажЕНВД", НСтр("ru = 'Статья учета себестоимости (ЕНВД)';
																	|en = 'Cost accounting item (UTII)'"));
	
	Возврат СчетаИмеющиеСубконто;

КонецФункции

Функция ЗначениеУмолчанияПоТипуЭлемента(ТипЭлементаНастройки) Экспорт
	
	СоответствиеТиповЭлементовЗначениямПоУмолчанию = Новый Соответствие;
	СоответствиеТиповЭлементовЗначениямПоУмолчанию.Вставить("СчетУчета", "ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)");
	СоответствиеТиповЭлементовЗначениямПоУмолчанию.Вставить("Субконто", "НЕОПРЕДЕЛЕНО");
	СоответствиеТиповЭлементовЗначениямПоУмолчанию.Вставить("ДопПоле", "ЛОЖЬ");
	
	Возврат СоответствиеТиповЭлементовЗначениямПоУмолчанию.Получить(ТипЭлементаНастройки);
	
КонецФункции

Функция СчетИспользуетСубконто(ИмяВидаСчета) Экспорт

	СчетаИмеющиеСубконто = СчетаИспользующиеСубконто();
	
	Возврат СчетаИмеющиеСубконто.НайтиПоЗначению(ИмяВидаСчета) <> Неопределено;

КонецФункции

Функция ИмяЭлементаНаФорме(Настройка, РазделУчета = Неопределено, ИмяЭлементаНастройки = "",
	Знач ТипЭлементаНастройки = "СчетУчета") Экспорт

	МассивСтрокИмениЭлемента = Новый Массив;
	Если ЗначениеЗаполнено(Настройка.ПрефиксЭлементовФормы) Тогда
		МассивСтрокИмениЭлемента.Добавить(Настройка.ПрефиксЭлементовФормы);
	КонецЕсли;

	Если ТипЭлементаНастройки = "Группа" Тогда
		МассивСтрокИмениЭлемента.Добавить(ТипЭлементаНастройки);
	КонецЕсли;
	Если ТипЭлементаНастройки = "ГруппаДолгосрочные" Тогда
		Если ИмяЭлементаНастройки = "Долгосрочные" Тогда
			МассивСтрокИмениЭлемента.Добавить("Группа");
		Иначе
			МассивСтрокИмениЭлемента.Добавить(ТипЭлементаНастройки);
		КонецЕсли;
	КонецЕсли;
	Если Настройка.РазбитьПоРазделам ИЛИ Настройка.РазбитьПоРазделамЭлементы И ТипЭлементаНастройки <> "Реквизит" И ТипЭлементаНастройки <> "Команда" Тогда
		Если РазделУчета = Неопределено Тогда
			РазделУчета = РазделУчетаПоНастройкам(Настройка);
		КонецЕсли;
		Если Настройка.РазбитьПоРазделам И РазделУчета <> ИмяЭлементаНастройки
			И (ТипЭлементаНастройки = "Реквизит" Или ТипЭлементаНастройки = "Группа" Или ТипЭлементаНастройки = "Команда") Тогда
			ПрефиксРаздела = ОбщийРазделУчетаПоИмениТекущегоРаздела(РазделУчета);
		Иначе
			ПрефиксРаздела = РазделУчета;
		КонецЕсли;
		МассивСтрокИмениЭлемента.Добавить(ПрефиксРаздела);
	КонецЕсли;
	Если Настройка.РедактированиеВСписке Тогда
		ТипЭлементаНастройки = СтрЗаменить(ТипЭлементаНастройки, "Долгосрочный", "");
	КонецЕсли;
	Если (СтрНачинаетсяС(ТипЭлементаНастройки, "СчетУчета") Или ТипЭлементаНастройки = "Субконто" Или ТипЭлементаНастройки = "ДопПоле")
		И Не СтрНачинаетсяС(ИмяЭлементаНастройки, ТипЭлементаНастройки) Тогда
		МассивСтрокИмениЭлемента.Добавить(ТипЭлементаНастройки);
	КонецЕсли;
	Если ЗначениеЗаполнено(ИмяЭлементаНастройки) И (РазделУчета <> ИмяЭлементаНастройки Или ТипЭлементаНастройки
		<> "Группа") И (Не Настройка.Свойство("НастройкаПоОдному") Или ТипЭлементаНастройки = "Реквизит" Или ТипЭлементаНастройки = "Команда") Тогда
		МассивСтрокИмениЭлемента.Добавить(ИмяЭлементаНастройки);
	КонецЕсли;
	ИмяЭлемента = СтрСоединить(МассивСтрокИмениЭлемента, "_");

	Возврат ИмяЭлемента;

КонецФункции

Функция ИмяЭлементаНастройкиПоИмениЭлементаФормы(ИмяЭлементаФормы, ОставлятьПрефиксТипа = Ложь) Экспорт

	МассивСтрокИмениЭлементаФормы = СтрРазделить(ИмяЭлементаФормы, "_");
	МассивСтрокВозврата = Новый Массив;
	ЧислоЧастейИмениЭлемента = ?(ОставлятьПрефиксТипа, 1, 0) + ?(СтрЗаканчиваетсяНа(ИмяЭлементаФормы, "_ЦФ"), 2, 1);
	Для Итератор = 1 По ЧислоЧастейИмениЭлемента Цикл
		МассивСтрокВозврата.Вставить(0, МассивСтрокИмениЭлементаФормы.Получить(
			МассивСтрокИмениЭлементаФормы.Количество() - Итератор));
	КонецЦикла;
	Возврат СтрСоединить(МассивСтрокВозврата, "_");

КонецФункции

Функция ПрефиксЭлементаНастройки(ИмяЭлемента) Экспорт

	Префикс = "СчетУчета";
	Если СтрНачинаетсяС(ИмяЭлемента, "Субконто") Тогда
		Префикс = "Субконто";
	ИначеЕсли СтрНачинаетсяС(ИмяЭлемента, "ДопПоле") Тогда
		Префикс = "ДопПоле";
	ИначеЕсли СтрНачинаетсяС(ИмяЭлемента, "СчетУчетаДолгосрочный") Тогда
		Префикс = "СчетУчетаДолгосрочный";
	КонецЕсли;
	
	Возврат Префикс;

КонецФункции

Функция ИмяРеквизитаНаФорме(Настройка, РазделУчета = Неопределено, ИмяЭлементаНастройки = "",
	Знач ТипЭлементаНастройки = "СчетУчета") Экспорт

	МассивСтрокИменРеквизита = Новый Массив;

	Если ТипЭлементаНастройки = "Реквизит" Тогда
		МассивСтрокИменРеквизита.Добавить(Настройка.ПрефиксПутиКДаннымРеквизитов);
	ИначеЕсли ТипЭлементаНастройки <> "Команда" ИЛИ Не Настройка.РедактированиеВСписке Тогда
		МассивСтрокИменРеквизита.Добавить(Настройка.ПрефиксПутиКДанным);
	КонецЕсли;

	Если Настройка.РазбитьПоРазделам Тогда
		Если РазделУчета = Неопределено Тогда
			РазделУчета = РазделУчетаПоНастройкам(Настройка);
		КонецЕсли;
		МассивСтрокИменРеквизита.Добавить(ОбщийРазделУчетаПоИмениТекущегоРаздела(РазделУчета) + ".");
	КонецЕсли;

	Если Настройка.РедактированиеВСписке Тогда
		ТипЭлементаНастройки = СтрЗаменить(ТипЭлементаНастройки, "Долгосрочный", "");
	КонецЕсли;

	Если ТипЭлементаНастройки <> "Реквизит" И ТипЭлементаНастройки <> "Команда" И ТипЭлементаНастройки <> "Переключатель" Тогда
		Если Не СтрНачинаетсяС(ИмяЭлементаНастройки, ТипЭлементаНастройки) ИЛИ Настройка.Свойство("НастройкаПоОдному") Тогда
			МассивСтрокИменРеквизита.Добавить(ТипЭлементаНастройки);
		КонецЕсли;
		Если Не СтрНачинаетсяС(ИмяЭлементаНастройки, ТипЭлементаНастройки) И Не Настройка.Свойство("НастройкаПоОдному") Тогда
			МассивСтрокИменРеквизита.Добавить("_");
		КонецЕсли;
		Если Не Настройка.Свойство("НастройкаПоОдному") Тогда
			МассивСтрокИменРеквизита.Добавить(ИмяЭлементаНастройки);
		КонецЕсли;
	Иначе
		МассивСтрокИменРеквизита.Добавить(ИмяЭлементаНастройки);
	КонецЕсли;

	Возврат СтрСоединить(МассивСтрокИменРеквизита);

КонецФункции

// Проверяет корректность выбранных счетов учета, а также настраивает элементы управления по переданным параметрам.
// 
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - Форма объекта настройки
//
Процедура УстановитьСвойстваЭлементовНастройкиСчетовУчета(Форма, ЗаполнитьПоУмолчанию = Ложь) Экспорт

	Настройки = Форма.КэшДанныхМеханизмов.НастройкаСчетовУчета;
	ТекущийРазделУчета = РазделУчетаПоНастройкам(Настройки);
	ТекущиеНастройкиРаздела = Настройки.НастройкиРазделов.Получить(ТекущийРазделУчета);
	Для каждого ИмяСчета Из Настройки.СчетаСДинамическимиПараметрамиВыбора Цикл
		Если ТекущиеНастройкиРаздела = Неопределено Или 
			ТекущиеНастройкиРаздела.ЭлементыНастройки.НайтиПоЗначению(ИмяСчета) = Неопределено Тогда
			
			Продолжить;
		КонецЕсли;
		ЭлементФормы = Форма.Элементы[ИмяЭлементаНаФорме(Настройки, Неопределено, ИмяСчета)];
		ЭлементФормы.ПараметрыВыбора = ПараметрыВыбораСчетаУчета(ИмяСчета, Настройки, Ложь);
		Если ТекущиеНастройкиРаздела <> Неопределено И 
			ТекущиеНастройкиРаздела.ДолгосрочныеСчета.Найти(ИмяСчета) <> Неопределено 
			И Не Настройки.РедактированиеВСписке Тогда
			
			ЭлементФормы = Форма.Элементы[ИмяЭлементаНаФорме(Настройки, Неопределено, ИмяСчета, "СчетУчетаДолгосрочный")];
			ЭлементФормы.ПараметрыВыбора = ПараметрыВыбораСчетаУчета(ИмяСчета, Настройки, Истина);
		КонецЕсли;
	КонецЦикла;
	Для Каждого ИмяСчета Из Настройки.СчетаСДинамическимиСвойствами Цикл
		Если ТекущиеНастройкиРаздела = Неопределено Или 
			ТекущиеНастройкиРаздела.ЭлементыНастройки.НайтиПоЗначению(ИмяСчета) = Неопределено Тогда
			
			Продолжить;
		КонецЕсли;
		ТипЭлемента = НастройкаСчетовУчетаКлиентСервер.ПрефиксЭлементаНастройки(ИмяСчета);
		ИмяЭлемента = СтрЗаменить(ИмяСчета, ТипЭлемента + "_", "");
		УстановитьСвойстваСчетовНаФорме(Форма, ИмяЭлемента, , ТипЭлемента);
	КонецЦикла;
	Если Настройки.ОбщиеРеквизиты.Свойство("СписокРазделовУчета") Тогда
		УстановитьВидимостьРаздела(Форма, Настройки.СписокРазделовУчета);
	КонецЕсли;
	Если Настройки.ИспользуютсяДолгосрочныеСчетаУчета Тогда
		Если ТекущиеНастройкиРаздела <> Неопределено И 
			ТекущиеНастройкиРаздела.ДолгосрочныеСчета.Количество() Тогда
			
			ГруппаКраткосрочные = ИмяЭлементаНаФорме(Настройки, ТекущийРазделУчета, "Краткосрочные", "Группа");
			ГруппаДолгосрочные = ИмяЭлементаНаФорме(Настройки, ТекущийРазделУчета, "Долгосрочные", "ГруппаДолгосрочные");
			Форма.Элементы[ГруппаКраткосрочные].ОтображатьЗаголовок = Настройки.ОбщиеРеквизиты.ПоказыватьДолгосрочныеСчета;
			Форма.Элементы[ГруппаДолгосрочные].Видимость = Настройки.ОбщиеРеквизиты.ПоказыватьДолгосрочныеСчета;
		КонецЕсли;
		КомандаПоказыватьДолгосрочные = ИмяЭлементаНаФорме(Настройки,, "ПоказыватьДолгосрочныеСчета", "Команда");
		Форма.Элементы[КомандаПоказыватьДолгосрочные].Пометка = Настройки.ОбщиеРеквизиты.ПоказыватьДолгосрочныеСчета;
	КонецЕсли;
	
	Если ЗаполнитьПоУмолчанию Тогда
		ОбработкаЗаполнения(Форма);
	КонецЕсли;

КонецПроцедуры

// Заполняет реквизиты параметров настройки счетов учета, которые влияют на настройку
// 
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - 
// 	АналитикаУчета - ОпределяемыйТип.НастройкаСчетовУчетаАналитикаУчета - аналитика учета порядка отражения
//
Процедура ЗаполнитьРеквизитыНастройкиСчетовУчетаПоДаннымОбъектаАналитики(Форма, АналитикаУчета) Экспорт

	ПутиКРеквизитамАналитики = Форма.КэшДанныхМеханизмов.НастройкаСчетовУчета.ПутиКРеквизитамАналитики;
	Реквизиты = Форма.КэшДанныхМеханизмов.НастройкаСчетовУчета.РеквизитыАналитики;

	Для Каждого ЭлементСоответствияИменРеквизитовИПути Из ПутиКРеквизитамАналитики Цикл
		Если Реквизиты.Свойство(ЭлементСоответствияИменРеквизитовИПути.Ключ) И ЗначениеЗаполнено(
			ЭлементСоответствияИменРеквизитовИПути.Значение) Тогда
			Реквизиты[ЭлементСоответствияИменРеквизитовИПути.Ключ] = АналитикаУчета[ЭлементСоответствияИменРеквизитовИПути.Значение];
		КонецЕсли;
	КонецЦикла;
	Если АналитикаУчета <> Неопределено И ТипЗнч(АналитикаУчета.Ссылка) = Тип(
		"СправочникСсылка.ГруппыФинансовогоУчетаРасчетов") Тогда
		МассивВидовРасчетов = Новый Массив;
		Для Каждого РеквизитВидаРасчетов Из ПутиКРеквизитамАналитики Цикл
			Если ВидыРасчетовСПартнерами().Найти(РеквизитВидаРасчетов.Значение) <> Неопределено И АналитикаУчета[РеквизитВидаРасчетов.Значение] Тогда
				МассивВидовРасчетов.Добавить(РеквизитВидаРасчетов.Ключ);
			КонецЕсли;
		КонецЦикла;
		Если МассивВидовРасчетов.Количество() <> 1 Тогда
			МассивВидовРасчетов = СтрРазделить(
				"РасчетыСКлиентами,РасчетыСПоставщиками,РасчетыСДебиторами,РасчетыСКредиторами,РасчетыСАрендодателями,РасчетыПоБронированию",
				",");
		КонецЕсли;
		Для Каждого ЭлементСписка Из Форма.КэшДанныхМеханизмов.НастройкаСчетовУчета.СписокРазделовУчета Цикл
			ЭлементСписка.Пометка = МассивВидовРасчетов.Найти(ЭлементСписка.Значение) <> Неопределено;
		КонецЦикла;
	КонецЕсли;
	Если АналитикаУчета <> Неопределено И ТипЗнч(АналитикаУчета.Ссылка) = Тип("СправочникСсылка.ГруппыФинансовогоУчетаДоходовРасходов") Тогда
		МассивВидовДоходовРасходов = Новый Массив;
		Для каждого РеквизитВидаРасчетов Из ПутиКРеквизитамАналитики Цикл
			Если АналитикаУчета[РеквизитВидаРасчетов.Значение] Тогда
				МассивВидовДоходовРасходов.Добавить(РеквизитВидаРасчетов.Ключ);
			КонецЕсли;
		КонецЦикла;
		Если МассивВидовДоходовРасходов.Количество() <> 1 Тогда
			МассивВидовДоходовРасходов = СтрРазделить("Доходы,Расходы", ",");
		КонецЕсли;
		Для каждого ЭлементСписка Из Форма.КэшДанныхМеханизмов.НастройкаСчетовУчета.СписокРазделовУчета Цикл
			ЭлементСписка.Пометка = МассивВидовДоходовРасходов.Найти(ЭлементСписка.Значение) <> Неопределено;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьСчетаУчетаНастройкамиРаздела(Раздел, СтруктураЗаполнения, НастройкиСчетовУчета, Источник) Экспорт

	НастройкаРаздела = НастройкиСчетовУчета.НастройкиРазделов.Получить(Раздел);

	ЭлементыНастройкиРаздела = ОбщегоНазначенияКлиентСервер.ОтмеченныеЭлементы(НастройкаРаздела.ЭлементыНастройки);
	Для Каждого ЭлементНастройки Из ЭлементыНастройкиРаздела Цикл
		
		ТипЭлемента = НастройкаСчетовУчетаКлиентСервер.ПрефиксЭлементаНастройки(ЭлементНастройки);
		ИмяЭлемента = СтрЗаменить(ЭлементНастройки, ТипЭлемента + "_", "");
		ИмяРеквизитаНаФорме = ИмяРеквизитаНаФорме(НастройкиСчетовУчета, Раздел, ИмяЭлемента, ТипЭлемента);
		ИмяИсточника = ?(НастройкиСчетовУчета.РедактированиеВСписке, СтрЗаменить(ИмяРеквизитаНаФорме,
			НастройкиСчетовУчета.ПрефиксПутиКДанным, ""), ИмяРеквизитаНаФорме);
		СтруктураЗаполнения.Вставить(ТипЭлемента + "_" + ИмяЭлемента, Источник[ИмяИсточника]);
		
	КонецЦикла;

	Для Каждого СчетРаздела Из НастройкаРаздела.ДолгосрочныеСчета Цикл
		ИмяРеквизитаНаФорме = ИмяРеквизитаНаФорме(НастройкиСчетовУчета, Раздел, СчетРаздела, "СчетУчетаДолгосрочный");
		ИмяИсточника = ?(НастройкиСчетовУчета.РедактированиеВСписке, СтрЗаменить(ИмяРеквизитаНаФорме,
			НастройкиСчетовУчета.ПрефиксПутиКДанным, ""), ИмяРеквизитаНаФорме);
		СтруктураЗаполнения.Вставить("СчетУчетаДолгосрочный_" + СчетРаздела, Источник[ИмяИсточника]);
	КонецЦикла;

КонецПроцедуры

Функция ИнициализироватьСтруктуруИзмеренийРегистра() Экспорт

	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("Организация", ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка"));
	СтруктураВозврата.Вставить("АналитикаУчета", Неопределено);
	СтруктураВозврата.Вставить("МестоУчета", Неопределено);
	СтруктураВозврата.Вставить("Долгосрочный", Ложь);
	Возврат СтруктураВозврата;

КонецФункции

#Область ОписанияРазделов

Функция ВидыРасчетовСПартнерами() Экспорт

	ВидыРасчетов = Новый Массив;
	ВидыРасчетов.Добавить("РасчетыСКлиентами");
	ВидыРасчетов.Добавить("РасчетыСПоставщиками");
	ВидыРасчетов.Добавить("РасчетыСКомиссионерами");
	ВидыРасчетов.Добавить("РасчетыСКомитентами");
	ВидыРасчетов.Добавить("РасчетыСКомитентамиПоЗакупке");
	ВидыРасчетов.Добавить("РасчетыСПереработчиками");
	ВидыРасчетов.Добавить("РасчетыСДавальцами");
	ВидыРасчетов.Добавить("РасчетыСКредиторами");
	ВидыРасчетов.Добавить("РасчетыСДебиторами");
	ВидыРасчетов.Добавить("РасчетыСАрендодателями");
	ВидыРасчетов.Добавить("РасчетыПоБронированию");

	Возврат ВидыРасчетов;

КонецФункции

Функция СписокРазделовСчетовУчета(ПолучатьОбщиеРазделы = Ложь) Экспорт

	СписокВозврата = Новый СписокЗначений;

	Если ПолучатьОбщиеРазделы Тогда
		СписокВозврата.Добавить("Номенклатура", НСтр("ru = 'номенклатуры';
													|en = 'products'"));
	Иначе
		СписокВозврата.Добавить("НоменклатураСобственная", НСтр("ru = 'номенклатуры';
																|en = 'products'"));
		СписокВозврата.Добавить("НоменклатураПереданная", НСтр("ru = 'номенклатуры переданной';
																|en = 'products transferred'"));
		СписокВозврата.Добавить("НоменклатураПринятая", НСтр("ru = 'номенклатуры принятой';
															|en = 'of products accepted'"));
	КонецЕсли;
	СписокВозврата.Добавить("ТМЦВЭксплуатации", НСтр("ru = 'ТМЦ в эксплуатации';
													|en = 'Inventory in operation'"));
	СписокВозврата.Добавить("ДенежныеСредства", НСтр("ru = 'денежных средств';
													|en = 'cash'"));
	СписокВозврата.Добавить("ПодарочныеСертификаты", НСтр("ru = 'подарочных сертификатов';
															|en = 'gift certificates'"));
	Если ПолучатьОбщиеРазделы Тогда
		СписокВозврата.Добавить("РасчетыСПартнерами", НСтр("ru = 'расчетов с партнерами';
															|en = 'settlements with partners'"));
	Иначе
		СписокВозврата.Добавить("РасчетыСКлиентами", НСтр("ru = 'расчетов с клиентами';
															|en = 'settlements with customers'"));
		СписокВозврата.Добавить("РасчетыСПоставщиками", НСтр("ru = 'расчетов с поставщиками';
															|en = 'settlements with suppliers'"));
		СписокВозврата.Добавить("РасчетыСКомиссионерами", НСтр("ru = 'расчетов с комиссионерами';
																|en = 'settlements with consignees'"));
		СписокВозврата.Добавить("РасчетыСКомитентами", НСтр("ru = 'расчетов с комитентами по продаже';
															|en = 'settlements with consignors on sales'"));
		СписокВозврата.Добавить("РасчетыСКомитентамиПоЗакупке", НСтр("ru = 'расчетов с комитентами по закупке';
																	|en = 'settlements with consignors on purchase'"));
		СписокВозврата.Добавить("РасчетыСПереработчиками", НСтр("ru = 'расчетов с переработчиками';
																|en = 'subcontractor AR/AP'"));
		СписокВозврата.Добавить("РасчетыСДавальцами", НСтр("ru = 'расчетов с давальцами';
															|en = 'providers AR/AP'"));
		СписокВозврата.Добавить("РасчетыСКредиторами", НСтр("ru = 'расчетов с кредиторами';
															|en = 'settlements with creditors'"));
		СписокВозврата.Добавить("РасчетыСДебиторами", НСтр("ru = 'расчетов с дебиторами';
															|en = 'settlements with debtors'"));
		СписокВозврата.Добавить("РасчетыСАрендодателями", НСтр("ru = 'расчетов с арендодателями';
																|en = 'settlements with lessors'"));
		СписокВозврата.Добавить("РасчетыПоБронированию", НСтр("ru = 'расчетов по бронированию';
																|en = 'booking payments'"));
	КонецЕсли;
	СписокВозврата.Добавить("Производство", НСтр("ru = 'Производства';
												|en = 'Production'"));
	СписокВозврата.Добавить("ВнеоборотныеАктивы", НСтр("ru = 'внеоборотных активов';
														|en = 'capital assets'"));
	СписокВозврата.Добавить("Расходы", НСтр("ru = 'прочих расходов';
											|en = 'other expenses'"));
	СписокВозврата.Добавить("Доходы", НСтр("ru = 'прочих доходов';
											|en = 'other expenses'"));
	СписокВозврата.Добавить("Резервы", НСтр("ru = 'резервов (оценочных обязательств)';
											|en = 'reserves (estimated liabilities)'"));
	СписокВозврата.Добавить("Эквайринг", НСтр("ru = 'эквайринга';
												|en = 'acquiring'"));
	
	Возврат СписокВозврата;

КонецФункции

Функция РазделыУчетаИспользующиеСубконто() Экспорт

	РазделыУчетаИмеющиеСубконто = Новый Массив;

	РазделыУчетаИмеющиеСубконто.Добавить("НоменклатураСобственная");
	РазделыУчетаИмеющиеСубконто.Добавить("НоменклатураПереданная");
	РазделыУчетаИмеющиеСубконто.Добавить("Номенклатура");
	
	Возврат РазделыУчетаИмеющиеСубконто;

КонецФункции

Функция РазделыУчетаДляКоторыхНастраиваютсяИсключения() Экспорт

	МассивВозврата = Новый Массив;

	МассивВозврата.Добавить("Доходы");
	МассивВозврата.Добавить("НоменклатураСобственная");
	МассивВозврата.Добавить("НоменклатураПереданная");
	МассивВозврата.Добавить("НоменклатураПринятая");
	МассивВозврата.Добавить("ПодарочныеСертификаты");
	МассивВозврата.Добавить("Производство");
	МассивВозврата.Добавить("Расходы");
	МассивВозврата.Добавить("Резервы");
	МассивВозврата.Добавить("ТМЦВЭксплуатации");
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивВозврата, ВидыРасчетовСПартнерами());

	Возврат МассивВозврата;
КонецФункции

Функция РазделыУчетаНеЗависящиеОтОрганизации() Экспорт

	МассивВозврата = Новый Массив;

	МассивВозврата.Добавить("ВнеоборотныеАктивы");

	Возврат МассивВозврата;

КонецФункции

Функция ЗаголовокПоРазделамУчета(РазделыУчета) Экспорт

	Если ТипЗнч(РазделыУчета) = Тип("Массив") Тогда
		СписокРазделов = СписокРазделовСчетовУчета();
	КонецЕсли;
	МассивЗаголовка = Новый Массив;

	Для Каждого Раздел Из РазделыУчета Цикл
		Пометка = ?(ТипЗнч(РазделыУчета) = Тип("Массив"), СписокРазделов.НайтиПоЗначению(Раздел) <> Неопределено,
			Раздел.Пометка);
		Если Пометка Тогда
			Представление = ?(ТипЗнч(РазделыУчета) = Тип("Массив"), СписокРазделов.НайтиПоЗначению(
				Раздел).Представление, Раздел.Представление);
			МассивЗаголовка.Добавить(Представление);
		КонецЕсли;
	КонецЦикла;

	Если МассивЗаголовка.Количество() Тогда
		Возврат СтрСоединить(МассивЗаголовка, ", ");
	Иначе
		Возврат НСтр("ru = '< не указаны >';
					|en = '< not specified >'");
	КонецЕсли;

КонецФункции

Функция РазделыУчетаСОбязательнойАналитикой() Экспорт

	МассивВозврата = Новый Массив;

	МассивВозврата.Добавить("ПодарочныеСертификаты");
	МассивВозврата.Добавить("ДенежныеСредства");
	
	Возврат МассивВозврата;

КонецФункции

Функция РазделУчетаПоИмениСчета(ИмяВидаСчета) Экспорт

	СоответствиеИменВидовСчетовРазделамУчета = Новый Соответствие;

	СоответствиеИменВидовСчетовРазделамУчета.Вставить("Расходы", "Расходы");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("СписаниеРасходовОСНО", "Расходы");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("СписаниеРасходовЕНВД", "Расходы");

	СоответствиеИменВидовСчетовРазделамУчета.Вставить("Доходы", "Доходы");

	СоответствиеИменВидовСчетовРазделамУчета.Вставить("ДенежныеСредства", "ДенежныеСредства");

	СоответствиеИменВидовСчетовРазделамУчета.Вставить("НаСкладе", "НоменклатураСобственная");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("ВыручкаОтПродаж", "Номенклатура");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("ВыручкаОтПродажЕНВД", "НоменклатураСобственная");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("ВыручкаРаздельныйНУ", "НоменклатураСобственная");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("СебестоимостьПродажЕНВД", "НоменклатураСобственная");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("СебестоимостьПродаж", "Номенклатура");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("НДСПриПродаже", "Номенклатура");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("РеализацияБезПереходаПраваСобственности",
		"НоменклатураСобственная");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("НДСПриОтгрузкеБезПереходаПраваСобственности",
		"НоменклатураСобственная");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("РезервыПодОбесценениеТМЦ", "НоменклатураСобственная");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("ЗатратыНаПриобретениеТМЦ", "НоменклатураПереданная");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("ПередачаВПереработку", "НоменклатураПереданная");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("НоменклатураПереданная", "НоменклатураПереданная");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("РезервыПодОбесценениеПереданные", "НоменклатураПереданная");

	СоответствиеИменВидовСчетовРазделамУчета.Вставить("ТМЦНаСкладах", "НоменклатураПринятая");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("ТМЦВПроизводстве", "НоменклатураПринятая");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("ТМЦПотребленныеВПроизводстве", "НоменклатураПринятая");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("ТМЦУХранителей", "НоменклатураПринятая");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("ТМЦКСписанию", "НоменклатураПринятая");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("МатериалыПринятыеВПереработку", "НоменклатураПринятая");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("МатериалыПринятыеВПереработкуВПроизводстве", "НоменклатураПринятая");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("МатериалыПотребленныеВПроизводстве", "НоменклатураПринятая");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("МатериалыПринятыеВПереработкуУХранителей", "НоменклатураПринятая");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("МатериалыПринятыеВПереработкуКСписанию", "НоменклатураПринятая");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("ТоварыНаСкладе", "НоменклатураПринятая");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("ТоварыПереданныеНаКомиссию", "НоменклатураПринятая");

	СоответствиеИменВидовСчетовРазделамУчета.Вставить("РасчетыСКлиентами", "РасчетыСПартнерами");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("АвансыПолученные", "РасчетыСПартнерами");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("РасчетыСПоставщиками", "РасчетыСПартнерами");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("АвансыВыданные", "РасчетыСПартнерами");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("РасчетыСКлиентамиТара", "РасчетыСКлиентами");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("РасчетыСПоставщикамиТара", "РасчетыСПоставщиками");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("НеотфактурованныеПоставки", "РасчетыСПоставщиками");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("ПроцентыПоДисконтированию", "РасчетыСПоставщиками");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("ПереносДолгосрочнойЗадолженности", "РасчетыСПартнерами");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("ТоварыКомитентовКОформлениюПоПродаже", "РасчетыСКомитентами");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("ТоварыКомитентовКОформлениюПоЗакупке",
		"РасчетыСКомитентамиПоЗакупке");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("РасчетыСКредиторамиОсновнойДолг", "РасчетыСКредиторами");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("РасчетыСКредиторамиПроценты", "РасчетыСКредиторами");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("РасчетыСКредиторамиКомиссия", "РасчетыСКредиторами");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("РасчетыСДебиторамиОсновнойДолг", "РасчетыСДебиторами");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("РасчетыСДебиторамиПроценты", "РасчетыСДебиторами");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("РасчетыСДебиторамиКомиссия", "РасчетыСДебиторами");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("ОбеспечительныйПлатеж", "РасчетыСАрендодателями");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("АрендныеОбязательства", "РасчетыСАрендодателями");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("УслугаПоАренде", "РасчетыСАрендодателями");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("ВыкупнаяСтоимость", "РасчетыСАрендодателями");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("НДСПриАренде", "РасчетыСАрендодателями");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("НачислениеПроцентовПоАренде", "РасчетыСАрендодателями");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("РезервыПоСомнительнымДолгам", "РасчетыСПартнерами");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("НДСАвансыПолученные", "РасчетыСКлиентами");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("НДСАвансыВыданные", "РасчетыСПартнерами");
	
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("РасчетыПоБронированию", "РасчетыПоБронированию");

	СоответствиеИменВидовСчетовРазделамУчета.Вставить("Производство", "Производство");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("РезервыПодОбесценениеПроизводства", "Производство");

	СоответствиеИменВидовСчетовРазделамУчета.Вставить("Резервы", "Резервы");

	СоответствиеИменВидовСчетовРазделамУчета.Вставить("ТМЦВЭксплуатации", "ТМЦВЭксплуатации");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("ТМЦВЭксплуатацииЗаБалансом", "ТМЦВЭксплуатации");

	СоответствиеИменВидовСчетовРазделамУчета.Вставить("РасчетыПоПодарочнымСертификатам", "ПодарочныеСертификаты");

	СоответствиеИменВидовСчетовРазделамУчета.Вставить("ПродажиПоЭквайрингу", "Эквайринг");
	
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("СтоимостьВНА", "ВнеоборотныеАктивы");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("АмортизацияВНА", "ВнеоборотныеАктивы");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("РезервДооценкиВНА", "ВнеоборотныеАктивы");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("РезервДоОценкиВНА", "ВнеоборотныеАктивы");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("ВыбытиеВНА", "ВнеоборотныеАктивы");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("СтоимостьВНА_ЦФ", "ВнеоборотныеАктивы");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("АмортизацияВНА_ЦФ", "ВнеоборотныеАктивы");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("ЗабалансовыйУчетВНА", "ВнеоборотныеАктивы");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("ОбесценениеВНА", "ВнеоборотныеАктивы");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("АрендныеПлатежи", "ВнеоборотныеАктивы");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("НГЛС", "ВнеоборотныеАктивы");
	СоответствиеИменВидовСчетовРазделамУчета.Вставить("ПроцентныйДисконт", "ВнеоборотныеАктивы");

	Возврат СоответствиеИменВидовСчетовРазделамУчета.Получить(ИмяВидаСчета);

КонецФункции

Функция ПодразделыОбщегоРаздела(ИмяРаздела) Экспорт

	МассивВозврата = Новый Массив;

	Если ИмяРаздела = "РасчетыСПартнерами" Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивВозврата, ВидыРасчетовСПартнерами());
	КонецЕсли;

	Если ИмяРаздела = "Номенклатура" Тогда
		МассивВозврата.Добавить("НоменклатураСобственная");
		МассивВозврата.Добавить("НоменклатураПереданная");
		МассивВозврата.Добавить("НоменклатураПринятая");
	КонецЕсли;

	Если ИмяРаздела = "ДоходыРасходы" Тогда
		МассивВозврата.Добавить("Доходы");
		МассивВозврата.Добавить("Расходы");
	КонецЕсли;
	
	Если МассивВозврата.Количество() = 0 Тогда
		МассивВозврата.Добавить(ИмяРаздела);
	КонецЕсли;

	Возврат МассивВозврата;

КонецФункции

Функция ОбщийРазделУчетаПоИмениТекущегоРаздела(ИмяРаздела) Экспорт

	СоответствиеРазделовУчетаИменамТаблиц = Новый Соответствие;
	Для каждого ВидРасчетовСПартнерами Из ВидыРасчетовСПартнерами() Цикл
		СоответствиеРазделовУчетаИменамТаблиц.Вставить(ВидРасчетовСПартнерами, "РасчетыСПартнерами");
	КонецЦикла;
	СоответствиеРазделовУчетаИменамТаблиц.Вставить("НоменклатураСобственная", "Номенклатура");
	СоответствиеРазделовУчетаИменамТаблиц.Вставить("НоменклатураПереданная", "Номенклатура");
	СоответствиеРазделовУчетаИменамТаблиц.Вставить("НоменклатураПринятая", "Номенклатура");
	СоответствиеРазделовУчетаИменамТаблиц.Вставить("Доходы", "ДоходыРасходы");
	СоответствиеРазделовУчетаИменамТаблиц.Вставить("Расходы", "ДоходыРасходы");

	СтрокаВозврата = СоответствиеРазделовУчетаИменамТаблиц.Получить(ИмяРаздела);
	Если СтрокаВозврата = Неопределено Тогда
		СтрокаВозврата = ИмяРаздела;
	КонецЕсли;

	Возврат СтрокаВозврата;

КонецФункции

Функция ЭтоПодраздел(ИмяРаздела) Экспорт

	Возврат ИмяРаздела <> ОбщийРазделУчетаПоИмениТекущегоРаздела(ИмяРаздела);

КонецФункции

Функция РазделУчетаПоНастройкам(Настройки) Экспорт

	Если Настройки.СписокРазделовУчета.Количество() = 1 Тогда
		Возврат Настройки.СписокРазделовУчета.Получить(0).Значение;
	КонецЕсли;

	ОтмеченныеРазделы = ОбщегоНазначенияКлиентСервер.ОтмеченныеЭлементы(Настройки.СписокРазделовУчета);
	Если ОтмеченныеРазделы.Количество() Тогда
		Возврат ОтмеченныеРазделы.Получить(0);
	Иначе
		Возврат "";
	КонецЕсли;

КонецФункции

Функция РазделУчетаПоАналитикеУчета(АналитикаУчета) Экспорт

	СоответствиеТиповАналитикУчетаРазделамУчета = Новый Соответствие;

	СоответствиеТиповАналитикУчетаРазделамУчета.Вставить(Тип("СправочникСсылка.ГруппыФинансовогоУчетаДоходовРасходов"), "ДоходыРасходы");
	СоответствиеТиповАналитикУчетаРазделамУчета.Вставить(Тип("СправочникСсылка.БанковскиеСчетаОрганизаций"),
		"ДенежныеСредства");
	СоответствиеТиповАналитикУчетаРазделамУчета.Вставить(Тип("СправочникСсылка.Кассы"), "ДенежныеСредства");
	СоответствиеТиповАналитикУчетаРазделамУчета.Вставить(Тип("СправочникСсылка.ГруппыФинансовогоУчетаНоменклатуры"), "Номенклатура");
	СоответствиеТиповАналитикУчетаРазделамУчета.Вставить(Тип("СправочникСсылка.ГруппыФинансовогоУчетаРасчетов"), "РасчетыСПартнерами");
	СоответствиеТиповАналитикУчетаРазделамУчета.Вставить(Тип("СправочникСсылка.Резервы"), "Резервы");
	СоответствиеТиповАналитикУчетаРазделамУчета.Вставить(Тип("СправочникСсылка.КатегорииЭксплуатации"), "ТМЦВЭксплуатации");
	СоответствиеТиповАналитикУчетаРазделамУчета.Вставить(Тип("СправочникСсылка.ГруппыФинансовогоУчетаВнеоборотныхАктивов"), "ВнеоборотныеАктивы");
	СоответствиеТиповАналитикУчетаРазделамУчета.Вставить(Тип("СправочникСсылка.ВидыПодарочныхСертификатов"), "ПодарочныеСертификаты");
	СоответствиеТиповАналитикУчетаРазделамУчета.Вставить(Тип("СправочникСсылка.ГруппыАналитическогоУчетаНоменклатуры"), "Производство");
	СоответствиеТиповАналитикУчетаРазделамУчета.Вставить(Тип("СправочникСсылка.ДоговорыЭквайринга"), "Эквайринг");
	
	РазделУчета = СоответствиеТиповАналитикУчетаРазделамУчета.Получить(ТипЗнч(АналитикаУчета));

	Возврат РазделУчета;

КонецФункции

#КонецОбласти

Функция ЭтоНеРеквизитАналитики(ИмяРеквизита) Экспорт
	Возврат СтрНачинаетсяС(ИмяРеквизита, "СчетУчета_") Или СтрНачинаетсяС(ИмяРеквизита, "Субконто_") Или ИмяРеквизита
		= "Организация";
КонецФункции

Функция ГруппаСчетаУчета(ИмяСчета, РазделУчета, ЭтоСписок, ТипЭлемента) Экспорт

	СоответствиеСчетовУчетаГруппам = Новый Соответствие;
	
	СписокГруппНаФорме = СписокГруппНаФорме(РазделУчета, ЭтоСписок);
	Для каждого Группа Из СписокГруппНаФорме Цикл
		Для Каждого СчетУчета Из СчетаУчетаГруппы(Группа.Значение, РазделУчета) Цикл
			Если СтрНачинаетсяС(СчетУчета, ТипЭлемента) ИЛИ ТипЭлемента = "СчетУчета" Тогда
				СтруктураГруппы = Новый Структура("ИмяГруппы, Заголовок", Группа.Значение, Группа.Представление);
				СтруктураГруппы.Вставить("Группировка", ГруппировкаГруппы(Группа.Значение, ЭтоСписок));
				Префикс = ?(ТипЭлемента = "СчетУчета", ТипЭлемента + "_", "");
				СоответствиеСчетовУчетаГруппам.Вставить(Префикс + СчетУчета, СтруктураГруппы);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;

	Возврат СоответствиеСчетовУчетаГруппам.Получить(ТипЭлемента + "_" + ИмяСчета);

КонецФункции

Функция ДопПредставлениеГруппы(Форма, ИмяГруппы) Экспорт

	СчетаУчетаГруппы = СчетаУчетаГруппы(ИмяГруппы);
	ЗначенияСчетов = Новый Массив;
	Для Каждого СчетУчета Из СчетаУчетаГруппы Цикл
		ИмяРеквизита = ИмяРеквизитаНаФорме(Форма.КэшДанныхМеханизмов.НастройкаСчетовУчета, , СчетУчета);
		Если Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, ИмяРеквизита) Тогда
			Продолжить;
		КонецЕсли;
		ЗначениеСчета = Форма[ИмяРеквизитаНаФорме(Форма.КэшДанныхМеханизмов.НастройкаСчетовУчета, , СчетУчета)];
		Если ЗначениеЗаполнено(ЗначениеСчета) Тогда
			ЗначенияСчетов.Добавить(ЗначениеСчета);
		КонецЕсли;
	КонецЦикла;

	Возврат " (" + ?(ЗначенияСчетов.Количество(), СтрСоединить(ЗначенияСчетов, ", "), НСтр("ru = 'не указаны';
																							|en = 'not specified'")) + ")";

КонецФункции

//-- Локализация

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Устанавливает видимость элементов управления настроек счетов учета
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма редактируемого документа
//  ПараметрыНастройки - см. НастройкаСчетовУчетаСервер.ПараметрыНастройки
//  ДанныеНастройкиСчетовУчета - см. НастройкаСчетовУчетаКлиентСервер.НовыеДанныеНастройкиСчетовУчета
//
Процедура УстановитьВидимостьЭлементовПоПараметрам(Форма, ПараметрыНастройки, ДанныеНастройкиСчетовУчета)

	КонтекстНастройки = ДанныеПоПути(Форма, ПараметрыНастройки.ПутьКДанным);

	Для Каждого ИмяЭлемента Из ПараметрыНастройки.ЭлементыФормы Цикл

		ДоступностьПоТипуСтатьи = Истина;
		ДоступностьПоЗначениюСтатьи = Истина;
		
		ЭлементФормы = Форма.Элементы[ИмяЭлемента];
		ТаблицаФормы = ОбщегоНазначенияУТКлиентСервер.ТаблицаФормыЭлемента(ЭлементФормы);
		
		Если ТаблицаФормы = Неопределено
			И ПараметрыНастройки.ДоступностьПоОперации
			И ДанныеНастройкиСчетовУчета.ДоступнаНастройкаСчетовУчета Тогда
			СлужебныйКонтекст = Неопределено;
			
			Если ТипЗнч(КонтекстНастройки) = Тип("ДанныеФормыКоллекция") Тогда
				Если КонтекстНастройки.Количество() > 0 Тогда
				// Редактируем таблицу в режиме "Без разбиения"
					КонтекстНастройки = КонтекстНастройки[0];
					СлужебныйКонтекст = КонтекстНастройки;
				Иначе
					КонтекстНастройки = Неопределено;
				КонецЕсли;
			ИначеЕсли ТипЗнч(КонтекстНастройки) = Тип("ДанныеФормыСтруктура")
				ИЛИ ТипЗнч(КонтекстНастройки) = Тип("ФормаКлиентскогоПриложения") Тогда
				СлужебныйКонтекст = Форма;
			КонецЕсли;
			
			Если СлужебныйКонтекст <> Неопределено
				И ЗначениеЗаполнено(ПараметрыНастройки.ТипСтатьи) Тогда
				ДоступностьПоТипуСтатьи = (СлужебныйКонтекст[ПараметрыНастройки.ТипСтатьи] = 3);
			КонецЕсли;
			
			Если КонтекстНастройки <> Неопределено
				И НЕ ДанныеНастройкиСчетовУчета.ИспользуетсяРегламентированныйУчет
				И (НЕ ЗначениеЗаполнено(ПараметрыНастройки.СтатьяАктивовПассивов)
				ИЛИ ДанныеНастройкиСчетовУчета.СтатьиСНастройкамиПоОбъектам.Найти(КонтекстНастройки[ПараметрыНастройки.СтатьяАктивовПассивов]) <> Неопределено) Тогда
				ДоступностьПоЗначениюСтатьи = Ложь;
				//++ НЕ УТКА
				ДанныеНастройки = ДанныеНастройкиСчетовУчета.НастройкиСчетовУчета[КонтекстНастройки[ПараметрыНастройки.НастройкаСчетовУчета]];
				Если ДанныеНастройки <> Неопределено Тогда
					Для Каждого ДанныеМеждународнойНастройки Из ДанныеНастройки.НастройкиМеждународногоУчета Цикл
						Если ЗначениеЗаполнено(ДанныеМеждународнойНастройки.СчетУчета) Тогда
							ДоступностьПоЗначениюСтатьи = Истина;
							Прервать;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
				//-- НЕ УТКА
			КонецЕсли;
		КонецЕсли;

		ЭлементФормы.Видимость = ДанныеНастройкиСчетовУчета.ДоступнаНастройкаСчетовУчета
			И ПараметрыНастройки.ДоступностьПоОперации
			И ДоступностьПоЗначениюСтатьи
			И ДоступностьПоТипуСтатьи;

	КонецЦикла;

КонецПроцедуры

Процедура ДобавитьСубконтоВПредставлениеНастройки(МассивПолей, ДанныеНастройки, ПараметрыНастройки)
	
	СвойстваСчета = НастройкаСчетовУчетаСлужебныйВызовСервера.ПолучитьСвойстваСчета(ДанныеНастройки.СчетУчета);
	
	Для НомерСубконто = 1 По СвойстваСчета.КоличествоСубконто Цикл
		ЗначениеСубконто = ДанныеНастройки["Субконто" + НомерСубконто];

		Если ЗначениеЗаполнено(ЗначениеСубконто) Тогда
			МассивПолей.Добавить(Строка(ЗначениеСубконто));
		ИначеЕсли ПараметрыНастройки <> Неопределено Тогда
			ОписаниеТиповСубконто = СвойстваСчета["ВидСубконто" + НомерСубконто + "ТипЗначения"];
			ПредставлениеЗаполнено = Ложь;
			Для Каждого Источник Из ПараметрыНастройки.ИсточникиПодбораСубконто Цикл
				Если ОписаниеТиповСубконто.СодержитТип(Источник.Значение) Тогда
					ПредставлениеЗаполнено = Истина;
					МассивПолей.Добавить(НСтр("ru = '<авто>';
												|en = '<auto>'"));
					Прервать;
				КонецЕсли;
			КонецЦикла;

			Если Не ПредставлениеЗаполнено Тогда
				МассивПолей.Добавить("<...>");
			КонецЕсли;
		Иначе
			МассивПолей.Добавить("<...>");
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьНастройкиСчетовУчетаПоУмолчанию(Форма, ПараметрыНастройки, ИдентификаторыСтрок)

	Если НЕ ПараметрыНастройки.ДоступностьПоОперации Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеНастройкиСчетовУчета = ОбщегоНазначенияУТКлиентСервер.ПолучитьДанныеМеханизмаИзКэшаФормы(Форма, "НастройкаСчетовУчета");
	
	КонтекстНастройки = ДанныеПоПути(Форма, ПараметрыНастройки.ПутьКДанным);
	Если ТипЗнч(КонтекстНастройки) = Тип("ДанныеФормыКоллекция") Тогда
		СписокСтатей = Новый Массив();
		Если ИдентификаторыСтрок <> Неопределено Тогда
			Для Каждого ИдентификаторСтроки Из ИдентификаторыСтрок Цикл
				СтрокаТаблицы = КонтекстНастройки.НайтиПоИдентификатору(ИдентификаторСтроки);
				Если ТребуетсяЗаполнениеНастройкиСчетовУчетаПоУмолчанию(Форма, СтрокаТаблицы, ПараметрыНастройки) Тогда
					СписокСтатей.Добавить(СтрокаТаблицы[ПараметрыНастройки.СтатьяАктивовПассивов]);
				КонецЕсли;
			КонецЦикла;
		Иначе
			Для Каждого СтрокаТаблицы Из КонтекстНастройки Цикл
				Если ТребуетсяЗаполнениеНастройкиСчетовУчетаПоУмолчанию(Форма, СтрокаТаблицы, ПараметрыНастройки) Тогда
					СписокСтатей.Добавить(СтрокаТаблицы[ПараметрыНастройки.СтатьяАктивовПассивов]);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		НастройкиПоУмолчанию = НастройкаСчетовУчетаСлужебныйВызовСервера.ДанныеНастроекСчетовУчетаПоУмолчанию(
			СписокСтатей, ДанныеНастройкиСчетовУчета);
		
		Если ИдентификаторыСтрок <> Неопределено Тогда
			Для Каждого ИдентификаторСтроки Из ИдентификаторыСтрок Цикл
				СтрокаТаблицы = КонтекстНастройки.НайтиПоИдентификатору(ИдентификаторСтроки);
				ЗаполнитьНастройкуСчетовУчетаПоУмолчанию(СтрокаТаблицы, ПараметрыНастройки, НастройкиПоУмолчанию, ДанныеНастройкиСчетовУчета);
			КонецЦикла;
		Иначе
			Для Каждого СтрокаТаблицы Из КонтекстНастройки Цикл
				ЗаполнитьНастройкуСчетовУчетаПоУмолчанию(СтрокаТаблицы, ПараметрыНастройки, НастройкиПоУмолчанию, ДанныеНастройкиСчетовУчета);
			КонецЦикла;
		КонецЕсли;
	ИначеЕсли ТребуетсяЗаполнениеНастройкиСчетовУчетаПоУмолчанию(Форма, КонтекстНастройки, ПараметрыНастройки) Тогда
		НастройкаПоУмолчанию = НастройкаСчетовУчетаСлужебныйВызовСервера.ДанныеНастроекСчетовУчетаПоУмолчанию(
			КонтекстНастройки[ПараметрыНастройки.СтатьяАктивовПассивов], ДанныеНастройкиСчетовУчета);
		ЗаполнитьНастройкуСчетовУчетаПоУмолчанию(КонтекстНастройки, ПараметрыНастройки, НастройкаПоУмолчанию, ДанныеНастройкиСчетовУчета);
	КонецЕсли;

КонецПроцедуры

//++ Локализация

// Устанавливает свойство элемента измерения
// 
// Параметры:
// 	ЭлементНаФорме - ПолеФормы, ГруппаФормы, ТаблицаФормы, КомандаФормы -
// 	СвойстваИзмерения - Структура, Неопределено -
Процедура УстановитьСвойствоЭлементаИзмерения(ЭлементНаФорме, СвойстваИзмерения = Неопределено)

	Если СвойстваИзмерения = Неопределено Тогда
		ЭлементНаФорме.Видимость = Ложь;
		ЭлементНаФорме.АвтоОтметкаНезаполненного = Ложь;
		ЭлементНаФорме.Заголовок = "";
		ЭлементНаФорме.Подсказка = "";
		ЭлементНаФорме.ПодсказкаВвода = "";
		ЭлементНаФорме.ОграничениеТипа = Новый ОписаниеТипов;
	Иначе
		ЭлементНаФорме.Видимость = СвойстваИзмерения.Используется;
		Если СвойстваИзмерения.Используется Тогда
			Если СвойстваИзмерения.Свойство("Заголовок") Тогда
				Если Не ЗначениеЗаполнено(ЭлементНаФорме.Заголовок) И Не ЗначениеЗаполнено(ЭлементНаФорме.Подсказка) Тогда
					ЭлементНаФорме.Заголовок = СвойстваИзмерения.Заголовок;
				ИначеЕсли ЗначениеЗаполнено(ЭлементНаФорме.Заголовок) И ЭлементНаФорме.Заголовок
					<> СвойстваИзмерения.Заголовок И Не ЗначениеЗаполнено(ЭлементНаФорме.Подсказка) Тогда
					ЭлементНаФорме.Подсказка = ЭлементНаФорме.Заголовок + " / " + СвойстваИзмерения.Заголовок;
					ЭлементНаФорме.Заголовок = "";
				ИначеЕсли Не ЗначениеЗаполнено(ЭлементНаФорме.Заголовок) И ЗначениеЗаполнено(ЭлементНаФорме.Подсказка)
					И СтрНайти(ЭлементНаФорме.Подсказка, СвойстваИзмерения.Заголовок) = 0 Тогда
					ЭлементНаФорме.Подсказка = ЭлементНаФорме.Подсказка + " / " + СвойстваИзмерения.Заголовок;
				КонецЕсли;
			КонецЕсли;
			Если СвойстваИзмерения.Свойство("ПредставлениеПустого") Тогда
				ИзмерениеОбязательно = Не ЗначениеЗаполнено(СвойстваИзмерения.ПредставлениеПустого);
				ЭлементНаФорме.АвтоОтметкаНезаполненного = ЭлементНаФорме.АвтоОтметкаНезаполненного = Истина
					Или ИзмерениеОбязательно;
				Если Не ИзмерениеОбязательно Тогда
					Если Не ЗначениеЗаполнено(ЭлементНаФорме.ПодсказкаВвода) Тогда
						ЭлементНаФорме.ПодсказкаВвода = СвойстваИзмерения.ПредставлениеПустого;
					ИначеЕсли ЗначениеЗаполнено(ЭлементНаФорме.ПодсказкаВвода) И ЭлементНаФорме.ПодсказкаВвода
						<> СвойстваИзмерения.ПредставлениеПустого Тогда
						Если СвойстваИзмерения.ПредставлениеПустого = НСтр("ru = 'не указан';
																			|en = 'not specified'") Тогда
							// Это аналитика, ничего не делаем
						Иначе
							ЭлементНаФорме.ПодсказкаВвода = НСтр("ru = 'Для всех мест учета';
																|en = 'For all accounting places'");
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			Если СвойстваИзмерения.Свойство("ТипЗначения") Тогда
				ЭлементНаФорме.ОграничениеТипа = Новый ОписаниеТипов(ЭлементНаФорме.ОграничениеТипа,
					СвойстваИзмерения.ТипЗначения.Типы());
			КонецЕсли;
			Если ЭлементНаФорме.ОграничениеТипа.Типы().Количество() > 1 Тогда
				ЭлементНаФорме.ПодсказкаВвода = "";
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Устанавлевает отбор по валютным счетам
// 
// Параметры:
// 	МассивПараметровВыбора - Массив - 
// 	Реквизиты - Структура - 
// 	ОбщиеРеквизиты - Структура
Процедура УстановитьОтборВалютныхСчетов(МассивПараметровВыбора, Реквизиты, ОбщиеРеквизиты)
		
		Если Реквизиты = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Если Реквизиты.Свойство("РасчетыВВалюте")
			И ТипЗнч(Реквизиты.РасчетыВВалюте) = Тип("Булево") Тогда
			МассивПараметровВыбора.Добавить(Новый ПараметрВыбора("Отбор.Валютный", Реквизиты.РасчетыВВалюте));
		ИначеЕсли Реквизиты.Свойство("ВалютаДенежныхСредств") 
			И ТипЗнч(Реквизиты.ВалютаДенежныхСредств) = Тип("СправочникСсылка.Валюты") 
			И ЗначениеЗаполнено(Реквизиты.ВалютаДенежныхСредств)Тогда
				МассивПараметровВыбора.Добавить(Новый ПараметрВыбора("Отбор.Валютный",
					Реквизиты.ВалютаДенежныхСредств <> ОбщиеРеквизиты.ВалютаРеглУчета));
		КонецЕсли;
		
КонецПроцедуры

Функция РазделСДинамическимиПараметрамиВыбораСчета(РазделУчета)

	Если РазделУчета = "ВнеоборотныеАктивы" Или РазделУчета = "ДенежныеСредства" 
			Или РазделУчета = "Расходы" Или РазделУчета = "РасчетыСПартнерами"
			Или ВидыРасчетовСПартнерами().Найти(РазделУчета) <> Неопределено Тогда

		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Функция СчетаБезОтбораПоВалюте()
	
	МассивВозврата = Новый Массив;
	МассивВозврата.Добавить("ТоварыКомитентовКОформлениюПоПродаже");
	МассивВозврата.Добавить("ТоварыКомитентовКОформлениюПоЗакупке");
	МассивВозврата.Добавить("РезервыПоСомнительнымДолгам");
	МассивВозврата.Добавить("НДСАвансыПолученные");
	МассивВозврата.Добавить("НДСАвансыВыданные");
	МассивВозврата.Добавить("НеотфактурованныеПоставки");
	Возврат МассивВозврата;
	
КонецФункции

#Область НастройкаСчетовУчетаРасходов

Функция МассивДоступныхСчетовРасходов(ИмяСчета, Реквизиты, ДоступныеСчетаУчета)
	
	МассивВозврата = Новый Массив;
	
	Если ИмяСчета = "Расходы" Тогда
		СчетаРасходов = ОбщегоНазначенияКлиентСервер.РазностьМассивов(ДоступныеСчетаУчета.СчетаРасходов, ДоступныеСчетаУчета.СчетаСписания);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивВозврата, СчетаРасходов);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивВозврата, ДоступныеСчетаУчета.СчетаПрочихРасходов);
	Иначе
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивВозврата, ДоступныеСчетаУчета.СчетаСписания);
	КонецЕсли;
	
	Возврат МассивВозврата;
	
КонецФункции

#КонецОбласти

#Область НастройкаСчетовУчетаДенежныхСредств

Функция МассивДоступныхСчетовДенежныхСредств(Реквизиты, ДоступныеСчетаУчета)

	МассивВозврата = Новый Массив;

	Если Реквизиты = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивВозврата, ДоступныеСчетаУчета.СчетаБезналичныхДенежныхСредств);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивВозврата, ДоступныеСчетаУчета.СчетаНаличныхДенежныхСредств);
	ИначеЕсли ТипЗнч(Реквизиты.АналитикаДенежныхСредств) = Тип("СправочникСсылка.БанковскиеСчетаОрганизаций") Тогда
		Если Реквизиты.ТипБанковскогоСчета = ПредопределенноеЗначение("Перечисление.ТипыБанковскихСчетов.Депозитный") Тогда
			Если Не ЗначениеЗаполнено(Реквизиты.ВалютаДенежныхСредств) Тогда
				МассивВозврата.Добавить(ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.ДепозитныеСчетаВал"));
				МассивВозврата.Добавить(ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.ДепозитныеСчета"));
			КонецЕсли;
		ИначеЕсли Реквизиты.ТипБанковскогоСчета = ПредопределенноеЗначение("Перечисление.ТипыБанковскихСчетов.Расчетный") Тогда
			Если Не ЗначениеЗаполнено(Реквизиты.ВалютаДенежныхСредств) Тогда
				МассивВозврата.Добавить(ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.ВалютныеСчета"));
				МассивВозврата.Добавить(ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.РасчетныеСчета"));
			КонецЕсли;
		Иначе
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивВозврата, ДоступныеСчетаУчета.СчетаБезналичныхДенежныхСредств);
		КонецЕсли;
	ИначеЕсли ТипЗнч(Реквизиты.АналитикаДенежныхСредств) = Тип("СправочникСсылка.Кассы") Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивВозврата, ДоступныеСчетаУчета.СчетаНаличныхДенежныхСредств);
	КонецЕсли;

	Если МассивВозврата.Количество() = 0 Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивВозврата, ДоступныеСчетаУчета.СчетаБезналичныхДенежныхСредств);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивВозврата, ДоступныеСчетаУчета.СчетаНаличныхДенежныхСредств);
	КонецЕсли;

	Возврат МассивВозврата;

КонецФункции

Процедура ЗаполнитьСчетаУчетаПоУмолчаниюДенежныеСредства(Форма)

	Настройки = Форма.КэшДанныхМеханизмов.НастройкаСчетовУчета;
	Аналитика = Настройки.РеквизитыАналитики.АналитикаДенежныхСредств;
	ТипБанковскогоСчета = ?(ТипЗнч(Аналитика) = Тип("СправочникСсылка.БанковскиеСчетаОрганизаций"),
		Настройки.РеквизитыАналитики.ТипБанковскогоСчета, Неопределено); 

	ИмяРеквизита = ИмяРеквизитаНаФорме(Настройки, "ДенежныеСредства", "ДенежныеСредства");
	Если ТипБанковскогоСчета = ПредопределенноеЗначение("Перечисление.ТипыБанковскихСчетов.Депозитный") Тогда
		Если Настройки.РеквизитыАналитики.ВалютаДенежныхСредств <> Настройки.ОбщиеРеквизиты.ВалютаРеглУчета Тогда
			Форма[ИмяРеквизита] = ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.ДепозитныеСчетаВал");
		КонецЕсли;
		Если Настройки.РеквизитыАналитики.ВалютаДенежныхСредств = Настройки.ОбщиеРеквизиты.ВалютаРеглУчета Тогда
			Форма[ИмяРеквизита] = ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.ДепозитныеСчета");
		КонецЕсли;
	ИначеЕсли ТипБанковскогоСчета = ПредопределенноеЗначение("Перечисление.ТипыБанковскихСчетов.Расчетный") Тогда
		Если Настройки.РеквизитыАналитики.ВалютаДенежныхСредств <> Настройки.ОбщиеРеквизиты.ВалютаРеглУчета Тогда
			Форма[ИмяРеквизита] = ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.ВалютныеСчета");
		КонецЕсли;
		Если Настройки.РеквизитыАналитики.ВалютаДенежныхСредств = Настройки.ОбщиеРеквизиты.ВалютаРеглУчета Тогда
			Форма[ИмяРеквизита] = ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.РасчетныеСчета");
		КонецЕсли;
	Иначе
		Форма[ИмяРеквизита] = ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.ПустаяСсылка");
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область НастройкаСчетовУчетаЭквайринга

Процедура ЗаполнитьСчетаУчетаПоУмолчаниюЭквайринг(Форма)
	
	Настройки = Форма.КэшДанныхМеханизмов.НастройкаСчетовУчета;
	
	ИмяРеквизита = ИмяРеквизитаНаФорме(Настройки, "Эквайринг", "ПродажиПоЭквайрингу");
	Форма[ИмяРеквизита] = ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.ПродажиПоПлатежнымКартам");
	
КонецПроцедуры

#КонецОбласти

#Область НастройкаСчетовУчетаВнеоборотныхАктивов

Функция МассивДоступныхСчетовВнеоборотныхАктивов(ИмяСчета, Реквизиты, ДоступныеСчетаУчета)

	МассивВозврата = Новый Массив;

	ВидАктиваНМА = 
		Реквизиты = Неопределено 
		Или Реквизиты.ВидАктива = ПредопределенноеЗначение("Перечисление.ВидыВнеоборотныхАктивов.НМА") 
		Или Реквизиты.ВидАктива = ПредопределенноеЗначение("Перечисление.ВидыВнеоборотныхАктивов.НИОКР");
		
	ВидАктиваОС = 
		Реквизиты = Неопределено 
		Или Реквизиты.ВидАктива <> ПредопределенноеЗначение("Перечисление.ВидыВнеоборотныхАктивов.НМА") 
			И Реквизиты.ВидАктива <> ПредопределенноеЗначение("Перечисление.ВидыВнеоборотныхАктивов.НИОКР");

	Если ИмяСчета = "СтоимостьВНА" И ВидАктиваНМА Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивВозврата, ДоступныеСчетаУчета.СчетаСтоимость_НМА);
	КонецЕсли;
	Если ИмяСчета = "СтоимостьВНА" И ВидАктиваОС Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивВозврата, ДоступныеСчетаУчета.СчетаСтоимость_ОС);
	КонецЕсли;

	Если ИмяСчета = "СтоимостьВНА_ЦФ" И ВидАктиваНМА Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивВозврата, ДоступныеСчетаУчета.СчетаСтоимость_НМА_ЦФ);
	КонецЕсли;
	Если ИмяСчета = "СтоимостьВНА_ЦФ" И ВидАктиваОС Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивВозврата, ДоступныеСчетаУчета.СчетаСтоимость_ОС_ЦФ);
	КонецЕсли;

	Если ИмяСчета = "АмортизацияВНА" И ВидАктиваНМА Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивВозврата, ДоступныеСчетаУчета.СчетаАмортизация_НМА);
	КонецЕсли;
	Если ИмяСчета = "АмортизацияВНА" И ВидАктиваОС Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивВозврата, ДоступныеСчетаУчета.СчетаАмортизация_ОС);
	КонецЕсли;

	Если ИмяСчета = "АмортизацияВНА_ЦФ" И ВидАктиваНМА Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивВозврата, ДоступныеСчетаУчета.СчетаАмортизация_НМА_ЦФ);
	КонецЕсли;
	Если ИмяСчета = "АмортизацияВНА_ЦФ" И ВидАктиваОС Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивВозврата, ДоступныеСчетаУчета.СчетаАмортизация_ОС_ЦФ);
	КонецЕсли;

	Если ИмяСчета = "ВыбытиеВНА" Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивВозврата, ДоступныеСчетаУчета.СчетаВыбытие_ОС);
	КонецЕсли;

	Если ИмяСчета = "РезервДооценкиВНА" И ВидАктиваНМА Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивВозврата, ДоступныеСчетаУчета.СчетаРезервДооценки_НМА);
	КонецЕсли;
	Если ИмяСчета = "РезервДооценкиВНА" И ВидАктиваОС Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивВозврата, ДоступныеСчетаУчета.СчетаРезервДооценки_ОС);
	КонецЕсли;

	Если ИмяСчета = "ЗабалансовыйУчетВНА" Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивВозврата, ДоступныеСчетаУчета.СчетаЗабалансовогоУчета_ОС);
	КонецЕсли;

	Если ИмяСчета = "ОбесценениеВНА" И ВидАктиваНМА Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивВозврата, ДоступныеСчетаУчета.СчетаОбесценения_НМА);
	КонецЕсли;
	Если ИмяСчета = "ОбесценениеВНА" И ВидАктиваОС Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивВозврата, ДоступныеСчетаУчета.СчетаОбесценения_ОС);
	КонецЕсли;
	
	Если (ИмяСчета = "АрендныеПлатежи" ИЛИ ИмяСчета = "НГЛС" ИЛИ ИмяСчета = "ПроцентныйДисконт") И (Реквизиты = Неопределено
		Или Реквизиты.ВидАктива = ПредопределенноеЗначение("Перечисление.ВидыВнеоборотныхАктивов.ОсновноеСредство")) Тогда
		ДоступныеСчетаУчета.Свойство(ИмяСчета, МассивВозврата);
	КонецЕсли;

	Возврат МассивВозврата;

КонецФункции

Процедура ЗаполнитьСчетаУчетаПоУмолчаниюВнеоборотныеАктивы(Форма)

	Настройки = Форма.КэшДанныхМеханизмов.НастройкаСчетовУчета;
	
	Для каждого ЭлементСоответствия Из ИменаСчетовРеглУчетаВНАПоВидуАктива(Настройки.РеквизитыАналитики.ВидАктива) Цикл
		
		ИмяРеквизита = ИмяРеквизитаНаФорме(Настройки, "ВнеоборотныеАктивы", ЭлементСоответствия.Ключ);
		ПутьКСчетуУчета = СтрШаблон("ПланСчетов.Хозрасчетный.%1", ЭлементСоответствия.Значение);
		
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, ИмяРеквизита) Тогда
			Форма[ИмяРеквизита] = ПредопределенноеЗначение(ПутьКСчетуУчета);
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

Функция ИменаСчетовРеглУчетаВНАПоВидуАктива(ВидАктива)
	
	СоответствиеВидовСчетовИменамСчетовРеглУчета = Новый Соответствие;
	
	Если ВидАктива = ПредопределенноеЗначение("Перечисление.ВидыВнеоборотныхАктивов.ОсновноеСредство")
		Или ВидАктива = ПредопределенноеЗначение("Перечисление.ВидыВнеоборотныхАктивов.ИнвестиционноеИмущество") Тогда
		
		СоответствиеВидовСчетовИменамСчетовРеглУчета.Вставить("СтоимостьВНА", "ОСвОрганизации");
		СоответствиеВидовСчетовИменамСчетовРеглУчета.Вставить("СтоимостьВНА_ЦФ", "ОСвОрганизации_ЦФ");
		СоответствиеВидовСчетовИменамСчетовРеглУчета.Вставить("АмортизацияВНА", "АмортизацияОС_01");
		СоответствиеВидовСчетовИменамСчетовРеглУчета.Вставить("АмортизацияВНА_ЦФ", "АмортизацияОС_01_ЦФ");
		СоответствиеВидовСчетовИменамСчетовРеглУчета.Вставить("ВыбытиеВНА", "ВыбытиеОС");
		СоответствиеВидовСчетовИменамСчетовРеглУчета.Вставить("РезервДооценкиВНА", "ПриростСтоимостиИмуществаПоПереоценке");
		СоответствиеВидовСчетовИменамСчетовРеглУчета.Вставить("ЗабалансовыйУчетВНА", "АрендованныеОсновныеСредства");
		
	ИначеЕсли ВидАктива = ПредопределенноеЗначение("Перечисление.ВидыВнеоборотныхАктивов.НМА") Тогда
		
		СоответствиеВидовСчетовИменамСчетовРеглУчета.Вставить("СтоимостьВНА", "НематериальныеАктивыОрганизации");
		СоответствиеВидовСчетовИменамСчетовРеглУчета.Вставить("СтоимостьВНА_ЦФ", "ПустаяСсылка");
		СоответствиеВидовСчетовИменамСчетовРеглУчета.Вставить("АмортизацияВНА", "АмортизацияНематериальныхАктивов");
		СоответствиеВидовСчетовИменамСчетовРеглУчета.Вставить("АмортизацияВНА_ЦФ", "ПустаяСсылка");
		СоответствиеВидовСчетовИменамСчетовРеглУчета.Вставить("ВыбытиеВНА", "ПустаяСсылка");
		СоответствиеВидовСчетовИменамСчетовРеглУчета.Вставить("РезервДооценкиВНА", "ПриростСтоимостиНМАПоПереоценке");
		СоответствиеВидовСчетовИменамСчетовРеглУчета.Вставить("ЗабалансовыйУчетВНА", "ПустаяСсылка");
		
	ИначеЕсли ВидАктива = ПредопределенноеЗначение("Перечисление.ВидыВнеоборотныхАктивов.НИОКР") Тогда
		
		СоответствиеВидовСчетовИменамСчетовРеглУчета.Вставить("СтоимостьВНА", "РасходыНаНИОКР");
		СоответствиеВидовСчетовИменамСчетовРеглУчета.Вставить("СтоимостьВНА_ЦФ", "ПустаяСсылка");
		СоответствиеВидовСчетовИменамСчетовРеглУчета.Вставить("АмортизацияВНА", "ПустаяСсылка");
		СоответствиеВидовСчетовИменамСчетовРеглУчета.Вставить("АмортизацияВНА_ЦФ", "ПустаяСсылка");
		СоответствиеВидовСчетовИменамСчетовРеглУчета.Вставить("ВыбытиеВНА", "ПустаяСсылка");
		СоответствиеВидовСчетовИменамСчетовРеглУчета.Вставить("РезервДооценкиВНА", "ПустаяСсылка");
		СоответствиеВидовСчетовИменамСчетовРеглУчета.Вставить("ЗабалансовыйУчетВНА", "ПустаяСсылка");
		
	КонецЕсли;
	
	Возврат СоответствиеВидовСчетовИменамСчетовРеглУчета;
	
КонецФункции

#КонецОбласти

#Область НастройкиГрупп

Функция СписокГруппНаФорме(РазделУчета, ЭтоСписок)

	СписокГруппНаФорме = Новый СписокЗначений;

	Если РазделУчета = "Номенклатура" Или РазделУчета = "НоменклатураСобственная" И ЭтоСписок Тогда
		СписокГруппНаФорме.Добавить("Стоимость", НСтр("ru = 'Учет стоимости товаров';
														|en = 'Goods cost accounting'"));
		СписокГруппНаФорме.Добавить("Выручка", НСтр("ru = 'Выручка от продаж';
													|en = 'Sales revenue'"));
		СписокГруппНаФорме.Добавить("Себестоимость", НСтр("ru = 'Себестоимость продаж';
															|en = 'COGS'"));
		СписокГруппНаФорме.Добавить("СтатьиУчетаВыручки", НСтр("ru = 'Статьи учета выручки';
																|en = 'Revenue accounting items'"));
		СписокГруппНаФорме.Добавить("СтатьиУчетаСебестоимости", НСтр("ru = 'Статьи учета себестоимости';
																	|en = 'Cost accounting items'"));
		СписокГруппНаФорме.Добавить("НДС", НСтр("ru = 'НДС товаров';
												|en = 'VAT goods'"));
	КонецЕсли;

	Если РазделУчета = "Номенклатура" Или РазделУчета = "НоменклатураСобственная" И Не ЭтоСписок Тогда
		СписокГруппНаФорме.Добавить("Стоимость", НСтр("ru = 'Счета учета стоимости номенклатуры';
														|en = 'Ledger accounts of product cost'"));
		СписокГруппНаФорме.Добавить("Продажи", НСтр("ru = 'Счета учета продаж';
													|en = 'Sales ledger account'"));
		СписокГруппНаФорме.Добавить("ПродажиСубконто", НСтр("ru = 'Статьи доходов / расходов';
															|en = 'Income / expense items'"));
		СписокГруппНаФорме.Добавить("НДС", НСтр("ru = 'Счета учета НДС';
												|en = 'VAT ledger account'"));
		СписокГруппНаФорме.Добавить("Резервы", НСтр("ru = 'Резервы';
													|en = 'Reserves'"));
	КонецЕсли;

	Если РазделУчета = "Номенклатура" Или РазделУчета = "НоменклатураПринятая" Тогда
		
		СписокГруппНаФорме.Добавить("ТМЦПринятыеНаОтветственноеХранение", НСтр("ru = 'ТМЦ, принятые на ответственное хранение';
																				|en = 'Available VMI'"));
		СписокГруппНаФорме.Добавить("МатериалыПринятыеВПереработку", НСтр("ru = 'Материалы, принятые в переработку';
																			|en = 'Materials received for processing'"));
		СписокГруппНаФорме.Добавить("ТоварыПринятыеНаКомиссию", НСтр("ru = 'Товары, принятые на комиссию';
																	|en = 'Goods received by consignee'"));

	КонецЕсли;

	Если РазделУчета = "РасчетыСКомиссионерами" Тогда
		
		СписокГруппНаФорме.Добавить("Товары", НСтр("ru = 'Расчеты за товары (услуги)';
													|en = 'Settlements for goods (services)'"));
		СписокГруппНаФорме.Добавить("Вознаграждение", НСтр("ru = 'Расчеты по вознаграждению';
															|en = 'Remuneration settlements'"));
		СписокГруппНаФорме.Добавить("Прочее", НСтр("ru = 'Прочее';
													|en = 'Other'"));
		
	КонецЕсли;

	Если РазделУчета = "РасчетыСКомитентами" Тогда
		
		СписокГруппНаФорме.Добавить("Товары", НСтр("ru = 'Расчеты за товары (услуги)';
													|en = 'Settlements for goods (services)'"));
		СписокГруппНаФорме.Добавить("Вознаграждение", НСтр("ru = 'Расчеты по вознаграждению';
															|en = 'Remuneration settlements'"));
		СписокГруппНаФорме.Добавить("ТоварыКОформлению", НСтр("ru = 'Товары к оформлению';
																|en = 'Goods for registration'"));
		СписокГруппНаФорме.Добавить("Прочее", НСтр("ru = 'Прочее';
													|en = 'Other'"));
		
	КонецЕсли;

	Если РазделУчета = "РасчетыСКомитентамиПоЗакупке" Тогда
		
		СписокГруппНаФорме.Добавить("ТоварыВознаграждение", НСтр("ru = 'Расчеты за товары (услуги), вознаграждение';
																|en = 'Settlements for goods (services), remuneration'"));
		СписокГруппНаФорме.Добавить("ТоварыКОформлению", НСтр("ru = 'Товары к оформлению';
																|en = 'Goods for registration'"));
		СписокГруппНаФорме.Добавить("Прочее", НСтр("ru = 'Прочее';
													|en = 'Other'"));
		
	КонецЕсли;

	Если РазделУчета = "РасчетыСПереработчиками" Тогда
		
		СписокГруппНаФорме.Добавить("Услуги", ?(ЭтоСписок, НСтр("ru = 'Услуги';
																|en = 'Services'"), НСтр("ru = 'Расчеты за услуги';
																						|en = 'Settlements for services'")));
		СписокГруппНаФорме.Добавить("Выкуп", ?(ЭтоСписок, НСтр("ru = 'Выкуп';
																|en = 'Redemption'"), НСтр("ru = 'Расчеты по выкупу';
																					|en = 'Buyback AR/AP'")));
		СписокГруппНаФорме.Добавить("Прочее", НСтр("ru = 'Прочее';
													|en = 'Other'"));
		
	КонецЕсли;

	Если РазделУчета = "РасчетыСДавальцами" Тогда
		СписокГруппНаФорме.Добавить("Услуги", ?(ЭтоСписок, НСтр("ru = 'Услуги';
																|en = 'Services'"), НСтр("ru = 'Расчеты за услуги';
																						|en = 'Settlements for services'")));
		СписокГруппНаФорме.Добавить("Выкуп", ?(ЭтоСписок, НСтр("ru = 'Выкуп';
																|en = 'Redemption'"), НСтр("ru = 'Расчеты по выкупу';
																					|en = 'Buyback AR/AP'")));
	КонецЕсли;
	
	Если РазделУчета = "ВнеоборотныеАктивы" Тогда
		
		СписокГруппНаФорме.Добавить("Основное", НСтр("ru = 'Основные счета учета';
													|en = 'Main ledger accounts'"));
		СписокГруппНаФорме.Добавить("ЦелевоеФинансирование", НСтр("ru = 'Целевое финансирование';
																	|en = 'Target financing'"));
		СписокГруппНаФорме.Добавить("ЗабалансовыйУчет", НСтр("ru = 'Забалансовый учет';
															|en = 'Off-balance accounting'"));
		СписокГруппНаФорме.Добавить("ФинансоваяАренда", НСтр("ru = 'Финансовая аренда';
															|en = 'Financial lease'"));
		
	КонецЕсли;

	Возврат СписокГруппНаФорме;

КонецФункции

Функция СчетаУчетаГруппы(ИмяГруппы, РазделУчета = Неопределено)

	МассивСчетов = Новый Массив;

	Если ИмяГруппы = "Стоимость" Тогда

		МассивСчетов.Добавить("НаСкладе");
		МассивСчетов.Добавить("ДопПоле_ОтноситсяКМалоценнымТМЦ");
		МассивСчетов.Добавить("РеализацияБезПереходаПраваСобственности");
		МассивСчетов.Добавить("ЗатратыНаПриобретениеТМЦ");
		МассивСчетов.Добавить("НоменклатураПереданная");
		МассивСчетов.Добавить("ПередачаВПереработку");

	КонецЕсли;

	Если ИмяГруппы = "Выручка" Или ИмяГруппы = "Продажи" Тогда

		МассивСчетов.Добавить("ВыручкаОтПродаж");
		МассивСчетов.Добавить("ВыручкаОтПродажЕНВД");
		МассивСчетов.Добавить("ВыручкаРаздельныйНУ");

	КонецЕсли;

	Если ИмяГруппы = "Себестоимость" Или ИмяГруппы = "Продажи" Тогда

		МассивСчетов.Добавить("СебестоимостьПродаж");
		МассивСчетов.Добавить("СебестоимостьПродажЕНВД");

	КонецЕсли;

	Если ИмяГруппы = "ПродажиСубконто" Или ИмяГруппы = "СтатьиУчетаВыручки" Тогда

		МассивСчетов.Добавить("Субконто_ВыручкаОтПродаж");
		МассивСчетов.Добавить("Субконто_ВыручкаОтПродажЕНВД");
		МассивСчетов.Добавить("Субконто_ВыручкаРаздельныйНУ");
	
	КонецЕсли;
	
	Если ИмяГруппы = "ПродажиСубконто" Или ИмяГруппы = "СтатьиУчетаСебестоимости" Тогда
	
		МассивСчетов.Добавить("Субконто_СебестоимостьПродаж");
		МассивСчетов.Добавить("Субконто_СебестоимостьПродажЕНВД");

	КонецЕсли;

	Если ИмяГруппы = "НДС" Тогда

		МассивСчетов.Добавить("НДСПриПродаже");
		МассивСчетов.Добавить("НДСПриОтгрузкеБезПереходаПраваСобственности");
		МассивСчетов.Добавить("Субконто_НДСПриПродаже");

	КонецЕсли;

	Если ИмяГруппы = "Резервы" Тогда

		МассивСчетов.Добавить("РезервыПодОбесценениеТМЦ");
		МассивСчетов.Добавить("РезервыПодОбесценениеПереданные");

	КонецЕсли;

	Если ИмяГруппы = "ТМЦПринятыеНаОтветственноеХранение" Тогда

		МассивСчетов.Добавить("ТМЦНаСкладах");
		МассивСчетов.Добавить("ТМЦВПроизводстве");
		МассивСчетов.Добавить("ТМЦПотребленныеВПроизводстве");
		МассивСчетов.Добавить("ТМЦУХранителей");
		МассивСчетов.Добавить("ТМЦКСписанию");

	КонецЕсли;

	Если ИмяГруппы = "МатериалыПринятыеВПереработку" Тогда

		МассивСчетов.Добавить("МатериалыПринятыеВПереработку");
		МассивСчетов.Добавить("МатериалыПринятыеВПереработкуВПроизводстве");
		МассивСчетов.Добавить("МатериалыПотребленныеВПроизводстве");
		МассивСчетов.Добавить("МатериалыПринятыеВПереработкуУХранителей");
		МассивСчетов.Добавить("МатериалыПринятыеВПереработкуКСписанию");

	КонецЕсли;

	Если ИмяГруппы = "ТоварыПринятыеНаКомиссию" Тогда

		МассивСчетов.Добавить("ТоварыНаСкладе");
		МассивСчетов.Добавить("ТоварыПереданныеНаКомиссию");

	КонецЕсли;

	Если ИмяГруппы = "ТоварыПринятыеНаКомиссию" Тогда

		МассивСчетов.Добавить("ТоварыНаСкладе");
		МассивСчетов.Добавить("ТоварыПереданныеНаКомиссию");

	КонецЕсли;

	Если ИмяГруппы = "ТоварыПринятыеНаКомиссию" Тогда

		МассивСчетов.Добавить("ТоварыНаСкладе");
		МассивСчетов.Добавить("ТоварыПереданныеНаКомиссию");

	КонецЕсли;

	Если РазделУчета = "РасчетыСКомиссионерами" И ИмяГруппы = "Товары"
		Или РазделУчета = "РасчетыСКомитентами" И ИмяГруппы = "Вознаграждение"
		Или РазделУчета = "РасчетыСКомитентамиПоЗакупке" И ИмяГруппы = "ТоварыВознаграждение"
		Или РазделУчета = "РасчетыСПереработчиками" И ИмяГруппы = "Выкуп"
		Или РазделУчета = "РасчетыСДавальцами" И ИмяГруппы = "Услуги" Тогда
		МассивСчетов.Добавить("РасчетыСКлиентами");
		МассивСчетов.Добавить("АвансыПолученные");
		МассивСчетов.Добавить("НДСАвансыПолученные");
	КонецЕсли;

	Если РазделУчета = "РасчетыСКомиссионерами" И ИмяГруппы = "Вознаграждение"
		Или РазделУчета = "РасчетыСКомитентами" И ИмяГруппы = "Товары"
		Или РазделУчета = "РасчетыСПереработчиками" И ИмяГруппы = "Услуги"
		Или РазделУчета = "РасчетыСДавальцами" И ИмяГруппы = "Выкуп" Тогда
		МассивСчетов.Добавить("РасчетыСПоставщиками");
		МассивСчетов.Добавить("АвансыВыданные");
		МассивСчетов.Добавить("НДСАвансыВыданные");
	КонецЕсли;

	Если РазделУчета = "РасчетыСКомитентами" И ИмяГруппы = "ТоварыКОформлению" Тогда
		МассивСчетов.Добавить("ТоварыКомитентовКОформлениюПоПродаже");
	КонецЕсли;

	Если РазделУчета = "РасчетыСКомитентамиПоЗакупке" И ИмяГруппы = "ТоварыКОформлению" Тогда
		МассивСчетов.Добавить("ТоварыКомитентовКОформлениюПоЗакупке");
	КонецЕсли;

	Если РазделУчета = "РасчетыСКомитентамиПоЗакупке" И ИмяГруппы = "Прочее" Тогда
		МассивСчетов.Добавить("РезервыПоСомнительнымДолгам");
		МассивСчетов.Добавить("НДСАвансыВыданные");
	КонецЕсли;

	Если ИмяГруппы = "Прочее" И (РазделУчета = "РасчетыСПереработчиками" ИЛИ РазделУчета = "РасчетыСКомиссионерами"
		ИЛИ РазделУчета = "РасчетыСКомитентами") Тогда
		МассивСчетов.Добавить("РезервыПоСомнительнымДолгам");
	КонецЕсли;

	Если ИмяГруппы = "Основное" Тогда

		МассивСчетов.Добавить("СтоимостьВНА");
		МассивСчетов.Добавить("АмортизацияВНА");
		МассивСчетов.Добавить("РезервДооценкиВНА");
		МассивСчетов.Добавить("ВыбытиеВНА");
		МассивСчетов.Добавить("ОбесценениеВНА");

	КонецЕсли;

	Если ИмяГруппы = "ЦелевоеФинансирование" Тогда

		МассивСчетов.Добавить("СтоимостьВНА_ЦФ");
		МассивСчетов.Добавить("АмортизацияВНА_ЦФ");

	КонецЕсли;

	Если ИмяГруппы = "ЗабалансовыйУчет" Тогда

		МассивСчетов.Добавить("ЗабалансовыйУчетВНА");

	КонецЕсли;

	Если ИмяГруппы = "ФинансоваяАренда" Тогда

		МассивСчетов.Добавить("АрендныеПлатежи");
		МассивСчетов.Добавить("НГЛС");
		МассивСчетов.Добавить("ПроцентныйДисконт");

	КонецЕсли;

	Возврат МассивСчетов;

КонецФункции

Функция ГруппировкаГруппы(ИмяГруппы, ЭтоСписок)
	
	СписокГруппСГоризонтальнойГруппировкой = Новый Массив;
	
	СписокГруппСГоризонтальнойГруппировкой.Добавить("Стоимость");
	СписокГруппСГоризонтальнойГруппировкой.Добавить("НДС");
	СписокГруппСГоризонтальнойГруппировкой.Добавить("Продажи");
	СписокГруппСГоризонтальнойГруппировкой.Добавить("Резервы");
	СписокГруппСГоризонтальнойГруппировкой.Добавить("ТМЦПринятыеНаОтветственноеХранение");
	СписокГруппСГоризонтальнойГруппировкой.Добавить("МатериалыПринятыеВПереработку");
	СписокГруппСГоризонтальнойГруппировкой.Добавить("ТоварыПринятыеНаКомиссию");
	
	СписокГруппСГоризонтальнойГруппировкой.Добавить("Основное");
	СписокГруппСГоризонтальнойГруппировкой.Добавить("ЦелевоеФинансирование");
	СписокГруппСГоризонтальнойГруппировкой.Добавить("ЗабалансовыйУчет");
	СписокГруппСГоризонтальнойГруппировкой.Добавить("ФинансоваяАренда");
	
	Если ЭтоСписок Тогда
		ГруппировкаГруппы = ГруппировкаКолонок.Горизонтальная;
	ИначеЕсли СписокГруппСГоризонтальнойГруппировкой.Найти(ИмяГруппы) <> Неопределено Тогда
		ГруппировкаГруппы = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяЕслиВозможно;
	Иначе
		ГруппировкаГруппы = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	КонецЕсли;
	
	Возврат ГруппировкаГруппы;
	
КонецФункции

#КонецОбласти

//-- Локализация

#Область НастройкаСчетовУчетаПрочихОпераций

Процедура ОчиститьНастройкиОтражения(Данные, ПараметрыНастройки, ТипСтатьи)
	
	Если Не ПараметрыНастройки.ДоступностьПоОперации Или (ТипСтатьи <> Неопределено И ТипСтатьи <> 3) Тогда
		Данные[ПараметрыНастройки.НастройкаСчетовУчета] = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
