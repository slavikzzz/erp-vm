
#Область ПрограммныйИнтерфейс

// Процедура для выбора условия цен поставщика из формы выбора.
//
// Параметры:
//	ОписаниеОповещения - ОписаниеОповещения - 
//	Партнер - СправочникСсылка.Партнеры - партнер, для которого выберите условия цен поставщиков.
//	ЦенаВключаетНДС - Булево, Неопределено - Отбор по виду цены, Неопределено - отбор отсутствует.
//
Процедура ВыбратьВидЦеныПоставщика(ОписаниеОповещения, Партнер, ЦенаВключаетНДС = Неопределено) Экспорт

	Отбор = Новый Структура("Владелец", Партнер);
	Если ЦенаВключаетНДС <> Неопределено Тогда
		Отбор.Вставить("ЦенаВключаетНДС", ЦенаВключаетНДС);
	КонецЕсли;

	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОписаниеОповещения", ОписаниеОповещения);
	
	ОткрытьФорму(
		"Справочник.ВидыЦенПоставщиков.ФормаВыбора",
		Новый Структура("Отбор", Отбор),,,,,
		Новый ОписаниеОповещения("ВыбратьВидЦеныПоставщикаЗавершение", ЗакупкиКлиент, ДополнительныеПараметры));
	
КонецПроцедуры

// Показывает оповещение пользователя об окончании заполнения цен по условию цен поставщика.
//
// Параметры:
//  ЦеныРассчитаны    - Булево - Признак успешного расчета цен хотя бы в одной строке.
//  ВидЦеныПоставщика - СправочникСсылка.ВидыЦенПоставщиков - Вид цены поставщика, по которому осуществлялось заполнение
//                                                            цен.
//
Процедура ОповеститьОбОкончанииЗаполненияЦенПоВидуЦеныПоставщика(ЦеныРассчитаны, ВидЦеныПоставщика) Экспорт

	Если ЦеныРассчитаны Тогда
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Цены заполнены';
				|en = 'Prices are filled out'"),
			,
			СтрЗаменить(НСтр("ru = 'Цены по виду цены поставщика ""%ВидЦеныПоставщика%"" заполнены';
							|en = 'Prices according to the ""%ВидЦеныПоставщика%"" vendor price type are filled in'"), "%ВидЦеныПоставщика%", ВидЦеныПоставщика),
			БиблиотекаКартинок.Информация32);
	Иначе
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Цены не заполнены';
				|en = 'Prices are not filled in'"),
			,
			СтрЗаменить(НСтр("ru = 'Ни в одной строке цены по виду цены поставщика ""%ВидЦеныПоставщика%"" не заполнены';
							|en = 'No lines have prices of the ""%ВидЦеныПоставщика%"" vendor price type filled in'"), "%ВидЦеныПоставщика%", ВидЦеныПоставщика),
			БиблиотекаКартинок.Информация32);
	КонецЕсли;
	
КонецПроцедуры

// Проверяет заполненность реквизитов, необходимых для заполнения цен в документе по соглашению с поставщиком.
//
// Параметры:
//  Документ                    - ДокументОбъект - документ, для которого выполняются проверки
//  ИмяТабличнойЧасти           - Строка - имя табличной части, в которой необходимо осуществить проверку
//  ПредставлениеТабличнойЧасти - Строка - представление табличной части для информирования пользователя.
//
// Возвращаемое значение:
//  Булево - Ложь, если необходимые данные не заполнены.
//
Функция НеобходимоЗаполнениеЦенПоСоглашению(Документ, ИмяТабличнойЧасти, ПредставлениеТабличнойЧасти) Экспорт

	Если Не ЗначениеЗаполнено(Документ.Соглашение) Тогда
		
		ПоказатьПредупреждение(,НСтр("ru = 'В документе не указано соглашение с поставщиком, или соглашения с поставщиком отсутствуют. Цены не могут быть заполнены';
									|en = 'Terms of purchase are not specified in the document, or there are no terms of purchase. Prices cannot be populated'"));
		Возврат Ложь;
		
	ИначеЕсли Не ЗначениеЗаполнено(Документ.Валюта) Тогда
		
		ПоказатьПредупреждение(,НСтр("ru = 'В документе не указана валюта. Цены не могут быть заполнены';
									|en = 'Currency is not specified in the document. Prices cannot be populated'"));
		Возврат Ложь;
		
	ИначеЕсли Документ[ИмяТабличнойЧасти].Количество() = 0 Тогда
		
		ТекстПредупреждения = НСтр("ru = 'В документе не заполнена таблица %ПредставлениеТабличнойЧасти%. Цены не могут быть заполнены';
									|en = 'Table %ПредставлениеТабличнойЧасти% in the document is not filled in. Cannot fill in the prices'");
		ТекстПредупреждения = СтрЗаменить(ТекстПредупреждения, "%ПредставлениеТабличнойЧасти%", ПредставлениеТабличнойЧасти);
		ПоказатьПредупреждение(,ТекстПредупреждения);
		Возврат Ложь;
		
	Иначе
		
		Возврат Истина;
		
	КонецЕсли;

КонецФункции

// Проверяет заполненность реквизитов, необходимых для заполнения цен в документе по условию цен поставщика.
//
// Параметры:
//  Документ                    - ДокументОбъект - документ, для которого выполняются проверки
//  ИмяТабличнойЧасти           - Строка - имя табличной части, в которой необходимо осуществить проверку
//  ПредставлениеТабличнойЧасти - Строка - представление табличной части для информирования пользователя.
//
// Возвращаемое значение:
//  Булево - Ложь, если необходимые данные не заполнены.
//
Функция НеобходимоЗаполнениеЦенПоВидуЦенПоставщика(Документ, ИмяТабличнойЧасти, ПредставлениеТабличнойЧасти) Экспорт

	Если Не ЗначениеЗаполнено(Документ.Валюта) Тогда
		
		ПоказатьПредупреждение(,НСтр("ru = 'В документе не указана валюта. Цены не могут быть заполнены';
									|en = 'Currency is not specified in the document. Prices cannot be populated'"));
		Возврат Ложь;
		
	ИначеЕсли Документ[ИмяТабличнойЧасти].Количество() = 0 Тогда
		
		ТекстПредупреждения = НСтр("ru = 'В документе не заполнена таблица %ПредставлениеТабличнойЧасти%. Цены не могут быть заполнены';
									|en = 'Table %ПредставлениеТабличнойЧасти% in the document is not filled in. Cannot fill in the prices'");
		ТекстПредупреждения = СтрЗаменить(ТекстПредупреждения, "%ПредставлениеТабличнойЧасти%", ПредставлениеТабличнойЧасти);
		ПоказатьПредупреждение(,ТекстПредупреждения);
		Возврат Ложь;
		
	Иначе
		
		Возврат Истина;
		
	КонецЕсли;

КонецФункции

#КонецОбласти

