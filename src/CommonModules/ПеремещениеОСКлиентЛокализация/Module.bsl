////////////////////////////////////////////////////////////////////////////////
// Процедуры, используемые для локализации документа "Перемещение ОС".
// 
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

#Область ФормаДокумента

// Параметры:
// 	Элемент - ПолеФормы - 
// 	Форма - ФормаКлиентскогоПриложения -
Процедура ПриИзмененииРеквизита(Элемент, Форма) Экспорт

	ДополнительныеПараметры = Неопределено;
	ТребуетсяВызовСервера = Ложь;
	ПродолжитьИзменениеРеквизита = Истина;
	
	//++ Локализация
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;

	Если Элемент.Имя = Элементы.ДокументНаОсновании.Имя Тогда
		
		ПродолжитьИзменениеРеквизита = ДокументНаОснованииПриИзменении(Объект, Форма);
		
	ИначеЕсли Элемент.Имя = Элементы.ОрганизацияСдатчик.Имя Тогда
		
		ОрганизацияСдатчикПриИзменении(Объект);
		
	ИначеЕсли Элемент.Имя = Элементы.ХозяйственнаяОперация.Имя Тогда
		
		ХозяйственнаяОперацияПриИзменении(Объект, Форма, ДополнительныеПараметры);
		
	ИначеЕсли Элемент.Имя = Элементы.СтатьяРасходовНалог.Имя Тогда
		
		ДоходыИРасходыКлиентСервер.СтатьяПриИзменении(Форма, Элемент);
		
	ИначеЕсли Элемент.Имя = Элементы.ИзменяетсяОтражениеРасходовПоНалогу.Имя Тогда
		
		ИзменяетсяОтражениеРасходовПоНалогуПриИзменении(ДополнительныеПараметры);
		
	КонецЕсли; 
	
	//-- Локализация
	
	Если ПродолжитьИзменениеРеквизита Тогда
		
		ОбщегоНазначенияУТКлиент.ПродолжитьИзменениеРеквизита(
			Форма, 
			Элемент.Имя, 
			ТребуетсяВызовСервера,
			ДополнительныеПараметры);
			
	КонецЕсли; 
	
КонецПроцедуры

Процедура НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка, Форма) Экспорт

	//++ Локализация
	Элементы = Форма.Элементы;
	Если Элемент = Элементы.АналитикаРасходовНалог Тогда
		ДоходыИРасходыКлиент.НачалоВыбораАналитикиРасходов(Форма, Элемент, СтандартнаяОбработка);
	КонецЕсли; 
	//-- Локализация
	
	Возврат; // В WE пустой обработчик.
	
КонецПроцедуры

Процедура АвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка, Форма) Экспорт

	//++ Локализация
	Элементы = Форма.Элементы;
	Если Элемент = Элементы.АналитикаРасходовНалог Тогда
		ДоходыИРасходыКлиент.АвтоПодборАналитикиРасходов(Форма, Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	КонецЕсли; 
	//-- Локализация
	
	Возврат; // В WE пустой обработчик.
	
КонецПроцедуры

Процедура ОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка, Форма) Экспорт

	//++ Локализация
	Элементы = Форма.Элементы;
	Если Элемент = Элементы.АналитикаРасходовНалог Тогда
		ДоходыИРасходыКлиент.ОкончаниеВводаТекстаАналитикиРасходов(Форма, Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	КонецЕсли; 
	//-- Локализация
	
	Возврат; // В WE пустой обработчик.
	
КонецПроцедуры

Процедура ПослеЗаписиВФорме(Форма, Объект) Экспорт

	//++ Локализация
	Если ЗначениеЗаполнено(Объект.ДокументОснование)
		И ТипЗнч(Объект.ДокументОснование) = Тип("ДокументСсылка.ИнвентаризацияОС") Тогда
		Оповестить("ЗаписьДокументаНаОснованииИнвентаризации",, Объект.Ссылка);
	КонецЕсли;
	//-- Локализация
	
	Возврат; // В WE пустой обработчик.
	
КонецПроцедуры
 
#КонецОбласти

#КонецОбласти

//++ Локализация

#Область СлужебныеПроцедурыИФункции

#Область ФормаДокумента

Функция ДокументНаОснованииПриИзменении(Объект, Форма)

	Если Объект.ДокументНаОсновании Тогда
		
		ОтборСписка = Новый Структура;
		ОтборСписка.Вставить("Проведен", Истина);
		Если ЗначениеЗаполнено(Объект.Организация) Тогда
			ОтборСписка.Вставить("Организация", Объект.Организация);
		КонецЕсли;
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Форма", Форма);
		ДополнительныеПараметры.Вставить("ИмяРеквизита", Форма.Элементы.ДокументНаОсновании.Имя);
		ОписаниеОповещения = Новый ОписаниеОповещения("ДокументНаОснованииПриИзмененииЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Отбор", ОтборСписка);
		ОткрытьФорму("Документ.ИнвентаризацияОС.ФормаВыбора", ПараметрыФормы,,,,, ОписаниеОповещения);
		
		Возврат Ложь;
		
	Иначе
		Объект.ДокументОснование = Неопределено;
	КонецЕсли; 
	
	Возврат Истина;
	
КонецФункции
 
Процедура ДокументНаОснованииПриИзмененииЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт

	Объект = ДополнительныеПараметры.Форма.Объект;
	
	Если ЗначениеЗаполнено(РезультатЗакрытия) Тогда
		Объект.ДокументОснование = РезультатЗакрытия;
	Иначе
		Объект.ДокументНаОсновании = Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		ТекстВопроса = НСтр("ru = 'Заполнить документ по инвентаризации?';
							|en = 'Fill in the stocktaking document?'");
		ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьПоДаннымОснованияЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет); 
	КонецЕсли; 
	
КонецПроцедуры

Процедура ЗаполнитьПоДаннымОснованияЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ОбщегоНазначенияУТКлиент.ПродолжитьИзменениеРеквизита(
			ДополнительныеПараметры.Форма, ДополнительныеПараметры.ИмяРеквизита, Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОрганизацияСдатчикПриИзменении(Объект)
	
	Если Объект.ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс")
		И ЗначениеЗаполнено(Объект.Организация)
		И ЗначениеЗаполнено(Объект.ОрганизацияПолучатель) Тогда
		Если НЕ ВнеоборотныеАктивыВызовСервера.ОрганизацииВзаимосвязаныПоОрганизационнойСтруктуре(Объект.ОрганизацияПолучатель, Объект.Организация) Тогда
			Объект.ОрганизацияПолучатель = ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ХозяйственнаяОперацияПриИзменении(Объект, Форма, ДополнительныеПараметры)
	
	Если Объект.ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс") Тогда
		Если Объект.Организация = Объект.ОрганизацияПолучатель Тогда
			Объект.ОрганизацияПолучатель = Неопределено;
		КонецЕсли;
		Если ЗначениеЗаполнено(Объект.Организация)
			И НЕ ВнеоборотныеАктивыВызовСервера.ОрганизацияВзаимосвязанаСДругимиОрганизациями(Объект.Организация) Тогда
			Объект.Организация = Неопределено;
		КонецЕсли; 
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Выполнить_НастроитьЗависимыеЭлементыФормы", "ХозяйственнаяОперация");
	
КонецПроцедуры

Процедура ИзменяетсяОтражениеРасходовПоНалогуПриИзменении(ДополнительныеПараметры)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Выполнить_НастроитьЗависимыеЭлементыФормы", "ИзменяетсяОтражениеРасходовПоНалогу");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

//-- Локализация