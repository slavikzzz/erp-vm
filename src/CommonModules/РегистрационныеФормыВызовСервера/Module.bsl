#Область ПрограммныйИнтерфейс

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция НачатьПолучениеКодаПодтверждения(ПараметрыВыполнения, Знач УникальныйИдентификатор) Экспорт
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	НастройкиЗапуска.НаименованиеФоновогоЗадания = ПараметрыВыполнения.НаименованиеЗадания;
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"РегистрационныеФормы.ОтправитьКодПодтвержденияВФоне",
		ПараметрыВыполнения,
		НастройкиЗапуска);
КонецФункции

Функция ТекстПовторнойОтправкиКодаПодтверждения(СрокДействияКодаПодтверждения) Экспорт
	
	Если Не ЗначениеЗаполнено(СрокДействияКодаПодтверждения) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекущаяДата = УниверсальноеВремя(ТекущаяДатаСеанса(), ЧасовойПоясСеанса());
	РазностьДат = СрокДействияКодаПодтверждения - ТекущаяДата;
	
	Если РазностьДат > 0 Тогда
		Возврат СтрШаблон(НСтр("ru = 'Запросить повторно код можно через %1 сек.';
								|en = 'Запросить повторно код можно через %1 сек.'"), РазностьДат);
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

Функция ТекстПодсказкиНомерТелефона(ИмяФормы) Экспорт
	
	Если СтрНайти(ИмяФормы, "РегистрацияООО") > 0 Тогда
		ТекстПодсказки = СтроковыеФункции.ФорматированнаяСтрока(
			НСтр("ru = 'Телефон указывается в <a href=""ОбразецЗаявленияОРегистрацииОрганизацииЛистИ"">Заявлении о регистрации</a>
			|Например, +7(123)1234567 или 8(123)1234567';
			|en = 'Телефон указывается в <a href=""ОбразецЗаявленияОРегистрацииОрганизацииЛистИ"">Заявлении о регистрации</a>
			|Например, +7(123)1234567 или 8(123)1234567'"));
	ИначеЕсли СтрНайти(ИмяФормы, "РегистрацияИП") > 0 Тогда
		ТекстПодсказки = СтроковыеФункции.ФорматированнаяСтрока(
			НСтр("ru = 'Телефон указывается в  <a href=""ОбразецЗаявленияОРегистрацииИПЛистБ"">Заявлении о регистрации</a>
			|Например, +7(123)1234567 или 8(123)1234567';
			|en = 'Телефон указывается в  <a href=""ОбразецЗаявленияОРегистрацииИПЛистБ"">Заявлении о регистрации</a>
			|Например, +7(123)1234567 или 8(123)1234567'"));
	Иначе
		ТекстПодсказки = НСтр("ru = 'Например, +7(123)1234567 или 8(123)1234567';
								|en = 'Например, +7(123)1234567 или 8(123)1234567'");
	КонецЕсли;
	
	Возврат ТекстПодсказки;
	
КонецФункции

Функция ПроверитьКодПодтверждения(Идентификатор, КодПроверки) Экспорт
	Если Не ОбщегоНазначения.ПодсистемаСуществует(
		"РегламентированнаяОтчетность.ЭлектронныйДокументооборотСКонтролирующимиОрганами.ГосударственнаяРегистрацияВФНС") Тогда
		
		Возврат Ложь;
	КонецЕсли;

	Результат = Ложь;
	Если СтрДлина(КодПроверки) < РегистрационныеФормыКлиентСервер.ДлинаКодаПодтверждения() Тогда
		Возврат Результат;
	КонецЕсли;
	
	МодульРегистрацияВФНС = ОбщегоНазначения.ОбщийМодуль("ДокументооборотРегистрацияВФНС");
	ДанныеПроверки = МодульРегистрацияВФНС.СервисРегистрацииФНСПроверитьКодПодтверждения(Идентификатор, КодПроверки);
	Если ДанныеПроверки.Выполнено Тогда
		Результат = ДанныеПроверки.Успешно;
	Иначе
		ОбщегоНазначения.СообщитьПользователю(СтрШаблон(НСтр("ru = 'Не удалось проверить код подтверждения. %1 Попробуйте ещё раз.';
															|en = 'Не удалось проверить код подтверждения. %1 Попробуйте ещё раз.'"), ДанныеПроверки.Ошибка));
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

Функция ТаблицаОтсканированныхСтраницДокумента(ВладелецФайла, ВидДокумента) Экспорт
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	УведомлениеОСпецрежимахНалогообложенияПрисоединенныеФайлы.Ссылка КАК Ссылка,
	|	УведомлениеОСпецрежимахНалогообложенияПрисоединенныеФайлы.Описание КАК Описание,
	|	УведомлениеОСпецрежимахНалогообложенияПрисоединенныеФайлы.Наименование КАК ИмяФайла
	|ИЗ
	|	Справочник.УведомлениеОСпецрежимахНалогообложенияПрисоединенныеФайлы КАК УведомлениеОСпецрежимахНалогообложенияПрисоединенныеФайлы
	|ГДЕ
	|	УведомлениеОСпецрежимахНалогообложенияПрисоединенныеФайлы.ВладелецФайла = &ВладелецФайла
	|	И НЕ УведомлениеОСпецрежимахНалогообложенияПрисоединенныеФайлы.ПометкаУдаления
	|	И УведомлениеОСпецрежимахНалогообложенияПрисоединенныеФайлы.Описание ПОДОБНО &ВидДокумента
	|	И УведомлениеОСпецрежимахНалогообложенияПрисоединенныеФайлы.Служебный");
	
	Запрос.УстановитьПараметр("ВладелецФайла", ВладелецФайла);
	Запрос.УстановитьПараметр("ВидДокумента", СтрШаблон("%1%%", ВидДокумента));
	
	Результат = НовыйТаблицаОтсканированныхСтраницДокумента();
	ОТЧ = Новый ОписаниеТипов("Число");
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Результат.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		Разложение = СтрРазделить(Выборка.Описание, ",");
		НоваяСтрока.НомерСтроки = ?(Разложение.Количество() >= 2, ОТЧ.ПривестиЗначение(Разложение[1]), 0);
		НоваяСтрока.НомерСтрокиГруппы = ?(Разложение.Количество() >= 3, ОТЧ.ПривестиЗначение(Разложение[2]), 0);
		ИндексРазделителя = СтрНайти(Выборка.Описание, "*");
		НоваяСтрока.ИмяДокумента = ?(ИндексРазделителя = 0, "<Набор приложенных документов>", Сред(Выборка.Описание, ИндексРазделителя + 1));
	КонецЦикла;
	
	Результат.Сортировать("НомерСтрокиГруппы ВОЗР,НомерСтроки ВОЗР");
	НомерСтрокиГруппы = Неопределено;
	Инд = 0;
	Для Каждого Стр Из Результат Цикл 
		Если НомерСтрокиГруппы <> Стр.НомерСтрокиГруппы Тогда 
			НомерСтрокиГруппы = Стр.НомерСтрокиГруппы;
			Инд = 0;
		КонецЕсли;
		Инд = Инд + 1;
		Стр.НомерСтроки = Инд;
	КонецЦикла;
	Возврат Результат;
КонецФункции

Функция ДанныеФайлаДляОткрытия(ПрисоединенныйФайл, ИдентификаторФормы) Экспорт
	ПараметрыДанныхФайла = РаботаСФайламиКлиентСервер.ПараметрыДанныхФайла();
	ПараметрыДанныхФайла.ИдентификаторФормы = ИдентификаторФормы;
	Возврат РаботаСФайлами.ДанныеФайла(ПрисоединенныйФайл, ПараметрыДанныхФайла);
КонецФункции

Процедура УдалитьПрикрепленныеФайлы(ВладелецФайла, ВидДокумента) Экспорт
	ПрикрепленныеФайлы = ТаблицаОтсканированныхСтраницДокумента(ВладелецФайла, ВидДокумента);
	Если ЗначениеЗаполнено(ПрикрепленныеФайлы) Тогда
		ИнтерфейсыВзаимодействияБРО.СохранитьСтатусОтправкиУведомления(ВладелецФайла,
			ИнтерфейсыВзаимодействияБРОКлиентСервер.СтатусВРаботеСтрокой());
	КонецЕсли;
	ПометитьНаУдалениеПрикрепленныеФайлы(ПрикрепленныеФайлы);
КонецПроцедуры

Функция ДоступенПросмотрЖурналаРегистрации() Экспорт
	Возврат ПравоДоступа("Просмотр", Метаданные.Обработки.ЖурналРегистрации);
КонецФункции

Функция ПрикрепленныеФайлыЗаявления(ПараметрыЗаявления, УникальныйИдентификатор) Экспорт
	Результат = Новый Структура;
	
	ВидыДокументов = ПараметрыЗаявления.ВидыДокументов;
	Для Каждого ТекущийВид Из ВидыДокументов Цикл
		ФайлыДокумента = ТаблицаОтсканированныхСтраницДокумента(ПараметрыЗаявления.Заявление, ТекущийВид.Ключ);
		
		Если ТекущийВид.Значение = ВидыДокументов.ИныеДокументы Тогда
			ЗначениеИнойДокумент = Новый Соответствие;
			СписокДокументов = Новый Массив;
			НомерСтрокиГруппы = Неопределено;
			Для Каждого ТекущийИнойДокумент Из ФайлыДокумента Цикл
				Если НомерСтрокиГруппы <> ТекущийИнойДокумент.НомерСтрокиГруппы И СписокДокументов.Количество() > 0 Тогда
					ЗначениеИнойДокумент.Вставить(Строка(НомерСтрокиГруппы),
						АдресаФайловВоВременномХранилище(СписокДокументов, УникальныйИдентификатор));
					СписокДокументов = Новый Массив;
				КонецЕсли;
				
				НомерСтрокиГруппы = ТекущийИнойДокумент.НомерСтрокиГруппы;
				СписокДокументов.Добавить(ТекущийИнойДокумент.Ссылка);
			КонецЦикла;
			
			Если СписокДокументов.Количество() > 0 Тогда
				ЗначениеИнойДокумент.Вставить(Строка(НомерСтрокиГруппы),
					АдресаФайловВоВременномХранилище(СписокДокументов, УникальныйИдентификатор));
			КонецЕсли;
			
			Результат.Вставить(ТекущийВид.Ключ, ЗначениеИнойДокумент);
		Иначе
			Результат.Вставить(ТекущийВид.Ключ, АдресаФайловВоВременномХранилище(
				ФайлыДокумента.ВыгрузитьКолонку("Ссылка"), УникальныйИдентификатор));
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

Функция НаименованиеПодписанногоКонтейнера(ПодписанныйКонтейнер) Экспорт
	РеквизитыКонтейнера = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПодписанныйКонтейнер, "Наименование, Расширение");
	Возврат СтрШаблон("%1.%2", РеквизитыКонтейнера.Наименование, РеквизитыКонтейнера.Расширение);
КонецФункции

Функция ДанныеСостоянияОтправкиДокументов(ЗаявлениеСсылка, ДокументыПодписаны, НетСертификата, ОшибкаПодписания, ЭтоЮрЛицо) Экспорт
	Результат = Новый Структура;
	Результат.Вставить("ТекстПодсказки", "");
	Результат.Вставить("Картинка");
	Результат.Вставить("Дополнение", "");
	
	СостояниеОтправкиДокументов = СостояниеОтправкиДокументовВФНС(ЗаявлениеСсылка);
	Если ДокументыПодписаны И СостояниеОтправкиДокументов = ПредопределенноеЗначение("Перечисление.СтатусыОтправки.НеПринят") Тогда
		Результат.ТекстПодсказки = СтроковыеФункции.ФорматированнаяСтрока(
			НСтр("ru = 'При проверке заявления были обнаружены ошибки. Исправьте их и отправьте заявление снова.
			|<a href=""Протокол"">Протокол</a>';
			|en = 'При проверке заявления были обнаружены ошибки. Исправьте их и отправьте заявление снова.
			|<a href=""Протокол"">Протокол</a>'"));
		Результат.Картинка = БиблиотекаКартинок.ОшибкаОтправки;
	ИначеЕсли ДокументыПодписаны И СостояниеОтправкиДокументов = ПредопределенноеЗначение("Перечисление.СтатусыОтправки.Сдан") Тогда
		Если ЭтоЮрЛицо Тогда
			Результат.ТекстПодсказки = СтроковыеФункции.ФорматированнаяСтрока(
				НСтр("ru = 'Заявление принято, изменения внесены в реестр.
				|<a href=""ЛистЗаписиЕГР"">Лист записи ЕГРЮЛ</a>';
				|en = 'Заявление принято, изменения внесены в реестр.
				|<a href=""ЛистЗаписиЕГР"">Лист записи ЕГРЮЛ</a>'"));
		Иначе
			Результат.ТекстПодсказки = СтроковыеФункции.ФорматированнаяСтрока(
				НСтр("ru = 'Заявление принято, изменения внесены в реестр.
				|<a href=""ЛистЗаписиЕГР"">Лист записи ЕГРИП</a>';
				|en = 'Заявление принято, изменения внесены в реестр.
				|<a href=""ЛистЗаписиЕГР"">Лист записи ЕГРИП</a>'"));
		КонецЕсли;
		
		Результат.Картинка = БиблиотекаКартинок.УспешнаяОтправка;
		Результат.Дополнение = НСтр("ru = 'Обновите реквизиты в программе при необходимости.';
									|en = 'Обновите реквизиты в программе при необходимости.'");
	ИначеЕсли ДокументыПодписаны Тогда
		Результат.ТекстПодсказки = НСтр("ru = 'Заявление успешно отправлено в ИФНС.
			|Срок рассмотрения заявления - до пяти рабочих дней.';
			|en = 'Заявление успешно отправлено в ИФНС.
			|Срок рассмотрения заявления - до пяти рабочих дней.'");
		Результат.Картинка = БиблиотекаКартинок.УспешнаяОтправка;
		Результат.Дополнение = НСтр("ru = 'Когда вы получите подтверждающие документы, обновите изменения в программе.';
									|en = 'Когда вы получите подтверждающие документы, обновите изменения в программе.'");
	ИначеЕсли НетСертификата Тогда
		Результат.ТекстПодсказки = СтроковыеФункции.ФорматированнаяСтрока(
			НСтр("ru = 'На компьютере не установлен сертификат квалифицированной электронной подписи.
			|Если у вас уже есть подпись, <a href=""https://its.1c.ru/db/metod81#content:7688:hdoc"">установите</a> её на локальный компьютер.';
			|en = 'На компьютере не установлен сертификат квалифицированной электронной подписи.
			|Если у вас уже есть подпись, <a href=""https://its.1c.ru/db/metod81#content:7688:hdoc"">установите</a> её на локальный компьютер.'"));
		Результат.Картинка = БиблиотекаКартинок.ОшибкаОтправки;
	ИначеЕсли ОшибкаПодписания Тогда
		Результат.ТекстПодсказки = СтроковыеФункции.ФорматированнаяСтрока(
			НСтр("ru = 'При выполнении подписания документов возникли ошибки.
			|<a href=""ЖурналРегистрации"">Подробнее</a>';
			|en = 'При выполнении подписания документов возникли ошибки.
			|<a href=""ЖурналРегистрации"">Подробнее</a>'"));
		Результат.Картинка = БиблиотекаКартинок.ОшибкаОтправки;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

Функция ДанныеФайлаОтФНСДляОткрытия(Заявление, ТипФайла, УникальныйИдентификатор) Экспорт
	Если Не ОбщегоНазначения.ПодсистемаСуществует(
		"РегламентированнаяОтчетность.ЭлектронныйДокументооборотСКонтролирующимиОрганами.ГосударственнаяРегистрацияВФНС") Тогда
		
		Возврат Неопределено;
	КонецЕсли;
	
	МодульРегистрацияВФНСВызовСервера = ОбщегоНазначения.ОбщийМодуль("ДокументооборотРегистрацияВФНСВызовСервера");
	МодульРегистрацияВФНС = ОбщегоНазначения.ОбщийМодуль("ДокументооборотРегистрацияВФНС");
	СвойстваОтправки = МодульРегистрацияВФНСВызовСервера.СвойстваОтправкиРегистрацииЮЛ(Заявление);
	ЭтапыОтправки = МодульРегистрацияВФНС.СформироватьЭтапыОтправки(СвойстваОтправки.ОтправкаСсылка);
	
	Результат = НовыеДанныеФайлаОтФНСДляОткрытия();
	Результат.ОтправкаСсылка = СвойстваОтправки.ОтправкаСсылка;
	
	ВидыФайловОбменаСКонтролирующимОрганом = МодульРегистрацияВФНС.ВидыФайловОбменаСКонтролирующимОрганом();
	Если ВидыФайловОбменаСКонтролирующимОрганом.Свойство(ТипФайла) Тогда
		НазначениеФайла = ВидыФайловОбменаСКонтролирующимОрганом[ТипФайла];
	Иначе
		Возврат Результат;
	КонецЕсли;
	
	Результат.ДанныеДляОткрытия = ДанныеФайловЭтапа(
		ЭтапыОтправки.ПринятВОбработку.Файлы, НазначениеФайла, УникальныйИдентификатор);
	
	Если Не ЗначениеЗаполнено(Результат.ДанныеДляОткрытия) Тогда
		Результат.ДанныеДляОткрытия = ДанныеФайловЭтапа(
			ЭтапыОтправки.Рассмотрение.Файлы, НазначениеФайла, УникальныйИдентификатор);
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДанныеВыгрузкиДляФормированияКонтейнера(ПараметрыОтчета, АдресХранилища) Экспорт
	РезультатВыгрузки = Новый Структура;
	ДокОбъект = ПараметрыОтчета.Ссылка.ПолучитьОбъект();
	СписокОшибок = ДокОбъект.ПроверитьДокументСВыводомВТаблицу(Неопределено);
	РезультатВыгрузки.Вставить("КоличествоОшибок", ?(ТипЗнч(СписокОшибок) = Тип("СписокЗначений"), СписокОшибок.Количество(), 0));
	
	Если ТипЗнч(СписокОшибок) = Тип("СписокЗначений") И РезультатВыгрузки.КоличествоОшибок = 0 Тогда 
		Выгрузка = ДокОбъект.ВыгрузитьДокумент();
		РезультатВыгрузки.Вставить("ФайлВыгрузкиXML", Выгрузка[0].ТестВыгрузки);
		РезультатВыгрузки.Вставить("ФайлВыгрузкиДД", ПолучитьИзВременногоХранилища(Выгрузка[0].АдресФайлаВыгрузки));
		ПДФФайлИзСервиса = ДокументооборотРегистрацияВФНС.СервисРегистрацииФНСФормаЗявления(Выгрузка[0].ТестВыгрузки);
		Если ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПДФФайлИзСервиса, "Выполнено", Ложь) Тогда 
			РезультатВыгрузки.Вставить("ПДФФайлИзСервисаДД", ПДФФайлИзСервиса.Файл);
		ИначеЕсли ПДФФайлИзСервиса.Свойство("Ошибка") Тогда 
			РезультатВыгрузки.Вставить("ОшибкаСервиса", ПДФФайлИзСервиса.Ошибка);
		КонецЕсли;
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(РезультатВыгрузки, АдресХранилища);
КонецПроцедуры

Функция ДанныеФайловЭтапа(ФайлыЭтапа, НазначениеФайла, УникальныйИдентификатор)
	Результат = Неопределено;
	
	Для Каждого ТекущийФайл Из ФайлыЭтапа Цикл
		Если НазначениеФайла = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущийФайл, "НазначениеФайла") Тогда
			Результат = ДанныеФайлаДляОткрытия(ТекущийФайл, УникальныйИдентификатор);
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;
КонецФункции

Функция НовыеДанныеФайлаОтФНСДляОткрытия()
	Результат = Новый Структура;
	Результат.Вставить("ОтправкаСсылка", Справочники.ОтправкиРегистрацияЮЛ.ПустаяСсылка());
	Результат.Вставить("ДанныеДляОткрытия", Неопределено);
	Возврат Результат;
КонецФункции

Процедура ПометитьНаУдалениеПрикрепленныеФайлы(ПрикрепленныеФайлы)
	Для Каждого ТекущийФайл Из ПрикрепленныеФайлы Цикл
		СпрОбъект = ТекущийФайл.Ссылка.ПолучитьОбъект();
		СпрОбъект.УстановитьПометкуУдаления(Истина);
	КонецЦикла;
КонецПроцедуры

Функция НовыйТаблицаОтсканированныхСтраницДокумента()
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("Ссылка",
		Новый ОписаниеТипов("СправочникСсылка.УведомлениеОСпецрежимахНалогообложенияПрисоединенныеФайлы"));
	Результат.Колонки.Добавить("НомерСтроки", ОбщегоНазначения.ОписаниеТипаЧисло(5, 0));
	Результат.Колонки.Добавить("НомерДокумента", ОбщегоНазначения.ОписаниеТипаСтрока(2));
	Результат.Колонки.Добавить("ИмяФайла", ОбщегоНазначения.ОписаниеТипаСтрока(
		Метаданные.Справочники.УведомлениеОСпецрежимахНалогообложенияПрисоединенныеФайлы.ДлинаНаименования));
	Результат.Колонки.Добавить("НомерСтрокиГруппы", ОбщегоНазначения.ОписаниеТипаЧисло(5, 0));
	Результат.Колонки.Добавить("ИмяДокумента", ОбщегоНазначения.ОписаниеТипаСтрока(
		Метаданные.Справочники.УведомлениеОСпецрежимахНалогообложенияПрисоединенныеФайлы.ДлинаНаименования));
	Возврат Результат;
КонецФункции

Функция АдресаФайловВоВременномХранилище(ПрикрепленныеФайлы, УникальныйИдентификатор)
	Результат = Новый Массив;
	
	Для Каждого ТекущийФайл Из ПрикрепленныеФайлы Цикл
		ДанныеФайла = ДанныеФайлаДляОткрытия(ТекущийФайл, УникальныйИдентификатор);
		Результат.Добавить(ДанныеФайла.СсылкаНаДвоичныеДанныеФайла);
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

Функция СостояниеОтправкиДокументовВФНС(ЗаявлениеСсылка)
	Если Не ОбщегоНазначения.ПодсистемаСуществует(
		"РегламентированнаяОтчетность.ЭлектронныйДокументооборотСКонтролирующимиОрганами.ГосударственнаяРегистрацияВФНС") Тогда
		
		Возврат Неопределено;
	КонецЕсли;
	
	МодульРегистрацияВФНСВызовСервера = ОбщегоНазначения.ОбщийМодуль("ДокументооборотРегистрацияВФНСВызовСервера");
	СвойстваОтправки = МодульРегистрацияВФНСВызовСервера.СвойстваЗаявленияРегистрацииЮЛ(ЗаявлениеСсылка);
	Возврат СвойстваОтправки.СтатусОтправки;
КонецФункции

#КонецОбласти