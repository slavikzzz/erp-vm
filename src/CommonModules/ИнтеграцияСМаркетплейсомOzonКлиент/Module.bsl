
#Область ПрограммныйИнтерфейс

// Вызывает диалог вопроса по очистке настроек авторизации.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - форма, из которой инициирована очистка настроек авторизации.
//
Процедура ОчисткаНастроекАвторизацииВопрос(Форма) Экспорт

	Оповещение = Новый ОписаниеОповещения("ОчиститьНастройкиЗавершение", Форма);
	ТекстВопроса = НСтр("ru = 'Настройки авторизации будут удалены, что приведет к невозможности управления продажами товаров текущей учетной записи.
			|Очистить настройки?';
			|en = 'Authorization settings will be deleted. You will not be able to manage sales of goods from the current account.
			|Do you want to clear the settings?'");
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Нет);

КонецПроцедуры

// Показывает состояние выполнения действия.
//
// Параметры:
//   Результат                - Структура - любая структура, ожидаются ключи "КодОшибки", "ОписаниеОшибки".
//   ДополнительныеПараметры  - см. ИнтеграцияСМаркетплейсомOzonКлиент.НовоеОписаниеПараметровДействия.
//   ВывестиСообщениеОбОшибке - Булево - признак вывода сообщения.
//
Процедура ВывестиСостояние(Результат, ДополнительныеПараметры, ВывестиСообщениеОбОшибке = Ложь) Экспорт

	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если Не ПустаяСтрока(Результат.КодОшибки) Тогда
		Если ВывестиСообщениеОбОшибке И Результат.Свойство("ОписаниеОшибки") Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ОписаниеОшибки);
		КонецЕсли;
		Если ВывестиСообщениеОбОшибке 
				И Результат.Свойство("Детализация")
				И Результат.Детализация <> Неопределено Тогда
			Префикс = ?(Результат.Свойство("ОписаниеОшибки"), Символы.Таб, "");
			Для Индекс = 0 По Мин(4, Результат.Детализация.ВГраница()) Цикл
				ЭлементКоллекции = Результат.Детализация[Индекс];
				ОбщегоНазначенияКлиент.СообщитьПользователю(Префикс + ЭлементКоллекции);
			КонецЦикла;
			Если Результат.Детализация.ВГраница() > 4 Тогда
				ОбщегоНазначенияКлиент.СообщитьПользователю(Префикс + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'и еще %1 аналогичных сообщений';
						|en = 'and %1 similar messages'"),
					Результат.Детализация.Количество() - 5));
			КонецЕсли;
		КонецЕсли;

		Если ДополнительныеПараметры <> Неопределено Тогда
			Состояние(ДополнительныеПараметры.ОписаниеДействия,, ДополнительныеПараметры.РезультатСОшибками, БиблиотекаКартинок.ЛоготипOzon2);
		КонецЕсли;
		
	ИначеЕсли ДополнительныеПараметры <> Неопределено Тогда
		Состояние(ДополнительныеПараметры.ОписаниеДействия,, ДополнительныеПараметры.УспешныйРезультат, БиблиотекаКартинок.ЛоготипOzon2);
	КонецЕсли;

КонецПроцедуры

// Возвращает новую структуру с описанием параметров действия.
//
// Возвращаемое значение:
//   Структура - структура с описанием параметров действия.
//
Функция НовоеОписаниеПараметровДействия() Экспорт
	
	ПараметрыДействия = Новый Структура;
	ПараметрыДействия.Вставить("Действие", "");
	ПараметрыДействия.Вставить("ОписаниеДействия", "");
	ПараметрыДействия.Вставить("УспешныйРезультат", "");
	ПараметрыДействия.Вставить("РезультатСОшибками", "");

	Возврат ПараметрыДействия;

КонецФункции

// Открывает форму печати этикеток поставки на склады Ozon.
//
// Параметры:
//   ОписаниеКоманды - Структура - сведения о выполняемой команде.
// 
// Возвращаемое значение:
//   Неопределено
//
Функция ПечатьЭтикетокПоставкиНаСклады(ОписаниеКоманды) Экспорт
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Объекты", ОписаниеКоманды.ОбъектыПечати);
	
	ОткрытьФорму(
		"Обработка.УправлениеПродажамиНаOzon.Форма.ПечатьЭтикетокПоставкиНаСклады",
		ПараметрыОткрытия);
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция КэшПодсистемы()

	Если ПараметрыПриложения = Неопределено Тогда
		ПараметрыПриложения = Новый Соответствие;
	КонецЕсли;

	ИмяПодсистемы = "Продажи.РаботаСOzon";
	Если ПараметрыПриложения[ИмяПодсистемы] = Неопределено Тогда
		ПараметрыПриложения.Вставить(ИмяПодсистемы, Новый Структура);
	КонецЕсли;

	Возврат ПараметрыПриложения[ИмяПодсистемы];

КонецФункции

Функция КэшКатегорий() Экспорт

	КэшПодсистемы = КэшПодсистемы();

	КэшКатегорий     = Неопределено;
	НаименованиеКэша = "КэшКатегорийOzon";

	КэшПодсистемы.Свойство(НаименованиеКэша, КэшКатегорий);

	Если КэшКатегорий = Неопределено Тогда
		КэшПодсистемы.Вставить(НаименованиеКэша, Новый Структура);
	КонецЕсли;

	Возврат КэшПодсистемы[НаименованиеКэша];

КонецФункции

// Возвращает данные из кэша категорий по запрашиваемому идентификатору.
//
// Параметры:
//   ИдентификаторДанных - Строка - идентификатор данных, начинается с "Категория_", "ХарактеристикаКатегории_",
//                           например: "Категория_100549328", "ХарактеристикаКатегории_8229".
//
// Возвращаемое значение:  
//   - Неопределено - при отсутствии данных в кэше для переданного идентификатора.
//   - Произвольный - данные, помещаемые в кэш для переданного идентификатора.
//
Функция ПолучитьДанныеИзКэшаКатегорий(ИдентификаторДанных) Экспорт

	КэшКатегорий = КэшКатегорий(); 

	ДанныеИзКэша = Неопределено;
	КэшКатегорий.Свойство(ИдентификаторДанных, ДанныеИзКэша); 

	Возврат ДанныеИзКэша;

КонецФункции

// Помещает данные в кэш категорий по переданному идентификатору.
//
// Параметры:
//   ДанныеКэша          - ТаблицаЗначений - данные, помещаемые в кэш для переданного идентификатора, см.
//                           ИнтеграцияСМаркетплейсомOzonСервер.НоваяТаблицаАтрибутовКатегорииМаркетплейса().
//   ИдентификаторДанных - Строка - идентификатор данных, начинается с "Категория_", "ХарактеристикаКатегории_",
//                           например: "Категория_100549328", "ХарактеристикаКатегории_8229".
//
Процедура СохранитьДанныеВКэшКатегорий(ДанныеКэша, ИдентификаторДанных) Экспорт

	КэшКатегорий = КэшКатегорий();
	КэшКатегорий.Вставить(ИдентификаторДанных, ДанныеКэша);

КонецПроцедуры

#КонецОбласти
