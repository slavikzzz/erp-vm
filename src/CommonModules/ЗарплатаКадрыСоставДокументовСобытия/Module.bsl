#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗаполнитьСоставСотрудниковДокументаПередЗаписью(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеДокумента = ЗарплатаКадрыСоставДокументов.ОписаниеОбъектаПоСсылке(Источник.Ссылка);
	Если ОписаниеДокумента = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СоставОбъекта = ЗарплатаКадрыСоставДокументов.СоставОбъекта(Источник);
	
	Если ОписаниеДокумента.ЗаполнятьФизическиеЛицаПоСотрудникам Тогда
		ЗарплатаКадрыСоставДокументов.ЗаполнитьФизическиеЛицаПоСотрудникамВДокументе(Источник, ОписаниеДокумента);
	КонецЕсли;

	Если ОписаниеДокумента.ЗаполнятьТабличнуюЧастьФизическиеЛицаДокумента Тогда
		ЗарплатаКадрыСоставДокументов.ЗаполнитьТабличнуюЧастьФизическиеЛицаДокумента(Источник, ОписаниеДокумента, СоставОбъекта.ФизическиеЛица);
	КонецЕсли;
	
	Если ОписаниеДокумента.ИспользоватьКраткийСостав Тогда
		ЗарплатаКадрыСоставДокументов.ЗаполнитьКраткийСоставДокумента(Источник, ОписаниеДокумента, СоставОбъекта.СотрудникиФизическиеЛица);
	КонецЕсли;
КонецПроцедуры

Процедура ЗаполнитьСоставСотрудниковДокументаПриЗаписи(Источник, Отказ) Экспорт
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеДокумента = ЗарплатаКадрыСоставДокументов.ОписаниеОбъектаПоСсылке(Источник.Ссылка);
	Если ОписаниеДокумента = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДокументСсылка = Источник.Ссылка;
	
	СоставОбъекта = ЗарплатаКадрыСоставДокументов.СоставОбъекта(Источник);

	Если ОписаниеДокумента.ИспользоватьОграничениеДоступа Тогда
		ЗарплатаКадрыСоставДокументов.ЗаполнитьРегистрФизическиеЛицаДокументов(ДокументСсылка, СоставОбъекта.ФизическиеЛица);
	КонецЕсли;
	
	Если ОписаниеДокумента.ЗаполнятьСоставДокументов Тогда
		ЗарплатаКадрыСоставДокументов.ЗаполнитьРегистрСоставДокументовЗарплатаКадры(ДокументСсылка, СоставОбъекта.СотрудникиФизическиеЛица, Источник.Дата);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
