#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив Из Строка - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ Локализация

	//++ НЕ УТ
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	МеханизмыДокумента.Добавить("УчетУСНПСН");
	//-- НЕ УТ

	//-- Локализация
	
КонецПроцедуры

// Процедура дополняет тексты запросов проведения документа.
//
// Параметры:
//  Запрос - Запрос - Общий запрос проведения документа.
//  ТекстыЗапроса - СписокЗначений Из Строка - Список текстов запроса проведения.
//  Регистры - Строка, Структура - Список регистров проведения документа через запятую или в ключах структуры.
//
Процедура ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	//++ Локализация

	//++ НЕ УТ
	РеглУчетПроведениеСервер.ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаКУДиР(Запрос, ТекстыЗапроса, Регистры);
	//-- НЕ УТ

	//-- Локализация
	
КонецПроцедуры

//++ НЕ УТ

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт
	
	ТекстыОтражения = Новый Массив;
	
	//++ Локализация
	
	#Область ВозвратАвансаРозничномуКлиентуНаличныеЭквайринг
	ТекстЗапроса = "
	|ВЫБРАТЬ //// Возврат аванса розничному клиенту (Эквайринг) (Дт 76.09 :: Кт 57.03, 50.02)
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	Строки.СуммаПостоплатыРегл + Строки.СуммаПредоплатыРегл КАК Сумма,
	|	Строки.СуммаПостоплаты + Строки.СуммаПредоплаты КАК СуммаУУ,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыПоПодарочнымСертификатам) КАК ВидСчетаДт,
	|	Строки.ОбъектРасчетов.Владелец КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	Строки.ВалютаПлатежа КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.Контрагент КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	Строки.СуммаПостоплатыВВалютеПлатежа + Строки.СуммаПредоплатыВВалютеПлатежа КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	ВЫБОР
	|		КОГДА Строки.ТипДенежныхСредств = ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.ДенежныеСредстваУЭквайера)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПродажиПоЭквайрингу)
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВидСчетаКт,
	|	ЕСТЬNULL(ДоговорыЭквайринга.Ссылка, НЕОПРЕДЕЛЕНО) КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ВЫБОР
	|		КОГДА Строки.ТипДенежныхСредств = ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.ДенежныеСредстваУЭквайера)
	|		ТОГДА Строки.ВалютаПлатежа
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)
	|	КОНЕЦ КАК ВалютаКт,
	|	ЕСТЬNULL(ДоговорыЭквайринга.Подразделение, Строки.Подразделение) КАК ПодразделениеКт,
	|	ЕСТЬNULL(ДоговорыЭквайринга.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельностиКт,
	|
	|	ВЫБОР
	|		КОГДА Строки.ТипДенежныхСредств = ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.ДенежныеСредстваУЭквайера)
	|		ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОперационнаяКасса)
	|	КОНЕЦ КАК СчетКт,
	|	ЕСТЬNULL(ДоговорыЭквайринга.Контрагент, Строки.СтатьяДвиженияДенежныхСредств) КАК СубконтоКт1,
	|	ЕСТЬNULL(ДоговорыЭквайринга.Ссылка, НЕОПРЕДЕЛЕНО) КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	ВЫБОР
	|		КОГДА Строки.ТипДенежныхСредств = ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.ДенежныеСредстваУЭквайера)
	|		ТОГДА Строки.СуммаПостоплатыВВалютеПлатежа + Строки.СуммаПредоплатыВВалютеПлатежа
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	ВЫБОР
	|		КОГДА Строки.ТипДенежныхСредств = ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.ДенежныеСредстваУЭквайера)
	|		ТОГДА ""Возврат аванса розничному клиенту (эквайринг)""
	|		ИНАЧЕ ""Возврат аванса розничному клиенту (наличные)""
	|	КОНЕЦ КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ДвиженияДенежныеСредстваКонтрагент КАК Строки
	|	ПО
	|		ДокументыКОтражению.Ссылка = Строки.Регистратор
	|		И Строки.ТипДенежныхСредств В (ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.ДенежныеСредстваУЭквайера),
	|										ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.Наличные))
	|		И НЕ Строки.Сторно
	|		
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.ДоговорыЭквайринга КАК ДоговорыЭквайринга
	|	ПО
	|		Строки.ДенежныеСредства = ДоговорыЭквайринга.Ссылка";
	
	ТекстыОтражения.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	//-- Локализация
	
	Возврат СтрСоединить(ТекстыОтражения, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете.
//
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	Возврат "";
	
КонецФункции

//++ Локализация

Функция ТекстЗапросаТаблицаКУДиР(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "КнигаУчетаДоходовИРасходов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапросаВтТаблицаСуммаПлатежнымиКартами(Запрос, ТекстыЗапроса);
	
	Если Не Запрос.Параметры.Свойство("КоэффициентПересчетаВВалютуРегл") Тогда
		
		Коэффициенты = РаботаСКурсамиВалютУТ.ПолучитьКоэффициентыПересчетаВалюты(
			Запрос.Параметры.Валюта, Запрос.Параметры.Валюта, Запрос.Параметры.Период, Запрос.Параметры.Организация);
	
		Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуРегл", Коэффициенты.КоэффициентПересчетаВВалютуРегл);
		
	КонецЕсли;
	
	Если Запрос.Параметры.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаПоПатенту Тогда
		ДействующиеПатенты = Справочники.Патенты.ПолучитьДействующиеПатентыНаДату(Запрос.Параметры.Организация, Запрос.Параметры.Подразделение, Запрос.Параметры.Период);
	Иначе
		ДействующиеПатенты = Новый Массив;
	КонецЕсли;
	ДействующийПатент = ?(ДействующиеПатенты.Количество() = 1, ДействующиеПатенты.Получить(0), Справочники.Патенты.ПустаяСсылка());
	Запрос.УстановитьПараметр("Патент", ДействующийПатент); 
	ПараметрыУчетаОрганизации = УчетНДСУП.ПараметрыУчетаПоОрганизации(Запрос.Параметры.Организация, Запрос.Параметры.Период);
	Если НЕ ЗначениеЗаполнено(ПараметрыУчетаОрганизации.СтавкаНДСПриУСН) Тогда
		ОсновнаяСтавкаНДС = 0;
	Иначе
		ОсновнаяСтавкаНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрыУчетаОрганизации.СтавкаНДСПриУСН, "Ставка");
	КонецЕсли;
	Запрос.УстановитьПараметр("СтавкаНДС", ОсновнаяСтавкаНДС);
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Ссылка КАК Регистратор,
	|	&Организация КАК Организация,
	|	&Ссылка КАК ДокументВозникновенияДоходовРасходов,
	|	- ВЫРАЗИТЬ((ДанныеДокумента.СуммаДокумента
	|		- (ЕСТЬNULL(ТаблицаОплатаПлатежнымиКартами.Сумма, 0))) * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2)) КАК Графа4,
	|	- ВЫБОР КОГДА НЕ &НалогообложениеНДС В (ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД),
	|											ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаПоПатенту))
	|		И ДанныеДокумента.СуммаДокумента - (ЕСТЬNULL(ТаблицаОплатаПлатежнымиКартами.Сумма, 0)) > 0
	|		ТОГДА ВЫРАЗИТЬ((ДанныеДокумента.СуммаДокумента
	|			- (ЕСТЬNULL(ТаблицаОплатаПлатежнымиКартами.Сумма, 0))) * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0 КОНЕЦ КАК Графа5,
	|	(0) КАК Графа6,
	|	(0) КАК Графа7,
	|	- ВЫБОР КОГДА НЕ &НалогообложениеНДС В (ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД),
	|											ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаПоПатенту))
	|		И ДанныеДокумента.СуммаДокумента - (ЕСТЬNULL(ТаблицаОплатаПлатежнымиКартами.Сумма, 0)) > 0
	|		ТОГДА &СтавкаНДС*ВЫРАЗИТЬ((ДанныеДокумента.СуммаДокумента
	|			- (ЕСТЬNULL(ТаблицаОплатаПлатежнымиКартами.Сумма, 0))) * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2)) / (100 + &СтавкаНДС)
	|		ИНАЧЕ 0 КОНЕЦ КАК НДС,
	|	- ВЫБОР КОГДА &НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД)
	|		ТОГДА ВЫРАЗИТЬ((ДанныеДокумента.СуммаДокумента
	|				- (ЕСТЬNULL(ТаблицаОплатаПлатежнымиКартами.Сумма, 0))) * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0 КОНЕЦ КАК ДоходЕНВД,
	|	- ВЫБОР КОГДА &НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаПоПатенту)
	|		И ДанныеДокумента.СуммаДокумента
	|		- (ЕСТЬNULL(ТаблицаОплатаПлатежнымиКартами.Сумма, 0)) > 0
	|		ТОГДА ВЫРАЗИТЬ((ДанныеДокумента.СуммаДокумента
	|			- (ЕСТЬNULL(ТаблицаОплатаПлатежнымиКартами.Сумма, 0))) * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0 КОНЕЦ КАК ДоходПатент,
	|	&Патент КАК Патент,
	|	ДанныеДокумента.Номер КАК Номер,
	|	&Период КАК Дата,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.Наличные) КАК ТипДенежныхСредств,
	|	НЕОПРЕДЕЛЕНО КАК Статья
	|	
	|ИЗ
	|	Документ.ВозвратПодарочныхСертификатов КАК ДанныеДокумента
	|   	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Константы КАК Константы
	|   	ПО ИСТИНА
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтТаблицаСуммаПлатежнымиКартами КАК ТаблицаОплатаПлатежнымиКартами
	|		ПО ИСТИНА
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И Константы.ДатаНачалаПризнанияДоходовОтчетомОРозничныхПродажах <= &Период
	|	И ДанныеДокумента.СуммаДокумента - (ЕСТЬNULL(ТаблицаОплатаПлатежнымиКартами.Сумма, 0)) > 0";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

 Функция ТекстЗапросаВтТаблицаСуммаПлатежнымиКартами(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаСуммаПлатежнымиКартами";
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	СУММА(ТабличнаяЧасть.Сумма) КАК Сумма
		|ПОМЕСТИТЬ ВтТаблицаСуммаПлатежнымиКартами
		|ИЗ
		|	Документ.ВозвратПодарочныхСертификатов.ОплатаПлатежнымиКартами КАК ТабличнаяЧасть
		|ГДЕ
		|	ТабличнаяЧасть.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

//-- Локализация

//-- НЕ УТ

#КонецОбласти

#Область Печать

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПодарочныеСертификаты") Тогда
		
		//++ Локализация
		
		// КМ-3
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Обработчик = "УправлениеПечатьюУТКлиентЛокализация.ПечатьКМ3";
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьКМ3";
		КомандаПечати.Идентификатор = "КМ3";
		КомандаПечати.Представление = НСтр("ru = 'КМ-3';
											|en = 'KM-3'");
		
		//-- Локализация
		
	КонецЕсли;

КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
КонецПроцедуры

// Функция получает данные для формирования печатной формы КМ-3.
//
// Возвращаемое значение:
// 	Структура:
// 		* РезультатЗапроса - РезультатЗапроса
// 		* ЗаголовокДокумента - Строка
//
Функция ПолучитьДанныеДляПечатнойФормыКМ3(ПараметрыПечати, МассивОбъектов) Экспорт
	
	СтруктураДанныхДляПечати = Неопределено;
	
	//++ Локализация
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов, МенеджерВременныхТаблиц);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДокЧек.Ссылка КАК Ссылка,
	|	ДокЧек.Номер КАК Номер,
	|	ДокЧек.Дата КАК ДатаДокумента,
	|	ДокЧек.КассаККМ КАК КассаККМ,
	|	ДокЧек.КассаККМ.ТипКассы КАК ТипКассы,
	|	ДокЧек.КассаККМ.Представление КАК Покупатель,
	|	ДокЧек.Кассир.ФизическоеЛицо КАК КассирККМ,
	|	ДокЧек.Валюта КАК Валюта,
	|	ДокЧек.Организация КАК Организация,
	|	ТаблицаОтветственныеЛица.РуководительНаименование КАК Руководитель,
	|	ТаблицаОтветственныеЛица.РуководительДолжность КАК ДолжностьРуководителя,
	|	ТаблицаОтветственныеЛица.ГлавныйБухгалтерНаименование КАК ГлавныйБухгалтер,
	|	ДокЧек.Организация.Представление КАК Поставщик,
	|	ДокЧек.КассаККМ.СерийныйНомер КАК СерийныйНомерККМ,
	|	ДокЧек.КассаККМ.РегистрационныйНомер КАК РегистрационныйНомерККМ,
	|	ДокЧек.КассаККМ.Наименование КАК ПредставлениеККМ
	|ИЗ
	|	Документ.ВозвратПодарочныхСертификатов КАК ДокЧек
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|		ПО ДокЧек.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|ГДЕ
	|	ДокЧек.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокЧек.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокЧек.Ссылка                                           КАК Документ,
	|	ФискальныеОперации.НомерЧекаККМ 						КАК НомерЧека,
	|	ДокЧек.СуммаДокумента                                   КАК СуммаДокумента
	|ИЗ
	|	Документ.ВозвратПодарочныхСертификатов КАК ДокЧек
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФискальныеОперации КАК ФискальныеОперации
	|		ПО ФискальныеОперации.ДокументОснование = ДокЧек.РеализацияПодарочныхСертификатов
	|ГДЕ
	|	ДокЧек.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Документ
	|ИТОГИ ПО
	|	Документ";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	СтруктураДанныхДляПечати    = Новый Структура("РезультатЗапроса, ЗаголовокДокумента",
		РезультатЗапроса, НСтр("ru = 'КМ-3';
								|en = 'KM-3'"));
	
	Если ПривилегированныйРежим() Тогда
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	//-- Локализация
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

#КонецОбласти

#Область Фискализация

//++ Локализация

// Определяет систему налогообложения по документу
// 
// Параметры:
// 	ДокументСсылка - ДокументСсылка - Документ для определения системы налогообложения
// Возвращаемое значение:
// 	ПеречислениеСсылка.ТипыСистемНалогообложенияККТ - Система налогообложения по документу
Функция СистемаНалогообложенияПоДокументу(ДокументСсылка) Экспорт
	
	РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, "Организация, КассаККМ");
	РеквизитыКассыККМ = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РеквизитыДокумента.КассаККМ, "Склад, Подразделение");
	
	НалогообложениеНДС = УчетНДСУП.ПараметрыУчетаПоОрганизации(РеквизитыДокумента.Организация, ТекущаяДатаСеанса(), РеквизитыКассыККМ.Склад, РеквизитыКассыККМ.Подразделение).НалогообложениеНДСРозничнойПродажи;
	
	Если НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД Тогда
		СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.ЕНВД;
	ИначеЕсли НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаПоПатенту Тогда
		СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.Патент;
	Иначе
		СистемаНалогообложения = РозничныеПродажиЛокализация.СистемаНалогообложенияФискальнойОперации(РеквизитыДокумента.Организация);
	КонецЕсли;
	
	Возврат СистемаНалогообложения;
	
КонецФункции

// Получить основные данные по таблице товаров для чека о розничной продаже
//
// Параметры:
//  ДокументСсылка - ДокументОбъект.ВозвратПодарочныхСертификатов - Документ.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Данные о продажах:
//  * НомерСтроки - Число -
//  * Номенклатура - Строка -
//  * Характеристика - Строка - 
//  * ТипНоменклатуры - Неопределено - 
//  * ПодакцизныйТовар - Булево - 
//  * НоменклатураНаименование - Строка - 
//  * ХарактеристикаНаименование - Строка - 
//  * Упаковка - Строка - 
//  * УпаковкаНаименование - Строка - 
//  * Количество - Число - 
//  * КоличествоУпаковок - Число - 
//  * Цена - Число - 
//  * СуммаСНДС - Число - 
//  * СтавкаНДС - СправочникСсылка.СтавкиНДС - 
//  * СуммаНДС - Число - 
//  * СуммаСкидки - Число - 
Функция ПредметыРасчетовПоДокументу(ДокументСсылка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		ДокументСсылка,
		"Дата, Организация, НалогообложениеНДС");
		
	СтавкаНДС = УчетНДСУП.СтавкаНДСПоНоменклатуреИНалогообложению(
		Неопределено,
		РеквизитыДокумента.НалогообложениеНДС,
		РеквизитыДокумента.Организация,
		РеквизитыДокумента.Дата);
	
	СтавкаНДСЧислом = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтавкаНДС, "Ставка");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПодарочныеСертификаты.НомерСтроки,
	|	ПодарочныеСертификаты.ПодарочныйСертификат.Наименование КАК Номенклатура,
	|	""""                                             		КАК Характеристика,
	|	""""                                             		КАК Упаковка,
	|
	|	1 														КАК Количество,
	|	1 														КАК КоличествоУпаковок,
	|
	|	ПодарочныеСертификаты.Сумма 							КАК Цена,
	|	ПодарочныеСертификаты.Сумма								КАК СуммаСНДС,
	|
	|	ВЫБОР
	|		КОГДА &НалогообложениеНДС В (ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД),
	|					ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|					ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаПоПатенту))
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)
	|		ИНАЧЕ &СтавкаНДС
	|	КОНЕЦ 													КАК СтавкаНДС,
	|
	|	ВЫБОР
	|		КОГДА &НалогообложениеНДС В (ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД),
	|					ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|					ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаПоПатенту))
	|			ИЛИ &СтавкаНДСЧислом = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(ПодарочныеСертификаты.Сумма / (100 + &СтавкаНДСЧислом) * &СтавкаНДСЧислом КАК ЧИСЛО(31,2))
	|	КОНЕЦ 													КАК СуммаНДС,
	|	
	|	0														КАК СуммаСкидки
	|
	|ПОМЕСТИТЬ ТаблицаТовары
	|ИЗ
	|	Документ.ВозвратПодарочныхСертификатов.ПодарочныеСертификаты КАК ПодарочныеСертификаты
	|ГДЕ
	|	ПодарочныеСертификаты.Ссылка = &Ссылка
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НомерСтроки,
	|	Товары.Номенклатура,
	|
	|	НЕОПРЕДЕЛЕНО					КАК ТипНоменклатуры,
	|	ЛОЖЬ							КАК ПодакцизныйТовар,
	|	
	|	Товары.Номенклатура   			КАК НоменклатураНаименование,
	|	Товары.Характеристика 			КАК ХарактеристикаНаименование,
	|	Товары.Упаковка                 КАК Упаковка,
	|	Товары.Упаковка					КАК УпаковкаНаименование,
	|	
	|	Товары.Характеристика,
	|	Товары.Количество,
	|	Товары.КоличествоУпаковок,
	|	Товары.Цена,
	|	Товары.СуммаСНДС,
	|	Товары.СтавкаНДС,
	|	Товары.СуммаНДС,
	|	Товары.СуммаСкидки
	|ИЗ
	|	ТаблицаТовары КАК Товары
	|
	|УПОРЯДОЧИТЬ ПО
	|	Товары.НомерСтроки
	|";
		
	Запрос.УстановитьПараметр("Ссылка"            , ДокументСсылка);
	Запрос.УстановитьПараметр("НалогообложениеНДС", РеквизитыДокумента.НалогообложениеНДС);
	Запрос.УстановитьПараметр("СтавкаНДС"         , СтавкаНДС);
	Запрос.УстановитьПараметр("СтавкаНДСЧислом"   , СтавкаНДСЧислом);
	
	ПредметыРасчетов = Запрос.Выполнить().Выгрузить();
	
	Возврат ПредметыРасчетов;
	
КонецФункции

//-- Локализация

// Определяет и возвращает статус, является ли организации плательщиком НДС
// 
// Параметры:
// 	Организация - СправочникСсылка.Организации - Организация, по которой определяется, облагается ли она НДС
// 	ОрганизацияПлательщикНДС - Булево - Параметр для сохранения и передачи во вне значения
Процедура ОбновитьОрганизацияПлательщикНДС(Организация, ОрганизацияПлательщикНДС) Экспорт
	
	//++ Локализация
	ОрганизацияПлательщикНДС = УчетнаяПолитика.ПлательщикНДС(Организация, ТекущаяДатаСеанса())
		И НЕ УчетнаяПолитика.ПрименяетсяОсвобождениеОтУплатыНДС(Организация, ТекущаяДатаСеанса());
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти