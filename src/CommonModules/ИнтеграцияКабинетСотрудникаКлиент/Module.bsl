
#Область СлужебныйПрограммныйИнтерфейс

// См. УправлениеПечатьюКлиентПереопределяемый.ПечатьДокументовВыполнитьКоманду
//
Процедура ПечатьДокументовВыполнитьКоманду(Форма, Команда, ПродолжитьВыполнениеНаСервере, ДополнительныеПараметры) Экспорт
	
	Если Команда.Имя = "ПередатьПодписанныеPDFВСервисКабинетСотрудника" Тогда
		КадровыйЭДОКлиент.ПодписатьПоНастройкам(Форма,
			ПредопределенноеЗначение("Перечисление.ДействияСФайламиДокументовКЭДО.ПередатьВКабинетСотрудников"));
	КонецЕсли;
	
КонецПроцедуры

// См. ОбщегоНазначенияКлиентПереопределяемый.ПриНачалеРаботыСистемы.
Процедура ПриНачалеРаботыСистемы(Параметры) Экспорт 
	
	ПараметрыРаботыКлиентаПриЗапуске = СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиентаПриЗапуске();
	Если СистемаВзаимодействия.ИспользованиеДоступно() И ПараметрыРаботыКлиентаПриЗапуске.ИспользуютсяОбсужденияКабинетСотрудника Тогда
		ИДОбсужденияСтрокой = ПараметрыРаботыКлиентаПриЗапуске.ИДОбсужденияДляСлужебныхОповещенийКабинетСотрудника;
		ПодключитьОбработчикиОбсуждений(ИДОбсужденияСтрокой);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОткрытьНавигационнуюСсылкуПодробноОСервисе() Экспорт
	
	ИнформационнаяСсылка = "https://welcome.1c-cabinet.ru/app/info?state=about";
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(ИнформационнаяСсылка);

КонецПроцедуры

Процедура ОткрытьНавигационнуюСсылкуОписаниеДоступаКДанным() Экспорт

	ИнформационнаяСсылка = "https://welcome.1c-cabinet.ru/app/info?state=security";
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(ИнформационнаяСсылка);	

КонецПроцедуры

Процедура ОткрытьНавигационнуюСсылкуПоддерживаемыеБраузеры() Экспорт

	ИнформационнаяСсылка = "https://welcome.1c-cabinet.ru/app/info?state=technicalinfo";
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(ИнформационнаяСсылка);

КонецПроцедуры

Процедура ОткрытьНавигационнуюСсылкуПокупкаЛицензий() Экспорт

	ИнформационнаяСсылка = "https://portal.1c.ru/applications/94#prices";
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(ИнформационнаяСсылка);

КонецПроцедуры

Функция ТекстСообщенияОБлокировкеСервиса() Экспорт

	Возврат НСтр("ru = 'Доступ к сервису ограничен: закончился оплаченный (тестовый) период или превышен оплаченный лимит на создание личных кабинетов.';
				|en = 'Service access is restricted: the paid (test) period has ended or the paid limit for creating accounts has been exceeded.'");

КонецФункции

#Область ОбработчикиОповещенийСВ

Процедура ПодключитьОбработчикиОбсуждений(ИДОбсужденияСтрокой) Экспорт

	Обработчик = Новый ОписаниеОповещения("ОбработатьДействияСообщения", ЭтотОбъект);
	СистемаВзаимодействия.ПодключитьОбработчикДействияСообщения(Обработчик);
	
	ИДОбсуждения = Новый ИдентификаторОбсужденияСистемыВзаимодействия(ИДОбсужденияСтрокой);
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаСлужебногоОповещения", ЭтотОбъект);
	СистемаВзаимодействия.НачатьПодключениеОбработчикаНовыхСообщений(, ИДОбсуждения, ОписаниеОповещения);	

КонецПроцедуры

Процедура ОбработатьДействияСообщения(Сообщение, Действие, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Действие) = Тип("ФиксированнаяСтруктура") Тогда
		
		ВыполняемоеДействие = Неопределено;
		Если Не Действие.Свойство("ВыполняемоеДействие", ВыполняемоеДействие) Тогда
			Возврат;
		КонецЕсли;
		
		НавигационнаяСсылка = Неопределено;
		Если Не Действие.Свойство("НавигационнаяСсылка", НавигационнаяСсылка) Тогда
			Возврат;
		КонецЕсли;
		
		ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(НавигационнаяСсылка);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаСлужебногоОповещения(Сообщение, ДополнительныеПараметры) Экспорт
	
	ОписаниеСообщения = Новый Структура();
	ОписаниеСообщения.Вставить("Идентификатор", Сообщение.Идентификатор);
	ОписаниеСообщения.Вставить("Данные", 		Сообщение.Данные);
	ОписаниеСообщения = Новый ФиксированнаяСтруктура(ОписаниеСообщения);
	
	ИнтеграцияКабинетСотрудникаВызовСервера.ОбработкаСлужебногоОповещения(ОписаниеСообщения, ДополнительныеПараметры);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти






