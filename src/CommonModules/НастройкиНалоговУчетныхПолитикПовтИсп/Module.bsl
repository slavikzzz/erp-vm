#Область СлужебныйПрограммныйИнтерфейс

// Возвращает систему налогообложения организации
// 
// Параметры:
// 	Организация - СправочникСсылка.Организации - ссылка на организацию.
// 	ДатаДействия - Дата - дата получения системы налогообложения.
// 	ПоУмолчанию - Булево - если истина, то возвращается система налогообложения по умолчанию,
// 	               иначе возвращается только из записи регистра.
// Возвращаемое значение:
// 	ПеречислениеСсылка.СистемыНалогообложения, Неопределено - система налогообложения организации.
Функция СистемаНалогообложенияОрганизации(Организация, ДатаДействия = Неопределено, ПоУмолчанию = Истина) Экспорт
	СистемаНалогообложения = НастройкиНалоговУчетныхПолитикЛокализация.СистемаНалогообложенияОрганизации(Организация,
		ДатаДействия,
		ПоУмолчанию);
	Возврат СистемаНалогообложения;
КонецФункции

// См. НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитикНаДату
Функция ДействующиеПараметрыНалоговУчетныхПолитик(ИмяРегистра, Организация, ДатаДействия = Неопределено, ВозвращатьПоУмолчанию = Истина, ВыдаватьИсключение = Ложь) Экспорт
	
	НастройкиУчетнойПолитики = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитик(ИмяРегистра,
		Организация,
		ДатаДействия,
		ВозвращатьПоУмолчанию,
		ВыдаватьИсключение);
		
	Возврат НастройкиУчетнойПолитики;
	
КонецФункции

// См. НастройкиНалоговУчетныхПолитик.ОрганизацииСЗаданнымПараметромПолитики
Функция ОрганизацииСЗаданнымПараметромПолитики(ИмяРегистра, ИмяПараметра, ЗначениеПараметра,
			Организации = Неопределено, Период = Неопределено) Экспорт
			
	СписокОрганизаций = НастройкиНалоговУчетныхПолитик.ОрганизацииСЗаданнымПараметромПолитики(
		ИмяРегистра, 
		ИмяПараметра, 
		ЗначениеПараметра,
		Организации, 
		Период);
			
	Возврат СписокОрганизаций;
	
КонецФункции

// Возвращает признак использования раздельного учета товаров по налогообложению НДС. 
//
// Параметры:
// 	Организация - СправочникСсылка.Организации - Организация, для которой необходимо получить признак учетной политики
// 	Период 		- Дата, Неопределено - Дата действия учетной политики, если Неопределено, то на текущую дату
//
// Возвращаемое значение:
// 	Булево - Значение признака.
// 
Функция РаздельныйУчетТоваровПоНалогообложениюНДС(Организация, Период = Неопределено) Экспорт
	
	Возврат НастройкиНалоговУчетныхПолитикЛокализация.РаздельныйУчетТоваровПоНалогообложениюНДС(Организация, Период);
	
КонецФункции

// Возвращает признак использования раздельного учета ВНА по налогообложению НДС. 
//
// Параметры:
// 	Организация - СправочникСсылка.Организации - Организация, для которой необходимо получить признак учетной политики
// 	Период 		- Дата, Неопределено - Дата действия учетной политики, если Неопределено, то на текущую дату
//
// Возвращаемое значение:
// 	Булево - Значение признака.
// 
Функция РаздельныйУчетВНАПоНалогообложениюНДС(Организация, Период = Неопределено) Экспорт
	
	Возврат НастройкиНалоговУчетныхПолитикЛокализация.РаздельныйУчетВНАПоНалогообложениюНДС(Организация, Период);
	
КонецФункции

// Возвращает признак использования раздельного учета постатейных производственных затрат по налогообложению НДС. 
//
// Параметры:
// 	Организация - СправочникСсылка.Организации - Организация, для которой необходимо получить признак учетной политики
// 	Период 		- Дата, Неопределено - Дата действия учетной политики, если Неопределено, то на текущую дату
//
// Возвращаемое значение:
// 	Булево - Значение признака.
// 
Функция РаздельныйУчетПостатейныхПроизводственныхЗатратПоНалогообложениюНДС(Организация, Период = Неопределено) Экспорт
	
	Возврат НастройкиНалоговУчетныхПолитикЛокализация.РаздельныйУчетПостатейныхПроизводственныхЗатратПоНалогообложениюНДС(Организация, Период);
	
КонецФункции

// Возвращает настройку формирования НДС Предъявленного при включении в стоимость
//
// Параметры:
// 	Организация - СправочникСсылка.Организации - Организация, для которой необходимо получить признак учетной политики
// 	Период 		- Дата, Неопределено - Дата действия учетной политики, если Неопределено, то на текущую дату
//
// Возвращаемое значение:
//	Булево - Значение признака
// 
Функция ФормироватьНДСПредъявленныйПриВключенииВСтоимость(Организация, Период = Неопределено) Экспорт
	
	ПараметрыПолитики = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитикНаДату("НастройкиУчетаНДС",
		Организация,
		Период);
		
	ФормироватьНДСПредъявленныйПриВключенииВСтоимость = ?(ПараметрыПолитики <> Неопределено, ПараметрыПолитики.ФормироватьНДСПредъявленныйПриВключенииВСтоимость, Ложь);

	Возврат ФормироватьНДСПредъявленныйПриВключенииВСтоимость;
	
КонецФункции

// Возвращает настройку списания НДС по расходам, не принимаемым в НУ
//
// Параметры:
// 	Организация - СправочникСсылка.Организации - Организация, для которой необходимо получить признак учетной политики
// 	Период 		- Дата, Неопределено - Дата действия учетной политики, если Неопределено, то на текущую дату
//
// Возвращаемое значение:
//	Булево - Значение признака
// 
Функция СписыватьНДСПоРасходамНеПринимаемымВНУ(Организация, Период = Неопределено) Экспорт
	
	ПараметрыПолитики = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитикНаДату("НастройкиУчетаНДС",
		Организация,
		Период);
		
	СписыватьНДСПоРасходамНеПринимаемымВНУ = ?(ПараметрыПолитики <> Неопределено, ПараметрыПолитики.СписыватьНДСПоРасходамНеПринимаемымВНУ, Ложь);

	Возврат СписыватьНДСПоРасходамНеПринимаемымВНУ;
	
КонецФункции

// Возвращает головную организацию для переданной организации-филиала, либо ссылку на себя.
// 
// Параметры:
//  Организация - СправочникСсылка.Организации - ссылка на справочник Организации.
// 
// Возвращаемое значение:
//  СправочникСсылка.Организации - Головная организация.
Функция ГоловнаяОрганизация(Организация) Экспорт
	ГоловныеОрганизации = Справочники.Организации.ГоловныеОрганизации(Организация);
	
	ГоловнаяОрганизация = Организация;
	Если ГоловныеОрганизации.Количество() > 0 Тогда
		ГоловнаяОрганизация = ГоловныеОрганизации.Получить(0);
	КонецЕсли;
	
	Возврат ГоловнаяОрганизация
КонецФункции

//++ НЕ УТ

// Определяет ведется ли учет ВНА ведется независимо от упр. учета в указанной организации.
// Если организация не указана, то проверяется во всех организациях доступных пользователю.
// 
// Параметры:
// 	Организация - СправочникСсылка.Организации, Неопределено - Организация, для которой проверяется настройка.
// 	Период 	- Дата, Неопределено - Дата действия учетной политики, если Неопределено, то на текущую дату.
// 
// Возвращаемое значение:
//  Булево - Истина, если хотя бы в одной доступной организации регл. учет ВНА ведется независимо от упр. учета.
Функция РеглУчетВНАВедетсяНезависимо(Организация = Неопределено, Период = Неопределено) Экспорт
	
	Результат = НастройкиНалоговУчетныхПолитикЛокализация.РеглУчетВНАВедетсяНезависимо(Организация, Период);
	
	Если Результат = Неопределено Тогда
		
		Если ЗначениеЗаполнено(Организация) Тогда
			
			ПараметрыПолитики = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитикНаДату(
				Метаданные.РегистрыСведений.УчетнаяПолитикаФинансовогоУчета.Имя, Организация, Период);
			
			Результат = 
				ПараметрыПолитики <> Неопределено
				И ПараметрыПолитики.ПорядокУчетаВНАРегл = Перечисления.ПорядокУчетаВНАРегл.Независимый;	
			
		Иначе
			
			МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
		
			НастройкиНалоговУчетныхПолитик.ДополнитьМенеджерВременныхТаблицДействующимиПараметрамиНалоговУчетныхПолитик(
				Метаданные.РегистрыСведений.УчетнаяПолитикаФинансовогоУчета.Имя,
				МенеджерВременныхТаблиц,
				Период,
				"втДанныеПолитик");
			
			Запрос = Новый Запрос;
			Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
			Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	ИСТИНА
			|ИЗ
			|	втДанныеПолитик КАК Т
			|ГДЕ
			|	Т.ПорядокУчетаВНАРегл = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАРегл.Независимый)
			|";
			Результат = НЕ Запрос.Выполнить().Пустой();
		
		КонецЕсли;

	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает порядок учета внеоборотных активов.
// 
// Параметры:
//	Организация - СправочникСсылка.Организации - организация, для которой определяется порядок.
//	Период - Дата - дата, на которую определяется порядок.
//
// Возвращаемое значение:
//	ПеречислениеСсылка.ПорядокУчетаВНА - Порядок учета внеоборотных активов.
//
Функция ПорядокУчетаВНА(Организация, Период) Экспорт
	
	Возврат НастройкиНалоговУчетныхПолитик.ПорядокУчетаВНА(Организация, Период);
	
КонецФункции

// Возвращает порядок начисления амортизации ОС в упр. учете.
// 
// Параметры:
//	Организация - СправочникСсылка.Организации - организация, для которой определяется порядок.
//	Период - Дата - дата, на которую определяется порядок.
//
// Возвращаемое значение:
//	ПеречислениеСсылка.ПорядокНачисленияАмортизации - Порядок начисления амортизации в бухгалтерском учет.
//
Функция ПорядокНачисленияАмортизацииОСУпр(Организация, Период) Экспорт
	
	ПараметрыПолитики = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитикНаДату(
		"УчетнаяПолитикаФинансовогоУчета", Организация, Период);
			
	Возврат ПараметрыПолитики.ПорядокНачисленияАмортизацииОС;
	
КонецФункции

// Возвращает порядок начисления амортизации ОС в регл. учете.
// 
// Параметры:
//	Организация - СправочникСсылка.Организации - организация, для которой определяется порядок.
//	Период - Дата - дата, на которую определяется порядок.
//
// Возвращаемое значение:
//	ПеречислениеСсылка.ПорядокНачисленияАмортизации - Порядок начисления амортизации в бухгалтерском учет.
//
Функция ПорядокНачисленияАмортизацииОСРегл(Организация, Период) Экспорт
	
	ПорядокНачисленияАмортизацииРегл = НастройкиНалоговУчетныхПолитикЛокализация.ПорядокНачисленияАмортизацииОСРегл(Организация, Период);
	
	Если ПорядокНачисленияАмортизацииРегл = Неопределено Тогда
		
		ПараметрыПолитики = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитикНаДату(
			"УчетнаяПолитикаФинансовогоУчета", Организация, Период);
			
		ПорядокНачисленияАмортизацииРегл = ПараметрыПолитики.ПорядокНачисленияАмортизацииОСРегл;
		
	КонецЕсли;
	
	Возврат ПорядокНачисленияАмортизацииРегл;
	
КонецФункции

// Возвращает применение ФСБУ 14 в бухгалтерском учете.
// 
// Параметры:
//	Организация - СправочникСсылка.Организации - организация, для которой определяется порядок.
//	Период - Дата - дата, на которую определяется порядок.
//
// Возвращаемое значение:
//	Булево - Истина, если применяется ФСБУ 6.
//
Функция ПрименяетсяФСБУ14(Организация, Период) Экспорт
	
	ЗначениеНастройки = НастройкиНалоговУчетныхПолитикЛокализация.ПрименяетсяФСБУ14(Организация, Период);
	
	Если ЗначениеНастройки = Неопределено Тогда
		ЗначениеНастройки = Истина;
	КонецЕсли;
	
	Возврат ЗначениеНастройки;
	
КонецФункции

// Возвращает применение ФСБУ 6 в бухгалтерском учете.
// 
// Параметры:
//	Организация - СправочникСсылка.Организации - организация, для которой определяется порядок.
//	Период - Дата - дата, на которую определяется порядок.
//
// Возвращаемое значение:
//	Булево - Истина, если применяется ФСБУ 6.
//
Функция ПрименяетсяФСБУ6(Организация, Период) Экспорт
	
	ПрименяетсяФСБУ6 = НастройкиНалоговУчетныхПолитикЛокализация.ПрименяетсяФСБУ6(Организация, Период);
	
	Если ПрименяетсяФСБУ6 = Неопределено Тогда
		ПрименяетсяФСБУ6 = Истина;
	КонецЕсли;
	
	Возврат ПрименяетсяФСБУ6;
	
КонецФункции

// Возвращает настройки учета внеоборотных активов.
// 
// Параметры:
//	Организация - СправочникСсылка.Организации - организация, для которой определяются настройки.
//	Период - Дата - дата, на которую определяются настройки.
//
// Возвращаемое значение:
//	Структура - содержит: 
//		* ПорядокУчетаВНА - ПеречислениеСсылка.ПорядокУчетаВНА - 
//		* ПорядокНачисленияАмортизацииОС - ПеречислениеСсылка.ПорядокНачисленияАмортизации - 
//		* ПорядокНачисленияАмортизацииНМА - ПеречислениеСсылка.ПорядокНачисленияАмортизации - 
//		* ПорядокНачисленияАмортизацииАренда - ПеречислениеСсылка.ПорядокНачисленияАмортизации - 
//		* ПорядокУчетаВНАРегл - ПеречислениеСсылка.ПорядокУчетаВНАРегл - 
//		* ПорядокНачисленияАмортизацииРегл - ПеречислениеСсылка.ПорядокНачисленияАмортизации - 
//		* ПорядокНачисленияАмортизацииАрендаРегл - ПеречислениеСсылка.ПорядокНачисленияАмортизации - 
//		* УчетАрендыПоФСБУ25_2018 - Булево - 
//		* ВариантПроводокПоОбесценениюВНА - ПеречислениеСсылка.ВариантыПроводокПоОбесценениюВНА - 
//		* ПрименяетсяФСБУ6 - Булево - 
//
Функция НастройкиУчетаВнеоборотныхАктивов(Организация, Период) Экспорт
	
	ПараметрыПолитики = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитикНаДату(
		"УчетнаяПолитикаФинансовогоУчета", Организация, Период);
	
	НастройкиУчета = Новый Структура;
	НастройкиУчета.Вставить("ПорядокУчетаВНА", ПараметрыПолитики.ПорядокУчетаВНА);
	НастройкиУчета.Вставить("ПорядокУчетаВНАРегл", ПараметрыПолитики.ПорядокУчетаВНАРегл);
	НастройкиУчета.Вставить("ПорядокНачисленияАмортизацииОС", ПараметрыПолитики.ПорядокНачисленияАмортизацииОС);
	НастройкиУчета.Вставить("ПорядокНачисленияАмортизацииОСРегл", ПараметрыПолитики.ПорядокНачисленияАмортизацииОСРегл);
	НастройкиУчета.Вставить("ПорядокНачисленияАмортизацииНМА", ПараметрыПолитики.ПорядокНачисленияАмортизацииНМА);
	НастройкиУчета.Вставить("ПорядокНачисленияАмортизацииНМАРегл", ПараметрыПолитики.ПорядокНачисленияАмортизацииНМАРегл);
	НастройкиУчета.Вставить("ПорядокНачисленияАмортизацииАренда", ПараметрыПолитики.ПорядокНачисленияАмортизацииАренда);
	НастройкиУчета.Вставить("ПорядокНачисленияАмортизацииАрендаРегл", ПараметрыПолитики.ПорядокНачисленияАмортизацииАрендаРегл);
	НастройкиУчета.Вставить("УчетАрендыПоФСБУ25_2018", ПараметрыПолитики.УчетАрендыПоФСБУ25_2018);
	НастройкиУчета.Вставить("ВариантПроводокПоОбесценениюВНА", ПараметрыПолитики.ВариантПроводокПоОбесценениюВНА);
	НастройкиУчета.Вставить("ПрименяетсяФСБУ6", Истина);

	НастройкиНалоговУчетныхПолитикЛокализация.ДополнитьНастройкиУчетаВнеоборотныхАктивов(Организация, Период, НастройкиУчета);
		
	Возврат НастройкиУчета;
	
КонецФункции

//-- НЕ УТ

#КонецОбласти
