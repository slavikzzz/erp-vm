////////////////////////////////////////////////////////////////////////////////
// Подсистема "Криптография".
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает список криптопровайдеров, поддерживаемых 1С-Отчетностью.
//
// Параметры:
//  Алгоритм            - Строка       - "GOST R 34.10-2001", "GOST R 34.10-2012-256", "GOST R 34.10-2012-512",
//                                       при значении "" или Неопределено возвращаются описания для всех алгоритмов.
//  ЭтоLinux            - Булево.
//                        Неопределено - возвращать описания для каждой операционной системы.
//  ТипКриптопровайдера - Перечисления.ТипыКриптоПровайдеров, Строка.
//                        Неопределено - возвращать все типы.
//  Путь                - Строка       - путь модуля криптографии в nix-системах.
//
// Возвращаемое значение:
//  ФиксированныйМассив - массив с описаниями криптопровайдеров.
//    * Имя                 - Строка - имя криптопровайдера.
//    * Тип                 - Число  - тип криптопровайдера.
//    * Путь                - Строка - путь к модулю криптопровайдера в nix-системах.
//    * Представление       - Строка - представление типа криптопровайдера для отображения в интерфейсе.
//    * ТипКриптопровайдера - Перечисления.ТипыКриптоПровайдеров.
//    * Алгоритм            - Строка - "GOST R 34.10-2001", "GOST R 34.10-2012-256", "GOST R 34.10-2012-512".
//    * Поддерживается      - Булево - Истина.
//
Функция ПоддерживаемыеКриптопровайдеры(
		Алгоритм = "",
		ЭтоLinux = Неопределено,
		ТипКриптопровайдера = Неопределено,
		Путь = "",
		ПриоритетViPNet = Ложь) Экспорт
	
	Возврат ИзвестныеКриптопровайдеры(Истина, Алгоритм, ЭтоLinux, ТипКриптопровайдера, Путь, ПриоритетViPNet);
	
КонецФункции

// Возвращает свойства криптопровайдера, поддерживаемого 1С-Отчетностью, или криптопровайдера из массива.
//
// Параметры:
//  Имя                            - Строка - имя криптопровайдера.
//  Тип                            - Число  - тип криптопровайдера.
//  ПоддерживаемыеКриптопровайдеры - Неопределено - получить вызовом ПоддерживаемыеКриптопровайдеры().
//                                   ФиксированныйМассив - массив с описаниями криптопровайдеров.
//    * Имя                 - Строка - имя криптопровайдера.
//    * Тип                 - Число  - тип криптопровайдера.
//    * Путь                - Строка - путь к модулю криптопровайдера в nix-системах.
//    * Представление       - Строка - представление типа криптопровайдера для отображения в интерфейсе.
//    * ТипКриптопровайдера - Перечисления.ТипыКриптоПровайдеров, Строка.
//    * Алгоритм            - Строка - "GOST R 34.10-2001", "GOST R 34.10-2012-256", "GOST R 34.10-2012-512".
//    * Поддерживается      - Булево - флаг поддержки криптопровайдера 1С-Отчетностью.
//  ИндексКриптопровайдеров        - Массив - имя, имя и тип или тип криптопровайдера, для ускорения поиска,
//                                            заполняется при первом и втором вызове автоматически,
//                                            при изменении заполненности параметров Имя, Тип
//                                            передать с Неопределено для перезаполнения.
//
// Возвращаемое значение:
//  Структура    - описание криптопровайдера:
//    * Имя                 - Строка - имя криптопровайдера.
//    * Тип                 - Число  - тип криптопровайдера.
//    * Путь                - Строка - путь к модулю криптопровайдера в nix-системах.
//    * Представление       - Строка - представление типа криптопровайдера для отображения в интерфейсе.
//    * ТипКриптопровайдера - Перечисления.ТипыКриптоПровайдеров, Строка.
//    * Алгоритм            - Строка - "GOST R 34.10-2001", "GOST R 34.10-2012-256", "GOST R 34.10-2012-512".
//    * Поддерживается      - Булево - флаг поддержки криптопровайдера 1С-Отчетностью.
//    * ИндексЭлемента      - Число  - индекс элемента в ПоддерживаемыеКриптопровайдеры, элемент присутствует,
//                                     если задан параметр ПоддерживаемыеКриптопровайдеры.
//  Неопределено - криптопровайдер не найден.
//
Функция СвойстваКриптопровайдера(
		Имя = Неопределено,
		Тип = Неопределено,
		ПоддерживаемыеКриптопровайдеры = Неопределено,
		ИндексКриптопровайдеров = Неопределено) Экспорт
	
	Результат = Неопределено;
	ИндексЭлемента = -1;
	
	Криптопровайдеры = ПоддерживаемыеКриптопровайдеры;
	Если Криптопровайдеры = Неопределено Тогда
		Криптопровайдеры = ПоддерживаемыеКриптопровайдеры();
	КонецЕсли;
	
	Если ИндексКриптопровайдеров = Неопределено ИЛИ НЕ ЗначениеЗаполнено(Тип) И НЕ ЗначениеЗаполнено(Имя) Тогда
		Если ЗначениеЗаполнено(Тип) ИЛИ ЗначениеЗаполнено(Имя) Тогда
			ИндексКриптопровайдеров = Новый Массив;
		КонецЕсли;
		Для каждого Криптопровайдер Из Криптопровайдеры Цикл
			ИндексЭлемента = ИндексЭлемента + 1;
			Если ЗначениеЗаполнено(Тип) ИЛИ ЗначениеЗаполнено(Имя) Тогда
				ТипИмя = ?(ЗначениеЗаполнено(Тип), Строка(Криптопровайдер.Тип) + ";", "")
					+ ?(ЗначениеЗаполнено(Имя), Строка(Криптопровайдер.Имя) + ";", "");
				ИндексКриптопровайдеров.Добавить(ТипИмя);
			КонецЕсли;
			
			Если (НЕ ЗначениеЗаполнено(Тип) ИЛИ Строка(Криптопровайдер.Тип) = Строка(Тип))
				И (НЕ ЗначениеЗаполнено(Имя) ИЛИ Криптопровайдер.Имя = Имя) Тогда
				
				Результат = Криптопровайдер;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	Иначе
		Для ИндексЭлемента = ИндексКриптопровайдеров.Количество() По Криптопровайдеры.Количество() - 1 Цикл
			Криптопровайдер = Криптопровайдеры[ИндексЭлемента];
			ТипИмя = ?(ЗначениеЗаполнено(Тип), Строка(Криптопровайдер.Тип) + ";", "")
				+ ?(ЗначениеЗаполнено(Имя), Строка(Криптопровайдер.Имя) + ";", "");
			ИндексКриптопровайдеров.Добавить(ТипИмя);
		КонецЦикла;
		
		ТипИмя = ?(ЗначениеЗаполнено(Тип), Строка(Тип) + ";", "") + ?(ЗначениеЗаполнено(Имя), Строка(Имя) + ";", "");
		ИндексЭлемента = ИндексКриптопровайдеров.Найти(ТипИмя);
		Если ИндексЭлемента <> Неопределено Тогда
			Результат = Криптопровайдеры[ИндексЭлемента];
		КонецЕсли;
	КонецЕсли;
	
	Если Результат <> Неопределено И ПоддерживаемыеКриптопровайдеры <> Неопределено Тогда
		ЭтоФиксированнаяСтруктура = (ТипЗнч(Результат) = Тип("ФиксированнаяСтруктура"));
		Если ЭтоФиксированнаяСтруктура Тогда
			Результат = Новый Структура(Результат);
		КонецЕсли;
		Результат.Вставить("ИндексЭлемента", ИндексЭлемента);
		Если ЭтоФиксированнаяСтруктура Тогда
			Результат = Новый ФиксированнаяСтруктура(Результат);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает основной используемый отечественный криптографический алгоритм.
//
Функция АлгоритмПоУмолчанию() Экспорт
	
	Возврат "GOST R 34.10-2012-256";
	
КонецФункции

// Возвращает свойства криптопровайдера с алгоритмом по умолчанию или заданным.
//
// Параметры:
//  ТипКриптопровайдера - Перечисления.ТипыКриптоПровайдеров.
//  Алгоритм            - Строка.
//  Путь                - Строка - путь модуля криптографии в nix-системах.
//
// Возвращаемое значение:
//  Структура    - описание криптопровайдера:
//    * Имя                 - Строка - имя криптопровайдера.
//    * Тип                 - Число  - тип криптопровайдера.
//    * Путь                - Строка - путь к модулю криптопровайдера в nix-системах.
//    * Представление       - Строка - представление типа криптопровайдера для отображения в интерфейсе.
//    * ТипКриптопровайдера - Перечисления.ТипыКриптоПровайдеров.
//    * Алгоритм            - Строка - "GOST R 34.10-2001", "GOST R 34.10-2012-256", "GOST R 34.10-2012-512".
//    * Поддерживается      - Булево - флаг поддержки криптопровайдера 1С-Отчетностью.
//
Функция СвойстваКриптопровайдераПоУмолчанию(ТипКриптопровайдера, Алгоритм = Неопределено, Путь = Неопределено) Экспорт
	
	МассивСвойствКриптопровайдеров = Неопределено;
	Если ЗначениеЗаполнено(ТипКриптопровайдера) Тогда
		АлгоритмКриптопровайдера = ?(Алгоритм = Неопределено, АлгоритмПоУмолчанию(), Алгоритм);
		#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
			ЭтоLinux = ОбщегоНазначенияЭДКО.ЭтоКриптопровайдерLinux();
		#Иначе
			ЭтоLinux = ОбщегоНазначенияЭДКОКлиент.ЭтоКриптопровайдерLinux();
		#КонецЕсли
		МассивСвойствКриптопровайдеров = ПоддерживаемыеКриптопровайдеры(
			АлгоритмКриптопровайдера,
			ЭтоLinux,
			ТипКриптопровайдера,
			Путь);
	КонецЕсли;
	
	Если ТипКриптопровайдера = ПредопределенноеЗначение("Перечисление.ТипыКриптоПровайдеров.CryptoProDSS") Тогда
		СвойстваКриптопровайдера = КриптопровайдерCryptoProDSS(Алгоритм);
	ИначеЕсли МассивСвойствКриптопровайдеров <> Неопределено И МассивСвойствКриптопровайдеров.Количество() >= 1 Тогда
		СвойстваКриптопровайдера = МассивСвойствКриптопровайдеров[0];
	Иначе
		СвойстваКриптопровайдера = Новый Структура;
		СвойстваКриптопровайдера.Вставить("Имя", 					"");
		СвойстваКриптопровайдера.Вставить("Тип", 					0);
		СвойстваКриптопровайдера.Вставить("Путь", 					"");
		СвойстваКриптопровайдера.Вставить("Представление", 			"");
		СвойстваКриптопровайдера.Вставить("ТипКриптопровайдера", 	ПредопределенноеЗначение("Перечисление.ТипыКриптоПровайдеров.ПустаяСсылка"));
		СвойстваКриптопровайдера.Вставить("Алгоритм", 				"");
		СвойстваКриптопровайдера.Вставить("Поддерживается", 		Ложь);
	КонецЕсли;
	
	Возврат СвойстваКриптопровайдера;
	
КонецФункции

// Возвращает список всех известных криптопровайдеров, поддерживающих отечественные алгоритмы.
//
// Параметры:
//  Поддерживается      - Булево       - поддерживается 1С-Отчетностью.
//                        Неопределено - возврат и поддерживаемых, и неподдерживаемых.
//  Алгоритм            - Строка       - "GOST R 34.10-2001", "GOST R 34.10-2012-256", "GOST R 34.10-2012-512",
//                                       при значении "" или Неопределено возвращаются описания для всех алгоритмов.
//  ЭтоLinux            - Булево.
//                        Неопределено - возвращать описания для каждой операционной системы.
//  ТипКриптопровайдера - Перечисления.ТипыКриптоПровайдеров, Строка.
//                        Неопределено - возвращать все типы.
//  Путь                - Строка       - путь модуля криптографии в nix-системах.
//
// Возвращаемое значение:
//  ФиксированныйМассив - массив с описаниями криптопровайдеров.
//    * Имя                 - Строка - имя криптопровайдера.
//    * Тип                 - Число  - тип криптопровайдера.
//    * Путь                - Строка - путь к модулю криптопровайдера в nix-системах.
//    * Представление       - Строка - представление типа криптопровайдера для отображения в интерфейсе.
//    * ТипКриптопровайдера - Перечисления.ТипыКриптоПровайдеров, Строка.
//    * Алгоритм            - Строка - "GOST R 34.10-2001", "GOST R 34.10-2012-256", "GOST R 34.10-2012-512".
//    * Поддерживается      - Булево - флаг поддержки криптопровайдера 1С-Отчетностью.
//
Функция ИзвестныеКриптопровайдеры(
		Поддерживается = Неопределено,
		Алгоритм = "",
		ЭтоLinux = Неопределено,
		ТипКриптопровайдера = Неопределено,
		Путь = "",
		ПриоритетViPNet = Ложь) Экспорт
	
	СписокКриптопровайдеров = Новый Массив;
	
	НачальныйИндексТипаКриптопровайдера = 1;
	КонечныйИндексТипаКриптопровайдера 	= 4;
	
	ИндексКриптоПро = ?(ПриоритетViPNet, 2, 1);
	ИндексViPNet 	= ?(ПриоритетViPNet, 1, 2);
	
	Если ЗначениеЗаполнено(ТипКриптопровайдера) Тогда
		ИндексЗаданногоТипаКриптопровайдера = 0;
		Если ТипКриптопровайдера = ПредопределенноеЗначение("Перечисление.ТипыКриптоПровайдеров.CryptoPro")
			ИЛИ ТипКриптопровайдера = "CryptoPro" Тогда
			ИндексЗаданногоТипаКриптопровайдера = ИндексКриптоПро;
		ИначеЕсли ТипКриптопровайдера = ПредопределенноеЗначение("Перечисление.ТипыКриптоПровайдеров.VipNet")
			ИЛИ ТипКриптопровайдера = "VipNet" Тогда
			ИндексЗаданногоТипаКриптопровайдера = ИндексViPNet;
		ИначеЕсли ТипКриптопровайдера = "SignalCOM" Тогда
			ИндексЗаданногоТипаКриптопровайдера = 3;
		ИначеЕсли ТипКриптопровайдера = "LISSI" Тогда
			ИндексЗаданногоТипаКриптопровайдера = 4;
		КонецЕсли;
		НачальныйИндексТипаКриптопровайдера = ИндексЗаданногоТипаКриптопровайдера;
		КонечныйИндексТипаКриптопровайдера 	= ИндексЗаданногоТипаКриптопровайдера;
	КонецЕсли;
	
	Для ИндексТипаКриптопровайдера = НачальныйИндексТипаКриптопровайдера По КонечныйИндексТипаКриптопровайдера Цикл
		МассивСвойств = Неопределено;
		
		Если ИндексТипаКриптопровайдера = ИндексКриптоПро И Поддерживается <> Ложь Тогда
			МассивСвойств = КриптопровайдерCryptoPro(Алгоритм, ЭтоLinux, 0, Путь);
			
		ИначеЕсли ИндексТипаКриптопровайдера = ИндексViPNet И Поддерживается <> Ложь И ЭтоLinux <> Истина Тогда
			МассивСвойств = КриптопровайдерViPNet(Алгоритм);
			
		ИначеЕсли ИндексТипаКриптопровайдера = 3 И Поддерживается <> Истина И ЭтоLinux <> Истина Тогда
			МассивСвойств = КриптопровайдерSignalCOM(Алгоритм);
			
		ИначеЕсли ИндексТипаКриптопровайдера = 4 И Поддерживается <> Истина И ЭтоLinux <> Истина Тогда
			МассивСвойств = КриптопровайдерЛИССИ(Алгоритм);
		КонецЕсли;
		
		Если МассивСвойств <> Неопределено Тогда
			Если ТипЗнч(МассивСвойств) <> Тип("Массив") И ТипЗнч(МассивСвойств) <> Тип("ФиксированныйМассив") Тогда
				МассивСвойств = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(МассивСвойств);
			КонецЕсли;
			
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СписокКриптопровайдеров, МассивСвойств);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Новый ФиксированныйМассив(СписокКриптопровайдеров);
	
КонецФункции

// Возвращает описание криптопровайдера CryptoPro CSP.
//
// Параметры:
//  Алгоритм        - Строка       - "GOST R 34.10-2001", "GOST R 34.10-2012-256", "GOST R 34.10-2012-512",
//                                   при значении "" или Неопределено возвращается массив свойств криптопровайдеров
//                                   всех алгоритмов и классов защиты.
//  ЭтоLinux        - Булево.
//                  - Неопределено - при пустых значениях Алгоритм или КлассЗащиты возвращаются массив свойств
//                                   криптопровайдеров для каждой операционной системы, иначе возвращаются свойства
//                                   криптопровайдера для текущей.
//  КлассЗащиты     - Число        - 1, 2 или 3,
//                                   при значении 0 или Неопределено возвращается массив свойств криптопровайдеров всех
//                                   классов защиты.
//  Путь            - Строка       - путь модуля криптографии в nix-системах.
//                  - Неопределено - определить путь модуля криптографии.
//  ВсеКлассыЗащиты - Булево       - возвращать имена криптопровайдеров со всеми классами защиты, а не только основной.
//
// Возвращаемое значение:
//  ФиксированнаяСтруктура или ФиксированныйМассив из ФиксированнаяСтруктура (при Алгоритм = Неопределено) - описание криптопровайдера.
//    * Имя                 - Строка - имя криптопровайдера.
//    * Тип                 - Число  - тип криптопровайдера.
//    * Путь                - Строка - путь к модулю криптопровайдера в nix-системах.
//    * Представление       - Строка - представление типа криптопровайдера для отображения в интерфейсе.
//    * ТипКриптопровайдера - Перечисления.ТипыКриптоПровайдеров.CryptoPro.
//    * Алгоритм            - Строка - "GOST R 34.10-2001", "GOST R 34.10-2012-256", "GOST R 34.10-2012-512".
//    * Поддерживается      - Булево - Истина.
//
Функция КриптопровайдерCryptoPro(
		Алгоритм = "GOST R 34.10-2012-256",
		ЭтоLinux = Неопределено,
		КлассЗащиты = 1,
		Путь = Неопределено,
		ВсеКлассыЗащиты = Ложь) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Алгоритм) ИЛИ НЕ ЗначениеЗаполнено(КлассЗащиты) Тогда
		МассивСвойств = Новый Массив;
		
		Если ЗначениеЗаполнено(Алгоритм) Тогда
			МассивАлгоритмов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Алгоритм);
		Иначе
			МассивАлгоритмов = ПоддерживаемыеАлгоритмы();
		КонецЕсли;
		
		Если ЭтоLinux = Неопределено Тогда
			ЭтоКриптопровайдерLinuxПервыйВариант 	= 0;
			ЭтоКриптопровайдерLinuxПоследнийВариант = 1;
		Иначе
			ЭтоКриптопровайдерLinuxПервыйВариант 	= ?(ЭтоLinux, 1, 0);
			ЭтоКриптопровайдерLinuxПоследнийВариант = ?(ЭтоLinux, 1, 0);
		КонецЕсли;
		
		ПутьМодуляКриптографииДляLinux = Путь;
		
		Для каждого ПроверяемыйАлгоритм Из МассивАлгоритмов Цикл
			Для ЭтоКриптопровайдерLinux = ЭтоКриптопровайдерLinuxПервыйВариант По ЭтоКриптопровайдерLinuxПоследнийВариант Цикл
				ПоследнийПроверяемыйКлассЗащиты = ?(ЭтоКриптопровайдерLinux = 1 И ВсеКлассыЗащиты, 3, 1);
				Для ПроверяемыйКлассЗащиты = 1 По ПоследнийПроверяемыйКлассЗащиты Цикл
					Если ПутьМодуляКриптографииДляLinux = Неопределено И ЭтоКриптопровайдерLinux = 1 Тогда
						ПутьМодуляКриптографииДляLinux =
							ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.ПутьМодуляКриптографии();
					КонецЕсли;
					ПутьМодуляКриптографии = ?(ЭтоКриптопровайдерLinux = 1, ПутьМодуляКриптографииДляLinux, Строка(Путь));
					
					Свойства = КриптопровайдерCryptoPro(
						ПроверяемыйАлгоритм,
						ЭтоКриптопровайдерLinux = 1,
						ПроверяемыйКлассЗащиты,
						ПутьМодуляКриптографии,
						ВсеКлассыЗащиты);
					МассивСвойств.Добавить(Свойства);
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
		
		Возврат Новый ФиксированныйМассив(МассивСвойств);
	КонецЕсли;
	
	ЭтоКриптопровайдерLinux = ЭтоLinux;
	Если ЭтоКриптопровайдерLinux = Неопределено Тогда
		#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
			ЭтоКриптопровайдерLinux = ОбщегоНазначенияЭДКО.ЭтоКриптопровайдерLinux();
		#Иначе
			ЭтоКриптопровайдерLinux = ОбщегоНазначенияЭДКОКлиент.ЭтоКриптопровайдерLinux();
		#КонецЕсли
	КонецЕсли;
	
	ПутьМодуляКриптографии = ?(Путь = Неопределено И ЭтоКриптопровайдерLinux,
		ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.ПутьМодуляКриптографии(), Строка(Путь));
	
	Если Алгоритм = "GOST R 34.10-2012-256" Тогда
		Если ЭтоКриптопровайдерLinux И (КлассЗащиты > 1 ИЛИ ВсеКлассыЗащиты) Тогда
			ИмяКриптопровайдера = СтрШаблон(
				"Crypto-Pro GOST R 34.10-2012 KC%1 CSP",
				Строка(КлассЗащиты));
			
		Иначе
			ИмяКриптопровайдера = "Crypto-Pro GOST R 34.10-2012 Cryptographic Service Provider";
		КонецЕсли;
		
		ТипКриптопровайдера 		= 80;
		АлгоритмКриптопровайдера 	= "GOST R 34.10-2012-256";
		
	ИначеЕсли Алгоритм = "GOST R 34.10-2012-512" Тогда
		Если ЭтоКриптопровайдерLinux И (КлассЗащиты > 1 ИЛИ ВсеКлассыЗащиты) Тогда
			ИмяКриптопровайдера = СтрШаблон(
				"Crypto-Pro GOST R 34.10-2012 KC%1 Strong CSP",
				Строка(КлассЗащиты));
			
		Иначе
			ИмяКриптопровайдера = "Crypto-Pro GOST R 34.10-2012 Strong Cryptographic Service Provider";
		КонецЕсли;
		
		ТипКриптопровайдера 		= 81;
		АлгоритмКриптопровайдера 	= "GOST R 34.10-2012-512";
		
	Иначе // Алгоритм "GOST R 34.10-2001"
		Если ЭтоКриптопровайдерLinux И (КлассЗащиты > 1 ИЛИ ВсеКлассыЗащиты) Тогда
			ИмяКриптопровайдера = СтрШаблон(
				"Crypto-Pro GOST R 34.10-2001 KC%1 CSP",
				Строка(КлассЗащиты));
			
		Иначе
			ИмяКриптопровайдера = "Crypto-Pro GOST R 34.10-2001 Cryptographic Service Provider";
		КонецЕсли;
		
		ТипКриптопровайдера 		= 75;
		АлгоритмКриптопровайдера 	= "GOST R 34.10-2001";
	КонецЕсли;
	
	Свойства = Новый Структура();
	Свойства.Вставить("Имя", 					ИмяКриптопровайдера);
	Свойства.Вставить("Путь", 					ПутьМодуляКриптографии);
	Свойства.Вставить("Тип", 					ТипКриптопровайдера);
	Свойства.Вставить("Представление", 			"CryptoPro CSP");
	Свойства.Вставить("ТипКриптопровайдера", 	ПредопределенноеЗначение("Перечисление.ТипыКриптоПровайдеров.CryptoPro"));
	Свойства.Вставить("Алгоритм", 				АлгоритмКриптопровайдера);
	Свойства.Вставить("Поддерживается", 		Истина);
	
	Возврат Новый ФиксированнаяСтруктура(Свойства);
	
КонецФункции

// Возвращает описание криптопровайдера ViPNet CSP.
//
// Параметры:
//  Алгоритм - Строка - "GOST R 34.10-2001", "GOST R 34.10-2012-256", "GOST R 34.10-2012-512".
//                      при значении "" или Неопределено возвращается массив свойств криптопровайдеров всех алгоритмов.
//
// Возвращаемое значение:
//  ФиксированнаяСтруктура или ФиксированныйМассив из ФиксированнаяСтруктура (при Алгоритм = Неопределено) - описание криптопровайдера.
//    * Имя                 - Строка - имя криптопровайдера.
//    * Тип                 - Число  - тип криптопровайдера.
//    * Путь                - Строка - путь к модулю криптопровайдера в nix-системах.
//    * Представление       - Строка - представление типа криптопровайдера для отображения в интерфейсе.
//    * ТипКриптопровайдера - Перечисления.ТипыКриптоПровайдеров.VipNet.
//    * Алгоритм            - Строка - "GOST R 34.10-2001", "GOST R 34.10-2012-256", "GOST R 34.10-2012-512".
//    * Поддерживается      - Булево - Истина.
//
Функция КриптопровайдерViPNet(Алгоритм = "GOST R 34.10-2012-256") Экспорт
	
	Если НЕ ЗначениеЗаполнено(Алгоритм) Тогда
		МассивСвойств = Новый Массив;
		МассивАлгоритмов = ПоддерживаемыеАлгоритмы();
		
		Для каждого ПроверяемыйАлгоритм Из МассивАлгоритмов Цикл
			Свойства = КриптопровайдерViPNet(ПроверяемыйАлгоритм);
			МассивСвойств.Добавить(Свойства);
		КонецЦикла;
		
		Возврат Новый ФиксированныйМассив(МассивСвойств);
	КонецЕсли;
	
	Если Алгоритм = "GOST R 34.10-2012-256" Тогда
		ИмяКриптопровайдера 		= "Infotecs GOST 2012/512 Cryptographic Service Provider";
		ТипКриптопровайдера 		= 77;
		АлгоритмКриптопровайдера 	= "GOST R 34.10-2012-256";
		
	ИначеЕсли Алгоритм = "GOST R 34.10-2012-512" Тогда
		ИмяКриптопровайдера 		= "Infotecs GOST 2012/1024 Cryptographic Service Provider";
		ТипКриптопровайдера 		= 78;
		АлгоритмКриптопровайдера 	= "GOST R 34.10-2012-512";
		
	Иначе // Алгоритм "GOST R 34.10-2001"
		ИмяКриптопровайдера 		= "Infotecs Cryptographic Service Provider";
		ТипКриптопровайдера			= 2;
		АлгоритмКриптопровайдера 	= "GOST R 34.10-2001";
	КонецЕсли;
	
	Свойства = Новый Структура();
	Свойства.Вставить("Имя", 					ИмяКриптопровайдера);
	Свойства.Вставить("Путь", 					"");
	Свойства.Вставить("Тип", 					ТипКриптопровайдера);
	Свойства.Вставить("Представление", 			"ViPNet CSP");
	Свойства.Вставить("ТипКриптопровайдера", 	ПредопределенноеЗначение("Перечисление.ТипыКриптоПровайдеров.VipNet"));
	Свойства.Вставить("Алгоритм", 				АлгоритмКриптопровайдера);
	Свойства.Вставить("Поддерживается", 		Истина);
	
	Возврат Новый ФиксированнаяСтруктура(Свойства);
	
КонецФункции

// Возвращает описание криптопровайдера Signal-COM CSP.
//
// Параметры:
//   Алгоритм - Строка - "GOST R 34.10-2001", "GOST R 34.10-2012-256", "GOST R 34.10-2012-512".
//                       при значении "" или Неопределено возвращается массив свойств криптопровайдеров всех алгоритмов.
//
// Возвращаемое значение:
//  ФиксированнаяСтруктура или ФиксированныйМассив из ФиксированнаяСтруктура (при Алгоритм = Неопределено) - описание криптопровайдера.
//    * Имя                 - Строка - имя криптопровайдера.
//    * Тип                 - Число  - тип криптопровайдера.
//    * Путь                - Строка - путь к модулю криптопровайдера в nix-системах.
//    * Представление       - Строка - представление типа криптопровайдера для отображения в интерфейсе.
//    * ТипКриптопровайдера - Строка - "SignalCOM".
//    * Алгоритм            - Строка - "GOST R 34.10-2001", "GOST R 34.10-2012-256", "GOST R 34.10-2012-512".
//    * Поддерживается      - Булево - Ложь.
//
Функция КриптопровайдерSignalCOM(Алгоритм = "GOST R 34.10-2012-256") Экспорт
	
	Если НЕ ЗначениеЗаполнено(Алгоритм) Тогда
		МассивСвойств = Новый Массив;
		МассивАлгоритмов = ПоддерживаемыеАлгоритмы();
		
		Для каждого ПроверяемыйАлгоритм Из МассивАлгоритмов Цикл
			Свойства = КриптопровайдерSignalCOM(ПроверяемыйАлгоритм);
			МассивСвойств.Добавить(Свойства);
		КонецЦикла;
		
		Возврат Новый ФиксированныйМассив(МассивСвойств);
	КонецЕсли;
	
	Если Алгоритм = "GOST R 34.10-2012-256" Тогда
		ИмяКриптопровайдера 		= "Signal-COM GOST R 34.10-2012 (256) Cryptographic Provider";
		ТипКриптопровайдера 		= 80;
		АлгоритмКриптопровайдера 	= "GOST R 34.10-2012-256";
		
	ИначеЕсли Алгоритм = "GOST R 34.10-2012-512" Тогда
		ИмяКриптопровайдера 		= "Signal-COM GOST R 34.10-2012 (512) Cryptographic Provider";
		ТипКриптопровайдера 		= 81;
		АлгоритмКриптопровайдера 	= "GOST R 34.10-2012-512";
		
	Иначе // Алгоритм "GOST R 34.10-2001"
		ИмяКриптопровайдера 		= "Signal-COM CPGOST Cryptographic Provider";
		ТипКриптопровайдера			= 75;
		АлгоритмКриптопровайдера 	= "GOST R 34.10-2001";
	КонецЕсли;
	
	Свойства = Новый Структура();
	Свойства.Вставить("Имя", 					ИмяКриптопровайдера);
	Свойства.Вставить("Путь", 					"");
	Свойства.Вставить("Тип", 					ТипКриптопровайдера);
	Свойства.Вставить("Представление", 			"Signal-COM CSP");
	Свойства.Вставить("ТипКриптопровайдера", 	"SignalCOM");
	Свойства.Вставить("Алгоритм", 				АлгоритмКриптопровайдера);
	Свойства.Вставить("Поддерживается", 		Ложь);
	
	Возврат Новый ФиксированнаяСтруктура(Свойства);
	
КонецФункции

// Возвращает описание криптопровайдера ЛИССИ-CSP.
//
// Параметры:
//   Алгоритм - Строка - "GOST R 34.10-2001", "GOST R 34.10-2012-256", "GOST R 34.10-2012-512".
//                       при значении "" или Неопределено возвращается массив свойств криптопровайдеров всех алгоритмов.
//
// Возвращаемое значение:
//  ФиксированнаяСтруктура или ФиксированныйМассив из ФиксированнаяСтруктура (при Алгоритм = Неопределено) - описание криптопровайдера.
//    * Имя                 - Строка - имя криптопровайдера.
//    * Тип                 - Число  - тип криптопровайдера.
//    * Путь                - Строка - путь к модулю криптопровайдера в nix-системах.
//    * Представление       - Строка - представление типа криптопровайдера для отображения в интерфейсе.
//    * ТипКриптопровайдера - Строка - "LISSI".
//    * Алгоритм            - Строка - "GOST R 34.10-2001", "GOST R 34.10-2012-256", "GOST R 34.10-2012-512".
//    * Поддерживается      - Булево - Ложь.
//
Функция КриптопровайдерЛИССИ(Алгоритм = "GOST R 34.10-2012-256") Экспорт
	
	Если НЕ ЗначениеЗаполнено(Алгоритм) Тогда
		МассивСвойств = Новый Массив;
		МассивАлгоритмов = ПоддерживаемыеАлгоритмы();
		
		Для каждого ПроверяемыйАлгоритм Из МассивАлгоритмов Цикл
			Свойства = КриптопровайдерЛИССИ(ПроверяемыйАлгоритм);
			МассивСвойств.Добавить(Свойства);
		КонецЦикла;
		
		Возврат Новый ФиксированныйМассив(МассивСвойств);
	КонецЕсли;
	
	Если Алгоритм = "GOST R 34.10-2012-256" Тогда
		ИмяКриптопровайдера 		= "LISSI GOST R 34.10-2012 (256) CSP";
		ТипКриптопровайдера 		= 80;
		АлгоритмКриптопровайдера 	= "GOST R 34.10-2012-256";
		
	ИначеЕсли Алгоритм = "GOST R 34.10-2012-512" Тогда
		ИмяКриптопровайдера 		= "LISSI GOST R 34.10-2012 (512) CSP";
		ТипКриптопровайдера 		= 81;
		АлгоритмКриптопровайдера 	= "GOST R 34.10-2012-512";
		
	Иначе // Алгоритм "GOST R 34.10-2001"
		ИмяКриптопровайдера 		= "LISSI-CSP";
		ТипКриптопровайдера			= 75;
		АлгоритмКриптопровайдера 	= "GOST R 34.10-2001";
	КонецЕсли;
	
	Свойства = Новый Структура();
	Свойства.Вставить("Имя", 					ИмяКриптопровайдера);
	Свойства.Вставить("Путь", 					"");
	Свойства.Вставить("Тип", 					ТипКриптопровайдера);
	Свойства.Вставить("Представление", 			"ЛИССИ-CSP");
	Свойства.Вставить("ТипКриптопровайдера", 	"LISSI");
	Свойства.Вставить("Алгоритм", 				АлгоритмКриптопровайдера);
	Свойства.Вставить("Поддерживается", 		Ложь);
	
	Возврат Новый ФиксированнаяСтруктура(Свойства);
	
КонецФункции

// Возвращает описание криптопровайдера КриптоПро DSS.
//
// Параметры:
//   Алгоритм - Строка - "GOST R 34.10-2001", "GOST R 34.10-2012-256", "GOST R 34.10-2012-512".
//                       при значении "" или Неопределено возвращается массив свойств криптопровайдеров всех алгоритмов.
//
// Возвращаемое значение:
//  ФиксированнаяСтруктура - описание криптопровайдера:
//    * Имя                 - Строка - имя криптопровайдера.
//    * Тип                 - Число  - тип криптопровайдера.
//    * Путь                - Строка - путь к модулю криптопровайдера в nix-системах.
//    * Представление       - Строка - представление типа криптопровайдера для отображения в интерфейсе.
//    * ТипКриптопровайдера - Перечисления.ТипыКриптоПровайдеров.CryptoProDSS.
//    * Алгоритм            - Строка - "GOST R 34.10-2012-256", "GOST R 34.10-2012-512".
//    * Поддерживается      - Булево - Ложь.
//
Функция КриптопровайдерCryptoProDSS(Алгоритм = "GOST R 34.10-2012-256") Экспорт
	
	Если Алгоритм = "GOST R 34.10-2012-256" Тогда
		ИмяКриптопровайдера 		= "Crypto-Pro GOST R 34.10-2012 HSM CSP";
		ТипКриптопровайдера 		= 80;
		АлгоритмКриптопровайдера 	= "GOST R 34.10-2012-256";
	ИначеЕсли Алгоритм = "GOST R 34.10-2012-512" Тогда
		ИмяКриптопровайдера 		= "Crypto-Pro GOST R 34.10-2012 HSM CSP";
		ТипКриптопровайдера 		= 81;
		АлгоритмКриптопровайдера 	= "GOST R 34.10-2012-512";
	Иначе
		ИмяКриптопровайдера 		= "Crypto-Pro GOST R 34.10-2012 HSM CSP";
		ТипКриптопровайдера 		= 80;
		АлгоритмКриптопровайдера 	= "GOST R 34.10-2012-256";
	КонецЕсли;
	
	Свойства = Новый Структура();
	Свойства.Вставить("Имя", 					ИмяКриптопровайдера);
	Свойства.Вставить("Путь", 					"");
	Свойства.Вставить("Тип", 					ТипКриптопровайдера);
	Свойства.Вставить("Представление", 			"КриптоПро DSS");
	Свойства.Вставить("ТипКриптопровайдера", 	ПредопределенноеЗначение("Перечисление.ТипыКриптоПровайдеров.CryptoProDSS"));
	Свойства.Вставить("Алгоритм", 				АлгоритмКриптопровайдера);
	Свойства.Вставить("Поддерживается", 		Ложь);
	
	Возврат Новый ФиксированнаяСтруктура(Свойства);
	
КонецФункции

// Возвращает описание криптопровайдера Microsoft Base Cryptographic Provider v1.0.
//
// Возвращаемое значение:
//  ФиксированнаяСтруктура - описание криптопровайдера.
//    * Имя                 - Строка - имя криптопровайдера.
//    * Тип                 - Число  - тип криптопровайдера.
//    * Путь                - Строка - путь к модулю криптопровайдера в *nix-системах.
//    * Представление       - Строка - представление типа криптопровайдера для отображения в интерфейсе.
//    * ТипКриптопровайдера - Строка - "MSRSA".
//    * Алгоритм            - Строка - "RSA".
//    * Поддерживается      - Булево - Ложь.
//
Функция КриптопровайдерMicrosoftBaseCryptographicProvider() Экспорт
	
	Свойства = Новый Структура();
	Свойства.Вставить("Имя", 					"Microsoft Base Cryptographic Provider v1.0");
	Свойства.Вставить("Путь", 					"");
	Свойства.Вставить("Тип", 					1);
	Свойства.Вставить("Представление", 			"Microsoft Base Cryptographic Provider v1.0");
	Свойства.Вставить("ТипКриптопровайдера", 	"MSRSA");
	Свойства.Вставить("Алгоритм", 				"RSA");
	Свойства.Вставить("Поддерживается", 		Ложь);
	
	Возврат Новый ФиксированнаяСтруктура(Свойства);
	
КонецФункции

// Возвращает свойства алгоритма.
//
// Параметры:
//  Алгоритм - Строка - "GOST R 34.10-2001", "GOST R 34.10-2012-256", "GOST R 34.10-2012-512".
//
// Возвращаемое значение:
//  ФиксированныйМассив - массив с описаниями криптопровайдеров.
//    * Имя                 - Строка.
//    * Алгоритм            - Строка - "GOST R 34.10-2001", "GOST R 34.10-2012-256", "GOST R 34.10-2012-512".
//    * АлгоритмХеширования - Число.
//    * АлгоритмХешированияВМоделиСервиса - Строка.
//
Функция СвойстваАлгоритма(Алгоритм = "GOST R 34.10-2012-256") Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Имя", 								Алгоритм);
	Результат.Вставить("Алгоритм", 							Алгоритм);
	Результат.Вставить("АлгоритмХеширования", 				0);
	Результат.Вставить("АлгоритмХешированияВМоделиСервиса", "");
	Результат.Вставить("АлгоритмХешированияОблачнойПодписи", "");
	
	Если Алгоритм = "GOST R 34.10-2012-256" Тогда
		Результат.Имя = НСтр("ru = 'ГОСТ Р 34.10-2012 с ключом 256 бит';
							|en = 'ГОСТ Р 34.10-2012 с ключом 256 бит'");
		Результат.АлгоритмХеширования = 32801;
		Результат.АлгоритмХешированияВМоделиСервиса = "GOST R 34.11-2012 256";
		Результат.АлгоритмХешированияОблачнойПодписи = "GR 34.11-2012 256";
		
	ИначеЕсли Алгоритм = "GOST R 34.10-2012-512" Тогда
		Результат.Имя = НСтр("ru = 'ГОСТ Р 34.10-2012 с ключом 512 бит';
							|en = 'ГОСТ Р 34.10-2012 с ключом 512 бит'");
		Результат.АлгоритмХеширования = 32802;
		Результат.АлгоритмХешированияВМоделиСервиса = "GOST R 34.11-2012 512";
		Результат.АлгоритмХешированияОблачнойПодписи = "GR 34.11-2012 512";
		
	Иначе // Aлгоритм "GOST R 34.10-2001"
		Результат.Имя = НСтр("ru = 'ГОСТ Р 34.10-2001';
							|en = 'ГОСТ Р 34.10-2001'");
		Результат.АлгоритмХеширования = 32798;
		Результат.АлгоритмХешированияВМоделиСервиса = "GOST R 34.11-94";
		Результат.АлгоритмХешированияОблачнойПодписи = "GOST R 34.11-94";
		
	КонецЕсли;
	
	Возврат Новый ФиксированнаяСтруктура(Результат);
	
КонецФункции

// Возвращает поддерживаемые алгоритмы для алгоритма по умолчанию.
//
// Параметры:
//  ИсключитьУстаревшие - Булево - при значении Истина исключается "GOST R 34.10-2001".
//
// Возвращаемое значение:
//  ФиксированныйМассив - массив строк с именами алгоритмов "GOST R 34.10-2012-256", "GOST R 34.10-2012-512".
//
Функция ПоддерживаемыеАлгоритмы(ИсключитьУстаревшие = Ложь) Экспорт
	
	МассивАлгоритмов = Новый Массив;
	
	ОсновнойАлгоритм = АлгоритмПоУмолчанию();
	
	Если ОсновнойАлгоритм = "GOST R 34.10-2012-256" Тогда
		МассивАлгоритмов.Добавить("GOST R 34.10-2012-256");
		МассивАлгоритмов.Добавить("GOST R 34.10-2012-512");
		Если НЕ ИсключитьУстаревшие Тогда
			МассивАлгоритмов.Добавить("GOST R 34.10-2001");
		КонецЕсли;
		
	ИначеЕсли ОсновнойАлгоритм = "GOST R 34.10-2012-512" Тогда
		МассивАлгоритмов.Добавить("GOST R 34.10-2012-512");
		МассивАлгоритмов.Добавить("GOST R 34.10-2012-256");
		Если НЕ ИсключитьУстаревшие Тогда
			МассивАлгоритмов.Добавить("GOST R 34.10-2001");
		КонецЕсли;
		
	Иначе
		Если НЕ ИсключитьУстаревшие Тогда
			МассивАлгоритмов.Добавить("GOST R 34.10-2001");
		КонецЕсли;
		МассивАлгоритмов.Добавить("GOST R 34.10-2012-256");
		МассивАлгоритмов.Добавить("GOST R 34.10-2012-512");
	КонецЕсли;
	
	Возврат МассивАлгоритмов;
	
КонецФункции

#Область Сертификаты

// Возвращает признак хранения сертификата в защищенном хранилище на сервере.
//
// Параметры:
//	Сертификат - Структура - Сведения о сертификате.
//
// Возвращаемое значение:
//	Булево - Истина, если сертификат хранится в защищенном хранилище на сервере.
//
Функция СертификатВЗащищенномХранилищеНаСервере(Сертификат) Экспорт
	
	Результат = Ложь;
	
	Если ЗначениеЗаполнено(Сертификат) Тогда
		МестоХраненияКлюча = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Сертификат, "МестоХраненияКлюча");
		ТекущийРежим = ЭтоПодписьСервиса(МестоХраненияКлюча);
		Если ТекущийРежим = Неопределено Тогда
			ТекущийРежим = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Сертификат, "ЭтоЭлектроннаяПодписьВМоделиСервиса");
		КонецЕсли;
		Если ТекущийРежим = Неопределено Тогда
			ТекущийРежим = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Сертификат, "ЭлектроннаяПодписьВМоделиСервиса");
		КонецЕсли;
		Результат = ТекущийРежим = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции   

// Возвращает признак хранения сертификата на сервере облачной подписи.
//
// Параметры:
//	Сертификат - Структура - Сведения о сертификате.
//
// Возвращаемое значение:
//	Булево - Истина, если сертификат храниться на сервере облачной подписи.
//
Функция СертификатОблачнойПодписи(Сертификат) Экспорт
	
	МестоХраненияКлюча = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Сертификат, "МестоХраненияКлюча");
	
	Возврат ЭтоОблачнаяПодпись(МестоХраненияКлюча);
	
КонецФункции
		
#КонецОбласти

#Область ОбработкаРезультатовВызова

// Возвращает описание результата выполнения действия.
//
// Параметры:
//	Выполнено - Булево - Истина, если действие выполнено успешно, иначе Ложь.
//	ИмяПоляРезультат - Строка, Неопределено - Имя поля со значением результата.
//	ЗначениеРезультат - Строка, Неопределено - Значение результата.
//	ВходящийКонтекст - Структура, Неопределено - Контекст выполнения.
//
// Возвращаемое значение:
//	Структура - Содержит как минимум ключ:
//		* Выполнено - Булево - признак выполнения.
//
Функция ПодготовитьРезультат(
		Выполнено,
		ИмяПоляРезультат = Неопределено,
		ЗначениеРезультат = Неопределено,
		ВходящийКонтекст = Неопределено) Экспорт
	
	Результат = Новый Структура("Выполнено", Выполнено);
	
	Если ВходящийКонтекст <> Неопределено Тогда
		Если ВходящийКонтекст.Свойство("МенеджерКриптографии") Тогда
			Результат.Вставить("МенеджерКриптографии", ВходящийКонтекст.МенеджерКриптографии);
		КонецЕсли;
		Если ВходящийКонтекст.Свойство("Алгоритм") Тогда
			Результат.Вставить("Алгоритм", ВходящийКонтекст.Алгоритм);
		КонецЕсли;
		Если ВходящийКонтекст.Свойство("ТипКриптопровайдераВладельца") Тогда
			Результат.Вставить("ТипКриптопровайдераВладельца", ВходящийКонтекст.ТипКриптопровайдераВладельца);
		КонецЕсли;
		Если ВходящийКонтекст.Свойство("ТипКриптопровайдераИнициализации") Тогда
			Результат.Вставить("ТипКриптопровайдераИнициализации", ВходящийКонтекст.ТипКриптопровайдераИнициализации);
		КонецЕсли;
		Если ВходящийКонтекст.Свойство("ДвоичныеДанные") Тогда
			Результат.Вставить("ДвоичныеДанные", ВходящийКонтекст.ДвоичныеДанные);
		КонецЕсли;
		Если ВходящийКонтекст.Свойство("ОписаниеОшибки") Тогда
			Результат.Вставить("ОписаниеОшибки", ВходящийКонтекст.ОписаниеОшибки);
		КонецЕсли;
		Если ВходящийКонтекст.Свойство("ИнформацияОбОшибке") И Не ВходящийКонтекст.Свойство("ОписаниеОшибки") Тогда
			Результат.Вставить("ОписаниеОшибки", КраткоеПредставлениеОшибкиКриптосервиса(ВходящийКонтекст.ИнформацияОбОшибке));
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИмяПоляРезультат) Тогда
		Результат.Вставить(ИмяПоляРезультат, ЗначениеРезультат);
	КонецЕсли;
		
	Возврат Результат;
	
КонецФункции

// Возвращает текстовое представление ошибки.
//
// Параметры:
//	ИнформацияОбОшибке - Строка, ИнформацияОбОшибке, Структура - Содержит информацию об ошибке.
//
// Возвращаемое значение:
//	Строка - Текстовое представление ошибки.
//
Функция КраткоеПредставлениеОшибкиКриптосервиса(Знач ИнформацияОбОшибке, Знач ИнформацияОбОшибкеКомпоненты = Неопределено) Экспорт
	
	ПредставлениеОшибки = "";
	ПредставлениеОшибкиКомпоненты = "";
	
	Если ИнформацияОбОшибкеКомпоненты <> Неопределено Тогда 
		ПредставлениеОшибкиКомпоненты = КраткоеПредставлениеОшибкиКриптосервиса(ИнформацияОбОшибкеКомпоненты);
	КонецЕсли;
		
	Если ТипЗнч(ИнформацияОбОшибке) = Тип("ИнформацияОбОшибке") Тогда 
		ПредставлениеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	ИначеЕсли ТипЗнч(ИнформацияОбОшибке) = Тип("Строка") Тогда 
		ПредставлениеОшибки = ИнформацияОбОшибке;
	ИначеЕсли ТипЗнч(ИнформацияОбОшибке) = Тип("Структура") Тогда 
		Если ИнформацияОбОшибке.Свойство("ИнформацияОбОшибке") Тогда
			ПредставлениеОшибки = КраткоеПредставлениеОшибкиКриптосервиса(ИнформацияОбОшибке.ИнформацияОбОшибке);
		ИначеЕсли ИнформацияОбОшибке.Свойство("ОписаниеОшибки") Тогда
			ПредставлениеОшибки = ИнформацияОбОшибке.ОписаниеОшибки;
		Иначе
			ПредставлениеОшибки = "";
			Для Каждого Пара Из ИнформацияОбОшибке Цикл 
				Если СтрНайти(НРег(Пара.Ключ), "ошибк") > 0 Тогда 
					ПредставлениеОшибки = Пара.Значение;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	Иначе
		ПредставлениеОшибки = "";
	КонецЕсли;
	
	Возврат ПредставлениеОшибки + ?(ПредставлениеОшибки = "", "", ?(ПредставлениеОшибкиКомпоненты = "", "", "
	|Ошибка криптокомпоненты: ")) + ПредставлениеОшибкиКомпоненты;
	
КонецФункции

// Проверяет, является ли переданная строка адресом во временном хранилище.
//
// Параметры:
//	Адрес - Строка - Проверяемая строка.
//
// Возвращаемое значение:
//	Булево - Истина, если строка является адресом во временном хранилище.
//
Функция ЭтоАдресВоВременномХранилище(Адрес) Экспорт
	
	Если СтрНайти(Адрес, "e1cib/tempstorage/") = 1 Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

#Область ОписанияОшибок

// Возвращает описание ошибки, в случае, если не удалось проверить подпись.
//
Функция ОписаниеОшибкиНеУдалосьПроверитьПодпись() Экспорт

	Возврат НСтр("ru = 'Не удалось проверить подпись.';
				|en = 'Не удалось проверить подпись.'");
	
КонецФункции

#КонецОбласти

// Проверяет, является ли ошибка криптопровайдера критической, свидетельствующей, что требуется настройка,
// продление лицензии или переустановка криптопровайдера.
//
// Параметры:
//	ОписаниеОшибки - Строка - проверяется на подстроки критических сообщений об ошибках.
//
// Возвращаемое значение:
//	Булево - Истина, если криптопровайдер работает некорректно или конфликт криптопровайдеров.
//
Функция ЭтоКритическаяОшибкаКриптопровайдера(ОписаниеОшибки) Экспорт
	
	ОписаниеОшибкиНРег = нрег(ОписаниеОшибки);
	
	Возврат СтрНайти(ОписаниеОшибкиНРег, "ошибка исполнения функции") > 0
		ИЛИ СтрНайти(ОписаниеОшибкиНРег, "function failed during execution") > 0
		ИЛИ СтрНайти(ОписаниеОшибкиНРег, "ошибка инициализации криптопровайдера") > 0
		ИЛИ СтрНайти(ОписаниеОшибкиНРег, "error initializing cryptoprovider") > 0
		ИЛИ СтрНайти(ОписаниеОшибкиНРег, "не удается найти указанный файл") > 0
		ИЛИ СтрНайти(ОписаниеОшибкиНРег, "specified file cannot be found") > 0
		ИЛИ СтрНайти(ОписаниеОшибкиНРег, "для выполнения операции права недостаточны") > 0
		ИЛИ СтрНайти(ОписаниеОшибкиНРег, "no permissions to perform operation") > 0
		ИЛИ СтрНайти(ОписаниеОшибкиНРег, "библиотека поставщика проинициализирована неправильно") > 0
		ИЛИ СтрНайти(ОписаниеОшибкиНРег, "library initialized incorrectly") > 0
		ИЛИ СтрНайти(ОписаниеОшибкиНРег, "библиотека поставщика не может быть найдена") > 0
		ИЛИ СтрНайти(ОписаниеОшибкиНРег, "library not found") > 0
		ИЛИ СтрНайти(ОписаниеОшибкиНРег, "неизвестный криптографический алгоритм") > 0
		ИЛИ СтрНайти(ОписаниеОшибкиНРег, "unknown cryptographic algorithm") > 0
		ИЛИ СтрНайти(ОписаниеОшибкиНРег, "указан неправильный алгоритм") > 0
		ИЛИ СтрНайти(ОписаниеОшибкиНРег, "algorithm is incorrect") > 0
		ИЛИ СтрНайти(ОписаниеОшибкиНРег, "операция успешно завершена") > 0
		ИЛИ СтрНайти(ОписаниеОшибкиНРег, "operation completed successfully") > 0;
	
КонецФункции

// Проверяет, что ошибка вызвана тем, что сертификат не входит в список авторизованных фирмой "1С".
//
// Параметры:
//	ОписаниеОшибки - Строка - проверяется на подстроку.
//
// Возвращаемое значение:
//	Булево - Истина, если текст ошибки указывает на то, что УЦ сертификата не авторизован фирмой "1С".
//
Функция ЭтоОшибкаНеавторизованногоУЦ(ОписаниеОшибки) Экспорт
	
	ОписаниеОшибкиНРег = нрег(ОписаниеОшибки);
	
	Возврат СтрНайти(ОписаниеОшибки, "не авторизован фирмой") <> 0
		ИЛИ СтрНайти(ОписаниеОшибки, "не найдено подключение к 1С-Отчетности") <> 0;
	
КонецФункции

#КонецОбласти

#Область НоваяСхемаХраненияКлючей

// Формирует структуру, которая хранит информацию о месте хранения ключа.
//
// Возвращаемое значение:
//	Структура - Содержит ключи:
//		* МодельХраненияЗакрытогоКлюча - ПеречислениеСсылка.МодельХраненияЗакрытогоКлюча - Допускает три варианта.
//		* УчетнаяЗаписьОблачнойПодписи - Неопределено, СправочникСсылка.УчетныеЗаписиDSS - Заполняется для варианта,
//											"Облачная подпись".
//
Функция СвойстваМестаХраненияКлюча(МодельХраненияЗакрытогоКлюча = Неопределено, УчетнаяЗаписьОблачнойПодписи = Неопределено) Экспорт
	
	Результат = Новый Структура();
	Если ЗначениеЗаполнено(МодельХраненияЗакрытогоКлюча) Тогда
		Результат.Вставить("МодельХраненияЗакрытогоКлюча", МодельХраненияЗакрытогоКлюча);
	Иначе	
		Результат.Вставить("МодельХраненияЗакрытогоКлюча", ПредопределенноеЗначение("Перечисление.МодельХраненияЗакрытогоКлюча.ЛокальныйКлюч"));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(УчетнаяЗаписьОблачнойПодписи) Тогда
		Результат.Вставить("УчетнаяЗаписьОблачнойПодписи", УчетнаяЗаписьОблачнойПодписи);
	Иначе	
		Результат.Вставить("УчетнаяЗаписьОблачнойПодписи", Неопределено);
	КонецЕсли;
	
	// Проверяется для облачной подписи
	Результат.Вставить("Пароль", Неопределено);
	
	Возврат Результат;
	
КонецФункции	

// Формирует структуру, которая хранит информацию о месте хранения ключа на основе переданных данные.
//
// Возвращаемое значение:
//	Структура - Содержит ключи:
//		* МодельХраненияЗакрытогоКлюча - ПеречислениеСсылка.МодельХраненияЗакрытогоКлюча
//		* УчетнаяЗаписьОблачнойПодписи - Неопределено, СправочникСсылка.УчетныеЗаписиDSS
//
Функция ОпределитьМестоХраненияКлюча(МодельХраненияЗакрытогоКлюча, УчетнаяЗаписьОблачнойПодписи = Неопределено) Экспорт
	
	Если ЭтоОблачнаяПодпись(МодельХраненияЗакрытогоКлюча) Тогда
		Результат = СвойстваМестаХраненияКлюча(МодельХраненияЗакрытогоКлюча, УчетнаяЗаписьОблачнойПодписи);
	ИначеЕсли ЗначениеЗаполнено(МодельХраненияЗакрытогоКлюча) Тогда	
		Результат = СвойстваМестаХраненияКлюча(МодельХраненияЗакрытогоКлюча);
	Иначе
		Результат = СвойстваМестаХраненияКлюча(ПредопределенноеЗначение("Перечисление.МодельХраненияЗакрытогоКлюча.ЛокальныйКлюч"));
	КонецЕсли;	
	
	Возврат Результат;
	
КонецФункции	

// Основная функция для получения данных о месте хранения ключа для последующего выбора варианта выполнения криптоопераций.
// Если в первом реквизите передана структура, то будет получено его поле "МестоХраненияКлюча", 
// иначе будет будет вычислено реальное место хранения ключа, на основе данных справочника "УчетныеЗаписиДокументооборота". 
// Порядок вычисления для облачной подписи: 
//	МодельХраненияКлюча - реквизит справочника
//	УчетнаяЗаписьОблачнойПодписи - по поиску отпечатка руководителя в регистре сведений "СертификатыПользователяDSS"
//
// Параметры:
//	Источник - Булево, Структура, СправочникСсылка.УчетныеЗаписиДокументооборота, СправочникСсылка.Организации - Если передана структура,
//					то она должна содержать поле "МестоХраненияКлюча" (см. ИмяМоделиХраненияКлюча). 
//					Булево используется для поддержки совместимости	со старыми вызовами.
//	КонтекстРаботы - Неопределено, Структура - Если передана структура, которая не содержит поля "МестоХраненияКлюча", 
//					то оно туда будет заполнено, иначе будет возращено его значение.
//
// Возвращаемое значение:
//	Структура - см. СвойстваМестаХраненияКлюча()
//
Функция КонтекстМоделиХраненияКлюча(
			Источник,
			КонтекстРаботы = Неопределено) Экспорт
			
	ИмяМоделиХранения = ИмяМоделиХраненияКлюча();
	
	Результат = СвойстваМестаХраненияКлюча();
	
	МестоХраненияКлюча = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(КонтекстРаботы, ИмяМоделиХранения);
	
	Если МестоХраненияКлюча <> Неопределено Тогда
		Результат = МестоХраненияКлюча;
		
	ИначеЕсли ТипЗнч(Источник) = Тип("Структура") 
		ИЛИ ТипЗнч(Источник) = Тип("ДанныеФормыЭлементКоллекции") Тогда // переданы реквизиты учетной записи или поле с местом хранения
		НашлиЗначение = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Источник, ИмяМоделиХранения);
		НашлиСтарое = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Источник, "ЭтоЭлектроннаяПодписьВМоделиСервиса");
		ДанныеМодели = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Источник, "МодельХраненияЗакрытогоКлюча");
		ДанныеСервиса = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Источник, "ЭлектроннаяПодписьВМоделиСервиса", Ложь);
		
		Если НашлиЗначение <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(Результат, НашлиЗначение);
		ИначеЕсли НашлиСтарое <> Неопределено Тогда
			Результат.МодельХраненияЗакрытогоКлюча = МодельХраненияЗакрытогоКлюча(НашлиСтарое);
			Результат.УчетнаяЗаписьОблачнойПодписи = Неопределено;
		ИначеЕсли НашлиЗначение = Неопределено И ЗначениеЗаполнено(ДанныеМодели) Тогда
			Результат.МодельХраненияЗакрытогоКлюча = Источник.МодельХраненияЗакрытогоКлюча;
			Результат.УчетнаяЗаписьОблачнойПодписи = Источник.УчетнаяЗаписьОблачнойПодписи;
		ИначеЕсли НашлиЗначение = Неопределено Тогда
			Результат.МодельХраненияЗакрытогоКлюча = МодельХраненияЗакрытогоКлюча(ДанныеСервиса);
			Результат.УчетнаяЗаписьОблачнойПодписи = Неопределено;
		КонецЕсли;	
		
	ИначеЕсли ТипЗнч(Источник) = Тип("Булево") Тогда
		Результат.МодельХраненияЗакрытогоКлюча = МодельХраненияЗакрытогоКлюча(Источник);
		Результат.УчетнаяЗаписьОблачнойПодписи = Неопределено;
		
	ИначеЕсли Источник = Неопределено Тогда
		Результат.МодельХраненияЗакрытогоКлюча = ПредопределенноеЗначение("Перечисление.МодельХраненияЗакрытогоКлюча.ЛокальныйКлюч");
		Результат.УчетнаяЗаписьОблачнойПодписи = Неопределено;
			
	ИначеЕсли ТипЗнч(Источник) = КриптографияЭДКОСлужебныйКлиентСервер.ПолучитьТипОблачнойПодписи() Тогда
		Результат.МодельХраненияЗакрытогоКлюча = ПредопределенноеЗначение("Перечисление.МодельХраненияЗакрытогоКлюча.ОблачнаяПодпись");
		Результат.УчетнаяЗаписьОблачнойПодписи = Источник;
		
	Иначе
		РеквизитыУчетнойЗаписи = ЭлектроннаяПодписьВМоделиСервисаБРОВызовСервера.РеквизитыУчетнойЗаписи(Источник);
		
		Если НЕ ЗначениеЗаполнено(РеквизитыУчетнойЗаписи.МодельХраненияЗакрытогоКлюча) Тогда
			Результат.МодельХраненияЗакрытогоКлюча = ПредопределенноеЗначение("Перечисление.МодельХраненияЗакрытогоКлюча.ЛокальныйКлюч");
			Результат.УчетнаяЗаписьОблачнойПодписи = Неопределено;
		Иначе
			Результат.МодельХраненияЗакрытогоКлюча = РеквизитыУчетнойЗаписи.МодельХраненияЗакрытогоКлюча;
			Результат.УчетнаяЗаписьОблачнойПодписи = РеквизитыУчетнойЗаписи.УчетнаяЗаписьОблачнойПодписи;
		КонецЕсли;
		
	КонецЕсли;	
	
	Если КонтекстРаботы = Неопределено Тогда
		КонтекстРаботы = Новый Структура;
	КонецЕсли;
	
	ЗаполнитьМестоХраненияКлюча(Результат, КонтекстРаботы);
	
	Возврат Результат;
	
КонецФункции

// Принудительно перезаполняет данные о месте хранения ключа в структуре.
//
// Параметры: 
//	МестоХраненияКлюча - Структура - См. СвойстваМестаХраненияКлюча().
//	КонтекстРаботы - Структура - целевая структура для заполнения.
//
Процедура ЗаполнитьМестоХраненияКлюча(МестоХраненияКлюча, КонтекстРаботы) Экспорт
	
	Если КонтекстРаботы <> Неопределено Тогда
		Если ТипЗнч(КонтекстРаботы) = Тип("ФиксированнаяСтруктура") Тогда
			КонтекстРаботы = Новый Структура(КонтекстРаботы);
		КонецЕсли;
		КонтекстРаботы.Вставить(ИмяМоделиХраненияКлюча(), МестоХраненияКлюча);
	КонецЕсли;
	
КонецПроцедуры

// Вспомогательная функция для вычисления модели хранения закрытого ключа, для реализации переходного периода.
//
// Параметры:
//	МестоХраненияКлюча - Структура, Булево, ПеречислениеСсылка.МодельХраненияЗакрытогоКлюча, ПеречислениеСсылка.МодельРаботыСКлючами
//
// Возвращаемое значение:
//	ПеречислениеСсылка.МодельХраненияЗакрытогоКлюча	
// 
Функция МодельХраненияЗакрытогоКлюча(МестоХраненияКлюча) Экспорт
	
	Результат = ПредопределенноеЗначение("Перечисление.МодельХраненияЗакрытогоКлюча.ЛокальныйКлюч");
	
	Если ТипЗнч(МестоХраненияКлюча) = Тип("Булево") Тогда
		Результат = ?(МестоХраненияКлюча, ПредопределенноеЗначение("Перечисление.МодельХраненияЗакрытогоКлюча.ВМоделиСервиса"), Результат);
		
	ИначеЕсли ТипЗнч(МестоХраненияКлюча) = Тип("ПеречислениеСсылка.МодельХраненияЗакрытогоКлюча") Тогда
		Результат = МестоХраненияКлюча;
		
	ИначеЕсли ТипЗнч(МестоХраненияКлюча) = Тип("ПеречислениеСсылка.МодельРаботыСКлючами") Тогда
		Результат = МестоХраненияКлюча;
		
	ИначеЕсли ТипЗнч(МестоХраненияКлюча) = Тип("Структура")
		ИЛИ ТипЗнч(МестоХраненияКлюча) = Тип("ДанныеФормыЭлементКоллекции") Тогда
		ДанныеКлюча = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(МестоХраненияКлюча, ИмяМоделиХраненияКлюча());
		Если ДанныеКлюча = Неопределено Тогда
			ДанныеКлюча = МестоХраненияКлюча;
		КонецЕсли;
		
		ДанныеКлюча = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеКлюча, "МодельХраненияЗакрытогоКлюча", Результат);
		Если ДанныеКлюча = Неопределено Тогда
			НашлиСтарое = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(МестоХраненияКлюча, "ЭтоЭлектроннаяПодписьВМоделиСервиса");
			Если НашлиСтарое = Неопределено Тогда
				НашлиСтарое = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(МестоХраненияКлюча, "ЭлектроннаяПодписьВМоделиСервиса");
			КонецЕсли;
			Если НашлиСтарое <> Неопределено Тогда
				Результат = ?(НашлиСтарое, ПредопределенноеЗначение("Перечисление.МодельХраненияЗакрытогоКлюча.ВМоделиСервиса"), Результат);
			КонецЕсли;	
		Иначе
			Результат = ДанныеКлюча;
		КонецЕсли;
		
	КонецЕсли;	
	
	Возврат Результат;
	
КонецФункции

// Определяет, является ли место хранения ключа "Облачная подпись".
//
// Параметры:
//	МестоХраненияКлюча - Структура, Булево, ПеречислениеСсылка.МодельХраненияЗакрытогоКлюча, ПеречислениеСсылка.МодельРаботыСКлючами -
//							См. МодельХраненияЗакрытогоКлюча().
//
// Возвращаемое значение:
//	Булево
// 
Функция ЭтоОблачнаяПодпись(МестоХраненияКлюча) Экспорт
	
	МодельХранения = МодельХраненияЗакрытогоКлюча(МестоХраненияКлюча);
	Результат = МодельХранения = ПредопределенноеЗначение("Перечисление.МодельХраненияЗакрытогоКлюча.ОблачнаяПодпись");
	
	Возврат Результат;
	
КонецФункции

// Определяет, является ли место хранения ключа "локальный компьютер".
//
// Параметры:
//	МестоХраненияКлюча - Структура, Булево, ПеречислениеСсылка.МодельХраненияЗакрытогоКлюча, ПеречислениеСсылка.МодельРаботыСКлючами -
//							См. МодельХраненияЗакрытогоКлюча().
//
// Возвращаемое значение:
//	Булево
// 
Функция ЭтоЛокальнаяПодпись(МестоХраненияКлюча) Экспорт
	
	МодельХранения = МодельХраненияЗакрытогоКлюча(МестоХраненияКлюча);
	Результат = МодельХранения = ПредопределенноеЗначение("Перечисление.МодельХраненияЗакрытогоКлюча.ЛокальныйКлюч")
				ИЛИ МодельХранения = ПредопределенноеЗначение("Перечисление.МодельРаботыСКлючами.ТолькоЛокальныеКлючи");
	
	Возврат Результат;
	
КонецФункции

// Определяет является ли место хранения ключа "подпись в модели сервиса"
//
// Параметры:
//	МестоХраненияКлюча - Структура, Булево, ПеречислениеСсылка.МодельХраненияЗакрытогоКлюча, ПеречислениеСсылка.МодельРаботыСКлючами -
//							См. МодельХраненияЗакрытогоКлюча().
//
// Возвращаемое значение:
//	Булево
// 
Функция ЭтоПодписьСервиса(МестоХраненияКлюча) Экспорт
	
	МодельХранения = МодельХраненияЗакрытогоКлюча(МестоХраненияКлюча);
	Результат = МодельХранения = ПредопределенноеЗначение("Перечисление.МодельХраненияЗакрытогоКлюча.ВМоделиСервиса")
				ИЛИ МодельХранения = ПредопределенноеЗначение("Перечисление.МодельРаботыСКлючами.ВМоделиСервиса");
				
	Возврат Результат;
	
КонецФункции

// Вычисляет учетную запись облачной подписи из данных о месте хранения ключа.
// Имеет смысл, если место хранения ключа "Облачная подпись".
//
// Параметры:
//	КонтекстХранения - Структура - См. СвойстваМестаХраненияКлюча()
//
// Возвращаемое значение:
//	Неопределено, СправочникСсылка.СправочникСсылка.УчетныеЗаписиDSS
// 
Функция ПолучитьУчетнуюЗаписьПодписи(КонтекстХранения) Экспорт
	
	Результат = Неопределено;
	
	Если ТипЗнч(КонтекстХранения) = Тип("Структура")
		ИЛИ ТипЗнч(КонтекстХранения) = Тип("ДанныеФормыЭлементКоллекции") Тогда
		МестоХраненияКлюча = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(КонтекстХранения, ИмяМоделиХраненияКлюча());
		Если МестоХраненияКлюча = Неопределено Тогда
			МестоХраненияКлюча = КонтекстХранения;
		КонецЕсли;
		Результат = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(МестоХраненияКлюча, "УчетнаяЗаписьОблачнойПодписи");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Получает и / или изменяет свойства места хранения ключа
//
// Параметры:
//	МестоХраненияКлюча 	- Структура - см. СвойстваМестаХраненияКлюча()
//	ИмяСвойства 		- Строка
//	НовоеЗначение		- Произвольное - допустимые значения определяются в СвойстваМестаХраненияКлюча()
//
// Возвращаемое значение
//	Неопределено, Произвольное
//
Функция ПараметрыМестаХраненияКлюча(МестоХраненияКлюча, ИмяСвойства, НовоеЗначение = Null) Экспорт
	
	Результат = Неопределено;
	
	Если МестоХраненияКлюча.Свойство(ИмяСвойства, Результат) И НовоеЗначение <> Null Тогда
		МестоХраненияКлюча.Вставить(ИмяСвойства, НовоеЗначение);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Формирует номер мобильного телефона без разделителей в формате +79999999999
//
// Параметры:
//	ТелефонМобильный - Строка
//
// Возвращаемое значение:
//	Строка
//
Функция ТелефонПодтвержденияОпераций(ТелефонМобильный) Экспорт
	
	ТекущийТелефон = ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.ТелефонМобильныйБезРазделителей(ТелефонМобильный);
	
	Возврат ТекущийТелефон

КонецФункции

#КонецОбласти

// Разбор строки владельца или издателя сертификата.
//
// Параметры:
//	СтрокаДляРазбора - Строка
//
// Возвращаемое значение:
//	Соответствие, основные составляющие:
//	  "CN"     - общее имя - имя организации или ФИО (commonName);
//	  "SN"     - фамилия (surname);
//	  "GN"     - имя и отчество (givenName);
//	  "O"      - организация (organizationName);
//	  "OU"     - подразделение (organizationUnitName);
//	  "T"      - должность (title);
//	  "C"      - страна, "RU" для России (countryName);
//	  "ST"     - область, например, "77 г. Москва" (stateOrProvinceName);
//	  "L"      - населенный пункт (localityName);
//	  "STREET" - наименование улицы, номер дома (streetAddress);
//	  "E"      - адрес электронной почты (emailAddress).
//
Функция РазобратьСтрокуСубъекта(СтрокаДляРазбора) Экспорт
	
	Составляющие = Новый Соответствие;
	
	ПодстрокаДляРазбора = СтрокаДляРазбора;
	
	ИндексРавно = СтрНайти(ПодстрокаДляРазбора, "=", НаправлениеПоиска.СКонца);
	Пока ИндексРавно Цикл
		Значение = Сред(ПодстрокаДляРазбора, ИндексРавно + 1);
		Если Прав(Значение, 1) = "," Тогда
			СтроковыеФункцииКлиентСервер.УдалитьПоследнийСимволВСтроке(Значение);
		КонецЕсли;
		
		ПодстрокаДляРазбора = Лев(ПодстрокаДляРазбора, ИндексРавно - 1);
		
		ИндексЗапятая = СтрНайти(ПодстрокаДляРазбора, ",", НаправлениеПоиска.СКонца);
		Если ИндексЗапятая Тогда
			Ключ = Сред(ПодстрокаДляРазбора, ИндексЗапятая + 1);
			ПодстрокаДляРазбора = Лев(ПодстрокаДляРазбора, ИндексЗапятая);
		Иначе
			Ключ = ПодстрокаДляРазбора;
		КонецЕсли;
		ИндексРавно = СтрНайти(ПодстрокаДляРазбора, "=", НаправлениеПоиска.СКонца);
		
		Ключ = ВРег(Ключ);
		Составляющие.Вставить(Ключ, Значение);
	КонецЦикла;
	
	Возврат Составляющие;
	
КонецФункции

// Определение типа владельца или издателя сертификата.
//
// Параметры:
//	СтрокаДляРазбораИлиСоответствие - Строка, Соответствие, ФиксированноеСоответствие
//
// Возвращаемое значение:
//	"ЮЛ" - юридическое лицо, "ИП" - индивидуальный предприниматель, "ФЛ" - физическое лицо.
//
Функция ТипСубъектаСертификата(СтрокаДляРазбораИлиСоответствие, Организация = Неопределено) Экспорт
	
	Если ТипЗнч(СтрокаДляРазбораИлиСоответствие) = Тип("Соответствие")
		ИЛИ ТипЗнч(СтрокаДляРазбораИлиСоответствие) = Тип("ФиксированноеСоответствие") Тогда
		
		СвойстваСубъекта = СтрокаДляРазбораИлиСоответствие;
		
	Иначе
		СвойстваСубъекта = РазобратьСтрокуСубъекта(СтрокаДляРазбораИлиСоответствие);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СвойстваСубъекта["OID1_2_643_100_1"]) ИЛИ ЗначениеЗаполнено(СвойстваСубъекта["OGRN"])
		ИЛИ ЗначениеЗаполнено(СвойстваСубъекта["OID1_2_643_100_4"]) ИЛИ ЗначениеЗаполнено(СвойстваСубъекта["INNLE"]) Тогда
		
		Возврат "ЮЛ";
		
	ИначеЕсли ЗначениеЗаполнено(СвойстваСубъекта["OID1_2_643_100_5"])
		ИЛИ ЗначениеЗаполнено(СвойстваСубъекта["OGRNIP"]) Тогда
		
		Возврат "ИП";
		
	Иначе
		// Например, портал ФСРАР принимает сертификаты физических лиц без ОГРНИП в качестве сертификатов ИП по ИНН
		Если ЗначениеЗаполнено(Организация) Тогда
			ИННСертификата = ИННСертификата(СвойстваСубъекта);
			Если ЗначениеЗаполнено(ИННСертификата)
				И ИННСертификата = ДокументооборотСКОВызовСервера.ИННОрганизации(Организация) Тогда
				
				Возврат "ИП";
			КонецЕсли;
		КонецЕсли;
		
		Возврат "ФЛ";
	КонецЕсли;
	
КонецФункции

// Определение типа владельца или издателя сертификата.
//
// Параметры:
//	СтрокаДляРазбораИлиСоответствие - Строка, Соответствие, ФиксированноеСоответствие
//
// Возвращаемое значение:
//	ИНН организации, при отсутствии - ИНН физического лица
//
Функция ИННСертификата(СтрокаДляРазбораИлиСоответствие) Экспорт
	
	Если ТипЗнч(СтрокаДляРазбораИлиСоответствие) = Тип("Соответствие")
		ИЛИ ТипЗнч(СтрокаДляРазбораИлиСоответствие) = Тип("ФиксированноеСоответствие") Тогда
		
		СвойстваСубъекта = СтрокаДляРазбораИлиСоответствие;
		
	Иначе
		СвойстваСубъекта = РазобратьСтрокуСубъекта(СтрокаДляРазбораИлиСоответствие);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СвойстваСубъекта["OID1_2_643_100_4"]) Тогда
		Возврат СокрЛП(СвойстваСубъекта["OID1_2_643_100_4"]);
		
	ИначеЕсли ЗначениеЗаполнено(СвойстваСубъекта["INNLE"]) Тогда
		Возврат СокрЛП(СвойстваСубъекта["INNLE"]);
		
	ИначеЕсли ЗначениеЗаполнено(СвойстваСубъекта["OID1_2_643_3_131_1_1"]) Тогда
		Возврат СокрЛП(СвойстваСубъекта["OID1_2_643_3_131_1_1"]);
		
	ИначеЕсли ЗначениеЗаполнено(СвойстваСубъекта["INN"]) Тогда
		Возврат СокрЛП(СвойстваСубъекта["INN"]);
		
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПереместитьВоВременномХранилище(Знач ОткудаАдрес, Знач КудаАдрес, Знач ОчиститьИсходный = Ложь, Знач ПолучатьАдрес = Ложь) Экспорт
	
	Если Не ЭтоАдресВременногоХранилища(ОткудаАдрес) Или 
		Не ПолучатьАдрес И Не ЭтоАдресВременногоХранилища(КудаАдрес) Тогда 
		Возврат Ложь;
	КонецЕсли;
		
	ДанныеСодержимое = ПолучитьИзВременногоХранилища(ОткудаАдрес);
	Результат = ПоместитьВоВременноеХранилище(ДанныеСодержимое, КудаАдрес);
	
	Если ОчиститьИсходный Тогда 
		УдалитьИзВременногоХранилища(ОткудаАдрес);
	КонецЕсли;
	
	Возврат ?(ПолучатьАдрес, Результат, Истина);
	
КонецФункции

Процедура ЗаписатьСобытиеВЖурнал(Имя, Уровень = "Ошибка", Комментарий) Экспорт
	
	КриптографияЭДКОСлужебныйВызовСервера.ЗаписатьСобытиеВЖурнал(Имя, Уровень, Комментарий);
	
КонецПроцедуры

Функция ПроверитьПодписьPKCS7ВМоделиСервиса(ФайлПодписи, ФайлДанных) Экспорт
	
	ИзвлеченныеСертификаты = Неопределено;
	ИзвлеченныеПодписанты = Неопределено;
	КомментарийПоОшибке = "";
	ПодписьВалидна = КриптографияЭДКОСлужебныйВызовСервера.ПроверитьПодписьPKCS7(
		ФайлПодписи, ФайлДанных, ИзвлеченныеСертификаты, ИзвлеченныеПодписанты, КомментарийПоОшибке);
		
	Если НЕ ПодписьВалидна И ЗначениеЗаполнено(КомментарийПоОшибке) Тогда
		Результат = ПодготовитьРезультат(Ложь, "ОписаниеОшибки", КомментарийПоОшибке);
	Иначе
		Результат = ПодготовитьРезультат(Истина, "ПодписьВалидна", ПодписьВалидна);
		Результат.Вставить("Подписанты", ИзвлеченныеПодписанты);
		Для ИндексПодписанта = 0 По Результат.Подписанты.Количество() - 1 Цикл
			Результат.Подписанты[ИндексПодписанта] = Новый ФиксированнаяСтруктура(Результат.Подписанты[ИндексПодписанта]);
		КонецЦикла;
		Результат.Подписанты = Новый ФиксированныйМассив(Результат.Подписанты);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПроверитьПодписьОблачнаяПодпись(УчетнаяЗаписьОблачнойПодписи, ФайлПодписи, ФайлДанных) Экспорт
	
	ИзвлеченныеСертификаты = Неопределено;
	ИзвлеченныеПодписанты = Неопределено;
	КомментарийПоОшибке = "";
	ПодписьВалидна = КриптографияЭДКОСлужебныйВызовСервера.ПроверитьПодписьОблачнаяПодпись(
		УчетнаяЗаписьОблачнойПодписи, ФайлПодписи, ФайлДанных, ИзвлеченныеСертификаты, ИзвлеченныеПодписанты, КомментарийПоОшибке);
		
	Если НЕ ПодписьВалидна И ЗначениеЗаполнено(КомментарийПоОшибке) Тогда
		Результат = ПодготовитьРезультат(Ложь, "ОписаниеОшибки", КомментарийПоОшибке);
	Иначе
		Результат = ПодготовитьРезультат(Истина, "ПодписьВалидна", ПодписьВалидна);
		Результат.Вставить("Подписанты", ИзвлеченныеПодписанты);
		Для ИндексПодписанта = 0 По Результат.Подписанты.Количество() - 1 Цикл
			Результат.Подписанты[ИндексПодписанта] = Новый ФиксированнаяСтруктура(Результат.Подписанты[ИндексПодписанта]);
		КонецЦикла;
		Результат.Подписанты = Новый ФиксированныйМассив(Результат.Подписанты);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Определяет единое имя ключа в структурах для данных о месте хранения ключа.
//
// Возвращаемое значение:
//	Строка
//
Функция ИмяМоделиХраненияКлюча()
	
	Результат = "МестоХраненияКлюча";
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти