////////////////////////////////////////////////////////////////////////////////
// ОтчетностьВБанкиСлужебный: Механизм отправки отчетов в банки.
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Добавляет дополнительные параметры в ЗаписьJSON
//
// Параметры:
//  ЗаписьJSON - ЗаписьJSON - запись, в которую добавляются дополнительные параметры;
//  ПараметрыКлиента - Структура - содержит параметры клиентского приложения:
//        * ТипПлатформы - Строка - тип клиентского приложения;
//        * ВерсияОС - Строка - версия ОС на клиенте;
//
Процедура ДобавитьДополнительныеПараметры(ЗаписьJSON, ПараметрыКлиента) Экспорт
	
	ЗаписьJSON.ЗаписатьИмяСвойства("params");
	ЗаписьJSON.ЗаписатьНачалоМассива();
	
	Язык = ТекущийЯзык();
	
	Если Язык = Неопределено Тогда
		Язык = КодОсновногоЯзыкаИнтерфейсаКонфигурации();
	ИначеЕсли ТипЗнч(Язык) = Тип("ОбъектМетаданных") Тогда
		Язык = Язык.КодЯзыка;
	КонецЕсли;
	
	ДобавитьПараметр(ЗаписьJSON,"сonfigLanguage", Язык);
	
	Язык = Метаданные.ОсновнойЯзык.КодЯзыка;
	ДобавитьПараметр(ЗаписьJSON,"сonfigMainLanguage", Язык);
	
	КодЛокализации = КодЛокализацииИнформационнойБазы();
	ДобавитьПараметр(ЗаписьJSON, "сurLocalizationCode", КодЛокализации);

	ЛокализацияПлатформы = ТекущийЯзыкСистемы();
	ДобавитьПараметр(ЗаписьJSON, "systemLanguage", ЛокализацияПлатформы);
	
	СмещениеВремени = Формат((ТекущаяДатаСеанса() - ТекущаяУниверсальнаяДата()), "ЧГ=0");
	ДобавитьПараметр(ЗаписьJSON, "clientTimeOffsetGMT", СмещениеВремени);

	ДобавитьПараметр(ЗаписьJSON, "clientPlatformType", Строка(ПараметрыКлиента.ТипПлатформы));
	ДобавитьПараметр(ЗаписьJSON, "clientOSVersion", ПараметрыКлиента.ВерсияОС);
	
	СистИнфо = Новый СистемнаяИнформация;
	ДобавитьПараметр(ЗаписьJSON, "serverPlatformType", Строка(СистИнфо.ТипПлатформы));
	ДобавитьПараметр(ЗаписьJSON, "serverOSVersion", СистИнфо.ВерсияОС);
	
	ДобавитьПараметр(ЗаписьJSON, "libraryVersion", ИнтернетПоддержкаПользователейКлиентСервер.ВерсияБиблиотеки());
	
	ДобавитьПараметр(ЗаписьJSON, "configName", Метаданные.Имя);
	ДобавитьПараметр(ЗаписьJSON, "configVersion", Метаданные.Версия);
	ДобавитьПараметр(ЗаписьJSON, "vendor", Метаданные.Поставщик);
	ДобавитьПараметр(ЗаписьJSON, "IBID", СтандартныеПодсистемыСервер.ИдентификаторИнформационнойБазы());
	
	НастройкиСоединения = ИнтернетПоддержкаПользователейСлужебныйПовтИсп.НастройкиСоединенияССерверамиИПП();
	Если НастройкиСоединения.ДоменРасположенияСерверовИПП = 0 Тогда
		ДоменнаяЗона = "ru";
	ИначеЕсли НастройкиСоединения.ДоменРасположенияСерверовИПП = 1 Тогда
		ДоменнаяЗона = "eu";
	КонецЕсли;

	ДобавитьПараметр(ЗаписьJSON, "domainZone", ДоменнаяЗона);
	ДобавитьПараметр(ЗаписьJSON, "countryId", "");
	
	ДобавитьПараметр(ЗаписьJSON, "PlatformVersion", СистИнфо.ВерсияПриложения);
	ДобавитьПараметр(ЗаписьJSON, "ConfigLanguage", КодЯзыкаИнтерфейсаКонфигурации());
	КодОсновногоЯзыкаИнтерфейса = КодОсновногоЯзыкаИнтерфейсаКонфигурации();
	ДобавитьПараметр(ЗаписьJSON, "ConfigMainLanguage", КодОсновногоЯзыкаИнтерфейса);
	ДобавитьПараметр(ЗаписьJSON, "CurLocalizationCode", ТекущийКодЛокализации());
	ДобавитьПараметр(ЗаписьJSON, "IBUserName", ИмяПользователя());
	
	ЗаписьJSON.ЗаписатьКонецМассива();
	
КонецПроцедуры

// Обновляет статусы отчетов в фоновом процессе.
//
// Параметры:
//  Параметры - Структура - параметры фоновой процедуры. Содержит поля:
//      * ПараметрыКлиента - Структура - содержит параметры клиентского приложения:
//        ** ТипПлатформы - Строка - тип клиентского приложения;
//        ** ВерсияОС - Строка - версия ОС на клиенте;
//  Адрес - Строка - адрес временного хранилища, в который помещается массив.
//
Процедура ОбновитьСтатусыОтчетов(Параметры, Адрес) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	РегламентированныйОтчет.Идентификатор,
	               |	РегламентированныйОтчет.Ссылка
	               |ИЗ
	               |	Документ.РегламентированныйОтчет КАК РегламентированныйОтчет
	               |ГДЕ
	               |	НЕ РегламентированныйОтчет.Идентификатор = """"
	               |	И РегламентированныйОтчет.СтатусОтчета = &СтатусОтправлено";
	
	Запрос.УстановитьПараметр("СтатусОтправлено", НСтр("ru = 'Отправлено';
														|en = 'Отправлено'"));
	
	Результат = Запрос.Выполнить();
	Выборка   = Результат.Выбрать();
	
	МассивОтчетов = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		
		Ответ          = ПолучитьСтатусОтчетаССервера(Выборка.Идентификатор, Параметры);
		Статус         = Ответ.Статус;
		ТекстОшибки    = Ответ.ЗначениеОшибки;
		ТекстСообщения = Ответ.ПредставлениеОшибки;
		
		Если Статус = "Доставлено" ИЛИ Статус = "Отклонено" Тогда
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ДокументОбъект.СтатусОтчета = Статус;
			ДокументОбъект.Записать();
			МассивОтчетов.Добавить(Выборка.Ссылка);
		КонецЕсли;
		
	КонецЦикла;
	
	ПоместитьВоВременноеХранилище(МассивОтчетов, Адрес);
	
	Если ЗначениеЗаполнено(ТекстОшибки)Тогда
		ВидОперации = НСтр("ru = 'Получение статуса отчета через сервис https://reportbank.1c.ru.';
							|en = 'Получение статуса отчета через сервис https://reportbank.1c.ru.'");
		ОтчетностьВБанкиСлужебныйВызовСервера.ОбработатьОшибку(ВидОперации, ТекстОшибки, ТекстСообщения);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстСообщения) Тогда
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
КонецПроцедуры

// Получает данные из строки в формате JSON.
// 
// Параметры:
//    Строка - Строка - Строка данных в формате JSON
// 
// Возвращаемое значение:
//    Структура - данные JSON в виде структуры.
//
Функция ДанныеИзСтрокиJSON(Строка) Экспорт
	
	Результат = Неопределено;
	
	ЧтениеJSON = Новый ЧтениеJSON;
	Попытка
		ЧтениеJSON.УстановитьСтроку(Строка);
		Результат = ПрочитатьJSON(ЧтениеJSON);
		ЧтениеJSON.Закрыть();
	Исключение
		Операция = НСтр("ru = 'Чтение ответа от сервиса https://reportbank.1c.ru.';
						|en = 'Чтение ответа от сервиса https://reportbank.1c.ru.'");
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		КраткоеПредставлениеОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ТекстСообщения = НСтр("ru = 'При чтении ответа от сервиса https://reportbank.1c.ru произошла ошибка:
									|%1';
									|en = 'При чтении ответа от сервиса https://reportbank.1c.ru произошла ошибка:
									|%1'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, КраткоеПредставлениеОшибки);
		ОтчетностьВБанкиСлужебныйВызовСервера.ОбработатьОшибку(Операция, ПодробноеПредставлениеОшибки, ТекстСообщения);
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

// Отправляет данные через интернет.
//
// Параметры:
//  АдресСервера - Строка - URI;
//  Ресурс - Строка - ресурс, на который отправляются данные;
//  Заголовки - Соответствие - заголовки запроса;
//  Данные - ДвоичныеДанные - тело запроса;
//  ПолучитьТелоКакСтроку - Булево - признак необходимости получения тела как строки;
//  Таймаут - Число - таймаут ожидания ответа сервера;
//
// Возвращаемое значение:
//   Структура - Структура со свойствами:
//      * Статус - Булево - результат получения файла. Истина - операция выполнена успешно.
//      * Тело - ДвоичныеДанные; Строка; Неопределено - данные ответа сервера.
//      * СообщениеОбОшибке - Строка; Неопределено - сообщение об ошибке, если статус Ложь.
//      * КодСостояния - Число; Неопределено - код состояния HTTP-ответа. Наличие кода означает, что был ответ от сервера.
//
Функция ОтправитьЗапросНаСервер(АдресСервера, Ресурс, Заголовки = Неопределено, Данные = Неопределено, ПолучитьТелоКакСтроку = Ложь, Таймаут = 60) Экспорт
	
	СтруктураВозврата = Новый Структура("Статус, Тело, СообщениеОбОшибке, КодСостояния");
	
	Соединение = СоединениеССервером(АдресСервера, Таймаут);

	Если ЗначениеЗаполнено(Заголовки) Тогда
		HTTPЗапрос = Новый HTTPЗапрос(Ресурс, Заголовки);
	Иначе
		HTTPЗапрос = Новый HTTPЗапрос(Ресурс);
	КонецЕсли;
	
	Если НЕ Данные = Неопределено Тогда
		HTTPЗапрос.УстановитьТелоИзДвоичныхДанных(Данные);
	КонецЕсли;

	Попытка
		Ответ = Соединение.ОтправитьДляОбработки(HTTPЗапрос);
	Исключение
		СтруктураВозврата.Статус = Ложь;
		СтруктураВозврата.СообщениеОбОшибке = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Возврат СтруктураВозврата;
	КонецПопытки;
	
	HTTPЗапрос = Неопределено;
	
	Если Ответ.КодСостояния = 200 Тогда
		СтруктураВозврата.Статус = Истина;
		Если ПолучитьТелоКакСтроку Тогда
			СтруктураВозврата.Тело = Ответ.ПолучитьТелоКакСтроку();
		Иначе
			СтруктураВозврата.Тело = Ответ.ПолучитьТелоКакДвоичныеДанные();
		КонецЕсли;
	Иначе
		СтруктураВозврата.Статус = Ложь;
		СтруктураВозврата.СообщениеОбОшибке = РасшифровкаКодаСостоянияHTTP(Ответ.КодСостояния);
		СтруктураВозврата.Тело = Ответ.ПолучитьТелоКакСтроку();
	КонецЕсли;
	
	СтруктураВозврата.КодСостояния = Ответ.КодСостояния;

	Возврат СтруктураВозврата;
	
КонецФункции

// Формирует описание счета для электронного представления формата 5.06 подверсии 2.9.
// 
// Параметры:
//  СчетКодСчета - Строка, ПланСчетовСсылка - Код счета или ссылка на счет.
//
// Возвращаемое значение:
//   Структура - Структура со свойствами:
//      * КодСчета - Строка - код описываемого счета;
//      * НаименованиеСчета - Строка - наименование описываемого счета;
//      * ТипСчета - Строка - тип описываемого счета (А, П, АП);
//      * КодСчетаР - Строка - код родителя описываемого счета;
//      * ТипСчетаР - Строка - наименование родителя описываемого счета;
//      * КодСчетаРР - Строка - код родителя родителя описываемого счета;
//      * ТипСчетаРР - Строка - наименование родителя родителя описываемого счета.
//
Функция ОписаниеСчетаДляВыгрузки(СчетКодСчета) Экспорт
	
	ОписаниеСчета = Новый Структура;
	
	ОписаниеСчета.Вставить("КодСчета", "");
	ОписаниеСчета.Вставить("НаименованиеСчета", "");
	ОписаниеСчета.Вставить("ТипСчета", "");
	
	ОписаниеСчета.Вставить("КодСчетаР", "");
	ОписаниеСчета.Вставить("ТипСчетаР", "");
	
	ОписаниеСчета.Вставить("КодСчетаРР", "");
	ОписаниеСчета.Вставить("ТипСчетаРР", "");
	
	Счет = Неопределено;
	
	Если ТипЗнч(СчетКодСчета) = Тип("Строка") Тогда
		ОтчетностьВБанкиСлужебныйПереопределяемый.УстановитьСчетУчетаПоКоду(СчетКодСчета, Счет);
	Иначе
		Счет = СчетКодСчета;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Счет) Тогда
		ОписаниеСчета.Вставить("КодСчета", Счет.Код);
		ОписаниеСчета.Вставить("НаименованиеСчета", Счет.Наименование);
		ОписаниеСчета.Вставить("ТипСчета", КодВидаСчета(Счет));
		
		Если ЗначениеЗаполнено(Счет.Родитель) Тогда
			ОписаниеСчета.Вставить("КодСчетаР", Счет.Родитель.Код);
			ОписаниеСчета.Вставить("ТипСчетаР", КодВидаСчета(Счет.Родитель));
			
			Если ЗначениеЗаполнено(Счет.Родитель.Родитель) Тогда
				ОписаниеСчета.Вставить("КодСчетаРР", Счет.Родитель.Родитель.Код);
				ОписаниеСчета.Вставить("ТипСчетаРР", КодВидаСчета(Счет.Родитель.Родитель));
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ОписаниеСчета;
	
КонецФункции

// Возвращает таблицу счетов для отчета.
//
// Параметры:
//   ВидОтчета - Строка - имя отчета, для которого надо заполнить список счетов.
//   ИмяФормы - Строка - имя формы отчета БухгалтерскаяОтчетностьВБанк, для которой заполняется список счетов.
//
// Возвращаемое значение:
//   ТаблицаЗначений - таблица с колонками:
//     * Включен - Булево - признак включения счета в отчет;
//     * Счет - ПланСчетовСсылка - ссылка на счет плана счетов, в котором ведется бухгалтерский учет;
//     * Наименование - Строка - наименование счета плана счетов, в котором ведется бухгалтерский учет.
//
Функция СчетаЗаполнения(ВидОтчета, ИмяФормы) Экспорт
	
	ТаблицаСчетов = Новый ТаблицаЗначений;
	ТаблицаСчетов.Колонки.Добавить("Включен", Новый ОписаниеТипов("Булево"));
	ТаблицаСчетов.Колонки.Добавить("Счет");
	ТаблицаСчетов.Колонки.Добавить("Наименование");
	
	ОтчетностьВБанкиСлужебныйПереопределяемый.ЗаполнитьТаблицуСчетовЗаполнения(ИмяФормы, ВидОтчета, ТаблицаСчетов);
	
	Возврат ТаблицаСчетов;
	
КонецФункции

// Возвращает таблицу с банками расчетных счетов организации.
// Параметры:
//   Организация - СправочникСсылка.Организация - организация, для которой нужно получить таблицу с банками.
//
// Возвращаемое значение:
//   ТаблицаЗначений - таблица с колонками:
//     * БИК  - Строка - БИК банка.
//     * Банк - ОпределяемыйТип.СправочникБанкиБРО - банк.
//
Функция БанкиРасчетныхСчетовОрганизации(Организация) Экспорт
	
	ТаблицаБанков = Новый ТаблицаЗначений;
	ТаблицаБанков.Колонки.Добавить("БИК");
	ТаблицаБанков.Колонки.Добавить("Банк");
	
	ОтчетностьВБанкиСлужебныйПереопределяемый.ЗаполнитьТаблицуБанковРасчетныхСчетовОрганизации(
		Организация, ТаблицаБанков);
	
	Возврат ТаблицаБанков;
	
КонецФункции

// Возвращает дерево счетов бухгалтерского учета.
//
// Возвращаемое значение:
//   ДеревоЗначений - дерево со счетами бухгалтерского учета.
//	 ВключатьЗабалансовые - Булево - Если установлен в значение Истина, то выгружаются в том числе и забалансовые счета.
//
Функция ДеревоСчетовБУ(ВключатьЗабалансовые = Ложь) Экспорт
	
	ДеревоСчетовБУ = Новый ДеревоЗначений;
	// Не передаем 2-ой параметр, чтобы не требовать обязательное изменение 
	// в переопределяемом модуле конфигураций-потребителей.
	Если ВключатьЗабалансовые Тогда
		МодульОтчетностьВБанкиСлужебныйПереопределяемый = ОбщегоНазначения.ОбщийМодуль("ОтчетностьВБанкиСлужебныйПереопределяемый");
		МодульОтчетностьВБанкиСлужебныйПереопределяемый.ЗаполнитьДеревоСчетовБУ(ДеревоСчетовБУ, ВключатьЗабалансовые);
	Иначе
		ОтчетностьВБанкиСлужебныйПереопределяемый.ЗаполнитьДеревоСчетовБУ(ДеревоСчетовБУ);
	КонецЕсли;
	
	Возврат ДеревоСчетовБУ;
	
КонецФункции

// Возвращает ссылку на банк заемщика, используемый по умолчанию.
//
// Возвращаемое значение:
//   СправочникСсылка - ссылка на элемент справочника банков.
//
Функция БанкЗаемщикаПоУмолчанию() Экспорт
	
	БанкОрганизации = Неопределено;
	
	ОтчетностьВБанкиСлужебныйПереопределяемый.ЗаполнитьБанкПоУмолчанию(БанкОрганизации);
	
	Возврат БанкОрганизации;
	
КонецФункции

// Возвращает массив с перечнем разделов, заполняемых по сведениям информационной базы.
//
// Параметры:
//   ИмяФормы - Строка - имя формы отчета БухгалтерскаяОтчетностьВБанк, для которой заполняется перечень разделов.
//   ЗаполняемыеРазделы - Массив - массив, в который будут добавлены идентификаторы заполняемых разделов.
//
// Возвращаемое значение:
//   Массив - массив с идентификаторами заполняемых разделов.
//
Функция РазделыЗаполняемыеПоСведениямИБ(ИмяФормыОтчета) Экспорт
	
	ЗаполняемыеРазделы = Новый Массив;
	
	ОтчетностьВБанкиСлужебныйПереопределяемый.ЗаполнитьПереченьЗаполняемыхРазделов(ИмяФормыОтчета, ЗаполняемыеРазделы);
	
	Возврат ЗаполняемыеРазделы;
	
КонецФункции

// Возвращает описание типов для сущности с указанным названием.
//
// Параметры:
//   НазваниеСущности - Строка - название сущности, для которой требуется получить описание типов.
//
// Возвращаемое значение:
//   ОписаниеТипов - описание типов сущности.
//
Функция ТипыСущности(НазваниеСущности) Экспорт
	
	ОписаниеТиповСущности = Новый ОписаниеТипов;
	
	ОтчетностьВБанкиСлужебныйПереопределяемый.ЗаполнитьТипыСущности(НазваниеСущности, ОписаниеТиповСущности);
	
	Возврат ОписаниеТиповСущности;
	
КонецФункции

Функция ПредставлениеАнализаДенежныхСредствКасса(ШаблонПредставления, ДанныеОтчета, ПостфиксЕдИзм = "") Экспорт
	
	ПредставлениеОтчета = Новый ТабличныйДокумент;
	
	Если ШаблонПредставления.Области.Найти("Заголовок") <> Неопределено Тогда
		Секция_Заголовок = ШаблонПредставления.ПолучитьОбласть("Заголовок");
		ПредставлениеОтчета.Вывести(Секция_Заголовок);
		
		Секция_ПустаяСтрока = ШаблонПредставления.ПолучитьОбласть("ПустаяСтрока");
		ПредставлениеОтчета.Вывести(Секция_ПустаяСтрока);
		
	КонецЕсли;
	
	Если ШаблонПредставления.Области.Найти("ШапкаТаблицыКасса") <> Неопределено Тогда
		Секция_ШапкаТаблицы = ШаблонПредставления.ПолучитьОбласть("ШапкаТаблицыКасса");
	Иначе
		Секция_ШапкаТаблицы = ШаблонПредставления.ПолучитьОбласть("ШапкаТаблицы");
	КонецЕсли;
	ПредставлениеОтчета.Вывести(Секция_ШапкаТаблицы);
	
	ПредставлениеОтчета.НачатьАвтогруппировкуСтрок();
	
	УровеньСчета = ДанныеОтчета.Строки;
	Для Каждого СтрокаУровняСчета Из УровеньСчета Цикл
		Секция_СтрокаТаблицыСчет = ШаблонПредставления.ПолучитьОбласть("СтрокаТаблицыСчет" + ПостфиксЕдИзм);
		
		Секция_СтрокаТаблицыСчет.Параметры.КодСчета = СтрокаУровняСчета.Счет.Код;
		Секция_СтрокаТаблицыСчет.Параметры.СНД = СтрокаУровняСчета.СНД;
		Секция_СтрокаТаблицыСчет.Параметры.СНК = СтрокаУровняСчета.СНК;
		
		ПредставлениеОтчета.Вывести(Секция_СтрокаТаблицыСчет, 1);
		
		ОборотыПоСчетуДт = 0;
		ОборотыПоСчетуКт = 0;
		
		УровеньПериода = СтрокаУровняСчета.Строки;
		
		Для Каждого СтрокаУровняПериода Из УровеньПериода Цикл
			Секция_СтрокаТаблицыПериод = ШаблонПредставления.ПолучитьОбласть("СтрокаТаблицыПериод" + ПостфиксЕдИзм);
			
			Секция_СтрокаТаблицыПериод.Параметры.Период = Формат(СтрокаУровняПериода.Период, "ДФ='ММММ гггг ''г.'''");
			Секция_СтрокаТаблицыПериод.Параметры.СНД = СтрокаУровняПериода.СНД;
			Секция_СтрокаТаблицыПериод.Параметры.СНК = СтрокаУровняПериода.СНК;
			
			ПредставлениеОтчета.Вывести(Секция_СтрокаТаблицыПериод, 2);
			
			ОборотыПоПериодуДт = 0;
			ОборотыПоПериодуКт = 0;
			
			УровеньКорреспонденции = СтрокаУровняПериода.Строки;
			
			Для Каждого СтрокаУровняКорреспонденции Из УровеньКорреспонденции Цикл
				Если ЗначениеЗаполнено(СтрокаУровняКорреспонденции.КорСчет) Тогда
					Секция_СтрокаТаблицыКорСчет = ШаблонПредставления.ПолучитьОбласть("СтрокаТаблицыКорСчет" + ПостфиксЕдИзм);
					
					Секция_СтрокаТаблицыКорСчет.Параметры.КодКорСчета = СтрокаУровняКорреспонденции.КорСчет.Код;
					Секция_СтрокаТаблицыКорСчет.Параметры.ДО = СтрокаУровняКорреспонденции.ДО;
					Секция_СтрокаТаблицыКорСчет.Параметры.КО = СтрокаУровняКорреспонденции.КО;
					
					ПредставлениеОтчета.Вывести(Секция_СтрокаТаблицыКорСчет, 3);
					
				Иначе
					// Сведения по счету без корреспонденции. Используются для вывода оборотов, так как в запросе
					// итоги по уровню счета задваиваются из-за объединения таблиц со счетами и с корреспонденцией.
					ОборотыПоПериодуДт = СтрокаУровняКорреспонденции.ДО;
					ОборотыПоПериодуКт = СтрокаУровняКорреспонденции.КО;
					
				КонецЕсли;
				
			КонецЦикла;
			
			Секция_СтрокаТаблицыОбороты = ШаблонПредставления.ПолучитьОбласть("СтрокаТаблицыОбороты" + ПостфиксЕдИзм);
			
			Секция_СтрокаТаблицыОбороты.Параметры.ДО = ОборотыПоПериодуДт;
			Секция_СтрокаТаблицыОбороты.Параметры.КО = ОборотыПоПериодуКт;
			
			ПредставлениеОтчета.Вывести(Секция_СтрокаТаблицыОбороты, 2);
			
			Секция_СтрокаТаблицыСальдоКонечное = ШаблонПредставления.ПолучитьОбласть("СтрокаТаблицыСальдоКонечное" + ПостфиксЕдИзм);
			Секция_СтрокаТаблицыСальдоКонечное.Параметры.СКД = СтрокаУровняПериода.СКД;
			Секция_СтрокаТаблицыСальдоКонечное.Параметры.СКК = СтрокаУровняПериода.СКК;
			
			ПредставлениеОтчета.Вывести(Секция_СтрокаТаблицыСальдоКонечное, 2);
			
			ОборотыПоСчетуДт = ОборотыПоСчетуДт + ОборотыПоПериодуДт;
			ОборотыПоСчетуКт = ОборотыПоСчетуКт + ОборотыПоПериодуКт;
			
		КонецЦикла;
		
		Секция_СтрокаТаблицыОбороты = ШаблонПредставления.ПолучитьОбласть("СтрокаТаблицыОбороты" + ПостфиксЕдИзм);
		
		Секция_СтрокаТаблицыОбороты.Параметры.ДО = ОборотыПоСчетуДт;
		Секция_СтрокаТаблицыОбороты.Параметры.КО = ОборотыПоСчетуКт;
		
		ПредставлениеОтчета.Вывести(Секция_СтрокаТаблицыОбороты, 1);
		
		Секция_СтрокаТаблицыСальдоКонечное = ШаблонПредставления.ПолучитьОбласть("СтрокаТаблицыСальдоКонечное" + ПостфиксЕдИзм);
		Секция_СтрокаТаблицыСальдоКонечное.Параметры.СКД = СтрокаУровняСчета.СКД;
		Секция_СтрокаТаблицыСальдоКонечное.Параметры.СКК = СтрокаУровняСчета.СКК;
		
		ПредставлениеОтчета.Вывести(Секция_СтрокаТаблицыСальдоКонечное, 1);
		
	КонецЦикла;
	
	ПредставлениеОтчета.ЗакончитьАвтогруппировкуСтрок();
	
	Возврат ПредставлениеОтчета;
	
КонецФункции

Функция ПредставлениеАнализаДенежныхСредств(ШаблонПредставления, ДанныеОтчета, ПостфиксЕдИзм = "") Экспорт
	
	ПредставлениеОтчета = Новый ТабличныйДокумент;
	
	Если ШаблонПредставления.Области.Найти("Заголовок") <> Неопределено Тогда
		Секция_Заголовок = ШаблонПредставления.ПолучитьОбласть("Заголовок");
		ПредставлениеОтчета.Вывести(Секция_Заголовок);
		
		Секция_ПустаяСтрока = ШаблонПредставления.ПолучитьОбласть("ПустаяСтрока");
		ПредставлениеОтчета.Вывести(Секция_ПустаяСтрока);
		
	КонецЕсли;
	
	Секция_ШапкаТаблицы = ШаблонПредставления.ПолучитьОбласть("ШапкаТаблицы");
	ПредставлениеОтчета.Вывести(Секция_ШапкаТаблицы);
	
	ПредставлениеОтчета.НачатьАвтогруппировкуСтрок();
	
	УровеньСчета = ДанныеОтчета.Строки;
	Для Каждого СтрокаУровняСчета Из УровеньСчета Цикл
		Секция_СтрокаТаблицыСчет = ШаблонПредставления.ПолучитьОбласть("СтрокаТаблицыСчет" + ПостфиксЕдИзм);
		
		Секция_СтрокаТаблицыСчет.Параметры.КодСчета = СтрокаУровняСчета.Счет.Код;
		Секция_СтрокаТаблицыСчет.Параметры.СНД = СтрокаУровняСчета.СНД;
		Секция_СтрокаТаблицыСчет.Параметры.СНК = СтрокаУровняСчета.СНК;
		
		ПредставлениеОтчета.Вывести(Секция_СтрокаТаблицыСчет, 1);
		
		ОборотыПоСчетуДт = 0;
		ОборотыПоСчетуКт = 0;
		
		УровеньБанковскогоСчета = СтрокаУровняСчета.Строки;
		
		Для Каждого СтрокаУровняБанковскогоСчета Из УровеньБанковскогоСчета Цикл
			Секция_СтрокаТаблицыБанковскийСчет = ШаблонПредставления.ПолучитьОбласть("СтрокаТаблицыБанковскийСчет" + ПостфиксЕдИзм);
			
			Секция_СтрокаТаблицыБанковскийСчет.Параметры.БанковскийСчет = СтрокаУровняБанковскогоСчета.БанковскийСчет;
			Секция_СтрокаТаблицыБанковскийСчет.Параметры.СНД = СтрокаУровняБанковскогоСчета.СНД;
			Секция_СтрокаТаблицыБанковскийСчет.Параметры.СНК = СтрокаУровняБанковскогоСчета.СНК;
			
			ПредставлениеОтчета.Вывести(Секция_СтрокаТаблицыБанковскийСчет, 2);
			
			УровеньПериода = СтрокаУровняБанковскогоСчета.Строки;
			
			ОборотыПоБанковскомуСчетуДт = 0;
			ОборотыПоБанковскомуСчетуКт = 0;
			
			Для Каждого СтрокаУровняПериода Из УровеньПериода Цикл
				Секция_СтрокаТаблицыПериод = ШаблонПредставления.ПолучитьОбласть("СтрокаТаблицыПериод" + ПостфиксЕдИзм);
				
				Секция_СтрокаТаблицыПериод.Параметры.Период = Формат(СтрокаУровняПериода.Период, "ДФ='ММММ гггг ''г.'''");
				Секция_СтрокаТаблицыПериод.Параметры.СНД = СтрокаУровняПериода.СНД;
				Секция_СтрокаТаблицыПериод.Параметры.СНК = СтрокаУровняПериода.СНК;
				
				ПредставлениеОтчета.Вывести(Секция_СтрокаТаблицыПериод, 3);
				
				ОборотыПоПериодуДт = 0;
				ОборотыПоПериодуКт = 0;
				
				УровеньКорреспонденции = СтрокаУровняПериода.Строки;
				
				Для Каждого СтрокаУровняКорреспонденции Из УровеньКорреспонденции Цикл
					Если ЗначениеЗаполнено(СтрокаУровняКорреспонденции.КорСчет) Тогда
						Секция_СтрокаТаблицыКорСчет = ШаблонПредставления.ПолучитьОбласть("СтрокаТаблицыКорСчет" + ПостфиксЕдИзм);
						
						Секция_СтрокаТаблицыКорСчет.Параметры.КодКорСчета = СтрокаУровняКорреспонденции.КорСчет.Код;
						Секция_СтрокаТаблицыКорСчет.Параметры.ДО = СтрокаУровняКорреспонденции.ДО;
						Секция_СтрокаТаблицыКорСчет.Параметры.КО = СтрокаУровняКорреспонденции.КО;
						
						ПредставлениеОтчета.Вывести(Секция_СтрокаТаблицыКорСчет, 4);
						
					Иначе
						// Сведения по счету без корреспонденции. Используются для вывода оборотов, так как в запросе
						// итоги по уровню счета задваиваются из-за объединения таблиц со счетами и с корреспонденцией.
						ОборотыПоПериодуДт = СтрокаУровняКорреспонденции.ДО;
						ОборотыПоПериодуКт = СтрокаУровняКорреспонденции.КО;
						
					КонецЕсли;
					
				КонецЦикла;
				
				Секция_СтрокаТаблицыОбороты = ШаблонПредставления.ПолучитьОбласть("СтрокаТаблицыОбороты" + ПостфиксЕдИзм);
				
				Секция_СтрокаТаблицыОбороты.Параметры.ДО = ОборотыПоПериодуДт;
				Секция_СтрокаТаблицыОбороты.Параметры.КО = ОборотыПоПериодуКт;
				
				ПредставлениеОтчета.Вывести(Секция_СтрокаТаблицыОбороты, 3);
				
				Секция_СтрокаТаблицыСальдоКонечное = ШаблонПредставления.ПолучитьОбласть("СтрокаТаблицыСальдоКонечное" + ПостфиксЕдИзм);
				Секция_СтрокаТаблицыСальдоКонечное.Параметры.СКД = СтрокаУровняПериода.СКД;
				Секция_СтрокаТаблицыСальдоКонечное.Параметры.СКК = СтрокаУровняПериода.СКК;
				
				ПредставлениеОтчета.Вывести(Секция_СтрокаТаблицыСальдоКонечное, 3);
				
				ОборотыПоБанковскомуСчетуДт = ОборотыПоБанковскомуСчетуДт + ОборотыПоПериодуДт;
				ОборотыПоБанковскомуСчетуКт = ОборотыПоБанковскомуСчетуКт + ОборотыПоПериодуКт;
				
			КонецЦикла;
			
			Секция_СтрокаТаблицыОбороты = ШаблонПредставления.ПолучитьОбласть("СтрокаТаблицыОбороты" + ПостфиксЕдИзм);
			
			Секция_СтрокаТаблицыОбороты.Параметры.ДО = ОборотыПоБанковскомуСчетуДт;
			Секция_СтрокаТаблицыОбороты.Параметры.КО = ОборотыПоБанковскомуСчетуКт;
			
			ПредставлениеОтчета.Вывести(Секция_СтрокаТаблицыОбороты, 2);
			
			Секция_СтрокаТаблицыСальдоКонечное = ШаблонПредставления.ПолучитьОбласть("СтрокаТаблицыСальдоКонечное" + ПостфиксЕдИзм);
			Секция_СтрокаТаблицыСальдоКонечное.Параметры.СКД = СтрокаУровняБанковскогоСчета.СКД;
			Секция_СтрокаТаблицыСальдоКонечное.Параметры.СКК = СтрокаУровняБанковскогоСчета.СКК;
			
			ПредставлениеОтчета.Вывести(Секция_СтрокаТаблицыСальдоКонечное, 2);
			
			ОборотыПоСчетуДт = ОборотыПоСчетуДт + ОборотыПоБанковскомуСчетуДт;
			ОборотыПоСчетуКт = ОборотыПоСчетуКт + ОборотыПоБанковскомуСчетуКт;
			
		КонецЦикла;
		
		Секция_СтрокаТаблицыОбороты = ШаблонПредставления.ПолучитьОбласть("СтрокаТаблицыОбороты" + ПостфиксЕдИзм);
		
		Секция_СтрокаТаблицыОбороты.Параметры.ДО = ОборотыПоСчетуДт;
		Секция_СтрокаТаблицыОбороты.Параметры.КО = ОборотыПоСчетуКт;
		
		ПредставлениеОтчета.Вывести(Секция_СтрокаТаблицыОбороты, 1);
		
		Секция_СтрокаТаблицыСальдоКонечное = ШаблонПредставления.ПолучитьОбласть("СтрокаТаблицыСальдоКонечное" + ПостфиксЕдИзм);
		Секция_СтрокаТаблицыСальдоКонечное.Параметры.СКД = СтрокаУровняСчета.СКД;
		Секция_СтрокаТаблицыСальдоКонечное.Параметры.СКК = СтрокаУровняСчета.СКК;
		
		ПредставлениеОтчета.Вывести(Секция_СтрокаТаблицыСальдоКонечное, 1);
		
	КонецЦикла;
	
	ПредставлениеОтчета.ЗакончитьАвтогруппировкуСтрок();
	
	Возврат ПредставлениеОтчета;
	
КонецФункции

// Выполняет оправку отчетности в банк из регламентного задания.
//
Процедура ОтправитьОтчетностьВБанк(Параметры) Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.ОтправкаОтчетностиВБанк);
	
	Организация = Параметры;
	ПодготовитьИОтправитьОтчетыВБанк(Организация, Ложь);
	ПодготовитьИОтправитьОтчетыВБанкПоПлану(Организация);
	
КонецПроцедуры

// Выполняет оправку отчетности в банк по подключенным видам отчетов без учета отправленных ранее.
//
Процедура ОтправитьОтчетностьВБанкПринудительно(Организация) Экспорт
	
	ПодготовитьИОтправитьОтчетыВБанк(Организация, Истина);
	ПодготовитьИОтправитьОтчетыВБанкПоПлану(Организация, Истина);
	
КонецПроцедуры

// Определяет статус отправленного отчета по идентификатору документа.
//
// Параметры:
//  ИдентификаторДокумента - Строка - идентификатор документа, переданного на сервер.
//  Параметры - Структура - параметры запроса к серверу. Содержит поля:
//      * ПараметрыКлиента - Структура - содержит параметры клиентского приложения.
//        ** ТипПлатформы - Строка - тип клиентского приложения;
//        ** ВерсияОС - Строка - версия ОС на клиенте;
//
// Возвращаемое значение:
//  Структура:
//     Статус - Строка - статус отправленного отчета (Доставлено, Отклонено, Отправлено).
//                       Может быть пустым, если заполнены ЗначениеОшибки или ПредставлениеОшибки 
//     ЗначениеОшибки - Строка - Текст ошибки ошибки для записи в журнал регистрации или др. служебных целей. 
//     ПредставлениеОшибки - Строка - Текст ошибки для отображения пользователю.
//
Функция ПолучитьСтатусОтчетаССервера(ИдентификаторДокумента, Параметры) Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить("Статус", "");
	Результат.Вставить("ЗначениеОшибки", "");
	Результат.Вставить("ПредставлениеОшибки", "");
	
	ВремФайл = ПолучитьИмяВременногоФайла();
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.ОткрытьФайл(ВремФайл);
	
	ЗаписьJSON.ЗаписатьНачалоОбъекта();
	ЗаписьJSON.ЗаписатьИмяСвойства("reportGuid");
	ЗаписьJSON.ЗаписатьЗначение(ИдентификаторДокумента);
	
	ДобавитьДополнительныеПараметры(ЗаписьJSON, Параметры.ПараметрыКлиента);
	
	ЗаписьJSON.ЗаписатьКонецОбъекта();
	ЗаписьJSON.Закрыть();
	
	Данные = Новый ДвоичныеДанные(ВремФайл);
	
	Попытка
		УдалитьФайлы(ВремФайл);
	Исключение
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Отчетность в банки. Ошибка удаления временного файла';
										|en = 'Отчетность в банки. Ошибка удаления временного файла'"),
			УровеньЖурналаРегистрации.Ошибка, , , ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/json");
	
	Результат = ОтправитьЗапросНаСервер(
		"https://reportbank.1c.ru", "/api/rest/report/checkStatus/", Заголовки, Данные, Истина, 30);
	
	Успех = Ложь;
	ТекстСообщения = "";
	ТекстОшибки = "";
	
	Если Результат.Статус Тогда
		ДанныеОтвета = ДанныеИзСтрокиJSON(Результат.Тело);
		Статус = ДанныеОтвета.status;
		Если Статус = "DELIVERED" Тогда
			Статус = НСтр("ru = 'Доставлено';
							|en = 'Доставлено'");
		ИначеЕсли Статус = "REJECTED" Тогда
			Статус = НСтр("ru = 'Отклонено';
							|en = 'Отклонено'");
		ИначеЕсли Статус = "SENT" Тогда
			Статус = НСтр("ru = 'Отправлено';
							|en = 'Отправлено'");
		КонецЕсли;
		
	Иначе
		Если ЗначениеЗаполнено(Результат.Тело) Тогда
			ДанныеОтвета = ДанныеИзСтрокиJSON(Результат.Тело);
			Если НЕ Данные = Неопределено Тогда
				Если ДанныеОтвета.Свойство("errorText") Тогда
					ТекстСообщения = ДанныеОтвета.errorText;
				Иначе
					ТекстСообщения = НСтр("ru = 'Получена неизвестная ошибка с сервиса https://reportbank.1c.ru.';
											|en = 'Получена неизвестная ошибка с сервиса https://reportbank.1c.ru.'");
				КонецЕсли;
			КонецЕсли;
			ТекстОшибки = НСтр("ru = 'Ошибка получения статуса отчета через сервис https://reportbank.1c.ru.
								|Код состояния: %1
								|%2';
								|en = 'Ошибка получения статуса отчета через сервис https://reportbank.1c.ru.
								|Код состояния: %1
								|%2'");
			ТекстОшибки = СтрШаблон(ТекстОшибки, Результат.КодСостояния, Результат.Тело);
		Иначе
			ТекстСообщения = Результат.СообщениеОбОшибке;
			ТекстОшибки = НСтр("ru = 'Ошибка получения статуса отчета через сервис https://reportbank.1c.ru.
								|Код состояния: %1';
								|en = 'Ошибка получения статуса отчета через сервис https://reportbank.1c.ru.
								|Код состояния: %1'");
			ТекстОшибки = СтрШаблон(ТекстОшибки, Результат.КодСостояния);
		КонецЕсли;
		
	КонецЕсли;
	
	Результат = Новый Структура();
	Результат.Вставить("Статус", Статус);
	Результат.Вставить("ЗначениеОшибки", ТекстОшибки);
	Результат.Вставить("ПредставлениеОшибки", ТекстСообщения);
	
	Возврат Результат;
	
КонецФункции

// Возвращает счета, по которым производится автоматическая отправка ОСВ по счетам в банки.
//
// Возвращаемое значение:
//   Массив из ПланСчетовСсылка - счета, по которым производится автоматическая отправка ОСВ по счетам в банки
//
Функция СчетаОСВПоСчетам() Экспорт
	
	СчетаОСВПоСчетам = Новый Массив;
	
	ОтчетностьВБанкиСлужебныйПереопределяемый.ЗаполнитьСчетаОСВПоСчетам(СчетаОСВПоСчетам);
	
	Возврат СчетаОСВПоСчетам;
	
КонецФункции

// Возвращает счета, по которым производится автоматическая отправка ОСВ по счетам в банки.
//
// Возвращаемое значение:
//  Массив из ПланСчетовСсылка - счета, по которым производится автоматическая отправка ОСВ по счетам в банки
//
Функция СчетаАнализовСчетов() Экспорт
	
	СчетаАнализовСчетов = Новый Массив;
	
	ОтчетностьВБанкиСлужебныйПереопределяемый.ЗаполнитьСчетаАнализовСчетов(СчетаАнализовСчетов);
	
	Возврат СчетаАнализовСчетов;
	
КонецФункции

// Возвращает счет бухгалтерского учета по коду счета
//
// Параметры:
//   КодСчета - Строка - код счета
//
// Возвращаемое значение:
//   ПланСчетовСсылка - ссылка на счет бухгалтерского учета
//
Функция СчетБухУчета(КодСчета) Экспорт
	
	Счет = Неопределено;
	ОтчетностьВБанкиСлужебныйПереопределяемый.УстановитьСчетУчетаПоКоду(КодСчета, Счет);
	
	Возврат Счет;
	
КонецФункции

// Возвращает все счета, где присутствует субконто "Банковские счета".
//
// Возвращаемое значение:
//	 СписокСчетов - Массив - массив счетов.
//
Функция ПолучитьСчетаССубконтоБанковскиеСчета() Экспорт
	
	СписокСчетов = Новый Массив;
	
	ОтчетностьВБанкиСлужебныйПереопределяемый.ПолучитьСчетаССубконтоБанковскиеСчета(СписокСчетов);
	
	Возврат СписокСчетов;
	
КонецФункции

// Возвращает префикс для сохраненного отчета "Карточка счета".
// 
// Параметры:
//   Счет - ПланСчетовСсылка.Хозрасчетный - счет, по которому формируется отчет.
//   НачалоПериода - Дата - дата начала формирования отчета.
//   КонецПериода - Дата - дата окончания формирования отчета.
// 
//  Возвращаемое значение:
//    Строка - Префикс карточки счета
//
Функция ПрефиксКарточкиСчета(Счет, НачалоПериода, КонецПериода) Экспорт
	
	Префикс = "";
	
	ОтчетностьВБанкиСлужебныйПереопределяемый.ПрефиксКарточкиСчета(Префикс, Счет, НачалоПериода, КонецПериода);
	
	Возврат Префикс;
	
КонецФункции

// Возвращает IP и MAC адрес сервера приложения.
//
// Возвращаемое значение:
//   Структура:
//     * IPАдрес - Строка
//     * MACАдрес - Строка
//
Функция ЦифровойСлед() Экспорт
	
	ЦифровойСлед = Новый Структура("IPАдрес, MACАдрес", "", "");
	
	//@skip-check undefined-function
	ВсеАдаптеры = ПолучитьИнформациюОСетевыхАдаптерах();
	
	Если ВсеАдаптеры.Количество() > 0 Тогда
		ТекущийАдаптер = ВсеАдаптеры[0];
		Для Каждого СетевойАдаптер Из ВсеАдаптеры Цикл
			Если СетевойАдаптер.ЭтоТекущийАдаптер = Истина Тогда
				ТекущийАдаптер = СетевойАдаптер;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		ЦифровойСлед.IPАдрес = ТекущийАдаптер.IPAddress;
		ЦифровойСлед.MACАдрес = ТекущийАдаптер.MACAddress;
		
	КонецЕсли;
	
	Возврат ЦифровойСлед;
	
КонецФункции

// Возращает варианты отправки счета и его субсчетов в составе отчетности.
//
// Параметры:
//   ОтправляемыйСчет - Строка - код головного счета.
//
// Возвращаемое значение:
//   ТаблицаЗначений:
//     * Счета - Массив - счет или его субсчета, использующие комбинацию субконто, указаннюу в колонке "Субконто";
//     * Субконто - Массив - комбинация субконто счете или его субсчетах;
//     * Количественный - Булево - признак отражения количества на счете или его субсчетах;
//     * РазвернутоеСальдо - Булево - признак развернутого отражения итогового сальдо по счету.
//
Функция ПараметрыОтправкиСчета(ОтправляемыйСчет) Экспорт
	
	ПараметрыСчета = Новый ТаблицаЗначений;
	ПараметрыСчета.Колонки.Добавить("Счета");
	ПараметрыСчета.Колонки.Добавить("Субконто");
	ПараметрыСчета.Колонки.Добавить("Количественный", Новый ОписаниеТипов("Булево"));
	ПараметрыСчета.Колонки.Добавить("РазвернутоеСальдо", Новый ОписаниеТипов("Булево"));
	
	СтрокаПараметров = ПараметрыСчета.Добавить(); // Для счетов отправляемых без аналитики
	СтрокаПараметров.Счета    = Новый Массив;
	СтрокаПараметров.Субконто = Новый Массив;
	
	ОтчетностьВБанкиСлужебныйПереопределяемый.УстановитьПараметрыСчета(ОтправляемыйСчет, ПараметрыСчета);
	
	Возврат ПараметрыСчета;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция КодЯзыкаИнтерфейсаКонфигурации()

	Язык = ТекущийЯзык();
	Если Язык = Неопределено Тогда
		// Для пользователя информационной базы не указан язык.
		Возврат КодОсновногоЯзыкаИнтерфейсаКонфигурации();
	КонецЕсли;

	КодЯзыкаВМетаданных = ?(ТипЗнч(Язык) = Тип("Строка"), Язык, Язык.КодЯзыка);
	Возврат КодЯзыкаВМетаданных;

КонецФункции

Функция КодОсновногоЯзыкаИнтерфейсаКонфигурации()

	КодЯзыкаВМетаданных = Метаданные.ОсновнойЯзык.КодЯзыка;
	
	Возврат КодЯзыкаВМетаданных;

КонецФункции

Функция СоединениеССервером(АдресСервера, Таймаут)
	
	Перем ЗащищенноеСоединение;
	
	Адрес = "";
	Протокол = "";
	
	ОпределитьПараметрыСайта(АдресСервера, ЗащищенноеСоединение, Адрес, Протокол);
	Прокси = СформироватьПрокси(Протокол);
	
	Соединение = Новый HTTPСоединение(Адрес, , , ,Прокси, Таймаут, ЗащищенноеСоединение);
	
	Возврат Соединение;
	
КонецФункции

Функция СформироватьПрокси(Протокол)
	
	// НастройкаПроксиСервера - Соответствие:
	//  ИспользоватьПрокси - использовать ли прокси-сервер;
	//  НеИспользоватьПроксиДляЛокальныхАдресов - использовать ли прокси-сервер для локальных адресов;
	//  ИспользоватьСистемныеНастройки - использовать ли системные настройки прокси-сервера;
	//  Сервер       - адрес прокси-сервера;
	//  Порт         - порт прокси-сервера;
	//  Пользователь - имя пользователя для авторизации на прокси-сервере;
	//  Пароль       - пароль пользователя.
	НастройкаПроксиСервера = ПолучениеФайловИзИнтернета.НастройкиПроксиНаСервере();
	Если НастройкаПроксиСервера <> Неопределено Тогда
		ИспользоватьПрокси = НастройкаПроксиСервера.Получить("ИспользоватьПрокси");
		ИспользоватьСистемныеНастройки = НастройкаПроксиСервера.Получить("ИспользоватьСистемныеНастройки");
		Если ИспользоватьПрокси Тогда
			Если ИспользоватьСистемныеНастройки Тогда
				// Системные настройки прокси-сервера.
				Прокси = Новый ИнтернетПрокси(Истина);
			Иначе
				// Ручные настройки прокси-сервера.
				Прокси = Новый ИнтернетПрокси;
				Прокси.Установить(Протокол, НастройкаПроксиСервера["Сервер"], НастройкаПроксиСервера["Порт"],
					НастройкаПроксиСервера["Пользователь"], НастройкаПроксиСервера["Пароль"]);
				Прокси.НеИспользоватьПроксиДляЛокальныхАдресов = НастройкаПроксиСервера["НеИспользоватьПроксиДляЛокальныхАдресов"];
			КонецЕсли;
		Иначе
			// Не использовать прокси-сервер.
			Прокси = Новый ИнтернетПрокси(Ложь);
		КонецЕсли;
	Иначе
		Прокси = Неопределено;
	КонецЕсли;
	
	Возврат Прокси;
	
КонецФункции

Процедура ОпределитьПараметрыСайта(Знач АдресСайта, ЗащищенноеСоединение, Адрес, Протокол)
	
	АдресСайта = СокрЛП(АдресСайта);
	
	АдресСайта = СтрЗаменить(АдресСайта, "\", "/");
	АдресСайта = СтрЗаменить(АдресСайта, " ", "");
	
	Если НРег(Лев(АдресСайта, 7)) = "http://" Тогда
		Протокол = "http";
		Адрес = Сред(АдресСайта,8);
		ЗащищенноеСоединение = Неопределено;
	ИначеЕсли НРег(Лев(АдресСайта, 8)) = "https://" Тогда
		Протокол =  "https";
		Адрес = Сред(АдресСайта,9);
		
		СертификатыУдостоверяющихЦентров = Неопределено;
		Если НЕ ОбщегоНазначения.ЭтоLinuxСервер() И НЕ ОбщегоНазначения.РазделениеВключено() Тогда
			// Ошибка фреша при работе с веб сервисами. Не работает проверка доверенных сертификатов при установке соединения.
			СертификатыУдостоверяющихЦентров = Новый СертификатыУдостоверяющихЦентровWindows;
		КонецЕсли;
		
		ЗащищенноеСоединение = ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение( , СертификатыУдостоверяющихЦентров);
		
	КонецЕсли;
	
КонецПроцедуры

Функция РасшифровкаКодаСостоянияHTTP(КодСостояния)
	
	СоответствиеКодов = Новый Соответствие;
	СоответствиеКодов.Вставить(400, НСтр("ru = 'Сервер обнаружил в запросе клиента синтаксическую ошибку.';
										|en = 'Сервер обнаружил в запросе клиента синтаксическую ошибку.'"));
	СоответствиеКодов.Вставить(401, НСтр("ru = 'Неверные данные аутентификации.';
										|en = 'Неверные данные аутентификации.'"));
	СоответствиеКодов.Вставить(403, НСтр("ru = 'У клиента нет доступа к ресурсу.';
										|en = 'У клиента нет доступа к ресурсу.'"));
	СоответствиеКодов.Вставить(404, НСтр("ru = 'На сервере нет ресурса по указанному URI.';
										|en = 'На сервере нет ресурса по указанному URI.'"));
	СоответствиеКодов.Вставить(405, НСтр("ru = 'Указанный клиентом метод нельзя применить к текущему ресурсу.';
										|en = 'Указанный клиентом метод нельзя применить к текущему ресурсу.'"));
	СоответствиеКодов.Вставить(406, НСтр("ru = 'Запрошенный URI не может удовлетворить переданным в заголовке характеристикам.';
										|en = 'Запрошенный URI не может удовлетворить переданным в заголовке характеристикам.'"));
	СоответствиеКодов.Вставить(407, НСтр("ru = 'Для доступа к серверу требуется аутентификация для прокси-сервера.';
										|en = 'Для доступа к серверу требуется аутентификация для прокси-сервера.'"));
	СоответствиеКодов.Вставить(408, НСтр("ru = 'Время ожидания сервером передачи от клиента истекло.';
										|en = 'Время ожидания сервером передачи от клиента истекло.'"));
	СоответствиеКодов.Вставить(409, НСтр("ru = 'Запрос не может быть выполнен из-за конфликтного обращения к ресурсу.';
										|en = 'Запрос не может быть выполнен из-за конфликтного обращения к ресурсу.'"));
	СоответствиеКодов.Вставить(410, НСтр("ru = 'Ресурс был удалён и теперь недоступен.';
										|en = 'Ресурс был удалён и теперь недоступен.'"));
	СоответствиеКодов.Вставить(411, НСтр("ru = 'Не указан объем передаемых данных в заголовке.';
										|en = 'Не указан объем передаемых данных в заголовке.'"));
	СоответствиеКодов.Вставить(412, НСтр("ru = 'Ни одно из условных полей заголовка запроса не было выполнено.';
										|en = 'Ни одно из условных полей заголовка запроса не было выполнено.'"));
	СоответствиеКодов.Вставить(413, НСтр("ru = 'Сервер отказался обработать запрос по причине слишком большого размера тела запроса.';
										|en = 'Сервер отказался обработать запрос по причине слишком большого размера тела запроса.'"));
	СоответствиеКодов.Вставить(414, НСтр("ru = 'Сервер не может обработать запрос из-за слишком длинного указанного URL.';
										|en = 'Сервер не может обработать запрос из-за слишком длинного указанного URL.'"));
	СоответствиеКодов.Вставить(415, НСтр("ru = 'Сервер отказывается работать с указанным типом данных при данном методе.';
										|en = 'Сервер отказывается работать с указанным типом данных при данном методе.'"));
	СоответствиеКодов.Вставить(422, НСтр("ru = 'Имеется логическая ошибка, из-за которой невозможно произвести операцию.';
										|en = 'Имеется логическая ошибка, из-за которой невозможно произвести операцию.'"));
	СоответствиеКодов.Вставить(423, НСтр("ru = 'Целевой ресурс из запроса заблокирован от применения к нему указанного метода.';
										|en = 'Целевой ресурс из запроса заблокирован от применения к нему указанного метода.'"));
	СоответствиеКодов.Вставить(426, НСтр("ru = 'Клиенту на необходимость обновить протокол.';
										|en = 'Клиенту на необходимость обновить протокол.'"));
	СоответствиеКодов.Вставить(429, НСтр("ru = 'Клиент попытался отправить слишком много запросов за короткое время.';
										|en = 'Клиент попытался отправить слишком много запросов за короткое время.'"));
	СоответствиеКодов.Вставить(431, НСтр("ru = 'Превышена допустимая длина заголовков.';
										|en = 'Превышена допустимая длина заголовков.'"));
	СоответствиеКодов.Вставить(434, НСтр("ru = 'Запрашиваемый адрес недоступен.';
										|en = 'Запрашиваемый адрес недоступен.'"));
	СоответствиеКодов.Вставить(449, НСтр("ru = 'Поступило недостаточно информации.';
										|en = 'Поступило недостаточно информации.'"));
	СоответствиеКодов.Вставить(451, НСтр("ru = 'Доступ к ресурсу закрыт по юридическим причинам.';
										|en = 'Доступ к ресурсу закрыт по юридическим причинам.'"));
	
	СоответствиеКодов.Вставить(500, НСтр("ru = 'Внутренняя ошибка сервера.';
										|en = 'Внутренняя ошибка сервера.'"));
	СоответствиеКодов.Вставить(501, НСтр("ru = 'Сервер не поддерживает возможностей, необходимых для обработки запроса.';
										|en = 'Сервер не поддерживает возможностей, необходимых для обработки запроса.'"));
	СоответствиеКодов.Вставить(502, НСтр("ru = 'Сервер, выступая в роли шлюза или прокси-сервера, получил недействительное ответное сообщение от вышестоящего сервера.';
										|en = 'Сервер, выступая в роли шлюза или прокси-сервера, получил недействительное ответное сообщение от вышестоящего сервера.'"));
	СоответствиеКодов.Вставить(503, НСтр("ru = 'Сервер временно не имеет возможности обрабатывать запросы по техническим причинам.';
										|en = 'Сервер временно не имеет возможности обрабатывать запросы по техническим причинам.'"));
	СоответствиеКодов.Вставить(504, НСтр("ru = 'Сервер в роли шлюза или прокси-сервера не дождался ответа от вышестоящего сервера для завершения текущего запроса.';
										|en = 'Сервер в роли шлюза или прокси-сервера не дождался ответа от вышестоящего сервера для завершения текущего запроса.'"));
	СоответствиеКодов.Вставить(505, НСтр("ru = 'Сервер не поддерживает указанную в запросе версию протокола HTTP.';
										|en = 'Сервер не поддерживает указанную в запросе версию протокола HTTP.'"));
	СоответствиеКодов.Вставить(507, НСтр("ru = 'Не хватает места для выполнения текущего запроса.';
										|en = 'Не хватает места для выполнения текущего запроса.'"));
	СоответствиеКодов.Вставить(510, НСтр("ru = 'На сервере отсутствует расширение, которое желает использовать клиент.';
										|en = 'На сервере отсутствует расширение, которое желает использовать клиент.'"));
	СоответствиеКодов.Вставить(511, НСтр("ru = 'Необходимо авторизоваться в сети провайдера.';
										|en = 'Необходимо авторизоваться в сети провайдера.'"));
	
	Возврат СоответствиеКодов.Получить(КодСостояния);
	
КонецФункции

Процедура ДобавитьПараметр(ЗаписьJSON, НазваниеПараметра, ЗначениеПараметра)
	
	ЗаписьJSON.ЗаписатьНачалоОбъекта();
	ЗаписьJSON.ЗаписатьИмяСвойства("key");
	ЗаписьJSON.ЗаписатьЗначение(НазваниеПараметра);
	ЗаписьJSON.ЗаписатьИмяСвойства("value");
	ЗаписьJSON.ЗаписатьЗначение(ЗначениеПараметра);
	ЗаписьJSON.ЗаписатьКонецОбъекта();
	
КонецПроцедуры

Функция КодВидаСчета(Счет)
	
	КодВидаСчета = "АП";
	
	Если Счет.Вид = ВидСчета.Активный Тогда
		КодВидаСчета = "А";
	ИначеЕсли Счет.Вид = ВидСчета.Пассивный Тогда
		КодВидаСчета = "П";
	КонецЕсли;
	
	Возврат КодВидаСчета;
	
КонецФункции

#Область ОтправкаОтчетностиИзРегламентногоЗадания

Процедура ПриОпределенииПсевдонимовОбработчиков(СоответствиеИменПсевдонимам) Экспорт
	
	СоответствиеИменПсевдонимам.Вставить(Метаданные.РегламентныеЗадания.ОтправкаОтчетностиВБанк.ИмяМетода);
	
КонецПроцедуры

Процедура ПриОпределенииНастроекРегламентныхЗаданий(Настройки) Экспорт
	
	Настройка = Настройки.Добавить();
	Настройка.РегламентноеЗадание = Метаданные.РегламентныеЗадания.ОтправкаОтчетностиВБанк;
	Настройка.ДоступноВПодчиненномУзлеРИБ = Ложь;
	Настройка.РаботаетСВнешнимиРесурсами = Истина;
	Настройка.Параметризуется = Истина;
	
КонецПроцедуры

Процедура ПодготовитьИОтправитьОтчетыВБанк(Организация, Принудительно)
	
	МоментОтправки = ТекущаяДатаСеанса();
	
	ПараметрыОтчетов = ПараметрыПредставляемыхОтчетов(Организация, МоментОтправки, Принудительно);
	
	Если НЕ Принудительно Тогда
		// Исключение ранее оправленных отчетов из списка отправки.
		Для Каждого ОтправляемыйОтчет Из ПараметрыОтчетов Цикл
			ВидОтчета = ОтправляемыйОтчет.Ключ;
			ПараметрыОтчета = ОтправляемыйОтчет.Значение;
			
			ЗаписьЖурналаПереданнойОтчетности = РегистрыСведений.ЖурналПереданнойОтчетностиВБанк.СоздатьМенеджерЗаписи();
			ЗаписьЖурналаПереданнойОтчетности.Организация = Организация;
			ЗаписьЖурналаПереданнойОтчетности.ВидОтчета = ВидОтчета;
			ЗаписьЖурналаПереданнойОтчетности.НачалоПериода = ПараметрыОтчета.ПериодРегистрацииОтправки.НачалоПериода;
			ЗаписьЖурналаПереданнойОтчетности.КонецПериода = ПараметрыОтчета.ПериодРегистрацииОтправки.КонецПериода;
			
			ЗаписьЖурналаПереданнойОтчетности.Прочитать();
			Если ЗаписьЖурналаПереданнойОтчетности.Выбран() Тогда
				ПараметрыОтчета.Отправлять = Ложь;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Если все отчеты отправлены, то пустой пакет отправлять не требуется.
	ТребуетсяОтправитьПакет = Ложь;
	Для Каждого ОтправляемыйОтчет Из ПараметрыОтчетов Цикл
		Если ОтправляемыйОтчет.Значение.Отправлять Тогда
			ТребуетсяОтправитьПакет = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ ТребуетсяОтправитьПакет Тогда
		Возврат;
	КонецЕсли;
	
	// Формирование и отправка пакета.
	ПараметрыВыгрузки = ПараметрыЭлектронногоПредставления(Организация, ПараметрыОтчетов, МоментОтправки);
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Организация", Организация);
	ПараметрыОтправки.Вставить("ИмяПакета", ПараметрыВыгрузки.ИдФайл);
	ПараметрыОтправки.Вставить("ДатаОтправки", МоментОтправки);
	ПараметрыОтправки.Вставить("ДатаНачала", ПараметрыВыгрузки.НачалоПериода);
	ПараметрыОтправки.Вставить("ДатаОкончания", ПараметрыВыгрузки.КонецПериода);
	
	ЗаписьЖурналаОтправки = РегистрыСведений.ЖурналПередачиОтчетностиВБанк.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(ЗаписьЖурналаОтправки, ПараметрыОтправки);
	ЗаписьЖурналаОтправки.Состояние = НСтр("ru = 'Начато';
											|en = 'Начато'");
	Если НЕ Принудительно Тогда
		ЗаписьЖурналаОтправки.Записать();
	КонецЕсли;
	
	ПакетОтправки = ПакетДляНеинтерактивнойОтправкиВБанк(ПараметрыВыгрузки);
	ДвоичныеДанныеПакета = ПакетОтправки.ДвоичныеДанныеПакета;
	ИмяФайлаПакета = ПакетОтправки.ИмяФайлаПакета;
	
	РезультатОтправки = ОтправитьОтчетностьВБанкБезПодписи(Организация, ДвоичныеДанныеПакета, ИмяФайлаПакета);
	
	ЗаписьЖурналаОтправки.ИдентификаторДокумента = РезультатОтправки.ИдентификаторДокумента;
	ЗаписьЖурналаОтправки.Состояние = РезультатОтправки.Статус;
	ЗаписьЖурналаОтправки.Записать();
	
	СтатусОшибка = НСтр("ru = 'Ошибка';
						|en = 'Ошибка'");
	Если РезультатОтправки.Статус = СтатусОшибка Тогда
		ВызватьИсключение НСтр("ru = 'Ошибка при передаче пакета с отчетностью на сервер.';
								|en = 'Ошибка при передаче пакета с отчетностью на сервер.'");
	КонецЕсли;
	
	// Регистрация отправленных отчетов для предотвращения повторной отправки.
	Для Каждого ОтправленныйОтчет Из ПараметрыОтчетов Цикл
		ВидОтчета = ОтправленныйОтчет.Ключ;
		ПараметрыОтчета = ОтправленныйОтчет.Значение;
		
		Если ПараметрыОтчета.Отправлять Тогда
			ЗаписьЖурналаПереданнойОтчетности = РегистрыСведений.ЖурналПереданнойОтчетностиВБанк.СоздатьМенеджерЗаписи();
			ЗаписьЖурналаПереданнойОтчетности.Организация = Организация;
			ЗаписьЖурналаПереданнойОтчетности.ВидОтчета = ВидОтчета;
			ЗаписьЖурналаПереданнойОтчетности.НачалоПериода = ПараметрыОтчета.ПериодРегистрацииОтправки.НачалоПериода;
			ЗаписьЖурналаПереданнойОтчетности.КонецПериода = ПараметрыОтчета.ПериодРегистрацииОтправки.КонецПериода;
			
			ЗаписьЖурналаПереданнойОтчетности.ДатаОтправки = МоментОтправки;
			ЗаписьЖурналаПереданнойОтчетности.ИмяПакета = ПакетОтправки.ИмяФайлаПакета;
			
			ЗаписьЖурналаПереданнойОтчетности.Записать(Истина);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПодготовитьИОтправитьОтчетыВБанкПоПлану(Организация, ОтправлятьВсе = Ложь)
	
	ГСЧ = Новый ГенераторСлучайныхЧисел(ТекущаяУниверсальнаяДатаВМиллисекундах());
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЖурналПередачиОтчетностиВБанк.ИмяПакета КАК ИмяПакета,
		|	ЖурналПередачиОтчетностиВБанк.ДатаОтправки КАК ДатаОтправки
		|ИЗ
		|	РегистрСведений.ЖурналПередачиОтчетностиВБанк КАК ЖурналПередачиОтчетностиВБанк
		|ГДЕ
		|	ЖурналПередачиОтчетностиВБанк.Организация = &Организация
		|	И ЖурналПередачиОтчетностиВБанк.Состояние = ""Запланирована""
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЖурналПередачиОтчетностиВБанк.ДатаОтправки УБЫВ";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ЗапланированнаяОтправка = РегистрыСведений.ЖурналПередачиОтчетностиВБанк.СоздатьМенеджерЗаписи();
		ЗапланированнаяОтправка.Организация = Организация;
		ЗапланированнаяОтправка.ИмяПакета = Выборка.ИмяПакета;
		
		МоментОтправки = Выборка.ДатаОтправки
		               + ГСЧ.СлучайноеЧисло(32400, 61200); // с 9:00 до 17:00
		
		Принудительно = Истина;
		ПараметрыОтчетов = ПараметрыПредставляемыхОтчетов(Организация, МоментОтправки, Принудительно);
		
		// Отчеты не входят в ретроспективную отправку.
		ПараметрыОтчетов.БанковскиеОперации.Отправлять = Ложь;
		ПараметрыОтчетов.ОСВ.Отправлять = Ложь;
		
		Для Каждого ОтправляемыйОтчет Из ПараметрыОтчетов Цикл
			ВидОтчета = ОтправляемыйОтчет.Ключ;
			ПараметрыОтчета = ОтправляемыйОтчет.Значение;
			
			Если НЕ ПараметрыОтчета.Отправлять Тогда
				Продолжить;
			КонецЕсли;
			
			ЗаписьЖурналаПереданнойОтчетности = РегистрыСведений.ЖурналПереданнойОтчетностиВБанк.СоздатьМенеджерЗаписи();
			ЗаписьЖурналаПереданнойОтчетности.Организация = Организация;
			ЗаписьЖурналаПереданнойОтчетности.ВидОтчета = ВидОтчета;
			ЗаписьЖурналаПереданнойОтчетности.НачалоПериода = ПараметрыОтчета.ПериодРегистрацииОтправки.НачалоПериода;
			ЗаписьЖурналаПереданнойОтчетности.КонецПериода = ПараметрыОтчета.ПериодРегистрацииОтправки.КонецПериода;
			
			ЗаписьЖурналаПереданнойОтчетности.Прочитать();
			Если ЗаписьЖурналаПереданнойОтчетности.Выбран() Тогда
				ПараметрыОтчета.Отправлять = Ложь;
			КонецЕсли;
			
		КонецЦикла;
		
		Если НЕ ПараметрыОтчетов.ОСВПоСчетам.Отправлять
		   И НЕ ПараметрыОтчетов.АнализСчетов.Отправлять Тогда
			// Отчеты были отправлены ранее.
			ЗапланированнаяОтправка.Удалить();
			Продолжить;
		КонецЕсли;
		
		// Формирование и отправка пакета.
		ПараметрыВыгрузки = ПараметрыЭлектронногоПредставления(
			Организация, ПараметрыОтчетов, МоментОтправки);
		
		ПараметрыОтправки = Новый Структура;
		ПараметрыОтправки.Вставить("Организация", Организация);
		ПараметрыОтправки.Вставить("ИмяПакета", ПараметрыВыгрузки.ИдФайл);
		ПараметрыОтправки.Вставить("ДатаОтправки", МоментОтправки);
		ПараметрыОтправки.Вставить("ДатаНачала", ПараметрыВыгрузки.НачалоПериода);
		ПараметрыОтправки.Вставить("ДатаОкончания", ПараметрыВыгрузки.КонецПериода);
		
		ПакетОтправки = ПакетДляНеинтерактивнойОтправкиВБанк(ПараметрыВыгрузки);
		ДвоичныеДанныеПакета = ПакетОтправки.ДвоичныеДанныеПакета;
		ИмяФайлаПакета = ПакетОтправки.ИмяФайлаПакета;
		
		РезультатОтправки = ОтправитьОтчетностьВБанкБезПодписи(Организация, ДвоичныеДанныеПакета, ИмяФайлаПакета);
		
		СтатусОшибка = НСтр("ru = 'Ошибка';
							|en = 'Ошибка'");
		Если РезультатОтправки.Статус = СтатусОшибка Тогда
			Возврат;
		КонецЕсли;
		
		НачатьТранзакцию();
		
		ЗапланированнаяОтправка.Удалить();
		
		ЗаписьЖурналаОтправки = РегистрыСведений.ЖурналПередачиОтчетностиВБанк.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(ЗаписьЖурналаОтправки, ПараметрыОтправки);
		ЗаписьЖурналаОтправки.ИдентификаторДокумента = РезультатОтправки.ИдентификаторДокумента;
		ЗаписьЖурналаОтправки.Состояние = РезультатОтправки.Статус;
		ЗаписьЖурналаОтправки.Записать();
		
		// Регистрация отправленных отчетов для предотвращения повторной отправки.
		Для Каждого ОтправленныйОтчет Из ПараметрыОтчетов Цикл
			ВидОтчета = ОтправленныйОтчет.Ключ;
			ПараметрыОтчета = ОтправленныйОтчет.Значение;
			
			Если ПараметрыОтчета.Отправлять Тогда
				ЗаписьЖурналаПереданнойОтчетности = РегистрыСведений.ЖурналПереданнойОтчетностиВБанк.СоздатьМенеджерЗаписи();
				ЗаписьЖурналаПереданнойОтчетности.Организация = Организация;
				ЗаписьЖурналаПереданнойОтчетности.ВидОтчета = ВидОтчета;
				ЗаписьЖурналаПереданнойОтчетности.НачалоПериода = ПараметрыОтчета.ПериодРегистрацииОтправки.НачалоПериода;
				ЗаписьЖурналаПереданнойОтчетности.КонецПериода = ПараметрыОтчета.ПериодРегистрацииОтправки.КонецПериода;
				
				ЗаписьЖурналаПереданнойОтчетности.ДатаОтправки = МоментОтправки;
				ЗаписьЖурналаПереданнойОтчетности.ИмяПакета = ПакетОтправки.ИмяФайлаПакета;
				
				ЗаписьЖурналаПереданнойОтчетности.Записать(Истина);
				
			КонецЕсли;
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
		Если НЕ ОтправлятьВсе Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Выполняет передачу пакета с отчетностью в банк без интерактивного взаимодействия с пользователем.
//
// Параметры:
//  Организация - СправочникСсылка.Организации
//  ДвоичныеДанныеПакета - ДвоичныеДанные
//  ИмяФайла - Строка - имя файла отправляемого пакета
//
// Возвращаемое значение:
//   Строка - "Отправлено", если отчет отправлен успешно;
//            "Ошибка", если при отправке отчета произошла ошибка.
//
Функция ОтправитьОтчетностьВБанкБезПодписи(Организация, ДвоичныеДанныеПакета, ИмяФайлаПакета)
	
	БИК = "044525225";
	ПараметрыКлиента = Новый СистемнаяИнформация;
	
	ЭлектроннаяПодписьПодОтчетом = ПолучитьОбщийМакет("ПодписьБанка");
	
	Результат = ОтчетностьВБанкиСлужебныйВызовСервера.СформироватьПакетИОтправитьДанныеВБанк(
		Организация, БИК, ДвоичныеДанныеПакета, ИмяФайлаПакета, ЭлектроннаяПодписьПодОтчетом, ПараметрыКлиента);
	
	Возврат Результат;
	
КонецФункции

Функция ПакетДляНеинтерактивнойОтправкиВБанк(ПараметрыВыгрузки)
	
	ВременныйКаталогПакета = ФайловаяСистема.СоздатьВременныйКаталог();
	
	ИмяФайлаПредставления = ПараметрыВыгрузки.ИдФайл;
	ПолноеИмяФайлаПредставления = ВременныйКаталогПакета + ИмяФайлаПредставления + ".xml";
	
	Компоновщик = НовыйКомпоновщикXML(ПолноеИмяФайлаПредставления);
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВывестиГоловныеУзлыПакета(Компоновщик, ПараметрыВыгрузки);
	
	ВывестиУзлы_ОСВ(Компоновщик, ПараметрыВыгрузки);
	
	ВывестиУзлы_ОСВПоСчетам(Компоновщик, ПараметрыВыгрузки);
	
	ВывестиУзлы_АнализСчетов(Компоновщик, ПараметрыВыгрузки);
	
	ВывестиУзел_ОперБанкСч(Компоновщик, ПараметрыВыгрузки);
	
	ВывестиЗавершающиеУзлыПакета(Компоновщик);
	
	Компоновщик.Закрыть();
	Компоновщик = Неопределено;
	
	ФайлАрхива = Новый ЗаписьZipФайла();
	ФайлАрхива.Добавить(ПолноеИмяФайлаПредставления);
	
	ДвоичныеДанныеФайла = ФайлАрхива.ПолучитьДвоичныеДанные( , , МетодСжатияZIP.Сжатие, УровеньСжатияZIP.Максимальный);
	
	ФайловаяСистема.УдалитьВременныйКаталог(ВременныйКаталогПакета);
	
	ПакетОтправки = Новый Структура;
	ПакетОтправки.Вставить("ДвоичныеДанныеПакета", ДвоичныеДанныеФайла);
	ПакетОтправки.Вставить("ИмяФайлаПакета", ИмяФайлаПредставления + ".zip");
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ПакетОтправки;
	
КонецФункции

Функция ПараметрыЭлектронногоПредставления(Организация, ПараметрыОтчетов, ДатаВыгрузки) Экспорт
	
	ЭтоИП = НЕ РегламентированнаяОтчетностьВызовСервера.ЭтоЮридическоеЛицо(Организация);
	
	СтрокаСведений = "ИННЮЛ, КППЮЛ, НаимЮЛПол, НаимЮЛСокр, ФИОРук, ФИО";
	
	СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(
		Организация, ДатаВыгрузки, СтрокаСведений);
	
	Если ЭтоИП Тогда
		НаименованиеОрганизации = СокрЛП(СведенияОбОрганизации.ФИО);
	Иначе
		НаименованиеОрганизации = СокрЛП(СведенияОбОрганизации.НаимЮлПол);
	КонецЕсли;
	
	ПараметрыВыгрузки = Новый Структура;
	
	ПараметрыВыгрузки.Вставить("ПараметрыОтчетов", ПараметрыОтчетов);
	
	ПараметрыВыгрузки.Вставить("ВерсФорм", "5.12.11");
	
	НазваниеИВерсияПрограммы = ВРЕГ(РегламентированнаяОтчетность.НазваниеПрограммы()
		+ " " + РегламентированнаяОтчетность.ВерсияПрограммы());
	ПараметрыВыгрузки.Вставить("ВерсПрог", НазваниеИВерсияПрограммы);
	ПараметрыВыгрузки.Вставить("ДатаДок",  Формат(ДатаВыгрузки, "ДФ=дд.ММ.гггг"));
	
	ПараметрыВыгрузки.Вставить("ВидДок",         "0");
	ПараметрыВыгрузки.Вставить("НаимОрг",        СокрЛП(НаименованиеОрганизации));
	ПараметрыВыгрузки.Вставить("НаимОргКраткое", СокрЛП(СведенияОбОрганизации.НаимЮЛСокр));
	
	КонецОтчетногоПериода = КонецМесяца(ДатаВыгрузки);
	
	ПараметрыВыгрузки.Вставить("Организация", Организация);
	ПараметрыВыгрузки.Вставить("НачалоПериода", ПараметрыОтчетов.БанковскиеОперации.НачалоПериода);
	ПараметрыВыгрузки.Вставить("КонецПериода", ПараметрыОтчетов.БанковскиеОперации.КонецПериода);
	ПараметрыВыгрузки.Вставить("ДатаВыгрузки", ДатаВыгрузки);
	ПараметрыВыгрузки.Вставить("КонецОтчетногоПериода", КонецОтчетногоПериода);
	
	Если ЭтоИП Тогда
		ПараметрыВыгрузки.Вставить("ИННФЛ", СокрЛП(Организация.ИНН));
		ФИО = РегламентированнаяОтчетность.РазложитьФИО(НаименованиеОрганизации);
		
		ПараметрыВыгрузки.Вставить("ФЛФамилия",  ФИО.Фамилия);
		ПараметрыВыгрузки.Вставить("ФЛИмя",      ФИО.Имя);
		ПараметрыВыгрузки.Вставить("ФЛОтчество", ФИО.Отчество);
		
	Иначе
		ПараметрыВыгрузки.Вставить("ИННОрг", СокрЛП(СведенияОбОрганизации.ИННЮЛ));
		ПараметрыВыгрузки.Вставить("КППОрг", СокрЛП(СведенияОбОрганизации.КППЮЛ));
		
	КонецЕсли;
	
	ПараметрыВыгрузки.Вставить("ЭтоПБОЮЛ", ЭтоИП);
	
	ЗаполнитьСведенияПодписантов(ПараметрыВыгрузки);
	
	ПараметрыВыгрузки.Вставить("ИдентификаторФайлаВыгрузки", Строка(Новый УникальныйИдентификатор));
	
	СформироватьИдФайл(ПараметрыВыгрузки);
	
	ПараметрыВыгрузки.Вставить("ДатаУтвОтч", Неопределено);
	
	ЦифровойСлед = ЦифровойСлед();
	ПараметрыВыгрузки.Вставить("IP", ЦифровойСлед.IPАдрес);
	ПараметрыВыгрузки.Вставить("MAC", ЦифровойСлед.MACАдрес);
	
	Возврат ПараметрыВыгрузки;
	
КонецФункции

Функция ПараметрыПредставляемыхОтчетов(Организация, ДатаВыгрузки, Принудительно) Экспорт
	
	ОтправлятьОСВ = Ложь;
	ОтправлятьОСВПоСчетам = Ложь;
	ОтправлятьАнализыСчетов = Ложь;
	ОтправлятьОперацииПоБанковскимСчетам = Ложь;
	
	НастройкиОтправки = РегистрыСведений.НастройкиОтправкиОтчетностиВБанк.СоздатьМенеджерЗаписи();
	НастройкиОтправки.Организация = Организация;
	НастройкиОтправки.Прочитать();
	
	Если НастройкиОтправки.Выбран() Тогда
		ОтправлятьОСВ = НастройкиОтправки.ОтправлятьОСВ;
		
		ОтправлятьОСВПоСчетам = НастройкиОтправки.ОтправлятьОСВПоСчетам;
		СчетаОСВПоСчетам = НастройкиОтправки.СчетаОСВПоСчетам.Получить();
		
		ОтправлятьАнализыСчетов = НастройкиОтправки.ОтправлятьАнализыСчетов;
		СчетаАнализовСчетов = НастройкиОтправки.СчетаАнализовСчетов.Получить();
		
		ОтправлятьОперацииПоБанковскимСчетам = НастройкиОтправки.ОтправлятьОтчеты;
		
	КонецЕсли;
	
	ПараметрыОтчетов = Новый Структура;
	
	ПараметрыОтчета = ПараметрыПредставляемыхОтчетов_ОСВ(ДатаВыгрузки, ОтправлятьОСВ);
	ПараметрыОтчетов.Вставить("ОСВ", ПараметрыОтчета);
	
	ПараметрыОтчета = ПараметрыПредставляемыхОтчетов_ОСВПоСчетам(ДатаВыгрузки, ОтправлятьОСВПоСчетам, СчетаОСВПоСчетам);
	ПараметрыОтчетов.Вставить("ОСВПоСчетам", ПараметрыОтчета);
	
	ПараметрыОтчета = ПараметрыПредставляемыхОтчетов_АнализСчетов(ДатаВыгрузки, ОтправлятьАнализыСчетов, СчетаАнализовСчетов);
	ПараметрыОтчетов.Вставить("АнализСчетов", ПараметрыОтчета);
	
	ПараметрыОтчета = ПараметрыПредставляемыхОтчетов_ОперацииПоБанковскимСчетам(ДатаВыгрузки, ОтправлятьОперацииПоБанковскимСчетам, Принудительно);
	ПараметрыОтчетов.Вставить("БанковскиеОперации", ПараметрыОтчета);
	
	Возврат ПараметрыОтчетов;
	
КонецФункции

Функция ПараметрыПредставляемыхОтчетов_ОСВ(ДатаВыгрузки, ОтправлятьОтчет)
	
	// Оборотно-сальдовая ведомость.
	Периоды = Новый Массив;
	
	Периоды = Новый Массив;
	ПериодРегистрацииОтправки = Новый Структура("НачалоПериода, КонецПериода", '0001-01-01', '0001-01-01');
	УстановитьПериодыОтправкиОСВАнализов(Периоды, ПериодРегистрацииОтправки, ДатаВыгрузки);
	
	ОтправкаПредусмотренаГрафиком = (Периоды.Количество() > 0);
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("Периоды", Периоды);
	ПараметрыОтчета.Вставить("Отправлять", ОтправкаПредусмотренаГрафиком И ОтправлятьОтчет);
	ПараметрыОтчета.Вставить("ПериодРегистрацииОтправки", ПериодРегистрацииОтправки);
	
	Возврат ПараметрыОтчета;
	
КонецФункции

Функция ПараметрыПредставляемыхОтчетов_ОСВПоСчетам(ДатаВыгрузки, ОтправлятьОтчет, СчетаОтчета)
	
	// Оборотно-сальдовые ведомости по счетам.
	Периоды = Новый Массив;
	ПериодРегистрацииОтправки = Новый Структура("НачалоПериода, КонецПериода", '0001-01-01', '0001-01-01');
	УстановитьПериодыОтправкиОСВАнализов(Периоды, ПериодРегистрацииОтправки, ДатаВыгрузки);
	
	Счета = Новый Массив;
	Если ОтправлятьОтчет Тогда
		Если СчетаОтчета = Неопределено Тогда
			Счета = СчетаОСВПоСчетам();
		Иначе
			Счета = СчетаОтчета;
		КонецЕсли;
	КонецЕсли;
	
	ОтправкаПредусмотренаГрафиком = (Периоды.Количество() > 0);
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("Периоды", Периоды);
	ПараметрыОтчета.Вставить("Счета", Счета);
	ПараметрыОтчета.Вставить("Отправлять", ОтправкаПредусмотренаГрафиком И ОтправлятьОтчет);
	ПараметрыОтчета.Вставить("ПериодРегистрацииОтправки", ПериодРегистрацииОтправки);
	
	Возврат ПараметрыОтчета;
	
КонецФункции

Функция ПараметрыПредставляемыхОтчетов_АнализСчетов(ДатаВыгрузки, ОтправлятьОтчет, СчетаОтчета)
	
	// Анализы счетов.
	Периоды = Новый Массив;
	ПериодРегистрацииОтправки = Новый Структура("НачалоПериода, КонецПериода", '0001-01-01', '0001-01-01');
	УстановитьПериодыОтправкиОСВАнализов(Периоды, ПериодРегистрацииОтправки, ДатаВыгрузки);
	
	Счета = Новый Массив;
	Если ОтправлятьОтчет Тогда
		Если СчетаОтчета = Неопределено Тогда
			Счета = СчетаАнализовСчетов();
		Иначе
			Счета = СчетаОтчета;
		КонецЕсли;
	КонецЕсли;
	
	ОтправкаПредусмотренаГрафиком = (Периоды.Количество() > 0);
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("Периоды", Периоды);
	ПараметрыОтчета.Вставить("Счета", Счета);
	ПараметрыОтчета.Вставить("Отправлять", ОтправкаПредусмотренаГрафиком И ОтправлятьОтчет);
	ПараметрыОтчета.Вставить("ПериодРегистрацииОтправки", ПериодРегистрацииОтправки);
	
	Возврат ПараметрыОтчета;
	
КонецФункции

Функция ПараметрыПредставляемыхОтчетов_ОперацииПоБанковскимСчетам(ДатаВыгрузки, ОтправлятьОтчет, Принудительно)
	
	Если Принудительно ИЛИ День(ДатаВыгрузки) >= 5 Тогда
		НачалоПериода = ДобавитьМесяц(НачалоМесяца(ДатаВыгрузки), -13);
	Иначе
		// Отложенное выполнение задания
		НачалоПериода = ДобавитьМесяц(НачалоМесяца(ДатаВыгрузки), -14);
	КонецЕсли;
	
	КонецПериода = КонецМесяца(ДобавитьМесяц(НачалоПериода, 12));
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("НачалоПериода", НачалоПериода);
	ПараметрыОтчета.Вставить("КонецПериода", КонецПериода);
	ПараметрыОтчета.Вставить("Отправлять", ОтправлятьОтчет);
	ПараметрыОтчета.Вставить("ПериодРегистрацииОтправки",
		Новый Структура("НачалоПериода, КонецПериода", НачалоПериода, КонецПериода));
	
	Возврат ПараметрыОтчета;
	
КонецФункции

Процедура УстановитьПериодыОтправкиОСВАнализов(Периоды, ПериодРегистрацииОтправки, ДатаВыгрузки)
	
	Январь = 1; Апрель = 4; Май = 5; Июнь = 6; Июль = 7;
	Август = 8; Сентябрь = 9; Октябрь = 10; Ноябрь = 11; Декабрь = 12;
	
	ГодВыгрузки = Год(ДатаВыгрузки);
	
	Если ДатаВыгрузки >= Дата(ГодВыгрузки, Май, 11)
	   И ДатаВыгрузки <= КонецДня(Дата(ГодВыгрузки, Июнь, 11)) Тогда
		// 1 квартал текущего года
		НачалоПериода = Дата(ГодВыгрузки, Январь, 1);
		КонецПериода  = КонецКвартала(НачалоПериода);
		Периоды.Добавить(Новый Структура("НачалоПериода, КонецПериода", НачалоПериода, КонецПериода));
		ПериодРегистрацииОтправки = Новый Структура("НачалоПериода, КонецПериода", НачалоПериода, КонецПериода);
		
	ИначеЕсли ДатаВыгрузки >= Дата(ГодВыгрузки, Август, 5)
	        И ДатаВыгрузки <= КонецДня(Дата(ГодВыгрузки, Сентябрь, 5)) Тогда
		// 2 квартал текущего года.
		НачалоПериода = Дата(ГодВыгрузки, Апрель, 1);
		КонецПериода  = КонецКвартала(НачалоПериода);
		Периоды.Добавить(Новый Структура("НачалоПериода, КонецПериода", НачалоПериода, КонецПериода));
		ПериодРегистрацииОтправки = Новый Структура("НачалоПериода, КонецПериода", НачалоПериода, КонецПериода);
		
		// 6 месяцев текущего года.
		НачалоПериода = НачалоГода(НачалоПериода);
		Периоды.Добавить(Новый Структура("НачалоПериода, КонецПериода", НачалоПериода, КонецПериода));
		
	ИначеЕсли ДатаВыгрузки >= Дата(ГодВыгрузки, Ноябрь, 9)
		    И ДатаВыгрузки <= КонецДня(Дата(ГодВыгрузки, Декабрь, 9)) Тогда
		// 3 квартал текущего года.
		НачалоПериода = Дата(ГодВыгрузки, Июль, 1);
		КонецПериода  = КонецКвартала(НачалоПериода);
		Периоды.Добавить(Новый Структура("НачалоПериода, КонецПериода", НачалоПериода, КонецПериода));
		ПериодРегистрацииОтправки = Новый Структура("НачалоПериода, КонецПериода", НачалоПериода, КонецПериода);
		
		// 9 месяцев текущего года.
		НачалоПериода = НачалоГода(НачалоПериода);
		Периоды.Добавить(Новый Структура("НачалоПериода, КонецПериода", НачалоПериода, КонецПериода));
		
	ИначеЕсли ДатаВыгрузки >= Дата(ГодВыгрузки, Апрель, 7)
	        И ДатаВыгрузки <= КонецДня(Дата(ГодВыгрузки, Май, 7)) Тогда
		// 4 квартал предшествующего года.
		НачалоПериода = Дата(ГодВыгрузки - 1 , Октябрь, 1);
		КонецПериода  = КонецКвартала(НачалоПериода);
		Периоды.Добавить(Новый Структура("НачалоПериода, КонецПериода", НачалоПериода, КонецПериода));
		ПериодРегистрацииОтправки = Новый Структура("НачалоПериода, КонецПериода", НачалоПериода, КонецПериода);
		
		// 12 месяцев предшествующего года.
		НачалоПериода = НачалоГода(НачалоПериода);
		Периоды.Добавить(Новый Структура("НачалоПериода, КонецПериода", НачалоПериода, КонецПериода));
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьСведенияПодписантов(ПараметрыВыгрузки)
	
	ПараметрыВыгрузки.Вставить("ПрПодп", "1");
	
	ПараметрыВыгрузки.Вставить("ПодпФамилия",  "Без");
	ПараметрыВыгрузки.Вставить("ПодпИмя",      "подписи");
	ПараметрыВыгрузки.Вставить("ПодпОтчество", "");
	
	ПараметрыВыгрузки.Вставить("ПодпНаимДок", "");
	
КонецПроцедуры

Процедура СформироватьИдФайл(ПараметрыВыгрузки)
	
	Если ПараметрыВыгрузки.ЭтоПБОЮЛ Тогда
		ИНН = ПараметрыВыгрузки.ИННФЛ;
		КПП = "000000000";
	Иначе
		ИНН = ПараметрыВыгрузки.ИННОрг;
		КПП = ПараметрыВыгрузки.КППОрг;
	КонецЕсли;
	
	ГодОтчета = Формат(ПараметрыВыгрузки.КонецОтчетногоПериода, "ДФ=гггг");
	КварталОтчета = Строка(Месяц(КонецКвартала(ПараметрыВыгрузки.КонецОтчетногоПериода)) / 3) + "Q";
	
	МоментВыгрузки = Формат(ПараметрыВыгрузки.ДатаВыгрузки, "ДФ=yyyyMMddHHmmss");
	
	ИдФайл = ИНН + "_" + КПП
	       + "_" + ГодОтчета + "_" + КварталОтчета
	       + "_" + МоментВыгрузки + "_TR";
	
	ПараметрыВыгрузки.Вставить("ИдФайл", ИдФайл);
	
КонецПроцедуры

Функция СведенияОСВ(Параметры)
	
	СведенияОСВ = Неопределено;
	
	ОтчетностьВБанкиСлужебныйПереопределяемый.ЗаполнитьСведенияОСВ(Параметры, СведенияОСВ);
	
	Возврат СведенияОСВ;
	
КонецФункции

Функция СведенияОСВПоСчету(Параметры)
	
	СведенияОСВПоСчету = Неопределено;
	
	ОтчетностьВБанкиСлужебныйПереопределяемый.ЗаполнитьСведенияОСВПоСчету(Параметры, СведенияОСВПоСчету);
	
	Возврат СведенияОСВПоСчету;
	
КонецФункции

Функция СведенияАнализаСчета(Параметры)
	
	СведенияАнализаСчета = Неопределено;
	
	ОтчетностьВБанкиСлужебныйПереопределяемый.ЗаполнитьСведенияАнализаСчета(Параметры, СведенияАнализаСчета);
	
	Возврат СведенияАнализаСчета;
	
КонецФункции

#КонецОбласти

#Область ФормированиеXML

Процедура ВывестиГоловныеУзлыПакета(Компоновщик, ПараметрыВыгрузки) Экспорт
	
	Компоновщик.ЗаписатьНачалоЭлемента("Файл");
	
	ВывестиАтрибут(Компоновщик, "xmlns:xsi", "http://www.w3.org/2001/XMLSchema-instance");
	ВывестиАтрибут(Компоновщик, "ИдФайл",   ПараметрыВыгрузки.ИдФайл);
	Если ДействуетСВерсииФормата("5.12.10", ПараметрыВыгрузки.ВерсФорм) Тогда
		ВывестиАтрибут(Компоновщик, "ВерсПрог", ПараметрыВыгрузки.ВерсПрог, "Т,255");
	Иначе
		ВывестиАтрибут(Компоновщик, "ВерсПрог", ПараметрыВыгрузки.ВерсПрог, "Т,40");
	КонецЕсли;
	ВывестиАтрибут(Компоновщик, "ВерсФорм", ПараметрыВыгрузки.ВерсФорм);
	
	Если ДействуетСВерсииФормата("5.12.10", ПараметрыВыгрузки.ВерсФорм)
	   И ПараметрыВыгрузки.Свойство("IP") Тогда
		ВывестиНеобязательныйАтрибут(Компоновщик, "IP", ПараметрыВыгрузки.IP, "Т,39");
		ВывестиНеобязательныйАтрибут(Компоновщик, "MAC", ПараметрыВыгрузки.MAC, "Т,17");
		
	ИначеЕсли ДействуетСВерсииФормата("5.12.08", ПараметрыВыгрузки.ВерсФорм)
	   И ПараметрыВыгрузки.Свойство("IP") Тогда
		ДополнениеПробелами = "                                        ";
		ВывестиНеобязательныйАтрибут(Компоновщик, "IP", Лев(ПараметрыВыгрузки.IP + ДополнениеПробелами, 39), "П");
		ВывестиНеобязательныйАтрибут(Компоновщик, "MAC", Лев(ПараметрыВыгрузки.MAC + ДополнениеПробелами, 17), "П");
		
	КонецЕсли;
	
	Компоновщик.ЗаписатьНачалоЭлемента("Документ");
	
	ВывестиАтрибут(Компоновщик, "КНД", "0710099");
	ВывестиАтрибут(Компоновщик, "ДатаДок", ПараметрыВыгрузки.ДатаДок);
	ВывестиНеобязательныйАтрибут(Компоновщик, "ДатаУтвОтч", ПараметрыВыгрузки.ДатаУтвОтч);
	ВывестиАтрибут(Компоновщик, "НомКорр", ПараметрыВыгрузки.ВидДок);
	
	ВывестиСведенияОНалогоплательщике(Компоновщик, ПараметрыВыгрузки);
	ВывестиСведенияОПодписанте(Компоновщик, ПараметрыВыгрузки);
	
	Если ДействуетСВерсииФормата("5.11.01", ПараметрыВыгрузки.ВерсФорм) Тогда
		ПараметрыВыгрузкиУзла = Новый Структура;
		ПараметрыВыгрузкиУзла.Вставить("ВыгружатьАтрибутЗабалансовый",
			ДействуетСВерсииФормата("5.12.10", ПараметрыВыгрузки.ВерсФорм));
		ВывестиУзел_БухгалтерскиеСчета(Компоновщик, ПараметрыВыгрузкиУзла);
	КонецЕсли;
	
КонецПроцедуры

Процедура ВывестиЗавершающиеУзлыПакета(Компоновщик) Экспорт
	
	Компоновщик.ЗаписатьКонецЭлемента(); // Документ
	
	Компоновщик.ЗаписатьКонецЭлемента(); // Файл
	
КонецПроцедуры

Процедура ВывестиСведенияОНалогоплательщике(Компоновщик, ПараметрыВыгрузки)
	
	Компоновщик.ЗаписатьНачалоЭлемента("СвНП");
	
	Если ПараметрыВыгрузки.Свойство("ИННФЛ") Тогда
		Компоновщик.ЗаписатьНачалоЭлемента("НПФЛ");
		
		ВывестиАтрибут(Компоновщик, "ИННФЛ", ПараметрыВыгрузки.ИННФЛ);
		
		Компоновщик.ЗаписатьНачалоЭлемента("ФИО");
		
		ВывестиАтрибут(Компоновщик, "Фамилия", ПараметрыВыгрузки.ФЛФамилия);
		ВывестиАтрибут(Компоновщик, "Имя", ПараметрыВыгрузки.ФЛИмя);
		ВывестиНеобязательныйАтрибут(Компоновщик, "Отчество", ПараметрыВыгрузки.ФЛОтчество);
		
		Компоновщик.ЗаписатьКонецЭлемента(); // ФИО
		
		Компоновщик.ЗаписатьКонецЭлемента(); // НПФЛ
		
	Иначе
		Компоновщик.ЗаписатьНачалоЭлемента("НПЮЛ");
		
		ВывестиАтрибут(Компоновщик, "НаимОрг", ПараметрыВыгрузки.НаимОрг);
		ВывестиАтрибут(Компоновщик, "НаимОргКраткое", ПараметрыВыгрузки.НаимОргКраткое);
		ВывестиАтрибут(Компоновщик, "ИННЮЛ", ПараметрыВыгрузки.ИННОрг);
		ВывестиАтрибут(Компоновщик, "КПП", ПараметрыВыгрузки.КППОрг);
		
		Компоновщик.ЗаписатьКонецЭлемента(); // НПЮЛ
		
	КонецЕсли;
	
	Компоновщик.ЗаписатьКонецЭлемента(); // СвНП
	
КонецПроцедуры

Процедура ВывестиСведенияОПодписанте(Компоновщик, ПараметрыВыгрузки)
	
	Компоновщик.ЗаписатьНачалоЭлемента("Подписант");
	
	ВывестиАтрибут(Компоновщик, "ПрПодп", ПараметрыВыгрузки.ПрПодп);
	
	Компоновщик.ЗаписатьНачалоЭлемента("ФИО");
	
	ВывестиАтрибут(Компоновщик, "Фамилия", ПараметрыВыгрузки.ПодпФамилия);
	ВывестиАтрибут(Компоновщик, "Имя", ПараметрыВыгрузки.ПодпИмя);
	ВывестиНеобязательныйАтрибут(Компоновщик, "Отчество", ПараметрыВыгрузки.ПодпОтчество);
	
	Компоновщик.ЗаписатьКонецЭлемента();
	Компоновщик.ЗаписатьКонецЭлемента();
	
КонецПроцедуры

Процедура ВывестиУзел_БухгалтерскиеСчета(Компоновщик, ПараметрыВыгрузкиУзла) Экспорт
	
	ВыгружатьАтрибутЗабалансовый = ПараметрыВыгрузкиУзла.ВыгружатьАтрибутЗабалансовый;
	
	ДеревоСчетов = ОтчетностьВБанкиСлужебный.ДеревоСчетовБУ(ВыгружатьАтрибутЗабалансовый);
	
	Если ДеревоСчетов.Строки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Компоновщик.ЗаписатьНачалоЭлемента("БухгалтерскиеСчета");
	
	СтрокиУровня1 = ДеревоСчетов.Строки;
	
	Для Каждого Выборка1Уровня Из СтрокиУровня1 Цикл
		Компоновщик.ЗаписатьНачалоЭлемента("Счет");
		
		ВывестиАтрибут(Компоновщик, "Код", Выборка1Уровня.Код, "Т,8");
		ВывестиАтрибут(Компоновщик, "Наименование", Выборка1Уровня.Наименование, "Т,120");
		ВывестиАтрибут(Компоновщик, "Тип", Выборка1Уровня.Вид, "Т,2");
		Если ВыгружатьАтрибутЗабалансовый Тогда
			ВывестиАтрибут(Компоновщик, "Забалансовый", ?(Выборка1Уровня.Забалансовый, "1", "0"));
		КонецЕсли;
		
		СтрокиУровня2 = Выборка1Уровня.Строки;
		
		Для Каждого Выборка2Уровня Из СтрокиУровня2 Цикл
			Если Выборка2Уровня.Ссылка = Выборка1Уровня.Ссылка Тогда
				Продолжить;
			КонецЕсли;
			
			Компоновщик.ЗаписатьНачалоЭлемента("СубСчет");
			
			ВывестиАтрибут(Компоновщик, "Код", Выборка2Уровня.Код, "Т,8");
			ВывестиАтрибут(Компоновщик, "Наименование", Выборка2Уровня.Наименование, "Т,120");
			ВывестиАтрибут(Компоновщик, "Тип", Выборка2Уровня.Вид, "Т,2");
			Если ВыгружатьАтрибутЗабалансовый Тогда
				ВывестиАтрибут(Компоновщик, "Забалансовый", ?(Выборка2Уровня.Забалансовый, "1", "0"));
			КонецЕсли;
			
			СтрокиУровня3 = Выборка2Уровня.Строки;
			
			Для Каждого Выборка3Уровня Из СтрокиУровня3 Цикл
				Если Выборка3Уровня.Ссылка = Выборка2Уровня.Ссылка Тогда
					Продолжить;
				КонецЕсли;
				
				Компоновщик.ЗаписатьНачалоЭлемента("СубСубСчет");
				
				ВывестиАтрибут(Компоновщик, "Код", Выборка3Уровня.Код, "Т,8");
				ВывестиАтрибут(Компоновщик, "Наименование", Выборка3Уровня.Наименование, "Т,120");
				ВывестиАтрибут(Компоновщик, "Тип", Выборка3Уровня.Вид, "Т,2");
				Если ВыгружатьАтрибутЗабалансовый Тогда
					ВывестиАтрибут(Компоновщик, "Забалансовый", ?(Выборка3Уровня.Забалансовый, "1", "0"));
				КонецЕсли;
				
				Компоновщик.ЗаписатьКонецЭлемента(); // СубСубСчет
				
			КонецЦикла;
			
			Компоновщик.ЗаписатьКонецЭлемента(); // СубСчет
			
		КонецЦикла;
		
		Компоновщик.ЗаписатьКонецЭлемента(); // Счет
		
	КонецЦикла;
	
	Компоновщик.ЗаписатьКонецЭлемента(); // БухгалтерскиеСчета
	
КонецПроцедуры

#Область ВыводОСВ

Процедура ВывестиУзлы_ОСВ(Компоновщик, ПараметрыВыгрузки)
	
	Организация = ПараметрыВыгрузки.Организация;
	ПараметрыОтчета = ПараметрыВыгрузки.ПараметрыОтчетов.ОСВ;
	
	Если НЕ ПараметрыОтчета.Отправлять Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Период Из ПараметрыОтчета.Периоды Цикл
		НачалоПериода = Период.НачалоПериода;
		КонецПериода =  Период.КонецПериода;
		
		АтрибутыПериода = АтрибутыПериода(НачалоПериода, КонецПериода);
		
		ПараметрыОСВ = Новый Структура("Организация, НачалоПериода, КонецПериода",
			Организация, НачалоПериода, КонецПериода);
		СтрокиОСВ = СведенияОСВ(ПараметрыОСВ);
		
		ПараметрыУзла = ОтчетностьВБанкиСлужебный.НовыеПараметрыУзла_ОСВ();
		ПараметрыУзла.Период = АтрибутыПериода.КодПериода;
		ПараметрыУзла.ОтчетГод = АтрибутыПериода.ГодПериода;
		ПараметрыУзла.ВерсияОтчета = ПараметрыВыгрузки.ВерсФорм;
		
		ВывестиСтраницуВУзел_ОСВ(Компоновщик, СтрокиОСВ.Строки, ПараметрыУзла);
		
	КонецЦикла;
	
КонецПроцедуры

Функция НовыеПараметрыУзла_ОСВ() Экспорт
	
	ПараметрыУзла = Новый Структура;
	
	ПараметрыУзла.Вставить("Период", "");
	ПараметрыУзла.Вставить("ОтчетГод", "");
	ПараметрыУзла.Вставить("ОКЕИ", "383");
	ПараметрыУзла.Вставить("ХешСумма", "");
	ПараметрыУзла.Вставить("ВерсияОтчета", "0.0.0.0");
	
	Возврат ПараметрыУзла;
	
КонецФункции

Процедура ВывестиСтраницуВУзел_ОСВ(Компоновщик, СтрокиОСВ, ПараметрыУзла) Экспорт
	
	ВерсияОтчета = ПараметрыУзла.ВерсияОтчета;
	
	Период = ПараметрыУзла.Период;
	ОтчетГод = ПараметрыУзла.ОтчетГод;
	ОКЕИ = "383";
	
	ХешСумма = ПараметрыУзла.ХешСумма;
	
	Компоновщик.ЗаписатьНачалоЭлемента("ОСВ");
	
	ВывестиАтрибут(Компоновщик, "Период", Период);
	ВывестиАтрибут(Компоновщик, "ОтчетГод", ОтчетГод);
	ВывестиАтрибут(Компоновщик, "ОКЕИ", ОКЕИ);
	
	Если ДействуетСВерсииФормата("5.11.01", ВерсияОтчета) Тогда
		ВывестиНеобязательныйАтрибут(Компоновщик, "ХешСумма", ХешСумма);
	КонецЕсли;
	
	Для Каждого СтрокаОСВ Из СтрокиОСВ Цикл
		Для Каждого СтрокаСчета Из СтрокаОСВ.Строки Цикл
			Компоновщик.ЗаписатьНачалоЭлемента("Счет");
			
			СведенияСчетаСтроки = ОтчетностьВБанкиСлужебный.ОписаниеСчетаДляВыгрузки(СтрокаСчета.Счет);
			
			ВывестиАтрибут(Компоновщик, "КодСчета", СведенияСчетаСтроки.КодСчета, "Т,8");
			ВывестиАтрибут(Компоновщик, "НаименованиеСчета", СведенияСчетаСтроки.НаименованиеСчета, "Т,120");
			ВывестиАтрибут(Компоновщик, "ТипСчета", СведенияСчетаСтроки.ТипСчета, "Т,2");
			
			ВывестиНеобязательныйАтрибут(Компоновщик, "КодСчетаР", СведенияСчетаСтроки.КодСчетаР, "Т,8");
			ВывестиНеобязательныйАтрибут(Компоновщик, "ТипСчетаР", СведенияСчетаСтроки.ТипСчетаР, "Т,2");
			ВывестиНеобязательныйАтрибут(Компоновщик, "КодСчетаРР", СведенияСчетаСтроки.КодСчетаРР, "Т,8");
			ВывестиНеобязательныйАтрибут(Компоновщик, "ТипСчетаРР", СведенияСчетаСтроки.ТипСчетаРР, "Т,2");
			
			ВывестиАтрибут(Компоновщик, "СНД", СтрокаСчета.СНД, "Ч,15,2");
			ВывестиАтрибут(Компоновщик, "СНК", СтрокаСчета.СНК, "Ч,15,2");
			ВывестиАтрибут(Компоновщик, "ДО",  СтрокаСчета.ДО,  "Ч,15,2");
			ВывестиАтрибут(Компоновщик, "КО",  СтрокаСчета.КО,  "Ч,15,2");
			ВывестиАтрибут(Компоновщик, "СКД", СтрокаСчета.СКД, "Ч,15,2");
			ВывестиАтрибут(Компоновщик, "СКК", СтрокаСчета.СКК, "Ч,15,2");
			
			Компоновщик.ЗаписатьКонецЭлемента();
			
		КонецЦикла;
		
	КонецЦикла;
	
	Компоновщик.ЗаписатьКонецЭлемента();
	
КонецПроцедуры

#КонецОбласти

#Область ВыводОСВПоСчетам

Процедура ВывестиУзлы_ОСВПоСчетам(Компоновщик, ПараметрыВыгрузки)
	
	Организация = ПараметрыВыгрузки.Организация;
	ПараметрыОтчета = ПараметрыВыгрузки.ПараметрыОтчетов.ОСВПоСчетам;
	
	Если НЕ ПараметрыОтчета.Отправлять Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Период Из ПараметрыОтчета.Периоды Цикл
		НачалоПериода = Период.НачалоПериода;
		КонецПериода =  Период.КонецПериода;
		
		Компоновщик.ЗаписатьНачалоЭлемента("ОСВПоСчетам");
		
		АтрибутыПериода = АтрибутыПериода(НачалоПериода, КонецПериода);
		
		ВывестиАтрибут(Компоновщик, "Период",   АтрибутыПериода.КодПериода);
		ВывестиАтрибут(Компоновщик, "ОтчетГод", АтрибутыПериода.ГодПериода);
		ВывестиАтрибут(Компоновщик, "ОКЕИ", "383");
		
		ПараметрыЗаполнения = Новый Структура;
		ПараметрыЗаполнения.Вставить("Организация", Организация);
		ПараметрыЗаполнения.Вставить("НачалоПериода", НачалоПериода);
		ПараметрыЗаполнения.Вставить("КонецПериода", КонецПериода);
		
		Для Каждого Счет Из ПараметрыОтчета.Счета Цикл
			ПараметрыЗаполнения.Вставить("Счет", Счет);
			
			ПараметрыОтправкиСчета = ОтчетностьВБанкиСлужебный.ПараметрыОтправкиСчета(Счет);
			РазвернутоеСальдо = (ПараметрыОтправкиСчета.Найти(Истина, "РазвернутоеСальдо")<> Неопределено);
			ПараметрыЗаполнения.Вставить("РазвернутоеСальдо", РазвернутоеСальдо);
			
			СведенияОСВПоСчету = СведенияОСВПоСчету(ПараметрыЗаполнения);
			
			ПараметрыУзла = Новый Структура;
			ПараметрыУзла.Вставить("ВыгружаемыйСчет", Счет);
			ПараметрыУзла.Вставить("СведенияОСВПоСчету", СведенияОСВПоСчету);
			ПараметрыУзла.Вставить("ВерсияФормата", ПараметрыВыгрузки.ВерсФорм);
			
			ВывестиСтраницуВУзел_ОСВПоСчетам_ОСВПоСчету(Компоновщик, ПараметрыУзла);
			
		КонецЦикла;
		
		Компоновщик.ЗаписатьКонецЭлемента(); // ОСВПоСчетам
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ВывестиСтраницуВУзел_ОСВПоСчетам_ОСВПоСчету(Компоновщик, ПараметрыУзла)
	
	ВыгружаемыйСчет = ПараметрыУзла.ВыгружаемыйСчет;
	СведенияОСВПоСчету = ПараметрыУзла.СведенияОСВПоСчету;
	ВерсияФормата = ПараметрыУзла.ВерсияФормата;
	
	ПараметрыОтправкиСчета = ПараметрыОтправкиСчета(ВыгружаемыйСчет);
	
	ОтражатьКоличество = (ДействуетСВерсииФормата("5.12.09", ВерсияФормата)
		И ПараметрыОтправкиСчета.Найти(Истина, "Количественный")<> Неопределено);
		
	Компоновщик.ЗаписатьНачалоЭлемента("ОСВПоСчету");
	
	ВывестиАтрибут(Компоновщик, "КодСчета", ВыгружаемыйСчет.Код);
	ВывестиАтрибут(Компоновщик, "НаименованиеСчета", ВыгружаемыйСчет.Наименование);
	
	ВыборкаПоСчетам = СведенияОСВПоСчету.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаПоСчетам.Следующий() Цикл
		ВыводимыйСубсчет = ВыборкаПоСчетам.Счет;
		
		Компоновщик.ЗаписатьНачалоЭлемента("Счет");
		
		ВывестиАтрибут(Компоновщик, "КодСчета", ВыборкаПоСчетам.КодСчета, "Т,8");
		
		ВывестиАтрибут(Компоновщик, "СНД", ВыборкаПоСчетам.СНД, "Ч,15,2");
		ВывестиАтрибут(Компоновщик, "СНК", ВыборкаПоСчетам.СНК, "Ч,15,2");
		ВывестиАтрибут(Компоновщик, "ДО",  ВыборкаПоСчетам.ДО,  "Ч,15,2");
		ВывестиАтрибут(Компоновщик, "КО",  ВыборкаПоСчетам.КО,  "Ч,15,2");
		ВывестиАтрибут(Компоновщик, "СКД", ВыборкаПоСчетам.СКД, "Ч,15,2");
		ВывестиАтрибут(Компоновщик, "СКК", ВыборкаПоСчетам.СКК, "Ч,15,2");
		
		Если ОтражатьКоличество
		   И РегламентированнаяОтчетностьКлиентСервер.СвойствоОпределено(ВыборкаПоСчетам, "КНОД") Тогда
			ВывестиАтрибут(Компоновщик, "КНОД", ВыборкаПоСчетам.КНОД, "Ч,15,3");
			ВывестиАтрибут(Компоновщик, "КНОК", ВыборкаПоСчетам.КНОК, "Ч,15,3");
			ВывестиАтрибут(Компоновщик, "КДО",  ВыборкаПоСчетам.КДО,  "Ч,15,3");
			ВывестиАтрибут(Компоновщик, "ККО",  ВыборкаПоСчетам.ККО,  "Ч,15,3");
			ВывестиАтрибут(Компоновщик, "ККОД", ВыборкаПоСчетам.ККОД, "Ч,15,3");
			ВывестиАтрибут(Компоновщик, "ККОК", ВыборкаПоСчетам.ККОК, "Ч,15,3");
		КонецЕсли;
		
		ВыборкаПоСубконто1 = ВыборкаПоСчетам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаПоСубконто1.Следующий() Цикл
			Если ВыборкаПоСубконто1.Субконто1 <> Null Тогда
				Компоновщик.ЗаписатьНачалоЭлемента("Субконто1");
				
				Субконто1Выведено = Ложь;
				ПредставлениеСубконто1 = ВыборкаПоСубконто1.ПредставлениеСубконто1;
				
				ВыборкаПоСубконто2 = ВыборкаПоСубконто1.Выбрать();
				Если ВыборкаПоСубконто2.Количество() = 0 Тогда
					ВывестиАтрибутыУзла_ОСВПоСчетам_ОСВПоСчету_Субконто(Компоновщик, ВыборкаПоСубконто1, ПредставлениеСубконто1, 1, ОтражатьКоличество);
					Субконто1Выведено = Истина;
				КонецЕсли;
				
				Пока ВыборкаПоСубконто2.Следующий() Цикл
					Если НЕ Субконто1Выведено Тогда
						ПредставлениеСубконто1 = ВыборкаПоСубконто2.ПредставлениеСубконто1;
						ВывестиАтрибутыУзла_ОСВПоСчетам_ОСВПоСчету_Субконто(Компоновщик, ВыборкаПоСубконто1, ПредставлениеСубконто1, 1, ОтражатьКоличество);
						Субконто1Выведено = Истина;
					КонецЕсли;
					
					Если ВыборкаПоСубконто2.Субконто2Оборотное
					   И ВыборкаПоСубконто2.ДО = 0 И ВыборкаПоСубконто2.КО = 0 Тогда
						// Пропуск строка с остатками по субконто 1 для группировки.
						Продолжить;
					КонецЕсли;
					
					Если Субконто1Выведено И ВыборкаПоСубконто2.Субконто2 <> Null Тогда
						Компоновщик.ЗаписатьНачалоЭлемента("Субконто2");
						
						ПредставлениеСубконто2 = ВыборкаПоСубконто2.ПредставлениеСубконто2;
						ВывестиАтрибутыУзла_ОСВПоСчетам_ОСВПоСчету_Субконто(Компоновщик, ВыборкаПоСубконто2, ПредставлениеСубконто2, 2, ОтражатьКоличество);
						
						Компоновщик.ЗаписатьКонецЭлемента(); // Субконто2
						
					КонецЕсли;
					
				КонецЦикла;
				
				Компоновщик.ЗаписатьКонецЭлемента(); // Субконто1
				
			КонецЕсли;
			
		КонецЦикла;
		
		Компоновщик.ЗаписатьКонецЭлемента(); // Счет
		
	КонецЦикла;
	
	Компоновщик.ЗаписатьКонецЭлемента(); // ОСВПоСчету
	
КонецПроцедуры

Процедура ВывестиАтрибутыУзла_ОСВПоСчетам_ОСВПоСчету_Субконто(Компоновщик, ВыборкаПоСубконто,
	Знач ПредставлениеСубконто, НомерСубконто, ОтражатьКоличество)
	
	ПредставлениеСубконто = ПредставлениеСубконтоИННКПП(ВыборкаПоСубконто, ПредставлениеСубконто, НомерСубконто);
	
	ВывестиАтрибут(Компоновщик, "Представление", ПредставлениеСубконто, "Т,1000");
	ВывестиАтрибут(Компоновщик, "ТипСубконто", Строка(ТипЗнч(ВыборкаПоСубконто["Субконто" + НомерСубконто])), "Т,100");
	ВывестиНеобязательныйАтрибут(Компоновщик, "ИНН", ИННКонтрагентаПоСубконто(ВыборкаПоСубконто, НомерСубконто), "Т,12");
	
	ВывестиАтрибут(Компоновщик, "СНД", ВыборкаПоСубконто.СНД, "Ч,15,2");
	ВывестиАтрибут(Компоновщик, "СНК", ВыборкаПоСубконто.СНК, "Ч,15,2");
	ВывестиАтрибут(Компоновщик, "ДО",  ВыборкаПоСубконто.ДО,  "Ч,15,2");
	ВывестиАтрибут(Компоновщик, "КО",  ВыборкаПоСубконто.КО,  "Ч,15,2");
	ВывестиАтрибут(Компоновщик, "СКД", ВыборкаПоСубконто.СКД, "Ч,15,2");
	ВывестиАтрибут(Компоновщик, "СКК", ВыборкаПоСубконто.СКК, "Ч,15,2");
	
	Если ОтражатьКоличество
	   И РегламентированнаяОтчетностьКлиентСервер.СвойствоОпределено(ВыборкаПоСубконто, "КНОД") Тогда
		ВывестиАтрибут(Компоновщик, "КНОД", ВыборкаПоСубконто.КНОД, "Ч,15,3");
		ВывестиАтрибут(Компоновщик, "КНОК", ВыборкаПоСубконто.КНОК, "Ч,15,3");
		ВывестиАтрибут(Компоновщик, "КДО",  ВыборкаПоСубконто.КДО,  "Ч,15,3");
		ВывестиАтрибут(Компоновщик, "ККО",  ВыборкаПоСубконто.ККО,  "Ч,15,3");
		ВывестиАтрибут(Компоновщик, "ККОД", ВыборкаПоСубконто.ККОД, "Ч,15,3");
		ВывестиАтрибут(Компоновщик, "ККОК", ВыборкаПоСубконто.ККОК, "Ч,15,3");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ВыводАнализовСчетов

Процедура ВывестиУзлы_АнализСчетов(Компоновщик, ПараметрыВыгрузки)
	
	Организация = ПараметрыВыгрузки.Организация;
	ПараметрыОтчета = ПараметрыВыгрузки.ПараметрыОтчетов.АнализСчетов;
	
	Если НЕ ПараметрыОтчета.Отправлять Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Период Из ПараметрыОтчета.Периоды Цикл
		НачалоПериода = Период.НачалоПериода;
		КонецПериода =  Период.КонецПериода;
		
		Компоновщик.ЗаписатьНачалоЭлемента("АнализСчетов");
		
		АтрибутыПериода = АтрибутыПериода(НачалоПериода, КонецПериода);
		
		ВывестиАтрибут(Компоновщик, "Период",   АтрибутыПериода.КодПериода);
		ВывестиАтрибут(Компоновщик, "ОтчетГод", АтрибутыПериода.ГодПериода);
		ВывестиАтрибут(Компоновщик, "ОКЕИ", "383");
		
		ПараметрыЗаполнения = Новый Структура;
		ПараметрыЗаполнения.Вставить("Организация", Организация);
		ПараметрыЗаполнения.Вставить("НачалоПериода", НачалоПериода);
		ПараметрыЗаполнения.Вставить("КонецПериода", КонецПериода);
		ПараметрыЗаполнения.Вставить("ВерсияФормата", ПараметрыВыгрузки.ВерсФорм);

		
		Для Каждого Счет Из ПараметрыОтчета.Счета Цикл
			ПараметрыЗаполнения.Вставить("Счет", Счет);
			ВывестиСтраницуВУзел_АнализСчетов_АнализСчета(Компоновщик, ПараметрыЗаполнения);
		КонецЦикла;
		
		Компоновщик.ЗаписатьКонецЭлемента(); // АнализСчетов
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ВывестиСтраницуВУзел_АнализСчетов_АнализСчета(Компоновщик, ПараметрыЗаполнения)
	
	ВыгружаемыйСчет = ПараметрыЗаполнения.Счет;
	ПараметрыОтправкиСчета = ПараметрыОтправкиСчета(ВыгружаемыйСчет);
	ВерсияФормата = ПараметрыЗаполнения.ВерсияФормата;
	
	Компоновщик.ЗаписатьНачалоЭлемента("АнализСчета");
	
	ВывестиАтрибут(Компоновщик, "КодСчета", ВыгружаемыйСчет.Код);
	ВывестиАтрибут(Компоновщик, "НаименованиеСчета", ВыгружаемыйСчет.Наименование);
	
	// Возвращаемый результат запроса всегда содержит иерархию Счет - Субконто 1 - Субконто 2 - Корреспонденция.
	// В случае, если у счета отсутствует субконто, то вместо него передается Null или "NULL".
	// Выборка по корреспонденции может быть пустой.
	СведенияАнализаСчета = СведенияАнализаСчета(ПараметрыЗаполнения);
	
	ВыборкаПоСчетам = СведенияАнализаСчета.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаПоСчетам.Следующий() Цикл
		Компоновщик.ЗаписатьНачалоЭлемента("Счет");
		
		ВывестиАтрибут(Компоновщик, "КодСчета", ВыборкаПоСчетам.КодСчета, "Т,8");
		
		ВывестиАтрибут(Компоновщик, "СНД", ВыборкаПоСчетам.СНД, "Ч,15,2");
		ВывестиАтрибут(Компоновщик, "СНК", ВыборкаПоСчетам.СНК, "Ч,15,2");
		ВывестиАтрибут(Компоновщик, "ДО",  ВыборкаПоСчетам.ДО,  "Ч,15,2");
		ВывестиАтрибут(Компоновщик, "КО",  ВыборкаПоСчетам.КО,  "Ч,15,2");
		ВывестиАтрибут(Компоновщик, "СКД", ВыборкаПоСчетам.СКД, "Ч,15,2");
		ВывестиАтрибут(Компоновщик, "СКК", ВыборкаПоСчетам.СКК, "Ч,15,2");
		
		ОтражатьКоличество = ДействуетСВерсииФормата("5.12.09", ВерсияФормата)
			И ПараметрыОтправкиСчета.Найти(Истина, "Количественный")<> Неопределено
			И РегламентированнаяОтчетностьКлиентСервер.СвойствоОпределено(ВыборкаПоСчетам, "КНОД");
		
		ВыборкаПоСубконто1 = ВыборкаПоСчетам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаПоСубконто1.Следующий() Цикл
			ВыводитьУзелСубконто1 = Ложь;
			Если ВыборкаПоСубконто1.Субконто1 <> Null И ВыборкаПоСубконто1.Субконто1 <> "NULL" Тогда
				ВыводитьУзелСубконто1 = Истина;
				
				Компоновщик.ЗаписатьНачалоЭлемента("Субконто1");
				
				ПредставлениеСубконто1 = Строка(ВыборкаПоСубконто1.Субконто1);
				ПредставлениеСубконто = ПредставлениеСубконтоИННКПП(ВыборкаПоСубконто1, ПредставлениеСубконто1, 1);
				
				ВывестиАтрибут(Компоновщик, "Представление", ПредставлениеСубконто, "Т,1000");
				ВывестиАтрибут(Компоновщик, "ТипСубконто", Строка(ТипЗнч(ВыборкаПоСубконто1.Субконто1)), "Т,100");
				ВывестиНеобязательныйАтрибут(Компоновщик, "ИНН", ИННКонтрагентаПоСубконто(ВыборкаПоСубконто1, 1), "Т,12");
				
				ВывестиАтрибут(Компоновщик, "СНД", ВыборкаПоСубконто1.СНД, "Ч,15,2");
				ВывестиАтрибут(Компоновщик, "СНК", ВыборкаПоСубконто1.СНК, "Ч,15,2");
				ВывестиАтрибут(Компоновщик, "ДО",  ВыборкаПоСубконто1.ДО,  "Ч,15,2");
				ВывестиАтрибут(Компоновщик, "КО",  ВыборкаПоСубконто1.КО,  "Ч,15,2");
				ВывестиАтрибут(Компоновщик, "СКД", ВыборкаПоСубконто1.СКД, "Ч,15,2");
				ВывестиАтрибут(Компоновщик, "СКК", ВыборкаПоСубконто1.СКК, "Ч,15,2");
				
				Если ОтражатьКоличество Тогда
					ВывестиАтрибут(Компоновщик, "КНОД", ВыборкаПоСубконто1.КНОД, "Ч,15,3");
					ВывестиАтрибут(Компоновщик, "КНОК", ВыборкаПоСубконто1.КНОК, "Ч,15,3");
					ВывестиАтрибут(Компоновщик, "КДО",  ВыборкаПоСубконто1.КДО,  "Ч,15,3");
					ВывестиАтрибут(Компоновщик, "ККО",  ВыборкаПоСубконто1.ККО,  "Ч,15,3");
					ВывестиАтрибут(Компоновщик, "ККОД", ВыборкаПоСубконто1.ККОД, "Ч,15,3");
					ВывестиАтрибут(Компоновщик, "ККОК", ВыборкаПоСубконто1.ККОК, "Ч,15,3");
				КонецЕсли;
				
			КонецЕсли;
			
			ВыборкаПоСубконто2 = ВыборкаПоСубконто1.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаПоСубконто2.Следующий() Цикл
				ВыводитьУзелСубконто2 = Ложь;
				Если ВыборкаПоСубконто2.Субконто2 <> Null И ВыборкаПоСубконто2.Субконто2 <> "NULL" Тогда
					ВыводитьУзелСубконто2 = Истина;
					
					Компоновщик.ЗаписатьНачалоЭлемента("Субконто2");
					
					ПредставлениеСубконто2 = Строка(ВыборкаПоСубконто2.Субконто2);
					ПредставлениеСубконто = ПредставлениеСубконтоИННКПП(ВыборкаПоСубконто2, ПредставлениеСубконто2, 1);
					
					ВывестиАтрибут(Компоновщик, "Представление", ПредставлениеСубконто, "Т,1000");
					ВывестиАтрибут(Компоновщик, "ТипСубконто", Строка(ТипЗнч(ВыборкаПоСубконто2.Субконто2)), "Т,100");
					ВывестиНеобязательныйАтрибут(Компоновщик, "ИНН", ИННКонтрагентаПоСубконто(ВыборкаПоСубконто2, 1), "Т,12");
					
					ВывестиАтрибут(Компоновщик, "СНД", ВыборкаПоСубконто2.СНД, "Ч,15,2");
					ВывестиАтрибут(Компоновщик, "СНК", ВыборкаПоСубконто2.СНК, "Ч,15,2");
					ВывестиАтрибут(Компоновщик, "ДО",  ВыборкаПоСубконто2.ДО,  "Ч,15,2");
					ВывестиАтрибут(Компоновщик, "КО",  ВыборкаПоСубконто2.КО,  "Ч,15,2");
					ВывестиАтрибут(Компоновщик, "СКД", ВыборкаПоСубконто2.СКД, "Ч,15,2");
					ВывестиАтрибут(Компоновщик, "СКК", ВыборкаПоСубконто2.СКК, "Ч,15,2");
					
					Если ОтражатьКоличество Тогда
						ВывестиАтрибут(Компоновщик, "КНОД", ВыборкаПоСубконто2.КНОД, "Ч,15,3");
						ВывестиАтрибут(Компоновщик, "КНОК", ВыборкаПоСубконто2.КНОК, "Ч,15,3");
						ВывестиАтрибут(Компоновщик, "КДО",  ВыборкаПоСубконто2.КДО,  "Ч,15,3");
						ВывестиАтрибут(Компоновщик, "ККО",  ВыборкаПоСубконто2.ККО,  "Ч,15,3");
						ВывестиАтрибут(Компоновщик, "ККОД", ВыборкаПоСубконто2.ККОД, "Ч,15,3");
						ВывестиАтрибут(Компоновщик, "ККОК", ВыборкаПоСубконто2.ККОК, "Ч,15,3");
					КонецЕсли;
					
				КонецЕсли;
				
				ВыборкаПоКорСчетам = ВыборкаПоСубконто2.Выбрать();
				
				Пока ВыборкаПоКорСчетам.Следующий() Цикл
					Если ВыборкаПоКорСчетам.Уровень = 2 Тогда
						ВывестиУниверсальныйУзел_Корреспонденция(Компоновщик, ВыборкаПоКорСчетам);
					КонецЕсли;
					
				КонецЦикла;
				
				Если ВыводитьУзелСубконто2 Тогда
					Компоновщик.ЗаписатьКонецЭлемента(); // Субконто2
				КонецЕсли;
				
			КонецЦикла;
			
			Если ВыводитьУзелСубконто1 Тогда
				Компоновщик.ЗаписатьКонецЭлемента(); // Субконто1
			КонецЕсли;
			
		КонецЦикла;
		
		Компоновщик.ЗаписатьКонецЭлемента(); // Счет
		
	КонецЦикла;
	
	Компоновщик.ЗаписатьКонецЭлемента(); // АнализСчета
	
КонецПроцедуры

#КонецОбласти

#Область ВыводОперацийПоБанковскимСчетам

Процедура ВывестиУзел_ОперБанкСч(Компоновщик, ПараметрыВыгрузки)
	
	Организация     = ПараметрыВыгрузки.Организация;
	ПараметрыОтчета = ПараметрыВыгрузки.ПараметрыОтчетов.БанковскиеОперации;
	
	Если НЕ ПараметрыОтчета.Отправлять Тогда
		Возврат;
	КонецЕсли;
	
	НачалоПериода = ПараметрыОтчета.НачалоПериода;
	КонецПериода =  ПараметрыОтчета.КонецПериода;
	
	Компоновщик.ЗаписатьНачалоЭлемента("ОперБанкСч");
	
	// Код периода задается по последнему месяцу
	АтрибутыПериода = АтрибутыПериода(НачалоМесяца(КонецПериода), КонецПериода);
	
	ВывестиАтрибут(Компоновщик, "Период",   АтрибутыПериода.КодПериода);
	ВывестиАтрибут(Компоновщик, "ОтчетГод", АтрибутыПериода.ГодПериода);
	ВывестиАтрибут(Компоновщик, "ОКЕИ",       "383");
	
	ПараметрыЗапросаСведений = Новый Структура;
	ПараметрыЗапросаСведений.Вставить("Организация", Организация);
	ПараметрыЗапросаСведений.Вставить("НачалоПериода", НачалоДня(НачалоПериода));
	ПараметрыЗапросаСведений.Вставить("КонецПериода", КонецДня(КонецПериода));
	
	СведенияОбОперациях = СведенияОбОперацияхПоБанковскимСчетам(ПараметрыЗапросаСведений);
	
	СводныеСведения = СведенияОбОперациях.Расчетные.СводныеСведения;
	ДетальныеСведения = СведенияОбОперациях.Расчетные.ДетальныеСведения;
	ВывестиУзел_ОперБанкСч_СчетУчета(Компоновщик, СводныеСведения, ДетальныеСведения);
	
	СводныеСведения = СведенияОбОперациях.Валютные.СводныеСведения;
	ДетальныеСведения = СведенияОбОперациях.Валютные.ДетальныеСведения;
	ВывестиУзел_ОперБанкСч_СчетУчета(Компоновщик, СводныеСведения, ДетальныеСведения);
	
	СводныеСведения = СведенияОбОперациях.Специальные.СводныеСведения;
	ДетальныеСведения = СведенияОбОперациях.Специальные.ДетальныеСведения;
	ВывестиУзел_ОперБанкСч_СчетУчета(Компоновщик, СводныеСведения, ДетальныеСведения);
	
	Компоновщик.ЗаписатьКонецЭлемента(); // ОперБанкСч
	
КонецПроцедуры

Процедура ВывестиУзел_ОперБанкСч_СчетУчета(Компоновщик, СводныеСведения, ДетальныеСведения)
	
	СтрокаСчетаВерхнегоУровня = СводныеСведения.Строки[0];
	
	Компоновщик.ЗаписатьНачалоЭлемента("СчетУчета");
	
	ВывестиАтрибут(Компоновщик, "КодСчета",        СтрокаСчетаВерхнегоУровня.Представление);
	ВывестиАтрибут(Компоновщик, "СальдоНачальное", СтрокаСчетаВерхнегоУровня.СальдоНачальноеДт, "Ч,15,2");
	ВывестиАтрибут(Компоновщик, "ОборотыДт",       СтрокаСчетаВерхнегоУровня.ОборотыДт, "Ч,15,2");
	ВывестиАтрибут(Компоновщик, "ОборотыКт",       СтрокаСчетаВерхнегоУровня.ОборотыКт, "Ч,15,2");
	ВывестиАтрибут(Компоновщик, "СальдоКонечное",  СтрокаСчетаВерхнегоУровня.СальдоКонечноеДт, "Ч,15,2");
	
	ВывестиУзел_ОперБанкСч_СчетУчета_БанкСч(Компоновщик, СтрокаСчетаВерхнегоУровня.Строки, ДетальныеСведения);
	
	Компоновщик.ЗаписатьКонецЭлемента(); // СчетУчета
	
КонецПроцедуры

Процедура ВывестиУзел_ОперБанкСч_СчетУчета_БанкСч(Компоновщик, СтрокиСчетов, ДетальныеСведения)
	
	ОписаниеТипаБанковскиеСчета = ОтчетностьВБанкиСлужебный.ТипыСущности("БанковскиеСчета");
	
	ВыборкаПоБанковскимСчетам = ДетальныеСведения.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Для Каждого СтрокаСчета Из СтрокиСчетов Цикл
		Если ОписаниеТипаБанковскиеСчета.СодержитТип(ТипЗнч(СтрокаСчета.Счет)) Тогда
			Компоновщик.ЗаписатьНачалоЭлемента("БанкСч");
			
			ВывестиНеобязательныйАтрибут(Компоновщик, "Наименование", СтрокаСчета.Представление, "Т,100");
			ВывестиАтрибут(Компоновщик, "Номер", СтрокаСчета.НомерСчета, "Т,34");
			ВывестиАтрибут(Компоновщик, "БИК", СтрокаСчета.БИКБанка, "Т,9");
			ВывестиНеобязательныйАтрибут(Компоновщик, "Банк", СтрокаСчета.НаименованиеБанка, "Т,100");
			Если РегламентированнаяОтчетностьКлиентСервер.СвойствоОпределено(СтрокаСчета, "СВИФТБИК") Тогда
				ВывестиНеобязательныйАтрибут(Компоновщик, "SWIFT", СтрокаСчета.СВИФТБИК, "Т,11");
				ВывестиНеобязательныйАтрибут(Компоновщик, "КодСтраны", СтрокаСчета.КодСтраны, "Т,3");
			КонецЕсли;
			
			Если СтрокаСчета.Валютный Тогда
				ВывестиАтрибут(Компоновщик, "Валюта", СтрокаСчета.КодВалюты, "Т,3");
			КонецЕсли;
			
			ВывестиАтрибут(Компоновщик, "СальдоНачальное", СтрокаСчета.СальдоНачальноеДт, "Ч,15,2");
			ВывестиАтрибут(Компоновщик, "ОборотыДт"      , СтрокаСчета.ОборотыДт, "Ч,15,2");
			ВывестиАтрибут(Компоновщик, "ОборотыКт"      , СтрокаСчета.ОборотыКт, "Ч,15,2");
			ВывестиАтрибут(Компоновщик, "СальдоКонечное" , СтрокаСчета.СальдоКонечноеДт, "Ч,15,2");
			
			Если СтрокаСчета.Валютный Тогда
				ВывестиАтрибут(Компоновщик, "СальдоНачальноеВал", СтрокаСчета.СальдоНачальноеВалДт, "Ч,15,2");
				ВывестиАтрибут(Компоновщик, "ОборотыДтВал"      , СтрокаСчета.ОборотыВалДт, "Ч,15,2");
				ВывестиАтрибут(Компоновщик, "ОборотыКтВал"      , СтрокаСчета.ОборотыВалКт, "Ч,15,2");
				ВывестиАтрибут(Компоновщик, "СальдоКонечноеВал" , СтрокаСчета.СальдоКонечноеВалДт, "Ч,15,2");
			КонецЕсли;
			
			СтруктураПоиска = Новый Структура;
			СтруктураПоиска.Вставить("БанковскийСчет", СтрокаСчета.Счет);
			ВыборкаПоБанковскимСчетам.Сбросить();
			Если ВыборкаПоБанковскимСчетам.НайтиСледующий(СтруктураПоиска) Тогда
				ВыборкаОпераций = ВыборкаПоБанковскимСчетам.Выбрать();
				Пока ВыборкаОпераций.Следующий() Цикл
					СтрокаСекции = ВыборкаОпераций;
					
					Компоновщик.ЗаписатьНачалоЭлемента("Операция");
					
					ТипДокумента = Строка(ТипЗнч(СтрокаСекции.Документ));
					ИдДокумента = СтрокаСекции.Документ.УникальныйИдентификатор();
					
					ВывестиАтрибут(Компоновщик, "ТипДок", ТипДокумента, "Т,255");
					ВывестиАтрибут(Компоновщик, "ИдДок", ИдДокумента, "Т,36");
					
					ВывестиАтрибут(Компоновщик, "Дата", СтрокаСекции.ДатаОперации, "Д");
					Если ЗначениеЗаполнено(СтрокаСекции.НомерДокумента)
					   И ЗначениеЗаполнено(СтрокаСекции.ДатаДокумента) Тогда
						ВывестиНеобязательныйАтрибут(Компоновщик, "ДокНомер", СтрокаСекции.НомерДокумента, "Т,255");
						ВывестиНеобязательныйАтрибут(Компоновщик, "ДокДата", СтрокаСекции.ДатаДокумента, "Д");
					КонецЕсли;
					
					ВывестиНеобязательныйАтрибут(Компоновщик, "ВидОперации", СтрокаСекции.ВидОперации, "Т,255");
					ВывестиНеобязательныйАтрибут(Компоновщик, "Назначение", СтрокаСекции.Назначение, "Т,255");
					
					ВывестиАтрибут(Компоновщик, "ВидДвижения", СтрокаСекции.ВидДвиженияПоСчету, "Ч,1,0");
					
					ВывестиАтрибут(Компоновщик, "СчетДт", СтрокаСекции.СчетДтКод, "Т,8");
					ВывестиАтрибут(Компоновщик, "СчетКт", СтрокаСекции.СчетКтКод, "Т,8");
					ВывестиАтрибут(Компоновщик, "Сумма", СтрокаСекции.Сумма, "Ч,15,2");
					
					Если СтрокаСчета.Валютный Тогда
						ВывестиАтрибут(Компоновщик, "СуммаВал", СтрокаСекции.СуммаВал, "Ч,15,2");
					КонецЕсли;
					
					Если ЗначениеЗаполнено(СтрокаСекции.Контрагент) Тогда
						Компоновщик.ЗаписатьНачалоЭлемента("Контрагент");
						
						ВывестиАтрибут(Компоновщик, "Наименование", СтрокаСекции.Контрагент, "Т,250");
						
						Если ЗначениеЗаполнено(СтрокаСекции.ИНН) Тогда
							Если СтрДлина(СтрокаСекции.ИНН) = 10 И СтрДлина(СтрокаСекции.КПП) = 9 Тогда
								ВывестиАтрибут(Компоновщик, "ИНН", СтрокаСекции.ИНН, "Т,10");
								ВывестиАтрибут(Компоновщик, "КПП", СтрокаСекции.КПП, "Т,9");
								
							ИначеЕсли СтрДлина(СтрокаСекции.ИНН) = 12 Тогда
								ВывестиАтрибут(Компоновщик, "ИНН", СтрокаСекции.ИНН, "Т,12");
								
							КонецЕсли;
							
						КонецЕсли;
						
						ВывестиНеобязательныйАтрибут(Компоновщик, "Договор", СтрокаСекции.Договор, "Т,255");
						
						ВывестиНеобязательныйАтрибут(Компоновщик, "НомерСчета", СтрокаСекции.СчетКонтрагентаНомер, "Т,34");
						ВывестиНеобязательныйАтрибут(Компоновщик, "БИК",        СтрокаСекции.СчетКонтрагентаБИК, "Т,9");
						Если РегламентированнаяОтчетностьКлиентСервер.СвойствоОпределено(СтрокаСекции, "СчетКонтрагентаСВИФТБИК") Тогда
							ВывестиНеобязательныйАтрибут(Компоновщик, "SWIFT",      СтрокаСекции.СчетКонтрагентаСВИФТБИК, "Т,11");
							ВывестиНеобязательныйАтрибут(Компоновщик, "КодСтраны",  СтрокаСекции.СчетКонтрагентаКодСтраны, "Т,3");
						КонецЕсли;
						
						Компоновщик.ЗаписатьКонецЭлемента(); // Контрагент
						
					КонецЕсли;
					
					Если ЗначениеЗаполнено(СтрокаСекции.ПП_КБК_104) Тогда
						Компоновщик.ЗаписатьНачалоЭлемента("ПлатПор");
						
						ВывестиНеобязательныйАтрибут(Компоновщик, "СтатПлат",  СтрокаСекции.ПП_СтатусПлательщика_101, "Т");
						ВывестиНеобязательныйАтрибут(Компоновщик, "КБК",       СтрокаСекции.ПП_КБК_104, "Т,20");
						ВывестиНеобязательныйАтрибут(Компоновщик, "ОКТМО",     СтрокаСекции.ПП_ОКАТО_105, "Т");
						ВывестиНеобязательныйАтрибут(Компоновщик, "Основание", СтрокаСекции.ПП_Основание_106, "Т");
						ВывестиНеобязательныйАтрибут(Компоновщик, "Период",    СтрокаСекции.ПП_Период_107, "Т");
						ВывестиНеобязательныйАтрибут(Компоновщик, "НомДок",  СтрокаСекции.ПП_Номер_108, "Т");
						ВывестиНеобязательныйАтрибут(Компоновщик, "ДатаДок", СтрокаСекции.ПП_Дата_109, "Т");
						
						Компоновщик.ЗаписатьКонецЭлемента(); // ПлатПор
						
					КонецЕсли;
					
					Компоновщик.ЗаписатьКонецЭлемента(); // Операция
					
				КонецЦикла;
				
			КонецЕсли;
			
			Компоновщик.ЗаписатьКонецЭлемента(); // БанкСч
			
		Иначе
			ВывестиУзел_ОперБанкСч_СчетУчета_БанкСч(Компоновщик, СтрокаСчета.Строки, ДетальныеСведения);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция СведенияОбОперацияхПоБанковскимСчетам(Параметры)
	
	СведенияОбОперациях = Новый Структура;
	СведенияОбОперациях.Вставить("Расчетные",   Новый Структура("СчетУчета, СводныеСведения, ДетальныеСведения"));
	СведенияОбОперациях.Вставить("Валютные",    Новый Структура("СчетУчета, СводныеСведения, ДетальныеСведения"));
	СведенияОбОперациях.Вставить("Специальные", Новый Структура("СчетУчета, СводныеСведения, ДетальныеСведения"));
	
	ОтчетностьВБанкиСлужебныйПереопределяемый.ЗаполнитьСведенияОбОперацияхПоБанковскимСчетам(Параметры, СведенияОбОперациях);
	
	Возврат СведенияОбОперациях;
	
КонецФункции

#КонецОбласти

Функция АтрибутыПериода(НачалоПериода, КонецПериода)
	
	АтрибутыПериода = Новый Структура;
	
	АтрибутыПериода.Вставить("ГодПериода", Формат(Год(КонецПериода), "ЧЦ=4; ЧВН=; ЧГ=0"));
	
	КодПериода = "00"; // нестандартный период
	
	ЭтоМесяц = (НачалоПериода = НачалоМесяца(КонецПериода));
	ЭтоКвартал = (НачалоПериода = НачалоКвартала(КонецПериода));
	ЭтоПериодСНачалаГода = (НачалоПериода = НачалоГода(КонецПериода));
	
	Если ЭтоМесяц Тогда
		КодыМесяцев = СтрРазделить("35,36,21,38,39,31,41,42,33,44,45,34", ",", Ложь);
		КодПериода = КодыМесяцев[Месяц(КонецПериода) - 1];
		
	ИначеЕсли ЭтоКвартал Тогда
		КодыКварталов = СтрРазделить("51,52,54,56", ",", Ложь);
		НомерКвартала = Месяц(КонецКвартала(КонецПериода)) / 3;
		КодПериода = КодыКварталов[НомерКвартала - 1];
		
	ИначеЕсли ЭтоПериодСНачалаГода Тогда
		КодыПериодовСНачалаГода = СтрРазделить("51,53,55,57", ",", Ложь); // 3 месяца совпадают с 1 кварталом
		НомерКвартала = Месяц(КонецКвартала(КонецПериода)) / 3;
		КодПериода = КодыПериодовСНачалаГода[НомерКвартала - 1];
		
	КонецЕсли;
	
	АтрибутыПериода.Вставить("КодПериода", КодПериода);
	
	Возврат АтрибутыПериода;
	
КонецФункции

Функция ДействуетСВерсииФормата(Знач НомерВерсии, Знач ВерсияОтчета) Экспорт
	
	НомерВерсии  = Число(СтрЗаменить(НомерВерсии, ".", ""));
	ВерсияОтчета = Число(СтрЗаменить(ВерсияОтчета, ".", ""));
	
	Возврат (ВерсияОтчета >= НомерВерсии);
	
КонецФункции

Функция ПредставлениеСубконтоИННКПП(Выборка, Знач ПредставлениеСубконто, НомерСубконто)
	
	ПредставлениеИНН = СокрЛП(Выборка["Субконто" + НомерСубконто + "ИНН"]);
	ПредставлениеКПП = СокрЛП(Выборка["Субконто" + НомерСубконто + "КПП"]);
	
	Если ЗначениеЗаполнено(ПредставлениеИНН) И ЗначениеЗаполнено(ПредставлениеКПП)
	   И СтрДлина(ПредставлениеИНН) = 10 И СтрДлина(ПредставлениеКПП) = 9 Тогда
		// Корректно указанные ИНН и КПП юридического лица.
		ПредставлениеСубконто = ПредставлениеСубконто
		                      + " " + ПредставлениеИНН
		                      + " " + ПредставлениеКПП;
		
	ИначеЕсли ЗначениеЗаполнено(ПредставлениеИНН) И СтрДлина(ПредставлениеИНН) = 12 Тогда
		// Корректно указанный ИНН физического лица.
		ПредставлениеСубконто = ПредставлениеСубконто
		                      + " " + ПредставлениеИНН;
		
	КонецЕсли;
	
	Возврат ПредставлениеСубконто;
	
КонецФункции

Функция ИННКонтрагентаПоСубконто(Выборка, НомерСубконто)
	
	ИНН = "";
	
	ИмяКолонкиИНН = "Субконто" + НомерСубконто + "ИНН";
	
	Если Выборка.Владелец().Колонки.Найти(ИмяКолонкиИНН) <> Неопределено Тогда
		ПредставлениеИНН = СокрЛП(Выборка[ИмяКолонкиИНН]);
		
		Если ЗначениеЗаполнено(ПредставлениеИНН)
		   И (СтрДлина(ПредставлениеИНН) = 10 ИЛИ СтрДлина(ПредставлениеИНН) = 12) Тогда
			ИНН = ПредставлениеИНН;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ИНН;
	
КонецФункции

Процедура ВывестиУниверсальныйУзел_Корреспонденция(Компоновщик, Корреспонденция)
	
	Компоновщик.ЗаписатьНачалоЭлемента("Корреспонденция");
	
	ВывестиАтрибут(Компоновщик, "КодСчета", Корреспонденция.КодКорСчета, "Т,8");
	ВывестиАтрибут(Компоновщик, "ДО", Корреспонденция.КорДО, "Ч,15,2");
	ВывестиАтрибут(Компоновщик, "КО", Корреспонденция.КорКО, "Ч,15,2");
	
	Компоновщик.ЗаписатьКонецЭлемента(); // Корреспонденция
	
КонецПроцедуры

#КонецОбласти

#Область КомпоновщикXML

Функция НовыйКомпоновщикXML(ИмяФайла, Кодировка = "windows-1251")
	
	КомпоновщикXML = Новый ЗаписьXML();
	КомпоновщикXML.ОткрытьФайл(ИмяФайла, Кодировка);
	КомпоновщикXML.ЗаписатьОбъявлениеXML();
	
	Возврат КомпоновщикXML;
	
КонецФункции

Процедура ВывестиАтрибут(КомпоновщикXML, ИмяАтрибута, ЗначениеАтрибута, ТипАтрибута = "Т")
	
	ОписаниеТипаАтрибута = СтрРазделить(ТипАтрибута, ",");
	
	Если ОписаниеТипаАтрибута[0] = "Д" Тогда
		ПредставлениеЗначения = Формат(ЗначениеАтрибута, "ДФ=dd.MM.yyyy");
		
	ИначеЕсли ОписаниеТипаАтрибута[0] = "Т" Тогда
		ПредставлениеЗначения = СокрЛП(ЗначениеАтрибута);
		Если ОписаниеТипаАтрибута.Количество() >= 2 Тогда
			МаксимальнаяДлина = Число(ОписаниеТипаАтрибута[1]);
			ПредставлениеЗначения = Лев(ПредставлениеЗначения, МаксимальнаяДлина);
		КонецЕсли;
		
	ИначеЕсли ОписаниеТипаАтрибута[0] = "Ч" Тогда
		СтрокаФормата = "ЧРД=.;ЧН=0;ЧГ=;";
		
		Если ОписаниеТипаАтрибута.Количество() >= 2 Тогда
			РазмерностьПолная = Число(ОписаниеТипаАтрибута[1]);
			СтрокаФормата = СтрокаФормата + "ЧЦ=" + Формат(РазмерностьПолная, "ЧГ=") + ";";
		КонецЕсли;
		
		Если ОписаниеТипаАтрибута.Количество() >= 3 Тогда
			РазмерностьДробнойЧасти = Число(ОписаниеТипаАтрибута[2]);
			СтрокаФормата = СтрокаФормата + "ЧДЦ=" + Формат(РазмерностьДробнойЧасти, "ЧГ=") + ";";
		КонецЕсли;
		
		ПредставлениеЗначения = Формат(ЗначениеАтрибута, СтрокаФормата);
		
	ИначеЕсли ОписаниеТипаАтрибута[0] = "П" Тогда
		ПредставлениеЗначения = Строка(ЗначениеАтрибута);
		
	Иначе
		ПредставлениеЗначения = СокрЛП(ЗначениеАтрибута);
		
	КонецЕсли;
	
	КомпоновщикXML.ЗаписатьАтрибут(ИмяАтрибута, ПредставлениеЗначения);
	
КонецПроцедуры

Процедура ВывестиНеобязательныйАтрибут(КомпоновщикXML, ИмяАтрибута, ЗначениеАтрибута, ТипАтрибута = "Т")
	
	Если ЗначениеЗаполнено(ЗначениеАтрибута) Тогда
		ВывестиАтрибут(КомпоновщикXML, ИмяАтрибута, ЗначениеАтрибута, ТипАтрибута);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
