#Область СлужебныйПрограммныйИнтерфейс

#Область Свойства

// См. УправлениеСвойствамиПереопределяемый.ПриПолученииПредопределенныхНаборовСвойств.
Процедура ПриПолученииПредопределенныхНаборовСвойств(Наборы) Экспорт
	
	УправлениеСвойствамиБЗК.ЗарегистрироватьНаборСвойств(Наборы, "d42dbf34-9802-11e9-80cd-4cedfb43b11a", Метаданные.Документы.ОграничениеВзысканий);
	УправлениеСвойствамиБЗК.ЗарегистрироватьНаборСвойств(Наборы, "d42dbf52-9802-11e9-80cd-4cedfb43b11a", Метаданные.Документы.ПогашениеЗадолженностиПоВзысканиям);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Функция РезультатОграниченияВзысканий(МенеджерВременныхТаблиц) Экспорт 
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ВидыНачислений", ВидыНачисленийЗаработнойПлаты());
	Запрос.УстановитьПараметр("ВидыНДФЛ", ВидыНДФЛДополненияРасчетнойБазыУдержаний());
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	УдержанияПоФизическимЛицам.ФизическоеЛицо КАК ФизическоеЛицо,
	               |	УдержанияПоФизическимЛицам.Подразделение КАК Подразделение,
	               |	УдержанияПоФизическимЛицам.НачислениеУдержание КАК НачислениеУдержание,
	               |	УдержанияПоФизическимЛицам.ИдентификаторСтроки КАК ИдентификаторСтроки,
	               |	УдержанияПоФизическимЛицам.СтатьяФинансирования КАК СтатьяФинансирования,
	               |	УдержанияПоФизическимЛицам.СтатьяРасходов КАК СтатьяРасходов,
	               |	УдержанияПоФизическимЛицам.ДокументОснование КАК ДокументОснование,
	               |	УдержанияПоФизическимЛицам.Сумма КАК Сумма,
	               |	УдержанияПоФизическимЛицам.ДатаНачала КАК ДатаНачала,
	               |	УдержанияПоФизическимЛицам.ДатаОкончания КАК ДатаОкончания,
	               |	УдержанияПоФизическимЛицам.Контрагент КАК Контрагент
	               |ПОМЕСТИТЬ ВТИсполнительныеЛисты
	               |ИЗ
	               |	ВТУдержанияПоФизическимЛицам КАК УдержанияПоФизическимЛицам
	               |ГДЕ
	               |	УдержанияПоФизическимЛицам.КатегорияУдержания = ЗНАЧЕНИЕ(Перечисление.КатегорииУдержаний.ИсполнительныйЛист)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ИсполнительныеЛисты.ДатаНачала КАК Период,
	               |	ИсполнительныеЛисты.ДокументОснование КАК ИсполнительныйДокумент
	               |ПОМЕСТИТЬ ВТИсполнительныеЛистыПериоды
	               |ИЗ
	               |	ВТИсполнительныеЛисты КАК ИсполнительныеЛисты";
	
	Запрос.Выполнить();			   
				   
	ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра("ВТИсполнительныеЛистыПериоды","Период,ИсполнительныйДокумент");
	ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТИмяРегистраСрезПоследних("УсловияУдержанияПоИсполнительномуДокументу", Запрос.МенеджерВременныхТаблиц, Истина, ОписаниеФильтра);
	
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	УсловияУдержанияПоИсполнительномуДокументу.Период КАК Период,
	               |	СохраняемыйЗаработок.ФизическоеЛицо КАК ФизическоеЛицо,
	               |	СохраняемыйЗаработок.ИсполнительныйДокумент КАК ИсполнительныйДокумент,
	               |	СохраняемыйЗаработок.ПрожиточныйМинимум КАК ПрожиточныйМинимум,
	               |	СохраняемыйЗаработок.КоличествоПрожиточныхМинимумов КАК КоличествоПрожиточныхМинимумов
	               |ПОМЕСТИТЬ ВТСохраняемыйЗаработок
	               |ИЗ
	               |	РегистрСведений.СохраняемыйЗаработокПриВзысканиях КАК СохраняемыйЗаработок
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТУсловияУдержанияПоИсполнительномуДокументуСрезПоследних КАК УсловияУдержанияПоИсполнительномуДокументу
	               |		ПО СохраняемыйЗаработок.ИсполнительныйДокумент = УсловияУдержанияПоИсполнительномуДокументу.ИсполнительныйДокумент
	               |			И СохраняемыйЗаработок.Период = УсловияУдержанияПоИсполнительномуДокументу.ПериодЗаписи
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	МАКСИМУМ(СохраняемыйЗаработок.Период) КАК Период,
	               |	СохраняемыйЗаработок.ПрожиточныйМинимум КАК ПрожиточныйМинимум
	               |ПОМЕСТИТЬ ВТПрожиточныеМинимумыОтбор
	               |ИЗ
	               |	ВТСохраняемыйЗаработок КАК СохраняемыйЗаработок
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СохраняемыйЗаработок.ПрожиточныйМинимум";
	
	Запрос.Выполнить();			   
	
	ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра("ВТПрожиточныеМинимумыОтбор", "Период, ПрожиточныйМинимум");
	ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТИмяРегистраСрезПоследних("ВеличинаПрожиточногоМинимума", Запрос.МенеджерВременныхТаблиц, Истина, ОписаниеФильтра);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	СохраняемыйЗаработок.ФизическоеЛицо КАК ФизическоеЛицо,
	               |	СохраняемыйЗаработок.ИсполнительныйДокумент КАК ИсполнительныйДокумент,
	               |	СУММА(СохраняемыйЗаработок.КоличествоПрожиточныхМинимумов * ВеличинаПрожиточногоМинимума.Размер) КАК Заработок
	               |ПОМЕСТИТЬ ВТСохраняемыйЗаработокДанныеДокументов
	               |ИЗ
	               |	ВТСохраняемыйЗаработок КАК СохраняемыйЗаработок
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТВеличинаПрожиточногоМинимумаСрезПоследних КАК ВеличинаПрожиточногоМинимума
	               |		ПО СохраняемыйЗаработок.ПрожиточныйМинимум = ВеличинаПрожиточногоМинимума.ПрожиточныйМинимум
	               |			И СохраняемыйЗаработок.Период = ВеличинаПрожиточногоМинимума.Период
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СохраняемыйЗаработок.ФизическоеЛицо,
	               |	СохраняемыйЗаработок.ИсполнительныйДокумент
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СохраняемыйЗаработок.ФизическоеЛицо КАК ФизическоеЛицо,
	               |	МАКСИМУМ(СохраняемыйЗаработок.Заработок) КАК Заработок
	               |ПОМЕСТИТЬ ВТСохраняемыйЗаработокФизическиеЛица
	               |ИЗ
	               |	ВТСохраняемыйЗаработокДанныеДокументов КАК СохраняемыйЗаработок
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СохраняемыйЗаработок.ФизическоеЛицо
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СохраняемыйЗаработок.ФизическоеЛицо КАК ФизическоеЛицо,
	               |	СохраняемыйЗаработок.Заработок - ЕСТЬNULL(ДанныеСохраняемогоЗаработка.Сумма, 0) КАК Заработок
	               |ПОМЕСТИТЬ ВТСохраняемыйЗаработокОстаток
	               |ИЗ
	               |	ВТСохраняемыйЗаработокФизическиеЛица КАК СохраняемыйЗаработок
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеСохраняемогоЗаработка КАК ДанныеСохраняемогоЗаработка
	               |		ПО СохраняемыйЗаработок.ФизическоеЛицо = ДанныеСохраняемогоЗаработка.ФизическоеЛицо
	               |ГДЕ
	               |	СохраняемыйЗаработок.Заработок - ЕСТЬNULL(ДанныеСохраняемогоЗаработка.Сумма, 0) > 0
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СохраняемыйЗаработокОстаток.ФизическоеЛицо КАК ФизическоеЛицо,
	               |	СохраняемыйЗаработокОстаток.Заработок КАК Заработок
	               |ИЗ
	               |	ВТСохраняемыйЗаработокОстаток КАК СохраняемыйЗаработокОстаток";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	СохраняемыйЗаработок = Новый Соответствие;
	Пока Выборка.Следующий() Цикл 
		СохраняемыйЗаработок.Вставить(Выборка.ФизическоеЛицо, Выборка.Заработок);
	КонецЦикла;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	УдержанияПоФизическимЛицам.ФизическоеЛицо КАК ФизическоеЛицо,
	               |	УдержанияПоФизическимЛицам.Подразделение КАК Подразделение,
	               |	УдержанияПоФизическимЛицам.НачислениеУдержание КАК Удержание,
	               |	УдержанияПоФизическимЛицам.ИдентификаторСтроки КАК ИдентификаторСтроки,
				   |	УдержанияПоФизическимЛицам.СтатьяФинансирования КАК СтатьяФинансирования,
				   |	УдержанияПоФизическимЛицам.СтатьяРасходов КАК СтатьяРасходов,
	               |	УдержанияПоФизическимЛицам.ДокументОснование КАК ДокументОснование,
	               |	УдержанияПоФизическимЛицам.Сумма КАК Сумма,
	               |	УдержанияПоФизическимЛицам.ДатаНачала КАК ДатаНачала,
	               |	УдержанияПоФизическимЛицам.ДатаОкончания КАК ДатаОкончания,
	               |	УсловияУдержанияПоИсполнительномуДокументу.Получатель КАК Получатель,
	               |	УсловияУдержанияПоИсполнительномуДокументу.ПлатежныйАгент КАК ПлатежныйАгент
	               |ПОМЕСТИТЬ ВТУдержанияПлатежныхАгентов
	               |ИЗ
	               |	ВТУдержанияПоФизическимЛицам КАК УдержанияПоФизическимЛицам
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТУсловияУдержанияПоИсполнительномуДокументуСрезПоследних КАК УсловияУдержанияПоИсполнительномуДокументу
	               |		ПО УдержанияПоФизическимЛицам.ДокументОснование = УсловияУдержанияПоИсполнительномуДокументу.ИсполнительныйДокумент
	               |			И УдержанияПоФизическимЛицам.ДатаНачала = УсловияУдержанияПоИсполнительномуДокументу.Период
	               |			И (УдержанияПоФизическимЛицам.КатегорияУдержания = ЗНАЧЕНИЕ(Перечисление.КатегорииУдержаний.ВознаграждениеПлатежногоАгента))
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	УдержанияПлатежныхАгентов.ФизическоеЛицо КАК ФизическоеЛицо,
	               |	УдержанияПлатежныхАгентов.Подразделение КАК Подразделение,
	               |	УдержанияПлатежныхАгентов.Удержание КАК Удержание,
	               |	УдержанияПлатежныхАгентов.ИдентификаторСтроки КАК ИдентификаторСтроки,
				   |	УдержанияПлатежныхАгентов.ДокументОснование КАК ДокументОснование,
	               |	УдержанияПлатежныхАгентов.ДатаНачала КАК ДатаНачала,
	               |	УдержанияПлатежныхАгентов.ДатаОкончания КАК ДатаОкончания,
	               |	УдержанияПлатежныхАгентов.Получатель КАК Получатель,
	               |	УдержанияПлатежныхАгентов.ПлатежныйАгент КАК ПлатежныйАгент,
	               |	СУММА(УдержанияПлатежныхАгентов.Сумма) КАК Рассчитано
	               |ИЗ
	               |	ВТУдержанияПлатежныхАгентов КАК УдержанияПлатежныхАгентов
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	УдержанияПлатежныхАгентов.ФизическоеЛицо,
	               |	УдержанияПлатежныхАгентов.Подразделение,
	               |	УдержанияПлатежныхАгентов.Удержание,
	               |	УдержанияПлатежныхАгентов.ИдентификаторСтроки,
				   |	УдержанияПлатежныхАгентов.ДокументОснование,
	               |	УдержанияПлатежныхАгентов.ДатаНачала,
	               |	УдержанияПлатежныхАгентов.ДатаОкончания,
	               |	УдержанияПлатежныхАгентов.Получатель,
	               |	УдержанияПлатежныхАгентов.ПлатежныйАгент";
	
	УдержанияПлатежныхАгентов = Запрос.Выполнить().Выгрузить();
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ИсполнительныеЛисты.ФизическоеЛицо КАК ФизическоеЛицо,
	               |	ИсполнительныеЛисты.НачислениеУдержание КАК Удержание,
	               |	ИсполнительныеЛисты.ИдентификаторСтроки КАК ИдентификаторСтроки,
	               |	ИсполнительныеЛисты.ДокументОснование КАК ДокументОснование,
	               |	МИНИМУМ(ВЫБОР
	               |			КОГДА УсловияУдержанияПоИсполнительномуДокументу.ОчередностьВзыскания = ЗНАЧЕНИЕ(Перечисление.ОчередностьВзысканияУдержаний.ПерваяОчередьПроцент70)
	               |				ТОГДА 0.7
	               |			ИНАЧЕ 0.5
	               |		КОНЕЦ) КАК Коэффициент,
	               |	МИНИМУМ(ВЫБОР
	               |			КОГДА УсловияУдержанияПоИсполнительномуДокументу.ОчередностьВзыскания = ЗНАЧЕНИЕ(Перечисление.ОчередностьВзысканияУдержаний.ПерваяОчередьПроцент70)
	               |					ИЛИ УсловияУдержанияПоИсполнительномуДокументу.ОчередностьВзыскания = ЗНАЧЕНИЕ(Перечисление.ОчередностьВзысканияУдержаний.ПерваяОчередьПроцент50)
	               |				ТОГДА 1
	               |			КОГДА УсловияУдержанияПоИсполнительномуДокументу.ОчередностьВзыскания = ЗНАЧЕНИЕ(Перечисление.ОчередностьВзысканияУдержаний.ВтораяОчередь)
	               |				ТОГДА 2
	               |			КОГДА УсловияУдержанияПоИсполнительномуДокументу.ОчередностьВзыскания = ЗНАЧЕНИЕ(Перечисление.ОчередностьВзысканияУдержаний.ТретьяОчередь)
	               |				ТОГДА 3
	               |			ИНАЧЕ 4
	               |		КОНЕЦ) КАК Очередность,
	               |	СУММА(ИсполнительныеЛисты.Сумма) КАК Рассчитано,
	               |	ИсполнительныеЛисты.Подразделение КАК Подразделение,
	               |	ИсполнительныеЛисты.СтатьяФинансирования КАК СтатьяФинансирования,
	               |	ИсполнительныеЛисты.СтатьяРасходов КАК СтатьяРасходов,
	               |	ИсполнительныеЛисты.ДатаНачала КАК ДатаНачала,
	               |	ИсполнительныеЛисты.ДатаОкончания КАК ДатаОкончания,
	               |	ИсполнительныеЛисты.Контрагент КАК Контрагент,
	               |	УсловияУдержанияПоИсполнительномуДокументу.ПлатежныйАгент КАК ПлатежныйАгент
	               |ПОМЕСТИТЬ ВТОчередностьУдержанийФизическиеЛица
	               |ИЗ
	               |	ВТИсполнительныеЛисты КАК ИсполнительныеЛисты
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТУсловияУдержанияПоИсполнительномуДокументуСрезПоследних КАК УсловияУдержанияПоИсполнительномуДокументу
	               |		ПО ИсполнительныеЛисты.ДокументОснование = УсловияУдержанияПоИсполнительномуДокументу.ИсполнительныйДокумент
	               |			И ИсполнительныеЛисты.ДатаНачала = УсловияУдержанияПоИсполнительномуДокументу.Период
	               |			И (УсловияУдержанияПоИсполнительномуДокументу.ОчередностьВзыскания <> ЗНАЧЕНИЕ(Перечисление.ОчередностьВзысканияУдержаний.ПустаяСсылка))
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ИсполнительныеЛисты.ФизическоеЛицо,
	               |	ИсполнительныеЛисты.НачислениеУдержание,
	               |	ИсполнительныеЛисты.ИдентификаторСтроки,
	               |	ИсполнительныеЛисты.ДокументОснование,
	               |	ИсполнительныеЛисты.Подразделение,
	               |	ИсполнительныеЛисты.СтатьяФинансирования,
	               |	ИсполнительныеЛисты.СтатьяРасходов,
	               |	ИсполнительныеЛисты.ДатаНачала,
	               |	ИсполнительныеЛисты.ДатаОкончания,
	               |	ИсполнительныеЛисты.Контрагент,
	               |	УсловияУдержанияПоИсполнительномуДокументу.ПлатежныйАгент
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	УдержанияПоФизическимЛицам.ФизическоеЛицо,
	               |	УдержанияПоФизическимЛицам.НачислениеУдержание,
	               |	УдержанияПоФизическимЛицам.ИдентификаторСтроки,
	               |	УдержанияПоФизическимЛицам.ДокументОснование,
	               |	0.2,
	               |	5,
	               |	СУММА(УдержанияПоФизическимЛицам.Сумма),
	               |	УдержанияПоФизическимЛицам.Подразделение,
	               |	УдержанияПоФизическимЛицам.СтатьяФинансирования,
	               |	УдержанияПоФизическимЛицам.СтатьяРасходов,
	               |	УдержанияПоФизическимЛицам.ДатаНачала,
	               |	УдержанияПоФизическимЛицам.ДатаОкончания,
	               |	УдержанияПоФизическимЛицам.Контрагент,
	               |	NULL
	               |ИЗ
	               |	ВТУдержанияПоФизическимЛицам КАК УдержанияПоФизическимЛицам
	               |ГДЕ
	               |	УдержанияПоФизическимЛицам.КатегорияУдержания <> ЗНАЧЕНИЕ(Перечисление.КатегорииУдержаний.ИсполнительныйЛист)
	               |	И УдержанияПоФизическимЛицам.ЯвляетсяВзысканием
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	УдержанияПоФизическимЛицам.ФизическоеЛицо,
	               |	УдержанияПоФизическимЛицам.НачислениеУдержание,
	               |	УдержанияПоФизическимЛицам.ИдентификаторСтроки,
	               |	УдержанияПоФизическимЛицам.ДокументОснование,
	               |	УдержанияПоФизическимЛицам.Подразделение,
	               |	УдержанияПоФизическимЛицам.СтатьяФинансирования,
	               |	УдержанияПоФизическимЛицам.СтатьяРасходов,
	               |	УдержанияПоФизическимЛицам.ДатаНачала,
	               |	УдержанияПоФизическимЛицам.ДатаОкончания,
	               |	УдержанияПоФизическимЛицам.Контрагент
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ОчередностьУдержанийФизическиеЛица.ФизическоеЛицо КАК ФизическоеЛицо,
	               |	СУММА(ОчередностьУдержанийФизическиеЛица.Рассчитано) КАК Рассчитано,
	               |	МИНИМУМ(ОчередностьУдержанийФизическиеЛица.Коэффициент) КАК Коэффициент
	               |ПОМЕСТИТЬ ВТУдержанияФизическихЛиц
	               |ИЗ
	               |	ВТОчередностьУдержанийФизическиеЛица КАК ОчередностьУдержанийФизическиеЛица
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ОчередностьУдержанийФизическиеЛица.ФизическоеЛицо
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ОчередностьУдержанийФизическиеЛица.ФизическоеЛицо КАК ФизическоеЛицо
	               |ПОМЕСТИТЬ ВТФизическиеЛица
	               |ИЗ
	               |	ВТОчередностьУдержанийФизическиеЛица КАК ОчередностьУдержанийФизическиеЛица
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ФизическиеЛица.ФизическоеЛицо КАК ФизическоеЛицо,
	               |	ВЫБОР
	               |		КОГДА НЕ НачисленияУдержанияПоСотрудникам.НачислениеУдержание ЕСТЬ NULL
	               |				И НачисленияУдержанияПоСотрудникам.НачислениеУдержание В (&ВидыНачислений)
	               |			ТОГДА НачисленияУдержанияПоСотрудникам.Сумма
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК СуммаНачислений,
	               |	ВЫБОР
	               |		КОГДА НЕ НачисленияУдержанияПоСотрудникам.НачислениеУдержание ЕСТЬ NULL
	               |				И НачисленияУдержанияПоСотрудникам.НачислениеУдержание В (&ВидыНДФЛ)
	               |			ТОГДА НачисленияУдержанияПоСотрудникам.Сумма
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК СуммаНДФЛ
	               |ПОМЕСТИТЬ ВТНачисленияУдержания
	               |ИЗ
	               |	ВТФизическиеЛица КАК ФизическиеЛица
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачисленияУдержанияПоСотрудникам КАК НачисленияУдержанияПоСотрудникам
	               |		ПО ФизическиеЛица.ФизическоеЛицо = НачисленияУдержанияПоСотрудникам.ФизическоеЛицо
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	НачисленияУдержания.ФизическоеЛицо КАК ФизическоеЛицо,
	               |	СУММА(НачисленияУдержания.СуммаНачислений) КАК СуммаНачислений,
	               |	СУММА(НачисленияУдержания.СуммаНДФЛ) КАК СуммаНДФЛ
	               |ПОМЕСТИТЬ ВТЗаработокФизическихЛиц
	               |ИЗ
	               |	ВТНачисленияУдержания КАК НачисленияУдержания
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	НачисленияУдержания.ФизическоеЛицо
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЗаработокФизическихЛиц.ФизическоеЛицо КАК ФизическоеЛицо,
	               |	ЗаработокФизическихЛиц.СуммаНачислений - ЗаработокФизическихЛиц.СуммаНДФЛ КАК ЗаработнаяПлата,
	               |	УдержанияФизическихЛиц.Рассчитано КАК Рассчитано,
	               |	УдержанияФизическихЛиц.Коэффициент КАК Коэффициент
	               |ПОМЕСТИТЬ ВТОграничениеУдержаний
	               |ИЗ
	               |	ВТЗаработокФизическихЛиц КАК ЗаработокФизическихЛиц
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТУдержанияФизическихЛиц КАК УдержанияФизическихЛиц
	               |		ПО ЗаработокФизическихЛиц.ФизическоеЛицо = УдержанияФизическихЛиц.ФизическоеЛицо
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТСохраняемыйЗаработокОстаток КАК СохраняемыйЗаработокОстаток
	               |		ПО ЗаработокФизическихЛиц.ФизическоеЛицо = СохраняемыйЗаработокОстаток.ФизическоеЛицо
	               |ГДЕ
	               |	((ЗаработокФизическихЛиц.СуммаНачислений - ЗаработокФизическихЛиц.СуммаНДФЛ) * УдержанияФизическихЛиц.Коэффициент < УдержанияФизическихЛиц.Рассчитано
	               |			ИЛИ НЕ СохраняемыйЗаработокОстаток.ФизическоеЛицо ЕСТЬ NULL)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ОграничениеУдержаний.ФизическоеЛицо КАК ФизическоеЛицо,
	               |	ОграничениеУдержаний.ЗаработнаяПлата КАК ЗаработнаяПлата,
	               |	ОчередностьУдержанийФизическиеЛица.Удержание КАК Удержание,
	               |	ОчередностьУдержанийФизическиеЛица.ИдентификаторСтроки КАК ИдентификаторСтроки,
	               |	ОчередностьУдержанийФизическиеЛица.ДокументОснование КАК ДокументОснование,
	               |	ОчередностьУдержанийФизическиеЛица.Коэффициент КАК Коэффициент,
	               |	ОчередностьУдержанийФизическиеЛица.Очередность КАК Очередность,
	               |	ОчередностьУдержанийФизическиеЛица.Рассчитано КАК Рассчитано,
	               |	0 КАК Результат,
	               |	ОчередностьУдержанийФизическиеЛица.Подразделение КАК Подразделение,
	               |	ОчередностьУдержанийФизическиеЛица.СтатьяФинансирования КАК СтатьяФинансирования,
	               |	ОчередностьУдержанийФизическиеЛица.СтатьяРасходов КАК СтатьяРасходов,
	               |	ОчередностьУдержанийФизическиеЛица.ДатаНачала КАК ДатаНачала,
	               |	ОчередностьУдержанийФизическиеЛица.ДатаОкончания КАК ДатаОкончания,
	               |	ОчередностьУдержанийФизическиеЛица.Контрагент КАК Получатель,
	               |	ОчередностьУдержанийФизическиеЛица.ПлатежныйАгент КАК ПлатежныйАгент
	               |ИЗ
	               |	ВТОчередностьУдержанийФизическиеЛица КАК ОчередностьУдержанийФизическиеЛица
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОграничениеУдержаний КАК ОграничениеУдержаний
	               |		ПО ОчередностьУдержанийФизическиеЛица.ФизическоеЛицо = ОграничениеУдержаний.ФизическоеЛицо
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ФизическоеЛицо,
	               |	Очередность,
	               |	Коэффициент,
	               |	ДокументОснование";
				   
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	УничтожитьВТ = Новый Массив;
	УничтожитьВТ.Добавить("ВТИсполнительныеЛисты");
	УничтожитьВТ.Добавить("ВТУсловияУдержанияПоИсполнительномуДокументуСрезПоследних");
	УничтожитьВТ.Добавить("ВТИсполнительныеЛистыПериоды");
	УничтожитьВТ.Добавить("ВТУдержанияПлатежныхАгентов");
	УничтожитьВТ.Добавить("ВТОчередностьУдержанийФизическиеЛица");
	УничтожитьВТ.Добавить("ВТУдержанияФизическихЛиц");
	УничтожитьВТ.Добавить("ВТФизическиеЛица");
	УничтожитьВТ.Добавить("ВТНачисленияУдержания");
	УничтожитьВТ.Добавить("ВТЗаработокФизическихЛиц");
	УничтожитьВТ.Добавить("ВТОграничениеУдержаний");
	УничтожитьВТ.Добавить("ВТСохраняемыйЗаработок");
	УничтожитьВТ.Добавить("ВТПрожиточныеМинимумыОтбор");
	УничтожитьВТ.Добавить("ВТВеличинаПрожиточногоМинимумаСрезПоследних");
	УничтожитьВТ.Добавить("ВТСохраняемыйЗаработокДанныеДокументов");
	УничтожитьВТ.Добавить("ВТСохраняемыйЗаработокФизическиеЛица");
	УничтожитьВТ.Добавить("ВТСохраняемыйЗаработокОстаток");
	ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, УничтожитьВТ);
	
	УдержанияФизическийЛиц = Новый Соответствие;
	
	СтрокиУдержаний = Новый Массив;
	СтрокиПлатежныхАгентов = Новый Массив;
	Пока Выборка.СледующийПоЗначениюПоля("ФизическоеЛицо") Цикл
		ЗаработнаяПлата = Выборка.ЗаработнаяПлата;
		РазмерСохраняемогоЗаработка = СохраняемыйЗаработок[Выборка.ФизическоеЛицо];
		УдержаннаяСумма = 0;
		УдержанияПоСтрокам = ТаблицаУдержанийФизическихЛиц();
		Пока Выборка.СледующийПоЗначениюПоля("Очередность") Цикл
			ПроцентыВзыскания = Новый Массив;
			ИндексыСтрок = Новый Соответствие;
			Пока Выборка.Следующий() Цикл
				ДанныеСтроки = УдержанияПоСтрокам.Добавить();
				ЗаполнитьЗначенияСвойств(ДанныеСтроки, Выборка);
				ИндексСтроки = УдержанияПоСтрокам.Индекс(ДанныеСтроки);
				Для Каждого ПроцентВзыскания Из ПроцентыВзыскания Цикл 
					СписокИндексов = ИндексыСтрок[ПроцентВзыскания];
					СписокИндексов.Добавить(ИндексСтроки);
				КонецЦикла;
				Если ПроцентыВзыскания.Найти(Выборка.Коэффициент) = Неопределено Тогда 
					ПроцентыВзыскания.Добавить(Выборка.Коэффициент);
					СписокИндексов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИндексСтроки);
					ИндексыСтрок.Вставить(Выборка.Коэффициент, СписокИндексов);
				КонецЕсли;
			КонецЦикла;
			Для Каждого ПроцентВзыскания Из ПроцентыВзыскания Цикл 
				РаспределяемаяСумма = ЗаработнаяПлата * ПроцентВзыскания - УдержаннаяСумма;
				Если РазмерСохраняемогоЗаработка <> Неопределено Тогда
					Если ЗаработнаяПлата - РазмерСохраняемогоЗаработка < РаспределяемаяСумма + УдержаннаяСумма Тогда 
						РаспределяемаяСумма = ЗаработнаяПлата - РазмерСохраняемогоЗаработка - УдержаннаяСумма;
					КонецЕсли;
				КонецЕсли;
				Если РаспределяемаяСумма <= 0 Тогда 
					Продолжить;
				КонецЕсли;
				КоэффициентыРаспределения = Новый Массив;
				СписокИндексов = ИндексыСтрок[ПроцентВзыскания];
				СуммаУдержанийСписка = 0;
				Для Каждого ИндексСтроки Из СписокИндексов Цикл
					ДанныеУдержания = УдержанияПоСтрокам[ИндексСтроки];
					ОсталосьУдержать = ДанныеУдержания.Рассчитано - ДанныеУдержания.Результат;
					СуммаУдержанийСписка = СуммаУдержанийСписка + ОсталосьУдержать;
					КоэффициентыРаспределения.Добавить(ДанныеУдержания.Рассчитано);
				КонецЦикла;
				Если СуммаУдержанийСписка <= РаспределяемаяСумма Тогда 
					УдержаннаяСумма = УдержаннаяСумма + СуммаУдержанийСписка;
					Для Каждого ИндексСтроки Из СписокИндексов Цикл
						ДанныеУдержания = УдержанияПоСтрокам[ИндексСтроки];
						ДанныеУдержания.Результат = ДанныеУдержания.Рассчитано;
					КонецЦикла;
					Прервать;
				КонецЕсли;
				РаспределениеСуммыУдержаний = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(РаспределяемаяСумма, КоэффициентыРаспределения);
				Если РаспределениеСуммыУдержаний = Неопределено Тогда 
					Продолжить;
				КонецЕсли;
				Для Индекс = 0 По КоэффициентыРаспределения.Количество() - 1 Цикл
					ДанныеУдержания = УдержанияПоСтрокам[СписокИндексов[Индекс]];
					ДанныеУдержания.Результат = ДанныеУдержания.Результат + РаспределениеСуммыУдержаний[Индекс];
					УдержаннаяСумма = УдержаннаяСумма + РаспределениеСуммыУдержаний[Индекс];
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
		УдержанияФизическийЛиц.Вставить(Выборка.ФизическоеЛицо, УдержанияПоСтрокам);
	КонецЦикла;
	
	РезультатВзысканий = Новый Структура;
	РезультатВзысканий.Вставить("УдержанияФизическийЛиц", УдержанияФизическийЛиц);
	РезультатВзысканий.Вставить("УдержанияПлатежныхАгентов", УдержанияПлатежныхАгентов);
	
	Возврат РезультатВзысканий;
	
КонецФункции

Функция РезультатОграниченияВзысканийДокумента(Начисления, Удержания, НДФЛ, МенеджерВременныхТаблиц, ПериодРегистрации, ИсключаемыйРегистратор) Экспорт 
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Начисления", Начисления);
	Запрос.УстановитьПараметр("Удержания", Удержания);
	Запрос.УстановитьПараметр("НДФЛ", НДФЛ);
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	Удержания.ФизическоеЛицо КАК ФизическоеЛицо,
	               |	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК Подразделение,
	               |	Удержания.ВидРасчета КАК НачислениеУдержание,
	               |	Удержания.ИдентификаторСтроки КАК ИдентификаторСтроки,
	               |	ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка) КАК СтатьяФинансирования,
	               |	ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка) КАК СтатьяРасходов,
	               |	Удержания.ДокументОснование КАК ДокументОснование,
	               |	Удержания.Результат КАК Сумма,
	               |	Удержания.БазовыйПериодНачало КАК ДатаНачала,
	               |	Удержания.БазовыйПериодКонец КАК ДатаОкончания,
				   |	Удержания.Получатель КАК Контрагент
	               |ПОМЕСТИТЬ ВТУдержания
	               |ИЗ
	               |	&Удержания КАК Удержания
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	НачисленияУдержания.ФизическоеЛицо КАК ФизическоеЛицо,
	               |	НачисленияУдержания.Подразделение КАК Подразделение,
	               |	НачисленияУдержания.НачислениеУдержание КАК НачислениеУдержание,
	               |	НачисленияУдержания.ИдентификаторСтроки КАК ИдентификаторСтроки,
	               |	НачисленияУдержания.СтатьяФинансирования КАК СтатьяФинансирования,
	               |	НачисленияУдержания.СтатьяРасходов КАК СтатьяРасходов,
	               |	НачисленияУдержания.ДокументОснование КАК ДокументОснование,
	               |	НачисленияУдержания.Сумма КАК Сумма,
	               |	НачисленияУдержания.ДатаНачала КАК ДатаНачала,
	               |	НачисленияУдержания.ДатаОкончания КАК ДатаОкончания,
	               |	НачисленияУдержания.Контрагент КАК Контрагент,
	               |	Удержания.ЯвляетсяВзысканием КАК ЯвляетсяВзысканием,
	               |	Удержания.КатегорияУдержания КАК КатегорияУдержания
	               |ПОМЕСТИТЬ ВТУдержанияПоФизическимЛицам
	               |ИЗ
	               |	ВТУдержания КАК НачисленияУдержания
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовРасчета.Удержания КАК Удержания
	               |		ПО НачисленияУдержания.НачислениеУдержание = Удержания.Ссылка
	               |			И (Удержания.ЯвляетсяВзысканием
	               |				ИЛИ Удержания.КатегорияУдержания = ЗНАЧЕНИЕ(Перечисление.КатегорииУдержаний.ВознаграждениеПлатежногоАгента))
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
	               |	Начисления.ВидРасчета КАК НачислениеУдержание,
	               |	Начисления.Результат КАК Сумма
	               |ПОМЕСТИТЬ ВТНачисления
	               |ИЗ
	               |	&Начисления КАК Начисления
				   |ГДЕ
	               |	Начисления.СторноТекущегоПериода = НЕОПРЕДЕЛЕНО
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДанныеНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
	               |	&Сумма КАК Сумма
	               |ПОМЕСТИТЬ ВТДанныеНДФЛ
	               |ИЗ
	               |	&НДФЛ КАК ДанныеНДФЛ
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
	               |	Начисления.НачислениеУдержание КАК НачислениеУдержание,
	               |	Начисления.Сумма КАК Сумма
	               |ПОМЕСТИТЬ ВТНачисленияУдержанияПоСотрудникам
	               |ИЗ
	               |	ВТНачисления КАК Начисления
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ДанныеНДФЛ.ФизическоеЛицо,
	               |	ЗНАЧЕНИЕ(Перечисление.ВидыОсобыхНачисленийИУдержаний.НДФЛ),
	               |	ДанныеНДФЛ.Сумма
	               |ИЗ
	               |	ВТДанныеНДФЛ КАК ДанныеНДФЛ";
	
	ИменаРесурсовНалога = УчетНДФЛ.РесурсыИсчисленногоНалогаВМассиве("ДанныеНДФЛ.Сумма");
	ТекстНалогов = "";
	Для каждого ИмяРесурса Из ИменаРесурсовНалога Цикл
		ТекстНалогов = ТекстНалогов + ?(ЗначениеЗаполнено(ТекстНалогов), "+ ", "") + ИмяРесурса + " ";
	КонецЦикла;
	Запрос.Текст = СтрЗаменить(ТекстЗапроса, "&Сумма", ТекстНалогов);
		
	Если ЗарплатаКадры.ВТСуществует(МенеджерВременныхТаблиц, "ВТРегистрНакопления_НачисленияУдержанияПоКонтрагентамАкционерам") Тогда
		Запрос.Текст = Запрос.Текст + "
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	 НачисленияУдержания.ФизическоеЛицо,
			|	 НачисленияУдержания.НачислениеУдержание,
			|	 НачисленияУдержания.Сумма
			|ИЗ
			|	ВТРегистрНакопления_НачисленияУдержанияПоКонтрагентамАкционерам КАК НачисленияУдержания";
	КонецЕсли;
	
	Запрос.Выполнить();
	
	Запрос.УстановитьПараметр("Период", ПериодРегистрации);
	Запрос.УстановитьПараметр("ИсключаемыйРегистратор", ИсключаемыйРегистратор);
	
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	УдержанияПоФизическимЛицам.ФизическоеЛицо КАК ФизическоеЛицо
	               |ПОМЕСТИТЬ ВТДанныеСохраняемогоЗаработкаОтбор
	               |ИЗ
	               |	ВТУдержанияПоФизическимЛицам КАК УдержанияПоФизическимЛицам
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДанныеСохраняемогоЗаработкаПриВзысканиях.ФизическоеЛицо КАК ФизическоеЛицо,
	               |	СУММА(ДанныеСохраняемогоЗаработкаПриВзысканиях.Сумма) КАК Сумма
	               |ПОМЕСТИТЬ ВТДанныеСохраняемогоЗаработка
	               |ИЗ
	               |	РегистрСведений.ДанныеСохраняемогоЗаработкаПриВзысканиях КАК ДанныеСохраняемогоЗаработкаПриВзысканиях
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДанныеСохраняемогоЗаработкаОтбор КАК Отбор
	               |		ПО ДанныеСохраняемогоЗаработкаПриВзысканиях.ФизическоеЛицо = Отбор.ФизическоеЛицо
	               |			И (ДанныеСохраняемогоЗаработкаПриВзысканиях.Период = &Период)
	               |			И (ДанныеСохраняемогоЗаработкаПриВзысканиях.Регистратор <> &ИсключаемыйРегистратор)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ДанныеСохраняемогоЗаработкаПриВзысканиях.ФизическоеЛицо";
				   
	Запрос.Выполнить();
	
	РезультатВзысканий = РезультатОграниченияВзысканий(МенеджерВременныхТаблиц);
	
	УничтожитьВТ = Новый Массив;
	УничтожитьВТ.Добавить("ВТУдержания");
	УничтожитьВТ.Добавить("ВТУдержанияПоФизическимЛицам");
	УничтожитьВТ.Добавить("ВТНачисления");
	УничтожитьВТ.Добавить("ВТДанныеНДФЛ");
	УничтожитьВТ.Добавить("ВТНачисленияУдержанияПоСотрудникам");
	УничтожитьВТ.Добавить("ВТДанныеСохраняемогоЗаработка");
	УничтожитьВТ.Добавить("ВТДанныеСохраняемогоЗаработкаОтбор");
	ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, УничтожитьВТ);
	
	Возврат РезультатВзысканий;
	
КонецФункции

Функция ВидыНачисленийЗаработнойПлаты()
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	УдержанияБазовыеВидыРасчета.ВидРасчета
	               |ИЗ
	               |	ПланВидовРасчета.Удержания.БазовыеВидыРасчета КАК УдержанияБазовыеВидыРасчета
	               |ГДЕ
	               |	УдержанияБазовыеВидыРасчета.Ссылка.КатегорияУдержания = ЗНАЧЕНИЕ(Перечисление.КатегорииУдержаний.ИсполнительныйЛист)
	               |	И НЕ УдержанияБазовыеВидыРасчета.Ссылка.ПометкаУдаления
	               |	И НЕ УдержанияБазовыеВидыРасчета.Ссылка.ВАрхиве";
				   
	СписокНачислений = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ВидРасчета");			   
	
	Если СписокНачислений.Количество() = 0 Тогда 
		СписокНачислений = РасчетнаяБазаИсполнительногоЛистаПоУмолчанию();
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СписокНачислений, ВидыНачисленийДополненияРасчетнойБазыУдержаний());
	
	Возврат СписокНачислений;
	
КонецФункции

Функция РасчетнаяБазаИсполнительногоЛистаПоУмолчанию()
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("КатегорииНачислений", ПланыВидовРасчета.Удержания.КатегорииБазовыхНачисленийУдержанийПоИсполнительнымДокументам());
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	Начисления.Ссылка
	               |ИЗ
	               |	ПланВидовРасчета.Начисления КАК Начисления
	               |ГДЕ
	               |	Начисления.КатегорияНачисленияИлиНеоплаченногоВремени В(&КатегорииНачислений)";
				   
	СписокНачислений = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Возврат СписокНачислений; 
	
КонецФункции

Функция ВидыНачисленийДополненияРасчетнойБазыУдержаний() 
	
	Возврат Обработки.МенеджерРасчетаЗарплаты.ВидыНачисленийДополненияРасчетнойБазыУдержаний();
	
КонецФункции

Функция ВидыНДФЛДополненияРасчетнойБазыУдержаний()
	
	Возврат Обработки.МенеджерРасчетаЗарплаты.ВидыНДФЛДополненияРасчетнойБазыУдержаний();
	
КонецФункции

Функция ТаблицаУдержанийФизическихЛиц()
	
	ТаблицаУдержаний = Новый ТаблицаЗначений;
	
	ТаблицаУдержаний.Колонки.Добавить("ФизическоеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	ТаблицаУдержаний.Колонки.Добавить("ЗаработнаяПлата", Новый ОписаниеТипов("Число"));
	ТаблицаУдержаний.Колонки.Добавить("Удержание", Новый ОписаниеТипов("ПланВидовРасчетаСсылка.Удержания"));
	ТаблицаУдержаний.Колонки.Добавить("ИдентификаторСтроки", Новый ОписаниеТипов("Число"));
	ТаблицаУдержаний.Колонки.Добавить("ДокументОснование", Метаданные.ОпределяемыеТипы.ОснованиеУдержания.Тип);
	ТаблицаУдержаний.Колонки.Добавить("Коэффициент", Новый ОписаниеТипов("Число"));
	ТаблицаУдержаний.Колонки.Добавить("Очередность", Новый ОписаниеТипов("Число"));
	ТаблицаУдержаний.Колонки.Добавить("Рассчитано", Новый ОписаниеТипов("Число"));
	ТаблицаУдержаний.Колонки.Добавить("Результат", Новый ОписаниеТипов("Число"));
	ТаблицаУдержаний.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	ТаблицаУдержаний.Колонки.Добавить("СтатьяФинансирования", Новый ОписаниеТипов("СправочникСсылка.СтатьиФинансированияЗарплата"));
	ТаблицаУдержаний.Колонки.Добавить("СтатьяРасходов", Новый ОписаниеТипов("СправочникСсылка.СтатьиРасходовЗарплата"));
	ТаблицаУдержаний.Колонки.Добавить("ДатаНачала", Новый ОписаниеТипов("Дата"));
	ТаблицаУдержаний.Колонки.Добавить("ДатаОкончания", Новый ОписаниеТипов("Дата"));
	ТаблицаУдержаний.Колонки.Добавить("Получатель", Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	ТаблицаУдержаний.Колонки.Добавить("ПлатежныйАгент", Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	ТаблицаУдержаний.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	
	Возврат ТаблицаУдержаний;
	                    
КонецФункции

Процедура ЗарегистрироватьСохраняемыйЗаработокПриВзысканиях(Движения, СохраняемыйЗаработок) Экспорт
	
	Для Каждого СтрокаТаблицы Из СохраняемыйЗаработок Цикл
		НоваяСтрока = Движения.СохраняемыйЗаработокПриВзысканиях.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
	КонецЦикла;	
	
	Движения.СохраняемыйЗаработокПриВзысканиях.Записывать = Истина;
	
КонецПроцедуры

Процедура ЗарегистрироватьДанныеСохраняемогоЗаработка(Движения, Отказ, ПериодРегистрации, ПорядокВыплаты = Неопределено) Экспорт 
	
	Если Не РасчетЗарплатыРасширенный.ЭтоМежрасчетнаяВыплата(ПорядокВыплаты) Тогда 
		Возврат;
	КонецЕсли;
	
	ТипыНачислениеУдержание = Метаданные.РегистрыНакопления.НачисленияУдержанияПоСотрудникам.Измерения.НачислениеУдержание.Тип.Типы();
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ТипыНачислениеУдержание, Метаданные.РегистрыНакопления.НачисленияУдержанияПоКонтрагентамАкционерам.Измерения.НачислениеУдержание.Тип.Типы(), Истина);
	
	НачисленияУдержанияПоСотрудникам = Новый ТаблицаЗначений;
	НачисленияУдержанияПоСотрудникам.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	НачисленияУдержанияПоСотрудникам.Колонки.Добавить("ФизическоеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	НачисленияУдержанияПоСотрудникам.Колонки.Добавить("НачислениеУдержание", Новый ОписаниеТипов(ТипыНачислениеУдержание));
	НачисленияУдержанияПоСотрудникам.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число"));
	
	Если Движения.Найти("НачисленияУдержанияПоСотрудникам") <> Неопределено Тогда 
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(Движения.НачисленияУдержанияПоСотрудникам, НачисленияУдержанияПоСотрудникам);
	КонецЕсли;
	
	Если Движения.Найти("НачисленияУдержанияПоСотрудникамАвансом") <> Неопределено Тогда 
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(Движения.НачисленияУдержанияПоСотрудникамАвансом, НачисленияУдержанияПоСотрудникам);
	КонецЕсли;
	
	Если Движения.Найти("НачисленияУдержанияПоКонтрагентамАкционерам") <> Неопределено Тогда 
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(Движения.НачисленияУдержанияПоКонтрагентамАкционерам, НачисленияУдержанияПоСотрудникам);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Период", ПериодРегистрации);
	Запрос.УстановитьПараметр("НачисленияУдержанияПоСотрудникам", НачисленияУдержанияПоСотрудникам);
	Запрос.УстановитьПараметр("Регистратор", Движения.ДанныеСохраняемогоЗаработкаПриВзысканиях.Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ВидыНачислений", ВидыНачисленийЗаработнойПлаты());
	Запрос.УстановитьПараметр("ВидыНДФЛ", ВидыНДФЛДополненияРасчетнойБазыУдержаний());
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	НачисленияУдержанияПоСотрудникам.Организация КАК Организация,
	               |	НачисленияУдержанияПоСотрудникам.ФизическоеЛицо КАК ФизическоеЛицо,
	               |	НачисленияУдержанияПоСотрудникам.НачислениеУдержание КАК НачислениеУдержание,
	               |	НачисленияУдержанияПоСотрудникам.Сумма КАК Сумма
	               |ПОМЕСТИТЬ ВТНачисленияУдержанияПоСотрудникам
	               |ИЗ
	               |	&НачисленияУдержанияПоСотрудникам КАК НачисленияУдержанияПоСотрудникам
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	&Период КАК Период,
	               |	НачисленияУдержанияПоСотрудникам.ФизическоеЛицо КАК ФизическоеЛицо
	               |ПОМЕСТИТЬ ВТФизическиеЛицаОтбор
	               |ИЗ
	               |	ВТНачисленияУдержанияПоСотрудникам КАК НачисленияУдержанияПоСотрудникам";
	
	Запрос.Выполнить();
	
	ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра("ВТФизическиеЛицаОтбор", "Период, ФизическоеЛицо");
	ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТИмяРегистраСрезПоследних("СохраняемыйЗаработокПриВзысканиях", Запрос.МенеджерВременныхТаблиц, Истина, ОписаниеФильтра);
	
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	               |	ИСТИНА КАК ЗначениеИстина
	               |ИЗ
	               |	ВТСохраняемыйЗаработокПриВзысканияхСрезПоследних КАК СохраняемыйЗаработокПриВзысканиях";

	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда 
		Возврат;
	КонецЕсли;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	МАКСИМУМ(СохраняемыйЗаработок.Период) КАК Период,
	               |	СохраняемыйЗаработок.ПрожиточныйМинимум КАК ПрожиточныйМинимум
	               |ПОМЕСТИТЬ ВТПрожиточныеМинимумыОтбор
	               |ИЗ
	               |	ВТСохраняемыйЗаработокПриВзысканияхСрезПоследних КАК СохраняемыйЗаработок
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СохраняемыйЗаработок.ПрожиточныйМинимум";
	
	Запрос.Выполнить();			   
	
	ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра("ВТПрожиточныеМинимумыОтбор", "Период, ПрожиточныйМинимум");
	ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТИмяРегистраСрезПоследних("ВеличинаПрожиточногоМинимума", Запрос.МенеджерВременныхТаблиц, Истина, ОписаниеФильтра);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	СохраняемыйЗаработок.ФизическоеЛицо КАК ФизическоеЛицо,
	               |	СохраняемыйЗаработок.ИсполнительныйДокумент КАК ИсполнительныйДокумент,
	               |	СУММА(СохраняемыйЗаработок.КоличествоПрожиточныхМинимумов * ВеличинаПрожиточногоМинимума.Размер) КАК Заработок
	               |ПОМЕСТИТЬ ВТСохраняемыйЗаработокДанныеДокументов
	               |ИЗ
	               |	ВТСохраняемыйЗаработокПриВзысканияхСрезПоследних КАК СохраняемыйЗаработок
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТВеличинаПрожиточногоМинимумаСрезПоследних КАК ВеличинаПрожиточногоМинимума
	               |		ПО СохраняемыйЗаработок.ПрожиточныйМинимум = ВеличинаПрожиточногоМинимума.ПрожиточныйМинимум
	               |			И СохраняемыйЗаработок.Период = ВеличинаПрожиточногоМинимума.Период
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СохраняемыйЗаработок.ФизическоеЛицо,
	               |	СохраняемыйЗаработок.ИсполнительныйДокумент
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СохраняемыйЗаработок.ФизическоеЛицо КАК ФизическоеЛицо,
	               |	МАКСИМУМ(СохраняемыйЗаработок.Заработок) КАК Заработок
	               |ПОМЕСТИТЬ ВТСохраняемыйЗаработок
	               |ИЗ
	               |	ВТСохраняемыйЗаработокДанныеДокументов КАК СохраняемыйЗаработок
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СохраняемыйЗаработок.ФизическоеЛицо
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДанныеСохраняемогоЗаработкаПриВзысканиях.ФизическоеЛицо КАК ФизическоеЛицо,
	               |	СУММА(ДанныеСохраняемогоЗаработкаПриВзысканиях.Сумма) КАК Сумма
	               |ПОМЕСТИТЬ ВТДанныеСохраняемогоЗаработка
	               |ИЗ
	               |	РегистрСведений.ДанныеСохраняемогоЗаработкаПриВзысканиях КАК ДанныеСохраняемогоЗаработкаПриВзысканиях
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСохраняемыйЗаработок КАК СохраняемыйЗаработок
	               |		ПО ДанныеСохраняемогоЗаработкаПриВзысканиях.ФизическоеЛицо = СохраняемыйЗаработок.ФизическоеЛицо
	               |			И (ДанныеСохраняемогоЗаработкаПриВзысканиях.Период = &Период)
	               |			И (ДанныеСохраняемогоЗаработкаПриВзысканиях.Регистратор <> &Регистратор)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ДанныеСохраняемогоЗаработкаПриВзысканиях.ФизическоеЛицо
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СохраняемыйЗаработок.ФизическоеЛицо КАК ФизическоеЛицо,
	               |	СохраняемыйЗаработок.Заработок - ЕСТЬNULL(ДанныеСохраняемогоЗаработка.Сумма, 0) КАК Заработок
	               |ПОМЕСТИТЬ ВТСохраняемыйЗаработокОстаток
	               |ИЗ
	               |	ВТСохраняемыйЗаработок КАК СохраняемыйЗаработок
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеСохраняемогоЗаработка КАК ДанныеСохраняемогоЗаработка
	               |		ПО СохраняемыйЗаработок.ФизическоеЛицо = ДанныеСохраняемогоЗаработка.ФизическоеЛицо
	               |ГДЕ
	               |	СохраняемыйЗаработок.Заработок - ЕСТЬNULL(ДанныеСохраняемогоЗаработка.Сумма, 0) > 0
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	НачисленияУдержанияПоСотрудникам.Организация КАК Организация,
	               |	НачисленияУдержанияПоСотрудникам.ФизическоеЛицо КАК ФизическоеЛицо,
	               |	ВЫБОР
	               |		КОГДА НачисленияУдержанияПоСотрудникам.НачислениеУдержание В (&ВидыНачислений)
	               |			ТОГДА НачисленияУдержанияПоСотрудникам.Сумма
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК СуммаНачислений,
	               |	ВЫБОР
	               |		КОГДА НачисленияУдержанияПоСотрудникам.НачислениеУдержание В (&ВидыНДФЛ)
	               |			ТОГДА НачисленияУдержанияПоСотрудникам.Сумма
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК СуммаНДФЛ
	               |ПОМЕСТИТЬ ВТНачисленияУдержания
	               |ИЗ
	               |	ВТНачисленияУдержанияПоСотрудникам КАК НачисленияУдержанияПоСотрудникам
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСохраняемыйЗаработокОстаток КАК СохраняемыйЗаработокОстаток
	               |		ПО НачисленияУдержанияПоСотрудникам.ФизическоеЛицо = СохраняемыйЗаработокОстаток.ФизическоеЛицо
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	НачисленияУдержания.Организация КАК Организация,
	               |	НачисленияУдержания.ФизическоеЛицо КАК ФизическоеЛицо,
	               |	СУММА(НачисленияУдержания.СуммаНачислений) КАК СуммаНачислений,
	               |	СУММА(НачисленияУдержания.СуммаНДФЛ) КАК СуммаНДФЛ
	               |ПОМЕСТИТЬ ВТЗаработокФизическихЛиц
	               |ИЗ
	               |	ВТНачисленияУдержания КАК НачисленияУдержания
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	НачисленияУдержания.Организация,
	               |	НачисленияУдержания.ФизическоеЛицо
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	НачисленияУдержания.ФизическоеЛицо КАК ФизическоеЛицо,
	               |	СУММА(НачисленияУдержания.Сумма) КАК Сумма
	               |ПОМЕСТИТЬ ВТУдержанияПоФизическимЛицам
	               |ИЗ
	               |	ВТНачисленияУдержанияПоСотрудникам КАК НачисленияУдержания
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовРасчета.Удержания КАК Удержания
	               |		ПО НачисленияУдержания.НачислениеУдержание = Удержания.Ссылка
	               |			И (Удержания.ЯвляетсяВзысканием
	               |				ИЛИ Удержания.КатегорияУдержания = ЗНАЧЕНИЕ(Перечисление.КатегорииУдержаний.ВознаграждениеПлатежногоАгента))
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	НачисленияУдержания.ФизическоеЛицо
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	&Период КАК Период,
	               |	&Регистратор КАК ДокументОснование,
				   |	ЗаработокФизическихЛиц.Организация.ГоловнаяОрганизация КАК Организация,
	               |	ЗаработокФизическихЛиц.ФизическоеЛицо КАК ФизическоеЛицо,
	               |	ЗаработокФизическихЛиц.СуммаНачислений - ЗаработокФизическихЛиц.СуммаНДФЛ - ЕСТЬNULL(УдержанияПоФизическимЛицам.Сумма, 0) КАК Сумма,
	               |	СохраняемыйЗаработокОстаток.Заработок КАК Заработок
	               |ИЗ
	               |	ВТЗаработокФизическихЛиц КАК ЗаработокФизическихЛиц
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСохраняемыйЗаработокОстаток КАК СохраняемыйЗаработокОстаток
	               |		ПО ЗаработокФизическихЛиц.ФизическоеЛицо = СохраняемыйЗаработокОстаток.ФизическоеЛицо
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТУдержанияПоФизическимЛицам КАК УдержанияПоФизическимЛицам
	               |		ПО ЗаработокФизическихЛиц.ФизическоеЛицо = УдержанияПоФизическимЛицам.ФизическоеЛицо
	               |ГДЕ
	               |	ЗаработокФизическихЛиц.СуммаНачислений - ЗаработокФизическихЛиц.СуммаНДФЛ - ЕСТЬNULL(УдержанияПоФизическимЛицам.Сумма, 0) > 0";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Движения.ДанныеСохраняемогоЗаработкаПриВзысканиях.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		НоваяСтрока.Сумма = Мин(Выборка.Заработок, Выборка.Сумма);
	КонецЦикла;	
	
	Движения.ДанныеСохраняемогоЗаработкаПриВзысканиях.Записывать = Истина;
	
КонецПроцедуры

#Область БлокФункцийПервоначальногоЗаполненияИОбновленияИБ

// Добавляет в список Обработчики процедуры-обработчики обновления,
// необходимые данной подсистеме.
//
// Параметры:
//   Обработчики - ТаблицаЗначений - см. описание функции НоваяТаблицаОбработчиковОбновления
//                                   общего модуля ОбновлениеИнформационнойБазы.
// 
Процедура ЗарегистрироватьОбработчикиОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия          = "3.1.11.97";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Идентификатор   = Новый УникальныйИдентификатор("e1b20b1a-03f2-4791-9687-9876ce77148b");
	Обработчик.Процедура       = "ОграничениеВзысканий.ЗаполнитьИдентификаторСтрокиИПериодДействияОграниченияВзысканий";
	Обработчик.Комментарий     = НСтр("ru = 'Заполнение идентификаторов строк для в ограничений взысканий';
										|en = 'Populating line IDs in penalty limitations'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия          = "3.1.23.55";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Идентификатор   = Новый УникальныйИдентификатор("d8c71d5a-7d33-478d-b897-4b1106b4cfb8");
	Обработчик.Процедура       = "ОграничениеВзысканий.УстановитьНастройкуУчитыватьЗадолженностьПоВзысканиямПриРасчетеУдержаний";
	Обработчик.Комментарий     = НСтр("ru = 'Установка значения настройки Учитывать задолженность по взысканиям при расчете удержаний';
										|en = 'Set the setting value Take into account penalty debt when calculating deductions'");
	
КонецПроцедуры

Процедура ЗаполнитьИдентификаторСтрокиИПериодДействияОграниченияВзысканий(ПараметрыОбновления = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НачисленияУдержанияПоСотрудникам.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВТРегистраторы
	|ИЗ
	|	РегистрНакопления.НачисленияУдержанияПоСотрудникам КАК НачисленияУдержанияПоСотрудникам
	|ГДЕ
	|	НачисленияУдержанияПоСотрудникам.Регистратор ССЫЛКА Документ.ОграничениеВзысканий
	|	И НачисленияУдержанияПоСотрудникам.ПериодДействия = ДАТАВРЕМЯ(1, 1, 1)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОграничениеВзысканийУдержания.Ссылка КАК Регистратор,
	|	ОграничениеВзысканийУдержания.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ОграничениеВзысканийУдержания.Удержание КАК НачислениеУдержание,
	|	ОграничениеВзысканийУдержания.ДокументОснование КАК ДокументОснование,
	|	НАЧАЛОПЕРИОДА(ОграничениеВзысканийУдержания.ДатаНачала, МЕСЯЦ) КАК ПериодДействия,
	|	ОграничениеВзысканийУдержания.ИдентификаторСтрокиВидаРасчета КАК ИдентификаторСтрокиВидаРасчета
	|ИЗ
	|	ВТРегистраторы КАК Регистраторы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОграничениеВзысканий.Удержания КАК ОграничениеВзысканийУдержания
	|		ПО Регистраторы.Регистратор = ОграничениеВзысканийУдержания.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачисленияУдержанияПоСотрудникам.Период КАК Период,
	|	НачисленияУдержанияПоСотрудникам.Регистратор КАК Регистратор,
	|	НачисленияУдержанияПоСотрудникам.НомерСтроки КАК НомерСтроки,
	|	НачисленияУдержанияПоСотрудникам.Организация КАК Организация,
	|	НачисленияУдержанияПоСотрудникам.ФизическоеЛицо КАК ФизическоеЛицо,
	|	НачисленияУдержанияПоСотрудникам.СтатьяФинансирования КАК СтатьяФинансирования,
	|	НачисленияУдержанияПоСотрудникам.СтатьяРасходов КАК СтатьяРасходов,
	|	НачисленияУдержанияПоСотрудникам.Сотрудник КАК Сотрудник,
	|	НачисленияУдержанияПоСотрудникам.Подразделение КАК Подразделение,
	|	НачисленияУдержанияПоСотрудникам.НачислениеУдержание КАК НачислениеУдержание,
	|	НачисленияУдержанияПоСотрудникам.Сумма КАК Сумма,
	|	НачисленияУдержанияПоСотрудникам.ГруппаНачисленияУдержанияВыплаты КАК ГруппаНачисленияУдержанияВыплаты,
	|	НачисленияУдержанияПоСотрудникам.ПериодДействия КАК ПериодДействия,
	|	НачисленияУдержанияПоСотрудникам.ДокументОснование КАК ДокументОснование,
	|	НачисленияУдержанияПоСотрудникам.Контрагент КАК Контрагент,
	|	НачисленияУдержанияПоСотрудникам.ДатаНачала КАК ДатаНачала,
	|	НачисленияУдержанияПоСотрудникам.ДатаОкончания КАК ДатаОкончания,
	|	НачисленияУдержанияПоСотрудникам.ДанныеМежрасчетногоПериода КАК ДанныеМежрасчетногоПериода,
	|	НачисленияУдержанияПоСотрудникам.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	НачисленияУдержанияПоСотрудникам.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	НачисленияУдержанияПоСотрудникам.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	НачисленияУдержанияПоСотрудникам.Сторно КАК Сторно,
	|	НачисленияУдержанияПоСотрудникам.ОграничениеВзыскания КАК ОграничениеВзыскания,
	|	НачисленияУдержанияПоСотрудникам.УчитыватьВРаспределенииНДФЛ КАК УчитыватьВРаспределенииНДФЛ,
	|	НачисленияУдержанияПоСотрудникам.ДатаПолученияДохода КАК ДатаПолученияДохода,
	|	НачисленияУдержанияПоСотрудникам.КатегорияДохода КАК КатегорияДохода,
	|	НачисленияУдержанияПоСотрудникам.МестоПолученияДохода КАК МестоПолученияДохода
	|ИЗ
	|	ВТРегистраторы КАК ВТРегистраторы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.НачисленияУдержанияПоСотрудникам КАК НачисленияУдержанияПоСотрудникам
	|		ПО ВТРегистраторы.Регистратор = НачисленияУдержанияПоСотрудникам.Регистратор
	|
	|УПОРЯДОЧИТЬ ПО
	|	Регистратор,
	|	НомерСтроки";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	Если РезультатЗапроса[2].Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	СтрокаПолей = "Регистратор,ФизическоеЛицо,НачислениеУдержание,ДокументОснование";
	ИдентификаторыСтрок = РезультатЗапроса[1].Выгрузить();
	Отбор = Новый Структура(СтрокаПолей);
	ИдентификаторыСтрок.Индексы.Добавить(СтрокаПолей);
	
	Выборка = РезультатЗапроса[2].Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Регистратор") Цикл
		
		Регистратор = Выборка.Регистратор;
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрНакопления.НачисленияУдержанияПоСотрудникам.НаборЗаписей", "Регистратор", Регистратор) Тогда
			Продолжить;
		КонецЕсли;
		
		НаборЗаписей = РегистрыНакопления.НачисленияУдержанияПоСотрудникам.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
		
		Пока Выборка.СледующийПоЗначениюПоля("НомерСтроки") Цикл
			
			НоваяЗапись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
			ЗаполнитьЗначенияСвойств(Отбор, Выборка);
			НайденныеСтроки = ИдентификаторыСтрок.НайтиСтроки(Отбор);
			Если НайденныеСтроки.Количество()>0 Тогда
				НоваяЗапись.ИдентификаторСтроки = НайденныеСтроки[0].ИдентификаторСтрокиВидаРасчета;
				НоваяЗапись.ПериодДействия = НайденныеСтроки[0].ПериодДействия;
			КонецЕсли;
			
		КонецЦикла;
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;

КонецПроцедуры

Процедура УстановитьНастройкуУчитыватьЗадолженностьПоВзысканиямПриРасчетеУдержаний(ПараметрыОбновления = Неопределено) Экспорт

	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	
	Настройки = РегистрыСведений.НастройкиРасчетаЗарплатыРасширенный.СоздатьНаборЗаписей();
	Настройки.Прочитать();
	Если Настройки.Количество() = 0 
		Или Не Настройки[0].ОграничиватьСуммуУдержанийПроцентомОтЗаработнойПлаты
		Или Настройки[0].УчитыватьЗадолженностьПоВзысканиямПриРасчетеУдержаний Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрСведений.НастройкиРасчетаЗарплаты", Неопределено, Неопределено) Тогда
		Возврат;
	КонецЕсли;
	
	Настройки[0].УчитыватьЗадолженностьПоВзысканиямПриРасчетеУдержаний = Истина;
	
	ОбновлениеИнформационнойБазы.ЗаписатьДанные(Настройки);
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

