#Область СлужебныеПроцедурыИФункции

Функция УдержанияПоРабочимМестам(РеквизитыДляПроведения) Экспорт 
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата") Тогда
		
		УдержанияПоРабочимМестам = УчетНачисленнойЗарплаты.ТаблицаРаспределенияПоРабочимМестам();
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Ссылка", РеквизитыДляПроведения.Ссылка);
		Запрос.Текст = "ВЫБРАТЬ
		               |	ВозвратНДФЛСуммыВозврата.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
		               |	ВозвратНДФЛРаспределениеРезультатовУдержаний.Сотрудник КАК Сотрудник,
		               |	ВозвратНДФЛРаспределениеРезультатовУдержаний.Подразделение КАК Подразделение,
		               |	ВозвратНДФЛРаспределениеРезультатовУдержаний.СтатьяФинансирования КАК СтатьяФинансирования,
		               |	ВозвратНДФЛРаспределениеРезультатовУдержаний.СтатьяРасходов КАК СтатьяРасходов,
		               |	ВозвратНДФЛРаспределениеРезультатовУдержаний.ВидУдержания КАК НачислениеУдержание,
		               |	СУММА(ВозвратНДФЛРаспределениеРезультатовУдержаний.Результат) КАК Сумма,
		               |	ВозвратНДФЛРаспределениеРезультатовУдержаний.ВидДоходаИсполнительногоПроизводства КАК ВидДоходаИсполнительногоПроизводства
		               |ИЗ
		               |	Документ.ВозвратНДФЛ.СуммыВозврата КАК ВозвратНДФЛСуммыВозврата
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратНДФЛ.РаспределениеРезультатовУдержаний КАК ВозвратНДФЛРаспределениеРезультатовУдержаний
		               |		ПО ВозвратНДФЛСуммыВозврата.Ссылка = ВозвратНДФЛРаспределениеРезультатовУдержаний.Ссылка
		               |			И ВозвратНДФЛСуммыВозврата.ИдентификаторСтрокиНДФЛ = ВозвратНДФЛРаспределениеРезультатовУдержаний.ИдентификаторСтроки
		               |			И (ВозвратНДФЛСуммыВозврата.Ссылка = &Ссылка)
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ВозвратНДФЛСуммыВозврата.РегистрацияВНалоговомОргане,
		               |	ВозвратНДФЛРаспределениеРезультатовУдержаний.Сотрудник,
		               |	ВозвратНДФЛРаспределениеРезультатовУдержаний.Подразделение,
		               |	ВозвратНДФЛРаспределениеРезультатовУдержаний.СтатьяФинансирования,
		               |	ВозвратНДФЛРаспределениеРезультатовУдержаний.СтатьяРасходов,
		               |	ВозвратНДФЛРаспределениеРезультатовУдержаний.ВидУдержания,
		               |	ВозвратНДФЛРаспределениеРезультатовУдержаний.ВидДоходаИсполнительногоПроизводства";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл 
			НоваяСтрока = УдержанияПоРабочимМестам.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, РеквизитыДляПроведения);
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		КонецЦикла;
		
		Возврат УдержанияПоРабочимМестам;
		
	Иначе
		
		Возврат ВозвратНДФЛБазовый.УдержанияПоРабочимМестам(РеквизитыДляПроведения);
		
	КонецЕсли;
	
КонецФункции 

Процедура ОбработкаПроверкиЗаполнения(Документ, Отказ, ПроверяемыеРеквизиты) Экспорт
	
	ОтражениеЗарплатыВБухучетеРасширенный.ПроверитьРезультатыРаспределенияНачисленийУдержанийОбъекта(Документ, "СуммыВозврата", Отказ);
	
КонецПроцедуры

#КонецОбласти

