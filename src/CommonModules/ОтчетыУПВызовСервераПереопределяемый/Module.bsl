
////////////////////////////////////////////////////////////////////////////////
// Варианты отчетов - Форма отчета УП (вызов сервера, переопределяемый)
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

//++ НЕ УТКА

// Вызывается из модуля ОтчетыУПКлиентПереопределяемый для отчета "ЗагрузкаВидовРабочихЦентров".
// 
// Возвращаемое значение:
//	Булево - флаг использования подсистемы управления производством версии 2.2
//
Функция ИспользуетсяПроизводство22() Экспорт
	
	Возврат ПроизводствоСервер.ИспользуетсяПроизводство22();
	
КонецФункции

// Вызывается из модуля ОтчетыУПКлиентПереопределяемый для отчета "КонфликтыВОчередиЗаказовНаПроизводство".
// 
// Параметры:
//	Расшифровка - ИдентификаторРасшифровкиКомпоновкиДанных - Расшифровка отчета
//	АдресДанныхРасшифровки - Строка - Адрес временного хранилища данных расшифровки
//
// Возвращаемое значение:
//	Структура
//
Функция ПараметрыРасшифровкиКонфликтыВОчередиЗаказовНаПроизводство(Расшифровка, АдресДанныхРасшифровки) Экспорт
	
	ДанныеРасшифровки = ПолучитьИзВременногоХранилища(АдресДанныхРасшифровки); // ДанныеРасшифровкиКомпоновкиДанных
	
	ТекущееПоле = Новый Структура;
	ОтчетыУТВызовСервераПереопределяемый.ПолучитьЗначениеПоля(ТекущееПоле, ДанныеРасшифровки.Элементы[Расшифровка]);
	
	ПоляГруппировки = Новый Структура;
	ОтчетыУТВызовСервераПереопределяемый.ПолучитьЗначенияПолей(ПоляГруппировки, ДанныеРасшифровки.Элементы, Расшифровка);
	
	Результат = Новый Структура;
	Результат.Вставить("ТекущееПоле", ТекущееПоле);
	Результат.Вставить("ПоляГруппировки", ПоляГруппировки);
	
	Возврат Результат;
	
КонецФункции

// Формирует параметры расшифровки аналогов 
//
// Параметры:
//  ПараметрыДействия    - см. ОтчетыУПКлиентПереопределяемый.ВыполнитьДействиеРасшифровки.ПараметрыДействия
//  ПараметрыРасшифровки - см. ОтчетыУПКлиентПереопределяемый.ВыполнитьДействиеРасшифровки.ПараметрыРасшифровки
//
// Возвращаемое значение:
//  см. АналогиМатериалов.ПараметрыВыбораАналогов
//
Функция ПараметрыРасшифровкиРасшифровкаПозицииНоменклатурногоПланаДляАналогов(
			ПараметрыДействия, ПараметрыРасшифровки) Экспорт
	
	ТипИсточника = Новый ОписаниеТипов("СправочникСсылка.ЭтапыПроизводства");
	
	ПараметрыВыбораАналогов = АналогиМатериалов.ПараметрыВыбораАналогов(ТипИсточника);
	
	// ПараметрыОбщие
	ПараметрыВыбораАналогов.ПараметрыОбщие.ПоказатьОстатки  = Истина;
	ПараметрыВыбораАналогов.ПараметрыОбщие.РежимРасшифровки = Истина;
	
	// ПараметрыТоваров
	СтруктураПолей = АналогиМатериалов.СтруктураПолейТаблицыДляЗаменыНаАналоги();
	СтруктураПолей.ДополнительныеПоля.Вставить("Ссылка", ТипИсточника);
	
	Товары = Новый ТаблицаЗначений;
	Товары.Колонки.Добавить("Ссылка",         ТипИсточника);
	Товары.Колонки.Добавить("НомерСтроки",    ОбщегоНазначения.ОписаниеТипаЧисло(10));
	Товары.Колонки.Добавить("Номенклатура",   Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	Товары.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	Товары.Колонки.Добавить("Назначение",     Новый ОписаниеТипов("СправочникСсылка.Назначения"));
	Товары.Колонки.Добавить("Обособленно",    Новый ОписаниеТипов("Булево"));
	Товары.Колонки.Добавить("Склад",          Новый ОписаниеТипов("СправочникСсылка.Склады"));
	Товары.Колонки.Добавить("Количество",     ОбщегоНазначения.ОписаниеТипаЧисло(15, 3));
	
	СтрокаТовары = Товары.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаТовары, ПараметрыРасшифровки);
	ПараметрыРасшифровки.Свойство("Этап",    СтрокаТовары.Ссылка);
	ПараметрыРасшифровки.Свойство("Аналоги", СтрокаТовары.Количество);
	
	ИмяОбъекта = "Объект";
	ИмяТаблицы = "Товары";
	ДанныеФормы = Новый Структура;
	ДанныеФормы.Вставить("УникальныйИдентификатор", Новый УникальныйИдентификатор);
	ДанныеФормы.Вставить(ИмяОбъекта, Новый Структура);
	ДанныеФормы[ИмяОбъекта].Вставить(ИмяТаблицы, Товары);
	
	ДанныеТоваров = АналогиМатериалов.ПолучитьДанныеТоваров(
		ДанныеФормы, ИмяОбъекта, ИмяТаблицы, 1, Ложь, СтруктураПолей);
		
	ЗаполнитьЗначенияСвойств(ПараметрыВыбораАналогов.ПараметрыТоваров, ДанныеТоваров);
	
	// ПараметрыАналогов
	ТаблицаПараметров = ПараметрыВыбораАналогов.ПараметрыАналогов.ТаблицаПараметров;
	
	СтрокаТаблицы = ТаблицаПараметров.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ПараметрыДействия.ДанныеПараметров);
	СтрокаТаблицы.ДатаДействияРазрешений = НачалоДня(ТекущаяДатаСеанса());
	СтрокаТаблицы.Ссылка                 = СтрокаТовары.Ссылка;
	ПараметрыРасшифровки.Свойство("ПодразделениеЭтапа", СтрокаТаблицы.Подразделение);
	
	ПараметрыВыбораАналогов.ПараметрыАналогов.ТаблицаПараметров = 
		ПоместитьВоВременноеХранилище(ТаблицаПараметров, ДанныеФормы.УникальныйИдентификатор);
	
	Возврат ПараметрыВыбораАналогов;
	
КонецФункции

//-- НЕ УТКА

#КонецОбласти
