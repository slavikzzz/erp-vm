////////////////////////////////////////////////////////////////////////////////
// Процедуры, используемые для локализации документа "Регистрация дефекта".
// 
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

// Добавляет команды печати.
// 
// Параметры:
// 	КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	//++ Локализация
	
	// Дефектная ведомость
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.РегистрацияДефекта";
	КомандаПечати.Идентификатор = "ДефектнаяВедомость";
	КомандаПечати.Представление = НСтр("ru = 'Дефектная ведомость';
										|en = 'Failure report'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	//-- Локализация
	
КонецПроцедуры

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	//++ Локализация
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ДефектнаяВедомость") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"ДефектнаяВедомость",
			НСтр("ru = 'Дефектная ведомость';
				|en = 'Failure report'"),
			ПечатьДефектнаяВедомость(МассивОбъектов, ОбъектыПечати, ПараметрыПечати));
		
	КонецЕсли;
	
	//-- Локализация
	
КонецПроцедуры

//++ Локализация

Функция ПечатьДефектнаяВедомость(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_РегистрацияДефекта_ДефектнаяВедомость";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.РегистрацияДефекта.ПФ_MXL_ДефектнаяВедомость");
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ОбластьСтрокаТаблицы = Макет.ПолучитьОбласть("СтрокаТаблицы");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка,
	|	ДанныеДокумента.Номер,
	|	ДанныеДокумента.Дата,
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.Подразделение,
	|	ДанныеДокумента.Подразделение.Представление КАК ПодразделениеПредставление,
	|	ДанныеДокумента.ОбъектЭксплуатации,
	|	ДанныеДокумента.ОбъектЭксплуатации.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	ДанныеДокумента.ОбъектЭксплуатации.Модель КАК Модель,
	|	ДанныеДокумента.ОбъектЭксплуатации.Представление КАК ОбъектЭксплуатацииПредставление,
	|	ДанныеДокумента.Решение,
	|	ДанныеДокумента.ОписаниеДефекта,
	|
	|	ДанныеДокумента.ДефектныеУзлы.(
	|		НомерСтроки,
	|		Узел,
	|		Узел.Представление КАК УзелПредставление,
	|		ОписаниеДефекта) КАК ДефектныеУзлы
	|ИЗ
	|	Документ.РегистрацияДефекта КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка,
	|	ДефектныеУзлы.НомерСтроки";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДокументов = РезультатЗапроса.Выбрать();
	
	ПервыйДокумент = Истина;
	
	Пока ВыборкаДокументов.Следующий() Цикл
		
		Если НЕ ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		#Область Шапка
		
		ОбластьШапка.Параметры.Заполнить(ВыборкаДокументов);
		
		СведенияОбОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(
			ВыборкаДокументов.Организация, ВыборкаДокументов.Дата);
		
		ПараметрыШапки = Новый Структура;
		ПараметрыШапки.Вставить("ОрганизацияПредставление", ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбОрганизации));
		ПараметрыШапки.Вставить("Номер", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ВыборкаДокументов.Номер));
		ПараметрыШапки.Вставить("Дата", Формат(ВыборкаДокументов.Дата, "ДЛФ=D;"));
		
		ОбъектЭксплуатацииПредставление = ВыборкаДокументов.ОбъектЭксплуатацииПредставление;
		Если ЗначениеЗаполнено(ВыборкаДокументов.ИнвентарныйНомер) Тогда
			ОбъектЭксплуатацииПредставление = ОбъектЭксплуатацииПредставление
				+ ", " + СтрШаблон(НСтр("ru = 'инвентарный номер %1';
										|en = 'inventory number %1'"), ВыборкаДокументов.ИнвентарныйНомер);
		КонецЕсли;
		Если ЗначениеЗаполнено(ВыборкаДокументов.Модель) Тогда
			ОбъектЭксплуатацииПредставление = ОбъектЭксплуатацииПредставление
				+ ", " + СтрШаблон(НСтр("ru = 'модель %1';
										|en = 'model %1'"), ВыборкаДокументов.Модель);
		КонецЕсли;
		ПараметрыШапки.Вставить("ОбъектЭксплуатацииПредставление", ОбъектЭксплуатацииПредставление);
		
		ОбластьШапка.Параметры.Заполнить(ПараметрыШапки);
		
		ТабличныйДокумент.Вывести(ОбластьШапка);

		#КонецОбласти
		
		#Область Таблица
		ТабличныйДокумент.Вывести(ОбластьЗаголовокТаблицы);
		ВыборкаДефектныеУзлы = ВыборкаДокументов.ДефектныеУзлы.Выбрать();
		Пока ВыборкаДефектныеУзлы.Следующий() Цикл
			ОбластьСтрокаТаблицы.Параметры.Заполнить(ВыборкаДефектныеУзлы);
			ТабличныйДокумент.Вывести(ОбластьСтрокаТаблицы);
		КонецЦикла;
		
		Если ВыборкаДефектныеУзлы.Количество() = 0 Тогда
			ПараметрыСтроки = Новый Структура("УзелПредставление,ОписаниеДефекта");
			ОбластьСтрокаТаблицы.Параметры.Заполнить(ПараметрыСтроки);
			ТабличныйДокумент.Вывести(ОбластьСтрокаТаблицы);
		КонецЕсли;
		
		#КонецОбласти
		
		ОбластьПодвал.Параметры.Заполнить(ВыборкаДокументов);
		ТабличныйДокумент.Вывести(ОбластьПодвал);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументов.Ссылка);
		
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

//-- Локализация

#КонецОбласти
