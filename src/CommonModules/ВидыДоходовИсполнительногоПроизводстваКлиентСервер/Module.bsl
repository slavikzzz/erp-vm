////////////////////////////////////////////////////////////////////////////////
// Виды доходов исполнительного производства.
// Клиент-серверные процедуры и функции.
////////////////////////////////////////////////////////////////////////////////

// Код, доступный и на клиенте, и на сервере, не обязательно должен использоваться в обоих режимах
// АПК:558-выкл 

#Область СлужебныйПрограммныйИнтерфейс

// Для методов служебного API использование не контролируем          
// АПК:581-выкл 
// АПК:580-выкл 
// АПК:299-выкл

Функция ВидДоходаОбязателенДляБанков(Дата) Экспорт
	Возврат НачалоДня(Дата) >= ДатаНачалаПередачиВидаДоходаБанку();
КонецФункции

Функция ДатаНачалаПередачиВидаДоходаБанку() Экспорт 
	Возврат '20200601';
КонецФункции

Функция КодВидаДохода(ВидДохода) Экспорт
	Если ВидДохода = ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.ДоходыБезОграниченияВзысканий") Тогда
		Возврат ""
	ИначеЕсли ВидДохода = ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.ЗарплатаВознаграждения") Тогда	
		Возврат "1"
	ИначеЕсли ВидДохода = ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.КомпенсацииНеоблагаемые") Тогда	
		Возврат "2"
	ИначеЕсли ВидДохода = ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.КомпенсацииНеоблагаемыеПериодические") Тогда	
		Возврат "2" 
	ИначеЕсли ВидДохода = ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.КомпенсацииОблагаемые") Тогда	
		Возврат "3" 
	ИначеЕсли ВидДохода = ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.КомпенсацииОблагаемыеПериодические") Тогда	
		Возврат "3"  
	ИначеЕсли ВидДохода = ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.КомпенсацииНеоблагаемыеЕдиновременные") Тогда	
		Возврат "4" 
	ИначеЕсли ВидДохода = ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.КомпенсацииОблагаемыеЕдиновременные") Тогда	
		Возврат "5" 
	Иначе
		Возврат Неопределено
	КонецЕсли
КонецФункции

Процедура НастроитьПолеВидДоходаДляВыплатыЗарплаты(Форма, Период, ИмяЭлемента = "ВидДоходаИсполнительногоПроизводства", ПоказатьВсеВидыДоходов = Ложь) Экспорт
	
	НастроитьПодсказкуПоляВидДохода(Форма, ИмяЭлемента, Период);
	НастроитьСписокВыбораПоляВидДохода(Форма, ИмяЭлемента, ЗначенияВыбораВидаДоходаДляВыплатыЗарплаты(Период, ПоказатьВсеВидыДоходов), Период);
		
КонецПроцедуры

Процедура НастроитьПолеВидДоходаДляВыплатыПрочихДоходов(Форма, Период, ИмяЭлемента = "ВидДоходаИсполнительногоПроизводства", ПоказатьВсеВидыДоходов = Ложь) Экспорт
	
	НастроитьПодсказкуПоляВидДохода(Форма, ИмяЭлемента, Период);
	НастроитьСписокВыбораПоляВидДохода(Форма, ИмяЭлемента, ЗначенияВыбораВидаДоходаДляВыплатыПрочихДоходов(Период, ПоказатьВсеВидыДоходов), Период);
		
КонецПроцедуры

Процедура НастроитьКолонкуВидДоходаДляВыплатыЗарплаты(Форма, ИмяТаблицы, ИмяКолонки, ПутьКДаннымКолонки, Период, ПоказатьВсеВидыДоходов = Ложь) Экспорт
	
	НастроитьУсловноеОформлениеКолонкиВидДохода(Форма, ИмяТаблицы, ИмяКолонки, ПутьКДаннымКолонки, Период, ПоказатьВсеВидыДоходов);
	НастроитьСписокВыбораПоляВидДохода(Форма, ИмяКолонки, ЗначенияВыбораВидаДоходаДляВыплатыЗарплаты(Период, ПоказатьВсеВидыДоходов), Период);
	
КонецПроцедуры

Процедура НастроитьКолонкуВидДоходаДляВыплатыПрочихДоходов(Форма, ИмяТаблицы, ИмяКолонки, ПутьКДаннымКолонки, Период, ПоказатьВсеВидыДоходов) Экспорт
	
	НастроитьУсловноеОформлениеКолонкиВидДохода(Форма, ИмяТаблицы, ИмяКолонки, ПутьКДаннымКолонки, Период, ПоказатьВсеВидыДоходов);
	НастроитьСписокВыбораПоляВидДохода(Форма, ИмяКолонки, ЗначенияВыбораВидаДоходаДляВыплатыПрочихДоходов(Период, ПоказатьВсеВидыДоходов), Период);
	
КонецПроцедуры

Процедура НастроитьПодсказкуПоляВидДохода(Форма, ИмяЭлемента, Период) Экспорт
	
	// Подсказка
	ЗарплатаКадрыКлиентСервер.УстановитьРасширеннуюПодсказкуЭлементуФормы(
		Форма, 
		ИмяЭлемента, 
		РасширеннаяПодсказкаПоляВидДохода(Период));
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы, 
		ИмяЭлемента, 
		"ОтображениеПодсказки", 
		ОтображениеПодсказки.Кнопка);
		
КонецПроцедуры

Функция ТекстПредупрежденияОбУстаревшемКодеДоходаИсполнительногоПроизводства() Экспорт
	
	Возврат НСтр("ru = 'В %1 %2 используется устаревший вид дохода исполнительного производства.
			|Если выплата периодическая, то можно оставить текущий код.
			|Если характер выплаты единовременный, то следует поменять вид дохода в шапке ведомости.
			|Если в выплате присутствуют суммы и периодического и единовременного характера, то необходимо сформировать несколько ведомостей по виду дохода.';
			|en = 'In %1%2, an obsolete kind of enforcement proceedings income is used.
			|If the payment is recurring, then you can use the current code.
			|If it is a one-time payment, change the income kind in the statement header.
			|If the payment contains amounts of both recurring and one-time payments, generate multiple statements by income kind.'");
	
КонецФункции

// АПК:299-вкл
// АПК:580-вкл
// АПК:581-вкл

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура НастроитьУсловноеОформлениеКолонкиВидДохода(Форма, ИмяТаблицы, ИмяКолонки, ПутьКДаннымКолонки, Период, ПоказатьВсеВидыДоходов = Ложь)
	
	Для Каждого Значение Из ЗначенияВыбораВидаДоходаДляВыплатыЗарплаты(Период, ПоказатьВсеВидыДоходов) Цикл
		
		ЭлементОформления = Форма.УсловноеОформление.Элементы.Добавить();
		ЭлементОформления.Использование	= Истина;
		
		ОформляемоеПоле =  ЭлементОформления.Поля.Элементы.Добавить();
		ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(ИмяКолонки); 
		ОформляемоеПоле.Использование = Истина;
	
		ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных(ПутьКДаннымКолонки);
		ЭлементОтбора.ПравоеЗначение = Значение;
		
		ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Текст", ПредставлениеДляСпискаВыбораПоляВидДохода(Значение, Период));
		
	КонецЦикла
	
КонецПроцедуры

Процедура НастроитьСписокВыбораПоляВидДохода(Форма, ИмяЭлемента, ЗначенияВыбора, Период)
	
	ПолеВидДохода = Форма.Элементы.Найти(ИмяЭлемента);
	Если ПолеВидДохода = Неопределено Тогда
		Возврат
	КонецЕсли;	

	ПолеВидДохода.РедактированиеТекста = Ложь;
	ПолеВидДохода.РежимВыбораИзСписка  = Истина;
	ПолеВидДохода.СписокВыбора.Очистить();
	Для Каждого Значение Из ЗначенияВыбора Цикл
		ПолеВидДохода.СписокВыбора.Добавить(Значение, ПредставлениеДляСпискаВыбораПоляВидДохода(Значение, Период));
	КонецЦикла	
	
КонецПроцедуры

Функция РасширеннаяПодсказкаПоляВидДохода(Период)
	
	Если ВидыДоходовИсполнительногоПроизводстваВызовСервера.ВступилиВСилуИзменения353ФЗ(Период) Тогда
		Возврат СтрШаблон(
					НСтр("ru = 'Согласно Указанию Банка России от 25.03.2022 N 6104-У при перечислении доходов физическому лицу
					           |следует указывать код вида дохода для целей исполнительного производства (Федеральный закон от 02.10.2007 N 229-ФЗ):
					           |
					           |%1
					           |Заработная плата или иные доходы, в отношении которых ст. 99 установлены ограничения размеров удержания. 
					           |
					           |%2
					           |Доходы, на которые в соответствии с ч.1 ст. 101 не может быть обращено взыскание и которые имеют характер периодических выплат, 
					           |за исключением доходов, к которым в соответствии с ч. 2 ст. 101 ограничения по обращению взыскания не применяются.
							   |
				               |%3
					           |Доходы, к которым в соответствии с ч. 2 ст. 101 ограничения по обращению взыскания не применяются 
							   |и которые имеют характер периодических выплат.
					           |
					           |%4
					           |Доходы, на которые в соответствии с ч.1 ст. 101 не может быть обращено взыскание и которые имеют характер единовременных выплат,
					           |за исключением доходов, к которым в соответствии с ч. 2 ст. 101 ограничения по обращению взыскания не применяются.
							   |
				               |%5
					           |Доходы, к которым в соответствии с ч. 2 ст. 101 ограничения по обращению взыскания не применяются 
							   |и которые имеют характер единовременных выплат.
					           |
					           |%6
					           |Другие доходы, на которые может быть обращено взыскание без ограничений.
					           |Код вида дохода не указывается';
					           |en = 'According to Instruction of the Bank of Russia No. 6104-U dated 03/25/2022, when transferring income to an individual,
					           |specify the income kind code for enforcement proceedings (Federal Law No. 229-FZ dated 10/02/2007):
					           |
					           |%1
					           |Payroll or other income for which Art. 99 limits the amount of deduction.
					           |
					           |%2
					           |Income that cannot be subject to collection in accordance with Part 1 of Art. 101 and that are recurring payments,
					           |except for income to which collection limits are not applied in accordance with Part 2 of Art. 101.
					           |
					           |%3
					           |Income to which collection limits are not applied in accordance with Part 2 of Art. 101
					           |and that are recurring payments.
					           |
					           |%4
					           |Income that cannot be subject to collection in accordance with Part 1 of Art. 101 and that are one-time payments,
					           |except for income to which collection limits are not applied in accordance with Part 2 of Art. 101.
					           |
					           |%5
					           |Income to which collection limits are not applied in accordance with Part 2 of Art. 101
					           |and that are one-time payments.
					           |
					           |%6
					           |Other income that can be subject to collection without limits.
					           |Income kind code is not specified.'"),
					ПредставлениеДляСпискаВыбораПоляВидДохода(ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.ЗарплатаВознаграждения"), Период),
					ПредставлениеДляСпискаВыбораПоляВидДохода(ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.КомпенсацииНеоблагаемыеПериодические"), Период),
					ПредставлениеДляСпискаВыбораПоляВидДохода(ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.КомпенсацииОблагаемыеПериодические"), Период),
					ПредставлениеДляСпискаВыбораПоляВидДохода(ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.КомпенсацииНеоблагаемыеЕдиновременные"), Период),
					ПредставлениеДляСпискаВыбораПоляВидДохода(ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.КомпенсацииОблагаемыеЕдиновременные"), Период),
					ПредставлениеДляСпискаВыбораПоляВидДохода(ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.ДоходыБезОграниченияВзысканий"),Период));
	Иначе
		Возврат СтрШаблон(
					НСтр("ru = 'Согласно Указанию Банка России от 14.10.2019 N 5286-У при перечислении доходов физическому лицу
					           |следует указывать код вида дохода для целей исполнительного производства (Федеральный закон от 02.10.2007 N 229-ФЗ):
					           |
					           |%1
					           |Заработная плата или иные доходы, в отношении которых ст. 99 установлены ограничения размеров удержания. 
					           |
					           |%2
					           |Доходы, на которые в соответствии с ч.1 ст. 101 не может быть обращено взыскание, за исключением доходов, 
					           |к которым в соответствии с ч. 2 ст. 101 ограничения по обращению взыскания не применяются.
					           |
					           |%3
					           |Доходы, к которым в соответствии с ч. 2 ст. 101 ограничения по обращению взыскания не применяются.
					           |
					           |%4
					           |Другие доходы, на которые может быть обращено взыскание без ограничений.
					           |Код вида дохода не указывается';
					           |en = 'According to the Ordinance of Bank of Russia dated 14.10.2019 No. 5286-U, when transferring income to an individual,
					           |the income kind code for enforcement proceedings should be indicated (Federal Law dated 02.10.2007 No. 229-FZ):
					           |
					           |%1
					           |Salary or other income in respect of which Art. 99 established deduction amount limits set. 
					           |
					           |%2
					           |Income that cannot be subject to collection in accordance with Art. 101, with the exception of income
					           | to which collection limits in accordance with Part 2 of Art. 101 are not applied. 
					           |
					           |%3
					           |Income to which collection limits in accordance with part 2 of Art. 101 do not apply.
					           |
					           |%4
					           |Other income that can be subject to collection without limitations.
					           |The income kind is not specified.'"),
					ПредставлениеДляСпискаВыбораПоляВидДохода(ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.ЗарплатаВознаграждения"), Период),
					ПредставлениеДляСпискаВыбораПоляВидДохода(ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.КомпенсацииНеоблагаемые"), Период),
					ПредставлениеДляСпискаВыбораПоляВидДохода(ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.КомпенсацииОблагаемые"), Период),
					ПредставлениеДляСпискаВыбораПоляВидДохода(ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.ДоходыБезОграниченияВзысканий"), Период));
	КонецЕсли;
	
КонецФункции

Функция ЗначенияВыбораВидаДоходаДляВыплатыЗарплаты(Период, ПоказатьВсеВидыДоходов)
	
	ЗначенияВыбора = Новый Массив;
	ЗначенияВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.ДоходыБезОграниченияВзысканий"));
	ЗначенияВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.ЗарплатаВознаграждения"));
	Если ВидыДоходовИсполнительногоПроизводстваВызовСервера.ВступилиВСилуИзменения353ФЗ(Период) Тогда
		ЗначенияВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.КомпенсацииНеоблагаемыеПериодические"));
		ЗначенияВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.КомпенсацииОблагаемыеПериодические"));
		ЗначенияВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.КомпенсацииНеоблагаемыеЕдиновременные"));
		ЗначенияВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.КомпенсацииОблагаемыеЕдиновременные"));
	КонецЕсли;
	Если Не ВидыДоходовИсполнительногоПроизводстваВызовСервера.ВступилиВСилуИзменения353ФЗ(Период) Или ПоказатьВсеВидыДоходов Тогда
		ЗначенияВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.КомпенсацииНеоблагаемые"));
		ЗначенияВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.КомпенсацииОблагаемые"));
	КонецЕсли;

	Возврат ЗначенияВыбора;
	
КонецФункции

Функция ЗначенияВыбораВидаДоходаДляВыплатыПрочихДоходов(Период, ПоказатьВсеВидыДоходов)
	
	ЗначенияВыбора = Новый Массив;
	ЗначенияВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.ДоходыБезОграниченияВзысканий"));
	ЗначенияВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.ЗарплатаВознаграждения"));
	Если ВидыДоходовИсполнительногоПроизводстваВызовСервера.ВступилиВСилуИзменения353ФЗ(Период) Тогда
		ЗначенияВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.КомпенсацииНеоблагаемыеПериодические"));
		ЗначенияВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.КомпенсацииОблагаемыеПериодические"));
		ЗначенияВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.КомпенсацииНеоблагаемыеЕдиновременные"));
		ЗначенияВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.КомпенсацииОблагаемыеЕдиновременные"));
	КонецЕсли;
	Если Не ВидыДоходовИсполнительногоПроизводстваВызовСервера.ВступилиВСилуИзменения353ФЗ(Период) Или ПоказатьВсеВидыДоходов Тогда
		ЗначенияВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.КомпенсацииНеоблагаемые"));
		ЗначенияВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.КомпенсацииОблагаемые"));
	КонецЕсли;

	Возврат ЗначенияВыбора;

КонецФункции

Функция ПредставлениеДляСпискаВыбораПоляВидДохода(Значение, Период)
	
	Если Значение = ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.ДоходыБезОграниченияВзысканий") Тогда
		Возврат СтрСоединить(Новый Массив(5), " ") + НСтр("ru = 'Доходы без ограничения взысканий';
															|en = 'Income without collection limits'")
	ИначеЕсли Значение = ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.ЗарплатаВознаграждения") Тогда
		Возврат НСтр("ru = '1 - Заработная плата и иные доходы с ограничением взыскания';
					|en = '1 - Salary and other income with collection limits'")
	ИначеЕсли Значение = ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.КомпенсацииНеоблагаемые") Тогда
		Возврат ?(ВидыДоходовИсполнительногоПроизводстваВызовСервера.ВступилиВСилуИзменения353ФЗ(Период),
			НСтр("ru = '2 - (устарел) Доходы, на которые не может быть обращено взыскание (без оговорок)';
				|en = '2 - (obsolete) Income that cannot be subject to collection (without reservations)'"),
			НСтр("ru = '2 - Доходы, на которые не может быть обращено взыскание (без оговорок)';
				|en = '2 - Income that cannot be subject to collection (without reservations)'"));
	ИначеЕсли Значение = ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.КомпенсацииНеоблагаемыеПериодические") Тогда
		Возврат НСтр("ru = '2 - Периодические доходы, на которые не может быть обращено взыскание (без оговорок)';
					|en = '2 - Recurring income that cannot be subject to collection (without reservations)'")
	ИначеЕсли Значение = ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.КомпенсацииОблагаемые") Тогда
		Возврат ?(ВидыДоходовИсполнительногоПроизводстваВызовСервера.ВступилиВСилуИзменения353ФЗ(Период),
			НСтр("ru = '3 - (устарел) Доходы, на которые не может быть обращено взыскание (с оговорками)';
				|en = '3 - (obsolete) Income that cannot be subject to collection (with reservations)'"),
			НСтр("ru = '3 - Доходы, на которые не может быть обращено взыскание (с оговорками)';
				|en = '3 - Income that cannot be subject to collection (with reservations)'"));
	ИначеЕсли Значение = ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.КомпенсацииОблагаемыеПериодические") Тогда
		Возврат НСтр("ru = '3 - Периодические доходы, на которые не может быть обращено взыскание (с оговорками)';
					|en = '3 - Recurring income that cannot be subject to collection (with reservations)'")
	ИначеЕсли Значение = ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.КомпенсацииНеоблагаемыеЕдиновременные") Тогда
		Возврат НСтр("ru = '4 - Единовременные доходы, на которые не может быть обращено взыскание (без оговорок)';
					|en = '4 - One-time income that cannot be subject to collection (without reservations)'")
	ИначеЕсли Значение = ПредопределенноеЗначение("Перечисление.ВидыДоходовИсполнительногоПроизводства.КомпенсацииОблагаемыеЕдиновременные") Тогда
		Возврат НСтр("ru = '5 - Единовременные доходы, на которые не может быть обращено взыскание (с оговорками)';
					|en = '5 - One-time income that  cannot be subject to collection (with reservations)'")
	Иначе
		Возврат ""
	КонецЕсли
	
КонецФункции	

#КонецОбласти

// АПК:558-вкл
