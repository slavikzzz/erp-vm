#Область СлужебныйПрограммныйИнтерфейс

#Область ОбработчикиЭтаповЗакрытияМесяца

//++ Локализация

//++ НЕ УТ
#Область СписаниеЗатратНаВыпускБезРаспоряжений

// Добавляет этап в таблицу этапов закрытия месяца.
// Элементы данной таблицы являются элементами второго уровня в дереве этапов в форме закрытия месяца.
// 
// Параметры:
// 	ТаблицаЭтапов - (См. Обработки.ОперацииЗакрытияМесяца.ЗаполнитьОписаниеЭтаповЗакрытияМесяца)
// 	ТекущийРодитель - Строка - идентификатор группы.
Процедура ДобавитьЭтап_СписаниеЗатратНаВыпускБезРаспоряжений(ТаблицаЭтапов,ТекущийРодитель) Экспорт
	НоваяСтрока = ЗакрытиеМесяцаСервер.ДобавитьЭтапВТаблицу(ТаблицаЭтапов, ТекущийРодитель,
		Перечисления.ОперацииЗакрытияМесяца.СписаниеЗатратНаВыпускБезРаспоряжений,,,,
		Перечисления.ОперацииЗакрытияМесяца.НачислениеСписаниеРезервовПредстоящихРасходов);
	НоваяСтрока.ВыполняетсяВручную = Истина;
	НоваяСтрока.ТекстВыполнить = НСтр("ru = 'Списать';
										|en = 'Write off'");
	НоваяСтрока.ДействиеОформление = ЗакрытиеМесяцаСервер.ОписаниеДействия_СервернаяПроцедура(
		"ЗатратыЛокализацияСервер.Оформление_СписаниеЗатратНаВыпускБезРаспоряжений");
	НоваяСтрока.ДействиеИспользование = ЗакрытиеМесяцаСервер.ОписаниеДействия_СервернаяПроцедура(
		"ЗатратыЛокализацияСервер.Использование_СписаниеЗатратНаВыпускБезРаспоряжений");
	НоваяСтрока.ДействиеВыполнить  = ЗакрытиеМесяцаСервер.ОписаниеДействия_ОткрытьФорму(
		Метаданные.Документы.СписаниеЗатратНаВыпуск.Формы.ФормаСпискаДокументов.ПолноеИмя());
КонецПроцедуры

// Обработчики этапа.

Процедура Использование_СписаниеЗатратНаВыпускБезРаспоряжений(ПараметрыОбработчика) Экспорт
	
	ЗакрытиеМесяцаСервер.УвеличитьКоличествоОбработанныхДанныхДляЗамера(ПараметрыОбработчика, 1);
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьПроизводство") Тогда
		
		ЗакрытиеМесяцаСервер.УстановитьСостояниеОтключено(
			ПараметрыОбработчика,
			НСтр("ru = 'Учет производственных операций не ведется';
				|en = 'Production operation accounting is not kept'", ОбщегоНазначения.КодОсновногоЯзыка()));
			
		Возврат;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ЗакрытиеМесяцаСервер.ИнициализироватьЗапрос(Запрос, ПараметрыОбработчика);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Т.Организация,
	|	Т.Распоряжение,
	|	Т.Номенклатура,
	|	Т.Характеристика,
	|	Т.КоличествоКонечныйОстаток КАК КонечныйОстаток,
	|	Т.КоличествоНачальныйОстаток КАК НачальныйОстаток,
	|	Т.КоличествоПриход КАК Оборот
	|ПОМЕСТИТЬ ВТРаспоряженияНаСписание
	|ИЗ
	|	РегистрНакопления.РаспоряженияНаСписаниеПоНормативам.ОстаткиИОбороты(&НачалоПериода, &КонецПериода,,,
	|		Организация В (&МассивОрганизаций)) КАК Т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Организация,
	|	Т.Распоряжение,
	|	СУММА(1) КАК КоличествоСтрок
	|ПОМЕСТИТЬ ВТКОформлению
	|ИЗ
	|	ВТРаспоряженияНаСписание КАК Т
	|ГДЕ
	|	Т.НачальныйОстаток = 0
	|	И Т.КонечныйОстаток <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Организация,
	|	Т.Распоряжение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК ЕстОбороты
	|ПОМЕСТИТЬ ВТОбороты
	|ИЗ
	|	ВТРаспоряженияНаСписание КАК Т
	|ГДЕ
	|	Т.НачальныйОстаток = 0
	|	И Т.КонечныйОстаток = 0
	|	И Т.Оборот <> 0";
	
	Запрос.Выполнить();
	
	РазмерыВременныхТаблиц = ЗакрытиеМесяцаСервер.РазмерыВременныхТаблиц(Запрос, ПараметрыОбработчика);
	
	Если РазмерыВременныхТаблиц.ВТКОформлению = 0 И РазмерыВременныхТаблиц.ВТОбороты = 0 Тогда
		
		ЗакрытиеМесяцаСервер.УстановитьСостояниеНеТребуется(
			ПараметрыОбработчика,
			НСтр("ru = 'Нет незакрытых оборотов по регистру ""Распоряжения на списание по нормативам""';
				|en = 'No unclosed turnovers in the ""Standard write-off references"" register'", ОбщегоНазначения.КодОсновногоЯзыка()));
		
	КонецЕсли;
	
КонецПроцедуры

// Описание оформления этама списания затрат на выпуск.
// Параметры:
//	ПараметрыОбработчика - см. ЗакрытиеМесяцаСервер.ИнициализироватьПараметрыОбработчикаЭтапаЗакрытияМесяцаДляПроверки
Процедура Оформление_СписаниеЗатратНаВыпускБезРаспоряжений(ПараметрыОбработчика) Экспорт
	
	ИспользуетсяПроизводство22 = ПроизводствоСервер.НастройкиПодсистемыПроизводство().ИспользуетсяПроизводство22;
	Если ИспользуетсяПроизводство22 Тогда
		ПараметрыОбработчика.ДанныеЭтапа.Наименование = НСтр("ru = 'Списание затрат на выпуск без распоряжений (2.1)';
															|en = 'Write off release costs without references (2.1)'");
	ИначеЕсли ПолучитьФункциональнуюОпцию("КомплекснаяАвтоматизация") Тогда
		ПараметрыОбработчика.ДанныеЭтапа.Наименование = НСтр("ru = 'Списание затрат на выпуск';
															|en = 'Write off release costs'");
	КонецЕсли;
	
	ПараметрыОбработчика.ДанныеЭтапа.ТекстВыполнить = НСтр("ru = 'Списать';
															|en = 'Write off'");
	
	ЗакрытиеМесяцаСервер.УвеличитьКоличествоОбработанныхДанныхДляЗамера(ПараметрыОбработчика);
	
КонецПроцедуры

// Проверки состояния системы, относящиеся к этапу.

Процедура ОписаниеПроверок_СписаниеЗатратНаВыпускБезРаспоряжений(ТаблицаПроверок) Экспорт
	
	// Списание затрат на выпуск без распоряжений.
	ОписаниеПроверки = ЗакрытиеМесяцаСервер.ДобавитьОписаниеНовойПроверки(ТаблицаПроверок,
		"НеОформленныеСписанияЗатратНаВыпускБезРаспоряжений",
		Перечисления.ОперацииЗакрытияМесяца.СписаниеЗатратНаВыпускБезРаспоряжений,
		Перечисления.МоментЗапускаПроверкиОперацииЗакрытияМесяца.ДоИПослеРасчета,
		"ЗатратыЛокализацияСервер.ПроверкаНеобходимостиСписанияЗатратНаВыпускБезРаспоряжений");
	
	ЗакрытиеМесяцаСервер.ЗаполнитьПредставлениеНовойПроверки(ОписаниеПроверки,
		НСтр("ru = 'Не оформленные списания материалов (работ) на выпуски продукции без распоряжений';
			|en = 'Non-registered material (work) write-off for product releases without references'", ОбщегоНазначения.КодОсновногоЯзыка()),
		НСтр("ru = 'Должно быть оформлены все списания материалов (работ) и распределены трудозатраты на все документы ""Выпуск продукции""
			|по подразделениям, для которых списание затрат выполняется документом ""Списание затрат на выпуск"".';
			|en = 'All material (work) write-offs should be registered and labor costs should be allocated to all documents ""Product release""
			|by business units for which costs are written off with the ""Write off release costs"" document.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
КонецПроцедуры

Процедура ПроверкаНеобходимостиСписанияЗатратНаВыпускБезРаспоряжений(ПараметрыПроверки) Экспорт
	
	Если НЕ ЗакрытиеМесяцаСервер.ПроверкаВыполняетсяМеханизмомЗакрытияМесяца(ПараметрыПроверки) Тогда
		Возврат;
	КонецЕсли;
	
	СписокПолей = Новый СписокЗначений;
	СписокПолей.Добавить("Организация", 	НСтр("ru = 'Организация';
												|en = 'Company'", ОбщегоНазначения.КодОсновногоЯзыка()));
	СписокПолей.Добавить("Распоряжение", 	НСтр("ru = 'Документ выпуска';
													|en = 'Release document'", ОбщегоНазначения.КодОсновногоЯзыка()));
	СписокПолей.Добавить("КоличествоСтрок", НСтр("ru = 'Количество строк с ошибками';
												|en = 'Quantity of lines with errors'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	ПараметрыРегистрации = ЗакрытиеМесяцаСервер.ИнициализироватьПараметрыРегистрацииПроблемПроверки(
		"ВТКОформлению",
		НСтр("ru = 'Обнаружены не оформленные списания материалов (работ) организации ""%1"" на конец периода %2';
			|en = 'Non-registered write-off of materials (works) is detected for the ""%1"" company at the end of %2 period'", ОбщегоНазначения.КодОсновногоЯзыка()),
		СписокПолей,
		"Распоряжение");
	
	ЗакрытиеМесяцаСервер.ЗарегистрироватьПроблемыВыполненияПроверки(
	 	ПараметрыПроверки,
		ПараметрыРегистрации);
	
КонецПроцедуры

#КонецОбласти

#Область РаспределениеМатериаловИРаботНаСебестоимостьПродукции21

// Добавляет этап в таблицу этапов закрытия месяца.
// Элементы данной таблицы являются элементами второго уровня в дереве этапов в форме закрытия месяца.
// 
// Параметры:
// 	ТаблицаЭтапов - (См. Обработки.ОперацииЗакрытияМесяца.ЗаполнитьОписаниеЭтаповЗакрытияМесяца)
// 	ТекущийРодитель - Строка - идентификатор группы.
Процедура ДобавитьЭтап_РаспределениеМатериаловИРаботНаСебестоимостьПродукции21(ТаблицаЭтапов,ТекущийРодитель) Экспорт
	
	НоваяСтрока = ЗакрытиеМесяцаСервер.ДобавитьЭтапВТаблицу(ТаблицаЭтапов, ТекущийРодитель,
		Перечисления.ОперацииЗакрытияМесяца.РаспределениеМатериаловИРаботНаСебестоимостьПродукции21,,,,
		Перечисления.ОперацииЗакрытияМесяца.СписаниеЗатратНаВыпускБезРаспоряжений);
	НоваяСтрока.ВыполняетсяВручную = Истина;
	НоваяСтрока.ДействиеОформление = ЗакрытиеМесяцаСервер.ОписаниеДействия_СервернаяПроцедура(
		"ЗатратыЛокализацияСервер.Оформление_РаспределениеМатериаловИРаботНаСебестоимостьПродукции21");
	НоваяСтрока.ДействиеИспользование = ЗакрытиеМесяцаСервер.ОписаниеДействия_СервернаяПроцедура(
		"ЗатратыЛокализацияСервер.Использование_РаспределениеМатериаловИРаботНаСебестоимостьПродукции21");
	НоваяСтрока.ДействиеВыполнить  = ЗакрытиеМесяцаСервер.ОписаниеДействия_ОткрытьФорму(
		Метаданные.Документы.РаспределениеПроизводственныхЗатрат.Формы.ФормаРабочееМесто.ПолноеИмя());
	// Доп. параметры формы.
	НоваяСтрока.ДействиеВыполнить.ПараметрыФормы.Вставить("Состояние",     		     "ТребуетсяРаспределить");
	НоваяСтрока.ДействиеВыполнить.ПараметрыФормы.Вставить("Подразделение", 		     Справочники.СтруктураПредприятия.ПустаяСсылка());
	НоваяСтрока.ДействиеВыполнить.ПараметрыФормы.Вставить("ТребуетсяСписатьЗатраты", Ложь);
	
КонецПроцедуры

// Обработчики этапа.

Процедура Использование_РаспределениеМатериаловИРаботНаСебестоимостьПродукции21(ПараметрыОбработчика) Экспорт
	
	ЗакрытиеМесяцаСервер.УвеличитьКоличествоОбработанныхДанныхДляЗамера(ПараметрыОбработчика, 1);
	
	Если ПараметрыОбработчика.АвтоматическоеТестирование Тогда
		
		ЗакрытиеМесяцаСервер.УстановитьСостояниеНеТребуется(
			ПараметрыОбработчика,
			НСтр("ru = 'Проверка отключена при автоматическом тестировании';
				|en = 'Checking is disabled during automatic testing'", ОбщегоНазначения.КодОсновногоЯзыка()));
			
		Возврат;
		
	КонецЕсли;
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьПроизводство") Тогда
		
		ЗакрытиеМесяцаСервер.УстановитьСостояниеОтключено(
			ПараметрыОбработчика,
			НСтр("ru = 'Учет производственных операций не ведется';
				|en = 'Production operation accounting is not kept'", ОбщегоНазначения.КодОсновногоЯзыка()));
			
		Возврат;
		
	КонецЕсли;
	
	ЗакрытиеМесяцаСервер.УвеличитьКоличествоОбработанныхДанныхДляЗамера(ПараметрыОбработчика, 1);
	
	НастройкиПроизводства = ПроизводствоСервер.НастройкиПодсистемыПроизводство();
	
	Если НЕ НастройкиПроизводства.ИспользуетсяПроизводство21 Тогда
		
		ЗакрытиеМесяцаСервер.УстановитьСостояниеНеТребуется(
			ПараметрыОбработчика,
			НСтр("ru = 'Не используется управление производством (версия 2.1)';
				|en = 'Production management, version 2.1 is not used'", ОбщегоНазначения.КодОсновногоЯзыка()));
			
		Возврат;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ЗакрытиеМесяцаСервер.ИнициализироватьЗапрос(Запрос, ПараметрыОбработчика);
	
	Запрос.Текст = Документы.РаспределениеПроизводственныхЗатрат.ТекстЗапросаЗаполнитьПроизводственныеЗатраты(Истина);
	
	// Доп. параметры запроса.
	Запрос.УстановитьПараметр("ОкончаниеПериода",  	     Запрос.Параметры.КонецПериода);
	Запрос.УстановитьПараметр("ГраницаОкончаниеПериода", Запрос.Параметры.ГраницаКонецПериода);
	Запрос.УстановитьПараметр("ПоВсемОрганизациям",	     Ложь);
	Запрос.УстановитьПараметр("Организация",       	     Запрос.Параметры.МассивОрганизаций);
	Запрос.УстановитьПараметр("ВсеПодразделения",  	     Истина);
	Запрос.УстановитьПараметр("Подразделения",     	     Новый Массив);
	
	Запрос.Выполнить();
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДД.Организация
	|ПОМЕСТИТЬ ДвиженияМатериаловВПроизводстве
	|ИЗ
	|	РегистрНакопления.МатериалыИРаботыВПроизводстве КАК ДД
	|ГДЕ
	|	ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ДД.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|	И ДД.Организация В (&МассивОрганизаций)
	|	И ДД.Активность
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МатериалыИРаботы.Организация,
	|	МатериалыИРаботы.Подразделение,
	|	МатериалыИРаботы.Номенклатура,
	|	МатериалыИРаботы.Характеристика,
	|	МатериалыИРаботы.Серия,
	|	МатериалыИРаботы.КРаспределению КАК Количество
	|ПОМЕСТИТЬ ВТНераспределенныеМатериалыИРаботы
	|ИЗ
	|	МатериалыИРаботы КАК МатериалыИРаботы
	|ГДЕ
	|	МатериалыИРаботы.КРаспределению <> 0
	|	И МатериалыИРаботы.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	МатериалыИРаботы.Организация,
	|	МатериалыИРаботы.Подразделение,
	|	МатериалыИРаботы.Номенклатура,
	|	МатериалыИРаботы.Характеристика,
	|	МатериалыИРаботы.Серия,
	|	МатериалыИРаботы.КРаспределению КАК Количество
	|ИЗ
	|	МатериалыИРаботы КАК МатериалыИРаботы
	|	ЛЕВОЕ СОЕДИНЕНИЕ ДвиженияМатериаловВПроизводстве КАК ДвиженияМатериаловВПроизводстве
	|	ПО МатериалыИРаботы.Организация = ДвиженияМатериаловВПроизводстве.Организация
	|ГДЕ
	|	МатериалыИРаботы.КРаспределению <> 0
	|	И МатериалыИРаботы.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|	И (НЕ ДвиженияМатериаловВПроизводстве.Организация ЕСТЬ NULL 
	|		ИЛИ НЕ &ИспользуетсяПроизводство22)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	МатериалыИРаботы.Организация
	|ПОМЕСТИТЬ ВТРаспределенныеМатериалыИРаботы
	|ИЗ
	|	МатериалыИРаботы КАК МатериалыИРаботы
	|ГДЕ
	|	МатериалыИРаботы.РаспределеноНаПроизводство > 0";
	
	Запрос.УстановитьПараметр("ИспользуетсяПроизводство22", НастройкиПроизводства.ИспользуетсяПроизводство22);
	
	Запрос.Выполнить();
	
	РазмерыВременныхТаблиц = ЗакрытиеМесяцаСервер.РазмерыВременныхТаблиц(Запрос, ПараметрыОбработчика);
	
	Если РазмерыВременныхТаблиц.ВТНераспределенныеМатериалыИРаботы = 0
	 И РазмерыВременныхТаблиц.ВТРаспределенныеМатериалыИРаботы = 0 Тогда
		
		ЗакрытиеМесяцаСервер.УстановитьСостояниеНеТребуется(
			ПараметрыОбработчика,
			НСтр("ru = 'Нет материалов и работ для распределения на производство';
				|en = 'No materials and works to allocate to production'", ОбщегоНазначения.КодОсновногоЯзыка()));
		
	КонецЕсли;
	
КонецПроцедуры

// Описание оформления этапа распределение материалов на себестоимость продукции.
// Параметры:
//	ПараметрыОбработчика - см. ЗакрытиеМесяцаСервер.ИнициализироватьПараметрыОбработчикаЭтапаЗакрытияМесяцаДляПроверки
Процедура Оформление_РаспределениеМатериаловИРаботНаСебестоимостьПродукции21(ПараметрыОбработчика) Экспорт
	
	НастройкиПодсистемыПроизводство = ПроизводствоСервер.НастройкиПодсистемыПроизводство();
	ИспользуетсяПроизводство21 = НастройкиПодсистемыПроизводство.ИспользуетсяПроизводство21;
	ИспользуетсяПроизводство22 = НастройкиПодсистемыПроизводство.ИспользуетсяПроизводство22;
	
	Если НЕ (ИспользуетсяПроизводство21 И ИспользуетсяПроизводство22) Тогда
		ПараметрыОбработчика.ДанныеЭтапа.Наименование = НСтр("ru = 'Распределение материалов и работ на себестоимость продукции';
															|en = 'Material and labor allocation to product cost'");
	КонецЕсли;
	
	ПараметрыОбработчика.ДанныеЭтапа.ТекстВыполнить = НСтр("ru = 'Распределить';
															|en = 'Allocate'");
	
	ЗакрытиеМесяцаСервер.УвеличитьКоличествоОбработанныхДанныхДляЗамера(ПараметрыОбработчика);
	
КонецПроцедуры

// Проверки состояния системы, относящиеся к этапу.

Процедура ОписаниеПроверок_РаспределениеМатериаловИРаботНаСебестоимостьПродукции21(ТаблицаПроверок) Экспорт
	
	// Распределение материалов и работ на себестоимость продукции (2.1).
	ОписаниеПроверки = ЗакрытиеМесяцаСервер.ДобавитьОписаниеНовойПроверки(ТаблицаПроверок,
		"НеРаспределеныМатериалыИРаботыНаСебестоимостьПродукции21",
		Перечисления.ОперацииЗакрытияМесяца.РаспределениеМатериаловИРаботНаСебестоимостьПродукции21,
		Перечисления.МоментЗапускаПроверкиОперацииЗакрытияМесяца.ДоИПослеРасчета,
		"ЗатратыЛокализацияСервер.ПроверкаНеобходимостиРаспределенияМатериаловИРаботНаСебестоимостьПродукции21");
	
	ЗакрытиеМесяцаСервер.ЗаполнитьПредставлениеНовойПроверки(ОписаниеПроверки,
		НСтр("ru = 'Не распределенные материалы (работы) на производство';
			|en = 'Non-allocated materials (works) to production'", ОбщегоНазначения.КодОсновногоЯзыка()),
		НСтр("ru = 'Должно быть оформлено распределение всех материалов и работ на производство документами ""Распределение материалов и работ"".';
			|en = 'Allocation of all materials and works to production should be registered with the ""Allocation of materials and works"" documents.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
КонецПроцедуры

Процедура ПроверкаНеобходимостиРаспределенияМатериаловИРаботНаСебестоимостьПродукции21(ПараметрыПроверки) Экспорт
	
	Если НЕ ЗакрытиеМесяцаСервер.ПроверкаВыполняетсяМеханизмомЗакрытияМесяца(ПараметрыПроверки) Тогда
		Возврат;
	КонецЕсли;
	
	СписокПолей = Новый СписокЗначений;
	СписокПолей.Добавить("Организация", 	НСтр("ru = 'Организация';
												|en = 'Company'", ОбщегоНазначения.КодОсновногоЯзыка()));
	СписокПолей.Добавить("Подразделение", 	НСтр("ru = 'Подразделение';
													|en = 'Business unit'", ОбщегоНазначения.КодОсновногоЯзыка()));
	СписокПолей.Добавить("Номенклатура", 	НСтр("ru = 'Номенклатура';
													|en = 'Products'", ОбщегоНазначения.КодОсновногоЯзыка()));
	СписокПолей.Добавить("Характеристика", 	НСтр("ru = 'Характеристика';
													|en = 'Characteristic'", ОбщегоНазначения.КодОсновногоЯзыка()));
	СписокПолей.Добавить("Серия", 			НСтр("ru = 'Серия';
													|en = 'Series'", ОбщегоНазначения.КодОсновногоЯзыка()));
	СписокПолей.Добавить("Количество",		НСтр("ru = 'Количество';
													|en = 'Quantity'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	ПараметрыРегистрации = ЗакрытиеМесяцаСервер.ИнициализироватьПараметрыРегистрацииПроблемПроверки(
		"ВТНераспределенныеМатериалыИРаботы",
		НСтр("ru = 'Обнаружены не распределенные на производство материалы (работы) организации ""%1"" на конец периода %2';
			|en = 'Materials (works) not allocated for production were detected for the ""%1"" company as of the end of %2 period'", ОбщегоНазначения.КодОсновногоЯзыка()),
		СписокПолей);
	
	ЗакрытиеМесяцаСервер.ЗарегистрироватьПроблемыВыполненияПроверки(
	 	ПараметрыПроверки,
		ПараметрыРегистрации);
	
КонецПроцедуры

#КонецОбласти

//-- НЕ УТ

//-- Локализация

#КонецОбласти

#КонецОбласти
