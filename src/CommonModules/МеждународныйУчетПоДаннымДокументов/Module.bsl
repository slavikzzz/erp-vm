#Область ПрограммныйИнтерфейс

// Выполняет формирование проводок международного учета непосредственно по данным документов.
// 
// Параметры:
// 	ПараметрыОтражения - Структура - Параметры формирования проводок.
// 	ТаблицаПроводок - ТаблицаЗначений - Таблица, в которую добавляются сформированные проводки.
//
Процедура Отразить(ПараметрыОтражения, ТаблицаПроводок) Экспорт
	
	//++ Локализация
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьДокументыВнеоборотныхАктивовМеждународныйУчет") Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого КлючИЗначение Из ТипыДокументовКОтражению() Цикл
		Если КлючИЗначение.Значение = Ложь Тогда
			ОтразитьДанныеДокумента(КлючИЗначение.Ключ, ПараметрыОтражения, ТаблицаПроводок);
		КонецЕсли;
	КонецЦикла;
	//-- Локализация
	
КонецПроцедуры

// Дополняет таблицы регистрации документа к отражению в международном учете по данным самого документа.
// 
// Параметры:
// 	Объект - ДокументОбъект - Документ, который проводится.
// 	ДополнительныеСвойства - Структура - Дополнительные свойства РС ОтражениеДокументовВМеждународномУчете.
// 	ТаблицаРегистрации - ТаблицаЗначений - Таблица с данными регистрации к отражению:
// 	      * Период - Дата - период регистрации (дата документа);
// 	      * Организация - СправочникСсылка.Организации - организация по которой документ формирует проводки;
// 	      * ДатаОтражения - Дата - дата, на которую документ формирует проводки;
// 	      * ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации - отражаемая хозяйственная операция.
//
Процедура ДополнитьТаблицыРегистрацииКОтражению(Объект, ДополнительныеСвойства, ТаблицаРегистрации) Экспорт
	
	Если ТипЗнч(Объект) = Тип("ДокументОбъект.РасчетКурсовыхРазниц")
		И ТаблицаРегистрации.Количество() = 0 Тогда
		
		НоваяСтрока = ТаблицаРегистрации.Добавить();
		НоваяСтрока.Период = Объект.Дата;
		НоваяСтрока.Организация = Объект.Организация;
		НоваяСтрока.ДатаОтражения = Объект.Дата;
		НоваяСтрока.ХозяйственнаяОперация = Объект.ХозяйственнаяОперация;
		
	КонецЕсли;
	//++ Локализация
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьДокументыВнеоборотныхАктивовМеждународныйУчет") Тогда
		Возврат;
	КонецЕсли;
	Документ = Объект.Ссылка;
	ТипДокумента = ТипЗнч(Документ);
	
	ТипыДокументовКОтражению = ТипыДокументовКОтражению();
	Если ТипыДокументовКОтражению[ТипДокумента] = Ложь Тогда
		МенеджерДокумента = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Документ);
		МенеджерДокумента.ЗаполнитьТаблицуДокументаКОтражениюВМеждународномУчете(ТаблицаРегистрации, Документ);
	КонецЕсли;
	//-- Локализация
	
КонецПроцедуры

// Возвращает типы документов, которые отражаются в учете непосредственно по своим данным.
// 
// Возвращаемое значение:
// 	Соответствие - Типы документов:
// 	               Ключ - Тип - Тип документа
// 	               Значение - Булево - Признак того, что документ отражается только при проведении - не может отражаться отложенно.
// 
Функция ТипыДокументовКОтражению() Экспорт
	
	ТипыДокументов = Новый Соответствие;
	//++ Локализация
	ТипыДокументов.Вставить(Тип("ДокументСсылка.ПринятиеКУчетуОСМеждународныйУчет"),       Ложь);
	ТипыДокументов.Вставить(Тип("ДокументСсылка.ПринятиеКУчетуНМАМеждународныйУчет"),      Ложь);
	ТипыДокументов.Вставить(Тип("ДокументСсылка.АмортизацияОСМеждународныйУчет"),          Истина);
	ТипыДокументов.Вставить(Тип("ДокументСсылка.АмортизацияНМАМеждународныйУчет"),         Истина);
	ТипыДокументов.Вставить(Тип("ДокументСсылка.ИзменениеПараметровНМАМеждународныйУчет"), Истина);
	ТипыДокументов.Вставить(Тип("ДокументСсылка.ИзменениеПараметровОСМеждународныйУчет"),  Истина);
	ТипыДокументов.Вставить(Тип("ДокументСсылка.ПеремещениеНМАМеждународныйУчет"),         Истина);
	ТипыДокументов.Вставить(Тип("ДокументСсылка.ПеремещениеОСМеждународныйУчет"),          Истина);
	ТипыДокументов.Вставить(Тип("ДокументСсылка.СписаниеНМАМеждународныйУчет"),            Истина);
	ТипыДокументов.Вставить(Тип("ДокументСсылка.СписаниеОСМеждународныйУчет"),             Истина);
	ТипыДокументов.Вставить(Тип("ДокументСсылка.ПереводОсновныхСредствИнвестиционногоИмуществаМеждународныйУчет"), Истина);
	//-- Локализация
	
	Возврат ТипыДокументов;
	
КонецФункции

#КонецОбласти

//++ Локализация

#Область СлужебныеПроцедурыИФункции

Процедура ОтразитьДанныеДокумента(ТипДокумента, ПараметрыОтражения, ТаблицаПроводок)
	
	МенеджерДокумента = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Новый(ТипДокумента));
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ПараметрыОтражения.МенеджерВременныхТаблиц;
	Запрос.Текст = МенеджерДокумента.ТекстОтраженияВМеждународномУчете();
	ВалютаПредставления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Справочники.ПланыСчетовМеждународногоУчета.Международный, "ВалютаПредставления");
	Запрос.УстановитьПараметр("ВалютаПредставления", ВалютаПредставления);
	Запрос.УстановитьПараметр("ВалютаУправленческогоУчета", ЗначениеНастроекПовтИсп.ВалютаУправленческогоУчета());
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Проводка = ОбщегоНазначения.СкопироватьРекурсивно(ПараметрыОтражения.СтруктураПроводки);
		ЗаполнитьЗначенияСвойств(Проводка, Выборка);
		
		ЗаполнитьЗначенияСвойств(Проводка, ВидыСубконтоСчета(Проводка.СчетДт, "Дт"));
		ЗаполнитьЗначенияСвойств(Проводка, ВидыСубконтоСчета(Проводка.СчетКт, "Кт"));
		
		МенеджерДокумента.ПроверитьЗаполнениеПроводкиМеждународногоУчета(Проводка, Выборка);
		
		МеждународныйУчетПроведениеСервер.ДобавитьПроводкуВТаблицуПроводок(ТаблицаПроводок, Проводка);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ВидыСубконтоСчета(Счет, Суффикс="Дт")
	
	СтруктураВидовСубконто = Новый Структура;
	СтруктураВидовСубконто.Вставить("ВидСубконто"+Суффикс+"1");
	СтруктураВидовСубконто.Вставить("ВидСубконто"+Суффикс+"2");
	СтруктураВидовСубконто.Вставить("ВидСубконто"+Суффикс+"3");
	
	Если ЗначениеЗаполнено(Счет) Тогда
		
		ВидыСубконто = Счет.ВидыСубконто;
		КоличествоСубконто = ВидыСубконто.Количество();
		Для К = 1 По КоличествоСубконто Цикл
			СтруктураВидовСубконто.Вставить("ВидСубконто"+Суффикс+Формат(К, "ЧГ=0"), ВидыСубконто[К - 1].ВидСубконто);
		КонецЦикла;
		
	КонецЕсли;
	Возврат СтруктураВидовСубконто;
	
КонецФункции

#КонецОбласти

//-- Локализация