
#Область ПрограммныйИнтерфейс

// Функция возвращает представление группы затрат.
//	Параметры:
//		ЗначениеПолей - Структура - значения полей, идентифицирующих группу затрат.
//	Возвращаемое значение:
//		Строка - представление группы затрат.
//
Функция ПредставлениеГруппыЗатрат(ЗначениеПолей) Экспорт
	
	СписокПолей = ПереченьПолейПоГруппировкеЗатрат(ЗначениеПолей.Документ, ЗначениеПолей.ГруппировкаЗатрат);
	
	ПоляГруппыЗатрат = СтруктураГруппыЗатрат(СписокПолей);
	ЗаполнитьЗначенияСвойств(ПоляГруппыЗатрат, ЗначениеПолей);
	ПредставлениеГруппы = "";
	
	Для Каждого ТекПоле Из ПоляГруппыЗатрат Цикл
		
		//++ НЕ УТКА
		Если ТекПоле.Ключ = "ВнутренняяПереработка" Тогда
			Продолжить;
		КонецЕсли;
		//-- НЕ УТКА
		
		ТекПредставление = Строка(ТекПоле.Значение);
		
		Если ЗначениеЗаполнено(ТекПредставление) Тогда
			ПредставлениеГруппы = ПредставлениеГруппы + ТекПредставление + " / ";
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ПредставлениеГруппы) Тогда
		ПредставлениеГруппы = Лев(ПредставлениеГруппы, СтрДлина(ПредставлениеГруппы) - 3);
	Иначе
		ПредставлениеГруппы = НСтр("ru = '<группа с пустыми значениями>';
									|en = '<group with empty values>'", ОбщегоНазначения.КодОсновногоЯзыка());
	КонецЕсли;
	
	Возврат ПредставлениеГруппы;
	
КонецФункции

// Функция возвращает перечень идентифицирующих полей, соответствующих переданной группировке затрат.
//	Параметры:
//		Документ - ДокументСсылка.ОтчетПереработчика,
//					ДокументСсылка.ПроизводствоБезЗаказа,
//					ДокументСсылка.ЗаказНаПроизводство2_2,
//					ДокументСсылка.ОтчетПереработчика2_5 - документ партии производства.
//		ГруппировкаЗатрат - ПеречислениеСсылка.ГруппировкиЗатратВЗаказеПереработчику,
//							ПеречислениеСсылка.ГруппировкиЗатратВПроизводствеБезЗаказа - тип группировки затрат.
//	Возвращаемое значение:
//		СписокЗначений - перечень полей, идентифицирующих группу затрат.
//
Функция ПереченьПолейПоГруппировкеЗатрат(Документ, ГруппировкаЗатрат) Экспорт
	
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.ПроизводствоБезЗаказа") Тогда
		СписокПолей = Документы.ПроизводствоБезЗаказа.ПереченьПолейГруппыЗатрат(ГруппировкаЗатрат);
	//++ Устарело_Переработка24
	ИначеЕсли ТипЗнч(Документ) = Тип("ДокументСсылка.ОтчетПереработчика") Тогда
		СписокПолей = Документы.ОтчетПереработчика.ПереченьПолейГруппыЗатрат(ГруппировкаЗатрат);
	//-- Устарело_Переработка24
	ИначеЕсли ТипЗнч(Документ) = Тип("ДокументСсылка.ОтчетПереработчика2_5") Тогда
		СписокПолей = ПереработкаНаСторонеКлиентСервер.ПереченьПолейГруппыЗатратОтчетаПереработчика(ГруппировкаЗатрат);
	//++ НЕ УТКА
	ИначеЕсли ТипЗнч(Документ) = Тип("ДокументСсылка.ЗаказНаПроизводство2_2") Тогда
		СписокПолей = Документы.ЗаказНаПроизводство2_2.ПереченьПолейГруппыЗатрат();
	//-- НЕ УТКА
	Иначе
		СписокПолей = Новый СписокЗначений;
	КонецЕсли;
	
	Возврат СписокПолей;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает структуру, содержащую перечень идентифицирующих группу затрат полей.
//	Параметры:
//		СписокПолей - СписокЗначений - перечень полей, идентифицирующих группу затрат.
//	Возвращаемое значение:
//		Структура - структура, содержащая перечень идентифицирующих группу затрат полей.
//
Функция СтруктураГруппыЗатрат(СписокПолей) Экспорт
	
	СтруктураГруппыЗатрат = Новый Структура;
	
	Для Каждого ТекПоле Из СписокПолей Цикл
		СтруктураГруппыЗатрат.Вставить(ТекПоле.Значение);
	КонецЦикла;
	
	Возврат СтруктураГруппыЗатрат;
	
КонецФункции

#КонецОбласти