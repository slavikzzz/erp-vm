
#Область СлужебныеПроцедурыИФункции

Функция РасширенноеОписаниеВидовДоходаСоСкидками() Экспорт 

	РасширенноеОписание	= Новый ТаблицаЗначений;
	РасширенноеОписание.Колонки.Добавить("Ссылка", Новый ОписаниеТипов("СправочникСсылка.ВидыДоходовПоСтраховымВзносам"));
	РасширенноеОписание.Колонки.Добавить("КодДоходаПоНДФЛ", Новый ОписаниеТипов("СправочникСсылка.ВидыДоходовНДФЛ"));
	РасширенноеОписание.Колонки.Добавить("КодВычетаПоНДФЛ", Новый ОписаниеТипов("СправочникСсылка.ВидыВычетовНДФЛ"));
	
	ДоходыВзносов = Справочники.ВидыДоходовПоСтраховымВзносам;
	ДоходыНДФЛ = Справочники.ВидыДоходовНДФЛ;
	ВычетПоМатпомощиНаДетей = УчетНДФЛ.ВычетПоМатпомощиНаДетей();
	ДобавитьСтрокуОписания(РасширенноеОписание, ДоходыВзносов.МатпомощьПриРожденииРебенкаПрокуроров,						ДоходыНДФЛ.Код2762, ВычетПоМатпомощиНаДетей);
	ДобавитьСтрокуОписания(РасширенноеОписание, ДоходыВзносов.МатпомощьПрокуроров,											ДоходыНДФЛ.Код2760);
	ДобавитьСтрокуОписания(РасширенноеОписание, ДоходыВзносов.МатпомощьПриРожденииРебенкаСтудентовИзСтудотряда,				ДоходыНДФЛ.Код2762,	ВычетПоМатпомощиНаДетей);
	ДобавитьСтрокуОписания(РасширенноеОписание, ДоходыВзносов.МатпомощьСтудентовИзСтудотряда,								ДоходыНДФЛ.Код2760);
	ДобавитьСтрокуОписания(РасширенноеОписание, ДоходыВзносов.МатпомощьПриРожденииРебенка,									ДоходыНДФЛ.Код2762,	ВычетПоМатпомощиНаДетей);
	ДобавитьСтрокуОписания(РасширенноеОписание, ДоходыВзносов.Матпомощь,													ДоходыНДФЛ.Код2760);
	ДобавитьСтрокуОписания(РасширенноеОписание, ДоходыВзносов.АвторскиеИсполнениеПроизведений,								ДоходыНДФЛ.Код2207);
	ДобавитьСтрокуОписания(РасширенноеОписание, ДоходыВзносов.АвторскиеИсполнениеПроизведенийОблагаемыеФСС_НС,				ДоходыНДФЛ.Код2207);
	ДобавитьСтрокуОписания(РасширенноеОписание, ДоходыВзносов.АвторскиеОткрытия,											ДоходыНДФЛ.Код2209);
	ДобавитьСтрокуОписания(РасширенноеОписание, ДоходыВзносов.АвторскиеОткрытияОблагаемыеФСС_НС,							ДоходыНДФЛ.Код2209);
	ДобавитьСтрокуОписания(РасширенноеОписание, ДоходыВзносов.АвторскиеАудиовизуальныеПроизведения,							ДоходыНДФЛ.Код2204);
	ДобавитьСтрокуОписания(РасширенноеОписание, ДоходыВзносов.АвторскиеАудиовизуальныеПроизведенияОблагаемыеФСС_НС,			ДоходыНДФЛ.Код2204);
	ДобавитьСтрокуОписания(РасширенноеОписание, ДоходыВзносов.АвторскиеДругиеМузыкальныеПроизведения,						ДоходыНДФЛ.Код2206);
	ДобавитьСтрокуОписания(РасширенноеОписание, ДоходыВзносов.АвторскиеДругиеМузыкальныеПроизведенияОблагаемыеФСС_НС,		ДоходыНДФЛ.Код2206);
	ДобавитьСтрокуОписания(РасширенноеОписание, ДоходыВзносов.АвторскиеЛитературныеПроизведения,							ДоходыНДФЛ.Код2201);
	ДобавитьСтрокуОписания(РасширенноеОписание, ДоходыВзносов.АвторскиеЛитературныеПроизведенияОблагаемыеФСС_НС,			ДоходыНДФЛ.Код2201);
	ДобавитьСтрокуОписания(РасширенноеОписание, ДоходыВзносов.АвторскиеМузыкальноСценическиеПроизведение,					ДоходыНДФЛ.Код2205);
	ДобавитьСтрокуОписания(РасширенноеОписание, ДоходыВзносов.АвторскиеМузыкальноСценическиеПроизведениеОблагаемыеФСС_НС,	ДоходыНДФЛ.Код2205);
	ДобавитьСтрокуОписания(РасширенноеОписание, ДоходыВзносов.АвторскиеНаучныеТруды,										ДоходыНДФЛ.Код2208);
	ДобавитьСтрокуОписания(РасширенноеОписание, ДоходыВзносов.АвторскиеНаучныеТрудыОблагаемыеФСС_НС,						ДоходыНДФЛ.Код2208);
	ДобавитьСтрокуОписания(РасширенноеОписание, ДоходыВзносов.АвторскиеСкульптуры,											ДоходыНДФЛ.Код2203);
	ДобавитьСтрокуОписания(РасширенноеОписание, ДоходыВзносов.АвторскиеСкульптурыОблагаемыеФСС_НС,							ДоходыНДФЛ.Код2203);
	ДобавитьСтрокуОписания(РасширенноеОписание, ДоходыВзносов.АвторскиеГрафическиеПроизведения,								ДоходыНДФЛ.Код2202);
	ДобавитьСтрокуОписания(РасширенноеОписание, ДоходыВзносов.АвторскиеГрафическиеПроизведенияОблагаемыеФСС_НС,				ДоходыНДФЛ.Код2202);
	
	Возврат РасширенноеОписание;
	
КонецФункции 

#Область УчетОтчитывающихсяПоВзносамОрганизаций

// Возвращает Истина, если организация сама зарегистрирована в ПФ и ФСС и представляет соответствующую отчетность.
//
// Параметры:
//		Организация - СправочникСсылка.Организации.
//
// Возвращаемое значение:
//	Булево.
// 
Функция ОрганизацияОтчитываетсяПоВзносамСамостоятельно(Организация, ДатаАктуальности) Экспорт
	
	Если ДатаАктуальности < '20210101' Тогда
		Возврат Истина
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ВестиУчетОтчитывающихсяПоВзносамОрганизаций") Тогда
		Возврат Истина
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОтчитывающиесяПоВзносамОрганизации.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.ОтчитывающиесяПоВзносамОрганизации КАК ОтчитывающиесяПоВзносамОрганизации
	|ГДЕ
	|	ОтчитывающиесяПоВзносамОрганизации.Организация = &Организация
	|	И ОтчитывающиесяПоВзносамОрганизации.ОтчитывающаясяОрганизация <> &Организация";
	Запрос.УстановитьПараметр("Организация", Организация);
	Возврат Запрос.Выполнить().Пустой()

КонецФункции

// Возвращает организацию, отчитывающуюся за переданную, для «самостоятельной» организации возвращается переданная.
//
// Параметры:
//		Организация - СправочникСсылка.Организации.
//
// Возвращаемое значение:
//	СправочникСсылка.Организации.
// 
Функция ОтчитывающаясяПоВзносамОрганизация(Организация, ДатаАктуальности) Экспорт

	Если ДатаАктуальности < '20210101' Тогда
		Возврат Организация
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ВестиУчетОтчитывающихсяПоВзносамОрганизаций") Тогда
		Возврат Организация
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОтчитывающиесяПоВзносамОрганизации.ОтчитывающаясяОрганизация КАК ОтчитывающаясяОрганизация
	|ИЗ
	|	РегистрСведений.ОтчитывающиесяПоВзносамОрганизации КАК ОтчитывающиесяПоВзносамОрганизации
	|ГДЕ
	|	ОтчитывающиесяПоВзносамОрганизации.Организация = &Организация";
	Запрос.УстановитьПараметр("Организация", Организация);
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Организация
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.ОтчитывающаясяОрганизация
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область ВспомогательныеПроцедурыИФункции

Процедура ДобавитьСтрокуОписания(РасширенноеОписание, Ссылка, КодДоходаПоНДФЛ, КодВычетаПоНДФЛ = Неопределено)

	НоваяСтрока = РасширенноеОписание.Добавить();	
	НоваяСтрока.Ссылка = Ссылка;
	НоваяСтрока.КодДоходаПоНДФЛ = КодДоходаПоНДФЛ;
	Если ЗначениеЗаполнено(КодВычетаПоНДФЛ) Тогда
		НоваяСтрока.КодВычетаПоНДФЛ = КодВычетаПоНДФЛ;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецОбласти


