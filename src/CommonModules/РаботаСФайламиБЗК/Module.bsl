#Область СлужебныйПрограммныйИнтерфейс

Процедура ДобавитьФайлВСписок(СписокФайлов, ПрисоединенныйФайл, Знач Имя, Знач Расширение, Знач Размер, Знач ПодписанЭП) Экспорт
	
	Если ЗначениеЗаполнено(Расширение) Тогда
		
		ИмяФайла = СтрШаблон("%1.%2 (%3)",
			Имя,
			Расширение,
			ПредставлениеРазмераФайла(Размер));
		
	Иначе
		
		ИмяФайла = СтрШаблон("%1 (%3)",
			Имя,
			ПредставлениеРазмераФайла(Размер));
		
	КонецЕсли;
	
	РасширениеВРег = ВРег(СокрЛП(Расширение));
	Если РасширениеВРег = "PDF" Тогда
		Если ПодписанЭП Тогда
			Картинка = БиблиотекаКартинок.PDFПодписанныйЭЦП;
		Иначе
			Картинка = БиблиотекаКартинок.ФорматPDF;
		КонецЕсли;
	Иначе
		Картинка = Новый Картинка;
	КонецЕсли;
	
	СписокФайлов.Добавить(ПрисоединенныйФайл, ИмяФайла, , Картинка);
	
КонецПроцедуры

Функция УникальноеИмяФайла(ИмяФайла) Экспорт
	
	Файл = Новый Файл(ИмяФайла);
	ИмяБезРасширения = Файл.ИмяБезРасширения;
	Расширение = Файл.Расширение;
	ИмяКаталога = Файл.Путь;
	
	Счетчик = 1;
	Пока Файл.Существует() Цикл
		Счетчик = Счетчик + 1;
		Файл = Новый Файл(ИмяКаталога + ИмяБезРасширения + " (" + Счетчик + ")" + Расширение);
	КонецЦикла;
	
	Возврат Файл.ПолноеИмя;
	
КонецФункции

Функция ВременныйКаталог() Экспорт
	
	ИмяВременногоКаталога = ПолучитьИмяВременногоФайла();
	СоздатьКаталог(ИмяВременногоКаталога);
	
	Возврат ИмяВременногоКаталога;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПредставлениеРазмераФайла(Знач Размер)
	
	Если Не ЗначениеЗаполнено(Размер) Тогда
		Размер = "???";
		ЕдиницаИзмерения = "";
	ИначеЕсли Размер < 512 Тогда
		ЕдиницаИзмерения = НСтр("ru = 'байт';
								|en = 'byte'");
	ИначеЕсли Размер < 1024 * 512 Тогда
		Размер = Окр(Размер / 1024, 1);
		ЕдиницаИзмерения = НСтр("ru = 'Кб';
								|en = 'KB'");
	ИначеЕсли Размер < 1024 * 1024 * 512 Тогда
		Размер = Окр(Размер / 1024 / 1024, 1);
		ЕдиницаИзмерения = НСтр("ru = 'Мб';
								|en = 'MB'");
	Иначе
		Размер = Окр(Размер / 1024 / 1024 / 1024, 1);
		ЕдиницаИзмерения = НСтр("ru = 'Гб';
								|en = 'GB'");
	КонецЕсли;
	
	Возврат СокрЛП(СтрШаблон("%1 %2", Размер, ЕдиницаИзмерения));
	
КонецФункции

#КонецОбласти
