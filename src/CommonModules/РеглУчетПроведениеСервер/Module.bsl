#Область ПрограммныйИнтерфейс

#Область Проведение

// Формирует параметры для проведения документа по регистрам учетного механизма через общий механизм проведения.
//
// Параметры:
//  Документ - ДокументОбъект - записываемый документ
//  Свойства - См. ПроведениеДокументов.СвойстваДокумента
//
// Возвращаемое значение:
//  Структура - См. ПроведениеДокументов.ПараметрыУчетногоМеханизма
//
Функция ПараметрыДляПроведенияДокумента(Документ, Свойства) Экспорт
	
	Параметры = ПроведениеДокументов.ПараметрыУчетногоМеханизма();
	
	// Проведение
	Если Свойства.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		Если Не ПолучитьФункциональнуюОпцию("ИспользоватьРеглУчет")
			Или Документ.ДополнительныеСвойства.Свойство("НеРегистрироватьКОтражениюВРеглУчете")
				И Документ.ДополнительныеСвойства.НеРегистрироватьКОтражениюВРеглУчете Тогда
			Возврат Параметры;
		КонецЕсли;
		
		Параметры.ПодчиненныеРегистры.Добавить(Метаданные.РегистрыСведений.ОтражениеДокументовВРеглУчете);

		Параметры.ПодчиненныеРегистры.Добавить(Метаданные.РегистрыНакопления.РасчетыПоЕдиномуНалоговомуСчету);
		Параметры.ПодчиненныеРегистры.Добавить(Метаданные.РегистрыНакопления.РасчетыПоНалогамНаЕдиномНалоговомСчете);
		Параметры.ПодчиненныеРегистры.Добавить(Метаданные.РегистрыНакопления.РасчетыПоСанкциямНаЕдиномНалоговомСчете);
		
		Документ.Движения.ОтражениеДокументовВРеглУчете.ДополнительныеСвойства.Вставить("ДатаРегистратора", Документ.Дата);
		
		// Оставлена для совместимости со старым подходом настройки отражения прочих операций.
		Параметры.НезависимыеРегистры.Добавить(Метаданные.РегистрыСведений.ПорядокОтраженияПрочихОпераций);
		
	КонецЕсли;
	
	// Контроль
	Если Свойства.РежимЗаписи <> РежимЗаписиДокумента.Запись Тогда
		
		Параметры.КонтрольныеРегистрыЗаданий.Добавить(Метаданные.РегистрыНакопления.РасчетыПоЕдиномуНалоговомуСчету);
		Параметры.КонтрольныеРегистрыЗаданий.Добавить(Метаданные.РегистрыНакопления.РасчетыПоНалогамНаЕдиномНалоговомСчете);
		Параметры.КонтрольныеРегистрыЗаданий.Добавить(Метаданные.РегистрыНакопления.РасчетыПоСанкциямНаЕдиномНалоговомСчете);

	КонецЕсли;
	
	Возврат Параметры;
	
КонецФункции

// Возвращает тексты запросов для сторнирования движений при исправлении документов
// 
// Параметры:
// 	МетаданныеДокумента - ОбъектМетаданныхДокумент - Метаданные документа, который проводится.
// 
// Возвращаемое значение:
// 	Соответствие - Соответствие полного имени регистра тексту запроса сторнирования
//
Функция ТекстыЗапросовСторнирования(МетаданныеДокумента) Экспорт
	
	ДвиженияДокумента = МетаданныеДокумента.Движения;

	ТекстыЗапросов = Новый Соответствие();
	
	МетаданныеРегистра = Метаданные.РегистрыСведений.ОтражениеДокументовВРеглУчете;
	Если ДвиженияДокумента.Содержит(МетаданныеРегистра) Тогда
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДокументыСторно.Ссылка КАК Регистратор,
		|	ДокументыСторно.Дата КАК Период,
		|	ОтражениеДокументовВРеглУчете.Организация КАК Организация,
		|	НАЧАЛОПЕРИОДА(ДокументыСторно.Дата, ДЕНЬ) КАК ДатаОтражения
		|ИЗ
		|	РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументовВРеглУчете
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		&ИмяДокумента КАК ДокументыСторно
		|	ПО
		|		ОтражениеДокументовВРеглУчете.Регистратор = ДокументыСторно.СторнируемыйДокумент
		|		И ДокументыСторно.Ссылка В (&Ссылка)
		|";
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяДокумента", МетаданныеДокумента.ПолноеИмя());
		
		ТекстыЗапросов.Вставить(МетаданныеРегистра.ПолноеИмя(), ТекстЗапроса);
	КонецЕсли;
	
	Возврат ТекстыЗапросов;
	
КонецФункции

// Дополняет текст запроса механизма проверки даты запрета по таблице изменений.
// 
// Параметры:
// 	Запрос - Запрос - используется для установки параметров запроса.
// 
// Возвращаемое значение:
//	Соответствие - соответствие имен таблиц изменения регистров и текстов запросов.
//	
Функция ТекстыЗапросовКонтрольДатыЗапретаПоТаблицеИзменений(Запрос) Экспорт

	СоответствиеТекстовЗапросов = Новый Соответствие();
	Возврат СоответствиеТекстовЗапросов;
	
КонецФункции

// Возвращает текст запроса для определения организаций, изменяемых документом во взаиморасчетах на основании платежей
// 
// Параметры:
// 	ПутьКТаблицеРасшифровкиПлатежа - Строка - Путь к таблице, содержащей объекты расчетов документа,
// 			например: "Документ.РеализацияТоваровУслуг.РасшифровкаПлатежа".
// 
// Возвращаемое значение:
// 	Строка - текст запроса
//
Функция ТекстЗапросаОтраженияПоОбъектамРасчетов(ПутьКТаблицеРасшифровкиПлатежа) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Период КАК Период,
	|	ОбъектыРасчетов.Организация КАК Организация,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ) КАК ДатаОтражения
	|ИЗ
	|	&РасшифровкаПлатежа КАК РасшифровкаПлатежа
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|		ПО РасшифровкаПлатежа.ОбъектРасчетов = ОбъектыРасчетов.Ссылка
	|ГДЕ
	|	РасшифровкаПлатежа.Ссылка В (&Ссылка)
	|	И ОбъектыРасчетов.Организация <> &Организация
	|";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&РасшифровкаПлатежа", ПутьКТаблицеРасшифровкиПлатежа);
	Если Не СтрНачинаетсяС(ПутьКТаблицеРасшифровкиПлатежа, "Документ.") Тогда
		// Передана временная таблица, она уже с отбором по документу:
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "РасшифровкаПлатежа.Ссылка В (&Ссылка)", "ИСТИНА");
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса для определения организаций, изменяемых документом во взаиморасчетах на основании договора
// 
// Параметры:
// 	ИмяПараметраДоговор - Строка - Имя параметра проведения документа, содержащего договор.
// 
// Возвращаемое значение:
// 	Строка - текст запроса
//
Функция ТекстЗапросаОтраженияПоДоговору(ИмяПараметраДоговор) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Период КАК Период,
	|	Договоры.Организация КАК Организация,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ) КАК ДатаОтражения
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК Договоры
	|ГДЕ
	|	Договоры.Ссылка В (&Договор)
	|";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Договор", ИмяПараметраДоговор);
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса для определения организаций, изменяемых документом во взаиморасчетах на основании заказа
// 
// Параметры:
// 	ПутьКТаблицеДокумента - Строка - Путь к таблице, содержащей данные документа,
// 			например: "Документ.РеализацияТоваровУслуг";
// 	ИмяРеквизитаЗаказа - Строка - Имя реквизита, по которому можно обратиться к заказу документа.
// 
// Возвращаемое значение:
// 	Строка - текст запроса
//
Функция ТекстЗапросаОтраженияПоЗаказу(ПутьКТаблицеДокумента, ИмяРеквизитаЗаказа) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Период КАК Период,
	|	ДанныеДокумента.Заказ.Организация КАК Организация,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ) КАК ДатаОтражения
	|ИЗ
	|	&ИмяДокумента КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка В (&Ссылка)
	|	И ЕСТЬNULL(ДанныеДокумента.Заказ.Организация, НЕОПРЕДЕЛЕНО) <> НЕОПРЕДЕЛЕНО
	|";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяДокумента", ПутьКТаблицеДокумента);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Заказ", ИмяРеквизитаЗаказа);
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Процедура формирования движений по подчиненным регистрам регламентированного учета.
//
// Параметры:
//   ТаблицыДляДвижений - Структура - таблицы данных документа
//   Движения - КоллекцияДвижений - коллекция наборов записей движений документа
//   Отказ - Булево - признак отказа от проведения документа.
//
Процедура ОтразитьДвижения(ТаблицыДляДвижений, Движения, Отказ) Экспорт
	
	Если Отказ Или Не ПолучитьФункциональнуюОпцию("ИспользоватьРеглУчет") Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ОтразитьДвижения(ТаблицыДляДвижений, Движения , Метаданные.РегистрыНакопления.РасчетыПоЕдиномуНалоговомуСчету.Имя);
	ПроведениеДокументов.ОтразитьДвижения(ТаблицыДляДвижений, Движения , Метаданные.РегистрыНакопления.РасчетыПоНалогамНаЕдиномНалоговомСчете.Имя);
	ПроведениеДокументов.ОтразитьДвижения(ТаблицыДляДвижений, Движения , Метаданные.РегистрыНакопления.РасчетыПоСанкциямНаЕдиномНалоговомСчете.Имя);
	
	Если ТаблицыДляДвижений.Свойство("ТаблицаОперацииСПрослеживаемымиТоварами") И Движения.Найти("ОперацииСПрослеживаемымиТоварами") <> Неопределено
		И Движения.Найти("ТоварыОрганизаций") <> Неопределено И Движения.ТоварыОрганизаций.Количество() = 0 Тогда
		
		ДвиженияОперацииСПрослеживаемымиТоварами = Движения.ОперацииСПрослеживаемымиТоварами;
		ДвиженияОперацииСПрослеживаемымиТоварами.Записывать = Истина;
		ДвиженияОперацииСПрослеживаемымиТоварами.Загрузить(ТаблицыДляДвижений.ТаблицаОперацииСПрослеживаемымиТоварами);
	КонецЕсли;
	
	Если Не ТаблицыДляДвижений.Свойство("Таблица" + "ОтражениеДокументовВРеглУчете") Тогда
		Возврат;
	КонецЕсли;
	
	Регистратор = Движения.ОтражениеДокументовВРеглУчете.Отбор.Регистратор.Значение;
	ДатаРегистратора = ?(Движения.ОтражениеДокументовВРеглУчете.ДополнительныеСвойства.Свойство("ДатаРегистратора"),
		Движения.ОтражениеДокументовВРеглУчете.ДополнительныеСвойства.ДатаРегистратора,
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Регистратор, "Дата"));
	
	Если ТипЗнч(Движения) = Тип("Структура") И Не Движения.Свойство("Хозрасчетный") Тогда
		Хозрасчетный = РегистрыБухгалтерии.Хозрасчетный.СоздатьНаборЗаписей();
		Хозрасчетный.Отбор.Регистратор.Установить(Регистратор);
		Движения.Вставить("Хозрасчетный", Хозрасчетный);
	КонецЕсли;
	
	ДвиженияХозрасчетный = Движения.Хозрасчетный;
	УстановитьДопСвойстваНабораЗаписейХозрасчетный(ДвиженияХозрасчетный, Регистратор);
	ДвиженияХозрасчетный.Записывать = Истина;
	
	ДвиженияОтражениеДокументовВРеглУчете = Движения.ОтражениеДокументовВРеглУчете;
	ДвиженияОтражениеДокументовВРеглУчете.Записывать = Истина;
	
	ТаблицаДанные = Новый ТаблицаЗначений;
	ТаблицаДанные.Колонки.Добавить("Период",         Новый ОписаниеТипов("Дата"));
	ТаблицаДанные.Колонки.Добавить("Организация",    Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаДанные.Колонки.Добавить("ДатаОтражения",  Новый ОписаниеТипов("Дата"));
	ТаблицаДанные.Колонки.Добавить("Статус",         Новый ОписаниеТипов("ПеречислениеСсылка.СтатусыОтраженияДокументовВРеглУчете"));
	
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицыДляДвижений["Таблица" + "ОтражениеДокументовВРеглУчете"], ТаблицаДанные);
	
	СтрокиКУдалению = Новый Массив;
	ТаблицаДанные.Свернуть("Период, Организация, ДатаОтражения", "Статус");
	Для каждого Строка Из ТаблицаДанные Цикл
		Если Не ЗначениеЗаполнено(Строка.ДатаОтражения) Тогда
			Строка.ДатаОтражения = НачалоДня(Строка.Период);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Строка.Статус) Тогда
			Строка.Статус = Перечисления.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВРеглУчете;
		КонецЕсли;
		Если Строка.Организация = Справочники.Организации.УправленческаяОрганизация
			ИЛИ Не ДокументОтражаетсяВРеглУчете(Регистратор, Строка.ДатаОтражения) Тогда
			СтрокиКУдалению.Добавить(Строка);
		КонецЕсли;
	КонецЦикла;
	
	Для каждого Строка Из СтрокиКУдалению Цикл
		ТаблицаДанные.Удалить(Строка);
	КонецЦикла;
	
	ВыборочнаяРегистрацияКОтражению = ТаблицыДляДвижений.Свойство("ТаблицаВыборочнойРегистрацииКОтражению");
	ИспользуетсяРучнаяКорректировкаПроводок = ПолучитьФункциональнуюОпцию("ИспользоватьРучнуюКорректировкуПроводокПоРеглУчету");
	
	Если НЕ ИспользуетсяРучнаяКорректировкаПроводок И НЕ ВыборочнаяРегистрацияКОтражению Тогда
		ДвиженияОтражениеДокументовВРеглУчете.Загрузить(ТаблицаДанные);
		Возврат;
	КонецЕсли;
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка",                  Регистратор);
	Запрос.УстановитьПараметр("Период",                  ДатаРегистратора);
	Запрос.УстановитьПараметр("ТаблицаДанные",           ТаблицаДанные);
	Запрос.УстановитьПараметр("ОтраженоВРеглУчете",      Перечисления.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВРеглУчете);
	Запрос.УстановитьПараметр("КОтражениюВРеглУчете",    Перечисления.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВРеглУчете);
	Запрос.УстановитьПараметр("ОтраженоВУчетеВручную",   Перечисления.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВУчетеВручную);
	Запрос.УстановитьПараметр("КОтражениюВУчетеВручную", Перечисления.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВУчетеВручную);
	Запрос.УстановитьПараметр("ПустойВидСубконто",       ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОтражениеДокументов.Организация КАК Организация,
	|	ОтражениеДокументов.ДатаОтражения КАК ДатаОтражения,
	|	ОтражениеДокументов.Статус КАК Статус,
	|	ОтражениеДокументов.Комментарий КАК Комментарий
	|ПОМЕСТИТЬ ТекущиеСтатусы
	|ИЗ
	|	РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументов
	|ГДЕ
	|	ОтражениеДокументов.Регистратор = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ДатаОтражения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	1 КАК Поле1
	|ИЗ
	|	ТекущиеСтатусы КАК ТекущиеСтатусы
	|ГДЕ
	|	ТекущиеСтатусы.Статус В (&ОтраженоВУчетеВручную, &КОтражениюВУчетеВручную)";
	
	Результат = Запрос.Выполнить();
	
	РучнаяКорректировкаПроводок = Не Результат.Пустой();
		
	Если Не РучнаяКорректировкаПроводок И Не ВыборочнаяРегистрацияКОтражению Тогда
		ДвиженияОтражениеДокументовВРеглУчете.Загрузить(ТаблицаДанные);
		Возврат;
	ИначеЕсли РучнаяКорректировкаПроводок Тогда
		
		#Область ТекстЗапросаРучнаяКорректировка
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТаблицаДанные.Организация    КАК Организация,
		|	ТаблицаДанные.ДатаОтражения КАК ДатаОтражения
		|ПОМЕСТИТЬ ДанныеКОтражению
		|ИЗ
		|	&ТаблицаДанные КАК ТаблицаДанные
		|ИНДЕКСИРОВАТЬ ПО
		|	Организация,
		|	ДатаОтражения
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	&Период КАК Период,
		|	ТекущиеСтатусы.Организация КАК Организация,
		|	ТекущиеСтатусы.ДатаОтражения КАК ДатаОтражения,
		|	&КОтражениюВУчетеВручную КАК Статус,
		|	ТекущиеСтатусы.Комментарий КАК Комментарий
		|ИЗ
		|	ТекущиеСтатусы КАК ТекущиеСтатусы
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ 
		|		ДанныеКОтражению КАК ДанныеКОтражению
		|	ПО 
		|		ТекущиеСтатусы.Организация = ДанныеКОтражению.Организация
		|		И ТекущиеСтатусы.ДатаОтражения = ДанныеКОтражению.ДатаОтражения
		|	
		|ОБЪЕДИНИТЬ ВСЕ
		|	
		|ВЫБРАТЬ
		|	&Период КАК Период,
		|	ДанныеКОтражению.Организация КАК Организация,
		|	ДанныеКОтражению.ДатаОтражения КАК ДатаОтражения,
		|	&КОтражениюВУчетеВручную КАК Статус,
		|	"""" КАК Комментарий
		|ИЗ
		|	ДанныеКОтражению КАК ДанныеКОтражению
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ 
		|		ТекущиеСтатусы КАК ТекущиеСтатусы
		|	ПО 
		|		ТекущиеСтатусы.Организация = ДанныеКОтражению.Организация
		|		И ТекущиеСтатусы.ДатаОтражения = ДанныеКОтражению.ДатаОтражения
		|ГДЕ
		|	ТекущиеСтатусы.Организация ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Хозрасчетный.Период                                       КАК Период,
		|	Хозрасчетный.Организация                                  КАК Организация,
		|	
		|	Хозрасчетный.ПодразделениеДт                              КАК ПодразделениеДт,
		|	Хозрасчетный.НаправлениеДеятельностиДт                    КАК НаправлениеДеятельностиДт,
		|	Хозрасчетный.СчетДт                                       КАК СчетДт,
		|	ЕСТЬNULL(Хозрасчетный.ВидСубконтоДт1, &ПустойВидСубконто) КАК ВидСубконтоДт1,
		|	ЕСТЬNULL(Хозрасчетный.СубконтоДт1, НЕОПРЕДЕЛЕНО)          КАК СубконтоДт1,
		|	ЕСТЬNULL(Хозрасчетный.ВидСубконтоДт2, &ПустойВидСубконто) КАК ВидСубконтоДт2,
		|	ЕСТЬNULL(Хозрасчетный.СубконтоДт2, НЕОПРЕДЕЛЕНО)          КАК СубконтоДт2,
		|	ЕСТЬNULL(Хозрасчетный.ВидСубконтоДт3, &ПустойВидСубконто) КАК ВидСубконтоДт3,
		|	ЕСТЬNULL(Хозрасчетный.СубконтоДт3, НЕОПРЕДЕЛЕНО)          КАК СубконтоДт3,
		|	Хозрасчетный.ВалютаДт                                     КАК ВалютаДт,
		|	Хозрасчетный.ВалютнаяСуммаДт                              КАК ВалютнаяСуммаДт,
		|	Хозрасчетный.КоличествоДт                                 КАК КоличествоДт,
		|	
		|	Хозрасчетный.ПодразделениеКт                              КАК ПодразделениеКт,
		|	Хозрасчетный.НаправлениеДеятельностиКт                    КАК НаправлениеДеятельностиКт,
		|	Хозрасчетный.СчетКт                                       КАК СчетКт,
		|	ЕСТЬNULL(Хозрасчетный.ВидСубконтоКт1, &ПустойВидСубконто) КАК ВидСубконтоКт1,
		|	ЕСТЬNULL(Хозрасчетный.СубконтоКт1, НЕОПРЕДЕЛЕНО)          КАК СубконтоКт1,
		|	ЕСТЬNULL(Хозрасчетный.ВидСубконтоКт2, &ПустойВидСубконто) КАК ВидСубконтоКт2,
		|	ЕСТЬNULL(Хозрасчетный.СубконтоКт2, НЕОПРЕДЕЛЕНО)          КАК СубконтоКт2,
		|	ЕСТЬNULL(Хозрасчетный.ВидСубконтоКт3, &ПустойВидСубконто) КАК ВидСубконтоКт3,
		|	ЕСТЬNULL(Хозрасчетный.СубконтоКт3, НЕОПРЕДЕЛЕНО)          КАК СубконтоКт3,
		|	Хозрасчетный.ВалютаКт                                     КАК ВалютаКт,
		|	Хозрасчетный.ВалютнаяСуммаКт                              КАК ВалютнаяСуммаКт,
		|	Хозрасчетный.КоличествоКт                                 КАК КоличествоКт,
		|	
		|	Хозрасчетный.Сумма                                        КАК Сумма,
		|	Хозрасчетный.СуммаУУ                                      КАК СуммаУУ,
		|	Хозрасчетный.СуммаФО                                      КАК СуммаФО,
		|	Хозрасчетный.СуммаНУДт                                    КАК СуммаНУДт,
		|	Хозрасчетный.СуммаНУКт                                    КАК СуммаНУКт,
		|	Хозрасчетный.СуммаПРДт                                    КАК СуммаПРДт,
		|	Хозрасчетный.СуммаПРКт                                    КАК СуммаПРКт,
		|	Хозрасчетный.СуммаВРДт                                    КАК СуммаВРДт,
		|	Хозрасчетный.СуммаВРКт                                    КАК СуммаВРКт,
		|	
		|	Хозрасчетный.Содержание                                   КАК Содержание
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(,,Регистратор = &Ссылка,,) КАК Хозрасчетный
		|";
		#КонецОбласти
		
		Запрос.УстановитьПараметр("ТаблицаДанные", ТаблицаДанные);
		Результат = Запрос.ВыполнитьПакет();
		
	Иначе //выборочная регистрация
		
		#Область ТекстЗапросаВыборочнаяРегистрация
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТаблицаДанных.Период         КАК Период,
		|	ТаблицаДанных.Организация    КАК Организация,
		|	ТаблицаДанных.ДатаОтражения  КАК ДатаОтражения
		|ПОМЕСТИТЬ НовыеДанные
		|ИЗ
		|	&ТаблицаДанные КАК ТаблицаДанных
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТаблицаВыборочнойРегистрации.Организация    КАК Организация,
		|	ТаблицаВыборочнойРегистрации.ДатаОтражения КАК ДатаОтражения
		|ПОМЕСТИТЬ ДанныеКОтражению
		|ИЗ
		|	&ТаблицаВыборочнойРегистрации КАК ТаблицаВыборочнойРегистрации
		|ИНДЕКСИРОВАТЬ ПО
		|	Организация,
		|	ДатаОтражения
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	&Период КАК Период,
		|	НовыеДанные.Организация КАК Организация,
		|	НовыеДанные.ДатаОтражения КАК ДатаОтражения,
		|	ВЫБОР
		|		КОГДА ДанныеКОтражению.ДатаОтражения ЕСТЬ NULL И НЕ ТекущиеСтатусы.Статус ЕСТЬ NULL
		|			ТОГДА ТекущиеСтатусы.Статус
		|		ИНАЧЕ &КОтражениюВРеглУчете
		|	КОНЕЦ КАК Статус,
		|	ВЫБОР
		|		КОГДА ДанныеКОтражению.ДатаОтражения ЕСТЬ NULL
		|			ТОГДА ТекущиеСтатусы.Комментарий
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК Комментарий
		|ПОМЕСТИТЬ НовыеСтатусы
		|ИЗ
		|	НовыеДанные КАК НовыеДанные
		|	ЛЕВОЕ СОЕДИНЕНИЕ 
		|		ТекущиеСтатусы КАК ТекущиеСтатусы
		|	ПО 
		|		НовыеДанные.Организация = ТекущиеСтатусы.Организация
		|		И НовыеДанные.ДатаОтражения = ТекущиеСтатусы.ДатаОтражения
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ 
		|		ДанныеКОтражению КАК ДанныеКОтражению
		|	ПО 
		|		НовыеДанные.Организация = ДанныеКОтражению.Организация	
		|		И НовыеДанные.ДатаОтражения = ДанныеКОтражению.ДатаОтражения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Таблица.Организация КАК Организация,
		|	Таблица.ДатаОтражения КАК ДатаОтражения
		|ПОМЕСТИТЬ ИзмененияСтатусов
		|ИЗ
		|	(ВЫБРАТЬ
		|		Таблица.ДатаОтражения КАК ДатаОтражения,
		|		Таблица.Организация КАК Организация,
		|		ВЫБОР
		|			КОГДА Таблица.Статус = &КОтражениюВРеглУчете
		|				ТОГДА 1
		|			КОГДА Таблица.Статус = &ОтраженоВРеглУчете
		|				ТОГДА 2
		|		КОНЕЦ КАК Статус
		|	ИЗ
		|		НовыеСтатусы КАК Таблица
		|	ГДЕ
		|		Таблица.Статус В (&КОтражениюВРеглУчете, &ОтраженоВРеглУчете)
		|		
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Таблица.ДатаОтражения,
		|		Таблица.Организация,
		|		ВЫБОР
		|			КОГДА Таблица.Статус = &КОтражениюВРеглУчете
		|				ТОГДА -1
		|			КОГДА Таблица.Статус = &ОтраженоВРеглУчете
		|				ТОГДА -2
		|		КОНЕЦ
		|	ИЗ
		|		ТекущиеСтатусы КАК Таблица
		|	ГДЕ
		|		Таблица.Статус В (&КОтражениюВРеглУчете, &ОтраженоВРеглУчете)) КАК Таблица,
		|
		|	(ВЫБРАТЬ
		|		МИНИМУМ(ДанныеКОтражению.ДатаОтражения) КАК ДатаОтражения
		|	ИЗ
		|		ДанныеКОтражению КАК ДанныеКОтражению) КАК НачалоИзменений
		|ГДЕ
		|	Таблица.ДатаОтражения >= ЕСТЬNULL(НАЧАЛОПЕРИОДА(НачалоИзменений.ДатаОтражения, МЕСЯЦ), ДАТАВРЕМЯ(1,1,1))
		|
		|СГРУППИРОВАТЬ ПО
		|	Таблица.ДатаОтражения,
		|	Таблица.Организация
		|
		|ИМЕЮЩИЕ
		|	СУММА(Таблица.Статус) <> 0
		|;
		|
		|ВЫБРАТЬ
		|	НовыеСтатусы.Период          КАК Период,
		|	НовыеСтатусы.Организация     КАК Организация,
		|	НовыеСтатусы.ДатаОтражения  КАК ДатаОтражения,
		|	НовыеСтатусы.Статус          КАК Статус,
		|	НовыеСтатусы.Комментарий     КАК Комментарий
		|ИЗ
		|	НовыеСтатусы КАК НовыеСтатусы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Хозрасчетный.Период                                       КАК Период,
		|	Хозрасчетный.Организация                                  КАК Организация,
		|	
		|	Хозрасчетный.ПодразделениеДт                              КАК ПодразделениеДт,
		|	Хозрасчетный.НаправлениеДеятельностиДт                    КАК НаправлениеДеятельностиДт,
		|	Хозрасчетный.СчетДт                                       КАК СчетДт,
		|	ЕСТЬNULL(Хозрасчетный.ВидСубконтоДт1, &ПустойВидСубконто) КАК ВидСубконтоДт1,
		|	ЕСТЬNULL(Хозрасчетный.СубконтоДт1, НЕОПРЕДЕЛЕНО)          КАК СубконтоДт1,
		|	ЕСТЬNULL(Хозрасчетный.ВидСубконтоДт2, &ПустойВидСубконто) КАК ВидСубконтоДт2,
		|	ЕСТЬNULL(Хозрасчетный.СубконтоДт2, НЕОПРЕДЕЛЕНО)          КАК СубконтоДт2,
		|	ЕСТЬNULL(Хозрасчетный.ВидСубконтоДт3, &ПустойВидСубконто) КАК ВидСубконтоДт3,
		|	ЕСТЬNULL(Хозрасчетный.СубконтоДт3, НЕОПРЕДЕЛЕНО)          КАК СубконтоДт3,
		|	Хозрасчетный.ВалютаДт                                     КАК ВалютаДт,
		|	Хозрасчетный.ВалютнаяСуммаДт                              КАК ВалютнаяСуммаДт,
		|	Хозрасчетный.КоличествоДт                                 КАК КоличествоДт,
		|	
		|	Хозрасчетный.ПодразделениеКт                              КАК ПодразделениеКт,
		|	Хозрасчетный.НаправлениеДеятельностиКт                    КАК НаправлениеДеятельностиКт,
		|	Хозрасчетный.СчетКт                                       КАК СчетКт,
		|	ЕСТЬNULL(Хозрасчетный.ВидСубконтоКт1, &ПустойВидСубконто) КАК ВидСубконтоКт1,
		|	ЕСТЬNULL(Хозрасчетный.СубконтоКт1, НЕОПРЕДЕЛЕНО)          КАК СубконтоКт1,
		|	ЕСТЬNULL(Хозрасчетный.ВидСубконтоКт2, &ПустойВидСубконто) КАК ВидСубконтоКт2,
		|	ЕСТЬNULL(Хозрасчетный.СубконтоКт2, НЕОПРЕДЕЛЕНО)          КАК СубконтоКт2,
		|	ЕСТЬNULL(Хозрасчетный.ВидСубконтоКт3, &ПустойВидСубконто) КАК ВидСубконтоКт3,
		|	ЕСТЬNULL(Хозрасчетный.СубконтоКт3, НЕОПРЕДЕЛЕНО)          КАК СубконтоКт3,
		|	Хозрасчетный.ВалютаКт                                     КАК ВалютаКт,
		|	Хозрасчетный.ВалютнаяСуммаКт                              КАК ВалютнаяСуммаКт,
		|	Хозрасчетный.КоличествоКт                                 КАК КоличествоКт,
		|	
		|	Хозрасчетный.Сумма                                        КАК Сумма,
		|	Хозрасчетный.СуммаУУ                                      КАК СуммаУУ,
		|	Хозрасчетный.СуммаФО                                      КАК СуммаФО,
		|	Хозрасчетный.СуммаНУДт                                    КАК СуммаНУДт,
		|	Хозрасчетный.СуммаНУКт                                    КАК СуммаНУКт,
		|	Хозрасчетный.СуммаПРДт                                    КАК СуммаПРДт,
		|	Хозрасчетный.СуммаПРКт                                    КАК СуммаПРКт,
		|	Хозрасчетный.СуммаВРДт                                    КАК СуммаВРДт,
		|	Хозрасчетный.СуммаВРКт                                    КАК СуммаВРКт,
		|	
		|	Хозрасчетный.Содержание                                   КАК Содержание
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(,,Регистратор = &Ссылка,,) КАК Хозрасчетный
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		ИзмененияСтатусов КАК ИзмененияСтатусов
		|	ПО
		|		Хозрасчетный.Организация = ИзмененияСтатусов.Организация
		|		И НАЧАЛОПЕРИОДА(Хозрасчетный.Период, ДЕНЬ) = ИзмененияСтатусов.ДатаОтражения
		|ГДЕ
		|	ИзмененияСтатусов.Организация ЕСТЬ NULL";
		#КонецОбласти
	
		Запрос.УстановитьПараметр("ТаблицаВыборочнойРегистрации", ТаблицыДляДвижений.ТаблицаВыборочнойРегистрацииКОтражению);
		Результат = Запрос.ВыполнитьПакет();
		ИзмененияСтатусов = Запрос.МенеджерВременныхТаблиц.Таблицы.Получить("ИзмененияСтатусов").ПолучитьДанные().Выгрузить();
		
		ШаблонЗапретаДанных = ДатыЗапретаИзменения.ШаблонДанныхДляПроверки();
		Для каждого СтрокаОтражения Из ИзмененияСтатусов Цикл
			СтрокаШаблона = ШаблонЗапретаДанных.Добавить();
			СтрокаШаблона.Дата   = СтрокаОтражения.ДатаОтражения;
			СтрокаШаблона.Раздел = "БухгалтерскийУчет";
			СтрокаШаблона.Объект = СтрокаОтражения.Организация;
		КонецЦикла;
		
		ПроверитьДатуЗапретаДляОтраженияДокументовВРеглУчете(ШаблонЗапретаДанных, ДвиженияОтражениеДокументовВРеглУчете, Регистратор);
	
	КонецЕсли;
	
	Количество = Результат.Количество();
	
	Хозрасчетный  = Результат[Количество - 1].Выгрузить();
	ДвиженияХозрасчетный.Загрузить(Хозрасчетный);
	
	ТаблицаДанные = Результат[Количество - 2].Выгрузить();
	ДвиженияОтражениеДокументовВРеглУчете.Загрузить(ТаблицаДанные);
	
КонецПроцедуры

// Процедура формирования движений
//
// Параметры:
//	ТаблицыДляДвижений - Структура - таблицы данных документа
//	Документ - ДокументСсылка - ссылка на документ
//	Отказ - Булево - признак отказа от проведения документа.
//
Процедура ЗаписатьДанные(ТаблицыДляДвижений, Документ, МенеджерВременныхТаблиц, Отказ) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если ТаблицыДляДвижений.Свойство("ТаблицаПорядокОтраженияПрочихОпераций") Тогда
		
		НаборЗаписей = РегистрыСведений.ПорядокОтраженияПрочихОпераций.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Документ.Установить(Документ);
		
		Если ЗначениеЗаполнено(ТаблицыДляДвижений.ТаблицаПорядокОтраженияПрочихОпераций) Тогда
			
			Запрос = Новый Запрос(
				"ВЫБРАТЬ
				|	Операция.Документ,
				|	Операция.ИдентификаторСтроки,
				|	Операция.Организация,
				|	Операция.Дата
				|ПОМЕСТИТЬ
				|	Операция
				|ИЗ &Таблица КАК Операция;
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	Операция.Документ,
				|	Операция.ИдентификаторСтроки,
				|	Операция.Организация,
				|	Операция.Дата,
				|	Счета.СчетУчета КАК СчетУчета,
				|	Счета.Субконто1 КАК Субконто1,
				|	Счета.Субконто2 КАК Субконто2,
				|	Счета.Субконто3 КАК Субконто3
				|ИЗ
				|	Операция
				|	ПОЛНОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияПрочихОпераций КАК Счета
				|		ПО Счета.Документ = Операция.Документ И Счета.ИдентификаторСтроки = Операция.ИдентификаторСтроки
				|ГДЕ
				|	НЕ Операция.Документ ЕСТЬ NULL");
			
			Запрос.УстановитьПараметр("Таблица", ТаблицыДляДвижений.ТаблицаПорядокОтраженияПрочихОпераций);
			Запрос.УстановитьПараметр("Документ", Документ);
			
			НаборЗаписей.Загрузить(Запрос.Выполнить().Выгрузить());
			
		КонецЕсли;
		
		НаборЗаписей.Записать();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

// Регистрирует документ для проведения в регл учете общим порядком: датой документа и по организации документа.
//
Функция ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	ИмяРегистра = "ОтражениеДокументовВРеглУчете";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	&Период                        КАК Период,
		|	&Организация                   КАК Организация,
		|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ)   КАК ДатаОтражения";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает параметры отражения в регламентированном учете
// 
// Возвращаемое значение:
// 	Структура - Параметры отражения:
// 		* Документы - ДокументСсылка, Массив Из ДокументСсылка - Отбор по конкретным документам. Если Неопределено, то все.
// 		* Организации - СправочникСсылка.Организации, Массив Из СправочникСсылка.Организации - Отбор по организациям. Если Неопределено, то все.
// 		* ПериодРасчета - Дата - Отбор документов к отражению по дате. Если Неопределено, то все.
// 		* ДатаЗапрета - Дата - Дата запрета изменения данных. Если Неопределено, то разрешено изменять все.
// 		* НеотраженныеДокументы - Массив Из ДокументСсылка - Документы, которые не должны быть отражены и документы с ошибками отражения.
// 		* ПакетноеОтражение - Булево - Признак пакетного отражения
// 		* ВременныеТаблицыПереданы - Булево - Признак передачи временных таблиц из вызывающего метода
// 		* СообщатьОбОшибках - Булево - Признак необходимости сообщений об ошибках
// 		* ДетализацияПроводок - Булево - Признак детализации проводок
// 		* ПрогнозныеПараметры - см. ПрогнозныеПараметрыОтраженияВРеглУчете.
// 		* МинимальныйРазмерПорции - Число - Количество формируемых проводок, начиная с которого отражение выполняется в несколько потоков, если возможно.
// 		* КоличествоПотоков - Число - Количество сеансов отражения документов, выполняющихся одновременно.
// 		* КоличествоПопыток - Число - Количество попыток обработки одной порции документов.
// 		* ИспользованиеТекущихИтогов - Булево - Сохраняет состояние использования текущих итогов в регистрах бухгалтерии перед отражением в многопоточном режиме.
// 		* МаксимальныйПериодИтогов - Дата - Сохраняет максимальный хранимый период итогов в регистрах бухгалтерии перед отражением в многопоточном режиме.
// 		* МинимальныйПериодИтогов - Дата - Сохраняет минимальный хранимый период итогов в регистрах бухгалтерии перед отражением в многопоточном режиме.
// 		* ВосстановитьСостояниеИтогов - Булево - Сохраняет необходимость восстановления состояния итогов регистров бухгалтерии после отражения в многопоточном режиме.
// 
Функция ПараметрыОтраженияВРеглУчете() Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("Документы", Неопределено);
	Параметры.Вставить("Организации", Неопределено);
	Параметры.Вставить("ПериодРасчета", Неопределено);
	Параметры.Вставить("ДатаЗапрета", Неопределено);
	Параметры.Вставить("НеотраженныеДокументы", Новый Массив());
	Параметры.Вставить("ПакетноеОтражение", Ложь);
	Параметры.Вставить("ВременныеТаблицыПереданы", Ложь);
	Параметры.Вставить("СообщатьОбОшибках", Ложь);
	Параметры.Вставить("ДетализацияПроводок", Ложь);
	Параметры.Вставить("ПрогнозныеПараметры", Неопределено);
	Параметры.Вставить("МинимальныйРазмерПорции", 2000);
	Параметры.Вставить("КоличествоПотоков", КоличествоПотоковОтраженияВРеглУчете());
	Параметры.Вставить("КоличествоПопыток", 3);
	Параметры.Вставить("ИспользованиеТекущихИтогов", Ложь);
	Параметры.Вставить("МаксимальныйПериодИтогов", '00010101');
	Параметры.Вставить("МинимальныйПериодИтогов", '00010101');
	Параметры.Вставить("ВосстановитьСостояниеИтогов", Ложь);
	
	Возврат Параметры;
	
КонецФункции

// Метод для вызова из регламентного задания отражения в регл. учете
Процедура ОтразитьВсеРегламент() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.ОтражениеДокументовВРеглУчете);
	
	ТекущаяДата = ТекущаяДатаСеанса();
	УчетНДСУП.ВыполнитьФормированиеДвиженийПоНДС(КонецМесяца(ТекущаяДата));
	
	ВыполнитьОффлайновыеРасчеты();
	
	ПараметрыОтражения = ПараметрыОтраженияВРеглУчете();
	ПараметрыОтражения.ПериодРасчета = КонецДня(ТекущаяДата);
	
	ОтразитьВсе(ПараметрыОтражения);
	
КонецПроцедуры

// Метод для вызова в фоне отражения в регл. учете
// 
// Параметры:
//  ПараметрыОтражения - см. ПараметрыОтраженияВРеглУчете
//  АдресХранилища - Строка - адрес хранилища для помещения информации о результатах отражения
//
Процедура ОтразитьВсеВФоне(ПараметрыОтражения, АдресХранилища) Экспорт
	
	ДанныеПоОтражениюВУчете = ИнициализироватьДанныеПоОтражениюВУчете();
	
	УчетНДСУП.ВыполнитьФормированиеДвиженийПоНДС(
		КонецМесяца(ПараметрыОтражения.ПериодРасчета),
		ПараметрыОтражения.Организации);
	
	ОтразитьВсе(ПараметрыОтражения, ДанныеПоОтражениюВУчете);
	
	ПоместитьВоВременноеХранилище(ДанныеПоОтражениюВУчете, АдресХранилища);
	
КонецПроцедуры

// Метод инициализирует дополнительную информацию по отражению документов в регл. учете (сколько отражено, количество ошибок).
//
//	Возвращаемое значение:
//		Структура - содержит результирующие данные по количеству отраженных документов в регламентированном учете:
//			* Отражено - Число - количество отраженных в учете документов.
//			* НеУказаныСчетаУчета - Число - количество документов, которые не были отражены в результате ненастроенных счетов учета.
//			* ОшибкиПриОтражении - Число - количество документов, которые не были отражены в результате прочих ошибок при отражении.
//			* ОписанияПроблем - Соответствие:
//				** Ключ - ДокументСсылка - отражаемый в учете документ
//				** Значение - Строка - описание проблемы отражения в учете
//
Функция ИнициализироватьДанныеПоОтражениюВУчете() Экспорт
	
	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("Отражено", 0);
	СтруктураРезультата.Вставить("НеУказаныСчетаУчета", 0);
	СтруктураРезультата.Вставить("ОшибкиПриОтражении", 0);
	СтруктураРезультата.Вставить("ОписанияПроблем", Новый Соответствие());
	
	Возврат СтруктураРезультата;
	
КонецФункции

// Формирует движения по регистру Хозрасчетный для всех документов, помеченных к отражению в регламентированном учете.
//
// Параметры:
//  ПараметрыОтражения - см. ПараметрыОтраженияВРеглУчете
//  ДанныеПоОтражениюВУчете - см. ИнициализироватьДанныеПоОтражениюВУчете
//
Процедура ОтразитьВсе(ПараметрыОтражения, ДанныеПоОтражениюВУчете = Неопределено) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьРеглУчет") Тогда
		Возврат;
	КонецЕсли;
	
	Если ДанныеПоОтражениюВУчете = Неопределено Тогда
		ДанныеПоОтражениюВУчете = ИнициализироватьДанныеПоОтражениюВУчете();
	КонецЕсли;
	
	ШаблонЗапретаДанных = ДатыЗапретаИзменения.ШаблонДанныхДляПроверки();
	СтрокаШаблона = ШаблонЗапретаДанных.Добавить();
	СтрокаШаблона.Дата   = Дата(1, 1, 1);
	СтрокаШаблона.Раздел = "БухгалтерскийУчет";
	СтрокаШаблона.Объект = ПараметрыОтражения.Организации;
	
	ПараметрыСообщенияОЗапрете = ДатыЗапретаИзменения.ПараметрыСообщенияОЗапрете();
	
	ОписаниеОшибки = Новый Структура;
	
	Если ТипЗнч(ПараметрыОтражения.Организации) <> Тип("Массив") Тогда
		Если ДатыЗапретаИзменения.НайденЗапретИзмененияДанных(ШаблонЗапретаДанных, ПараметрыСообщенияОЗапрете, ОписаниеОшибки)
			И ОписаниеОшибки.Свойство("Запреты") И ОписаниеОшибки.Запреты.Количество() > 0 Тогда
			ПараметрыОтражения.ДатаЗапрета = КонецДня(ОписаниеОшибки.Запреты[0].ДатаЗапрета);
		КонецЕсли;
	КонецЕсли;

	// Отразим документы пакетно
	Результат = ОтразитьДокументыПакетноСЗамеромВремени(ПараметрыОтражения);
	ДанныеПоОтражениюВУчете.Отражено = ДанныеПоОтражениюВУчете.Отражено + Результат.ОтраженоВУчете;
	ДанныеПоОтражениюВУчете.НеУказаныСчетаУчета = ДанныеПоОтражениюВУчете.НеУказаныСчетаУчета + Результат.НеУказаныСчетаУчета;
	ДанныеПоОтражениюВУчете.ОшибкиПриОтражении = ДанныеПоОтражениюВУчете.ОшибкиПриОтражении + Результат.ОшибкиПриОтражении;
	ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(ДанныеПоОтражениюВУчете.ОписанияПроблем, Результат.ОписанияПроблем, Истина);
	
	// Отразим документы последовательно
	Результат = ОтразитьДокументыПоследовательноСЗамеромВремени(ПараметрыОтражения);
	ДанныеПоОтражениюВУчете.Отражено = ДанныеПоОтражениюВУчете.Отражено + Результат.ОтраженоВУчете;
	ДанныеПоОтражениюВУчете.НеУказаныСчетаУчета = ДанныеПоОтражениюВУчете.НеУказаныСчетаУчета + Результат.НеУказаныСчетаУчета;
	ДанныеПоОтражениюВУчете.ОшибкиПриОтражении = ДанныеПоОтражениюВУчете.ОшибкиПриОтражении + Результат.ОшибкиПриОтражении;
	ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(ДанныеПоОтражениюВУчете.ОписанияПроблем, Результат.ОписанияПроблем, Истина);
	
	Если ДанныеПоОтражениюВУчете.ОшибкиПриОтражении > 0 Тогда
		ВызватьИсключение НСтр("ru = 'При отражении документов в регл. учете возникли ошибки. Подробнее см. в журнале регистрации.';
								|en = 'Errors occurred while recording the documents in local accounting. For more information, see the event log.'");
	КонецЕсли;
	
КонецПроцедуры

// Формирует движения по регистру Хозрасчетный для указанных документов
//
// Параметры:
//    МассивДокументов - Массив - Документы
//    ВыполнитьПересчеты - Булево - Признак, что перед отражением документа необходимо выполнить отражение в учете НДС и
//                                  распределение взаиморасчетов.
//
// Возвращаемое значение:
//    Массив - Документы, которые не удалось отразить в регл. учете.
//
Функция ОтразитьДокументыВРеглУчете(МассивДокументов, ВыполнитьПересчеты = Ложь) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьРеглУчет") Тогда
		Возврат МассивДокументов;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ВыполнитьПересчеты Тогда
		ВыполнитьОффлайновыеРасчеты(МассивДокументов);
	КонецЕсли;
	
	ПараметрыОтраженияВРеглУчете = ПараметрыОтраженияВРеглУчете();
	ПараметрыОтраженияВРеглУчете.Документы = МассивДокументов;
	ПараметрыОтраженияВРеглУчете.ПакетноеОтражение = Истина;
	ПараметрыОтраженияВРеглУчете.СообщатьОбОшибках = Истина;
	
	ВременныеТаблицы = ВременныеТаблицы();
	
	// Отразим документы пакетно
	ТипыДокументов = ТипыДокументовКПакетномуОтражению(ПараметрыОтраженияВРеглУчете);
	Для каждого ТипДокумента Из ТипыДокументов Цикл
		Пока СформироватьТаблицыОтбораДанных(ТипДокумента, ВременныеТаблицы, ПараметрыОтраженияВРеглУчете) Цикл
			Результат = ВыполнитьОтражение(ТипДокумента, ВременныеТаблицы, ПараметрыОтраженияВРеглУчете);
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПараметрыОтраженияВРеглУчете.НеотраженныеДокументы, Результат.НеУказаныСчетаУчета);
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПараметрыОтраженияВРеглУчете.НеотраженныеДокументы, Результат.ОшибкиПриОтражении);
		КонецЦикла;
	КонецЦикла;
	
	// Отразим документы последовательно
	Выборка = ДокументыКПоследовательномуОтражению(ПараметрыОтраженияВРеглУчете);
	Если Не Выборка = Неопределено Тогда
		Пока Выборка.Следующий() Цикл
			ТипДокумента = Выборка.ТипДокумента;
			ПараметрыОтраженияВРеглУчете.Документы = Выборка.Ссылка;
			
			Если СформироватьТаблицыОтбораДанных(ТипДокумента, ВременныеТаблицы, ПараметрыОтраженияВРеглУчете) Тогда
				Результат = ВыполнитьОтражение(ТипДокумента, ВременныеТаблицы, ПараметрыОтраженияВРеглУчете);
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПараметрыОтраженияВРеглУчете.НеотраженныеДокументы, Результат.НеУказаныСчетаУчета);
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПараметрыОтраженияВРеглУчете.НеотраженныеДокументы, Результат.ОшибкиПриОтражении);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ВременныеТаблицы.Закрыть();
	
	Возврат ПараметрыОтраженияВРеглУчете.НеотраженныеДокументы;
	
КонецФункции

// Формирует движения по регистру Хозрасчетный для указанных документов
//
// Параметры:
//    МассивДокументов - Массив - Документы.
//
// Возвращаемое значение:
//    Массив - Документы, которые не удалось отразить в регл. учете.
//
Функция ОтразитьДокументыВРеглУчетеПриПечати(МассивДокументов) Экспорт
	
	Возврат ОтразитьДокументыВРеглУчете(МассивДокументов, Истина);
	
КонецФункции

// Формирует движения по регистру Хозрасчетный для указанного документа. Аналог "ОтразитьДокумент", вызываемый для
// фоновых операций.
//
// Параметры:
// 	ПараметрыОтражения - Структура - Параметры отражения:
// 		*РеквизитыДокумента - Структура - реквизиты документа:
// 			** Ссылка - ДокументСсылка - ссылка на документ
// 			** Дата - Дата - Дата документа
// 			** Организация - СправочникСсылка.Организации - Организация документа
//		* ВыполнитьПересчеты - Булево - Если истина, запускает актуализацию взаиморасчетов с партнерами
//		* ОтборПоОрганизациям - СправочникСсылка.Организации, Массив Из СправочникСсылка.Организации
//		* ВременныеТаблицыПереданы - Булево - Признак, что документ отражается при проведении
// 			и для него не надо выполнять стандартное заполнение временных таблиц
//		* ДетализацияПроводок - Булево - признак дополнительной детализации проводок по Кт 20-го счета.
// 	АдресРезультата - Строка - Адрес временного хранилища для помещения результата.
//
Процедура ОтразитьДокументВФоне(ПараметрыОтражения, АдресРезультата) Экспорт
	
	Статус = ОтразитьДокумент(ПараметрыОтражения.РеквизитыДокумента,, ПараметрыОтражения);
	
	ПоместитьВоВременноеХранилище(Статус, АдресРезультата);
	
КонецПроцедуры

// Формирует движения по регистру Хозрасчетный для указанного документа.
//
// Параметры:
// 	РеквизитыДокумента - Структура - реквизиты документа:
// 		* Ссылка - ДокументСсылка - ссылка на документ
// 		* Дата - Дата - Дата документа
// 		* Организация - СправочникСсылка.Организации - Организация документа
// 	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Поставляемый внешний менеджер временных таблиц.
// 	ПараметрыОтражения - Структура - Параметры отражения:
// 		* ВыполнитьПересчеты - Булево - Признак необходимости выполнить пересчеты, запускает актуализацию взаиморасчетов с партнерами
//		* ОтборПоОрганизациям - СправочникСсылка.Организации, Массив Из СправочникСсылка.Организации
//		* ВременныеТаблицыПереданы - Булево - Признак, что документ отражается при проведении
// 			и для него не надо выполнять стандартное заполнение временных таблиц
//		* ДетализацияПроводок - Булево - признак дополнительной детализации проводок по Кт 20-го счета.
//
// Возвращаемое значение:
//    ПеречислениеСсылка.СтатусыОтраженияДокументовВРеглУчете - Статус отражения документа в учете.
//
Функция ОтразитьДокумент(РеквизитыДокумента, МенеджерВременныхТаблиц = Неопределено, ПараметрыОтражения = Неопределено) Экспорт
	
	ВыполнитьПересчеты = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОтражения, "ВыполнитьПересчеты", Ложь);
	ОтборПоОрганизациям = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОтражения, "ОтборПоОрганизациям");
	ВременныеТаблицыПереданы = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОтражения, "ВременныеТаблицыПереданы", Ложь);
	ДетализацияПроводок = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОтражения, "ДетализацияПроводок", Ложь);
	
	ТипДокумента = ТипЗнч(РеквизитыДокумента.Ссылка);
	Статусы = Перечисления.СтатусыОтраженияДокументовВРеглУчете;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если РеквизитыДокумента.Организация = Справочники.Организации.УправленческаяОрганизация Тогда
		Возврат Статусы.ОтраженоВРеглУчете;
	КонецЕсли;
	
	Если ВыполнитьПересчеты Тогда
		ВыполнитьОффлайновыеРасчеты(РеквизитыДокумента.Ссылка);
	КонецЕсли;
	
	ВременныеТаблицы = ВременныеТаблицы(МенеджерВременныхТаблиц);
	
	ПараметрыОтраженияВРеглУчете = ПараметрыОтраженияВРеглУчете();
	ПараметрыОтраженияВРеглУчете.Документы = РеквизитыДокумента.Ссылка;
	ПараметрыОтраженияВРеглУчете.Организации = ОтборПоОрганизациям;
	ПараметрыОтраженияВРеглУчете.ВременныеТаблицыПереданы = ВременныеТаблицыПереданы;
	ПараметрыОтраженияВРеглУчете.ДетализацияПроводок = ДетализацияПроводок;
	
	Если НЕ СформироватьТаблицыОтбораДанных(ТипДокумента, ВременныеТаблицы, ПараметрыОтраженияВРеглУчете) Тогда
		Возврат Статусы.ОтраженоВРеглУчете;
	КонецЕсли;
	
	РезультатОтражения = ВыполнитьОтражение(ТипДокумента, ВременныеТаблицы, ПараметрыОтраженияВРеглУчете);
	ВременныеТаблицы.Закрыть();
	
	Если РезультатОтражения.ОтраженоВУчете.Количество() = 1 Тогда
		Статус = Статусы.ОтраженоВРеглУчете;
	Иначе
		Статус = Статусы.НеУказаныСчетаУчета;
	КонецЕсли;
	
	Возврат Статус;
	
КонецФункции

// Возвращает документы к отражению в регл. учете
//
// Параметры:
// 	ДокументыКОтражению -   ТаблицаЗначений, 
// 							МенеджерВременныхТаблиц - Таблица документов, которые надо вернуть к отражению, 
// 													  или менеджер временных таблиц имеющий таблицу ДокументыКОтражению
// 													  Таблица должна иметь колонки Документ, Организация, ДатаОтражения. 
// 	КоличествоОбработанных - Число - В данном параметр устанавливается количество возвращенных к отражению документов.
// 									 Параметр не является обязательным.
//
Процедура ВернутьДокументыКОтражению(ДокументыКОтражению, КоличествоОбработанных = Неопределено) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьРеглУчет") Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ТекстыЗапросаПодготовкиВременныхТаблиц = Новый Массив;
	Если ТипЗнч(ДокументыКОтражению) = Тип("ТаблицаЗначений") Тогда
		Если ДокументыКОтражению.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДокументыКОтражению.Документ КАК Документ,
		|	ДокументыКОтражению.Организация КАК Организация,
		|	ДокументыКОтражению.ДатаОтражения КАК ДатаОтражения
		|ПОМЕСТИТЬ ДокументыКОтражению
		|ИЗ
		|	&ДокументыКОтражению КАК ДокументыКОтражению
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Документ,
		|	Организация,
		|	ДатаОтражения";
		Запрос.УстановитьПараметр("ДокументыКОтражению", ДокументыКОтражению);
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		ТекстыЗапросаПодготовкиВременныхТаблиц.Добавить(ТекстЗапроса);
		ДанныеКОтражению = ДокументыКОтражению;
	Иначе
		Запрос.МенеджерВременныхТаблиц = ДокументыКОтражению;
		ДанныеКОтражению = ДокументыКОтражению.Таблицы["ДокументыКОтражению"].ПолучитьДанные().Выгрузить();
	КонецЕсли;
	
	ШаблонЗапретаДанных = ДатыЗапретаИзменения.ШаблонДанныхДляПроверки();
	Для каждого СтрокаОтражения Из ДанныеКОтражению Цикл
		СтрокаШаблона = ШаблонЗапретаДанных.Добавить();
		СтрокаШаблона.Дата   = СтрокаОтражения.ДатаОтражения;
		СтрокаШаблона.Раздел = "БухгалтерскийУчет";
		СтрокаШаблона.Объект = СтрокаОтражения.Организация;
	КонецЦикла;
	
	ПроверитьДатуЗапретаДляОтраженияДокументовВРеглУчете(ШаблонЗапретаДанных);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтражениеДокументовВРеглУчете.Период КАК Период,
	|	ДокументыКОтражению.Документ КАК Регистратор,
	|	ДокументыКОтражению.Организация КАК Организация,
	|	ОтражениеДокументовВРеглУчете.ДатаОтражения КАК ДатаОтражения,
	|	Регистр.Статус КАК Статус,
	|	ВЫРАЗИТЬ(Регистр.Комментарий КАК СТРОКА(1024)) КАК Комментарий
	|ПОМЕСТИТЬ ТаблицаРегистра
	|ИЗ
	|	РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументовВРеглУчете
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыКОтражению КАК ДокументыКОтражению
	|			ПО (ДокументыКОтражению.Документ = ОтражениеДокументовВРеглУчете.Регистратор)
	|			И (ДокументыКОтражению.Организация = ОтражениеДокументовВРеглУчете.Организация)
	|			И ТИПЗНАЧЕНИЯ(ОтражениеДокументовВРеглУчете.Регистратор) <> ТИП(Документ.КорректировкаРегистров)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОтражениеДокументовВРеглУчете КАК Регистр
	|			ПО ОтражениеДокументовВРеглУчете.Период = Регистр.Период
	|			И ОтражениеДокументовВРеглУчете.Регистратор = Регистр.Регистратор
	|			И ОтражениеДокументовВРеглУчете.Организация = Регистр.Организация
	|			И ОтражениеДокументовВРеглУчете.ДатаОтражения = Регистр.ДатаОтражения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	Организация,
	|	ДатаОтражения";
	ТекстыЗапросаПодготовкиВременныхТаблиц.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДокументыКОтражению.Документ КАК Документ,
	|	ДокументыКОтражению.Организация КАК Организация,
	|	ДокументыКОтражению.ДатаОтражения КАК ДатаОтражения,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ОтражениеДокументовВРеглУчете.Период, ДАТАВРЕМЯ(1, 1, 1)) <> ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ЕСТЬNULL(ОтражениеДокументовВРеглУчете.Период, ДАТАВРЕМЯ(1, 1, 1))
	|		КОГДА МИНИМУМ(ЕСТЬNULL(ТаблицаРегистраПервыйПериод.Период, ДАТАВРЕМЯ(1, 1, 1))) <> ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА МИНИМУМ(ЕСТЬNULL(ТаблицаРегистраПервыйПериод.Период, ДАТАВРЕМЯ(1, 1, 1)))
	|		ИНАЧЕ МИНИМУМ(ЕСТЬNULL(ТолькоДокументыКОтражению.ДатаОтражения, ДАТАВРЕМЯ(1, 1, 1)))
	|	КОНЕЦ КАК Период
	|ПОМЕСТИТЬ ПериодыРегистрации
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыКОтражению КАК ТолькоДокументыКОтражению
	|		ПО ДокументыКОтражению.Документ = ТолькоДокументыКОтражению.Документ
	|			И ДокументыКОтражению.Организация = ТолькоДокументыКОтражению.Организация
	|			И ТИПЗНАЧЕНИЯ(ДокументыКОтражению.Документ) <> ТИП(Документ.КорректировкаРегистров)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаРегистра КАК ОтражениеДокументовВРеглУчете
	|		ПО ДокументыКОтражению.Документ = ОтражениеДокументовВРеглУчете.Регистратор
	|			И ДокументыКОтражению.Организация = ОтражениеДокументовВРеглУчете.Организация
	|			И ДокументыКОтражению.ДатаОтражения = ОтражениеДокументовВРеглУчете.ДатаОтражения
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаРегистра КАК ТаблицаРегистраПервыйПериод
	|			ПО ДокументыКОтражению.Документ = ТаблицаРегистраПервыйПериод.Регистратор
	|СГРУППИРОВАТЬ ПО
	|	ДокументыКОтражению.Документ,
	|	ДокументыКОтражению.Организация,
	|	ДокументыКОтражению.ДатаОтражения,
	|	ОтражениеДокументовВРеглУчете.Период
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Документ,
	|	Организация,
	|	ДатаОтражения";
	ТекстыЗапросаПодготовкиВременныхТаблиц.Добавить(ТекстЗапроса);
	
	ТекстыЗапросаВыборки = Новый Массив;
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ОтражениеДокументовВРеглУчете.Период         КАК Период,
	|	ОтражениеДокументовВРеглУчете.Регистратор    КАК Регистратор,
	|	ОтражениеДокументовВРеглУчете.Организация    КАК Организация,
	|	ОтражениеДокументовВРеглУчете.ДатаОтражения  КАК ДатаОтражения,
	|	ВЫБОР 
	|		КОГДА НЕ ДокументыКОтражению.Организация ЕСТЬ NULL
	|			ИЛИ ТИПЗНАЧЕНИЯ(ОтражениеДокументовВРеглУчете.Регистратор) = ТИП(Документ.КорректировкаРеализации)
	|				И НАЧАЛОПЕРИОДА(ОтражениеДокументовВРеглУчете.ДатаОтражения, ГОД) <
	|					НАЧАЛОПЕРИОДА(ОтражениеДокументовВРеглУчете.Период, ГОД)
	|		ТОГДА ВЫБОР
	|				КОГДА ОтражениеДокументовВРеглУчете.Статус В (
	|					ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВУчетеВручную),
	|					ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВУчетеВручную))
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВУчетеВручную)
	|				ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВРеглУчете)
	|			КОНЕЦ
	|		ИНАЧЕ ОтражениеДокументовВРеглУчете.Статус
	|	КОНЕЦ КАК Статус,
	|	ВЫБОР
	|		КОГДА НЕ ДокументыКОтражению.Организация ЕСТЬ NULL
	|			ИЛИ ТИПЗНАЧЕНИЯ(ОтражениеДокументовВРеглУчете.Регистратор) = ТИП(Документ.КорректировкаРеализации)
	|				И НАЧАЛОПЕРИОДА(ОтражениеДокументовВРеглУчете.ДатаОтражения, ГОД) <
	|					НАЧАЛОПЕРИОДА(ОтражениеДокументовВРеглУчете.Период, ГОД)
	|		ТОГДА ВЫБОР
	|				КОГДА ОтражениеДокументовВРеглУчете.Статус В (
	|					ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВУчетеВручную),
	|					ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВУчетеВручную))
	|				ТОГДА ОтражениеДокументовВРеглУчете.Комментарий
	|				ИНАЧЕ """"
	|			КОНЕЦ
	|		ИНАЧЕ ОтражениеДокументовВРеглУчете.Комментарий
	|	КОНЕЦ КАК Комментарий,
	|	(НЕ ДокументыКОтражению.Организация ЕСТЬ NULL
	|			ИЛИ ТИПЗНАЧЕНИЯ(ОтражениеДокументовВРеглУчете.Регистратор) = ТИП(Документ.КорректировкаРеализации)
	|				И НАЧАЛОПЕРИОДА(ОтражениеДокументовВРеглУчете.ДатаОтражения, ГОД) <
	|					НАЧАЛОПЕРИОДА(ОтражениеДокументовВРеглУчете.Период, ГОД))
	|		И ОтражениеДокументовВРеглУчете.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВУчетеВручную),
	|			ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.НеУказаныСчетаУчета),
	|			ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВРеглУчете)) КАК ЕстьИзменения
	|ИЗ
	|	ТаблицаРегистра КАК ОтражениеДокументовВРеглУчете
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПериодыРегистрации КАК ДокументыКОтражению
	|	ПО
	|		ДокументыКОтражению.Документ = ОтражениеДокументовВРеглУчете.Регистратор
	|		И ДокументыКОтражению.Организация = ОтражениеДокументовВРеглУчете.Организация
	|		И ДокументыКОтражению.ДатаОтражения = ОтражениеДокументовВРеглУчете.ДатаОтражения
	|	
	|ГДЕ
	|	ОтражениеДокументовВРеглУчете.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|	И (ОтражениеДокументовВРеглУчете.ДатаОтражения >= &ДатаНачалаВеденияРеглУчета)";
	ДобавитьВЗапросФильтрОтраженияВРеглУчете(ТекстЗапроса, "ОтражениеДокументовВРеглУчете.Регистратор");
	ТекстыЗапросаВыборки.Добавить(ТекстЗапроса);
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДокументыКОтражению.Период          КАК Период,
	|	ДокументыКОтражению.Документ       КАК Регистратор,
	|	ДокументыКОтражению.Организация    КАК Организация,
	|	ДокументыКОтражению.ДатаОтражения  КАК ДатаОтражения,
	|	ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВРеглУчете) КАК Статус,
	|	"""" КАК Комментарий,
	|	ИСТИНА
	|ИЗ
	|	ПериодыРегистрации КАК ДокументыКОтражению
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ТаблицаРегистра КАК ОтражениеДокументовВРеглУчете
	|	ПО
	|		ДокументыКОтражению.Документ = ОтражениеДокументовВРеглУчете.Регистратор
	|		И ДокументыКОтражению.Организация = ОтражениеДокументовВРеглУчете.Организация
	|		И ДокументыКОтражению.ДатаОтражения = ОтражениеДокументовВРеглУчете.ДатаОтражения
	|	
	|ГДЕ
	|	ДокументыКОтражению.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|	И ОтражениеДокументовВРеглУчете.Регистратор ЕСТЬ NULL
	|	И (ДокументыКОтражению.ДатаОтражения >= &ДатаНачалаВеденияРеглУчета)";
	
	ДобавитьВЗапросФильтрОтраженияВРеглУчете(ТекстЗапроса, "ДокументыКОтражению.Документ");
	ТекстыЗапросаВыборки.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = СтрСоединить(ТекстыЗапросаВыборки, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	//Добавим итоги в текст запроса отдельно для корректного открытия текста в конструкторе запроса
	ТекстЗапроса = ТекстЗапроса + "
	|	
	|ИТОГИ
	|	МАКСИМУМ(ЕстьИзменения)
	|ПО
	|	Регистратор";
	ТекстыЗапросаПодготовкиВременныхТаблиц.Добавить(ТекстЗапроса);
	Запрос.Текст = СтрСоединить(ТекстыЗапросаПодготовкиВременныхТаблиц, ОбщегоНазначения.РазделительПакетаЗапросов());
	Запрос.УстановитьПараметр("ДатаНачалаВеденияРеглУчета", Константы.ДатаНачалаВеденияРеглУчета.Получить());
	Результат = Запрос.Выполнить();
	ВыборкаПоДокументам = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ТипыДокументовКОтражению = Метаданные.РегистрыСведений.ОтражениеДокументовВРеглУчете.СтандартныеРеквизиты.Регистратор.Тип;
	Пока ВыборкаПоДокументам.Следующий() Цикл
		Если ТипыДокументовКОтражению.СодержитТип(ТипЗнч(ВыборкаПоДокументам.Регистратор)) И ВыборкаПоДокументам.ЕстьИзменения Тогда
			НаборЗаписей = РегистрыСведений.ОтражениеДокументовВРеглУчете.СоздатьНаборЗаписей();
			НаборЗаписей.ДополнительныеСвойства.Вставить("ПроверкаДатыЗапретаИзменения", Ложь);
			НаборЗаписей.Отбор.Регистратор.Установить(ВыборкаПоДокументам.Регистратор);
			Выборка = ВыборкаПоДокументам.Выбрать();
			Пока Выборка.Следующий() Цикл
				НоваяЗапись = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
			КонецЦикла;
			НаборЗаписей.Записать();
			Если ТипЗнч(КоличествоОбработанных) = Тип("Число") Тогда
				КоличествоОбработанных = КоличествоОбработанных + 1;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	СписокТаблицДляУничтожения = "ПериодыРегистрации,ТаблицаРегистра";
	ОбщегоНазначенияУТ.УничтожитьВременныеТаблицы(Запрос.МенеджерВременныхТаблиц, СписокТаблицДляУничтожения);
	
КонецПроцедуры

// Записывает движения по регистру ОтражениеДокументаВРеглУчете, выполняет очистку неактуальных записей в Хозрасчетный.
// Вызывается из оффлайновых общих модулей НДС.
//
// Параметры:
// 	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - менеджер временных таблиц, имеющий таблицы
// 		ТаблицаТекущейРегистрации и ТаблицаВыборочнойРегистрации.
// 		Таблицы должны иметь колонки Документ, Организация, ДатаОтражения.
//
// 	КоличествоОбработанных - Число - В данном параметр устанавливается количество возвращенных к отражению документов.
// 									 Параметр не является обязательным.
//
Процедура ВернутьДокументыКОтражениюВыборочноПакетно(МенеджерВременныхТаблиц, КоличествоОбработанных = Неопределено) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьРеглУчет") Тогда
	 	Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	#Область ТекстЗапроса
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаТекущейРегистрации.Документ КАК Документ
	|ПОМЕСТИТЬ ТолькоДокументыКОтражению
	|ИЗ
	|	ТаблицаТекущейРегистрации КАК ТаблицаТекущейРегистрации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтражениеДокументов.Период КАК Период,
	|	ОтражениеДокументов.Регистратор КАК Документ,
	|	ОтражениеДокументов.Организация КАК Организация,
	|	ОтражениеДокументов.ДатаОтражения КАК ДатаОтражения,
	|	ОтражениеДокументов.Статус КАК Статус,
	|	ОтражениеДокументов.Комментарий КАК Комментарий
	|ПОМЕСТИТЬ ТекущиеСтатусы
	|ИЗ
	|	РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТолькоДокументыКОтражению КАК ТолькоДокументыКОтражению
	|		ПО ОтражениеДокументов.Регистратор = ТолькоДокументыКОтражению.Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаВыборочнойРегистрации.Документ КАК Документ,
	|	МИНИМУМ(ТаблицаВыборочнойРегистрации.ДатаОтражения) КАК ДатаОтражения
	|ПОМЕСТИТЬ НачалоИзмененийВыборочнойРегистрации
	|ИЗ
	|	ТаблицаВыборочнойРегистрации КАК ТаблицаВыборочнойРегистрации
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаВыборочнойРегистрации.Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ТекущиеСтатусы.Период, ТаблицаТекущейРегистрации.Период) КАК Период,
	|	ТаблицаТекущейРегистрации.Документ КАК Документ,
	|	ТаблицаТекущейРегистрации.Организация КАК Организация,
	|	ТаблицаТекущейРегистрации.ДатаОтражения КАК ДатаОтражения,
	|	ВЫБОР
	|		КОГДА ТаблицаВыборочнойРегистрации.ДатаОтражения ЕСТЬ NULL
	|				И НЕ ТекущиеСтатусы.Статус ЕСТЬ NULL
	|			ТОГДА ТекущиеСтатусы.Статус
	|		КОГДА ЕСТЬNULL(ТекущиеСтатусы.Статус, НЕОПРЕДЕЛЕНО) В (&ОтраженоВУчетеВручную, &КОтражениюВУчетеВручную)
	|			ТОГДА &КОтражениюВУчетеВручную
	|		ИНАЧЕ &КОтражениюВРеглУчете
	|	КОНЕЦ КАК Статус,
	|	ВЫБОР
	|		КОГДА ТаблицаВыборочнойРегистрации.ДатаОтражения ЕСТЬ NULL
	|			ТОГДА ТекущиеСтатусы.Комментарий
	|		КОГДА ЕСТЬNULL(ТекущиеСтатусы.Статус, НЕОПРЕДЕЛЕНО) В (&ОтраженоВУчетеВручную, &КОтражениюВУчетеВручную)
	|			ТОГДА ТекущиеСтатусы.Комментарий
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК Комментарий
	|ПОМЕСТИТЬ НовыеСтатусы
	|ИЗ
	|	ТаблицаТекущейРегистрации КАК ТаблицаТекущейРегистрации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТекущиеСтатусы КАК ТекущиеСтатусы
	|		ПО ТаблицаТекущейРегистрации.Документ = ТекущиеСтатусы.Документ
	|			И ТаблицаТекущейРегистрации.Организация = ТекущиеСтатусы.Организация
	|			И ТаблицаТекущейРегистрации.ДатаОтражения = ТекущиеСтатусы.ДатаОтражения
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаВыборочнойРегистрации КАК ТаблицаВыборочнойРегистрации
	|		ПО ТаблицаТекущейРегистрации.Документ = ТаблицаВыборочнойРегистрации.Документ
	|			И ТаблицаТекущейРегистрации.Организация = ТаблицаВыборочнойРегистрации.Организация
	|			И ТаблицаТекущейРегистрации.ДатаОтражения = ТаблицаВыборочнойРегистрации.ДатаОтражения
	|ГДЕ
	|	ТаблицаВыборочнойРегистрации.ДатаОтражения ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.Документ КАК Документ,
	|	Таблица.Организация КАК Организация,
	|	Таблица.ДатаОтражения КАК ДатаОтражения
	|ПОМЕСТИТЬ ИзмененияСтатусов
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Документ КАК Документ,
	|		Таблица.ДатаОтражения КАК ДатаОтражения,
	|		Таблица.Организация КАК Организация,
	|		ВЫБОР
	|			КОГДА Таблица.Статус = &КОтражениюВРеглУчете
	|				ТОГДА 1
	|			КОГДА Таблица.Статус = &ОтраженоВРеглУчете
	|				ТОГДА 2
	|		КОНЕЦ КАК Статус
	|	ИЗ
	|		НовыеСтатусы КАК Таблица
	|	ГДЕ
	|		Таблица.Статус В (&КОтражениюВРеглУчете, &ОтраженоВРеглУчете)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Документ,
	|		Таблица.ДатаОтражения,
	|		Таблица.Организация,
	|		ВЫБОР
	|			КОГДА Таблица.Статус = &КОтражениюВРеглУчете
	|				ТОГДА -1
	|			КОГДА Таблица.Статус = &ОтраженоВРеглУчете
	|				ТОГДА -2
	|		КОНЕЦ
	|	ИЗ
	|		ТекущиеСтатусы КАК Таблица
	|	ГДЕ
	|		Таблица.Статус В (&КОтражениюВРеглУчете, &ОтраженоВРеглУчете)) КАК Таблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ НачалоИзмененийВыборочнойРегистрации КАК НачалоИзменений
	|		ПО Таблица.Документ = НачалоИзменений.Документ
	|ГДЕ
	|	Таблица.ДатаОтражения >= ЕСТЬNULL(НАЧАЛОПЕРИОДА(НачалоИзменений.ДатаОтражения, МЕСЯЦ), ДАТАВРЕМЯ(1, 1, 1))
	|
	|СГРУППИРОВАТЬ ПО
	|	Таблица.Документ,
	|	Таблица.ДатаОтражения,
	|	Таблица.Организация
	|
	|ИМЕЮЩИЕ
	|	СУММА(Таблица.Статус) <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Таблица.Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НовыеСтатусы.Период КАК Период,
	|	НовыеСтатусы.Документ КАК Документ,
	|	НовыеСтатусы.Организация КАК Организация,
	|	НовыеСтатусы.ДатаОтражения КАК ДатаОтражения,
	|	НовыеСтатусы.Статус КАК Статус,
	|	НовыеСтатусы.Комментарий КАК Комментарий
	|ИЗ
	|	НовыеСтатусы КАК НовыеСтатусы
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаОтражения
	|ИТОГИ ПО
	|	Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Хозрасчетный.Период КАК Период,
	|	Хозрасчетный.Регистратор КАК Регистратор,
	|	Хозрасчетный.Организация КАК Организация,
	|	Хозрасчетный.ПодразделениеДт КАК ПодразделениеДт,
	|	Хозрасчетный.НаправлениеДеятельностиДт КАК НаправлениеДеятельностиДт,
	|	Хозрасчетный.СчетДт КАК СчетДт,
	|	ЕСТЬNULL(Хозрасчетный.ВидСубконтоДт1, &ПустойВидСубконто) КАК ВидСубконтоДт1,
	|	ЕСТЬNULL(Хозрасчетный.СубконтоДт1, НЕОПРЕДЕЛЕНО) КАК СубконтоДт1,
	|	ЕСТЬNULL(Хозрасчетный.ВидСубконтоДт2, &ПустойВидСубконто) КАК ВидСубконтоДт2,
	|	ЕСТЬNULL(Хозрасчетный.СубконтоДт2, НЕОПРЕДЕЛЕНО) КАК СубконтоДт2,
	|	ЕСТЬNULL(Хозрасчетный.ВидСубконтоДт3, &ПустойВидСубконто) КАК ВидСубконтоДт3,
	|	ЕСТЬNULL(Хозрасчетный.СубконтоДт3, НЕОПРЕДЕЛЕНО) КАК СубконтоДт3,
	|	Хозрасчетный.ВалютаДт КАК ВалютаДт,
	|	Хозрасчетный.ВалютнаяСуммаДт КАК ВалютнаяСуммаДт,
	|	Хозрасчетный.КоличествоДт КАК КоличествоДт,
	|	Хозрасчетный.ПодразделениеКт КАК ПодразделениеКт,
	|	Хозрасчетный.НаправлениеДеятельностиКт КАК НаправлениеДеятельностиКт,
	|	Хозрасчетный.СчетКт КАК СчетКт,
	|	ЕСТЬNULL(Хозрасчетный.ВидСубконтоКт1, &ПустойВидСубконто) КАК ВидСубконтоКт1,
	|	ЕСТЬNULL(Хозрасчетный.СубконтоКт1, НЕОПРЕДЕЛЕНО) КАК СубконтоКт1,
	|	ЕСТЬNULL(Хозрасчетный.ВидСубконтоКт2, &ПустойВидСубконто) КАК ВидСубконтоКт2,
	|	ЕСТЬNULL(Хозрасчетный.СубконтоКт2, НЕОПРЕДЕЛЕНО) КАК СубконтоКт2,
	|	ЕСТЬNULL(Хозрасчетный.ВидСубконтоКт3, &ПустойВидСубконто) КАК ВидСубконтоКт3,
	|	ЕСТЬNULL(Хозрасчетный.СубконтоКт3, НЕОПРЕДЕЛЕНО) КАК СубконтоКт3,
	|	Хозрасчетный.ВалютаКт КАК ВалютаКт,
	|	Хозрасчетный.ВалютнаяСуммаКт КАК ВалютнаяСуммаКт,
	|	Хозрасчетный.КоличествоКт КАК КоличествоКт,
	|	Хозрасчетный.Сумма КАК Сумма,
	|	Хозрасчетный.СуммаУУ КАК СуммаУУ,
	|	Хозрасчетный.СуммаФО КАК СуммаФО,
	|	Хозрасчетный.СуммаНУДт КАК СуммаНУДт,
	|	Хозрасчетный.СуммаНУКт КАК СуммаНУКт,
	|	Хозрасчетный.СуммаПРДт КАК СуммаПРДт,
	|	Хозрасчетный.СуммаПРКт КАК СуммаПРКт,
	|	Хозрасчетный.СуммаВРДт КАК СуммаВРДт,
	|	Хозрасчетный.СуммаВРКт КАК СуммаВРКт,
	|	Хозрасчетный.Содержание КАК Содержание
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	|			,
	|			,
	|			Регистратор В
	|				(ВЫБРАТЬ
	|					Т.Документ
	|				ИЗ
	|					ТолькоДокументыКОтражению КАК Т),
	|			,
	|			) КАК Хозрасчетный
	|		ЛЕВОЕ СОЕДИНЕНИЕ ИзмененияСтатусов КАК ИзмененияСтатусов
	|		ПО Хозрасчетный.Регистратор = ИзмененияСтатусов.Документ
	|			И Хозрасчетный.Организация = ИзмененияСтатусов.Организация
	|			И (НАЧАЛОПЕРИОДА(Хозрасчетный.Период, ДЕНЬ) = ИзмененияСтатусов.ДатаОтражения)
	|ГДЕ
	|	ИзмененияСтатусов.Организация ЕСТЬ NULL";
	#КонецОбласти
	
	Запрос.УстановитьПараметр("ПустойВидСубконто",  ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка());
	
	Запрос.УстановитьПараметр("ОтраженоВРеглУчете",      Перечисления.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВРеглУчете);
	Запрос.УстановитьПараметр("КОтражениюВРеглУчете",    Перечисления.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВРеглУчете);
	Запрос.УстановитьПараметр("ОтраженоВУчетеВручную",   Перечисления.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВУчетеВручную);
	Запрос.УстановитьПараметр("КОтражениюВУчетеВручную", Перечисления.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВУчетеВручную);
	
	Результат = Запрос.ВыполнитьПакет();
	Количество = Результат.Количество();
	
	ИзмененияСтатусов = Запрос.МенеджерВременныхТаблиц.Таблицы.Получить("ИзмененияСтатусов").ПолучитьДанные().Выгрузить();
	
	ШаблонЗапретаДанных = ДатыЗапретаИзменения.ШаблонДанныхДляПроверки();
	Для Каждого СтрокаОтражения Из ИзмененияСтатусов Цикл
		СтрокаШаблона = ШаблонЗапретаДанных.Добавить();
		СтрокаШаблона.Дата   = СтрокаОтражения.ДатаОтражения;
		СтрокаШаблона.Раздел = "БухгалтерскийУчет";
		СтрокаШаблона.Объект = СтрокаОтражения.Организация;
	КонецЦикла;
	
	ПроверитьДатуЗапретаДляОтраженияДокументовВРеглУчете(ШаблонЗапретаДанных);
	
	Хозрасчетный = Результат[Количество - 1].Выгрузить();
	Хозрасчетный.Индексы.Добавить("Регистратор");
	
	ВыборкаПоДокументам = Результат[Количество - 2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ТипыДокументовКОтражению = Метаданные.РегистрыСведений.ОтражениеДокументовВРеглУчете.СтандартныеРеквизиты.Регистратор.Тип;
	Пока ВыборкаПоДокументам.Следующий() Цикл
		ДвиженияХозрасчетный = РегистрыБухгалтерии.Хозрасчетный.СоздатьНаборЗаписей();
		ДвиженияХозрасчетный.Отбор.Регистратор.Установить(ВыборкаПоДокументам.Документ);
		УстановитьДопСвойстваНабораЗаписейХозрасчетный(ДвиженияХозрасчетный, ВыборкаПоДокументам.Документ);
		ДвиженияХозрасчетный.Записывать = Истина;
		
		ХозрасчетныйПоДокументу = Хозрасчетный.СкопироватьКолонки();
		НайденныеСтроки = Хозрасчетный.НайтиСтроки(Новый Структура("Регистратор", ВыборкаПоДокументам.Документ));
		Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
			НоваяСтрока = ХозрасчетныйПоДокументу.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, НайденнаяСтрока);
		КонецЦикла;
		
		ДвиженияХозрасчетный.Загрузить(ХозрасчетныйПоДокументу);
		ДвиженияХозрасчетный.Записать();
		
		ЕстьИзменения = Не ИзмененияСтатусов.Найти(ВыборкаПоДокументам.Документ, "Документ") = Неопределено;
		Если ТипыДокументовКОтражению.СодержитТип(ТипЗнч(ВыборкаПоДокументам.Документ)) И ЕстьИзменения Тогда
			НаборЗаписей = РегистрыСведений.ОтражениеДокументовВРеглУчете.СоздатьНаборЗаписей();
			НаборЗаписей.ДополнительныеСвойства.Вставить("ПроверкаДатыЗапретаИзменения", Ложь);
			НаборЗаписей.Отбор.Регистратор.Установить(ВыборкаПоДокументам.Документ);
			
			Выборка = ВыборкаПоДокументам.Выбрать();
			Пока Выборка.Следующий() Цикл
				НоваяЗапись = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
			КонецЦикла;
			
			НаборЗаписей.Записать();
			Если ТипЗнч(КоличествоОбработанных) = Тип("Число") Тогда
				КоличествоОбработанных = КоличествоОбработанных + 1;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	СписокТаблицДляУничтожения = "ТолькоДокументыКОтражению,НачалоИзмененийВыборочнойРегистрации,ТекущиеСтатусы,НовыеСтатусы,ИзмененияСтатусов";
	ОбщегоНазначенияУТ.УничтожитьВременныеТаблицы(Запрос.МенеджерВременныхТаблиц, СписокТаблицДляУничтожения);
	
КонецПроцедуры

// Отменяет отражение документов в учете.
// 
// Параметры:
//  МассивДокументов - Массив из ДокументСсылка - Массив документов;
//  Отказ - Булево - признак ошибки при записи данных об отмене отражения.
//
Процедура ОтменитьОтражениеДокументовВУчете(МассивДокументов, Отказ = Ложь) Экспорт
	
	СписокТиповИсключений = ИсключенияДляПереотражения();
	
	МассивДокументовДляАвтоматическогоОтражения = Новый Массив;
	
	ТекстСообщения = "";
	
	Для каждого Документ Из МассивДокументов Цикл
		
		// Обработаем исключения документов, для которых нельзя откатить отражение в регл. учете:
		Если СписокТиповИсключений.Найти(ТипЗнч(Документ)) = Неопределено Тогда
			МассивДокументовДляАвтоматическогоОтражения.Добавить(Документ);
		Иначе
			ШаблонСообщения = НСтр("ru = 'Для документа ""%1"" невозможно отменить отражение в учете.';
									|en = 'Cannot cancel recording in accounting for the ""%1"" document.'");
			ШаблонСообщения = СтрШаблон(ШаблонСообщения, Документ);
			ТекстСообщения = ТекстСообщения + ?(ЗначениеЗаполнено(ТекстСообщения), Символы.ПС, "") + ШаблонСообщения;
		КонецЕсли;
		
	КонецЦикла;
	
	СтатусыОтражения = Перечисления.СтатусыОтраженияДокументовВРеглУчете;
	СтатусыОтбора = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СтатусыОтражения.ОтраженоВРеглУчете);
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьРучнуюКорректировкуПроводокПоРеглУчету") Тогда
		// Возможна ситуация когда функциональная опция ручного отражения снята, но документы отраженные вручную есть - для
		// них тоже надо снимать отражение:
		СтатусыОтбора.Добавить(СтатусыОтражения.КОтражениюВУчетеВручную);
		СтатусыОтбора.Добавить(СтатусыОтражения.ОтраженоВУчетеВручную);
	КонецЕсли;
	
	Отказ = ЗаписатьНовыйСтатусОтражения(МассивДокументовДляАвтоматическогоОтражения, СтатусыОтражения.КОтражениюВРеглУчете, СтатусыОтбора);
	
	Если ЗначениеЗаполнено(ТекстСообщения) Тогда
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

// Записывает новый статус отражения в учете.
// 
// Параметры:
//  МассивДокументов - Массив из ДокументСсылка - Массив документов.
//  НовыйСтатусОтражения - ПеречислениеСсылка.СтатусыОтраженияДокументовВРеглУчете - Новый статус отражения.
//  СтарыеСтатусыОтражения - Массив из ПеречислениеСсылка.СтатусыОтраженияДокументовВРеглУчете - Старые статусы отражения.
//
//	Возвращаемое значение:
//		Булево - признак успешности записи нового статуса отражения.
//
Функция ЗаписатьНовыйСтатусОтражения(МассивДокументов, НовыйСтатусОтражения, СтарыеСтатусыОтражения) Экспорт
	
	Отказ = Ложь;
	
	БлокировкаДанных = Новый БлокировкаДанных();
	Для каждого ДокументРегистратор Из МассивДокументов Цикл
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.ОтражениеДокументовВРеглУчете.НаборЗаписей");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Регистратор", ДокументРегистратор);
	КонецЦикла;
	
	НачатьТранзакцию();
	
	Попытка
		
		БлокировкаДанных.Заблокировать();
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОтражениеДокументовВРеглУчете.Период,
		|	ОтражениеДокументовВРеглУчете.Регистратор,
		|	ОтражениеДокументовВРеглУчете.Организация,
		|	ОтражениеДокументовВРеглУчете.ДатаОтражения,
		|	ВЫБОР КОГДА ОтражениеДокументовВРеглУчете.Статус В (&СтарыеСтатусыОтражения) ТОГДА
		|		&НовыйСтатус 
		|	ИНАЧЕ
		|		ОтражениеДокументовВРеглУчете.Статус
		|	КОНЕЦ КАК Статус,
		|	ОтражениеДокументовВРеглУчете.Комментарий
		|ИЗ
		|	РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументовВРеглУчете
		|ГДЕ
		|	ОтражениеДокументовВРеглУчете.Регистратор В(&МассивДокументов)
		|ИТОГИ ПО
		|	Регистратор";
		Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
		Запрос.УстановитьПараметр("НовыйСтатус", НовыйСтатусОтражения);
		Запрос.УстановитьПараметр("СтарыеСтатусыОтражения", СтарыеСтатусыОтражения);
		
		ВыборкаДокументов = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		НаборЗаписей = РегистрыСведений.ОтражениеДокументовВРеглУчете.СоздатьНаборЗаписей();
		
		Пока ВыборкаДокументов.Следующий() Цикл
			
			НаборЗаписей.Очистить();
			НаборЗаписей.Отбор.Регистратор.Установить(ВыборкаДокументов.Регистратор);
			
			Выборка = ВыборкаДокументов.Выбрать();
			
			Пока Выборка.Следующий() Цикл
				
				ЗаписьОтражения = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(ЗаписьОтражения, Выборка);
			
			КонецЦикла;
			
			НаборЗаписей.Записать();
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		Отказ = Истина;
		
		ШаблонСообщения = НСтр("ru = 'Операция изменения статуса отражения не выполнена по причине:
								|%1';
								|en = 'Cannot change the recording status due to:
								|%1'");
		ОбщегоНазначения.СообщитьПользователю(СтрШаблон(ШаблонСообщения, КраткоеПредставлениеОшибки(ИнформацияОбОшибке())));
		ЗаписьЖурналаРегистрации(НСТр("ru = 'Операция не выполнена';
										|en = 'Transaction is not executed'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,, СтрШаблон(ШаблонСообщения, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())));
		
	КонецПопытки;
	
	Возврат Отказ;
	
КонецФункции

// Проверяет переданные документы на факт отражения ОтразитьДокументов регл. учете при наличии прав на формирование проводок.
//
// Параметры:
//    МассивДокументов - Массив - Документы.
//
// Возвращаемое значение:
//    Массив - Документы, которые не отражены в регл. учете.
//
Функция ПроверитьПраваДоступаОтражениеДокументовВРеглУчете(МассивДокументов) Экспорт
	
	Если ПравоДоступа("Редактирование", Метаданные.РегистрыБухгалтерии.Хозрасчетный)
		И ПолучитьФункциональнуюОпцию("ИспользоватьРеглУчет") Тогда
		Возврат НеотраженныеВРеглУчетеДокументы(МассивДокументов);
	Иначе
		Возврат Новый Массив;
	КонецЕсли;
	
КонецФункции

// Получает результат отражения в регл. учете. Результат помещается во временное хранилище.
//
// Параметры:
//		ПараметрыОтражения - Структура - параметров отражения, где:
//			* Ссылка - ДокументСсылка - документ, для которого надо получить таблицу проводок;
//			* Дата - дата документа;
//			* Организация - ОрганизацияСсылка - организация, по которой получается таблица проводок;
//			* ВыполнитьПересчеты - булево, флажок о том, необходимо ли перед формированием проводок выполнять оффлайновые пересчеты по документу
//			* ДетализацияПроводок - Булево - признак дополнительной детализации проводок по Кт 20-го счета
// 		АдресРезультата - Строка - Адрес временного хранилища для помещения результата.
//
Процедура ВернутьРезультатОтраженияДокумента(ПараметрыОтражения, АдресРезультата) Экспорт
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьРеглУчет") Тогда
		СтруктураВозврата = Новый Структура("ТаблицаПроводок, КомментарийОшибки", Новый ТаблицаЗначений, "");
		ПоместитьВоВременноеХранилище(СтруктураВозврата, АдресРезультата);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ПараметрыОтражения.ВыполнитьПересчеты Тогда
		ВыполнитьОффлайновыеРасчеты(ПараметрыОтражения.Ссылка);
	КонецЕсли;
	
	ВременныеТаблицы = ВременныеТаблицы();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ВременныеТаблицы;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Данные.Регистратор                  КАК Ссылка,
	|	Данные.Период                       КАК Период,
	|	Данные.Организация                  КАК Организация,
	|	Данные.Организация.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	Данные.ДатаОтражения               КАК ДатаОтражения
	|ПОМЕСТИТЬ РазрезыКОтражению
	|ИЗ
	|	РегистрСведений.ОтражениеДокументовВРеглУчете КАК Данные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Константы КАК Константы
	|		ПО (ИСТИНА)
	|ГДЕ
	|	Данные.Регистратор = &Ссылка
	|	И (Данные.ДатаОтражения >= Константы.ДатаНачалаВеденияРеглУчета)
	|;
	|
	|//////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РазрезыКОтражению.Ссылка КАК Ссылка,
	|	РазрезыКОтражению.Период КАК Период
	|ПОМЕСТИТЬ ДокументыКОтражению
	|ИЗ
	|	РазрезыКОтражению КАК РазрезыКОтражению
	|";
	Запрос.УстановитьПараметр("Ссылка",            ПараметрыОтражения.Ссылка);
	ДобавитьВЗапросФильтрОтраженияВРеглУчете(Запрос.Текст, "Данные.Регистратор");
	Запрос.Выполнить();
	
	ТипДокумента = ТипЗнч(ПараметрыОтражения.Ссылка);
	МетаданныеДокумента = Метаданные.НайтиПоТипу(ТипДокумента);
	ИмяДокумента = МетаданныеДокумента.Имя;
	
	ПараметрыОтраженияВРеглУчете = ПараметрыОтраженияВРеглУчете();
	ПараметрыОтраженияВРеглУчете.ДетализацияПроводок = ПараметрыОтражения.ДетализацияПроводок;
	
	Результат = ВыборкиОтраженияДокументов(ИмяДокумента, ВременныеТаблицы, ПараметрыОтраженияВРеглУчете);
	
	СтруктураВозврата = Новый Структура("ТаблицаПроводок, КомментарийОшибки, СтатусОтражения");
	
	Если Результат.ВыборкаСтатусов.Следующий() Тогда
		
		Результат.ВыборкаХозрасчетный.Следующий();
		СтруктураВозврата.ТаблицаПроводок = ТаблицаПроводок();
		ДобавитьСтрокиВТаблицуПроводок(СтруктураВозврата.ТаблицаПроводок, Результат.ВыборкаХозрасчетный);
		Результат.ВыборкаХозрасчетныйСторно.Следующий();
		ДобавитьСтрокиВТаблицуПроводок(СтруктураВозврата.ТаблицаПроводок, Результат.ВыборкаХозрасчетныйСторно);
		СтруктураВозврата.КомментарийОшибки = "";
		СтруктураВозврата.СтатусОтражения = Перечисления.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВРеглУчете;
		
		Если Результат.ВыборкаПроверки.Следующий() Тогда
			СтруктураВозврата.КомментарийОшибки = КомментарийОшибокЗаполнения(Результат.ВыборкаПроверки);
			СтруктураВозврата.СтатусОтражения = Перечисления.СтатусыОтраженияДокументовВРеглУчете.НеУказаныСчетаУчета;
		КонецЕсли;
		
	КонецЕсли;
	
	ВременныеТаблицы.Закрыть();
	
	ПоместитьВоВременноеХранилище(СтруктураВозврата, АдресРезультата);
	
КонецПроцедуры

// Записывает статус отражения документа в регистре сведений "ОтражениеДокументовВРеглУчете" как ОтраженоВУчетеВручную.
//
//	Параметры:
//		Документ - ДокументСсылка - ссылка на документ, для которого должен быть записан статус ручного отражения. Если ссылка - пустая, записи не происходит.
//		СтатусОтражения - ПеречислениеСсылка.СтатусыОтраженияДокументовВРеглУчете - статус, который будет установлен после выполнения процедуры.
//		Комментарий - строка - дополнительные сведения о ручном отражении документа.
//		ТаблицаОтражения - Таблица значений - если документ ранее не был отражен, выполняет отражение документа по этой таблице. Таблица значений с колонками:
//			* Период - дата отражения,
//			* Регистратор - отражаемый документ,
//			* Организация - организация по которой происходит отражение.
//
Процедура ЗарегистрироватьОтражениеДокумента(Документ, СтатусОтражения, Комментарий = Неопределено, ТаблицаОтражения = Неопределено) Экспорт
	
	Если Документ.Пустая() ИЛИ Не ПолучитьФункциональнуюОпцию("ИспользоватьРеглУчет") Тогда
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Попытка
		
		ПараметрыБлокировки	= Новый Структура("ТипТаблицы, ИмяТаблицы", "РегистрСведений", "ОтражениеДокументовВРеглУчете.НаборЗаписей");
		ЗначенияБлокировки	= Новый Структура("Регистратор", Документ);
		ОбщегоНазначенияБПВызовСервера.УстановитьУправляемуюБлокировку(ПараметрыБлокировки, ЗначенияБлокировки);
		
		ОтражениеДокументовВРегламентированномУчете = РегистрыСведений.ОтражениеДокументовВРеглУчете.СоздатьНаборЗаписей();
		ОтражениеДокументовВРегламентированномУчете.Отбор.Регистратор.Установить(Документ);
		ОтражениеДокументовВРегламентированномУчете.Прочитать();
		
		Если Не ОтражениеДокументовВРегламентированномУчете.Количество() И ТаблицаОтражения <> Неопределено Тогда
			ДатаОтраженияРеглУчета = Константы.ДатаНачалаВеденияРеглУчета.Получить();
			Для каждого СтрокаИсточник из ТаблицаОтражения Цикл
				СтрокаПриемник = ОтражениеДокументовВРегламентированномУчете.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаПриемник, СтрокаИсточник);
				СтрокаПриемник.ДатаОтражения = ?(СтрокаИсточник.Период >= ДатаОтраженияРеглУчета, СтрокаИсточник.Период, ДатаОтраженияРеглУчета);
			КонецЦикла;
		КонецЕсли;
		
		Для каждого Запись Из ОтражениеДокументовВРегламентированномУчете Цикл
			
			Запись.Статус = СтатусОтражения;
			
			Если Не Запись.Комментарий = Комментарий Тогда
				Запись.Комментарий = Комментарий;
			КонецЕсли;
			
		КонецЦикла;
		
		ОтражениеДокументовВРегламентированномУчете.Записать();
	
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ОшибкаКратко = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ОшибкаПодробно = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ТекстОшибки = НСтр("ru = 'Не удалось установить статус отражения в учете ""%1"" для документа ""%2"" по причине: %3';
							|en = 'Cannot set the recording status in the ""%1"" accounting for the ""%2"" document due to: %3'");
		ОписаниеОшибки = СтрШаблон(ТекстОшибки, СтатусОтражения, Документ, ОшибкаПодробно);
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Установка нового статуса отражения в учете';
				|en = 'Set new status of recording in accounting'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,
			Документ.Метаданные(),
			Документ,
			ОписаниеОшибки);
		ВызватьИсключение СтрШаблон(ТекстОшибки, СтатусОтражения, Документ, ОшибкаКратко);
			
	КонецПопытки;
	
КонецПроцедуры

// Получает данные об отражении документа в регламентированном учете. Причем получает в определенном порядке. 
//
//	Параметры:
//		Документ - ДокументСсылка - ссылка на документ, для которого необходимо получить данные отражения.
//		Организация - СправочникСсылка.Организации, Массив Из СправочникСсылка.Организации, Неопределено -
//
//	Возвращаемое значение:
//		Структура - содержит следующие данные:
//			* Статус - ПеречислениеСсылка.СтатусыОтраженияДокументовВРеглУчете - текущий статус отражения документа;
//			* Отражен - Булево - признак отражения документа, истина если Статус = ОтраженоВРеглУчете ИЛИ Статус = ОтраженоВУчетеВручную;
//			* РучноеОтражение - Булево - признак ручного отражения документа, истина если Статус = ОтраженоВУчетеВручную ИЛИ Статус = КОтражениюВУчетеВручную;
//			* Комментарий - Строка - дополнительная информация по отражению документа. Заполняется пользователем если отражение ручное или содержит перечень ошибок, если не удалось отразить документ автоматически.
//
Функция ПолучитьДанныеОтраженияДокумента(Документ, Организация = Неопределено) Экспорт
	
	СтруктураВозврата = Новый Структура(
		"Статус, Отражен, РучноеОтражение, Комментарий", 
		Перечисления.СтатусыОтраженияДокументовВРеглУчете.ПустаяСсылка(), Ложь, Ложь);
	
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
		|	ОтражениеДокументовВРеглУчете.Статус,
		|	ВЫБОР
		|		КОГДА ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.НеУказаныСчетаУчета)
		|			ТОГДА 1
		|		КОГДА ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВУчетеВручную)
		|			ТОГДА 2
		|		КОГДА ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВУчетеВручную)
		|			ТОГДА 3
		|		КОГДА ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВРеглУчете)
		|			ТОГДА 4
		|		КОГДА ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВРеглУчете)
		|			ТОГДА 5
		|	КОНЕЦ КАК Приоритет,
		|	ОтражениеДокументовВРеглУчете.Комментарий,
		|	ВЫБОР
		|		КОГДА ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВУчетеВручную)
		|				ИЛИ ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВРеглУчете)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Отражен,
		|	ВЫБОР
		|		КОГДА ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВУчетеВручную)
		|				ИЛИ ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВУчетеВручную)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК РучноеОтражение
		|ИЗ
		|	РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументовВРеглУчете
		|ГДЕ
		|	ОтражениеДокументовВРеглУчете.Регистратор = &Документ
		|	И (&ВсеОрганизации ИЛИ ОтражениеДокументовВРеглУчете.Организация В (&Организация))
		|
		|УПОРЯДОЧИТЬ ПО
		|	Приоритет";
		Запрос.УстановитьПараметр("Документ", Документ);
		Запрос.УстановитьПараметр("Организация", Организация);
		Запрос.УстановитьПараметр("ВсеОрганизации", НЕ ЗначениеЗаполнено(Организация));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(СтруктураВозврата, Выборка);
	КонецЕсли;
	
	Возврат СтруктураВозврата;
	
КонецФункции

// Возвращает составной комментарий к неотраженному в регл. учете документу
//
// Параметры:
// 	Документ - ДокументСсылка - Документ, по которому необходимо получить комментарий
// 	Организация - СправочникСсылка.Организации, Массив Из СправочникСсылка.Организации - Дополнительный отбор, если необходимо получить комментарий отражения по конкретной организации
// Возвращаемое значение:
// 	 Строка - составной комментарий к неотраженному в учете документу.
//
Функция СводныйКомментарийПоДокументу(Документ, Организация = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Записи.Комментарий КАК Комментарий
	|ИЗ
	|	РегистрСведений.ОтражениеДокументовВРеглУчете КАК Записи
	|ГДЕ
	|	Записи.Регистратор = &Документ
	|	И (Записи.Организация В (&Организация )
	|		ИЛИ &ВсеОрганизации)
	|";
	Запрос.УстановитьПараметр("Статус",         Перечисления.СтатусыОтраженияДокументовВРеглУчете.НеУказаныСчетаУчета);
	Запрос.УстановитьПараметр("Документ",       Документ);
	Запрос.УстановитьПараметр("Организация",    Организация);
	Запрос.УстановитьПараметр("ВсеОрганизации", НЕ ЗначениеЗаполнено(Организация));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Подстроки = Новый Массив;
	Пока Выборка.Следующий() Цикл
		Если Выборка.Комментарий = "" Тогда
			Продолжить;
		КонецЕсли;
		Если Подстроки.Найти(Выборка.Комментарий) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Подстроки.Добавить(Выборка.Комментарий);
	КонецЦикла;
	
	Результат = СтрСоединить(Подстроки, Символы.ПС);
	
	Возврат Результат;
	
КонецФункции

// Функция-конструктор таблицы выборочной регистрации к отражению.
//
// Возвращаемое значение:
//  ТаблицаЗначений - таблица с колонками:
//     * Организация - СправочникСсылка.Организации - организация
//     * ДатаОтражения - Дата - дата отражения.
//
Функция ТаблицаВыборочнойРегистрацииКОтражению() Экспорт
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Организация",     Новый ОписаниеТипов("СправочникСсылка.Организации"));
	Таблица.Колонки.Добавить("ДатаОтражения",   Новый ОписаниеТипов("Дата"));
	
	Возврат Таблица;
	
КонецФункции

// Устанавливает параметры выборочной регистрации документа к отражению в регл. учете.
// 
// Параметры:
//   Таблица     - ТаблицаЗначений - таблица выборочной регистрации к отражению,
//                 см. РеглУчетПроведениеСервер.ТаблицаВыборочнойРегистрацииКОтражению
//   Организация - СправочникСсылка.Организации - организация для выборочной регистрации к отражению
//   Период      - Дата - дата выборочной регистрации к отражению в регл. учете.
//
Процедура ДобавитьПараметрыВыборочнойРегистрацииКОтражениюВРеглУчете(Таблица, Организация, Период) Экспорт
	
	Отбор = Новый Структура;
	Отбор.Вставить("Организация",   Организация);
	Отбор.Вставить("ДатаОтражения", НачалоДня(Период));
	
	РезультатПоиска = Таблица.НайтиСтроки(Отбор);
	
	Если РезультатПоиска.Количество() = 0 Тогда
		НоваяСтрока = Таблица.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Отбор);
	КонецЕсли;
	
КонецПроцедуры

// Отключает регистрацию документа к отражению в регл. учете 
// 
// Параметры:
// 	 ДопСвойства - Структура - Дополнительные свойства объекта.
//
Процедура НеРегистрироватьКОтражениюВРеглУчете(ДопСвойства) Экспорт
	
	ДопСвойства.Вставить("НеРегистрироватьКОтражениюВРеглУчете", Истина);
	
КонецПроцедуры

// Определяет, должен ли документ отражаться в регл. учете или нет (по дате и дополнительным условиям:
//	некоторые документы ввод остатков, операция бух. - не зависят от даты начала ведения регл. учета).
//
//	Параметры:
//		Документ - ДокументСсылка - документ, для которого определяется возможность отражения в регл. учете;
//		ДатаДокумента - Дата - дата отражения документа, сравнивается с датой начала ведения регл. учета;
//		ДокументОснование - ДокументСсылка - если документ основания принадлежит к списку документов исключений, то и
//			текущий документ будет подпадать под исключение;
//
//	Возвращаемое значение:
//		Булево - отражается / не отражается в учете.
//
Функция ДокументОтражаетсяВРеглУчете(Документ, ДатаДокумента = Неопределено, ДокументОснование = Неопределено) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьРеглУчет") Тогда
		Возврат Ложь;
	КонецЕсли;
		
	МассивИсключений = ИменаДокументовОтражаемыеВРеглУчетеВнеЗависимостиОтДаты();
	
	Для каждого ИмяДокументаИсключения Из МассивИсключений Цикл
		Если ТипЗнч(Документ) = Тип("ДокументСсылка."+ИмяДокументаИсключения) Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	ДокументыЗависимыеОтОснования = ДокументыОтражаемыеВРеглУчетеВнеЗависимостиОтДатыЗависящиеОтОснования();
	
	РеквизитыДокумента = Новый Структура("Дата, Основание");
	РеквизитыДляПолучения = Новый Структура;
	Если ДатаДокумента <> Неопределено Тогда
		РеквизитыДокумента.Вставить("Дата", ДатаДокумента);
	Иначе
		РеквизитыДляПолучения.Вставить("Дата");
	КонецЕсли;
	
	МетаданныеДокумента = Документ.Метаданные();
	Если ДокументыЗависимыеОтОснования.Получить(МетаданныеДокумента.Имя) <> Неопределено Тогда
		Если ДокументОснование <> Неопределено Тогда
			РеквизитыДокумента.Вставить("Основание", ДокументОснование);
		ИначеЕсли МетаданныеДокумента.Реквизиты.Найти("ДокументОснование") <> Неопределено Тогда
			РеквизитыДляПолучения.Вставить("Основание", "ДокументОснование");
		ИначеЕсли МетаданныеДокумента.Реквизиты.Найти("Основание") <> Неопределено Тогда
			РеквизитыДляПолучения.Вставить("Основание", "Основание");
		КонецЕсли;
	КонецЕсли;
	Если РеквизитыДляПолучения.Количество() Тогда
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Документ, РеквизитыДляПолучения);
		ЗаполнитьЗначенияСвойств(РеквизитыДокумента, ЗначенияРеквизитов);
	КонецЕсли;
	
	Если ДокументыЗависимыеОтОснования.Получить(МетаданныеДокумента.Имя) <> Неопределено Тогда
		Для каждого ИмяДокументаИсключения Из МассивИсключений Цикл
			Если ТипЗнч(РеквизитыДокумента.Основание) = Тип("ДокументСсылка."+ИмяДокументаИсключения) Тогда
				Возврат Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат ?(РеквизитыДокумента.Дата < Константы.ДатаНачалаВеденияРеглУчета.Получить(), Ложь, Истина);
	
КонецФункции

// Дополняет текст запроса дополнительными условиями по типу документов, которые могут отражаться в регл. учете
//	вне зависимости от даты начала отражения в регл. учете, учитываются так же и документы основания.
//
//	Параметры:
//		ТекстЗапроса - Строка - текст запроса, который будет обрабатываться, необходимо, чтобы в нем присутствовало условие на дату начала ведения регл. учета;
//		ИмяПоляДокумента - Строка - строка вида "Данные.Документ", где данные - таблица, для которой накладываются условия, документ - имя поля документа.
//
Процедура ДобавитьВЗапросФильтрОтраженияВРеглУчете(ТекстЗапроса, ИмяПоляДокумента) Экспорт
	
	Т = УниверсальныеТекстыЗамены();
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, Т.ПодстрокаЗаменыДопСоединений, Т.ТекстДопСоединений + Т.ПодстрокаЗаменыДопСоединений);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, Т.ПодстрокаЗаменыДопУсловий, Т.ПодстрокаЗаменыДопУсловий + Т.ТекстДопУсловий);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяПоляДокумента%", ИмяПоляДокумента);
	
КонецПроцедуры

// Регистрирует документы расчетов с партнерами к отражению в регламентированном учете.
//
// Параметры:
//	Расчеты - ТаблицаЗначений - Таблица движений по расчетам с партнерами или менеджер временных таблиц имеющий таблицу РасчетыКОтражениюВУчете.
//	                         Таблица должна содержать колонки:
//	                         * Регистратор - ДокументСсылка - Документ-регистратор движений
//	                         * Период - Дата - Период движений
//	                         * АналитикаУчетаПоПартнерам - СправочникСсылка.КлючиАналитикиУчетаПоПартнерам - Аналитика учета.
//
Процедура ЗарегистрироватьДокументыРасчетовСПартнерамиКОтражениюВРеглУчете(Расчеты) Экспорт
	
	Запрос = Новый Запрос;
	МассивТекстовЗапроса = Новый Массив;
	Если ТипЗнч(Расчеты) = Тип("ТаблицаЗначений") Тогда
		Если Расчеты.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДокументыКОтражению.Регистратор КАК Регистратор,
		|	ДокументыКОтражению.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	ДокументыКОтражению.Период КАК Период
		|ПОМЕСТИТЬ РасчетыКОтражениюВУчете
		|ИЗ
		|	&ДокументыКОтражению КАК ДокументыКОтражению
		|ГДЕ
		|	ДокументыКОтражению.Регистратор <> НЕОПРЕДЕЛЕНО
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	АналитикаУчетаПоПартнерам";
		Запрос.УстановитьПараметр("ДокументыКОтражению", Расчеты);
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	Иначе
		Запрос.МенеджерВременныхТаблиц = Расчеты;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Расчеты.Регистратор                        КАК Документ,
	|	КлючиАналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	НАЧАЛОПЕРИОДА(Расчеты.Период, ДЕНЬ)        КАК ДатаОтражения
	|ПОМЕСТИТЬ ДокументыКОтражению
	|ИЗ
	|	РасчетыКОтражениюВУчете КАК Расчеты
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК КлючиАналитикаУчетаПоПартнерам
	|	ПО
	|		Расчеты.АналитикаУчетаПоПартнерам = КлючиАналитикаУчетаПоПартнерам.КлючАналитики
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КонтрагентКонтрагент.Регистратор                 КАК Документ,
	|	КонтрагентКонтрагент.Организация                 КАК Организация,
	|	НАЧАЛОПЕРИОДА(КонтрагентКонтрагент.Период, ДЕНЬ) КАК ДатаОтражения
	|ИЗ
	|	РегистрНакопления.ДвиженияКонтрагентКонтрагент КАК КонтрагентКонтрагент
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РасчетыКОтражениюВУчете КАК Расчеты
	|	ПО
	|		Расчеты.Регистратор = КонтрагентКонтрагент.Регистратор
	|ГДЕ
	|	КонтрагентКонтрагент.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВзаимозачетЗадолженности)
	|;";
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	Запрос.Текст = СтрСоединить(МассивТекстовЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	Запрос.Выполнить();
	ВернутьДокументыКОтражению(Запрос.МенеджерВременныхТаблиц);
	ОбщегоНазначенияУТ.УничтожитьВременныеТаблицы(Запрос.МенеджерВременныхТаблиц, "ДокументыКОтражению");
	
КонецПроцедуры
	
#Область НазначениеПараметровОтраженияРеглУчета

// Дополняет параметры запроса отражения документов в регл. учете значениями, необходимыми для документов
// последовательного отражения:
//		Ссылка - ДокументСсылка - ссылка на отражаемый документ;
//		Дата - Дата - период отражения документа;
//		ГраницаМесяцНачало - Граница - начало месяца отражения документа;
//		ГраницаМесяцОкончание - Граница - конец месяца отражения документа;
//		ВидыСубконтоХозрасчетныеОсновныеСредстваКонтрагенты - Массив, состоящий из значений типа "ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные";
//
//	Параметры:
//		ПараметрыЗапроса - Структура - структура параметров для заполнения (см. РеглУчетВыборкиСерверПовтИсп.ПараметрыОтраженияРеглУчетаПоУмолчанию);
//		Ссылка - ДокументСсылка - ссылка на отражаемый документ;
//		Период - Дата - период отражения документа;
//
Процедура ДополнитьПараметрыОтраженияРеглУчетаДляДокументовПоследовательногоОтражения(ПараметрыЗапроса, Ссылка, Период) Экспорт
	
	ПараметрыЗапроса.Вставить("Ссылка", Ссылка);
	ПараметрыЗапроса.Вставить("Дата",   Период);
	
	ГраницаМесяцНачало = Новый Граница(НачалоМесяца(Период), ВидГраницы.Включая);
	ГраницаМесяцОкончание = Новый Граница(КонецМесяца(Период), ВидГраницы.Включая);
	ПараметрыЗапроса.Вставить("ГраницаМесяцНачало",    ГраницаМесяцНачало);
	ПараметрыЗапроса.Вставить("ГраницаМесяцОкончание", ГраницаМесяцОкончание);
	
	МассивВидовСубконто = Новый Массив;
	МассивВидовСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства);
	МассивВидовСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	ПараметрыЗапроса.Вставить("ВидыСубконтоХозрасчетныеОсновныеСредстваКонтрагенты", МассивВидовСубконто);
		
КонецПроцедуры

//	На основании переданных значений параметров заполняет параметры запроса данных.
//
//	Параметры:
//		ЗапросДанных - Запрос - запрос, параметры которого будут заполнены;
//		ЗначенияПараметровДляЗаполнения - Структура - содержит значения по умолчанию ((см. РеглУчетВыборкиСерверПовтИсп.ПараметрыОтраженияРеглУчетаПоУмолчанию);
//		ПараметрыЗапроса - коллекция параметров запроса - список параметров, которые будут заполнены, если не определен - будет заполнен на основании параметров запроса.
//
Процедура ЗаполнениеПараметровЗапросаИПрочихФункциональныхОпций(ЗапросДанных, ЗначенияПараметровДляЗаполнения, ПараметрыЗапроса = Неопределено) Экспорт
	
	Если ПараметрыЗапроса = Неопределено Тогда
		ПараметрыЗапроса = ЗапросДанных.НайтиПараметры();
	КонецЕсли;
	
	Для Каждого ПараметрДанных Из ПараметрыЗапроса Цикл
		ЗначениеПараметра = Неопределено;
		Если Не ЗначенияПараметровДляЗаполнения.Свойство(ПараметрДанных.Имя, ЗначениеПараметра) Тогда
			ЗначениеПараметра = ПолучитьФункциональнуюОпцию(ПараметрДанных.Имя);
		КонецЕсли;
		ЗапросДанных.УстановитьПараметр(ПараметрДанных.Имя, ЗначениеПараметра);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает прогнозные параметры отражения в регламентированном учете сводно и в разрезе типов документов
// 
// Возвращаемое значение:
// 	Структура - Прогнозные параметры отражения:
// 		* ДатаПервойПроводки - Дата - Дата самой ранней формируемой проводки
// 		* КоличествоДокументов - Число - Общее количество отражаемых документов.
// 		* КоличествоПроводок - Число - Общее количество фомируемых проводок.
// 		* ПроводокНаПорцию - Число - Количество проводок, формируемых в одном потоке.
// 		* ПоТипамДокументов - Массив из Структура - аналогичные данные по типам документов. Свойства структуры:
// 			** ТипДокумента - Тип - Тип документа
// 			** КоличествоДокументов - Число - Общее количество отражаемых документов данного типа
// 			** КоличествоПроводок - Число - Общее количество фомируемых проводок по документам данного типа
// 	
Функция ПрогнозныеПараметрыОтраженияВРеглУчете() Экспорт
	
	ПрогнозныеПараметры = Новый Структура();
	ПрогнозныеПараметры.Вставить("ДатаПервойПроводки", ДобавитьМесяц(НачалоМесяца(ТекущаяДатаСеанса()), 1));
	ПрогнозныеПараметры.Вставить("КоличествоДокументов", 0);
	ПрогнозныеПараметры.Вставить("КоличествоПроводок", 0);
	ПрогнозныеПараметры.Вставить("ПроводокНаПорцию", 0);
	ПрогнозныеПараметры.Вставить("ПоТипамДокументов", Новый Массив());
	
	Возврат ПрогнозныеПараметры;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Выполняет обработку установки даты начала ведения УУ на плане счетов Хозрасчетный:
// - возвращает документы к отражению в регл. учете начиная с даты начала ведения УУ
// - очищает ресурс СуммаУУ в движениях регистра Хозрасчетный ранее даты начала ведения УУ.
//
Процедура ОбработкаУстановкиДатыНачалаУУНаПланеСчетовХозрасчетный(Параметры, АдресХранилища) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДатаНачалаУУ = Константы.ДатаНачалаУУНаПланеСчетовХозрасчетный.Получить();
	
	Результат = Новый Структура;
	Результат.Вставить("ВозвращеноКОтражению", 0);
	Результат.Вставить("ОчищенаСуммаУУ", 0);
	
	НачатьТранзакцию();
	Попытка
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОтражениеДокументовВРеглУчете.Регистратор
		|ИЗ
		|	РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументовВРеглУчете
		|ГДЕ
		|	ОтражениеДокументовВРеглУчете.ДатаОтражения >= &ДатаНачалаУУ
		|	И НЕ ОтражениеДокументовВРеглУчете.Статус В (
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВРеглУчете),
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВУчетеВручную))
		|;
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Хозрасчетный.Регистратор
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
		|ГДЕ
		|	Хозрасчетный.Период < &ДатаНачалаУУ
		|	И Хозрасчетный.СуммаУУ <> 0
		|	И НЕ ТИПЗНАЧЕНИЯ(Хозрасчетный.Регистратор) В (
		|			ТИП(Документ.ВводОстатков),
		|			ТИП(Документ.ВводОстатковВзаиморасчетов),
		|			ТИП(Документ.ВводОстатковДенежныхСредств),
		|			ТИП(Документ.ВводОстатковНДСПредъявленного),
		|			ТИП(Документ.ВводОстатковОПродажахЗаПрошлыеПериоды),
		|			ТИП(Документ.ВводОстатковПоФинансовымИнструментам),
		|			ТИП(Документ.ВводОстатковПрочиеРасходы),
		|			ТИП(Документ.ВводОстатковПрочихАктивовПассивов),
		//++ НЕ УТ
		|			ТИП(Документ.ВводОстатковРасходовПриУСН),
		//-- НЕ УТ
		|			ТИП(Документ.ВводОстатковРасчетовПоЭквайрингу),
		|			ТИП(Документ.ВводОстатковСПодотчетниками),
		|			ТИП(Документ.ВводОстатковТМЦВЭксплуатации),
		|			ТИП(Документ.ВводОстатковТоваров),
		|			ТИП(Документ.ОперацияБух))
		|";
		Запрос.УстановитьПараметр("ДатаНачалаУУ", ДатаНачалаУУ); 
		РезультатЗапроса = Запрос.ВыполнитьПакет();
		
		ОтражениеВРеглУчете = РезультатЗапроса[0];
		ВыборкаОтражениеВРеглУчете = ОтражениеВРеглУчете.Выбрать();
		КоличествоВозвращеноКОтражению = ВыборкаОтражениеВРеглУчете.Количество();
		
		Хозрасчетный = РезультатЗапроса[1];
		ВыборкаХозрасчетный = Хозрасчетный.Выбрать();
		КоличествоОчищенаСуммаУУ = ВыборкаХозрасчетный.Количество();

		Блокировка = Новый БлокировкаДанных;
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОтражениеДокументовВРеглУчете.НаборЗаписей");
		ЭлементБлокировки.ИсточникДанных = ОтражениеВРеглУчете;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Регистратор", "Регистратор");
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Хозрасчетный.НаборЗаписей");
		ЭлементБлокировки.ИсточникДанных = Хозрасчетный;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Регистратор", "Регистратор");
		
		Блокировка.Заблокировать();
		
		// Вернем документы к отражению в регл. учете
		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ОтражениеДокументовВРеглУчете.Регистратор КАК Документ,
		|	ОтражениеДокументовВРеглУчете.Организация КАК Организация,
		|	ОтражениеДокументовВРеглУчете.ДатаОтражения КАК ДатаОтражения
		|ПОМЕСТИТЬ ДокументыКОтражению
		|ИЗ
		|	РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументовВРеглУчете
		|ГДЕ
		|	ОтражениеДокументовВРеглУчете.ДатаОтражения >= &ДатаНачалаУУ
		|";
		Запрос.УстановитьПараметр("ДатаНачалаУУ", ДатаНачалаУУ); 
		Запрос.Выполнить();
		
		РеглУчетСервер.ВернутьДокументыКОтражению(МенеджерВременныхТаблиц);
		Результат.ВозвращеноКОтражению = КоличествоВозвращеноКОтражению;
		
		// Очистим ресурс СуммаУУ
		Пока ВыборкаХозрасчетный.Следующий() Цикл
			
			НаборЗаписей = РегистрыБухгалтерии.Хозрасчетный.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(ВыборкаХозрасчетный.Регистратор);
			НаборЗаписей.Прочитать();
			
			Для каждого Запись Из НаборЗаписей Цикл
				Если Запись.СуммаУУ <> 0 Тогда
					Запись.СуммаУУ = 0;
				КонецЕсли;
			КонецЦикла;
			
			НаборЗаписей.ОбменДанными.Загрузка = Истина;
			НаборЗаписей.Записать();
			
		КонецЦикла;
		Результат.ОчищенаСуммаУУ = КоличествоОчищенаСуммаУУ;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось вернуть документы к отражению по причине: %1';
										|en = 'Cannot return documents for record due to: %1'"),
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Установка даты начала УУ на плане счетов Хозрасчетный.';
					|en = 'Set the start date of MA on the Self-financing chart of accounts.'", ОбщегоНазначения.КодОсновногоЯзыка()), 
				УровеньЖурналаРегистрации.Ошибка, , , ТекстСообщения);
				
		ВызватьИсключение ТекстСообщения;
		
	КонецПопытки;
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫРАЗИТЬ(ДокументыКОтражению.Документ КАК Документ.СчетФактураВыданныйАванс).ДокументОснование КАК Документ, 
	|	ВЫРАЗИТЬ(ДокументыКОтражению.Документ КАК Документ.СчетФактураВыданныйАванс).Контрагент КАК Контрагент,
	|	ДокументыКОтражению.ДатаОтражения КАК Период 
	|ИЗ 
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|ГДЕ ТИПЗНАЧЕНИЯ(ДокументыКОтражению.Документ) = ТИП(Документ.СчетФактураВыданныйАванс)";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Документы.СчетФактураВыданныйАванс.СформироватьЗаданияКФормированиюДвиженийПоНДС(РезультатЗапроса.Выгрузить());
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫРАЗИТЬ(ДокументыКОтражению.Документ КАК Документ.СчетФактураПолученныйАванс).ДокументОснование КАК Документ, 
	|	ВЫРАЗИТЬ(ДокументыКОтражению.Документ КАК Документ.СчетФактураПолученныйАванс).Контрагент КАК Контрагент,
	|	ДокументыКОтражению.ДатаОтражения КАК Период 
	|ИЗ 
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|ГДЕ ТИПЗНАЧЕНИЯ(ДокументыКОтражению.Документ) = ТИП(Документ.СчетФактураПолученныйАванс)";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Документы.СчетФактураПолученныйАванс.СформироватьЗаданияКФормированиюДвиженийПоНДС(РезультатЗапроса.Выгрузить());
	КонецЕсли;
		
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫРАЗИТЬ(ДокументыКОтражению.Документ КАК Документ.СчетФактураНалоговыйАгент).ДокументОснование КАК Документ, 
	|	ВЫРАЗИТЬ(ДокументыКОтражению.Документ КАК Документ.СчетФактураНалоговыйАгент).Поставщик КАК Контрагент,
	|	ДокументыКОтражению.ДатаОтражения КАК Период 
	|ИЗ 
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|ГДЕ ТИПЗНАЧЕНИЯ(ДокументыКОтражению.Документ) = ТИП(Документ.СчетФактураНалоговыйАгент)";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Документы.СчетФактураНалоговыйАгент.СформироватьДвиженияНДС(РезультатЗапроса.Выгрузить());
	КонецЕсли;
		
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);
	
КонецПроцедуры

// Восстановление партий, расчетов по документам, сумм в валюте регл., расчет себестоимости.
Процедура ВыполнитьОффлайновыеРасчеты(Документы = Неопределено)
	
	Если Документы = Неопределено Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ОтражениеДокументовВРеглУчете.Регистратор КАК Документ
		|ИЗ
		|	РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументовВРеглУчете
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Константы КАК Константы
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ОтражениеДокументовВРеглУчете.Статус В (&СтатусыОтражения)
		|	И (ОтражениеДокументовВРеглУчете.ДатаОтражения >= Константы.ДатаНачалаВеденияРеглУчета)
		|";
		
		ДобавитьВЗапросФильтрОтраженияВРеглУчете(Запрос.Текст, "ОтражениеДокументовВРеглУчете.Регистратор");
		
		СтатусыОтражения = Новый Массив;
		СтатусыОтражения.Добавить(Перечисления.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВРеглУчете);
		СтатусыОтражения.Добавить(Перечисления.СтатусыОтраженияДокументовВРеглУчете.НеУказаныСчетаУчета);
		// Возможна ситуация когда ФО ручного отражения снята, но документы отраженные вручную есть - для них тоже надо делать отражение:
		Если Не ПолучитьФункциональнуюОпцию("ИспользоватьРучнуюКорректировкуПроводокПоРеглУчету") Тогда
			СтатусыОтражения.Добавить(Перечисления.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВУчетеВручную);
			СтатусыОтражения.Добавить(Перечисления.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВУчетеВручную);
		КонецЕсли;
		
		Запрос.УстановитьПараметр("СтатусыОтражения", СтатусыОтражения);
		РезультатЗапроса = Запрос.Выполнить();
		
		МассивДокументов = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Документ");
	Иначе
		МассивДокументов = ОбщегоНазначенияУТКлиентСервер.Массив(Документы);
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	МАКСИМУМ(Данные.ДатаОтражения) КАК Период
		|ИЗ
		|	РегистрСведений.ОтражениеДокументовВРеглУчете КАК Данные
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Константы КАК Константы
		|		ПО (ИСТИНА)
		|ГДЕ
		|	Данные.Регистратор В (&МассивСсылок)
		|	И (Данные.ДатаОтражения >= Константы.ДатаНачалаВеденияРеглУчета)
		|ИМЕЮЩИЕ НЕ МАКСИМУМ(Данные.ДатаОтражения) ЕСТЬ NULL
		|;
		|
		|//////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщиками КАК Расчеты
		|ГДЕ
		|	Расчеты.Регистратор В (&МассивСсылок)
		|	И Расчеты.Активность
		|;
		|
		|//////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентами КАК Расчеты
		|ГДЕ
		|	Расчеты.Регистратор В (&МассивСсылок)
		|	И Расчеты.Активность
		|";
		
		ДобавитьВЗапросФильтрОтраженияВРеглУчете(Запрос.Текст, "Данные.Регистратор");
		
		Запрос.УстановитьПараметр("МассивСсылок", МассивДокументов);
		Результат = Запрос.ВыполнитьПакет();
		
		ВыборкаПериодРасчета = Результат[0].Выбрать();
		Если ВыборкаПериодРасчета.Следующий() Тогда
			ПериодРасчета = КонецМесяца(ВыборкаПериодРасчета.Период) + 1;
		Иначе
			// Переданные документы не требуют отражения, расчеты не выполняем
			Возврат;
		КонецЕсли;
		
		МассивАналитикПоставщиков = Результат[1].Выгрузить().ВыгрузитьКолонку("АналитикаУчетаПоПартнерам");
		Если МассивАналитикПоставщиков.Количество() > 0 Тогда
			АналитикиРасчета = РаспределениеВзаиморасчетовВызовСервера.АналитикиРасчета();
			АналитикиРасчета.АналитикиУчетаПоПартнерам = МассивАналитикПоставщиков;
			РаспределениеВзаиморасчетовВызовСервера.РаспределитьВсеРасчетыСПоставщиками(ПериодРасчета, АналитикиРасчета);
		КонецЕсли;
		
		МассивАналитикКлиентов = Результат[2].Выгрузить().ВыгрузитьКолонку("АналитикаУчетаПоПартнерам");
		Если МассивАналитикКлиентов.Количество() > 0 Тогда
			АналитикиРасчета = РаспределениеВзаиморасчетовВызовСервера.АналитикиРасчета();
			АналитикиРасчета.АналитикиУчетаПоПартнерам = МассивАналитикКлиентов;
			РаспределениеВзаиморасчетовВызовСервера.РаспределитьВсеРасчетыСКлиентами(ПериодРасчета, АналитикиРасчета);
		КонецЕсли;
	КонецЕсли;
	
	Если МассивДокументов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПроцедурыРасчетов = Новый ТаблицаЗначений();
	ПроцедурыРасчетов.Колонки.Добавить("ИмяМетода", ОбщегоНазначения.ОписаниеТипаСтрока(250));
	ПроцедурыРасчетов.Колонки.Добавить("МногопоточноеВыполнение", Новый ОписаниеТипов("Булево"));
	
	ПроцедураРасчета = ПроцедурыРасчетов.Добавить();
	ПроцедураРасчета.ИмяМетода = "РассчитатьСуммыДокументовВВалютахУчета";
	ПроцедураРасчета.МногопоточноеВыполнение = Истина;
	
	ПроцедураРасчета = ПроцедурыРасчетов.Добавить();
	ПроцедураРасчета.ИмяМетода = "СформироватьДвиженияПоВНА";
	ПроцедураРасчета.МногопоточноеВыполнение = Ложь;
	
	ПроцедураРасчета = ПроцедурыРасчетов.Добавить();
	ПроцедураРасчета.ИмяМетода = "ОтразитьДокументыВУчетеНДС";
	ПроцедураРасчета.МногопоточноеВыполнение = Истина;
	
	Для Каждого ПроцедураРасчета Из ПроцедурыРасчетов Цикл
		ВыполнитьОффлайновыйРасчет(МассивДокументов, ПроцедураРасчета);
	КонецЦикла;
	
КонецПроцедуры

Функция ОтразитьДокументыПакетноСЗамеромВремени(ПараметрыОтражения)
	
	КлючеваяОперация = "ОтражениеВРеглУчете.ПакетноеОтражениеДокументов";
	ОписаниеЗамера = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(КлючеваяОперация);
	
	Результат = ОтразитьДокументыПакетно(ПараметрыОтражения);
	
	КоличествоОбработано = 
		Результат.ОтраженоВУчете
		+ Результат.НеУказаныСчетаУчета 
		+ Результат.ОшибкиПриОтражении;
		
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(ОписаниеЗамера, КоличествоОбработано);
	
	Возврат Результат;
	
КонецФункции

Функция ОтразитьДокументыПакетно(ПараметрыОтражения, Знач НомерИтерации = 0)
	
	НомерИтерации = НомерИтерации + 1;
	
	РезультатВозврата = НовыйРезультатОтражения(Ложь);
	
	ПараметрыОтражения.ПакетноеОтражение = Истина;
	
	Если ПараметрыОтражения.ПрогнозныеПараметры = Неопределено Тогда
		ТипыДокументов = ТипыДокументовКПакетномуОтражению(ПараметрыОтражения);
		ЗаполнитьПрогнозныеПараметры(ПараметрыОтражения, ТипыДокументов);
	КонецЕсли;
	
	Если ПараметрыОтражения.ПрогнозныеПараметры.КоличествоПроводок <= ПараметрыОтражения.ПрогнозныеПараметры.ПроводокНаПорцию Тогда
		КоличествоПотоков = 1;
	Иначе
		КоличествоПотоков = ПараметрыОтражения.КоличествоПотоков;
	КонецЕсли;
	
	ОдинТипНаПорцию = КоличествоПотоков = 1;
	
	ТаблицаУправленияПотоками = ТаблицаУправленияПотоками();
	ТаблицаУправленияПорциями = ТаблицаУправленияПорциями(ПараметрыОтражения, ОдинТипНаПорцию);
	
	Если КоличествоПотоков > 1 Тогда
		ПорцииРазныхТипов = ТаблицаУправленияПорциями.Количество() >= КоличествоПотоков;
		МенеджерВременныхТаблиц = Неопределено;
	Иначе
		ПорцииРазныхТипов = Ложь;
		МенеджерВременныхТаблиц = ВременныеТаблицы();
	КонецЕсли;
	
	Если НомерИтерации = 1
	   И РекомендуетсяВыключениеИтоговРегистра(ПараметрыОтражения) Тогда
		ВыключитьИтогиРегистраБухгалтерии(ПараметрыОтражения);
	КонецЕсли;
	
	ИндексПорции = 0;
	
	Пока ТаблицаУправленияПорциями.Количество() > 0 Цикл
		Если ИндексПорции >= ТаблицаУправленияПорциями.Количество() Тогда
			ИндексПорции = 0;
		КонецЕсли;
		
		Если ТаблицаУправленияПорциями.Количество() < КоличествоПотоков Тогда
			ПорцииРазныхТипов = Ложь;
		КонецЕсли;
		
		ПараметрыПорции = ТаблицаУправленияПорциями[ИндексПорции];
		
		Если ПорцииРазныхТипов И ТаблицаУправленияПотоками.Найти(ПараметрыПорции.НомерПорции, "НомерПорции") <> Неопределено Тогда
			ИндексПорции = ИндексПорции + 1;
			Продолжить;
		КонецЕсли;
		
		Если КоличествоПотоков = 1
		 ИЛИ ПараметрыПорции.КоличествоПорций > 1 Тогда
			Если НЕ СформироватьТаблицыОтбораДанных(
				ПараметрыПорции.ТипыДокументов[0],
				МенеджерВременныхТаблиц,
				ПараметрыОтражения,
				ПараметрыПорции) Тогда
				ТаблицаУправленияПорциями.Удалить(ПараметрыПорции);
				Продолжить;
			КонецЕсли;
		Иначе
			ДополнитьПараметрыЕдинственнойПорции(ПараметрыОтражения, ПараметрыПорции);
		КонецЕсли;
		
		Если ПараметрыПорции.ПоследняяПорция
		   И ПараметрыПорции.НачальныйМоментВремени = ПараметрыПорции.КонечныйМоментВремени Тогда
			ТаблицаУправленияПорциями.Удалить(ПараметрыПорции);
			Продолжить;
		КонецЕсли;
		
		Если КоличествоПотоков = 1 Тогда
			РезультатОтражения = ВыполнитьОтражениеПорцииТекущимПотоком(
				ПараметрыПорции.ТипыДокументов[0],
				МенеджерВременныхТаблиц,
				ПараметрыОтражения);
			ДобавитьРезультатОтраженияКВозврату(РезультатВозврата, РезультатОтражения);
		Иначе
			ПараметрыПотока = ТаблицаУправленияПотоками.Добавить();
			ЗаполнитьЗначенияСвойств(ПараметрыПотока, ПараметрыПорции);
			
			ЭтоПоследняяПорция = ПараметрыПорции.ПоследняяПорция И ТаблицаУправленияПорциями.Количество() = 1;
			НачатьОтражениеПорцииНовымПотоком(ПараметрыОтражения, ПараметрыПотока, ЭтоПоследняяПорция);
			
			Если ЭтоПоследняяПорция Тогда
				РезультатОтражения = ПолучитьИзВременногоХранилища(ПараметрыПотока.АдресРезультата);
				ДобавитьРезультатОтраженияКВозврату(РезультатВозврата, РезультатОтражения);
				
				ТаблицаУправленияПотоками.Удалить(ПараметрыПотока);
				ОжидатьЗавершенияВыполнения = Истина;
			Иначе
				ОжидатьЗавершенияВыполнения = ТаблицаУправленияПотоками.Количество() >= КоличествоПотоков;
			КонецЕсли;
			
			Если ОжидатьЗавершенияВыполнения Тогда
				ОжидатьЗавершенияПотоковОтражения(
					ПараметрыОтражения,
					ТаблицаУправленияПотоками,
					РезультатВозврата,
					ЭтоПоследняяПорция);
				ИндексПорции = 0;
			ИначеЕсли НЕ ПараметрыПорции.ПоследняяПорция Тогда
				ИндексПорции = ИндексПорции + 1;
			КонецЕсли;
		КонецЕсли;
		
		Если ПараметрыПорции.ПоследняяПорция Тогда
			ТаблицаУправленияПорциями.Удалить(ПараметрыПорции);
		КонецЕсли;
	КонецЦикла;
	
	Если МенеджерВременныхТаблиц <> Неопределено Тогда
		МенеджерВременныхТаблиц.Закрыть();
	КонецЕсли;
	
	Если ТаблицаУправленияПотоками.Количество() > 0 Тогда
		ОжидатьЗавершенияПотоковОтражения(
			ПараметрыОтражения,
			ТаблицаУправленияПотоками,
			РезультатВозврата,
			Истина);
	КонецЕсли;
	
	Если НомерИтерации < ПараметрыОтражения.КоличествоПопыток Тогда
		ТипыДокументов = ТипыДокументовКПакетномуОтражению(ПараметрыОтражения);
		ЗаполнитьПрогнозныеПараметры(ПараметрыОтражения, ТипыДокументов);
		Если ПараметрыОтражения.ПрогнозныеПараметры.КоличествоДокументов > 0 Тогда
			РезультатОтражения = ОтразитьДокументыПакетно(ПараметрыОтражения, НомерИтерации);
			ДобавитьРезультатОтраженияКВозврату(РезультатВозврата, РезультатОтражения);
		КонецЕсли;
	КонецЕсли;
	
	Если НомерИтерации = 1
	   И ПараметрыОтражения.ВосстановитьСостояниеИтогов Тогда
		ВключитьИтогиРегистраБухгалтерии(ПараметрыОтражения);
	КонецЕсли;
	
	Возврат РезультатВозврата;
	
КонецФункции

Функция ОтразитьДокументыПоследовательноСЗамеромВремени(ПараметрыОтражения)
	
	ОписаниеЗамера = ОценкаПроизводительности.НачатьЗамерДлительнойОперации("ОтражениеВРеглУчете.ДокументыСПоследовательнымОтражением");
	
	Результат = ОтразитьДокументыПоследовательно(ПараметрыОтражения);
	
	КоличествоОбработано = 
		Результат.ОтраженоВУчете
		+ Результат.НеУказаныСчетаУчета 
		+ Результат.ОшибкиПриОтражении;
		
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(ОписаниеЗамера, КоличествоОбработано);
	
	Возврат Результат;
	
КонецФункции

Функция ОтразитьДокументыПоследовательно(ПараметрыОтражения)
	
	РезультатВозврата = НовыйРезультатОтражения(Ложь);
	ВременныеТаблицы = ВременныеТаблицы();
	
	ПараметрыОтражения.ПакетноеОтражение = Истина;
	
	Выборка = ДокументыКПоследовательномуОтражению(ПараметрыОтражения);
	
	Если Не Выборка = Неопределено Тогда
		Пока Выборка.Следующий() Цикл
			ТипДокумента = Выборка.ТипДокумента;
			ПараметрыОтражения.Документы = Выборка.Ссылка;
			
			Если СформироватьТаблицыОтбораДанных(ТипДокумента, ВременныеТаблицы, ПараметрыОтражения) Тогда 
				РезультатОтражения = ВыполнитьОтражение(ТипДокумента, ВременныеТаблицы, ПараметрыОтражения);
				ДобавитьРезультатОтраженияКВозврату(РезультатВозврата, РезультатОтражения);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ВременныеТаблицы.Закрыть();
	
	Возврат РезультатВозврата;
	
КонецФункции

Функция НовыйРезультатОтражения(Подробно = Истина)
	
	Результат = Новый Структура();
	Результат.Вставить("ОтраженоВУчете",      ?(Подробно, Новый Массив(), 0));
	Результат.Вставить("НеУказаныСчетаУчета", ?(Подробно, Новый Массив(), 0));
	Результат.Вставить("ОшибкиПриОтражении",  ?(Подробно, Новый Массив(), 0));
	Результат.Вставить("ОписанияПроблем",     Новый Соответствие());
	
	Возврат Результат;
	
КонецФункции

Процедура ДобавитьРезультатОтраженияКВозврату(РезультатВозврата, РезультатОтражения)
	
	РезультатВозврата.ОтраженоВУчете = РезультатВозврата.ОтраженоВУчете
		+ ?(ТипЗнч(РезультатОтражения.ОтраженоВУчете) = Тип("Массив"),
			РезультатОтражения.ОтраженоВУчете.Количество(),
			РезультатОтражения.ОтраженоВУчете);
	
	РезультатВозврата.НеУказаныСчетаУчета = РезультатВозврата.НеУказаныСчетаУчета
		+ ?(ТипЗнч(РезультатОтражения.НеУказаныСчетаУчета) = Тип("Массив"),
			РезультатОтражения.НеУказаныСчетаУчета.Количество(),
			РезультатОтражения.НеУказаныСчетаУчета);
	
	РезультатВозврата.ОшибкиПриОтражении = РезультатВозврата.ОшибкиПриОтражении
		+ ?(ТипЗнч(РезультатОтражения.ОшибкиПриОтражении) = Тип("Массив"),
			РезультатОтражения.ОшибкиПриОтражении.Количество(),
			РезультатОтражения.ОшибкиПриОтражении);
	
	ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(РезультатВозврата.ОписанияПроблем, РезультатОтражения.ОписанияПроблем, Истина);
	
КонецПроцедуры

Функция ВыполнитьОтражениеПорцииТекущимПотоком(ТипДокумента, ВременныеТаблицы, ПараметрыОтраженияВРеглУчете)
	
	Для НомерПопытки = 1 По ПараметрыОтраженияВРеглУчете.КоличествоПопыток Цикл
		Попытка
			РезультатОтражения = ВыполнитьОтражение(ТипДокумента, ВременныеТаблицы, ПараметрыОтраженияВРеглУчете);
			Прервать;
		Исключение
			Если НомерПопытки = ПараметрыОтраженияВРеглУчете.КоличествоПопыток Тогда
				ВызватьИсключение;
			КонецЕсли;
		КонецПопытки;
	КонецЦикла;
	
	Возврат РезультатОтражения;
	
КонецФункции

Процедура НачатьОтражениеПорцииНовымПотоком(ПараметрыОтраженияВРеглУчете, ПараметрыПотока, ЭтоПоследняяПорция)
	
	ПараметрыОтражения = ПараметрыОтраженияВРеглУчете();
	ЗаполнитьЗначенияСвойств(ПараметрыОтражения, ПараметрыОтраженияВРеглУчете,
		"Организации,ПериодРасчета,ДатаЗапрета,НеотраженныеДокументы,ПакетноеОтражение,ВременныеТаблицыПереданы,СообщатьОбОшибках,ДетализацияПроводок");
	
	ПараметрыПорции = Новый Структура("ТипыДокументов,НачальныйМоментВремени,КонечныйМоментВремени");
	ЗаполнитьЗначенияСвойств(ПараметрыПорции, ПараметрыПотока);
	
	АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено);
	ПараметрыПотока.АдресРезультата = АдресХранилища;
	
	Если ЭтоПоследняяПорция Тогда
		Для НомерПопытки = 1 По ПараметрыОтраженияВРеглУчете.КоличествоПопыток Цикл
			Попытка
				ВыполнитьОтражениеПорцииНовымПотоком(ПараметрыОтражения, ПараметрыПорции, АдресХранилища);
				Прервать;
			Исключение
				Если НомерПопытки = ПараметрыОтраженияВРеглУчете.КоличествоПопыток Тогда
					ВызватьИсключение;
				КонецЕсли;
			КонецПопытки;
		КонецЦикла;
	Иначе
		ИмяМетода = "РеглУчетПроведениеСервер.ВыполнитьОтражениеПорцииНовымПотоком";
		Наименование = НСтр("ru = 'Пакетное отражение документов в регламентированном учете';
							|en = 'Batch local accounting posting'");
		
		ПараметрыВыполнения = Новый Массив();
		ПараметрыВыполнения.Добавить(ПараметрыОтражения);
		ПараметрыВыполнения.Добавить(ПараметрыПорции);
		ПараметрыВыполнения.Добавить(АдресХранилища);
		
		НовоеЗадание = ФоновыеЗадания.Выполнить(ИмяМетода, ПараметрыВыполнения,, Наименование);
		ПараметрыПотока.Идентификатор = НовоеЗадание.УникальныйИдентификатор;
		ПараметрыПотока.КоличествоПопыток = ПараметрыПотока.КоличествоПопыток + 1;
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьОтражениеПорцииНовымПотоком(ПараметрыОтраженияВРеглУчете, ПараметрыПорции, АдресХранилища) Экспорт
	
	РезультатВозврата = НовыйРезультатОтражения(Ложь);
	ВременныеТаблицы = ВременныеТаблицы();
	
	Для Каждого ТипДокумента Из ПараметрыПорции.ТипыДокументов Цикл
		Если СформироватьТаблицыОтбораДанных(
			ТипДокумента,
			ВременныеТаблицы,
			ПараметрыОтраженияВРеглУчете,
			ПараметрыПорции) Тогда
			РезультатОтражения = ВыполнитьОтражение(ТипДокумента, ВременныеТаблицы, ПараметрыОтраженияВРеглУчете);
			ДобавитьРезультатОтраженияКВозврату(РезультатВозврата, РезультатОтражения);
		КонецЕсли;
	КонецЦикла;
	
	ВременныеТаблицы.Закрыть();
	ПоместитьВоВременноеХранилище(РезультатВозврата, АдресХранилища);
	
КонецПроцедуры

Процедура ОжидатьЗавершенияПотоковОтражения(ПараметрыОтраженияВРеглУчете,
	ТаблицаУправленияПотоками, РезультатВозврата, ЗавершениеВсехПотоков)
	
	ВремяОжидания = 60;
	АктивныеЗадания = Новый Массив();
	АварийныеЗадания = Новый Массив();
	ЗавершенныеЗадания = Новый Массив();
	
	Для Каждого ПараметрыПотока Из ТаблицаУправленияПотоками Цикл
		ФоновоеЗадание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(ПараметрыПотока.Идентификатор);
		Если ФоновоеЗадание = Неопределено Тогда
			АварийныеЗадания.Добавить(ПараметрыПотока.Идентификатор);
		ИначеЕсли ФоновоеЗадание.Состояние = СостояниеФоновогоЗадания.Активно Тогда
			АктивныеЗадания.Добавить(ФоновоеЗадание);
		ИначеЕсли ФоновоеЗадание.Состояние = СостояниеФоновогоЗадания.Завершено Тогда
			ЗавершенныеЗадания.Добавить(ПараметрыПотока.Идентификатор);
		Иначе
			АварийныеЗадания.Добавить(ПараметрыПотока.Идентификатор);
		КонецЕсли;
	КонецЦикла;
	
	Пока (ЗавершенныеЗадания.Количество() = 0 ИЛИ ЗавершениеВсехПотоков) И АктивныеЗадания.Количество() > 0 Цикл
		АктивныеЗадания = ФоновыеЗадания.ОжидатьЗавершенияВыполнения(АктивныеЗадания, ВремяОжидания);
		КоличествоЗаданий = АктивныеЗадания.Количество();
		
		Для НомерЗадания = 1 По КоличествоЗаданий Цикл
			ОбратныйИндекс = КоличествоЗаданий - НомерЗадания;
			ФоновоеЗадание = АктивныеЗадания[ОбратныйИндекс];
			
			Если ФоновоеЗадание.Состояние <> СостояниеФоновогоЗадания.Активно Тогда
				Если ФоновоеЗадание.Состояние = СостояниеФоновогоЗадания.Завершено Тогда
					ЗавершенныеЗадания.Добавить(ФоновоеЗадание.УникальныйИдентификатор);
				Иначе
					АварийныеЗадания.Добавить(ФоновоеЗадание.УникальныйИдентификатор);
				КонецЕсли;
				АктивныеЗадания.Удалить(ОбратныйИндекс);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Для Каждого ЗавершенноеЗадание Из ЗавершенныеЗадания Цикл
		ПараметрыПотока = ТаблицаУправленияПотоками.Найти(ЗавершенноеЗадание, "Идентификатор");
		Если ПараметрыПотока <> Неопределено Тогда
			РезультатОтражения = ПолучитьИзВременногоХранилища(ПараметрыПотока.АдресРезультата);
			ДобавитьРезультатОтраженияКВозврату(РезультатВозврата, РезультатОтражения);
			ТаблицаУправленияПотоками.Удалить(ПараметрыПотока);
		КонецЕсли;
	КонецЦикла;
	
	Если АварийныеЗадания.Количество() > 0 Тогда
		ТекстыСообщений = Новый Массив();
		
		Для Каждого АварийноеЗадание Из АварийныеЗадания Цикл
			ПараметрыПотока = ТаблицаУправленияПотоками.Найти(АварийноеЗадание, "Идентификатор");
			
			Если ПараметрыПотока = Неопределено Тогда
			ИначеЕсли ПараметрыПотока.КоличествоПопыток < ПараметрыОтраженияВРеглУчете.КоличествоПопыток Тогда
				НачатьОтражениеПорцииНовымПотоком(ПараметрыОтраженияВРеглУчете, ПараметрыПотока, Ложь);
			Иначе
				ФоновоеЗадание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(АварийноеЗадание);
				Если ФоновоеЗадание = Неопределено Тогда
					ТекстСообщения = СтрШаблон(НСтр("ru = 'Фоновое задание с идентификатором %1 не найдено.';
													|en = 'A background job with the %1 ID is not found.'"), АварийноеЗадание);
				ИначеЕсли ФоновоеЗадание.Состояние = СостояниеФоновогоЗадания.Отменено Тогда
					ТекстСообщения = СтрШаблон(НСтр("ru = 'Фоновое задание с идентификатором %1 отменено администратором.';
													|en = 'The background job with the %1 ID is canceled by the administrator.'"), АварийноеЗадание);
				ИначеЕсли ФоновоеЗадание.Состояние = СостояниеФоновогоЗадания.ЗавершеноАварийно Тогда
					ТекстСообщения = СтрШаблон(НСтр("ru = 'Фоновое задание с идентификатором %1 завершено аварийно.';
													|en = 'The background job with the %1 ID crashed.'"), АварийноеЗадание)
						+ Символы.ПС + КраткоеПредставлениеОшибки(ФоновоеЗадание.ИнформацияОбОшибке);
				Иначе
					ТекстСообщения = СтрШаблон(НСтр("ru = 'Фоновое задание с идентификатором %1 не выполнено.';
													|en = 'The background job with the %1 ID is not performed.'"), АварийноеЗадание);
				КонецЕсли;
				
				ТекстыСообщений.Добавить(ТекстСообщения);
				
				Если ФоновоеЗадание = Неопределено ИЛИ ФоновоеЗадание.ИнформацияОбОшибке = Неопределено Тогда
					ПредставлениеОшибки = ТекстСообщения;
				Иначе
					ПредставлениеОшибки = СтрШаблон(НСтр("ru = 'Фоновое задание с идентификатором %1 завершено аварийно.';
														|en = 'The background job with the %1 ID crashed.'"), АварийноеЗадание)
						+ Символы.ПС + ПодробноеПредставлениеОшибки(ФоновоеЗадание.ИнформацияОбОшибке);
				КонецЕсли;
				
				ЗаписьЖурналаРегистрации(
					НСтр("ru = 'Отражение в регламентированном учете';
						|en = 'Record in local accounting'", ОбщегоНазначения.КодОсновногоЯзыка()),
					УровеньЖурналаРегистрации.Ошибка,,,
					ПредставлениеОшибки);
			КонецЕсли;
		КонецЦикла;
		
		Если ТекстыСообщений.Количество() > 0 Тогда
			ТекстыСообщений.Вставить(0, НСтр("ru = 'При отражении документов в регл. учете возникли ошибки.';
											|en = 'Errors occurred during local accounting posting.'"));
			ТекстСообщения = СтрСоединить(ОбщегоНазначенияКлиентСервер.СвернутьМассив(ТекстыСообщений), Символы.ПС);
			ВызватьИсключение ТекстСообщения;
		ИначеЕсли ЗавершенныеЗадания.Количество() = 0 ИЛИ ЗавершениеВсехПотоков Тогда
			ОжидатьЗавершенияПотоковОтражения(ПараметрыОтраженияВРеглУчете,
				ТаблицаУправленияПотоками, РезультатВозврата, ЗавершениеВсехПотоков);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция ВыполнитьОтражение(ТипДокумента, ВременныеТаблицы, ПараметрыОтраженияВРеглУчете)
	
	ВходящиеТаблицы = ОбщегоНазначенияУТ.СписокВременныхТаблиц(ВременныеТаблицы);
	
	РезультатОтражения = НовыйРезультатОтражения();
	
	УстановитьПривилегированныйРежим(Истина);
	
	МетаданныеДокумента = Метаданные.НайтиПоТипу(ТипДокумента);
	ИмяДокумента = МетаданныеДокумента.Имя;
	
	Результат = ВыборкиОтраженияДокументов(ИмяДокумента, ВременныеТаблицы, ПараметрыОтраженияВРеглУчете);
	
	ВыборкаСтатусов = Результат.ВыборкаСтатусов;
	
	//++ НЕ УТКА
	ВыборкаСтатусовМФУ = Результат.ВыборкаСтатусовМФУ;
	ВыборкаСтатусовМФУ.Следующий();
	//-- НЕ УТКА
	
	ВыборкаПроверки = Результат.ВыборкаПроверки;
	ВыборкаПроверки.Следующий();
	
	ВыборкаХозрасчетный = Результат.ВыборкаХозрасчетный;
	ВыборкаХозрасчетный.Следующий();
	
	ВыборкаХозрасчетныйДополнение = Результат.ВыборкаХозрасчетныйДополнение;
	ВыборкаХозрасчетныйДополнение.Следующий();
	
	ВыборкаХозрасчетныйСторно = Результат.ВыборкаХозрасчетныйСторно;
	ВыборкаХозрасчетныйСторно.Следующий();
	
	ВыборкаСвязанныеДокументыКОтражению = Результат.ВыборкаСвязанныеДокументыКОтражению;
	ВыборкаСвязанныеДокументыКОтражению.Следующий();
	
	Пока ВыборкаСтатусов.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОтражениеДокументовВРеглУчете.НаборЗаписей");
			ЭлементБлокировки.УстановитьЗначение("Регистратор", ВыборкаСтатусов.Ссылка);
			//++ НЕ УТКА
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОтражениеДокументовВМеждународномУчете.НаборЗаписей");
			ЭлементБлокировки.УстановитьЗначение("Регистратор", ВыборкаСтатусов.Ссылка);
			//-- НЕ УТКА
			ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Хозрасчетный.НаборЗаписей");
			ЭлементБлокировки.УстановитьЗначение("Регистратор", ВыборкаСтатусов.Ссылка);
			
			Блокировка.Заблокировать();
			
			Если ДанныеДокументаИзменились(ВыборкаСтатусов) Тогда
				ОтменитьТранзакцию();
				СдвинутьВыборки(Результат);
				Продолжить;
			КонецЕсли;
			
			ОшибкиОтражения = ОшибкиОтраженияСЗаписьюВРегистрТребующихсяНастроек(ВыборкаСтатусов.Ссылка, ВыборкаПроверки);
			
			СформироватьОтражениеДокументовВРеглУчете(ВыборкаСтатусов, ОшибкиОтражения);
			//++ НЕ УТКА
			СформироватьОтражениеДокументовВМФУ(ВыборкаСтатусов, ВыборкаСтатусовМФУ, ОшибкиОтражения);
			//-- НЕ УТКА
			СформироватьХозрасчетный(ВыборкаСтатусов, 
				ВыборкаХозрасчетный,
				ВыборкаХозрасчетныйДополнение,
				ВыборкаХозрасчетныйСторно);
			
			ЗарегистрироватьСвязанныеДокументыКОтражению(ВыборкаСтатусов, ВыборкаСвязанныеДокументыКОтражению);
			
			Если ОшибкиОтражения.Количество() > 0 Тогда
				РезультатОтражения.НеУказаныСчетаУчета.Добавить(ВыборкаСтатусов.Ссылка);
				РезультатОтражения.ОписанияПроблем.Вставить(ВыборкаСтатусов.Ссылка,
					СтрСоединить(ОшибкиОтражения.ВыгрузитьКолонку("Комментарий"), Символы.ПС));
			Иначе
				РезультатОтражения.ОтраженоВУчете.Добавить(ВыборкаСтатусов.Ссылка);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			
			РезультатОтражения.ОшибкиПриОтражении.Добавить(ВыборкаСтатусов.Ссылка);
			РезультатОтражения.ОписанияПроблем.Вставить(ВыборкаСтатусов.Ссылка, КраткоеПредставлениеОшибки(ИнформацияОбОшибке));
			СдвинутьВыборки(Результат);
			
			ТекстШаблона = НСтр("ru = 'Не удалось отразить в регл. учете документ ""%1"" по причине: %2';
								|en = 'Cannot record document ""%1"" in compl. accounting due to: %2'");
			
			ТекстСообщения = СтрШаблон(
					ТекстШаблона,
					ВыборкаСтатусов.Ссылка,
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
			
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Отражение в регламентированном учете';
					|en = 'Record in local accounting'", ОбщегоНазначения.КодОсновногоЯзыка()), 
				УровеньЖурналаРегистрации.Ошибка, , , ТекстСообщения);
			
			Если ПараметрыОтраженияВРеглУчете.СообщатьОбОшибках Тогда
				ОбщегоНазначения.СообщитьПользователю(СтрШаблон(ТекстШаблона, ВыборкаСтатусов.Ссылка,
					КраткоеПредставлениеОшибки(ИнформацияОбОшибке)));
			КонецЕсли;
				
			Если Не ПараметрыОтраженияВРеглУчете.ПакетноеОтражение Тогда
				// Если отражаем документы не пакетно, то необходимо вызывать исключение при ошибки, для дополнительного уведомления пользователя.
				ВызватьИсключение ТекстСообщения;
			КонецЕсли;
			
		КонецПопытки;
		
	КонецЦикла;
	
	ЗапросЗаданийКФормированиюФинансовогоРезультата = РеглУчетВыборкиСерверПовтИсп.ЗапросЗаданияКФормированиюФинРезультата();
	ЗапросЗаданийКФормированиюФинансовогоРезультата.МенеджерВременныхТаблиц = ВременныеТаблицы;
	ЗапросЗаданийКФормированиюФинансовогоРезультата.УстановитьПараметр("ТипыОперацийФинРеза", Документы.РегламентнаяОперация.ТипыОперацийФинРеза());
	Выборка = ЗапросЗаданийКФормированиюФинансовогоРезультата.Выполнить().Выбрать();
	РегистрыСведений.ЗаданияКЗакрытиюМесяца.СоздатьЗаписиРегистраПоДаннымВыборки(Выборка);
	
	ДополнительнаяОбработкаПриОтраженииДокумента(ТипДокумента, ВременныеТаблицы);
	
	ОбщегоНазначенияУТ.УничтожитьВременныеТаблицы(ВременныеТаблицы,,ВходящиеТаблицы);
	ОбщегоНазначенияУТ.УничтожитьВременныеТаблицы(ВременныеТаблицы,"РазрезыКОтражению,ДокументыКОтражению");
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат РезультатОтражения;
	
КонецФункции

Функция ВыборкиОтраженияДокументов(ИмяДокумента, ВременныеТаблицы, ПараметрыОтраженияВРеглУчете)
	
	// Выборка статусов отражения документов из регистра ОтражениеДокументовВРеглУчете
	ЗапросОтражениеДокументовВРеглУчете = РеглУчетВыборкиСерверПовтИсп.ЗапросОтражениеДокументовВРеглУчете();
	ЗапросОтражениеДокументовВРеглУчете.МенеджерВременныхТаблиц = ВременныеТаблицы;
	
	РезультатОтражениеДокументовВРеглУчете = ЗапросОтражениеДокументовВРеглУчете.Выполнить();
	ВыборкаСтатусов = РезультатОтражениеДокументовВРеглУчете.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	//++ НЕ УТКА

	// Выборка статусов отражения документов из регистра ОтражениеДокументовВМеждународномУчете.
	ЗапросОтражениеДокументовВМФУ = РеглУчетВыборкиСерверПовтИсп.ЗапросОтражениеДокументовВМеждународномУчете();
	ЗапросОтражениеДокументовВМФУ.МенеджерВременныхТаблиц = ВременныеТаблицы;
	ЗапросОтражениеДокументовВМФУ.УстановитьПараметр("ПроводкиМеждународногоУчетаПоДаннымРегл", ПолучитьФункциональнуюОпцию("ФормироватьПроводкиМеждународногоУчетаПоДаннымРегламентированного"));
	
	РезультатОтражениеДокументовВМФУ = ЗапросОтражениеДокументовВМФУ.Выполнить();
	ВыборкаСтатусовМФУ = РезультатОтражениеДокументовВМФУ.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	//-- НЕ УТКА
	
	// Выборка контекстных данных (данных из документов)
	ЭтоОбъектРасчетов = РеглУчетВыборкиСерверПовтИсп.ЭтоОбъектРасчетов(ИмяДокумента);
	ЗапросДанных = РеглУчетВыборкиСерверПовтИсп.ЗапросДанных(ИмяДокумента, ЭтоОбъектРасчетов, ПараметрыОтраженияВРеглУчете.ВременныеТаблицыПереданы);
	ЗапросДанных.МенеджерВременныхТаблиц = ВременныеТаблицы;
	УстановитьПараметрыЗапросаДанных(ЗапросДанных, ИмяДокумента, ВыборкаСтатусов, ПараметрыОтраженияВРеглУчете.ДетализацияПроводок);
	
	ЗапросДанных.Выполнить();
	
	// Выборка счетов учета прочих операций
	ЗапросПрочихСчетов = РеглУчетВыборкиСерверПовтИсп.ЗапросПрочихСчетов();
	ЗапросПрочихСчетов.МенеджерВременныхТаблиц = ВременныеТаблицы;
	ЗапросПрочихСчетов.Выполнить();
	
	// Выборка создаваемых движений
	ЗапросСопоставлений = РеглУчетВыборкиСерверПовтИсп.ЗапросСопоставлений();
	ЗапросСопоставлений.УстановитьПараметр("ДетализацияПроводок", ПараметрыОтраженияВРеглУчете.ДетализацияПроводок);
	ЗапросСопоставлений.МенеджерВременныхТаблиц = ВременныеТаблицы;
	ЗапросСопоставлений.Выполнить();
	
	// Проверяем полученные и дополненные данные
	ЗапросПроверки = РеглУчетВыборкиСерверПовтИсп.ЗапросПроверки();
	ЗапросПроверки.МенеджерВременныхТаблиц = ВременныеТаблицы;
	РезультатПроверки = ЗапросПроверки.Выполнить();
	ВыборкаПроверки = РезультатПроверки.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ЗапросХозрасчетный = РеглУчетВыборкиСерверПовтИсп.ЗапросХозрасчетный();
	ЗапросХозрасчетный.МенеджерВременныхТаблиц = ВременныеТаблицы;
	РезультатХозрасчетный = ЗапросХозрасчетный.Выполнить();
	ВыборкаХозрасчетный = РезультатХозрасчетный.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ЗапросХозрасчетныйДополнение = РеглУчетВыборкиСерверПовтИсп.ЗапросХозрасчетныйДополнение();
	ЗапросХозрасчетныйДополнение.МенеджерВременныхТаблиц = ВременныеТаблицы;
	ЗапросХозрасчетныйДополнение.УстановитьПараметр("ДокументыОтражаемыеПоследовательно", РеглУчетВыборкиСерверПовтИсп.ТипыДокументовКПоследовательномуОтражению());
	РезультатХозрасчетныйДополнение = ЗапросХозрасчетныйДополнение.Выполнить();
	ВыборкаХозрасчетныйДополнение = РезультатХозрасчетныйДополнение.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ЗапросХозрасчетныйСторно = РеглУчетВыборкиСерверПовтИсп.ЗапросХозрасчетныйСторно();
	ЗапросХозрасчетныйСторно.МенеджерВременныхТаблиц = ВременныеТаблицы;
	РезультатХозрасчетныйСторно = ЗапросХозрасчетныйСторно.Выполнить();
	ВыборкаХозрасчетныйСторно = РезультатХозрасчетныйСторно.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	// Выборка связанных документов, которые требуется вернуть к отражению в регл. учете
	ЗапросСвязанныеДокументыКОтражению = РеглУчетВыборкиСерверПовтИсп.ЗапросСвязанныеДокументыКОтражению();
	ЗапросСвязанныеДокументыКОтражению.МенеджерВременныхТаблиц = ВременныеТаблицы;
	РезультатСвязанныеДокументыКОтражению = ЗапросСвязанныеДокументыКОтражению.Выполнить();
	ВыборкаСвязанныеДокументыКОтражению = РезультатСвязанныеДокументыКОтражению.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("ВыборкаСтатусов",                 ВыборкаСтатусов);
	//++ НЕ УТКА
	СтруктураВозврата.Вставить("ВыборкаСтатусовМФУ",              ВыборкаСтатусовМФУ);
	//-- НЕ УТКА
	СтруктураВозврата.Вставить("ВыборкаПроверки",                 ВыборкаПроверки);
	СтруктураВозврата.Вставить("ВыборкаХозрасчетный",             ВыборкаХозрасчетный);
	СтруктураВозврата.Вставить("ВыборкаХозрасчетныйДополнение",   ВыборкаХозрасчетныйДополнение);
	СтруктураВозврата.Вставить("ВыборкаХозрасчетныйСторно",       ВыборкаХозрасчетныйСторно);
	СтруктураВозврата.Вставить("ВыборкаСвязанныеДокументыКОтражению", ВыборкаСвязанныеДокументыКОтражению);
	
	Возврат СтруктураВозврата;
	
КонецФункции

Функция КомментарийОшибокЗаполнения(РезультатПроверки)
	
	СтрокиКомментарий = Новый Массив();
	
	ВсегоОшибок = 0;
	ОШ = РезультатПроверки.Выбрать();
	Пока ОШ.Следующий() Цикл
		КомментарийОшибки = КомментарийОшибки(ОШ);
		Если ЗначениеЗаполнено(КомментарийОшибки) Тогда
			ВсегоОшибок = ВсегоОшибок + 1;
			СтрокиКомментарий.Добавить(КомментарийОшибки);
		КонецЕсли;
	КонецЦикла;
	
	Комментарий = СтрСоединить(СтрокиКомментарий, Символы.ПС);
	
	Возврат Комментарий;
	
КонецФункции

Функция ВременныеТаблицы(ВнешнийМенеджерВременныхТаблиц = Неопределено)
	
	ВременныеТаблицы = ВнешнийМенеджерВременныхТаблиц;
	
	Если ВременныеТаблицы = Неопределено Тогда
		
		ВременныеТаблицы = Новый МенеджерВременныхТаблиц;
		
		МассивТекстовЗапроса = Новый Массив;
		МассивТекстовЗапроса.Добавить(УчетНМА.ТекстЗапросаПустаяВременнаяТаблицаАмортизации());
		
		ЗапросПустыхТаблиц = Новый Запрос(СтрСоединить(МассивТекстовЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов()));
		ЗапросПустыхТаблиц.МенеджерВременныхТаблиц = ВременныеТаблицы;
		ЗапросПустыхТаблиц.Выполнить();
		
	КонецЕсли;
	
	ЗапросПланаСчетов = РеглУчетВыборкиСерверПовтИсп.ЗапросПланаСчетов();
	ЗапросПланаСчетов.МенеджерВременныхТаблиц = ВременныеТаблицы;
	ЗапросПланаСчетов.Выполнить();
	// выборка вариабельных счетов учета
	ЗапросСчетов = РеглУчетВыборкиСерверПовтИсп.ЗапросСчетов();
	ЗапросСчетов.МенеджерВременныхТаблиц = ВременныеТаблицы;
	ЗапросСчетов.Выполнить();
	
	// выборка счетов по умолчанию
	УстановитьПривилегированныйРежим(Истина);
	ЗапросСчетов = РеглУчетВыборкиСерверПовтИсп.ЗапросСчетовПоУмолчанию();
	ЗапросСчетов.МенеджерВременныхТаблиц = ВременныеТаблицы;
	ЗапросСчетов.Выполнить();
	
	Возврат ВременныеТаблицы;
КонецФункции

Процедура ДополнительнаяОбработкаПриОтраженииДокумента(ТипДокумента, МенеджерВременныхТаблиц)
	
	Если ТипДокумента = Тип("ДокументСсылка.ПринятиеКУчетуОС") Тогда
		Документы.ПринятиеКУчетуОС.ЗаполнитьПервоначальныеСуммыПриОтражении(МенеджерВременныхТаблиц);
	КонецЕсли;
	
	Если ТипДокумента = Тип("ДокументСсылка.ПринятиеКУчетуНМА") Тогда
		Документы.ПринятиеКУчетуНМА.ЗаполнитьПервоначальныеСуммыПриОтражении(МенеджерВременныхТаблиц);
	КонецЕсли;
	
	Если ТипДокумента = Тип("ДокументСсылка.МодернизацияОС") Тогда
		Документы.МодернизацияОС.ЗаполнитьПараметрыНачисленияАмортизацииПриОтражении(МенеджерВременныхТаблиц);
	КонецЕсли;
	
	Если ТипДокумента = Тип("ДокументСсылка.ВнутреннееПотребление") Тогда
		ВнутреннееПотреблениеЛокализация.ДополнительнаяОбработкаПриОтраженииДокумента(МенеджерВременныхТаблиц);
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьПараметрыЗапросаДанных(ЗапросДанных, ИмяДокумента, ВыборкаДокументов, ДетализацияПроводок)
	
	СтруктураПараметров = РеглУчетВыборкиСерверПовтИсп.ПараметрыОтраженияРеглУчетаПоУмолчанию(ДетализацияПроводок);
	
	ТипДокумента = Тип("ДокументСсылка." + ИмяДокумента);
	ТипыДокументов = РеглУчетВыборкиСерверПовтИсп.ТипыДокументовКПоследовательномуОтражению();
	Если ТипыДокументов.Найти(ТипДокумента) <> Неопределено 
		И ВыборкаДокументов.Количество() = 1 Тогда
		
		ВыборкаДокументов.Следующий();
		
		ДополнитьПараметрыОтраженияРеглУчетаДляДокументовПоследовательногоОтражения(СтруктураПараметров, ВыборкаДокументов.Ссылка, ВыборкаДокументов.Период);
		
		// Спозиционируем выборку в начало
		ВыборкаДокументов.Сбросить();
		
	КонецЕсли;
	
	// Устанавливаем параметры по-умолчанию и {&ИмяФункциональнойОпции}
	РасчетСебестоимостиНДС.ДополнитьСтруктуруПараметровДетализацииПартийНДСРеглУчет(ЗапросДанных, СтруктураПараметров, ИмяДокумента);
	ЭтоОбъектРасчетов = РеглУчетВыборкиСерверПовтИсп.ЭтоОбъектРасчетов(ИмяДокумента); 
	ПараметрыДанных = РеглУчетВыборкиСерверПовтИсп.ЗапросДанныхПараметры(ИмяДокумента, ЭтоОбъектРасчетов);
	ЗаполнениеПараметровЗапросаИПрочихФункциональныхОпций(ЗапросДанных, СтруктураПараметров, ПараметрыДанных);
	
КонецПроцедуры

Процедура УстановитьОтборДокументовПоПараметрамОтражения(Запрос, ПараметрыОтражения)
	
	Если НЕ ЗначениеЗаполнено(ПараметрыОтражения.ДатаЗапрета) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Данные.ДатаОтражения >= &ДатаЗапрета", "ИСТИНА");
	Иначе
		Запрос.УстановитьПараметр("ДатаЗапрета", КонецДня(ПараметрыОтражения.ДатаЗапрета) + 1);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ПараметрыОтражения.ПериодРасчета) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Данные.ДатаОтражения <= &ПериодОтражения", "ИСТИНА");
	Иначе
		Запрос.УстановитьПараметр("ПериодОтражения", ПараметрыОтражения.ПериодРасчета);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ПараметрыОтражения.Организации) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Данные.Организация В (&Организации)", "ИСТИНА");
	Иначе
		Запрос.УстановитьПараметр("Организации", ПараметрыОтражения.Организации);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ПараметрыОтражения.Документы) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Данные.Регистратор В (&МассивСсылок)", "ИСТИНА");
	Иначе
		Запрос.УстановитьПараметр("МассивСсылок", ПараметрыОтражения.Документы);
	КонецЕсли;
	
	// Будем отражать только проверенные документы только в том случае, если включены соотв. опции
	// и отражение происходит по всем документам (в противном случае не будут отражаться печатаемые документы):
	Если НЕ ЗначениеЗаполнено(ПараметрыОтражения.Документы)
		И ПолучитьФункциональнуюОпцию("ИспользоватьПроверкуФинансовыхДокументов")
		И ПолучитьФункциональнуюОпцию("ОтражатьВРеглУчетеТолькоПроверенныеДокументы") Тогда
		ТекстЗапросаПроверки = 
			"ЕСТЬNULL(ПроверкаДокументовПереопределяемый.СтатусПроверки, ЗНАЧЕНИЕ(Перечисление.СтатусыПроверкиФинансовыхДокументов.Проверен)) 
			|	= ЗНАЧЕНИЕ(Перечисление.СтатусыПроверкиФинансовыхДокументов.Проверен)";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДокументПроверен", ТекстЗапросаПроверки);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДокументПроверен", "ИСТИНА");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ОтражатьВУчетеТолькоПроверенныеДокументы", ПолучитьФункциональнуюОпцию("ОтражатьВРеглУчетеТолькоПроверенныеДокументы"));
	Запрос.УстановитьПараметр("МассивИсключаемыхСсылок", ПараметрыОтражения.НеотраженныеДокументы);
	
КонецПроцедуры

Функция СтатусыДокументовКОтражению(ТипДокумента = Неопределено)
	
	ВозможноРучноеОтражениеДокумента = ПолучитьФункциональнуюОпцию("ИспользоватьРучнуюКорректировкуПроводокПоРеглУчету");
	
	Если ВозможноРучноеОтражениеДокумента И ТипДокумента <> Неопределено Тогда
		ИсключенияИзРучнойКорректировки = РеглУчетВыборкиСерверПовтИсп.ИсключенияИзРучнойКорректировкиПроводок();
		ВозможноРучноеОтражениеДокумента = ИсключенияИзРучнойКорректировки.Найти(ТипДокумента) = Неопределено;
	КонецЕсли;
	
	СтатусыОтражения = Новый Массив;
	СтатусыОтражения.Добавить(Перечисления.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВРеглУчете);
	СтатусыОтражения.Добавить(Перечисления.СтатусыОтраженияДокументовВРеглУчете.НеУказаныСчетаУчета);
	
	// Возможна ситуация когда ФО ручного отражения снята, но документы отраженные вручную есть - для них тоже надо делать отражение:
	Если Не ВозможноРучноеОтражениеДокумента Тогда
		СтатусыОтражения.Добавить(Перечисления.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВУчетеВручную);
		СтатусыОтражения.Добавить(Перечисления.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВУчетеВручную);
	КонецЕсли;
	
	Возврат СтатусыОтражения;
	
КонецФункции

Функция ТипыДокументовКПакетномуОтражению(ПараметрыОтражения)
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьРеглУчет") Тогда
		Возврат Новый Массив;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ РАЗРЕШЕННЫЕ
	|	ТИПЗНАЧЕНИЯ(Данные.Регистратор) КАК ТипДокумента
	|ИЗ
	|	РегистрСведений.ОтражениеДокументовВРеглУчете КАК Данные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Константы КАК Константы
	|		ПО (ИСТИНА)
	|ГДЕ
	|	Данные.ДатаОтражения >= &ДатаЗапрета
	|	И Данные.ДатаОтражения <= &ПериодОтражения
	|	И Данные.Организация В (&Организации)
	|	И Данные.Регистратор В (&МассивСсылок)
	|	И Данные.Статус В (
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВРеглУчете),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.НеУказаныСчетаУчета))
	|	И НЕ ТИПЗНАЧЕНИЯ(Данные.Регистратор) В (&ТипыПоследовательныхДокументов)
	|	И (Данные.ДатаОтражения >= Константы.ДатаНачалаВеденияРеглУчета)
	|";
	
	УстановитьОтборДокументовПоПараметрамОтражения(Запрос, ПараметрыОтражения);
	ДобавитьВЗапросФильтрОтраженияВРеглУчете(Запрос.Текст, "Данные.Регистратор");
	
	Запрос.УстановитьПараметр("ТипыПоследовательныхДокументов", РеглУчетВыборкиСерверПовтИсп.ТипыДокументовКПоследовательномуОтражению());
	Результат = Запрос.Выполнить();
	
	Возврат Результат.Выгрузить().ВыгрузитьКолонку("ТипДокумента");
	
КонецФункции

Функция СформироватьТаблицыОтбораДанных(ТипДокумента, ВременныеТаблицы, ПараметрыОтражения, ПараметрыПорции = Неопределено)
	
	МногопоточныйРежим = ПараметрыПорции <> Неопределено;
	ФормированиеПорции = ТипЗнч(ПараметрыПорции) = Тип("СтрокаТаблицыЗначений");
	ФормированиеПроводок = ВременныеТаблицы <> Неопределено;
	
	Если ФормированиеПроводок Тогда
		МенеджерВременныхТаблиц = ВременныеТаблицы;
	Иначе
		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ РАЗРЕШЕННЫЕ
	|	Данные.МоментВремени КАК МоментВремени,
	|	Данные.Период        КАК Период,
	|	Данные.Регистратор   КАК Ссылка,
	|	Данные.Организация   КАК Организация,
	|	Данные.ДатаОтражения КАК ДатаОтражения
	|ПОМЕСТИТЬ ДанныеРегистра
	|ИЗ
	|	РегистрСведений.ОтражениеДокументовВРеглУчете КАК Данные
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПроверкаДокументовПереопределяемый КАК ПроверкаДокументовПереопределяемый
	|		ПО Данные.Организация = ПроверкаДокументовПереопределяемый.Организация
	|		 И Данные.Регистратор = ПроверкаДокументовПереопределяемый.Документ
	|		 И &ОтражатьВУчетеТолькоПроверенныеДокументы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Константы КАК Константы
	|		ПО (ИСТИНА)
	|ГДЕ
	|	&ОтборПоНачальномуМоментуВремени
	|	И Данные.ДатаОтражения >= &ДатаЗапрета
	|	И Данные.ДатаОтражения <= &ПериодОтражения
	|	И Данные.Организация В (&Организации)
	|	И Данные.Регистратор В (&МассивСсылок)
	|	И НЕ Данные.Регистратор В (&МассивИсключаемыхСсылок)
	|	И ТИПЗНАЧЕНИЯ(Данные.Регистратор) = &ТипДокумента
	|	И Данные.Статус В (&СтатусыОтражения)
	|	И (Данные.ДатаОтражения >= Константы.ДатаНачалаВеденияРеглУчета)
	|	И &ДокументПроверен
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 500 
	|	Данные.МоментВремени КАК МоментВремени,
	|	Данные.Ссылка        КАК Ссылка,
	|	Данные.Период        КАК Период,
	|	1                    КАК Количество
	|ПОМЕСТИТЬ ДокументыКОтражению
	|ИЗ
	|	ДанныеРегистра КАК Данные
	|ГДЕ
	|	&ОтборПоКонечномуМоментуВремени";
	
	ПроверкаДокументовСервер.ПереопределитьВТекстеЗапросаСтатусыПроверкиСУчетомИсключений(Запрос.Текст, "Данные.Регистратор");
	Если ФормированиеПорции Тогда
		Запрос.Текст = Запрос.Текст + "
		|УПОРЯДОЧИТЬ ПО
		|	Данные.МоментВремени";
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + "
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|";
	
	Если ФормированиеПроводок Тогда
		Запрос.Текст = Запрос.Текст + ОбщегоНазначения.РазделительПакетаЗапросов() + "
		|ВЫБРАТЬ
		|	ДанныеРегистра.Период        КАК Период,
		|	ДанныеРегистра.Ссылка        КАК Ссылка,
		|	ДанныеРегистра.Организация   КАК Организация,
		|	ДанныеРегистра.Организация.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
		|	ДанныеРегистра.ДатаОтражения КАК ДатаОтражения
		|ПОМЕСТИТЬ РазрезыКОтражению
		|ИЗ
		|	ДанныеРегистра КАК ДанныеРегистра
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыКОтражению КАК ДокументыКОтражению
		|		ПО ДанныеРегистра.Ссылка = ДокументыКОтражению.Ссылка
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|";
	КонецЕсли; 
	
	Запрос.Текст = Запрос.Текст + ОбщегоНазначения.РазделительПакетаЗапросов() + "
	|УНИЧТОЖИТЬ ДанныеРегистра
	|";
	
	Если ФормированиеПорции Тогда
		Запрос.Текст = Запрос.Текст + ОбщегоНазначения.РазделительПакетаЗапросов() + "
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ДокументыКОтражению.МоментВремени КАК МоментВремени
		|ИЗ
		|	ДокументыКОтражению КАК ДокументыКОтражению
		|УПОРЯДОЧИТЬ ПО
		|	ДокументыКОтражению.МоментВремени УБЫВ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(ДокументыКОтражению.Количество) КАК Количество
		|ИЗ
		|	ДокументыКОтражению КАК ДокументыКОтражению
		|ИМЕЮЩИЕ
		|	СУММА(ДокументыКОтражению.Количество) = &КоличествоДокументов
		|";
	КонецЕсли;
	
	Если НЕ МногопоточныйРежим Тогда
		Первые500 = "ПЕРВЫЕ 500";//@Query-part
		ОтборПоНачальномуМоментуВремени = "ИСТИНА";
		ОтборПоКонечномуМоментуВремени = "ИСТИНА";
	ИначеЕсли ФормированиеПорции Тогда
		ДокументовНаПорцию = ПараметрыПорции.КоличествоДокументов / ПараметрыПорции.КоличествоПорций;
		ОкруглитьПорциюДо = Мин(10, Цел(ДокументовНаПорцию / 10) + 1);
		ДокументовНаПорцию = ОкруглитьПорциюДо * (Цел(ДокументовНаПорцию / ОкруглитьПорциюДо) + 1);
		
		Первые500 = "ПЕРВЫЕ " + Формат(ДокументовНаПорцию, "ЧДЦ=0; ЧГ=0");//@Query-part
		ОтборПоНачальномуМоментуВремени = "Данные.МоментВремени > &МоментВремени";
		ОтборПоКонечномуМоментуВремени = "ИСТИНА";
		
		Запрос.УстановитьПараметр("КоличествоДокументов", ДокументовНаПорцию);
		Запрос.УстановитьПараметр("МоментВремени", ПараметрыПорции.КонечныйМоментВремени);
	ИначеЕсли ФормированиеПроводок Тогда
		Первые500 = "";
		ОтборПоНачальномуМоментуВремени = "Данные.МоментВремени > &НачальныйМоментВремени";
		ОтборПоКонечномуМоментуВремени = "Данные.МоментВремени <= &КонечныйМоментВремени"; 
		
		Запрос.УстановитьПараметр("НачальныйМоментВремени", ПараметрыПорции.НачальныйМоментВремени);
		Запрос.УстановитьПараметр("КонечныйМоментВремени", ПараметрыПорции.КонечныйМоментВремени);
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 500", Первые500);//@Query-part
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоНачальномуМоментуВремени", ОтборПоНачальномуМоментуВремени);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоКонечномуМоментуВремени", ОтборПоКонечномуМоментуВремени);
	
	УстановитьОтборДокументовПоПараметрамОтражения(Запрос, ПараметрыОтражения);
	ДобавитьВЗапросФильтрОтраженияВРеглУчете(Запрос.Текст, "Данные.Регистратор");
	
	Запрос.УстановитьПараметр("СтатусыОтражения", СтатусыДокументовКОтражению(ТипДокумента));
	Запрос.УстановитьПараметр("ТипДокумента",     ТипДокумента);
		
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	МаксимальныйИндекс = РезультатыЗапроса.ВГраница();
	
	Выборка = РезультатыЗапроса[0].Выбрать();
	Если Выборка.Следующий() Тогда
		ЕстьДокументыКОтражению = Выборка.Количество <> 0;
	Иначе
		ЕстьДокументыКОтражению = Ложь;
	КонецЕсли;
	
	Если ФормированиеПорции Тогда
		РезультатМоментВремени = РезультатыЗапроса[МаксимальныйИндекс - 1];
		РезультатПолнаяПорция = РезультатыЗапроса[МаксимальныйИндекс];
		
		ВыборкаМоментВремени = РезультатМоментВремени.Выбрать();
		Если ВыборкаМоментВремени.Следующий() Тогда
			КонечныйМоментВремени = ВыборкаМоментВремени.МоментВремени;
		Иначе
			КонечныйМоментВремени = ПараметрыПорции.КонечныйМоментВремени;
		КонецЕсли;
		
		ПараметрыПорции.НачальныйМоментВремени = ПараметрыПорции.КонечныйМоментВремени;
		ПараметрыПорции.КонечныйМоментВремени = КонечныйМоментВремени;
		ПараметрыПорции.ПоследняяПорция = РезультатПолнаяПорция.Пустой();
	КонецЕсли;
	
	Если НЕ МногопоточныйРежим ИЛИ ФормированиеПорции Тогда
		ПроверитьБлокировкуВходящихДанных(ТипДокумента, МенеджерВременныхТаблиц);
	КонецЕсли;
	
	Если НЕ ФормированиеПроводок Тогда
		МенеджерВременныхТаблиц.Закрыть();
	ИначеЕсли НЕ ЕстьДокументыКОтражению Тогда
		// Уничтожим пустую таблицу документов к отражению
		ОбщегоНазначенияУТ.УничтожитьВременныеТаблицы(МенеджерВременныхТаблиц, "РазрезыКОтражению,ДокументыКОтражению");
	КонецЕсли;
	
	Возврат ЕстьДокументыКОтражению;
	
КонецФункции

Функция ДокументыКПоследовательномуОтражению(ПараметрыОтражения)
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьРеглУчет") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Данные.Регистратор   КАК Ссылка,
	|	Данные.МоментВремени КАК МоментВремени,
	|	ТИПЗНАЧЕНИЯ(Данные.Регистратор) КАК ТипДокумента
	|ИЗ
	|	РегистрСведений.ОтражениеДокументовВРеглУчете КАК Данные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Константы КАК Константы
	|		ПО (ИСТИНА)
	|ГДЕ
	|	Данные.ДатаОтражения <= &ПериодОтражения
	|	И Данные.Организация В (&Организации)
	|	И Данные.Регистратор В (&МассивСсылок)
	|	И ТИПЗНАЧЕНИЯ(Данные.Регистратор) В (&ТипыДокументов)
	|	И Данные.Статус В (
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВРеглУчете),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.НеУказаныСчетаУчета))
	|	И (Данные.ДатаОтражения >= Константы.ДатаНачалаВеденияРеглУчета)
	|УПОРЯДОЧИТЬ ПО
	|	Данные.МоментВремени
	|";
	
	УстановитьОтборДокументовПоПараметрамОтражения(Запрос, ПараметрыОтражения);
	ДобавитьВЗапросФильтрОтраженияВРеглУчете(Запрос.Текст, "Данные.Регистратор");

	Запрос.УстановитьПараметр("ТипыДокументов", РеглУчетВыборкиСерверПовтИсп.ТипыДокументовКПоследовательномуОтражению());
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

Функция НеотраженныеВРеглУчетеДокументы(МассивДокументов)
	
	УстановитьПривилегированныйРежим(Истина);
	
	НеотраженныеДокументы = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтражениеДокументовВРеглУчете.Регистратор КАК Ссылка
	|ИЗ
	|	РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументовВРеглУчете
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Константы КАК Константы
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ОтражениеДокументовВРеглУчете.Регистратор В (&МассивДокументов)
	|	И (ОтражениеДокументовВРеглУчете.ДатаОтражения >= Константы.ДатаНачалаВеденияРеглУчета)
	|	И НЕ ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВРеглУчете)
	|	И НЕ ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВУчетеВручную)
	|";
	
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	
	ДобавитьВЗапросФильтрОтраженияВРеглУчете(Запрос.Текст, "ОтражениеДокументовВРеглУчете.Регистратор");
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		НеотраженныеДокументы = Результат.Выгрузить().ВыгрузитьКолонку("Ссылка");
	КонецЕсли;
	
	Возврат НеотраженныеДокументы;
	
КонецФункции

Функция ДанныеДокументаИзменились(ВыборкаСтатусов) 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Статусы.ИдентификаторСтатуса КАК ИдентификаторСтатуса
	|ИЗ
	|	РегистрСведений.ОтражениеДокументовВРеглУчете КАК Статусы
	|ГДЕ
	|	Статусы.Регистратор = &Ссылка
	|";
	Запрос.УстановитьПараметр("Ссылка", ВыборкаСтатусов.Ссылка);
	ВыборкаТекущихСтатусов = Запрос.Выполнить().Выбрать();
	
	Если ВыборкаТекущихСтатусов.Следующий() Тогда
		ДанныеИзменились = (ВыборкаТекущихСтатусов.ИдентификаторСтатуса <> ВыборкаСтатусов.ИдентификаторСтатуса);
	Иначе
		ДанныеИзменились = Истина;
	КонецЕсли;
	
	Возврат ДанныеИзменились;
	
КонецФункции

Процедура СдвинутьВыборки(Выборки)
	
	ТекущаяСсылка = Выборки.ВыборкаСтатусов.Ссылка;
	
	//++ НЕ УТКА
	Если Выборки.ВыборкаСтатусовМФУ.Ссылка = ТекущаяСсылка Тогда
		Выборки.ВыборкаСтатусовМФУ.Следующий();
	КонецЕсли;
	//-- НЕ УТКА
	Если Выборки.ВыборкаПроверки.Ссылка = ТекущаяСсылка Тогда
		Выборки.ВыборкаПроверки.Следующий();
	КонецЕсли;
	Если Выборки.ВыборкаХозрасчетный.Ссылка = ТекущаяСсылка Тогда
		Выборки.ВыборкаХозрасчетный.Следующий();
	КонецЕсли;
	Если Выборки.ВыборкаХозрасчетныйДополнение.Ссылка = ТекущаяСсылка Тогда
		Выборки.ВыборкаХозрасчетныйДополнение.Следующий();
	КонецЕсли;
	Если Выборки.ВыборкаХозрасчетныйСторно.Ссылка = ТекущаяСсылка Тогда
		Выборки.ВыборкаХозрасчетныйСторно.Следующий();
	КонецЕсли;
	Если Выборки.ВыборкаСвязанныеДокументыКОтражению.Ссылка = ТекущаяСсылка Тогда
		Выборки.ВыборкаСвязанныеДокументыКОтражению.Следующий();
	КонецЕсли;
	
КонецПроцедуры

#Область УправлениеВыполнениемОффлайновыхРасчетов

Процедура РассчитатьСуммыДокументовВВалютахУчета(МассивДокументов) Экспорт
	
	РегистрыСведений.СуммыДокументовВВалютахУчета.РассчитатьСуммыДокументовВВалютахУчета(МассивДокументов);
	УправленческийУчетПроведениеСервер.ОбновитьДвиженияПоОборотнымРегистрам(МассивДокументов);
	
КонецПроцедуры

Процедура СформироватьДвиженияПоВНА(МассивДокументов) Экспорт
	
	ОтложенноеФормированиеДвиженийВНА.ВыполнитьОперациюПоДокументам(МассивДокументов);
	РасчетСтоимостиВНА.ВыполнитьОперациюПоДокументам(МассивДокументов);
	ЗакрытиеРасходовОтВыбытияОС.ВыполнитьОперациюПоДокументам(МассивДокументов);
	РасчетСтоимостиИнвестицииВАренду.ВыполнитьОперациюПоДокументам(МассивДокументов);
	
КонецПроцедуры

Процедура ОтразитьДокументыВУчетеНДС(МассивДокументов) Экспорт
	
	ПериодРасчетаНДС = КонецМесяца(ТекущаяДатаСеанса());
	УчетНДСУП.ОтразитьДокументыВУчетеНДС(ПериодРасчетаНДС, МассивДокументов);
	
КонецПроцедуры

Процедура ВыполнитьОффлайновыйРасчет(МассивДокументов, ПроцедураРасчета)
	
	ДокументовНаПорцию = 1000;
	КоличествоДокументов = МассивДокументов.Количество();
	
	Если НЕ ПроцедураРасчета.МногопоточноеВыполнение Тогда
		КоличествоПотоков = 1;
	ИначеЕсли КоличествоДокументов <= ДокументовНаПорцию Тогда
		КоличествоПотоков = 1;
	Иначе
		КоличествоПотоков = КоличествоПотоковОтраженияВРеглУчете();
	КонецЕсли;
	
	Если КоличествоПотоков = 1 Тогда
		ВыполнитьОффлайновыйРасчетТекущимПотоком(МассивДокументов, ПроцедураРасчета.ИмяМетода);
	Иначе
		ТаблицаУправленияПотоками = Новый ТаблицаЗначений();
		ТаблицаУправленияПотоками.Колонки.Добавить("ИмяМетода", ОбщегоНазначения.ОписаниеТипаСтрока(250));
		ТаблицаУправленияПотоками.Колонки.Добавить("Документы", Новый ОписаниеТипов("Массив"));
		ТаблицаУправленияПотоками.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("УникальныйИдентификатор"));
		ТаблицаУправленияПотоками.Колонки.Добавить("КоличествоПопыток", ОбщегоНазначения.ОписаниеТипаЧисло(3, 0));
		
		КоличествоПорций = Цел(КоличествоДокументов / ДокументовНаПорцию) + 1;
		
		Для НомерПорции = 1 По КоличествоПорций Цикл
			ПорцияДокументов = Новый Массив();
			
			Для НомерДокумента = 1 По ДокументовНаПорцию Цикл
				ИндексДокумента = (НомерПорции - 1) * ДокументовНаПорцию + НомерДокумента - 1;
				Если ИндексДокумента < КоличествоДокументов Тогда
					ПорцияДокументов.Добавить(МассивДокументов[ИндексДокумента]);
				Иначе
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Если ПорцияДокументов.Количество() > 0 Тогда
				ПараметрыПотока = ТаблицаУправленияПотоками.Добавить();
				ПараметрыПотока.ИмяМетода = ПроцедураРасчета.ИмяМетода;
				ПараметрыПотока.Документы = ПорцияДокументов;
				
				НачатьВыполнениеОффлайновогоРасчетаНовымПотоком(ПараметрыПотока);
				
				Если ТаблицаУправленияПотоками.Количество() >= КоличествоПотоков Тогда
					ОжидатьЗавершенияОффлайновогоРасчета(ТаблицаУправленияПотоками, Ложь);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		Если ТаблицаУправленияПотоками.Количество() > 0 Тогда
			ОжидатьЗавершенияОффлайновогоРасчета(ТаблицаУправленияПотоками, Истина);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьОффлайновыйРасчетТекущимПотоком(МассивДокументов, ИмяМетода)
	
	ОбщийМодуль = "РеглУчетПроведениеСервер";
	ПолноеИмяМетода = ОбщийМодуль + "." + ИмяМетода;
	
	ПараметрыВыполнения = Новый Массив();
	ПараметрыВыполнения.Добавить(МассивДокументов);
	
	ОбщегоНазначения.ВыполнитьМетодКонфигурации(ПолноеИмяМетода, ПараметрыВыполнения);
	
КонецПроцедуры

Процедура НачатьВыполнениеОффлайновогоРасчетаНовымПотоком(ПараметрыПотока)
	
	ОбщийМодуль = "РеглУчетПроведениеСервер";
	ПолноеИмяМетода = ОбщийМодуль + "." + ПараметрыПотока.ИмяМетода;
	
	Наименование = НСтр("ru = 'Доформирование движений документов перед отражением в регламентированном учете';
						|en = 'Finalize the generation of document register records before recording them in local accounting'");
	
	ПараметрыВыполнения = Новый Массив();
	ПараметрыВыполнения.Добавить(ПараметрыПотока.Документы);
	
	НовоеЗадание = ФоновыеЗадания.Выполнить(ПолноеИмяМетода, ПараметрыВыполнения,, Наименование);
	ПараметрыПотока.Идентификатор = НовоеЗадание.УникальныйИдентификатор;
	ПараметрыПотока.КоличествоПопыток = ПараметрыПотока.КоличествоПопыток + 1;
	
КонецПроцедуры

Процедура ОжидатьЗавершенияОффлайновогоРасчета(ТаблицаУправленияПотоками, ЗавершениеВсехПотоков)
	
	ВремяОжидания = 60;
	КоличествоПопыток = 3;
	
	АктивныеЗадания = Новый Массив();
	АварийныеЗадания = Новый Массив();
	ЗавершенныеЗадания = Новый Массив();
	
	Для Каждого ПараметрыПотока Из ТаблицаУправленияПотоками Цикл
		ФоновоеЗадание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(ПараметрыПотока.Идентификатор);
		Если ФоновоеЗадание = Неопределено Тогда
			АварийныеЗадания.Добавить(ПараметрыПотока.Идентификатор);
		ИначеЕсли ФоновоеЗадание.Состояние = СостояниеФоновогоЗадания.Активно Тогда
			АктивныеЗадания.Добавить(ФоновоеЗадание);
		ИначеЕсли ФоновоеЗадание.Состояние = СостояниеФоновогоЗадания.Завершено Тогда
			ЗавершенныеЗадания.Добавить(ПараметрыПотока.Идентификатор);
		Иначе
			АварийныеЗадания.Добавить(ПараметрыПотока.Идентификатор);
		КонецЕсли;
	КонецЦикла;
	
	Пока (ЗавершенныеЗадания.Количество() = 0 ИЛИ ЗавершениеВсехПотоков) И АктивныеЗадания.Количество() > 0 Цикл
		АктивныеЗадания = ФоновыеЗадания.ОжидатьЗавершенияВыполнения(АктивныеЗадания, ВремяОжидания);
		КоличествоЗаданий = АктивныеЗадания.Количество();
		
		Для НомерЗадания = 1 По КоличествоЗаданий Цикл
			ОбратныйИндекс = КоличествоЗаданий - НомерЗадания;
			ФоновоеЗадание = АктивныеЗадания[ОбратныйИндекс];
			
			Если ФоновоеЗадание.Состояние <> СостояниеФоновогоЗадания.Активно Тогда
				Если ФоновоеЗадание.Состояние = СостояниеФоновогоЗадания.Завершено Тогда
					ЗавершенныеЗадания.Добавить(ФоновоеЗадание.УникальныйИдентификатор);
				Иначе
					АварийныеЗадания.Добавить(ФоновоеЗадание.УникальныйИдентификатор);
				КонецЕсли;
				АктивныеЗадания.Удалить(ОбратныйИндекс);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Для Каждого ЗавершенноеЗадание Из ЗавершенныеЗадания Цикл
		ПараметрыПотока = ТаблицаУправленияПотоками.Найти(ЗавершенноеЗадание, "Идентификатор");
		Если ПараметрыПотока <> Неопределено Тогда
			ТаблицаУправленияПотоками.Удалить(ПараметрыПотока);
		КонецЕсли;
	КонецЦикла;
	
	Если АварийныеЗадания.Количество() > 0 Тогда
		ТекстыСообщений = Новый Массив();
		
		Для Каждого АварийноеЗадание Из АварийныеЗадания Цикл
			ПараметрыПотока = ТаблицаУправленияПотоками.Найти(АварийноеЗадание, "Идентификатор");
			
			Если ПараметрыПотока = Неопределено Тогда
			ИначеЕсли ПараметрыПотока.КоличествоПопыток < КоличествоПопыток Тогда
				НачатьВыполнениеОффлайновогоРасчетаНовымПотоком(ПараметрыПотока);
			Иначе
				ФоновоеЗадание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(АварийноеЗадание);
				Если ФоновоеЗадание = Неопределено Тогда
					ТекстСообщения = СтрШаблон(НСтр("ru = 'Фоновое задание с идентификатором %1 не найдено.';
													|en = 'A background job with the %1 ID is not found.'"), АварийноеЗадание);
				ИначеЕсли ФоновоеЗадание.Состояние = СостояниеФоновогоЗадания.Отменено Тогда
					ТекстСообщения = СтрШаблон(НСтр("ru = 'Фоновое задание с идентификатором %1 отменено администратором.';
													|en = 'The background job with the %1 ID is canceled by the administrator.'"), АварийноеЗадание);
				ИначеЕсли ФоновоеЗадание.Состояние = СостояниеФоновогоЗадания.ЗавершеноАварийно Тогда
					ТекстСообщения = СтрШаблон(НСтр("ru = 'Фоновое задание с идентификатором %1 завершено аварийно.';
													|en = 'The background job with the %1 ID crashed.'"), АварийноеЗадание)
						+ Символы.ПС + КраткоеПредставлениеОшибки(ФоновоеЗадание.ИнформацияОбОшибке);
				Иначе
					ТекстСообщения = СтрШаблон(НСтр("ru = 'Фоновое задание с идентификатором %1 не выполнено.';
													|en = 'The background job with the %1 ID is not performed.'"), АварийноеЗадание);
				КонецЕсли;
				
				ТекстыСообщений.Добавить(ТекстСообщения);
				
				Если ФоновоеЗадание = Неопределено ИЛИ ФоновоеЗадание.ИнформацияОбОшибке = Неопределено Тогда
					ПредставлениеОшибки = ТекстСообщения;
				Иначе
					ПредставлениеОшибки = СтрШаблон(НСтр("ru = 'Фоновое задание с идентификатором %1 завершено аварийно.';
														|en = 'The background job with the %1 ID crashed.'"), АварийноеЗадание)
						+ Символы.ПС + ПодробноеПредставлениеОшибки(ФоновоеЗадание.ИнформацияОбОшибке);
				КонецЕсли;
				
				ЗаписьЖурналаРегистрации(
					НСтр("ru = 'Доформирование движений документов перед отражением в регламентированном учете';
						|en = 'Finalize the generation of document register records before recording them in local accounting'", ОбщегоНазначения.КодОсновногоЯзыка()),
					УровеньЖурналаРегистрации.Ошибка,,,
					ПредставлениеОшибки);
			КонецЕсли;
		КонецЦикла;
		
		Если ТекстыСообщений.Количество() > 0 Тогда
			ТекстыСообщений.Вставить(0, НСтр("ru = 'При доформировании движений документов перед отражением в регл. учете возникли ошибки.';
											|en = 'Errors occurred when finalizing the generation of document register records before recording them in local accounting.'"));
			ТекстСообщения = СтрСоединить(ОбщегоНазначенияКлиентСервер.СвернутьМассив(ТекстыСообщений), Символы.ПС);
			ВызватьИсключение ТекстСообщения;
		ИначеЕсли ЗавершенныеЗадания.Количество() = 0 ИЛИ ЗавершениеВсехПотоков Тогда
			ОжидатьЗавершенияОффлайновогоРасчета(ТаблицаУправленияПотоками, ЗавершениеВсехПотоков);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область УправлениеМногопоточнымПакетнымОтражением

Функция КоличествоПотоковОтраженияВРеглУчете()
	
	КоличествоПотоковСРазделением = 1;
	КоличествоПотоковБезРазделения = 6;
	
	Если НЕ ВозможноМногопоточноеОтражение() Тогда
		КоличествоПотоков = 1;
	ИначеЕсли ОбщегоНазначения.РазделениеВключено() Тогда
		КоличествоПотоков = КоличествоПотоковСРазделением;
	Иначе
		КоличествоПотоков = КоличествоПотоковБезРазделения;
	КонецЕсли;
	
	Возврат КоличествоПотоков;
	
КонецФункции

Функция ВозможноМногопоточноеОтражение()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат НЕ ОбщегоНазначения.РежимОтладки()
		И НЕ ОбщегоНазначения.ИнформационнаяБазаФайловая()
		И РегистрыБухгалтерии.Хозрасчетный.ПолучитьРежимРазделенияИтогов();
	
КонецФункции

Функция КоличествоПроводокНаПорцию(КоличествоПотоков = 1)
	
	Если КоличествоПотоков > 10 Тогда
		ПроводокНаПорцию = 2000;
	ИначеЕсли КоличествоПотоков > 3 Тогда
		ПроводокНаПорцию = 3000;
	ИначеЕсли КоличествоПотоков > 1 Тогда
		ПроводокНаПорцию = 4000;
	Иначе
		ПроводокНаПорцию = 5000;
	КонецЕсли;
	
	Возврат ПроводокНаПорцию;
	
КонецФункции

Процедура ЗаполнитьПрогнозныеПараметры(ПараметрыОтражения, ТипыДокументов)
	
	ПрогнозныеПараметры = ПрогнозныеПараметрыОтраженияВРеглУчете();
	
	МесяцевСтатистики = 1;
	КоличествоПроводокБезСтатистики = 10;
	РассчитыватьПрогнозныеПараметры = ПолучитьФункциональнуюОпцию("ИспользоватьРеглУчет");
	
	Если ПараметрыОтражения.Документы = Неопределено Тогда
	ИначеЕсли ТипЗнч(ПараметрыОтражения.Документы) <> Тип("Массив")
		ИЛИ ПараметрыОтражения.Документы.Количество() = 1 Тогда
		ПрогнозныеПараметры.КоличествоДокументов = 1;
		РассчитыватьПрогнозныеПараметры = Ложь;
	КонецЕсли;
	
	Если РассчитыватьПрогнозныеПараметры Тогда
		Запрос = Новый Запрос();
		Запрос.Текст = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТИПЗНАЧЕНИЯ(Данные.Регистратор) КАК ТипРегистратора,
		|	МИНИМУМ(Данные.ДатаОтражения) КАК ДатаНачала,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Данные.Регистратор) КАК КоличествоДокументов
		|ИЗ
		|	РегистрСведений.ОтражениеДокументовВРеглУчете КАК Данные
		|	ЛЕВОЕ СОЕДИНЕНИЕ ПроверкаДокументовПереопределяемый КАК ПроверкаДокументовПереопределяемый
		|		ПО Данные.Организация = ПроверкаДокументовПереопределяемый.Организация
		|		 И Данные.Регистратор = ПроверкаДокументовПереопределяемый.Документ
		|		 И &ОтражатьВУчетеТолькоПроверенныеДокументы
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Константы КАК Константы
		|		ПО (ИСТИНА)
		|ГДЕ
		|	Данные.ДатаОтражения >= &ДатаЗапрета
		|	И Данные.ДатаОтражения <= &ПериодОтражения
		|	И Данные.Организация В (&Организации)
		|	И Данные.Регистратор В (&МассивСсылок)
		|	И НЕ Данные.Регистратор В (&МассивИсключаемыхСсылок)
		|	И ТИПЗНАЧЕНИЯ(Данные.Регистратор) В (&ТипыДокументов)
		|	И Данные.Статус В (&СтатусыОтражения)
		|	И (Данные.ДатаОтражения >= Константы.ДатаНачалаВеденияРеглУчета)
		|	И &ДокументПроверен
		|СГРУППИРОВАТЬ ПО
		|	ТИПЗНАЧЕНИЯ(Данные.Регистратор)
		|ИТОГИ
		|	МИНИМУМ(ДатаНачала) КАК ДатаНачала,
		|	СУММА(КоличествоДокументов) КАК КоличествоДокументов
		|ПО
		|	ОБЩИЕ
		|";
		
		ПроверкаДокументовСервер.ПереопределитьВТекстеЗапросаСтатусыПроверкиСУчетомИсключений(Запрос.Текст, "Данные.Регистратор");
		УстановитьОтборДокументовПоПараметрамОтражения(Запрос, ПараметрыОтражения);
		ДобавитьВЗапросФильтрОтраженияВРеглУчете(Запрос.Текст, "Данные.Регистратор");
		
		Запрос.УстановитьПараметр("СтатусыОтражения", СтатусыДокументовКОтражению());
		Запрос.УстановитьПараметр("ТипыДокументов",   ТипыДокументов);
		
		Результат = Запрос.Выполнить();
		
		ВыборкаДокументыОбщийИтог = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		ВыборкаДокументыОбщийИтог.Следующий();
		
		Если ЗначениеЗаполнено(ВыборкаДокументыОбщийИтог.КоличествоДокументов) Тогда
			ПрогнозныеПараметры.ДатаПервойПроводки = ВыборкаДокументыОбщийИтог.ДатаНачала;
			ПрогнозныеПараметры.КоличествоДокументов = ВыборкаДокументыОбщийИтог.КоличествоДокументов;
			
			Запрос.УстановитьПараметр("ДатаНачалаСтатистики", ДобавитьМесяц(НачалоМесяца(ВыборкаДокументыОбщийИтог.ДатаНачала), -МесяцевСтатистики));
			Запрос.УстановитьПараметр("ДатаОкончанияСтатистики", НачалоМесяца(ВыборкаДокументыОбщийИтог.ДатаНачала) - 1);
			
			Запрос.Текст = "
			|ВЫБРАТЬ
			|	ТИПЗНАЧЕНИЯ(Хозрасчетный.Регистратор) КАК ТипРегистратора,
			|	ВЫРАЗИТЬ(КОЛИЧЕСТВО(Хозрасчетный.Регистратор) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Хозрасчетный.Регистратор) КАК ЧИСЛО(31,2)) КАК КоличествоПроводок
			|ИЗ
			|	РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
			|ГДЕ
			|	Хозрасчетный.Период МЕЖДУ &ДатаНачалаСтатистики И &ДатаОкончанияСтатистики
			|	И ТИПЗНАЧЕНИЯ(Хозрасчетный.Регистратор) В (&ТипыДокументов)
			|	И Хозрасчетный.Организация В (&Организации)
			|СГРУППИРОВАТЬ ПО
			|	ТИПЗНАЧЕНИЯ(Хозрасчетный.Регистратор)
			|ИТОГИ
			|	ВЫРАЗИТЬ(СРЕДНЕЕ(КоличествоПроводок) КАК ЧИСЛО(31,2)) КАК КоличествоПроводок
			|ПО
			|	ОБЩИЕ
			|";
			
			Если НЕ ЗначениеЗаполнено(ПараметрыОтражения.Организации) Тогда
				Запрос.Текст = СтрЗаменить(Запрос.Текст, "Хозрасчетный.Организация В (&Организации)", "ИСТИНА");
			КонецЕсли;
			
			Результат = Запрос.Выполнить();
			
			ВыборкаПроводкиОбщийИтог = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			ВыборкаПроводкиОбщийИтог.Следующий();
			
			ВыборкаПроводки = ВыборкаПроводкиОбщийИтог.Выбрать();
			ВыборкаДокументы = ВыборкаДокументыОбщийИтог.Выбрать();
			
			Пока ВыборкаДокументы.Следующий() Цикл
				ВыборкаПроводки.Сбросить();
				СтруктураПоиска = Новый Структура("ТипРегистратора", ВыборкаДокументы.ТипРегистратора);
				
				Если ВыборкаПроводки.НайтиСледующий(СтруктураПоиска) Тогда
					УдельноеКоличествоПроводок = ВыборкаПроводки.КоличествоПроводок;
				ИначеЕсли ЗначениеЗаполнено(ВыборкаПроводкиОбщийИтог.КоличествоПроводок) Тогда
					УдельноеКоличествоПроводок = ВыборкаПроводкиОбщийИтог.КоличествоПроводок;
				Иначе
					УдельноеКоличествоПроводок = КоличествоПроводокБезСтатистики;
				КонецЕсли;
				
				КоличествоПроводок = Окр(УдельноеКоличествоПроводок * ВыборкаДокументы.КоличествоДокументов, 0);
				ПрогнозныеПараметры.КоличествоПроводок = ПрогнозныеПараметры.КоличествоПроводок + КоличествоПроводок;
				
				ПараметрыПоДокументу = Новый Структура("ТипДокумента,КоличествоДокументов,КоличествоПроводок",
					ВыборкаДокументы.ТипРегистратора, ВыборкаДокументы.КоличествоДокументов, КоличествоПроводок);
				ПрогнозныеПараметры.ПоТипамДокументов.Добавить(ПараметрыПоДокументу);
			КонецЦикла;
		КонецЕсли;
	Иначе
		ПрогнозныеПараметры.КоличествоПроводок = КоличествоПроводокБезСтатистики * ПрогнозныеПараметры.КоличествоДокументов;
	КонецЕсли;
	
	Если ПрогнозныеПараметры.КоличествоДокументов > 0 Тогда
		ПроводокНаПорцию = КоличествоПроводокНаПорцию(ПараметрыОтражения.КоличествоПотоков);
		
		Если ПараметрыОтражения.КоличествоПотоков = 1 Тогда
		ИначеЕсли ПрогнозныеПараметры.КоличествоПроводок >= ПроводокНаПорцию * ПараметрыОтражения.КоличествоПотоков Тогда
		Иначе
			ПроводокНаПорцию = Окр(ПрогнозныеПараметры.КоличествоПроводок / ПараметрыОтражения.КоличествоПотоков, 0);
			ПроводокНаПорцию = Макс(ПроводокНаПорцию, ПараметрыОтражения.МинимальныйРазмерПорции);
		КонецЕсли;
		
		ПрогнозныеПараметры.ПроводокНаПорцию = ПроводокНаПорцию;
	КонецЕсли;
	
	ПараметрыОтражения.ПрогнозныеПараметры = ПрогнозныеПараметры;
	
КонецПроцедуры

Функция РекомендуетсяВыключениеИтоговРегистра(ПараметрыОтражения)
	
	ПрогнозныеПараметры = ПараметрыОтражения.ПрогнозныеПараметры;
	
	Возврат НЕ ОбщегоНазначения.ИнформационнаяБазаФайловая()
		И ПрогнозныеПараметры.КоличествоДокументов > 0
		И ПрогнозныеПараметры.КоличествоПроводок >= ПрогнозныеПараметры.ПроводокНаПорцию;
	
КонецФункции

Процедура ВыключитьИтогиРегистраБухгалтерии(ПараметрыОтражения)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИспользованиеТекущихИтогов = РегистрыБухгалтерии.Хозрасчетный.ПолучитьИспользованиеТекущихИтогов();
	МаксимальныйПериодИтогов = РегистрыБухгалтерии.Хозрасчетный.ПолучитьМаксимальныйПериодРассчитанныхИтогов();
	МинимальныйПериодИтогов = РегистрыБухгалтерии.Хозрасчетный.ПолучитьМинимальныйПериодРассчитанныхИтогов();
	
	ПараметрыОтражения.ИспользованиеТекущихИтогов = ИспользованиеТекущихИтогов;
	ПараметрыОтражения.МаксимальныйПериодИтогов = МаксимальныйПериодИтогов;
	ПараметрыОтражения.МинимальныйПериодИтогов = МинимальныйПериодИтогов;
	
	КоличествоПопытокОтключенияИтогов = ПараметрыОтражения.КоличествоПопыток * 2;
	РегистрыБухгалтерииМетаданные = Метаданные.РегистрыБухгалтерии;
	
	Если ИспользованиеТекущихИтогов Тогда
		Для НомерПопытки = 1 По КоличествоПопытокОтключенияИтогов Цикл
			Попытка
				РегистрыБухгалтерии.Хозрасчетный.УстановитьИспользованиеТекущихИтогов(Ложь);
				ПараметрыОтражения.ВосстановитьСостояниеИтогов = Истина;
				Прервать;
			Исключение
				Если НомерПопытки = КоличествоПопытокОтключенияИтогов Тогда
					ЗаписьЖурналаРегистрации(
						НСтр("ru = 'Отражение в регламентированном учете';
							|en = 'Record in local accounting'", ОбщегоНазначения.КодОсновногоЯзыка()),
						УровеньЖурналаРегистрации.Ошибка,
						РегистрыБухгалтерииМетаданные.Хозрасчетный.Имя,,
						НСтр("ru = 'Выключение использования текущих итогов завершилось с ошибкой.';
							|en = 'An error occurred when disabling the current totals.'")
							+ Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				КонецЕсли;
			КонецПопытки;
		КонецЦикла;
	КонецЕсли;
	
	ДатаПервойПроводки = ПараметрыОтражения.ПрогнозныеПараметры.ДатаПервойПроводки;
	ПериодОтключения = ДобавитьМесяц(НачалоМесяца(ДатаПервойПроводки), -1);
	
	МаксимальныйПериодОтключения = Мин(МаксимальныйПериодИтогов, КонецМесяца(ПериодОтключения));
	МинимальныйПериодОтключения = Мин(МинимальныйПериодИтогов, ПериодОтключения);
	
	Если МаксимальныйПериодОтключения <> МаксимальныйПериодИтогов
	 ИЛИ МинимальныйПериодОтключения <> МинимальныйПериодИтогов Тогда
		Для НомерПопытки = 1 По КоличествоПопытокОтключенияИтогов Цикл
			Попытка
				РегистрыБухгалтерии.Хозрасчетный.УстановитьМинимальныйИМаксимальныйПериодыРассчитанныхИтогов(
					МинимальныйПериодОтключения, МаксимальныйПериодОтключения);
				ПараметрыОтражения.ВосстановитьСостояниеИтогов = Истина;
				Прервать;
			Исключение
				Если НомерПопытки = КоличествоПопытокОтключенияИтогов Тогда
					ЗаписьЖурналаРегистрации(
						НСтр("ru = 'Отражение в регламентированном учете';
							|en = 'Record in local accounting'", ОбщегоНазначения.КодОсновногоЯзыка()),
						УровеньЖурналаРегистрации.Ошибка,
						РегистрыБухгалтерииМетаданные.Хозрасчетный.Имя,,
						НСтр("ru = 'Сдвиг хранимых периодов итогов перед отражением завершился с ошибкой.';
							|en = 'An error occurred when shifting stored total periods before recording.'")
							+ Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				КонецЕсли;
			КонецПопытки;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ВключитьИтогиРегистраБухгалтерии(ПараметрыОтражения)
	
	УстановитьПривилегированныйРежим(Истина);
	
	КоличествоПопытокВосстановленияИтогов = ПараметрыОтражения.КоличествоПопыток * 2;
	РегистрыБухгалтерииМетаданные = Метаданные.РегистрыБухгалтерии;
	
	Если ПараметрыОтражения.ИспользованиеТекущихИтогов
		<> РегистрыБухгалтерии.Хозрасчетный.ПолучитьИспользованиеТекущихИтогов() Тогда
		Для НомерПопытки = 1 По КоличествоПопытокВосстановленияИтогов Цикл
			Попытка
				РегистрыБухгалтерии.Хозрасчетный.УстановитьИспользованиеТекущихИтогов(
					ПараметрыОтражения.ИспользованиеТекущихИтогов);
				Прервать;
			Исключение
				Если НомерПопытки = КоличествоПопытокВосстановленияИтогов Тогда
					ЗаписьЖурналаРегистрации(
						НСтр("ru = 'Отражение в регламентированном учете';
							|en = 'Record in local accounting'", ОбщегоНазначения.КодОсновногоЯзыка()),
						УровеньЖурналаРегистрации.Ошибка,
						РегистрыБухгалтерииМетаданные.Хозрасчетный.Имя,,
						НСтр("ru = 'Включение использования текущих итогов завершилось с ошибкой.';
							|en = 'An error occurred when enabling the current totals.'")
							+ Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				КонецЕсли;
			КонецПопытки;
		КонецЦикла;
	КонецЕсли;
	
	Если (ПараметрыОтражения.МаксимальныйПериодИтогов
		<> РегистрыБухгалтерии.Хозрасчетный.ПолучитьМаксимальныйПериодРассчитанныхИтогов())
	 ИЛИ (ПараметрыОтражения.МинимальныйПериодИтогов
		<> РегистрыБухгалтерии.Хозрасчетный.ПолучитьМинимальныйПериодРассчитанныхИтогов()) Тогда
		Для НомерПопытки = 1 По КоличествоПопытокВосстановленияИтогов Цикл
			Попытка
				РегистрыБухгалтерии.Хозрасчетный.УстановитьМинимальныйИМаксимальныйПериодыРассчитанныхИтогов(
					ПараметрыОтражения.МинимальныйПериодИтогов,
					ПараметрыОтражения.МаксимальныйПериодИтогов);
				Прервать;
			Исключение
				Если НомерПопытки = КоличествоПопытокВосстановленияИтогов Тогда
					ЗаписьЖурналаРегистрации(
						НСтр("ru = 'Отражение в регламентированном учете';
							|en = 'Record in local accounting'", ОбщегоНазначения.КодОсновногоЯзыка()),
						УровеньЖурналаРегистрации.Ошибка,
						РегистрыБухгалтерииМетаданные.Хозрасчетный.Имя,,
						НСтр("ru = 'Восстановление хранимых периодов итогов после отражения завершилось с ошибкой.';
							|en = 'An error occurred when restoring stored total periods after recording.'")
							+ Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				КонецЕсли;
			КонецПопытки;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Функция ТаблицаУправленияПорциями(ПараметрыОтражения, ОдинТипНаПорцию)
	
	ТаблицаУправления = Новый ТаблицаЗначений();
	ТаблицаУправления.Колонки.Добавить("НомерПорции", ОбщегоНазначения.ОписаниеТипаЧисло(5, 0));
	ТаблицаУправления.Колонки.Добавить("ТипыДокументов", Новый ОписаниеТипов("Массив"));
	ТаблицаУправления.Колонки.Добавить("КоличествоДокументов", ОбщегоНазначения.ОписаниеТипаЧисло(10, 0));
	ТаблицаУправления.Колонки.Добавить("КоличествоПроводок", ОбщегоНазначения.ОписаниеТипаЧисло(15, 0));
	ТаблицаУправления.Колонки.Добавить("КоличествоПорций", ОбщегоНазначения.ОписаниеТипаЧисло(10, 0));
	ТаблицаУправления.Колонки.Добавить("НачальныйМоментВремени", Новый ОписаниеТипов("МоментВремени"));
	ТаблицаУправления.Колонки.Добавить("КонечныйМоментВремени", Новый ОписаниеТипов("МоментВремени"));
	ТаблицаУправления.Колонки.Добавить("ПоследняяПорция", Новый ОписаниеТипов("Булево"));
	
	ПрогнозныеПараметры = ПараметрыОтражения.ПрогнозныеПараметры;
	
	Для Каждого ПараметрыТипа Из ПрогнозныеПараметры.ПоТипамДокументов Цикл
		ДобавитьНовуюСтроку = Истина;
		
		Если НЕ ОдинТипНаПорцию И ПараметрыТипа.КоличествоПроводок < ПрогнозныеПараметры.ПроводокНаПорцию Тогда
			Для Каждого ПараметрыПорции Из ТаблицаУправления Цикл
				Если (ПараметрыПорции.КоличествоПроводок + ПараметрыТипа.КоличествоПроводок) <= ПрогнозныеПараметры.ПроводокНаПорцию Тогда
					ПараметрыПорции.КоличествоДокументов = ПараметрыПорции.КоличествоДокументов + ПараметрыТипа.КоличествоДокументов;
					ПараметрыПорции.КоличествоПроводок = ПараметрыПорции.КоличествоПроводок + ПараметрыТипа.КоличествоПроводок;
					ПараметрыПорции.ТипыДокументов.Добавить(ПараметрыТипа.ТипДокумента);
					ДобавитьНовуюСтроку = Ложь;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если ДобавитьНовуюСтроку Тогда
			ПараметрыПорции = ТаблицаУправления.Добавить();
			ЗаполнитьЗначенияСвойств(ПараметрыПорции, ПараметрыТипа);
			ПараметрыПорции.ТипыДокументов.Добавить(ПараметрыТипа.ТипДокумента);
			ПараметрыПорции.КоличествоПорций = 1 + Цел(ПараметрыПорции.КоличествоПроводок / ПрогнозныеПараметры.ПроводокНаПорцию);
		КонецЕсли;
	КонецЦикла;
	
	ТаблицаУправления.Сортировать("КоличествоПроводок УБЫВ");
	
	ОбщегоНазначенияУТ.ДобавитьИдентификаторСтрокВТаблицу(ТаблицаУправления, "НомерПорции");
	
	Возврат ТаблицаУправления;
	
КонецФункции

Функция ТаблицаУправленияПотоками()
	
	ТаблицаУправления = Новый ТаблицаЗначений();
	ТаблицаУправления.Колонки.Добавить("НомерПорции", ОбщегоНазначения.ОписаниеТипаЧисло(5, 0));
	ТаблицаУправления.Колонки.Добавить("ТипыДокументов", Новый ОписаниеТипов("Массив"));
	ТаблицаУправления.Колонки.Добавить("НачальныйМоментВремени", Новый ОписаниеТипов("МоментВремени"));
	ТаблицаУправления.Колонки.Добавить("КонечныйМоментВремени", Новый ОписаниеТипов("МоментВремени"));
	ТаблицаУправления.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("УникальныйИдентификатор"));
	ТаблицаУправления.Колонки.Добавить("АдресРезультата", ОбщегоНазначения.ОписаниеТипаСтрока(100));
	ТаблицаУправления.Колонки.Добавить("КоличествоПопыток", ОбщегоНазначения.ОписаниеТипаЧисло(3, 0));
	
	Возврат ТаблицаУправления;
	
КонецФункции

Процедура ДополнитьПараметрыЕдинственнойПорции(ПараметрыОтражения, ПараметрыПорции)
	
	Для Каждого ТипДокумента Из ПараметрыПорции.ТипыДокументов Цикл
		ПараметрыПорции.НачальныйМоментВремени = Новый МоментВремени('00010101');
		ПараметрыПорции.КонечныйМоментВремени = Новый МоментВремени('00010101');
		СформироватьТаблицыОтбораДанных(ТипДокумента, Неопределено, ПараметрыОтражения, ПараметрыПорции);
	КонецЦикла;
	
	ПараметрыПорции.НачальныйМоментВремени = Новый МоментВремени('00010101');
	ПараметрыПорции.КонечныйМоментВремени = Новый МоментВремени('39991231235959');
	ПараметрыПорции.ПоследняяПорция = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ФормированиеИЗаписьДвижений

Процедура СформироватьОтражениеДокументовВРеглУчете(ВыборкаСтатусов, ОшибкиОтражения)
	
	СтатусыОтраженияВРеглУчете = Перечисления.СтатусыОтраженияДокументовВРеглУчете;
	
	ОтражениеВРеглУчете = РегистрыСведений.ОтражениеДокументовВРеглУчете.СоздатьНаборЗаписей();
	ОтражениеВРеглУчете.Отбор.Регистратор.Установить(ВыборкаСтатусов.Ссылка);
	
	ВыборкаСтатусовПоОрганизациям = ВыборкаСтатусов.Выбрать();
	
	ШаблонЗапретаДанных = ДатыЗапретаИзменения.ШаблонДанныхДляПроверки();
	Пока ВыборкаСтатусовПоОрганизациям.Следующий() Цикл
		
		ОбновитьСтатус = (ВыборкаСтатусовПоОрганизациям.Статус = Неопределено);
		
		НоваяЗапись = ОтражениеВРеглУчете.Добавить();
		
		ЗаполнитьЗначенияСвойств(НоваяЗапись, ВыборкаСтатусовПоОрганизациям);
		
		Если ОбновитьСтатус Тогда
			Отбор = Новый Структура("Организация, ДатаОтражения", НоваяЗапись.Организация, НоваяЗапись.ДатаОтражения);
			Ошибки = ОшибкиОтражения.НайтиСтроки(Отбор);
			Если Ошибки.Количество() > 0 Тогда
				НоваяЗапись.Статус = СтатусыОтраженияВРеглУчете.НеУказаныСчетаУчета;
				НоваяЗапись.Комментарий = Ошибки[0].Комментарий;
			Иначе
				НоваяЗапись.Статус = СтатусыОтраженияВРеглУчете.ОтраженоВРеглУчете;
				НоваяЗапись.Комментарий = "";
			КонецЕсли;
			Если ТипЗнч(ВыборкаСтатусов.Ссылка) = Тип("ДокументСсылка.КорректировкаРеализации")
				И ВыборкаСтатусовПоОрганизациям.ДатаОтражения <> НачалоДня(ВыборкаСтатусовПоОрганизациям.Период) Тогда
				Продолжить;
			КонецЕсли;
			СтрокаШаблона = ШаблонЗапретаДанных.Добавить();
			СтрокаШаблона.Дата   = ВыборкаСтатусовПоОрганизациям.ДатаОтражения;
			СтрокаШаблона.Раздел = "БухгалтерскийУчет";
			СтрокаШаблона.Объект = ВыборкаСтатусовПоОрганизациям.Организация;
		КонецЕсли;
		
	КонецЦикла;
	
	ПроверитьДатуЗапретаДляОтраженияДокументовВРеглУчете(ШаблонЗапретаДанных, ОтражениеВРеглУчете, ВыборкаСтатусов.Ссылка);

	ДанныеВыборочнойПроверкиБлокировкиЗакрытияМесяца = ДанныеВыборочнойПроверкиБлокировкиЗакрытияМесяца(ВыборкаСтатусов.Ссылка, 
		ОтражениеВРеглУчете); 
	
	Если ДанныеВыборочнойПроверкиБлокировкиЗакрытияМесяца.Количество() <> 0 Тогда
		ЗакрытиеМесяцаСервер.ОтключитьПроверкуБлокировкуПериодаДляНабораЗаписей(ОтражениеВРеглУчете);						
		ПроверитьБлокировкуЗакрытияМесяцаОтИзменений(ДанныеВыборочнойПроверкиБлокировкиЗакрытияМесяца);
	КонецЕсли;	
	
	ОтражениеВРеглУчете.Записать();
	
КонецПроцедуры

//++ НЕ УТКА

Процедура СформироватьОтражениеДокументовВМФУ(ВыборкаСтатусов, ВыборкаСтатусовМФУ, ОшибкиОтражения)
	
	Если ВыборкаСтатусов.Ссылка <> ВыборкаСтатусовМФУ.Ссылка Тогда
		Возврат;
	КонецЕсли;
	
	СтатусыОтраженияВМФУ = Перечисления.СтатусыОтраженияВМеждународномУчете;
	
	ОтражениеВМФУ = РегистрыСведений.ОтражениеДокументовВМеждународномУчете.СоздатьНаборЗаписей();
	ОтражениеВМФУ.Отбор.Регистратор.Установить(ВыборкаСтатусовМФУ.Ссылка);
	
	ВыборкаСтатусовМФУПоОрганизациям = ВыборкаСтатусовМФУ.Выбрать();
	Пока ВыборкаСтатусовМФУПоОрганизациям.Следующий() Цикл
		ОбновитьСтатус = ВыборкаСтатусовМФУПоОрганизациям.ОбновитьСтатус;
		НоваяЗапись = ОтражениеВМФУ.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, ВыборкаСтатусовМФУПоОрганизациям);
		Если ОбновитьСтатус Тогда
			Отбор = Новый Структура("Организация, ДатаОтражения", НоваяЗапись.Организация, НачалоДня(НоваяЗапись.Период));
			Ошибки = ОшибкиОтражения.НайтиСтроки(Отбор);
			Если Ошибки.Количество() > 0 Тогда
				НоваяЗапись.Статус = СтатусыОтраженияВМФУ.ОжидаетсяОтражениеВРеглУчете;
			Иначе
				Если ВыборкаСтатусовМФУПоОрганизациям.РучноеОтражение Тогда
					НоваяЗапись.Статус = СтатусыОтраженияВМФУ.КОтражениюВУчетеВручную;
				Иначе
					НоваяЗапись.Статус = СтатусыОтраженияВМФУ.КОтражениюВУчете;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	ОтражениеВМФУ.Записать();
	ВыборкаСтатусовМФУ.Следующий();
	
КонецПроцедуры
//-- НЕ УТКА

Процедура СформироватьХозрасчетный(ВыборкаСтатусов, ВыборкаХозрасчетный, ВыборкаХозрасчетныйДополнение, ВыборкаХозрасчетныйСторно)
	
	НаборЗаписей = РегистрыБухгалтерии.Хозрасчетный.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(ВыборкаСтатусов.Ссылка);
	УстановитьДопСвойстваНабораЗаписейХозрасчетный(НаборЗаписей, ВыборкаСтатусов.Ссылка);
	
	ТаблицаПроводок = ТаблицаПроводок(НаборЗаписей);
	Если ВыборкаСтатусов.Ссылка = ВыборкаХозрасчетный.Ссылка Тогда
		ДобавитьСтрокиВТаблицуПроводок(ТаблицаПроводок, ВыборкаХозрасчетный);
		ВыборкаХозрасчетный.Следующий();
	КонецЕсли;
		
	// Выполним доп. обработку новых проводок.
	НаборЗаписей.Загрузить(ТаблицаПроводок);
	РегистрыБухгалтерии.Хозрасчетный.ВыполнитьДопОбработкуПроводок(
		НаборЗаписей);
		
	// Дополним набор проводками, которые необходимо оставить без изменения.
	ТаблицаПроводок = ТаблицаПроводок(НаборЗаписей);
		
	Если ВыборкаСтатусов.Ссылка = ВыборкаХозрасчетныйСторно.Ссылка Тогда
		ДобавитьСтрокиВТаблицуПроводок(ТаблицаПроводок, ВыборкаХозрасчетныйСторно);
		ВыборкаХозрасчетныйСторно.Следующий();
	КонецЕсли;

	Если ВыборкаСтатусов.Ссылка = ВыборкаХозрасчетныйДополнение.Ссылка Тогда
		ДобавитьСтрокиВТаблицуПроводок(ТаблицаПроводок, ВыборкаХозрасчетныйДополнение);
		ВыборкаХозрасчетныйДополнение.Следующий();
	КонецЕсли;
	
	ТаблицаПроводок.Сортировать("Сторно Убыв, Период");
	
	НаборЗаписей.Загрузить(ТаблицаПроводок);
	// Выполним доп. обработку новых сторно- и доп. проводок
	РегистрыБухгалтерии.Хозрасчетный.ПривестиПустыеЗначенияСубконтоСоставногоТипа(НаборЗаписей);
	НаборЗаписей.Записать();
	
КонецПроцедуры

Функция ОшибкиОтраженияСЗаписьюВРегистрТребующихсяНастроек(Документ, ВыборкаПроверки)
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("Организация");
	Результат.Колонки.Добавить("ДатаОтражения");
	Результат.Колонки.Добавить("Комментарий");
	
	Если Документ <> ВыборкаПроверки.Ссылка Тогда
		Возврат Результат;
	КонецЕсли;
	
	ВыборкаПоОрганизации = ВыборкаПроверки.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоОрганизации.Следующий() Цикл
		ВыборкаПоМесяцуОтражения = ВыборкаПоОрганизации.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПоМесяцуОтражения.Следующий() Цикл
			НоваяСтрока = Результат.Добавить();
			НоваяСтрока.Организация   = ВыборкаПоМесяцуОтражения.Организация;
			НоваяСтрока.ДатаОтражения = ВыборкаПоМесяцуОтражения.ДатаОтражения;
			ТаблицаОшибок			  = ТаблицаОшибокОтражения(ВыборкаПоМесяцуОтражения);
			НоваяСтрока.Комментарий   = СтрСоединить(ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаОшибок, "Комментарий", Истина), Символы.ПС);
			ЗаписатьТребующиеНастройкиСчетаРеглУчета(ТаблицаОшибок, Документ);
		КонецЦикла;
	КонецЦикла;
	
	ВыборкаПроверки.Следующий();
	
	Возврат Результат;
	
КонецФункции

Функция ТаблицаПроводок(НаборЗаписей = Неопределено)
	
	Если НаборЗаписей = Неопределено Тогда
		НаборЗаписей = РегистрыБухгалтерии.Хозрасчетный.СоздатьНаборЗаписей();
	КонецЕсли;
	
	ТаблицаПроводок = НаборЗаписей.Выгрузить();
	ТаблицаПроводок.Колонки.Удалить("Активность");
	
	Возврат ТаблицаПроводок;
	
КонецФункции

Процедура ДобавитьСтрокиВТаблицуПроводок(ТаблицаПроводок, ВыборкаПоДокументу)
	
	Выборка = ВыборкаПоДокументу.Выбрать(); 
	
	Пока Выборка.Следующий() Цикл
		НоваяЗапись = ТаблицаПроводок.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
	КонецЦикла;
	
КонецПроцедуры

Процедура УстановитьДопСвойстваНабораЗаписейХозрасчетный(НаборЗаписей, Ссылка)
	
	ТипСсылки = ТипЗнч(Ссылка);
	
	Если ТипСсылки = Тип("ДокументСсылка.КорректировкаРеализации")
		Или ТипСсылки = Тип("ДокументСсылка.ВозвратТоваровОтКлиента")
		Или ТипСсылки = Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями")
		Или ТипСсылки = Тип("ДокументСсылка.РаспределениеПрочихЗатрат")
		Или ТипСсылки = Тип("ДокументСсылка.РасчетРезервовПодОбесценениеЗапасов")
		Или ТипСсылки = Тип("ДокументСсылка.РасчетКурсовыхРазниц")
		Или ВнеоборотныеАктивыЛокализация.СуммыНалоговогоУчетаЗаполнены(ТипСсылки)
		Тогда
		
		НаборЗаписей.ДополнительныеСвойства.Вставить("СуммыНалоговогоУчетаЗаполнены", Истина);
	КонецЕсли;
	
	НаборЗаписей.ДополнительныеСвойства.Вставить("ПропуститьПроверкуЗапретаИзменения", Истина);
	НаборЗаписей.ДополнительныеСвойства.Вставить("НеВыполнятьДопОбработкуПроводок", Истина);
	
КонецПроцедуры

Процедура ЗарегистрироватьСвязанныеДокументыКОтражению(ВыборкаСтатусов, ВыборкаСвязанныеДокументыКОтражению)
	
	Если ВыборкаСтатусов.Ссылка <> ВыборкаСвязанныеДокументыКОтражению.Ссылка Тогда
		Возврат;
	КонецЕсли;
	
	ВыборкаДокументы = ВыборкаСвязанныеДокументыКОтражению.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Если ВыборкаДокументы.Следующий() Тогда
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОтражениеДокументовВРеглУчете.НаборЗаписей");
		ЭлементБлокировки.УстановитьЗначение("Регистратор", ВыборкаДокументы.Регистратор);
		Блокировка.Заблокировать();
		
		ОтражениеВРеглУчете = РегистрыСведений.ОтражениеДокументовВРеглУчете.СоздатьНаборЗаписей();
		ОтражениеВРеглУчете.Отбор.Регистратор.Установить(ВыборкаДокументы.Регистратор);
		
		ВыборкаДетальныеЗаписи = ВыборкаДокументы.Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			НоваяЗапись = ОтражениеВРеглУчете.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ВыборкаДетальныеЗаписи);
		КонецЦикла;
		
		ОтражениеВРеглУчете.Записать();
	КонецЕсли;
	
	ВыборкаСвязанныеДокументыКОтражению.Следующий();
	
КонецПроцедуры

Функция ТаблицаОшибокОтражения(РезультатПроверки)
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("Организация");
	Результат.Колонки.Добавить("АналитикаУчета");
	Результат.Колонки.Добавить("МестоУчета");
	Результат.Колонки.Добавить("ВидСчета");
	Результат.Колонки.Добавить("Долгосрочный");
	Результат.Колонки.Добавить("Комментарий");
	
	ВыборкаОшибок = РезультатПроверки.Выбрать();
	Пока ВыборкаОшибок.Следующий() Цикл
		
		КомментарийОшибки = КомментарийОшибки(ВыборкаОшибок);
		Если ЗначениеЗаполнено(КомментарийОшибки) Тогда
			НоваяСтрокаОшибки = Результат.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаОшибки, ВыборкаОшибок);
			НоваяСтрокаОшибки.Комментарий = КомментарийОшибки;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаписатьТребующиеНастройкиСчетаРеглУчета(ТаблицаОшибокОтражения, ОтражаемыйДокумент)
	
	НачатьТранзакцию();
	
	Попытка
		
		Для каждого СтрокаОшибки Из ТаблицаОшибокОтражения Цикл
			
			ИзмеренияРегистра = РегистрыСведений.СчетаРеглУчетаТребующиеНастройки.СписокИзмеренийРегистраПоВидуСчета(СтрокаОшибки.ВидСчета);
			ИзмеренияРегистра.Добавить("ВидСчета");
			
			Если ТипЗнч(СтрокаОшибки.ВидСчета) <> Тип("ПеречислениеСсылка.ВидыСчетовРеглУчета") Тогда
				ТекстСообщения = НСтр("ru = 'При записи счетов требующих настроек, ошибка: некорректно задан вид счета ""%1"".';
										|en = 'An error occurred when writing the accounts requiring settings: the ""%1"" account kind is specified incorrectly.'", ОбщегоНазначения.КодОсновногоЯзыка());
				ТекстСообщения = СтрШаблон(ТекстСообщения, СтрокаОшибки.ВидСчета);
				ЗаписьЖурналаРегистрации(НСтр("ru = 'Отражение в регламентированном учете';
												|en = 'Record in local accounting'", ОбщегоНазначения.КодОсновногоЯзыка()), 
					УровеньЖурналаРегистрации.Ошибка, Метаданные.РегистрыСведений.СчетаРеглУчетаТребующиеНастройки, ОтражаемыйДокумент, ТекстСообщения);
				Продолжить;
			КонецЕсли;
			
			Если РегистрыСведений.СчетаРеглУчетаТребующиеНастройки.НастраиваемыеВидыСчетов().Найти(СтрокаОшибки.ВидСчета) = Неопределено Тогда
				// Ненастраиваемые счета учета не записываем в регистр.
				Продолжить;
			КонецЕсли;
			
			БлокировкаДанных = Новый БлокировкаДанных;
			ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.СчетаРеглУчетаТребующиеНастройки");
			Для каждого ЭлементИзмерения Из ИзмеренияРегистра Цикл
				ЭлементБлокировкиДанных.УстановитьЗначение(ЭлементИзмерения, СтрокаОшибки[ЭлементИзмерения]);
			КонецЦикла;
			ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
			БлокировкаДанных.Заблокировать();
		
			НаборЗаписей = РегистрыСведений.СчетаРеглУчетаТребующиеНастройки.СоздатьНаборЗаписей();
			НаборЗаписей.ДополнительныеСвойства.Вставить("Документ", ОтражаемыйДокумент);
			Для каждого ЭлементИзмерения Из ИзмеренияРегистра Цикл
				НаборЗаписей.Отбор[ЭлементИзмерения].Установить(СтрокаОшибки[ЭлементИзмерения]);
			КонецЦикла;
			НоваяЗапись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, СтрокаОшибки);
			НаборЗаписей.Записать();
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ВызватьИсключение ОписаниеОшибки();
		
	КонецПопытки;
		
КонецПроцедуры

Функция КомментарийОшибки(СтрокаОшибки)
	
	КодОшибки = СтрокаОшибки.КодОшибки;
	Если КодОшибки = NULL Тогда
		// Строка итогов
		Возврат "";
	КонецЕсли;
	
	СоответствиеКодовОшибокПредставлениям = Новый Соответствие;
	СоответствиеКодовОшибокПредставлениям.Вставить("СЧЕТ_ЗАПРЕЩЕН", НСтр("ru = 'указан запрещенный счет ""%Счет%""';
																		|en = 'The ""%Счет%"" banned account is specified'"));
	СоответствиеКодовОшибокПредставлениям.Вставить("СЧЕТ_ЗАБАЛАНСОВЫЙ", НСтр("ru = 'указан забалансовый счет ""%Счет%""';
																			|en = 'The ""%Счет%"" off-balance account is specified'"));
	СоответствиеКодовОшибокПредставлениям.Вставить("СЧЕТ_ПУСТОЙ", НСтр("ru = 'не указан счет';
																		|en = 'account is not specified'"));
	СоответствиеКодовОшибокПредставлениям.Вставить("АНАЛИТИКА_ПУСТАЯ", НСтр("ru = 'не указана настройка счетов учета';
																			|en = 'ledger account settings are not specified'"));
	
	ПредставлениеОшибки = СоответствиеКодовОшибокПредставлениям.Получить(КодОшибки);
	Если ПредставлениеОшибки = Неопределено Тогда
		Возврат НСтр("ru = 'Неизвестная ошибка формирования проводок!';
					|en = 'Unknown error occurred while generating postings.'");
	КонецЕсли;
	
	КомментарийОшибки = НСтр("ru = 'Для учета %ВидСчета%%Долгосрочный% %Измерение%%Место%%Аналитика% %КодОшибки%.';
							|en = 'For accounting %ВидСчета%%Долгосрочный% %Измерение%%Место%%Аналитика% %КодОшибки%.'");
	КомментарийОшибки = СтрЗаменить(КомментарийОшибки, "%КодОшибки%", ПредставлениеОшибки);
	КомментарийОшибки = СтрЗаменить(КомментарийОшибки, "%Счет%", Строка(СтрокаОшибки.Счет));
	
	ИмяВидаСчета = XMLСтрока(СтрокаОшибки.ВидСчета);
	Если КодОшибки <> "АНАЛИТИКА_ПУСТАЯ" И ЗначениеЗаполнено(СтрокаОшибки.ДопИнформация) Тогда
		РазделУчета = СтрокаОшибки.ДопИнформация;
	Иначе
		РазделУчета = РеглУчетВыборкиСерверПовтИсп.РазделУчетаПоВидуСчета(ИмяВидаСчета);
	КонецЕсли;
	
	КомментарийОшибки = СтрЗаменить(КомментарийОшибки, "%ВидСчета%", 
		РеглУчетВыборкиСерверПовтИсп.СтрокаВидСчета(ИмяВидаСчета, РазделУчета));
		
	СтрокаДолгосрочный = ?(СтрокаОшибки.Долгосрочный, " " + НСтр("ru = '(долгосрочный)';
																|en = '(long-term)'"), "");
	КомментарийОшибки = СтрЗаменить(КомментарийОшибки, "%Долгосрочный%", СтрокаДолгосрочный);
	
	Если СтрокаОшибки.ВидСчета = Перечисления.ВидыСчетовРеглУчета.ПрочиеАктивыПассивы Тогда
		Если ЗначениеЗаполнено(СтрокаОшибки.ИдентификаторСтроки) Тогда
			СтрокаИзмерениеНастройки = НСтр("ru = 'в строке ""%ИдентификаторСтроки%"" документа';
											|en = 'in the ""%ИдентификаторСтроки%"" line of the document'");
			СтрокаИзмерениеНастройки = СтрЗаменить(СтрокаИзмерениеНастройки, "%ИдентификаторСтроки%", Строка(СтрокаОшибки.ИдентификаторСтроки));
		Иначе
			СтрокаИзмерениеНастройки = НСтр("ru = 'в документе';
											|en = 'in document'");
		КонецЕсли;
	Иначе
		СтрокаИзмерениеНастройки = НСтр("ru = 'в организации ""%Организация%""';
										|en = 'in the ""%Организация%"" company'");
		СтрокаИзмерениеНастройки = СтрЗаменить(СтрокаИзмерениеНастройки, "%Организация%", Строка(СтрокаОшибки.Организация));
	КонецЕсли;
	КомментарийОшибки = СтрЗаменить(КомментарийОшибки, "%Измерение%", СтрокаИзмерениеНастройки);
	
	СтрокаАналитики = "";
	Если РазделУчета = Неопределено Или НастройкаСчетовУчетаСервер.НастройкиАналитикиУчетаПоРазделуУчета(РазделУчета).Используется Тогда
		АналитикаУчета = ?(КодОшибки = "АНАЛИТИКА_ПУСТАЯ", СтрокаОшибки.ДопИнформация, СтрокаОшибки.АналитикаУчета);
		СтрокаАналитики = РеглУчетВыборкиСерверПовтИсп.СтрокаАналитикаУчета(АналитикаУчета);
	КонецЕсли;
	КомментарийОшибки = СтрЗаменить(КомментарийОшибки, "%Аналитика%", СтрокаАналитики);
	
	Если РазделУчета = Неопределено Или НастройкаСчетовУчетаСервер.НастройкиМестаУчетаПоРазделуУчета(РазделУчета).Используется Тогда
		КомментарийОшибки = СтрЗаменить(КомментарийОшибки, "%Место%", 
			РеглУчетВыборкиСерверПовтИсп.СтрокаМестоУчета(СтрокаОшибки.МестоУчета));
	Иначе
		КомментарийОшибки = СтрЗаменить(КомментарийОшибки, "%Место%", "");
	КонецЕсли;
	
	Возврат КомментарийОшибки;
	
КонецФункции

Функция УниверсальныеТекстыЗамены()
	
	// Добавим в запрос соединение с документами, зависящие от документа основания:
	
	ДокументыЗависимыеОтОснования = ДокументыОтражаемыеВРеглУчетеВнеЗависимостиОтДатыЗависящиеОтОснования();
	
	ТекстДопСоединений = "";
	ПодстрокаЗаменыДопСоединений = 
	"ВНУТРЕННЕЕ СОЕДИНЕНИЕ Константы КАК Константы";
	Шаблон = 
	"ЛЕВОЕ СОЕДИНЕНИЕ Документ.%ВидДокументаЗависимогоОтОснования% КАК %ВидДокументаЗависимогоОтОснования%
	|		ПО %ВидДокументаЗависимогоОтОснования%.Ссылка = %ИмяПоляДокумента%
	|		";
	
	Для Каждого ДокументЗависимыйОтОснования Из ДокументыЗависимыеОтОснования Цикл
		ТекущийШаблон = СтрЗаменить(Шаблон, "%ВидДокументаЗависимогоОтОснования%", ДокументЗависимыйОтОснования.Ключ);
		ТекстДопСоединений = ТекстДопСоединений + ТекущийШаблон;
	КонецЦикла;
	
	// Добавим в запрос фильтр по документам исключений:
	
	ПодстрокаЗаменыДопУсловий = "Константы.ДатаНачалаВеденияРеглУчета";
	
	ТекстДопУсловий = "";
	МассивИсключений = ИменаДокументовОтражаемыеВРеглУчетеВнеЗависимостиОтДаты();
	
	ШаблонДопУсловийДляДокументаИсключений = "
		|		ИЛИ ТИПЗНАЧЕНИЯ(%ИмяПоляДокумента%) = ТИП(Документ.%ВидДокумента%)";
	ШаблонДопУсловийДляДокументаЗависимогоОтОснования = "
		|		ИЛИ (НЕ %ВидДокументаЗависимогоОтОснования%.Ссылка ЕСТЬ NULL И %ВидДокументаЗависимогоОтОснования%.ДокументОснование ССЫЛКА Документ.%ВидДокумента%)";
	
	Для Каждого ИмяДокументаИсключения Из МассивИсключений Цикл
		ТекущийШаблон = СтрЗаменить(ШаблонДопУсловийДляДокументаИсключений, "%ВидДокумента%", ИмяДокументаИсключения);
		ТекстДопУсловий = ТекстДопУсловий + ТекущийШаблон;
		Для Каждого ДокументЗависимыйОтОснования Из ДокументыЗависимыеОтОснования Цикл
			МетаданныеОснования = ДокументЗависимыйОтОснования.Значение;
			Если МетаданныеОснования <> Неопределено И МетаданныеОснования.Тип.СодержитТип(Тип("ДокументСсылка."+ИмяДокументаИсключения)) Тогда
				ТекущийШаблон = ШаблонДопУсловийДляДокументаЗависимогоОтОснования;
				ТекущийШаблон = СтрЗаменить(ТекущийШаблон, "%ВидДокумента%", ИмяДокументаИсключения);
				ТекущийШаблон = СтрЗаменить(ТекущийШаблон, "%ВидДокументаЗависимогоОтОснования%", ДокументЗависимыйОтОснования.Ключ);
				ТекстДопУсловий = ТекстДопУсловий + ТекущийШаблон;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	ТекстыЗамены = Новый Структура;
	ТекстыЗамены.Вставить("ПодстрокаЗаменыДопСоединений", ПодстрокаЗаменыДопСоединений);
	ТекстыЗамены.Вставить("ТекстДопСоединений",           ТекстДопСоединений);
	ТекстыЗамены.Вставить("ПодстрокаЗаменыДопУсловий",    ПодстрокаЗаменыДопУсловий);
	ТекстыЗамены.Вставить("ТекстДопУсловий",              ТекстДопУсловий);
	
	Возврат ТекстыЗамены;
	
КонецФункции

#КонецОбласти

#Область ПроверкаБлокировкиДанных

Процедура ПроверитьБлокировкуВходящихДанных(ТипДокумента, Отбор = Неопределено, МассивСсылок = Неопределено)
	
	Если ПолучитьФункциональнуюОпцию("ОтложенноеОбновлениеЗавершеноУспешно") Тогда
		Возврат; // обновление ИБ завершено полностью
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипДокумента);
	ВходящиеДанные = РеглУчетВыборкиСерверПовтИсп.ИспользуемыеТаблицыДляОтраженияТипа(ОбъектМетаданных.Имя);
	
	ИмяВременнойТаблицы = Неопределено;
	Если ТипЗнч(Отбор) = Тип("МенеджерВременныхТаблиц") Тогда
		ИмяВременнойТаблицы = "ДокументыКОтражению";
	КонецЕсли;
	
	ЕстьБлокировкаДанных = ЕстьБлокировкаДанных(ВходящиеДанные, Отбор, ИмяВременнойТаблицы);
	Если ЕстьБлокировкаДанных Тогда
		// Нельзя выполнять отражение документов в регл. учете - не завершено обновление всех входящих данных.
		ОписаниеОшибки = СтрШаблон(
			НСтр("ru = 'Отражение документов ""%1""
				|в регламентированном учете остановлено из-за блокировки входящих данных - не завершено обновление ИБ.';
				|en = 'Recording of the ""%1"" documents
				|in local accounting is stopped as the incoming data is locked – infobase update is not completed.'"),
			ТипДокумента);
		
		Если ТипЗнч(Отбор) <> Тип("МенеджерВременныхТаблиц") И ТипЗнч(Отбор) <> Тип("Массив") И ЗначениеЗаполнено(Отбор) Тогда
			ОписаниеОшибки = СтрШаблон(
			НСтр("ru = 'Отражение документа ""%1""
				|в регламентированном учете остановлено из-за блокировки входящих данных - не завершено обновление ИБ.';
				|en = 'Recording of the ""%1"" document
				|in local accounting is stopped as the incoming data is locked – infobase update is not completed.'"),
			Отбор);
		КонецЕсли;
		
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Отражение в регламентированном учете';
				|en = 'Record in local accounting'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,
			ОбъектМетаданных,
			Отбор,
			ОписаниеОшибки);
			
		ВызватьИсключение ОписаниеОшибки;
	КонецЕсли;
		
КонецПроцедуры

// Проверяет, есть ли еще заблокированные обновлением данные.
// В случае если передан массив проверяемых объектов и среди них присутствуют независимые регистры сведений,
// то для таких регистров наличие блокировки проверяется без отбора - если есть ли хоть одна заблокированная запись, 
// то регистр сведений считается заблокированным полностью.
// Проверки независимых регистров сведений с отбором по измерениям возможна только для одного объекта метаданных.
//
// Параметры:
//  ПолныеИменаМетаданныеОбъектов - Строка, ОбъектМетаданных, Массив строк, Массив ОбъектовМетаданных - полное имя
//      обрабатываемого объекта или его метаданные или массив имен или объектов.
//									Например, "Документ.ПриходныйОрдерНаТовары"
//  Отбор - ЛюбаяСсылка, Структура, Неопределено, Массив, МенеджерВременныхТаблиц - отбор данных для проверки.
//									Если передано Неопределено - проверяется по всему типу объекта без отбора,
//									Если объект - регистр, подчиненный регистратору, то в отборе - ссылка на регистратор или массив ссылок
//									Если объект ссылочного типа, то в отборе - или ссылка, или массив ссылок
//									Если объект - независимый регистр сведений, то в отборе - структура со значениями измерений.
//										Ключ структуры - имя измерения, значение - значение отбора (можно передать массив значений)
//	ИмяВременнойТаблицыОтбора - Строка - Имя временной таблицы если отбор задан МенеджеромВременныхТаблиц, если отбор не МенеджерВременныхТаблиц, то игнорируется
//	ИзмеренияРегистраСведений - Строка, Массив - Имена измерений независимого регистра сведений через запятую или в массиве, 
//									по которым необходимо выполнить внутреннее соединение с таблицей отбора переданной в МенеджереВременныхТаблиц.
// 
// Возвращаемое значение:
//  Булево
//
Функция ЕстьБлокировкаДанных(ПолныеИменаМетаданныеОбъектов, Отбор = Неопределено, ИмяВременнойТаблицыОтбора = Неопределено, ИзмеренияРегистраСведений = Неопределено)
	
	Если ПолучитьФункциональнуюОпцию("ОтложенноеОбновлениеЗавершеноУспешно") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ТипЗнч(ПолныеИменаМетаданныеОбъектов) = Тип("Массив") Тогда
		МассивПолныхИменМетаданныхОбъектов = ПолныеИменаМетаданныеОбъектов;
	Иначе
		МассивПолныхИменМетаданныхОбъектов = Новый Массив;
		МассивПолныхИменМетаданныхОбъектов.Добавить(ПолныеИменаМетаданныеОбъектов);
	КонецЕсли;
	
	Если ТипЗнч(ИзмеренияРегистраСведений) = Тип("Массив") Тогда
		МассивИзмеренийРегистраСведений = ИзмеренияРегистраСведений;
	ИначеЕсли ТипЗнч(ИзмеренияРегистраСведений) = Тип("Строка") Тогда
		МассивИзмеренийРегистраСведений = СтрРазделить(ИзмеренияРегистраСведений,",",Ложь);
	КонецЕсли;
	
	МассивТекстовЗапросов = Новый Массив;
	ОтборПоСсылкам = ТипЗнч(Отбор) <> Тип("МенеджерВременныхТаблиц") И ЗначениеЗаполнено(Отбор);
	ОтборПоВременнойТаблице = ТипЗнч(Отбор) = Тип("МенеджерВременныхТаблиц");
	Для Каждого ПолноеИмяМетаданныеОбъекта Из МассивПолныхИменМетаданныхОбъектов Цикл
	
		Если ТипЗнч(ПолноеИмяМетаданныеОбъекта) = Тип("Строка") Тогда
			МетаданныеОбъекта = Метаданные.НайтиПоПолномуИмени(ПолноеИмяМетаданныеОбъекта);
			ПолноеИмяОбъекта = ПолноеИмяМетаданныеОбъекта;
		Иначе
			МетаданныеОбъекта = ПолноеИмяМетаданныеОбъекта;
			ПолноеИмяОбъекта  = МетаданныеОбъекта.ПолноеИмя();
		КонецЕсли;
		
		УсловиеОтбораДанных = "ИСТИНА";
		ТекстВыборкиИзмерений = "ТаблицаИзменений.Ссылка КАК Ссылка";
		Если ОбщегоНазначения.ЭтоОбъектСсылочногоТипа(МетаданныеОбъекта) Тогда
			ТекстЗапроса =
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	ТаблицаИзменений.Ссылка КАК Ссылка
			|ИЗ
			|	#ТаблицаИзменения КАК ТаблицаИзменений
			|ГДЕ
			|	ТаблицаИзменений.Узел ССЫЛКА ПланОбмена.ОбновлениеИнформационнойБазы
			|	И &УсловиеОтбораДанных";
			
			Если ОтборПоСсылкам Тогда
				УсловиеОтбораДанных = "ТаблицаИзменений.Ссылка В (&МассивСсылок)";
			ИначеЕсли ОтборПоВременнойТаблице Тогда
				
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
								"	#ТаблицаИзменения КАК ТаблицаИзменений",
								"	#ТаблицаИзменения КАК ТаблицаИзменений
								|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ #ТаблицаОтбора КАК Отбор
								|	ПО ТаблицаИзменений.Ссылка = Отбор.Ссылка");
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТаблицаОтбора", ИмяВременнойТаблицыОтбора);
				
			КонецЕсли;
				
		ИначеЕсли ОбщегоНазначения.ЭтоРегистрСведений(МетаданныеОбъекта)
			И МетаданныеОбъекта.РежимЗаписи = Метаданные.СвойстваОбъектов.РежимЗаписиРегистра.Независимый Тогда
			
			ТекстЗапроса =
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	&ТекстВыборкиИзмерений
			|ИЗ
			|	#ТаблицаИзменения КАК ТаблицаИзменений
			|ГДЕ
			|	ТаблицаИзменений.Узел ССЫЛКА ПланОбмена.ОбновлениеИнформационнойБазы
			|	И &УсловиеОтбораДанных";
			
			ТекстВыборкиИзмерений = "";
			НовоеУсловиеОтбораДанных = "";
			Для Каждого Измерение Из МетаданныеОбъекта.Измерения Цикл
				
				Если МассивПолныхИменМетаданныхОбъектов.Количество() > 1 Тогда
					ТекстВыборкиИзмерений = "ТаблицаИзменений." + Измерение.Имя + " КАК Ссылка";
					Прервать;
					
				Иначе
					ТекстВыборкиИзмерений = ТекстВыборкиИзмерений + "
						|	ТаблицаИзменений." + Измерение.Имя + " КАК " + Измерение.Имя + ",";
					Если ТипЗнч(Отбор) = Тип("Структура") И Отбор.Свойство(Измерение.Имя) Тогда
						НовоеУсловиеОтбораДанных = НовоеУсловиеОтбораДанных + "
							|	(ТаблицаИзменений." + Измерение.Имя + " В (&" + Измерение.Имя + ")
							|		ИЛИ &" + Измерение.Имя + " = НЕОПРЕДЕЛЕНО)
							|	И ";
					КонецЕсли;
				КонецЕсли;
					
			КонецЦикла;// по измерениям регистра
			
			Если МассивПолныхИменМетаданныхОбъектов.Количество() = 1 Тогда
				ТекстВыборкиИзмерений = Лев(ТекстВыборкиИзмерений, СтрДлина(ТекстВыборкиИзмерений) - 1);
				Если НЕ ПустаяСтрока(НовоеУсловиеОтбораДанных) Тогда
					УсловиеОтбораДанных = Лев(НовоеУсловиеОтбораДанных, СтрДлина(НовоеУсловиеОтбораДанных) - 3);
				КонецЕсли;
			КонецЕсли;
			
			Если ОтборПоВременнойТаблице И ИзмеренияРегистраСведений <> Неопределено Тогда
				
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
						"	#ТаблицаИзменения КАК ТаблицаИзменений",
						"	#ТаблицаИзменения КАК ТаблицаИзменений
						|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ #ТаблицаОтбора КАК Отбор
						|	ПО ТаблицаИзменений.Ссылка = Отбор.Ссылка");
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТаблицаОтбора", ИмяВременнойТаблицыОтбора);
				
				ТекстСоединения = "";
				Для Каждого Измерение Из МассивИзмеренийРегистраСведений Цикл
					ТекстСоединения = ТекстСоединения +
						СтрШаблон("ТаблицаИзменений.%1 = Отбор.Ссылка
								|	И ", Измерение);
				КонецЦикла;
				ТекстСоединения = Лев(ТекстСоединения, СтрДлина(ТекстСоединения) - 3);
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТаблицаИзменений.Ссылка = Отбор.Ссылка", ТекстСоединения);
				
			КонецЕсли;
			
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстВыборкиИзмерений", ТекстВыборкиИзмерений);
			
		ИначеЕсли ОбщегоНазначения.ЭтоРегистр(МетаданныеОбъекта) Тогда
			
			ТекстЗапроса =
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	ТаблицаИзменений.Регистратор КАК Ссылка
			|ИЗ
			|	#ТаблицаИзменения КАК ТаблицаИзменений
			|ГДЕ
			|	ТаблицаИзменений.Узел ССЫЛКА ПланОбмена.ОбновлениеИнформационнойБазы
			|	И &УсловиеОтбораДанных";
			
			Если ОтборПоСсылкам Тогда
				УсловиеОтбораДанных = "ТаблицаИзменений.Регистратор В (&МассивСсылок)";
			ИначеЕсли ОтборПоВременнойТаблице Тогда
				
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
								"	#ТаблицаИзменения КАК ТаблицаИзменений",
								"	#ТаблицаИзменения КАК ТаблицаИзменений
								|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ #ТаблицаОтбора КАК Отбор
								|	ПО ТаблицаИзменений.Регистратор = Отбор.Ссылка");
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТаблицаОтбора", ИмяВременнойТаблицыОтбора);
				
			КонецЕсли;
			
		Иначе
			ТекстИсключения = НСтр("ru = 'Для этого типа метаданных не поддерживается проверка в функции ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки';
									|en = 'Check in the ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки function is not available for this metadata type'");
			ВызватьИсключение ТекстИсключения;
		КонецЕсли;
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТаблицаИзменения", ПолноеИмяОбъекта + ".Изменения");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеОтбораДанных", УсловиеОтбораДанных);
		МассивТекстовЗапросов.Добавить(ТекстЗапроса);
		
	КонецЦикла;
	
	Соединитель = "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|";
	
	ТекстЗапроса = СтрСоединить(МассивТекстовЗапросов, Соединитель);
	Запрос = Новый Запрос;
	Если ОтборПоСсылкам Тогда
		Запрос.УстановитьПараметр("МассивСсылок", Отбор);
	ИначеЕсли ТипЗнч(Отбор) = Тип("Структура") Тогда
		Для Каждого Измерение Из Отбор Цикл
			Запрос.УстановитьПараметр(Измерение.Ключ, ?(ЗначениеЗаполнено(Измерение.Значение), Измерение.Значение, Неопределено));
		КонецЦикла;
	ИначеЕсли ОтборПоВременнойТаблице Тогда
		Запрос.МенеджерВременныхТаблиц = Отбор;
	КонецЕсли;
	Запрос.Текст = ТекстЗапроса;
	
	Возврат НЕ Запрос.Выполнить().Пустой(); 
	
КонецФункции

#КонецОбласти

Функция ИменаДокументовОтражаемыеВРеглУчетеВнеЗависимостиОтДаты()
	
	МассивВозврата = Новый Массив;
	
	МассивВозврата.Добавить("ВводОстатков");
	МассивВозврата.Добавить("ВводОстатковВнеоборотныхАктивов");
	МассивВозврата.Добавить("ВводОстатковВзаиморасчетовПоАренде");
	МассивВозврата.Добавить("ВводОстатковВнеоборотныхАктивов2_4");
	//++ НЕ УТКА
	МассивВозврата.Добавить("НачальныеОстаткиНЗППоПартиямПроизводства");
	//-- НЕ УТКА
	МассивВозврата.Добавить("ОперацияБух");
	МассивВозврата.Добавить("ПервичныйДокумент");
	МассивВозврата.Добавить("ВводОстатковВзаиморасчетов");
	МассивВозврата.Добавить("ВводОстатковДенежныхСредств");
	МассивВозврата.Добавить("ВводОстатковНДСПредъявленного");
	МассивВозврата.Добавить("ВводОстатковОПродажахЗаПрошлыеПериоды");
	МассивВозврата.Добавить("ВводОстатковПоФинансовымИнструментам");
	МассивВозврата.Добавить("ВводОстатковПрочиеРасходы");
	МассивВозврата.Добавить("ВводОстатковПрочихАктивовПассивов");
	//++ НЕ УТ
	МассивВозврата.Добавить("ВводОстатковРасходовПриУСН");
	МассивВозврата.Добавить("ВводДанныхПоНалогуНаПрибыль");
	//-- НЕ УТ
	МассивВозврата.Добавить("ВводОстатковРасчетовПоЭквайрингу");
	МассивВозврата.Добавить("ВводОстатковСПодотчетниками");
	МассивВозврата.Добавить("ВводОстатковТМЦВЭксплуатации");
	МассивВозврата.Добавить("ВводОстатковТоваров");
	
	Возврат МассивВозврата;
	
КонецФункции

Функция ДокументыОтражаемыеВРеглУчетеВнеЗависимостиОтДатыЗависящиеОтОснования()
	
	МетаданныеДокументов = Новый Соответствие;
	
	МетаданныеДокументов.Вставить("СчетФактураВыданныйАванс",
		Метаданные.НайтиПоПолномуИмени("Документ.СчетФактураВыданныйАванс").Реквизиты.Найти("ДокументОснование"));
	МетаданныеДокументов.Вставить("СчетФактураПолученныйАванс",
		Метаданные.НайтиПоПолномуИмени("Документ.СчетФактураПолученныйАванс").Реквизиты.Найти("ДокументОснование"));
	МетаданныеДокументов.Вставить("СчетФактураНалоговыйАгент",
		Метаданные.НайтиПоПолномуИмени("Документ.СчетФактураНалоговыйАгент").Реквизиты.Найти("ДокументОснование"));
	
	Возврат МетаданныеДокументов;
	
КонецФункции

Процедура ПроверитьДатуЗапретаДляОтраженияДокументовВРеглУчете(ШаблонЗапретаДанных, НаборЗаписей = Неопределено, ОтражаемыйДокумент = Неопределено)
	
	Если НаборЗаписей = Неопределено Тогда
		Отбор = Новый Структура;
		НаборЗаписей = Новый Структура("Регистр, Отбор", "РегистрСведений.ОтражениеДокументовВРеглУчете", Отбор);
	Иначе
		НаборЗаписей.ДополнительныеСвойства.Вставить("ПроверкаДатыЗапретаИзменения", Ложь);
	КонецЕсли;
	
	ОписаниеДанных = Новый Структура("НоваяВерсия, Данные", Истина, НаборЗаписей);
	ОписаниеОшибки = "";
	Если ДатыЗапретаИзменения.НайденЗапретИзмененияДанных(ШаблонЗапретаДанных, ОписаниеДанных, ОписаниеОшибки) Тогда
	
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Установка нового статуса отражения в учете';
				|en = 'Set new status of recording in accounting'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,
			?(ОтражаемыйДокумент = Неопределено, ОтражаемыйДокумент, ОтражаемыйДокумент.Метаданные()),
			?(ОтражаемыйДокумент = Неопределено, ОтражаемыйДокумент, ОтражаемыйДокумент),
			ОписаниеОшибки);
		
		ВызватьИсключение ОписаниеОшибки;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ИсключенияДляПереотражения()
	
	СписокТиповИсключений = Новый Массив;
	СписокТиповИсключений.Добавить(Тип("ДокументСсылка.АмортизацияНМА"));
	СписокТиповИсключений.Добавить(Тип("ДокументСсылка.АмортизацияОС"));
	СписокТиповИсключений.Добавить(Тип("ДокументСсылка.ПередачаОСВАренду"));
	СписокТиповИсключений.Добавить(Тип("ДокументСсылка.ВозвратОСИзАренды"));
	СписокТиповИсключений.Добавить(Тип("ДокументСсылка.ВыбытиеАрендованныхОС"));
	СписокТиповИсключений.Добавить(Тип("ДокументСсылка.ИзменениеПараметровОС"));
	СписокТиповИсключений.Добавить(Тип("ДокументСсылка.МодернизацияОС"));
	СписокТиповИсключений.Добавить(Тип("ДокументСсылка.ПеремещениеОС"));
	СписокТиповИсключений.Добавить(Тип("ДокументСсылка.ПереоценкаНМА"));
	СписокТиповИсключений.Добавить(Тип("ДокументСсылка.ПереоценкаОС"));
	СписокТиповИсключений.Добавить(Тип("ДокументСсылка.ПодготовкаКПередачеНМА"));
	СписокТиповИсключений.Добавить(Тип("ДокументСсылка.ПодготовкаКПередачеОС"));
	СписокТиповИсключений.Добавить(Тип("ДокументСсылка.ПринятиеКУчетуНМА"));
	СписокТиповИсключений.Добавить(Тип("ДокументСсылка.ПринятиеКУчетуОС"));
	СписокТиповИсключений.Добавить(Тип("ДокументСсылка.СписаниеНМА"));
	СписокТиповИсключений.Добавить(Тип("ДокументСсылка.СписаниеОС"));
	СписокТиповИсключений.Добавить(Тип("ДокументСсылка.ОперацияБух"));
	СписокТиповИсключений.Добавить(Тип("ДокументСсылка.РегламентнаяОперация"));
	СписокТиповИсключений.Добавить(Тип("ДокументСсылка.КорректировкаНалогообложенияНДСПартийТоваров"));
	
	Возврат СписокТиповИсключений;
	
КонецФункции

Функция ДанныеВыборочнойПроверкиБлокировкиЗакрытияМесяца(Знач Документ, ОтражениеВРеглУчете)
	
	ДанныеПроверки = ТаблицаВыборочнойПроверкиБлокировки();
	
	Если ТипЗнч(Документ) <> Тип("ДокументСсылка.КорректировкаРеализации") 
		ИЛИ ОтражениеВРеглУчете.Количество() < 2 Тогда
	    Возврат ДанныеПроверки; 
	КонецЕсли;   
	
	Для Каждого Строка Из ОтражениеВРеглУчете Цикл
		Если Строка.ДатаОтражения < НачалоДня(Строка.Период) Тогда
			Продолжить;	
		КонецЕсли;
		
		СтрокаДанныхКОтражению = ДанныеПроверки.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаДанныхКОтражению, Строка);
		СтрокаДанныхКОтражению.Ссылка = Документ;
	КонецЦикла;
	
	Возврат ДанныеПроверки;
	 
КонецФункции

Функция ТаблицаВыборочнойПроверкиБлокировки()
	
	ДанныеКОтражению = Новый ТаблицаЗначений;
	ДанныеКОтражению.Колонки.Добавить("Организация",    Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ДанныеКОтражению.Колонки.Добавить("ДатаОтражения",  Новый ОписаниеТипов("Дата"));
	ДанныеКОтражению.Колонки.Добавить("Период",  Новый ОписаниеТипов("Дата"));
	ДанныеКОтражению.Колонки.Добавить("Ссылка", Документы.ТипВсеСсылки());
	
	Возврат ДанныеКОтражению;
	 
КонецФункции

Процедура ПроверитьБлокировкуЗакрытияМесяцаОтИзменений(ДанныеВыборочнойПроверкиБлокировкиЗакрытияМесяца)
	
	Если НЕ РегистрыСведений.НастройкаБлокировкиОтИзменений.ИспользоватьМеханизмБлокировкиОтИзменений() Тогда 
		Возврат;
	КонецЕсли;
	
	Организации = ДанныеВыборочнойПроверкиБлокировкиЗакрытияМесяца.ВыгрузитьКолонку("Организация");
	ДатыПоОрганизациям = РегистрыСведений.НастройкаБлокировкиОтИзменений.ДатыБлокировкиПоОрганизациям(Организации);
	
	ИмяРегистра = Метаданные.РегистрыСведений.ОтражениеДокументовВРеглУчете.Имя;
	
	Для Каждого Строка Из ДанныеВыборочнойПроверкиБлокировкиЗакрытияМесяца Цикл
		
		ЗаблокироватьПо = ДатыПоОрганизациям.Получить(Строка.Организация);

		Если ЗначениеЗаполнено(ЗаблокироватьПо) И НачалоМесяца(Строка.ДатаОтражения) <= ЗаблокироватьПо Тогда

			ТекстОшибки = ЗакрытиеМесяцаСервер.ТекстОшибкиЗаписиЗаданияВЗаблокированномПериоде(Строка.Организация, 
				Строка.ДатаОтражения, ЗаблокироватьПо, ИмяРегистра);
			
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Установка нового статуса отражения в учете';
					|en = 'Set new recording status in accounting'", ОбщегоНазначения.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Ошибка,
				,
				,
				ТекстОшибки);
			
			ВызватьИсключение ТекстОшибки;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
