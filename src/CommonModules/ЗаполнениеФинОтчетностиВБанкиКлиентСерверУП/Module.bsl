
#Область ПрограммныйИнтерфейс

//++ НЕ УТ

// Сопоставляет для указанного вида отчета соответствующий ему источник (способ) заполнения.
//
// Параметры:
//  ВидОтчета    - Строка - см. колонку из результата ЗаполнениеФинОтчетностиВБанки.ОписаниеВидовОтчетов()
//
// Возвращаемое значение:
//  Строка, Неопределено - принимает значения: ОтчетБРО, СтандартныйОтчет, ВнешнийФайл, ПроизвольныйФайл.
//
Функция ОпределитьСпособЗаполненияВидаОтчета(ВидОтчета) Экспорт
	Перем СпособЗаполнения;

	Если ВидОтчета = "СправкаБанка"
	 Или ВидОтчета = "СправкаФНСОбИсполненииОбязанностейПоУплатеНалогов"
	 Или ВидОтчета = "СправкаФНСОбОткрытыхРасчетныхСчетах"
	 Или ВидОтчета = "КнигаУчетаДоходовИРасходовПредпринимателя"
	 Или ВидОтчета = "РасшифровкаЗадолженности"
	 Или ВидОтчета = "БухгалтерскаяОтчетностьАудиторскоеЗаключение"
	 Или ВидОтчета = "БухгалтерскаяОтчетностьПояснения"
	 Или ВидОтчета = "Патент" Тогда
		СпособЗаполнения = "ВнешнийФайл";

	ИначеЕсли ВидОтчета = "КарточкаСчета"
	 Или ВидОтчета = "АнализСчета"
	 Или ВидОтчета = "ОборотноСальдоваяВедомость"
	 Или ВидОтчета = "ОборотноСальдоваяВедомостьПоСчету"
	 Или ВидОтчета = "ОборотыСчета"
	 Или ВидОтчета = "ВедомостьАмортизацииОС"
	 Или ВидОтчета = "ВаловаяПрибыль"
	 Или ВидОтчета = "ОстаткиТоваровПоСрокамХранения"
	 Или ВидОтчета = "КнигаУчетаДоходовИРасходов"
	 Или ВидОтчета = "КнигаУчетаДоходовПатент"
	 Или ВидОтчета = "КассоваяКнига" Тогда
		СпособЗаполнения = "СтандартныйОтчет";

	ИначеЕсли ВидОтчета = "БухгалтерскаяОтчетность"
	 Или ВидОтчета = "ДекларацияПрибыль"
	 Или ВидОтчета = "ДекларацияНДС"
	 Или ВидОтчета = "ДекларацияУСН"
	 Или ВидОтчета = "ДекларацияЕНВД"
	 Или ВидОтчета = "ДекларацияИмущество"
	 Или ВидОтчета = "Декларация3НДФЛ"
	 Или ВидОтчета = "ДекларацияЕСХН"
	 Или ВидОтчета = "РасчетПоСтраховымВзносам" Тогда
		СпособЗаполнения = "ОтчетБРО";

	ИначеЕсли ВидОтчета = "ОтчетПоФормеБанка" Тогда 
		СпособЗаполнения = "ОтчетПоФормеБанка";

	КонецЕсли;
	
	Возврат СпособЗаполнения;
	
КонецФункции

// Отображает или скрывает кнопку "Настройка отправки отчетности".
//
// Параметры:
//  Форма        - ФормаКлиентскогоПриложения - должна содержать кнопку СписокНастроитьОтправку.
//
Процедура УстановитьВидимостьКнопкиНастройкиОтправки(Форма) Экспорт

	Форма.Элементы.СписокНастроитьОтправку.Видимость = Форма.ЕстьПравоРедактированияОтчета
		И ЗначениеЗаполнено(Форма.Банк) И Не ФинОтчетностьВБанкиКлиентСервер.ЭтоБанкУниверсальногоОбмена(Форма.Банк);
	
КонецПроцедуры

//-- НЕ УТ

#КонецОбласти
