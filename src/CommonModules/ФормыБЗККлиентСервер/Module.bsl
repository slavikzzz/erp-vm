#Область СлужебныйПрограммныйИнтерфейс

// Находит строки в таблице удовлетворяющие фильтру без вызова сервера.
//   Метод подходит для использования в случаях, когда заранее известно что в таблице небольшое количество записей.
//   Если записей много - рекомендуется использовать метод НайтиСтроки объекта ДанныеФормыКоллекция.
//
// Параметры:
//   Таблица - ДанныеФормыКоллекция
//   Фильтр - Структура
//
// Возвращаемое значение:
//   Массив - Массив строк коллекции, соответствующих условиям поиска.
//
Функция НайтиСтроки(Таблица, Фильтр) Экспорт
	Результат = Новый Массив;
	Для Каждого СтрокаТаблицы Из Таблица Цикл
		Если СтрокаТаблицыУдовлетворяетФильтру(СтрокаТаблицы, Фильтр) Тогда
			Результат.Добавить(СтрокаТаблицы);
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СтрокаТаблицыУдовлетворяетФильтру(СтрокаТаблицы, Фильтр)
	Для Каждого КлючИЗначение Из Фильтр Цикл
		Если КлючИЗначение.Значение <> СтрокаТаблицы[КлючИЗначение.Ключ] Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	Возврат Истина;
КонецФункции

#КонецОбласти
