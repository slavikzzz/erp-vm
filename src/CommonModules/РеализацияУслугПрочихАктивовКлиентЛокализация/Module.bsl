////////////////////////////////////////////////////////////////////////////////
// Процедуры, используемые для локализации документа "Реализация услуг и прочих активов".
// 
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

#Область ФормаДокумента

// Процедура - обратчик при изменении реквизита формы.
// 
// Параметры:
//	ИмяЭлемента - Строка - имя элемента формы
//	Форма - ФормаКлиентскогоПриложения - форма документа
//	ДополнительныеПараметры - Структура - дополнительные параметры обработки
//
Процедура ПриИзмененииРеквизита(ИмяЭлемента, Форма, ДополнительныеПараметры = Неопределено) Экспорт

	ТребуетсяВызовСервера = Ложь;
	ПродолжитьИзменениеРеквизита = Истина;
	Элементы = Форма.Элементы;
	
	//++ Локализация
	
	//++ НЕ УТ
	ЭлементРеализацияВзаимозависимомуЛицу = Элементы.РеализацияВзаимозависимомуЛицу; // ПолеФормы
	Если ИмяЭлемента = ЭлементРеализацияВзаимозависимомуЛицу.Имя Тогда	
		РеализацияВзаимозависимомуЛицуПриИзменении(Форма, ТребуетсяВызовСервера, ДополнительныеПараметры);
	КонецЕсли; 
	//-- НЕ УТ
	
	//-- Локализация
	
	Если ПродолжитьИзменениеРеквизита Тогда
		
		ОбщегоНазначенияУТКлиент.ПродолжитьИзменениеРеквизита(
			Форма, 
			ИмяЭлемента, 
			ТребуетсяВызовСервера,
			ДополнительныеПараметры);
			
	КонецЕсли; 
	
КонецПроцедуры

// Процедура - обработчик при выполнении команды формы.
// 
// Параметры:
//	Команда - КомандаФормы - команды формы
//	Форма - ФормаКлиентскогоПриложения - форма
// 
Процедура ПриВыполненииКоманды(Команда, Форма) Экспорт

	ДополнительныеПараметры = Неопределено;
	ТребуетсяВызовСервера = Ложь;
	Элементы = Форма.Элементы;
	ПродолжитьВыполнениеКоманды = Истина;
	
	//++ Локализация
	
	//++ НЕ УТ
	ЭлементРасходыЗаполнитьВосстановлениеАмортизационнойПремии = Элементы.РасходыЗаполнитьВосстановлениеАмортизационнойПремии; // КнопкаФормы
	Если Команда.Имя = ЭлементРасходыЗаполнитьВосстановлениеАмортизационнойПремии.ИмяКоманды Тогда
		
		ЗаполнитьВосстановлениеАмортизационнойПремии(Форма, ТребуетсяВызовСервера, ПродолжитьВыполнениеКоманды);
		
	ИначеЕсли Команда.Имя = Элементы.РасходыЗаполнитьСтатьюАмортизационнойПремии.ИмяКоманды Тогда
		
		ЗаполнитьСтатьюАмортизационнойПремии(Форма, ПродолжитьВыполнениеКоманды);
		
	КонецЕсли; 
	//-- НЕ УТ
	
	//-- Локализация
	
	Если ПродолжитьВыполнениеКоманды Тогда
		
		ОбщегоНазначенияУТКлиент.ПродолжитьВыполнениеКоманды(
			Форма, 
			Команда.Имя, 
			ТребуетсяВызовСервера,
			ДополнительныеПараметры);
			
	КонецЕсли; 
	
КонецПроцедуры

// Процедура дополняет список типов документов, которые могут являться основаниями для ввода документа.
// Параметры:
//	Форма - ФормаКлиентскогоПриложения - форма документа
//	СписокТипов - СписокЗначений - список типов документов
//
Процедура ДополнитьСписокТиповОснований(Форма, СписокТипов) Экспорт

	//++ Локализация
	
	//++ НЕ УТ
	
	Объект = Форма.Объект;
	
	Если Объект.ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.РеализацияОСсОтложеннымПереходомПрав")
		ИЛИ Объект.ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности") Тогда
		
		Если НЕ Форма.ИспользуетсяУчетВНА_2_4 Тогда
			СписокТипов.Добавить("Документ.ПодготовкаКПередачеОС.ФормаВыбора", НСтр("ru = 'Подготовка к передаче ОС (2.2)';
																					|en = 'Preparation for FA handover (2.2)'"));
		КонецЕсли;
		
	Иначе
		
		Если НЕ Форма.ИспользуетсяУчетВНА_2_4 Тогда
			СписокТипов.Добавить("Документ.ПодготовкаКПередачеНМА.ФормаВыбора", НСтр("ru = 'Подготовка к передаче НМА (2.2)';
																					|en = 'Preparation for IA handover (2.2)'"));
			СписокТипов.Добавить("Документ.ПодготовкаКПередачеОС.ФормаВыбора", НСтр("ru = 'Подготовка к передаче ОС (2.2)';
																					|en = 'Preparation for FA handover (2.2)'"));
		КонецЕсли;
		
	КонецЕсли; 
	
	//-- НЕ УТ
	
	//-- Локализация
	
КонецПроцедуры
 
#КонецОбласти

#КонецОбласти

//++ Локализация

//++ НЕ УТ

#Область СлужебныеПроцедурыИФункции

#Область ФормаДокумента

Процедура РеализацияВзаимозависимомуЛицуПриИзменении(Форма, ТребуетсяВызовСервера, ДополнительныеПараметры)
	
	Объект = Форма.Объект;
	
	Если Объект.РеализацияВзаимозависимомуЛицу Тогда
		ТребуетсяВызовСервера = Истина;
	Иначе
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Выполнить_НастроитьЗависимыеЭлементыФормы", "РеализацияВзаимозависимомуЛицу");
	КонецЕсли; 
	
КонецПроцедуры

Процедура ЗаполнитьВосстановлениеАмортизационнойПремии(Форма, ТребуетсяВызовСервера, ПродолжитьВыполнениеКоманды)
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Если Объект.Расходы.Количество() = 0 Тогда
		ПродолжитьВыполнениеКоманды = Ложь;
		Возврат;
	КонецЕсли; 
	
	Если Элементы.Расходы.ВыделенныеСтроки.Количество() = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Необходимо выбрать строки, в которых будет заполнено восстановление амортизационной премии';
										|en = 'Select the lines, in which bonus depreciation restoration will be filled in'"));
		ПродолжитьВыполнениеКоманды = Ложь;
		Возврат;
	КонецЕсли;
	
	ТребуетсяВызовСервера = Истина;
	
КонецПроцедуры

Процедура ЗаполнитьСтатьюАмортизационнойПремии(Форма, ПродолжитьВыполнениеКоманды)
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	ПродолжитьВыполнениеКоманды = Ложь;
	
	Если Объект.Расходы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли; 
	
	Если Элементы.Расходы.ВыделенныеСтроки.Количество() = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Необходимо выбрать строки, в которых будет заполнена статья доходов';
										|en = 'Select the lines, in which the income item will be filled in'"));
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма", Форма);
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьСтатьюАмортизационнойПремииЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ОткрытьФорму("ПланВидовХарактеристик.СтатьиДоходов.ФормаВыбора",,,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

Процедура ЗаполнитьСтатьюАмортизационнойПремииЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
	Если ЗначениеЗаполнено(Результат) Тогда
		
		ДополнительныеПараметрыДействия = Новый Структура;
		
		ПараметрыДействия = Новый Структура;
		ПараметрыДействия.Вставить("СтатьяДоходов", Результат);
		ПараметрыДействия.Вставить("ИмяТЧ", "Расходы");
		ДополнительныеПараметрыДействия.Вставить("Выполнить_УстановитьСтатьюДоходов", ПараметрыДействия);
		
		ОбщегоНазначенияУТКлиент.ПродолжитьВыполнениеКоманды(
			ДополнительныеПараметры.Форма,
			"РасходыЗаполнитьСтатьюАмортизационнойПремии",,
			ДополнительныеПараметрыДействия);
			
    КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецОбласти

//-- НЕ УТ

//-- Локализация