
#Область ПрограммныйИнтерфейс

Процедура ДобавитьОтборДатаОтветаПросрочена(ГруппаОтбора, Использование, Дата) Экспорт
	
	ГруппаОтбораОтветПросрочен = СоздатьГруппуОтбораСУдалением(
		ГруппаОтбора, 
		НСтр("ru = 'Ответ просрочен';
			|en = 'Ответ просрочен'"), 
		Использование,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИЛИ);
		
	ГруппаОтбораПросроченоОтносительноДатаИзмененная = СоздатьГруппуЭлементовОтбора(
		ГруппаОтбораОтветПросрочен, 
		НСтр("ru = 'Ответ просрочен относительно измененной даты';
			|en = 'Ответ просрочен относительно измененной даты'"), 
		Использование,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбораПросроченоОтносительноДатаИзмененная,
		"ДатаОтветаИзмененная",
		ВидСравненияКомпоновкиДанных.Меньше,
		Дата,
		,
		Использование);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбораПросроченоОтносительноДатаИзмененная,
		"ДатаОтветаИзмененная",
		ВидСравненияКомпоновкиДанных.Заполнено,
		,
		,
		Использование);
		
	ГруппаОтбораПросроченоОтносительноДатаРасчетная = СоздатьГруппуЭлементовОтбора(
		ГруппаОтбораОтветПросрочен, 
		НСтр("ru = 'Ответ просрочен относительно расчетная даты';
			|en = 'Ответ просрочен относительно расчетная даты'"),
		Использование,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
				
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбораПросроченоОтносительноДатаРасчетная,
		"ОтветитьДо",
		ВидСравненияКомпоновкиДанных.Меньше,
		Дата,
		,
		Использование);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбораПросроченоОтносительноДатаРасчетная,
		"ОтветитьДо",
		ВидСравненияКомпоновкиДанных.Заполнено,
		,
		,
		Использование);
		
КонецПроцедуры

Процедура ДобавитьОтборДатаОтветаЗаполнена(ГруппаОтбора, Использование, ИмяПоляДаты) Экспорт
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбора,
		ИмяПоляДаты,
		ВидСравненияКомпоновкиДанных.Заполнено,
		,
		,
		Использование);
	
КонецПроцедуры

Функция ДобавитьЭлементКомпоновкиСУдалением(Куда, ИмяПоля, ВидСравнения, ПравоеЗначение = Неопределено, Использование = Истина) Экспорт
	
	Результат = ОбщегоНазначенияКлиентСервер.НайтиЭлементыИГруппыОтбора(
		Куда,
		ИмяПоля);
		
	Если Результат.Количество() > 0 Тогда
		
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(
			Куда,
			ИмяПоля);
			
	КонецЕсли;
	
	Элемент = ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		Куда,
		ИмяПоля,
		ВидСравнения,
		ПравоеЗначение,
		,
		Использование);
	
	Возврат Элемент;
	
КонецФункции

Функция ВыделитьЦветом(Форма, Цвет, Поле, ПравоеЗначение) Экспорт
	
	ЭлементОформления = Форма.УсловноеОформление.Элементы.Добавить();
	
	ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных(Поле); 
	ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;  
	ЭлементОтбора.ПравоеЗначение = ПравоеЗначение;
	ЭлементОтбора.Использование  = Истина;

	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", Цвет);

	Возврат ЭлементОформления;
	 
КонецФункции

Функция СоздатьГруппуОтбораСУдалением(Куда, ПредставлениеГруппы, Использование, ТипГруппы) Экспорт
	
	Результат = ОбщегоНазначенияКлиентСервер.НайтиЭлементыИГруппыОтбора(
		Куда,
		,
		ПредставлениеГруппы);
		
	Если Результат.Количество() > 0 Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(
			Куда,
			,
			ПредставлениеГруппы);
	КонецЕсли;
	
	ГруппаОтбора = СоздатьГруппуЭлементовОтбора(
		Куда, 
		ПредставлениеГруппы,
		Использование,
		ТипГруппы);
	
	Возврат ГруппаОтбора;
	
КонецФункции

Функция СоздатьГруппуЭлементовОтбора(Куда, ПредставлениеГруппы, Использование, ТипГруппы) Экспорт
	
	ГруппаОтбора = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
		Куда, 
		ПредставлениеГруппы, 
		ТипГруппы);
	ГруппаОтбора.Использование = Использование;
	
	Возврат ГруппаОтбора;
	
КонецФункции

Функция ТекстДляПанелиТребований(ДобавитьСсылку, Организация = Неопределено) Экспорт
	
	ШаблонПодтверждений = НСтр("ru = ';%1 подтверждение о приеме;;%1 подтверждения о приеме;%1 подтверждений о приеме;%1 подтверждений о приеме';
								|en = ';%1 подтверждение о приеме;;%1 подтверждения о приеме;%1 подтверждений о приеме;%1 подтверждений о приеме'");
	ШаблонОтвет = НСтр("ru = ';%1 ответ на требование;;%1 ответа на требования;%1 ответов на требования;%1 ответов на требования';
						|en = ';%1 ответ на требование;;%1 ответа на требования;%1 ответов на требования;%1 ответов на требования'");
	
	Состояние = ТребованияФНСВызовСервера.СостояниеТребованийДляПанели(Организация);
	СостояниеСФР = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.СостояниеТребованийСФРДляПанели();
	
	Если ТипЗнч(Состояние) = Тип("Структура") Тогда
		Состояние.БезОтвета = Состояние.БезОтвета + СостояниеСФР.БезОтвета;
		Состояние.Подтвердить = Состояние.Подтвердить + СостояниеСФР.Подтвердить;
	КонецЕсли;
	
	Ссылка = Новый ФорматированнаяСтрока("Показать", , , , "ОткрытьСписокТребований");
	
	ПредставлениеПодтверждений = ДокументооборотСКОКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(ШаблонПодтверждений, Состояние.Подтвердить);
	ПредставлениеОтветов = ДокументооборотСКОКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(ШаблонОтвет, Состояние.БезОтвета);
	
	Если Состояние.Подтвердить > 0 И Состояние.БезОтвета > 0 Тогда
		Тело = ПредставлениеПодтверждений + НСтр("ru = ' и ';
												|en = ' и '") + ПредставлениеОтветов;
	ИначеЕсли Состояние.Подтвердить > 0 И Состояние.БезОтвета = 0 Тогда
		Тело = ПредставлениеПодтверждений;
	ИначеЕсли Состояние.Подтвердить = 0  И Состояние.БезОтвета > 0 Тогда
		Тело = ПредставлениеОтветов;
	Иначе
		Возврат "";
	КонецЕсли;
	
	Текст = Новый ФорматированнаяСтрока(
		 	НСтр("ru = 'Отправьте ';
					|en = 'Отправьте '"),
			Тело,
			НСтр("ru = ' в ближайшее время. ';
				|en = ' в ближайшее время. '"),
			?(ДобавитьСсылку, Ссылка, ""));
			
	Возврат Текст;
			
КонецФункции

Процедура ДополнитьОтборПоТипуВходящих(Форма, Использование = Истина) Экспорт
	
	Элементы = Форма.Элементы;
	
	ТекущаяСтраница = Элементы.ГруппаКатегориииВх.ТекущаяСтраница;
	
	Если ТекущаяСтраница = Элементы.СтраницаРассылки Тогда
		ВидДокумента = ПредопределенноеЗначение("Перечисление.ВидыДокументовВЖурнале.Рассылка");
	ИначеЕсли ТекущаяСтраница = Элементы.СтраницаТребования Тогда
		ВидДокумента = ПредопределенноеЗначение("Перечисление.ВидыДокументовВЖурнале.ТребованиеИлиУведомление");
	ИначеЕсли ТекущаяСтраница = Элементы.СтраницаВходящиеПисьма Тогда
		ВидДокумента = ПредопределенноеЗначение("Перечисление.ВидыДокументовВЖурнале.ВходящееПисьмо");
	КонецЕсли;

	ТребованияФНСКлиентСервер.ДобавитьЭлементКомпоновкиСУдалением(
		Форма.Входящие.Отбор, 
		"ВидДокумента", 
		ВидСравненияКомпоновкиДанных.Равно, 
		ВидДокумента, 
		Использование);
	
КонецПроцедуры
	
Функция ЭтоФормат_ON_DOCNPNO_1_886_00_05_02() Экспорт
	
	Возврат Истина;
	
КонецФункции

Функция ПараметрыВыбораФайловВОтветНаТребованиеДокументов(МножественныйВыбор = Истина) Экспорт
	
	Требования = ТребованияКИзображениямОтветаНаТребованиеДокументов();
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("ДопустимыеТипыФайлов", 	Требования.ДопустимыеТипыФайлов);
	ДополнительныеПараметры.Вставить("МаксимальныйРазмерФайла", 60*1024*1024);
	ДополнительныеПараметры.Вставить("ВозвращатьРазмер", 		Истина);
	ДополнительныеПараметры.Вставить("МножественныйВыбор", 		МножественныйВыбор);
	
	Возврат ДополнительныеПараметры;
	
КонецФункции

Функция ТребованияКИзображениямОтветаНаТребованиеДокументов() Экспорт
	
	// Требования установлены ПРИКАЗом от 18 января 2017 г. N ММВ-7-6/16@ (ответ на требование документов). 
	// Расширение имени файла - tif | jpg | pdf | png. К файлам, содержащим отсканированные изображения, предъявляются следующие требования: 
	// черно-белое изображение с разрешением отсканированного документа не менее 150 и не более 300 точек на дюйм 
	// с использованием 256 градаций серого цвета. ФНС проверяет только те свойства, которые есть в картинке.
	
	ПараметрыМетода = ОперацииСФайламиЭДКОКлиентСервер.ТребованияКСканам();
	
	ДопустимыеТипыФайлов = ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.ДопустимыеТипыФайловТребования();
	
	ПараметрыМетода.Вставить("ДопустимыеТипыФайлов", 	    ДопустимыеТипыФайлов);
	ПараметрыМетода.Вставить("ГлубинаЦвета", 				"БитНаПиксел8"); // 2^8 = 256
	ПараметрыМетода.Вставить("ПреобразоватьВОттенкиСерого", Истина);
	ПараметрыМетода.Вставить("МинимальнаяПлотность",		150);
	ПараметрыМетода.Вставить("МаксимальнаяПлотность", 		300);
	ПараметрыМетода.Вставить("МаксимальныйРазмерФайла", 	60*1024*1024);
	ПараметрыМетода.Вставить("Пояснение", 					НСтр("ru = 'Суммарный размер файлов должен быть не более 72 Мб';
																	|en = 'Суммарный размер файлов должен быть не более 72 Мб'"));
	
	Возврат ПараметрыМетода;
	
КонецФункции

Функция Подчерк() Экспорт
	
	Возврат СтроковыеФункцииКлиентСервер.СформироватьСтрокуСимволов(" ", 10);
	
КонецФункции

Функция Оранжевый() Экспорт
	
	Возврат Новый Цвет(250, 204, 30);
	
КонецФункции

Функция ШаблонЗначенияШаблона(
		Знач Значение = "", 
		Знач Замена = "", 
		ЦветФонаЗначения = "", 
		ЦветФонаЗамены = "") Экспорт
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("Значение", Значение);
	ДополнительныеПараметры.Вставить("Замена", ?(ЗначениеЗаполнено(Замена),Замена,Подчерк()));
	
	ДополнительныеПараметры.Вставить("ЦветФонаЗначения", ?(ЦветФонаЗначения = "", Оранжевый(), ЦветФонаЗначения));
	ДополнительныеПараметры.Вставить("ЦветФонаЗамены",   ?(ЦветФонаЗамены = "", Оранжевый(), ЦветФонаЗамены));
	
	Возврат ДополнительныеПараметры;
	
КонецФункции

Процедура ДобавитьЗначениеПараметраШаблона(Данные, Ключ, Значение, Знач Замена = "") Экспорт
	
	Если Замена = "" Тогда
		Замена = "<" + Ключ + ">";
	КонецЕсли;
	
	Структура = ШаблонЗначенияШаблона(Значение, Замена, Новый Цвет);
	Данные.Вставить(Ключ, Структура);
	
КонецПроцедуры

Функция ПотомкиСтроки(СтрокаДерева) Экспорт
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаДерева, "Строки") Тогда
		Возврат СтрокаДерева.Строки;
	Иначе
		Возврат СтрокаДерева.ПолучитьЭлементы();
	КонецЕсли;

КонецФункции

Функция РодительСтроки(СтрокаДерева) Экспорт
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаДерева, "Родитель")  Тогда
		Возврат СтрокаДерева.Родитель;
	Иначе
		Возврат СтрокаДерева.ПолучитьРодителя();
	КонецЕсли;

КонецФункции

Процедура ПроставитьПометкиВниз(СтрокаДерева) Экспорт
	
	Потомки = ПотомкиСтроки(СтрокаДерева);
	
	Если Потомки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ЗначениеФлажка = СтрокаДерева.Пометка;
	
	Для каждого Потомок из Потомки Цикл
		
		Если ЗначениеФлажка <> 2 И ВидимостьВСтрокеДерева(Потомок) Тогда
			Потомок.Пометка = ЗначениеФлажка;
		КонецЕсли;
		
		ПроставитьПометкиВниз(Потомок);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроставитьПометкиВверх(СтрокаДерева) Экспорт
	
	Родитель = РодительСтроки(СтрокаДерева);
	
	Если Родитель = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВсеИстина = Истина;
	ВсеЛожь = Истина;
	
	Потомки = ПотомкиСтроки(Родитель);
	Для каждого Потомок из Потомки Цикл
		
		Если НЕ ВидимостьВСтрокеДерева(Потомок) Тогда
			Продолжить;
		КонецЕсли;
		
		ВсеИстина = ВсеИстина И Потомок.Пометка = 1;
		ВсеЛожь = ВсеЛожь И Потомок.Пометка = 0;
		
		Если ВсеИстина = Ложь И ВсеЛожь = Ложь Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ВсеИстина Тогда
		Родитель.Пометка = 1;
	ИначеЕсли ВсеЛожь Тогда
		Родитель.Пометка = 0;
	Иначе
		Родитель.Пометка = 2;
	КонецЕсли;
	
	ПроставитьПометкиВверх(Родитель);
	
КонецПроцедуры

Функция ВидимостьВСтрокеДерева(Потомок)

	Если НЕ ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Потомок, "Видимость") Тогда
		Возврат Истина;
	Иначе
		Возврат Потомок.Видимость;
	КонецЕсли;

КонецФункции

Процедура ДеревоПометкаПриИзмененииПоСтроке(СтрокаДерева) Экспорт
	
	// Флажок с тремя значениями, при клике меняются, по умолчанию, в следующем порядке: 
	//		0(отключено) -> 1(включено) -> 2(неопределено) -> 0 и т.д.
	
	Если СтрокаДерева.Пометка = 0 Тогда  
		// Если в колонке было значение 2, то при клике будет 0, 
		// скорее всего пользователь хочет установить все отчеты в группе, изменим значение на 1.
		СтрокаДерева.Пометка = 1;
	Иначе
		// В других случаях сработает значение по модулю 2. 
		// Если получится 0, то он сам в дальнейшем преобразуется в 2, если необходимо.
		СтрокаДерева.Пометка = СтрокаДерева.Пометка % 2;
	КонецЕсли;
	
	ПроставитьПометкиВниз(СтрокаДерева);
	
	ПроставитьПометкиВверх(СтрокаДерева);
	
КонецПроцедуры

#КонецОбласти