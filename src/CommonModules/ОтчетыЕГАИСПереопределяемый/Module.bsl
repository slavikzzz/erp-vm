#Область ПрограммныйИнтерфейс

// В процедуре нужно заполнить реквизиты переданных контрагентов для отображения в отчете "Информация об организации ЕГАИС".
//
// Параметры:
//  СоответствиеРеквизитовКонтрагентам - Соответствие - соответствие для заполнения.
//   * Ключ - ссылка на контрагента,
//   * Значение - заполненная структура значений.
//  СписокКонтрагентов - Массив - массив контрагентов, выводимых в отчет,
//  Реквизиты - Структура - ключ - имя реквизита, значение - значение, которое нужно заполнить:
//   * ТипОрганизации - ПеречислениеСсылка.ТипыОрганизацийЕГАИС
//   * Наименование - Строка
//   * НаименованиеПолное - Строка
//   * ИНН - Строка
//   * КПП - Строка
//   * КодСтраны - Число
//   * КодРегиона - Число
//   * ПочтовыйИндекс - Число
//   * Адрес - Строка.
Процедура ЗаполнитьРеквизитыКонтрагентов(СоответствиеРеквизитовКонтрагентам, СписокКонтрагентов, Реквизиты) Экспорт
	
	//++ НЕ ГОСИС
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокКонтрагентов", СписокКонтрагентов);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА Контрагенты.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицоНеРезидент)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыОрганизацийЕГАИС.ИностранныйКонтрагент)
	|		КОГДА Контрагенты.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ФизЛицо)
	|				ИЛИ Контрагенты.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ИндивидуальныйПредприниматель)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыОрганизацийЕГАИС.ИндивидуальныйПредпринимательРФ)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыОрганизацийЕГАИС.ЮридическоеЛицоРФ)
	|	КОНЕЦ КАК ТипОрганизации,
	|	Контрагенты.Наименование КАК Наименование,
	|	Контрагенты.НаименованиеПолное КАК НаименованиеПолное,
	|	Контрагенты.ИНН КАК ИНН,
	|	Контрагенты.КПП КАК КПП,
	|	ЕСТЬNULL(КонтрагентыКонтактнаяИнформация.ЗначенияПолей, """") КАК ЗначенияПолей,
	|	Контрагенты.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты.КонтактнаяИнформация КАК КонтрагентыКонтактнаяИнформация
	|		ПО Контрагенты.Ссылка = КонтрагентыКонтактнаяИнформация.Ссылка
	|			И (КонтрагентыКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес))
	|			И (КонтрагентыКонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ФактАдресКонтрагента))
	|ГДЕ
	|	Контрагенты.Ссылка В(&СписокКонтрагентов)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗначенияРеквизитов = ОбщегоНазначения.СкопироватьРекурсивно(Реквизиты, Ложь);
		ЗаполнитьЗначенияСвойств(ЗначенияРеквизитов, Выборка);
		
		Если ЗначениеЗаполнено(Выборка.ЗначенияПолей) Тогда
			АдресXDTO = УправлениеКонтактнойИнформациейЛокализация.КонтактнаяИнформацияИзXML(Выборка.ЗначенияПолей);
			СтруктураАдреса = РаботаСАдресами.СведенияОбАдресе(АдресXDTO);
			
			ЗначенияРеквизитов.КодСтраны = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(СтруктураАдреса.КодСтраны);
			ЗначенияРеквизитов.КодРегиона =СтроковыеФункцииКлиентСервер.СтрокаВЧисло(АдресныйКлассификатор.КодРегионаПоНаименованию(СтруктураАдреса.Регион));
			ЗначенияРеквизитов.ПочтовыйИндекс = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(СтруктураАдреса.Индекс);
			ЗначенияРеквизитов.Адрес = АдресXDTO.Представление;
		КонецЕсли;
		
		СоответствиеРеквизитовКонтрагентам.Вставить(Выборка.Ссылка, ЗначенияРеквизитов);
	КонецЦикла;
	//-- НЕ ГОСИС
	
КонецПроцедуры

// В процедуре нужно заполнить таблицу продаж по переданным параметрам для отчета "Обработанные чеки ЕГАИС".
//
// Параметры:
//  ТаблицаПродаж - ТаблицаЗначений - таблица, которую требуется заполнить. Колонки:
//   * Период - Дата
//   * ОрганизацияЕГАИС - СправочникСсылка.КлассификаторОрганизацийЕГАИС
//   * АлкогольнаяПродукция - СправочникСсылка.КлассификаторАлкогольнойПродукцииЕГАИС
//   * ЧековПродаж - Число
//   * ЧековНаВозврат - Число
Процедура ЗаполнитьКоличествоЧековПродаж(ТаблицаПродаж) Экспорт
	
	//++ НЕ ГОСИС
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаПродаж", ТаблицаПродаж);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаПродаж.Период КАК Период,
	|	ВЫРАЗИТЬ(ТаблицаПродаж.ОрганизацияЕГАИС КАК Справочник.КлассификаторОрганизацийЕГАИС) КАК ОрганизацияЕГАИС,
	|	ВЫРАЗИТЬ(ТаблицаПродаж.АлкогольнаяПродукция КАК Справочник.КлассификаторАлкогольнойПродукцииЕГАИС) КАК АлкогольнаяПродукция
	|ПОМЕСТИТЬ ВТ_ТаблицаПродаж
	|ИЗ
	|	&ТаблицаПродаж КАК ТаблицаПродаж
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПродаж.Период КАК Период,
	|	ТаблицаПродаж.ОрганизацияЕГАИС КАК ОрганизацияЕГАИС,
	|	ТаблицаПродаж.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ТаблицаПродаж.ОрганизацияЕГАИС.Контрагент КАК Организация,
	|	ТаблицаПродаж.ОрганизацияЕГАИС.ТорговыйОбъект КАК Склад
	|ПОМЕСТИТЬ ТаблицаПродаж
	|ИЗ
	|	ВТ_ТаблицаПродаж КАК ТаблицаПродаж
	|ГДЕ
	|	ТаблицаПродаж.ОрганизацияЕГАИС.Сопоставлено
	|	И ТаблицаПродаж.ОрганизацияЕГАИС.СоответствуетОрганизации
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Склад,
	|	АлкогольнаяПродукция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЧекККМ.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ СписокЧековПродаж
	|ИЗ
	|	Документ.ЧекККМ КАК ЧекККМ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЕГАИСПрисоединенныеФайлы КАК ЕГАИСПрисоединенныеФайлы
	|		ПО ЕГАИСПрисоединенныеФайлы.Документ = ЧекККМ.Ссылка
	|ГДЕ
	|	НАЧАЛОПЕРИОДА(ЧекККМ.Дата, МЕСЯЦ) В
	|			(ВЫБРАТЬ
	|				ТаблицаПродаж.Период
	|			ИЗ
	|				ТаблицаПродаж КАК ТаблицаПродаж)
	|	И ЧекККМ.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЧековККМ.Пробит)
	|	И НЕ ЕГАИСПрисоединенныеФайлы.Ссылка ЕСТЬ NULL
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПродаж.Период КАК Период,
	|	ТаблицаПродаж.ОрганизацияЕГАИС КАК ОрганизацияЕГАИС,
	|	ТаблицаПродаж.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЧекККМТовары.Ссылка) КАК ЧековПродаж
	|ПОМЕСТИТЬ Продажи
	|ИЗ
	|	Документ.ЧекККМ.Товары КАК ЧекККМТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаПродаж КАК ТаблицаПродаж
	|		ПО ЧекККМТовары.Ссылка.Организация = ТаблицаПродаж.Организация
	|			И ЧекККМТовары.Ссылка.Склад = ТаблицаПродаж.Склад
	|			И ЧекККМТовары.НоменклатураЕГАИС = ТаблицаПродаж.АлкогольнаяПродукция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СписокЧековПродаж КАК СписокЧековПродаж
	|		ПО ЧекККМТовары.Ссылка = СписокЧековПродаж.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаПродаж.Период,
	|	ТаблицаПродаж.ОрганизацияЕГАИС,
	|	ТаблицаПродаж.АлкогольнаяПродукция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЧекККМВозврат.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ СписокЧековНаВозврат
	|ИЗ
	|	Документ.ЧекККМВозврат КАК ЧекККМВозврат
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЕГАИСПрисоединенныеФайлы КАК ЕГАИСПрисоединенныеФайлы
	|		ПО ЕГАИСПрисоединенныеФайлы.Документ = ЧекККМВозврат.Ссылка
	|ГДЕ
	|	НАЧАЛОПЕРИОДА(ЧекККМВозврат.Дата, МЕСЯЦ) В
	|			(ВЫБРАТЬ
	|				ТаблицаПродаж.Период
	|			ИЗ
	|				ТаблицаПродаж КАК ТаблицаПродаж)
	|	И ЧекККМВозврат.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЧековККМ.Пробит)
	|	И НЕ ЕГАИСПрисоединенныеФайлы.Ссылка ЕСТЬ NULL
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПродаж.Период КАК Период,
	|	ТаблицаПродаж.ОрганизацияЕГАИС КАК ОрганизацияЕГАИС,
	|	ТаблицаПродаж.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЧекККМВозвратТовары.Ссылка) КАК ЧековНаВозврат
	|ПОМЕСТИТЬ Возвраты
	|ИЗ
	|	Документ.ЧекККМВозврат.Товары КАК ЧекККМВозвратТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаПродаж КАК ТаблицаПродаж
	|		ПО ЧекККМВозвратТовары.Ссылка.Организация = ТаблицаПродаж.Организация
	|			И ЧекККМВозвратТовары.Ссылка.Склад = ТаблицаПродаж.Склад
	|			И ЧекККМВозвратТовары.НоменклатураЕГАИС = ТаблицаПродаж.АлкогольнаяПродукция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СписокЧековНаВозврат КАК СписокЧековНаВозврат
	|		ПО ЧекККМВозвратТовары.Ссылка = СписокЧековНаВозврат.Ссылка
	|ГДЕ
	|	ЧекККМВозвратТовары.КоличествоУпаковок <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаПродаж.Период,
	|	ТаблицаПродаж.ОрганизацияЕГАИС,
	|	ТаблицаПродаж.АлкогольнаяПродукция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Период КАК Период,
	|	ВложенныйЗапрос.ОрганизацияЕГАИС КАК ОрганизацияЕГАИС,
	|	ВложенныйЗапрос.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	СУММА(ВложенныйЗапрос.ЧековПродаж) КАК ЧековПродаж,
	|	СУММА(ВложенныйЗапрос.ЧековНаВозврат) КАК ЧековНаВозврат
	|ИЗ
	|	(ВЫБРАТЬ
	|		Продажи.Период КАК Период,
	|		Продажи.ОрганизацияЕГАИС КАК ОрганизацияЕГАИС,
	|		Продажи.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|		Продажи.ЧековПродаж КАК ЧековПродаж,
	|		0 КАК ЧековНаВозврат
	|	ИЗ
	|		Продажи КАК Продажи
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Возвраты.Период,
	|		Возвраты.ОрганизацияЕГАИС,
	|		Возвраты.АлкогольнаяПродукция,
	|		0,
	|		Возвраты.ЧековНаВозврат
	|	ИЗ
	|		Возвраты КАК Возвраты) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Период,
	|	ВложенныйЗапрос.ОрганизацияЕГАИС,
	|	ВложенныйЗапрос.АлкогольнаяПродукция";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Период", Выборка.Период);
		ПараметрыОтбора.Вставить("ОрганизацияЕГАИС", Выборка.ОрганизацияЕГАИС);
		ПараметрыОтбора.Вставить("АлкогольнаяПродукция", Выборка.АлкогольнаяПродукция);
		
		МассивСтрок = ТаблицаПродаж.НайтиСтроки(ПараметрыОтбора);
		Для Каждого СтрокаТаблицы Из МассивСтрок Цикл
			ЗаполнитьЗначенияСвойств(СтрокаТаблицы, Выборка);
		КонецЦикла;
	КонецЦикла;
	//-- НЕ ГОСИС
	
КонецПроцедуры

#КонецОбласти