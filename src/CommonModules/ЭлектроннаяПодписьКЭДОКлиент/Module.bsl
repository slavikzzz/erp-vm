#Область СлужебныеПроцедурыИФункции

#Область ПроверкаПодписейВФормеДоверенности

Процедура ПроверитьПодписи(УправляемаяФорма, ОповещениеЗавершения, ПроверяемыеПодписи = Неопределено) Экспорт
	
	Если ПроверяемыеПодписи = Неопределено Тогда
		ПроверяемаяКоллекция = УправляемаяФорма.ЭлектронныеПодписи;
	Иначе
		ПроверяемаяКоллекция = Новый Массив;
		Для Каждого ИдентификаторПроверяемойПодписи Из ПроверяемыеПодписи Цикл
			ПроверяемаяКоллекция.Добавить(
				УправляемаяФорма.ЭлектронныеПодписи.НайтиПоИдентификатору(ИдентификаторПроверяемойПодписи));
		КонецЦикла;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПроверяемаяКоллекция) Тогда
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("АдресДоверенности", УправляемаяФорма.АдресДоверенности);
		ДополнительныеПараметры.Вставить("ПроверяемаяКоллекция", ПроверяемаяКоллекция);
		ДополнительныеПараметры.Вставить("ИндексПроверяемойПодписи", 0);
		Если ОповещениеЗавершения <> Неопределено Тогда
			ДополнительныеПараметры.Вставить("ОповещениеЗавершения", ОповещениеЗавершения);
		КонецЕсли;
		
		Оповещение = Новый ОписаниеОповещения("ПослеПроверкиПодписиКоллекции", ЭтотОбъект, ДополнительныеПараметры);
		ПроверитьПодписьКоллекции(Оповещение);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьПодписьКоллекции(Оповещение)
	
	ИндексПроверяемойПодписи = Оповещение.ДополнительныеПараметры.ИндексПроверяемойПодписи;
	Если ИндексПроверяемойПодписи < Оповещение.ДополнительныеПараметры.ПроверяемаяКоллекция.Количество() Тогда
		ДанныеПодписи = Оповещение.ДополнительныеПараметры.ПроверяемаяКоллекция[ИндексПроверяемойПодписи];
		ПроверитьПодпись(Оповещение, Оповещение.ДополнительныеПараметры.АдресДоверенности,
			ДанныеПодписи.АдресПодписи, , ДанныеПодписи.ДатаПодписи);
	Иначе
		Если Оповещение.ДополнительныеПараметры.Свойство("ОповещениеЗавершения") Тогда
			ВыполнитьОбработкуОповещения(Оповещение.ДополнительныеПараметры.ОповещениеЗавершения, Истина);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеПроверкиПодписиКоллекции(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Истина Тогда
		ДанныеПодписи = ДополнительныеПараметры.ПроверяемаяКоллекция[ДополнительныеПараметры.ИндексПроверяемойПодписи];
		ДанныеПодписи.ДатаПроверкиПодписи = ОбщегоНазначенияКлиент.ДатаСеанса();
		ДанныеПодписи.ПодписьВерна        = Истина;
		ДанныеПодписи.ОписаниеОшибки      = "";
		ЭлектроннаяПодписьКЭДОКлиентСервер.ЗаполнитьСтатусПроверкиПодписиОбъекта(ДанныеПодписи);
	ИначеЕсли ТипЗнч(Результат) = Тип("Строка") Тогда
		ДанныеПодписи = ДополнительныеПараметры.ПроверяемаяКоллекция[ДополнительныеПараметры.ИндексПроверяемойПодписи];
		ДанныеПодписи.ДатаПроверкиПодписи = ОбщегоНазначенияКлиент.ДатаСеанса();
		ДанныеПодписи.ПодписьВерна        = Ложь;
		ДанныеПодписи.ОписаниеОшибки      = Результат;
		ЭлектроннаяПодписьКЭДОКлиентСервер.ЗаполнитьСтатусПроверкиПодписиОбъекта(ДанныеПодписи);
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеПроверкиПодписиКоллекции", ЭтотОбъект, ДополнительныеПараметры);
	ДополнительныеПараметры.ИндексПроверяемойПодписи =
		ДополнительныеПараметры.ИндексПроверяемойПодписи + 1;
	
	ПроверитьПодписьКоллекции(Оповещение);
	
КонецПроцедуры

Процедура ПроверитьПодпись(Оповещение, ПодписанныеДанные, ДанныеПодписи, МенеджерКриптографии = Неопределено, ДатаПодписи = Неопределено)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеЗавершения", Оповещение);
	ДополнительныеПараметры.Вставить("ПодписанныеДанные",    ПодписанныеДанные);
	ДополнительныеПараметры.Вставить("ДанныеПодписи",        ДанныеПодписи);
	ДополнительныеПараметры.Вставить("ДатаПодписи",          ДатаПодписи);
	
	Оповещение = Новый ОписаниеОповещения("ПроверитьПодписьСПомощьюМенеджераКриптографии", ЭтотОбъект, ДополнительныеПараметры);
	Если МенеджерКриптографии = Неопределено Тогда
		
		ПараметрыСоздания = ЭлектроннаяПодписьСлужебныйКлиент.ПараметрыСозданияМенеджераКриптографии();
		ПараметрыСоздания.ПоказатьОшибку = Неопределено;
		
		ЭлектроннаяПодписьСлужебныйКлиент.СоздатьМенеджерКриптографии(Оповещение, "", ПараметрыСоздания);
		
	Иначе
		ВыполнитьОбработкуОповещения(Оповещение, МенеджерКриптографии);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьПодписьСПомощьюМенеджераКриптографии(МенеджерКриптографии, ДополнительныеПараметры) Экспорт
	
	Если МенеджерКриптографии <> Неопределено Тогда
		
		ЭлектроннаяПодписьСлужебныйКлиент.ПроверитьПодпись(
			ДополнительныеПараметры.ОповещениеЗавершения,
			ДополнительныеПараметры.ПодписанныеДанные,
			ДополнительныеПараметры.ДанныеПодписи,
			МенеджерКриптографии,
			ДополнительныеПараметры.ДатаПодписи, Ложь);
		
	Иначе
		Состояние(НСтр("ru = 'Ошибка проверки подписи';
						|en = 'Signature verification error'"), , НСтр("ru = 'Не найден криптопровайдер';
																|en = 'A cryptographic service provider is not found'"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПодписаниеДанных

Процедура ПодписатьДоверенность(УправляемаяФорма, Объект, АдресДанных, ОбработкаЗавершения) Экспорт
	
	ОписаниеДанных = Новый Структура;
	ОписаниеДанных.Вставить("БезПодтверждения",    Истина);
	ОписаниеДанных.Вставить("ПоказатьКомментарий", Ложь);
	ОписаниеДанных.Вставить("Операция",            НСтр("ru = 'Подписание доверенности';
														|en = 'Sign the authorization letter'"));
	ОписаниеДанных.Вставить("ЗаголовокДанных",     НСтр("ru = 'Доверенность';
														|en = 'Authorization letter'"));
	ОписаниеДанных.Вставить("Данные",              АдресДанных);
	ОписаниеДанных.Вставить("Представление",       Строка(Объект));
	ОписаниеДанных.Вставить("Объект",              Объект);
	
	Подписать(УправляемаяФорма, ОписаниеДанных, ОбработкаЗавершения);
	
КонецПроцедуры

Процедура Подписать(УправляемаяФорма, ОписаниеДанных, ОбработкаЗавершения, ОбработкаЗавершенияСОшибкой = Неопределено)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОписаниеДанных", ОписаниеДанных);
	ДополнительныеПараметры.Вставить("ОбработкаЗавершения", ОбработкаЗавершения);
	Если ОбработкаЗавершенияСОшибкой <> Неопределено Тогда
		ДополнительныеПараметры.Вставить("ОбработкаЗавершенияСОшибкой", ОбработкаЗавершенияСОшибкой);
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеПодписанияДанныхСертификатом", ЭтотОбъект, ДополнительныеПараметры);
	ЭлектроннаяПодписьКлиент.Подписать(ОписаниеДанных, УправляемаяФорма, Оповещение);
	
КонецПроцедуры

Процедура ПослеПодписанияДанныхСертификатом(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") И Результат.Успех Тогда
		Если ДополнительныеПараметры.Свойство("ОбработкаЗавершения") Тогда
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОбработкаЗавершения, Истина);
		КонецЕсли;
	Иначе
		Если ДополнительныеПараметры.Свойство("ОбработкаЗавершенияСОшибкой") Тогда
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОбработкаЗавершенияСОшибкой, Результат);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти