#Область СлужебныеПроцедурыИФункции

Процедура ОткрытьФормуНастройкиОтправкиСотрудника(Форма) Экспорт

	ДополнительныеПараметры = Новый Структура("ЗаписатьЭлемент, Форма", Истина, Форма);

	Если Форма.СозданиеНового И НЕ ЗначениеЗаполнено(Форма.ФизическоеЛицо.Ссылка) Тогда

		ТекстВопроса = НСтр("ru = 'Данные еще не записаны.
			|Переход к отправке сотрудника в систему бронирования возможен только после записи данных.
			|Данные будут записаны.';
			|en = 'Data is not saved yet.
			|You can send employee data to the booking system only after saving the data.
			|Data will be saved.'");

		Оповещение = Новый ОписаниеОповещения("ОткрытьФормуНастройкиОтправкиСотрудникаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);

	Иначе

		ДополнительныеПараметры.ЗаписатьЭлемент = Ложь;
		ОткрытьФормуНастройкиОтправкиСотрудникаЗавершение(Неопределено, ДополнительныеПараметры);

	КонецЕсли;

КонецПроцедуры

Процедура ОткрытьФормуНастройкиОтправкиСотрудникаЗавершение(Ответ, ДополнительныеПараметры) Экспорт

	Если Ответ = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;

	ФормаВладелец = ДополнительныеПараметры.Форма;

	Если ДополнительныеПараметры.ЗаписатьЭлемент И Не ФормаВладелец.Записать() Тогда
		Возврат;
	КонецЕсли;

	Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '%1: Отправка в %2';
			|en = '%1: Sending to %2'"), ФормаВладелец.ФизическоеЛицо.Наименование, "Smartway");

	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ЗакрыватьПриЗакрытииВладельца", Истина);
	ПараметрыОткрытия.Вставить("ФизическоеЛицо", ФормаВладелец.ФизическоеЛицо.Ссылка);
	ПараметрыОткрытия.Вставить("Заголовок", Заголовок);

	ОткрытьФорму("Обработка.НастройкаОтправкиСотрудниковБронированияКомандировок.Форма.НастройкаОтправкиСотрудника",
		ПараметрыОткрытия, ФормаВладелец, Новый УникальныйИдентификатор());

КонецПроцедуры

#КонецОбласти
