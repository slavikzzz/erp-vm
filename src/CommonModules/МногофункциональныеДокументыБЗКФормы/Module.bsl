
#Область СлужебныйПрограммныйИнтерфейс


// Устанавливает видимость команды "Утвердить" в формах списках или журналов многофункциональных документов
// 
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - форма списка
// 	РазделДанных - Строка - имя реквизита формы типа динамическим список, связанного с элементов формы
// 							отображающим список документов	
//
Процедура УстановитьВидимостьКомандыУтвердитьВСписке(Форма, ИмяРеквизитаСписок = "Список") Экспорт
	Список = Форма[ИмяРеквизитаСписок]; // ДинамическийСписок

	МетаданныеОтображаемогоОбъекта = Метаданные.НайтиПоПолномуИмени(Список.ОсновнаяТаблица); // ОбъектМетаданныхДокумент, ОбъектМетаданныхЖурналДокументов
	
	Если Метаданные.Документы.Содержит(МетаданныеОтображаемогоОбъекта) Тогда
		МетаданныеОтображаемыхДокументов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(
			МетаданныеОтображаемогоОбъекта);
			
	ИначеЕсли Метаданные.ЖурналыДокументов.Содержит(МетаданныеОтображаемогоОбъекта) Тогда 
		МетаданныеОтображаемыхДокументов = МетаданныеОтображаемогоОбъекта.РегистрируемыеДокументы;
			
	Иначе
		Сообщение = НСтр("ru = 'Поддерживается только работа со списками отображающими данные документов и журналов документов.';
						|en = 'Working with lists displaying document data and document journals is supported only.'");
		ВызватьИсключение Сообщение;
	КонецЕсли;
	
	Видимость = КомандаУтвердитьВСпискеДоступна(МетаданныеОтображаемыхДокументов);	
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы, 
		"ФормаУтвердить", 
		"Видимость", 
		Видимость);
КонецПроцедуры	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция КомандаУтвердитьВСпискеДоступна(МетаданныеОтображаемыхДокументов)
	ВсеРазделы = МногофункциональныеДокументыБЗККлиентСервер.РазделыДанных();
	ВсеВидыПрав = МногофункциональныеДокументыБЗККлиентСервер.ВидыПравНаРазделыДанных();
	
	ВыполняетсяПредварительныйРасчет 
		= РасчетЗарплатыРасширенный.НастройкиРасчетаЗарплаты().ВыполнятьПредварительныйРасчетДокументов;	
	
	Для Каждого ОтотбражаемыйДокумент Из МетаданныеОтображаемыхДокументов Цикл
		ТипДокумента = Тип("ДокументОбъект." + ОтотбражаемыйДокумент.Имя);
		Если Не Метаданные.ОпределяемыеТипы.МногофункциональныйДокументОбъектБЗК.Тип.СодержитТип(ТипДокумента) Тогда
			Продолжить;
		КонецЕсли;	
		
		МенеджерДокумента = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ОтотбражаемыйДокумент.ПолноеИмя());
		
		ОписаниеРазделов = МенеджерДокумента.ОписаниеРазделовДанных();
		
		Для Каждого Раздел Из ОписаниеРазделов Цикл
			Если Раздел.Ключ = ВсеРазделы.НачисленнаяЗарплата
				И Не ВыполняетсяПредварительныйРасчет Тогда
					
				Возврат Ложь;
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(Раздел.Значение.РеквизитСостояние) 
				Или Раздел.Значение.РеквизитСостояние = "Проведен" Тогда
				Продолжить;
			КонецЕсли;
				
			Если МногофункциональныеДокументыБЗК.ЕстьПравоНаРаздел(ВсеВидыПрав.Редактирование, Раздел.Ключ) Тогда
				Возврат Истина;
			КонецЕсли;	
		КонецЦикла;	
	КонецЦикла;	
	
	Возврат Ложь;		
КонецФункции	

#КонецОбласти

