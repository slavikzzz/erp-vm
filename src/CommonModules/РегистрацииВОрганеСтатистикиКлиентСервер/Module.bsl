#Область СлужебныйПрограммныйИнтерфейс

Процедура НастроитьОтображениеРегистрацииВОрганеСтатистики(Форма, ИмяРеквизита = "ИспользованиеРегистрацийВОрганеСтатистики") Экспорт
	
	ПараметрыПредставления = ПараметрыПредставленияРегистрации(
		Форма[ИмяРеквизита].Регистрация,
		Форма[ИмяРеквизита].Период,
		Форма.Объект.Ссылка);
		
	Форма.ПредставлениеРегистрацииВОрганеСтатистики = ПараметрыПредставления.Представление;
	
	ЭлементПредставления = Форма.Элементы.ПредставлениеРегистрацииВОрганеСтатистики;
	ЭлементПредставления.Видимость = Истина;
	ЭлементПредставления.Заголовок = ПараметрыПредставления.Заголовок;
	Если ПараметрыПредставления.НеИспользуется Тогда
		ЭлементПредставления.Подсказка = НСтр("ru = 'Действует регистрация организации';
												|en = 'The company registration is active'");
		ЭлементПредставления.ОтображениеПодсказки = ОтображениеПодсказки.Всплывающая;
		ЭлементПредставления.Высота = 0;
		ЭлементПредставленияВысотаЗаголовка = 1;
	Иначе
		ЭлементПредставления.Подсказка = "";
		ЭлементПредставления.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
		ЭлементПредставления.Высота = 4;
		ЭлементПредставленияВысотаЗаголовка = 2;
	КонецЕсли;
	
	#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
		ЭлементПредставления.ВысотаЗаголовка = ЭлементПредставленияВысотаЗаголовка;
	#КонецЕсли
	
КонецПроцедуры

Процедура НастроитьОтображениеРегистрацииВОрганеСтатистикиСУчетомИерархии(Форма) Экспорт
	
	ИмяРеквизита = "ИспользованиеРегистрацийВОрганеСтатистики";
	Если ЗначениеЗаполнено(Форма[ИмяРеквизита].Регистрация) Тогда
		Форма.Элементы.ИсторияИспользованияРегистрацииВОрганеСтатистики.Видимость = Истина;
		Форма.Элементы.ИзменитьРегистрациюВОрганеСтатистики.Видимость = Ложь;
	Иначе
		ИмяРеквизита = "ИспользованиеРегистрацийВОрганеСтатистикиГоловного";
		Если ЗначениеЗаполнено(Форма[ИмяРеквизита].Регистрация) Тогда
			Форма.Элементы.ИзменитьРегистрациюВОрганеСтатистики.Видимость = Истина;
			Форма.Элементы.ИсторияИспользованияРегистрацииВОрганеСтатистики.Видимость = Ложь;
		Иначе
			Форма.Элементы.ИзменитьРегистрациюВОрганеСтатистики.Видимость = Ложь;
			Форма.Элементы.ИсторияИспользованияРегистрацииВОрганеСтатистики.Видимость = Истина;
		КонецЕсли;
	КонецЕсли;
	
	НастроитьОтображениеРегистрацииВОрганеСтатистики(Форма, ИмяРеквизита);
	
КонецПроцедуры

Процедура ОбновитьОбластьИсключитьТОСП(Форма, ИмяТД = "ПолеТабличногоДокументаФормаОтчета") Экспорт
	
	Если Не ИспользуютсяРегистрацииВОрганеСтатистики() Тогда
		Возврат;
	КонецЕсли;
	
	Если Форма.РегистрацияРеспондента <> Неопределено Тогда
		СвойстваРеспондента = РегистрацииВОрганеСтатистикиВызовСервера.СвойстваРеспондентаПоРегистрации(Форма.РегистрацияРеспондента);
		ВидимостьФлага = СвойстваРеспондента.ИмеетТОСП;
	Иначе
		ВидимостьФлага = Истина;
	КонецЕсли;
	
	НастроитьОбластьИсключитьТОСП(Форма[ИмяТД], ВидимостьФлага, Форма.ИсключитьДанныеОбособленныхПодразделений);
	
КонецПроцедуры

Функция ИспользуютсяРегистрацииВОрганеСтатистики() Экспорт
	
	Возврат РегламентированнаяОтчетностьКлиентСервер.ИспользуютсяРегистрацииВОрганеСтатистики();
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПараметрыПредставленияРегистрации(Регистрация, Период, СтруктурнаяЕдиница)
	
	Результат = Новый Структура;
	Результат.Вставить("ЭтоРеспондент", Ложь);
	Результат.Вставить("Представление", "");
	Результат.Вставить("Заголовок", "");
	Результат.Вставить("НеИспользуется", Ложь);
	
	Если Регистрация.Пустая() Тогда
		Результат.Заголовок = НСтр("ru = 'Регистрация Росстата';
									|en = 'Registration of Russian Federal State Statistics Service'");
		Результат.Представление = Новый ФорматированнаяСтрока(НСтр("ru = '<Не используется>';
																	|en = '<Not used>'"));
		Результат.НеИспользуется = Истина;
	Иначе
		Данные = РегистрацииВОрганеСтатистикиВызовСервера.СвойстваРегистрацииВОрганеСтатистики(Регистрация);
		Данные.Вставить("Период", Период);
		Данные.Вставить("Ссылка", Регистрация);
		Результат.ЭтоРеспондент = Данные.СтруктурнаяЕдиница = СтруктурнаяЕдиница;
		Если Результат.ЭтоРеспондент Тогда
			Результат.Заголовок = НСтр("ru = 'Регистрация Росстата (является респондентом)';
										|en = 'Registration of Russian Federal State Statistics Service (is a respondent)'");
			Результат.Представление = ПредставлениеРегистрации(Данные, Истина);
		Иначе
			Результат.Заголовок = НСтр("ru = 'Регистрация отчитывающегося респондента Росстата';
										|en = 'Register a reporting respondent of the Russian Federal State Statistics Service'");
			Результат.Представление = ПредставлениеРегистрации(Данные);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПредставлениеРегистрации(Данные, ПоказатьСсылку = Ложь)
	
	ЧастиПредставления = Новый Массив;
	Если ЗначениеЗаполнено(Данные) Тогда
		Если ПоказатьСсылку Тогда
			ЧастиПредставления.Добавить(Новый ФорматированнаяСтрока(Данные.Наименование,,,,ПолучитьНавигационнуюСсылку(Данные.Ссылка)));
		Иначе
			ЧастиПредставления.Добавить(Данные.Наименование);
		КонецЕсли;
		ЧастиПредставления.Добавить(Символы.ПС);
		ЧастиПредставления.Добавить(СтрШаблон(НСтр("ru = 'Используется с %1';
													|en = 'Used since %1'"), Формат(Данные.Период, "ДЛФ=DD")) + Символы.ПС);
		ЧастиПредставления.Добавить(СтрШаблон(НСтр("ru = 'Код органа ФСГС: %1';
													|en = 'FSSS authority code: %1'"), Данные.КодОрганаФСГС) + Символы.ПС);
		Если ЗначениеЗаполнено(Данные.ИдентификационныйНомер) Тогда
			ЧастиПредставления.Добавить(СтрШаблон(НСтр("ru = 'Идентификационный номер: %1';
														|en = 'Identification number: %1'"), Данные.ИдентификационныйНомер) + Символы.ПС);
		КонецЕсли;
	КонецЕсли;
	Представление = Новый ФорматированнаяСтрока(ЧастиПредставления);
	
	Возврат Представление;
	
КонецФункции

Процедура НастроитьОбластьИсключитьТОСП(ТабДокумент, Знач Видимость = Истина, Знач Значение = Ложь)
	
	ОбластьФлаг = ТабДокумент.Области["ИсключитьДанныеТОСП"];
	ОбластьИсключить = ТабДокумент.Области["ИсключитьДанныеТОСП_Заголовок"];
	
	Если Видимость Тогда
		ОбластьИсключить.Текст = НСтр("ru = 'Исключить данные обособленных подразделений';
										|en = 'Exclude data of branch offices'");
		
		ОбластьФлаг.Защита = Ложь;
		ОбластьФлаг.Значение = Значение;
		ОбластьФлаг.Гиперссылка = Истина;
		ОбластьФлаг.ЦветФона = Новый Цвет(255,255,192);
		ОбластьФлаг.ГраницаСнизу  = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная, 1);
		ОбластьФлаг.ГраницаСверху = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная, 1);
		ОбластьФлаг.ГраницаСправа = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная, 1);
		ОбластьФлаг.ГраницаСлева  = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная, 1);		
	Иначе
		ОбластьИсключить.Текст = "";
		
		ОбластьФлаг.Защита = Истина;
		ОбластьФлаг.Значение = "";
		ОбластьФлаг.Гиперссылка = Ложь;
		ОбластьФлаг.ЦветФона = Новый Цвет(255,255,255);
		ОбластьФлаг.ГраницаСнизу  = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.НетЛинии);
		ОбластьФлаг.ГраницаСверху = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.НетЛинии);
		ОбластьФлаг.ГраницаСправа = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.НетЛинии);
		ОбластьФлаг.ГраницаСлева  = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.НетЛинии);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти