////////////////////////////////////////////////////////////////////////////////
// Процедуры, используемые для локализации документа "Наработка объектов эксплуатации".
// 
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

Функция ТекстЗапросаДляЗаполненияДокументаНаОснованииОбъектаЭксплуатации() Экспорт

	ТекстЗапроса = Неопределено;
	
	//++ Локализация
	
	ТекстЗапроса =
	"
	//++ НЕ УТКА
	|ВЫБРАТЬ
	|	Объекты.Ссылка КАК ОбъектЭксплуатации,
	|	ЕСТЬNULL(ПоказателиНаработки.ПоказательНаработки, ЗНАЧЕНИЕ(Справочник.ПоказателиНаработки.ПустаяСсылка)) КАК ПоказательНаработки,
	|	Объекты.Статус КАК Статус,
	|	ВЫБОР
	|		КОГДА Объекты.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОбъектовЭксплуатации.ВЭксплуатации)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьОшибкиСтатус,
	|	Объекты.ПометкаУдаления КАК ЕстьОшибкиУдален,
	|	Объекты.ЭтоГруппа КАК ЕстьОшибкиГруппа
	|ИЗ
	|	Справочник.ОбъектыЭксплуатации КАК Объекты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассыОбъектовЭксплуатации.ПоказателиНаработки КАК ПоказателиНаработки
	|		ПО Объекты.Класс = ПоказателиНаработки.Ссылка
	|			И (НЕ ПоказателиНаработки.РегистрироватьОтИсточника)
	|ГДЕ
	|	Объекты.Ссылка = &ОбъектЭксплуатации И &ИспользоватьУправлениеРемонтами
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	//-- НЕ УТКА
	|ВЫБРАТЬ
	|	Объекты.Ссылка КАК ОбъектЭксплуатации,
	|	ЕСТЬNULL(ПорядокУчетаОС.ПоказательНаработки, ЗНАЧЕНИЕ(Справочник.ПоказателиНаработки.ПустаяСсылка)) КАК ПоказательНаработки,
	|	Объекты.Статус КАК Статус,
	|	ЛОЖЬ КАК ЕстьОшибкиСтатус,
	|	Объекты.ПометкаУдаления КАК ЕстьОшибкиУдален,
	|	Объекты.ЭтоГруппа КАК ЕстьОшибкиГруппа
	|ИЗ
	|	Справочник.ОбъектыЭксплуатации КАК Объекты
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОС.СрезПоследних(
	|				&Дата, 
	|				ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|					И ОсновноеСредство В (&ОбъектЭксплуатации)) КАК ПорядокУчетаОС
	|		ПО Объекты.Ссылка = ПорядокУчетаОС.ОсновноеСредство
	|ГДЕ
	|	Объекты.Ссылка = &ОбъектЭксплуатации
	|	И НЕ &ИспользоватьУправлениеРемонтами 
	|
	//++ НЕ УТКА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Узлы.Ссылка КАК ОбъектЭксплуатации,
	|	ЕСТЬNULL(ПоказателиНаработки.ПоказательНаработки, ЗНАЧЕНИЕ(Справочник.ПоказателиНаработки.ПустаяСсылка)) КАК ПоказательНаработки,
	|	Узлы.Статус КАК Статус,
	|	ВЫБОР
	|		КОГДА Узлы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОбъектовЭксплуатации.ВЭксплуатации)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьОшибкиСтатус,
	|	Узлы.ПометкаУдаления КАК ЕстьОшибкиУдален
	|ИЗ
	|	Справочник.УзлыОбъектовЭксплуатации КАК Узлы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассыОбъектовЭксплуатации.ПоказателиНаработки КАК ПоказателиНаработки
	|		ПО Узлы.Класс = ПоказателиНаработки.Ссылка
	|			И (НЕ ПоказателиНаработки.РегистрироватьОтИсточника)
	|ГДЕ
	|	Узлы.Ссылка = &ОбъектЭксплуатации
	//-- НЕ УТКА
	|";
	
	//-- Локализация

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПоказателиРегл() Экспорт

	ТекстЗапроса = Неопределено;
	
	//++ Локализация
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПорядокУчетаОСБУ.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ СписокПоНаработке
	|ИЗ
	|	РегистрСведений.ПараметрыАмортизацииОСУУ.СрезПоследних(&ТекущаяДата, ДатаИсправления = ДАТАВРЕМЯ(1,1,1)) КАК ПорядокУчетаОСБУ
	|ГДЕ
	|	ПорядокУчетаОСБУ.МетодНачисленияАмортизации В(&СпособыНачисленияАмортизации)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПервоначальныеСведенияОС.ОсновноеСредство
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияОС.СрезПоследних(&ТекущаяДата, ДатаИсправления = ДАТАВРЕМЯ(1,1,1)) КАК ПервоначальныеСведенияОС
	|ГДЕ
	|	ПервоначальныеСведенияОС.МетодНачисленияАмортизацииБУ В(&СпособыНачисленияАмортизации)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПорядокУчетаОСБУ.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ ПринятыеКУчетуОС
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОСБУ.СрезПоследних(
	|			&ТекущаяДата,
	|			ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|				И ОсновноеСредство В
	|					(ВЫБРАТЬ
	|						СписокПоНаработке.ОсновноеСредство
	|					ИЗ
	|						СписокПоНаработке)) КАК ПорядокУчетаОСБУ
	|ГДЕ
	|	ПорядокУчетаОСБУ.СостояниеБУ = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
	|	И ВЫБОР
	|			КОГДА &ИспользоватьУправлениеРемонтами
	|				ТОГДА ПорядокУчетаОСБУ.ОсновноеСредство.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыОбъектовЭксплуатации.ВЭксплуатации))
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПорядокУчетаОСУУ.ОсновноеСредство
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОСУУ.СрезПоследних(
	|			&ТекущаяДата,
	|			ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|				И ОсновноеСредство В
	|					(ВЫБРАТЬ
	|						СписокПоНаработке.ОсновноеСредство
	|					ИЗ
	|						СписокПоНаработке)) КАК ПорядокУчетаОСУУ
	|ГДЕ
	|	ПорядокУчетаОСУУ.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
	|	И ВЫБОР
	|			КОГДА &ИспользоватьУправлениеРемонтами
	|				ТОГДА ПорядокУчетаОСУУ.ОсновноеСредство.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыОбъектовЭксплуатации.ВЭксплуатации))
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПорядокУчетаОС.ОсновноеСредство КАК ОбъектЭксплуатации,
	|	ПРЕДСТАВЛЕНИЕ(ПорядокУчетаОС.ОсновноеСредство) КАК Представление,
	|	ПорядокУчетаОС.ПоказательНаработки КАК ПоказательНаработки,
	|	1 КАК Периодичность,
	|	1 КАК ВидПериодичности
	|ПОМЕСТИТЬ ПоказателиРегл
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОС.СрезПоследних(
	|			&ТекущаяДата,
	|			ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|				И ОсновноеСредство В
	|					(ВЫБРАТЬ
	|						ПринятыеКУчетуОС.ОсновноеСредство
	|					ИЗ
	|						ПринятыеКУчетуОС)) КАК ПорядокУчетаОС
	|ГДЕ
	|	ПорядокУчетаОС.ПоказательНаработки <> ЗНАЧЕНИЕ(Справочник.ПоказателиНаработки.ПустаяСсылка)";
	
	//-- Локализация
	
	Возврат ТекстЗапроса;

КонецФункции
 
#КонецОбласти
