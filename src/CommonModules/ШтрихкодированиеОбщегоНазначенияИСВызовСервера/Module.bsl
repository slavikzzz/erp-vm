
#Область ПрограммныйИнтерфейс

// Выполняет обработку штрихкода и возвращает результат этой обработки.
//
// Параметры:
//  ВходящиеДанные - Структура - Данные штрихкода.
//  ПараметрыСканирования - (См. ШтрихкодированиеОбщегоНазначенияИСКлиент.ПараметрыСканирования).
//  КэшированныеЗначения - Структура - Содержит поля кэшируемых значений.
//  УникальныйИдентификатор - УникальныйИдентификатор - Идентификатор, по которому будут помещены данные по обработанным
//                                                      штрихкодам в хранилище.
// Возвращаемое значение:
//  Структура - (См. ШтрихкодированиеОбщегоНазначенияИС.ИнициализироватьРезультатОбработкиШтрихкода).
Функция ОбработатьШтрихкод(ВходящиеДанные, ПараметрыСканирования, КэшированныеЗначения, УникальныйИдентификатор) Экспорт
	
	ДанныеШтрихкода = ОбщегоНазначения.СкопироватьРекурсивно(ВходящиеДанные);
	ШтрихкодированиеОбщегоНазначенияИСКлиентСервер.ДекодироватьШтрихкодДанныхBase64(ДанныеШтрихкода, Истина);
	
	СписокДанныхШтрихкодов = Новый Массив;
	СписокДанныхШтрихкодов.Добавить(ДанныеШтрихкода);
	
	РезультатОбработкиШтрихкодов = ШтрихкодированиеОбщегоНазначенияИС.ОбработатьШтрихкоды(
		СписокДанныхШтрихкодов, ПараметрыСканирования, КэшированныеЗначения, УникальныйИдентификатор);
	
	// Если нашли в данных исходный штрихкод - возвращаем его.
	ДанныеШтрихкода = РезультатОбработкиШтрихкодов.РезультатыОбработки[СокрЛП(ДанныеШтрихкода.Штрихкод)];
	Если ДанныеШтрихкода <> Неопределено Тогда
		Возврат ДанныеШтрихкода;
	КонецЕсли;
	
	// Иначе - первый по списку.
	Для Каждого КлючЗНачение Из РезультатОбработкиШтрихкодов.РезультатыОбработки Цикл
		ДанныеШтрихкода = КлючЗНачение.Значение;
		Прервать;
	КонецЦикла;
	
	//@skip-check constructor-function-return-section
	Возврат ДанныеШтрихкода;
	
КонецФункции

// Функция проверяет, являются ли выбранные данные данными выбытия
// 
// Параметры:
//  Номенклатура - ОпределяемыйТип.Номенклатура - исходная номенклатура
//  Характеристика - ОпределяемыйТип.ХарактеристикаНоменклатуры - характеристика исходной номенклатуры
//  НоменклатураВыбытия - ОпределяемыйТип.Номенклатура - номенклатура настроенного выбытия
//  ХарактеристикаВыбытия - ОпределяемыйТип.ХарактеристикаНоменклатуры - характеристики номенклатуры настроенного выбытия
// 
// Возвращаемое значение:
//  Булево
Функция ЭтоНастроенноеВыбытие(Номенклатура, Характеристика, НоменклатураВыбытия, ХарактеристикаВыбытия) Экспорт
	
	Возврат РегистрыСведений.НастройкиЧастичногоВыбытияПродукцииИС.ЭтоНастроенноеВыбытие(
			Номенклатура, Характеристика, НоменклатураВыбытия, ХарактеристикаВыбытия);
	
КонецФункции

#КонецОбласти
