
#Область ПрограммныйИнтерфейс

//++ НЕ УТ

// Открывает форму отчета-ведомости по ОС.
//
// Параметры:
//  ПараметрыОтчета - Структура - см. ЗаполнениеФинОтчетностиВБанки.ПараметрыОтчетаВедомостьАмортизацииОС()
//  ФормаВладелец - ФормаУправляемогоПриложения - форма документа ФинОтчетВБанк.
//
Процедура ОткрытьОтчетВедомостьАмортизацииОС(ПараметрыОтчета, ФормаВладелец) Экспорт
	
	ПараметрыОткрытия = ПараметрыОтчета.ПользовательскиеНастройки.ДополнительныеСвойства;

	ВедомостьПоОС = ЗаполнениеФинОтчетностиВБанкиВызовСервера.ДоступныйВариантОтчета(
						"ВедомостьПоОС2_4", ПараметрыОткрытия.ИмяВарианта, Истина);

	ВариантыОтчетовКлиент.ОткрытьФормуОтчета(ФормаВладелец, ВедомостьПоОС, ПараметрыОтчета);
	
КонецПроцедуры

// Открывает форму отчета по валовой прибыли.
//
// Параметры:
//  ПараметрыОтчета - Структура - см. ЗаполнениеФинОтчетностиВБанки.ПараметрыОтчетаВаловаяПрибыль()
//  ФормаВладелец - ФормаУправляемогоПриложения - форма документа ФинОтчетВБанк.
//
Процедура ОткрытьОтчетВаловаяПрибыль(ПараметрыОтчета, ФормаВладелец) Экспорт

	ПараметрыОткрытия = ПараметрыОтчета.ПользовательскиеНастройки.ДополнительныеСвойства;

	ВыручкаИСебестоимостьПродаж = ЗаполнениеФинОтчетностиВБанкиВызовСервера.ДоступныйВариантОтчета(
						"ВыручкаИСебестоимостьПродаж", ПараметрыОткрытия.ИмяВарианта, Истина);

	ВариантыОтчетовКлиент.ОткрытьФормуОтчета(ФормаВладелец, ВыручкаИСебестоимостьПродаж, ПараметрыОтчета);

КонецПроцедуры

// Формирует печатную форму "Кассовая книга".
//
// Параметры:
//  ПараметрыОтчета - Структура - см. ЗаполнениеФинОтчетностиВБанки.ПараметрыОтчетаКассоваяКнига()
//  ФормаВладелец - ФормаУправляемогоПриложения - форма документа ФинОтчетВБанк.
//
Процедура ОткрытьОтчетКассоваяКнига(ПараметрыОтчета, ФормаВладелец) Экспорт
	
	КлючиСтрокАвтозаполняемыхОтчетов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(
		ПараметрыОтчета.ПараметрыРежимаВыгрузки.КлючСтрокиОтчета);
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("КлючСтрокиСубъекта", 0);
	ДополнительныеПараметры.Вставить("КлючиСтрокОтчетов",  КлючиСтрокАвтозаполняемыхОтчетов);
	
	ФормаВладелец.ВыполнитьМассовуюОперацию("Заполнить", ДополнительныеПараметры);
	
КонецПроцедуры

// Открывает форму списка пакетов фин.отчетности.
//
// Параметры:
//  ПараметрыКоманды - ПараметрыВыполненияКоманды - параметры обработчика команды.
//
Процедура ОткрытьСписокПакетовФинОтчетностьВБанки(ПараметрыКоманды) Экспорт

	ОткрытьФорму("РегистрСведений.ЖурналСтатусовФинОтчетностиВБанки.ФормаСписка", ,
		ПараметрыКоманды.Источник,
		ПараметрыКоманды.Уникальность,
		ПараметрыКоманды.Окно,
		ПараметрыКоманды.НавигационнаяСсылка);

КонецПроцедуры

// Открывает форму настройки периодической отправки пакетов отчетности в банки.
//
// Параметры:
//   Форма       - ФормаКлиентскогоПриложения - форма, из которой открывается настройка.
//   Банк        - ОпределяемыйТип.СправочникБанкиФинОтчетность - для какого банка настраивается периодичность.
//
Процедура НастроитьОтправку(Форма, Банк) Экспорт
	
	Если ЗначениеЗаполнено(Банк)
	   И Не ФинОтчетностьВБанкиКлиентСервер.ЭтоБанкУниверсальногоОбмена(Банк) Тогда
		ОткрытьФорму("ОбщаяФорма.НастройкаОтправкиОтчетностиВБанк");
	КонецЕсли;
	
КонецПроцедуры

//-- НЕ УТ

#КонецОбласти
