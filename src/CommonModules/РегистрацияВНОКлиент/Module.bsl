#Область ПрограммныйИнтерфейс

Процедура ОткрытьФормуРедактированияПредставителей(Форма, АдресВременногоХранилищаПредставителей, ОписаниеОповещенияОЗакрытии) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("АдресВременногоХранилищаПредставителей", АдресВременногоХранилищаПредставителей);
	
	ОткрытьФорму("Справочник.РегистрацииВНалоговомОргане.Форма.ФормаВыбораПредставителей", ПараметрыФормы, Форма, , , , ОписаниеОповещенияОЗакрытии);
	
КонецПроцедуры

Процедура ДобавитьПодписанта(Форма, Объект) Экспорт
	
	ЗначенияЗаполнения = РегистрацияВНОКлиентСервер.ПараметрыПодписанта();
	ЗначенияЗаполнения.НесколькоПодписантов = Истина;
	ЗначенияЗаполнения.ЭтоДобавление = Истина;
	ЗначенияЗаполнения.Владелец = Объект.Владелец;
	
	ОткрытьФормуПредставителя(Форма, ЗначенияЗаполнения);

КонецПроцедуры

Процедура УдалитьПодписанта(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	
	ТекущиеДанные = Элементы.Подписанты.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Форма.Модифицированность = Истина;
	
	СтрокаДляУдаления = Форма.Подписанты.НайтиПоИдентификатору(Элементы.Подписанты.ТекущаяСтрока);
	Форма.Подписанты.Удалить(СтрокаДляУдаления);
	
КонецПроцедуры

Процедура ОткрытьФормуПредставителя(Форма, ЗначенияЗаполнения) Экспорт
	
	ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму(
		"Справочник.РегистрацииВНалоговомОргане.Форма.ФормаПредставителя", 
		ПараметрыФормы, 
		Форма, 
		ЗначенияЗаполнения);
	
КонецПроцедуры
	
Процедура ОтчетностьПодписываетПредставительПриИзменении(Форма, Объект) Экспорт

	Если Форма.ОтчетностьПодписываетПредставитель = 2 Тогда
		Форма.ПеренестиПодписантаИзШапкиВТаблицу();
		РегистрацияВНОКлиентСервер.ОчиститьПредставителя(Объект);
	Иначе
		Форма.Подписанты.Очистить();
	КонецЕсли;
	
	Форма.ПредыдущийВидПодписанта = Форма.ОтчетностьПодписываетПредставитель;
		
	РегистрацияВНОКлиентСервер.ИзменитьОформлениеПодписантов(Форма, Объект);
	
КонецПроцедуры

Процедура ОбработкаВыбораПодписантов(Форма, Объект, ВыбранноеЗначение) Экспорт

	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
		
		Форма.Модифицированность = Истина;
		Если ВыбранноеЗначение.НесколькоПодписантов Тогда

			Отбор = Новый Структура();
			Отбор.Вставить("Пользователь", ВыбранноеЗначение.Пользователь);
			
			НайденныеСтроки = Форма.Подписанты.НайтиСтроки(Отбор);
			
			Если НайденныеСтроки.Количество() >= 1 И ВыбранноеЗначение.ЭтоДобавление Тогда
			
				ТекстОшибки = НСтр("ru = 'Пользователь %1 не может быть добавлен, поскольку уже есть в таблице подписантов.';
									|en = 'Пользователь %1 не может быть добавлен, поскольку уже есть в таблице подписантов.'");
				ТекстОшибки = СтрШаблон(ТекстОшибки, ВыбранноеЗначение.Пользователь);
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки);
				Возврат;
			КонецЕсли;
			
			Если ВыбранноеЗначение.ЭтоДобавление Тогда
				Строка = Форма.Подписанты.Добавить();
			Иначе
				Строка = Форма.Подписанты.НайтиПоИдентификатору(ВыбранноеЗначение.ВыбраннаяСтрока);
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(Строка, ВыбранноеЗначение);
			РегистрацияВНОКлиентСервер.ЗаполнитьПредставлениеПодписанта(Строка);
			
		Иначе
			ЗаполнитьЗначенияСвойств(Форма,  ВыбранноеЗначение);
			ЗаполнитьЗначенияСвойств(Объект, ВыбранноеЗначение);
			Если ВыбранноеЗначение.Свойство("Представитель") Тогда
				Форма.ОтчетностьПодписываетПредставитель = ?(ЗначениеЗаполнено(Объект.Представитель), 1, 0);
			КонецЕсли;
		КонецЕсли;
	
	КонецЕсли;
	
	РегистрацияВНОКлиентСервер.ИзменитьОформлениеПодписантов(Форма, Объект);
	
КонецПроцедуры

Процедура ПодписантыВыбор(Форма, Объект, ВыбраннаяСтрока, СтандартнаяОбработка) Экспорт
	
	Элементы = Форма.Элементы;
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.Подписанты.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗначенияЗаполнения = РегистрацияВНОКлиентСервер.ПараметрыПодписанта();
	ЗаполнитьЗначенияСвойств(ЗначенияЗаполнения, Объект);
	ЗаполнитьЗначенияСвойств(ЗначенияЗаполнения, ТекущиеДанные);
	ЗначенияЗаполнения.НесколькоПодписантов = Истина;
	ЗначенияЗаполнения.ВыбраннаяСтрока = ВыбраннаяСтрока;
	
	ОткрытьФормуПредставителя(Форма, ЗначенияЗаполнения);
	
КонецПроцедуры

Процедура ПредставлениеПредставителяНажатие(Форма, Объект, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ЗначенияЗаполнения = РегистрацияВНОКлиентСервер.ПараметрыПодписанта();
	ЗаполнитьЗначенияСвойств(ЗначенияЗаполнения, Объект);
	
	ОткрытьФормуПредставителя(Форма, ЗначенияЗаполнения);
	
КонецПроцедуры

#КонецОбласти
