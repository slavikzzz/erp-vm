#Область ПрограммныйИнтерфейс

Процедура ПриИзмененииРеквизита(ИмяЭлемента, Форма) Экспорт
	
	ОбщегоНазначенияУТКлиент.ПродолжитьИзменениеРеквизита(Форма, ИмяЭлемента, Истина);
	
КонецПроцедуры

Процедура ВыполнитьКоманду(ИмяКоманды, Форма) Экспорт
	
	//++ Локализация

	//++ НЕ УТ
	Если ИмяКоманды = "ЦелевыеПараметрыКонтракта"
		ИЛИ ИмяКоманды = "ЦелевыеПараметрыГосконтракта" Тогда
		// Состав параметров должен соответствовать Отчеты.ИсполнениеКонтрактовГОЗ.СтруктураЦелевыхПараметровКонтрактаГОЗ()
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ПлановаяДатаЗавершения");
		ПараметрыФормы.Вставить("ОбъемФинансированияКонтракта");
		ПараметрыФормы.Вставить("СуммаНДС");
		ПараметрыФормы.Вставить("ЗатратыНаМатериалы");
		ПараметрыФормы.Вставить("ЗатратыНаОплатуТруда");
		ПараметрыФормы.Вставить("ПрочиеПроизводственныеЗатраты");
		ПараметрыФормы.Вставить("ОбщепроизводственныеЗатраты");
		ПараметрыФормы.Вставить("ОбщехозяйственныеЗатраты");
		ПараметрыФормы.Вставить("КоммерческиеРасходы");
		ПараметрыФормы.Вставить("ПрибыльКонтракта");
		ПараметрыФормы.Вставить("УправленческиеРасходы");
		ПараметрыФормы.Вставить("ОбъемКредитования");
		ПараметрыФормы.Вставить("ПроцентыПоКредитам");
		ПараметрыФормы.Вставить("ГосударственныйКонтракт");
		ПараметрыФормы.Вставить("НомерКонтракта");
		ПараметрыФормы.Вставить("ДатаЗаключенияКонтракта");
		ПараметрыФормы.Вставить("НомерОтдельногоСчета");
		ПараметрыФормы.Вставить("ФактическаяДатаЗавершения");
		ПараметрыФормы.Вставить("РазделЛицевогоСчета");
		ПараметрыФормы.Вставить("СуммаВозмещаемыхРасходов");
		
		Если ИмяКоманды = "ЦелевыеПараметрыКонтракта" Тогда
			ПараметрыФормы.Вставить("ТипНаправленияДеятельности", ПредопределенноеЗначение("Перечисление.ТипыНаправленийДеятельности.КонтрактГОЗ"));
		Иначе
			ПараметрыФормы.Вставить("ТипНаправленияДеятельности", ПредопределенноеЗначение("Перечисление.ТипыНаправленийДеятельности.ГосударственныйКонтракт"));
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ПараметрыФормы, Форма.Объект);
		
		ДопПараметры = Новый Структура();
		ДопПараметры.Вставить("Форма", Форма);
		ОписаниеОповещения = Новый ОписаниеОповещения("ЦелевыеПараметрыКонтрактаОбработкаВыбора", ЭтотОбъект, ДопПараметры);
		
		ОткрытьФорму("Отчет.ИсполнениеКонтрактовГОЗ.Форма.ФормаПараметрыКонтрактаГОЗ", 
			ПараметрыФормы, Форма, , , , ОписаниеОповещения);
	КонецЕсли;
	//-- НЕ УТ

	//-- Локализация
	
	ОбщегоНазначенияУТКлиент.ПродолжитьВыполнениеКоманды(Форма, ИмяКоманды, Ложь);
	
КонецПроцедуры

#КонецОбласти

//++ Локализация

//++ НЕ УТ

#Область СлужебныеПроцедурыИФункции

Процедура ЦелевыеПараметрыКонтрактаОбработкаВыбора(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено ИЛИ Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	Форма = ДополнительныеПараметры.Форма;
	Форма.Модифицированность = Истина;
	
	ЗаполнитьЗначенияСвойств(Форма.Объект, Результат);
	
КонецПроцедуры

#КонецОбласти

//-- НЕ УТ

//-- Локализация
