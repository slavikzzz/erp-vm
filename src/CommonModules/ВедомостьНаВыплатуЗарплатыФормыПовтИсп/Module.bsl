#Область СлужебныйПрограммныйИнтерфейс

Функция ПредставленияСтатейФинансирования() Экспорт
	
	Представления = Новый Соответствие();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтатьиФинансированияЗарплата.Ссылка,
	|	СтатьиФинансированияЗарплата.Код
	|ИЗ
	|	Справочник.СтатьиФинансированияЗарплата КАК СтатьиФинансированияЗарплата";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Представления.Вставить(Выборка.Ссылка, СтроковыеФункцииКлиентСервер.ДополнитьСтроку(Выборка.Код, 3, " ", "Справа"));
	КонецЦикла;
	
	Возврат Представления
	
КонецФункции

Функция ПредставленияСтатейРасходов(ПолеПредставления = "Код") Экспорт
	
	Представления = Новый Соответствие();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтатьиРасходовЗарплата.Ссылка КАК Ссылка,
	|	&ПолеПредставления КАК Представление
	|ИЗ
	|	Справочник.СтатьиРасходовЗарплата КАК СтатьиРасходовЗарплата";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПолеПредставления", ПолеПредставления);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	МаксДлинаПредставления = 0;
	Пока Выборка.Следующий() Цикл
		МаксДлинаПредставления = МАКС(МаксДлинаПредставления, СтрДлина(СокрЛП(Выборка.Представление)));
	КонецЦикла;
	
	Выборка.Сбросить();
	Пока Выборка.Следующий() Цикл
		Представления.Вставить(
			Выборка.Ссылка, 
			СтроковыеФункцииКлиентСервер.ДополнитьСтроку(Выборка.Представление, МаксДлинаПредставления, " ", "Слева"));
	КонецЦикла;
	
	Возврат Представления
	
КонецФункции

#КонецОбласти
