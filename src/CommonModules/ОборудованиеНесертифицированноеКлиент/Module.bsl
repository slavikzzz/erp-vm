
#Область ПрограммныйИнтерфейс

// Выполняет команду драйвера
//
// Параметры:
//   ОповещениеПриЗавершении - ОписаниеОповещения - оповещение при завершении.
//   ИдентификаторКлиента    - ФормаКлиентскогоПриложения -идентификатор формы.
//   ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - идентификатор устройства, если неопределенно - будет предложен выбор.
//   ПараметрыОперации       - Структура - параметры выполнения операции.
//   ДополнительныеПараметры - Структура - дополнительные команды.
//
Процедура НачатьВыполнениеКоманды(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, ПараметрыОперации, ДополнительныеПараметры = Неопределено) Экспорт
	
	КонтекстОперации = Новый Структура();
	КонтекстОперации.Вставить("ОповещениеПриЗавершении", ОповещениеПриЗавершении);
	КонтекстОперации.Вставить("ИдентификаторКлиента"   , ИдентификаторКлиента);   
	КонтекстОперации.Вставить("ИдентификаторУстройства", ИдентификаторУстройства);
	КонтекстОперации.Вставить("ПараметрыОперации"      , ПараметрыОперации);
	КонтекстОперации.Вставить("ДополнительныеПараметры", ДополнительныеПараметры); 
	КонтекстОперации.Вставить("ПодготовитьДанные"      , ПараметрыОперации.ПодготовитьДанные); 
	КонтекстОперации.Вставить("ОбработатьДанные"       , ПараметрыОперации.ОбработатьДанные); 
	КонтекстОперации.Вставить("Команда"                , ПараметрыОперации.Команда); 
	
	Если ИдентификаторУстройства = Неопределено Или ПустаяСтрока(ИдентификаторУстройства) Тогда
		
		ПоддерживаемыеТипыВО = Новый Массив();
		ПоддерживаемыеТипыВО.Добавить("ЭлектронныеВесы");       
		
		Оповещение = Новый ОписаниеОповещения("НачатьОперациюНесертифицированноеОборудованиеЗавершение", ЭтотОбъект, КонтекстОперации);
		МенеджерОборудованияКлиент.ВыбратьУстройство(Оповещение, ПоддерживаемыеТипыВО,
			НСтр("ru = 'Выберите оборудование';
				|en = 'Выберите оборудование'"),
			НСтр("ru = 'Оборудование не подключено.';
				|en = 'Оборудование не подключено.'"), 
			НСтр("ru = 'Оборудование не выбрано.';
				|en = 'Оборудование не выбрано.'"));
	Иначе
		РезультатВыполнения = МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании();
		РезультатВыполнения.Результат = Истина;
		РезультатВыполнения.ИдентификаторУстройства = ИдентификаторУстройства;
		НачатьОперациюНесертифицированноеОборудованиеЗавершение(РезультатВыполнения, КонтекстОперации);
	КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура НачатьОперациюНесертифицированноеОборудованиеЗавершение(РезультатВыполнения, КонтекстОперации) Экспорт
	
	Если РезультатВыполнения.Результат Тогда 
		ПараметрыВыполнениеКоманды = МенеджерОборудованияКлиентСервер.ПараметрыВыполнениеКоманды(
			КонтекстОперации.Команда, 
			"ОборудованиеНесертифицированноеВызовСервера",
			КонтекстОперации.ДополнительныеПараметры, 
			КонтекстОперации.ПодготовитьДанные, 
			КонтекстОперации.ОбработатьДанные);
		МенеджерОборудованияКлиент.НачатьВыполнениеКоманды(
			КонтекстОперации.ОповещениеПриЗавершении, 
			КонтекстОперации.ИдентификаторКлиента, 
			РезультатВыполнения.ИдентификаторУстройства, 
			КонтекстОперации.ПараметрыОперации, 
			ПараметрыВыполнениеКоманды);
	Иначе
		ВыполнитьОбработкуОповещения(КонтекстОперации.ОповещениеПриЗавершении, РезультатВыполнения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти  
