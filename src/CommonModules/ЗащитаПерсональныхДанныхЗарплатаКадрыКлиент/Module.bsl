#Область СлужебныйПрограммныйИнтерфейс

// Параметры:
//  Форма - ФормаКлиентскогоПриложения -
Процедура ЛетДоУничтоженияПерсональныхДанныхСотрудниковПриИзменении(Форма) Экспорт
	
	КоличествоЛет = Форма.ЛетДоУничтоженияПерсональныхДанныхСотрудников; // Число
	ЗарплатаКадрыВызовСервера.УстановитьЗначениеКонстанты("ЛетДоУничтоженияПерсональныхДанныхСотрудников",
		КоличествоЛет);
		
	ПредставлениеЛет = СтрРазделить(ЗарплатаКадрыКлиентСервер.ПредставлениеЛет(КоличествоЛет), " ");
	Форма.Элементы.ПолеЛетДоУничтоженияПерсональныхДанныхСотрудников.Заголовок =
		СтрШаблон(НСтр("ru = '%1';
						|en = '%1'"), ?(ПредставлениеЛет.Количество() > 1, ПредставлениеЛет[1], "лет"));
	
	ОбновитьПовторноИспользуемыеЗначения();
	
	Оповестить("Запись_НаборКонстант", Новый Структура, "ЛетДоУничтоженияПерсональныхДанныхСотрудников");
	
КонецПроцедуры

// Параметры:
//  Форма - ФормаКлиентскогоПриложения - настраиваемая форма.
Процедура НастройкиУничтоженияПерсональныхДанныхПриИзменении(Форма) Экспорт
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ЛетДоУничтоженияПерсональныхДанныхСотрудниковГруппа",
		"Видимость",
		Форма.НаборКонстант.ИспользоватьСкрытиеПерсональныхДанныхСубъектов);
	
КонецПроцедуры

// Создает новую структуру данных для заполнения согласия на обработку персональных данных.
// 
// Возвращаемое значение:
//  Структура:
//   * Организация - СправочникСсылка.Организации -
//   * Субъект - ОпределяемыйТип.СубъектПерсональныхДанных, Неопределено -
//   * ДатаСогласия - Дата -
Функция НовыеДанныеДляЗаполненияСогласия() Экспорт
	
	ДанныеЗаполнения = Новый Структура;
	ДанныеЗаполнения.Вставить("Организация", ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка"));
	ДанныеЗаполнения.Вставить("Субъект", Неопределено);
	ДанныеЗаполнения.Вставить("ДатаСогласия", ОбщегоНазначенияКлиент.ДатаСеанса());
	
	Возврат ДанныеЗаполнения;
	
КонецФункции

// Открывает форму нового документа при обработке навигационной ссылки.
// 
// Параметры:
//  НавигационнаяСсылкаФорматированнойСтроки - Строка
//  ДанныеЗаполнения - Структура
Процедура ПриОбработкеНавигационнойСсылкиНовоеСогласие(НавигационнаяСсылкаФорматированнойСтроки, ДанныеЗаполнения) Экспорт
	
	ОткрытьФорму("Документ.СогласиеНаОбработкуПерсональныхДанных.ФормаОбъекта", ДанныеЗаполнения);
	
КонецПроцедуры

#КонецОбласти
