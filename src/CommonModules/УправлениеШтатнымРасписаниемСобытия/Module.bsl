////////////////////////////////////////////////////////////////////////////////
// Подсистема «Учет штатного расписания».
//
// Методы, обслуживающие подписки на события.
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Процедура заполняет настройки учета среднего заработка в зависимости 
//  от количества в информационной базе специализированных начислений.
//
Процедура УстановитьИспользованиеИндексацииШтатногоРасписания(Источник, Отказ, Замещение) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Или Источник.Количество() = 0  Тогда
		Возврат;
	КонецЕсли;

	Если ПолучитьФункциональнуюОпцию("ИспользоватьИндексациюШтатногоРасписания") <> 
		Источник[0].ИспользоватьИндексациюЗаработка И ПолучитьФункциональнуюОпцию("ИспользоватьИсториюИзмененияШтатногоРасписания") Тогда
		НаборНастройкиШтатногоРасписания = РегистрыСведений.НастройкиШтатногоРасписания.СоздатьНаборЗаписей();
		НаборНастройкиШтатногоРасписания.Прочитать();
		НаборНастройкиШтатногоРасписания[0].ИспользоватьИндексациюШтатногоРасписания = Источник[0].ИспользоватьИндексациюЗаработка И ПолучитьФункциональнуюОпцию("ИспользоватьИсториюИзмененияШтатногоРасписания");
		НаборНастройкиШтатногоРасписания.ОбменДанными.Загрузка = Истина;
		НаборНастройкиШтатногоРасписания.Записать();
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьНовоеПодразделение(Источник, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	ЗаполнитьПодразделение(Источник);
	
КонецПроцедуры

Процедура ЗаполнитьНовоеПодразделениеПриКопированииПриКопировании(Источник, ОбъектКопирования) Экспорт
	
	ЗаполнитьПодразделение(Источник);
	
КонецПроцедуры

Процедура ЗапомнитьДанныеПоУмолчаниюНовогоПодразделенияПриЗаписи(Источник, Отказ) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник.ДополнительныеСвойства.Свойство("ЗаписьНового") Тогда
		
		Если Источник.ДатаСоздания < ПодразделенияОрганизацийБЗККлиентСерверРасширенный.ДатаСозданияПоУмолчанию(ТекущаяДатаСеанса()) Тогда
			ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("СправочникПодразделенияОрганизаций", "ДатаРегистрацииДатыСоздания", ТекущаяДатаСеанса());
			ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("СправочникПодразделенияОрганизаций", "ДатаСоздания", Источник.ДатаСоздания);
		Иначе
			ОбщегоНазначения.ХранилищеОбщихНастроекУдалить("СправочникПодразделенияОрганизаций", "ДатаРегистрацииДатыСоздания", ИмяПользователя());
			ОбщегоНазначения.ХранилищеОбщихНастроекУдалить("СправочникПодразделенияОрганизаций", "ДатаСоздания", ИмяПользователя());
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПодразделение(Источник)
	
	Источник.Сформировано = Ложь;
	Источник.ДатаСоздания = '00010101';
	Источник.Расформировано = Ложь;
	Источник.ДатаРасформирования = '00010101';
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьШтатноеРасписание") Тогда
		
		Если Не ПолучитьФункциональнуюОпцию("ИспользоватьИсториюИзмененияШтатногоРасписания") Тогда
			
			Источник.Сформировано = Истина;
			ДатаРегистрации = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("СправочникПодразделенияОрганизаций", "ДатаРегистрацииДатыСоздания", '00010101');
			Если ЗарплатаКадрыКлиентСервер.ДнейВПериоде(ДатаРегистрации, ТекущаяДатаСеанса()) < 7 Тогда
				Источник.ДатаСоздания = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("СправочникПодразделенияОрганизаций", "ДатаСоздания", '00010101');
			Иначе
				Источник.ДатаСоздания = ПодразделенияОрганизацийБЗККлиентСерверРасширенный.ДатаСозданияПоУмолчанию(ТекущаяДатаСеанса());
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
