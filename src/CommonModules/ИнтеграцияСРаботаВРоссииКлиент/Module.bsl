#Область ПрограммныйИнтерфейс

// Открывает форму настроек интеграции с порталом "Работа в России".
//
// Параметры:
//   Организация - СправочникСсылка.Организации - Организация, для которой будут показаны настройки.
//
Процедура ОткрытьНастройкиОрганизации(Организация = Неопределено) Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("Организация", Организация);
	
	ОткрытьФорму("ОбщаяФорма.ИнтеграцияСПорталомРаботаВРоссии", Параметры,,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// См. УправлениеПечатьюКлиентПереопределяемый.ПечатьДокументовВыполнитьКоманду
//
Процедура ПечатьДокументовВыполнитьКоманду(Форма, Команда, ПродолжитьВыполнениеНаСервере, ДополнительныеПараметры) Экспорт
	
	Если Команда.Имя = "ПередатьПодписанныеPDFНаРаботаВРоссии" Тогда
		КадровыйЭДОКлиент.ПодписатьПоНастройкам(Форма,
			ПредопределенноеЗначение("Перечисление.ДействияСФайламиДокументовКЭДО.ПередатьНаРаботаВРоссии"));
	КонецЕсли;
	
КонецПроцедуры

Функция ТекстПредупрежденияОрганизацияНеИспользуетПРР() Экспорт
	
	Возврат НСтр("ru = 'Организация не имеет регистрации на портале ""Работа в России""';
				|en = 'Организация не имеет регистрации на портале ""Работа в России""'");
	
КонецФункции
	
Процедура ПередатьДокументы(ПечатныеФормы) Экспорт
	
	ОписанияПечатныхФорм = Новый Массив;
	ПрисоединенныеФайлы = Новый Массив;
	Для Каждого ПечатнаяФорма Из ПечатныеФормы Цикл
		
		Если ТипЗнч(ПечатнаяФорма) = Тип("Структура") Тогда
			ОписанияПечатныхФорм.Добавить(ПечатнаяФорма);
		Иначе
			ПрисоединенныеФайлы.Добавить(ПечатнаяФорма);
		КонецЕсли;
		
	КонецЦикла;
	
	Если ПрисоединенныеФайлы.Количество() > 0 Тогда
		КадровыйЭДОВызовСервера.ЗапланироватьДействияСПечатнымиФормами(ПечатныеФормы,
			ПредопределенноеЗначение("Перечисление.ДействияСФайламиДокументовКЭДО.ПередатьНаРаботаВРоссии"));
	КонецЕсли;
	
	Если ОписанияПечатныхФорм.Количество() > 0 Тогда
		ИнтеграцияСРаботаВРоссииВызовСервера.ПередатьДокументыПоОписаниямПечатныхФорм(ОписанияПечатныхФорм);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
