#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗарегистрироватьИзменениеКадровойИсторииДляПерсональныхДанныхПриЗаписи(Источник, Отказ, Замещение) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗащитаПерсональныхДанных.ИспользоватьУничтожениеПерсональныхДанныхСубъектов() Тогда
		Возврат;
	КонецЕсли;
	
	ИзменившиесяДанные = Источник.ТаблицаИзменившихсяДанныхНабора();
	Если ИзменившиесяДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник.Количество() > 0 Тогда
		ЕстьОбрабатываемоеСобытие = Ложь;
		Для Каждого ЗаписьРегистра Из Источник Цикл
			Если ЗаписьРегистра.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Прием
				Или ЗаписьРегистра.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Увольнение Тогда
				ЕстьОбрабатываемоеСобытие = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если Не ЕстьОбрабатываемоеСобытие Тогда
			Возврат;
		КонецЕсли;
		
		ДатаСобытия = Источник[0].Период;
	Иначе
		ДатаСобытия = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Субъекты = ОбщегоНазначения.ВыгрузитьКолонку(ИзменившиесяДанные, "ФизическоеЛицо", Истина);

	ЗащитаПерсональныхДанных.ДобавитьСубъектыДляРасчетаСроковХранения(Субъекты, ДатаСобытия);
	
КонецПроцедуры

Процедура ЗарегистрироватьИзменениеПериодаДействияДоговораГПХ(Источник, Отказ, Замещение) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗащитаПерсональныхДанных.ИспользоватьУничтожениеПерсональныхДанныхСубъектов() Тогда
		Возврат;
	КонецЕсли;
	
	Сотрудники = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Источник.ДополнительныеСвойства, "ОбновляемыеСотрудники");
	Если Не ЗначениеЗаполнено(Сотрудники) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	ФизическиеЛицаСотрудников = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(Сотрудники, "ФизическоеЛицо");
	УстановитьПривилегированныйРежим(Ложь);
	Субъекты = ОбщегоНазначения.ВыгрузитьКолонку(ФизическиеЛицаСотрудников, "Значение", Истина);
	
	Если Источник.Количество() > 0 Тогда
		ДатаСобытия = Источник[0].Период;
	Иначе
		ДатаСобытия = ТекущаяДатаСеанса();
	КонецЕсли;
	
	ЗащитаПерсональныхДанных.ДобавитьСубъектыДляРасчетаСроковХранения(Субъекты, ДатаСобытия);
	
КонецПроцедуры

#КонецОбласти