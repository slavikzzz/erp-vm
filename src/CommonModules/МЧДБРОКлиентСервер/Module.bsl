
#Область ПрограммныйИнтерфейс

Функция ЭтоПолномочиеДляОтчетностиПоКоду(Форма, Код, Родитель) Экспорт
	
	ПолномочияДляОтчетности = Новый Массив;

	// ЦБ. Родитель нужен потому что у ФНС тоже есть полномочия с таким кодом
	Если Форма.СоздатьМЧДЦБ И Родитель = МЧД_ЦБ() Тогда
		ПолномочияДляОтчетности.Добавить("02");
		ПолномочияДляОтчетности.Добавить("07");
		ПолномочияДляОтчетности.Добавить("08");
		ПолномочияДляОтчетности.Добавить("13");
		ПолномочияДляОтчетности.Добавить("21");
	КонецЕсли;
	
	// Росстат
	Если Форма.СоздатьМЧДРосстат Тогда
		ПолномочияДляОтчетности.Добавить("ROSSTAT_STAT_001");
	КонецЕсли;
		
	// ФСРАР
	Если Форма.СоздатьМЧДФСРАР Тогда
		ПолномочияДляОтчетности.Добавить("RAR_00000001");
	КонецЕсли;
	
	// ФТС
	Если Форма.СоздатьМЧДФТС Тогда
		ПолномочияДляОтчетности.Добавить("FTS_10001");
		ПолномочияДляОтчетности.Добавить("FTS_10002");
	КонецЕсли;
	
	// РПН
	Если Форма.СоздатьМЧДРПН Тогда
		ПолномочияДляОтчетности.Добавить("RPNDZ_REPORT");
		ПолномочияДляОтчетности.Добавить("RPNDZ_APPLICATION");
	КонецЕсли;
	
	Возврат ПолномочияДляОтчетности.Найти(Код) <> Неопределено;

КонецФункции

Функция ЭтоПолномочиеДляОтчетностиПоНаименованию(Форма, Наименование) Экспорт
	
	ПолномочияДляОтчетности = Новый Массив;
	
	Если Форма.СоздатьМЧДФНС Тогда
		ПолномочияДляОтчетности.Добавить(МЧД_ФНС());
	КонецЕсли;
	
	Если Форма.СоздатьМЧДСФР Тогда
		ПолномочияДляОтчетности.Добавить(МЧД_СФР());
	КонецЕсли;
	
	Возврат ПолномочияДляОтчетности.Найти(Наименование) <> Неопределено;

КонецФункции

Функция ЭтоПолномочиеДляОтчетности(Форма, Строка, Родитель) Экспорт
	
	Возврат ЭтоПолномочиеДляОтчетностиПоНаименованию(Форма, Строка.Наименование)
			ИЛИ ЭтоПолномочиеДляОтчетностиПоКоду(Форма, Строка.Код, Родитель);
			
КонецФункции

Функция МЧД_ФНС() Экспорт
	
	Возврат "МЧД для ФНС";
	
КонецФункции

Функция МЧД_СФР() Экспорт
	
	Возврат "МЧД для СФР (бывш. ФСС)";
	
КонецФункции

Функция МЧД_ЦБ() Экспорт
	
	Возврат "МЧД для ЦБ";
	
КонецФункции

Функция МЧД_ЕФ() Экспорт
	
	Возврат "МЧД единого формата (в т.ч. для ФСРАР, ФТС, РПН, Росстата)";
	
КонецФункции

Функция ВсеПолномочия() Экспорт
	
	Возврат НСтр("ru = 'Не ограничены';
				|en = 'Не ограничены'");
	
КонецФункции

Функция ТолькоДляОтчетности() Экспорт
	
	Возврат НСтр("ru = 'Только для отчетности';
				|en = 'Только для отчетности'");
	
КонецФункции

Функция ПроизвольныйНабор() Экспорт
	
	Возврат НСтр("ru = 'Произвольный набор';
				|en = 'Произвольный набор'");
	
КонецФункции

Процедура ВыделитьПолномочияДляОтчетностиРекурсивно(Форма, Корень, Родитель = Неопределено) Экспорт
	
	Для Каждого Строка Из ТребованияФНСКлиентСервер.ПотомкиСтроки(Корень) Цикл
		
		Строка.Пометка = Ложь;
		
		Если Строка.Наименование = МЧД_ФНС()
			ИЛИ Строка.Наименование = МЧД_СФР()
			ИЛИ Строка.Наименование = МЧД_ЕФ()
			ИЛИ Строка.Наименование = МЧД_ЦБ() Тогда
			
			Родитель = Строка.Наименование;
			
		КонецЕсли;
			
		// Если в этот орган МЧД не нужен, то у него полномочия для отчетности не выделяем
		ПропуститьУзел =
			НЕ Форма.СоздатьМЧДФНС И Строка.Наименование = МЧД_ФНС()
			ИЛИ НЕ Форма.СоздатьМЧДСФР И Строка.Наименование = МЧД_СФР()
			ИЛИ НЕ СоздатьМЧД_ЕФ(Форма) И Строка.Наименование = МЧД_ЕФ()
			ИЛИ НЕ Форма.СоздатьМЧДЦБ И Строка.Наименование = МЧД_ЦБ();
			
		Если ПропуститьУзел Тогда
			
			ТребованияФНСКлиентСервер.ПроставитьПометкиВниз(Строка);
			
		ИначеЕсли ЭтоПолномочиеДляОтчетности(Форма, Строка, Родитель) Тогда
			
			Строка.Пометка = Истина;
			ТребованияФНСКлиентСервер.ДеревоПометкаПриИзмененииПоСтроке(Строка);
			
		ИначеЕсли ТребованияФНСКлиентСервер.ПотомкиСтроки(Строка).Количество() > 0 Тогда
			
			ВыделитьПолномочияДляОтчетностиРекурсивно(Форма, Строка, Родитель);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция СоздатьМЧД_ЕФ(Форма) Экспорт
	
	Возврат Форма.СоздатьМЧДФСРАР ИЛИ Форма.СоздатьМЧДФТС ИЛИ Форма.СоздатьМЧДРосстат ИЛИ Форма.СоздатьМЧДРПН;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПредставлениеМЧД(МЧД) Экспорт
	
	ИмяТипаСправочникаМашиночитаемыеДоверенностиЦБ = "МашиночитаемыеДоверенностиЦБ";
	
	Если ТипЗнч(МЧД) = Тип("СправочникСсылка.МашиночитаемыеДоверенностиФНС") Тогда
		Возврат МЧД_ФНС();
	ИначеЕсли ТипЗнч(МЧД) = Тип("СправочникСсылка.МашиночитаемыеДоверенностиФСС") Тогда
		Возврат МЧД_СФР();
	ИначеЕсли ДокументооборотСКОКлиентСервер.ПодсистемаЦБСуществует() 
		И ТипЗнч(МЧД) = Тип("СправочникСсылка." + ИмяТипаСправочникаМашиночитаемыеДоверенностиЦБ) Тогда
		Возврат МЧД_ЦБ();
	ИначеЕсли ТипЗнч(МЧД) = Тип("ДокументСсылка.УведомлениеОПредоставленииПолномочийПредставителю") Тогда
		Возврат НСтр("ru = 'МЧД для СФР (бывш. ПФР)';
					|en = 'МЧД для СФР (бывш. ПФР)'");
	ИначеЕсли ЭтоМЧДЕФ(МЧД) Тогда
		Возврат НСтр("ru = 'МЧД для Росстата, ФСРАР, ФТС, РПН';
					|en = 'МЧД для Росстата, ФСРАР, ФТС, РПН'");
	ИначеЕсли ТипЗнч(МЧД) = Тип("ДокументСсылка.ЗаявленияПоЭлДокументооборотуСПФР") Тогда
		Возврат НСтр("ru = 'Заявление на подключение к ЭДО СФР';
					|en = 'Заявление на подключение к ЭДО СФР'");
	КонецЕсли;
	
КонецФункции

Функция ЭтоМЧДЕФ(МЧД) Экспорт
	
	Если ДокументооборотСКОКлиентСервер.ПодсистемаЦПРРМЧДСуществует() Тогда
	
		ИмяТипа = "МашиночитаемыеДоверенности";
	
		Возврат ТипЗнч(МЧД) = Тип("СправочникСсылка." + ИмяТипа);
			
	Иначе
		
		Возврат ложь;
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти
