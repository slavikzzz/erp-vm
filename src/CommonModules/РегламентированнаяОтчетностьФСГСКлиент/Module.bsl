
////////////////////////////////////////////////////////////////////////////////
// Модуль содержит общие процедуры и функции для форм статистической отчетности.
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Процедура устанавливает отметку в группе ячеек табличного документа
//
// Параметры:
//   Форма                   - Форма клиентского приложения - Форма статистического отчета.
//   Область                 - ОбластьЯчеекТабличногоДокумента - выделеннная область табличного документа.
//   НачальнаяСтрокаВГруппе  - Число - начальная строка в группе.
//   КонечнаяСтрокаВГруппе   - Число - конечная строка в группе.
//   НачальнаяКолонкаВГруппе - Число - начальная колонка в группе.
//   КонечнаяКолонкаВГруппе  - Число - конечная колонка в группе.
//   КоличествоОтметок       - Число - количество возможных отметок в группе.
//   СимволОтметки           - Строка - символ которым ставится отметка в табличном документе
//   ИмяТД                   - Строка - имя табличного документа.
//
// Пример:
//   РегламентированнаяОтчетностьФСГСКлиент.УстановитьОтметкуВГруппеЯчеек(ЭтотОбъект, Область, 1, 14, 2, 2, 3, "1", "ТабличныйДокумент");
//
Процедура УстановитьОтметкуВГруппеЯчеек(Форма, Область, НачальнаяСтрокаВГруппе, КонечнаяСтрокаВГруппе,
	НачальнаяКолонкаВГруппе, КонечнаяКолонкаВГруппе, КоличествоОтметок = 1,
	СимволОтметки = "Х", ИмяТД = "ПолеТабличногоДокументаФормаОтчета") Экспорт
	
	ТекТабличноеПоле     = Форма[ИмяТД];
	МногострочнаяОбласть = ?(СтрНайти(Область.Имя, "_") > 0, Истина, Ложь);
	
	ТекЗначениеОбласти   = Область.Значение;
	НовЗначениеОбласти   = ?(Область.Значение = "", СимволОтметки, "");
	
	МассивОтмеченныхОбластей = Новый Массив;
	Для Строка = НачальнаяСтрокаВГруппе По КонечнаяСтрокаВГруппе Цикл
		
		Для Колонка = НачальнаяКолонкаВГруппе По КонечнаяКолонкаВГруппе Цикл
			
			Если МногострочнаяОбласть Тогда
				ТекОбластьИмя = Лев(Область.Имя, 11) + Прав("00" + Колонка, 2) + "_" + Формат(Строка, "ЧГ=");
			Иначе
				ТекОбластьИмя = Лев(Область.Имя, 7) + Формат(Строка, "ЧЦ=4; ЧВН=; ЧГ=") + Прав("00" + Колонка, 2);
			КонецЕсли;
			
			ТекОбласть = ТекТабличноеПоле.Области[ТекОбластьИмя];
			
			Если ТекОбласть.Имя = Область.Имя Тогда
				Область.Значение = НовЗначениеОбласти;
			Иначе
				Если КоличествоОтметок > 1 Тогда
					Если ЗначениеЗаполнено(ТекОбласть.Значение) Тогда
						МассивОтмеченныхОбластей.Добавить(ТекОбласть.Имя);
					КонецЕсли;
				Иначе
					ТекОбласть.Значение = ?(НовЗначениеОбласти = "", "", ТекЗначениеОбласти);
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Если КоличествоОтметок = МассивОтмеченныхОбластей.Количество() Тогда
		ТекТабличноеПоле.Области[МассивОтмеченныхОбластей[0]].Значение = "";
	КонецЕсли;
	
	Форма.Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти