////////////////////////////////////////////////////////////////////////////////
// Процедуры, используемые для локализации документа "Выбытие арендованных ОС (забалансовый учет)".
// 
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

#Область ФормаДокумента

Процедура ПослеЗаписи(Форма, ПараметрыЗаписи) Экспорт
	
	//++ Локализация
	Объект = Форма.Объект;
	
	Если Объект.ДокументНаОсновании И ТипЗнч(Объект.ДокументОснование) = Тип("ДокументСсылка.ИнвентаризацияОС") Тогда
		Оповестить("ЗаписьДокументаНаОснованииИнвентаризации",, Объект.Ссылка);
	КонецЕсли;
	//-- Локализация
	
КонецПроцедуры

// Параметры:
// 	Элемент - ПолеФормы - 
// 	Форма - ФормаКлиентскогоПриложения -
Процедура ПриИзмененииРеквизита(Элемент, Форма) Экспорт

	ДополнительныеПараметры = Неопределено;
	ТребуетсяВызовСервера = Ложь;
	ПродолжитьИзменениеРеквизита = Истина;
	
	//++ Локализация
	
	Элементы = Форма.Элементы;

	Если Элемент.Имя = Элементы.ДокументНаОсновании.Имя Тогда
		
		ДокументНаОснованииПриИзменении(Форма);
		
	КонецЕсли; 
	
	//-- Локализация
	
	Если ПродолжитьИзменениеРеквизита Тогда
		
		ОбщегоНазначенияУТКлиент.ПродолжитьИзменениеРеквизита(
			Форма, 
			Элемент.Имя, 
			ТребуетсяВызовСервера,
			ДополнительныеПараметры);
			
	КонецЕсли; 
	
КонецПроцедуры

//++ Локализация

Процедура ДокументНаОснованииПриИзменении(Форма)
	
	Объект = Форма.Объект;
	
	Если Объект.ДокументНаОсновании Тогда
		ДополнительныеПараметры = Новый Структура("Объект", Объект);
		ОписаниеОповещения = Новый ОписаниеОповещения("ВыборДокументаОснования", ЭтотОбъект, ДополнительныеПараметры);
		ОткрытьФорму("Документ.ИнвентаризацияОС.ФормаВыбора",, Форма, Форма,,, ОписаниеОповещения);
	Иначе
		Объект.ДокументОснование = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыборДокументаОснования(Результат, ДополнительныеПараметры) Экспорт
	
	Объект = ДополнительныеПараметры.Объект;
	
	Если ЗначениеЗаполнено(Результат) Тогда
		Объект.ДокументОснование = Результат;
	Иначе
		Объект.ДокументНаОсновании = Ложь;
	КонецЕсли;
	
КонецПроцедуры

//-- Локализация

#КонецОбласти

#КонецОбласти
