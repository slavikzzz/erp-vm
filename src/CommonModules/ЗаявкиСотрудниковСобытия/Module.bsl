
#Область СлужебныеПроцедурыИФункции

Процедура ЗаявкаКабинетСотрудникаПриЗаписиОбработчик(Источник) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запись = РегистрыСведений.ДанныеЗаявокКабинетСотрудника.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(Запись, Источник);
	Запись.Заявка = Источник.Ссылка;
	Запись.Записать();
	
	Если ТипЗнч(Источник.Ссылка) = Тип("ДокументСсылка.ЗаявкаПрежняяВерсияКабинетСотрудника") Тогда
		ВариантФормированияФайлаОтвета = Неопределено;
		Если Источник.Задание.Метаданные().Реквизиты.Найти("ВариантФормированияФайлаОтвета") <> Неопределено Тогда
			ВариантФормированияФайлаОтвета = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник.Задание,"ВариантФормированияФайлаОтвета");
		КонецЕсли;
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Источник.Задание);
		ДокументыПоЗаявке = Новый Массив;
		МенеджерОбъекта.ЗаполнитьДокументыПоЗаявке(Источник.Задание, ДокументыПоЗаявке);
		НаборЗаписей = РегистрыСведений.ДокументыПоЗаявкамКабинетСотрудника.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Заявка.Установить(Источник.Ссылка);
		Если ЗначениеЗаполнено(ДокументыПоЗаявке) Тогда
			ДокументыПоЗаявке = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ДокументыПоЗаявке);
			Для каждого ДокументЗаявки Из ДокументыПоЗаявке Цикл
				Если Не ЗначениеЗаполнено(ДокументЗаявки) 
					Или Не Метаданные.ОпределяемыеТипы.ДокументыПоЗаявкомКабинетСотрудника.Тип.СодержитТип(ТипЗнч(ДокументЗаявки)) Тогда
					Продолжить;
				КонецЕсли;
				ЗаписьНабора = НаборЗаписей.Добавить();
				ЗаписьНабора.Заявка 			= Источник.Ссылка;
				ЗаписьНабора.ДокументПоЗаявке 	= ДокументЗаявки;
				ЗаписьНабора.ВариантФормированияФайлаОтвета = ВариантФормированияФайлаОтвета;
			КонецЦикла;
		КонецЕсли;
		НаборЗаписей.Записать();
	ИначеЕсли Источник.Метаданные().ТабличныеЧасти.Найти("ДокументыПоЗаявке") <> Неопределено Тогда
		НаборЗаписей = РегистрыСведений.ДокументыПоЗаявкамКабинетСотрудника.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Заявка.Установить(Источник.Ссылка);
		Для каждого СтрокаТЧ Из Источник.ДокументыПоЗаявке Цикл
			Если Не ЗначениеЗаполнено(СтрокаТЧ.ДокументСсылка) Тогда
				Продолжить;
			КонецЕсли;
			ЗаписьНабора = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(ЗаписьНабора, Источник);
			ЗаписьНабора.Заявка 			= Источник.Ссылка;
			ЗаписьНабора.ДокументПоЗаявке 	= СтрокаТЧ.ДокументСсылка;
		КонецЦикла;
		НаборЗаписей.Записать();
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти
