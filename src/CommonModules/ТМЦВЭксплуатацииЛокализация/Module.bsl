////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции подсистемы ТМЦ в эксплуатации.
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

//++ Локализация

//++ НЕ УТ

#Область ОбработчикиЭтаповЗакрытияМесяца

#Область ПогашениеСтоимостиТМЦВЭксплуатации

// Добавляет этап в таблицу этапов закрытия месяца.
// Элементы данной таблицы являются элементами второго уровня в дереве этапов в форме закрытия месяца.
// 
// Параметры:
// 	ТаблицаЭтапов - (См. Обработки.ОперацииЗакрытияМесяца.ЗаполнитьОписаниеЭтаповЗакрытияМесяца)
// 	ТекущийРодитель - Строка - идентификатор группы.
Процедура ДобавитьЭтап_ПогашениеСтоимостиТМЦВЭксплуатации(ТаблицаЭтапов,ТекущийРодитель) Экспорт
	
	НоваяСтрока = ЗакрытиеМесяцаСервер.ДобавитьЭтапВТаблицу(ТаблицаЭтапов, ТекущийРодитель,
		Перечисления.ОперацииЗакрытияМесяца.ПогашениеСтоимостиТМЦВЭксплуатации,,,,
		Перечисления.ОперацииЗакрытияМесяца.ПризнаниеВНалоговомУчетеАрендныхПлатежей);
		
	НоваяСтрока.ТекстВыполнить = НСтр("ru = 'Погасить';
										|en = 'Pay off'");
	
	НоваяСтрока.ДействиеИспользование = ЗакрытиеМесяцаСервер.ОписаниеДействия_СервернаяПроцедура(
		"ТМЦВЭксплуатацииЛокализация.Использование_ПогашениеСтоимостиТМЦВЭксплуатации");
		
	НоваяСтрока.ДействиеВыполнить  = ЗакрытиеМесяцаСервер.ОписаниеДействия_ВыполнитьРасчет(
		"ТМЦВЭксплуатацииЛокализация.Выполнить_ПогашениеСтоимостиТМЦВЭксплуатации");
		
	НоваяСтрока.ДействиеПодробнее = ЗакрытиеМесяцаСервер.ОписаниеДействия_ОткрытьФорму(
		Метаданные.Документы.ПогашениеСтоимостиТМЦВЭксплуатации.Формы.ФормаСписка.ПолноеИмя());
		
КонецПроцедуры

// Процедура определяет необходимость выполнения этапа.
//
// Параметры:
//  ПараметрыОбработчика - Структура - параметры обработчика события этапа.
//
Процедура Использование_ПогашениеСтоимостиТМЦВЭксплуатации(ПараметрыОбработчика) Экспорт
	
	ЗакрытиеМесяцаСервер.ПроверитьИспользованиеРегламентированногоУчета(ПараметрыОбработчика);
	
	Если ЗакрытиеМесяцаСервер.РасчетЭтапаНеТребуется(ПараметрыОбработчика.ДанныеЭтапа) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗакрытиеМесяцаСервер.ПроверитьНаличиеЗаданийКЗакрытиюМесяца(ПараметрыОбработчика,Истина,, "ЗаданияКПогашениюСтоимостиТМЦВЭксплуатации") Тогда
		Возврат;
	КонецЕсли;
	
	ЗакрытиеМесяцаСервер.ПроверитьНаличиеРегламентногоДокументаЭтапаЗакрытияМесяца(
		ПараметрыОбработчика,
		Метаданные.Документы.ПогашениеСтоимостиТМЦВЭксплуатации.Имя);
	
КонецПроцедуры

// Выполняет операцию "ПогашениеСтоимостиТМЦВЭксплуатации".
// 
// Параметры:
//  ПараметрыОбработчика - Структура - Параметры обработчика
Процедура Выполнить_ПогашениеСтоимостиТМЦВЭксплуатации(ПараметрыОбработчика) Экспорт
	
	ПараметрыРасчета = ПараметрыОбработчика.ПараметрыРасчета;
	
	НачалоРасчета = РегистрыСведений.ЗаданияКПогашениюСтоимостиТМЦВЭксплуатации.НачалоРасчета(
		ПараметрыРасчета.КонецПериода,
		ПараметрыРасчета.МассивОрганизаций);
	
	ЕстьОшибки = Ложь;
	
	Пока НачалоРасчета <= ПараметрыРасчета.КонецПериода Цикл
		
		ОкончаниеПериода = КонецМесяца(НачалоРасчета);
		
		Документы.ПогашениеСтоимостиТМЦВЭксплуатации.СоздатьПакетыПогашенияСтоимостиТМЦ(
			НачалоРасчета,
			ПараметрыРасчета.МассивОрганизаций,
			ЕстьОшибки);
		
		Если ЕстьОшибки Тогда
			Прервать;
		КонецЕсли;
		
		НомерЗадания = РегистрыСведений.ЗаданияКПогашениюСтоимостиТМЦВЭксплуатации.УвеличитьНомерЗадания();
		
		НачатьТранзакцию();
		
		Попытка
			
			РегистрыСведений.ЗаданияКПогашениюСтоимостиТМЦВЭксплуатации.ЗаблокироватьРегистрЗаданий(
				НомерЗадания,
				ПараметрыРасчета.МассивОрганизаций);
			
			ЗаданияКРасчету = РегистрыСведений.ЗаданияКПогашениюСтоимостиТМЦВЭксплуатации.ЗаданияКРасчетуЗаМесяц(
				НачалоРасчета,
				ОкончаниеПериода,
				НомерЗадания,
				ПараметрыРасчета.МассивОрганизаций);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'При выполнении расчета за период %1 произошла ошибка:
					|%2';
					|en = 'An error occurred when calculating for the %1 period:
					|%2'", ОбщегоНазначения.КодОсновногоЯзыка()),
				РасчетСебестоимостиПротоколРасчета.ПредставлениеПериодаРасчета(ПараметрыРасчета.ПериодРегистрации),
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗакрытиеМесяцаСервер.ЗафиксироватьНаличиеПроблемыПриВыполненииРасчета(
				ПараметрыОбработчика,
				ТекстОшибки,
				ПараметрыРасчета.МассивОрганизаций,
				НачалоРасчета);
			
		КонецПопытки;
		
		Если ЗаданияКРасчету.Организация.Количество() <> 0 Тогда
		
			Попытка
				
				Документы.ПогашениеСтоимостиТМЦВЭксплуатации.СоздатьДокументы(
					НачалоРасчета,
					ПараметрыРасчета.МассивОрганизаций,
					ЕстьОшибки);
					
				ЕстьОшибкиВыполненияРасчета = ЗакрытиеМесяцаСервер.ЕстьОшибкиВыполненияРасчета(
					ПараметрыОбработчика.ДанныеЭтапа.Код,
					ПараметрыРасчета.МассивОрганизаций, 
					НачалоРасчета);
													
				Если ЕстьОшибкиВыполненияРасчета Тогда
					ЕстьОшибки = Истина;
				КонецЕсли;
				
			Исключение
				
				ЕстьОшибки = Истина;
				
				ЗакрытиеМесяцаСервер.ЗафиксироватьНаличиеПроблемыПриВыполненииРасчета(
					ПараметрыОбработчика,
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()),
					,
					НачалоРасчета);
			
			КонецПопытки;
			
			Если ЕстьОшибки Тогда
				Прервать;
			КонецЕсли;
		
		КонецЕсли; 
		
		НачатьТранзакцию();
		
		Попытка
			
			РегистрыСведений.ЗаданияКПогашениюСтоимостиТМЦВЭксплуатации.ЗаблокироватьРегистрЗаданий(
				НомерЗадания,
				ПараметрыРасчета.МассивОрганизаций); 
			
			РегистрыСведений.ЗаданияКПогашениюСтоимостиТМЦВЭксплуатации.ЗафиксироватьРасчет(
				ПараметрыРасчета.МассивОрганизаций,
				НачалоРасчета,
				ЗаданияКРасчету);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'При выполнении расчета за период %1 произошла ошибка:
					|%2';
					|en = 'An error occurred when calculating for the %1 period:
					|%2'", ОбщегоНазначения.КодОсновногоЯзыка()),
				РасчетСебестоимостиПротоколРасчета.ПредставлениеПериодаРасчета(ПараметрыРасчета.ПериодРегистрации),
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗакрытиеМесяцаСервер.ЗафиксироватьНаличиеПроблемыПриВыполненииРасчета(
				ПараметрыОбработчика,
				ТекстОшибки,
				ПараметрыРасчета.МассивОрганизаций,
				НачалоРасчета);
			
		КонецПопытки;
		
		НачалоРасчета = КонецМесяца(НачалоРасчета) + 1;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ПроводкиРегУчета

// 
// Параметры:
//  ТекстыОтражения - Массив из Строка - 
Процедура ДобавитьТекстыОтраженияВРеглУчетеПередачиВЭксплуатацию(ТекстыОтражения) Экспорт
	
	#Область ПередачаВЭксплуатациюПоФСБУ // Дт 20, 2Х, 44, 91.02 :: Кт 20, 41.01, 76.ОК
	ТекстЗапроса =
	"ВЫБРАТЬ //// Списание на расходы товаров (Дт 20, 2Х, 44, 91.02 :: Кт 20, 41.01, 76.ОК)
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	ЕСТЬNULL(Стоимости.Сумма, Строки.Сумма) КАК Сумма,
	|	ЕСТЬNULL(Стоимости.СуммаУУ, Строки.СуммаУУ) КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	Строки.СтатьяРасходов КАК АналитикаУчетаДт,
	|	Операция.Подразделение КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	ЕСТЬNULL(Стоимости.КорНаправлениеДеятельности, Строки.КорНаправлениеДеятельности) КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.СтатьяРасходов КАК СубконтоДт1,
	|	ВЫБОР
	|		КОГДА Строки.СтатьяРасходов.ВидДеятельностиРасходов = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиРасходов.ПрочаяДеятельность)
	|		ТОГДА Строки.Номенклатура
	|		ИНАЧЕ Строки.АналитикаРасходов
	|	КОНЕЦ КАК СубконтоДт2,
	|	ВЫБОР
	|		КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL
	|		ТОГДА ВЫБОР
	|				КОГДА ОбъектыСтроительства.СпособСтроительства <> ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.ПустаяСсылка) 
	|				ТОГДА ОбъектыСтроительства.СпособСтроительства
	|				ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Хозспособ) 
	|			КОНЕЦ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее)
	|	КОНЕЦ КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|
	|	(ВЫБОР КОГДА Строки.СтатьяРасходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|		 И НЕ Строки.СтатьяРасходов.ПринятиеКНалоговомуУчету
	|		ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(Стоимости.СуммаНУ, Строки.СуммаНУ) 
	|	КОНЕЦ)														КАК СуммаНУДт,
	|
	|	(ВЫБОР КОГДА Строки.СтатьяРасходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|		 И НЕ Строки.СтатьяРасходов.ПринятиеКНалоговомуУчету
	|		ТОГДА ЕСТЬNULL(Стоимости.СуммаНУ, Строки.СуммаНУ) + ЕСТЬNULL(Стоимости.СуммаПР, Строки.СуммаПР)
	|		ИНАЧЕ ЕСТЬNULL(Стоимости.СуммаПР, Строки.СуммаПР)
	|	КОНЕЦ)														КАК СуммаПРДт,
	|
	|	ЕСТЬNULL(Стоимости.СуммаВР, Строки.СуммаВР)					КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ВЫБОР
	|		КОГДА Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|			И НЕ Строки.Склад ССЫЛКА Справочник.Партнеры И НЕ Строки.Склад ССЫЛКА Справочник.Организации
	|			ИЛИ Строки.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство)
	|		КОГДА Строки.Склад ССЫЛКА Справочник.Партнеры ИЛИ Строки.Склад ССЫЛКА Справочник.Организации
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТоварыКомитентовКОформлениюПоПродаже)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НаСкладе)
	|	КОНЕЦ КАК ВидСчетаКт,
	|	
	|	ВЫБОР
	|		КОГДА Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|			И НЕ Строки.Склад ССЫЛКА Справочник.Партнеры И НЕ Строки.Склад ССЫЛКА Справочник.Организации
	|			ИЛИ Строки.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|		ТОГДА Строки.ГруппаПродукции
	|		КОГДА Строки.Склад ССЫЛКА Справочник.Партнеры ИЛИ Строки.Склад ССЫЛКА Справочник.Организации
	|		ТОГДА Строки.ГруппаФинансовогоУчетаРасчетов
	|		ИНАЧЕ Строки.ГруппаФинансовогоУчета
	|	КОНЕЦ КАК АналитикаУчетаКт,
	|	ВЫБОР
	|		КОГДА Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|			И НЕ Строки.Склад ССЫЛКА Справочник.Партнеры И НЕ Строки.Склад ССЫЛКА Справочник.Организации
	|			ИЛИ Строки.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|		ТОГДА Строки.ПодразделениеАналитики
	|		КОГДА Строки.Склад ССЫЛКА Справочник.Партнеры ИЛИ Строки.Склад ССЫЛКА Справочник.Организации
	|		ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ Строки.Склад
	|	КОНЕЦ КАК МестоУчетаКт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.ПодразделениеАналитики КАК ПодразделениеКт,
	|	ВЫБОР
	|		КОГДА Строки.Склад ССЫЛКА Справочник.Партнеры ИЛИ Строки.Склад ССЫЛКА Справочник.Организации
	|		ТОГДА Строки.КорНаправлениеДеятельности
	|		ИНАЧЕ Строки.НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельностиКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|	
	|	Строки.Номенклатура КАК СубконтоКт1,
	|	ВЫБОР
	|		КОГДА Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|			И НЕ Строки.Склад ССЫЛКА Справочник.Партнеры И НЕ Строки.Склад ССЫЛКА Справочник.Организации
	|			ИЛИ Строки.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|		ТОГДА Строки.ГруппаПродукции
	|		КОГДА Строки.Склад ССЫЛКА Справочник.Партнеры ИЛИ Строки.Склад ССЫЛКА Справочник.Организации
	|		ТОГДА Строки.Контрагент
	|		ИНАЧЕ Строки.Склад
	|	КОНЕЦ КАК СубконтоКт2,
	|	Строки.ТипЗатрат КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	Строки.Количество КАК КоличествоКт,
	|
	|	ЕСТЬNULL(Стоимости.СуммаНУ, Строки.СуммаНУ) КАК СуммаНУКт,
	|	ЕСТЬNULL(Стоимости.СуммаПР, Строки.СуммаПР) КАК СуммаПРКт,
	|	ЕСТЬNULL(Стоимости.СуммаВР, Строки.СуммаВР) КАК СуммаВРКт,
	|
	|	""Списание товаров на расходы"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ВнутреннееПотребление КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВтСтроки КАК Строки
	|	ПО
	|		Операция.Ссылка = Строки.Ссылка
	|		И Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтСтоимости КАК Стоимости
	|	ПО
	|		Строки.Ссылка = Стоимости.Ссылка
	|		И Строки.Номенклатура = Стоимости.Номенклатура
	|		И Строки.Склад = Стоимости.Склад
	|		И Строки.ГруппаФинансовогоУчета = Стоимости.ГруппаФинансовогоУчета
	|		И Строки.ТипЗапасов = Стоимости.ТипЗапасов
	|		И Строки.Контрагент = Стоимости.Контрагент
	|		И Строки.СтатьяРасходов = Стоимости.СтатьяРасходов
	|		И Строки.АналитикаРасходов = Стоимости.АналитикаРасходов
	|		И Строки.РазделУчета = Стоимости.РазделУчета
	|		И Строки.ГруппаПродукции = Стоимости.ГруппаПродукции
	|		И Строки.ИдентификаторСтроки = Стоимости.ИдентификаторСтроки
	|		И Строки.НаправлениеДеятельности = Стоимости.НаправлениеДеятельности
	|		И Строки.КорНаправлениеДеятельности = Стоимости.КорНаправлениеДеятельности
	|		И Строки.ИдентификаторФинЗаписи = Стоимости.ИдентификаторФинЗаписи
	|		И Строки.НастройкаХозяйственнойОперации = Стоимости.НастройкаХозяйственнойОперации
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
	|	ПО
	|		Строки.СтатьяРасходов = СтатьиСтроительства.Ссылка
	|		И СтатьиСтроительства.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиНаПрочиеАктивы
	|	ПО
	|		Строки.СтатьяРасходов = СтатьиНаПрочиеАктивы.Ссылка
	|		И СтатьиНаПрочиеАктивы.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПрочиеАктивы)
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Справочник.ОбъектыСтроительства КАК ОбъектыСтроительства
	|	ПО 
	|		ОбъектыСтроительства.Ссылка = Строки.АналитикаРасходов
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаПереходаНаФСБУ5 КАК НастройкаПереходаНаФСБУ5
	|	ПО НастройкаПереходаНаФСБУ5.Организация = ВЫРАЗИТЬ(Операция.Организация КАК Справочник.Организации).ГоловнаяОрганизация
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаПереходаНаФСБУ5 КАК НастройкаПереходаНаФСБУ5ПоВсем
	|	ПО НастройкаПереходаНаФСБУ5ПоВсем.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|
	|ГДЕ
	|	(Строки.РазделУчета В (ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах),
	|							ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты))
	|		ИЛИ Строки.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа))
	|
	|	И Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаВЭксплуатацию)
	|
	|	И (Операция.Дата >= &ДатаНачалаДействияФСБУ5
	|		ИЛИ Операция.Дата >= ДОБАВИТЬКДАТЕ(&ДатаНачалаДействияФСБУ5, МЕСЯЦ, -1)
	|			И ГОД(Операция.Дата) = ЕСТЬNULL(НастройкаПереходаНаФСБУ5.ГодСписанияСтоимости, ЕСТЬNULL(НастройкаПереходаНаФСБУ5ПоВсем.ГодСписанияСтоимости, ГОД(&ДатаНачалаДействияФСБУ5)-1))
	|			И КОНЕЦПЕРИОДА(Операция.Дата, ГОД) = КОНЕЦПЕРИОДА(Операция.Дата, МЕСЯЦ))
	|
	|	И Строки.СтатьяРасходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|	И СтатьиНаПрочиеАктивы.Ссылка ЕСТЬ NULL
	|";
	ТекстыОтражения.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область ПередачаВЭксплуатацию // Дт 10.11.Х :: Кт 10.10, 10.09, 20
	ТекстЗапроса =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Передача в эксплуатацию товаров (Дт 10.11.Х :: Кт 10.10, 10.09, 20)
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	ЕСТЬNULL(Стоимости.Сумма, Строки.Сумма) КАК Сумма,	
	|	ЕСТЬNULL(Стоимости.СуммаУУ, Строки.СуммаУУ) КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТМЦВЭксплуатации) КАК ВидСчетаДт,
	|	ТабличнаяЧасть.КатегорияЭксплуатации КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	ЕСТЬNULL(Стоимости.КорНаправлениеДеятельности, Строки.КорНаправлениеДеятельности) КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.Номенклатура КАК СубконтоДт1,
	|	ТабличнаяЧасть.Партия КАК СубконтоДт2,
	|	ТабличнаяЧасть.ФизическоеЛицо КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	Строки.Количество КАК КоличествоДт,
	|
	|	ЕСТЬNULL(Стоимости.СуммаНУ, Строки.СуммаНУ) КАК СуммаНУДт,
	|	ЕСТЬNULL(Стоимости.СуммаПР, Строки.СуммаПР) КАК СуммаПРДт,
	|	ЕСТЬNULL(Стоимости.СуммаВР, Строки.СуммаВР) КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ВЫБОР
	|		КОГДА Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|			И НЕ Строки.Склад ССЫЛКА Справочник.Партнеры И НЕ Строки.Склад ССЫЛКА Справочник.Организации
	|			ИЛИ Строки.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство)
	|		КОГДА Строки.Склад ССЫЛКА Справочник.Партнеры ИЛИ Строки.Склад ССЫЛКА Справочник.Организации
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТоварыКомитентовКОформлениюПоПродаже)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НаСкладе)
	|	КОНЕЦ КАК ВидСчетаКт,
	|	
	|	ВЫБОР
	|		КОГДА Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|			И НЕ Строки.Склад ССЫЛКА Справочник.Партнеры И НЕ Строки.Склад ССЫЛКА Справочник.Организации
	|			ИЛИ Строки.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|		ТОГДА Строки.ГруппаПродукции
	|		КОГДА Строки.Склад ССЫЛКА Справочник.Партнеры ИЛИ Строки.Склад ССЫЛКА Справочник.Организации
	|		ТОГДА Строки.ГруппаФинансовогоУчетаРасчетов
	|		ИНАЧЕ Строки.ГруппаФинансовогоУчета
	|	КОНЕЦ КАК АналитикаУчетаКт,
	|	ВЫБОР
	|		КОГДА Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|			И НЕ Строки.Склад ССЫЛКА Справочник.Партнеры И НЕ Строки.Склад ССЫЛКА Справочник.Организации
	|			ИЛИ Строки.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|		ТОГДА Строки.ПодразделениеАналитики
	|		КОГДА Строки.Склад ССЫЛКА Справочник.Партнеры ИЛИ Строки.Склад ССЫЛКА Справочник.Организации
	|		ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ Строки.Склад
	|	КОНЕЦ КАК МестоУчетаКт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.ПодразделениеАналитики КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|	
	|	Строки.Номенклатура КАК СубконтоКт1,
	|	ВЫБОР
	|		КОГДА Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|			И НЕ Строки.Склад ССЫЛКА Справочник.Партнеры И НЕ Строки.Склад ССЫЛКА Справочник.Организации
	|			ИЛИ Строки.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|		ТОГДА Строки.ГруппаПродукции
	|		КОГДА Строки.Склад ССЫЛКА Справочник.Партнеры ИЛИ Строки.Склад ССЫЛКА Справочник.Организации
	|		ТОГДА Строки.Контрагент
	|		ИНАЧЕ Строки.Склад
	|	КОНЕЦ КАК СубконтоКт2,
	|	Строки.ТипЗатрат КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	Строки.Количество КАК КоличествоКт,
	|
	|	ЕСТЬNULL(Стоимости.СуммаНУ, Строки.СуммаНУ) КАК СуммаНУКт,
	|	ЕСТЬNULL(Стоимости.СуммаПР, Строки.СуммаПР) КАК СуммаПРКт,
	|	ЕСТЬNULL(Стоимости.СуммаВР, Строки.СуммаВР) КАК СуммаВРКт,
	|
	|	""Передача ТМЦ в эксплуатацию"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ВнутреннееПотребление КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ВнутреннееПотребление.Товары КАК ТабличнаяЧасть
	|	ПО
	|		Операция.Ссылка = ТабличнаяЧасть.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВтСтроки КАК Строки
	|	ПО
	|		ТабличнаяЧасть.Ссылка = Строки.Ссылка
	|		И Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Строки.ИдентификаторСтроки = ТабличнаяЧасть.ИдентификаторСтроки
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтСтоимости КАК Стоимости
	|	ПО
	|		Строки.Ссылка = Стоимости.Ссылка
	|		И Строки.Номенклатура = Стоимости.Номенклатура
	|		И Строки.Склад = Стоимости.Склад
	|		И Строки.ГруппаФинансовогоУчета = Стоимости.ГруппаФинансовогоУчета
	|		И Строки.ТипЗапасов = Стоимости.ТипЗапасов
	|		И Строки.Контрагент = Стоимости.Контрагент
	|		И Строки.СтатьяРасходов = Стоимости.СтатьяРасходов
	|		И Строки.АналитикаРасходов = Стоимости.АналитикаРасходов
	|		И Строки.РазделУчета = Стоимости.РазделУчета
	|		И Строки.ИдентификаторСтроки = Стоимости.ИдентификаторСтроки
	|		И Строки.ИдентификаторФинЗаписи = Стоимости.ИдентификаторФинЗаписи
	|		И Строки.НастройкаХозяйственнойОперации = Стоимости.НастройкаХозяйственнойОперации
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаПереходаНаФСБУ5 КАК НастройкаПереходаНаФСБУ5
	|	ПО НастройкаПереходаНаФСБУ5.Организация = ВЫРАЗИТЬ(Операция.Организация КАК Справочник.Организации).ГоловнаяОрганизация
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаПереходаНаФСБУ5 КАК НастройкаПереходаНаФСБУ5ПоВсем
	|	ПО НастройкаПереходаНаФСБУ5ПоВсем.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|	
	|ГДЕ
	|	(Строки.РазделУчета В (ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах), 
	|							ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты))
	|		ИЛИ Строки.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа))
	|	И Операция.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаВЭксплуатацию))
	|	И Строки.ИдентификаторСтроки <> """"
	|	И ТабличнаяЧасть.КатегорияЭксплуатации.ИмущественныйУчет
	|
	|	И (Операция.Дата < ДОБАВИТЬКДАТЕ(&ДатаНачалаДействияФСБУ5, МЕСЯЦ, -1)
	|		ИЛИ Операция.Дата < &ДатаНачалаДействияФСБУ5
	|			И (ГОД(Операция.Дата) <> ЕСТЬNULL(НастройкаПереходаНаФСБУ5.ГодСписанияСтоимости, ЕСТЬNULL(НастройкаПереходаНаФСБУ5ПоВсем.ГодСписанияСтоимости, ГОД(&ДатаНачалаДействияФСБУ5)-1))
	|				ИЛИ КОНЕЦПЕРИОДА(Операция.Дата, ГОД) <> КОНЕЦПЕРИОДА(Операция.Дата, МЕСЯЦ)))
	|";
	ТекстыОтражения.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ПередачаВЭксплуатациюПоСтаройСхеме // Дт 20, 2Х, 44, 91.02 :: Кт 20, 41.01, 76.ОК
	ТекстЗапроса =
	"ВЫБРАТЬ //// Передача в эксплуатацию (Дт 20, 2Х, 44, 91.02 :: Кт 20, 41.01, 76.ОК)
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	ЕСТЬNULL(Стоимости.Сумма, Строки.Сумма) КАК Сумма,
	|	ЕСТЬNULL(Стоимости.СуммаУУ, Строки.СуммаУУ) КАК СуммаУУ,
	|	
	|	ВЫБОР
	|		КОГДА СтатьиРасходов.Ссылка ЕСТЬ NULL
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы)
	|	КОНЕЦ КАК ВидСчетаДт,
	|	ЕСТЬNULL(СтатьиРасходов.Ссылка, Строки.ГруппаПродукции) КАК АналитикаУчетаДт,
	|	Операция.Подразделение КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	ЕСТЬNULL(Стоимости.КорНаправлениеДеятельности, Строки.КорНаправлениеДеятельности) КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	ЕСТЬNULL(СтатьиРасходов.Ссылка, Строки.Номенклатура) КАК СубконтоДт1,
	|	ВЫБОР
	|		КОГДА СтатьиРасходов.Ссылка ЕСТЬ NULL
	|		ТОГДА Строки.ГруппаПродукции
	|		КОГДА СтатьиРасходов.ВидДеятельностиРасходов = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиРасходов.ПрочаяДеятельность)
	|		ТОГДА Строки.Номенклатура
	|		ИНАЧЕ Строки.АналитикаРасходов
	|	КОНЕЦ КАК СубконтоДт2,
	|	ВЫБОР
	|		КОГДА СтатьиРасходов.Ссылка ЕСТЬ NULL
	|		ТОГДА Строки.ТипЗатрат
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее)
	|	КОНЕЦ КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СтатьиРасходов.ПринятиеКНалоговомуУчету, ИСТИНА) = ИСТИНА
	|		ТОГДА ЕСТЬNULL(Стоимости.СуммаНУ, Строки.СуммаНУ)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаНУДт,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СтатьиРасходов.ПринятиеКНалоговомуУчету, ИСТИНА) = ЛОЖЬ
	|		ТОГДА ЕСТЬNULL(Стоимости.СуммаНУ, Строки.СуммаНУ) + ЕСТЬNULL(Стоимости.СуммаПР, Строки.СуммаПР)
	|		ИНАЧЕ ЕСТЬNULL(Стоимости.СуммаПР, Строки.СуммаПР)
	|	КОНЕЦ КАК СуммаПРДт,
	|	ЕСТЬNULL(Стоимости.СуммаВР, Строки.СуммаВР) КАК СуммаВРДт,
	|
	|	ВЫБОР
	|		КОГДА Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|			И НЕ Строки.Склад ССЫЛКА Справочник.Партнеры И НЕ Строки.Склад ССЫЛКА Справочник.Организации
	|			ИЛИ Строки.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство)
	|		КОГДА Строки.Склад ССЫЛКА Справочник.Партнеры ИЛИ Строки.Склад ССЫЛКА Справочник.Организации
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТоварыКомитентовКОформлениюПоПродаже)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НаСкладе)
	|	КОНЕЦ КАК ВидСчетаКт,
	|
	|	ВЫБОР
	|		КОГДА Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|			И НЕ Строки.Склад ССЫЛКА Справочник.Партнеры И НЕ Строки.Склад ССЫЛКА Справочник.Организации
	|			ИЛИ Строки.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|		ТОГДА Строки.ГруппаПродукции
	|		КОГДА Строки.Склад ССЫЛКА Справочник.Партнеры ИЛИ Строки.Склад ССЫЛКА Справочник.Организации
	|		ТОГДА Строки.ГруппаФинансовогоУчетаРасчетов
	|		ИНАЧЕ Строки.ГруппаФинансовогоУчета
	|	КОНЕЦ КАК АналитикаУчетаКт,
	|	ВЫБОР
	|		КОГДА Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|			И НЕ Строки.Склад ССЫЛКА Справочник.Партнеры И НЕ Строки.Склад ССЫЛКА Справочник.Организации
	|			ИЛИ Строки.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|		ТОГДА Строки.ПодразделениеАналитики
	|		КОГДА Строки.Склад ССЫЛКА Справочник.Партнеры ИЛИ Строки.Склад ССЫЛКА Справочник.Организации
	|		ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ Строки.Склад
	|	КОНЕЦ КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.ПодразделениеАналитики КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|
	|	Строки.Номенклатура КАК СубконтоКт1,
	|	ВЫБОР
	|		КОГДА Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|			И НЕ Строки.Склад ССЫЛКА Справочник.Партнеры И НЕ Строки.Склад ССЫЛКА Справочник.Организации
	|			ИЛИ Строки.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|		ТОГДА Строки.ГруппаПродукции
	|		КОГДА Строки.Склад ССЫЛКА Справочник.Партнеры ИЛИ Строки.Склад ССЫЛКА Справочник.Организации
	|		ТОГДА Строки.Контрагент
	|		ИНАЧЕ Строки.Склад
	|	КОНЕЦ КАК СубконтоКт2,
	|	Строки.ТипЗатрат КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	Строки.Количество КАК КоличествоКт,
	|	ЕСТЬNULL(Стоимости.СуммаНУ, Строки.СуммаНУ) КАК СуммаНУКт,
	|	ЕСТЬNULL(Стоимости.СуммаПР, Строки.СуммаПР) КАК СуммаПРКт,
	|	ЕСТЬNULL(Стоимости.СуммаВР, Строки.СуммаВР) КАК СуммаВРКт,
	|	""Списание товаров на расходы"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ВнутреннееПотребление КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВтСтроки КАК Строки
	|	ПО
	|		Операция.Ссылка = Строки.Ссылка
	|		И Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтСтоимости КАК Стоимости
	|	ПО
	|		Строки.Ссылка = Стоимости.Ссылка
	|		И Строки.Номенклатура = Стоимости.Номенклатура
	|		И Строки.Склад = Стоимости.Склад
	|		И Строки.ПодразделениеАналитики = Стоимости.ПодразделениеАналитики
	|		И Строки.ГруппаФинансовогоУчета = Стоимости.ГруппаФинансовогоУчета
	|		И Строки.НаправлениеДеятельности = Стоимости.НаправлениеДеятельности
	|		И Строки.КорНаправлениеДеятельности = Стоимости.КорНаправлениеДеятельности
	|		И Строки.ТипЗапасов = Стоимости.ТипЗапасов
	|		И Строки.Контрагент = Стоимости.Контрагент
	|		И Строки.СтатьяРасходов = Стоимости.СтатьяРасходов
	|		И Строки.АналитикаРасходов = Стоимости.АналитикаРасходов
	|		И Строки.РазделУчета = Стоимости.РазделУчета
	|		И Строки.ГруппаПродукции = Стоимости.ГруппаПродукции
	|		И Строки.ИдентификаторФинЗаписи = Стоимости.ИдентификаторФинЗаписи
	|		И Строки.НастройкаХозяйственнойОперации = Стоимости.НастройкаХозяйственнойОперации
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|	ПО
	|		Строки.СтатьяРасходов = СтатьиРасходов.Ссылка
	|
	|ГДЕ
	|	(Строки.РазделУчета В (ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах),
	|							ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты))
	|		ИЛИ Строки.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа))
	|	И ЕСТЬNULL(СтатьиРасходов.ВариантРаспределенияРасходовРегл, НЕОПРЕДЕЛЕНО) <> ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПрочиеАктивы)
	|	И Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаВЭксплуатацию)
	|	И Строки.ИдентификаторСтроки = """"
	|";
	ТекстыОтражения.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область СписаниеНаРасходы // Дт 20, 2Х, 44, 91.02 :: Кт 10.11.Х
	ТекстЗапроса =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Списание на расходы ТМЦ, переданных в эксплуатацию (Дт 20, 2Х, 44, 91.02 :: Кт 10.11.Х)
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	ВЫБОР КОГДА ТабличнаяЧасть.КатегорияЭксплуатации.СпособПогашенияСтоимостиБУ = ЗНАЧЕНИЕ(Перечисление.СпособыПогашенияСтоимостиТМЦ.ПриПередаче)
	|		ТОГДА ЕСТЬNULL(Стоимости.Сумма, Строки.Сумма) ИНАЧЕ 0 КОНЕЦ КАК Сумма,
	|	ЕСТЬNULL(Стоимости.СуммаУУ, Строки.СуммаУУ) КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ВЫБОР
	|		КОГДА СтатьиРасходов.Ссылка ЕСТЬ NULL
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы)
	|	КОНЕЦ КАК ВидСчетаДт,
	|	ЕСТЬNULL(СтатьиРасходов.Ссылка, Строки.ГруппаПродукции) КАК АналитикаУчетаДт,
	|	Операция.Подразделение КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	ЕСТЬNULL(Стоимости.КорНаправлениеДеятельности, Строки.КорНаправлениеДеятельности) КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	ЕСТЬNULL(СтатьиРасходов.Ссылка, Строки.Номенклатура) КАК СубконтоДт1,
	|	ВЫБОР
	|		КОГДА СтатьиРасходов.Ссылка ЕСТЬ NULL
	|		ТОГДА Строки.ГруппаПродукции
	|		ИНАЧЕ Строки.АналитикаРасходов
	|	КОНЕЦ КАК СубконтоДт2,
	|	ВЫБОР
	|		КОГДА СтатьиРасходов.Ссылка ЕСТЬ NULL
	|		ТОГДА Строки.ТипЗатрат
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее)
	|	КОНЕЦ КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СтатьиРасходов.ПринятиеКНалоговомуУчету, ИСТИНА) = ИСТИНА
	|			И ТабличнаяЧасть.КатегорияЭксплуатации.СпособПогашенияСтоимостиНУ = ЗНАЧЕНИЕ(Перечисление.СпособыПогашенияСтоимостиТМЦ.ПриПередаче)
	|		ТОГДА ЕСТЬNULL(Стоимости.СуммаНУ, Строки.СуммаНУ)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаНУДт,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СтатьиРасходов.ПринятиеКНалоговомуУчету, ИСТИНА) = ЛОЖЬ
	|			И ТабличнаяЧасть.КатегорияЭксплуатации.СпособПогашенияСтоимостиНУ = ЗНАЧЕНИЕ(Перечисление.СпособыПогашенияСтоимостиТМЦ.ПриПередаче)
	|		ТОГДА ЕСТЬNULL(Стоимости.СуммаНУ, Строки.СуммаНУ)
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ТабличнаяЧасть.КатегорияЭксплуатации.СпособПогашенияСтоимостиБУ = ЗНАЧЕНИЕ(Перечисление.СпособыПогашенияСтоимостиТМЦ.ПриПередаче)
	|		ТОГДА ЕСТЬNULL(Стоимости.СуммаПР, Строки.СуммаПР)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаПРДт,
	|
	|	ВЫБОР 
	|		КОГДА ТабличнаяЧасть.КатегорияЭксплуатации.СпособПогашенияСтоимостиБУ = ЗНАЧЕНИЕ(Перечисление.СпособыПогашенияСтоимостиТМЦ.ПриПередаче)
	|		ТОГДА ЕСТЬNULL(Стоимости.СуммаВР, Строки.СуммаВР)
	|		КОГДА ТабличнаяЧасть.КатегорияЭксплуатации.СпособПогашенияСтоимостиНУ = ЗНАЧЕНИЕ(Перечисление.СпособыПогашенияСтоимостиТМЦ.ПриПередаче)
	|		ТОГДА -ЕСТЬNULL(Стоимости.СуммаНУ, Строки.СуммаНУ) 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТМЦВЭксплуатации) КАК ВидСчетаКт,
	|	
	|	ТабличнаяЧасть.КатегорияЭксплуатации КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	ЕСТЬNULL(Стоимости.КорНаправлениеДеятельности, Строки.КорНаправлениеДеятельности) КАК НаправлениеДеятельностиКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|	
	|	Строки.Номенклатура КАК СубконтоКт1,
	|	ТабличнаяЧасть.Партия КАК СубконтоКт2,
	|	ТабличнаяЧасть.ФизическоеЛицо КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|
	|	ВЫБОР
	|		КОГДА ТабличнаяЧасть.КатегорияЭксплуатации.СпособПогашенияСтоимостиБУ = ЗНАЧЕНИЕ(Перечисление.СпособыПогашенияСтоимостиТМЦ.ПриПередаче)
	|			ТОГДА Строки.Количество 
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоКт,
	|
	|	ВЫБОР
	|		КОГДА ТабличнаяЧасть.КатегорияЭксплуатации.СпособПогашенияСтоимостиНУ = ЗНАЧЕНИЕ(Перечисление.СпособыПогашенияСтоимостиТМЦ.ПриПередаче)
	|		ТОГДА ЕСТЬNULL(Стоимости.СуммаНУ, Строки.СуммаНУ) 
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаНУКт,
	|
	|	ВЫБОР
	|		КОГДА ТабличнаяЧасть.КатегорияЭксплуатации.СпособПогашенияСтоимостиБУ = ЗНАЧЕНИЕ(Перечисление.СпособыПогашенияСтоимостиТМЦ.ПриПередаче)
	|		ТОГДА ЕСТЬNULL(Стоимости.СуммаПР, Строки.СуммаПР)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаПРКт,
	|
	|	ВЫБОР 
	|		КОГДА ТабличнаяЧасть.КатегорияЭксплуатации.СпособПогашенияСтоимостиБУ = ЗНАЧЕНИЕ(Перечисление.СпособыПогашенияСтоимостиТМЦ.ПриПередаче)
	|		ТОГДА ЕСТЬNULL(Стоимости.СуммаВР, Строки.СуммаВР)
	|		КОГДА ТабличнаяЧасть.КатегорияЭксплуатации.СпособПогашенияСтоимостиНУ = ЗНАЧЕНИЕ(Перечисление.СпособыПогашенияСтоимостиТМЦ.ПриПередаче)
	|		ТОГДА -ЕСТЬNULL(Стоимости.СуммаНУ, Строки.СуммаНУ) 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СуммаВРКт,
	|
	|	""Списание на расходы ТМЦ, переданных в эксплуатацию"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ВнутреннееПотребление КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ВнутреннееПотребление.Товары КАК ТабличнаяЧасть
	|	ПО
	|		Операция.Ссылка = ТабличнаяЧасть.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВтСтроки КАК Строки
	|	ПО
	|		Строки.Ссылка = ТабличнаяЧасть.Ссылка
	|		И Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Строки.ИдентификаторСтроки = ТабличнаяЧасть.ИдентификаторСтроки
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтСтоимости КАК Стоимости
	|	ПО
	|		Строки.Ссылка = Стоимости.Ссылка
	|		И Строки.Номенклатура = Стоимости.Номенклатура
	|		И Строки.Склад = Стоимости.Склад
	|		И Строки.ГруппаФинансовогоУчета = Стоимости.ГруппаФинансовогоУчета
	|		И Строки.ТипЗапасов = Стоимости.ТипЗапасов
	|		И Строки.Контрагент = Стоимости.Контрагент
	|		И Строки.СтатьяРасходов = Стоимости.СтатьяРасходов
	|		И Строки.АналитикаРасходов = Стоимости.АналитикаРасходов
	|		И Строки.РазделУчета = Стоимости.РазделУчета
	|		И Строки.ИдентификаторСтроки = Стоимости.ИдентификаторСтроки
	|		И Строки.ИдентификаторФинЗаписи = Стоимости.ИдентификаторФинЗаписи
	|		И Строки.НастройкаХозяйственнойОперации = Стоимости.НастройкаХозяйственнойОперации
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаПереходаНаФСБУ5 КАК НастройкаПереходаНаФСБУ5
	|	ПО НастройкаПереходаНаФСБУ5.Организация = ВЫРАЗИТЬ(Операция.Организация КАК Справочник.Организации).ГоловнаяОрганизация
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаПереходаНаФСБУ5 КАК НастройкаПереходаНаФСБУ5ПоВсем
	|	ПО НастройкаПереходаНаФСБУ5ПоВсем.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|	ПО
	|		Строки.СтатьяРасходов = СтатьиРасходов.Ссылка
	|	
	|ГДЕ
	|	Операция.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаВЭксплуатацию))
	|	И Строки.РазделУчета В (ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах),
	|							ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты))
	|	И ЕСТЬNULL(СтатьиРасходов.ВариантРаспределенияРасходовРегл, НЕОПРЕДЕЛЕНО) <> ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПрочиеАктивы)
	|	И Строки.ИдентификаторСтроки <> """"
	
	|	И (Операция.Дата < ДОБАВИТЬКДАТЕ(&ДатаНачалаДействияФСБУ5, МЕСЯЦ, -1)
	|		ИЛИ Операция.Дата < &ДатаНачалаДействияФСБУ5
	|			И (ГОД(Операция.Дата) <> ЕСТЬNULL(НастройкаПереходаНаФСБУ5.ГодСписанияСтоимости, ЕСТЬNULL(НастройкаПереходаНаФСБУ5ПоВсем.ГодСписанияСтоимости, ГОД(&ДатаНачалаДействияФСБУ5)-1))
	|				ИЛИ КОНЕЦПЕРИОДА(Операция.Дата, ГОД) <> КОНЕЦПЕРИОДА(Операция.Дата, МЕСЯЦ)))
	|";
	ТекстыОтражения.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ПередачаВЭксплуатациюЗаБаланс // Дт МЦ.0Х :: Кт ---
	ТекстЗапроса =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Передача товаров в эксплуатацию, забалансовый учет (Дт МЦ.0Х :: Кт ---)
	|ВЫБРАТЬ //
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	ВЫБОР
	|		КОГДА Операция.Дата < ДОБАВИТЬКДАТЕ(&ДатаНачалаДействияФСБУ5, МЕСЯЦ, -1)
	|			ИЛИ Операция.Дата < &ДатаНачалаДействияФСБУ5
	|				И (ГОД(Операция.Дата) <> ЕСТЬNULL(НастройкаПереходаНаФСБУ5.ГодСписанияСтоимости, ЕСТЬNULL(НастройкаПереходаНаФСБУ5ПоВсем.ГодСписанияСтоимости, ГОД(&ДатаНачалаДействияФСБУ5)-1))
	|					ИЛИ КОНЕЦПЕРИОДА(Операция.Дата, ГОД) <> КОНЕЦПЕРИОДА(Операция.Дата, МЕСЯЦ)) 
	|
	|			ТОГДА ЕСТЬNULL(Стоимости.Сумма, Строки.Сумма)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Сумма,	
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТМЦВЭксплуатацииЗаБалансом) КАК ВидСчетаДт,
	|	ТабличнаяЧасть.КатегорияЭксплуатации КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.Номенклатура КАК СубконтоДт1,
	|	ТабличнаяЧасть.Партия КАК СубконтоДт2,
	|	ТабличнаяЧасть.ФизическоеЛицо КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	Строки.Количество КАК КоличествоДт,
	|
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|
	|	""Передача товаров в эксплуатацию, забалансовый учет"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ВнутреннееПотребление КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ВнутреннееПотребление.Товары КАК ТабличнаяЧасть
	|	ПО
	|		Операция.Ссылка = ТабличнаяЧасть.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВтСтроки КАК Строки
	|	ПО
	|		Строки.Ссылка = ТабличнаяЧасть.Ссылка
	|		И Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Строки.ИдентификаторСтроки = ТабличнаяЧасть.ИдентификаторСтроки
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтСтоимости КАК Стоимости
	|	ПО
	|		Строки.Ссылка = Стоимости.Ссылка
	|		И Строки.Номенклатура = Стоимости.Номенклатура
	|		И Строки.Склад = Стоимости.Склад
	|		И Строки.ГруппаФинансовогоУчета = Стоимости.ГруппаФинансовогоУчета
	|		И Строки.ТипЗапасов = Стоимости.ТипЗапасов
	|		И Строки.Контрагент = Стоимости.Контрагент
	|		И Строки.СтатьяРасходов = Стоимости.СтатьяРасходов
	|		И Строки.АналитикаРасходов = Стоимости.АналитикаРасходов
	|		И Строки.РазделУчета = Стоимости.РазделУчета
	|		И Строки.ИдентификаторСтроки = Стоимости.ИдентификаторСтроки
	|		И Строки.ИдентификаторФинЗаписи = Стоимости.ИдентификаторФинЗаписи
	|		И Строки.НастройкаХозяйственнойОперации = Стоимости.НастройкаХозяйственнойОперации
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизации
	|	ПО
	|		Операция.Ссылка = УчетнаяПолитикаОрганизации.Ссылка
	|		И Операция.Организация = УчетнаяПолитикаОрганизации.Организация
	|		И НАЧАЛОПЕРИОДА(Операция.Дата, ДЕНЬ) = УчетнаяПолитикаОрганизации.ДатаОтражения
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаПереходаНаФСБУ5 КАК НастройкаПереходаНаФСБУ5
	|	ПО НастройкаПереходаНаФСБУ5.Организация = ВЫРАЗИТЬ(Операция.Организация КАК Справочник.Организации).ГоловнаяОрганизация
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаПереходаНаФСБУ5 КАК НастройкаПереходаНаФСБУ5ПоВсем
	|	ПО НастройкаПереходаНаФСБУ5ПоВсем.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|
	|ГДЕ
	|	Операция.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаВЭксплуатацию))
	|	И Строки.РазделУчета В (ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах),
	|		ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты))
	|	И ЕСТЬNULL(УчетнаяПолитикаОрганизации.ЗабалансовыйУчетТМЦВЭксплуатации, ЛОЖЬ) = ИСТИНА
	|	И Строки.ИдентификаторСтроки <> """"
	|	И ТабличнаяЧасть.КатегорияЭксплуатации.ИмущественныйУчет
	|";
	ТекстыОтражения.Добавить(ТекстЗапроса);
	#КонецОбласти
		
КонецПроцедуры

#КонецОбласти

//-- НЕ УТ

//-- Локализация

#Область Прочее

//  См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа 
//
Процедура ПриЗаполненииСписковСОграничениемДоступа(Списки) Экспорт
	
	//++ Локализация

	//++ НЕ УТ
	Списки.Вставить(Метаданные.Документы.НаработкаТМЦВЭксплуатации, Истина);
	Списки.Вставить(Метаданные.РегистрыСведений.НастройкаПереходаНаФСБУ5, Истина);
	//-- НЕ УТ

	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДополнитьПараметрыДляПроведенияДокумента(Параметры, Документ, Свойства) Экспорт
	
	//++ Локализация
	
	// Проведение
	Если Свойства.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		//++ НЕ УТ
		Параметры.ПодчиненныеРегистры.Добавить(Метаданные.РегистрыСведений.НаработкиТМЦВЭксплуатации);
		//-- НЕ УТ
		
	КонецЕсли;
	
	//-- Локализация
	
КонецПроцедуры

// Процедура формирования движений по подчиненным регистрам взаиморасчетов.
//
// Параметры:
//   ТаблицыДляДвижений - Структура - таблицы данных документа
//   Движения - КоллекцияДвижений - коллекция наборов записей движений документа
//   Отказ - Булево - признак отказа от проведения документа.
//
Процедура ОтразитьДвижения(ТаблицыДляДвижений, Движения, Отказ) Экспорт
	
	//++ Локализация
	ПроведениеДокументов.ОтразитьДвижения(ТаблицыДляДвижений, Движения, "НаработкиТМЦВЭксплуатации");
	//-- Локализация
	
КонецПроцедуры

// См. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.
//
Процедура НастроитьВариантыОтчетов(Настройки) Экспорт

	//++ Локализация
	
	#Область ОстаткиТМЦВЭксплуатацииУТ
	ОписаниеОтчета = ВариантыОтчетов.ОписаниеОтчета(Настройки, Метаданные.Отчеты.ОстаткиТМЦВЭксплуатацииУТ);
	ОписаниеОтчета.ФункциональныеОпции.Добавить("УправлениеТорговлей");
	ОписаниеОтчета.ОпределитьНастройкиФормы = Истина;
	
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "");
	ОписаниеВарианта.Описание = НСтр("ru = 'Анализ остатков ТМЦ в эксплуатации с действующим сроком эксплуатации.';
									|en = 'Analysis of balance of inventory in operation with the valid operation life.'");
	#КонецОбласти
	
	//++ НЕ УТ
	#Область ВедомостьТМЦВЭксплуатации
	
	ОписаниеОтчета = ВариантыОтчетов.ОписаниеОтчета(Настройки, Метаданные.Отчеты.ВедомостьТМЦВЭксплуатации);
	ОписаниеОтчета.ОпределитьНастройкиФормы = Истина;
	
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "ВедомостьТМЦВЭксплуатацииРНПТ");
	ОписаниеВарианта.Описание = НСтр("ru = 'Анализ количественного движения ТМЦ в эксплуатации и стоимостная оценка остатков по РНПТ.';
									|en = 'Analyze the quantitative movement of inventory in operation and evaluate available inventory by RNPT.'");
	ОписаниеВарианта.ФункциональныеОпции.Добавить("ИспользоватьУчетПрослеживаемыхИмпортныхТоваров");
	
	#КонецОбласти
	
	#Область ОстаткиТМЦВЭксплуатации
	
	ОписаниеОтчета = ВариантыОтчетов.ОписаниеОтчета(Настройки, Метаданные.Отчеты.ОстаткиТМЦВЭксплуатации);
	ОписаниеОтчета.ОпределитьНастройкиФормы = Истина;
	
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "ОстаткиТМЦВЭксплуатацииРНПТ");
	ОписаниеВарианта.Описание = НСтр("ru = 'Анализ остатков ТМЦ в эксплуатации по РНПТ.';
									|en = 'Analyze available inventory in operation by RNPT.'");
	ОписаниеВарианта.ФункциональныеОпции.Добавить("ИспользоватьУчетПрослеживаемыхИмпортныхТоваров");
	
	#КонецОбласти
	//-- НЕ УТ
	
	//-- Локализация

КонецПроцедуры

// Определяет объекты конфигурации, в модулях менеджеров которых предусмотрена процедура ДобавитьКомандыОтчетов,
// описывающая команды открытия контекстных отчетов.
// Синтаксис процедуры ДобавитьКомандыОтчетов см. в документации.
//
// Параметры:
//   Объекты - Массив - объекты метаданных (ОбъектМетаданных) с командами отчетов.
//
Процедура ОпределитьОбъектыСКомандамиОтчетов(Объекты) Экспорт
	
	//++ Локализация

	//++ НЕ УТ
	Объекты.Добавить(Метаданные.Документы.НаработкаТМЦВЭксплуатации);
	Объекты.Добавить(Метаданные.Документы.ПогашениеСтоимостиТМЦВЭксплуатации);
	//-- НЕ УТ

	//-- Локализация
	
КонецПроцедуры

// Добавляет в профиль роли, позволяющие работать в подсистеме ВНА с правами на изменение.
// 
// Параметры:
// 	ОписаниеПрофиля - см. УправлениеДоступом.НовоеОписаниеПрофиляГруппДоступа
Процедура ДобавитьРолиДляДоступаКПодсистемеНаИзменение(ОписаниеПрофиля) Экспорт
	
	//++ Локализация
	
	//++ НЕ УТ
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеНастройкиПереходаНаФСБУ5");
	//-- НЕ УТ
	
	//-- Локализация

КонецПроцедуры

//++ НЕ УТ

Процедура ДополнитьХозяйственныеОперацииИДокументы(ХозяйственныеОперацииИДокументы) Экспорт

	//++ Локализация

	// НаработкаТМЦВЭксплуатации
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.НаработкаТМЦВЭксплуатации;
	Строка.ТипДокумента                 = Тип("ДокументСсылка.НаработкаТМЦВЭксплуатации");
	Строка.ПолноеИмяДокумента			= Метаданные.Документы.НаработкаТМЦВЭксплуатации.ПолноеИмя();
	Строка.ДокументПредставление        = НСтр("ru = 'Наработка';
												|en = 'Asset activity'");
	Строка.ГруппаКнопок                 = "НаработкаТМЦВЭксплуатации";
	Строка.Порядок                      = 20;
	Строка.ДобавитьКнопкуСоздать        = Ложь;
	
	// НаработкаТМЦВЭксплуатации
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	Строка.ХозяйственнаяОперация 		= Перечисления.ХозяйственныеОперации.ВводОстатковТМЦВЭксплуатации;
	Строка.ТипДокумента                 = Тип("ДокументСсылка.ВводОстатковТМЦВЭксплуатации");
	Строка.ПолноеИмяДокумента			= Метаданные.Документы.ВводОстатковТМЦВЭксплуатации.ПолноеИмя();
	Строка.ДокументПредставление        = НСтр("ru = 'Ввод остатков';
												|en = 'Balance entry'");
	Строка.Порядок                      = 21;
	Строка.ДобавитьКнопкуСоздать        = Ложь;

	//-- Локализация
	
КонецПроцедуры

Процедура ПриЗаписиРегистраТМЦВЭксплуатации(Регистратор, МенеджерВременныхТаблиц) Экспорт
	
	//++ Локализация
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Таблица.Организация КАК Организация,
	|	&Регистратор КАК Документ,
	|	ЕСТЬNULL(ПакетыПогашенияСтоимостиТМЦ.НомерПакета, 0) КАК НомерПакета,
	|	МИНИМУМ(ВЫБОР
	|			КОГДА ТИПЗНАЧЕНИЯ(&Регистратор) = ТИП(Документ.ВнутреннееПотребление)
	|				ТОГДА ДОБАВИТЬКДАТЕ(Таблица.Период, МЕСЯЦ, 1)
	|			КОГДА ТИПЗНАЧЕНИЯ(&Регистратор) = ТИП(Документ.ВводОстатков)
	|				ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ВводОстатков.Дата, МЕСЯЦ), МЕСЯЦ, 1)
	|			КОГДА ТИПЗНАЧЕНИЯ(&Регистратор) = ТИП(Документ.ВводОстатковТМЦВЭксплуатации)
	|				ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ВводОстатковТМЦВЭксплуатации.Дата, МЕСЯЦ), МЕСЯЦ, 1)
	|			КОГДА ТИПЗНАЧЕНИЯ(&Регистратор) = ТИП(Документ.ПрочееОприходованиеТоваров)
	|					И НАЧАЛОПЕРИОДА(ПрочееОприходованиеТоваров.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(Таблица.Партия.ДатаНачалаЭксплуатации, МЕСЯЦ)
	|				ТОГДА НАЧАЛОПЕРИОДА(ПрочееОприходованиеТоваров.Дата, МЕСЯЦ)
	|			КОГДА ТИПЗНАЧЕНИЯ(&Регистратор) = ТИП(Документ.ПеремещениеВЭксплуатации)
	|					И НАЧАЛОПЕРИОДА(ПеремещениеВЭксплуатации.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(Таблица.Партия.ДатаНачалаЭксплуатации, МЕСЯЦ)
	|				ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ПеремещениеВЭксплуатации.Дата, МЕСЯЦ), МЕСЯЦ, 1)
	|			КОГДА ТИПЗНАЧЕНИЯ(&Регистратор) = ТИП(Документ.СписаниеИзЭксплуатации)
	|					И НАЧАЛОПЕРИОДА(СписаниеИзЭксплуатации.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(Таблица.Партия.ДатаНачалаЭксплуатации, МЕСЯЦ)
	|				ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(СписаниеИзЭксплуатации.Дата, МЕСЯЦ), МЕСЯЦ, 1)
	|			ИНАЧЕ Таблица.Период
	|		КОНЕЦ) КАК Месяц
	|ИЗ
	|	ДвиженияТМЦВЭксплуатацииИзменение КАК Таблица
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВводОстатков КАК ВводОстатков
	|		ПО (ВводОстатков.Ссылка = &Регистратор)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВводОстатковТМЦВЭксплуатации КАК ВводОстатковТМЦВЭксплуатации
	|		ПО (ВводОстатковТМЦВЭксплуатации.Ссылка = &Регистратор)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПрочееОприходованиеТоваров КАК ПрочееОприходованиеТоваров
	|		ПО (ПрочееОприходованиеТоваров.Ссылка = &Регистратор)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПеремещениеВЭксплуатации КАК ПеремещениеВЭксплуатации
	|		ПО (ПеремещениеВЭксплуатации.Ссылка = &Регистратор)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СписаниеИзЭксплуатации КАК СписаниеИзЭксплуатации
	|		ПО (СписаниеИзЭксплуатации.Ссылка = &Регистратор)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПакетыПогашенияСтоимостиТМЦ КАК ПакетыПогашенияСтоимостиТМЦ
	|		ПО (ПакетыПогашенияСтоимостиТМЦ.Организация = Таблица.Организация)
	|			И (ПакетыПогашенияСтоимостиТМЦ.Партия = Таблица.Партия)
	|
	|СГРУППИРОВАТЬ ПО
	|	Таблица.Организация,
	|	ЕСТЬNULL(ПакетыПогашенияСтоимостиТМЦ.НомерПакета, 0)
	|
	|ИМЕЮЩИЕ
	|	МИНИМУМ(ВЫБОР
	|			КОГДА ТИПЗНАЧЕНИЯ(&Регистратор) = ТИП(Документ.ВнутреннееПотребление)
	|				ТОГДА ДОБАВИТЬКДАТЕ(Таблица.Период, МЕСЯЦ, 1)
	|			ИНАЧЕ НАЧАЛОПЕРИОДА(Таблица.Партия.ДатаНачалаЭксплуатации, МЕСЯЦ)
	|		КОНЕЦ) < &ДатаНачалаДействияФСБУ5";
		
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	Запрос.УстановитьПараметр("ДатаНачалаДействияФСБУ5", РеглУчетКлиентСервер.НачалоПримененияФСБУ5_2019());
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();

	РегистрыСведений.ЗаданияКПогашениюСтоимостиТМЦВЭксплуатации.СоздатьЗаписиРегистраПоДаннымВыборки(Выборка);

	//-- Локализация

КонецПроцедуры

//++ Локализация

Процедура СформироватьЗаписиРегистровЗаданий(ДокументСсылка, ДанныеТаблиц) Экспорт

	ОписаниеЗамера = ОценкаПроизводительности.НачатьЗамерДлительнойОперации("ТМЦВЭксплуатацииСервер.ФормированиеЗаданийКЗакрытиюМесяца");
	
	СписокТаблиц = Новый Массив;
	СписокТаблиц.Добавить("ПрочееОприходованиеТоваров");
	
	ТекстОбъединенияДанных = ВнеоборотныеАктивы.СформироватьТекстОбъединенияДанныхДляФормированияЗадания(СписокТаблиц, ДанныеТаблиц, "Партия");
	
	Если ТекстОбъединенияДанных = "" Тогда
		Возврат;
	КонецЕсли;
	
	СписокЗапросов = Новый Массив;
	
	#Область ИзмененныеДанные
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ИзмененныеДанные.Организация КАК Организация,
	|	ИзмененныеДанные.Партия      КАК Партия,
	|	ИзмененныеДанные.Документ    КАК Документ,
	|	ИзмененныеДанные.Период      КАК Период
	|ПОМЕСТИТЬ ИзмененныеДанные
	|ИЗ
	|	(" + ТекстОбъединенияДанных + ") КАК ИзмененныеДанные
	|
	|СГРУППИРОВАТЬ ПО
	|	ИзмененныеДанные.Организация,
	|	ИзмененныеДанные.Партия,
	|	ИзмененныеДанные.Документ,
	|	ИзмененныеДанные.Период
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Партия";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ФормированиеЗаданий
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ИзмененныеДанные.Организация,
	|	ИзмененныеДанные.Документ,
	|	ЕСТЬNULL(ПакетыПогашенияСтоимостиТМЦ.НомерПакета, 0) КАК НомерПакета,
	|	МИНИМУМ(ВЫБОР
	|			КОГДА ТИПЗНАЧЕНИЯ(ИзмененныеДанные.Документ) = ТИП(Документ.ВнутреннееПотребление)
	|				ТОГДА ДОБАВИТЬКДАТЕ(ИзмененныеДанные.Период, МЕСЯЦ, 1)
	|			КОГДА ТИПЗНАЧЕНИЯ(ИзмененныеДанные.Документ) = ТИП(Документ.ВводОстатков)
	|				ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ВводОстатков.Дата, МЕСЯЦ), МЕСЯЦ, 1)
	|			КОГДА ТИПЗНАЧЕНИЯ(ИзмененныеДанные.Документ) = ТИП(Документ.ВводОстатковТМЦВЭксплуатации)
	|				ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ВводОстатковТМЦВЭксплуатации.Дата, МЕСЯЦ), МЕСЯЦ, 1)
	|			КОГДА ТИПЗНАЧЕНИЯ(ИзмененныеДанные.Документ) = ТИП(Документ.ПрочееОприходованиеТоваров)
	|					И НАЧАЛОПЕРИОДА(ПрочееОприходованиеТоваров.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(ВЫРАЗИТЬ(ИзмененныеДанные.Партия КАК Справочник.ПартииТМЦВЭксплуатации).Дата, МЕСЯЦ)
	|				ТОГДА НАЧАЛОПЕРИОДА(ПрочееОприходованиеТоваров.Дата, МЕСЯЦ)
	|			КОГДА ТИПЗНАЧЕНИЯ(ИзмененныеДанные.Документ) = ТИП(Документ.ПеремещениеВЭксплуатации)
	|					И НАЧАЛОПЕРИОДА(ПеремещениеВЭксплуатации.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(ВЫРАЗИТЬ(ИзмененныеДанные.Партия КАК Справочник.ПартииТМЦВЭксплуатации).Дата, МЕСЯЦ)
	|				ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ПеремещениеВЭксплуатации.Дата, МЕСЯЦ), МЕСЯЦ, 1)
	|			КОГДА ТИПЗНАЧЕНИЯ(ИзмененныеДанные.Документ) = ТИП(Документ.СписаниеИзЭксплуатации)
	|					И НАЧАЛОПЕРИОДА(СписаниеИзЭксплуатации.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(ВЫРАЗИТЬ(ИзмененныеДанные.Партия КАК Справочник.ПартииТМЦВЭксплуатации).Дата, МЕСЯЦ)
	|				ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(СписаниеИзЭксплуатации.Дата, МЕСЯЦ), МЕСЯЦ, 1)
	|			ИНАЧЕ ИзмененныеДанные.Период
	|		КОНЕЦ) КАК Месяц
	|ИЗ
	|	ИзмененныеДанные КАК ИзмененныеДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВводОстатков КАК ВводОстатков
	|		ПО (ВводОстатков.Ссылка = ИзмененныеДанные.Документ)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВводОстатковТМЦВЭксплуатации КАК ВводОстатковТМЦВЭксплуатации
	|		ПО (ВводОстатковТМЦВЭксплуатации.Ссылка = ИзмененныеДанные.Документ)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПрочееОприходованиеТоваров КАК ПрочееОприходованиеТоваров
	|		ПО (ПрочееОприходованиеТоваров.Ссылка = ИзмененныеДанные.Документ)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПеремещениеВЭксплуатации КАК ПеремещениеВЭксплуатации
	|		ПО (ПеремещениеВЭксплуатации.Ссылка = ИзмененныеДанные.Документ)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СписаниеИзЭксплуатации КАК СписаниеИзЭксплуатации
	|		ПО (СписаниеИзЭксплуатации.Ссылка = ИзмененныеДанные.Документ)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПакетыПогашенияСтоимостиТМЦ КАК ПакетыПогашенияСтоимостиТМЦ
	|		ПО (ПакетыПогашенияСтоимостиТМЦ.Организация = ИзмененныеДанные.Организация)
	|			И (ПакетыПогашенияСтоимостиТМЦ.Партия = ИзмененныеДанные.Партия)
	|ГДЕ
	|	(ВЫРАЗИТЬ(ИзмененныеДанные.Партия КАК Справочник.ПартииТМЦВЭксплуатации).КатегорияЭксплуатации.СпособПогашенияСтоимостиБУ В (
	|						ЗНАЧЕНИЕ(Перечисление.СпособыПогашенияСтоимостиТМЦ.ПоСроку), 
	|						ЗНАЧЕНИЕ(Перечисление.СпособыПогашенияСтоимостиТМЦ.ПоНаработке))
	|			ИЛИ ТИПЗНАЧЕНИЯ(ИзмененныеДанные.Документ) = ТИП(Документ.ПрочееОприходованиеТоваров)
	|			ИЛИ ТИПЗНАЧЕНИЯ(ИзмененныеДанные.Документ) = ТИП(Документ.ПеремещениеВЭксплуатации)
	|			ИЛИ ТИПЗНАЧЕНИЯ(ИзмененныеДанные.Документ) = ТИП(Документ.СписаниеИзЭксплуатации))
	|
	|СГРУППИРОВАТЬ ПО
	|	ИзмененныеДанные.Организация,
	|	ИзмененныеДанные.Документ,
	|	ЕСТЬNULL(ПакетыПогашенияСтоимостиТМЦ.НомерПакета, 0)
	|
	|ИМЕЮЩИЕ МИНИМУМ(ВЫБОР
	|			КОГДА ТИПЗНАЧЕНИЯ(ИзмененныеДанные.Документ) = ТИП(Документ.ВнутреннееПотребление)
	|				ТОГДА ДОБАВИТЬКДАТЕ(ИзмененныеДанные.Период, МЕСЯЦ, 1)
	|			КОГДА ТИПЗНАЧЕНИЯ(ИзмененныеДанные.Документ) = ТИП(Документ.ВводОстатков)
	|				ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ВводОстатков.Дата, МЕСЯЦ), МЕСЯЦ, 1)
	|			КОГДА ТИПЗНАЧЕНИЯ(ИзмененныеДанные.Документ) = ТИП(Документ.ВводОстатковТМЦВЭксплуатации)
	|				ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ВводОстатковТМЦВЭксплуатации.Дата, МЕСЯЦ), МЕСЯЦ, 1)
	|			КОГДА ТИПЗНАЧЕНИЯ(ИзмененныеДанные.Документ) = ТИП(Документ.ПрочееОприходованиеТоваров)
	|					И НАЧАЛОПЕРИОДА(ПрочееОприходованиеТоваров.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(ВЫРАЗИТЬ(ИзмененныеДанные.Партия КАК Справочник.ПартииТМЦВЭксплуатации).Дата, МЕСЯЦ)
	|				ТОГДА НАЧАЛОПЕРИОДА(ПрочееОприходованиеТоваров.Дата, МЕСЯЦ)
	|			КОГДА ТИПЗНАЧЕНИЯ(ИзмененныеДанные.Документ) = ТИП(Документ.ПеремещениеВЭксплуатации)
	|					И НАЧАЛОПЕРИОДА(ПеремещениеВЭксплуатации.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(ВЫРАЗИТЬ(ИзмененныеДанные.Партия КАК Справочник.ПартииТМЦВЭксплуатации).Дата, МЕСЯЦ)
	|				ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ПеремещениеВЭксплуатации.Дата, МЕСЯЦ), МЕСЯЦ, 1)
	|			КОГДА ТИПЗНАЧЕНИЯ(ИзмененныеДанные.Документ) = ТИП(Документ.СписаниеИзЭксплуатации)
	|					И НАЧАЛОПЕРИОДА(СписаниеИзЭксплуатации.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(ВЫРАЗИТЬ(ИзмененныеДанные.Партия КАК Справочник.ПартииТМЦВЭксплуатации).Дата, МЕСЯЦ)
	|				ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(СписаниеИзЭксплуатации.Дата, МЕСЯЦ), МЕСЯЦ, 1)
	|			ИНАЧЕ ИзмененныеДанные.Период
	|		КОНЕЦ) < &ДатаНачалаДействияФСБУ5";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ИспользуемыеВременныеТаблицы = ОбщегоНазначенияУТ.СписокВременныхТаблиц(ДанныеТаблиц.МенеджерВременныхТаблиц);
	
	ТекстЗапроса = СтрСоединить(СписокЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДанныеТаблиц.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ДатаНачалаДействияФСБУ5", РеглУчетКлиентСервер.НачалоПримененияФСБУ5_2019());
	РезультатЗапроса = Запрос.Выполнить();
	
	ОбщегоНазначенияУТ.УничтожитьВременныеТаблицы(ДанныеТаблиц.МенеджерВременныхТаблиц,, ИспользуемыеВременныеТаблицы);
	
	РегистрыСведений.ЗаданияКПогашениюСтоимостиТМЦВЭксплуатации.СоздатьЗаписиРегистраПоДаннымВыборки(РезультатЗапроса.Выбрать());
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(ОписаниеЗамера, 1);
	
КонецПроцедуры

//-- Локализация

//-- НЕ УТ

#КонецОбласти
