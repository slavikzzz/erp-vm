////////////////////////////////////////////////////////////////////////////////
// Клиентские процедуры и функции (локализация)
//
////////////////////////////////////////////////////////////////////////////////

Процедура ОбработкаНавигационнойСсылкиФормы(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка, Форма) Экспорт
	//++ Локализация
	СтандартнаяОбработка = Ложь;
	Если НавигационнаяСсылкаФорматированнойСтроки = "ДанныеДляБазыРаспределения" Тогда
		ДанныеДляБазыРаспределения(Форма);
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "СписатьЗатраты" Тогда
		СписатьЗатраты(Форма);
	КонецЕсли;
	//-- Локализация
	Возврат;
КонецПроцедуры

Функция РасшифроватьПоле(Форма, ИмяПоля, СтандартнаяОбработка) Экспорт
	//++ Локализация
	Если ИмяПоля = "ЗатратыПоступило" Тогда
		
		СтандартнаяОбработка = Ложь;
		РасшифровкаЗатратыПоступило(Форма);
		
	ИначеЕсли ИмяПоля = "ЗатратыПередано" Тогда
		
		СтандартнаяОбработка = Ложь;
		РасшифровкаЗатратыПоступило(Форма);
		
	ИначеЕсли ИмяПоля = "ЗатратыНорматив" Тогда
		
		СтандартнаяОбработка = Ложь;
		РасшифровкаРасходовНаПроизводство(Форма);
		
	ИначеЕсли ИмяПоля = "ЗатратыОтклонение" Тогда
		
		СтандартнаяОбработка = Ложь;
		РасшифровкаРасходовНаПроизводство(Форма);
		
	Иначе
		
		Возврат Ложь;
		
	КонецЕсли; 
	//-- Локализация
	Возврат Истина;
	
КонецФункции

//++ Локализация
Процедура ДанныеДляБазыРаспределения(Форма)
	Объект = Форма.Объект;
	
	ПараметрыФормы = Новый Структура("Отбор, ФиксированныеНастройки, КлючНазначенияИспользования, КлючВарианта, СформироватьПриОткрытии, ВидимостьКомандВариантовОтчетов");
	ПараметрыФормы.СформироватьПриОткрытии = Истина;
	ПараметрыФормы.ВидимостьКомандВариантовОтчетов = Ложь;
	ПараметрыФормы.Отбор = Новый Структура;
	
	ПараметрыФормы.Отбор.Вставить("Организация", Объект.Организация);
	ПараметрыФормы.Отбор.Вставить("Подразделение", Объект.Подразделение);
	ДатаДокумента = Объект.Дата;
	ПараметрыФормы.Отбор.Вставить("НачалоПериода", (НачалоМесяца(ДатаДокумента)));
	ПараметрыФормы.Отбор.Вставить("ОкончаниеПериода", (КонецМесяца(ДатаДокумента)));
	ПараметрыФормы.Отбор.Вставить("ГруппыПродукции", Форма.ОтборПоГруппамПродукции.ВыгрузитьЗначения());
	ПараметрыФормы.Отбор.Вставить("ПоВсемГруппамПродукции", Форма.ОтборПоГруппамПродукции.Количество() = 0
																			ИЛИ НЕ Форма.ИспользоватьОтборПоГруппамПродукции);
	ПараметрыФормы.Отбор.Вставить("ВидыРабот", Форма.ОтборПоВидамРабот.ВыгрузитьЗначения());
	ПоВсемВидамРабот = Форма.ОтборПоВидамРабот.Количество() = 0
		ИЛИ Объект.БазаРаспределения <> ПредопределенноеЗначение("Перечисление.ТипыБазыРаспределенияРасходов.КоличествоРаботУказанныхВидов");
	ПараметрыФормы.Отбор.Вставить("ПоВсемВидамРабот", ПоВсемВидамРабот);
	ПараметрыФормы.Отбор.Вставить("Материалы", Форма.ОтборПоМатериалам.ВыгрузитьЗначения());
	ПоВсемМатериалам = Форма.ОтборПоМатериалам.Количество() = 0
		ИЛИ НЕ (Объект.БазаРаспределения = ПредопределенноеЗначение("Перечисление.ТипыБазыРаспределенияРасходов.КоличествоУказанныхМатериалов")
				ИЛИ Объект.БазаРаспределения = ПредопределенноеЗначение("Перечисление.ТипыБазыРаспределенияРасходов.ВесУказанныхМатериалов")
				ИЛИ Объект.БазаРаспределения = ПредопределенноеЗначение("Перечисление.ТипыБазыРаспределенияРасходов.ОбъемУказанныхМатериалов"));
	ПараметрыФормы.Отбор.Вставить("ПоВсемМатериалам", ПоВсемМатериалам);
	Если Объект.БазаРаспределения = ПредопределенноеЗначение("Перечисление.ТипыБазыРаспределенияРасходов.НормативРасходаМатериала") Тогда
		ПараметрыФормы.Отбор.Вставить("Номенклатура", Объект.Номенклатура);
		ПараметрыФормы.Отбор.Вставить("Характеристика", Объект.Характеристика);
	КонецЕсли;
	ПараметрыФормы.Отбор.Вставить("Назначение", Объект.Назначение);
	ПараметрыФормы.Отбор.Вставить("ПоВсемНазначениям", НЕ ЗначениеЗаполнено(Объект.Назначение));
	ПараметрыФормы.Отбор.Вставить("ВидПлановыхЦен", Форма.ВидЦенПлановойСтоимости);
	ПараметрыФормы.Отбор.Вставить("БазаРаспределения", Объект.БазаРаспределения);
	ПараметрыФормы.Отбор.Вставить("ПартионныйУчетВключен", Форма.ПартионныйУчетВключен);
	ПараметрыФормы.Отбор.Вставить("АналитическийУчетПоГруппамПродукции", Форма.АналитическийУчетПоГруппамПродукции);
	ПараметрыФормы.Отбор.Вставить("НеВедетсяУчетСебестоимостиПоВидамЗапасов", НЕ Форма.ВедетсяУчетСебестоимостиПоВидамЗапасов);
	ПараметрыФормы.Отбор.Вставить("ФормироватьВыпускРаботСписанныхНаЗатраты", Ложь);
		
	Если Объект.БазаРаспределения = ПредопределенноеЗначение("Перечисление.ТипыБазыРаспределенияРасходов.КоличествоУказанныхМатериалов")
		ИЛИ Объект.БазаРаспределения = ПредопределенноеЗначение("Перечисление.ТипыБазыРаспределенияРасходов.ВесУказанныхМатериалов")
		ИЛИ Объект.БазаРаспределения = ПредопределенноеЗначение("Перечисление.ТипыБазыРаспределенияРасходов.ОбъемУказанныхМатериалов") Тогда
		КлючВарианта = "ПоУказаннымМатериалам";
	ИначеЕсли Объект.БазаРаспределения = ПредопределенноеЗначение("Перечисление.ТипыБазыРаспределенияРасходов.КоличествоПродукции")
		ИЛИ Объект.БазаРаспределения = ПредопределенноеЗначение("Перечисление.ТипыБазыРаспределенияРасходов.ОбъемПродукции")
		ИЛИ Объект.БазаРаспределения = ПредопределенноеЗначение("Перечисление.ТипыБазыРаспределенияРасходов.ВесПродукции") Тогда
		КлючВарианта = "ПоПродукции";
	ИначеЕсли Объект.БазаРаспределения = ПредопределенноеЗначение("Перечисление.ТипыБазыРаспределенияРасходов.НормативРасходаМатериала") Тогда
		КлючВарианта = "ПоНормативам";
	ИначеЕсли Объект.БазаРаспределения = ПредопределенноеЗначение("Перечисление.ТипыБазыРаспределенияРасходов.КоличествоРаботУказанныхВидов") Тогда
		КлючВарианта = "ПоУказаннымТрудозатратам";
	ИначеЕсли Объект.БазаРаспределения = ПредопределенноеЗначение("Перечисление.ТипыБазыРаспределенияРасходов.ПлановаяСтоимостьПродукции") Тогда
		КлючВарианта = "ПоПлановойСтоимости";
	Иначе
		Возврат;
	КонецЕсли;
	
	Если Форма.ЭтоКА Тогда
		КлючВарианта = КлючВарианта + "КА";
	КонецЕсли;
	
	ПараметрыФормы.КлючВарианта = КлючВарианта;
	ПараметрыФормы.Отбор.Вставить("КлючВарианта", КлючВарианта);
		
	ОткрытьФорму("Отчет.ДанныеДляБазыРаспределенияРасходов.ФормаОбъекта", ПараметрыФормы, Форма);
	
КонецПроцедуры

Процедура СписатьЗатраты(Форма)
	ОткрытьФорму("Документ.СписаниеЗатратНаВыпуск.Форма.ФормаСпискаДокументов", ,Форма);
КонецПроцедуры

Процедура РасшифровкаЗатратыПоступило(Форма)

	ТекущиеДанные = Форма.Элементы.Затраты.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормыРасшифровки = Новый Структура;
	ПараметрыФормыРасшифровки.Вставить("Организация", ТекущиеДанные.Организация);
	ПараметрыФормыРасшифровки.Вставить("Подразделение", ТекущиеДанные.Подразделение);
	ПараметрыФормыРасшифровки.Вставить("Номенклатура", ТекущиеДанные.Номенклатура);
	ПараметрыФормыРасшифровки.Вставить("Характеристика", ТекущиеДанные.Характеристика);
	Если ЗначениеЗаполнено(ТекущиеДанные.Серия) Тогда
		ПараметрыФормыРасшифровки.Вставить("Серия", ТекущиеДанные.Серия);
	КонецЕсли;
	ПараметрыФормыРасшифровки.Вставить("Назначение", ТекущиеДанные.Назначение);
	ПараметрыФормыРасшифровки.Вставить("Период", Новый СтандартныйПериод(НачалоМесяца(Форма.Период), КонецМесяца(Форма.Период)));
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимРасшифровки", Истина);
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	ПараметрыФормы.Вставить("Отбор", ПараметрыФормыРасшифровки);
	
	ОткрытьФорму("Отчет.РасшифровкаПоступлениеМатериаловВПроизводство.ФормаОбъекта", ПараметрыФормы);

КонецПроцедуры

Процедура РасшифровкаРасходовНаПроизводство(Форма)

	ТекущиеДанные = Форма.Элементы.Затраты.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ПараметрыФормыРасшифровки = Новый Структура;
	ПараметрыФормыРасшифровки.Вставить("Организация", ТекущиеДанные.Организация);
	ПараметрыФормыРасшифровки.Вставить("Подразделение", ТекущиеДанные.Подразделение);
	ПараметрыФормыРасшифровки.Вставить("Номенклатура", ТекущиеДанные.Номенклатура);
	ПараметрыФормыРасшифровки.Вставить("Характеристика", ТекущиеДанные.Характеристика);
	Если ЗначениеЗаполнено(ТекущиеДанные.Серия) Тогда
		ПараметрыФормыРасшифровки.Вставить("Серия", ТекущиеДанные.Серия);
	КонецЕсли;
	ПараметрыФормыРасшифровки.Вставить("Назначение", ТекущиеДанные.Назначение);
	ПараметрыФормыРасшифровки.Вставить("Период", Новый СтандартныйПериод(НачалоМесяца(Форма.Период), КонецМесяца(Форма.Период)));
	
	НаименованиеВариантаРасшифровки = "РасшифровкаРасходовНаПроизводство1КА";
	//++ НЕ УТКА
	НаименованиеВариантаРасшифровки = "РасшифровкаРасходовНаПроизводство1УП";
	//-- НЕ УТКА
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимРасшифровки", Истина);
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	ПараметрыФормы.Вставить("Отбор", ПараметрыФормыРасшифровки);
	ПараметрыФормы.Вставить("КлючПользовательскихНастроек", НаименованиеВариантаРасшифровки);
	ПараметрыФормы.Вставить("КлючНазначенияИспользования",  ТекущиеДанные.Номенклатура);
	ПараметрыФормы.Вставить("КлючВарианта",                 НаименованиеВариантаРасшифровки);
	
	ОткрытьФорму("Отчет.РасшифровкаРасходовНаПроизводство.ФормаОбъекта", ПараметрыФормы);

КонецПроцедуры

//-- Локализация