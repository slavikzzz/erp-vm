////////////////////////////////////////////////////////////////////////////////
// Процедуры подсистемы "ТМЦ в эксплуатации".
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Параметры подбора ТМЦ в эксплуатации.
// 
// Параметры:
//  Дата - Дата - На какую дату подбираются ТМЦ.
//  Организация - СправочникСсылка.Организации - Отбор ТМЦ по организации.
//  Подразделение - СправочникСсылка.СтруктураПредприятия - Отбор ТМЦ по подразделению.
// 
// Возвращаемое значение:
//  Структура - содержит:
//		* Дата - Дата, Неопределено - На какую дату подбираются ТМЦ.
//		* Организация - СправочникСсылка.Организации, Неопределено - Отбор ТМЦ по организации.
//		* Подразделение - СправочникСсылка.СтруктураПредприятия, Неопределено - Отбор ТМЦ по подразделению.
//		* Номенклатура - СправочникСсылка.Номенклатура, Неопределено - Отбор ТМЦ по номенклатуре.
//		* Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры, Неопределено - Отбор ТМЦ по характеристике.
//		* Серия - СправочникСсылка.СерииНоменклатуры, Неопределено - Отбор ТМЦ по серии.
//		* СтатусУказанияСерий - Число, Неопределено - Для отбора ТМЦ по серии.
//		* ФизическоеЛицо - СправочникСсылка.ФизическиеЛица, Неопределено - Отбор ТМЦ по физ. лицу.
//		* СпособПогашенияСтоимостиБУ - ПеречислениеСсылка.СпособыПогашенияСтоимостиТМЦ, Неопределено - Отбор способу погашения стоимости.
//		* ИнвентарныйНомер - Строка, Неопределено - Отбор ТМЦ по инвентарному номеру.
//		* ИнвентарныйНомерСодержит - Строка, Неопределено - Отбор ТМЦ по совпадению текста в инвентарном номере.
//		* ВыбратьПервые - Число - Выбрать первые записи.
//		* ИнвентарныйУчет - Число, Неопределено - Отбор ТМЦ по признаку инвентарного учета (0 - Любой, 1 - Не ведется, 2 - Ведется).
//		* НазначенныйРесурс - Строка - (Выработан, НеВыработан).
//		* ТекущийРегистратор - ДокументСсылка, Неопределено - Заполняется, если нужно учесть изменение остатков документом.
//		* ДатаНачалаЭксплуатацииС - Дата - Отбор по дате начала эксплуатации.
//		* ДатаНачалаЭксплуатацииПо - Дата - Отбор по дате начала эксплуатации.
//		* ДляДинамическогоСписка - Булево - Истина, если подбор выполняется в дин. списке.
//	
Функция ПараметрыПодбораТМЦВЭксплуатации(Дата = Неопределено, Организация = Неопределено, Подразделение = Неопределено) Экспорт
	
	ПараметрыПодбора = Новый Структура;
	ПараметрыПодбора.Вставить("Дата", Дата);
	ПараметрыПодбора.Вставить("Организация", Организация);
	ПараметрыПодбора.Вставить("Подразделение", Подразделение);
	ПараметрыПодбора.Вставить("Номенклатура", Неопределено);
	ПараметрыПодбора.Вставить("Характеристика", Неопределено);
	ПараметрыПодбора.Вставить("Серия", Неопределено);
	ПараметрыПодбора.Вставить("СтатусУказанияСерий", Неопределено);
	ПараметрыПодбора.Вставить("СтатусУказанияСерийОтправитель", Неопределено);
	ПараметрыПодбора.Вставить("ФизическоеЛицо", Неопределено);
	ПараметрыПодбора.Вставить("СпособПогашенияСтоимостиБУ", Неопределено);
	ПараметрыПодбора.Вставить("ТекущийРегистратор", Неопределено);
	ПараметрыПодбора.Вставить("ИнвентарныйНомер", Неопределено);
	ПараметрыПодбора.Вставить("ИнвентарныйНомерСодержит", Неопределено);
	ПараметрыПодбора.Вставить("ВыбратьПервые", 0);	
	ПараметрыПодбора.Вставить("НазначенныйРесурс", Неопределено);	
	ПараметрыПодбора.Вставить("ИнвентарныйУчет", 0);
	ПараметрыПодбора.Вставить("ДатаНачалаЭксплуатацииС", '000101010000');	
	ПараметрыПодбора.Вставить("ДатаНачалаЭксплуатацииПо", '000101010000');	
	ПараметрыПодбора.Вставить("ДляДинамическогоСписка", Ложь);	
	
	Возврат ПараметрыПодбора;
	
КонецФункции

// Параметры заполнения признаков категории эксплуатации.
// 
// Возвращаемое значение:
//  Структура - Параметры заполнения признаков категории эксплуатации:
//		* ЗаполнитьСтатьюРасходов - Булево -
//		* ЗаполнитьСтатьюРасходовДляГрупповогоОС - Булево -
//		* ЗаполнитьСрокЭксплуатации - Булево -
Функция ПараметрыЗаполненияПризнаковКатегорииЭксплуатации() Экспорт

	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("ЗаполнитьСтатьюРасходов", Истина);
	ПараметрыЗаполнения.Вставить("ЗаполнитьСтатьюРасходовДляГрупповогоОС", Истина);
	ПараметрыЗаполнения.Вставить("ЗаполнитьСрокЭксплуатации", Ложь);
	
	Возврат ПараметрыЗаполнения;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ВводОстатковТМЦВЭксплуатации

Функция ЗначенияСвойствЗависимыхРеквизитов_ВводОстатковТМЦВЭксплуатации(Объект, ДопПараметры, ИзмененныеРеквизиты = "") Экспорт
	
	СтруктураИзмененныхРеквизитов = Новый Структура(ИзмененныеРеквизиты);
	ОбновитьВсе = СтруктураИзмененныхРеквизитов.Количество() = 0;
	
	ПараметрыРеквизитовОбъекта = Новый Массив;

	Если СтруктураИзмененныхРеквизитов.Свойство("Дата")
		ИЛИ ОбновитьВсе Тогда
			
		ЗначениеСвойства = Ложь;
		//++ НЕ УТ
		ЗначениеСвойства = 
			Объект.Дата <> '000101010000'
			И НачалоМесяца(Объект.Дата) < НачалоМесяца(РеглУчетКлиентСервер.НачалоПримененияФСБУ5_2019());
		//-- НЕ УТ
	
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"ТМЦВЭксплуатации.ПервоначальнаяСумма", 
			"ТМЦВЭксплуатацииПервоначальнаяСумма", 
			"АвтоОтметкаНезаполненного", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
		
	КонецЕсли;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("Дата")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("Организация")
		ИЛИ ОбновитьВсе Тогда
		
		ЗначениеСвойства = НЕ ДопПараметры.СтоимостьСписана;
		
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"ТМЦВЭксплуатации.СтатьяРасходов", 
			"ТМЦВЭксплуатацииСтатьяРасходов", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"ТМЦВЭксплуатации.АналитикаРасходов", 
			"ТМЦВЭксплуатацииАналитикаРасходов", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"ТМЦВЭксплуатации.Сумма", 
			"ТМЦВЭксплуатацииСумма", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
		
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"ТМЦВЭксплуатации.СуммаВР", 
			"ТМЦВЭксплуатацииСуммаВР", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
		
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"ТМЦВЭксплуатации.СуммаПР", 
			"ТМЦВЭксплуатацииСуммаПР", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
		
	КонецЕсли;
	
	Возврат ПараметрыРеквизитовОбъекта;
	
КонецФункции

#КонецОбласти

//++ НЕ УТ

#Область ИнвентаризацияТМЦВЭксплуатации

// Заполнить действие на основании инвентаризации.
// 
// Параметры:
//  ДанныеСтроки - СтрокаТабличнойЧасти - Данные строки:
//  	* Действие - ПеречислениеСсылка.ДействияНаОснованииИнвентаризации -
//  ДанныеУчета - Структура -
//  Организация - СправочникСсылка.Организации - Организация
//  Подразделение - СправочникСсылка.СтруктураПредприятия - Подразделение
//  ФизическоеЛицо - СправочникСсылка.ФизическиеЛица - МОЛ
//	УстановитьДействие - Булево - Ложь, если нужно оставить ранее установленное действие
Процедура ЗаполнитьДействиеНаОснованииИнвентаризации(ДанныеСтроки, ДанныеУчета, Организация, Подразделение, ФизическоеЛицо, УстановитьДействие = Истина) Экспорт
	
	Если ДанныеУчета <> Неопределено Тогда
		ОрганизацияПоДаннымУчета = ДанныеУчета.Организация;
		ПодразделениеПоДаннымУчета = ДанныеУчета.Подразделение;
		ФизическоеЛицоПоДаннымУчета = ДанныеУчета.ФизическоеЛицо;
	ИначеЕсли ТипЗнч(ДанныеСтроки) = Тип("ДанныеФормыЭлементКоллекции") Тогда
		ОрганизацияПоДаннымУчета = ДанныеСтроки.ОрганизацияПоДаннымУчета;
		ПодразделениеПоДаннымУчета = ДанныеСтроки.ПодразделениеПоДаннымУчета;
		ФизическоеЛицоПоДаннымУчета = ДанныеСтроки.ФизическоеЛицоПоДаннымУчета;
	Иначе
		ОрганизацияПоДаннымУчета = ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка");
		ПодразделениеПоДаннымУчета = ПредопределенноеЗначение("Справочник.СтруктураПредприятия.ПустаяСсылка");
		ФизическоеЛицоПоДаннымУчета = ПредопределенноеЗначение("Справочник.ФизическиеЛица.ПустаяСсылка");
	КонецЕсли;
	
	Если ДанныеСтроки.КоличествоПоДаннымУчета <> 0
		И (ДанныеСтроки.КоличествоФактическое = 0
			ИЛИ ДанныеСтроки.КоличествоПоДаннымУчета > ДанныеСтроки.КоличествоФактическое
				И ОрганизацияПоДаннымУчета = Организация
				И ПодразделениеПоДаннымУчета = Подразделение
				И (ФизическоеЛицоПоДаннымУчета = ФизическоеЛицо
					ИЛИ НЕ ЗначениеЗаполнено(ФизическоеЛицо)
						И ФизическоеЛицоПоДаннымУчета = ДанныеСтроки.ФизическоеЛицо)) Тогда
			
		ДанныеСтроки.Действие = ПредопределенноеЗначение("Перечисление.ДействияНаОснованииИнвентаризации.Списание");
		
	ИначеЕсли ДанныеСтроки.КоличествоФактическое <> 0
		И (ДанныеСтроки.КоличествоПоДаннымУчета = 0
			ИЛИ ДанныеСтроки.КоличествоПоДаннымУчета < ДанныеСтроки.КоличествоФактическое) Тогда
			
		Если УстановитьДействие
			ИЛИ ДанныеСтроки.Действие = ПредопределенноеЗначение("Перечисление.ДействияНаОснованииИнвентаризации.ПустаяСсылка")
			ИЛИ ДанныеСтроки.Действие = ПредопределенноеЗначение("Перечисление.ДействияНаОснованииИнвентаризации.Списание")
			ИЛИ ДанныеСтроки.Действие = ПредопределенноеЗначение("Перечисление.ДействияНаОснованииИнвентаризации.ВозвратИзЭксплуатации")
				И ДанныеСтроки.КоличествоПоДаннымУчета = 0 Тогда
			
			ДанныеСтроки.Действие = ПредопределенноеЗначение("Перечисление.ДействияНаОснованииИнвентаризации.Оприходование");
			
		КонецЕсли;
		
	Иначе
	
		ДанныеСтроки.Действие = ПредопределенноеЗначение("Перечисление.ДействияНаОснованииИнвентаризации.ПустаяСсылка");
		
	КонецЕсли;

КонецПроцедуры

// Заполнить строку инвентаризации по данным учета.
// 
// Параметры:
//  Объект - ДокументОбъект.ИнвентаризацияТМЦВЭксплуатации - 
//  ДанныеСтроки - ДанныеФормыСтруктура - 
//  ДанныеУчета - Структура - 
//  ЗаполнитьСлужебныеРеквизиты - Булево - 
//	УстановитьДействие - Булево - Ложь, если нужно оставить ранее установленное действие
Процедура ЗаполнитьСтрокуИнвентаризацииПоДаннымУчета(Объект, ДанныеСтроки, ДанныеУчета, ЗаполнитьСлужебныеРеквизиты = Ложь, УстановитьДействие = Истина) Экспорт
	
	Если ДанныеУчета <> Неопределено
		И ДанныеУчета.Организация = Объект.Организация
		И ДанныеУчета.Подразделение = Объект.Подразделение
		И (ДанныеУчета.ФизическоеЛицо = Объект.ФизическоеЛицо
			ИЛИ НЕ ЗначениеЗаполнено(Объект.ФизическоеЛицо)
				И ДанныеСтроки.ФизическоеЛицо = ДанныеУчета.ФизическоеЛицо) Тогда

		ДанныеСтроки.КоличествоПоДаннымУчета = ДанныеУчета.КоличествоПоДаннымУчета;
	Иначе
		ДанныеСтроки.КоличествоПоДаннымУчета = 0;
	КонецЕсли;
	
	Если ЗаполнитьСлужебныеРеквизиты Тогда
		Если ДанныеУчета <> Неопределено Тогда
			ДанныеСтроки.ОрганизацияПоДаннымУчета = ДанныеУчета.Организация;
			ДанныеСтроки.ПодразделениеПоДаннымУчета = ДанныеУчета.Подразделение;
			ДанныеСтроки.ФизическоеЛицоПоДаннымУчета = ДанныеУчета.ФизическоеЛицо;
		Иначе
			ДанныеСтроки.ОрганизацияПоДаннымУчета = Неопределено;
			ДанныеСтроки.ПодразделениеПоДаннымУчета = Неопределено;
			ДанныеСтроки.ФизическоеЛицоПоДаннымУчета = Неопределено;
		КонецЕсли;
	КонецЕсли; 
	
	ЗаполнитьДействиеНаОснованииИнвентаризации(
		ДанныеСтроки,
		ДанныеУчета,
		Объект.Организация, 
		Объект.Подразделение,
		Объект.ФизическоеЛицо,
		УстановитьДействие);
	
КонецПроцедуры

#КонецОбласти

//-- НЕ УТ

#Область КатегорииЭксплуатации

// 
// Параметры:
//  Объект - СправочникОбъект.КатегорииЭксплуатации, ДанныеФормыСтруктура - 
// 	ИзмененныеРеквизиты - Строка - список измененных реквизитов.
// 	
// Возвращаемое значение:
// 	Массив из см. НовыйПараметрРеквизитаОбъекта 
Функция ЗначенияСвойствЗависимыхРеквизитов_КатегорииЭксплуатации(Объект, ИзмененныеРеквизиты = "") Экспорт

	СтруктураИзмененныхРеквизитов = Новый Структура(ИзмененныеРеквизиты);
	ОбновитьВсе = СтруктураИзмененныхРеквизитов.Количество() = 0;
	
	ПараметрыРеквизитовОбъекта = Новый Массив;

	//++ НЕ УТ
	
	Если СтруктураИзмененныхРеквизитов.Свойство("ИмущественныйУчет")
		ИЛИ ОбновитьВсе Тогда
		
		ЗначениеСвойства = НЕ Объект.ИмущественныйУчет;
										
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"ИнвентарныйУчет", 
			"ИнвентарныйУчет", 
			"ТолькоПросмотр", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
	
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"УчетПоФизЛицам", 
			"УчетПоФизЛицам", 
			"ТолькоПросмотр", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
	
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"СрокЭксплуатации", 
			"СрокЭксплуатации", 
			"ТолькоПросмотр", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
	
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"УчитыватьВВидеГрупповогоОС", 
			"УчитыватьВВидеГрупповогоОС", 
			"ТолькоПросмотр", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
	
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"СпособПогашенияСтоимостиБУ", 
			"СпособПогашенияСтоимостиБУ", 
			"ТолькоПросмотрБезОчистки", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
	
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"", 
			"НастройкаСчетовУчета_СчетУчета_ТМЦВЭксплуатацииЗаБалансом", 
			"ТолькоПросмотр", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);

	КонецЕсли;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("ИмущественныйУчет")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("УчитыватьВВидеГрупповогоОС")
		ИЛИ ОбновитьВсе Тогда
		
		ЗначениеСвойства = 
			НЕ Объект.УчитыватьВВидеГрупповогоОС
			ИЛИ НЕ Объект.ИмущественныйУчет;
										
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"ГруппаОС", 
			"ГруппаОС", 
			"ТолькоПросмотрБезОтметкиНезаполненного", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"СтатьяКапитализацииСтоимостиОС", 
			"СтатьяКапитализацииСтоимостиОС", 
			"ТолькоПросмотрБезОтметкиНезаполненного", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
	КонецЕсли;
		
	Если СтруктураИзмененныхРеквизитов.Свойство("ИмущественныйУчет")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("СпособПогашенияСтоимостиБУ")
		ИЛИ ОбновитьВсе Тогда

		ЗначениеСвойства = 
			Объект.СпособПогашенияСтоимостиБУ = ПредопределенноеЗначение("Перечисление.СпособыПогашенияСтоимостиТМЦ.ПриПередаче")
			ИЛИ НЕ Объект.ИмущественныйУчет;
										
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"СпособПогашенияСтоимостиНУ", 
			"СпособПогашенияСтоимостиНУ", 
			"ТолькоПросмотрБезОчистки", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
	КонецЕсли;
	
	//-- НЕ УТ

	Возврат ПараметрыРеквизитовОбъекта;

КонецФункции

#КонецОбласти

#Область ОпределениеСвойствЗависимыхРеквизитовОбъекта

// 
// Параметры:
//  Объект - ДокументОбъект.СписаниеИзЭксплуатации - 
// 	ВспомогательныеРеквизиты - Структура - 
// 	ИзмененныеРеквизиты - Строка - список измененных реквизитов.
// 	
// Возвращаемое значение:
// 	Массив из см. ОбщегоНазначенияУТКлиентСервер.НовыйПараметрРеквизитаОбъекта 
Функция ЗначенияСвойствЗависимыхРеквизитов_СписаниеИзЭксплуатации(Объект, ВспомогательныеРеквизиты, ИзмененныеРеквизиты = "") Экспорт
	
	СтруктураИзмененныхРеквизитов = Новый Структура(ИзмененныеРеквизиты);
	ОбновитьВсе = СтруктураИзмененныхРеквизитов.Количество() = 0;
	
	ПараметрыРеквизитовОбъекта = Новый Массив;

	Если СтруктураИзмененныхРеквизитов.Свойство("Дата")
		ИЛИ ОбновитьВсе Тогда
		
		ЗначениеСвойства = ВспомогательныеРеквизиты.ИспользоватьУчетПрослеживаемыхТоваров;
										
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"Товары.НомерГТД", 
			"ТоварыНомерГТД", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);

		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"Товары.КоличествоПоРНПТ", 
			"ТоварыКоличествоПоРНПТ", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);

	КонецЕсли;
	
	Возврат ПараметрыРеквизитовОбъекта;
		
КонецФункции

#КонецОбласти

#Область Прочее

// Формирует новое значение выбора партии ТМЦ.
// 
// Возвращаемое значение:
//  Структура - содержит:
// 		* Номенклатура - СправочникСсылка.Номенклатура -
// 		* Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры -
// 		* Серия - СправочникСсылка.СерииНоменклатуры -
// 		* Партия - СправочникСсылка.ПартииТМЦВЭксплуатации -
// 		* ФизическоеЛицо - СправочникСсылка.ФизическиеЛица -
// 		* НаправлениеДеятельности - СправочникСсылка.НаправленияДеятельности
// 		* ИнвентарныйНомер - Строка -
// 		* Подразделение - СправочникСсылка.СтруктураПредприятия -
// 		* НомерГТД - СправочникСсылка.НомераГТД -
// 		* ИнвентарныйУчет - Булево -
// 		* УчетПоФизЛицам - Булево -
// 		* ОбъемНаработки - Число - 
// 		* ДатаЗавершенияЭксплуатации - Дата -
// 		* Количество - Число -
// 		* КоличествоУпаковок - Число -
// 		* КоличествоПоРНПТ - Число -
Функция НовоеЗначениеВыбора() Экспорт
	
	ЗначениеВыбора = Новый Структура;
	
	ЗначениеВыбора.Вставить("Номенклатура", ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка"));
	ЗначениеВыбора.Вставить("Характеристика", ПредопределенноеЗначение("Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка"));
	ЗначениеВыбора.Вставить("Серия", ПредопределенноеЗначение("Справочник.СерииНоменклатуры.ПустаяСсылка"));
	ЗначениеВыбора.Вставить("Партия", ПредопределенноеЗначение("Справочник.ПартииТМЦВЭксплуатации.ПустаяСсылка"));
	ЗначениеВыбора.Вставить("ФизическоеЛицо", ПредопределенноеЗначение("Справочник.ФизическиеЛица.ПустаяСсылка"));
	ЗначениеВыбора.Вставить("НаправлениеДеятельности", ПредопределенноеЗначение("Справочник.НаправленияДеятельности.ПустаяСсылка"));
	ЗначениеВыбора.Вставить("ИнвентарныйНомер", "");
	ЗначениеВыбора.Вставить("Подразделение", ПредопределенноеЗначение("Справочник.СтруктураПредприятия.ПустаяСсылка"));
	ЗначениеВыбора.Вставить("НомерГТД", ПредопределенноеЗначение("Справочник.НомераГТД.ПустаяСсылка"));
	ЗначениеВыбора.Вставить("ИнвентарныйУчет", Ложь);
	ЗначениеВыбора.Вставить("УчетПоФизЛицам", Ложь);
	ЗначениеВыбора.Вставить("ОбъемНаработки", 0);
	ЗначениеВыбора.Вставить("ДатаЗавершенияЭксплуатации", '000101010000');
	ЗначениеВыбора.Вставить("Количество", 0);
	ЗначениеВыбора.Вставить("КоличествоУпаковок", 0);
	ЗначениеВыбора.Вставить("КоличествоПоРНПТ", 0);
	
	Возврат ЗначениеВыбора;
	
КонецФункции

//++ НЕ УТ

// Формирует описание запроса для выбора партии ТМЦ.
// 
// Параметры:
//  Параметры - см. ТМЦВЭксплуатацииКлиентСервер.ПараметрыПодбораТМЦВЭксплуатации.
// 
// Возвращаемое значение:
//  Структура - Описание запроса для выбора партии ТМЦ:
// 		* ТекстЗапроса - Строка - Текст запроса.
// 		* ПараметрыЗапроса - Структура - Параметры запроса.
Функция ОписаниеЗапросаДляВыбораПартииТМЦ(Параметры) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 10
	|	ТМЦВЭксплуатации.Организация КАК Организация,
	|	ТМЦВЭксплуатации.Подразделение КАК Подразделение,
	|	ТМЦВЭксплуатации.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ТМЦВЭксплуатации.Номенклатура КАК Номенклатура,
	|	ТМЦВЭксплуатации.Характеристика КАК Характеристика,
	|	ТМЦВЭксплуатации.Серия КАК Серия,
	|	ТМЦВЭксплуатации.Партия КАК Партия,
	|	ТМЦВЭксплуатации.Партия.Наименование КАК ПартияПредставление,
	|	ТМЦВЭксплуатации.Партия.КатегорияЭксплуатации.ИнвентарныйУчет КАК ИнвентарныйУчет,
	|	ТМЦВЭксплуатации.Партия.КатегорияЭксплуатации.УчетПоФизЛицам КАК УчетПоФизЛицам,
	|	ТМЦВЭксплуатации.Партия.КатегорияЭксплуатации.СпособПогашенияСтоимостиБУ КАК СпособПогашенияСтоимостиБУ,
	|	ТМЦВЭксплуатации.Партия.КатегорияЭксплуатации.ОбъемНаработки КАК ОбъемНаработки,
	|	ТМЦВЭксплуатации.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТМЦВЭксплуатации.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	ТМЦВЭксплуатации.НомерГТД КАК НомерГТД,
	|
	|	СУММА(ТМЦВЭксплуатации.Количество) КАК Количество,
	|	СУММА(ТМЦВЭксплуатации.КоличествоПоРНПТ) КАК КоличествоПоРНПТ
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТМЦВЭксплуатацииПереопределяемый.Организация КАК Организация,
	|		ТМЦВЭксплуатацииПереопределяемый.Подразделение КАК Подразделение,
	|		ТМЦВЭксплуатацииПереопределяемый.ФизическоеЛицо КАК ФизическоеЛицо,
	|		ТМЦВЭксплуатацииПереопределяемый.Номенклатура КАК Номенклатура,
	|		ТМЦВЭксплуатацииПереопределяемый.Характеристика КАК Характеристика,
	|		ТМЦВЭксплуатацииПереопределяемый.Серия КАК Серия,
	|		ТМЦВЭксплуатацииПереопределяемый.Партия КАК Партия,
	|		ТМЦВЭксплуатацииПереопределяемый.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ТМЦВЭксплуатацииПереопределяемый.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|		ТМЦВЭксплуатацииПереопределяемый.НомерГТД КАК НомерГТД,
	|		ТМЦВЭксплуатацииПереопределяемый.КоличествоОстаток КАК Количество,
	|		ТМЦВЭксплуатацииПереопределяемый.КоличествоПоРНПТОстаток КАК КоличествоПоРНПТ
	|	ИЗ
	|		РегистрНакопления.ТМЦВЭксплуатации.Остатки(
	|			&ОтборДата,
	|			(Организация = &ОтборОрганизация
	|					ИЛИ &ОтборОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
	|
	|				И (Подразделение = &ОтборПодразделение
	|					ИЛИ &ОтборПодразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
	|
	|				И (&ПодборПоДаннымСтроки
	|					ИЛИ ФизическоеЛицо = &ОтборФизическоеЛицо
	|					ИЛИ &ОтборФизическоеЛицо = ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка))
	|
	|				И (ИнвентарныйНомер = &ИнвентарныйНомер
	|					ИЛИ НЕ &ИспользоватьОтборПоИнвентарномуНомеру)
	|
	|				И (ИнвентарныйНомер ПОДОБНО &ИнвентарныйНомерСодержит
	|					ИЛИ &ИнвентарныйНомерСодержит = """")
	|
	|				И (НЕ &ПодборПоДаннымСтроки
	|					ИЛИ Номенклатура = &ОтборНоменклатура
	|							И Характеристика = &ОтборХарактеристика
	|							И Серия = &ОтборСерия
	|							И ФизическоеЛицо = &ОтборФизическоеЛицо)) КАК ТМЦВЭксплуатацииПереопределяемый
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ТМЦВЭксплуатацииПереопределяемый.Организация КАК Организация,
	|		ТМЦВЭксплуатацииПереопределяемый.Подразделение КАК Подразделение,
	|		ТМЦВЭксплуатацииПереопределяемый.ФизическоеЛицо КАК ФизическоеЛицо,
	|		ТМЦВЭксплуатацииПереопределяемый.Номенклатура КАК Номенклатура,
	|		ТМЦВЭксплуатацииПереопределяемый.Характеристика КАК Характеристика,
	|		ТМЦВЭксплуатацииПереопределяемый.Серия КАК Серия,
	|		ТМЦВЭксплуатацииПереопределяемый.Партия КАК Партия,
	|		ТМЦВЭксплуатацииПереопределяемый.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ТМЦВЭксплуатацииПереопределяемый.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|		ТМЦВЭксплуатацииПереопределяемый.НомерГТД КАК НомерГТД,
	|		ВЫБОР
	|			КОГДА ТМЦВЭксплуатацииПереопределяемый.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ТМЦВЭксплуатацииПереопределяемый.Количество
	|			ИНАЧЕ ТМЦВЭксплуатацииПереопределяемый.Количество
	|		КОНЕЦ КАК Количество,
	|		ВЫБОР
	|			КОГДА ТМЦВЭксплуатацииПереопределяемый.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ТМЦВЭксплуатацииПереопределяемый.КоличествоПоРНПТ
	|			ИНАЧЕ ТМЦВЭксплуатацииПереопределяемый.КоличествоПоРНПТ
	|		КОНЕЦ КАК КоличествоПоРНПТ
	|	ИЗ
	|		РегистрНакопления.ТМЦВЭксплуатации КАК ТМЦВЭксплуатацииПереопределяемый
	|	ГДЕ
	|		ТМЦВЭксплуатацииПереопределяемый.Регистратор = &ТекущийРегистратор
	|		И ТМЦВЭксплуатацииПереопределяемый.Активность
	|
	|		И (ТМЦВЭксплуатацииПереопределяемый.Период < &ОтборДата
	|			ИЛИ &ОтборДата = ДАТАВРЕМЯ(1,1,1))
	|
	|		И (ТМЦВЭксплуатацииПереопределяемый.Организация = &ОтборОрганизация
	|				ИЛИ &ОтборОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
	|
	|		И (ТМЦВЭксплуатацииПереопределяемый.Подразделение = &ОтборПодразделение
	|				ИЛИ &ОтборПодразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
	|
	|		И (&ПодборПоДаннымСтроки
	|				ИЛИ ТМЦВЭксплуатацииПереопределяемый.ФизическоеЛицо = &ОтборФизическоеЛицо
	|				ИЛИ &ОтборФизическоеЛицо = ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка))
	|
	|		И (ТМЦВЭксплуатацииПереопределяемый.ИнвентарныйНомер = &ИнвентарныйНомер
	|				ИЛИ НЕ &ИспользоватьОтборПоИнвентарномуНомеру)
	|
	|		И (ТМЦВЭксплуатацииПереопределяемый.ИнвентарныйНомер ПОДОБНО &ИнвентарныйНомерСодержит
	|				ИЛИ &ИнвентарныйНомерСодержит = """")
	|
	|		И (НЕ &ПодборПоДаннымСтроки
	|				ИЛИ ТМЦВЭксплуатацииПереопределяемый.Номенклатура = &ОтборНоменклатура
	|					И ТМЦВЭксплуатацииПереопределяемый.Характеристика = &ОтборХарактеристика
	|					И ТМЦВЭксплуатацииПереопределяемый.Серия = &ОтборСерия
	|					И ТМЦВЭксплуатацииПереопределяемый.ФизическоеЛицо = &ОтборФизическоеЛицо)
	|
	|	) КАК ТМЦВЭксплуатации
	|
	|ГДЕ
	|	(&ОтборНазначенныйРесурс = """"
	|		ИЛИ &ОтборНазначенныйРесурс = ""Выработан""
	|			И ТМЦВЭксплуатации.Партия.ДатаЗавершенияЭксплуатации < &ОтборДата
	|		ИЛИ &ОтборНазначенныйРесурс = ""НеВыработан""
	|			И ТМЦВЭксплуатации.Партия.ДатаЗавершенияЭксплуатации >= &ОтборДата)
	|
	|	И (&ОтборИнвентарныйУчет = 0
	|		ИЛИ &ОтборИнвентарныйУчет = 1
	|				И НЕ ТМЦВЭксплуатации.Партия.КатегорияЭксплуатации.ИнвентарныйУчет
	|		ИЛИ &ОтборИнвентарныйУчет = 2
	|				И ТМЦВЭксплуатации.Партия.КатегорияЭксплуатации.ИнвентарныйУчет)
	|
	|	И (&СпособПогашенияСтоимостиБУ = ЗНАЧЕНИЕ(Перечисление.СпособыПогашенияСтоимостиТМЦ.ПустаяСсылка)
	|		ИЛИ ТМЦВЭксплуатации.Партия.КатегорияЭксплуатации.СпособПогашенияСтоимостиБУ = &СпособПогашенияСтоимостиБУ)
	|
	|	И (&ДатаНачалаЭксплуатацииС = ДАТАВРЕМЯ(1,1,1)
	|		ИЛИ ТМЦВЭксплуатации.Партия.ДатаНачалаЭксплуатации >= &ДатаНачалаЭксплуатацииС)
	|
	|	И (&ДатаНачалаЭксплуатацииПо = ДАТАВРЕМЯ(1,1,1)
	|		ИЛИ ТМЦВЭксплуатации.Партия.ДатаНачалаЭксплуатации <= &ДатаНачалаЭксплуатацииПо)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТМЦВЭксплуатации.Организация,
	|	ТМЦВЭксплуатации.Подразделение,
	|	ТМЦВЭксплуатации.ФизическоеЛицо,
	|	ТМЦВЭксплуатации.Номенклатура,
	|	ТМЦВЭксплуатации.Характеристика,
	|	ТМЦВЭксплуатации.Серия,
	|	ТМЦВЭксплуатации.Партия,
	|	ТМЦВЭксплуатации.НаправлениеДеятельности,
	|	ТМЦВЭксплуатации.ИнвентарныйНомер,
	|	ТМЦВЭксплуатации.НомерГТД
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТМЦВЭксплуатации.Количество) > 0";
	
	Если НЕ Параметры.ДляДинамическогоСписка Тогда
		
		ТекстЗапроса = ТекстЗапроса + "
		|
		|УПОРЯДОЧИТЬ ПО
		|	Организация,
		|	Подразделение,
		|	ФизическоеЛицо,
		|	Номенклатура,
		|	Характеристика,
		|	Серия,
		|	НаправлениеДеятельности,
		|	ИнвентарныйНомер";
		
	КонецЕсли;
	
	ОтборДата = ?(
		Параметры.Дата <> Неопределено И Параметры.Дата <> '000101010000', 
		КонецДня(Параметры.Дата) + 1, 
		'000101010000');
	
	ОтборНазначенныйРесурс = ?(
		Параметры.НазначенныйРесурс <> Неопределено, 
		Параметры.НазначенныйРесурс, 
		"");
	
	ОтборОрганизация = ?(
		Параметры.Организация <> Неопределено, 
		Параметры.Организация, 
		ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка"));

	ОтборПодразделение = ?(
		Параметры.Подразделение <> Неопределено, 
		Параметры.Подразделение, 
		ПредопределенноеЗначение("Справочник.СтруктураПредприятия.ПустаяСсылка"));

	ОтборНоменклатура = ?(
		Параметры.Номенклатура <> Неопределено, 
		Параметры.Номенклатура, 
		ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка"));

	ОтборХарактеристика = ?(
		Параметры.Характеристика <> Неопределено, 
		Параметры.Характеристика, 
		ПредопределенноеЗначение("Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка"));

	Если Параметры.Серия <> Неопределено
		И (Параметры.СтатусУказанияСерийОтправитель = 19
			ИЛИ Параметры.СтатусУказанияСерийОтправитель = 20
			ИЛИ Параметры.СтатусУказанияСерийОтправитель = Неопределено
				И (Параметры.СтатусУказанияСерий = 19
					ИЛИ Параметры.СтатусУказанияСерий = 20)) Тогда
		ОтборСерия = Параметры.Серия;
	Иначе
		ОтборСерия = ПредопределенноеЗначение("Справочник.СерииНоменклатуры.ПустаяСсылка");
	КонецЕсли;

	ОтборФизическоеЛицо = ?(
		Параметры.ФизическоеЛицо <> Неопределено, 
		Параметры.ФизическоеЛицо, 
		ПредопределенноеЗначение("Справочник.ФизическиеЛица.ПустаяСсылка"));
	
	ОтборСпособПогашенияСтоимостиБУ = ?(
		Параметры.СпособПогашенияСтоимостиБУ <> Неопределено, 
		Параметры.СпособПогашенияСтоимостиБУ, 
		ПредопределенноеЗначение("Перечисление.СпособыПогашенияСтоимостиТМЦ.ПустаяСсылка"));
	
	ТекущийРегистратор = Параметры.ТекущийРегистратор;
		
	ИнвентарныйНомер = ?(
		Параметры.ИнвентарныйНомер <> Неопределено, 
		Параметры.ИнвентарныйНомер, 
		"");
		
	ИспользоватьОтборПоИнвентарномуНомеру = ЗначениеЗаполнено(Параметры.ИнвентарныйНомер);
	
	ИнвентарныйНомерСодержит = ?(
		Параметры.ИнвентарныйНомерСодержит <> Неопределено, 
		"%" + Параметры.ИнвентарныйНомерСодержит + "%", 
		"");
		
	ОтборИнвентарныйУчет = ?(
		ТипЗнч(Параметры.ИнвентарныйУчет) = Тип("Булево"),
		?(Параметры.ИнвентарныйУчет, 2, 0),
		Параметры.ИнвентарныйУчет);
		
	ПодборПоДаннымСтроки = 
		Параметры.Номенклатура <> Неопределено
		ИЛИ Параметры.Характеристика <> Неопределено
		ИЛИ Параметры.Серия <> Неопределено;
	
	Если Параметры.ДляДинамическогоСписка Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "РАЗРЕШЕННЫЕ ПЕРВЫЕ 10", "");
	КонецЕсли;
	
	Если Параметры.ВыбратьПервые > 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПЕРВЫЕ 10", "ПЕРВЫЕ" + " " + Формат(Параметры.ВыбратьПервые, "ЧГ=0;"));
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПЕРВЫЕ 10", "");
	КонецЕсли;
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("ОтборДата", ОтборДата);
	ПараметрыЗапроса.Вставить("ОтборНазначенныйРесурс", ОтборНазначенныйРесурс);
	ПараметрыЗапроса.Вставить("ОтборОрганизация", ОтборОрганизация);
	ПараметрыЗапроса.Вставить("ОтборПодразделение", ОтборПодразделение);
	ПараметрыЗапроса.Вставить("ОтборНоменклатура", ОтборНоменклатура);
	ПараметрыЗапроса.Вставить("ОтборХарактеристика", ОтборХарактеристика);
	ПараметрыЗапроса.Вставить("ОтборСерия", ОтборСерия);
	ПараметрыЗапроса.Вставить("ОтборФизическоеЛицо", ОтборФизическоеЛицо);
	ПараметрыЗапроса.Вставить("СпособПогашенияСтоимостиБУ", ОтборСпособПогашенияСтоимостиБУ);
	ПараметрыЗапроса.Вставить("ПодборПоДаннымСтроки", ПодборПоДаннымСтроки);
	ПараметрыЗапроса.Вставить("ТекущийРегистратор", ТекущийРегистратор);
	ПараметрыЗапроса.Вставить("ИнвентарныйНомер", ИнвентарныйНомер);
	ПараметрыЗапроса.Вставить("ИспользоватьОтборПоИнвентарномуНомеру", ИспользоватьОтборПоИнвентарномуНомеру);
	ПараметрыЗапроса.Вставить("ИнвентарныйНомерСодержит", ИнвентарныйНомерСодержит);
	ПараметрыЗапроса.Вставить("ОтборИнвентарныйУчет", ОтборИнвентарныйУчет);
	ПараметрыЗапроса.Вставить("ДатаНачалаЭксплуатацииС", Параметры.ДатаНачалаЭксплуатацииС);
	ПараметрыЗапроса.Вставить("ДатаНачалаЭксплуатацииПо",Параметры.ДатаНачалаЭксплуатацииПо);
	
	ОписаниеЗапросаДляВыбора = Новый Структура("ТекстЗапроса,ПараметрыЗапроса", ТекстЗапроса, ПараметрыЗапроса);
	
	Возврат ОписаниеЗапросаДляВыбора;
	
КонецФункции

//-- НЕ УТ

// Формирует описание запроса для выбора партии ТМЦ.
// 
// Параметры:
//  Параметры - см. ТМЦВЭксплуатацииКлиентСервер.ПараметрыПодбораТМЦВЭксплуатации.
// 
// Возвращаемое значение:
//  Структура - Описание запроса для выбора партии ТМЦ:
// 		* ТекстЗапроса - Строка - Текст запроса.
// 		* ПараметрыЗапроса - Структура - Параметры запроса.
Функция ОписаниеЗапросаДляВыбораПартииТМЦ_УТ(Параметры) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПартииТМЦ.Ссылка КАК Ссылка
	|
	|ПОМЕСТИТЬ ПартииТМЦ
	|
	|ИЗ
	|	Справочник.ПартииТМЦВЭксплуатации КАК ПартииТМЦ
	|
	|ГДЕ
	|	ПартииТМЦ.ДатаЗавершенияЭксплуатации >= &ОтборДата
	|	И НЕ ПартииТМЦ.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 10
	|	ТМЦВЭксплуатации.Организация КАК Организация,
	|	ТМЦВЭксплуатации.Подразделение КАК Подразделение,
	|	ТМЦВЭксплуатации.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ТМЦВЭксплуатации.Номенклатура КАК Номенклатура,
	|	ТМЦВЭксплуатации.Характеристика КАК Характеристика,
	|	ЛОЖЬ КАК ИнвентарныйУчет,
	|	ИСТИНА КАК УчетПоФизЛицам,
	|	ТМЦВЭксплуатации.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТМЦВЭксплуатации.ДатаЗавершенияЭксплуатации КАК ДатаЗавершенияЭксплуатации,
	|
	|	СУММА(ТМЦВЭксплуатации.Количество) КАК Количество
	|
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаТовары.Ссылка.Организация КАК Организация,
	|		ТаблицаТовары.Ссылка.Подразделение КАК Подразделение,
	|		ТаблицаТовары.ФизическоеЛицо КАК ФизическоеЛицо,
	|		ТаблицаТовары.Номенклатура КАК Номенклатура,
	|		ТаблицаТовары.Характеристика КАК Характеристика,
	|		ТаблицаТовары.Ссылка.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ТаблицаТовары.Партия.ДатаЗавершенияЭксплуатации КАК ДатаЗавершенияЭксплуатации,
	|		ТаблицаТовары.Количество КАК Количество
	|		
	|	ИЗ
	|		Документ.ВнутреннееПотребление.Товары КАК ТаблицаТовары
	|
	|	ГДЕ
	|		ТаблицаТовары.Партия В
	|			(ВЫБРАТЬ
	|				ПартииТМЦ.Ссылка
	|			ИЗ
	|				ПартииТМЦ КАК ПартииТМЦ)
	|		И ТаблицаТовары.Ссылка.Проведен
	|
	|		И (ТаблицаТовары.Ссылка.Организация = &ОтборОрганизация
	|			ИЛИ &ОтборОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
	|
	|		И (ТаблицаТовары.Ссылка.Подразделение = &ОтборПодразделение
	|			ИЛИ &ОтборПодразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
	|
	|		И (&ПодборПоДаннымСтроки
	|			ИЛИ ТаблицаТовары.ФизическоеЛицо = &ОтборФизическоеЛицо
	|			ИЛИ &ОтборФизическоеЛицо = ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка))
	|
	|		И (НЕ &ПодборПоДаннымСтроки
	|			ИЛИ ТаблицаТовары.Номенклатура = &ОтборНоменклатура
	|				И ТаблицаТовары.Характеристика = &ОтборХарактеристика
	|				И ТаблицаТовары.ФизическоеЛицо = &ОтборФизическоеЛицо)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ТаблицаТовары.Ссылка.Организация КАК Организация,
	|		ТаблицаТовары.Ссылка.Подразделение КАК Подразделение,
	|		ТаблицаТовары.ФизическоеЛицо КАК ФизическоеЛицо,
	|		ТаблицаТовары.Номенклатура КАК Номенклатура,
	|		ТаблицаТовары.Характеристика КАК Характеристика,
	|		ТаблицаТовары.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ТаблицаТовары.Партия.ДатаЗавершенияЭксплуатации КАК ДатаЗавершенияЭксплуатации,
	|		ТаблицаТовары.Количество КАК Количество
	|	ИЗ
	|		Документ.ВводОстатковТМЦВЭксплуатации.ТМЦВЭксплуатации КАК ТаблицаТовары
	|
	|	ГДЕ
	|		ТаблицаТовары.Партия В
	|			(ВЫБРАТЬ
	|				ПартииТМЦ.Ссылка
	|			ИЗ
	|				ПартииТМЦ КАК ПартииТМЦ)
	|		И ТаблицаТовары.Ссылка.Проведен
	|
	|		И (ТаблицаТовары.Ссылка.Организация = &ОтборОрганизация
	|			ИЛИ &ОтборОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
	|
	|		И (ТаблицаТовары.Ссылка.Подразделение = &ОтборПодразделение
	|			ИЛИ &ОтборПодразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
	|
	|		И (&ПодборПоДаннымСтроки
	|			ИЛИ ТаблицаТовары.ФизическоеЛицо = &ОтборФизическоеЛицо
	|			ИЛИ &ОтборФизическоеЛицо = ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка))
	|
	|		И (НЕ &ПодборПоДаннымСтроки
	|			ИЛИ ТаблицаТовары.Номенклатура = &ОтборНоменклатура
	|				И ТаблицаТовары.Характеристика = &ОтборХарактеристика
	|				И ТаблицаТовары.ФизическоеЛицо = &ОтборФизическоеЛицо)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ТаблицаТовары.Ссылка.Организация КАК Организация,
	|		ТаблицаТовары.Ссылка.Подразделение КАК Подразделение,
	|		ТаблицаТовары.ФизическоеЛицо КАК ФизическоеЛицо,
	|		ТаблицаТовары.Номенклатура КАК Номенклатура,
	|		ТаблицаТовары.Характеристика КАК Характеристика,
	|		ТаблицаТовары.Ссылка.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ТаблицаТовары.Партия.ДатаЗавершенияЭксплуатации КАК ДатаЗавершенияЭксплуатации,
	|		ТаблицаТовары.Количество КАК Количество
	|	ИЗ
	|		Документ.ВводОстатков.ТМЦВЭксплуатации КАК ТаблицаТовары
	|
	|	ГДЕ
	|		ТаблицаТовары.Партия В
	|			(ВЫБРАТЬ
	|				ПартииТМЦ.Ссылка
	|			ИЗ
	|				ПартииТМЦ КАК ПартииТМЦ)
	|		И ТаблицаТовары.Ссылка.Проведен
	|
	|		И (ТаблицаТовары.Ссылка.Организация = &ОтборОрганизация
	|			ИЛИ &ОтборОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
	|
	|		И (ТаблицаТовары.Ссылка.Подразделение = &ОтборПодразделение
	|			ИЛИ &ОтборПодразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
	|
	|		И (&ПодборПоДаннымСтроки
	|			ИЛИ ТаблицаТовары.ФизическоеЛицо = &ОтборФизическоеЛицо
	|			ИЛИ &ОтборФизическоеЛицо = ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка))
	|
	|		И (НЕ &ПодборПоДаннымСтроки
	|			ИЛИ ТаблицаТовары.Номенклатура = &ОтборНоменклатура
	|				И ТаблицаТовары.Характеристика = &ОтборХарактеристика
	|				И ТаблицаТовары.ФизическоеЛицо = &ОтборФизическоеЛицо)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ТаблицаТовары.Ссылка.Организация КАК Организация,
	|		ТаблицаТовары.Ссылка.Подразделение КАК Подразделение,
	|		ТаблицаТовары.ФизическоеЛицо КАК ФизическоеЛицо,
	|		ТаблицаТовары.Номенклатура КАК Номенклатура,
	|		ТаблицаТовары.Характеристика КАК Характеристика,
	|		ТаблицаТовары.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ТаблицаТовары.ДатаЗавершенияЭксплуатации КАК ДатаЗавершенияЭксплуатации,
	|		-ТаблицаТовары.Количество КАК Количество
	|		
	|	ИЗ
	|		Документ.ПрочееОприходованиеТоваров.Товары КАК ТаблицаТовары
	|
	|	ГДЕ
	|		ТаблицаТовары.ДатаЗавершенияЭксплуатации >= &ОтборДата
	|		И ТаблицаТовары.Ссылка.Дата <= &ОтборДата
	|		И ТаблицаТовары.Ссылка.Проведен
	|		И ТаблицаТовары.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратИзЭксплуатации)
	|
	|		И (ТаблицаТовары.Ссылка.Организация = &ОтборОрганизация
	|			ИЛИ &ОтборОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
	|
	|		И (ТаблицаТовары.Ссылка.Подразделение = &ОтборПодразделение
	|			ИЛИ &ОтборПодразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
	|
	|		И (&ПодборПоДаннымСтроки
	|			ИЛИ ТаблицаТовары.ФизическоеЛицо = &ОтборФизическоеЛицо
	|			ИЛИ &ОтборФизическоеЛицо = ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка))
	|
	|		И (НЕ &ПодборПоДаннымСтроки
	|			ИЛИ ТаблицаТовары.Номенклатура = &ОтборНоменклатура
	|				И ТаблицаТовары.Характеристика = &ОтборХарактеристика
	|				И ТаблицаТовары.ФизическоеЛицо = &ОтборФизическоеЛицо)
	|			
	|	) КАК ТМЦВЭксплуатации
	|
	|СГРУППИРОВАТЬ ПО
	|	ТМЦВЭксплуатации.Организация,
	|	ТМЦВЭксплуатации.Подразделение,
	|	ТМЦВЭксплуатации.ФизическоеЛицо,
	|	ТМЦВЭксплуатации.Номенклатура,
	|	ТМЦВЭксплуатации.Характеристика,
	|	ТМЦВЭксплуатации.НаправлениеДеятельности,
	|	ТМЦВЭксплуатации.ДатаЗавершенияЭксплуатации
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТМЦВЭксплуатации.Количество) > 0";
	
	Если НЕ Параметры.ДляДинамическогоСписка Тогда
		
		ТекстЗапроса = ТекстЗапроса + "
		|
		|УПОРЯДОЧИТЬ ПО
		|	Организация,
		|	Подразделение,
		|	ФизическоеЛицо,
		|	Номенклатура,
		|	Характеристика,
		|	НаправлениеДеятельности";
		
	КонецЕсли;
	
	ОтборДата = ?(
		Параметры.Дата <> Неопределено И Параметры.Дата <> '000101010000', 
		КонецДня(Параметры.Дата) + 1, 
		'000101010000');
	
	ОтборОрганизация = ?(
		Параметры.Организация <> Неопределено, 
		Параметры.Организация, 
		ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка"));

	ОтборПодразделение = ?(
		Параметры.Подразделение <> Неопределено, 
		Параметры.Подразделение, 
		ПредопределенноеЗначение("Справочник.СтруктураПредприятия.ПустаяСсылка"));

	ОтборНоменклатура = ?(
		Параметры.Номенклатура <> Неопределено, 
		Параметры.Номенклатура, 
		ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка"));

	ОтборХарактеристика = ?(
		Параметры.Характеристика <> Неопределено, 
		Параметры.Характеристика, 
		ПредопределенноеЗначение("Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка"));

	ОтборФизическоеЛицо = ?(
		Параметры.ФизическоеЛицо <> Неопределено, 
		Параметры.ФизическоеЛицо, 
		ПредопределенноеЗначение("Справочник.ФизическиеЛица.ПустаяСсылка"));
	
	
	ПодборПоДаннымСтроки = 
		Параметры.Номенклатура <> Неопределено
		ИЛИ Параметры.Характеристика <> Неопределено;
	
	Если Параметры.ДляДинамическогоСписка Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "РАЗРЕШЕННЫЕ ПЕРВЫЕ 10", "");
	КонецЕсли;
	
	Если Параметры.ВыбратьПервые > 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПЕРВЫЕ 10", "ПЕРВЫЕ" + " " + Формат(Параметры.ВыбратьПервые, "ЧГ=0;"));
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПЕРВЫЕ 10", "");
	КонецЕсли;
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("ОтборДата", ОтборДата);
	ПараметрыЗапроса.Вставить("ОтборОрганизация", ОтборОрганизация);
	ПараметрыЗапроса.Вставить("ОтборПодразделение", ОтборПодразделение);
	ПараметрыЗапроса.Вставить("ОтборНоменклатура", ОтборНоменклатура);
	ПараметрыЗапроса.Вставить("ОтборХарактеристика", ОтборХарактеристика);
	ПараметрыЗапроса.Вставить("ОтборФизическоеЛицо", ОтборФизическоеЛицо);
	ПараметрыЗапроса.Вставить("ПодборПоДаннымСтроки", ПодборПоДаннымСтроки);
	
	ОписаниеЗапросаДляВыбора = Новый Структура("ТекстЗапроса,ПараметрыЗапроса", ТекстЗапроса, ПараметрыЗапроса);
	
	Возврат ОписаниеЗапросаДляВыбора;
	
КонецФункции

#КонецОбласти

#КонецОбласти
