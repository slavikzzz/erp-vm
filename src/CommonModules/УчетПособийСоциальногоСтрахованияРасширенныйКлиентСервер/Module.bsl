
#Область СлужебныеПроцедурыИФункции

// Функция конструирует структуру, описывающую данные о начислениях 
// для расчета среднего заработка за один месяц расчетного периода.
// Поля структуры заполнены пустыми значениями соответствующих типов.
//
Функция ОписаниеСтрокиДанныхОНачисленияхСреднегоЗаработкаФСС() Экспорт
	
	ПоляОписания = Новый Соответствие;
	ПоляОписания.Вставить("ФизическоеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	ПоляОписания.Вставить("Начисление", Новый ОписаниеТипов("ПланВидовРасчетаСсылка.Начисления,СправочникСсылка.ВидыВыплатБывшимСотрудникам,СправочникСсылка.ВидыПрочихДоходовФизическихЛиц,ПеречислениеСсылка.ВидыОсобыхНачисленийИУдержаний"));
	ПоляОписания.Вставить("ПорядокРасчета", Новый ОписаниеТипов("ПеречислениеСсылка.ПорядокРасчетаСреднегоЗаработкаФСС"));
	ПоляОписания.Вставить("СпособУчета", Новый ОписаниеТипов("ПеречислениеСсылка.УчетНачисленийВСреднемЗаработкеФСС"));
	ПоляОписания.Вставить("СтатьяФинансирования", Новый ОписаниеТипов("СправочникСсылка.СтатьиФинансированияЗарплата"));
	ПоляОписания.Вставить("СпособОтраженияЗарплатыВБухучете", Новый ОписаниеТипов("СправочникСсылка.СпособыОтраженияЗарплатыВБухУчете"));
	ПоляОписания.Вставить("СтатьяРасходов", Новый ОписаниеТипов("СправочникСсылка.СтатьиРасходовЗарплата"));
	ПоляОписания.Вставить("ОблагаетсяЕНВД", Новый ОписаниеТипов("Булево"));
	ПоляОписания.Вставить("Период", Новый ОписаниеТипов("Дата"));
	ПоляОписания.Вставить("Сумма", Новый ОписаниеТипов("Число"));
	ПоляОписания.Вставить("Источник", Новый ОписаниеТипов("ПеречислениеСсылка.ИсточникиДанныхДляРасчетаСреднегоЗаработка"));
	
	Описание = Новый Структура;
	Для Каждого КлючИЗначение Из ПоляОписания Цикл
		Описание.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение.ПривестиЗначение());
	КонецЦикла;
	
	Возврат Описание;
	
КонецФункции

// Функция конструирует структуру, описывающую данные периодах отсутствия.
// Поля структуры заполнены пустыми значениями соответствующих типов.
//
Функция ОписаниеСтрокиПериодовДнейБолезниУходаЗаДетьми() Экспорт
	
	ПоляОписания = Новый Соответствие;
	ПоляОписания.Вставить("Начало", Новый ОписаниеТипов("Дата"));
	ПоляОписания.Вставить("Окончание", Новый ОписаниеТипов("Дата"));
	ПоляОписания.Вставить("Дни", Новый ОписаниеТипов("Число"));
	ПоляОписания.Вставить("ВидБолезниУходаЗаДетьми", Новый ОписаниеТипов("Строка"));
	ПоляОписания.Вставить("Источник", Новый ОписаниеТипов("ПеречислениеСсылка.ИсточникиДанныхДляРасчетаСреднегоЗаработка"));
	
	Описание = Новый Структура;
	Для Каждого КлючИЗначение Из ПоляОписания Цикл
		Описание.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение.ПривестиЗначение());
	КонецЦикла;
	
	Возврат Описание;
	
КонецФункции

Функция КатегорияНачисленияОплатаБольничногоЛистаЗаСчетРаботодателя() Экспорт
	
	КатегорияНачисления = ПредопределенноеЗначение("Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ОплатаБольничногоЛистаЗаСчетРаботодателя");
	
	Возврат КатегорияНачисления;
	
КонецФункции

Функция КатегорияНачисленияДоплатаДоСреднегоЗаработкаЗаДниБолезни() Экспорт
	
	КатегорияНачисления = ПредопределенноеЗначение("Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ДоплатаДоСреднегоЗаработкаЗаДниБолезни");
	
	Возврат КатегорияНачисления;
	
КонецФункции

Функция КатегорияНеоплачиваемогоВремениПоПричинеНетрудоспособности(ПричинаНетрудоспособности) Экспорт
	
	КатегорияНачисления = ПредопределенноеЗначение("Перечисление.КатегорииНачисленийИНеоплаченногоВремени.БолезньБезОплаты");
	Если ПричинаНетрудоспособности = ПредопределенноеЗначение("Перечисление.ПричиныНетрудоспособности.ПоБеременностиИРодам") Тогда
		КатегорияНачисления = ПредопределенноеЗначение("Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ОтпускПоБеременностиИРодамБезОплаты");
	КонецЕсли;
	
	Возврат КатегорияНачисления;
	
КонецФункции

Функция ОписаниеСпособаРасчетаКатегорииОплатаДнейУходаЗаДетьмиИнвалидами() Экспорт
	Возврат НСтр("ru = 'Дополнительные выходные дни для ухода за детьми-инвалидами выплачиваются исходя из среднего заработка в порядке, установленном законодательством о социальном страховании.';
				|en = 'Extra days off for disabled child care are paid according to the average earnings in the order established by the social insurance legislation.'");
КонецФункции

Функция АдресСсылкиДляОтключенияПодпискиНаЭЛН() Экспорт
	Возврат "СЭДОФССРасширенныйКлиент_ОтключитьПодписку";
КонецФункции

#КонецОбласти
