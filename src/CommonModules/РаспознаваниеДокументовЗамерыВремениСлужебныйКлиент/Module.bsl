#Область СлужебныеПроцедурыИФункции

Процедура ПодключитьОбработчикОтправкиЗамеровВремени() Экспорт
	
	Если ПараметрыПриложения = Неопределено Тогда
		ВызватьИсключение НСтр(
			"ru = 'Не инициализирован параметр ПараметрыПриложения.
			      |
			      |Скорее всего это ошибка связана с неправильным порядком запуска
			      |или отключением логики начала работы системы.
			      |
			      |Убедитесь, что Библиотека Стандартных Подсистем внедрена корректно и все
			      |обработчики начала работы системы переадресованы на подписки БСП.';
			      |en = 'The ПараметрыПриложения parameter is not initialized.
			      |
			      |This error is most likely due to the wrong startup order
			      |or disabling the system startup logic.
			      |
			      |Make sure that Standard Subsystem Library is implemented correctly and all
			      |system startup handlers are forwarded to SSL subscriptions.'");
	КонецЕсли;
	
	ИмяПараметра = "РаспознаваниеДокументов.ЗамерыВремени";
	Если ПараметрыПриложения[ИмяПараметра] = Неопределено Тогда
		ПараметрыПриложения.Вставить(ИмяПараметра, Новый Соответствие);
		РаспознаваниеДокументовПараметрыПриложения = ПараметрыПриложения[ИмяПараметра];
		РаспознаваниеДокументовПараметрыПриложения.Вставить("ЕстьОбработчик", Ложь);
	Иначе 
		РаспознаваниеДокументовПараметрыПриложения = ПараметрыПриложения[ИмяПараметра];
	КонецЕсли;
	
	Если Не РаспознаваниеДокументовПараметрыПриложения["ЕстьОбработчик"] Тогда 
		Интервал = 300; // секунд, т.е. 5 минут.
		
		// См. РаспознаваниеДокументовГлобальный.ОтправитьЗамерыВремени
		ПодключитьОбработчикОжидания("РаспознаваниеДокументовОтправитьЗамерыВремени", Интервал);
		
		РаспознаваниеДокументовПараметрыПриложения["ЕстьОбработчик"] = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти