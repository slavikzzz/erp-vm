// @strict-types

#Область СлужебныйПрограммныйИнтерфейс

// Получает объекты и их управляющие модули встраивания в механизм ФормированиеПечатныхФорм
//
// Возвращаемое значение:
//  ФиксированноеСоответствие Из КлючИЗначение - Управляющие модули объектов:
//   * Ключ     - Строка                        - Полное имя метаданных встраиваемого объекта
//   * Значение - ОбщийМодуль, ДокументМенеджер - Объект, в котором расположена логика интеграции с механизмом ФормированиеПечатныхФорм
Функция УправляющиеМодули() Экспорт
	
	Результат = Новый Соответствие;
	
	ФормированиеПечатныхФорм.ПриЗаполненииУправляющихМодулей(Результат);
	
	Для каждого УправляющийМодуль Из Результат Цикл
		
		Если УправляющийМодуль.Значение = Неопределено Тогда
			ПолноеИмяМетаданных = УправляющийМодуль.Ключ; // Строка
			
			Если Метаданные.НайтиПоПолномуИмени(ПолноеИмяМетаданных) = Неопределено Тогда
				Продолжить; // Объект отсутствует в конфигурации (вырезка)
			КонецЕсли;
			
			Модуль = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмяМетаданных);
			Результат.Вставить(УправляющийМодуль.Ключ, Модуль);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Новый ФиксированноеСоответствие(Результат);
	
КонецФункции

#КонецОбласти