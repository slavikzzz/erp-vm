////////////////////////////////////////////////////////////////////////////////
// Отражение зарплаты в бухгалтерском учете.
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает структуру с настройкой бухучета сотрудника на указанную дату.
//
// Параметры:
//  Сотрудник - Тип СправочникСсылка.Сотрудник
//  ДатаАктуальности - Тип Дата, дата на которую получаем способ отражения.
//
// Возвращаемое значение: Структура, ключи СпособОтраженияЗарплатыВБухучете, ОтношениеКЕНВД, Период
//		СпособОтраженияЗарплатыВБухучете, тип Справочник.СпособыОтраженияЗарплатыВБухУчете
//		ОтношениеКЕНВД, тип Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.
//		Период, дата на которую установлено значение.
//
Функция НастройкаБухучетаЗарплатыСотрудника(Сотрудник, ДатаАктуальности) Экспорт
	
	БухучетСотрудника = Новый Структура("СтатьяФинансирования, СтатьяРасходов, СпособОтраженияЗарплатыВБухучете, ОтношениеКЕНВД, Период, ДействуетДо");
	БухучетСотрудника.СтатьяФинансирования = Справочники.СтатьиФинансированияЗарплата.ПустаяСсылка();
	БухучетСотрудника.СтатьяРасходов = Справочники.СтатьиРасходовЗарплата.ПустаяСсылка();
	БухучетСотрудника.СпособОтраженияЗарплатыВБухучете = Справочники.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка();
	БухучетСотрудника.ОтношениеКЕНВД = Перечисления.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка();
	БухучетСотрудника.Период = Дата(1,1,1);
	БухучетСотрудника.ДействуетДо = Дата(1,1,1);
	
	ТаблицаФильтра = Новый ТаблицаЗначений;
	ТаблицаФильтра.Колонки.Добавить("Сотрудник",  	Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	ТаблицаФильтра.Колонки.Добавить("Период",  		Новый ОписаниеТипов("Дата"));
	НоваяСтрока = ТаблицаФильтра.Добавить();
	НоваяСтрока.Сотрудник 	= Сотрудник;
	НоваяСтрока.Период 		= ДатаАктуальности;
	
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	СоздатьВТБухучетЗарплатыСотрудников(МенеджерВТ, ТаблицаФильтра);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(Бухучет.ПериодЗаписи, ДЕНЬ) КАК Период,
	|	Бухучет.ПериодВозвратногоСобытия КАК ДействуетДо,
	|	Бухучет.СтатьяФинансирования КАК СтатьяФинансирования,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка) КАК СтатьяРасходов,
	|	Бухучет.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	Бухучет.ОтношениеКЕНВД КАК ОтношениеКЕНВД
	|ИЗ
	|	ВТБухучетЗарплатыСотрудников КАК Бухучет";
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(БухучетСотрудника, Выборка);
	КонецЕсли;
	
	Возврат БухучетСотрудника;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область Свойства

// См. УправлениеСвойствамиПереопределяемый.ПриПолученииПредопределенныхНаборовСвойств.
Процедура ПриПолученииПредопределенныхНаборовСвойств(Наборы) Экспорт
	
	УправлениеСвойствамиБЗК.ЗарегистрироватьНаборСвойств(Наборы, "d42dbf2d-9802-11e9-80cd-4cedfb43b11a", Метаданные.Документы.БухучетНачисленийСотрудников);
	УправлениеСвойствамиБЗК.ЗарегистрироватьНаборСвойств(Наборы, "d42dbeee-9802-11e9-80cd-4cedfb43b11a", Метаданные.Документы.ПереносЗатратНаПерсоналМеждуСтатьями);
	УправлениеСвойствамиБЗК.ЗарегистрироватьНаборСвойств(Наборы, "d42dbfa7-9802-11e9-80cd-4cedfb43b11a", Метаданные.Документы.РаспределениеОсновногоЗаработка);
	УправлениеСвойствамиБЗК.ЗарегистрироватьНаборСвойств(Наборы, "158b7d3d-114a-42f8-9c4b-29e9415163aa", Метаданные.Документы.БухучетЗарплатыСотрудников);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработкаНачисленийУдержанийПередЗаписью

Процедура УдержаниеПередЗаписью(ВидРасчетаОбъект) Экспорт
	
	// Уточнение стратегии отражения в бухучете.
	СтратегияОтраженияВсегдаПоБазе = ВидРасчетаОбъект.КатегорияУдержания = Перечисления.КатегорииУдержаний.ИсполнительныйЛист
		Или ВидРасчетаОбъект.КатегорияУдержания = Перечисления.КатегорииУдержаний.ВознаграждениеПлатежногоАгента;
		
	Если СтратегияОтраженияВсегдаПоБазе И ВидРасчетаОбъект.СтратегияОтраженияВУчете <> Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазовымРасчетам Тогда
		ВидРасчетаОбъект.СтратегияОтраженияВУчете = Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазовымРасчетам;
	Иначе
		Если Не ЗначениеЗаполнено(ВидРасчетаОбъект.СтратегияОтраженияВУчете) Тогда
			Если ВидРасчетаОбъект.ТребуетсяРасчетБазы Тогда
				ВидРасчетаОбъект.СтратегияОтраженияВУчете = Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазовымРасчетам;
			Иначе
				ВидРасчетаОбъект.СтратегияОтраженияВУчете = Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоФактическимНачислениям;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ВидРасчетаОбъект.ДоступнаСтратегияОтраженияКакЗаданоВидуРасчета = Не СтратегияОтраженияВсегдаПоБазе;
	
	УстановитьОчередностьОтраженияВУчете(ВидРасчетаОбъект);
	
	// Уточнение значения свойства ЯвляетсяОснованиемОформленияКассовогоЧека.
	Если ВидРасчетаОбъект.ЯвляетсяОснованиемОформленияКассовогоЧека Тогда
		МожетЯвляетсяОснованием = ОтражениеЗарплатыВБухучетеКлиентСерверРасширенный.УдержаниеМожетЯвляетсяОснованиемОформленияКассовогоЧека(ВидРасчетаОбъект.КатегорияУдержания, ВидРасчетаОбъект.ВидОперацииПоЗарплате);
		Если Не МожетЯвляетсяОснованием Тогда
			ВидРасчетаОбъект.ЯвляетсяОснованиемОформленияКассовогоЧека = Ложь;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Заполняет реквизит ОчередностьОтраженияВУчете для начислений и удержаний.
//
Процедура УстановитьОчередностьОтраженияВУчете(ВидРасчетаОбъект) Экспорт 

	Если ВидРасчетаОбъект.СтратегияОтраженияВУчете = Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазовымРасчетам Тогда 
		ВидРасчетаОбъект.ОчередностьОтраженияВУчете = ВидРасчетаОбъект.ОчередностьРасчета;
	Иначе 
		ВидРасчетаОбъект.ОчередностьОтраженияВУчете = 1;
	КонецЕсли;
	
КонецПроцедуры

Процедура УточнитьСтратегиюОтраженияВУчетеНачисления(ВидРасчетаОбъект) Экспорт

	ИспользоватьСтатьиФинансирования = ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата");
	РаботаВБюджетномУчреждении = ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении");
	ИспользуетсяЕНВД = ПолучитьФункциональнуюОпцию("ИспользуетсяЕНВД");
	
	Если РаботаВБюджетномУчреждении И ВидРасчетаОбъект.ОтношениеКЕНВД = Перечисления.ОтношениеКЕНВДЗатратНаЗарплату.ОпределяетсяЕжемесячноПроцентом Тогда
		ВидРасчетаОбъект.ОтношениеКЕНВД = Перечисления.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка();
	КонецЕсли;
	
	КатегорияНачисления = ВидРасчетаОбъект.КатегорияНачисленияИлиНеоплаченногоВремени;
	Если ОтражениеЗарплатыВБухучетеКлиентСерверРасширенный.ЭтоКатегорияНачисленияОплатаДолиРКСН(КатегорияНачисления) Тогда
		КатегорияНачисления = ОтражениеЗарплатыВБухучетеКлиентСерверРасширенный.КатегрияОсновногоВидаРасчета(ВидРасчетаОбъект);
	КонецЕсли;
	
	// Для оплаты дней ухода игнорируем статью расходов при вычислении стратегии отражения в бухучете.
	ЭтоОплатаДнейУходаЗаДетьмиИнвалидами = КатегорияНачисления = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаДнейУходаЗаДетьмиИнвалидами
						Или КатегорияНачисления = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ДенежноеСодержаниеОплатаДнейУходаЗаДетьмиИнвалидами;
	
	СтратегияОтраженияВУчете = ВидРасчетаОбъект.СтратегияОтраженияВУчете;
	СтатьяФинансирования = ВидРасчетаОбъект.СтатьяФинансирования;
	СтатьяРасходов = ВидРасчетаОбъект.СтатьяРасходов;
	СпособОтраженияЗарплатыВБухучете = ВидРасчетаОбъект.СпособОтраженияЗарплатыВБухучете;
	ОтношениеКЕНВД = ВидРасчетаОбъект.ОтношениеКЕНВД;
	
	СтатьяФинансированияЗаполнена = ИспользоватьСтатьиФинансирования И Не СтатьяФинансирования.Пустая();
	СтатьяРасходовЗаполнена = РаботаВБюджетномУчреждении И Не ЭтоОплатаДнейУходаЗаДетьмиИнвалидами И Не СтатьяРасходов.Пустая();
	СпособОтраженияЗаполнен = Не СпособОтраженияЗарплатыВБухучете.Пустая();
	ОтношениеКЕНВДЗаполнено = ИспользуетсяЕНВД И Не ОтношениеКЕНВД.Пустая();
	НастройкаЗаполнена = СпособОтраженияЗаполнен Или ОтношениеКЕНВДЗаполнено Или СтатьяФинансированияЗаполнена Или СтатьяРасходовЗаполнена;
	
	ЭтоПособиеЗаСчетФСС 	= УчетПособийСоциальногоСтрахованияРасширенный.НачислениеЯвляетсяПособиемЗаСчетФСС(ВидРасчетаОбъект);
	НачислениеБезОплаты 	= ОтражениеЗарплатыВБухучетеКлиентСерверРасширенный.КатегорияНачисленийБезОплаты(ВидРасчетаОбъект.КатегорияНачисленияИлиНеоплаченногоВремени);
	ОплатаПоСреднему    	= ОтражениеЗарплатыВБухучетеКлиентСерверРасширенный.НачислениеОплатаПоСреднемуОбщий(ВидРасчетаОбъект);
	ОплатаПоСохраняемомуДС 	= ВидРасчетаОбъект.Рассчитывается И ОтражениеЗарплатыВБухучетеКлиентСерверРасширенный.КатегорияНачисленийПоСохраняемомуДС(КатегорияНачисления);
	
	Если СтратегияОтраженияВУчете.Пустая() Тогда
		// Начальное заполнение стратегии.
		Если ЭтоПособиеЗаСчетФСС Тогда
			СтратегияОтраженияВУчете = Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоДаннымОСотрудникеИЕгоПлановыхНачислениях;
		ИначеЕсли НачислениеБезОплаты Тогда
			СтратегияОтраженияВУчете = Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоДаннымОСотрудникеИЕгоПлановыхНачислениях;
		ИначеЕсли НастройкаЗаполнена Тогда
			СтратегияОтраженияВУчете = Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.КакЗаданоВидуРасчета;
		Иначе
			СтратегияОтраженияВУчете = Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоДаннымОСотрудникеИЕгоПлановыхНачислениях;
		КонецЕсли;
	ИначеЕсли Не НастройкаЗаполнена И СтратегияОтраженияВУчете = Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.КакЗаданоВидуРасчета Тогда
		СтратегияОтраженияВУчете = Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоДаннымОСотрудникеИЕгоПлановыхНачислениях;
	ИначеЕсли НастройкаЗаполнена Тогда
		СтратегияОтраженияВУчете = Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.КакЗаданоВидуРасчета;
	ИначеЕсли СтратегияОтраженияВУчете = Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазеСреднегоЗаработка Тогда
		
		Если НЕ ЭтоПособиеЗаСчетФСС И Не ОплатаПоСреднему
				И НЕ КатегорияНачисления = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаБольничногоЛистаЗаСчетРаботодателя Тогда
			СтратегияОтраженияВУчете = Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоДаннымОСотрудникеИЕгоПлановыхНачислениях;
		КонецЕсли;	
		
	ИначеЕсли СтратегияОтраженияВУчете = Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазовымРасчетам Тогда	
		
		Если Не ОплатаПоСохраняемомуДС И ЭтоПособиеЗаСчетФСС Или ОплатаПоСреднему
				Или КатегорияНачисления = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаБольничногоЛистаЗаСчетРаботодателя Тогда
			СтратегияОтраженияВУчете = Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоДаннымОСотрудникеИЕгоПлановыхНачислениях;
		Иначе
			
			БазаДоступна = ОплатаПоСохраняемомуДС Или ВидРасчетаОбъект.ТребуетсяРасчетБазы
			И ВидРасчетаОбъект.СпособРасчета <> Перечисления.СпособыРасчетаНачислений.ДоплатаДоСреднегоЗаработка
			И ВидРасчетаОбъект.СпособРасчета <> Перечисления.СпособыРасчетаНачислений.ДоплатаДоСреднегоЗаработкаФСС
			И ВидРасчетаОбъект.СпособРасчета <> Перечисления.СпособыРасчетаНачислений.ДоплатаДоСохраняемогоДенежногоСодержанияЗаДниБолезни
			И ВидРасчетаОбъект.ПериодРасчетаБазовыхНачислений = Перечисления.ПериодыРасчетаБазовыхНачислений.ТекущийМесяц;
			
			Если Не БазаДоступна Тогда
				СтратегияОтраженияВУчете = Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоДаннымОСотрудникеИЕгоПлановыхНачислениях;	
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если НастройкаЗаполнена И СтратегияОтраженияВУчете <> Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.КакЗаданоВидуРасчета Тогда
		ВидРасчетаОбъект.СтатьяФинансирования 				= Справочники.СтатьиФинансированияЗарплата.ПустаяСсылка();
		ВидРасчетаОбъект.СтатьяРасходов 					= Справочники.СтатьиРасходовЗарплата.ПустаяСсылка();
		ВидРасчетаОбъект.СпособОтраженияЗарплатыВБухучете 	= Справочники.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка();
		ВидРасчетаОбъект.ОтношениеКЕНВД 					= Перечисления.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка();
	КонецЕсли;
	
	ВидРасчетаОбъект.СтратегияОтраженияВУчете = СтратегияОтраженияВУчете;
	
КонецПроцедуры

Процедура УточнитьВидОперацииПоЗарплатеНачисления(ВидРасчетаОбъект) Экспорт
	
	Если ВидРасчетаОбъект.КатегорияНачисленияИлиНеоплаченногоВремени = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаОтпуска
		Или ВидРасчетаОбъект.КатегорияНачисленияИлиНеоплаченногоВремени = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ДенежноеСодержаниеНаПериодОтпуска Тогда
		ОтпускЯвляетсяЕжегодным = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидРасчетаОбъект.ВидОтпуска,"ОтпускЯвляетсяЕжегодным");
		Если ОтпускЯвляетсяЕжегодным <> Неопределено И ОтпускЯвляетсяЕжегодным И ВидРасчетаОбъект.ВидОперацииПоЗарплате <> Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпуск Тогда
			ВидРасчетаОбъект.ВидОперацииПоЗарплате = Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпуск;
		КонецЕсли;
	ИначеЕсли ВидРасчетаОбъект.КатегорияНачисленияИлиНеоплаченногоВремени = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.КомпенсацияОтпуска
		Или ВидРасчетаОбъект.КатегорияНачисленияИлиНеоплаченногоВремени = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ДенежноеСодержаниеКомпенсацияОтпуска Тогда
		ОтпускЯвляетсяЕжегодным = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидРасчетаОбъект.ВидОтпуска,"ОтпускЯвляетсяЕжегодным");
		Если ОтпускЯвляетсяЕжегодным <> Неопределено И ОтпускЯвляетсяЕжегодным И ВидРасчетаОбъект.ВидОперацииПоЗарплате <> Перечисления.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпуска Тогда
			ВидРасчетаОбъект.ВидОперацииПоЗарплате = Перечисления.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпуска;
		КонецЕсли;
	ИначеЕсли ОтражениеЗарплатыВБухучетеКлиентСерверРасширенный.ЭтоКатегорияНачисленияОплатаДолиРКСН(ВидРасчетаОбъект.КатегорияНачисленияИлиНеоплаченногоВремени) Тогда
		Если ЗначениеЗаполнено(ВидРасчетаОбъект.ОсновнойВидРасчета) Тогда
			ВидРасчетаОбъект.ВидОперацииПоЗарплате = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидРасчетаОбъект.ОсновнойВидРасчета, "ВидОперацииПоЗарплате");
		ИначеЕсли Не ЗначениеЗаполнено(ВидРасчетаОбъект.ВидОперацииПоЗарплате) Тогда
			ВидРасчетаОбъект.ВидОперацииПоЗарплате = Перечисления.ВидыОперацийПоЗарплате.НачисленоДоход;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область РаспределениеКоллекцийПоБазеПоУмолчанию

// Заполняет в переданной таблице БазаПоУмолчанию поля бухучета, 
// по каждому сотруднику в таблице только одна строка.
//
// Параметры:
// 	БазаПоУмолчанию - ТаблицаЗначений - содержит колонки
// 			* Сотрудник
// 			* Подразделение
// 			* ТерриторияВыполненияРаботВОрганизации
// 			* СтатьяФинансирования
// 			* СтатьяРасходов
// Организация - СправочникСсылка.Организации
// Период - Дата.
//
Процедура ДополнитьБазуУдержанийПоУмолчаниюСтатьями(БазаПоУмолчанию, Организация, Период) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	// Настройки бухучета получаем по состоянию на конец месяца.
	Запрос.УстановитьПараметр("Период", КонецМесяца(Период));
	Запрос.УстановитьПараметр("БазаПоУмолчанию", БазаПоУмолчанию);
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	БазаПоУмолчанию.Сотрудник КАК Сотрудник,
	|	БазаПоУмолчанию.Подразделение КАК Подразделение,
	|	БазаПоУмолчанию.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	АВТОНОМЕРЗАПИСИ() КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ВТСотрудники
	|ИЗ
	|	&БазаПоУмолчанию КАК БазаПоУмолчанию";
	Запрос.Выполнить();
	
	СоздатьВТНастройкиБухучетаДляРаспределенияПоУмолчанию(Запрос.МенеджерВременныхТаблиц, "ВТСотрудники");
	
	Запрос.УстановитьПараметр("СтатьяРасходов", СтатьяОплатаТруда());
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СведенияОБухучете.Сотрудник КАК Сотрудник,
	|	СведенияОБухучете.СтатьяФинансирования КАК СтатьяФинансирования,
	|	&СтатьяРасходов КАК СтатьяРасходов
	|ИЗ
	|	ВТСотрудники КАК Сотрудники
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиБухучета КАК СведенияОБухучете
	|		ПО Сотрудники.Сотрудник = СведенияОБухучете.Сотрудник";
	Выборка = Запрос.Выполнить().Выбрать();
	
	БухучетСтрок = Новый Соответствие;
	Пока Выборка.Следующий() Цикл
		БухучетСтроки = Новый Структура("СтатьяФинансирования,СтатьяРасходов");
		ЗаполнитьЗначенияСвойств(БухучетСтроки, Выборка);
		БухучетСтрок.Вставить(Выборка.Сотрудник, БухучетСтроки);
	КонецЦикла;
	
	Для каждого СтрокаТЗ Из БазаПоУмолчанию Цикл
		ЗаполнитьЗначенияСвойств(СтрокаТЗ, БухучетСтрок[СтрокаТЗ.Сотрудник]);
	КонецЦикла;

КонецПроцедуры

// Заполняет в переданной коллекции строк поля бухучета. 
//
// Параметры:
// 	КоллекцияСтрокВзносов - Массив - содержит ссылки на строки таблицы значений
// 					со структурой см ОтражениеЗарплатыВБухучете.НоваяТаблицыОтражениеВУчетеСтраховыеВзносы
// Организация - СправочникСсылка.Организации
// Период - Дата.
//
Процедура РаспределитьВзносыПоБазеПоУмолчанию(КоллекцияСтрокВзносов, Организация, Период) Экспорт
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("ИдентификаторСтроки", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(7, 0)));
	Таблица.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	Таблица.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	Таблица.Колонки.Добавить("Сотрудник",  Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	Таблица.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	Таблица.Колонки.Добавить("ТерриторияВыполненияРаботВОрганизации", Новый ОписаниеТипов(Метаданные.ОпределяемыеТипы.ТерриторияВыполненияРаботВОрганизации.Тип));
	
	// Настройки бухучета получаем по состоянию на конец месяца.
	ПериодВТаблицу = КонецМесяца(Период);
	Для каждого СтрокаТЗ Из КоллекцияСтрокВзносов Цикл
		НоваяСтрока = Таблица.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
		НоваяСтрока.Организация = Организация;
		НоваяСтрока.Период = ПериодВТаблицу;
	КонецЦикла;
	
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	ЗарплатаКадры.СоздатьВТПоТаблицеЗначений(МенеджерВТ, Таблица, "ВТСотрудники");
	
	СоздатьВТНастройкиБухучетаДляРаспределенияПоУмолчанию(МенеджерВТ, "ВТСотрудники");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.УстановитьПараметр("СтатьяРасходов", СтатьяОплатаТруда());
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтрокиВзносов.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	НастройкиБухучета.СтатьяФинансирования КАК СтатьяФинансирования,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	НастройкиБухучета.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете
	|ИЗ
	|	ВТСотрудники КАК СтрокиВзносов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиБухучета КАК НастройкиБухучета
	|		ПО СтрокиВзносов.ИдентификаторСтроки = НастройкиБухучета.ИдентификаторСтроки";
	Выборка = Запрос.Выполнить().Выбрать();
	
	БухучетСтрок = Новый Соответствие;
	ПоляБухучета = "СпособОтраженияЗарплатыВБухучете";
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
		ПоляБухучета = ПоляБухучета + ",СтатьяФинансирования";
	КонецЕсли;
	Если ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		ПоляБухучета = ПоляБухучета + ",СтатьяРасходов";
	КонецЕсли;
	Пока Выборка.Следующий() Цикл
		БухучетСтроки = Новый Структура(ПоляБухучета);
		ЗаполнитьЗначенияСвойств(БухучетСтроки, Выборка);
		БухучетСтрок.Вставить(Выборка.ИдентификаторСтроки, БухучетСтроки);
	КонецЦикла;
	
	Для каждого СтрокаТЗ Из КоллекцияСтрокВзносов Цикл
		ЗаполнитьЗначенияСвойств(СтрокаТЗ, БухучетСтрок[СтрокаТЗ.ИдентификаторСтроки]);
	КонецЦикла;	
	
КонецПроцедуры

// Распределяет сведения о доходах по статьям по базе по умолчанию.
//
// Параметры:
// 	СведенияОДоходах - ТаблицаЗначений - должна содержать колонки
// 			* ИдентификаторСтроки
// 			* Организация
// 			* Сотрудник
// 			* Период
// 			* Подразделение
// 			* ТерриторияВыполненияРаботВОрганизации
// 			* Начисление
// 	РезультатРаспределения - ТаблицаЗначений - в таблицу помещаются результаты распределения.
//
Процедура РаспределитьСведенияОДоходахПоСтатьямиПоБазеПоУмолчанию(СведенияОДоходах, РезультатРаспределения) Экспорт
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("ИдентификаторСтроки", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(7, 0)));
	Таблица.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	Таблица.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	Таблица.Колонки.Добавить("Сотрудник",  Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	Таблица.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	Таблица.Колонки.Добавить("ТерриторияВыполненияРаботВОрганизации", Новый ОписаниеТипов(Метаданные.ОпределяемыеТипы.ТерриторияВыполненияРаботВОрганизации.Тип));
	
	Для каждого СтрокаТЗ Из СведенияОДоходах Цикл
		ЗаполнитьЗначенияСвойств(Таблица.Добавить(), СтрокаТЗ);
	КонецЦикла;
	
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	ЗарплатаКадры.СоздатьВТПоТаблицеЗначений(МенеджерВТ, Таблица, "ВТСотрудники");
	
	СоздатьВТНастройкиБухучетаДляРаспределенияПоУмолчанию(МенеджерВТ, "ВТСотрудники");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СведенияОДоходах.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	НастройкиБухучета.СтатьяФинансирования КАК СтатьяФинансирования
	|ИЗ
	|	ВТСотрудники КАК СведенияОДоходах
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиБухучета КАК НастройкиБухучета
	|		ПО СведенияОДоходах.ИдентификаторСтроки = НастройкиБухучета.ИдентификаторСтроки";
	Выборка = Запрос.Выполнить().Выбрать();
	
	БухучетСтрок = Новый Соответствие;
	Пока Выборка.Следующий() Цикл
		БухучетСтрок.Вставить(Выборка.ИдентификаторСтроки, Выборка.СтатьяФинансирования);
	КонецЦикла;
	
	РаботаВХозрасчетнойОрганизации = ПолучитьФункциональнуюОпцию("РаботаВХозрасчетнойОрганизации");
	СтатьяОплатаТруда = СтатьяОплатаТруда();
	
	Если РаботаВХозрасчетнойОрганизации Тогда
		НачислениеУдержаниеВидОперации = ОтражениеЗарплатыВУчете.НачислениеУдержаниеВидОперации();
		ОписаниеСтатейРасходов = ЗарплатаКадры.СтатьиРасходовПоСпособамРасчетовСФизическимиЛицами();
	КонецЕсли;
	
	Для каждого СтрокаТЗ Из СведенияОДоходах Цикл
		НоваяСтрока = РезультатРаспределения.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
		НоваяСтрока.СтатьяФинансирования = БухучетСтрок[СтрокаТЗ.ИдентификаторСтроки];
		Если Не РаботаВХозрасчетнойОрганизации Тогда
			НоваяСтрока.СтатьяРасходов = СтатьяОплатаТруда;
		Иначе
			
			ВидОперации = НачислениеУдержаниеВидОперации[СтрокаТЗ.Начисление];
			Если ВидОперации = Перечисления.ВидыОперацийПоЗарплате.Дивиденды Тогда
				СтатьяРасходов = ОписаниеСтатейРасходов[Перечисления.СпособыРасчетовСФизическимиЛицами.Дивиденды];
			ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ВыплатыБывшимСотрудникам 
				Или ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ДоходыКонтрагентов Тогда
				СтатьяРасходов = ОписаниеСтатейРасходов[Перечисления.СпособыРасчетовСФизическимиЛицами.РасчетыСКонтрагентами];
			Иначе
				СтатьяРасходов = СтатьяОплатаТруда;
			КонецЕсли;
			
			НоваяСтрока.СтатьяРасходов = СтатьяРасходов;
			
		КонецЕсли;	
	КонецЦикла;

КонецПроцедуры

// Возвращает настройки бухучета сотрудника для распределения по умолчанию.
//
// Параметры:
// 	Сотрудник - СправочникСсылка.Сотрудники.
// 	ДатаАктуальности - Дата - дата на которую получаются настройки.
//
// Возвращаемое значение:
// 	Структура - свойства структуры
// 			*СтатьяФинансирования
// 			*СтатьяРасходов
// 			*СпособОтраженияЗарплатыВБухучете
// 			*ОтношениеКЕНВД
// 			*ОблагаетсяЕНВД
//
Функция БухучетСотрудникаПоУмолчанию(Сотрудник, ДатаАктуальности) Экспорт
	
	БухучетПоУмолчанию = Новый Структура("СтатьяФинансирования,СпособОтраженияЗарплатыВБухучете,СтатьяРасходов,ОтношениеКЕНВД,ОблагаетсяЕНВД");
	
	КадровыеДанные = КадровыйУчет.КадровыеДанныеСотрудников(Истина, Сотрудник, "Организация,Подразделение,ТерриторияВыполненияРаботВОрганизации", ДатаАктуальности);
	КадровыеДанные = КадровыеДанные.Скопировать(,"Сотрудник,Период,Организация,Подразделение,ТерриторияВыполненияРаботВОрганизации");
	КадровыеДанные.Колонки.Добавить("ИдентификаторСтроки", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(7, 0)));
	
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	ИмяИсходнойВТ = "ВТСотрудники";
	ЗарплатаКадры.СоздатьВТПоТаблицеЗначений(МенеджерВТ, КадровыеДанные, ИмяИсходнойВТ);
	
	СоздатьВТНастройкиБухучетаДляРаспределенияПоУмолчанию(МенеджерВТ, ИмяИсходнойВТ);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиБухучета.СтатьяФинансирования КАК СтатьяФинансирования,
	|	НастройкиБухучета.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	НастройкиБухучета.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	ВЫБОР
	|		КОГДА НастройкиБухучета.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ЕНВД)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОблагаетсяЕНВД
	|ИЗ
	|	ВТНастройкиБухучета КАК НастройкиБухучета";
	Выборка = Запрос.Выполнить().Выбрать();
	
	ПоляБухучета = "СпособОтраженияЗарплатыВБухучете,ОтношениеКЕНВД,ОблагаетсяЕНВД";
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
		ПоляБухучета = ПоляБухучета + ",СтатьяФинансирования";
	КонецЕсли;

	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(БухучетПоУмолчанию, Выборка, ПоляБухучета);
		Если ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
			БухучетПоУмолчанию.СтатьяРасходов = СтатьяОплатаТруда();
		КонецЕсли;
	КонецЕсли;
	
	Возврат БухучетПоУмолчанию;

КонецФункции

// Возвращает настройки бухучета организации.
//
// Параметры:
// 	Организация - СправочникСсылка.Организации.
// 	ДатаАктуальности - Дата - дата на которую получаются настройки.
//
// Возвращаемое значение:
// 	Структура - свойства структуры
// 			*СтатьяФинансирования
// 			*СтатьяРасходов
// 			*СпособОтраженияЗарплатыВБухучете
// 			*ОтношениеКЕНВД
// 			*ОблагаетсяЕНВД
//
Функция БухучетОрганизации(Организация, ДатаАктуальности) Экспорт

	БухучетПоУмолчанию = Новый Структура("СтатьяФинансирования,СпособОтраженияЗарплатыВБухучете,СтатьяРасходов,ОтношениеКЕНВД,ОблагаетсяЕНВД");
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ДатаАктуальности", ДатаАктуальности);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиБухучета.СтатьяФинансирования КАК СтатьяФинансирования,
	|	НастройкиБухучета.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	НастройкиБухучета.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	ВЫБОР
	|		КОГДА НастройкиБухучета.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ЕНВД)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОблагаетсяЕНВД
	|ИЗ
	|	РегистрСведений.БухучетЗарплатыОрганизаций.СрезПоследних(&ДатаАктуальности, Организация = &Организация) КАК НастройкиБухучета";
	Выборка = Запрос.Выполнить().Выбрать();
	
	ПоляБухучета = "СпособОтраженияЗарплатыВБухучете,ОтношениеКЕНВД,ОблагаетсяЕНВД";
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
		ПоляБухучета = ПоляБухучета + ",СтатьяФинансирования";
	КонецЕсли;
	
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(БухучетПоУмолчанию, Выборка, ПоляБухучета);
	КонецЕсли;
	
	Возврат БухучетПоУмолчанию;

КонецФункции

// Распределяет по статьям финансирования начисления ПризПодарок.
// Результат помещается во временную таблицу: ВТРаспределениеПризПодарокПоСтатьям.
//
// Параметры:
// 	МенеджерВТ - МенеджерВременныхТаблиц - содержит таблицу ВТНачисленияДляРаспределения.
// 	НастройкиИзДокумента - ТаблицаЗначений - см НоваяТаблицаБухучетЗарплатыПервичныхДокументов,
// 								содержит одну строку с настройками бухучета, указанными в документе.
// 	Организация - СправочникСсылка.Организации.
// 	Период - Дата.
//
Процедура СоздатьВТРаспределениеПризПодарокПоСтатьям(МенеджерВТ, НастройкиИзДокумента, Организация, Период) Экспорт
	
	УдалитьВТ = Новый Массив;
	
	ПрименяетсяЕНВД = ОтражениеЗарплатыВБухучете.ПлательщикЕНВД(Организация, Период);
	
	СтатьяФинансирования 			 = НастройкиИзДокумента[0].СтатьяФинансирования;
	СпособОтраженияЗарплатыВБухучете = НастройкиИзДокумента[0].СпособОтраженияЗарплатыВБухучете;
	ТребуетсяБухучетПоУмолчанию =  Не ЗначениеЗаполнено(СтатьяФинансирования) Или Не ЗначениеЗаполнено(СпособОтраженияЗарплатыВБухучете);
	
	ОблагаетсяЕНВД = Ложь;
	Если ПрименяетсяЕНВД Тогда
		ОтношениеКЕНВД = НастройкиИзДокумента[0].ОтношениеКЕНВД;
		ОблагаетсяЕНВД = (ОтношениеКЕНВД = Перечисления.ОтношениеКЕНВДЗатратНаЗарплату.ЕНВД);
	КонецЕсли;
	
	СтатьяРасходов = НастройкиИзДокумента[0].СтатьяРасходов;
	Если Не ЗначениеЗаполнено(СтатьяРасходов) Тогда
		СтатьяРасходов = СтатьяОплатаТруда();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.УстановитьПараметр("Период", НачалоМесяца(Период));
	Запрос.УстановитьПараметр("СтатьяФинансирования", СтатьяФинансирования);
	Запрос.УстановитьПараметр("СтатьяРасходов", СтатьяРасходов);
	Запрос.УстановитьПараметр("СпособОтраженияЗарплатыВБухучете", СпособОтраженияЗарплатыВБухучете);
	Запрос.УстановитьПараметр("ОблагаетсяЕНВД", ОблагаетсяЕНВД);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Таблица.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Таблица.Организация КАК Организация,
	|	Таблица.Сотрудник КАК Сотрудник,
	|	Таблица.Подразделение КАК Подразделение,
	|	Таблица.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	&Период КАК Период,
	|	Таблица.Сумма КАК Сумма,
	|	&СтатьяФинансирования КАК СтатьяФинансирования,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	&ОблагаетсяЕНВД КАК ОблагаетсяЕНВД
	|ПОМЕСТИТЬ ВТРаспределениеПризПодарокПоСтатьям
	|ИЗ
	|	ВТНачисленияДляРаспределения КАК Таблица";
	
	Если Не ТребуетсяБухучетПоУмолчанию Тогда
		Запрос.Выполнить();
		Возврат;
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТРаспределениеПризПодарокПоСтатьям", "ВТСотрудникиПризПодарок");
	Запрос.Выполнить();
	УдалитьВТ.Добавить("ВТСотрудникиПризПодарок");
	
	СоздатьВТНастройкиБухучетаДляРаспределенияПоУмолчанию(МенеджерВТ, "ВТСотрудникиПризПодарок");
	УдалитьВТ.Добавить("ВТНастройкиБухучета");
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Начисления.Сумма КАК Сумма,
	|	ВЫБОР
	|		КОГДА Начисления.СтатьяФинансирования = ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|			ТОГДА НастройкиБухучета.СтатьяФинансирования
	|		ИНАЧЕ Начисления.СтатьяФинансирования
	|	КОНЕЦ КАК СтатьяФинансирования,
	|	ВЫБОР
	|		КОГДА Начисления.СпособОтраженияЗарплатыВБухучете = ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
	|			ТОГДА НастройкиБухучета.СпособОтраженияЗарплатыВБухучете
	|		ИНАЧЕ Начисления.СпособОтраженияЗарплатыВБухучете
	|	КОНЕЦ КАК СпособОтраженияЗарплатыВБухучете,
	|	Начисления.СтатьяРасходов КАК СтатьяРасходов,
	|	Начисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД
	|ПОМЕСТИТЬ ВТРаспределениеПризПодарокПоСтатьям
	|ИЗ
	|	ВТСотрудникиПризПодарок КАК Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиБухучета КАК НастройкиБухучета
	|		ПО Начисления.ИдентификаторСтроки = НастройкиБухучета.ИдентификаторСтроки";
	Запрос.Выполнить();
	
	ЗарплатаКадры.УничтожитьВТ(МенеджерВТ, УдалитьВТ);
	
КонецПроцедуры

#КонецОбласти

#Область ФормированиеДвижений

Функция НовыеДанныеДляОтраженияВБухучете() Экспорт
	
	ДанныеДляОтраженияВБухучете = Новый Структура;
	ДанныеДляОтраженияВБухучете.Вставить("Начисления", Неопределено);
	ДанныеДляОтраженияВБухучете.Вставить("Удержания", Неопределено);
	ДанныеДляОтраженияВБухучете.Вставить("НДФЛ", Неопределено);
	
Возврат ДанныеДляОтраженияВБухучете;
	
КонецФункции

Процедура СформироватьДвиженияБухучетНачисленияУдержанияПоКонтрагентамАкционерам(
	Движения, Отказ, Организация, ПериодРегистрации, 
	НачисленияУдержанияНДФЛ, ДатаВыплаты, ЗаписыватьДвижения = Ложь) Экспорт

	Начисления				= НачисленияУдержанияНДФЛ.Начисления;
	Удержания				= НачисленияУдержанияНДФЛ.Удержания;
	РезультатыРасчетаНДФЛ	= НачисленияУдержанияНДФЛ.НДФЛ;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата") Тогда
		Возврат;
	КонецЕсли;
	
	ВыделятьНДФЛСПревышения = (ПериодРегистрации < УчетНДФЛ.ДатаЗакона176ФЗ());
	НачислениеУдержаниеВидОперации = ОтражениеЗарплатыВУчете.НачислениеУдержаниеВидОперации(ВыделятьНДФЛСПревышения);
	
	Если Начисления <> НеОпределено Тогда
		
		ВидыДоходаИсполнительногоПроизводства = УчетНачисленнойЗарплаты.ВидыДоходовИсполнительногоПроизводстваНачислений(ДатаВыплаты);
		
		Для Каждого Строка Из Начисления Цикл
			
			НоваяСтрока = Движения.БухучетНачисленияУдержанияПоКонтрагентамАкционерам.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			
			НоваяСтрока.Период				= ПериодРегистрации;
			НоваяСтрока.НачислениеУдержание	= Строка.Начисление;
			НоваяСтрока.Организация			= Организация;
			НоваяСтрока.ВидОперации			= НачислениеУдержаниеВидОперации[НоваяСтрока.НачислениеУдержание];
			Если Не ЗначениеЗаполнено(НоваяСтрока.ТерриторияВыполненияРаботВОрганизации) Тогда
				НоваяСтрока.ТерриторияВыполненияРаботВОрганизации = НоваяСтрока.Подразделение;
			КонецЕсли;
			НоваяСтрока.ГруппаНачисленияУдержанияВыплаты = Перечисления.ГруппыНачисленияУдержанияВыплаты.Начислено;
			НоваяСтрока.ВидДоходаИсполнительногоПроизводства = ВидыДоходаИсполнительногоПроизводства[Строка.Начисление];
			
		КонецЦикла;
		
		Движения.БухучетНачисленияУдержанияПоКонтрагентамАкционерам.Записывать = Истина;
		
	КонецЕсли;
	
	Если Удержания <> НеОпределено Тогда
		
		Для Каждого Строка Из Удержания Цикл
			
			НоваяСтрока = Движения.БухучетНачисленияУдержанияПоКонтрагентамАкционерам.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			
			ВидОперации = НачислениеУдержаниеВидОперации[НоваяСтрока.НачислениеУдержание];
			Если ВидОперации = Перечисления.ВидыОперацийПоЗарплате.АлиментыПрочиеИсполнительныеЛисты Тогда
				ВидОперации = Перечисления.ВидыОперацийПоЗарплате.АлиментыПрочиеИсполнительныеЛистыКонтрагенты;
			ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ВознаграждениеПлатежногоАгента Тогда
				ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ВознаграждениеПлатежногоАгентаКонтрагенты;
			КонецЕсли;
			
			НоваяСтрока.Период				= ПериодРегистрации;
			НоваяСтрока.Организация			= Организация;
			НоваяСтрока.ВидОперации			= ВидОперации;
			
			Если Строка.НачислениеУдержание = Перечисления.ВидыОсобыхНачисленийИУдержаний.МатериальнаяВыгодаПоЗаймам
					Или Строка.НачислениеУдержание = Перечисления.ВидыОсобыхНачисленийИУдержаний.НачисленоПроцентовПоЗайму Тогда
				НоваяСтрока.ГруппаНачисленияУдержанияВыплаты = Перечисления.ГруппыНачисленияУдержанияВыплаты.Справочно;
			Иначе
				НоваяСтрока.ГруппаНачисленияУдержанияВыплаты = Перечисления.ГруппыНачисленияУдержанияВыплаты.Удержано;
			КонецЕсли
			
		КонецЦикла;
		
		Движения.БухучетНачисленияУдержанияПоКонтрагентамАкционерам.Записывать = Истина;
		
	КонецЕсли;

	Если РезультатыРасчетаНДФЛ <> НеОпределено Тогда
		
		Для Каждого Строка Из РезультатыРасчетаНДФЛ Цикл
			
			НоваяСтрока = Движения.БухучетНачисленияУдержанияПоКонтрагентамАкционерам.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			
			НоваяСтрока.Период				= ПериодРегистрации;
			НоваяСтрока.Организация			= Организация;
			НоваяСтрока.ВидОперации			= НачислениеУдержаниеВидОперации[Строка.ВидУдержания];
			НоваяСтрока.ТерриторияВыполненияРаботВОрганизации 	= Строка.Подразделение;
			НоваяСтрока.ГруппаНачисленияУдержанияВыплаты 		= Перечисления.ГруппыНачисленияУдержанияВыплаты.Удержано;
			НоваяСтрока.НачислениеУдержание 					= Строка.ВидУдержания;
			
		КонецЦикла;
		
		Движения.БухучетНачисленияУдержанияПоКонтрагентамАкционерам.Записывать = Истина;
		
	КонецЕсли;
	
	Если ЗаписыватьДвижения Тогда
		Движения.БухучетНачисленияУдержанияПоКонтрагентамАкционерам.Записать();
		Движения.БухучетНачисленияУдержанияПоКонтрагентамАкционерам.Записывать = Ложь;
	КонецЕсли;
	
КонецПроцедуры

// Добавляет запись о бухучете зарплаты по документу основанию.
//
Процедура ЗарегистрироватьБухучетЗарплатыПервичныхДокументов(БухучетЗарплаты) Экспорт
	
	НаборЗаписей = РегистрыСведений.БухучетЗарплатыПервичныхДокументов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ДокументОснование.Установить(БухучетЗарплаты.ДокументОснование);
	
	Для каждого СтрокаТаблицы Из БухучетЗарплаты.ТаблицаБухучетЗарплаты Цикл
		НоваяЗапись = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, СтрокаТаблицы);
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Добавляет в коллекцию движений запись о бухучете планового удержания.
// 
// Параметры:
//			Движения,
//			БухучетПлановыхУдержаний - Коллекция записей c полями.
//
Процедура СформироватьДвиженияБухучетПлановыхУдержаний(Движения, БухучетПлановыхУдержаний) Экспорт
		
	Для каждого Строка Из БухучетПлановыхУдержаний Цикл
		НоваяЗапись = Движения.БухучетПлановыхУдержаний.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, Строка);
		Движения.БухучетПлановыхУдержаний.Записывать = Истина;
	КонецЦикла;
	
	Если Движения.БухучетПлановыхУдержаний.Записывать Тогда
		Движения.БухучетПлановыхУдержаний.Записать();
		Движения.БухучетПлановыхУдержаний.Записывать = Ложь;
	КонецЕсли;
	
КонецПроцедуры

// Заполняет данные для проведения плановых удержаний для многосотрудникового документа.
//
// Параметры:
//		ДанныеДляПроведения - Структура, описанная в СоздатьДанныеДляРегистрацииПлановыхУдержаний.
//		Документ - Ссылка на документ.
//
Функция ДанныеДляРегистрацииБухучетаПлановыхУдержаний(ДокументСсылка) Экспорт
	
	МетаданныеДокумента = ДокументСсылка.Метаданные();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Документ", ДокументСсылка);
	Запрос.УстановитьПараметр("ПустойДокументОснование", Документы[МетаданныеДокумента.Имя].ПустаяСсылка());
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДокументУдержания.Ссылка.Организация КАК Организация,
	|	ДокументУдержания.ФизическоеЛицо,
	|	ДокументУдержания.Ссылка.ДатаНачала КАК Период,
	|	ВЫБОР
	|		КОГДА ДокументУдержания.Ссылка.ДатаОкончания > ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ДОБАВИТЬКДАТЕ(ДокументУдержания.Ссылка.ДатаОкончания, ДЕНЬ, 1)
	|		ИНАЧЕ ДокументУдержания.Ссылка.ДатаОкончания
	|	КОНЕЦ КАК ДействуетДо,
	|	ДокументУдержания.Ссылка.Удержание,
	|	ВЫБОР
	|		КОГДА ДокументУдержания.Ссылка.ДокументОснование = &ПустойДокументОснование
	|			ТОГДА ДокументУдержания.Ссылка
	|		ИНАЧЕ ДокументУдержания.Ссылка.ДокументОснование
	|	КОНЕЦ КАК ДокументОснование,
	|	ДокументУдержания.Ссылка.СтатьяФинансирования,
	|	ДокументУдержания.Ссылка.СтатьяРасходов
	|ПОМЕСТИТЬ ВТДанныеДокумента
	|ИЗ
	|	#ТаблицаУдержанийДокумента КАК ДокументУдержания
	|ГДЕ
	|	ДокументУдержания.Ссылка = &Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокумента.ФизическоеЛицо,
	|	ДанныеДокумента.ДокументОснование,
	|	ДанныеДокумента.Период,
	|	МАКСИМУМ(БухучетПлановыхУдержаний.Период) КАК ПериодРегистра
	|ПОМЕСТИТЬ ВТМаксимальныеПериоды
	|ИЗ
	|	ВТДанныеДокумента КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БухучетПлановыхУдержаний КАК БухучетПлановыхУдержаний
	|		ПО ДанныеДокумента.ДокументОснование = БухучетПлановыхУдержаний.ДокументОснование
	|			И ДанныеДокумента.ФизическоеЛицо = БухучетПлановыхУдержаний.ФизическоеЛицо
	|			И ДанныеДокумента.Период > БухучетПлановыхУдержаний.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокумента.ФизическоеЛицо,
	|	ДанныеДокумента.ДокументОснование,
	|	ДанныеДокумента.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МаксимальныеПериоды.ФизическоеЛицо,
	|	МаксимальныеПериоды.Период,
	|	ВЫБОР
	|		КОГДА БухучетПлановыхУдержаний.ДействуетДо <> ДАТАВРЕМЯ(1, 1, 1)
	|				И БухучетПлановыхУдержаний.ДействуетДо < МаксимальныеПериоды.Период
	|			ТОГДА БухучетПлановыхУдержаний.СтатьяФинансированияПоОкончании
	|		ИНАЧЕ БухучетПлановыхУдержаний.СтатьяФинансирования
	|	КОНЕЦ КАК СтатьяФинансирования,
	|	ВЫБОР
	|		КОГДА БухучетПлановыхУдержаний.ДействуетДо <> ДАТАВРЕМЯ(1, 1, 1)
	|				И БухучетПлановыхУдержаний.ДействуетДо < МаксимальныеПериоды.Период
	|			ТОГДА БухучетПлановыхУдержаний.СтатьяРасходовПоОкончании
	|		ИНАЧЕ БухучетПлановыхУдержаний.СтатьяРасходов
	|	КОНЕЦ КАК СтатьяРасходов
	|ПОМЕСТИТЬ ВТЗначенияПоОкончании
	|ИЗ
	|	ВТМаксимальныеПериоды КАК МаксимальныеПериоды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БухучетПлановыхУдержаний КАК БухучетПлановыхУдержаний
	|		ПО МаксимальныеПериоды.ФизическоеЛицо = БухучетПлановыхУдержаний.ФизическоеЛицо
	|			И МаксимальныеПериоды.ДокументОснование = БухучетПлановыхУдержаний.ДокументОснование
	|			И МаксимальныеПериоды.ПериодРегистра = БухучетПлановыхУдержаний.Период
	|ГДЕ
	|	ВЫБОР
	|			КОГДА БухучетПлановыхУдержаний.ДействуетДо <> ДАТАВРЕМЯ(1, 1, 1)
	|					И БухучетПлановыхУдержаний.ДействуетДо < МаксимальныеПериоды.Период
	|				ТОГДА БухучетПлановыхУдержаний.ИспользуетсяПоОкончании
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.ФизическоеЛицо,
	|	ДанныеДокумента.Период,
	|	ДанныеДокумента.ДействуетДо,
	|	ДанныеДокумента.Удержание,
	|	ДанныеДокумента.ДокументОснование,
	|	ДанныеДокумента.СтатьяФинансирования,
	|	ДанныеДокумента.СтатьяРасходов,
	|	ЗначенияПоОкончании.СтатьяФинансирования КАК СтатьяФинансированияПоОкончании,
	|	ЗначенияПоОкончании.СтатьяРасходов КАК СтатьяРасходовПоОкончании,
	|	ВЫБОР
	|		КОГДА ЗначенияПоОкончании.СтатьяФинансирования ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ИспользуетсяПоОкончании
	|ИЗ
	|	ВТДанныеДокумента КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЗначенияПоОкончании КАК ЗначенияПоОкончании
	|		ПО ДанныеДокумента.ФизическоеЛицо = ЗначенияПоОкончании.ФизическоеЛицо
	|			И ДанныеДокумента.Период = ЗначенияПоОкончании.Период";
	
	ИмяДокумента = МетаданныеДокумента.ПолноеИмя();
	Если МетаданныеДокумента.ТабличныеЧасти.Найти("Удержания") = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ДокументУдержания.Ссылка.", "ДокументУдержания.");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ТаблицаУдержанийДокумента", ИмяДокумента);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ТаблицаУдержанийДокумента", ИмяДокумента + ".Удержания");
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Процедура СформироватьДвиженияПоДокументу(Движения, Отказ, Организация, ПериодРегистрации, ДанныеДляПроведения, СтрокаСписокТаблиц) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтрНайти(СтрокаСписокТаблиц, "НачисленныйНДФЛ") > 0 Тогда
		
		Если ДанныеДляПроведения.НачисленияУдержания = Неопределено Или ДанныеДляПроведения.НачисленияУдержания.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		ВидыОсобыхНачисленийИУдержанийНДФЛ = ОтражениеЗарплатыВУчете.ВидыОсобыхНачисленийИУдержанийНДФЛ();
		
		ВыделятьНДФЛСПревышения = (ПериодРегистрации < УчетНДФЛ.ДатаЗакона176ФЗ());
		НачислениеУдержаниеВидОперации = ОтражениеЗарплатыВУчете.НачислениеУдержаниеВидОперации(ВыделятьНДФЛСПревышения);
		
		Для каждого СтрокаТЗ Из ДанныеДляПроведения.НачисленияУдержания Цикл
		
			Если ВидыОсобыхНачисленийИУдержанийНДФЛ.Найти(СтрокаТЗ.НачислениеУдержание) <> Неопределено Тогда
				 
				Движения.БухучетНачисленияУдержанияПоСотрудникам.Записывать = Истина;
				
				НоваяСтрока = Движения.БухучетНачисленияУдержанияПоСотрудникам.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
				НоваяСтрока.ВидОперации = НачислениеУдержаниеВидОперации[НоваяСтрока.НачислениеУдержание];
				
			КонецЕсли;
		
		КонецЦикла;
	
	КонецЕсли; 

КонецПроцедуры

Процедура ДанныеДляПроведенияСоздатьВТНачисленияСтраховыеВзносы(МенеджерВременныхТаблиц, РеквизитыДляПроведения, ИмяВТНачисленияСтраховыеВзносы) Экспорт

	Организация = РеквизитыДляПроведения.Организация;
	ПериодРегистрации = РеквизитыДляПроведения.МесяцНачисления;
	
	УдалитьВТ = Новый Массив;
	
	Если ПолучитьФункциональнуюОпцию("РаботаВХозрасчетнойОрганизации") Тогда
		ОписаниеСтатейРасходов = ЗарплатаКадры.СтатьиРасходовПоСпособамРасчетовСФизическимиЛицами();
		ОплатаТруда = ОписаниеСтатейРасходов[Перечисления.СпособыРасчетовСФизическимиЛицами.ОплатаТруда];
		РасчетыСКонтрагентами = ОписаниеСтатейРасходов[Перечисления.СпособыРасчетовСФизическимиЛицами.РасчетыСКонтрагентами];
	Иначе
		ОплатаТруда = Справочники.СтатьиРасходовЗарплата.ПустаяСсылка();
		РасчетыСКонтрагентами = Справочники.СтатьиРасходовЗарплата.ПустаяСсылка();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("МесяцНачисления", ПериодРегистрации);
	Запрос.УстановитьПараметр("ОплатаТруда", ОплатаТруда);
	Запрос.УстановитьПараметр("РасчетыСКонтрагентами", РасчетыСКонтрагентами);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НачисленийПоДоговорам.Договор КАК Договор,
	|	МАКСИМУМ(БухучетДоговоровГПХ.СтатьяРасходов) КАК СтатьяРасходов
	|ПОМЕСТИТЬ ВТСтатьиРасходовДоговоров
	|ИЗ
	|	ВТЗаписиНачисленийПоДоговорам КАК НачисленийПоДоговорам
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.БухучетДоговоровГПХ КАК БухучетДоговоровГПХ
	|		ПО НачисленийПоДоговорам.Договор = БухучетДоговоровГПХ.ДоговорАкт
	|
	|СГРУППИРОВАТЬ ПО
	|	НачисленийПоДоговорам.Договор";
	Запрос.Выполнить();
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НачисленийПоДоговорам.Договор КАК Договор,
	|	ЕСТЬNULL(ПлановыеНачисленияПоДоговорам.КодДоходаСтраховыеВзносы, ЕСТЬNULL(УсловияДоговораГПХ.КодДоходаСтраховыеВзносы, УсловияДоговораОпеки.КодДоходаСтраховыеВзносы)) КАК КодДоходаСтраховыеВзносы,
	|	ЕСТЬNULL(ПлановыеНачисленияПоДоговорам.ЗаключенСоСтудентомРаботающимВСтудотряде, УсловияДоговораГПХ.ЗаключенСоСтудентомРаботающимВСтудотряде) КАК ЗаключенСоСтудентомРаботающимВСтудотряде
	|ПОМЕСТИТЬ ВТДанныеДоговоров
	|ИЗ
	|	ВТЗаписиНачисленийПоДоговорам КАК НачисленийПоДоговорам
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПлановыеНачисленияПоДоговорам КАК ПлановыеНачисленияПоДоговорам
	|		ПО НачисленийПоДоговорам.Договор = ПлановыеНачисленияПоДоговорам.ДоговорАкт
	|			И (ПлановыеНачисленияПоДоговорам.МесяцНачисления = &МесяцНачисления)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УсловияДоговораГПХ КАК УсловияДоговораГПХ
	|		ПО НачисленийПоДоговорам.Договор = УсловияДоговораГПХ.Договор
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УсловияДоговораОпеки КАК УсловияДоговораОпеки
	|		ПО НачисленийПоДоговорам.Договор = УсловияДоговораОпеки.Договор";
	Запрос.Выполнить();
	УдалитьВТ.Добавить("ВТДанныеДоговоров");
	
	ПрименяетсяЕНВД = ОтражениеЗарплатыВБухучете.ПлательщикЕНВД(Организация, ПериодРегистрации);
	Если Не ПрименяетсяЕНВД Тогда
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗаписиНачисленийПоДоговорам.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	ЗаписиНачисленийПоДоговорам.Сотрудник КАК Сотрудник,
		|	ЗаписиНачисленийПоДоговорам.Подразделение КАК Подразделение,
		|	ЗаписиНачисленийПоДоговорам.Начисление КАК Начисление,
		|	ЗаписиНачисленийПоДоговорам.Сумма КАК СуммаДохода,
		|	ЗаписиНачисленийПоДоговорам.СкидкаПоВзносам КАК СуммаВычетаВзносы,
		|	ЗаписиНачисленийПоДоговорам.ДатаНачала КАК ДатаНачала,
		|	ЛОЖЬ КАК ОблагаетсяЕНВД,
		|	ЕСТЬNULL(ДанныеДоговоров.КодДоходаСтраховыеВзносы, ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.ДоговорыГПХ)) КАК КодДоходаСтраховыеВзносы,
		|	ЕСТЬNULL(ДанныеДоговоров.ЗаключенСоСтудентомРаботающимВСтудотряде, ЛОЖЬ) КАК ЗаключенСоСтудентомРаботающимВСтудотряде
		|ПОМЕСТИТЬ ВТНачисленияСтраховыеВзносы
		|ИЗ
		|	ВТЗаписиНачисленийПоДоговорам КАК ЗаписиНачисленийПоДоговорам
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеДоговоров КАК ДанныеДоговоров
		|		ПО ЗаписиНачисленийПоДоговорам.Договор = ДанныеДоговоров.Договор";
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияСтраховыеВзносы", ИмяВТНачисленияСтраховыеВзносы);
		Запрос.Выполнить();
		
	Иначе
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
			
			// Сведения о бухучете договоров есть во временной таблице ВТНачисленияПоДоговорамСРаспределением.
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	НачисленияПоДоговорамСРаспределением.Договор,
			|	НачисленияПоДоговорамСРаспределением.ОблагаетсяЕНВД,
			|	СУММА(НачисленияПоДоговорамСРаспределением.Сумма) КАК Сумма
			|ПОМЕСТИТЬ ВТНачисленияСЕНВД
			|ИЗ
			|	ВТНачисленияПоДоговорамСРаспределением КАК НачисленияПоДоговорамСРаспределением
			|
			|СГРУППИРОВАТЬ ПО
			|	НачисленияПоДоговорамСРаспределением.Договор,
			|	НачисленияПоДоговорамСРаспределением.ОблагаетсяЕНВД";
			Запрос.Выполнить();
			УдалитьВТ.Добавить("ВТНачисленияСЕНВД");
			
		Иначе
			
			ОтражениеЗарплатыВУчете.СоздатьВТНачислениеУдержаниеВидОперации(МенеджерВременныхТаблиц);
			// Поле ТерриторияВыполненияРаботВОрганизации имеет пустое значение, для вычисления доли ЕНВД это допустимо.
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ЗаписиНачисленийПоДоговорам.Организация КАК Организация,
			|	ЗаписиНачисленийПоДоговорам.ПериодРегистрации КАК ПериодРегистрации,
			|	ЗаписиНачисленийПоДоговорам.ДокументСсылка КАК ДокументСсылка,
			|	АВТОНОМЕРЗАПИСИ() КАК ИдентификаторСтроки,
			|	ЗаписиНачисленийПоДоговорам.ФизическоеЛицо КАК ФизическоеЛицо,
			|	ЗаписиНачисленийПоДоговорам.Сотрудник КАК Сотрудник,
			|	ЗаписиНачисленийПоДоговорам.Подразделение КАК Подразделение,
			|	ЗаписиНачисленийПоДоговорам.Начисление КАК Начисление,
			|	ЗаписиНачисленийПоДоговорам.Сумма КАК Сумма,
			|	ЗаписиНачисленийПоДоговорам.СкидкаПоВзносам КАК СкидкаПоВзносам,
			|	ЗаписиНачисленийПоДоговорам.КодДохода КАК КодДохода,
			|	ЗаписиНачисленийПоДоговорам.КодВычета КАК КодВычета,
			|	ЗаписиНачисленийПоДоговорам.Договор КАК Договор,
			|	ЗаписиНачисленийПоДоговорам.ДокументОснование КАК ДокументОснование,
			|	ЗаписиНачисленийПоДоговорам.ДатаНачала КАК ДатаНачала,
			|	ЗаписиНачисленийПоДоговорам.ДатаОкончания КАК ДатаОкончания,
			|	ЗаписиНачисленийПоДоговорам.ПланируемаяДатаВыплаты КАК ПланируемаяДатаВыплаты,
			|	ЗНАЧЕНИЕ(Справочник.ТерриторииВыполненияРабот.ПустаяСсылка) КАК ТерриторияВыполненияРаботВОрганизации,
			|	ЕСТЬNULL(НачисленияВидОперации.ВидОперации, ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ПустаяСсылка)) КАК ВидОперации
			|ПОМЕСТИТЬ ВТНачисленияПоДоговорамГПХ
			|ИЗ
			|	ВТЗаписиНачисленийПоДоговорам КАК ЗаписиНачисленийПоДоговорам
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачислениеУдержаниеВидОперации КАК НачисленияВидОперации
			|		ПО ЗаписиНачисленийПоДоговорам.Начисление = НачисленияВидОперации.НачислениеУдержание";
			Запрос.Выполнить();
			УдалитьВТ.Добавить("ВТНачисленияПоДоговорамГПХ");
			
			ИсходныеДанные = ОписаниеИсходныхДанныхДляОтраженияНачисленийВБухучете();
			ИсходныеДанные.МенеджерВременныхТаблиц 		= МенеджерВременныхТаблиц;
			ИсходныеДанные.ИмяВТНачисления		 		= "ВТНачисленияПоДоговорамГПХ";
			ИсходныеДанные.МесяцНачисления 				= ПериодРегистрации;
			ИсходныеДанные.Организация 					= Организация;
			ПолучитьБухучетНачисленийПоДоговорамСоздатьВТ(ИсходныеДанные, "ВТБухучетНачисленийПоДоговорам");
			УдалитьВТ.Добавить("ВТБухучетНачисленийПоДоговорам");
			
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	НачисленияПоДоговорамСРаспределением.ДокументОснование КАК Договор,
			|	НачисленияПоДоговорамСРаспределением.ОблагаетсяЕНВД,
			|	СУММА(НачисленияПоДоговорамСРаспределением.Сумма) КАК Сумма
			|ПОМЕСТИТЬ ВТНачисленияСЕНВД
			|ИЗ
			|	ВТБухучетНачисленийПоДоговорам КАК НачисленияПоДоговорамСРаспределением
			|
			|СГРУППИРОВАТЬ ПО
			|	НачисленияПоДоговорамСРаспределением.ДокументОснование,
			|	НачисленияПоДоговорамСРаспределением.ОблагаетсяЕНВД";
			Запрос.Выполнить();
			УдалитьВТ.Добавить("ВТНачисленияСЕНВД");
			
		КонецЕсли;
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗаписиНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	ЗаписиНачислений.Сотрудник КАК Сотрудник,
		|	ЗаписиНачислений.Подразделение КАК Подразделение,
		|	ЗаписиНачислений.Начисление КАК Начисление,
		|	ЗаписиНачислений.ДатаНачала КАК ДатаНачала,
		|	ЕСТЬNULL(НачисленияСЕНВД.ОблагаетсяЕНВД, ЛОЖЬ) КАК ОблагаетсяЕНВД,
		|	ЕСТЬNULL(НачисленияСЕНВД.Сумма, 0) КАК СуммаДохода,
		|	ВЫБОР
		|		КОГДА НачисленияСЕНВД.ОблагаетсяЕНВД ЕСТЬ NULL
		|			ТОГДА 0
		|		КОГДА ЗаписиНачислений.Сумма = 0
		|			ТОГДА 0
		|		КОГДА ЗаписиНачислений.Сумма = НачисленияСЕНВД.Сумма
		|			ТОГДА ЗаписиНачислений.СкидкаПоВзносам
		|		КОГДА НачисленияСЕНВД.ОблагаетсяЕНВД
		|			ТОГДА ВЫРАЗИТЬ(ЗаписиНачислений.СкидкаПоВзносам * (ВЫРАЗИТЬ(НачисленияСЕНВД.Сумма / ЗаписиНачислений.Сумма КАК ЧИСЛО(25, 10))) КАК ЧИСЛО(15, 2))
		|		ИНАЧЕ ЗаписиНачислений.СкидкаПоВзносам - (ВЫРАЗИТЬ(ЗаписиНачислений.СкидкаПоВзносам * (ВЫРАЗИТЬ((ЗаписиНачислений.Сумма - НачисленияСЕНВД.Сумма) / ЗаписиНачислений.Сумма КАК ЧИСЛО(25, 10))) КАК ЧИСЛО(15, 2)))
		|	КОНЕЦ КАК СуммаВычетаВзносы,
		|	ЕСТЬNULL(ДанныеДоговоров.КодДоходаСтраховыеВзносы, ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.ДоговорыГПХ)) КАК КодДоходаСтраховыеВзносы,
		|	ЕСТЬNULL(ДанныеДоговоров.ЗаключенСоСтудентомРаботающимВСтудотряде, ЛОЖЬ) КАК ЗаключенСоСтудентомРаботающимВСтудотряде
		|ПОМЕСТИТЬ ВТНачисленияСтраховыеВзносы
		|ИЗ
		|	ВТЗаписиНачисленийПоДоговорам КАК ЗаписиНачислений
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачисленияСЕНВД КАК НачисленияСЕНВД
		|		ПО ЗаписиНачислений.Договор = НачисленияСЕНВД.Договор
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеДоговоров КАК ДанныеДоговоров
		|		ПО ЗаписиНачислений.Договор = ДанныеДоговоров.Договор";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияСтраховыеВзносы", ИмяВТНачисленияСтраховыеВзносы);
		Запрос.Выполнить();
		
	КонецЕсли;
	
	ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
	
КонецПроцедуры

Процедура СформироватьДвиженияКоэффициентыРаспределенияСреднегоЗаработка(Движения, КоэффициентыРаспределенияСреднегоЗаработка) Экспорт

	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата") Тогда
		Возврат;
	КонецЕсли;
	
	Если КоэффициентыРаспределенияСреднегоЗаработка.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого СтрокаТЗ Из КоэффициентыРаспределенияСреднегоЗаработка Цикл
		НоваяЗапись = Движения.КоэффициентыРаспределенияСреднегоЗаработка.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, СтрокаТЗ);
		Движения.КоэффициентыРаспределенияСреднегоЗаработка.Записывать = Истина;
	КонецЦикла;

КонецПроцедуры

Функция ПараметрыРегистрацииБухучетаСотрудниковСписком() Экспорт

	ПараметрыОбновленияБухучета = Новый Структура("
	|НеРегистрироватьБухучет,
	|ИмяТаблицы,
	|ИмяПоляПериод,
	|ИмяПоляДействуетДо,
	|ИмяПоляРегистрироватьБухучет,
	|НетПоляЕНВД");
	
	ПараметрыОбновленияБухучета.НетПоляЕНВД = Истина;
	
	Возврат ПараметрыОбновленияБухучета;
	
КонецФункции

Процедура СформироватьДвиженияКоэффициентыРаспределенияДенежногоСодержания(Движения, КоэффициентыРаспределенияДенежногоСодержания) Экспорт

	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба.РасчетДенежногоСодержания") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("РасчетДенежногоСодержания");
		Модуль.СформироватьДвиженияКоэффициентыРаспределенияДенежногоСодержания(Движения, КоэффициентыРаспределенияДенежногоСодержания);
	КонецЕсли;
	
КонецПроцедуры

// Формирует движения по регистрам подсистемы.
//
// Параметры:
//		ДанныеДляПроведения
//		Начисления - регистрируемые начисления.
//		Удержания  - регистрируемые удержания.
//		РезультатыРасчетаНДФЛ - таблица с НДФЛ.
//		ЗаписыватьДвижения - тип Булево, признак того, что движения необходимо записать
//
Процедура ЗарегистрироватьНачисленияУдержания(ДанныеДляПроведения, Отказ, Начисления, Удержания, РезультатыРасчетаНДФЛ, ЗаписыватьДвижения = Ложь) Экспорт

	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата") Тогда
		Возврат;
	КонецЕсли;
	
	Движения 				= ДанныеДляПроведения.Движения;
	Организация 			= ДанныеДляПроведения.Организация;
	ПериодРегистрации 		= ДанныеДляПроведения.ПериодРегистрации;
	ПланируемаяДатаВыплаты 	= ДанныеДляПроведения.ПланируемаяДатаВыплаты;
	ПорядокВыплаты 			= ДанныеДляПроведения.ПорядокВыплаты;
	ВыплатитьКакАванс 		= ДанныеДляПроведения.ВыплатитьКакАванс;
	ДниОтпускаАвансом 		= ДанныеДляПроведения.ДниОтпускаАвансом;
	
	ДанныеМежрасчетногоПериода = ?(ПорядокВыплаты = Неопределено, Ложь, РасчетЗарплатыРасширенный.ЭтоМежрасчетнаяВыплата(ПорядокВыплаты));
	
	ВыделятьНДФЛСПревышения = (ПериодРегистрации < УчетНДФЛ.ДатаЗакона176ФЗ());
	НачислениеУдержаниеВидОперации = ОтражениеЗарплатыВУчете.НачислениеУдержаниеВидОперации(ВыделятьНДФЛСПревышения);
	
	Если Начисления <> НеОпределено Тогда
		
		ВидыДоходаИсполнительногоПроизводства = УчетНачисленнойЗарплаты.ВидыДоходовИсполнительногоПроизводстваНачислений(ПланируемаяДатаВыплаты);
		
		ВыделятьДниОтпускаАвансом = ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") И ДниОтпускаАвансом <> Неопределено И ДниОтпускаАвансом.Количество() > 0;
		
		Если ВыделятьДниОтпускаАвансом Тогда
			ТаблицаНачислений = Начисления.Скопировать();
			ЗаполнитьВидОперацииВНачислениях(ТаблицаНачислений, НачислениеУдержаниеВидОперации, ДниОтпускаАвансом);
		Иначе
			ТаблицаНачислений = Начисления;
		КонецЕсли;
		
		Для Каждого Строка Из ТаблицаНачислений Цикл
			
			НоваяСтрока = Движения.БухучетНачисленияУдержанияПоСотрудникам.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			
			НоваяСтрока.Период				= ПериодРегистрации;
			НоваяСтрока.НачислениеУдержание	= Строка.Начисление;
			НоваяСтрока.Организация			= Организация;
			НоваяСтрока.ПериодДействия		= НачалоМесяца(НоваяСтрока.ДатаНачала);
			НоваяСтрока.ДанныеМежрасчетногоПериода  = ДанныеМежрасчетногоПериода;
			НоваяСтрока.УчитыватьВРаспределенииНДФЛ = ВыплатитьКакАванс;
			Если Не ВыделятьДниОтпускаАвансом Тогда
				НоваяСтрока.ВидОперации = НачислениеУдержаниеВидОперации[Строка.Начисление];
			КонецЕсли;
			
			НоваяСтрока.ГруппаНачисленияУдержанияВыплаты = Перечисления.ГруппыНачисленияУдержанияВыплаты.Начислено;
			Если Не ЗначениеЗаполнено(НоваяСтрока.ПодразделениеУчетаЗатрат) Тогда
				НоваяСтрока.ПодразделениеУчетаЗатрат = НоваяСтрока.Подразделение;
			КонецЕсли;
			
			НоваяСтрока.ВидДоходаИсполнительногоПроизводства = ВидыДоходаИсполнительногоПроизводства[Строка.Начисление];
			
		КонецЦикла;
		
		Движения.БухучетНачисленияУдержанияПоСотрудникам.Записывать = Истина;
		
	КонецЕсли;
	
	Если Удержания <> НеОпределено Тогда
		
		Для Каждого Строка Из Удержания Цикл
			
			НоваяСтрока = Движения.БухучетНачисленияУдержанияПоСотрудникам.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			
			НоваяСтрока.Период				= ПериодРегистрации;
			НоваяСтрока.НачислениеУдержание = Строка.Удержание;
			НоваяСтрока.Организация			= Организация;
			НоваяСтрока.ПериодДействия		= ПериодРегистрации;
			НоваяСтрока.ВидОперации			= НачислениеУдержаниеВидОперации[НоваяСтрока.НачислениеУдержание];
			НоваяСтрока.ДанныеМежрасчетногоПериода = ДанныеМежрасчетногоПериода;
			
			Если Строка.Удержание = Перечисления.ВидыОсобыхНачисленийИУдержаний.МатериальнаяВыгодаПоЗаймам
					Или Строка.Удержание = Перечисления.ВидыОсобыхНачисленийИУдержаний.НачисленоПроцентовПоЗайму Тогда
				НоваяСтрока.ГруппаНачисленияУдержанияВыплаты = Перечисления.ГруппыНачисленияУдержанияВыплаты.Справочно;
			Иначе
				НоваяСтрока.ГруппаНачисленияУдержанияВыплаты = Перечисления.ГруппыНачисленияУдержанияВыплаты.Удержано;
			КонецЕсли
			
		КонецЦикла;
		
		Движения.БухучетНачисленияУдержанияПоСотрудникам.Записывать = Истина;
		
	КонецЕсли;

	Если РезультатыРасчетаНДФЛ <> НеОпределено Тогда
		
		Для Каждого Строка Из РезультатыРасчетаНДФЛ Цикл
			
			НоваяСтрока = Движения.БухучетНачисленияУдержанияПоСотрудникам.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			
			НоваяСтрока.Подразделение							= Строка.ПодразделениеСотрудника;
			НоваяСтрока.ТерриторияВыполненияРаботВОрганизации 	= Строка.Подразделение;
			
			НоваяСтрока.Период				= ПериодРегистрации;
			НоваяСтрока.ПериодДействия		= ПериодРегистрации;
			НоваяСтрока.НачислениеУдержание = Строка.ВидУдержания;
			НоваяСтрока.Организация			= Организация;
			НоваяСтрока.ВидОперации			= НачислениеУдержаниеВидОперации[НоваяСтрока.НачислениеУдержание];
			НоваяСтрока.ДанныеМежрасчетногоПериода = ДанныеМежрасчетногоПериода;
			НоваяСтрока.ДатаПолученияДохода = Строка.МесяцНалоговогоПериода;
			
			НоваяСтрока.ГруппаНачисленияУдержанияВыплаты = Перечисления.ГруппыНачисленияУдержанияВыплаты.Удержано;
			
		КонецЦикла;
		
		Движения.БухучетНачисленияУдержанияПоСотрудникам.Записывать = Истина;
		
	КонецЕсли;
	
	Если ЗаписыватьДвижения Тогда
		Движения.БухучетНачисленияУдержанияПоСотрудникам.Записать();
		Движения.БухучетНачисленияУдержанияПоСотрудникам.Записывать = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьВидОперацииВНачислениях(Начисления, НачислениеУдержаниеВидОперации, ДниОтпускаАвансом)

	Начисления.Колонки.Добавить("ВидОперации", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыОперацийПоЗарплате"));
	
	НачисленияОтпуска = Начисления.СкопироватьКолонки();
	ПрочиеНачисления  = Начисления.СкопироватьКолонки();
	
	Отбор = Новый Структура("Начисление,Сторно");
	
	Для каждого СтрокаТЗ Из Начисления Цикл
		
		ЗаполнитьЗначенияСвойств(Отбор, СтрокаТЗ);
		
		НайденныеСтроки = ДниОтпускаАвансом.НайтиСтроки(Отбор);
		Если НайденныеСтроки.Количество() > 0 Тогда
			ЗаполнитьЗначенияСвойств(НачисленияОтпуска.Добавить(), СтрокаТЗ);
		Иначе
			НоваяСтрока = ПрочиеНачисления.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
			НоваяСтрока.ВидОперации = НачислениеУдержаниеВидОперации[СтрокаТЗ.Начисление];
		КонецЕсли;
		
	КонецЦикла;
	
	Начисления = ПрочиеНачисления;
	
	Если НачисленияОтпуска.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
		
	Отбор = Новый Структура("Начисление,Сторно");
	ОтборПоПериоду = Новый Структура("Начисление,Сторно,ДатаНачала");
	
	Для каждого СтрокаТЗ Из ДниОтпускаАвансом Цикл
		
		ЗаполнитьЗначенияСвойств(Отбор, СтрокаТЗ);
		
		СтрокиОтпуска = НачисленияОтпуска.Скопировать(Отбор);
		СтрокиОтпуска.Свернуть("ДатаНачала,ОплаченоДней");
		СтрокиОтпуска.Сортировать("ДатаНачала Убыв");
		
		ОсталосьДнейАвансом = СтрокаТЗ.КоличествоДнейАвансом;
		Для каждого СтрокаОтпуска Из СтрокиОтпуска Цикл
			
			ОплаченоДней = ?(СтрокаТЗ.Сторно, -СтрокаОтпуска.ОплаченоДней, СтрокаОтпуска.ОплаченоДней);
			
			Если ОсталосьДнейАвансом = 0 Тогда
				ДоляАванса = 0;
			ИначеЕсли ОплаченоДней <= ОсталосьДнейАвансом Тогда
				ОсталосьДнейАвансом = ОсталосьДнейАвансом - ОплаченоДней;
				ДоляАванса = 1;
			Иначе
				ДоляАванса = ОсталосьДнейАвансом / ОплаченоДней;
				ОсталосьДнейАвансом = 0;
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(ОтборПоПериоду, СтрокаТЗ);
			ОтборПоПериоду.ДатаНачала = СтрокаОтпуска.ДатаНачала;
			
			СтрокиДляРаспределения = НачисленияОтпуска.Скопировать(ОтборПоПериоду);
			
			Для каждого СтрокаРаспределения Из СтрокиДляРаспределения Цикл
				
				НоваяСтрока = Начисления.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРаспределения);
				Если ДоляАванса = 0 Тогда
					НоваяСтрока.ВидОперации = НачислениеУдержаниеВидОперации[Отбор.Начисление];
				ИначеЕсли ДоляАванса = 1 Тогда
					НоваяСтрока.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпускАвансом;
				Иначе
					
					НоваяСтрока.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпускАвансом;
					СуммаАвансом = Окр(НоваяСтрока.Сумма * ДоляАванса, 2);
					НоваяСтрока.Сумма = СуммаАвансом;
					
					НоваяСтрока = Начисления.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРаспределения);
					НоваяСтрока.ВидОперации = НачислениеУдержаниеВидОперации[Отбор.Начисление];
					НоваяСтрока.Сумма = НоваяСтрока.Сумма - СуммаАвансом;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
		
КонецПроцедуры

// Формирует сторно записи отменяющие движения исправленного документа по регистрам подсистемы.
//
// Параметры:
//  Движения				 - КоллекцияДвижений, Структура	 - Коллекция движений в которую будут добавлены сторно записи.
//  ИсправленныйДокумент	 - ДокументСсылка				 - Документ, записи которого необходимо сторнировать.
//  Записывать				 - Булево						 - Если Истина, то наборы будут записаны сразу, если Ложь, то наборам будет установлен признак Записывать = Истина.
//
Процедура СторнироватьДвиженияДокумента(Движения, ИсправленныйДокумент, Записывать = Ложь) Экспорт
	
	ТолькоИзолироватьНаборы = Не ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата");
	
	ИмяУчета = "ОтражениеЗарплатыВБухучете";
	МетаданныеРегистров = МетаданныеРегистровПодсистемы();
	
	ДвиженияВСтруктуре = ТипЗнч(Движения) = Тип("Структура");
	Набор = Неопределено;
	
	// Сторнирование универсальными алгоритмами.
	
	Для Каждого МетаданныеРегистра Из МетаданныеРегистров Цикл
		
		Если ДвиженияВСтруктуре Тогда 
			Движения.Свойство(МетаданныеРегистра.Имя, Набор);
		Иначе 
			Набор = Движения.Найти(МетаданныеРегистра.Имя);
		КонецЕсли;
		
		Если Набор = Неопределено Или Не ИсправлениеДокументовЗарплатаКадры.ИзолироватьУчетом(Набор, ИмяУчета) Или ТолькоИзолироватьНаборы Тогда
			Продолжить;
		КонецЕсли;
		
		Если ОбщегоНазначения.ЭтоРегистрНакопления(МетаданныеРегистра) Тогда
			ИсправлениеДокументовЗарплатаКадры.СторнироватьДвиженияВРегистреНакопления(Набор, ИсправленныйДокумент, МетаданныеРегистра, Записывать);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗарегистрироватьБухучетРабочегоВремениСотрудников(Движения, ПериодРегистрации, ДокументОснование, БухучетДанныхОВремени) Экспорт
	
	Если БухучетДанныхОВремени.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Строка Из БухучетДанныхОВремени Цикл
		
		Если Не ЗначениеЗаполнено(Строка.СпособОтраженияЗарплатыВБухучете)
			И Не ЗначениеЗаполнено(Строка.СтатьяФинансирования)
			И Не ЗначениеЗаполнено(Строка.СтатьяРасходов)
			И Не ЗначениеЗаполнено(Строка.ОтношениеКЕНВД) Тогда
			
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = Движения.БухучетРабочегоВремениСотрудников.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		НоваяСтрока.ПериодРегистрации 	= ПериодРегистрации;
		НоваяСтрока.ДокументОснование 	= ДокументОснование;
		НоваяСтрока.ВидУчетаВремени 	= Строка.ВидВремени;
		НоваяСтрока.Часы 				= Строка.Часов;
		
		Движения.БухучетРабочегоВремениСотрудников.Записывать = Истина;
		
	КонецЦикла;

КонецПроцедуры

Процедура ДополнитьСведенияОВзносахДаннымиБухучета(Движения, Организация, ПериодРегистрации, Ссылка, МенеджерВременныхТаблиц, ИмяВременнойТаблицы) Экспорт

	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата") Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	// Прочитаем уже зарегистрированные данные о бухучете начислений.
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ФизическиеЛица.ФизическоеЛицо
	|ИЗ
	|	ВТФизическиеЛица КАК ФизическиеЛица";
	МассивФизическихЛиц = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ФизическоеЛицо");
	
	// Получаем уже зарегистрированные сведения о бухучете начислений всех сотрудников по физическим лицам из массива МассивФизическихЛиц,
	// без учета регистратора, указанного в параметре Ссылка.
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИсключаемыйРегистратор", Ссылка);
	ДополнительныеПараметры.Вставить("ФизическиеЛица", МассивФизическихЛиц);
	БухучетНачислений = БухучетНачисленийПоСтатьям(Организация, ПериодРегистрации, ДополнительныеПараметры);
	
	БухучетНачисленияУдержанияПоСотрудникам = Неопределено;
	БухучетНачисленияУдержанияПоКонтрагентамАкционерам = Неопределено;  
	
	Если ТипЗнч(Движения) = Тип("Структура") Тогда
		Движения.Свойство("БухучетНачисленияУдержанияПоСотрудникам", БухучетНачисленияУдержанияПоСотрудникам);
		Движения.Свойство("БухучетНачисленияУдержанияПоКонтрагентамАкционерам", БухучетНачисленияУдержанияПоКонтрагентамАкционерам);
	Иначе
		БухучетНачисленияУдержанияПоСотрудникам = Движения.Найти("БухучетНачисленияУдержанияПоСотрудникам");
		БухучетНачисленияУдержанияПоКонтрагентамАкционерам = Движения.Найти("БухучетНачисленияУдержанияПоКонтрагентамАкционерам");
	КонецЕсли;
	
	Если БухучетНачисленияУдержанияПоСотрудникам <> Неопределено Тогда
		
		Для каждого СтрокаКоллекции Из БухучетНачисленияУдержанияПоСотрудникам Цикл
			
			Если ЗначениеЗаполнено(СтрокаКоллекции.Организация) И (СтрокаКоллекции.ГруппаНачисленияУдержанияВыплаты = Перечисления.ГруппыНачисленияУдержанияВыплаты.Начислено
				Или СтрокаКоллекции.ГруппаНачисленияУдержанияВыплаты = Перечисления.ГруппыНачисленияУдержанияВыплаты.Льготы)
				Или СтрокаКоллекции.ГруппаНачисленияУдержанияВыплаты = Перечисления.ГруппыНачисленияУдержанияВыплаты.Справочно
				И (СтрокаКоллекции.НачислениеУдержание = Перечисления.ВидыОсобыхНачисленийИУдержаний.СтоимостьПодарковПризов
				Или ТипЗнч(СтрокаКоллекции.НачислениеУдержание) = Тип("ПланВидовРасчетаСсылка.Начисления")) Тогда
				
				НоваяСтрока = БухучетНачислений.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаКоллекции);
				НоваяСтрока.Начисление = СтрокаКоллекции.НачислениеУдержание;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если БухучетНачисленияУдержанияПоКонтрагентамАкционерам <> Неопределено Тогда
		
		Для каждого СтрокаКоллекции Из БухучетНачисленияУдержанияПоКонтрагентамАкционерам Цикл
			Если ЗначениеЗаполнено(СтрокаКоллекции.Организация) И СтрокаКоллекции.ГруппаНачисленияУдержанияВыплаты = Перечисления.ГруппыНачисленияУдержанияВыплаты.Начислено Тогда
				НоваяСтрока = БухучетНачислений.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаКоллекции);
				НоваяСтрока.Начисление = СтрокаКоллекции.НачислениеУдержание;
				НоваяСтрока.ДатаНачала = СтрокаКоллекции.Период;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	&Организация КАК Организация, 
	|	*
	|ИЗ
	|	ВТРасширенныеСведенияОВзносах КАК СведенияОВзносах"
	 + Символы.ПС + "ГДЕ" + Символы.ПС + СтрЗаменить(УчетСтраховыхВзносов.ОтражаемыеВУчетеВзносы(Истина, "СведенияОВзносах"), ",", " <> 0 ИЛИ ") + " <> 0";
	
	ТаблицаВзносов = ОтражениеЗарплатыВУчете.ДанныеДляОтражениеВУчетеВзносовПоТаблице(Запрос.Выполнить().Выгрузить(), Неопределено);
	БухучетВзносов = ОтражениеЗарплатыВБухучете.БухучетСтраховыхВзносов(Организация, ПериодРегистрации, ТаблицаВзносов, БухучетНачислений);
	
	// Помещаем в переданный параметр имя временной таблицы.
	ИмяВременнойТаблицы = "ВТСтраховыеВзносыПоИсточникамФинансирования";
	ЗарплатаКадры.СоздатьВТПоТаблицеЗначений(МенеджерВременныхТаблиц, БухучетВзносов, ИмяВременнойТаблицы);
	
КонецПроцедуры

Процедура СформироватьДвиженияБухучетНачисленийСотрудников(РегистраторОбъект, Движения, БухучетНачислений, ФормироватьЗаписиТолькоДляИзменений = Ложь) Экспорт

	ЗаписиДобавлены = Ложь;
	ЗаписиРаспределенияДобавлены = Ложь;
	Если ЗначениеЗаполнено(БухучетНачислений) Тогда
		
		ПоляПоискаПредыдущихЗначений = "Сотрудник,Начисление,ДокументОснование";
		ПредыдущиеЗначения = Неопределено;
		Если ФормироватьЗаписиТолькоДляИзменений Тогда
			
			ВремяРегистрацииДокумента = Неопределено;
			Движения.БухучетНачисленийСотрудников.ДополнительныеСвойства.Свойство("ВремяРегистрацииДокумента", ВремяРегистрацииДокумента);
			
			Если ВремяРегистрацииДокумента = Неопределено Тогда
				СотрудникиДаты = БухучетНачислений.Скопировать(,"Сотрудник,Период");
				СотрудникиДаты.Колонки.Период.Имя = "ДатаСобытия";
				СотрудникиДаты.Свернуть("Сотрудник,ДатаСобытия");
				ВремяРегистрацииДокумента = ЗарплатаКадрыРасширенный.ЗначенияВремениРегистрацииДокумента(РегистраторОбъект.Ссылка, СотрудникиДаты);
			КонецЕсли;
			
			ТаблицаОтборов = Новый ТаблицаЗначений;
			ТаблицаОтборов.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
			ТаблицаОтборов.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
			ТаблицаОтборов.Колонки.Добавить("Начисление", Новый ОписаниеТипов("ПланВидовРасчетаСсылка.Начисления"));
			ТаблицаОтборов.Колонки.Добавить("ДокументОснование", Метаданные.ОпределяемыеТипы.РегистраторыБухучетаНачисленийСотрудников.Тип);
			
			Для каждого СтрокаТЗ Из БухучетНачислений Цикл
				НоваяСтрока = ТаблицаОтборов.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ,,"Период,ДокументОснование");
				Если ЗначениеЗаполнено(СтрокаТЗ.ДокументОснование) Тогда
					НоваяСтрока.ДокументОснование = СтрокаТЗ.ДокументОснование;
				КонецЕсли;
				ВремяРегистрацииСотрудников = ВремяРегистрацииДокумента.Получить(СтрокаТЗ.Период);
				Если ВремяРегистрацииСотрудников <> Неопределено Тогда 
					НоваяСтрока.Период = ВремяРегистрацииСотрудников.Получить(СтрокаТЗ.Сотрудник);
				КонецЕсли;
			КонецЦикла;
			
			Если ТаблицаОтборов.Количество() > 0 Тогда
				
				ТаблицаОтборов.Свернуть("Период,Сотрудник,Начисление,ДокументОснование");
				
				ПараметрыПостроения = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез();
				ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(ПараметрыПостроения.Отборы, "Регистратор", "<>", РегистраторОбъект.Ссылка);
				ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(ТаблицаОтборов, "Сотрудник,Начисление,ДокументОснование");
				
				МенеджерВТ = Новый МенеджерВременныхТаблиц;	
				СоздатьВТБухучетНачисленийСотрудников(МенеджерВТ, ОписаниеФильтра, ПараметрыПостроения,,Ложь);
				
				Запрос = Новый Запрос;
				Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
				Запрос.Текст =
				"ВЫБРАТЬ
				|	Бухучет.Сотрудник КАК Сотрудник
				|ИЗ
				|	ВТБухучетНачисленийСотрудниковИсходныеЗаписи КАК Бухучет
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	Бухучет.Сотрудник КАК Сотрудник
				|ИЗ
				|	ВТБухучетНачисленийСотрудников КАК Бухучет";
				Запрос.Текст = СтрЗаменить(Запрос.Текст, "Бухучет.Сотрудник КАК Сотрудник", "Бухучет.*");
				РезультатЗапроса = Запрос.ВыполнитьПакет();
				
				ПредыдущиеЗначения = РезультатЗапроса[0].Выгрузить();
				ПредыдущиеЗначенияРаспределение = РезультатЗапроса[1].Выгрузить();
				
				ПредыдущиеЗначения.Индексы.Добавить(ПоляПоискаПредыдущихЗначений);
				ПредыдущиеЗначенияРаспределение.Индексы.Добавить(ПоляПоискаПредыдущихЗначений);
				
			КонецЕсли;
			
		КонецЕсли;
		ПроверятьИзменения = ЗначениеЗаполнено(ПредыдущиеЗначения);
		
		ПроверяемыеСвойства = "СпособОтраженияЗарплатыВБухучете";
		Если ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
			ПроверяемыеСвойства = СтрШаблон("%1,%2", ПроверяемыеСвойства, "СтатьяФинансирования");
		КонецЕсли;
		Если ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
			ПроверяемыеСвойства = СтрШаблон("%1,%2", ПроверяемыеСвойства, "СтатьяРасходов");
		КонецЕсли;
		ПроверяемыеСвойстваРаспределения = СтрШаблон("%1,%2",ПроверяемыеСвойства,"ДоляРаспределения");
		ПроверяемыеСвойства = СтрРазделить(ПроверяемыеСвойства,",");
		ПроверяемыеСвойстваРаспределения = СтрРазделить(ПроверяемыеСвойстваРаспределения,",");
		
		ПоляГруппировки = "Период,Сотрудник,Начисление,ДокументОснование,Организация";
		ПоляИсключения = "СтатьяФинансирования,СпособОтраженияЗарплатыВБухучете,СтатьяРасходов,ОтношениеКЕНВД";
		ДанныеПоСотрудникам = БухучетНачислений.Скопировать(,ПоляГруппировки);
		ДанныеПоСотрудникам.Свернуть(ПоляГруппировки);
		БухучетНачислений.Индексы.Добавить(ПоляГруппировки);
		Отбор = Новый Структура(ПоляГруппировки);
		СтруктураПоиска = Новый Структура(ПоляПоискаПредыдущихЗначений);
		Для каждого СтрокаТаблицы Из ДанныеПоСотрудникам Цикл
			
			ЗаполнитьЗначенияСвойств(Отбор, СтрокаТаблицы);
			БухучетНачисленийСотрудника = БухучетНачислений.НайтиСтроки(Отбор);
			
			Если БухучетНачисленийСотрудника.Количество() = 1 Тогда
				
				СтрокаБухучет = БухучетНачисленийСотрудника[0];
				Если ПроверятьИзменения Тогда
					
					ДобавлятьЗапись = Ложь;
					ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаТаблицы);
					СтрокиПредыдущихЗначений = ПредыдущиеЗначения.НайтиСтроки(СтруктураПоиска);
					Если Не ЗначениеЗаполнено(СтрокиПредыдущихЗначений) Тогда
						// нет предыдущих значений
						ДобавлятьЗапись = СтрокаБухучет.БухучетЗадан;
					Иначе
						
						Если Не СтрокаБухучет.БухучетЗадан Тогда
							// есть предыдущие значения, регистрируем отмену действия бухучета
							ДобавлятьЗапись = Истина;
							СтрокаБухучет.Используется = Ложь;
						Иначе
							СтрокаПредыдущихЗначений = СтрокиПредыдущихЗначений[0];
							Если Не ЗначениеЗаполнено(СтрокаБухучет.ДействуетДо) И ЗначениеЗаполнено(СтрокаПредыдущихЗначений.ПериодВозвратногоСобытия)Тогда
								СтрокаБухучет.ДействуетДо = СтрокаПредыдущихЗначений.ПериодВозвратногоСобытия;
							КонецЕсли;
							Если ЗначениеЗаполнено(СтрокаПредыдущихЗначений.ИдентификаторЗаписи) Тогда
								// предыдущая запись была с распределением
								ДобавлятьЗапись = Истина;
							ИначеЕсли СтрокаБухучет.Используется <> СтрокаПредыдущихЗначений.Используется Тогда
								ДобавлятьЗапись = Истина;
							ИначеЕсли ЗначениеЗаполнено(СтрокаПредыдущихЗначений.ПериодВозвратногоСобытия)
								И СтрокаБухучет.ДействуетДо > СтрокаПредыдущихЗначений.ПериодВозвратногоСобытия Тогда
								ДобавлятьЗапись = Истина;
							КонецЕсли;
							Если Не ДобавлятьЗапись Тогда
								Для каждого ИмяКолонки Из ПроверяемыеСвойства Цикл
									Если СтрокаБухучет[ИмяКолонки] <> СтрокаПредыдущихЗначений[ИмяКолонки] Тогда
										ДобавлятьЗапись = Истина;
										Прервать;
									КонецЕсли;
								КонецЦикла;
							КонецЕсли;
						КонецЕсли;
						
					КонецЕсли;
					
					Если Не ДобавлятьЗапись Тогда
						Продолжить;
					КонецЕсли; 
					
				ИначеЕсли Не СтрокаБухучет.БухучетЗадан Тогда
					// Нет предыдущего значения и не указан бухучет в текущем значении.
					Продолжить;
				КонецЕсли;
				
				НоваяСтрока = Движения.БухучетНачисленийСотрудников.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаБухучет,, "ДокументОснование");
				Если ЗначениеЗаполнено(СтрокаБухучет.ДокументОснование) Тогда
					НоваяСтрока.ДокументОснование = СтрокаБухучет.ДокументОснование;
				КонецЕсли;
				ЗаписиДобавлены = Истина;
				
			Иначе
				
				СтрокаБухучет = БухучетНачисленийСотрудника[0];
				Если ПроверятьИзменения Тогда
					ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаТаблицы);
					СтрокиПредыдущихЗначений = ПредыдущиеЗначенияРаспределение.НайтиСтроки(СтруктураПоиска);
					Если ЗначениеЗаполнено(СтрокиПредыдущихЗначений) Тогда
						
						ДобавлятьЗапись = Ложь;
						СтрокаПредыдущихЗначений = СтрокиПредыдущихЗначений[0];
						
						Если Не ЗначениеЗаполнено(СтрокаБухучет.ДействуетДо) И ЗначениеЗаполнено(СтрокаПредыдущихЗначений.ПериодВозвратногоСобытия)Тогда
							СтрокаБухучет.ДействуетДо = СтрокаПредыдущихЗначений.ПериодВозвратногоСобытия;
						КонецЕсли;
						Если Не ЗначениеЗаполнено(СтрокаПредыдущихЗначений.ИдентификаторЗаписи) Тогда
							// предыдущая запись была без распределения
							ДобавлятьЗапись = Истина;
						ИначеЕсли СтрокаБухучет.Используется <> СтрокаПредыдущихЗначений.Используется Тогда
							ДобавлятьЗапись = Истина;
						ИначеЕсли ЗначениеЗаполнено(СтрокаПредыдущихЗначений.ПериодВозвратногоСобытия)
							И СтрокаБухучет.ДействуетДо > СтрокаПредыдущихЗначений.ПериодВозвратногоСобытия Тогда
							ДобавлятьЗапись = Истина;
						КонецЕсли;
						Если Не ДобавлятьЗапись Тогда
							ДобавлятьЗапись = Не КоллекцииБухучетаИдентичны(БухучетНачисленийСотрудника, СтрокиПредыдущихЗначений, ПроверяемыеСвойстваРаспределения);
						КонецЕсли;
						Если Не ДобавлятьЗапись Тогда
							Продолжить;
						КонецЕсли;
						
					КонецЕсли;
				ИначеЕсли Не СтрокаБухучет.БухучетЗадан Тогда
					// Нет предыдущего значения и не указан бухучет в текущем значении
					Продолжить;
				КонецЕсли;
				
				ИдентификаторЗаписи = Строка(Новый УникальныйИдентификатор);
				НоваяСтрока = Движения.БухучетНачисленийСотрудников.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаБухучет,,ПоляИсключения);
				Если ЗначениеЗаполнено(СтрокаБухучет.ДокументОснование) Тогда
					НоваяСтрока.ДокументОснование = СтрокаБухучет.ДокументОснование;
				КонецЕсли;
				НоваяСтрока.ИдентификаторЗаписи = ИдентификаторЗаписи;
				ЗаписиДобавлены = Истина;
				
				Идентификатор = 1;
				Для каждого СтрокаТЗ Из БухучетНачисленийСотрудника Цикл
					НоваяСтрока = Движения.БухучетНачисленийСотрудниковРаспределение.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
					НоваяСтрока.ИдентификаторЗаписи = ИдентификаторЗаписи;
					НоваяСтрока.Идентификатор = Идентификатор;
					Идентификатор = Идентификатор + 1;
				КонецЦикла;
				ЗаписиРаспределенияДобавлены = Истина;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если ЗаписиДобавлены Тогда
		
		Движения.БухучетНачисленийСотрудников.Записывать = Истина;
		Если ЗаписиРаспределенияДобавлены Тогда
			Движения.БухучетНачисленийСотрудниковРаспределение.Записывать = Истина;
		КонецЕсли;
		
		Если Не Документы.ТипВсеСсылки().СодержитТип(ТипЗнч(РегистраторОбъект)) Тогда
			Если РегистраторОбъект.ДополнительныеСвойства.Свойство("ОтключитьПроверкуДатыЗапретаИзменения")
				И РегистраторОбъект.ДополнительныеСвойства.ОтключитьПроверкуДатыЗапретаИзменения Тогда
				Движения.БухучетНачисленийСотрудников.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
				Движения.БухучетНачисленийСотрудниковРаспределение.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

Функция НоваяТаблицаБухучетНачисленийСотрудников() Экспорт
	
	МассивТиповНачисления = Новый Массив;
	МассивТиповНачисления.Добавить(Тип("ПеречислениеСсылка.ВидыОсобыхНачисленийИУдержаний"));
	МассивТиповНачисления.Добавить(Тип("ПланВидовРасчетаСсылка.Начисления"));
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Организация",  						Новый ОписаниеТипов("СправочникСсылка.Организации"));
	Таблица.Колонки.Добавить("Сотрудник",  							Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	Таблица.Колонки.Добавить("Начисление",  						Новый ОписаниеТипов(МассивТиповНачисления));
	Таблица.Колонки.Добавить("Период", 								Новый ОписаниеТипов("Дата"));
	Таблица.Колонки.Добавить("ДокументОснование", 					Новый ОписаниеТипов(Метаданные.ОпределяемыеТипы.ОснованиеБухучетаНачисления.Тип));
	Таблица.Колонки.Добавить("СтатьяФинансирования",				Новый ОписаниеТипов("СправочникСсылка.СтатьиФинансированияЗарплата"));
	Таблица.Колонки.Добавить("СпособОтраженияЗарплатыВБухучете", 	Новый ОписаниеТипов("СправочникСсылка.СпособыОтраженияЗарплатыВБухучете"));
	Таблица.Колонки.Добавить("СтатьяРасходов", 						Новый ОписаниеТипов("СправочникСсылка.СтатьиРасходовЗарплата"));
	Таблица.Колонки.Добавить("ОтношениеКЕНВД",  					Новый ОписаниеТипов("ПеречислениеСсылка.ОтношениеКЕНВДЗатратНаЗарплату"));
	Таблица.Колонки.Добавить("Используется", 						Новый ОписаниеТипов("Булево"));
	Таблица.Колонки.Добавить("ДействуетДо",  						Новый ОписаниеТипов("Дата"));
	Таблица.Колонки.Добавить("ДоляРаспределения",  					Новый ОписаниеТипов(Метаданные.ОпределяемыеТипы.ЗначениеДоляРаспределенияБухучетЗарплаты.Тип));
	Таблица.Колонки.Добавить("БухучетЗадан", 						Новый ОписаниеТипов("Булево"));
	
	Возврат Таблица;
	
КонецФункции

Процедура СформироватьДвиженияБухучетСотрудников(РегистраторОбъект, Движения, НастройкиБухучета, Записывать = Ложь) Экспорт

	Если Не ЗначениеЗаполнено(НастройкиБухучета) Тогда
		Возврат;
	КонецЕсли; 
	
	Движения.БухучетЗарплатыСотрудников.Записывать = Истина;
	
	Если РегистраторОбъект.ДополнительныеСвойства.Свойство("ОтключитьПроверкуДатыЗапретаИзменения")
		И РегистраторОбъект.ДополнительныеСвойства.ОтключитьПроверкуДатыЗапретаИзменения Тогда
		Движения.БухучетЗарплатыСотрудников.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
	КонецЕсли;
	
	Для каждого СтрокаТЗ Из НастройкиБухучета Цикл
		НоваяСтрока = Движения.БухучетЗарплатыСотрудников.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
	КонецЦикла;
	
	Если Записывать Тогда
		Движения.БухучетЗарплатыСотрудников.Записать();
		Движения.БухучетЗарплатыСотрудников.Записывать = Ложь;
	КонецЕсли;

КонецПроцедуры

Функция НоваяТаблицаНастройкиБухучетаЗаплатыСотрудников() Экспорт
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Сотрудник",  							Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	Таблица.Колонки.Добавить("Период",  							Новый ОписаниеТипов("Дата"));
	Таблица.Колонки.Добавить("СтатьяФинансирования",				Новый ОписаниеТипов("СправочникСсылка.СтатьиФинансированияЗарплата"));
	Таблица.Колонки.Добавить("СпособОтраженияЗарплатыВБухучете", 	Новый ОписаниеТипов("СправочникСсылка.СпособыОтраженияЗарплатыВБухучете"));
	Таблица.Колонки.Добавить("ОтношениеКЕНВД",  					Новый ОписаниеТипов("ПеречислениеСсылка.ОтношениеКЕНВДЗатратНаЗарплату"));
	Таблица.Колонки.Добавить("ДействуетДо",  						Новый ОписаниеТипов("Дата"));
	
	Возврат Таблица;
	
КонецФункции

Процедура СформироватьПрекращениеБухучетаНачисленийСотрудников(РегистраторОбъект, Движения, НаборЗаписейПлановыеНачисления) Экспорт
	
	ЗаписиДобавлены = Ложь;
	Если ЗначениеЗаполнено(НаборЗаписейПлановыеНачисления) Тогда
		
		// Прекращаем бухучет начисления, если действие начисление прекращено не временно.
		ПлановыеНачисления = НаборЗаписейПлановыеНачисления.Выгрузить();
		ПлановыеНачисления = ПлановыеНачисления.Скопировать(Новый Структура("Используется,ДействуетДо",Ложь, Дата(1,1,1)));
		Если ПлановыеНачисления.Количество() > 0 Тогда
			
			ОснованияБухучетаНачислений = Метаданные.ОпределяемыеТипы.ОснованиеБухучетаНачисления.Тип;
			Для каждого СтрокаТЗ Из ПлановыеНачисления Цикл
				ДокументОснование = Неопределено;
				Если ЗначениеЗаполнено(СтрокаТЗ.ДокументОснование) И ОснованияБухучетаНачислений.СодержитТип(ТипЗнч(СтрокаТЗ.ДокументОснование)) Тогда
					ДокументОснование = СтрокаТЗ.ДокументОснование;
				КонецЕсли;
				СтрокаТЗ.ДокументОснование = ДокументОснование;
			КонецЦикла;
			
			Запрос = Новый Запрос;
			Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
			Запрос.УстановитьПараметр("ПлановыеНачисления", ПлановыеНачисления);
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ПлановыеНачисления.Период КАК Период,
			|	ПлановыеНачисления.Сотрудник КАК Сотрудник,
			|	ПлановыеНачисления.Начисление КАК Начисление,
			|	ПлановыеНачисления.ДокументОснование КАК ДокументОснование,
			|	ПлановыеНачисления.ДействуетДо КАК ДействуетДо
			|ПОМЕСТИТЬ ВТПлановыеНачисления
			|ИЗ
			|	&ПлановыеНачисления КАК ПлановыеНачисления";
			Запрос.Выполнить();
			
			Если Документы.ТипВсеСсылки().СодержитТип(ТипЗнч(РегистраторОбъект)) Тогда
				ИсключаемыйРегистратор = РегистраторОбъект;
			Иначе
				ИсключаемыйРегистратор = РегистраторОбъект.Ссылка;
			КонецЕсли;
			ПараметрыПостроения = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез();
			ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(ПараметрыПостроения.Отборы, "Регистратор", "<>", ИсключаемыйРегистратор);
			ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра("ВТПлановыеНачисления", "Сотрудник,Начисление,ДокументОснование");
			СоздатьВТБухучетНачисленийСотрудников(Запрос.МенеджерВременныхТаблиц, ОписаниеФильтра, ПараметрыПостроения,,Ложь);
			
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ПлановыеНачисления.Период КАК Период,
			|	ПлановыеНачисления.Сотрудник КАК Сотрудник,
			|	ПлановыеНачисления.Начисление КАК Начисление,
			|	ПлановыеНачисления.ДокументОснование КАК ДокументОснование,
			|	ПлановыеНачисления.ДействуетДо КАК ДействуетДо,
			|	ЛОЖЬ КАК Используется,
			|	БухучетНачислений.Организация КАК Организация,
			|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
			|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
			|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
			|	БухучетНачислений.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
			|	БухучетНачислений.ИдентификаторЗаписи КАК ИдентификаторЗаписи
			|ИЗ
			|	ВТПлановыеНачисления КАК ПлановыеНачисления
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТБухучетНачисленийСотрудниковИсходныеЗаписи КАК БухучетНачислений
			|		ПО ПлановыеНачисления.Сотрудник = БухучетНачислений.Сотрудник
			|			И ПлановыеНачисления.Начисление = БухучетНачислений.Начисление
			|			И ПлановыеНачисления.ДокументОснование = БухучетНачислений.ДокументОснование
			|			И (БухучетНачислений.Используется)";
			РезультатЗапроса = Запрос.Выполнить();
			Если Не РезультатЗапроса.Пустой() Тогда
				ЗаписиДобавлены = Истина;
				Выборка = РезультатЗапроса.Выбрать();
				Пока Выборка.Следующий() Цикл
					НоваяСтрока = Движения.БухучетНачисленийСотрудников.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗаписиДобавлены Тогда
		Движения.БухучетНачисленийСотрудников.Записывать = Истина;
		Если Не Документы.ТипВсеСсылки().СодержитТип(ТипЗнч(РегистраторОбъект)) Тогда
			Если РегистраторОбъект.ДополнительныеСвойства.Свойство("ОтключитьПроверкуДатыЗапретаИзменения")
				И РегистраторОбъект.ДополнительныеСвойства.ОтключитьПроверкуДатыЗапретаИзменения Тогда
				Движения.БухучетНачисленийСотрудников.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбслуживаниеМенеджераРасчетов

// Выполняет отражение начислений в бухучете, создает временную таблицу с именем указанным в ИсходныеДанные - ИмяВТБухучетНачислений.
//
// Параметры:
// 		ИсходныеДанные - структура, см ОписаниеИсходныхДанныхДляОтраженияНачисленийВБухучете
//			Организация
//			МесяцНачисления
//			МенеджерВременныхТаблиц 	- менеджер временных таблиц, содержит таблицу с начислениями, имя ВТ в значении ИмяВТНачисленияИсходная 
//			РаботаВБюджетномУчреждении 	- Булево, значение ФО РаботаВБюджетномУчреждении
//			ИспользоватьСтатьиФинансирования  - Булево, значение ФО ИспользоватьСтатьиФинансирования
//			ДокументСсылка
//
//			ИмяВТБухучетНачислений - имя ВТ в которую будут помещены результаты отражения в бухучете 
//					ИдентификаторСтроки
//					Сотрудник
//					ФизическоеЛицо
//					Подразделение
//					ПодразделениеУчетаЗатрат
//					СпособОтраженияЗарплатыВБухучете
//					СтатьяФинансирования
//					СтатьяРасходов
//					Сумма
//					ОблагаетсяЕНВД
//					ДатаНачала
//					Начисление
//					Территория
//					КодСтатьиФинансирования
//					МестоПолученияДохода
//
//			ИмяВТНачисленияИсходная - имя ВТ с начислениями для получения бухучета, содержит поля
//					ПериодРегистрации
//					Организация
//					Сотрудник
//					ФизическоеЛицо
//					Подразделение
//					ТерриторияВыполненияРаботВОрганизации
//					ДатаНачала
//					ДатаОкончания
//					Начисление
//					ДокументОснование
//					ИдентификаторСтроки
//					Сумма
//					Сторно
//					ФиксСторно
//					ВидОперации
//					МестоПолученияДохода
//
//			СтрокиБухучетСторноНачислений - соответствие
//					Ключ	 - ИдентификаторСтроки
//					Значение - таблица значений бухучетом, описание см НоваяТаблицаРаспределениеРезультатовНачислений().
//
//			СтрокиКоэффициентыСреднегоЗаработка - соответствие
//					Ключ	 - ИдентификаторСтроки
//					Значение - таблица значений с коэффициентами распределения, описание см НоваяТаблицаКоэффициентыРаспределенияСреднегоЗаработка().
//
//			КоэффициентыСреднегоЗаработкаДокумента - Соответствие
//					Ключ	 - Перечисления.СпособыРасчетаНачислений
//					Значение - таблица значений с коэффициентами распределения, описание см НоваяТаблицаКоэффициентыРаспределенияСреднегоЗаработка().
//	
//			КоэффициентыСреднегоЗаработкаФССДокумента - таблица значений с коэффициентами распределения, описание см НоваяТаблицаКоэффициентыРаспределенияСреднегоЗаработкаФСС().
//
//			БухучетПервичногоДокумента - таблица значений, описание см НоваяТаблицаБухучетЗарплатыПервичныхДокументов().
//
// Возвращаемое значение: таблица значений с бухучетом начислений
//		описание таблицы см НоваяТаблицаРаспределениеРезультатовНачислений().
//
Функция ВыполнитьОтражениеНачисленийВБухучете(ИсходныеДанные) Экспорт
	
	// В случае если отражение в бухучете выполняется сразу при расчете
	// уточним отражение в бухучете для учтенных ранее начисленных сумм.
	РаспределятьРанееНачисленныеСуммы = (ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата")
												И ИсходныеДанные.МенеджерРасчетаЗарплаты <> Неопределено);
	
	УдалитьВТ = Новый Массив;
	
	ИмяВТБухучетНачислений 	= ИсходныеДанные.ИмяВТБухучетНачислений;
	МенеджерВременныхТаблиц = ИсходныеДанные.МенеджерВременныхТаблиц;
	ИмяВТНачисления			= ИсходныеДанные.ИмяВТНачисления;
	СтрокиБухучетСторноНачислений = ИсходныеДанные.СтрокиБухучетСторноНачислений;
	
	СоздатьВТБухучетНачисленийСторно(МенеджерВременныхТаблиц, ИмяВТНачисления, СтрокиБухучетСторноНачислений);
	УдалитьВТ.Добавить("ВТБухучетНачисленийСторно");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Если РаспределятьРанееНачисленныеСуммы Тогда
		СоздатьВТБухучетКорректировкаРанееВыполненногоНачисления(ИсходныеДанные);
	Иначе
		СоздатьПустуюВТБухучетНачислений(Запрос, "ВТБухучетКорректировкаРанееВыполненногоНачисления");
	КонецЕсли;
	УдалитьВТ.Добавить("ВТБухучетКорректировкаРанееВыполненногоНачисления");
	
	Запрос.УстановитьПараметр("РаспределятьРанееНачисленныеСуммы", РаспределятьРанееНачисленныеСуммы);
	
	// Входная таблица ВТНачисленияДляОтраженияВБухучет содержит поля идентификаторов строк.
	// ИдентификаторСтроки - уникальный в пределах этой таблицы идентификатор строки.
	// ИдентификаторСтрокиБухучет - идентификатор строки таблицы начислений менеджера, используется для
	// помещения результатов распределения в таблицу начислений менеджера, может быть не уникальным
	// в пределах этой таблицы, когда ведется учет по территориям.
	// ИдентификаторСтрокиНачисления - идентификатор строки таблицы начислений менеджера, используемый
	// в алгоритмах расчета менеджера.
	// В выходной таблице ИмяВТБухучетНачислений поле ИдентификаторСтроки будет содержать данные из поля ИдентификаторСтрокиБухучет.
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НачисленияДляРаспределения.ПериодРегистрации КАК ПериодРегистрации,
	|	НачисленияДляРаспределения.Организация КАК Организация,
	|	НачисленияДляРаспределения.Сотрудник КАК Сотрудник,
	|	НачисленияДляРаспределения.ФизическоеЛицо КАК ФизическоеЛицо,
	|	НачисленияДляРаспределения.Подразделение КАК Подразделение,
	|	НачисленияДляРаспределения.Начисление КАК Начисление,
	|	НачисленияДляРаспределения.ДатаНачала КАК ДатаНачала,
	|	НачисленияДляРаспределения.ДатаОкончания КАК ДатаОкончания,
	|	НачисленияДляРаспределения.РанееНачислено КАК РанееНачислено,
	|	НачисленияДляРаспределения.ДокументОснование КАК ДокументОснование,
	|	НачисленияДляРаспределения.ИдентификаторСтрокиБухучет КАК ИдентификаторСтрокиБухучет,
	|	НачисленияДляРаспределения.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	НачисленияДляРаспределения.ИдентификаторСтрокиНачисления КАК ИдентификаторСтрокиНачисления,
	|	НачисленияДляРаспределения.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	НачисленияДляРаспределения.Сторно КАК Сторно,
	|	НачисленияДляРаспределения.ФиксСторно КАК ФиксСторно,
	|	НачисленияДляРаспределения.ВидОперации КАК ВидОперации,
	|	ВЫБОР
	|		КОГДА &РаспределятьРанееНачисленныеСуммы
	|				И ЕСТЬNULL(Начисления.СтратегияОтраженияВУчете, ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазовымРасчетам)
	|			ТОГДА НачисленияДляРаспределения.Сумма + НачисленияДляРаспределения.РанееНачислено
	|		ИНАЧЕ НачисленияДляРаспределения.Сумма
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА &РаспределятьРанееНачисленныеСуммы
	|				И ЕСТЬNULL(Начисления.СтратегияОтраженияВУчете, ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазовымРасчетам)
	|				И НачисленияДляРаспределения.РанееНачислено <> 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК РанееНачисленныеДанные,
	|	НачисленияДляРаспределения.РассчитыватьПоРазовымНачислениямДокумента КАК РассчитыватьПоРазовымНачислениямДокумента
	|ПОМЕСТИТЬ ВТНачисленияДляРаспределения
	|ИЗ
	|	ВТНачисленияДляОтраженияВБухучете КАК НачисленияДляРаспределения
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБухучетНачисленийСторно КАК БухучетНачисленийСторно
	|		ПО НачисленияДляРаспределения.ИдентификаторСтроки = БухучетНачисленийСторно.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБухучетКорректировкаРанееВыполненногоНачисления КАК БухучетКорректировка
	|		ПО НачисленияДляРаспределения.ИдентификаторСтроки = БухучетКорректировка.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления КАК Начисления
	|		ПО НачисленияДляРаспределения.Начисление = Начисления.Ссылка
	|ГДЕ
	|	БухучетНачисленийСторно.ИдентификаторСтроки ЕСТЬ NULL
	|	И БухучетКорректировка.ИдентификаторСтроки ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачисленияДляРаспределения.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	НачисленияДляРаспределения.ИдентификаторСтрокиНачисления КАК ИдентификаторСтрокиНачисления,
	|	НачисленияДляРаспределения.Сотрудник КАК Сотрудник,
	|	НачисленияДляРаспределения.ФизическоеЛицо КАК ФизическоеЛицо,
	|	НачисленияДляРаспределения.Подразделение КАК Подразделение,
	|	НачисленияДляРаспределения.Начисление КАК Начисление,
	|	НачисленияДляРаспределения.ДатаНачала КАК ДатаНачала,
	|	СУММА(НачисленияДляРаспределения.РанееНачислено) КАК РанееНачислено,
	|	НачисленияДляРаспределения.ТерриторияВыполненияРаботВОрганизации КАК Территория
	|ИЗ
	|	ВТНачисленияДляРаспределения КАК НачисленияДляРаспределения
	|ГДЕ
	|	НачисленияДляРаспределения.РанееНачисленныеДанные
	|
	|СГРУППИРОВАТЬ ПО
	|	НачисленияДляРаспределения.ИдентификаторСтроки,
	|	НачисленияДляРаспределения.ИдентификаторСтрокиНачисления,
	|	НачисленияДляРаспределения.Сотрудник,
	|	НачисленияДляРаспределения.ФизическоеЛицо,
	|	НачисленияДляРаспределения.Подразделение,
	|	НачисленияДляРаспределения.Начисление,
	|	НачисленияДляРаспределения.ДатаНачала,
	|	НачисленияДляРаспределения.ТерриторияВыполненияРаботВОрганизации";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляОтраженияВБухучете", ИмяВТНачисления);
	ТаблицаРанееНачислено = Запрос.Выполнить().Выгрузить();
	
	УдалитьВТ.Добавить("ВТНачисленияДляРаспределения");
	
	ИсходныеДанные.ИмяВТНачисления = "ВТНачисленияДляРаспределения";
	ПолучитьБухучетНачисленийБезДоговоровСоздатьВТ(ИсходныеДанные, "ВТБухучетНачисленийВыходнаяТаблица");
	УдалитьВТ.Добавить("ВТБухучетНачисленийВыходнаяТаблица");
	
	Если ТаблицаРанееНачислено.Количество() > 0 Тогда
		СоздатьВТБухучетНачисленийРанееНачислено(ТаблицаРанееНачислено, ИсходныеДанные);
	Иначе
		СоздатьПустуюВТБухучетНачислений(Запрос, "ВТБухучетНачисленийРанееНачислено");
	КонецЕсли;
	УдалитьВТ.Добавить("ВТБухучетНачисленийРанееНачислено");
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Начисления.ИдентификаторСтрокиБухучет КАК ИдентификаторСтроки,
	|	Начисления.МестоПолученияДохода КАК МестоПолученияДохода,
	|	Начисления.ТерриторияВыполненияРаботВОрганизации КАК Территория,
	|	Начисления.Подразделение КАК Подразделение,
	|	Начисления.Сумма КАК ИсходнаяСумма,
	|	БухучетНачислений.Сотрудник КАК Сотрудник,
	|	БухучетНачислений.ФизическоеЛицо КАК ФизическоеЛицо,
	|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
	|	СУММА(БухучетНачислений.Сумма) КАК Сумма,
	|	БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	ВЫБОР
	|		КОГДА БухучетНачислений.ПодразделениеУчетаЗатрат = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
	|			ТОГДА Начисления.Подразделение
	|		ИНАЧЕ БухучетНачислений.ПодразделениеУчетаЗатрат
	|	КОНЕЦ КАК ПодразделениеУчетаЗатрат,
	|	БухучетНачислений.ДатаНачала КАК ДатаНачала,
	|	БухучетНачислений.Начисление КАК Начисление,
	|	ЕСТЬNULL(СтатьиФинансированияЗарплата.Код, """") КАК КодСтатьиФинансирования
	|ПОМЕСТИТЬ ИмяВТБухучетНачислений
	|ИЗ
	|	(ВЫБРАТЬ
	|		БухучетНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|		БухучетНачислений.Сотрудник КАК Сотрудник,
	|		БухучетНачислений.ФизическоеЛицо КАК ФизическоеЛицо,
	|		БухучетНачислений.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|		БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|		БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
	|		БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
	|		БухучетНачислений.Сумма КАК Сумма,
	|		БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|		БухучетНачислений.ДатаНачала КАК ДатаНачала,
	|		БухучетНачислений.Начисление КАК Начисление
	|	ИЗ
	|		ВТБухучетНачисленийВыходнаяТаблица КАК БухучетНачислений
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		БухучетНачислений.ИдентификаторСтроки,
	|		БухучетНачислений.Сотрудник,
	|		БухучетНачислений.ФизическоеЛицо,
	|		БухучетНачислений.Подразделение,
	|		БухучетНачислений.СпособОтраженияЗарплатыВБухучете,
	|		БухучетНачислений.СтатьяФинансирования,
	|		БухучетНачислений.СтатьяРасходов,
	|		БухучетНачислений.Сумма,
	|		БухучетНачислений.ОблагаетсяЕНВД,
	|		БухучетНачислений.ДатаНачала,
	|		БухучетНачислений.Начисление
	|	ИЗ
	|		ВТБухучетНачисленийСторно КАК БухучетНачислений
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		БухучетНачислений.ИдентификаторСтроки,
	|		БухучетНачислений.Сотрудник,
	|		БухучетНачислений.ФизическоеЛицо,
	|		БухучетНачислений.ПодразделениеУчетаЗатрат,
	|		БухучетНачислений.СпособОтраженияЗарплатыВБухучете,
	|		БухучетНачислений.СтатьяФинансирования,
	|		БухучетНачислений.СтатьяРасходов,
	|		БухучетНачислений.Сумма,
	|		БухучетНачислений.ОблагаетсяЕНВД,
	|		БухучетНачислений.ДатаНачала,
	|		БухучетНачислений.Начисление
	|	ИЗ
	|		ВТБухучетНачисленийРанееНачислено КАК БухучетНачислений
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		БухучетНачислений.ИдентификаторСтроки,
	|		БухучетНачислений.Сотрудник,
	|		БухучетНачислений.ФизическоеЛицо,
	|		БухучетНачислений.ПодразделениеУчетаЗатрат,
	|		БухучетНачислений.СпособОтраженияЗарплатыВБухучете,
	|		БухучетНачислений.СтатьяФинансирования,
	|		БухучетНачислений.СтатьяРасходов,
	|		БухучетНачислений.Сумма,
	|		БухучетНачислений.ОблагаетсяЕНВД,
	|		БухучетНачислений.ДатаНачала,
	|		БухучетНачислений.Начисление
	|	ИЗ
	|		ВТБухучетКорректировкаРанееВыполненногоНачисления КАК БухучетНачислений) КАК БухучетНачислений
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтатьиФинансированияЗарплата КАК СтатьиФинансированияЗарплата
	|		ПО БухучетНачислений.СтатьяФинансирования = СтатьиФинансированияЗарплата.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачисленияДляОтраженияВБухучете КАК Начисления
	|		ПО БухучетНачислений.ИдентификаторСтроки = Начисления.ИдентификаторСтроки
	|
	|СГРУППИРОВАТЬ ПО
	|	БухучетНачислений.Сотрудник,
	|	БухучетНачислений.ФизическоеЛицо,
	|	БухучетНачислений.ОблагаетсяЕНВД,
	|	БухучетНачислений.ДатаНачала,
	|	БухучетНачислений.Начисление,
	|	Начисления.Подразделение,
	|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачислений.СтатьяФинансирования,
	|	БухучетНачислений.СтатьяРасходов,
	|	Начисления.ТерриторияВыполненияРаботВОрганизации,
	|	Начисления.МестоПолученияДохода,
	|	Начисления.Сумма,
	|	Начисления.ИдентификаторСтрокиБухучет,
	|	ЕСТЬNULL(СтатьиФинансированияЗарплата.Код, """"),
	|	ВЫБОР
	|		КОГДА БухучетНачислений.ПодразделениеУчетаЗатрат = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
	|			ТОГДА Начисления.Подразделение
	|		ИНАЧЕ БухучетНачислений.ПодразделениеУчетаЗатрат
	|	КОНЕЦ
	|
	|ИМЕЮЩИЕ
	|	(СУММА(БухучетНачислений.Сумма) <> 0
	|		ИЛИ СУММА(БухучетНачислений.Сумма) = 0
	|			И Начисления.Сумма = 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БухучетНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
	|	БухучетНачислений.Сумма КАК Результат,
	|	БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	БухучетНачислений.Территория КАК Территория,
	|	БухучетНачислений.КодСтатьиФинансирования КАК КодСтатьиФинансирования,
	|	БухучетНачислений.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат
	|ИЗ
	|	ИмяВТБухучетНачислений КАК БухучетНачислений
	|
	|УПОРЯДОЧИТЬ ПО
	|	БухучетНачислений.ИдентификаторСтроки,
	|	БухучетНачислений.Территория,
	|	БухучетНачислений.СтатьяФинансирования,
	|	БухучетНачислений.СтатьяРасходов,
	|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачислений.ОблагаетсяЕНВД";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ИмяВТБухучетНачислений", ИмяВТБухучетНачислений);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляОтраженияВБухучете", ИмяВТНачисления);
	
	БухучетНачислений = Запрос.Выполнить().Выгрузить();
	
	ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, УдалитьВТ);
	
	Возврат БухучетНачислений;

КонецФункции

// Выполняет отражение начислений по договорам ГПХ в бухучете, создает временную таблицу с именем указанным в
// ИсходныеДанные - ИмяВТБухучетНачислений.
//
// Параметры:
// 		ИсходныеДанные - структура, см ОписаниеИсходныхДанныхДляОтраженияНачисленийВБухучете.
//
// Возвращаемое значение: таблица значений с бухучетом начислений
//		описание таблицы см НоваяТаблицаРаспределениеРезультатовНачислений().
//
Функция ВыполнитьОтражениеНачисленийПоДоговорамВБухучете(ИсходныеДанные) Экспорт

	ИмяВТБухучетНачислений  = ИсходныеДанные.ИмяВТБухучетНачислений;
	МенеджерВременныхТаблиц = ИсходныеДанные.МенеджерВременныхТаблиц;
	
	УдалитьВТ = Новый Массив;
	
	// Входная таблица ИсходныеДанные.ИмяВТНачисления содержит поля идентификаторов строк.
	// ИдентификаторСтроки - уникальный в пределах этой таблицы идентификатор строки.
	// ИдентификаторСтрокиБухучет - идентификатор строки таблицы начислений менеджера, используется для
	// 		помещения результатов распределения в таблицу начислений менеджера, может быть не уникальным
	// 		в пределах этой таблицы, когда ведется учет по территориям.
	// В выходной таблице ИмяВТБухучетНачислений поле ИдентификаторСтроки будет содержать данные из поля ИдентификаторСтрокиБухучет.
		
	ПолучитьБухучетНачисленийПоДоговорамСоздатьВТ(ИсходныеДанные, "ВТБухучетНачисленийПоДоговорамВременная");
	УдалитьВТ.Добавить("ВТБухучетНачисленийПоДоговорамВременная");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Начисления.МестоПолученияДохода КАК МестоПолученияДохода,
	|	Начисления.ТерриторияВыполненияРаботВОрганизации КАК Территория,
	|	Начисления.ИдентификаторСтрокиБухучет КАК ИдентификаторСтроки,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Начисления.Подразделение КАК Подразделение,
	|	Начисления.Начисление КАК Начисление,
	|	Начисления.ДатаНачала КАК ДатаНачала,
	|	Начисления.ВидОперации КАК ВидОперации,
	|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
	|	ВЫБОР
	|		КОГДА Начисления.ДатаНачала = ДАТАВРЕМЯ(1, 1, 1)
	|				ИЛИ Начисления.ДатаНачала < Начисления.ПериодРегистрации
	|			ТОГДА Начисления.ПериодРегистрации
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(Начисления.ДатаНачала, МЕСЯЦ)
	|	КОНЕЦ КАК ПериодПринятияРасходов,
	|	ВЫБОР
	|		КОГДА БухучетНачислений.ПодразделениеУчетаЗатрат = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
	|			ТОГДА Начисления.Подразделение
	|		ИНАЧЕ БухучетНачислений.ПодразделениеУчетаЗатрат
	|	КОНЕЦ КАК ПодразделениеУчетаЗатрат,
	|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	БухучетНачислений.Сумма КАК Сумма,
	|	ЕСТЬNULL(СтатьиФинансированияЗарплата.Код, """") КАК КодСтатьиФинансирования
	|ПОМЕСТИТЬ ВТБухучетНачисленийВыходнаяТаблица
	|ИЗ
	|	ВТБухучетНачисленийПоДоговорамВременная КАК БухучетНачислений
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтатьиФинансированияЗарплата КАК СтатьиФинансированияЗарплата
	|		ПО БухучетНачислений.СтатьяФинансирования = СтатьиФинансированияЗарплата.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачисленияДляОтраженияВБухучете КАК Начисления
	|		ПО БухучетНачислений.ИдентификаторСтроки = Начисления.ИдентификаторСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БухучетНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	БухучетНачислений.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
	|	БухучетНачислений.Сумма КАК Результат,
	|	БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	БухучетНачислений.Территория КАК Территория,
	|	БухучетНачислений.КодСтатьиФинансирования КАК КодСтатьиФинансирования
	|ИЗ
	|	ВТБухучетНачисленийВыходнаяТаблица КАК БухучетНачислений
	|
	|УПОРЯДОЧИТЬ ПО
	|	БухучетНачислений.ИдентификаторСтроки,
	|	БухучетНачислений.Территория,
	|	БухучетНачислений.СтатьяФинансирования,
	|	БухучетНачислений.СтатьяРасходов,
	|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачислений.ОблагаетсяЕНВД";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТБухучетНачисленийВыходнаяТаблица", ИмяВТБухучетНачислений);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляОтраженияВБухучете", ИсходныеДанные.ИмяВТНачисления);
	
	БухучетНачисленийПоДоговорам = Запрос.Выполнить().Выгрузить();
	ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, УдалитьВТ);
	
	Возврат БухучетНачисленийПоДоговорам;

КонецФункции

// Выполняет отражение удержаний и займов в бухучете
//
// Параметры:
// 		ИсходныеДанные - структура, см ОписаниеИсходныхДанныхДляОтраженияУдержанийВБухучете
//				* МенеджерВременныхТаблиц
//				* ИмяВТБухучетНачислений
//				* ОкончательныйРасчет
//				* ПериодРегистрации
//				* Организация
//				* ИсключаемыйРегистратор
//				* ТаблицаУдержаний, описание см ОтражениеЗарплатыВУчете.НоваяТаблицаРезультатРасчетаУдержаний
//				* ТаблицаЗаймов, описание см ОтражениеЗарплатыВУчетеРасширенный.НоваяТаблицаРезультатПогашениеЗаймов.
//
// Возвращаемое значение: структура с ключами
//		БухучетУдержаний - Таблица значений, описание см в  ОтражениеЗарплатыВУчете.НоваяТаблицаРаспределениеРезультатовУдержаний()
//		БухучетЗаймов    - Таблица значений, описание см в  ОтражениеЗарплатыВУчете.НоваяТаблицаРаспределениеРезультатовУдержаний().
//
Функция ВыполнитьОтражениеУдержанийИЗаймовВБухучете(ИсходныеДанные) Экспорт

	БухучетУдержаний  = ОтражениеЗарплатыВУчете.НоваяТаблицаРаспределениеРезультатовУдержаний();
	БухучетЗаймов 	  = ОтражениеЗарплатыВУчете.НоваяТаблицаРаспределениеРезультатовУдержаний();
	
	МенеджерВременныхТаблиц = ИсходныеДанные.МенеджерВременныхТаблиц;
	ИмяВТБухучетНачислений 	= ИсходныеДанные.ИмяВТБухучетНачислений;
	ОкончательныйРасчет		= ИсходныеДанные.ОкончательныйРасчет;
	ПериодРегистрации       = ИсходныеДанные.МесяцНачисления;
	Организация      		= ИсходныеДанные.Организация;
	ИсключаемыйРегистратор  = ИсходныеДанные.ИсключаемыйРегистратор;
	РегистраторыУдержанийОбновленияБухучета = ИсходныеДанные.РегистраторыУдержанийОбновленияБухучета;
	
	ТаблицаУдержаний = ИсходныеДанные.ТаблицаУдержаний;
	ТаблицаЗаймов	 = ИсходныеДанные.ТаблицаЗаймов;
	
	// Получение данных учета для распределения
	// БазовыеНачисления - таблица значений с базовыми начислениями
	// УжеУдержано - таблица значений, содержит данные об уже выполненных удержаниях по физическому лицу.
	
	ФизическиеЛицаМассив = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ФизическиеЛицаМассив, ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаЗаймов, "ФизическоеЛицо", Истина), Истина);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ФизическиеЛицаМассив, ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаУдержаний, "ФизическоеЛицо", Истина), Истина);
	
	УдержанияМассив 	 = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(УдержанияМассив, ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаУдержаний, "ВидУдержания", Истина), Истина);
	
	// Получение данных для распределения, ДанныеДляРаспределения - структура
	// БазаВсеНачисления - таблица значений с базовыми начислениями
	// УжеУдержано - таблица значений, содержит данные об уже выполненных удержаниях по физическому лиц.
	ПараметрыПолученияДанных = ОтражениеЗарплатыВУчете.НовоеОписаниеПараметровПолученияДанныхДляРаспределенияУдержаний();
	ЗаполнитьЗначенияСвойств(ПараметрыПолученияДанных ,ИсходныеДанные, "МенеджерВременныхТаблиц,Организация,ОкончательныйРасчет,РегистраторыУдержанийОбновленияБухучета");
	ПараметрыПолученияДанных.ПериодРегистрации = ПериодРегистрации;
	ПараметрыПолученияДанных.ИмяВТДанныеТекущегоДокумента = ИмяВТБухучетНачислений;
	
	ИсключаемыеРегистраторы = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИсключаемыйРегистратор);
	
	ДанныеДляРаспределения = ОтражениеЗарплатыВУчете.ДанныеДляРаспределенияУдержаний(ФизическиеЛицаМассив, УдержанияМассив, ИсключаемыеРегистраторы, ПараметрыПолученияДанных);
	
	БазовыеНачисления = ДанныеДляРаспределения.БазовыеНачисления;
	БазовыеНачисленияЗаймы = БазовыеНачисления.СкопироватьКолонки();
	Если ПолучитьФункциональнуюОпцию("РаботаВХозрасчетнойОрганизации") Тогда
		СтатьиРасходовПоСпособамРасчетов  = ЗарплатаКадры.СтатьиРасходовПоСпособамРасчетовСФизическимиЛицами();
		РасчетыПоОплатеТруда = СтатьиРасходовПоСпособамРасчетов[Перечисления.СпособыРасчетовСФизическимиЛицами.ОплатаТруда];
		СтрокиКУдалению = Новый Массив;
		Для каждого СтрокаТЗ Из БазовыеНачисления Цикл
			Если СтрокаТЗ.СтатьяРасходов = РасчетыПоОплатеТруда Тогда
				ЗаполнитьЗначенияСвойств(БазовыеНачисленияЗаймы.Добавить(), СтрокаТЗ);
			КонецЕсли;
		КонецЦикла;
	Иначе
		БазовыеНачисленияЗаймы = БазовыеНачисления;
	КонецЕсли;
	БазовыеНачисления.Индексы.Добавить("ФизическоеЛицо");
	
	СтрокиУжеУдержано = ДанныеДляРаспределения.СтрокиУжеУдержано;
	
	// ТаблицаЗаймов
	ПараметрыРаспределения = ОтражениеЗарплатыВУчете.НовоеОписаниеПараметровРаспределенияУдержаний();
	ПараметрыРаспределения.Организация 		 = Организация;
	ПараметрыРаспределения.ПериодРегистрации = ПериодРегистрации;
	БухучетЗаймов = ОтражениеЗарплатыВУчете.РаспределениеУдержанийПоРабочимМестамИСтатьям(ТаблицаЗаймов, БазовыеНачисленияЗаймы, Новый Соответствие, ПараметрыРаспределения);
	
	Если ПолучитьФункциональнуюОпцию("РаботаВХозрасчетнойОрганизации") Тогда
		// Уточним статью расходов.
		Для каждого СтрокаТЗ Из БухучетЗаймов Цикл
			Если СтрокаТЗ.ВидУдержания = Перечисления.ВидыОсобыхНачисленийИУдержаний.НачисленоПроцентовПоЗайму Тогда
				СтрокаТЗ.СтатьяРасходов = Справочники.СтатьиРасходовЗарплата.ПустаяСсылка();
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// ТаблицаУдержаний
	ДополнительныеПараметры = ОтражениеЗарплатыВУчетеРасширенный.ОписаниеИсходныхДанныхДляРаспределенияУдержаний();
	ДополнительныеПараметры.БазовыеНачисления = БазовыеНачисления;
	ДополнительныеПараметры.СтрокиУжеУдержаноПоФизическимЛицам = СтрокиУжеУдержано;
	ДополнительныеПараметры.ТаблицаУдержаний = ТаблицаУдержаний;
	ДополнительныеПараметры.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ДополнительныеПараметры.Организация = Организация;
	ДополнительныеПараметры.ПериодРегистрации = ПериодРегистрации;
	ДополнительныеПараметры.ВидыНачисленийДополненияРасчетнойБазы = ИсходныеДанные.ВидыНачисленийДополненияРасчетнойБазы;
	ДополнительныеПараметры.ОкончательныйРасчет = ОкончательныйРасчет;
	ОтражениеЗарплатыВУчетеРасширенный.ВыполнитьРаспределениеУдержаний(БухучетУдержаний, ДополнительныеПараметры);
	
	// сортировка результатов
	УпорядочитьТаблицуРаспределениеУдержаний(БухучетУдержаний);
	УпорядочитьТаблицуРаспределениеУдержаний(БухучетЗаймов);
	
	// заполним колонку КодСтатьиФинансирования в таблицах
	КодыСтатейФинансирования = КодыСтатейФинансирования();
	ЗаполнитьКодСтатьиФинансирования(БухучетУдержаний, КодыСтатейФинансирования);
	ЗаполнитьКодСтатьиФинансирования(БухучетЗаймов, КодыСтатейФинансирования);
	
	РезультатыОтраженияВБухучете = Новый Структура("БухучетУдержаний,БухучетЗаймов");
	РезультатыОтраженияВБухучете.БухучетУдержаний  = БухучетУдержаний;
	РезультатыОтраженияВБухучете.БухучетЗаймов 	   = БухучетЗаймов;
	
	Возврат РезультатыОтраженияВБухучете;

КонецФункции

// Создает временную таблицу с именем ИмяВТ помещая в нее содержимое таблицы значений
//	Параметры
//		МенеджерВременныхТаблиц - менеджер временных таблиц
//		БухучетНачислений - таблица значений, описание см НоваяТаблицаБухучетНачислений()
//		ИмяВТ - строка, имя создаваемой временной таблицы.
//		ПланируемаяДатаВыплаты - дата
//
Процедура СоздатьВТБухучетНачисленийПоТаблицеБухучетНачислений(МенеджерВременныхТаблиц, БухучетНачислений, ИмяВТ, ПланируемаяДатаВыплаты) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Если БухучетНачислений.Количество() = 0 Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 0
		|	0 КАК ИдентификаторСтроки,
		|	0 КАК ИдентификаторСтрокиНачисления,
		|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
		|	ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка) КАК Сотрудник,
		|	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка) КАК ФизическоеЛицо,
		|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК Подразделение,
		|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК ПодразделениеУчетаЗатрат,
		|	ЗНАЧЕНИЕ(Справочник.ТерриторииВыполненияРабот.ПустаяСсылка) КАК МестоПолученияДохода,
		|	ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка) КАК СпособОтраженияЗарплатыВБухучете,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка) КАК СтатьяФинансирования,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка) КАК СтатьяРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыДоходовИсполнительногоПроизводства.ПустаяСсылка) КАК ВидДоходаИсполнительногоПроизводства,
		|	0 КАК Сумма,
		|	ЛОЖЬ КАК ОблагаетсяЕНВД,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаНачала,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаОкончания,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ПериодДействия,
		|	ЗНАЧЕНИЕ(ПланВидовРасчета.Начисления.ПустаяСсылка) КАК Начисление,
		|	ЛОЖЬ КАК ДанныеТекущегоДокумента,
		|	ЗНАЧЕНИЕ(Документ.НачислениеЗарплаты.ПустаяСсылка) КАК ДокументОснованиеНДФЛ,
		|	ЗНАЧЕНИЕ(Справочник.ТерриторииВыполненияРабот.ПустаяСсылка) КАК Территория
		|ПОМЕСТИТЬ ВТБухучетНачисленийВыходнаяТаблица";
		
	Иначе
		
		Если БухучетНачислений.Колонки.Найти("ВидДохода") = Неопределено Тогда
			ВидыДоходовИсполнительногоПроизводства = УчетНачисленнойЗарплаты.ВидыДоходовИсполнительногоПроизводстваНачислений(ПланируемаяДатаВыплаты);
			БухучетНачислений.Колонки.Добавить("ВидДохода",  Новый ОписаниеТипов("ПеречислениеСсылка.ВидыДоходовИсполнительногоПроизводства"));
			Для каждого СтрокаТЗ Из БухучетНачислений Цикл
				СтрокаТЗ.ВидДохода = ВидыДоходовИсполнительногоПроизводства[СтрокаТЗ.Начисление];
			КонецЦикла;
		КонецЕсли;
		
		Запрос.УстановитьПараметр("БухучетНачислений", БухучетНачислений);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	БухучетНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	БухучетНачислений.ИдентификаторСтрокиНачисления КАК ИдентификаторСтрокиНачисления,
		|	БухучетНачислений.Организация КАК Организация,
		|	БухучетНачислений.Сотрудник КАК Сотрудник,
		|	БухучетНачислений.ФизическоеЛицо КАК ФизическоеЛицо,
		|	БухучетНачислений.Подразделение КАК Подразделение,
		|	БухучетНачислений.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
		|	БухучетНачислений.МестоПолученияДохода КАК МестоПолученияДохода,
		|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
		|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
		|	БухучетНачислений.ВидДохода КАК ВидДоходаИсполнительногоПроизводства,
		|	БухучетНачислений.Сумма КАК Сумма,
		|	БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	БухучетНачислений.ДатаНачала КАК ДатаНачала,
		|	БухучетНачислений.ДатаОкончания КАК ДатаОкончания,
		|	БухучетНачислений.ПериодДействия КАК ПериодДействия,
		|	БухучетНачислений.Начисление КАК Начисление,
		|	БухучетНачислений.ДанныеТекущегоДокумента КАК ДанныеТекущегоДокумента,
		|	БухучетНачислений.ДокументОснованиеНДФЛ КАК ДокументОснованиеНДФЛ,
		|	БухучетНачислений.Территория КАК Территория
		|ПОМЕСТИТЬ ВТБухучетНачисленийВыходнаяТаблица
		|ИЗ
		|	&БухучетНачислений КАК БухучетНачислений";
		
		Если БухучетНачислений.Колонки.Найти("ДанныеТекущегоДокумента") = Неопределено Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "БухучетНачислений.ДанныеТекущегоДокумента КАК ДанныеТекущегоДокумента,", "");
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "БухучетНачислений.ДокументОснованиеНДФЛ КАК ДокументОснованиеНДФЛ,", "");
		КонецЕсли;
		
	КонецЕсли;

	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТБухучетНачисленийВыходнаяТаблица", ИмяВТ);
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура СоздатьВТБухучетВсехНачисленийДляБухучетаУдержанийНДФЛ(МенеджерВременныхТаблиц, ПараметрыБухучета, ПланируемаяДатаВыплаты) Экспорт
	
	ВидыДоходовИсполнительногоПроизводства = 
		УчетНачисленнойЗарплаты.ВидыДоходовИсполнительногоПроизводстваНачислений(ПланируемаяДатаВыплаты);
	
	МассивТиповНачисления = Новый Массив;
	МассивТиповНачисления.Добавить(Тип("ПеречислениеСсылка.ВидыОсобыхНачисленийИУдержаний"));
	МассивТиповНачисления.Добавить(Тип("ПланВидовРасчетаСсылка.Начисления"));
	ОтражениеЗарплатыВУчетеРасширенный.ДополнитьМассивТиповНачисления(МассивТиповНачисления);

	ВидыДоходовНачислений = Новый ТаблицаЗначений;
	ВидыДоходовНачислений.Колонки.Добавить("Начисление", Новый ОписаниеТипов(МассивТиповНачисления));
	ВидыДоходовНачислений.Колонки.Добавить("ВидДохода",  Новый ОписаниеТипов("ПеречислениеСсылка.ВидыДоходовИсполнительногоПроизводства"));
	
	Для Каждого Элемент Из ВидыДоходовИсполнительногоПроизводства Цикл
		ВидДоходаНачисления = ВидыДоходовНачислений.Добавить();
		ВидДоходаНачисления.Начисление = Элемент.Ключ;
		ВидДоходаНачисления.ВидДохода  = Элемент.Значение;
	КонецЦикла;
	
	ЗарплатаКадры.СоздатьВТПоТаблицеЗначений(МенеджерВременныхТаблиц, ВидыДоходовНачислений, "ВТВидыДоходовНачислений");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	БухучетНачислений.Сотрудник КАК Сотрудник,
	|	БухучетНачислений.ФизическоеЛицо КАК ФизическоеЛицо,
	|	БухучетНачислений.Подразделение КАК Подразделение,
	|	БухучетНачислений.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|	БухучетНачислений.МестоПолученияДохода КАК МестоПолученияДохода,
	|	БухучетНачислений.ДатаНачала КАК ДатаНачала,
	|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
	|	БухучетНачислений.Сумма КАК Сумма,
	|	БухучетНачислений.Начисление КАК Начисление,
	|	БухучетНачислений.Территория КАК Территория,
	|	ВидыДоходовНачислений.ВидДохода КАК ВидДоходаИсполнительногоПроизводства
	|ПОМЕСТИТЬ ВТБухучетНачисленийВыходнаяТаблица
	|ИЗ
	|	ВТБухучетНачисленийВходнаяТаблица КАК БухучетНачислений
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВидыДоходовНачислений КАК ВидыДоходовНачислений
	|		ПО БухучетНачислений.Начисление = ВидыДоходовНачислений.Начисление
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	БухучетНачислений.Сотрудник,
	|	БухучетНачислений.ФизическоеЛицо,
	|	БухучетНачислений.Подразделение,
	|	БухучетНачислений.ПодразделениеУчетаЗатрат,
	|	БухучетНачислений.МестоПолученияДохода,
	|	БухучетНачислений.ДатаНачала,
	|	БухучетНачислений.СтатьяФинансирования,
	|	БухучетНачислений.СтатьяРасходов,
	|	БухучетНачислений.Сумма,
	|	БухучетНачислений.Начисление,
	|	БухучетНачислений.Территория,
	|	ВидыДоходовНачислений.ВидДохода
	|ИЗ
	|	ВТБухучетНачисленийПоДоговорамВходнаяТаблица КАК БухучетНачислений
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВидыДоходовНачислений КАК ВидыДоходовНачислений
	|		ПО БухучетНачислений.Начисление = ВидыДоходовНачислений.Начисление";
		
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТБухучетНачисленийВходнаяТаблица", ПараметрыБухучета.ИмяВТБухучетНачислений);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТБухучетНачисленийПоДоговорамВходнаяТаблица", ПараметрыБухучета.ИмяВТБухучетНачисленийПоДоговорам);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТБухучетНачисленийВыходнаяТаблица", ПараметрыБухучета.ИмяВТБухучетВсехНачислений);
	Запрос.Выполнить();
	
	ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц,"ВТВидыДоходовНачислений")
	
КонецПроцедуры

// Возвращает строку с идентификаторами колонок таблицы значений,
// исключая служебную колонку ИдентификаторСтроки.
//
// Параметры
//		ТаблицаРаспределения - таблица значений.
//
Функция СтрокаКолонокРаспределенияБезИдентификатораСтроки(ТаблицаРаспределения) Экспорт

	Колонки = "";
	Для Каждого Колонка Из ТаблицаРаспределения.Колонки Цикл
		Если Колонка.Имя = "ИдентификаторСтроки" Тогда
			Продолжить;
		КонецЕсли;
		Колонки = Колонки + "," + Колонка.Имя;
	КонецЦикла;
	Колонки = Сред(Колонки, 2);
	
	Возврат Колонки;

КонецФункции

Функция НоваяТаблицаНачисленияДляБухучета() Экспорт

	ОписаниеТипаДата = Новый ОписаниеТипов("Дата",Новый КвалификаторыДаты(ЧастиДаты.Дата));
	МассивТиповНачислениеУдержание = Метаданные.РегистрыНакопления.НачисленияУдержанияПоСотрудникам.Измерения.НачислениеУдержание.Тип.Типы();
	МассивТиповДокументОснование   = Метаданные.РегистрыНакопления.НачисленияУдержанияПоСотрудникам.Реквизиты.ДокументОснование.Тип.Типы();
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("ИдентификаторСтроки", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(7,0)));
	Таблица.Колонки.Добавить("ИдентификаторСтрокиНачисления", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(7,0)));
	Таблица.Колонки.Добавить("Организация", 		Новый ОписаниеТипов("СправочникСсылка.Организации"));
	Таблица.Колонки.Добавить("ФизическоеЛицо", 		Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	Таблица.Колонки.Добавить("Сотрудник",  			Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	Таблица.Колонки.Добавить("Подразделение",		Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	Таблица.Колонки.Добавить("Начисление", 			Новый ОписаниеТипов(МассивТиповНачислениеУдержание));
	Таблица.Колонки.Добавить("Результат",			Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	Таблица.Колонки.Добавить("РанееНачислено",		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	Таблица.Колонки.Добавить("ПериодРегистрации",	ОписаниеТипаДата);
	Таблица.Колонки.Добавить("ПериодДействия", 		ОписаниеТипаДата);
	Таблица.Колонки.Добавить("ДатаНачала", 			ОписаниеТипаДата);
	Таблица.Колонки.Добавить("ДатаОкончания", 		ОписаниеТипаДата);
	Таблица.Колонки.Добавить("ДокументОснование", 	Новый ОписаниеТипов(МассивТиповДокументОснование));
	Таблица.Колонки.Добавить("Территория",			Новый ОписаниеТипов(Метаданные.ОпределяемыеТипы.ТерриторияВыполненияРаботВОрганизации.Тип));
	Таблица.Колонки.Добавить("МестоПолученияДохода",Новый ОписаниеТипов(Метаданные.ОпределяемыеТипы.ТерриторияВыполненияРаботВОрганизации.Тип));
	Таблица.Колонки.Добавить("Сторно", 				Новый ОписаниеТипов("Булево"));
	Таблица.Колонки.Добавить("ФиксСторно", 			Новый ОписаниеТипов("Булево"));
	Таблица.Колонки.Добавить("КорректировкаРанееВыполненногоНачисления", 			Новый ОписаниеТипов("Булево"));
	Таблица.Колонки.Добавить("ВидОперации", 		Новый ОписаниеТипов("ПеречислениеСсылка.ВидыОперацийПоЗарплате"));
	Таблица.Колонки.Добавить("РассчитыватьПоРазовымНачислениямДокумента", Новый ОписаниеТипов("Булево"));
		
	Возврат Таблица;

КонецФункции

// Выполняет отражение НДФЛ в бухучете
//
// Параметры:
// 		ИсходныеДанные - структура, см ОписаниеИсходныхДанныхДляОтраженияУдержанийВБухучете
//				* МенеджерВременныхТаблиц
//				* ИмяВТБухучетНачислений
//				* ОкончательныйРасчет
//				* ПериодРегистрации
//				* Организация
//				* ИсключаемыйРегистратор
//				* РезультатРасчетаНДФЛ, описание см ОтражениеЗарплатыВУчете.НоваяТаблицаРезультатРасчетаНДФЛ.
//
// Возвращаемое значение: таблица значений с бухучетом НДФЛ
//		описание таблицы см ОтражениеЗарплатыВУчете.НоваяТаблицаРаспределениеРезультатовУдержаний().
//
Функция ВыполнитьОтражениеНДФЛВБухучете(ИсходныеДанные) Экспорт

	БухучетНДФЛ = ОтражениеЗарплатыВУчете.НоваяТаблицаРаспределениеРезультатовУдержаний();
	
	РезультатРасчетаНДФЛ = ИсходныеДанные.РезультатРасчетаНДФЛ;
	Если РезультатРасчетаНДФЛ.Количество() = 0 Тогда
		Возврат БухучетНДФЛ;
	КонецЕсли;
	
	ТаблицаНДФЛ = РезультатРасчетаНДФЛ.Скопировать();
	ТаблицаНДФЛ.Свернуть("ФизическоеЛицо,ДатаПолученияДохода,Территория,ВидУдержания,КатегорияДохода", "Сумма");
	ОтражениеЗарплатыВУчете.ДобавитьКолонкуИдентификаторСтроки(ТаблицаНДФЛ, Истина);
	
	МенеджерВременныхТаблиц = ИсходныеДанные.МенеджерВременныхТаблиц;
	ИмяВТБухучетНачислений 	= ИсходныеДанные.ИмяВТБухучетНачислений;
	ОкончательныйРасчет		= ИсходныеДанные.ОкончательныйРасчет;
	ПериодРегистрации       = ИсходныеДанные.МесяцНачисления;
	Организация      		= ИсходныеДанные.Организация;
	ИсключаемыйРегистратор  = ИсходныеДанные.ИсключаемыйРегистратор;
	РегистраторыНДФЛОбновленияБухучета 	= ИсходныеДанные.РегистраторыУдержанийОбновленияБухучета;
	ОснованияУчтенныеПриРасчетеНДФЛ 	= ИсходныеДанные.ОснованияУчтенныеПриРасчетеНДФЛ;
	ПерваяПоловинаМесяца 				= ИсходныеДанные.ПерваяПоловинаМесяца;
	
	УдалитьВТ = Новый Массив;
	
	// Если переданы основания, создаем временную таблицу ВТУсловияОтбораДляРаспределенияНДФЛ.
	Если ОснованияУчтенныеПриРасчетеНДФЛ <> Неопределено
		И ПериодРегистрации >= ОтражениеЗарплатыВУчете.ДатаНачалаИспользованияОснованийВРаспределенииНДФЛ() Тогда
		ОтражениеЗарплатыВУчете.СоздатьВТУсловияОтбораДляРаспределенияНДФЛ(МенеджерВременныхТаблиц, ОснованияУчтенныеПриРасчетеНДФЛ, ИсключаемыйРегистратор);
		УдалитьВТ.Добавить("ВТУсловияОтбораДляРаспределенияНДФЛ");
	КонецЕсли;
	
	ФизическиеЛицаМассив = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаНДФЛ, "ФизическоеЛицо", Истина);
	УдержанияМассив = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаНДФЛ, "ВидУдержания", Истина);
	
	// Получение данных для распределения, ДанныеДляРаспределенияНДФЛ - структура
	// БазаВсеНачисления - таблица значений с базовыми начислениями
	// УжеУдержано - таблица значений, содержит данные об уже выполненных удержаниях по физическому лиц
	// в менеджере временных таблиц будет создана таблица ВТРаспределениеНачисленийДляБазыНДФЛ для получения базы НДФЛ.
	
	ПараметрыРаспределения = ОтражениеЗарплатыВУчете.НовоеОписаниеПараметровРаспределенияНДФЛ();
	ПараметрыРаспределения.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ПараметрыРаспределения.Организация = Организация;
	ПараметрыРаспределения.ПериодРегистрации = ПериодРегистрации;
	ПараметрыРаспределения.МассивФизическихЛиц = ФизическиеЛицаМассив;
	ПараметрыРаспределения.МассивУдержаний = УдержанияМассив;
	ПараметрыРаспределения.ИсключаемыйРегистратор = ИсключаемыйРегистратор;
	ПараметрыРаспределения.ОкончательныйРасчет = ОкончательныйРасчет;
	ПараметрыРаспределения.РегистраторыНДФЛОбновленияБухучета = РегистраторыНДФЛОбновленияБухучета;
	ПараметрыРаспределения.ИмяВТДанныеТекущегоДокумента = ИмяВТБухучетНачислений;
	
	ДанныеДляРаспределенияНДФЛ = ОтражениеЗарплатыВУчете.ДанныеДляРаспределенияНДФЛ(ПараметрыРаспределения);
	УдалитьВТ.Добавить("ВТРаспределениеНачисленийДляБазыНДФЛ");
	
	БазаВсеНачисления = ДанныеДляРаспределенияНДФЛ.БазаВсеНачисления;
	СтрокиУжеУдержано = ДанныеДляРаспределенияНДФЛ.СтрокиУжеУдержано;
	
	СведенияОДоходахНДФЛ = Неопределено;
	Если ИсходныеДанные.ЗасчитыватьДанныеАвансовНДФЛ И ЗарплатаКадры.ВТСуществует(МенеджерВременныхТаблиц, "ВТРегистрНакопления_СведенияОДоходахНДФЛ") Тогда
		ДоговорыГПХ = Новый Массив;
		ДоговорыГПХ.Добавить(Перечисления.ВидыОсобыхНачисленийИУдержаний.ДоговорАвторскогоЗаказа);
		ДоговорыГПХ.Добавить(Перечисления.ВидыОсобыхНачисленийИУдержаний.ДоговорРаботыУслуги);
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ДоговорыГПХ", ДоговорыГПХ);
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА СведенияОДоходах.Начисление В (&ДоговорыГПХ)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоДоговорГПХ,
		|	*
		|ИЗ
		|	ВТРегистрНакопления_СведенияОДоходахНДФЛ КАК СведенияОДоходах";
		СведенияОДоходахНДФЛ = Запрос.Выполнить().Выгрузить();
		Если ПериодРегистрации < УчетНДФЛ.ДатаЗакона263ФЗ() Тогда
			СведенияОДоходахНДФЛБезАвансов = СведенияОДоходахНДФЛ.Скопировать(Новый Структура("ЗарегистрированоПриНачисленииАванса", Дата(1,1,1,0,0,0)));
			ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, "ВТРегистрНакопления_СведенияОДоходахНДФЛ");
			ЗарплатаКадры.СоздатьВТПоТаблицеЗначений(МенеджерВременныхТаблиц, СведенияОДоходахНДФЛБезАвансов, "ВТРегистрНакопления_СведенияОДоходахНДФЛ");
			СведенияОДоходахНДФЛБезАвансов = Неопределено;
		Иначе
			Если Не ПерваяПоловинаМесяца Тогда
				Запрос.УстановитьПараметр("ТаблицаНДФЛ", ТаблицаНДФЛ);
				Запрос.УстановитьПараметр("Организация", Организация);
				Запрос.Текст = 
				"ВЫБРАТЬ РАЗЛИЧНЫЕ
				|	&Организация КАК Организация,
				|	Таблица.ФизическоеЛицо КАК ФизическоеЛицо,
				|	Таблица.ДатаПолученияДохода КАК ДатаПолученияДохода,
				|	Таблица.КатегорияДохода КАК КатегорияДохода
				|ПОМЕСТИТЬ ВТОтборЗачетАванса
				|ИЗ
				|	&ТаблицаНДФЛ КАК Таблица";
				Запрос.Выполнить();
				УдалитьВТ.Добавить("ВТОтборЗачетАванса");
			КонецЕсли;
			Если ЗарплатаКадры.ВТСуществует(МенеджерВременныхТаблиц, "ВТСведенияОДоходахНДФЛ_Бухучет") Тогда
				ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, "ВТРегистрНакопления_СведенияОДоходахНДФЛ");
				Отбор = Новый Структура("ЭтоДоговорГПХ", Истина);
				СведенияОДоходахДоговорыГПХ = СведенияОДоходахНДФЛ.Скопировать(Отбор);
				Если ЗначениеЗаполнено(СведенияОДоходахДоговорыГПХ) Тогда
					Запрос.Текст = 
					"ВЫБРАТЬ
					|	*
					|ИЗ
					|	ВТСведенияОДоходахНДФЛ_Бухучет КАК СведенияОДоходах";
					СведенияОДоходахБухучет = Запрос.Выполнить().Выгрузить();
					ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, "ВТСведенияОДоходахНДФЛ_Бухучет");
					ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(СведенияОДоходахДоговорыГПХ, СведенияОДоходахБухучет);
					Запрос.УстановитьПараметр("СведенияОДоходах", СведенияОДоходахБухучет);
					Запрос.Текст = 
					"ВЫБРАТЬ
					|	&Поля
					|ПОМЕСТИТЬ ВТРегистрНакопления_СведенияОДоходахНДФЛ
					|ИЗ
					|	&СведенияОДоходах КАК СведенияОДоходах";
					МассивПоляЗапроса = Новый Массив;
					Для каждого Колонка Из СведенияОДоходахБухучет.Колонки Цикл
						ИмяКолонки = Колонка.Имя;
						МассивПоляЗапроса.Добавить(СтрШаблон("СведенияОДоходах.%1 КАК %1", ИмяКолонки));
					КонецЦикла;
					Разделитель = СтрШаблон(",%1",Символы.ПС);
					ПоляЗапроса = СтрСоединить(МассивПоляЗапроса, Разделитель);
					Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Поля", ПоляЗапроса);
					Запрос.Выполнить();
				Иначе
					Запрос.Текст = 
					"ВЫБРАТЬ
					|	*
					|ПОМЕСТИТЬ ВТРегистрНакопления_СведенияОДоходахНДФЛ
					|ИЗ
					|	ВТСведенияОДоходахНДФЛ_Бухучет КАК СведенияОДоходах";
					Запрос.Выполнить();
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		СведенияОДоходахНДФЛ.Колонки.Удалить(СведенияОДоходахНДФЛ.Колонки.Индекс(СведенияОДоходахНДФЛ.Колонки.ЭтоДоговорГПХ));
	КонецЕсли;
		
	БазаРасчетаНДФЛ = ОтражениеЗарплатыВУчете.БазаДляРаспределенияНДФЛ(ПараметрыРаспределения);
	Если СведенияОДоходахНДФЛ <> Неопределено Тогда
		ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, "ВТРегистрНакопления_СведенияОДоходахНДФЛ");
		ЗарплатаКадры.СоздатьВТПоТаблицеЗначений(МенеджерВременныхТаблиц, СведенияОДоходахНДФЛ, "ВТРегистрНакопления_СведенияОДоходахНДФЛ");	
	КонецЕсли;
	
	РаспределениеНДФЛ = ОтражениеЗарплатыВУчете.НДФЛПоРабочимМестамИСтатьям(ТаблицаНДФЛ, БазаРасчетаНДФЛ, БазаВсеНачисления,
				СтрокиУжеУдержано, Организация, ПериодРегистрации);
				
	РаспределениеНДФЛ.Индексы.Добавить("ИдентификаторСтроки");
	ОтборБухучет = Новый Структура("ИдентификаторСтроки");
	РезультатРасчетаНДФЛ.Индексы.Добавить("ФизическоеЛицо,ДатаПолученияДохода,Территория,ВидУдержания,КатегорияДохода");
	Отбор = Новый Структура("ФизическоеЛицо,ДатаПолученияДохода,Территория,ВидУдержания,КатегорияДохода");
	Для каждого СтрокаТЗ Из ТаблицаНДФЛ Цикл
	
		ОтборБухучет.ИдентификаторСтроки = СтрокаТЗ.ИдентификаторСтроки;
		РаспределениеСтрокиНДФЛ = РаспределениеНДФЛ.НайтиСтроки(ОтборБухучет);
		Если РаспределениеСтрокиНДФЛ.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(Отбор, СтрокаТЗ);
		СтрокиНДФЛ = РезультатРасчетаНДФЛ.НайтиСтроки(Отбор);
		Если СтрокиНДФЛ.Количество() = 1 Тогда
			Для каждого СтрокаРаспределения Из РаспределениеСтрокиНДФЛ Цикл
				НоваяСтрока = БухучетНДФЛ.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРаспределения);
				НоваяСтрока.ИдентификаторСтроки = СтрокиНДФЛ[0].ИдентификаторСтроки;
			КонецЦикла;
		Иначе
			Для каждого СтрокаНДФЛ Из СтрокиНДФЛ Цикл
				// Распределяем пропорционально суммам в найденных строках.
				Коэффициенты = ОбщегоНазначения.ВыгрузитьКолонку(РаспределениеСтрокиНДФЛ, "Результат");
				// распределяем суммы с учетом ранее удержанного
				РаспределенныеСуммы = ЗарплатаКадры.РаспределитьСуммуПропорциональноБазе(СтрокаНДФЛ.Сумма, Коэффициенты, 0);
				Если РаспределенныеСуммы = Неопределено Тогда
					НоваяСтрока = БухучетНДФЛ.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, РаспределениеСтрокиНДФЛ[0]);
					НоваяСтрока.ИдентификаторСтроки = СтрокаНДФЛ.ИдентификаторСтроки;
					НоваяСтрока.Результат           = СтрокаНДФЛ.Сумма;
				Иначе
					Для Индекс = 0 По РаспределениеСтрокиНДФЛ.Количество() - 1 Цикл
						НоваяСтрока = БухучетНДФЛ.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрока, РаспределениеСтрокиНДФЛ[Индекс]);
						НоваяСтрока.ИдентификаторСтроки = СтрокаНДФЛ.ИдентификаторСтроки;
						НоваяСтрока.Результат           = РаспределенныеСуммы[Индекс];
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	
	КонецЦикла;
	
	// сортировка результатов
	УпорядочитьТаблицуРаспределениеУдержаний(БухучетНДФЛ);
	
	// заполним колонку КодСтатьиФинансирования в таблицах
	КодыСтатейФинансирования = КодыСтатейФинансирования();
	ЗаполнитьКодСтатьиФинансирования(БухучетНДФЛ, КодыСтатейФинансирования);
	
	ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, УдалитьВТ);
	
	Возврат БухучетНДФЛ;
	
КонецФункции

// Выполняет отражение КорректировокВыплаты в бухучете
//
// Параметры:
// 		ИсходныеДанные - структура, см ОписаниеИсходныхДанныхДляОтраженияУдержанийВБухучете
//				* МенеджерВременныхТаблиц
//				* ИмяВТБухучетНачислений
//				* ОкончательныйРасчет
//				* ПериодРегистрации
//				* Организация
//				* ИсключаемыйРегистратор
//				* РезультатРасчетаНДФЛ, описание см ОтражениеЗарплатыВУчете.НоваяТаблицаРезультатРасчетаНДФЛ
//				* НДФЛКЗачету, описание см НоваяТаблицаКорректировкиВыплаты
//				* НДФЛЗачтено, описание см НоваяТаблицаКорректировкиВыплаты.
//
// Возвращаемое значение: таблица значений с бухучетом Корректировок выплаты
//		описание таблицы см ОтражениеЗарплатыВУчете.НоваяТаблицаРаспределениеРезультатовУдержаний().
//
Функция ВыполнитьОтражениеКорректировокВыплатыВБухучете(ИсходныеДанные) Экспорт

	БухучетКорректировкиВыплаты  = ОтражениеЗарплатыВУчете.НоваяТаблицаРаспределениеРезультатовУдержаний();
	
	МенеджерВременныхТаблиц = ИсходныеДанные.МенеджерВременныхТаблиц;
	ПериодРегистрации       = ИсходныеДанные.МесяцНачисления;
	Организация      		= ИсходныеДанные.Организация;
	ИсключаемыйРегистратор  = ИсходныеДанные.ИсключаемыйРегистратор;
	РезультатРасчетаНДФЛ 	= ИсходныеДанные.РезультатРасчетаНДФЛ;
	БухучетНДФЛ 			= ИсходныеДанные.БухучетНДФЛ;
	НДФЛКЗачету		 		= ИсходныеДанные.НДФЛКЗачету;
	НДФЛЗачтено		 		= ИсходныеДанные.НДФЛЗачтено;
	
	УдалитьВТ = Новый Массив;
	
	Если НДФЛКЗачету.Количество() > 0 Тогда
		
		ФизическиеЛицаМассив = ОбщегоНазначения.ВыгрузитьКолонку(НДФЛКЗачету, "ФизическоеЛицо", Истина);
		База = ОтражениеЗарплатыВУчете.БазаДляРаспределенияНДФЛКЗачету(ФизическиеЛицаМассив, БухучетНДФЛ, РезультатРасчетаНДФЛ);
		БухучетНДФЛКЗачету = ОтражениеЗарплатыВУчете.КорректировкиВыплатыПоРабочимМестамИСтатьям(НДФЛКЗачету, База, Организация, ПериодРегистрации);
		
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(БухучетНДФЛКЗачету, БухучетКорректировкиВыплаты);
		
	КонецЕсли;
	
	Если НДФЛЗачтено.Количество()>0 Тогда
		
		ФизическиеЛицаМассив = ОбщегоНазначения.ВыгрузитьКолонку(НДФЛЗачтено, "ФизическоеЛицо", Истина);
		
		// НакопленныеКорректировки являются базой для распределения, нужно переименовать колонку "КорректировкаВыплаты".
		НакопленныеКорректировки = ВзаиморасчетыССотрудниками.КорректировкиВыплаты(Организация, ПериодРегистрации, ФизическиеЛицаМассив, ИсключаемыйРегистратор);
		НакопленныеКорректировки.Колонки.СуммаКорректировки.Имя = "Сумма";
		
		БухучетНДФЛЗачтено = ОтражениеЗарплатыВУчете.КорректировкиВыплатыПоРабочимМестамИСтатьям(НДФЛЗачтено, НакопленныеКорректировки, Организация, ПериодРегистрации);
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(БухучетНДФЛЗачтено, БухучетКорректировкиВыплаты);
		
	КонецЕсли;
	
	// сортировка результатов
	УпорядочитьТаблицуРаспределениеУдержаний(БухучетКорректировкиВыплаты);
	
	// заполним колонку КодСтатьиФинансирования в таблицах
	КодыСтатейФинансирования = КодыСтатейФинансирования();
	ЗаполнитьКодСтатьиФинансирования(БухучетКорректировкиВыплаты, КодыСтатейФинансирования);
	
	ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, УдалитьВТ);
	
	Возврат БухучетКорректировкиВыплаты;
	
КонецФункции

#КонецОбласти

#Область ОпределениеДоступностиДанных

Функция ДоступноЧтениеБухучетаЗарплатыТерриторийВыполненияРабот() Экспорт
	
	Возврат ПравоДоступа("Чтение", Метаданные.РегистрыСведений.БухучетЗарплатыТерриторийВыполненияРабот);
	
КонецФункции

Функция ДоступноИзменениеБухучетаЗарплатыТерриторийВыполненияРабот() Экспорт
	
	Возврат ПравоДоступа("Изменение", Метаданные.РегистрыСведений.БухучетЗарплатыТерриторийВыполненияРабот);
	
КонецФункции

Функция ДоступноИзменениеБухучетаЗарплатыПодразделений() Экспорт
	
	Возврат ПравоДоступа("Изменение", Метаданные.РегистрыСведений.БухучетЗарплатыПодразделений);
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ЗаполнитьБухучетНовойОрганизацииПриЗаписи(Источник, Отказ) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Константы.ИспользоватьСтатьиФинансированияЗарплата.Получить() Тогда
		Возврат;
	КонецЕсли;
	
	РаботаВБюджетномУчреждении = Константы.РаботаВБюджетномУчреждении.Получить();
	РаботаВХозрасчетнойОрганизации = Константы.РаботаВХозрасчетнойОрганизации.Получить();
	
	ЗаписьПоУмолчанию = Неопределено;
	Если РаботаВБюджетномУчреждении И ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.УчетБюджетныхУчреждений") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("УчетБюджетныхУчреждений");
		ЗаписьПоУмолчанию = Модуль.БухучетОрганизацииПоУмолчанию();
	ИначеЕсли РаботаВХозрасчетнойОрганизации И ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.УчетХозрасчетныхОрганизаций") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("УчетХозрасчетныхОрганизаций");
		ЗаписьПоУмолчанию = Модуль.БухучетОрганизацииПоУмолчанию();	
	КонецЕсли;
	
	Если ЗаписьПоУмолчанию = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	НаборЗаписей = РегистрыСведений.БухучетЗарплатыОрганизаций.СоздатьНаборЗаписей();
	НаборЗаписей.ДополнительныеСвойства.Вставить("ПропуститьПроверкуЗапретаИзменения");
	НаборЗаписей.Отбор.Организация.Установить(Источник.Ссылка);
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей.Прочитать();
	Если НаборЗаписей.Количество() = 0 Тогда
		
		НоваяЗапись = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, ЗаписьПоУмолчанию);
		НоваяЗапись.Организация = Источник.Ссылка;
		НаборЗаписей.Записать();
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура УстановитьВидОперацииПоЗарплатеЕжегодныйОтпускПриЗаписи(Источник, Отказ) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидОтпуска", Источник.Ссылка);
	Запрос.УстановитьПараметр("ОтпускЯвляетсяЕжегодным", Источник.ОтпускЯвляетсяЕжегодным);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Начисления.Ссылка,
	|	ВЫБОР
	|		КОГДА Начисления.КатегорияНачисленияИлиНеоплаченногоВремени В (ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.КомпенсацияОтпуска), ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ДенежноеСодержаниеКомпенсацияОтпуска))
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК КомпенсацияОтпуска
	|ИЗ
	|	ПланВидовРасчета.Начисления КАК Начисления
	|ГДЕ
	|	Начисления.ВидОтпуска = &ВидОтпуска
	|	И (&ОтпускЯвляетсяЕжегодным
	|				И НЕ Начисления.ВидОперацииПоЗарплате В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ЕжегодныйОтпуск), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпуска))
	|			ИЛИ НЕ &ОтпускЯвляетсяЕжегодным
	|				И Начисления.ВидОперацииПоЗарплате В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ЕжегодныйОтпуск), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпуска)))";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Источник.ОтпускЯвляетсяЕжегодным Тогда
			ВидОперации = ?(Выборка.КомпенсацияОтпуска,Перечисления.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпуска,Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпуск);
		Иначе
			ВидОперации = Перечисления.ВидыОперацийПоЗарплате.НачисленоДоход;
		КонецЕсли;
		ВидРасчетаОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ВидРасчетаОбъект.ВидОперацииПоЗарплате = ВидОперации;
		ВидРасчетаОбъект.ОбменДанными.Загрузка = Истина;
		ВидРасчетаОбъект.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

Процедура НастройкиСистемыНалогообложенияПриЗаписи(Источник, Отказ, Замещение) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);

	УстановитьИспользованиеЕНВД();
	
КонецПроцедуры

#КонецОбласти

#Область ОтражениеВБухучетеНачисленийКонтрагентов

// Возвращает структуру с описанием исходных данных для отражения начислений контрагентов в бухучете.
//
// Возвращаемое значение:
//  Структура - Ключ содержит имя параметра.
//
Функция ОписаниеИсходныхДанныхДляОтраженияНачисленийКонтрагентовВБухучете() Экспорт

	Параметры = Новый Структура(
	"Организация,
	|МесяцНачисления,
	|Подразделение,
	|Начисление,
	|БухучетПервичногоДокумента,
	|КоэффициентыРаспределения,
	|ТаблицаНачислений");
	
	Параметры.БухучетПервичногоДокумента = НоваяТаблицаБухучетЗарплатыПервичныхДокументов();
	
	Возврат Параметры;
	
КонецФункции

// Выполняет отражение начислений контрагентов в бухучете.
//
// Параметры:
//   ИсходныеДанные - структура, см ОписаниеИсходныхДанныхДляОтраженияНачисленийКонтрагентовВБухучете
//
// Возвращаемое значение:
//  ТаблицаЗначений - таблица значений с бухучетом начислений,
//		описание таблицы см НоваяТаблицаРаспределениеРезультатовНачислений().
//
Функция ОтражениеНачисленийКонтрагентовВБухучете(ИсходныеДанные) Экспорт

	Организация					= ИсходныеДанные.Организация;
	МесяцНачисления				= ИсходныеДанные.МесяцНачисления;
	Подразделение				= ИсходныеДанные.Подразделение;
	Начисление 					= ИсходныеДанные.Начисление;
	БухучетПервичногоДокумента	= ИсходныеДанные.БухучетПервичногоДокумента;
	КоэффициентыРаспределения 	= ИсходныеДанные.КоэффициентыРаспределения;
	ТаблицаНачислений 			= ИсходныеДанные.ТаблицаНачислений;
	
	РаботаВХозрасчетнойОрганизации = ПолучитьФункциональнуюОпцию("РаботаВХозрасчетнойОрганизации");
	ИспользоватьСтатьиФинансирования = ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный");
	ЕстьЕНВД = ОтражениеЗарплатыВБухучете.ПлательщикЕНВД(Организация, МесяцНачисления);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаСреза", МесяцНачисления);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Бухучет.СтатьяФинансирования КАК СтатьяФинансирования,
	|	Бухучет.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	Бухучет.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	ВЫБОР
	|		КОГДА Бухучет.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ЕНВД)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОблагаетсяЕНВД
	|ИЗ
	|	РегистрСведений.БухучетЗарплатыОрганизаций.СрезПоследних(&ДатаСреза, Организация = &Организация) КАК Бухучет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Бухучет.СтатьяФинансирования КАК СтатьяФинансирования,
	|	Бухучет.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	Бухучет.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Бухучет.ИдентификаторСтроки) КАК ИдентификаторСтроки,
	|	ВЫБОР
	|		КОГДА Бухучет.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ЕНВД)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОблагаетсяЕНВД
	|ИЗ
	|	(ВЫБРАТЬ
	|		Бухучет.СтатьяФинансирования КАК СтатьяФинансирования,
	|		Бухучет.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|		Бухучет.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|		Бухучет.ИдентификаторСтроки КАК ИдентификаторСтроки
	|	ИЗ
	|		РегистрСведений.БухучетЗарплатыПодразделений.СрезПоследних(&ДатаСреза, Подразделение = &Подразделение) КАК Бухучет) КАК Бухучет
	|
	|СГРУППИРОВАТЬ ПО
	|	Бухучет.СпособОтраженияЗарплатыВБухучете,
	|	Бухучет.СтатьяФинансирования,
	|	Бухучет.ОтношениеКЕНВД
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Бухучет.ИдентификаторСтроки) = 1";
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	НастройкиБухучетаПоУмолчанию = Новый Структура("СтатьяФинансирования,СпособОтраженияЗарплатыВБухучете,ОблагаетсяЕНВД,ОтношениеКЕНВД");
	
	ВыборкаПодразделение = РезультатЗапроса[1].Выбрать();
	ВыборкаПодразделение.Следующий();
	ЗаполнитьЗначенияСвойств(НастройкиБухучетаПоУмолчанию, ВыборкаПодразделение);
	
	ВыборкаОрганизация = РезультатЗапроса[0].Выбрать();
	ВыборкаОрганизация.Следующий();
	
	Если Не ЗначениеЗаполнено(НастройкиБухучетаПоУмолчанию.СпособОтраженияЗарплатыВБухучете) Тогда
		НастройкиБухучетаПоУмолчанию.СпособОтраженияЗарплатыВБухучете = ВыборкаОрганизация.СпособОтраженияЗарплатыВБухучете;
	КонецЕсли;
	
	ПоляБухучета = "СпособОтраженияЗарплатыВБухучете,СтатьяРасходов";
	Если ИспользоватьСтатьиФинансирования Тогда
		ПоляБухучета = ПоляБухучета + ",СтатьяФинансирования";
		Если Не ЗначениеЗаполнено(НастройкиБухучетаПоУмолчанию.СтатьяФинансирования) Тогда
			НастройкиБухучетаПоУмолчанию.СтатьяФинансирования = ВыборкаОрганизация.СтатьяФинансирования;
		КонецЕсли;
	КонецЕсли;
	Если ЕстьЕНВД Тогда
		ПоляБухучета = ПоляБухучета + ",ОблагаетсяЕНВД";
		Если Не ЗначениеЗаполнено(НастройкиБухучетаПоУмолчанию.ОтношениеКЕНВД) Тогда
			НастройкиБухучетаПоУмолчанию.ОблагаетсяЕНВД = ВыборкаОрганизация.ОблагаетсяЕНВД;
		КонецЕсли;
	КонецЕсли;
	БухучетПоУмолчанию = Новый Структура(ПоляБухучета);
	ЗаполнитьЗначенияСвойств(БухучетПоУмолчанию, НастройкиБухучетаПоУмолчанию);
	БухучетПоУмолчанию.СтатьяРасходов = СтатьяОплатаТруда();
	
	// Статья расходов по умолчанию, если РаботаВХозрасчетнойОрганизации передается из документа иначе определяется видом начисления.
	СтатьяРасходовПоУмолчанию = Неопределено;
	Если Не РаботаВХозрасчетнойОрганизации Тогда
		СтатьиРасходовПоВидамНачислений = Новый Соответствие;
		СтатьиРасходовНачисленийБюджет(Новый МенеджерВременныхТаблиц, Организация, МесяцНачисления, СтатьиРасходовПоВидамНачислений);
		СтатьяРасходовПоУмолчанию = СтатьиРасходовПоВидамНачислений[Начисление];
	КонецЕсли;
	
	// Настройки бухучета указанные в документе.
	КолонкиБухучетаДокумента = "";
	НастройкиБухучетДокумента = Новый Структура(ПоляБухучета);
	НайденныеСтроки = БухучетПервичногоДокумента.НайтиСтроки(Новый Структура("НачислениеУдержание", Начисление));
	Если НайденныеСтроки.Количество() > 0 Тогда
		
		КолонкиБухучет = Новый Массив;
		Если ИспользоватьСтатьиФинансирования И ЗначениеЗаполнено(НайденныеСтроки[0].СтатьяФинансирования) Тогда
			КолонкиБухучет.Добавить("СтатьяФинансирования");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(НайденныеСтроки[0].СпособОтраженияЗарплатыВБухучете) Тогда
			КолонкиБухучет.Добавить("СпособОтраженияЗарплатыВБухучете");
		КонецЕсли;
		
		Если ЕстьЕНВД И ЗначениеЗаполнено(НайденныеСтроки[0].ОтношениеКЕНВД) Тогда
			НастройкиБухучетДокумента.ОблагаетсяЕНВД = (НайденныеСтроки[0].ОтношениеКЕНВД = Перечисления.ОтношениеКЕНВДЗатратНаЗарплату.ЕНВД);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(НайденныеСтроки[0].СтатьяРасходов) Тогда
			Если РаботаВХозрасчетнойОрганизации Тогда
				СтатьяРасходовПоУмолчанию = НайденныеСтроки[0].СтатьяРасходов;
			Иначе
				КолонкиБухучет.Добавить("СтатьяРасходов");
			КонецЕсли;
		КонецЕсли;
		
		КолонкиБухучетаДокумента = СтрСоединить(КолонкиБухучет, ",");
		
		Если Не ПустаяСтрока(КолонкиБухучетаДокумента) Тогда
			ЗаполнитьЗначенияСвойств(НастройкиБухучетДокумента, НайденныеСтроки[0], КолонкиБухучетаДокумента);
		КонецЕсли;
		
	КонецЕсли;
	
	ТаблицаБухучетНачислений = НоваяТаблицаРаспределениеРезультатовНачислений();
	РаспределятьПоКоэффициентам = (КоэффициентыРаспределения <> Неопределено И КоэффициентыРаспределения.Количество() > 0);
	Если РаспределятьПоКоэффициентам Тогда
		ПоляКоэффициентов = ПоляБухучета + ",ФизическоеЛицо,ПодразделениеУчетаЗатрат";
		КоэффициентыРаспределения.Свернуть(ПоляКоэффициентов, "Коэффициент");
	КонецЕсли;
	
	ОтборКоэффициентов = Новый Структура("ФизическоеЛицо");
	
	Для каждого СтрокаТЗ Из ТаблицаНачислений Цикл
		
		НоваяСтрокаБухучета = ТаблицаБухучетНачислений.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаБухучета, СтрокаТЗ);
		НоваяСтрокаБухучета.Результат = СтрокаТЗ.Сумма;
		
		// Настройки бухучета по умолчанию.
		ЗаполнитьЗначенияСвойств(НоваяСтрокаБухучета, БухучетПоУмолчанию);
		
		Если ЗначениеЗаполнено(СтатьяРасходовПоУмолчанию) Тогда
			НоваяСтрокаБухучета.СтатьяРасходов = СтатьяРасходовПоУмолчанию;
		КонецЕсли;
		
		Если Не ПустаяСтрока(КолонкиБухучетаДокумента) Тогда
			// Задан бухучет в документе.
			ЗаполнитьЗначенияСвойств(НоваяСтрокаБухучета, НастройкиБухучетДокумента, КолонкиБухучетаДокумента);
			
		ИначеЕсли СтрокаТЗ.Сумма <> 0 И РаспределятьПоКоэффициентам Тогда
			
			ЗаполнитьЗначенияСвойств(ОтборКоэффициентов, СтрокаТЗ);
			СтрокиРаспределения = КоэффициентыРаспределения.НайтиСтроки(ОтборКоэффициентов);
			
			Коэффициенты = ОбщегоНазначения.ВыгрузитьКолонку(СтрокиРаспределения,"Коэффициент");
			Результаты = ЗарплатаКадрыКлиентСервер.РаспределитьПропорциональноКоэффициентам(СтрокаТЗ.Сумма, Коэффициенты);
			
			Если Результаты <> Неопределено Тогда
				
				Индекс = 0;
				Для Каждого СтрокаРаспределения Из СтрокиРаспределения Цикл
					
					СтрокаБухучета = ТаблицаБухучетНачислений.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаБухучета, НоваяСтрокаБухучета);
					
					СтрокаБухучета.Результат = Результаты[Индекс];
					
					Если ИспользоватьСтатьиФинансирования И ЗначениеЗаполнено(СтрокаРаспределения.СтатьяФинансирования) Тогда
						СтрокаБухучета.СтатьяФинансирования = СтрокаРаспределения.СтатьяФинансирования;
					КонецЕсли;
					
					Если ЗначениеЗаполнено(СтрокаРаспределения.СпособОтраженияЗарплатыВБухучете) Тогда
						СтрокаБухучета.СпособОтраженияЗарплатыВБухучете = СтрокаРаспределения.СпособОтраженияЗарплатыВБухучете;
					КонецЕсли;
					
					Если Не РаботаВХозрасчетнойОрганизации И Не ЗначениеЗаполнено(СтатьяРасходовПоУмолчанию) И ЗначениеЗаполнено(СтрокаРаспределения.СтатьяРасходов) Тогда
						// Заменяем статью расходов если не задана статья по умолчанию.
						СтрокаБухучета.СтатьяРасходов = СтрокаРаспределения.СтатьяРасходов;
					КонецЕсли;
					
					Если ЕстьЕНВД  Тогда
						СтрокаБухучета.ОблагаетсяЕНВД = СтрокаРаспределения.ОблагаетсяЕНВД;
					КонецЕсли;
					
					СтрокаБухучета.ПодразделениеУчетаЗатрат = СтрокаРаспределения.ПодразделениеУчетаЗатрат;
					
					Индекс = Индекс + 1;
					
				КонецЦикла;
				
				ТаблицаБухучетНачислений.Удалить(НоваяСтрокаБухучета);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ИспользоватьСтатьиФинансирования Тогда
		КодыСтатейФинансирования = КодыСтатейФинансирования();
		Для каждого СтрокаТЗ Из ТаблицаБухучетНачислений Цикл
			СтрокаТЗ.КодСтатьиФинансирования = КодыСтатейФинансирования[СтрокаТЗ.СтатьяФинансирования];
		КонецЦикла;
	КонецЕсли;
	
	ОтражениеЗарплатыВУчете.СвернутьТаблицу(ТаблицаБухучетНачислений);
	
	Возврат ТаблицаБухучетНачислений;
	
КонецФункции

#КонецОбласти

#Область ОбслуживаниеЕНВД

// Создает временную таблицу с данными ЕНВД.
//
// Параметры:
// 	МенеджерВТ - МенеджерВременныхТаблиц - содержит таблицу ВТНачисления с полями
// 				* Сотрудник
// 				* ДатаНачала
// 				* Подразделения
// 				* ТерриторияВыполненияРаботВОрганизации
// 	Организация - СправочникСсылка.Организации.
// 	Период - Дата.
//
Процедура СоздатьВТНачисленияСДаннымиЕНВДПоЕжемесячнойДоле(МенеджерВТ, Организация, Период) Экспорт
	
	ЕжемесячнаяДоляЕНВД = 0;
	ПроцентЕНВД = ОтражениеЗарплатыВБухучете.ПроцентЕНВД(Организация, Период);
	Если ПроцентЕНВД = 100 Тогда
		ЕжемесячнаяДоляЕНВД = 1;
	КонецЕсли;
	
	УдалитьВТ = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ЕжемесячнаяДоляЕНВД", ЕжемесячнаяДоляЕНВД);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Начисления.ДатаНачала КАК Период,
	|	&Организация КАК Организация,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.Подразделение КАК Подразделение,
	|	Начисления.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	АВТОНОМЕРЗАПИСИ() КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ВТСотрудникиДляПолученияНастроек
	|ИЗ
	|	ВТНачисления КАК Начисления";
	Запрос.Выполнить();
	УдалитьВТ.Добавить("ВТСотрудникиДляПолученияНастроек");
	
	СоздатьВТНастройкиБухучетаДляРаспределенияПоУмолчанию(МенеджерВТ, "ВТСотрудникиДляПолученияНастроек");
	УдалитьВТ.Добавить("ВТНастройкиБухучета");
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.Начисление КАК Начисление,
	|	Начисления.Подразделение КАК Подразделение,
	|	Начисления.ДатаНачала КАК ДатаНачала,
	|	ВЫБОР
	|		КОГДА НастройкиБухучета.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ОпределяетсяЕжемесячноПроцентом)
	|			ТОГДА &ЕжемесячнаяДоляЕНВД
	|		КОГДА НастройкиБухучета.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ЕНВД)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ДоляЕНВД
	|ПОМЕСТИТЬ ВТНачисленияСДаннымиЕНВД
	|ИЗ
	|	ВТНачисления КАК Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиБухучета КАК НастройкиБухучета
	|		ПО Начисления.Сотрудник = НастройкиБухучета.Сотрудник
	|			И Начисления.Подразделение = НастройкиБухучета.Подразделение
	|			И Начисления.ДатаНачала = НастройкиБухучета.Период
	|			И Начисления.ТерриторияВыполненияРаботВОрганизации = НастройкиБухучета.ТерриторияВыполненияРаботВОрганизации";
	
	Запрос.Выполнить();
	
	ЗарплатаКадры.УничтожитьВТ(МенеджерВТ, УдалитьВТ);
	
КонецПроцедуры

// Выполняет проверку необходимости регистрации процента ЕНВД перед расчетом страховых взносов.
// Проверяется наличие настроек отношения к ЕНВД как "определяется ежемесячно процентом". 
//
// Параметры:
// 	МенеджерВТ - МенеджерВременныхТаблиц - содержит таблицу начислений с именем ИмяИсходнойВТ с полями
// 				* Сотрудник
// 				* Период
// 				* Подразделения
// 				* ТерриторияВыполненияРаботВОрганизации
// 				* Начисление
// 	ИмяИсходнойВТ - Строка - имя временной таблицы с начислениями сотрудников.
// 	ИменаПолейВТ - Строка - имена полей сотрудник и период, через запятую, в таблице с именем ИмяИсходнойВТ.
// 	Организация - СправочникСсылка.Организации.
// 	Период - Дата.
//
Функция ТребуетсяРегистрацияПроцентаЕНВД(МенеджерВТ, ИмяИсходнойВТ, Организация, Период) Экспорт
	
	УдалитьВТ = Новый Массив;
	
	// Подготовим временную таблицу для получения настроек бухучета.
	Параметры = ПараметрыПолученияНастроекБухучета();
	Параметры.ИмяПоляПериод 	= "ДатаНачала";
	СоздатьВТИсходнаяТаблицаДляНастроекБухучета(МенеджерВТ, ИмяИсходнойВТ, Период, Параметры);
	УдалитьВТ.Добавить("ВТИсходнаяТаблицаДляНастроекБухучета");
	
	// Настройки получаем с учетом зарегистрированных распределений основного заработка.
	СоздатьВТНастройкиБухучета(МенеджерВТ, Организация, Период, Истина);
	УдалитьВТ.Добавить("ВТНастройкиБухучета");
	УдалитьВТ.Добавить("ВТНастройкиБухучетаПоУмолчанию");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиБухучета.ОтношениеКЕНВД КАК ОтношениеКЕНВД
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ВЫБОР
	|			КОГДА ЕСТЬNULL(НастройкиНачислений.ОтношениеКЕНВД, ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)
	|				ТОГДА НастройкиБухучета.ОтношениеКЕНВД
	|			ИНАЧЕ НастройкиНачислений.ОтношениеКЕНВД
	|		КОНЕЦ КАК ОтношениеКЕНВД
	|	ИЗ
	|		ИмяВременнойТаблицы КАК Таблица
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиБухучета КАК НастройкиБухучета
	|			ПО Таблица.Сотрудник = НастройкиБухучета.Сотрудник
	|				И Таблица.Подразделение = НастройкиБухучета.Подразделение
	|				И Таблица.ТерриторияВыполненияРаботВОрганизации = НастройкиБухучета.ТерриторияВыполненияРаботВОрганизации
	|				И Таблица.ДатаНачала = НастройкиБухучета.Период
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления КАК НастройкиНачислений
	|			ПО Таблица.Начисление = НастройкиНачислений.Ссылка
	|				И (НастройкиНачислений.СтратегияОтраженияВУчете = ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.КакЗаданоВидуРасчета))) КАК НастройкиБухучета
	|ГДЕ
	|	НастройкиБухучета.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ОпределяетсяЕжемесячноПроцентом)";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ИмяВременнойТаблицы", ИмяИсходнойВТ);
	Результат = Запрос.Выполнить();
	
	ЗарплатаКадры.УничтожитьВТ(МенеджерВТ, УдалитьВТ);
	
	Возврат Не Результат.Пустой();

КонецФункции

#КонецОбласти

#Область КонструкторыСлужебногоПрограммногоИнтерфейса

Функция НоваяТаблицаБухучетЗарплатыПервичныхДокументов() Экспорт
	
	МассивТиповНачисленияУдержания = Новый Массив;
	МассивТиповНачисленияУдержания.Добавить(Тип("ПеречислениеСсылка.ВидыОсобыхНачисленийИУдержаний"));
	МассивТиповНачисленияУдержания.Добавить(Тип("ПланВидовРасчетаСсылка.Начисления"));
	МассивТиповНачисленияУдержания.Добавить(Тип("ПланВидовРасчетаСсылка.Удержания"));
	МассивТиповНачисленияУдержания.Добавить(Тип("СправочникСсылка.ВидыВыплатБывшимСотрудникам"));
	МассивТиповНачисленияУдержания.Добавить(Тип("СправочникСсылка.ВидыПрочихДоходовФизическихЛиц"));
	
	МассивТиповДокументОснование = Метаданные.РегистрыСведений.БухучетЗарплатыПервичныхДокументов.Измерения.ДокументОснование.Тип.Типы();
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("ДокументОснование",  	Новый ОписаниеТипов(МассивТиповДокументОснование));
	Таблица.Колонки.Добавить("НачислениеУдержание", Новый ОписаниеТипов(МассивТиповНачисленияУдержания));  
	Таблица.Колонки.Добавить("СпособОтраженияЗарплатыВБухучете",  Новый ОписаниеТипов("СправочникСсылка.СпособыОтраженияЗарплатыВБухучете"));
	Таблица.Колонки.Добавить("ОтношениеКЕНВД",  		Новый ОписаниеТипов("ПеречислениеСсылка.ОтношениеКЕНВДЗатратНаЗарплату"));
	Таблица.Колонки.Добавить("СтатьяФинансирования",  	Новый ОписаниеТипов("СправочникСсылка.СтатьиФинансированияЗарплата"));
	Таблица.Колонки.Добавить("СтатьяРасходов",  		Новый ОписаниеТипов("СправочникСсылка.СтатьиРасходовЗарплата"));
	Таблица.Колонки.Добавить("ДоляРаспределения", 		Новый ОписаниеТипов(Метаданные.ОпределяемыеТипы.ЗначениеДоляРаспределенияБухучетЗарплаты.Тип));
	Таблица.Колонки.Добавить("ИдентификаторСтроки", 	Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(3, 0)));
	
	Возврат Таблица;
КонецФункции

Функция ОписаниеИсходныхДанныхДляОтраженияНачисленийВБухучете() Экспорт

	Параметры = Новый Структура(
	"Организация,
	|МесяцНачисления,
	|МенеджерВременныхТаблиц,
	|МенеджерКадровогоУчета,
	|ИмяВТБухучетНачислений,
	|ИмяВТНачисления,
	|СтрокиБухучетСторноНачислений,
	|БухучетПервичногоДокумента,
	|ИсключаемыйРегистратор,
	|СтрокиКоэффициентыСреднегоЗаработка,
	|СтрокиКоэффициентыСохраняемогоДС,
	|КоэффициентыСреднегоЗаработкаДокумента,
	|КоэффициентыСреднегоЗаработкаФССДокумента,
	|КоэффициентыРаспределенияДенежногоСодержания,
	|ВидыОперацийРасходыФСС,
	|МенеджерДанныхУчетаВремени,
	|РаспределениеСохраняемогоДС,
	|СоответствиеСотрудников,
	|ДатаНачалаДляБухучета,
	|МенеджерРасчетаЗарплаты,
	|РегистраторТекущегоРасчета,
	|ИсточникДанныхОДатахНачислений");
	
	Параметры.СтрокиБухучетСторноНачислений = Новый Соответствие;
	Параметры.БухучетПервичногоДокумента 	= НоваяТаблицаБухучетЗарплатыПервичныхДокументов();
	Параметры.СтрокиКоэффициентыСреднегоЗаработка 		= Новый Соответствие;
	Параметры.КоэффициентыСреднегоЗаработкаДокумента 	= Новый Соответствие;
	Параметры.КоэффициентыСреднегоЗаработкаФССДокумента = НоваяТаблицаКоэффициентыРаспределенияСреднегоЗаработкаФСС();
	Параметры.ИмяВТНачисления 				= "ВТНачисленияДляРаспределения";
	Параметры.РаспределениеСохраняемогоДС 	= Ложь;
	Параметры.СоответствиеСотрудников 	  	= Новый Соответствие;
	Параметры.ДатаНачалаДляБухучета 		= Дата(0001,1,1);
	
	// Объект в котором есть метод ДатыНачисленийОплачивающихВидыВремени,
	Параметры.ИсточникДанныхОДатахНачислений = Неопределено;
	
	// Виды операций, которые обрабатываются при получении бухучета как расходы ФСС.
	Параметры.ВидыОперацийРасходыФСС = ВидыОперацийРасходыФСС();
	
	Возврат Параметры;	

КонецФункции 

Функция ОписаниеИсходныхДанныхДляОтраженияУдержанийВБухучете() Экспорт

	Параметры = Новый Структура(
	"Организация,
	|МесяцНачисления,
	|МенеджерВременныхТаблиц,
	|ИмяВТБухучетНачислений,
	|ОкончательныйРасчет,
	|ИсключаемыйРегистратор,
	|ВидыНачисленийДополненияРасчетнойБазы,
	|ТаблицаУдержаний,
	|ТаблицаЗаймов,
	|РезультатРасчетаНДФЛ,
	|БухучетНДФЛ,
	|НДФЛКЗачету,
	|НДФЛЗачтено,
	|РегистраторыУдержанийОбновленияБухучета,
	|ОснованияУчтенныеПриРасчетеНДФЛ,
	|ЗасчитыватьДанныеАвансовНДФЛ,
	|ПерваяПоловинаМесяца");
	
	Возврат Параметры;	

КонецФункции

Функция НоваяТаблицаКоэффициентыРаспределенияСреднегоЗаработка() Экспорт

	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Начисление", 						 Новый ОписаниеТипов("ПланВидовРасчетаСсылка.Начисления"));
	Таблица.Колонки.Добавить("СтатьяФинансирования", 			 Новый ОписаниеТипов("СправочникСсылка.СтатьиФинансированияЗарплата"));
	Таблица.Колонки.Добавить("СтатьяРасходов", 					 Новый ОписаниеТипов("СправочникСсылка.СтатьиРасходовЗарплата"));
	Таблица.Колонки.Добавить("СпособОтраженияЗарплатыВБухучете", Новый ОписаниеТипов("СправочникСсылка.СпособыОтраженияЗарплатыВБухУчете"));
	Таблица.Колонки.Добавить("ОблагаетсяЕНВД", 					 Новый ОписаниеТипов("Булево"));
	Таблица.Колонки.Добавить("Коэффициент", 		 			 Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	
	Возврат Таблица;

КонецФункции

Функция НоваяТаблицаРаспределениеРезультатовНачислений() Экспорт
	
	Таблица = Новый ТаблицаЗначений;
	
	Таблица.Колонки.Добавить("ИдентификаторСтроки", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(7, 0)));
	Таблица.Колонки.Добавить("ПодразделениеУчетаЗатрат", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	Таблица.Колонки.Добавить("Территория", Новый ОписаниеТипов(Метаданные.ОпределяемыеТипы.ТерриторияВыполненияРаботВОрганизации.Тип));
	Таблица.Колонки.Добавить("СтатьяФинансирования", Новый ОписаниеТипов("СправочникСсылка.СтатьиФинансированияЗарплата"));
	Таблица.Колонки.Добавить("СтатьяРасходов", Новый ОписаниеТипов("СправочникСсылка.СтатьиРасходовЗарплата"));
	Таблица.Колонки.Добавить("СпособОтраженияЗарплатыВБухучете", Новый ОписаниеТипов("СправочникСсылка.СпособыОтраженияЗарплатыВБухУчете"));
	Таблица.Колонки.Добавить("ОблагаетсяЕНВД", Новый ОписаниеТипов("Булево"));
	Таблица.Колонки.Добавить("Результат", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	Таблица.Колонки.Добавить("КодСтатьиФинансирования", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(7)));
	
	Возврат Таблица;
	
КонецФункции

Функция НоваяТаблицаБухучетНачислений() Экспорт

	ОписаниеТипаДата = Новый ОписаниеТипов("Дата",Новый КвалификаторыДаты(ЧастиДаты.Дата));
	
	МассивТиповНачисления = Новый Массив;
	МассивТиповНачисления.Добавить(Тип("ПеречислениеСсылка.ВидыОсобыхНачисленийИУдержаний"));
	МассивТиповНачисления.Добавить(Тип("ПланВидовРасчетаСсылка.Начисления"));
	МассивТиповНачисления.Добавить(Тип("СправочникСсылка.ВидыВыплатБывшимСотрудникам"));
	МассивТиповНачисления.Добавить(Тип("СправочникСсылка.ВидыПрочихДоходовФизическихЛиц"));
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("ИдентификаторСтроки", 				Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(7,0)));
	Таблица.Колонки.Добавить("ИдентификаторСтрокиНачисления", 		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(7,0)));
	Таблица.Колонки.Добавить("Организация",  						Новый ОписаниеТипов("СправочникСсылка.Организации"));
	Таблица.Колонки.Добавить("ФизическоеЛицо", 						Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	Таблица.Колонки.Добавить("Сотрудник",  							Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	Таблица.Колонки.Добавить("Подразделение", 						Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	Таблица.Колонки.Добавить("ПодразделениеУчетаЗатрат", 			Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	Таблица.Колонки.Добавить("Начисление", 							Новый ОписаниеТипов(МассивТиповНачисления));
	Таблица.Колонки.Добавить("ДатаНачала", 							ОписаниеТипаДата);
	Таблица.Колонки.Добавить("ДатаОкончания", 						ОписаниеТипаДата);
	Таблица.Колонки.Добавить("ПериодДействия", 						ОписаниеТипаДата);
	Таблица.Колонки.Добавить("Территория",							Новый ОписаниеТипов(Метаданные.ОпределяемыеТипы.ТерриторияВыполненияРаботВОрганизации.Тип));
	Таблица.Колонки.Добавить("МестоПолученияДохода", 				Новый ОписаниеТипов(Метаданные.ОпределяемыеТипы.ТерриторияВыполненияРаботВОрганизации.Тип));
	Таблица.Колонки.Добавить("СтатьяФинансирования",				Новый ОписаниеТипов("СправочникСсылка.СтатьиФинансированияЗарплата"));
	Таблица.Колонки.Добавить("СтатьяРасходов",  					Новый ОписаниеТипов("СправочникСсылка.СтатьиРасходовЗарплата"));
	Таблица.Колонки.Добавить("ОблагаетсяЕНВД",						Новый ОписаниеТипов("Булево"));
	Таблица.Колонки.Добавить("СпособОтраженияЗарплатыВБухучете", 	Новый ОписаниеТипов("СправочникСсылка.СпособыОтраженияЗарплатыВБухучете"));
	
	Таблица.Колонки.Добавить("Сумма", 		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	Таблица.Колонки.Добавить("Результат",	Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	
	Возврат Таблица;

КонецФункции

Функция НоваяТаблицаКоэффициентыРаспределенияСохраняемогоДС() Экспорт

	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Сотрудник", 						 Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	Таблица.Колонки.Добавить("Начисление", 						 Новый ОписаниеТипов("ПланВидовРасчетаСсылка.Начисления"));
	Таблица.Колонки.Добавить("НазначениеРасчета", 				 Новый ОписаниеТипов("ПеречислениеСсылка.НазначениеРасчетаСохраняемогоДенежногоСодержания"));
	Таблица.Колонки.Добавить("ПодразделениеУчетаЗатрат", 		 Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	Таблица.Колонки.Добавить("СтатьяФинансирования", 			 Новый ОписаниеТипов("СправочникСсылка.СтатьиФинансированияЗарплата"));
	Таблица.Колонки.Добавить("СтатьяРасходов", 					 Новый ОписаниеТипов("СправочникСсылка.СтатьиРасходовЗарплата"));
	Таблица.Колонки.Добавить("СпособОтраженияЗарплатыВБухучете", Новый ОписаниеТипов("СправочникСсылка.СпособыОтраженияЗарплатыВБухУчете"));
	Таблица.Колонки.Добавить("ОблагаетсяЕНВД", 					 Новый ОписаниеТипов("Булево"));
	Таблица.Колонки.Добавить("Коэффициент", 		 			 Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	
	Возврат Таблица;

КонецФункции

// Конструктор структуры параметров используемых при вызове метода заполнения
// документов ОтражениеЗарплатыВБухучете.
//
// Возвращаемое значение:
//	Структура
//
Функция ПараметрыДляЗаполненияТаблицДокумента() Экспорт

	Параметры = Новый Структура("
	|Организация,
	|ПериодРегистрации,
	|ДокументСсылка,
	|СтатьиФинансирования,
	|Подразделение");
	
	Возврат Параметры;

КонецФункции

Функция НоваяТаблицаКоэффициентыРаспределенияСреднегоЗаработкаФСС() Экспорт

	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Начисление", 			 			 Новый ОписаниеТипов(Метаданные.ОпределяемыеТипы.ВидыНачисленийСреднегоЗаработкаФСС.Тип));
	Таблица.Колонки.Добавить("СтатьяФинансирования", 			 Новый ОписаниеТипов("СправочникСсылка.СтатьиФинансированияЗарплата"));
	Таблица.Колонки.Добавить("СтатьяРасходов", 					 Новый ОписаниеТипов("СправочникСсылка.СтатьиРасходовЗарплата"));
	Таблица.Колонки.Добавить("СпособОтраженияЗарплатыВБухучете", Новый ОписаниеТипов("СправочникСсылка.СпособыОтраженияЗарплатыВБухУчете"));
	Таблица.Колонки.Добавить("ОблагаетсяЕНВД", 					 Новый ОписаниеТипов("Булево"));
	Таблица.Колонки.Добавить("Коэффициент", 		 			 Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	
	Возврат Таблица;

КонецФункции

#КонецОбласти

#Область ПроверкаРаспределенияПоИсточникамФинансирования

// Выполняет проверку заполнения полей в результатах распределения по статьям, если есть ошибки заполнения,
// значению параметра Отказ буден установлено Истина.
// Вызывается из обработчиков проверки заполнения объектов.
//
// Параметры
// 			ДокументОбъект - ДокументОбъект - документ, табличные части которого проверяются.
// 			ИменаТаблиц - Строка - имена табличных частей документа для проверки, например "Начисления,НачисленияПерерасчет,Удержания,НДФЛ".
// 			Отказ - Булево - если есть ошибки заполнение, параметру будет установлено значение Истина.
// 			ВидНачисленияВШапке - ПеречислениеСсылка.ВидыОсобыхНачисленийИУдержаний, 
// 								ПланВидовРасчетаСсылка.Удержания, ПланВидовРасчетаСсылка.Начисления,
// 								СправочникСсылка.ВидыВыплатБывшимСотрудникам,
// 								СправочникСсылка.ВидыПрочихДоходовФизическихЛиц - необязательный.
//
Процедура ПроверитьРезультатыРаспределенияНачисленийУдержанийОбъекта(ДокументОбъект, ИменаТаблиц, Отказ, ВидНачисленияВШапке = Неопределено, БухучетПрочихДоходов = Ложь) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата") Тогда
		Возврат;
	КонецЕсли;
	
	НачислениеУдержаниеВидОперации = ОтражениеЗарплатыВУчете.НачислениеУдержаниеВидОперации();
	
	ТаблицаРезультатовПроверки = Новый ТаблицаЗначений;
	ТаблицаРезультатовПроверки.Колонки.Добавить("ИмяТаблицы",  Новый ОписаниеТипов("Строка"));
	ТаблицаРезультатовПроверки.Колонки.Добавить("НомерСтроки",  Новый ОписаниеТипов("Число"));
	ТаблицаРезультатовПроверки.Колонки.Добавить("ЕстьОшибкиЗаполнения",  Новый ОписаниеТипов("Булево"));
	ТаблицаРезультатовПроверки.Колонки.Добавить("ЕстьОшибкиРаспределения",  Новый ОписаниеТипов("Булево"));
	
	РаботаВБюджетномУчреждении = ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении");
	
	ИменаТаблицРаспределенияРезультатов = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИменаТаблиц, ",", Истина, Истина);
	
	Для Каждого ИмяТаблицыНачисленийУдержаний Из ИменаТаблицРаспределенияРезультатов Цикл
		
		ИмяТаблицыРаспределениеРезультатовРасчета = ИмяТаблицыДляРаспределенияРезультата(ИмяТаблицыНачисленийУдержаний);
		ТаблицаРаспределенияРезультатовИзХранилища = ДокументОбъект[ИмяТаблицыРаспределениеРезультатовРасчета];
		
		ИмяРеквизитаИдентификаторСтроки = ИмяРеквизитаИдентификаторСтрокиПоИмениТаблицы(ИмяТаблицыНачисленийУдержаний);
		
		Для каждого СтрокаТЧ Из ДокументОбъект[ИмяТаблицыНачисленийУдержаний] Цикл
			
			Отбор = Новый Структура("ИдентификаторСтроки", СтрокаТЧ[ИмяРеквизитаИдентификаторСтроки]);
			РезультатыРаспределения = ТаблицаРаспределенияРезультатовИзХранилища.НайтиСтроки(Отбор);
			
			РезультатПроверкиСтроки = ПроверитьРезультатыРаспределенияНачисленийУдержанийВСтрокеОбъекта(
				СтрокаТЧ, РезультатыРаспределения, ИмяТаблицыНачисленийУдержаний, НачислениеУдержаниеВидОперации,
				РаботаВБюджетномУчреждении, ВидНачисленияВШапке, БухучетПрочихДоходов);
			
			Если РезультатПроверкиСтроки.ЕстьОшибкиЗаполнения Или РезультатПроверкиСтроки.ЕстьОшибкиРаспределения Тогда
				
				НоваяСтрока = ТаблицаРезультатовПроверки.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, РезультатПроверкиСтроки);
				НоваяСтрока.НомерСтроки = СтрокаТЧ.НомерСтроки;
				НоваяСтрока.ИмяТаблицы  = ИмяТаблицыНачисленийУдержаний;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Если ТаблицаРезультатовПроверки.Количество() > 0 Тогда
		
		МаксимальноСообщений = 10;
		ВыводимыхСообщений = 0;
		НевыводимыхСообщений = 0;
		
		МетаданныеДокумента = ДокументОбъект.Ссылка.Метаданные();
		ПредставленияТаблиц = Новый Соответствие;
		
		Для каждого СтрокаТаблицы Из ТаблицаРезультатовПроверки Цикл
			
			Отказ = Истина;
			
			ПредставлениеТаблицы = ПредставленияТаблиц.Получить(СтрокаТаблицы.ИмяТаблицы);
			Если ПредставлениеТаблицы = Неопределено Тогда
				
				ПредставлениеТаблицы = МетаданныеДокумента.ТабличныеЧасти.Найти(СтрокаТаблицы.ИмяТаблицы).Синоним;
				ПредставленияТаблиц.Вставить(СтрокаТаблицы.ИмяТаблицы, ПредставлениеТаблицы);
				
			КонецЕсли;
			
			Если ВыводимыхСообщений > МаксимальноСообщений Тогда
				НевыводимыхСообщений = НевыводимыхСообщений + 1;
			Иначе
				
				ОбщегоНазначения.СообщитьПользователю(
					СтрШаблон(
						НСтр("ru = 'В строке %1 таблицы %2 обнаружены незаполненные поля при распределении результата';
							|en = 'In row %1 of table %2, unpopulated fields were found when allocating the result'"),
						СтрокаТаблицы.НомерСтроки,
						ПредставлениеТаблицы),, 
					ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
						СтрШаблон("Объект.%1", СтрокаТаблицы.ИмяТаблицы),
						СтрокаТаблицы.НомерСтроки,
						"КомандаРедактированияРаспределения"),, 
					Отказ);
				ВыводимыхСообщений = ВыводимыхСообщений + 1;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ЗначенияСтатейРасходовПоУмолчанию

// Возвращает ссылку на элемент справочника СтатьиРасходовЗарплата
//	соответствующий способу расчетов ОплатаТруда.
Функция СтатьяОплатаТруда() Экспорт 
	
	Если ПолучитьФункциональнуюОпцию("РаботаВХозрасчетнойОрганизации") Тогда
		ОписаниеСтатейРасходов = ЗарплатаКадры.СтатьиРасходовПоСпособамРасчетовСФизическимиЛицами();
		ОплатаТруда = ОписаниеСтатейРасходов[Перечисления.СпособыРасчетовСФизическимиЛицами.ОплатаТруда];
	Иначе
		ОплатаТруда = СтатьяРасходов211();
	КонецЕсли;
	
	Возврат ОплатаТруда;
	
КонецФункции

// Возвращает ссылку на элемент справочника СтатьиРасходовЗарплата с кодом 211.
Функция СтатьяРасходов211() Экспорт
	
	Статья211 = Справочники.СтатьиРасходовЗарплата.ПустаяСсылка();
	Если ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		Статья211 = Справочники.СтатьиРасходовЗарплата.НайтиПоКоду("211");
	КонецЕсли;

	Возврат Статья211;
	
КонецФункции

// Возвращает ссылку на элемент справочника СтатьиРасходовЗарплата с кодом 212.
Функция СтатьяРасходов212() Экспорт
	
	Статья = Справочники.СтатьиРасходовЗарплата.ПустаяСсылка();
	Если ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		Статья = Справочники.СтатьиРасходовЗарплата.НайтиПоКоду("212");
	КонецЕсли;

	Возврат Статья;
	
КонецФункции

// Возвращает ссылку на элемент справочника СтатьиРасходовЗарплата с кодом 213.
Функция СтатьяРасходов213() Экспорт
	
	Статья213 = Справочники.СтатьиРасходовЗарплата.ПустаяСсылка();
	Если ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		Статья213 = Справочники.СтатьиРасходовЗарплата.НайтиПоКоду("213");
	КонецЕсли;
	
	Возврат Статья213;
	
КонецФункции 

// Возвращает ссылку на элемент справочника СтатьиРасходовЗарплата с кодом 214.
Функция СтатьяРасходов214() Экспорт
	
	Статья214 = Справочники.СтатьиРасходовЗарплата.ПустаяСсылка();
	Если ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		Статья214 = Справочники.СтатьиРасходовЗарплата.НайтиПоКоду("214");
	КонецЕсли;
	
	Возврат Статья214;
	
КонецФункции

// Возвращает ссылку на элемент справочника СтатьиРасходовЗарплата с кодом 266.
Функция СтатьяРасходов266() Экспорт
	
	Статья = Справочники.СтатьиРасходовЗарплата.ПустаяСсылка();
	Если ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		Статья = Справочники.СтатьиРасходовЗарплата.НайтиПоКоду("266");
	КонецЕсли;

	Возврат Статья;
	
КонецФункции

// Возвращает ссылку на элемент справочника СтатьиРасходовЗарплата с кодом 264.
Функция СтатьяРасходов264() Экспорт
	
	Статья = Справочники.СтатьиРасходовЗарплата.ПустаяСсылка();
	Если ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		Статья = Справочники.СтатьиРасходовЗарплата.НайтиПоКоду("264");
	КонецЕсли;

	Возврат Статья;
	
КонецФункции

// Возвращает статью расходов, которая используется по умолчанию для 
//	сохраняемого заработка на период трудоустройства.
//
// Параметры:
//  ПериодРегистрации - дата, на которую определяется статья.
//
// Возвращаемое значение:
//   Статья расходов - тип СправочникСсылка.СтатьиРасходовЗарплата.
//
Функция СтатьяРасходовДляСохраняемогоЗаработкаНаПериодТрудоустройства(ПериодРегистрации) Экспорт

	Если СтатьиРасходовПоПриказу209нПрименяются(ПериодРегистрации) Тогда
		Возврат СтатьяРасходов264();
	Иначе
		Возврат СтатьяРасходов211();
	КонецЕсли;
	
КонецФункции

// Возвращает статью расходов, которая используется по умолчанию для 
//	компенсации за задержку зарплаты в бюджетных учреждениях.
//
// Параметры:
//  ПериодРегистрации - дата, на которую определяется статья.
//
// Возвращаемое значение:
//   Статья расходов - тип СправочникСсылка.СтатьиРасходовЗарплата.
//
Функция СтатьяРасходовДляКомпенсацииЗаЗадержкуЗарплаты(ПериодРегистрации) Экспорт

	Если Не ЗначениеЗаполнено(ПериодРегистрации) Или ПериодРегистрации >= ДатаПриказа222н() Тогда
		Возврат СтатьяРасходов296();
	Иначе
		Возврат СтатьяРасходов290();
	КонецЕсли;
	
КонецФункции

// Возвращает статью расходов, которая используется по умолчанию для 
//	пособия на погребение, выплачиваемое стороннему лицу.
//
// Параметры:
//  ПериодРегистрации - дата, на которую определяется статья.
//
// Возвращаемое значение:
//   Статья расходов - тип СправочникСсылка.СтатьиРасходовЗарплата.
//
Функция СтатьяРасходовДляПособияНаПогребение(ПериодРегистрации) Экспорт

	Если Не ЗначениеЗаполнено(ПериодРегистрации) Или ПериодРегистрации >= ДатаПриказа222н() Тогда
		Возврат СтатьяРасходов265();
	Иначе
		Возврат СтатьяРасходов213();
	КонецЕсли;	

КонецФункции

// Возвращает ссылку на элемент справочника СтатьиРасходовЗарплата
//	соответствующий способу расчетов РасчетыСКонтрагентами.
Функция СтатьяРасчетыСКонтрагентами() Экспорт 
	
	Если ПолучитьФункциональнуюОпцию("РаботаВХозрасчетнойОрганизации") Тогда
		ОписаниеСтатейРасходов = ЗарплатаКадры.СтатьиРасходовПоСпособамРасчетовСФизическимиЛицами();
		РасчетыСКонтрагентами = ОписаниеСтатейРасходов[Перечисления.СпособыРасчетовСФизическимиЛицами.РасчетыСКонтрагентами];
	Иначе
		РасчетыСКонтрагентами = Справочники.СтатьиРасходовЗарплата.ПустаяСсылка();
	КонецЕсли;
	
	Возврат РасчетыСКонтрагентами;
	
КонецФункции

// Возвращает ссылку на элемент справочника СтатьиРасходовЗарплата
//	соответствующий способу расчетов ПрочиеРасчетыСПерсоналом.
Функция СтатьяПрочиеРасчетыСПерсоналом() Экспорт 
	
	Если ПолучитьФункциональнуюОпцию("РаботаВХозрасчетнойОрганизации") Тогда
		ОписаниеСтатейРасходов = ЗарплатаКадры.СтатьиРасходовПоСпособамРасчетовСФизическимиЛицами();
		ПрочиеРасчетыСПерсоналом = ОписаниеСтатейРасходов[Перечисления.СпособыРасчетовСФизическимиЛицами.ПрочиеРасчетыСПерсоналом];
	Иначе
		ПрочиеРасчетыСПерсоналом = Справочники.СтатьиРасходовЗарплата.ПустаяСсылка();
	КонецЕсли;
	
	Возврат ПрочиеРасчетыСПерсоналом;
	
КонецФункции

#КонецОбласти

#Область ОбслуживаниеРедактированияБухучетаВДокументах

Функция ПредставлениеРаспределенияБухучетаДокумента(НастройкиБухучета, ЕстьПодразделение = Ложь) Экспорт

	Если НастройкиБухучета.Количество() = 0 Тогда
		ТекстПредставления = НСтр("ru = '<распределение не задано>';
									|en = '<allocation is not set>'");
	Иначе
		
		РаботаВБюджетномУчреждении = ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении");
		ИспользоватьСтатьиФинансирования = ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный");
		ТекстПредставления = "";
		
		КодыСтатейФинансирования = Новый Соответствие;
		Если ИспользоватьСтатьиФинансирования Тогда
			КодыСтатейФинансирования = КодыСтатейФинансирования();
		КонецЕсли;
		
		НомСтроки = 1;
		Для каждого СтрокаТЧ Из НастройкиБухучета Цикл
			
			Если НомСтроки > 3 Тогда
				ТекстПредставления = СтрШаблон("%1%2", ТекстПредставления, "...");
				Прервать;
			КонецЕсли;
			
			ПредставлениеПодразделения 			= "";
			ПредставлениеСтатьиФинансирования 	= "";
			ПредставлениеСтатьиРасходов 		= "";
			ПредставлениеСпособаОтражения 		= "";
			
			ПредставлениеСтроки = "";
			
			Если ЕстьПодразделение Тогда
				ПредставлениеПодразделения = СОКРЛП(СтрокаТЧ.ПодразделениеУчетаЗатрат);
				Если Не ЗначениеЗаполнено(ПредставлениеПодразделения) Тогда
					ПредставлениеПодразделения = "<...>";
				КонецЕсли;
				ПредставлениеСтроки = СтрШаблон("%1 / ", ПредставлениеПодразделения);
			КонецЕсли;
			
			Если ИспользоватьСтатьиФинансирования Тогда
				ПредставлениеСтатьиФинансирования = КодыСтатейФинансирования[СтрокаТЧ.СтатьяФинансирования];
				Если Не ЗначениеЗаполнено(ПредставлениеСтатьиФинансирования) Тогда
					ПредставлениеСтатьиФинансирования = "<...>";
				КонецЕсли;
				Если ПустаяСтрока(ПредставлениеСтроки) Тогда
					ПредставлениеСтроки = СтрШаблон("%1 / ", ПредставлениеСтатьиФинансирования);
				Иначе
					ПредставлениеСтроки = СтрШаблон("%1%2 / ", ПредставлениеСтроки, ПредставлениеСтатьиФинансирования);
				КонецЕсли;
			КонецЕсли;
			
			ПредставлениеСпособаОтражения = СОКРЛП(СтрокаТЧ.СпособОтраженияЗарплатыВБухучете);
			Если Не ЗначениеЗаполнено(ПредставлениеСпособаОтражения) Тогда
				ПредставлениеСпособаОтражения = "<...>";
			КонецЕсли;
			Если ПустаяСтрока(ПредставлениеСтроки) Тогда
				ПредставлениеСтроки = СтрШаблон("%1 / ", ПредставлениеСпособаОтражения);
			Иначе
				ПредставлениеСтроки = СтрШаблон("%1%2 / ", ПредставлениеСтроки, ПредставлениеСпособаОтражения);
			КонецЕсли;
			
			Если РаботаВБюджетномУчреждении Тогда
				ПредставлениеСтатьиРасходов = СОКРЛП(СтрокаТЧ.СтатьяРасходов);
				Если Не ЗначениеЗаполнено(ПредставлениеСтатьиРасходов) Тогда
					ПредставлениеСтатьиРасходов = "<...>";
				КонецЕсли;
				Если ПустаяСтрока(ПредставлениеСтроки) Тогда
					ПредставлениеСтроки = СтрШаблон("%1 / ", ПредставлениеСтатьиРасходов);
				Иначе
					ПредставлениеСтроки = СтрШаблон("%1%2 / ", ПредставлениеСтроки, ПредставлениеСтатьиРасходов);
				КонецЕсли;
			КонецЕсли;
			
			ПредставлениеДолиРаспределения = Формат(СтрокаТЧ.ДоляРаспределения, "ЧЦ=16; ЧДЦ=5");
			ПредставлениеСтроки = СтрШаблон("%1%2", ПредставлениеСтроки, ПредставлениеДолиРаспределения);
			
			Если ПустаяСтрока(ТекстПредставления) Тогда
				ТекстПредставления = ПредставлениеСтроки;
			Иначе
				ТекстПредставления = СтрШаблон("%1%2%3", ТекстПредставления, Символы.ПС, ПредставлениеСтроки);
			КонецЕсли;
			
			НомСтроки = НомСтроки + 1;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ТекстПредставления;

КонецФункции

Функция ДанныеДляБухучетаЗарплатыПервичныхДокументов(ДокументОбъект, Начисления, ОбработатьСпособРасчетов = Ложь) Экспорт

	РаботаВХозрасчетнойОрганизации = ПолучитьФункциональнуюОпцию("РаботаВХозрасчетнойОрганизации");
	
	СтатьяРасходов = Справочники.СтатьиРасходовЗарплата.ПустаяСсылка();
	Если РаботаВХозрасчетнойОрганизации Тогда
		Если ОбработатьСпособРасчетов Тогда
			ПравилаУчетаНачисленийСотрудников = УчетНачисленнойЗарплатыПовтИсп.ПравилаУчетаНачисленийСотрудников();
			ПравилоУчета = ПравилаУчетаНачисленийСотрудников[Начисления[0]];
			ИспользуетсяСпособРасчетов = (ПравилоУчета <> Неопределено И ПравилоУчета.ДоступноИзменениеСпособаРасчетов);
			Если ИспользуетсяСпособРасчетов Тогда
				ОписаниеСтатейРасходов = ЗарплатаКадры.СтатьиРасходовПоСпособамРасчетовСФизическимиЛицами();
				Если ЗначениеЗаполнено(ДокументОбъект.СпособРасчетовСФизическимиЛицами) Тогда
					СтатьяРасходов = ОписаниеСтатейРасходов[ДокументОбъект.СпособРасчетовСФизическимиЛицами];
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	Иначе
		СтатьяРасходов = ДокументОбъект.СтатьяРасходов;
	КонецЕсли;
	
	ТаблицаБухучетЗарплаты = НоваяТаблицаБухучетЗарплатыПервичныхДокументов();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Начисления", Начисления);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Начисления.Ссылка КАК Ссылка
	|ИЗ
	|	ПланВидовРасчета.Начисления КАК Начисления
	|ГДЕ
	|	Начисления.ОсновнойВидРасчета В(&Начисления)";
	ДополнительныеНачисления = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	Для каждого Ссылка Из ДополнительныеНачисления Цикл
		Начисления.Добавить(Ссылка);
	КонецЦикла;
	
	Для каждого Начисление Из Начисления Цикл
		Если ДокументОбъект.БухучетУказываетсяРаспределением Тогда
			Для каждого СтрокаТЧ Из ДокументОбъект.НастройкиБухучета Цикл
				НоваяСтрока = ТаблицаБухучетЗарплаты.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЧ);
				НоваяСтрока.ИдентификаторСтроки = СтрокаТЧ.НомерСтроки;
				НоваяСтрока.ДокументОснование 	= ДокументОбъект.Ссылка;
				НоваяСтрока.НачислениеУдержание = Начисление;
				Если РаботаВХозрасчетнойОрганизации Тогда
					НоваяСтрока.СтатьяРасходов = СтатьяРасходов;
				КонецЕсли;
			КонецЦикла;
		Иначе
			НоваяСтрока = ТаблицаБухучетЗарплаты.Добавить();
			НоваяСтрока.ДокументОснование 					= ДокументОбъект.Ссылка;
			НоваяСтрока.НачислениеУдержание 				= Начисление;
			НоваяСтрока.СпособОтраженияЗарплатыВБухучете 	= ДокументОбъект.СпособОтраженияЗарплатыВБухучете;
			НоваяСтрока.ОтношениеКЕНВД 						= ДокументОбъект.ОтношениеКЕНВД;
			НоваяСтрока.СтатьяФинансирования 				= ДокументОбъект.СтатьяФинансирования;
			НоваяСтрока.СтатьяРасходов 						= СтатьяРасходов;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ТаблицаБухучетЗарплаты;

КонецФункции

Процедура ОбновитьДанныеБухучетаДокументаПередЗаписью(ДокументОбъект) Экспорт

	Если ДокументОбъект.БухучетУказываетсяРаспределением Тогда
		ДокументОбъект.СтатьяФинансирования 			= Справочники.СтатьиФинансированияЗарплата.ПустаяСсылка();
		ДокументОбъект.СтатьяРасходов 					= Справочники.СтатьиРасходовЗарплата.ПустаяСсылка();
		ДокументОбъект.СпособОтраженияЗарплатыВБухучете = Справочники.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка();
		ДокументОбъект.ОтношениеКЕНВД 					= Перечисления.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка();
	Иначе
		ДокументОбъект.НастройкиБухучета.Очистить();
	КонецЕсли;

КонецПроцедуры

Процедура УстановитьСписокВыборкаВариантУказанияБухучета(СписокВыбора) Экспорт

	СписокВыбора.Очистить();
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
		ТекстВыбораЗначения = НСтр("ru = 'Отразить по статье, на счете, субконто';
									|en = 'Record by item, on account, extra dimension'");
		СписокВыбора.Добавить(0, ТекстВыбораЗначения);
		ТекстВыбораЗначения = НСтр("ru = 'Распределить по статьям';
									|en = 'Allocate among items'");
		СписокВыбора.Добавить(1, ТекстВыбораЗначения);
	Иначе
		ТекстВыбораЗначения = НСтр("ru = 'Отразить на счете, субконто';
									|en = 'Record on account, extra dimension'");
		СписокВыбора.Добавить(0, ТекстВыбораЗначения);
		ТекстВыбораЗначения = НСтр("ru = 'Распределить по счетам, субконто';
									|en = 'Allocate among accounts, extra dimension'");
		СписокВыбора.Добавить(1, ТекстВыбораЗначения);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбменДанными

Процедура ПриЗаполненииТаблицыЗависимыхДанныхДляОбмена(ЗависимыеДанные) Экспорт
	
	// Метаданные.РегистрыСведений.БухучетНачисленийСотрудников
	НоваяСтрока = ЗависимыеДанные.Добавить();
	НоваяСтрока.ВедущиеМетаданные = Метаданные.РегистрыСведений.БухучетНачисленийСотрудников;
	НоваяСтрока.ЗависимыеМетаданные = Метаданные.РегистрыСведений.БухучетНачисленийСотрудниковИнтервальный;

	// Метаданные.РегистрыСведений.БухучетЗарплатыСотрудников
	НоваяСтрока = ЗависимыеДанные.Добавить();
	НоваяСтрока.ВедущиеМетаданные = Метаданные.РегистрыСведений.БухучетЗарплатыСотрудников;
	НоваяСтрока.ЗависимыеМетаданные = Метаданные.РегистрыСведений.БухучетЗарплатыСотрудниковИнтервальный;
	
КонецПроцедуры

#КонецОбласти

#Область Прочие

Процедура СвернутьТаблицуКоэффициентовРаспределенияСреднегоЗаработка(ТаблицаКоэффициентов) Экспорт
	
	КолонкиГруппировок  = Новый Массив;
	Для каждого КолонкаТЗ Из ТаблицаКоэффициентов.Колонки Цикл
		Если ВРег(КолонкаТЗ.Имя) = "КОЭФФИЦИЕНТ" Тогда
			Продолжить;
		КонецЕсли;
		КолонкиГруппировок.Добавить(КолонкаТЗ.Имя);
	КонецЦикла;
	
	КолонкиГруппировок = СтрСоединить(КолонкиГруппировок, ",");

	ТаблицаКоэффициентов.Свернуть(КолонкиГруппировок, "Коэффициент");

КонецПроцедуры

// Создает временную таблицу ВТФинансированиеПоДокументам.
// Используется при обновлении учета НДФЛ.
//
//	Параметры
//		МенеджерВременныхТаблиц, менеджер временных таблиц, содержит ВТ ВТДокументыКОбработке с полями
//			* Регистратор
//		ТипыДокументов, массив, содержит типы документов к обработке.
//
//	Временная таблица ВТФинансированиеПоДокументам содержит поля
//			* Регистратор
//			* СтатьяФинансирования
//			* СтатьяРасходов
//
Процедура СоздатьВТФинансированиеПоДокументам(МенеджерВременныхТаблиц, ТипыДокументов) Экспорт

	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
		Возврат;
	КонецЕсли;
	
	ТипыДокументовСФинансированием = Новый Массив;
	Для каждого ТипДокумента Из ТипыДокументов Цикл
		МдДокумент = Метаданные.НайтиПоТипу(ТипДокумента);
		Если МдДокумент.Реквизиты.Найти("СтатьяФинансирования") <> Неопределено
			И МдДокумент.Реквизиты.Найти("СтатьяРасходов") <> Неопределено Тогда
			ТипыДокументовСФинансированием.Добавить(ТипДокумента);
		КонецЕсли;
	КонецЦикла;
	
	Если ТипыДокументовСФинансированием.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТипыДокументов", ТипыДокументовСФинансированием);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДокументыКОбработке.Регистратор КАК Регистратор,
	|	ДокументыКОбработке.Регистратор.СтатьяФинансирования КАК СтатьяФинансирования,
	|	ДокументыКОбработке.Регистратор.СтатьяРасходов КАК СтатьяРасходов
	|ПОМЕСТИТЬ ВТФинансированиеПоДокументам
	|ИЗ
	|	ВТДокументыКОбработке КАК ДокументыКОбработке
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ДокументыКОбработке.Регистратор) В (&ТипыДокументов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПодтверждениеВыплатыДоходов.Ссылка,
	|	ПодтверждениеВыплатыДоходов.Ведомость.СтатьяФинансирования,
	|	ПодтверждениеВыплатыДоходов.Ведомость.СтатьяРасходов
	|ИЗ
	|	Документ.ПодтверждениеВыплатыДоходов КАК ПодтверждениеВыплатыДоходов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДокументыКОбработке КАК ДокументыКОбработке
	|		ПО ПодтверждениеВыплатыДоходов.Ссылка = ДокументыКОбработке.Регистратор";
	Запрос.Выполнить();

КонецПроцедуры

// Формирует таблицу значений с коэффициентами распределения сохраняемого денежного содержания
// Параметры
//		РаспределениеСохраняемыхНачислений - Коллекция, содержит результаты распределения начислений,
//											сохраняемых при расчете сохраняемого ДС
//			* Начисление
//			* РаспределениеПоСтатьям - ТаблицаЗначений, структура см НоваяТаблицаРаспределениеРезультатовНачислений
//			* Сотрудник
//		НачисленияДляРаспределения - ТаблицаЗначений, содержит начисления, учитываемые по фактическим начислениям, а также строки с 
//							с результатами расчета РК и СН
//			* Начисление
//			* Результат
//			* Сотрудник
//			* Подразделение
//		НазначениеРасчета
//		Организация
//		ПериодРасчета
//		ДатаНачалаСобытия
//
//	Возвращаемое значение - ТаблицаЗначений, описание см НоваяТаблицаКоэффициентыРаспределенияСохраняемогоДС
//
Функция ТаблицаКоэффициентовРаспределенияСохраняемогоДС(РаспределениеСохраняемыхНачислений, НачисленияДляРаспределения, НазначениеРасчета, Организация, ПериодРасчета, ДатаНачалаСобытия) Экспорт
	
	ТаблицаКоэффициентов = НоваяТаблицаКоэффициентыРаспределенияСохраняемогоДС();
	ТаблицаКоэффициентов.Колонки.Коэффициент.Имя = "Результат";
	
	// Переносим в таблицу результаты распределения сохраняемых начислений.
	Для каждого СтрокаТЗ Из РаспределениеСохраняемыхНачислений Цикл
		
		Если СтрокаТЗ.РаспределениеПоСтатьям <> Неопределено Тогда
			
			Для каждого СтрокаРаспределения Из СтрокаТЗ.РаспределениеПоСтатьям Цикл
			
				НоваяСтрока = ТаблицаКоэффициентов.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРаспределения);
				НоваяСтрока.Сотрудник 			= СтрокаТЗ.Сотрудник;
				НоваяСтрока.Начисление 			= СтрокаТЗ.Начисление;
				НоваяСтрока.НазначениеРасчета 	= НазначениеРасчета;
			
			КонецЦикла;
			
		КонецЕсли;
	
	КонецЦикла;
	ОтражениеЗарплатыВУчете.СвернутьТаблицу(ТаблицаКоэффициентов);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	// Заполним соответствие базовых в.р. для РК и СН, при условии, что стратегия отражения в бухучете - ПоБазовымРасчетам.
	БазовыеВидыРасчета = Новый Соответствие;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НачисленияБазовыеВидыРасчета.Ссылка КАК Ссылка,
	|	НачисленияБазовыеВидыРасчета.ВидРасчета КАК ВидРасчета
	|ИЗ
	|	ПланВидовРасчета.Начисления.БазовыеВидыРасчета КАК НачисленияБазовыеВидыРасчета
	|ГДЕ
	|	НачисленияБазовыеВидыРасчета.Ссылка.КатегорияНачисленияИлиНеоплаченногоВремени В (ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.РайонныйКоэффициент), ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.СевернаяНадбавка))
	|	И НачисленияБазовыеВидыРасчета.Ссылка.СтратегияОтраженияВУчете = ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазовымРасчетам)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Ссылка") Цикл
		ОсновнойВР = Выборка.Ссылка;
		БазовыеВР = Новый Массив;
		Пока Выборка.Следующий() Цикл
			БазовыеВР.Добавить(Выборка.ВидРасчета);
		КонецЦикла;
		БазовыеВидыРасчета.Вставить(ОсновнойВР, БазовыеВР);
	КонецЦикла;
	
	ТаблицаРКиСН = НачисленияДляРаспределения.СкопироватьКолонки();
	
	Начисления = ОтражениеЗарплатыВУчете.НоваяТаблицаНачислений();
	
	ИдентификаторСтроки = 1;
	ПериодДействия = НачалоМесяца(ДатаНачалаСобытия);
	ДатаОкончания = КонецМесяца(ДатаНачалаСобытия);
	Для каждого СтрокаТЗ Из НачисленияДляРаспределения Цикл
		
		Если БазовыеВидыРасчета[СтрокаТЗ.Начисление] <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(ТаблицаРКиСН.Добавить(), СтрокаТЗ);
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = Начисления.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
		
		НоваяСтрока.ИдентификаторСтроки = ИдентификаторСтроки;
		НоваяСтрока.Организация 		= Организация;
		НоваяСтрока.ПериодДействия 		= ПериодДействия;
		НоваяСтрока.ДатаНачала 			= ДатаНачалаСобытия;
		НоваяСтрока.ДатаОкончания 		= ДатаОкончания;
		НоваяСтрока.Сумма 				= СтрокаТЗ.Результат;
		НоваяСтрока.ВидОперации 		= Перечисления.ВидыОперацийПоЗарплате.НачисленоДоход;
		
		ИдентификаторСтроки = ИдентификаторСтроки + 1;
		
	КонецЦикла;
	
	Если Начисления.Количество() > 0 Тогда
		
		БухучетНачислений = БухучетНачислений(Организация, ПериодДействия, Начисления);
		ОтражениеЗарплатыВУчете.СвернутьТаблицу(БухучетНачислений);
		СтатьяРасходов211 = СтатьяРасходов211();
		
		Для каждого СтрокаТЗ Из БухучетНачислений Цикл
			НоваяСтрока = ТаблицаКоэффициентов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
			НоваяСтрока.Результат = СтрокаТЗ.Сумма;
			НоваяСтрока.НазначениеРасчета = НазначениеРасчета;
			НоваяСтрока.СтатьяРасходов = СтатьяРасходов211;
		КонецЦикла;
		
	КонецЕсли;
	
	Для каждого СтрокаРКиСН Из ТаблицаРКиСН Цикл
		
		БазовыеВР = БазовыеВидыРасчета[СтрокаРКиСН.Начисление];
		ТаблицаРаспределения = ТаблицаКоэффициентов.СкопироватьКолонки();
		Для каждого СтрокаТЗ Из ТаблицаКоэффициентов Цикл
			Если ЗначениеЗаполнено(СтрокаТЗ.Начисление) И БазовыеВР.Найти(СтрокаТЗ.Начисление) = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(ТаблицаРаспределения.Добавить(), СтрокаТЗ);
		КонецЦикла;
		
		ТаблицаРаспределения.Свернуть("ПодразделениеУчетаЗатрат,СтатьяФинансирования,СтатьяРасходов,СпособОтраженияЗарплатыВБухучете,ОблагаетсяЕНВД","Результат");
		Если ТаблицаРаспределения.Количество() > 0 Тогда
			ЗарплатаКадры.РазнестиСуммуПоБазе(СтрокаРКиСН.Результат, ТаблицаРаспределения, "Результат");
		КонецЕсли;
		
		Для каждого СтрокаТЗ Из ТаблицаРаспределения Цикл
			НоваяСтрока = ТаблицаКоэффициентов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
			НоваяСтрока.НазначениеРасчета = НазначениеРасчета;
			НоваяСтрока.Сотрудник 	= СтрокаРКиСН.Сотрудник;
			НоваяСтрока.Начисление 	= СтрокаРКиСН.Начисление;
		КонецЦикла;
		
	КонецЦикла;
	
	ОтражениеЗарплатыВУчете.СвернутьТаблицу(ТаблицаКоэффициентов);
	ТаблицаКоэффициентов.Колонки.Результат.Имя = "Коэффициент";
	
	Возврат ТаблицаКоэффициентов;

КонецФункции

// Функция определяет состав видов особых начислений, 
//	включаемых в состав расчетной базы.
//
Функция ВидыНачисленийДополненияРасчетнойБазыУдержаний() Экспорт
	
	ВидыНачислений = Новый Массив;
	ВидыНачислений.Добавить(Перечисления.ВидыОсобыхНачисленийИУдержаний.ДоговорАвторскогоЗаказа);
	ВидыНачислений.Добавить(Перечисления.ВидыОсобыхНачисленийИУдержаний.ДоговорРаботыУслуги);
	ВидыНачислений.Добавить(Перечисления.ВидыОсобыхНачисленийИУдержаний.КомпенсацияЗаЗадержкуЗарплаты);
	ВидыНачислений.Добавить(Перечисления.ВидыОсобыхНачисленийИУдержаний.СтоимостьПодарковПризов);
	
	Возврат ВидыНачислений;
	
КонецФункции

// Создает временную таблицу ВТСведенияОБухучетеЗарплатыСотрудников.
//
//	Параметры
//		МенеджерВТ - МенеджерВременныхТаблиц - содержит таблицу с именем, указанным в параметре ИмяВТ с полями
//			* Организация
//			* Подразделение
//			* ТерриторияВыполненияРаботВОрганизации
//			* Сотрудник
//			* Период
//		ИмяВТ - Строка - имя временной таблицы.
//
//	Временная таблица ВТСведенияОБухучетеЗарплатыСотрудников содержит поля
//			* Организация
//			* Подразделение
//			* ТерриторияВыполненияРаботВОрганизации
//			* Сотрудник
//			* Период
//			* СтатьяФинансирования
//			* СтатьяРасходов
//			* СпособОтраженияЗарплатыВБухучете
//			* ОтношениеКЕНВД
//
Процедура СоздатьВТСведенияОБухучетеЗарплатыСотрудников(МенеджерВТ, ИмяВТ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Организация КАК Организация,
	|	Таблица.Подразделение КАК Подразделение,
	|	Таблица.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	Таблица.Сотрудник КАК Сотрудник,
	|	Таблица.Период КАК Период,
	|	АВТОНОМЕРЗАПИСИ() КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ВТИсходнаяТаблица
	|ИЗ
	|	ИмяВременнойТаблицы КАК Таблица";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ИмяВременнойТаблицы", ИмяВТ);
	Запрос.Выполнить();
	
	УдалитьВТ = Новый Массив;
	УдалитьВТ.Добавить("ВТИсходнаяТаблица");
	
	// В менеджере будут таблицы с настройками.
	ИменаВременныхТаблиц = Новый Массив;
	ПрочитатьНастройкиБухучетаСоздатьВременныеТаблицы(МенеджерВТ, "ВТИсходнаяТаблица", ИменаВременныхТаблиц);
	// Получим итоговую таблицу без учета распределения.
	ИмяИтоговойТаблицы = "ВТСведенияОБухучетеЗарплатыСотрудниковВременная";
	СоздатьВТНастройкиБухучетаБезУчетаНастроекРаспределения(МенеджерВТ, "ВТИсходнаяТаблица", ИмяИтоговойТаблицы);
	ЗарплатаКадры.УничтожитьВТ(МенеджерВТ, ИменаВременныхТаблиц);
	
	Запрос.УстановитьПараметр("СтатьяОплатаТруда", СтатьяОплатаТруда());
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&СтатьяОплатаТруда КАК СтатьяРасходов,
	|	&ПоляТаблицы КАК ПоляТаблицы
	|ПОМЕСТИТЬ ВТСведенияОБухучетеЗарплатыСотрудников
	|ИЗ
	|	ВТСведенияОБухучетеЗарплатыСотрудниковВременная КАК Таблица";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПоляТаблицы КАК ПоляТаблицы", "*");
	Запрос.Выполнить();
	УдалитьВТ.Добавить("ВТСведенияОБухучетеЗарплатыСотрудниковВременная");
	
	ЗарплатаКадры.УничтожитьВТ(МенеджерВТ, УдалитьВТ);
	
КонецПроцедуры

// Создает временную таблицу ВТНастройкиБухучетаСотрудниковДляАванса.
//
//	Параметры
//		МенеджерВТ - МенеджерВременныхТаблиц - в него помещается временная таблица ВТНастройкиБухучетаСотрудниковДляАванса.
//		Организация - СправочникСсылка.Организации
//		Период - Дата - определяет месяц, для которого получаются данные, любая дата месяца
//		Сотрудники - Массив
//
//	Временная таблица ВТНастройкиБухучетаСотрудников содержит поля
// 			* Организация
// 			* Сотрудник
// 			* Подразделение
// 			* СтатьяФинансирования
// 			* СтатьяРасходов
// 			* ДоляРаспределения
//
Процедура СоздатьВТНастройкиБухучетаСотрудниковДляАванса(МенеджерВТ, Организация, Период, Сотрудники) Экспорт
	
	КадровыеДанныеСтрока = "Подразделение,ТерриторияВыполненияРаботВОрганизации";
	ТаблицаСотрудников = КадровыйУчет.КадровыеДанныеСотрудников(Истина, Сотрудники, КадровыеДанныеСтрока, КонецМесяца(Период));
	
	УдалитьВТ = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Период", КонецМесяца(Период));
	Запрос.УстановитьПараметр("ТаблицаСотрудников", ТаблицаСотрудников);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	&Организация КАК Организация,
	|	&Период КАК Период,
	|	Таблица.Подразделение КАК Подразделение,
	|	Таблица.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	Таблица.Сотрудник КАК Сотрудник
	|ПОМЕСТИТЬ ВТСотрудникиДляАванса
	|ИЗ
	|	&ТаблицаСотрудников КАК Таблица";
	Запрос.Выполнить();
	УдалитьВТ.Добавить("ВТСотрудникиДляАванса");
	
	СоздатьВТНастройкиБухучетаСотрудников(Организация, Период, МенеджерВТ, "ВТСотрудникиДляАванса");
	УдалитьВТ.Добавить("ВТНастройкиБухучетаСотрудников");
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиБухучета.Организация КАК Организация,
	|	НастройкиБухучета.Сотрудник КАК Сотрудник,
	|	НастройкиБухучета.Подразделение КАК Подразделение,
	|	НастройкиБухучета.СтатьяФинансирования КАК СтатьяФинансирования,
	|	НастройкиБухучета.СтатьяРасходов КАК СтатьяРасходов,
	|	СУММА(НастройкиБухучета.ДоляРаспределения) КАК ДоляРаспределения
	|ПОМЕСТИТЬ ВТНастройкиБухучетаСотрудниковДляАванса
	|ИЗ
	|	ВТНастройкиБухучетаСотрудников КАК НастройкиБухучета
	|
	|СГРУППИРОВАТЬ ПО
	|	НастройкиБухучета.Организация,
	|	НастройкиБухучета.СтатьяФинансирования,
	|	НастройкиБухучета.СтатьяРасходов,
	|	НастройкиБухучета.Сотрудник,
	|	НастройкиБухучета.Подразделение";
	Запрос.Выполнить();
	
	ЗарплатаКадры.УничтожитьВТ(МенеджерВТ, УдалитьВТ);
	
КонецПроцедуры

Функция СтатьяФинансированияОтрицательногоНалога(Организация, Период) Экспорт

	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	БухучетИзлишнеУдержанногоНДФЛ.СтатьяФинансирования КАК СтатьяФинансирования
	|ИЗ
	|	РегистрСведений.БухучетИзлишнеУдержанногоНДФЛ.СрезПоследних(&Период, Организация = &Организация) КАК БухучетИзлишнеУдержанногоНДФЛ";
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	Иначе
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.СтатьяФинансирования;
	КонецЕсли;

КонецФункции

Процедура УстановитьВидимостьВариантУказанияБухучета(Форма) Экспорт

	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ВариантУказанияБухучета",
		"Видимость",
		ПолучитьФункциональнуюОпцию("РегистрироватьБухучетДокументовКакРаспределение"));

КонецПроцедуры

Функция КоллекцииБухучетаИдентичны(Коллекция1, Коллекция2, ПроверяемыеСвойства) Экспорт
	
	Если Не ЗначениеЗаполнено(Коллекция1) И Не ЗначениеЗаполнено(Коллекция2) Тогда
		Возврат Истина;
	ИначеЕсли Не ЗначениеЗаполнено(Коллекция1) Тогда
		Возврат Ложь;
	ИначеЕсли Не ЗначениеЗаполнено(Коллекция2) Тогда
		Возврат Ложь;
	ИначеЕсли Коллекция1.Количество() <> Коллекция1.Количество() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ТипЗнч(ПроверяемыеСвойства) = Тип("Строка") Тогда
		ИменаКолонок = СтрРазделить(ПроверяемыеСвойства,",");
	Иначе
		ИменаКолонок = ПроверяемыеСвойства;
	КонецЕсли;
	
	Таблица1 = Новый ТаблицаЗначений;
	Для каждого ИмяКолонки Из ИменаКолонок Цикл
		Таблица1.Колонки.Добавить(ИмяКолонки);
	КонецЦикла;
	Таблица2 = Таблица1.СкопироватьКолонки();
	
	Для каждого СтрокаКоллекции Из Коллекция1 Цикл
		ЗаполнитьЗначенияСвойств(Таблица1.Добавить(), СтрокаКоллекции);
	КонецЦикла;
	
	Для каждого СтрокаКоллекции Из Коллекция2 Цикл
		ЗаполнитьЗначенияСвойств(Таблица2.Добавить(), СтрокаКоллекции);
	КонецЦикла;
	
	Возврат ОбщегоНазначения.КоллекцииИдентичны(Таблица1, Таблица2);

КонецФункции

Функция ИменаКолонокБухучетНачислений(Форма) Экспорт

	ИменаКолонок = "СпособОтраженияЗарплатыВБухучете,ДоляРаспределения";
	Если Форма.ИспользоватьСтатьиФинансирования Тогда
		ИменаКолонок = СтрШаблон("%1,%2", ИменаКолонок, "СтатьяФинансирования");
	КонецЕсли;
	Если Форма.РаботаВБюджетномУчреждении Тогда
		ИменаКолонок = СтрШаблон("%1,%2", ИменаКолонок, "СтатьяРасходов");
	КонецЕсли;
	
	Возврат ИменаКолонок;

КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ДанныеДляОтраженияЗарплатыВБухучете

// Расширенная реализация одноименной функции программного интерфейса,
// описание см ОтражениеЗарплатыВБухучете.ДанныеДляОтраженияЗарплатыВБухучете.
//
Функция ДанныеДляОтраженияЗарплатыВБухучете(Организация, ПериодРегистрации) Экспорт

	ДанныеДляОтражения = ОтражениеЗарплатыВБухучете.НоваяСтруктураДанныеДляОтраженияЗарплатыВБухучете();
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
		
		БухучетНачислений 	= БухучетНачисленийПоСтатьям(Организация, ПериодРегистрации);
		БухучетВзносов 		= БухучетВзносовПоСтатьям(Организация, ПериодРегистрации);
		ДанныеДляОтражения.НачисленнаяЗарплатаИВзносы 	= БухучетНачисленийИВзносов(БухучетНачислений, БухучетВзносов);
		БухучетНачислений 	= Неопределено;
		БухучетВзносов 		= Неопределено;
		
		ДанныеДляОтражения.НачисленныйНДФЛ 		= БухучетНДФЛПоСтатьям(Организация, ПериодРегистрации);
		ДанныеДляОтражения.УдержаннаяЗарплата 	= БухучетУдержанийПоСтатьям(Организация, ПериодРегистрации);
		
	Иначе
		
		ТаблицаНачислений = ОтражениеЗарплатыВУчете.ДанныеДляОтражениеВУчетеНачислений(Организация, ПериодРегистрации);
		БухучетНачислений = БухучетНачислений(Организация, ПериодРегистрации, ТаблицаНачислений);
		ТаблицаНачислений 	= Неопределено;
		
		ТаблицаВзносов = ОтражениеЗарплатыВУчете.ДанныеДляОтражениеВУчетеВзносов(Организация, ПериодРегистрации);
		БухучетВзносов = ОтражениеЗарплатыВБухучете.БухучетСтраховыхВзносов(Организация, ПериодРегистрации, ТаблицаВзносов, БухучетНачислений);
		ТаблицаВзносов 		= Неопределено;
		
		ДанныеДляОтражения.НачисленнаяЗарплатаИВзносы = БухучетНачисленийИВзносов(БухучетНачислений, БухучетВзносов);
		БухучетНачислений 	= Неопределено;
		БухучетВзносов 		= Неопределено;
		
		ТаблицаНДФЛ = ОтражениеЗарплатыВУчете.ДанныеДляОтражениеВУчетеНДФЛ(Организация, ПериодРегистрации);
		ДанныеДляОтражения.НачисленныйНДФЛ = ОтражениеЗарплатыВБухучете.БухучетНДФЛ(ТаблицаНДФЛ);
		ТаблицаНДФЛ = Неопределено;
		
		ТаблицаУдержаний = ОтражениеЗарплатыВУчете.ДанныеДляОтражениеВУчетеУдержаний(Организация, ПериодРегистрации);
		ДанныеДляОтражения.УдержаннаяЗарплата = БухучетУдержаний(ТаблицаУдержаний, ДанныеДляОтражения.НачисленнаяЗарплатаИВзносы);
		
	КонецЕсли;
	
	Возврат ДанныеДляОтражения;

КонецФункции

// Формирует данные для заполнения таблиц документа ОтражениеЗарплатыВБухучете.
//
// Параметры:
// 		ПараметрыДляЗаполнения - Структура - описание см ПараметрыДляЗаполненияТаблицДокумента
//
// Возвращаемое значение:
// 		Структура
//			* НачисленнаяЗарплатаИВзносы - ТаблицаЗначений - соответствует структуре ТЧ НачисленнаяЗарплатаИВзносы
//			* НачисленныйНДФЛ - ТаблицаЗначений - соответствует структуре ТЧ НачисленныйНДФЛ
//			* УдержаннаяЗарплата - ТаблицаЗначений - соответствует структуре ТЧ УдержаннаяЗарплата
//			* ВыплатаОтпусковЗаСчетРезерва - ТаблицаЗначений - соответствует структуре ТЧ ВыплатаОтпусковЗаСчетРезерва
//
Функция ДанныеДляЗаполненияТаблицДокумента(ПараметрыДляЗаполнения) Экспорт
	
	Организация 	 	= ПараметрыДляЗаполнения.Организация;
	ПериодРегистрации 	= ПараметрыДляЗаполнения.ПериодРегистрации;
	ДокументСсылка 		= ПараметрыДляЗаполнения.ДокументСсылка;
	
	РезультатЗаполнения = ДанныеДляОтраженияЗарплатыВБухучете(Организация, ПериодРегистрации);
	РезультатЗаполнения.Вставить("ВыплатаЗаСчетРезерва", ОтражениеЗарплатыВБухучете.НоваяТаблицаНачисленоЗаСчетРезерва());
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОценочныеОбязательстваЗарплатаКадры") Тогда
		
		МодульРезервОтпусков = ОбщегоНазначения.ОбщийМодуль("РезервОтпусков");
		НастройкиРезервовОтпусков = МодульРезервОтпусков.НастройкиРезервовОтпусков(Организация, ПериодРегистрации);
		
		МодульРезервыПоОплатеТруда = ОбщегоНазначения.ОбщийМодуль("РезервыПоОплатеТруда");
		ЕстьНастройкиРезерваОтпусков = МодульРезервыПоОплатеТруда.ЕстьНастройкиРезерваОтпусков(Организация, ПериодРегистрации);
		
		Если НастройкиРезервовОтпусков.ФормироватьРезервБУ И НЕ ЕстьНастройкиРезерваОтпусков Тогда
			ПараметрыДляСписанияРасходов = МодульРезервОтпусков.ПараметрыДляСписанияРасходовПоОплатеОтпуска();
			ПараметрыДляСписанияРасходов.Организация 						= Организация;
			ПараметрыДляСписанияРасходов.ПериодРегистрации 					= ПериодРегистрации;
			ПараметрыДляСписанияРасходов.НачисленнаяЗарплатаИВзносы 		= РезультатЗаполнения.НачисленнаяЗарплатаИВзносы;
			ПараметрыДляСписанияРасходов.НачисленныеОтпуска 			 	= РезультатЗаполнения.ВыплатаЗаСчетРезерва;
			ПараметрыДляСписанияРасходов.УчитыватьОперацииТекущегоМесяца 	= Ложь;
			МодульРезервОтпусков.СписатьРасходыПоОтпускамЗаСчетОценочныхОбязательств(ПараметрыДляСписанияРасходов);
		КонецЕсли;
		
		Если МодульРезервыПоОплатеТруда.ВыплачиваютсяРезервыВОрганизации(Организация, ПериодРегистрации) Тогда
			ПараметрыДляСписанияРасходов = МодульРезервыПоОплатеТруда.ПараметрыДляСписанияРасходов();
			ПараметрыДляСписанияРасходов.Организация 						= Организация;
			ПараметрыДляСписанияРасходов.ПериодРегистрации 					= ПериодРегистрации;
			ПараметрыДляСписанияРасходов.НачисленнаяЗарплатаИВзносы 		= РезультатЗаполнения.НачисленнаяЗарплатаИВзносы;
			ПараметрыДляСписанияРасходов.ВыплатаЗаСчетРезерва 			 	= РезультатЗаполнения.ВыплатаЗаСчетРезерва;
			ПараметрыДляСписанияРасходов.УчитыватьОперацииТекущегоМесяца 	= Ложь;
			МодульРезервыПоОплатеТруда.СписатьРасходыЗаСчетОценочныхОбязательств(ПараметрыДляСписанияРасходов);
		КонецЕсли;
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОрганизационнаяСтруктура") Тогда 
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОрганизационнаяСтруктура");
		Модуль.ДополнитьДанныеДокументаМестомВСтруктуреПредприятия(РезультатЗаполнения, "НачисленнаяЗарплатаИВзносы,УдержаннаяЗарплата");
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.КонфигурацииЗарплатаКадрыРасширенная.ОтражениеРасчетовЗарплатыВБухучете") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОтражениеРасчетовЗарплатыВБухучете");
		Модуль.ОтобратьРезультатыЗаполненияТаблицДокумента(РезультатЗаполнения, ПараметрыДляЗаполнения);
	КонецЕсли;
	
	// ERP Удалено ОтражениеЗарплатыВБухучете.ПривестиРезультатыЗаполненияКСтруктуреТаблицДокумента()
	ОтражениеЗарплатыВФинансовомУчетеУП.ДополнитьТаблицуНачислений(РезультатЗаполнения.НачисленнаяЗарплатаИВзносы);
	
	КолонкиУдаления = "Начисление,ДатаНачала,ПодразделениеУчетаЗатрат,СтатьяФинансирования";
	ОтражениеЗарплатыВБухучете.СвернутьДанныеДляОтраженияЗарплатыВБухучете(РезультатЗаполнения, КолонкиУдаления);
	ОтражениеЗарплатыВБухучете.УпорядочитьДанныеДляОтраженияЗарплатыВБухучете(РезультатЗаполнения);
	
	Возврат РезультатЗаполнения;

КонецФункции

Функция ОписаниеУдержанийДляОформленияКассовогоЧека()

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИспользоватьОснованияОформленияКассовогоЧека", ПолучитьФункциональнуюОпцию("ИспользоватьУдержанияЯвляющиесяОснованиемОформленияКассовогоЧека"));
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Удержания.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА &ИспользоватьОснованияОформленияКассовогоЧека
	|			ТОГДА Удержания.Наименование
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ОписаниеУдержанияДляЧека
	|ИЗ
	|	ПланВидовРасчета.Удержания КАК Удержания
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &ИспользоватьОснованияОформленияКассовогоЧека
	|				ТОГДА Удержания.ЯвляетсяОснованиемОформленияКассовогоЧека
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Настройки = Новый Соответствие;
	Пока Выборка.Следующий() Цикл
		Настройки.Вставить(Выборка.Ссылка, Выборка.ОписаниеУдержанияДляЧека);
	КонецЦикла;
	
	Возврат Настройки;

КонецФункции

Функция БухучетНачислений(Организация, Период, ТаблицаНачислений)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТаблицаНачислений", ТаблицаНачислений);
	Запрос.УстановитьПараметр("ПериодРегистрации", Период);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	&ПериодРегистрации КАК ПериодРегистрации,
	|	&Организация КАК Организация,
	|	НачисленияДляРаспределения.Сотрудник КАК Сотрудник,
	|	НачисленияДляРаспределения.ФизическоеЛицо КАК ФизическоеЛицо,
	|	НачисленияДляРаспределения.Подразделение КАК Подразделение,
	|	НачисленияДляРаспределения.СтатьяРасходов КАК СтатьяРасходов,
	|	НачисленияДляРаспределения.Начисление КАК Начисление,
	|	НачисленияДляРаспределения.ВидОперации КАК ВидОперации,
	|	НачисленияДляРаспределения.ДатаНачала КАК ДатаНачала,
	|	НачисленияДляРаспределения.ДатаОкончания КАК ДатаОкончания,
	|	НачисленияДляРаспределения.Сумма КАК Сумма,
	|	НачисленияДляРаспределения.ДокументОснование КАК ДокументОснование,
	|	НачисленияДляРаспределения.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	НачисленияДляРаспределения.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации
	|ПОМЕСТИТЬ ВТНачисленияПоДоговорамГПХ
	|ИЗ
	|	&ТаблицаНачислений КАК НачисленияДляРаспределения
	|ГДЕ
	|	НачисленияДляРаспределения.ВидОперации В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ДоговорАвторскогоЗаказа), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ДоговорРаботыУслуги))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&ПериодРегистрации КАК ПериодРегистрации,
	|	&Организация КАК Организация,
	|	НачисленияДляРаспределения.Регистратор КАК Регистратор,
	|	НачисленияДляРаспределения.Сотрудник КАК Сотрудник,
	|	НачисленияДляРаспределения.ФизическоеЛицо КАК ФизическоеЛицо,
	|	НачисленияДляРаспределения.Подразделение КАК Подразделение,
	|	НачисленияДляРаспределения.СтатьяРасходов КАК СтатьяРасходов,
	|	НачисленияДляРаспределения.ВидОперации КАК ВидОперации,
	|	НачисленияДляРаспределения.Начисление КАК Начисление,
	|	НачисленияДляРаспределения.ДатаНачала КАК ДатаНачала,
	|	НачисленияДляРаспределения.ДатаОкончания КАК ДатаОкончания,
	|	НачисленияДляРаспределения.Сумма КАК Сумма,
	|	НачисленияДляРаспределения.ДокументОснование КАК ДокументОснование,
	|	НачисленияДляРаспределения.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	НачисленияДляРаспределения.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	НачисленияДляРаспределения.Сторно КАК Сторно,
	|	НачисленияДляРаспределения.ФиксСторно КАК ФиксСторно,
	|	ЛОЖЬ КАК РассчитыватьПоРазовымНачислениямДокумента,
	|	НачисленияДляРаспределения.ИдентификаторСтрокиНачисления КАК ИдентификаторСтрокиНачисления
	|ПОМЕСТИТЬ ВТНачисленияБезДоговоровГПХ
	|ИЗ
	|	&ТаблицаНачислений КАК НачисленияДляРаспределения
	|ГДЕ
	|	НЕ НачисленияДляРаспределения.ВидОперации В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ДоговорАвторскогоЗаказа), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ДоговорРаботыУслуги))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Таблица.ПериодРегистрации КАК ПериодРегистрации
	|ИЗ
	|	ВТНачисленияПоДоговорамГПХ КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Таблица.ПериодРегистрации КАК ПериодРегистрации
	|ИЗ
	|	ВТНачисленияБезДоговоровГПХ КАК Таблица";
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	КоличествоРезультатов = РезультатЗапроса.ВГраница();
	
	ИсходныеДанные = ОписаниеИсходныхДанныхДляОтраженияНачисленийВБухучете();
	ИсходныеДанные.Организация    			  = Организация;
	ИсходныеДанные.МесяцНачисления 			  = Период;
	ИсходныеДанные.МенеджерВременныхТаблиц    = Запрос.МенеджерВременныхТаблиц;
	
	ИменаВТ = Новый Массив;
	// ВТБухучетНачисленияПоДоговорамГПХ
	Если Не РезультатЗапроса[КоличествоРезультатов-1].Пустой() Тогда
		ИсходныеДанные.ИмяВТНачисления = "ВТНачисленияПоДоговорамГПХ";
		ПолучитьБухучетНачисленийПоДоговорамСоздатьВТ(ИсходныеДанные, "ВТБухучетНачисленияПоДоговорамГПХ");
		ИменаВТ.Добавить("ВТБухучетНачисленияПоДоговорамГПХ");
	КонецЕсли;
	
	// ВТБухучетНачисленияБезДоговоровГПХ
	Если Не РезультатЗапроса[КоличествоРезультатов].Пустой() Тогда
		ИсходныеДанные.ИмяВТНачисления = "ВТНачисленияБезДоговоровГПХ";
		ПолучитьБухучетНачисленийБезДоговоровСоздатьВТ(ИсходныеДанные, "ВТБухучетНачисленияБезДоговоровГПХ");
		ИменаВТ.Добавить("ВТБухучетНачисленияБезДоговоровГПХ");
	КонецЕсли;
	
	БухучетНачислений = НоваяТаблицаДанныеДляОтраженияВБухучетеНачислений();
	
	Для каждого ИмяВТ Из ИменаВТ Цикл
	
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Начисления.Сотрудник КАК Сотрудник,
		|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
		|	Начисления.Подразделение КАК Подразделение,
		|	Начисления.Начисление КАК Начисление,
		|	Начисления.ДатаНачала КАК ДатаНачала,
		|	Начисления.ВидОперации КАК ВидОперации,
		|	Начисления.СтатьяРасходов КАК СтатьяРасходов,
		|	Начисления.ДокументОснование КАК ДокументОснование,
		|	ВЫБОР
		|		КОГДА Начисления.ДатаНачала = ДАТАВРЕМЯ(1, 1, 1)
		|				ИЛИ Начисления.ДатаНачала < Начисления.ПериодРегистрации
		|			ТОГДА Начисления.ПериодРегистрации
		|		ИНАЧЕ НАЧАЛОПЕРИОДА(Начисления.ДатаНачала, МЕСЯЦ)
		|	КОНЕЦ КАК ПериодПринятияРасходов,
		|	ВЫБОР
		|		КОГДА БухучетНачислений.ПодразделениеУчетаЗатрат = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
		|			ТОГДА Начисления.Подразделение
		|		ИНАЧЕ БухучетНачислений.ПодразделениеУчетаЗатрат
		|	КОНЕЦ КАК ПодразделениеУчетаЗатрат,
		|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
		|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	БухучетНачислений.Сумма КАК Сумма
		|ИЗ
		|	ВТБухучетНачислений КАК БухучетНачислений
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачисления КАК Начисления
		|		ПО БухучетНачислений.ИдентификаторСтроки = Начисления.ИдентификаторСтроки";
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТБухучетНачислений", ИмяВТ);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисления", СтрЗаменить(ИмяВТ,"Бухучет",""));
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(БухучетНачислений.Добавить(), Выборка);
		КонецЦикла;
	
	КонецЦикла;
	
	Возврат БухучетНачислений;
	
КонецФункции

Функция БухучетУдержаний(ТаблицаУдержаний, ТаблицаНачислений)
	
	НоваяТаблицаУдержаний = ОтражениеЗарплатыВБухучете.НоваяТаблицаБухучетУдержаннаяЗарплата();
	НастройкиУдержаний = ОписаниеУдержанийДляОформленияКассовогоЧека();
	
	ВидыОпераций = ОтражениеЗарплатыВБухучете.ВидыОперацийУдержанийТребующиеВводаДокументаОснования();
	ВидыОперацийДокументаОснования = ОбщегоНазначенияБЗККлиентСервер.МассивВСоответствие(ВидыОпераций);
	
	Начисления = ТаблицаНачислений.Скопировать(,"Сотрудник,Подразделение,ПодразделениеУчетаЗатрат,Сумма");
	Начисления.Свернуть("Сотрудник,Подразделение,ПодразделениеУчетаЗатрат","Сумма");
	
	БазаНачислений = Начисления.СкопироватьКолонки();
	Для каждого СтрокаТЗ Из Начисления Цикл
		Если СтрокаТЗ.Подразделение <> СтрокаТЗ.ПодразделениеУчетаЗатрат
					И ЗначениеЗаполнено(СтрокаТЗ.ПодразделениеУчетаЗатрат) Тогда
			ЗаполнитьЗначенияСвойств(БазаНачислений.Добавить(), СтрокаТЗ);
		КонецЕсли;
	КонецЦикла;
	БазаНачислений.Индексы.Добавить("Сотрудник,Подразделение");
	ОтборСтрок = Новый Структура("Сотрудник,Подразделение");
	
	Для каждого СтрокаТЗ Из ТаблицаУдержаний Цикл
		
		ИсключаемыеСвойства = "ДокументОснование";
		Если ВидыОперацийДокументаОснования[СтрокаТЗ.ВидОперации] = Истина Тогда
			ИсключаемыеСвойства = Неопределено;
		КонецЕсли;
	
		ЗаполнитьЗначенияСвойств(ОтборСтрок, СтрокаТЗ);
		СтрокиБазы = БазаНачислений.НайтиСтроки(ОтборСтрок);
		
		ЯвляетсяОснованиемОформленияКассовогоЧека = Ложь;
		ОписаниеУдержанияДляЧека = "";
		Если НастройкиУдержаний[СтрокаТЗ.Удержание] <> Неопределено Тогда
			ЯвляетсяОснованиемОформленияКассовогоЧека = Истина;
			ОписаниеУдержанияДляЧека = НастройкиУдержаний[СтрокаТЗ.Удержание];
		КонецЕсли;
		
		Если СтрокиБазы.Количество() = 0 Тогда
			
			НоваяСтрока = НоваяТаблицаУдержаний.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ,,ИсключаемыеСвойства);
			НоваяСтрока.ПодразделениеУчетаЗатрат = НоваяСтрока.Подразделение;
			НоваяСтрока.ЯвляетсяОснованиемОформленияКассовогоЧека = ЯвляетсяОснованиемОформленияКассовогоЧека;
			НоваяСтрока.ОписаниеУдержанияДляЧека = ОписаниеУдержанияДляЧека;
			
		Иначе
			
			Коэффициенты = ОбщегоНазначения.ВыгрузитьКолонку(СтрокиБазы,"Сумма");
			Результаты = ЗарплатаКадрыКлиентСервер.РаспределитьПропорциональноКоэффициентам(СтрокаТЗ.Сумма, Коэффициенты);
			
			Если Результаты = Неопределено Тогда
				
				НоваяСтрока = НоваяТаблицаУдержаний.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ,,ИсключаемыеСвойства);
				НоваяСтрока.ПодразделениеУчетаЗатрат = НоваяСтрока.Подразделение;
				НоваяСтрока.ЯвляетсяОснованиемОформленияКассовогоЧека = ЯвляетсяОснованиемОформленияКассовогоЧека;
				НоваяСтрока.ОписаниеУдержанияДляЧека = ОписаниеУдержанияДляЧека;
				
			Иначе
				
				Индекс = 0;
				Для Каждого СтрокаБазы Из СтрокиБазы Цикл
					
					НоваяСтрока = НоваяТаблицаУдержаний.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ,,ИсключаемыеСвойства);
					НоваяСтрока.Сумма = Результаты[Индекс];
					НоваяСтрока.ПодразделениеУчетаЗатрат = СтрокаБазы.ПодразделениеУчетаЗатрат;
					НоваяСтрока.ЯвляетсяОснованиемОформленияКассовогоЧека = ЯвляетсяОснованиемОформленияКассовогоЧека;
					НоваяСтрока.ОписаниеУдержанияДляЧека = ОписаниеУдержанияДляЧека;
					
					Индекс = Индекс + 1;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
	
	КонецЦикла;
	
	ОтражениеЗарплатыВУчете.СвернутьТаблицу(НоваяТаблицаУдержаний);
	
	Возврат НоваяТаблицаУдержаний;
	
КонецФункции 

Функция БухучетНачисленийПоСтатьям(Организация, Период, ДополнительныеПараметры = Неопределено)
	
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	
	ОтражениеЗарплатыВУчете.СоздатьВТНачислениеУдержаниеВидОперации(МенеджерВТ);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Период));
	Запрос.УстановитьПараметр("КонецПериода",КонецМесяца(Период));
	Запрос.УстановитьПараметр("РаботаВБюджетномУчреждении", ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении"));
	
	УсловиеПоСотрудникам = "ИСТИНА";
	УсловиеПоФизическимЛицам = "ИСТИНА";
	ИсключаемыйРегистратор = Неопределено;
	Если ДополнительныеПараметры <> Неопределено Тогда
		
		ДополнительныеПараметры.Свойство("ИсключаемыйРегистратор", ИсключаемыйРегистратор);
		
		Сотрудники = Неопределено;
		ДополнительныеПараметры.Свойство("Сотрудники", Сотрудники);
		Если Сотрудники <> Неопределено Тогда
			Запрос.УстановитьПараметр("Сотрудники", Сотрудники);
			УсловиеПоСотрудникам = "БухучетНачислений.Сотрудник В(&Сотрудники)";
		КонецЕсли;
		
		ФизическиеЛица = Неопределено;
		ДополнительныеПараметры.Свойство("ФизическиеЛица", ФизическиеЛица);
		Если ФизическиеЛица <> Неопределено Тогда
			Запрос.УстановитьПараметр("ФизическиеЛица", ФизическиеЛица);
			УсловиеПоФизическимЛицам = "БухучетНачислений.ФизическоеЛицо В(&ФизическиеЛица)";
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ИсключаемыйРегистратор", ИсключаемыйРегистратор);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	БухучетНачислений.Сотрудник КАК Сотрудник,
	|	БухучетНачислений.ФизическоеЛицо КАК ФизическоеЛицо,
	|	БухучетНачислений.Подразделение КАК Подразделение,
	|	БухучетНачислений.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|	БухучетНачислений.НачислениеУдержание КАК Начисление,
	|	БухучетНачислений.ВидОперации КАК ВидОперации,
	|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
	|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	БухучетНачислений.ДатаНачала КАК ДатаНачала,
	|	БухучетНачислений.ПериодПринятияРасходов КАК ПериодПринятияРасходов,
	|	СУММА(БухучетНачислений.ОтпускАвансом) КАК ОтпускАвансом,
	|	БухучетНачислений.ДокументОснование КАК ДокументОснование,
	|	СУММА(БухучетНачислений.Сумма) КАК Сумма
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА БухучетНачислений.ДатаНачала = ДАТАВРЕМЯ(1, 1, 1)
	|					ИЛИ БухучетНачислений.ДатаНачала < &НачалоПериода
	|				ТОГДА &НачалоПериода
	|			ИНАЧЕ НАЧАЛОПЕРИОДА(БухучетНачислений.ДатаНачала, МЕСЯЦ)
	|		КОНЕЦ КАК ПериодПринятияРасходов,
	|		БухучетНачислений.ФизическоеЛицо КАК ФизическоеЛицо,
	|		БухучетНачислений.Сотрудник КАК Сотрудник,
	|		БухучетНачислений.Подразделение КАК Подразделение,
	|		БухучетНачислений.НачислениеУдержание КАК НачислениеУдержание,
	|		ВЫБОР
	|			КОГДА &РаботаВБюджетномУчреждении
	|					И БухучетНачислений.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ЕжегодныйОтпускАвансом)
	|				ТОГДА НачислениеУдержаниеВидОперации.ВидОперации
	|			ИНАЧЕ БухучетНачислений.ВидОперации
	|		КОНЕЦ КАК ВидОперации,
	|		ВЫБОР
	|			КОГДА БухучетНачислений.ПодразделениеУчетаЗатрат = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
	|				ТОГДА БухучетНачислений.Подразделение
	|			ИНАЧЕ БухучетНачислений.ПодразделениеУчетаЗатрат
	|		КОНЕЦ КАК ПодразделениеУчетаЗатрат,
	|		БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
	|		ВЫБОР
	|			КОГДА НЕ &РаботаВБюджетномУчреждении
	|					И БухучетНачислений.НачислениеУдержание = ЗНАЧЕНИЕ(Перечисление.ВидыОсобыхНачисленийИУдержаний.СтоимостьПодарковПризов)
	|				ТОГДА ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
	|			ИНАЧЕ БухучетНачислений.СтатьяРасходов
	|		КОНЕЦ КАК СтатьяРасходов,
	|		БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|		БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|		БухучетНачислений.ДатаНачала КАК ДатаНачала,
	|		БухучетНачислений.ДокументОснование КАК ДокументОснование,
	|		БухучетНачислений.Сумма КАК Сумма,
	|		ВЫБОР
	|			КОГДА &РаботаВБюджетномУчреждении
	|					И БухучетНачислений.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ЕжегодныйОтпускАвансом)
	|				ТОГДА БухучетНачислений.Сумма
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК ОтпускАвансом
	|	ИЗ
	|		РегистрНакопления.БухучетНачисленияУдержанияПоСотрудникам КАК БухучетНачислений
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТНачислениеУдержаниеВидОперации КАК НачислениеУдержаниеВидОперации
	|			ПО БухучетНачислений.НачислениеУдержание = НачислениеУдержаниеВидОперации.НачислениеУдержание
	|	ГДЕ
	|		БухучетНачислений.Организация = &Организация
	|		И БухучетНачислений.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И (БухучетНачислений.ГруппаНачисленияУдержанияВыплаты В (ЗНАЧЕНИЕ(Перечисление.ГруппыНачисленияУдержанияВыплаты.Начислено), ЗНАЧЕНИЕ(Перечисление.ГруппыНачисленияУдержанияВыплаты.Льготы))
	|				ИЛИ БухучетНачислений.ГруппаНачисленияУдержанияВыплаты = ЗНАЧЕНИЕ(Перечисление.ГруппыНачисленияУдержанияВыплаты.Справочно)
	|					И (БухучетНачислений.НачислениеУдержание ССЫЛКА ПланВидовРасчета.Начисления
	|						ИЛИ БухучетНачислений.НачислениеУдержание = ЗНАЧЕНИЕ(Перечисление.ВидыОсобыхНачисленийИУдержаний.СтоимостьПодарковПризов)))
	|		И БухучетНачислений.Регистратор <> &ИсключаемыйРегистратор
	|		И &УсловиеПоСотрудникам
	|		И &УсловиеПоФизическимЛицам
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА БухучетНачислений.Период = ДАТАВРЕМЯ(1, 1, 1)
	|					ИЛИ БухучетНачислений.Период < &НачалоПериода
	|				ТОГДА &НачалоПериода
	|			ИНАЧЕ НАЧАЛОПЕРИОДА(БухучетНачислений.Период, МЕСЯЦ)
	|		КОНЕЦ,
	|		БухучетНачислений.ФизическоеЛицо,
	|		БухучетНачислений.Сотрудник,
	|		БухучетНачислений.Подразделение,
	|		БухучетНачислений.НачислениеУдержание,
	|		БухучетНачислений.ВидОперации,
	|		БухучетНачислений.Подразделение,
	|		БухучетНачислений.СтатьяФинансирования,
	|		БухучетНачислений.СтатьяРасходов,
	|		БухучетНачислений.СпособОтраженияЗарплатыВБухучете,
	|		БухучетНачислений.ОблагаетсяЕНВД,
	|		БухучетНачислений.Период,
	|		БухучетНачислений.ДокументОснование,
	|		БухучетНачислений.Сумма,
	|		0
	|	ИЗ
	|		РегистрНакопления.БухучетНачисленияУдержанияПоКонтрагентамАкционерам КАК БухучетНачислений
	|	ГДЕ
	|		БухучетНачислений.Организация = &Организация
	|		И БухучетНачислений.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И БухучетНачислений.ГруппаНачисленияУдержанияВыплаты = ЗНАЧЕНИЕ(Перечисление.ГруппыНачисленияУдержанияВыплаты.Начислено)
	|		И БухучетНачислений.Регистратор <> &ИсключаемыйРегистратор
	|		И &УсловиеПоСотрудникам
	|		И &УсловиеПоФизическимЛицам) КАК БухучетНачислений
	|
	|СГРУППИРОВАТЬ ПО
	|	БухучетНачислений.СтатьяРасходов,
	|	БухучетНачислений.ОблагаетсяЕНВД,
	|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачислений.ДатаНачала,
	|	БухучетНачислений.НачислениеУдержание,
	|	БухучетНачислений.ПодразделениеУчетаЗатрат,
	|	БухучетНачислений.ПериодПринятияРасходов,
	|	БухучетНачислений.ФизическоеЛицо,
	|	БухучетНачислений.Сотрудник,
	|	БухучетНачислений.Подразделение,
	|	БухучетНачислений.ВидОперации,
	|	БухучетНачислений.СтатьяФинансирования,
	|	БухучетНачислений.ДокументОснование
	|
	|ИМЕЮЩИЕ
	|	СУММА(БухучетНачислений.Сумма) <> 0";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПоСотрудникам", УсловиеПоСотрудникам);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПоФизическимЛицам", УсловиеПоФизическимЛицам);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция БухучетУдержанийПоСтатьям(Организация, ПериодРегистрации)

	БухучетУдержаний = ОтражениеЗарплатыВБухучете.НоваяТаблицаБухучетУдержаннаяЗарплата();
	
	ВидыОпераций = ОтражениеЗарплатыВБухучете.ВидыОперацийУдержанийТребующиеВводаДокументаОснования();
	ВидыОперацийДокументаОснования = ОбщегоНазначенияБЗККлиентСервер.МассивВСоответствие(ВидыОпераций);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("КонецПериода",КонецМесяца(ПериодРегистрации));
	ВидыОсобыхНачисленийИУдержанийНДФЛ = ОтражениеЗарплатыВУчете.ВидыОсобыхНачисленийИУдержанийНДФЛ();
	Запрос.УстановитьПараметр("ВидыОсобыхНачисленийИУдержанийНДФЛ", ВидыОсобыхНачисленийИУдержанийНДФЛ);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	БухучетУдержаний.ФизическоеЛицо КАК ФизическоеЛицо,
	|	БухучетУдержаний.Сотрудник КАК Сотрудник,
	|	БухучетУдержаний.ВидОперации КАК ВидОперации,
	|	БухучетУдержаний.Подразделение КАК Подразделение,
	|	БухучетУдержаний.ДатаНачала КАК ДатаНачала,
	|	БухучетУдержаний.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|	БухучетУдержаний.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетУдержаний.СтатьяРасходов КАК СтатьяРасходов,
	|	БухучетУдержаний.Контрагент КАК Контрагент,
	|	СУММА(БухучетУдержаний.Сумма) КАК Сумма,
	|	БухучетУдержаний.ЯвляетсяОснованиемОформленияКассовогоЧека КАК ЯвляетсяОснованиемОформленияКассовогоЧека,
	|	БухучетУдержаний.ОписаниеУдержанияДляЧека КАК ОписаниеУдержанияДляЧека,
	|	БухучетУдержаний.ДокументОснование КАК ДокументОснование
	|ИЗ
	|	(ВЫБРАТЬ
	|		БухучетУдержаний.ФизическоеЛицо КАК ФизическоеЛицо,
	|		БухучетУдержаний.Сотрудник КАК Сотрудник,
	|		БухучетУдержаний.ВидОперации КАК ВидОперации,
	|		БухучетУдержаний.Подразделение КАК Подразделение,
	|		БухучетУдержаний.ДатаНачала КАК ДатаНачала,
	|		ВЫБОР
	|			КОГДА БухучетУдержаний.ПодразделениеУчетаЗатрат = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
	|				ТОГДА БухучетУдержаний.Подразделение
	|			ИНАЧЕ БухучетУдержаний.ПодразделениеУчетаЗатрат
	|		КОНЕЦ КАК ПодразделениеУчетаЗатрат,
	|		БухучетУдержаний.СтатьяФинансирования КАК СтатьяФинансирования,
	|		БухучетУдержаний.СтатьяРасходов КАК СтатьяРасходов,
	|		БухучетУдержаний.Контрагент КАК Контрагент,
	|		БухучетУдержаний.Сумма КАК Сумма,
	|		ЕСТЬNULL(Удержания.ЯвляетсяОснованиемОформленияКассовогоЧека, ЛОЖЬ) КАК ЯвляетсяОснованиемОформленияКассовогоЧека,
	|		ВЫБОР
	|			КОГДА Удержания.ЯвляетсяОснованиемОформленияКассовогоЧека = ИСТИНА
	|				ТОГДА Удержания.Наименование
	|			ИНАЧЕ """"
	|		КОНЕЦ КАК ОписаниеУдержанияДляЧека,
	|		БухучетУдержаний.ДокументОснование КАК ДокументОснование
	|	ИЗ
	|		РегистрНакопления.БухучетНачисленияУдержанияПоСотрудникам КАК БухучетУдержаний
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.Удержания КАК Удержания
	|			ПО БухучетУдержаний.НачислениеУдержание = Удержания.Ссылка
	|	ГДЕ
	|		БухучетУдержаний.Организация = &Организация
	|		И БухучетУдержаний.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И БухучетУдержаний.Сумма <> 0
	|		И (БухучетУдержаний.ГруппаНачисленияУдержанияВыплаты = ЗНАЧЕНИЕ(Перечисление.ГруппыНачисленияУдержанияВыплаты.Удержано)
	|					И НЕ БухучетУдержаний.НачислениеУдержание В (&ВидыОсобыхНачисленийИУдержанийНДФЛ)
	|				ИЛИ БухучетУдержаний.НачислениеУдержание = ЗНАЧЕНИЕ(Перечисление.ВидыОсобыхНачисленийИУдержаний.НачисленоПроцентовПоЗайму))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		БухучетУдержаний.ФизическоеЛицо,
	|		БухучетУдержаний.Сотрудник,
	|		БухучетУдержаний.ВидОперации,
	|		БухучетУдержаний.Подразделение,
	|		&НачалоПериода,
	|		БухучетУдержаний.Подразделение,
	|		БухучетУдержаний.СтатьяФинансирования,
	|		БухучетУдержаний.СтатьяРасходов,
	|		БухучетУдержаний.Контрагент,
	|		БухучетУдержаний.Сумма,
	|		ЕСТЬNULL(Удержания.ЯвляетсяОснованиемОформленияКассовогоЧека, ЛОЖЬ),
	|		ВЫБОР
	|			КОГДА Удержания.ЯвляетсяОснованиемОформленияКассовогоЧека = ИСТИНА
	|				ТОГДА Удержания.Наименование
	|			ИНАЧЕ """"
	|		КОНЕЦ,
	|		БухучетУдержаний.ДокументОснование
	|	ИЗ
	|		РегистрНакопления.БухучетНачисленияУдержанияПоКонтрагентамАкционерам КАК БухучетУдержаний
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.Удержания КАК Удержания
	|			ПО БухучетУдержаний.НачислениеУдержание = Удержания.Ссылка
	|	ГДЕ
	|		БухучетУдержаний.Организация = &Организация
	|		И БухучетУдержаний.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И БухучетУдержаний.Сумма <> 0
	|		И (БухучетУдержаний.ГруппаНачисленияУдержанияВыплаты = ЗНАЧЕНИЕ(Перечисление.ГруппыНачисленияУдержанияВыплаты.Удержано)
	|					И НЕ БухучетУдержаний.НачислениеУдержание В (&ВидыОсобыхНачисленийИУдержанийНДФЛ)
	|				ИЛИ БухучетУдержаний.НачислениеУдержание = ЗНАЧЕНИЕ(Перечисление.ВидыОсобыхНачисленийИУдержаний.НачисленоПроцентовПоЗайму))) КАК БухучетУдержаний
	|
	|СГРУППИРОВАТЬ ПО
	|	БухучетУдержаний.Подразделение,
	|	БухучетУдержаний.ВидОперации,
	|	БухучетУдержаний.ПодразделениеУчетаЗатрат,
	|	БухучетУдержаний.СтатьяРасходов,
	|	БухучетУдержаний.Контрагент,
	|	БухучетУдержаний.ЯвляетсяОснованиемОформленияКассовогоЧека,
	|	БухучетУдержаний.СтатьяФинансирования,
	|	БухучетУдержаний.ОписаниеУдержанияДляЧека,
	|	БухучетУдержаний.ДатаНачала,
	|	БухучетУдержаний.Сотрудник,
	|	БухучетУдержаний.ФизическоеЛицо,
	|	БухучетУдержаний.ДокументОснование
	|
	|ИМЕЮЩИЕ
	|	СУММА(БухучетУдержаний.Сумма) <> 0";
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ИсключаемыеСвойства = "ДокументОснование";
		Если ВидыОперацийДокументаОснования[Выборка.ВидОперации] = Истина Тогда
			ИсключаемыеСвойства = Неопределено;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(БухучетУдержаний.Добавить(), Выборка,,ИсключаемыеСвойства);
	КонецЦикла;
	
	Возврат БухучетУдержаний;

КонецФункции

Функция БухучетНДФЛПоСтатьям(Организация, ПериодРегистрации)
	
	БухучетНДФЛ = ОтражениеЗарплатыВБухучете.НоваяТаблицаБухучетНачисленныйНДФЛ();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("КонецПериода",КонецМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("Организация", Организация);
	ВидыОсобыхНачисленийИУдержанийНДФЛ = ОтражениеЗарплатыВУчете.ВидыОсобыхНачисленийИУдержанийНДФЛ(Истина);
	Запрос.УстановитьПараметр("ВидыОсобыхНачисленийИУдержанийНДФЛ", ВидыОсобыхНачисленийИУдержанийНДФЛ);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	БухучетНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
	|	БухучетНДФЛ.Сотрудник КАК Сотрудник,
	|	БухучетНДФЛ.ВидОперации КАК ВидОперации,
	|	БухучетНДФЛ.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетНДФЛ.СтатьяРасходов КАК СтатьяРасходов,
	|	БухучетНДФЛ.Сумма КАК Сумма,
	|	БухучетНДФЛ.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ЕСТЬNULL(РегистрацииВНалоговомОргане.КодПоОКАТО, """") КАК КодПоОКАТО,
	|	ЕСТЬNULL(РегистрацииВНалоговомОргане.КодПоОКТМО, """") КАК КодПоОКТМО,
	|	ЕСТЬNULL(РегистрацииВНалоговомОргане.КПП, """") КАК КПП,
	|	ЕСТЬNULL(РегистрацииВНалоговомОргане.Код, """") КАК КодНалоговогоОргана
	|ИЗ
	|	(ВЫБРАТЬ
	|		БухучетНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
	|		БухучетНДФЛ.Сотрудник КАК Сотрудник,
	|		БухучетНДФЛ.ВидОперации КАК ВидОперации,
	|		БухучетНДФЛ.СтатьяФинансирования КАК СтатьяФинансирования,
	|		БухучетНДФЛ.СтатьяРасходов КАК СтатьяРасходов,
	|		СУММА(БухучетНДФЛ.Сумма) КАК Сумма,
	|		БухучетНДФЛ.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
	|	ИЗ
	|		РегистрНакопления.БухучетНачисленияУдержанияПоСотрудникам КАК БухучетНДФЛ
	|	ГДЕ
	|		БухучетНДФЛ.Организация = &Организация
	|		И БухучетНДФЛ.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И БухучетНДФЛ.НачислениеУдержание В(&ВидыОсобыхНачисленийИУдержанийНДФЛ)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		БухучетНДФЛ.ФизическоеЛицо,
	|		БухучетНДФЛ.СтатьяРасходов,
	|		БухучетНДФЛ.СтатьяФинансирования,
	|		БухучетНДФЛ.ВидОперации,
	|		БухучетНДФЛ.Сотрудник,
	|		БухучетНДФЛ.РегистрацияВНалоговомОргане
	|	
	|	ИМЕЮЩИЕ
	|		СУММА(БухучетНДФЛ.Сумма) <> 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		БухучетНДФЛ.ФизическоеЛицо,
	|		БухучетНДФЛ.Сотрудник,
	|		БухучетНДФЛ.ВидОперации,
	|		БухучетНДФЛ.СтатьяФинансирования,
	|		БухучетНДФЛ.СтатьяРасходов,
	|		СУММА(БухучетНДФЛ.Сумма),
	|		БухучетНДФЛ.РегистрацияВНалоговомОргане
	|	ИЗ
	|		РегистрНакопления.БухучетНачисленияУдержанияПоКонтрагентамАкционерам КАК БухучетНДФЛ
	|	ГДЕ
	|		БухучетНДФЛ.Организация = &Организация
	|		И БухучетНДФЛ.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И БухучетНДФЛ.НачислениеУдержание В(&ВидыОсобыхНачисленийИУдержанийНДФЛ)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		БухучетНДФЛ.ФизическоеЛицо,
	|		БухучетНДФЛ.СтатьяРасходов,
	|		БухучетНДФЛ.СтатьяФинансирования,
	|		БухучетНДФЛ.ВидОперации,
	|		БухучетНДФЛ.Сотрудник,
	|		БухучетНДФЛ.РегистрацияВНалоговомОргане
	|	
	|	ИМЕЮЩИЕ
	|		СУММА(БухучетНДФЛ.Сумма) <> 0) КАК БухучетНДФЛ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.РегистрацииВНалоговомОргане КАК РегистрацииВНалоговомОргане
	|		ПО БухучетНДФЛ.РегистрацияВНалоговомОргане = РегистрацииВНалоговомОргане.Ссылка";
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(БухучетНДФЛ.Добавить(), Выборка);
	КонецЦикла;
	
	Возврат БухучетНДФЛ;
	
КонецФункции

Функция БухучетВзносовПоСтатьям(Организация, Период, Сотрудники = Неопределено)
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОтложенноеПроведениеДокументов") Тогда 
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОтражениеДокументовВУчетеСтраховыхВзносов");
		Модуль.ОтразитьДокументыВУчетеСтраховыхВзносов(Организация);
	КонецЕсли;
	
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	
	ОтражениеЗарплатыВУчете.СоздатьВТНачислениеУдержаниеВидОперации(МенеджерВТ);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Период));
	Запрос.УстановитьПараметр("КонецПериода",КонецМесяца(Период));
	Запрос.УстановитьПараметр("Сотрудники", Сотрудники);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СтраховыеВзносы.Сотрудник КАК Сотрудник,
	|	СтраховыеВзносы.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СтраховыеВзносы.Подразделение КАК Подразделение,
	|	СтраховыеВзносы.Начисление КАК Начисление,
	|	НачислениеУдержаниеВидОперации.ВидОперации КАК ВидОперации,
	|	СУММА(0) КАК СтраховыеВзносы,
	|	ВЫБОР
	|		КОГДА СтраховыеВзносы.ПодразделениеУчетаЗатрат = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
	|			ТОГДА СтраховыеВзносы.Подразделение
	|		ИНАЧЕ СтраховыеВзносы.ПодразделениеУчетаЗатрат
	|	КОНЕЦ КАК ПодразделениеУчетаЗатрат,
	|	СтраховыеВзносы.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	СтраховыеВзносы.СтатьяФинансирования КАК СтатьяФинансирования,
	|	СтраховыеВзносы.СтатьяРасходов КАК СтатьяРасходов,
	|	СтраховыеВзносы.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	ВЫБОР
	|		КОГДА СтраховыеВзносы.ДатаНачала = ДАТАВРЕМЯ(1, 1, 1)
	|				ИЛИ СтраховыеВзносы.ДатаНачала < &НачалоПериода
	|			ТОГДА &НачалоПериода
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(СтраховыеВзносы.ДатаНачала, МЕСЯЦ)
	|	КОНЕЦ КАК ПериодПринятияРасходов,
	|	СтраховыеВзносы.ДатаНачала КАК ДатаНачала
	|ИЗ
	|	РегистрНакопления.СтраховыеВзносыПоФизическимЛицам КАК СтраховыеВзносы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачислениеУдержаниеВидОперации КАК НачислениеУдержаниеВидОперации
	|		ПО СтраховыеВзносы.Начисление = НачислениеУдержаниеВидОперации.НачислениеУдержание
	|ГДЕ
	|	СтраховыеВзносы.Организация = &Организация
	|	И СтраховыеВзносы.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И &УсловиеПоСотрудникам
	|
	|СГРУППИРОВАТЬ ПО
	|	СтраховыеВзносы.ФизическоеЛицо,
	|	НачислениеУдержаниеВидОперации.ВидОперации,
	|	СтраховыеВзносы.ОблагаетсяЕНВД,
	|	СтраховыеВзносы.СтатьяФинансирования,
	|	СтраховыеВзносы.СтатьяРасходов,
	|	СтраховыеВзносы.СпособОтраженияЗарплатыВБухучете,
	|	ВЫБОР
	|		КОГДА СтраховыеВзносы.ДатаНачала = ДАТАВРЕМЯ(1, 1, 1)
	|				ИЛИ СтраховыеВзносы.ДатаНачала < &НачалоПериода
	|			ТОГДА &НачалоПериода
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(СтраховыеВзносы.ДатаНачала, МЕСЯЦ)
	|	КОНЕЦ,
	|	СтраховыеВзносы.Подразделение,
	|	СтраховыеВзносы.Начисление,
	|	СтраховыеВзносы.ДатаНачала,
	|	СтраховыеВзносы.Сотрудник,
	|	ВЫБОР
	|		КОГДА СтраховыеВзносы.ПодразделениеУчетаЗатрат = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
	|			ТОГДА СтраховыеВзносы.Подразделение
	|		ИНАЧЕ СтраховыеВзносы.ПодразделениеУчетаЗатрат
	|	КОНЕЦ";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &УсловиеПоСотрудникам", ?(Сотрудники = Неопределено,"","И СтраховыеВзносы.Сотрудник В(&Сотрудники)"));
	
	ТекстПолейВзносов = "";
	КолонкиВзносов = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(УчетСтраховыхВзносов.ОтражаемыеВУчетеВзносы(Истина));
	ВзносыОбъединены = Период >= УчетСтраховыхВзносов.ДатаОбъединенияВзносов();
	Для каждого ИмяПоля Из КолонкиВзносов Цикл
		Если ВзносыОбъединены И (ВРег(ИмяПоля) = ВРег("ПФРДоПредельнойВеличины") Или ВРег(ИмяПоля) = ВРег("ПФРСПревышения")) Тогда
			Продолжить;
		КонецЕсли;
		ТекстПолейВзносов = ТекстПолейВзносов + "СУММА(СтраховыеВзносы." + ИмяПоля + ") КАК " + ИмяПоля + "," + Символы.ПС;
	КонецЦикла;
	Если ВзносыОбъединены Тогда
		ТекстПолейВзносов = ТекстПолейВзносов + "СУММА(СтраховыеВзносы.ПФРДоПредельнойВеличины + СтраховыеВзносы.ПФРСПревышения) КАК ВзносыПоЕдиномуТарифу," + Символы.ПС;
	КонецЕсли;
	Запрос.Текст = СтрЗаменить(ТекстЗапроса, "СУММА(0) КАК СтраховыеВзносы,", ТекстПолейВзносов);
	
	БухучетВзносов = Запрос.Выполнить().Выгрузить();
	
	Возврат БухучетВзносов;

КонецФункции

Функция БухучетНачисленийИВзносов(БухучетНачислений, БухучетВзносов)
	
	НачислениеВидТрудаДляНУ = ОтражениеЗарплатыВБухучете.НачислениеВидНачисленияОплатыТрудаДляНУ();
	
	Таблица = ОтражениеЗарплатыВБухучете.НоваяТаблицаБухучетНачисленнаяЗарплатаИВзносы();
	Таблица.Колонки.Добавить("ОтпускАвансом", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	
	Для каждого СтрокаТЗ Из БухучетНачислений Цикл
		Если СтрокаТЗ.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ДивидендыСотрудников
			Или СтрокаТЗ.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.Дивиденды Тогда
			Продолжить;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(Таблица.Добавить(), СтрокаТЗ);
	КонецЦикла;
	
	Для каждого СтрокаТЗ Из БухучетВзносов Цикл
		ЗаполнитьЗначенияСвойств(Таблица.Добавить(), СтрокаТЗ);
	КонецЦикла;
	
	ОтражениеЗарплатыВУчете.СвернутьТаблицу(Таблица);
	
	СтрокиОтпускаАвансом = Новый Массив;
	Для каждого СтрокаТЗ Из Таблица Цикл
		
		СтрокаТЗ.ВидНачисленияОплатыТрудаДляНУ = НачислениеВидТрудаДляНУ[СтрокаТЗ.Начисление];
		
		Если СтрокаТЗ.ОтпускАвансом <> 0 И СтрокаТЗ.Сумма <> 0 Тогда
			СтрокиОтпускаАвансом.Добавить(СтрокаТЗ);
		КонецЕсли;
		
	КонецЦикла;
	
	ПоляВзносов = ОтражениеЗарплатыВУчете.КолонкиВзносов();
	Для каждого СтрокаТЗ Из СтрокиОтпускаАвансом Цикл
		
		ДополнительнаяСтрока = Таблица.Добавить();
		ЗаполнитьЗначенияСвойств(ДополнительнаяСтрока, СтрокаТЗ);
		
		ДополнительнаяСтрока.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпускАвансом;
		
		ДоляАванса = СтрокаТЗ.ОтпускАвансом/СтрокаТЗ.Сумма;
		СтрокаТЗ.Сумма = СтрокаТЗ.Сумма - СтрокаТЗ.ОтпускАвансом;
		ДополнительнаяСтрока.Сумма = СтрокаТЗ.ОтпускАвансом;
		
		Для каждого ПолеВзносов Из ПоляВзносов Цикл
			
			СуммаВзносаАванс = Окр(СтрокаТЗ[ПолеВзносов] * ДоляАванса, 2);
			СуммаВзноса = СтрокаТЗ[ПолеВзносов] - СуммаВзносаАванс;
			
			СтрокаТЗ[ПолеВзносов] = СуммаВзноса;
			ДополнительнаяСтрока[ПолеВзносов] = СуммаВзносаАванс;
			
		КонецЦикла;
		
	КонецЦикла;
		
	Таблица.Колонки.Удалить("ОтпускАвансом");
		
	Возврат Таблица;
	
КонецФункции

#КонецОбласти

#Область ПолучениеНастроекБухучета

// Получает настройки бухучета по умолчанию, которые будут использоваться для распределения
// результатов расчета по  т.н. "по базе по умолчанию", когда нет точных сведений
// для распределения результатов, но поля бухучета необходимо заполнить ожидаемыми значениями.
// Результат помещается во временную таблицу ВТНастройкиБухучета.
// Настройки получаются из всех мест хранения с учетом приоритетов, если в настройках указано распределение,
// оно игнорируется, т.е. каждой строке входной таблицы соответствует одна строка выходной таблицы.
//
// Поля таблицы ВТНастройкиБухучета
// 			* ИдентификаторСтроки
// 			* Организация
// 			* Подразделение
// 			* ТерриторияВыполненияРаботВОрганизации
// 			* Сотрудник
// 			* Период
// 			* СтатьяФинансирования
// 			* СпособОтраженияЗарплатыВБухучете
// 			* ОтношениеКЕНВД
//
// Параметры:
// 	МенеджерВТ - МенеджерВременныхТаблиц - содержит таблицу с именем, указанном в параметре ИмяИсходнойВТ.
// 			Таблица должна содержать поля:
// 			* Организация
// 			* Сотрудник
// 			* Период
// 			* Подразделение
// 			* ТерриторияВыполненияРаботВОрганизации
// 			* ИдентификаторСтроки
// 	ИмяИсходнойВТ - Строка - имя временной таблицы для получения настроек.
//
Процедура СоздатьВТНастройкиБухучетаДляРаспределенияПоУмолчанию(МенеджерВТ, ИмяИсходнойВТ)
	
	ИменаВТСПромежуточнымиРезультатами = Новый Массив;
	ПрочитатьНастройкиБухучетаСоздатьВременныеТаблицы(МенеджерВТ, ИмяИсходнойВТ, ИменаВТСПромежуточнымиРезультатами);
	
	ИмяИтоговойТаблицы = "ВТНастройкиБухучета";
	СоздатьВТНастройкиБухучетаБезУчетаНастроекРаспределения(МенеджерВТ, ИмяИсходнойВТ, ИмяИтоговойТаблицы);
	
	ЗарплатаКадры.УничтожитьВТ(МенеджерВТ, ИменаВТСПромежуточнымиРезультатами);

КонецПроцедуры

// Создает временную таблицу ВТИсходнаяТаблицаДляНастроекБухучета, для получения "полных" настроек бухучета,
// в т.ч. учитывающих настройки бухучета заданные в виде распределения.
// Временная таблица ВТИсходнаяТаблицаДляНастроекБухучета содержит поля:
// 					* Организация
// 					* Подразделение
// 					* ТерриторияВыполненияРаботВОрганизации
// 					* Сотрудник
// 					* ИсходныйПериод - значение соответствует полю Период из таблицы ИмяИсходнойВТ.
// 					* Период - значение поля Период из таблицы ИмяИсходнойВТ, ограниченное значением из параметра Период.
// 					* ИдентификаторСтроки
//
// Параметры:
// 	МенеджерВТ - МенеджерВременныхТаблиц - содержит временную таблицу с именем ИмяИсходнойВТ,
// 					должна содержать поля:
// 					* Сотрудник - имя поля может уточняться
// 					* Период - имя поля может уточняться
// 					может содержать поля:
// 					* Организация
// 					* Подразделение
// 					* ТерриторияВыполненияРаботВОрганизации
// 	ИмяИсходнойВТ - Строка - имя входной временной таблицы
// 	Период - Дата - определяет период, для которого получаются настройки,
// 					что бы не учитывать данные будущих периодов.
// Параметры - Структура - не обязательный, может содержать свойства:
// 					* ИмяПоляСотрудник - уточняет имя поля Сотрудник во временной таблице с именем ИмяИсходнойВТ.
// 					* ИмяПоляПериод - уточняет имя поля Период во временной таблице с именем ИмяИсходнойВТ.
// 					* Организация - передается Организация, если во временной таблице отсутствует поле Организация.
// 					* Подразделение - передается Подразделение, если во временной таблице отсутствует поле Подразделение.
// 					* ТерриторияВыполненияРаботВОрганизации - передается ТерриторияВыполненияРаботВОрганизации,
//						если во временной таблице отсутствует поле ТерриторияВыполненияРаботВОрганизации.
//
Процедура СоздатьВТИсходнаяТаблицаДляНастроекБухучета(МенеджерВТ, ИмяИсходнойВТ, Период, Параметры = Неопределено)
	
	ИмяПоляСотрудник 	= Неопределено;
	ИмяПоляПериод 		= Неопределено;
	Организация 		= Неопределено;
	Подразделение 		= Неопределено;
	ТерриторияВыполненияРаботВОрганизации = Неопределено;
	
	Если Параметры <> Неопределено Тогда
		Параметры.Свойство("ИмяПоляСотрудник", 	ИмяПоляСотрудник);
		Параметры.Свойство("ИмяПоляПериод", 	ИмяПоляПериод);
		Параметры.Свойство("Организация", 		Организация);
		Параметры.Свойство("Подразделение", 	Подразделение);
		Параметры.Свойство("ТерриторияВыполненияРаботВОрганизации", ТерриторияВыполненияРаботВОрганизации);
	КонецЕсли;
	
	ИмяПоляСотрудник = ?(ИмяПоляСотрудник = Неопределено, "Сотрудник", ИмяПоляСотрудник);
	ИмяПоляСотрудник = "Таблица." + ИмяПоляСотрудник;
	
	ИмяПоляПериод = ?(ИмяПоляПериод = Неопределено, "Период", ИмяПоляПериод);
	ИмяПоляПериод = "Таблица." + ИмяПоляПериод;

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	
	Если Организация = Неопределено Тогда
		ИмяПоляОрганизация = "Таблица.Организация";
	Иначе
		ИмяПоляОрганизация = "&Организация";
		Запрос.УстановитьПараметр("Организация", Организация);
	КонецЕсли;
	
	Если Подразделение = Неопределено Тогда
		ИмяПоляПодразделение = "Таблица.Подразделение";
	Иначе
		ИмяПоляПодразделение = "&Подразделение";
		Запрос.УстановитьПараметр("Подразделение", Подразделение);
	КонецЕсли;
	
	Если ТерриторияВыполненияРаботВОрганизации = Неопределено Тогда
		ИмяПоляТерритория = "Таблица.ТерриторияВыполненияРаботВОрганизации";
	Иначе
		ИмяПоляТерритория = "&ТерриторияВыполненияРаботВОрганизации";
		Запрос.УстановитьПараметр("ТерриторияВыполненияРаботВОрганизации", ТерриторияВыполненияРаботВОрганизации);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ОкончаниеПериода", НачалоДня(КонецМесяца(Период)));
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&ИмяПоляОрганизация КАК Организация,
	|	&ИмяПоляПодразделение КАК Подразделение,
	|	&ИмяПоляТерритория КАК ТерриторияВыполненияРаботВОрганизации,
	|	&ИмяПоляСотрудник КАК Сотрудник,
	|	&ИмяПоляПериод КАК ИсходныйПериод,
	|	ВЫБОР
	|		КОГДА &ИмяПоляПериод > &ОкончаниеПериода
	|			ТОГДА &ОкончаниеПериода
	|		ИНАЧЕ &ИмяПоляПериод
	|	КОНЕЦ КАК Период,
	|	АВТОНОМЕРЗАПИСИ() КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ВТИсходнаяТаблицаДляНастроекБухучета
	|ИЗ
	|	ИмяВременнойТаблицы КАК Таблица";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ИмяВременнойТаблицы", 	ИмяИсходнойВТ);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИмяПоляСотрудник", 		ИмяПоляСотрудник);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИмяПоляПериод", 			ИмяПоляПериод);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИмяПоляПодразделение", 	ИмяПоляПодразделение);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИмяПоляТерритория", 		ИмяПоляТерритория);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИмяПоляОрганизация", 	ИмяПоляОрганизация);
	Запрос.Выполнить();

КонецПроцедуры

// Получает настройки бухучета и помещает их во временные таблицы.
//
// Параметры:
// 	МенеджерВТ - МенеджерВременныхТаблиц - содержит таблицу с именем ИмяИсходнойВТ.
// 	ИмяИсходнойВТ - Строка - имя временной таблицы.
// 	ИменаТаблицСНастройкамиБухучета - Массив - в массив будут помещены имена временных таблиц с настройками,
// 						используется для быстрого уничтожения этих таблиц.
//
Процедура ПрочитатьНастройкиБухучетаСоздатьВременныеТаблицы(МенеджерВТ, ИмяИсходнойВТ, ИменаТаблицСНастройкамиБухучета)
	
	ИменаТаблицСНастройкамиБухучета.Добавить("ВТБухучетОрганизаций");
	ИменаТаблицСНастройкамиБухучета.Добавить("ВТБухучетПодразделений");
	ИменаТаблицСНастройкамиБухучета.Добавить("ВТБухучетПодразделенийСРаспределением");
	ИменаТаблицСНастройкамиБухучета.Добавить("ВТБухучетТерриторий");
	ИменаТаблицСНастройкамиБухучета.Добавить("ВТБухучетТерриторийСРаспределением");
	ИменаТаблицСНастройкамиБухучета.Добавить("ВТБухучетСотрудников");
	
	УдалитьВТ = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	
	// Бухучет организации.
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Организация КАК Организация,
	|	Таблица.Период КАК Период
	|ПОМЕСТИТЬ ВТОтборОрганизаций
	|ИЗ
	|	ВТИсходнаяТаблица КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МаксимальныеПериоды.Организация КАК Организация,
	|	МаксимальныеПериоды.Период КАК Период,
	|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК ПодразделениеУчетаЗатрат,
	|	БухучетЗарплаты.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетЗарплаты.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетЗарплаты.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	ЛОЖЬ КАК ПодразделениеУчетаЗатратЗаполнено,
	|	БухучетЗарплаты.СтатьяФинансирования <> ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка) КАК СтатьяФинансированияЗаполнена,
	|	БухучетЗарплаты.СпособОтраженияЗарплатыВБухучете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка) КАК СпособОтраженияЗаполнен,
	|	БухучетЗарплаты.ОтношениеКЕНВД <> ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка) КАК ОтношениеКЕНВДЗаполнено
	|ПОМЕСТИТЬ ВТБухучетОрганизаций
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Организация КАК Организация,
	|		Таблица.Период КАК Период,
	|		МАКСИМУМ(БухучетЗарплаты.Период) КАК ПериодРегистра
	|	ИЗ
	|		ВТОтборОрганизаций КАК Таблица
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БухучетЗарплатыОрганизаций КАК БухучетЗарплаты
	|			ПО Таблица.Организация = БухучетЗарплаты.Организация
	|				И Таблица.Период >= БухучетЗарплаты.Период
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Таблица.Организация,
	|		Таблица.Период) КАК МаксимальныеПериоды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БухучетЗарплатыОрганизаций КАК БухучетЗарплаты
	|		ПО МаксимальныеПериоды.Организация = БухучетЗарплаты.Организация
	|			И МаксимальныеПериоды.ПериодРегистра = БухучетЗарплаты.Период
	|ГДЕ
	|	НЕ(БухучетЗарплаты.СтатьяФинансирования = ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|				И БухучетЗарплаты.СпособОтраженияЗарплатыВБухучете = ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
	|				И БухучетЗарплаты.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Организаций.Организация КАК Организация
	|ИЗ
	|	ВТОтборОрганизаций КАК Организаций";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТИсходнаяТаблица", ИмяИсходнойВТ);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	Организация = Выборка.Организация;
	
	УдалитьВТ.Добавить("ВТОтборОрганизаций");
	
	ИспользоватьОбособленныеТерритории = ПолучитьФункциональнуюОпцию("ИспользоватьОбособленныеТерритории", Новый Структура("Организация", Организация));
	
	// Бухучет подразделений.
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Подразделение КАК Подразделение,
	|	Таблица.Период КАК Период
	|ПОМЕСТИТЬ ВТОтборПодразделений
	|ИЗ
	|	ВТИсходнаяТаблица КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МаксимальныеПериоды.Подразделение КАК Подразделение,
	|	МаксимальныеПериоды.Период КАК Период,
	|	БухучетЗарплаты.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетЗарплаты.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетЗарплаты.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	ВЫБОР
	|		КОГДА БухучетЗарплаты.ДоляРаспределения = 0
	|			ТОГДА 1
	|		ИНАЧЕ БухучетЗарплаты.ДоляРаспределения
	|	КОНЕЦ КАК ДоляРаспределения,
	|	БухучетЗарплаты.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ВТБухучетПодразделенийВесь
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Подразделение КАК Подразделение,
	|		Таблица.Период КАК Период,
	|		МАКСИМУМ(БухучетЗарплаты.Период) КАК ПериодРегистра
	|	ИЗ
	|		ВТОтборПодразделений КАК Таблица
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БухучетЗарплатыПодразделений КАК БухучетЗарплаты
	|			ПО Таблица.Подразделение = БухучетЗарплаты.Подразделение
	|				И Таблица.Период >= БухучетЗарплаты.Период
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Таблица.Подразделение,
	|		Таблица.Период) КАК МаксимальныеПериоды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БухучетЗарплатыПодразделений КАК БухучетЗарплаты
	|		ПО МаксимальныеПериоды.Подразделение = БухучетЗарплаты.Подразделение
	|			И МаксимальныеПериоды.ПериодРегистра = БухучетЗарплаты.Период
	|ГДЕ
	|	НЕ(БухучетЗарплаты.СтатьяФинансирования = ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|				И БухучетЗарплаты.СпособОтраженияЗарплатыВБухучете = ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
	|				И БухучетЗарплаты.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Бухучет.Подразделение КАК Подразделение,
	|	Бухучет.Период КАК Период,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Бухучет.ИдентификаторСтроки) КАК КоличествоСтрок
	|ПОМЕСТИТЬ ВТБухучетПодразделенийСтрокиСРаспределением
	|ИЗ
	|	ВТБухучетПодразделенийВесь КАК Бухучет
	|
	|СГРУППИРОВАТЬ ПО
	|	Бухучет.Подразделение,
	|	Бухучет.Период
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Бухучет.ИдентификаторСтроки) > 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БухучетПодразделений.Подразделение КАК Подразделение,
	|	БухучетПодразделений.Период КАК Период,
	|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК ПодразделениеУчетаЗатрат,
	|	БухучетПодразделений.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетПодразделений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетПодразделений.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	ЛОЖЬ КАК ПодразделениеУчетаЗатратЗаполнено,
	|	БухучетПодразделений.СтатьяФинансирования <> ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка) КАК СтатьяФинансированияЗаполнена,
	|	БухучетПодразделений.СпособОтраженияЗарплатыВБухучете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка) КАК СпособОтраженияЗаполнен,
	|	БухучетПодразделений.ОтношениеКЕНВД <> ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка) КАК ОтношениеКЕНВДЗаполнено
	|ПОМЕСТИТЬ ВТБухучетПодразделений
	|ИЗ
	|	ВТБухучетПодразделенийВесь КАК БухучетПодразделений
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБухучетПодразделенийСтрокиСРаспределением КАК СтрокиСРаспределением
	|		ПО БухучетПодразделений.Подразделение = СтрокиСРаспределением.Подразделение
	|			И БухучетПодразделений.Период = СтрокиСРаспределением.Период
	|ГДЕ
	|	СтрокиСРаспределением.КоличествоСтрок ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БухучетПодразделений.Подразделение КАК Подразделение,
	|	БухучетПодразделений.Период КАК Период,
	|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК ПодразделениеУчетаЗатрат,
	|	БухучетПодразделений.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетПодразделений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетПодразделений.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	БухучетПодразделений.ДоляРаспределения КАК ДоляРаспределения,
	|	ЛОЖЬ КАК ПодразделениеУчетаЗатратЗаполнено,
	|	БухучетПодразделений.СтатьяФинансирования <> ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка) КАК СтатьяФинансированияЗаполнена,
	|	БухучетПодразделений.СпособОтраженияЗарплатыВБухучете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка) КАК СпособОтраженияЗаполнен,
	|	БухучетПодразделений.ОтношениеКЕНВД <> ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка) КАК ОтношениеКЕНВДЗаполнено
	|ПОМЕСТИТЬ ВТБухучетПодразделенийСРаспределением
	|ИЗ
	|	ВТБухучетПодразделенийВесь КАК БухучетПодразделений
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТБухучетПодразделенийСтрокиСРаспределением КАК СтрокиСРаспределением
	|		ПО БухучетПодразделений.Подразделение = СтрокиСРаспределением.Подразделение
	|			И БухучетПодразделений.Период = СтрокиСРаспределением.Период";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТИсходнаяТаблица", ИмяИсходнойВТ);
	Запрос.Выполнить();
	
	УдалитьВТ.Добавить("ВТОтборПодразделений");
	УдалитьВТ.Добавить("ВТБухучетПодразделенийВесь");
	УдалитьВТ.Добавить("ВТБухучетПодразделенийСтрокиСРаспределением");
	
	// Бухучет территорий.
	Если Не ИспользоватьОбособленныеТерритории Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 0
		|	ЗНАЧЕНИЕ(Справочник.ТерриторииВыполненияРабот.ПустаяСсылка) КАК ТерриторияВыполненияРаботВОрганизации,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК Период,
		|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК ПодразделениеУчетаЗатрат,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка) КАК СтатьяФинансирования,
		|	ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка) КАК СпособОтраженияЗарплатыВБухучете,
		|	ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка) КАК ОтношениеКЕНВД,
		|	ЛОЖЬ КАК ПодразделениеУчетаЗатратЗаполнено,
		|	ЛОЖЬ КАК СтатьяФинансированияЗаполнена,
		|	ЛОЖЬ КАК СпособОтраженияЗаполнен,
		|	ЛОЖЬ КАК ОтношениеКЕНВДЗаполнено
		|ПОМЕСТИТЬ ВТБухучетТерриторий
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 0
		|	ЗНАЧЕНИЕ(Справочник.ТерриторииВыполненияРабот.ПустаяСсылка) КАК ТерриторияВыполненияРаботВОрганизации,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК Период,
		|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК ПодразделениеУчетаЗатрат,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка) КАК СтатьяФинансирования,
		|	ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка) КАК СпособОтраженияЗарплатыВБухучете,
		|	ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка) КАК ОтношениеКЕНВД,
		|	0 КАК ДоляРаспределения,
		|	ЛОЖЬ КАК ПодразделениеУчетаЗатратЗаполнено,
		|	ЛОЖЬ КАК СтатьяФинансированияЗаполнена,
		|	ЛОЖЬ КАК СпособОтраженияЗаполнен,
		|	ЛОЖЬ КАК ОтношениеКЕНВДЗаполнено
		|ПОМЕСТИТЬ ВТБухучетТерриторийСРаспределением";
		Запрос.Выполнить();
		
	Иначе
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Таблица.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
		|	Таблица.Период КАК Период
		|ПОМЕСТИТЬ ВТОтборТерриторий
		|ИЗ
		|	ВТИсходнаяТаблица КАК Таблица
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МаксимальныеПериоды.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
		|	МаксимальныеПериоды.Период КАК Период,
		|	БухучетЗарплаты.СтатьяФинансирования КАК СтатьяФинансирования,
		|	БухучетЗарплаты.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	БухучетЗарплаты.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
		|	ВЫБОР
		|		КОГДА БухучетЗарплаты.ДоляРаспределения = 0
		|			ТОГДА 1
		|		ИНАЧЕ БухучетЗарплаты.ДоляРаспределения
		|	КОНЕЦ КАК ДоляРаспределения,
		|	БухучетЗарплаты.ИдентификаторСтроки КАК ИдентификаторСтроки
		|ПОМЕСТИТЬ ВТБухучетТерриторийВесь
		|ИЗ
		|	(ВЫБРАТЬ
		|		Таблица.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
		|		Таблица.Период КАК Период,
		|		МАКСИМУМ(БухучетЗарплаты.Период) КАК ПериодРегистра
		|	ИЗ
		|		ВТОтборТерриторий КАК Таблица
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БухучетЗарплатыТерриторийВыполненияРабот КАК БухучетЗарплаты
		|			ПО Таблица.ТерриторияВыполненияРаботВОрганизации = БухучетЗарплаты.ТерриторияВыполненияРабот
		|				И Таблица.Период >= БухучетЗарплаты.Период
		|	
		|	СГРУППИРОВАТЬ ПО
		|		Таблица.ТерриторияВыполненияРаботВОрганизации,
		|		Таблица.Период) КАК МаксимальныеПериоды
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БухучетЗарплатыТерриторийВыполненияРабот КАК БухучетЗарплаты
		|		ПО МаксимальныеПериоды.ТерриторияВыполненияРаботВОрганизации = БухучетЗарплаты.ТерриторияВыполненияРабот
		|			И МаксимальныеПериоды.ПериодРегистра = БухучетЗарплаты.Период
		|ГДЕ
		|	НЕ(БухучетЗарплаты.СтатьяФинансирования = ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
		|				И БухучетЗарплаты.СпособОтраженияЗарплатыВБухучете = ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
		|				И БухучетЗарплаты.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Бухучет.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
		|	Бухучет.Период КАК Период,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Бухучет.ИдентификаторСтроки) КАК КоличествоСтрок
		|ПОМЕСТИТЬ ВТБухучетТерриторийСтрокиСРаспределением
		|ИЗ
		|	ВТБухучетТерриторийВесь КАК Бухучет
		|
		|СГРУППИРОВАТЬ ПО
		|	Бухучет.ТерриторияВыполненияРаботВОрганизации,
		|	Бухучет.Период
		|
		|ИМЕЮЩИЕ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Бухучет.ИдентификаторСтроки) > 1
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	БухучетТерриторий.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
		|	БухучетТерриторий.Период КАК Период,
		|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК ПодразделениеУчетаЗатрат,
		|	БухучетТерриторий.СтатьяФинансирования КАК СтатьяФинансирования,
		|	БухучетТерриторий.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	БухучетТерриторий.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
		|	ЛОЖЬ КАК ПодразделениеУчетаЗатратЗаполнено,
		|	БухучетТерриторий.СтатьяФинансирования <> ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка) КАК СтатьяФинансированияЗаполнена,
		|	БухучетТерриторий.СпособОтраженияЗарплатыВБухучете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка) КАК СпособОтраженияЗаполнен,
		|	БухучетТерриторий.ОтношениеКЕНВД <> ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка) КАК ОтношениеКЕНВДЗаполнено
		|ПОМЕСТИТЬ ВТБухучетТерриторий
		|ИЗ
		|	ВТБухучетТерриторийВесь КАК БухучетТерриторий
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБухучетТерриторийСтрокиСРаспределением КАК СтрокиСРаспределением
		|		ПО БухучетТерриторий.ТерриторияВыполненияРаботВОрганизации = СтрокиСРаспределением.ТерриторияВыполненияРаботВОрганизации
		|			И БухучетТерриторий.Период = СтрокиСРаспределением.Период
		|ГДЕ
		|	СтрокиСРаспределением.КоличествоСтрок ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	БухучетТерриторий.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
		|	БухучетТерриторий.Период КАК Период,
		|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК ПодразделениеУчетаЗатрат,
		|	БухучетТерриторий.СтатьяФинансирования КАК СтатьяФинансирования,
		|	БухучетТерриторий.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	БухучетТерриторий.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
		|	БухучетТерриторий.ДоляРаспределения КАК ДоляРаспределения,
		|	ЛОЖЬ КАК ПодразделениеУчетаЗатратЗаполнено,
		|	БухучетТерриторий.СтатьяФинансирования <> ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка) КАК СтатьяФинансированияЗаполнена,
		|	БухучетТерриторий.СпособОтраженияЗарплатыВБухучете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка) КАК СпособОтраженияЗаполнен,
		|	БухучетТерриторий.ОтношениеКЕНВД <> ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка) КАК ОтношениеКЕНВДЗаполнено
		|ПОМЕСТИТЬ ВТБухучетТерриторийСРаспределением
		|ИЗ
		|	ВТБухучетТерриторийВесь КАК БухучетТерриторий
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТБухучетТерриторийСтрокиСРаспределением КАК СтрокиСРаспределением
		|		ПО БухучетТерриторий.ТерриторияВыполненияРаботВОрганизации = СтрокиСРаспределением.ТерриторияВыполненияРаботВОрганизации
		|			И БухучетТерриторий.Период = СтрокиСРаспределением.Период";
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТИсходнаяТаблица", ИмяИсходнойВТ);
		Запрос.Выполнить();
		
		УдалитьВТ.Добавить("ВТОтборТерриторий");
		УдалитьВТ.Добавить("ВТБухучетТерриторийВесь");
		УдалитьВТ.Добавить("ВТБухучетТерриторийСтрокиСРаспределением");
		
	КонецЕсли;
	
	// Бухучет Сотрудников.
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Сотрудник КАК Сотрудник,
	|	Таблица.Период КАК Период
	|ПОМЕСТИТЬ ВТОтборСотрудников
	|ИЗ
	|	ВТИсходнаяТаблица КАК Таблица";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТИсходнаяТаблица", ИмяИсходнойВТ);
	Запрос.Выполнить();
	УдалитьВТ.Добавить("ВТОтборСотрудников");
	
	СоздатьВТБухучетЗарплатыСотрудников(МенеджерВТ, "ВТОтборСотрудников");
	УдалитьВТ.Добавить("ВТБухучетЗарплатыСотрудников");
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	БухучетЗарплаты.Сотрудник КАК Сотрудник,
	|	БухучетЗарплаты.Период КАК Период,
	|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК ПодразделениеУчетаЗатрат,
	|	БухучетЗарплаты.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетЗарплаты.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетЗарплаты.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	ЛОЖЬ КАК ПодразделениеУчетаЗатратЗаполнено,
	|	БухучетЗарплаты.СтатьяФинансирования <> ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка) КАК СтатьяФинансированияЗаполнена,
	|	БухучетЗарплаты.СпособОтраженияЗарплатыВБухучете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка) КАК СпособОтраженияЗаполнен,
	|	БухучетЗарплаты.ОтношениеКЕНВД <> ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка) КАК ОтношениеКЕНВДЗаполнено
	|ПОМЕСТИТЬ ВТБухучетСотрудников
	|ИЗ
	|	ВТБухучетЗарплатыСотрудников КАК БухучетЗарплаты
	|ГДЕ
	|	НЕ(БухучетЗарплаты.СтатьяФинансирования = ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|				И БухучетЗарплаты.СпособОтраженияЗарплатыВБухучете = ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
	|				И БухучетЗарплаты.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка))";
	Запрос.Выполнить();
	
	ЗарплатаКадры.УничтожитьВТ(МенеджерВТ, УдалитьВТ);
	
КонецПроцедуры

// Объединяет настройки бухучета в одну таблицу, используются таблицы, полученные в ПрочитатьНастройкиБухучетаСоздатьВременныеТаблицы.
// Выходная таблица содержит поля:
// 		* Организация
// 		* Подразделение
// 		* ТерриторияВыполненияРаботВОрганизации
// 		* Сотрудник
// 		* Период
// 		* ИдентификаторСтроки
// 		* СтатьяФинансирования
// 		* СпособОтраженияЗарплатыВБухучете
// 		* ОтношениеКЕНВД
//
// Параметры:
// 	МенеджерВТ - МенеджерВременныхТаблиц - содержит таблицу с именем ИмяИсходнойВТ.
// 	ИмяИсходнойВТ - Строка - имя временной таблицы.
// 	ИмяИтоговойТаблицы- Строка - имя временной таблицы, в которую будет помещен результат.
//
Процедура СоздатьВТНастройкиБухучетаБезУчетаНастроекРаспределения(МенеджерВТ, ИмяИсходнойВТ, ИмяИтоговойТаблицы)
	
	УдалитьВТ = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Таблица.Организация КАК Организация,
	|	Таблица.Подразделение КАК Подразделение,
	|	Таблица.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	Таблица.Сотрудник КАК Сотрудник,
	|	Таблица.Период КАК Период,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(БухучетСотрудников.ПодразделениеУчетаЗатратЗаполнено, ЛОЖЬ)
	|			ТОГДА БухучетСотрудников.ПодразделениеУчетаЗатрат
	|		КОГДА ЕСТЬNULL(БухучетТерриторий.ПодразделениеУчетаЗатратЗаполнено, ЛОЖЬ)
	|			ТОГДА БухучетТерриторий.ПодразделениеУчетаЗатрат
	|		КОГДА ЕСТЬNULL(БухучетПодразделений.ПодразделениеУчетаЗатратЗаполнено, ЛОЖЬ)
	|			ТОГДА БухучетПодразделений.ПодразделениеУчетаЗатрат
	|		КОГДА ЕСТЬNULL(БухучетОрганизаций.ПодразделениеУчетаЗатратЗаполнено, ЛОЖЬ)
	|			ТОГДА БухучетОрганизаций.ПодразделениеУчетаЗатрат
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
	|	КОНЕЦ КАК ПодразделениеУчетаЗатрат,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(БухучетСотрудников.СтатьяФинансированияЗаполнена, ЛОЖЬ)
	|			ТОГДА БухучетСотрудников.СтатьяФинансирования
	|		КОГДА ЕСТЬNULL(БухучетТерриторий.СтатьяФинансированияЗаполнена, ЛОЖЬ)
	|			ТОГДА БухучетТерриторий.СтатьяФинансирования
	|		КОГДА ЕСТЬNULL(БухучетПодразделений.СтатьяФинансированияЗаполнена, ЛОЖЬ)
	|			ТОГДА БухучетПодразделений.СтатьяФинансирования
	|		КОГДА ЕСТЬNULL(БухучетОрганизаций.СтатьяФинансированияЗаполнена, ЛОЖЬ)
	|			ТОГДА БухучетОрганизаций.СтатьяФинансирования
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|	КОНЕЦ КАК СтатьяФинансирования,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(БухучетСотрудников.СпособОтраженияЗаполнен, ЛОЖЬ)
	|			ТОГДА БухучетСотрудников.СпособОтраженияЗарплатыВБухучете
	|		КОГДА ЕСТЬNULL(БухучетТерриторий.СпособОтраженияЗаполнен, ЛОЖЬ)
	|			ТОГДА БухучетТерриторий.СпособОтраженияЗарплатыВБухучете
	|		КОГДА ЕСТЬNULL(БухучетПодразделений.СпособОтраженияЗаполнен, ЛОЖЬ)
	|			ТОГДА БухучетПодразделений.СпособОтраженияЗарплатыВБухучете
	|		КОГДА ЕСТЬNULL(БухучетОрганизаций.СпособОтраженияЗаполнен, ЛОЖЬ)
	|			ТОГДА БухучетОрганизаций.СпособОтраженияЗарплатыВБухучете
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
	|	КОНЕЦ КАК СпособОтраженияЗарплатыВБухучете,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(БухучетСотрудников.ОтношениеКЕНВДЗаполнено, ЛОЖЬ)
	|			ТОГДА БухучетСотрудников.ОтношениеКЕНВД
	|		КОГДА ЕСТЬNULL(БухучетТерриторий.ОтношениеКЕНВДЗаполнено, ЛОЖЬ)
	|			ТОГДА БухучетТерриторий.ОтношениеКЕНВД
	|		КОГДА ЕСТЬNULL(БухучетПодразделений.ОтношениеКЕНВДЗаполнено, ЛОЖЬ)
	|			ТОГДА БухучетПодразделений.ОтношениеКЕНВД
	|		КОГДА ЕСТЬNULL(БухучетОрганизаций.ОтношениеКЕНВДЗаполнено, ЛОЖЬ)
	|			ТОГДА БухучетОрганизаций.ОтношениеКЕНВД
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)
	|	КОНЕЦ КАК ОтношениеКЕНВД
	|ПОМЕСТИТЬ ВТНастройкиБухучетаБезРаспределения
	|ИЗ
	|	ВТИсходнаяТаблица КАК Таблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБухучетСотрудников КАК БухучетСотрудников
	|		ПО Таблица.Сотрудник = БухучетСотрудников.Сотрудник
	|			И Таблица.Период = БухучетСотрудников.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБухучетПодразделений КАК БухучетПодразделений
	|		ПО Таблица.Подразделение = БухучетПодразделений.Подразделение
	|			И Таблица.Период = БухучетПодразделений.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБухучетТерриторий КАК БухучетТерриторий
	|		ПО Таблица.ТерриторияВыполненияРаботВОрганизации = БухучетТерриторий.ТерриторияВыполненияРаботВОрганизации
	|			И Таблица.Период = БухучетТерриторий.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБухучетОрганизаций КАК БухучетОрганизаций
	|		ПО Таблица.Организация = БухучетОрганизаций.Организация
	|			И Таблица.Период = БухучетОрганизаций.Период";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТИсходнаяТаблица", ИмяИсходнойВТ);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНастройкиБухучетаБезРаспределения", ИмяИтоговойТаблицы);
	
	Запрос.Выполнить();
	
КонецПроцедуры

// Получает настройки распределения основного заработка сотрудников.
// Результат помещается во временную таблицу ВТБухучетСотрудниковЕжемесячноеРаспределение
//
// Параметры:
// 	МенеджерВТ - МенеджерВременныхТаблиц - содержит временную таблицу с
// 					именем, указанном в параметре ИмяИсходнойВТ, таблица содержит поля:
// 					* Организация
// 					* Сотрудник
// 					* Период
// 	ИмяИсходнойВТ - Строка - имя временной таблицы.
//
Процедура СоздатьВТБухучетСотрудниковЕжемесячноеРаспределение(МенеджерВТ, ИмяИсходнойВТ)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Организация КАК Организация,
	|	Таблица.Сотрудник КАК Сотрудник,
	|	Таблица.Период КАК Период
	|ПОМЕСТИТЬ ВТОтборСотрудников
	|ИЗ
	|	ВТИсходнаяТаблица КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.Сотрудник КАК Сотрудник,
	|	Таблица.Период КАК Период,
	|	РаспределениеОсновногоЗаработка.СтатьяФинансирования КАК СтатьяФинансирования,
	|	РаспределениеОсновногоЗаработка.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	ВЫБОР
	|		КОГДА РаспределениеОсновногоЗаработка.ОблагаетсяЕНВД
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ЕНВД)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.НеЕНВД)
	|	КОНЕЦ КАК ОтношениеКЕНВД,
	|	РаспределениеОсновногоЗаработка.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|	РаспределениеОсновногоЗаработка.ДоляРаспределения КАК ДоляРаспределения
	|ПОМЕСТИТЬ ВТБухучетСотрудниковЕжемесячноеРаспределение
	|ИЗ
	|	ВТОтборСотрудников КАК Таблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БухучетРаспределениеОсновногоЗаработка КАК РаспределениеОсновногоЗаработка
	|		ПО Таблица.Сотрудник = РаспределениеОсновногоЗаработка.Сотрудник
	|			И Таблица.Организация = РаспределениеОсновногоЗаработка.Организация
	|			И (Таблица.Период МЕЖДУ РаспределениеОсновногоЗаработка.ПериодРегистрации И КОНЕЦПЕРИОДА(РаспределениеОсновногоЗаработка.ПериодРегистрации, МЕСЯЦ))";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТИсходнаяТаблица", ИмяИсходнойВТ);
	Запрос.Выполнить();
	
	ЗарплатаКадры.УничтожитьВТ(МенеджерВТ, "ВТОтборСотрудников");
	
КонецПроцедуры

// Получает настройки бухучета, которые будут использоваться для распределения.
// Учитываются настройки заданные в виде распределения.
// Результат помещается в таблицы ВТНастройкиБухучетаПоУмолчанию и ВТНастройкиБухучета.
// Таблица ВТНастройкиБухучетаПоУмолчанию содержит настройки, полученные без учета настроек распределения.
// Таблица ВТНастройкиБухучета содержит настройки, полученные с учетом настроек распределения.
// Таблицы содержат поля:
// 			* Организация
// 			* Сотрудник
// 			* Период
// 			* Подразделение
// 			* ТерриторияВыполненияРаботВОрганизации
// 			* СтатьяФинансирования
// 			* СпособОтраженияЗарплатыВБухучете
// 			* ОтношениеКЕНВД
// 			* ДоляРаспределения
// 			* СтрокаСРаспределением
//
// Параметры:
// 	МенеджерВТ - МенеджерВременныхТаблиц - содержит таблицу с именем ВТИсходнаяТаблицаДляНастроекБухучета.
// 			Таблица должна содержать поля:
// 			* Организация
// 			* Сотрудник
// 			* Период
// 			* ИсходныйПериод
// 			* Подразделение
// 			* ТерриторияВыполненияРаботВОрганизации
// 			* ИдентификаторСтроки
// 	Организация - СправочникСсылка.Организация.
// 	Период - Дата.
// 	УчитыватьЕжемесячноеРаспределение - Булево - определяет необходимость учета настроек
// 							распределения основного заработка сотрудников.
//
Процедура СоздатьВТНастройкиБухучета(МенеджерВТ, Организация, Период, УчитыватьЕжемесячноеРаспределение)
	
	УдалитьВТ = Новый Массив;
	
	// Имя временной таблицы с исходами данными для получения настроек,
	// таблица создается в СоздатьВТИсходнаяТаблицаДляНастроекБухучета.
	// МенеджерВТ содержит эту таблицу
	ИмяВТИсходнаяТаблица = "ВТИсходнаяТаблицаДляНастроекБухучета";
	
	// Массив будет содержать имена таблиц с прочитанными настройками бухучета
	// заданными для организации, подразделений, территорий, сотрудников.
	// Используется для уничтожения этих таблиц по окончании работы метода.
	ИменаТаблицСНастройкамиБухучета = Новый Массив;
	ПрочитатьНастройкиБухучетаСоздатьВременныеТаблицы(МенеджерВТ, ИмяВТИсходнаяТаблица, ИменаТаблицСНастройкамиБухучета);
	
	// Создание таблицы с настройками бухучета без учета настроек распределения.
	ИмяВТНастройкиБухучетаБезРаспределения = "ВТНастройкиБухучетаБезРаспределения";
	СоздатьВТНастройкиБухучетаБезУчетаНастроекРаспределения(МенеджерВТ, ИмяВТИсходнаяТаблица, ИмяВТНастройкиБухучетаБезРаспределения);
	УдалитьВТ.Добавить(ИмяВТНастройкиБухучетаБезРаспределения);
	
	ПрименяетсяЕНВД = ОтражениеЗарплатыВБухучете.ПлательщикЕНВД(Организация, Период);
	ИспользоватьСтатьиФинансирования   = ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный");
	ИспользоватьОбособленныеТерритории = ПолучитьФункциональнуюОпцию("ИспользоватьОбособленныеТерритории", Новый Структура("Организация", Организация));
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.УстановитьПараметр("ПрименяетсяЕНВД", ПрименяетсяЕНВД);
	Запрос.УстановитьПараметр("ИспользоватьСтатьиФинансирования", ИспользоватьСтатьиФинансирования);
	
	// ВТНастройкиБухучетаПоУмолчанию
	// Создание таблицы с настройками "по умолчанию".
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Организация КАК Организация,
	|	Таблица.Подразделение КАК Подразделение,
	|	Таблица.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	Таблица.Сотрудник КАК Сотрудник,
	|	Таблица.ИсходныйПериод КАК Период,
	|	НастройкиБухучета.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|	ВЫБОР
	|		КОГДА &ИспользоватьСтатьиФинансирования
	|			ТОГДА НастройкиБухучета.СтатьяФинансирования
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|	КОНЕЦ КАК СтатьяФинансирования,
	|	НастройкиБухучета.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	ВЫБОР
	|		КОГДА &ПрименяетсяЕНВД
	|			ТОГДА НастройкиБухучета.ОтношениеКЕНВД
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)
	|	КОНЕЦ КАК ОтношениеКЕНВД,
	|	1 КАК ДоляРаспределения,
	|	ЛОЖЬ КАК СтрокаСРаспределением
	|ПОМЕСТИТЬ ВТНастройкиБухучетаПоУмолчанию
	|ИЗ
	|	ВТИсходнаяТаблицаДляНастроекБухучета КАК Таблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиБухучетаБезРаспределения КАК НастройкиБухучета
	|		ПО Таблица.ИдентификаторСтроки = НастройкиБухучета.ИдентификаторСтроки";
	Запрос.Выполнить();
	
	ЗаданоРаспределениеВПодразделениях = ЗарплатаКадры.ВТСодержитСтроки(МенеджерВТ, "ВТБухучетПодразделенийСРаспределением");
	ЗаданоРаспределениеВТерриториях    = ЗарплатаКадры.ВТСодержитСтроки(МенеджерВТ, "ВТБухучетТерриторийСРаспределением");
	
	// Определим необходимость учета настроек распределения основного заработка.
	ЗаданоЕжемесячноеРаспределениеСотрудников = Ложь;
	Если УчитыватьЕжемесячноеРаспределение Тогда
		СоздатьВТБухучетСотрудниковЕжемесячноеРаспределение(МенеджерВТ, ИмяВТИсходнаяТаблица);
		УдалитьВТ.Добавить("ВТБухучетСотрудниковЕжемесячноеРаспределение");
		ЗаданоЕжемесячноеРаспределениеСотрудников = ЗарплатаКадры.ВТСодержитСтроки(МенеджерВТ, "ВТБухучетСотрудниковЕжемесячноеРаспределение");
	КонецЕсли;
	
	// Если в настройках бухучета не используется распределение, 
	// в качестве таблицы с "полными" настройками используем таблицу с распределением "по умолчанию".
	Если НЕ ЗаданоЕжемесячноеРаспределениеСотрудников И Не ЗаданоРаспределениеВПодразделениях И Не ЗаданоРаспределениеВТерриториях Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Таблица.Организация КАК Организация,
		|	Таблица.Подразделение КАК Подразделение,
		|	Таблица.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
		|	Таблица.Сотрудник КАК Сотрудник,
		|	Таблица.Период КАК Период,
		|	Таблица.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
		|	Таблица.СтатьяФинансирования КАК СтатьяФинансирования,
		|	Таблица.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	Таблица.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
		|	Таблица.ДоляРаспределения КАК ДоляРаспределения,
		|	Таблица.СтрокаСРаспределением КАК СтрокаСРаспределением
		|ПОМЕСТИТЬ ВТНастройкиБухучета
		|ИЗ
		|	ВТНастройкиБухучетаПоУмолчанию КАК Таблица";
		Запрос.Выполнить();
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(УдалитьВТ, ИменаТаблицСНастройкамиБухучета);
		ЗарплатаКадры.УничтожитьВТ(МенеджерВТ, УдалитьВТ);
		
		Возврат;
		
	КонецЕсли;
	
	НастройкиБухучета = Новый ТаблицаЗначений;
	НастройкиБухучета.Колонки.Добавить("ИдентификаторСтроки", 					Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(7, 0)));
	НастройкиБухучета.Колонки.Добавить("Организация", 							Новый ОписаниеТипов("СправочникСсылка.Организации"));
	НастройкиБухучета.Колонки.Добавить("Подразделение", 						Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	НастройкиБухучета.Колонки.Добавить("ТерриторияВыполненияРаботВОрганизации", Новый ОписаниеТипов(Метаданные.ОпределяемыеТипы.ТерриторияВыполненияРаботВОрганизации.Тип));
	НастройкиБухучета.Колонки.Добавить("Сотрудник", 							Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	НастройкиБухучета.Колонки.Добавить("Период", 								Новый ОписаниеТипов("Дата",Новый КвалификаторыДаты(ЧастиДаты.Дата)));
	НастройкиБухучета.Колонки.Добавить("ИсходныйПериод", 						Новый ОписаниеТипов("Дата",Новый КвалификаторыДаты(ЧастиДаты.Дата)));
	НастройкиБухучета.Колонки.Добавить("ПодразделениеУчетаЗатрат", 				Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	НастройкиБухучета.Колонки.Добавить("СтатьяФинансирования",					Новый ОписаниеТипов("СправочникСсылка.СтатьиФинансированияЗарплата"));
	НастройкиБухучета.Колонки.Добавить("СпособОтраженияЗарплатыВБухучете", 		Новый ОписаниеТипов("СправочникСсылка.СпособыОтраженияЗарплатыВБухучете"));
	НастройкиБухучета.Колонки.Добавить("ОтношениеКЕНВД",						Новый ОписаниеТипов("ПеречислениеСсылка.ОтношениеКЕНВДЗатратНаЗарплату"));
	НастройкиБухучета.Колонки.Добавить("ДоляРаспределения", 					Новый ОписаниеТипов(Метаданные.ОпределяемыеТипы.ЗначениеДоляРаспределенияБухучетЗарплаты.Тип));
	НастройкиБухучета.Колонки.Добавить("СтрокаСРаспределением",					Новый ОписаниеТипов("Булево"));
	НастройкиБухучета.Колонки.Добавить("ПодразделениеУчетаЗатратЗаполнено",		Новый ОписаниеТипов("Булево"));
	НастройкиБухучета.Колонки.Добавить("СтатьяФинансированияЗаполнена",			Новый ОписаниеТипов("Булево"));
	НастройкиБухучета.Колонки.Добавить("СпособОтраженияЗаполнен",				Новый ОписаниеТипов("Булево"));
	НастройкиБухучета.Колонки.Добавить("ОтношениеКЕНВДЗаполнено",				Новый ОписаниеТипов("Булево"));

	ОбработанныеСтроки = Новый Соответствие;
	
	СвойстваИсключения = "";
	Если Не ПрименяетсяЕНВД Тогда
		СвойстваИсключения = "ОтношениеКЕНВД";
	КонецЕсли;
	Если Не ИспользоватьСтатьиФинансирования Тогда
		СвойстваИсключения = СвойстваИсключения + ?(ПустаяСтрока(СвойстваИсключения),"СтатьяФинансирования",",СтатьяФинансирования");
	КонецЕсли;
	
	// Получаем настройки с учетом приоритетов.
	
	// 1. Настройка распределения основного заработка сотрудников.
	// 2. Бухучет задан в настройках сотрудника.
	// 3. Бухучет задан в настройках территории.
	// 4. Бухучет задан в настройках подразделения.
	
	РаспределениеБезАналитики = Новый Массив;
	Если ЗаданоЕжемесячноеРаспределениеСотрудников Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИсходнаяТаблица.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	ИсходнаяТаблица.Организация КАК Организация,
		|	ИсходнаяТаблица.Подразделение КАК Подразделение,
		|	ИсходнаяТаблица.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
		|	ИсходнаяТаблица.Сотрудник КАК Сотрудник,
		|	ИсходнаяТаблица.Период КАК Период,
		|	ИсходнаяТаблица.ИсходныйПериод КАК ИсходныйПериод,
		|	СтрокиБухучета.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
		|	СтрокиБухучета.СтатьяФинансирования КАК СтатьяФинансирования,
		|	СтрокиБухучета.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	СтрокиБухучета.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
		|	СтрокиБухучета.ПодразделениеУчетаЗатрат <> ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК ПодразделениеУчетаЗатратЗаполнено,
		|	НЕ &ИспользоватьСтатьиФинансирования
		|		ИЛИ СтрокиБухучета.СтатьяФинансирования <> ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка) КАК СтатьяФинансированияЗаполнена,
		|	СтрокиБухучета.СпособОтраженияЗарплатыВБухучете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка) КАК СпособОтраженияЗаполнен,
		|	ИСТИНА КАК ОтношениеКЕНВДЗаполнено,
		|	СтрокиБухучета.ДоляРаспределения КАК ДоляРаспределения,
		|	ИСТИНА КАК СтрокаСРаспределением
		|ИЗ
		|	ВТИсходнаяТаблицаДляНастроекБухучета КАК ИсходнаяТаблица
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТБухучетСотрудниковЕжемесячноеРаспределение КАК СтрокиБухучета
		|		ПО ИсходнаяТаблица.Сотрудник = СтрокиБухучета.Сотрудник
		|			И ИсходнаяТаблица.Период = СтрокиБухучета.Период";
		РезультатЗапроса = Запрос.Выполнить();
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			НоваяСтрока = НастройкиБухучета.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка,,СвойстваИсключения);
			
			ОбработанныеСтроки.Вставить(Выборка.ИдентификаторСтроки, Истина);
			
			Если Не Выборка.СпособОтраженияЗаполнен 
				Или Не Выборка.СтатьяФинансированияЗаполнена
				Или Не Выборка.ОтношениеКЕНВДЗаполнено Тогда
				РаспределениеБезАналитики.Добавить(НоваяСтрока);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	// УсловиеЗаданБухучет
	// Определим условия для отбора строк с настройками в зависимости от настроек программы.
	УсловиеЗаданБухучет = "СтрокиБухучета.СпособОтраженияЗаполнен";
	Если ИспользоватьСтатьиФинансирования Тогда
		УсловиеЗаданБухучет = УсловиеЗаданБухучет + " ИЛИ СтрокиБухучета.СтатьяФинансированияЗаполнена";
	КонецЕсли;
	Если ПрименяетсяЕНВД Тогда
		УсловиеЗаданБухучет = УсловиеЗаданБухучет + " ИЛИ СтрокиБухучета.ОтношениеКЕНВДЗаполнено";
	КонецЕсли;
	
	// Настройки сотрудников.
	СотрудникиБезАналитики = Новый Массив;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИсходнаяТаблица.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ИсходнаяТаблица.Организация КАК Организация,
	|	ИсходнаяТаблица.Подразделение КАК Подразделение,
	|	ИсходнаяТаблица.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	ИсходнаяТаблица.Сотрудник КАК Сотрудник,
	|	ИсходнаяТаблица.Период КАК Период,
	|	ИсходнаяТаблица.ИсходныйПериод КАК ИсходныйПериод,
	|	СтрокиБухучета.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|	СтрокиБухучета.СтатьяФинансирования КАК СтатьяФинансирования,
	|	СтрокиБухучета.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	СтрокиБухучета.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	СтрокиБухучета.ПодразделениеУчетаЗатратЗаполнено КАК ПодразделениеУчетаЗатратЗаполнено,
	|	НЕ &ИспользоватьСтатьиФинансирования
	|		ИЛИ СтрокиБухучета.СтатьяФинансированияЗаполнена КАК СтатьяФинансированияЗаполнена,
	|	СтрокиБухучета.СпособОтраженияЗаполнен КАК СпособОтраженияЗаполнен,
	|	НЕ &ПрименяетсяЕНВД
	|		ИЛИ СтрокиБухучета.ОтношениеКЕНВДЗаполнено КАК ОтношениеКЕНВДЗаполнено,
	|	1 КАК ДоляРаспределения,
	|	ЛОЖЬ КАК СтрокаСРаспределением
	|ИЗ
	|	ВТИсходнаяТаблицаДляНастроекБухучета КАК ИсходнаяТаблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТБухучетСотрудников КАК СтрокиБухучета
	|		ПО ИсходнаяТаблица.Сотрудник = СтрокиБухучета.Сотрудник
	|			И ИсходнаяТаблица.Период = СтрокиБухучета.Период
	|			И (&УсловиеЗаданБухучет)";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеЗаданБухучет", УсловиеЗаданБухучет);
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если ОбработанныеСтроки[Выборка.ИдентификаторСтроки] = Истина Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = НастройкиБухучета.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка,,СвойстваИсключения);
		
		ОбработанныеСтроки.Вставить(Выборка.ИдентификаторСтроки, Истина);
		
		Если Не Выборка.СпособОтраженияЗаполнен 
			Или Не Выборка.СтатьяФинансированияЗаполнена
			Или Не Выборка.ОтношениеКЕНВДЗаполнено Тогда
			СотрудникиБезАналитики.Добавить(НоваяСтрока);
		КонецЕсли;
		
	КонецЦикла;
	
	ТерриторииБезАналитики = Новый Массив;
	Если ИспользоватьОбособленныеТерритории Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИсходнаяТаблица.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	ИсходнаяТаблица.Организация КАК Организация,
		|	ИсходнаяТаблица.Подразделение КАК Подразделение,
		|	ИсходнаяТаблица.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
		|	ИсходнаяТаблица.Сотрудник КАК Сотрудник,
		|	ИсходнаяТаблица.Период КАК Период,
		|	ИсходнаяТаблица.ИсходныйПериод КАК ИсходныйПериод,
		|	СтрокиБухучета.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
		|	СтрокиБухучета.СтатьяФинансирования КАК СтатьяФинансирования,
		|	СтрокиБухучета.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	СтрокиБухучета.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
		|	СтрокиБухучета.ПодразделениеУчетаЗатратЗаполнено КАК ПодразделениеУчетаЗатратЗаполнено,
		|	НЕ &ИспользоватьСтатьиФинансирования
		|		ИЛИ СтрокиБухучета.СтатьяФинансированияЗаполнена КАК СтатьяФинансированияЗаполнена,
		|	СтрокиБухучета.СпособОтраженияЗаполнен КАК СпособОтраженияЗаполнен,
		|	НЕ &ПрименяетсяЕНВД
		|		ИЛИ СтрокиБухучета.ОтношениеКЕНВДЗаполнено КАК ОтношениеКЕНВДЗаполнено,
		|	1 КАК ДоляРаспределения,
		|	ЛОЖЬ КАК СтрокаСРаспределением
		|ИЗ
		|	ВТИсходнаяТаблицаДляНастроекБухучета КАК ИсходнаяТаблица
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТБухучетТерриторий КАК СтрокиБухучета
		|		ПО ИсходнаяТаблица.ТерриторияВыполненияРаботВОрганизации = СтрокиБухучета.ТерриторияВыполненияРаботВОрганизации
		|			И ИсходнаяТаблица.Период = СтрокиБухучета.Период
		|			И (&УсловиеЗаданБухучет)";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеЗаданБухучет", УсловиеЗаданБухучет);
		РезультатЗапроса = Запрос.Выполнить();
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Если ОбработанныеСтроки[Выборка.ИдентификаторСтроки] = Истина Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = НастройкиБухучета.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка,,СвойстваИсключения);
			
			ОбработанныеСтроки.Вставить(Выборка.ИдентификаторСтроки, Истина);
			
			Если Не Выборка.СпособОтраженияЗаполнен 
				Или Не Выборка.СтатьяФинансированияЗаполнена
				Или Не Выборка.ОтношениеКЕНВДЗаполнено Тогда
				ТерриторииБезАналитики.Добавить(НоваяСтрока);
			КонецЕсли;
			
		КонецЦикла;
		
		Если ЗаданоРаспределениеВТерриториях Тогда
			
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ИсходнаяТаблица.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	ИсходнаяТаблица.Организация КАК Организация,
			|	ИсходнаяТаблица.Подразделение КАК Подразделение,
			|	ИсходнаяТаблица.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
			|	ИсходнаяТаблица.Сотрудник КАК Сотрудник,
			|	ИсходнаяТаблица.Период КАК Период,
			|	ИсходнаяТаблица.ИсходныйПериод КАК ИсходныйПериод,
			|	СтрокиБухучета.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
			|	СтрокиБухучета.СтатьяФинансирования КАК СтатьяФинансирования,
			|	СтрокиБухучета.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
			|	СтрокиБухучета.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
			|	СтрокиБухучета.ПодразделениеУчетаЗатратЗаполнено КАК ПодразделениеУчетаЗатратЗаполнено,
			|	НЕ &ИспользоватьСтатьиФинансирования
			|		ИЛИ СтрокиБухучета.СтатьяФинансированияЗаполнена КАК СтатьяФинансированияЗаполнена,
			|	СтрокиБухучета.СпособОтраженияЗаполнен КАК СпособОтраженияЗаполнен,
			|	НЕ &ПрименяетсяЕНВД
			|		ИЛИ СтрокиБухучета.ОтношениеКЕНВДЗаполнено КАК ОтношениеКЕНВДЗаполнено,
			|	СтрокиБухучета.ДоляРаспределения КАК ДоляРаспределения,
			|	ИСТИНА КАК СтрокаСРаспределением
			|ИЗ
			|	ВТИсходнаяТаблицаДляНастроекБухучета КАК ИсходнаяТаблица
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТБухучетТерриторийСРаспределением КАК СтрокиБухучета
			|		ПО ИсходнаяТаблица.ТерриторияВыполненияРаботВОрганизации = СтрокиБухучета.ТерриторияВыполненияРаботВОрганизации
			|			И ИсходнаяТаблица.Период = СтрокиБухучета.Период
			|
			|УПОРЯДОЧИТЬ ПО
			|	ИдентификаторСтроки";
			
			РезультатЗапроса = Запрос.Выполнить();
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.СледующийПоЗначениюПоля("ИдентификаторСтроки") Цикл
				
				Если ОбработанныеСтроки[Выборка.ИдентификаторСтроки] = Истина Тогда
					Продолжить;
				КонецЕсли;
				
				ОбработанныеСтроки.Вставить(Выборка.ИдентификаторСтроки, Истина);
				
				Пока Выборка.Следующий() Цикл
					
					НоваяСтрока = НастройкиБухучета.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка,,СвойстваИсключения);
					
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ПодразделенияБезАналитики = Новый Массив;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИсходнаяТаблица.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ИсходнаяТаблица.Организация КАК Организация,
	|	ИсходнаяТаблица.Подразделение КАК Подразделение,
	|	ИсходнаяТаблица.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	ИсходнаяТаблица.Сотрудник КАК Сотрудник,
	|	ИсходнаяТаблица.Период КАК Период,
	|	ИсходнаяТаблица.ИсходныйПериод КАК ИсходныйПериод,
	|	СтрокиБухучета.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|	СтрокиБухучета.СтатьяФинансирования КАК СтатьяФинансирования,
	|	СтрокиБухучета.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	СтрокиБухучета.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	СтрокиБухучета.ПодразделениеУчетаЗатратЗаполнено КАК ПодразделениеУчетаЗатратЗаполнено,
	|	НЕ &ИспользоватьСтатьиФинансирования
	|		ИЛИ СтрокиБухучета.СтатьяФинансированияЗаполнена КАК СтатьяФинансированияЗаполнена,
	|	СтрокиБухучета.СпособОтраженияЗаполнен КАК СпособОтраженияЗаполнен,
	|	НЕ &ПрименяетсяЕНВД
	|		ИЛИ СтрокиБухучета.ОтношениеКЕНВДЗаполнено КАК ОтношениеКЕНВДЗаполнено,
	|	1 КАК ДоляРаспределения,
	|	ЛОЖЬ КАК СтрокаСРаспределением
	|ИЗ
	|	ВТИсходнаяТаблицаДляНастроекБухучета КАК ИсходнаяТаблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТБухучетПодразделений КАК СтрокиБухучета
	|		ПО ИсходнаяТаблица.Подразделение = СтрокиБухучета.Подразделение
	|			И ИсходнаяТаблица.Период = СтрокиБухучета.Период
	|			И (&УсловиеЗаданБухучет)";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеЗаданБухучет", УсловиеЗаданБухучет);
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если ОбработанныеСтроки[Выборка.ИдентификаторСтроки] = Истина Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = НастройкиБухучета.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка,,СвойстваИсключения);
		
		ОбработанныеСтроки.Вставить(Выборка.ИдентификаторСтроки, Истина);
		
		Если Не Выборка.СпособОтраженияЗаполнен 
			Или Не Выборка.СтатьяФинансированияЗаполнена
			Или Не Выборка.ОтношениеКЕНВДЗаполнено Тогда
			ПодразделенияБезАналитики.Добавить(НоваяСтрока);
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЗаданоРаспределениеВПодразделениях Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИсходнаяТаблица.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	ИсходнаяТаблица.Организация КАК Организация,
		|	ИсходнаяТаблица.Подразделение КАК Подразделение,
		|	ИсходнаяТаблица.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
		|	ИсходнаяТаблица.Сотрудник КАК Сотрудник,
		|	ИсходнаяТаблица.Период КАК Период,
		|	ИсходнаяТаблица.ИсходныйПериод КАК ИсходныйПериод,
		|	СтрокиБухучета.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
		|	СтрокиБухучета.СтатьяФинансирования КАК СтатьяФинансирования,
		|	СтрокиБухучета.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	СтрокиБухучета.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
		|	СтрокиБухучета.ПодразделениеУчетаЗатратЗаполнено КАК ПодразделениеУчетаЗатратЗаполнено,
		|	НЕ &ИспользоватьСтатьиФинансирования
		|		ИЛИ СтрокиБухучета.СтатьяФинансированияЗаполнена КАК СтатьяФинансированияЗаполнена,
		|	СтрокиБухучета.СпособОтраженияЗаполнен КАК СпособОтраженияЗаполнен,
		|	НЕ &ПрименяетсяЕНВД
		|		ИЛИ СтрокиБухучета.ОтношениеКЕНВДЗаполнено КАК ОтношениеКЕНВДЗаполнено,
		|	СтрокиБухучета.ДоляРаспределения КАК ДоляРаспределения,
		|	ИСТИНА КАК СтрокаСРаспределением
		|ИЗ
		|	ВТИсходнаяТаблицаДляНастроекБухучета КАК ИсходнаяТаблица
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТБухучетПодразделенийСРаспределением КАК СтрокиБухучета
		|		ПО ИсходнаяТаблица.Подразделение = СтрокиБухучета.Подразделение
		|			И ИсходнаяТаблица.Период = СтрокиБухучета.Период
		|
		|УПОРЯДОЧИТЬ ПО
		|	ИдентификаторСтроки";
		РезультатЗапроса = Запрос.Выполнить();
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.СледующийПоЗначениюПоля("ИдентификаторСтроки") Цикл
			
			Если ОбработанныеСтроки[Выборка.ИдентификаторСтроки] = Истина Тогда
				Продолжить;
			КонецЕсли;
			
			ОбработанныеСтроки.Вставить(Выборка.ИдентификаторСтроки, Истина);
			
			Пока Выборка.Следующий() Цикл
				
				НоваяСтрока = НастройкиБухучета.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка,,СвойстваИсключения);
				
			КонецЦикла;
			
		КонецЦикла;
	
	КонецЕсли;
	
	ДополнитьАналитику = РаспределениеБезАналитики.Количество() > 0
			Или СотрудникиБезАналитики.Количество() > 0
			Или ТерриторииБезАналитики.Количество() > 0
			Или ПодразделенияБезАналитики.Количество() > 0;
	
	Если ДополнитьАналитику Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиБухучета.Сотрудник КАК Сотрудник,
		|	НастройкиБухучета.Период КАК Период,
		|	НастройкиБухучета.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
		|	НастройкиБухучета.СтатьяФинансирования КАК СтатьяФинансирования,
		|	НастройкиБухучета.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	НастройкиБухучета.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
		|	НастройкиБухучета.ПодразделениеУчетаЗатратЗаполнено КАК ПодразделениеУчетаЗатратЗаполнено,
		|	НастройкиБухучета.СтатьяФинансированияЗаполнена КАК СтатьяФинансированияЗаполнена,
		|	НастройкиБухучета.СпособОтраженияЗаполнен КАК СпособОтраженияЗаполнен,
		|	НастройкиБухучета.ОтношениеКЕНВДЗаполнено КАК ОтношениеКЕНВДЗаполнено
		|ИЗ
		|	ВТБухучетСотрудников КАК НастройкиБухучета
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период,
		|	Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НастройкиБухучета.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
		|	НастройкиБухучета.Период КАК Период,
		|	НастройкиБухучета.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
		|	НастройкиБухучета.СтатьяФинансирования КАК СтатьяФинансирования,
		|	НастройкиБухучета.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	НастройкиБухучета.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
		|	НастройкиБухучета.ПодразделениеУчетаЗатратЗаполнено КАК ПодразделениеУчетаЗатратЗаполнено,
		|	НастройкиБухучета.СтатьяФинансированияЗаполнена КАК СтатьяФинансированияЗаполнена,
		|	НастройкиБухучета.СпособОтраженияЗаполнен КАК СпособОтраженияЗаполнен,
		|	НастройкиБухучета.ОтношениеКЕНВДЗаполнено КАК ОтношениеКЕНВДЗаполнено
		|ИЗ
		|	ВТБухучетТерриторий КАК НастройкиБухучета
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период,
		|	ТерриторияВыполненияРаботВОрганизации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НастройкиБухучета.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
		|	НастройкиБухучета.Период КАК Период,
		|	НастройкиБухучета.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
		|	НастройкиБухучета.СтатьяФинансирования КАК СтатьяФинансирования,
		|	НастройкиБухучета.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	НастройкиБухучета.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
		|	НастройкиБухучета.ПодразделениеУчетаЗатратЗаполнено КАК ПодразделениеУчетаЗатратЗаполнено,
		|	НастройкиБухучета.СтатьяФинансированияЗаполнена КАК СтатьяФинансированияЗаполнена,
		|	НастройкиБухучета.СпособОтраженияЗаполнен КАК СпособОтраженияЗаполнен,
		|	НастройкиБухучета.ОтношениеКЕНВДЗаполнено КАК ОтношениеКЕНВДЗаполнено,
		|	НастройкиБухучета.ДоляРаспределения КАК ДоляРаспределения
		|ИЗ
		|	ВТБухучетТерриторийСРаспределением КАК НастройкиБухучета
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период,
		|	ТерриторияВыполненияРаботВОрганизации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НастройкиБухучета.Подразделение КАК Подразделение,
		|	НастройкиБухучета.Период КАК Период,
		|	НастройкиБухучета.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
		|	НастройкиБухучета.СтатьяФинансирования КАК СтатьяФинансирования,
		|	НастройкиБухучета.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	НастройкиБухучета.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
		|	НастройкиБухучета.ПодразделениеУчетаЗатратЗаполнено КАК ПодразделениеУчетаЗатратЗаполнено,
		|	НастройкиБухучета.СтатьяФинансированияЗаполнена КАК СтатьяФинансированияЗаполнена,
		|	НастройкиБухучета.СпособОтраженияЗаполнен КАК СпособОтраженияЗаполнен,
		|	НастройкиБухучета.ОтношениеКЕНВДЗаполнено КАК ОтношениеКЕНВДЗаполнено
		|ИЗ
		|	ВТБухучетПодразделений КАК НастройкиБухучета
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период,
		|	Подразделение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НастройкиБухучета.Подразделение КАК Подразделение,
		|	НастройкиБухучета.Период КАК Период,
		|	НастройкиБухучета.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
		|	НастройкиБухучета.СтатьяФинансирования КАК СтатьяФинансирования,
		|	НастройкиБухучета.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	НастройкиБухучета.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
		|	НастройкиБухучета.ПодразделениеУчетаЗатратЗаполнено КАК ПодразделениеУчетаЗатратЗаполнено,
		|	НастройкиБухучета.СтатьяФинансированияЗаполнена КАК СтатьяФинансированияЗаполнена,
		|	НастройкиБухучета.СпособОтраженияЗаполнен КАК СпособОтраженияЗаполнен,
		|	НастройкиБухучета.ОтношениеКЕНВДЗаполнено КАК ОтношениеКЕНВДЗаполнено,
		|	НастройкиБухучета.ДоляРаспределения КАК ДоляРаспределения
		|ИЗ
		|	ВТБухучетПодразделенийСРаспределением КАК НастройкиБухучета
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период,
		|	Подразделение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НастройкиБухучета.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	НастройкиБухучета.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
		|	НастройкиБухучета.СтатьяФинансирования КАК СтатьяФинансирования,
		|	НастройкиБухучета.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	НастройкиБухучета.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
		|	ИСТИНА КАК ПодразделениеУчетаЗатратЗаполнено,
		|	ИСТИНА КАК СтатьяФинансированияЗаполнена,
		|	ИСТИНА КАК СпособОтраженияЗаполнен,
		|	ИСТИНА КАК ОтношениеКЕНВДЗаполнено
		|ИЗ
		|	ВТНастройкиБухучетаБезРаспределения КАК НастройкиБухучета";
		РезультатЗапроса = Запрос.ВыполнитьПакет();
		
		ПараметрыДополнения = Новый Структура("
		|БухучетСотрудников,
		|БухучетТерриторий,
		|БухучетТерриторийСРаспределением,
		|БухучетПодразделений,
		|БухучетПодразделенийСРаспределением,
		|БухучетПоУмолчанию,
		|ОтборПоСотрудникам,
		|ОтборПоТерритории,
		|ОтборПоПодразделению,
		|ОтборПоУмолчанию,
		|УчитыватьСотрудников,
		|УчитыватьТерритории,
		|УчитыватьПодразделения");
		
		ПараметрыДополнения.БухучетСотрудников 					= РезультатЗапроса[0].Выгрузить();
		ПараметрыДополнения.БухучетТерриторий 					= РезультатЗапроса[1].Выгрузить();
		ПараметрыДополнения.БухучетТерриторийСРаспределением 	= РезультатЗапроса[2].Выгрузить();
		ПараметрыДополнения.БухучетПодразделений 				= РезультатЗапроса[3].Выгрузить();
		ПараметрыДополнения.БухучетПодразделенийСРаспределением = РезультатЗапроса[4].Выгрузить();
		ПараметрыДополнения.БухучетПоУмолчанию 					= РезультатЗапроса[5].Выгрузить();
		
		ПараметрыДополнения.БухучетСотрудников.Индексы.Добавить("Период,Сотрудник");
		ПараметрыДополнения.ОтборПоСотрудникам = Новый Структура("Период,Сотрудник");
		
		ПараметрыДополнения.БухучетТерриторий.Индексы.Добавить("Период,ТерриторияВыполненияРаботВОрганизации");
		ПараметрыДополнения.БухучетТерриторийСРаспределением.Индексы.Добавить("Период,ТерриторияВыполненияРаботВОрганизации");
		ПараметрыДополнения.ОтборПоТерритории 		= Новый Структура("Период,ТерриторияВыполненияРаботВОрганизации");
		
		ПараметрыДополнения.БухучетПодразделений.Индексы.Добавить("Период,Подразделение");
		ПараметрыДополнения.БухучетПодразделенийСРаспределением.Индексы.Добавить("Период,Подразделение");
		ПараметрыДополнения.ОтборПоПодразделению 	= Новый Структура("Период,Подразделение");
		
		ПараметрыДополнения.БухучетПоУмолчанию.Индексы.Добавить("ИдентификаторСтроки");
		ПараметрыДополнения.ОтборПоУмолчанию = Новый Структура("ИдентификаторСтроки");
		
		ПараметрыДополнения.УчитыватьТерритории    = ИспользоватьОбособленныеТерритории;
		ПараметрыДополнения.УчитыватьПодразделения = Истина;
		ПараметрыДополнения.УчитыватьСотрудников   = Истина;
		
		Для каждого СтрокаТЗ Из РаспределениеБезАналитики Цикл
			ЗаполнитьПустыеНастройкиБухучета(НастройкиБухучета, СтрокаТЗ, ПараметрыДополнения);
		КонецЦикла;
		
		ПараметрыДополнения.УчитыватьСотрудников = Ложь;
		Для каждого СтрокаТЗ Из СотрудникиБезАналитики Цикл
			ЗаполнитьПустыеНастройкиБухучета(НастройкиБухучета, СтрокаТЗ, ПараметрыДополнения);
		КонецЦикла;
		
		ПараметрыДополнения.УчитыватьТерритории = Ложь;
		Для каждого СтрокаТЗ Из ТерриторииБезАналитики Цикл
			ЗаполнитьПустыеНастройкиБухучета(НастройкиБухучета, СтрокаТЗ, ПараметрыДополнения);
		КонецЦикла;
		
		ПараметрыДополнения.УчитыватьПодразделения = Ложь;
		Для каждого СтрокаТЗ Из ПодразделенияБезАналитики Цикл
			ЗаполнитьПустыеНастройкиБухучета(НастройкиБухучета, СтрокаТЗ, ПараметрыДополнения);
		КонецЦикла;
		
	КонецЕсли;
	
	// Бухучет по умолчанию для оставшихся строк.
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИсходнаяТаблица.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ИсходнаяТаблица.Организация КАК Организация,
	|	ИсходнаяТаблица.Подразделение КАК Подразделение,
	|	ИсходнаяТаблица.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	ИсходнаяТаблица.Сотрудник КАК Сотрудник,
	|	ИсходнаяТаблица.Период КАК Период,
	|	ИсходнаяТаблица.ИсходныйПериод КАК ИсходныйПериод,
	|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК ПодразделениеУчетаЗатрат,
	|	СтрокиБухучета.СтатьяФинансирования КАК СтатьяФинансирования,
	|	СтрокиБухучета.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	СтрокиБухучета.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	1 КАК ДоляРаспределения,
	|	ЛОЖЬ КАК СтрокаСРаспределением
	|ИЗ
	|	ВТИсходнаяТаблицаДляНастроекБухучета КАК ИсходнаяТаблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНастройкиБухучетаБезРаспределения КАК СтрокиБухучета
	|		ПО ИсходнаяТаблица.ИдентификаторСтроки = СтрокиБухучета.ИдентификаторСтроки";
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если ОбработанныеСтроки[Выборка.ИдентификаторСтроки] = Истина Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = НастройкиБухучета.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка,,СвойстваИсключения);
		
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(УдалитьВТ, ИменаТаблицСНастройкамиБухучета);
	ЗарплатаКадры.УничтожитьВТ(МенеджерВТ, УдалитьВТ);
	
	НастройкиБухучета.Колонки.Удалить("Период");
	НастройкиБухучета.Колонки.ИсходныйПериод.Имя = "Период";
	
	ЗарплатаКадры.СоздатьВТПоТаблицеЗначений(МенеджерВТ, НастройкиБухучета, "ВТНастройкиБухучета");
	
КонецПроцедуры

Процедура ЗаполнитьПустыеНастройкиБухучета(НастройкиБухучета, СтрокаТЗ, Параметры)
	
	Если Параметры.УчитыватьСотрудников Тогда
		
		// Подбор свойства в настройках территорий.
		ЗаполнитьЗначенияСвойств(Параметры.ОтборПоСотрудникам, СтрокаТЗ);
		
		НайденныеСтроки = Параметры.БухучетСотрудников.НайтиСтроки(Параметры.ОтборПоСотрудникам);
		ЗаполнитьАналитикуПоСтрокеНастроек(НайденныеСтроки, СтрокаТЗ);
		
		Если СтрокаТЗ.СтатьяФинансированияЗаполнена И СтрокаТЗ.СпособОтраженияЗаполнен И СтрокаТЗ.ОтношениеКЕНВДЗаполнено Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Параметры.УчитыватьТерритории Тогда
		
		// Подбор свойства в настройках территорий.
		ЗаполнитьЗначенияСвойств(Параметры.ОтборПоТерритории, СтрокаТЗ);
		
		НайденныеСтроки = Параметры.БухучетТерриторий.НайтиСтроки(Параметры.ОтборПоТерритории);
		ЗаполнитьАналитикуПоСтрокеНастроек(НайденныеСтроки, СтрокаТЗ);
		
		Если СтрокаТЗ.СтатьяФинансированияЗаполнена И СтрокаТЗ.СпособОтраженияЗаполнен И СтрокаТЗ.ОтношениеКЕНВДЗаполнено Тогда
			Возврат;
		КонецЕсли;
		
		НайденныеСтроки = Параметры.БухучетТерриторийСРаспределением.НайтиСтроки(Параметры.ОтборПоТерритории);
		Если НайденныеСтроки.Количество() > 0 Тогда
			// В настройках распределения заполнены все свойства.
			ЗаполнитьАналитикуПоСтрокамНастроек(НастройкиБухучета, НайденныеСтроки, СтрокаТЗ);
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Параметры.УчитыватьПодразделения Тогда
		
		// Подбор свойства в настройках Подразделений.
		ЗаполнитьЗначенияСвойств(Параметры.ОтборПоПодразделению, СтрокаТЗ);
		
		НайденныеСтроки = Параметры.БухучетПодразделений.НайтиСтроки(Параметры.ОтборПоПодразделению);
		ЗаполнитьАналитикуПоСтрокеНастроек(НайденныеСтроки, СтрокаТЗ);
		
		Если СтрокаТЗ.СтатьяФинансированияЗаполнена И СтрокаТЗ.СпособОтраженияЗаполнен И СтрокаТЗ.ОтношениеКЕНВДЗаполнено Тогда
			Возврат;
		КонецЕсли;
		
		НайденныеСтроки = Параметры.БухучетПодразделенийСРаспределением.НайтиСтроки(Параметры.ОтборПоПодразделению);
		Если НайденныеСтроки.Количество() > 0 Тогда
			// В настройках распределения заполнены все свойства.
			ЗаполнитьАналитикуПоСтрокамНастроек(НастройкиБухучета, НайденныеСтроки, СтрокаТЗ);
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	// Подбор свойства по таблицы по умолчанию.
	ЗаполнитьЗначенияСвойств(Параметры.ОтборПоУмолчанию, СтрокаТЗ);
	
	НайденныеСтроки = Параметры.БухучетПоУмолчанию.НайтиСтроки(Параметры.ОтборПоУмолчанию);
	Если НайденныеСтроки.Количество() > 0 Тогда
		ЗаполнитьАналитикуПоСтрокеНастроек(НайденныеСтроки, СтрокаТЗ);
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьАналитикуПоСтрокеНастроек(НайденныеСтроки, СтрокаТЗ)

	Если НайденныеСтроки.Количество() > 0 Тогда
		
		Если Не СтрокаТЗ.ПодразделениеУчетаЗатратЗаполнено И НайденныеСтроки[0].ПодразделениеУчетаЗатратЗаполнено Тогда
			СтрокаТЗ.ПодразделениеУчетаЗатрат = НайденныеСтроки[0].ПодразделениеУчетаЗатрат;
			СтрокаТЗ.ПодразделениеУчетаЗатратЗаполнено = Истина;
		КонецЕсли;
		
		Если Не СтрокаТЗ.СтатьяФинансированияЗаполнена И НайденныеСтроки[0].СтатьяФинансированияЗаполнена Тогда
			СтрокаТЗ.СтатьяФинансирования = НайденныеСтроки[0].СтатьяФинансирования;
			СтрокаТЗ.СтатьяФинансированияЗаполнена = Истина;
		КонецЕсли;
		
		Если Не СтрокаТЗ.СпособОтраженияЗаполнен И НайденныеСтроки[0].СпособОтраженияЗаполнен Тогда
			СтрокаТЗ.СпособОтраженияЗарплатыВБухучете = НайденныеСтроки[0].СпособОтраженияЗарплатыВБухучете;
			СтрокаТЗ.СпособОтраженияЗаполнен = Истина;
		КонецЕсли;
		
		Если Не СтрокаТЗ.ОтношениеКЕНВДЗаполнено И НайденныеСтроки[0].ОтношениеКЕНВДЗаполнено Тогда
			СтрокаТЗ.ОтношениеКЕНВД = НайденныеСтроки[0].ОтношениеКЕНВД;
			СтрокаТЗ.ОтношениеКЕНВДЗаполнено = Истина;
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьАналитикуПоСтрокамНастроек(НастройкиБухучета, НайденныеСтроки, СтрокаТЗ)

	// Исходная строка уже является строкой с распределением.
	РаспределятьДолю = СтрокаТЗ.СтрокаСРаспределением;
	Если РаспределятьДолю Тогда
		ДоляРаспределенияИсходная = СтрокаТЗ.ДоляРаспределения;
		СтрокиДляРаспределения = Новый Массив;
		СтрокиДляРаспределения.Добавить(СтрокаТЗ);
		Коэффициенты = Новый Массив;
		Коэффициенты.Добавить(НайденныеСтроки[0].ДоляРаспределения);
	КонецЕсли;
	
	// сначала добавим дополнительные строки
	Для Индекс = 1 По НайденныеСтроки.ВГраница()  Цикл
		
		ДополнительнаяСтрока = НастройкиБухучета.Добавить();
		ЗаполнитьЗначенияСвойств(ДополнительнаяСтрока, СтрокаТЗ);
		
		Если Не ДополнительнаяСтрока.ПодразделениеУчетаЗатратЗаполнено Тогда
			ДополнительнаяСтрока.ПодразделениеУчетаЗатрат = НайденныеСтроки[Индекс].ПодразделениеУчетаЗатрат;
			ДополнительнаяСтрока.ПодразделениеУчетаЗатратЗаполнено = Истина;
		КонецЕсли;
		
		Если Не ДополнительнаяСтрока.СтатьяФинансированияЗаполнена  Тогда
			ДополнительнаяСтрока.СтатьяФинансирования = НайденныеСтроки[Индекс].СтатьяФинансирования;
			ДополнительнаяСтрока.СтатьяФинансированияЗаполнена = Истина;
		КонецЕсли;
		
		Если Не ДополнительнаяСтрока.СпособОтраженияЗаполнен Тогда
			ДополнительнаяСтрока.СпособОтраженияЗарплатыВБухучете = НайденныеСтроки[Индекс].СпособОтраженияЗарплатыВБухучете;
			ДополнительнаяСтрока.СпособОтраженияЗаполнен = Истина;
		КонецЕсли;
		
		Если Не ДополнительнаяСтрока.ОтношениеКЕНВДЗаполнено Тогда
			ДополнительнаяСтрока.ОтношениеКЕНВД = НайденныеСтроки[Индекс].ОтношениеКЕНВД;
			ДополнительнаяСтрока.ОтношениеКЕНВДЗаполнено = Истина;
		КонецЕсли;
		
		ДополнительнаяСтрока.ДоляРаспределения = НайденныеСтроки[Индекс].ДоляРаспределения;
		ДополнительнаяСтрока.СтрокаСРаспределением = Истина;
		
		Если РаспределятьДолю Тогда
			СтрокиДляРаспределения.Добавить(ДополнительнаяСтрока);
			Коэффициенты.Добавить(ДополнительнаяСтрока.ДоляРаспределения);
		КонецЕсли;
		
	КонецЦикла;
	
	// заполним аналитику в переданной строке СтрокаТЗ
	Если Не СтрокаТЗ.ПодразделениеУчетаЗатратЗаполнено Тогда
			СтрокаТЗ.ПодразделениеУчетаЗатрат = НайденныеСтроки[0].ПодразделениеУчетаЗатрат;
			СтрокаТЗ.ПодразделениеУчетаЗатратЗаполнено = Истина;
		КонецЕсли;
	Если Не СтрокаТЗ.СтатьяФинансированияЗаполнена Тогда
		СтрокаТЗ.СтатьяФинансирования = НайденныеСтроки[0].СтатьяФинансирования;
		СтрокаТЗ.СтатьяФинансированияЗаполнена = Истина;
	КонецЕсли;
	Если Не СтрокаТЗ.СпособОтраженияЗаполнен Тогда
		СтрокаТЗ.СпособОтраженияЗарплатыВБухучете = НайденныеСтроки[0].СпособОтраженияЗарплатыВБухучете;
		СтрокаТЗ.СпособОтраженияЗаполнен = Истина;
	КонецЕсли;
	Если Не СтрокаТЗ.ОтношениеКЕНВДЗаполнено Тогда
		СтрокаТЗ.ОтношениеКЕНВД = НайденныеСтроки[0].ОтношениеКЕНВД;
		СтрокаТЗ.ОтношениеКЕНВДЗаполнено = Истина;
	КонецЕсли;
	СтрокаТЗ.ДоляРаспределения 		= НайденныеСтроки[0].ДоляРаспределения;
	СтрокаТЗ.СтрокаСРаспределением 	= Истина;
	
	Если РаспределятьДолю Тогда
		
		Результаты = ЗарплатаКадрыКлиентСервер.РаспределитьПропорциональноКоэффициентам(ДоляРаспределенияИсходная, Коэффициенты);
		
		Индекс = 0;
		Для Каждого СтрокаРаспределения Из СтрокиДляРаспределения Цикл
			СтрокаРаспределения.ДоляРаспределения = Результаты[Индекс];
			Индекс = Индекс + 1;
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

Функция ПараметрыПолученияНастроекБухучета()

	ПараметрыПолученияНастроек = Новый Структура("
	|ИмяПоляСотрудник,
	|ИмяПоляПериод,
	|Организация,
	|Подразделение,
	|ТерриторияВыполненияРаботВОрганизации");
	
	Возврат ПараметрыПолученияНастроек;

КонецФункции

// Создает временную таблицу ВТНастройкиБухучетаСотрудников.
//
//	Параметры
//		Организация - СправочникСсылка.Организации
//		Период - Дата - определяет месяц, для которого получаются данные, любая дата месяца
//		МенеджерВТ - МенеджерВременныхТаблиц - содержит таблицу с именем, указанным в параметре ИмяВТ с полями
//			* Организация
//			* Подразделение
//			* ТерриторияВыполненияРаботВОрганизации
//			* Сотрудник
//			* Период
//		ИмяВТ - Строка - имя временной таблицы.
//
//	Временная таблица ВТНастройкиБухучетаСотрудников содержит поля
// 			* Организация
// 			* Сотрудник
// 			* Период
// 			* Подразделение
// 			* ТерриторияВыполненияРаботВОрганизации
// 			* СтатьяФинансирования
// 			* СпособОтраженияЗарплатыВБухучете
// 			* ОблагаетсяЕНВД
// 			* ДоляРаспределения
//
Процедура СоздатьВТНастройкиБухучетаСотрудников(Организация, Период, МенеджерВТ, ИмяВТ)
	
	УдалитьВТ = Новый Массив;
	
	// Подготовка временной таблицы ВТИсходнаяТаблицаДляНастроекБухучета для получения настроек бухучета.
	СоздатьВТИсходнаяТаблицаДляНастроекБухучета(МенеджерВТ, ИмяВТ, Период);
	УдалитьВТ.Добавить("ВТИсходнаяТаблицаДляНастроекБухучета");
	
	// ВТНастройкиБухучета и ВТНастройкиБухучетаПоУмолчанию
	// Получение настроек бухучета с учетом зарегистрированного распределения основного заработка сотрудников.
	СоздатьВТНастройкиБухучета(МенеджерВТ, Организация, Период, Истина);
	УдалитьВТ.Добавить("ВТНастройкиБухучета");
	УдалитьВТ.Добавить("ВТНастройкиБухучетаПоУмолчанию");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.УстановитьПараметр("СтатьяОплатаТруда", СтатьяОплатаТруда());
	Запрос.УстановитьПараметр("ПрименяетсяЕНВД", ОтражениеЗарплатыВБухучете.ПлательщикЕНВД(Организация, Период));
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Организация КАК Организация,
	|	Таблица.Сотрудник КАК Сотрудник,
	|	Таблица.Период КАК Период,
	|	Таблица.Подразделение КАК Подразделение,
	|	Таблица.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	Таблица.СтатьяФинансирования КАК СтатьяФинансирования,
	|	Таблица.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	&СтатьяОплатаТруда КАК СтатьяРасходов,
	|	ВЫБОР
	|		КОГДА &ПрименяетсяЕНВД
	|				И Таблица.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ЕНВД)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОблагаетсяЕНВД,
	|	Таблица.ДоляРаспределения КАК ДоляРаспределения
	|ПОМЕСТИТЬ ВТНастройкиБухучетаСотрудников
	|ИЗ
	|	ВТНастройкиБухучета КАК Таблица";
	Запрос.Выполнить();
	
	ЗарплатаКадры.УничтожитьВТ(МенеджерВТ, УдалитьВТ);
	
КонецПроцедуры

#КонецОбласти

#Область БухучетНачислений

Функция НастройкиСтатейФинансирования(ПериодРегистрации)
	
	Описание = "Действует,ИспользуетсяВБазеСреднего,УстаревшаяСтатьяИсключаетсяИзБазы,КатегорииНачислений";
	Настройки = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Статьи.Ссылка КАК Ссылка,
	|	НЕ(Статьи.ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1)
	|			И Статьи.ДатаОкончания < &ПериодРегистрации) КАК Действует,
	|	ВЫБОР
	|		КОГДА Статьи.ИспользованиеВБазеСреднегоЗаработка = ЗНАЧЕНИЕ(Перечисление.ИспользованиеСтатьиФинансированияВБазеСреднегоЗаработка.Используется)
	|				ИЛИ Статьи.ИспользованиеВБазеСреднегоЗаработка = ЗНАЧЕНИЕ(Перечисление.ИспользованиеСтатьиФинансированияВБазеСреднегоЗаработка.ПустаяСсылка)
	|				ИЛИ СтатьиЗамены.СтатьяФинансированияДляЗамены ЕСТЬ NULL
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ИспользуетсяВБазеСреднего,
	|	ВЫБОР
	|		КОГДА Статьи.ПравилоЗамены = ЗНАЧЕНИЕ(Перечисление.ПравилаЗаменыУстаревшейАналитикиБухучета.ИсключатьИзБазы)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК УстаревшаяСтатьяИсключаетсяИзБазы,
	|	Статьи.КатегорииНачислений.(
	|		Ссылка КАК Ссылка,
	|		НомерСтроки КАК НомерСтроки,
	|		КатегорияНачисления КАК КатегорияНачисления
	|	) КАК КатегорииНачислений
	|ИЗ
	|	Справочник.СтатьиФинансированияЗарплата КАК Статьи
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатьиФинансированияБазыСреднегоЗаработка.СрезПоследних(&ПериодРегистрации, ) КАК СтатьиЗамены
	|		ПО Статьи.Ссылка = СтатьиЗамены.СтатьяФинансирования";
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		НастройкиСтатьи = Новый Структура(Описание);
		НастройкиСтатьи.Действует 							= Выборка.Действует;
		НастройкиСтатьи.ИспользуетсяВБазеСреднего 			= Выборка.ИспользуетсяВБазеСреднего;
		НастройкиСтатьи.УстаревшаяСтатьяИсключаетсяИзБазы 	= Выборка.УстаревшаяСтатьяИсключаетсяИзБазы;
		НастройкиСтатьи.КатегорииНачислений = ОбщегоНазначенияБЗККлиентСервер.МассивВСоответствие(Выборка.КатегорииНачислений.Выгрузить().ВыгрузитьКолонку("КатегорияНачисления"));
		Настройки.Вставить(Выборка.Ссылка, НастройкиСтатьи);
	КонецЦикла;
	
	Возврат Настройки;

КонецФункции

Функция АктуальныеСтатьиФинансирования(Организация, ПериодРегистрации)
	
	СоответствиеСтатьей = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтатьиФинансированияЗарплата.Ссылка КАК СтатьяФинансирования,
	|	ВЫБОР
	|		КОГДА СтатьиФинансированияЗарплата.ПравилоЗамены = ЗНАЧЕНИЕ(Перечисление.ПравилаЗаменыУстаревшейАналитикиБухучета.ПоНастройкамОрганизации)
	|			ТОГДА ЕСТЬNULL(АктуальнаяСтатья.СтатьяФинансирования, СтатьиФинансированияЗарплата.Ссылка)
	|		КОГДА СтатьиФинансированияЗарплата.ПравилоЗамены = ЗНАЧЕНИЕ(Перечисление.ПравилаЗаменыУстаревшейАналитикиБухучета.ПоНастройкамОбъекта)
	|			ТОГДА ЕСТЬNULL(СтатьиЗамены.НоваяАналитика, СтатьиФинансированияЗарплата.Ссылка)
	|		КОГДА СтатьиФинансированияЗарплата.ПравилоЗамены = ЗНАЧЕНИЕ(Перечисление.ПравилаЗаменыУстаревшейАналитикиБухучета.ИсключатьИзБазы)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|		ИНАЧЕ СтатьиФинансированияЗарплата.Ссылка
	|	КОНЕЦ КАК АктуальнаяСтатьяФинансирования
	|ИЗ
	|	Справочник.СтатьиФинансированияЗарплата КАК СтатьиФинансированияЗарплата
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатьяФинансированияДляЗаменыУстаревшейАналитикиВБухучете.СрезПоследних(&ПериодРегистрации, Организация = &Организация) КАК АктуальнаяСтатья
	|		ПО (СтатьиФинансированияЗарплата.ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1))
	|			И (СтатьиФинансированияЗарплата.ДатаОкончания < &ПериодРегистрации)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПравилаЗаменыУстаревшейАналитикиБухучета.СрезПоследних(&ПериодРегистрации, ) КАК СтатьиЗамены
	|		ПО (СтатьиФинансированияЗарплата.ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1))
	|			И (СтатьиФинансированияЗарплата.ДатаОкончания < &ПериодРегистрации)
	|			И СтатьиФинансированияЗарплата.Ссылка = СтатьиЗамены.СтараяАналитика";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СоответствиеСтатьей.Вставить(Выборка.СтатьяФинансирования, Выборка.АктуальнаяСтатьяФинансирования);
	КонецЦикла;
	
	Возврат СоответствиеСтатьей;

КонецФункции

Функция СтатьиФинансированияЗамены(ПериодРегистрации)

	СтатьиДляЗамены = Новый Соответствие;
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтатьиФинансирования.СтатьяФинансирования КАК СтатьяФинансирования,
	|	СтатьиФинансирования.СтатьяФинансированияДляЗамены КАК СтатьяФинансированияДляЗамены
	|ИЗ
	|	РегистрСведений.СтатьиФинансированияБазыСреднегоЗаработка.СрезПоследних(&ПериодРегистрации, ) КАК СтатьиФинансирования";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СтатьиДляЗамены.Вставить(Выборка.СтатьяФинансирования, Выборка.СтатьяФинансированияДляЗамены);
	КонецЦикла;
	
	Возврат СтатьиДляЗамены;

КонецФункции

Процедура СтатьиРасходовНачисленийБюджет(МенеджерВТ, Организация, ПериодРегистрации, СтатьиРасходовПоВидамНачислений)
	
	ВидыОперацийРасходыФСС = Новый Массив;
	ВидыОперацийРасходыФСС.Добавить(Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФСС); 
	ВидыОперацийРасходыФСС.Добавить(Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФССНС);
	ВидыОперацийРасходыФСС.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПособиеНаПогребение);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.УстановитьПараметр("Статья213", СтатьяРасходов213());
	Запрос.УстановитьПараметр("Статья211", СтатьяРасходов211());
	Запрос.УстановитьПараметр("РасходыФСС", ВидыОперацийРасходыФСС);
	
	УдалитьВТ = Новый Массив;
	
	Если Не СтатьиРасходовПоПриказу209нПрименяются(ПериодРегистрации) Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Начисления.Ссылка КАК Начисление,
		|	ВЫБОР
		|		КОГДА Начисления.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ОплатаБольничногоЛистаЗаСчетРаботодателя)
		|				И Начисления.СтатьяРасходов = ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
		|			ТОГДА &Статья211
		|		КОГДА Начисления.ВидОперацииПоЗарплате В (&РасходыФСС)
		|			ТОГДА &Статья213
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
		|	КОНЕЦ КАК СтатьяРасходов
		|ПОМЕСТИТЬ ВТСтатьиРасходовБюджетПоВидамНачислений
		|ИЗ
		|	ПланВидовРасчета.Начисления КАК Начисления";
		Запрос.Выполнить();
		
	Иначе
		
		ПрямыеВыплатыПособий = ПрямыеВыплатыПособийСоциальногоСтрахования.ПособиеПлатитУчастникПилотногоПроекта(Организация, ПериодРегистрации);
		
		СохраняемыйЗаработок = Новый Массив;
		СохраняемыйЗаработок.Добавить(ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыВыплатБывшимСотрудникам.СохраняемыйЗаработокНаВремяТрудоустройства"));
		СохраняемыйЗаработок.Добавить(ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыВыплатБывшимСотрудникам.СохраняемоеДенежноеСодержаниеНаПериодТрудоустройства"));
		
		Запрос.УстановитьПараметр("СохраняемыйЗаработок", СохраняемыйЗаработок);
		Запрос.УстановитьПараметр("Статья264", СтатьяРасходов264());
		Запрос.УстановитьПараметр("Статья266", СтатьяРасходов266());
		Запрос.УстановитьПараметр("ПрямыеВыплатыПособий", ПрямыеВыплатыПособий);
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВидыВыплатБывшимСотрудникам.Ссылка КАК Начисление,
		|	&Статья264 КАК СтатьяРасходов
		|ПОМЕСТИТЬ ВТСтатьиРасходовБюджетПоВидамНачислений
		|ИЗ
		|	Справочник.ВидыВыплатБывшимСотрудникам КАК ВидыВыплатБывшимСотрудникам
		|ГДЕ
		|	ВидыВыплатБывшимСотрудникам.Ссылка В(&СохраняемыйЗаработок)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Начисления.Ссылка,
		|	ВЫБОР
		|		КОГДА Начисления.КатегорияНачисленияИлиНеоплаченногоВремени В (ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ПособиеПоУходуЗаРебенкомДоТрехЛет), ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ОплатаБольничногоЛистаЗаСчетРаботодателя), ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.КомпенсацияЗаНеотработанныеДниЧасыПриУвольнении), ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.КомпенсацияЗаНеотработанныеДниПриУвольненииГосслужащего), ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ВыходноеПособиеМесячноеДенежноеСодержание), ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ВыходноеПособие))
		|				И Начисления.СтатьяРасходов = ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
		|			ТОГДА &Статья266
		|		КОГДА Начисления.КатегорияНачисленияИлиНеоплаченногоВремени В (ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ОплатаДнейУходаЗаДетьмиИнвалидами), ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ДенежноеСодержаниеОплатаДнейУходаЗаДетьмиИнвалидами))
		|			ТОГДА ВЫБОР
		|					КОГДА НЕ &ПрямыеВыплатыПособий
		|							ИЛИ Начисления.СтатьяРасходов = ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
		|						ТОГДА &Статья213
		|					ИНАЧЕ Начисления.СтатьяРасходов
		|				КОНЕЦ
		|		КОГДА Начисления.ВидОперацииПоЗарплате В (&РасходыФСС)
		|			ТОГДА &Статья213
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
		|	КОНЕЦ
		|ИЗ
		|	ПланВидовРасчета.Начисления КАК Начисления";
		Запрос.Выполнить();
		
	КонецЕсли;
	УдалитьВТ.Добавить("ВТСтатьиРасходовБюджетПоВидамНачислений");
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Начисления.Начисление КАК Начисление,
	|	ВЫБОР
	|		КОГДА НЕ СтатьиПоВидамНачислений.СтатьяРасходов ЕСТЬ NULL
	|			ТОГДА СтатьиПоВидамНачислений.СтатьяРасходов
	|		КОГДА Начисления.СтатьяРасходов <> ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
	|			ТОГДА Начисления.СтатьяРасходов
	|		ИНАЧЕ &Статья211
	|	КОНЕЦ КАК СтатьяРасходов
	|ПОМЕСТИТЬ ВТСтатьиРасходовНачисленийБюджетПоУмолчанию
	|ИЗ
	|	(ВЫБРАТЬ
	|		Начисления.Ссылка КАК Начисление,
	|		Начисления.СтатьяРасходов КАК СтатьяРасходов
	|	ИЗ
	|		ПланВидовРасчета.Начисления КАК Начисления
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВидыВыплатБывшимСотрудникам.Ссылка,
	|		ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
	|	ИЗ
	|		Справочник.ВидыВыплатБывшимСотрудникам КАК ВидыВыплатБывшимСотрудникам
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВидыПрочихДоходовФизическихЛиц.Ссылка,
	|		ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
	|	ИЗ
	|		Справочник.ВидыПрочихДоходовФизическихЛиц КАК ВидыПрочихДоходовФизическихЛиц
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВидыОсобыхНачисленийИУдержаний.Ссылка,
	|		ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
	|	ИЗ
	|		Перечисление.ВидыОсобыхНачисленийИУдержаний КАК ВидыОсобыхНачисленийИУдержаний) КАК Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатьиРасходовБюджетПоВидамНачислений КАК СтатьиПоВидамНачислений
	|		ПО Начисления.Начисление = СтатьиПоВидамНачислений.Начисление
	|			И (СтатьиПоВидамНачислений.СтатьяРасходов <> ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтатьиПоВидамНачислений.Начисление КАК Начисление,
	|	СтатьиПоВидамНачислений.СтатьяРасходов КАК СтатьяРасходов
	|ИЗ
	|	ВТСтатьиРасходовБюджетПоВидамНачислений КАК СтатьиПоВидамНачислений
	|ГДЕ
	|	СтатьиПоВидамНачислений.СтатьяРасходов <> ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)";
	
	СтатьиРасходовПоВидамНачислений = Новый Соответствие;
	
	Выборка = Запрос.Выполнить().Выбрать(); 
	
	Пока Выборка.Следующий() Цикл
		СтатьиРасходовПоВидамНачислений.Вставить(Выборка.Начисление, Выборка.СтатьяРасходов);
	КонецЦикла;
	
	ЗарплатаКадры.УничтожитьВТ(МенеджерВТ, УдалитьВТ);
	
КонецПроцедуры

Функция АктуальныеСпособыОтражения(Организация, ПериодРегистрации)
	
	СоответствиеСпособовОтражения = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СпособыОтражения.Ссылка КАК СпособОтраженияЗарплатыВБухучете,
	|	ВЫБОР
	|		КОГДА СпособыОтражения.ПравилоЗамены = ЗНАЧЕНИЕ(Перечисление.ПравилаЗаменыУстаревшейАналитикиБухучета.ПоНастройкамОрганизации)
	|			ТОГДА ЕСТЬNULL(АктуальныйСпособОтражения.СпособОтраженияЗарплатыВБухучете, СпособыОтражения.Ссылка)
	|		КОГДА СпособыОтражения.ПравилоЗамены = ЗНАЧЕНИЕ(Перечисление.ПравилаЗаменыУстаревшейАналитикиБухучета.ПоНастройкамОбъекта)
	|			ТОГДА ЕСТЬNULL(СпособыЗамены.НоваяАналитика, СпособыОтражения.Ссылка)
	|		КОГДА СпособыОтражения.ПравилоЗамены = ЗНАЧЕНИЕ(Перечисление.ПравилаЗаменыУстаревшейАналитикиБухучета.ИсключатьИзБазы)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка)
	|		ИНАЧЕ СпособыОтражения.Ссылка
	|	КОНЕЦ КАК АктуальныйСпособОтраженияЗарплатыВБухучете
	|ИЗ
	|	Справочник.СпособыОтраженияЗарплатыВБухУчете КАК СпособыОтражения
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СпособОтраженияДляЗаменыУстаревшейАналитикиВБухучете.СрезПоследних(&ПериодРегистрации, Организация = &Организация) КАК АктуальныйСпособОтражения
	|		ПО (СпособыОтражения.ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1))
	|			И (СпособыОтражения.ДатаОкончания < &ПериодРегистрации)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПравилаЗаменыУстаревшейАналитикиБухучета.СрезПоследних(&ПериодРегистрации, ) КАК СпособыЗамены
	|		ПО (СпособыОтражения.ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1))
	|			И (СпособыОтражения.ДатаОкончания < &ПериодРегистрации)
	|			И СпособыОтражения.Ссылка = СпособыЗамены.СтараяАналитика";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СоответствиеСпособовОтражения.Вставить(Выборка.СпособОтраженияЗарплатыВБухучете, Выборка.АктуальныйСпособОтраженияЗарплатыВБухучете);
	КонецЦикла;
	
	Возврат СоответствиеСпособовОтражения;

КонецФункции

// Получает настройки начислений для алгоритмов бухучета.
// Данные помещаются в таблицу ВТНастройкиНачислений.
//
Процедура СоздатьВТНастройкиНачисленийДляБухучета(Запрос)
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Начисления.Ссылка КАК Начисление,
	|	Начисления.ИспользуетСдельныйЗаработок
	|		И Начисления.СтратегияОтраженияВУчете = ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоСдельномуЗаработку) КАК ИспользуетСдельныйЗаработок,
	|	Начисления.ИспользоватьОперативныеПоказателиВЦеломЗаМесяц КАК ИспользоватьОперативныеПоказателиВЦеломЗаМесяц,
	|	Начисления.СтратегияОтраженияВУчете = ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазовымРасчетам) КАК БухучетПоБазовымРасчетам,
	|	Начисления.СтратегияОтраженияВУчете = ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазеСреднегоЗаработка) КАК БухучетПоБазеСреднегоЗаработка,
	|	Начисления.СтратегияОтраженияВУчете = ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоФактическимНачислениям) КАК БухучетПоФактическимНачислениям,
	|	Начисления.СтратегияОтраженияВУчете = ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.КакЗаданоВидуРасчета) КАК БухучетУказанВНачислении,
	|	ВЫБОР
	|		КОГДА &ИспользоватьСтатьиФинансирования
	|			ТОГДА Начисления.СтатьяФинансирования
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|	КОНЕЦ КАК СтатьяФинансирования,
	|	ВЫБОР
	|		КОГДА &РаботаВБюджетномУчреждении
	|			ТОГДА Начисления.СтатьяРасходов
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
	|	КОНЕЦ КАК СтатьяРасходов,
	|	Начисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	ВЫБОР
	|		КОГДА &ЕстьЕНВД
	|			ТОГДА Начисления.ОтношениеКЕНВД
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)
	|	КОНЕЦ КАК ОтношениеКЕНВД,
	|	Начисления.ОчередностьОтраженияВУчете КАК ОчередностьОтраженияВУчете,
	|	ВЫБОР
	|		КОГДА Начисления.ВидОперацииПоЗарплате В (&РасходыФСС)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоРасходыФСС,
	|	Начисления.ВидОперацииПоЗарплате = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюРаботодатель) КАК РасходыПоСтрахованиюРаботодатель,
	|	Начисления.ИспользуетСреднийЗаработокОбщий КАК ИспользуетСреднийЗаработокОбщий,
	|	ВЫБОР
	|		КОГДА Начисления.ВидОтпуска.СпособРасчетаОтпуска В (ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОтпуска.ВКалендарныхДнях), ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОтпуска.ВКалендарныхИлиРабочихДняхВЗависимостиОтТрудовогоДоговора))
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаНачислений.ОплатаОтпускаПоКалендарнымДням)
	|		КОГДА Начисления.ВидОтпуска.СпособРасчетаОтпуска = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОтпуска.ВРабочихДнях)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаНачислений.ОплатаОтпускаПоШестидневке)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаНачислений.ПустаяСсылка)
	|	КОНЕЦ КАК СпособРасчетаПоСреднемуЗаработку,
	|	Начисления.КодДоходаСтраховыеВзносы.ВходитВБазуФСС КАК ВходитВБазуФСС,
	|	Начисления.ИспользуетБухучетПоказателей КАК ИспользуетБухучетПоказателей,
	|	&НазначениеРасчетаСохраняемогоДенежногоСодержания КАК НазначениеРасчетаСохраняемогоДенежногоСодержания,
	|	Начисления.КатегорияНачисленияИлиНеоплаченногоВремени КАК КатегорияНачисления,
	|	Начисления.ОсновнойВидРасчета КАК ОсновнойВидРасчета,
	|	ОсновныеНачисления.КатегорияНачисленияИлиНеоплаченногоВремени КАК КатегорияНачисленияОсновнойВидРасчета
	|ПОМЕСТИТЬ ВТНастройкиНачислений
	|ИЗ
	|	ПланВидовРасчета.Начисления КАК Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления КАК ОсновныеНачисления
	|		ПО Начисления.ОсновнойВидРасчета = ОсновныеНачисления.Ссылка";
	
	ОписаниеПоля = "НЕОПРЕДЕЛЕНО";
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба.РасчетДенежногоСодержания") Тогда
		// Уточнение получения поля НазначениеРасчетаСохраняемогоДенежногоСодержания.
		Модуль = ОбщегоНазначения.ОбщийМодуль("РасчетДенежногоСодержания");
		Модуль.ОписаниеПоляНазначениеРасчетаСохраняемогоДенежногоСодержания(ОписаниеПоля);
	КонецЕсли;
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&НазначениеРасчетаСохраняемогоДенежногоСодержания", ОписаниеПоля);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура СоздатьВТПоказателиНачисленийОпределяющиеБухучет(Запрос)

	Запрос.Текст =
	"ВЫБРАТЬ
	|	НачисленияПоказатели.Ссылка КАК Начисление,
	|	НачисленияПоказатели.Показатель КАК Показатель
	|ПОМЕСТИТЬ ВТПоказателиНачисленийОпределяющиеБухучет
	|ИЗ
	|	ПланВидовРасчета.Начисления.Показатели КАК НачисленияПоказатели
	|ГДЕ
	|	НачисленияПоказатели.ЗадаетБухучет";
	Запрос.Выполнить();

КонецПроцедуры

// Получение бухучета начислений, для которых определяется только доля ЕНВД
// по месячной доле сотрудника, это единовременные пособия ФСС, компенсация за задержку зарплаты.
// Вызывается только если выключен учет по статьям финансирования.
//
Функция СоздатьВТБухучетНачисленийЕНВДПоЕжемесячнойДоле(Запрос, ИмяВТНачисленияИсходная)
	
	ПрименяетсяЕНВД = Запрос.Параметры.ЕстьЕНВД;
	
	Если ПрименяетсяЕНВД Тогда
		
		СтрокиЕНВД = Новый ТаблицаЗначений;
		СтрокиЕНВД.Колонки.Добавить("ИдентификаторСтроки", Новый ОписаниеТипов("Число"));
		СтрокиЕНВД.Колонки.Добавить("ОблагаетсяЕНВД", Новый ОписаниеТипов("Булево"));
		
		ПроцентЕНВД = Запрос.Параметры.ПроцентЕНВД;
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	ЕСТЬNULL(НастройкиБухучета.ОтношениеКЕНВД, ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.НеЕНВД)) КАК ОтношениеКЕНВД
		|ИЗ
		|	ВТНачисленияДляРаспределения КАК Начисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиБухучетаВсехСтрок КАК НастройкиБухучета
		|		ПО Начисления.ИдентификаторСтроки = НастройкиБухучета.ИдентификаторСтроки
		|ГДЕ
		|	Начисления.Начисление В (ЗНАЧЕНИЕ(Перечисление.ВидыОсобыхНачисленийИУдержаний.КомпенсацияЗаЗадержкуЗарплаты), ЗНАЧЕНИЕ(Перечисление.ВидыОсобыхНачисленийИУдержаний.ПособиеПриПостановкеНаУчетВРанниеСрокиБеременности), ЗНАЧЕНИЕ(Перечисление.ВидыОсобыхНачисленийИУдержаний.ПособиеПриРожденииРебенка), ЗНАЧЕНИЕ(Перечисление.ВидыОсобыхНачисленийИУдержаний.ПособиеНаПогребение), ЗНАЧЕНИЕ(Перечисление.ВидыОсобыхНачисленийИУдержаний.ПособиеНаПогребениеСотруднику))
		|
		|УПОРЯДОЧИТЬ ПО
		|	ИдентификаторСтроки";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
		РезультатЗапроса = Запрос.Выполнить();
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.СледующийПоЗначениюПоля("ИдентификаторСтроки") Цикл
			
			НоваяСтрока = СтрокиЕНВД.Добавить();
			НоваяСтрока.ИдентификаторСтроки = Выборка.ИдентификаторСтроки;
			
			ОблагаетсяЕНВД = Неопределено;
			
			Пока Выборка.Следующий() Цикл
				Если ПроцентЕНВД = 100 И Выборка.ОтношениеКЕНВД = Перечисления.ОтношениеКЕНВДЗатратНаЗарплату.ОпределяетсяЕжемесячноПроцентом Тогда
					СтрокаОблагаетсяЕНВД = Истина;
				ИначеЕсли Выборка.ОтношениеКЕНВД = Перечисления.ОтношениеКЕНВДЗатратНаЗарплату.ЕНВД Тогда
					СтрокаОблагаетсяЕНВД = Истина;
				Иначе
					СтрокаОблагаетсяЕНВД = Ложь;
				КонецЕсли;
				ОблагаетсяЕНВД = ?(ОблагаетсяЕНВД = Неопределено, СтрокаОблагаетсяЕНВД, ОблагаетсяЕНВД И СтрокаОблагаетсяЕНВД);
			КонецЦикла;
			
			НоваяСтрока.ОблагаетсяЕНВД = ОблагаетсяЕНВД;
			
		КонецЦикла;
		
		Если СтрокиЕНВД.Количество() = 0 Тогда
			Возврат Ложь;
		КонецЕсли;
		
		Запрос.УстановитьПараметр("СтрокиЕНВД", СтрокиЕНВД);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СтрокиЕНВД.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	СтрокиЕНВД.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД
		|ПОМЕСТИТЬ ВТСтрокиЕНВД
		|ИЗ
		|	&СтрокиЕНВД КАК СтрокиЕНВД
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	Начисления.Сотрудник КАК Сотрудник,
		|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
		|	Начисления.Начисление КАК Начисление,
		|	Начисления.ДатаНачала КАК ДатаНачала,
		|	Начисления.ДатаОкончания КАК ДатаОкончания,
		|	Начисления.Подразделение КАК ПодразделениеУчетаЗатрат,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка) КАК СтатьяФинансирования,
		|	ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка) КАК СпособОтраженияЗарплатыВБухучете,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка) КАК СтатьяРасходов,
		|	СтрокиЕНВД.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	Начисления.Сумма КАК Сумма
		|ПОМЕСТИТЬ ВТБухучетНачисленийЕНВДПоЕжемесячнойДоле
		|ИЗ
		|	ВТНачисленияДляРаспределения КАК Начисления
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСтрокиЕНВД КАК СтрокиЕНВД
		|		ПО Начисления.ИдентификаторСтроки = СтрокиЕНВД.ИдентификаторСтроки";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
		Запрос.Выполнить();
		
		ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, "ВТСтрокиЕНВД");
		
	Иначе
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	Начисления.Сотрудник КАК Сотрудник,
		|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
		|	Начисления.Начисление КАК Начисление,
		|	Начисления.ДатаНачала КАК ДатаНачала,
		|	Начисления.ДатаОкончания КАК ДатаОкончания,
		|	Начисления.Подразделение КАК ПодразделениеУчетаЗатрат,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка) КАК СтатьяФинансирования,
		|	ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка) КАК СпособОтраженияЗарплатыВБухучете,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка) КАК СтатьяРасходов,
		|	ЛОЖЬ КАК ОблагаетсяЕНВД,
		|	Начисления.Сумма КАК Сумма
		|ПОМЕСТИТЬ ВТБухучетНачисленийЕНВДПоЕжемесячнойДоле
		|ИЗ
		|	ВТНачисленияДляРаспределения КАК Начисления
		|ГДЕ
		|	Начисления.Начисление В (ЗНАЧЕНИЕ(Перечисление.ВидыОсобыхНачисленийИУдержаний.КомпенсацияЗаЗадержкуЗарплаты), ЗНАЧЕНИЕ(Перечисление.ВидыОсобыхНачисленийИУдержаний.ПособиеПриПостановкеНаУчетВРанниеСрокиБеременности), ЗНАЧЕНИЕ(Перечисление.ВидыОсобыхНачисленийИУдержаний.ПособиеПриРожденииРебенка), ЗНАЧЕНИЕ(Перечисление.ВидыОсобыхНачисленийИУдержаний.ПособиеНаПогребение), ЗНАЧЕНИЕ(Перечисление.ВидыОсобыхНачисленийИУдержаний.ПособиеНаПогребениеСотруднику))";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
		Запрос.Выполнить();
		
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Бухучет.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ИЗ
	|	ВТБухучетНачисленийЕНВДПоЕжемесячнойДоле КАК Бухучет";
	
	ЕстьТаблица = Не Запрос.Выполнить().Пустой();
	Если ЕстьТаблица Тогда
		// Зарегистрируем обработанные идентификаторы строк.
		ДополнитьВТОбработанныеСтроки(Запрос, "ВТБухучетНачисленийЕНВДПоЕжемесячнойДоле");
	Иначе
		ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, "ВТБухучетНачисленийЕНВДПоЕжемесячнойДоле");
	КонецЕсли;
	
	Возврат ЕстьТаблица;
	
КонецФункции

Функция СоздатьВТБухучетНачисленийСдельногоЗаработка(Запрос, ИмяВТНачисленияИсходная, ПараметрыРаспределенияБюджет)
	
	РаботаВБюджетномУчреждении = Запрос.Параметры.РаботаВБюджетномУчреждении;
	
	ПоказательСдельногоЗаработка = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ПоказателиРасчетаЗарплаты.СдельныйЗаработок");
	Запрос.УстановитьПараметр("ПоказательСдельногоЗаработка", ПоказательСдельногоЗаработка);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Начисления.Начисление КАК Начисление,
	|	Начисления.Подразделение КАК Подразделение,
	|	Начисления.ДатаНачала КАК ДатаНачала,
	|	Начисления.ДатаОкончания КАК ДатаОкончания,
	|	Начисления.Сумма КАК Сумма
	|ИЗ
	|	ВТНачисленияДляРаспределения КАК Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОбработанныеСтроки КАК ОбработанныеСтроки
	|		ПО Начисления.ИдентификаторСтроки = ОбработанныеСтроки.ИдентификаторСтроки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНастройкиНачислений КАК НастройкиНачислений
	|		ПО Начисления.Начисление = НастройкиНачислений.Начисление
	|			И (НастройкиНачислений.ИспользуетСдельныйЗаработок)
	|ГДЕ
	|	ОбработанныеСтроки.ИдентификаторСтроки ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	СУММА(ЗначенияПоказателей.Значение) КАК Коэффициент,
	|	ВЫБОР
	|		КОГДА НЕ &ИспользоватьСтатьиФинансирования
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|		ИНАЧЕ ЗначенияПоказателей.СтатьяФинансирования
	|	КОНЕЦ КАК СтатьяФинансирования,
	|	ЗначенияПоказателей.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	ВЫБОР
	|		КОГДА &ЕстьЕНВД
	|			ТОГДА ЗначенияПоказателей.ОтношениеКЕНВД
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.НеЕНВД)
	|	КОНЕЦ КАК ОтношениеКЕНВД,
	|	ЗначенияПоказателей.Подразделение КАК ПодразделениеУчетаЗатрат
	|ИЗ
	|	ВТНачисленияДляРаспределения КАК СписокНачислений
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНастройкиНачислений КАК НастройкиНачислений
	|		ПО СписокНачислений.Начисление = НастройкиНачислений.Начисление
	|			И (НастройкиНачислений.ИспользуетСдельныйЗаработок)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ЗначенияОперативныхПоказателейРасчетаЗарплатыСотрудников КАК ЗначенияПоказателей
	|		ПО СписокНачислений.Сотрудник = ЗначенияПоказателей.Сотрудник
	|			И (ЗначенияПоказателей.Показатель = &ПоказательСдельногоЗаработка)
	|			И (ЗначенияПоказателей.СпособОтраженияЗарплатыВБухучете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка))
	|			И (НастройкиНачислений.ИспользоватьОперативныеПоказателиВЦеломЗаМесяц
	|					И НАЧАЛОПЕРИОДА(ЗначенияПоказателей.Период, МЕСЯЦ) = НАЧАЛОПЕРИОДА(СписокНачислений.ДатаНачала, МЕСЯЦ)
	|				ИЛИ ЗначенияПоказателей.Период МЕЖДУ СписокНачислений.ДатаНачала И СписокНачислений.ДатаОкончания)
	|
	|СГРУППИРОВАТЬ ПО
	|	СписокНачислений.ИдентификаторСтроки,
	|	ЗначенияПоказателей.СтатьяФинансирования,
	|	ЗначенияПоказателей.СпособОтраженияЗарплатыВБухучете,
	|	ЗначенияПоказателей.ОтношениеКЕНВД,
	|	ЗначенияПоказателей.Подразделение";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Если РезультатЗапроса[0].Пустой() Тогда
		Возврат Ложь;
	КонецЕсли;
		
	ТаблицаБухучет = НоваяТаблицаБухучетНачисленийПромежуточная();
	
	ТаблицаКоэффициентов = РезультатЗапроса[1].Выгрузить();
	ТаблицаКоэффициентов.Индексы.Добавить("ИдентификаторСтроки");
	Отбор = Новый Структура("ИдентификаторСтроки");
	
	ВыборкаНачисления = РезультатЗапроса[0].Выбрать();
	Пока ВыборкаНачисления.Следующий() Цикл
		
		Отбор.ИдентификаторСтроки = ВыборкаНачисления.ИдентификаторСтроки;
		
		ЗаполнитьЗначенияСвойств(Отбор, ВыборкаНачисления);
		СтрокиОтражения = ТаблицаКоэффициентов.НайтиСтроки(Отбор);
		Если СтрокиОтражения.Количество() = 0 Тогда
			// пропускаем эту строку, т.к. бухучет сдельного заработка не задан
			Продолжить;
		КонецЕсли;
		
		Коэффициенты = ОбщегоНазначения.ВыгрузитьКолонку(СтрокиОтражения,"Коэффициент");
		Результаты = ЗарплатаКадрыКлиентСервер.РаспределитьПропорциональноКоэффициентам(ВыборкаНачисления.Сумма, Коэффициенты);
		
		Если Результаты = Неопределено Тогда
			// пропускаем эту строку, т.к. бухучет сдельного заработка не удалось получить
			Продолжить;
		Иначе
			
			Индекс = 0;
			Для Каждого СтрокаОтражения Из СтрокиОтражения Цикл
				
				НоваяСтрока = ТаблицаБухучет.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаНачисления);
				
				НоваяСтрока.СтатьяФинансирования 				= СтрокаОтражения.СтатьяФинансирования;
				НоваяСтрока.СпособОтраженияЗарплатыВБухучете 	= СтрокаОтражения.СпособОтраженияЗарплатыВБухучете;
				НоваяСтрока.ПодразделениеУчетаЗатрат 			= СтрокаОтражения.ПодразделениеУчетаЗатрат;
				НоваяСтрока.ОблагаетсяЕНВД = ?(СтрокаОтражения.ОтношениеКЕНВД = Перечисления.ОтношениеКЕНВДЗатратНаЗарплату.ЕНВД, Истина, Ложь);
				
				НоваяСтрока.Сумма = Результаты[Индекс];
				
				Если РаботаВБюджетномУчреждении Тогда
					НоваяСтрока.СтатьяРасходов = СтатьяРасходовБюджетНачисления(ВыборкаНачисления.Начисление, ПараметрыРаспределенияБюджет);
				КонецЕсли;
				
				Индекс = Индекс + 1;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ТаблицаБухучет.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ЗарплатаКадры.СоздатьВТПоТаблицеЗначений(Запрос.МенеджерВременныхТаблиц, ТаблицаБухучет, "ВТБухучетНачисленийСдельногоЗаработка");
	// Зарегистрируем обработанные идентификаторы строк.
	ДополнитьВТОбработанныеСтроки(Запрос, "ВТБухучетНачисленийСдельногоЗаработка");
	
	Возврат Истина;
		
КонецФункции

Функция СоздатьВТНачисленияПраздничныхСверхурочныхДляБухучета(Запрос, ИмяВТНачисленияИсходная, ИсточникДанныхОДатахНачислений, ПараметрыРаспределенияБюджет)

	УдалитьВТ = Новый Массив;
	
	ВидыВремени = Новый Массив;
	ВидыВремени.Добавить(ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыИспользованияРабочегоВремени.Праздники"));
	ВидыВремени.Добавить(ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыИспользованияРабочегоВремени.ПраздникиБезПовышеннойОплаты"));
	ВидыВремени.Добавить(ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыИспользованияРабочегоВремени.Сверхурочные"));
	ВидыВремени.Добавить(ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыИспользованияРабочегоВремени.СверхурочныеБезПовышеннойОплаты"));
	
	Запрос.УстановитьПараметр("ВидыВремениПраздничныхСверхурочных", ВидыВремени);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НачисленияВидыВремени.Ссылка КАК Ссылка,
	|	НачисленияВидыВремени.ВидВремени КАК ВидВремени
	|ПОМЕСТИТЬ ВТНачисленияПраздничныхСверхурочных
	|ИЗ
	|	ПланВидовРасчета.Начисления.ВидыВремени КАК НачисленияВидыВремени
	|ГДЕ
	|	НачисленияВидыВремени.Ссылка.СпособВыполненияНачисления = ЗНАЧЕНИЕ(Перечисление.СпособыВыполненияНачислений.ПоЗначениюВидаВремениПриОкончательномРасчете)
	|	И НачисленияВидыВремени.ВидВремени В(&ВидыВремениПраздничныхСверхурочных)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СписокНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	СписокНачислений.Начисление КАК Начисление,
	|	СписокНачислений.ПериодРегистрации КАК ПериодРегистрации,
	|	СписокНачислений.Сотрудник КАК Сотрудник,
	|	СписокНачислений.ДатаНачала КАК ДатаНачала,
	|	СписокНачислений.ДатаОкончания КАК ДатаОкончания,
	|	Начисления.ВидВремени КАК ВидУчетаВремени
	|ПОМЕСТИТЬ ВТСтрокиКОбработке
	|ИЗ
	|	ВТНачисленияДляРаспределения КАК СписокНачислений
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОбработанныеСтроки КАК ОбработанныеСтроки
	|		ПО СписокНачислений.ИдентификаторСтроки = ОбработанныеСтроки.ИдентификаторСтроки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНачисленияПраздничныхСверхурочных КАК Начисления
	|		ПО СписокНачислений.Начисление = Начисления.Ссылка
	|ГДЕ
	|	ОбработанныеСтроки.ИдентификаторСтроки ЕСТЬ NULL
	|	И СписокНачислений.Сумма <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтрокиКОбработке.ПериодРегистрации КАК ПериодРегистрации,
	|	СтрокиКОбработке.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	СтрокиКОбработке.Сотрудник КАК Сотрудник,
	|	СтрокиКОбработке.Начисление КАК Начисление,
	|	НАЧАЛОПЕРИОДА(СтрокиКОбработке.ДатаНачала, МЕСЯЦ) КАК ПериодДействия,
	|	НастройкиБухучета.Дата КАК Дата,
	|	НастройкиБухучета.ВидУчетаВремени КАК ВидУчетаВремени,
	|	НастройкиБухучета.Часы КАК Часы,
	|	НастройкиБухучета.СтатьяФинансирования КАК СтатьяФинансирования,
	|	НастройкиБухучета.СтатьяРасходов КАК СтатьяРасходов,
	|	НастройкиБухучета.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	НастройкиБухучета.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	ВЫБОР
	|		КОГДА &ИспользоватьСтатьиФинансирования
	|					И НастройкиБухучета.СтатьяФинансирования = ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|				ИЛИ &ЕстьЕНВД
	|					И НастройкиБухучета.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)
	|				ИЛИ НастройкиБухучета.СпособОтраженияЗарплатыВБухучете = ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НеЗаполненаАналитика
	|ИЗ
	|	ВТСтрокиКОбработке КАК СтрокиКОбработке
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БухучетРабочегоВремениСотрудников КАК НастройкиБухучета
	|		ПО СтрокиКОбработке.Сотрудник = НастройкиБухучета.Сотрудник
	|			И СтрокиКОбработке.ВидУчетаВремени = НастройкиБухучета.ВидУчетаВремени
	|			И СтрокиКОбработке.ПериодРегистрации >= НастройкиБухучета.ПериодРегистрации
	|			И (НастройкиБухучета.Дата МЕЖДУ СтрокиКОбработке.ДатаНачала И СтрокиКОбработке.ДатаОкончания)
	|			И (НастройкиБухучета.СпособОтраженияЗарплатыВБухучете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
	|				ИЛИ &ИспользоватьСтатьиФинансирования
	|					И НастройкиБухучета.СтатьяФинансирования <> ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|				ИЛИ &РаботаВБюджетномУчреждении
	|					И НастройкиБухучета.СтатьяРасходов <> ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
	|				ИЛИ &ЕстьЕНВД
	|					И НастройкиБухучета.ОтношениеКЕНВД <> ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка))";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
	БухучетРабочегоВремени = Запрос.Выполнить().Выгрузить();
	УдалитьВТ.Добавить("ВТНачисленияПраздничныхСверхурочных");
	УдалитьВТ.Добавить("ВТСтрокиКОбработке");
	
	Если БухучетРабочегоВремени.Количество() = 0 Тогда
		// Бухучет не задан.
		ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
		Возврат Ложь;
	КонецЕсли;
	
	// Идентификаторы строк, по которым требуется дополнить аналитику.
	Отбор = Новый Структура("НеЗаполненаАналитика", Истина);
	МассивСтрокБезАналитики = ОбщегоНазначения.ВыгрузитьКолонку(БухучетРабочегоВремени.Скопировать(Отбор),"ИдентификаторСтроки", Истина);
	ИдентификаторыСтрокБезАналитики = ОбщегоНазначенияБЗККлиентСервер.МассивВСоответствие(МассивСтрокБезАналитики);
	
	ПрименяетсяЕНВД = Запрос.Параметры.ЕстьЕНВД;
	ИспользоватьСтатьиФинансирования = Запрос.Параметры.ИспользоватьСтатьиФинансирования;
	РаботаВБюджетномУчреждении 		 = Запрос.Параметры.РаботаВБюджетномУчреждении;
	
	ПоляБухучета = "СпособОтраженияЗарплатыВБухучете";
	Если РаботаВБюджетномУчреждении Тогда
		ПоляБухучета = ПоляБухучета + ",СтатьяРасходов";
	КонецЕсли;
	Если ИспользоватьСтатьиФинансирования Тогда
		ПоляБухучета = ПоляБухучета + ",СтатьяФинансирования";
	КонецЕсли;
	Если ПрименяетсяЕНВД Тогда
		ПоляБухучета = ПоляБухучета + ",ОтношениеКЕНВД";
	КонецЕсли;
	
	// Получим фактически оплаченные дни по видам времени.
	ТаблицаЗапросаОплаченногоВремени = БухучетРабочегоВремени.Скопировать(,"ПериодРегистрации,ПериодДействия,Сотрудник,Начисление");
	ТаблицаЗапросаОплаченногоВремени.Свернуть("ПериодРегистрации,ПериодДействия,Сотрудник,Начисление");
	
	БухучетРабочегоВремени.Свернуть("ИдентификаторСтроки,Сотрудник,Начисление,Дата,ВидУчетаВремени,Часы,СтатьяФинансирования,СтатьяРасходов,СпособОтраженияЗарплатыВБухучете,ОтношениеКЕНВД");
	
	// Получим таблицу оплаченного времени - ТаблицаОплаченногоВремени.
	// колонки таблицы: Сотрудник, ПериодДействия, Начисление, Дата, ВидУчетаВремени.
	Если ИсточникДанныхОДатахНачислений = Неопределено Тогда
		ТаблицаОплаченногоВремени = РасчетЗарплатыРасширенный.ДатыНачисленийОплачивающихВидыВремени(ТаблицаЗапросаОплаченногоВремени);
	Иначе
		ТаблицаОплаченногоВремени = ИсточникДанныхОДатахНачислений.ДатыНачисленийОплачивающихВидыВремени(ТаблицаЗапросаОплаченногоВремени);
	КонецЕсли;
	
	ТаблицаБухучета = НоваяТаблицаНастройкиНачисленийДляБухучета();
	
	ИдентификаторыСтрок = ОбщегоНазначения.ВыгрузитьКолонку(БухучетРабочегоВремени, "ИдентификаторСтроки", Истина);
	Запрос.УстановитьПараметр("ОтборСтрок", ИдентификаторыСтрок);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(Начисления.ДатаНачала, МЕСЯЦ) КАК ПериодДействия,
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Начисления.Начисление КАК Начисление,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Начисления.ДатаНачала КАК ДатаНачала,
	|	Начисления.ДатаОкончания КАК ДатаОкончания,
	|	Начисления.Подразделение КАК ПодразделениеУчетаЗатрат,
	|	Начисления.Сумма КАК Сумма
	|ИЗ
	|	ВТНачисленияДляРаспределения КАК Начисления
	|ГДЕ
	|	Начисления.ИдентификаторСтроки В(&ОтборСтрок)";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
	ТаблицаНачислений = Запрос.Выполнить().Выгрузить();
	
	// В таблицу собираем строки начислений, для которых бухучет задан не для всех оплаченных дней.
	// Доля суммы, приходящаяся на такие дни будет отражена в бухучете по общим правилам.
	НеОбработанныеНачисления = ТаблицаНачислений.СкопироватьКолонки("ИдентификаторСтроки,Сумма");
	// В таблицу собираем идентификаторы строк тех начислений, которые полностью распределили,
	// эти строки будут помещены в таблицу ВТСтрокиКУдалению.
	ОбработанныеНачисления = НеОбработанныеНачисления.СкопироватьКолонки("ИдентификаторСтроки");
	
	ТаблицаНачислений.Индексы.Добавить("ИдентификаторСтроки");
	ОтборНачислений = Новый Структура("ИдентификаторСтроки");
	
	БухучетРабочегоВремени.Индексы.Добавить("ИдентификаторСтроки,Дата,ВидУчетаВремени");
	ОтборБухучета = Новый Структура("ИдентификаторСтроки,Дата,ВидУчетаВремени");
	
	ТаблицаОплаченногоВремени.Индексы.Добавить("Сотрудник,Начисление,ПериодДействия");
	ОтборОплаченныхДней = Новый Структура("Сотрудник,Начисление,ПериодДействия");
	
	// Оплаченные дни, по которым зарегистрирован бухучет.
	ОплаченныеДниСБухучетом = Новый Массив;
	// Оплаченные дни, по которым не зарегистрирован бухучет.
	ОплаченныеДниБезБухучета = Новый Массив;
	// Строки с бухучетом рабочего времени.
	СтрокиБазы = Новый Массив;
	
	Для каждого ИдентификаторСтроки Из ИдентификаторыСтрок Цикл
		
		ОтборНачислений.ИдентификаторСтроки = ИдентификаторСтроки;
		ОтборБухучета.ИдентификаторСтроки   = ИдентификаторСтроки;
		
		СтрокиНачислений = ТаблицаНачислений.НайтиСтроки(ОтборНачислений);
		ДатаНачала 		= СтрокиНачислений[0].ДатаНачала;
		ДатаОкончания 	= СтрокиНачислений[0].ДатаОкончания;
		
		ОплаченныеДниСБухучетом.Очистить();
		ОплаченныеДниБезБухучета.Очистить();
		СтрокиБазы.Очистить();
		
		ВсегоЧасов = 0;
		РаспределятьПоЧасам = Истина;
		
		ЗаполнитьЗначенияСвойств(ОтборОплаченныхДней, СтрокиНачислений[0]);
		ОплаченныеДни = ТаблицаОплаченногоВремени.НайтиСтроки(ОтборОплаченныхДней);
		Для каждого СтрокаОплаченныйДень Из ОплаченныеДни Цикл
			
			Если СтрокаОплаченныйДень.Дата < ДатаНачала Или СтрокаОплаченныйДень.Дата > ДатаОкончания Тогда
				// день не в периоде начисления
				Продолжить;
			КонецЕсли;
			
			// Дополним отбор строк бухучета полями: Дата,ВидУчетаВремени.
			ЗаполнитьЗначенияСвойств(ОтборБухучета, СтрокаОплаченныйДень);
			СтрокиБухучет = БухучетРабочегоВремени.НайтиСтроки(ОтборБухучета);
			Если СтрокиБухучет.Количество() = 0 Тогда
				// Для оплаченного дня не задан бухучет по этому виду времени.
				ОплаченныеДниБезБухучета.Добавить(СтрокаОплаченныйДень.Дата);
			Иначе
				// Для оплаченного дня задан бухучет.
				ОплаченныеДниСБухучетом.Добавить(СтрокаОплаченныйДень.Дата);
				Для каждого СтрокаБухучета Из СтрокиБухучет Цикл
					СтрокиБазы.Добавить(СтрокаБухучета);
					ВсегоЧасов = ВсегоЧасов + СтрокаБухучета.Часы;
					Если СтрокаБухучета.Часы = 0 Тогда
						РаспределятьПоЧасам = Ложь;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
		КонецЦикла;
		
		ДнейОплаченоСБухучетом = ОплаченныеДниСБухучетом.Количество();
		Если ДнейОплаченоСБухучетом = 0 Тогда
			// У начисления нет оплаченных дней, для которых задан бухучет.
			Продолжить;
		КонецЕсли;
		
		// Распределение начислений.
		
		ДнейОплаченоБезБухучета = ОплаченныеДниБезБухучета.Количество();
		Для каждого СтрокаНачисления Из СтрокиНачислений Цикл
			
			Сумма = СтрокаНачисления.Сумма;
			Если ДнейОплаченоБезБухучета > 0 Тогда
				// Сумма по строке, которую необходимо отразить в бухучете по общим правилам.
				СуммаБезРаспределения = ОКР(ДнейОплаченоБезБухучета/(ДнейОплаченоБезБухучета+ДнейОплаченоСБухучетом)*Сумма,2);
				// Добавим в таблицу строку начисления, которую необходимо отразить в бухучете по общим правилам.
				НоваяСтрока = НеОбработанныеНачисления.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНачисления);
				НоваяСтрока.Сумма = СуммаБезРаспределения;
				// Сумма строки начисления, которую необходимо распределить.
				Сумма = Сумма - СуммаБезРаспределения;
			Иначе
				// Добавим в таблицу строку, которая полностью обработана, для помещения в таблицу ВТОбработанныеСтроки.
				НоваяОбработаннаяСтрока = ОбработанныеНачисления.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяОбработаннаяСтрока, СтрокаНачисления);
			КонецЕсли;
			
			РаспределеннаяСумма = 0;
			Если РаспределятьПоЧасам Тогда
				СтоимостьДняЧаса = Сумма/ВсегоЧасов;
			Иначе
				СтоимостьДняЧаса = Сумма/ДнейОплаченоСБухучетом;
			КонецЕсли;
			
			КоличествоСтрокБазы = СтрокиБазы.ВГраница();
			Для Индекс = 0 По КоличествоСтрокБазы Цикл
				
				СтрокаБазы = СтрокиБазы[Индекс];
				Если Индекс = КоличествоСтрокБазы Тогда
					// Это последняя строка.
					СуммаДня = Сумма - РаспределеннаяСумма;
				Иначе
					Если РаспределятьПоЧасам Тогда
						СуммаДня = ОКР(СтоимостьДняЧаса * СтрокаБазы.Часы,2);
					Иначе
						СуммаДня = ОКР(СтоимостьДняЧаса, 2);
					КонецЕсли;
				КонецЕсли;
				РаспределеннаяСумма = РаспределеннаяСумма + СуммаДня;
				
				НоваяСтрока = ТаблицаБухучета.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНачисления);
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаБазы, ПоляБухучета);
				НоваяСтрока.Сумма = СуммаДня;
				НоваяСтрока.НеЗаполненаАналитика = ИдентификаторыСтрокБезАналитики[НоваяСтрока.ИдентификаторСтроки] <> Неопределено;
				
				Если РаботаВБюджетномУчреждении Тогда
					Если Не ЗначениеЗаполнено(НоваяСтрока.СтатьяРасходов) Тогда
						НоваяСтрока.СтатьяРасходов = СтатьяРасходовБюджетНачисления(СтрокаНачисления.Начисление, ПараметрыРаспределенияБюджет);
					КонецЕсли;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Если ТаблицаБухучета.Количество() = 0 Тогда
		ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
		Возврат Ложь;
	Иначе
		
		// Свернем результаты, нулевые строки не удаляем.
		ОтражениеЗарплатыВУчете.СвернутьТаблицу(ТаблицаБухучета, Ложь);
		
		ЗарплатаКадры.СоздатьВТПоТаблицеЗначений(Запрос.МенеджерВременныхТаблиц, ТаблицаБухучета, "ВТНачисленияПраздничныхСверхурочныхДляБухучета");
		Если ИдентификаторыСтрокБезАналитики.Количество() > 0 Тогда
			ДополнитьВТСтрокиБезАналитики(Запрос, "ВТНачисленияПраздничныхСверхурочныхДляБухучета");
		КонецЕсли;
		
		// Зарегистрируем идентификаторы полностью обработанных строк в таблице ВТОбработанныеСтроки.
		ЗарплатаКадры.СоздатьВТПоТаблицеЗначений(Запрос.МенеджерВременныхТаблиц, ОбработанныеНачисления, "ВТОбработанныеНачисления");
		УдалитьВТ.Добавить("ВТОбработанныеНачисления");
		ДополнитьВТОбработанныеСтроки(Запрос, "ВТОбработанныеНачисления");
		
		Если НеОбработанныеНачисления.Количество() > 0 Тогда
			
			Запрос.УстановитьПараметр("НеОбработанныеНачисления", НеОбработанныеНачисления);
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	СписокНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	СписокНачислений.Сумма КАК Сумма
			|ПОМЕСТИТЬ ВТНеОбработанныеНачисления
			|ИЗ
			|	&НеОбработанныеНачисления КАК СписокНачислений
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	СписокНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	СписокНачислений.Сумма КАК Сумма,
			|	&ПоляТаблицы КАК ПоляТаблицы
			|ПОМЕСТИТЬ ВТНачисленияВременная
			|ИЗ
			|	ВТНачисленияДляРаспределения КАК СписокНачислений
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ВТНачисленияДляРаспределения
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	НачисленияДляРаспределенияВременная.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	ЕСТЬNULL(НеОбработанныеНачисления.Сумма, НачисленияДляРаспределенияВременная.Сумма) КАК Сумма,
			|	&ПоляТаблицы КАК ПоляТаблицы
			|ПОМЕСТИТЬ ВТНачисленияДляРаспределения
			|ИЗ
			|	ВТНачисленияВременная КАК НачисленияДляРаспределенияВременная
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНеОбработанныеНачисления КАК НеОбработанныеНачисления
			|		ПО НачисленияДляРаспределенияВременная.ИдентификаторСтроки = НеОбработанныеНачисления.ИдентификаторСтроки";
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПоляТаблицы КАК ПоляТаблицы", "*");
			Запрос.Выполнить();
			УдалитьВТ.Добавить("ВТНеОбработанныеНачисления");
			УдалитьВТ.Добавить("ВТНачисленияВременная");
			
		КонецЕсли;
		
	КонецЕсли;
	
	ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
	
	Возврат Истина;
	
КонецФункции

Функция СоздатьВТНачисленияПоПоказателямДляБухучета(Запрос, ИмяВТНачисленияИсходная, ПараметрыРаспределенияБюджет)
	
	УдалитьВТ = Новый Массив;
	
	ИмяТаблицыБухучетПоказателей = "";
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОбразовательныеУчреждения") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОбразовательныеУчреждения");
		ИмяТаблицыБухучетПоказателей = Модуль.ИмяТаблицыБухучетПоказателейДляЗапроса();
	КонецЕсли;
	
	Если ПустаяСтрока(ИмяТаблицыБухучетПоказателей) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Начисления.Организация КАК Организация,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.ДатаНачала КАК ДатаНачала,
	|	ПоказателиОпределяющиеБухучет.Показатель КАК Показатель
	|ПОМЕСТИТЬ ВТПоказателиДляБухучета
	|ИЗ
	|	ВТНачисленияДляРаспределения КАК Начисления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПоказателиНачисленийОпределяющиеБухучет КАК ПоказателиОпределяющиеБухучет
	|		ПО Начисления.Начисление = ПоказателиОпределяющиеБухучет.Начисление
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОбработанныеСтроки КАК ОбработанныеСтроки
	|		ПО Начисления.ИдентификаторСтроки = ОбработанныеСтроки.ИдентификаторСтроки
	|ГДЕ
	|	ОбработанныеСтроки.ИдентификаторСтроки ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВТПоказателиДляБухучета.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ИЗ
	|	ВТПоказателиДляБухучета КАК ВТПоказателиДляБухучета";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
	Если Запрос.Выполнить().Пустой() Тогда
		ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, "ВТПоказателиДляБухучета");
		Возврат Ложь;
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПоказателиДляБухучета.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ПоказателиДляБухучета.Организация КАК Организация,
	|	ПоказателиДляБухучета.Сотрудник КАК Сотрудник,
	|	ПоказателиДляБухучета.Показатель КАК Показатель,
	|	МАКСИМУМ(БухучетПоказателей.Период) КАК Период,
	|	ПоказателиДляБухучета.ДатаНачала КАК ДатаНачала
	|ПОМЕСТИТЬ ВТМаксимальныеПериоды
	|ИЗ
	|	ВТПоказателиДляБухучета КАК ПоказателиДляБухучета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ #ТаблицаБухучетПоказателей КАК БухучетПоказателей
	|		ПО ПоказателиДляБухучета.Организация = БухучетПоказателей.Организация
	|			И ПоказателиДляБухучета.Сотрудник = БухучетПоказателей.Сотрудник
	|			И ПоказателиДляБухучета.Показатель = БухучетПоказателей.Показатель
	|			И ПоказателиДляБухучета.ДатаНачала >= БухучетПоказателей.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоказателиДляБухучета.ИдентификаторСтроки,
	|	ПоказателиДляБухучета.Организация,
	|	ПоказателиДляБухучета.Сотрудник,
	|	ПоказателиДляБухучета.Показатель,
	|	ПоказателиДляБухучета.ДатаНачала
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Коэффициенты.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Коэффициенты.СтатьяФинансирования КАК СтатьяФинансирования,
	|	Коэффициенты.СтатьяРасходов КАК СтатьяРасходов,
	|	Коэффициенты.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	Коэффициенты.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	СУММА(Коэффициенты.Коэффициент) КАК Коэффициент
	|ПОМЕСТИТЬ ВТКоэффициенты
	|ИЗ
	|	(ВЫБРАТЬ
	|		МаксимальныеПериоды.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|		БухучетПоказателей.СтатьяФинансирования КАК СтатьяФинансирования,
	|		БухучетПоказателей.СтатьяРасходов КАК СтатьяРасходов,
	|		БухучетПоказателей.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|		БухучетПоказателей.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|		СУММА(БухучетПоказателей.Значение) КАК Коэффициент
	|	ИЗ
	|		ВТМаксимальныеПериоды КАК МаксимальныеПериоды
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ #ТаблицаБухучетПоказателей КАК БухучетПоказателей
	|			ПО МаксимальныеПериоды.Организация = БухучетПоказателей.Организация
	|				И МаксимальныеПериоды.Сотрудник = БухучетПоказателей.Сотрудник
	|				И МаксимальныеПериоды.Показатель = БухучетПоказателей.Показатель
	|				И МаксимальныеПериоды.Период = БухучетПоказателей.Период
	|				И МаксимальныеПериоды.ДатаНачала <= БухучетПоказателей.ДатаОкончания
	|				И (БухучетПоказателей.ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1))
	|	
	|	СГРУППИРОВАТЬ ПО
	|		МаксимальныеПериоды.ИдентификаторСтроки,
	|		БухучетПоказателей.СтатьяФинансирования,
	|		БухучетПоказателей.СтатьяРасходов,
	|		БухучетПоказателей.СпособОтраженияЗарплатыВБухучете,
	|		БухучетПоказателей.ОтношениеКЕНВД
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		МаксимальныеПериоды.ИдентификаторСтроки,
	|		БухучетПоказателей.СтатьяФинансирования,
	|		БухучетПоказателей.СтатьяРасходов,
	|		БухучетПоказателей.СпособОтраженияЗарплатыВБухучете,
	|		БухучетПоказателей.ОтношениеКЕНВД,
	|		СУММА(БухучетПоказателей.Значение)
	|	ИЗ
	|		ВТМаксимальныеПериоды КАК МаксимальныеПериоды
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ #ТаблицаБухучетПоказателей КАК БухучетПоказателей
	|			ПО МаксимальныеПериоды.Организация = БухучетПоказателей.Организация
	|				И МаксимальныеПериоды.Сотрудник = БухучетПоказателей.Сотрудник
	|				И МаксимальныеПериоды.Показатель = БухучетПоказателей.Показатель
	|				И МаксимальныеПериоды.Период = БухучетПоказателей.Период
	|				И (БухучетПоказателей.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1))
	|	
	|	СГРУППИРОВАТЬ ПО
	|		МаксимальныеПериоды.ИдентификаторСтроки,
	|		БухучетПоказателей.СтатьяФинансирования,
	|		БухучетПоказателей.СтатьяРасходов,
	|		БухучетПоказателей.СпособОтраженияЗарплатыВБухучете,
	|		БухучетПоказателей.ОтношениеКЕНВД) КАК Коэффициенты
	|
	|СГРУППИРОВАТЬ ПО
	|	Коэффициенты.СтатьяФинансирования,
	|	Коэффициенты.ИдентификаторСтроки,
	|	Коэффициенты.ОтношениеКЕНВД,
	|	Коэффициенты.СпособОтраженияЗарплатыВБухучете,
	|	Коэффициенты.СтатьяРасходов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтрокиКОбработке.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ВТСтрокиКОбработке
	|ИЗ
	|	ВТКоэффициенты КАК СтрокиКОбработке
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Коэффициенты.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Коэффициенты.СтатьяФинансирования КАК СтатьяФинансирования,
	|	Коэффициенты.СтатьяРасходов КАК СтатьяРасходов,
	|	Коэффициенты.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	Коэффициенты.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	Коэффициенты.Коэффициент КАК Коэффициент
	|ИЗ
	|	ВТКоэффициенты КАК Коэффициенты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Начисления.Начисление КАК Начисление,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Начисления.ДатаНачала КАК ДатаНачала,
	|	Начисления.ДатаОкончания КАК ДатаОкончания,
	|	Начисления.Сумма КАК Сумма
	|ИЗ
	|	ВТНачисленияДляРаспределения КАК Начисления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСтрокиКОбработке КАК СтрокиКОбработке
	|		ПО Начисления.ИдентификаторСтроки = СтрокиКОбработке.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиБухучетаВсехСтрокПоУмолчанию КАК НастройкиБухучетаПоУмолчанию
	|		ПО Начисления.ИдентификаторСтроки = НастройкиБухучетаПоУмолчанию.ИдентификаторСтроки";
	
	УдалитьВТ.Добавить("ВТПоказателиДляБухучета");
	УдалитьВТ.Добавить("ВТМаксимальныеПериоды");
	УдалитьВТ.Добавить("ВТКоэффициенты");
	УдалитьВТ.Добавить("ВТСтрокиКОбработке");
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ТаблицаБухучетПоказателей", ИмяТаблицыБухучетПоказателей);
	
	УстановитьПривилегированныйРежим(Истина); // Для чтения из РС #ТаблицаБухучетПоказателей.
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);
	КоличествоРезультатов = РезультатыЗапроса.ВГраница();
	
	Если РезультатыЗапроса[КоличествоРезультатов].Пустой() Тогда
		ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
		Возврат Ложь;
	КонецЕсли;
	
	ТаблицаБухучет = НоваяТаблицаНастройкиНачисленийДляБухучета();
	
	ЕстьЕНВД = Запрос.Параметры.ЕстьЕНВД;
	РаботаВБюджетномУчреждении = Запрос.Параметры.РаботаВБюджетномУчреждении;
	
	ТаблицаКоэффициентов = РезультатыЗапроса[КоличествоРезультатов-1].Выгрузить();
	ТаблицаКоэффициентов.Индексы.Добавить("ИдентификаторСтроки");
	Отбор = Новый Структура("ИдентификаторСтроки");
	
	ВыборкаНачисления = РезультатыЗапроса[КоличествоРезультатов].Выбрать();
	Пока ВыборкаНачисления.Следующий() Цикл
		
		Отбор.ИдентификаторСтроки = ВыборкаНачисления.ИдентификаторСтроки;
		
		ЗаполнитьЗначенияСвойств(Отбор, ВыборкаНачисления);
		СтрокиОтражения = ТаблицаКоэффициентов.НайтиСтроки(Отбор);
		Если СтрокиОтражения.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Коэффициенты = ОбщегоНазначения.ВыгрузитьКолонку(СтрокиОтражения,"Коэффициент");
		Результаты = ЗарплатаКадрыКлиентСервер.РаспределитьПропорциональноКоэффициентам(ВыборкаНачисления.Сумма, Коэффициенты);
		
		Если Результаты = Неопределено Тогда
			
			НоваяСтрока = ТаблицаБухучет.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаНачисления);
			НоваяСтрока.НеЗаполненаАналитика = Истина;
			Если РаботаВБюджетномУчреждении Тогда
				НоваяСтрока.СтатьяРасходов = СтатьяРасходовБюджетНачисления(ВыборкаНачисления.Начисление, ПараметрыРаспределенияБюджет);
			КонецЕсли;
			
		Иначе
			
			Индекс = 0;
			Для Каждого СтрокаОтражения Из СтрокиОтражения Цикл
				
				НоваяСтрока = ТаблицаБухучет.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаНачисления);
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаОтражения);
				НоваяСтрока.Сумма = Результаты[Индекс];
				
				Если Не ЗначениеЗаполнено(СтрокаОтражения.СтатьяФинансирования) Или Не ЗначениеЗаполнено(СтрокаОтражения.СпособОтраженияЗарплатыВБухучете) Тогда
					НоваяСтрока.НеЗаполненаАналитика = Истина;
				КонецЕсли;
				
				Если РаботаВБюджетномУчреждении Тогда
					НоваяСтрока.СтатьяРасходов = СтрокаОтражения.СтатьяРасходов;
					Если Не ЗначениеЗаполнено(СтрокаОтражения.СтатьяРасходов) Тогда
						НоваяСтрока.СтатьяРасходов = СтатьяРасходовБюджетНачисления(ВыборкаНачисления.Начисление, ПараметрыРаспределенияБюджет);
					КонецЕсли;
				КонецЕсли;
				
				Если ЕстьЕНВД Тогда
					НоваяСтрока.ОблагаетсяЕНВД = ?(СтрокаОтражения.ОтношениеКЕНВД = Перечисления.ОтношениеКЕНВДЗатратНаЗарплату.ЕНВД, Истина, Ложь);
				КонецЕсли;
				
				Индекс = Индекс + 1;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ЗарплатаКадры.СоздатьВТПоТаблицеЗначений(Запрос.МенеджерВременныхТаблиц, ТаблицаБухучет, "ВТНачисленияПоПоказателямДляБухучета");
	
	// Зарегистрируем обработанные идентификаторы строк.
	ДополнитьВТОбработанныеСтроки(Запрос, "ВТНачисленияПоПоказателямДляБухучета");
	ДополнитьВТСтрокиБезАналитики(Запрос, "ВТНачисленияПоПоказателямДляБухучета");
	
	ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
	
	Возврат Истина;
	
КонецФункции

Функция СоздатьВТНачисленияПоДокументамДляБухучета(Запрос, ИмяВТНачисленияИсходная)
	
	УдалитьВТ = Новый Массив;
	
	ИспользоватьСтатьиФинансирования = Запрос.Параметры.ИспользоватьСтатьиФинансирования;
	
	Если Не ИспользоватьСтатьиФинансирования Тогда
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	Начисления.Начисление КАК Начисление,
		|	Начисления.Сумма КАК Сумма,
		|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК ПодразделениеУчетаЗатрат,
		|	НастройкиБухучета.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка) КАК СтатьяФинансирования,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка) КАК СтатьяРасходов,
		|	НастройкиБухучета.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
		|	ВЫБОР
		|		КОГДА &ЕстьЕНВД
		|					И НастройкиБухучета.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)
		|				ИЛИ НастройкиБухучета.СпособОтраженияЗарплатыВБухучете = ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК НеЗаполненаАналитика,
		|	НастройкиБухучета.ДоляРаспределения КАК ДоляРаспределения
		|ПОМЕСТИТЬ ВТНачисленияПоДокументамДляБухучета
		|ИЗ
		|	ВТНачисленияДляРаспределения КАК Начисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОбработанныеСтроки КАК ОбработанныеСтроки
		|		ПО Начисления.ИдентификаторСтроки = ОбработанныеСтроки.ИдентификаторСтроки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БухучетЗарплатыПервичныхДокументов КАК НастройкиБухучета
		|		ПО Начисления.Начисление = НастройкиБухучета.НачислениеУдержание
		|			И Начисления.ДокументОснование = НастройкиБухучета.ДокументОснование
		|			И (НастройкиБухучета.СпособОтраженияЗарплатыВБухучете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
		|				ИЛИ НастройкиБухучета.ДоляРаспределения <> 0
		|				ИЛИ &ЕстьЕНВД
		|					И НастройкиБухучета.ОтношениеКЕНВД <> ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка))
		|ГДЕ
		|	ОбработанныеСтроки.ИдентификаторСтроки ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	Таблица.ИдентификаторСтроки КАК ИдентификаторСтроки
		|ИЗ
		|	ВТНачисленияПоДокументамДляБухучета КАК Таблица";
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
		НетНастройки = Запрос.Выполнить().Пустой();
		
	Иначе
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	БухучетПервичныхДокументов.ДокументОснование КАК ДокументОснование,
		|	БухучетПервичныхДокументов.НачислениеУдержание КАК Начисление,
		|	БухучетПервичныхДокументов.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	БухучетПервичныхДокументов.СтатьяФинансирования КАК СтатьяФинансирования,
		|	БухучетПервичныхДокументов.СтатьяРасходов КАК СтатьяРасходов,
		|	БухучетПервичныхДокументов.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
		|	БухучетПервичныхДокументов.ДоляРаспределения КАК ДоляРаспределения
		|ПОМЕСТИТЬ ВТБухучетПервичногоДокументаПараметр
		|ИЗ
		|	&БухучетПервичногоДокумента КАК БухучетПервичныхДокументов";
		Запрос.Выполнить();
		УдалитьВТ.Добавить("ВТБухучетПервичногоДокументаПараметр");
		
		РаботаВБюджетномУчреждении = Запрос.Параметры.РаботаВБюджетномУчреждении;
		Если РаботаВБюджетномУчреждении Тогда
			
			Запрос.Текст =
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	Начисления.Начисление КАК Начисление,
			|	Начисления.Сумма КАК Сумма,
			|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК ПодразделениеУчетаЗатрат,
			|	НастройкиБухучета.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
			|	НастройкиБухучета.СтатьяФинансирования КАК СтатьяФинансирования,
			|	ВЫБОР
			|		КОГДА НастройкиБухучета.СтатьяРасходов = ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
			|			ТОГДА СтатьиРасходовПоУмолчанию.СтатьяРасходов
			|		ИНАЧЕ НастройкиБухучета.СтатьяРасходов
			|	КОНЕЦ КАК СтатьяРасходов,
			|	НастройкиБухучета.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
			|	ВЫБОР
			|		КОГДА НастройкиБухучета.СтатьяФинансирования = ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
			|				ИЛИ &ЕстьЕНВД
			|					И НастройкиБухучета.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)
			|				ИЛИ НастройкиБухучета.СпособОтраженияЗарплатыВБухучете = ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК НеЗаполненаАналитика,
			|	НастройкиБухучета.ДоляРаспределения КАК ДоляРаспределения
			|ПОМЕСТИТЬ ВТНачисленияПоДокументамДляБухучета
			|ИЗ
			|	ВТНачисленияДляРаспределения КАК Начисления
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОбработанныеСтроки КАК ОбработанныеСтроки
			|		ПО Начисления.ИдентификаторСтроки = ОбработанныеСтроки.ИдентификаторСтроки
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТБухучетПервичногоДокументаПараметр КАК НастройкиБухучета
			|		ПО Начисления.Начисление = НастройкиБухучета.Начисление
			|			И Начисления.ДокументОснование = НастройкиБухучета.ДокументОснование
			|			И (НастройкиБухучета.СпособОтраженияЗарплатыВБухучете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
			|				ИЛИ НастройкиБухучета.СтатьяФинансирования <> ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
			|				ИЛИ НастройкиБухучета.СтатьяРасходов <> ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
			|				ИЛИ НастройкиБухучета.ДоляРаспределения <> 0
			|				ИЛИ &ЕстьЕНВД
			|					И НастройкиБухучета.ОтношениеКЕНВД <> ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка))
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатьиРасходовНачисленийБюджетПоУмолчанию КАК СтатьиРасходовПоУмолчанию
			|		ПО Начисления.Начисление = СтатьиРасходовПоУмолчанию.Начисление
			|ГДЕ
			|	ОбработанныеСтроки.ИдентификаторСтроки ЕСТЬ NULL
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Начисления.ИдентификаторСтроки,
			|	Начисления.Начисление,
			|	Начисления.Сумма,
			|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка),
			|	НастройкиБухучета.СпособОтраженияЗарплатыВБухучете,
			|	НастройкиБухучета.СтатьяФинансирования,
			|	ВЫБОР
			|		КОГДА НастройкиБухучета.СтатьяРасходов = ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
			|			ТОГДА СтатьиРасходовПоУмолчанию.СтатьяРасходов
			|		ИНАЧЕ НастройкиБухучета.СтатьяРасходов
			|	КОНЕЦ,
			|	НастройкиБухучета.ОтношениеКЕНВД,
			|	ВЫБОР
			|		КОГДА НастройкиБухучета.СтатьяФинансирования = ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
			|				ИЛИ &ЕстьЕНВД
			|					И НастройкиБухучета.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)
			|				ИЛИ НастройкиБухучета.СпособОтраженияЗарплатыВБухучете = ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ,
			|	НастройкиБухучета.ДоляРаспределения
			|ИЗ
			|	ВТНачисленияДляРаспределения КАК Начисления
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОбработанныеСтроки КАК ОбработанныеСтроки
			|		ПО Начисления.ИдентификаторСтроки = ОбработанныеСтроки.ИдентификаторСтроки
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БухучетЗарплатыПервичныхДокументов КАК НастройкиБухучета
			|		ПО Начисления.Начисление = НастройкиБухучета.НачислениеУдержание
			|			И Начисления.ДокументОснование = НастройкиБухучета.ДокументОснование
			|			И (НастройкиБухучета.ДокументОснование <> &ИсключаемыйРегистратор)
			|			И (НастройкиБухучета.СпособОтраженияЗарплатыВБухучете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
			|				ИЛИ НастройкиБухучета.СтатьяФинансирования <> ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
			|				ИЛИ НастройкиБухучета.СтатьяРасходов <> ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
			|				ИЛИ НастройкиБухучета.ДоляРаспределения <> 0
			|				ИЛИ &ЕстьЕНВД
			|					И НастройкиБухучета.ОтношениеКЕНВД <> ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка))
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатьиРасходовНачисленийБюджетПоУмолчанию КАК СтатьиРасходовПоУмолчанию
			|		ПО Начисления.Начисление = СтатьиРасходовПоУмолчанию.Начисление
			|ГДЕ
			|	ОбработанныеСтроки.ИдентификаторСтроки ЕСТЬ NULL
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ ПЕРВЫЕ 1
			|	Таблица.ИдентификаторСтроки КАК ИдентификаторСтроки
			|ИЗ
			|	ВТНачисленияПоДокументамДляБухучета КАК Таблица";
			
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
			НетНастройки = Запрос.Выполнить().Пустой();
			
		Иначе
			
			Запрос.Текст =
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	Начисления.Начисление КАК Начисление,
			|	Начисления.Сумма КАК Сумма,
			|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК ПодразделениеУчетаЗатрат,
			|	НастройкиБухучета.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
			|	НастройкиБухучета.СтатьяФинансирования КАК СтатьяФинансирования,
			|	ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка) КАК СтатьяРасходов,
			|	НастройкиБухучета.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
			|	ВЫБОР
			|		КОГДА НастройкиБухучета.СтатьяФинансирования = ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
			|				ИЛИ &ЕстьЕНВД
			|					И НастройкиБухучета.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)
			|				ИЛИ НастройкиБухучета.СпособОтраженияЗарплатыВБухучете = ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК НеЗаполненаАналитика,
			|	НастройкиБухучета.ДоляРаспределения КАК ДоляРаспределения
			|ПОМЕСТИТЬ ВТНачисленияПоДокументамДляБухучета
			|ИЗ
			|	ВТНачисленияДляРаспределения КАК Начисления
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОбработанныеСтроки КАК ОбработанныеСтроки
			|		ПО Начисления.ИдентификаторСтроки = ОбработанныеСтроки.ИдентификаторСтроки
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТБухучетПервичногоДокументаПараметр КАК НастройкиБухучета
			|		ПО Начисления.Начисление = НастройкиБухучета.Начисление
			|			И Начисления.ДокументОснование = НастройкиБухучета.ДокументОснование
			|			И (НастройкиБухучета.СпособОтраженияЗарплатыВБухучете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
			|				ИЛИ НастройкиБухучета.СтатьяФинансирования <> ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
			|				ИЛИ НастройкиБухучета.ДоляРаспределения <> 0
			|				ИЛИ &ЕстьЕНВД
			|					И НастройкиБухучета.ОтношениеКЕНВД <> ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка))
			|ГДЕ
			|	ОбработанныеСтроки.ИдентификаторСтроки ЕСТЬ NULL
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Начисления.ИдентификаторСтроки,
			|	Начисления.Начисление,
			|	Начисления.Сумма,
			|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка),
			|	НастройкиБухучета.СпособОтраженияЗарплатыВБухучете,
			|	НастройкиБухучета.СтатьяФинансирования,
			|	ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка),
			|	НастройкиБухучета.ОтношениеКЕНВД,
			|	ВЫБОР
			|		КОГДА НастройкиБухучета.СтатьяФинансирования = ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
			|				ИЛИ &ЕстьЕНВД
			|					И НастройкиБухучета.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)
			|				ИЛИ НастройкиБухучета.СпособОтраженияЗарплатыВБухучете = ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ,
			|	НастройкиБухучета.ДоляРаспределения
			|ИЗ
			|	ВТНачисленияДляРаспределения КАК Начисления
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОбработанныеСтроки КАК ОбработанныеСтроки
			|		ПО Начисления.ИдентификаторСтроки = ОбработанныеСтроки.ИдентификаторСтроки
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БухучетЗарплатыПервичныхДокументов КАК НастройкиБухучета
			|		ПО Начисления.Начисление = НастройкиБухучета.НачислениеУдержание
			|			И Начисления.ДокументОснование = НастройкиБухучета.ДокументОснование
			|			И (НастройкиБухучета.ДокументОснование <> &ИсключаемыйРегистратор)
			|			И (НастройкиБухучета.СпособОтраженияЗарплатыВБухучете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
			|				ИЛИ НастройкиБухучета.ДоляРаспределения <> 0
			|				ИЛИ &ЕстьЕНВД
			|					И НастройкиБухучета.ОтношениеКЕНВД <> ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка))
			|ГДЕ
			|	ОбработанныеСтроки.ИдентификаторСтроки ЕСТЬ NULL
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ ПЕРВЫЕ 1
			|	Таблица.ИдентификаторСтроки КАК ИдентификаторСтроки
			|ИЗ
			|	ВТНачисленияПоДокументамДляБухучета КАК Таблица";
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
			НетНастройки = Запрос.Выполнить().Пустой();
			
			// Получим статьи расходов, которые заданы в первичном документе.
			// Указание статьи в этом режиме не означает указание бухучета.
			// При окончательно обработке для строк с этими идентификаторами будут установлены
			// эти статьи расходов.
			Запрос.Текст = 
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	БухучетПервичногоДокумента.СтатьяРасходов КАК СтатьяРасходов
			|ПОМЕСТИТЬ ВТСтатьиРасходовПервичныхДокументов
			|ИЗ
			|	ВТНачисленияДляРаспределения КАК Начисления
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТБухучетПервичногоДокументаПараметр КАК БухучетПервичногоДокумента
			|		ПО Начисления.Начисление = БухучетПервичногоДокумента.Начисление
			|			И Начисления.ДокументОснование = БухучетПервичногоДокумента.ДокументОснование
			|			И (БухучетПервичногоДокумента.СтатьяРасходов <> ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка))";
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
			Если Не Запрос.Выполнить().Пустой() Тогда
				ДополнитьВТСтатьиРасходовНачисленийСотрудников(Запрос, "ВТСтатьиРасходовПервичныхДокументов");
			КонецЕсли;
			УдалитьВТ.Добавить("ВТСтатьиРасходовПервичныхДокументов");
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если НетНастройки Тогда
		// Добавляем в удаляемые "основную" таблицу.
		УдалитьВТ.Добавить("ВТНачисленияПоДокументамДляБухучета");
		ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
		Возврат Ложь;
	КонецЕсли;
	
	ОбработатьСтрокиСРаспределением(Запрос, "ВТНачисленияПоДокументамДляБухучета");
	ДополнитьВТОбработанныеСтроки(Запрос, "ВТНачисленияПоДокументамДляБухучета");
	ДополнитьВТСтрокиБезАналитики(Запрос, "ВТНачисленияПоДокументамДляБухучета");
	
	ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);

	Возврат Истина;

КонецФункции

Функция СоздатьВТНастройкиНачисленийСотрудниковДляБухучета(Запрос, ИмяВТНачисленияИсходная)
	
	РаботаВБюджетномУчреждении = Запрос.Параметры.РаботаВБюджетномУчреждении;
	ИспользоватьСтатьиФинансирования = Запрос.Параметры.ИспользоватьСтатьиФинансирования;
	
	УдалитьВТ = Новый Массив;
	
	ПустыеЗначенияДокументОснование = Новый Массив;
	ПустыеЗначенияДокументОснование.Добавить(Неопределено);
	ПустыеЗначенияДокументОснование.Добавить(Null);
	Для Каждого ТипДокументОснование Из Метаданные.ОпределяемыеТипы.ОснованиеБухучетаНачисления.Тип.Типы() Цикл
		ПустыеЗначенияДокументОснование.Добавить(ОбщегоНазначенияБЗК.МенеджерОбъектаПоТипу(ТипДокументОснование).ПустаяСсылка());
	КонецЦикла;
	
	ТипыОснованийБухучетаНачислений = Метаданные.ОпределяемыеТипы.ОснованиеБухучетаНачисления.Тип.Типы();
	
	Запрос.УстановитьПараметр("ПустыеЗначенияДокументОснование", ПустыеЗначенияДокументОснование);
	Запрос.УстановитьПараметр("ТипыОснованийБухучетаНачислений", ТипыОснованийБухучетаНачислений);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Начисления.Начисление КАК Начисление,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.ДатаНачала КАК Период,
	|	Начисления.ДокументОснование КАК ДокументОснование
	|ПОМЕСТИТЬ ВТНачисленияСОснованиемПодробно
	|ИЗ
	|	ВТНачисленияДляРаспределения КАК Начисления
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(Начисления.ДокументОснование) В (&ТипыОснованийБухучетаНачислений)
	|	И НЕ Начисления.ДокументОснование В (&ПустыеЗначенияДокументОснование)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Начисления.Начисление КАК Начисление,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.ДатаНачала КАК Период
	|ПОМЕСТИТЬ ВТНачисленияБезОснования
	|ИЗ
	|	ВТНачисленияДляРаспределения КАК Начисления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НачисленияСОснованием.Начисление КАК Начисление,
	|	НачисленияСОснованием.Сотрудник КАК Сотрудник,
	|	НачисленияСОснованием.Период КАК Период,
	|	НачисленияСОснованием.ДокументОснование КАК ДокументОснование
	|ПОМЕСТИТЬ ВТНачисленияСОснованием
	|ИЗ
	|	ВТНачисленияСОснованиемПодробно КАК НачисленияСОснованием";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
	Запрос.Выполнить();
	УдалитьВТ.Добавить("ВТНачисленияСОснованием");
	УдалитьВТ.Добавить("ВТНачисленияБезОснования");
	УдалитьВТ.Добавить("ВТНачисленияСОснованиемПодробно");
	
	ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра("ВТНачисленияСОснованием", "Сотрудник,Начисление,ДокументОснование");
	СоздатьВТБухучетНачисленийСотрудников(Запрос.МенеджерВременныхТаблиц, ОписаниеФильтра,,"ВТБухучетНачисленийСотрудниковСОснованием");
	УдалитьВТ.Добавить("ВТБухучетНачисленийСотрудниковСОснованием");
	
	ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра("ВТНачисленияБезОснования", "Сотрудник,Начисление");
	СоздатьВТБухучетНачисленийСотрудников(Запрос.МенеджерВременныхТаблиц, ОписаниеФильтра,,"ВТБухучетНачисленийСотрудниковБезОснования");
	УдалитьВТ.Добавить("ВТБухучетНачисленийСотрудниковБезОснования");
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Начисления.Начисление КАК Начисление,
	|	Начисления.Сумма КАК Сумма,
	|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК ПодразделениеУчетаЗатрат,
	|	ВЫБОР
	|		КОГДА &ИспользоватьСтатьиФинансирования
	|			ТОГДА БухучетНачислений.СтатьяФинансирования
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
	|	КОНЕЦ КАК СтатьяФинансирования,
	|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
	|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачислений.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	БухучетНачислений.ДоляРаспределения КАК ДоляРаспределения
	|ИЗ
	|	ВТНачисленияДляРаспределения КАК Начисления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТБухучетНачисленийСотрудниковСОснованием КАК БухучетНачислений
	|		ПО Начисления.Сотрудник = БухучетНачислений.Сотрудник
	|			И (БухучетНачислений.Используется)
	|			И Начисления.Организация = БухучетНачислений.Организация
	|			И Начисления.Начисление = БухучетНачислений.Начисление
	|			И Начисления.ДатаНачала = БухучетНачислений.Период
	|			И Начисления.ДокументОснование = БухучетНачислений.ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Начисления.Начисление КАК Начисление,
	|	Начисления.Сумма КАК Сумма,
	|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК ПодразделениеУчетаЗатрат,
	|	ВЫБОР
	|		КОГДА &ИспользоватьСтатьиФинансирования
	|			ТОГДА БухучетНачислений.СтатьяФинансирования
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
	|	КОНЕЦ КАК СтатьяФинансирования,
	|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
	|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачислений.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	БухучетНачислений.ДоляРаспределения КАК ДоляРаспределения
	|ИЗ
	|	ВТНачисленияДляРаспределения КАК Начисления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТБухучетНачисленийСотрудниковБезОснования КАК БухучетНачислений
	|		ПО Начисления.Сотрудник = БухучетНачислений.Сотрудник
	|			И (БухучетНачислений.Используется)
	|			И (БухучетНачислений.ДокументОснование В (&ПустыеЗначенияДокументОснование))
	|			И Начисления.Организация = БухучетНачислений.Организация
	|			И Начисления.Начисление = БухучетНачислений.Начисление
	|			И Начисления.ДатаНачала = БухучетНачислений.Период";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	БухучетНачисленийСотрудников = РезультатЗапроса[0].Выгрузить();
	БухучетЛюбоеОснование = РезультатЗапроса[1].Выгрузить();
	БухучетНачисленийСотрудников.Индексы.Добавить("ИдентификаторСтроки");
	БухучетЛюбоеОснование.Индексы.Добавить("ИдентификаторСтроки");
	Отбор = Новый Структура("ИдентификаторСтроки");
	
	ОбработанныеСтроки = Новый Соответствие;
	Для каждого СтрокаТЗ Из БухучетНачисленийСотрудников Цикл
		Отбор.ИдентификаторСтроки = СтрокаТЗ.ИдентификаторСтроки;
		ОбработанныеСтроки.Вставить(СтрокаТЗ.ИдентификаторСтроки, Истина);
		НайденныеСтроки = БухучетЛюбоеОснование.НайтиСтроки(Отбор);
		ЕстьСтрокиБухучетЛюбоеОснование = НайденныеСтроки.Количество() > 0;
		Если ИспользоватьСтатьиФинансирования И Не ЗначениеЗаполнено(СтрокаТЗ.СтатьяФинансирования) И ЕстьСтрокиБухучетЛюбоеОснование Тогда
				СтрокаТЗ.СтатьяФинансирования = НайденныеСтроки[0].СтатьяФинансирования;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(СтрокаТЗ.СпособОтраженияЗарплатыВБухучете) И ЕстьСтрокиБухучетЛюбоеОснование Тогда
			СтрокаТЗ.СпособОтраженияЗарплатыВБухучете = НайденныеСтроки[0].СпособОтраженияЗарплатыВБухучете;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(СтрокаТЗ.СтатьяРасходов) И ЕстьСтрокиБухучетЛюбоеОснование Тогда
			СтрокаТЗ.СтатьяРасходов = НайденныеСтроки[0].СтатьяРасходов;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(СтрокаТЗ.ОтношениеКЕНВД) И ЕстьСтрокиБухучетЛюбоеОснование Тогда
			СтрокаТЗ.ОтношениеКЕНВД = НайденныеСтроки[0].ОтношениеКЕНВД;
		КонецЕсли;
	КонецЦикла;
	
	Для каждого СтрокаТЗ Из БухучетЛюбоеОснование Цикл
		Если ОбработанныеСтроки[СтрокаТЗ.ИдентификаторСтроки] = Неопределено Тогда
			ЗаполнитьЗначенияСвойств(БухучетНачисленийСотрудников.Добавить(), СтрокаТЗ);
		КонецЕсли;
	КонецЦикла;
	
	ЗарплатаКадры.СоздатьВТПоТаблицеЗначений(Запрос.МенеджерВременныхТаблиц, БухучетНачисленийСотрудников, "ВТНастройкиБухучетаНачисленийСотрудников");
	
	Если РаботаВБюджетномУчреждении Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиБухучета.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	НастройкиБухучета.Начисление КАК Начисление,
		|	НастройкиБухучета.Сумма КАК Сумма,
		|	НастройкиБухучета.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
		|	НастройкиБухучета.СтатьяФинансирования КАК СтатьяФинансирования,
		|	ВЫБОР
		|		КОГДА НастройкиБухучета.СтатьяРасходов = ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
		|			ТОГДА СтатьиРасходовПоУмолчанию.СтатьяРасходов
		|		ИНАЧЕ НастройкиБухучета.СтатьяРасходов
		|	КОНЕЦ КАК СтатьяРасходов,
		|	НастройкиБухучета.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	НастройкиБухучета.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
		|	НастройкиБухучета.ДоляРаспределения КАК ДоляРаспределения,
		|	ВЫБОР
		|		КОГДА НастройкиБухучета.СтатьяФинансирования = ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
		|				ИЛИ &ЕстьЕНВД
		|					И НастройкиБухучета.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)
		|				ИЛИ НастройкиБухучета.СпособОтраженияЗарплатыВБухучете = ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК НеЗаполненаАналитика
		|ПОМЕСТИТЬ ВТНастройкиНачисленийСотрудниковДляБухучета
		|ИЗ
		|	ВТНастройкиБухучетаНачисленийСотрудников КАК НастройкиБухучета
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОбработанныеСтроки КАК ОбработанныеСтроки
		|		ПО НастройкиБухучета.ИдентификаторСтроки = ОбработанныеСтроки.ИдентификаторСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатьиРасходовНачисленийБюджетПоУмолчанию КАК СтатьиРасходовПоУмолчанию
		|		ПО НастройкиБухучета.Начисление = СтатьиРасходовПоУмолчанию.Начисление
		|ГДЕ
		|	ОбработанныеСтроки.ИдентификаторСтроки ЕСТЬ NULL";
		
	Иначе
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиБухучета.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	НастройкиБухучета.Начисление КАК Начисление,
		|	НастройкиБухучета.Сумма КАК Сумма,
		|	НастройкиБухучета.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
		|	НастройкиБухучета.СтатьяФинансирования КАК СтатьяФинансирования,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка) КАК СтатьяРасходов,
		|	НастройкиБухучета.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	НастройкиБухучета.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
		|	НастройкиБухучета.ДоляРаспределения КАК ДоляРаспределения,
		|	ВЫБОР
		|		КОГДА &ИспользоватьСтатьиФинансирования
		|					И НастройкиБухучета.СтатьяФинансирования = ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
		|				ИЛИ &ЕстьЕНВД
		|					И НастройкиБухучета.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)
		|				ИЛИ НастройкиБухучета.СпособОтраженияЗарплатыВБухучете = ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК НеЗаполненаАналитика
		|ПОМЕСТИТЬ ВТНастройкиНачисленийСотрудниковДляБухучета
		|ИЗ
		|	ВТНастройкиБухучетаНачисленийСотрудников КАК НастройкиБухучета
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОбработанныеСтроки КАК ОбработанныеСтроки
		|		ПО НастройкиБухучета.ИдентификаторСтроки = ОбработанныеСтроки.ИдентификаторСтроки
		|ГДЕ
		|	ОбработанныеСтроки.ИдентификаторСтроки ЕСТЬ NULL";
		
	КонецЕсли;
	Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ВТНеДополнятьБухучетомНачисленийСотрудников
	|ИЗ
	|	ВТНастройкиНачисленийСотрудниковДляБухучета КАК Таблица
	|ГДЕ
	|	Таблица.НеЗаполненаАналитика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Таблица.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ИЗ
	|	ВТНастройкиНачисленийСотрудниковДляБухучета КАК Таблица";
	
	Если Запрос.Выполнить().Пустой() Тогда
		// Добавляем в удаляемые "основную" таблицу.
		УдалитьВТ.Добавить("ВТНастройкиНачисленийСотрудниковДляБухучета");
		ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
		Возврат Ложь;
	КонецЕсли;
	
	ОбработатьСтрокиСРаспределением(Запрос, "ВТНастройкиНачисленийСотрудниковДляБухучета");
	ДополнитьВТОбработанныеСтроки(Запрос, "ВТНастройкиНачисленийСотрудниковДляБухучета");
	ДополнитьВТСтрокиБезАналитики(Запрос, "ВТНастройкиНачисленийСотрудниковДляБухучета");

	ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
	
	Возврат Истина;

КонецФункции

Функция СоздатьВТБухучетНачисленийПоБазеСреднегоЗаработка(Запрос, ИмяВТНачисленияИсходная, ПараметрыРаспределенияПоСтатьям, ПараметрыРаспределенияБюджет)
	
	УдалитьВТ = Новый Массив;
	
	РаботаВБюджетномУчреждении = Запрос.Параметры.РаботаВБюджетномУчреждении;
	ИспользоватьСтатьиФинансирования = Запрос.Параметры.ИспользоватьСтатьиФинансирования;
	
	КоэффициентыСреднегоЗаработкаФССДокумента 	= ПараметрыРаспределенияПоСтатьям.КоэффициентыСреднегоЗаработкаФССДокумента;
	КоэффициентыСреднегоЗаработкаДокумента 		= ПараметрыРаспределенияПоСтатьям.КоэффициентыСреднегоЗаработкаДокумента;
	СтрокиКоэффициентыСреднегоЗаработка 		= ПараметрыРаспределенияПоСтатьям.СтрокиКоэффициентыСреднегоЗаработка;
	
	НачисленияРКиСН = Новый Соответствие;
	РК = ПланыВидовРасчета.Начисления.НачислениеРайонныйКоэффициент();
	Если ЗначениеЗаполнено(РК) Тогда
		НачисленияРКиСН.Вставить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаДолиРайонногоКоэффициентаВСреднемЗаработке, РК);
	КонецЕсли;
	СН = ПланыВидовРасчета.Начисления.НачислениеСевернаяНадбавка();
	Если ЗначениеЗаполнено(СН) Тогда
		НачисленияРКиСН.Вставить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаДолиСевернойНадбавкиВСреднемЗаработке, СН);
	КонецЕсли;
	
	АктуальныеСтатьиФинансирования 	= ПараметрыРаспределенияПоСтатьям.АктуальныеСтатьиФинансирования;
	АктуальныеСпособыОтражения 		= ПараметрыРаспределенияПоСтатьям.АктуальныеСпособыОтражения;
	СтатьиФинансированияЗамены 		= ПараметрыРаспределенияПоСтатьям.СтатьиФинансированияЗамены;
	НастройкиСтатейФинансирования 	= ПараметрыРаспределенияПоСтатьям.НастройкиСтатейФинансирования;
	
	СтатьиРасходовБюджетПоВидамНачислений = ПараметрыРаспределенияБюджет.СтатьиРасходовБюджетПоВидамНачислений;
	СтатьяРасходовОплатаТруда = ПараметрыРаспределенияБюджет.СтатьяРасходовОплатаТруда;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Начисления.Начисление КАК Начисление,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Начисления.ДатаНачала КАК ДатаНачала,
	|	Начисления.ДатаОкончания КАК ДатаОкончания,
	|	Начисления.Подразделение КАК ПодразделениеУчетаЗатрат,
	|	НастройкиБухучетаПоУмолчанию.СтатьяФинансирования КАК СтатьяФинансирования,
	|	НастройкиБухучетаПоУмолчанию.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	НастройкиНачислений.СпособРасчетаПоСреднемуЗаработку КАК СпособРасчетаПоСреднемуЗаработку,
	|	НастройкиНачислений.ЭтоРасходыФСС КАК ЭтоРасходыФСС,
	|	НастройкиНачислений.ИспользуетСреднийЗаработокОбщий КАК ИспользуетСреднийЗаработокОбщий,
	|	НастройкиНачислений.КатегорияНачисления КАК КатегорияНачисления,
	|	НастройкиНачислений.КатегорияНачисленияОсновнойВидРасчета КАК КатегорияНачисленияОсновнойВидРасчета,
	|	НастройкиНачислений.ОсновнойВидРасчета КАК ОсновнойВидРасчета,
	|	Начисления.Сумма КАК Сумма
	|ПОМЕСТИТЬ ВТВсеНачисленияПоБазеСреднегоЗаработка
	|ИЗ
	|	ВТНачисленияДляРаспределения КАК Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОбработанныеСтроки КАК ОбработанныеСтроки
	|		ПО Начисления.ИдентификаторСтроки = ОбработанныеСтроки.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиБухучетаВсехСтрокПоУмолчанию КАК НастройкиБухучетаПоУмолчанию
	|		ПО Начисления.ИдентификаторСтроки = НастройкиБухучетаПоУмолчанию.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиНачислений КАК НастройкиНачислений
	|		ПО Начисления.Начисление = НастройкиНачислений.Начисление
	|ГДЕ
	|	ОбработанныеСтроки.ИдентификаторСтроки ЕСТЬ NULL
	|	И НастройкиНачислений.БухучетПоБазеСреднегоЗаработка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица1.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ИЗ
	|	ВТВсеНачисленияПоБазеСреднегоЗаработка КАК Таблица1
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНачисленияДляРаспределения КАК Таблица2
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиНачислений КАК НастройкиНачислений
	|			ПО Таблица2.Начисление = НастройкиНачислений.Начисление
	|		ПО (Таблица1.Начисление = НастройкиНачислений.ОсновнойВидРасчета)
	|			И Таблица1.Сотрудник = Таблица2.Сотрудник
	|			И Таблица1.ДатаНачала = Таблица2.ДатаНачала
	|			И Таблица1.ДатаОкончания = Таблица2.ДатаОкончания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Начисления.Начисление КАК Начисление,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Начисления.ДатаНачала КАК ДатаНачала,
	|	Начисления.ДатаОкончания КАК ДатаОкончания,
	|	Начисления.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|	Начисления.СтатьяФинансирования КАК СтатьяФинансирования,
	|	Начисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	Начисления.СпособРасчетаПоСреднемуЗаработку КАК СпособРасчетаПоСреднемуЗаработку,
	|	Начисления.ЭтоРасходыФСС КАК ЭтоРасходыФСС,
	|	Начисления.ИспользуетСреднийЗаработокОбщий КАК ИспользуетСреднийЗаработокОбщий,
	|	Начисления.КатегорияНачисления КАК КатегорияНачисления,
	|	Начисления.КатегорияНачисленияОсновнойВидРасчета КАК КатегорияНачисленияОсновнойВидРасчета,
	|	Начисления.ОсновнойВидРасчета КАК ОсновнойВидРасчета,
	|	Начисления.Сумма КАК Сумма
	|ИЗ
	|	ВТВсеНачисленияПоБазеСреднегоЗаработка КАК Начисления";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	УдалитьВТ.Добавить("ВТВсеНачисленияПоБазеСреднегоЗаработка");
	
	КоличествоЗапросов = РезультатЗапроса.ВГраница();
	
	Если РезультатЗапроса[КоличествоЗапросов].Пустой() Тогда
		ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
		Возврат Ложь;
	КонецЕсли;
	
	НачисленияУточненияБазы = РезультатЗапроса[КоличествоЗапросов-1].Выгрузить();
	СтрокиУточненияБазы = Новый Соответствие;
	Для каждого СтрокаТЗ Из НачисленияУточненияБазы Цикл
		СтрокиУточненияБазы.Вставить(СтрокаТЗ.ИдентификаторСтроки, Истина);
	КонецЦикла;
	
	ЕстьЕНВД = Запрос.Параметры.ЕстьЕНВД;
	ТаблицаБухучет = НоваяТаблицаБухучетНачисленийПромежуточная();
	
	НачисленияПоБазе = РезультатЗапроса[КоличествоЗапросов].Выгрузить();
	
	Для каждого СтрокаНачисления Из НачисленияПоБазе Цикл
		
		СтрокиОтражения = СтрокиКоэффициентыСреднегоЗаработка[СтрокаНачисления.ИдентификаторСтроки];
		Если СтрокиОтражения = Неопределено Тогда
			// получим коэффициенты для всего документа
			Если СтрокаНачисления.ИспользуетСреднийЗаработокОбщий Тогда
				КоэффициентыСреднегоЗаработкаПоСотруднику = КоэффициентыСреднегоЗаработкаДокумента[СтрокаНачисления.Сотрудник];
				Если КоэффициентыСреднегоЗаработкаПоСотруднику = Неопределено Тогда
					СтрокиОтражения = КоэффициентыСреднегоЗаработкаДокумента[СтрокаНачисления.СпособРасчетаПоСреднемуЗаработку];
				Иначе
					СтрокиОтражения = КоэффициентыСреднегоЗаработкаПоСотруднику[СтрокаНачисления.СпособРасчетаПоСреднемуЗаработку];
				КонецЕсли;
			Иначе
				СтрокиОтражения = КоэффициентыСреднегоЗаработкаФССДокумента;
			КонецЕсли;
		КонецЕсли;
		
		Если СтрокиОтражения <> Неопределено И (СтрокиУточненияБазы[СтрокаНачисления.ИдентификаторСтроки] = Истина 
			Или ЗначениеЗаполнено(СтрокаНачисления.КатегорияНачисленияОсновнойВидРасчета)) Тогда
			СтрокиОтражения = БазаСреднегоЗаработкаПоКатегорииНачисления(НачисленияРКиСН, СтрокаНачисления.КатегорияНачисления, СтрокиОтражения);
		КонецЕсли;
		
		База = Новый Массив;
		Если Не ЗначениеЗаполнено(СтрокиОтражения) Тогда
			Результаты = Неопределено;
		Иначе
			
			База = СтрокиОтражения.Скопировать();
			Если ИспользоватьСтатьиФинансирования Тогда
				СтрокиКУдалению = Новый Массив;
				Для Каждого СтрокаБазы Из База Цикл
					Если ЗначениеЗаполнено(СтрокаБазы.СтатьяФинансирования) Тогда
						НастройкиСтатьи = НастройкиСтатейФинансирования[СтрокаБазы.СтатьяФинансирования];
						Если НастройкиСтатьи.Действует Тогда
							Продолжить;
						Иначе
							Используется = НастройкиСтатьи.ИспользуетсяВБазеСреднего Или НастройкиСтатьи.КатегорииНачислений[СтрокаНачисления.КатегорияНачисления] = Истина;
							Если Используется И НастройкиСтатьи.УстаревшаяСтатьяИсключаетсяИзБазы Тогда
								СтрокиКУдалению.Добавить(СтрокаБазы);
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
				Для каждого СтрокаБазы Из СтрокиКУдалению Цикл
					База.Удалить(СтрокаБазы);
				КонецЦикла;
			КонецЕсли;
			
			Если База.Количество() = 0 Тогда
				Результаты = Неопределено;
			Иначе
				Коэффициенты = База.ВыгрузитьКолонку("Коэффициент");
				Результаты = ЗарплатаКадрыКлиентСервер.РаспределитьПропорциональноКоэффициентам(СтрокаНачисления.Сумма, Коэффициенты);
			КонецЕсли;
			
		КонецЕсли;
		
		Если Результаты = Неопределено Тогда
			НоваяСтрока = ТаблицаБухучет.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНачисления);
			Если РаботаВБюджетномУчреждении Тогда
				НоваяСтрока.СтатьяРасходов = СтатьяРасходовБюджетНачисления(СтрокаНачисления.Начисление, ПараметрыРаспределенияБюджет);
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		
		Индекс = 0;
		Для Каждого СтрокаБазы Из База Цикл
			
			НоваяСтрока = ТаблицаБухучет.Добавить();
			// Заполняем все поля, в т.ч. бухучетом по умолчанию.
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНачисления);
			НоваяСтрока.Сумма = Результаты[Индекс];
			
			Если ЕстьЕНВД Тогда
				НоваяСтрока.ОблагаетсяЕНВД = СтрокаБазы.ОблагаетсяЕНВД;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтрокаБазы.СтатьяФинансирования) Тогда
				НастройкиСтатьи = НастройкиСтатейФинансирования[СтрокаБазы.СтатьяФинансирования];
				Если НастройкиСтатьи.ИспользуетсяВБазеСреднего Тогда
					НоваяСтрока.СтатьяФинансирования = АктуальныеСтатьиФинансирования[СтрокаБазы.СтатьяФинансирования];
				Иначе
					Если НастройкиСтатьи.КатегорииНачислений[СтрокаНачисления.КатегорияНачисления] = Истина Тогда
						НоваяСтрока.СтатьяФинансирования = АктуальныеСтатьиФинансирования[СтрокаБазы.СтатьяФинансирования];
					Иначе
						НоваяСтрока.СтатьяФинансирования = СтатьиФинансированияЗамены[СтрокаБазы.СтатьяФинансирования];
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			// Заполнение поля СпособОтраженияЗарплатыВБухучете.
			Если Не СтрокаНачисления.ЭтоРасходыФСС Тогда
				Если ЗначениеЗаполнено(СтрокаБазы.СпособОтраженияЗарплатыВБухучете) Тогда
					НоваяСтрока.СпособОтраженияЗарплатыВБухучете = АктуальныеСпособыОтражения[СтрокаБазы.СпособОтраженияЗарплатыВБухучете];
				КонецЕсли;
			КонецЕсли;
			
			Если РаботаВБюджетномУчреждении Тогда
				
				НоваяСтатьяРасходов = СтатьиРасходовБюджетПоВидамНачислений[СтрокаНачисления.Начисление];
				Если Не ЗначениеЗаполнено(НоваяСтатьяРасходов) Тогда
					НоваяСтатьяРасходов = СтрокаБазы.СтатьяРасходов;
				КонецЕсли;
				Если Не ЗначениеЗаполнено(НоваяСтатьяРасходов) Тогда
					НоваяСтатьяРасходов = СтатьяРасходовОплатаТруда;
				КонецЕсли;
				НоваяСтрока.СтатьяРасходов = НоваяСтатьяРасходов;
				
			КонецЕсли;
			
			Индекс = Индекс + 1;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ЗарплатаКадры.СоздатьВТПоТаблицеЗначений(Запрос.МенеджерВременныхТаблиц, ТаблицаБухучет, "ВТБухучетНачисленийПоБазеСреднегоЗаработка");
	
	// Зарегистрируем обработанные идентификаторы строк.
	ДополнитьВТОбработанныеСтроки(Запрос, "ВТБухучетНачисленийПоБазеСреднегоЗаработка");
	
	ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
	
	Возврат Истина;
	
КонецФункции

Функция СоздатьВТБухучетНачисленийПоФактическимНачислениям(Запрос, ИмяВТНачисленияИсходная, ПараметрыРаспределенияПоСтатьям, ПараметрыРаспределенияБюджет)
	
	Если Месяц(Запрос.Параметры.ПериодРегистрации) = 1 Тогда
		// Для первого месяца нет смысла получать фактические начисления.
		Возврат Ложь;
	КонецЕсли;
	
	УдалитьВТ = Новый Массив;
	
	РаботаВБюджетномУчреждении = Запрос.Параметры.РаботаВБюджетномУчреждении;
	ИспользоватьСтатьиФинансирования = Запрос.Параметры.ИспользоватьСтатьиФинансирования;
	
	АктуальныеСтатьиФинансирования 	= ПараметрыРаспределенияПоСтатьям.АктуальныеСтатьиФинансирования;
	АктуальныеСпособыОтражения 		= ПараметрыРаспределенияПоСтатьям.АктуальныеСпособыОтражения;
	СтатьиФинансированияЗамены 		= ПараметрыРаспределенияПоСтатьям.СтатьиФинансированияЗамены;
	НастройкиСтатейФинансирования 	= ПараметрыРаспределенияПоСтатьям.НастройкиСтатейФинансирования;
	
	СтатьиРасходовБюджетПоВидамНачислений = ПараметрыРаспределенияБюджет.СтатьиРасходовБюджетПоВидамНачислений;
	СтатьяРасходовОплатаТруда = ПараметрыРаспределенияБюджет.СтатьяРасходовОплатаТруда;
	
	НачисленияРКиСН = Новый Соответствие;
	РК = ПланыВидовРасчета.Начисления.НачислениеРайонныйКоэффициент();
	Если ЗначениеЗаполнено(РК) Тогда
		НачисленияРКиСН.Вставить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаДолиРайонногоКоэффициентаВСреднемЗаработке, РК);
	КонецЕсли;
	СН = ПланыВидовРасчета.Начисления.НачислениеСевернаяНадбавка();
	Если ЗначениеЗаполнено(СН) Тогда
		НачисленияРКиСН.Вставить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаДолиСевернойНадбавкиВСреднемЗаработке, СН);
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Начисления.Начисление КАК Начисление,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Начисления.ДатаНачала КАК ДатаНачала,
	|	Начисления.ДатаОкончания КАК ДатаОкончания,
	|	Начисления.Подразделение КАК ПодразделениеУчетаЗатрат,
	|	НастройкиБухучетаПоУмолчанию.СтатьяФинансирования КАК СтатьяФинансирования,
	|	НастройкиБухучетаПоУмолчанию.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	НастройкиБухучетаПоУмолчанию.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	НастройкиНачислений.ЭтоРасходыФСС КАК ЭтоРасходыФСС,
	|	НастройкиНачислений.КатегорияНачисления КАК КатегорияНачисления,
	|	НастройкиНачислений.КатегорияНачисленияОсновнойВидРасчета КАК КатегорияНачисленияОсновнойВидРасчета,
	|	НастройкиНачислений.ОсновнойВидРасчета КАК ОсновнойВидРасчета,
	|	Начисления.Сумма КАК Сумма
	|ПОМЕСТИТЬ ВТВсеНачисленияПоФактическимНачислениям
	|ИЗ
	|	ВТНачисленияДляРаспределения КАК Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОбработанныеСтроки КАК ОбработанныеСтроки
	|		ПО Начисления.ИдентификаторСтроки = ОбработанныеСтроки.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиБухучетаВсехСтрокПоУмолчанию КАК НастройкиБухучетаПоУмолчанию
	|		ПО Начисления.ИдентификаторСтроки = НастройкиБухучетаПоУмолчанию.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиНачислений КАК НастройкиНачислений
	|		ПО Начисления.Начисление = НастройкиНачислений.Начисление
	|ГДЕ
	|	ОбработанныеСтроки.ИдентификаторСтроки ЕСТЬ NULL
	|	И НастройкиНачислений.БухучетПоФактическимНачислениям
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица1.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ИЗ
	|	ВТВсеНачисленияПоФактическимНачислениям КАК Таблица1
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНачисленияДляРаспределения КАК Таблица2
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиНачислений КАК НастройкиНачислений
	|			ПО Таблица2.Начисление = НастройкиНачислений.Начисление
	|		ПО (Таблица1.Начисление = НастройкиНачислений.ОсновнойВидРасчета)
	|			И Таблица1.Сотрудник = Таблица2.Сотрудник
	|			И Таблица1.ДатаНачала = Таблица2.ДатаНачала
	|			И Таблица1.ДатаОкончания = Таблица2.ДатаОкончания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Начисления.Начисление КАК Начисление,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Начисления.ДатаНачала КАК ДатаНачала,
	|	Начисления.ДатаОкончания КАК ДатаОкончания,
	|	Начисления.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|	Начисления.СтатьяФинансирования КАК СтатьяФинансирования,
	|	Начисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	Начисления.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	Начисления.ЭтоРасходыФСС КАК ЭтоРасходыФСС,
	|	Начисления.КатегорияНачисления КАК КатегорияНачисления,
	|	Начисления.КатегорияНачисленияОсновнойВидРасчета КАК КатегорияНачисленияОсновнойВидРасчета,
	|	Начисления.Сумма КАК Сумма
	|ИЗ
	|	ВТВсеНачисленияПоФактическимНачислениям КАК Начисления";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	УдалитьВТ.Добавить("ВТВсеНачисленияПоФактическимНачислениям");
	
	КоличествоЗапросов = РезультатЗапроса.ВГраница();
	
	Если РезультатЗапроса[КоличествоЗапросов].Пустой() Тогда
		ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
		Возврат Ложь;
	КонецЕсли;
	
	ЕстьЕНВД = Запрос.Параметры.ЕстьЕНВД;
	ПериодРегистрации =  Запрос.Параметры.ПериодРегистрации;
	
	НачисленияУточненияБазы = РезультатЗапроса[КоличествоЗапросов-1].Выгрузить();
	СтрокиУточненияБазы = Новый Соответствие;
	Для каждого СтрокаТЗ Из НачисленияУточненияБазы Цикл
		СтрокиУточненияБазы.Вставить(СтрокаТЗ.ИдентификаторСтроки, Истина);
	КонецЦикла;

	НачисленияПоБазе = РезультатЗапроса[КоличествоЗапросов].Выгрузить();
	ТаблицаБухучет = НоваяТаблицаБухучетНачисленийПромежуточная();
	
	НачПериода = НачалоГода(ПериодРегистрации);
	КонПериода = КонецМесяца(ДобавитьМесяц(ПериодРегистрации, -1));
	ФизическиеЛицаДляОтбора = ОбщегоНазначения.ВыгрузитьКолонку(НачисленияПоБазе, "ФизическоеЛицо", Истина);
	
	Запрос.УстановитьПараметр("ФизическиеЛицаДляОтбора",ФизическиеЛицаДляОтбора);
	Запрос.УстановитьПараметр("НачалоПериодаФактическихНачислений", НачПериода);
	Запрос.УстановитьПараметр("КонецПериодаФактическихНачислений", КонПериода);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	БухучетНачисления.ФизическоеЛицо КАК ФизическоеЛицо,
	|	БухучетНачисления.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетНачисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	БухучетНачисления.НачислениеУдержание КАК Начисление,
	|	СУММА(БухучетНачисления.Сумма) КАК Сумма
	|ИЗ
	|	РегистрНакопления.БухучетНачисленияУдержанияПоСотрудникам КАК БухучетНачисления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНастройкиНачислений КАК НастройкиНачислений
	|		ПО БухучетНачисления.НачислениеУдержание = НастройкиНачислений.Начисление
	|			И (НастройкиНачислений.ВходитВБазуФСС)
	|ГДЕ
	|	БухучетНачисления.Период МЕЖДУ &НачалоПериодаФактическихНачислений И &КонецПериодаФактическихНачислений
	|	И БухучетНачисления.Организация = &Организация
	|	И БухучетНачисления.ФизическоеЛицо В(&ФизическиеЛицаДляОтбора)
	|	И БухучетНачисления.Регистратор <> &ИсключаемыйРегистратор
	|
	|СГРУППИРОВАТЬ ПО
	|	БухучетНачисления.СтатьяФинансирования,
	|	БухучетНачисления.СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачисления.ОблагаетсяЕНВД,
	|	БухучетНачисления.ФизическоеЛицо,
	|	БухучетНачисления.НачислениеУдержание
	|
	|ИМЕЮЩИЕ
	|	СУММА(БухучетНачисления.Сумма) > 0";
	
	БазаДляРасходовРаботодателя = Запрос.Выполнить().Выгрузить();
	БазаДляРасходовФСС = БазаДляРасходовРаботодателя.Скопировать();
	БазаДляРасходовФСС.Свернуть("ФизическоеЛицо,Начисление,СтатьяФинансирования,ОблагаетсяЕНВД","Сумма");
	
	БазаДляРасходовРаботодателя.Индексы.Добавить("ФизическоеЛицо");
	БазаДляРасходовФСС.Индексы.Добавить("ФизическоеЛицо");
	Отбор = Новый Структура("ФизическоеЛицо");
	
	Для каждого СтрокаНачисления Из НачисленияПоБазе Цикл
		
		Если СтрокаНачисления.ЭтоРасходыФСС Тогда
			БазаРаспределения = БазаДляРасходовФСС;
		Иначе
			БазаРаспределения = БазаДляРасходовРаботодателя;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(Отбор, СтрокаНачисления);
		СтрокиОтражения = БазаРаспределения.Скопировать(Отбор);
		Если ЗначениеЗаполнено(СтрокиОтражения) И (СтрокиУточненияБазы[СтрокаНачисления.ИдентификаторСтроки] = Истина 
			Или ЗначениеЗаполнено(СтрокаНачисления.КатегорияНачисленияОсновнойВидРасчета)) Тогда
			СтрокиОтражения = БазаФактическихНачисленийПоКатегорииНачисления(НачисленияРКиСН, СтрокаНачисления.КатегорияНачисления, СтрокиОтражения);
		КонецЕсли;
		
		Если СтрокиОтражения.Количество() = 0 Тогда
			НоваяСтрока = ТаблицаБухучет.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНачисления);
			Если РаботаВБюджетномУчреждении Тогда
				НоваяСтрока.СтатьяРасходов = СтатьяРасходовБюджетНачисления(СтрокаНачисления.Начисление, ПараметрыРаспределенияБюджет);
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		
		База = СтрокиОтражения.Скопировать();
		Если ИспользоватьСтатьиФинансирования Тогда
			СтрокиКУдалению = Новый Массив;
			Для Каждого СтрокаБазы Из База Цикл
				Если ЗначениеЗаполнено(СтрокаБазы.СтатьяФинансирования) Тогда
					НастройкиСтатьи = НастройкиСтатейФинансирования[СтрокаБазы.СтатьяФинансирования];
					Если НастройкиСтатьи.Действует Тогда
						Продолжить;
					Иначе
						Используется = НастройкиСтатьи.ИспользуетсяВБазеСреднего Или НастройкиСтатьи.КатегорииНачислений[СтрокаНачисления.КатегорияНачисления] = Истина;
						Если Используется И НастройкиСтатьи.УстаревшаяСтатьяИсключаетсяИзБазы Тогда
							СтрокиКУдалению.Добавить(СтрокаБазы);
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			Для каждого СтрокаБазы Из СтрокиКУдалению Цикл
				База.Удалить(СтрокаБазы);
			КонецЦикла;
		КонецЕсли;
		
		Если База.Количество() = 0 Тогда
			Результаты = Неопределено;
		Иначе
			Коэффициенты = База.ВыгрузитьКолонку("Сумма");
			Результаты = ЗарплатаКадрыКлиентСервер.РаспределитьПропорциональноКоэффициентам(СтрокаНачисления.Сумма, Коэффициенты);
		КонецЕсли;
		
		Если Результаты = Неопределено Тогда
			НоваяСтрока = ТаблицаБухучет.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНачисления);
			Если РаботаВБюджетномУчреждении Тогда
				НоваяСтрока.СтатьяРасходов = СтатьяРасходовБюджетНачисления(СтрокаНачисления.Начисление, ПараметрыРаспределенияБюджет);
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		
		Индекс = 0;
		Для Каждого СтрокаБазы Из База Цикл
		
			НоваяСтрока = ТаблицаБухучет.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНачисления);
			НоваяСтрока.Сумма = Результаты[Индекс];
			Если ЕстьЕНВД Тогда
				НоваяСтрока.ОблагаетсяЕНВД = СтрокаБазы.ОблагаетсяЕНВД;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтрокаБазы.СтатьяФинансирования) Тогда
				НастройкиСтатьи = НастройкиСтатейФинансирования[СтрокаБазы.СтатьяФинансирования];
				Если НастройкиСтатьи.ИспользуетсяВБазеСреднего Тогда
					НоваяСтрока.СтатьяФинансирования = АктуальныеСтатьиФинансирования[СтрокаБазы.СтатьяФинансирования];
				Иначе
					Если НастройкиСтатьи.КатегорииНачислений[СтрокаНачисления.КатегорияНачисления] = Истина Тогда
						НоваяСтрока.СтатьяФинансирования = АктуальныеСтатьиФинансирования[СтрокаБазы.СтатьяФинансирования];
					Иначе
						НоваяСтрока.СтатьяФинансирования = СтатьиФинансированияЗамены[СтрокаБазы.СтатьяФинансирования];
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			Если Не СтрокаНачисления.ЭтоРасходыФСС Тогда
				Если ЗначениеЗаполнено(СтрокаБазы.СпособОтраженияЗарплатыВБухучете) Тогда
					НоваяСтрока.СпособОтраженияЗарплатыВБухучете = АктуальныеСпособыОтражения[СтрокаБазы.СпособОтраженияЗарплатыВБухучете];
				КонецЕсли;
			КонецЕсли;
			
			Если РаботаВБюджетномУчреждении Тогда
				НоваяСтрока.СтатьяРасходов = СтатьяРасходовБюджетНачисления(СтрокаНачисления.Начисление, ПараметрыРаспределенияБюджет);
			КонецЕсли;
			
			Индекс = Индекс + 1;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ЗарплатаКадры.СоздатьВТПоТаблицеЗначений(Запрос.МенеджерВременныхТаблиц, ТаблицаБухучет, "ВТБухучетНачисленийПоФактическимНачислениям");
	
	// Зарегистрируем обработанные идентификаторы строк.
	ДополнитьВТОбработанныеСтроки(Запрос, "ВТБухучетНачисленийПоФактическимНачислениям");
	
	ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
	
	Возврат Истина;
	
КонецФункции

Функция СоздатьВТБухучетНачисленийПоБазеСохраняемогоДС(Запрос, ИмяВТНачисленияИсходная, ПараметрыСохраняемогоДенежногоСодержания, ПараметрыРаспределенияБюджет)
	
	УдалитьВТ = Новый Массив;
	
	КоэффициентыРаспределенияДенежногоСодержания = ПараметрыСохраняемогоДенежногоСодержания.КоэффициентыРаспределенияДенежногоСодержания;
	СтрокиКоэффициентыСохраняемогоДС = ПараметрыСохраняемогоДенежногоСодержания.СтрокиКоэффициентыСохраняемогоДС;
	
	НачисленияРКиСН = Новый Соответствие;
	РК = ПланыВидовРасчета.Начисления.НачислениеРайонныйКоэффициент();
	Если ЗначениеЗаполнено(РК) Тогда
		НачисленияРКиСН.Вставить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаДолиРайонногоКоэффициентаВСДС, РК);
	КонецЕсли;
	СН = ПланыВидовРасчета.Начисления.НачислениеСевернаяНадбавка();
	Если ЗначениеЗаполнено(СН) Тогда
		НачисленияРКиСН.Вставить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаДолиСевернойНадбавкиВСДС, СН);
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Начисления.Начисление КАК Начисление,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Начисления.ДатаНачала КАК ДатаНачала,
	|	Начисления.ДатаОкончания КАК ДатаОкончания,
	|	Начисления.Подразделение КАК ПодразделениеУчетаЗатрат,
	|	НастройкиБухучетаПоУмолчанию.СтатьяФинансирования КАК СтатьяФинансирования,
	|	НастройкиБухучетаПоУмолчанию.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	НастройкиБухучетаПоУмолчанию.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	НастройкиНачислений.ЭтоРасходыФСС КАК ЭтоРасходыФСС,
	|	НастройкиНачислений.НазначениеРасчетаСохраняемогоДенежногоСодержания КАК НазначениеРасчетаСохраняемогоДенежногоСодержания,
	|	НастройкиНачислений.КатегорияНачисления КАК КатегорияНачисления,
	|	НастройкиНачислений.КатегорияНачисленияОсновнойВидРасчета КАК КатегорияНачисленияОсновнойВидРасчета,
	|	НастройкиНачислений.ОсновнойВидРасчета КАК ОсновнойВидРасчета,
	|	Начисления.Сумма КАК Сумма
	|ПОМЕСТИТЬ ВТВсеНачисленияПоБазеДС
	|ИЗ
	|	ВТНачисленияДляРаспределения КАК Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОбработанныеСтроки КАК ОбработанныеСтроки
	|		ПО Начисления.ИдентификаторСтроки = ОбработанныеСтроки.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиБухучетаВсехСтрокПоУмолчанию КАК НастройкиБухучетаПоУмолчанию
	|		ПО Начисления.ИдентификаторСтроки = НастройкиБухучетаПоУмолчанию.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиНачислений КАК НастройкиНачислений
	|		ПО Начисления.Начисление = НастройкиНачислений.Начисление
	|ГДЕ
	|	ОбработанныеСтроки.ИдентификаторСтроки ЕСТЬ NULL
	|	И НастройкиНачислений.НазначениеРасчетаСохраняемогоДенежногоСодержания <> НЕОПРЕДЕЛЕНО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица1.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ИЗ
	|	ВТВсеНачисленияПоБазеДС КАК Таблица1
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНачисленияДляРаспределения КАК Таблица2
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиНачислений КАК НастройкиНачислений
	|			ПО Таблица2.Начисление = НастройкиНачислений.Начисление
	|		ПО (Таблица1.Начисление = НастройкиНачислений.ОсновнойВидРасчета)
	|			И Таблица1.Сотрудник = Таблица2.Сотрудник
	|			И Таблица1.ДатаНачала = Таблица2.ДатаНачала
	|			И Таблица1.ДатаОкончания = Таблица2.ДатаОкончания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Начисления.Начисление КАК Начисление,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Начисления.ДатаНачала КАК ДатаНачала,
	|	Начисления.ДатаОкончания КАК ДатаОкончания,
	|	Начисления.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|	Начисления.СтатьяФинансирования КАК СтатьяФинансирования,
	|	Начисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	Начисления.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	Начисления.ЭтоРасходыФСС КАК ЭтоРасходыФСС,
	|	Начисления.НазначениеРасчетаСохраняемогоДенежногоСодержания КАК НазначениеРасчетаСохраняемогоДенежногоСодержания,
	|	Начисления.КатегорияНачисления КАК КатегорияНачисления,
	|	Начисления.КатегорияНачисленияОсновнойВидРасчета КАК КатегорияНачисленияОсновнойВидРасчета,
	|	Начисления.Сумма КАК Сумма
	|ИЗ
	|	ВТВсеНачисленияПоБазеДС КАК Начисления";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	УдалитьВТ.Добавить("ВТВсеНачисленияПоБазеДС");
	
	КоличествоЗапросов = РезультатЗапроса.ВГраница();
	
	Если РезультатЗапроса[КоличествоЗапросов].Пустой() Тогда
		ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
		Возврат Ложь;
	КонецЕсли;
	
	ЕстьЕНВД = Запрос.Параметры.ЕстьЕНВД;
	РаботаВБюджетномУчреждении = Запрос.Параметры.РаботаВБюджетномУчреждении;
	
	ТаблицаБухучет = НоваяТаблицаБухучетНачисленийПромежуточная();
	
	НачисленияУточненияБазы = РезультатЗапроса[КоличествоЗапросов-1].Выгрузить();
	СтрокиУточненияБазы = Новый Соответствие;
	Для каждого СтрокаТЗ Из НачисленияУточненияБазы Цикл
		СтрокиУточненияБазы.Вставить(СтрокаТЗ.ИдентификаторСтроки, Истина);
	КонецЦикла;
	
	НачисленияПоБазе = РезультатЗапроса[КоличествоЗапросов].Выгрузить();
	Отбор = Новый Структура("НазначениеРасчета,Сотрудник");
	
	Для каждого СтрокаНачисления Из НачисленияПоБазе Цикл
		
		Отбор.НазначениеРасчета = СтрокаНачисления.НазначениеРасчетаСохраняемогоДенежногоСодержания;
		Отбор.Сотрудник 		= СтрокаНачисления.Сотрудник;
		
		СтрокиОтражения = СтрокиКоэффициентыСохраняемогоДС[СтрокаНачисления.ИдентификаторСтроки];
		Если СтрокиОтражения = Неопределено Тогда
			// получим коэффициенты для всего документа
			СтрокиОтражения = КоэффициентыРаспределенияДенежногоСодержания;
		КонецЕсли;
		
		Если СтрокиОтражения <> Неопределено И (СтрокиУточненияБазы[СтрокаНачисления.ИдентификаторСтроки] = Истина
			Или ЗначениеЗаполнено(СтрокаНачисления.КатегорияНачисленияОсновнойВидРасчета)) Тогда
			СтрокиОтражения = БазаСохраняемогоДенежногоСодержанияПоКатегорииНачисления(НачисленияРКиСН, СтрокаНачисления.КатегорияНачисления, СтрокиОтражения);
		КонецЕсли;
		
		Если СтрокиОтражения = Неопределено Тогда
			Результаты = Неопределено;
		Иначе
			
			ИспользуемыеСтрокиОтражения = СтрокиОтражения.Скопировать(Отбор);
			Если ИспользуемыеСтрокиОтражения.Количество() > 0 Тогда
				Коэффициенты = ИспользуемыеСтрокиОтражения.ВыгрузитьКолонку("Коэффициент");
				Результаты = ЗарплатаКадрыКлиентСервер.РаспределитьПропорциональноКоэффициентам(СтрокаНачисления.Сумма, Коэффициенты);
			Иначе
				Результаты = Неопределено;
			КонецЕсли;
			
		КонецЕсли;
		
		Если Результаты = Неопределено Тогда
			НоваяСтрока = ТаблицаБухучет.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНачисления);
			Если РаботаВБюджетномУчреждении Тогда
				НоваяСтрока.СтатьяРасходов = СтатьяРасходовБюджетНачисления(СтрокаНачисления.Начисление, ПараметрыРаспределенияБюджет);
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		
		Индекс = 0;
		Для Каждого СтрокаОтражения Из ИспользуемыеСтрокиОтражения Цикл
			
			НоваяСтрока = ТаблицаБухучет.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНачисления);
			НоваяСтрока.Сумма = Результаты[Индекс];
			Если ЕстьЕНВД Тогда
				НоваяСтрока.ОблагаетсяЕНВД = СтрокаОтражения.ОблагаетсяЕНВД;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтрокаОтражения.СтатьяФинансирования) Тогда
				НоваяСтрока.СтатьяФинансирования = СтрокаОтражения.СтатьяФинансирования;
			КонецЕсли;
			
			Если Не СтрокаНачисления.ЭтоРасходыФСС Тогда
				Если ЗначениеЗаполнено(СтрокаОтражения.СпособОтраженияЗарплатыВБухучете) Тогда
					НоваяСтрока.СпособОтраженияЗарплатыВБухучете = СтрокаОтражения.СпособОтраженияЗарплатыВБухучете;
				КонецЕсли;
			КонецЕсли;
			
			Если РаботаВБюджетномУчреждении Тогда
				НоваяСтрока.СтатьяРасходов = СтатьяРасходовБюджетНачисления(СтрокаНачисления.Начисление, ПараметрыРаспределенияБюджет);
			КонецЕсли;
			
			Индекс = Индекс + 1;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ЗарплатаКадры.СоздатьВТПоТаблицеЗначений(Запрос.МенеджерВременныхТаблиц, ТаблицаБухучет, "ВТБухучетНачисленийПоБазеСохраняемогоДС");
	
	// Зарегистрируем обработанные идентификаторы строк.
	ДополнитьВТОбработанныеСтроки(Запрос, "ВТБухучетНачисленийПоБазеСохраняемогоДС");
	
	ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
	
	Возврат Истина;
	
КонецФункции

Функция СоздатьВТНастройкиИзНачисленийДляБухучета(Запрос, ИмяВТНачисленияИсходная)
	
	РаботаВБюджетномУчреждении = Запрос.Параметры.РаботаВБюджетномУчреждении;
	
	УдалитьВТ = Новый Массив;
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СписокНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	СписокНачислений.Начисление КАК Начисление,
	|	СписокНачислений.Сумма КАК Сумма,
	|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК ПодразделениеУчетаЗатрат,
	|	НастройкиНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
	|	НастройкиНачислений.СтатьяРасходов КАК СтатьяРасходов,
	|	НастройкиНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	НастройкиНачислений.ОтношениеКЕНВД КАК ОтношениеКЕНВД
	|ПОМЕСТИТЬ ВТНастройкиБухучетаНачислений
	|ИЗ
	|	ВТНачисленияДляРаспределения КАК СписокНачислений
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНастройкиНачислений КАК НастройкиНачислений
	|		ПО СписокНачислений.Начисление = НастройкиНачислений.Начисление
	|			И (НастройкиНачислений.БухучетУказанВНачислении)";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
	Запрос.Выполнить();
	
	Если РаботаВБюджетномУчреждении Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиБухучета.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	НастройкиБухучета.Начисление КАК Начисление,
		|	НастройкиБухучета.Сумма КАК Сумма,
		|	НастройкиБухучета.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
		|	НастройкиБухучета.СтатьяФинансирования КАК СтатьяФинансирования,
		|	ВЫБОР
		|		КОГДА НастройкиБухучета.СтатьяРасходов = ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
		|			ТОГДА СтатьиРасходовПоУмолчанию.СтатьяРасходов
		|		ИНАЧЕ НастройкиБухучета.СтатьяРасходов
		|	КОНЕЦ КАК СтатьяРасходов,
		|	НастройкиБухучета.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	НастройкиБухучета.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
		|	ВЫБОР
		|		КОГДА НастройкиБухучета.СтатьяФинансирования = ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
		|				ИЛИ &ЕстьЕНВД
		|					И НастройкиБухучета.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)
		|				ИЛИ НастройкиБухучета.СпособОтраженияЗарплатыВБухучете = ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК НеЗаполненаАналитика
		|ПОМЕСТИТЬ ВТНастройкиИзНачисленийДляБухучета
		|ИЗ
		|	ВТНастройкиБухучетаНачислений КАК НастройкиБухучета
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОбработанныеСтроки КАК ОбработанныеСтроки
		|		ПО НастройкиБухучета.ИдентификаторСтроки = ОбработанныеСтроки.ИдентификаторСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатьиРасходовНачисленийБюджетПоУмолчанию КАК СтатьиРасходовПоУмолчанию
		|		ПО НастройкиБухучета.Начисление = СтатьиРасходовПоУмолчанию.Начисление
		|ГДЕ
		|	ОбработанныеСтроки.ИдентификаторСтроки ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	Таблица.ИдентификаторСтроки КАК ИдентификаторСтроки
		|ИЗ
		|	ВТНастройкиИзНачисленийДляБухучета КАК Таблица";
		
	Иначе
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиБухучета.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	НастройкиБухучета.Начисление КАК Начисление,
		|	НастройкиБухучета.Сумма КАК Сумма,
		|	НастройкиБухучета.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
		|	НастройкиБухучета.СтатьяФинансирования КАК СтатьяФинансирования,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка) КАК СтатьяРасходов,
		|	НастройкиБухучета.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	НастройкиБухучета.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
		|	ВЫБОР
		|		КОГДА &ИспользоватьСтатьиФинансирования
		|					И НастройкиБухучета.СтатьяФинансирования = ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
		|				ИЛИ &ЕстьЕНВД
		|					И НастройкиБухучета.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)
		|				ИЛИ НастройкиБухучета.СпособОтраженияЗарплатыВБухучете = ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК НеЗаполненаАналитика
		|ПОМЕСТИТЬ ВТНастройкиИзНачисленийДляБухучета
		|ИЗ
		|	ВТНастройкиБухучетаНачислений КАК НастройкиБухучета
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОбработанныеСтроки КАК ОбработанныеСтроки
		|		ПО НастройкиБухучета.ИдентификаторСтроки = ОбработанныеСтроки.ИдентификаторСтроки
		|ГДЕ
		|	ОбработанныеСтроки.ИдентификаторСтроки ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	Таблица.ИдентификаторСтроки КАК ИдентификаторСтроки
		|ИЗ
		|	ВТНастройкиИзНачисленийДляБухучета КАК Таблица";
		
	КонецЕсли;
	
	Если Запрос.Выполнить().Пустой() Тогда
		// Добавляем в удаляемые "основную" таблицу.
		УдалитьВТ.Добавить("ВТНастройкиИзНачисленийДляБухучета");
		ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
		Возврат Ложь;
	КонецЕсли;
	
	ДополнитьВТОбработанныеСтроки(Запрос, "ВТНастройкиИзНачисленийДляБухучета");
	ДополнитьВТСтрокиБезАналитики(Запрос, "ВТНастройкиИзНачисленийДляБухучета");
	
	Возврат Истина;
	
КонецФункции

Функция СоздатьВТНачисленияПоБазовымРасчетам(Запрос, ИмяВТНачисленияИсходная)
	
	УдалитьВТ = Новый Массив;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СписокНачислений.Организация КАК Организация,
	|	СписокНачислений.ПериодРегистрации КАК ПериодРегистрации,
	|	СписокНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	СписокНачислений.Сотрудник КАК Сотрудник,
	|	СписокНачислений.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СписокНачислений.Начисление КАК Начисление,
	|	СписокНачислений.ДатаНачала КАК ДатаНачала,
	|	СписокНачислений.ДатаОкончания КАК ДатаОкончания,
	|	СписокНачислений.Сумма КАК Сумма,
	|	СписокНачислений.Сторно КАК Сторно,
	|	СписокНачислений.ФиксСторно КАК ФиксСторно,
	|	СписокНачислений.РассчитыватьПоРазовымНачислениямДокумента КАК РассчитыватьПоРазовымНачислениямДокумента,
	|	Начисления.ОчередностьОтраженияВУчете КАК ОчередностьОтраженияВУчете
	|ПОМЕСТИТЬ ВТНачисленияПоБазовымРасчетам
	|ИЗ
	|	ВТНачисленияДляРаспределения КАК СписокНачислений
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНастройкиНачислений КАК Начисления
	|		ПО (Начисления.БухучетПоБазовымРасчетам)
	|			И СписокНачислений.Начисление = Начисления.Начисление
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОбработанныеСтроки КАК ОбработанныеСтроки
	|		ПО СписокНачислений.ИдентификаторСтроки = ОбработанныеСтроки.ИдентификаторСтроки
	|ГДЕ
	|	ОбработанныеСтроки.ИдентификаторСтроки ЕСТЬ NULL
	|	И СписокНачислений.Сумма <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Таблица.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ИЗ
	|	ВТНачисленияПоБазовымРасчетам КАК Таблица";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
	НетНастройки = Запрос.Выполнить().Пустой();
	Если НетНастройки Тогда
		// Добавляем в удаляемые "основную" таблицу.
		УдалитьВТ.Добавить("ВТНачисленияПоБазовымРасчетам");
		ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
		Возврат Ложь;
	КонецЕсли;
	
	ДополнитьВТОбработанныеСтроки(Запрос, "ВТНачисленияПоБазовымРасчетам");
	
	Возврат Истина;
	
КонецФункции

Функция СоздатьВТНачисленияПоНастройкамБухучетаДляБухучета(Запрос, ИмяВТНачисленияИсходная, ПараметрыРаспределенияБюджет)
	
	// Обработка всех оставшихся строк.
	
	РаботаВБюджетномУчреждении = Запрос.Параметры.РаботаВБюджетномУчреждении;
	
	УдалитьВТ = Новый Массив;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СписокНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	СписокНачислений.Начисление КАК Начисление,
	|	СписокНачислений.Сумма КАК Сумма,
	|	НастройкиБухучета.СтатьяФинансирования КАК СтатьяФинансирования,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка) КАК СтатьяРасходов,
	|	НастройкиБухучета.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	НастройкиБухучета.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	НастройкиБухучета.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|	НастройкиБухучета.ДоляРаспределения КАК ДоляРаспределения,
	|	НастройкиБухучета.СтрокаСРаспределением КАК СтрокаСРаспределением
	|ПОМЕСТИТЬ ВТСтрокиКОбработке
	|ИЗ
	|	ВТНачисленияДляРаспределения КАК СписокНачислений
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОбработанныеСтроки КАК ОбработанныеСтроки
	|		ПО СписокНачислений.ИдентификаторСтроки = ОбработанныеСтроки.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиБухучетаВсехСтрок КАК НастройкиБухучета
	|		ПО СписокНачислений.ИдентификаторСтроки = НастройкиБухучета.ИдентификаторСтроки
	|ГДЕ
	|	ОбработанныеСтроки.ИдентификаторСтроки ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтрокиКОбработке.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	СтрокиКОбработке.Начисление КАК Начисление,
	|	СтрокиКОбработке.Сумма КАК Сумма,
	|	СтрокиКОбработке.СтатьяФинансирования КАК СтатьяФинансирования,
	|	СтрокиКОбработке.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	СтрокиКОбработке.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	СтрокиКОбработке.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|	СтрокиКОбработке.ДоляРаспределения КАК ДоляРаспределения,
	|	СтрокиКОбработке.СтрокаСРаспределением КАК СтрокаСРаспределением
	|ИЗ
	|	ВТСтрокиКОбработке КАК СтрокиКОбработке
	|ГДЕ
	|	СтрокиКОбработке.СтрокаСРаспределением
	|	И СтрокиКОбработке.Сумма <> 0";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
	ТаблицаСтрокСРаспределением = Запрос.Выполнить().Выгрузить();
	УдалитьВТ.Добавить("ВТСтрокиКОбработке");
	
	Если ТаблицаСтрокСРаспределением.Количество()>0 Тогда
		
		ТаблицаДляБухучета = НоваяТаблицаНастройкиНачисленийДляБухучета();
		
		ИдентификаторыСтрок = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаСтрокСРаспределением, "ИдентификаторСтроки", Истина);
		ТаблицаСтрокСРаспределением.Индексы.Добавить("ИдентификаторСтроки");
		Отбор = Новый Структура("ИдентификаторСтроки");
		
		Для каждого ИдентификаторСтроки Из ИдентификаторыСтрок Цикл
			
			Отбор.ИдентификаторСтроки = ИдентификаторСтроки;
			НайденныеСтроки = ТаблицаСтрокСРаспределением.НайтиСтроки(Отбор);
			
			Коэффициенты = ОбщегоНазначения.ВыгрузитьКолонку(НайденныеСтроки, "ДоляРаспределения");
			Результаты = ЗарплатаКадрыКлиентСервер.РаспределитьПропорциональноКоэффициентам(НайденныеСтроки[0].Сумма, Коэффициенты);
			
			Если Результаты = Неопределено Тогда
				
				НоваяСтрока = ТаблицаДляБухучета.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, НайденныеСтроки[0]);
				Если РаботаВБюджетномУчреждении Тогда
					НоваяСтрока.СтатьяРасходов = СтатьяРасходовБюджетНачисления(НайденныеСтроки[0].Начисление, ПараметрыРаспределенияБюджет);
				КонецЕсли;
				
			Иначе
				
				Индекс = 0;
				Для каждого СтрокаТЗ Из НайденныеСтроки Цикл
					
					НоваяСтрока = ТаблицаДляБухучета.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
					НоваяСтрока.Сумма = Результаты[Индекс];
					
					Если РаботаВБюджетномУчреждении Тогда
						НоваяСтрока.СтатьяРасходов = СтатьяРасходовБюджетНачисления(СтрокаТЗ.Начисление, ПараметрыРаспределенияБюджет);
					КонецЕсли;
					
					Индекс = Индекс + 1;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если РаботаВБюджетномУчреждении Тогда
			
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	СтрокиКОбработке.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	СтрокиКОбработке.Начисление КАК Начисление,
			|	СтрокиКОбработке.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
			|	СтрокиКОбработке.СтатьяФинансирования КАК СтатьяФинансирования,
			|	СтрокиКОбработке.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
			|	СтрокиКОбработке.СтатьяРасходов КАК СтатьяРасходов,
			|	СтрокиКОбработке.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
			|	СтрокиКОбработке.Сумма КАК Сумма,
			|	ЛОЖЬ КАК НеЗаполненаАналитика
			|ИЗ
			|	ВТСтрокиКОбработке КАК СтрокиКОбработке
			|ГДЕ
			|	(НЕ СтрокиКОбработке.СтрокаСРаспределением
			|			ИЛИ СтрокиКОбработке.Сумма = 0)";
			Таблица = Запрос.Выполнить().Выгрузить();
			Для каждого СтрокаТЗ Из Таблица Цикл
				СтрокаТЗ.СтатьяРасходов = СтатьяРасходовБюджетНачисления(СтрокаТЗ.Начисление, ПараметрыРаспределенияБюджет);
			КонецЦикла;
			
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(Таблица,ТаблицаДляБухучета);
			ЗарплатаКадры.СоздатьВТПоТаблицеЗначений(Запрос.МенеджерВременныхТаблиц, ТаблицаДляБухучета, "ВТНачисленияПоНастройкамБухучетаДляБухучета");
			
		Иначе
			
			ЗарплатаКадры.СоздатьВТПоТаблицеЗначений(Запрос.МенеджерВременныхТаблиц, ТаблицаДляБухучета, "ВТНачисленияПоНастройкамСРаспределением");
			УдалитьВТ.Добавить("ВТНачисленияПоНастройкамСРаспределением");
			
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	СтрокиКОбработке.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	СтрокиКОбработке.Начисление КАК Начисление,
			|	СтрокиКОбработке.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
			|	СтрокиКОбработке.СтатьяФинансирования КАК СтатьяФинансирования,
			|	СтрокиКОбработке.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
			|	СтрокиКОбработке.СтатьяРасходов КАК СтатьяРасходов,
			|	СтрокиКОбработке.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
			|	СтрокиКОбработке.Сумма КАК Сумма,
			|	ЛОЖЬ КАК НеЗаполненаАналитика
			|ПОМЕСТИТЬ ВТНачисленияПоНастройкамБухучетаДляБухучета
			|ИЗ
			|	ВТСтрокиКОбработке КАК СтрокиКОбработке
			|ГДЕ
			|	(НЕ СтрокиКОбработке.СтрокаСРаспределением
			|			ИЛИ СтрокиКОбработке.Сумма = 0)
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	СтрокиКОбработке.ИдентификаторСтроки,
			|	СтрокиКОбработке.Начисление,
			|	СтрокиКОбработке.ПодразделениеУчетаЗатрат,
			|	СтрокиКОбработке.СтатьяФинансирования,
			|	СтрокиКОбработке.СпособОтраженияЗарплатыВБухучете,
			|	СтрокиКОбработке.СтатьяРасходов,
			|	СтрокиКОбработке.ОтношениеКЕНВД,
			|	СтрокиКОбработке.Сумма,
			|	ЛОЖЬ
			|ИЗ
			|	ВТНачисленияПоНастройкамСРаспределением КАК СтрокиКОбработке";
			Запрос.Выполнить();
			
		КонецЕсли;
		
	Иначе
		
		Если РаботаВБюджетномУчреждении Тогда
			
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	НастройкиБухучета.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	НастройкиБухучета.Начисление КАК Начисление,
			|	НастройкиБухучета.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
			|	НастройкиБухучета.СтатьяФинансирования КАК СтатьяФинансирования,
			|	НастройкиБухучета.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
			|	ВЫБОР
			|		КОГДА НастройкиБухучета.СтатьяРасходов = ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
			|			ТОГДА СтатьиРасходовПоУмолчанию.СтатьяРасходов
			|		ИНАЧЕ НастройкиБухучета.СтатьяРасходов
			|	КОНЕЦ КАК СтатьяРасходов,
			|	НастройкиБухучета.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
			|	НастройкиБухучета.Сумма КАК Сумма,
			|	ЛОЖЬ КАК НеЗаполненаАналитика
			|ПОМЕСТИТЬ ВТНачисленияПоНастройкамБухучетаДляБухучета
			|ИЗ
			|	ВТСтрокиКОбработке КАК НастройкиБухучета
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатьиРасходовНачисленийБюджетПоУмолчанию КАК СтатьиРасходовПоУмолчанию
			|		ПО НастройкиБухучета.Начисление = СтатьиРасходовПоУмолчанию.Начисление";
			Запрос.Выполнить();
			
		Иначе
			
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	НастройкиБухучета.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	НастройкиБухучета.Начисление КАК Начисление,
			|	НастройкиБухучета.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
			|	НастройкиБухучета.СтатьяФинансирования КАК СтатьяФинансирования,
			|	НастройкиБухучета.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
			|	НастройкиБухучета.СтатьяРасходов КАК СтатьяРасходов,
			|	НастройкиБухучета.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
			|	НастройкиБухучета.Сумма КАК Сумма,
			|	ЛОЖЬ КАК НеЗаполненаАналитика
			|ПОМЕСТИТЬ ВТНачисленияПоНастройкамБухучетаДляБухучета
			|ИЗ
			|	ВТСтрокиКОбработке КАК НастройкиБухучета";
			Запрос.Выполнить();
			
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Таблица.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ИЗ
	|	ВТНачисленияПоНастройкамБухучетаДляБухучета КАК Таблица";
	НетНастройки = Запрос.Выполнить().Пустой();
	Если НетНастройки Тогда
		// Добавляем в удаляемые "основную" таблицу.
		УдалитьВТ.Добавить("ВТНачисленияПоНастройкамБухучетаДляБухучета");
		ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
		Возврат Ложь;
	КонецЕсли;
	
	ДополнитьВТОбработанныеСтроки(Запрос, "ВТНачисленияПоНастройкамБухучетаДляБухучета");
	
	ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
	
	Возврат Истина;

КонецФункции

Процедура СоздатьВТБухучетНачисленийПоТаблицамДляБухучета(Запрос, ИмяВТНачисленияИсходная, ИменаТаблиц)

	УдалитьВТ = Новый Массив;
	
	ПрименяетсяЕНВД = Запрос.Параметры.ЕстьЕНВД;
	ТребуетсяДополнитьАналитику = ЗарплатаКадры.ВТСодержитСтроки(Запрос.МенеджерВременныхТаблиц, "ВТСтрокиБезАналитики");
	
	// Объединение в одну таблицу данных из таблиц с постфиксом "ДляБухучета".
	ШаблонТекстаЗапроса = 
	"ВЫБРАТЬ
	|	Таблица.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Таблица.Начисление КАК Начисление,
	|	Таблица.Сумма КАК Сумма,
	|	Таблица.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|	Таблица.СтатьяФинансирования КАК СтатьяФинансирования,
	|	Таблица.СтатьяРасходов КАК СтатьяРасходов,
	|	Таблица.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	Таблица.ОтношениеКЕНВД КАК ОтношениеКЕНВД
	|ИЗ
	|	#ИмяТаблицы КАК Таблица";
	
	ТекстВложенногоЗапроса = "";
	Для Индекс = 0 По ИменаТаблиц.ВГраница() Цикл
		ИмяТаблицы = ИменаТаблиц[Индекс];
		Если Индекс > 0 Тогда
			ТекстВложенногоЗапроса = ТекстВложенногоЗапроса + " 
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|";
		КонецЕсли;
		ФрагментТекстаЗапроса = СтрЗаменить(ШаблонТекстаЗапроса, "#ИмяТаблицы", ИмяТаблицы);
		ТекстВложенногоЗапроса = ТекстВложенногоЗапроса + ФрагментТекстаЗапроса;
	КонецЦикла;
	ТекстВложенногоЗапроса = "("+ТекстВложенногоЗапроса+")";
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СписокНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	СписокНачислений.Сотрудник КАК Сотрудник,
	|	СписокНачислений.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СписокНачислений.Начисление КАК Начисление,
	|	СписокНачислений.ДатаНачала КАК ДатаНачала,
	|	СписокНачислений.ДатаОкончания КАК ДатаОкончания,
	|	НастройкиБухучета.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|	НастройкиБухучета.СтатьяФинансирования КАК СтатьяФинансирования,
	|	НастройкиБухучета.СтатьяРасходов КАК СтатьяРасходов,
	|	НастройкиБухучета.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	НастройкиБухучета.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	НастройкиБухучета.Сумма КАК Сумма
	|ПОМЕСТИТЬ ВТСтрокиКОбработке
	|ИЗ
	|	ВТНачисленияДляРаспределения КАК СписокНачислений
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаНастройкиДляБухучета КАК НастройкиБухучета
	|		ПО СписокНачислений.ИдентификаторСтроки = НастройкиБухучета.ИдентификаторСтроки";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ТаблицаНастройкиДляБухучета", ТекстВложенногоЗапроса);
	Если Не ПрименяетсяЕНВД И Не ТребуетсяДополнитьАналитику Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТСтрокиКОбработке", "ВТБухучетНачисленийПоТаблицамДляБухучета");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "НастройкиБухучета.ОтношениеКЕНВД КАК ОтношениеКЕНВД", "ЛОЖЬ КАК ОблагаетсяЕНВД");
		Запрос.Выполнить();
		УдалитьВТ.Добавить("ВТСтрокиКОбработке");
		Возврат;
	Иначе
		Запрос.Выполнить();
		УдалитьВТ.Добавить("ВТСтрокиКОбработке");
	КонецЕсли;
	
	Если ТребуетсяДополнитьАналитику Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Таблица.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	Таблица.Сотрудник КАК Сотрудник,
		|	Таблица.ФизическоеЛицо КАК ФизическоеЛицо,
		|	Таблица.Начисление КАК Начисление,
		|	Таблица.ДатаНачала КАК ДатаНачала,
		|	Таблица.ДатаОкончания КАК ДатаОкончания,
		|	Таблица.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
		|	Таблица.Сумма КАК Сумма,
		|	Таблица.СтатьяФинансирования КАК СтатьяФинансирования,
		|	Таблица.СтатьяРасходов КАК СтатьяРасходов,
		|	Таблица.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	Таблица.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
		|	НеДополнять.ИдентификаторСтроки ЕСТЬ NULL КАК ДополнятьБухучетомНачисленийСотрудников
		|ИЗ
		|	ВТСтрокиКОбработке КАК Таблица
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСтрокиБезАналитики КАК СтрокиБезАналитики
		|		ПО Таблица.ИдентификаторСтроки = СтрокиБезАналитики.ИдентификаторСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНеДополнятьБухучетомНачисленийСотрудников КАК НеДополнять
		|		ПО Таблица.ИдентификаторСтроки = НеДополнять.ИдентификаторСтроки";
		ТаблицаНеЗаполненаАналитика = Запрос.Выполнить().Выгрузить();
		
		ДополнитьАналитику(Запрос, ТаблицаНеЗаполненаАналитика);
		
		ЗарплатаКадры.СоздатьВТПоТаблицеЗначений(Запрос.МенеджерВременныхТаблиц, ТаблицаНеЗаполненаАналитика, "ВТТаблицаДополненнаяАналитикой");
		УдалитьВТ.Добавить("ВТТаблицаДополненнаяАналитикой");
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиБухучета.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	НастройкиБухучета.Сотрудник КАК Сотрудник,
		|	НастройкиБухучета.ФизическоеЛицо КАК ФизическоеЛицо,
		|	НастройкиБухучета.Начисление КАК Начисление,
		|	НастройкиБухучета.ДатаНачала КАК ДатаНачала,
		|	НастройкиБухучета.ДатаОкончания КАК ДатаОкончания,
		|	НастройкиБухучета.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
		|	НастройкиБухучета.СтатьяФинансирования КАК СтатьяФинансирования,
		|	НастройкиБухучета.СтатьяРасходов КАК СтатьяРасходов,
		|	НастройкиБухучета.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	НастройкиБухучета.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
		|	ЛОЖЬ КАК ОблагаетсяЕНВД,
		|	НастройкиБухучета.Сумма КАК Сумма
		|ПОМЕСТИТЬ ВТСтрокиКОбработкеЕНВД
		|ИЗ
		|	ВТСтрокиКОбработке КАК НастройкиБухучета
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтрокиБезАналитики КАК СтрокиБезАналитики
		|		ПО НастройкиБухучета.ИдентификаторСтроки = СтрокиБезАналитики.ИдентификаторСтроки
		|ГДЕ
		|	СтрокиБезАналитики.ИдентификаторСтроки ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	НастройкиДляБухучета.ИдентификаторСтроки,
		|	НастройкиДляБухучета.Сотрудник,
		|	НастройкиДляБухучета.ФизическоеЛицо,
		|	НастройкиДляБухучета.Начисление,
		|	НастройкиДляБухучета.ДатаНачала,
		|	НастройкиДляБухучета.ДатаОкончания,
		|	НастройкиДляБухучета.ПодразделениеУчетаЗатрат,
		|	НастройкиДляБухучета.СтатьяФинансирования,
		|	НастройкиДляБухучета.СтатьяРасходов,
		|	НастройкиДляБухучета.СпособОтраженияЗарплатыВБухучете,
		|	НастройкиДляБухучета.ОтношениеКЕНВД,
		|	ЛОЖЬ,
		|	НастройкиДляБухучета.Сумма
		|ИЗ
		|	ВТТаблицаДополненнаяАналитикой КАК НастройкиДляБухучета";
		
		Если Не ПрименяетсяЕНВД Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТСтрокиКОбработкеЕНВД", "ВТБухучетНачисленийПоТаблицамДляБухучета");
			Запрос.Выполнить();
			ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
			Возврат;
		Иначе
			Запрос.Выполнить();
			УдалитьВТ.Добавить("ВТСтрокиКОбработкеЕНВД");
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПрименяетсяЕНВД Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СтрокиКОбработке.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	СтрокиКОбработке.Сотрудник КАК Сотрудник,
		|	СтрокиКОбработке.ФизическоеЛицо КАК ФизическоеЛицо,
		|	СтрокиКОбработке.Начисление КАК Начисление,
		|	СтрокиКОбработке.ДатаНачала КАК ДатаНачала,
		|	СтрокиКОбработке.ДатаОкончания КАК ДатаОкончания,
		|	СтрокиКОбработке.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
		|	СтрокиКОбработке.СтатьяФинансирования КАК СтатьяФинансирования,
		|	СтрокиКОбработке.СтатьяРасходов КАК СтатьяРасходов,
		|	СтрокиКОбработке.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	ВЫБОР
		|		КОГДА СтрокиКОбработке.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ОпределяетсяЕжемесячноПроцентом)
		|				И &РассчитыватьДолюЕНВД
		|			ТОГДА ВЫБОР
		|					КОГДА ОтношениеКЕНВДЗатратНаЗарплату.Ссылка = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ЕНВД)
		|						ТОГДА ВЫРАЗИТЬ(СтрокиКОбработке.Сумма * &ПроцентЕНВД / 100 КАК ЧИСЛО(15, 2))
		|					ИНАЧЕ СтрокиКОбработке.Сумма - (ВЫРАЗИТЬ(СтрокиКОбработке.Сумма * &ПроцентЕНВД / 100 КАК ЧИСЛО(15, 2)))
		|				КОНЕЦ
		|		ИНАЧЕ СтрокиКОбработке.Сумма
		|	КОНЕЦ КАК Сумма,
		|	ВЫБОР
		|		КОГДА ВЫБОР
		|				КОГДА СтрокиКОбработке.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ОпределяетсяЕжемесячноПроцентом)
		|					ТОГДА ВЫБОР
		|							КОГДА &РассчитыватьДолюЕНВД
		|								ТОГДА ОтношениеКЕНВДЗатратНаЗарплату.Ссылка
		|							КОГДА &ПроцентЕНВД = 0
		|								ТОГДА ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.НеЕНВД)
		|							ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ЕНВД)
		|						КОНЕЦ
		|				ИНАЧЕ СтрокиКОбработке.ОтношениеКЕНВД
		|			КОНЕЦ = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ЕНВД)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ОблагаетсяЕНВД
		|ПОМЕСТИТЬ ВТБухучетНачисленийПоТаблицамДляБухучета
		|ИЗ
		|	ВТСтрокиКОбработке КАК СтрокиКОбработке
		|		ЛЕВОЕ СОЕДИНЕНИЕ Перечисление.ОтношениеКЕНВДЗатратНаЗарплату КАК ОтношениеКЕНВДЗатратНаЗарплату
		|		ПО (ОтношениеКЕНВДЗатратНаЗарплату.Ссылка В (ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ЕНВД), ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.НеЕНВД)))
		|			И (СтрокиКОбработке.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ОпределяетсяЕжемесячноПроцентом))
		|			И (&РассчитыватьДолюЕНВД)";
		
		Если ТребуетсяДополнитьАналитику Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТСтрокиКОбработке", "ВТСтрокиКОбработкеЕНВД");
			УдалитьВТ.Добавить("ВТСтрокиКОбработкеЕНВД");
		Иначе
			УдалитьВТ.Добавить("ВТСтрокиКОбработке");
		КонецЕсли;
		Запрос.Выполнить();
		
		ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьВТБухучетНачисленийПоБазовымРасчетам(Запрос, ИмяВТНачисленияИсходная, МенеджерКадровогоУчета, МенеджерДанныхУчетаВремени, ПараметрыРаспределенияБюджет)
	
	УдалитьВТ = Новый Массив;
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.ОчередностьОтраженияВУчете КАК ОчередностьОтраженияВУчете
	|ИЗ
	|	ВТНачисленияПоБазовымРасчетам КАК Таблица
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОчередностьОтраженияВУчете";
	РезультатОчередностьРаспределенияПоБазе = Запрос.Выполнить();
	
	РаботаВБюджетномУчреждении = Запрос.Параметры.РаботаВБюджетномУчреждении;
	ИспользоватьСтатьиФинансирования = Запрос.Параметры.ИспользоватьСтатьиФинансирования;
	ПрименяетсяЕНВД = Запрос.Параметры.ЕстьЕНВД;
	
	ЗарплатаКадры.СоздатьПустуюВТ(Запрос.МенеджерВременныхТаблиц, "РегистрРасчета.Начисления");
	
	// ВТНачисленияПоБазеИзРегистраРасчета
	Если Не ИспользоватьСтатьиФинансирования Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Начисления.Регистратор КАК Регистратор,
		|	Начисления.НомерСтроки КАК НомерСтроки,
		|	ЛОЖЬ КАК РассчитыватьПоРазовымНачислениямДокумента,
		|	НачисленияПоБазе.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	НачисленияПоБазе.Сотрудник КАК Сотрудник,
		|	НачисленияПоБазе.ФизическоеЛицо КАК ФизическоеЛицо,
		|	НачисленияПоБазе.Начисление КАК ВидРасчета,
		|	НачисленияПоБазе.ДатаНачала КАК ДатаНачала,
		|	НачисленияПоБазе.ДатаОкончания КАК ДатаОкончания,
		|	НачисленияПоБазе.ОчередностьОтраженияВУчете КАК ОчередностьОтраженияВУчете
		|ПОМЕСТИТЬ ВТНачисленияПоБазеИзРегистраРасчета
		|ИЗ
		|	ВТНачисленияПоБазовымРасчетам КАК НачисленияПоБазе
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрРасчета.Начисления КАК Начисления
		|		ПО НачисленияПоБазе.ПериодРегистрации = Начисления.ПериодРегистрации
		|			И НачисленияПоБазе.Организация = Начисления.Организация
		|			И НачисленияПоБазе.Сотрудник = Начисления.Сотрудник
		|			И НачисленияПоБазе.Начисление = Начисления.ВидРасчета
		|			И НачисленияПоБазе.ДатаНачала = Начисления.ПериодДействияНачало
		|			И (НачисленияПоБазе.ДатаОкончания = НАЧАЛОПЕРИОДА(Начисления.ПериодДействияКонец, ДЕНЬ))";
		
	Иначе
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Начисления.Регистратор КАК Регистратор,
		|	Начисления.НомерСтроки КАК НомерСтроки,
		|	Начисления.РассчитыватьПоРазовымНачислениямДокумента КАК РассчитыватьПоРазовымНачислениямДокумента,
		|	НачисленияПоБазе.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	НачисленияПоБазе.Сотрудник КАК Сотрудник,
		|	НачисленияПоБазе.ФизическоеЛицо КАК ФизическоеЛицо,
		|	НачисленияПоБазе.Начисление КАК ВидРасчета,
		|	НачисленияПоБазе.ДатаНачала КАК ДатаНачала,
		|	НачисленияПоБазе.ДатаОкончания КАК ДатаОкончания,
		|	НачисленияПоБазе.ОчередностьОтраженияВУчете КАК ОчередностьОтраженияВУчете
		|ПОМЕСТИТЬ ВТНачисленияПоБазеИзРегистраРасчета
		|ИЗ
		|	ВТНачисленияПоБазовымРасчетам КАК НачисленияПоБазе
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачисленияДляРаспределения КАК НачисленияДляРаспределения
		|		ПО НачисленияПоБазе.ИдентификаторСтроки = НачисленияДляРаспределения.ИдентификаторСтроки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРегистрРасчета_Начисления КАК Начисления
		|		ПО НачисленияДляРаспределения.ИдентификаторСтрокиНачисления = Начисления.ИдентификаторСтроки";
		
	КонецЕсли;
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
	Запрос.Выполнить();
	УдалитьВТ.Добавить("ВТНачисленияПоБазеИзРегистраРасчета");
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтборНачислений.Регистратор,
	|	ОтборНачислений.НомерСтроки
	|ПОМЕСТИТЬ ВТОтборНачислений
	|ИЗ
	|	ВТНачисленияПоБазеИзРегистраРасчета КАК ОтборНачислений";
	Запрос.Выполнить();
	УдалитьВТ.Добавить("ВТОтборНачислений");
	
	РасчетЗарплатыРасширенный.СоздатьВТРасчетнаяБазаНачисленийПоВременнойТаблицеКаскадно(Запрос.МенеджерВременныхТаблиц, Запрос.Параметры.ИсключаемыйРегистратор, МенеджерКадровогоУчета, МенеджерДанныхУчетаВремени);
	УдалитьВТ.Добавить("ВТРасчетнаяБаза");
	
	// ВТБазовыеНачисления
	// ИдентификаторСтроки - строка таблицы, которая распределяется по базе,
	// ОчередностьОтраженияВУчете - очередность строки, распределяемой по базе,
	// РегистраторБаза и НомерСтрокиБаза - ключ, определяющий базовую строку,
	// остальные поля - свойства базового начисления из регистра расчетов.
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасчетнаяБаза.Регистратор КАК Регистратор,
	|	РасчетнаяБаза.НомерСтроки КАК НомерСтроки,
	|	РасчетнаяБаза.РезультатБаза КАК РезультатБаза,
	|	РасчетнаяБаза.ВидРасчетаРазрез КАК ВидРасчетаРазрез,
	|	РасчетнаяБаза.ПериодДействияНачалоРазрез КАК ПериодДействияНачалоРазрез,
	|	РасчетнаяБаза.ПериодДействияКонецРазрез КАК ПериодДействияКонецРазрез,
	|	РасчетнаяБаза.РегистраторРазрез КАК РегистраторРазрез,
	|	РасчетнаяБаза.ИдентификаторСтрокиРазрез КАК ИдентификаторСтрокиРазрез,
	|	РасчетнаяБаза.НомерСтрокиРазрез КАК НомерСтрокиРазрез,
	|	Начисления.Сторно КАК Сторно
	|ПОМЕСТИТЬ ВТРасчетнаяБазаСПериодом
	|ИЗ
	|	ВТРасчетнаяБаза КАК РасчетнаяБаза
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрРасчета.Начисления КАК Начисления
	|		ПО РасчетнаяБаза.РегистраторРазрез = Начисления.Регистратор
	|			И РасчетнаяБаза.НомерСтрокиРазрез = Начисления.НомерСтроки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасчетнаяБаза.Регистратор,
	|	РасчетнаяБаза.НомерСтроки,
	|	РасчетнаяБаза.РезультатБаза,
	|	РасчетнаяБаза.ВидРасчетаРазрез,
	|	РасчетнаяБаза.ПериодДействияНачалоРазрез,
	|	РасчетнаяБаза.ПериодДействияКонецРазрез,
	|	РасчетнаяБаза.РегистраторРазрез,
	|	РасчетнаяБаза.ИдентификаторСтрокиРазрез,
	|	РасчетнаяБаза.НомерСтрокиРазрез,
	|	Начисления.Сторно
	|ИЗ
	|	ВТРасчетнаяБаза КАК РасчетнаяБаза
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРегистрРасчета_Начисления КАК Начисления
	|		ПО РасчетнаяБаза.РегистраторРазрез = Начисления.Регистратор
	|			И РасчетнаяБаза.НомерСтрокиРазрез = Начисления.НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачисленияПоБазе.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	НачисленияПоБазе.ОчередностьОтраженияВУчете КАК ОчередностьОтраженияВУчете,
	|	НачисленияПоБазе.Сотрудник КАК СотрудникБаза,
	|	НачисленияПоБазе.ФизическоеЛицо КАК ФизическоеЛицоБаза,
	|	НачисленияПоБазе.РассчитыватьПоРазовымНачислениямДокумента КАК РассчитыватьПоРазовымНачислениямДокумента,
	|	СУММА(База.РезультатБаза) КАК РезультатБаза,
	|	База.ВидРасчетаРазрез КАК НачислениеБаза,
	|	НАЧАЛОПЕРИОДА(База.ПериодДействияНачалоРазрез, ДЕНЬ) КАК ДатаНачалаБаза,
	|	НАЧАЛОПЕРИОДА(База.ПериодДействияКонецРазрез, ДЕНЬ) КАК ДатаОкончанияБаза,
	|	База.РегистраторРазрез КАК РегистраторБаза,
	|	База.ИдентификаторСтрокиРазрез КАК ИдентификаторСтрокиРазрез,
	|	База.НомерСтрокиРазрез КАК НомерСтрокиБаза,
	|	База.Сторно КАК Сторно,
	|	ВЫБОР
	|		КОГДА База.РегистраторРазрез = База.Регистратор
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК БазоваяЗаписьТекущегоРегистратора
	|ПОМЕСТИТЬ ВТБазовыеНачисления
	|ИЗ
	|	ВТНачисленияПоБазеИзРегистраРасчета КАК НачисленияПоБазе
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРасчетнаяБазаСПериодом КАК База
	|		ПО НачисленияПоБазе.Регистратор = База.Регистратор
	|			И НачисленияПоБазе.НомерСтроки = База.НомерСтроки
	|
	|СГРУППИРОВАТЬ ПО
	|	НачисленияПоБазе.ИдентификаторСтроки,
	|	НачисленияПоБазе.Сотрудник,
	|	НачисленияПоБазе.ФизическоеЛицо,
	|	НачисленияПоБазе.РассчитыватьПоРазовымНачислениямДокумента,
	|	База.ВидРасчетаРазрез,
	|	НАЧАЛОПЕРИОДА(База.ПериодДействияНачалоРазрез, ДЕНЬ),
	|	НАЧАЛОПЕРИОДА(База.ПериодДействияКонецРазрез, ДЕНЬ),
	|	База.РегистраторРазрез,
	|	База.ИдентификаторСтрокиРазрез,
	|	База.НомерСтрокиРазрез,
	|	НачисленияПоБазе.ОчередностьОтраженияВУчете,
	|	База.Сторно,
	|	ВЫБОР
	|		КОГДА База.РегистраторРазрез = База.Регистратор
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|
	|ИМЕЮЩИЕ
	|	СУММА(База.РезультатБаза) <> 0";
	Запрос.Выполнить();
	УдалитьВТ.Добавить("ВТРасчетнаяБазаСПериодом");
	УдалитьВТ.Добавить("ВТБазовыеНачисления");
	
	// ВТБухучетНачисленийВременная
	Если НЕ ИспользоватьСтатьиФинансирования Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	БухучетНачисления.Сотрудник КАК Сотрудник,
		|	БухучетНачисления.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ВЫБОР
		|		КОГДА БухучетНачисления.ПодразделениеУчетаЗатрат = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
		|			ТОГДА НачисленияДляРаспределения.Подразделение
		|		ИНАЧЕ БухучетНачисления.ПодразделениеУчетаЗатрат
		|	КОНЕЦ КАК ПодразделениеУчетаЗатрат,
		|	БухучетНачисления.Начисление КАК Начисление,
		|	БухучетНачисления.ДатаНачала КАК ДатаНачала,
		|	БухучетНачисления.ДатаОкончания КАК ДатаОкончания,
		|	БухучетНачисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	БухучетНачисления.СтатьяФинансирования КАК СтатьяФинансирования,
		|	БухучетНачисления.СтатьяРасходов КАК СтатьяРасходов,
		|	БухучетНачисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	НачисленияДляРаспределения.Сторно КАК Сторно,
		|	СУММА(БухучетНачисления.Сумма) КАК Сумма,
		|	НачисленияДляРаспределения.ИдентификаторСтрокиНачисления КАК ИдентификаторСтрокиНачисления,
		|	НачисленияДляРаспределения.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ ВТБухучетНачисленийВременная
		|ИЗ
		|	ВТБухучетНачисленийБезРаспределяемыхПоБазе КАК БухучетНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачисленияДляРаспределения КАК НачисленияДляРаспределения
		|		ПО БухучетНачисления.ИдентификаторСтроки = НачисленияДляРаспределения.ИдентификаторСтроки
		|
		|СГРУППИРОВАТЬ ПО
		|	БухучетНачисления.СпособОтраженияЗарплатыВБухучете,
		|	БухучетНачисления.ОблагаетсяЕНВД,
		|	БухучетНачисления.СтатьяРасходов,
		|	БухучетНачисления.СтатьяФинансирования,
		|	БухучетНачисления.Сотрудник,
		|	БухучетНачисления.ФизическоеЛицо,
		|	ВЫБОР
		|		КОГДА БухучетНачисления.ПодразделениеУчетаЗатрат = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
		|			ТОГДА НачисленияДляРаспределения.Подразделение
		|		ИНАЧЕ БухучетНачисления.ПодразделениеУчетаЗатрат
		|	КОНЕЦ,
		|	БухучетНачисления.Начисление,
		|	БухучетНачисления.ДатаНачала,
		|	БухучетНачисления.ДатаОкончания,
		|	НачисленияДляРаспределения.Сторно,
		|	НачисленияДляРаспределения.ИдентификаторСтрокиНачисления,
		|	НачисленияДляРаспределения.Регистратор
		|
		|ИМЕЮЩИЕ
		|	СУММА(БухучетНачисления.Сумма) <> 0";
		
	Иначе
		
		ИменаТаблиц = Новый Массив;
		ИменаТаблиц.Добавить("ВТБухучетНачисленийБезРаспределяемыхПоБазе");
		Если ЗарплатаКадры.ВТСуществует(Запрос.МенеджерВременныхТаблиц, "ВТБухучетНачисленийИзФормы") Тогда
			ИменаТаблиц.Добавить("ВТБухучетНачисленийИзФормы");
		КонецЕсли;
		Если ЗарплатаКадры.ВТСуществует(Запрос.МенеджерВременныхТаблиц, "ВТБухучетНачисленийСторно") Тогда
			ИменаТаблиц.Добавить("ВТБухучетНачисленийСторно");
		КонецЕсли;
		
		ШаблонТекстаЗапроса = 
		"ВЫБРАТЬ
		|	БухучетНачисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	БухучетНачисления.ИдентификаторСтрокиНачисления КАК ИдентификаторСтрокиНачисления,
		|	БухучетНачисления.Сотрудник КАК Сотрудник,
		|	БухучетНачисления.ФизическоеЛицо КАК ФизическоеЛицо,
		|	БухучетНачисления.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
		|	БухучетНачисления.Начисление КАК Начисление,
		|	БухучетНачисления.ДатаНачала КАК ДатаНачала,
		|	БухучетНачисления.ДатаОкончания КАК ДатаОкончания,
		|	БухучетНачисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	БухучетНачисления.СтатьяФинансирования КАК СтатьяФинансирования,
		|	БухучетНачисления.СтатьяРасходов КАК СтатьяРасходов,
		|	БухучетНачисления.Сумма КАК Сумма,
		|	БухучетНачисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	ЛОЖЬ КАК Сторно
		|ИЗ
		|	#ИмяТаблицы КАК БухучетНачисления";
		
		ТекстВложенногоЗапроса = "";
		Для Индекс = 0 По ИменаТаблиц.ВГраница() Цикл
			ИмяТаблицы = ИменаТаблиц[Индекс];
			Если Индекс > 0 Тогда
				ТекстВложенногоЗапроса = ТекстВложенногоЗапроса + " 
				|
				|ОБЪЕДИНИТЬ ВСЕ
				|
				|";
			КонецЕсли;
			ФрагментТекстаЗапроса = СтрЗаменить(ШаблонТекстаЗапроса, "#ИмяТаблицы", ИмяТаблицы);
			Если ИмяТаблицы = "ВТБухучетНачисленийСторно" Тогда
				ФрагментТекстаЗапроса = СтрЗаменить(ФрагментТекстаЗапроса, "ЛОЖЬ КАК Сторно", "ИСТИНА КАК Сторно");
			ИначеЕсли ИмяТаблицы = "ВТБухучетНачисленийБезРаспределяемыхПоБазе" Тогда
				ФрагментТекстаЗапроса = СтрЗаменить(ФрагментТекстаЗапроса, "БухучетНачисления.ИдентификаторСтрокиНачисления", "NULL");	
			КонецЕсли;
			ТекстВложенногоЗапроса = ТекстВложенногоЗапроса + ФрагментТекстаЗапроса;
		КонецЦикла;
		ТекстВложенногоЗапроса = "("+ТекстВложенногоЗапроса+")";
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	БухучетНачисления.Сотрудник КАК Сотрудник,
		|	БухучетНачисления.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ВЫБОР
		|		КОГДА БухучетНачисления.ПодразделениеУчетаЗатрат = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
		|			ТОГДА НачисленияДляРаспределения.Подразделение
		|		ИНАЧЕ БухучетНачисления.ПодразделениеУчетаЗатрат
		|	КОНЕЦ КАК ПодразделениеУчетаЗатрат,
		|	БухучетНачисления.Начисление КАК Начисление,
		|	БухучетНачисления.ДатаНачала КАК ДатаНачала,
		|	БухучетНачисления.ДатаОкончания КАК ДатаОкончания,
		|	БухучетНачисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	БухучетНачисления.СтатьяФинансирования КАК СтатьяФинансирования,
		|	БухучетНачисления.СтатьяРасходов КАК СтатьяРасходов,
		|	БухучетНачисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	БухучетНачисления.Сторно КАК Сторно,
		|	ЕСТЬNULL(БухучетНачисления.ИдентификаторСтрокиНачисления, НачисленияДляРаспределения.ИдентификаторСтрокиНачисления) КАК ИдентификаторСтрокиНачисления,
		|	СУММА(БухучетНачисления.Сумма) КАК Сумма
		|ПОМЕСТИТЬ ВТБухучетНачисленийВременная
		|ИЗ
		|	ТаблицаБухучетНачисленийВременная КАК БухучетНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачисленияДляРаспределения КАК НачисленияДляРаспределения
		|		ПО БухучетНачисления.ИдентификаторСтроки = НачисленияДляРаспределения.ИдентификаторСтроки
		|
		|СГРУППИРОВАТЬ ПО
		|	БухучетНачисления.СпособОтраженияЗарплатыВБухучете,
		|	БухучетНачисления.ОблагаетсяЕНВД,
		|	БухучетНачисления.Сторно,
		|	БухучетНачисления.СтатьяРасходов,
		|	БухучетНачисления.СтатьяФинансирования,
		|	БухучетНачисления.Сотрудник,
		|	БухучетНачисления.ФизическоеЛицо,
		|	ВЫБОР
		|		КОГДА БухучетНачисления.ПодразделениеУчетаЗатрат = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
		|			ТОГДА НачисленияДляРаспределения.Подразделение
		|		ИНАЧЕ БухучетНачисления.ПодразделениеУчетаЗатрат
		|	КОНЕЦ,
		|	БухучетНачисления.Начисление,
		|	БухучетНачисления.ДатаНачала,
		|	БухучетНачисления.ДатаОкончания,
		|	ЕСТЬNULL(БухучетНачисления.ИдентификаторСтрокиНачисления, НачисленияДляРаспределения.ИдентификаторСтрокиНачисления)
		|
		|ИМЕЮЩИЕ
		|	СУММА(БухучетНачисления.Сумма) <> 0";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ТаблицаБухучетНачисленийВременная", ТекстВложенногоЗапроса);
		
	КонецЕсли;
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
	Запрос.Выполнить();
	УдалитьВТ.Добавить("ВТБухучетНачисленийВременная");
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	БухучетНачисленийВременная.Регистратор КАК Регистратор,
	|	БухучетНачисленийВременная.Сотрудник КАК Сотрудник,
	|	БухучетНачисленийВременная.ФизическоеЛицо КАК ФизическоеЛицо,
	|	БухучетНачисленийВременная.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|	БухучетНачисленийВременная.Начисление КАК Начисление,
	|	БухучетНачисленийВременная.ДатаНачала КАК ДатаНачала,
	|	БухучетНачисленийВременная.ДатаОкончания КАК ДатаОкончания,
	|	БухучетНачисленийВременная.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачисленийВременная.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетНачисленийВременная.СтатьяРасходов КАК СтатьяРасходов,
	|	БухучетНачисленийВременная.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	БухучетНачисленийВременная.Сумма КАК Сумма,
	|	БухучетНачисленийВременная.ИдентификаторСтрокиНачисления КАК ИдентификаторСтрокиНачисления,
	|	БухучетНачисленийВременная.Сторно КАК Сторно
	|ИЗ
	|	ВТБухучетНачисленийВременная КАК БухучетНачисленийВременная";
	Если ИспользоватьСтатьиФинансирования Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "БухучетНачисленийВременная.Регистратор", "ЗНАЧЕНИЕ(Документ.НачислениеЗарплаты.ПустаяСсылка)"); 
	КонецЕсли;
	БухучетНачисленийВременная = Запрос.Выполнить().Выгрузить();
	
	ТаблицаБухучет 				= НоваяТаблицаБухучетНачисленийПромежуточная();
	ТаблицаБухучетОчередности 	= НоваяТаблицаБухучетНачисленийПромежуточная();
	
	Выборка = РезультатОчередностьРаспределенияПоБазе.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если ТаблицаБухучетОчередности.Количество() > 0 Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаБухучетОчередности, БухучетНачисленийВременная);
			ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, "ВТБухучетНачисленийВременная");
			ЗарплатаКадры.СоздатьВТПоТаблицеЗначений(Запрос.МенеджерВременныхТаблиц, БухучетНачисленийВременная, "ВТБухучетНачисленийВременная");
			ТаблицаБухучетОчередности.Очистить();
		КонецЕсли;
		
		ТаблицаБухучетНетБазы 		= НоваяТаблицаБухучетНачисленийПромежуточная();
		ТаблицаБухучетНетБазы.Колонки.Добавить("ОтношениеКЕНВД", Новый ОписаниеТипов("ПеречислениеСсылка.ОтношениеКЕНВДЗатратНаЗарплату"));
		
		УдалитьВТОперативно = Новый Массив;
		
		ОчередностьОтраженияВУчете = Выборка.ОчередностьОтраженияВУчете;
		Запрос.УстановитьПараметр("ОчередностьОтраженияВУчете", ОчередностьОтраженияВУчете);
		
		// ВТБазовыеНачисленияБУ.
		Если НЕ ИспользоватьСтатьиФинансирования Тогда
			
			Запрос.Текст =
			"ВЫБРАТЬ
			|	БазаНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	БазаНачислений.НачислениеБаза КАК НачислениеБаза,
			|	БазаНачислений.РезультатБаза КАК РезультатБаза,
			|	БазаНачислений.РегистраторБаза КАК РегистраторБаза,
			|	БазаНачислений.НомерСтрокиБаза КАК НомерСтрокиБаза,
			|	БухучетНачислений.ДатаНачала КАК ДатаНачала,
			|	БухучетНачислений.ДатаОкончания КАК ДатаОкончания,
			|	СУММА(БухучетНачислений.Сумма) КАК СуммаБУ,
			|	БухучетНачислений.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
			|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
			|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
			|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
			|	БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
			|	БухучетНачислений.Сторно КАК Сторно
			|ПОМЕСТИТЬ ВТБазовыеНачисленияБУ
			|ИЗ
			|	ВТБазовыеНачисления КАК БазаНачислений
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТБухучетНачисленийВременная КАК БухучетНачислений
			|		ПО БазаНачислений.СотрудникБаза = БухучетНачислений.Сотрудник
			|			И (БазаНачислений.ОчередностьОтраженияВУчете = &ОчередностьОтраженияВУчете)
			|			И БазаНачислений.НачислениеБаза = БухучетНачислений.Начисление
			|			И БазаНачислений.РегистраторБаза = БухучетНачислений.Регистратор
			|			И БазаНачислений.ИдентификаторСтрокиРазрез = БухучетНачислений.ИдентификаторСтрокиНачисления
			|
			|СГРУППИРОВАТЬ ПО
			|	БазаНачислений.ИдентификаторСтроки,
			|	БазаНачислений.НачислениеБаза,
			|	БазаНачислений.РезультатБаза,
			|	БазаНачислений.РегистраторБаза,
			|	БухучетНачислений.ДатаОкончания,
			|	БухучетНачислений.ПодразделениеУчетаЗатрат,
			|	БазаНачислений.НомерСтрокиБаза,
			|	БухучетНачислений.ДатаНачала,
			|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете,
			|	БухучетНачислений.СтатьяФинансирования,
			|	БухучетНачислений.СтатьяРасходов,
			|	БухучетНачислений.Сторно,
			|	БухучетНачислений.ОблагаетсяЕНВД";
			
		Иначе
			
			Запрос.Текст =
			"ВЫБРАТЬ
			|	БазовыеНачисленияБУ.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	БазовыеНачисленияБУ.НачислениеБаза КАК НачислениеБаза,
			|	СУММА(БазовыеНачисленияБУ.РезультатБаза) КАК РезультатБаза,
			|	БазовыеНачисленияБУ.РегистраторБаза КАК РегистраторБаза,
			|	БазовыеНачисленияБУ.НомерСтрокиБаза КАК НомерСтрокиБаза,
			|	БазовыеНачисленияБУ.ДатаНачала КАК ДатаНачала,
			|	БазовыеНачисленияБУ.ДатаОкончания КАК ДатаОкончания,
			|	СУММА(БазовыеНачисленияБУ.СуммаБУ) КАК СуммаБУ,
			|	БазовыеНачисленияБУ.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
			|	БазовыеНачисленияБУ.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
			|	БазовыеНачисленияБУ.СтатьяФинансирования КАК СтатьяФинансирования,
			|	БазовыеНачисленияБУ.СтатьяРасходов КАК СтатьяРасходов,
			|	БазовыеНачисленияБУ.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД
			|ПОМЕСТИТЬ ВТБазовыеНачисленияБУ
			|ИЗ
			|	(ВЫБРАТЬ
			|		БазаНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|		БухучетНачислений.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
			|		БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
			|		БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
			|		БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
			|		БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
			|		БазаНачислений.НачислениеБаза КАК НачислениеБаза,
			|		БазаНачислений.РезультатБаза КАК РезультатБаза,
			|		БухучетНачислений.ДатаНачала КАК ДатаНачала,
			|		БухучетНачислений.ДатаОкончания КАК ДатаОкончания,
			|		ВЫРАЗИТЬ(БухучетНачислений.Сумма КАК ЧИСЛО(13, 2)) КАК СуммаБУ,
			|		БазаНачислений.РегистраторБаза КАК РегистраторБаза,
			|		БазаНачислений.НомерСтрокиБаза КАК НомерСтрокиБаза
			|	ИЗ
			|		ВТБазовыеНачисления КАК БазаНачислений
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТБухучетНачисленийВременная КАК БухучетНачислений
			|			ПО БазаНачислений.ИдентификаторСтрокиРазрез = БухучетНачислений.ИдентификаторСтрокиНачисления
			|				И (БазаНачислений.БазоваяЗаписьТекущегоРегистратора)
			|				И (БазаНачислений.ОчередностьОтраженияВУчете = &ОчередностьОтраженияВУчете)
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		БазаНачислений.ИдентификаторСтроки,
			|		ВЫБОР
			|			КОГДА БухучетНачислений.ПодразделениеУчетаЗатрат = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
			|				ТОГДА БухучетНачислений.Подразделение
			|			ИНАЧЕ БухучетНачислений.ПодразделениеУчетаЗатрат
			|		КОНЕЦ,
			|		БухучетНачислений.СпособОтраженияЗарплатыВБухучете,
			|		БухучетНачислений.СтатьяФинансирования,
			|		БухучетНачислений.СтатьяРасходов,
			|		БухучетНачислений.ОблагаетсяЕНВД,
			|		БазаНачислений.НачислениеБаза,
			|		БазаНачислений.РезультатБаза,
			|		БухучетНачислений.ДатаНачала,
			|		БухучетНачислений.ДатаОкончания,
			|		ВЫРАЗИТЬ(БухучетНачислений.Сумма КАК ЧИСЛО(13, 2)),
			|		БазаНачислений.РегистраторБаза,
			|		БазаНачислений.НомерСтрокиБаза
			|	ИЗ
			|		ВТБазовыеНачисления КАК БазаНачислений
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.БухучетНачисленияУдержанияПоСотрудникам КАК БухучетНачислений
			|			ПО БазаНачислений.СотрудникБаза = БухучетНачислений.Сотрудник
			|				И (НЕ БазаНачислений.БазоваяЗаписьТекущегоРегистратора)
			|				И (БазаНачислений.ОчередностьОтраженияВУчете = &ОчередностьОтраженияВУчете)
			|				И БазаНачислений.РегистраторБаза = БухучетНачислений.Регистратор
			|				И БазаНачислений.НачислениеБаза = БухучетНачислений.НачислениеУдержание
			|				И БазаНачислений.ИдентификаторСтрокиРазрез = БухучетНачислений.ИдентификаторСтроки) КАК БазовыеНачисленияБУ
			|
			|СГРУППИРОВАТЬ ПО
			|	БазовыеНачисленияБУ.ИдентификаторСтроки,
			|	БазовыеНачисленияБУ.СпособОтраженияЗарплатыВБухучете,
			|	БазовыеНачисленияБУ.СтатьяФинансирования,
			|	БазовыеНачисленияБУ.СтатьяРасходов,
			|	БазовыеНачисленияБУ.ОблагаетсяЕНВД,
			|	БазовыеНачисленияБУ.НачислениеБаза,
			|	БазовыеНачисленияБУ.ДатаНачала,
			|	БазовыеНачисленияБУ.ДатаОкончания,
			|	БазовыеНачисленияБУ.ПодразделениеУчетаЗатрат,
			|	БазовыеНачисленияБУ.РегистраторБаза,
			|	БазовыеНачисленияБУ.НомерСтрокиБаза";
			
		КонецЕсли;
		Запрос.Выполнить();
		УдалитьВТОперативно.Добавить("ВТБазовыеНачисленияБУ");
		
		Если Не ИспользоватьСтатьиФинансирования Тогда
			
			Запрос.Текст =
			"ВЫБРАТЬ
			|	БазовыеНачисленияБУ.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	БазовыеНачисленияБУ.РегистраторБаза КАК РегистраторБаза,
			|	БазовыеНачисленияБУ.НомерСтрокиБаза КАК НомерСтрокиБаза,
			|	БазовыеНачисленияБУ.Сторно КАК Сторно,
			|	СУММА(БазовыеНачисленияБУ.СуммаБУ) КАК СуммаБУ
			|ПОМЕСТИТЬ ВТБазовыеНачисленияБУСвод
			|ИЗ
			|	ВТБазовыеНачисленияБУ КАК БазовыеНачисленияБУ
			|
			|СГРУППИРОВАТЬ ПО
			|	БазовыеНачисленияБУ.ИдентификаторСтроки,
			|	БазовыеНачисленияБУ.РегистраторБаза,
			|	БазовыеНачисленияБУ.НомерСтрокиБаза,
			|	БазовыеНачисленияБУ.Сторно
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	БазовыеНачисленияБУ.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	БазовыеНачисленияБУ.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
			|	БазовыеНачисленияБУ.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
			|	БазовыеНачисленияБУ.СтатьяФинансирования КАК СтатьяФинансирования,
			|	БазовыеНачисленияБУ.СтатьяРасходов КАК СтатьяРасходов,
			|	БазовыеНачисленияБУ.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
			|	БазовыеНачисленияБУ.Сторно КАК Сторно,
			|	СУММА(ВЫБОР
			|			КОГДА ВТБазовыеНачисленияБУСвод.СуммаБУ = 0
			|				ТОГДА 0
			|			ИНАЧЕ ВЫРАЗИТЬ(БазовыеНачисленияБУ.РезультатБаза * БазовыеНачисленияБУ.СуммаБУ / ВТБазовыеНачисленияБУСвод.СуммаБУ КАК ЧИСЛО(25, 10))
			|		КОНЕЦ) КАК Сумма
			|ПОМЕСТИТЬ ВТБазовыеНачисленияБУКоэффициенты
			|ИЗ
			|	ВТБазовыеНачисленияБУ КАК БазовыеНачисленияБУ
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБазовыеНачисленияБУСвод КАК ВТБазовыеНачисленияБУСвод
			|		ПО БазовыеНачисленияБУ.ИдентификаторСтроки = ВТБазовыеНачисленияБУСвод.ИдентификаторСтроки
			|			И БазовыеНачисленияБУ.РегистраторБаза = ВТБазовыеНачисленияБУСвод.РегистраторБаза
			|			И БазовыеНачисленияБУ.НомерСтрокиБаза = ВТБазовыеНачисленияБУСвод.НомерСтрокиБаза
			|			И БазовыеНачисленияБУ.Сторно = ВТБазовыеНачисленияБУСвод.Сторно
			|
			|СГРУППИРОВАТЬ ПО
			|	БазовыеНачисленияБУ.ИдентификаторСтроки,
			|	БазовыеНачисленияБУ.СпособОтраженияЗарплатыВБухучете,
			|	БазовыеНачисленияБУ.СтатьяФинансирования,
			|	БазовыеНачисленияБУ.СтатьяРасходов,
			|	БазовыеНачисленияБУ.ОблагаетсяЕНВД,
			|	БазовыеНачисленияБУ.ПодразделениеУчетаЗатрат,
			|	БазовыеНачисленияБУ.Сторно
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	НачисленияПоБазе.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	НачисленияПоБазе.РассчитыватьПоРазовымНачислениямДокумента КАК РассчитыватьПоРазовымНачислениямДокумента,
			|	НачисленияБУ.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
			|	НачисленияПоБазе.Сумма КАК Сумма,
			|	НачисленияБУ.СтатьяФинансирования КАК СтатьяФинансирования,
			|	НачисленияБУ.СтатьяРасходов КАК СтатьяРасходов,
			|	НачисленияБУ.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
			|	НачисленияБУ.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
			|	НачисленияПоБазе.Сторно КАК Сторно,
			|	НачисленияБУ.Сумма КАК СуммаБУ
			|ПОМЕСТИТЬ ВТНачисленияПоБазеБУКоэффициенты
			|ИЗ
			|	ВТНачисленияПоБазовымРасчетам КАК НачисленияПоБазе
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТБазовыеНачисленияБУКоэффициенты КАК НачисленияБУ
			|		ПО НачисленияПоБазе.ИдентификаторСтроки = НачисленияБУ.ИдентификаторСтроки
			|			И НачисленияПоБазе.Сторно = НачисленияБУ.Сторно
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	НачисленияПоБазе.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	НачисленияПоБазе.Сторно КАК Сторно,
			|	СУММА(НачисленияПоБазе.СуммаБУ) КАК СуммаБУ
			|ПОМЕСТИТЬ ВТНачисленияПоБазеБУКоэффициентыСвод
			|ИЗ
			|	ВТНачисленияПоБазеБУКоэффициенты КАК НачисленияПоБазе
			|
			|СГРУППИРОВАТЬ ПО
			|	НачисленияПоБазе.ИдентификаторСтроки,
			|	НачисленияПоБазе.Сторно
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	Начисления.РассчитыватьПоРазовымНачислениямДокумента КАК РассчитыватьПоРазовымНачислениямДокумента,
			|	Начисления.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
			|	Начисления.СтатьяФинансирования КАК СтатьяФинансирования,
			|	ВЫБОР
			|		КОГДА &РаботаВБюджетномУчреждении
			|			ТОГДА Начисления.СтатьяРасходов
			|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
			|	КОНЕЦ КАК СтатьяРасходов,
			|	Начисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
			|	Начисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
			|	ВЫБОР
			|		КОГДА НачисленияСвод.СуммаБУ = 0
			|			ТОГДА 0
			|		ИНАЧЕ ВЫРАЗИТЬ(Начисления.Сумма * Начисления.СуммаБУ / НачисленияСвод.СуммаБУ КАК ЧИСЛО(25, 10))
			|	КОНЕЦ КАК База,
			|	Начисления.Сторно КАК Сторно
			|ИЗ
			|	ВТНачисленияПоБазеБУКоэффициенты КАК Начисления
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачисленияПоБазеБУКоэффициентыСвод КАК НачисленияСвод
			|		ПО Начисления.ИдентификаторСтроки = НачисленияСвод.ИдентификаторСтроки
			|			И Начисления.Сторно = НачисленияСвод.Сторно
			|ГДЕ
			|	ВЫБОР
			|			КОГДА НачисленияСвод.СуммаБУ = 0
			|				ТОГДА 0
			|			ИНАЧЕ ВЫРАЗИТЬ(Начисления.Сумма * Начисления.СуммаБУ / НачисленияСвод.СуммаБУ КАК ЧИСЛО(25, 10))
			|		КОНЕЦ <> 0";
			
		Иначе
			
			Запрос.Текст =
			"ВЫБРАТЬ
			|	БазовыеНачисленияБУ.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	БазовыеНачисленияБУ.РегистраторБаза КАК РегистраторБаза,
			|	БазовыеНачисленияБУ.НомерСтрокиБаза КАК НомерСтрокиБаза,
			|	СУММА(БазовыеНачисленияБУ.СуммаБУ) КАК СуммаБУ
			|ПОМЕСТИТЬ ВТБазовыеНачисленияБУСвод
			|ИЗ
			|	ВТБазовыеНачисленияБУ КАК БазовыеНачисленияБУ
			|
			|СГРУППИРОВАТЬ ПО
			|	БазовыеНачисленияБУ.ИдентификаторСтроки,
			|	БазовыеНачисленияБУ.РегистраторБаза,
			|	БазовыеНачисленияБУ.НомерСтрокиБаза
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	БазовыеНачисленияБУ.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	БазовыеНачисленияБУ.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
			|	БазовыеНачисленияБУ.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
			|	БазовыеНачисленияБУ.СтатьяФинансирования КАК СтатьяФинансирования,
			|	БазовыеНачисленияБУ.СтатьяРасходов КАК СтатьяРасходов,
			|	БазовыеНачисленияБУ.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
			|	СУММА(ВЫБОР
			|			КОГДА ВТБазовыеНачисленияБУСвод.СуммаБУ = 0
			|				ТОГДА 0
			|			ИНАЧЕ ВЫРАЗИТЬ(БазовыеНачисленияБУ.РезультатБаза * БазовыеНачисленияБУ.СуммаБУ / ВТБазовыеНачисленияБУСвод.СуммаБУ КАК ЧИСЛО(25, 10))
			|		КОНЕЦ) КАК Сумма
			|ПОМЕСТИТЬ ВТБазовыеНачисленияБУКоэффициенты
			|ИЗ
			|	ВТБазовыеНачисленияБУ КАК БазовыеНачисленияБУ
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБазовыеНачисленияБУСвод КАК ВТБазовыеНачисленияБУСвод
			|		ПО БазовыеНачисленияБУ.ИдентификаторСтроки = ВТБазовыеНачисленияБУСвод.ИдентификаторСтроки
			|			И БазовыеНачисленияБУ.РегистраторБаза = ВТБазовыеНачисленияБУСвод.РегистраторБаза
			|			И БазовыеНачисленияБУ.НомерСтрокиБаза = ВТБазовыеНачисленияБУСвод.НомерСтрокиБаза
			|
			|СГРУППИРОВАТЬ ПО
			|	БазовыеНачисленияБУ.ИдентификаторСтроки,
			|	БазовыеНачисленияБУ.СпособОтраженияЗарплатыВБухучете,
			|	БазовыеНачисленияБУ.СтатьяФинансирования,
			|	БазовыеНачисленияБУ.СтатьяРасходов,
			|	БазовыеНачисленияБУ.ОблагаетсяЕНВД,
			|	БазовыеНачисленияБУ.ПодразделениеУчетаЗатрат
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	НачисленияПоБазе.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	НачисленияПоБазе.РассчитыватьПоРазовымНачислениямДокумента КАК РассчитыватьПоРазовымНачислениямДокумента,
			|	НачисленияБУ.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
			|	НачисленияПоБазе.Сумма КАК Сумма,
			|	НачисленияБУ.СтатьяФинансирования КАК СтатьяФинансирования,
			|	НачисленияБУ.СтатьяРасходов КАК СтатьяРасходов,
			|	НачисленияБУ.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
			|	НачисленияБУ.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
			|	НачисленияБУ.Сумма КАК СуммаБУ
			|ПОМЕСТИТЬ ВТНачисленияПоБазеБУКоэффициенты
			|ИЗ
			|	ВТНачисленияПоБазовымРасчетам КАК НачисленияПоБазе
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТБазовыеНачисленияБУКоэффициенты КАК НачисленияБУ
			|		ПО НачисленияПоБазе.ИдентификаторСтроки = НачисленияБУ.ИдентификаторСтроки
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	НачисленияПоБазе.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	СУММА(НачисленияПоБазе.СуммаБУ) КАК СуммаБУ
			|ПОМЕСТИТЬ ВТНачисленияПоБазеБУКоэффициентыСвод
			|ИЗ
			|	ВТНачисленияПоБазеБУКоэффициенты КАК НачисленияПоБазе
			|
			|СГРУППИРОВАТЬ ПО
			|	НачисленияПоБазе.ИдентификаторСтроки
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	Начисления.РассчитыватьПоРазовымНачислениямДокумента КАК РассчитыватьПоРазовымНачислениямДокумента,
			|	Начисления.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
			|	Начисления.СтатьяФинансирования КАК СтатьяФинансирования,
			|	ВЫБОР
			|		КОГДА &РаботаВБюджетномУчреждении
			|			ТОГДА Начисления.СтатьяРасходов
			|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
			|	КОНЕЦ КАК СтатьяРасходов,
			|	Начисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
			|	Начисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
			|	ВЫБОР
			|		КОГДА НачисленияСвод.СуммаБУ = 0
			|			ТОГДА 0
			|		ИНАЧЕ ВЫРАЗИТЬ(Начисления.Сумма * Начисления.СуммаБУ / НачисленияСвод.СуммаБУ КАК ЧИСЛО(25, 10))
			|	КОНЕЦ КАК База
			|ИЗ
			|	ВТНачисленияПоБазеБУКоэффициенты КАК Начисления
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачисленияПоБазеБУКоэффициентыСвод КАК НачисленияСвод
			|		ПО Начисления.ИдентификаторСтроки = НачисленияСвод.ИдентификаторСтроки
			|ГДЕ
			|	ВЫБОР
			|			КОГДА НачисленияСвод.СуммаБУ = 0
			|				ТОГДА 0
			|			ИНАЧЕ ВЫРАЗИТЬ(Начисления.Сумма * Начисления.СуммаБУ / НачисленияСвод.СуммаБУ КАК ЧИСЛО(25, 10))
			|		КОНЕЦ <> 0";
			
		КонецЕсли;
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
		ТаблицаКоэффициентов = Запрос.Выполнить().Выгрузить();
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	НачисленияПоБазе.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	НачисленияДляРаспределения.ИдентификаторСтрокиНачисления КАК ИдентификаторСтрокиНачисления,
		|	НачисленияДляРаспределения.Регистратор КАК Регистратор,
		|	НачисленияПоБазе.РассчитыватьПоРазовымНачислениямДокумента КАК РассчитыватьПоРазовымНачислениямДокумента,
		|	НачисленияПоБазе.Сотрудник КАК Сотрудник,
		|	НачисленияПоБазе.ФизическоеЛицо КАК ФизическоеЛицо,
		|	НачисленияПоБазе.Начисление КАК Начисление,
		|	НачисленияПоБазе.ДатаНачала КАК ДатаНачала,
		|	НачисленияПоБазе.ДатаОкончания КАК ДатаОкончания,
		|	НачисленияПоБазе.Сторно КАК Сторно,
		|	НачисленияПоБазе.Сумма КАК Сумма
		|ИЗ
		|	ВТНачисленияПоБазовымРасчетам КАК НачисленияПоБазе
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачисленияДляРаспределения КАК НачисленияДляРаспределения
		|		ПО НачисленияПоБазе.ИдентификаторСтроки = НачисленияДляРаспределения.ИдентификаторСтроки
		|ГДЕ
		|	НачисленияПоБазе.ОчередностьОтраженияВУчете = &ОчередностьОтраженияВУчете";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
		Если ИспользоватьСтатьиФинансирования Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "НачисленияДляРаспределения.Регистратор", "ЗНАЧЕНИЕ(Документ.НачислениеЗарплаты.ПустаяСсылка)");
		КонецЕсли;
		НачисленияПоБазе = Запрос.Выполнить().Выгрузить();
		
		УдалитьВТОперативно.Добавить("ВТБазовыеНачисленияБУСвод");
		УдалитьВТОперативно.Добавить("ВТБазовыеНачисленияБУКоэффициенты");
		УдалитьВТОперативно.Добавить("ВТНачисленияПоБазеБУКоэффициенты");
		УдалитьВТОперативно.Добавить("ВТНачисленияПоБазеБУКоэффициентыСвод");
		
		Если Не ИспользоватьСтатьиФинансирования Тогда
			ТаблицаКоэффициентов.Индексы.Добавить("ИдентификаторСтроки,Сторно");
			Отбор = Новый Структура("ИдентификаторСтроки,Сторно");
		Иначе
			ТаблицаКоэффициентов.Индексы.Добавить("ИдентификаторСтроки");
			Отбор = Новый Структура("ИдентификаторСтроки");
		КонецЕсли;
		
		Для каждого СтрокаНачисления Из НачисленияПоБазе Цикл
			
			ЗаполнитьЗначенияСвойств(Отбор, СтрокаНачисления);
			СтрокиОтражения = ТаблицаКоэффициентов.НайтиСтроки(Отбор);
			Если СтрокаНачисления.РассчитыватьПоРазовымНачислениямДокумента Тогда
				НовыеСтрокиОтражения = Новый Массив;
				Для каждого СтрокаОтражения Из СтрокиОтражения Цикл
					Если СтрокаОтражения.РассчитыватьПоРазовымНачислениямДокумента Тогда
						НовыеСтрокиОтражения.Добавить(СтрокаОтражения);
					КонецЕсли;
				КонецЦикла;
				СтрокиОтражения = НовыеСтрокиОтражения;
			КонецЕсли;
			Коэффициенты = ОбщегоНазначения.ВыгрузитьКолонку(СтрокиОтражения,"База");
			Результаты = ЗарплатаКадрыКлиентСервер.РаспределитьПропорциональноКоэффициентам(СтрокаНачисления.Сумма, Коэффициенты);
			
			Если Результаты = Неопределено Тогда
				
				НоваяСтрока = ТаблицаБухучетНетБазы.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНачисления);
				Если РаботаВБюджетномУчреждении Тогда
					НоваяСтрока.СтатьяРасходов = СтатьяРасходовБюджетНачисления(СтрокаНачисления.Начисление, ПараметрыРаспределенияБюджет);
				КонецЕсли;
				
			Иначе
				
				Индекс = 0;
				Для Каждого СтрокаОтражения Из СтрокиОтражения Цикл
					
					НоваяСтрока = ТаблицаБухучетОчередности.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНачисления);
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаОтражения);
					НоваяСтрока.Сумма = Результаты[Индекс];
					
					Если РаботаВБюджетномУчреждении Тогда
						Если Не ЗначениеЗаполнено(НоваяСтрока.СтатьяРасходов) Тогда
							НоваяСтрока.СтатьяРасходов = СтатьяРасходовБюджетНачисления(СтрокаНачисления.Начисление, ПараметрыРаспределенияБюджет);
						КонецЕсли;
					КонецЕсли;
					
					Индекс = Индекс + 1;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если ТаблицаБухучетНетБазы.Количество() > 0 Тогда
			ДополнитьАналитику(Запрос, ТаблицаБухучетНетБазы);
			Если ПрименяетсяЕНВД Тогда
				Для каждого СтрокаТЗ Из ТаблицаБухучетНетБазы Цикл
					СтрокаТЗ.ОблагаетсяЕНВД = (СтрокаТЗ.ОтношениеКЕНВД = Перечисления.ОтношениеКЕНВДЗатратНаЗарплату.ЕНВД);
				КонецЦикла;
			КонецЕсли;
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаБухучетНетБазы, ТаблицаБухучетОчередности);
		КонецЕсли;
		
		ОтражениеЗарплатыВУчете.СвернутьТаблицу(ТаблицаБухучетОчередности);
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаБухучетОчередности, ТаблицаБухучет);
		
		ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТОперативно);
		
	КонецЦикла;
	
	ЗарплатаКадры.СоздатьВТПоТаблицеЗначений(Запрос.МенеджерВременныхТаблиц, ТаблицаБухучет, "ВТБухучетНачисленийПоБазовымРасчетам");
	
	ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
	
КонецПроцедуры

// Выполняет отражение начислений в бухучете, кроме начислений по договорам ГПХ .
// Результат помещается во временную таблицу с именем ИмяВТБухучетНачислений,
//
// Параметры:
// 		ИсходныеДанные - Структура - описание см ОписаниеИсходныхДанныхДляОтраженияНачисленийВБухучете.
// 			Должны быть заполнены следующие свойства:
// 			* МенеджерВременныхТаблиц
// 			* ИмяВТНачисления - имя временной таблицы с начислениями по договорам.
// 			* МесяцНачисления
// 			* Организация
// 		ИмяВТБухучетНачислений - Строка - имя временной таблицы в которую помещаются результаты.
//
Процедура ПолучитьБухучетНачисленийБезДоговоровСоздатьВТ(ИсходныеДанные, ИмяВТБухучетНачислений)
	
	УдалитьВТ = Новый Массив;
	
	Организация              = ИсходныеДанные.Организация;
	ПериодРегистрации        = ИсходныеДанные.МесяцНачисления;
	МенеджерВТ			     = ИсходныеДанные.МенеджерВременныхТаблиц;
	ИмяВТНачисленияИсходная  = ИсходныеДанные.ИмяВТНачисления;
	ИсключаемыйРегистратор 	 = ИсходныеДанные.ИсключаемыйРегистратор;
	ВидыОперацийРасходыФСС   = ИсходныеДанные.ВидыОперацийРасходыФСС;
	БухучетПервичногоДокумента = ИсходныеДанные.БухучетПервичногоДокумента;
	
	// Получение дополнительных параметров по настройкам программы.
	ПрименяетсяЕНВД = ОтражениеЗарплатыВБухучете.ПлательщикЕНВД(Организация, ПериодРегистрации);
	ПроцентЕНВД = 0;
	РассчитыватьДолюЕНВД = Ложь;
	Если ПрименяетсяЕНВД Тогда
		ПроцентЕНВД = ОтражениеЗарплатыВБухучете.ПроцентЕНВД(Организация, ПериодРегистрации);
		ПроцентЕНВД = ?(ПроцентЕНВД = Неопределено, 0, ПроцентЕНВД);
		РассчитыватьДолюЕНВД = ?(ПроцентЕНВД = 0 Или ПроцентЕНВД = 100, Ложь, Истина);
	КонецЕсли;
	
	// Дополнительные параметры, используемые при распределении, когда ведется учет по статьям.
	ИспользоватьСтатьиФинансирования = ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата");
	ПараметрыРаспределенияПоСтатьям = Новый Структура("
	|КоэффициентыСреднегоЗаработкаФССДокумента,
	|КоэффициентыСреднегоЗаработкаДокумента,
	|СтрокиКоэффициентыСреднегоЗаработка,
	|АктуальныеСтатьиФинансирования,
	|АктуальныеСпособыОтражения,
	|СтатьиФинансированияЗамены,
	|НастройкиСтатейФинансирования");
	Если ИспользоватьСтатьиФинансирования Тогда
		
		// Заполнение свойств: КоэффициентыСреднегоЗаработкаФССДокумента,КоэффициентыСреднегоЗаработкаДокумента,СтрокиКоэффициентыСреднегоЗаработка.
		ЗаполнитьЗначенияСвойств(ПараметрыРаспределенияПоСтатьям, ИсходныеДанные);
		
		ПараметрыРаспределенияПоСтатьям.АктуальныеСтатьиФинансирования 	= АктуальныеСтатьиФинансирования(Организация, ПериодРегистрации);
		ПараметрыРаспределенияПоСтатьям.АктуальныеСпособыОтражения 		= АктуальныеСпособыОтражения(Организация, ПериодРегистрации);
		ПараметрыРаспределенияПоСтатьям.СтатьиФинансированияЗамены 		= СтатьиФинансированияЗамены(ПериодРегистрации);
		ПараметрыРаспределенияПоСтатьям.НастройкиСтатейФинансирования 	= НастройкиСтатейФинансирования(ПериодРегистрации);
		
	КонецЕсли;
	
	// Дополнительные параметры, используемые при распределении, когда РаботаВБюджетномУчреждении.
	РаботаВБюджетномУчреждении = ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении");
	ПараметрыРаспределенияБюджет = Новый Структура("
	|СтатьиРасходовБюджетПоВидамНачислений,
	|СтатьяРасходовОплатаТруда");
	Если РаботаВБюджетномУчреждении Тогда
		
		// ПараметрыРаспределенияБюджет.СтатьиРасходовБюджетПоВидамНачислений
		// соответствие, ключ - начисление, ссылка - статья расходов.
		// Содержит только те начисления, для которых определена статья расходов,
		// в зависимости от вида начисления.
		
		ПараметрыРаспределенияБюджет.СтатьяРасходовОплатаТруда = СтатьяРасходов211();
		
		// Будет создана таблица ВТСтатьиРасходовНачисленийБюджетПоУмолчанию.
		СтатьиРасходовНачисленийБюджет(МенеджерВТ, Организация, ПериодРегистрации,
															ПараметрыРаспределенияБюджет.СтатьиРасходовБюджетПоВидамНачислений);
		УдалитьВТ.Добавить("ВТСтатьиРасходовНачисленийБюджетПоУмолчанию");
		
	КонецЕсли;

	ИспользоватьРасчетСохраняемогоДенежногоСодержания = ПолучитьФункциональнуюОпцию("ИспользоватьРасчетСохраняемогоДенежногоСодержания");
	ПараметрыСохраняемогоДенежногоСодержания = Новый Структура("
	|КоэффициентыРаспределенияДенежногоСодержания,
	|СтрокиКоэффициентыСохраняемогоДС");
	Если ИспользоватьРасчетСохраняемогоДенежногоСодержания Тогда
		// Заполнение свойств: КоэффициентыРаспределенияДенежногоСодержания, СтрокиКоэффициентыСохраняемогоДС
		ЗаполнитьЗначенияСвойств(ПараметрыСохраняемогоДенежногоСодержания, ИсходныеДанные);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	
	// Параметры, используемые при получении бухучета начислений сохраняемого денежного содержания
	// РаспределениеСохраняемогоДС - признак того, что выполняется получение бухучета начислений,
	// входящих в расчет сохраняемого ДС.
	РаспределениеСохраняемогоДС = ИсходныеДанные.РаспределениеСохраняемогоДС;
	Если РаспределениеСохраняемогоДС Тогда
		
		// СоответствиеСотрудников - ключ, это ссылка на "временного сотрудника", значение - ссылка на настоящего сотрудника
		СоответствиеСотрудников 	= ИсходныеДанные.СоответствиеСотрудников;
		// ДатаНачалаДляБухучета - настоящая дата, для получения бухучета
		ДатаНачалаДляБухучета = ИсходныеДанные.ДатаНачалаДляБухучета;
		
		ТаблицаСоответствия = Новый ТаблицаЗначений;
		ТаблицаСоответствия.Колонки.Добавить("ВременныйСотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
		ТаблицаСоответствия.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
		Для каждого ЭлементКоллекции Из СоответствиеСотрудников Цикл
			НоваяСтрока = ТаблицаСоответствия.Добавить();
			НоваяСтрока.ВременныйСотрудник = ЭлементКоллекции.Ключ;
			НоваяСтрока.Сотрудник = ЭлементКоллекции.Значение;
		КонецЦикла;
		Запрос.УстановитьПараметр("ТаблицаСоответствия", ТаблицаСоответствия);
		Запрос.УстановитьПараметр("ДатаНачалаДляБухучета", ДатаНачалаДляБухучета);
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаСоответствия.Сотрудник КАК Сотрудник,
		|	ТаблицаСоответствия.ВременныйСотрудник КАК ВременныйСотрудник
		|ПОМЕСТИТЬ ВТТаблицаСоответствия
		|ИЗ
		|	&ТаблицаСоответствия КАК ТаблицаСоответствия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаСоответствия.Сотрудник КАК Сотрудник,
		|	&ДатаНачалаДляБухучета КАК ДатаНачала,
		|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо,
		|	НачисленияДляРаспределения.*
		|ПОМЕСТИТЬ ВТНачисленияДляРаспределенияСохраняемогоДС
		|ИЗ
		|	ВТНачисленияДляРаспределения КАК НачисленияДляРаспределения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТТаблицаСоответствия КАК ТаблицаСоответствия
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
		|			ПО ТаблицаСоответствия.Сотрудник = Сотрудники.Ссылка
		|		ПО НачисленияДляРаспределения.Сотрудник = ТаблицаСоответствия.ВременныйСотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТТаблицаСоответствия";
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
		Запрос.Выполнить();
		ИмяВТНачисленияИсходная = "ВТНачисленияДляРаспределенияСохраняемогоДС";
		УдалитьВТ.Добавить(ИмяВТНачисленияИсходная);
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ПроцентЕНВД", ПроцентЕНВД);
	Запрос.УстановитьПараметр("ЕстьЕНВД", ПрименяетсяЕНВД);
	Запрос.УстановитьПараметр("РассчитыватьДолюЕНВД", РассчитыватьДолюЕНВД);
	Запрос.УстановитьПараметр("ИспользоватьСтатьиФинансирования", ИспользоватьСтатьиФинансирования);
	Запрос.УстановитьПараметр("РаботаВБюджетномУчреждении", РаботаВБюджетномУчреждении);
	Запрос.УстановитьПараметр("ИсключаемыйРегистратор", ИсключаемыйРегистратор);
	Запрос.УстановитьПараметр("РасходыФСС", ВидыОперацийРасходыФСС);
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("БухучетПервичногоДокумента", БухучетПервичногоДокумента);
	
	// Подготовка временной таблицы ВТИсходнаяТаблицаДляНастроекБухучета для получения настроек бухучета.
	ПараметрыПолученияНастроек = ПараметрыПолученияНастроекБухучета();
	ПараметрыПолученияНастроек.ИмяПоляПериод = "ДатаНачала";
	СоздатьВТИсходнаяТаблицаДляНастроекБухучета(МенеджерВТ, ИмяВТНачисленияИсходная, ПериодРегистрации, ПараметрыПолученияНастроек);
	УдалитьВТ.Добавить("ВТИсходнаяТаблицаДляНастроекБухучета");
	
	// ВТНастройкиБухучета и ВТНастройкиБухучетаПоУмолчанию
	// Получение настроек бухучета с учетом зарегистрированного распределения основного заработка сотрудников.
	СоздатьВТНастройкиБухучета(МенеджерВТ, Организация, ПериодРегистрации, Истина);
	УдалитьВТ.Добавить("ВТНастройкиБухучета");
	УдалитьВТ.Добавить("ВТНастройкиБухучетаПоУмолчанию");
	
	// ВТНастройкиНачислений
	// Настройки начислений, используемые в алгоритмах распределения.
	СоздатьВТНастройкиНачисленийДляБухучета(Запрос);
	УдалитьВТ.Добавить("ВТНастройкиНачислений");
	
	// ВТПоказателиНачисленийОпределяющиеБухучет
	СоздатьВТПоказателиНачисленийОпределяющиеБухучет(Запрос);
	УдалитьВТ.Добавить("ВТПоказателиНачисленийОпределяющиеБухучет");
	
	// ВТНастройкиБухучетаВсехСтрок и ВТНастройкиБухучетаВсехСтрокПоУмолчанию
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	НастройкиБухучета.СтатьяФинансирования КАК СтатьяФинансирования,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(НастройкиНачислений.ЭтоРасходыФСС, ЛОЖЬ)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
	|		ИНАЧЕ НастройкиБухучета.СпособОтраженияЗарплатыВБухучете
	|	КОНЕЦ КАК СпособОтраженияЗарплатыВБухучете,
	|	ВЫБОР
	|		КОГДА НЕ &ЕстьЕНВД
	|			ТОГДА НастройкиБухучета.ОтношениеКЕНВД
	|		КОГДА ЕСТЬNULL(НастройкиНачислений.ОтношениеКЕНВД, ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)
	|			ТОГДА НастройкиБухучета.ОтношениеКЕНВД
	|		ИНАЧЕ НастройкиНачислений.ОтношениеКЕНВД
	|	КОНЕЦ КАК ОтношениеКЕНВД,
	|	НастройкиБухучета.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|	НастройкиБухучета.ДоляРаспределения КАК ДоляРаспределения,
	|	НастройкиБухучета.СтрокаСРаспределением КАК СтрокаСРаспределением
	|ПОМЕСТИТЬ ВТНастройкиБухучетаВсехСтрок
	|ИЗ
	|	ВТНачисленияДляРаспределения КАК Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиНачислений КАК НастройкиНачислений
	|		ПО Начисления.Начисление = НастройкиНачислений.Начисление
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиБухучета КАК НастройкиБухучета
	|		ПО Начисления.Сотрудник = НастройкиБухучета.Сотрудник
	|			И Начисления.ДатаНачала = НастройкиБухучета.Период
	|			И Начисления.Подразделение = НастройкиБухучета.Подразделение
	|			И Начисления.ТерриторияВыполненияРаботВОрганизации = НастройкиБухучета.ТерриторияВыполненияРаботВОрганизации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	НастройкиБухучета.СтатьяФинансирования КАК СтатьяФинансирования,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(НастройкиНачислений.ЭтоРасходыФСС, ЛОЖЬ)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
	|		ИНАЧЕ НастройкиБухучета.СпособОтраженияЗарплатыВБухучете
	|	КОНЕЦ КАК СпособОтраженияЗарплатыВБухучете,
	|	ВЫБОР
	|		КОГДА НЕ &ЕстьЕНВД
	|			ТОГДА НастройкиБухучета.ОтношениеКЕНВД
	|		КОГДА ЕСТЬNULL(НастройкиНачислений.ОтношениеКЕНВД, ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)
	|			ТОГДА НастройкиБухучета.ОтношениеКЕНВД
	|		ИНАЧЕ НастройкиНачислений.ОтношениеКЕНВД
	|	КОНЕЦ КАК ОтношениеКЕНВД,
	|	НастройкиБухучета.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|	НастройкиБухучета.ДоляРаспределения КАК ДоляРаспределения,
	|	НастройкиБухучета.СтрокаСРаспределением КАК СтрокаСРаспределением
	|ПОМЕСТИТЬ ВТНастройкиБухучетаВсехСтрокПоУмолчанию
	|ИЗ
	|	ВТНачисленияДляРаспределения КАК Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиНачислений КАК НастройкиНачислений
	|		ПО Начисления.Начисление = НастройкиНачислений.Начисление
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиБухучетаПоУмолчанию КАК НастройкиБухучета
	|		ПО Начисления.Сотрудник = НастройкиБухучета.Сотрудник
	|			И Начисления.ДатаНачала = НастройкиБухучета.Период
	|			И Начисления.Подразделение = НастройкиБухучета.Подразделение
	|			И Начисления.ТерриторияВыполненияРаботВОрганизации = НастройкиБухучета.ТерриторияВыполненияРаботВОрганизации";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
	Запрос.Выполнить();
	УдалитьВТ.Добавить("ВТНастройкиБухучетаВсехСтрок");
	УдалитьВТ.Добавить("ВТНастройкиБухучетаВсехСтрокПоУмолчанию");
	
	// Имена промежуточных временных таблиц с бухучетом.
	// Таблицы "ВТБухучет..." содержат все сведения о бухучете.
	// Таблицы "...ДляБухучета" требуют дозаполнения аналитики.
	ИменаВТБухучет 		= Новый Массив;
	ИменаВТДляБухучета 	= Новый Массив;
	
	// Подготовка временных таблиц:
	// ВТОбработанныеСтроки с идентификаторами обработанных строк.
	// ВТСтрокиБезАналитики - идентификаторы строк, в которых требуется дозаполнить аналитику.
	// ВТСтатьиРасходовНачисленийСотрудников - когда Не РаботаВБюджетномУчреждении.
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 0
	|	0 КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ВТОбработанныеСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 0
	|	0 КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ВТСтрокиБезАналитики";
	Запрос.Выполнить();
	УдалитьВТ.Добавить("ВТОбработанныеСтроки");
	УдалитьВТ.Добавить("ВТСтрокиБезАналитики");
	Если ИспользоватьСтатьиФинансирования И Не РаботаВБюджетномУчреждении Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 0
		|	0 КАК ИдентификаторСтроки,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка) КАК СтатьяРасходов
		|ПОМЕСТИТЬ ВТСтатьиРасходовНачисленийСотрудников";
		Запрос.Выполнить();
		УдалитьВТ.Добавить("ВТСтатьиРасходовНачисленийСотрудников");
	КонецЕсли;
	
	// ВТБухучетНачисленийЕНВДПоЕжемесячнойДоле
	// Обработка строк, для которых нужна только доля ЕНВД, например,
	// единовременные выплаты ФСС, компенсации за задержку зарплаты.
	Если Не ИспользоватьСтатьиФинансирования Тогда
		ТаблицаСоздана = СоздатьВТБухучетНачисленийЕНВДПоЕжемесячнойДоле(Запрос, ИмяВТНачисленияИсходная);
		Если ТаблицаСоздана Тогда
			ИменаВТБухучет.Добавить("ВТБухучетНачисленийЕНВДПоЕжемесячнойДоле");
		КонецЕсли;
	КонецЕсли;
	
	// ВТБухучетНачисленийСдельногоЗаработка
	// ВТНачисленияПраздничныхСверхурочныхДляБухучета
	// ВТНачисленияПоПоказателямДляБухучета
	// ВТНачисленияПоДокументамДляБухучета
	Если Не РаспределениеСохраняемогоДС Тогда
		
		// ВТБухучетНачисленийСдельногоЗаработка
		ТаблицаСоздана = СоздатьВТБухучетНачисленийСдельногоЗаработка(Запрос, ИмяВТНачисленияИсходная, ПараметрыРаспределенияБюджет);
		Если ТаблицаСоздана Тогда
			ИменаВТБухучет.Добавить("ВТБухучетНачисленийСдельногоЗаработка");
		КонецЕсли;
		
		// ВТНачисленияПраздничныхСверхурочныхДляБухучета
		ТаблицаСоздана = СоздатьВТНачисленияПраздничныхСверхурочныхДляБухучета(Запрос, ИмяВТНачисленияИсходная, ИсходныеДанные.ИсточникДанныхОДатахНачислений, ПараметрыРаспределенияБюджет);
		Если ТаблицаСоздана Тогда
			ИменаВТДляБухучета.Добавить("ВТНачисленияПраздничныхСверхурочныхДляБухучета");
		КонецЕсли;
		
		// ВТНачисленияПоПоказателямДляБухучета
		ТаблицаСоздана = СоздатьВТНачисленияПоПоказателямДляБухучета(Запрос, ИмяВТНачисленияИсходная, ПараметрыРаспределенияБюджет);
		Если ТаблицаСоздана Тогда
			ИменаВТДляБухучета.Добавить("ВТНачисленияПоПоказателямДляБухучета");
		КонецЕсли;
		
		// ВТНачисленияПоДокументамДляБухучета
		// Начисления, для которых бухучет задан в первичном документе,
		// например, РазовоеНачисление, Премия и т.д..
		ТаблицаСоздана = СоздатьВТНачисленияПоДокументамДляБухучета(Запрос, ИмяВТНачисленияИсходная);
		Если ТаблицаСоздана Тогда
			ИменаВТДляБухучета.Добавить("ВТНачисленияПоДокументамДляБухучета");
		КонецЕсли;
		
	КонецЕсли;
	
	// ВТНастройкиНачисленийСотрудниковДляБухучета
	// Начисления сотрудников, зарегистрированных в БухучетНачисленийСотрудников.
	// Дополнительно всегда создается таблица - ВТНастройкиБухучетаНачисленийСотрудников,
	// в таблице будут все зарегистрированные настройки бухучета начислений сотрудников для дополнения аналитики.
	// Дополнительно всегда создается таблица - ВТНеДополнятьБухучетомНачисленийСотрудников.,
	ТаблицаСоздана = СоздатьВТНастройкиНачисленийСотрудниковДляБухучета(Запрос, ИмяВТНачисленияИсходная);
	Если ТаблицаСоздана Тогда
		ИменаВТДляБухучета.Добавить("ВТНастройкиНачисленийСотрудниковДляБухучета");
	КонецЕсли;
	УдалитьВТ.Добавить("ВТНастройкиБухучетаНачисленийСотрудников");
	УдалитьВТ.Добавить("ВТНеДополнятьБухучетомНачисленийСотрудников");
	
	// ВТБухучетНачисленийПоБазеСреднегоЗаработка
	// ВТБухучетНачисленийПоФактическимНачислениям
	// ВТБухучетНачисленийПоБазеСохраняемогоДС
	Если ИспользоватьСтатьиФинансирования И Не РаспределениеСохраняемогоДС Тогда
		
		ТаблицаСоздана = СоздатьВТБухучетНачисленийПоБазеСреднегоЗаработка(Запрос, ИмяВТНачисленияИсходная, ПараметрыРаспределенияПоСтатьям, ПараметрыРаспределенияБюджет);
		Если ТаблицаСоздана Тогда
			ИменаВТБухучет.Добавить("ВТБухучетНачисленийПоБазеСреднегоЗаработка");
		КонецЕсли;
		
		ТаблицаСоздана = СоздатьВТБухучетНачисленийПоФактическимНачислениям(Запрос, ИмяВТНачисленияИсходная, ПараметрыРаспределенияПоСтатьям, ПараметрыРаспределенияБюджет);
		Если ТаблицаСоздана Тогда
			ИменаВТБухучет.Добавить("ВТБухучетНачисленийПоФактическимНачислениям");
		КонецЕсли;
		
		// ВТБухучетНачисленийПоБазеСохраняемогоДС
		Если ПолучитьФункциональнуюОпцию("ИспользоватьРасчетСохраняемогоДенежногоСодержания") Тогда
			ТаблицаСоздана = СоздатьВТБухучетНачисленийПоБазеСохраняемогоДС(Запрос, ИмяВТНачисленияИсходная, ПараметрыСохраняемогоДенежногоСодержания, ПараметрыРаспределенияБюджет);
			Если ТаблицаСоздана Тогда
				ИменаВТБухучет.Добавить("ВТБухучетНачисленийПоБазеСохраняемогоДС");
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	// ВТНастройкиИзНачисленийДляБухучета
	// Начисления со стратегией КакЗаданоВидуРасчета. 
	// Дополнительно всегда создается таблица - ВТНастройкиБухучетаНачислений,
	// в таблице будут настройки бухучета всех начислений у которых задан бухучет, для дополнения аналитики.
	ТаблицаСоздана = СоздатьВТНастройкиИзНачисленийДляБухучета(Запрос, ИмяВТНачисленияИсходная);
	Если ТаблицаСоздана Тогда
		ИменаВТДляБухучета.Добавить("ВТНастройкиИзНачисленийДляБухучета");
	КонецЕсли;
	УдалитьВТ.Добавить("ВТНастройкиБухучетаНачислений");
	
	// ВТНачисленияПоБазовымРасчетам
	// Отбор начислений со стратегией отражения ПоБазовымРасчетам.
	// Когда РаспределениеСохраняемогоДС имя исходной таблицы в ИсходныеДанные.ИмяВТНачисления,
	// а в ИмяВТНачисленияИсходная таблица в которой поля Сотрудник, ФизическоеЛицо и ДатаНачала заполнены временными данными.
	ИмяВТНачисления = ?(РаспределениеСохраняемогоДС, ИсходныеДанные.ИмяВТНачисления, ИмяВТНачисленияИсходная);
	РаспределятьПоБазовымРасчетам = СоздатьВТНачисленияПоБазовымРасчетам(Запрос, ИмяВТНачисления);
	Если РаспределятьПоБазовымРасчетам Тогда
		УдалитьВТ.Добавить("ВТНачисленияПоБазовымРасчетам");
	КонецЕсли;
	
	// ВТНачисленияПоНастройкамБухучетаДляБухучета
	// Обработка оставшихся строк, получение настроек из ВТНастройкиБухучета.
	ТаблицаСоздана = СоздатьВТНачисленияПоНастройкамБухучетаДляБухучета(Запрос, ИмяВТНачисленияИсходная, ПараметрыРаспределенияБюджет);
	Если ТаблицаСоздана Тогда
		ИменаВТДляБухучета.Добавить("ВТНачисленияПоНастройкамБухучетаДляБухучета");
	КонецЕсли;
		
	// ВТБухучетНачисленийПоТаблицамДляБухучета
	// Объединение всех таблиц "...ДляБухучета",
	// дозаполнение аналитики в строках,
	// если ПрименяетсяЕНВД вычисление значения ОблагаетсяЕНВД.
	Если ИменаВТДляБухучета.Количество()>0 Тогда
		СоздатьВТБухучетНачисленийПоТаблицамДляБухучета(Запрос, ИмяВТНачисленияИсходная, ИменаВТДляБухучета);
		ИменаВТБухучет.Добавить("ВТБухучетНачисленийПоТаблицамДляБухучета");
	КонецЕсли;
	
	// ВТБухучетНачисленийБезРаспределяемыхПоБазе
	// Объединение всех таблиц "ВТБухучет..."
	Если ИменаВТБухучет.Количество() = 0 Тогда
		ТаблицаБухучет = НоваяТаблицаБухучетНачисленийПромежуточная();
		ЗарплатаКадры.СоздатьВТПоТаблицеЗначений(Запрос.МенеджерВременныхТаблиц, ТаблицаБухучет, "ВТБухучетНачисленийБезРаспределяемыхПоБазе");
	Иначе
		ШаблонТекстаЗапроса = 
		"ВЫБРАТЬ
		|	Таблица.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	Таблица.Сотрудник КАК Сотрудник,
		|	Таблица.ФизическоеЛицо КАК ФизическоеЛицо,
		|	Таблица.Начисление КАК Начисление,
		|	Таблица.ДатаНачала КАК ДатаНачала,
		|	Таблица.ДатаОкончания КАК ДатаОкончания,
		|	Таблица.Сумма КАК Сумма,
		|	Таблица.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
		|	Таблица.СтатьяФинансирования КАК СтатьяФинансирования,
		|	Таблица.СтатьяРасходов КАК СтатьяРасходов,
		|	Таблица.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	Таблица.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД
		|ПОМЕСТИТЬ ВТБухучетНачисленийБезРаспределяемыхПоБазе
		|ИЗ
		|	#ИмяТаблицы КАК Таблица";
		
		ТекстЗапроса = "";
		Для Индекс = 0 По ИменаВТБухучет.ВГраница() Цикл
			ИмяТаблицы = ИменаВТБухучет[Индекс];
			Если Индекс > 0 Тогда
				ТекстЗапроса = ТекстЗапроса + " 
				|
				|ОБЪЕДИНИТЬ ВСЕ
				|
				|";
				ФрагментТекстаЗапроса = СтрЗаменить(ШаблонТекстаЗапроса,"ПОМЕСТИТЬ ВТБухучетНачисленийБезРаспределяемыхПоБазе","");
			Иначе
				ФрагментТекстаЗапроса = ШаблонТекстаЗапроса;
			КонецЕсли;
			ФрагментТекстаЗапроса = СтрЗаменить(ФрагментТекстаЗапроса, "#ИмяТаблицы", ИмяТаблицы);
			ТекстЗапроса = ТекстЗапроса + ФрагментТекстаЗапроса;
		КонецЦикла;
		Запрос.Текст = ТекстЗапроса;
		
		Если РаспределятьПоБазовымРасчетам И РаспределениеСохраняемогоДС Тогда
			
			// В таблице ВТБухучетНачисленийБезРаспределяемыхПоБазе
			// устанавливаем исходные значения полей Сотрудник, ФизическоеЛицо, Начисление, ДатаНачала.
			
			Запрос.Текст = СтрЗаменить(Запрос.Текст,"ВТБухучетНачисленийБезРаспределяемыхПоБазе", "ВТБухучетНачисленийБезРаспределяемыхПоБазеВременная");
			Запрос.Выполнить();
			УдалитьВТ.Добавить("ВТБухучетНачисленийБезРаспределяемыхПоБазеВременная");
			
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	Таблица.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	НачисленияДляРаспределения.Сотрудник КАК Сотрудник,
			|	НачисленияДляРаспределения.ФизическоеЛицо КАК ФизическоеЛицо,
			|	НачисленияДляРаспределения.Начисление КАК Начисление,
			|	НачисленияДляРаспределения.ДатаНачала КАК ДатаНачала,
			|	Таблица.ДатаОкончания КАК ДатаОкончания,
			|	Таблица.Сумма КАК Сумма,
			|	Таблица.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
			|	Таблица.СтатьяФинансирования КАК СтатьяФинансирования,
			|	Таблица.СтатьяРасходов КАК СтатьяРасходов,
			|	Таблица.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
			|	Таблица.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД
			|ПОМЕСТИТЬ ВТБухучетНачисленийБезРаспределяемыхПоБазе
			|ИЗ
			|	ВТБухучетНачисленийБезРаспределяемыхПоБазеВременная КАК Таблица
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачисленияДляРаспределения КАК НачисленияДляРаспределения
			|		ПО Таблица.ИдентификаторСтроки = НачисленияДляРаспределения.ИдентификаторСтроки";
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИсходныеДанные.ИмяВТНачисления);
			Запрос.Выполнить();
			
		ИначеЕсли РаботаВБюджетномУчреждении Тогда
			
			// Уточнение статьи расходов оплаты БЛ за счет работодателя,
			// для уволенных сотрудников.
			УточнитьСтатьиРасходыПоСтрахованиюРаботодатель = Ложь;
			Если СтатьиРасходовПоПриказу209нПрименяются(ПериодРегистрации) Тогда
				ЗапросНачислений = Новый Запрос;
				ЗапросНачислений.МенеджерВременныхТаблиц = МенеджерВТ;
				ЗапросНачислений.Текст = 
				"ВЫБРАТЬ
				|	НачисленияДляРаспределения.ИдентификаторСтроки КАК ИдентификаторСтроки,
				|	НачисленияДляРаспределения.Сотрудник КАК Сотрудник,
				|	НачисленияДляРаспределения.ДатаНачала КАК ДатаНачала
				|ИЗ
				|	ВТНачисленияДляРаспределения КАК НачисленияДляРаспределения
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНастройкиНачислений КАК НастройкиНачислений
				|		ПО НачисленияДляРаспределения.Начисление = НастройкиНачислений.Начисление
				|			И (НастройкиНачислений.РасходыПоСтрахованиюРаботодатель)";
				ЗапросНачислений.Текст = СтрЗаменить(ЗапросНачислений.Текст, "ВТНачисленияДляРаспределения", ИсходныеДанные.ИмяВТНачисления);
				РезультатЗапроса = ЗапросНачислений.Выполнить();
				Если Не РезультатЗапроса.Пустой() Тогда
					ТаблицаСотрудников = РезультатЗапроса.Выгрузить();
					СтрокиДляКорректировки = ТаблицаСотрудников.СкопироватьКолонки();
					Сотрудники = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаСотрудников, "Сотрудник", Истина);
					КадровыеДанные = КадровыйУчет.КадровыеДанныеСотрудников(Истина, Сотрудники, "ДатаЗавершенияРаботы", КонецМесяца(ПериодРегистрации));
					Отбор = Новый Структура("Сотрудник");
					Для каждого СтрокаТЗ Из КадровыеДанные Цикл
						Если ЗначениеЗаполнено(СтрокаТЗ.ДатаЗавершенияРаботы) Тогда
							СотрудникУволен = Ложь;
							Если СтрокаТЗ.ДатаЗавершенияРаботы <= КонецМесяца(ПериодРегистрации) Тогда
								// уволен в месяце начисления пособия
								Отбор.Сотрудник = СтрокаТЗ.Сотрудник;
								НайденныеСтроки = ТаблицаСотрудников.НайтиСтроки(Отбор);
								ДатаНачала = КонецМесяца(ПериодРегистрации);
								Для каждого СтрокаПоСотруднику Из НайденныеСтроки Цикл
									Если СтрокаПоСотруднику.ДатаНачала < ДатаНачала Тогда
										ДатаНачала = СтрокаПоСотруднику.ДатаНачала;
									КонецЕсли;
								КонецЦикла;
								Если СтрокаТЗ.ДатаЗавершенияРаботы < ДатаНачала Тогда
									СотрудникУволен = Истина;
								КонецЕсли;
							Иначе
								Продолжить;
							КонецЕсли;
							Если СотрудникУволен Тогда
								Отбор.Сотрудник = СтрокаТЗ.Сотрудник;
								НайденныеСтроки = ТаблицаСотрудников.НайтиСтроки(Отбор);
								Для каждого СтрокаПоСотруднику Из НайденныеСтроки Цикл
									ЗаполнитьЗначенияСвойств(СтрокиДляКорректировки.Добавить(), СтрокаПоСотруднику);
								КонецЦикла;
							КонецЕсли;
						КонецЕсли;
					КонецЦикла;
					Если СтрокиДляКорректировки.Количество() > 0 Тогда
						ЗарплатаКадры.СоздатьВТПоТаблицеЗначений(Запрос.МенеджерВременныхТаблиц, СтрокиДляКорректировки, "ВТСтрокиДляКорректировки");
						УдалитьВТ.Добавить("ВТСтрокиДляКорректировки");
						УточнитьСтатьиРасходыПоСтрахованиюРаботодатель = Истина;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			Если Не УточнитьСтатьиРасходыПоСтрахованиюРаботодатель Тогда
				Запрос.Выполнить();
			Иначе
				
				Запрос.Текст = СтрЗаменить(Запрос.Текст,"ВТБухучетНачисленийБезРаспределяемыхПоБазе", "ВТБухучетНачисленийБезРаспределяемыхПоБазеВременная");
				Запрос.Выполнить();
				УдалитьВТ.Добавить("ВТБухучетНачисленийБезРаспределяемыхПоБазеВременная");
				
				Запрос.УстановитьПараметр("Статья264", СтатьяРасходов264());
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	Таблица.ИдентификаторСтроки КАК ИдентификаторСтроки,
				|	Таблица.Сотрудник КАК Сотрудник,
				|	Таблица.ФизическоеЛицо КАК ФизическоеЛицо,
				|	Таблица.Начисление КАК Начисление,
				|	Таблица.ДатаНачала КАК ДатаНачала,
				|	Таблица.ДатаОкончания КАК ДатаОкончания,
				|	Таблица.Сумма КАК Сумма,
				|	Таблица.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
				|	Таблица.СтатьяФинансирования КАК СтатьяФинансирования,
				|	ВЫБОР
				|		КОГДА СтрокиДляКорректировки.ИдентификаторСтроки ЕСТЬ NULL
				|			ТОГДА Таблица.СтатьяРасходов
				|		ИНАЧЕ &Статья264
				|	КОНЕЦ КАК СтатьяРасходов,
				|	Таблица.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
				|	Таблица.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД
				|ПОМЕСТИТЬ ВТБухучетНачисленийБезРаспределяемыхПоБазе
				|ИЗ
				|	ВТБухучетНачисленийБезРаспределяемыхПоБазеВременная КАК Таблица
				|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтрокиДляКорректировки КАК СтрокиДляКорректировки
				|		ПО Таблица.ИдентификаторСтроки = СтрокиДляКорректировки.ИдентификаторСтроки";
				Запрос.Выполнить();
				
			КонецЕсли;
			
		Иначе
			Запрос.Выполнить();
		КонецЕсли;
		
	КонецЕсли;
	УдалитьВТ.Добавить("ВТБухучетНачисленийБезРаспределяемыхПоБазе");
	
	// ВТБухучетНачисленийПоБазовымРасчетам
	Если Не РаспределятьПоБазовымРасчетам Тогда
		ТаблицаБухучет = НоваяТаблицаБухучетНачисленийПромежуточная();
		ЗарплатаКадры.СоздатьВТПоТаблицеЗначений(Запрос.МенеджерВременныхТаблиц, ТаблицаБухучет, "ВТБухучетНачисленийПоБазовымРасчетам");
	Иначе
		СоздатьВТБухучетНачисленийПоБазовымРасчетам(Запрос, ИмяВТНачисленияИсходная, 
						ИсходныеДанные.МенеджерКадровогоУчета, ИсходныеДанные.МенеджерДанныхУчетаВремени,
						ПараметрыРаспределенияБюджет);
	КонецЕсли;
	УдалитьВТ.Добавить("ВТБухучетНачисленийПоБазовымРасчетам");
	
	// ВТБухучетНачисленийВыходнаяТаблица - объединение сведений о бухучете начислений.
	Если Не РаботаВБюджетномУчреждении И ИспользоватьСтатьиФинансирования Тогда
		
		УчетНачисленнойЗарплатыРасширенный.СоздатьВТСтатьиРасходовНачисленийПоСпособамРасчетов(Запрос.МенеджерВременныхТаблиц);
		Запрос.УстановитьПараметр("СтатьяРасходов", СтатьяОплатаТруда());
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	БухучетНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	БухучетНачислений.Сотрудник КАК Сотрудник,
		|	БухучетНачислений.ФизическоеЛицо КАК ФизическоеЛицо,
		|	БухучетНачислений.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
		|	БухучетНачислений.Начисление КАК Начисление,
		|	БухучетНачислений.ДатаНачала КАК ДатаНачала,
		|	БухучетНачислений.ДатаОкончания КАК ДатаОкончания,
		|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
		|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
		|	БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	СУММА(БухучетНачислений.Сумма) КАК Сумма
		|ПОМЕСТИТЬ ВТБухучетНачисленийВыходнаяТаблица
		|ИЗ
		|	(ВЫБРАТЬ
		|		БухучетНачисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|		БухучетНачисления.Сотрудник КАК Сотрудник,
		|		БухучетНачисления.ФизическоеЛицо КАК ФизическоеЛицо,
		|		БухучетНачисления.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
		|		БухучетНачисления.Начисление КАК Начисление,
		|		БухучетНачисления.ДатаНачала КАК ДатаНачала,
		|		БухучетНачисления.ДатаОкончания КАК ДатаОкончания,
		|		БухучетНачисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|		БухучетНачисления.СтатьяФинансирования КАК СтатьяФинансирования,
		|		ВЫБОР
		|			КОГДА НЕ СтатьиРасходовНачисленийСотрудников.СтатьяРасходов ЕСТЬ NULL
		|				ТОГДА СтатьиРасходовНачисленийСотрудников.СтатьяРасходов
		|			КОГДА НЕ СтатьиРасходовНачислений.СтатьяРасходов ЕСТЬ NULL
		|				ТОГДА СтатьиРасходовНачислений.СтатьяРасходов
		|			ИНАЧЕ &СтатьяРасходов
		|		КОНЕЦ КАК СтатьяРасходов,
		|		БухучетНачисления.Сумма КАК Сумма,
		|		БухучетНачисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД
		|	ИЗ
		|		ВТБухучетНачисленийБезРаспределяемыхПоБазе КАК БухучетНачисления
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатьиРасходовНачисленийСотрудников КАК СтатьиРасходовНачисленийСотрудников
		|			ПО БухучетНачисления.ИдентификаторСтроки = СтатьиРасходовНачисленийСотрудников.ИдентификаторСтроки
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатьиРасходовНачисленийПоСпособамРасчетов КАК СтатьиРасходовНачислений
		|			ПО БухучетНачисления.Начисление = СтатьиРасходовНачислений.Ссылка
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		БухучетНачисления.ИдентификаторСтроки,
		|		БухучетНачисления.Сотрудник,
		|		БухучетНачисления.ФизическоеЛицо,
		|		БухучетНачисления.ПодразделениеУчетаЗатрат,
		|		БухучетНачисления.Начисление,
		|		БухучетНачисления.ДатаНачала,
		|		БухучетНачисления.ДатаОкончания,
		|		БухучетНачисления.СпособОтраженияЗарплатыВБухучете,
		|		БухучетНачисления.СтатьяФинансирования,
		|		ВЫБОР
		|			КОГДА НЕ СтатьиРасходовНачисленийСотрудников.СтатьяРасходов ЕСТЬ NULL
		|				ТОГДА СтатьиРасходовНачисленийСотрудников.СтатьяРасходов
		|			КОГДА НЕ СтатьиРасходовНачислений.СтатьяРасходов ЕСТЬ NULL
		|				ТОГДА СтатьиРасходовНачислений.СтатьяРасходов
		|			ИНАЧЕ &СтатьяРасходов
		|		КОНЕЦ,
		|		БухучетНачисления.Сумма,
		|		БухучетНачисления.ОблагаетсяЕНВД
		|	ИЗ
		|		ВТБухучетНачисленийПоБазовымРасчетам КАК БухучетНачисления
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатьиРасходовНачисленийСотрудников КАК СтатьиРасходовНачисленийСотрудников
		|			ПО БухучетНачисления.ИдентификаторСтроки = СтатьиРасходовНачисленийСотрудников.ИдентификаторСтроки
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатьиРасходовНачисленийПоСпособамРасчетов КАК СтатьиРасходовНачислений
		|			ПО БухучетНачисления.Начисление = СтатьиРасходовНачислений.Ссылка) КАК БухучетНачислений
		|
		|СГРУППИРОВАТЬ ПО
		|	БухучетНачислений.ОблагаетсяЕНВД,
		|	БухучетНачислений.ДатаОкончания,
		|	БухучетНачислений.ИдентификаторСтроки,
		|	БухучетНачислений.СтатьяРасходов,
		|	БухучетНачислений.Сотрудник,
		|	БухучетНачислений.СтатьяФинансирования,
		|	БухучетНачислений.ФизическоеЛицо,
		|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете,
		|	БухучетНачислений.Начисление,
		|	БухучетНачислений.ДатаНачала,
		|	БухучетНачислений.ПодразделениеУчетаЗатрат";
		
	Иначе
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	БухучетНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	БухучетНачислений.Сотрудник КАК Сотрудник,
		|	БухучетНачислений.ФизическоеЛицо КАК ФизическоеЛицо,
		|	БухучетНачислений.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
		|	БухучетНачислений.Начисление КАК Начисление,
		|	БухучетНачислений.ДатаНачала КАК ДатаНачала,
		|	БухучетНачислений.ДатаОкончания КАК ДатаОкончания,
		|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
		|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
		|	БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	СУММА(БухучетНачислений.Сумма) КАК Сумма
		|ПОМЕСТИТЬ ВТБухучетНачисленийВыходнаяТаблица
		|ИЗ
		|	(ВЫБРАТЬ
		|		БухучетНачисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|		БухучетНачисления.Сотрудник КАК Сотрудник,
		|		БухучетНачисления.ФизическоеЛицо КАК ФизическоеЛицо,
		|		БухучетНачисления.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
		|		БухучетНачисления.Начисление КАК Начисление,
		|		БухучетНачисления.ДатаНачала КАК ДатаНачала,
		|		БухучетНачисления.ДатаОкончания КАК ДатаОкончания,
		|		БухучетНачисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|		БухучетНачисления.СтатьяФинансирования КАК СтатьяФинансирования,
		|		БухучетНачисления.СтатьяРасходов КАК СтатьяРасходов,
		|		БухучетНачисления.Сумма КАК Сумма,
		|		БухучетНачисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД
		|	ИЗ
		|		ВТБухучетНачисленийБезРаспределяемыхПоБазе КАК БухучетНачисления
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		БухучетНачисления.ИдентификаторСтроки,
		|		БухучетНачисления.Сотрудник,
		|		БухучетНачисления.ФизическоеЛицо,
		|		БухучетНачисления.ПодразделениеУчетаЗатрат,
		|		БухучетНачисления.Начисление,
		|		БухучетНачисления.ДатаНачала,
		|		БухучетНачисления.ДатаОкончания,
		|		БухучетНачисления.СпособОтраженияЗарплатыВБухучете,
		|		БухучетНачисления.СтатьяФинансирования,
		|		БухучетНачисления.СтатьяРасходов,
		|		БухучетНачисления.Сумма,
		|		БухучетНачисления.ОблагаетсяЕНВД
		|	ИЗ
		|		ВТБухучетНачисленийПоБазовымРасчетам КАК БухучетНачисления) КАК БухучетНачислений
		|
		|СГРУППИРОВАТЬ ПО
		|	БухучетНачислений.ОблагаетсяЕНВД,
		|	БухучетНачислений.ДатаОкончания,
		|	БухучетНачислений.ИдентификаторСтроки,
		|	БухучетНачислений.СтатьяРасходов,
		|	БухучетНачислений.Сотрудник,
		|	БухучетНачислений.СтатьяФинансирования,
		|	БухучетНачислений.ФизическоеЛицо,
		|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете,
		|	БухучетНачислений.Начисление,
		|	БухучетНачислений.ДатаНачала,
		|	БухучетНачислений.ПодразделениеУчетаЗатрат";
		
	КонецЕсли;
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТБухучетНачисленийВыходнаяТаблица", ИмяВТБухучетНачислений);
	Запрос.Выполнить();
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(УдалитьВТ,ИменаВТДляБухучета);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(УдалитьВТ,ИменаВТБухучет);
	ЗарплатаКадры.УничтожитьВТ(МенеджерВТ, УдалитьВТ);
	
КонецПроцедуры

// Выполняет отражение начислений по договорам ГПХ в бухучете.
// Результат помещается во временную таблицу с именем ИмяВТБухучетНачислений,
//
// Параметры:
// 		ИсходныеДанные - Структура - описание см ОписаниеИсходныхДанныхДляОтраженияНачисленийВБухучете.
// 			Должны быть заполнены следующие свойства:
// 			* МенеджерВременныхТаблиц
// 			* ИмяВТНачисления - имя временной таблицы с начислениями по договорам.
// 			* МесяцНачисления
// 			* Организация
// 		ИмяВТБухучетНачислений - Строка - имя временной таблицы в которую помещаются результаты.
//
Процедура ПолучитьБухучетНачисленийПоДоговорамСоздатьВТ(ИсходныеДанные, ИмяВТБухучетНачислений)
	
	МенеджерВТ 				= ИсходныеДанные.МенеджерВременныхТаблиц;
	ИмяВТНачисленияИсходная = ИсходныеДанные.ИмяВТНачисления;
	Период 					= ИсходныеДанные.МесяцНачисления;
	Организация 			= ИсходныеДанные.Организация;
	
	ИспользоватьСтатьиФинансирования   = ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата");
	ПрименяетсяЕНВД = ОтражениеЗарплатыВБухучете.ПлательщикЕНВД(Организация, Период);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.УстановитьПараметр("ЕстьЕНВД", ПрименяетсяЕНВД);
	Запрос.УстановитьПараметр("ИспользоватьСтатьиФинансирования", ИспользоватьСтатьиФинансирования);
	Запрос.УстановитьПараметр("СтатьяРасходов", СтатьяОплатаТруда());
	
	УдалитьВТ = Новый Массив;
	
	// Получим сведения о настройке бухучета для договорников.
	ПараметрыПолученияНастроек = ПараметрыПолученияНастроекБухучета();
	ПараметрыПолученияНастроек.ИмяПоляПериод = "ПериодРегистрации";
	СоздатьВТИсходнаяТаблицаДляНастроекБухучета(МенеджерВТ, ИмяВТНачисленияИсходная, Период, ПараметрыПолученияНастроек);
	УдалитьВТ.Добавить("ВТИсходнаяТаблицаДляНастроекБухучета");
	
	СоздатьВТНастройкиБухучетаДляРаспределенияПоУмолчанию(МенеджерВТ, "ВТИсходнаяТаблицаДляНастроекБухучета");
	УдалитьВТ.Добавить("ВТНастройкиБухучета"); 
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	НастройкиБухучета.СтатьяФинансирования КАК СтатьяФинансирования,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	НастройкиБухучета.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	ВЫБОР
	|		КОГДА НастройкиБухучета.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ОпределяетсяЕжемесячноПроцентом)
	|				ИЛИ НастройкиБухучета.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.НеЕНВД)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ОблагаетсяЕНВД
	|ПОМЕСТИТЬ ВТБухучетДоговоровПоУмолчанию
	|ИЗ
	|	ВТНачисленияПоДоговорамГПХ КАК Начисления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНастройкиБухучета КАК НастройкиБухучета
	|		ПО Начисления.Сотрудник = НастройкиБухучета.Сотрудник
	|			И Начисления.Подразделение = НастройкиБухучета.Подразделение
	|			И Начисления.ТерриторияВыполненияРаботВОрганизации = НастройкиБухучета.ТерриторияВыполненияРаботВОрганизации
	|			И Начисления.ПериодРегистрации = НастройкиБухучета.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТНачисленияПоДоговорамГПХ.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	БухучетДоговоровГПХ.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|	БухучетДоговоровГПХ.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетДоговоровГПХ.СтатьяРасходов КАК СтатьяРасходов,
	|	БухучетДоговоровГПХ.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетДоговоровГПХ.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	БухучетДоговоровГПХ.ДоляРаспределения КАК ДоляРаспределения
	|ПОМЕСТИТЬ ВТНастройкиБухучетаДоговоров
	|ИЗ
	|	ВТНачисленияПоДоговорамГПХ КАК ВТНачисленияПоДоговорамГПХ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.БухучетДоговоровГПХ КАК БухучетДоговоровГПХ
	|		ПО ВТНачисленияПоДоговорамГПХ.ДокументОснование = БухучетДоговоровГПХ.ДоговорАкт
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ЕСТЬNULL(БухучетДоговоров.ПодразделениеУчетаЗатрат, ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)) КАК ПодразделениеУчетаЗатрат,
	|	ВЫБОР
	|		КОГДА &ИспользоватьСтатьиФинансирования
	|			ТОГДА ВЫБОР
	|					КОГДА БухучетДоговоров.СтатьяФинансирования = ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|							ИЛИ БухучетДоговоров.СтатьяФинансирования ЕСТЬ NULL
	|						ТОГДА БухучетПоУмолчанию.СтатьяФинансирования
	|					ИНАЧЕ БухучетДоговоров.СтатьяФинансирования
	|				КОНЕЦ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|	КОНЕЦ КАК СтатьяФинансирования,
	|	ВЫБОР
	|		КОГДА БухучетДоговоров.СтатьяРасходов = ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
	|				ИЛИ БухучетДоговоров.СтатьяРасходов ЕСТЬ NULL
	|			ТОГДА БухучетПоУмолчанию.СтатьяРасходов
	|		ИНАЧЕ БухучетДоговоров.СтатьяРасходов
	|	КОНЕЦ КАК СтатьяРасходов,
	|	ВЫБОР
	|		КОГДА БухучетДоговоров.СпособОтраженияЗарплатыВБухучете = ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
	|				ИЛИ БухучетДоговоров.СпособОтраженияЗарплатыВБухучете ЕСТЬ NULL
	|			ТОГДА БухучетПоУмолчанию.СпособОтраженияЗарплатыВБухучете
	|		ИНАЧЕ БухучетДоговоров.СпособОтраженияЗарплатыВБухучете
	|	КОНЕЦ КАК СпособОтраженияЗарплатыВБухучете,
	|	ВЫБОР
	|		КОГДА &ЕстьЕНВД
	|			ТОГДА ВЫБОР
	|					КОГДА БухучетДоговоров.ОблагаетсяЕНВД ЕСТЬ NULL
	|						ТОГДА БухучетПоУмолчанию.ОблагаетсяЕНВД
	|					ИНАЧЕ БухучетДоговоров.ОблагаетсяЕНВД
	|				КОНЕЦ
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОблагаетсяЕНВД,
	|	БухучетДоговоров.ДоляРаспределения КАК ДоляРаспределения
	|ИЗ
	|	ВТНачисленияПоДоговорамГПХ КАК Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиБухучетаДоговоров КАК БухучетДоговоров
	|		ПО Начисления.ИдентификаторСтроки = БухучетДоговоров.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБухучетДоговоровПоУмолчанию КАК БухучетПоУмолчанию
	|		ПО Начисления.ИдентификаторСтроки = БухучетПоУмолчанию.ИдентификаторСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Начисления.Сумма КАК Сумма
	|ИЗ
	|	ВТНачисленияПоДоговорамГПХ КАК Начисления";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияПоДоговорамГПХ", ИмяВТНачисленияИсходная);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	УдалитьВТ.Добавить("ВТБухучетДоговоровПоУмолчанию");
	УдалитьВТ.Добавить("ВТНастройкиБухучетаДоговоров");
	
	КоличествоРезультатов = РезультатЗапроса.ВГраница();
	База = РезультатЗапроса[КоличествоРезультатов - 1].Выгрузить();
	Начисления = РезультатЗапроса[КоличествоРезультатов].Выгрузить();
	
	ТаблицаБухучет = Новый ТаблицаЗначений;
	ТаблицаБухучет.Колонки.Добавить("ИдентификаторСтроки", 				Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(7, 0)));
	ТаблицаБухучет.Колонки.Добавить("ПодразделениеУчетаЗатрат", 		Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	ТаблицаБухучет.Колонки.Добавить("СтатьяФинансирования",				Новый ОписаниеТипов("СправочникСсылка.СтатьиФинансированияЗарплата"));
	ТаблицаБухучет.Колонки.Добавить("СпособОтраженияЗарплатыВБухучете", Новый ОписаниеТипов("СправочникСсылка.СпособыОтраженияЗарплатыВБухучете"));
	ТаблицаБухучет.Колонки.Добавить("СтатьяРасходов",  					Новый ОписаниеТипов("СправочникСсылка.СтатьиРасходовЗарплата"));
	ТаблицаБухучет.Колонки.Добавить("ОблагаетсяЕНВД",					Новый ОписаниеТипов("Булево"));
	ТаблицаБухучет.Колонки.Добавить("Сумма", 							Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	
	База.Индексы.Добавить("ИдентификаторСтроки");
	Отбор = Новый Структура("ИдентификаторСтроки");
	
	Для каждого СтрокаТЗ Из Начисления Цикл
		
		ЗаполнитьЗначенияСвойств(Отбор, СтрокаТЗ);
		СтрокиБазы = База.НайтиСтроки(Отбор);
		
		Если СтрокиБазы.Количество() = 0 Тогда
			НоваяСтрока = ТаблицаБухучет.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
		ИначеЕсли СтрокиБазы.Количество() = 1 Тогда
			НоваяСтрока = ТаблицаБухучет.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокиБазы[0]);
		Иначе
			
			Коэффициенты = ОбщегоНазначения.ВыгрузитьКолонку(СтрокиБазы,"ДоляРаспределения");
			Результаты = ЗарплатаКадрыКлиентСервер.РаспределитьПропорциональноКоэффициентам(СтрокаТЗ.Сумма, Коэффициенты);
			
			Если Результаты = Неопределено Тогда
				НоваяСтрока = ТаблицаБухучет.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
			Иначе
				Индекс = 0;
				Для Каждого СтрокаБазы Из СтрокиБазы Цикл
					НоваяСтрока = ТаблицаБухучет.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаБазы);
					НоваяСтрока.Сумма = Результаты[Индекс];
					Индекс = Индекс + 1;
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ЗарплатаКадры.СоздатьВТПоТаблицеЗначений(Запрос.МенеджерВременныхТаблиц, ТаблицаБухучет, "ВТБухучетДоговоровВременная");
	УдалитьВТ.Добавить("ВТБухучетДоговоровВременная");
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Начисления.Начисление КАК Начисление,
	|	Начисления.ДатаНачала КАК ДатаНачала,
	|	Начисления.ДокументОснование КАК ДокументОснование,
	|	Начисления.ВидОперации КАК ВидОперации,
	|	Бухучет.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|	Бухучет.СтатьяФинансирования КАК СтатьяФинансирования,
	|	Бухучет.СтатьяРасходов КАК СтатьяРасходов,
	|	Бухучет.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	Бухучет.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	Бухучет.Сумма КАК Сумма
	|ПОМЕСТИТЬ ВТБухучетНачисленийПоДоговорам
	|ИЗ
	|	ВТНачисленияПоДоговорамГПХ КАК Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБухучетДоговоровВременная КАК Бухучет
	|		ПО Начисления.ИдентификаторСтроки = Бухучет.ИдентификаторСтроки";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияПоДоговорамГПХ", ИмяВТНачисленияИсходная);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТБухучетНачисленийПоДоговорам", ИмяВТБухучетНачислений);
	Запрос.Выполнить();
	
	ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);

КонецПроцедуры

// Дополнительные алгоритмы обработки данных.

Процедура ДополнитьВТОбработанныеСтроки(Запрос, ИмяВТ)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Таблица.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ВТОбработанныеСтрокиВременная
	|ИЗ
	|	ВТОбработанныеСтроки КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТОбработанныеСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ВТОбработанныеСтроки
	|ИЗ
	|	ВТОбработанныеСтрокиВременная КАК Таблица
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.ИдентификаторСтроки
	|ИЗ
	|	#Таблица КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТОбработанныеСтрокиВременная";
	Запрос.Текст = СтрЗаменить(ТекстЗапроса, "#Таблица", ИмяВТ);
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ДополнитьВТСтрокиБезАналитики(Запрос, ИмяВТ)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Таблица.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ВТСтрокиБезАналитикиВременная
	|ИЗ
	|	ВТСтрокиБезАналитики КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТСтрокиБезАналитики
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ВТСтрокиБезАналитики
	|ИЗ
	|	ВТСтрокиБезАналитикиВременная КАК Таблица
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.ИдентификаторСтроки
	|ИЗ
	|	#Таблица КАК Таблица
	|ГДЕ
	|	Таблица.НеЗаполненаАналитика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТСтрокиБезАналитикиВременная";
	Запрос.Текст = СтрЗаменить(ТекстЗапроса, "#Таблица", ИмяВТ);
	Запрос.Выполнить();

КонецПроцедуры

Процедура ДополнитьВТСтатьиРасходовНачисленийСотрудников(Запрос, ИмяВТ)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Таблица.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Таблица.СтатьяРасходов КАК СтатьяРасходов
	|ПОМЕСТИТЬ ВТСтатьиРасходовНачисленийСотрудниковВременная
	|ИЗ
	|	ВТСтатьиРасходовНачисленийСотрудников КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТСтатьиРасходовНачисленийСотрудников
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Таблица.СтатьяРасходов КАК СтатьяРасходов
	|ПОМЕСТИТЬ ВТСтатьиРасходовНачисленийСотрудников
	|ИЗ
	|	ВТСтатьиРасходовНачисленийСотрудниковВременная КАК Таблица
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.ИдентификаторСтроки,
	|	Таблица.СтатьяРасходов
	|ИЗ
	|	#Таблица КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТСтатьиРасходовНачисленийСотрудниковВременная";
	Запрос.Текст = СтрЗаменить(ТекстЗапроса, "#Таблица", ИмяВТ);
	Запрос.Выполнить();

КонецПроцедуры

Процедура ДополнитьАналитику(Запрос, ТаблицаБухучета)
	
	ИдентификаторыСтрок = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаБухучета, "ИдентификаторСтроки", Истина);
	
	Запрос.УстановитьПараметр("СтрокиБезАналитики", ИдентификаторыСтрок);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиБухучета.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	НастройкиБухучета.СтатьяФинансирования КАК СтатьяФинансирования,
	|	НастройкиБухучета.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	НастройкиБухучета.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	НастройкиБухучета.ДоляРаспределения КАК ДоляРаспределения
	|ИЗ
	|	ВТНастройкиБухучетаНачисленийСотрудников КАК НастройкиБухучета
	|ГДЕ
	|	НастройкиБухучета.ИдентификаторСтроки В(&СтрокиБезАналитики)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НастройкиБухучета.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	НастройкиБухучета.СтатьяФинансирования КАК СтатьяФинансирования,
	|	НастройкиБухучета.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	НастройкиБухучета.ОтношениеКЕНВД КАК ОтношениеКЕНВД
	|ИЗ
	|	ВТНастройкиБухучетаНачислений КАК НастройкиБухучета
	|ГДЕ
	|	НастройкиБухучета.ИдентификаторСтроки В(&СтрокиБезАналитики)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НастройкиБухучета.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	НастройкиБухучета.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|	НастройкиБухучета.СтатьяФинансирования КАК СтатьяФинансирования,
	|	НастройкиБухучета.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	НастройкиБухучета.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	НастройкиБухучета.ДоляРаспределения КАК ДоляРаспределения
	|ИЗ
	|	ВТНастройкиБухучетаВсехСтрок КАК НастройкиБухучета
	|ГДЕ
	|	НастройкиБухучета.ИдентификаторСтроки В(&СтрокиБезАналитики)";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	КоличествоРезультатов = РезультатЗапроса.ВГраница();
	
	НастройкиНачисленийСотрудников = РезультатЗапроса[КоличествоРезультатов-2].Выгрузить();
	НастройкиНачисленийСотрудников.Индексы.Добавить("ИдентификаторСтроки");
	
	НастройкиИзНачислений = РезультатЗапроса[КоличествоРезультатов-1].Выгрузить();
	НастройкиИзНачислений.Индексы.Добавить("ИдентификаторСтроки");
	
	НастройкиБухучета = РезультатЗапроса[КоличествоРезультатов].Выгрузить();
	НастройкиБухучета.Индексы.Добавить("ИдентификаторСтроки");
	
	Отбор = Новый Структура("ИдентификаторСтроки");
	
	ИспользоватьСтатьиФинансирования = ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный");
	ПрименяетсяЕНВД = Запрос.Параметры.ЕстьЕНВД;
	
	СтрокиКУдалению = Новый Массив;
	НоваяТаблицаБухучета = ТаблицаБухучета.СкопироватьКолонки();
	СтрокиНовойТаблицы = Новый Массив;
	
	Для каждого СтрокаТЗ Из ТаблицаБухучета Цикл
		
		СтрокиНовойТаблицы.Очистить();
		
		ЗаполнятьСпособОтражения = Не ЗначениеЗаполнено(СтрокаТЗ.СпособОтраженияЗарплатыВБухучете);
		ЗаполнятьСтатьюФинансирования = ИспользоватьСтатьиФинансирования И Не ЗначениеЗаполнено(СтрокаТЗ.СтатьяФинансирования);
		ЗаполнятьЕНВД = ПрименяетсяЕНВД И Не ЗначениеЗаполнено(СтрокаТЗ.ОтношениеКЕНВД);
		
		Если Не ЗаполнятьСпособОтражения И Не ЗаполнятьСтатьюФинансирования И Не ЗаполнятьЕНВД Тогда
			Продолжить;
		КонецЕсли;
		
		Отбор.ИдентификаторСтроки = СтрокаТЗ.ИдентификаторСтроки;
		
		Если СтрокаТЗ.ДополнятьБухучетомНачисленийСотрудников Тогда
			// Заполнение из настроек начислений сотрудников.
			НастройкиБухучетаСтроки = НастройкиНачисленийСотрудников.НайтиСтроки(Отбор);
			Если НастройкиБухучетаСтроки.Количество() > 0  Тогда
				Если НастройкиБухучетаСтроки.Количество() = 1 Тогда
					Если ЗаполнятьСпособОтражения И ЗначениеЗаполнено(НастройкиБухучетаСтроки[0].СпособОтраженияЗарплатыВБухучете) Тогда
						СтрокаТЗ.СпособОтраженияЗарплатыВБухучете = НастройкиБухучетаСтроки[0].СпособОтраженияЗарплатыВБухучете;
						ЗаполнятьСпособОтражения = Ложь;
					КонецЕсли;
					Если ЗаполнятьСтатьюФинансирования И ЗначениеЗаполнено(НастройкиБухучетаСтроки[0].СтатьяФинансирования) Тогда
						СтрокаТЗ.СтатьяФинансирования = НастройкиБухучетаСтроки[0].СтатьяФинансирования;
						ЗаполнятьСтатьюФинансирования = Ложь;
					КонецЕсли;
					Если ЗаполнятьЕНВД И ЗначениеЗаполнено(НастройкиБухучетаСтроки[0].ОтношениеКЕНВД) Тогда
						СтрокаТЗ.ОтношениеКЕНВД = НастройкиБухучетаСтроки[0].ОтношениеКЕНВД;
						ЗаполнятьЕНВД = Ложь;
					КонецЕсли;
				Иначе
					
					Если СтрокаТЗ.Сумма = 0 Тогда
						Для каждого СтрокаБухучета Из НастройкиБухучетаСтроки Цикл
							НоваяСтрока = НоваяТаблицаБухучета.Добавить();
							ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
							Если ЗаполнятьСпособОтражения И ЗначениеЗаполнено(СтрокаБухучета.СпособОтраженияЗарплатыВБухучете) Тогда
								НоваяСтрока.СпособОтраженияЗарплатыВБухучете = СтрокаБухучета.СпособОтраженияЗарплатыВБухучете;
							КонецЕсли;
							Если ЗаполнятьСтатьюФинансирования И ЗначениеЗаполнено(СтрокаБухучета.СтатьяФинансирования) Тогда
								НоваяСтрока.СтатьяФинансирования = СтрокаБухучета.СтатьяФинансирования;
							КонецЕсли;
							Если ЗаполнятьЕНВД И ЗначениеЗаполнено(СтрокаБухучета.ОтношениеКЕНВД) Тогда
								СтрокаТЗ.ОтношениеКЕНВД = СтрокаБухучета.ОтношениеКЕНВД;
							КонецЕсли;
							СтрокиНовойТаблицы.Добавить(НоваяСтрока);
						КонецЦикла;
					Иначе
						Коэффициенты = ОбщегоНазначения.ВыгрузитьКолонку(НастройкиБухучетаСтроки,"ДоляРаспределения");
						Результаты = ЗарплатаКадрыКлиентСервер.РаспределитьПропорциональноКоэффициентам(СтрокаТЗ.Сумма, Коэффициенты);
						Если Результаты <> Неопределено Тогда
							Индекс = 0;
							Для каждого СтрокаБухучета Из НастройкиБухучетаСтроки Цикл
								
								НоваяСтрока = НоваяТаблицаБухучета.Добавить();
								ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
								Если ЗаполнятьСпособОтражения Тогда
									НоваяСтрока.СпособОтраженияЗарплатыВБухучете = СтрокаБухучета.СпособОтраженияЗарплатыВБухучете;
								КонецЕсли;
								Если ЗаполнятьСтатьюФинансирования Тогда
									НоваяСтрока.СтатьяФинансирования = СтрокаБухучета.СтатьяФинансирования;
								КонецЕсли;
								Если ЗаполнятьЕНВД Тогда
									НоваяСтрока.ОтношениеКЕНВД = СтрокаБухучета.ОтношениеКЕНВД;
								КонецЕсли;
								НоваяСтрока.Сумма = Результаты[Индекс];
								
								СтрокиНовойТаблицы.Добавить(НоваяСтрока);
								Индекс = Индекс + 1;
								
							КонецЦикла;
						КонецЕсли;
					КонецЕсли;
					
					Если СтрокиНовойТаблицы.Количество() > 0 Тогда
						СтрокиКУдалению.Добавить(СтрокаТЗ);
					КонецЕсли;
					
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если СтрокиНовойТаблицы.Количество() > 0 Тогда
			
			СтрокиНовойТаблицыКУдалению = Новый Массив;
			Для каждого СтрокаТаблицы Из СтрокиНовойТаблицы Цикл
				УдалятьСтроку = ДополнитьСтрокуБухучета(СтрокаТаблицы, Отбор, НастройкиИзНачислений, НастройкиБухучета, НоваяТаблицаБухучета,
					ИспользоватьСтатьиФинансирования, ПрименяетсяЕНВД);
				Если УдалятьСтроку Тогда
					СтрокиНовойТаблицыКУдалению.Добавить(СтрокаТаблицы);
				КонецЕсли;
			КонецЦикла;
			
			Для каждого СтрокаКУдалению Из СтрокиНовойТаблицыКУдалению Цикл
				НоваяТаблицаБухучета.Удалить(СтрокаКУдалению);
			КонецЦикла;
			
		Иначе
			
			УдалятьСтроку = ДополнитьСтрокуБухучета(СтрокаТЗ, Отбор, НастройкиИзНачислений, НастройкиБухучета, НоваяТаблицаБухучета,
				ИспользоватьСтатьиФинансирования, ПрименяетсяЕНВД);
			Если УдалятьСтроку Тогда
				СтрокиКУдалению.Добавить(СтрокаТЗ);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
		ТаблицаБухучета.Удалить(СтрокаКУдалению);
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(НоваяТаблицаБухучета,ТаблицаБухучета);
	
КонецПроцедуры

Функция СтатьяРасходовБюджетНачисления(Начисление, ПараметрыРаспределенияБюджет)

	СтатьяРасходов = ПараметрыРаспределенияБюджет.СтатьиРасходовБюджетПоВидамНачислений[Начисление];
	Если Не ЗначениеЗаполнено(СтатьяРасходов) Тогда
		СтатьяРасходов = ПараметрыРаспределенияБюджет.СтатьяРасходовОплатаТруда;
	КонецЕсли;
	
	Возврат СтатьяРасходов;
	
КонецФункции

// Конструктор таблицы значений с данными для отражения начислений в бухучете.
// Используется при формировании временных таблиц с префиксом "ВТБухучетНачислений".
//
Функция НоваяТаблицаБухучетНачисленийПромежуточная()

	ОписаниеТипаДата = Новый ОписаниеТипов("Дата",Новый КвалификаторыДаты(ЧастиДаты.Дата));
	
	МассивТиповНачисления = Новый Массив;
	МассивТиповНачисления.Добавить(Тип("ПеречислениеСсылка.ВидыОсобыхНачисленийИУдержаний"));
	МассивТиповНачисления.Добавить(Тип("ПланВидовРасчетаСсылка.Начисления"));
	МассивТиповНачисления.Добавить(Тип("СправочникСсылка.ВидыВыплатБывшимСотрудникам"));
	МассивТиповНачисления.Добавить(Тип("СправочникСсылка.ВидыПрочихДоходовФизическихЛиц"));
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("ИдентификаторСтроки", 				Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(7, 0)));
	Таблица.Колонки.Добавить("ИдентификаторСтрокиНачисления", 		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(7, 0)));
	Таблица.Колонки.Добавить("Сотрудник",  							Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	Таблица.Колонки.Добавить("ФизическоеЛицо", 						Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	Таблица.Колонки.Добавить("Начисление", 							Новый ОписаниеТипов(МассивТиповНачисления));
	Таблица.Колонки.Добавить("ДатаНачала", 							ОписаниеТипаДата);
	Таблица.Колонки.Добавить("ДатаОкончания", 						ОписаниеТипаДата);
	Таблица.Колонки.Добавить("ПодразделениеУчетаЗатрат", 			Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	Таблица.Колонки.Добавить("СтатьяФинансирования",				Новый ОписаниеТипов("СправочникСсылка.СтатьиФинансированияЗарплата"));
	Таблица.Колонки.Добавить("СпособОтраженияЗарплатыВБухучете", 	Новый ОписаниеТипов("СправочникСсылка.СпособыОтраженияЗарплатыВБухучете"));
	Таблица.Колонки.Добавить("СтатьяРасходов",  					Новый ОписаниеТипов("СправочникСсылка.СтатьиРасходовЗарплата"));
	Таблица.Колонки.Добавить("ОблагаетсяЕНВД",						Новый ОписаниеТипов("Булево"));
	Таблица.Колонки.Добавить("Сторно",								Новый ОписаниеТипов("Булево"));
	Таблица.Колонки.Добавить("Сумма", 								Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2))); 
	Таблица.Колонки.Добавить("ДополнятьБухучетомНачисленийСотрудников",Новый ОписаниеТипов("Булево"));
	
	ТипыРегистраторов = Метаданные.РегистрыНакопления.НачисленияУдержанияПоСотрудникам.СтандартныеРеквизиты.Регистратор.Тип.Типы();
	ТипыДополняемыхРегистраторов = Метаданные.РегистрыНакопления.НачисленияУдержанияПоКонтрагентамАкционерам.СтандартныеРеквизиты.Регистратор.Тип.Типы();
	Для каждого ТипРегистратора Из ТипыДополняемыхРегистраторов Цикл
		ТипыРегистраторов.Добавить(ТипРегистратора);
	КонецЦикла;
	Таблица.Колонки.Добавить("Регистратор", Новый ОписаниеТипов(ТипыРегистраторов));
	
	Возврат Таблица;

КонецФункции

// Конструктор таблицы значений с данными для отражения начислений в бухучете.
// Используется при формировании временных таблиц с постфиксом "ДляБухучета".
//
Функция НоваяТаблицаНастройкиНачисленийДляБухучета()

	ОписаниеТипаДата = Новый ОписаниеТипов("Дата",Новый КвалификаторыДаты(ЧастиДаты.Дата));
	
	МассивТиповНачисления = Новый Массив;
	МассивТиповНачисления.Добавить(Тип("ПеречислениеСсылка.ВидыОсобыхНачисленийИУдержаний"));
	МассивТиповНачисления.Добавить(Тип("ПланВидовРасчетаСсылка.Начисления"));
	МассивТиповНачисления.Добавить(Тип("СправочникСсылка.ВидыВыплатБывшимСотрудникам"));
	МассивТиповНачисления.Добавить(Тип("СправочникСсылка.ВидыПрочихДоходовФизическихЛиц"));
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("ИдентификаторСтроки", 				Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(7, 0)));
	Таблица.Колонки.Добавить("Начисление", 							Новый ОписаниеТипов(МассивТиповНачисления));
	Таблица.Колонки.Добавить("ПодразделениеУчетаЗатрат", 			Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	Таблица.Колонки.Добавить("СтатьяФинансирования",				Новый ОписаниеТипов("СправочникСсылка.СтатьиФинансированияЗарплата"));
	Таблица.Колонки.Добавить("СпособОтраженияЗарплатыВБухучете", 	Новый ОписаниеТипов("СправочникСсылка.СпособыОтраженияЗарплатыВБухучете"));
	Таблица.Колонки.Добавить("СтатьяРасходов",  					Новый ОписаниеТипов("СправочникСсылка.СтатьиРасходовЗарплата"));
	Таблица.Колонки.Добавить("ОтношениеКЕНВД",						Новый ОписаниеТипов("ПеречислениеСсылка.ОтношениеКЕНВДЗатратНаЗарплату"));
	Таблица.Колонки.Добавить("Сумма", 								Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	Таблица.Колонки.Добавить("НеЗаполненаАналитика",				Новый ОписаниеТипов("Булево"));
	
	Возврат Таблица;

КонецФункции

// Конструктор таблицы значений с данными для отражения начислений в бухучете.
// Используется при формировании данных, предназначенных для передачи в бухгалтерскую программу.
//
Функция НоваяТаблицаДанныеДляОтраженияВБухучетеНачислений()

	ОписаниеТипаДата = Новый ОписаниеТипов("Дата",Новый КвалификаторыДаты(ЧастиДаты.Дата));
	
	МассивТиповНачисления = Новый Массив;
	МассивТиповНачисления.Добавить(Тип("ПеречислениеСсылка.ВидыОсобыхНачисленийИУдержаний"));
	МассивТиповНачисления.Добавить(Тип("ПланВидовРасчетаСсылка.Начисления"));
	МассивТиповНачисления.Добавить(Тип("СправочникСсылка.ВидыВыплатБывшимСотрудникам"));
	МассивТиповНачисления.Добавить(Тип("СправочникСсылка.ВидыПрочихДоходовФизическихЛиц"));
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Сотрудник",  							Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	Таблица.Колонки.Добавить("ФизическоеЛицо", 						Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	Таблица.Колонки.Добавить("Подразделение", 						Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	Таблица.Колонки.Добавить("ПодразделениеУчетаЗатрат", 			Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	Таблица.Колонки.Добавить("Начисление", 							Новый ОписаниеТипов(МассивТиповНачисления));
	Таблица.Колонки.Добавить("ВидОперации", 						Новый ОписаниеТипов("ПеречислениеСсылка.ВидыОперацийПоЗарплате"));
	Таблица.Колонки.Добавить("СтатьяФинансирования",				Новый ОписаниеТипов("СправочникСсылка.СтатьиФинансированияЗарплата"));
	Таблица.Колонки.Добавить("СтатьяРасходов",  					Новый ОписаниеТипов("СправочникСсылка.СтатьиРасходовЗарплата"));
	Таблица.Колонки.Добавить("СпособОтраженияЗарплатыВБухучете", 	Новый ОписаниеТипов("СправочникСсылка.СпособыОтраженияЗарплатыВБухучете"));
	Таблица.Колонки.Добавить("ОблагаетсяЕНВД",						Новый ОписаниеТипов("Булево"));
	Таблица.Колонки.Добавить("ДатаНачала", 							ОписаниеТипаДата);
	Таблица.Колонки.Добавить("ПериодПринятияРасходов", 				ОписаниеТипаДата);
	Таблица.Колонки.Добавить("ДокументОснование", 					Новый ОписаниеТипов(Метаданные.ОпределяемыеТипы.ОснованиеНачисленияУдержания.Тип));
	Таблица.Колонки.Добавить("Сумма", 								Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	
	Возврат Таблица;

КонецФункции

Процедура ОбработатьСтрокиСРаспределением(Запрос, ИмяВТ)

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Таблица1.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	КОЛИЧЕСТВО(Таблица1.Начисление) КАК Количество
	|ИЗ
	|	ИмяВТ КАК Таблица1
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИмяВТ КАК Таблица2
	|		ПО Таблица1.ИдентификаторСтроки = Таблица2.ИдентификаторСтроки
	|
	|СГРУППИРОВАТЬ ПО
	|	Таблица1.ИдентификаторСтроки
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(Таблица1.Начисление) > 1";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ИмяВТ", ИмяВТ);
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		
		ТаблицаДляБухучета = НоваяТаблицаНастройкиНачисленийДляБухучета();
		ИдентификаторыСРаспределением = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("ИдентификаторСтроки");
		
		Запрос.УстановитьПараметр("ИдентификаторыСРаспределением", ИдентификаторыСРаспределением);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Таблица.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	Таблица.Начисление КАК Начисление,
		|	Таблица.Сумма КАК Сумма,
		|	Таблица.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
		|	Таблица.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	Таблица.СтатьяФинансирования КАК СтатьяФинансирования,
		|	Таблица.СтатьяРасходов КАК СтатьяРасходов,
		|	Таблица.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
		|	Таблица.НеЗаполненаАналитика КАК НеЗаполненаАналитика,
		|	Таблица.ДоляРаспределения КАК ДоляРаспределения
		|ИЗ
		|	ИмяВТ КАК Таблица
		|ГДЕ
		|	НЕ Таблица.ИдентификаторСтроки В (&ИдентификаторыСРаспределением)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Таблица.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	Таблица.Начисление КАК Начисление,
		|	Таблица.Сумма КАК Сумма,
		|	Таблица.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
		|	Таблица.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	Таблица.СтатьяФинансирования КАК СтатьяФинансирования,
		|	Таблица.СтатьяРасходов КАК СтатьяРасходов,
		|	Таблица.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
		|	Таблица.НеЗаполненаАналитика КАК НеЗаполненаАналитика,
		|	Таблица.ДоляРаспределения КАК ДоляРаспределения
		|ИЗ
		|	ИмяВТ КАК Таблица
		|ГДЕ
		|	Таблица.ИдентификаторСтроки В(&ИдентификаторыСРаспределением)";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ИмяВТ", ИмяВТ);
		РезультатЗапроса = Запрос.ВыполнитьПакет();
		ТаблицаБезРаспределения = РезультатЗапроса[0].Выгрузить();
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаБезРаспределения, ТаблицаДляБухучета);
		
		ТаблицаСРаспределением = РезультатЗапроса[1].Выгрузить();
		ТаблицаСРаспределением.Индексы.Добавить("ИдентификаторСтроки");
		Отбор = Новый Структура("ИдентификаторСтроки");
		
		Для каждого ИдентификаторСтроки Из ИдентификаторыСРаспределением Цикл
			
			Отбор.ИдентификаторСтроки = ИдентификаторСтроки;
			СтрокиРаспределения = ТаблицаСРаспределением.НайтиСтроки(Отбор);
			
			РаспределяемаяСумма = СтрокиРаспределения[0].Сумма;
			Коэффициенты = ОбщегоНазначения.ВыгрузитьКолонку(СтрокиРаспределения,"ДоляРаспределения");
			Результаты = ЗарплатаКадрыКлиентСервер.РаспределитьПропорциональноКоэффициентам(РаспределяемаяСумма, Коэффициенты);
			
			Если Результаты = Неопределено Тогда
				НоваяСтрока = ТаблицаДляБухучета.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокиРаспределения[0]);
			Иначе
				Индекс = 0;
				Для Каждого СтрокаРаспределения Из СтрокиРаспределения Цикл
					НоваяСтрока = ТаблицаДляБухучета.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРаспределения);
					НоваяСтрока.Сумма = Результаты[Индекс];
					Индекс = Индекс + 1;
				КонецЦикла;
			КонецЕсли;
			
		КонецЦикла;
		
		ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, ИмяВТ);
		ЗарплатаКадры.СоздатьВТПоТаблицеЗначений(Запрос.МенеджерВременныхТаблиц, ТаблицаДляБухучета, ИмяВТ);
		
	КонецЕсли;

КонецПроцедуры

Функция ДополнитьСтрокуБухучета(СтрокаДляОбработки, Отбор, НастройкиИзНачислений, НастройкиБухучета, НоваяТаблицаБухучета,
			ИспользоватьСтатьиФинансирования, ПрименяетсяЕНВД)
			
	УдалятьСтроку = Ложь;
	ЗаполнятьСпособОтражения 		= Не ЗначениеЗаполнено(СтрокаДляОбработки.СпособОтраженияЗарплатыВБухучете);
	ЗаполнятьСтатьюФинансирования 	= ИспользоватьСтатьиФинансирования И Не ЗначениеЗаполнено(СтрокаДляОбработки.СтатьяФинансирования);
	ЗаполнятьЕНВД 					= ПрименяетсяЕНВД И Не ЗначениеЗаполнено(СтрокаДляОбработки.ОтношениеКЕНВД);
	
	Если Не ЗаполнятьСпособОтражения И Не ЗаполнятьСтатьюФинансирования И Не ЗаполнятьЕНВД Тогда
		Возврат УдалятьСтроку;
	КонецЕсли;
	
	// Заполнение из настроек указанных в начислении.
	НастройкиБухучетаСтроки = НастройкиИзНачислений.НайтиСтроки(Отбор);
	Если НастройкиБухучетаСтроки.Количество() > 0  Тогда
		Если ЗаполнятьСпособОтражения И ЗначениеЗаполнено(НастройкиБухучетаСтроки[0].СпособОтраженияЗарплатыВБухучете) Тогда
			СтрокаДляОбработки.СпособОтраженияЗарплатыВБухучете = НастройкиБухучетаСтроки[0].СпособОтраженияЗарплатыВБухучете;
			ЗаполнятьСпособОтражения = Ложь;
		КонецЕсли;
		Если ЗаполнятьСтатьюФинансирования И ЗначениеЗаполнено(НастройкиБухучетаСтроки[0].СтатьяФинансирования) Тогда
			СтрокаДляОбработки.СтатьяФинансирования = НастройкиБухучетаСтроки[0].СтатьяФинансирования;
			ЗаполнятьСтатьюФинансирования = Ложь;
		КонецЕсли;
		Если ЗаполнятьЕНВД И ЗначениеЗаполнено(НастройкиБухучетаСтроки[0].ОтношениеКЕНВД) Тогда
			СтрокаДляОбработки.ОтношениеКЕНВД = НастройкиБухучетаСтроки[0].ОтношениеКЕНВД;
			ЗаполнятьЕНВД = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗаполнятьСпособОтражения И Не ЗаполнятьСтатьюФинансирования И Не ЗаполнятьЕНВД Тогда
		Возврат УдалятьСтроку;
	КонецЕсли;
	
	// Заполнение из настроек бухучете.
	// Дополнительно заполняем ПодразделениеУчетаЗатрат.
	ЗаполнятьПодразделение = Не ЗначениеЗаполнено(СтрокаДляОбработки.ПодразделениеУчетаЗатрат);
	НастройкиБухучетаСтроки = НастройкиБухучета.НайтиСтроки(Отбор);
	Если НастройкиБухучетаСтроки.Количество() = 1 Тогда
		Если ЗаполнятьПодразделение Тогда
			СтрокаДляОбработки.ПодразделениеУчетаЗатрат = НастройкиБухучетаСтроки[0].ПодразделениеУчетаЗатрат;
		КонецЕсли;
		Если ЗаполнятьСпособОтражения Тогда
			СтрокаДляОбработки.СпособОтраженияЗарплатыВБухучете = НастройкиБухучетаСтроки[0].СпособОтраженияЗарплатыВБухучете;
		КонецЕсли;
		Если ЗаполнятьСтатьюФинансирования Тогда
			СтрокаДляОбработки.СтатьяФинансирования = НастройкиБухучетаСтроки[0].СтатьяФинансирования;
		КонецЕсли;
		Если ЗаполнятьЕНВД Тогда
			СтрокаДляОбработки.ОтношениеКЕНВД = НастройкиБухучетаСтроки[0].ОтношениеКЕНВД;
		КонецЕсли;
	Иначе
		
		УдалятьСтроку = Истина;
		
		Если СтрокаДляОбработки.Сумма = 0 Тогда
			Для каждого СтрокаБухучета Из НастройкиБухучетаСтроки Цикл
				НоваяСтрока = НоваяТаблицаБухучета.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДляОбработки);
				Если ЗаполнятьПодразделение Тогда
					НоваяСтрока.ПодразделениеУчетаЗатрат = СтрокаБухучета.ПодразделениеУчетаЗатрат;
				КонецЕсли;
				Если ЗаполнятьСпособОтражения Тогда
					НоваяСтрока.СпособОтраженияЗарплатыВБухучете = СтрокаБухучета.СпособОтраженияЗарплатыВБухучете;
				КонецЕсли;
				Если ЗаполнятьСтатьюФинансирования Тогда
					НоваяСтрока.СтатьяФинансирования = СтрокаБухучета.СтатьяФинансирования;
				КонецЕсли;
				Если ЗаполнятьЕНВД Тогда
					НоваяСтрока.ОтношениеКЕНВД = СтрокаБухучета.ОтношениеКЕНВД;
				КонецЕсли;
			КонецЦикла;
		Иначе
			
			Коэффициенты = ОбщегоНазначения.ВыгрузитьКолонку(НастройкиБухучетаСтроки,"ДоляРаспределения");
			Результаты = ЗарплатаКадрыКлиентСервер.РаспределитьПропорциональноКоэффициентам(СтрокаДляОбработки.Сумма, Коэффициенты);
			
			Если Результаты = Неопределено Тогда
				ЗаполнитьЗначенияСвойств(НоваяТаблицаБухучета.Добавить(), СтрокаДляОбработки);
			Иначе
				
				Индекс = 0;
				Для каждого СтрокаБухучета Из НастройкиБухучетаСтроки Цикл
					
					НоваяСтрока = НоваяТаблицаБухучета.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДляОбработки);
					Если ЗаполнятьПодразделение Тогда
						НоваяСтрока.ПодразделениеУчетаЗатрат = СтрокаБухучета.ПодразделениеУчетаЗатрат;
					КонецЕсли;
					Если ЗаполнятьСпособОтражения Тогда
						НоваяСтрока.СпособОтраженияЗарплатыВБухучете = СтрокаБухучета.СпособОтраженияЗарплатыВБухучете;
					КонецЕсли;
					Если ЗаполнятьСтатьюФинансирования Тогда
						НоваяСтрока.СтатьяФинансирования = СтрокаБухучета.СтатьяФинансирования;
					КонецЕсли;
					Если ЗаполнятьЕНВД Тогда
						НоваяСтрока.ОтношениеКЕНВД = СтрокаБухучета.ОтношениеКЕНВД;
					КонецЕсли;
					НоваяСтрока.Сумма = Результаты[Индекс];
					
					Индекс = Индекс + 1;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат УдалятьСтроку;

КонецФункции

Функция БазаСреднегоЗаработкаПоКатегорииНачисления(НачисленияРКиСН, КатегорияНачисления, СтрокиБазы)
	
	База = НоваяТаблицаКоэффициентыРаспределенияСреднегоЗаработка();
	База.Колонки.Удалить("Начисление");
	
	НачислениеОтбора = НачисленияРКиСН[КатегорияНачисления];
	Если ЗначениеЗаполнено(НачислениеОтбора) Тогда
		
		Для каждого СтрокаТЗ Из СтрокиБазы Цикл
			Если СтрокаТЗ.Начисление = НачислениеОтбора Тогда
				ЗаполнитьЗначенияСвойств(База.Добавить(), СтрокаТЗ);
			КонецЕсли;
		КонецЦикла;
		
	Иначе
		
		ИсключаемыеНачисления = Новый Соответствие;
		Для каждого КлючИЗначение Из НачисленияРКиСН Цикл
			ИсключаемыеНачисления.Вставить(КлючИЗначение.Значение, Истина);
		КонецЦикла;
		
		Для каждого СтрокаТЗ Из СтрокиБазы Цикл
			Если ИсключаемыеНачисления[СтрокаТЗ.Начисление] = Истина Тогда
				Продолжить;
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(База.Добавить(), СтрокаТЗ);
		КонецЦикла;
		
	КонецЕсли;
	
	СвернутьТаблицуКоэффициентовРаспределенияСреднегоЗаработка(База);
	
	Возврат База;

КонецФункции

Функция БазаФактическихНачисленийПоКатегорииНачисления(НачисленияРКиСН, КатегорияНачисления, СтрокиБазы)
	
	База = СтрокиБазы.СкопироватьКолонки();
	База.Колонки.Удалить("Начисление");
	
	НачислениеОтбора = НачисленияРКиСН[КатегорияНачисления];
	Если ЗначениеЗаполнено(НачислениеОтбора) Тогда
		
		Для каждого СтрокаТЗ Из СтрокиБазы Цикл
			Если СтрокаТЗ.Начисление = НачислениеОтбора Тогда
				ЗаполнитьЗначенияСвойств(База.Добавить(), СтрокаТЗ);
			КонецЕсли;
		КонецЦикла;
		
	Иначе
		
		ИсключаемыеНачисления = Новый Соответствие;
		Для каждого КлючИЗначение Из НачисленияРКиСН Цикл
			ИсключаемыеНачисления.Вставить(КлючИЗначение.Значение, Истина);
		КонецЦикла;
		
		Для каждого СтрокаТЗ Из СтрокиБазы Цикл
			Если ИсключаемыеНачисления[СтрокаТЗ.Начисление] = Истина Тогда
				Продолжить;
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(База.Добавить(), СтрокаТЗ);
		КонецЦикла;
		
	КонецЕсли;
	
	ОтражениеЗарплатыВУчете.СвернутьТаблицу(База);
	
	Возврат База;

КонецФункции

Функция БазаСохраняемогоДенежногоСодержанияПоКатегорииНачисления(НачисленияРКиСН, КатегорияНачисления, СтрокиБазы)
	
	База = НоваяТаблицаКоэффициентыРаспределенияСохраняемогоДС();
	База.Колонки.Удалить("Начисление");
	
	НачислениеОтбора = НачисленияРКиСН[КатегорияНачисления];
	Если ЗначениеЗаполнено(НачислениеОтбора) Тогда
		
		Для каждого СтрокаТЗ Из СтрокиБазы Цикл
			Если СтрокаТЗ.Начисление = НачислениеОтбора Тогда
				ЗаполнитьЗначенияСвойств(База.Добавить(), СтрокаТЗ);
			КонецЕсли;
		КонецЦикла;
		
	Иначе
		
		ИсключаемыеНачисления = Новый Соответствие;
		Для каждого КлючИЗначение Из НачисленияРКиСН Цикл
			ИсключаемыеНачисления.Вставить(КлючИЗначение.Значение, Истина);
		КонецЦикла;
		
		Для каждого СтрокаТЗ Из СтрокиБазы Цикл
			Если ИсключаемыеНачисления[СтрокаТЗ.Начисление] = Истина Тогда
				Продолжить;
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(База.Добавить(), СтрокаТЗ);
		КонецЦикла;
		
	КонецЕсли;
	
	ОтражениеЗарплатыВУчете.СвернутьТаблицу(База);
	
	Возврат База;

КонецФункции

#КонецОбласти

#Область ОбслуживаниеОценочныхОбязательств

Функция НачисленияИВзносыДляРасчетаОценочныхОбязательствОтпусков(Организация, ПериодРегистрации, Сотрудники)
		
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
		БухучетНачислений 	= БухучетНачисленийПоСтатьям(Организация, ПериодРегистрации, Новый Структура("Сотрудники", Сотрудники));
		БухучетВзносов 		= БухучетВзносовПоСтатьям(Организация, ПериодРегистрации, Сотрудники);
	Иначе
		
		ТаблицаНачислений = ОтражениеЗарплатыВУчете.ДанныеДляОтражениеВУчетеНачислений(Организация, ПериодРегистрации, Сотрудники);
		БухучетНачислений = БухучетНачислений(Организация, ПериодРегистрации, ТаблицаНачислений);
		
		ТаблицаВзносов = ОтражениеЗарплатыВУчете.ДанныеДляОтражениеВУчетеВзносов(Организация, ПериодРегистрации, Сотрудники);
		БухучетВзносов = ОтражениеЗарплатыВБухучете.БухучетСтраховыхВзносов(Организация, ПериодРегистрации, ТаблицаВзносов, БухучетНачислений);
		
	КонецЕсли;
	
	Возврат БухучетНачисленийИВзносов(БухучетНачислений, БухучетВзносов);
	
КонецФункции

// Заполняет параметры для расчета оценочных обязательств по отпускам.
//
// Параметры:
// 	Организация - СправочникСсылка.Организации.
// 	ПериодРегистрации - Дата.
// 	ПараметрыДляРасчета - Структура - Параметры, которые будут заполняться.
//                        См. ОтражениеЗарплатыВБухучете.ОписаниеПараметровДляРасчетаОценочныхОбязательствОтпусков.
// 	Сотрудники - Массив, Неопределенно.
//
Процедура ЗаполнитьПараметрыДляРасчетаОценочныхОбязательствОтпусков(Организация, ПериодРегистрации, ПараметрыДляРасчета, Сотрудники) Экспорт

	ПодработкаСотрудник = Новый Соответствие;
	СотрудникиДляНачисленийИВзносов = Сотрудники;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПодработки") И Сотрудники <> Неопределено Тогда
		
		// Для получения начислений и взносов дополним сотрудников подработками.
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Сотрудники", Сотрудники);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Сотрудники.Ссылка КАК Сотрудник,
		|	Сотрудники.ГоловнойСотрудник КАК ГоловнойСотрудник
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.ГоловнойСотрудник В(&Сотрудники)
		|	И Сотрудники.Ссылка <> Сотрудники.ГоловнойСотрудник";
		РезультатЗапроса = Запрос.Выполнить();
		
		Если Не РезультатЗапроса.Пустой() Тогда
			
			СотрудникиДляНачисленийИВзносов = ОбщегоНазначения.СкопироватьРекурсивно(Сотрудники);
			
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				ПодработкаСотрудник.Вставить(Выборка.Сотрудник, Выборка.ГоловнойСотрудник);
				СотрудникиДляНачисленийИВзносов.Добавить(Выборка.Сотрудник);
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	НачисленнаяЗарплатаИВзносы = НачисленияИВзносыДляРасчетаОценочныхОбязательствОтпусков(Организация, ПериодРегистрации, СотрудникиДляНачисленийИВзносов);
	ФондОплатыТрудаИСтраховыеВзносы = ОтражениеЗарплатыВБухучете.ФондОплатыТрудаИСтраховыеВзносыДляРасчетаОценочныхОбязательствОтпусков(Организация, ПериодРегистрации, НачисленнаяЗарплатаИВзносы);
	
	Отбор = Новый Структура("РасчетПоСохраняемомуЗаработку", Ложь);
	ТаблицаФОТ = ФондОплатыТрудаИСтраховыеВзносы.Скопировать(Отбор);
	УдалитьСтрокиИсключаемыеИзРасчетаСреднегоЗаработка(ТаблицаФОТ, Организация, ПериодРегистрации);
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаФОТ, ПараметрыДляРасчета.ФондОплатыТрудаИСтраховыеВзносы);
	
	Отбор.РасчетПоСохраняемомуЗаработку = Истина;
	ТаблицаФОТ = ФондОплатыТрудаИСтраховыеВзносы.Скопировать(Отбор);
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба.РасчетДенежногоСодержания") Тогда
		МодульРасчетДенежногоСодержания = ОбщегоНазначения.ОбщийМодуль("РасчетДенежногоСодержания");
		БазовыеНачисленияОтпуска = МодульРасчетДенежногоСодержания.БазаОтпускаПоСохраняемомуЗаработкуДляОценочныхОбязательствОтпусков();
		ПересчитатьСуммыФОТПоСохраняемомуЗаработку(ТаблицаФОТ, БазовыеНачисленияОтпуска, Организация, ПериодРегистрации);
	КонецЕсли;
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаФОТ, ПараметрыДляРасчета.ФондОплатыТрудаИСтраховыеВзносы);
	
	ОстаткиОтпусковДляРасчетаОценочныхОбязательств = ОтражениеЗарплатыВБухучете.ОстаткиОтпусковДляРасчетаОценочныхОбязательств(Организация, ПериодРегистрации, Сотрудники, "Подразделение,ТерриторияВыполненияРаботВОрганизации");
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ОстаткиОтпусковДляРасчетаОценочныхОбязательств, ПараметрыДляРасчета.ОстаткиОтпусков);
	
	НастройкиБухучетаСотрудников = НастройкиБухучетаСотрудниковДляРасчетаОбязательств(Организация, ПериодРегистрации, Сотрудники);
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(НастройкиБухучетаСотрудников, ПараметрыДляРасчета.НастройкиБухучетаСотрудников);
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОрганизационнаяСтруктура") Тогда 
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОрганизационнаяСтруктура");
		ПараметрыДляРасчета.ОстаткиОтпусков.ЗаполнитьЗначения(КонецМесяца(ПериодРегистрации),"ДатаНачала");
		ПараметрыДляРасчета.НастройкиБухучетаСотрудников.ЗаполнитьЗначения(КонецМесяца(ПериодРегистрации),"ДатаНачала");
		ИменаТаблиц = "ФондОплатыТрудаИСтраховыеВзносы,ОстаткиОтпусков,НастройкиБухучетаСотрудников";
		Модуль.ДополнитьДанныеДокументаМестомВСтруктуреПредприятия(ПараметрыДляРасчета, ИменаТаблиц);
	КонецЕсли;
	
	Для каждого ЭлементСтруктуры Из ПараметрыДляРасчета Цикл
		Если ЭлементСтруктуры.Значение.Колонки.Найти("ПодразделениеУчетаЗатрат") <> Неопределено Тогда
			ЭлементСтруктуры.Значение.Колонки.Удалить("Подразделение");
			ЭлементСтруктуры.Значение.Колонки.ПодразделениеУчетаЗатрат.Имя = "Подразделение";
		КонецЕсли;
	КонецЦикла;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПодработки") И Сотрудники = Неопределено Тогда
		
		СотрудникиНачисленийИВзносов = ОбщегоНазначения.ВыгрузитьКолонку(ПараметрыДляРасчета.ФондОплатыТрудаИСтраховыеВзносы, "Сотрудник", Истина);
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Сотрудники", СотрудникиНачисленийИВзносов);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Сотрудники.Ссылка КАК Сотрудник,
		|	Сотрудники.ГоловнойСотрудник КАК ГоловнойСотрудник
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.Ссылка В(&Сотрудники)
		|	И Сотрудники.Ссылка <> Сотрудники.ГоловнойСотрудник";
		РезультатЗапроса = Запрос.Выполнить();
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			ПодработкаСотрудник.Вставить(Выборка.Сотрудник, Выборка.ГоловнойСотрудник);
		КонецЦикла;
		
	КонецЕсли;
	
	Если ПодработкаСотрудник.Количество() > 0 Тогда
		Для каждого СтрокаТЗ Из ПараметрыДляРасчета.ФондОплатыТрудаИСтраховыеВзносы Цикл
			ГоловнойСотрудник = ПодработкаСотрудник[СтрокаТЗ.Сотрудник];
			Если ГоловнойСотрудник <> Неопределено Тогда
				СтрокаТЗ.Сотрудник = ГоловнойСотрудник;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// Не выполняем свертку таблицы ОстаткиОтпусков, т.к. в ней могут быть сотрудники с нулевыми остатками.
	ВременныеПараметры = Новый Структура;
	ВременныеПараметры.Вставить("ФондОплатыТрудаИСтраховыеВзносы", ПараметрыДляРасчета.ФондОплатыТрудаИСтраховыеВзносы);
	ОтражениеЗарплатыВБухучете.СвернутьДанныеДляОтраженияЗарплатыВБухучете(ВременныеПараметры, "ДатаНачала,Начисление");
	
	ПараметрыДляРасчета.ФондОплатыТрудаИСтраховыеВзносы = ВременныеПараметры.ФондОплатыТрудаИСтраховыеВзносы;
	ПараметрыДляРасчета.ОстаткиОтпусков.Колонки.Удалить("ДатаНачала");
	ПараметрыДляРасчета.НастройкиБухучетаСотрудников.Колонки.Удалить("ДатаНачала");

КонецПроцедуры

Процедура СоздатьВТНачисленияБазаОтпуска(МенеджерВременныхТаблиц)

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НастройкиНачислений.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА НастройкиНачислений.Ссылка.СтратегияОтраженияВСреднемЗаработке = ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазовымРасчетам)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПоБазовымРасчетам
	|ПОМЕСТИТЬ ВТНачисленияБазаОтпуска
	|ИЗ
	|	ПланВидовРасчета.Начисления.СреднийЗаработокОбщий КАК НастройкиНачислений
	|ГДЕ
	|	НастройкиНачислений.Значение <> ЗНАЧЕНИЕ(Перечисление.УчетНачисленийВСреднемЗаработкеОбщий.НеВключать)";
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура УдалитьСтрокиИсключаемыеИзРасчетаСреднегоЗаработка(ИсходнаяТаблица, Организация, ПериодРегистрации)
	
	Если ИсходнаяТаблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Сотрудники = ОбщегоНазначения.ВыгрузитьКолонку(ИсходнаяТаблица, "Сотрудник", Истина);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	Запрос.УстановитьПараметр("Сотрудники", Сотрудники);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Начисления.Регистратор КАК Регистратор,
	|	Начисления.ПериодДействия КАК ПериодДействия,
	|	Начисления.НачислениеУдержание КАК Начисление,
	|	Начисления.ДатаНачала КАК ДатаНачала,
	|	Начисления.ДатаОкончания КАК ДатаОкончания,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.Подразделение КАК Подразделение,
	|	СУММА(Начисления.Сумма) КАК Сумма
	|ПОМЕСТИТЬ ВТНачисления
	|ИЗ
	|	РегистрНакопления.НачисленияУдержанияПоСотрудникам КАК Начисления
	|ГДЕ
	|	Начисления.Организация = &Организация
	|	И Начисления.ГруппаНачисленияУдержанияВыплаты В (ЗНАЧЕНИЕ(Перечисление.ГруппыНачисленияУдержанияВыплаты.Начислено), ЗНАЧЕНИЕ(Перечисление.ГруппыНачисленияУдержанияВыплаты.Льготы))
	|	И Начисления.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Начисления.Сотрудник В(&Сотрудники)
	|
	|СГРУППИРОВАТЬ ПО
	|	Начисления.НачислениеУдержание,
	|	Начисления.ПериодДействия,
	|	Начисления.Регистратор,
	|	Начисления.ДатаНачала,
	|	Начисления.ДатаОкончания,
	|	Начисления.Сотрудник,
	|	Начисления.Подразделение";
	Запрос.Выполнить();
	
	// Обработка начислений, исключаемых из среднего заработка в периоде командировок.
	// Если есть исключаемые командировки, в этой переменной будет таблица значений с полями:
	// Регистратор,Сотрудник,Подразделение,Начисление,ДатаНачала,Сумма,РезультатБаза. 
	НачисленияИсключаемыеВПериодКомандировок = Неопределено;
	ОбработатьСтрокиНачисленийИсключаемыеВПериодКомандировок(Запрос.МенеджерВременныхТаблиц, ИсходнаяТаблица, ПериодРегистрации, НачисленияИсключаемыеВПериодКомандировок);
	
	// Исключение из ФОТ РК и СН, приходящихся на не включаемые в расчет среднего суммы.
	ОбработатьСтрокиНачисленийИсключаемыеИзРаспределяемыхПоБазеСреднего(Запрос.МенеджерВременныхТаблиц, ИсходнаяТаблица, ПериодРегистрации, НачисленияИсключаемыеВПериодКомандировок);
	
КонецПроцедуры

Процедура ОбработатьСтрокиНачисленийИсключаемыеВПериодКомандировок(МенеджерВременныхТаблиц, ИсходнаяТаблица, ПериодРегистрации, НачисленияИсключаемыеВПериодКомандировок)

	ИсключатьНачисленияВПериодКомандировок = Ложь;
	УчетСреднегоЗаработка.СоздатьВТДляИсключенияКомандировок(МенеджерВременныхТаблиц, ИсключатьНачисленияВПериодКомандировок);
	
	УдалитьВТ = Новый массив;
	
	Если ИсключатьНачисленияВПериодКомандировок Тогда
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Начисления.Регистратор КАК Регистратор
		|ИЗ
		|	ВТНачисления КАК Начисления";
		РегистраторыДляОтбора = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
		
		Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	&ПериодРегистрации КАК ПериодРегистрации,
		|	ПериодыКомандировок.Регистратор КАК Регистратор,
		|	ПериодыКомандировок.НомерСтроки КАК НомерСтроки,
		|	ПериодыКомандировок.Сотрудник КАК Сотрудник,
		|	ПериодыКомандировок.ВидРасчета КАК ВидРасчета,
		|	ПериодыКомандировок.Начало КАК БазовыйПериодНачало,
		|	ПериодыКомандировок.Окончание КАК БазовыйПериодКонец
		|ПОМЕСТИТЬ ВТОсновныеЗаписи
		|ИЗ
		|	ВТПериодыКомандировок КАК ПериодыКомандировок";
		Запрос.Выполнить();
		УдалитьВТ.Добавить("ВТОсновныеЗаписи");
		
		// Готовим запрос к расчетной базе начислений.
		ИменаИзмерений = РасчетЗарплатыРасширенный.ИменаИзмеренийРасчетнойБазыНачислений();
		ИменаИзмерений.Сотрудник = "Сотрудник";
		
		ОтборБазовыхЗаписей = Новый Массив;
		ОтборБазовыхЗаписей.Добавить(РасчетЗарплатыРасширенный.ЭлементОтбораБазовыхЗаписей("Регистратор", РегистраторыДляОтбора));
		
		РасчетЗарплатыРасширенный.СоздатьВТРасчетнаяБаза(МенеджерВременныхТаблиц, ИменаИзмерений, "ВТИсключаемыеВПериодКомандировки", ОтборБазовыхЗаписей);
		УдалитьВТ.Добавить("ВТРасчетнаяБаза");
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасчетнаяБаза.РегистраторРазрез КАК Регистратор,
		|	РасчетнаяБаза.ВидРасчетаРазрез КАК ВидРасчета,
		|	Начисления.Сотрудник КАК Сотрудник,
		|	НАЧАЛОПЕРИОДА(Начисления.ПериодДействияНачало, ДЕНЬ) КАК ДатаНачала,
		|	НАЧАЛОПЕРИОДА(Начисления.ПериодДействияКонец, ДЕНЬ) КАК ДатаОкончания,
		|	СУММА(РасчетнаяБаза.РезультатБаза) КАК РезультатБаза
		|ПОМЕСТИТЬ ВТРасчетнаяБазаСПериодом
		|ИЗ
		|	ВТРасчетнаяБаза КАК РасчетнаяБаза
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрРасчета.Начисления КАК Начисления
		|		ПО РасчетнаяБаза.РегистраторРазрез = Начисления.Регистратор
		|			И РасчетнаяБаза.НомерСтрокиРазрез = Начисления.НомерСтроки
		|
		|СГРУППИРОВАТЬ ПО
		|	РасчетнаяБаза.РегистраторРазрез,
		|	РасчетнаяБаза.ВидРасчетаРазрез,
		|	НАЧАЛОПЕРИОДА(Начисления.ПериодДействияНачало, ДЕНЬ),
		|	НАЧАЛОПЕРИОДА(Начисления.ПериодДействияКонец, ДЕНЬ),
		|	Начисления.Сотрудник
		|
		|ИМЕЮЩИЕ
		|	СУММА(РасчетнаяБаза.РезультатБаза) <> 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Начисления.Регистратор КАК Регистратор,
		|	Начисления.Подразделение КАК Подразделение,
		|	Начисления.Начисление КАК Начисление,
		|	Начисления.ДатаНачала КАК ДатаНачала,
		|	Начисления.ДатаОкончания КАК ДатаОкончания,
		|	Начисления.Сотрудник КАК Сотрудник,
		|	СУММА(Начисления.Сумма) КАК Сумма,
		|	СУММА(РасчетнаяБазаСПериодом.РезультатБаза) КАК РезультатБаза
		|ИЗ
		|	ВТНачисления КАК Начисления
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРасчетнаяБазаСПериодом КАК РасчетнаяБазаСПериодом
		|		ПО Начисления.Регистратор = РасчетнаяБазаСПериодом.Регистратор
		|			И Начисления.Начисление = РасчетнаяБазаСПериодом.ВидРасчета
		|			И Начисления.ДатаНачала = РасчетнаяБазаСПериодом.ДатаНачала
		|			И Начисления.ДатаОкончания = РасчетнаяБазаСПериодом.ДатаОкончания
		|			И Начисления.Сотрудник = РасчетнаяБазаСПериодом.Сотрудник
		|
		|СГРУППИРОВАТЬ ПО
		|	Начисления.Регистратор,
		|	Начисления.ДатаНачала,
		|	Начисления.Начисление,
		|	Начисления.ДатаОкончания,
		|	Начисления.Сотрудник,
		|	Начисления.Подразделение";
		
		НачисленияИсключаемыеВПериодКомандировок = Запрос.Выполнить().Выгрузить();
		УдалитьВТ.Добавить("ВТРасчетнаяБазаСПериодом");
		
		КолонкиОтбора = "Сотрудник,Начисление,ДатаНачала,Подразделение";
		ИсходнаяТаблица.Индексы.Добавить(КолонкиОтбора);
		Отбор = Новый Структура(КолонкиОтбора);
		
		УдаляемыеСтроки = Новый Соответствие;
		
		Для каждого СтрокаТЗ Из НачисленияИсключаемыеВПериодКомандировок Цикл
			
			Если (СтрокаТЗ.Сумма>0 И СтрокаТЗ.РезультатБаза<0) Или (СтрокаТЗ.Сумма<0 И СтрокаТЗ.РезультатБаза>0) Тогда
				// Суммы разного знака, такие строки не обрабатываем.
				Продолжить;
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(Отбор, СтрокаТЗ);
			СтрокиИсходнойТаблицы = ИсходнаяТаблица.НайтиСтроки(Отбор);
			
			Если СтрокаТЗ.Сумма = 0
				Или СтрокаТЗ.Сумма > 0 И СтрокаТЗ.Сумма <= СтрокаТЗ.РезультатБаза
				Или СтрокаТЗ.Сумма < 0 И СтрокаТЗ.Сумма >= СтрокаТЗ.РезультатБаза Тогда
				
				// Сумма нулевая, меньше или равна базе, полностью исключаем строку.
				Для каждого СтрокаИсходнойТаблицы Из СтрокиИсходнойТаблицы Цикл
					УдаляемыеСтроки.Вставить(СтрокаИсходнойТаблицы);
				КонецЦикла;
				
				Продолжить;
				
			КонецЕсли;
			
			ДоляБазы = (СтрокаТЗ.Сумма - СтрокаТЗ.РезультатБаза)/СтрокаТЗ.Сумма;
			
			ПривестиСуммыИсходнойТаблицыКДолеБазы(СтрокиИсходнойТаблицы, ДоляБазы);
			
		КонецЦикла;
		
		ИсходнаяТаблица.Индексы.Очистить();
		
		Для каждого ЭлементКоллекции Из УдаляемыеСтроки Цикл
			ИсходнаяТаблица.Удалить(ЭлементКоллекции.Ключ);
		КонецЦикла;
		
		ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, УдалитьВТ);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьСтрокиНачисленийИсключаемыеИзРаспределяемыхПоБазеСреднего(МенеджерВременныхТаблиц, ИсходнаяТаблица, ПериодРегистрации, НачисленияИсключаемыеВПериодКомандировок)
	
	СоздатьВТНачисленияБазаОтпуска(МенеджерВременныхТаблиц);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	// Проверка наличия начислений, которые входят в средний заработок по базовым расчетам.
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Начисления.Начисление КАК Начисление,
	|	НачисленияБазовыеВидыРасчета.ВидРасчета КАК БазовыйВидРасчета
	|ПОМЕСТИТЬ ВТРаспределяемыеПоБазе
	|ИЗ
	|	ВТНачисления КАК Начисления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНачисленияБазаОтпуска КАК НачисленияБазаОтпуска
	|		ПО Начисления.Начисление = НачисленияБазаОтпуска.Ссылка
	|			И (НачисленияБазаОтпуска.ПоБазовымРасчетам)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления.БазовыеВидыРасчета КАК НачисленияБазовыеВидыРасчета
	|		ПО Начисления.Начисление = НачисленияБазовыеВидыРасчета.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Начисления.Сотрудник КАК Сотрудник
	|ИЗ
	|	ВТНачисления КАК Начисления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРаспределяемыеПоБазе КАК РаспределяемыеПоБазе
	|		ПО Начисления.Начисление = РаспределяемыеПоБазе.Начисление
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РаспределяемыеПоБазе.Начисление КАК Начисление
	|ИЗ
	|	ВТРаспределяемыеПоБазе КАК РаспределяемыеПоБазе
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления КАК НастройкиНачислений
	|		ПО РаспределяемыеПоБазе.Начисление = НастройкиНачислений.Ссылка
	|			И (НастройкиНачислений.СтратегияОтраженияВУчете = ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазовымРасчетам))";
	
	Результат = Запрос.ВыполнитьПакет();
	
	// Сотрудники, имеющие начисления, отражаемые в учете среднего по базе.
	СотрудникиПоБазе = Результат[1].Выгрузить().ВыгрузитьКолонку("Сотрудник");
	
	Если СотрудникиПоБазе.Количество() = 0 Тогда
		// Нет начислений, распределяемых по базе.
		Возврат;
	КонецЕсли;
	
	// Проверка наличия начислений, которые входят в состав РК и СН и не входят в расчет среднего.
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РаспределяемыеПоБазе.БазовыйВидРасчета КАК БазовыйВидРасчета
	|ПОМЕСТИТЬ ВТБазовыеВидыРасчета
	|ИЗ
	|	ВТРаспределяемыеПоБазе КАК РаспределяемыеПоБазе
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачисленияБазаОтпуска КАК НачисленияБазаОтпуска
	|		ПО РаспределяемыеПоБазе.БазовыйВидРасчета = НачисленияБазаОтпуска.Ссылка
	|ГДЕ
	|	НачисленияБазаОтпуска.ПоБазовымРасчетам ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК Поле1
	|ИЗ
	|	ВТБазовыеВидыРасчета КАК БазовыеВидыРасчета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНачисления КАК Начисления
	|		ПО БазовыеВидыРасчета.БазовыйВидРасчета = Начисления.Начисление";
	НетБазовыхНачислений = Запрос.Выполнить().Пустой();
	
	// Проверка наличия в составе базовых начислений РК и СН исключенных в период командировки начислений.
	// Определим переменную как пустую коллекцию.
	НачисленияИсключаемые = Новый Массив;
	Если НачисленияИсключаемыеВПериодКомандировок <> Неопределено 
		И НачисленияИсключаемыеВПериодКомандировок.Количество() > 0 Тогда
		
		НачисленияИсключаемые = НачисленияИсключаемыеВПериодКомандировок.СкопироватьКолонки();
		НачисленияИсключаемыеВПериодКомандировок.Индексы.Добавить("Сотрудник");
		
		Отбор = Новый Структура("Сотрудник");
		Для каждого Сотрудник Из СотрудникиПоБазе Цикл
			Отбор.Сотрудник = Сотрудник;
			НайденныеСтроки = НачисленияИсключаемыеВПериодКомандировок.НайтиСтроки(Отбор);
			Для каждого СтрокаТЗ Из НайденныеСтроки Цикл
				ЗаполнитьЗначенияСвойств(НачисленияИсключаемые.Добавить(), СтрокаТЗ);
			КонецЦикла;
		КонецЦикла;
		
	КонецЕсли;
	
	Если НетБазовыхНачислений И НачисленияИсключаемые.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// ВТНачисленияПоБазе - отобранные из таблицы ВТНачисления
	// входящие в средний по базовым начислениям.
	Запрос.УстановитьПараметр("Сотрудники", СотрудникиПоБазе);
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Начисления.Регистратор КАК Регистратор,
	|	Начисления.ПериодДействия КАК ПериодДействия,
	|	Начисления.Начисление КАК Начисление,
	|	Начисления.ДатаНачала КАК ДатаНачала,
	|	Начисления.ДатаОкончания КАК ДатаОкончания,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.Подразделение КАК Подразделение,
	|	Начисления.Сумма КАК Сумма
	|ПОМЕСТИТЬ ВТНачисленияПоБазе
	|ИЗ
	|	ВТНачисления КАК Начисления
	|ГДЕ
	|	Начисления.Сотрудник В(&Сотрудники)
	|	И Начисления.Начисление В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				РаспределяемыеПоБазе.Начисление
	|			ИЗ
	|				ВТРаспределяемыеПоБазе КАК РаспределяемыеПоБазе)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Начисления.Регистратор КАК Регистратор,
	|	Начисления.НомерСтроки КАК НомерСтроки,
	|	НачисленияПоБазе.Сотрудник КАК Сотрудник,
	|	НачисленияПоБазе.Подразделение КАК Подразделение,
	|	НачисленияПоБазе.Начисление КАК Начисление,
	|	НачисленияПоБазе.ДатаНачала КАК ДатаНачала,
	|	НачисленияПоБазе.ДатаОкончания КАК ДатаОкончания
	|ПОМЕСТИТЬ ВТНачисленияПоБазеИзРегистраРасчета
	|ИЗ
	|	ВТНачисленияПоБазе КАК НачисленияПоБазе
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрРасчета.Начисления КАК Начисления
	|		ПО НачисленияПоБазе.Регистратор = Начисления.Регистратор
	|			И НачисленияПоБазе.Сотрудник = Начисления.Сотрудник
	|			И НачисленияПоБазе.Начисление = Начисления.ВидРасчета
	|			И (НачисленияПоБазе.Сумма <> 0)
	|			И НачисленияПоБазе.ДатаНачала = Начисления.ПериодДействияНачало
	|			И (НачисленияПоБазе.ДатаОкончания = НАЧАЛОПЕРИОДА(Начисления.ПериодДействияКонец, ДЕНЬ))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&ПериодРегистрации КАК ПериодРегистрации,
	|	Начисления.Регистратор КАК Регистратор,
	|	Начисления.НомерСтроки КАК НомерСтроки,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.Начисление КАК ВидРасчета,
	|	Начисления.ДатаНачала КАК БазовыйПериодНачало,
	|	Начисления.ДатаОкончания КАК БазовыйПериодКонец
	|ПОМЕСТИТЬ ВТОтборНачислений
	|ИЗ
	|	ВТНачисленияПоБазеИзРегистраРасчета КАК Начисления";
	
	Запрос.Выполнить();
	
	// Получение базы.
	РасчетЗарплатыРасширенный.СоздатьВТРасчетнаяБазаНачисленийПоВременнойТаблицеКаскадно(МенеджерВременныхТаблиц);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасчетнаяБаза.Регистратор КАК Регистратор,
	|	РасчетнаяБаза.НомерСтроки КАК НомерСтроки,
	|	РасчетнаяБаза.РегистраторРазрез КАК РегистраторРазрез,
	|	РасчетнаяБаза.ВидРасчетаРазрез КАК ВидРасчетаРазрез,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	НАЧАЛОПЕРИОДА(Начисления.ПериодДействияНачало, ДЕНЬ) КАК ДатаНачала,
	|	НАЧАЛОПЕРИОДА(Начисления.ПериодДействияКонец, ДЕНЬ) КАК ДатаОкончания,
	|	СУММА(РасчетнаяБаза.РезультатБаза) КАК РезультатБаза
	|ПОМЕСТИТЬ ВТРасчетнаяБазаСПериодом
	|ИЗ
	|	ВТРасчетнаяБаза КАК РасчетнаяБаза
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрРасчета.Начисления КАК Начисления
	|		ПО РасчетнаяБаза.РегистраторРазрез = Начисления.Регистратор
	|			И РасчетнаяБаза.НомерСтрокиРазрез = Начисления.НомерСтроки
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(Начисления.ПериодДействияНачало, ДЕНЬ),
	|	РасчетнаяБаза.Регистратор,
	|	НАЧАЛОПЕРИОДА(Начисления.ПериодДействияКонец, ДЕНЬ),
	|	Начисления.Сотрудник,
	|	РасчетнаяБаза.НомерСтроки,
	|	РасчетнаяБаза.РегистраторРазрез,
	|	РасчетнаяБаза.ВидРасчетаРазрез
	|
	|ИМЕЮЩИЕ
	|	СУММА(РасчетнаяБаза.РезультатБаза) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Начисления.Регистратор КАК Регистратор,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.Подразделение КАК Подразделение,
	|	Начисления.Начисление КАК Начисление,
	|	Начисления.ДатаНачала КАК ДатаНачала,
	|	Начисления.ДатаОкончания КАК ДатаОкончания,
	|	СУММА(РасчетнаяБазаСПериодом.РезультатБаза) КАК РезультатБаза,
	|	РасчетнаяБазаСПериодом.РегистраторРазрез КАК РегистраторРазрез,
	|	РасчетнаяБазаСПериодом.ВидРасчетаРазрез КАК ВидРасчетаРазрез,
	|	РасчетнаяБазаСПериодом.ДатаНачала КАК ДатаНачалаБаза,
	|	РасчетнаяБазаСПериодом.ДатаОкончания КАК ДатаОкончанияБаза,
	|	ВЫБОР
	|		КОГДА НачисленияБазаОтпуска.Ссылка ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ВходитВБазуОтпуска
	|ПОМЕСТИТЬ ВТНачисленияПоБазаИБазовые
	|ИЗ
	|	ВТНачисленияПоБазеИзРегистраРасчета КАК Начисления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРасчетнаяБазаСПериодом КАК РасчетнаяБазаСПериодом
	|		ПО Начисления.Регистратор = РасчетнаяБазаСПериодом.Регистратор
	|			И Начисления.НомерСтроки = РасчетнаяБазаСПериодом.НомерСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачисленияБазаОтпуска КАК НачисленияБазаОтпуска
	|		ПО (РасчетнаяБазаСПериодом.ВидРасчетаРазрез = НачисленияБазаОтпуска.Ссылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетнаяБазаСПериодом.РегистраторРазрез,
	|	Начисления.Сотрудник,
	|	Начисления.Регистратор,
	|	ВЫБОР
	|		КОГДА НачисленияБазаОтпуска.Ссылка ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ,
	|	РасчетнаяБазаСПериодом.ВидРасчетаРазрез,
	|	РасчетнаяБазаСПериодом.ДатаНачала,
	|	РасчетнаяБазаСПериодом.ДатаОкончания,
	|	Начисления.ДатаОкончания,
	|	Начисления.Подразделение,
	|	Начисления.ДатаНачала,
	|	Начисления.Начисление
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Начисления.Регистратор КАК Регистратор,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.Подразделение КАК Подразделение,
	|	Начисления.Начисление КАК Начисление,
	|	Начисления.ДатаНачала КАК ДатаНачала,
	|	Начисления.ДатаОкончания КАК ДатаОкончания,
	|	ВЫБОР
	|		КОГДА НастройкиНачислений.СтратегияОтраженияВУчете = ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазовымРасчетам)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК БухучетПоБазовымРасчетам,
	|	СУММА(РасчетнаяБазаСПериодом.РезультатБаза) КАК РезультатБаза
	|ИЗ
	|	ВТНачисленияПоБазеИзРегистраРасчета КАК Начисления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРасчетнаяБазаСПериодом КАК РасчетнаяБазаСПериодом
	|		ПО Начисления.Регистратор = РасчетнаяБазаСПериодом.Регистратор
	|			И Начисления.НомерСтроки = РасчетнаяБазаСПериодом.НомерСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления КАК НастройкиНачислений
	|		ПО Начисления.Начисление = НастройкиНачислений.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	Начисления.ДатаОкончания,
	|	Начисления.ДатаНачала,
	|	Начисления.Сотрудник,
	|	Начисления.Регистратор,
	|	Начисления.Начисление,
	|	Начисления.Подразделение,
	|	ВЫБОР
	|		КОГДА НастройкиНачислений.СтратегияОтраженияВУчете = ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазовымРасчетам)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|
	|ИМЕЮЩИЕ
	|	СУММА(РасчетнаяБазаСПериодом.РезультатБаза) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БазовыеНачисления.Регистратор КАК Регистратор,
	|	БазовыеНачисления.Сотрудник КАК Сотрудник,
	|	БазовыеНачисления.Подразделение КАК Подразделение,
	|	БазовыеНачисления.Начисление КАК Начисление,
	|	БазовыеНачисления.ДатаНачала КАК ДатаНачала,
	|	БазовыеНачисления.ДатаОкончания КАК ДатаОкончания,
	|	БазовыеНачисления.РезультатБаза КАК РезультатБаза,
	|	БазовыеНачисления.РегистраторРазрез КАК РегистраторРазрез,
	|	БазовыеНачисления.ВидРасчетаРазрез КАК ВидРасчетаРазрез,
	|	БазовыеНачисления.ДатаНачалаБаза КАК ДатаНачалаБаза,
	|	БазовыеНачисления.ДатаОкончанияБаза КАК ДатаОкончанияБаза,
	|	БазовыеНачисления.ВходитВБазуОтпуска КАК ВходитВБазуОтпуска
	|ИЗ
	|	ВТНачисленияПоБазаИБазовые КАК БазовыеНачисления";
	
	Результат = Запрос.ВыполнитьПакет();
	КоличествоРезультатов = Результат.ВГраница();
	
	// Начисления распределяемые по базе с колонкой РезультатБаза
	НачисленияПоБазе = Результат[КоличествоРезультатов-1].Выгрузить();
	БазовыеНачисления = Результат[КоличествоРезультатов].Выгрузить();
	
	КолонкиОтбора = "Регистратор,Сотрудник,Начисление,ДатаНачала,ДатаОкончания,Подразделение";
	БазовыеНачисления.Индексы.Добавить(КолонкиОтбора);
	ОтборБазы = Новый Структура(КолонкиОтбора);
	
	КолонкиОтбораЧастичноИсключаемых = "Регистратор,Сотрудник,Начисление,ДатаНачала,ДатаОкончания";
	ОтборЧастичноИсключаемых = Новый Структура(КолонкиОтбораЧастичноИсключаемых);
	
	ОбрабатыватьИсключаемыеИзКомандировок = (НачисленияИсключаемые.Количество()>0);
	Если ОбрабатыватьИсключаемыеИзКомандировок Тогда
		НачисленияИсключаемые.Индексы.Добавить(КолонкиОтбораЧастичноИсключаемых);
	КонецЕсли;
	
	КолонкиОтбораИсходнойТаблицы = "Сотрудник,Начисление,ДатаНачала,Подразделение";
	ИсходнаяТаблица.Индексы.Добавить(КолонкиОтбораИсходнойТаблицы);
	Отбор = Новый Структура(КолонкиОтбораИсходнойТаблицы);
	УдаляемыеСтроки = Новый Соответствие;
	
	Для каждого СтрокаНачисления Из НачисленияПоБазе Цикл
		
		РезультатБаза = СтрокаНачисления.РезультатБаза;
		
		СтрокиИсключаемые = Новый Массив;
		СтрокиЧастичноИсключаемые = Новый Массив;
		
		ЗаполнитьЗначенияСвойств(Отбор, СтрокаНачисления);
		ЗаполнитьЗначенияСвойств(ОтборБазы, СтрокаНачисления);
		БазовыеСтроки = БазовыеНачисления.НайтиСтроки(ОтборБазы);
		
		Для каждого БазоваяСтрока Из БазовыеСтроки Цикл
			
			Если Не БазоваяСтрока.ВходитВБазуОтпуска Тогда
				СтрокиИсключаемые.Добавить(БазоваяСтрока);
			ИначеЕсли ОбрабатыватьИсключаемыеИзКомандировок Тогда
				
				ОтборЧастичноИсключаемых.Регистратор 	= БазоваяСтрока.РегистраторРазрез;
				ОтборЧастичноИсключаемых.Сотрудник 		= БазоваяСтрока.Сотрудник;
				ОтборЧастичноИсключаемых.Начисление 	= БазоваяСтрока.ВидРасчетаРазрез;
				ОтборЧастичноИсключаемых.ДатаНачала 	= БазоваяСтрока.ДатаНачалаБаза;
				ОтборЧастичноИсключаемых.ДатаОкончания 	= БазоваяСтрока.ДатаОкончанияБаза;
				
				НайденныеСтроки = НачисленияИсключаемые.НайтиСтроки(ОтборЧастичноИсключаемых);
				Для каждого НайденнаяСтрока Из НайденныеСтроки Цикл
					СтрокиЧастичноИсключаемые.Добавить(НайденнаяСтрока);
				КонецЦикла;
				
			КонецЕсли;
		
		КонецЦикла;
		
		СуммаИсключаемаяИзБазы = 0;
		Для каждого СтрокаИсключаемая Из СтрокиИсключаемые Цикл
			СуммаИсключаемаяИзБазы = СуммаИсключаемаяИзБазы + СтрокаИсключаемая.РезультатБаза;
		КонецЦикла;
		
		Для каждого СтрокаИсключаемая Из СтрокиЧастичноИсключаемые Цикл
			СуммаИсключаемаяИзБазы = СуммаИсключаемаяИзБазы + СтрокаИсключаемая.РезультатБаза;
		КонецЦикла;
		
		// Доля расчетной базы, включаемая в расчет среднего.
		ДоляБазы = ?(РезультатБаза=0,0,(РезультатБаза - СуммаИсключаемаяИзБазы)/РезультатБаза);
		
		Если ДоляБазы = 1 Тогда
			Продолжить;
		ИначеЕсли ДоляБазы = 0 Тогда
			
			СтрокиИсходнойТаблицы = ИсходнаяТаблица.НайтиСтроки(Отбор);
			Для каждого СтрокаИсходнойТаблицы Из СтрокиИсходнойТаблицы Цикл
				УдаляемыеСтроки.Вставить(СтрокаИсходнойТаблицы);
			КонецЦикла;
			
			Продолжить;
			
		КонецЕсли;
		
		// Обработка строк у которых ДоляБазы больше 0 и меньше 1.
		
		СтрокиИсходнойТаблицы = ИсходнаяТаблица.НайтиСтроки(Отбор);
		
		ПривестиСуммыИсходнойТаблицыКДолеБазы(СтрокиИсходнойТаблицы, ДоляБазы);
	
	КонецЦикла;
	
	ИсходнаяТаблица.Индексы.Очистить();
	
	Для каждого ЭлементКоллекции Из УдаляемыеСтроки Цикл
		ИсходнаяТаблица.Удалить(ЭлементКоллекции.Ключ);
	КонецЦикла;	

КонецПроцедуры

Процедура ПривестиСуммыИсходнойТаблицыКДолеБазы(СтрокиТаблицы, ДоляБазы)

	ФондОплатыТруда 	= 0;
	СтраховыеВзносы 	= 0;
	ФССНесчастныеСлучаи = 0;
	
	СтрокаДляПогрешностиФОТ 	= Неопределено;
	СтрокаДляПогрешностиВзносы 	= Неопределено;
	СтрокаДляПогрешностиФСС 	= Неопределено;
	МаксимальноеЗначениеФОТ		= 0;
	МаксимальноеЗначениеВзносы 	= 0;
	МаксимальноеЗначениеФСС 	= 0;
	
	Для каждого СтрокаТЗ Из СтрокиТаблицы Цикл
		
		ФондОплатыТруда 	= ФондОплатыТруда + СтрокаТЗ.ФондОплатыТруда;
		СтраховыеВзносы 	= СтраховыеВзносы + СтрокаТЗ.СтраховыеВзносы;
		ФССНесчастныеСлучаи = ФССНесчастныеСлучаи + СтрокаТЗ.ФССНесчастныеСлучаи;
		
		АбсолютноеЗначение = ?(СтрокаТЗ.ФондОплатыТруда > 0, СтрокаТЗ.ФондОплатыТруда, -СтрокаТЗ.ФондОплатыТруда);
		Если МаксимальноеЗначениеФОТ < АбсолютноеЗначение Тогда
			МаксимальноеЗначениеФОТ = АбсолютноеЗначение;
			СтрокаДляПогрешностиФОТ = СтрокаТЗ;
		КонецЕсли;
		
		АбсолютноеЗначение = ?(СтрокаТЗ.СтраховыеВзносы > 0, СтрокаТЗ.СтраховыеВзносы, -СтрокаТЗ.СтраховыеВзносы);
		Если МаксимальноеЗначениеВзносы < АбсолютноеЗначение Тогда
			МаксимальноеЗначениеВзносы = АбсолютноеЗначение;
			СтрокаДляПогрешностиВзносы = СтрокаТЗ;
		КонецЕсли;
		
		АбсолютноеЗначение = ?(СтрокаТЗ.ФССНесчастныеСлучаи > 0, СтрокаТЗ.ФССНесчастныеСлучаи, -СтрокаТЗ.ФССНесчастныеСлучаи);
		Если МаксимальноеЗначениеФСС < АбсолютноеЗначение Тогда
			МаксимальноеЗначениеФСС = АбсолютноеЗначение;
			СтрокаДляПогрешностиФСС = СтрокаТЗ;
		КонецЕсли;
		
	КонецЦикла;
	
	ФондОплатыТруда 	= ОКР(ФондОплатыТруда * ДоляБазы, 2);
	СтраховыеВзносы 	= ОКР(СтраховыеВзносы * ДоляБазы, 2);
	ФССНесчастныеСлучаи = ОКР(ФССНесчастныеСлучаи * ДоляБазы, 2);
	
	НовыйФондОплатыТруда 	= 0;
	НовыйСтраховыеВзносы 	= 0;
	НовыйФССНесчастныеСлучаи = 0;
	
	Для каждого СтрокаТЗ Из СтрокиТаблицы Цикл
		
		СтрокаТЗ.ФондОплатыТруда = ОКР(СтрокаТЗ.ФондОплатыТруда * ДоляБазы, 2);
		НовыйФондОплатыТруда = НовыйФондОплатыТруда + СтрокаТЗ.ФондОплатыТруда;
		
		СтрокаТЗ.СтраховыеВзносы = ОКР(СтрокаТЗ.СтраховыеВзносы * ДоляБазы, 2);
		НовыйСтраховыеВзносы = НовыйСтраховыеВзносы + СтрокаТЗ.СтраховыеВзносы;
		
		СтрокаТЗ.ФССНесчастныеСлучаи = ОКР(СтрокаТЗ.ФССНесчастныеСлучаи * ДоляБазы, 2);
		НовыйФССНесчастныеСлучаи = НовыйФССНесчастныеСлучаи + СтрокаТЗ.ФССНесчастныеСлучаи;
		
	КонецЦикла;
	
	Если НовыйФондОплатыТруда <> ФондОплатыТруда Тогда
		СтрокаДляПогрешностиФОТ.ФондОплатыТруда = СтрокаДляПогрешностиФОТ.ФондОплатыТруда + ФондОплатыТруда - НовыйФондОплатыТруда;
	КонецЕсли;
	
	Если НовыйСтраховыеВзносы <> СтраховыеВзносы Тогда
		СтрокаДляПогрешностиВзносы.СтраховыеВзносы = СтрокаДляПогрешностиФОТ.СтраховыеВзносы + СтраховыеВзносы - НовыйСтраховыеВзносы;
	КонецЕсли;
	
	Если НовыйФССНесчастныеСлучаи <> ФССНесчастныеСлучаи Тогда
		СтрокаДляПогрешностиФСС.ФССНесчастныеСлучаи = СтрокаДляПогрешностиФОТ.ФССНесчастныеСлучаи + ФССНесчастныеСлучаи - НовыйФССНесчастныеСлучаи;
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьПараметрыПолученияФОТОрганизацийДляОценочныхОбязательствОтпусков(Организации, Период, ТаблицаПараметров) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	СоздатьВТНачисленияБазаОтпуска(МенеджерВременныхТаблиц);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НачисленияБазаОтпуска.Ссылка КАК Ссылка
	|ИЗ
	|	ВТНачисленияБазаОтпуска КАК НачисленияБазаОтпуска";
	БазаОтпускаПоСреднемуЗаработку = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	УчитыватьОсобенностиПредприятия = Ложь;
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба") Тогда
		МодульГосударственнаяСлужба = ОбщегоНазначения.ОбщийМодуль("ГосударственнаяСлужба");
		УчитыватьОсобенностиПредприятия = МодульГосударственнаяСлужба.УчитыватьОсобенностиПредприятияПриРасчетеОценочныхОбязательствОтпусков();
	КонецЕсли;
	
	Если Не УчитыватьОсобенностиПредприятия Тогда
		
		НоваяСтрока = ТаблицаПараметров.Добавить();
		НоваяСтрока.Сотрудники = Неопределено;
		НоваяСтрока.БазовыеНачисления = БазаОтпускаПоСреднемуЗаработку;
		
	Иначе
		
		// Получим всех сотрудников, по которым собирается ФОТ текущего месяца.
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Организации", Организации);
		Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Период));
		Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Период));
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НачисленияПоСотрудникам.Сотрудник КАК Сотрудник
		|ИЗ
		|	РегистрНакопления.НачисленияУдержанияПоСотрудникам КАК НачисленияПоСотрудникам
		|ГДЕ
		|	НачисленияПоСотрудникам.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И НачисленияПоСотрудникам.Организация В(&Организации)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СтраховыеВзносыПоФизическимЛицам.Сотрудник
		|ИЗ
		|	РегистрНакопления.СтраховыеВзносыПоФизическимЛицам КАК СтраховыеВзносыПоФизическимЛицам
		|ГДЕ
		|	СтраховыеВзносыПоФизическимЛицам.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И СтраховыеВзносыПоФизическимЛицам.Организация В(&Организации)";
		
		УстановитьПривилегированныйРежим(Истина);
		СотрудникиДляРасчета = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Сотрудник");
		УстановитьПривилегированныйРежим(Ложь);
		
		// Получим таблицу, в которой указаны способы расчета ФОТ для каждого сотрудника.
		УстановитьПривилегированныйРежим(Истина);
		СотрудникиПоСпособамРасчета = СотрудникиПоСпособамРасчетаОбязательствОтпусков(СотрудникиДляРасчета, Период);
		УстановитьПривилегированныйРежим(Истина);
		
		СотрудникиПоСреднемуЗаработку  = СотрудникиПоСпособамРасчета.Скопировать(Новый Структура("РасчетПоСреднемуЗаработку", Истина)).ВыгрузитьКолонку("Сотрудник");
		Если СотрудникиПоСреднемуЗаработку.Количество() > 0 Тогда
			НоваяСтрока = ТаблицаПараметров.Добавить();
			НоваяСтрока.Сотрудники = СотрудникиПоСреднемуЗаработку;
			НоваяСтрока.БазовыеНачисления = БазаОтпускаПоСреднемуЗаработку;
		КонецЕсли;
		
		СотрудникиПоСохраняемомуЗаработку = СотрудникиПоСпособамРасчета.Скопировать(Новый Структура("РасчетПоСохраняемомуЗаработку", Истина)).ВыгрузитьКолонку("Сотрудник");
		Если СотрудникиПоСохраняемомуЗаработку.Количество() > 0 Тогда
			
			НоваяСтрока = ТаблицаПараметров.Добавить();
			НоваяСтрока.Сотрудники = СотрудникиПоСохраняемомуЗаработку;
			
			БазовыеНачисления = Новый Массив;
			Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба.РасчетДенежногоСодержания") Тогда
				МодульРасчетДенежногоСодержания = ОбщегоНазначения.ОбщийМодуль("РасчетДенежногоСодержания");
				БазовыеНачисленияОтпуска = МодульРасчетДенежногоСодержания.БазаОтпускаПоСохраняемомуЗаработкуДляОценочныхОбязательствОтпусков();
				БазовыеНачисления = БазовыеНачисленияОтпуска.ВыгрузитьКолонку("Начисление");
			КонецЕсли;
			НоваяСтрока.БазовыеНачисления = БазовыеНачисления;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыПолученияФОТСотрудниковДляОценочныхОбязательствОтпусков(СотрудникиДляРасчета, Период, ПараметрыПолученияФОТ) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	СоздатьВТНачисленияБазаОтпуска(МенеджерВременныхТаблиц);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НачисленияБазаОтпуска.Ссылка КАК Ссылка
	|ИЗ
	|	ВТНачисленияБазаОтпуска КАК НачисленияБазаОтпуска";
	БазаОтпускаПоСреднемуЗаработку = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	ПараметрыПолученияФОТ.БазаОтпускаПоСреднемуЗаработку = ОбщегоНазначенияБЗККлиентСервер.МассивВСоответствие(БазаОтпускаПоСреднемуЗаработку);
	
	УчитыватьОсобенностиПредприятия = Ложь;
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба") Тогда
		МодульГосударственнаяСлужба = ОбщегоНазначения.ОбщийМодуль("ГосударственнаяСлужба");
		УчитыватьОсобенностиПредприятия = МодульГосударственнаяСлужба.УчитыватьОсобенностиПредприятияПриРасчетеОценочныхОбязательствОтпусков();
	КонецЕсли;
	
	Если Не УчитыватьОсобенностиПредприятия Тогда
		
		ПараметрыПолученияФОТ.Сотрудники = ОбщегоНазначенияБЗККлиентСервер.МассивВСоответствие(СотрудникиДляРасчета);
		
	Иначе
		
		// Получим таблицу, в которой указаны способы расчета ФОТ для каждого сотрудника.
		СотрудникиПоСпособамРасчета = СотрудникиПоСпособамРасчетаОбязательствОтпусков(СотрудникиДляРасчета, Период);
		
		СотрудникиПоСреднемуЗаработку  = СотрудникиПоСпособамРасчета.Скопировать(Новый Структура("РасчетПоСреднемуЗаработку", Истина)).ВыгрузитьКолонку("Сотрудник");
		Если СотрудникиПоСреднемуЗаработку.Количество() > 0 Тогда
			ПараметрыПолученияФОТ.Сотрудники = ОбщегоНазначенияБЗККлиентСервер.МассивВСоответствие(СотрудникиПоСреднемуЗаработку);
		КонецЕсли;
		
		СотрудникиПоСохраняемомуЗаработку = СотрудникиПоСпособамРасчета.Скопировать(Новый Структура("РасчетПоСохраняемомуЗаработку", Истина)).ВыгрузитьКолонку("Сотрудник");
		Если СотрудникиПоСохраняемомуЗаработку.Количество() > 0 Тогда
			
			БазовыеНачисления = Новый Соответствие;
			Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба.РасчетДенежногоСодержания") Тогда
				МодульРасчетДенежногоСодержания = ОбщегоНазначения.ОбщийМодуль("РасчетДенежногоСодержания");
				БазовыеНачисленияОтпуска = МодульРасчетДенежногоСодержания.БазаОтпускаПоСохраняемомуЗаработкуДляОценочныхОбязательствОтпусков();
				БазовыеНачисления = БазовыеНачисленияОтпуска.ВыгрузитьКолонку("Начисление");
				БазовыеНачисления = ОбщегоНазначенияБЗККлиентСервер.МассивВСоответствие(БазовыеНачисления);
			КонецЕсли;
			ПараметрыПолученияФОТ.БазаОтпускаПоСохраняемомуЗаработку = БазовыеНачисления;
			
			Для каждого Сотрудник Из СотрудникиПоСохраняемомуЗаработку Цикл
				ПараметрыПолученияФОТ.Сотрудники.Вставить(Сотрудник, Ложь);
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьСведенияОбОтпускахСотрудниковДляОценочныхОбязательств(СведенияОбОтпусках, СотрудникиДляОбработки, Организация, Период) Экспорт
	
	УчитыватьОсобенностиПредприятия = Ложь;
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба") Тогда
		МодульГосударственнаяСлужба = ОбщегоНазначения.ОбщийМодуль("ГосударственнаяСлужба");
		УчитыватьОсобенностиПредприятия = МодульГосударственнаяСлужба.УчитыватьОсобенностиПредприятияПриРасчетеОценочныхОбязательствОтпусков();
	КонецЕсли;
	
	СотрудникиДляРасчета = СотрудникиДляОбработки;
	
	Если УчитыватьОсобенностиПредприятия Тогда
		ТаблицаСотрудников = СотрудникиПоСпособамРасчетаОбязательствОтпусков(СотрудникиДляОбработки, Период);
		СотрудникиДляРасчета = ТаблицаСотрудников.ВыгрузитьКолонку("Сотрудник");
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Сотрудники", СотрудникиДляРасчета);
	Запрос.УстановитьПараметр("ПериодНачисления", НачалоМесяца(Период));
	Запрос.УстановитьПараметр("КонецМесяца", КонецМесяца(Период));
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ФактическиеОтпуска.Сотрудник КАК Сотрудник,
	|	СУММА(ФактическиеОтпуска.Количество) КАК ОтпускАвансом
	|ИЗ
	|	РегистрНакопления.ФактическиеОтпуска КАК ФактическиеОтпуска
	|ГДЕ
	|	ФактическиеОтпуска.Сотрудник В(&Сотрудники)
	|	И НАЧАЛОПЕРИОДА(ФактическиеОтпуска.ПериодНачисления, МЕСЯЦ) = &ПериодНачисления
	|	И ФактическиеОтпуска.Период > &КонецМесяца
	|	И НЕ ФактическиеОтпуска.ВидЕжегодногоОтпуска.ОтпускБезОплаты
	|
	|СГРУППИРОВАТЬ ПО
	|	ФактическиеОтпуска.Сотрудник
	|
	|ИМЕЮЩИЕ
	|	СУММА(ФактическиеОтпуска.Количество) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыОтпусков.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ВидыОтпусков КАК ВидыОтпусков
	|ГДЕ
	|	ВидыОтпусков.ОтпускЯвляетсяЕжегодным
	|	И НЕ ВидыОтпусков.ОтпускБезОплаты";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ОтпускаАвансом = Новый Соответствие;
	Выборка = РезультатЗапроса[0].Выбрать();
	Пока Выборка.Следующий() Цикл
		ОтпускаАвансом.Вставить(Выборка.Сотрудник, Выборка.ОтпускАвансом);
	КонецЦикла;
	
	ВидыОтпусков = РезультатЗапроса[1].Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	ОстаткиОтпусковСотрудников = ОстаткиОтпусков.ОстаткиОтпусковСотрудниковНаДату(СотрудникиДляРасчета, КонецМесяца(Период), ВидыОтпусков);
	ОстаткиОтпусковСотрудников.Индексы.Добавить("Сотрудник");
	Отбор = Новый Структура("Сотрудник");
	
	Если УчитыватьОсобенностиПредприятия Тогда
		СотрудникиПоСреднемуЗаработку = ТаблицаСотрудников.Скопировать(Новый Структура("РасчетПоСреднемуЗаработку", Истина)).ВыгрузитьКолонку("Сотрудник");
	Иначе
		СотрудникиПоСреднемуЗаработку = СотрудникиДляРасчета;
	КонецЕсли;
	СпособыРасчетаОтпусковСотрудников = ОстаткиОтпусков.СпособыРасчетаОтпусковСотрудников(СотрудникиПоСреднемуЗаработку, КонецМесяца(Период));
	
	// Дата на которую рассчитывается средний и/или сохраняемый заработок,
	// первое число месяца, следующего за месяцем указанном в параметре Период.
	ДатаСобытия = НачалоМесяца(ДобавитьМесяц(Период, 1));
	
	Для каждого Сотрудник Из СотрудникиПоСреднемуЗаработку Цикл
		
		Отбор.Сотрудник = Сотрудник;
		
		НоваяСтрока = СведенияОбОтпусках.Добавить();
		НоваяСтрока.Сотрудник 		= Сотрудник;
		НоваяСтрока.ОтпускАвансом 	= ОтпускаАвансом[Сотрудник];
		
		ДополнительныеПараметры = УчетСреднегоЗаработкаКлиентСервер.ДополнительныеПараметрыРасчетаСреднегоЗаработка();
		ДополнительныеПараметры.СпособРасчетаОтпуска = СпособыРасчетаОтпусковСотрудников[Сотрудник];
		НоваяСтрока.СреднийЗаработок = УчетСреднегоЗаработка.СреднийЗаработок(Сотрудник, ДатаСобытия, ДополнительныеПараметры);
		
		НайденныеСтроки = ОстаткиОтпусковСотрудников.НайтиСтроки(Отбор);
		ОстатокОтпусков = 0;
		Если НайденныеСтроки.Количество() > 0 Тогда
			ОстатокОтпусков = НайденныеСтроки[0].КоличествоДней;
		КонецЕсли;
		НоваяСтрока.ОстатокОтпусков = ОстатокОтпусков;
		
	КонецЦикла;
	
	Если УчитыватьОсобенностиПредприятия Тогда
		
		СохраняемыйЗаработок = Новый Соответствие;
		СотрудникиПоСохраняемомуЗаработку = ТаблицаСотрудников.Скопировать(Новый Структура("РасчетПоСохраняемомуЗаработку", Истина)).ВыгрузитьКолонку("Сотрудник");
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба.РасчетДенежногоСодержания") Тогда
			МодульРасчетДенежногоСодержания = ОбщегоНазначения.ОбщийМодуль("РасчетДенежногоСодержания");
			СохраняемыйЗаработок = МодульРасчетДенежногоСодержания.СохраняемыйЗаработокСотрудниковДляОбязательствПоОтпускам(СотрудникиПоСохраняемомуЗаработку, Организация, Период);
		КонецЕсли;
		
		Для каждого Сотрудник Из СотрудникиПоСохраняемомуЗаработку Цикл
			
			Отбор.Сотрудник = Сотрудник;
			
			НоваяСтрока = СведенияОбОтпусках.Добавить();
			НоваяСтрока.Сотрудник 			= Сотрудник;
			НоваяСтрока.ОтпускАвансом 		= ОтпускаАвансом[Сотрудник];
			НоваяСтрока.СреднийЗаработок 	= СохраняемыйЗаработок[Сотрудник];
			НоваяСтрока.РасчетПоСохраняемомуЗаработку = Истина;
			
			НайденныеСтроки = ОстаткиОтпусковСотрудников.НайтиСтроки(Отбор);
			ОстатокОтпусков = 0;
			Если НайденныеСтроки.Количество() > 0 Тогда
				ОстатокОтпусков = НайденныеСтроки[0].КоличествоДней;
			КонецЕсли;
			НоваяСтрока.ОстатокОтпусков = ОстатокОтпусков;
			
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

Функция СотрудникиПоСпособамРасчетаОбязательствОтпусков(СотрудникиДляРасчета, Период) Экспорт
	
	ТаблицаСотрудников = Новый ТаблицаЗначений;
	ТаблицаСотрудников.Колонки.Добавить("Сотрудник");
	ТаблицаСотрудников.Колонки.Добавить("РасчетПоСреднемуЗаработку", 	 Новый ОписаниеТипов("Булево"));
	ТаблицаСотрудников.Колонки.Добавить("РасчетПоСохраняемомуЗаработку", Новый ОписаниеТипов("Булево"));
	
	ЗапрашиваемыеДанные = "ВидДоговора,ВидДолжностиГосударственнойСлужбы,ЯвляетсяСудьей,ЯвляетсяВоеннослужащим";
	ДатаСобытия = КонецМесяца(Период);
	КадровыеДанныеСотрудников = КадровыйУчет.КадровыеДанныеСотрудников(Истина, СотрудникиДляРасчета, ЗапрашиваемыеДанные, ДатаСобытия);
	
	СтрокиДляДополнительнойОбработки = Новый Массив;
	Для каждого СтрокаТЗ Из КадровыеДанныеСотрудников Цикл
		
		Если СтрокаТЗ.ВидДоговора = Перечисления.ВидыДоговоровССотрудниками.ТрудовойДоговор Тогда
			НоваяСтрока = ТаблицаСотрудников.Добавить();
			НоваяСтрока.Сотрудник = СтрокаТЗ.Сотрудник;
			НоваяСтрока.РасчетПоСреднемуЗаработку = Истина;
		ИначеЕсли ЗначениеЗаполнено(СтрокаТЗ.ВидДоговора) Тогда
			СтрокиДляДополнительнойОбработки.Добавить(СтрокаТЗ);
		КонецЕсли;
		
	КонецЦикла;
	
	Если СтрокиДляДополнительнойОбработки.Количество() > 0 И ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба") Тогда
		МодульГосударственнаяСлужба = ОбщегоНазначения.ОбщийМодуль("ГосударственнаяСлужба");
		МодульГосударственнаяСлужба.ДополнитьТаблицуСотрудниковПоСпособамРасчетаОбязательствОтпусков(ТаблицаСотрудников, СтрокиДляДополнительнойОбработки);
	КонецЕсли; 
	
	Возврат ТаблицаСотрудников;

КонецФункции 

// Получает настройки бухучета сотрудников для расчета оценочных обязательств.
//
// Параметры:
// 	ТаблицаСотрудников - ТаблицаЗначений - содержит колонки
// 			* Сотрудник
// 			* Подразделение
// 			* ТерриторияВыполненияРаботВОрганизации
// 	Организация - СправочникСсылка.Организации.
// 	Период - Дата.
//
// Возвращаемое значение:
// 	ТаблицаЗначений
//
Функция НастройкиБухучетаДляРасчетаОбязательств(ТаблицаСотрудников, Организация, Период) Экспорт
	
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Период", КонецМесяца(Период));
	Запрос.УстановитьПараметр("ТаблицаСотрудников", ТаблицаСотрудников);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Организация КАК Организация,
	|	&Период КАК Период,
	|	Таблица.Подразделение КАК Подразделение,
	|	Таблица.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	Таблица.Сотрудник КАК Сотрудник,
	|	АВТОНОМЕРЗАПИСИ() КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ВТСотрудники
	|ИЗ
	|	&ТаблицаСотрудников КАК Таблица";
	Запрос.Выполнить();
	
	СоздатьВТНастройкиБухучетаДляРаспределенияПоУмолчанию(МенеджерВТ, "ВТСотрудники");
	
	Запрос.УстановитьПараметр("ЕстьЕНВД", ОтражениеЗарплатыВБухучете.ПлательщикЕНВД(Организация, Период));
	Запрос.УстановитьПараметр("ИспользоватьСтатьиФинансирования", ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный"));
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НастройкиБухучета.Сотрудник КАК Сотрудник,
	|	НастройкиБухучета.Подразделение КАК Подразделение,
	|	ВЫБОР
	|		КОГДА &ИспользоватьСтатьиФинансирования
	|			ТОГДА НастройкиБухучета.СтатьяФинансирования
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|	КОНЕЦ КАК СтатьяФинансирования,
	|	НастройкиБухучета.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	ВЫБОР
	|		КОГДА &ЕстьЕНВД
	|				И НастройкиБухучета.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ЕНВД)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОблагаетсяЕНВД
	|ИЗ
	|	ВТНастройкиБухучета КАК НастройкиБухучета";
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции 

Функция НастройкиБухучетаСотрудниковДляРасчетаОбязательств(Организация, Период, Сотрудники)

	НастройкиБухучетаСотрудников = ОтражениеЗарплатыВБухучете.НоваяТаблицаНастройкиБухучетаСотрудников();
	
	КадровыеДанныеСтрока = "Подразделение,ТерриторияВыполненияРаботВОрганизации";
	Если Сотрудники = Неопределено Тогда
		ПараметрыПолучения = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
		ПараметрыПолучения.Организация      = Организация;
		ПараметрыПолучения.НачалоПериода    = НачалоМесяца(Период);
		ПараметрыПолучения.ОкончаниеПериода = КонецМесяца(Период);
		ПараметрыПолучения.Вставить("КадровыеДанные", КадровыеДанныеСтрока);
		ТаблицаСотрудников = КадровыйУчет.СотрудникиОрганизации(Истина, ПараметрыПолучения);
	Иначе
		ТаблицаСотрудников = КадровыйУчет.КадровыеДанныеСотрудников(Истина, Сотрудники, КадровыеДанныеСтрока, КонецМесяца(Период));
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Период", КонецМесяца(Период));
	Запрос.УстановитьПараметр("ТаблицаСотрудников", ТаблицаСотрудников);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	&Организация КАК Организация,
	|	&Период КАК Период,
	|	Таблица.Подразделение КАК Подразделение,
	|	Таблица.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	Таблица.Сотрудник КАК Сотрудник
	|ПОМЕСТИТЬ ВТСотрудники
	|ИЗ
	|	&ТаблицаСотрудников КАК Таблица";
	Запрос.Выполнить();
	
	СоздатьВТНастройкиБухучетаСотрудников(Организация, Период, Запрос.МенеджерВременныхТаблиц, "ВТСотрудники");
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиБухучета.Организация КАК Организация,
	|	НастройкиБухучета.Сотрудник КАК Сотрудник,
	|	НастройкиБухучета.Подразделение КАК Подразделение,
	|	НастройкиБухучета.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	НастройкиБухучета.СтатьяФинансирования КАК СтатьяФинансирования,
	|	НастройкиБухучета.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	НастройкиБухучета.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	НастройкиБухучета.ДоляРаспределения КАК ДоляРаспределения
	|ИЗ
	|	ВТНастройкиБухучетаСотрудников КАК НастройкиБухучета";
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(НастройкиБухучетаСотрудников.Добавить(), Выборка);
	КонецЦикла;
	
	Возврат НастройкиБухучетаСотрудников;

КонецФункции

// Возвращает бухучет начислений, входящий в базу расчета оценочных обязательств.
//
// 	Параметры:
// 		Организация - СправочникСсылка.Организации
// 		Сотрудники - Массив
// 		НачалоПериода – Дата 
// 		КонецПериода – Дата
// 		ОбязательстваПоОтпускам - Булево
// 		БазовыеНачисления - Массив, Неопределено.
//
// Возвращаемое значение:
// 		ТаблицаЗначений, содержит колонки
// 			* Сотрудник - СправочникСсылка.Сотрудники
// 			* ФизическоеЛицо - СправочникСсылка.ФизическиеЛица
// 			* Подразделение - СправочникСсылка.ПодразделенияОрганизаций
// 			* ПодразделениеУчетаЗатрат - СправочникСсылка.ПодразделенияОрганизаций
// 			* МестоВСтруктуреПредприятия - ОпределяемыйТип.МестоВСтруктуреПредприятия
// 			* СтатьяФинансирования - СправочникСсылка.СтатьиФинансированияЗарплата
// 			* СпособОтраженияЗарплатыВБухучете - СправочникСсылка.СпособыОтраженияЗарплатыВБухУчете
// 			* Сумма - Число.
//
Функция БухучетБазовыхНачисленийОценочныхОбязательств(Организация, Сотрудники, НачалоПериода, КонецПериода, ОбязательстваПоОтпускам, БазовыеНачисления) Экспорт
	
	ПодработкаСотрудник = Новый Соответствие;
	СотрудникиДляОбработки = Сотрудники;
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПодработки") И Сотрудники <> Неопределено Тогда
		
		// Для получения начислений и взносов дополним сотрудников подработками.
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Сотрудники", Сотрудники);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Сотрудники.Ссылка КАК Сотрудник,
		|	Сотрудники.ГоловнойСотрудник КАК ГоловнойСотрудник
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.ГоловнойСотрудник В(&Сотрудники)
		|	И Сотрудники.Ссылка <> Сотрудники.ГоловнойСотрудник";
		РезультатЗапроса = Запрос.Выполнить();
		
		Если Не РезультатЗапроса.Пустой() Тогда
			СотрудникиДляОбработки = ОбщегоНазначения.СкопироватьРекурсивно(Сотрудники);
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				ПодработкаСотрудник.Вставить(Выборка.Сотрудник, Выборка.ГоловнойСотрудник);
				СотрудникиДляОбработки.Добавить(Выборка.Сотрудник);
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
	Периоды = Новый Массив;
	ТекущийПериод = НачалоМесяца(НачалоПериода);
	Пока ТекущийПериод <= КонецПериода Цикл
		Периоды.Добавить(ТекущийПериод);
		ТекущийПериод = ДобавитьМесяц(ТекущийПериод, 1);
	КонецЦикла;
	
	БухучетБазовыхНачислений = ТаблицаБухучетБазовыхНачислений();
	БухучетНачисленийЗаПериод = ТаблицаБухучетБазовыхНачисленийЗаПериод();
	
	ИспользоватьСтатьиФинансирования = ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный");
	
	Для каждого Период Из Периоды Цикл
		
		БухучетНачисленийЗаПериод.Очистить();
		
		Если ИспользоватьСтатьиФинансирования Тогда
			БухучетНачислений = БухучетНачисленийПоСтатьям(Организация, Период, Новый Структура("Сотрудники", Сотрудники));
		Иначе
			ТаблицаНачислений = ОтражениеЗарплатыВУчете.ДанныеДляОтражениеВУчетеНачислений(Организация, Период, Сотрудники);
			БухучетНачислений = БухучетНачислений(Организация, Период, ТаблицаНачислений);
			ТаблицаНачислений 	= Неопределено;
		КонецЕсли;
		
		Если ОбязательстваПоОтпускам Тогда
			
			БухучетНачислений = БазовыеНачисленияДляРасчетаОценочныхОбязательствОтпусков(Организация, Период, БухучетНачислений);
			
			Отбор = Новый Структура("РасчетПоСохраняемомуЗаработку", Ложь);
			
			ТаблицаНачисленийСреднему = БухучетНачислений.Скопировать(Отбор);
			
			ТаблицаНачисленийСреднему.Колонки.Сумма.Имя = "ФондОплатыТруда";
			ТаблицаНачисленийСреднему.Колонки.Добавить("СтраховыеВзносы", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
			ТаблицаНачисленийСреднему.Колонки.Добавить("ФССНесчастныеСлучаи", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
			
			УдалитьСтрокиИсключаемыеИзРасчетаСреднегоЗаработка(ТаблицаНачисленийСреднему, Организация, Период);
			ТаблицаНачисленийСреднему.Колонки.ФондОплатыТруда.Имя = "Сумма";
			
			Для каждого СтрокаТЗ Из ТаблицаНачисленийСреднему Цикл
				ЗаполнитьЗначенияСвойств(БухучетНачисленийЗаПериод.Добавить(), СтрокаТЗ);
			КонецЦикла;
			
			Отбор.РасчетПоСохраняемомуЗаработку = Истина;
			ТаблицаНачисленийПоСохраняемомуЗаработку = БухучетНачислений.НайтиСтроки(Отбор);
			Для каждого СтрокаТЗ Из ТаблицаНачисленийПоСохраняемомуЗаработку Цикл
				ЗаполнитьЗначенияСвойств(БухучетНачисленийЗаПериод.Добавить(), СтрокаТЗ);
			КонецЦикла;
			
		Иначе
			
			Если Не ЗначениеЗаполнено(БазовыеНачисления) Тогда
				НачисленияДляОтбора = БазовыеНачисленияОплатаТруда();
			Иначе
				НачисленияДляОтбора = БазовыеНачисления;
			КонецЕсли;
			
			НачисленияБазы = Новый Соответствие;
			Для каждого Начисление Из НачисленияДляОтбора Цикл
				НачисленияБазы.Вставить(Начисление, Истина);
			КонецЦикла;
			
			Для каждого СтрокаТЗ Из БухучетНачислений Цикл
				Если НачисленияБазы[СтрокаТЗ.Начисление] <> Неопределено Тогда
					ЗаполнитьЗначенияСвойств(БухучетНачисленийЗаПериод.Добавить(), СтрокаТЗ);
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОрганизационнаяСтруктура") Тогда 
			Модуль = ОбщегоНазначения.ОбщийМодуль("ОрганизационнаяСтруктура");
			ИмяТаблицы = "БухучетНачисленийЗаПериод";
			ДанныеДокумента = Новый Структура(ИмяТаблицы, БухучетНачисленийЗаПериод);
			Модуль.ДополнитьДанныеДокументаМестомВСтруктуреПредприятия(ДанныеДокумента, ИмяТаблицы);
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(БухучетНачисленийЗаПериод, БухучетБазовыхНачислений);
		
	КонецЦикла;
	
	ОбработанныеСотрудники = ОбщегоНазначения.ВыгрузитьКолонку(БухучетБазовыхНачислений, "Сотрудник", Истина);
	СотрудникиСБухучетом = ОбщегоНазначенияБЗККлиентСервер.МассивВСоответствие(ОбработанныеСотрудники);
	
	СотрудникиБезБухучета = Новый Массив;
	Для каждого Сотрудник Из Сотрудники Цикл
		Если СотрудникиСБухучетом[Сотрудник] = Неопределено Тогда
			СотрудникиБезБухучета.Добавить(Сотрудник);
		КонецЕсли;
	КонецЦикла;
	
	Если СотрудникиБезБухучета.Количество() > 0 Тогда
		
		БухучетНачисленийПоУмолчанию = НастройкиБухучетаДляРаспределенияПоУмолчанию(Организация, СотрудникиБезБухучета, КонецПериода);
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(БухучетНачисленийПоУмолчанию, БухучетБазовыхНачислений);
		
	КонецЕсли;
	
	Если ПодработкаСотрудник.Количество() > 0 Тогда
		Для каждого СтрокаТЗ Из БухучетБазовыхНачислений Цикл
			ГоловнойСотрудник = ПодработкаСотрудник[СтрокаТЗ.Сотрудник];
			Если ГоловнойСотрудник <> Неопределено Тогда
				СтрокаТЗ.Сотрудник = ГоловнойСотрудник;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ОтражениеЗарплатыВУчете.СвернутьТаблицу(БухучетБазовыхНачислений);
	
	Возврат БухучетБазовыхНачислений;

КонецФункции

// Возвращает бухучет начислений по умолчанию
//
// 	Параметры:
// 		Организация - СправочникСсылка.Организации
// 		Сотрудники - Массив
// 		Период – Дата 
//
// Возвращаемое значение:
// 		ТаблицаЗначений, содержит колонки
// 			* Сотрудник - СправочникСсылка.Сотрудники
// 			* ФизическоеЛицо - СправочникСсылка.ФизическиеЛица
// 			* Подразделение - СправочникСсылка.ПодразделенияОрганизаций
// 			* ПодразделениеУчетаЗатрат - СправочникСсылка.ПодразделенияОрганизаций
// 			* МестоВСтруктуреПредприятия - ОпределяемыйТип.МестоВСтруктуреПредприятия
// 			* СтатьяФинансирования - СправочникСсылка.СтатьиФинансированияЗарплата
// 			* СпособОтраженияЗарплатыВБухучете - СправочникСсылка.СпособыОтраженияЗарплатыВБухУчете
// 			* Сумма - Число.
//
Функция НастройкиБухучетаДляРаспределенияПоУмолчанию(Организация, СотрудникиБезБухучета, КонецПериода) Экспорт
	
	БухучетНачисленийЗаПериод = ТаблицаБухучетБазовыхНачисленийЗаПериод();
	
	КадровыеДанныеСтрока = "Подразделение,ТерриторияВыполненияРаботВОрганизации";
	ТаблицаСотрудников = КадровыйУчет.КадровыеДанныеСотрудников(Истина, СотрудникиБезБухучета, КадровыеДанныеСтрока, КонецМесяца(КонецПериода));
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТаблицаСотрудников", ТаблицаСотрудников);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&Организация КАК Организация,
	|	ТаблицаСотрудников.Сотрудник КАК Сотрудник,
	|	ТаблицаСотрудников.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ТаблицаСотрудников.Период КАК Период,
	|	ТаблицаСотрудников.Подразделение КАК Подразделение,
	|	ТаблицаСотрудников.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	АВТОНОМЕРЗАПИСИ() КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ВТСотрудники
	|ИЗ
	|	&ТаблицаСотрудников КАК ТаблицаСотрудников"; 
	Запрос.Выполнить();
	
	СоздатьВТНастройкиБухучетаДляРаспределенияПоУмолчанию(Запрос.МенеджерВременныхТаблиц, "ВТСотрудники");
	
	Запрос.УстановитьПараметр("ДатаНачала", КонецМесяца(КонецПериода));
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Сотрудники.Сотрудник КАК Сотрудник,
	|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Сотрудники.Подразделение КАК Подразделение,
	|	Сотрудники.Подразделение КАК ПодразделениеУчетаЗатрат,
	|	НастройкиБухучета.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	НастройкиБухучета.СтатьяФинансирования КАК СтатьяФинансирования,
	|	&ДатаНачала КАК ДатаНачала,
	|	100 КАК Сумма
	|ИЗ
	|	ВТСотрудники КАК Сотрудники
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиБухучета КАК НастройкиБухучета
	|		ПО Сотрудники.Сотрудник = НастройкиБухучета.Сотрудник";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(БухучетНачисленийЗаПериод.Добавить(), Выборка);
	КонецЦикла;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОрганизационнаяСтруктура") Тогда 
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОрганизационнаяСтруктура");
		ИмяТаблицы = "БухучетНачисленийЗаПериод";
		ДанныеДокумента = Новый Структура(ИмяТаблицы, БухучетНачисленийЗаПериод);
		Модуль.ДополнитьДанныеДокументаМестомВСтруктуреПредприятия(ДанныеДокумента, ИмяТаблицы);
	КонецЕсли;
	
	Возврат БухучетНачисленийЗаПериод;
	
КонецФункции

Функция БазовыеНачисленияДляРасчетаОценочныхОбязательствОтпусков(Организация, Период, ТаблицаНачислений)
	
	БазовыеНачисления = Новый ТаблицаЗначений;
	БазовыеНачисления.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	БазовыеНачисления.Колонки.Добавить("ФизическоеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	БазовыеНачисления.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	БазовыеНачисления.Колонки.Добавить("ПодразделениеУчетаЗатрат", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	БазовыеНачисления.Колонки.Добавить("МестоВСтруктуреПредприятия", Метаданные.ОпределяемыеТипы.МестоВСтруктуреПредприятия.Тип);
	БазовыеНачисления.Колонки.Добавить("СпособОтраженияЗарплатыВБухучете", Новый ОписаниеТипов("СправочникСсылка.СпособыОтраженияЗарплатыВБухУчете"));
	БазовыеНачисления.Колонки.Добавить("СтатьяФинансирования", Новый ОписаниеТипов("СправочникСсылка.СтатьиФинансированияЗарплата"));
	БазовыеНачисления.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	БазовыеНачисления.Колонки.Добавить("РасчетПоСохраняемомуЗаработку", Новый ОписаниеТипов("Булево"));
	БазовыеНачисления.Колонки.Добавить("ДатаНачала", Новый ОписаниеТипов("Дата"));
	БазовыеНачисления.Колонки.Добавить("Начисление", Новый ОписаниеТипов("ПланВидовРасчетаСсылка.Начисления"));
	
	СотрудникиДляОбработки = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаНачислений, "Сотрудник", Истина);
	ПараметрыПолученияФОТ  = ОтражениеЗарплатыВБухучете.ПараметрыПолученияФОТСотрудниковДляОценочныхОбязательствОтпусков(СотрудникиДляОбработки, Период);
	
	// СоответствиеСотрудников - фильтр по сотрудникам, если Значение = Истина, расчет обязательств по сотруднику
	// выполняется по среднему заработку, если Ложь - по сохраняемому заработку.
	СоответствиеСотрудников 			= ПараметрыПолученияФОТ.Сотрудники;
	СоответствиеПоСреднемуЗаработку 	= ПараметрыПолученияФОТ.БазаОтпускаПоСреднемуЗаработку;
	СоответствиеПоСохраняемомуЗаработку = ПараметрыПолученияФОТ.БазаОтпускаПоСохраняемомуЗаработку;
	
	Для каждого СтрокаТЗ Из ТаблицаНачислений Цикл
		
		СтрокаПодходит = Ложь;
		
		РасчетПоСреднемуЗаработку = СоответствиеСотрудников[СтрокаТЗ.Сотрудник];
		Если РасчетПоСреднемуЗаработку = Истина Тогда
			СтрокаПодходит = (СоответствиеПоСреднемуЗаработку[СтрокаТЗ.Начисление] = Истина);
		ИначеЕсли РасчетПоСреднемуЗаработку = Ложь Тогда
			СтрокаПодходит = (СоответствиеПоСохраняемомуЗаработку[СтрокаТЗ.Начисление] = Истина);
		КонецЕсли;
		
		Если СтрокаПодходит Тогда
			НоваяСтрока = БазовыеНачисления.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
			НоваяСтрока.РасчетПоСохраняемомуЗаработку = Не РасчетПоСреднемуЗаработку;
		КонецЕсли;
		
	КонецЦикла;
	
	ОтражениеЗарплатыВУчете.СвернутьТаблицу(БазовыеНачисления);
	
	Возврат БазовыеНачисления;
	
КонецФункции

Функция БазовыеНачисленияОплатаТруда() Экспорт

	СписокКатегорий = Новый Массив;
	СписокКатегорий.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ПовременнаяОплатаТруда);
	СписокКатегорий.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.СдельнаяОплатаТруда);
	СписокКатегорий.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаТрудаВНатуральнойФорме);
	СписокКатегорий.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.Премия);
	СписокКатегорий.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.КвартальнаяПремия);
	СписокКатегорий.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.РайонныйКоэффициент);
	СписокКатегорий.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.СевернаяНадбавка);
	СписокКатегорий.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ДоплатаЗаСовмещение);
	СписокКатегорий.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.НадбавкаЗаВредность);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокКатегорий", СписокКатегорий);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Начисления.Ссылка КАК Ссылка
	|ИЗ
	|	ПланВидовРасчета.Начисления КАК Начисления
	|ГДЕ
	|	Начисления.КатегорияНачисленияИлиНеоплаченногоВремени В (&СписокКатегорий)";
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");	

КонецФункции

// Возвращает страховые взносы по сотрудникам.
// 	Параметры:
// 		Организация - СправочникСсылка.Организации
// 		Период - Дата
// 		Сотрудники - Массив
// 		ОбязательстваПоОтпускам - Булево
// 		БазовыеНачисления - Массив, Неопределено.
//
// Возвращаемое значение:
// 		ТаблицаЗначений, содержит колонки
// 			* Сотрудник - СправочникСсылка.Сотрудники
// 			* ФизическоеЛицо - СправочникСсылка.ФизическиеЛица
// 			* СтраховыеВзносы - Число
// 			* ФССНесчастныеСлучаи - Число
//
Функция ИсчисленныеВзносыДляРасчетаОценочныхОбязательств(Организация, Период, Сотрудники, ОбязательстваПоОтпускам, БазовыеНачисления) Экспорт

	Взносы = ОтражениеЗарплатыВУчете.ДанныеДляОтражениеВУчетеВзносов(Организация, Период, Сотрудники);
	
	ТаблицаВзносов = Новый ТаблицаЗначений;
	ТаблицаВзносов.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	ТаблицаВзносов.Колонки.Добавить("ФизическоеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	ТаблицаВзносов.Колонки.Добавить("СтраховыеВзносы", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	ТаблицаВзносов.Колонки.Добавить("ФССНесчастныеСлучаи", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	
	Если ОбязательстваПоОтпускам Тогда
		
		Взносы.Колонки.Добавить("СтраховыеВзносы", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
		Взносы.Колонки.Добавить("ФССНесчастныеСлучаи", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
		
		Для каждого СтрокаТЗ Из Взносы Цикл
			Если СтрокаТЗ.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ФССНС Тогда
				СтрокаТЗ.ФССНесчастныеСлучаи = СтрокаТЗ.Сумма;
			Иначе
				СтрокаТЗ.СтраховыеВзносы = СтрокаТЗ.Сумма;
			КонецЕсли;
		КонецЦикла;
		
		ОтражениеЗарплатыВУчете.СвернутьТаблицу(Взносы);
		
		Взносы = ВзносыДляРасчетаОценочныхОбязательствОтпусков(Организация, Период, Взносы);
		
		Отбор = Новый Структура("РасчетПоСохраняемомуЗаработку", Ложь);
		ВзносыНачисленийСреднему = Взносы.Скопировать(Отбор);
		ВзносыНачисленийСреднему.Колонки.Добавить("ФондОплатыТруда", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
		
		УдалитьСтрокиИсключаемыеИзРасчетаСреднегоЗаработка(ВзносыНачисленийСреднему, Организация, Период);
		
		Для каждого СтрокаТЗ Из ВзносыНачисленийСреднему Цикл
			ЗаполнитьЗначенияСвойств(ТаблицаВзносов.Добавить(), СтрокаТЗ);
		КонецЦикла;
		
		Отбор.РасчетПоСохраняемомуЗаработку = Истина;
		ВзносыПоСохраняемомуЗаработку = Взносы.НайтиСтроки(Отбор);
		Для каждого СтрокаТЗ Из ВзносыПоСохраняемомуЗаработку Цикл
			ЗаполнитьЗначенияСвойств(ТаблицаВзносов.Добавить(), СтрокаТЗ);
		КонецЦикла;
		
	Иначе
		
		Если Не ЗначениеЗаполнено(БазовыеНачисления) Тогда
			НачисленияДляОтбора = БазовыеНачисленияОплатаТруда();
		Иначе
			НачисленияДляОтбора = БазовыеНачисления;
		КонецЕсли;
		
		НачисленияБазы = ОбщегоНазначенияБЗККлиентСервер.МассивВСоответствие(НачисленияДляОтбора);
		
		Для каждого СтрокаТЗ Из Взносы Цикл
			
			Если НачисленияБазы[СтрокаТЗ.Начисление] = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = ТаблицаВзносов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ, "Сотрудник,ФизическоеЛицо");
			Если СтрокаТЗ.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ФССНС Тогда
				НоваяСтрока.ФССНесчастныеСлучаи = СтрокаТЗ.Сумма;
			Иначе
				НоваяСтрока.СтраховыеВзносы = СтрокаТЗ.Сумма;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ОтражениеЗарплатыВУчете.СвернутьТаблицу(ТаблицаВзносов);
	
	Возврат ТаблицаВзносов;

КонецФункции

Функция ВзносыДляРасчетаОценочныхОбязательствОтпусков(Организация, Период, ТаблицаВзносов) Экспорт
	
	Взносы = Новый ТаблицаЗначений;
	Взносы.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	Взносы.Колонки.Добавить("ФизическоеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	Взносы.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	Взносы.Колонки.Добавить("СтраховыеВзносы", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	Взносы.Колонки.Добавить("ФССНесчастныеСлучаи", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	Взносы.Колонки.Добавить("РасчетПоСохраняемомуЗаработку", Новый ОписаниеТипов("Булево"));
	Взносы.Колонки.Добавить("ДатаНачала", Новый ОписаниеТипов("Дата"));
	Взносы.Колонки.Добавить("Начисление", Новый ОписаниеТипов("ПланВидовРасчетаСсылка.Начисления"));
	
	СотрудникиДляОбработки = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаВзносов, "Сотрудник", Истина);
	ПараметрыПолученияФОТ  = ОтражениеЗарплатыВБухучете.ПараметрыПолученияФОТСотрудниковДляОценочныхОбязательствОтпусков(СотрудникиДляОбработки, Период);
	
	// СоответствиеСотрудников - фильтр по сотрудникам, если Значение = Истина, расчет обязательств по сотруднику
	// выполняется по среднему заработку, если Ложь - по сохраняемому заработку.
	СоответствиеСотрудников 			= ПараметрыПолученияФОТ.Сотрудники;
	СоответствиеПоСреднемуЗаработку 	= ПараметрыПолученияФОТ.БазаОтпускаПоСреднемуЗаработку;
	СоответствиеПоСохраняемомуЗаработку = ПараметрыПолученияФОТ.БазаОтпускаПоСохраняемомуЗаработку;
	
	Для каждого СтрокаТЗ Из ТаблицаВзносов Цикл
		
		СтрокаПодходит = Ложь;
		
		РасчетПоСреднемуЗаработку = СоответствиеСотрудников[СтрокаТЗ.Сотрудник];
		Если РасчетПоСреднемуЗаработку = Истина Тогда
			СтрокаПодходит = (СоответствиеПоСреднемуЗаработку[СтрокаТЗ.Начисление] = Истина);
		ИначеЕсли РасчетПоСреднемуЗаработку = Ложь Тогда
			СтрокаПодходит = (СоответствиеПоСохраняемомуЗаработку[СтрокаТЗ.Начисление] = Истина);
		КонецЕсли;
		
		Если СтрокаПодходит Тогда
			НоваяСтрока = Взносы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
			НоваяСтрока.РасчетПоСохраняемомуЗаработку = Не РасчетПоСреднемуЗаработку;
		КонецЕсли;
		
	КонецЦикла;
	
	ОтражениеЗарплатыВУчете.СвернутьТаблицу(Взносы);
	
	Возврат Взносы;
	
КонецФункции

Процедура ПересчитатьСуммыФОТПоСохраняемомуЗаработку(ТаблицаФОТ, БазовыеНачисленияОтпуска, Организация, Период)

	Если ТаблицаФОТ.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Коэффициенты = Новый Соответствие;
	Для каждого СтрокаТЗ Из БазовыеНачисленияОтпуска Цикл
		Коэффициенты.Вставить(СтрокаТЗ.Начисление, СтрокаТЗ.Коэффициент);
	КонецЦикла;
	
	Для каждого СтрокаТЗ Из ТаблицаФОТ Цикл
		Коэффициент = Коэффициенты[СтрокаТЗ.Начисление];
		Если Коэффициент <> 1 Тогда
			СтрокаТЗ.ФондОплатыТруда 		= Окр(Коэффициент * СтрокаТЗ.ФондОплатыТруда ,2);
			СтрокаТЗ.СтраховыеВзносы 		= Окр(Коэффициент * СтрокаТЗ.СтраховыеВзносы ,2);
			СтрокаТЗ.ФССНесчастныеСлучаи 	= Окр(Коэффициент * СтрокаТЗ.ФССНесчастныеСлучаи ,2);
		КонецЕсли;
	КонецЦикла;
	
	Сотрудники = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаФОТ, "Сотрудник", Истина);
	
	НачисленияОтбора = Новый Массив;
	НачисленияОтбора.Добавить(ПланыВидовРасчета.Начисления.НачислениеРайонныйКоэффициент());
	НачисленияОтбора.Добавить(ПланыВидовРасчета.Начисления.НачислениеСевернаяНадбавка());
	
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Период));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Период));
	Запрос.УстановитьПараметр("Сотрудники", Сотрудники);
	Запрос.УстановитьПараметр("НачисленияОтбора", НачисленияОтбора);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Начисления.Регистратор КАК Регистратор,
	|	Начисления.ПериодДействия КАК ПериодДействия,
	|	Начисления.НачислениеУдержание КАК Начисление,
	|	Начисления.ДатаНачала КАК ДатаНачала,
	|	Начисления.ДатаОкончания КАК ДатаОкончания,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.Подразделение КАК Подразделение,
	|	СУММА(Начисления.Сумма) КАК Сумма
	|ПОМЕСТИТЬ ВТНачисленияПоБазе
	|ИЗ
	|	РегистрНакопления.НачисленияУдержанияПоСотрудникам КАК Начисления
	|ГДЕ
	|	Начисления.Организация = &Организация
	|	И Начисления.НачислениеУдержание В (&НачисленияОтбора)
	|	И Начисления.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Начисления.Сотрудник В(&Сотрудники)
	|
	|СГРУППИРОВАТЬ ПО
	|	Начисления.НачислениеУдержание,
	|	Начисления.ПериодДействия,
	|	Начисления.Регистратор,
	|	Начисления.ДатаНачала,
	|	Начисления.ДатаОкончания,
	|	Начисления.Сотрудник,
	|	Начисления.Подразделение";
	Запрос.Выполнить();
	
	Если Не ЗарплатаКадры.ВТСодержитСтроки(МенеджерВТ, "ВТНачисленияПоБазе") Тогда
		Возврат;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ПериодРегистрации", НачалоМесяца(Период));
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Начисления.Регистратор КАК Регистратор,
	|	Начисления.НомерСтроки КАК НомерСтроки,
	|	НачисленияПоБазе.Сотрудник КАК Сотрудник,
	|	НачисленияПоБазе.Подразделение КАК Подразделение,
	|	НачисленияПоБазе.Начисление КАК Начисление,
	|	НачисленияПоБазе.ДатаНачала КАК ДатаНачала,
	|	НачисленияПоБазе.ДатаОкончания КАК ДатаОкончания
	|ПОМЕСТИТЬ ВТНачисленияПоБазеИзРегистраРасчета
	|ИЗ
	|	ВТНачисленияПоБазе КАК НачисленияПоБазе
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрРасчета.Начисления КАК Начисления
	|		ПО НачисленияПоБазе.Регистратор = Начисления.Регистратор
	|			И НачисленияПоБазе.Сотрудник = Начисления.Сотрудник
	|			И НачисленияПоБазе.Начисление = Начисления.ВидРасчета
	|			И (НачисленияПоБазе.Сумма <> 0)
	|			И НачисленияПоБазе.ДатаНачала = Начисления.ПериодДействияНачало
	|			И (НачисленияПоБазе.ДатаОкончания = НАЧАЛОПЕРИОДА(Начисления.ПериодДействияКонец, ДЕНЬ))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&ПериодРегистрации КАК ПериодРегистрации,
	|	Начисления.Регистратор КАК Регистратор,
	|	Начисления.НомерСтроки КАК НомерСтроки,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.Начисление КАК ВидРасчета,
	|	Начисления.ДатаНачала КАК БазовыйПериодНачало,
	|	Начисления.ДатаОкончания КАК БазовыйПериодКонец
	|ПОМЕСТИТЬ ВТОтборНачислений
	|ИЗ
	|	ВТНачисленияПоБазеИзРегистраРасчета КАК Начисления";
	Запрос.Выполнить();
	
	// Получение базы.
	РасчетЗарплатыРасширенный.СоздатьВТРасчетнаяБазаНачисленийПоВременнойТаблицеКаскадно(МенеджерВТ);
	
	Запрос.УстановитьПараметр("БазовыеНачисленияОтпуска", БазовыеНачисленияОтпуска);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасчетнаяБаза.Регистратор КАК Регистратор,
	|	РасчетнаяБаза.НомерСтроки КАК НомерСтроки,
	|	РасчетнаяБаза.РегистраторРазрез КАК РегистраторРазрез,
	|	РасчетнаяБаза.НомерСтрокиРазрез КАК НомерСтрокиРазрез
	|ПОМЕСТИТЬ ВТТаблица1
	|ИЗ
	|	ВТРасчетнаяБаза КАК РасчетнаяБаза
	|ГДЕ
	|	РасчетнаяБаза.Регистратор <> РасчетнаяБаза.РегистраторРазрез
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетнаяБаза.Регистратор КАК Регистратор,
	|	РасчетнаяБаза.НомерСтроки КАК НомерСтроки,
	|	РасчетнаяБаза.РегистраторРазрез КАК РегистраторРазрез,
	|	РасчетнаяБаза.НомерСтрокиРазрез КАК НомерСтрокиРазрез
	|ПОМЕСТИТЬ ВТТаблица2
	|ИЗ
	|	ВТРасчетнаяБаза КАК РасчетнаяБаза
	|ГДЕ
	|	РасчетнаяБаза.Регистратор = РасчетнаяБаза.РегистраторРазрез
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица1.Регистратор КАК Регистратор,
	|	Таблица1.НомерСтроки КАК НомерСтроки,
	|	Таблица1.РегистраторРазрез КАК РегистраторРазрез,
	|	Таблица1.НомерСтрокиРазрез КАК НомерСтрокиРазрез
	|ПОМЕСТИТЬ ВТИсключаемыеСтроки
	|ИЗ
	|	ВТТаблица1 КАК Таблица1
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТТаблица2 КАК Таблица2
	|		ПО Таблица1.РегистраторРазрез = Таблица2.РегистраторРазрез
	|			И Таблица1.НомерСтрокиРазрез = Таблица2.НомерСтрокиРазрез
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетнаяБаза.Регистратор КАК Регистратор,
	|	РасчетнаяБаза.НомерСтроки КАК НомерСтроки,
	|	РасчетнаяБаза.РегистраторРазрез КАК РегистраторРазрез,
	|	РасчетнаяБаза.НомерСтрокиРазрез КАК НомерСтрокиРазрез,
	|	РасчетнаяБаза.ВидРасчетаРазрез КАК ВидРасчетаРазрез,
	|	РасчетнаяБаза.РезультатБаза КАК РезультатБаза
	|ПОМЕСТИТЬ ВТРасчетнаяБазаСКорректировкой
	|ИЗ
	|	ВТРасчетнаяБаза КАК РасчетнаяБаза
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИсключаемыеСтроки КАК ИсключаемыеСтроки
	|		ПО РасчетнаяБаза.Регистратор = ИсключаемыеСтроки.Регистратор
	|			И РасчетнаяБаза.НомерСтроки = ИсключаемыеСтроки.НомерСтроки
	|			И РасчетнаяБаза.РегистраторРазрез = ИсключаемыеСтроки.РегистраторРазрез
	|			И РасчетнаяБаза.НомерСтрокиРазрез = ИсключаемыеСтроки.НомерСтрокиРазрез
	|ГДЕ
	|	ИсключаемыеСтроки.НомерСтроки ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БазовыеНачисленияОтпуска.Начисление КАК Начисление,
	|	БазовыеНачисленияОтпуска.Коэффициент КАК Коэффициент
	|ПОМЕСТИТЬ ВТБазовыеНачисленияОтпуска
	|ИЗ
	|	&БазовыеНачисленияОтпуска КАК БазовыеНачисленияОтпуска
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетнаяБаза.Регистратор КАК Регистратор,
	|	РасчетнаяБаза.НомерСтроки КАК НомерСтроки,
	|	СУММА(РасчетнаяБаза.РезультатБаза) КАК РезультатБаза,
	|	СУММА(ВЫБОР
	|			КОГДА БазовыеНачисления.Коэффициент ЕСТЬ NULL
	|				ТОГДА 0
	|			ИНАЧЕ ВЫРАЗИТЬ(БазовыеНачисления.Коэффициент * РасчетнаяБаза.РезультатБаза КАК ЧИСЛО(25, 10))
	|		КОНЕЦ) КАК РезультатБазаПриведенная,
	|	НачисленияПоБазе.Сотрудник КАК Сотрудник,
	|	НачисленияПоБазе.Подразделение КАК Подразделение,
	|	НачисленияПоБазе.Начисление КАК Начисление,
	|	НачисленияПоБазе.ДатаНачала КАК ДатаНачала,
	|	НачисленияПоБазе.ДатаОкончания КАК ДатаОкончания
	|ПОМЕСТИТЬ ВТРасчетнаяБазаПриведенная
	|ИЗ
	|	ВТРасчетнаяБазаСКорректировкой КАК РасчетнаяБаза
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБазовыеНачисленияОтпуска КАК БазовыеНачисления
	|		ПО РасчетнаяБаза.ВидРасчетаРазрез = БазовыеНачисления.Начисление
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНачисленияПоБазеИзРегистраРасчета КАК НачисленияПоБазе
	|		ПО РасчетнаяБаза.Регистратор = НачисленияПоБазе.Регистратор
	|			И РасчетнаяБаза.НомерСтроки = НачисленияПоБазе.НомерСтроки
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетнаяБаза.Регистратор,
	|	РасчетнаяБаза.НомерСтроки,
	|	НачисленияПоБазе.Сотрудник,
	|	НачисленияПоБазе.Подразделение,
	|	НачисленияПоБазе.Начисление,
	|	НачисленияПоБазе.ДатаНачала,
	|	НачисленияПоБазе.ДатаОкончания
	|
	|ИМЕЮЩИЕ
	|	СУММА(РасчетнаяБаза.РезультатБаза) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетнаяБазаПриведенная.Сотрудник КАК Сотрудник,
	|	РасчетнаяБазаПриведенная.Подразделение КАК Подразделение,
	|	РасчетнаяБазаПриведенная.Начисление КАК Начисление,
	|	РасчетнаяБазаПриведенная.ДатаНачала КАК ДатаНачала
	|ИЗ
	|	ВТРасчетнаяБазаПриведенная КАК РасчетнаяБазаПриведенная
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетнаяБазаПриведенная.Сотрудник КАК Сотрудник,
	|	РасчетнаяБазаПриведенная.Подразделение КАК Подразделение,
	|	РасчетнаяБазаПриведенная.Начисление КАК Начисление,
	|	РасчетнаяБазаПриведенная.ДатаНачала КАК ДатаНачала,
	|	Начисления.Результат КАК Результат,
	|	РасчетнаяБазаПриведенная.РезультатБаза КАК РезультатБаза,
	|	РасчетнаяБазаПриведенная.РезультатБазаПриведенная КАК РезультатБазаПриведенная
	|ИЗ
	|	ВТРасчетнаяБазаПриведенная КАК РасчетнаяБазаПриведенная
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрРасчета.Начисления КАК Начисления
	|		ПО РасчетнаяБазаПриведенная.Регистратор = Начисления.Регистратор
	|			И РасчетнаяБазаПриведенная.НомерСтроки = Начисления.НомерСтроки";
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	КоличествоЗапросов = РезультатЗапроса.ВГраница();
	
	ТаблицаСотрудников = РезультатЗапроса[КоличествоЗапросов - 1].Выгрузить();
	НачисленияПоБазе = РезультатЗапроса[КоличествоЗапросов].Выгрузить();
	
	КолонкиОтбора = "Сотрудник,Начисление,ДатаНачала,Подразделение";
	Отбор = Новый Структура(КолонкиОтбора);
	НачисленияПоБазе.Индексы.Добавить(КолонкиОтбора);
	ТаблицаФОТ.Индексы.Добавить(КолонкиОтбора);
	
	Для каждого СтрокаПоСотруднику Из ТаблицаСотрудников Цикл
	
		ЗаполнитьЗначенияСвойств(Отбор, СтрокаПоСотруднику);
		
		НайденныеСтроки = НачисленияПоБазе.НайтиСтроки(Отбор);
		РезультатИтог = 0;
		РезультатПриведенныйИтог = 0;
		Для каждого СтрокаТЗ Из НайденныеСтроки Цикл
			
			РезультатИтог = РезультатИтог + СтрокаТЗ.Результат;
			
			Результат = СтрокаТЗ.Результат;
			Если СтрокаТЗ.РезультатБазаПриведенная <> СтрокаТЗ.РезультатБаза Тогда
				Результат = Результат * СтрокаТЗ.РезультатБазаПриведенная/СтрокаТЗ.РезультатБаза;
			КонецЕсли;
			
			РезультатПриведенныйИтог = РезультатПриведенныйИтог + Результат;
			
		КонецЦикла;
		
		Если РезультатИтог = РезультатПриведенныйИтог Тогда
			Продолжить;
		КонецЕсли;
		
		ДоляБазы = РезультатПриведенныйИтог/РезультатИтог;
		
		СтрокиФОТ = ТаблицаФОТ.НайтиСтроки(Отбор);
		ПривестиСуммыИсходнойТаблицыКДолеБазы(СтрокиФОТ, ДоляБазы);
		
	КонецЦикла;
	
	ТаблицаФОТ.Индексы.Очистить();

КонецПроцедуры

Функция ТаблицаБухучетБазовыхНачислений()
	
	БухучетБазовыхНачислений = Новый ТаблицаЗначений;
	БухучетБазовыхНачислений.Колонки.Добавить("ФизическоеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	БухучетБазовыхНачислений.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	БухучетБазовыхНачислений.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	БухучетБазовыхНачислений.Колонки.Добавить("ПодразделениеУчетаЗатрат", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	БухучетБазовыхНачислений.Колонки.Добавить("МестоВСтруктуреПредприятия", Метаданные.ОпределяемыеТипы.МестоВСтруктуреПредприятия.Тип);
	БухучетБазовыхНачислений.Колонки.Добавить("СпособОтраженияЗарплатыВБухучете", Новый ОписаниеТипов("СправочникСсылка.СпособыОтраженияЗарплатыВБухУчете"));
	БухучетБазовыхНачислений.Колонки.Добавить("СтатьяФинансирования", Новый ОписаниеТипов("СправочникСсылка.СтатьиФинансированияЗарплата"));
	БухучетБазовыхНачислений.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	
	Возврат БухучетБазовыхНачислений;
	
КонецФункции

Функция ТаблицаБухучетБазовыхНачисленийЗаПериод()
	
	БухучетБазовыхНачисленийЗаПериод = ТаблицаБухучетБазовыхНачислений();
	БухучетБазовыхНачисленийЗаПериод.Колонки.Добавить("ДатаНачала", Новый ОписаниеТипов("Дата"));
	
	Возврат БухучетБазовыхНачисленийЗаПериод;
	
КонецФункции

#КонецОбласти

#Область ОбслуживаниеРаспределенияРезультатов

Процедура СоздатьВТБухучетНачисленийСторно(МенеджерВременныхТаблиц, ИмяВТНачисления, СтрокиБухучетСторноНачислений)

	ИспользоватьСтатьиФинансирования = ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата");
	РаботаВБюджетномУчреждении 		 = ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Если ИспользоватьСтатьиФинансирования И СтрокиБухучетСторноНачислений.Количество() > 0 Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Начисления.ПериодРегистрации КАК ПериодРегистрации,
		|	Начисления.Организация КАК Организация,
		|	Начисления.Сотрудник КАК Сотрудник,
		|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
		|	Начисления.Подразделение КАК Подразделение,
		|	Начисления.Начисление КАК Начисление,
		|	Начисления.ДатаНачала КАК ДатаНачала,
		|	Начисления.ДатаОкончания КАК ДатаОкончания,
		|	Начисления.Сумма КАК Результат,
		|	Начисления.ДокументОснование КАК ДокументОснование,
		|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	Начисления.ИдентификаторСтрокиНачисления КАК ИдентификаторСтрокиНачисления,
		|	Начисления.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
		|	Начисления.Сторно КАК Сторно,
		|	Начисления.ФиксСторно КАК ФиксСторно
		|ИЗ
		|	ВТНачисленияДляОтраженияВБухучете КАК Начисления
		|ГДЕ
		|	Начисления.Сторно
		|	И НЕ Начисления.ФиксСторно
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Начисления.ПериодРегистрации КАК ПериодРегистрации,
		|	Начисления.Организация КАК Организация,
		|	Начисления.Сотрудник КАК Сотрудник,
		|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
		|	Начисления.Подразделение КАК Подразделение,
		|	Начисления.Начисление КАК Начисление,
		|	Начисления.ДатаНачала КАК ДатаНачала,
		|	Начисления.ДатаОкончания КАК ДатаОкончания,
		|	Начисления.Сумма КАК Результат,
		|	Начисления.ДокументОснование КАК ДокументОснование,
		|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	Начисления.ИдентификаторСтрокиНачисления КАК ИдентификаторСтрокиНачисления,
		|	Начисления.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
		|	Начисления.Сторно КАК Сторно,
		|	Начисления.ФиксСторно КАК ФиксСторно
		|ИЗ
		|	ВТНачисленияДляОтраженияВБухучете КАК Начисления
		|ГДЕ
		|	Начисления.ФиксСторно";
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляОтраженияВБухучете", ИмяВТНачисления);
		
		РезультатЗапроса = Запрос.ВыполнитьПакет();
		
		ТаблицаНачислений = РезультатЗапроса[0].Выгрузить();
		БухучетНачисленийСторно = БухучетНачисленийСторноИзИсходнойТаблицы(ТаблицаНачислений, СтрокиБухучетСторноНачислений, ИспользоватьСтатьиФинансирования, РаботаВБюджетномУчреждении, Ложь);
		
		ТаблицаНачислений = РезультатЗапроса[1].Выгрузить();
		БухучетНачисленийФиксСторно = БухучетНачисленийСторноИзИсходнойТаблицы(ТаблицаНачислений, СтрокиБухучетСторноНачислений, ИспользоватьСтатьиФинансирования, РаботаВБюджетномУчреждении, Истина);
		
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(БухучетНачисленийФиксСторно, БухучетНачисленийСторно);
		
		Запрос.УстановитьПараметр("БухучетНачисленийСторно", БухучетНачисленийСторно);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	БухучетНачисленийСторно.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	БухучетНачисленийСторно.ИдентификаторСтрокиНачисления КАК ИдентификаторСтрокиНачисления,
		|	БухучетНачисленийСторно.Организация КАК Организация,
		|	БухучетНачисленийСторно.Сотрудник КАК Сотрудник,
		|	БухучетНачисленийСторно.ФизическоеЛицо КАК ФизическоеЛицо,
		|	БухучетНачисленийСторно.Подразделение КАК Подразделение,
		|	БухучетНачисленийСторно.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
		|	БухучетНачисленийСторно.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	БухучетНачисленийСторно.СтатьяФинансирования КАК СтатьяФинансирования,
		|	БухучетНачисленийСторно.СтатьяРасходов КАК СтатьяРасходов,
		|	БухучетНачисленийСторно.Результат КАК Сумма,
		|	БухучетНачисленийСторно.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	БухучетНачисленийСторно.ДатаНачала КАК ДатаНачала,
		|	БухучетНачисленийСторно.ДатаОкончания КАК ДатаОкончания,
		|	БухучетНачисленийСторно.Начисление КАК Начисление,
		|	БухучетНачисленийСторно.Территория КАК Территория
		|ПОМЕСТИТЬ ВТБухучетНачисленийСторноВременная
		|ИЗ
		|	&БухучетНачисленийСторно КАК БухучетНачисленийСторно
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	БухучетНачисленийСторно.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	БухучетНачисленийСторно.ИдентификаторСтрокиНачисления КАК ИдентификаторСтрокиНачисления,
		|	БухучетНачисленийСторно.Организация КАК Организация,
		|	БухучетНачисленийСторно.Сотрудник КАК Сотрудник,
		|	БухучетНачисленийСторно.ФизическоеЛицо КАК ФизическоеЛицо,
		|	БухучетНачисленийСторно.Подразделение КАК Подразделение,
		|	БухучетНачисленийСторно.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
		|	БухучетНачисленийСторно.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	БухучетНачисленийСторно.СтатьяФинансирования КАК СтатьяФинансирования,
		|	БухучетНачисленийСторно.СтатьяРасходов КАК СтатьяРасходов,
		|	БухучетНачисленийСторно.Сумма КАК Сумма,
		|	БухучетНачисленийСторно.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	БухучетНачисленийСторно.ДатаНачала КАК ДатаНачала,
		|	БухучетНачисленийСторно.ДатаОкончания КАК ДатаОкончания,
		|	БухучетНачисленийСторно.Начисление КАК Начисление,
		|	ВЫБОР
		|		КОГДА БухучетНачисленийСторно.Территория = НЕОПРЕДЕЛЕНО
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.ТерриторииВыполненияРабот.ПустаяСсылка)
		|		ИНАЧЕ БухучетНачисленийСторно.Территория
		|	КОНЕЦ КАК Территория
		|ПОМЕСТИТЬ ВТБухучетНачисленийСторно
		|ИЗ
		|	ВТБухучетНачисленийСторноВременная КАК БухучетНачисленийСторно
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТБухучетНачисленийСторноВременная";
		Запрос.Выполнить();
		
	Иначе
		
		СоздатьПустуюВТБухучетНачислений(Запрос, "ВТБухучетНачисленийСторно");
		
	КонецЕсли;

КонецПроцедуры

Функция БухучетНачисленийСторноИзИсходнойТаблицы(ТаблицаНачислений, СтрокиНачисленийСторно, ИспользоватьСтатьиФинансирования, РаботаВБюджетномУчреждении, ФиксСторно)

	ТаблицаРаспределение = НоваяТаблицаБухучетНачислений();
	
	Если ТаблицаНачислений.Количество()=0 Тогда
		Возврат ТаблицаРаспределение;
	КонецЕсли;
	
	// Параметры для проверки результатов распределения.
	ПараметрыДляПроверки = ПараметрыДляПроверкиРезультатовРаспределенияНачислений();
	ПараметрыДляПроверки.ПроверяемыеПоля.СтатьяФинансирования = ИспользоватьСтатьиФинансирования;
	ПараметрыДляПроверки.ПроверяемыеПоля.СтатьяРасходов       = РаботаВБюджетномУчреждении;
	
	Для каждого СтрокаТЗ Из ТаблицаНачислений Цикл
		
		РезультатыРаспределения = СтрокиНачисленийСторно[СтрокаТЗ.ИдентификаторСтроки];
		Если РезультатыРаспределения = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		// отберем строки распределения по территории 
		КоллекцияРаспределения = НоваяТаблицаРаспределениеРезультатовНачислений();
		Для каждого СтрокаРаспределения Из РезультатыРаспределения Цикл
			Если (СтрокаРаспределения.Территория = СтрокаТЗ.ТерриторияВыполненияРаботВОрганизации)
				Или (НЕ ЗначениеЗаполнено(СтрокаРаспределения.Территория) И Не ЗначениеЗаполнено(СтрокаТЗ.ТерриторияВыполненияРаботВОрганизации)) Тогда
				НоваяСтрока = КоллекцияРаспределения.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРаспределения);
				НоваяСтрока.ИдентификаторСтроки = СтрокаТЗ.ИдентификаторСтроки;
			КонецЕсли;
		КонецЦикла;
		
		Если КоллекцияРаспределения.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ФиксСторно Тогда
			ЗарплатаКадры.РазнестиСуммуПоБазе(СтрокаТЗ.Результат, КоллекцияРаспределения, "Результат");
		КонецЕсли;
		
		// Выполним проверку результата распределения
		ВидОперации = ПараметрыДляПроверки.НачислениеУдержаниеВидОперации[СтрокаТЗ.Начисление];
		ПараметрыДляПроверки.ПроверяемыеПоля.СпособОтраженияЗарплатыВБухучете = ПараметрыДляПроверки.ВидыОперацийПособия.Найти(ВидОперации) = Неопределено;
		ЕстьОшибкиРаспределения = РезультатРаспределенияСодержитОшибки(СтрокаТЗ.Результат, КоллекцияРаспределения, ПараметрыДляПроверки.ПроверяемыеПоля);
		
		Если ЕстьОшибкиРаспределения Тогда
			
			Если ФиксСторно Тогда
				
				// попробуем привести результат распределения к сумме строки
				ЗарплатаКадры.РазнестиСуммуПоБазе(СтрокаТЗ.Результат, КоллекцияРаспределения, "Результат");
				Если РезультатРаспределенияСодержитОшибки(СтрокаТЗ.Результат, КоллекцияРаспределения, ПараметрыДляПроверки.ПроверяемыеПоля) Тогда
					Продолжить;	
				КонецЕсли;
				
			Иначе
				Продолжить;	
			КонецЕсли;
			
		КонецЕсли;
		
		Для каждого ЭлементКоллекции Из КоллекцияРаспределения Цикл
			
			НоваяСтрока = ТаблицаРаспределение.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ЭлементКоллекции);
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ТаблицаРаспределение;
	
КонецФункции

Процедура УпорядочитьТаблицуРаспределениеУдержаний(ТаблицаУдержаний)
	
	Если ТаблицаУдержаний.Количество() > 0 Тогда
		
		КолонкиСортировки = 
		"ИдентификаторСтроки,
		|ВидУдержания,
		|Сотрудник,
		|Подразделение,
		|СтатьяФинансирования,
		|СтатьяРасходов";
		
		ТаблицаУдержаний.Сортировать(КолонкиСортировки, Новый СравнениеЗначений);
		
	КонецЕсли;
	
КонецПроцедуры 

Процедура СоздатьВТБухучетНачисленийРанееНачислено(ТаблицаРанееНачислено, ИсходныеДанные)
	
	// Соответствие идентификаторов таблицы Начислений и Бухучета.
	СоответствиеИдентификаторов = Новый Соответствие;
	Для каждого СтрокаТЗ Из ТаблицаРанееНачислено Цикл
		СоответствиеИдентификаторов.Вставить(СтрокаТЗ.ИдентификаторСтрокиНачисления, СтрокаТЗ.ИдентификаторСтроки);
	КонецЦикла;
	
	КоэффициентыРанееНачисленныхСумм = ИсходныеДанные.МенеджерРасчетаЗарплаты.КоэффициентыРанееНачисленныхСумм();
	ОбщегоНазначенияБЗК.ДобавитьИндексКоллекции(КоэффициентыРанееНачисленныхСумм, "ИдентификаторСтроки");
	ОтборКоэффициентов = Новый Структура("ИдентификаторСтроки");
		
	ФильтрБухучета = Новый ТаблицаЗначений;
	ФильтрБухучета.Колонки.Добавить("Регистратор", РегистрыРасчета.Начисления.ОписаниеТиповПоляРегистратор());
	ФильтрБухучета.Колонки.Добавить("ИдентификаторСтроки", Новый ОписаниеТипов("Число"));
	ФильтрБухучета.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	// для корректировки перерасчетов в разовых начислениях
	ФильтрБухучета.Колонки.Добавить("ЗаписьТекущегоРегистратора", Новый ОписаниеТипов("Булево"));
	
	Для Каждого СтрокаНачисления Из ТаблицаРанееНачислено Цикл
		
		ОтборКоэффициентов.ИдентификаторСтроки = СтрокаНачисления.ИдентификаторСтрокиНачисления;
		СтрокиКоэффициентов = КоэффициентыРанееНачисленныхСумм.НайтиСтроки(ОтборКоэффициентов);
		
		Для Каждого СтрокаКоэффициентов Из СтрокиКоэффициентов Цикл
			Если СтрокаКоэффициентов.Регистратор <> ИсходныеДанные.РегистраторТекущегоРасчета Тогда
				СтрокаФильтра = ФильтрБухучета.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаФильтра, СтрокаКоэффициентов);
				// ИдентификаторСтрокиБазы - идентификатор строки ранее начисленного.
				СтрокаФильтра.ИдентификаторСтроки = СтрокаКоэффициентов.ИдентификаторСтрокиБазы;
				СтрокаФильтра.Подразделение = СтрокаНачисления.Подразделение;
				СтрокаФильтра.ЗаписьТекущегоРегистратора = Не ЗначениеЗаполнено(СтрокаКоэффициентов.Регистратор);
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
	ФильтрБухучета.Свернуть("Регистратор, ИдентификаторСтроки, Подразделение, ЗаписьТекущегоРегистратора");
	
	ИдентификаторыСтрок = ТаблицаРанееНачислено.ВыгрузитьКолонку("ИдентификаторСтроки");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ИсходныеДанные.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ФильтрБухУчета", ФильтрБухУчета); 
	Запрос.УстановитьПараметр("ИдентификаторыСтрок", ИдентификаторыСтрок);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ФильтрБухУчета.Регистратор КАК Регистратор,
	|	ФильтрБухУчета.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ФильтрБухУчета.Подразделение КАК Подразделение,
	|	ФильтрБухУчета.ЗаписьТекущегоРегистратора КАК ЗаписьТекущегоРегистратора
	|ПОМЕСТИТЬ ВТФильтрБухУчета
	|ИЗ
	|	&ФильтрБухУчета КАК ФильтрБухУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ФильтрБухУчета.Регистратор КАК Регистратор,
	|	ФильтрБухУчета.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ВЫБОР
	|		КОГДА БухучетНачислений.ПодразделениеУчетаЗатрат = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
	|			ТОГДА ФильтрБухУчета.Подразделение
	|		ИНАЧЕ БухучетНачислений.ПодразделениеУчетаЗатрат
	|	КОНЕЦ КАК ПодразделениеУчетаЗатрат,
	|	БухучетНачислений.Территория КАК Территория,
	|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
	|	БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	БухучетНачислений.Результат КАК Результат
	|ИЗ
	|	ВТФильтрБухУчета КАК ФильтрБухУчета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеРезультатовНачислений КАК БухучетНачислений
	|		ПО ФильтрБухУчета.Регистратор = БухучетНачислений.Регистратор
	|			И ФильтрБухУчета.ИдентификаторСтроки = БухучетНачислений.ИдентификаторСтроки
	|			И (НЕ ФильтрБухУчета.ЗаписьТекущегоРегистратора)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ФильтрБухУчета.Регистратор,
	|	ФильтрБухУчета.ИдентификаторСтроки,
	|	ВЫБОР
	|		КОГДА БухучетНачислений.ПодразделениеУчетаЗатрат = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
	|			ТОГДА ФильтрБухУчета.Подразделение
	|		ИНАЧЕ БухучетНачислений.ПодразделениеУчетаЗатрат
	|	КОНЕЦ,
	|	Начисления.ТерриторияВыполненияРаботВОрганизации,
	|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачислений.СтатьяФинансирования,
	|	БухучетНачислений.СтатьяРасходов,
	|	БухучетНачислений.ОблагаетсяЕНВД,
	|	БухучетНачислений.Сумма
	|ИЗ
	|	ВТФильтрБухУчета КАК ФильтрБухУчета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТБухучетНачисленийВыходнаяТаблица КАК БухучетНачислений
	|		ПО ФильтрБухУчета.ИдентификаторСтроки = БухучетНачислений.ИдентификаторСтроки
	|			И (ФильтрБухУчета.ЗаписьТекущегоРегистратора)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачисленияДляОтраженияВБухучете КАК Начисления
	|		ПО (БухучетНачислений.ИдентификаторСтроки = Начисления.ИдентификаторСтроки)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ФильтрБухУчета.Регистратор,
	|	ФильтрБухУчета.ИдентификаторСтроки,
	|	ВЫБОР
	|		КОГДА БухучетНачислений.ПодразделениеУчетаЗатрат = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
	|			ТОГДА ФильтрБухУчета.Подразделение
	|		ИНАЧЕ БухучетНачислений.ПодразделениеУчетаЗатрат
	|	КОНЕЦ,
	|	Начисления.ТерриторияВыполненияРаботВОрганизации,
	|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачислений.СтатьяФинансирования,
	|	БухучетНачислений.СтатьяРасходов,
	|	БухучетНачислений.ОблагаетсяЕНВД,
	|	БухучетНачислений.Сумма
	|ИЗ
	|	ВТФильтрБухУчета КАК ФильтрБухУчета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТБухучетНачисленийСторно КАК БухучетНачислений
	|		ПО ФильтрБухУчета.ИдентификаторСтроки = БухучетНачислений.ИдентификаторСтроки
	|			И (ФильтрБухУчета.ЗаписьТекущегоРегистратора)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачисленияДляОтраженияВБухучете КАК Начисления
	|		ПО (БухучетНачислений.ИдентификаторСтроки = Начисления.ИдентификаторСтроки)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БухучетНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	БухучетНачислений.Сотрудник КАК Сотрудник,
	|	БухучетНачислений.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Начисления.Подразделение КАК Подразделение,
	|	БухучетНачислений.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
	|	БухучетНачислений.Сумма КАК Результат,
	|	БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	БухучетНачислений.ДатаНачала КАК ДатаНачала,
	|	БухучетНачислений.Начисление КАК Начисление,
	|	Начисления.ТерриторияВыполненияРаботВОрганизации КАК Территория
	|ИЗ
	|	ВТБухучетНачисленийВыходнаяТаблица КАК БухучетНачислений
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачисленияДляОтраженияВБухучете КАК Начисления
	|		ПО БухучетНачислений.ИдентификаторСтроки = Начисления.ИдентификаторСтроки
	|ГДЕ
	|	БухучетНачислений.ИдентификаторСтроки В(&ИдентификаторыСтрок)";
	
	Результат = Запрос.ВыполнитьПакет();
	КоличествоРезультатов = Результат.ВГраница(); 
	
	БухучетТекущегоРасчета = Результат[КоличествоРезультатов].Выгрузить();
	БухучетТекущегоРасчета.Индексы.Добавить("ИдентификаторСтроки");
	ОтборТекущегоРасчета = Новый Структура("ИдентификаторСтроки");
	
	БухучетБазовыхНачислений = Результат[КоличествоРезультатов-1].Выгрузить();
	БухучетБазовыхНачислений.Индексы.Добавить("Регистратор, ИдентификаторСтроки");
	ОтборРаспределения = Новый Структура("Регистратор, ИдентификаторСтроки");
	
	БухучетНачислений = БухучетТекущегоРасчета.СкопироватьКолонки();
	
	ИспользоватьОбособленныеТерритории = ПолучитьФункциональнуюОпцию("ИспользоватьОбособленныеТерритории", Новый Структура("Организация", ИсходныеДанные.Организация));
	
	Для Каждого СтрокаНачисления Из ТаблицаРанееНачислено Цикл
		
		ИзмеренияРаспределения = Новый Массив;
		Коэффициенты = Новый Массив;
		
		ОтборКоэффициентов.ИдентификаторСтроки = СтрокаНачисления.ИдентификаторСтрокиНачисления;
		СтрокиКоэффициентов = КоэффициентыРанееНачисленныхСумм.НайтиСтроки(ОтборКоэффициентов);
		
		Для Каждого СтрокаКоэффициентов Из СтрокиКоэффициентов Цикл
			
			Если СтрокаКоэффициентов.Регистратор = ИсходныеДанные.РегистраторТекущегоРасчета Тогда
				ОтборТекущегоРасчета.ИдентификаторСтроки = СоответствиеИдентификаторов[СтрокаКоэффициентов.ИдентификаторСтрокиБазы];
				НайденныеСтроки = БухучетТекущегоРасчета.НайтиСтроки(ОтборТекущегоРасчета);
			Иначе	
				ЗаполнитьЗначенияСвойств(ОтборРаспределения, СтрокаКоэффициентов);
				ОтборРаспределения.ИдентификаторСтроки = СтрокаКоэффициентов.ИдентификаторСтрокиБазы;
				НайденныеСтроки = БухучетБазовыхНачислений.НайтиСтроки(ОтборРаспределения);
			КонецЕсли;
			
			СтрокиРаспределения = Новый Массив;
			Если ИспользоватьОбособленныеТерритории Тогда
				// Отберем строки с учетом Территории.
				Для каждого СтрокаРаспределения Из НайденныеСтроки Цикл
					Если СтрокаНачисления.Территория = СтрокаРаспределения.Территория
						Или Не ЗначениеЗаполнено(СтрокаНачисления.Территория) И Не ЗначениеЗаполнено(СтрокаРаспределения.Территория) Тогда
						СтрокиРаспределения.Добавить(СтрокаРаспределения);
					КонецЕсли;
				КонецЦикла;
				
				Если СтрокиРаспределения.Количество() = 0 Тогда
					// Не удалось подобрать прошлое распределение с учетом территории,
					// распределим по всей базе.
					СтрокиРаспределения = НайденныеСтроки;
				КонецЕсли;
				
			Иначе
				СтрокиРаспределения = НайденныеСтроки;
			КонецЕсли;
			
			Для Каждого СтрокаРаспределения Из СтрокиРаспределения Цикл
				Если СтрокаРаспределения.Результат <> 0 Тогда
					ИзмеренияРаспределения.Добавить(СтрокаРаспределения);
					Коэффициенты.Добавить(СтрокаРаспределения.Результат * СтрокаКоэффициентов.Коэффициент);
				КонецЕсли;	
			КонецЦикла;
			
		КонецЦикла;
		
		// Если базы распределения не нашлось, то распределим сумму РанееНачислено по текущему распределению этого начисления
		Если ИзмеренияРаспределения.Количество() = 0 Тогда
			ОтборТекущегоРасчета.ИдентификаторСтроки = СтрокаНачисления.ИдентификаторСтроки;	
			СтрокиРаспределения = БухучетТекущегоРасчета.НайтиСтроки(ОтборТекущегоРасчета);
			Для Каждого СтрокаРаспределения Из СтрокиРаспределения Цикл
				ИзмеренияРаспределения.Добавить(СтрокаРаспределения);
				Коэффициенты.Добавить(СтрокаРаспределения.Результат);
			КонецЦикла;	
		КонецЕсли;	
		
		Распределение = ЗарплатаКадрыКлиентСервер.РаспределитьПропорциональноКоэффициентам(СтрокаНачисления.РанееНачислено, Коэффициенты); 
		Если Распределение <> Неопределено Тогда
			Для Индекс = 0 По Распределение.Количество() - 1 Цикл
				СтрокаБухучет = БухучетНачислений.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаБухучет, СтрокаНачисления);
				ЗаполнитьЗначенияСвойств(СтрокаБухучет, ИзмеренияРаспределения[Индекс]);
				СтрокаБухучет.ИдентификаторСтроки = СтрокаНачисления.ИдентификаторСтроки;
				СтрокаБухучет.Результат = - Распределение[Индекс]
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
	Запрос.УстановитьПараметр("БухучетНачисленийРанееНачислено", БухучетНачислений);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	БухучетНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	БухучетНачислений.Сотрудник КАК Сотрудник,
	|	БухучетНачислений.ФизическоеЛицо КАК ФизическоеЛицо,
	|	БухучетНачислений.Подразделение КАК Подразделение,
	|	БухучетНачислений.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
	|	БухучетНачислений.Результат КАК Сумма,
	|	БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	БухучетНачислений.ДатаНачала КАК ДатаНачала,
	|	БухучетНачислений.Начисление КАК Начисление,
	|	БухучетНачислений.Территория КАК Территория
	|ПОМЕСТИТЬ ВТБухучетНачисленийРанееНачислено
	|ИЗ
	|	&БухучетНачисленийРанееНачислено КАК БухучетНачислений";
	Запрос.Выполнить();
	
	УдалитьВТ = Новый Массив;
	УдалитьВТ.Добавить("ВТФильтрБухУчета");
	ЗарплатаКадры.УничтожитьВТ(ИсходныеДанные.МенеджерВременныхТаблиц, УдалитьВТ);
	
КонецПроцедуры

Процедура СоздатьВТБухучетКорректировкаРанееВыполненногоНачисления(ИсходныеДанные)
	
	УдалитьВТ = Новый Массив;
	БухучетНачислений = НоваяТаблицаБухучетНачислений();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ИсходныеДанные.МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НачисленияДляРаспределения.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	НачисленияДляРаспределения.ИдентификаторСтрокиНачисления КАК ИдентификаторСтрокиНачисления,
	|	НачисленияДляРаспределения.Сотрудник КАК Сотрудник,
	|	НачисленияДляРаспределения.ФизическоеЛицо КАК ФизическоеЛицо,
	|	НачисленияДляРаспределения.Подразделение КАК Подразделение,
	|	НачисленияДляРаспределения.Начисление КАК Начисление,
	|	НачисленияДляРаспределения.ДатаНачала КАК ДатаНачала,
	|	НачисленияДляРаспределения.ТерриторияВыполненияРаботВОрганизации КАК Территория,
	|	НачисленияДляРаспределения.Сумма КАК Сумма
	|ИЗ
	|	ВТНачисленияДляОтраженияВБухучете КАК НачисленияДляРаспределения
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБухучетНачисленийСторно КАК БухучетНачисленийСторно
	|		ПО НачисленияДляРаспределения.ИдентификаторСтроки = БухучетНачисленийСторно.ИдентификаторСтроки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления КАК Начисления
	|		ПО НачисленияДляРаспределения.Начисление = Начисления.Ссылка
	|			И (Начисления.СтратегияОтраженияВУчете <> ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазовымРасчетам))
	|			И (НачисленияДляРаспределения.КорректировкаРанееВыполненногоНачисления)
	|ГДЕ
	|	БухучетНачисленийСторно.ИдентификаторСтроки ЕСТЬ NULL";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляОтраженияВБухучете", ИсходныеДанные.ИмяВТНачисления);
	ТаблицаРанееНачислено = Запрос.Выполнить().Выгрузить();
	
	Если ТаблицаРанееНачислено.Количество() > 0 Тогда
		
		// ИдентификаторСтроки соответствует полю ИдентификаторСтрокиНачисления
		КоэффициентыРанееНачисленныхСумм = ИсходныеДанные.МенеджерРасчетаЗарплаты.КоэффициентыРанееНачисленныхСумм();
		ОбщегоНазначенияБЗК.ДобавитьИндексКоллекции(КоэффициентыРанееНачисленныхСумм, "ИдентификаторСтроки");
		ОтборКоэффициентов = Новый Структура("ИдентификаторСтроки");
		
		ФильтрБухучета = Новый ТаблицаЗначений;
		ФильтрБухучета.Колонки.Добавить("Регистратор", РегистрыРасчета.Начисления.ОписаниеТиповПоляРегистратор());
		ФильтрБухучета.Колонки.Добавить("ИдентификаторСтроки", Новый ОписаниеТипов("Число"));
		ФильтрБухучета.Колонки.Добавить("ИдентификаторСтрокиБухучета", Новый ОписаниеТипов("Число"));
		
		Для Каждого СтрокаНачисления Из ТаблицаРанееНачислено Цикл
			
			ОтборКоэффициентов.ИдентификаторСтроки = СтрокаНачисления.ИдентификаторСтрокиНачисления;
			СтрокиКоэффициентов = КоэффициентыРанееНачисленныхСумм.НайтиСтроки(ОтборКоэффициентов);
			
			Для Каждого СтрокаКоэффициентов Из СтрокиКоэффициентов Цикл
				Если СтрокаКоэффициентов.Регистратор <> ИсходныеДанные.РегистраторТекущегоРасчета Тогда
					СтрокаФильтра = ФильтрБухучета.Добавить();
					СтрокаФильтра.Регистратор = СтрокаКоэффициентов.Регистратор;
					СтрокаФильтра.ИдентификаторСтроки = СтрокаКоэффициентов.ИдентификаторСтрокиБазы;
					СтрокаФильтра.ИдентификаторСтрокиБухучета = СтрокаНачисления.ИдентификаторСтроки;
				КонецЕсли;
			КонецЦикла;
			
		КонецЦикла;
		
		ФильтрБухучета.Свернуть("Регистратор, ИдентификаторСтроки, ИдентификаторСтрокиБухучета");
		
		Регистраторы = ОбщегоНазначения.ВыгрузитьКолонку(ФильтрБухучета, "Регистратор", Истина);
		Запрос.УстановитьПараметр("Регистраторы", Регистраторы);
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	БухучетНачисления.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрНакопления.БухучетНачисленияУдержанияПоСотрудникам КАК БухучетНачисления
		|ГДЕ
		|	БухучетНачисления.Регистратор В(&Регистраторы)
		|	И БухучетНачисления.ДанныеМежрасчетногоПериода";
		ИсключаемыеРегистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
		
		УдаляемыеСтроки = Новый Массив;
		Отбор = Новый Структура("Регистратор");
		Для каждого Регистратор Из ИсключаемыеРегистраторы Цикл
			Отбор.Регистратор = Регистратор;
			НайденныеСтроки = ФильтрБухучета.НайтиСтроки(Отбор);
			Для каждого СтрокаТЗ Из НайденныеСтроки Цикл
				УдаляемыеСтроки.Добавить(СтрокаТЗ);
			КонецЦикла;
		КонецЦикла;
		
		Для каждого СтрокаТЗ Из УдаляемыеСтроки Цикл
			ФильтрБухучета.Удалить(СтрокаТЗ);
		КонецЦикла;
		
		Если ФильтрБухучета.Количество() > 0 Тогда
			
			ИспользоватьОбособленныеТерритории = ПолучитьФункциональнуюОпцию("ИспользоватьОбособленныеТерритории", Новый Структура("Организация", ИсходныеДанные.Организация));
			
			Запрос.УстановитьПараметр("ФильтрБухУчета", ФильтрБухУчета);
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ФильтрБухУчета.Регистратор КАК Регистратор,
			|	ФильтрБухУчета.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	ФильтрБухУчета.ИдентификаторСтрокиБухучета КАК ИдентификаторСтрокиБухучета
			|ПОМЕСТИТЬ ВТФильтрБухУчета
			|ИЗ
			|	&ФильтрБухУчета КАК ФильтрБухУчета
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ФильтрБухУчета.ИдентификаторСтрокиБухучета КАК ИдентификаторСтрокиБухучета,
			|	БухучетНачислений.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
			|	БухучетНачислений.Территория КАК Территория,
			|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
			|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
			|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
			|	БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
			|	БухучетНачислений.Результат КАК Результат
			|ИЗ
			|	ВТФильтрБухУчета КАК ФильтрБухУчета
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеРезультатовНачислений КАК БухучетНачислений
			|		ПО ФильтрБухУчета.Регистратор = БухучетНачислений.Регистратор
			|			И ФильтрБухУчета.ИдентификаторСтроки = БухучетНачислений.ИдентификаторСтроки";
			
			БухучетРанееНачислено = Запрос.Выполнить().Выгрузить();
			УдалитьВТ.Добавить("ВТФильтрБухУчета");
			
			БухучетРанееНачислено.Индексы.Добавить("ИдентификаторСтрокиБухучета");
			ОтборРанееНачислено = Новый Структура("ИдентификаторСтрокиБухучета");
			
			Для каждого СтрокаНачисления Из ТаблицаРанееНачислено Цикл
			
				ОтборРанееНачислено.ИдентификаторСтрокиБухучета = СтрокаНачисления.ИдентификаторСтроки;
				НайденныеСтроки = БухучетРанееНачислено.НайтиСтроки(ОтборРанееНачислено);
				
				СтрокиРаспределения = Новый Массив;
				Если ИспользоватьОбособленныеТерритории Тогда
					// Отберем строки с учетом Территории.
					Для каждого СтрокаРаспределения Из НайденныеСтроки Цикл
						Если СтрокаНачисления.Территория = СтрокаРаспределения.Территория
							Или Не ЗначениеЗаполнено(СтрокаНачисления.Территория) И Не ЗначениеЗаполнено(СтрокаРаспределения.Территория) Тогда
							СтрокиРаспределения.Добавить(СтрокаРаспределения);
						КонецЕсли;
					КонецЦикла;
					
					Если СтрокиРаспределения.Количество() = 0 Тогда
						// Не удалось подобрать прошлое распределение с учетом территории,
						// распределим по всей базе.
						СтрокиРаспределения = НайденныеСтроки;
					КонецЕсли;
					
				Иначе
					СтрокиРаспределения = НайденныеСтроки;
				КонецЕсли;
				
				Коэффициенты = ОбщегоНазначения.ВыгрузитьКолонку(СтрокиРаспределения,"Результат");
				Результаты = ЗарплатаКадрыКлиентСервер.РаспределитьПропорциональноКоэффициентам(СтрокаНачисления.Сумма, Коэффициенты);
				
				Если Результаты <> Неопределено Тогда
					
					Индекс = 0;
					Для Каждого СтрокаРаспределения Из СтрокиРаспределения Цикл
						
						СтрокаТаблицы = БухучетНачислений.Добавить();
						ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтрокаНачисления);
						ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтрокаРаспределения);
						СтрокаТаблицы.Территория = СтрокаНачисления.Территория;
						СтрокаТаблицы.Сумма = Результаты[Индекс];
						
						Индекс = Индекс + 1;
						
					КонецЦикла;
					
				КонецЕсли;
			
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если БухучетНачислений.Количество() = 0 Тогда
		СоздатьПустуюВТБухучетНачислений(Запрос, "ВТБухучетКорректировкаРанееВыполненногоНачисления");
	Иначе
		
		Запрос.УстановитьПараметр("БухучетНачислений", БухучетНачислений);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	БухучетНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	БухучетНачислений.Сотрудник КАК Сотрудник,
		|	БухучетНачислений.ФизическоеЛицо КАК ФизическоеЛицо,
		|	БухучетНачислений.Подразделение КАК Подразделение,
		|	БухучетНачислений.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
		|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
		|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
		|	БухучетНачислений.Сумма КАК Сумма,
		|	БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	БухучетНачислений.ДатаНачала КАК ДатаНачала,
		|	БухучетНачислений.Начисление КАК Начисление,
		|	БухучетНачислений.Территория КАК Территория
		|ПОМЕСТИТЬ ВТБухучетКорректировкаРанееВыполненногоНачисления
		|ИЗ
		|	&БухучетНачислений КАК БухучетНачислений";
		Запрос.Выполнить();
		
	КонецЕсли;
	
	Если УдалитьВТ.Количество() > 0 Тогда
		ЗарплатаКадры.УничтожитьВТ(ИсходныеДанные.МенеджерВременныхТаблиц, УдалитьВТ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкаРаспределенияПоИсточникамФинансирования

Функция ПроверитьРезультатыРаспределенияНачисленийУдержанийВСтрокеОбъекта(ПроверяемаяСтрока, СтрокиРаспределенияРезультатовРасчета, ИмяТаблицы, НачислениеУдержаниеВидОперации, РаботаВБюджетномУчреждении, ВидНачисленияВШапке, БухучетПрочихДоходов)
	
	РезультатПроверки = СтруктураРезультатаПроверки();
	
	ИмяТаблицыРаспределениеРезультатовРасчета = ИмяТаблицыДляРаспределенияРезультата(ИмяТаблицы);
	ЭтоТаблицаНачислений = ИмяТаблицыРаспределениеРезультатовРасчета = "РаспределениеРезультатовНачислений";
	
	Если Не ЗначениеЗаполнено(ВидНачисленияВШапке) И ЭтоТаблицаНачислений И ИмяТаблицы <> "НачисленияПоДоговорам" Тогда
		ВидРасчета = ПроверяемаяСтрока.Начисление;
	ИначеЕсли ЗначениеЗаполнено(ВидНачисленияВШапке) Тогда
		ВидРасчета = ВидНачисленияВШапке;
	Иначе
		ВидРасчета = Неопределено;
	КонецЕсли;
	
	ВидыОперацийПособия = Новый Массив;
	ВидыОперацийПособия.Добавить(Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФСС);
	ВидыОперацийПособия.Добавить(Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФССНС);
	
	ПроверяемыеПоля = Новый Структура;
	ПроверяемыеПоля.Вставить("СтатьяФинансирования", Истина);
	ПроверяемыеПоля.Вставить("СтатьяРасходов", РаботаВБюджетномУчреждении);
	
	ЭтоТаблицаНДФЛ						= ИмяТаблицы = "НДФЛ";
	ЭтоТаблицаСуммыВозврата				= ИмяТаблицы = "СуммыВозврата";
	ЭтоТаблицаКорректировкиВыплаты		= ИмяТаблицы = "КорректировкиВыплаты";
	ЭтоТаблицаЗаймов					= ИмяТаблицы = "ПогашениеЗаймов";
	ЭтоТаблицаПрочихДоходов				= ИмяТаблицы = "НачисленияУдержанияВзносы";
	УточнятьПроверкуСпособаОтражения	= ЭтоТаблицаНачислений И ИмяТаблицы <> "НачисленияПоДоговорам" И ВидРасчета <> Неопределено;
	
	Если ЭтоТаблицаНачислений Тогда
		ПроверяемыеПоля.Вставить("СпособОтраженияЗарплатыВБухучете", Истина);
		ПроверяемыеПоля.Вставить("Сотрудник", Ложь);
	ИначеЕсли ЭтоТаблицаПрочихДоходов Тогда
		ПроверяемыеПоля.Вставить("СпособОтраженияЗарплатыВБухучете", Истина);
	Иначе
		// Для удержаний не проверяем способ отражения.
		ПроверяемыеПоля.Вставить("СпособОтраженияЗарплатыВБухучете", Ложь);
		Если БухучетПрочихДоходов Тогда
			ПроверяемыеПоля.Вставить("Сотрудник", Ложь);
		Иначе
			ПроверяемыеПоля.Вставить("Сотрудник", Истина);
		КонецЕсли;
	КонецЕсли;
	
	Если СтрокиРаспределенияРезультатовРасчета <> Неопределено
		И СтрокиРаспределенияРезультатовРасчета.Количество() > 0 Тогда
		
		СтрокаРаспределения = СтрокиРаспределенияРезультатовРасчета[0];
		Для Каждого ПроверяемоеПоле Из ПроверяемыеПоля Цикл
			ИмяПоля = ПроверяемоеПоле.Ключ;
			Если Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаРаспределения, ИмяПоля) Тогда
				ПроверяемыеПоля[ИмяПоля] = Ложь;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Если УточнятьПроверкуСпособаОтражения Тогда
		ПроверяемыеПоля.СпособОтраженияЗарплатыВБухучете = ВидыОперацийПособия.Найти(НачислениеУдержаниеВидОперации[ВидРасчета]) = Неопределено;
	КонецЕсли;
	
	Если ЭтоТаблицаНДФЛ Тогда
		Результат  = УчетНДФЛ.ИтоговаяСуммаНДФЛпоСтроке(ПроверяемаяСтрока, "Налог") - УчетНДФЛ.ИтоговаяСуммаНДФЛпоСтроке(ПроверяемаяСтрока, "ЗачтеноАвансовыхПлатежей");
	ИначеЕсли ЭтоТаблицаСуммыВозврата Тогда
		Результат  = УчетНДФЛ.ИтоговаяСуммаНДФЛпоСтроке(ПроверяемаяСтрока, "Налог");
	ИначеЕсли ЭтоТаблицаКорректировкиВыплаты Тогда
		Результат  = ПроверяемаяСтрока.КорректировкаВыплаты;
	ИначеЕсли ЭтоТаблицаЗаймов Тогда
		Результат  = ПроверяемаяСтрока.НачисленоПроцентов + ПроверяемаяСтрока.ПогашениеПроцентов + ПроверяемаяСтрока.ПогашениеЗайма + ПроверяемаяСтрока.МатериальнаяВыгода + ПроверяемаяСтрока.НалогНаМатериальнуюВыгоду;
	ИначеЕсли ЭтоТаблицаПрочихДоходов Тогда
		Результат  = ПроверяемаяСтрока.Начислено;
	Иначе
		Результат  = ПроверяемаяСтрока.Результат;
	КонецЕсли;
	
	РезультатРаспределения = 0;
	ЕстьОшибкиЗаполнения = Ложь;
	Если СтрокиРаспределенияРезультатовРасчета <> Неопределено
		И СтрокиРаспределенияРезультатовРасчета.Количество() > 0 Тогда
		
		Для каждого СтрокаРаспределения Из СтрокиРаспределенияРезультатовРасчета Цикл
			РезультатРаспределения = РезультатРаспределения + СтрокаРаспределения.Результат;
			Для Каждого КлючИЗначение Из ПроверяемыеПоля Цикл
				Если КлючИЗначение.Значение И Не ЗначениеЗаполнено(СтрокаРаспределения[КлючИЗначение.Ключ]) Тогда
					ЕстьОшибкиЗаполнения = Истина;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
	КонецЕсли;
	
	Если Результат = 0 И РезультатРаспределения = 0 Тогда
		ЕстьОшибкиРаспределения = Ложь;
		ЕстьОшибкиЗаполнения = Ложь;
	Иначе
		ЕстьОшибкиРаспределения = Результат <> РезультатРаспределения;
	КонецЕсли;
	
	РезультатПроверки.ЕстьОшибкиРаспределения = ЕстьОшибкиРаспределения;
	РезультатПроверки.ЕстьОшибкиЗаполнения    = ЕстьОшибкиЗаполнения;
	РезультатПроверки.СуммаКРаспределению     = Результат;
	РезультатПроверки.РезультатРаспределения  = РезультатРаспределения;
	
	Возврат РезультатПроверки;
	
КонецФункции

Функция ИмяТаблицыДляРаспределенияРезультата(ИмяТаблицы)
	
	ИмяТаблицыДляРаспределения = "";
	
	Если ИмяТаблицы = "Начисления"
		Или ИмяТаблицы = "НачисленияПерерасчет"
		Или ИмяТаблицы = "НачисленияПоДоговорам"
		Или ИмяТаблицы = "Пособия"
		Или ИмяТаблицы = "ПособияПерерасчет"
		Или ИмяТаблицы = "ДоначисленияИПерерасчеты"
		Или ИмяТаблицы = "ОплатаТруда"
		Или ИмяТаблицы = "Сторнировано"
		Или ИмяТаблицы = "НачисленияУдержанияВзносы" Тогда
		
		ИмяТаблицыДляРаспределения = "РаспределениеРезультатовНачислений";
		
	ИначеЕсли ИмяТаблицы = "НДФЛ"
		Или ИмяТаблицы = "СуммыВозврата"
		Или ИмяТаблицы = "Удержания"
		Или ИмяТаблицы = "ПогашениеЗаймов"
		Или ИмяТаблицы = "КорректировкиВыплаты" Тогда
		
		ИмяТаблицыДляРаспределения = "РаспределениеРезультатовУдержаний";
		
	КонецЕсли;
	
	Если ПустаяСтрока(ИмяТаблицыДляРаспределения) Тогда
		
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ЛьготыСотрудников") Тогда
			МодульЛьготыСотрудниковКлиентСервер = ОбщегоНазначения.ОбщийМодуль("ЛьготыСотрудниковКлиентСервер");
			ИмяТаблицыДляРаспределения = МодульЛьготыСотрудниковКлиентСервер.ИмяТаблицыДляРаспределенияРезультата(ИмяТаблицы);
		КонецЕсли;
	
	КонецЕсли;
	
	Возврат ИмяТаблицыДляРаспределения;
	
КонецФункции

Функция СтруктураРезультатаПроверки()
	
	РезультатПроверки = Новый Структура;
	РезультатПроверки.Вставить("ЕстьОшибкиРаспределения", Истина);
	РезультатПроверки.Вставить("ЕстьОшибкиЗаполнения", Истина);
	РезультатПроверки.Вставить("СуммаКРаспределению", 0);
	РезультатПроверки.Вставить("РезультатРаспределения", Неопределено);
	
	Возврат РезультатПроверки;
	
КонецФункции

Функция ИмяРеквизитаИдентификаторСтрокиПоИмениТаблицы(ИмяТаблицы)
	
	ИмяРеквизитаИдентификаторСтроки = "";
	
	Если ИмяТаблицы = "НДФЛ" Или ИмяТаблицы = "СуммыВозврата" Тогда
		ИмяРеквизитаИдентификаторСтроки = "ИдентификаторСтрокиНДФЛ";
	ИначеЕсли ИмяТаблицы = "ПогашениеЗаймов" Или ИмяТаблицы = "КорректировкиВыплаты" Тогда
		ИмяРеквизитаИдентификаторСтроки = "ИдентификаторСтроки";
	ИначеЕсли ИмяТаблицы = "НачисленияПоДоговорам" Или ИмяТаблицы = "НачисленияУдержанияВзносы" Тогда
		ИмяРеквизитаИдентификаторСтроки = "ИдентификаторСтроки";
	Иначе
		ИмяРеквизитаИдентификаторСтроки = "ИдентификаторСтрокиВидаРасчета";
	КонецЕсли;
	
	Возврат ИмяРеквизитаИдентификаторСтроки;
	
КонецФункции

#КонецОбласти

#Область РедактированиеПроцентаЕНВДВФормахДокументов

Процедура ОбъектПриЧтенииНаСервереПроцентЕНВД(Форма, Организация, МесяцНачисления, ДобавлятьЭлементыФормы = Истина, ДобавлятьРеквизитыФормы = Истина, ОтложенноеИзменение = Ложь) Экспорт

	Если (НЕ ПолучитьФункциональнуюОпцию("ИспользоватьРасчетЗарплатыРасширенная")) 
		ИЛИ ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	ОтражениеЗарплатыВБухучете.ОбъектПриЧтенииНаСервереПроцентЕНВД(Форма, Организация, МесяцНачисления, ДобавлятьЭлементыФормы, ДобавлятьРеквизитыФормы);
	УстановитьПривилегированныйРежим(Ложь);

КонецПроцедуры

Процедура ОбъектПриСозданииНаСервереПроцентЕНВД(Форма, Организация, МесяцНачисления) Экспорт
	
	Если (НЕ ПолучитьФункциональнуюОпцию("ИспользоватьРасчетЗарплатыРасширенная")) 
		ИЛИ ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		Возврат;
	КонецЕсли;
	ОтражениеЗарплатыВБухучете.ОбъектПриСозданииНаСервереПроцентЕНВД(Форма, Организация, МесяцНачисления);
	
КонецПроцедуры

Процедура ОбработатьИзменениеОрганизацииПроцентЕНВД(Форма, Организация, МесяцНачисления) Экспорт
	
	Если (НЕ ПолучитьФункциональнуюОпцию("ИспользоватьРасчетЗарплатыРасширенная")) 
		ИЛИ ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		Возврат;
	КонецЕсли;
	ОтражениеЗарплатыВБухучете.ОбработатьИзменениеОрганизацииПроцентЕНВД(Форма, Организация, МесяцНачисления)
	
КонецПроцедуры

Процедура ОбработатьИзменениеМесяцНачисленияПроцентЕНВД(Форма, Организация, МесяцНачисления) Экспорт
	
	Если (НЕ ПолучитьФункциональнуюОпцию("ИспользоватьРасчетЗарплатыРасширенная")) 
		ИЛИ ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		Возврат;
	КонецЕсли;
	ОтражениеЗарплатыВБухучете.ОбработатьИзменениеМесяцНачисленияПроцентЕНВД(Форма, Организация, МесяцНачисления)
	
КонецПроцедуры

Процедура ЗарегистрироватьПроцентЕНВДПослеРедактированияВФорме(Организация, Месяц, ПроцентЕНВД, ПроцентЕНВДСтрока) Экспорт

	Если (НЕ ПолучитьФункциональнуюОпцию("ИспользоватьРасчетЗарплатыРасширенная")) 
		ИЛИ ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		Возврат;
	КонецЕсли;
	ОтражениеЗарплатыВБухучете.ЗарегистрироватьПроцентЕНВДПослеРедактированияВФорме(Организация, Месяц, ПроцентЕНВД, ПроцентЕНВДСтрока)
	
КонецПроцедуры

#КонецОбласти

#Область ПредставлениеРезультатовРаспределения

Процедура ОбъектПриЧтенииНаСервереПредставлениеРаспределения(Форма, ОписаниеДокумента) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
		Возврат;
	КонецЕсли;
	
	Объект = Форма.Объект;
	
	РаботаВБюджетномУчреждении = ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении");
	НачислениеУдержаниеВидОперации = ОтражениеЗарплатыВУчете.НачислениеУдержаниеВидОперации();
	
	НачислениеИзШапкиДокумента = Неопределено;
	Если ОписаниеДокумента.ВидНачисленияВШапке И ЗначениеЗаполнено(ОписаниеДокумента.ВидНачисленияИмя) Тогда
		НачислениеИзШапкиДокумента = Объект[ОписаниеДокумента.ИменаПолейНачисления];
	КонецЕсли;
	
	Если ОписаниеДокумента.НачисленияИмя <> Неопределено Тогда
		ЗаполнитьПредставлениеРаспределения(Объект[ОписаниеДокумента.НачисленияИмя], ОписаниеДокумента, ОписаниеДокумента.НачисленияИмя, НачислениеИзШапкиДокумента, РаботаВБюджетномУчреждении, НачислениеУдержаниеВидОперации);
	КонецЕсли;
	Если ОписаниеДокумента.НачисленияПерерасчетИмя <> Неопределено Тогда
		ЗаполнитьПредставлениеРаспределения(Объект[ОписаниеДокумента.НачисленияПерерасчетИмя], ОписаниеДокумента, ОписаниеДокумента.НачисленияПерерасчетИмя, НачислениеИзШапкиДокумента, РаботаВБюджетномУчреждении, НачислениеУдержаниеВидОперации);
	КонецЕсли;
	Если ОписаниеДокумента.ПособияИмя <> Неопределено Тогда
		ЗаполнитьПредставлениеРаспределения(Объект[ОписаниеДокумента.ПособияИмя], ОписаниеДокумента, ОписаниеДокумента.ПособияИмя, НачислениеИзШапкиДокумента, РаботаВБюджетномУчреждении, НачислениеУдержаниеВидОперации);
	КонецЕсли;
	Если ОписаниеДокумента.НачисленияПоДоговорамИмя <> Неопределено Тогда
		ЗаполнитьПредставлениеРаспределения(Объект[ОписаниеДокумента.НачисленияПоДоговорамИмя], ОписаниеДокумента, ОписаниеДокумента.НачисленияПоДоговорамИмя, НачислениеИзШапкиДокумента, РаботаВБюджетномУчреждении, НачислениеУдержаниеВидОперации);
	КонецЕсли;
	Если ОписаниеДокумента.СторноИмя <> Неопределено Тогда
		ЗаполнитьПредставлениеРаспределения(Объект[ОписаниеДокумента.СторноИмя], ОписаниеДокумента, ОписаниеДокумента.СторноИмя, НачислениеИзШапкиДокумента, РаботаВБюджетномУчреждении, НачислениеУдержаниеВидОперации);
	КонецЕсли;
	Если ОписаниеДокумента.ЛьготыИмя <> Неопределено Тогда
		ЗаполнитьПредставлениеРаспределения(Объект[ОписаниеДокумента.ЛьготыИмя], ОписаниеДокумента, ОписаниеДокумента.ЛьготыИмя, НачислениеИзШапкиДокумента, РаботаВБюджетномУчреждении, НачислениеУдержаниеВидОперации);
	КонецЕсли;
	Если ОписаниеДокумента.ЛьготыПерерасчетИмя <> Неопределено Тогда
		ЗаполнитьПредставлениеРаспределения(Объект[ОписаниеДокумента.ЛьготыПерерасчетИмя], ОписаниеДокумента, ОписаниеДокумента.ЛьготыПерерасчетИмя, НачислениеИзШапкиДокумента, РаботаВБюджетномУчреждении, НачислениеУдержаниеВидОперации);
	КонецЕсли;
	Если ОписаниеДокумента.УдержанияИмя <> Неопределено Тогда
		ЗаполнитьПредставлениеРаспределения(Объект[ОписаниеДокумента.УдержанияИмя], ОписаниеДокумента, ОписаниеДокумента.УдержанияИмя, НачислениеИзШапкиДокумента, РаботаВБюджетномУчреждении, НачислениеУдержаниеВидОперации);
	КонецЕсли;
	Если ОписаниеДокумента.УдержанияПерерасчетИмя <> Неопределено Тогда
		ЗаполнитьПредставлениеРаспределения(Объект[ОписаниеДокумента.УдержанияПерерасчетИмя], ОписаниеДокумента, ОписаниеДокумента.УдержанияПерерасчетИмя, НачислениеИзШапкиДокумента, РаботаВБюджетномУчреждении, НачислениеУдержаниеВидОперации);
	КонецЕсли;
	Если ОписаниеДокумента.НДФЛИмя <> Неопределено Тогда
		ЗаполнитьПредставлениеРаспределения(Объект[ОписаниеДокумента.НДФЛИмя], ОписаниеДокумента, ОписаниеДокумента.НДФЛИмя, НачислениеИзШапкиДокумента, РаботаВБюджетномУчреждении, НачислениеУдержаниеВидОперации);
	КонецЕсли;
	Если ОписаниеДокумента.КорректировкиВыплатыИмя <> Неопределено Тогда
		ЗаполнитьПредставлениеРаспределения(Объект[ОписаниеДокумента.КорректировкиВыплатыИмя], ОписаниеДокумента, ОписаниеДокумента.КорректировкиВыплатыИмя, НачислениеИзШапкиДокумента, РаботаВБюджетномУчреждении, НачислениеУдержаниеВидОперации);
	КонецЕсли;
	Если ОписаниеДокумента.ПогашениеЗаймовИмя <> Неопределено Тогда
		ЗаполнитьПредставлениеРаспределения(Объект[ОписаниеДокумента.ПогашениеЗаймовИмя], ОписаниеДокумента, ОписаниеДокумента.ПогашениеЗаймовИмя, НачислениеИзШапкиДокумента, РаботаВБюджетномУчреждении, НачислениеУдержаниеВидОперации);
	КонецЕсли;
	
	Если ОписаниеДокумента.Свойство("НачисленияДолейРКСНИмя")
		И ОписаниеДокумента.НачисленияДолейРКСНИмя <> Неопределено Тогда
		ЗаполнитьПредставлениеРаспределения(Объект[ОписаниеДокумента.НачисленияДолейРКСНИмя], ОписаниеДокумента, ОписаниеДокумента.НачисленияДолейРКСНИмя, НачислениеИзШапкиДокумента, РаботаВБюджетномУчреждении, НачислениеУдержаниеВидОперации);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПредставлениеРаспределения(ДанныеЗаполнения, ОписаниеДокумента, ИмяТаблицы, ВидРасчетаИзШапки, РаботаВБюджетномУчреждении, НачислениеУдержаниеВидОперации) 
	
	Если ДанныеЗаполнения.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Для Каждого СтрокаТаблицы Из ДанныеЗаполнения Цикл
			
		РезультатПроверкиСтроки = ПроверитьРезультатыРаспределенияНачисленийУдержанийВСтроке(
			СтрокаТаблицы, ИмяТаблицы, ОписаниеДокумента, ВидРасчетаИзШапки, НачислениеУдержаниеВидОперации, РаботаВБюджетномУчреждении);
			
		РаспределениеСодержитОшибки = РезультатПроверкиСтроки.ЕстьОшибкиРаспределения Или РезультатПроверкиСтроки.ЕстьОшибкиЗаполнения;
		
		ОтражениеЗарплатыВБухучетеКлиентСерверРасширенный.ЗаполнитьПредставлениеРаспределенияВСтроке(
			СтрокаТаблицы, РаспределениеСодержитОшибки, ИмяТаблицы, РаботаВБюджетномУчреждении);			
			
	КонецЦикла	

КонецПроцедуры

Функция ПроверитьРезультатыРаспределенияНачисленийУдержанийВСтроке(ПроверяемаяСтрока, ИмяТаблицы, ОписаниеДокумента, ВидРасчетаИзШапки, НачислениеУдержаниеВидОперации, РаботаВБюджетномУчреждении, РезультатыРаспределения = Неопределено)
	
	ОписаниеТаблицыФормы = Неопределено;
	Если Не ОписаниеДокумента.ОписанияТаблицДляРаспределенияРезультата.Свойство(ИмяТаблицы, ОписаниеТаблицыФормы) Тогда
		Возврат СтруктураРезультатаПроверки();
	КонецЕсли;
	
	Если РезультатыРаспределения = Неопределено Тогда 
		РезультатыРаспределения = ПроверяемаяСтрока.РезультатРаспределения;
	КонецЕсли;
	
	БухучетПрочихДоходов = Ложь;
	Если ОписаниеДокумента.Свойство("БухучетПрочихДоходов") Тогда
		БухучетПрочихДоходов = ОписаниеДокумента.БухучетПрочихДоходов;
	КонецЕсли;
	
	Возврат ПроверитьРезультатыРаспределенияНачисленийУдержанийВСтрокеОбъекта(ПроверяемаяСтрока, РезультатыРаспределения, ИмяТаблицы,
		НачислениеУдержаниеВидОперации,РаботаВБюджетномУчреждении, ВидРасчетаИзШапки, БухучетПрочихДоходов);
	
КонецФункции

Процедура ДополнитьТаблицуРаспределенияКодомСтатьиФинансирования(ТаблицаРаспределения) Экспорт
	
	КодыСтатей = КодыСтатейФинансирования();
	
	Если ТаблицаРаспределения.Колонки.Найти("КодСтатьиФинансирования") = Неопределено Тогда
		ТаблицаРаспределения.Колонки.Добавить("КодСтатьиФинансирования", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(7)));
	КонецЕсли;
	Для каждого СтрокаТЗ Из ТаблицаРаспределения Цикл
		СтрокаТЗ.КодСтатьиФинансирования = КодыСтатей[СтрокаТЗ.СтатьяФинансирования]
	КонецЦикла;
	
КонецПроцедуры

Функция КодыСтатейФинансирования() Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СтатьиФинансированияЗарплата.Код КАК КодСтатьиФинансирования,
	|	СтатьиФинансированияЗарплата.Ссылка
	|ИЗ
	|	Справочник.СтатьиФинансированияЗарплата КАК СтатьиФинансированияЗарплата";
	Выборка = Запрос.Выполнить().Выбрать();
	
	КодыСтатей = Новый Соответствие;
	Пока Выборка.Следующий() Цикл
		КодыСтатей.Вставить(Выборка.Ссылка, Выборка.КодСтатьиФинансирования);
	КонецЦикла;
	КодыСтатей.Вставить(Справочники.СтатьиФинансированияЗарплата.ПустаяСсылка(), "");
	
	Возврат КодыСтатей;

КонецФункции 

// Формирует представление результата распределения строки начисления
// Параметры
//	СуммаОсновнойСтроки - число, результат расчета строки начисления 
//	ВидРасчета - вид расчета из строки начисления
//	СтрокиРаспределения - коллекция строк с результатами распределения строки начисления, содержит поле КодСтатьиФинансирования
//	ПараметрыДляПроверки - структура, см ПараметрыДляПроверкиРезультатовРаспределенияНачислений.
//
// Возвращаемое значение - строка, для помещения в реквизит КомандаРедактированияРаспределения.
//
Функция ПредставлениеРезультатаРаспределенияСтрокиНачисления(СуммаОсновнойСтроки, ВидРасчета, СтрокиРаспределения, ПараметрыДляПроверки) Экспорт
	
	ВидОперации = ПараметрыДляПроверки.НачислениеУдержаниеВидОперации[ВидРасчета];
	ПараметрыДляПроверки.ПроверяемыеПоля.СпособОтраженияЗарплатыВБухучете = ПараметрыДляПроверки.ВидыОперацийПособия.Найти(ВидОперации) = Неопределено;
	ЕстьОшибкиРаспределения = РезультатРаспределенияСодержитОшибки(СуммаОсновнойСтроки, СтрокиРаспределения, ПараметрыДляПроверки.ПроверяемыеПоля);
	
	Возврат ОтражениеЗарплатыВБухучетеКлиентСерверРасширенный.ПредставлениеРаспределения(СтрокиРаспределения, ЕстьОшибкиРаспределения, ПараметрыДляПроверки.ПроверяемыеПоля.СтатьяРасходов)

КонецФункции

Функция ПредставлениеРезультатаРаспределенияСтрокиУдержания(СуммаОсновнойСтроки, СтрокиРаспределения, ПроверяемыеПоля) Экспорт
	
	ЕстьОшибкиРаспределения = РезультатРаспределенияСодержитОшибки(СуммаОсновнойСтроки, СтрокиРаспределения, ПроверяемыеПоля);
	Возврат ОтражениеЗарплатыВБухучетеКлиентСерверРасширенный.ПредставлениеРаспределения(СтрокиРаспределения, ЕстьОшибкиРаспределения, ПроверяемыеПоля.СтатьяРасходов)

КонецФункции

Функция ПредставлениеРезультатаРаспределенияСтрокиПогашениеЗаймов(СуммаОсновнойСтроки, СтрокиРаспределения, ПроверяемыеПоля) Экспорт
	
	ЕстьОшибкиРаспределения = РезультатРаспределенияСодержитОшибки(СуммаОсновнойСтроки, СтрокиРаспределения, ПроверяемыеПоля);
	
	РаспределениеВсейСуммыСтроки = Новый Массив;
	
	ИменаКолонок = "СтатьяФинансирования,СтатьяРасходов,КодСтатьиФинансирования,Результат,ВидУдержания,Сотрудник,Подразделение";
	Для каждого СтрокаРаспределения Из СтрокиРаспределения Цикл
		
		ОписаниеСтроки = Новый Структура(ИменаКолонок);
		ЗаполнитьЗначенияСвойств(ОписаниеСтроки, СтрокаРаспределения);
		
		СтрокаНеНайдена = Истина;
		
		Для Каждого СтрокаРаспределенияВсейСуммыСтроки Из РаспределениеВсейСуммыСтроки Цикл
			Если СтрокаРаспределенияВсейСуммыСтроки.СтатьяФинансирования = ОписаниеСтроки.СтатьяФинансирования
				И СтрокаРаспределенияВсейСуммыСтроки.КодСтатьиФинансирования = ОписаниеСтроки.КодСтатьиФинансирования
				И СтрокаРаспределенияВсейСуммыСтроки.СтатьяРасходов = ОписаниеСтроки.СтатьяРасходов
				И СтрокаРаспределенияВсейСуммыСтроки.Сотрудник = ОписаниеСтроки.Сотрудник
				И СтрокаРаспределенияВсейСуммыСтроки.Подразделение = ОписаниеСтроки.Подразделение Тогда
				СтрокаРаспределенияВсейСуммыСтроки.Результат = СтрокаРаспределенияВсейСуммыСтроки.Результат + ОписаниеСтроки.Результат;
				СтрокаНеНайдена = Ложь;
			КонецЕсли;				
		КонецЦикла;
		
		Если СтрокаНеНайдена Тогда
			РаспределениеВсейСуммыСтроки.Добавить(ОписаниеСтроки);
		КонецЕсли;			
		
	КонецЦикла;
	
	Возврат ОтражениеЗарплатыВБухучетеКлиентСерверРасширенный.ПредставлениеРаспределения(РаспределениеВсейСуммыСтроки, ЕстьОшибкиРаспределения, ПроверяемыеПоля.СтатьяРасходов)

КонецФункции

// ПроверяемыеПоля см ПараметрыДляПроверкиРезультатовРаспределенияНачислений.ПроверяемыеПоля.
Функция РезультатРаспределенияСодержитОшибки(СуммаОсновнойСтроки, СтрокиРаспределения, ПроверяемыеПоля)
	
	РезультатРаспределения = 0;
	Для каждого СтрокаРаспределения Из СтрокиРаспределения Цикл
		РезультатРаспределения = РезультатРаспределения + СтрокаРаспределения.Результат;
		Для Каждого КлючИЗначение Из ПроверяемыеПоля Цикл
			Если КлючИЗначение.Значение И Не ЗначениеЗаполнено(СтрокаРаспределения[КлючИЗначение.Ключ]) Тогда
				Возврат Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат СуммаОсновнойСтроки <> РезультатРаспределения;

КонецФункции 

Функция ПараметрыДляПроверкиРезультатовРаспределенияНачислений() Экспорт

	ВидыОперацийПособия = Новый Массив;
	ВидыОперацийПособия.Добавить(Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФСС);
	ВидыОперацийПособия.Добавить(Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФССНС);
	
	ПроверяемыеПоля = Новый Структура;
	ПроверяемыеПоля.Вставить("СтатьяФинансирования", ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата"));
	ПроверяемыеПоля.Вставить("СтатьяРасходов", ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении"));
	ПроверяемыеПоля.Вставить("СпособОтраженияЗарплатыВБухучете", Ложь);
	
	НачислениеУдержаниеВидОперации = ОтражениеЗарплатыВУчете.НачислениеУдержаниеВидОперации();
	
	Параметры = Новый Структура;
	Параметры.Вставить("ВидыОперацийПособия", ВидыОперацийПособия);
	Параметры.Вставить("НачислениеУдержаниеВидОперации", НачислениеУдержаниеВидОперации);
	Параметры.Вставить("ПроверяемыеПоля", ПроверяемыеПоля);
	
	Возврат Параметры;

КонецФункции

Функция ПараметрыДляПроверкиРезультатовРаспределенияУдержаний() Экспорт

	Параметры = Новый Структура;
	Параметры.Вставить("СтатьяФинансирования", ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата"));
	Параметры.Вставить("СтатьяРасходов", ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении"));
	Параметры.Вставить("Сотрудник", Истина);
	
	Возврат Параметры;

КонецФункции

Процедура ЗаполнитьКодСтатьиФинансирования(Таблица, Коды) Экспорт

	Для каждого СтрокаТЗ Из Таблица Цикл
		СтрокаТЗ.КодСтатьиФинансирования = Коды[СтрокаТЗ.СтатьяФинансирования];
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область ЗначенияСтатейРасходовПоУмолчанию

// Возвращает ссылку на элемент справочника СтатьиРасходовЗарплата с кодом 290.
Функция СтатьяРасходов290()
	
	Статья290 = Справочники.СтатьиРасходовЗарплата.ПустаяСсылка();
	Если ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		Статья290 = Справочники.СтатьиРасходовЗарплата.НайтиПоКоду("290");
	КонецЕсли;

	Возврат Статья290;
	
КонецФункции

// Возвращает ссылку на элемент справочника СтатьиРасходовЗарплата с кодом 296.
Функция СтатьяРасходов296()
	
	Статья296 = Справочники.СтатьиРасходовЗарплата.ПустаяСсылка();
	Если ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		Статья296 = Справочники.СтатьиРасходовЗарплата.НайтиПоКоду("296");
	КонецЕсли;

	Возврат Статья296;
	
КонецФункции

// Возвращает ссылку на элемент справочника СтатьиРасходовЗарплата с кодом 265.
Функция СтатьяРасходов265() Экспорт
	
	Статья265 = Справочники.СтатьиРасходовЗарплата.ПустаяСсылка();
	Если ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		Статья265 = Справочники.СтатьиРасходовЗарплата.НайтиПоКоду("265");
	КонецЕсли;

	Возврат Статья265;
	
КонецФункции

// Возвращает дату вступления в силу приказа Минфина РФ от 29 сентября 2020 г. №222н.
//
// Параметры:
//  нет
//
// Возвращаемое значение:
//   дата
//
Функция ДатаПриказа222н()

	Возврат '20210101';

КонецФункции

#КонецОбласти

#Область ПолучениеНастроекБухучетаСотрудниковНачислений

Функция ЗапросПредставленияБухучетЗарплатыСотрудников(ТолькоРазрешенные, ИмяВТИсточникДанных, ИмяВТ = "Представления_БухучетЗарплатыСотрудников") Экспорт
	
	ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(ИмяВТИсточникДанных, "Сотрудник");
	Запрос = ЗарплатаКадрыОбщиеНаборыДанных.ЗапросВТИмяРегистраСрез(
		"БухучетЗарплатыСотрудников", 
		ТолькоРазрешенные,
		ОписаниеФильтра,
		,
		Истина,
		ИмяВТ);

	Возврат Запрос;
	
КонецФункции

Процедура СоздатьВТБухучетЗарплатыСотрудников(МенеджерВТ, ТаблицаФильтра, ИмяВТ = "ВТБухучетЗарплатыСотрудников")

	ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(ТаблицаФильтра, "Сотрудник");
	Запрос = ЗарплатаКадрыОбщиеНаборыДанных.ЗапросВТИмяРегистраСрез(
		"БухучетЗарплатыСотрудников", 
		Ложь,
		ОписаниеФильтра,
		,
		Истина,
		ИмяВТ);
		
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Выполнить();

КонецПроцедуры

Процедура СоздатьВТБухучетНачисленийСотрудников(МенеджерВТ, ОписаниеФильтра, ПараметрыПостроения = Неопределено, ИмяТаблицы = "", УдалятьВТ = Истина)

	Если ЗначениеЗаполнено(ИмяТаблицы) Тогда
		ИмяВТ = ИмяТаблицы;
	Иначе
		ИмяВТ = "ВТБухучетНачисленийСотрудников"
	КонецЕсли;
	
	Запрос = ЗарплатаКадрыОбщиеНаборыДанных.ЗапросВТИмяРегистраСрез(
		"БухучетНачисленийСотрудников", 
		Ложь,
		ОписаниеФильтра,
		ПараметрыПостроения,
		Истина,
		"ВТБухучетНачисленийСотрудниковИсходныеЗаписи");
		
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Бухучет.Сотрудник КАК Сотрудник,
	|	ЕСТЬNULL(БухучетРаспределение.СтатьяФинансирования, Бухучет.СтатьяФинансирования) КАК СтатьяФинансирования,
	|	ЕСТЬNULL(БухучетРаспределение.СтатьяРасходов, Бухучет.СтатьяРасходов) КАК СтатьяРасходов,
	|	ЕСТЬNULL(БухучетРаспределение.СпособОтраженияЗарплатыВБухучете, Бухучет.СпособОтраженияЗарплатыВБухучете) КАК СпособОтраженияЗарплатыВБухучете,
	|	ЕСТЬNULL(БухучетРаспределение.ОтношениеКЕНВД, Бухучет.ОтношениеКЕНВД) КАК ОтношениеКЕНВД,
	|	ЕСТЬNULL(БухучетРаспределение.ДоляРаспределения, 1) КАК ДоляРаспределения
	|ПОМЕСТИТЬ ИмяВТ
	|ИЗ
	|	ВТБухучетНачисленийСотрудниковИсходныеЗаписи КАК Бухучет
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.БухучетНачисленийСотрудниковРаспределение КАК БухучетРаспределение
	|		ПО Бухучет.ИдентификаторЗаписи = БухучетРаспределение.ИдентификаторЗаписи";
	Запрос.Текст = СтрЗаменить(Запрос.Текст,"ИмяВТ", ИмяВТ);
	Запрос.Текст = СтрЗаменить(Запрос.Текст,"Бухучет.Сотрудник КАК Сотрудник", "Бухучет.*");
	Запрос.Выполнить();
	
	Если УдалятьВТ Тогда
		ЗарплатаКадры.УничтожитьВТ(МенеджерВТ, "ВТБухучетНачисленийСотрудниковИсходныеЗаписи");
	КонецЕсли

КонецПроцедуры

Функция БухучетНачисленийСотрудника(Сотрудник, ДатаАктуальности) Экспорт

	ТаблицаФильтра = Новый ТаблицаЗначений;
	ТаблицаФильтра.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	ТаблицаФильтра.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	
	СтрокаСотрудник = ТаблицаФильтра.Добавить();
	СтрокаСотрудник.Сотрудник = Сотрудник;
	СтрокаСотрудник.Период = ДатаАктуальности; 
	
	ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(ТаблицаФильтра, "Сотрудник");
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	СоздатьВТБухучетНачисленийСотрудников(МенеджерВТ, ОписаниеФильтра);

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Бухучет.Сотрудник КАК Сотрудник,
	|	Бухучет.Начисление КАК Начисление,
	|	Бухучет.ДокументОснование КАК ДокументОснование,
	|	Бухучет.РегистраторСобытия КАК Регистратор,
	|	Бухучет.СтатьяФинансирования КАК СтатьяФинансирования,
	|	Бухучет.СтатьяРасходов КАК СтатьяРасходов,
	|	Бухучет.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	Бухучет.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	Бухучет.ДоляРаспределения КАК ДоляРаспределения,
	|	Бухучет.ДатаНачала КАК ДатаНачала,
	|	Бухучет.ПериодВозвратногоСобытия КАК ДействуетДо
	|ИЗ
	|	ВТБухучетНачисленийСотрудников КАК Бухучет
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления КАК Начисления
	|		ПО Бухучет.Начисление = Начисления.Ссылка
	|ГДЕ
	|	Бухучет.Используется
	|
	|УПОРЯДОЧИТЬ ПО
	|	Начисления.РеквизитДопУпорядочивания,
	|	Регистратор,
	|	ДействуетДо,
	|	ДокументОснование";
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

Функция ИсторияБухучетаНачисленийСотрудника(Сотрудник) Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаНачала,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаОкончания,
	|	&Сотрудник КАК Сотрудник
	|ПОМЕСТИТЬ ВТСотрудникиПериоды";
	Запрос.Выполнить();
	
	ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(
		"ВТСотрудникиПериоды", "Сотрудник");
	
	ПараметрыПостроения = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистра();
	ПараметрыПостроения.ФормироватьСПериодичностьДень = Ложь;
	
	ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТИмяРегистра(
		"БухучетНачисленийСотрудников",
		Запрос.МенеджерВременныхТаблиц,
		Истина,
		ОписаниеФильтра,
		ПараметрыПостроения);
		
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	БухучетНачислений.Сотрудник КАК Сотрудник,
	|	БухучетНачислений.Период КАК Период,
	|	БухучетНачислений.Начисление КАК Начисление,
	|	БухучетНачислений.ДокументОснование КАК ДокументОснование
	|ПОМЕСТИТЬ ВТНачисления
	|ИЗ
	|	ВТБухучетНачисленийСотрудников КАК БухучетНачислений";
	Запрос.Выполнить();
	
	ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра("ВТНачисления", "Сотрудник,Начисление,ДокументОснование");
	СоздатьВТБухучетНачисленийСотрудников(Запрос.МенеджерВременныхТаблиц, ОписаниеФильтра,,"ВТИстория");
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Начисления.РеквизитДопУпорядочивания КАК РеквизитДопУпорядочивания,
	|	Бухучет.Сотрудник КАК Сотрудник,
	|	Бухучет.Начисление КАК Начисление,
	|	Бухучет.ДокументОснование КАК ДокументОснование,
	|	Бухучет.РегистраторСобытия КАК Регистратор,
	|	Бухучет.СтатьяФинансирования КАК СтатьяФинансирования,
	|	Бухучет.СтатьяРасходов КАК СтатьяРасходов,
	|	Бухучет.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	Бухучет.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	Бухучет.ДоляРаспределения КАК ДоляРаспределения,
	|	Бухучет.ПериодЗаписи КАК Период,
	|	Бухучет.ПериодВозвратногоСобытия КАК ДействуетДо,
	|	Бухучет.Используется КАК Используется
	|ИЗ
	|	ВТИстория КАК Бухучет
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления КАК Начисления
	|		ПО Бухучет.Начисление = Начисления.Ссылка
	|ГДЕ
	|	НЕ Бухучет.ПустойИнтервал
	|
	|УПОРЯДОЧИТЬ ПО
	|	Начисления.РеквизитДопУпорядочивания,
	|	Бухучет.ПериодЗаписи,
	|	Используется УБЫВ,
	|	ДокументОснование,
	|	ДействуетДо
	|ИТОГИ ПО
	|	Начисление";
	Возврат Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);

КонецФункции

#КонецОбласти

#Область РедактированиеБухучетаНачислений

// Получает бухучет действующих начислений сотрудника.
//
// Параметры:
// 		Форма - ФормаКлиентскогоПриложения
// 		СотрудникиДаты - ТаблицаЗначений
// 		НеУчитыватьРегистраторы - Массив, ДокументСсылка
// 		ОписанияТаблиц - Массив, Структура
//
// Возвращаемое значение:
// 		ТаблицаЗначений - колонки таблицы
// 			* Начисление
// 			* ДокументОснование
// 			* СтатьяФинансирования
// 			* СпособОтраженияЗарплатыВБухучете
// 			* СтатьяРасходов
// 			* ОтношениеКЕНВД
// 			* ДоляРаспределения
//			* КодСтатьиФинансирования - если ведется учет по статьям  финансирования.
//
Функция БухучетДействующихНачислений(Форма, СотрудникиДаты, НеУчитыватьРегистраторы, ОписанияТаблиц)

	ТаблицаСотрудников = СотрудникиДаты.СкопироватьКолонки("Сотрудник,Период");
	ТаблицаСотрудников.Колонки.Добавить("Начисление", Новый ОписаниеТипов("ПланВидовРасчетаСсылка.Начисления"));
	ТаблицаСотрудников.Колонки.Добавить("ДокументОснование", Метаданные.ОпределяемыеТипы.РегистраторыБухучетаНачисленийСотрудников.Тип);
	
	ПараметрыПостроения = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез();
	ПараметрыПостроения.ФормироватьСПериодичностьДень = Ложь;
	Если ЗначениеЗаполнено(НеУчитыватьРегистраторы) Тогда
		Если ТипЗнч(НеУчитыватьРегистраторы) <> Тип("Массив") Тогда
			ИсключаемыеРегистраторы = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(НеУчитыватьРегистраторы);
		Иначе
			ИсключаемыеРегистраторы = НеУчитыватьРегистраторы;
		КонецЕсли;
		ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(ПараметрыПостроения.Отборы, "Регистратор", "НЕ В", ИсключаемыеРегистраторы);
	КонецЕсли;
	
	Для Каждого ОписаниеТаблицы Из ОписанияТаблиц Цикл
		ПутьКДанным  = ОписаниеТаблицы.ПутьКДанным;
		ИмяРеквизитаВидРасчета = ОписаниеТаблицы.ИмяРеквизитаВидРасчета;
		ИмяРеквизитаДокументОснование = ОписаниеТаблицы.ИмяРеквизитаДокументОснование;
		ДанныеОсновнаяТаблица = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, ПутьКДанным);
		Для Каждого СтрокаОсновнойТаблицы Из ДанныеОсновнаяТаблица Цикл
			НоваяСтрока = ТаблицаСотрудников.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СотрудникиДаты[0]);
			НоваяСтрока.Начисление = СтрокаОсновнойТаблицы[ИмяРеквизитаВидРасчета];
			Если Не ПустаяСтрока(ИмяРеквизитаДокументОснование) И ЗначениеЗаполнено(СтрокаОсновнойТаблицы[ИмяРеквизитаДокументОснование]) Тогда
				НоваяСтрока.ДокументОснование = СтрокаОсновнойТаблицы[ИмяРеквизитаДокументОснование];
			КонецЕсли;
		КонецЦикла;
		
		Если ЗарплатаКадрыРасширенныйКлиентСервер.РедактироватьНачисленияВОтдельныхПолях(1, ОписаниеТаблицы) Тогда
			Для каждого СтрокаТаблицы Из Форма.БухучетНачисленийРедактируемыхВОтдельныхПолях Цикл
				НоваяСтрока = ТаблицаСотрудников.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СотрудникиДаты[0]);
				НоваяСтрока.Начисление = СтрокаТаблицы[ИмяРеквизитаВидРасчета];
			КонецЦикла;
			ТаблицаСотрудников.Свернуть("Сотрудник,Период,Начисление,ДокументОснование");
		КонецЕсли;
		
	КонецЦикла;
	
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(ТаблицаСотрудников, "Сотрудник,Начисление,ДокументОснование");
	СоздатьВТБухучетНачисленийСотрудников(МенеджерВТ, ОписаниеФильтра, ПараметрыПостроения);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Бухучет.Начисление КАК Начисление,
	|	Бухучет.ДокументОснование КАК ДокументОснование,
	|	Бухучет.СтатьяФинансирования КАК СтатьяФинансирования,
	|	Бухучет.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	Бухучет.СтатьяРасходов КАК СтатьяРасходов,
	|	Бухучет.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	Бухучет.ДоляРаспределения КАК ДоляРаспределения,
	|	Бухучет.Используется КАК Используется
	|ИЗ
	|	ВТБухучетНачисленийСотрудников КАК Бухучет
	|ГДЕ
	|	Бухучет.Используется
	|
	|УПОРЯДОЧИТЬ ПО
	|	Начисление,
	|	СтатьяФинансирования,
	|	СпособОтраженияЗарплатыВБухучете,
	|	СтатьяРасходов,
	|	ОтношениеКЕНВД,
	|	ДоляРаспределения";
	БухучетНачислений = Запрос.Выполнить().Выгрузить();
	Для каждого СтрокаТЗ Из БухучетНачислений Цикл
		Если Не ЗначениеЗаполнено(СтрокаТЗ.ДокументОснование) Тогда
			СтрокаТЗ.ДокументОснование = Неопределено;
		КонецЕсли;
	КонецЦикла;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
		ДополнитьТаблицуРаспределенияКодомСтатьиФинансирования(БухучетНачислений);
	КонецЕсли;
	
	Возврат БухучетНачислений;

КонецФункции

// Получает бухучет начислений сотрудников.
// Вызывается из документов, для  последующего редактирования плановых начислений в форме РедактированиеСоставаНачисленийИУдержаний.
//
// Параметры:
// 		ТаблицаНачисленийСотрудников - ТаблицаЗначений - колонки таблицы
// 			* Сотрудник
// 			* Период
// 			* Начисление
// 		ИсключаемыйРегистратор - ДокументСсылка.
//
// Возвращаемое значение:
// 		ТаблицаЗначений - колонки таблицы
// 			* Сотрудник
// 			* Начисление
// 			* ДокументОснование
// 			* СтатьяФинансирования
// 			* СпособОтраженияЗарплатыВБухучете
// 			* СтатьяРасходов
// 			* ОтношениеКЕНВД
// 			* ДоляРаспределения
//
Функция БухучетНачисленийСотрудников(ТаблицаНачисленийСотрудников, ИсключаемыйРегистратор) Экспорт

	ПараметрыПостроения = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез();
	ПараметрыПостроения.ФормироватьСПериодичностьДень = Ложь;
	ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(ПараметрыПостроения.Отборы, "Регистратор", "<>", ИсключаемыйРегистратор);
	
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(ТаблицаНачисленийСотрудников, "Сотрудник,Начисление,ДокументОснование");
	СоздатьВТБухучетНачисленийСотрудников(МенеджерВТ, ОписаниеФильтра, ПараметрыПостроения);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Бухучет.Сотрудник КАК Сотрудник,
	|	Бухучет.Начисление КАК Начисление,
	|	Бухучет.ДокументОснование КАК ДокументОснование,
	|	Бухучет.СтатьяФинансирования КАК СтатьяФинансирования,
	|	Бухучет.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	Бухучет.СтатьяРасходов КАК СтатьяРасходов,
	|	Бухучет.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	Бухучет.ДоляРаспределения КАК ДоляРаспределения,
	|	Бухучет.Используется КАК Используется
	|ИЗ
	|	ВТБухучетНачисленийСотрудников КАК Бухучет
	|ГДЕ
	|	Бухучет.Используется
	|
	|УПОРЯДОЧИТЬ ПО
	|	Начисление,
	|	СтатьяФинансирования,
	|	СпособОтраженияЗарплатыВБухучете,
	|	СтатьяРасходов,
	|	ОтношениеКЕНВД,
	|	ДоляРаспределения";
	БухучетНачислений = Запрос.Выполнить().Выгрузить();
	Для каждого СтрокаТЗ Из БухучетНачислений Цикл
		Если Не ЗначениеЗаполнено(СтрокаТЗ.ДокументОснование) Тогда
			СтрокаТЗ.ДокументОснование = Неопределено;
		КонецЕсли;
	КонецЦикла;
	
	Возврат БухучетНачислений;

КонецФункции

// Дополняет описание строк начислений бухучетом.
// Вызывается при редактировании плановых начислений в форме РедактированиеСоставаНачисленийИУдержаний.
//
// Параметры:
// 		МассивНачислений - Массив - массив структур, описывающих начисление сотрудника.
// 		ТаблицаБухучетНачислений - ТаблицаЗначений - бухучет начислений
// 		СотрудникиДаты - ТаблицаЗначений.
// 		ИсключаемыйРегистратор - ДокументСсылка.
//
Функция ДополнитьСтрокиНачисленийБухучетом(МассивНачислений, ТаблицаБухучетНачислений, СотрудникиДаты, ИсключаемыйРегистратор) Экспорт
	
	ИспользоватьСтатьиФинансирования = ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный");
	РаботаВБюджетномУчреждении = ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении");
	
	Если ИспользоватьСтатьиФинансирования Тогда
		ДополнитьТаблицуРаспределенияКодомСтатьиФинансирования(ТаблицаБухучетНачислений);
	КонецЕсли;
	
	ТаблицаСотрудников = СотрудникиДаты.СкопироватьКолонки("Сотрудник,Период");
	ТаблицаСотрудников.Колонки.Добавить("Начисление", Новый ОписаниеТипов("ПланВидовРасчетаСсылка.Начисления"));
	ТаблицаСотрудников.Колонки.Добавить("ДокументОснование", Метаданные.ОпределяемыеТипы.РегистраторыБухучетаНачисленийСотрудников.Тип);
	
	Отбор = Новый Структура("Начисление,ДокументОснование");
	Для каждого ОписаниеСтрокаНачислений Из МассивНачислений Цикл
		
		ОписаниеСтрокаНачислений.Вставить("БухучетНачисления");
		ЗаполнитьЗначенияСвойств(Отбор, ОписаниеСтрокаНачислений);
		БухучетНачисления = ТаблицаБухучетНачислений.НайтиСтроки(Отбор);
		Если ЗначениеЗаполнено(БухучетНачисления) Тогда
			ОписаниеСтрокаНачислений.БухучетНачисления = ЗарплатаКадрыРасширенный.ТаблицаЗначенийВСтруктуру(БухучетНачисления);
		КонецЕсли;
		
		НоваяСтрока = ТаблицаСотрудников.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СотрудникиДаты[0]);
		НоваяСтрока.Начисление = ОписаниеСтрокаНачислений.Начисление;
		Если ЗначениеЗаполнено(ОписаниеСтрокаНачислений.ДокументОснование) Тогда
			НоваяСтрока.ДокументОснование = ОписаниеСтрокаНачислений.ДокументОснование;
		КонецЕсли;
		
	КонецЦикла; 
	
	ПараметрыПостроения = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез();
	ПараметрыПостроения.ФормироватьСПериодичностьДень = Ложь;
	ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(ПараметрыПостроения.Отборы, "Регистратор", "<>", ИсключаемыйРегистратор);
	
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(ТаблицаСотрудников, "Сотрудник,Начисление,ДокументОснование");
	СоздатьВТБухучетНачисленийСотрудников(МенеджерВТ, ОписаниеФильтра, ПараметрыПостроения);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Бухучет.Начисление КАК Начисление,
	|	Бухучет.ДокументОснование КАК ДокументОснование,
	|	Бухучет.СтатьяФинансирования КАК СтатьяФинансирования,
	|	Бухучет.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	Бухучет.СтатьяРасходов КАК СтатьяРасходов,
	|	Бухучет.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	Бухучет.ДоляРаспределения КАК ДоляРаспределения,
	|	Бухучет.Используется КАК Используется
	|ИЗ
	|	ВТБухучетНачисленийСотрудников КАК Бухучет
	|ГДЕ
	|	Бухучет.Используется
	|
	|УПОРЯДОЧИТЬ ПО
	|	Начисление,
	|	СтатьяФинансирования,
	|	СпособОтраженияЗарплатыВБухучете,
	|	СтатьяРасходов,
	|	ОтношениеКЕНВД,
	|	ДоляРаспределения";
	БухучетНачислений = Запрос.Выполнить().Выгрузить();
	Для каждого СтрокаТЗ Из БухучетНачислений Цикл
		Если Не ЗначениеЗаполнено(СтрокаТЗ.ДокументОснование) Тогда
			СтрокаТЗ.ДокументОснование = Неопределено;
		КонецЕсли;
	КонецЦикла;
	
	Если ИспользоватьСтатьиФинансирования Тогда
		ДополнитьТаблицуРаспределенияКодомСтатьиФинансирования(БухучетНачислений);
	КонецЕсли;
	
	ИменаКолонок = ИменаКолонокБухучетНачислений(Новый Структура("ИспользоватьСтатьиФинансирования,РаботаВБюджетномУчреждении",ИспользоватьСтатьиФинансирования,РаботаВБюджетномУчреждении));
	КомментарийБухучетИзменился = НСтр("ru = 'Бухучет изменен';
										|en = 'Accounting is changed'");
	ОтборТекущийБухучет = Новый Структура("Начисление,ДокументОснование");
	Для каждого ОписаниеНачисления Из МассивНачислений Цикл 
		
		ОписаниеНачисления.Вставить("КомандаРедактированияБухучетаНачисления");
		ОписаниеНачисления.Вставить("ТекущееЗначениеБухучетНачисления");
		ОписаниеНачисления.Вставить("КомментарийБухучет");  
		
		ТекстПредставления = ОтражениеЗарплатыВБухучетеКлиентСерверРасширенный.ПредставлениеБухучетаНачисления(ОписаниеНачисления.БухучетНачисления, ИспользоватьСтатьиФинансирования, РаботаВБюджетномУчреждении);
		ОписаниеНачисления.КомандаРедактированияБухучетаНачисления = ТекстПредставления;
		
		ОтборТекущийБухучет.Начисление = ОписаниеНачисления.Начисление;
		ОтборТекущийБухучет.ДокументОснование = ?(ЗначениеЗаполнено(ОписаниеНачисления.ДокументОснование),ОписаниеНачисления.ДокументОснование,Неопределено);
		ТекущееЗначениеБухучетНачисления = БухучетНачислений.Скопировать(ОтборТекущийБухучет);
		Если ЗначениеЗаполнено(ТекущееЗначениеБухучетНачисления) Тогда
			ОписаниеНачисления.ТекущееЗначениеБухучетНачисления = ЗарплатаКадрыРасширенный.ТаблицаЗначенийВСтруктуру(ТекущееЗначениеБухучетНачисления);
			Если Не КоллекцииБухучетаИдентичны(ОписаниеНачисления.БухучетНачисления, ОписаниеНачисления.ТекущееЗначениеБухучетНачисления, ИменаКолонок) Тогда
				ОписаниеНачисления.КомментарийБухучет = КомментарийБухучетИзменился;
			КонецЕсли;
		ИначеЕсли ЗначениеЗаполнено(ОписаниеНачисления.БухучетНачисления) Тогда
			ОписаниеНачисления.КомментарийБухучет = КомментарийБухучетИзменился;
		КонецЕсли;
		
	КонецЦикла;

КонецФункции 

Процедура БухучетНачисленийУстановитьУсловноеОформление(Форма, ОписанияТаблицФормы, РегистрацияНачисленийДоступна) Экспорт

	Если Не РегистрацияНачисленийДоступна Или Не ПолучитьФункциональнуюОпцию("РегистрироватьБухучетПлановыхНачислений") Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ОписанияТаблицФормы) = Тип("Массив") Тогда
		ОписанияТаблиц = ОписанияТаблицФормы;
	Иначе
		ОписанияТаблиц = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ОписанияТаблицФормы);
	КонецЕсли;
	
	Для каждого ОписаниеТаблицы Из ОписанияТаблиц Цикл
		
		ИмяТаблицы = ОписаниеТаблицы.ИмяТаблицы;
		ПутьКДанным = ОписаниеТаблицы.ПутьКДанным;
		
		// Оформление поля КомандаРедактированияБухучетаНачисления.
		ТекстПустогоЗначения = НСтр("ru = '<подбирается автоматически>';
									|en = '<picked automatically>'");
		ЭлементОформления = Форма.УсловноеОформление.Элементы.Добавить();
		// Вид оформления
		ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Текст", ТекстПустогоЗначения);
		ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийТекст);
		// Оформляемое поле
		КомандаРедактирования = Форма.Элементы.Найти(ИмяТаблицы + "КомандаРедактированияБухучетаНачисления");
		ПолеОформления = ЭлементОформления.Поля.Элементы.Добавить();
		ПолеОформления.Поле = Новый ПолеКомпоновкиДанных(КомандаРедактирования.Имя);
		// условие для оформления
		ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(КомандаРедактирования.ПутьКДанным);
		
		// Строки с отмененными начислениями.
		ЭлементОформления = Форма.УсловноеОформление.Элементы.Добавить();
		// Вид оформления
		ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.УдаленныеСтроки);
		ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
		// Оформляемое поле
		ПолеОформления = ЭлементОформления.Поля.Элементы.Добавить();
		ПолеОформления.Поле = Новый ПолеКомпоновкиДанных(ИмяТаблицы);
		// условие для оформления
		ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.Использование = Истина;
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКДанным + ".Действие" );
		ЭлементОтбора.ПравоеЗначение = Перечисления.ДействияСНачислениямиИУдержаниями.Отменить;
		
	КонецЦикла;

КонецПроцедуры

Процедура БухучетНачисленийДанныеВРеквизит(Форма, ОписанияТаблицФормы, ИсключаемыеРегистраторы = Неопределено, СотрудникиДаты = Неопределено) Экспорт

	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьРасчетЗарплатыРасширенная") Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ОписанияТаблицФормы) = Тип("Массив") Тогда
		ОписанияТаблиц = ОписанияТаблицФормы;
	Иначе
		ОписанияТаблиц = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ОписанияТаблицФормы);
	КонецЕсли;
	
	ЗаполнятьТекущиеЗначения = ЗначениеЗаполнено(СотрудникиДаты);
	Если ЗаполнятьТекущиеЗначения Тогда
		ТекущиеЗначенияБухучетНачислений = БухучетДействующихНачислений(Форма, СотрудникиДаты, ИсключаемыеРегистраторы, ОписанияТаблиц);
	КонецЕсли;
	
	КомментарийБухучетИзменился = НСтр("ru = 'Бухучет изменен';
										|en = 'Accounting is changed'");
	ИменаКолонок = ИменаКолонокБухучетНачислений(Форма);
	
	ОтборТекущийБухучет = Новый Структура("Начисление,ДокументОснование");
	Для Каждого ОписаниеТаблицы Из ОписанияТаблиц Цикл 
		
		НачисленияРедактируемыеВОтдельныхПолях = Новый Соответствие;
		Если ЗарплатаКадрыРасширенныйКлиентСервер.РедактироватьНачисленияВОтдельныхПолях(1, ОписаниеТаблицы) Тогда
			Если ЗарплатаКадрыРасширенныйКлиентСервер.РайонныйКоэффициентВходитВСоставПлановыхНачислений(Форма) Тогда
				НачисленияРедактируемыеВОтдельныхПолях.Вставить(ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, "НачислениеРайонныйКоэффициент"), Истина);
			КонецЕсли;
			Если ЗарплатаКадрыРасширенныйКлиентСервер.СевернаяНадбавкаВходитВСоставПлановыхНачислений(Форма) Тогда
				НачисленияРедактируемыеВОтдельныхПолях.Вставить(ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, "НачислениеСевернаяНадбавка"), Истина);
			КонецЕсли;
			Если ЗарплатаКадрыРасширенныйКлиентСервер.НадбавкаЗаВредностьВходитВСоставПлановыхНачислений(Форма) Тогда
				НачисленияРедактируемыеВОтдельныхПолях.Вставить(ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, "НачислениеНадбавкаЗаВредность"), Истина);
			КонецЕсли;
			Форма.БухучетНачисленийРедактируемыхВОтдельныхПолях.Очистить();
		КонецЕсли;
		
		ДанныеОсновнаяТаблица = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, ОписаниеТаблицы.ПутьКДанным);
		БухучетНачислений = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, ОписаниеТаблицы.ПутьКДаннымБухучетНачислений).Выгрузить();
		ИмяРеквизитаИдентификаторСтроки = ОписаниеТаблицы.ИмяРеквизитаИдентификаторСтроки;
		ИмяРеквизитаВидРасчета = ОписаниеТаблицы.ИмяРеквизитаВидРасчета;
		
		Если Форма.ИспользоватьСтатьиФинансирования Тогда
			ДополнитьТаблицуРаспределенияКодомСтатьиФинансирования(БухучетНачислений);
		КонецЕсли;
		
		БухучетНачислений.Индексы.Добавить("ИдентификаторСтрокиВидаРасчета");
		ОтборСтрок = Новый Структура("ИдентификаторСтрокиВидаРасчета");
		
		Для Каждого СтрокаОсновнойТаблицы Из ДанныеОсновнаяТаблица Цикл
			Начисление = СтрокаОсновнойТаблицы[ИмяРеквизитаВидРасчета];
			ОтборСтрок.ИдентификаторСтрокиВидаРасчета = СтрокаОсновнойТаблицы[ИмяРеквизитаИдентификаторСтроки];
			БухучетНачисления = БухучетНачислений.Скопировать(ОтборСтрок);
			Если ЗначениеЗаполнено(БухучетНачисления) Тогда
				СтрокаОсновнойТаблицы.БухучетНачисления = ЗарплатаКадрыРасширенный.ТаблицаЗначенийВСтруктуру(БухучетНачисления);
				ТекстПредставления = ОтражениеЗарплатыВБухучетеКлиентСерверРасширенный.ПредставлениеБухучетаНачисления(СтрокаОсновнойТаблицы.БухучетНачисления, Форма.ИспользоватьСтатьиФинансирования, Форма.РаботаВБюджетномУчреждении);
				СтрокаОсновнойТаблицы.КомандаРедактированияБухучетаНачисления = ТекстПредставления;
			КонецЕсли;
			Если ЗаполнятьТекущиеЗначения Тогда
				ОтборТекущийБухучет.Начисление = Начисление;
				ОтборТекущийБухучет.ДокументОснование = ?(ЗначениеЗаполнено(СтрокаОсновнойТаблицы.ДокументОснование),СтрокаОсновнойТаблицы.ДокументОснование,Неопределено);
				ТекущееЗначениеБухучетНачисления = ТекущиеЗначенияБухучетНачислений.Скопировать(ОтборТекущийБухучет);
				Если ЗначениеЗаполнено(ТекущееЗначениеБухучетНачисления) Тогда
					СтрокаОсновнойТаблицы.ТекущееЗначениеБухучетНачисления = ЗарплатаКадрыРасширенный.ТаблицаЗначенийВСтруктуру(ТекущееЗначениеБухучетНачисления);
					Если Не КоллекцииБухучетаИдентичны(СтрокаОсновнойТаблицы.БухучетНачисления, СтрокаОсновнойТаблицы.ТекущееЗначениеБухучетНачисления, ИменаКолонок) Тогда
						СтрокаОсновнойТаблицы.КомментарийБухучет = КомментарийБухучетИзменился;
					КонецЕсли;
				ИначеЕсли ЗначениеЗаполнено(СтрокаОсновнойТаблицы.БухучетНачисления) Тогда
					СтрокаОсновнойТаблицы.КомментарийБухучет = КомментарийБухучетИзменился;
				КонецЕсли;
			КонецЕсли;
			Если НачисленияРедактируемыеВОтдельныхПолях[Начисление] = Истина Тогда
				НоваяСтрока = Форма.БухучетНачисленийРедактируемыхВОтдельныхПолях.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаОсновнойТаблицы);
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура БухучетНачисленийРеквизитВДанные(Форма, Объект, ОписанияТаблицФормы) Экспорт

	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьРасчетЗарплатыРасширенная") Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ОписанияТаблицФормы) = Тип("Массив") Тогда
		ОписанияТаблиц = ОписанияТаблицФормы;
	Иначе
		ОписанияТаблиц = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ОписанияТаблицФормы);
	КонецЕсли;
	
	Объект["БухучетНачислений"].Очистить();

	Для Каждого ОписаниеТаблицы Из ОписанияТаблиц Цикл
		
		ПутьКДанным  = ОписаниеТаблицы.ПутьКДанным;
		ИмяРеквизитаИдентификаторСтроки = ОписаниеТаблицы.ИмяРеквизитаИдентификаторСтроки;
		
		ДанныеОсновнаяТаблица = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, ПутьКДанным);
		Для Каждого СтрокаОсновнойТаблицы Из ДанныеОсновнаяТаблица Цикл
			БухучетНачисления = СтрокаОсновнойТаблицы.БухучетНачисления;
			Если ЗначениеЗаполнено(БухучетНачисления) Тогда
				Для Каждого СтрокаБухучет Из БухучетНачисления Цикл
					НоваяСтрока = Объект["БухучетНачислений"].Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаБухучет);
					НоваяСтрока.ИдентификаторСтрокиВидаРасчета = СтрокаОсновнойТаблицы[ИмяРеквизитаИдентификаторСтроки];
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
		
		Если ЗарплатаКадрыРасширенныйКлиентСервер.РедактироватьНачисленияВОтдельныхПолях(1, ОписаниеТаблицы) Тогда
			Для каждого СтрокаТаблицы Из Форма.БухучетНачисленийРедактируемыхВОтдельныхПолях Цикл
				БухучетНачисления = СтрокаТаблицы.БухучетНачисления;
				Если ЗначениеЗаполнено(БухучетНачисления) Тогда
					Для Каждого СтрокаБухучет Из БухучетНачисления Цикл
						НоваяСтрока = Объект["БухучетНачислений"].Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаБухучет);
						НоваяСтрока.ИдентификаторСтрокиВидаРасчета = СтрокаТаблицы[ИмяРеквизитаИдентификаторСтроки];
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры 

Процедура ВписатьИдентификаторНачисленийРедактируемыхВОтдельныхПолях(Форма, СтрокаОсновнойТаблицы, ОписаниеТаблицы) Экспорт
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "БухучетНачисленийРедактируемыхВОтдельныхПолях") Тогда
		ИмяРеквизитаИдентификаторСтроки = ОписаниеТаблицы.ИмяРеквизитаИдентификаторСтроки;
		НайденныеСтроки = Форма.БухучетНачисленийРедактируемыхВОтдельныхПолях.НайтиСтроки(Новый Структура("Начисление", СтрокаОсновнойТаблицы.Начисление));
		Если ЗначениеЗаполнено(НайденныеСтроки) Тогда
			НайденныеСтроки[0].ИдентификаторСтрокиВидаРасчета = СтрокаОсновнойТаблицы[ИмяРеквизитаИдентификаторСтроки];
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура УстановитьБухучетНачисленийРедактируемыхВОтдельныхПолях(Форма, Начисление, ВходитВСоставПлановых, ОтменитьНачисление) Экспорт
	
	УстановитьБухучет = Ложь;
	РеквизитыФормы = Форма.ПолучитьРеквизиты();
	Для Каждого РеквизитФормы Из РеквизитыФормы Цикл
		Если РеквизитФормы.Имя = "БухучетНачисленийРедактируемыхВОтдельныхПолях" Тогда
			УстановитьБухучет = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если Не УстановитьБухучет Тогда
		Возврат;
	КонецЕсли;
	
	НайденныеСтроки = Форма.БухучетНачисленийРедактируемыхВОтдельныхПолях.НайтиСтроки(Новый Структура("Начисление", Начисление));
	Если ЗначениеЗаполнено(НайденныеСтроки) Тогда
		Если Не ВходитВСоставПлановых Тогда
			Форма.БухучетНачисленийРедактируемыхВОтдельныхПолях.Удалить(НайденныеСтроки[0]);
		ИначеЕсли ОтменитьНачисление Тогда
			НайденныеСтроки[0].Действие = Перечисления.ДействияСНачислениямиИУдержаниями.Отменить;
		Иначе
			НайденныеСтроки[0].Действие = Перечисления.ДействияСНачислениямиИУдержаниями.ПустаяСсылка();
		КонецЕсли;
	Иначе
		Если ВходитВСоставПлановых Тогда
			НоваяСтрока = Форма.БухучетНачисленийРедактируемыхВОтдельныхПолях.Добавить();
			НоваяСтрока.Начисление = Начисление;
			Если ОтменитьНачисление Тогда
				НоваяСтрока.Действие = Перечисления.ДействияСНачислениямиИУдержаниями.Отменить;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура БухучетДействующихНачисленийВРеквизиты(Форма, ОписанияТаблицФормы, ИсключаемыеРегистраторы, СотрудникиДаты, БухучетПрежнихНачислений) Экспорт

	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьРасчетЗарплатыРасширенная") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СотрудникиДаты) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ОписанияТаблицФормы) = Тип("Массив") Тогда
		ОписанияТаблиц = ОписанияТаблицФормы;
	Иначе
		ОписанияТаблиц = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ОписанияТаблицФормы);
	КонецЕсли; 
	
	ИменаКолонок = ИменаКолонокБухучетНачислений(Форма);
	КомментарийБухучетИзменился = НСтр("ru = 'Бухучет изменен';
										|en = 'Accounting is changed'");
	
	БухучетНачислений = БухучетДействующихНачислений(Форма, СотрудникиДаты, ИсключаемыеРегистраторы, ОписанияТаблиц);
	
	Отбор = Новый Структура("Начисление,ДокументОснование");
	Для Каждого ОписаниеТаблицы Из ОписанияТаблиц Цикл
		
		ДанныеОсновнаяТаблица = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, ОписаниеТаблицы.ПутьКДанным);
		ИмяРеквизитаВидРасчета = ОписаниеТаблицы.ИмяРеквизитаВидРасчета;
		Для Каждого СтрокаОсновнойТаблицы Из ДанныеОсновнаяТаблица Цикл
			
			Отбор.Начисление = СтрокаОсновнойТаблицы[ИмяРеквизитаВидРасчета];
			Отбор.ДокументОснование = ?(ЗначениеЗаполнено(СтрокаОсновнойТаблицы.ДокументОснование),СтрокаОсновнойТаблицы.ДокументОснование,Неопределено);
			БухучетНачисления = БухучетНачислений.Скопировать(Отбор);
			Если ЗначениеЗаполнено(БухучетНачисления) Тогда
				СтрокаОсновнойТаблицы.БухучетНачисления = ЗарплатаКадрыРасширенный.ТаблицаЗначенийВСтруктуру(БухучетНачисления);
			Иначе
				СтрокаОсновнойТаблицы.БухучетНачисления = Неопределено;
			КонецЕсли;
			СтрокаОсновнойТаблицы.ТекущееЗначениеБухучетНачисления = СтрокаОсновнойТаблицы.БухучетНачисления;
			ТекстПредставления = ОтражениеЗарплатыВБухучетеКлиентСерверРасширенный.ПредставлениеБухучетаНачисления(СтрокаОсновнойТаблицы.БухучетНачисления, Форма.ИспользоватьСтатьиФинансирования, Форма.РаботаВБюджетномУчреждении);
			СтрокаОсновнойТаблицы.КомандаРедактированияБухучетаНачисления = ТекстПредставления;
			СтрокаОсновнойТаблицы.КомментарийБухучет = "";
			
			Если ЗначениеЗаполнено(БухучетПрежнихНачислений) Тогда
				ЗаполнитьЗначенияСвойств(Отбор, СтрокаОсновнойТаблицы);
				НайденныеСтроки = БухучетПрежнихНачислений.НайтиСтроки(Отбор);
				Если ЗначениеЗаполнено(НайденныеСтроки) Тогда
					СтрокаОсновнойТаблицы.БухучетНачисления = ОбщегоНазначения.СкопироватьРекурсивно(НайденныеСтроки[0].БухучетНачисления, Истина);
					ТекстПредставления = ОтражениеЗарплатыВБухучетеКлиентСерверРасширенный.ПредставлениеБухучетаНачисления(СтрокаОсновнойТаблицы.БухучетНачисления, Форма.ИспользоватьСтатьиФинансирования, Форма.РаботаВБюджетномУчреждении);
					СтрокаОсновнойТаблицы.КомандаРедактированияБухучетаНачисления = ТекстПредставления;
					Если Не КоллекцииБухучетаИдентичны(СтрокаОсновнойТаблицы.БухучетНачисления, СтрокаОсновнойТаблицы.ТекущееЗначениеБухучетНачисления, ИменаКолонок) Тогда
						СтрокаОсновнойТаблицы.КомментарийБухучет = КомментарийБухучетИзменился;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
		Если ЗарплатаКадрыРасширенныйКлиентСервер.РедактироватьНачисленияВОтдельныхПолях(1, ОписаниеТаблицы) Тогда
			
			Для каждого СтрокаТаблицы Из Форма.БухучетНачисленийРедактируемыхВОтдельныхПолях Цикл
				Отбор.Начисление = СтрокаТаблицы[ИмяРеквизитаВидРасчета];
				Отбор.ДокументОснование = Неопределено;
				БухучетНачисления = БухучетНачислений.Скопировать(Отбор);
				Если ЗначениеЗаполнено(БухучетНачисления) Тогда
					СтрокаТаблицы.БухучетНачисления = ЗарплатаКадрыРасширенный.ТаблицаЗначенийВСтруктуру(БухучетНачисления);
				Иначе
					СтрокаТаблицы.БухучетНачисления = Неопределено;
				КонецЕсли;
				СтрокаТаблицы.ТекущееЗначениеБухучетНачисления = СтрокаТаблицы.БухучетНачисления;
				ТекстПредставления = ОтражениеЗарплатыВБухучетеКлиентСерверРасширенный.ПредставлениеБухучетаНачисления(СтрокаТаблицы.БухучетНачисления, Форма.ИспользоватьСтатьиФинансирования, Форма.РаботаВБюджетномУчреждении);
				СтрокаТаблицы.КомандаРедактированияБухучетаНачисления = ТекстПредставления;
				СтрокаТаблицы.КомментарийБухучет = "";
				
				Если ЗначениеЗаполнено(БухучетПрежнихНачислений) Тогда
					ЗаполнитьЗначенияСвойств(Отбор, СтрокаТаблицы);
					НайденныеСтроки = БухучетПрежнихНачислений.НайтиСтроки(Отбор);
					Если ЗначениеЗаполнено(НайденныеСтроки) Тогда
						СтрокаТаблицы.БухучетНачисления = ОбщегоНазначения.СкопироватьРекурсивно(НайденныеСтроки[0].БухучетНачисления, Истина);
						ТекстПредставления = ОтражениеЗарплатыВБухучетеКлиентСерверРасширенный.ПредставлениеБухучетаНачисления(СтрокаТаблицы.БухучетНачисления, Форма.ИспользоватьСтатьиФинансирования, Форма.РаботаВБюджетномУчреждении);
						СтрокаТаблицы.КомандаРедактированияБухучетаНачисления = ТекстПредставления;
						Если Не КоллекцииБухучетаИдентичны(СтрокаТаблицы.БухучетНачисления, СтрокаТаблицы.ТекущееЗначениеБухучетНачисления, ИменаКолонок) Тогда
							СтрокаТаблицы.КомментарийБухучет = КомментарийБухучетИзменился;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

Процедура УстановитьВидимостьБухучетНачисленийРедактируемыхВОтдельныхПолях(Форма, Видимость) Экспорт

	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"БухучетНачисленийРедактируемыхВОтдельныхПолях",
		"Видимость",
		Видимость);

КонецПроцедуры

#КонецОбласти

#Область Прочие

Функция ИспользуетсяЕНВД() Экспорт

	Возврат ПолучитьФункциональнуюОпцию("ИспользуетсяЕНВД");
	
КонецФункции

// Устанавливает подсказку ввода для элемента СтатьяРасходов.
// Параметры:
// 		Форма - форма объекта
// 		ПутьРеквизита - путь к реквизиту типа ПланыВидовРасчетаСсылка.Начисления.
//
Процедура УстановитьПодсказкуВводаСтатьиРасходовПоНачислению(Форма, ПутьРеквизита) Экспорт

	Если Не ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		Возврат;
	КонецЕсли;
	
	ПодсказкаВвода = "211";
	
	ВидРасчета = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, ПутьРеквизита);
	Если ЗначениеЗаполнено(ВидРасчета) Тогда
		СтатьяРасходов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидРасчета, "СтатьяРасходов");
		Если ЗначениеЗаполнено(СтатьяРасходов) Тогда
			ПодсказкаВвода = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтатьяРасходов, "Код");
		КонецЕсли;
	КонецЕсли;	
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы, "СтатьяРасходов", "ПодсказкаВвода", ПодсказкаВвода);

КонецПроцедуры

// Устанавливает подсказку ввода для элемента СтатьяРасходов.
// Параметры:
// 		Форма - форма объекта
// 		ВидВыплаты - типа СправочникСсылка.ВидыВыплатБывшимСотрудникам
//		ПериодРегистрации - месяц регистрации выплаты.
//
Процедура УстановитьПодсказкуВводаСтатьиРасходовВыплатыБывшимСотрудникам(Форма, ВидВыплаты, ПериодРегистрации) Экспорт

	Если Не ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		Возврат;
	КонецЕсли;
	
	ПодсказкаВвода = "211";
	
	Если ЗначениеЗаполнено(ВидВыплаты) Тогда
		
		Если ВидВыплаты = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыВыплатБывшимСотрудникам.СохраняемыйЗаработокНаВремяТрудоустройства")
			Или ВидВыплаты = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыВыплатБывшимСотрудникам.СохраняемоеДенежноеСодержаниеНаПериодТрудоустройства") Тогда
			
			СтатьяРасходов = СтатьяРасходовДляСохраняемогоЗаработкаНаПериодТрудоустройства(ПериодРегистрации);
			Если ЗначениеЗаполнено(СтатьяРасходов) Тогда
				ПодсказкаВвода = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтатьяРасходов, "Код");
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;	
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы, "СтатьяРасходов", "ПодсказкаВвода", ПодсказкаВвода);

КонецПроцедуры

Процедура УстановитьСписокВыбораОтношениеКЕНВД(ЭлементыФормы, ИмяЭлемента) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		
		ЭлементФормы = ЭлементыФормы.Найти(ИмяЭлемента);
		Если ЭлементФормы <> Неопределено Тогда
			ЭлементФормы.РежимВыбораИзСписка = Истина;
			ЭлементФормы.СписокВыбора.Добавить(Перечисления.ОтношениеКЕНВДЗатратНаЗарплату.ЕНВД);
			ЭлементФормы.СписокВыбора.Добавить(Перечисления.ОтношениеКЕНВДЗатратНаЗарплату.НеЕНВД);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

Процедура УточнитьСоставПроверяемыхРеквизитовБухучетПлановыхУдержаний(ДокументОбъект, ПроверяемыеРеквизиты) Экспорт

	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата")
		Или Не ДокументОбъект.БухучетЗаданВДокументе
		Или ДокументОбъект.Действие = Перечисления.ДействияСУдержаниями.Прекратить
		Или Не ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОбъект.Удержание, "ДоступнаСтратегияОтраженияКакЗаданоВидуРасчета") Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "СтатьяФинансирования");
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "СтатьяРасходов");
	ИначеЕсли НЕ ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "СтатьяРасходов");
	КонецЕсли;

КонецПроцедуры

Функция МетаданныеРегистровПодсистемы()
	
	Регистры = Новый Массив;
	Регистры.Добавить(Метаданные.РегистрыНакопления.БухучетНачисленияУдержанияПоКонтрагентамАкционерам);
	Регистры.Добавить(Метаданные.РегистрыНакопления.БухучетНачисленияУдержанияПоСотрудникам);
	Регистры.Добавить(Метаданные.РегистрыНакопления.БухучетНачисленияУдержанияПоСотрудникамАвансом);
	
	Возврат Регистры;
КонецФункции

// Возвращает признак применения статей утвержденных
// Приказом Минфина России от 29.11.2017 N 209н (ред. от 30.11.2018) на дату, переданную в параметре.
//
// Параметры:
//  Период - дата
//
// Возвращаемое значение:
//   Булево
//
Функция СтатьиРасходовПоПриказу209нПрименяются(Период)
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.УчетБюджетныхУчреждений") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("УчетБюджетныхУчреждений");
		ДатаПрименения = Модуль.ДатаНачалаПримененияПриказа209н();
		Если Не ЗначениеЗаполнено(ДатаПрименения) Тогда
			Возврат Ложь;
		Иначе
			Возврат (НачалоМесяца(Период) >= ДатаПрименения);
		КонецЕсли;
	Иначе
		Возврат Ложь;
	КонецЕсли;

КонецФункции

Процедура ЗаполнитьРегистрациюВНалоговомОрганеВКоллекцииСтрок(Организация, Период, КоллекцияСтрокНДФЛ) Экспорт
	
	ГоловнаяОрганизация = ЗарплатаКадры.ГоловнаяОрганизация(Организация);
	ЮридическоеФизическоеЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ГоловнаяОрганизация,"ЮридическоеФизическоеЛицо");
	ОрганизацияЮрлицо = ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", ГоловнаяОрганизация);
	// АВТОУПОРЯДОЧИВАНИЕ, важна повторяемость упорядочивания.
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РегистрацииВНалоговомОргане.Ссылка КАК РегистрацияВНалоговомОргане,
	|	РегистрацииВНалоговомОргане.КодПоОКАТО КАК КодПоОКАТО,
	|	РегистрацииВНалоговомОргане.КодПоОКТМО КАК КодПоОКТМО,
	|	РегистрацииВНалоговомОргане.КПП КАК КПП,
	|	РегистрацииВНалоговомОргане.Код КАК КодНалоговогоОргана,
	|	РегистрацииВНалоговомОргане.ПометкаУдаления КАК ПометкаУдаления
	|ИЗ
	|	Справочник.РегистрацииВНалоговомОргане КАК РегистрацииВНалоговомОргане
	|ГДЕ
	|	РегистрацииВНалоговомОргане.Владелец = &Организация
	|
	|УПОРЯДОЧИТЬ ПО
	|	РегистрацияВНалоговомОргане
	|АВТОУПОРЯДОЧИВАНИЕ";
	РегистрацииНО = Запрос.Выполнить().Выгрузить(); 
	
	Если ТипЗнч(КоллекцияСтрокНДФЛ) <> Тип("ДанныеФормыКоллекция") Тогда
		// передана таблица значений
		Если КоллекцияСтрокНДФЛ.Колонки.Найти("РегистрацияВНалоговомОргане") = Неопределено Тогда
			КоллекцияСтрокНДФЛ.Колонки.Добавить("РегистрацияВНалоговомОргане", Новый ОписаниеТипов("СправочникСсылка.РегистрацииВНалоговомОргане"));
		КонецЕсли;
	КонецЕсли;
	
	ПоляПоиска = "КодНалоговогоОргана";
	Если ОрганизацияЮрлицо Тогда
		ПоляПоиска = СтрШаблон("%1,%2",ПоляПоиска,"КПП");
	КонецЕсли;
	Если Период < УчетНДФЛ.ДатаПереходаНаКодыОКТМО() Тогда
		ПоляПоиска = СтрШаблон("%1,%2",ПоляПоиска,"КодПоОКАТО");
	Иначе
		ПоляПоиска = СтрШаблон("%1,%2",ПоляПоиска,"КодПоОКТМО");
	КонецЕсли;
	
	Отбор = Новый Структура(ПоляПоиска);
	РегистрацииНО.Индексы.Добавить(ПоляПоиска);
	Для каждого СтрокаТаблицы Из КоллекцияСтрокНДФЛ Цикл
		ЗаполнитьЗначенияСвойств(Отбор, СтрокаТаблицы);
		НайденныеСтроки = РегистрацииНО.НайтиСтроки(Отбор);
		Если НайденныеСтроки.Количество() = 1 Тогда
			СтрокаТаблицы.РегистрацияВНалоговомОргане = НайденныеСтроки[0].РегистрацияВНалоговомОргане;
		ИначеЕсли НайденныеСтроки.Количество() > 1 Тогда
			РегистрацияВНалоговомОргане = Неопределено;
			Для каждого СтрокаТЗ Из НайденныеСтроки Цикл
				Если Не СтрокаТЗ.ПометкаУдаления Тогда
					РегистрацияВНалоговомОргане = СтрокаТЗ.РегистрацияВНалоговомОргане;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если Не ЗначениеЗаполнено(РегистрацияВНалоговомОргане) Тогда
				РегистрацияВНалоговомОргане = НайденныеСтроки[0].РегистрацияВНалоговомОргане;
			КонецЕсли;
			СтрокаТаблицы.РегистрацияВНалоговомОргане = РегистрацияВНалоговомОргане;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура УстановитьИспользованиеЕНВД()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НастройкиСистемыНалогообложения.ПрименяетсяЕНВД КАК ПлательщикЕНВД
	|ИЗ
	|	РегистрСведений.НастройкиСистемыНалогообложения КАК НастройкиСистемыНалогообложения
	|ГДЕ
	|	НастройкиСистемыНалогообложения.ПрименяетсяЕНВД";
	
	Результат = Запрос.Выполнить();
	
	Константы.ИспользуетсяЕНВД.Установить(Не Результат.Пустой());
	Константы.ИспользуетсяЕНВДВБюджетномУчреждении.Установить(Не Результат.Пустой() И Константы.РаботаВБюджетномУчреждении.Получить());

КонецПроцедуры

Процедура СоздатьВТНачисленияСДаннымиЕНВД(Организация, Период, МенеджерВременныхТаблиц, НачисленияПоСотрудникам) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата") И ЗарплатаКадры.ВТСуществует(МенеджерВременныхТаблиц, "ВТНачисленияСРаспределением") Тогда
		// Таблица ВТНачисленияСРаспределением содержит сведения о бухучете начислений.
		ОтражениеЗарплатыВБухучете.СоздатьВТНачисленияСДаннымиЕНВДПоТаблицеБухучетНачислений(МенеджерВременныхТаблиц, "ВТНачисленияСРаспределением", "ВТНачисления");
	Иначе
		
		// Таблица ВТНачисленияСРаспределением не содержит сведения о бухучете начислений,
		// получим отражение в бухучете начислений из таблицы НачисленияПоСотрудникам.
		
		ТаблицаНачислений = ОтражениеЗарплатыВУчете.НоваяТаблицаНачислений();
		Для каждого СтрокаТЗ Из НачисленияПоСотрудникам Цикл
			ЗаполнитьЗначенияСвойств(ТаблицаНачислений.Добавить(), СтрокаТЗ);
		КонецЦикла;
		БухучетНачислений = БухучетНачислений(Организация, Период, ТаблицаНачислений);
		ЗарплатаКадры.СоздатьВТПоТаблицеЗначений(МенеджерВременныхТаблиц, БухучетНачислений, "ВТБухучетНачислений");
		ОтражениеЗарплатыВБухучете.СоздатьВТНачисленияСДаннымиЕНВДПоТаблицеБухучетНачислений(МенеджерВременныхТаблиц, "ВТБухучетНачислений", "ВТНачисления");
		
	КонецЕсли;

КонецПроцедуры

Процедура СоздатьПустуюВТБухучетНачислений(Запрос, ИмяВТ)
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 0
	|	0 КАК ИдентификаторСтроки,
	|	0 КАК ИдентификаторСтрокиНачисления,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ПериодРегистрации,
	|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка) КАК Сотрудник,
	|	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка) КАК ФизическоеЛицо,
	|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК ПодразделениеУчетаЗатрат,
	|	ЗНАЧЕНИЕ(Справочник.ТерриторииВыполненияРабот.ПустаяСсылка) КАК ТерриторияВыполненияРаботВОрганизации,
	|	ЗНАЧЕНИЕ(Справочник.ТерриторииВыполненияРабот.ПустаяСсылка) КАК Территория,
	|	ЗНАЧЕНИЕ(ПланВидовРасчета.Начисления.ПустаяСсылка) КАК Начисление,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ПустаяСсылка) КАК ВидОперации,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаНачала,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаОкончания,
	|	НЕОПРЕДЕЛЕНО КАК ДокументОснование,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка) КАК СтатьяФинансирования,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка) КАК СтатьяРасходов,
	|	ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка) КАК СпособОтраженияЗарплатыВБухучете,
	|	ЛОЖЬ КАК ОблагаетсяЕНВД,
	|	0 КАК Сумма
	|ПОМЕСТИТЬ ВТБухучетНачисленийПустая";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТБухучетНачисленийПустая", ИмяВТ);
	Запрос.Выполнить();

КонецПроцедуры

Процедура ИсключитьСтрокиНеОблагаемыеВзносами(БухучетНачислений) Экспорт

	БухучетНачислений.Колонки.Добавить("ИсключатьПриОбработкеВзносов", Новый ОписаниеТипов("Булево"));
	
	СтрокиКОбработке = Новый Массив;
	ОбрабатываемыеДокументы = Новый Соответствие;
	Для каждого СтрокаТЗ Из БухучетНачислений Цикл
		Если ТипЗнч(СтрокаТЗ.ДокументОснование) = Тип("ДокументСсылка.ПризПодарок") Тогда
			СтрокиКОбработке.Добавить(СтрокаТЗ);
			ОбрабатываемыеДокументы.Вставить(СтрокаТЗ.ДокументОснование);
		КонецЕсли;
	КонецЦикла;
	
	Если СтрокиКОбработке.Количество() > 0 Тогда
		УстановитьПривилегированныйРежим(Истина);
		ДокументыПризПодарок = ОбщегоНазначения.ВыгрузитьКолонку(ОбрабатываемыеДокументы, "Ключ");
		ЗначенияРеквизита = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(ДокументыПризПодарок, "ПредусмотреноКолдоговором");
		УстановитьПривилегированныйРежим(Ложь);
		Для каждого СтрокаТЗ Из СтрокиКОбработке Цикл
			Если ЗначенияРеквизита[СтрокаТЗ.ДокументОснование] = Ложь Тогда
				СтрокаТЗ.ИсключатьПриОбработкеВзносов = Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Функция ПодсказкаВводаСпособаРасчетовСФизическимиЛицами(Начисление) Экспорт

	ТекстПодсказки = НСтр("ru = 'Расчеты по оплате труда';
							|en = 'Payroll calculations'");
	Если ЗначениеЗаполнено(Начисление) Тогда
		СпособРасчетов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Начисление, "СпособРасчетовСФизическимиЛицами");
		Если СпособРасчетов = Перечисления.СпособыРасчетовСФизическимиЛицами.ПрочиеРасчетыСПерсоналом Тогда
			ТекстПодсказки = НСтр("ru = 'Прочие расчеты с персоналом';
									|en = 'Other payroll statement'");
		КонецЕсли;
	КонецЕсли;
	
	Возврат ТекстПодсказки;

КонецФункции

// Формирует массив видов операций по зарплате, для которых не заполняется
// способ отражения зарплаты в бухучете.
//
// Возвращаемое значение:
// 	Массив
//
Функция ВидыОперацийПоЗарплатеБезСпособаОтражения() Экспорт

	РасходыБезСпособаОтражения = Новый Массив;
	РасходыБезСпособаОтражения.Добавить(Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФСС);
	РасходыБезСпособаОтражения.Добавить(Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФССНС);
	РасходыБезСпособаОтражения.Добавить(Перечисления.ВидыОперацийПоЗарплате.КомпенсацияЗаЗадержкуЗарплаты);
	РасходыБезСпособаОтражения.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПособиеНаПогребение);
	
	Возврат РасходыБезСпособаОтражения;

КонецФункции

Функция ВидыОперацийРасходыФСС() Экспорт

	ВидыОперацийРасходыФСС = Новый Массив;
	ВидыОперацийРасходыФСС.Добавить(Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФСС); 
	ВидыОперацийРасходыФСС.Добавить(Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФССНС);
	ВидыОперацийРасходыФСС.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПособиеНаПогребение);
	
	Возврат ВидыОперацийРасходыФСС;

КонецФункции

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// Добавляет в список Обработчики процедуры-обработчики обновления,
// необходимые данной подсистеме.
//
// Параметры:
//   Обработчики - ТаблицаЗначений - см. описание функции НоваяТаблицаОбработчиковОбновления
//                                   общего модуля ОбновлениеИнформационнойБазы.
// 
Процедура ЗарегистрироватьОбработчикиОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.7.3";
	Обработчик.Процедура = "ОтражениеЗарплатыВБухучетеРасширенный.ОбновитьДокументыРаспределениеОсновногоЗаработка";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();   
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("59648975-c23c-4326-9d8a-2bd3659d1ecc");
	Обработчик.Комментарий = НСтр("ru = 'Обновление документов ""Распределение основного заработка.""';
									|en = 'Update ""Basic earnings allocation"" documents.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.14.79";
	Обработчик.Процедура = "ОтражениеЗарплатыВБухучетеРасширенный.ОбновитьСтратегиюБухучетаСдельногоЗаработка";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();   
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("94c0b423-469c-4a0d-a815-10110e07fa87");
	Обработчик.Комментарий = НСтр("ru = 'Обновление стратегии отражения в учете начислений по сдельному заработку.';
									|en = 'Updating the strategy for recording piecework accruals in the accounting.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.14.128";
	Обработчик.Процедура = "РегистрыНакопления.БухучетНачисленияУдержанияПоКонтрагентамАкционерам.ОчиститьВидыДоходовИсполнительногоПроизводства";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("a4cce4bb-ab14-42f1-bc99-72494c78ea61"); 
	Обработчик.Комментарий = НСтр("ru = 'Очистка видов дохода исполнительного производства старых движений бухучета начислений и удержаний по контрагентам, акционерам.';
									|en = 'Clearing of enforcement proceedings income kinds of the old records in counterparties accrual and deduction accounting.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.14.128";
	Обработчик.Процедура = "РегистрыНакопления.БухучетНачисленияУдержанияПоСотрудникам.ОчиститьВидыДоходовИсполнительногоПроизводства";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("f5e15567-6901-4105-a6ef-9dade1de3a88"); 
	Обработчик.Комментарий = НСтр("ru = 'Очистка видов дохода исполнительного производства старых движений бухучета начислений и удержаний по сотрудникам.';
									|en = 'Clearing of enforcement proceedings income kinds of the old records in employee accrual and deduction accounting.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.14.128";
	Обработчик.Процедура = "РегистрыНакопления.БухучетНачисленияУдержанияПоСотрудникамАвансом.ОчиститьВидыДоходовИсполнительногоПроизводства";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("eb959c60-0c81-4172-b176-555702f73ae1"); 
	Обработчик.Комментарий = НСтр("ru = 'Очистка видов дохода исполнительного производства старых движений бухучета начислений и удержаний по сотрудникам авансом.';
									|en = 'Clearing of enforcement proceedings income kinds of the old records in employee accrual and deduction accounting as advance.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.17.54";
	Обработчик.Процедура = "ОтражениеЗарплатыВБухучетеРасширенный.ЗаполнитьСтатьюРасходовОплатаДнейУходаЗаДетьмиИнвалидами";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("a4cffb10-5f10-11eb-810b-4cedfb95099a"); 
	Обработчик.Комментарий = НСтр("ru = 'Заполнение статьи расходов начисления Оплата дней ухода за детьми-инвалидами.';
									|en = 'Populating the accrual expense item Payment for disabled child care days.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.20.68";
	Обработчик.Процедура = "ОтражениеЗарплатыВБухучетеРасширенный.СтатьиФинансированияЗаполнитьИспользованиеВБазеСреднегоЗаработка";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("4c6a2a4e-6e8b-4f80-9791-856f4505b809"); 
	Обработчик.Комментарий = НСтр("ru = 'Обновление свойств статей финансирования.';
									|en = 'Update financing item properties.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.20.70";
	Обработчик.Процедура = "ОтражениеЗарплатыВБухучетеРасширенный.ОбновитьБухучетСотрудниковПоСписочнымКадровымДокументам";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("8894235f-c495-4aa9-aa10-22f38f2f8911"); 
	Обработчик.Комментарий = НСтр("ru = 'Обновление бухучета сотрудников.';
									|en = 'Update employee accounting.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.25.36";
	Обработчик.Процедура = "ОтражениеЗарплатыВБухучетеРасширенный.ПеренестиБухучетЗарплатыСотрудников";
	Обработчик.РежимВыполнения = "Монопольно";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.25.36";
	Обработчик.Процедура = "ОтражениеЗарплатыВБухучетеРасширенный.ПеренестиБухучетНачисленийСотрудников";
	Обработчик.РежимВыполнения = "Монопольно";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.25.125";
	Обработчик.Процедура = "ОтражениеЗарплатыВБухучетеРасширенный.ОбновитьСтратегиюБухучетаУдержаний";
	Обработчик.РежимВыполнения = "Монопольно";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.28.76";
	Обработчик.Процедура = "ОтражениеЗарплатыВБухучетеРасширенный.ПроверитьОбновитьСтратегиюБухучетаУдержаний";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("165add29-49e0-437a-a5d5-8367cb0be84c");
	Обработчик.Комментарий = НСтр("ru = 'Обновление стратегии отражения в учете удержаний.';
									|en = 'Update the strategy of recording in deduction accounting.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.29.75";
	Обработчик.Процедура = "ОтражениеЗарплатыВБухучетеРасширенный.ПеренестиНастройкиБухучетаДоговоровОпеки";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("a53819ca-6de6-4d6b-858e-b78b7f2d49f0");
	Обработчик.Комментарий = НСтр("ru = 'Перенос настроек бухучета договоров опеки.';
									|en = 'Transfer accounting settings of guardianship contracts.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.29.75";
	Обработчик.Процедура = "ОтражениеЗарплатыВБухучетеРасширенный.ПеренестиНастройкиБухучетаДоговоровГПХ";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("1c9843b8-9624-411c-b236-cfc622e13a3a");
	Обработчик.Комментарий = НСтр("ru = 'Перенос настроек бухучета договоров ГПХ.';
									|en = 'Transfer accounting settings of civil law contracts.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.29.75";
	Обработчик.Процедура = "ОтражениеЗарплатыВБухучетеРасширенный.ПеренестиНастройкиБухучетаВыплатПоДоговорамГПХ";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("924a1500-be05-475c-980f-4f89c1e87cea");
	Обработчик.Комментарий = НСтр("ru = 'Перенос настроек бухучета выплат по договорам ГПХ.';
									|en = 'Transferring settings of civil law contract payment accounting.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.30.149";
	Обработчик.Процедура = "ОтражениеЗарплатыВБухучетеРасширенный.ЗаполнитьПравилаЗаменыСтатейФинансирования";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("69d97eb6-9c64-4718-b68e-8eb2469fa8f4");
	Обработчик.Комментарий = НСтр("ru = 'Заполнение правил замены в статьях финансирования.';
									|en = 'Заполнение правил замены в статьях финансирования.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.30.149";
	Обработчик.Процедура = "ОтражениеЗарплатыВБухучетеРасширенный.ЗаполнитьПравилаЗаменыСпособовОтражения";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("82611f2a-294e-441d-b867-62f09b0ed96c");
	Обработчик.Комментарий = НСтр("ru = 'Заполнение правил замены в способах отражения зарплаты в бухучете.';
									|en = 'Заполнение правил замены в способах отражения зарплаты в бухучете.'");
	
КонецПроцедуры

Процедура ОбновитьДокументыРаспределениеОсновногоЗаработка(ПараметрыОбновления = Неопределено) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДокументыКОбработке.Ссылка КАК Ссылка,
	|	ДокументыКОбработке.Проведен КАК Проведен,
	|	ДокументыКОбработке.УдалитьФизическоеЛицо КАК ФизическоеЛицо,
	|	ДокументыКОбработке.Дата КАК Дата
	|ИЗ
	|	Документ.РаспределениеОсновногоЗаработка КАК ДокументыКОбработке
	|ГДЕ
	|	ДокументыКОбработке.УдалитьСотрудник <> ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументыКОбработке.ПериодРегистрации УБЫВ";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "Документ.РаспределениеОсновногоЗаработка", "Ссылка", Выборка.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		
		НоваяСтрока = ДокументОбъект.Сотрудники.Добавить();
		НоваяСтрока.Сотрудник = ДокументОбъект.УдалитьСотрудник;
		НоваяСтрока.ИдентификаторСтрокиСотрудника = 1;
		
		НоваяСтрока = ДокументОбъект.ФизическиеЛица.Добавить();
		НоваяСтрока.ФизическоеЛицо = Выборка.ФизическоеЛицо;
				
		Для каждого СтрокаТЧ Из ДокументОбъект.РаспределениеЗаработка Цикл
			СтрокаТЧ.ИдентификаторСтрокиСотрудника = 1;
		КонецЦикла;
		
		ДокументОбъект.УдалитьСотрудник = Справочники.Сотрудники.ПустаяСсылка();
		ДокументОбъект.УдалитьФизическоеЛицо = Справочники.ФизическиеЛица.ПустаяСсылка();
		
		ФизическиеЛица = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Выборка.ФизическоеЛицо);
		ДокументОбъект.КраткийСоставДокумента = ЗарплатаКадры.КраткийСоставСотрудниковПоСпискуФизическихЛиц(ФизическиеЛица, Выборка.Дата);
		
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект);
		
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;

КонецПроцедуры

Процедура ЗаполнитьСтатьюРасходовВБухучетНачисленияУдержанияПоСотрудникам(ПараметрыОбновления = Неопределено) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ГруппыОпераций = Новый Массив();
	ГруппыОпераций.Добавить(Перечисления.ГруппыНачисленияУдержанияВыплаты.Начислено);
	ГруппыОпераций.Добавить(Перечисления.ГруппыНачисленияУдержанияВыплаты.Удержано);
	ГруппыОпераций.Добавить(Перечисления.ГруппыНачисленияУдержанияВыплаты.Льготы);
	
	СтатьиРасходовПоСпособамРасчетов 	= ЗарплатаКадры.СтатьиРасходовПоСпособамРасчетовСФизическимиЛицами();
	СтатьяРасчетыПоОплатеТруда 			= СтатьиРасходовПоСпособамРасчетов[Перечисления.СпособыРасчетовСФизическимиЛицами.ОплатаТруда];
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ГруппыОпераций", ГруппыОпераций);
	Запрос.УстановитьПараметр("СтатьяРасчетыПоОплатеТруда", СтатьяРасчетыПоОплатеТруда);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НачисленияУдержанияПоСотрудникам.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВТРегистраторы
	|ИЗ
	|	РегистрНакопления.БухучетНачисленияУдержанияПоСотрудникам КАК НачисленияУдержанияПоСотрудникам
	|ГДЕ
	|	НачисленияУдержанияПоСотрудникам.СтатьяРасходов = ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
	|	И НачисленияУдержанияПоСотрудникам.ГруппаНачисленияУдержанияВыплаты В(&ГруппыОпераций)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачисленияУдержанияПоСотрудникам.Регистратор КАК Регистратор,
	|	НачисленияУдержанияПоСотрудникам.НомерСтроки КАК НомерСтроки,
	|	ВЫБОР
	|		КОГДА НачисленияУдержанияПоСотрудникам.СтатьяРасходов = ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
	|				И НачисленияУдержанияПоСотрудникам.ГруппаНачисленияУдержанияВыплаты В (&ГруппыОпераций)
	|			ТОГДА &СтатьяРасчетыПоОплатеТруда
	|		ИНАЧЕ НачисленияУдержанияПоСотрудникам.СтатьяРасходов
	|	КОНЕЦ КАК СтатьяРасходов,
	|	НачисленияУдержанияПоСотрудникам.*
	|ИЗ
	|	РегистрНакопления.БухучетНачисленияУдержанияПоСотрудникам КАК НачисленияУдержанияПоСотрудникам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРегистраторы КАК Регистраторы
	|		ПО НачисленияУдержанияПоСотрудникам.Регистратор = Регистраторы.Регистратор
	|
	|УПОРЯДОЧИТЬ ПО
	|	Регистратор,
	|	НомерСтроки";
	
	РезультатЗапроса = Запрос.Выполнить();
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Регистратор") Цикл
		
		Регистратор = Выборка.Регистратор;
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрНакопления.БухучетНачисленияУдержанияПоСотрудникам.НаборЗаписей", "Регистратор", Регистратор) Тогда
			Продолжить;
		КонецЕсли;
		
		НаборЗаписей = РегистрыНакопления.БухучетНачисленияУдержанияПоСотрудникам.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
		
		Пока Выборка.СледующийПоЗначениюПоля("НомерСтроки") Цикл
			НоваяЗапись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
		КонецЦикла;
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;

КонецПроцедуры

Процедура ЗаполнитьСтатьюРасходовВСтраховыеВзносыПоФизическимЛицам(ПараметрыОбновления = Неопределено) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	СтатьиРасходовПоСпособамРасчетов 	= ЗарплатаКадры.СтатьиРасходовПоСпособамРасчетовСФизическимиЛицами();
	СтатьяРасчетыПоОплатеТруда 			= СтатьиРасходовПоСпособамРасчетов[Перечисления.СпособыРасчетовСФизическимиЛицами.ОплатаТруда];
	СтатьяРасчетыСКонтрагентами			= СтатьиРасходовПоСпособамРасчетов[Перечисления.СпособыРасчетовСФизическимиЛицами.РасчетыСКонтрагентами];
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СтатьяРасчетыПоОплатеТруда", СтатьяРасчетыПоОплатеТруда);
	Запрос.УстановитьПараметр("СтатьяРасчетыСКонтрагентами", СтатьяРасчетыСКонтрагентами);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтраховыеВзносыПоФизическимЛицам.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВТРегистраторы
	|ИЗ
	|	РегистрНакопления.СтраховыеВзносыПоФизическимЛицам КАК СтраховыеВзносыПоФизическимЛицам
	|ГДЕ
	|	СтраховыеВзносыПоФизическимЛицам.СтатьяРасходов = ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтраховыеВзносыПоФизическимЛицам.Регистратор КАК Регистратор,
	|	СтраховыеВзносыПоФизическимЛицам.НомерСтроки КАК НомерСтроки,
	|	ВЫБОР
	|		КОГДА СтраховыеВзносыПоФизическимЛицам.СтатьяРасходов = ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
	|			ТОГДА ВЫБОР
	|					КОГДА СтраховыеВзносыПоФизическимЛицам.Начисление ССЫЛКА Справочник.ВидыВыплатБывшимСотрудникам
	|						ТОГДА &СтатьяРасчетыСКонтрагентами
	|					КОГДА СтраховыеВзносыПоФизическимЛицам.Начисление ССЫЛКА Справочник.ВидыПрочихДоходовФизическихЛиц
	|						ТОГДА &СтатьяРасчетыСКонтрагентами
	|					ИНАЧЕ &СтатьяРасчетыПоОплатеТруда
	|				КОНЕЦ
	|		ИНАЧЕ СтраховыеВзносыПоФизическимЛицам.СтатьяРасходов
	|	КОНЕЦ КАК СтатьяРасходов,
	|	СтраховыеВзносыПоФизическимЛицам.*
	|ИЗ
	|	РегистрНакопления.СтраховыеВзносыПоФизическимЛицам КАК СтраховыеВзносыПоФизическимЛицам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРегистраторы КАК Регистраторы
	|		ПО СтраховыеВзносыПоФизическимЛицам.Регистратор = Регистраторы.Регистратор
	|
	|УПОРЯДОЧИТЬ ПО
	|	Регистратор,
	|	НомерСтроки";
	
	РезультатЗапроса = Запрос.Выполнить();
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Регистратор") Цикл
		
		Регистратор = Выборка.Регистратор;
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрНакопления.СтраховыеВзносыПоФизическимЛицам.НаборЗаписей", "Регистратор", Регистратор) Тогда
			Продолжить;
		КонецЕсли;
		
		НаборЗаписей = РегистрыНакопления.СтраховыеВзносыПоФизическимЛицам.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
		
		Пока Выборка.СледующийПоЗначениюПоля("НомерСтроки") Цикл
			НоваяЗапись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
		КонецЦикла;
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;

КонецПроцедуры

Процедура ЗаполнитьСтатьюРасходовВБухучетНачисленияУдержанияПоКонтрагентамАкционерам(ПараметрыОбновления = Неопределено) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	СтатьиРасходовПоСпособамРасчетов 	= ЗарплатаКадры.СтатьиРасходовПоСпособамРасчетовСФизическимиЛицами();
	СтатьяРасчетыПоОплатеТруда 			= СтатьиРасходовПоСпособамРасчетов[Перечисления.СпособыРасчетовСФизическимиЛицами.ОплатаТруда];
	СтатьяРасчетыСКонтрагентами			= СтатьиРасходовПоСпособамРасчетов[Перечисления.СпособыРасчетовСФизическимиЛицами.РасчетыСКонтрагентами];
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СтатьяРасчетыПоОплатеТруда", СтатьяРасчетыПоОплатеТруда);
	Запрос.УстановитьПараметр("СтатьяРасчетыСКонтрагентами", СтатьяРасчетыСКонтрагентами);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НачисленияУдержания.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВТРегистраторы
	|ИЗ
	|	РегистрНакопления.БухучетНачисленияУдержанияПоКонтрагентамАкционерам КАК НачисленияУдержания
	|ГДЕ
	|	НачисленияУдержания.СтатьяРасходов = ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачисленияУдержания.Регистратор КАК Регистратор,
	|	НачисленияУдержания.НомерСтроки КАК НомерСтроки,
	|	ВЫБОР
	|		КОГДА НачисленияУдержания.СтатьяРасходов = ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
	|			ТОГДА ВЫБОР
	|					КОГДА НачисленияУдержания.НачислениеУдержание В (ЗНАЧЕНИЕ(Перечисление.ВидыОсобыхНачисленийИУдержаний.ДивидендыСотрудников), ЗНАЧЕНИЕ(Перечисление.ВидыОсобыхНачисленийИУдержаний.НФДЛДивидендыСотрудникам))
	|						ТОГДА &СтатьяРасчетыПоОплатеТруда
	|					ИНАЧЕ &СтатьяРасчетыСКонтрагентами
	|				КОНЕЦ
	|		ИНАЧЕ НачисленияУдержания.СтатьяРасходов
	|	КОНЕЦ КАК СтатьяРасходов,
	|	НачисленияУдержания.*
	|ИЗ
	|	РегистрНакопления.БухучетНачисленияУдержанияПоКонтрагентамАкционерам КАК НачисленияУдержания
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРегистраторы КАК Регистраторы
	|		ПО НачисленияУдержания.Регистратор = Регистраторы.Регистратор
	|
	|УПОРЯДОЧИТЬ ПО
	|	Регистратор,
	|	НомерСтроки";
	
	РезультатЗапроса = Запрос.Выполнить();
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Регистратор") Цикл
		
		Регистратор = Выборка.Регистратор;
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрНакопления.БухучетНачисленияУдержанияПоКонтрагентамАкционерам.НаборЗаписей", "Регистратор", Регистратор) Тогда
			Продолжить;
		КонецЕсли;
		
		НаборЗаписей = РегистрыНакопления.БухучетНачисленияУдержанияПоКонтрагентамАкционерам.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
		
		Пока Выборка.СледующийПоЗначениюПоля("НомерСтроки") Цикл
			НоваяЗапись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
		КонецЦикла;
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;

КонецПроцедуры

// Удаление роли из пользовательских профилей.
//
Процедура УдалитьРольОтражениеЗарплатыВБухгалтерскомУчете() Экспорт
	
	ИмяРоли = "? ОтражениеЗарплатыВБухгалтерскомУчете";
	МассивРолей = Новый Массив;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.БухучетХозрасчетныхОрганизаций") Тогда
		МассивРолей.Добавить("ДобавлениеИзменениеОтраженияЗарплатыВБухучетеХозрасчетныхОрганизаций");
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.УчетБюджетныхУчреждений") Тогда
		МассивРолей.Добавить("ДобавлениеИзменениеОтраженияЗарплатыВБухучетеБюджетныхУчреждений");
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОценочныеОбязательстваЗарплатаКадры") Тогда
		МассивРолей.Добавить("ДобавлениеИзменениеОценочныхОбязательствЗарплатаКадры");
		МассивРолей.Добавить("ЧтениеНастроекОценочныхОбязательствЗарплатаКадры");
	КонецЕсли;
	
	СоответствиеРолей = Новый Соответствие;
	СоответствиеРолей.Вставить(ИмяРоли, МассивРолей);
	УправлениеДоступом.ЗаменитьРолиВПрофилях(СоответствиеРолей);

КонецПроцедуры

Процедура ОбновитьСтратегиюБухучетаСдельногоЗаработка(ПараметрыОбновления = Неопределено) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Начисления.Ссылка КАК Ссылка
	|ИЗ
	|	ПланВидовРасчета.Начисления КАК Начисления
	|ГДЕ
	|	Начисления.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.СдельнаяОплатаТруда)
	|	И Начисления.СтратегияОтраженияВУчете <> ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоСдельномуЗаработку)";
	
	РезультатЗапроса = Запрос.Выполнить();
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;

	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "ПланВидовРасчета.Начисления", "Ссылка", Выборка.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		
		НачислениеОбъект = Выборка.Ссылка.ПолучитьОбъект();
		НачислениеОбъект.СтратегияОтраженияВУчете = Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоСдельномуЗаработку;
		
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(НачислениеОбъект);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьСтатьюРасходовОплатаДнейУходаЗаДетьмиИнвалидами(ПараметрыОбновления = Неопределено) Экспорт

	Если Не ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Начисления.Ссылка КАК Ссылка
	|ИЗ
	|	ПланВидовРасчета.Начисления КАК Начисления
	|ГДЕ
	|	Начисления.КатегорияНачисленияИлиНеоплаченногоВремени В (ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ОплатаДнейУходаЗаДетьмиИнвалидами), ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ДенежноеСодержаниеОплатаДнейУходаЗаДетьмиИнвалидами))
	|	И Начисления.СтатьяРасходов = ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)";
	РезультатЗапроса = Запрос.Выполнить();
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Статья266 = СтатьяРасходов266();
	Если Не ЗначениеЗаполнено(Статья266) Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "ПланВидовРасчета.Начисления", "Ссылка", Выборка.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		
		НачислениеОбъект = Выборка.Ссылка.ПолучитьОбъект();
		НачислениеОбъект.СтатьяРасходов = Статья266;
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(НачислениеОбъект);
		
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;

КонецПроцедуры

Процедура СтатьиФинансированияЗаполнитьИспользованиеВБазеСреднегоЗаработка(ПараметрыОбновления = Неопределено) Экспорт

	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтатьиФинансирования.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СтатьиФинансированияЗарплата КАК СтатьиФинансирования
	|ГДЕ
	|	СтатьиФинансирования.ИспользованиеВБазеСреднегоЗаработка = ЗНАЧЕНИЕ(Перечисление.ИспользованиеСтатьиФинансированияВБазеСреднегоЗаработка.ПустаяСсылка)";
	РезультатЗапроса = Запрос.Выполнить();
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "Справочник.СтатьиФинансированияЗарплата", "Ссылка", Выборка.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		
		СтатьяОбъект = Выборка.Ссылка.ПолучитьОбъект();
		СтатьяОбъект.ИспользованиеВБазеСреднегоЗаработка = Перечисления.ИспользованиеСтатьиФинансированияВБазеСреднегоЗаработка.Используется;
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(СтатьяОбъект);
		
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;

КонецПроцедуры

Процедура ОбновитьБухучетСотрудниковПоСписочнымКадровымДокументам(ПараметрыОбновления = Неопределено) Экспорт

	Если Не ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ШаблонТекстаЗапроса = 
	"ВЫБРАТЬ
	|	БухучетЗарплатыСотрудников.ДокументОснование КАК ДокументОснование
	|ПОМЕСТИТЬ ВТДокументыКОбработке
	|ИЗ
	|	РегистрСведений.УдалитьБухучетЗарплатыСотрудников КАК БухучетЗарплатыСотрудников
	|ГДЕ
	|	&УсловиеДокументСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.Сотрудник КАК Сотрудник,
	|	ДокументыКОбработке.ДокументОснование КАК ДокументОснование
	|ИЗ
	|	#ИмяТаблицы КАК Таблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДокументыКОбработке КАК ДокументыКОбработке
	|		ПО Таблица.Ссылка = ДокументыКОбработке.ДокументОснование
	|			И (&УсловиеНеРегистрироватьБухучет)";
	
	ТекстЗапроса = СтрЗаменить(ШаблонТекстаЗапроса, "&УсловиеДокументСсылка", "БухучетЗарплатыСотрудников.ДокументОснование ССЫЛКА Документ.КадровыйПереводСписком");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ИмяТаблицы", "Документ.КадровыйПереводСписком.Сотрудники");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеНеРегистрироватьБухучет", "НЕ Таблица.ИзменитьПодразделениеИДолжность");
	
	Запрос.Текст = ТекстЗапроса;
	ТаблицаДанных = Запрос.Выполнить().Выгрузить();
	
	ПараметрыБухучета = Неопределено;
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба.КадровыйУчетВоеннослужащих") Тогда 
		Модуль = ОбщегоНазначения.ОбщийМодуль("КадровыйУчетВоеннослужащих");
		ПараметрыБухучета = Модуль.КадровыйПереводСпискомПараметрыОбновленияБухучетаСотрудников();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыБухучета) Тогда
		
		УсловиеДокументСсылка = СтрШаблон("%1 %2", "БухучетЗарплатыСотрудников.ДокументОснование ССЫЛКА", ПараметрыБухучета.ИмяДокументСсылка);
		УсловиеНеРегистрироватьБухучет = СтрШаблон("%1 %2", "НЕ", ПараметрыБухучета.ИмяПоляРегистрироватьБухучет);
		
		ТекстЗапроса = СтрЗаменить(ШаблонТекстаЗапроса, "&УсловиеДокументСсылка", УсловиеДокументСсылка);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ИмяТаблицы", ПараметрыБухучета.ИмяТаблицы);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеНеРегистрироватьБухучет", УсловиеНеРегистрироватьБухучет);
		
		Запрос.Текст = ТекстЗапроса;
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(Запрос.Выполнить().Выгрузить(), ТаблицаДанных);
		
	КонецЕсли;
	
	Если ТаблицаДанных.Количество() = 0 Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Для каждого СтрокаТЗ Из ТаблицаДанных Цикл
	
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрСведений.УдалитьБухучетЗарплатыСотрудников", "ДокументОснование", СтрокаТЗ.ДокументОснование) Тогда
			Продолжить;
		КонецЕсли;
		
		НаборЗаписей = РегистрыСведений.УдалитьБухучетЗарплатыСотрудников.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ДокументОснование.Установить(СтрокаТЗ.ДокументОснование);
		НаборЗаписей.Отбор.Сотрудник.Установить(СтрокаТЗ.Сотрудник);
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;

КонецПроцедуры

Процедура ОчиститьПоляРезервовПоОплатеТрудаВДокументах(ИмяТаблицы, ПараметрыОбновления) Экспорт

	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОценочныеОбязательстваЗарплатаКадры") Тогда
		МодульРезервыПоОплатеТруда = ОбщегоНазначения.ОбщийМодуль("РезервыПоОплатеТрудаРасширенный");
		МодульРезервыПоОплатеТруда.СоздатьВТНастройкиРасчетаРезервовОтпусков(МенеджерВТ);
	Иначе
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ИмяТаблицыНачисленнаяЗарплатаИВзносы = СтрШаблон("%1.%2", ИмяТаблицы, "НачисленнаяЗарплатаИВзносы");
	ИмяТаблицыВыплатаЗаСчетРезерва = СтрШаблон("%1.%2", ИмяТаблицы, "ВыплатаЗаСчетРезерва");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Ссылка КАК Ссылка,
	|	ЛОЖЬ КАК ТаблицаРезервов
	|ПОМЕСТИТЬ ВТДокументы
	|ИЗ
	|	#ИмяТаблицыНачисленнаяЗарплатаИВзносы КАК Таблица
	|ГДЕ
	|	(Таблица.РезервБУ
	|			ИЛИ Таблица.РезервНУ
	|			ИЛИ Таблица.Резерв <> ЗНАЧЕНИЕ(Справочник.Резервы.ПустаяСсылка))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Ссылка,
	|	ИСТИНА
	|ИЗ
	|	#ИмяТаблицыВыплатаЗаСчетРезерва КАК Таблица
	|ГДЕ
	|	(Таблица.РезервБУ
	|			ИЛИ Таблица.РезервНУ
	|			ИЛИ Таблица.Резерв <> ЗНАЧЕНИЕ(Справочник.Резервы.ПустаяСсылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтражениеЗарплатыВБухучете.Ссылка КАК Ссылка,
	|	ОтражениеЗарплатыВБухучете.Организация КАК Организация,
	|	ОтражениеЗарплатыВБухучете.ПериодРегистрации КАК ПериодРегистрации,
	|	Документы.ТаблицаРезервов КАК ТаблицаРезервов
	|ПОМЕСТИТЬ ВТДокументыКОбработке
	|ИЗ
	|	ВТДокументы КАК Документы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ #ИмяТаблицы КАК ОтражениеЗарплатыВБухучете
	|		ПО Документы.Ссылка = ОтражениеЗарплатыВБухучете.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Документы.Ссылка КАК Ссылка,
	|	Документы.ТаблицаРезервов КАК ТаблицаРезервов
	|ПОМЕСТИТЬ ВТДокументыТаблица
	|ИЗ
	|	ВТДокументыКОбработке КАК Документы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиРасчетаРезервовПоОплатеТруда КАК Настройки
	|		ПО Документы.Организация = Настройки.Организация
	|			И (Документы.ПериодРегистрации МЕЖДУ Настройки.НачалоПериода И Настройки.КонецПериода)
	|ГДЕ
	|	Настройки.НачалоПериода ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Документы.Ссылка КАК Ссылка
	|ИЗ
	|	ВТДокументыТаблица КАК Документы";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ИмяТаблицыНачисленнаяЗарплатаИВзносы", ИмяТаблицыНачисленнаяЗарплатаИВзносы);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ИмяТаблицыВыплатаЗаСчетРезерва", ИмяТаблицыВыплатаЗаСчетРезерва);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ИмяТаблицы", ИмяТаблицы);
	ДокументыКОбработке = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	Если Не ЗначениеЗаполнено(ДокументыКОбработке) Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Таблица.Ссылка КАК Ссылка,
	|	Таблица.НомерСтроки КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(Справочник.Резервы.ПустаяСсылка) КАК Резерв,
	|	ЛОЖЬ КАК РезервБУ,
	|	ЛОЖЬ КАК РезервНУ,
	|	&ПоляТаблицы КАК ПоляТаблицы
	|ИЗ
	|	#ИмяТаблицыНачисленнаяЗарплатаИВзносы КАК Таблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДокументыТаблица КАК Документы
	|		ПО Таблица.Ссылка = Документы.Ссылка
	|			И (НЕ Документы.ТаблицаРезервов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.Ссылка КАК Ссылка,
	|	Таблица.НомерСтроки КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(Справочник.Резервы.ПустаяСсылка) КАК Резерв,
	|	ЛОЖЬ КАК РезервБУ,
	|	ЛОЖЬ КАК РезервНУ,
	|	&ПоляТаблицы КАК ПоляТаблицы
	|ИЗ
	|	#ИмяТаблицыВыплатаЗаСчетРезерва КАК Таблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДокументыТаблица КАК Документы
	|		ПО Таблица.Ссылка = Документы.Ссылка
	|			И (Документы.ТаблицаРезервов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ИмяТаблицыНачисленнаяЗарплатаИВзносы", ИмяТаблицыНачисленнаяЗарплатаИВзносы);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ИмяТаблицыВыплатаЗаСчетРезерва", ИмяТаблицыВыплатаЗаСчетРезерва);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПоляТаблицы КАК ПоляТаблицы", "Таблица.*");
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Начисления = РезультатЗапроса[0].Выгрузить();
	Начисления.Индексы.Добавить("Ссылка");
	
	Резервы = РезультатЗапроса[1].Выгрузить();
	Резервы.Индексы.Добавить("Ссылка");
	
	Отбор = Новый Структура("Ссылка");
	Для каждого Ссылка Из ДокументыКОбработке Цикл
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, ИмяТаблицы, "Ссылка", Ссылка) Тогда
			ОбработкаЗавершена = Ложь;
			Продолжить;
		КонецЕсли;
		
		Отбор.Ссылка = Ссылка;
		ДокументОбъект = Ссылка.ПолучитьОбъект();
		
		НайденныеСтроки = Начисления.НайтиСтроки(Отбор);
		Если ЗначениеЗаполнено(НайденныеСтроки) Тогда
			ДокументОбъект.НачисленнаяЗарплатаИВзносы.Очистить();
			Для каждого СтрокаТЗ Из НайденныеСтроки Цикл
				ЗаполнитьЗначенияСвойств(ДокументОбъект.НачисленнаяЗарплатаИВзносы.Добавить(), СтрокаТЗ);
			КонецЦикла;
		КонецЕсли;
		
		НайденныеСтроки = Резервы.НайтиСтроки(Отбор);
		Если ЗначениеЗаполнено(НайденныеСтроки) Тогда
			ДокументОбъект.ВыплатаЗаСчетРезерва.Очистить();
			Для каждого СтрокаТЗ Из НайденныеСтроки Цикл
				ЗаполнитьЗначенияСвойств(ДокументОбъект.ВыплатаЗаСчетРезерва.Добавить(), СтрокаТЗ);
			КонецЦикла;
		КонецЕсли;
		
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект,,,РежимЗаписиДокумента.Запись);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
	
	КонецЦикла;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", ОбработкаЗавершена);

КонецПроцедуры

Процедура ПеренестиБухучетЗарплатыСотрудников() Экспорт
	
	ПустыеЗначенияДокументОснование = Новый Массив;
	ПустыеЗначенияДокументОснование.Добавить(Неопределено);
	ПустыеЗначенияДокументОснование.Добавить(Null);
	Для Каждого ТипДокументОснование Из Метаданные.ОпределяемыеТипы.РегистраторыБухучетаЗарплатыСотрудников.Тип.Типы() Цикл
		ПустыеЗначенияДокументОснование.Добавить(ОбщегоНазначенияБЗК.МенеджерОбъектаПоТипу(ТипДокументОснование).ПустаяСсылка());
	КонецЦикла;
	
	ШаблонТекстЗапроса = 
	"ВЫБРАТЬ
	|	Таблица.ИсправленныйДокумент КАК ИсправленныйДокумент,
	|	Таблица.Ссылка КАК Ссылка,
	|	Таблица.Проведен КАК Проведен
	|ИЗ
	|	#Таблица КАК Таблица
	|ГДЕ
	|	Таблица.ИсправленныйДокумент В(&ИсправленныеДокументы)
	|	И Таблица.Проведен";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПустыеЗначенияДокументОснование", ПустыеЗначенияДокументОснование);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Регистратор КАК Регистратор,
	|	ТИПЗНАЧЕНИЯ(Таблица.Регистратор) КАК ТипДокумента
	|ИЗ
	|	РегистрСведений.БухучетЗарплатыСотрудников КАК Таблица
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.ДокументОснование,
	|	ТИПЗНАЧЕНИЯ(Таблица.ДокументОснование)
	|ИЗ
	|	РегистрСведений.УдалитьБухучетЗарплатыСотрудников КАК Таблица
	|ГДЕ
	|	НЕ Таблица.ДокументОснование В (&ПустыеЗначенияДокументОснование)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТипДокумента";
	РезультатЗапроса = Запрос.Выполнить();
	ИсправленныеДокументы = Новый Массив;
	Выборка= РезультатЗапроса.Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("ТипДокумента") Цикл
		
		ДокументыПоТипу = Новый Массив;
		Пока Выборка.Следующий() Цикл
			ДокументыПоТипу.Добавить(Выборка.Регистратор);
		КонецЦикла;
		
		МД = ДокументыПоТипу[0].Метаданные();
		Если МД.Реквизиты.Найти("ИсправленныйДокумент") = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяДокумента = МД.Имя;
		ИмяТаблицы = СтрШаблон("Документ.%1",ИмяДокумента);
		
		Запрос.УстановитьПараметр("ИсправленныеДокументы", ДокументыПоТипу);
		Запрос.Текст = СтрЗаменить(ШаблонТекстЗапроса, "#Таблица", ИмяТаблицы);
		ВыборкаДокументов = Запрос.Выполнить().Выбрать();
		Пока ВыборкаДокументов.Следующий() Цикл
			ИсправленныеДокументы.Добавить(ВыборкаДокументов.ИсправленныйДокумент);
		КонецЦикла;
		
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ИсправленныеДокументы", ИсправленныеДокументы);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	БухучетЗарплатыСотрудников.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрСведений.БухучетЗарплатыСотрудников КАК БухучетЗарплатыСотрудников
	|ГДЕ
	|	БухучетЗарплатыСотрудников.Регистратор В(&ИсправленныеДокументы)";
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			НаборЗаписей = РегистрыСведений.БухучетЗарплатыСотрудников.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей,,Истина);
		КонецЦикла;
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтарыйБухучет.ДокументОснование КАК ДокументОснование
	|ПОМЕСТИТЬ ВТДокументыКОбработке
	|ИЗ
	|	РегистрСведений.УдалитьБухучетЗарплатыСотрудников КАК СтарыйБухучет
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.БухучетЗарплатыСотрудников КАК НовыйБухучет
	|		ПО СтарыйБухучет.ДокументОснование = НовыйБухучет.Регистратор
	|ГДЕ
	|	НЕ СтарыйБухучет.ДокументОснование В (&ПустыеЗначенияДокументОснование)
	|	И НовыйБухучет.Период ЕСТЬ NULL
	|	И СтарыйБухучет.ДокументОснование.Проведен
	|	И НЕ СтарыйБухучет.ДокументОснование В (&ИсправленныеДокументы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Бухучет.Период КАК Период,
	|	Бухучет.Сотрудник КАК Сотрудник,
	|	Бухучет.ДокументОснование КАК ДокументОснование,
	|	Бухучет.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	Бухучет.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	Бухучет.СтатьяФинансирования КАК СтатьяФинансирования,
	|	ВЫБОР
	|		КОГДА Бухучет.ДействуетДо > ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ДОБАВИТЬКДАТЕ(Бухучет.ДействуетДо, ДЕНЬ, 1)
	|		ИНАЧЕ Бухучет.ДействуетДо
	|	КОНЕЦ КАК ДействуетДо
	|ПОМЕСТИТЬ ВТБухучет
	|ИЗ
	|	РегистрСведений.УдалитьБухучетЗарплатыСотрудников КАК Бухучет
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДокументыКОбработке КАК ДокументыКОбработке
	|		ПО Бухучет.ДокументОснование = ДокументыКОбработке.ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Бухучет.Период КАК Период,
	|	Бухучет.Сотрудник КАК Сотрудник,
	|	МАКСИМУМ(Бухучет.ДокументОснование) КАК ДокументОснование
	|ПОМЕСТИТЬ ВТОснования
	|ИЗ
	|	ВТБухучет КАК Бухучет
	|
	|СГРУППИРОВАТЬ ПО
	|	Бухучет.Сотрудник,
	|	Бухучет.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВремяРегистрацииДокументов.Дата КАК Дата,
	|	ВремяРегистрацииДокументов.Сотрудник КАК Сотрудник,
	|	ВремяРегистрацииДокументов.Документ КАК Документ,
	|	ВремяРегистрацииДокументов.ВремяРегистрации КАК ВремяРегистрации
	|ИЗ
	|	РегистрСведений.ВремяРегистрацииДокументовПлановыхНачислений КАК ВремяРегистрацииДокументов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДокументыКОбработке КАК ДокументыКОбработке
	|		ПО ВремяРегистрацииДокументов.Документ = ДокументыКОбработке.ДокументОснование
	|ГДЕ
	|	ВремяРегистрацииДокументов.Проведен
	|
	|УПОРЯДОЧИТЬ ПО
	|	Документ,
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтарыйБухучет.Период КАК Период,
	|	СтарыйБухучет.Сотрудник КАК Сотрудник,
	|	СтарыйБухучет.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	СтарыйБухучет.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	СтарыйБухучет.СтатьяФинансирования КАК СтатьяФинансирования,
	|	СтарыйБухучет.ДействуетДо КАК ДатаОкончания,
	|	ВЫБОР
	|		КОГДА СтарыйБухучет.ДействуетДо > ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ДОБАВИТЬКДАТЕ(СтарыйБухучет.ДействуетДо, ДЕНЬ, 1)
	|		ИНАЧЕ СтарыйБухучет.ДействуетДо
	|	КОНЕЦ КАК ДействуетДо,
	|	СтарыйБухучет.Сотрудник.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СтарыйБухучет.Сотрудник.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	СтарыйБухучет.Сотрудник.ГоловнойСотрудник КАК ГоловнойСотрудник,
	|	ФизическиеЛица.ФИО КАК ФИО
	|ИЗ
	|	РегистрСведений.УдалитьБухучетЗарплатыСотрудников КАК СтарыйБухучет
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.БухучетЗарплатыСотрудников КАК НовыйБухучет
	|		ПО (СтарыйБухучет.Период = НАЧАЛОПЕРИОДА(НовыйБухучет.Период, ДЕНЬ))
	|			И СтарыйБухучет.Сотрудник = НовыйБухучет.Сотрудник
	|			И (НовыйБухучет.Регистратор ССЫЛКА Документ.БухучетЗарплатыСотрудников)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
	|		ПО СтарыйБухучет.Сотрудник.ФизическоеЛицо = ФизическиеЛица.Ссылка
	|ГДЕ
	|	СтарыйБухучет.ДокументОснование В(&ПустыеЗначенияДокументОснование)
	|	И НовыйБухучет.Период ЕСТЬ NULL
	|	И НЕ ФизическиеЛица.ФИО ЕСТЬ NULL
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Бухучет.Период КАК Период,
	|	Бухучет.Сотрудник КАК Сотрудник,
	|	Бухучет.ДокументОснование КАК ДокументОснование,
	|	Бухучет.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	Бухучет.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	Бухучет.СтатьяФинансирования КАК СтатьяФинансирования,
	|	Бухучет.ДействуетДо КАК ДействуетДо
	|ИЗ
	|	ВТОснования КАК Основания
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТБухучет КАК Бухучет
	|		ПО Основания.Период = Бухучет.Период
	|			И Основания.Сотрудник = Бухучет.Сотрудник
	|			И Основания.ДокументОснование = Бухучет.ДокументОснование
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументОснование";
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	КоличествоРезультатов = РезультатЗапроса.ВГраница();
	
	// Обработка ручных корректировок.
	Если Не РезультатЗапроса[КоличествоРезультатов-1].Пустой() Тогда
		
		СотрудникиДаты = Новый ТаблицаЗначений;
		СотрудникиДаты.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
		СотрудникиДаты.Колонки.Добавить("ДатаСобытия", Новый ОписаниеТипов("Дата"));
		
		БухучетСотрудников = РезультатЗапроса[КоличествоРезультатов-1].Выгрузить();
		
		Сотрудники = ОбщегоНазначения.ВыгрузитьКолонку(БухучетСотрудников, "Сотрудник", Истина);
		КадровыеДанные = КадровыйУчет.КадровыеДанныеСотрудников(Ложь, Сотрудники, "Организация");
		ОрганизацииСотрудников = Новый Соответствие;
		Для каждого СтрокаТЗ Из КадровыеДанные Цикл
			ОрганизацииСотрудников.Вставить(СтрокаТЗ.Сотрудник, СтрокаТЗ.Организация);
		КонецЦикла;
		
		Для каждого СтрокаТЗ Из БухучетСотрудников Цикл
			
			Если Не ЗначениеЗаполнено(СтрокаТЗ.ГоловнойСотрудник) Тогда
				Продолжить;
			КонецЕсли;
			
			Организация = ОрганизацииСотрудников[СтрокаТЗ.Сотрудник];
			Если Организация = Неопределено Тогда
				Организация = СтрокаТЗ.ГоловнаяОрганизация;
			КонецЕсли;
			
			ДокументОбъект = Документы.БухучетЗарплатыСотрудников.СоздатьДокумент();
			ДокументОбъект.Организация 		= Организация;
			ДокументОбъект.Дата 			= СтрокаТЗ.Период;
			ДокументОбъект.ДатаНачала 		= СтрокаТЗ.Период;
			ДокументОбъект.ДатаОкончания 	= СтрокаТЗ.ДатаОкончания;
			ДокументОбъект.Проведен 		= Истина;
			ДокументОбъект.УстановитьНовыйНомер();
			
			НоваяСтрока = ДокументОбъект.Сотрудники.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ, "Сотрудник,СтатьяФинансирования,СпособОтраженияЗарплатыВБухучете,ОтношениеКЕНВД");
			
			НоваяСтрока = ДокументОбъект.ФизическиеЛица.Добавить();
			НоваяСтрока.ФизическоеЛицо = СтрокаТЗ.ФизическоеЛицо;
		
			ДокументОбъект.ДополнительныеСвойства.Вставить("ПроверятьБизнесЛогикуПриЗаписи");
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект,,,РежимЗаписиДокумента.Запись);
			
			СотрудникиДаты.Очистить();
			НоваяСтрока = СотрудникиДаты.Добавить();
			НоваяСтрока.Сотрудник 	= СтрокаТЗ.Сотрудник;
			НоваяСтрока.ДатаСобытия = СтрокаТЗ.Период;
			
			ЗарплатаКадрыРасширенный.УстановитьВремяРегистрацииДокумента(ДокументОбъект.Движения, СотрудникиДаты, ДокументОбъект.Ссылка);
			
			ВремяРегистрацииДокумента = Неопределено;
			ДокументОбъект.Движения.БухучетЗарплатыСотрудников.ДополнительныеСвойства.Свойство("ВремяРегистрацииДокумента", ВремяРегистрацииДокумента);
			
			НаборЗаписей = РегистрыСведений.БухучетЗарплатыСотрудников.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(ДокументОбъект.Ссылка);
			ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), СтрокаТЗ);
			Если ВремяРегистрацииДокумента <> Неопределено Тогда
				ПроверитьВремяРегистрацииДокумента(ВремяРегистрацииДокумента, НаборЗаписей);
				НаборЗаписей.ДополнительныеСвойства.Вставить("ВремяРегистрацииДокумента", ВремяРегистрацииДокумента);
			КонецЕсли;
			
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей,,Истина);
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Обработка кадровых документов.
	Если Не РезультатЗапроса[КоличествоРезультатов].Пустой() Тогда
		
		ВремяРегистрацииДокументов = Новый Соответствие;
		Выборка = РезультатЗапроса[КоличествоРезультатов-2].Выбрать();
		Пока Выборка.СледующийПоЗначениюПоля("Документ") Цикл
			ВремяРегистрацииДокумента = Новый Соответствие;
			Пока Выборка.СледующийПоЗначениюПоля("Дата") Цикл
				ВремяРегистрацииСотрудников = Новый Соответствие;
				Пока Выборка.Следующий() Цикл
					ВремяРегистрацииСотрудников.Вставить(Выборка.Сотрудник, Выборка.ВремяРегистрации);
				КонецЦикла;
				ВремяРегистрацииДокумента.Вставить(Выборка.Дата, ВремяРегистрацииСотрудников);
			КонецЦикла;
			ВремяРегистрацииДокументов.Вставить(Выборка.Документ, ВремяРегистрацииДокумента);
		КонецЦикла;
		
		Выборка = РезультатЗапроса[КоличествоРезультатов].Выбрать();
		Пока Выборка.СледующийПоЗначениюПоля("ДокументОснование") Цикл
			
			ИмяПоляСотрудник = "";
			ОписаниеДокумента = Новый Структура("СписочныйДокумент,ИзменитьБухучетЗарплаты", Ложь, Ложь);
			МД = Выборка.ДокументОснование.Метаданные();
			ОписаниеДокумента.СписочныйДокумент = МД.ТабличныеЧасти.Найти("Сотрудники") <> Неопределено;
			Если ОписаниеДокумента.СписочныйДокумент Тогда
				ОписаниеДокумента.ИзменитьБухучетЗарплаты = МД.ТабличныеЧасти.Сотрудники.Реквизиты.Найти("ИзменитьБухучетЗарплаты") <> Неопределено;
			Иначе
				ОписаниеДокумента.ИзменитьБухучетЗарплаты = МД.Реквизиты.Найти("ИзменитьБухучетЗарплаты") <> Неопределено;
				Если МД.Реквизиты.Найти("Сотрудник") <> Неопределено Тогда
					ИмяПоляСотрудник = "Сотрудник";
				ИначеЕсли МД.Реквизиты.Найти("СовмещающийСотрудник") <> Неопределено Тогда
					ИмяПоляСотрудник = "СовмещающийСотрудник";
				КонецЕсли;
			КонецЕсли;
			
			ДокументОбъект = Выборка.ДокументОснование.ПолучитьОбъект();
			
			НастройкиБухучета = НоваяТаблицаНастройкиБухучетаЗаплатыСотрудников();
			Пока Выборка.Следующий() Цикл
				Если Не ПустаяСтрока(ИмяПоляСотрудник) И Выборка.Сотрудник <> ДокументОбъект[ИмяПоляСотрудник] Тогда
					Продолжить;
				КонецЕсли;
				НоваяСтрока = НастройкиБухучета.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка,,"ДействуетДо");
				Если ЗначениеЗаполнено(Выборка.ДействуетДо) Тогда
					НоваяСтрока.ДействуетДо = Выборка.ДействуетДо;
				КонецЕсли;
			КонецЦикла;
			
			ЗаписыватьДокумент = Ложь;
			Если ОписаниеДокумента.ИзменитьБухучетЗарплаты Тогда
				Если ОписаниеДокумента.СписочныйДокумент Тогда
					СотрудникиТЧ = Новый Соответствие;
					Для каждого СтрокаТЧ Из ДокументОбъект.Сотрудники Цикл
						СотрудникиТЧ.Вставить(СтрокаТЧ.Сотрудник, Истина);
						СтрокаТЗ = НастройкиБухучета.Найти(СтрокаТЧ.Сотрудник, "Сотрудник");
						Если СтрокаТЗ = Неопределено Тогда
							Продолжить;
						КонецЕсли;
						СтрокаТЧ.ИзменитьБухучетЗарплаты = Истина;
						ЗаписыватьДокумент = Истина;
					КонецЦикла;
					СтрокиКУдалению = Новый Массив;
					Для каждого СтрокаТЗ Из НастройкиБухучета Цикл
						Если СотрудникиТЧ[СтрокаТЗ.Сотрудник] = Неопределено Тогда
							СтрокиКУдалению.Добавить(СтрокаТЗ);
						КонецЕсли;
					КонецЦикла;
					Для каждого СтрокаТЗ Из СтрокиКУдалению Цикл
						НастройкиБухучета.Удалить(СтрокаТЗ);
					КонецЦикла;
				Иначе
					ДокументОбъект.ИзменитьБухучетЗарплаты = Истина;
					ЗаписыватьДокумент = Истина;
				КонецЕсли;
			КонецЕсли;
			
			НаборЗаписей = РегистрыСведений.БухучетЗарплатыСотрудников.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(Выборка.ДокументОснование);
			Для каждого СтрокаТЗ Из НастройкиБухучета Цикл
				НоваяСтрока = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
			КонецЦикла;
			
			Если НаборЗаписей.Количество() > 0 Тогда
				ВремяРегистрацииДокумента = ВремяРегистрацииДокументов[Выборка.ДокументОснование];
				Если ВремяРегистрацииДокумента <> Неопределено Тогда
					ПроверитьВремяРегистрацииДокумента(ВремяРегистрацииДокумента, НаборЗаписей);
					НаборЗаписей.ДополнительныеСвойства.Вставить("ВремяРегистрацииДокумента", ВремяРегистрацииДокумента);
				КонецЕсли;
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей,,Истина);
				Если ЗаписыватьДокумент Тогда
					ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект,,,РежимЗаписиДокумента.Запись);
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Обработка исправлений.
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтарыйБухучет.ДокументОснование КАК ДокументОснование,
	|	ТИПЗНАЧЕНИЯ(СтарыйБухучет.ДокументОснование) КАК ТипДокумента
	|ПОМЕСТИТЬ ВТДокументыКОбработке
	|ИЗ
	|	РегистрСведений.УдалитьБухучетЗарплатыСотрудниковИсправления КАК СтарыйБухучет
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.БухучетЗарплатыСотрудниковИспр КАК НовыйБухучет
	|		ПО СтарыйБухучет.ДокументОснование = НовыйБухучет.РегистраторИзмерение
	|ГДЕ
	|	НЕ СтарыйБухучет.ДокументОснование В (&ПустыеЗначенияДокументОснование)
	|	И НовыйБухучет.ПериодИзмерение ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВремяРегистрацииДокументов.Дата КАК Дата,
	|	ВремяРегистрацииДокументов.Сотрудник КАК Сотрудник,
	|	ВремяРегистрацииДокументов.Документ КАК Документ,
	|	ВремяРегистрацииДокументов.ВремяРегистрации КАК ВремяРегистрации
	|ИЗ
	|	РегистрСведений.ВремяРегистрацииДокументовПлановыхНачислений КАК ВремяРегистрацииДокументов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДокументыКОбработке КАК ДокументыКОбработке
	|		ПО ВремяРегистрацииДокументов.Документ = ДокументыКОбработке.ДокументОснование
	|ГДЕ
	|	ВремяРегистрацииДокументов.Проведен
	|
	|УПОРЯДОЧИТЬ ПО
	|	Документ,
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыКОбработке.ДокументОснование КАК ДокументОснование,
	|	ДокументыКОбработке.ТипДокумента КАК ТипДокумента
	|ИЗ
	|	ВТДокументыКОбработке КАК ДокументыКОбработке
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТипДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтарыйБухучет.ПериодИзмерение КАК ПериодИзмерение,
	|	СтарыйБухучет.Сотрудник КАК Сотрудник,
	|	СтарыйБухучет.ДокументОснование КАК ДокументОснование,
	|	СтарыйБухучет.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	СтарыйБухучет.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	СтарыйБухучет.СтатьяФинансирования КАК СтатьяФинансирования,
	|	ВЫБОР
	|		КОГДА СтарыйБухучет.ДействуетДо > ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ДОБАВИТЬКДАТЕ(СтарыйБухучет.ДействуетДо, ДЕНЬ, 1)
	|		ИНАЧЕ СтарыйБухучет.ДействуетДо
	|	КОНЕЦ КАК ДействуетДо
	|ИЗ
	|	РегистрСведений.УдалитьБухучетЗарплатыСотрудниковИсправления КАК СтарыйБухучет
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДокументыКОбработке КАК ДокументыКОбработке
	|		ПО СтарыйБухучет.ДокументОснование = ДокументыКОбработке.ДокументОснование
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументОснование";
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	Если РезультатЗапроса[3].Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ВремяРегистрацииДокументов = Новый Соответствие;
	Выборка = РезультатЗапроса[1].Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Документ") Цикл
		ВремяРегистрацииДокумента = Новый Соответствие;
		Пока Выборка.СледующийПоЗначениюПоля("Дата") Цикл
			ВремяРегистрацииСотрудников = Новый Соответствие;
			Пока Выборка.Следующий() Цикл
				ВремяРегистрацииСотрудников.Вставить(Выборка.Сотрудник, Выборка.ВремяРегистрации);
			КонецЦикла;
			ВремяРегистрацииДокумента.Вставить(Выборка.Дата, ВремяРегистрацииСотрудников);
		КонецЦикла;
		ВремяРегистрацииДокументов.Вставить(Выборка.Документ, ВремяРегистрацииДокумента);
	КонецЦикла;
	
	ШаблонТекстЗапроса = 
	"ВЫБРАТЬ
	|	Таблица.ИсправленныйДокумент КАК ИсправленныйДокумент,
	|	Таблица.Ссылка КАК Ссылка,
	|	Таблица.Проведен КАК Проведен
	|ИЗ
	|	#Таблица КАК Таблица
	|ГДЕ
	|	Таблица.ИсправленныйДокумент В(&ИсправленныеДокументы)
	|	И Таблица.Проведен";
	Выборка = РезультатЗапроса[2].Выбрать();
	ДокументыИсправления = Новый Соответствие;
	Пока Выборка.СледующийПоЗначениюПоля("ТипДокумента") Цикл
		
		ИсправленныеДокументы = Новый Массив;
		Пока Выборка.Следующий() Цикл
			ИсправленныеДокументы.Добавить(Выборка.ДокументОснование);
		КонецЦикла;
		
		МД = ИсправленныеДокументы[0].Метаданные();
		Если МД.Реквизиты.Найти("ИсправленныйДокумент") = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ИмяДокумента = МД.Имя;
		ИмяТаблицы = СтрШаблон("Документ.%1",ИмяДокумента);
		
		Запрос.УстановитьПараметр("ИсправленныеДокументы", ИсправленныеДокументы);
		Запрос.Текст = СтрЗаменить(ШаблонТекстЗапроса, "#Таблица", ИмяТаблицы);
		ВыборкаДокументов = Запрос.Выполнить().Выбрать();
		Пока ВыборкаДокументов.Следующий() Цикл
			ДокументыИсправления.Вставить(ВыборкаДокументов.ИсправленныйДокумент, ВыборкаДокументов.Ссылка);
		КонецЦикла;
		
	КонецЦикла;
	
	Выборка = РезультатЗапроса[3].Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("ДокументОснование") Цикл
		
		РегистраторИзмерение = ДокументыИсправления[Выборка.ДокументОснование];
		Если РегистраторИзмерение = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ВремяРегистрацииДокумента = ВремяРегистрацииДокументов[Выборка.ДокументОснование];
		
		НаборЗаписей = РегистрыСведений.БухучетЗарплатыСотрудниковИспр.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.РегистраторИзмерение.Установить(РегистраторИзмерение);
		
		СотрудникиСБухучетом = Новый Соответствие;
		Пока Выборка.Следующий() Цикл
			
			ПериодИзмерение = Неопределено;
			Если ВремяРегистрацииДокумента <> Неопределено Тогда
				ВремяРегистрацииДаты = ВремяРегистрацииДокумента[Выборка.ПериодИзмерение];
				Если ВремяРегистрацииДаты <> Неопределено Тогда
					ПериодИзмерение = ВремяРегистрацииДаты[Выборка.Сотрудник];
				КонецЕсли;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ПериодИзмерение) Тогда
				ПериодИзмерение = Выборка.ПериодИзмерение;
			КонецЕсли;
			
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.РегистраторИзмерение 				= РегистраторИзмерение;
			НоваяЗапись.ПериодИзмерение 					= ПериодИзмерение;
			НоваяЗапись.Сотрудник 							= Выборка.Сотрудник;
			НоваяЗапись.СтатьяФинансирования 				= Выборка.СтатьяФинансирования;
			НоваяЗапись.СпособОтраженияЗарплатыВБухучете 	= Выборка.СпособОтраженияЗарплатыВБухучете;
			НоваяЗапись.ОтношениеКЕНВД 						= Выборка.ОтношениеКЕНВД;
			НоваяЗапись.ДействуетДо 						= Выборка.ДействуетДо;
			
			СотрудникиСБухучетом.Вставить(Выборка.Сотрудник, Истина);
			
		КонецЦикла;
		
		МД = Выборка.ДокументОснование.Метаданные();
		СписочныйДокумент = МД.ТабличныеЧасти.Найти("Сотрудники") <> Неопределено;
		Если СписочныйДокумент Тогда
			ИзменитьБухучетЗарплаты = МД.ТабличныеЧасти.Сотрудники.Реквизиты.Найти("ИзменитьБухучетЗарплаты") <> Неопределено;
		Иначе
			ИзменитьБухучетЗарплаты = МД.Реквизиты.Найти("ИзменитьБухучетЗарплаты") <> Неопределено;
		КонецЕсли;
		
		ЗаписыватьДокумент = Ложь;
		Если ИзменитьБухучетЗарплаты Тогда
			ДокументОбъект = Выборка.ДокументОснование.ПолучитьОбъект();
			Если СписочныйДокумент Тогда
				Для каждого СтрокаТЧ Из ДокументОбъект.Сотрудники Цикл
					Если СотрудникиСБухучетом[СтрокаТЧ.Сотрудник] <> Неопределено Тогда
						СтрокаТЧ.ИзменитьБухучетЗарплаты = Истина;
						ЗаписыватьДокумент = Истина;
					КонецЕсли;
				КонецЦикла;
			Иначе
				ДокументОбъект.ИзменитьБухучетЗарплаты = Истина;
				ЗаписыватьДокумент = Истина;
			КонецЕсли;
		КонецЕсли;
		
		Если ЗаписыватьДокумент Тогда
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект,,,РежимЗаписиДокумента.Запись);
		КонецЕсли;
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПеренестиБухучетНачисленийСотрудников() Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Совмещение.ИсправленныйДокумент КАК ИсправленныйДокумент
	|ПОМЕСТИТЬ ВТИсправленныеДокументы
	|ИЗ
	|	Документ.Совмещение КАК Совмещение
	|ГДЕ
	|	Совмещение.ИсправленныйДокумент <> ЗНАЧЕНИЕ(Документ.Совмещение.ПустаяСсылка)
	|	И Совмещение.Проведен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ИсправленныеДокументы.ИсправленныйДокумент КАК ИсправленныйДокумент
	|ПОМЕСТИТЬ ВТИсправленныеДокументыКОбработке
	|ИЗ
	|	РегистрСведений.УдалитьБухучетПлановыхНачислений КАК Бухучет
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТИсправленныеДокументы КАК ИсправленныеДокументы
	|		ПО Бухучет.Регистратор = ИсправленныеДокументы.ИсправленныйДокумент
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.БухучетНачисленийСотрудниковИспр КАК БухучетИспр
	|		ПО Бухучет.Регистратор = БухучетИспр.РегистраторИзмерение
	|ГДЕ
	|	БухучетИспр.Используется ЕСТЬ NULL";
	Запрос.Выполнить();
	
	Если ЗарплатаКадры.ВТСодержитСтроки(Запрос.МенеджерВременныхТаблиц, "ВТИсправленныеДокументыКОбработке") Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВремяРегистрацииДокументов.Дата КАК Дата,
		|	ВремяРегистрацииДокументов.Сотрудник КАК Сотрудник,
		|	ВремяРегистрацииДокументов.Документ КАК Документ,
		|	ВремяРегистрацииДокументов.ВремяРегистрации КАК ВремяРегистрации
		|ИЗ
		|	РегистрСведений.ВремяРегистрацииДокументовПлановыхНачислений КАК ВремяРегистрацииДокументов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТИсправленныеДокументыКОбработке КАК ДокументыКОбработке
		|		ПО ВремяРегистрацииДокументов.Документ = ДокументыКОбработке.ИсправленныйДокумент
		|ГДЕ
		|	ВремяРегистрацииДокументов.Проведен
		|
		|УПОРЯДОЧИТЬ ПО
		|	Документ,
		|	Дата
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СтарыйБухучет.Период КАК ПериодИзмерение,
		|	СтарыйБухучет.Регистратор КАК РегистраторИзмерение,
		|	СтарыйБухучет.Сотрудник КАК Сотрудник,
		|	СтарыйБухучет.Организация КАК Организация,
		|	СтарыйБухучет.Начисление КАК Начисление,
		|	СтарыйБухучет.ДокументОснование КАК ДокументОснование,
		|	СтарыйБухучет.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	СтарыйБухучет.СтатьяФинансирования КАК СтатьяФинансирования,
		|	СтарыйБухучет.СтатьяРасходов КАК СтатьяРасходов,
		|	СтарыйБухучет.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
		|	СтарыйБухучет.Используется КАК Используется,
		|	СтарыйБухучет.ДействуетДо КАК ДействуетДо
		|ИЗ
		|	РегистрСведений.УдалитьБухучетПлановыхНачислений КАК СтарыйБухучет
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТИсправленныеДокументыКОбработке КАК ДокументыКОбработке
		|		ПО СтарыйБухучет.Регистратор = ДокументыКОбработке.ИсправленныйДокумент
		|
		|УПОРЯДОЧИТЬ ПО
		|	РегистраторИзмерение";
		РезультатЗапроса = Запрос.ВыполнитьПакет();
		
		ВремяРегистрацииДокументов = Новый Соответствие;
		Выборка = РезультатЗапроса[0].Выбрать();
		Пока Выборка.СледующийПоЗначениюПоля("Документ") Цикл
			ВремяРегистрацииДокумента = Новый Соответствие;
			Пока Выборка.СледующийПоЗначениюПоля("Дата") Цикл
				ВремяРегистрацииСотрудников = Новый Соответствие;
				Пока Выборка.Следующий() Цикл
					ВремяРегистрацииСотрудников.Вставить(Выборка.Сотрудник, Выборка.ВремяРегистрации);
				КонецЦикла;
				ВремяРегистрацииДокумента.Вставить(Выборка.Дата, ВремяРегистрацииСотрудников);
			КонецЦикла;
			ВремяРегистрацииДокументов.Вставить(Выборка.Документ, ВремяРегистрацииДокумента);
		КонецЦикла;
		
		Выборка = РезультатЗапроса[1].Выбрать();
		Пока Выборка.СледующийПоЗначениюПоля("РегистраторИзмерение") Цикл
			
			ВремяРегистрацииДокумента = ВремяРегистрацииДокументов[Выборка.РегистраторИзмерение];
			
			НаборЗаписей = РегистрыСведений.БухучетНачисленийСотрудниковИспр.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.РегистраторИзмерение.Установить(Выборка.РегистраторИзмерение);
			
			Пока Выборка.Следующий() Цикл
				
				ПериодИзмерение = Неопределено;
				Если ВремяРегистрацииДокумента <> Неопределено Тогда
					ВремяРегистрацииДаты = ВремяРегистрацииДокумента[Выборка.ПериодИзмерение];
					Если ВремяРегистрацииДаты <> Неопределено Тогда
						ПериодИзмерение = ВремяРегистрацииДаты[Выборка.Сотрудник];
					КонецЕсли;
				КонецЕсли;
				Если Не ЗначениеЗаполнено(ПериодИзмерение) Тогда
					ПериодИзмерение = Выборка.ПериодИзмерение;
				КонецЕсли;
				
				НоваяЗапись = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
				НоваяЗапись.ПериодИзмерение = ПериодИзмерение;
				
			КонецЦикла;
			
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтарыйБухучет.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВТДокументыКОбработке
	|ИЗ
	|	РегистрСведений.УдалитьБухучетПлановыхНачислений КАК СтарыйБухучет
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.БухучетНачисленийСотрудников КАК НовыйБухучет
	|		ПО СтарыйБухучет.Регистратор = НовыйБухучет.Регистратор
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИсправленныеДокументыКОбработке КАК ИсправленныеДокументы
	|		ПО СтарыйБухучет.Регистратор = ИсправленныеДокументы.ИсправленныйДокумент
	|ГДЕ
	|	(СтарыйБухучет.Регистратор ССЫЛКА Документ.БухучетНачисленийСотрудников
	|			ИЛИ СтарыйБухучет.Регистратор ССЫЛКА Документ.НазначениеПлановогоНачисления
	|			ИЛИ СтарыйБухучет.Регистратор ССЫЛКА Документ.Совмещение)
	|	И НовыйБухучет.Используется ЕСТЬ NULL
	|	И ИсправленныеДокументы.ИсправленныйДокумент ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВремяРегистрацииДокументов.Дата КАК Дата,
	|	ВремяРегистрацииДокументов.Сотрудник КАК Сотрудник,
	|	ВремяРегистрацииДокументов.Документ КАК Документ,
	|	ВремяРегистрацииДокументов.ВремяРегистрации КАК ВремяРегистрации
	|ИЗ
	|	РегистрСведений.ВремяРегистрацииДокументовПлановыхНачислений КАК ВремяРегистрацииДокументов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДокументыКОбработке КАК ДокументыКОбработке
	|		ПО ВремяРегистрацииДокументов.Документ = ДокументыКОбработке.Регистратор
	|ГДЕ
	|	ВремяРегистрацииДокументов.Проведен
	|
	|УПОРЯДОЧИТЬ ПО
	|	Документ,
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтарыйБухучет.Период КАК Период,
	|	СтарыйБухучет.Регистратор КАК Регистратор,
	|	СтарыйБухучет.Сотрудник КАК Сотрудник,
	|	СтарыйБухучет.Организация КАК Организация,
	|	СтарыйБухучет.Начисление КАК Начисление,
	|	СтарыйБухучет.ДокументОснование КАК ДокументОснование,
	|	СтарыйБухучет.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	СтарыйБухучет.СтатьяФинансирования КАК СтатьяФинансирования,
	|	СтарыйБухучет.СтатьяРасходов КАК СтатьяРасходов,
	|	СтарыйБухучет.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	СтарыйБухучет.Используется КАК Используется,
	|	СтарыйБухучет.ДействуетДо КАК ДействуетДо
	|ИЗ
	|	РегистрСведений.УдалитьБухучетПлановыхНачислений КАК СтарыйБухучет
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДокументыКОбработке КАК ДокументыКОбработке
	|		ПО СтарыйБухучет.Регистратор = ДокументыКОбработке.Регистратор
	|
	|УПОРЯДОЧИТЬ ПО
	|	Регистратор";
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	Если РезультатЗапроса[2].Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ВремяРегистрацииДокументов = Новый Соответствие;
	Выборка = РезультатЗапроса[1].Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Документ") Цикл
		ВремяРегистрацииДокумента = Новый Соответствие;
		Пока Выборка.СледующийПоЗначениюПоля("Дата") Цикл
			ВремяРегистрацииСотрудников = Новый Соответствие;
			Пока Выборка.Следующий() Цикл
				ВремяРегистрацииСотрудников.Вставить(Выборка.Сотрудник, Выборка.ВремяРегистрации);
			КонецЦикла;
			ВремяРегистрацииДокумента.Вставить(Выборка.Дата, ВремяРегистрацииСотрудников);
		КонецЦикла;
		ВремяРегистрацииДокументов.Вставить(Выборка.Документ, ВремяРегистрацииДокумента);
	КонецЦикла;

	Выборка = РезультатЗапроса[2].Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Регистратор") Цикл
		
		НаборЗаписей = РегистрыСведений.БухучетНачисленийСотрудников.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
		Пока Выборка.Следующий() Цикл
			НоваяЗапись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
		КонецЦикла;
		
		Если ТипЗнч(Выборка.Регистратор) = Тип("ДокументСсылка.БухучетНачисленийСотрудников") Тогда
			СотрудникиДаты = НаборЗаписей.Выгрузить(,"Сотрудник,Период");
			СотрудникиДаты.Колонки.Период.Имя = "ДатаСобытия";
			Движения = Новый Массив;
			Движения.Добавить(НаборЗаписей);
			ЗарплатаКадрыРасширенный.УстановитьВремяРегистрацииДокумента(Движения, СотрудникиДаты, Выборка.Регистратор);
		Иначе
			ВремяРегистрацииДокумента = ВремяРегистрацииДокументов[Выборка.Регистратор];
			Если ВремяРегистрацииДокумента <> Неопределено Тогда
				ПроверитьВремяРегистрацииДокумента(ВремяРегистрацииДокумента, НаборЗаписей);
				НаборЗаписей.ДополнительныеСвойства.Вставить("ВремяРегистрацииДокумента", ВремяРегистрацииДокумента);
			КонецЕсли;
		КонецЕсли;
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей,,Истина);
		
	КонецЦикла;

КонецПроцедуры

Процедура ПроверитьВремяРегистрацииДокумента(ВремяРегистрацииДокумента, НаборЗаписей)

	Для каждого ЗаписьРегистра Из НаборЗаписей Цикл
		ДатаСобытия = НачалоДня(ЗаписьРегистра.Период);
		ВремяРегистрацииСотрудников = ВремяРегистрацииДокумента.Получить(ДатаСобытия);
		Если ЗначениеЗаполнено(ВремяРегистрацииСотрудников) Тогда
			ВремяРегистрации = ВремяРегистрацииСотрудников.Получить(ЗаписьРегистра.Сотрудник);
			Если Не ЗначениеЗаполнено(ВремяРегистрации) Тогда
				ВремяРегистрацииСотрудников.Вставить(ЗаписьРегистра.Сотрудник, ДатаСобытия + 1);
			КонецЕсли;
		Иначе
			ВремяРегистрацииСотрудников = Новый Соответствие;
			ВремяРегистрацииСотрудников.Вставить(ЗаписьРегистра.Сотрудник, ДатаСобытия + 1);
			ВремяРегистрацииДокумента.Вставить(ДатаСобытия, ВремяРегистрацииСотрудников);
		КонецЕсли;
	
	КонецЦикла;

КонецПроцедуры

Процедура ОбновитьСтратегиюБухучетаУдержаний() Экспорт

	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Удержания.Ссылка КАК Ссылка
	|ИЗ
	|	ПланВидовРасчета.Удержания КАК Удержания
	|ГДЕ
	|	Удержания.КатегорияУдержания <> ЗНАЧЕНИЕ(Перечисление.КатегорииУдержаний.ВознаграждениеПлатежногоАгента)
	|	И Удержания.КатегорияУдержания <> ЗНАЧЕНИЕ(Перечисление.КатегорииУдержаний.ИсполнительныйЛист)
	|	И Удержания.СтратегияОтраженияВУчете = ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоФактическимНачислениям)
	|	И Удержания.ТребуетсяРасчетБазы";
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		УдержаниеОбъект = Выборка.Ссылка.ПолучитьОбъект();
		УдержаниеОбъект.СтратегияОтраженияВУчете = Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазовымРасчетам;
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(УдержаниеОбъект);
	КонецЦикла;

КонецПроцедуры

Процедура ПроверитьОбновитьСтратегиюБухучетаУдержаний(ПараметрыОбновления = Неопределено) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Удержания.Ссылка КАК Ссылка,
	|	Удержания.КатегорияУдержания КАК КатегорияУдержания,
	|	Удержания.ТребуетсяРасчетБазы КАК ТребуетсяРасчетБазы
	|ИЗ
	|	ПланВидовРасчета.Удержания КАК Удержания
	|ГДЕ
	|	Удержания.СтратегияОтраженияВУчете = ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПустаяСсылка)";
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	ОбработкаЗавершена = Истина;
	Пока Выборка.Следующий() Цикл
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "ПланВидовРасчета.Удержания", "Ссылка", Выборка.Ссылка) Тогда
			ОбработкаЗавершена = Ложь;
			Продолжить;
		КонецЕсли;
		УдержаниеОбъект = Выборка.Ссылка.ПолучитьОбъект();
		Если Выборка.ТребуетсяРасчетБазы Или Выборка.КатегорияУдержания = Перечисления.КатегорииУдержаний.ИсполнительныйЛист
			Или Выборка.КатегорияУдержания = Перечисления.КатегорииУдержаний.ВознаграждениеПлатежногоАгента Тогда
			УдержаниеОбъект.СтратегияОтраженияВУчете = Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазовымРасчетам;
		Иначе
			УдержаниеОбъект.СтратегияОтраженияВУчете = Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоФактическимНачислениям;
		КонецЕсли;
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(УдержаниеОбъект);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
	КонецЦикла;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", ОбработкаЗавершена);


КонецПроцедуры

Процедура ПеренестиНастройкиБухучетаДоговоровОпеки(ПараметрыОбновления = Неопределено) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
	|	СтарыеНастройки.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВТРегистраторы
	|ИЗ
	|	РегистрСведений.УсловияДоговораОпеки КАК СтарыеНастройки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.БухучетДоговоровГПХ КАК НовыеНастройки
	|		ПО СтарыеНастройки.Договор = НовыеНастройки.ДоговорАкт
	|ГДЕ
	|	НовыеНастройки.НомерСтроки ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтарыеНастройки.Регистратор КАК Регистратор,
	|	СтарыеНастройки.НомерСтроки КАК НомерСтроки,
	|	СтарыеНастройки.Договор КАК ДоговорАкт,
	|	СтарыеНастройки.Подразделение КАК ПодразделениеУчетаЗатрат,
	|	СтарыеНастройки.УдалитьСтатьяФинансирования КАК СтатьяФинансирования,
	|	СтарыеНастройки.УдалитьСтатьяРасходов КАК СтатьяРасходов,
	|	СтарыеНастройки.УдалитьСпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете
	|ИЗ
	|	РегистрСведений.УсловияДоговораОпеки КАК СтарыеНастройки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРегистраторы КАК Регистраторы
	|		ПО СтарыеНастройки.Регистратор = Регистраторы.Регистратор
	|
	|УПОРЯДОЧИТЬ ПО
	|	Регистратор,
	|	НомерСтроки"; 
	
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, " ПЕРВЫЕ 1000", "");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
	Иначе
		
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.СледующийПоЗначениюПоля("Регистратор") Цикл
			
			Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрСведений.БухучетДоговоровГПХ.НаборЗаписей", "Регистратор", Выборка.Регистратор) Тогда
				Продолжить;
			КонецЕсли;
			
			НаборЗаписей = РегистрыСведений.БухучетДоговоровГПХ.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
			ИдентификаторСтроки = 0;
			Пока Выборка.Следующий() Цикл
				ЗаписьНабора = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(ЗаписьНабора, Выборка);
				ЗаписьНабора.ИдентификаторСтроки 	= ИдентификаторСтроки;
				ЗаписьНабора.ОблагаетсяЕНВД 		= Ложь;
				ЗаписьНабора.ДоляРаспределения 		= 1;
				ИдентификаторСтроки = ИдентификаторСтроки + 1;
			КонецЦикла;
			
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
			ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
			
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

Процедура ПеренестиНастройкиБухучетаДоговоровГПХ(ПараметрыОбновления = Неопределено) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
	|	СтарыеНастройки.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВТРегистраторы
	|ИЗ
	|	РегистрСведений.ПлановыеНачисленияПоДоговорам КАК СтарыеНастройки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.БухучетДоговоровГПХ КАК НовыеНастройки
	|		ПО СтарыеНастройки.ДоговорАкт = НовыеНастройки.ДоговорАкт
	|ГДЕ
	|	НовыеНастройки.НомерСтроки ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтарыеНастройки.Регистратор КАК Регистратор,
	|	СтарыеНастройки.НомерСтроки КАК НомерСтроки,
	|	СтарыеНастройки.ДоговорАкт КАК ДоговорАкт,
	|	СтарыеНастройки.Подразделение КАК ПодразделениеУчетаЗатрат,
	|	СтарыеНастройки.УдалитьСтатьяФинансирования КАК СтатьяФинансирования,
	|	СтарыеНастройки.УдалитьСтатьяРасходов КАК СтатьяРасходов,
	|	СтарыеНастройки.УдалитьСпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	СтарыеНастройки.Сумма КАК Сумма,
	|	СтарыеНастройки.УдалитьСуммаЕНВД КАК СуммаЕНВД
	|ИЗ
	|	РегистрСведений.ПлановыеНачисленияПоДоговорам КАК СтарыеНастройки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРегистраторы КАК Регистраторы
	|		ПО СтарыеНастройки.Регистратор = Регистраторы.Регистратор
	|
	|УПОРЯДОЧИТЬ ПО
	|	Регистратор,
	|	НомерСтроки";
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, " ПЕРВЫЕ 1000", "");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
	Иначе
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
		ПеренестиБухучетДоговоровГПХ(РезультатЗапроса, ПараметрыОбновления);
	КонецЕсли;

КонецПроцедуры

Процедура ПеренестиНастройкиБухучетаВыплатПоДоговорамГПХ(ПараметрыОбновления = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
	|	СтарыеНастройки.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВТРегистраторы
	|ИЗ
	|	РегистрСведений.УсловияДоговораГПХ КАК СтарыеНастройки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.БухучетДоговоровГПХ КАК НовыеНастройки
	|		ПО СтарыеНастройки.Договор = НовыеНастройки.ДоговорАкт
	|ГДЕ
	|	НовыеНастройки.НомерСтроки ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтарыеНастройки.Регистратор КАК Регистратор,
	|	СтарыеНастройки.НомерСтроки КАК НомерСтроки,
	|	СтарыеНастройки.Договор КАК ДоговорАкт,
	|	СтарыеНастройки.Подразделение КАК ПодразделениеУчетаЗатрат,
	|	СтарыеНастройки.УдалитьСтатьяФинансирования КАК СтатьяФинансирования,
	|	СтарыеНастройки.УдалитьСтатьяРасходов КАК СтатьяРасходов,
	|	СтарыеНастройки.УдалитьСпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	СтарыеНастройки.Сумма КАК Сумма,
	|	СтарыеНастройки.УдалитьСуммаЕНВД КАК СуммаЕНВД
	|ИЗ
	|	РегистрСведений.УсловияДоговораГПХ КАК СтарыеНастройки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРегистраторы КАК Регистраторы
	|		ПО СтарыеНастройки.Регистратор = Регистраторы.Регистратор";
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, " ПЕРВЫЕ 1000", "");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
	Иначе
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
		ПеренестиБухучетДоговоровГПХ(РезультатЗапроса, ПараметрыОбновления);
	КонецЕсли;

КонецПроцедуры

Процедура ПеренестиБухучетДоговоровГПХ(РезультатЗапроса, ПараметрыОбновления)
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Регистратор") Цикл
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрСведений.БухучетДоговоровГПХ.НаборЗаписей", "Регистратор", Выборка.Регистратор) Тогда
			Продолжить;
		КонецЕсли;
		
		НаборЗаписей = РегистрыСведений.БухучетДоговоровГПХ.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
		ИдентификаторСтроки = 0;
		Пока Выборка.Следующий() Цикл
			Если Выборка.СуммаЕНВД > 0 Тогда
				ЗаписьНабора = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(ЗаписьНабора, Выборка);
				ЗаписьНабора.ИдентификаторСтроки 	= ИдентификаторСтроки;
				ЗаписьНабора.ОблагаетсяЕНВД 		= Истина;
				ЗаписьНабора.ДоляРаспределения 		= Выборка.СуммаЕНВД;
				ИдентификаторСтроки = ИдентификаторСтроки + 1;
			КонецЕсли;
			Если Выборка.Сумма > Выборка.СуммаЕНВД Тогда
				ЗаписьНабора = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(ЗаписьНабора, Выборка);
				ЗаписьНабора.ИдентификаторСтроки 	= ИдентификаторСтроки;
				ЗаписьНабора.ОблагаетсяЕНВД 		= Ложь;
				Если Выборка.СуммаЕНВД > 0 Тогда
					ЗаписьНабора.ДоляРаспределения = Выборка.Сумма - Выборка.СуммаЕНВД;
				Иначе
					ЗаписьНабора.ДоляРаспределения = 1;
				КонецЕсли;
				ИдентификаторСтроки = ИдентификаторСтроки + 1;
			ИначеЕсли Выборка.Сумма = 0 Тогда
				ЗаписьНабора = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(ЗаписьНабора, Выборка);
				ЗаписьНабора.ИдентификаторСтроки 	= ИдентификаторСтроки;
				ЗаписьНабора.ОблагаетсяЕНВД 		= Ложь;
				ЗаписьНабора.ДоляРаспределения 		= 1;
			КонецЕсли;
		КонецЦикла;
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПравилаЗаменыСтатейФинансирования(ПараметрыОбновления = Неопределено) Экспорт

	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Статьи.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СтатьиФинансированияЗарплата КАК Статьи
	|ГДЕ
	|	Статьи.ПравилоЗамены = ЗНАЧЕНИЕ(Перечисление.ПравилаЗаменыУстаревшейАналитикиБухучета.ПустаяСсылка)
	|	И Статьи.ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1)";
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "Справочник.СтатьиФинансированияЗарплата", "Ссылка", Выборка.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		
		Объект = Выборка.Ссылка.ПолучитьОбъект();
		Объект.ПравилоЗамены = Перечисления.ПравилаЗаменыУстаревшейАналитикиБухучета.ПоНастройкамОрганизации;
		
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(Объект);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПравилаЗаменыСпособовОтражения(ПараметрыОбновления = Неопределено) Экспорт

	 Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СпособыОтражения.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СпособыОтраженияЗарплатыВБухУчете КАК СпособыОтражения
	|ГДЕ
	|	СпособыОтражения.ПравилоЗамены = ЗНАЧЕНИЕ(Перечисление.ПравилаЗаменыУстаревшейАналитикиБухучета.ПустаяСсылка)
	|	И СпособыОтражения.ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1)";
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "Справочник.СпособыОтраженияЗарплатыВБухУчете", "Ссылка", Выборка.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		
		Объект = Выборка.Ссылка.ПолучитьОбъект();
		Объект.ПравилоЗамены = Перечисления.ПравилаЗаменыУстаревшейАналитикиБухучета.ПоНастройкамОрганизации;
		
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(Объект);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#КонецОбласти


