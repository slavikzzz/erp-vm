#Область ПрограммныйИнтерфейс

// Возващает двоичные данные как пробразование по формату multipart/form-data.
//
// Параметры:
//  ПоляФормы - Массив из Структура:
//   * ИмяПоля  - Строка - Имя поля формы.
//   * ИмяФайла - Строка - Имя файла.
//   * Тип      - Строка - Тип содержимого.
//   * Тело     - Строка - Содержание поля.
// Возвращаемое значение:
//  Структура - представление вебзапроса типа multipart/form-data:
//   * ДвоичныеДанные - ДвоичныеДанные - Результат преобразования.
//   * Размер         - Число          - Размер данных.
//   * Разделитель    - Строка         - Разделитель полей.
Функция ДвоичныеДанныеPOSTЗапросаКакФорма(ПоляФормы) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура();
	ВозвращаемоеЗначение.Вставить("ДвоичныеДанные", Неопределено);
	ВозвращаемоеЗначение.Вставить("Размер",         0);
	ВозвращаемоеЗначение.Вставить("Разделитель",    "");
	
	Разделитель = СтрЗаменить(Строка(Новый УникальныйИдентификатор()), "-", "");
	
	НаборСтрок = Новый Массив;
	РазделительСтрок = Символы.ВК + Символы.ПС;
	
	ТекущийИндекс = 0;
	Для Каждого ПолеФормы Из ПоляФормы Цикл
		
		ОписаниеСодержимого = СтрШаблон(
			"Content-Disposition: form-data; name=""%1""; filename=""%2""",
			ПолеФормы.ИмяПоля,
			ПолеФормы.ИмяФайла);
		ОписаниеТипа = СтрШаблон("Content-Type: %1", ПолеФормы.Тип);
		
		НаборСтрок.Добавить("--" + Разделитель);
		НаборСтрок.Добавить(ОписаниеСодержимого);
		НаборСтрок.Добавить(ОписаниеТипа);
		НаборСтрок.Добавить("");
		НаборСтрок.Добавить(ПолеФормы.Тело);
		Если ТекущийИндекс = ПоляФормы.ВГраница() Тогда
			НаборСтрок.Добавить("--" + Разделитель + "--");
		Иначе
			НаборСтрок.Добавить("--" + Разделитель);
		КонецЕсли;
		
		ТекущийИндекс = ТекущийИндекс + 1;
		
	КонецЦикла;
	
	ВозвращаемоеЗначение.ДвоичныеДанные = ПолучитьДвоичныеДанныеИзСтроки(СтрСоединить(НаборСтрок,РазделительСтрок));
	
	ВозвращаемоеЗначение.Размер      = ВозвращаемоеЗначение.ДвоичныеДанные.Размер();
	ВозвращаемоеЗначение.Разделитель = Разделитель;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИмяСобытияИзмененОбъект(ИмяПодсистемы, ИсточникИмениОбъекта = Неопределено) Экспорт
	
	Если ИсточникИмениОбъекта = Неопределено Тогда
		ИмяОбъекта = "";
	ИначеЕсли ТипЗнч(ИсточникИмениОбъекта) = Тип("ФормаКлиентскогоПриложения") Тогда
		ИмяОбъекта = ОбщегоНазначенияИСКлиентСервер.ИмяОбъектаИзИмениФормы(ИсточникИмениОбъекта);
	Иначе
		ИмяОбъекта = ИсточникИмениОбъекта;
	КонецЕсли;
	
	Возврат
		ОбщийПрефиксСобытияОповещения(ИмяПодсистемы)
		+ ПрефиксСобытияИзмененОбъект() + РазделительСобытияОповещения()
		+ ИмяОбъекта;
	
КонецФункции

Функция ИмяСобытияИзмененоСостояние(ИмяПодсистемы) Экспорт
	
	Возврат
		ОбщийПрефиксСобытияОповещения(ИмяПодсистемы)
		+ ПрефиксСобытияИзмененоСостояние();
	
КонецФункции

Функция ИмяСобытияВыполненОбмен(ИмяПодсистемы) Экспорт
	
	Возврат
		ОбщийПрефиксСобытияОповещения(ИмяПодсистемы)
		+ ПрефиксСобытияВыполненОбмен();
	
КонецФункции

Функция ИмяСвойстваОбновлятьСтатусВФормахДокументов(ИмяПодсистемы) Экспорт
	
	Возврат СтрШаблон("ОбновлятьСтатус%1ВФормахДокументов", ИмяПодсистемы);
	
КонецФункции

Функция ОбщийПрефиксСобытияОповещения(ИмяПодсистемы) Экспорт
	
	Возврат 
		"ИнтеграцияИС_СобытиеОповещения" + РазделительСобытияОповещения()
		+ ИмяПодсистемы + РазделительСобытияОповещения();
	
КонецФункции

Функция ПрефиксСобытияИзмененОбъект() Экспорт
	
	Возврат "ИзмененОбъект";
	
КонецФункции

Функция ПрефиксСобытияИзмененоСостояние() Экспорт
	
	Возврат "ИзмененоСостояние";
	
КонецФункции

Функция ПрефиксСобытияВыполненОбмен() Экспорт
	
	Возврат "ВыполненОбмен";

КонецФункции

Функция РазделительСобытияОповещения() Экспорт
	
	Возврат "#";
	
КонецФункции

#КонецОбласти