
#Область ПрограммныйИнтерфейс

// Возвращает ставку налога УСН, указанную в НК РФ.
//
// Параметры:
//  ДоходыМинусРасходы - Булево - признак возврата ставки для объекта налогообложения "Доходы минус расходы".
//
// Возвращаемое значение:
//  Число - п. 1 ст. 346.20 НК РФ или п. 2 ст. 346.20 НК РФ в зависимости от параметра.
//
Функция НалоговаяСтавкаПоУмолчанию(ДоходыМинусРасходы = Ложь) Экспорт
	
	Ставки = УчетУСНКлиентСервер.НалоговыеСтавкиПоУмолчанию();
	Возврат ?(ДоходыМинусРасходы, Ставки.СтавкаУСНДоходыМинусРасходы, Ставки.СтавкаУСНДоходы);
	
КонецФункции

// Возвращает повышенную ставку налога УСН, указанную в НК РФ.
//
// Параметры:
//  ДоходыМинусРасходы - Булево - признак возврата ставки для объекта налогообложения "Доходы минус расходы".
//
// Возвращаемое значение:
//  Число - п. 1.1 ст. 346.20 НК РФ или п. 2.1 ст. 346.20 НК РФ в зависимости от параметра.
//
Функция НалоговаяСтавкаПовышенная(ДоходыМинусРасходы = Ложь) Экспорт
	
	Ставки = УчетУСНКлиентСервер.НалоговыеСтавкиПоУмолчанию();
	Возврат ?(ДоходыМинусРасходы, Ставки.СтавкаУСНДоходыМинусРасходыПовышенная, Ставки.СтавкаУСНДоходыПовышенная);
	
КонецФункции

// Возвращает границы переходного периода, на который можно получить патент при переходе с ЕНВД
// не дожидаясь регионального закона
//
// Возвращаемое значение:
//  Структура:
//     * Начало - Дата
//     * Конец  - Дата
//
Функция ПереходныйПериод() Экспорт
	
	Период = Новый Структура;
	Период.Вставить("Начало", Дата(2021, 1, 1));
	Период.Вставить("Конец",  Дата(2021, 3, 31));
	
	Возврат Период;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает шаблон строки ссылки на закон субъекта РФ,
// раздел "Особенности регионального законодательства"
//
// Возвращаемое значение:
//  Строка
//
Функция ШаблонСсылкиНаЗаконРегиона() Экспорт
	
	Возврат "https://www.nalog.ru/rn%1/taxation/taxes/patent/#title22";
	
КонецФункции

// Возвращает шаблон строки ссылки на Классификатор видов предпринимательской деятельности
//
// Возвращаемое значение:
//  Строка
//
Функция ШаблонСсылкиНаКлассификаторВидовДеятельности() Экспорт
	
	Возврат "https://www.nalog.ru/rn%1/taxation/taxes/patent/#title23";
	
КонецФункции

// Возвращает виды деятельности, которые можно применять при переходе с ЕНВД не дожидаясь регионального закона
//
// Возвращаемое значение:
//   Соответствие
//
Функция КодыВидовДеятельностиПереходногоПериода() Экспорт
	
	КодыФедеральныхВидовДеятельности = Новый Соответствие;
	КодыФедеральныхВидовДеятельности.Вставить("94",
		"Деятельность стоянок для транспортных средств");
	КодыФедеральныхВидовДеятельности.Вставить("95",
		"Розничная торговля, осуществляемая через объекты стационарной торговой сети с площадью торгового зала свыше 50 квадратных метров, но не более 150 квадратных метров по каждому объекту организации торговли");
	КодыФедеральныхВидовДеятельности.Вставить("96",
		"Оказание услуг общественного питания, осуществляемых через объекты организации общественного питания с площадью зала обслуживания посетителей свыше 50 квадратных метров, но не более 150 квадратных метров по каждому объекту организации общественного питания");
	КодыФедеральныхВидовДеятельности.Вставить("97",
		"Ремонт, техническое обслуживание автотранспортных и мототранспортных средств, мотоциклов, машин и оборудования, мойки транспортных средств, полирования и предоставления аналогичных услуг");
	
	Возврат КодыФедеральныхВидовДеятельности;
	
КонецФункции

// Возвращает подробное описание ставки налога УСН с учетом всех лимитов.
//
// Параметры:
//  ПрименяетсяПовышеннаяСтавка - Булево - признак применения повышенной ставки.
//  Период - Дата - дата, на которую необходимо определить используемые лимиты.
//
// Возвращаемое значение:
//  Строка - описание использование ставки налога УСН.
//
Функция УточнениеПримененияСтавкиНалогаУСН(ПрименяетсяПовышеннаяСтавка, Период) Экспорт
	
	Подсказка = "";
	
	Если Период < УчетУСНКлиентСервер.ДатаНачалаПрогрессивнойШкалы() Тогда
		Возврат Подсказка;
	КонецЕсли;
	
	ЛимитДоходовУСН = КонтрольПраваПримененияСпецрежимаКлиентСервер.ГраницаДоходовДляПримененияПовышеннойСтавкиУСН(Период);
	ЛимитДоходовУСН = Формат(Окр(ЛимитДоходовУСН / 1000000, 2), "ЧГ=;");
	ЛимитДоходов = КонтрольПраваПримененияСпецрежимаКлиентСервер.ГраницаДоходовДляПримененияОсновнойСтавкиУСН(Период);
	ЛимитДоходов = Формат(Окр(ЛимитДоходов / 1000000, 2), "ЧГ=;");
	ЛимитЧисленностиУСН = Формат(
		КонтрольПраваПримененияСпецрежимаКлиентСервер.ГраницаЧисленностиРаботниковДляПримененияПовышеннойСтавкиУСН(Период),
			"ЧДЦ=0; ЧГ=;");
	ЛимитЧисленности = Формат(
		КонтрольПраваПримененияСпецрежимаКлиентСервер.ГраницаЧисленностиРаботниковДляПримененияОсновнойСтавкиУСН(Период),
			"ЧДЦ=0; ЧГ=;");
	Если ПрименяетсяПовышеннаяСтавка Тогда
		Подсказка = НСтр("ru = 'Применяется повышенная налоговая ставка. Действует с начала квартала, в котором доходы превысили %1%2млн.%2руб. либо средняя численность наемных работников превысила %3%2человек и до достижения суммы доходов %4%2млн.%2руб. при средней численности наемных работников не более %5%2человек.';
						|en = 'An increased tax rate applies. It has been in effect since the beginning of the quarter in which revenues exceeded %1%2 million %2 rubles or the average number of employees exceeded %3%2 people and before reaching the amount of income%4%2 million %2rubles with an average number of employees no more than%5%2 people.'");
		Подсказка = СтрШаблон(Подсказка, ЛимитДоходов, Символы.НПП, ЛимитЧисленности, ЛимитДоходовУСН, ЛимитЧисленностиУСН);
	Иначе
		Подсказка = НСтр("ru = 'Применяется основная налоговая ставка. Действует до достижения суммы доходов %1%2млн.%2руб. при средней численности наемных работников не более %3%2человек.';
						|en = 'The basic tax rate applies. It is in effect until the amount of income %1%2 million %2 rubles is reached with an average number of employees no more than %3%2 people.'");
		Подсказка = СтрШаблон(Подсказка, ЛимитДоходов, Символы.НПП, ЛимитЧисленности);
	КонецЕсли;
	
	Возврат Подсказка;
	
КонецФункции

#КонецОбласти