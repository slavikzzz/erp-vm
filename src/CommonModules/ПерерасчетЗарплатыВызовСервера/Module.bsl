#Область СлужебныйПрограммныйИнтерфейс

// Возвращает структуру, содержащую месяц открытого периода и список документов
// начисления зарплаты в режиме доначисления.
//
// Параметры:
//		Организация		- СправочникСсылка.Организации
//		МесяцНачисления	- Дата
//
// Возвращаемое значение:
//		Структура:
//			* МесяцНачисления	- Дата
//			* Документы			- Массив
//
Функция ДокументыПерерасчета(Организация, МесяцНачисления, Отбор = Неопределено) Экспорт
	
	ОткрытыйПериод = ПерерасчетЗарплаты.ОткрытыйПериодНачисленияЗарплаты(Организация, МесяцНачисления);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("МесяцНачисления", ОткрытыйПериод);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	НачислениеЗарплаты.Ссылка
		|ИЗ
		|	Документ.НачислениеЗарплаты КАК НачислениеЗарплаты
		|ГДЕ
		|	НачислениеЗарплаты.МесяцНачисления = &МесяцНачисления
		|	И НачислениеЗарплаты.Организация = &Организация
		|	И НачислениеЗарплаты.РежимДоначисления
		|	И НЕ НачислениеЗарплаты.ПометкаУдаления
		|	И &Отбор
		|
		|УПОРЯДОЧИТЬ ПО
		|	НачислениеЗарплаты.Проведен УБЫВ,
		|	НачислениеЗарплаты.Дата";
	
	Если Отбор = Неопределено Тогда
		Запрос.УстановитьПараметр("Отбор", Истина);
	Иначе
		ЧастиОтбора = Новый Массив;
		Для Каждого КлючИЗначение Из Отбор Цикл
			ЧастиОтбора.Добавить(СтрШаблон("НачислениеЗарплаты.%1 = &%1", КлючИЗначение.Ключ));
			Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
		КонецЦикла;
		Разделитель = Символы.ПС + "И ";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Отбор", СтрСоединить(ЧастиОтбора, Разделитель));
	КонецЕсли;
	
	ДоступныеДоначисления = Новый Структура;
	ДоступныеДоначисления.Вставить("МесяцНачисления", ОткрытыйПериод);
	ДоступныеДоначисления.Вставить("Документы", Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
	Возврат ДоступныеДоначисления;
	
КонецФункции

#КонецОбласти
