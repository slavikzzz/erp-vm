
#Область ПрограммныйИнтерфейс

#Область Форма

// Обрабатывает нажатие гиперссылки заказа переработчика
// 
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения
// 	НавигационнаяСсылкаФорматированнойСтроки - Строка
// 	СтандартнаяОбработка - Булево
//
Процедура НадписьЗаказПереработчикуОбработкаНавигационнойСсылки(Форма, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка) Экспорт
	
	Объект = Форма.Объект; // ДокументОбъект.ЭтапПроизводства2_2 - объект формы
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "#СоздатьЗаказ"
		Или НавигационнаяСсылкаФорматированнойСтроки = "#СоздатьЗаказ2_5" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ТекстВопроса = Неопределено;
		Если Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыЭтаповПроизводства2_2.Формируется")
			ИЛИ Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыЭтаповПроизводства2_2.Сформирован") Тогда
			
			ТекстВопроса = НСтр("ru = 'Для создания заказа необходимо установить статус ""К выполнению"".
										|Изменить статус этапа и создать заказ?';
										|en = 'To create an order, set the ""For release"" status.
										|Change the stage status and create an order?'");
										
			СписокКнопок = Новый СписокЗначений;
			СписокКнопок.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Изменить статус и создать заказ';
																|en = 'Change status and create order'"));
			СписокКнопок.Добавить(КодВозвратаДиалога.Отмена);
			
		ИначеЕсли НЕ ЗначениеЗаполнено(Объект.Ссылка) ИЛИ Форма.Модифицированность Тогда
			
			ТекстВопроса = НСтр("ru = 'Для создания заказа необходимо записать документ.
										|Записать документ и создать заказ?';
										|en = 'You should save the document to create an order.
										|Save the document and create an order?'");
			
			СписокКнопок = Новый СписокЗначений;
			СписокКнопок.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Записать и создать заказ';
																|en = 'Save and create order'"));
			СписокКнопок.Добавить(КодВозвратаДиалога.Отмена);
			
		КонецЕсли; 
		
		Если ТекстВопроса <> Неопределено Тогда
			
			ОписаниеОповещения =
				Новый ОписаниеОповещения(
					"ЗаписатьОбъектИСоздатьЗаказПереработчику",
					Форма,
					НавигационнаяСсылкаФорматированнойСтроки);
			
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, СписокКнопок);
			
		Иначе
			СоздатьЗаказПереработчику(Объект, НавигационнаяСсылкаФорматированнойСтроки);
		КонецЕсли; 
		
	КонецЕсли;
	
КонецПроцедуры

// Открывает форму нового заказа переработчику, созданного на основании этапа производства
// 
// Параметры:
// 	Объект - ДокументОбъект.ЭтапПроизводства2_2 - объект формы документа
// 	Гиперссылка - Строка - гиперссылка
//
Процедура СоздатьЗаказПереработчику(Объект, Гиперссылка) Экспорт
	
	ПараметрыФормы = Новый Структура("Основание", Объект.Ссылка);
	Если Гиперссылка = "#СоздатьЗаказ2_5" Тогда
		ОткрытьФорму("Документ.ЗаказПереработчику2_5.ФормаОбъекта", ПараметрыФормы);
	//++ Устарело_Переработка24
	Иначе
		ОткрытьФорму("Документ.ЗаказПереработчику.ФормаОбъекта", ПараметрыФормы);
	//-- Устарело_Переработка24
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти