
// Вызываются процедуры и функции общих модулей:
//	- РасчетСебестоимостиПрикладныеАлгоритмы.
//	- РасчетСебестоимостиПроизводство

#Область ПрограммныйИнтерфейс

// Получает данные для построения расшифровки базы распределения материалов и работ.
//
// Параметры:
//		Период - Дата - период расчета
//		МассивОрганизаций - СправочникСсылка.Организации - массив организаций;
//		Регистратор - ДокументСсылка.РаспределениеПроизводственныхЗатрат - документ распределения, для которого строится расшифровка.
//
// Возвращаемое значение:
//		МенеджерВременныхТаблиц - таблицы для построения отчета.
//
Функция ДанныеДляРасшифровкиРаспределенияНоменклатурыНаПроизводство(Период, МассивОрганизаций, Регистратор) Экспорт
	
	ПараметрыРасчета = РасчетСебестоимостиПроизводство.ИнициализироватьПараметрыРасшифровкиРаспределения(
		Период,
		РасчетСебестоимостиПрикладныеАлгоритмы.СвязиОрганизацийПоСхемеИнтеркампани(Период, МассивОрганизаций),
		Регистратор);
	
	РаспределениеНоменклатурыНаПроизводство(ПараметрыРасчета);
	
	Возврат ПараметрыРасчета.МенеджерВременныхТаблиц;
	
КонецФункции

#Область ЭтапыРасчета

// Этап 4 (Контекст: "РаспределениеНоменклатурыНаПроизводство")
// ПУ 2.1: РассчитатьРаспределениеМатериаловИРабот(), область РаспределениеМатериаловИРабот.
//
// Параметры:
//	ПараметрыРасчета - Структура - параметры расчета себестоимости
//
Процедура РаспределениеНоменклатурыНаПроизводство(ПараметрыРасчета) Экспорт
	
	Если ПараметрыРасчета.РежимЗакрытияМесяца = Перечисления.РежимыЗакрытияМесяца.ПредварительноеЗакрытие Тогда
		Возврат;
	КонецЕсли;
	
	РасчетСебестоимостиПротоколРасчета.НачалоЭтапаРасчета(ПараметрыРасчета, "РаспределениеНоменклатурыНаПроизводство");
	
	// 1. Инициализация структуры данных для расчета
	//	- формирует структуру ПараметрыРасчета.РаспределениеПартий.
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьРаспределениеПартий(
		ПараметрыРасчета,
		ТаблицаДляРаспределенияНоменклатурыНаПроизводство(ПараметрыРасчета),
		ОписаниеЦепочекРаспределенияНоменклатурыНаПроизводство(ПараметрыРасчета),
		ОписаниеДвиженийРаспределенияНоменклатурыНаПроизводство(ПараметрыРасчета),
		ОписаниеНезаписываемыхРаспределенияНоменклатурыНаПроизводство(ПараметрыРасчета));
		
	// 2. Получение исходных данных для расчета
	// 	- формирует временную таблицу Данные.
	ПолучитьДанныеДляРаспределенияНоменклатурыНаПроизводство(ПараметрыРасчета);
	
	// 3. Получение цепочек из исходных данных
	// 	- формирует временные таблицы Источники и Приемники.
	РасчетСебестоимостиПрикладныеАлгоритмы.ПостроитьЦепочкиДвижений(ПараметрыРасчета);
	
	// 4. Заполнение партий в цепочках
	// 	- заполняет таблицу ПараметрыРасчета.РаспределениеПартий.РасчетныеПартии.
	РасчетСебестоимостиПрикладныеАлгоритмы.РассчитатьПартииПоЦепочкам(ПараметрыРасчета);
	
	// 5. Помещение данных в таблицу-приемник и очистка вспомогательных временных таблиц.
	// 	- добавляет записи во временную таблицу ВтПроизводственныеЗатратыСебестоимостьТоваров.
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗавершитьРаспределениеПартий(ПараметрыРасчета);
	
КонецПроцедуры

// Этап 5 (Контекст: "РаспределениеНоменклатурыНаВыпуск")
// ПУ 2.1: РассчитатьПартииНЗП(), область РасчетПартийНезавершенногоПроизводства.
//
// Параметры:
//	ПараметрыРасчета - Структура - параметры расчета себестоимости
//
Процедура РаспределениеНоменклатурыНаВыпуск(ПараметрыРасчета) Экспорт
	
	Если ПараметрыРасчета.РежимЗакрытияМесяца = Перечисления.РежимыЗакрытияМесяца.ПредварительноеЗакрытие Тогда
		Возврат;
	КонецЕсли;
	
	РасчетСебестоимостиПротоколРасчета.НачалоЭтапаРасчета(ПараметрыРасчета, "РаспределениеНоменклатурыНаВыпуск");
	
	// 1. Инициализация структуры данных для расчета
	//	- формирует структуру ПараметрыРасчета.РаспределениеПартий.
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьРаспределениеПартий(
		ПараметрыРасчета,
		ТаблицаДляРаспределенияНоменклатурыНаВыпуск(ПараметрыРасчета),
		ОписаниеЦепочекРаспределенияНоменклатурыНаВыпуск(ПараметрыРасчета),
		ОписаниеДвиженийРаспределенияНоменклатурыНаВыпуск(ПараметрыРасчета),
		ОписаниеНезаписываемыхРаспределенийНоменклатурыНаВыпуск(ПараметрыРасчета));
	
	// 2. Получение исходных данных для расчета
	// 	- формирует временную таблицу Данные.
	ПолучитьДанныеДляРаспределенияНоменклатурыНаВыпуск(ПараметрыРасчета);
	
	// 3. Получение цепочек из исходных данных
	// 	- формирует временные таблицы Источники и Приемники.
	РасчетСебестоимостиПрикладныеАлгоритмы.ПостроитьЦепочкиДвижений(ПараметрыРасчета);
	
	// 4. Заполнение партий в цепочках
	// 	- заполняет таблицу ПараметрыРасчета.РаспределениеПартий.РасчетныеПартии.
	РасчетСебестоимостиПрикладныеАлгоритмы.РассчитатьПартииПоЦепочкам(ПараметрыРасчета);
	
	// 5. Помещение данных в таблицу-приемник и очистка вспомогательных временных таблиц.
	// 	- добавляет записи в регистр СебестоимостьТоваров
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗавершитьРаспределениеПартий(ПараметрыРасчета);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПроцедурыЭтапа4_РаспределениеНоменклатурыНаПроизводство

// Инициализация данных

Функция ПоляПотребленийРаспределенияНоменклатурыНаПроизводство(ПараметрыРасчета)
	
	Возврат "Регистратор, ВидЗапасов";
	
КонецФункции

Функция ОписаниеЦепочекРаспределенияНоменклатурыНаПроизводство(ПараметрыРасчета)
	
	ОписаниеЦепочек = Новый Соответствие;
	ПоляПотреблений = ПоляПотребленийРаспределенияНоменклатурыНаПроизводство(ПараметрыРасчета);
	
	Партия                  = Перечисления.ТипыЗаписейПартий.Партия;
	ПотреблениеАвто         = Перечисления.ТипыЗаписейПартий.ПотреблениеАвто;
	ПеремещениеАвто         = Перечисления.ТипыЗаписейПартий.ПеремещениеАвто;
	
	// Потребление <- (Партия)
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьОписаниеПриемника(ОписаниеЦепочек, ПотреблениеАвто, ПоляПотреблений);
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьОписаниеИсточника(ОписаниеЦепочек, ПотреблениеАвто, Партия, ПоляПотреблений);
	
	// Перемещение <- (Потребление)
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьОписаниеПриемника(ОписаниеЦепочек, ПеремещениеАвто, "Регистратор, ВидЗапасов, Партия");
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьОписаниеИсточника(ОписаниеЦепочек, ПеремещениеАвто, ПотреблениеАвто, "Регистратор, ВидЗапасов, КорПартия");
	
	Возврат ОписаниеЦепочек;
	
КонецФункции

Функция ОписаниеДвиженийРаспределенияНоменклатурыНаПроизводство(ПараметрыРасчета)
	
	КолонкиТаблицыРаспределения = ТаблицаДляРаспределенияНоменклатурыНаПроизводство(ПараметрыРасчета).Колонки;
	
	ОписаниеДвижений = РасчетСебестоимостиПрикладныеАлгоритмы.ОписаниеДвижений();
	ОписаниеДвижений.Вставить("Контекст", "РаспределениеНоменклатурыНаПроизводство");
	ОписаниеДвижений.Вставить("ПоляРасчета", РасчетСебестоимостиПрикладныеАлгоритмы.ПереченьПолей(КолонкиТаблицыРаспределения));
	ОписаниеДвижений.Вставить("КлючиСравнения",	ПоляПотребленийРаспределенияНоменклатурыНаПроизводство(ПараметрыРасчета));
	ОписаниеДвижений.Вставить("БазисПрихода", "Знаменатель");
	ОписаниеДвижений.Вставить("БазисРасхода", "Знаменатель");
	ОписаниеДвижений.Вставить("ПолеПорядка", "Период");
	
	ОписаниеДвижений.Вставить("ИмяВременнойТаблицы", "ВтПроизводственныеЗатратыСебестоимостьТоваров");
	ОписаниеДвижений.Вставить(
		"ВременныеТаблицыДляСледующихЭтапов",
		"ПартииПроизводстваПоЭтапам,
		//++ Устарело_Переработка24
		|РаспределенныеОтчеты,
		//-- Устарело_Переработка24
		|ПартииПроизводстваСводно");
	
	Возврат ОписаниеДвижений;
	
КонецФункции

Функция ОписаниеНезаписываемыхРаспределенияНоменклатурыНаПроизводство(ПараметрыРасчета)
	
	НезаписываемыеТипыЗаписей = Новый Соответствие;
	НезаписываемыеТипыЗаписей.Вставить(Перечисления.ТипыЗаписейПартий.Партия, Истина);
	
	Возврат РасчетСебестоимостиПрикладныеАлгоритмы.ОписаниеНезаписываемыхДанных(Ложь, НезаписываемыеТипыЗаписей);
	
КонецФункции

Функция ТаблицаДляРаспределенияНоменклатурыНаПроизводство(ПараметрыРасчета)
	
	Возврат РасчетСебестоимостиПрикладныеАлгоритмы.ТаблицаДляРаспределенияПартий(ПараметрыРасчета,
		ТекстОписаниеДанныхДляРаспределенияНоменклатурыНаПроизводство());
	
КонецФункции

// Выборка данных

Процедура ПолучитьДанныеДляРаспределенияНоменклатурыНаПроизводство(ПараметрыРасчета)
	
	ДляРасшифровки = РасчетСебестоимостиПрикладныеАлгоритмы.РасчетВызванДляРасшифровки(ПараметрыРасчета);
	
	ДопПараметры = ПараметрыРасчета.ДополнительныеПараметрыЗапросаЭтапа;
	ДопПараметры.Вставить("ВключатьДанныеПроизводства21", Ложь);
	ДопПараметры.Вставить("РасчетСебестоимостиВыполнен",  Ложь);
	ДопПараметры.Вставить("РасшифровкаРаспределения", 	  Ложь);
	ДопПараметры.Вставить("УчитыватьБудущиеВыпуски", 	  Ложь);
	ДопПараметры.Вставить("ОтборПоМатериаламИерархия",    РасчетСебестоимостиПроизводство.ОтборПоМатериаламИерархия(ПараметрыРасчета));
	
	ДопПараметры.Вставить("Регистратор",
		?(ПараметрыРасчета.Свойство("Регистратор"),ПараметрыРасчета.Регистратор, Неопределено));
	
	//++ НЕ УТКА
	ПараметрыПолученияПартий = ЗатратыСервер.ОписаниеПараметровПолученияАктивныхПартийБезСпецификаций();
	ПараметрыПолученияПартий.НачалоПериода = ПараметрыРасчета.РасчетныйПериод.НачалоПериода;
	ПараметрыПолученияПартий.ОкончаниеПериода = ПараметрыРасчета.РасчетныйПериод.КонецПериода;
	ПараметрыПолученияПартий.Организации = ПараметрыРасчета.МассивОрганизаций;
	
	ЗатратыСервер.СформироватьАктивныеПартииБезСпецификаций(ПараметрыПолученияПартий, ПараметрыРасчета.МенеджерВременныхТаблиц);
	//-- НЕ УТКА
	
	ПараметрыНумерации = РасчетСебестоимостиПрикладныеАлгоритмы.СформироватьПараметрыНумерацииСтрокВременнойТаблицы(
		"Организация", // разделитель
		"Количество, Знаменатель", // ресурсы
		"Период, Регистратор, ТипЗаписи"); // порядок
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПолучитьДанныеЭтапаРасчета(
		ПараметрыРасчета,
		ТекстЗапросаДляРаспределенияНоменклатурыНаПроизводство(ПараметрыРасчета),
		ПараметрыНумерации,
		НЕ ДляРасшифровки);
	
КонецПроцедуры

// Тексты запросов

// ... общий

Функция ТекстЗапросаДляРаспределенияНоменклатурыНаПроизводство(ПараметрыРасчета = Неопределено) Экспорт
	
	ДляРасшифровки = РасчетСебестоимостиПрикладныеАлгоритмы.РасчетВызванДляРасшифровки(ПараметрыРасчета);
	
	ТекстЗапроса = ""
		// подготовка временных таблиц
		+ РасчетСебестоимостиПроизводство.ТекстПартииПроизводства() // вт Продукция

		//++ НЕ УТКА
		+ РасчетСебестоимостиПроизводство.ТекстПартииДавальческогоПроизводства() // вт ПартииДавальческогоПроизводства

		//-- НЕ УТКА
		+ РасчетСебестоимостиПроизводство.ТекстПустаяТаблицаПартииМатериаловВНЗП()
		+ РасчетСебестоимостиПроизводство.ТекстДолиСтоимостиПоПродукции()		
		+ ТекстРаспределенияНоменклатурыНаПроизводствоИнициализация() // вт Регистраторы, Распределить
		+ ТекстДанныеПоМатериальнымЗатратам(ДляРасшифровки) // вт ДанныеПоМатериальнымЗатратам
		+ РасчетСебестоимостиПроизводство.ТекстДанныеПоВыпущеннойПродукции() //вт ДанныеПоПродукции
		+ РасчетСебестоимостиПроизводство.ТекстДанныеПоТрудозатратам() //вт ДанныеПоТрудозатратам
		+ РасчетСебестоимостиПроизводство.ТекстРаспаковкаНаборов("Продукция") + ОбщегоНазначения.РазделительПакетаЗапросов()// вт МатериалыИУслугиСпецификаций
		+ ТекстДанныеПоНормативамМатериала() //вт ДанныеПоНормативамРасходаМатериала

		//++ НЕ УТКА
		+ ТекстНазначенияДавальческихМатериалов() //вт НазначенияДавальческихМатериалов

		//-- НЕ УТКА
		+ ТекстБазаРаспределенияНоменклатурыНаПроизводство() // вт БазаРаспределенияДетально, БазаРаспределения
		+ ТекстСуммыПоказателейРаспределенияНоменклатурыНаПроизводство() // использует БазаРаспределения, вт ОбщиеСуммы
		// выборка данных
		+ ТекстОписаниеДанныхДляРаспределенияНоменклатурыНаПроизводство()
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстКРаспределениюНоменклатурыНаПроизводство() // использует Распределить, ОбщиеСуммы
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстРасходыСебестоимостиНаПроизводство() // использует БазаРаспределения
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстПриходыСебестоимостиНаПроизводство() // использует БазаРаспределения
		+ "";
		
	Возврат РасчетСебестоимостиПрикладныеАлгоритмы.ПодставитьШаблоныВесаИОбъемаВТекстЗапроса(ТекстЗапроса, "СН");
	
КонецФункции

// ... описания данных

Функция ТекстОписаниеДанныхДляРаспределенияНоменклатурыНаПроизводство()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ ПЕРВЫЕ 0
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(80))       									КАК ЗапросИсточник,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ПустаяСсылка)                   КАК ТипЗаписи,
	|	ЛОЖЬ                                                                    КАК РасчетЗавершен,
	|	ДД.ВидДвижения                                                          КАК ВидДвижения,
	|	ДД.Период                                                               КАК Период,
	|	ДД.Регистратор                                                          КАК Регистратор,
	|	ДД.АналитикаУчетаНоменклатуры                                           КАК АналитикаУчетаНоменклатуры,
	|	ДД.РазделУчета                                                          КАК РазделУчета,
	|	ДД.ВидЗапасов                                                           КАК ВидЗапасов,
	|	ДД.Организация                                                          КАК Организация,
	|	ДД.Партия                                                               КАК Партия,
	|	ДД.АналитикаУчетаПартий                                                 КАК АналитикаУчетаПартий,
	|	ДД.АналитикаФинансовогоУчета                                            КАК АналитикаФинансовогоУчета,
	|	ДД.ВидДеятельностиНДС                                                   КАК ВидДеятельностиНДС,
	|	0                                                                       КАК Знаменатель,
	|	ДД.Количество                                                           КАК Количество,
	|	ДД.Стоимость                                                            КАК Стоимость,
	|	ДД.СтоимостьРегл                                                        КАК СтоимостьРегл,
	|	ДД.ПостояннаяРазница                                                    КАК ПостояннаяРазница,
	|	ДД.ХозяйственнаяОперация                                                КАК ХозяйственнаяОперация,
	|	ДД.КорРазделУчета                                                       КАК КорРазделУчета,
	|	ДД.КорАналитикаУчетаНоменклатуры                                        КАК КорАналитикаУчетаНоменклатуры,
	|	ДД.КорВидЗапасов                                                        КАК КорВидЗапасов,
	|	ДД.КорПартия                                                            КАК КорПартия,
	|	ДД.КорВидДеятельностиНДС                                                КАК КорВидДеятельностиНДС,
	|	ДД.КорАналитикаФинансовогоУчета                                         КАК КорАналитикаФинансовогоУчета,
	|	ДД.АналитикаУчетаПоПартнерам                                            КАК АналитикаУчетаПоПартнерам,
	|	ДД.ИдентификаторСтроки                                                  КАК ИдентификаторСтроки,
	
	|	ДД.СтатьяРасходовСписания                                               КАК СтатьяРасходовСписания,
	|	ДД.АналитикаРасходов                                                    КАК АналитикаРасходов,
	|	ДД.СтатьяАктивовПассивов                                                КАК СтатьяАктивовПассивов,
	|	ДД.АналитикаАктивовПассивов                                             КАК АналитикаАктивовПассивов,
	
	|	КК.Номенклатура                                                         КАК Номенклатура,
	|	КК.Характеристика                                                       КАК Характеристика,
	|	КК.Серия                                                                КАК Серия,
	|	КК.МестоХранения														КАК Склад,
	|	КК.Назначение                                                           КАК Назначение,
	|	КК.СтатьяКалькуляции                                                    КАК СтатьяКалькуляции,
	|	ДД.НастройкаХозяйственнойОперации                                       КАК НастройкаХозяйственнойОперации,
	|	ДД.ИдентификаторФинЗаписи                                               КАК ИдентификаторФинЗаписи
	|ПОМЕСТИТЬ Данные
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК ДД,
	|		Справочник.КлючиАналитикиУчетаНоменклатуры КАК КК
	|";

	ТекстЗапроса = РасчетСебестоимостиПрикладныеАлгоритмы.ПодготовитьЗапросОписанияДанных(ТекстЗапроса);

	Возврат ТекстЗапроса;

КонецФункции

// ... формирования временные таблиц

// Таблицы: втОтборПоМатериалам, Регистраторы, Распределить
Функция ТекстРаспределенияНоменклатурыНаПроизводствоИнициализация()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДД.Регистратор КАК Регистратор,
	|	ДД.Материал КАК Материал
	|ПОМЕСТИТЬ втОтборПоМатериалам
	|ИЗ
	|	&ОтборПоМатериаламИерархия КАК ДД
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДД.Ссылка КАК Регистратор
	|ПОМЕСТИТЬ Регистраторы
	|ИЗ
	|	Документ.РаспределениеПроизводственныхЗатрат КАК ДД
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.РеестрДокументов КАК РеестрСторно
	|	ПО
	|		РеестрСторно.СторнируемыйДокумент = ДД.Ссылка
	|		И НЕ РеестрСторно.ДополнительнаяЗапись
	|		И РеестрСторно.ДатаДокументаИБ >= &НачалоПериода 
	|		И РеестрСторно.ДатаДокументаИБ <= &КонецПериода
	|		И РеестрСторно.Проведен
	|ГДЕ
	|	ДД.Проведен
	|	И ДД.Организация В(&МассивОрганизаций)
	|	И ДД.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ДД.КоличествоКРаспределениюПоПравилу <> 0
	|	И (&Регистратор = ДД.Ссылка ИЛИ &Регистратор = НЕОПРЕДЕЛЕНО)
	// Проверка на наличие сторно в текущем периоде.
	|	И РеестрСторно.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДД.Ссылка КАК Регистратор
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов КАК ДД
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.РеестрДокументов КАК РеестрСторно
	|	ПО
	|		РеестрСторно.СторнируемыйДокумент = ДД.Ссылка
	|		И НЕ РеестрСторно.ДополнительнаяЗапись
	|		И РеестрСторно.ДатаДокументаИБ >= &НачалоПериода 
	|		И РеестрСторно.ДатаДокументаИБ <= &КонецПериода
	|		И РеестрСторно.Проведен
	|ГДЕ
	|	ДД.Проведен
	|	И ДД.Организация В(&МассивОрганизаций)
	|	И ДД.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ДД.КоличествоПоПравилу <> 0
	|	И (&Регистратор = ДД.Ссылка ИЛИ &Регистратор = НЕОПРЕДЕЛЕНО)
	// Проверка на наличие сторно в текущем периоде.
	|	И РеестрСторно.Ссылка ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Регистраторы.Регистратор,
	|	Движения.АналитикаУчетаНоменклатуры,
	|	Движения.ВидЗапасов,
	|	Движения.ИдентификаторФинЗаписи,
	|	СУММА(Движения.Стоимость) КАК Стоимость,
	|	СУММА(Движения.СтоимостьРегл) КАК СтоимостьРегл,
	|	СУММА(Движения.ПостояннаяРазница) КАК ПостояннаяРазница
	|ПОМЕСТИТЬ ОбщаяСтоимостьВО
	|ИЗ
	|	Регистраторы КАК Регистраторы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеВозвратныхОтходов КАК Реквизиты
	|		ПО Регистраторы.Регистратор = Реквизиты.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТКэшРасчетныеОборотыСебестоимостьТоваров КАК Движения
	|		ПО Регистраторы.Регистратор = Движения.Регистратор
	|			И Движения.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)
	|			И Движения.Количество > 0
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеВозвратныхОтходов.ВидыЗапасов КАК ТабличнаяЧасть
	|		ПО ТабличнаяЧасть.Ссылка = Реквизиты.Ссылка
	|			И ТабличнаяЧасть.ИдентификаторФинЗаписи = Движения.ИдентификаторФинЗаписи
	|			И ТабличнаяЧасть.СтатьяРасходов = НЕОПРЕДЕЛЕНО
	|			И ТабличнаяЧасть.ПартияПроизводства = ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка)
	|ГДЕ
	|	НЕ ТабличнаяЧасть.Ссылка ЕСТЬ NULL
	// Для работ, распределяемых по правилу, в движения пишутся идентификатор документа.
	|	ИЛИ Реквизиты.ИдентификаторДокумента = Движения.ИдентификаторФинЗаписи
	|
	|СГРУППИРОВАТЬ ПО
	|	Регистраторы.Регистратор,
	|	Движения.АналитикаУчетаНоменклатуры,
	|	Движения.ВидЗапасов,
	|	Движения.ИдентификаторФинЗаписи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДД.Ссылка                            КАК Регистратор,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			ТОГДА ДД.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ АналитикаБезНазначения.КлючАналитики
	|	КОНЕЦ КАК АналитикаУчетаНоменклатуры,
	|	АналитикаБезНазначения.КлючАналитики КАК АналитикаУчетаНоменклатурыБезНазначения,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК КорАналитикаУчетаНоменклатуры,
	|	КлючиКомитента.КлючАналитики         КАК АналитикаКомитента,
	|	АналитикаРасчетовСКомитентом.КлючАналитики КАК АналитикаУчетаПоПартнерам,
	|	ДД.Номенклатура                      КАК Номенклатура,
	|	ДД.Характеристика                    КАК Характеристика,
	|	ДД.Серия                             КАК Серия,
	|	ДД.Назначение                        КАК Назначение,
	|	ДД.Организация                       КАК Организация,
	|	ДД.Подразделение                     КАК Подразделение,
	|	ДД.ПравилоРаспределения              КАК ПравилоРаспределения,
	|	ДД.БазаРаспределения                 КАК БазаРаспределения,
	|	ДД.СтатьяКалькуляции                 КАК СтатьяКалькуляции,
	|	Запасы.ВидЗапасов                    КАК ВидЗапасов,
	|	Запасы.ВидЗапасовПолучателя          КАК ВидЗапасовПолучателя,
	|	СпрВидыЗапасов.ТипЗапасов            КАК ТипЗапасов,
	|	СУММА(Запасы.Количество)             КАК Количество,
	|	0						             КАК Стоимость,
	|	0						             КАК СтоимостьРегл,
	|	0						             КАК ПостояннаяРазница,
	|	ЕСТЬNULL(втОтборПоМатериалам.Материал, НЕОПРЕДЕЛЕНО) КАК Материал,
	|	ЕСТЬNULL(втОтборПоВидамРабот.ВидРабот, НЕОПРЕДЕЛЕНО) КАК ВидРабот,
	|	ЕСТЬNULL(втОтборПоГруппамПродукции.ГруппаПродукции, НЕОПРЕДЕЛЕНО) КАК ГруппаПродукции,
	|	втОтборПоГруппамПродукции.ГруппаПродукции ЕСТЬ NULL КАК ПоВсемГруппамПродукции,
	|	"""" КАК ИдентификаторФинЗаписи
	|ПОМЕСТИТЬ Распределить
	|ИЗ
	|	Документ.РаспределениеПроизводственныхЗатрат КАК ДД
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеПроизводственныхЗатрат.ВидыЗапасов КАК Запасы
	|		ПО ДД.Ссылка = Запасы.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Справочник.ВидыЗапасов КАК СпрВидыЗапасов
	|	ПО
	|		Запасы.ВидЗапасов = СпрВидыЗапасов.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Регистраторы КАК Регистраторы
	|		ПО Регистраторы.Регистратор = ДД.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОтборПоМатериалам КАК втОтборПоМатериалам
	|		ПО втОтборПоМатериалам.Регистратор = ДД.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПроизводственныхЗатрат.ОтборПоГруппамПродукции КАК втОтборПоГруппамПродукции
	|		ПО втОтборПоГруппамПродукции.Ссылка = ДД.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПроизводственныхЗатрат.ОтборПоВидамРабот КАК втОтборПоВидамРабот
	|		ПО втОтборПоВидамРабот.Ссылка = ДД.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|		ПО ДД.АналитикаУчетаНоменклатуры = Аналитика.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаБезНазначения
	|	ПО
	|		Аналитика.Номенклатура = АналитикаБезНазначения.Номенклатура
	|		И Аналитика.Характеристика = АналитикаБезНазначения.Характеристика
	|		И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) = АналитикаБезНазначения.Назначение
	|		И Аналитика.Серия = АналитикаБезНазначения.Серия
	|		И Аналитика.МестоХранения = АналитикаБезНазначения.МестоХранения
	|		И Аналитика.СтатьяКалькуляции = АналитикаБезНазначения.СтатьяКалькуляции
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК КлючиКомитента
	|	ПО
	|		ДД.Номенклатура = КлючиКомитента.Номенклатура
	|		И ДД.Характеристика = КлючиКомитента.Характеристика
	|		И ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) = КлючиКомитента.СтатьяКалькуляции
	|		И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) = КлючиКомитента.Назначение
	|		И ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) = КлючиКомитента.Серия
	|		И СпрВидыЗапасов.ВладелецТовара = КлючиКомитента.МестоХранения
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаРасчетовСКомитентом
	|		ПО ДД.Организация = АналитикаРасчетовСКомитентом.Организация
	|		И СпрВидыЗапасов.ВладелецТовара = АналитикаРасчетовСКомитентом.Партнер
	|		И СпрВидыЗапасов.Контрагент = АналитикаРасчетовСКомитентом.Контрагент
	|		И СпрВидыЗапасов.Договор = АналитикаРасчетовСКомитентом.Договор
	|		И ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) = АналитикаРасчетовСКомитентом.НаправлениеДеятельности
	|	
	|ГДЕ
	|	Запасы.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовНаПартииПроизводства)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДД.Ссылка,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			ТОГДА ДД.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ АналитикаБезНазначения.КлючАналитики
	|	КОНЕЦ,
	|	АналитикаБезНазначения.КлючАналитики,
	|	КлючиКомитента.КлючАналитики,
	|	АналитикаРасчетовСКомитентом.КлючАналитики,
	|	ДД.Номенклатура,
	|	ДД.Характеристика,
	|	ДД.Серия,
	|	ДД.Назначение,
	|	ДД.Организация,
	|	ДД.Подразделение,
	|	ДД.ПравилоРаспределения,
	|	ДД.БазаРаспределения,
	|	ДД.СтатьяКалькуляции,
	|	Запасы.ВидЗапасов,
	|	Запасы.ВидЗапасовПолучателя,
	|	СпрВидыЗапасов.ТипЗапасов,
	|	ЕСТЬNULL(втОтборПоМатериалам.Материал, НЕОПРЕДЕЛЕНО),
	|	ЕСТЬNULL(втОтборПоВидамРабот.ВидРабот, НЕОПРЕДЕЛЕНО),
	|	ЕСТЬNULL(втОтборПоГруппамПродукции.ГруппаПродукции, НЕОПРЕДЕЛЕНО),
	|	втОтборПоГруппамПродукции.ГруппаПродукции ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДД.Ссылка                            КАК Регистратор,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			ТОГДА ДД.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ АналитикаБезНазначения.КлючАналитики
	|	КОНЕЦ КАК АналитикаУчетаНоменклатуры,
	|	АналитикаБезНазначения.КлючАналитики КАК АналитикаУчетаНоменклатурыБезНазначения,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК КорАналитикаУчетаНоменклатуры,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК АналитикаКомитента,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК АналитикаУчетаПоПартнерам,
	|	ДД.Номенклатура                      КАК Номенклатура,
	|	ДД.Характеристика                    КАК Характеристика,
	|	ДД.Серия                             КАК Серия,
	|	ДД.Назначение                        КАК Назначение,
	|	ДД.Организация                       КАК Организация,
	|	ДД.Подразделение                     КАК Подразделение,
	|	ДД.ПравилоРаспределения              КАК ПравилоРаспределения,
	|	ДД.БазаРаспределения                 КАК БазаРаспределения,
	|	ДД.СтатьяКалькуляции                 КАК СтатьяКалькуляции,
	|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК ВидЗапасов,
	|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК ВидЗапасовПолучателя,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПустаяСсылка) КАК ТипЗапасов,
	|	ДД.КоличествоКРаспределениюПоПравилу КАК Количество,
	|	0						             КАК Стоимость,
	|	0						             КАК СтоимостьРегл,
	|	0						             КАК ПостояннаяРазница,
	|	ЕСТЬNULL(втОтборПоМатериалам.Материал, НЕОПРЕДЕЛЕНО) КАК Материал,
	|	ЕСТЬNULL(втОтборПоВидамРабот.ВидРабот, НЕОПРЕДЕЛЕНО) КАК ВидРабот,
	|	ЕСТЬNULL(втОтборПоГруппамПродукции.ГруппаПродукции, НЕОПРЕДЕЛЕНО) КАК ГруппаПродукции,
	|	втОтборПоГруппамПродукции.ГруппаПродукции ЕСТЬ NULL КАК ПоВсемГруппамПродукции,
	|	"""" КАК ИдентификаторФинЗаписи
	|ИЗ
	|	Документ.РаспределениеПроизводственныхЗатрат КАК ДД
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Регистраторы КАК Регистраторы
	|		ПО Регистраторы.Регистратор = ДД.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО СпрНоменклатура.Ссылка = ДД.Номенклатура
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОтборПоМатериалам КАК втОтборПоМатериалам
	|		ПО втОтборПоМатериалам.Регистратор = ДД.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПроизводственныхЗатрат.ОтборПоГруппамПродукции КАК втОтборПоГруппамПродукции
	|		ПО втОтборПоГруппамПродукции.Ссылка = ДД.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПроизводственныхЗатрат.ОтборПоВидамРабот КАК втОтборПоВидамРабот
	|		ПО втОтборПоВидамРабот.Ссылка = ДД.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаБезНазначения
	|	ПО
	|		ДД.Номенклатура = АналитикаБезНазначения.Номенклатура
	|		И ДД.Характеристика = АналитикаБезНазначения.Характеристика
	|		И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) = АналитикаБезНазначения.Назначение
	|		И ДД.Серия = АналитикаБезНазначения.Серия
	|		И ДД.Подразделение = АналитикаБезНазначения.МестоХранения
	|		И ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) = АналитикаБезНазначения.СтатьяКалькуляции
	|
	|ГДЕ
	|	СпрНоменклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДД.Ссылка КАК Регистратор,
	|	ОбщаяСтоимостьВО.АналитикаУчетаНоменклатуры,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка),
	|	ОбщаяСтоимостьВО.АналитикаУчетаНоменклатуры,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка),
	|	ДД.Номенклатура,
	|	ДД.Характеристика,
	|	ДД.Серия,
	|	ДД.Назначение,
	|	ДД.Организация,
	|	ДД.Подразделение,
	|	ДД.ПравилоРаспределения,
	|	ДД.БазаРаспределения,
	|	ДД.СтатьяКалькуляции,
	|	ОбщаяСтоимостьВО.ВидЗапасов,
	|	ОбщаяСтоимостьВО.ВидЗапасов,
	|	ЕСТЬNULL(РеквизитыВидаЗапасов.ТипЗапасов, 
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПустаяСсылка)),
	|	-ДД.КоличествоПоПравилу				КАК Количество,
	|	-ОбщаяСтоимостьВО.Стоимость			КАК Стоимость,
	|	-ОбщаяСтоимостьВО.СтоимостьРегл		КАК СтоимостьРегл,
	|	-ОбщаяСтоимостьВО.ПостояннаяРазница КАК ПостояннаяРазница,
	|	ЕСТЬNULL(втОтборПоМатериалам.Материал, НЕОПРЕДЕЛЕНО) КАК Материал,
	|	ЕСТЬNULL(втОтборПоВидамРабот.ВидРабот, НЕОПРЕДЕЛЕНО) КАК ВидРабот,
	|	ЕСТЬNULL(втОтборПоГруппамПродукции.ГруппаПродукции, НЕОПРЕДЕЛЕНО) КАК ГруппаПродукции,
	|	втОтборПоГруппамПродукции.ГруппаПродукции ЕСТЬ NULL КАК ПоВсемГруппамПродукции,
	|	ОбщаяСтоимостьВО.ИдентификаторФинЗаписи
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов КАК ДД
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Регистраторы КАК Регистраторы
	|		ПО Регистраторы.Регистратор = ДД.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОбщаяСтоимостьВО КАК ОбщаяСтоимостьВО
	|		ПО ДД.Ссылка = ОбщаяСтоимостьВО.Регистратор
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК РеквизитыВидаЗапасов
	|		ПО ДД.Ссылка = РеквизитыВидаЗапасов.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОтборПоМатериалам КАК втОтборПоМатериалам
	|		ПО втОтборПоМатериалам.Регистратор = ДД.Ссылка
	|	
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеВозвратныхОтходов.ОтборПоГруппамПродукции КАК втОтборПоГруппамПродукции
	|		ПО втОтборПоГруппамПродукции.Ссылка = ДД.Ссылка
	|	
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеВозвратныхОтходов.ОтборПоВидамРабот КАК втОтборПоВидамРабот
	|		ПО втОтборПоВидамРабот.Ссылка = ДД.Ссылка
	|;
	|";
	
	Возврат ТекстЗапроса
	
КонецФункции

// Таблицы: ДанныеПоМатериальнымЗатратам
Функция ТекстДанныеПоМатериальнымЗатратам(ДляРасшифровки)
	
	// Для расшифровки отчета исходными данными являются документы ЭтапПроизводства
	// и движения по себестоимости для производства без заказов.
	// Для закрытия месяца данными являются результаты расчета предыдущих этапов.
	Если ДляРасшифровки Тогда
		
		Возврат "
		|ВЫБРАТЬ
		|	ДД.Организация,
		|	ДД.Подразделение,
		|	ДД.Материал,
		|	ДД.Назначение,
		|	ДД.ГруппаПродукции,
		|	ДД.ПартияПроизводства,
		|	ДД.ВидДеятельностиНДС,
		|	ДД.НаправлениеДеятельности,
		|	СУММА(ЕСТЬNULL(Цены.Цена, 0) * ДД.Количество) КАК Стоимость,
		|	СУММА(ДД.Количество) КАК Количество,
		|	СУММА(ДД.Объем) КАК Объем,
		|	СУММА(ДД.Вес) КАК Вес
		|ПОМЕСТИТЬ ДанныеПоМатериальнымЗатратам
		|ИЗ
		// Производство без заказа
		|	(ВЫБРАТЬ
		|		ДД.Организация								КАК Организация,
		|		Аналитика.МестоХранения						КАК Подразделение,
		|		Аналитика.Номенклатура						КАК Материал,
		|		Аналитика.Характеристика					КАК Характеристика,
		|		СпрПартииПроизводства.Назначение			КАК Назначение,
		|		ДД.АналитикаФинансовогоУчета				КАК ГруппаПродукции,
		|		ДД.Партия									КАК ПартияПроизводства,
		|		СпрПартииПроизводства.ВидДеятельностиНДС	КАК ВидДеятельностиНДС,
		|		ЕСТЬNULL(СпрПартииПроизводства.Назначение.НаправлениеДеятельности,
		|			ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности,
		|		СУММА(ДД.Количество)						КАК Количество,
		|		СУММА(&Объем * ДД.Количество)				КАК Объем,
		|		СУММА(&Вес * ДД.Количество)					КАК Вес
		|	ИЗ
		|		РегистрНакопления.СебестоимостьТоваров КАК ДД
		|
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|			ПО Аналитика.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК СпрПартииПроизводства
		|			ПО СпрПартииПроизводства.Ссылка = ДД.Партия
		|
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СН
		|			ПО СН.Ссылка = Аналитика.Номенклатура
		|
		|	ГДЕ
		|		ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ДД.Организация В(&МассивОрганизаций)
		|		И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		И ДД.Количество > 0
		|		И ДД.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРасходовНаПартииПроизводства),
		|										ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовНаПартииПроизводства),
		|										ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость))
		|		И ТИПЗНАЧЕНИЯ(СпрПартииПроизводства.Документ) = ТИП(Документ.ПроизводствоБезЗаказа)
		// Исключим из базы движения документов распределения, т.к. на момент расчета себестоимости их ещё нет 
		|		И ТИПЗНАЧЕНИЯ(ДД.Регистратор) <> ТИП(Документ.РаспределениеПроизводственныхЗатрат)
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ДД.Организация,
		|		Аналитика.МестоХранения,
		|		Аналитика.Номенклатура,
		|		Аналитика.Характеристика,
		|		СпрПартииПроизводства.Назначение,
		|		ДД.АналитикаФинансовогоУчета,
		|		ДД.Партия,
		|		СпрПартииПроизводства.ВидДеятельностиНДС,
		|		ЕСТЬNULL(СпрПартииПроизводства.Назначение.НаправлениеДеятельности,
		|			ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))
		//++ НЕ УТКА
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		// Этапы
		|	ВЫБРАТЬ
		|		ПартииПроизводства.Организация КАК Организация,
		|		Аналитика.МестоХранения КАК Подразделение,
		|		Аналитика.Номенклатура КАК Материал,
		|		Аналитика.Характеристика КАК Характеристика,
		|		ПартииПроизводства.Назначение КАК Назначение,
		|		ПартииПроизводства.АналитикаФинансовогоУчета КАК ГруппаПродукции,
		|		ПартииПроизводства.ПартияПроизводства КАК ПартияПроизводства,
		|		ПартииПроизводства.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
		|		ЕСТЬNULL(ПартииПроизводства.Назначение.НаправлениеДеятельности,
		|			ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности,
		|		СУММА(ДД.Количество) КАК Количество,
		|		СУММА(&Объем * ДД.Количество) КАК Объем,
		|		СУММА(&Вес * ДД.Количество) КАК Вес
		|	ИЗ
		|		Документ.ЭтапПроизводства2_2.РасходМатериаловИРабот КАК ДД
		|
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПартииПроизводстваПоЭтапам КАК ПартииПроизводства
		|			ПО ПартииПроизводства.Этап = ДД.Ссылка
		|
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|			ПО Аналитика.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СН
		|			ПО СН.Ссылка = Аналитика.Номенклатура
		|
		|	ГДЕ
		|		НЕ ДД.Ссылка.ПроизводствоНаСтороне
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ПартииПроизводства.Организация,
		|		Аналитика.МестоХранения,
		|		Аналитика.Номенклатура,
		|		Аналитика.Характеристика,
		|		ПартииПроизводства.Назначение,
		|		ПартииПроизводства.АналитикаФинансовогоУчета,
		|		ПартииПроизводства.ПартияПроизводства,
		|		ПартииПроизводства.ВидДеятельностиНДС,
		|		ЕСТЬNULL(ПартииПроизводства.Назначение.НаправлениеДеятельности,
		|			ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))
		//-- НЕ УТКА
		|	) КАК ДД
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&КонецПериода) КАК Цены
		|		ПО (ДД.Материал = Цены.Номенклатура)
		|		И (ДД.Характеристика = Цены.Характеристика)
		|		И (&ВидЦеныПлановойСтоимостиМатериаловРабот = Цены.ВидЦены)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.Организация,
		|	ДД.Подразделение,
		|	ДД.НаправлениеДеятельности,
		|	ДД.Материал,
		|	ДД.Назначение,
		|	ДД.ГруппаПродукции,
		|	ДД.ВидДеятельностиНДС,
		|	ДД.ПартияПроизводства
		|;
		|";
		
	Иначе
		
		Возврат "
		|ВЫБРАТЬ
		|	ДД.Организация,
		|	Аналитика.МестоХранения КАК Подразделение,
		|	Аналитика.Номенклатура КАК Материал,
		|	СпрПартииПроизводства.Назначение КАК Назначение,
		|	ДД.КорАналитикаФинансовогоУчета КАК ГруппаПродукции,
		|	ДД.КорПартия КАК ПартияПроизводства,
		|	СпрПартииПроизводства.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
		|	СУММА(ДД.Стоимость) КАК Стоимость,
		|	СУММА(ДД.Количество) КАК Количество,
		|	СУММА(&Объем * ДД.Количество) КАК Объем,
		|	СУММА(&Вес * ДД.Количество) КАК Вес
		|ПОМЕСТИТЬ ДанныеПоМатериальнымЗатратам
		|ИЗ
		|	ВТКэшРасчетныеОборотыСебестоимостьТоваров КАК ДД
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО (Аналитика.Ссылка = ДД.КорАналитикаУчетаНоменклатуры)
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК СпрПартииПроизводства
		|		ПО СпрПартииПроизводства.Ссылка = ДД.КорПартия
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СН
		|		ПО СН.Ссылка = Аналитика.Номенклатура
		|
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ДД.Организация В(&МассивОрганизаций)
		|	И НЕ ДД.СлужебноеВидДвиженияПриход
		|	И ДД.Количество <> 0
		|	И ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРасходовНаПартииПроизводства)
		|	И ДД.РазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеПереработчику)
		|	И ДД.РазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработкуПереданныеПартнерам)
		|	И ДД.РазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранениеПереданныеПартнерам)
		|	И ТИПЗНАЧЕНИЯ(СпрПартииПроизводства.Документ) В (
		//++ НЕ УТКА
		|													ТИП(Документ.ЗаказНаПроизводство2_2),
		//-- НЕ УТКА
		|													ТИП(Документ.ПроизводствоБезЗаказа)
		|													)
		|	
		|СГРУППИРОВАТЬ ПО
		|	ДД.Организация,
		|	Аналитика.МестоХранения,
		|	Аналитика.Номенклатура,
		|	СпрПартииПроизводства.Назначение,
		|	ДД.КорАналитикаФинансовогоУчета,
		|	СпрПартииПроизводства.ВидДеятельностиНДС,
		|	ДД.КорПартия
		|;
		|";
	КонецЕсли;
	
КонецФункции

// Таблицы: втКоэффициенты, ДанныеПоНормативамРасходаМатериала
Функция ТекстДанныеПоНормативамМатериала()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ДД.ПартияПроизводства КАК ПартияПроизводства,
	|	СУММА(ВЫБОР КОГДА (РС.КоличествоУпаковок * ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки1, 1)) = 0
	|		ТОГДА 0
	|	ИНАЧЕ
	|		ДД.КоличествоПродукции/(РС.КоличествоУпаковок * ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки1, 1))
	|	КОНЕЦ) КАК Коэффициент
	|ПОМЕСТИТЬ втКоэффициенты
	|ИЗ
	|	Продукция КАК ДД
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РесурсныеСпецификации.ВыходныеИзделия КАК РС
	|		ПО ДД.Спецификация = РС.Ссылка
	|		И ВЫБОР
	|			КОГДА РС.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ
	|				ДД.Номенклатура = РС.Номенклатура
	|		КОНЕЦ
	|
	|ГДЕ
	|	РС.НомерСтроки = 1
	|	И ДД.ДоляНаименованияМесяца > 0
	|СГРУППИРОВАТЬ ПО
	|	ДД.ПартияПроизводства
	|;
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДД.Организация                   КАК Организация,
	|	РС.Этап.Подразделение            КАК Подразделение,
	|	РС.Номенклатура                  КАК Материал,
	|	ДД.АналитикаФинансовогоУчета     КАК ГруппаПродукции,
	|	СпрПартииПроизводства.Назначение КАК Назначение,
	|	ДД.ПартияПроизводства            КАК ПартияПроизводства,
	|	ДД.ВидДеятельностиНДС            КАК ВидДеятельностиНДС,
	|	ВЫРАЗИТЬ(РС.Количество * КФ.Коэффициент КАК ЧИСЛО(15,3))   КАК Количество
	|ПОМЕСТИТЬ ДанныеПоНормативамРасходаМатериала
	|ИЗ
	|	Продукция КАК ДД
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКоэффициенты КАК КФ
	|		ПО ДД.ПартияПроизводства = КФ.ПартияПроизводства
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ МатериалыИУслугиСпецификаций КАК РС
	|		ПО ДД.Спецификация = РС.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК СпрПартииПроизводства
	|		ПО СпрПартииПроизводства.Ссылка = ДД.ПартияПроизводства
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДД.Организация                   КАК Организация,
	|	РС.Этап.Подразделение            КАК Подразделение,
	|	РС.Номенклатура                  КАК Материал,
	|	ДД.АналитикаФинансовогоУчета     КАК ГруппаПродукции,
	|	СпрПартииПроизводства.Назначение КАК Назначение,
	|	ДД.ПартияПроизводства            КАК ПартияПроизводства,
	|	ДД.ВидДеятельностиНДС            КАК ВидДеятельностиНДС,
	|	ВЫРАЗИТЬ(РС.КоличествоУпаковок * ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки2, 1) * КФ.Коэффициент КАК ЧИСЛО(15,3)) КАК Количество
	|ИЗ
	|	Продукция КАК ДД
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКоэффициенты КАК КФ
	|		ПО ДД.ПартияПроизводства = КФ.ПартияПроизводства
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РесурсныеСпецификации.ВозвратныеОтходы КАК РС
	|		ПО ДД.Спецификация = РС.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК СпрПартииПроизводства
	|		ПО СпрПартииПроизводства.Ссылка = ДД.ПартияПроизводства
	|;
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаКоэффициентУпаковки1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"РС.Упаковка",
			"ДД.Номенклатура"));
			
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаКоэффициентУпаковки2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"РС.Упаковка",
			"РС.Номенклатура"));
			
	Возврат ТекстЗапроса;
	
КонецФункции

//++ НЕ УТКА

// Таблицы: НазначенияДавальческихМатериалов
Функция ТекстНазначенияДавальческихМатериалов()
	
	Возврат "
	|ВЫБРАТЬ
	|	НазначенияМатериалов.Ссылка КАК НазначениеМатериала,
	|	НазначенияМатериалов.Партнер КАК Партнер,
	|	НазначенияМатериалов.Договор КАК Договор
	|ПОМЕСТИТЬ НазначенияДавальческихМатериалов
	|ИЗ
	|	Справочник.Назначения КАК НазначенияМатериалов
	|ГДЕ
	|	НазначенияМатериалов.ТипНазначения В (
	//++ Устарело_Переработка24
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ДавальческоеМатериалы22),
	//-- Устарело_Переработка24
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.Давальческое2_5))
	|	И ИСТИНА В 
	|		(ВЫБРАТЬ ПЕРВЫЕ 1 ИСТИНА ИЗ Распределить КАК ДД ГДЕ ДД.Назначение = НазначенияМатериалов.Ссылка)
	|ИНДЕКСИРОВАТЬ ПО 
	|	НазначениеМатериала
	|;
	|";
	
КонецФункции

//-- НЕ УТКА

// Таблицы: БазаРаспределенияДетально, БазаРаспределения
Функция ТекстБазаРаспределенияНоменклатурыНаПроизводство()
	
	Возврат "
	// по указанным материалам
	|ВЫБРАТЬ
	|	ДД.Регистратор              КАК Регистратор,
	|	ДД.ВидЗапасов               КАК ВидЗапасов,
	|	ДД.ТипЗапасов               КАК ТипЗапасов,
	|	ДД.БазаРаспределения        КАК БазаРаспределения,
	|	Данные.Подразделение        КАК Подразделение,
	|	Данные.ПартияПроизводства   КАК ПартияПроизводства,
	|	Данные.ВидДеятельностиНДС   КАК ВидДеятельностиНДС,
	|	Данные.ГруппаПродукции      КАК АналитикаФинансовогоУчета,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.РаспределениеВозвратныхОтходов)
	|			И &АналитическийУчетПоГруппамПродукции
	|			И НЕ ДД.Номенклатура.ГруппаАналитическогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА ДД.Номенклатура.ГруппаАналитическогоУчета
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                       КАК КорАналитикаФинансовогоУчета,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.РаспределениеВозвратныхОтходов)
	|			ТОГДА ДД.КорАналитикаУчетаНоменклатуры
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                       КАК КорАналитикаУчетаНоменклатуры,
	|	Данные.Назначение           КАК Назначение,
	|	Данные.Материал             КАК Материал,
	|	НЕОПРЕДЕЛЕНО                КАК Продукция,
	|	Данные.ГруппаПродукции      КАК ГруппаПродукции,
	|	НЕОПРЕДЕЛЕНО                КАК ВидРабот,
	|	СУММА(ВЫБОР ДД.БазаРаспределения
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоУказанныхМатериалов) ТОГДА Данные.Количество
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ОбъемУказанныхМатериалов) ТОГДА Данные.Объем
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВесУказанныхМатериалов) ТОГДА Данные.Вес
	|	КОНЕЦ)                      КАК Знаменатель,
	|	""Материал""                КАК ВидПоказателя
	|ПОМЕСТИТЬ БазаРаспределенияДетально
	|ИЗ
	|	Распределить КАК ДД
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеПоМатериальнымЗатратам КАК Данные
	|		ПО Данные.Организация = ДД.Организация
	|		И Данные.Подразделение = ДД.Подразделение
	|		И Данные.Материал = ДД.Материал
	|		И (Данные.ГруппаПродукции = ДД.ГруппаПродукции ИЛИ ДД.ПоВсемГруппамПродукции)
	|	
	//++ НЕ УТКА
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПартииДавальческогоПроизводства КАК ПартииДавальцев
	|	ПО Данные.ПартияПроизводства = ПартииДавальцев.ПартияПроизводства
	//-- НЕ УТКА
	|
	|ГДЕ
	|	(ДД.БазаРаспределения = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоУказанныхМатериалов)
	|	ИЛИ ДД.БазаРаспределения = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ОбъемУказанныхМатериалов)
	|	ИЛИ ДД.БазаРаспределения = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВесУказанныхМатериалов))
	|	И ((Данные.Назначение = ДД.Назначение
	|		ИЛИ ВЫРАЗИТЬ(Данные.Назначение КАК Справочник.Назначения).НаправлениеДеятельности = ДД.Назначение.НаправлениеДеятельности)
	//++ НЕ УТКА
	|			И ПартииДавальцев.ПартияПроизводства ЕСТЬ NULL
	//-- НЕ УТКА
	|		ИЛИ ДД.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка))
	|
	|СГРУППИРОВАТЬ ПО
	|	ДД.Регистратор,
	|	ДД.ВидЗапасов,
	|	ДД.ТипЗапасов,
	|	ДД.БазаРаспределения,
	|	Данные.Подразделение,
	|	Данные.ПартияПроизводства,
	|	Данные.ВидДеятельностиНДС,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.РаспределениеВозвратныхОтходов)
	|			И &АналитическийУчетПоГруппамПродукции
	|			И НЕ ДД.Номенклатура.ГруппаАналитическогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА ДД.Номенклатура.ГруппаАналитическогоУчета
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.РаспределениеВозвратныхОтходов)
	|			ТОГДА ДД.КорАналитикаУчетаНоменклатуры
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	Данные.Назначение,
	|	Данные.Материал,
	|	Данные.ГруппаПродукции
	|
	// по продукции, по продукции с учетом назначений давальческой схемы
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДД.Регистратор                   КАК Регистратор,
	|	ДД.ВидЗапасов                    КАК ВидЗапасов,
	|	ДД.ТипЗапасов                    КАК ТипЗапасов,
	|	ДД.БазаРаспределения             КАК БазаРаспределения,
	|	Данные.Подразделение             КАК Подразделение,
	|	Данные.ПартияПроизводства        КАК ПартияПроизводства,
	|	Данные.ВидДеятельностиНДС        КАК ВидДеятельностиНДС,
	|	Данные.ГруппаПродукции           КАК АналитикаФинансовогоУчета,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.РаспределениеВозвратныхОтходов)
	|			И &АналитическийУчетПоГруппамПродукции
	|			И НЕ ДД.Номенклатура.ГруппаАналитическогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА ДД.Номенклатура.ГруппаАналитическогоУчета
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                            КАК КорАналитикаФинансовогоУчета,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.РаспределениеВозвратныхОтходов)
	|			ТОГДА ДД.КорАналитикаУчетаНоменклатуры
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                            КАК КорАналитикаУчетаНоменклатуры,
	|	Данные.Назначение                КАК Назначение,
	|	НЕОПРЕДЕЛЕНО                     КАК Материал,
	|	Данные.Продукция                 КАК Продукция,
	|	Данные.ГруппаПродукции           КАК ГруппаПродукции,
	|	НЕОПРЕДЕЛЕНО                     КАК ВидРабот,
	|	СУММА(ВЫБОР ДД.БазаРаспределения
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоПродукции) ТОГДА Данные.Количество
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ОбъемПродукции) ТОГДА Данные.Объем
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВесПродукции) ТОГДА Данные.Вес
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ПлановаяСтоимостьПродукции) ТОГДА Данные.Стоимость
	|	КОНЕЦ)                           КАК Знаменатель,
	|	""Продукция""                    КАК ВидПоказателя
	|ИЗ
	|	Распределить КАК ДД
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеПоПродукции КАК Данные
	|		ПО Данные.Организация = ДД.Организация
	|		И Данные.Подразделение = ДД.Подразделение
	|		И (Данные.ГруппаПродукции = ДД.ГруппаПродукции ИЛИ ДД.ПоВсемГруппамПродукции)
	//++ НЕ УТКА
	|	ЛЕВОЕ СОЕДИНЕНИЕ НазначенияДавальческихМатериалов КАК НазначенияМатериалов
	|		ПО (НазначенияМатериалов.НазначениеМатериала = ДД.Назначение
	|			ИЛИ НазначенияМатериалов.Партнер = ДД.Назначение.Партнер
	|			ИЛИ НазначенияМатериалов.Договор = ДД.Назначение.Договор)
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПартииДавальческогоПроизводства КАК ПартииДавальцев
	|		ПО НазначенияМатериалов.Партнер = ПартииДавальцев.Партнер
	|			И НазначенияМатериалов.Договор = ПартииДавальцев.Договор		
	//-- НЕ УТКА
	|ГДЕ
	|	(ДД.БазаРаспределения = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоПродукции)
	|		ИЛИ ДД.БазаРаспределения = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ОбъемПродукции)
	|		ИЛИ ДД.БазаРаспределения = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВесПродукции)
	|		ИЛИ ДД.БазаРаспределения = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ПлановаяСтоимостьПродукции)
	|	)
	|	И (
	//++ НЕ УТКА 
	|		Данные.ПартияПроизводства = ПартииДавальцев.ПартияПроизводства
	|		ИЛИ
	//-- НЕ УТКА
	|		(
	|			(Данные.Назначение = ДД.Назначение
	|				ИЛИ Данные.НаправлениеДеятельности = ЕСТЬNULL(ДД.Назначение.НаправлениеДеятельности, НЕОПРЕДЕЛЕНО)
	|				ИЛИ ВЫРАЗИТЬ(Данные.Назначение КАК Справочник.Назначения).НаправлениеДеятельности = ЕСТЬNULL(ДД.Назначение.НаправлениеДеятельности, НЕОПРЕДЕЛЕНО)
	|				ИЛИ ДД.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|			)
	|			И ТИПЗНАЧЕНИЯ(Данные.ПартияПроизводства.Документ) В (
	//++ НЕ УТКА
	|													ТИП(Документ.ЗаказНаПроизводство2_2),
	//-- НЕ УТКА
	|													ТИП(Документ.ПроизводствоБезЗаказа)
	|													)
	|		)
	|	)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДД.Регистратор,
	|	ДД.ВидЗапасов,
	|	ДД.ТипЗапасов,
	|	ДД.БазаРаспределения,
	|	Данные.Подразделение,
	|	Данные.ПартияПроизводства,
	|	Данные.ВидДеятельностиНДС,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.РаспределениеВозвратныхОтходов)
	|			И &АналитическийУчетПоГруппамПродукции
	|			И НЕ ДД.Номенклатура.ГруппаАналитическогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА ДД.Номенклатура.ГруппаАналитическогоУчета
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.РаспределениеВозвратныхОтходов)
	|			ТОГДА ДД.КорАналитикаУчетаНоменклатуры
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	Данные.Назначение,
	|	Данные.Продукция,
	|	Данные.ГруппаПродукции
	|
	// по указанным трудозатратам
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДД.Регистратор             КАК Регистратор,
	|	ДД.ВидЗапасов              КАК ВидЗапасов,
	|	ДД.ТипЗапасов              КАК ТипЗапасов,
	|	ДД.БазаРаспределения       КАК БазаРаспределения,
	|	Данные.Подразделение       КАК Подразделение,
	|	Данные.ПартияПроизводства  КАК ПартияПроизводства,
	|	Данные.ВидДеятельностиНДС  КАК ВидДеятельностиНДС,
	|	Данные.ГруппаПродукции     КАК АналитикаФинансовогоУчета,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.РаспределениеВозвратныхОтходов)
	|			И &АналитическийУчетПоГруппамПродукции
	|			И НЕ ДД.Номенклатура.ГруппаАналитическогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА ДД.Номенклатура.ГруппаАналитическогоУчета
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                      КАК КорАналитикаФинансовогоУчета,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.РаспределениеВозвратныхОтходов)
	|			ТОГДА ДД.КорАналитикаУчетаНоменклатуры
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                      КАК КорАналитикаУчетаНоменклатуры,
	|	Данные.Назначение          КАК Назначение,
	|	НЕОПРЕДЕЛЕНО               КАК Материал,
	|	НЕОПРЕДЕЛЕНО               КАК Продукция,
	|	Данные.ГруппаПродукции     КАК ГруппаПродукции,
	|	Данные.ВидРабот            КАК ВидРабот,
	|	СУММА(ВЫБОР ДД.БазаРаспределения
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоРаботУказанныхВидов) ТОГДА Данные.Количество
	|	КОНЕЦ)                     КАК Знаменатель,
	|	""ВидРабот""               КАК ВидПоказателя
	|ИЗ
	|	Распределить КАК ДД
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеПоТрудозатратам КАК Данные
	|		ПО Данные.Организация = ДД.Организация
	|		И Данные.Подразделение = ДД.Подразделение
	|		И Данные.ВидРабот = ДД.ВидРабот
	|		И (Данные.ГруппаПродукции = ДД.ГруппаПродукции ИЛИ ДД.ПоВсемГруппамПродукции)
	|		И (Данные.Назначение = ДД.Назначение
	|			ИЛИ Данные.НаправлениеДеятельности = ЕСТЬNULL(ДД.Назначение.НаправлениеДеятельности, НЕОПРЕДЕЛЕНО)
	|			ИЛИ ВЫРАЗИТЬ(Данные.Назначение КАК Справочник.Назначения).НаправлениеДеятельности = ДД.Назначение.НаправлениеДеятельности
	|			ИЛИ ДД.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка))
	|
	|ГДЕ
	|	ДД.БазаРаспределения = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоРаботУказанныхВидов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДД.Регистратор,
	|	ДД.ВидЗапасов,
	|	ДД.ТипЗапасов,
	|	ДД.БазаРаспределения,
	|	Данные.Подразделение,
	|	Данные.ПартияПроизводства,
	|	Данные.ВидДеятельностиНДС,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.РаспределениеВозвратныхОтходов)
	|			И &АналитическийУчетПоГруппамПродукции
	|			И НЕ ДД.Номенклатура.ГруппаАналитическогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА ДД.Номенклатура.ГруппаАналитическогоУчета
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.РаспределениеВозвратныхОтходов)
	|			ТОГДА ДД.КорАналитикаУчетаНоменклатуры
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	Данные.Назначение,
	|	Данные.ВидРабот,
	|	Данные.ГруппаПродукции
	|
	// по нормативам
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДД.Регистратор             КАК Регистратор,
	|	ДД.ВидЗапасов              КАК ВидЗапасов,
	|	ДД.ТипЗапасов              КАК ТипЗапасов,
	|	ДД.БазаРаспределения       КАК БазаРаспределения,
	|	Данные.Подразделение       КАК Подразделение,
	|	Данные.ПартияПроизводства  КАК ПартияПроизводства,
	|	Данные.ВидДеятельностиНДС  КАК ВидДеятельностиНДС,
	|	Данные.ГруппаПродукции     КАК АналитикаФинансовогоУчета,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.РаспределениеВозвратныхОтходов)
	|			И &АналитическийУчетПоГруппамПродукции
	|			И НЕ ДД.Номенклатура.ГруппаАналитическогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА ДД.Номенклатура.ГруппаАналитическогоУчета
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                      КАК КорАналитикаФинансовогоУчета,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.РаспределениеВозвратныхОтходов)
	|			ТОГДА ДД.КорАналитикаУчетаНоменклатуры
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                      КАК КорАналитикаУчетаНоменклатуры,
	|	Данные.Назначение          КАК Назначение,
	|	Данные.Материал            КАК Материал,
	|	НЕОПРЕДЕЛЕНО               КАК Продукция,
	|	Данные.ГруппаПродукции     КАК ГруппаПродукции,
	|	НЕОПРЕДЕЛЕНО               КАК ВидРабот,
	|	СУММА(ВЫБОР ДД.БазаРаспределения
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.НормативРасходаМатериала) ТОГДА Данные.Количество
	|	КОНЕЦ)                     КАК Знаменатель,
	|	""Материал""               КАК ВидПоказателя
	|ИЗ
	|	Распределить КАК ДД
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеПоНормативамРасходаМатериала КАК Данные
	|		ПО Данные.Организация = ДД.Организация
	|		И Данные.Подразделение = ДД.Подразделение
	|		И Данные.Материал = ДД.Номенклатура
	|		И (Данные.ГруппаПродукции = ДД.ГруппаПродукции ИЛИ ДД.ПоВсемГруппамПродукции)
	|	
	//++ НЕ УТКА
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПартииДавальческогоПроизводства КАК ПартииДавальцев
	|	ПО Данные.ПартияПроизводства = ПартииДавальцев.ПартияПроизводства
	//-- НЕ УТКА
	|
	|ГДЕ
	|	ДД.БазаРаспределения = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.НормативРасходаМатериала)
	|	И ТИПЗНАЧЕНИЯ(Данные.ПартияПроизводства.Документ) В (
	//++ НЕ УТКА
	|														ТИП(Документ.ЗаказНаПроизводство2_2),
	//-- НЕ УТКА
	|														ТИП(Документ.ПроизводствоБезЗаказа)
	|														)
	|	И ((Данные.Назначение = ДД.Назначение
	|		ИЛИ ВЫРАЗИТЬ(Данные.Назначение КАК Справочник.Назначения).НаправлениеДеятельности = ДД.Назначение.НаправлениеДеятельности)
	//++ НЕ УТКА
	|			И ПартииДавальцев.ПартияПроизводства ЕСТЬ NULL
	//-- НЕ УТКА
	|		ИЛИ ДД.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка))
	|
	|СГРУППИРОВАТЬ ПО
	|	ДД.Регистратор,
	|	ДД.ВидЗапасов,
	|	ДД.ТипЗапасов,
	|	ДД.БазаРаспределения,
	|	Данные.Подразделение,
	|	Данные.ПартияПроизводства,
	|	Данные.ВидДеятельностиНДС,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.РаспределениеВозвратныхОтходов)
	|			И &АналитическийУчетПоГруппамПродукции
	|			И НЕ ДД.Номенклатура.ГруппаАналитическогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА ДД.Номенклатура.ГруппаАналитическогоУчета
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.РаспределениеВозвратныхОтходов)
	|			ТОГДА ДД.КорАналитикаУчетаНоменклатуры
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	Данные.Назначение,
	|	Данные.Материал,
	|	Данные.ГруппаПродукции
	|
	//++ НЕ УТКА

	// по указанным материалам с учетом назначений давальческой схемы
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДД.Регистратор              КАК Регистратор,
	|	ДД.ВидЗапасов               КАК ВидЗапасов,
	|	ДД.ТипЗапасов               КАК ТипЗапасов,
	|	ДД.БазаРаспределения        КАК БазаРаспределения,
	|	Данные.Подразделение        КАК Подразделение,
	|	Данные.ПартияПроизводства   КАК ПартияПроизводства,
	|	Данные.ВидДеятельностиНДС   КАК ВидДеятельностиНДС,
	|	Данные.ГруппаПродукции      КАК АналитикаФинансовогоУчета,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.РаспределениеВозвратныхОтходов)
	|			И &АналитическийУчетПоГруппамПродукции
	|			И НЕ ДД.Номенклатура.ГруппаАналитическогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА ДД.Номенклатура.ГруппаАналитическогоУчета
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                       КАК КорАналитикаФинансовогоУчета,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.РаспределениеВозвратныхОтходов)
	|			ТОГДА ДД.КорАналитикаУчетаНоменклатуры
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                       КАК КорАналитикаУчетаНоменклатуры,
	|	Данные.Назначение           КАК Назначение,
	|	Данные.Материал             КАК Материал,
	|	НЕОПРЕДЕЛЕНО                КАК Продукция,
	|	Данные.ГруппаПродукции      КАК ГруппаПродукции,
	|	НЕОПРЕДЕЛЕНО                КАК ВидРабот,
	|	СУММА(ВЫБОР ДД.БазаРаспределения
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоУказанныхМатериалов) ТОГДА Данные.Количество
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ОбъемУказанныхМатериалов) ТОГДА Данные.Объем
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВесУказанныхМатериалов) ТОГДА Данные.Вес
	|	КОНЕЦ)                      КАК Знаменатель,
	|	""Материал""                КАК ВидПоказателя
	|ИЗ
	|	Распределить КАК ДД
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ НазначенияДавальческихМатериалов КАК НазначенияМатериалов
	|		ПО (НазначенияМатериалов.НазначениеМатериала = ДД.Назначение
	|			ИЛИ НазначенияМатериалов.Партнер = ДД.Назначение.Партнер
	|			ИЛИ НазначенияМатериалов.Договор = ДД.Назначение.Договор)
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПартииДавальческогоПроизводства КАК ПартииДавальцев
	|		ПО НазначенияМатериалов.Партнер = ПартииДавальцев.Партнер
	|		И НазначенияМатериалов.Договор = ПартииДавальцев.Договор
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеПоМатериальнымЗатратам КАК Данные
	|		ПО Данные.Организация = ДД.Организация
	|		И Данные.Подразделение = ДД.Подразделение
	|		И Данные.Материал = ДД.Материал
	|		И Данные.ПартияПроизводства = ПартииДавальцев.ПартияПроизводства
	|		И (Данные.ГруппаПродукции = ДД.ГруппаПродукции ИЛИ ДД.ПоВсемГруппамПродукции)
	|
	|ГДЕ
	|	ДД.БазаРаспределения = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоУказанныхМатериалов)
	|	ИЛИ ДД.БазаРаспределения = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ОбъемУказанныхМатериалов)
	|	ИЛИ ДД.БазаРаспределения = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВесУказанныхМатериалов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДД.Регистратор,
	|	ДД.ВидЗапасов,
	|	ДД.ТипЗапасов,
	|	ДД.БазаРаспределения,
	|	Данные.Подразделение,
	|	Данные.ПартияПроизводства,
	|	Данные.ВидДеятельностиНДС,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.РаспределениеВозвратныхОтходов)
	|			И &АналитическийУчетПоГруппамПродукции
	|			И НЕ ДД.Номенклатура.ГруппаАналитическогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА ДД.Номенклатура.ГруппаАналитическогоУчета
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.РаспределениеВозвратныхОтходов)
	|			ТОГДА ДД.КорАналитикаУчетаНоменклатуры
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	Данные.Назначение,
	|	Данные.Материал,
	|	Данные.ГруппаПродукции
	|
	// по нормативам с учетом назначений давальческой схемы
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДД.Регистратор             КАК Регистратор,
	|	ДД.ВидЗапасов              КАК ВидЗапасов,
	|	ДД.ТипЗапасов              КАК ТипЗапасов,
	|	ДД.БазаРаспределения       КАК БазаРаспределения,
	|	Данные.Подразделение       КАК Подразделение,
	|	Данные.ПартияПроизводства  КАК ПартияПроизводства,
	|	Данные.ВидДеятельностиНДС  КАК ВидДеятельностиНДС,
	|	Данные.ГруппаПродукции     КАК АналитикаФинансовогоУчета,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.РаспределениеВозвратныхОтходов)
	|			И &АналитическийУчетПоГруппамПродукции
	|			И НЕ ДД.Номенклатура.ГруппаАналитическогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА ДД.Номенклатура.ГруппаАналитическогоУчета
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                      КАК КорАналитикаФинансовогоУчета,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.РаспределениеВозвратныхОтходов)
	|			ТОГДА ДД.КорАналитикаУчетаНоменклатуры
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                      КАК КорАналитикаУчетаНоменклатуры,
	|	Данные.Назначение          КАК Назначение,
	|	Данные.Материал            КАК Материал,
	|	НЕОПРЕДЕЛЕНО               КАК Продукция,
	|	Данные.ГруппаПродукции     КАК ГруппаПродукции,
	|	НЕОПРЕДЕЛЕНО               КАК ВидРабот,
	|	СУММА(ВЫБОР ДД.БазаРаспределения
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.НормативРасходаМатериала) ТОГДА Данные.Количество
	|	КОНЕЦ)                     КАК Знаменатель,
	|	""Материал""               КАК ВидПоказателя
	|ИЗ
	|	Распределить КАК ДД
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ НазначенияДавальческихМатериалов КАК НазначенияМатериалов
	|		ПО (НазначенияМатериалов.НазначениеМатериала = ДД.Назначение
	|			ИЛИ НазначенияМатериалов.Партнер = ДД.Назначение.Партнер
	|			ИЛИ НазначенияМатериалов.Договор = ДД.Назначение.Договор)
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПартииДавальческогоПроизводства КАК ПартииДавальцев
	|		ПО НазначенияМатериалов.Партнер = ПартииДавальцев.Партнер
	|		И НазначенияМатериалов.Договор = ПартииДавальцев.Договор
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеПоНормативамРасходаМатериала КАК Данные
	|		ПО Данные.Организация = ДД.Организация
	|		И Данные.Подразделение = ДД.Подразделение
	|		И Данные.Материал = ДД.Номенклатура
	|		И Данные.ПартияПроизводства = ПартииДавальцев.ПартияПроизводства
	|		И (Данные.ГруппаПродукции = ДД.ГруппаПродукции ИЛИ ДД.ПоВсемГруппамПродукции)
	|
	|ГДЕ
	|	ДД.БазаРаспределения = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.НормативРасходаМатериала)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДД.Регистратор,
	|	ДД.ВидЗапасов,
	|	ДД.ТипЗапасов,
	|	ДД.БазаРаспределения,
	|	Данные.Подразделение,
	|	Данные.ПартияПроизводства,
	|	Данные.ВидДеятельностиНДС,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.РаспределениеВозвратныхОтходов)
	|			И &АналитическийУчетПоГруппамПродукции
	|			И НЕ ДД.Номенклатура.ГруппаАналитическогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА ДД.Номенклатура.ГруппаАналитическогоУчета
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.РаспределениеВозвратныхОтходов)
	|			ТОГДА ДД.КорАналитикаУчетаНоменклатуры
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	Данные.Назначение,
	|	Данные.Материал,
	|	Данные.ГруппаПродукции
	//-- НЕ УТКА
	|;
	|
	|ВЫБРАТЬ
	|	ДД.Регистратор,
	|	ДД.ВидЗапасов,
	|	ДД.ТипЗапасов,
	|	ДД.БазаРаспределения,
	|	ДД.Подразделение,
	|	ДД.ПартияПроизводства,
	|	ДД.ВидДеятельностиНДС,
	|	(ВЫБОР
	|		КОГДА ДД.АналитикаФинансовогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ДД.АналитикаФинансовогоУчета КОНЕЦ) КАК АналитикаФинансовогоУчета,
	|	ДД.КорАналитикаФинансовогоУчета,
	|	ДД.КорАналитикаУчетаНоменклатуры,
	|	ДД.Назначение,
	|	СУММА(ДД.Знаменатель)
	|ПОМЕСТИТЬ БазаРаспределения
	|ИЗ
	|	БазаРаспределенияДетально КАК ДД
	|
	|СГРУППИРОВАТЬ ПО
	|	ДД.Регистратор,
	|	ДД.ВидЗапасов,
	|	ДД.ТипЗапасов,
	|	ДД.БазаРаспределения,
	|	ДД.Подразделение,
	|	ДД.ПартияПроизводства,
	|	ДД.ВидДеятельностиНДС,
	|	(ВЫБОР
	|		КОГДА ДД.АналитикаФинансовогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ДД.АналитикаФинансовогоУчета КОНЕЦ), // АналитикаФинансовогоУчета
	|	ДД.КорАналитикаФинансовогоУчета,
	|	ДД.КорАналитикаУчетаНоменклатуры,
	|	ДД.Назначение
	|;
	|";
	
КонецФункции

// Таблицы: ОбщиеСуммы
Функция ТекстСуммыПоказателейРаспределенияНоменклатурыНаПроизводство() // вт ОбщиеСуммы
	Возврат "
	|ВЫБРАТЬ
	|	ДД.Регистратор        КАК Регистратор,
	|	ДД.ВидЗапасов         КАК ВидЗапасов,
	|	СУММА(ДД.Знаменатель) КАК Знаменатель
	|ПОМЕСТИТЬ ОбщиеСуммы
	|ИЗ
	|	БазаРаспределения КАК ДД
	|СГРУППИРОВАТЬ ПО
	|	ДД.Регистратор,
	|	ДД.ВидЗапасов
	|;
	|";
	
КонецФункции

// ... выборки данных

Функция ТекстКРаспределениюНоменклатурыНаПроизводство() // использует Распределить, ОбщиеСуммы
	
	Возврат "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ТекстКРаспределениюНоменклатурыНаПроизводство""                      КАК ЗапросИсточник,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия)                        КАК ТипЗаписи,
	|	ИСТИНА                                                                 КАК РасчетЗавершен,
	|	НЕОПРЕДЕЛЕНО                                                           КАК ВидДвижения,
	|	&КонецПериода                                                          КАК Период,
	|	ДД.Регистратор                                                         КАК Регистратор,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.РаспределениеВозвратныхОтходов)
	|			ТОГДА ДД.АналитикаУчетаНоменклатуры
	|		КОГДА ДД.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|			ТОГДА ДД.АналитикаКомитента
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			ТОГДА ДД.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ ДД.АналитикаУчетаНоменклатурыБезНазначения
	|	КОНЕЦ                                                                  КАК АналитикаУчетаНоменклатуры,
	|	ВЫБОР
	|		КОГДА ДД.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.МатериалДавальца)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку)
	|		КОГДА ДД.ТипЗапасов В (
	|				ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПолуфабрикатДавальца),
	|				ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПродукцияДавальца))
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение)
	|		КОГДА ДД.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|		КОГДА ДД.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ТоварНаХраненииСПравомПродажи)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаХраненииСПравомПродажи)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|	КОНЕЦ                                                                  КАК РазделУчета,
	|	ВЫБОР
	|		КОГДА ДД.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ДД.ВидЗапасов
	|	КОНЕЦ                                                                  КАК ВидЗапасов,
	|	ДД.Организация                                                         КАК Организация,
	|	НЕОПРЕДЕЛЕНО                                                           КАК Партия,
	|	НЕОПРЕДЕЛЕНО                                                           КАК АналитикаУчетаПартий,
	|	НЕОПРЕДЕЛЕНО                                                           КАК АналитикаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО                                                           КАК ВидДеятельностиНДС,
	|	Итог.Знаменатель                                                       КАК Знаменатель,
	|	ДД.Количество                                                          КАК Количество,
	|	ДД.Стоимость                                                           КАК Стоимость,
	|	ДД.СтоимостьРегл                                                       КАК СтоимостьРегл,
	|	ДД.ПостояннаяРазница                                                   КАК ПостояннаяРазница,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.РаспределениеВозвратныхОтходов)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)
	|			ИНАЧЕ
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовНаПартииПроизводства)
	|	КОНЕЦ 																   КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                                                           КАК КорРазделУчета,
	|	НЕОПРЕДЕЛЕНО                                                           КАК КорАналитикаУчетаНоменклатуры,
	|	ДД.ВидЗапасовПолучателя                                                КАК КорВидЗапасов,
	|	НЕОПРЕДЕЛЕНО                                                           КАК КорПартия,
	|	НЕОПРЕДЕЛЕНО                                                           КАК КорВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО                                                           КАК КорАналитикаФинансовогоУчета,
	|	ДД.АналитикаУчетаПоПартнерам                                           КАК АналитикаУчетаПоПартнерам,
	|	НЕОПРЕДЕЛЕНО                                                           КАК ИдентификаторСтроки,
	|	НЕОПРЕДЕЛЕНО                                                           КАК СтатьяРасходовСписания,
	|	НЕОПРЕДЕЛЕНО                                                           КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                                                           КАК СтатьяАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО                                                           КАК АналитикаАктивовПассивов,
	|	ДД.Номенклатура                                                        КАК Номенклатура,
	|	ДД.Характеристика                                                      КАК Характеристика,
	|	ДД.Серия                                                               КАК Серия,
	|	ДД.Подразделение                                                       КАК Склад,
	|	НЕОПРЕДЕЛЕНО                                                           КАК Назначение,
	|	ДД.СтатьяКалькуляции                                                   КАК СтатьяКалькуляции,
	|	НастройкаХозОперации.Ссылка                                            КАК НастройкаХозяйственнойОперации,
	|	ДД.ИдентификаторФинЗаписи                                              КАК ИдентификаторФинЗаписи
	|ИЗ
	|	Распределить КАК ДД
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОбщиеСуммы КАК Итог
	|		ПО Итог.Регистратор = ДД.Регистратор
	|		И Итог.ВидЗапасов = ДД.ВидЗапасов
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НастройкиХозяйственныхОпераций КАК НастройкаХозОперации
	|		ПО НастройкаХозОперации.ХозяйственнаяОперация = 
	|		(ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.РаспределениеВозвратныхОтходов)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовНаПартииПроизводства) КОНЕЦ)
	|";
	
КонецФункции

Функция ТекстРасходыСебестоимостиНаПроизводство() // использует БазаРаспределения
	
	Возврат "
	|ВЫБРАТЬ
	|	""ТекстРасходыСебестоимостиНаПроизводство""                            КАК ЗапросИсточник,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ПотреблениеАвто)               КАК ТипЗаписи,
	|	ЛОЖЬ                                                                   КАК РасчетЗавершен,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)                                 КАК ВидДвижения,
	|	&КонецПериода                                                          КАК Период,
	|	ДД.Регистратор                                                         КАК Регистратор,
	|	НЕОПРЕДЕЛЕНО                                                           КАК АналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО                                                           КАК РазделУчета,
	|	ВЫБОР
	|		КОГДА ДД.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ДД.ВидЗапасов
	|	КОНЕЦ                                                                  КАК ВидЗапасов,
	|	НЕОПРЕДЕЛЕНО                                                           КАК Организация,
	|	НЕОПРЕДЕЛЕНО                                                           КАК Партия,
	|	НЕОПРЕДЕЛЕНО                                                           КАК АналитикаУчетаПартий,
	|	НЕОПРЕДЕЛЕНО                                                           КАК АналитикаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО                                                           КАК ВидДеятельностиНДС,
	|	ДД.Знаменатель                                                         КАК Знаменатель,
	|	0                                                                      КАК Количество,
	|	0                                                                      КАК Стоимость,
	|	0                                                                      КАК СтоимостьРегл,
	|	0                                                                      КАК ПостояннаяРазница,
	|	НЕОПРЕДЕЛЕНО                                                           КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                                                           КАК КорРазделУчета,
	|	НЕОПРЕДЕЛЕНО                                                           КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО                                                           КАК КорВидЗапасов,
	|	ДД.ПартияПроизводства                                                  КАК КорПартия,
	|	ДД.ВидДеятельностиНДС                                                  КАК КорВидДеятельностиНДС,
	|	ДД.АналитикаФинансовогоУчета                                           КАК КорАналитикаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО                                                           КАК АналитикаУчетаПоПартнерам,
	|	НЕОПРЕДЕЛЕНО                                                           КАК ИдентификаторСтроки,
	|	НЕОПРЕДЕЛЕНО                                                           КАК СтатьяРасходовСписания,
	|	НЕОПРЕДЕЛЕНО                                                           КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                                                           КАК СтатьяАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО                                                           КАК АналитикаАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО                                                           КАК Номенклатура,
	|	НЕОПРЕДЕЛЕНО                                                           КАК Характеристика,
	|	НЕОПРЕДЕЛЕНО                                                           КАК Серия,
	|	ДД.Подразделение                                                       КАК Склад,
	|	ДД.Назначение                                                          КАК Назначение,
	|	НЕОПРЕДЕЛЕНО                                                           КАК СтатьяКалькуляции,
	|	НЕОПРЕДЕЛЕНО                                                           КАК НастройкаХозяйственнойОперации,
	|	""""                                                                   КАК ИдентификаторФинЗаписи
	|ИЗ
	|	БазаРаспределения КАК ДД
	|";
	
КонецФункции

Функция ТекстПриходыСебестоимостиНаПроизводство() // использует БазаРаспределения
	
	Возврат "
	|ВЫБРАТЬ
	|	""ТекстПриходыСебестоимостиНаПроизводство""                            КАК ЗапросИсточник,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ПеремещениеАвто)               КАК ТипЗаписи,
	|	ЛОЖЬ                                                                   КАК РасчетЗавершен,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                                 КАК ВидДвижения,
	|	&КонецПериода                                                          КАК Период,
	|	ДД.Регистратор                                                         КАК Регистратор,
	|	НЕОПРЕДЕЛЕНО                                                           КАК АналитикаУчетаНоменклатуры,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство) КАК РазделУчета,
	|	ВЫБОР
	|		КОГДА ДД.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ДД.ВидЗапасов
	|	КОНЕЦ                                                                  КАК ВидЗапасов,
	|	НЕОПРЕДЕЛЕНО                                                           КАК Организация,
	|	ДД.ПартияПроизводства                                                  КАК Партия,
	|	НЕОПРЕДЕЛЕНО                                                           КАК АналитикаУчетаПартий,
	|	ДД.АналитикаФинансовогоУчета                                           КАК АналитикаФинансовогоУчета,
	|	ДД.ВидДеятельностиНДС                                                  КАК ВидДеятельностиНДС,
	|	ДД.Знаменатель                                                         КАК Знаменатель,
	|	0                                                                      КАК Количество,
	|	0                                                                      КАК Стоимость,
	|	0                                                                      КАК СтоимостьРегл,
	|	0                                                                      КАК ПостояннаяРазница,
	|	НЕОПРЕДЕЛЕНО                                                           КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                                                           КАК КорРазделУчета,
	|	ДД.КорАналитикаУчетаНоменклатуры                                       КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО                                                           КАК КорВидЗапасов,
	|	НЕОПРЕДЕЛЕНО                                                           КАК КорПартия,
	|	НЕОПРЕДЕЛЕНО                                                           КАК КорВидДеятельностиНДС,
	|	ДД.КорАналитикаФинансовогоУчета                                        КАК КорАналитикаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО                                                           КАК АналитикаУчетаПоПартнерам,
	|	НЕОПРЕДЕЛЕНО                                                           КАК ИдентификаторСтроки,
	
	|	НЕОПРЕДЕЛЕНО                                                           КАК СтатьяРасходовСписания,
	|	НЕОПРЕДЕЛЕНО                                                           КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                                                           КАК СтатьяАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО                                                           КАК АналитикаАктивовПассивов,
	
	|	НЕОПРЕДЕЛЕНО                                                           КАК Номенклатура,
	|	НЕОПРЕДЕЛЕНО                                                           КАК Характеристика,
	|	НЕОПРЕДЕЛЕНО                                                           КАК Серия,
	|	ДД.Подразделение                                                       КАК Склад,
	|	ДД.Назначение                                                          КАК Назначение,
	|	НЕОПРЕДЕЛЕНО                                                           КАК СтатьяКалькуляции,
	|	НЕОПРЕДЕЛЕНО                                                           КАК НастройкаХозяйственнойОперации,
	|	""""                                                                   КАК ИдентификаторФинЗаписи
	|ИЗ
	|	БазаРаспределения КАК ДД
	|";
	
КонецФункции

// Заполнение расчетной партии

Процедура ЗаполнитьРасчетнуюПартиюРаспределенияНоменклатурыНаПроизводство(ПараметрыРасчета, РасчетнаяПартия, Расход, Приход)
	
	ЗаполнитьЗначенияСвойств(РасчетнаяПартия, Расход);
	
	Если Приход.Знаменатель = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если РасчетнаяПартия.ТипЗаписи = Перечисления.ТипыЗаписейПартий.ПотреблениеАвто Тогда
		
		// расчетная партия заполняется на наибольшее общее вычитаемое
		ЗнаменательСписания = Мин(Расход.Знаменатель, Приход.Знаменатель);
		
		// заполняем показатели расчетной партии
		Если ЗнаменательСписания = Приход.Знаменатель Тогда

			РасчетнаяПартия.Количество = Приход.Количество;
			РасчетнаяПартия.Стоимость = Приход.Стоимость;
			РасчетнаяПартия.СтоимостьРегл = Приход.СтоимостьРегл;
			РасчетнаяПартия.ПостояннаяРазница = Приход.ПостояннаяРазница;
			
		Иначе
			
			РасчетнаяПартия.Количество = Окр(ЗнаменательСписания * Приход.Количество / Приход.Знаменатель, 3);
			РасчетнаяПартия.Стоимость = Окр(ЗнаменательСписания * Приход.Стоимость / Приход.Знаменатель, 2);
			РасчетнаяПартия.СтоимостьРегл = Окр(ЗнаменательСписания * Приход.СтоимостьРегл / Приход.Знаменатель, 2);
			РасчетнаяПартия.ПостояннаяРазница = Окр(ЗнаменательСписания * Приход.ПостояннаяРазница / Приход.Знаменатель, 2);
			
		КонецЕсли;
		
		// корректируем базу расчета в приходе
		Приход.Знаменатель = Приход.Знаменатель - ЗнаменательСписания;
		Приход.Количество  = Приход.Количество - РасчетнаяПартия.Количество;
		Приход.Стоимость   = Приход.Стоимость - РасчетнаяПартия.Стоимость;
		Приход.СтоимостьРегл = Приход.СтоимостьРегл - РасчетнаяПартия.СтоимостьРегл;
		Приход.ПостояннаяРазница = Приход.ПостояннаяРазница - РасчетнаяПартия.ПостояннаяРазница;
		
		ЗаполнитьЗначенияСвойств(РасчетнаяПартия, Приход, 
			"РасчетЗавершен, РазделУчета, Организация, АналитикаУчетаНоменклатуры, АналитикаУчетаПоПартнерам, Номенклатура, 
			|Характеристика, СтатьяКалькуляции, Серия, ВидЗапасов, КорВидЗапасов, ХозяйственнаяОперация,
			|НастройкаХозяйственнойОперации, ИдентификаторФинЗаписи");
		
		Если РасчетнаяПартия.РазделУчета = Перечисления.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты
			Или РасчетнаяПартия.РазделУчета = Перечисления.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах
			Или РасчетнаяПартия.РазделУчета = Перечисления.РазделыУчетаСебестоимостиТоваров.ТоварыНаХраненииСПравомПродажи Тогда
			РасчетнаяПартия.КорРазделУчета = Перечисления.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство;
		ИначеЕсли РасчетнаяПартия.РазделУчета = Перечисления.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение Тогда
			РасчетнаяПартия.КорРазделУчета = Перечисления.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение;
		ИначеЕсли РасчетнаяПартия.РазделУчета = Перечисления.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку Тогда
			РасчетнаяПартия.КорРазделУчета = Перечисления.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку;
		КонецЕсли;
		
		Если ТипЗнч(РасчетнаяПартия.Регистратор) = Тип("ДокументСсылка.РаспределениеВозвратныхОтходов") Тогда
			РасчетнаяПартия.РазделУчета = Перечисления.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка();
		КонецЕсли;
		
		Если Не ПараметрыРасчета.УчитыватьСебестоимостьТоваровПоНазначениям Тогда
			РасчетнаяПартия.Назначение = Справочники.Назначения.ПустаяСсылка();
		КонецЕсли;
		
		РасчетнаяПартия.КорАналитикаУчетаНоменклатуры = РегистрыСведений.АналитикаУчетаНоменклатуры.ЗначениеКлючаАналитики(РасчетнаяПартия);
		Если Не ЗначениеЗаполнено(РасчетнаяПартия.ИдентификаторФинЗаписи) Тогда
			РасчетнаяПартия.ИдентификаторФинЗаписи = Строка(Новый УникальныйИдентификатор);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(РасчетнаяПартия.НастройкаХозяйственнойОперации) Тогда
			РасчетнаяПартия.НастройкаХозяйственнойОперации = Справочники.НастройкиХозяйственныхОпераций.СписаниеРасходовНаПартииПроизводства;
		КонецЕсли;
		
	Иначе
		
		ЗаполнитьЗначенияСвойств(РасчетнаяПартия, Приход, "РасчетЗавершен, Организация, ХозяйственнаяОперация, 
			|НастройкаХозяйственнойОперации, ИдентификаторФинЗаписи,
			|Количество, Стоимость, СтоимостьРегл, ПостояннаяРазница");
		
		РасчетнаяПартия.АналитикаУчетаНоменклатуры    = Приход.КорАналитикаУчетаНоменклатуры;
		РасчетнаяПартия.ВидЗапасов                    = Приход.КорВидЗапасов;
		РасчетнаяПартия.РазделУчета                   = Приход.КорРазделУчета;
		
		Приход.Знаменатель = 0;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыЭтапа5_РаспределениеНоменклатурыНаВыпуск

// Инициализация данных

Функция ПоляПотребленийРаспределенияНоменклатурыНаВыпуск(ПараметрыРасчета)
	
	Возврат "Организация, Партия, Номенклатура, Характеристика, Подразделение, Назначение, СтатьяКалькуляции,
		| ПобочныеИзделия";
	
КонецФункции

Функция ОписаниеЦепочекРаспределенияНоменклатурыНаВыпуск(ПараметрыРасчета)
	
	ОписаниеЦепочек = Новый Соответствие;
	ПоляПотреблений = ПоляПотребленийРаспределенияНоменклатурыНаВыпуск(ПараметрыРасчета);
	
	Партия          = Перечисления.ТипыЗаписейПартий.Партия;
	Расчетное       = Перечисления.ТипыЗаписейПартий.Расчетное;
	СписанияНаВыпуск = Перечисления.ТипыЗаписейПартий.СписанияНаВыпуск;
	
	// Расчетное <- Партия
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьОписаниеПриемника(ОписаниеЦепочек, Расчетное, ПоляПотреблений);
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьОписаниеИсточника(ОписаниеЦепочек, Расчетное, Партия, ПоляПотреблений);
	
	// ПеремещениеАвто <- Расчетное
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьОписаниеПриемника(ОписаниеЦепочек, СписанияНаВыпуск,
		"Партия, Организация");
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьОписаниеИсточника(ОписаниеЦепочек, СписанияНаВыпуск, Расчетное,
		"Партия, Организация");
	
	// Дополнение <- (<без источников - для формирования записей по данным запроса>)
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьОписаниеДополнения(ОписаниеЦепочек, Перечисления.ТипыЗаписейПартий.Дополнение);
	
	Возврат ОписаниеЦепочек;
	
КонецФункции

Функция ОписаниеДвиженийРаспределенияНоменклатурыНаВыпуск(ПараметрыРасчета)
	
	КолонкиТаблицыРаспределения = ТаблицаДляРаспределенияНоменклатурыНаВыпуск(ПараметрыРасчета).Колонки;
	
	ОписаниеДвижений = РасчетСебестоимостиПрикладныеАлгоритмы.ОписаниеДвижений();
	ОписаниеДвижений.Вставить("Контекст", "РаспределениеНоменклатурыНаВыпуск");
	ОписаниеДвижений.Вставить("ИмяРегистра", Метаданные.РегистрыНакопления.СебестоимостьТоваров.Имя);
	ОписаниеДвижений.Вставить("ПоляРасчета", РасчетСебестоимостиПрикладныеАлгоритмы.ПереченьПолей(КолонкиТаблицыРаспределения));
	ОписаниеДвижений.Вставить("КлючиСравнения",	ПоляПотребленийРаспределенияНоменклатурыНаВыпуск(ПараметрыРасчета));
	ОписаниеДвижений.Вставить("Показатели", "Количество");
	ОписаниеДвижений.Вставить("БазисПрихода", "Знаменатель");
	ОписаниеДвижений.Вставить("БазисРасхода", "Знаменатель");
	ОписаниеДвижений.Вставить("ПолеПорядка", "Период");
	
	ОписаниеДвижений.Вставить("ЕстьСторно", Истина);
	
	Возврат ОписаниеДвижений;
	
КонецФункции

Функция ОписаниеНезаписываемыхРаспределенийНоменклатурыНаВыпуск(ПараметрыРасчета)
	
	НезаписываемыеТипыЗаписей = Новый Соответствие;
	НезаписываемыеТипыЗаписей.Вставить(Перечисления.ТипыЗаписейПартий.Партия, Истина);
	НезаписываемыеТипыЗаписей.Вставить(Перечисления.ТипыЗаписейПартий.Расчетное, Истина);
	
	Возврат РасчетСебестоимостиПрикладныеАлгоритмы.ОписаниеНезаписываемыхДанных(Ложь, НезаписываемыеТипыЗаписей);
	
КонецФункции

Функция ТаблицаДляРаспределенияНоменклатурыНаВыпуск(ПараметрыРасчета)
	
	Возврат РасчетСебестоимостиПрикладныеАлгоритмы.ТаблицаДляРаспределенияПартий(ПараметрыРасчета,
		ТекстОписаниеДанныхДляРаспределенияНоменклатурыНаВыпуск());
	
КонецФункции

// Выборка данных

Процедура ПолучитьДанныеДляРаспределенияНоменклатурыНаВыпуск(ПараметрыРасчета)
	
	ПараметрыНумерации = РасчетСебестоимостиПрикладныеАлгоритмы.СформироватьПараметрыНумерацииСтрокВременнойТаблицы(
		"Организация", // разделитель
		"Знаменатель, Количество", // ресурсы
		"Организация, Партия, АналитикаУчетаНоменклатуры"); // порядок
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПолучитьДанныеЭтапаРасчета(
		ПараметрыРасчета,
		ТекстЗапросаДляРаспределенияНоменклатурыНаВыпуск(),
		ПараметрыНумерации);
	
КонецПроцедуры

// Тексты запросов

// ... общий

Функция ТекстЗапросаДляРаспределенияНоменклатурыНаВыпуск() Экспорт
	
	ТекстЗапроса = ""
		// подготовка временных таблиц
		+ ТекстРасчетноеРаспределениеНоменклатурыНаВыпуск() // ОстаткиМатериалов, Расчетное
		// выборка данных
		+ ТекстОписаниеДанныхДляРаспределенияНоменклатурыНаВыпуск()
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстЗацикленнаяНоменклатураНаВыпуск()
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстНоменклатураКРаспределениюНаВыпуск() // использует ОстаткиМатериалов, ДолиВыпуска
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстРасчетноеКоличествоКРаспределению() // использует Расчетное
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстРаспределениеНоменклатурыНаВыпускПоПродукции() // использует Продукция
		+ "";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// ... описания данных

Функция ТекстОписаниеДанныхДляРаспределенияНоменклатурыНаВыпуск()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ ПЕРВЫЕ 0
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(80))          								 КАК ЗапросИсточник,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ПустаяСсылка)                КАК ТипЗаписи,
	|	ЛОЖЬ                                                                 КАК РасчетЗавершен,
	|	ЛОЖЬ                                                                 КАК ЭтоСторно,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
	|	ДД.ВидДвижения                                                       КАК ВидДвижения,
	|	ДД.Период                                                            КАК Период,
	|	ДД.Регистратор                                                       КАК Регистратор,
	|	ДД.АналитикаУчетаНоменклатуры                                        КАК АналитикаУчетаНоменклатуры,
	|	ДД.АналитикаУчетаНоменклатуры.Номенклатура                           КАК Номенклатура,
	|	ДД.АналитикаУчетаНоменклатуры.Характеристика                         КАК Характеристика,
	|	ДД.АналитикаУчетаНоменклатуры.Подразделение                          КАК Подразделение,
	|	ДД.АналитикаУчетаНоменклатуры.Назначение                             КАК Назначение,
	|	ДД.СтатьяКалькуляции                                                 КАК СтатьяКалькуляции,
	|	ДД.ВидЗапасов                                                        КАК ВидЗапасов,
	|	ДД.Организация                                                       КАК Организация,
	|	ДД.Партия                                                            КАК Партия,
	|	ДД.АналитикаУчетаПартий                                              КАК АналитикаУчетаПартий,
	|	ДД.АналитикаФинансовогоУчета                                         КАК АналитикаФинансовогоУчета,
	|	ДД.ВидДеятельностиНДС                                                КАК ВидДеятельностиНДС,
	|	0                                                                    КАК Знаменатель,
	|	0                                                                    КАК ДоляВыпуска,
	|	0                                                                    КАК Количество,
	|	ЛОЖЬ                                                                 КАК ПобочныеИзделия,
	|	ДД.ХозяйственнаяОперация                                             КАК ХозяйственнаяОперация,
	|	ДД.КорАналитикаУчетаНоменклатуры                                     КАК КорАналитикаУчетаНоменклатуры,
	|	ДД.КорРазделУчета                                                    КАК КорРазделУчета,
	|	ДД.КорВидЗапасов                                                     КАК КорВидЗапасов,
	|	ДД.СтатьяРасходовСписания                                            КАК СтатьяРасходовСписания,
	|	ДД.АналитикаРасходов                                                 КАК АналитикаРасходов,
	|	ДД.СтатьяАктивовПассивов                                             КАК СтатьяАктивовПассивов,
	|	ДД.АналитикаАктивовПассивов                                          КАК АналитикаАктивовПассивов,
	|	ДД.КорПартия                                                         КАК КорПартия,
	|	ДД.КорАналитикаУчетаПартий                                           КАК КорАналитикаУчетаПартий,
	|	ДД.КорВидДеятельностиНДС                                             КАК КорВидДеятельностиНДС,
	|	ДД.КорАналитикаФинансовогоУчета                                      КАК КорАналитикаФинансовогоУчета,
	|	ДД.ИдентификаторСтроки                                               КАК ИдентификаторСтроки,
	|	ДД.ДокументИсточник                                                  КАК ДокументИсточник,
	|	ДД.НастройкаХозяйственнойОперации                                    КАК НастройкаХозяйственнойОперации,
	|	ДД.ИдентификаторФинЗаписи                                            КАК ИдентификаторФинЗаписи
	|ПОМЕСТИТЬ Данные
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК ДД
	|";
	
	ТекстЗапроса = РасчетСебестоимостиПрикладныеАлгоритмы.ПодготовитьЗапросОписанияДанных(ТекстЗапроса);

	Возврат ТекстЗапроса;

КонецФункции

// ... формирования временные таблиц

// Таблицы: втОстаткиМатериалов, ОстаткиМатериалов, ОстаткиМатериаловСводно, втПланФактМатериалов,
//          ПланФактМатериалов, втСписаноВПрошлыхМесяцах, втРасчетноеКоличество, Расчетное.
Функция ТекстРасчетноеРаспределениеНоменклатурыНаВыпуск()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДД.Организация                                  КАК Организация,
	|	ДД.АналитикаУчетаНоменклатуры.Номенклатура      КАК Номенклатура,
	|	ДД.АналитикаУчетаНоменклатуры.Характеристика    КАК Характеристика,
	|	ДД.АналитикаУчетаНоменклатуры.Подразделение     КАК Подразделение,
	|	ДД.АналитикаУчетаНоменклатуры.Назначение        КАК Назначение,
	|	ДД.АналитикаУчетаНоменклатуры.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	ДД.ПартияПроизводства                           КАК ПартияПроизводства,
	|	ВЫБОР КОГДА ДД.Количество < 0
	|		ТОГДА -1
	|		ИНАЧЕ 1
	|	КОНЕЦ											КАК КоэффициентПриведения,
	|	СУММА(ДД.Количество)                            КАК Количество
	|ПОМЕСТИТЬ ОстаткиМатериаловСводно
	|ИЗ
	|	ОстаткиМатериалов КАК ДД
	|
	|СГРУППИРОВАТЬ ПО
	|	ДД.Организация,
	|	ДД.АналитикаУчетаНоменклатуры.Номенклатура,
	|	ДД.АналитикаУчетаНоменклатуры.Характеристика,
	|	ДД.АналитикаУчетаНоменклатуры.Подразделение,
	|	ДД.АналитикаУчетаНоменклатуры.Назначение,
	|	ДД.АналитикаУчетаНоменклатуры.СтатьяКалькуляции,
	|	ДД.ПартияПроизводства,
	|	ВЫБОР КОГДА ДД.Количество < 0 ТОГДА -1 ИНАЧЕ 1 КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПартияПроизводства,
	|	Номенклатура,
	|	Характеристика,
	|	Подразделение,
	|	Назначение,
	|	СтатьяКалькуляции,
	|	Организация,
	|	КоэффициентПриведения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// факт материалов и отходов текущего периода
	|ВЫБРАТЬ
	|	ПланФактМатериалов.Организация           КАК Организация,
	|	ПланФактМатериалов.ПартияПроизводства    КАК ПартияПроизводства,
	|	ПланФактМатериалов.Номенклатура          КАК Номенклатура,
	|	ПланФактМатериалов.Характеристика        КАК Характеристика,
	|	ПланФактМатериалов.Подразделение         КАК Подразделение,
	|	ПланФактМатериалов.Назначение            КАК Назначение,
	|	ПланФактМатериалов.СтатьяКалькуляции     КАК СтатьяКалькуляции,
	|	ПланФактМатериалов.КоэффициентПриведения КАК КоэффициентПриведения,
	|	СУММА(ПланФактМатериалов.План)           КАК План,
	|	СУММА(ПланФактМатериалов.Факт)           КАК Факт
	|ПОМЕСТИТЬ ПланФактМатериалов
	|ИЗ
	|	(ВЫБРАТЬ
	|		Материалы.Организация                                  КАК Организация,
	|		Материалы.Партия                                       КАК ПартияПроизводства,
	|		Материалы.АналитикаУчетаНоменклатуры.Номенклатура      КАК Номенклатура,
	|		Материалы.АналитикаУчетаНоменклатуры.Характеристика    КАК Характеристика,
	|		Материалы.АналитикаУчетаНоменклатуры.Подразделение     КАК Подразделение,
	|		Материалы.АналитикаУчетаНоменклатуры.Назначение        КАК Назначение,
	|		Материалы.АналитикаУчетаНоменклатуры.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|		0                                                      КАК План,
	|		ВЫБОР
	|			КОГДА Материалы.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)
	|				ТОГДА -1 * Материалы.Количество
	|			ИНАЧЕ Материалы.Количество
	|		КОНЕЦ                                                  КАК Факт,
	|		ВЫБОР
	|			КОГДА Материалы.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)
	|				ТОГДА -1
	|			ИНАЧЕ 1
	|		КОНЕЦ                                                  КАК КоэффициентПриведения
	|	ИЗ
	|		ПартииПроизводстваСводно КАК ДД
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТКэшРасчетныеОборотыСебестоимостьТоваров КАК Материалы
	|			ПО ДД.ПартияПроизводства = Материалы.Партия
	|			И ДД.Организация = Материалы.Организация
	|
	//++ Устарело_Переработка24
	|			ЛЕВОЕ СОЕДИНЕНИЕ РаспределенныеОтчеты КАК РаспределенныеОтчеты
	|			ПО Материалы.Регистратор = РаспределенныеОтчеты.ОтчетПереработчика
	|				И Материалы.Партия = РаспределенныеОтчеты.ПартияПроизводства
	//-- Устарело_Переработка24
	|	ГДЕ
	|		Материалы.СлужебноеВидДвиженияПриход
	//++ Устарело_Переработка24
	
	// Данные переработки выпускающего этапа на стороне исключаются, т.к. движения формируются отчетом без распределения.
	|		И РаспределенныеОтчеты.ОтчетПереработчика ЕСТЬ NULL
	//-- Устарело_Переработка24
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
		// факт материалов и отходов, распределенных по правилам
	|	ВЫБРАТЬ
	|		Материалы.Организация                                  КАК Организация,
	|		Материалы.Партия                                       КАК ПартияПроизводства,
	|		Материалы.АналитикаУчетаНоменклатуры.Номенклатура      КАК Номенклатура,
	|		Материалы.АналитикаУчетаНоменклатуры.Характеристика    КАК Характеристика,
	|		Материалы.АналитикаУчетаНоменклатуры.Подразделение     КАК Подразделение,
	|		Материалы.АналитикаУчетаНоменклатуры.Назначение        КАК Назначение,
	|		Материалы.АналитикаУчетаНоменклатуры.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|		0                                                      КАК План,
	|		ВЫБОР
	|			КОГДА Материалы.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)
	|				ТОГДА -1 * Материалы.Количество
	|			ИНАЧЕ Материалы.Количество
	|		КОНЕЦ                                                  КАК Факт,
	|		ВЫБОР
	|			КОГДА Материалы.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)
	|				ТОГДА -1
	|			ИНАЧЕ 1
	|		КОНЕЦ                                                  КАК КоэффициентПриведения
	|	ИЗ
	|		ПартииПроизводстваСводно КАК ДД
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПроизводственныеЗатратыСебестоимостьТоваров КАК Материалы
	|			ПО ДД.ПартияПроизводства = Материалы.Партия
	|			И ДД.Организация = Материалы.Организация
	|	ГДЕ
	|		Материалы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	//++ НЕ УТКА
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
		// план материалов этапов
	|	ВЫБРАТЬ
	|		ДД.Организация                КАК Организация,
	|		ДД.ПартияПроизводства         КАК ПартияПроизводства,
	|		Обеспечение.Номенклатура      КАК Номенклатура,
	|		Обеспечение.Характеристика    КАК Характеристика,
	|		Обеспечение.Подразделение     КАК Подразделение,
	|		ВЫБОР
	|			КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|				ТОГДА Обеспечение.Ссылка.Назначение
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|		КОНЕЦ                         КАК Назначение,
	|		Обеспечение.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|		Обеспечение.Количество        КАК План,
	|		0                             КАК Факт,
	|		1                             КАК КоэффициентПриведения
	|	ИЗ
	|		ПартииПроизводстваСводно КАК ДД
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ОбеспечениеМатериаламиИРаботами КАК Обеспечение
	|			ПО ДД.ПартияПроизводства = Обеспечение.Ссылка.ПартияПроизводства
	|
	|	ГДЕ
			// Данные переработки выпускающего этапа на стороне исключаются, т.к. движения формируются отчетом без распределения.
	|		НЕ Обеспечение.Ссылка.ПроизводствоНаСтороне
	|		И НЕ Обеспечение.Отменено
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
		// план материалов этапов, выполняющихся на стороне (переработка 2.5 по этапам)
	|	ВЫБРАТЬ
	|		ДД.Организация                КАК Организация,
	|		ДД.ПартияПроизводства         КАК ПартияПроизводства,
	|		Обеспечение.Номенклатура      КАК Номенклатура,
	|		Обеспечение.Характеристика    КАК Характеристика,
	|		ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
	|		ВЫБОР
	|			КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|				ТОГДА ДД.Назначение
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|		КОНЕЦ                         КАК Назначение,
	|		Обеспечение.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|		Обеспечение.Количество        КАК План,
	|		0                             КАК Факт,
	|		1                             КАК КоэффициентПриведения
	|	ИЗ
	|		ПартииПроизводстваПоЭтапам КАК ДД
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ОбеспечениеМатериаламиИРаботами КАК Обеспечение
	|			ПО ДД.Этап = Обеспечение.Ссылка
	|
	|	ГДЕ
	|		Обеспечение.Ссылка.ПроизводствоНаСтороне2_5
	|		И НЕ Обеспечение.Отменено
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
		// план услуг по переработке в заказах переработчика (переработка 2.5 по этапам)
	|	ВЫБРАТЬ
	|		ДД.Организация                КАК Организация,
	|		ДД.ПартияПроизводства         КАК ПартияПроизводства,
	|		Услуги.Номенклатура			КАК Номенклатура,
	|		Услуги.Характеристика    КАК Характеристика,
	|		ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
	|		ВЫБОР
	|			КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|				ТОГДА ДД.Назначение
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|		КОНЕЦ                         КАК Назначение,
	|		Услуги.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|		Услуги.Количество        КАК План,
	|		0                             КАК Факт,
	|		1                             КАК КоэффициентПриведения
	|	ИЗ
	|		ПартииПроизводстваПоЭтапам КАК ДД
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказПереработчику2_5.Услуги КАК Услуги
	|			ПО ДД.Этап = Услуги.Ссылка.Распоряжение
	|
	|	ГДЕ
	|		НЕ Услуги.Отменено
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
		// план отходов этапов цепочки
	|	ВЫБРАТЬ
	|		ДД.Организация                  КАК Организация,
	|		ДД.ПартияПроизводства           КАК ПартияПроизводства,
	|		ТаблицаТовары.Номенклатура      КАК Номенклатура,
	|		ТаблицаТовары.Характеристика    КАК Характеристика,
	|		ТаблицаТовары.Подразделение     КАК Подразделение,
	|		ВЫБОР
	|			КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|				ТОГДА ТаблицаТовары.Ссылка.Назначение
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|		КОНЕЦ                           КАК Назначение,
	|		ТаблицаТовары.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|		ТаблицаТовары.Количество        КАК План,
	|		0                               КАК Факт,
	|		-1                              КАК КоэффициентПриведения
	|	ИЗ
	|		ПартииПроизводстваСводно КАК ДД
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ПобочныеИзделия КАК ТаблицаТовары
	|			ПО ДД.ПартияПроизводства = ТаблицаТовары.Ссылка.ПартияПроизводства
	|
	|	ГДЕ
			// Данные переработки выпускающего этапа на стороне исключаются, т.к. движения формируются отчетом без распределения.
	|		НЕ ТаблицаТовары.Ссылка.ПроизводствоНаСтороне
	|		И НЕ ТаблицаТовары.Отменено
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	// план отходов этапов других цепочек
	|	ВЫБРАТЬ
	|		ДД.Организация                  КАК Организация,
	|		ДД.ПартияПроизводства           КАК ПартияПроизводства,
	|		ТаблицаТовары.Номенклатура      КАК Номенклатура,
	|		ТаблицаТовары.Характеристика    КАК Характеристика,
	|		ТаблицаТовары.Подразделение     КАК Подразделение,
	|		ВЫБОР
	|			КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|				ТОГДА ТаблицаТовары.Ссылка.Назначение
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|		КОНЕЦ                           КАК Назначение,
	|		ТаблицаТовары.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|		ТаблицаТовары.Количество        КАК План,
	|		0                               КАК Факт,
	|		-1                              КАК КоэффициентПриведения
	|	ИЗ
	|		ПартииПроизводстваСводно КАК ДД
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК ЭтапыЦепочки
	|			ПО ДД.ПартияПроизводства = ЭтапыЦепочки.ПартияПроизводства
	|
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ПобочныеИзделия КАК ТаблицаТовары
	|			ПО ЭтапыЦепочки.Ссылка = ТаблицаТовары.ЭтапПотребитель
	|
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК ЭтапыДругихЦепочек
	|			ПО ЭтапыДругихЦепочек.Ссылка = ТаблицаТовары.Ссылка
	|
	|	ГДЕ
	// Данные переработки выпускающего этапа на стороне исключаются, т.к. движения формируются отчетом без распределения.
	|		НЕ ТаблицаТовары.Ссылка.ПроизводствоНаСтороне
	|		И НЕ ТаблицаТовары.Отменено
	|		И НЕ ЭтапыЦепочки.ПартияПроизводства = ЭтапыДругихЦепочек.ПартияПроизводства
	//-- НЕ УТКА
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	// факт материалов и отходов за прошлые периоды
	|	ВЫБРАТЬ
	|		Материалы.Организация                                  КАК Организация,
	|		Материалы.Партия                                       КАК ПартияПроизводства,
	|		Материалы.АналитикаУчетаНоменклатуры.Номенклатура      КАК Номенклатура,
	|		Материалы.АналитикаУчетаНоменклатуры.Характеристика    КАК Характеристика,
	|		Материалы.АналитикаУчетаНоменклатуры.Подразделение     КАК Подразделение,
	|		Материалы.АналитикаУчетаНоменклатуры.Назначение        КАК Назначение,
	|		Материалы.АналитикаУчетаНоменклатуры.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|		0                                                      КАК План,
	|		ВЫБОР
	|			КОГДА Материалы.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)
	|				ТОГДА -1 * Материалы.Количество
	|			ИНАЧЕ Материалы.Количество
	|		КОНЕЦ                                                  КАК Факт,
	|		ВЫБОР
	|			КОГДА Материалы.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)
	|				ТОГДА -1
	|			ИНАЧЕ 1
	|		КОНЕЦ                                                  КАК КоэффициентПриведения
	|	ИЗ
	|		ПартииПроизводстваСводно КАК ДД
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК Материалы
	|			ПО ДД.ПартияПроизводства = Материалы.Партия
	|			И ДД.Организация = Материалы.Организация
	|
	//++ Устарело_Переработка24
	|			ЛЕВОЕ СОЕДИНЕНИЕ РаспределенныеОтчеты КАК РаспределенныеОтчеты
	|			ПО Материалы.Регистратор = РаспределенныеОтчеты.ОтчетПереработчика
	|				И Материалы.Партия = РаспределенныеОтчеты.ПартияПроизводства
	//-- Устарело_Переработка24
	|	ГДЕ
	|		Материалы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И Материалы.Период < &НачалоПериода
	//++ Устарело_Переработка24
	
	// Данные переработки выпускающего этапа на стороне исключаются, т.к. движения формируются отчетом без распределения.
	|		И РаспределенныеОтчеты.ОтчетПереработчика ЕСТЬ NULL
	//-- Устарело_Переработка24
	|) КАК ПланФактМатериалов
	|
	|СГРУППИРОВАТЬ ПО
	|	ПланФактМатериалов.Организация,
	|	ПланФактМатериалов.ПартияПроизводства,
	|	ПланФактМатериалов.Номенклатура,
	|	ПланФактМатериалов.Характеристика,
	|	ПланФактМатериалов.Подразделение,
	|	ПланФактМатериалов.Назначение,
	|	ПланФактМатериалов.СтатьяКалькуляции,
	|	ПланФактМатериалов.КоэффициентПриведения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Расходы.Партия                                       КАК ПартияПроизводства,
	|	Расходы.АналитикаУчетаНоменклатуры.Номенклатура      КАК Номенклатура,
	|	Расходы.АналитикаУчетаНоменклатуры.Характеристика    КАК Характеристика,
	|	Расходы.АналитикаУчетаНоменклатуры.Подразделение     КАК Подразделение,
	|	Расходы.АналитикаУчетаНоменклатуры.Назначение        КАК Назначение,
	|	Расходы.АналитикаУчетаНоменклатуры.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	СУММА(Расходы.КоличествоРасход)                      КАК Количество
	|ПОМЕСТИТЬ втСписаноВПрошлыхМесяцах
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров.Обороты(,
	|		ДОБАВИТЬКДАТЕ(&НачалоПериода, СЕКУНДА, - 1),,
	|		Партия В (ВЫБРАТЬ
	|					Т.ПартияПроизводства
	|				ИЗ ПартииПроизводстваСводно КАК Т)
	|		) КАК Расходы
	|СГРУППИРОВАТЬ ПО
	|	Расходы.Партия,
	|	Расходы.АналитикаУчетаНоменклатуры.Номенклатура,
	|	Расходы.АналитикаУчетаНоменклатуры.Характеристика,
	|	Расходы.АналитикаУчетаНоменклатуры.Подразделение,
	|	Расходы.АналитикаУчетаНоменклатуры.Назначение,
	|	Расходы.АналитикаУчетаНоменклатуры.СтатьяКалькуляции
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДД.Организация                      КАК Организация,
	|	ДД.ПартияПроизводства               КАК ПартияПроизводства,
	|	ДД.Номенклатура                     КАК Номенклатура,
	|	ДД.Характеристика                   КАК Характеристика,
	|	ДД.Подразделение                    КАК Подразделение,
	|	ДД.Назначение                       КАК Назначение,
	|	ДД.СтатьяКалькуляции                КАК СтатьяКалькуляции,
	|	ДД.КоэффициентПриведения            КАК КоэффициентПриведения,
	|	ВЫБОР
	|		КОГДА ДД.План > ДД.Факт
	|			ТОГДА ДД.План
	|		ИНАЧЕ ДД.Факт
	|	КОНЕЦ * ДолиВыпуска.ДоляВыпускаВсего - ДД.КоэффициентПриведения * ЕСТЬNULL(Списано.Количество, 0) КАК РасчетноеКоличество,
	|	ДолиВыпуска.ПроизводствоЗавершено   КАК ПроизводствоЗавершено,
	// дополнительные поля для расшифровки
	|	ДД.План                             КАК План,
	|	ДД.Факт                             КАК Факт,
	|	ДолиВыпуска.ДоляВыпускаВсего        КАК ДоляВыпускаВсего,
	|	ДолиВыпуска.ДоляВыпускаМесяца       КАК ДоляВыпускаМесяца,
	|	Списано.Количество                  КАК КоличествоСписаноВПрошлыхМесяцах
	|ПОМЕСТИТЬ втРасчетноеКоличество
	|ИЗ
	|	ПланФактМатериалов КАК ДД
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДолиВыпуска КАК ДолиВыпуска
	|		ПО ДД.ПартияПроизводства = ДолиВыпуска.ПартияПроизводства
	|		И ДД.Организация = ДолиВыпуска.Организация
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСписаноВПрошлыхМесяцах КАК Списано
	|		ПО ДД.ПартияПроизводства = Списано.ПартияПроизводства
	|			И ДД.Номенклатура = Списано.Номенклатура
	|			И ДД.Характеристика = Списано.Характеристика
	|			И ДД.Подразделение = Списано.Подразделение
	|			И ДД.Назначение = Списано.Назначение
	|			И ДД.СтатьяКалькуляции = Списано.СтатьяКалькуляции
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДД.Организация                      КАК Организация,
	|	ДД.ПартияПроизводства               КАК ПартияПроизводства,
	|	ДД.Номенклатура                     КАК Номенклатура,
	|	ДД.Характеристика                   КАК Характеристика,
	|	ДД.Подразделение                    КАК Подразделение,
	|	ДД.Назначение                       КАК Назначение,
	|	ДД.СтатьяКалькуляции                КАК СтатьяКалькуляции,
	|	ДД.КоэффициентПриведения < 0		КАК ПобочныеИзделия,
	|	ВЫБОР
	|		КОГДА ДД.ПроизводствоЗавершено
	|			ТОГДА
	|				ВЫБОР
	|					КОГДА ЕСТЬNULL(Остатки.Количество, 0) = 0
	|						ТОГДА ДД.РасчетноеКоличество
	|					ИНАЧЕ
	|						Остатки.Количество
	|				КОНЕЦ
	|		КОГДА ДД.РасчетноеКоличество > ЕСТЬNULL(Остатки.Количество, 0) * ДД.КоэффициентПриведения
	|			ТОГДА ЕСТЬNULL(Остатки.Количество, 0)
	|		ИНАЧЕ
	|			ДД.КоэффициентПриведения * ДД.РасчетноеКоличество
	|	КОНЕЦ                               КАК РасчетноеКоличество,
	// дополнительные поля для расшифровки
	|	ДД.План                             КАК КоличествоВсегоПлан,
	|	ДД.Факт                             КАК КоличествоВсегоФакт,
	|	ДД.ДоляВыпускаВсего                 КАК ДоляВыпускаВсего,
	|	ДД.ДоляВыпускаМесяца                КАК ДоляВыпускаМесяца,
	|	ДД.КоличествоСписаноВПрошлыхМесяцах КАК КоличествоСписаноВПрошлыхМесяцах
	|ПОМЕСТИТЬ Расчетное
	|ИЗ
	|	втРасчетноеКоличество КАК ДД
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиМатериаловСводно КАК Остатки
	|		ПО ДД.ПартияПроизводства = Остатки.ПартияПроизводства
	|			И ДД.Номенклатура = Остатки.Номенклатура
	|			И ДД.Характеристика = Остатки.Характеристика
	|			И ДД.Подразделение = Остатки.Подразделение
	|			И ДД.Назначение = Остатки.Назначение
	|			И ДД.СтатьяКалькуляции = Остатки.СтатьяКалькуляции
	|			И ДД.КоэффициентПриведения = Остатки.КоэффициентПриведения
	|;
	|";
	
	Возврат ТекстЗапроса
	
КонецФункции

// ... выборки данных

Функция ТекстНоменклатураКРаспределениюНаВыпуск() // использует ОстаткиМатериалов, ДолиВыпуска
	
	Возврат "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ТекстНоменклатураКРаспределениюНаВыпуск""                          КАК ЗапросИсточник,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия)                      КАК ТипЗаписи,
	|	ИСТИНА                                                               КАК РасчетЗавершен,
	|	ВЫБОР КОГДА ДД.Количество - ЕСТЬNULL(ЗамкнутыеПартии.Количество, 0) < 0 ТОГДА ИСТИНА ИНАЧЕ ЛОЖЬ КОНЕЦ КАК Сторно,
	|	ДД.РазделУчета                                                       КАК РазделУчета,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)                               КАК ВидДвижения,
	|	&КонецПериода                                                        КАК Период,
	|	ЗамкнутыеПартии.ПартияВыпуска                                        КАК Регистратор,
	|	ДД.АналитикаУчетаНоменклатуры                                        КАК АналитикаУчетаНоменклатуры,
	|	ДД.АналитикаУчетаНоменклатуры.Номенклатура                           КАК Номенклатура,
	|	ДД.АналитикаУчетаНоменклатуры.Характеристика                         КАК Характеристика,
	|	ДД.АналитикаУчетаНоменклатуры.Подразделение                          КАК Подразделение,
	|	ДД.АналитикаУчетаНоменклатуры.Назначение                             КАК Назначение,
	|	ДД.АналитикаУчетаНоменклатуры.СтатьяКалькуляции                      КАК СтатьяКалькуляции,
	|	ДД.ВидЗапасов                                                        КАК ВидЗапасов,
	|	ДД.Организация                                                       КАК Организация,
	|	ДД.ПартияПроизводства                                                КАК Партия,
	|	ДД.АналитикаУчетаПартий                                              КАК АналитикаУчетаПартий,
	|	ДД.АналитикаФинансовогоУчета                                         КАК АналитикаФинансовогоУчета,
	|	ДД.ВидДеятельностиНДС                                                КАК ВидДеятельностиНДС,
	|	ВЫРАЗИТЬ(ВЫБОР
	|		КОГДА ДД.Количество - ЕСТЬNULL(ЗамкнутыеПартии.Количество, 0) < 0 ТОГДА
	|			(-1)*(ДД.Количество - ЕСТЬNULL(ЗамкнутыеПартии.Количество, 0))
	|		ИНАЧЕ
	|			ДД.Количество - ЕСТЬNULL(ЗамкнутыеПартии.Количество, 0)
	|	КОНЕЦ КАК ЧИСЛО(23,10))                                              КАК Знаменатель,
	|	Итог.ДоляВыпускаМесяца                                               КАК ДоляВыпуска,
	|	ДД.Количество - ЕСТЬNULL(ЗамкнутыеПартии.Количество, 0)              КАК Количество,
	|	ДД.Количество < 0                                                    КАК ПобочныеИзделия,
	|	НЕОПРЕДЕЛЕНО                                                         КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                                                         КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО                                                         КАК КорРазделУчета,
	|	НЕОПРЕДЕЛЕНО                                                         КАК КорВидЗапасов,
	|	НЕОПРЕДЕЛЕНО                                                         КАК СтатьяРасходовСписания,
	|	НЕОПРЕДЕЛЕНО                                                         КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                                                         КАК СтатьяАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО                                                         КАК АналитикаАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО                                                         КАК КорПартия,
	|	НЕОПРЕДЕЛЕНО                                                         КАК КорАналитикаУчетаПартий,
	|	НЕОПРЕДЕЛЕНО                                                         КАК КорВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО                                                         КАК КорАналитикаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО                                                         КАК ИдентификаторСтроки,
	|	НЕОПРЕДЕЛЕНО                                                         КАК ДокументИсточник,
	|	НЕОПРЕДЕЛЕНО                                                         КАК НастройкаХозяйственнойОперации,
	|	""""                                                                 КАК ИдентификаторФинЗаписи
	|ИЗ
	|	ОстаткиМатериалов КАК ДД
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДолиВыпуска КАК Итог
	|		ПО Итог.ПартияПроизводства = ДД.ПартияПроизводства
	|		И ДД.Организация = Итог.Организация
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ЗамкнутыеПартии КАК ЗамкнутыеПартии
	|		ПО ЗамкнутыеПартии.ПартияПроизводства = ДД.ПартияПроизводства
	|		И ЗамкнутыеПартии.АналитикаУчетаНоменклатуры = ДД.АналитикаУчетаНоменклатуры
	|";
	
КонецФункции

Функция ТекстЗацикленнаяНоменклатураНаВыпуск()
	
	Возврат "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""Циклы""                                                            КАК ЗапросИсточник,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Дополнение)                  КАК ТипЗаписи,
	|	ИСТИНА                                                               КАК РасчетЗавершен,
	|	ВЫБОР КОГДА ПартииМатериаловВНЗП.Количество < 0 ТОГДА ИСТИНА ИНАЧЕ ЛОЖЬ КОНЕЦ КАК ЭтоСторно,
	|	ДД.РазделУчета                                                       КАК РазделУчета,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)                               КАК ВидДвижения,
	|	&КонецПериода                                                        КАК Период,
	|	Продукция.ПартияВыпуска                                              КАК Регистратор,
	|	ДД.АналитикаУчетаНоменклатуры                                        КАК АналитикаУчетаНоменклатуры,
	|	ДД.АналитикаУчетаНоменклатуры.Номенклатура                           КАК Номенклатура,
	|	ДД.АналитикаУчетаНоменклатуры.Характеристика                         КАК Характеристика,
	|	ДД.АналитикаУчетаНоменклатуры.Подразделение                          КАК Подразделение,
	|	ДД.АналитикаУчетаНоменклатуры.Назначение                             КАК Назначение,
	|	ДД.АналитикаУчетаНоменклатуры.СтатьяКалькуляции                      КАК СтатьяКалькуляции,
	|	ДД.ВидЗапасов                                                        КАК ВидЗапасов,
	|	ДД.Организация                                                       КАК Организация,
	|	ДД.ПартияПроизводства                                                КАК Партия,
	|	ДД.АналитикаУчетаПартий                                              КАК АналитикаУчетаПартий,
	|	ДД.АналитикаФинансовогоУчета                                         КАК АналитикаФинансовогоУчета,
	|	ДД.ВидДеятельностиНДС                                                КАК ВидДеятельностиНДС,
	|	0                                                                    КАК Знаменатель,
	|	0                                                                    КАК ДоляВыпуска,
	|	ПартииМатериаловВНЗП.Количество                                      КАК Количество,
	|	ДД.Количество < 0                                                    КАК ПобочныеИзделия,
	|	Продукция.ХозяйственнаяОперация                                      КАК ХозяйственнаяОперация,
	|	Продукция.АналитикаУчетаНоменклатуры                                 КАК КорАналитикаУчетаНоменклатуры,
	|	Продукция.РазделУчета                                                КАК КорРазделУчета,
	|	Продукция.ВидЗапасов                                                 КАК КорВидЗапасов,
	|	НЕОПРЕДЕЛЕНО                                                         КАК СтатьяРасходовСписания,
	|	НЕОПРЕДЕЛЕНО                                                         КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                                                         КАК СтатьяАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО                                                         КАК АналитикаАктивовПассивов,
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22
	|				И Продукция.Организация В (&ОрганизацииСФИФОСкользящая)
	|			ТОГДА Продукция.ПартияВыпуска
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                                КАК КорПартия,
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22
	|				И Продукция.Организация В (&ОрганизацииСФИФОСкользящая)
	|			ТОГДА Продукция.АналитикаУчетаПартий
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                                КАК КорАналитикаУчетаПартий,
	|	ЕСТЬNULL(Движения.ВидДеятельностиНДС, Продукция.ВидДеятельностиНДС)  КАК КорВидДеятельностиНДС,
	|	ЕСТЬNULL(Движения.АналитикаФинансовогоУчета, Продукция.АналитикаФинансовогоУчета) КАК КорАналитикаФинансовогоУчета,
	|	Продукция.ИдентификаторСтроки                                        КАК ИдентификаторСтроки,
	|	Движения.Регистратор                                                 КАК ДокументИсточник,
	|	НЕОПРЕДЕЛЕНО                                                         КАК НастройкаХозяйственнойОперации,
	|	""""                                                                 КАК ИдентификаторФинЗаписи
	|ИЗ
	|	ОстаткиМатериалов КАК ДД
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПартииМатериаловВНЗП КАК ПартииМатериаловВНЗП
	|		ПО ПартииМатериаловВНЗП.ПартияПроизводства = ДД.ПартияПроизводства
	|		И ПартииМатериаловВНЗП.АналитикаУчетаНоменклатуры = ДД.АналитикаУчетаНоменклатуры
	|		И ПартииМатериаловВНЗП.Организация = ДД.Организация
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Продукция КАК Продукция
	|		ПО ПартииМатериаловВНЗП.ПартияПроизводства = Продукция.ПартияПроизводства
	|		И ПартииМатериаловВНЗП.ПартияМатериала = Продукция.ПартияВыпуска
	|		И ПартииМатериаловВНЗП.АналитикаУчетаПартий = Продукция.АналитикаУчетаПартий
	|		И Продукция.Организация = ДД.Организация
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК Движения
	|	ПО Движения.Регистратор = Продукция.ПартияВыпуска
	|		И Движения.ИдентификаторСтроки = Продукция.ИдентификаторСтроки
	|		И Продукция.ИдентификаторСтроки <> """"
	|		И Движения.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции)
	|		И НЕ Движения.РасчетПартий
	|		И НЕ Движения.РасчетСебестоимости
	// исключение движений по работам для давальца
	|		И Движения.КорАналитикаУчетаНоменклатуры = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	
	|";
	
КонецФункции

Функция ТекстРасчетноеКоличествоКРаспределению() // использует Расчетное
	
	Возврат "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ТекстРасчетноеКоличествоКРаспределению""                           КАК ЗапросИсточник,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Расчетное)                   КАК ТипЗаписи,
	|	ЛОЖЬ                                                                 КАК РасчетЗавершен,
	|	ВЫБОР КОГДА ДД.РасчетноеКоличество - ЕСТЬNULL(ЗамкнутыеПартии.Количество, 0) < 0 ТОГДА ИСТИНА ИНАЧЕ ЛОЖЬ КОНЕЦ КАК ЭтоСторно,
	|	НЕОПРЕДЕЛЕНО                                                         КАК РазделУчета,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)                               КАК ВидДвижения,
	|	&КонецПериода                                                        КАК Период,
	|	ЗамкнутыеПартии.ПартияВыпуска                                        КАК Регистратор,
	|	НЕОПРЕДЕЛЕНО                                                         КАК АналитикаУчетаНоменклатуры,
	|	ДД.Номенклатура                                                      КАК Номенклатура,
	|	ДД.Характеристика                                                    КАК Характеристика,
	|	ДД.Подразделение                                                     КАК Подразделение,
	|	ДД.Назначение                                                        КАК Назначение,
	|	ДД.СтатьяКалькуляции                                                 КАК СтатьяКалькуляции,
	|	НЕОПРЕДЕЛЕНО                                                         КАК ВидЗапасов,
	|	ДД.Организация                                                       КАК Организация,
	|	ДД.ПартияПроизводства                                                КАК Партия,
	|	НЕОПРЕДЕЛЕНО                                                         КАК АналитикаУчетаПартий,
	|	НЕОПРЕДЕЛЕНО                                                         КАК АналитикаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО                                                         КАК ВидДеятельностиНДС,
	|	ВЫРАЗИТЬ(ВЫБОР
	|		КОГДА ДД.РасчетноеКоличество - ЕСТЬNULL(ЗамкнутыеПартии.Количество, 0) < 0 ТОГДА
	|			(-1)*(ДД.РасчетноеКоличество - ЕСТЬNULL(ЗамкнутыеПартии.Количество, 0))
	|		ИНАЧЕ
	|			ДД.РасчетноеКоличество - ЕСТЬNULL(ЗамкнутыеПартии.Количество, 0)
	|	КОНЕЦ КАК ЧИСЛО(23,10))                                              КАК Знаменатель,
	|	0                                                                    КАК ДоляВыпуска,
	|	ДД.РасчетноеКоличество - ЕСТЬNULL(ЗамкнутыеПартии.Количество, 0)     КАК Количество,
	|	ДД.ПобочныеИзделия                                                   КАК ПобочныеИзделия,
	|	НЕОПРЕДЕЛЕНО                                                         КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                                                         КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО                                                         КАК КорРазделУчета,
	|	НЕОПРЕДЕЛЕНО                                                         КАК КорВидЗапасов,
	|	НЕОПРЕДЕЛЕНО                                                         КАК СтатьяРасходовСписания,
	|	НЕОПРЕДЕЛЕНО                                                         КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                                                         КАК СтатьяАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО                                                         КАК АналитикаАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО                                                         КАК КорПартия,
	|	НЕОПРЕДЕЛЕНО                                                         КАК КорАналитикаУчетаПартий,
	|	НЕОПРЕДЕЛЕНО                                                         КАК КорВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО                                                         КАК КорАналитикаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО                                                         КАК ИдентификаторСтроки,
	|	НЕОПРЕДЕЛЕНО                                                         КАК ДокументИсточник,
	|	НЕОПРЕДЕЛЕНО                                                         КАК НастройкаХозяйственнойОперации,
	|	""""                                                                 КАК ИдентификаторФинЗаписи
	|ИЗ
	|	Расчетное КАК ДД
	|	ЛЕВОЕ СОЕДИНЕНИЕ ЗамкнутыеПартии КАК ЗамкнутыеПартии
	|		ПО ЗамкнутыеПартии.ПартияПроизводства = ДД.ПартияПроизводства
	|		И ЗамкнутыеПартии.АналитикаУчетаНоменклатуры.Номенклатура = ДД.Номенклатура
	|		И ЗамкнутыеПартии.АналитикаУчетаНоменклатуры.Характеристика = ДД.Характеристика
	|		И ЗамкнутыеПартии.АналитикаУчетаНоменклатуры.Подразделение = ДД.Подразделение
	|		И ЗамкнутыеПартии.АналитикаУчетаНоменклатуры.Назначение = ДД.Назначение
	|		И ЗамкнутыеПартии.АналитикаУчетаНоменклатуры.СтатьяКалькуляции = ДД.СтатьяКалькуляции
	|";
	
КонецФункции

Функция ТекстРаспределениеНоменклатурыНаВыпускПоПродукции() // использует Продукция
	
	Возврат "
	|ВЫБРАТЬ
	|	""ТекстРаспределениеНоменклатурыНаВыпускПоПродукции""                КАК ЗапросИсточник,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.СписанияНаВыпуск)            КАК ТипЗаписи,
	|	ЛОЖЬ                                                                 КАК РасчетЗавершен,
	|	ЛОЖЬ                                                                 КАК ЭтоСторно,
	|	НЕОПРЕДЕЛЕНО                                                         КАК РазделУчета,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)                               КАК ВидДвижения,
	|	ДД.ДатаПроизводства                                                  КАК Период,
	|	ДД.ПартияВыпуска                                                     КАК Регистратор,
	|	НЕОПРЕДЕЛЕНО                                                         КАК АналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО                                                         КАК Номенклатура,
	|	НЕОПРЕДЕЛЕНО                                                         КАК Характеристика,
	|	НЕОПРЕДЕЛЕНО                                                         КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО                                                         КАК Назначение,
	|	НЕОПРЕДЕЛЕНО                                                         КАК СтатьяКалькуляции,
	|	НЕОПРЕДЕЛЕНО                                                         КАК ВидЗапасов,
	|	ДД.Организация                                                       КАК Организация,
	|	ДД.ПартияПроизводства                                                КАК Партия,
	|	НЕОПРЕДЕЛЕНО                                                         КАК АналитикаУчетаПартий,
	|	НЕОПРЕДЕЛЕНО                                                         КАК АналитикаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО                                                         КАК ВидДеятельностиНДС,
	|	ДД.ДоляНаименованияМесяца                                            КАК Знаменатель,
	|	0                                                                    КАК ДоляВыпуска,
	|	0                                                                    КАК Количество,
	|	ЛОЖЬ                                                                 КАК ПобочныеИзделия,
	|	ДД.ХозяйственнаяОперация                                             КАК ХозяйственнаяОперация,
	|	ДД.АналитикаУчетаНоменклатуры                                        КАК КорАналитикаУчетаНоменклатуры,
	|	ДД.РазделУчета                                                       КАК КорРазделУчета,
	|	ДД.ВидЗапасов                                                        КАК КорВидЗапасов,
	|	НЕОПРЕДЕЛЕНО                                                         КАК СтатьяРасходовСписания,
	|	НЕОПРЕДЕЛЕНО                                                         КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                                                         КАК СтатьяАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО                                                         КАК АналитикаАктивовПассивов,
	|	ВЫБОР
	|		КОГДА НЕ Движения.Партия ЕСТЬ NULL
	|			ТОГДА Движения.Партия
	|		КОГДА &ПартионныйУчетВерсии22
	|				И ДД.Организация В (&ОрганизацииСФИФОСкользящая)
	|			ТОГДА ДД.ПартияВыпуска
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                                КАК КорПартия,
	|	ВЫБОР
	|		КОГДА НЕ Движения.АналитикаУчетаПартий ЕСТЬ NULL
	|			ТОГДА Движения.АналитикаУчетаПартий
	|		КОГДА &ПартионныйУчетВерсии22
	|				И ДД.Организация В (&ОрганизацииСФИФОСкользящая)
	|			ТОГДА ДД.АналитикаУчетаПартий
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                                КАК КорАналитикаУчетаПартий,
	|	ЕСТЬNULL(Движения.ВидДеятельностиНДС, ДД.ВидДеятельностиНДС) 		 КАК КорВидДеятельностиНДС,
	|	ЕСТЬNULL(Движения.АналитикаФинансовогоУчета, ДД.АналитикаФинансовогоУчета) КАК КорАналитикаФинансовогоУчета,
	|	ДД.ИдентификаторСтроки                                               КАК ИдентификаторСтроки,
	|	Движения.Регистратор                                                 КАК ДокументИсточник,
	|	НастройкаХозОперации.Ссылка                                          КАК НастройкаХозяйственнойОперации,
	|	ДД.ИдентификаторФинЗаписи                                            КАК ИдентификаторФинЗаписи
	|ИЗ
	|	Продукция КАК ДД
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК РеквизитыПартииПроизводства
	|		ПО ДД.ПартияПроизводства = РеквизитыПартииПроизводства.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК Движения
	|		ПО ДД.ПартияВыпуска = Движения.Регистратор
	|			И ДД.ИдентификаторСтроки = Движения.ИдентификаторСтроки
	|			И ДД.ИдентификаторСтроки <> """"
	|			И Движения.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции)
	|			И НЕ Движения.РасчетПартий
	|			И НЕ Движения.РасчетСебестоимости
	// исключение движений по работам для давальца
	|		И Движения.КорАналитикаУчетаНоменклатуры = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|		И НЕ Движения.Сторно
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НастройкиХозяйственныхОпераций КАК НастройкаХозОперации
	|		ПО НастройкаХозОперации.ХозяйственнаяОперация = ДД.ХозяйственнаяОперация
	|ГДЕ
	|	ДД.ДоляНаименованияМесяца > 0
	|";
	
КонецФункции

// Заполнение расчетной партии

Процедура ЗаполнитьРасчетнуюПартиюРаспределениеНоменклатурыНаВыпуск(ПараметрыРасчета, РасчетнаяПартия, Расход, Приход)
	
	ЗаполнитьЗначенияСвойств(РасчетнаяПартия, Расход);
	
	Если Расход.ТипЗаписи = Перечисления.ТипыЗаписейПартий.Расчетное Тогда
		
		Если Приход.Знаменатель = 0 Тогда
			Возврат;
		КонецЕсли;
		
		ЗнаменательСписания = Мин(Расход.Знаменатель, Приход.Знаменатель);
		
		Если Расход.Количество < 0 Тогда
			КоличествоСписания = Макс(Расход.Количество, Приход.Количество);
		Иначе
			КоличествоСписания = Мин(Расход.Количество, Приход.Количество);
		КонецЕсли;
		
		// заполняем показатели расчетной партии
		РасчетнаяПартия.Количество = КоличествоСписания;
		
		// корректируем базу расчета в расходе
		Приход.Количество = Приход.Количество - РасчетнаяПартия.Количество;
		Расход.Количество = Расход.Количество - РасчетнаяПартия.Количество;
		
		Приход.Знаменатель = Приход.Знаменатель - ЗнаменательСписания;
		
		РасчетнаяПартия.Знаменатель = Приход.ДоляВыпуска;
		
		ЗаполнитьЗначенияСвойств(РасчетнаяПартия, Приход, "РасчетЗавершен,ВидЗапасов,АналитикаУчетаПартий,
			|АналитикаФинансовогоУчета,ВидДеятельностиНДС,РазделУчета,АналитикаУчетаНоменклатуры,Организация");
		
	Иначе
		
		Если Приход.Знаменатель = 0 Тогда
			Возврат;
		КонецЕсли;
		
		// расчетная партия заполняется на наибольшее общее вычитаемое
		ЗнаменательСписания = Мин(Расход.Знаменатель, Приход.Знаменатель);
		
		// заполняем показатели расчетной партии
		Если Приход.Знаменатель - ЗнаменательСписания = 0 Тогда
			РасчетнаяПартия.Количество = Приход.Количество;
		Иначе
			РасчетнаяПартия.Количество = Окр(ЗнаменательСписания * Приход.Количество / Приход.Знаменатель, 3);
		КонецЕсли;
		
		// корректируем базы расчета
		Приход.Знаменатель = Приход.Знаменатель - ЗнаменательСписания;
		Приход.Количество  = Приход.Количество - РасчетнаяПартия.Количество;
		
		// Если в приемнике количество не инвернтируется, а в источнике количество инвертировано, то необходимо
		// инвертировать количество в приемнике, иначе оно останется положительным.
		Если НЕ РасчетнаяПартия.ЭтоСторно И Приход.ЭтоСторно Тогда
			РасчетнаяПартия.Количество = -РасчетнаяПартия.Количество; 
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(РасчетнаяПартия, Приход, "РасчетЗавершен,СтатьяКалькуляции,ВидЗапасов,АналитикаУчетаПартий,
			|АналитикаФинансовогоУчета,ВидДеятельностиНДС,АналитикаУчетаНоменклатуры,РазделУчета,Организация");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыЭтапов_Контекстные

// Используется для всех вызовов заполнения расчетной партии.
//
Процедура ЗаполнитьРасчетнуюПартию(ПараметрыРасчета, Контекст, РасчетнаяПартия, Расход, Приход, ПартияЗаполнена) Экспорт
	
	// Вызывается из процедуры РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьРасчетнуюПартию()
	
	Если Контекст = "РаспределениеНоменклатурыНаПроизводство" Тогда
		// Этап 4
		ЗаполнитьРасчетнуюПартиюРаспределенияНоменклатурыНаПроизводство(ПараметрыРасчета, РасчетнаяПартия, Расход, Приход);
		ПартияЗаполнена = Истина;
	ИначеЕсли Контекст = "РаспределениеНоменклатурыНаВыпуск" Тогда
		// Этап 5
		ЗаполнитьРасчетнуюПартиюРаспределениеНоменклатурыНаВыпуск(ПараметрыРасчета, РасчетнаяПартия, Расход, Приход);
		ПартияЗаполнена = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Тестирование

// Дополняет список этапов расчета.
// 
// Параметры:
// 	СписокЭтапов - СписокЗначений - 
//
Процедура ДополнитьЭтапыСРаспределениемПартий(СписокЭтапов) Экспорт
	
	СписокЭтапов.Добавить("РаспределениеНоменклатурыНаПроизводство");
	СписокЭтапов.Добавить("РаспределениеНоменклатурыНаВыпуск");
	
КонецПроцедуры

Процедура ТекстЗапросаДляРасчетаЭтапа(ИмяЭтапа, ПараметрыРасчета, ТекстЗапроса) Экспорт
	
	Если ИмяЭтапа = "РаспределениеНоменклатурыНаПроизводство" Тогда
		ТекстЗапроса = ТекстЗапросаДляРаспределенияНоменклатурыНаПроизводство(ПараметрыРасчета);
		
	ИначеЕсли ИмяЭтапа = "РаспределениеНоменклатурыНаВыпуск" Тогда
		ТекстЗапроса = ТекстЗапросаДляРаспределенияНоменклатурыНаВыпуск();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти