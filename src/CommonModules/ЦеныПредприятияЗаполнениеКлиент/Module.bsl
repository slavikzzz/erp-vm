#Область ПрограммныйИнтерфейс

// Процедура для выбора вида цен из единой формы выбора.
//
// Параметры:
//	ПараметрыЗаполнения - см. НовыйПараметрыЗаполненияВыбратьВидЦен
Процедура ВыбратьВидЦен(ПараметрыЗаполнения) Экспорт
	
	ОписаниеОповещения = ПараметрыЗаполнения.ОписаниеОповещения;
	ЦенаВключаетНДС = ПараметрыЗаполнения.ЦенаВключаетНДС;
	ИспользоватьПриПродаже = ПараметрыЗаполнения.ИспользоватьПриПродаже;
	ИспользоватьПриПередачеМеждуОрганизациями = ПараметрыЗаполнения.ИспользоватьПриПередачеМеждуОрганизациями;
	//++ НЕ УТ
	ИспользоватьПриПередачеПродукцииДавальцу = ПараметрыЗаполнения.ИспользоватьПриПередачеПродукцииДавальцу;
	//-- НЕ УТ

	ВидЦеныПрайсЛист = ЦенообразованиеВызовСервера.ВидЦеныПрайсЛист(ЦенаВключаетНДС);
	Если ВидЦеныПрайсЛист <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, ВидЦеныПрайсЛист);
		Возврат;
	КонецЕсли;
	
	СтруктураОтбор = Новый Структура;
	СтруктураОтбор.Вставить("Статус", ПредопределенноеЗначение("Перечисление.СтатусыДействияВидовЦен.Действует"));
	СтруктураОтбор.Вставить("ПометкаУдаления", Ложь);
	Если ЦенаВключаетНДС <> Неопределено Тогда
			СтруктураОтбор.Вставить("ЦенаВключаетНДС", ЦенаВключаетНДС);
	КонецЕсли;
	Если ИспользоватьПриПродаже <> Неопределено Тогда
		СтруктураОтбор.Вставить("ИспользоватьПриПродаже", ИспользоватьПриПродаже);
	КонецЕсли;
	Если ИспользоватьПриПередачеМеждуОрганизациями <> Неопределено Тогда
		СтруктураОтбор.Вставить("ИспользоватьПриПередачеМеждуОрганизациями", ИспользоватьПриПередачеМеждуОрганизациями);
	КонецЕсли;
	//++ НЕ УТ
	Если ИспользоватьПриПередачеПродукцииДавальцу <> Неопределено Тогда
		СтруктураОтбор.Вставить("ИспользоватьПриПередачеПродукцииДавальцу", ИспользоватьПриПередачеПродукцииДавальцу);
	КонецЕсли;
	//-- НЕ УТ
	
	ОткрытьФорму(
		"Справочник.ВидыЦен.ФормаВыбора",
		Новый Структура("Отбор", СтруктураОтбор),,,,,
		ОписаниеОповещения);
		
КонецПроцедуры

// Вопрос по пересчету цен при изменении даты документа.
//
// Параметры:
//	Документ - ДокументОбъект - для которого выполняются проверки
//	ОповещениеОРезультате - ОписаниеОповещения - оповещение о результате вопроса, 
//			Ложь, если необходимые данные не заполнены или на вопрос пользователь ответил отрицательно.
//	ИмяТЧ -Строка - Имя табличной части, значение по-умолчанию "Товары".
//	ПроверитьСоглашение - Булево - признак необходимости проверки соглашение,
//	ТекстВопроса - Строка - текст вопроса.
//
Процедура ЗадатьВопросПересчетаЦеныПриИзмененииДаты(
			Документ,
			ОповещениеОРезультате,
			ИмяТЧ="Товары",
			ПроверитьСоглашение = Истина,
			ТекстВопроса = "") Экспорт
	
	Если ПроверитьСоглашение И Не ЗначениеЗаполнено(Документ.Соглашение)
		Или Не ЗначениеЗаполнено(Документ.Валюта)
		Или Документ[ИмяТЧ].Количество() = 0 Тогда
		ВыполнитьОбработкуОповещения(ОповещениеОРезультате, Ложь);
		Возврат;
	КонецЕсли;
	
	ВариантыОтветов = Новый СписокЗначений;
	ВариантыОтветов.Добавить(Истина, НСтр("ru = 'Перезаполнить';
											|en = 'Refill'"));
	ВариантыОтветов.Добавить(Ложь, НСтр("ru = 'Не перезаполнять';
										|en = 'Do not refill'"));
	
	ПоказатьВопрос(
		ОповещениеОРезультате,
		?(ЗначениеЗаполнено(ТекстВопроса), ТекстВопроса, НСтр("ru = 'Перезаполнить цены по соглашению?';
																|en = 'Refill prices by terms of sales?'")),
		ВариантыОтветов);
	
КонецПроцедуры

// Вопрос по пересчету цен при изменении соглашения.
//
// Параметры:
//	Документ - ДокументОбъект - для которого выполняются проверки
//	ОповещениеОРезультате - ОписаниеОповещения - оповещение о результате вопроса, 
//			Ложь, если необходимые данные не заполнены или на вопрос пользователь ответил отрицательно.
//	ИмяТЧ -Строка - Имя табличной части, значение по-умолчанию "Товары".
//
//
Процедура ЗадатьВопросПересчетаЦеныПриИзмененииСоглашения(Документ, ОповещениеОРезультате, ИмяТЧ="Товары") Экспорт
	
	Если Не ЗначениеЗаполнено(Документ.Соглашение)
		Или Документ[ИмяТЧ].Количество() = 0 Тогда
		ВыполнитьОбработкуОповещения(ОповещениеОРезультате, Ложь);
		Возврат;
	КонецЕсли;
	
	ВариантыОтветов = Новый СписокЗначений;
	ВариантыОтветов.Добавить(Истина, НСтр("ru = 'Перезаполнить';
											|en = 'Refill'"));
	ВариантыОтветов.Добавить(Ложь, НСтр("ru = 'Не перезаполнять';
										|en = 'Do not refill'"));
	
	ПоказатьВопрос(ОповещениеОРезультате, НСтр("ru = 'Перезаполнить цены по соглашению?';
												|en = 'Refill prices by terms of sales?'"), ВариантыОтветов);
	
КонецПроцедуры

// Вопрос по пересчету цен при изменении договора.
//
// Параметры:
//	Документ - ДокументОбъект - для которого выполняются проверки
//	ОповещениеОРезультате - ОписаниеОповещения - оповещение о результате вопроса, 
//			Ложь, если необходимые данные не заполнены или на вопрос пользователь ответил отрицательно.
//	ИмяТЧ -Строка - Имя табличной части, значение по-умолчанию "Товары".
//
//
Процедура ЗадатьВопросПересчетаЦеныПриИзмененииДоговора(Документ, ОповещениеОРезультате, ИмяТЧ="Товары") Экспорт
	
	Если Не ЗначениеЗаполнено(Документ.Соглашение)
		Или Документ[ИмяТЧ].Количество() = 0 Тогда
		ВыполнитьОбработкуОповещения(ОповещениеОРезультате, Ложь);
		Возврат;
	КонецЕсли;
	
	ВариантыОтветов = Новый СписокЗначений;
	ВариантыОтветов.Добавить(Истина, НСтр("ru = 'Перезаполнить';
											|en = 'Refill'"));
	ВариантыОтветов.Добавить(Ложь, НСтр("ru = 'Не перезаполнять';
										|en = 'Do not refill'"));
	
	ПоказатьВопрос(ОповещениеОРезультате, НСтр("ru = 'Перезаполнить цены по договору?';
												|en = 'Refill prices under the contract?'"), ВариантыОтветов);
	
КонецПроцедуры


// Проверяет заполненность реквизитов, необходимых для заполнения цен в документе по виду цен.
//
// Параметры:
//   Документ                    - ДокументОбъект - объект, для которого выполняются проверки.
//   ИмяТабличнойЧасти           - Строка - имя табличной части, в которой необходимо осуществить проверку.
//   ПредставлениеТабличнойЧасти - Строка - представление табличной части для информирования пользователя.
//
// Возвращаемое значение:
//   Булево - Ложь, если необходимые данные не заполнены.
//
Функция НеобходимоЗаполнениеЦенПоВидуЦен(Документ, ИмяТабличнойЧасти, ПредставлениеТабличнойЧасти) Экспорт

	Если Не ЗначениеЗаполнено(Документ.Валюта) Тогда
		
		ПоказатьПредупреждение(,НСтр("ru = 'В документе не указана валюта. Цены не могут быть заполнены.';
									|en = 'Currency is not specified in the document. Prices cannot be populated.'"));
		Возврат Ложь;
		
	ИначеЕсли Документ[ИмяТабличнойЧасти].Количество() = 0 Тогда
		
		ТекстПредупреждения = НСтр("ru = 'В документе не заполнен список %ПредставлениеТабличнойЧасти%. Цены не могут быть заполнены.';
									|en = 'The %ПредставлениеТабличнойЧасти% list in the document is not filled in. Cannot fill in the prices.'");
		ТекстПредупреждения = СтрЗаменить(ТекстПредупреждения, "%ПредставлениеТабличнойЧасти%", ПредставлениеТабличнойЧасти);
		ПоказатьПредупреждение(,ТекстПредупреждения);
		Возврат Ложь;
		
	КонецЕсли;
	
	Возврат Истина;

КонецФункции

// Проверяет заполненность реквизитов, необходимых для заполнения цен в документе по соглашению с клиентом.
//
// Параметры:
//	Документ                    - ДокументОбъект - объект, для которого выполняются проверки
//	ИмяТабличнойЧасти           - Строка - имя табличной части, в которой необходимо осуществить проверку
//	ПредставлениеТабличнойЧасти - Строка - представление табличной части для информирования пользователя.
//
// Возвращаемое значение:
//	Булево - Ложь, если необходимые данные не заполнены.
//
Функция НеобходимоЗаполнениеЦенПоСоглашению(Документ, ИмяТабличнойЧасти, ПредставлениеТабличнойЧасти) Экспорт

	Если Не ЗначениеЗаполнено(Документ.Соглашение) Тогда
		
		ПоказатьПредупреждение(,НСтр("ru = 'В документе не указано соглашение с клиентом. Цены не могут быть заполнены.';
									|en = 'Agreement with customer is not specified in the document. Prices cannot be filled in.'"));
		Возврат Ложь;
		
	ИначеЕсли Не ЗначениеЗаполнено(Документ.Валюта) Тогда
		
		ПоказатьПредупреждение(,НСтр("ru = 'В документе не указана валюта. Цены не могут быть заполнены.';
									|en = 'Currency is not specified in the document. Prices cannot be populated.'"));
		Возврат Ложь;
		
	ИначеЕсли Документ[ИмяТабличнойЧасти].Количество() = 0 Тогда
		
		ТекстПредупреждения = НСтр("ru = 'В документе не заполнен список %ПредставлениеТабличнойЧасти%. Цены не могут быть заполнены.';
									|en = 'The %ПредставлениеТабличнойЧасти% list in the document is not filled in. Cannot fill in the prices.'");
		ТекстПредупреждения = СтрЗаменить(ТекстПредупреждения, "%ПредставлениеТабличнойЧасти%", ПредставлениеТабличнойЧасти);
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат Ложь;
		
	КонецЕсли;
	
	Возврат Истина;
		
КонецФункции

// Проверяет заполненность реквизитов, необходимых для заполнения цен в документе по договору с комиссионером.
//
// Параметры:
//	Документ            - ДанныеФормыСтруктура - объект, для которого выполняются проверки
//	ПараметрыЗаполнения - см. ЦеныПредприятияЗаполнениеСервер.НовыйПараметрыПроверкиВидаЦенДоговора
//
// Возвращаемое значение:
//	Булево - Ложь, если необходимые данные не заполнены.
//
Функция НеобходимоЗаполнениеЦенПоВиду(Документ, ПараметрыЗаполнения) Экспорт
	
	Результат = Истина;
	
	Если Не ЗначениеЗаполнено(Документ.Договор) Тогда
		
		ПоказатьПредупреждение(,НСтр("ru = 'В документе не указан договор с комиссионером. Цены не могут быть заполнены.';
									|en = 'The contract with a consignee is not specified in the document. Prices cannot be filled in.'"));
		Результат = Ложь;
		
	ИначеЕсли Не ЗначениеЗаполнено(ПараметрыЗаполнения.ВидЦены) Тогда
		
		ВидЦены = ?(ПараметрыЗаполнения.ПолеВидаЦен = "ВидЦенПродажи", НСтр("ru = 'вид цен продажи';
																			|en = 'sales price type'"), НСтр("ru = 'учетный вид цен';
																											|en = 'accounting price type'"));
		ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'В договоре не указан %1. Цены не могут быть заполнены.';
																							|en = '%1 is not specified in the contract. Cannot fill in the prices.'"), ВидЦены);
		ПоказатьПредупреждение(,ТекстПредупреждения);
		Результат = Ложь;
		
	ИначеЕсли Не ПараметрыЗаполнения.ЦенаВключаетНДС = Неопределено И Не Документ.ЦенаВключаетНДС = ПараметрыЗаполнения.ЦенаВключаетНДС Тогда
		
		ТекстПредупреждения = НСтр("ru = 'Признак ""Цена включает НДС"" различается в виде цен договора и документа. Цены не могут быть заполнены.';
									|en = 'The ""Price includes VAT"" flag differs in the price type of the contract and the document. Cannot fill in the prices.'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат Ложь;
		
	ИначеЕсли Документ[ПараметрыЗаполнения.ИмяТабличнойЧасти].Количество() = 0 Тогда
		
		ТекстПредупреждения = НСтр("ru = 'В документе не заполнен список %ПредставлениеТабличнойЧасти%. Цены не могут быть заполнены.';
									|en = 'The %ПредставлениеТабличнойЧасти% list in the document is not filled in. Cannot fill in the prices.'");
		ТекстПредупреждения = СтрЗаменить(ТекстПредупреждения, "%ПредставлениеТабличнойЧасти%", ПараметрыЗаполнения.ПредставлениеТабличнойЧасти);
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат Ложь;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#Область Конструкторы

// Конструктор параметров метода ВыбратьВидЦен.
//
// Возвращаемое значение:
//	Структура - Полный перечень возможных параметров,некоторые могут быть необязательными к заполнению:
//		* ОписаниеОповещения - ОписаниеОповещения,Неопределено - оповещение, которое должно быть обработано после выбора вида цен
//		* ЦенаВключаетНДС 							- Булево, Неопределено - отбор при выборе цены
//		* ИспользоватьПриПродаже 					- Булево, Неопределено - отбор при выборе цены
//		* ИспользоватьПриПередачеМеждуОрганизациями - Булево, Неопределено - отбор при выборе цены
//		* ВладелецИндивидуальногоВидаЦен			- СправочникСсылка.СоглашенияСКлиентами,СправочникСсылка.Партнеры,СправочникСсылка.ФорматыМагазинов,СправочникСсылка.Склады,Неопределено

//++ НЕ УТ

//		* ИспользоватьПриПередачеПродукцииДавальцу 	- Булево, Неопределено - отбор при выборе цены.

//-- НЕ УТ

// Новый параметры заполнения выбрать вид цен.
// 
// Возвращаемое значение:
//  Структура - Новый параметры заполнения выбрать вид цен:
// * ОписаниеОповещения - Неопределено -
// * ЦенаВключаетНДС - Неопределено -
// * ИспользоватьПриПродаже - Неопределено -
// * ИспользоватьПриПередачеМеждуОрганизациями - Неопределено -
// * ВладелецИндивидуальногоВидаЦен - Неопределено -
// * ИспользоватьПриПередачеПродукцииДавальцу - Неопределено -
Функция НовыйПараметрыЗаполненияВыбратьВидЦен() Экспорт
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ОписаниеОповещения", Неопределено);
	СтруктураПараметров.Вставить("ЦенаВключаетНДС", Неопределено);
	СтруктураПараметров.Вставить("ИспользоватьПриПродаже", Неопределено);
	СтруктураПараметров.Вставить("ИспользоватьПриПередачеМеждуОрганизациями", Неопределено);
	СтруктураПараметров.Вставить("ВладелецИндивидуальногоВидаЦен", Неопределено);
//++ НЕ УТ
	СтруктураПараметров.Вставить("ИспользоватьПриПередачеПродукцииДавальцу", Неопределено);
//-- НЕ УТ
	
	Возврат СтруктураПараметров; 
	
КонецФункции

#КонецОбласти

#КонецОбласти
