// Содержимое модуля отличается в версиях ПРОФ и КОРП.
// В версии ПРОФ отсутствует:
// - тело экспортных процедур (остаются только сигнатуры методов)
// - неэкспортные процедуры и все функции.
// Поэтому не допускается реализация в модуле экспортных функций: вместо них следует использовать процедуры с возвращаемым параметром.

#Область СлужебныйПрограммныйИнтерфейс

#Область ПодключениеСтандартныхПодсистем

// Подключает объекты подсистемы к механизмам ограничения доступа.
// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа
//
// Параметры:
//  Списки - Соответствие:
//     * Ключ - ОбъектМетаданных - объекты подсистемы, имеющие списки, требующие подключения
//     * Значение - Булево - Истина
//
Процедура ПриЗаполненииСписковСОграничениемДоступа(Списки) Экспорт
	
	Списки.Вставить(Метаданные.РегистрыСведений.НастройкиЗаполненияБухгалтерскойОтчетности, Истина);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Описывает возможности конфигурации по настройке заполнения отчета.
//
// Параметры:
//  НастраиваемыеРазделыОтчета - см. БухгалтерскаяОтчетностьБРОПереопределяемый.ПриЗаполненииПеречняНастраиваемыхРазделов - (возвращаемый)
//  Контекст - см. БухгалтерскаяОтчетностьБРО.НовыйКонтекстОтчета
//
Процедура УстановитьНастраиваемыеРазделы(НастраиваемыеРазделыОтчета, Контекст) Экспорт
	
	Если Не ПодсистемаДоступна() Тогда
		Возврат;
	КонецЕсли;
	
	НастраиваемыеРазделыОтчета.ОФР = Метаданные.Обработки.НастройкаЗаполненияБухгалтерскойОтчетности.Формы.НастройкаПоказателей.ПолноеИмя();
	
КонецПроцедуры

// Читает ранее выполненные пользователем и сохраненные настройки заполнения отчета.
//
// Параметры:
//  СохраненныеНастройки - см. БухгалтерскаяОтчетностьБРО.НовыйЗаполняемыеПоказатели - (возвращаемый) прочитанные настройки.
//  Контекст - см. БухгалтерскаяОтчетностьБРО.НовыйКонтекстОтчета
//
Процедура ПрочитатьСохраненныеНастройки(СохраненныеНастройки, Контекст) Экспорт

	Если Не ПодсистемаДоступна() Тогда
		Возврат;
	КонецЕсли;
	
	СохраненныеНастройки = РегистрыСведений.НастройкиЗаполненияБухгалтерскойОтчетности.ПрочитатьНастройкиЗаполнения(
		Контекст.Период,
		Контекст.Организация);
	
КонецПроцедуры

// Записывает настройки заполнения, дополняя ранее сохраненные.
//
// Параметры:
//  ЗаполняемыеПоказатели - см. БухгалтерскаяОтчетностьБРО.НовыйЗаполняемыеПоказатели - (возвращаемый) выполненные настройки.
//                          Может содержать настройки части разделов.
//                        - Неопределено - при работе с отчетом настройки не модифицировались
//  ЭкземплярОтчета - ДокументСсылка.РегламентированныйОтчет - записанный отчет
//
Процедура ЗаписатьНастройкиЗаполнения(ЗаполняемыеПоказатели, ЭкземплярОтчета) Экспорт
	
	Если Не ПодсистемаДоступна() Тогда
		Возврат;
	КонецЕсли;
	
	РегистрыСведений.НастройкиЗаполненияБухгалтерскойОтчетности.ДополнитьНастройкиЗаполненияЭкземпляраОтчета(ЗаполняемыеПоказатели, ЭкземплярОтчета);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПодсистемаДоступна()
	
	Доступна = Истина;
	
	Возврат Доступна;
	
КонецФункции

#КонецОбласти
