#Область ПрограммныйИнтерфейс

Функция НачатьОбновлениеФинОтчетности(ИдентификаторФормы = Неопределено) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(ИдентификаторФормы);
	ПараметрыВыполнения.ОжидатьЗавершение           = 0; // Не ждем завершения.
	ПараметрыВыполнения.ЗапуститьВФоне              = Истина;
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Обновление данных сервиса отправки 1С:ФинОтчетности';
															|en = 'Update data of 1C:Financial Reporting sending service'");
	ПараметрыВыполнения.КлючФоновогоЗадания         = "ОбновитьДанныеСервиса";
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения, "ФинОтчетностьВБанки.ОбновитьДанныеСервисаФинОтчетности");	
	
КонецФункции

Функция ЭтоАрхивныйКомплект(Получатель, ИдентификаторКомплекта) Экспорт
	
	НастройкиКомплекта = ЗаполнениеФинОтчетностиВБанки.НастройкиКомплектаОтчетности(Получатель, ИдентификаторКомплекта);
	
	Если НастройкиКомплекта <> Неопределено Тогда
		Возврат НастройкиКомплекта.Архивный;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Процедура ЗакончитьВыполнениеПравила(Знач Правило) Экспорт
	
	ОкончаниеВыполнения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Правило, "ОкончаниеВыполнения");
	Если ЗначениеЗаполнено(ОкончаниеВыполнения) Тогда
		Возврат;
	КонецЕсли;
	
	// Текущий период считается заключительным.
	ОкончаниеВыполнения = НачалоМесяца(ОбщегоНазначенияБП.ТекущаяДатаНаСервере());
	Справочники.ПравилаФинОтчетности.УстановитьОкончаниеВыполнения(Правило, ОкончаниеВыполнения);
	
КонецПроцедуры

// Возвращает ссылку на вариант отчета.
//
// Параметры:
//  ИдентификаторОтчета - Строка - имя отчета.
//  КлючВарианта - Строка - имя варианта отчета.
//  НайтиЭлементСправочника - Булево - Ложь, возвращается только имя отчета; Истина, ссылка на справочник вариантов отчетов.
//
// Возвращаемое значение:
//  СправочникСсылка.ВариантыОтчетов - ссылка на вариант отчета, если НайтиЭлементСправочника = Истина.
//  Строка - имя доступного варианта, если НайтиЭлементСправочника = Ложь.
//  Неопределено - если отчет отсутствует или недоступен по правам.
//
Функция ДоступныйВариантОтчета(Знач ИдентификаторОтчета, Знач ИмяВарианта, НайтиЭлементСправочника = Ложь) Экспорт
	Перем ВариантОтчета;
	
	Отчет = ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Отчет." + ИдентификаторОтчета);
	Если Не ПустаяСтрока(ИмяВарианта) Тогда
		ВариантОтчета = ВариантыОтчетов.ВариантОтчета(Отчет, ИмяВарианта);
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ВариантОтчета) Тогда
		
		ЗаполнениеФинОтчетностиВБанкиПереопределяемый.ИмяВариантаПоУмолчанию(ИдентификаторОтчета, ИмяВарианта);
		ВариантОтчета = ВариантыОтчетов.ВариантОтчета(Отчет, ИмяВарианта);
		
	КонецЕсли;
	
	Возврат ?(Не НайтиЭлементСправочника И ЗначениеЗаполнено(ВариантОтчета), ИмяВарианта, ВариантОтчета);
	
КонецФункции

#КонецОбласти